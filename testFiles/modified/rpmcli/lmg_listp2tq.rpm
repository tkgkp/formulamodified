{TITLE: Delivery note}
{VERSION:Bookman}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),exec('nopanel','param_wydr','S'),1,0,,'Q','C',,,,,
   0,1,PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,exec('get','#params',1121,2,OPERATOR.USER)<>'T'}
{BEGIN:
   params_set(params_get());
   exec('czytaj','#stalesys',ND.D,XINFO);
   POLA.NAZ_WYDR:='Delivery note '+exec('str_to_pth','#string',ND.SYM)+'.pdf';
   ttf:={? rep_ver()='Bookman' | rep_ver()='BookWal' | rep_ver()='BookRR' || 1 || 0 ?};
   nazwaa:='';
   warj:=FUN.choice('Wariant wydruku',,'Polski','Agielski');
   undefine();
   define('UWAGI','T','Drukowaç uwagi ?','Dopuszczalne wartoûci, T lub N',2);
   define('ZAMK','T','Drukowaç zam.klienta ?','Dopuszczalne wartoûci, T lub N',2);
   define('ZAMU','T','Drukowaç opis dodatkowy z zam¢wienia ?','Dopuszczalne wartoûci, T lub N',2);
   define('MOSL','T','Drukowaç info Mass Balance ?','Dopuszczalne wartoûci, T lub N',2);
   _btn:=def_btn('text=%1,icon=xwin16.png:13'['OK'@],'key:F2');
   def_bopt(_btn,'default=1');
   def_btn('text=%1,icon=xwin16.png:14'['Anuluj'@],'key:Esc');
   def_edit("{? (-DEFINE.ZAMK<>\'t\' & -DEFINE.ZAMK<>\'n\')
             || FUN.emsg('Dopuszczalne wartoûci, T lub N. Popraw dane...');
                 'ZAMK'
             |? (-DEFINE.UWAGI<>\'t\' & -DEFINE.UWAGI<>\'n\')
             || FUN.emsg('Dopuszczalne wartoûci, T lub N. Popraw dane...');
                'UWAGI'
             |? (-DEFINE.ZAMU<>\'t\' & -DEFINE.ZAMU<>\'n\')
             || FUN.emsg('Dopuszczalne wartoûci, T lub N. Popraw dane...');
                'ZAMU'
             |? (-DEFINE.MOSL<>\'t\' & -DEFINE.MOSL<>\'n\')
             || FUN.emsg('Dopuszczalne wartoûci, T lub N. Popraw dane...');
                'MOSL'
             || chk_rec
             ?}",'Parametry zestawienia');
   uwagi:=-DEFINE.UWAGI='t';
   PARAM_W.ZAM:=-DEFINE.ZAMK;
   zamu:=-DEFINE.ZAMU='t';
   mosl:=-DEFINE.ZAMU='t';
   {? var_pres('__pdcp')>0 || obj_del(__pdcp) ?};
   __pdcp:=tab_tmp(1,
      'SYM','STRING[20]','Symbol',
      'ESN','STRING[20]','ESN'
   );
   __pdcp.blank();
   __pdcp.SYM:=ND.SYM;
   __pdcp.add();
   data:=ND.D;
   sym:=ND.SYM;
   zam:=exec('zam_kl','qlmg',ND.ref(),ND.SYM,1);
   memo:={? zamu || exec('memo_zkn','qlmg',ND.ref(),ND.SYM) || '' ?};
   ND.KH();
   khnaz1:=khnaz2:=khnaz3:='';
   _tmp:=obj_new(@.CLASS.PRINT, 1);
   _tmp.add("KH.NAZ_P",58,'');
   _tmp.ini_line();
   {? _tmp.next() || khnaz1:=((2-_tmp.line())-2) ?};
   kraj:=KH.KRAJ;
   miasto:=KH.POCZ+' '+KH.MIASTO;
   ulica:=KH.UL;
   nasz_kod:=KH.NASZ_KOD;
   {? ND.KH_ODB<>null;0
   || khnaz1:=ND.KH_ODB().NAZ;
      khna2:='';
      khnaz3:='';
      miasto:=ND.KH_ODB().POCZ+' '+ND.KH_ODB().MIASTO;
      ulica:=ND.KH_ODB().UL
   ?};
   {? _tmp.next() || khnaz2:=((2-_tmp.line())-2) ?};
   {? _tmp.next() || khnaz3:=((2-_tmp.line())-2) ?};
   VAR_DEL.delete('k','w','PRN');
   k:=obj_new(8); "--szerokosci kolumn--";
   w:=obj_new(8); "--wartosci kolumn--";
   dokl:=3; "--ilosc miejsc dziesietnych--";
   PRN:=obj_new(@.CLASS.PRINT,9); PRN.s_frame();
   k[1]:=70+{? ~params_get().par.ask_tw || 10 ?};
   k[2]:=5;
   k[3]:=8;
   k[4]:=7;
   k[5]:=9;
   k[6]:=9;
   k[7]:=9;
   k[8]:=10;
   w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:=w[8]:='';
   Lp:=0;
   VAR_DEL.delete('X_Xb','X_Xc','qnd_X_Xb','color','prn1','tryb','lm','ll','vp','lmt','rsep');
   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
   undefine();
   VAR_DEL.delete('__pdcp','data','sym','zam','nazwaa','memo','k','w','PRN','Lp','X_Xb','X_Xc','qnd_X_Xb');
   VAR_DEL.delete('color','prn1','tryb','lm','ll','vp','lmt','rsep')
}
{INCLUDE: wsp_pw.rpi}
{IF: ttf}
   {DEFINEFONT: 'TF=Bookman Old Style,,F,,0,0'}
   {DEFINEFONT: 'TFB=Bookman Old Style Bold,,F,,0,0'}
   {DEFINEFONT: 'TFI=Bookman Old Style Italic,,F,,0,0'}
   {DEFINEFONT: 'TI=Bookman Old Style,,I,,0,0'}
   {DEFINEFONT: 'TIB=Bookman Old Style Bold,,I,,0,0'}
   {DEFINEFONT: 'TII=Bookman Old Style Italic,,I,,0,0'}
   {DEFINEFONT: 'TM=Bookman Old Style,,M,,0,0'}
   {DEFINEFONT: 'TMB=Bookman Old Style Bold,,M,,0,0'}
   {DEFINEFONT: 'TMI=Bookman Old Style Italic,,M,,0,0'}
{FI}
{HEADER}

{FONT: 'TFB'}{<1>80'DELIVERY NOTE: '+form(ND.NR,5)}{FONT:'Q'}
{FONT: 'TFB'}
⁄{78*'ƒ'}ø
≥{<3'Date of departure: '+data$4} {<80}≥
≥{<3'Document date: '+ND.D$4} {<80}≥
≥{<3'Place: '+XINFO.MIA}{>78'Supplier number: '+nasz_kod}{<80}≥
¿{78*'ƒ'}Ÿ
{FONT:'TFB'}
   VENDOR:                       {<30 XINFO.NAZ}
   PLACE OF DEPARTURE:  {<30 XINFO.KRAJ().NAZ}
                                       {<30 XINFO.KOD_P+' '+XINFO.POCZ}
                                       {<30 XINFO.UL+' '+XINFO.NR_D+' '+XINFO.NR_L}
{IF:ND.KH_ODB<>0}
   PLACE OF DESTINATION:      {<30 ND.KH_ODB().NAZ}
                                             {<30 ND.KH_ODB().POCZ+' '+ND.KH_ODB().MIASTO}
                                             {<30 ND.KH_ODB().UL}
{FI}
   PURCHASER:    {<30 khnaz1}
                          {IF: khnaz2<>''}{<30khnaz2}{FI}
                          {IF: khnaz3<>''}{<30khnaz3}{FI}
                          {<30 kraj}
                          {<30 miasto}
                          {<30 ulica}
{FONT: 'TFI'}

  Gross weight:   {<20,2form(suma_b,,2,' ,')}
  Total packages: {<20,0form(suma_n$0,,0,' ,')}
{IF: ND.O<>'';0}
  {ND.O}
{FI}
{IF: zamu & memo<>''}
  {form(memo)}
{FI}
{FONT: 'Q'}
{HEADER-}
{  PRN.add("{? w[1]='Total:' || ' ' || Lp+=1; $Lp ?}",5,'Lp.');
   PRN.add("w[1]",k[1],'Description');
   PRN.add("w[2]",k[2],'PKGS');
   PRN.add("w[3]",k[3],'Quantity PCS');
   PRN.add("w[4]",k[4],'Total PCS',1);
   PRN.add("w[5]",k[7],'Batch no',1);
   PRN.add("w[6]",k[5],'Weight (KGS)',1);
   PRN.add("w[7]",k[6],'Gross Weight',1);
   {? params_get().par.ask_tw
   || PRN.add("w[8]",k[8],'Expiration date',1)
   ?};
   suma_n:=suma_b:=0;
   _QPDC:=params_get().par.QPDC;
   _QPDC.prefix($ND.ref());
   {? _QPDC.first()
   || {!
      |? {? _QPDC.R='N'
         || suma_n+=_QPDC.PKGS;
            suma_b+=_QPDC.GKGS
         || suma_b+=_QPDC.GKGS
         ?};
         _QPDC.next()
      !}
   ?};
   ~~
}
{  VAR_DEL.delete('X_Xb','X_Xc');
   X_Xb:=tab_tmp(1,'ND','STRING[16]','Dokument magazynowy',
      'LP','INTEGER','Lp',
      'R','STRING[1]','Rodzaj',
      'KOD','STRING[16]','Kod towarowy',
      'KTM' ,'STRING[50]'  ,'Indeks',
      'N'   ,'STRING[255]' ,'Nazwa',
      'PKGS'     ,'REAL'      ,'Iloûç opakowa§',
      'QPCS'     ,'REAL'      ,'Iloûç w opakowaniu',
      'TPCS'    ,'REAL'       ,'Iloûç caíkowita',
      'QSER_TXT'  ,'STRING[20]' ,'Partia',
      'KGS'     ,'REAL'        ,'Waga netto',
      'GKGS'    ,'REAL'   ,'Waga brutto',
      'UWAGI'   ,'STRING[255]' ,'Uwagi',
      'MOSL'   ,'STRING[255]' ,'Mosl',
      'WP','STRING[20]','Wymiar palety',
      'UWPAL','STRING[255]','Zawartoûç',
      'TW','STRING[10]','Termin waßnoûci'
   );
   qnd_X_Xb:=X_Xb.ndx_tmp('',,'ND',,,'LP',,,'KOD',,);

   X_Xc:=tab_tmp(1,
      'S_PKGS'  ,'REAL'   ,'Suma Iloûç opakowa§',
      'S_TPCS'  ,'REAL'   ,'Suma Iloûç caíkowita',
      'S_KGS'   ,'REAL'   ,'Suma Waga netto',
      'S_GKGS'  ,'REAL'   ,'Suma Waga brutto');

   _par:=params_get().par;
   _QPDC:=_par.QPDC;
   _QPDCP:=_par.QPDCP;
   _QPDC.index(_par.qpdc_nlk);
   _QPDC.prefix($ND.ref());
   {? _QPDC.first()
   || X_Xb.index(qnd_X_Xb);
      X_Xb.prefix();
      _put:=1;
      {!
      |? {? _QPDC.R='N'
         || nazwaa:=form(
               exec('nazwalp','qlsp',{? M.seek(_QPDC.KOD,) || M.ref() || null() ?},{? warj=2 || 'ANG' ||'PL' ?},,,ND.KH)
            );
            {? -PARAM_W.ZAM='t'
            || _zamk:={? warj || '; Customer order: ' || '; Zam¢wienie klienta: '  ?};
               symz:='';
               symz:=exec('zam_kl2','qlmg',ND.ref(),ND.SYM,_QPDC.KOD);
               _QPDCP.index(_par.qpdcp);
               _QPDCP.prefix($_QPDC.ref());
               {? _QPDCP.first()
               || symz:='';
                  {!
                  |? symz+={? symz<>'' || '; ' || '' ?};
                     symz+=exec('zam_kl2','qlmg',null(),_QPDCP.SYM,_QPDC.KOD);
                     _QPDCP.next()
                  !}
               ?};
               {? symz<>'' || nazwaa+=_zamk+symz ?}
            ?};
            X_Xb.ND:=_QPDC.ND;
            X_Xb.LP:=_QPDC.LP;
            X_Xb.R:=_QPDC.R;
            X_Xb.KOD:=_QPDC.KOD;
            X_Xb.KTM:=_QPDC.KTM;
            X_Xb.N:=X_Xb.KTM+' - '+nazwaa;
            ND.cntx_psh();
            ND.clear();
            {? ND.seek(_QPDC.REFND,) & uwagi || X_Xb.UWAGI:='; '+ND.O ?};
            ND.cntx_pop();
            {? M.seek(_QPDC.KOD,) & mosl || X_Xb.MOSL:='; '+M.OSL ?};
            X_Xb.PKGS:=_QPDC.PKGS;
            X_Xb.QPCS:=_QPDC.QPCS;
            X_Xb.TPCS:=_QPDC.TPCS;
            X_Xb.QSER_TXT:=_QPDC.QSER_TXT;
            X_Xb.KGS:=_QPDC.KGS;
            X_Xb.GKGS:=_QPDC.GKGS;
            X_Xb.TW:=_QPDC.TW;
            X_Xb.add();

            {?_put=1
            || X_Xc.S_PKGS:=_QPDC.PKGS;
               X_Xc.S_TPCS:=_QPDC.TPCS;
               X_Xc.S_KGS:=_QPDC.KGS;
               X_Xc.S_GKGS:=_QPDC.GKGS;
               X_Xc.add();
               _put:=0
            || X_Xc.S_PKGS+=_QPDC.PKGS;
               X_Xc.S_TPCS+=_QPDC.TPCS;
               X_Xc.S_KGS+=_QPDC.KGS;
               X_Xc.S_GKGS+=_QPDC.GKGS;
               X_Xc.put()
            ?}
         ?};
         _QPDC.next()
      !};
      X_Xb.ndx_drop()
   ?};
   prn1:='PRN';
   ~~
}
{  dalej:=0;
   _par:=params_get().par;
   _QPDC:=_par.QPDC;

   _QPDC.index(_par.qpdc_nlk);
   _QPDC.prefix($ND.ref());
   {? _QPDC.first()
   || X_Xb.index(qnd_X_Xb);
      X_Xb.prefix();
      {!
      |? {? _QPDC.R='P'
         || X_Xb.ND:=_QPDC.ND;
            X_Xb.LP:=_QPDC.LP;
            X_Xb.R:=_QPDC.R;
            X_Xb.KOD:=_QPDC.KOD;
            X_Xb.KTM:=_QPDC.KTM;
            X_Xb.WP:={? _QPDC.DLUG || '('+$_QPDC.DLUG+'x'+$_QPDC.SZER+'x'+$_QPDC.WYS+')' || '' ?};
            X_Xb.UWPAL:=_QPDC.UWPAL;
            X_Xb.N:=X_Xb.KTM+' - ';
            X_Xb.N+=exec('nazwalp','qlsp',
               {? M.seek(_QPDC.KOD,) || M.ref() || null() ?},{? warj=2 || 'ANG' ||'PL' ?},,,ND.KH
            );
            X_Xb.N+={? +X_Xb.WP || X_Xb.WP || '' ?};
            ND.cntx_psh();
            ND.clear();
            {? ND.seek(_QPDC.REFND,) & uwagi || X_Xb.UWAGI:='; '+ND.O ?};
            ND.cntx_pop();
            {? M.seek(_QPDC.KOD,) & mosl || X_Xb.MOSL:='; '+M.OSL ?};
            X_Xb.PKGS:=_QPDC.PKGS;
            X_Xb.QPCS:=0;
            X_Xb.TPCS:=_QPDC.TPCS;
            X_Xb.QSER_TXT:=_QPDC.QSER_TXT;
            X_Xb.KGS:=_QPDC.KGS;
            X_Xb.GKGS:=_QPDC.GKGS;
            X_Xb.TW:=_QPDC.TW;
            dalej:=1;
            X_Xb.add()
         ?};
         _QPDC.next()
      !};
      X_Xb.ndx_drop()
   ?};
   ~~
}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE}
{FOOTER-}
{COMMENT}   drukowanie pozycji wydruku                               {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{rsep:=PAR_WYDR.RSEP; ~~}
{FOR: 'X_Xb'}
{INDEX: qnd_X_Xb}
{PREFIX: $ND.ref() }
{DO}
{IF: X_Xb.R='N'}
   {  w[1]:=form(X_Xb.N)+form(X_Xb.UWAGI)+form(X_Xb.MOSL);
      w[2]:=form(X_Xb.PKGS,,0,' ,');
      w[3]:=form(X_Xb.QPCS,,0,' ,');
      w[4]:=form(X_Xb.TPCS,,0,' ,');
      w[5]:=form(X_Xb.QSER_TXT);
      w[6]:=form(X_Xb.KGS,,2,' ,');
      w[7]:=form(X_Xb.GKGS,,2,' ,');
      w[8]:=form(X_Xb.TW,,2,' ,');
    ~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{OD}
{COMMENT}   drukowanie podsumowania wydruku                               {COMMENT-}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{COMMENT}{SUBSTITUTE: 'LINIAF'}{COMMENT-}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
   {  Lp:=0;
      w[3] :=w[5] :='';
      w[1] :='Total:';
      w[2]:=form(X_Xc.S_PKGS,,0,' ,');
      w[4]:=form(X_Xc.S_TPCS,,0,' ,');
      w[6]:=form(X_Xc.S_KGS,,2,' ,');
      w[7]:=form(X_Xc.S_GKGS,,2,' ,');
      w[8]:='';
      ~~
   }
   {SUBSTITUTE: 'LINIA0'}
{IF: dalej=0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER}{FOOTER-}
{ELSE}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: 'X_Xb'}
{INDEX: qnd_X_Xb}
{PREFIX: $ND.ref() }
{DO}
{IF: X_Xb.R='P'}
   {  w[1]:=form(X_Xb.N)+form(X_Xb.UWAGI)+form(X_Xb.MOSL)+'; '+form(X_Xb.UWPAL);
      w[2]:=form(X_Xb.PKGS,,0,' ,');
      w[3]:=form('waga');
      w[4]:=form(X_Xb.TPCS,,0,' ,');
      w[5]:=form(X_Xb.QSER_TXT);
      w[6]:=form('total');
      w[7]:=form(X_Xb.GKGS,,2,' ,');
      w[8]:=form(X_Xb.TW,,2,' ,');
    ~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FOOTER}{FOOTER-}
{FI}
{COMMENT}{FONT:'R'}{COMMENT-}
{FONT:'T8G'}{SPACE: 'C'}
{FOR: params_get().par.QPDC}
{INDEX: params_get().par.qpdc_nlk}
{PREFIX: $ND.ref()}
{DO}
   {FOR: params_get().par.QPDCP}
   {INDEX: params_get().par.qpdcp}
   {PREFIX: $params_get().par.QPDC.ref() }
   {DO}
      {  _QPDCP:=params_get().par.QPDCP;
         {? ~(__pdcp.find_key(_QPDCP.SYM))
         || __pdcp.blank();
            __pdcp.SYM:=_QPDCP.SYM;
            __pdcp.add()
         ?};
         ~~
      }
   {OD}
{OD}
{FOR: __pdcp}
{DO}
   {  VAR_DEL.delete('zam_klx');
      zam_klx:=sql('
         select ZK_N.SYM, ZK_N.ZAM_KL
         from @ZK_RN
            left join @ZK_N using (ZK_RN.N,ZK_N.REFERENCE)
         where ZK_RN.SWZ=\':_a\'
         order by 1,2',
         __pdcp.SYM
      );
      ~~
   }
{FOR: zam_klx}
{DO}
{FIRST}{<1ND.TYP().T+' no.: '+__pdcp.SYM}{IF: __pdcp.ESN<>''}{' - ESN no.: '+__pdcp.ESN}{FI}{FIRST-}{IF: zam_klx.SYM<>''}{<58'Order no:'+zam_klx.SYM}{FI}{IF: zam_klx.ZAM_KL<>'' & PARAM_W.ZAM='t'}{<78'Customer order  no.: '+zam_klx.ZAM_KL}{FI}
{OD}
{OD}
{ENTIRE}


Data i pieczëç: ..............................

 NIP: PL 1250032326  REGON 010845103  KRAJOWY REJESTR SèDOWY - REJESTR PRZEDSIêBIORC£W NR KRS 0000078477
{ENTIRE-}

:!UTF-8
{TITLE:NUCO - Dyspozycja wydania}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   zkzam_01:="
      VAR_DEL.delete('PRN','PRNS','NAG','w','s','k','p','na','szer','PDCZ');
      VAR_DEL.delete('dokl','ddokl','doklw');
      VAR_DEL.delete('color','prn1','prn2');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('il_lin','dokl','doklw','lp');
      VAR_DEL.delete('zakoncz','proforma');
      VAR_DEL.delete('oddzial');
      VAR_DEL.delete('byl','wsk');
      VAR_DEL.delete('M_GD','S_GD');
      VAR_DEL.delete('zero_na','print5','op_1','op_2','op_3','op_4');
      VAR_DEL.delete('max_T8','max_T8G','max_T10B');
      VAR_DEL.delete('wydr_naz','wyjscie','ilwar','__sel_size','__tab_sel','__lista_zam','__zkn_ref')
      ";
   zkzam_01();
   __lista_zam:='';__zkn_ref:='';
{? (__sel_size:=ZK_N.sel_size())>0
||
    ZK_N.cntx_psh();
    __tab_sel:=ZK_N.sel_aget();
   {? __tab_sel.first()
   ||
      {!|?
           {? ZK_N.seek(__tab_sel.REF,) & ZK_N.A='A' & (ZK_N.AKC='T' | ZK_N.AKCH='T' | ZK_N.AKCL='T')
           ||
              exec('dod_pdcz_all','qlsp',ZK_N.ref());
              __lista_zam += ' '+ZK_N.SYM+' date '+$ZK_N.DP+',';
              ZK_N.sel_del()
           ?};
           __tab_sel.next()
      !};
       __lista_zam := __lista_zam-1
   ?};
   ZK_N.cntx_pop()
||
   __lista_zam := ZK_N.SYM+' date '+$ZK_N.DP;
   __zkn_ref:=ZK_N.ref();
   exec('dod_pdcz_dr','qlsp',ZK_N.ref())
?};
   KSTE.get();
   VAR_DEL.delete('PRN','PRNS','w','s','k','p','na','szer');
   PRN:=obj_new(@.CLASS.PRINT,12); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,5); PRNS.s_frame();
   NAG:= obj_new(@.CLASS.PRINT,2); NAG.n_frame();
   kolej:=ask('Podaj sposób sortowania: ',,0,' Pozycje ',' Nazwy');
   ind:={? kolej || ZK_P.ndx_tmp(,,'A',,,'T',,,'N',,,'TOP',,,'M','KTM',) || 'TYPN' ?};
   w:=obj_new(12); "--wartosci kolumn--";
   s:=obj_new(1); "--razem--";
   k:=obj_new(7);  {! _i..obj_len(k) |! k[_i]:=0  !}; "--szerokosc kolumn--";
   p:=obj_new(7);  {! _i..obj_len(k) |! p[_i]:='' !}; "--tymczasowa do szacowania szerokosci kolumn--";
   dokl :=3; "--dokladnosc ilosci--";
   ddokl:=0; "--najwieksza drukowana dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   il_lin:=0;
   zakoncz:=1;
   oddzial:=KSTE.NAZ<>'';
   byl:=wsk:=0;
   proforma:=0;

   ilwar:=1;

   wydr_naz:='zkzam_06';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OZ.USER,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OZ.USER;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   PARAM_W.JEZYK := 0;
   wyjscie:=0
;~~}
{END:
   zkzam_01();
   ZK_P.ndx_drop();
   ZK_P.index('TYPN');
   VAR_DEL.delete('zkzam_01')
;~~}
{EXIT: {? var_pres('PDCZ')<0 || wyjscie:=1 ?};wyjscie=1}
{FONT: 'T8'}{SPACE: 'B'}
{
"--szerokosc kolumn--";
   ddokl:=0;
   VAR_DEL.delete('X_Xw1');
   X_Xw1:=tab_tmp(1
      ,'P','INTEGER',''
      ,'M','STRING[160]',''
      ,'ILKAR','REAL',''
      ,'SZTKAR','REAL',''
      ,'SZTRAZ','REAL',''
      ,'KARNWAG','REAL',''
      ,'KARBWAG','REAL',''
      ,'KARRNWAG','REAL',''
      ,'KARRBWAG','REAL',''
      ,'REF','STRING[16]','');

   _war:=0;
   {? var_pres('PDCZ')>100
   ||
      PDCZ.index(pdcz1); PDCZ.prefix();
      {? PDCZ.first
      || {!
         |?
            X_Xw1.clear();
            X_Xw1.blank();
            X_Xw1.P:=PDCZ.LP;
            X_Xw1.M:=PDCZ.KOD+' - '+PDCZ.NAZ;
            X_Xw1.ILKAR:=PDCZ.IL_KAR;
            X_Xw1.SZTKAR:=PDCZ.IL;
            X_Xw1.SZTRAZ:=PDCZ.SZT;
            X_Xw1.KARNWAG:=PDCZ.WKN;
            X_Xw1.KARBWAG:=PDCZ.WKB;
            X_Xw1.KARRNWAG:=PDCZ.RWN;
            X_Xw1.KARRBWAG:=PDCZ.RWB;
            X_Xw1.REF:=$PDCZ.ref();
            X_Xw1.add(1);
            PDCZ.next()
         !}
      ?}
    ?};
   lmt:=charleft()-4;
   k[1]:=10;                           p[1] :='X_Xw1.ILKAR';
   k[3]:=-4;                           p[3] :='X_Xw1.P';
   k[2]:=0;                            p[2] :='X_Xw1.M';
   k[4]:=10;                           p[4] :='X_Xw1.SZTKAR';
   k[5]:=10;                           p[5] :='X_Xw1.SZTRAZ';
   k[6]:=15;                           p[6] :='';
   k[7]:=15;                           p[7] :='';
   _len:=obj_len(k);
   _buf:=obj_new(_len); {! _i.._len |! _buf[_i]:=0 !};
   _max:=obj_new(_len); {! _i.._len |! _max[_i]:=0 !};

   X_Xw1.clear();
   {? X_Xw1.first()
   || {!
      |? {! _i.._len
         |! _war:=($p[_i])();
            _typ:=type_of(_war);
            {? _typ=1 || _w:=(+form(_war));
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=2 || _w:=(+_war);
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=4 || _buf[_i]:=10
            |? _typ=5 || _buf[_i]:=5
            ?}
         !};
         X_Xw1.next()
      !}
   ?};

   _ods:=0; _all:=0;
   {! _i.._len
   |! {? k[_i]<0         || k[_i]:=-k[_i]
      |? k[_i]<=_buf[_i] || k[_i]:=_buf[_i]
      ?};
      _all+=k[_i]; {? k[_i]>0 || _ods+=3 ?}
   !};
   k[2]:=(lmt-(_all-k[2])-_ods); {? k[2]<0 || msg('Błąd') ?};
   obj_del(_buf); obj_del(_max);

   PRN.add("w[1]",k[3],'Lp.',1);
   PRN.add("w[3]",k[2],'Asortyment');
   PRN.add("w[4]",k[1],'Ilość kartonów',1);
   PRN.add("w[5]",k[4],'Sztuk w kartonie',1);
   PRN.add("w[6]",k[5],'Razem sztuk',1);
   PRN.add("w[7]",k[6],'      Wydano kartonów',1);
   PRN.add("w[8]",k[7],'Razem szt.',1)
;~~}
{DEFINEFONT: 'TM=Bookman Old Style,,M,,0,0'}
{DEFINEFONT: 'TMB=Bookman Old Style,,N,,0,0'}
{DEFINEFONT: 'TF=Bookman Old Style,,F,,0,0'}
{FONT: 'T8G'}{SPACE: 'C'}
{
   lmt:=int((charleft()-PRN.width())/2);
   PAR_WYDR.TITLE:='Dyspozycja wydania\n'+__lista_zam;
   prn1:='PRN'; prn2:='PRNS'
;~~}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_lic.rpi}
{INCLUDE: wsp_strony.rpi}
{INCLUDE: wsp_stronyq.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEADNUCO'}
{COMMENT}   strony transakcji                                        {COMMENT-}
{ __STRTAB:='ZK_N'; __STRODB:='ZK_N.ODB'; __STRLIN:=''; __STRDAT:=ZK_N.DP;~~}
{SUBSTITUTE: 'qstronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{COMMENT}   termin realizacji     {COMMENT-}
{IF: ZK_N.ODB<>null}
{<{lmt+1}'Consignee: '+ZK_N.ODB().NAZ}
{FI}
{<{lmt+1}'Customer order no.: '+ZK_N.ZAM_KL}
{<{lmt+1}'Sales: '+ZK_N.HAN().NAZ}
{<{lmt+1}'      tel.: '+ZK_N.HAN().TEL}
{<{lmt+1}'     email: '+ZK_N.HAN().EMAIL}
{LINE}
{<{lmt+1}'Delivery: '+$ZK_N.DT}
{COMMENT}   forma platnosci
{IF: ZK_N.PL<>null}
   {LINE}
   {<{lmt+1}'Forma płatności: '+ZK_N.PL().TR}
{FI}
{COMMENT-}
{IF: ZK_N.OP<>''}
{<{lmt+1}'Others: '+ZK_N.OP}
{FI}
{COMMENT}   miejsce dostawy                                          {COMMENT-}
{IF: ZK_N.KH_MSC<>null}
   {LINE}
   {<{lmt+1}exec('msc_dost','kontrahent','ZK_N')}
{FI}
{COMMENT}   informacje dodatkowe dla naglowka dokumentu              {COMMENT-}
{
   prn1:='PRN';
   'pobieram dane'; exec('pobgdnag','fakso','Z',ZK_N.ref);
   VAR_DEL.delete('oo','OPIK')
;~~}
{FOR: S_GD}
{PREFIX: 'G'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   naglowek tabeli                                          {COMMENT-}
{  prn1:='PRN';~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEADNUCO'}
   {IF: zakoncz}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
   {FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{IF: _a:=exec('get_par', '#parametr', 34); _a='Z' | _a='A'}
   {FOOTER}
   {SUBSTITUTE: 'FOOT_LIC'}
   {~~}
   {~~}
   {~~}
   {FOOTER-}
{ELSE}
   {FOOTER}{FOOTER-}
{FI}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{
rsep:=PAR_WYDR.RSEP;
w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:='';
s[1]:=0;
lp:=0
;~~}
{FOR:'PDCZ'}{INDEX: pdcz1}{PREFIX:  __zkn_ref}
{DO}
   {
      lp+=1;
      rsep:=PAR_WYDR.RSEP;
      w[1]:=form(lp);
      w[3]:=form(PDCZ.KOD + ' ' + PDCZ.NAZ);
      w[4]:=form(PDCZ.IL_KAR,,ddokl,'.,');
      w[5]:=form(PDCZ.IL,,ddokl,'.,');
      w[6]:=form(PDCZ.SZT,,doklw,'.,');
      w[7]:='';
      w[8]:='';
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))() + 2
   ;~~}
   {IF: lineleft<=il_lin}
      {SUBSTITUTE: 'LINIAF'}{PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{
rsep:=1
;~~}
{IF: ilwar=2}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {IF: lineleft()>0 & lineleft()<3}
      {SUBSTITUTE: 'LINIAF'}
      {IF: lineleft()>0}{PAGE}{FI}
   {FI}
   {SUBSTITUTE: 'LINIA02'}
   {  prn1:='PRNS'; ~~}
{FI}
{SUBSTITUTE: 'LINIAF'}
{  zakoncz:=0; ~~}
{COMMENT}   informacje dodatkowe pod dokumentem              {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: S_GD}
{PREFIX: 'D'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   podpisy                                                  {COMMENT-}
{LINE: 3}
{FONT: 'HEAD'}{SPACE: 'B'}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,'');
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=30*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:='Wydanie przygotował';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}


:!UTF-8
{TITLE:NUCO 1. Porównanie ilości zużytych surowców z normami technologicznymi}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  ZL.cntx_psh();
  ZTP.cntx_psh();
  ZLIM.cntx_psh();
  VAR_DEL.delete('TAB1','PRN','ZLST','ndx');
  TAB1:=tab_tmp(2,
     'SYM','STRING[20]','Symbol zlecenia',
     'KTM','STRING[50]','Surowiec',
     'ILZ','REAL','Il.zużyta',
     'ILNOR','REAL','Il.wg normy',
     'ILTECH','REAL','Il.techn.',
     'IL_P','REAL','Il.produktu.',
     'LIMIT','STRING[1]','Limit',
     'KTM_P','STRING[50]','Produkt',
     'N_P','STRING[80]','Nazwa produktu',
     'N','STRING[80]','Nazwa surowca',
     'WYD','STRING[10]','Wydział',
     'ODD','DATE','Data rozpoczęcia',
     'DOD','DATE','Data zakończenia',
     'JM','STRING[10]','Jednostka miary'
  );
  PRN:=obj_new(@.CLASS.PRINT, 16);
  PRN.s_frame();
  prn1:='PRN';
  tryb:=rep_export();
  waslbar:=0;
  dalej:=dalej1:=dalej2:=dalej3:=suma:=sumalim:=0;
  color:='';
  lim:='T';
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  ZL.cntx_pop();
  ZTP.cntx_pop();
  ZLIM.cntx_pop();
  VAR_DEL.delete('TAB1','PRN','ZLST','ndx');
  &dalej;
  &dalej1;
  &dalej2;
  &dalej3;
  &suma;
  &lim;
  &sumalim;
  &prn1;
  &color;
  &lm; &ll; &vp; &lmt; &rsep;
  &tryb
}
{COMMENT}
  Wydruk porównania ilości zużytych surowców z normami technologicznymi.
  [MKO] - 2002/01/30 - [7.60] OWwPRO27/5.2
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{ _wydr_naz:='zlec_005q';
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.ODDNIA:=WYDRUKIL.OD_DATY;
     WYDRUK.DODNIA:=WYDRUKIL.DO_DATY;
     WYDRUK.ZLECOZ:=WYDRUKIL.SORTUJ
  ?};
  WYDRUK.win_edit(); WYDRUK.win_edit('ZLEC_001');~~}
{IF: WYDRUK.edit() }
{ WYDRUKIL.SORTUJ:=WYDRUK.ZLECOZ;
  WYDRUKIL.OD_DATY:=WYDRUK.ODDNIA;
  WYDRUKIL.DO_DATY:=WYDRUK.DODNIA;
  WYDRUKIL.put(1);
  typ:='P';
  rodzaj:=WYDRUK.ZLECOZ;
  oddnia:=WYDRUK.ODDNIA;
  dodnia:=WYDRUK.DODNIA;
~~}
{ELSE}
{EXIT}
{FI}
{COMMENT}--Definicja wiersza wydruku --------------------{COMMENT-}
{COMMENT} n_s:=n_p:=ktm_p:=data_od:=data_do:='' {COMMENT-}
{ PRN.add("sym",20,'Symbol zlecenia'@);
  {? tryb<>1
  || PRN.add("wyd",4,'Wyd.'@,,'#');
     PRN.add("ktm_p",17,'Kod produktu'@,,'#');
     PRN.add("ktm",17,'Kod surowca'@,,'#');
     PRN.add("limit",3,'L\N'@,,'#');
     {? typ='P' ||
        PRN.add("t",12,'Ilość wg#technologii'@,1,'#')
     ?};
     PRN.add("n",12,'Ilość wg#normy'@,1,'#');
     PRN.add("z",12,'Ilość zużyta'@,1,'#');
     PRN.add("jm",6,'Jedn. miary'@,,'#');
     PRN.add("p",8,'%% wyk.'@,1,'#')
  || PRN.add("wyd",10,'Wydział'@);
     PRN.add("data_od",10,'Start zlec.'@);
     PRN.add("data_do",10,'Koniec zlec.'@);
     PRN.add("ktm_p",20,'Kod produktu'@);
     PRN.add("n_p",40,'Nazwa produktu'@);
     PRN.add("il_p",12,'Ilość produktu'@,1);
     PRN.add("ktm",20,'Kod surowca'@);
     PRN.add("n_s",40,'Nazwa surowca'@);
     PRN.add("limit",5,'Limit'@);
     {? typ='P' ||
        PRN.add("t",12,'Ilość wg#technologii'@,1)
     ?};
     PRN.add("n",12,'Ilość wg#normy'@,1);
     PRN.add("z",12,'Ilość zużyta'@,1);
     PRN.add("jm",6,'Jedn. miary'@,,'#');
     PRN.add("p",8,'%% wyk.'@,1)
  ?};
  PAR_WYDR.TITLE:='PORÓWNANIE ILOŚCI ZUŻYTYCH SUROWCÓW Z NORMAMI'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Typ: %1'@[{? typ='P' || 'Zlecenie produkcyjne'@ || 'Zlecenie warsztatowe'@ ?}]}
{<{lmt+1} {? rodzaj='O' || 'Zlecenia otwarte'@ |? rodzaj='Z' || 'Zlecenia zamknięte'@ || 'Zlecenia wszystkie'@ ?}}
{FONT: 'T10B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}----------------------------PRZYGOTOWANIE DANYCH-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP;
  exec('tktl_cntx_psh','tech_common');
  ~~
}
{IF: typ='P'}
  { ZTP.clear(); ZTP.index('RDTP'); ZTP.prefix('P'); ~~}
{ELSE}
  { ZTP.clear(); ZTP.index('RDTP'); ZTP.prefix('W'); ~~}
{FI}
{IF: ZTP.first}{dalej:=1;~~}{FI}
{WHILE: dalej}
{DO}
  {IF: rodzaj='Z'}
  { ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref(),'N','Z'); ~~}
  {ELIF: rodzaj='O'}
  { ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref(),'N','O'); ~~}
  {ELIF: rodzaj='N'}
  { ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref(),'N','N'); ~~}
  {ELSE}
  { ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref(),'N'); ~~}
  {FI}
  {IF: ZL.first()}{ dalej1:=1; ~~}{FI}
  {WHILE: dalej1}
  {DO}
{IF: (oddnia=date(0,0,0)| ZL.STARTD>=oddnia) & (dodnia=date(0,0,0)| ZL.STARTD<=dodnia)}
{  VAR_DEL.delete('ZLST');
tktl_sqlref:='';
ZLST:=exec('zlst','zl_common',ZL.ref());
ndx:=ZLST.ndx_tmp('',,'ZL',,,'KOD',,);
~~
}
 {IF: ZLIM.use('zlim'+ZL.MASKA)}
{ TKTL.clear();
  {? ZL.RTKTL<>''
  || tktl_sqlref:=ZL.RTKTL
  |? ZL.RKTL<>''
  || tktl_sqlref:=ZL.RKTL
  ?};
  exec('tktl_use','tech_common',(8+tktl_sqlref)+3);
  ~~
}
{IF: TKTL.seek(tktl_sqlref) | typ='W'}
  { TMAT.clear(); TMAT.index('NL'); TMAT.prefix(TKTL.ref()); ~~}
  {IF: TMAT.first()}{dalej2:=1;~~}{FI}
  {WHILE: dalej2}
  {DO}
{
ZLST.clear();
ZLST.index(ndx);
ZLST.prefix($ZL.ref(),$TMAT.PT);
{? ZLST.first() || ZLST.last(); suma:=ZLST.IL || suma:=0 ?};
 ~~}
  {IF: ZL.RODZAJ='P'}
{ ZLIM.clear(); ZLIM.index('ZKN'); ~~}
  {ELSE}
{ ZLIM.clear(); ZLIM.index('ZKP'); ~~}
  {FI}
{ ZLIM.prefix(ZL.ref(),'T',TMAT.PT); dalej3:=0; ~~}
  {IF: ZLIM.first()}
{ dalej3:=1;~~}
  {FI}
  {sumalim:=0; ~~}
  {IF: typ='P'}
{
  _xjm:=exec('FindAndGet','#table',TKTL,ZL.RKTL,,"XJM",1);
  wsp:=ZL.IL/_xjm;
  sumatech:=TMAT.WARB*wsp; ~~}
  {FI}
 {WHILE: dalej3}
 {DO}
{ sumalim+=ZLIM.LIL; ~~}
{ dalej3:=ZLIM.next(); ~~}
 {OD}
{
TAB1.prefix(ZL.SYM,TMAT.PT().KTM);
{? ~TAB1.first()
|| TAB1.SYM:=ZL.SYM;
   TAB1.WYD:=ZL.JORG().KOD;
   TAB1.ODD:=ZL.STARTD;
   TAB1.DOD:=ZL.ENDD;
   TAB1.IL_P:=ZL.ILDOK;
   TAB1.KTM_P:=ZL.KTM().KTM;
   TAB1.N_P:=ZL.KTM().N;
   TAB1.KTM:=TMAT.PT().KTM;
   TAB1.N:=TMAT.PT().N;
   TAB1.JM:=TMAT.PT().J().KOD;
   TAB1.LIMIT:=TMAT.LIMIT;
   TAB1.ILZ:=suma;
   TAB1.ILNOR:=sumalim;
{? typ='P'
|| TAB1.ILTECH:=sumatech
?};
TAB1.add()
?}; ~~}
 { dalej2:=TMAT.next(); dalej3:=0; ~~}
  {OD}
  {IF: ZL.RODZAJ='P' & ZL.NRNZL=0}
{ ZLIM.clear(); ZLIM.index('ZKN'); ~~}
  {ELIF: (ZL.RODZAJ<>'P' & ZL.GENLIM='N' & ZL.NRNZL=0) |
 (ZL.RODZAJ='P' &  ZL.GENLIM='P' & ZL.NRNZL<>0) }
{ ZLIM.clear(); ZLIM.index('ZKN'); ~~}
  {ELSE}
{ ZLIM.clear(); ZLIM.index('ZKP'); ~~}
  {FI}
  { ZLIM.prefix(ZL.ref); dalej3:=0; ~~}
  {IF: ZLIM.first()}
  { dalej3:=1;~~}
  {FI}
{WHILE: dalej3}
 {DO}
{ TAB1.prefix(ZL.SYM,ZLIM.KTM().KTM);
{? ~TAB1.first()
|| TAB1.SYM:=ZL.SYM;
   TAB1.WYD:=ZL.JORG().KOD;
   TAB1.IL_P:=ZL.ILDOK;
   TAB1.KTM:=ZLIM.KTM().KTM;
   TAB1.JM:=ZLIM.KTM().J().KOD;
   TAB1.LIMIT:='T';
   TAB1.ODD:=ZL.STARTD;
   TAB1.DOD:=ZL.ENDD;
   TAB1.KTM_P:=ZL.KTM().KTM;
   TAB1.N_P:=ZL.KTM().N;
   TAB1.N:=ZLIM.KTM().N;
ZLST.index(ndx);
{? (ZL.NRNZL=0) |
(ZL.GENLIM='P' & ZL.NRNZL<>0)
|| ZLST.prefix($ZL.ref(),$ZLIM.KTM)
|| ZL.cntx_psh();
ZL.index('UNRZL');
ZL.prefix(ZL.NRNZL);
ZL.first();
_zl:=ZL.ref();
ZL.cntx_pop();
ZLST.prefix($_zl,$ZLIM.KTM)
?};
{? ZLST.first() || ZLST.last(); TAB1.ILZ:=ZLST.IL || TAB1.ILZ:=0 ?};
TAB1.ILNOR:=ZLIM.LIL;
TAB1.ILTECH:=0;
TAB1.add()
|| {? TAB1.ILTECH=0 & typ='P'
|| TAB1.ILNOR+=ZLIM.LIL;
TAB1.put()
?}
?}; ~~}
{ dalej3:=ZLIM.next(); ~~}
 {OD}
 {FI}
  {FI}
  {FI}
 { dalej1:=ZL.next(); dalej2:=dalej3:=0; ~~}
  {OD}
  { dalej:=ZTP.next(); dalej1:=dalej2:=dalej3:=0; ~~}
{OD}
{ dalej:=0;
  exec('tktl_cntx_psh','tech_common');
  ~~
}
{COMMENT}--------------------WŁAŚCIWY WYDRUK-----------------------{COMMENT-}
{IF: TAB1.first()}
    { dalej:=z:=n:=t:=p:=ktm:=limit:=jm:='';sumaz:=suman:=sumat:=sumap:=0;~~}
    { il_p:=wyd:=n_s:=n_p:=ktm_p:=data_od:=data_do:='';~~}
{FI}
{FOR: TAB1}
{DO}
{IF: tryb<>1}
    {z+=form(TAB1.ILZ,12,4)+'#';
     n+=form(TAB1.ILNOR,12,4)+'#';
     {? typ='P' || t+=form(TAB1.ILTECH,12,4)+'#' ?};
    ktm+=form(TAB1.KTM,16)+'#';
    ktm_p:=form(TAB1.KTM_P,16);
    limit+=form(TAB1.LIMIT,3)+'#';
    jm+=form(TAB1.JM,6)+'#';
    wyd:=TAB1.WYD;
    p+={? TAB1.ILNOR
    || form(({? TAB1.ILNOR=0 || 0 || TAB1.ILZ/TAB1.ILNOR ?})*100,8,2)
    || form(({? TAB1.ILTECH=0 || 0 || TAB1.ILZ/TAB1.ILTECH ?})*100,8,2) ?}+'#';
    sumaz+=TAB1.ILZ;
    suman+=TAB1.ILNOR;
    sym:=TAB1.SYM;
    {? typ='P' || sumat+=TAB1.ILTECH ?};
    {? TAB1.ILNOR >0 ||
       sumap+=({? TAB1.ILNOR=0 || 0 || TAB1.ILZ/TAB1.ILNOR*100 ?}) ||
    {? typ='P'|| sumap+=({? TAB1.ILTECH=0 || 0 || TAB1.ILZ/TAB1.ILTECH*100 ?}) || sumap:=0 ?}
    ?};
    ~~}
{ELSE}
    { z:=form(TAB1.ILZ,12,4);
      n:=form(TAB1.ILNOR,12,4);
      {? typ='P' || t:=form(TAB1.ILTECH,12,4) ?};
      il_p:=form(TAB1.IL_P,12,4);
      ktm:=form(TAB1.KTM,20);
      ktm_p:=form(TAB1.KTM_P,20);
      n_s:=form(TAB1.N,80);
      n_p:=form(TAB1.N_P,80);
      wyd:=form(TAB1.WYD,10);
      data_od:=form(TAB1.ODD,10);
      data_do:=form(TAB1.DOD,10);
      limit:=form(TAB1.LIMIT,5);
      jm:=form(TAB1.JM,6);
      p:={? TAB1.ILNOR
         || form(({? TAB1.ILNOR=0 || 0 || TAB1.ILZ/TAB1.ILNOR ?})*100,8,2)
         || form(({? TAB1.ILTECH=0 || 0 || TAB1.ILZ/TAB1.ILTECH ?})*100,8,2)
         ?};
      sym:=TAB1.SYM;
    ~~}
    {SUBSTITUTE: 'LINIA0'}
{FI}
{TOTAL: TAB1.SYM}
      {IF: tryb<>1}
         {SUBSTITUTE: 'LINIA0'}
         {z:=n:=t:=p:=ktm:=wyd:=ktm_p:=limit:=jm:='';sumaz:=suman:=sumat:=sumap:=0;~~}
      {FI}
{OD}

:!UTF-8
{TITLE:A. NUCO - Wydruk zamówienia}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   {? exec('czy_grp_dr','faktury_wydruk')
   || __GRP_DR.WZ:='lzk_zdzam_01'
   ?};
   POLA.NAZ_WYDR:='Zamowienie dostaw '+exec('str_to_pth','#string',ZD_NAG.SYM)+'.pdf';
   zdzam_01:="
      VAR_DEL.delete('PRN','PRNS','NAG','w','s','k','p','ddokl','dokl_c');
      VAR_DEL.delete('color','prn1','prn2');
      VAR_DEL.delete('ok');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('il_lin');
      VAR_DEL.delete('zakoncz');
      VAR_DEL.delete('oddzial');
      VAR_DEL.delete('byl','wsk');
      VAR_DEL.delete('M_GD','S_GD');
      VAR_DEL.delete('walh','wyjscie');
      VAR_DEL.delete('max_T8','max_T8G','max_T10B');
      VAR_DEL.delete('__tab_sel','ns','NOMAXP')
      ";
   zdzam_01();

   exec('czytaj','#stalesys',,XINFO); KSTE.get();
   PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,4); PRNS.s_frame();
   NAG:=obj_new(@.CLASS.PRINT,2); NAG.n_frame();
   w:=obj_new(7);
   s:=obj_new(4);
   k:=obj_new(6);  {! _i..6 |! k[_i]:=0  !}; "--szerokosc kolumn--";
   p:=obj_new(6);  {! _i..6 |! p[_i]:='' !}; "--tymczasowa do szacowania szerokosci kolumn--";
   ddokl:=0; "--dokladnosc ilosci--";
   dokl_c:=0; "---dokladnosc ceny--";

   color:=prn1:=prn2:='';
   ok:=0;
   lm:=ll:=vp:=lmt:=rsep:=0;
   il_lin:=0;
   zakoncz:=1;
   oddzial:=KSTE.NAZ<>'';
   byl:=wsk:=0;
   PARAM_W.JEZYK:=0;

   _wydr_naz:='zdzam_01';
   _wydr_edi:='WYDR_ZD';

   wyjscie:=1;

   _par:='IN';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ||
      _par:=WYDRUKIL.FAKS_OD
   ?};
   PARAM_W.INDEKS:='N';
   PARAM_W.NAZWA:='N';
   PARAM_W.UWAGI:='N';
   PARAM_W.KODOBCY:='N';
   PARAM_W.DK_C:='N';
   PARAM_W.DTPREAL:='N';
   PARAM_W.ZDSWD:='N';
   {? _par*'I' ||    PARAM_W.INDEKS:='T' ?};
   {? _par*'N' ||    PARAM_W.NAZWA:='T' ?};
   {? _par*'U' ||    PARAM_W.UWAGI:='T' ?};
   {? _par*'K' ||    PARAM_W.KODOBCY:='T' ?};
   {? _par*'C' ||    PARAM_W.DK_C:='T' ?};
   {? _par*'D' ||    PARAM_W.DTPREAL:='T' ?};
   {? _par*'W' ||    PARAM_W.ZDSWD:='T' ?};

   {? WYDRUKIL.FAK<>''
   || PARAM_W.WALH:=WYDRUKIL.FAK;
      {? PARAM_W.WALH='T' & ZD_NAG.WAL=INFO.NAROD || PARAM_W.WALH:='N' ?}
   || PARAM_W.WALH:={? ZD_NAG.WAL<>INFO.NAROD || 'T' || 'N' ?}
   ?};
   PARAM_W.JEZYK:=WYDRUKIL.JEZYK;
   _djezyk:={? ZD_NAG.KH<>null() || exec('kh_jezyk','tlumaczenia',ZD_NAG.KH) || null() ?};
   PARAM_W.JEZYK:={? _djezyk || _djezyk || WYDRUKIL.JEZYK ?};
   PARAM_W.JEZYK_KH:={? _djezyk & _djezyk=PARAM_W.JEZYK || 'T' || 'N' ?};
   PARAM_W.win_edit(_wydr_edi);
   {? {? exec('czy_grp_dr','faktury_wydruk') || __GRP_DR.FIRST=0 ?} | PARAM_W.edit("
         {? PARAM_W.WALH='T' & ZD_NAG.WAL=INFO.NAROD
         || FUN.info('Wydruk w walucie jest niedostępny.'@);
            'WALH'
         || ''
         ?}
      ")
   ||
      wyjscie:=0;
      WYDRUKIL.FAKS_OD:='';
      {? PARAM_W.INDEKS='T'  || WYDRUKIL.FAKS_OD+='I' ?};
      {? PARAM_W.NAZWA='T'   || WYDRUKIL.FAKS_OD+='N' ?};
      {? PARAM_W.UWAGI='T'   || WYDRUKIL.FAKS_OD+='U' ?};
      {? PARAM_W.KODOBCY='T' || WYDRUKIL.FAKS_OD+='K' ?};
      {? PARAM_W.DK_C='T'    || WYDRUKIL.FAKS_OD+='C' ?};
      {? PARAM_W.DTPREAL='T' || WYDRUKIL.FAKS_OD+='D' ?};
      {? PARAM_W.ZDSWD='T'   || WYDRUKIL.FAKS_OD+='W' ?};
      WYDRUKIL.FAK:=PARAM_W.WALH;
      {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
      WYDRUKIL.JEZYK:=PARAM_W.JEZYK;
      {? PARAM_W.JEZYK<>null() & PARAM_W.JEZYK().KOD<>'POL'
      || PAGELANG:=1;
         SKID.LANG_SLO:=PARAM_W.JEZYK
      || PAGELANG:=0;
         SKID.LANG_SLO:=PARAM_W.JEZYK
      ?};
      WYDRUKIL.put
   ?};

   walh:={? PARAM_W.WALH='T' || 2 || 1 ?};

   FAKSO.cntx_psh;
   FAKSO.use('fakto' + (ZD_NAG.name() + 3));
   ZD_POZ.cntx_psh;
   "PeKa - MOD NUCO - kolejnosc sortowania wg indeksu";
   sortuj:={? __PARAM_W.SORTUJ='T' || ZD_POZ.ndx_tmp(,,'ZD_NAG',,,'M','KTM',) || 'POZ' ?}
;~~}
{END:
   FAKSO.cntx_pop;
   ZD_POZ.cntx_pop;
   "PeKa - MOD NUCO - kolejnosc sortowania wg indeksu";
   ZD_POZ.ndx_drop();&sortuj;
   zdzam_01(); VAR_DEL.delete()
;~~}
{EXIT: wyjscie=1}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_lic.rpi}
{INCLUDE: wsp_strony.rpi}
{IF:ZD_NAG.sel_size()>0 & __GRP_DR.PDF=0}
   {
        ZD_NAG.cntx_psh();
        ZD_POZ.cntx_psh();
      __tab_sel:=ZD_NAG.sel_aget()
   ;~~}
{FOR: __tab_sel}
   {DO}
      {IF: ZD_NAG.seek(__tab_sel.REF,)}
      {
         VAR_DEL.delete('PRN','PRNS','NAG','w','s','k','p');
         VAR_DEL.delete('ok');
         VAR_DEL.delete('zakoncz');

         exec('czytaj','#stalesys',,XINFO); KSTE.get();
         PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame();
         PRNS:=obj_new(@.CLASS.PRINT,4); PRNS.s_frame();
         NAG:=obj_new(@.CLASS.PRINT,2); NAG.n_frame();
         w:=obj_new(7);
         s:=obj_new(4);
         k:=obj_new(6);  {! _i..6 |! k[_i]:=0  !}; "--szerokosc kolumn--";
         p:=obj_new(6);  {! _i..6 |! p[_i]:='' !}; "--tymczasowa do szacowania szerokosci kolumn--";
         NOMAXP:=1;

         ok:=0;
         zakoncz:=1;
         ~~}
         {INCLUDE: lzk_zdzam_01q.rpi}
         {IF: lineleft()>0}{PAGE}{ns:=1;page(1);~~}{FI}
      {FI}
   {OD}
   {
      ZD_NAG.cntx_pop();
      ZD_POZ.cntx_pop()
   ;~~}
{ELSE}
   {ZD_NAG.cntx_psh();
   ZD_POZ.cntx_psh();~~}
   {INCLUDE: lzk_zdzam_01q.rpi}
   {ZD_NAG.cntx_pop();
   ZD_POZ.cntx_pop();~~}
{FI}
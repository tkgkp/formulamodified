:!UTF-8
{TITLE: Stan magazynowy wybranych pozycji do wysyłki}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   5,5,5,5,0,1}
{BEGIN:

   _par:=obj_new('TAB','ndx_TAB','SCEAN','exit','delete');
   _par.TAB:=params_get().TAB;
   {? _par.TAB.find_tab(1,'WYBOR',,'=','T')
   || _par.ndx_TAB:=_par.TAB.ndx_tmp(,1,'WYBOR',,);
      _par.SCEAN:=tab_tmp(,
         'SCEAN','STRING[30]','SCEAN',
         'IL','REAL','Ilość'
      );
      _par.exit:=0;
      _par.delete:="VAR_DEL.delete('PRN','prn1','il_lin','w','color','lm','ll','vp','lmt','rsep')";
      _par.delete();
      w:=obj_new(7);
      PRN:=obj_new(@.CLASS.PRINT,7);
      PRN.s_frame();
      M.cntx_psh()
   || _par.exit:=1
   ?};
   params_set('par',_par);
   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   il_lin:=0;
   ~~
}
{END:
   params_get().par.delete();
   M.cntx_pop();
   ~~
}
{EXIT: params_get().par.exit}
{FONT: 'T8'}{SPACE: 'B'}
{  PRN.add("w[1]",40,'Asortyment');
   PRN.add("w[2]",10,'Zamówienie');
   PRN.add("w[3]",20,'Zamówienie klienta');
   PRN.add("w[4]",25,'Kontrahent');
   PRN.add("w[5]",10,'Pozycja zamówienia',1);
   PRN.add("w[6]",12,'Ilość zamówiona',1);
   PRN.add("w[7]",12,'Ilość dostępna',1);
   ~~
}
{FONT: 'T8G'}{SPACE: 'C'}
{  lmt:=int((charleft()-PRN.width())/2);
   PAR_WYDR.TITLE:='Stan pozycji zamówienia do wysyłki';
   prn1:='PRN';
   ~~
}
{INCLUDE: wsp_pw.rpi}
{DEFINEFONT: 'TMB=Bookman Old Style,,N,,0,0'}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   naglowek tabeli                                          {COMMENT-}
{  prn1:='PRN';~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP;
   {! _ii:=1 .. obj_len(w)
   |! w[_ii]:=''
   !};
   ~~
}
{FOR: params_get().par.TAB}
{INDEX: params_get().par.ndx_TAB}
{PREFIX: 'T'}
{DO}
   {  rsep:=PAR_WYDR.RSEP;
      _TAB:=params_get().par.TAB;
      w[1]:=form(_TAB.M);
      w[2]:=form(_TAB.SYM);
      w[3]:=form(_TAB.ZAM_KL);
      w[4]:=form(_TAB.KH);
      w[5]:=form(_TAB.POZ);
      w[6]:=form(_TAB.ILP,,_TAB.DOKL,'.,');
      w[7]:=form(_TAB.ILD,,_TAB.DOKL,'.,');
      _f:=prn1+'.ini_line()';
      _v:=($(_f))();
      _f:=prn1+'.length()';
      il_lin:=($(_f))()+2;

      _SCEAN:=params_get().par.SCEAN;
      _SCEAN.erase();
      {? M.seek(_TAB.KTM,,1,1)
      || _SL:=sql('
            select
               SLD.SCEAN as SCEAN,
               sum(SLD.IL) as IL
            from @SLD
               left join @SL using (SLD.SL,SL.reference)
               left join M using (SL.M,M.reference)
            where M.REFERENCE=:_a
            group by SLD.SCEAN
            order by 1',
            M.ref()
         );
         {? _SL.first()
         || {!
            |? {! _ii:=1 .. _SCEAN.fld_num()
               |! _acr:=_SCEAN.fld_acr(_ii);
               ($('_a.%1:=_b.%1'[_acr]))(_SCEAN,_SL)
               !};
               _SCEAN.add(1);
               _SL.next()
            !}
         ?}
      ?};
      ~~
   }
   {IF: lineleft()<=il_lin}
      {SUBSTITUTE: 'LINIAF'}{PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
   {IF: params_get().par.SCEAN.size()}
      {FOR: params_get().par.SCEAN}
      {DO}
         {<{lmt+10} 'Kod dostawy   :   '}{params_get().par.SCEAN.SCEAN}{'   Ilość   :   '}{params_get().par.SCEAN.IL}
         {BARCODE: params_get().par.SCEAN.SCEAN;30;1}
      {OD}
   {FI}
{OD}
{SUBSTITUTE: 'LINIAF'}
{ENTIRE-}
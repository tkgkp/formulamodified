:!UTF-8
{TITLE:NUCO - Zamówienia}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   zkzam_01:="
      VAR_DEL.delete('PRN','PRNS','NAG','w','s','k','p','na','szer');
      VAR_DEL.delete('dokl','ddokl','doklw');
      VAR_DEL.delete('color','prn1','prn2');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('il_lin','dokl','doklw','lp');
      VAR_DEL.delete('zakoncz','proforma');
      VAR_DEL.delete('oddzial');
      VAR_DEL.delete('byl','wsk');
      VAR_DEL.delete('M_GD','S_GD');
      VAR_DEL.delete('zero_na','print5','op_1','op_2','op_3','op_4');
      VAR_DEL.delete('max_T8','max_T8G','max_T10B');
      VAR_DEL.delete('wydr_naz','wyjscie','ilwar','__tab_sel')
      ";
   zkzam_01();
   KSTE.get();
   VAR_DEL.delete('PRN','PRNS','w','s','k','p','na','szer');
   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,2); PRNS.s_frame();
   NAG:= obj_new(@.CLASS.PRINT,2); NAG.n_frame();
   kolej:=ask('Podaj sposób sortowania: ',,0,' Pozycje ',' Nazwy');
   ind:={? kolej || ZK_P.ndx_tmp(,,'A',,,'T',,,'N',,,'TOP',,,'M','KTM',) || 'TYPN' ?};
   w:=obj_new(20); "--wartosci kolumn--";
   s:=obj_new(1); "--razem--";
   k:=obj_new(8);  {! _i..obj_len(k) |! k[_i]:=0  !}; "--szerokosc kolumn--";
   p:=obj_new(8);  {! _i..obj_len(k) |! p[_i]:='' !}; "--tymczasowa do szacowania szerokosci kolumn--";
   dokl :=3; "--dokladnosc ilosci--";
   ddokl:=0; "--najwieksza drukowana dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   il_lin:=0;
   zakoncz:=1;
   oddzial:=KSTE.NAZ<>'';
   byl:=wsk:=0;
   proforma:=0;

   ilwar:=1;

   wydr_naz:='zkzam_01';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OZ.USER,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OZ.USER;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   wyjscie:=1;
   {? WYDRUKIL.RADIOB_1:=FUN.choice('Jaki wariant drukować?',WYDRUKIL.RADIOB_1,'Ilościowy','Wartościowy')
   || {? WYDRUKIL.put()
      || wyjscie:=0;
         ilwar:=WYDRUKIL.RADIOB_1
      ?}
   ?}
;~~}
{END:
   zkzam_01();
   ZK_P.ndx_drop();
   ZK_P.index('TYPN');
   VAR_DEL.delete('zkzam_01')
;~~}
{EXIT: wyjscie=1}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_lic.rpi}
{INCLUDE: wsp_strony.rpi}
{MACRO: 'begin'}
{
   XINFO.get(); KSTE.get();
   VAR_DEL.delete('PRN','PRNS','w','s','k','p','na','szer');
   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,2); PRNS.s_frame();
   ind:={? kolej || ZK_P.ndx_tmp(,,'A',,,'T',,,'N',,,'TOP',,,'M','KTM',) || 'TYPN' ?};
   w:=obj_new(11); "--wartosci kolumn--";
   s:=obj_new(1); "--razem--";
   k:=obj_new(8);  {! _i..obj_len(k) |! k[_i]:=0  !}; "--szerokosc kolumn--";
   p:=obj_new(8);  {! _i..obj_len(k) |! p[_i]:='' !}; "--tymczasowa do szacowania szerokosci kolumn--";
   dokl :=3; "--dokladnosc ilosci--";
   ddokl:=0; "--najwieksza drukowana dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";

   lm:=ll:=vp:=lmt:=rsep:=0;
   il_lin:=0;
   zakoncz:=1;
   oddzial:=KSTE.NAZ<>'';
   byl:=wsk:=0;
   proforma:=0;
~~
}
{MACRO-}
{MACRO: 'BODY'}
{FONT: 'T8'}{SPACE: 'B'}
{
"--szerokosc kolumn--";
   ddokl:=0;
   VAR_DEL.delete('X_Xw1');
   X_Xw1:=tab_tmp(1
      ,'P','INTEGER',''
      ,'M','STRING[160]',''
      ,'IL','REAL',''
      ,'J','STRING[10]',''
      ,'C','REAL',''
      ,'WAR','REAL',''
      ,'DOST_D','DATE',''
      ,'DOST_P','DATE',''
      ,'REF','STRING[16]','');

   _war:=0;
   ZK_P.index('NAG'); ZK_P.prefix(ZK_N.ref());
   {? ZK_P.first
   || {!
      |? _pop:=((+form((frac(ZK_P.IL2))))-2);
         {? _pop>0 & _pop>ddokl || ddokl:=_pop ?};
         X_Xw1.clear();
         X_Xw1.blank();
         X_Xw1.P:=ZK_P.POZ;
         X_Xw1.M:=ZK_P.M().KTM+' - '+M.N;
         X_Xw1.IL:=ZK_P.IL2;
         X_Xw1.J:=ZK_P.J2().KOD;
         X_Xw1.C:={? ZK_N.CB='T' || ZK_P.CB || ZK_P.CN ?};
         X_Xw1.WAR:=ZK_P.IL2*X_Xw1.C;
         X_Xw1.DOST_P:=date(0,0,0);
         X_Xw1.DOST_D:=date(0,0,0);
         X_Xw1.REF:=$ZK_P.ref();
         X_Xw1.add(1);
         ZK_P.next
      !}
   ?};
   lmt:=charleft()-4;
   k[1]:=10;                            p[1] :='form(X_Xw1.IL,,ddokl,\' ,\')';
   k[3]:=-4;                           p[3] :='X_Xw1.P';
   k[2]:=0;                            p[2] :='X_Xw1.M';
   k[4]:=5;                            p[4] :='X_Xw1.J';
   k[5]:=13;                           p[5] :='X_Xw1.DOST_D';
   k[6]:=13;                           p[6]:='X_Xw1.DOST_P';
   {? ilwar=2
   || k[7]:=6;                            p[7] :='form(X_Xw1.C,,4,\' ,\')';
      _szer:=+form(_war,,2,' ,');
      k[8]:={? _szer>7 || _szer || 7 ?};  p[8] :='form(X_Xw1.WAR,,4,\' ,\')'
   || k[7]:=0;                            p[7] :='';
      _szer:=+form(ZK_N.NET,,2,' ,');
      k[8]:=0;                            p[8] :=''
   ?};

   _len:=obj_len(k);
   _buf:=obj_new(_len); {! _i.._len |! _buf[_i]:=0 !};
   _max:=obj_new(_len); {! _i.._len |! _max[_i]:=0 !};

   X_Xw1.clear();
   {? X_Xw1.first()
   || {!
      |? {! _i.._len
         |! _war:=($p[_i])();
            _typ:=type_of(_war);
            {? _typ=1 || _w:=(+form(_war));
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=2 || _w:=(+_war);
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=4 || _buf[_i]:=10
            |? _typ=5 || _buf[_i]:=5
            ?}
         !};
         X_Xw1.next()
      !}
   ?};

   _ods:=-3; _all:=0;
   {! _i.._len
   |! {? k[_i]<0         || k[_i]:=-k[_i]
      |? k[_i]<=_buf[_i] || k[_i]:=_buf[_i]
      ?};
      _all+=k[_i]; {? k[_i]>0 || _ods+=3 ?}
   !};
   k[2]:=(lmt-(_all-k[2])-_ods); {? k[2]<0 || msg('Błąd') ?};
   obj_del(_buf); obj_del(_max);

   PRN.add("w[1]",k[3],'Lp.',1);
   PRN.add("w[3]",k[2],'Materiał/Usługa');
   PRN.add("w[4]",k[1],'Ilość',1);
   PRN.add("w[7]",k[4],'jm',1);
   {? ilwar=2
   || PRN.add("w[5]",k[7],{? ZK_N.CB='T' || 'Cena brutto' || 'Cena netto' ?},1);
      PRN.add("w[6]",k[8],{? ZK_N.CB='T' || 'Wartość brutto' || 'Wartość netto' ?},1);

      PRNS.add("'Razem w '+ZK_N.WAL().KOD+':'",k[3]+k[2]+k[1]+k[4]+k[7]+4*3,,1);
      PRNS.add("form(s[1],,doklw)",k[8],,1)
   ?};
   PRN.add("w[10]",k[5],'Data dostawy',1);
   PRN.add("w[11]",k[6],'Potwierdzona data dostawy',1)
;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{
   lmt:=int((charleft()-PRN.width())/2);
   PAR_WYDR.TITLE:='Zamówienie Nr '+ZK_N.SYM+'\n z dnia '+$ZK_N.DP;
   prn1:='PRN'; prn2:='PRNS'
;~~}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEADNUCO'}
{COMMENT}   strony transakcji                                        {COMMENT-}
{ __STRTAB:='ZK_N'; __STRODB:='ZK_N.ODB'; __STRLIN:=''; __STRDAT:=ZK_N.DP;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{COMMENT}   termin realizacji     {COMMENT-}
{IF: ZK_N.ODB<>null}
{<{lmt+1}'Odbiorca: '+ZK_N.ODB().NAZ}
{FI}
{<{lmt+1}'Symbol zamówienia klienta: '+ZK_N.ZAM_KL}
{<{lmt+1}'Handlowiec: '+ZK_N.HAN().NAZ}
{<{lmt+1}'      tel.: '+ZK_N.HAN().TEL}
{<{lmt+1}'     email: '+ZK_N.HAN().EMAIL}
{LINE}
{<{lmt+1}'Termin realizacji: '+$ZK_N.DT}
{COMMENT}   forma platnosci                                          {COMMENT-}
{IF: ZK_N.PL<>null}
   {LINE}
   {<{lmt+1}'Forma płatności: '+ZK_N.PL().TR}
{FI}
{IF: ZK_N.OP<>''}
{<{lmt+1}'Uwagi: '+ZK_N.PL().TR}
{FI}
{COMMENT}   miejsce dostawy                                          {COMMENT-}
{IF: ZK_N.KH_MSC<>null}
   {LINE}
   {<{lmt+1}exec('msc_dost','kontrahent','ZK_N')}
{FI}
{COMMENT}   informacje dodatkowe dla naglowka dokumentu              {COMMENT-}
{
   prn1:='PRN';
   'pobieram dane'; exec('pobgdnag','fakso','Z',ZK_N.ref);
   VAR_DEL.delete('oo','OPIK')
;~~}
{FOR: S_GD}
{PREFIX: 'G'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   naglowek tabeli                                          {COMMENT-}
{  prn1:='PRN';~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEADNUCO'}
   {IF: zakoncz}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
   {FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{IF: _a:=exec('get_par','#parametr',34); _a='Z' | _a='A'}
   {FOOTER}
   {SUBSTITUTE: 'FOOT_LIC'}
   {~~}
   {~~}
   {~~}
   {FOOTER-}
{ELSE}
   {FOOTER}{FOOTER-}
{FI}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{
rsep:=PAR_WYDR.RSEP;
w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:='';
s[1]:=0;
FAKSO.cntx_psh();
FAKSO.use('fakto' + (ZK_N.name() + 3));
lp:=0
;~~}
{FOR:'ZK_P'}{INDEX: ind}{PREFIX: ZK_N.A,'Z',ZK_N.ref,1}
{DO}
   {
      lp+=1;
      rsep:=PAR_WYDR.RSEP;
      w[1]:=form(lp);
      w[3]:=form(ZK_P.M().KTM+' '+ZK_P.M().N);
      w[4]:=form(ZK_P.IL2,,ddokl,'.,');
      w[5]:=form({? ZK_P.N().CB='T' || ZK_P.CB || ZK_P.CN ?},,ZK_P.M().DOKL_C,'.,');
      w[6]:=form(ZK_P.IL2*{? ZK_P.N().CB='T' || ZK_P.CB || ZK_P.CN ?},,doklw,'.,');
      w[7]:=ZK_P.J2().KOD;
      w[8]:=ZK_P.IL2-ZK_P.ILP;
      w[9]:=ZK_P.ILP;
      w[10]:='';
      w[11]:='';
      s[1]+=(ZK_P.IL2*{? ZK_P.N().CB='T' || ZK_P.CB || ZK_P.CN ?})$doklw;

      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))() + 2
   ;~~}
   {IF: lineleft<=il_lin}
      {SUBSTITUTE: 'LINIAF'}{PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
{COMMENT}   informacje dodatkowe dla pozycji dokumentu               {COMMENT-}
   {FOR: FAKSO}{INDEX: 'ZK_P'}{PREFIX: ZK_P.ref}
   {DO}
      {FIRST}
         {
         VAR_DEL.delete('w_tmp');
         w_tmp:=obj_new(obj_len(w));
         {! _i..obj_len(w) |! w_tmp[_i]:=w[_i] !}
         ;~~}
      {FIRST-}
      { {! _i..7 |! w[_i]:='' !};
        w[3]:={? FAKSO.PRN='T' || FAKSO.O |? FAKSO.PRN='A' || FAKSO.T+' '+FAKSO.O || '' ?};
        rsep:=0; _f:=prn1+'.ini_line()'; ($(_f))();~~}
      {WHILE: _f:=prn1+'.next()'; ($(_f))()}
      {DO}
         {IF: lineleft()>0 & lineleft()<4}
            { bpage:=page(); {! _i..obj_len(w) |! w[_i]:=w_tmp[_i] !};~~}
            {SUBSTITUTE: 'LINIAF'}
            {IF: bpage=page}{PAGE}{FI}
            { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
            {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
            { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
         {FI}
         {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
      {OD}
      {TOTAL}
         { VAR_DEL.delete('w_tmp'); vp:=0;~~}
   {OD}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{
FAKSO.cntx_pop();
rsep:=1
;~~}
{IF: ilwar=2}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {IF: lineleft()>0 & lineleft()<3}
      {SUBSTITUTE: 'LINIAF'}
      {IF: lineleft()>0}{PAGE}{FI}
   {FI}
   {SUBSTITUTE: 'LINIA02'}
   {  prn1:='PRNS'; ~~}
{FI}
{SUBSTITUTE: 'LINIAF'}
{  zakoncz:=0; ~~}
{COMMENT}   informacje dodatkowe pod dokumentem              {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: S_GD}
{PREFIX: 'D'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   podpisy                                                  {COMMENT-}
{LINE: 3}
{FONT: 'HEAD'}{SPACE: 'B'}
{IF: ZK_N.US<>null}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,ZK_N.US().DANE);
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=30*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:='Zamówienie przyjął';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{FI}
{MACRO-}
{  ZK_N.cntx_psh();
   {? ZK_N.sel_size()>0
   ||
      __tab_sel:=ZK_N.sel_aget();
      __tab_sel.first()
   ?};
   __i:=1;
~~
}
{WHILE: __i}
{DO}
   {IF: var_pres('__tab_sel')>0 & ZK_N.seek(__tab_sel.REF,)}
         {NOMAXP:=1;~~}
         {SUBSTITUTE: 'begin'}
         {SUBSTITUTE: 'BODY'}
         {ZK_N.sel_del();
          __i:=__tab_sel.next();
         ~~
         }
         {IF: __i}
            {HEADER}
            {HEADER-}
            {PAGE}
            {page(1);NOMAXP:=1;~~}
         {FI}
    {ELSE}
         {SUBSTITUTE: 'BODY'}
         {__i:=0;
         ~~
         }
    {FI}
{OD}
{ZK_N.cntx_pop();
~~
}
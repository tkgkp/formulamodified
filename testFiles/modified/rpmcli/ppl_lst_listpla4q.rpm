:!UTF-8
{TITLE:Lista płac A4 - NUCO}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'I','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('tresc','tekst','dane','next_p','pko_kw','__upr','szer','__TAB','add','lp');
   PAR_WYDR.TITLE:='Lista płac'@;
   tresc:='listpla4';
   P.cntx_psh();
   __upr:=exec('uprawnieniawrap','pkd',
               'Naliczonych składnikach płacowych'@,
               'PPL_PLL_RSKP',
               'PPL_PLL_PSKP'
             );
   {? +__upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[__upr])
   || _args:=exec('wybierz_args','pracownik');
      _args.DOMAIN:='PPL';
      _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
      _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
      _args.F_ZATR:=__PARSES.getVal('F_ZATR').KOD;
      _args.VIEW:=__PARSES.getVal('ZakresDanych');
      _args.SQL_WHERE:='"P".REFERENCE in '
         '(select O_P.P from O_P where O_P.LS=\'T\' and O_P.O=\''+$O.ref()+'\' order by 1)';
      params_set('P',exec('wybierz','pracownik',_args).P);
      tekst:='';
      next_p:=0;
      dane:=0;
      pko_kw:=0;

      szer:=obj_new('L','N','K','X');
      szer.L:=54;
      szer.N:=20;
      szer.K:=12;
      szer.X:=2+szer.L+3+szer.N+3+szer.K+2;

      __TAB:=tab_tmp(1,
         'LP','INTEGER','Lp.',

         'LKRESKA','INTEGER','Kreska?',
         'LTYT','STRING['+$szer.L+']','Etykieta',
         'LTYTSZER','INTEGER','Szerokość etykiety',
         'LFONTW','STRING[1]','Czionka (wartość)',
         'LWART','STRING['+$szer.L+']','Wartość',
         'L','INTEGER','Znacznik użycia',

         'PKRESKA','INTEGER','Kreska?',
         'PFONT','STRING[1]','Czcionka',
         'PNAZWA','STRING['+$szer.N+']','Nazwa składnika',
         'PKWOTA','STRING['+$szer.K+']','Kwota',
         'P','INTEGER','Znacznik użycia'
      );
      add:="
         _kol:=_a;
         _lp:=_b;
         {? ~(_put:=__TAB.find_key(_lp))
         || __TAB.blank();
            __TAB.LP:=_lp;
            __TAB.LFONTW:='i';
            __TAB.PFONT:='i'
         ?};
         {? _kol='L'
         || {? var_pres('_c')=type_of(0)
            || __TAB.LKRESKA:=_c
            ?};
            {? var_pres('_d')=type_of('')
            || __TAB.LTYT:=_d
            ?};
            {? var_pres('_e')=type_of(0)
            || __TAB.LTYTSZER:=_e
            ?};
            {? var_pres('_f')=type_of('')
            || __TAB.LFONTW:=_f
            ?};
            {? var_pres('_g')=type_of('')
            || __TAB.LWART:=_g
            ?};
            __TAB.L:=1
         |? _kol='P'
         || {? var_pres('_c')=type_of(0)
            || __TAB.PKRESKA:=_c
            ?};
            {? var_pres('_d')=type_of('')
            || __TAB.PFONT:=_d
            ?};
            {? var_pres('_e')=type_of('')
            || __TAB.PNAZWA:=_e
            ?};
            {? var_pres('_f')=type_of('')
            || __TAB.PKWOTA:=_f
            ?};
            __TAB.P:=1
         ?};

         {? _put
         || __TAB.put()
         || __TAB.add()
         ?}
      ";
      lp:=1

   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('tresc','tekst','dane','next_p','pko_kw','__upr','szer','__TAB','add','lp')
}
{COMMENT}OLD: listpla4.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: +__upr}
{EXIT: ~params_get().P.first()}
{HEADER}
{FONT:'i'}
{>{charleft} KST.MIASTO+', dn. '@+$date()}
{<1 STR.split(KST.NAZWA);STR.line(charleft-1)}
{WHILE: STR.next()}{DO}{<1 STR.line(charleft-1)}{OD}
{charleft*'─'}
{FONT:'j'}
{<1>{charleft} 'L I S T A   P Ł A C'@}
{<1>{charleft} VAR.NAZWALIS+' ('+date(O.R,O.M,1)$8+')'}
{IF: +O.O}
{<1>{charleft}O.O}
{FI}
{FONT:'i'}
{HEADER-}
{MACRO: tresc}
{
   next_p:=0;
   __TAB.erase();
   lp:=1;

   LS.cntx_psh();
   LS.index('PRACLPRU');
   LS.prefix(P.ref());
   _again:=LS.first();
   {!
   |? _again
   |! {? LS.RB().KDL='T'
      || {? lp=1
         || lp+=add('P',lp,,,'Składnik','Kwota');
            lp+=add('P',lp,1)
         ?};
         _sf:={? 'GT'*LS.RB().RK | LS.RB().RN=990 || 'j' || 'i' ?};
         {? LS.KW<>0
         || lp+=add('P',lp,,_sf,LS.RB().RT,form(LS.KW,szer.K,2,'9,'))
         ?}
      ?};
      _again:=LS.next()
   !};
   LS.cntx_pop();

   {? lp>1
   || lp:=1;
      _szer:=15;
      lp+=add('L',lp,,'Nazwisko',_szer,'j',P.OSOBA().NAZWISKO);
      lp+=add('L',lp,,'Imię (pierwsze)',_szer,'j',P.OSOBA().PIERWSZE);
      {? P.OSOBA().DRUGIE<>''
      || lp+=add('L',lp,,'     (drugie)',_szer,'j',P.OSOBA().DRUGIE)
      ?};
      lp+=add('L',lp,,'Numer teczki',_szer,'j',|P.T);

      'NUCO';
      lp+=add('L',lp,1);
      _et:='Jednostka org.';
      STR.split(P.WYDZIAL().SYMBOL+'-'+P.WYDZIAL().OPIS);
      {!|?
        STR.next()
      |! lp+=add('L',lp,,_et,_szer,'j',STR.line(szer.L-_szer));
         _et:=''
      !};


      H.cntx_psh();
      H.index('_HISTDAT');
      H.prefix(P.ref());
      {? H.first()
      || lp+=add('L',lp,1);
         lp+=add('L',lp,,'Zmiany w przebiegu zatrudnienia');
         lp+=add('L',lp,1);
         _szer:=23;
         _first:=1;
         {!
         |? {? H.OD<=date(O.R,O.M,0) & (H.DO=date(0,0,0) | date(O.R,O.M,1)<=H.DO)
            || {? _first
               || _first:=0
               || lp+=add('L',lp,,)
               ?};
               lp+=add('L',lp,,H.KZ().R,_szer,,
                  'od '+H.OD$4+{? H.DO=date(0,0,0) || '' || ' do '+H.DO$4 ?}
               );
               _et:='Stanowisko';
               STR.split(H.ST().ST);
               {!
               |? STR.next()
               |! lp+=add('L',lp,,_et,_szer,,STR.line(szer.L-_szer));
                  _et:=''
               !};
               lp+=add('L',lp,,'Wymiar',_szer,,$H.WYL+'/'+$H.WYM);
               lp+=add('L',lp,,'płaca zas./stawka godz.',_szer,,
                  form(H.S1,20,2,'9,')+' '+{? H.CZYWAL='T' || H.WAL().KOD || 'PLN' ?}
               );
               {? H.S2<>0
               || lp+=add('L',lp,,'dod. funkc./brygadz.',_szer,,
                     form(H.S2,20,2,'9,')+' '+{? H.S2T='P' || '%' |? H.CZYWAL2='T' || H.WAL().KOD || 'PLN' ?}
                  );
                  {? H.S2T='P'
                  || STR.split(H.S2P().NAZWA);
                     {!
                     |? STR.next()
                     |! lp+=add('L',lp,,,_szer,,STR.line(szer.L-_szer))
                     !}
                  ?}
               ?};
               {? H.S3<>0
               || lp+=add('L',lp,,'dodatek',_szer,,
                     form(H.S3,20,2,'9,')+' '+{? H.S3T='P' || '%' |? H.CZYWAL3='T' || H.WAL().KOD || 'PLN' ?}
                  );
                  {? H.S3T='P'
                  || STR.split(H.S3P().NAZWA);
                     {!
                     |? STR.next()
                     |! lp+=add('L',lp,,,_szer,,STR.line(szer.L-_szer))
                     !}
                  ?}
               ?};
               {? H.S4<>0
               || lp+=add('L',lp,,'płaca oddelegowanie',_szer,,
                     form(H.S4,20,2,'9,')+' '+{? H.CZYWAL4='T' || H.WAL().KOD || 'PLN' ?}
                  )
               ?};
               {? O.DKURS<>date(0,0,0) & (H.CZYWAL='T' | H.CZYWAL2='T' | H.CZYWAL3='T')
               || exec('waluta_miara','lista_licz',SLO.KOD);
                  lp+=add('L',lp,,'(kurs z dnia '+O.DKURS$4+':'+SLO.KOD+' = '+
                     form(exec('waluta_kurs','lista_licz',H.ZWAL,O.DKURS,H.WAL),,4,'9,')+' PLN)')
               ?}
            ?};
            H.next()
         !}
      ?};
      H.cntx_pop();

      N.cntx_psh();
      N.index('NIEOBECL');
      N.prefix('N',P.ref(),VAR.NAZWALIS);
      {? N.first()
      || lp+=add('L',lp,1);
         lp+=add('L',lp,,'Nieobecności rozliczone listą płac');
         lp+=add('L',lp,1);
         _first:=1;
         _szer:=20;
         {!
         |? {? _first
            || _first:=0
            || lp+=add('L',lp,,)
            ?};
            lp+=add('L',lp,,N.NB().RT,_szer,,'od '+N.OD$4+' do '+N.DO$4);
            lp+=add('L',lp,,,_szer,,'dni: kalend. '+form(N.NK,2)+', robocze '+form(N.NR,2));
            lp+=add('L',lp,,,_szer,,'podstawa '+form(N.POD,,2,'9,'));
            lp+=add('L',lp,,,_szer,,'st. dzienna '+form(N.STD,,2,'9,'));
            N.next()
         !}
      ?};
      N.cntx_pop()
   ?};

   {? __TAB.first()
   || _maxl:=_maxp:=1;
      {!
      |? {? __TAB.L
         || _maxl:=__TAB.LP
         ?};
         {? __TAB.P
         || _maxp:=__TAB.LP
         ?};
         __TAB.next()
      !};
      add('L',_maxl+1,2);
      add('P',_maxp+1,2)
   ?};
   ~~
}
{FOR: __TAB}
{DO}
   {FIRST}
      {<1 '┌'+((szer.L+2)*'─')+'┬'+((szer.N+2)*'─')+'┬'+((szer.K+2)*'─')+'┐' }
   {FIRST-}
   {IF: lineleft()<2 & __TAB.LP<__TAB.size()}
      {REP:
         _rep:='';
         __TAB.cntx_psh();
         {? __TAB.prev()
         || _rep:=
               {? __TAB.L & __TAB.LKRESKA<2
               || '{<1 \'└\'+((szer.L+2)*\'─\')}'
               || ''
               ?}+
               {? __TAB.L & __TAB.LKRESKA<2 & __TAB.P & __TAB.PKRESKA<2
               || '{<{szer.L+4} \'┴\'}'
               |? __TAB.L & __TAB.LKRESKA<2 & __TAB.PKRESKA<>1
               || '{<{szer.L+4} \'┘\'}'
               |? __TAB.LKRESKA<>1 & __TAB.P & __TAB.PKRESKA<2
               || '{<{szer.L+4} \'└\'}'
               || ''
               ?}+
               {? __TAB.P & __TAB.PKRESKA<2
               || '{<{szer.L+5} ((szer.N+2)*\'─\')+\'┴\'+((szer.K+2)*\'─\')+\'┘\'}'
               || ''
               ?}+'{PAGE}';
            next_p:=1
         ?};
         __TAB.cntx_pop();
         _rep
      }
      {REP:
         {? __TAB.L & __TAB.LKRESKA<2
         || '{<1 \'┌\'+((szer.L+2)*\'─\')}'
         || ''
         ?}+
         {? __TAB.L & __TAB.LKRESKA<2 & __TAB.P & __TAB.PKRESKA<2
         || '{<{szer.L+4} \'┬\'}'
         |? __TAB.L & __TAB.LKRESKA<2 & __TAB.PKRESKA<>1
         || '{<{szer.L+4} \'┐\'}'
         |? __TAB.LKRESKA<>1 & __TAB.P & __TAB.PKRESKA<2
         || '{<{szer.L+4} \'┌\'}'
         || ''
         ?}+
         {? __TAB.P & __TAB.PKRESKA<2
         || '{<{szer.L+5} ((szer.N+2)*\'─\')+\'┬\'+((szer.K+2)*\'─\')+\'┐\'}'
         || ''
         ?}
      }
   {FI}
   {REP:
      {? __TAB.L
      || {? next_p & __TAB.LKRESKA
         || ''
         |? __TAB.LKRESKA=1
         || '{<1 \'├\'+((szer.L+2)*\'─\')}'
         |? __TAB.LKRESKA=2
         || '{<1 \'└\'+((szer.L+2)*\'─\')}'
         || '{<1 \'│\'} {__TAB.LTYT} {<{__TAB.LTYTSZER+4}}{FONT:__TAB.LFONTW}{__TAB.LWART}{FONT:\'i\'}'
         ?}
      || ''
      ?}+
      {? next_p
      || '{<{szer.L+4} \'│\'}'
      |? __TAB.L & __TAB.LKRESKA & __TAB.P & __TAB.PKRESKA
      || {? __TAB.LKRESKA=2 & __TAB.PKRESKA=2
         || '{<{szer.L+4} \'┴\'}'
         || '{<{szer.L+4} \'┼\'}'
         ?}
      |? (__TAB.L | __TAB.P) & ~__TAB.LKRESKA & ~__TAB.PKRESKA
      || '{<{szer.L+4} \'│\'}'
      |? __TAB.L & __TAB.LKRESKA
      || {? __TAB.P | __TAB.LKRESKA=1
         || '{<{szer.L+4} \'┤\'}'
         || '{<{szer.L+4} \'┘\'}'
         ?}
      |? __TAB.P & __TAB.PKRESKA
      || {? __TAB.L | __TAB.PKRESKA=1
         || '{<{szer.L+4} \'├\'}'
         || '{<{szer.L+4} \'└\'}'
         ?}
      || '?'
      ?}+
      {? __TAB.P
      || {? next_p & __TAB.PKRESKA
         || ''
         |? __TAB.PKRESKA=1
         || '{<{szer.L+5} ((szer.N+2)*\'─\')+\'┼\'+((szer.K+2)*\'─\')+\'┤\'}'
         |? __TAB.PKRESKA=2
         || '{<{szer.L+5} ((szer.N+2)*\'─\')+\'┴\'+((szer.K+2)*\'─\')+\'┘\'}'
         || '{<{szer.L+5}} {FONT:__TAB.PFONT}{__TAB.PNAZWA}{FONT:\'i\'}'+
            '{<{szer.L+5+szer.N+2} \'│\'} {FONT:__TAB.PFONT}{__TAB.PKWOTA}{FONT:\'i\'}'+
            '{<{szer.L+5+szer.N+2+szer.K+3} \'│\'}'
         ?}
      || ''
      ?}
   }
   { next_p:=0; ~~}
{OD}
{IF: dane:=__TAB.first()}
{ dane:=0; ~~ }
{FOR: PKO}{INDEX: '_PKO'}{PREFIX: P.OSOBA}{DO}
{FIRST}
{ dane:=1; ~~ }
┌{(szer.X-2)*'─'}┐
│ Informacje o przekazaniu wynagrodzenia na rachunki oszcz.-rozl.{>{szer.X}}│
├{(szer.X-2)*'─'}┤
{FIRST-}
{ pko_kw:=FUNKCJE.L(PKO.R().RN); ~~ }
{IF: +('│ '+PKO.N+' '+PKO.BA().NB+'    '+form(pko_kw,,2,'9,')+' zł. │')<=szer.X}
│ {PKO.N} {PKO.BA().NB}{>{szer.X} form(pko_kw,,2,'9,')+' zł. │'}
{ELSE}
│ {PKO.N}{>{szer.X} form(pko_kw,,2,'9,')+' zł. │'}
│ {PKO.BA().NB}{>{szer.X} ' │'}
{FI}
{OD}
{IF: dane}
└{(szer.X-2)*'─'}┘
{FI}
{IF: O.D<>date(0,0,0)}
Wynagrodzenie zostało przekazane (wypłacone) dnia {O.D$1}
{FI}
{FOR: KART_URL}{INDEX: 'PRAC_ROK'}{PREFIX: P.ref,O.R}{DO}
{FIRST}
{  _r:=(O.R>=2004);
   _ld:=KART_URL.LIM_AKT;
   _lg:=KART_URL.LIM_AKTG;

   _a:='Przysługujący limit urlopu wypoczynkowego w roku '@+$O.R+' wynosi '@+
       $_ld+{? _ld=1 || ' dzień'@ || ' dni'@?}+
       {? _r || ' (godz. '@+$_lg+')' || '' ?}+'.'+
       {? KART_URL.LIM_ZAL
       || ' W ubiegłym roku nie wykorzystano '@+$KART_URL.LIM_ZAL+
          {? KART_URL.LIM_ZAL=1 || '-ego dnia'@ || ' dni'@ ?}+
          {? _r || ' (godz. '@+$KART_URL.LIM_ZALG+') ' || '' ?}+
          ' urlopu.'@
       || ''
       ?}+
       {? KART_URL.DATA_DOD<>date(0,0,0)
       || ' Od dnia '@+(KART_URL.DATA_DOD$4)+' przysługuje prawo do '@+
          $KART_URL.URL_DOD+{? KART_URL.URL_DOD=1 || '-ego dnia'@ || ' dni'@ ?}+
          {? _r || ' (godz. '@+$KART_URL.URL_DODG+')' || '' ?}+
          ' urlopu uzupełniającego.'@
       || ''
       ?}+
       {? KART_URL.DATA_NSP<>date(0,0,0)
       || ' Od dnia '+(KART_URL.DATA_NSP$4)+' przysługuje prawo do '@+
          $KART_URL.URL_NSP+{? KART_URL.URL_NSP=1 || '-ego dnia'@ || ' dni'@ ?}+
          {? _r || ' (godz. '@+$KART_URL.URL_NSPG+')' || '' ?}+
          ' urlopu dodatkowego.'@+
          {? KART_URL.NSP_ZAL
          || ' W ubiegłym roku nie wykorzystano '@+$KART_URL.NSP_ZAL+
             {? KART_URL.NSP_ZAL=1 || '-ego dnia'@ || ' dni'@ ?}+
             {? _r || ' (godz. '@+$KART_URL.NSP_ZALG+') ' || '' ?}+
             ' tego urlopu.'@
          || ''
          ?}
       || ''
       ?}+
       {? KART_URL.URL_WYK
       || ' W roku bieżącym wykorzystano '@+$KART_URL.URL_WYK+
          {? KART_URL.URL_WYK=1 || ' dzień'@ || ' dni'@ ?}+
          {? _r || ' (godz. '@+$KART_URL.URL_WYKG+')' || '' ?}+
          ' urlopu.'@+
          {? KART_URL.cntx_psh(); KART_URL.index('PRAC_ROK'); KART_URL.prefix(); KART_URL.find_key(P.ref(),O.R-1)
          || {? KART_URL.PIERWSZY = 'T'
             || _x:=
                  ' (w tym '@+$KART_URL.URL_WYK+{? KART_URL.URL_WYK=1 || ' dzień'@ || ' dni'@ ?}+
                  ' wykorzystanych w '@+$(O.R-1)+' roku)'@
             || _x:=''
             ?};
            KART_URL.cntx_pop();
            _x
          || KART_URL.cntx_pop();
             ''
          ?}
       || ''
       ?}+
       {? KART_URL.URL_POZ
       || ' Pozostał'@+
          {? KART_URL.URL_POZ+KART_URL.NSP_POZ=1 || '1 dzień'@ || 'o '@+$(KART_URL.URL_POZ+KART_URL.NSP_POZ)+' dni'@?}+
          {? _r || ' (godz. '@+$(KART_URL.URL_POZG+KART_URL.NSP_POZG)+')' || '' ?}+
          ' do wykorzystania.'@
       || ''
       ?};
       STR.split(_a);
   ~~
}

{WHILE: +(tekst:=STR.line(szer.X))}
{DO}{tekst}{LINE}{OD}
{FIRST-}
{OD}
{PAGE}
{FI}
{MACRO-}
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
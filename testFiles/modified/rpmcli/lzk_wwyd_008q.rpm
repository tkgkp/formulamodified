{TITLE:I. stany iložciowe towar¢w z uwzgl‘dnieniem iložci zarezerwowanych}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
  M.cntx_psh();
  MG.cntx_psh();
  SM.cntx_psh();
  {? var_pres('PRN') > 0 || obj_del(PRN) ?};
  rezerw := wdrodze := stan := minimum := braki := 0;
  re_mag := w_dr_mag := stan_mag := mini_mag := brak_mag := 0;
  magazyn := towar := '';
  color:=prn1:=prn2:='';
  lm:=ll:=vp:=lmt:=rsep:=0;
  PRN := obj_new(@.CLASS.PRINT, 9);
  prn1 := 'PRN';
  PRN.s_frame();

  "ZAMOW.CZ_SP:=WYDRUK.ZAM";
  "ZAMOW.CZ_R:=WYDRUK.RODZAJ";
  "ZAMOW.CZ_ZK:=WYDRUK.WYBOR";
  "ZAMOW.CZ_MN:=WYDRUK.NWART";
  WYDRUK.ZAM:=WYDRUK.RODZAJ:=WYDRUK.WYBOR:=WYDRUK.NWART:='T';
  "ZAMOW.TOW_USL := 'T'";
  WYDRUK.TOW:='T';
  "ZAMOW.win_edit('WYDRUK1')";
  _edit:=WYDRUK.mk_edit();
  WYDRUK.win_efld(_edit,,'ZAM',,,,,,'Czy zam¢wienia sprzeda§y?',,,'check-box',,"'T'","'N'");
  WYDRUK.win_efld(_edit,,'RODZAJ',,,,,,'Czy rezerwacje?',,,'check-box',,"'T'","'N'");
  WYDRUK.win_efld(_edit,,'WYBOR',,,,,,'Czy zam¢wienia zakupu?',,,'check-box',,"'T'","'N'");
  WYDRUK.win_efld(_edit,,'NWART',,,,,,'Czy minimalne?',,,'check-box',,"'T'","'N'");
  WYDRUK.win_edit(_edit);

  _edit2:=BEER.mk_edit();
  BEER.win_efld(_edit2,,'ODM','KTM','MATKTM',,,0,'Od towaru');
  BEER.win_efld(_edit2,,'DOM','KTM','MATKTM',,,0,'Do towaru');
  BEER.win_edit(_edit2);
  BEER.T:='I';

  {? WYDRUK.edit()
  || akcja := 1
  || akcja := 0
  ?};

  {? M.first() || BEER.ODM := M.ref() ?};
  {? M.last() || BEER.DOM := M.ref() ?};

  {? akcja & BEER.edit()
  || akcja := 1
  || akcja := 0
  ?};

  {? akcja
  ||
     {? BEER.ODM = null()
     || {? M.first() || BEER.ODM := M.ref() ?}
     ?};
     {? BEER.DOM = null()
     || {? M.last() || BEER.DOM := M.ref() ?}
     ?};
        {? BEER.MG = null()
     ||
        MG.clear();
        MG.index('MAGAZYNY');
        _magazyny := '';
        {? MG.first()
        ||
           {!|?
                _magazyny += '\'' + $MG.ref() + '\',';
                MG.next()
           !};
           {? _magazyny <> '' || _magazyny -= 1 ?}
        ?}
     ?};
     VAR_DEL.delete('REZS','REZD');
     {? WYDRUK.ZAM = 'T'
     ||
     REZS := sql('select ZK_P.MG, ZK_P.M, ' +
                 'ZK_P.ILP, ZK_P.ILR ' +
                 'from @ZK_P join @ZK_N using(ZK_P.N, ZK_N.REFERENCE) ' +
                 'join M using(ZK_P.M, M.REFERENCE) ' +
                 'where M.KTM between \':_a\' and \':_b\' ' +
                 'and ZK_N.A = \'T\' '+
                 {? BEER.MG = null()
                 || ' and ZK_P.MG in (:_c) '
                 || ' and ZK_P.MG = :_c '
                 ?}+
                 ' order by 1,2'
                , BEER.ODM().KTM, BEER.DOM().KTM, {? BEER.MG=null() || _magazyny || BEER.MG ?} )
     ?};
     {? WYDRUK.WYBOR = 'T'
     ||
     REZD := sql('select ZD_POZ.MG, ZD_POZ.M, ' +
                 'ZD_POZ.IL_POZ ILW ' +
                 'from @ZD_POZ join @ZD_NAG using(ZD_POZ.ZD_NAG, ZD_NAG.REFERENCE) ' +
                 'join M using(ZD_POZ.M, M.REFERENCE) ' +
                 'where M.KTM between \':_a\' and \':_b\' ' +
                 {? BEER.MG = null()
                 || ' and ZD_POZ.MG in (:_c) '
                 || ' and ZD_POZ.MG = :_c '
                 ?}+
                 ' order by 1,2'
                , BEER.ODM().KTM, BEER.DOM().KTM, {? BEER.MG=null() || _magazyny || BEER.MG ?} )
     ?};
     PRN.add("towar", 30, 'Towar');
     {? BEER.MG = null()
     || PRN.add("magazyn", 15, 'Magazyn')
     ?};
     PRN.add("form(stan,15,4,' ,')",20,'Stan',1);
     {? WYDRUK.ZAM = 'T'
     ||
        {? WYDRUK.RODZAJ = 'T'
        ||
           PRN.add("form(rezerw, 15,4, ' ,')", 20, 'Rezerwacja', 1)
        ?}
     ?};
     {? WYDRUK.WYBOR = 'T'
     ||
        PRN.add("form(wdrodze, 15,4, ' ,')", 20, 'W drodze', 1)
     ?};
     PRN.add("form({? braki < 0 || -braki || braki ?}, 15, 4, ' ,')", 20, 'Braki', 1)
  ?};
  PAR_WYDR.TITLE := 'S T A N Y  T O W A R O W E   Z   U W Z G L  D N I E N I E M \nI L O ˜ C I   Z A R E Z E R W O W A N Y C H'
}
{END:
  SM.cntx_pop();
  MG.cntx_pop();
  M.cntx_pop();
  &rezerw; &wdrodze; &minimum; &braki;
  &re_mag; &w_dr_mag; &stan_mag; &mini_mag; &brak_mag;
  &magazyn; &towar; &prn1; &rsep; &lmt;
  obj_del(PRN)
}
{EXIT: ~akcja}
{INCLUDE: wsp_pw.rpi}
{vp := 1; rsep:=PAR_WYDR.RSEP; lmt:=int((charleft()-PRN.width())/2);~~}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE}
{FONT: 'T8G'}{SPACE: 'C'}
{<1 'PARAMETRY'}
{<1 'MAGAZYN: ' + {? BEER.MG <> null() || BEER.MG().SYM || 'WSZYSTKIE'?} }
{<1 'ZAKRES KOD£W TOWAR£W: OD KODU ' + BEER.ODM().KTM + ' DO KODU ' + BEER.DOM().KTM}
{IF: WYDRUK.ZAM='T'}{<1 'REZERWACJE LICZONE NA PODSTAWIE ZAM£WIE¥ SPRZEDA¡Y'}{FI}
{IF: WYDRUK.WYBOR='T'}{<1 'ILO˜CI ZAM£WIONE \'W DRODZE\' LICZONE NA PODSTAWIE ZAM£WIE¥ ZAKUPU'}{FI}
{<1 'CZY UWZGLDNIONO STANY MINIMALNE: ' +{? WYDRUK.NWART='T' || 'TAK' || 'NIE' ?}}
{LINE}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{vp:=1;~~}
{HEADER-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE}
{FOOTER-}
{MACRO: 'rezerw'}
{IF: WYDRUK.ZAM = 'T' & WYDRUK.RODZAJ = 'T'}
     { REZS.prefix(); REZS.prefix($MG.ref(), $M.ref());
       {? REZS.first()
       ||
          {!|? re_mag += rezerw := REZS.ILR; REZS.next() !}
       ?}
     ;~~}
{FI}
{MACRO-}
{MACRO: 'wdrodze'}
{IF: WYDRUK.WYBOR = 'T'}
     { REZD.prefix(); REZD.prefix($MG.ref(), $M.ref());
       {? REZD.first()
       ||
          {!|? w_dr_mag += wdrodze := REZD.ILW; REZD.next() !}
       ?}
     ;~~}
{FI}
{MACRO-}
{FOR: 'M'}
{INDEX: 'RODZ'}
{FROM: 'T', BEER.ODM().KTM, BEER.ODM().KTM}
{TO: 'T', BEER.DOM().KTM, BEER.DOM().KTM}
{DO}
   {towar := M.KTM + ' - ' + M.N; ~~}
   {FOR: MG}
   {INDEX: 'MAGAZYNY'}
   {PREFIX: {? BEER.MG <> null() || BEER.MG().SYM || '' ?} }
   {DO}
       {rezerw:=wdrodze:=stan:=minimum:=braki:=0;
       SM.clear();
       SM.index('SM');
       {? SM.find_key(MG.ref(),M.ref() )
       ||
          stan += SM.S;
          stan_mag += SM.S
       ?};~~}
       {SUBSTITUTE: 'rezerw'}
       {SUBSTITUTE: 'wdrodze'}
       {brak_mag += braki := stan - rezerw - minimum + wdrodze; ~~}
       {IF: rezerw <> 0 | wdrodze <> 0 | stan <> 0 | minimum <> 0 | braki <> 0}
       {SUBSTITUTE: 'LINIA0'}
       {FI}
   {OD}
{IF: BEER.MG = null()
     & ( re_mag <> 0 | w_dr_mag <> 0 | stan_mag <> 0 | mini_mag <> 0 | brak_mag <> 0 )}
{magazyn:='OG£œEM'; towar := ''; rezer := re_mag; wdrodze := w_dr_mag; stan := stan_mag;
 minimum := mini_mag; braki := brak_mag
;~~}
{SUBSTITUTE:'LINIA0'}
{re_mag := w_dr_mag := stan_mag := mini_mag := brak_mag := 0;~~}
{FI}
{OD}
:!UTF-8
{TITLE:NUCO - Stan wykonania zamówienia (tabela)}
{BEGIN:
   VAR_DEL.delete('kolej','ind','dokl','ddokl','doklw','wyjscie');
   kolej:=ask('Podaj sposób sortowania: ',,0,' Pozycje ',' Nazwy');
   ind:={? kolej || ZK_P.ndx_tmp(,,'A',,,'T',,,'N',,,'TOP',,,'M','KTM',) || 'TYPN' ?};
      dokl :=3; "--dokladnosc ilosci--";
   ddokl:=0; "--najwieksza drukowana dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";
   wyjscie:=0
;~~}
{END:
   undefine();
   ZK_P.ndx_drop();
   ZK_P.index('TYPN');
   VAR_DEL.delete('kolej','ind','dokl','ddokl','doklw','wyjscie','X_Xw1')
;~~}
{EXIT: {? ~wyjscie
       || define('DAT_REAL',date(),'Wykonanie na dzień:',,10,10);
          wyjscie:=~def_edit("chk_rec")
       ?};
       wyjscie=1}
{
   VAR_DEL.delete('X_Xw1');
   X_Xw1:=tab_tmp(1
      ,'P','INTEGER','Lp.'
      ,'KTM','STRING[50]','KTM'
      ,'NAZ','STRING[100]','Nazwa'
      ,'IL','REAL','Zamówiono'
      ,'J','STRING[10]','jm'
      ,'C','REAL','Cena'
      ,'WAR','REAL','Wartość'
      ,'IL_ZRE','REAL','Wysłano'
      ,'STAN','REAL','Stan magazynu'
      ,'PROC','REAL','Procent wykonania'
      ,'IL_POZ','REAL','Pozostało'
      ,'MAG','STRING[8]','Magazyn'
      ,'DOST_D','DATE',''
      ,'REF','STRING[16]','');

   ZK_P.index('NAG'); ZK_P.prefix(ZK_N.ref());
   {? ZK_P.first
   || {!
      |? _pop:=((+form((frac(ZK_P.IL2))))-2);
         {? _pop>0 & _pop>ddokl || ddokl:=_pop ?};
         X_Xw1.clear();
         X_Xw1.blank();
         X_Xw1.P:=ZK_P.POZ;
         X_Xw1.KTM:=ZK_P.M().KTM;
         X_Xw1.NAZ:=ZK_P.M().N;
         X_Xw1.IL:=ZK_P.IL2$ddokl;
         X_Xw1.J:=ZK_P.J2().KOD;
         X_Xw1.C:={? ZK_N.CB='T' || ZK_P.CB$2 || ZK_P.CN$2 ?};
         X_Xw1.WAR:=(ZK_P.IL2*X_Xw1.C)$2;
         X_Xw1.IL_ZRE:=(exec('real2date','qlsp',DEFINE.DAT_REAL,ZK_P.POZ))$ddokl;
         X_Xw1.STAN:=(exec('stan','qlsp',ZK_P.M,DEFINE.DAT_REAL))$ddokl;
         X_Xw1.IL_POZ:=(X_Xw1.IL-X_Xw1.IL_ZRE)$ddokl;
         X_Xw1.PROC:=(((X_Xw1.IL_ZRE+X_Xw1.STAN)/ZK_P.IL2)*100)$doklw;
         X_Xw1.MAG:=ZK_P.MG().SYM;
         X_Xw1.add(1);
         ZK_P.next
      !}
   ?};

_winsel:=X_Xw1.mk_sel('STAN WYKONANIA ZAMÓWIENIA Nr '+ZK_N.SYM+'\nNA DZIEŃ: '+DEFINE.DAT_REAL$1,,,,1);
X_Xw1.win_fld(_winsel,,'P',,,3,,,'Lp.');
X_Xw1.win_fld(_winsel,,'KTM',,,14,,,'KTM');
X_Xw1.win_fld(_winsel,,'NAZ',,,15,,,'Nazwa');
X_Xw1.win_fld(_winsel,,'IL',,,10,ddokl,,'Zamówiono');
X_Xw1.win_fld(_winsel,,'J',,,4,,,'jm');
X_Xw1.win_fld(_winsel,,'C',,,5,2,,'Cena');
X_Xw1.win_fld(_winsel,,'WAR',,,12,2,,'Wartość');
X_Xw1.win_fld(_winsel,,'IL_ZRE',,,10,ddokl,,'Wysłano');
X_Xw1.win_fld(_winsel,,'STAN',,,10,ddokl,,'Stan mag.');
X_Xw1.win_fld(_winsel,,'PROC',,,10,ddokl,,'% wykonania');
X_Xw1.win_fld(_winsel,,'IL_POZ',,,10,ddokl,,'Pozostało');
X_Xw1.win_fld(_winsel,,'MAG',,,5,,,'Magazyn');
X_Xw1.win_sel(_winsel);
X_Xw1.select();
~~
}
{EXIT}




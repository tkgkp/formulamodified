:!UTF-8
{COMMENT}OLD: zaswump2.rpi{COMMENT-}
{  __uzas1:=__uzas2:='';
   'Ustalenie czy jest to nowa umowa na czas określony wraz z uzasadnieniem zawarcia';
   {? H_UM.RU().K='B' & H_UM.OD>=date(2016,2,22)
   || {? H_UM.WYM_UZAS='T'
      || __okr_new:=1;
         _uzasd:=exec('memo2print','#string',H_UM,'UZASDN');
         __uzas1:=253+_uzasd;
         __uzas2:=253-_uzasd
      || __okr_new:=0
      ?}
   || __okr_new:=0
   ?};
   H.cntx_psh();
   H.index('HISTUM');
   H.prefix(H_UM.ref());
   H.first();
   info.OZA:=H.P().OSOBA().PIERWSZE+' '+OSOBA.NAZWISKO+
           ', zamieszkał'+{? OSOBA.PLEC='K' || 'a' || 'y' ?}+' '+
           exec('adres','osoba',H.OS_ADRES().RODZAJ,H.OS_ADRES().OD);
   info.PESEL:=OSOBA.PESEL;
   info.DZU:=H.PODPIS;
   info.ORZ:=H.PRZEDST;
   _ru:=H.RU().K;
   info.UNA:=
      {? _ru='A'
      || 'okres próbny, do dnia zakończenia %1 r.' [H_UM.P_DO$6]
      |? ((_ru='B' & H_UM.OKR_UZAS().KOD='1') | _ru='D')  & H_UM.P_DO=date(0,0,0)
      || 'czas nieobecności zastępowanego pracownika'
      |? _ru='B' & H_UM.P_OD>=date(2016,2,22)
         & H_UM.OKR_UZAS().KOD='1'
      || 'czas nieobecności zastępowanego pracownika, do dnia zakończenia %1 r.' [H_UM.P_DO$6]
      |? _ru='B' & H_UM.P_OD>=date(2016,2,22)
      || 'czas określony, do dnia zakończenia %1 r.' [H_UM.P_DO$6]
      |? _ru='C'
      || 'czas nieokreślony'
      |? _ru='D'
      || 'zastępstwo, do dnia zakończenia %1 r.' [H_UM.P_DO$6]
      |? _ru='E'
      || 'czas określony, do dnia porodu'
      || ''
      ?};
   info.STN:=H.ST().ST;
   info.WYD:=H.MIEJSCE;
   info.WYM:={? H.RWYM=1 || 'pełen etat' || '%1/%2 etatu' [$H.RWYL,$H.RWYM] ?};
   _um:=1+-H.CP().S='u';
   _wal:=H.WAL().KOD;
   _waluta:={? H.CZYWAL='T' || _wal+'' || 'zł' ?};
   _waluta2:={? H.CZYWAL2='T' || _wal+'' || 'zł' ?};
   _waluta3:={? H.CZYWAL3='T' || _wal+'' || 'zł' ?};
   _waluta4:={? H.CZYWAL4='T' || _wal+'' || 'zł' ?};
   _prec:={? H.S1=int(H.S1) & H.S2=int(H.S2) & H.S3=int(H.S3) & H.S4=int(H.S4) & H.PAW=int(H.PAW) || 0 || 2 ?};
   'nuco - dodana tresc brutto do sw1';
   info.SW1:={? H.S1
             || {? _um || 'płaca zasadnicza' || 'stawka godzinowa' ?}+' brutto '+form(H.S1,,_prec)+' '+_waluta
             || ''
             ?};
   info.SW2:={? H.S2
             || {? _um || 'dodatek funkcyjny' || 'dodatek brygadzistowski' ?}+' '+
                {? H.S2T='P'
                || 'wyrażony w '+_waluta2+', '+form(H.S2,,_prec)+'% '+H.S2P().NAZWA
                || form(H.S2,,_prec)+' '+_waluta2
                ?}
             || ''
             ?};
   info.SW3:={? H.S3
             || {? H.S3T='P'
                || 'dodatek wyrażony w %1, %2\% %3' [_waluta3,form(H.S3,,_prec),H.S3P().NAZWA]
                || 'dodatek %1 %2' [form(H.S3,,_prec),_waluta3]
                ?}
             || ''
             ?};
   info.SW4:='';
   'NUCO';
   {? (cli_ver()='jterm' | exec('cli_ver','#system')='interm') & exec('rep_set','rap_zew','PXX_ZASWUMPR',1)<>'T'
   || info.SW4:=exec('zatrudnienie_procprem','qkali',H)
   ?};

   info.SW5:={? H.S4
             || {? _um || 'płaca oddelegowanie' || 'stawka godzinowa płacy związanej z oddelegowaniem' ?}+
                ' '+form(H.S4,,_prec)+' '+_waluta4
             || ''
             ?};
   H.memo_get(,'INNE');
   STR.split(exec('memo2print','#string',H,'INNE'));
   info.IW:=STR.line(240);
   info.GOD:={? H.CZY_NADG='T'
             || _dop:='';
                {? H.DOP_NADG
                || _dop+=$H.DOP_NADG+' godz. dobowo';
                   {? H.DOP_ST || _dop+=', ' ?}
                ?};
                {? H.DOP_ST<>0
                || _dop+=$H.DOP_ST+' godz. średniotygodniowo'
                ?};
                _dop
             || ''
             ?};
   info.DRP:=H_UM.P_OD;
   info.DRU:=H.PRZYG;
   info.PAT:=H.PAT;
   info.PAW:=form(H.PAW,,_prec)+{? info.PAT='K' || ' zł' |? info.PAT='P' || '%' || '' ?};
   info.KW_SL1:={? H.S1<>0 & H.CZYWAL='N' || STR.kwota_sł(H.S1) || '' ?};
   info.KW_SL2:={? H.S2<>0 || {? H.S2T='P' | H.CZYWAL2='T' || '' || STR.kwota_sł(H.S2) ?} || '' ?};
   info.KW_SL3:={? H.S3<>0 || {? H.S3T='P' | H.CZYWAL3='T' || '' || STR.kwota_sł(H.S3) ?} || '' ?};
   info.KW_SL4:={? H.S4<>0 & H.CZYWAL4='N' || STR.kwota_sł(H.S4) || '' ?};
   {? __nru01 & var_pres('ZAM_OKR',info)>0
   || info.ZAM_OKR:=H_UM.ZAM_OKR
   ?};
~~}{
   info.PPSF_OD:=info.PPSF_DO:=#0;
   info.PPSF_A:=info.PPSF_R:=info.PPSF_I:='';
   {? __ppsf & +|par.UM.PPSF_H & PPSF_H.seek(par.UM.PPSF_H,,1)
   || info.PPSF_OD:=PPSF_H.OD;
      info.PPSF_DO:=PPSF_H.DO;
      _co:=obj_new(3); _na:=obj_new(obj_len(_co));
      _co[1]:='\n'; _na[1]:=' ';
      _co[2]:='\t'; _na[2]:=' ';
      _co[3]:='\"'; _na[3]:='\'';
      {? PPSF_H.PPSFROZD
      || PPSF_H.PPSFROZD();
         info.PPSF_R:=gsub(PPSFROZD.memo_txt(,1,'TEKST'),_co,_na)
      ?};
      info.PPSF_I:=gsub(PPSF_H.memo_txt(,1,'OPIS'),_co,_na);
      PPSF_ADR.index('PRAC_AK');
      _wh:='
         PPSF_ADR.P=:_a and
         PPSF_ADR.A=\'T\' and
         (PPSF_ADR.DO is null or PPSF_ADR.DO>to_date(:_b)) and
         (PPSF_ADR.OD<to_date(:_c) or to_date(:_c) is null)
      ';
      {? ~PPSF_ADR.f_set(,,_wh,P.ref,H_UM.P_OD,H_UM.P_DO)
      || PPSF_ADR.f_set(,,'0=1')
      ?};
      {? PPSF_ADR.f_first()
      || {!
         |? info.PPSF_A+=exec('opis_warp','ppsf','adres');
            info.PPSF_A+='; ';
            PPSF_ADR.f_next()
         !};
         info.PPSF_A-=2
      ?}
   ?};
   indent:=1;
   cnt+=1;
   H.cntx_pop();
   {? cli_ver()='jterm' & exec('rep_set','rap_zew','zaswumpr')='T'
   || {? par.ZAL='' || par.ZAL:='0' ?}
   ?};
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'H_UM_REF',$H_UM.ref(),
      'F_NAZWA',KST.NAZWA,
      'MIEJSCE',PAR_WYDR.MIEJSC,
      'DATA',info.DRU$4,
      'F_ULICA',exec('ulica','stalesys'),
      'F_POCZTA',exec('poczta','stalesys'),
      'F_REGON',KST.REG,
      'F_NIP',KST.NIP,
      'F_PKD',KST.PKD,
      'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?},
      'DATA_ZAW',{? info.DZU<>#0 || info.DZU$6+' r.' || '' ?},
      'F_OSREP',info.ORZ);
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'OSOBA',info.OZA,
      'PESEL',info.PESEL,
      'NACZAS',info.UNA,
      'STANOW',info.STN,
      'WYDZIAL',info.WYD,
      'WYMIAR',info.WYM,
      'INNEWAR',form(info.IW),
      'GODZNADL',info.GOD,
      'ROZPRAC',{? info.DRP<>#0 || info.DRP$6+' r.' || '' ?},
      'CZY_ZAP',par.ZAL,
      'NR_UM',form(H_UM.NUMER));
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'SW1',info.SW1,
      'SW2',info.SW2,
      'SW3',info.SW3,
      'SW4',info.SW4,
      'SW5',info.SW5,
      'PAT',info.PAT,
      'PAW',info.PAW,
      'KW_SL1',info.KW_SL1,
      'KW_SL2',info.KW_SL2,
      'KW_SL3',info.KW_SL3,
      'KW_SL4',info.KW_SL4);
      _txt:={? $__okr_new<>'0' | (__nru01 & (H_UM.PRZ_USPR='T' | form(H_UM.ZAM_OKR)<>''))
               | form(info.IW)<>'' | info.GOD<>''
            || 'T'
            || 'N'
            ?};
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'OKR_NEW',$__okr_new,
      'UZAS1',__uzas1,
      'UZAS2',__uzas2,
      'INNEW3',_txt
      );
   exec('ins_par','rap_zew',__xr_ins,cnt,'PRACA_ZD',$__xr_ins);
   exec('ins_par','rap_zew',__xr_ins,cnt,'PRACA_ZW',$cnt);
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'PRACA_DT', {? __ppsf || {? info.PPSF_OD<>#0 || info.PPSF_OD$6 || '' ?} || '' ?},
      'PRACA_DO', {? __ppsf || {? info.PPSF_DO<>#0 || info.PPSF_DO$6 || '' ?} || '' ?},
      'PR_ZD_2A', {? __ppsf || 'adres wykonywania pracy zdalnej: %1'[form(info.PPSF_A)] || '' ?},
      'PR_ZD_3R', {? __ppsf || 'tryb pracy zdalnej: %1'[info.PPSF_R] || '' ?},
      'PR_ZD_4I', {? __ppsf || 'inne postanowienia dotyczące pracy zdalnej: %1'[info.PPSF_I] || '' ?}
   );
   {? __nru01
   || exec('ins_par','rap_zew',__xr_ins,cnt,
         'ZAM_OKR', form(H_UM.ZAM_OKR),
         'PRZ_USPR', H_UM.PRZ_USPR
      )
   ?};
   ~~
}
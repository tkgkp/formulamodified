:!UTF-8
{COMMENT}   MACRO - LINIA0                                              {COMMENT-}
{COMMENT}
Makro używane do drukowania rekordu z linią poprzedzającą
dla wydruków z obiektem PRINTER o nazwie przekazywanej przez zmienną prn1.
Bez ENTIRE
{COMMENT-}
{DEFINEFONT: 'TM=Inter,75,Q,,0,0'}
{DEFINEFONT: 'TMB=Inter,75,R,,0,0'}
{DEFINEFONT: 'TF=Inter,75,F,,0,0'}
{DEFINEFONT: 'IE=Inter,150,E,,0,0'}
{DEFINEFONT: 'IF=Inter,150,F,,0,0'}
{MACRO: 'MY_LINIA0'}
{  ll:=lineleft(); ~~}
{IF:  _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; _v:=($(_f))();
      ll<(rsep+_v) & ll<>0}{PAGE}{FI}
{IF: vp}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn1+'.fbar()'; ($(_f))()}
   { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: _f:=prn1+'.fbar()'; ($(_f))()}
   {ELIF: rsep}
      {REP: _f:=prn1+'.fbar()'; ($(_f))()}
   {FI}
{FI}
{WHILE: _f:=prn1+'.next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.fline()'; ($(_f))()}
{OD}
{IF: var_pres('fun')>=0 & fun<>''}
{  $fun(); ~~}
{FI}
{MACRO-}
{COMMENT}   MACRO - druk                                             {COMMENT-}
{MACRO: 'druk'}
{IF: wyjscie=1}{EXIT}{FI}
{WHILE: ilkop<=PARAM_W.KOPIA}
{DO}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}
{IF: exec('czy_grp_dr','faktury_wydruk') | PARAM_W.KOPIA }
   {strona:=1;~~}
   {FOOTER}
   {<1}{charleft_foot:=charleft();~~}{>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {<1}{IF: exec('get_footer','img_blob')}{IMAGE: exec('get_footer','img_blob');;1}{FI}{>{charleft_foot-3}}{$strona}{' '}{COLOR: color_oo}{'  '}{COLOR: color_bw}{strona+=1;~~}
   {>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {FOOTER-}
{ELSE}
   {FOOTER}
   {<1}{charleft_foot:=charleft();~~}{>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {<1}{IF: exec('get_footer','img_blob')}{IMAGE: exec('get_footer','img_blob');;1}{FI}{MAXPAGE: >{charleft_foot-3};$page+' / '; 3L;' '}{' '}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {FOOTER-}
{FI}
   {
   PAR_WYDR.TITLE:=exec('trans_typysp','tlumaczenia',TYPYSP.ref(),PARAM_W.JEZYK);
   PAR_WYDR.SYM:=FAKS.SYM;
   TYPYSP.cntx_psh();
   PAR_WYDR.TYPE:=FAKS.T().T;
   TYPYSP.cntx_pop();
   rsep:=0
   ;~~}
{COMMENT}   REPORT - HEADER                                          {COMMENT-}
   { head:=PAR_WYDR.HEAD; PAR_WYDR.HEAD:=0;~~}
   { __STRTAB:='FAKS'; __STRODB:=''; __STRLIN:='KOR'; __STRDAT:=FAKS.DW;~~}
   {
    {? -PARAM_W.DR_DUPL='t'
    ||
       dup_head:=exec('tr','tlumaczenia','1000105');
       dup_val:=gsub(form(PARAM_W.DUPLIKAT,,3),'/','-');
       dup_sym:=1
    ?};
    ~~}
   {SUBSTITUTE: 'HEADF'}
{COMMENT}   oryginal / kopia, tryb probny                            {COMMENT-}
   {FONT: exec('font','param_wydr','ory_kop')}{SPACE: exec('space','param_wydr')}
   {IF: (';NZA'*FAKS.STAT_REJ)>1}{<1}{>{charleft}exec('tr','tlumaczenia','100021'+{? FAKS.STAT_REJ='A' || '5' || '0' ?})}{FI}
   {<1}{>{charleft}{? ~skladanka
      || {? ilkop=0 || exec('tr','tlumaczenia','1000103') || exec('tr','tlumaczenia','1000104') ?}
      || '' ?}}
{COMMENT}   HEDER obowiazujacy przed wydrukiem pozycji               {COMMENT-}
   {HEADER}
   {FONT: exec('font','param_wydr','head')}{SPACE: exec('space','param_wydr')}
   {SUBSTITUTE: 'HEADF'}
   {HEADER-}
{ ilkop+=1;~~}
{COMMENT}   DANE NAGLOWKOWE                                          {COMMENT-}
{COMMENT}   strony transakcji                                        {COMMENT-}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{COMMENT}   TRESC FAKTURY                                            {COMMENT-}
   {FONT: exec('font','param_wydr','poz')}
   {LINE: 2}
   {lmt:=2;~~}
   {<{lmt}
      }{FONT: exec('font','param_wydr','kz_oddo_tr')}{exec('tr','tlumaczenia','2000210')+' '+exec('tr','tlumaczenia','2000211')
      }{FONT: exec('font','param_wydr','kz_oddo')}{' '}{$FAKS.KZ_OD
      }{FONT: exec('font','param_wydr','kz_oddo_tr')}{' '}{exec('tr','tlumaczenia','2000212')
      }{FONT: exec('font','param_wydr','kz_oddo')}{' '}{$FAKS.KZ_DO
      }{FONT: exec('font','param_wydr','kz_oddo_tr')}{':'}
{COMMENT}   LISTA KORYGOWANYCH DOKUMENTOW                            {COMMENT-}
   {FONT: exec('font','param_wydr','poz')}
   {FOR: 'FAKS_KZF'}
   {INDEX: 'POZ'}
   {PREFIX: FAKS.ref}
   {DO}
      {IF: ref_name(FAKS_KZF.FAKS)=FAKS.name()}
         {<{lmt}exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000101'))+' '
            +exec('tr','tlumaczenia','1000102')+' '+FAKS_KZF.FAK_SYM+' '
            +exec('tr','tlumaczenia','1000127')+' '+$FAKS_KZF.FAK_DW}
      {FI}
   {OD}
{COMMENT}   LISTA INDEKSOW                                           {COMMENT-}
   {IF: FAKS.KZ_ADOST='N' & __Ind.first()}
      {FONT: exec('font','param_wydr','poz')}
      {<{lmt}}{ prn1:='PRN1'; print1();~~}
      {HEADER}
      {SUBSTITUTE: 'HEADF'}
      {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
      {HEADER-}
      {IF: lineleft() & lineleft()<7}
         {PAGE}
      {ELSE}
         {LINE:2}
         {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
         {SUBSTITUTE: 'TABHEAD'}
      {FI}
      { vp:=1;~~}
      {FONT: exec('font','param_wydr','poz')}
      {FOR: __Ind}
      {DO}
         {
         il_lin:=0;
         ww1[1]:=__Ind.N;
         "szacowanie ilosci lini pozycji";
         _f:=prn1+'.ini_line()'; _v:=($(_f))();
         _f:=prn1+'.length()'; il_lin:=($(_f))()+1;
         ~~}
         {IF: lineleft() & lineleft()<il_lin}{SUBSTITUTE: 'LINIAF'}{IF: lineleft()}{PAGE}{FI}{FI}
         {SUBSTITUTE: 'LINIA0'}
      {TOTAL}
         { VAR_DEL.delete('il_lin');~~}
      {OD}
      {HEADER}
      {SUBSTITUTE: 'HEADF'}
      {HEADER-}
      {SUBSTITUTE: 'LINIAF'}
   {FI}
{COMMENT}   RABAT, KWOTA ZMNIEJSZENIA LUB ZWIĘKSZENIA PODATKU        {COMMENT-}
   {ENTIRE}
   {
   wspw:={? FAKS.BRTW<0 || -1 || 1 ?};
   wsp:={? FAKS.BRUTTO<0 || -1 || 1 ?};
   ~~}
   {LINE:2}
   {IF: FAKS.RAB<>0}
   {<{lmt}
      }{FONT: exec('font','param_wydr','kz_rab_tr')}{exec('tr','tlumaczenia','1000187')+':'
      }{FONT: exec('font','param_wydr','kz_rab')}{' '}{form(FAKS.RAB,,2,',.')
      }{FONT: exec('font','param_wydr','kz_rab_proc')}{'%'}
   {FI}
   {IF: FAKS.TZP<>''}{<{lmt}exec('tr','tlumaczenia','3000111')+': '+FAKS.TZP}{FI}
   {LINE}
   {IF: wspw<0 & walutowa & (PARAM_W.WALH='H' | PARAM_W.WALH='T')}
      {<{lmt}
         }{FONT: exec('font','param_wydr','kz_kwrab_tr')}{exec('tr','tlumaczenia','1000199')+':'
         }{FONT: exec('font','param_wydr','kz_kwrab')}{' '}{form(wspw*FAKS.BRTW,,2,',.')
         }{FONT: exec('font','param_wydr','kz_kwrab_wal')}{' '}{FAKS.WAL().KOD}
   {FI}
   {IF: walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='T'}
      {IF: wsp<0}
         {<{lmt}
            }{FONT: exec('font','param_wydr','kz_kwrab_tr')}{exec('tr','tlumaczenia','1000199')+':'
            }{FONT: exec('font','param_wydr','kz_kwrab')}{' '}{form(wsp*FAKS.BRUTTO,,2,',.')
            }{FONT: exec('font','param_wydr','kz_kwrab_wal')}{' '}{FAKS.NWAL().KOD}
      {FI}
      {<{lmt}
         }{FONT: exec('font','param_wydr','kz_kwzp_tr')
          }{{? wsp<0 || exec('tr','tlumaczenia','2000213') || exec('tr','tlumaczenia','2000214') ?}+':'
         }{FONT: exec('font','param_wydr','kz_kwzp')}{' '}{form(wsp*FAKS.VAT,,2,',.')
         }{FONT: exec('font','param_wydr','kz_kwzp_wal')}{' '}{FAKS.NWAL().KOD}
   {FI}
   {LINE}
{COMMENT}   TABELA VAT                                               {COMMENT-}
{COMMENT}   mod: sitek - wylaczenie tabeli VAT dla walutowych        {COMMENT-}
   {IF: walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='T'}
   {<{lmt}}{FONT: exec('font','param_wydr','poz')}{exec('tr','tlumaczenia','1000132')}
   { prn1:='PRN'; ~~}
   {FONT: exec('font','param_wydr','tabhead_vat')}{SPACE: exec('space','param_wydr')}
   {lmt:=0;~~}
   {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
   {lmt:=2;~~}
   {FONT: exec('font','param_wydr','vat')}
   {FOR: FAKSV}
   {INDEX: 'FF_SV'}
   {PREFIX: '',$FAKS.ref}
   {DO}
      {FIRST}
      {FIRST-}
      {
      ww[1]:=form(wsp*FAKSV.WN,,2,',.');
      ww[2]:=form(FAKSV.SV().KOD,,2,',.');
      ww[3]:=form(wsp*FAKSV.WV,,2,',.');
      ww[4]:=form(wsp*FAKSV.WB,,2,',.');
      {? FAKSV.SV().KOD=' -o' | FKOR.SV().KOD=' -o' || __oo:=1 ?};
      ~~
      }
      {SUBSTITUTE: 'MY_LINIA0'}
   {OD}
   {
   VAR_DEL.delete('wspw','wsp');
   ~~}
   {lmt:=0;~~}
   {SUBSTITUTE: 'LINIAF'}
   {lmt:=2;~~}
   {FI}
   {ENTIRE-}
{COMMENT}   opis stawki odwrotne obciążenie                          {COMMENT-}
   {  SLU.cntx_psh(); SLO.cntx_psh(); ~~}
   {IF:
      __oo
      &  (
            SLU.index('NAZ');
            SLO.index('SL');
            SLU.prefix();
            SLU.find_key('~STAWKI VAT '+KSTE.KRAJ().KOD);
            SLO.prefix(SLU.ref,' -o');
            SLO.first()
         )
   }
      {<1}{<{lmt}}{SLO.KOD+' - '+exec('tr','tlumaczenia','1000168')}
   {FI}
   {  SLU.cntx_pop(); SLO.cntx_pop(); ~~}
{COMMENT}   do zaplaty                                               {COMMENT-}
   {LINE}
   {
   zap:=
      {? __walh=__walp | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
      ||
         FAKS.BRUTTO
      ||
         FAKS.BRTW
      ?};
   slownie:=exec('slownie','tlumaczenia',fabs(zap),{? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || __walh || __walp ?});
   {? slownie=''
   || kw_fak:={? zap<0 || -zap || zap ?};
      slownie:=
         {? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
            | __walp<>INFO.NAROD().KOD
         ||
            STR.słownie(kw_fak)+{? frac(kw_fak) ||' '+$(#(form(kw_fak,,2,',.')+2))+'/100'||''?}+' '+
               {? PARAM_W.WALH='T' | PARAM_W.WALH='H' || __walh || __walp ?}
         ||
            STR.kwota_sł(fabs(kw_fak))
         ?}
   ?}
   ;~~}
   {IF: zap<0}
      {wp_tr:=exec('tr','tlumaczenia','2000110');
       wp_war:=form(-zap,,2,'.,');
       wp_wal:=' '+{? PARAM_W.WALH='T' | PARAM_W.WALH='H' || __walh || __walp ?};
       ~~}
   {ELSE}
      {wp_tr:=exec('tr','tlumaczenia','1000139');
       wp_war:=form(zap,,2,'.,');
       wp_wal:=' '+{? PARAM_W.WALH='T' | PARAM_W.WALH='H' || __walh || __walp ?};
       ~~}
   {FI}
   {  fp_war:=fp_tr:=tp_war:=tp_tr:=bank_tr:=bank_war:=rach_tr:=rach_war:=swift_tr:=swift_war:='';STR.split('');
      line_sign:='';
      grey_line:=0;
      {? PARAM_W.SLOWNIE='T' || slownie:=(~(1+slownie))+(1-slownie) || slownie:='' ?} ;
   ~~}
{COMMENT}   rysowanie paska z danymi {COMMENT-}
   {SUBSTITUTE: 'panel_platnosci'}
{COMMENT}   uwagi                                                    {COMMENT-}
   {FONT: exec('font','param_wydr','uwagi')}
   { __memo:=FAKS.memo_get('r','UWAGI'); text:='';~~}
   {IF: __memo.is_open}
      {WHILE: text:=fread(__memo); text<>'\n'}
      {DO}
         {FIRST}{LINE}{FIRST-}
         {<{lmt+1}}{
         max_T8:=charleft-(lmt+8);
         STR.split(text);
         Text:='';
         sep:=0
         ;~~}
         {WHILE: STR.next()}
         {DO}
            { word:=STR.get_word(); sep+=1;~~}
            {IF: +Text+(+word)>=(max_T8-4-8)}
               {<{lmt+1}}{JUSTIFY}{<{lmt+1}>{max_T8-8}Text}
               { Text:=word; sep:=1;~~}
            {ELSE}
               { Text+={? sep>1 || ' ' || '' ?}+word;~~}
            {FI}
         {OD}
         {IF: +Text}
            {<{lmt+1}}{Text}
         {FI}
      {OD}
      {CENTER}
   {FI}
   { VAR_DEL.delete('__memo','text','Text');~~}
{COMMENT}   podpisujacy                                              {COMMENT-}
   {FONT: exec('font','param_wydr','podpisy')}
   {<1}{
   max_T8:=charleft;
   _p100230:=exec('get','#params',100230,2);
   {? var_pres('STR1')>=100 || obj_del(STR1) ?};
   STR1:=obj_new(@.CLASS.STRING);
   Text:={? _p100230='O' | _p100230='N' || exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000145')) || '' ?};
   Text1:={? _p100230='O' | _p100230='S' || exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000146')) || '' ?};
   STR.split(Text);
   STR1.split(Text1);
   ~~}
   {WHILE: _a:=+(Text:=STR.line(((max_T8-4)/2)$0-30)); _b:=+(Text1:=STR1.line(((max_T8-4)/2)$0-30)); _a | _b}
   {DO}
      {FIRST}
         {IF: lineleft()<6}{PAGE}{FI}{LINE: 1}
      {FIRST-}
      {IF: +Text | +Text1}
         {IF: +Text}{<{lmt+1}Text}{FI}{IF: +Text1}{<{((max_T8-4)/2)$0+20}Text1}{FI}
      {FI}
   {OD}
{COMMENT}   upowaznienie                                             {COMMENT-}
   {FONT: exec('font','param_wydr','upowaznienie')}
   {_p100230:=exec('get','#params',100230,2);
   _os:=form(exec('kh_osob_default','kontrahent',KH.ref));
   USERS.cntx_psh;
   USERS.index('USR_KKOD');
   USERS.prefix;
   Text:={? _p100230='O' | _p100230='N' || {? +_os || _os || 20*'.' ?} || '' ?};
   Text1:={? _p100230='O' | _p100230='S' || {? USERS.find_key(FAKS.USER,FAKS.USER) & +USERS.DANE || USERS.DANE || 20*'.' ?} || '' ?};
   USERS.cntx_pop;
   STR.split(Text);
   STR1.split(Text1)
   ;~~}
   {WHILE: _a:=+(Text:=STR.line(((max_T8-4)/2)$0-30)); _b:=+(Text1:=STR1.line(((max_T8-4)/2)$0-30)); _a | _b}
   {DO}
      {FIRST}
         {LINE: 4}
      {FIRST-}
      {IF: +Text | +Text1}
         {IF: +Text}{<{lmt+1}Text}{FI}{IF: +Text1}{<{((max_T8-4)/2)$0+20}Text1}{FI}
      {FI}
   {OD}
   {ENTIRE-}
{COMMENT}   KONIEC FAKTURY                                           {COMMENT-}
   {HEADER}{HEADER-}
   { duplex:=page%*2=1;~~}
   {IF: lineleft()>0}{PAGE}{FI}
   {IF: BEER.SZ='S' & exec('get','#params',2124,2,OPERATOR.USER)='T' & duplex=1}
      {duplex:=2;~~}
      {PAGE}
      {duplex:=0;~~}
   {FI}
{OD}
{MACRO-}
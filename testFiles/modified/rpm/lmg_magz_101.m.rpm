:!UTF-8
{TITLE:A. Aktywny stan magazynowy na podany dzień}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_101:="
      VAR_DEL.delete('color','prn1','prn2','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');

      VAR_DEL.delete('k','w','s','sg','fm','nadzien','iw','gr','dokl','kgr','kgl','dok2','j2');
      VAR_DEL.delete('PRN','PRNF');

      VAR_DEL.delete('wyjscie','X_Xa','X_Xd')";
   mag_101();

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   k :=obj_new(7); "--szerokosci kolumn--";
   w :=obj_new(7); "--wartosci kolumn--";
   s :=0; "--razem--";
   sg:=0; "--razem grupa--";
   dokl:=0; "--dokladnosc ilosci--";
   dok2:=0;

   k[1]:=20;
   k[2]:=60;
   k[3]:=5;
   k[4]:=16;
   k[5]:=16;
   w[1]:=w[2]:=w[3]:=w[6]:='';
   w[4]:=w[5]:=w[7]:=0;

   tryb:=rep_export();
   kgr:=kgl:='';

   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();

   fm:='N';
   nadzien:=date(ST.AR,ST.AM,0);
   iw:='N';
   gr:='T';
   j2:='N';

   _wydr_naz:='mag_101';
   _wydr_edi:='MAG_101';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ?};
   {? WYDRUKIL.RADIOB_1=2 & exec('upr_cm','ceny')=0 || WYDRUKIL.RADIOB_1:=1 ?};
   {? WYDRUKIL.RADIOB_1=2 & ST.MAG().IL='T' || WYDRUKIL.RADIOB_1:=1 ?};

   WYDRUKIL.win_edit(_wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("
         _noupr:=exec('upr_cm','ceny')=0;
         {? ~_noupr & ST.MAG().IL='T' & WYDRUKIL.RADIOB_1=2
         || FUN.info('Magazyn tylko ilościowy zmieniono parametr Rodzaj na wydruk ilościowy.'@);
            WYDRUKIL.RADIOB_1:=1
         ?};
         {? WYDRUKIL.OD_DATY=date(0,0,0)
         || FUN.info('Należy wypełnić pole: Stan na dzień.'@); 'OD_DATY'
         |? WYDRUKIL.RADIOB_1=2 & _noupr
         || FUN.info('Brak uprawnień do wartości magazynowych.'@); 'RADIOB_1'
         |? WYDRUKIL.RADIOB_1=2 & WYDRUKIL.RADIOB_2=2
         || FUN.info('Grupowanie wg lokalizacji dostępne jest w wariancie ilościowym.'@); 'RADIOB_1'
         || ''
         ?}")
   || WYDRUKIL.put();
      wyjscie:=0;
      fm:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
      nadzien:=WYDRUKIL.OD_DATY;
      iw:={? WYDRUKIL.RADIOB_1=1 || 'N' || 'T' ?};
      gr:={? WYDRUKIL.RADIOB_2=0 || 'N' |? WYDRUKIL.RADIOB_2=1 || 'T' || 'L' ?};
      j2:={? WYDRUKIL.CHKBOX02=1 || 'T' || 'N' ?};
      wyjscie:=exec('filtr_m','material',fm,{? gr='T' || 'T' || 'N' ?},'T',1)
   ?}
}
{END:
   mag_101(); VAR_DEL.delete(mag_101)
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1
|| {? gr='L' || PRN.add("form(kgl)",8,'Lokalizacja'@) ?};
   PRN.add("form(kgr)",8,'Grupa materiałowa'@)
?};
{? j2='T' || k[2]:=40 ?};
PRN.add("form(w[1])",k[1],'Indeks'@);
PRN.add("form(w[2])",k[2],'Nazwa materiału'@);
PRN.add("form(w[3])",k[3],'jm'@);
PRN.add("form(w[4],,dokl)",k[4],'Stan ilościowy'@,1);
{? j2='T'
|| PRN.add("form(w[6])",k[3],'jm'@);
   PRN.add("{? w[6]='' || '' || form(w[7],,dok2) ?}",k[4],'Stan ilościowy 2'@,1)
?};
{? iw='T' || PRN.add("form(w[5],,2)",k[5],'Stan wartościowy'@,1) ?};
"--obliczenia--";
{? gr='N' | gr='L' | iw='N'
|| X_Xa.index(X_Xa.ndx_tmp(,,'KTM',,))
|| X_Xa.index(X_Xa.ndx_tmp(,,'MGRKOD',,,'KTM',,))
?};
{? X_Xa.first()
||
   X_Xd:=tab_tmp(2
      ,'GR' ,'STRING[20]'  ,'Grupowanie'
      ,'KTM','STRING[50]'  ,'Indeks'
      ,'N'  ,'STRING[100]' ,'Nazwa'
      ,'JM' ,'STRING[10]'  ,'jm'
      ,'SI' ,'REAL'        ,'Stan ilościowy'
      ,'SW' ,'REAL'        ,'Stan wartościowy'
      ,'MGR','STRING[8]'   ,'Grupa materiałowa'
      ,'J2' ,'STRING[10]'  ,'jm'
      ,'S2' ,'REAL'        ,'Stan ilościowy');
   _il:=$X_Xa.size();
   _ii:=0;
   M.cntx_psh();
   M.prefix();
   {? gr='L'
   || "-- dla lokalizacji --";
      OKR.cntx_psh(); ND.cntx_psh(); DK.cntx_psh(); DK_L.cntx_psh();
      OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {!
      |? M.seek(BB.sqlint(X_Xa.REF),);
         _cont:=1;
         _loop:=OKR.first();
         {!
         |? _loop
         |!
            _ii+=1; echo('Trwają obliczenia dla materiału: %1 %2'@[M.KTM,M.N],'%1 z %2'@[$_ii,_il]);
            _j2:=M.J2<>null();
            DK_L.use('doklk'+ST.ODDZ+($OKR.ROK+2));
            DK_L.index('MMG'); DK_L.prefix(M.ref(),ST.MAG);
            {? DK_L.first()
            || {!
               |?
                  _dkld:=
                     {? DK_L.DK<>null
                     || DK.use(ref_name(DK_L.DK));
                        ND.use(ref_name(DK_L.DK().N));
                        DK.N().D
                     || DK_L.DT
                     ?};
                  {? DK_L.Z='T' & _dkld<=nadzien
                  || _eanl:=DK_L.LOK().KOD;
                     {? X_Xd.find_key(_eanl,M.KTM)
                     || X_Xd.SI+=DK_L.IL;
                        X_Xd.S2+={? _j2 || DK_L.IL2 || 0 ?};
                        X_Xd.put()
                     || X_Xd.blank(1);
                        X_Xd.KTM:=M.KTM;
                        X_Xd.N:=M.N;
                        X_Xd.JM:=M.J().KOD;
                        X_Xd.J2:={? _j2 || M.J2().KOD || '' ?};
                        X_Xd.SI:=DK_L.IL;
                        X_Xd.S2:={? _j2 || DK_L.IL2 || 0 ?};
                        X_Xd.GR:=_eanl;
                        X_Xd.MGR:=M.MGR().KOD;
                        X_Xd.add()
                     ?}
                  |? _dkld>nadzien
                  || _cont:=0
                  ?};
                  _cont & DK_L.next()
               !}
            ?};
            _loop:=_cont & OKR.next()
         !};
         X_Xa.next()
      !};
      OKR.cntx_pop(); ND.cntx_pop(); DK.cntx_pop(); DK_L.cntx_pop()
   || "-- bez lokalizacji --";
      {!
      |? M.seek(BB.sqlint(X_Xa.REF),);
         _ii+=1; echo('Trwają obliczenia dla materiału: %1 %2'@[M.KTM,M.N],'%1 z %2'@[$_ii,_il]);
         _j2:=M.J2<>null();
         X_Xd.blank(1);
         X_Xd.KTM:=M.KTM;
         X_Xd.N:=M.N;
         X_Xd.JM:=M.J().KOD;
         X_Xd.J2:={? _j2 || M.J2().KOD || '' ?};
         exec('stan_zest','magazyn_stan',ST.MAG,M.ref(),nadzien,1,"X_Xd.SI","X_Xd.SW");
         X_Xd.S2:=HELP.SIL2;
         {? gr='T' || X_Xd.GR:=M.MGR().KOD ?};
         X_Xd.MGR:=M.MGR().KOD;
         X_Xd.add();
         X_Xa.next()
      !}
   ?};
   M.cntx_pop();
   "-- usuniecie zapisow z zerowymi stanami --";
   {? X_Xd.first() || {! |? {? X_Xd.SI=0 & X_Xd.SW=0 & X_Xd.S2=0 || X_Xd.del() || X_Xd.next() ?} !} ?};
   echo()
?};

PAR_WYDR.TITLE:=
   {? iw='T'
   || {? gr='L' || 'ZESTAWIENIE STANÓW ILOŚCIOWO-WARTOŚCIOWYCH WG LOKALIZACJI'@ || 'ZESTAWIENIE STANÓW ILOŚCIOWO-WARTOŚCIOWYCH'@ ?}
   || {? gr='L' || 'ZESTAWIENIE STANÓW ILOŚCIOWYCH WG LOKALIZACJI'@ || 'ZESTAWIENIE STANÓW ILOŚCIOWYCH'@ ?}
   ?};
prn1:='PRN'; prn2:='PRNF';
~~}
{IF: X_Xa.first()=0 | X_Xd.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: ~tryb}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Na dzień: %1'@[(nadzien$6)]}
{IF:fm='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
{IF:j2='T'}{<{lmt+1}}{'Druga jednostka ewidencyjna'@}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{ prn1:='PRN';~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Na dzień: %1'@[(nadzien$6)]}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE}
{FOOTER-}
{COMMENT}   Pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{
rsep:=PAR_WYDR.RSEP;
w[1]:=0;
M.index('RODZ'); M.prefix('T');
~~}
{IF: gr='N'}
{COMMENT}   wydruk bez grup towarowych                               {COMMENT-}
   {FOR: 'X_Xd'}
   {DO}
      {FIRST}
      { dokl:=exec('fld_prec','#field',X_Xd,'SI');
        dok2:=exec('fld_prec','#field',X_Xd,'S2');
      ~~}
      {FIRST-}
      {
      w[1]:=X_Xd.KTM;
      w[2]:=X_Xd.N;
      w[3]:=X_Xd.JM;
      w[4]:=X_Xd.SI;
      w[5]:=X_Xd.SW;
      w[6]:=X_Xd.J2;
      w[7]:=X_Xd.S2;
      kgr :=X_Xd.MGR;
      kgl :=X_Xd.GR;
      ~~}
      {SUBSTITUTE: 'LINIA0'}
      { sg+=w[5]; s+=w[5];~~}
   {OD}
{ELSE}
{COMMENT}   wydruk z grupami towarowymi                              {COMMENT-}
   {FOR: 'X_Xd'}
   {DO}
      {FIRST}{ dokl:=exec('fld_prec','#field',X_Xd,'SI'); dok2:=exec('fld_prec','#field',X_Xd,'S2'); kolejny:=0;~~}{FIRST-}
      {IF: tryb<>1 & kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
      {
      w[1]:=X_Xd.KTM;
      w[2]:=X_Xd.N;
      w[3]:=X_Xd.JM;
      kgr :=X_Xd.MGR;
      kgl :=X_Xd.GR;
      w[4]:=X_Xd.SI;
      w[5]:=X_Xd.SW;
      w[6]:=X_Xd.J2;
      w[7]:=X_Xd.S2;
      ~~}
      {SUBSTITUTE: 'LINIA0'}
      {
      sg+=w[5]; s+=w[5];
      kolejny:=0; rsep:=PAR_WYDR.RSEP;
      ~~}
      {TOTAL: X_Xd.GR}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
         {IF: tryb<>1}
         {
         w[1]:=X_Xd.KTM;
         w[2]:=X_Xd.N;
         w[3]:=X_Xd.JM;
         kgr :=X_Xd.MGR;
         kgl :=X_Xd.GR;
         w[4]:=X_Xd.SI;
         w[5]:=X_Xd.SW;
         w[6]:=X_Xd.J2;
         w[7]:=X_Xd.S2;
         _f:=prn1+'.ini_line()'; _v:=($(_f))();
         _f:=prn1+'.length()'; il_lin:=($(_f))()+2+3;
         ~~}
         {IF: tryb<>1 & lineleft>0 & lineleft<il_lin}{PAGE}{FI}
         {SUBSTITUTE: 'LINIAF'}
         {
         kolejny:=1; rsep:=0;
         PRN.n_frame();
         w[1]:='';
         w[2]:={? iw='T' || 'Razem'@ || {? iw='N' & gr='L' || 'Lokalizacja:'@ || '' ?} ?}+
            {? grp_fld()='' || 'Bez lokalizacji'@ || grp_fld ?};  PRN.FORM[2]:=0;
         w[3]:=w[4]:='';
         w[6]:=w[7]:='';
         w[5]:=form(sg,,2,'.,');
         ~~}
         {FONT: 'T8GB'}{SPACE: 'C'}
         {SUBSTITUTE: 'LINIA0'}
         {FONT: 'T8G'}{SPACE: 'C'}
         {
         sg:=0;
         PRN.s_frame();
         PRN.FORM[2]:=1
         ;~~}
         {FI}
      {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
   {OD}
{FI}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1 & iw='T'}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {IF: gr='N'}{SUBSTITUTE: 'LINIAF'}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {
   PRN.n_frame();
   w[1]:='';
   w[2]:='Razem'@; PRN.FORM[2]:=0;
   w[3]:=w[4]:='';
   w[6]:=w[7]:='';
   w[5]:=form(s,,2,'.,');
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{ELIF: gr<>'N'}
   { PRN.n_frame();~~}
{FI}
:!UTF-8
{DEFINEFONT: 'TM=Inter,100,Q,,0,0'}
{DEFINEFONT: 'TMB=Inter,100,R,,0,0'}
{DEFINEFONT: 'TF=Inter,100,F,,0,0'}
{DEFINEFONT: 'IE=Inter,150,E,,0,0'}
{DEFINEFONT: 'IF=Inter,150,F,,0,0'}
{FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
{ exec('dokm','magdok_wydruk');~~}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{nd_date_head:=exec('tr','tlumaczenia','6000304')+';';
nd_date_val:=(gsub(form(ND.D,,3),'/','-')+';');
~~}
{ __STRTAB:='ND'; __STRODB:='ND.KH_ODB'; __STRLIN:=''; __STRDAT:=ND.D;~~}
{SUBSTITUTE: 'HEADF'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{FONT: exec('font','param_wydr','header')}{SPACE: exec('space','param_wydr')}
{IF: ND.Z='N'}{>{charleft()} exec('tr','tlumaczenia','5000020')}{FI}
{LINE}
{ lmt:=0; ND.cntx_psh();~~}
{COMMENT}   strony transakcji                                        {COMMENT-}
{IF: ND.TYP().Z='T'}
   { __STRTAB:='ND'; __STRODB:='ND.KH_ODB'; __STRLIN:=''; __STRDAT:=ND.D;~~}
   {SUBSTITUTE: 'stronyf'}
   { VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{ELSE}
   {FONT: exec('font','param_wydr','fp_tr')}
   {<1}{
   charleft_blank:=charleft();
   blank_space:=charleft_blank-({? +nd_date_head> +nd_date_val || +nd_date_head || +nd_date_val ?}+6);
   ~~
   }
   {<1}{COLOR: color_ww}{blank_space*' '}{COLOR: color_gg}{<{(charleft_blank+1)}}{COLOR: color_ww}{LINE}
   {<1}{COLOR: color_ww}{blank_space*' '}{COLOR: color_gg}{>{charleft_blank-3} (nd_date_head-1)}{<{(charleft_blank+1)}}{COLOR: color_ww}
   {<1}{COLOR: color_ww}{blank_space*' '}{COLOR: color_bg}{FONT: exec('font','param_wydr','fp_war')}{>{charleft_blank-3}(nd_date_val-1)}{<{(charleft_blank+1)}}{COLOR: color_ww}
   {<1}{COLOR: color_ww}{blank_space*' '}{COLOR: color_gg}{<{(charleft_blank+1)}}{COLOR: color_bw}{LINE}
{FI}
{COMMENT}   przesunięcie magazynowe                                  {COMMENT-}
{FONT: exec('font','param_wydr','strony_po')}{SPACE: exec('space','param_wydr')}
{IF: ND.TYP().P='N' & ND.TYP().TD<>'' & ND.MD<>null}
   {<{lmt+1}exec('tr','tlumaczenia','5000021')+' '+ND.MD().SYM}{
      _n_utw:=sql('select ND.REFERENCE,ND.SYM from @ND where ND.NDSQL=\':_a\'',$ND.ref());
   {? _n_utw.first ||', ' + exec('tr','tlumaczenia','5000022')+' '+_n_utw.SYM || '' ?}}
{ELIF: ND.TYP().T='MP' & ND.NDSQL<>''}
   { ND.prefix();~~}
   {IF: ND.seek(BB.sqlint(ND.NDSQL),form(8+ND.NDSQL)) }
      {<{lmt+1}exec('tr','tlumaczenia','5000023')+' '+ND.MAG().SYM+', '+exec('tr','tlumaczenia','5000024')+' '+ND.SYM}
   {FI}
{FI}
{ prn1:='PRN'; prn2:='PRNF'; ND.cntx_pop();~~}
{IF: ND.KH_MSC<>null}
   {<{lmt+1}exec('msc_dost','kontrahent','ND')}
{FI}
{COMMENT}   opis                                                     {COMMENT-}
{IF: ND.O<>''}
   {LINE}
   {<{lmt+1}exec('tr','tlumaczenia','5000019')+' '+ND.O}
{FI}
{IF: +skad<>0}{<{lmt+1} skad}{FI}
{IF: dok_walu}{<{lmt+1} exec('tr','tlumaczenia','5000025')+' '+ND.WAL().KOD+' - '+ND.WAL().TR}{FI}
{COMMENT}   informacje dodatkowe dla naglowka                        {COMMENT-}
{
"--pobieram dane--";
exec('pobgdnag','fakso','M',ND.ref);
VAR_DEL.delete('oo','OPIK')
;~~}
{COMMENT}   projekty                               {COMMENT-}
{inf_head:='';
inf_val:='';
~~}
{IF: czy_proj='T'
& (ND.PROJEKTY<>null()
| (
DK.cntx_psh();
PROJEKTY.cntx_psh();
DK.index('DOKMAG');
DK.prefix(ND.ref());
_wyn:=0;
{? DK.first()
||
   {!|?
      {? DK.PROJEKTY<>null() || _wyn+=1 ?};
      DK.next()
   !}
?};
DK.cntx_pop();
PROJEKTY.cntx_pop();
_wyn
))}
   {  inf_head+=exec('tr','tlumaczenia','6000113')+';';
      _wyd:='';
      DK.cntx_psh();
      PROJEKTY.cntx_psh();
      DK.index('DOKMAG');
      DK.prefix(ND.ref());
      {? ND.PROJEKTY<>null() || _wyd+=ND.PROJEKTY().SYM?};
      {? DK.first()
      ||
         {!|?
            {? DK.PROJEKTY<>null()
            ||
               {? ~(_wyd*DK.PROJEKTY().SYM)
               ||
                  {? _wyd<>''
                  || _wyd+=', ' + DK.PROJEKTY().SYM
                  || _wyd+=DK.PROJEKTY().SYM
                  ?}
               ?}
            ?};
            DK.next()
         !}
      ?};
      DK.cntx_pop();
      PROJEKTY.cntx_pop();
      inf_val+=_wyd+';';
   ~~}
{FI}
{COMMENT}   FOOTER obowiazujacy przy drukowaniu pozycji dokumentu    {COMMENT-}
{SUBSTITUTE: 'footer_nd'}
{COMMENT}informacje dodatkowe: projekty {COMMENT-}
{ENTIRE}
{SUBSTITUTE: 'panel_informacji'}
{ENTIRE-}
{FOR: S_GD}
{PREFIX: 'G'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{ prn1:='PRN';~~}
{COMMENT}   Nagłówek tabeli                                          {COMMENT-}
{FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{COMMENT}   HEADER obowiazujacy przy drukowaniu pozycji dokumentu    {COMMENT-}
{HEADER}
{ prn1a:=prn1; prn2a:=prn2; prn1:='PRN'; prn2:='PRN'; ~~}
{SUBSTITUTE: 'HEADF'}
{FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{vp:=1; ~~}
{
{? sekcja='bad'
|| prn1:='PRN'; prn2:='PRNP'
|? sekcja='end'
|| prn1:=prn1a; prn2:=prn2a
|| prn1:='PRN'; prn2:='PRN'
?};
~~}
{HEADER-}
{COMMENT}   wydruk pozycji dokumentu                                 {COMMENT-}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
rsep:=PAR_WYDR.RSEP;
ww[1]:=ww[2]:=ww[3]:=ww[12]:=0;
ww[7]:=ww[8]:='';
__ref:='';
sekcja:='dk';
~~}
{FOR: X_Xw1}
{INDEX: X_Xw1.ndx_tmp(,,'KO',,)}
{PREFIX:}
{DO}
   {<1}{poz+=1;
   {? __page || poz+=1;__page:=0 ?};
   ww[1]:=form(X_Xw1.IL,,dokl_il,' ,');
   ww[12]:=form(X_Xw1.ILK,,dokl_ilk,' ,');
   ww[2]:={? czy_war=2 | czy_war=3 || {? X_Xw1.SKL='N' || form(X_Xw1.C,,dokl_c,' ,') || '' ?} || '' ?};
   ww[3]:={? czy_war=2 | czy_war=3 || {? X_Xw1.SKL='N' || form(X_Xw1.WAR,,2,' ,') || '' ?} || '' ?};
   {! _i:=5..9 |! ww[_i]:=($pp[_i])() !};
   ww[9]:='';
   ww[10]:=X_Xw1.ZL;
   ww[11]:=X_Xw1.TW;
   ww[13]:=X_Xw1.KK;
   rsep:=PAR_WYDR.RSEP;
   {? X_Xw1.SKL='N' || lp+=1; ww[4]:=form(lp,,0,'99') || ww[4]:='' ?};
   "-- pobieranie parametrów badania --";
   __param:='';
   {? czy_par & +X_Xw1.BAD
   || BADH.cntx_psh();
      BADH.use(8+X_Xw1.BAD);
      BADH.clear();
      {? BADH.seek(BB.sqlint(X_Xw1.BAD),)
      || BADP.cntx_psh();
         BADP.clear();
         BADP.index('BADHBPAR');
         BADP.prefix(BADH.ref);
         {? BADP.first()
         || {!|?
              {? BADP.BADMSEP().BADSEP().DRUK_DK='T'
              || __param+=BADP.BADMSEP().SOURCE+': '+BADP.WAR+' ~ '
              ?};
               BADP.next()
            !}
         ?};
         BADP.cntx_pop()
      ?};
      BADH.cntx_pop();
      __param:=__param-3
   ?};
   "--szacowanie ilosci lini pozycji--";
   _f:='PRN.ini_line()'; _v:=($(_f))();
   _f:='PRN.length()'; il_lin:=($(_f))()+rsep;
   __ll:=lineleft();
   {? __ll>0 & __ll-il_lin>=0
      || "--pozycja zmiesci sie na stronie--";
         wartosc+={? X_Xw1.SKL='N' || X_Xw1.WAR$2 ?}
      || "--pozycja nie zmiesci sie na stronie--";
         il_lin:=-1
   ?};
   ~~}
{COMMENT}   drukowanie pozycji                                       {COMMENT-}
   {IF: __ll>0 & il_lin<0}
   {__page:=1;~~}
   {PAGE}
      { prn1:='PRN'; prn2:='PRN'; ~~}
   {ELSE}
      { {? sekcja='bad' & __ll>0 || prn1:='PRNP'; prn2:='PRN' || prn1:='PRN'; prn2:='PRN' ?}; ~~}
   {FI}
   { sekcja:='dk'; ~~}
   {IF: prn1<>prn2}
      { __rsep:=rsep; rsep:=1;~~}
      {SUBSTITUTE: 'LINIA02'}
      { rsep:=__rsep;~~}
   {ELSE}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
   {  {? (czy_war=2 | czy_war=3) & il_lin<0
   || "--pozycja nie zmiescila sie na stronie--";
      wartosc+={? X_Xw1.SKL='N' || X_Xw1.WAR$2 ?}
      ?};
   ~~}
{COMMENT}   ilosci w drugiej jednostce ewidencyjnej                  {COMMENT-}
   {IF: j2='T'}
      {  sekcja:='il2';
         __ref:={? X_Xw1.ZP=0 || X_Xw1.REF || __ref ?};
         prn1:='PRN'; prn2:='PRN';
      ~~}
      { {! _i..obj_len(kk) |! ww[_i]:='' !};
        ww[1]:=form(X_Xw1.IL2,,dokl_il,' ,'); ww[6]:=X_Xw1.J2;
        rsep:=0; ~~}
        {IF: ww[6]<>''}{SUBSTITUTE: 'LINIA0'}{FI}
   {FI}
{COMMENT}   informacje dodatkowe dla pozycji                         {COMMENT-}
   {IF: czy_gr=0 & ~(prod='T' & MrDOKS*(ND.TYP().T+' ')) }
      {  sekcja:='fakso';
         __ref:={? X_Xw1.ZP=0 || X_Xw1.REF || __ref ?};
         prn1:='PRN'; prn2:='PRN';
      ~~}
      {FOR: FAKSO}{INDEX: 'DK'}{PREFIX: exec('FindAndGet','#table','DK',__ref)}
      {DO}
         { {! _i..obj_len(kk) |! ww[_i]:='' !};
           ww[5]:=exec('printFAKSO','fakso',PARAM_W.JEZYK);
           rsep:=0; ~~}
         {IF: ww[5]<>''}{SUBSTITUTE: 'LINIA0'}{FI}
      {OD}
   {FI}
{COMMENT}   drukowanie parametrów badań                              {COMMENT-}
   {IF: czy_par & +X_Xw1.BAD & +__param}
      {<1}{  "--szacowanie ilosci lini pozycji--";
         il_lin:=0;
         _q:=PRNP.ini_line();
         il_lin+=PRNP.length()+1;
         __ll:=lineleft();
         {? __ll>0 & __ll-il_lin>=0
         || "--pozycja zmiesci sie na stronie--";
            ''
         || "--pozycja nie zmiesci sie na stronie--";
            il_lin:=-1
         ?};
      ~~}
      {IF: __ll>0 & il_lin<0}
         { __page:=1; ~~}
         {PAGE}
      {FI}
      {  sekcja:='bad';
         prn1:='PRN'; prn2:='PRNP';
         __rsep:=rsep; rsep:=1;
      ~~}
         {SUBSTITUTE: 'LINIA02'}
         { rsep:=__rsep;~~}
   {FI}
{TOTAL}
   { VAR_DEL.delete('__rsep');~~}
{OD}
{COMMENT}   FOOTER obowiazujacy do konca wydruku                     {COMMENT-}
{SUBSTITUTE: 'clean_footer'}
{ __ll:=lineleft(); ~~}
{IF: czy_war=2 | czy_war=3}
{COMMENT}   podsumowanie pozycji                                     {COMMENT-}
   {REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
   {PRNF.framecol(color_frame_w);~~}
   { w1:=exec('tr','tlumaczenia','5000026')+' '
      +{? ND.TYP().P='N' & czy_war=3 || ND.NWAL().KOD || INFO.NAROD().KOD ?}+':'; w2:=form(wartosc,kk[3],2,' ,');~~}
   {IF: sekcja='bad' & __ll>0}
      { prn1:='PRNP'; prn2:='PRNF';~~}
   {ELSE}
      { prn1:='PRN'; prn2:='PRNF';~~}
   {FI}
   { sekcja:='end'; ~~}
   {FONT: exec('font','param_wydr','razem')}
   {PRNF.setcolor(color_g);~~}
   { rsep:=0; vp:=0;~~}
   {SUBSTITUTE: 'LINIAFSI'}
   {FONT: exec('font','param_wydr','poz')}
   {PRNF.setcolor(color_w);~~}
   { rsep:=0; vp:=0;~~}
{ELIF: lineleft()>0}
{COMMENT}   linia zamykająca tabelę pozycji                          {COMMENT-}
   { w1:=''; w2:=''; ww[5]:='';~~}
   {IF: sekcja='bad' & __ll>0}
      { prn1:='PRNP'; prn2:='PRNF';~~}
   {ELSE}
      { prn1:='PRN'; prn2:='PRNF';~~}
   {FI}
   { sekcja:='end'; ~~}
   {SUBSTITUTE: 'LINIAF'}
{FI}
{COMMENT}   HEADER obowiazujacy do konca wydruku                     {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEADF'}
{HEADER-}
{COMMENT}   informacje dodatkowe dla naglowka                        {COMMENT-}
{FONT: exec('font','param_wydr','inf_dod')}{SPACE: exec('space','param_wydr')}
{FOR: S_GD}
{PREFIX: 'D'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   podpisy                                                  {COMMENT-}
{FONT: exec('font','param_wydr','podpisy')}{SPACE: exec('space','param_wydr')}
{ENTIRE}
{LINE: 3}
{
_szer:=PRN.width();
_szero:=((_szer-3*3-4)/4)$0;
_szerl:=_szer-_szero*3-3*3-4;
kk[1]:=kk[2]:=kk[3]:=_szero;
kk[4]:=_szerl
;~~}
{
VAR_DEL.delete('PRN');
prn1:='PRN';
PRN:=obj_new(@.CLASS.PRINT,5); PRN.n_frame();
PRN.add("",kk[1],exec('tr','tlumaczenia','5000014'));"--Wystawił--";
PRN.add("",kk[2],exec('tr','tlumaczenia','5000015'));"--Zatwierdził--";
{? ND.TYP().P='T'
|| PRN.add("",kk[3],exec('tr','tlumaczenia','5000016')); "--Przyjął--"
|| PRN.add("",kk[3],exec('tr','tlumaczenia','5000017')); "--Wydał--"
?};
PRN.add("",kk[4],exec('tr','tlumaczenia','5000018'));"--Odebrał--";
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{LINE: 3}
{
_szer:=20;
PRN.HEAD[1]:={? ND.US<>null || ND.US().DANE || _szer*'.' ?};
PRN.HEAD[2]:=_szer*'.';
PRN.HEAD[3]:=_szer*'.';
PRN.HEAD[4]:=_szer*'.';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{ENTIRE-}
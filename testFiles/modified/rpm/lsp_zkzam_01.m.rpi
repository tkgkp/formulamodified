:!UTF-8
{DEFINEFONT: 'TM=Inter,75,Q,,0,0'}
{DEFINEFONT: 'TMB=Inter,75,R,,0,0'}
{DEFINEFONT: 'TF=Inter,75,F,,0,0'}
{DEFINEFONT: 'IE=Inter,150,E,,0,0'}
{DEFINEFONT: 'IF=Inter,150,F,,0,0'}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
"--szerokosc kolumn--";
   ddokl:=0;
   VAR_DEL.delete('X_Xw1');
   X_Xw1:=tab_tmp(1
      ,'P','INTEGER',''
      ,'M','STRING[160]',''
      ,'IL','REAL',''
      ,'J','STRING[10]',''
      ,'C','REAL',''
      ,'WAR','REAL',''
      ,'KWRAB','REAL',''
      ,'WAR1','REAL',''
      ,'DT','DATE',''
      ,'REF','STRING[16]','');

   _war:=kwrab:=0;
   ZK_P.index('TYPN');
   ZK_P.prefix(ZK_N.A,'Z',ZK_N.ref,1);
   {? ZK_P.first
   || {!
      |? _jmg:=ZK_P.M().J2<>null() & ~exec('czyJS','jm',ZK_P.M);
         _il2:={? _jmg || ZK_P.ILZ || ZK_P.IL2 ?};
         _pop:=((+form((frac(_il2))))-2);
         {? _pop>0 & _pop>ddokl || ddokl:=_pop ?};
         _pop:=((+form((frac(ZK_P.CENA))))-2);
         {? _pop>0 & _pop>cdokl || cdokl:=_pop ?};
         _pop:={? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?};
         {? exec('get','#params', 100235, 2)='T' & _pop>0 & _pop>cdokl || cdokl:=_pop ?};
         X_Xw1.clear();
         X_Xw1.blank();
         X_Xw1.P:=ZK_P.POZ;
         X_Xw1.M:=ZK_P.M().KTM+' - '+M.N;
         X_Xw1.DT:=ZK_P.DT;
         X_Xw1.IL:=_il2;
         X_Xw1.J:={? _jmg || ZK_P.JM().KOD || ZK_P.J2().KOD ?};

         {? wal
         ||
            {? ZK_N.SPOBLRAB='W'
            ||
               X_Xw1.C:=ZK_P.CWAL;
               X_Xw1.WAR:={? ZK_N.CB='T' || ZK_P.BRUTTOW || ZK_P.NETTOW ?};
               X_Xw1.WAR1:=(_il2*X_Xw1.C)$2;
               X_Xw1.KWRAB:=X_Xw1.WAR1-X_Xw1.WAR
            ||
               X_Xw1.C:=
                  {? ZK_N.RAB_TYP='P'
                  || _rabat:=1-(ZK_P.RAB+ZK_P.N().RAB)/100;
                     (ZK_P.CWAL*_rabat)$4
                  || ZK_P.CWAL-ZK_P.RABK-(ZK_N.RAB*ZK_P.CWAL/100)$4
                  ?};
               X_Xw1.WAR:={? ZK_N.CB='T' || ZK_P.BRUTTOW || ZK_P.NETTOW ?}
            ?}
         ||
            {? ZK_N.SPOBLRAB='W'
            ||
               X_Xw1.C:=ZK_P.CENA;
               X_Xw1.WAR:={? ZK_N.CB='T' || ZK_P.BRUTTO || ZK_P.NETTO ?};
               X_Xw1.WAR1:={? ZK_P.CWAL & ZK_N.SPP='T' || (((_il2*ZK_P.CWAL)$2)*ZK_P.KRS)$2 || (_il2*X_Xw1.C)$2 ?};
               X_Xw1.KWRAB:=X_Xw1.WAR1-X_Xw1.WAR
            ||
               X_Xw1.C:={? ZK_N.CB='T' || ZK_P.CB || ZK_P.CN ?};
               X_Xw1.WAR:={? ZK_N.CB='T' || ZK_P.BRUTTO || ZK_P.NETTO ?}
            ?}
         ?};
         _war+=X_Xw1.WAR;
         kwrab+=X_Xw1.KWRAB;
         X_Xw1.REF:=$ZK_P.ref();
         X_Xw1.add(1);
         ZK_P.next
      !}
   ?};

   lmt:=charleft()-4;
   k[1]:=8;                               p[1] :='form(X_Xw1.IL,,ddokl,\' ,\')';
   {? ilwar
   || k[2]:=6;                            p[2] :='form(X_Xw1.C,,cdokl,\' ,\')';
      _szer:=+form(_war,,2,' ,');
      k[3]:={? _szer>7 || _szer || 7 ?};  p[3] :='form(X_Xw1.WAR,,2,\' ,\')'
   || k[2]:=0;                            p[2] :='';
      _szer:=+form(ZK_N.NET,,2,' ,');
      k[3]:=0;                            p[3] :=''
   ?};
   k[4]:=-4;                              p[4] :='X_Xw1.P';
   k[5]:=0;                               p[5] :='X_Xw1.M';
   k[6]:=4;                               p[6] :='X_Xw1.J';
   {? ZK_N.SPOBLRAB='W'
   ||
      k[7]:=14;                           p[7] :='X_Xw1.KWRAB';
      k[8]:=6;                            p[8] :='X_Xw1.WAR1'
   ||
      k[7]:=0;                            p[7] :='';
      k[8]:=0;                            p[8] :=''
   ?};
   k[10]:=10;                             p[10]:='form(X_Xw1.DT)';

   _len:=obj_len(k);
   _buf:=obj_new(_len); {! _i.._len |! _buf[_i]:=0 !};
   _max:=obj_new(_len); {! _i.._len |! _max[_i]:=0 !};

   X_Xw1.clear();
   {? X_Xw1.first()
   || {!
      |? {! _i.._len
         |! _war:=($p[_i])();
            _typ:=type_of(_war);
            {? _typ=1 || _w:=(+form(_war));
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=2 || _w:=(+_war);
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=4 || _buf[_i]:=10
            |? _typ=5 || _buf[_i]:=5
            ?}
         !};
         X_Xw1.next()
      !}
   ?};

   {! _i.._len
   |! {? k[_i]<0         || k[_i]:=-k[_i]
      |? k[_i]<=_buf[_i] || k[_i]:=_buf[_i]
      ?}
   !};
   k[5]:=1;
   obj_del(_buf); obj_del(_max);

   {! _ii:=1..2
   |!
      PRN.add("w[1]",k[4],exec('tr','tlumaczenia','6000201'),1);
      PRN.add("w[3]",k[5],exec('tr','tlumaczenia','1000181'));
      PRN.add("w[10]",k[10],exec('tr','tlumaczenia','6000102'));
      PRN.add("w[4]",k[1],exec('tr','tlumaczenia','1000183'),1);
      PRN.add("w[7]",k[6],exec('tr','tlumaczenia','1000184'),1);
      {? ilwar
      || PRN.add("w[5]",k[2],{? ZK_N.CB='T'
                             || exec('tr','tlumaczenia','1000193')
                             || exec('tr','tlumaczenia','1000188')
                             ?},1);
         {? ZK_N.SPOBLRAB='W' & kwrab
         ||
            PRN.add("w[8]",k[7],{? ZK_N.CB='T'
                                || exec('tr','tlumaczenia','1000198')
                                || exec('tr','tlumaczenia','1000197')
                                ?},1);
            PRN.add("w[9]",k[8],exec('tr','tlumaczenia','1000199'),1);
            PRN.add("w[6]",k[3],{? ZK_N.CB='T'
                                || exec('tr','tlumaczenia','1000192')
                                || exec('tr','tlumaczenia','1000189')
                                ?},1)
         ||
            PRN.add("w[6]",k[3],{? ZK_N.CB='T'
                                || exec('tr','tlumaczenia','1000192')
                                || exec('tr','tlumaczenia','1000189')
                                ?},1)
         ?}
      ?};
      {? _ii=1
      ||
         k[5]+=charleft-PRN.width();
         VAR_DEL.delete('PRN');
         PRN:=obj_new(@.CLASS.PRINT,9,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
         PRN.framecol(color_frame_g)
      ||
         {? ilwar
         ||
            _white:='\'%1\''[color_bw];
            PRNS.add("exec('tr','tlumaczenia','6000302')+' '+{? wal || ZK_N.WAL().KOD || KSTE.WAL().KOD?}",
               k[4]+k[5]+k[1]+k[6]+k[2]+k[10]+{? ZK_N.SPOBLRAB='W' & kwrab || k[7]+k[8]+7*3 || 5*3 ?},,1,,$_white);
            PRNS.add("form(s[1],,doklw)",k[3],,1)
         ?}
      ?}
   !};
~~}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
   lmt:=int((charleft()-PRN.width())/2);
   PAR_WYDR.TITLE:=exec('tr','tlumaczenia','6000101');
   PAR_WYDR.SYM:=ZK_N.SYM;
   TYPYZAM.cntx_psh();
   PAR_WYDR.TYPE:=ZK_N.T().T;
   TYPYZAM.cntx_pop();
   zkn_date_head:=(exec('tr','tlumaczenia','6000303')+';');
   zkn_date_val:=(gsub(form(ZK_N.DP,,3),'/','-')+';');
   prn1:='PRN'; prn2:='PRNS'
;~~}
{COMMENT}   termin realizacji                                        {COMMENT-}
{
   zkn_date_head+=(exec('tr','tlumaczenia','6000102')+';');
   zkn_date_val+=(gsub(form(ZK_N.DT,,3),'/','-')+';');
~~}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}
{IF: ZK_N.sel_size()>0 | PARAM_W.KOPIA }
   {strona:=1;~~}
   {FOOTER}
   {<1}{charleft_foot:=charleft();~~}{>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {<1}{IF: exec('get_footer','img_blob')}{IMAGE: exec('get_footer','img_blob');;1}{FI}{>{charleft_foot-3}}{$strona}{' '}{COLOR: color_oo}{'  '}{COLOR: color_bw}{strona+=1;~~}
   {>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {FOOTER-}
{ELSE}
   {FOOTER}
   {<1}{charleft_foot:=charleft();~~}{>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {<1}{IF: exec('get_footer','img_blob')}{IMAGE: exec('get_footer','img_blob');;1}{FI}{MAXPAGE: >{charleft_foot-3};$page+' / '; 3L;' '}{' '}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {FOOTER-}
{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEADF'}
{ns:=0;~~}
{COMMENT}   dokument anulowany                                       {COMMENT-}
{FONT: exec('font','param_wydr','ory_kop')}{SPACE: exec('space','param_wydr')}
{IF: ZK_N.STAT_REJ='A'}{<1}{>{charleft}exec('tr','tlumaczenia','1000215')}{FI}
{COMMENT}   strony transakcji                                        {COMMENT-}
{LINE}
{ __STRTAB:='ZK_N'; __STRODB:='ZK_N.ODB'; __STRLIN:=''; __STRDAT:=ZK_N.DP;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{FONT: exec('font','param_wydr','strony_po')}
{COMMENT}   miejsce dostawy                                          {COMMENT-}
{IF: ZK_N.KH_MSC<>null}
   {LINE}
   {<{lmt+1}exec('msc_dost','kontrahent','ZK_N')}
{FI}
{COMMENT}   informacje dodatkowe dla naglowka dokumentu              {COMMENT-}
{
   prn1:='PRN';
   'pobieram dane'; exec('pobgdnag','fakso','Z',ZK_N.ref);
   VAR_DEL.delete('oo','OPIK')
;~~}
{FOR: S_GD}
{PREFIX: 'G'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}  zamowienie klienta     NUCO                                  {COMMENT-}
{<{lmt+1}
   }{FONT: exec('font','param_wydr','tr_tr')}{exec('tr','tlumaczenia','6000104')+':'
   }{FONT: exec('font','param_wydr','tr_war')}{' '}{$ZK_N.ZAM_KL}
{COMMENT}  warunki dostawy        NUCO                               {COMMENT-}
{<{lmt+1}
   }{FONT: exec('font','param_wydr','tr_tr')}{exec('tr','tlumaczenia','6000112')+': '
   }{FONT: exec('font','param_wydr','tr_war')}{' '}{$ZK_N.ZDSWD().KOD+' - '
   }{FONT: exec('font','param_wydr','tr_war')}{' '}{$ZK_N.ZDSWD().OPIS}
{COMMENT}   naglowek tabeli                                          {COMMENT-}
{FONT: exec('font','param_wydr','tabhead_poz')}
{ prn1:='PRN';~~}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {IF: ~ns}
   {SUBSTITUTE: 'HEADF'}
   {IF: zakoncz}
      {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
   {FI}
   {FI}
{HEADER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
rsep:=PAR_WYDR.RSEP;
s[1]:=0;
FAKSO.cntx_psh();
FAKSO.use('fakto' + (ZK_N.name() + 3));
lp:=0
;~~}
{FOR:X_Xw1}
{DO}
   {
      ZK_P.prefix;
      ZK_P.seek(BB.sqlint(X_Xw1.REF),);
      lp+=1;
      rsep:=PAR_WYDR.RSEP;
      w[1]:=form(lp);
      w[3]:=form(ZK_P.M().KTM+' '+ZK_P.M().N);
      w[4]:=form(X_Xw1.IL,,ddokl,'.,');
      w[5]:=form(X_Xw1.C,,cdokl,'.,');
      w[6]:=form(X_Xw1.WAR,,doklw,'.,');
      {? PARAM_W.JEZYK<>null
      || w[7]:=exec('trans_jm','tlumaczenia',ZK_P.JM,PARAM_W.JEZYK)
      || w[7]:=ZK_P.J2().KOD
      ?};
      w[8]:=form(X_Xw1.WAR1,,doklw,'.,');
      w[9]:=form(X_Xw1.KWRAB,,doklw,'.,');
      w[10]:=form(X_Xw1.DT);
      s[1]+=X_Xw1.WAR$doklw;

      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))() + 2
   ;~~}
   {IF: lineleft<=il_lin}
      {SUBSTITUTE: 'LINIAF'}{PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
{COMMENT}   informacje dodatkowe dla pozycji dokumentu               {COMMENT-}
   {FOR: FAKSO}{INDEX: 'ZK_P'}{PREFIX: ZK_P.ref}
   {DO}
      {FIRST}
         {
         VAR_DEL.delete('w_tmp');
         w_tmp:=obj_new(obj_len(w));
         {! _i..obj_len(w) |! w_tmp[_i]:=w[_i] !}
         ;~~}
      {FIRST-}
      { {! _i..ilk |! w[_i]:='' !};
        w[3]:=exec('printFAKSO','fakso',PARAM_W.JEZYK);
        rsep:=0; _f:=prn1+'.ini_line()'; ($(_f))();~~}
      {WHILE: _f:=prn1+'.next()'; ($(_f))()}
      {DO}
         {IF: lineleft()>0 & lineleft()<4}
            { bpage:=page(); {! _i..obj_len(w) |! w[_i]:=w_tmp[_i] !};~~}
            {SUBSTITUTE: 'LINIAF'}
            {IF: bpage=page}{PAGE}{FI}
            { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
            {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
            { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
         {FI}
         {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
      {OD}
      {TOTAL}
         { VAR_DEL.delete('w_tmp'); vp:=0;~~}
   {OD}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{
FAKSO.cntx_pop();
rsep:=1
;~~}
{IF: ilwar}
   {IF: lineleft()>0 & lineleft()<3}
      {SUBSTITUTE: 'LINIAF'}
      {IF: lineleft()>0}{PAGE}{FI}
   {FI}
   {FONT: exec('font','param_wydr','razem')}
   {PRNS.setcolor(color_g);~~}
   {SUBSTITUTE: 'LINIA02'}
   {FONT: exec('font','param_wydr','poz')}
   {PRNS.setcolor(color_w);~~}
   {  prn1:='PRNS'; ~~}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}

{  zakoncz:=0; ~~}
{COMMENT}   platnosci                                                {COMMENT-}
{  fp_tr:=fp_war:=tp_tr:=tp_war:=wp_tr:=wp_war:=wp_wal:=slownie:='';
   FAKSPL.cntx_psh();
   FAKSPL.use((5+FAKSPL.name())+(ref_name(ZK_N.ref())+3));
   FAKSPL.index('ZK_N');
   FAKSPL.prefix(ZK_N.ref());
   pl_size:=(FAKSPL.size()<>1);
   FAKSPL.cntx_pop();
   FAKSPL.use((5+FAKSPL.name())+(ref_name(ZK_N.ref())+3));
   ~~
}
{COLOR: color_bw}
{FOR: FAKSPL}
{INDEX: 'ZK_N'}
{PREFIX: ZK_N.ref}
{DO}
   {IF: ilwar & FAKSPL.WAR}
      {
      _tlumacz:={? PARAM_W.JEZYK=null || '' || exec('trans_slo','tlumaczenia',FAKSPL.PL,PARAM_W.JEZYK) ?};
      fp_tr:=exec('tr','tlumaczenia','1000121')+' ';
      fp_war:=
         {? PARAM_W.JEZYK<>null & _tlumacz<>''
         || _tlumacz
         || FAKSPL.PL().TR
         ?};
      tp_tr:=exec('tr','tlumaczenia','1000124');
      tp_war:=' '+gsub(form(FAKSPL.TERMPLAT,,3),'/','-');
      wp_tr:=', '+exec('tr','tlumaczenia','1000125');
      wp_war:=' '+form(FAKSPL.WAR,,2,'.,');
      wp_wal:=' '+ZK_N.WAL().KOD;
      odstep:=charleft()-(+fp_tr)-(+fp_war)-(+tp_tr)-(+tp_war)-(+wp_tr)-(+wp_war)-(+wp_wal);
      odstep:={? odstep>0 || ' '*odstep || ' ' ?};
      ~~}
      {IF: pl_size}
         {<{5}
         }{FONT: exec('font','param_wydr','fp_tr')}{fp_tr
         }{FONT: exec('font','param_wydr','fp_war')}{fp_war
         }{FONT: exec('font','param_wydr','fp_ods')}{odstep
         }{FONT: exec('font','param_wydr','tp_tr')}{tp_tr
         }{FONT: exec('font','param_wydr','tp_war')}{tp_war
         }{FONT: exec('font','param_wydr','wp_tr')}{wp_tr
         }{FONT: exec('font','param_wydr','wp_war')}{wp_war
         }{FONT: exec('font','param_wydr','wp_wal')}{wp_wal}
      {FI}
   {FI}
{OD}
{
   bank_tr:=bank_war:=rach_tr:=rach_war:=swift_tr:=swift_war:='';STR.split('');
   grey_line:=+fp_tr;
   {? +grey_line<+fp_war||grey_line:=+fp_war?};
   {? +grey_line<+tp_tr||grey_line:=+tp_tr?};
   {? +grey_line<+tp_war||grey_line:=+tp_war?};
   grey_line+=12;
~~}
{COMMENT}   czyszczenie etykiet jeśli nie mają wartości {COMMENT-}
{
   wp_tr:=(~(1+exec('tr','tlumaczenia','1000125')))+(1-exec('tr','tlumaczenia','1000125'))-1;
   {? pl_size      || fp_war:=''; tp_war:='' ?};
   {? fp_war=''    || fp_tr:=''   ?};
   {? tp_war=''    || tp_tr:=''   ?};
   {? bank_war=''  || bank_tr:='' ?};
   {? rach_war=''  || rach_tr:='' ?};
   {? swift_war='' || swift_tr:=''?};

   line_sign:='';
   {? fp_war='' & tp_war=''
   || line_sign:='';
      grey_line:=0
   ?};
 ~~}
{COMMENT}   rysowanie paska z danymi {COMMENT-}
{IF: ~pl_size & ilwar}
   {LINE}
   {SUBSTITUTE: 'panel_platnosci'}
{FI}

{COMMENT}   informacje dodatkowe pod dokumentem              {COMMENT-}
{FONT: exec('font','param_wydr','inf_dod')}{SPACE: exec('space','param_wydr')}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: S_GD}
{PREFIX: 'D'}
{DO}
   {FIRST}
   {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   uwagi                                                    {COMMENT-}
{FONT: exec('font','param_wydr','uwagi')}{SPACE: exec('space','param_wydr')}
{IF: exec('get','#params',3011,2,OPERATOR.USER)='T'}
   { __memo:=ZK_N.memo_get('r','UW'); text:='';~~}
   {IF: __memo.is_open}
      {WHILE: text:=fread(__memo); text<>'\n'}
      {DO}
         {<{lmt+1}}{
         max_T8:=charleft;
         STR.split(text);
         Text:='';
         sep:=0
         ;~~}
         {WHILE: STR.next()}
         {DO}
            {FIRST}{LINE}{FIRST-}
            { word:=STR.get_word(); sep+=1;~~}
            {IF: +Text+(+word)>=(max_T8-4-8)}
               {<{lmt+1}}{JUSTIFY}{<{lmt+1}>{max_T8-8}Text}
               { Text:=word; sep:=1;~~}
            {ELSE}
               { Text+={? sep>1 || ' ' || '' ?}+word;~~}
            {FI}
         {OD}
         {IF: +Text}
            {<{lmt+1}}{Text}
         {FI}
      {OD}
      {CENTER}
   {FI}
   { VAR_DEL.delete('__memo','text','Text');~~}
{FI}
{COMMENT}   podpisy                                                  {COMMENT-}
{FONT: exec('font','param_wydr','podpisy')}{SPACE: exec('space','param_wydr')}
{LINE: 3}
{IF: ZK_N.US<>null}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   PRN.framecol(color_frame_g);
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,ZK_N.US().DANE);
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{FI}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=30*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=exec('tr','tlumaczenia','6000301');
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
:!UTF-8
{TITLE:A. Wydruk realizacji zamówienia}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   zkrea_01:="
      VAR_DEL.delete('PRN','NAG','w','s','k','p','na','szer');
      VAR_DEL.delete('dokl','ddokl','doklw');
      VAR_DEL.delete('color','prn1','prn2');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('il_lin','dokl','doklw','lp');
      VAR_DEL.delete('zakoncz','proforma');
      VAR_DEL.delete('oddzial');
      VAR_DEL.delete('byl','wsk');
      VAR_DEL.delete('M_GD','S_GD');
      VAR_DEL.delete('zero_na','print5','op_1','op_2','op_3','op_4');
      VAR_DEL.delete('max_T8','max_T8G','max_T10B')";
   zkrea_01();

   exec('czytaj','#stalesys',,XINFO); KSTE.get();

   PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame();
   NAG:=obj_new(@.CLASS.PRINT,2); NAG.n_frame();

   w:=obj_new(8); "--wartosci kolumn--";
   s:=obj_new(1); "--razem--";
   k:=obj_new(5);  {! _i..5 |! k[_i]:=0  !}; "--szerokosc kolumn--";
   p:=obj_new(5);  {! _i..5 |! p[_i]:='' !}; "--tymczasowa do szacowania szerokosci kolumn--";
   dokl:=3; "--dokladnosc ilosci--";
   ddokl:=0; "--najwieksza drukowana dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   il_lin:=0;
   zakoncz:=1;
   oddzial:=KSTE.NAZ<>'';
   byl:=wsk:=0;
   proforma:=0;

   PARAM_W.JEZYK:=0;
~~}
{END:
   zkrea_01(); VAR_DEL.delete('zkrea_01');
~~}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_lic.rpi}
{INCLUDE: wsp_strony.rpi}
{DEFINEFONT: 'TM=Inter,,M,,0,0'}
{DEFINEFONT: 'TMB=Inter,,N,,0,0'}
{DEFINEFONT: 'TF=Inter,,F,,0,0'}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
"--szerokosc kolumn--";
   ddokl:=0;
   VAR_DEL.delete('X_Xw1');
   X_Xw1:=tab_tmp(1
      ,'P','INTEGER',''
      ,'M','STRING[160]',''
      ,'ILZ','REAL',''
      ,'IL','REAL',''
      ,'J','STRING[10]',''
      ,'C','REAL',''
      ,'WAR','REAL',''
      ,'REF','STRING[16]','');

   ZK_RP.index('POZR'); ZK_RP.prefix(ZK_RN.ref());
   {? ZK_RP.first
   || ZK_P.cntx_psh();
      {!
      |? _pop:=((+form((frac(ZK_P.IL2))))-2);
         {? _pop>0 & _pop>ddokl || ddokl:=_pop ?};
         X_Xw1.clear();
         X_Xw1.blank();
         X_Xw1.P:=ZK_RP.P().POZ;
         X_Xw1.M:=ZK_RP.M().KTM+' - '+M.N;
         X_Xw1.ILZ:=ZK_P.ILZ;
         X_Xw1.IL:=ZK_RP.ILR;
         X_Xw1.J:=ZK_RP.P().J2().KOD;
         X_Xw1.REF:=$ZK_RP.ref();
         X_Xw1.add(1);
         ZK_RP.next
      !};
      ZK_P.cntx_pop()
   ?};

   k[1]:=5;                            p[1] :='X_Xw1.P';
   k[2]:=0;                            p[2] :='X_Xw1.M';
   k[3]:=15;                           p[3] :='form(X_Xw1.ILZ,,ddokl,\' ,\')';
   k[4]:=15;                           p[4] :='form(X_Xw1.IL,,ddokl,\' ,\')';
   k[5]:=10;                           p[5] :='X_Xw1.J';

   _len:=obj_len(k);
   _buf:=obj_new(_len); {! _i.._len |! _buf[_i]:=0 !};
   _max:=obj_new(_len); {! _i.._len |! _max[_i]:=0 !};

   X_Xw1.clear();
   {? X_Xw1.first()
   || {!
      |? {! _i.._len
         |! _war:=($p[_i])();
            _typ:=type_of(_war);
            {? _typ=1 || _w:=(+form(_war));
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=2 || _w:=(+_war);
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=4 || _buf[_i]:=10
            |? _typ=5 || _buf[_i]:=5
            ?}
         !};
         X_Xw1.next()
      !}
   ?};

   {! _i.._len
   |! {? k[_i]<0         || k[_i]:=-k[_i]
      |? k[_i]<=_buf[_i] || k[_i]:=_buf[_i]
      ?}
   !};

   k[2]:=1;
   {! _ii:=1..2
   |!
      PRN.add("w[1]",k[1],'Lp.'@,1);
      PRN.add("w[2]",k[2],'Materiał/Usługa'@);
      PRN.add("w[3]",k[3],'Ilość zamawiana'@,1);
      PRN.add("w[4]",k[4],'Ilość zrealizowana'@,1);
      PRN.add("w[5]",k[5],'jm'@);
      {? _ii=1
      ||
         k[2]+=charleft-PRN.width();
         VAR_DEL.delete('PRN');
         PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame()
      ?}
   !};
~~}
{FONT: exec('font','param_wydr','header')}{SPACE: exec('space','param_wydr')}
{
   lmt:=int((charleft()-PRN.width())/2);
   PAR_WYDR.TITLE:='Realizacja Nr '@+ZK_RN.SYM+' do zamówienia Nr '@+ZK_N.SYM;
   prn1:='PRN'; prn2:='PRNS'
;~~}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEADF'}
{LINE}
{COMMENT}   strony transakcji                                        {COMMENT-}
{IF: ZK_RN.N().T().R='Z'}
{ __STRTAB:='ZK_N'; __STRODB:='ZK_N.ODB'; __STRLIN:=''; __STRDAT:=ZK_N.DP;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{ELSE}
{LINE: 2}
{FI}
{FONT: exec('font','param_wydr','strony_po')}{SPACE: exec('space','param_wydr')}
{COMMENT}   termin realizacji                                        {COMMENT-}
{<{lmt+1}'Zrealizowano: '@+ZK_RN.DR$1+','+ZK_RN.TR$1}
{<{lmt+1}'Magazyn: '@+ZK_RN.MG().SYM}
{IF: ZK_RN.SWZ<>''}{<{lmt+1}'Symbol dokumentu wydania: '@+ZK_RN.SWZ}{FI}
{IF: ZK_RN.SFK<>''}{<{lmt+1}'Symbol faktury: '@+ZK_RN.SFK}{FI}
{COMMENT}   naglowek tabeli                                          {COMMENT-}
{  prn1:='PRN';~~}
{FONT: exec('font','param_wydr','tabhead_poz')}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEADF'}
   {IF: zakoncz}
      {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
   {FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{IF: _a:=exec('get_par','#parametr',34); _a='Z' | _a='A'}
   {FOOTER}
   {SUBSTITUTE: 'FOOT_LIC'}
   {~~}
   {~~}
   {~~}
   {FOOTER-}
{ELSE}
   {FOOTER}{FOOTER-}
{FI}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{FOR:'X_Xw1'}
{DO}
   {FIRST}{ rsep:=PAR_WYDR.RSEP; lp:=0;~~}{FIRST-}
   {
   lp+=1;
   w[1]:=form(lp);
   w[2]:=X_Xw1.M;
   w[3]:=form(X_Xw1.ILZ,,ddokl,'.,');
   w[4]:=form(X_Xw1.IL,,ddokl,'.,');
   w[5]:=X_Xw1.J;

   _f:=prn1+'.ini_line()'; _v:=($(_f))();
   _f:=prn1+'.length()'; il_lin:=($(_f))()+1;
   ~~}
   {IF: lineleft()<il_lin}{SUBSTITUTE: 'LINIAF'}{IF:lineleft>0}{PAGE}{FI}{FI}
   {SUBSTITUTE: 'LINIA0'}
   {TOTAL}
      { VAR_DEL.delete('lp');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{SUBSTITUTE: 'LINIAF'}
{ zakoncz:=0;~~}
{COMMENT}   podpisy                                                  {COMMENT-}
{FONT: exec('font','param_wydr','podpisy')}{SPACE: exec('space','param_wydr')}
{LINE: 3}
{IF: ZK_RN.US<>null}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,ZK_RN.US().DANE);
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{FI}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=30*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:='Zrealizował'@;
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}

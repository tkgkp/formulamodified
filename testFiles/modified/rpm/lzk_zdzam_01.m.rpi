:!UTF-8
{COMMENT}   Makro uzywane do drukowania stopki tabeli z pozycjami    {COMMENT-}
{DEFINEFONT: 'TM=Inter,75,Q,,0,0'}
{DEFINEFONT: 'TMB=Inter,75,R,,0,0'}
{DEFINEFONT: 'TF=Inter,75,F,,0,0'}
{DEFINEFONT: 'IE=Inter,150,E,,0,0'}
{DEFINEFONT: 'IF=Inter,150,F,,0,0'}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
   _nag_wc1:=charleft()/2;
   _nag_wc2:=charleft()-_nag_wc1;
   NAG.add("na[1]",_nag_wc1,'');
   NAG.add("na[2]",_nag_wc2,'');

   lmt:=int((charleft()-PRN.width())/2);
   PAR_WYDR.TITLE:=exec('tr','tlumaczenia','6000101');
   PAR_WYDR.SYM:=ZD_NAG.SYM;
   TYPYZAM.cntx_psh();
   PAR_WYDR.TYPE:=ZD_NAG.T().T;
   TYPYZAM.cntx_pop();
   zkn_date_head:=(exec('tr','tlumaczenia','6000304')+';');
   zkn_date_val:=(gsub(form(ZD_NAG.DATA,,3),'/','-')+';');
   {? ZD_NAG.DTPREAL<>date(0,0,0) & PARAM_W.DTPREAL='T'
   ||
      zkn_date_head+=(exec('tr','tlumaczenia','6000110')+';');
      zkn_date_val+=(gsub(form(ZD_NAG.DTPREAL,,3),'/','-')+';')
   ?};
   prn1:='PRN'; prn2:='PRNS'
;~~}
{COMMENT}   FOOTER                                                {COMMENT-}
{SUBSTITUTE: 'clean_footer'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEADF'}
{ns:=0;~~}
{COMMENT}   dokument anulowany                                       {COMMENT-}
{FONT: exec('font','param_wydr','ory_kop')}{SPACE: exec('space','param_wydr')}
{IF: ZD_NAG.STAT_REJ='A'}{<1}{>{charleft}exec('tr','tlumaczenia','1000215')}{FI}
{LINE}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
"--szerokosc kolumn--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1
   ,'P','INTEGER',''
   ,'M','STRING[160]',''
   ,'JM','STRING[5]',''
   ,'IL','REAL',''
   ,'C','REAL',''
   ,'WAR','REAL',''
   ,'REF','STRING[16]','');

_sum_war:=0;
ZD_POZ.index('POZ'); ZD_POZ.prefix(ZD_NAG.ref());
{? ZD_POZ.first
||
   dokl_c:=exec('fld_prec','#field',ZD_POZ,{? walh=1 || 'CENA' || 'CWAL' ?});
   {!
   |? _pop:=((+form((frac(ZD_POZ.IL))))-2);
      {? _pop>0 & _pop>ddokl || ddokl:=_pop ?};
      X_Xw1.clear();
      X_Xw1.blank();
      X_Xw1.P:=ZD_POZ.POZ;
      X_Xw1.M:=ZD_POZ.M().KTM+' - '+M.N;
      X_Xw1.JM:=ZD_POZ.JM().KOD;
      X_Xw1.IL:=ZD_POZ.IL;
      X_Xw1.C:={? walh=1 || ZD_POZ.CENA || ZD_POZ.CWAL ?};
      X_Xw1.WAR:={? walh=1 || ZD_POZ.WAR || ZD_POZ.WARW ?};
      X_Xw1.REF:=$ZD_POZ.ref();
      X_Xw1.add(1);
      _sum_war+=X_Xw1.WAR $2;
      ZD_POZ.next
   !}
?};

lmt:=charleft()-4;
'NUCO - zmiana domyślnej szerokości pola ilość';
k[1]:=-10;                           p[1] :='form(X_Xw1.IL,,ddokl,\' ,\')';
k[2]:=5;                            p[2] :='form(X_Xw1.C,,dokl_c,\' ,\')';
k[3]:=7;                            p[3] :='form(X_Xw1.WAR,,2,\' ,\')';
k[4]:=-4;                           p[4] :='X_Xw1.P';
k[5]:=0;                            p[5] :='X_Xw1.M';
k[6]:=-4;                           p[6] :='X_Xw1.JM';

_len:=obj_len(k);
_buf:=obj_new(_len); {! _i.._len |! _buf[_i]:=0 !};
_max:=obj_new(_len); {! _i.._len |! _max[_i]:=0 !};

X_Xw1.clear();
{? X_Xw1.first()
|| {!
   |? {! _i.._len
      |! _war:=($p[_i])();
         _typ:=type_of(_war);
         {? _typ=1 || _w:=(+form(_war));
                      {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
         |? _typ=2 || _w:=(+_war);
                      {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
         |? _typ=4 || _buf[_i]:=10
         |? _typ=5 || _buf[_i]:=5
         ?}
      !};
      X_Xw1.next()
   !}
?};

_max[3]:=_buf[3]:=(+form(_sum_war,,2));

{! _i.._len
|! {? k[_i]<0         || k[_i]:=-k[_i]
   |? k[_i]<=_buf[_i] || k[_i]:=_buf[_i]
   ?}
!};
k[5]:=1;
obj_del(_buf); obj_del(_max);

{! _ii:=1..2
|!
   PRN.add("w[1]",k[4],exec('tr','tlumaczenia','6000201'),1);
   PRN.add("form(w[3])",k[5],exec('tr','tlumaczenia','1000181'));
   PRN.add("form(w[7])",k[6],exec('tr','tlumaczenia','1000184'));
   PRN.add("form(w[4],,ddokl)",k[1],exec('tr','tlumaczenia','1000183'),1);
   PRN.add("form(w[5],,dokl_c)",k[2],exec('tr','tlumaczenia','1000188'),1);
   PRN.add("form(w[6],,2)",k[3],exec('tr','tlumaczenia','1000189'),1);
   {? kol>6
   ||
      PRN.add("form(w[8])",k[6],exec('tr','tlumaczenia','1000184'));
      PRN.add("form(w[9],,ddokl)",k[1],exec('tr','tlumaczenia','1000183'),1);
      PRN.add("form(w[10],,dokl_c)",k[2],exec('tr','tlumaczenia','1000188'),1)
   ?};
   {? _ii=1
   ||
      k[5]+=charleft-PRN.width();
      VAR_DEL.delete('PRN');
      PRN:=obj_new(@.CLASS.PRINT,kol,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN.framecol(color_frame_g)
   ?}
!};

_white:='\'%1\''[color_bw];
PRNS.add(" exec('tr','tlumaczenia','6000302')+' '+{? walh=1 || INFO.NAROD().KOD || ZD_NAG.WAL().KOD ?}",k[4]+3+k[5]+3+k[6]+3+k[1]+3+k[2],,1,,$_white);
PRNS.add("form(s[2],,2)",k[3],,1);
~~}
{COMMENT}   strony transakcji                                        {COMMENT-}
{ __STRTAB:='ZD_NAG'; __STRODB:=''; __STRLIN:=''; __STRDAT:=ZD_NAG.DATA;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{inf_head:='';
 inf_val:='';
 ~~}
{COMMENT}   miejsce dostawy                                          {COMMENT-}
{FONT: exec('font','param_wydr','strony_po')}{SPACE: exec('space','param_wydr')}
{IF: ZD_NAG.ZAM_DST<>''}
   {inf_head+=(exec('tr','tlumaczenia','6000109')+';');
    inf_val+=(ZD_NAG.ZAM_DST+';');
    ~~}
{FI}
{IF: ZD_NAG.KH_MSC<>null}
   {inf_head+=(exec('tr','tlumaczenia','6000103')+';');
    inf_val+=((+exec('tr','tlumaczenia','6000103')-exec('msc_dost','kontrahent','ZD_NAG'))+';');
    ~~}
{FI}
{COMMENT}   projekty                               {COMMENT-}
{IF: PARAM_W.PROJEKTY='T'
& (ZD_NAG.PROJEKTY<>null()
| (
ZD_POZ.cntx_psh();
PROJEKTY.cntx_psh();
ZD_POZ.index('POZ');
ZD_POZ.prefix(ZD_NAG.ref());
_wyn:=0;
{? ZD_POZ.first()
||
   {!|?
      {? ZD_POZ.PROJEKTY<>null() || _wyn+=1 ?};
      ZD_POZ.next()
   !}
?};
ZD_POZ.cntx_pop();
PROJEKTY.cntx_pop();
_wyn
))}
           {
            inf_head+=(exec('tr','tlumaczenia','6000113')+';');
            _wyd:='';
            ZD_POZ.cntx_psh();
            PROJEKTY.cntx_psh();
            ZD_POZ.index('POZ');
            ZD_POZ.prefix(ZD_NAG.ref());
            {? ZD_NAG.PROJEKTY<>null() || _wyd+=ZD_NAG.PROJEKTY().SYM?};
            {? ZD_POZ.first()
            ||
               {!|?
                  {? ZD_POZ.PROJEKTY<>null()
                  ||
                     {? ~(_wyd*ZD_POZ.PROJEKTY().SYM)
                     ||
                        {? _wyd<>''
                        || _wyd+=', ' + ZD_POZ.PROJEKTY().SYM
                        || _wyd+=ZD_POZ.PROJEKTY().SYM
                        ?}
                     ?}
                  ?};
                  ZD_POZ.next()
               !}
            ?};
            ZD_POZ.cntx_pop();
            PROJEKTY.cntx_pop();
            inf_val+=_wyd+';';
            ~~
            }
{FI}
{COMMENT}   warunki dostawy                               {COMMENT-}
{IF: PARAM_W.ZDSWD='T' & ZD_NAG.ZDSWD<>null() & ZD_NAG.ZDSWD().OPIS<>''}
   {inf_head+=(exec('tr','tlumaczenia','6000112')+';');
    inf_val+=(ZD_NAG.ZDSWD().OPIS+';');
    ~~}
{FI}
{COMMENT}informacje dodatkowe  {COMMENT-}
{ENTIRE}
{SUBSTITUTE: 'panel_informacji'}
{ENTIRE-}
{COMMENT} Dane dodatkowe wynikajace z informacji dodatkowych zawartych do zamowienia {COMMENT-}
{
   'pobieram dane'; exec('pobgdnag','fakso','D',ZD_NAG.ref);
   VAR_DEL.delete('oo','OPIK')
;~~}
{FOR: S_GD}
{PREFIX: 'G'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{  prn1:='PRN'; ~~}
{FONT: exec('font','param_wydr','tabhead_poz')}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {IF: ~ns}
   {SUBSTITUTE: 'HEADF'}
   {IF: zakoncz}
      {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
      {SUBSTITUTE: 'TABHEAD'}
   {FI}
   {FI}
{HEADER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{  rsep:=PAR_WYDR.RSEP; w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=0;w[7]:='';s[1]:=s[2]:=s[3]:=s[4]:=0;
   {? kol>6 || w[9]:=w[10]:=0; w[8]:='' ?};
~~}
{FOR:'ZD_POZ'}{INDEX: 'POZ'}{PREFIX: ZD_NAG.ref}
{DO}
   {
      rsep:=PAR_WYDR.RSEP;
      w[1]+=1;ok:=w[1];

      w[3]:='';
      {? PARAM_W.INDEKS='T' || w[3] += ZD_POZ.M().KTM+' ' ?};
      _nazwa:='';
      {? PARAM_W.JEZYK<>null || _nazwa:=exec('trans_m','tlumaczenia',ZD_POZ.M,PARAM_W.JEZYK) ?};
      {? _nazwa='' || _nazwa:=ZD_POZ.M().N ?};
      {? PARAM_W.NAZWA='T'  || w[3] += {? w[3]<>'' || ' - ' || '' ?}+_nazwa+' ' ?};
      {? PARAM_W.UWAGI='T'  & ZD_POZ.UWAGI<>'' || w[3] += {? w[3]<>'' || ' - ' || '' ?}+ZD_POZ.UWAGI ?};
      {? PARAM_W.KODOBCY='T' & ZD_POZ.M<>null  & ZD_NAG.KH<>null
      || _kod_obcy:=exec('kod_obcy','kody_kresk',ZD_POZ.M,ZD_NAG.KH);
         {? _kod_obcy<>'' || w[3] += {? w[3]<>''||' - ' || '' ?}+_kod_obcy ?}
      ?};
      {? PARAM_W.DK_C='T' & ZD_POZ.DK_C<>null
      || _dk_c:=form(exec('sym_dk_c','mat_atr',ZD_POZ.DK_C));
         {? _dk_c<>''
         || w[3]+={? w[3]<>''||' - ' || '' ?}+_dk_c
         ?}
      ?};

      s[1]+=w[4]:=ZD_POZ.IL;
      w[5]:={? walh=1 || ZD_POZ.CENA || ZD_POZ.CWAL ?};
      w[7]:=ZD_POZ.JM().KOD;
      s[2]+=w[6]:={? walh=1 || ZD_POZ.WAR || ZD_POZ.WARW ?};
      {? kol>6
      ||
         w[8]:=w[9]:=w[10]:='';
         {? ZD_NAG.WAL=ZD_POZ.WAL & ZD_POZ.J2 & ZD_POZ.J2<>ZD_POZ.JM & ZD_POZ.CENA2 & ZD_POZ.IL2
         ||
            w[8]:=ZD_POZ.J2().KOD;
            w[9]:=ZD_POZ.IL2;
            w[10]:=ZD_POZ.CENA2;
            {? w[7]=w[8] & w[4]=w[9] & w[5]=w[10] || w[8]:=w[9]:=w[10]:='' ?}
         ?}
      ?};
   ~~}
   {
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))() + 2;
   ~~}
   {IF: lineleft <= il_lin}
      {SUBSTITUTE: 'LINIAF'}{PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
{COMMENT}   informacje dodatkowe dla pozycji dokumentu               {COMMENT-}
   {FOR: FAKSO}{INDEX: 'ZD_POZ'}{PREFIX: ZD_POZ.ref}
   {DO}
      { {! _i..{? kol>6 || 10 || 7 ?} |! w[_i]:='' !};
        {? FAKSO.O<>'' || w[3]:=exec('printFAKSO','fakso',PARAM_W.JEZYK) ?};
        rsep:=0; ~~}
      {SUBSTITUTE: 'LINIA0'}
      {  w[1]:=ok; ~~}
   {OD}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{  rsep:=1; ~~}
   {IF: lineleft()>0 & lineleft()<3}
      {SUBSTITUTE: 'LINIAF'}
      {IF: lineleft()>0}{PAGE}{FI}
   {FI}
   {FONT: exec('font','param_wydr','razem')}
   {PRNS.setcolor(color_g);~~}
   {SUBSTITUTE: 'LINIA02'}
   {FONT: exec('font','param_wydr','poz')}
   {PRNS.setcolor(color_w);~~}
{ zakoncz:=0; ~~}
{LINE: 1}
{COMMENT}   forma platnosci                                          {COMMENT-}
{  fp_tr:=fp_war:=tp_tr:=tp_war:=wp_tr:=wp_war:=wp_wal:=slownie:='';~~ }
{IF: ZD_NAG.KH().PLATNOSC<>null | ZD_NAG.PL<>null}
   {LINE}
   {
   fp_tr:=exec('tr','tlumaczenia','1000121');
   fp_war:={? ZD_NAG.PL=null() || ZD_NAG.KH().PLATNOSC().TR || ZD_NAG.PL().TR ?};
   tp_tr:={? ZD_NAG.TZ<>date(0,0,0) || exec('tr','tlumaczenia','1000124') || '' ?};
   tp_war:={? ZD_NAG.TZ<>date(0,0,0) || ' '+form(ZD_NAG.TZ) || '' ?};
   wp_tr:=wp_war:=wp_wal:=slownie:='';
   ~~}
{FI}
{
   bank_tr:=bank_war:=rach_tr:=rach_war:=swift_tr:=swift_war:='';STR.split('');
   grey_line:=+fp_tr;
   {? +grey_line<+fp_war||grey_line:=+fp_war?};
   {? +grey_line<+tp_tr||grey_line:=+tp_tr?};
   {? +grey_line<+tp_war||grey_line:=+tp_war?};
   grey_line+=12;
~~}
{COMMENT}   czyszczenie etykiet jeśli nie mają wartości {COMMENT-}
{
   {? fp_war=''    || fp_tr:=''   ?};
   {? tp_war=''    || tp_tr:=''   ?};
   {? bank_war=''  || bank_tr:='' ?};
   {? rach_war=''  || rach_tr:='' ?};
   {? swift_war='' || swift_tr:=''?};

   line_sign:='';
   {? fp_war='' & tp_war=''
   || line_sign:='';
      grey_line:=0
   ?};
 ~~}
{COMMENT}   rysowanie paska z danymi {COMMENT-}
{SUBSTITUTE: 'panel_platnosci'}

{COMMENT}   informacje dodatkowe dla naglowka dokumentu              {COMMENT-}
{FONT: exec('font','param_wydr','inf_dod')}{SPACE: exec('space','param_wydr')}
{  rsep:=PAR_WYDR.RSEP; ~~}
{FOR: S_GD}
{PREFIX: 'D'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   uwagi                                                    {COMMENT-}
{IF: ZD_NAG.UWAGI<>''}
   {FONT: exec('font','param_wydr','uwagi')}
   {COLOR: color_gw}
   {<1}{exec('tr','tlumaczenia','6000111')}
   {<1}{charleft()*'─'}
   {COLOR: color_bw}
   {ZD_NAG.UWAGI}
{FI}
{COMMENT}   podpisy                                                  {COMMENT-}
{FONT: exec('font','param_wydr','podpisy')}{SPACE: exec('space','param_wydr')}
{LINE: 3}
{IF: ZD_NAG.US<>null}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,ZD_NAG.US().DANE);
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{FI}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=30*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=exec('tr','tlumaczenia','6000108');
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
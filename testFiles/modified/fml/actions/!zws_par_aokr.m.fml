:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_aokr.fml [17.00]
:: Utworzony: 2015/10/19
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności rejestracji okresow
::======================================================================================================================


\set_okr_srodki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła ustawia okresy i lata podatkowe dla środków trwałych
::       w tabeli okresów obszaru FKS (OKRO_F)
:: zmiana ze względu na podział roku 2023
::----------------------------------------------------------------------------------------------------------------------
:: nuco dostosowanie roku środków trwałych do roku kalendarzowego
OKRO_F.cntx_psh();
OKRO_F.index('FIRMA_NR');
OKRO_F.prefix(REF.FIRMA,);
{? OKRO_F.first()
|| _res:=OKRO_F.ROK().POCZ_ROK~1;
   _rok:=null;
   {! |?
:: jak długi rok?
      {? _rok<>OKRO_F.ROK
      || {? _rok<>null || _res+=1 ?};
         _rok:=OKRO_F.ROK
      ?};
:: nuco rok kalendarzowy
      _res:=OKRO_F.ROK().POCZ_ROK~1;
      OKRO_F.RES:=_res;
      OKRO_F.cntx_psh();
      OKRO_F.index('ROK');
      OKRO_F.prefix(_rok);
      _size:=OKRO_F.size()-2;
      OKRO_F.cntx_pop();
      {? _size=12
      || {? OKRO_F.NR<13 || OKRO_F.OES:=OKRO_F.NR || OKRO_F.OES:=13 ?}
:: nuco zablokowanie przenumerowania okresów
      |? _size<12;0
      || {? OKRO_F.NR=0
         || OKRO_F.OES:=0
         |? OKRO_F.NR=_size+1
         || OKRO_F.OES:=13
         || OKRO_F.OES:=OKRO_F.NR+(12-_size)
         ?}
      |? _size>12
      || {? OKRO_F.NR=0
         || OKRO_F.OES:=0
         |? OKRO_F.NR=_size+1
         || OKRO_F.OES:=13
         || {? OKRO_F.NR<=(_size-12)
            || OKRO_F.OES:=OKRO_F.NR+(12-(_size-12));
               {? OKRO_F.NR=(_size-12) || _res+=1 ?}
            || OKRO_F.OES:=OKRO_F.NR-(_size-12)
            ?}
         ?}
      ?};
      OKRO_F.TAB_KRST:=exec('tab_krst','!zws_par_aokr',OKRO_F.RES);
      OKRO_F.cntx_psh(); OKRO_F.prefix(); OKRO_F.put(); OKRO_F.cntx_pop();
      OKRO_F.next()
   !}
?};
OKRO_F.cntx_pop()



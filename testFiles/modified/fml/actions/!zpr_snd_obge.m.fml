:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zpr_snd_obge.fml
:: Utworzony: 21.11.2016
:: Autor: AFI
::======================================================================================================================
:: Zawartość: Formuły czynności ZPR_SND_OBGE - Wysyłanie e-maili (obiegi)
::======================================================================================================================

\send_remind
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [12.10]
:: OPIS: Start systemu - wprowadzanie danych
::   WE: _a - nadawca
::       _b - od
::       _c - odpowiedź do
::       _d - email administratora - wysylanie maila z informacją o błędach
::       _e - wyświetlać podsumowanie? 1-tak 0-nie
::       _f - czy delegacje
::       _g - czy wnioski
::       _h - czy faktury
::       _i - czy zaliczki
::       _j - liczba dni
::----------------------------------------------------------------------------------------------------------------------
_err:=_d<>'';
_prevUser:=''; _usr:=0; _usrNew:=1; _next:=0;
{? _err
|| VAR_DEL.delete('Body','EdokTab','tmpstring1');
   Body:=obj_new('BODY','LP','ind','add','first');
   Body.first:=1;
   Body.BODY:=tab_tmp(1,
      'LP','INTEGER','Lp',
      'TEKST','STRING[255]','Tekst',
      'USR','INTEGER',
   );
   Body.ind:=Body.BODY.ndx_tmp('',1,'USR',,0);
   Body.LP:=0;
   tmpstring1:='<b>'+'Błędy w adresach e-mail użytkowników dokumentów w obiegu:'@+'</b>';
   Body.add:="
      {? Body.first
      || Body.first:=0;
         Body.add(tmpstring1)
      ?};
      {? var_press('_b')>0 & _b
      || Body.BODY.cntx_psh();
         Body.BODY.index(Body.ind); Body.BODY.prefix(#_b);
         _ok:=~Body.BODY.first();
         Body.BODY.cntx_pop()
      || _ok:=1
      ?};
      {? _ok
      || Body.LP+=1;
         Body.BODY.LP:=Body.LP;
         Body.BODY.TEKST:=_a;
         Body.BODY.USR:={? var_press('_b')>0 || #_b || 0 ?};
         Body.BODY.add()
      ?}
   "
?};
_o_del:=_f;
_o_wni:=_g;
_o_fak:=_h;
_o_zal:=_i;
_l_dni:=_j;
exec('inispdob','!zpr_snd_obge');
exec('inikondob','!zpr_snd_obge');
exec('user_info_ini','!zpr_snd_obge');
UsrInfo.setType(_o_fak,_o_wni,_o_del,_o_zal);

USERS.cntx_psh(); EDOKUM.cntx_psh();

EdokTab:=exec('edokum_list','!zpr_snd_obge',_l_dni);
__bool_ed:=1;
:: Okienko podglądu zapytania do testów
::EdokTab.win_edit(EdokTab.mk_edit(,1));
::EdokTab.win_sel(EdokTab.mk_sel(,,1));
::EdokTab.select();
{? EdokTab.first()
|| {! |?
   {? EdokTab.E_REF<>''
   || exec('addsprdob','qobiegi',1,_a,1)
   ?};
   EdokTab.next()
   !}
?};
_tp:=sql('select EDOKUM.REFERENCE as REF, EDOKOS.STATUS as STATUS, EDOKOS.OPERACJA as OPERACJA from @EDOKUM
join @EDOKOS using (EDOKOS.EDOKUM, EDOKUM.REFERENCE)
where EDOKUM.ALERTY=\'T\' and EDOKUM.ZAM<>\'T\' and (EDOKOS.STATUS=\'N\') and EDOKOS.OPERACJA=\'A\' and EDOKUM.TYPOBIEG=\'typobieg00000001\'
group by EDOKUM.REFERENCE, EDOKOS.STATUS,EDOKOS.OPERACJA
');
_temp:=0;
{? _tp.first()
|| {! |?
   _tab_edok:=sql('select OPIS from @EDOKLOG WHERE EDOKLOG.EDOKUM=\':_a\'',_tp.REF);
   {? _tab_edok.first()
   || {! |?
      {? _tab_edok.OPIS='Odrzucenie faktury'
      || _temp:=1
      ?};
      {? _temp=1
      || {? _tab_edok.last()
         || {? _tab_edok.OPIS='Przekazanie faktury do akceptacji' | _tab_edok.OPIS='Odrzucenie faktury'
            || exec('addsprdob','qobiegi',1,_a,_tp.REF);
               _temp:=0
            ?}
         ?}
      ?};
      _tab_edok.next()
      !}
   ?};
   &_tab_edok;
   _tp.next()
   !}
?};
__bool_ed:=2;
__bool_em:=0;
{? EdokTab.first()
|| {!
   |? {? _usrNew
      || __bool_em:=0;
         TABSPDOK.erase(); TABSPDOK.clear();
         TABSPZAL.erase(); TABSPZAL.prefix();
         _prevUser:=EdokTab.U_REF;
         _usr:=USERS.seek(EdokTab.U_REF,,1) & UsrInfo.setUser(USERS.ref());
         {? __tab.first() & ( USERS.EMAIL='Wojciech.Wypych@assecobs.pl' |  USERS.EMAIL='a.gryczka@nuco.pl'
         |  USERS.EMAIL='j.lewandowska@nuco.pl' | USERS.EMAIL='finanse@nuco.pl')
         || __bool_em:=1;
            {! |?
            exec('addsprdob','qobiegi',1,_a);
            __tab.next()
            !}
         ?}
      ?};
      {? _usr
      || _size:=TABSPDOK.size()+TABSPZAL.size();
         {? _usrNew & exec('mail_ok','#email',USERS.EMAIL)=0
         || {? _err
            || Body.add(USERS.KOD+' - nieprawidłowy adres e-mail: '@+USERS.EMAIL,USERS.ref())
            ?}
         || {? EdokTab.E_REF<>''
            || {? _size<=1000 || exec('addsprdob','!zpr_snd_obge',1,_a) ?}
            |? _o_zal & UsrInfo.ZALICZKI[1] & EdokTab.EZ_REF<>''
            || {? _size<=1000 || exec('addsprzal','!zpr_snd_obge',1) ?}
            ?};
            _next:={? EdokTab.next()
                   || {? _prevUser<>EdokTab.U_REF
                      || _usrNew:=1;
                         TABSPDOK.prefix(); TABSPZAL.prefix();
                         {? TABSPDOK.first() | TABSPZAL.first()
                         || {? USERS.EMAIL='j.lewandowska@nuco.pl'
                            || exec('add','!zpr_snd_obge',_b,_c,_d,null,USERS.EMAIL);
                               exec('add','!zpr_snd_obge',_b,_c,_d,null,'finanse@nuco.pl')
                            || exec('add','!zpr_snd_obge',_b,_c,_d,null,USERS.EMAIL)
                            ?}
                         ?};
                         1
                      || _usrNew:=0;
                         1
                      ?}
                   || TABSPDOK.prefix(); TABSPZAL.prefix();
                      {? TABSPDOK.first() | TABSPZAL.first()
                      || {? USERS.EMAIL='j.lewandowska@nuco.pl'
                         || exec('add','!zpr_snd_obge',_b,_c,_d,null,USERS.EMAIL);
                            exec('add','!zpr_snd_obge',_b,_c,_d,null,'finanse@nuco.pl')
                         || exec('add','!zpr_snd_obge',_b,_c,_d,null,USERS.EMAIL)
                         ?}
                      ?};
                      0
                   ?}
         ?}
      || _next:=EdokTab.next()
      ?};
      _next
   !}
?};
USERS.cntx_pop(); EDOKUM.cntx_pop();
{? _err
|| {? Body.BODY.first()
   || exec('add_err','!zpr_snd_obge',_b,_a,_c,_d,Body.BODY)
   ?};
   VAR_DEL.delete('Body','tmpstring1')
?};
&__tab;
exec('delspzal','!zpr_snd_obge');
exec('delspdob','!zpr_snd_obge');
VAR_DEL.delete('UsrInfo','EdokTab');
1


\addsprdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Dodawanie do tabelki tymczasowej kontrola dokumentow w obiegu - zadanie
::  OLD: \addsprdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUM.seek(EdokTab.E_REF,ref_name(EdokTab.E_REF),1)
|| USERS.cntx_psh();
   _status:={? EdokTab.OPERACJA='W' & EdokTab.STATUS='O'
            || 1
            |? EdokTab.OPERACJA='W' & EdokTab.STATUS='N'
            || 2
            |? EdokTab.OPERACJA='P' & EdokTab.STATUS='O'
            || 3
            |? EdokTab.OPERACJA='P' & EdokTab.STATUS='N'
            || 4
            |? EdokTab.OPERACJA='A' & EdokTab.STATUS='O'
            || 5
            |? EdokTab.OPERACJA='A' & EdokTab.STATUS='N'
            || 6
            |? EdokTab.OPERACJA='D' & EdokTab.STATUS='N'
            || 7
            || 0
            ?};
   EDOKUM.TYPOBIEG();
   _typ:={? TYPOBIEG.NAZWA='Obieg faktur'
         || 1
         |? TYPOBIEG.NAZWA='Obieg wniosków'
         || 2
         || 3
         ?};
   _upr:={? _status=7
         || exec('usr_fjks','b_perm',EDOKUM.ODD,USERS.ref())
         || 1
         ?};
   {? _upr
   || _upr:=UsrInfo[_typ][{? EdokTab.OPERACJA='W' || 1
                          |? EdokTab.OPERACJA='P' || 2
                          |? EdokTab.OPERACJA='A' || 3
                          |? EdokTab.OPERACJA='D' || 4 ?}]
   ?};
   {? _upr
   || TABSPDOK.cntx_psh(); TABSPDOK.index(ind_spd); TABSPDOK.prefix($EDOKUM.ref());
      _upr:=~TABSPDOK.first();
      TABSPDOK.cntx_pop()
   ?};
:: Sprawdzenie statusu dekretacji na dokumencie
   {? _upr
   || _upr:={? _status=7
            || ~exec('zn_deleg','obiegi_dekr',EDOKUM.ref())
            |? _status<>0
            || 1
            || 0
            ?}
   ?};
:: Sprawdzenie, czy proces jest aktywny i nie wystąpił problem
   {? _upr
   || _res:=exec('isActiveProcOrProblems','#b__box',EDOKUM.uidref());
      _upr:=(_res.isActive & ~_res.isProblem);
:: Sprawdzenie, czy wprowadzenie wniosku jest przydzielona osobie wykonującej
      {? _upr & _typ=2 & (_status=1 | _status=2) & _res.b_proc<>null()
      || BI_TODO.cntx_psh(); {? BI_TODO.name()='' || BI_TODO.use('bi_t____') ?};
         BI_TODO.index('USERPROC'); BI_TODO.prefix(_res.b_proc,USERS.ref);
         _upr:=BI_TODO.find_tab('first','BI_STAT',,'=',exec('TODO_MOJE','#bi_stat'));
         BI_TODO.cntx_pop()
      ?}
   ?};
   _tab:=sql('select OPIS from @EDOKLOG where EDOKLOG.EDOKUM=\':_a\'',$EDOKUM.ref);
   _temp:=0;
   _next:=1;
   {? _tab.first()
   || {! |?
      {? _tab.OPIS='Odrzucenie faktury'
      || _temp:=1;
         _next:=0
      ?};
      _tab.next() & _next
      !}
   ?};
   {? (__bool_em=0 & _upr) | (__bool_em=1 & _temp=0 & (_status<>1 & _status<>3 & _status<>5))
   || TABSPDOK.TYP_WP:=_status;
      TABSPDOK.ODD:=EDOKUM.ODD().OD;
      TABSPDOK.KH:=EDOKUM.KHKH().SKR;
      TABSPDOK.SYM:={? _typ=1 || EDOKUM.SYM || EDOKUM.ID ?};
      TABSPDOK.WAL:=EDOKUM.WAL().KOD;
      TABSPDOK.NETTO:=EDOKUM.NETTO;
      TABSPDOK.WART:=EDOKUM.WART;
      TABSPDOK.ROK:=EDOKUM.ROK_F().NAZ;
      TABSPDOK.DELEG:=EDOKUM.DOSTAWCA().NAZWISKO + ' '+EDOKUM.DOSTAWCA().PIERWSZE;
      TABSPDOK.DEL_MIE:=EDOKUM.DEL_MIE;
      TABSPDOK.REFEDOK:=$EDOKUM.ref();
      TABSPDOK.REFROK:=$EDOKUM.ROK_F;
      TABSPDOK.TYP_OB:=_typ;
      TABSPDOK.CZY_WAR:=EDOKUM.TYP().CZY_WAR;
      TABSPDOK.TYP:=EDOKUM.TYP().NAZWA;
      TABSPDOK.DOD:=$EDOKUM.DATA_OD;
      TABSPDOK.DDO:=$EDOKUM.DATA_DO;
      TABSPDOK.TP:=$EDOKUM.TP;
      TABSPDOK.DATAW:=$EDOKUM.DATAW;
      TABSPDOK.DS:=$EDOKUM.DOP;
      TABSPDOK.TR:=EDOKUM.TR;
      TABSPDOK.CEL_DEL:=EDOKUM.DEL_CEL().TR;
      {? Plugin.runnable('OB_MAIL_F_001')
      || Plugin.run('OB_MAIL_F_001')
      ?};
      TABSPDOK.add()
   ?};
   USERS.cntx_pop()
?}


\body
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [2008]
:: OPIS: Tresc wiadomosci e-mail wysylanej z zadania DOK_OB_SPR
::   WE: _a - zadeklarowane i rowne 1 - sprawdzanie wnioskow, rowne 3 - sprawdzanie delegacji
::  OLD: \body1/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('ML',@)<=100 || ML:=obj_new(@.CLASS.MEMO) ?};
{? var_pres('BODY')>0
|| BODY.erase()
|| BODY:=tab_tmp(1,'LP','INTEGER','Lp',
                   'TEKST','STRING[255]','Tekst')
?};
__LP:=0;
_add:="__LP+=1; BODY.LP:=__LP; BODY.TEKST:=_a; BODY.add()";
_ad:=obj_new(3);

_tekst:='<!DOCTYPE HTML PUBLIC \"-//W3C//DTD HTML 4.0 Final//EN\">'; _add(_tekst);
_tekst:='<html>'; _add(_tekst);
_tekst:='<head>'; _add(_tekst);
_tekst:='<meta http-equiv=\"content-type\" content=\"text/html; charset=iso-8859-2\">'; _add(_tekst);
_tekst:='</head>'; _add(_tekst);
_tekst:='<body>'; _add(_tekst);
_tekst:='<BR><BR>'; _add(_tekst);
_tekst:='<CENTER><H3>'+'Przypomnienie o wykonaniu akcji w obiegu'@+'</H3></CENTER>'; _add(_tekst);
_tekst:='<CENTER><H3><font color="black">'+REF.FIRMA().OPIS+'</font></H3></CENTER><BR><BR>'; _add(_tekst);

{? __bool_em=0 || exec('tab_mail','!zpr_snd_obge',1,'Faktury odrzucone do użytkownika wprowadzającego.'@,1)?};
exec('tab_mail','!zpr_snd_obge',2,'Faktury nieprzekazane dalej przez użytkownika wprowadzającego.'@,1);
{? __bool_em=0 || exec('tab_mail','!zpr_snd_obge',3,'Faktury odrzucone do użytkownika przekazującego.'@,1)?};
exec('tab_mail','!zpr_snd_obge',4,'Faktury nieprzekazane dalej przez użytkownika przekazującego.'@,1);
{? __bool_em=0 || exec('tab_mail','!zpr_snd_obge',5,'Faktury odrzucone do użytkownika akceptującego.'@,1) ?};
exec('tab_mail','!zpr_snd_obge',6,'Faktury niezaakceptowane przez użytkownika akceptującego.'@,1);
exec('tab_mail','!zpr_snd_obge',7,'Faktury niezadekretowane.'@,1);
{? __bool_em=1 || exec('tab_mail','!zpr_snd_obge',8,'Faktury odrzucone, które są niezaakceptowane'@,1)?};

exec('tab_mail','!zpr_snd_obge',1,'Wnioski odrzucone do użytkownika wprowadzającego.'@,2);
exec('tab_mail','!zpr_snd_obge',2,'Wnioski nieprzekazane dalej przez użytkownika wprowadzającego.'@,2);
exec('tab_mail','!zpr_snd_obge',3,'Wnioski odrzucone do użytkownika przekazującego.'@,2);
exec('tab_mail','!zpr_snd_obge',4,'Wnioski nieprzekazane dalej przez użytkownika przekazującego.'@,2);
exec('tab_mail','!zpr_snd_obge',5,'Wnioski odrzucone do użytkownika akceptującego.'@,2);
exec('tab_mail','!zpr_snd_obge',6,'Wnioski niezaakceptowane przez użytkownika akceptującego.'@,2);
exec('tab_mail','!zpr_snd_obge',7,'Wnioski niezadekretowane.'@,0,2);

exec('tab_mail','!zpr_snd_obge',1,'Delegacje odrzucone do użytkownika wprowadzającego.'@,3);
exec('tab_mail','!zpr_snd_obge',2,'Delegacje nieprzekazane dalej przez użytkownika wprowadzającego.'@,3);
exec('tab_mail','!zpr_snd_obge',3,'Delegacje odrzucone do użytkownika przekazującego.'@,3);
exec('tab_mail','!zpr_snd_obge',4,'Delegacje nieprzekazane dalej przez użytkownika przekazującego.'@,3);
exec('tab_mail','!zpr_snd_obge',5,'Delegacje odrzucone do użytkownika akceptującego.'@,3);
exec('tab_mail','!zpr_snd_obge',6,'Delegacje niezaakceptowane przez użytkownika akceptującego.'@,3);
exec('tab_mail','!zpr_snd_obge',7,'Delegacje niezadekretowane.'@,3);

exec('tab_zmail','!zpr_snd_obge',1,'Zaliczki odrzucone do użytkownika rejestrującego.'@);
exec('tab_zmail','!zpr_snd_obge',2,'Zaliczki niezaakceptowane przez użytkownika rejestrującego.'@);

_tekst:='</body>'; _add(_tekst);
_tekst:='</html>'; _add(_tekst);
obj_del(_ad); VAR_DEL.delete('__LP','ML');
BODY


:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dfwb.fml
:: Utworzony: 26.06.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DFWB - dekretowanie wyciągów
::======================================================================================================================


\fbdekrwb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.10]
:: OPIS: Formula dekretuje zaimportowane przelewy dla wybranego wyciagu bankowego i przenosi je do
::       archiwum. Uzupelnia w tabeli PW pola zgodnie z opisem: REJ - rejestr dokumentu księgowego;
::       NRD - symbol dokumentu.
::  OLD: \fbdekrwb/przelewy.fml
::----------------------------------------------------------------------------------------------------------------------
_wy:=null;
{? exec('czy_okr_zam_con','konsolidacja',1)
|| return()
|? SSTALE.AO().NR=0
|| FUN.info('Akcja niedostępna w okresie \'Bilans otwarcia\'.'@);
   return()
?};
_pwnrfordok:=-1;
_newdref:=null;
PW.cntx_psh();
{? PW.size()>0
|| PW.first();
   _pw_ref:=PWN.ref();
   REJ.prefix();
   DOK_REJ.prefix();
   DOK.prefix();
   DOK.DTO:=PW.DK;
   DOK.REJ:=DOK.DZ:=ROZNE.DOK_REJ:=null;
   DOK.NK:=_vnrwb:=PWN.NRW;
   _vrblwb:=PWN.RBL;
   DOK.TR:='wyciąg bankowy';
   DOK.DTW:=PW.DK;
   DOK.DOP:=PW.DK;
   KONTO.K1:='';
   czy_podp:=0;
   ODD.cntx_psh();
   ODD.index('ODDZIALY');
   ODD.prefix(REF.FIRMA);
   PWN.ODD();
   ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ');
   ROK_F.prefix(REF.FIRMA);
   SSTALE.AR();
   gokno:=REJ.win_dict('?');
   gpomakc:=POMOC.AKC;
   {? OPERATOR.DEPT=null
   || POMOC.AKC:='W' ;
      REJ.win_dict('SLO1_ODD')
   || POMOC.AKC:='O';
      REJ.win_dict('SLO1')
   ?};
   exec('gr_bufor','dok_fks');
   KONTO.K1:=KONTO.K1_BE:=KONTO.K1_AE:='';
   KONTO.K1_RODZ:=0;
:: Poczatek modyfikacji dla Maclex 13.10.2009 AK [1120]
:: Dodane uzupelnienie konta K1 na podstawie MLCONNEC
   __komt:='';
   {? MLEX.FIKSB
   || exec('uzkk1','ml_przel');
      exec('ini_kom','#message','Uwagi dekretacji wyciągu',,,,200)
   ?};
:: Koniec modyfikacji dla Maclex
   _vrbl:=RB.getrrban(PW.PWN().RBL,REF.INFO,0); vrbwal:=null; vrbwalk:='';
   {? _vrbl
   || {? SKID_RBK.clear(); SKID_RBK.seek(_vrbl)
      || vrbwal:=SKID_RBK.WAL; vrbwalk:=SKID_RBK.WAL().KOD
      ?}
   ?};
:: sprawdzenie czy na wyciągu są operacje w obcej walucie
   obca:=0;
:: PW.index('WB'); PW.prefix(_vnrwb, _vrblwb);
   PW.cntx_psh();
   {? PW.first()
   || {! |? {? +PW.WAL & PW.WAL<>FINFO.NAROD().KOD
            || obca:=1;
               0
            || PW.next()
            ?}
      !}
   ?};
   PW.cntx_pop();
:: jeśli waluta rachunku inna niż waluta narodowa lub występują na księgowanym wyciągu operacje w obcej walucie to wyświetlenie okienka z pytaniem o bank i tabelę kursów
   {? vrbwal<>FINFO.NAROD | obca
   || DOK.win_edit('IMPWBWAL');
      {? FINFO.B_WYDR<>null
           || exec('slo_slu2','slo_slu',FINFO.B_WYDR().SLU().NAZ,'ONE');
              POMOC.B:=FINFO.B_WYDR
      ?};
      DOK.TYPKRS:='R';
      DajTKurs:="POMOC.TKRS:=null;
                 TKRS.cntx_psh();
                 TKRS.index('TKRS_DT');
                 _dpp:=DOK.DOP;
                 _date:={? _dpp>=date(1,1,2)
                        || exec('dzienRob','kalendarz',_dpp-1,1,1,0,-1)
                        || date(0,0,0)
                        ?};
                 TKRS.prefix(POMOC.B,FINFO.NAROD,_date);
                 {? TKRS.first() || POMOC.TKRS:=TKRS.ref() || POMOC.TKRS:=null ?};
                 TKRS.cntx_pop()
                ";
      DajTKurs();
      DOK.fld_fml('TYPKRS','BEFORE_EDIT',"");
      DOK.fld_fml('DOP','AFTER_EDIT',"DajTKurs()");
      POMOC.fld_fml('B','AFTER_EDIT',"DajTKurs()")
   || DOK.win_edit('IMPWB');
      POMOC.B:=POMOC.TKRS:=null
   ?};
   _esc:=0;
   {? DOK.edit("_ret:=exec('chk_dok','!fks_dks_dfwb');
               {? _ret='' & (vrbwal<>FINFO.NAROD | obca)
               || {? POMOC.B=null
                  || _ask:=FUN.ask('Nie wskazano banku do wyznaczenia kursu dla zapłat za należności i zobowiązania.\n'+
                                   'Kwoty w walucie nie zostaną automatycznie przeliczone na walutę narodową.\n'+
                                   'Czy na pewno pozostawić pole puste?'@);
                     {? _ask=0 || _ret:='B' ?}
                  ?}
               ?};
               _ret")
   || {? vrbwal<>FINFO.NAROD | obca || DOK.fld_fml('TYPKRS','BEFORE_EDIT',"*") ?};
      _typkurs:=DOK.TYPKRS;
      {? ~PWN.seek(_pw_ref)
      || FUN.emsg('Nie znaleziono wyciągu bankowego.\nZostał on usunięty przez innego użytkownika.'@)
      |? PWN.lock(1)
      || _pwn_rbl:=PWN.RBL;
         _pwn_bl:=PWN.BL;
         _pwn_nr:=PWN.NRW;
         _pwn_odd:=PWN.ODD;
         &_vrbl;
         _vrej:=ROZNE.DOK_REJ;
         _vtr:=DOK.TR;
         _vnk:=DOK.NK;
         _vdto:=DOK.DTO;
         _vdtw:=DOK.DTW;
         _vdop:=DOK.DOP;
         DOK.blank(0);
         DOK.TYPKRS:=_typkurs;
         DOK.DOK_REJ:=_vrej;
    DOK.JPK_V_T:=exec('typ_dok','obiegi2');
         DOK.TR:=_vtr;
         DOK.cntx_psh();
         DOK.index('REJ');
         DOK.prefix(DOK.REJ);
         {? DOK.last() || _vnr:=DOK.NR+1 || _vnr:=1 ?};
         DOK.cntx_pop();
         DOK.DR:=date();
         DOK.NR:=_vnr;
         sourcedk:='HBN';
         DOK.ZAR:=exec('usr_zar','dok_fks');
         DOK.NK:=_vnk;
         DOK.A:=DOK.ZK:=DOK.ZP:=DOK.WSK_XD:='N';
         DOK.DTW:=_vdtw;
         DOK.DOP:=_vdop;
         DOK.DTO:=_vdto;
         DOK.ODD:=DOK.REJ().ODD;
         DOK.RVAT:=null;
         DOK.DOKZRODL:='pw'+(4+$DOK.DTW)+$(#PW.ref());
         {? (vrbwal<>FINFO.NAROD | obca)
         || DOK.BANK:=POMOC.B;
            DOK.TKRS:=POMOC.TKRS;
:: 20211103 podpowiedz waluty na podstawie rachunku bankowego
            {? vrbwal<>null()
            || DOK.WAL:=vrbwal
            ?}
         ?};
         do();
         {? DOK.add()
         || _newdref:=DOK.ref();
            exec('pop_poz','dok_fks','DOK',0);
            _vcont:=_vsumwn:=_vsumma:=0;
            PW.index('PWN');
            PW.prefix(PWN.ref());
:: Poczatek modyfikacji dla Maclex 13.10.2009 AK [1120]
            {? MLEX.FIKSB || exec('ml_pwbl','ml_przel') ?};
:: Koniec modyfikacji Maclex
            {? PW.first()
            || _lp:=0; _ile_pw:=PW.size;
               exec('tt_wbpoz','!fks_dks_dfwb');
               {! |?
                  _lp+=1;
                  PW_OP.prefix(PW.ref());
                  {? MLEX.FIKSB
                  || exec('ml_pwdan','ml_przel')
                  ?};
                  _licz:=0; _vsum_op:=0;
:: dla przelewu zbiorczego
                  {? PW.SYM='' & PW_OP.first()
                  || {! |?
                        _licz+=1;
                        OP.index('KON_O');
                        OP.prefix(FINFO.NAROD,PW_OP.ODD,PW_OP.AN,PW_OP.SYM,PW_OP.SYM);
                        {? OP.first() & OP.size()=1
                        || _vtroz:=OP.TYP;
                           _vdoroz:=OP.DO;
                           _vtzroz:=OP.TZ;
:: było                           _vopis:=PW_OP.TYT;
                           _vopis:=OP.OP;
                           {? _vopis='' || _vopis:=PW_OP.TYT ?};
                           _vsym:=PW_OP.SYM;
                           _vodd:=PW_OP.ODD
                        || _vtroz:={? PW.STR='Wn' || 'ZOB' || 'NAL' ?};
                           _vdoroz:=_vtzroz:=DOK.DTW;
                           _vopis:=PW_OP.TYT;
                           {? _vopis='' || _vopis:=PW.TYPTRAN ?};
                           _vsym:='';
                           {? DOK.DOK_REJ().ODD_ROZR='T' || _vodd:=PW.ODD || _vodd:=DOK.ODD ?}
                        ?};
:: jesli rachunek bankowy jest prowadzony w walucie obcej
                        {? vrbwal<>FINFO.NAROD
                        || _vwal:=vrbwalk;
:: jezeli transakcja jest w walucie rachunku bankowego - w walucie obcej
                           {? ~+PW.WAL | PW.WAL=vrbwalk
                           || _vsumw:=PW_OP.K
:: jezeli transakcja jest w walucie innej niz waluta rachunku bankowego - innej niz obca rachunku
                           || _vsumw:={? PW.KURS || (PW_OP.K*(PW.KW*PW.KURS/PW.KW_WAL)/PW.KURS)$2 || 0 ?}
                           ?};
                           _vkurs:=exec('kurs','!fks_dks_dfwb',_vwal,PW.DK);
                           _vjedn:=exec('jed_wal','waluty',_vwal);
                           _vsum:={? _vjedn<>0 || (_vsumw*_vkurs/_vjedn)$2 || 0 ?};
                           {? PW.WAL<>''
                           || _vopis+=' //transakcja: '+$PW_OP.K+' '+PW.WAL
                           ?}
:: jesli rachunek bankowy jest prowadzony w walucie narodowej
                        || _vwal:=PW.WAL;
:: jezeli transakcja jest w walucie rachunku bankowego
                           {? ~+PW.WAL | PW.WAL=FINFO.NAROD().KOD
                           || _vsumw:=_vkurs:=0;
                              _vsum:=PW_OP.K
:: jezeli transakcja jest w walucie innej niz waluta rachunku bankowego - innej niz narodowa
                           || _vsumw:=PW_OP.K;
                              _vkurs:=exec('kurs','!fks_dks_dfwb',_vwal,PW.DK);
                              _vjedn:=exec('jed_wal','waluty',_vwal);
                              _vsum:={? _vkurs || (PW_OP.K*_vjedn/_vkurs)$2 || 0 ?}
                           ?}
                        ?};
:: NUCO zmiana opisu dla konta bankowego oraz zmiana lp
                        _lp+=1;
                        exec('tt_add','!fks_dks_dfwb',_vsum,PW.STR,PW_OP.AN,_vsym,_vtroz,
                           _vdoroz,_vtzroz,_vopis,_vwal,_vsumw,_vkurs,PW.DK,#_vodd,PW.WSK_R,PW.ref(),_lp,PW_OP.SP_V);
                        _vsum_op+=_vsum;
:: NUCO zmiana opisu dla konta bankowego oraz zmiana lp
                        {? (MLEX.FIKSB & _licz=1) | ~MLEX.FIKSB
                        || _lp+=1;
                           exec('tt_add','!fks_dks_dfwb',_vsum,{? PW.STR='Ma' || 'Wn' || 'Ma' ?},KONTO.K1,
                                {? MLEX.FIKSB || _vsym || '' ?},'',date(0,0,0),date(0,0,0),
                                {? exec('get_par','#parametr',45)='T'
                                || _vopis
                                || {? PW.STR='Ma' || 'wpłata' || 'wypłata' ?};_vopis
                                ?},_vwal,_vsumw,_vkurs,date(0,0,0),0,{? MLEX.FIKSB || PW.WSK_R || 'T' ?},PW.ref(),{? exec('get_par','#parametr',45)='T'  || _lp+=1 || _ile_pw+2;_lp ?})
                        ?};
                        PW_OP.next()
                     !};
                     {? _vsum_op<PW.KW
                     || _vsum:=PW.KW-_vsum_op;
:: NUCO zmiana opisu dla konta bankowego oraz zmiana lp
                        _lp+=1;
                        exec('tt_add','!fks_dks_dfwb',_vsum,PW.STR,'','','',
                              date(0,0,0),date(0,0,0),_vopis,_vwal,0,0,date(0,0,0),0,'T',PW.ref(),_lp);
                        {? (MLEX.FIKSB & _licz=1) | ~MLEX.FIKSB
                        || _lp+=1;
                            exec('tt_add','!fks_dks_dfwb',_vsum,{? PW.STR='Ma' || 'Wn' || 'Ma' ?},KONTO.K1,
                                {? MLEX.FIKSB || _vsym || '' ?},'',date(0,0,0),date(0,0,0),
                                {? exec('get_par','#parametr',45)='T'
                                || _vopis
                                || {? PW.STR='Ma' || 'wpłata' || 'wypłata' ?};_vopis
                                ?},_vwal,0,0,date(0,0,0),0,{? MLEX.FIKSB || PW.WSK_R || 'T' ?},PW.ref(),{? exec('get_par','#parametr',45)='T'  || _lp+=1 || _ile_pw+2;_lp ?})
                        ?}
                     ?};
                     {? PW.STR='Wn' || _vsumwn+=PW.KW || _vsumma+=PW.KW ?};
                     PW.use('pw'+(4+$DOK.DTW));
                     PWN.use('pn'+(4+$DOK.DTW));
                     PW.prefix();
                     PWN.index('PWN');
                     PWN.prefix(_pwn_bl,_pwn_rbl,_pwn_nr,);
                     {? ~PWN.first
                     || PWN.BL:=_pwn_bl;
                        PWN.ODD:=_pwn_odd;
                        PWN.RBL:=exec('rb_nosp','#string',_pwn_rbl);
                        PWN.NRW:=_pwn_nr;
                        PWN.PLIK:=PW.PLIK;
                        PWN.STATUS:='T';
                        PWN.add()
                     ?};
                     PW.PWN:=PWN.ref();
                     PW.REJ:=DOK.REJ;
                     PW.NRD:=$DOK.NR+'/'+$(DOK.DTW~2);
                     PW.add();
                     _pwnrfordok:=#PW.ref();
                     PW_OP.first();
                     {! |?
                        PW_OP.use('pwp'+(4+$DOK.DTW));
                        PW_OP.prefix();
                        PW_OP.PW:=PW.ref();
                        PW_OP.add();
                        PW_OP.use('pwpxxxx');
                        PW_OP.del()
                     !}
:: dla przelewu pojedynczego
                  || OP.index('KON_O');
                     OP.prefix(FINFO.NAROD,PW.ODD,PW.AN,PW.SYM,PW.SYM);
                     {? PW.TYPTRAN<>''
                     || _vtyptr:={? exec('findttr','hbn') & TYPYTRAN.ROZR='T' || 1 || 0 ?}
                     || _vtyptr:=0
                     ?};
                     {? OP.first() & OP.size()=1
                     || _vtroz:=OP.TYP;
                        _vdoroz:=OP.DO;
                        _vtzroz:=OP.TZ;
:: NUCO 20201215 zmiana opisu  było: _vopis:=PW.TYPTRAN;
                        _vopis:=OP.OP;
                        {? _vopis='' || _vopis:=PW.TYTOP ?};
                        _vsym:=PW.SYM;
                        _vodd:=OP.ODD
                     || _vtroz:={? PW.STR='Wn' || 'ZOB' || 'NAL' ?};
                        {? _vtyptr || _vdoroz:=_vtzroz:=DOK.DTW || _vdoroz:=_vtzroz:=date(0,0,0) ?};
                        _vopis:=PW.TYTOP;
                        {? _vopis='' || _vopis:=PW.TYPTRAN ?};
                        _vsym:=PW.SYM;
                        {? DOK.DOK_REJ().ODD_ROZR='T' || _vodd:=PW.ODD || _vodd:=DOK.ODD ?}
                     ?};
                     {? PW.WSK_R<>'T' & PW.TYPTRAN<>''
                     || {? exec('findttr','hbn') & TYPYTRAN.ROZR='T' & ~+_vsym || _vsym:=PW.TYTOP ?}
                     ?};
:: jesli rachunek bankowy jest prowadzony w walucie obcej
                     {? vrbwal<>FINFO.NAROD
                     || _vwal:=vrbwalk;
                        _vsumw:=PW.KW;
                        {? +PW.WAL & PW.KW_WAL
                        || _vopis+=' //transakcja: '+$PW.KW_WAL+' '+PW.WAL
                        ?};
                        _vkurs:=exec('kurs','!fks_dks_dfwb', _vwal, PW.DK);
                        _vjedn:=exec('jed_wal','waluty',_vwal);
                        _vsum:={? _vjedn<>0 || (_vsumw*_vkurs/_vjedn)$2 || 0 ?}
:: jesli rachunek bankowy jest prowadzony w walucie narodowej
                     || _vwal:=PW.WAL;
                        _vsumw:=PW.KW_WAL;
                        _vkurs:=PW.KURS;
                        _vsum:=PW.KW
                     ?};
                     exec('tt_add','!fks_dks_dfwb',_vsum,PW.STR,PW.AN,_vsym,_vtroz,
                        _vdoroz,_vtzroz,_vopis,_vwal,_vsumw,_vkurs,PW.DK,#_vodd,PW.WSK_R,PW.ref(),_lp,
                        {? PW.SP='T' || PW.SP_V || ~~ ?}
                     );
:: NUCO zmiana opisu dla konta bankowego oraz zmiana lp
                     _lp+=1;
                     exec('tt_add','!fks_dks_dfwb',_vsum,{? PW.STR='Ma' || 'Wn' || 'Ma' ?},KONTO.K1,{? MLEX.FIKSB || _vsym || '' ?},'',
                        date(0,0,0),date(0,0,0),{? exec('get_par','#parametr',45)='T' || _vopis || {? PW.STR='Ma' || 'wpłata' || 'wypłata' ?};_vopis ?},
                        _vwal,_vsumw,_vkurs,date(0,0,0),0,{? MLEX.FIKSB || PW.WSK_R || 'T' ?},PW.ref(),{? exec('get_par','#parametr',45)='T'  || _lp+=1 || _ile_pw+2;_lp ?});
                     {? PW.STR='Wn' || _vsumwn+=PW.KW || _vsumma+=PW.KW ?};
                     PW.use('pw'+(4+$DOK.DTW));
                     PWN.use('pn'+(4+$DOK.DTW));
                     PW.prefix();
                     PWN.index('PWN');
                     PWN.prefix(_pwn_bl,_pwn_rbl,_pwn_nr,);
                     {? ~PWN.first
                     || PWN.BL:=_pwn_bl;
                        PWN.ODD:=_pwn_odd;
                        PWN.RBL:=exec('rb_nosp','#string',_pwn_rbl);
                        PWN.NRW:=_pwn_nr;
                        PWN.PLIK:=PW.PLIK;
                        PWN.STATUS:='T';
                        PWN.add()
                     ?};
                     PW.PWN:=PWN.ref();
                     PW.REJ:=DOK.REJ;
                     PW.NRD:=$DOK.NR+'/'+$(DOK.DTW~2);
                     PW.add();
                     _pwnrfordok:=#PW.ref();
                     {? PW_OP.first()
                     || {! |?
                           PW_OP.use('pwp'+(4+$DOK.DTW));
                           PW_OP.prefix();
                           PW_OP.PW:=PW.ref();
                           PW_OP.add();
                           PW_OP.use('pwpxxxx');
                           PW_OP.del()
                        !}
                     ?}
                  ?};
                  PW.use('pwxxxx');
                  PWN.use('pnxxxx');
                  _vcont:=PW.del();
                  _vcont
               !};
::Poczatek modyfikacji dla Maclex 13.10.2009 AK [1120]
::          exec('tt2poz','przelewy')
               {? MLEX.FIKSB
               ||   exec('tt2poz','ml_przel')
               ||   exec('tt2poz','!fks_dks_dfwb')
               ?}
::Koniec modyfikacji Maclex
            ?}
         ?};
         DOK.cntx_psh(); DOK.prefix();
         {? _pwnrfordok>0 & _newdref<>null & DOK.seek(_newdref)
         || DOK.DOKZRODL:='pw'+(4+$DOK.DTW)+$(_pwnrfordok);
            DOK.put(1)
         ?};
         DOK.cntx_pop();
         {?  do_state()=1 || _wy:=DOK.ref() ?};
         end();
         PWN.unlock();
         PW.index('PWN');
         PW.prefix(PWN.ref());
         {? PW.size()=0 & PWN.count()=0 || PWN.del() ?}
      || {? _cont
         || FUN.emsg('Dekretowanie niemożliwe!\nWyciąg jest obsługiwany przez innego użytkownika.'@)
         ?}
      ?}
   || _esc:=1
   ?};
   {? MLEX.FIKSB
   || exec('end_kom','#message')
   ?};
   ODD.cntx_pop();
   ROK_F.cntx_pop();
   POMOC.AKC:=gpomakc;
   REJ.win_dict(gokno);
   VAR_DEL.delete('pamietaj','czy_podp','gokno','gpomakc', 'vrbwal', 'vrbwalk', 'obca','__komt');
   {? PWN.f_active()
   || PWN.f_rfresh();
      {? PWN.f_first()
      || PW.index('PWN');
         PW.prefix(PWN.ref())
      ?};
      {? _esc || PWN.f_seek(_pw_ref) ?}
   ?}
?};
DOK.fld_fml('TYPKRS','BEFORE_EDIT',"*");
DOK.fld_fml('DOP','AFTER_EDIT',"*");
POMOC.fld_fml('B','AFTER_EDIT',"*");
VAR_DEL.delete('__TWYC');
PW.cntx_pop();
_wy


\tt_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2008]
:: OPIS: Tworzy tabele grupujaca dekrety powstale z wyciagow bankowych
::       Przy parametrze 45='T' grupuje dekrety po opisie
::   WE: _a - kwota
::       _b - strona
::       _c - symbol konta
::       _d - symbol rozrachunku
::       _e - typ rozrachunku
::       _f - data otwarcia rozrachunku
::       _g - termin platnosci rozrachunku
::       _h - opis
::       _i - kod waluty
::       _j - kwota w walucie
::       _k - kurs
::       _l - data rozliczenia rozrachunku
::       _m - nr rekordu oddzialu
::       _n - znacznik rozpoznania
::       _o - ref do PW dla potrzeb FIKSB
::       _p - nr kolejny
::       _q - VAT z podzielnej płatności
::  OLD: \tt_add/przelewy.fml
::----------------------------------------------------------------------------------------------------------------------
{? _l<>date(0,0,0)
|| TT_WBPOZ.index(__ittdo); TT_WBPOZ.clear();
   {? exec('get_par','#parametr',45)='T'
   || _find:=TT_WBPOZ.find_key(_c,_b,_d,_k,_m,_n,_l,_h)
   || _find:=TT_WBPOZ.find_key(_c,_b,_d,_k,_m,_n,_l)
   ?}
|| TT_WBPOZ.index(__ittod); TT_WBPOZ.clear();
   {? exec('get_par','#parametr',45)='T'
   || _find:=TT_WBPOZ.find_key(_c,_b,_d,_k,_m,_n,_h)
   || _find:=TT_WBPOZ.find_key(_c,_b,_d,_k,_m,_n)
   ?}
?};
:: NUCO 20190426 Z/001064/0028/19 brak grupowania zapisów
_find:=0;
:: koniec zmian
{? _n='T' & _find
|| TT_WBPOZ.KW+=_a;
   TT_WBPOZ.KW_WAL+=_j;
   {? var_press('_q')>0 || TT_WBPOZ.SP_V+=_q ?};
   TT_WBPOZ.put()
|| TT_WBPOZ.KW:=_a;
   TT_WBPOZ.STR:=_b;
   TT_WBPOZ.KONTO:=_c;
   TT_WBPOZ.R_SYM:=_d;
   TT_WBPOZ.R_TYP:=_e;
   TT_WBPOZ.DO:=_f;
   TT_WBPOZ.TP:=_g;
   TT_WBPOZ.OP:=_h;
   TT_WBPOZ.WAL:=_i;
   TT_WBPOZ.KW_WAL:=_j;
   TT_WBPOZ.KURS:=_k;
   TT_WBPOZ.DR:=_l;
   TT_WBPOZ.ODD:=_m;
   TT_WBPOZ.ROZ:=_n;
   TT_WBPOZ.PW:=_o;
   TT_WBPOZ.LP:=_p;
   TT_WBPOZ.SP_V:={? var_press('_q')>0 || _q ?};
   TT_WBPOZ.add()
?}



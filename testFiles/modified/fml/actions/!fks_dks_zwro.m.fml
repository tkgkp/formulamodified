:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_zwro.fml
:: Utworzony: 27.08.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_ZWRO - Analizy zapisów wyróżników
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Analizy zapisów wyróżników - główna formuła czynności FKS_DKS_ZWRO.
::       Wyswietla zapisy księgowe z wyróżnikami według słowników
::  OLD: \sl_wyr/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
{? exec('czy_wyroz','wyrozniki','Wyróżniki')
|| SLUAPPL.index('NAZ');
   SLUAPPL.prefix('F');
   SLO.index('SL');
   SLU.index('NAZ');
   exec('tab_s','wyrozniki');
   SSTALE.AR();
   _fpow:='\'pow_'+ROK_F.KOD+'*\'';
   _fpoz:='\'pozy'+ROK_F.KOD+'*\'';
   _fdok:='\'doku'+ROK_F.KOD+'*\'';
   _dept:=OPERATOR.DEPT;
   {? var_pres('WYR')>0 || obj_del(WYR) ?};
:WW: modyfikacja zapytania SQL (dodanie pól KH_KOD i KH_FULL do zapytania + złączenia do tabel)
:PS: wybór zakresu
   WYR:=sql('select '+
   '/*+MASK_FILTER(POW,:_a) MASK_FILTER(POZ,:_b) MASK_FILTER(DOK,:_c) '+
   'INDEX (POW,POZ) */ '+
   'SLU.NAZ, SLO.KOD, SLO.TR, POZ.KON, DOK.DTW, DOK.DOP, DOK.NK, POW.KW, TK.KOD as KH_KOD, DOK.KH_FULL,'+
   'case when POZ.STR=\'Wn\' then POW.KW else 0 end as WN,'+
   'case when POZ.STR=\'Ma\' then POW.KW else 0 end as MA,'+
   'POZ.OP, ODD.OD as OD, REJ.KOD as REJ, DOK.NR, KS.SYM as KS_SYM, KS.NAZ as KS_NAZ, POZ.POZ, POZ.REFERENCE as REF '+
   'from @POW join SLO using (POW.SLO, SLO.REFERENCE) '+
   'join SLUAPPL using (POW.SLU, SLUAPPL.REFERENCE) '+
   'join SLU using (SLUAPPL.SLU, SLU.REFERENCE) '+
   'join @POZ using (POW.POZ, POZ.REFERENCE) '+
   'join KOM using (POZ.KOM,KOM.REFERENCE) '+
   'join KS using (KOM.KS,KS.REFERENCE) '+
   'join @DOK using (POZ.DOK, DOK.REFERENCE) '+
   'left join SLO as TK using (DOK.WYS,TK.REFERENCE) '+
   'join ODD using (DOK.ODD, ODD.REFERENCE) '+
   'join REJ using (DOK.REJ,REJ.REFERENCE) '+
   'where POZ.ZP=''T'' '+
   {? OPERATOR.DEPT
   || ' AND ODD.OD='+''''+OPERATOR.DEPT().OD+''''+' '
   || ''
   ?}+
   {? FUN.ask('Analityka dla bieżącego roku czy bieżącego miesiąca?','Zakres analizy',0,'Rok','Miesiąc')
   ||' AND POZ.REFERENCE like \'pozy'+ROK_F.KOD+form(SSTALE.AO().NR,-2)+'%\' '
   ||' AND POZ.REFERENCE<\'pozy'+ROK_F.KOD+form(SSTALE.AO().NR+1,-2)+'\' '
   ?}+
   'order by SLU.NAZ, SLO.KOD, POZ.KON, DOK.DTW',_fpow,_fpoz,_fdok);
   _wer:=WYR.mk_sel('Zapisy z wyróżnikami'@,'P',,'wyr_wer',,,,,'U');
   WYR.win_fld(_wer,,'OD',,,6,,,'Jednostka księgowa'@);
   WYR.win_fld(_wer,,'REJ',,,8,,,'Rejestr'@);
   WYR.win_fld(_wer,,'NR',,,5,,,'Nr'@);
   WYR.win_fld(_wer,,'POZ',,,5,,,'Pozycja'@);
   WYR.win_fld(_wer,,'DTW',,,5,,,'Data zapisu'@);
   WYR.win_fld(_wer,,'KON',,,20,,,'Konto'@);
   WYR.win_fld(_wer,,'OP',,,20,,,'Opis'@);
   WYR.win_fld(_wer,,'KW',,,16,2,,'Kwota'@);
:WW: dodanie pól do okienka
   WYR.win_fld(_wer,,'KH_KOD',,,1,,,'Kod kontrahenta'@);
   WYR.win_fld(_wer,,'KH_FULL',,,1,,,'Nazwa kontrahenta'@);
:: Koniec
   WYR.win_act(_wer,,'Formuła','Zestawienia dla słownika'@@,,,,"
      SLO.cntx_psh();
      SLO.win_sel('ONE_SEL');
      exec('rep_exec','#b_report','FKS_DKS_ZWRO','fks_zap_w*',,1);
      SLO.cntx_pop()
   ",,,,,'Z');
   WYR.win_act(_wer,,'Szukaj');
   WYR.win_act(_wer,,'Kolejność');
   WYR.win_sel(_wer);
   _ed:=WYR.mk_edit('Zapis'@,0,'wyr_edit');
   WYR.win_efld(_ed,,'KON',,,45,,,'Konto'@);
   WYR.win_efld(_ed,,'DTW',,,42,,,'Data zapisu'@);
   WYR.win_efld(_ed,,'OD',,,45,,,'Jednostka księgowa'@);
   WYR.win_efld(_ed,,'REJ',,,45,,,'Rejestr'@);
   WYR.win_efld(_ed,,'NR',,,45,,,'Nr'@);
   WYR.win_efld(_ed,,'KW',,,45,2,,'Wn'@);
   WYR.win_efld(_ed,,'OP',,,45,,,'Opis'@);
   _btnzap:=WYR.win_ebtn(_ed,'text=%1'['Zapi&sz'@],'key:F2');
   _btnan:=WYR.win_ebtn(_ed,'text=%1'['Anuluj'@],'key:Esc');
   WYR.btn_eopt(_ed,_btnzap,'tooltip='+exec('help_red_ok','#window','Z'));
   WYR.btn_eopt(_ed,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
   WYR.win_edit(_ed);
   _wer_slo:=SLO.mk_sel('Pozycje słownika'@,'P',,'slo_wer2',,,,,'U');
   SLO.win_fld(_wer_slo,,'KOD',,,8,,,'Kod'@);
   SLO.win_fld(_wer_slo,,'TR',,,30,,,'Treść'@);
   SLO.win_sel(_wer_slo);
   SLO.win_act(_wer_slo,,'Rekord',,,,"
      {? WYR.prefix(SLU.NAZ,SLO.KOD); WYR.first
      || exec('findtmp','#color')
      ?}
   ");
   SLO.win_act(_wer_slo,0,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,,"
      exec('legenda','color','$Wyróżniki z wprowadzonymi zapisami'@)
   ",,,,,'L');
   SLO.win_act(_wer_slo,,'Szukaj');
   SLO.win_act(_wer_slo,,'Kolejność');
   _gr:=SLO.grp_make('Analiza wyróżników ze słownika: '@,,'#slowyrana');
   SLO.grp_sel(_gr,,_wer_slo,,"
      WYR.prefix(SLU.NAZ,SLO.KOD);
      WYR.first();
      grp_disp(WYR,WYR.win_sel('?'))
   ",,,,,,,,'maximized_with_title');
   SLO.grp_splt(_gr,,'vertical','left');
   SLO.grp_sel(_gr,WYR,_wer,,,,,,,,,,'maximized_with_title');
   SLO.win_sel(_gr);
   TAB_S.first();
   {!
   |? {? TAB_S.select(,1)
      || SLU.prefix(TAB_S.NAZ);
         {? SLU.first() & SLU.NAZ=TAB_S.NAZ & SLUAPPL.find_key(SLU.NAZ)
         || SLO.prefix(SLU.ref);
            SLO.hdr_sel(); SLO.hdr_sel(SLU.NAZ);
            SLO.select(,1)
         ?};
         1
      ?}
   !};
   VAR_DEL.delete('WYR','TAB_S');
   SLO.hdr_sel()
?}



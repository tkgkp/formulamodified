:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !hbn_prz_zbpr.fml
:: Utworzony: 15.02.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności HBN_PRZ_ZBPR - Przegląd - archiwum przelewów
::======================================================================================================================


\przywr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DPU
:: OPIS: Przywrócenie przelewu do ponownego wyslania
::  OLD: \przywr/homebank.fml
:: zmiana daty utworzenia w przypadku przywracania przelewu
::----------------------------------------------------------------------------------------------------------------------
PB.cntx_psh();
_dalej:=0;
{? PB.PR='A'
|| {? PB.sel_size()=0
   || {? ~FUN.ask('Ten przelew był już przywracany i jest oznaczony jako nieudany!\n'
                  'Przywrócić ponownie? '
                  '(zostanie stworzona kolejna kopia\nprzelewu do ponownego wysłania)'@)
      || _dalej:=-1
      || _dalej:=1
      ?}
   ?}
?};
{? _dalej<>-1
|| {? PB.sel_size()>0 | _dalej=1 | FUN.ask('Stworzyć kopie przelewu do ponownego wysłania?\n'
                                           'Bieżący przelew zostanie oznaczony jako nieudany.'@)
   || SKID_RBK.cntx_psh();
      SKID_RBK.prefix();
      _v:=PB.RD;
      {? +form(3+_v)=2 & +form(12+_v)=11 || _v:=3-_v ?};
      {? (9+_v)+1=' ' | (9+_v)+1='-' || _v:=9-_v ?};
      SKID_RBK.index('TAB');
      SKID_RBK.prefix(null,REF.INFO,REF.INFO,0,0,_v);
      {? SKID_RBK.size()=0
      || FUN.emsg('Utworzenie kopii przelewu na nieistniejący rachunek jest niemożliwe!'@)
      || PB.PR:='N';
         {? XINFO.REST_ACC<>'' || PB.ZT:=XINFO.REST_ACC ?};
         PB.EMAIL:='';
:: Nuco Z/03830/0222/23 przy przywracaniu ustawienie daty generowania na bieżącą oraz uaktualnienie danych usera
         _old_dz:=PB.DZ;
         _old_user:=PB.USER_GEN;
         PB.DZ:=date();
         PB.USER_GEN:=exec('name','users');
         PB.ZT:='N';
         _old_tyt:=PB.TYT;
         _poz:=PB.TYT*' TMS ';
         {? _poz || PB.TYT:=form((_poz-1)+PB.TYT) ?};
         _vref:=PB.ref();
         PB.use('pbxxxx');
         PB.prefix();
         {? PB.add()
         || {? var_pres('g_tot')>0 || g_cnt+=1 ?};
            PB_OP.index('PB');
            PB_OP.prefix(_vref);
            {? PB_OP.first()
            || {! |?
                  PB_OP.PB:=PB.ref();
                  PB_OP.cntx_psh();
                  PB_OP.use('popxxxx');
                  PB_OP.prefix();
                  PB_OP.add();
                  exec('upd_pb_zal','zaliczki');
                  PB_OP.cntx_pop();
                  PB_OP.next()
               !}
            ?}
         ?};
         PB.use('pb'+TT_ROK.ROK);
         PB.PR:='A';
         PB.TYT:=_old_tyt;
:: Nuco Z/03830/0222/23 przy przywracaniu ustawienie daty generowania na bieżącą oraz uaktualnienie danych usera
         PB.DZ:=_old_dz;
         PB.USER_GEN:=_old_user;
         PB.ZT:='T';
         {? PB.put()
         || PBHIST.index('PBHIST');
            PBHIST.prefix();
            PBHIST.blank();
            PBHIST.KTO:='PROCES: '+app_info('app_user');
            PBHIST.PB:=PB.ref();
            PBHIST.add()
         ?}
      ?};
      SKID_RBK.cntx_pop()
   ?}
?};
PB.cntx_pop()


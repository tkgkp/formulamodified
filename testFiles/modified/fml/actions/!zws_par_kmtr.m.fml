:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_kmtr.fml
:: Utworzony: 02.03.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Kartoteka materiałów i usług
::======================================================================================================================


\m_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Materiał/Usługa - Dołącz
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
:: Poprawka ze względu na błąd związany z dodawaniem indeksu - już istniejący indeks
DISP.KTM:='';

_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_PAR_KMTR';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';

exec('mp_run','#b__box',_params)


\mat_powiel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Powiela biezacy rekord tabeli M i udostepnia do redakcji (jak dla akcji Dolacz, ale z wypelnionym buforem)
::  OLD: \mat_powiel/magkop.fml
::----------------------------------------------------------------------------------------------------------------------
exec('m_win_edit','material');
{? exec('pr_dolm','!zws_par_kmtr')
||
:: UWAGA: jezeli nie chcesz, zeby pojawialy sie dane intrastat dla nowego rekordu -
::        wyczysc albo zmien zawartosc pola M.KTM, np. dopisz:  M.KTM:='';
:: NUCO - PeKa - przy kopiowaniu indeksu czyscze pole kod kreskowy - uzupelnione zostanie we wtyczce na walidacji
   M.KODK:='';
   BEER.BADSEH:=exec('FindInSet','#table','M_DOD','M_DOD',M.ref(),exec('firma','ustawienia'),"M_DOD.BADSEH",,,null());
   BEER.BNFTT:=exec('FindInSet','#table','M_DOD','M_DOD',M.ref(),exec('firma','ustawienia'),"M_DOD.BNFTT",,,null());
   BEER.KK:=exec('FindInSet','#table','M_DOD','M_DOD',M.ref(),exec('firma','ustawienia'),"M_DOD.KK",,,null());
   VAR.M_POWIEL:=1;
   _src:=M.IDADD;
   _ref:=M.ref();
   M.hdr_edit(' — '+'kopia'@);
   M.CZY_CON:=exec('bl_m_czy_con','!zws_par_kmtr');
   M.C_SYM:=M.C_OPIS:='';
   M.CPV:=M.POZ_BUD:=null();
   M.IDMOB:='N';
   M.GOMOB:='N';
   M.IDZP:=0;
   M.FOMOB:=null();
   M.ABSTORE:='';
   M.ABSTOREC:='';
   ATR.M_ATR_B:=M.M_ATR_B;
   _valid:="_result:=exec('chk_m','!zws_par_kmtr');
            {? _result=''
            || _result:=Plugin.run('M_VALID_001','ADD',0)
            ?};
            _result";
   VAR.DEFSTATS:=exec('statsM','statexam',M.ref());
   exec('ustawJMwym','jm');
   {? M.edit(_valid)
   || M.COPY_SRC:=_src;
      {? M.add()
      || exec('stats_akt','!zws_par_kmtr',M.ref(),VAR.DEFSTATS);
         {? M.J2<>null() || exec('act_mjm','jm',M.ref(),M.J2,M.WS2,null()) ?};
         exec('po_dolm','!zws_par_kmtr');
         exec('m_sv_copy','!zws_par_kmtr',_ref,M.ref())
      ?}
   ?};
   VAR.M_POWIEL:=0
?};
~~



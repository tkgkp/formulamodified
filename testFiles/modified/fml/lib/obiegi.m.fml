:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obiegi.fml
:: Utworzony: 15.02.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa obiegów dokumentów
::======================================================================================================================


\aekwdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2008]
:: OPIS: Po redakcji pole EDOKUM.WART i EDOKUM.NETTO
::  OLD: \aekwdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| _fld:=web_top_fld_acr();
   {? _fld='NETTO'
   || EDOKUM.NETTO:=EDOKUM.NETTO$2
   || EDOKUM.WART:=EDOKUM.WART$2
   ?}
|| fld(fld()$2); _fld:=cur_afld()
?};
EDOKUM.TYP();
{? typobi=1
|| {? _fld='WART' & ETYPY.TYPVATPR || EDOKUM.NETTO:=EDOKUM.WART ?};

:: NUCO - zmiana - podpowiadanie wartości netto z brutto i brutto z netto
::                 w przypadku gdy pola są niewypełnione, i waluta jest różna od narodowej
   {? EDOKUM.WAL().KOD<>'PLN'
   || {? _fld='NETTO' & EDOKUM.WART$2=0
      || EDOKUM.WART:=EDOKUM.NETTO
      |? _fld='WART' & EDOKUM.NETTO$2=0
      || EDOKUM.NETTO:=EDOKUM.WART
      ?}
   ?}
:: Koniec zmiany

|? typobi=2
|| {? ETYPY.OBIEGKON=2
   || {? _fld='WART' || EDOKUM.NETTO:=EDOKUM.WART ?}
   || {? _fld='NETTO' & (ETYPY.CZY_WAR=2 | EDOKUM.WART$2=0)
      || EDOKUM.WART:=EDOKUM.NETTO
      |? _fld='WART' & (ETYPY.CZY_WAR=1 | EDOKUM.NETTO$2=0)
      || EDOKUM.NETTO:=EDOKUM.WART
      ?}
   ?};
   {? ETYPY.CZY_ZALP='T' & ETYPY.ETYPZAL
   || EDOKUM.NETTO:=EDOKUM.WART
   ?}
?}; 1


\okn_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2008]
:: OPIS: Okienko z wyświelaniem poszczególnych czynności obiegu informacji
::  OLD: \okn_opis/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUM.B_PREL<>''
||
:: PK - otwieranie odpowiedniej maski dla funkcji podglądu obiegu w xFunckje NUCO
   _mas:=EDOKUM.name()+2;
   EDOKLOG.cntx_psh();EDOKLOG.use('skid_d'+_mas);
   EDOKLOG.index('DISP'); EDOKLOG.prefix(EDOKUM.ref());
   EDOKOS.cntx_psh(); EDOKOS.use('skid_y'+_mas);
   EDOKOS.index('DISP1'); EDOKOS.prefix('T',EDOKUM.ref(),EDOKUM.B_PREL);
   _okn_grp:=EDOKLOG.grp_make('Obieg dokumentu'@,,'edoklog_selxx');
   EDOKLOG.grp_sel(_okn_grp,EDOKLOG,'WER','Wykonane operacje'@,,,,,,,,,'maximized');
   EDOKLOG.grp_sel(_okn_grp,EDOKOS,'WER','Bieżąca czynność'@,,,,,,,,,'maximized');
   EDOKLOG.win_sel(_okn_grp);
   EDOKLOG.select();
   EDOKOS.cntx_pop(); EDOKLOG.cntx_pop()
|| EDOKLOG.index('DISP'); EDOKLOG.prefix(EDOKUM.ref());
   EDOKLOG.win_sel('WER'); EDOKLOG.select()
?}



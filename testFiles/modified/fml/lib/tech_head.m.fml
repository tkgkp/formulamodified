:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tech_head.fml
:: Utworzony: 23.02.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa nagłówków kart technologicznych (T), wzorców (W) i technologii zleceń (Z) - tabela TKTL
::            Plik biblioteczny - wspólna obsługa dla TTE_TEC, TTE_WTE, TTE_PZL
::======================================================================================================================


\find_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Znajduje nową pierwszą wolną wersję dla TKTL
::   WE: _a - STRING - T/W - technologia czy wzorzec
::       _b - STRING - symbol karty
::       _c - STRING - wersja wejściowa
::       [_d] - INTEGER - 0/[1] czy wywołanie rekurencyjne
::   WY: STRING - znaleziona wersja lub ''
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_torw:=_a;
_symbol:=_b;
_ver:=_c;
_nr:=~~;
_rec:=0;
{? var_pres('_d')=type_of(0)
|| _rec:=_d
?};

_result:='';

TKTL.cntx_psh();
TKTL.index('NRK');
_precision:=-1;
{? _ver*'.'>0
|| _fract_pos:=_ver*'.';
   _precision:=(+_ver)-_fract_pos
|? _ver*','>0
||
   _fract_pos:=_ver*',';
   _precision:=(+_ver)-_fract_pos
?};

{? #_ver>0
|| _nr:=#_ver
?};

_chk:=0;
{? type_of(_nr)=type_of(0)
|| _str:='';
   {? _precision>-1
   || _str:=form(_nr,,_precision,'..')
   || _str:=form(_nr,,,'..')
   ?};
   _chk:=exec('chk_symbol','tech_head',_torw,_symbol,_str)
|| _chk:=exec('chk_symbol','tech_head',_torw,_symbol,_ver)
?};

{? _chk=0
||
   _rec_ver:='';
:: Jest konflikt wyznaczam nowy numer
   {? type_of(_nr)=type_of(0)
   || _nr:=exec('next_value','#math',_nr,,_precision);
      {? _precision>-1
      || _rec_ver:=form(_nr,,_precision,'..')
      || _rec_ver:=form(_nr,,,'..')
      ?}
   || _num:=1;
      {? _ver*'_'>0
      || _split:=spli_str(_ver,'_');
         _len:=obj_len(_split);
         {? _len>1
         || _last:=_split[_len];
            _nr:=#_last;
            _num:=exec('next_value','#math',_nr)
         ?};
         _ver:=((_ver*'_')-1)+_ver
      || _ver:=3+_ver
      ?};
      _rec_ver:=_ver+'_'+form(_num,,,'..');
:: Jeśli _rec_ver jet dłuższe niż 6 znaków zaczynam numeracje od nowa
      {? +_rec_ver>6
      || _rec_ver:='0'
      ?}
   ?};
:: !!! REKURENCJA !!!
   _result:=exec('find_wer','tech_head',_torw,_symbol,_rec_ver,1)
||
:: Brak konfliktu biore to co przyszło
   {? type_of(_nr)=type_of(0)
   || {? _precision>-1
      || _result:=form(_nr,,_precision,'..')
      || _result:=form(_nr,,,'..')
      ?}
   || _result:=_ver
   ?}
?};
TKTL.cntx_pop();
_result



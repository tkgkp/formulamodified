:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: faktury_poz.fml
:: Utworzony: 27.03.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa dokumentów sprzedaży i zakupu - pozycje dokumentów
::======================================================================================================================


\kor_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: koryguje (edytuje) pozycje faktury
::   WE: [_a] - 1-(domyslnie) z okienkiem 0-bez okienka
::       [_b] - 0/1 - okno dla danych do intrastat
::   WY: czy byla edycja
::  OLD: \kor_poz/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_edit:={? var_pres('_a')=type_of(0) || _a || 1 ?};
_intrastat:={? var_pres('_b')=type_of(0) || _b || 0 ?};

_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};

_wyn:=0;

_env_fak_poz:=exec('env','faktury_poz');
_env_fak_poz.POPRAW:='T';
params_set('env_fak_poz',_env_fak_poz);

_win_edit:=exec('fap_win_edit','faktury_poz',_intrastat);
FAP.win_edit(_win_edit);
exec('set_efld_opt','faktury_poz');
_oldm:=FAP.M;

{? FAP.POZP=0 & FAP.M
|| exec('zmvat_chk','faktury_wspolne',FAP.M,FAKS.KH,FAKS.AR,FAKS.AM,
      FAP.SV,FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FAKS.SZ)
?};

{? FAKS.WAL=null || FAKS.WAL:=INFO.NAROD; FAKS.put(1) ?};

FAP.CWAL:=FAP.CWAL+FKOR.CWAL;
FAP.CPR:=FAP.CPR+FKOR.CPR;
FAP.CN:=FAP.CN+FKOR.CN;
FAP.CB:=FAP.CB+FKOR.CB;

{? FAP.POZP=0
||
   FAP.IL:=FAP.IL+FKOR.IL;
   FAP.IL2:=FAP.IL2+FKOR.IL2;
   FAP.WS2:=FAP.WS2+FKOR.WS2;

:: narodowa
   FAP.WN:=FAP.WN+FKOR.WN;
   FAP.WV:=FAP.WV+FKOR.WV;
   FAP.WB:=FAP.WB+FKOR.WB;

:: handlowa
   FAP.WWAL:=FAP.WWAL+FKOR.WWAL;
   FAP.VWAL:=FAP.VWAL+FKOR.VWAL;
   FAP.BWAL:=FAP.BWAL+FKOR.BWAL;

:: opodatkowania
   FAP.WWAL_P:=FAP.WWAL_P+FKOR.WWAL_P;
   FAP.VWAL_P:=FAP.VWAL_P+FKOR.VWAL_P;
   FAP.BWAL_P:=FAP.BWAL_P+FKOR.BWAL_P;

:: intrastat
   FAP.MASAN:=FAP.MASAN+FKOR.PMASAN;
   FAP.ILUJM:=FAP.ILUJM+FKOR.PILUJM;
   FAP.WF:=FAP.WF+FKOR.PWF;
   FAP.WS:=FAP.WS+FKOR.PWS
?};

:: data zwrotu
_dz:=FAKS.DZ;
FKOR.DZ:=FAKS.DZ;

exec('pops','intrastat');
FAP.memo_get(,'UWAGI');

{? ~_edit | FAP.edit("exec('spr_poz_kor','faktury_poz',0)")
||
   _wyn:=1;
:: NUCO - modyfikacja dla samofakturowania (korekty)
   {? FAP.FAKS().T().T='KORWDT' | FAP.FAKS().T().T='KORWDTS'
   || exec('liczfak','faktury_wspolne')
   ?};
   FKOR.M:=FAP.M;
   FAP.CWAL:=FAP.CWAL-FKOR.CWAL;
   FAP.CPR:=FAP.CPR-FKOR.CPR;
   FAP.CN:=FAP.CN-FKOR.CN;
   FAP.CB:=FAP.CB-FKOR.CB;

   {? FAP.POZP=0
   ||
      _vat:=#((FAP.SV().KOD*'%') + FAP.SV().KOD-1) / 100;

      FAP.IL:=FAP.IL-FKOR.IL;
      FAP.IL2:=FAP.IL2-FKOR.IL2;
      FAP.WS2:=FAP.WS2-FKOR.WS2;

      FAP.WN:=FAP.WN-FKOR.WN;
      FAP.WV:=FAP.WV-FKOR.WV;
      FAP.WB:=FAP.WB-FKOR.WB;

::    handlowa
      FAP.WWAL:=FAP.WWAL-FKOR.WWAL;
      FAP.VWAL:=FAP.VWAL-FKOR.VWAL;
      FAP.BWAL:=FAP.BWAL-FKOR.BWAL;

::    opodatkowania
      FAP.WWAL_P:=FAP.WWAL_P-FKOR.WWAL_P;
      FAP.VWAL_P:=FAP.VWAL_P-FKOR.VWAL_P;
      FAP.BWAL_P:=FAP.BWAL_P-FKOR.BWAL_P;

::    intrastat
      FAP.MASAN:=FAP.MASAN-FKOR.PMASAN;
      FAP.ILUJM:=FAP.ILUJM-FKOR.PILUJM;
      FAP.WF:=FAP.WF-FKOR.PWF;
      FAP.WS:=FAP.WS-FKOR.PWS
   ?};
:: rozliczenia projektów
   {? FAKS.PROJEKTY
   || FAP.PROJEKTY:=FAKS.PROJEKTY;
      exec('projdel','projekty',FAP.ref());
      exec('projwgfap','projekty',FAP.ref())
   |? FAP.PROJEKTY
   || exec('projdel','projekty',FAP.ref());
      exec('projwgfap','projekty',FAP.ref())
   ?};
   {? (_put:=FAP.put()) & _dz<>FKOR.DZ
   || FAKS.DZ:=FKOR.DZ;
      FAKS.put()
   ?};
   {? _put
   ||
      FAP.memo_put();
      exec('FindAndGet','#table',FAKS,FAKS.LKSQL,,"
         _name:=ref_name(FAKS.ref());
         FAP.cntx_psh();
         FAP.use((FAP.name()-3)+(_name+3));
         FAP.cntx_psh();
         FAP.index('FAP');
         FAP.prefix(FAKS.ref(),FAP.POZ);
         {? FAP.first()
         ||
            {? __POPS.MASAN || FAP.MASAN:=FKOR.PMASAN ?};
            {? __POPS.ILUJM || FAP.ILUJM:=FKOR.PILUJM ?};
            {? __POPS.WF || FAP.WF:=FKOR.PWF ?};
            {? __POPS.WS || FAP.WS:=FKOR.PWS ?};
            FAP.put()
         ?};
         FAP.cntx_pop();
         FAP.cntx_pop()
      ");
::    aktualizacja kwoty naliczonego rabatu
      {? FAKS.KZ<>'' || exec('faks_kzr_war_rabk_licz','faktury_nag1') ?}
   ?};
:: naliczenie opłat dodatkowych
   {? _put || exec('actTAXS','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),FAP.M,_oldm) ?};
   FAP.cntx_psh();
   {? FAKS.T().HIS<>'T' || exec('sumfak','faktury_wspolne') ?};
   FAP.cntx_pop()
?};
exec('fap_win_edel','faktury_poz',_win_edit);
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
_wyn


\zakpowdm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: akcja powiazania pozycji PZ-tek, WNT z dokumentami zakupowymi
::  OLD: \zakpowdm/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.get();
{? FAKS.T().KOR='T' || exec('zakpowkm','faktury_poz'); return() ?};
FAP.cntx_psh();
_nopos:=~FAP.size();
_ile_rozl:={? ~_nopos || exec('ile_rozf','faktury_wspolne',$FAP.ref,FAKS.WHERE) || 0 ?};
_opc:=0;
_waluta:=FAKS.WAL;
BEER.JMZ:=FAKS.JMZ;
_powusl:=FAP.M().RODZ='U';
_jmz:=BEER.JMZ & FAP.J2<>FAP.JM & FAP.WS2<>1;
_fapil:={? _jmz || FAP.IL2 || FAP.IL ?};
_nousl:=exec('get','#params',200400,2)='N';
_czy2pow:=exec('get','#params',200310,2)='T';
_where:={? FAKS.WHERE='Z' || 'E' || 'Z' ?};
{? (_nousl | FAKS.WHERE='E') & FAP.M().RODZ='U'
|| FUN.info('Funkcja niedostępna dla pozycji usługowych.'@);
   ''
|? FAKS.STAT_REJ='A'
|| FUN.info('Dokument został anulowany.\nFunkcja niedostępna.'@);
   ''
|? {? FAKS.ZAK='T'
   || 0
   |? FAKS.STAT_REJ<>'N' | FAKS.AKC='T'
   || {? _nopos | _fapil-_ile_rozl>0 || 1 || FUN.info('Pozycja dokumentu jest w całości powiązana.\n'@); 0 ?}
   || 1
   ?}
 & {? FAKS.STAT_REJ<>'N' | FAKS.AKC='T' || BEER.SP_POW:=0; 1
   |? exec('FindInSet','#table','FAP','FAP',FAKS.ref)=null
   || _opc:=1;
      {? FAKS.WHERE='Z'
      || _opc:=FUN.choice('Wybierz jedną z opcji powiązania pozycji faktury\n '
                       'z pozycjami dokumentów magazynowych lub z pozycjami realizacji zamówień dostaw.'@,
                       ,'Dopisać pozycje Materiałowe'@
                       ,'Dopisać pozycje Usługowe'@);
         _powusl:={? _opc=2 || 1 || 0 ?}
      ?};
      BEER.SP_POW:=1; _opc
   |? _fapil-_ile_rozl>0
   || BEER.SP_POW:={? FAKS.WHERE='Z' & ~_nousl
                   || _opc:=FUN.choice('Wybierz jedną z opcji powiązania pozycji faktury\n '
                       'z pozycjami dokumentów magazynowych lub z pozycjami realizacji zamówień dostaw.'@,
                       ,'Powiązać z bieżącą pozycją'@
                       ,'Dopisać pozycje Materiałowe'@
                       ,'Dopisać pozycje Usługowe'@);
                       {? _opc=3 || _powusl:=1; _opc:=2 ?};
                      _opc-1
                   || _opc:=FUN.choice('Wybierz jedną z opcji powiązania pozycji faktury '
                       'z pozycjami dokumentów magazynowych.'@,,'Powiązać z bieżącą poz.faktury'@,
                       'Dopisać do pozycji faktury'@);
                      _opc-1
                   ?};
      _opc
   || {? FAKS.WHERE='Z'
      || _opc:=FUN.choice('Pozycja dokumentu jest w całości powiązana.\n'
                          'Dopisać pozycje?'@,,'Materiałowe'@,'Usługowe'@);
         _powusl:=_opc=2
      || _opc:=FUN.ask('Pozycja dokumentu jest w całości powiązana.\n'
                       'Dopisać pozycje materiałowe?'@);
         _powusl:=0
      ?};
      BEER.SP_POW:=1; _opc
   ?}
||
   _kh:=FAKS.KH;
   _ref_m:={? ~BEER.SP_POW || FAP.M || null ?};

   _mnd:=(ND.name-2); _mdk:=(DK.name-2); _akt:=FAKS.name+2;

   _msk:=exec('tab_mask','faktury_wspolne',2,_akt);

   VAR_DEL.delete('__powzak');
   __powzak:=tab_tmp(3,'TREE','TREE_REF','galazka'
              ,'LAB','STRING[100]','Etykieta'
              ,'LA2','STRING[100]','Etykieta2'
              ,'MAG','STRING[10]','Magazyn'
              ,'SYM','STRING[100]','Symbol-KTM'
              ,'TYP','STRING[10]','Typ dokumentu'
              ,'DAT','DATE','Data wystawienia'
              ,'ILE','REAL','Ilosc'
              ,'CEN','REAL','Cena'
              ,'RDK','STRING[20]','ref DK'
              ,'RND','STRING[20]','ref ND'
              ,'ILP','REAL','Ilosc rozpisana'
              ,'ILR','REAL','Ilosc do rozpisania'
              ,'ICON','INTEGER','Ikonka'
              ,'DOK','STRING[40]','Ref dokumentu'
              ,'KTM','STRING[50]','Indeks materialowy'
              ,'POZ','STRING[2]','typ pozycji'
              ,'CWL','REAL','cena w walucie'
              ,'KRS','REAL','kurs'
              ,'WAL','STRING[3]','waluta'
              ,'FAP','STRING[20]','poz.faktury'
              ,'COW','REAL','obliczona cena w walucie'
              ,'JM1','STRING[10]','jednostka miary'
              ,'DKC','STRING[20]','cecha'
              ,'DKL','INTEGER','dokladnosc'
              ,'WS2','REAL','wsp.przeliczenia'
              ,'JM2','STRING[10]','jm2'
              ,'TUE','STRING[1]','typ ue T/N'
              ,'PRJ','STRING[20]','ref PROJEKTY');
   exec('war_hid','ceny',__powzak,'CEN','CWL','COW');

   _win_sel:=__powzak.mk_sel({? ~_powusl
                             || {? BEER.SP_POW
                                || 'DOPISANIE POZYCJI: Dokumenty przychodowe dla kontrahenta %1'@[FAKS.KH().NAZ]
                                || 'POWIĄZANIE POZYCJI: Dokumenty przychodowe dla kontrahenta %1'@[FAKS.KH().NAZ]
                                ?}
                             || {? BEER.SP_POW
                                || 'DOPISANIE POZYCJI: Realizacje zamówień dla kontrahenta %1'@[FAKS.KH().NAZ]
                                || 'POWIĄZANIE POZYCJI: Realizacje zamówień dla kontrahenta %1'@[FAKS.KH().NAZ]
                                ?}
                             ?},'P',,'#pow_zak_sel',,,__powzak.size(),1);

   _czy_wal:=FAKS.WAL<>INFO.NAROD;

   __powzak.win_fld(_win_sel,,'LAB',,,{? _czy_wal || 8 || 27 ?},,1,'');
   __powzak.win_fld(_win_sel,,'SYM',,,15,,1,'Symbol dokumentu / materiał'@);
   __powzak.win_fld(_win_sel,,'JM1',,,4,,1,'jm'@);
   __powzak.win_fld(_win_sel,,'DKC',,,5,,1,'Cecha'@);
   __powzak.win_fld(_win_sel,,'ILE',,,12,3,1,'Ilość'@);
   __powzak.win_fld(_win_sel,,'ILP',,,12,3,1,'Ilość rozpisana'@);
   __powzak.win_fld(_win_sel,,'CEN',,,10,ST.DOKL_C,1,'Cena'@);
   {? _czy_wal
   || __powzak.win_fld(_win_sel,,'CWL',,,10,ST.DOKL_C,1,'w %1 Kurs: %2'@[FAKS.WAL().KOD,form(FAKS.KRS,,4,'99')])
   ?};
   __powzak.win_fld(_win_sel,,'ILR',,,12,3,,'Do rozliczenia'@);
   __powzak.win_act(_win_sel,0,'Formuła','&Zwiń/rozwiń całość'@@,,'Zwijanie/rozwijanie całości'@,
             "exec('zwrw_all','#tree','__powzak','TREE')",,1,,,,'Z');
   __powzak.win_act(_win_sel,0,'Popraw',,,,"exec('pop_pow','faktury_poz')",,0);
   __powzak.win_act(_win_sel,0,'Rekord',,,,"exec('rek_pow1','faktury_poz')","exec('rek_pow2','faktury_poz')",0);
   __powzak.win_act(_win_sel,0,'Formuła','&Akceptuj'@@,,,,"exec('akc_pow','faktury_poz')",,0,,,'A');
   __powzak.win_act(_win_sel,0,'Formuła','&Wybierz'@@,,,,"exec('zatw_wyb','faktury_poz')"
    ,,1,"exec('zatw_wyb','faktury_poz')",,'W');
   __powzak.win_act(_win_sel,0,'Formuła','Z&rezygnuj z wyboru'@@,,,,"exec('zatw_wyb','faktury_poz',0)"
    ,,1,"exec('zatw_wyb','faktury_poz',0)",,'R');
   __powzak.win_act(_win_sel,0,'Formuła','Dane wg &indeksu'@@,,,,"exec('prezdane','faktury_poz','I')",,0,,,'I');
   __powzak.win_act(_win_sel,0,'Formuła','Dane wg &dokumentu'@@,,,,"exec('prezdane','faktury_poz','D')",,0,,,'D');
   __powzak.win_act(_win_sel,0,'Szukaj');
   __powzak.win_act(_win_sel,0,'Kolejność');
   __powzak.tr_fml(_win_sel,,"{? _a || {? __powzak.TREE=0 || 1 || _a ?} || _a ?}");
   __powzak.win_fml(_win_sel,,'LAB',,'ICON_BEFORE',"
            {? __powzak.TREE=0
            || 'xwin16.png:32'
            |? __powzak.TREE<>0 & __powzak.ILR>0
            || 'xwin16.png:38'
            || ''
            ?}
        ");
   __powzak.win_sel(_win_sel);
   BEER.fld_fml('SW','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $M.DOKL || $ST.DOKL ?}");

   __powzak.fld_fml('ILE','EDIT_FORMAT'
             ,"'in_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILE','DISPLAY_FORMAT'
             ,"'out_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILP','EDIT_FORMAT'
             ,"'in_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILP','DISPLAY_FORMAT'
             ,"'out_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILR','EDIT_FORMAT'
             ,"'in_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILR','DISPLAY_FORMAT'
             ,"'out_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('CEN','EDIT_FORMAT'
             ,"'in_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL_C\")
                          || $ST.DOKL_C
                          ?}");
   __powzak.fld_fml('CEN','DISPLAY_FORMAT'
             ,"'out_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL_C\")
                          || $ST.DOKL_C
                          ?}");
   __powzak.fld_fml('CEN','DISPLAY_FORMAT',"{? ~__powzak.TREE || 'empty=1' || 'empty=0' ?}");
   __powzak.fld_fml('CWL','DISPLAY_FORMAT',"{? ~__powzak.TREE || 'empty=1' || 'empty=0' ?}");

   __powzak.clear();

   _tue:=exec('czytypue','zamsiw_rea');
:: NUCO - zmiana wynika z zablokowania zapisów z przed rozpoczęcia łączenia dokumentów PZ z FAKZ
   _pow_od:=exec('get_w','#params',999008,type_of(date()));
   {? ~_powusl
   || ND.cntx_psh();
      DK.cntx_psh();
      _msk.clear();
      {? _msk.first()
      || {!
         |? ND.use(_mnd+_msk.ROK);
            DK.use(_mdk+_msk.ROK);
            USERS_UP.index('MG');
            USERS_UP.prefix(OPERATOR.USER,'MG');
            {? USERS_UP.first
            || {!
               |? ND.index('KH');
                  ND.prefix(_kh,'T','N',USERS_UP.MG);
                  {? ND.first()
                  || {!
                     |? {? ND.TYP().DN='N' &  (exec('get','#params',200300,2)='T' | ND.WAL=_waluta) &
                         {? FAKS.WHERE='E' || ND.TYP().UE='T' & FAKS.KPW=ND.KPW & FAKS.RTRANSAK=ND.RTRANSAK || 1 ?}
                         & {? _tue='' || 1 || ND.TYP().UE=_tue ?}
                         & (BEER.SP_POW | FAP.D=date(0,0,0) | FAP.D=ND.D)
                         & ND.D>_pow_od
:: NUCO - zmiana wynika z zablokowania zapisów z przed rozpoczęcia łączenia dokumentów PZ z FAKZ
                        || DK.index('DOKMAG');
                           DK.prefix(ND.ref());
                           {? ND.Z='T'
                            & (_ok:=0;
                             {? DK.first()
                             || {!
                                |? {? ~_czy2pow & exec('FindInSet','#table','FAP2DK','DK',$DK.ref(),_where)<>null()
                                   || DK.next()
                                   |? DK.IL>0 & (_ref_m=null | _ref_m=DK.M)
                                     & {? FAKS.AKC='T'
                                       || exec('FindInSet','#table','FAP','FAM',DK.M,FAKS.ref())<>null
                                       || 1
                                       ?}
                                   || _ok:=1; 0
                                   || DK.next()
                                   ?}
                                !}
                             ?};
                             _ok)
                           ||
                              __powzak.blank();
                              _etyk:=ND.MAG().SYM+' ... '+ND.TYP().T+' ... '+ND.SYM+' z dnia '+form(ND.D);
                              __powzak.MAG:=ND.MAG().SYM;
                              __powzak.TYP:=ND.TYP().T;
                              __powzak.LAB:=_etyk;
                              __powzak.LA2:='';
                              __powzak.DAT:=ND.D;
                              __powzak.SYM:=ND.SYM+{? ND.FZAK<>'' || ' , '+ND.FZAK || '' ?};
                              __powzak.DOK:=ND.SYM+{? ND.FZAK<>'' || ' , '+ND.FZAK || '' ?}+' '+$ND.ref();
                              __powzak.KTM:='';
                              __powzak.TREE:=0;
                              __powzak.POZ:='DN';
                              __powzak.JM1:='';
                              __powzak.TUE:=ND.TYP().UE;
                              _korzen:={? __powzak.add(1) || _czy_add:=0;#__powzak.ref() || _czy_add:=1;0 ?};
                              {!
                              |? _ilp:={? ~_czy2pow & exec('FindInSet','#table','FAP2DK','DK',$DK.ref(),_where)<>null()
                                       || DK.IL
                                       || exec('ile_rozp','magdok_wspolne',$DK.ref(),FAKS.WHERE)
                                       ?};
                                 {? (DK.IL-_ilp)>0 & (_ref_m=null | _ref_m=DK.M)
                                  & {? FAKS.AKC='T' || exec('FindInSet','#table','FAP','FAM',DK.M,FAKS.ref)<>null || 1 ?}
                                 ||
                                    __powzak.blank();
                                    __powzak.TREE:=_korzen;
                                    __powzak.LAB:=form(DK.P,4,,'99')+'. '+DK.M().KTM+' - '+DK.M().N;
                                    __powzak.LA2:=_etyk;
                                    __powzak.TYP:=DK.N().TYP().T;
                                    __powzak.TUE:=DK.N().TYP().UE;
                                    __powzak.MAG:=DK.N().MAG().SYM;
                                    __powzak.SYM:=DK.M().KTM;
                                    __powzak.JM1:=DK.M().J().KOD;
                                    __powzak.ILE:=DK.IL;
                                    __powzak.ILP:=_ilp;
                                    __powzak.ILR:=0;
                                    __powzak.DOK:=ND.SYM+{? ND.FZAK<>'' || ' , '+ND.FZAK || '' ?}+' '+$ND.ref();
                                    __powzak.KTM:=DK.M().KTM;
                                    __powzak.POZ:='DP';
                                    {? DK.N().WAL<>null & DK.N().WAL<>INFO.NAROD
                                    || __powzak.CWL:=DK.CWAL;
                                       __powzak.WAL:=DK.N().WAL().KOD;
                                       __powzak.KRS:={? exec('czy_nwal','ustawienia',INFO.NAROD)|| DK.N().KRS || 1  ?}
                                    |? DK.CWAL=0 & FAKS.KRS>0 & DK.N().WAL<>FAKS.WAL & FAKS.NWAL=INFO.NAROD
                                    || __powzak.CWL:=(DK.C/FAKS.KRS) $2;
                                       __powzak.WAL:=FAKS.WAL().KOD;
                                       __powzak.KRS:=FAKS.KRS
                                    || __powzak.CWL:=0;
                                       __powzak.WAL:='PLN';
                                       __powzak.KRS:=0
                                    ?};
                                    __powzak.CEN:={? _czy_wal & FAKS.KRS<>0 || __powzak.CWL*FAKS.KRS || DK.C ?}$DK.M().DOKL_C;
                                    __powzak.RDK:=$DK.ref();
                                    __powzak.RND:=$ND.ref();
                                    __powzak.COW:={? _czy_wal & FAKS.KRS<>0 || __powzak.CEN/FAKS.KRS || 0 ?} $DK.M().DOKL_C;
                                    __powzak.DKC:=DK.DK_C().SYM;
                                    __powzak.DKL:=exec('jaka_dok_m','jm',DK.M);
                                    __powzak.JM2:=DK.J2().KOD;
                                    __powzak.WS2:=DK.WS2;
                                    __powzak.PRJ:=$DK.PROJEKTY;
                                    _czy_add:=__powzak.add(1)
                                 ?};
                                 DK.next()
                              !};
                              {? _czy_add=0
                              || __powzak.del
                              ?}
                           ?}
                        ?};
                        ND.next()
                     !}
                  ?};
                  USERS_UP.next
               !}
            ?};
            _msk.next()
         !}
      ?};
      ND.use(_mnd+_akt);
      DK.use(_mdk+_akt);
      ND.cntx_pop();
      DK.cntx_pop()
   || _mzd:=ZD_RN.names();
      exec('openzd_psh','open_tab');
      _mzd.clear();
      {? _mzd.first()
      || {!
         |? exec('openzd','open_tab',_mzd.NAME+3);
            ZD_RN.index('KH');
            ZD_RN.prefix(_kh,'- USŁUGI -',);
            {? ZD_RN.first()
            || {!
               |? _czy_add:=0;
                  ZD_RP.index('POZ');
                  ZD_RP.prefix(ZD_RN.ref());
                  {? ZD_RP.first() & (exec('get','#params',200300,2)='T' | ZD_RN.ZD_NAG().WAL=_waluta)
                   & (FAP.D=date(0,0,0) | FAP.D=ZD_RN.DR)
                  || {!
                     |? _ilf:=0;
                        FAP2DK.index('ZD_RP');
                        FAP2DK.prefix($ZD_RP.ref());
                        {? FAP2DK.first() || {! |? {? FAP2DK.FAP<>'' || _ilf+=FAP2DK.IL ?}; FAP2DK.next() !} ?};
                        {? (ZD_RP.IL_ZRE-_ilf)>0 & (_ref_m=null | _ref_m=ZD_RP.M)
                        || {? ~_czy_add
                           || __powzak.blank();
                              _etyk:=ZD_RN.ZD_NAG().T().T+' ... '+ZD_RN.ZD_NAG().SYM+' realizacja z dnia '+form(ZD_RN.DR);
                              __powzak.MAG:='- USŁUGI -';
                              __powzak.TYP:=ZD_RN.ZD_NAG().T().T;
                              __powzak.LAB:=_etyk;
                              __powzak.LA2:='';
                              __powzak.DAT:=ZD_RN.DR;
                              __powzak.SYM:=ZD_RN.ZD_NAG().SYM;
                              __powzak.DOK:=$ZD_RN.ref();
                              __powzak.KTM:='';
                              __powzak.TREE:=0;
                              __powzak.POZ:='DN';
                              __powzak.JM1:='';
                              __powzak.TUE:='N';
                              _korzen:={? __powzak.add(1) || _czy_add:=0;#__powzak.ref() || _czy_add:=1;0 ?}
                           ?};
                           __powzak.blank();
                           __powzak.TREE:=_korzen;
                           __powzak.LAB:=form(ZD_RP.ZD_POZ().POZ,4,,'99')+'. '+ZD_RP.M().KTM+' - '+ZD_RP.M().N;
                           __powzak.LA2:=_etyk;
                           __powzak.TYP:=ZD_RN.ZD_NAG().T().T;
                           __powzak.TUE:='N';
                           __powzak.MAG:='- USŁUGI -';
                           __powzak.SYM:=ZD_RP.M().KTM;
                           __powzak.JM1:=ZD_RP.M().J().KOD;
                           __powzak.ILE:=ZD_RP.IL_ZRE;
                           __powzak.ILP:=_ilf;
                           __powzak.ILR:=0;
                           __powzak.DOK:=$ZD_RN.ref();
                           __powzak.KTM:=ZD_RP.M().KTM;
                           __powzak.POZ:='DP';
                           {? ZD_RN.ZD_NAG().WAL<>null & ZD_RN.ZD_NAG().WAL<>INFO.NAROD
                           || __powzak.CWL:=ZD_RP.ZD_POZ().CWAL;
                              __powzak.WAL:=ZD_RN.ZD_NAG().WAL().KOD;
                              __powzak.KRS:=ZD_RN.ZD_NAG().KRS
                           |? ZD_RP.ZD_POZ().CWAL=0 & FAKS.KRS>0 & ZD_RN.ZD_NAG().WAL<>FAKS.WAL & FAKS.NWAL=INFO.NAROD
                           || __powzak.CWL:=(ZD_RP.ZD_POZ().CENA/FAKS.KRS) $2;
                              __powzak.WAL:=FAKS.WAL().KOD;
                              __powzak.KRS:=FAKS.KRS
                           || __powzak.CWL:=0;
                              __powzak.WAL:='PLN';
                              __powzak.KRS:=0
                           ?};
                           __powzak.CEN:={? _czy_wal & FAKS.KRS<>0
                                         || __powzak.CWL*FAKS.KRS
                                         || ZD_RP.ZD_POZ().CENA
                                         ?}$ZD_RP.M().DOKL_C;
                           __powzak.RDK:=$ZD_RP.ref();
                           __powzak.RND:=$ZD_RN.ref();
                           __powzak.COW:={? _czy_wal & FAKS.KRS<>0 || __powzak.CEN/FAKS.KRS || 0 ?} $ZD_RP.M().DOKL_C;
                           __powzak.DKC:=ZD_RP.ZD_POZ().DK_C().SYM;
                           __powzak.DKL:=exec('jaka_dok_m','jm',ZD_RP.M);
                           __powzak.JM2:=ZD_RP.ZD_POZ().J2().KOD;
                           __powzak.WS2:=ZD_RP.ZD_POZ().WS2;
                           _czy_add:=__powzak.add(1)
                        ?};
                        ZD_RP.next()
                     !}
                  ?};
                  ZD_RN.next()
               !}
            ?};
            _mzd.next()
         !}
      ?};
      obj_del(_mzd);
      exec('openzd_pop','open_tab')
   ?};

   obj_del(_msk);

   __powzak.clear();
   {? __powzak.first()
   || __powzak.hdr_sel(' (prezentacja wg Dokumentów).'@);
      __powzak.select();
      exec('odbl_all','faktury_poz')
   || {? _powusl
      || FUN.info({? exec('get','#params',200300,2)='N'
                  || 'Brak niepowiązanych realizacji zamówień dostaw '
                     'związanych z kontrahentem:\n%1 - %2.'
                     '\n\nWystawionych w walucie %3.'@[FAKS.KH().KOD,FAKS.KH().NAZ,FAKS.WAL().KOD]
                  || 'Brak niepowiązanych realizacji zamówień dostaw '
                     'związanych z kontrahentem:\n%1 - %2.'@[FAKS.KH().KOD,FAKS.KH().NAZ]
                  ?})
      || FUN.info({? exec('get','#params',200300,2)='N'
                  || 'Brak niepowiązanych pozycji dokumentów magazynowych '
                     'związanych z kontrahentem:\n%1 - %2.'
                     '\n\nWystawionych w walucie %3.'@[FAKS.KH().KOD,FAKS.KH().NAZ,FAKS.WAL().KOD]
                  || 'Brak niepowiązanych pozycji dokumentów magazynowych '
                     'związanych z kontrahentem:\n%1 - %2.'@[FAKS.KH().KOD,FAKS.KH().NAZ]
                  ?})
      ?}
   ?}
|? ~_opc
|| ''
||
   {? FAKS.ZAK='T'
   || FUN.info('Dokument jest zaksięgowany. Powiązanie pozycji nie jest możliwe.'@)
   || FUN.info('Pozycja dokumentu jest w całości powiązana.'@)
   ?}
?};
FAP.cntx_pop();
1



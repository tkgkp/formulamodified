:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: magdok_nag.fml
:: Utworzony: 24.06.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa dokumentów magazynowych - nagłówki dokumentów
::======================================================================================================================


\nd_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Ustawia okno redakcji tabeli ND, wymagane pola, okna słowników
::   WE: [_a] - 1-tryb Display 0-(domyślnie) nie
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_disp:={? var_pres('_a')=type_of(0) || _a || 0 ?};

BPMN.END:=0;
_win_red:=exec('nd_win_edit','magdok_nag');
{? ~_disp
||
:WW: modyfikacja podłączająca pod pole ND.O słownik POLE_O_W_ND
   _ff:="
   {? TYPYDOK.T='RWZ' | TYPYDOK.T='RWB' | TYPYDOK.T='RWL' | TYPYDOK.T='MW' | TYPYDOK.T='ZWM' | TYPYDOK.T='RWP'
      | TYPYDOK.T='RWPZL' | TYPYDOK.T='RWS' | TYPYDOK.T='RWZM'
   ||
      SLO.win_sel('EDKON');
      SLO.index('SL');
      _ref:=exec('FindInSet','#table','SLU','NAZ','POLE_O_W_ND');
      SLO.prefix(_ref);
      {? SLO.select()
      || ND.O:=SLO.TR
      || 0
      ?}
   ?}
   ";
   ND.fld_fml('O','F3',_ff);
   _ff:="
   {? TYPYDOK.T='RWZ' | TYPYDOK.T='RWB' | TYPYDOK.T='RWL' | TYPYDOK.T='MW' | TYPYDOK.T='ZWM' | TYPYDOK.T='RWP'
      | TYPYDOK.T='RWPZL' | TYPYDOK.T='RWS' | TYPYDOK.T='RWZM'
   || _cnt:=0;
      SLO.index('SL');
      _ref:=exec('FindInSet','#table','SLU','NAZ','POLE_O_W_ND');
      SLO.prefix(_ref);
      {? SLO.first()
      || {! |?
         {? ND.O=SLO.TR
         || _cnt:=1
         ?};
         SLO.next()
         !}
      ?}
:      {? _cnt=1
:      || ND.O
:      || ND.O:='';
:         FUN.info('Pole słownikowe. Proszę wybrać element z listy używając klawisza F3.');
:         ND.O
:      ?}
   || ND.O
   ?}
   ";
   ND.fld_fml('O','AFTER_EDIT',_ff);
:WW: koniec modyfikacji
   _ff:="params_exec('nd_pozycje_red','magdok_nag')";
   ND.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Poz&ycje'@],_ff);

    _ff:="params_exec('priority_action_red','#b__box')";
    ND.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Pr&iorytet'@],_ff);

   _ff:="_wyn:=params_exec('nd_zakoncz_red','magdok_nag');
         VAR_DEL.delete('ND_SPRAWDZONE_F2');
         {? _wyn='key:F2'
         || ND_SPRAWDZONE_F2:=1
         ?};
         _wyn";
   ND.win_ebtn(_win_red,'text=%1,icon=xwin16.png:23,btn_label_align=center,panel=bottom,align=end'['&Zakończ'@],_ff);
   exec('ok_esc','#window',ND,_win_red,1)
?};
ND.win_edit(_win_red);
exec('set_efld_opt','magdok_nag',_win_red);
exec('set_win_slo','magdok_nag',_win_red);
exec('sel_win_kh','ustawienia');
ST.MAG_SYM:=ND.MD().SYM;
_win_red


\n_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2006]
:: OPIS: usuwa naglowek dokumentu oraz pozycje
::   WE:  [_a] - czy pytac (0/[1])
::        [_b] - czy wywolywac transakcje (0/[1])
::        [_c] - gdy _a=0 przekazuje wartosc dla zmiennej _ur
::        [_d] - aktualizacja 1(domyslnie) rezerwacji 0-nie
::        [_e] - 0/[1] - czy sprawdzac powiazania z partiami i jesli sa to blokowac usuwanie? (domyslnie: tak)
::        [_f] - 1-bez ND.del (domyślnie 0)
::        [_g] - odświeżyć stany (domyślnie 0)
::        [_h] - tabela z listą refów DK, dla których wymuszać przywracanie rezerwacji
::        [_i] - [0]/2 - czy usuwać FAP2DK
::   WY:  (0/1) czy usunieto
::  OLD: \n_usun/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=1 ?}  || _a:=1  ?};
{? _>=2  || {? type_of(_b)<>1 || _b:=1 ?}  || _b:=1  ?};
{? _>=3  || {? type_of(_c)<>1 || _c:=1 ?}  || _c:=1  ?};
{? _>=4  || {? type_of(_d)<>1 || _d:=1 ?}  || _d:=1  ?};
{? var_pres('_e')=type_of(0) || _part_chk:=_e || _part_chk:=1 ?};
{? _>=6  || {? type_of(_f)<>1 || _f:=0 ?}  || _f:=0  ?};
{? _>=7  || {? type_of(_g)<>1 || _g:=0 ?}  || _g:=0  ?};
_dk_rez:=~~;
_zl_rez:=0;
{? var_pres('_h')>100
|| _dk_rez:=_h;
   _zl_rez:=1
?};
{? _>=9  || {? type_of(_i)<>type_of(1) || _i:=0 ?}  || _i:=0  ?};
_wyn:=0;

_ur:=_ui:=0;
_up:=_i;
_inw:=0;
{? ND.get() & exec('blok_nd','magdok_nag','T')
 & exec('czy_z_ok','okresy',1,1,ND.D~1,ND.D~2,ND.MAG)
 & {? exec('czyTR_NZL','transport_wspolne',ND.uidref(),1)
   || FUN.info('Do dokumentu wystawiono dyspozycje transportu.\nUsunięcie niemożliwe.'@);
      0
   || 1
   ?}
 & {? (_rek:=exec('dokWITHrek','reklamacje',ND.uidref()); _rek<>'')
   || {? 1+_rek='S'
      || FUN.info('Dokument powiązany z reklamacją klienta %1.\nUsunięcie niemożliwe.'@[2-_rek])
      || FUN.info('Dokument powiązany z reklamacją dostaw %1.\nUsunięcie niemożliwe.'@[2-_rek])
      ?};
      0
   || 1
   ?}
||
   _inw:=ND.INN='T';
   {? exec('mod_dok','magdok_nag') & (_rr:=exec('spr_doku','magdok_wspolne',ND.ref()))<>0
   ||
      _no_ans:=(_rr=1 | _rr=2) & exec('get','#params',3008,2,OPERATOR.USER)='P';
::    kontrola wydawanych palet
      _lpalet:={? ND.MAG().PAL='T' & ND.TYP().P<>'T' || exec('czy_lpal','magdok_palety',1,ND.ref()) || '' ?};

::    _a=0 podczas usuwania korekt KR+ KR-; dok. powiazanych typu MP, RKOM; realizacji zam. gdy przekroczony limit
      {? _a=0 || _ur:=_c || _ur:=exec('spr_zk','magdok_wspolne',0,1,'u') ?};
      {? _ur>0 & (_a=0 | (_ui:=exec('spr_inwe','magdok_wspolne')))
      || {? _lpalet<>''
         || FUN.info('Jedna z pozycji dokumentu zawiera rozpakowaną paletę,\n'
             'która została całkowicie wydana (pozycja dokumentu wydania).\n'
             '%1\n'
             'Usunięcie pozycji dokumentu niemożliwe.\nUsunięcie dokumentu niemożliwe.'@[_lpalet]);
            _wyn:=0
         |? ND.TYP().P='T' & ND.TYP().Z='T' & ND.TYP().DS='N' & ND.TYP().DK='N' &
             ~(_up:=exec('skad_gen','magdok_wspolne',$ND.ref(),_a))
         || _wyn:=0
         |?
            _zlgd:=exec('has_zlgd_nd','magdok_wspolne',ND.ref());
            _can_continue:=1;
            {? _zlgd>0
            ||
               {? _a
               || _can_continue:={? ~_zlgd & _no_ans
                                 || 1
                                 || FUN.ask({? _zlgd
                                            || 'Usunąć dokument %1 o symbolu: %2?\n'
                                               '\nUwaga: dokument powiązany z rejestracją robocizny.'@[ND.TYP().T,ND.SYM]
                                            || 'Usunąć dokument %1 o symbolu: %2?'@[ND.TYP().T,ND.SYM]
                                            ?})
                                 ?}
               || _can_continue:=1
               ?}
            ||
               {? _a
               || _czypow:=ND.MAG().MWS='T' & exec('FindInSet','#table','DK_LN','ZDOK',$ND.ref(),$ND.ref());
                  _czyope:=exec('stND2EANN','magazyn_mobi')='K';
                  _can_continue:={? ~_czypow & ~_czyope & _no_ans
                                 || 1
                                 || FUN.ask(
                                    {? _czyope
                                    || 'Uwaga. Na podstawie dokumentu wygenerowano operację na urządzeniu mobilnym.\n'
                                       'Usunięcie dokumentu usuwa powiązaną operację.\n\n'
                                       'Usunąć dokument %1 o symbolu: %2?'@[ND.TYP().T,ND.SYM]
                                    |? _czypow
                                    || 'Uwaga. Na podstawie dokumentu wygenerowano reorganizaję magazynu.\n'
                                       'Usunięcie dokumentu nie usuwa powiązanej reorganizacji.\n\n'
                                       'Usunąć dokument %1 o symbolu: %2?'@[ND.TYP().T,ND.SYM]
                                    || 'Usunąć dokument %1 o symbolu: %2?'@[ND.TYP().T,ND.SYM]
                                    ?})
                                 ?}
               || _can_continue:=1
               ?}
            ?};
            {? _can_continue>0 & exec('nd_il_zpars','magdok_wspolne',$ND.ref())>0
            ||
               {? _part_chk>0
               ||
::                Obsluga partii produkcyjnych - sprawdzenie czy dokument ma powiazania z partiami
::                Jesli tak to nie moge go usunac
                  _can_continue:=0;
                  _msg0:='';
                  _msg1:='Dokument: %1 jest powiązany z partiami produktów.'@[ND.SYM];
                  _msg2:='Usunięcie dokumentu niemożliwe.'@;
                  {? ND.ZL<>null()
                  || _msg3:='Aby usunąć ten dokument należy w obszarze Zleceń '@;
                     _msg4:='na zleceniu: %1 uruchomić \'ROZLICZENIE PARTII\''@[ND.ZL().SYM];
                     _msg5:='i stamtąd usunąć powiązania dokumentu z partiami zlecenia.'@
                  || _msg3:='Aby usunąć ten dokument należy w obszarze Zleceń '@;
                     _msg4:='na zleceniach powiązanych z dokumentem uruchomić \'ROZLICZENIE PARTII\''@;
                     _msg5:='i stamtąd usunąć powiązania dokumentu z partiami zlecenia.'@
                  ?};
                  _msg_glued:=exec('form','#string','L',_msg1
                                                       ,_msg2
                                                       ,_msg0
                                                       ,_msg3
                                                       ,_msg4
                                                       ,_msg5);
:: NUCO
                  FUN.emsg(_msg_glued);
                  {? ND.ZL<>null() & FUN.ask('Czy uruchomić rozliczenie partii ?')
                  || exec('creator_run','magdok_partie',,ND.ZL,1,0)
                  ?}
               ||
::                Nie sprawdzamy powiazan z partami, tylko je wycinamy
                  _can_continue:=exec('delete4nd','magdok_nag',ND.ref())
               ?}
            ?};

            {? ND.TYP().P='N' & _can_continue>0 & exec('spr_nd_lim','magdok_wspolne',ND.ref())=0
            || _can_continue:=0;
               FUN.emsg(exec('form','#string','L',
                  'Usunięcie pozycji dokumentu spowoduje ujemne stany na limitach zleceń.'@,
                  'Usunięcie dokumentu niemożliwe.'@
               ))
            ?};
            {? _can_continue>0 & (_rem_grm:=0;
                                 DK.cntx_psh();
                                 DK.index('DOKMAG');
                                 DK.prefix(ND.ref());
                                 {? DK.first()
                                 || {!|?
                                       {? DK.REM_GRM<>null() || _rem_grm:=1 ?};
                                       DK.next() & ~_rem_grm
                                    !}
                                 ?};
                                 DK.cntx_pop();
                                 _rem_grm)
            || _can_continue:=FUN.ask('Dokument powiązany ze zgłoszeniem remontowym.\nUsunąć dokument?'@)
            ?};

            _can_continue
         ||
            {? _b || do() ?};
            {? _ur=2 || exec('del_rear','magdok_nag'); exec('del_real','zamsiw_rea','N',,_d) ?};
            {? _ur=3 || exec('usun_rea','zamdst_rea',$ND.ref,2,1) ?};
            {? _up=2 | ND.TYP().DK='T' || exec('skad_del','magdok_wspolne',$ND.ref()) ?};
::          usuwa zalaczniki
            _ok:=0;
            {? exec('deladok','dokum','ND')<>1
            || _wyn:=0;
               undo();
               FUN.info('Dokumentu %1 nie udało się usunąć,\n'
                'ponieważ usunięcie załączników nie powiodło się.'@[ND.SYM])
            || _ok:=1
            ?};
::          usunięcie powiązań ze zgłoszniami remontowymi
            {? _ok
            ||
               REM_GRM.cntx_psh();
               REM_GRM.prefix();
               DK.cntx_psh();
               DK.index('DOKMAG');
               DK.prefix(ND.ref());
               {? DK.first()
               || {!|?
                  {? DK.REM_GRM<>null()
                  || REM_GRM.use(ref_name(DK.REM_GRM));
                     DK.REM_GRM();
                     REM_GRM.ND:='N';
                     REM_GRM.UIDREFDK:='';
                     _ok:=REM_GRM.put()
                  ?};
                  DK.next() & _ok
                  !}
               ?};
               DK.cntx_pop();
               REM_GRM.cntx_pop();
               {? ~_ok
               ||
                  _wyn:=0;
                  undo();
                  FUN.info('Dokumentu %1 nie udało się usunąć,\n'
                   'ponieważ nie udało się usunąć powiązań ze zgłoszeniami remontowymi.'@[ND.SYM])
               ?}
            ?};
::          usunięcie powiązań z pozycjami dokumentów sprzedaży
            {? _ok
            ||
               DK.cntx_psh();
               DK.index('DOKMAG');
               DK.prefix(ND.ref());
               {? DK.first()
               || {!
                  |?
                     exec('delfap2dk','magdok_wspolne',$DK.ref());
                     DK.next()
                  !}
               ?};
               DK.cntx_pop()
            ?};

            DK.index('DOKMAG');
            DK.prefix(ND.ref());
            _il_poz:=DK.size(); _i:=0;
            _pal:=tab_tmp(1,'PAL','STRING[16]','');

            {? _ok & DK.first()
            || _mat:=tab_tmp(1,'MAT','STRING[16]',''
                      ,'NRK','INTEGER',''
                      ,'ILE','REAL','');
::             przywrócenie rezerwacji na fakturze
               {? ND.FAKS_REF<>'' & ND.TYP().P='N' & exec('FindAndGet','#table',FAKS,ND.FAKS_REF,,"WHERE",'N')='G'
               || exec('newrezfp','faktury_wspolne',ND.ref)
               ?};

               _is_rp:=exec('nd_is_rp','magdok_wspolne',ND.ref());
               {!
               |?
::                kontroling
:: DRO:[rr] schematy.fml
::                  exec('aktualizacja_mp','schematy',DK.N,DK.ref(),'U');
                  exec('delprzyp','fakso','FAKSO','DK',DK.ref());
:: produkcja kooperacja aktualizacja ilosci na pozycji przewodnika
                  {? DK.ZGP<>null & DK.N().TYP().KOOP='T' & DK.N().TYP().P='T'
                  || exec('akt_zgp_ilkoop','zl_koop',DK.ZGP,DK.IL)
                  ?};
                  exec('delFAP_K','oplaty_dod',DK.N().uidref(),DK.uidref(),null());
                  FAP_K.index('DKSQL');
                  FAP_K.prefix($DK.ref());
                  {? FAP_K.first() || {! |? exec('dk_k_del','magdok_poz') !} ?};
                  {? DK.count()>0
                  || DK_L.index('DK');
                     DK_L.prefix(DK.ref(),null);
                     {? DK_L.first()
                     || {!
                        |? {? DK_L.PAL<>null
                           || _pal.clear();
                              {? ~_pal.find_key($DK_L.PAL)
                              || _pal.blank();
                                 _pal.PAL:=$DK_L.PAL;
                                 _pal.add(1)
                              ?};
                              {? DK_L.ZPALET=2
                              || _cen_dk:={? (';FL'*DK.N().TYP().AFIFO)>1 || -1 || DK_L.DK().C ?};
                                 exec('poz_bpal','magdok_palety',0,$DK_L.PALDO,$DK_L.PAL,DK_L.DK().M().KTM,DK_L.IL
                                  ,_cen_dk,DK_L.TW);
                                 exec('zwrilpal','magdok_palety',DK_L.PAL,DK_L.DK().M().KTM,DK_L.IL,_cen_dk,DK_L.TW)
                              ?}
                           ?};
                           DK_L.del()
                        !}
                     ?}
                  ?};

                  _i+=1;
                  _mat.clear();
                  {? _mat.find_key($DK.M)
                  || _mat.ILE+=DK.IL;
                     _mat.put(1)
                  || _mat.blank();
                     _mat.MAT:=$DK.M;
                     _mat.NRK:=exec('FindInSet','#table','REZ','RODZ',DK.M,'W',"REZ.NRK",,,0);
                     _mat.ILE:=DK.IL;
                     _mat.add(1)
                  ?};

                  _ref_kor:=DK.REF_KOR;

                  exec('dk_del','magdok_poz',$DK.ref());

                  {? ND.TYP().DK='W'
                  ||
::                   poprawia pole SM.ST_WAR
                     exec('sm_upd','magazyn_stan',DK.name()+3,DK.M,DK.WAR)
                  ?};

                  SCH_XD.cntx_psh;
                  SCH_XD.use('scxd_'+(8+$DK.ref()+3));
:: DRO:[rr] schematy.fml
::                  exec('del_sch_xd','schematy','M');
                  SCH_XD.cntx_pop;

                  exec('DK_HS_del4DK','statexam',DK.ref());
                  exec('karo_dk_del','magdok_poz',$DK.ref());

                  {? DK.PLUS='N'
                  || {? _ur<>2 || exec('update','rezerwacje','DK',DK.ref(),ND.MAG,DK.M,0,DK.SRDK) ?};
::                   Produkcja: Jeżeli pozycja dokumentu powstała na podstawie dedykowanej rezerwacji
::                   surowca nielimitowanego, to przywracam wykorzystaną rezerwację
                     exec('returnrt','rezerwacje',$DK.ref(),form((8+($DK.ref())))+3,
                     {? _zl_rez>0 & _dk_rez.find_key($DK.ref()) || 2 || _rr ?},DK.N().KH)
                  ?};

                  {? DK.N().TYP().P='T' & ~(DK.N().TYP().DN='T' & DK.DOST<>date(0,0,0))
                   & DK.SCEAN<>'' & DK.M().IDMOB='D' & DK.SRDK=DK.PRDK &
                   (DK.N().INN='T' & DK.N().TYP().INW='I' & DK.N().TYP().P='T')
                  || _ok:=exec('mkodkadd','kody_kresk',DK.M,DK.SCEAN,$DK.ref(),1)
                  ?};

                  exec('del','dokuml',DK.uidref());

::                Dla dokumentu RW sprawdzane jest, czy usuwana pozycja jest ostanią pozycją dokumentu RW danego limitu.
::                Jeżeli tak to odblokowuję możliwość rejestracji nielimitu/odpadu na zleceniu montażowym/podzleceniu.
                  _chk_lim:=(exec('nd_is_rw','magdok_wspolne',DK.N) | exec('nd_is_zw','magdok_wspolne',DK.N)) & DK.ZLIM<>null()
                            & exec('FindAndGet','#table',ZLIM,DK.ZLIM,,
                               "ZLIM.PODZL<>'' & (ZLIM.SO='O' | ZLIM.SO='S' & ZLIM.LIMIT='N')",0);
                  _uidref:=DK.uidref();
                  _zlim:=DK.ZLIM;
                  _zgp:=DK.ZGP;
                  _zparn:=DK.PARTIA;

                  _prev:=$DK.ref();

                  _ok:=DK.del();

                  {? _chk_lim & _prev<>$DK.ref()
                  || exec('zlim_chk_podzl','zl_limit',_zlim)
                  ?};

                  {? _is_rp>0 & _prev<>$DK.ref()
                  ||
::                   Usunięcie powiązań z robocizną
                     exec('robocizna_dk_del','magdok_prod',_uidref,_zgp,_zparn)
                  ?};

                  {? ~_ok & ~exec('czy_kor','magdok_wspolne',_ref_kor) & _ref_kor<>''
                  ||
                     _msk:=form(8+_ref_kor);
                     ND.cntx_psh();
                     DK.cntx_psh();
                     {? _msk<>DK.name()
                     || ND.use('nagdo'+(_msk+3));
                        DK.use(_msk)
                     ?};
                     ND.prefix();
                     DK.prefix();
                     {? DK.seek(_ref_kor,) & ND.seek(DK.N) || ND.KOR:='N'; ND.put() ?};
                     DK.cntx_pop();
                     ND.cntx_pop()
                  ?};
                  _ok
               !}
            ?};
::          aktualizacja znacznikow na paletach
            {? _pal.size() & _pal.first()
            || {!
               |? exec('znacznik','magdok_palety',exec('FindAndGet','#table','PAL',_pal.PAL,,,null()));
                  _pal.next()
               !}
            ?};
            obj_del(_pal);
            exec('delprzyp','fakso','FAKSO','ND',ND.ref());
            FAKS_K.index('N');
            FAKS_K.prefix('N',ND.IDADD);
            {? FAKS_K.first() || {! |? FAKS_K.del() !} ?};
            {? do_state()<2 & ~Plugin.run('BEFORE_DELTAB_001',ND.ref()) & do_state() || undo() ?};
            POM.TAB:='ND';
            POM.TYPDOK:=ND.TYP().KOD;
            numer:=ND.NR;
            oldnumer:=1;
            exec('nr_old','numery');

            exec('nd_del','magdok_nag',$ND.ref());
::          wyczyszczenie informacji Z EANN
            _msk1:=EANP.name();
            _msk2:=form((ND.D~1)-2000,-2,0,'99');
            EANP.cntx_psh();
            EANN.cntx_psh();
            EANW.cntx_psh();
            EANN.use('eann'+(1+(_msk1+3))+_msk2);
            EANP.use('eanp'+(1+(_msk1+3))+_msk2);
            EANW.use('eanw'+(1+(_msk1+3))+_msk2);
            EANP.index('RNAG');
            EANP.prefix($ND.ref(),$ND.ref());
            {? EANP.first()
            || {!
               |? _ref:=EANP.ref(); _ok:=EANP.next();
                  EANP.cntx_psh();
                  EANP.clear();
                  {? EANP.seek(_ref)
                  || EANP.RSQL:='delete';
                     EANP.RNAG:='delete';
                     EANP.put(1);
                     EANN.clear();
                     {? EANN.seek(EANP.EANN) & EANN.REFSQL=$ND.ref()
                     || EANN.REFSQL:='delete';
                        EANN.put(1)
                     ?}

                  ?};
                  EANP.cntx_pop();
                  _ok
               !}
            ?};
            EANP.cntx_pop();
            EANN.cntx_pop();
            EANW.cntx_pop();
            EANN.cntx_psh();
            EANN.index('REFSQL');
            EANN.prefix($ND.ref(),$ND.ref());
            {? EANN.first()
            || {!
               |? _ref:=EANN.ref(); _ok:=EANN.next();
                  EANN.cntx_psh();
                  EANN.clear();
                  {? EANN.seek(_ref)
                  || {? EANN.AKT='T'
                     || EANP.index('EANN');
                        EANP.prefix(EANN.ref());
                        {? EANP.first()
                        || {!
                           |? EANW.index('EANP');
                              EANW.prefix(EANP.ref());
                              {? EANW.first() || {! |? EANW.del() !} ?};
                              EANP.del()
                           !}
                        ?};
                        EANN.del()
                  || EANN.REFSQL:='';
                     EANN.put(1)
                     ?}
                  ?};
                  EANN.cntx_pop();
                  _ok
               !}
            ?};
            EANN.cntx_pop();
::          wyczyszczenie informacji z DK_LN
            DK_LN.cntx_psh();
            DK_LN.index('ZDOK');
            DK_LN.prefix($ND.ref(),$ND.ref());
            {? DK_LN.first()
            || {!
               |? _dkln:=DK_LN.ref(); _ok:=DK_LN.next();
                  DK_LN.cntx_psh();
                  DK_LN.clear();
                  {? DK_LN.seek(_dkln)
                  || DK_LN.ZDOK:='';
                     DK_LN.put(1)
                  ?};
                  DK_LN.cntx_pop();
                  _ok
               !}
            ?};
            DK_LN.cntx_pop();

            _mag:=ND.MAG;
            _plus:=ND.TYP().P;
::          usuniecie informacji o realizacjach
            {? _plus='T'
            || ZD_RN.cntx_psh();
               ZD_RN.index('ND');
               ZD_RN.prefix($ND.ref());
               {? ZD_RN.first()
               || {!
                  |? _zd:=ZD_RN.ref();
                     _next:=ZD_RN.next();
                     ZD_RN.cntx_psh();
                     ZD_RN.clear();
                     {? ZD_RN.seek(_zd) || ZD_RN.ND:=''; ZD_RN.SPZ:=''; ZD_RN.put(1) ?};
                     ZD_RN.cntx_pop();
                     _next
                  !}
               ?};
               ZD_RN.cntx_pop()
            ?};
::          usuwa podziały controllingowe
            SCH_XD.cntx_psh(); SCH_XD.use('scxd_'+(ND.name()+3));
            SCH_XD.index('ND'); SCH_XD.prefix(ND.ref());
            {? SCH_XD.first() || {! |? SCH_XD.del() !} ?};
            SCH_XD.cntx_pop();
::          aktualizacja dyspozycji w magazynie
            exec('delDYSPdok','transport',ND.uidref());
            {? ~_f
            || _wyn:=ND.del(1,1)
            ?};
            {? _b || end() ?};
            _tran:=do_state();

::          aktualizacja rezerwacji po usunieciu dokumentu
::          tylko gdy nie ma transakcji z uwagi na mozliwosc blokowania rekordow
::          aktualizuje stany magazynowe

            {? ~_tran & _il_poz>0 & (_b | _g | _inw) & _plus='N'
            || _mat.clear();
               {? _mat.first()
               || {!
                  |? _refmate:=exec('FindAndGet','#table','M',_mat.MAT,,,null());
                      exec('obl_stan','magazyn_stan',_refmate,1,_mag);
                      exec('aktu_rez','rezerwacje',_refmate,_mat.NRK,_mat.ILE);
                     _mat.next()
                  !}
               ?}
            ?}
         ?}
       ?}
   ?}
?};
{? _wyn=0 || ND.r_unlock() ?};
_wyn


\akc_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja akceptuj dokument magazynowy
::       !! UWAGA -> nie ma juz miejsca na zmienne lokalne, nalezy uzywac globalnych
::   WE: [_a] - czy pytac o akceptacje dokumentu (T/N)
::       [_b] - czy wyswietlac drzewko z informacjami (T/N) - domyslnie T
::       [_c] - czy odblokowywac naglowek = domyslnie 1=tak 0-nie
::       [_d] - czy trwa realizacja zamowienia (1/0) - domyslnie 0
::       [_e] - czy trwa transakcja
::       [_f] - inicjowac tabele komunikatow 1-domyslnie=tak, 0-nie
::   WY: czy zaakceptowano dokument 1 - tak, 0 lub 2 nie
::  OLD: \akc_dok/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
_service:=0;
_group:=0;
{? var_pres('_a')=type_of(obj_new('a'))
|| _aa:=_a.ASK;
   _b:=_a.INFO;
   _c:=_a.ND_UNLOCK;
   _d:=_a.REA;
   _e:=_a.DO_STATE;
   _f:=_a.KOMM;
   _service:=_a.SERVICE;
   _group:=_a.GROUP;
   obj_del(_a);
   _a:=_aa
|| {? _>=1 || {? type_of(_a)<>2 || _a:='T' ?} || _a:='T' ?};
   {? _>=2 || {? type_of(_b)<>2 || _b:='T' ?} || _b:='T' ?};
   {? _>=3 || {? type_of(_c)<>1 || _c:=1 ?} || _c:=1 ?};
   {? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
   {? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};
   {? _>=6 || {? type_of(_f)<>1 || _f:=1 ?} || _f:=1 ?}
?};

{? _group
|| _a:='N';
   _b:='N';
   _f:=0
?};

POM.PAR1:='';
POM.PAR2:='';
BEER.NDPOW:=null();
ND.get();

:: ustawienie zmiennych
VAR_DEL.delete('__akcept','__lp','__czy_c_sp','__cena_e','__cena','__ktm','__spr_cene','__txt','__wyb_nr','__pprod'
   ,'__gen_mm','__typ_mm','__kod_mm','__nrt_mm','__l_log','__buf_kom','__nr0','__Real','__wybnr1','__znrt','__coscn');
VAR_DEL.delete('__st_oddz','__md_oddz','__dk_m','__dk_il','__dk_n_d','__dk_dk_c','__dk_prdk','__dk_war','__dk_tw'
   ,'__nd_md','__nd_kk','__nd_d','__nd_kh','__nd_ref','__nd_nam','__nd_o','__nd_zl','__nd_wyd','__tmgo','__bufpal'
   ,'__nd_t','__nd_p');
VAR_DEL.delete('__aktzkp');

__aktzkp:=tab_tmp(1,'MAT','STRING[16]',''
           ,'NRK','INTEGER',''
           ,'ILR','REAL','');

__bufpal:=tab_tmp(1,'PAL','STRING[16]','');
__coscn:=obj_new(6); __coscn[1]:=__coscn[2]:=__coscn[3]:=__coscn[4]:=__coscn[5]:=__coscn[6]:=0;

_wyn:=0;_ref:=null;_refn:=null;_ref_old:=ND.ref; ZMIENNE.WAR_SKL:=0;
__pprod:=exec('tte_lic','tte');

:: zapamietuje nagłówek akceptowanego dokumentu
_main_dok:=ND.ref();

:: typ magazynu
_tmg:={? ND.MAG().TYP*'EWI' & ND.INN='T' & ND.TYP().INW='E' || 'E' || (1+ND.MAG().TYP) ?};
_mgi:=ND.MAG().IL='T';
__tmgo:=_tmg;
:: dokumenty inwentaryzacji
{? _tmg<>'E' & ND.INN='T' & ND.TYP().INW='I' &  ND.TYP().P='N' || _tmg:='D' ?};
:: autofifo
{? _tmg='D' & (ND.TYP().AFIFO<>'' | ND.MAG().IL='T') || _tmg:=ND.TYP().AFIFO; {? _tmg='' || _tmg:='F' ?} ?};
:: inwentaryzacja zawsze wg dostaw
{? ND.TYP().DK='I' || _tmg:='D' ?};

MG.cntx_psh();
_tmg2:=(1+ND.MD().TYP);
MG.cntx_pop();
__akcept:='';
:: wlaczenie tabeli komunikatow
{? ND.sel_size=0 & _f=1 || exec('ini_kom','#message','Informacje o dokumentach'@,,,,200) ?};__lp:=0;
__kom_gr:=ND.SYM+', magazyn: '+ND.MAG().SYM;
:: formula dla nieprawidlowych parametrow
_esc:="{? _a=0 || exec('akcadok','magdok_nag',_b) ?}";

ND.cntx_psh();
DK.cntx_psh();
DK_C.cntx_psh();
{? exec('blok_nd','magdok_nag',{? ND.sel_size=0 || 'T' || 'N' ?})
||
   exec('dk_sum','magdok_wspolne',ND.ref());
:: sprawdzanie czy mozna zaakceptowac dokument

   _czy_akc:=1;
   {? _a='T' & ND.sel_size=0 & ND.STAT_REJ='N'
   || _czy_akc:=FUN.ask('Czy zakończyć redakcję dokumentu %1 o symbolu: %2?'@[ND.TYP().T,ND.SYM])
   |? ND.STAT_REJ='T'
   || _txt:='Dokument jest już zaakceptowany.'@;
      {? _group
      || exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
      || exec('add_kom','#message',_txt,2)
      ?};
      _czy_akc:=0
   ?};

   {? _czy_akc=1
   || {? ND.TYP().Z='T' & ND.KH=null
      || _txt:='Wymagany kontrahent w nagłówku dokumentu.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || _txt:=_txt+' '+'Dokument nie został zaakceptowany.'@;
            exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? ND.TYP().WK='T' & ND.KK=null
      || _txt:='Wymagane konto w nagłówku dokumentu.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || _txt:=_txt+' '+'Dokument nie został zaakceptowany.'@;
            exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? ND.TYP().UE='T' & ND.NIP_UE=''
      || _txt:='Wymagany NIP UE.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || _txt:=_txt+' '+'Dokument nie został zaakceptowany.'@;
            exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? ND.MAG().SKL='T' & ND.SCPOZ=null
      || _txt:='Wymagane pole pozwolenie w nagłówku dokumentu.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || _txt:=_txt+' '+'Dokument nie został zaakceptowany.'@;
            exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? ND.MAG().SKL='T' & ND.TYP().TD='' & ND.WD=''
      || _txt:='Wymagane pole warunki dostawy w nagłówku dokumentu.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || _txt:=_txt+' '+'Dokument nie został zaakceptowany.'@;
            exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? ~exec('ctrldkzl','magdok_wspolne',ND.ref())
      || _txt:='Wymagane uzupełnienie zleceń lub grup operacji na pozycjach dokumentu.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? _tmg='D' & ND.TYP().P='T' & (ND.TYP().DN='T' | (ND.INN='T' & ND.TYP().INW='I' &  ND.TYP().P='T')) & ND.FAKS_REF='' & ND.NDSQL=''
       & ~exec('ctrlilzw','magdok_wspolne',ND.ref())
      || _txt:='Zbyt duża ilość do zwrotu na dokumencie (większa od pierwotnej dostawy). '
          'UWAGA. Dostawa może być na kilku pozycjach.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? {? ND.TYP().P='N'
         || ND.TYP().AFIFO='' & ND.MAG().IL<>'T'
         || ND.NDSQL='' | exec('get','#params',500705,2)*(ND.TYP().T+' ')
         ?}
        & ~exec('ctrl_atr','magdok_wspolne',ND.ref())
      || _txt:='Wymagane uzupełnienie cech dostaw na pozycjach dokumentu.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? ~(ND.INN='T' & ND.TYP().INW='I' &  ND.TYP().P='N') & ND.TYP().P='N' & ND.MAG().SL='T' & ND.MAG().PAL='N'
       & exec('get','#params',600200,2)='T'
      || _czy_akc:=exec('ctrl_wym','magdok_wspolne',0,_group)
      |? (ND.INN='T' & ND.TYP().INW='E' &  ND.TYP().P='T') & ~exec('sprakcmi','magdok_nag')
      || _txt:='Nie zaakceptowano dokumentu PRC- dla danej przeceny.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || _txt:=_txt+' '+'Zakończenie dokumentu niemożliwe.'@;
            exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? ND.MAG().TYP*'EWI' & ~exec('ctrlEWID','magdok_nag',ND.ref())
      || _txt:='Na dokumencie są niewycenione pozycje '
          'oraz nie zaakceptowano wcześniejszych dokumentów.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || _txt:=_txt+' '+'Akceptacja dokumentu niemożliwa.'@;
            exec('add_kom','#message',_txt,2)
         ?};
         _czy_akc:=0
      |? ~exec('ctrlTAXJ','oplaty_dod',ND.uidref())
      || _czy_akc:=0
      ?}
   ?};

:: sprawdza czy KH nie jest zablokowany
   {? _czy_akc
   ||
      {? ND.TYP().P<>'T'
      ||
         exec('list_zam','magdok_wspolne',ND.ref,0);
         _kh_dod_b:=exec('kh_dod_b','kontrahent','nd_akc',ND.KH,__list_zk.first());
         {? _kh_dod_b='B'
         || _txt:='Kontrahent %1 jest zablokowany bezwarunkowo.'@[ND.KH().SKR];
            {? _group
            || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
            || _txt:=_txt+' '+'Dokument nie został zaakceptowany.'@;
               exec('add_kom','#message',_txt,2)
            ?};
            _czy_akc:=0
         |? _kh_dod_b='W'
         || _txt:='Kontrahent %1 jest zablokowany warunkowo.'@[ND.KH().SKR];
            {? _group
            || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
            || exec('add_kom','#message',_txt,2)
            ?}
         ?}
      ?}
   ?};

   {? ND.Z='N' & _czy_akc
   ||
::    sprawdzanie zamkniecia okresu
      _czy_akc:=exec('czy_z_ok','okresy',1,0,ND.D~1,ND.D~2,ND.MAG);
      {? _czy_akc=0
      || _txt:='Okres jest zamknięty.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || exec('add_kom','#message',_txt,4)
         ?}
      ?};
      _esc(_czy_akc,_f);

::    sprawdzenie wypelnienia dokumentu magazynowego

      {? _czy_akc=1
      ||
         DK.index('DOKMAG');
         DK.prefix(ND.ref);
         {? DK.first()
         ||
            _czy_akc:=exec('ok_dk','magdok_poz')
         ||
            _czy_akc:=0;
            _txt:='Dokument magazynowy nie ma pozycji materiałowych/usługowych '
             'i nie może być zakończony.'@;
            {? _group
            || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
            || exec('add_kom','#message',_txt,2)
            ?}
         ?};
         _esc(_czy_akc,_f)
      ?};

::    czy material nie wystepuje na inwentaryzacji
      {? exec('spr_ndin','magdok_poz',ND.ref())=0
      ||
         _txt:='Materiał w pozycjach dokumentu występuje w niezakceptowanym arkuszu inwentaryzacyjnym.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || exec('add_kom','#message',_txt,2);
            exec('add_kom','#message','Data dokumentu jest wcześniejsza lub równa dacie rozpoczęcia danej inwentaryzacji.'@,2)
         ?};
         _czy_akc:=0;
         _esc(_czy_akc,_f)
      ?};
::    czy material nie wystepuje na inwentaryzacji
      {? exec('spr_ndin','magdok_poz',ND.ref(),1)=0
      ||
         _txt:='Materiał w pozycjach dokumentu występuje w arkuszu przecen.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || exec('add_kom','#message',_txt,2);
            exec('add_kom','#message','Data i godzina dokumentu jest wcześniejsza lub równa dacie i godzinie przeceny.'@,2)
         ?};
         _czy_akc:=0;
         _esc(_czy_akc,_f)
      ?};

::    czy wypełniona stawka vat na zewnętrznym dokumencie przychodowym
      {? _poz:=exec('spr_stv','magdok_poz',ND.ref())
      || {? _group
         || _txt:='Pozycja bez stawki VAT'@;
            exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || exec('add_kom','#message','Pozycja %1 nie ma stawki vat.'@[$_poz],2)
         ?};
         _czy_akc:=0;
         _esc(_czy_akc,_f)
      ?};

::    sprawdzanie limitu kredytowego
      {? _czy_akc=1
      || _czy_akc:=exec('klim_akc','magdok_nag',_d);
         _esc(_czy_akc,_f)
      ?};

::    oznaczenie palet
      {? ND.MAG().PAL='T' || exec('ozpalakc','magdok_poz',ND.ref()) ?};
::    sprawdzenie poprawnosci wypelnienia wymiarow
      {? ND.MAG().SL='T' & exec('sprndlok','magdok_poz',~_group)<>''
      || _czy_akc:=0;
         _esc(_czy_akc,_f)
      ?};

::    sprawdzenie poprawnosci wypelnienia palet dla przesuniecia na magazyn paletowy
      {? ND.MAG().PAL='N' & ND.MD<>null & ND.MD().PAL='T'
      || _czy_akc:=exec('sprndpal','magdok_palety',ND.ref(),~_group);
         _esc(_czy_akc,_f)
      ?};
      __gen_mm:=0; __typ_mm:=null;
      {? _czy_akc=1 & ND.TYP().TD<>''
      ||
         _typ_dok:=exec('get','#params',1001,2,OPERATOR.USER);
         {? _typ_dok<>''
         ||
            {? (_typ_dok*(ND.TYP().TD+' ')) =0
            || _czy_akc:=0;
               _txt:='Brak uprawnień do tworzenia dokumentów typu %1.'@[ND.TYP().TD];
               {? _group
               || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
               || exec('add_kom','#message',_txt,2)
               ?}
            ?}
         ?};
         {? ND.MD<>null
         ||
            __gen_mm:=exec('spr_info_mm','magdok_nag',~_group);
            {? __mm_utw.first
            ||
               _txt:='Akceptowany dokument utworzył dokument %1'
                  ' nr %2 w magazynie %3.'@[ND.TYP().TD,$__mm_utw[2],ND.MD().SYM];
               {? _group
               || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
               || exec('add_kom','#message',_txt,4)
               ?}
            |? __gen_mm
            || TYPYDOK.cntx_psh();
               {? TYPYDOK.index('TYP');TYPYDOK.clear();~TYPYDOK.find_key(ND.TYP().TD)
               ||
                  _czy_akc:=0;
                  _txt:='Typ dokumentu %1'
                     ' nie istnieje. Dokument nie będzie utworzony.'@[ND.TYP().TD];
                  {? _group
                  || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                  || exec('add_kom','#message',_txt,2)
                  ?}
               || __typ_mm:=TYPYDOK.ref;
                  __kod_mm:=TYPYDOK.KOD;
                  __nrt_mm:=TYPYDOK.NRT
               ?};
               TYPYDOK.cntx_pop();

               __st_oddz:='';
               {? ST.ODDZ<>ND.MD().ODDZ || __st_oddz:=ST.ODDZ; ST.ODDZ:=ND.MD().ODDZ ?};
               {? exec('czy_z_ok','okresy',1,0,ST.AR,ST.AM,ND.MD)=0
               || _czy_akc:=0;
                  _txt:='W magazynie %1 zablokowano wystawianie dokumentów '
                     '(okres jest zamknięty)'@[ND.MD().SYM];
                  {? _group
                  || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                  || exec('add_kom','#message',_txt,2)
                  ?}
               ?};
               {? __st_oddz<>'' || ST.ODDZ:=__st_oddz ?}
            ||
               _czy_akc:=0
            ?};
            obj_del(__mm_utw)
         || _czy_akc:=0;
            _txt:='Nie wypełniono magazynu.'@;
            {? _group
            || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
            || exec('add_kom','#message',_txt,2)
            ?}
         ?}
      ?};
      {? _czy_akc=0 & ~_group || exec('add_kom','#message','Dokument nie został zaakceptowany.'@,2) ?};
      _esc(_czy_akc,_f)
   ||
      {? _czy_akc=1 & _a='T' || exec('add_kom','#message','Dokument jest już zaakceptowany.'@,4);_czy_akc:=0 ?}
   ?};

:: Produkcja - generowanie powiązań z partiami dla RP
   {? _czy_akc=1 & __pprod='T' & ND.sel_size()=0
   || {? (' '+exec('get','#params',500701,2,null()))*(' '+ND.TYP().T+' ')>0
         & exec('nd_is_rp','magdok_wspolne',ND.ref()) & exec('nd_party','zl_common',ND.ref())>0
      ||
::       Generowanie powiazan partii z wydaniami surowcow
         {? _service=0
:: NUCO - ominięcie okna z informacjami o rozliczeniu partii jeśli wszystko jest ok
         || _czy_akc:=exec('creator_run','magdok_partie',ND.ref(),,0)
         ?}
      ?}
   ?};

   {? _czy_akc>0 & __pprod='T' & ND.sel_size()=0 & exec('nd_is_rp','magdok_wspolne',ND.ref())>0
   ||
::    Sprawdzanie wartosci atrybutow na dokumencie i zleceniu
      {? _czy_akc>0
      || _czy_akc:=exec('chk_atr_zl','magdok_nag',ND.ref(),~_group)
      ?}
   ?};

   {? _czy_akc>0 & __pprod='T' & ND.sel_size()=0 & exec('nd_is_rw','magdok_wspolne',ND.ref())>0
   ||
::    Sprawdzanie wartosci atrybutow na dokumencie i limicie
      {? _czy_akc>0
      || _czy_akc:=exec('chk_atr_zlim','magdok_nag',ND.ref(),~_group)
      ?}
   ?};
   {? _czy_akc>0 & __pprod='T' & ND.sel_size()=0 & exec('nd_is_koop','magdok_wspolne',ND.ref())>0
   ||
::    Sprawdzanie wypełnienie pola partii dla wydań kooperacji
      {? _czy_akc>0
      || _czy_akc:=exec('chk_partia_dk','magdok_nag',ND.ref(),~_group)
      ?}
   ?};
:: Koniec fragmentu produkcyjnego
:: akceptacja dokumentu
   {? _czy_akc
   ||
::    numeracja tymczasow - wyznaczenie ostatecznego numeru
      __nr0:=ND.NR=0;
      {? __nr0
      ||
         __Real:={? ND.TYP().P='N' || ZK_RN || ZD_RN ?};
         POM.TYPDOK:=ND.TYP().KOD;
         POM.TAB:='ND';
         POM.NRT:=0;

         ND.cntx_psh;
         __wybnr1:=exec('numer_new','numery','N');
         ND.NR:=__wybnr1;
         exec('znak','numery','ND',2);
         __wybsy1:=ND.SYM;
         ND.cntx_pop
      ?};

::    szukanie wolnych numerow dla generowanego dokumentu
      __wyb_nr:=0;
      __znrt:=~(__akcept='T' | __akcept<>'T' & TYPYDOK.NRT=0);
      {? __gen_mm
      ||
::       numeracja tymczasowa - wyznaczanie numeru dla generowanego dokumentu jesli od razu jest akceptowany
         __nd_md:=ND.MD;
         __nd_kk:=ND.KK;
         __nd_d:=ND.D;
         __nd_kh:=ND.KH;
         __nd_ref:=#ND.ref();
         __nd_nam:=ND.name();
         __nd_o:=ND.O;
         __nd_zl:=ND.ZL;
         __nd_wyd:=ND.WYD;
         __nd_t:=ND.T;
         __nd_p:=ND.PROJEKTY;

         ND.cntx_psh();
         __st_oddz:='';
         {? ST.ODDZ<>ND.MD().ODDZ
         ||
            __st_oddz:=ST.ODDZ;
            ST.ODDZ:=ND.MD().ODDZ;
            exec('open','open_tab',ST.ODDZ,2-$ST.AR)
         ?};
         __ptab:=POM.TAB;
         __ptyp:=POM.TYPDOK;
         __pnrt:=POM.NRT;
::       ustawienie numeracji dla mm
         POM.TAB:='ND';
         POM.TYPDOK:=__kod_mm;
         POM.NRT:=__nrt_mm;

         exec('addnag','magdok_nag',__nd_md,ST.AR,ST.AM,__typ_mm,'N',__nd_kk,__nd_d,
          ,__nd_kh,,__nd_ref,__nd_nam,__nd_o,__nd_zl,__nd_wyd,1,,,,__nd_t,__nd_p);
         {? (__akcept='T' | __akcept<>'T' & TYPYDOK.NRT=0) & exec('spr_wol','numery')
         || {? _group | FUN.ask('W numeracji dokumentów generowanych podczas\n'
                  'akceptacji dokumentu występują wolne numery.\n'
                  'Czy chcesz je wykorzystać?'@)
            || __znrt:=0;
               __wyb_nr:=exec('nr_dok','numery','ND','T')
            ?}
         ?};
::       zamiana na numerację dokumentu i zapamiętanie dla mm
         __ptab==POM.TAB;
         __ptyp==POM.TYPDOK;
         __pnrt==POM.NRT;
         {? __st_oddz<>''
         ||
            ST.ODDZ:=__st_oddz;
            exec('open','open_tab',ST.ODDZ,2-$ST.AR)
         ?};
         ND.cntx_pop()
      ?};
      __l_log:=exec('llog_pocz','users',$ND.ref,'Zakończenie redakcji dokumentu magazynowego '+ND.SYM);

      {? ~_e || do() ?};
::    numeracja tymczasowa - przypisanie ostatecznego numeru, nadanie symbolu, aktualizacja w dokumentach powiazanych
      {? __nr0
      ||
         ND.NR:=__wybnr1;
         ND.SYM:=__wybsy1;
         {? ~_group
         || exec('add_kom','#message','Nadano numer, symbol: %1, %2'@[form(ND.NR,,,'99'),ND.SYM],7,,,0)
         ?};
         {? ND.put
         ||
::          zapamiętanie ND.uidref() w numeracji
            exec('nr_uidref','numery',ND.NR);
            {?  ND.TYP().INW<>'N'
            ||
               INN.cntx_psh;
               {? ND.TYP().INW='E' || INN.use('inw_nwyc') ?};
               INN.index({? ND.TYP().P='T' || 'INP_R' || 'INR_R' ?});
               INN.prefix($ND.ref);
               {? INN.first()
               || {?  ND.TYP().P='T' || INN.INP:=ND.NR; INN.INP_S:=ND.SYM || INN.INR:=ND.NR; INN.INR_S:=ND.SYM ?};
                  INN.put
               ?};
               INN.cntx_pop
            ||
               __Real.cntx_psh; OKR.cntx_psh;
               __Real.use({? ND.TYP().P='N' || 'zkhin' || 'zdhin' ?}+ST.ODDZ+'__');
               __Real.index('ND'); __Real.prefix($ND.ref);
               {? __Real.first
               || __reals:='';
                  {! |? __reals+=$__Real.ref; __Real.next !};
                  {!
                  |? __reals<>''
                  |!
                     {? __Real.seek(__reals+16)
                     || {? ND.TYP().P='N' || __Real.SWZ:=ND.SYM || __Real.SPZ:=ND.SYM ?};
                        __Real.put
                     ?};
                     __reals-=16
                  !}
               ?};
               OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
               {? OKR.first
               ||
                  {!
                  |?
                     __Real.use({? ND.TYP().P='N' || 'zkhin' || 'zdhin' ?}+ST.ODDZ+(form(OKR.ROK,,,'99')+2));
                     __Real.index('ND'); __Real.prefix($ND.ref);
                     {? __Real.first
                     ||
                        __reals:='';
                        {! |? __reals+=$__Real.ref; __Real.next !};
                        {!
                        |? __reals<>''
                        |!
                           {? __Real.seek(__reals+16)
                           || {? ND.TYP().P='N' || __Real.SWZ:=ND.SYM || __Real.SPZ:=ND.SYM ?};
                              __Real.put
                           ?};
                           __reals-=16
                        !}
                     ?};
                     OKR.next
                  !}
               ?};
               __Real.cntx_pop; OKR.cntx_pop;
               VAR_DEL.delete('__reals')
            ?}
         ?}
      ?};
::    naliczenie kosztow skladu celnego
      exec('nal_kos','magdok_koszty');

::    _md_oddz<>'' gdy przesuniecie do innego oddzialu
      __st_oddz:=__md_oddz:='';
::    tworzenie dokumentu powiazanego
      {? __gen_mm
      ||
         __nd_md:=ND.MD;
         __nd_kk:=ND.KK;
         __nd_d:=ND.D;
         __nd_kh:={? ND.KH<>null() || ND.KH
                  |? ND.MD<>null() & ND.MD().COS='T' & ND.DLAKH<>''
                  || exec('FindInSet','#table','KH','KOD',ND.DLAKH,2,,,,null())
                  || null()
                  ?};
         __nd_ref:=#ND.ref();
         __nd_nam:=ND.name();
         __nd_o:=ND.O;
         __nd_zl:=ND.ZL;
         __nd_wyd:=ND.WYD;
         __nd_t:=ND.T;
         __nd_p:=ND.PROJEKTY;

         ND.cntx_psh();
         {? ST.ODDZ<>ND.MD().ODDZ & ND.TYP().P='N'
         ||
            __st_oddz:=ST.ODDZ;
            __md_oddz:=ND.MD().ODDZ;

            ST.ODDZ:=__md_oddz;
            exec('open','open_tab',ST.ODDZ,2-$ST.AR)
         ?};

::       zamiana na numerację dla mm i zapamiętanie dla dokumentu
         __ptab==POM.TAB;
         __ptyp==POM.TYPDOK;
         __pnrt==POM.NRT;
         _ref:=exec('addnag','magdok_nag',__nd_md,ST.AR,ST.AM,__typ_mm,'N',__nd_kk,__nd_d,
                ,__nd_kh,,__nd_ref,__nd_nam,__nd_o,__nd_zl,__nd_wyd,,__wyb_nr,__znrt,,__nd_t,__nd_p);
::       ustawienie numeracji dla dokumentu
         POM.TAB:=__ptab;
         POM.TYPDOK:=__ptyp;
         POM.NRT:=__pnrt;
         VAR_DEL.delete('__ptab','__ptyp','__pnrt');

         {? ND.clear; ND.seek(_ref)
         ||
            BEER.MSK:='nagdo';
            exec('inf_dod','fakso',-1,'nagdo');
            {? _group
            || _txt:='Utworzono dokument w magazynie %1.'@[ND.MAG().SYM];
               exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
            || _txt:='Utworzono dokument %1 w magazynie %2.'@[ND.SYM,ND.MAG().SYM];
               exec('add_kom','#message',_txt,7,,,0)
            ?};
            BEER.NDPOW:=ND.ref()
         ?};

         {? __md_oddz<>''
         ||
            ST.ODDZ:=__st_oddz;
            exec('open','open_tab',ST.ODDZ,2-$ST.AR)
         ?};
         ND.cntx_pop();

         {? ND.TYP().P<>'T' & ND.TYP().DN<>'D'
         ||
::          generacja dokumentu przychodowego
            {? DK.first()
            ||
               __sklad:=ND.MAG().SKL='T';
               _czypal:=DK.N().MD().PAL='T';
               ND.cntx_psh; MG.cntx_psh;
               _mgsl:=DK.N().MAG().SL='T' & DK.N().MD().SL='T';
               _mgeanl:=DK.N().MD().EANL;
               ND.cntx_pop; MG.cntx_pop;
               _pal:=tab_tmp(1,'PAL','STRING[16]','');
               {!
               |?
                  _mat:=DK.M;
                  _mag:=DK.N().MD;
                  _czyid:=(';DAPZ'*DK.M().IDMOB)>1;

                  __cena:=DK.C;
                  {? (DK.N().MD().TYP*'EWI')
                  ||
                     __cena_e:=exec('biez_cen','ceny_dok',_mat,_mag,DK.N().D,,,1);
                     {? {? __cena_e=-1 || __cena_e:=0; 1 || __cena_e<>0 ?} & __cena_e<>__cena
                     ||
::                      domyslnie  cena na przyjeta wg ewidencji
                        __cena:=__cena_e
                     ?}
                  |? __sklad=1
                  || __cena:=exec('cena','ceny_dok',DK.ref(),ND.SCKRS,DK.WAR,DK.IL,DK.RDK,DK.NDK)
                  ?};
                  {? DK.J2<>null() & DK.J2<>DK.M().J
                  || exec('przyjdod','jm',$DK.J2,DK.WS2,DK.IL2)
                  || _dokl:=exec('jaka_dok_mjm','jm',DK.M,DK.J2,DK.M().J);
                     _il22:={? DK.WS2 || DK.IL/DK.WS2 || 0 ?} $ (_dokl+1);
                     roundmet(BEER.MDOKL);
                     _il22:=_il22 $ _dokl;
                     exec('przyjdod','jm',{? DK.J2=null || $DK.M().J || $DK.J2 ?},
                     {? DK.WS2<>0 || DK.WS2 || 1 ?},{? _dokl>=0 || _il22 || DK.IL2 ?});
                     roundmet(5)
                  ?};

                  __dk_m:=DK.M;
                  __dk_il:=DK.IL;
                  __dk_n_d:=DK.N().D;
                  __dk_dk_c:=exec('odd_matr','mat_atr',__md_oddz,ST.ODDZ,DK.DK_C);
                  __dk_prdk:={? DK.M().SETW='P'
                              & (exec('FindAndGet','#table',DK,DK.PRDK,,"TW",date(0,0,0))<>DK.TW
                               | exec('FindAndGet','#table',DK,DK.PRDK,,"SCEAN",'')<>DK.SCEAN)
                             || DK.SRDK
                             || DK.PRDK
                             ?};
                  __dk_war:=DK.WAR;
                  __dk_tw:={? DK.M().SETW='P' & DK.TW=date(0,0,0)
                           || exec('FindInSet','#table','DK_L','DK',null(),DK.ref(),"@.DK_L.TW",,,date(0,0,0))
                           || DK.TW
                           ?};
                  _dk_src:=DK.PRDK;
                  _dk_sts:=DK.STATS;
                  _scean:={? _czyid
                          || exec('FindAndGet','#table',DK,__dk_prdk,,"SCEAN",'')
                          || ''
                          ?};

                  {? exec('FindAndGet','#table',ND,_ref,,"MAG().COS<>'T'",1)
                  || __coscn[1]:=__coscn[2]:=__coscn[3]:=__coscn[4]:=__coscn[5]:=__coscn[6]:=0
                  || __coscn[1]:=DK.RAB; __coscn[2]:=DK.CN; __coscn[3]:=DK.CB;
                     __coscn[4]:=DK.WN;  __coscn[5]:=DK.WV; __coscn[6]:=DK.WB
                  ?};

                  {? __md_oddz<>''
                  || DK.cntx_psh();
                     DK_C.cntx_psh();
                     DK_L.cntx_psh();
                     ST.ODDZ:=ST.ODDZ_KOD:=__md_oddz;
                     exec('open','open_tab',ST.ODDZ,2-$ST.AR)
                  ?};

                  VAR_DEL.delete('__dk_p');
                  __dk_p:=DK.PROJEKTY;
                  _refnew:=exec('adddk','magdok_poz',_ref,__dk_m,__dk_il,__cena,__dk_n_d
                            ,__coscn[1],__coscn[2],__coscn[3],__coscn[4],,__coscn[5],__coscn[6],7
                            ,,,__dk_dk_c,,__dk_prdk,,,,,__dk_war,,_scean,__dk_tw);
                  VAR_DEL.delete('__dk_p');
                  _mskdkl:=DK_L.name();

::                dodanie historii dla nowo powstalego
                  exec('DK_HS_new4DK','statexam',_refnew,'M',,,_dk_sts,_dk_src,1);
                  {? __md_oddz<>''
                  ||
                     ST.ODDZ:=ST.ODDZ_KOD:=__st_oddz;
                     exec('open','open_tab',ST.ODDZ,2-$ST.AR);
                     DK.cntx_pop();
                     DK_C.cntx_pop();
                     DK_L.cntx_pop()
                  ?};

                  {? _czypal & ~DK.ZP
                  || DK.cntx_psh();
                     DK_L.index('DK');
                     DK_L.prefix(DK.ref(),null);
                     {? DK_L.first()
                     || {!
                        |? DK_L.cntx_psh();
                           DK_L.use(_mskdkl);
                           DK_L.clear();
                           DK_L.DK:=_refnew;
                           {? DK_L.UIDDO<>''
                            & exec('FindAndGet','#table',EANL,DK_L.UIDDO,,"MG",null())=DK_L.DK().N().MAG
                           || DK_L.LOK:=exec('FindAndGet','#table',EANL,DK_L.UIDDO,,,null());
                              {? DK_L.LOK=null() || DK_L.LOK:=_mgeanl ?}
                           || DK_L.LOK:=_mgeanl
                           ?};
                           DK_L.UIDDO:='';
                           DK_L.SCSQL:=$_refnew;
                           DK_L.TM+=0.03;
                           exec('reoIL2','magdok_wymiary');
                           exec('uzupIDkod','magdok_palety',DK_L);
                           {? DK_L.add() & _tmg='D' & DK_L.PAL().STAN='N'
                           || DK_L.cntx_pop();
                              PAL_POZ.cntx_psh();
                              PAL_POZ.clear();
                              PAL_POZ.blank();
                              PAL_POZ.PAL:=DK_L.PAL;
                              PAL_POZ.IL:=DK_L.IL;
                              PAL_POZ.ILP:=DK_L.IL;
                              PAL_POZ.AKT:='T';
                              PAL_POZ.M:=DK_L.DK().M;
                              PAL_POZ.TW:=DK_L.TW;
                              PAL_POZ.JM:=DK_L.DK().M().J;
                              PAL_POZ.C:=DK_L.DK().C;
                              PAL_POZ.DK_C:=DK_L.DK().DK_C;
                              exec('uzupIDkod','magdok_palety',PAL_POZ);
                              PAL_POZ.add(1);
                              PAL_POZ.cntx_pop();
                              _pal.clear();
                              {? ~_pal.find_key($DK_L.PAL)
                              || _pal.blank();
                                 _pal.PAL:=$DK_L.PAL;
                                 _pal.add(1)
                              ?}
                           || DK_L.cntx_pop()
                           ?};
                           DK_L.next()
                        !}
                     ?};
                     DK.cntx_pop()
                  |? _mgsl & ~DK.ZP
                  || DK.cntx_psh();
                     DK_L.index('DK');
                     DK_L.prefix(DK.ref(),null);
                     {? DK_L.first()
                     || {!
                        |? {? DK_L.TW<>date(0,0,0) | DK_L.M().SETW<>'P' | DK_L.UIDDO<>''
                           ||
                              DK_L.cntx_psh;
                              DK_L.use(_mskdkl);
                              DK_L.clear;
                              DK_L.DK:=_refnew;
                              {? DK_L.UIDDO<>''
                               & exec('FindAndGet','#table',EANL,DK_L.UIDDO,,"MG",null())=DK_L.DK().N().MAG
                              || DK_L.LOK:=exec('FindAndGet','#table',EANL,DK_L.UIDDO,,,null());
                                 {? DK_L.LOK=null() || DK_L.LOK:=_mgeanl ?}
                              || DK_L.LOK:=_mgeanl
                              ?};
                              DK_L.UIDDO:='';
                              DK_L.SCSQL:=$_refnew;
                              DK_L.AUTO:=0;
                              DK_L.TM+=0.03;
                              exec('reoIL2','magdok_wymiary');
                              {? DK_L.LOK().MG().PAL='N'
                              || DK_L.PAL:=DK_L.PALDO:=null();
                                 DK_L.IDPAL:=DK_L.IDPALD:=DK_L.KODPAL:=DK_L.KODPALD:=''
                              || exec('uzupIDkod','magdok_palety',DK_L)
                              ?};
                              DK_L.add;
                              DK_L.cntx_pop
                           ?};
                           DK_L.next
                        !}
                     ?};
                     DK.cntx_pop()
                  ?};

                  DK.next()
               !};
::             akceptacja palet
               {? _tmg='D' & _czypal
               || PAL.cntx_psh();
                  _pal.clear();
                  {? _pal.first()
                  || {!
                     |? PAL.clear();
                        {? PAL.seek(_pal.PAL)
                        || PAL.STAN:='T';
                           PAL.put(1);
                           exec('znacznik','magdok_palety',PAL.ref())
                        ?};
                        _pal.next()
                     !}
                  ?};
                  PAL.cntx_pop()
               ?};
               VAR_DEL.delete('__sklad');
               obj_del(_pal)
            ?};

            ND.cntx_psh();
            {? __md_oddz<>''
            ||
               ST.ODDZ:=ST.ODDZ_KOD:=__md_oddz;
               exec('open','open_tab',ST.ODDZ,2-$ST.AR)
            ?};
            {? ND.clear; ND.seek(_ref)
            || exec('dk_sum','magdok_wspolne',ND.ref);
::             domyslne informacje o wymiarach
               {? __akcept='T'
               || {? ND.MAG().SL='T' & exec('sprndlok','magdok_poz',{? _group || 0 || __gen_mm ?})<>''
                  || __akcept:='N'
                  || USERS_UP.cntx_psh();
                     USERS_UP.index('MG');
                     USERS_UP.prefix(OPERATOR.USER,'MG',ST.ODDZ_KOD);
                     {? ~USERS_UP.find_key(ND.MAG) || __akcept:='N' ?};
                     USERS_UP.cntx_pop()
                  ?}
               ?}
            ?};
            {? __md_oddz<>''
            ||
               ST.ODDZ:=ST.ODDZ_KOD:=__st_oddz;
               exec('open','open_tab',ST.ODDZ,2-$ST.AR)
            ?};
            ND.cntx_pop()
         ||
::          jezeli przychodowy to generuje komplet - tylko dla biezacego oddzialu
            {? ND.TYP().DN<>'D'
            || _tmg:=(1+DK.N().MAG().TYP);
               DK.first();
               {!
               |? _il:=DK.IL;
                  _mat:=DK.M;
                  exec('gen_skl','material',_mat,_il,_ref);
                  DK.next()
               !}
            ?};
            ND.cntx_psh();
            {? ND.clear; ND.seek(_ref)
            || exec('dk_sum','magdok_wspolne',ND.ref);
::             domyslne informacje o wymiarach
               {? __akcept='T' & ND.MAG().SL='T' || {? exec('sprndlok','magdok_poz',~_group)<>'' || __akcept:='N' ?} ?}
            ?};
            ND.cntx_pop();
            ZMIENNE.WAR_SKL:=1
         ?}
      ?};
::    akceptacja dokumentu / dokumentow
      {? __akcept='T' & _ref<>null || _ile_dok:=2 || _ile_dok:=1 ?};
      ND.cntx_psh();
      _stmag:=ST.MAG;
      {!  _ii:=1.._ile_dok
      |!
         {? _ii=2 & ~ZMIENNE.WAR_SKL & __md_oddz<>''
         ||
            ST.ODDZ:=ST.ODDZ_KOD:=__md_oddz;
            exec('open','open_tab',ST.ODDZ,2-$ST.AR)
         ?};

::       dodanie pozycji do tabeli pozycji dostaw magazynowych
         {? exec('get','#params',100210,2)='T'
         ||
            ND.cntx_psh();

            ND.prefix();
            {? ND.seek({? _ile_dok=2 || _ref || _ref_old ?})
            ||
               exec('dkdost_update','magdok_poz')
            ?};
            ND.cntx_pop()
         ?};

         ST.MAG:=ND.MAG;
         {? _ii=2
         ||
            {? ~ZMIENNE.WAR_SKL
            ||
               ND.clear();
               {? ND.seek(_ref)
               ||
::                drugi magazyn
                  _tmg:=(1+ND.MAG().TYP);
                  DK.index('DOKMAG');DK.prefix(ND.ref);
                  _rp:=_refn
               || _ok:=0
               ?}
            ||
               ND.clear();
               {? ND.seek(_ref_old)
               ||
                  _tmg:=(1+ND.MAG().TYP);
                  DK.index('DOKMAG');DK.prefix(ND.ref);
                  _rp:=null
               || _ok:=0
               ?}
            ?}
         |? _ii=0
         ||
            ~~
         ||
::          _ii=1
            {? ~ZMIENNE.WAR_SKL
            ||
               ND.clear();
               {? ND.seek(_ref_old)
               ||
                  DK.index('DOKMAG');DK.prefix(ND.ref);
                  _rp:=null
               || _ok:=0
               ?}
            ||
               ND.clear();
               {? ND.seek({? _ile_dok=2 || _ref || _ref_old ?})
               ||
::                drugi magazyn
                  _tmg:=(1+ND.MAG().TYP);
                  DK.index('DOKMAG');DK.prefix(ND.ref);
                  _rp:=_refn
               || _ok:=0
               ?}
            ?}
         ?};

::       generacja pozycji opakowan - przed rozpisaniem FIFO/LIFO
::       tylko dla dokumentow zewnetrznych
         {? ND.TYP().Z='T' | ND.TYP().T='BO' || exec('opak_wer','opakow',ND.ref) ?};

         {? DK.first()
         ||
            _ok:=1;
            _nnn:=ND.ref;
::          exec('data_akcept','mag_fun','T');
            _czy__all:=0;
            {? ND.MAG().TYP*'EWI' & ND.TYP().P='T'
            ||
::             wyznaczenie ceny ewidencyjnej w dokumentach przychodowych
               DK.cntx_psh();
               {!
               |? __cena_e:=exec('biez_cen','ceny_dok',DK.M,ND.MAG,ND.D);
                  __ktm:=DK.M;
                  {? __cena_e=0
                  ||
::                   brak ceny ewidencyjnej - ustalenie ceny ewidencyjnej z pierwszej pozycji na material DK.M
::                   w akceptowanym dokumencie
                     DK.cntx_psh();
                     {? DK.first()
                     ||
                        {!
                        |? {? DK.M=__ktm || __cena_e:=DK.C ?};
                           __cena_e=0 & DK.next()
                        !}
                     ?};
                     DK.cntx_pop()
                  ?};
                  DK.C:=__cena_e;
                  DK.WAR:=(DK.IL*DK.C)$2;
                  DK.SCC:=DK.C;
                  DK.SCWAR:=DK.WAR;
                  DK.put();
                  DK.next()
               !};
               DK.cntx_pop();
               DK.get()
            ?};
            {? 'Ś'=__tmgo & 'D'=_tmg & ND.TYP().P<>'T'
            ||
               DK.cntx_psh;
               {? DK.first
               ||
                  {!
                  |?
                     DK.C:=exec('cenas','magazyn_stan',DK.M,__tmgo);
                     DK.WAR:=(DK.IL*DK.C)$2;
                     DK.put & DK.next
                  !}
               ?};
               DK.cntx_pop;
               DK.get
            ?};
            {? _tmg='D' & ND.TYP().P<>'T' & ND.TYP().DN='D' & _ref<>null()
            || _tmg:=(1+DK.N().MAG().TYP);
               DK.first();
               {!
               |? _il:=DK.IL;
                  _mat:=DK.M;
                  exec('gen_skl','material',_mat,_il,_ref);
                  DK.next()
               !};
               ND.cntx_psh();
               {? ND.clear; ND.seek(_ref) || exec('dk_sum','magdok_wspolne',ND.ref) ?};
               ND.cntx_pop();
               ZMIENNE.WAR_SKL:=1
            ?};
            {? 'FLŚE'*_tmg & ND.TYP().P<>'T'
            ||
               _czy__all:=exec('gen_fifo','magdok_poz',_tmg,0);
               {? _czy__all<>0
               ||
                  _txt:={? 'F'*_tmg || 'Rozpisanie pozycji wg FIFO nie powiodło się.'@
                                  |? 'L'*_tmg || 'Rozpisanie pozycji wg LIFO nie powiodło się.'@
                                  |? 'Ś'*_tmg || 'Rozpisanie pozycji wg ŚREDNIE nie powiodło się.'@
                        || 'Rozpisanie pozycji wg EWIDEN nie powiodło się.'@ ?};
                  {? _group
                  || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                  || exec('add_kom','#message',_txt,2)
                  ?};
                  undo()
               |? ND.MAG().SL='T'
               || _spr:=0;
                  DK.cntx_psh();
                  DK.index('DOKZP');
                  DK.prefix(ND.ref(),0);
                  {? DK.first()
                  || {!
                     |? {? DK.SCEAN<>''
                        || _spr:=1;
                           DK_L.index('DK');
                           DK_L.prefix(DK.ref(),null());
                           {? DK_L.first() || {! |? {? DK_L.AUTO || DK_L.del() || DK_L.next() ?} !} ?}
                        ?};
                        DK.next()
                     !}
                  ?};
                  DK.cntx_pop();
                  {? _spr || exec('sprndlok','magdok_poz',{? _group || -1 || 0 ?}) ?}
               ?};
               {? ND.TYP().DN='D' & _ref<>null()
               || _tmg:=(1+DK.N().MAG().TYP);
                  DK.first();
                  {!
                  |? _il:=DK.IL;
                     _mat:=DK.M;
                     exec('gen_skl','material',_mat,_il,_ref);
                     DK.next()
                  !};
                  ND.cntx_psh();
                  {? ND.clear; ND.seek(_ref) || exec('dk_sum','magdok_wspolne',ND.ref) ?};
                  ND.cntx_pop();
                  ZMIENNE.WAR_SKL:=1
               ?};
               DK.first();

::             generownie pozycji po wycenie dokumentow FIFO / LIFO
               DK.cntx_psh();
               ND.cntx_psh();

               {? {? ZMIENNE.WAR_SKL || _ref<>null & _ii=2 || _ref_old<>null & _ii=1 ?}<>0 &
                  {? ZMIENNE.WAR_SKL || _ref_old<>null || _ref<>null ?}
               ||
                  {? __md_oddz<>''
                  || DK.cntx_psh();
                     DK_C.cntx_psh();
                     DK_L.cntx_psh();
                     ST.ODDZ:=ST.ODDZ_KOD:=__md_oddz;
                     exec('open','open_tab',ST.ODDZ,2-$ST.AR)
                  ?};
                  DK.prefix({? ZMIENNE.WAR_SKL || _ref_old || _ref ?});
                  {? DK.first
                  || _pal:=tab_tmp(1,'PAL','STRING[16]','');
                     {!
                     |?
                        exec('delprzyp','fakso','FAKSO','DK',DK.ref());
                        exec('delFAP_K','oplaty_dod',DK.N().uidref(),DK.uidref(),null());
                        FAP_K.index('DKSQL');
                        FAP_K.prefix($DK.ref());
                        {? FAP_K.first() || {! |? exec('dk_k_del','magdok_poz') !}?};
                        {? DK.count()>0
                        || DK_L.index('DK');
                           DK_L.prefix(DK.ref(),null);
                           {? DK_L.first()
                           || {!
                              |? {? DK_L.PAL<>null
                                 || _pal.clear();
                                    {? ~_pal.find_key($DK_L.PAL)
                                    || _pal.blank();
                                       _pal.PAL:=$DK_L.PAL;
                                       _pal.add(1)
                                    ?}
                                 ?};
                                 DK_L.del()
                              !}
                           ?}
                        ?};
                        {? DK.count>0
                        || DK_HS.index('DK');
                           DK_HS.prefix(DK.ref());
                           {? DK_HS.first() || {! |? DK_HS.del() !} ?}
                        ?};
                        {? DK.PLUS='N' || exec('update','rezerwacje','DK',DK.ref(),ND.MAG,DK.M,0,DK.SRDK) ?};
::                      Dla dokumentu RW sprawdzane jest, czy usuwana pozycja jest ostanią pozycją dokumentu RW danego limitu.
::                      Jeżeli tak to odblokowuję możliwość rejestracji nielimitu/odpadu na zleceniu montażowym/podzleceniu.
                        _chk_lim:=(exec('nd_is_rw','magdok_wspolne',DK.N) | exec('nd_is_zw','magdok_wspolne',DK.N)) & DK.ZLIM<>null()
                                  & exec('FindAndGet','#table',ZLIM,DK.ZLIM,,
                                     "ZLIM.PODZL<>'' & (ZLIM.SO='O' | ZLIM.SO='S' & ZLIM.LIMIT='N')",0);
                        _zlim:=DK.ZLIM;
                        _del:=DK.del(,1);
                        {? _del & _chk_lim || exec('zlim_chk_podzl','zl_limit',_zlim) ?};
                        _del>1
                     !};
::                   aktualizacja znacznikow na paletach
                     {? _pal.size() & _pal.first()
                     || {!
                        |? exec('znacznik','magdok_palety',exec('FindAndGet','#table','PAL',_pal.PAL,,,null()));
                           _pal.next()
                        !}
                     ?};
                     obj_del(_pal)
                  ?};
                  {? __md_oddz<>''
                  ||
                     ST.ODDZ:=ST.ODDZ_KOD:=__st_oddz;
                     exec('open','open_tab',ST.ODDZ,2-$ST.AR);
                     DK.cntx_pop();
                     DK_C.cntx_pop();
                     DK_L.cntx_pop()
                  ?};

                  _pal:=tab_tmp(1,'PAL','STRING[16]','');
                  DK.prefix(_nnn);
                  {? DK.first
                  ||
                     _tabdkl:=tab_tmp(1,'DKL','STRING[16]',,'ILE','REAL','');
                     _mgeanl:=DK.N().MD().EANL;
                     {!
                     |?
                        {? (DK.N().MD().TYP*'EWI')
                        || __cena:=exec('biez_cen','ceny_dok',DK.M,DK.N().MD,DK.N().D,,,1);
                           {? __cena=-1 || __cena:=0 || {? ~__cena || __cena:=DK.C ?} ?}
                        || __cena:=DK.C
                        ?};
                        _czyid:=(';DAPZ'*DK.M().IDMOB)>1;

                        _dokl:=exec('jaka_dok_mjm','jm',DK.M,DK.J2,DK.M().J);
                        _il22:={? DK.WS2 || DK.IL/DK.WS2 || 0 ?} $ (_dokl+1);
                        roundmet(BEER.MDOKL);
                        _il22:=_il22 $ _dokl;
                        exec('przyjdod','jm',{? DK.J2=null || $DK.M().J || $DK.J2 ?},
                        {? DK.WS2<>0 || DK.WS2 || 1 ?},{? _dokl>=0 || _il22 || DK.IL2 ?});
                        roundmet(5);

                        __dk_m:=DK.M;
                        __dk_il:=DK.IL;
                        __dk_n_d:=DK.N().D;
                        {? ~(DK.N().MAG().TYP*'DOST')
                        || __dk_dk_c:=exec('odd_matr','mat_atr',__md_oddz,ST.ODDZ
                                       ,exec('FindAndGet','#table',DK,DK.PRDK,,"DK_C",DK.DK_C))
                        || __dk_dk_c:=exec('odd_matr','mat_atr',__md_oddz,ST.ODDZ,DK.DK_C)
                        ?};
                        __dk_prdk:={? DK.M().SETW='P'
                                    & (exec('FindAndGet','#table',DK,DK.PRDK,,"TW",date(0,0,0))<>DK.TW
                                     | exec('FindAndGet','#table',DK,DK.PRDK,,"SCEAN",'')<>DK.SCEAN)
                                   || DK.SRDK
                                   || DK.PRDK
                                   ?};
                        __dk_war:={? (1+DK.N().MD().TYP)='E' || DK.WAR || 0 ?};
                        __dk_tw:={? DK.M().SETW='P' & DK.TW=date(0,0,0)
                                 || exec('FindInSet','#table','DK_L','DK',null(),DK.ref(),"@.DK_L.TW",,,date(0,0,0))
                                 || DK.TW
                                 ?};

                        _dk_src:=DK.PRDK;
                        _dk_sts:=DK.STATS;
                        _scean:={? _czyid
                                || exec('FindAndGet','#table','DK',__dk_prdk,,"DK.SCEAN",'')
                                || ''
                                ?};
                        {? DK.PLUS='N' & _czyid & _scean=''
                        || _scean:=exec('old_idmob','magdok_nag',DK.M().KTM,DK.M)
                        ?};
                        {? ~ZMIENNE.WAR_SKL & _ii=1 & __md_oddz<>''
                        || DK.cntx_psh();
                           DK_C.cntx_psh();
                           DK_L.cntx_psh();
                           ST.ODDZ:=ST.ODDZ_KOD:=__md_oddz;
                           exec('open','open_tab',ST.ODDZ,2-$ST.AR)
                        ?};
                        VAR_DEL.delete('__dk_p');
                        __dk_p:=DK.PROJEKTY;
                        _refnew:=exec('adddk','magdok_poz',{? ZMIENNE.WAR_SKL || _ref_old || _ref ?},__dk_m,__dk_il
                                  ,__cena,__dk_n_d,,,,,,,,7,,,__dk_dk_c,,__dk_prdk,,,,,__dk_war,,_scean,__dk_tw);
                        VAR_DEL.delete('__dk_p');
                        _mskdkl:=DK_L.name();

                        exec('DK_HS_new4DK','statexam',_refnew,'M',,,_dk_sts,_dk_src,1);

                        {? ~ZMIENNE.WAR_SKL & _ii=1 & __md_oddz<>''
                        ||
                           ST.ODDZ:=ST.ODDZ_KOD:=__st_oddz;
                           exec('open','open_tab',ST.ODDZ,2-$ST.AR);
                           DK.cntx_pop();
                           DK_C.cntx_pop();
                           DK_L.cntx_pop()
                        ?};

                        {? _czypal
                        || DK.cntx_psh();
                           _iledk:=DK.IL;
                           _refold:={? ~DK.ZP
                                    || DK.ref()
                                    || DK.clear;
                                       {? DK.seek(DK.ZP,) || DK.ref() || null ?}
                                    ?};
                           DK_L.index('DK');
                           DK_L.prefix(_refold,null);
                           {? DK_L.first()
                           || {!
                              |? _iledkl:=DK_L.IL;
                                 _tabdkl.clear();
                                 {? _tabdkl.find_key($DK_L.ref()) || _iledkl-=_tabdkl.ILE ?};
                                 {? _iledkl>0
                                 || {? _iledk>=_iledkl
                                    || _ilenew:=_iledkl;
                                       _iledk-=_ilenew
                                    |? _iledk<_iledkl
                                    || _ilenew:=_iledk;
                                       _iledk:=0
                                    ?};
                                    _tabdkl.clear();
                                    {? _tabdkl.find_key($DK_L.ref())
                                    || _tabdkl.ILE+=_ilenew;
                                       _tabdkl.put(1)
                                    || _tabdkl.blank();
                                       _tabdkl.DKL:=$DK_L.ref();
                                       _tabdkl.ILE:=_ilenew;
                                       _tabdkl.add(1)
                                    ?};
                                    DK_L.cntx_psh();
                                    DK_L.use(_mskdkl);
                                    DK_L.clear();
                                    DK_L.DK:=_refnew;
                                    {? DK_L.UIDDO<>''
                                     & exec('FindAndGet','#table',EANL,DK_L.UIDDO,,"MG",null())=DK_L.DK().N().MAG
                                    || DK_L.LOK:=exec('FindAndGet','#table',EANL,DK_L.UIDDO,,,null());
                                       {? DK_L.LOK=null() || DK_L.LOK:=_mgeanl ?}
                                    || DK_L.LOK:=_mgeanl
                                    ?};
                                    DK_L.UIDDO:='';
                                    DK_L.IL:=_ilenew;
                                    DK_L.SCSQL:=$_refnew;
                                    DK_L.TM+=0.03;
                                    exec('reoIL2','magdok_wymiary');
                                    exec('uzupIDkod','magdok_palety',DK_L);
                                    {? DK_L.add() & _czypal & DK_L.PAL().STAN='N'
                                    || DK_L.cntx_pop();
                                       PAL_POZ.cntx_psh();
                                       PAL_POZ.clear();
                                       PAL_POZ.blank();
                                       PAL_POZ.PAL:=DK_L.PAL;
                                       PAL_POZ.IL:=DK_L.IL;
                                       PAL_POZ.ILP:=DK_L.IL;
                                       PAL_POZ.AKT:='T';
                                       PAL_POZ.M:=DK_L.DK().M;
                                       PAL_POZ.TW:=DK_L.TW;
                                       PAL_POZ.JM:=DK_L.DK().M().J;
                                       PAL_POZ.C:=DK_L.DK().C;
                                       PAL_POZ.DK_C:=DK_L.DK().DK_C;
                                       exec('uzupIDkod','magdok_palety',PAL_POZ);
                                       PAL_POZ.add(1);
                                       PAL_POZ.cntx_pop();
                                       _pal.clear();
                                       {? ~_pal.find_key($DK_L.PAL)
                                       || _pal.blank();
                                          _pal.PAL:=$DK_L.PAL;
                                          _pal.add(1)
                                       ?}
                                    || DK_L.cntx_pop()
                                    ?}
                                 ?};
                                 _iledk>0 & DK_L.next()
                              !}
                           ?};
                           DK.cntx_pop()
                        |? _mgsl
                        || DK.cntx_psh();
                           _iledk:=DK.IL;
                           _refold:={? ~DK.ZP
                                    || DK.ref()
                                    || DK.clear;
                                       {? DK.seek(DK.ZP,) || DK.ref() || null ?}
                                    ?};
                           DK_L.index('DK');
                           DK_L.prefix(_refold,null);
                           {? DK_L.first()
                           || {!
                              |? _iledkl:=DK_L.IL;
                                 _tabdkl.clear();
                                 {? _tabdkl.find_key($DK_L.ref()) || _iledkl-=_tabdkl.ILE ?};
                                 {? _iledkl>0
                                 || {? _iledk>=_iledkl
                                    || _ilenew:=_iledkl;
                                       _iledk-=_ilenew
                                    |? _iledk<_iledkl
                                    || _ilenew:=_iledk;
                                       _iledk:=0
                                    ?};
                                    _tabdkl.clear();
                                    {? _tabdkl.find_key($DK_L.ref())
                                    || _tabdkl.ILE+=_ilenew;
                                       _tabdkl.put(1)
                                    || _tabdkl.blank();
                                       _tabdkl.DKL:=$DK_L.ref();
                                       _tabdkl.ILE:=_ilenew;
                                       _tabdkl.add(1)
                                    ?};
                                    {? DK_L.TW<>date(0,0,0)
                                    ||
                                       DK_L.cntx_psh;
                                       DK_L.use(_mskdkl);
                                       DK_L.clear;
                                       DK_L.DK:=_refnew;
                                       {? DK_L.UIDDO<>''
                                        & exec('FindAndGet','#table',EANL,DK_L.UIDDO,,"MG",null())=DK_L.DK().N().MAG
                                       || DK_L.LOK:=exec('FindAndGet','#table',EANL,DK_L.UIDDO,,,null());
                                          {? DK_L.LOK=null() || DK_L.LOK:=_mgeanl ?}
                                       || DK_L.LOK:=_mgeanl
                                       ?};
                                       DK_L.UIDDO:='';
                                       DK_L.IL:=_ilenew;
                                       DK_L.SCSQL:=$_refnew;
                                       DK_L.AUTO:=0;
                                       DK_L.TM+=0.03;
                                       exec('reoIL2','magdok_wymiary');
                                       exec('uzupIDkod','magdok_palety',DK_L);
                                       DK_L.add;
                                       DK_L.cntx_pop
                                    ?}
                                 ?};
                                 _iledk>0 & DK_L.next
                              !}
                           ?};
                           DK.cntx_pop
                        ?};
                        DK.next()
                     !};
                     obj_del(_tabdkl)
                  ?};
::                akceptacja palet
                  {? _czypal
                  || PAL.cntx_psh();
                     _pal.clear();
                     {? _pal.first()
                     || {!
                        |? PAL.clear();
                           {? PAL.seek(_pal.PAL)
                           || PAL.STAN:='T';
                              PAL.put(1);
                              exec('znacznik','magdok_palety',PAL.ref())
                           ?};
                           _pal.next()
                        !}
                     ?};
                     PAL.cntx_pop()
                  ?};
                  obj_del(_pal);

                  {? ~ZMIENNE.WAR_SKL & _ii=1 & __md_oddz<>''
                  ||
                     ST.ODDZ:=ST.ODDZ_KOD:=__md_oddz;
                     exec('open','open_tab',ST.ODDZ,2-$ST.AR)
                  ?};

                  exec('dk_sum','magdok_wspolne',{? ZMIENNE.WAR_SKL || _ref_old || _ref ?});
::                domyslne informacje o wymiarach
                  {? __akcept='T' & ND.MAG().SL='T' & (ND.clear(); ND.seek({? ZMIENNE.WAR_SKL || _ref_old || _ref ?}))
                  || {? exec('sprndlok','magdok_poz',0)<>'' || __akcept:='N' ?}
                  ?};

                  {? ~ZMIENNE.WAR_SKL & _ii=1 & __md_oddz<>''
                  ||
                     ST.ODDZ:=ST.ODDZ_KOD:=__st_oddz;
                     exec('open','open_tab',ST.ODDZ,2-$ST.AR)
                  ?}
               ?};
               ND.cntx_pop();
               DK.cntx_pop()
            ?};
            {? _czy__all=0
            ||
               __czy_c_sp:='';
               {!
               |?
                  echo('Trwa akceptowanie dokumentu ... %1'@[DK.N().SYM],'pozycja %1'@[$DK.P]);
::                ewentualna wycena pozycji dla PKOM-a
                  {? (_wgskl:=exec('get','#params',600300,2))<>'N' & DK.N().TYP().DN='K'
                  || {? ~(DK.N().MAG().TYP*'EWI') | exec('biez_cen','ceny_dok',DK.M,DK.N().MAG,DK.N().D)=0
                     ||
                        _warskl:={? DK.IL || exec('sklsql2c','magdok_poz',$DK.ref) || 0 ?};

                        DK.C:={? DK.IL || _warskl/DK.IL || 0 ?}$DK.M().DOKL_C;
                        {? DK.N().MAG().SKL<>'T'
                        || DK.SCC:=DK.C
                        ?};
                        exec('war_mag','magdok_poz');
                        {? (1+DK.N().MAG().TYP)<>'E' & _wgskl='W' || DK.WAR:=_warskl ?}
                     ?}
                  ?};
                  {? DK.N().TYP().CS='T' & (DK.CN=0 | DK.CB=0) & DK.ZP=0
                  || __czy_c_sp+=$DK.P+','
                  ?};
                  {? DK.M().RODZ='T'
                  ||
                     __stan_s:=0;
                     __stan_k:=1;
                     {? DK.PLUS='T'
                     ||
                        __stan_s:=DK.IL
                     ||
                        {? _tmg='D'
                        ||
                           __stan_k:={? ~exec('noDostBO','magdok_wspolne',DK.PRDK,DK.N().D)
                                     || exec('dok_po_kor','magdok_wspolne',DK.N().D,DK.RDK,DK.NDK,DK.N().MAG,DK.M)
                                     || -1
                                     ?};
                           {? __stan_k>0
                           ||
                              HELP.POP:=1; HELP.RDK:=DK.RDK; HELP.NDK:=DK.NDK; HELP.IL:=DK.IL; HELP.IL2:=DK.IL2;
                              __stan_s:=exec('obl_dost','magazyn_stan',DK.N().MAG,DK.M,DK.RDK,DK.NDK);
                              HELP.POP:=0; HELP.IL:=0; HELP.IL2:=0; HELP.RDK:=-1; HELP.NDK:='~~'
                           ?}
                        ?}
                     ?};
                     _czyid:=(_tmg='D' | _mgi) & DK.N().MAG().SL='T' & DK.N().MAG().PAL<>'T';
                     {? DK.PLUS='T' & (DK.N().TYP().DN='T'
                     | (DK.N().INN='T' & DK.N().TYP().INW='I' & DK.N().TYP().P='T'))
                      & exec('ctrlSETW','magdok_wspolne',DK.M,DK.SRDK)
                     || DK.SRDK:=DK.PRDK:=$DK.ref();
                        DK.RDK:=#DK.ref();
                        DK.NDK:=DK.name();
                        {? DK.M().IDMOB='D' || DK.SCEAN:='' ?};
                        {? DK.put(1) & _tmg='D' & DK.N().MAG().SL='T'
                        || DK_L.index('DK');
                           DK_L.prefix(DK.ref(),null());
                           {? DK_L.first()
                           || {!
                              |? {? DK.M().IDMOB='D' || DK_L.SCEAN:='' ?};
                                 DK_L.SCSQL:=DK.SRDK;
                                 DK_L.put(1);
                                 DK_L.next()
                              !}
                           ?}
                        ?}
                     ?};
                     {? DK.PLUS='T' & DK.M().IDMOB<>'N'
                     || {? DK.SCEAN='' & DK.PRDK<>DK.SRDK
                        || DK.SCEAN:=exec('FindAndGet','#table','DK',DK.PRDK,,"DK.SCEAN",'')
                        ?};
                        _scakt:=DK.SCEAN;
                        _scean:={? DK.SCEAN<>'' & DK.M().IDMOB='D'
                                & (DK.N().INN='T' & DK.N().TYP().INW='E' &  DK.N().TYP().P='T')
                                || DK.SCEAN
                                |? _tmg<>'D' & ~_mgi
                                || exec('old_idmob','magdok_nag',DK.M().KTM,DK.M)
                                |? DK.PLUS='T' & (DK.PRDK=DK.SRDK | DK.SCEAN='')
                                 & {? ~(DK.N().TYP().DN='T'
                                   | (DK.N().INN='T' & DK.N().TYP().INW='I' & DK.N().TYP().P='T'))
                                   || 1
                                   || DK.SCEAN=''
                                   ?}
                                || exec('newscean','magdok_poz',DK.M,DK.M().IDMOB,DK.DK_C,DK.DK_C().ZPARN,DK.ZL,DK.SCEAN)
                                || DK.SCEAN
                                ?};
                        {? _scean<>''
                        || DK.SCEAN:=_scean;
                           {? DK.put(1)
                           || {? _scakt<>_scean & (_tmg='D' | _mgi)
                              || _txt:='Przypisano kod kreskowy dla dostawy %1.'@[DK.SCEAN];
                                 {? _group
                                 || exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
                                 || exec('add_kom','#message',_txt,4)
                                 ?};
                                 DK_L.cntx_psh();
                                 DK_L.index('DK');
                                 DK_L.prefix(DK.ref(),null());
                                 {? DK_L.first() || {! |? DK_L.SCEAN:=DK_L.DK().SCEAN; DK_L.put(1); DK_L.next() !} ?};
                                 DK_L.cntx_pop()
                              ?}
                           || _txt:='Nie przypisano kodu kreskowy dla dostawy %1.'@[DK.M().KTM];
                              {? _group
                              || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                              || exec('add_kom','#message',_txt,2)
                              ?};
                              undo()
                           ?}
                        |? _czyid & {? (DK.N().INN='T' & DK.N().TYP().INW='I' & DK.N().TYP().P='T')
                                    || ~(_scakt<>'' & DK.M().IDMOB='Z')
                                    || 1
                                    ?}
                        || _txt:='Nie został wygenerowany kod kreskowy dla dostawy %1.'@[DK.M().KTM];
                           {? _group
                           || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                           || exec('add_kom','#message',_txt,2)
                           ?};
                           undo()
                        ?};
                        {? DK.SCEAN<>''
                        || exec('aktzwmkodk','kody_kresk',DK.M,DK.SCEAN);
                           {? DK.M().IDMOB='D' & (DK.N().INN='T' & DK.N().TYP().INW='E' &  DK.N().TYP().P='T')
                           || exec('mkodkadd','kody_kresk',DK.M,DK.SCEAN,DK.PRDK,2)
                           ?}
                        ?}
                     ?};
                     {? _tmg='D' & __stan_s<DK.IL
                     ||
                        {? __stan_k>0
                        || _txt:='Brak stanu dla materiału %1.'@[DK.M().KTM];
                           {? _group
                           || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                           || exec('add_kom','#message',_txt,2)
                           ?}
                        |? __stan_k<0
                        || _txt:='Wskazana dostawa pochodzi z dokumentu BO wystawionego później niż dany dokument.'@;
                           {? _group
                           || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                           || exec('add_kom','#message',_txt,2);
                           exec('add_kom','#message','- Dotyczy materiału: %1 pozycja: %2.'@[DK.M().KTM,form(DK.P,,0,'99')],2)
                           ?}
                        || _txt:='Do dnia wystawienia korekty włącznie stan korygowanej dostawy jest niedostępny.'@;
                           {? _group
                           || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                           || exec('add_kom','#message',_txt,2);
                           exec('add_kom','#message','- Dotyczy materiału: %1.'@[DK.M().KTM],2)
                           ?}
                        ?};
                        undo()
                     |? exec('OBLSTAN','magazyn_stan',DK.N().MAG,DK.M,{? DK.PLUS='T' || ND.D || DK.DOST ?}
                           ,DK.C,DK.PLUS,0,DK.RDK,DK.NDK,DK.SRDK,DK.PRDK,DK.SCC,DK.N().SCWAL,DK.STATS,DK.SCEAN,DK.TW)=0
                     ||
                        _ok:=0
                     ?}
                  ?};
                  DK.next()
               !};
               VAR_DEL.delete('__stan_s','__stan_k');
               echo();
               {? __czy_c_sp<>''
               ||
                  __czy_c_sp:=__czy_c_sp-1;
                  {? _group
                  || _txt:='Nie podano ceny sprzedaży na wszystkich pozycjach.'@;
                     exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                  || exec('add_kom','#message','Nie podano ceny sprzedaży na pozycjach: %1.'@[__czy_c_sp],4)
                  ?};
                  _ok:=0
               ?}
            ?};
::          Produkcja:- nawijanie stanow zlecen i przypisywanie dostaw do zlecenia dla rw na surowce nielimitowane
::          dost_zl scalic z akzlt i przeniesc do jednego pliku.
            {? __pprod='T'
             & (' '+exec('get','#params',500705,2,null()))*(' '+DK.N().TYP().T+' ')=0
            || _wyn:=1;
               {? ND.TYP().ZLEC<>'N' & _ok=1 & __pprod='T' & _ii<>0
               || {? _wyn=1
                  ||
                     {? (' '+exec('get','#params',500701,2,null()))*(' '+ND.TYP().T+' ')>0
                      | ND.TYP().WYR='T'
                      | exec('is_usluga_typ','zl_uslugi',ND.TYP)
                     || exec('aktuzl','zl_common',ND.ref(),1,_group)
                     |? ND.TYP().ZLEC<>'N' & ND.TYP().WYR<>'T' & ND.TYP().KOOP<>'T'
                     || DK.prefix(ND.ref);
                        {? DK.first
                        || {!
                           |? {? DK.ZL<>null
                              || _ok:=exec('akzlst','magdok_poz',DK.ZL,1,DK.WYD,,_group)
                              ?};
                              DK.next & _ok
                           !}
                        ?}
                     ?}
                  || undo()
                  ?}
               ?}
            |? (__pprod='N' & DK.ZL<>null) |
              (__pprod='T' & DK.ZL<>null & (' '+exec('get','#params',500705,2,null()))*(' '+DK.N().TYP().T+' ')>0)
            || DK.prefix(ND.ref);
               {? DK.first
               || {!
                  |? {? DK.ZL<>null & DK.N().TYP().KOOP<>'T'
                     || _ok:=exec('akzlst','magdok_poz',DK.ZL,1,DK.WYD,,_group)
                     ?};
                     DK.next  & _ok
                  !}
               ?}
            ?};
::          Obsluga partii produktu - generowanie ZPARS dla RW, jesli pole partia wypelnione na DK
            DK.cntx_psh();
            TYPYDOK.cntx_psh();
            {? exec('nd_is_rw','magdok_wspolne',ND.ref())>0 | exec('nd_is_zw','magdok_wspolne',ND.ref())>0
            ||
               _can_continue:=0;
               DK.prefix(ND.ref);
               {? DK.first()
               || {!
                  |? _can_continue:=exec('create4dk','magdok_partie',DK.ref());
                     DK.next() & _can_continue>0
                  !}
               ?}
            ?};
            TYPYDOK.cntx_pop();
            DK.cntx_pop();

::          koniec fragmentu wklejonego z produkcji

::          tylko dla dokumentu akceptowanego
::          kontrola wypełnienia ceny tylko dla magazynow srednich i ewidencyjnych
::          lub gdy jest przesuniecie na te magazyny
            {? ND.ref()=_ref_old
            ||
               MG.cntx_psh();
               ND.cntx_psh();
               DK.cntx_psh();
               DK.index('DOKMAG');
               DK.prefix(ND.ref());
               {? DK.first()
               ||
                  {!
                  |?
                     __spr_cene:=0;
                     __txt:='';
                     {? ('Ś'+{? exec('get','#params',600104,2)<>'T' || 'E' || '' ?})*_tmg>0
                     ||
                        __txt:='Magazyn typu %1.'@[DK.N().MAG().TYP];
                        __spr_cene:=1
                     ||
                        {? _tmg2<>'' & ('Ś'+{? exec('get','#params',600104,2)<>'T' || 'E' || '' ?})*_tmg2>0
                        ||
                           __txt:='Przesunięcie do magazynu typu %1.'@[DK.N().MD().TYP];
                           __spr_cene:=1
                        ?}
                     ?};
                     {? DK.C=0 & DK.M().RODZ<>'U' & DK.N().MAG().IL<>'T'
                     ||
                        {? 'FL'*_tmg>0
                        || __txt+='Po rozpisaniu pozycja '@
                        || __txt+='Pozycja '@
                        ?};
                        __txt+='%1 - zerowa cena dostawy.'@[$DK.P];
                        {? var_pres('__kom')>100
                        || {? _group
                           || exec('add_kom','#message',ND.SYM,5,gsub(__txt,'\n',' '))
                        || exec('add_kom','#message',__txt,{? __spr_cene=1 || 2 || 4 ?})
                           ?}
                        || FUN.info(__txt)
                        ?};
                        {? __spr_cene=1 || _ok:=0 ?}
                     ?};

                     DK.next()
                  !}
               ?};
               DK.cntx_pop();
               ND.cntx_pop();
               MG.cntx_pop()
            ?};

            {? _ok=0
            || undo()
            || DK.N;
               ND.US:=OPERATOR.USER;
               ND.Z:={? _ii=1 || 'T' || 'N' ?};
               ND.STAT_REJ:={? _ii=1 || 'Z' || 'N' ?};
               {? _ii=1
               || {? ND.IST_TYP<>'' || ND.INTRAKC:=exec('get','#params',100202,2) ?}
               || ND.INTRAKC:='N'
               ?};
::             w ponizszej formule obliczana jest tez wartosc dokumentu
               exec('data_akcept','magdok_nag','T',_ii=2,POM.PAR2);
::             akceptacja powiazanego dokumentu opakowan
               {? exec('nd_z','opakow',1)=0
               || _txt:='Powiązany dokument opakowań nie został zaakceptowany.'@;
                  {? _group
                  || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                  || exec('add_kom','#message',_txt,4)
                  ?};
                  undo()
               ?};
::             aktualizacja statusow dostaw
               exec('nd_dk_stats','statexam',ND.ref());
               ~~
            ?}
         || {? _ile_dok=_ii
            || _txt:='Dokument magazynowy nie ma pozycji materiałowych '
                'i nie może być zakończony.'@;
               {? _group
               || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
               || exec('add_kom','#message',_txt,4,,,1)
               ?}
            ?};
            undo()
         ?};
         {? do_state=2
         || {? ~_group
            || exec('add_kom','#message','Dokument nie został zakończony.'@,2,,,_ii%*2)
            ?};
            {? _ii=2
            || _txt:='Wycofano akceptację poprzedniego dokumentu.'@;
               {? _group
               || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
               || exec('add_kom','#message',_txt,2,,,1)
               ?}
            ?}
         ?};

         {? _ii=2 & ~ZMIENNE.WAR_SKL & __md_oddz<>''
         ||
            ST.ODDZ:=ST.ODDZ_KOD:=__st_oddz;
            exec('open','open_tab',ST.ODDZ,2-$ST.AR)
         ?}
      !};
      ND.cntx_pop();
      ND.get();
      ST.MAG:=_stmag;

::    aktualizacja wartosci do kontrolingu
:: DRO:[rr] nie przeniesiono
::      {? exec('aktualizacja_m','schematy',ND.ref(),'D')=0
::      || undo();
::         exec('add_kom','#message','Wyliczenie wartości do controllingu nie powiodło się.',2)
::      ?};
::    rezerwacje dla zamowien sprzedazy
      DK.index('DOKMAG');
      DK.prefix(ND.ref());
      {? DK.first
      ||
         ZK_P.cntx_psh; ZD_RP.cntx_psh;
         ZK_P.index('ZD_POZ');
         {!
         |?
            {? 5+DK.ZAM_RP='zdhip'
            ||
               ZD_RP.use(8+DK.ZAM_RP);
               ZD_RP.prefix;
               {? ZD_RP.seek(DK.ZAM_RP)
               ||
                  ZK_P.prefix($ZD_RP.ZD_POZ);
                  {? ZK_P.first
                  ||
                     {!
                     |?
                        ZK_P.REZ:=1;
                        ZK_P.put;
                        ZK_P.next
                     !}
                  ?}
               ?}
            ?};
            DK.next
         !};
         ZK_P.cntx_pop; ZD_RP.cntx_pop
      ?};
      _wyn:=do_state();
      {? _wyn & (ND.get(); ND.Z='T') || exec('ilmg2ZAM','magdok_wspolne',ND.ref(),1) ?};
      {? ~_e || end() ?};
      exec('llog_kon','users',__l_log);
::    aktualizacja rezerwacji po akceptacji dokumentow
      {? _wyn=1
      ||
         {? var_pres('__zam')>100 || __no_stat:=0 || __no_stat:=1 ?};
         {? ND.sel_size=0 || {? __no_stat=1 || exec('utw_zk_tymc','zamsiw_wspolne') ?} ?};
         {! _ii:=1.._ile_dok
         |! _stmag:=ST.MAG;
            {? _ii=2
            ||
               {? __md_oddz<>''
               ||
                  ST.ODDZ:=ST.ODDZ_KOD:=__md_oddz;
                  exec('open','open_tab',ST.ODDZ,2-$ST.AR)
               ?};
               {? ND.clear;ND.seek(_ref)
               || {? ND.STAT_REJ='Z'
                  ||
::                     exec('add_kom','#message','Zakończono rejestrację dokumentu %1 w magazynie '
::                        '%2 został zakończony.'@[ND.SYM,ND.MAG().SYM],1);
                     {? _group
                     || _txt:='Zakończono rejestrację dokumentu.'@;
                         exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
                     ?};
                     {? ND.TYP().P='T' & ND.TYP().Z='T' & ND.TYP().DN<>'N' & ND.F='T' & exec('spr_zz','opakow')
                     || _txt:='Do pozycji dokumentu wygenerowano automatycznie pozycje opakowań zwrotnych.'@;
                        {? _group
                        || exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
                        || exec('add_kom','#message',_txt,4);
                        exec('add_kom','#message','Uwaga. Możliwa jest modyfikacja pozycji opakowań '
                                                 'po wycofaniu akceptacji dokumentu.'@,4)
                     ?}
                     ?}
                  ?};
                  __mat:='';
                  DK.index('DOKMAG');
                  DK.prefix(ND.ref());
                  {? DK.first
                  || {!
                     |? {? __mat*('|'+$#DK.M+';')=0
                        || __mat+='|'+$#DK.M+';';
                           exec('obl_stan','magazyn_stan',DK.M,1,DK.N().MAG)
                        ?};
                        DK.next
                     !};
::                   aktualizacja statusow zamowien
                     {? ND.TYP().P='T'
                     || exec('Rez2Rez','zamsiw_wspolne',1,ND.ref());
                        {? exec('czyMgUprZam','zamsiw_wspolne',ND.MAG)
                        || exec('akt_wgdk','rezerwacje',ND.ref)
                        |? ND.TYP().ZLEC='T' & ND.TYP().WYR='T'
                        || _txt:='Magazyn %1 (przychód z produkcji) nie jest uprawniony do rezerwacji.'
                             ' Rezerwacje nie zostały zaktualizowane.'@[ND.MAG().SYM];
                           {? _group
                           || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                           || exec('add_kom','#message',_txt,4)
                           ?}
                        ?};
::                      rezerwacje materialow z projektow
:: DRO:[rr] nie przeniesiono
::                        exec('rez_auto','skid_prm',ND.MAG,__mat,__no_stat)
                        1
                     ?}
                  ?}
               ?};
               {? __md_oddz<>''
               ||
                  ST.ODDZ:=ST.ODDZ_KOD:=__st_oddz;
                  exec('open','open_tab',ST.ODDZ,2-$ST.AR)
               ?}
            ||
               {? ND.clear();ND.seek(_ref_old)
               || {? ND.STAT_REJ='Z'
                  ||
::                     exec('add_kom','#message','Zakończono rejestrację dokumentu %1.'@[ND.SYM],1);
                     {? _group
                     || _txt:='Zakończono rejestrację dokumentu.';
                        exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
                     ?};
                     {? ND.TYP().P='T' & ND.TYP().Z='T' & ND.TYP().DN<>'N' & ND.F='T' & exec('spr_zz','opakow')
                     || _txt:='Do pozycji dokumentu wygenerowano automatycznie pozycje opakowań zwrotnych.'@;
                        {? _group
                        || exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
                        || exec('add_kom','#message',_txt,4);
                        exec('add_kom','#message','Uwaga. Możliwa jest modyfikacja pozycji opakowań '
                                                 'po wycofaniu akceptacji dokumentu.'@,4)
                     ?}
                     ?}
                  ?};
                  __mat:='';
                  DK.index('DOKMAG');
                  DK.prefix(ND.ref());
                  {? DK.first
                  || {!
                     |? {? __mat*('|'+$#DK.M+';')=0
                        || __mat+='|'+$#DK.M+';';
                           exec('obl_stan','magazyn_stan',DK.M,1,DK.N().MAG)
                        ?};
                        DK.next
                     !};
::                   aktualizacja statusow zamowien
                     {? ND.TYP().P='T'
                     || exec('Rez2Rez','zamsiw_wspolne',1,ND.ref());
                        {? exec('czyMgUprZam','zamsiw_wspolne',ND.MAG)
                        || exec('akt_wgdk','rezerwacje',ND.ref)
                        |? ND.TYP().ZLEC='T' & ND.TYP().WYR='T'
                        || _txt:='Magazyn %1 (przychód z produkcji) nie jest uprawniony do rezerwacji. '
                                 'Rezerwacje nie zostały zaktualizowane.'@[ND.MAG().SYM];
                           {? _group
                           || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
                           || exec('add_kom','#message',_txt,4)
                           ?}
                        ?};
::                      rezerwacje materialow z projektow
:: DRO:[rr] nie przeniesiono
::                        exec('rez_auto','skid_prm',ND.MAG,__mat,1)
                        1
                     ?}
                  ?}
               ?}
            ?}
         !};
::       aktualizacja statusow zamowien
         {? ND.sel_size=0 || {? __no_stat=1 & _e=0 || exec('odt_zk_tymc','zamsiw_wspolne') ?} ?}
      |? __nr0
      ||
::       usuwanie numeru
         numer:=__wybnr1;
         oldnumer:=1;
         exec('nr_old','numery')
      ?}
   ?};
   ND.clear;
   {? ND.seek(_ref_old)
   ||
::    archiwizacja cech dostawy
      {? ND.STAT_REJ<>'N' & (1+ND.MAG().TYP)='D' || exec('atrnarch','mat_atr',ND.ref(),1) ?};
      {? _c || ND.r_unlock() ?}
   ?};
:: DRO:[rr] nie przeniesiono
   {? ND.sel_size=0 & (_b='T' | POM.PAR5='X') || exec('akcadok','magdok_nag',_f) ?}
?};
DK_C.cntx_pop();
DK.cntx_pop();
ND.cntx_pop();
ND.get();
VAR_DEL.delete('__akcept','__lp','__czy_c_sp','__cena_e','__cena','__ktm','__spr_cene','__txt','__mat','__wyb_nr'
   ,'__gen_mm','__typ_mm','__l_log','__no_stat','__buf_kom','__nr0','__Real','__wybnr1','__znrt','__coscn');
VAR_DEL.delete('__st_oddz','__md_oddz','__dk_m','__dk_il','__dk_n_d','__dk_dk_c','__dk_prdk','__dk_war','__dk_tw'
   ,'__nd_md','__nd_kk','__nd_d','__nd_kh','__nd_ref','__nd_nam','__nd_o','__nd_zl','__nd_wyd','__tmgo','__bufpal'
   ,'__nd_t','__nd_p');
VAR_DEL.delete('__aktzkp');
_wyn


\pr_stmag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009+]
:: OPIS: przed redakcja pola ST.MAG_SYM
::  OLD: \pr_stmag/magazyn2.fml
::----------------------------------------------------------------------------------------------------------------------
exec('set_efld_opt_st','magdok_nag');
{? (5+cur_tab.name())='nagdo' & (1+menu_txt())<>'D'
|| (ND.MD=null & ND.TYP().TD<>'') | exec('FindInSet','#table','DK','DOKMAG',ND.ref())=null
|| 1
?};
::----------------------------------------------------------------------------------------------------------------------
::  MOD: PeKa [02.10.2023]
:: OPIS: Zezwoelenie na poprawę magaazynu do przesuniecia nawet jeli ktos juz dodal pozycje
::       Chodzi o to, że na MW ktos pomylił magazyn do przesuniecia i aby nie robic wszytkiego od nowa
1
::----------------------------------------------------------------------------------------------------------------------


\wyc_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: akcja wycofaj akceptacje dokumentu magazynowego
::   WE: [_a] - czy pytanie o wycofanie (0/1)
::       [_b] - czy wyswietlac drzewko z informacjami (T/N) - domyslnie T
::       [_c] - czy odblokowywac naglowek dokumentu 1-domyslnie=tak 0-nie
::       [_d] - inicjowac tabele komunikatow 1-domyslnie=tak, 0-nie
::       [_e] - rodzaj wycofania 1-zakończenie 2-akceptacja 0-brak informacji (domyślnie)
::   WY: (0/1) czy wycofano akceptacje
::  OLD: \wyc_dok/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
{? _>=2 || {? type_of(_b)<>2 || _b:='T' ?} || _b:='T' ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=1 ?} || _c:=1 ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=1 ?} || _d:=1 ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};

_lmg_act:={? ND.MAG().KOOP='T'
          || {? TYPYDOK.P='T' || 'TTE_WYK_DKOW' || 'TTE_WYK_DKOP' ?}
          || {? TYPYDOK.P='T' || 'LMG_MAG_DAPZ' || 'LMG_MAG_DWYD' ?}
          ?};
_upr:=exec('chk_EndOrAcc','#b__box',_lmg_act,'LMG_MAG_EWMG');
_notkor:=0;

POM.PAR2:='';
:: wlaczenie tabeli komunikatow
{? ND.sel_size=0 & _d=1 || exec('ini_kom','#message','Informacje o wycofaniu dokumentów'@) ?};
__kom_gr:=ND.SYM+', magazyn: '+ND.MAG().SYM;
__lp:=0;

VAR_DEL.delete('__bufpal');

__bufpal:=tab_tmp(1,'PAL','STRING[16]','');

VAR_DEL.delete('__aktzkp');

__aktzkp:=tab_tmp(1,'MAT','STRING[16]',''
           ,'NRK','INTEGER',''
           ,'ILR','REAL','');

DK.cntx_psh();
_wyn:=0;
_wyc_dok:=1;
_ok:=1;
_war2bez:=0;
ND.get();
_recplug:=ND.STAT_REJ;
_stat_rej:=ND.STAT_REJ;
{? ND.STAT_REJ<>'Z'
|| _kom:='Wycofanie akceptacji niemożliwe.'@;
   _kom2:='akceptację'@
|| _kom:='Wycofanie zakończenia redakcji niemożliwe.'@ ;
   _kom2:='zakończenie redakcji'@
?};
{? exec('blok_nd','magdok_nag',{? ND.sel_size=0 || 'T' || 'N' ?})=0
||
   _ok:=0
|? ND.ZAK='T'
||
   exec('add_kom','#message','Dokument jest zaksięgowany. '@+_kom,2,,__lp+=1);
   _ok:=0
|? ND.KOR='T'
||
   exec('add_kom','#message','Dokument był korygowany. '@+_kom,2,,__lp+=1);
   _ok:=0
|? ND.STAT_REJ='N'
||
   exec('add_kom','#message','Dokument nie jest zaakceptowany.'@,4,,__lp+=1);
   _ok:=0
|? exec('czy_z_ok','okresy',1,1,ND.D~1,ND.D~2,ND.MAG)=0
||
   _ok:=0
|? ND.INTRA='T'
||
   exec('add_kom','#message','Dokument uwzlgędniony w deklaracji Intrastat. '@+_kom,2,,__lp+=1);
   _ok:=0
|? ND.EDI_W='T'
||
   exec('add_kom','#message','Na podstawie dokumentu utworzono komunikat EDI. '@+_kom,2,,__lp+=1);
   _ok:=0
|? ND.TYP().P='T' & ~(ND.TYP().DK='T' & ND.TYP().Z='T') & (_war2bez:=exec('sprpozdk','magdok_nag',ND.ref()); ~_war2bez)
|| exec('add_kom','#message',_kom,2,,__lp+=1);
   _ok:=0
|? ND.TYP().P<>'T' & ND.MD<>null() & ND.TYP().TD<>''
 & (ND.cntx_psh();
    _mp:=exec('FindInSet','#table','ND','MM',$ND.ref(),,,1,,null());
    {? _mp<>null() & exec('FindAndGet','#table',ND,_mp,,"(';ZA'*STAT_REJ)>1",0)
    || _war2bez:=exec('sprpozdk','magdok_nag',_mp,1)
    || _war2bez:=1
    ?};
    ND.cntx_pop();
   ~_war2bez)
|| exec('add_kom','#message',_kom,2,,__lp+=1);
   _ok:=0
|? exec('chk_wyc','magdok_wspolne',ND.D,,,ND.T,ND.MAG)=0
||
   exec('add_kom','#message'
        ,'W systemie jest niezamknięty arkusz inwentaryzacyjny z datą otwarcia późniejszą lub równą niż data dokumentu.'@
        ,4,,__lp+=1);
   exec('add_kom','#message',_kom,2,,__lp+=1);
   _ok:=0
|? ~(ND.INN='T' & ND.TYP().INW='I')
 & exec('chk_wyc','magdok_wspolne',ND.D,1
    ,exec('FindInSet','#table','DK','DOKMAG',ND.ref(),,"DK.M",,,null()),ND.T,ND.MAG)=0
||
   exec('add_kom','#message'
        ,'W systemie jest zamknięty arkusz inwentaryzacyjny z datą późniejszą lub równą niż data dokumentu.'@
        ,4,,__lp+=1);
   exec('add_kom','#message',_kom,2,,__lp+=1);
   _ok:=0
|? exec('spr_dkin','magdok_wspolne',ND.ref())=0
||
   exec('add_kom','#message','Pozycja dokumentu jest powiązana z arkuszem inwentaryzacyjnym.'@,4,,__lp+=1);
   exec('add_kom','#message',_kom,2,,__lp+=1);
   _ok:=0
|? ND.MAG().PAL='T' & ~exec('niedepal','magdok_palety',ND.ref())
||
   exec('add_kom','#message','Pozycja dokumentu zawiera paletę zdepaletyzowaną.'@,4,,__lp+=1);
   exec('add_kom','#message',_kom,2,,__lp+=1);
   _ok:=0
|? ND.MAG().PAL='T' & exec('palINarch','magdok_palety',ND.ref())
||
   exec('add_kom','#message','Pozycja dokumentu zawiera zarchiwizowaną paletę.'@,4,,__lp+=1);
   exec('add_kom','#message',_kom,2,,__lp+=1);
   _ok:=0
|? ND.MAG().PAL='T' & exec('palROZnext','magdok_palety',ND.ref())
||
   exec('add_kom','#message'
    ,'Pozycja dokumentu ma wydanie bez rozpakowania, a rozpakowana paleta w późniejszych dokumentach.'@,4,,__lp+=1);
   exec('add_kom','#message',_kom,2,,__lp+=1);
   _ok:=0
|? ((_opc:=exec('sprprzec','magazyn_przec',ND.MAG,ND.D,null(),ND.T,
   {? ND.INN='T' & ND.TYP().INW='E' || $ND.ref() || '' ?}));
    {? (_opc=1 | _opc=2) & ~(ND.INN='T' & ND.TYP().INW='E') || ~exec('sprmdatn','magdok_wspolne',ND.ref()) || _opc ?})
||
   exec('add_kom','#message','Do daty dokumentu wprowadzono przecenę. '@+_kom,2,,__lp+=1);
   _ok:=0
|? ND.MD<>null() & ((_opc:=exec('sprprzec','magazyn_przec',ND.MD,ND.D,null(),ND.T));
    {? (_opc=1 | _opc=2) || ~exec('sprmdatn','magdok_wspolne',ND.ref()) || _opc ?})
||
   exec('add_kom','#message','W magazynie %1 do daty dokumentu wprowadzono przecenę. '@[ND.MD().SYM]+_kom,2,,__lp+=1);
   _ok:=0
|? ND.INN='T' & ND.TYP().INW='E' & _e<>2 & (_prc:=exec('sprprcnd','magdok_wspolne'))>0
|| {? _prc=2
   || exec('add_kom','#message','Dokument przecen przychodowy jest zakończony. Musi najpierw zostać wycofany. '@+_kom,2,,__lp+=1)
   |? _prc=4
   || exec('add_kom','#message','Dokument przecen przychodowy jest zaakceptowany. Musi najpierw zostać wycofany. '@+_kom,2,,__lp+=1)
   || exec('add_kom','#message','Po zaakceptowaniu dokumetu wystawiono już dokumenty przychodowe. '@+_kom
       ,2,,__lp+=1)
   ?};
   _ok:=0
?};
_prod:=exec('tte_lic','tte');
_pdok:={? _prod='T' &
         (' '+exec('get','#params',500701,2,null()))*(' '+ND.TYP().T+' ')>0 |
         exec('is_usluga_typ','zl_uslugi',ND.TYP)
       || 1
       || 0
       ?};

:: sprawdzenie powiazania z zakupami
{? ND.TYP().DK<>'T' & ND.TYP().P='T' & (_opc:=exec('sprdk2fp','magdok_wspolne',ND.ref(),'Z',1); _opc=0 | _opc=3)
|| exec('add_kom','#message','Dokument powiązany z fakturą zakupową. '@+_kom,2,,__lp+=1);
   _ok:=0
?};
{? ND.TYP().DK<>'T' & ND.TYP().P='T' & ND.TYP().UE='T' & (_opc:=exec('sprdk2fp','magdok_wspolne',ND.ref(),'E',1); _opc=0 | _opc=3)
|| exec('add_kom','#message','Dokument powiązany z fakturą wewnętrzną. '@+_kom,2,,__lp+=1);
   _ok:=0
?};
{? ND.MAG().PAL='T' & ND.MD<>null & ND.MD().PAL='T' & ~exec('czyp2dok','magdok_palety',ND.ref(),1)
|| _ok:=0
?};

:: sprawdzenie powiazania z kooperacja
{? _prod='T' & ND.TYP().KOOP='T' & exec('sprdk2zgp','zl_guide',ND.ref())
|| exec('add_kom','#message','Dokument powiązany z pozycją przewodnika. '@+_kom,2,,__lp+=1);
   _ok:=0
?};

:: sprawdzenie powiazania z kooperacja - powiazanie robocizny z dokumentami RP
{? _prod='T' & ND.TYP().KOOP='T' & ND.TYP().P='N' & exec('chk_nd_zlnd','zl_koop',ND.ref())=0
|| exec('add_kom','#message','Należy najpierw usunąć dokumenty powiązane. '@+_kom,2,,__lp+=1);
   _ok:=0
?};

:: sprawdzenie powiązania z reklamacją
{? (_rek:=exec('dokWITHrek','reklamacje',ND.uidref()); _rek<>'')
|| {? 1+_rek='S'
   || exec('add_kom','#message','Dokument powiązany z reklamacją klienta %1. '@[2-_rek]+_kom,2,,__lp+=1)
   || exec('add_kom','#message','Dokument powiązany z reklamacją dostaw %1. '@[2-_rek]+_kom,2,,__lp+=1)
   ?};
   _ok:=0
?};

{? _ok & (_ko:=exec('mozna_wyc_dost','statexam',ND.ref()))
|| _txt:='';
   {? (_ko=1 | _ko=3)
   || _txt+='zmieniano status dostawy'@;
      {? exec('get','#params',8801,2,OPERATOR.USER)<>'T' || _ok:=0 ?}
   ?};
   {? (_ko=2 | _ko=3)
   || _txt+={? _txt='' || '' || ', ' ?}+'dostawa powiązana z badaniem'@;
      {? ~exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_DWYK') || _ok:=0 ?}
   ?};
   _txt:='Uwaga: '+_txt;
   {? ~_ok || _txt+='. '+_kom ?};
   exec('add_kom','#message',_txt,{? _ok || 4 || 2 ?},,__lp+=1)
?};

{? (_ok=1 & ND.TYP().TD<>'') | (_ok=1 & _prod='T' & _pdok=1)
||
:: NUCO - MM/MP
   _n_utw:=tab_tmp(
      ,'REFERENC','STRING[20]','REFERENCE'
      ,'NR','INTEGER','NR'
      ,'SYM','STRING[20]','SYM'
      ,'ZAK','STRING[1]','ZAK'
      ,'MAG','STRING[8]','MAG'
      ,'Z','STRING[1]','Z'
   );
   _jestnd:=0;
   {? ND.MD<>null() |
      (' '+exec('get','#params',500701,2,null()))*(' '+ND.TYP().T+' ')<>0 |
      exec('is_usluga_typ','zl_uslugi',ND.TYP)
   || _mm_ref:=$ND.ref();
      _mp_name:='nagdo'+{? ND.MD=null() || ND.ODDZ || ND.MD().ODDZ ?}+(ND.name()+2);
      ND.cntx_psh();
      ND.use(_mp_name);
      ND.index('MM');
      ND.prefix(_mm_ref,_mm_ref);
      {? _jestnd:=ND.first()
      ||
         {!
         |?
:: NUCO MM/MP
            _n_utw.blank();
            _n_utw.REFERENC:=$ND.ref();
            _n_utw.NR:=ND.NR;
            _n_utw.SYM:=ND.SYM;
            _n_utw.ZAK:=ND.ZAK;
            _n_utw.MAG:=ND.MAG().SYM;
            _n_utw.Z:=ND.STAT_REJ;
            _n_utw.add();
            ND.next()
         !}
      ?};
      ND.cntx_pop()
   ?};

   _nr:=0;
   _sym:='';
   {? _n_utw.first() || _nr:=_n_utw.NR; _sym:=_n_utw.SYM ?};
   {? _jestnd>0
   ||
      {? exec('get','#params',600000,2)='N'
      || _ok:=0;
         exec('add_kom','#message','Wycofywany dokument %1 utworzył'@[ND.SYM],7,,__lp+=1);
         exec('add_kom','#message','dokument %1 nr %2 symbol %3'@[ND.TYP().TD,$_nr,_sym],7,,__lp+=1);
         exec('add_kom','#message','w magazynie %1.'@[ND.MD().SYM],7,,__lp+=1);
         exec('add_kom','#message','Parametr systemu nie pozwala usunąć takiego dokumentu.'@,4,,__lp+=1);
         exec('add_kom','#message',_kom,2,,__lp+=1)
      |? _n_utw.ZAK='T'
      || _ok:=0;
         exec('add_kom','#message','Wycofywany dokument %1 utworzył'@[ND.SYM],7,,__lp+=1);
         exec('add_kom','#message','zaksięgowany dokument %1 nr %2 symbol %3'@[ND.TYP().TD,$_nr,_sym],7,,__lp+=1);
         exec('add_kom','#message','w magazynie %1.'@[ND.MD().SYM],7,,__lp+=1);
         exec('add_kom','#message',_kom,2,,__lp+=1)
      ||
         __st_oddz:='';
         {? ST.ODDZ<>ND.MD().ODDZ || __st_oddz:=ST.ODDZ; ST.ODDZ:=ND.MD().ODDZ ?};
         _ok:=exec('czy_z_ok','okresy',1,0,ST.AR,ST.AM,ND.MD);
         {? __st_oddz<>'' || ST.ODDZ:=__st_oddz ?};
         {? _ok=1
         ||
            {?
               {? _a=1 & ND.sel_size=0
               || {? (_prod='N' | _prod='T' & _n_utw.size<=1 & (' '+exec('get','#params',500701,2,null()))*(' '+ND.TYP().T+' ')=0)
                  ||
:: NUCO MM/MP
                     {? _n_utw.Z='Z'|_n_utw.Z='N'
                     || FUN.ask('Wycofywany dokument utworzył dokument %1'
                        ' nr %2 symbol %3\n w magazynie %4'
                        '.\n\nWycofać komplet dokumentów?'@[ND.TYP().TD,$_nr,_sym,ND.MD().SYM])
                     || exec('add_kom','#message','Wycofywany dokument %1 utworzył'@[ND.SYM],7,,__lp+=1);
                        exec('add_kom','#message','dokument %1 nr %2 symbol %3'@[ND.TYP().TD,$_nr,_sym],7,,__lp+=1);
                        exec('add_kom','#message','w magazynie %1.'@[ND.MD().SYM],7,,__lp+=1);
                        exec('add_kom','#message','Przed wycofaniem dokumentu %1 wycofaj akceptacje dokumentu %2.'@[ND.SYM,_n_utw.SYM],4,,__lp+=1);
                        exec('add_kom','#message',_kom,2,,__lp+=1);
                        0
                     ?}
                  ||
:: NUCO MM/MP
                     _z:=0;
                     _n_utw.cntx_psh();
                     _n_utw.clear();
                     _txt:='\n';
                     {? _n_utw.first()
                     || {!
:: NUCO MM/MP
                        |? _txt+='\n %1 w magazynie %2, zakończony: %3'@[_n_utw.SYM,_n_utw.MAG,_n_utw.Z];
                           {? _n_utw.Z='T' || _z:=1 ?};
                           _n_utw.next()
                        !}
                     ?};
                     _n_utw.cntx_pop();
:: NUCO MM/MP
                     {? ~_z
                     || FUN.ask('Wycofywany dokument utworzył dokumenty: %1'
                        '\n\nWycofać komplet dokumentów?'@[_txt])
                     || exec('add_kom','#message','Wycofywany dokument %1 utworzył'@[ND.SYM],7,,__lp+=1);
                        exec('add_kom','#message','dokumenty: %1'@[_txt],7,,__lp+=1);
                        exec('add_kom','#message','Przed wycofaniem dokumentu %1 wycofaj akceptacje ze wszystkich powiązanych dokumentów.'@[ND.SYM],4,,__lp+=1);
                        exec('add_kom','#message',_kom,2,,__lp+=1);
                        0
                     ?}
                  ?}
               || 1
               ?}
            ||
               {? _prod='T' & _n_utw.size>1 & _pdok=1
               || _wyc_dok:=_n_utw.size+1
               || _wyc_dok:=2
               ?}
            ||
               _ok:=0
            ?}
         ||
            exec('add_kom','#message','W magazynie %1 zablokowano modyfikowanie dokumentów.'@[ND.MD().SYM],4,,__lp+=1);
            exec('add_kom','#message',_kom,2,,__lp+=1)
         ?}
      ?}
   ?}
?};
{? _ok=1  & ND.NDSQL<>''
|| TYPYDOK.cntx_psh();
   TYPYDOK.prefix();
   ND.cntx_psh();
   ND.prefix();
   {? ND.seek(ND.NDSQL,form(8+ND.NDSQL))
   ||
::    sprawdzenie czy tworzony dokument byl automatycznie akceptowany
:: NUCO MM/MP
      {? 0 & exec('get','#params',600000,2)='T'
      ||
         exec('add_kom','#message','Wycofywany dokument został utworzony'@,7,,__lp+=1);
         exec('add_kom','#message','z dokumentu %1 nr %2 w magazynie %3.'@[ND.TYP().T,$ND.NR,ND.MAG().SYM],7,,__lp+=1);
         exec('add_kom','#message','Dokument %1 należy wycofać jako pierwszy.'@[ND.SYM],2,,__lp+=1);
         _ok:=0
      ?}
   ?};
   ND.cntx_pop();
   TYPYDOK.cntx_pop()
?};
:: kontrola czy pozniej byla korekta dostawy (tylko dla magazynu DOSTAWY, DOSTEWI który nie jest celnym)
{? _ok=1 & ';DFL'*(1+ND.MAG().TYP)>1 & ND.MAG().SKL='N' & ~((';KR-;KR+;'*ND.TYP().T)>1)
|| _data:={? (1+ND.MAG().TYP)='D' || date(0,0,0) || ND.D ?};
   DK.cntx_psh();
   DK.index('DOKMAG');
   DK.prefix(ND.ref);
   {? DK.first()
   ||
      {!
      |?
::       kontrola cen magazynowych podczas wycofywania akceptacji dokumentu
::       sprawdza cene aktualna (po ewentualnej korekcie)
         {? (DK.PLUS='N' & DK.N().TYP().DK<>'T' & ~((DK.N().INN='T' & DK.N().TYP().INW='E' & DK.N().TYP().P='N')))
         | (DK.PLUS='T' & DK.N().TYP().DN='T')
         ||
            _head:=0;
            _cena_mag:={? ND.MAG().TYP*'EWI'
                       || exec('biez_cen','ceny_dok',DK.M,DK.N().MAG,DK.N().D)
                       || exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C,,,_data)
                       ?};
            {? DK.C<>_cena_mag
            ||
               {? _head=0
               ||
                  exec('add_kom','#message','Wskazane pozycje zostały skorygowane wartościowo:'@,7,,__lp+=1);
                  _head:=1
               ?};
               exec('add_kom','#message','Pozycja %1. Indeks: %2'@[$DK.P,DK.M().KTM],7,,__lp+=1);
               _ok:=0
            ?}
         ?};
         DK.next()
      !};
      {? _ok=0
      || exec('add_kom','#message',_kom,2,,__lp+=1)
      ?}
   ?};
   DK.cntx_pop()
?};
FAKS.cntx_psh();
{? ND.FAKS().WHERE='K'
||
   exec('add_kom','#message','Dokument %1 został wygenerowany z korekty zbiorczej %2.'@[ND.SYM,FAKS.SYM],19,,__lp+=1)
?};
FAKS.cntx_pop();

_ref_n:=ND.ref();
_czypal:=ND.MAG().PAL='N' & ND.MD<>null;
_notkor:=ND.TYP().DK='T' & ND.TYP().Z='T';
_no_sel:=ND.sel_size()=0;
{? _ok & ~_a & _e
|| _ask:=_e
|? _ok & _a & _no_sel
|| _ask:=0;
:: NUCO - zmiana dla potrzeb obsługi MM
   {? ND.NDSQL<>'' & ND.TYP().ZLEC<>'T'
   || _upr:=4;
      {? ND.STAT_REJ='Z'
      || FUN.info('Dokument powstał na podstawie innego dokumentu wycofanie zakończenia nie jest możliwe.\n'
                  'Aby go usunąć wycofaj dokument źródłowy: %1'@[exec('FindAndGet','#table',ND,ND.NDSQL,,"SYM",'')])
      ?}
   ?};
:: KONIEC
   {? ND.TYP().VALREC<>null() & ~exec('validate','wzorce',ND.TYP().VALREC,ND,ND.ref())
   || _ask:=0
   |? _upr=1 & ND.STAT_REJ='T' & _wyc_dok<2
   || _ask:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia dokumentu %1'@[ND.SYM]
             ,,'Akceptację i &zakończenie','&Akceptację')
   |? (_upr=1 | _upr=2) & (ND.STAT_REJ='Z' | _wyc_dok>=2)
   || _ask:=FUN.ask('Wycofać zakończenie redakcji dokumentu %1?'@[ND.SYM])
   |? _upr=4 & ND.STAT_REJ='T'
   || _ask:=FUN.ask('Wycofać akceptację dokumentu %1?'@[ND.SYM]);
      {? _ask || _ask:=2 ?}
   ?}
|? _ok & ~_no_sel & var_pres('__askwyc')>0
|| {? ND.TYP().VALREC<>null() & ~exec('validate','wzorce',ND.TYP().VALREC,ND,ND.ref())
   || _ask:=0
   || _ask:=__askwyc
   ?}
|| _ask:=1
?};

{? _notkor & _ask || _notkor+=_ask; _ask:=0 ?};
:: sprawdzenie czy nie spowoduje stanow minusowych
{? _ask=1
|| {! _i:=1.._wyc_dok
   |!
      ND.cntx_psh();

      _msk:=0;
      {? _i>=2 & (6+_n_utw[1])+1<>ST.ODDZ
      ||
         _msk:=1;
         exec('open','open_tab',(6+_n_utw[1])+1,2-$ST.AR)
      ?};

      ND.prefix();
      {? _i=1
      ||
         {? ~ND.seek(_ref_n)
         || undo();
            exec('add_kom','#message','Nie znaleziono dokumentu.'@,4,,__lp+=1)
         ?}
      |? _i>=2
      ||
         _ref:=_n_utw[1];
         {? ~ND.seek(_ref,form(8+_n_utw[1]))
         || undo();
            exec('add_kom','#message','Nie znaleziono dokumentu powiązanego.'@,4,,__lp+=1)
         ?};
         {? _prod='T' & _pdok=1
         || _n_utw.next()

         ?}
      ?};
      _czyzakc:=ND.Z='T' | (';ZT'*ND.STAT_REJ)>1;
      {? _ok=1 & _czyzakc & ~_notkor
      ||
         DK.index('DOKMAG');
         DK.prefix(ND.ref());
         SC.index('SN');
         SC.prefix(ND.MAG);
         {? DK.first()
         ||
            {!
            |?
               {? DK.M().RODZ='T'
               ||
                  {? SC.find_key(DK.M,DK.RDK,DK.NDK)
                  ||
                     _stan:=SC.S+{?DK.PLUS='T'||-1||1?}*DK.IL;
                     {? _stan<0
                     ||
                        exec('add_kom','#message','Wycofanie spowoduje ujemny stan materiału:'@,4,,__lp+=1);
                        exec('add_kom','#message','Mag.: %1, dok. %2'
                        ', poz. %3: %4 - %5'@[DK.N().MAG().SYM,DK.N().SYM,$DK.P,DK.M().KTM,DK.M().N],4,,__lp+=1);
                        _ok:=0
                     ||
                        {? SC.count()>0
                        ||
                           INP.index('SC');
                           INP.prefix(SC.RDK,SC.NDK);
                           {? INP.first()
                           ||
                              {? ~(DK.N().INN='T' & DK.N().TYP().INW='I')
                               & {? INP.IN().D>DK.N().D
                                 || 1
                                 |? INP.IN().D=DK.N().D
                                 || {? INP.IN().T=time(0,0,0) | DK.N().T=time(0,0,0)
                                    || 1
                                    || DK.N().T<=INP.IN().T
                                    ?}
                                 || 0
                                 ?}
                              ||
                                 exec('add_kom','#message','Wycofywany stan jest inwentaryzowany:'@,7,,__lp+=1);
                                 exec('add_kom','#message','Poz. %1: %2 - %3'@[$DK.P,DK.M().KTM,DK.M().N],7,,__lp+=1);
                                 _ok:=0
                              ?}
                           ?}
                        ?}
                     ?};
::                komunikat dotyczacy "zaburzonych" ilosci w wydaniu
                     {? DK.N().TYP().P='T'
                     || {? _ok & exec('obl_ssc','magazyn_stan',DK.RDK,DK.NDK,ND.MAG,DK.M,'S')>0
                        || {? DK.N().TYP().KOOP='T'
                           ||
::                         Jesli dokument kooperacyjny to nie pozwalam wycofac
                              _ok:=0
                           ?};
                           exec('add_kom','#message','Dostawa na niezaakceptowanych dokumentach wydania:'@,4,,__lp+=1);
                           exec('add_kom','#message','Poz. %1: %2 - %3'@[$DK.P,DK.M().KTM,DK.M().N],4,,__lp+=1)
                        |? _ok & (1+ND.MAG().TYP)<>'D'
                         & ((exec('FindInSet','#table','SM','SM',DK.M,ND.MAG,"SM.S",,,0)
                         -exec('iledk_nd','magdok_nag',ND.ref(),DK.M)
                         -exec('FindInSet','#table','SM','SM',DK.M,ND.MAG,"SM.R",,,0))
                         -exec('ilwydmat','magdok_wspolne',DK.M,ND.MAG))<0
                        ||
                           exec('add_kom','#message','Stan ujemny dostawy dla niezaakceptowanych dokumentów:'@,4
                            ,,__lp+=1);
                           exec('add_kom','#message','Poz. %1: %2 - %3'@[$DK.P,DK.M().KTM,DK.M().N],4,,__lp+=1)
                        ?}
                     ?}
                  ?}
               ?};
               DK.next()
            !}
         ?};

         {? _ok=0
         || exec('add_kom','#message',_kom,2,,__lp+=1)
         ?}

      ?};

      {? _ok=1
      || _ok:=exec('spr_rdst','magdok_wspolne')
      ?};

      {? _msk=1
      ||
         exec('open','open_tab',ST.ODDZ,2-$ST.AR)
      ?};

      ND.cntx_pop()
   !}
?};

{? _ask=2 & _wyc_dok<2
|| {? ND.STAT_REJ='T'
   || ND.STAT_REJ:='Z';
      {? ND.put(1) || _wyn:=1 || undo() ?}
   || _wyn:=0
   ?}
|? _ask=1 & _ok=1 &
   {? _wyc_dok>=2
   || 1
   ||
      {? _a & ND.sel_size=0
      || _ask:=1
      || 1
      ?}
   ?}
||
   _pkom:=ND.TYP().TD<>'';
   _listkom:='';
   _do_state:=do_state();
   {? _do_state=0 || do() ?};
   {? _pdok=1
   || _n_utw.first()
   ?};
:: usuniecie kosztow celnych
   exec('usu_kos','magdok_nag');

   {! _i:=1.._wyc_dok
   |!
      ND.cntx_psh();

      _msk:=0;
      {? _i>=2 & (6+_n_utw[1])+1<>ST.ODDZ
      ||
         _msk:=1;
         exec('open','open_tab',(6+_n_utw[1])+1,2-$ST.AR)
      ?};

::    usunięcie pozycji z tabeli pozycji dostaw magazynowych
      {? exec('get','#params',100210,2)='T'
      ||
         ND.cntx_psh();
         TYPYDOK.cntx_psh();
         ND.prefix();
         {? ND.seek({? _wyc_dok=2 || _n_utw[1] || _ref_n ?})
            & ND.TYP().P='T' & TYPYDOK.DK<>'T' & TYPYDOK.DN<>'T'
         ||
            DK.cntx_psh();
            DKDOST.cntx_psh();
            FIRMA.cntx_psh();
            DKDOST.index('UNIK');
            DKDOST.prefix();
            DK.index('DOKMAG');
            DK.prefix(ND.ref);
            _loop:=DK.first();
            {!
            |? _loop
            |!
               _srdk_uidref:=exec('FindAndGet','#table',DK,DK.SRDK,,"DK.uidref()",'');
               {? DKDOST.find_key(REF.FIRMA().SYMBOL,_srdk_uidref)
               ||
                  DKDOST.del()
               ?};
               _loop:=DK.next()
            !};
            FIRMA.cntx_pop();
            DKDOST.cntx_pop();
            DK.cntx_pop()
         ?};
         ND.cntx_pop();
         TYPYDOK.cntx_pop()
      ?};

      ND.prefix();
      {? _i=1
      ||
         {? ~ND.seek(_ref_n)
         || undo();
            exec('add_kom','#message','Nie znaleziono dokumentu.'@,4,,__lp+=1)
         ?}
      |? _i>=2
      ||
         _ref:=_n_utw[1];
         {? ~ND.seek(_ref,form(8+_n_utw[1]))
         || undo();
            exec('add_kom','#message','Nie znaleziono dokumentu powiązanego.'@,4,,__lp+=1)
         ?};
         {? _prod='T' & _pdok=1
         || _n_utw.next()
         ?}
      ?};

      DK.index('DOKMAG');
      DK.prefix(ND.ref());
      _czyzakc:=ND.Z='T' | (';ZT'*ND.STAT_REJ)>1;
      {? DK.first() & _czyzakc
      || {? _prod='T' &
            ((' '+exec('get','#params',500701,2,null()))*(' '+ND.TYP().T+' ')>0 |  ND.TYP().WYR='T' ) |
            exec('is_usluga_typ','zl_uslugi',ND.TYP) &
            ND.TYP().DK='N'
         || exec('aktuzl','zl_common',ND.ref(),-1);
            ~~
         ?};
         DK.first();
         {!
         |?
            DK.cntx_psh();
            {? _prod='T'
            || {? ND.TYP().ZLEC<>'N' & ND.TYP().WYR='N' | ND.TYP().DK='T'
               || {? DK.ZL<>null
                  || ND.STAT_REJ:='N';
                     ND.Z:='N';
                     ND.INTRAKC:='N';
                     ND.put();
                     _wyn:=exec('akzlst','magdok_poz',DK.ZL,-1,DK.WYD);
                     ND.STAT_REJ:='T';
                     ND.Z:='T';
                     {? ND.IST_TYP<>'' || ND.INTRAKC:=exec('get','#params',100202,2) ?};
                     ND.put()
                  ?}
               ?}
            ||
               {? DK.ZL<>null
               ||
                  {? exec('akzlst','magdok_poz',DK.ZL,-1,DK.WYD)
                  || _wyn:=1
                  || exec('add_kom','#message','Nie powiodła się aktualizacja powiązanych zleceń.'@,7,,__lp+=1);
                     _wyn:=0
                  ?}
               ?}
            ?};
            DK.cntx_pop();
            DK.get();
            {? DK.M().RODZ='T' & (DK.N().TYP().DK<>'W' | (DK.M().J2<>null() & DK.IL=0 & DK.IL2<>0))
            || _ok:=exec('DELSTAN','magazyn_stan',DK.N().MAG,DK.M,{? DK.PLUS='T' || ND.D || DK.DOST ?}
                     ,DK.C,DK.PLUS,DK.RDK,DK.NDK)
            || {? DK.N().TYP().DK='W' & DK.IL=0
               || exec('stan_zb','magazyn_stan',DK.N().MAG,DK.M,0,DK.WAR,ND.AR,{? DK.M().J2<>null() || DK.IL2 || 0 ?})
               ?};
               DK.Z:='N'
            ?};
            {? ~_ok
            || undo();
               exec('add_kom','#message',_kom,2,,__lp+=1)
            ?};
            {? _ok & DK.SCEAN<>'' & DK.M().IDMOB='D' & (DK.N().INN='T' & DK.N().TYP().INW='I' & DK.N().TYP().P='T')
            || exec('mkodkadd','kody_kresk',DK.M,DK.SCEAN,DK.PRDK,3)
            ?};
            DK.next()
         !};
         _tmg:=(1+ND.MAG().TYP);
::       dokumenty inwentaryzacji
         {? _tmg<>'E' & ND.INN='T' & ND.TYP().INW='I' & ND.TYP().P='N' || _tmg:='D' ?};
         {? _tmg='D' & (ND.TYP().AFIFO<>'' | ND.MAG().IL='T') || _tmg:=ND.TYP().AFIFO; {? _tmg='' || _tmg='F' ?} ?};
         {? _ok & 'FLŚE'*_tmg & ND.TYP().P<>'T' || exec('akt_fifo','magdok_poz') ?};
         ND.STAT_REJ:='N';
         ND.Z:='N';
         ND.INTRAKC:='N';
         exec('dk_sum','magdok_wspolne',ND.ref());
         ND.WAR:=SUM.WAR;
         _wyn:=exec('data_akcept','magdok_nag','N',_i=1 & _wyc_dok>1,POM.PAR2)
      || _bylakc:=ND.STAT_REJ<>'N';
         ND.STAT_REJ:='N';
         ND.Z:='N';
         ND.INTRAKC:='N';
         exec('dk_sum','magdok_wspolne',ND.ref());
         ND.WAR:=SUM.WAR;
         _wyn:={? _bylakc || exec('data_akcept','magdok_nag','N',_i=1 & _wyc_dok>1,POM.PAR2) || 1 ?}
      ?};
      {? _ok & _i>=2 & ND.Z='N'
      || _czypal:=_czypal & ND.MAG().PAL='T';
         {? _pkom
         ||
            DK.index('DOKMAG');
            DK.prefix(ND.ref());
            {? DK.first
            ||
               {!
               |? {? _listkom*($DK.M)=0 || _listkom+=$DK.M ?};
                  DK.next
               !}
            ?}
         ?};
         exec('n_usun','magdok_nag',0,0,,,0,,1);
         {? _czypal || {? ~exec('del_ppal','magdok_palety',_ref_n) || _wyn:=0; undo() ?} ?}
      ?};

      {? _msk=1
      ||
         exec('open','open_tab',ST.ODDZ,2-$ST.AR)
      ?};

      ND.cntx_pop()
   !};

:: wycofanie dokumentu opakowan
   {? _wyn & ~exec('nd_z','opakow',0)
   || undo();
      exec('add_kom','#message','Akceptacja powiązanego dokumentu opakowań nie została wycofana.'@,4,,__lp+=1);
      _wyn:=0
   ?};

:: usuniecie powiazanych wykonan do kooperacji
   {? ND.TYP().KOOP='T' & ND.TYP().P='N' & exec('del_nd_wyk','zl_koop',ND.ref())=0
   || undo();
      exec('add_kom','#message','Usunięcie wykonań do kooperacji nie powiodło się.'@,4,,__lp+=1);
      _wyn:=0
   ?};
   {? _wyn & (ND.get(); ND.Z='N') || exec('ilmg2ZAM','magdok_wspolne',ND.ref(),0) ?};

   {? _do_state=0 || end() ?};

:: aktualizacja statusu zamowien
   _ok:=1;
   ND.cntx_psh();
   ND.prefix();
   {? ~ND.seek(_ref_n) || _ok:=0 ?};
   {? _ok=1
   ||
      {? ND.sel_size=0 || {? ND.TYP().P='T' || exec('utw_zk_tymc','zamsiw_wspolne') ?} ?};
      _list_m:='';
      {? _pkom
      ||
         {!
         |?
            exec('obl_stan','magazyn_stan',exec('FindAndGet','#table','M',_listkom+16,,,null()),1,ND.MD);
            _listkom-=16;
            _listkom<>''
         !}
      ?};
      DK.index('DOKMAG');
      DK.prefix(ND.ref());
      {? DK.first()
      ||
         {!
         |?
            {? _list_m * ('|'+$#DK.M+';')=0
            ||
               _list_m+='|'+$#DK.M+';';
               exec('obl_stan','magazyn_stan',DK.M,1,DK.N().MAG);
               {? _wyc_dok=2 & _pkom=0 || exec('obl_stan','magazyn_stan',DK.M,1,DK.N().MD) ?};
               {? DK.N().TYP().P='T' || __uzup_z(DK.M) ?}
            ?};
            DK.next()
         !}
      ?};
      {? ND.sel_size=0
      || {? ND.TYP().P='T'
         || exec('Rez2Rez','zamsiw_wspolne',0,ND.ref());
            exec('akt_wgdk','zamsiw_wspolne',ND.ref());
            exec('odt_zk_tymc','zamsiw_wspolne')
         ?}
      ?}
   ?};
   ND.cntx_pop()
?};
{? _notkor>1
|| {? _notkor=2
   || ND.DA:=date(0,0,0);
      ND.TA:=time(0,0,0);
      ND.Z:='N';
      ND.STAT_REJ:='N';
      ND.INTRAKC:='N';
      {? ND.put(1)
      || _wyn:=1;
         DK.cntx_psh();
         DK.index('DOKMAG');
         DK.prefix(ND.ref());
         {? DK.first() || {! |? DK.Z:='N'; DK.put(1); DK.next() !} ?};
         DK.cntx_pop()
      ?}
   || ND.STAT_REJ:='Z';
      ND.put(1)
   ?}
?};
:: rearchiwizacja cech dostawy
{? ~_notkor & (1+ND.MAG().TYP)='D' || exec('atrnarch','mat_atr',ND.ref(),0) ?};
{? _c || ND.r_unlock() ?};
{? _wyn=1 & (ND.get();';NZ'*ND.STAT_REJ)>1 & _recplug<>ND.STAT_REJ || Plugin.run('AFTER_RECALL_001','DM') ?};

DK.cntx_pop();

VAR_DEL.delete('__aktzkp');
VAR_DEL.delete('__bufpal');

:: komunikaty
{? _wyn=1
|| _kom:={? _stat_rej='Z' & _ask=1
         || 'zakończenie rejestracji'
         |? _stat_rej='T' & _ask=1
         || 'akceptację i zakończenie rejestracji'
         |? _stat_rej='T' & _ask=2
         || 'akceptację'
         || ''
         ?};
   exec('add_kom','#message','Wycofano %1 dokumentu.'@[_kom],1)
|? ~_wyn & ~_no_sel
|| exec('add_kom','#message','Pominięto dokument.'@,1)
?};
{? ND.sel_size=0 & _b='T' || exec('wycadok','magdok_nag'); {? _ask || exec('end_kom','#message') ?} ?};

_wyn


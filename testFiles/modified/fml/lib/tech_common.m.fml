:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tech_common.fml
:: Utworzony: 05.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi wspólnych elementów na technologii (nie dotyczy nagłówków)
::            Biblioteka obsługuje tabele TOPER, TECHZAMS, TMAT, TCHMAT, TACTTLS
::======================================================================================================================

\tktl_analizas_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Analiza dostępności surowców w czasie - akcja 'przed grupą' dla tabeli TKTL
::  MOD: SITEK - dodanie możliwości wykonania analizy grupowej z inna iloscia i data wymagalnosci dla pozycji
::----------------------------------------------------------------------------------------------------------------------
exec('env_create','tech_structure',1);
{? ask('Czy podać ilość i termin wymagalności taki sam dla wszystkich pozycji?','Ilość i termin dla pozycji',1,'NIE','TAK')
||
_can_continue:=exec('edit_params','tech_structure',1,1,1);
_dlaKazdejOsobno:=0
||
_can_continue:=1;
_dlaKazdejOsobno:=1
?};
_env:=__ENV_STR;
::  Rozpoczynam pętle
_exit:=1;
_firstRun:=1;
{!
|?
   {? var_press('TAB',__ENV_STR.STRUCT)>0 || __ENV_STR.STRUCT.TAB.erase  ?};
   {? var_press('ELEMENT',__ENV_STR.STRUCT)>0 || __ENV_STR.STRUCT.ELEMENT.erase  ?};
   {? var_press('TAB',__ENV_STR.CHECK) > 0 || _env.CHECK.TAB.erase ?};

   {? _can_continue>0
   ||
      {? var_press('_selected')<=0 || _selected:=TKTL.sel_aget() ?};
      TKTL.sel_adel();
      {? _selected.first()
      || 
        {!
         |? TKTL.cntx_psh();
            TKTL.prefix();
            {? TKTL.seek(_selected.REF)
            ||
               {? _dlaKazdejOsobno & _firstRun > 1
                ||
                 _wybranaPozycja:='Dla: %1'@[TKTL.KTM().KTM] ;
                 _can_continue:=exec('edit_params','tech_structure',1,1,1,_wybranaPozycja)
               ?};
               {? var_pres('_args') > 0 || obj_del(_args) ?};
               _args:=exec('add_a','tech_structure');
               _args.M:=TKTL.KTM;
               _args.IL:={? _env.PARAMS.IL=0 || TKTL.XJM || _env.PARAMS.IL ?};
               _args.DW:=_env.PARAMS.DW;
               _args.TW:=time(0,0,0);
               _args.TKTL:=TKTL.ref();
               _args.SRC_REF:=$TKTL.ref();
               exec('add','tech_structure',_args);
               exec('expand','tech_structure',_args.ref)
            ?};
            TKTL.cntx_pop();
            _selected.next()
         !}
      ?}
   ?};
::     Wyświetlam tabelę z problemami i uzupełniam tabelę zamienników
   _ok:=1;
   {? _env.CHECK.TAB.size()>0
   || _env.CHECK.TAB.win_sel(_env.CHECK.WERA);
      _ok:=exec('tktl_chck_sel','tech_structure',1);
      {? _ok=0 || _exit:=0 ?}
   || _exit:=0
   ?};
:: MOD: SITEK - jesli wybrana opcja dotyczy innej ilosci dla kazdej pozycji i nie było błędów w pierwszym przebiegu powtórz przebieg pytajac o ilosci i daty
{? _ok=1 & _exit=0 & _firstRun=1 & _dlaKazdejOsobno
||
_firstRun:=_firstRun+1;
_exit:=1
||
_firstRun:=_firstRun+1

?};
_exit
!};
::Koniec petli

{? _ok
|| exec('cumulate','tech_structure');
   exec('select','tech_structure')
?};
{? var_press('_selected')>0 || obj_del(_selected) ?};
~~

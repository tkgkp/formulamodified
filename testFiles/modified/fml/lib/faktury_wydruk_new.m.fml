:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: faktury_wydruk.fml
:: Utworzony: 04.05.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa wydruku dokumentu sprzedaży, zakupu
::======================================================================================================================


\begin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: kluzula BEGIN fak_new.rpm
::   WE:  _a  - nazwa wydruku
::       [_b] - czy pokazywac panel dialogowy z parametrami ([1] -tak, 0 - nie) - gdy nie pokazuje panel nie zapisuje
::              rowniez ustawien wybranych
::       [_c] =1-odkładać konteksty; =0-wpp
::  OLD: \begin/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};
{? var_pres('_c')<>type_of(1) || _c:=1 ?};

{? _c || FAP.cntx_psh ?};

exec('end','faktury_wydruk',0);
__empty_char:={? exec('get','#params', 100236, 2)='N' || ^160 || ' ' ?};
headfoot:=0;

__wydrfa:=obj_new('uproszcz','samof');

:: do numeracji stron przy drukowaniu paczek faktur
pages:=0;

:: dla wydruku graficznego 1, dla iglowki 0
graf:=exec('get','#params',{? FAKS.SZ='S' || 2121 || 6121 ?},2,OPERATOR.USER)<>'T';
:: dla wydruku na skladance 1 (nie drukuje ORYGINAL/KOPIA)
skladanka:=exec('get','#params',{? FAKS.SZ='S' || 2122 || 6122 ?},2,OPERATOR.USER)='T';
duplex:=0;
:: odstep miedzykolumnowy
__ods:=exec('get','#params',{? FAKS.SZ='S' || 2125 || 6125 ?},2,OPERATOR.USER)='T';
__odsram:={? __ods || 3 || 3 ?};
__odsskr:={? __ods || 2 || 2 ?};
:: czy walutowa
__walh:=FAKS.WAL().KOD;
__walp:=FAKS.NWAL().KOD;
__waln:=INFO.NAROD().KOD;
wal_h:=1;
walutowa:=__walh<>__walp;
:: czy kwota vat liczona od ceny burtto
odbrutto:=FAKS.T().FIS='T';
:: uproszczony wydruk faktury
__wydrfa.uproszcz:=_a='fak_newu' | _a='fakpnewu';
:: czy samofakturowanie
__wydrfa.samof:=_a='zak_wew1' | _a='zakpwew1';
::[PD]sprawdzenie dla starszych werscji
VAR_DEL.delete('mpp');
mpp:=0;
mpp:={? exec('sp_active','faktury_wspolne') || 1 || 0 ?};

color:=prn1:=prn2:='';
lm:=ll:=vp:=lmt:=lmt_org:=rsep:=0;
wyjscie:=0;
exec('color_define','faktury_wydruk_new');

FAP.cntx_psh();
FAP.index('FAP'); FAP.prefix(FAKS.ref());
{? ~FAP.first() || FUN.info('Brak pozycji faktury.'@); wyjscie:=1 ?};
FAP.cntx_pop();

_wydr_naz:=_a;

PARAM_W.KOPIA:=2;
PARAM_W.ZAM:='N';
PARAM_W.WZ:='N';
{? _b | exec('czy_grp_dr','faktury_wydruk')=0
|| PARAM_W.DR_DUPL:='N';
   PARAM_W.DUPLIKAT:=date(0,0,0)
?};
_par:='IN';
PARAM_W.SLOWNIE:='T';
PARAM_W.JEZYK:=null;
PARAM_W.DR_RAB:='T';
PAR_WYDR.SHAD:=3;

_edokument:=_a='fakenew';

_wydrukil:=exec('wydrukil_prefix','faktury_wydruk_new',_a,_b,_edokument);
_b:=_wydrukil.SHOW_PARAM;
wyjscie:=_wydrukil.EXIT; {? wyjscie || return() ?};
{? ~WYDRUKIL.first()
||
   WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add()
||
   PARAM_W.KOPIA:=WYDRUKIL.TYP_ZEST;
   PARAM_W.ZAM:=WYDRUKIL.STR1;
   PARAM_W.WZ:=WYDRUKIL.STR2;
:   PARAM_W.DR_DUPL:=WYDRUKIL.STR3;
   {? _b | exec('czy_grp_dr','faktury_wydruk')=0
   ||
      PARAM_W.DUPLIKAT:={? PARAM_W.DR_DUPL='T' || date() || date(0,0,0) ?}
   ?};
   _par:=WYDRUKIL.FAKS_OD;
   PARAM_W.SLOWNIE:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?};
   PARAM_W.JEZYK:=WYDRUKIL.JEZYK;
   PARAM_W.DR_RAB:={? WYDRUKIL.CHKBOX02 | FAKS.SPOBLRAB='W'  || 'T' || 'N' ?}
?};
_jezyk_:=PARAM_W.JEZYK;
_djezyk:=exec('kh_jezyk','tlumaczenia',FAKS.KH,_edokument);
{? _b | _edokument
|| PARAM_W.JEZYK:={? _djezyk || _djezyk || _jezyk_ ?};
   PARAM_W.JEZYK_KH:={? _djezyk & _djezyk=PARAM_W.JEZYK || 'T' || 'N' ?}
|? exec('czy_grp_dr','faktury_wydruk') & ~_edokument
|| PARAM_W.JEZYK:={? __GRP_DR.JEZYK_KH='T' & _djezyk || _djezyk || __GRP_DR.JEZYK ?}
?};

PARAM_W.INDEKS:='N';
PARAM_W.NAZWA:='N';
PARAM_W.MC:='N';
PARAM_W.UWAGI:='N';
PARAM_W.KODOBCY:='N';
PARAM_W.DK_C:='N';
PARAM_W.KP:='N';
{? _par*'I' || PARAM_W.INDEKS:='T' ?};
{? _par*'N' || PARAM_W.NAZWA:='T' ?};
{? _par*'M' || PARAM_W.MC:='T' ?};
{? _par*'U' || PARAM_W.UWAGI:='T' ?};
{? _par*'K' || PARAM_W.KODOBCY:='T' ?};
{? _par*'C' || PARAM_W.DK_C:='T' ?};
{? _par*'P' || PARAM_W.KP:='T' ?};

{? FAKS.KRAJ_VAT & __walh<>__walp
|| PARAM_W.WALH:='H'
|? _b
|| PARAM_W.WALH:={? __walh<>__walp || 'T' || 'N' ?}
|| PARAM_W.WALH:={? __walh<>__walp ||  {? var_press('__pwwalh')=type_of('T') || __pwwalh || 'T' ?} || 'N' ?}
?};

:: zmiana okienka - inf o zamowieniach/inf o dok. mag.
PARAM_W.win_edit('WYDR_FAK');

{? wyjscie=0
||
   _chk_par:="
      _wyn:='';
      {? PARAM_W.KOPIA<0
      || FUN.info('Ilość kopii nie może być ujemna.'@);
         _wyn:='KOPIA'
      ?};
      {? _wyn='' & PARAM_W.DR_DUPL='T'
      || {? PARAM_W.DUPLIKAT=date(0,0,0)
         || FUN.info('Niewypełniona data wystawienia duplikatu.'@);
            _wyn:='DUPLIKAT'
         ?};
         {? _wyn=''
         || {? PARAM_W.DUPLIKAT<FAKS.DW
            || FUN.info('Data duplikatu wcześniejsza od daty wystawienia dokumentu.'@);
               _wyn:='DUPLIKAT'
            ?}
         ?}
      ?};
      {? _wyn=''
      || {? exec('czy_grp_dr','faktury_wydruk')=0
         || {? FAKS.KRAJ_VAT & __walh<>__walp
            || {? PARAM_W.WALH<>'H'
               || FUN.info('Wybrano kraj vat w nagłówku dokumentu.\n'
                           'Wydruk dostępny tylko w walucie handlowej'@);
                  _wyn:='WALH'
               ?}
            |? PARAM_W.WALH='H' & __walh=__walp
            || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\n'@+
                        'Wydruk w walucie handlowej niedostępny.'@);
               _wyn:='WALH'
            |? PARAM_W.WALH='T' & __walh=__walp
            || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\n'@+
                        'Wydruk w obu walutach niedostępny.'@);
               _wyn:='WALH'
            ?}
         ?}
      ?};
      _wyn
   ";
   {? {? _b=1 & exec('noparam','param_wydr',FAKS.SZ)=0 & exec('upr_cscz','ceny')
      || exec('set_efld_opt_paramw','faktury_wydruk',FAKS.SZ='S' & FAKS.SPOBLRAB='C');
         {? PARAM_W.edit(_chk_par)
         || 1
         || {? exec('czy_grp_dr','faktury_wydruk') || __GRP_DR.ESC:=1 ?}; 0
         ?}
      || 1
      ?}
   ||
      {? _b
      || WYDRUKIL.index('WYDRUKI');
         WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
         {? WYDRUKIL.first()
         ||
            WYDRUKIL.TYP_ZEST:=PARAM_W.KOPIA;
            WYDRUKIL.STR1:=PARAM_W.ZAM;
            WYDRUKIL.STR2:=PARAM_W.WZ;
            WYDRUKIL.STR3:=PARAM_W.DR_DUPL;
            WYDRUKIL.OD_DATY:=PARAM_W.DUPLIKAT;
            WYDRUKIL.FAKS_OD:='';
            {? PARAM_W.INDEKS='T'  || WYDRUKIL.FAKS_OD+='I' ?};
            {? PARAM_W.NAZWA='T'   || WYDRUKIL.FAKS_OD+='N' ?};
            {? PARAM_W.MC='T'      || WYDRUKIL.FAKS_OD+='M' ?};
            {? PARAM_W.UWAGI='T'   || WYDRUKIL.FAKS_OD+='U' ?};
            {? PARAM_W.KODOBCY='T' || WYDRUKIL.FAKS_OD+='K' ?};
            {? PARAM_W.DK_C='T'    || WYDRUKIL.FAKS_OD+='C' ?};
            {? PARAM_W.KP='T'      || WYDRUKIL.FAKS_OD+='P' ?};
            WYDRUKIL.CHKBOX01:=PARAM_W.SLOWNIE='T';
            WYDRUKIL.JEZYK:={? _djezyk & PARAM_W.JEZYK=_djezyk || _jezyk_ || PARAM_W.JEZYK ?};
            {? exec('czy_grp_dr','faktury_wydruk')
            || __GRP_DR.JEZYK:=PARAM_W.JEZYK;
               __GRP_DR.JEZYK_KH:=PARAM_W.JEZYK_KH
            ?};
            {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
            WYDRUKIL.CHKBOX02:=PARAM_W.DR_RAB='T';
            WYDRUKIL.put();
            __pwwalh:=PARAM_W.WALH
         ?}
      ?}
   ||
      wyjscie:=1
   ?}
?};

{? wyjscie=0
||
:: szerokosc kolumn
   VAR_DEL.delete('k');    k:=obj_new(16);
:: szerokosc kolumn dla podsumowania VAT dla dokumentow gdzie VAT liczony od sumy w stawkach
   VAR_DEL.delete('k1');   k1:=obj_new(4);
:: dane drukowane
   VAR_DEL.delete('w');    w:=obj_new(obj_len(k));
:: nazwy kolumn
   VAR_DEL.delete('n');    n:=obj_new(obj_len(k));
:: sumy
   VAR_DEL.delete('s');    s:=obj_new(6);
   VAR_DEL.delete('sp');   sp:=obj_new(13);
   VAR_DEL.delete('spz');  spz:=obj_new(3);
   VAR_DEL.delete('sw');   sw:=obj_new(13);
:: do przeniesienia
   VAR_DEL.delete('do_p'); do_p:=obj_new(6);
:: dane naglowkowe
   VAR_DEL.delete('na');   na:=obj_new(10);
:: przeniesienie
   VAR_DEL.delete('prz');  prz:=obj_new(6);
:: szerokosci kolumn naglowkowych
   VAR_DEL.delete('szer'); szer:=obj_new(4);
:: szerokosci pozycji faktur
   VAR_DEL.delete('fak');  fak:=obj_new(obj_len(k));
:: w tym VAT
   VAR_DEL.delete('vat');  vat:=obj_new(3);
   VAR_DEL.delete('vatk'); vatk:=obj_new(3);

   VAR_DEL.delete('PRN1'); PRN1:=obj_new(@.CLASS.PRINT,obj_len(k),__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   VAR_DEL.delete('PRN2'); PRN2:=obj_new(@.CLASS.PRINT,5,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   VAR_DEL.delete('PRN3'); PRN3:=obj_new(@.CLASS.PRINT,5,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   VAR_DEL.delete('PRN4'); PRN4:=obj_new(@.CLASS.PRINT,2,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   VAR_DEL.delete('PRN5'); PRN5:=obj_new(@.CLASS.PRINT,4,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');

:: opisy w obiektach PRN4 i PRN5
   op_1:=op_2:=op_3:=op_4:='';
:: Poz
   n[1]:=exec('tr','tlumaczenia','6000201');           k[1]:=4+{? mpp || (FAKS.SP_WYM='T')?};
:: Towar / usluga
   n[2]:=exec('tr','tlumaczenia','1000181');           k[2]:=40;
:: CN / PKWiU
   n[3]:=
      {? exec('get','#params',300208,2)='T'
      ||
         exec('tr','tlumaczenia','1000218')+'/'
      ||
         ''
      ?}+
      exec('tr','tlumaczenia','1000182');              k[3]:=10;
:: Ilosc
   n[4]:=exec('tr','tlumaczenia','1000183');           k[4]:=6;
:: jm
   n[5]:=exec('tr','tlumaczenia','1000184');           k[5]:=4;
   {? FAKS.T().FIS='T'
:: Cena brutto przed rab.
   || n[6]:=
         {? exec('czy_rab','faktury_wspolne')
         || exec('tr','tlumaczenia','1000185')
         || exec('tr','tlumaczenia','1000193')
         ?};                                          k[6]:=11
:: Cena netto przed rab.
   || n[6]:=
         {? exec('czy_rab','faktury_wspolne')
         || exec('tr','tlumaczenia','1000186')
         || exec('tr','tlumaczenia','1000188')
         ?};                                          k[6]:=10
   ?};
:: % rab.
   {? exec('czyrabp','ceny',FAKS.RAB_TYP)
   || n[7]:=exec('tr','tlumaczenia','1000187');          k[7]:=5
   || n[7]:=exec('tr','tlumaczenia','1000199');          k[7]:=6
   ?};
   {? odbrutto=1 & FAKS.NDVAT='N'
:: Cena brutto
   ||
      n[8]:=exec('tr','tlumaczenia','1000193');          k[8]:=10
   ||
:: Cena netto
      n[8]:=exec('tr','tlumaczenia','1000188');          k[8]:=10
   ?};
:: Wartosc netto
   n[9]:=exec('tr','tlumaczenia','1000189');             k[9]:=10;  k1[1]:=10;
:: Stawka VAT
   n[10]:=exec('tr','tlumaczenia','1000190');           k[10]:=6;
:: Kwota VAT
   n[11]:=exec('tr','tlumaczenia','1000191');           k[11]:=10;  k1[2]:=10;
:: Wartosc brutto
   n[12]:=exec('tr','tlumaczenia','1000192');           k[12]:=10;  k1[3]:=10;
:: Waluta
   {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
   || n[13]:=exec('tr','tlumaczenia','1000152');        k[13]:=6
   ?};
   {? odbrutto=1 & FAKS.NDVAT='N'
   ||
:: Wartosc brutto przed rabatem
      n[14]:=exec('tr','tlumaczenia','1000198');         k[14]:=14
   ||
:: Wartosc netto przed rabatem
      n[14]:=exec('tr','tlumaczenia','1000197');         k[14]:=13
   ?};
:: Kwota rabatu
   n[15]:=exec('tr','tlumaczenia','1000199');            k[15]:=8;
:: Data sprzzedaży pozycji
   n[16]:=exec('tr','tlumaczenia','1000112');            k[16]:=10;

:: wyliczenie szerokosci kolumn wg etykiet
   {! _i:=1..obj_len(n)
   |!
      {? var_pres('['+$_i+']',n)=type_of('stringi')
      ||
         _max:=0;
         STR.split(n[_i]);
         {! |? _word:=STR.get_word(); {? +_word>_max || _max:=+_word ?}; STR.next() !};
         k[_i]:=_max
      ?}
   !};
   k1[1]:=k[9];
   k1[2]:=k[11];
   k1[3]:=k[12];

   {! _i:=1..obj_len(k) |! w[_i]:=0 !};
   {! _i:=1..obj_len(s) |! s[_i]:=0 !};
   zero_na:="{! _i:=1..obj_len(na)|! na[_i]:='' !}";
   w[2]:=w[6]:='';

:: ilosc znakow w wierszu
   max_T8B:=max_T10B:=max_T8G:=max_T8:=0;
   licznik:=ilkop:=stron:=0;
   Text:=Text1:='';
:: ilosc pozycji
   poz:=0;

   ndvat:=FAKS.NDVAT;
   __oo:=0;
   vatzpoz:=FAKS.T().VATZPOZ='T';

:: spr. ile miejsc po przecinku drukowac
   FAP.index('FAP');
   FAP.prefix(FAKS.ref);
:: dokładność ilości
   ddokl:=exec('fld_prec','#field',FAP,'IL');
:: dokładność ceny przed i po rabacie
   _cdokl_cwal:=exec('fld_prec','#field',FAP,'CWAL');
   _cdokl_cpr:=exec('fld_prec','#field',FAP,'CPR');
   _cdokl_cb:=exec('fld_prec','#field',FAP,'CB');
   _cdokl_cn:=exec('fld_prec','#field',FAP,'CN');
   cprdokl:=0;
   cdokl:=0;
   {? walutowa=0 | walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='N')
   ||
      _spp:=__walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & FAKS.T().SPP='T';
      {? _spp=0 || cprdokl:=_cdokl_cpr ?};
      cdokl:={? odbrutto=1 || _cdokl_cb || _cdokl_cn ?}
   ?};
   {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
   ||
      {? cprdokl=0 | _cdokl_cwal>cprdokl || cprdokl:=_cdokl_cwal ?};
      FAP.cntx_psh();
      FAP.index('FAP');
      FAP.prefix(FAKS.ref());
      _loop:=FAP.first();
      {!
      |? _loop
      |!
         _cdokl:=
            {? FAKS.SZ='S'
            || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
            || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
            ?};
         _cdokl:=+(2-$
            fabs(
               frac(
                  {? exec('czyrabp','ceny',FAKS.RAB_TYP)
                  || _rabat:=1-exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)/100;
                     (FAP.CWAL*_rabat)$_cdokl
                  || FAP.CWAL-FAP.RABK-(FAKS.RAB*FAP.CWAL/100)$_cdokl
                  ?}
               )
            )
         );
         {? cdokl=0 | _cdokl>cdokl || cdokl:=_cdokl ?};
         _loop:=FAP.next()
      !};
      FAP.cntx_pop()
      ?};
:: dokładność rabatu
   _rozn:=cdokl-2;
   {? _rozn<0 || _rozn:=0 ?};
   k[6]+=_rozn;
   k[8]+=_rozn;
   rdokl:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || _rozn:=0; 2 || cdokl ?};
   k[7]+=_rozn;
:: Jeżeli parametr jest włączony to sugerujemy się tylko dokładnością na materiale
   {? exec('get','#params', 100235, 2)='T'
   || FAP.cntx_psh();
      FAP.index('FAP');
      FAP.prefix(FAKS.ref());
      {? FAP.first()
      || {!|?
            _cdokl:=
            {? FAKS.SZ='S'
            || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
            || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
            ?};
            {? _cdokl>cdokl || cdokl:=_cdokl ?};
            FAP.next()
         !}
      ?};
      FAP.cntx_pop();
      {? cdokl>cprdokl  || cprdokl:=cdokl ?};
      {? cdokl>rdokl    || rdokl:=cdokl   ?}
   ?};

   print1:="
      VAR_DEL.delete('PRN1');
      PRN1:=obj_new(@.CLASS.PRINT,obj_len(k),__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN1.framecol(color_frame_g);
      _w10:='{? form(w[10])*''%''|| gsub(form(w[10]),'' '','''')||form(w[10])?}';
      {? k[1] || PRN1.add($('w[1]'),               k[1], n[1],1) ?};
      {? k[2] || PRN1.add($('form(w[2])'),         k[2], n[2]) ?};
      {? k[3] || PRN1.add($('form(w[3])'),         k[3], n[3]) ?};
      {? k[4] || PRN1.add($('form(w[4],,ddokl)'),  k[4], n[4],1  ) ?};
      {? k[5] || PRN1.add($('form(w[5],,2)'),      k[5], n[5]) ?};
      {? k[6] || PRN1.add($('form(w[6],,cprdokl)'),k[6], n[6],1) ?};
      {? k[7] || PRN1.add($('form(w[7],,rdokl)'),  k[7], n[7],1) ?};
      {? k[8] || PRN1.add($('form(w[8],,cdokl)'),  k[8], n[8],1) ?};
      {? k[16] || PRN1.add($('form(w[16])'),       k[16], n[16]) ?};
      {? k[14] || PRN1.add($('form(w[14],,2)'),    k[14], n[14],1) ?};
      {? k[15] || PRN1.add($('form(w[15],,2)'),    k[15], n[15],1) ?};
      {? k[9] | k[12]
      || {? vatzpoz=1 | odbrutto=0
        ||      PRN1.add($('form(w[9],,2)'),      k[9], n[9],1)
         |? odbrutto=1
         ||      PRN1.add($('form(w[12],,2)'),     k[12],n[12],1)
         ?}
      ?};
      {? k[10] & ~('TO'*ndvat)
      ||         PRN1.add($_w10,                   k[10],n[10],1)
      ?};
      {? k[11] & ~('TO'*ndvat) & vatzpoz=1
      ||         PRN1.add($('form(w[11],,2)'),     k[11],n[11],1)
      ?};
      {? k[12] & ~('TO'*ndvat) & vatzpoz=1
      ||         PRN1.add($('form(w[12],,2)'),     k[12],n[12],1)
      ?};
      {? k[13] & walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      ||         PRN1.add($('form(w[13],,2)'),     k[13],n[13])
      ?}
   ";
   print2:="
::    WE:   _a - 0-uwzgledniac kolumne waluty, 1-wpp
::          _b - 0-podsumowanie pozycji, 1-tabelka vat
::          _c - 0-w tym/razem, 1-faktura walutowa rozliczona- rozliczenie kursu
      {? var_pres('_a')<>type_of(0) || _a:=0 ?};
      {? var_pres('_b')<>type_of(0) || _b:=0 ?};
      {? var_pres('_c')<>type_of(0) || _c:=0 ?};
      VAR_DEL.delete('PRN2');
      PRN2:=obj_new(@.CLASS.PRINT,7,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN2.framecol(color_frame_w);
      _white:='\\'%1\\''[color_bw];
      _w10:='{? form(w[10])*''%''|| gsub(form(w[10]),'' '','''')||form(w[10])?}';
      {? _b=0 & k[1]
      ||
         PRN2.add($('form(w[1])')
            ,_ret:=0; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13)|| k[_ii]+__odsram || 0 ?}) !}; _ret-__odsram
            ,n[1],1,,$_white)
      ?};
      {? ~('TO'*ndvat)
      ||
         {? _b=0
         ||
            {? vatzpoz
            ||
            {? k[9]  || PRN2.add($('form(w[9],,2)')   ,k[9] ,n[9] ,1) ?};
            {? k[10] || PRN2.add($('form(w[10])')     ,k[10],n[10],1) ?};
            {? k[11] || PRN2.add($('form(w[11],,2)')  ,k[11],n[11],1) ?};
            {? k[12] || PRN2.add($('form(w[12],,2)')  ,k[12],n[12],1) ?}
            ||
               {? odbrutto
               ||
                  {? k[12] || PRN2.add($('form(w[12],,2)')  ,k[12],n[12],1) ?};
                  {? k[10] || PRN2.add($_w10                ,k[10],n[10],1) ?}
               ||
                  {? k[9]  || PRN2.add($('form(w[9],,2)')   ,k[9] ,n[9] ,1) ?};
                  {? k[10] || PRN2.add($_w10                ,k[10],n[10],1) ?}
               ?}
            ?}
         |? _b=1
         ||
            {? _c=0
            ||
               {? k[6] & k[8]=0 || PRN2.add($('form(w[6])') , _wyn:=0;
               {! _ii:=5..8 |! _wyn+=({? k[_ii]<>0 || k[_ii]+__odsram || 0 ?}) !};_wyn-__odsram,,1,,$_white,$_white) ?};
               {? k[8]  || PRN2.add($('form(w[8])')         ,_wyn:=0;
               {! _ii:=5..8 |! _wyn+=({? k[_ii]<>0 || k[_ii]+__odsram || 0 ?}) !};_wyn-__odsram,,1,,$_white,$_white) ?}
            ?};
            {? k1[1] || PRN2.add($('form(w[9],,2)')   ,k1[1],n[9] ,1) ?};
            {? k[10] || PRN2.add($_w10                ,k[10],n[10],1) ?};
            {? k1[2] || PRN2.add($('form(w[11],,2)')  ,k1[2],n[11],1) ?};
            {? k1[3] || PRN2.add($('form(w[12],,2)')  ,k1[3],n[12],1) ?}
         ?}
      |? k[9]
      ||
         {? k[6]  || PRN2.add($('form(w[6])')      ,k[6],n[6] ,1,,$_white) ?};
         {? k[8]  || PRN2.add($('form(w[8])')      ,k[8],n[8] ,1,,$_white) ?};
         {? k[9] || PRN2.add($('form(w[9],,2)'),k[9],n[9],1) ?}
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & k[13] || PRN2.add($('form(w[13],,2)'),  k[13],n[13]) ?}
   ";
   print3:="
      VAR_DEL.delete('PRN3');
      PRN3:=obj_new(@.CLASS.PRINT,6,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN3.framecol(color_frame_g);
      {? k[1]
      || PRN3.add($('form(prz[1])'),
            _ret:=0; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13) || k[_ii]+__odsram || 0 ?}) !};_ret-__odsram,n[1])
      ?};
      {? ~('TO'*ndvat) & vatzpoz=0 & odbrutto=1
      ||
         {? k[12] || PRN3.add($('form(prz[5],,2)') ,k[12],n[12],1) ?};
         {? k[10] || PRN3.add($('form(prz[3])')    ,k[10],n[10],1) ?}
      |? ~('TO'*ndvat) & vatzpoz=0 & odbrutto=0
      ||
         {? k[9]  || PRN3.add($('form(prz[2],,2)') ,k[9] ,n[9] ,1) ?};
         {? k[10] || PRN3.add($('form(prz[3])')    ,k[10],n[10],1) ?}
      |? ~('TO'*ndvat)
      ||
         {? k[9]  || PRN3.add($('form(prz[2],,2)') ,k[9] ,n[9] ,1) ?};
         {? k[10] || PRN3.add($('form(prz[3])')    ,k[10],n[10],1) ?};
         {? k[11] || PRN3.add($('form(prz[4],,2)') ,k[11],n[11],1) ?};
         {? k[12] || PRN3.add($('form(prz[5],,2)') ,k[12],n[12],1) ?}
      ||
         {? k[9] || PRN3.add($('form(prz[2],,2)'),k[9],n[9],1) ?}
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & k[13] || PRN3.add($('form(prz[6],,2)'),  k[13],n[13]) ?}
   ";
   print4:="
      prn1:='PRN4';
      op_1:=op_2:='';
      VAR_DEL.delete('PRN4');
      PRN4:=obj_new(@.CLASS.PRINT,2,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      {? _a || PRN4.add($('form(op_1)'),_a,'') ?};
      {? _b || PRN4.add($('form(op_2)'),_b,'',1) ?}
   ";
::informacje dodatkowe
   VAR_DEL.delete('inf_head_beg','inf_val_beg'); inf_head_beg:=inf_val_beg:='';
:: zamowienia
   {? -PARAM_W.ZAM='t'
   ||
      {? var_pres('X_Xp')>0 || obj_del(X_Xp) ?};
      ___zam_kl:=exec('ask_zam_klienta','qjoko');
      {? FAKS.WHERE='L'
      ||
      {? ___zam_kl
      ||
        _qq:=$"select ZK_N.ZAM_KL SYM from FAPOW JOIN @ZK_N USING(FAPOW.ZK_N,ZK_N.REFERENCE) where FAPOW.FAKS=':_a' and FAPOW.ZK_N<>'' group by ZK_N.ZAM_KL order by 1"
      ||
        _qq:=$"select ZK_N.SYM SYM from FAPOW JOIN @ZK_N USING(FAPOW.ZK_N,ZK_N.REFERENCE) where FAPOW.FAKS=':_a' and FAPOW.ZK_N<>'' group by ZK_N.SYM order by 1"
      ?};
         X_Xp:=sql(_qq,$FAKS.ref())
      |? FAKS.SZ='S'
      ||
      {? ___zam_kl
      ||

      {? FAKS.TZP<>'' & FAKS.WHERE='P'
      ||
         _ref_zk_n:=exec('FindAndGet','#table','ZK_N',FAKS.TZP,,"ref",null);
         {? ZK_N.seek(_ref_zk_n)
         || X_Xp:=sql('select ZK_N.ZAM_KL SYM FROM @ZK_N WHERE ZK_N.REFERENCE LIKE \':_a\' AND ZK_N.SYM LIKE \':_b\' group by ZK_N.ZAM_KL order by 1',$_ref_zk_n,ZK_N.SYM)
         || X_Xp:=sql('select ZK_N.ZAM_KL SYM  from @ZK_RN join @ZK_N using (ZK_RN.N, ZK_N.REFERENCE)
            where ZK_RN.FAKS=\':_a\' group by ZK_N.ZAM_KL order by 1',$FAKS.ref)
         ?}
      || X_Xp:=sql('select ZK_N.ZAM_KL SYM  from @ZK_RN join @ZK_N using (ZK_RN.N, ZK_N.REFERENCE)
         where ZK_RN.FAKS=\':_a\' group by ZK_N.ZAM_KL order by 1',$FAKS.ref)
      ?}
     ||
        {? FAKS.TZP<>'' & FAKS.WHERE='P'
        || _ref_zk_n:=exec('FindAndGet','#table','ZK_N',FAKS.TZP,,"ref",null);
        {? ZK_N.seek(_ref_zk_n)
        || X_Xp:=sql('select ZK_N.SYM SYM FROM @ZK_N WHERE ZK_N.REFERENCE LIKE \':_a\' AND ZK_N.SYM LIKE \':_b\' group by ZK_N.SYM order by 1',$_ref_zk_n,ZK_N.SYM)
        || X_Xp:=sql('select ZK_N.SYM SYM  from @ZK_RN join @ZK_N using (ZK_RN.N, ZK_N.REFERENCE)
                     where ZK_RN.FAKS=\':_a\' group by ZK_N.SYM order by 1',$FAKS.ref)
        ?}
        || X_Xp:=sql('select ZK_N.SYM SYM  from @ZK_RN join @ZK_N using (ZK_RN.N, ZK_N.REFERENCE)
                     where ZK_RN.FAKS=\':_a\' group by ZK_N.SYM order by 1',$FAKS.ref)
        ?}
     ?}
      |? FAKS.SZ='Z'
      ||
      {? ___zam_kl
      ||
         X_Xp:=sql('select ZD_NAG.ZAM_DST SYM  from @ZD_RN join @ZD_NAG using (ZD_RN.ZD_NAG, ZD_NAG.REFERENCE)
                     where ZD_RN.FAKS=\':_a\' group by ZD_NAG.ZAM_DST order by 1',$FAKS.ref)
     ||
         X_Xp:=sql('select ZD_NAG.SYM SYM  from @ZD_RN join @ZD_NAG using (ZD_RN.ZD_NAG, ZD_NAG.REFERENCE)
                     where ZD_RN.FAKS=\':_a\' group by ZD_NAG.SYM order by 1',$FAKS.ref)
     ?}
      ?};
      {? X_Xp.first
      || inf_head_beg+=exec('tr','tlumaczenia','1000148')+';';
         _sep:=''; {! |? inf_val_beg+=_sep+X_Xp.SYM; _sep:=', ';X_Xp.next !};inf_val_beg+=';'
      ?}
   ?};

:: dokumenty magazynowe
   {? -PARAM_W.WZ='t'
   ||
      exec('dok_maga','powdok',0); __tt.prefix();
      {? __tt.first || _sep:=''; {! |? inf_val_beg+=_sep+'%1 %3 %2'@[__tt.SYM,$__tt.DATA,exec('tr','tlumaczenia','1000219')];_sep:=', ';__tt.next !};inf_val_beg+=';' ?};
      {? __tt.first || inf_head_beg+=exec('tr','tlumaczenia','1000149')+';'  ?}
   ?};

:: lista paragonów
   paragon:='';
   {? FAKS.SZ='S' & FAKS.T().PAR='N' & FAKS.T().FIS='T' & FAKS.SYMF<>''
   ||

      VAR_DEL.delete('X_Xp');
      X_Xp:=sql($"select FAKS.SYM SYM, FAKS.PAR PAR, FAKS.D DS from @FAKS where FAKS.SYMF=':_a'",$FAKS.ref());
      {? X_Xp.first
      || inf_head_beg+=((exec('tr','tlumaczenia','1000169')-1)+';');
         _ds:=' '+exec('tr','tlumaczenia','1000112')+' ';
         _sep:='';
         {!
         |? inf_val_beg+=_sep+{? X_Xp.PAR=''
                          || X_Xp.SYM
                          || X_Xp.PAR
                          ?}+_ds+$X_Xp.DS; _sep:=', ';
            X_Xp.next
         !};
         inf_val_beg+=';'
      ?};
      VAR_DEL.delete('X_Xp')
   ?};

:: przypisanie wartosci dla pozycji dokumentu
   pozycja:="
      _spp:=walutowa=0 & __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & FAKS.T().SPP='T';
      {? mpp
      ||
      w[1]:='';
      {? walutowa=0 | PARAM_W.WALH='H'
      ||
         _mpp:='';
         {? FAKS.SP_WYM='T' & FAKS.SZ='S' & FAKS.KOR=''
         ||
            _mpp:=' ';
            {? FAP.SP_WYM='T' || _mpp:='*' ?}
         ?};
         w[1]:=form(FAP.POZ,,0,'99')+_mpp
         ?}
      ||
          w[1]:={? walutowa=0 | PARAM_W.WALH='H' || form(FAP.POZ,,0,'99') || '' ?}
      ?};
      w[2]:='';
      _tlumacz:='';
      {? PARAM_W.JEZYK<>null
      || _language:=PARAM_W.JEZYK;
         {? PARAM_W.JEZYK().KOD='OBA'
         || _slu:=exec('FindInSet','#table','SLU','NAZ','Języki obce',,,1);
            _language:=exec('FindInSet','#table','SLO','SL','ANG',_slu,,1)
         ?};
         _tlumacz:=exec('trans_m','tlumaczenia',FAP.M,_language)
::         _tlumacz:=exec('trans_m','tlumaczenia',FAP.M,PARAM_W.JEZYK)
      ?};
      {? PARAM_W.INDEKS='T'
      ||
         {? +FAP.MX
         || w[2]+=FAP.MX+' '
         || w[2]+={? FAP.M<>null || FAP.M().KTM+' ' || '' ?}
         ?}
      ?};
::      {? PARAM_W.NAZWA='T' & (PARAM_W.JEZYK=null | _tlumacz='')
      {? PARAM_W.NAZWA='T'
      ||
         {? +FAP.NX
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+FAP.NX+' '
         |? FAP.M
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+{? FAP.M<>null || FAP.M().N+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.JEZYK<>null & _tlumacz<>''
      ||
         {? +FAP.MX
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_tlumacz+' '
         || w[2]+={? w[2]<>''||' - ' || '' ?}+{? FAP.M<>null || _tlumacz+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.UWAGI='T' & FAP.memo_lin('UWAGI')<>'\n' & (FAP.M<>null | +FAP.NX)
      || FAP.memo_get(,'UWAGI');
         w[2]+={? w[2]<>'' || ' - ' || '' ?}+gsub(FAP.memo_txt(0,1,'UWAGI'),'\n',' ')
      ?};
      {? PARAM_W.KODOBCY='T' & FAP.M<>null & FAKS.KH<>null
      || _kod_obcy:=exec('kod_obcy','kody_kresk',FAP.M,FAKS.KH);
         {? _kod_obcy<>''
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_kod_obcy
         ?}
      ?};
      {? PARAM_W.DK_C='T' & FAP.DK_C<>null
      || _dk_c:=form(exec('sym_dk_c','mat_atr',FAP.DK_C));
         {? _dk_c<>''
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_dk_c
         ?}
      ?};
      {? PARAM_W.KP='T' & FAP.KP<>''
      || _txt01:=exec('tr','tlumaczenia','1000230');
         _txt02:=FAP.KP;
         w[2]+={? w[2]<>''
               || ' - %1 %2'[_txt01,_txt02]
               || '%1 %2'[_txt01,_txt02]
               ?}
      ?};
      w[2]:={? walutowa=0 | PARAM_W.WALH='H' || w[2] || '' ?};

      w[3]:={? walutowa=0 | PARAM_W.WALH='H' || exec('pkwiu','material',FAKS.DW,FAP.M,1,FAP.PKWIU_CN) || '' ?};
      w[4]:={? walutowa=0 | PARAM_W.WALH='H' || FAP.IL || '' ?};
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_jm','tlumaczenia',FAP.JM,PARAM_W.JEZYK)
      ?};
      w[5]:={? walutowa=0 | PARAM_W.WALH='H' || {? PARAM_W.JEZYK<>null & _tlumacz<>'' || _tlumacz || FAP.JM().KOD ?} || '' ?};
      w[10]:=FAP.SV().KOD;
      w[13]:={? walutowa=1 & (PARAM_W.WALH='T'  | PARAM_W.WALH='H') || __walh || __walp ?};

      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      || _cdokl:={? FAKS.SZ='S'
                 || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
                 || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
                 ?};
         w[6]:=FAP.CWAL;
         w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP)
               || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
               || FAP.RABK+(FAKS.RAB*FAP.CWAL/100)$_cdokl
               ?};
         w[8]:=
            {? exec('czyrabp','ceny',FAKS.RAB_TYP)
            || _rabat:=1-exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)/100;
               (FAP.CWAL*_rabat)$_cdokl
            || FAP.CWAL-FAP.RABK-(FAKS.RAB*FAP.CWAL/100)$_cdokl
            ?};
         s[4]+=w[9]:=FAP.WWAL;
         s[5]+=w[11]:=FAP.VWAL;
         s[6]+=w[12]:=FAP.BWAL;
         sw[1]+=FAP.WWAL_P;sw[2]+=FAP.VWAL_P;sw[3]+=FAP.BWAL_P
      ||
         w[6]:={? _spp || '' || FAP.CPR ?};
         w[8]:={? odbrutto=1 || FAP.CB || FAP.CN ?};
         w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP) || FAP.CPR-w[8] ?};
         s[1]+=w[9]:=FAP.WWAL_P;
         s[2]+=w[11]:=FAP.VWAL_P;
         s[3]+=w[12]:=FAP.BWAL_P;
         sp[1]+=FAP.WWAL_P;sp[2]+=FAP.VWAL_P;sp[3]+=FAP.BWAL_P
      ?};
      w[14]:={? _spp || '' || (FAP.IL*w[6])$2 ?};
      w[15]:=w[14]-{? odbrutto=1 || w[12] || w[9] ?};
      w[16]:={? FAP.D<>date(0,0,0) & (walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='H')
             || gsub(form(FAP.D,,3),'/','-')
             || ''
             ?};
::    szacowanie ilosci lini pozycji
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+1
   ";

:: agregowanie wartosci do przeniesienia
   do_prze:="
      do_p[1]+=FAP.WWAL_P;
      do_p[2]+=FAP.VWAL_P;
      do_p[3]+=FAP.BWAL_P;
      do_p[4]+=FAP.WWAL;
      do_p[5]+=FAP.VWAL;
      do_p[6]+=FAP.BWAL
   ";
   do_przep:="
      do_p[1]+=FAP.WWAL_P;
      do_p[2]+=FAP.VWAL_P;
      do_p[3]+=FAP.BWAL_P;
      do_p[4]+=0;
      do_p[5]+=0;
      do_p[6]+=0";
   do_przeh:="
      do_p[1]+=0;
      do_p[2]+=0;
      do_p[3]+=0;
      do_p[4]+=FAP.WWAL;
      do_p[5]+=FAP.VWAL;
      do_p[6]+=FAP.BWAL";

   zero_na();
   kw_fak:=0;
   faks:=BB.refsql(FAKS.ref);
   FAKSV.clear;
   FAKSV.index('FF_SV');
   FAKSV.prefix('',faks);
   {? FAKS.SZ='S' & ~FAKSV.first
   ||
      exec('faksv_mk','faktury_vat',faks)
   ?}
?}


\begin_k1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: kluzula BEGIN kor_new.rpm
::   WE:  _a  - nazwa wydruku
::       [_b] - czy pokazywac panel dialogowy z parametrami ([1] -tak, 0 - nie) - gdy nie pokazuje panel nie zapisuje
::              rowniez ustawien wybranych
::  OLD: \begin_k1/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
_wydr_naz:=_a;
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};


exec('end_k1','faktury_wydruk_new');
__empty_char:={? exec('get','#params', 100236, 2)='N' || ^160 || ' ' ?};
headfoot:=0;

__wydrfa:=obj_new('uproszcz','samof');

:: do numeracji stron przy drukowaniu paczek faktur
pages:=0;

:: dla wydruku graficznego 1, dla iglowki 0
graf:=exec('get','#params',{? FAKS.SZ='S' || 2121 || 6121 ?},2,OPERATOR.USER)<>'T';
:: dla wydruku na skladance 1 (nie drukuje ORGINAL/KOPIA)
skladanka:=exec('get','#params',{? FAKS.SZ='S' || 2122 || 6122 ?},2,OPERATOR.USER)='T';
duplex:=0;
:: odstep miedzykolumnowy
__ods:=exec('get','#params',{? FAKS.SZ='S' || 2125 || 6125 ?},2,OPERATOR.USER)='T';
__odsram:={? __ods || 3 || 3 ?};
__odsskr:={? __ods || 2 || 2 ?};
:: czy walutowa
__walh:=FAKS.WAL().KOD;
__walp:=FAKS.NWAL().KOD;
__waln:=INFO.NAROD().KOD;
wal_h:=1;
walutowa:=__walh<>__walp;
:: czy kwota vat liczona od ceny burtto
odbrutto:=FAKS.T().FIS='T';
:: pozycje korygowne
p_druk:='';
:: uproszczony wydruk faktury
__wydrfa.uproszcz:=0;
:: czy samofakturowanie
__wydrfa.samof:=0;
::[PD]sprawdzenie dla starszych werscji
VAR_DEL.delete('mpp');
mpp:=0;
mpp:={? _wydr_naz<>'korzb_poz' & exec('sp_active','faktury_wspolne') || 1 || 0 ?};

:: korekta zbiorcza
loop:=obj_new('loop','faks_ref','kz');
loop.loop:=1;
loop.faks_ref:=FAKS.ref();
loop.kz:=exec('FindInSet','#table','FAKS_KZF','POZ',FAKS.ref())<>null();

color:=prn1:=prn2:='';
lm:=ll:=vp:=lmt:=lmt_org:=rsep:=0;
wyjscie:=0;
exec('color_define','faktury_wydruk_new');

{? ~loop.kz
||
   FAP.cntx_psh();
   FAP.index('FAP'); FAP.prefix(FAKS.ref());
   {? ~FAP.first() || FUN.info('Brak pozycji faktury.'@); wyjscie:=1 ?};
   FAP.cntx_pop()
?};

PARAM_W.KOPIA:=2;
PARAM_W.ZAM:='N';
PARAM_W.WZ:='N';
PARAM_W.POZY:='W';
PARAM_W.DR_TERM:='N';
{? _b | exec('czy_grp_dr','faktury_wydruk')=0
|| PARAM_W.DR_DUPL:='N';
   PARAM_W.DUPLIKAT:=date(0,0,0)
?};
_par:='IN';
PARAM_W.SLOWNIE:='T';
PARAM_W.JEZYK:=null;
PARAM_W.DR_RAB:='T';
PAR_WYDR.SHAD:=3;

_edokument:=_a='korsenew' | _a='korze_poz';

_wydrukil:=exec('wydrukil_prefix','faktury_wydruk_new',_a,_b,_edokument);
_b:=_wydrukil.SHOW_PARAM;
wyjscie:=_wydrukil.EXIT; {? wyjscie || return() ?};
{? ~WYDRUKIL.first()
||
   WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add()
||
   PARAM_W.KOPIA:=WYDRUKIL.TYP_ZEST;
   PARAM_W.POZY:=WYDRUKIL.STR1;
   PARAM_W.DR_TERM:=WYDRUKIL.STR2;
:   PARAM_W.DR_DUPL:=WYDRUKIL.STR3;
   PARAM_W.ZAM:={? WYDRUKIL.CHKBOX04 || 'T' || 'N' ?};
   PARAM_W.WZ:={? WYDRUKIL.CHKBOX05 || 'T' || 'N' ?};
   {? _b | exec('czy_grp_dr','faktury_wydruk')=0
   ||
      PARAM_W.DUPLIKAT:=WYDRUKIL.OD_DATY
   ?};
   _par:=WYDRUKIL.FAKS_OD;
   PARAM_W.SLOWNIE:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?};
   PARAM_W.JEZYK:=WYDRUKIL.JEZYK;
   PARAM_W.PODNALDR:={? WYDRUKIL.CHKBOX02 || 'T' || 'N' ?};
   PARAM_W.DR_RAB:={? WYDRUKIL.CHKBOX03 | FAKS.SPOBLRAB='W' || 'T' || 'N' ?}
?};
_jezyk_:=PARAM_W.JEZYK;
_djezyk:=exec('kh_jezyk','tlumaczenia',FAKS.KH,_edokument);
{? _b | _edokument
|| PARAM_W.JEZYK:={? _djezyk || _djezyk || _jezyk_ ?};
   PARAM_W.JEZYK_KH:={? _djezyk & _djezyk=PARAM_W.JEZYK || 'T' || 'N' ?}
|? exec('czy_grp_dr','faktury_wydruk') & ~_edokument
|| PARAM_W.JEZYK:={? __GRP_DR.JEZYK_KH='T' & _djezyk || _djezyk || __GRP_DR.JEZYK ?}
?};

PARAM_W.INDEKS:='N';
PARAM_W.NAZWA:='N';
PARAM_W.MC:='N';
PARAM_W.UWAGI:='N';
PARAM_W.KODOBCY:='N';
PARAM_W.DK_C:='N';
{? _par*'I' || PARAM_W.INDEKS:='T' ?};
{? _par*'N' || PARAM_W.NAZWA:='T' ?};
{? _par*'M' || PARAM_W.MC:='T' ?};
{? _par*'U' || PARAM_W.UWAGI:='T' ?};
{? _par*'K' || PARAM_W.KODOBCY:='T' ?};
{? _par*'C' || PARAM_W.DK_C:='T' ?};
{? _par*'P' || PARAM_W.KP:='T' ?};

{? _b
|| PARAM_W.WALH:={? __walh<>__walp || 'T' || 'N' ?}
|| PARAM_W.WALH:={? __walh<>__walp ||  {? var_press('__pwwalh')=type_of('T') || __pwwalh || 'T' ?} || 'N' ?}
?};

PARAM_W.win_edit('WYDR_KOR');
{? wyjscie=0
||
   _chk_par:="
      _wyn:='';
      {? PARAM_W.KOPIA<0
      || FUN.info('Ilość kopii nie może być ujemna.'@);
         _wyn:='KOPIA'
      ?};
      {? _wyn='' & PARAM_W.DR_DUPL='T'
      || {? PARAM_W.DUPLIKAT=date(0,0,0)
         || FUN.info('Niewypełniona data wystawienia duplikatu.'@);
            _wyn:='DUPLIKAT'
         ?};
         {? _wyn=''
         || {? PARAM_W.DUPLIKAT<FAKS.DW
            || FUN.info('Data duplikatu wcześniejsza od daty wystawienia dokumentu.'@);
               _wyn:='DUPLIKAT'
            ?}
         ?}
      ?};
      {? _wyn=''
      || {? PARAM_W.WALH='H' & __walh=__walp
         || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\nWydruk w walucie handlowej niedostępny.'@);
            _wyn:='WALH'
         |? PARAM_W.WALH='T' & __walh=__walp
         || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\nWydruk w obu walutach niedostępny.'@);
            _wyn:='WALH'
         ?}
      ?};
      _wyn
   ";
   {? {? _b=1 & exec('noparam','param_wydr',FAKS.SZ)=0
      || exec('set_efld_opt_paramw','faktury_wydruk',FAKS.SZ='S' & FAKS.SPOBLRAB='C');
         {? PARAM_W.edit(_chk_par)
         || 1
         || {? exec('czy_grp_dr','faktury_wydruk') || __GRP_DR.ESC:=1 ?}; 0
         ?}
      || 1
      ?}
   ||
      {? _b
      || WYDRUKIL.index('WYDRUKI');
         WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
         {? WYDRUKIL.first()
         ||
            WYDRUKIL.TYP_ZEST:=PARAM_W.KOPIA;
            WYDRUKIL.STR1:=PARAM_W.POZY;
            WYDRUKIL.STR2:=PARAM_W.DR_TERM;
            WYDRUKIL.STR3:=PARAM_W.DR_DUPL;
            WYDRUKIL.CHKBOX04:=PARAM_W.ZAM='T';
            WYDRUKIL.CHKBOX05:=PARAM_W.WZ='T';
            WYDRUKIL.OD_DATY:=PARAM_W.DUPLIKAT;
            WYDRUKIL.FAKS_OD:='';
            {? PARAM_W.INDEKS='T'  || WYDRUKIL.FAKS_OD+='I' ?};
            {? PARAM_W.NAZWA='T'   || WYDRUKIL.FAKS_OD+='N' ?};
            {? PARAM_W.MC='T'      || WYDRUKIL.FAKS_OD+='M' ?};
            {? PARAM_W.UWAGI='T'   || WYDRUKIL.FAKS_OD+='U' ?};
            {? PARAM_W.KODOBCY='T' || WYDRUKIL.FAKS_OD+='K' ?};
            {? PARAM_W.DK_C='T'    || WYDRUKIL.FAKS_OD+='C' ?};
            {? PARAM_W.KP='T'      || WYDRUKIL.FAKS_OD+='P' ?};
            WYDRUKIL.CHKBOX01:=PARAM_W.SLOWNIE='T';
            WYDRUKIL.JEZYK:={? _djezyk & PARAM_W.JEZYK=_djezyk || _jezyk_ || PARAM_W.JEZYK ?};
            {? exec('czy_grp_dr','faktury_wydruk')
            || __GRP_DR.JEZYK:=PARAM_W.JEZYK;
               __GRP_DR.JEZYK_KH:=PARAM_W.JEZYK_KH
            ?};
            {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
            WYDRUKIL.CHKBOX03:=PARAM_W.DR_RAB='T';
            WYDRUKIL.put();
            __pwwalh:=PARAM_W.WALH
         ?}
      ?};
      {? ~loop.kz
      ||
         FAP.cntx_psh();
         FAP.index('FAP');
         FAP.prefix(FAKS.ref());
         {? FAP.first() || {! |? {? exec('spr_kor','faktury_poz') || p_druk+='/'+$FAP.POZ+'/' ?}; FAP.next() !} ?};
         FAP.cntx_pop();
         {? -PARAM_W.POZY='k' & p_druk=''
         || FUN.info('Brak korygowanych pozycji.'@);
            wyjscie:=1
         ?}
      ?}
   ||
      wyjscie:=1
   ?}
?};

{? wyjscie=0
||
:: szerokosc kolumn
   k:=obj_new(16);
:: szerokosc kolumn dla podsumowania VAT dla dokumentow gdzie VAT liczony od sumy w stawkach
   VAR_DEL.delete('k1');   k1:=obj_new(3);
:: dane drukowane
   w:=obj_new(obj_len(k));
:: nazwy kolumn
   n:=obj_new(obj_len(k));
:: sumy
   s:=obj_new(6);
   VAR_DEL.delete('sp');   sp:=obj_new(13);
:: do przeniesienia
   do_p:=obj_new(6);
:: dane naglowkowe
   na:=obj_new(10);
:: przeniesienie
   prz:=obj_new(6); {! _i..6 |! prz[_i]:=0 !};
:: szerokosci kolumn nagłówkowych
   szer:=obj_new(4);
:: szerokosci pozycji faktur
   fak:=obj_new(obj_len(k));
:: w tym VAT
   vat:=obj_new(3);

   PRN1:=obj_new(@.CLASS.PRINT,obj_len(k),__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   PRN2:=obj_new(@.CLASS.PRINT,5,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   PRN3:=obj_new(@.CLASS.PRINT,5,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   PRN4:=obj_new(@.CLASS.PRINT,2,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   PRN5:=obj_new(@.CLASS.PRINT,4,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');

   ndvat:=FAKS.NDVAT;
   __oo:=0;
   vatzpoz:=FAKS.T().VATZPOZ='T';

:: opisy w obiektach PRN4 i PRN5
   op_1:=op_2:=op_3:=op_4:='';
:: Poz
   n[1]:=exec('tr','tlumaczenia','6000201');           k[1]:=4+{? mpp || (FAKS.SP_WYM='T') ?};
:: Material / usluga
   n[2]:=exec('tr','tlumaczenia','1000181');           k[2]:={? ~('TO'*ndvat) || 40 || 60 ?};
:: CN / PKWiU
   n[3]:=
      {? exec('get','#params',300208,2)='T'
      ||
         exec('tr','tlumaczenia','1000218')+'/'
      ||
         ''
      ?}+
      exec('tr','tlumaczenia','1000182');              k[3]:=10;
:: Ilosc
   n[4]:=exec('tr','tlumaczenia','1000183');           k[4]:=6;
:: jm
   n[5]:=exec('tr','tlumaczenia','1000184');           k[5]:=4;
   {? FAKS.T().FIS='T'
:: Cena brutto przed rab.
   || n[6]:=
         {? exec('czy_rab','faktury_wspolne','FAKS')
         || exec('tr','tlumaczenia','1000185')
         || exec('tr','tlumaczenia','1000193')
         ?};                                          k[6]:=11
:: Cena netto przed rab.
   || n[6]:=
         {? exec('czy_rab','faktury_wspolne','FAKS')
         || exec('tr','tlumaczenia','1000186')
         || exec('tr','tlumaczenia','1000188')
         ?};                                          k[6]:=10
   ?};
:: % rab.
   {? exec('czyrabp','ceny',FAKS.RAB_TYP)
   || n[7]:=exec('tr','tlumaczenia','1000187');          k[7]:=5
   || n[7]:=exec('tr','tlumaczenia','1000199');          k[7]:=6
   ?};
   {? odbrutto=1 & FAKS.NDVAT='N'
:: Cena brutto
   ||
      n[8]:=exec('tr','tlumaczenia','1000193');          k[8]:=10
   ||
:: Cena netto
      n[8]:=exec('tr','tlumaczenia','1000188');          k[8]:=10
   ?};
:: Wartosc netto
   n[9]:=exec('tr','tlumaczenia','1000189');           k[9]:=10;  k1[1]:=10;
:: Stawka VAT
   n[10]:=exec('tr','tlumaczenia','1000190');           k[10]:=6;
:: Kwota VAT
   n[11]:=exec('tr','tlumaczenia','1000191');           k[11]:=10;  k1[2]:=10;
:: Wartosc brutto
   n[12]:=exec('tr','tlumaczenia','1000192');           k[12]:=10;  k1[3]:=10;
:: Waluta
   {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
   || n[13]:=exec('tr','tlumaczenia','1000152');        k[13]:=6
   ?};
   {? odbrutto=1 & FAKS.NDVAT='N'
   ||
:: Wartosc brutto przed rabatem
      n[14]:=exec('tr','tlumaczenia','1000198');         k[14]:=14
   ||
:: Wartosc netto przed rabatem
      n[14]:=exec('tr','tlumaczenia','1000197');         k[14]:=13
   ?};
:: Kwota rabatu
   n[15]:=exec('tr','tlumaczenia','1000199');            k[15]:=8;
:: Data sprzzedaży pozycji
   n[16]:=exec('tr','tlumaczenia','1000112');            k[16]:=10;

:: wyliczenie szerokosci kolumn wg etykiet
   {! _i:=1..obj_len(n)
   |!
      {? var_pres('['+$_i+']',n)=type_of('stringi')
      ||
         _max:=0;
         STR.split(n[_i]);
         {! |? _word:=STR.get_word(); {? +_word>_max || _max:=+_word ?}; STR.next() !};
         k[_i]:=_max
      ?}
   !};
   k1[1]:=k[9];
   k1[2]:=k[11];
   k1[3]:=k[12];

   {! _i:=1..obj_len(k) |! w[_i]:=0 !};
   {! _i:=1..obj_len(s) |! s[_i]:=0 !};
   zero_na:="{! _i:=1..obj_len(na)|! na[_i]:='' !}";
   w[2]:=w[6]:='';

:: ilosc znakow w wierszu
   max_T8B:=max_T10B:=max_T8G:=max_T8:=0;
   licznik:=ilkop:=stron:=0;
   Text:=Text1:='';
:: ilosc pozycji
   poz:=0;

:: spr. ile miejsc po przecinku drukowac

   ddokl:=0;
   ddokl_c:=2;
   rdokl_c:=2;
   _dokl_c:="
      _dokl:=+form((frac(FKOR.CPR)))-2;
      {? ddokl_c<_dokl || ddokl_c:=_dokl ?};
      _dokl:=+form((frac(FKOR.BCPR)))-2;
      {? ddokl_c<_dokl || ddokl_c:=_dokl ?};
      _cdokl:=
         {? FAKS.SZ='S'
         || {? FKOR.M<>null() || FKOR.M().DOKL_S || ST.DOKL_S ?}
         || {? FKOR.M<>null() || FKOR.M().DOKL_Z || ST.DOKL_Z ?}
         ?};
      {? _cdokl>ddokl_c || ddokl_c:=_cdokl ?}
   ";
   _dokl_cw:="
      _dokl:=+form((frac(FKOR.CWAL)))-2;
      {? ddokl_c<_dokl || ddokl_c:=_dokl ?};
      _dokl:=+form((frac(FKOR.BCWAL)))-2;
      {? ddokl_c<_dokl || ddokl_c:=_dokl ?};
      _cdokl:=
         {? FAKS.SZ='S'
         || {? FKOR.M<>null() || FKOR.M().DOKL_S || ST.DOKL_S ?}
         || {? FKOR.M<>null() || FKOR.M().DOKL_Z || ST.DOKL_Z ?}
         ?};
      {? _cdokl>ddokl_c || ddokl_c:=_cdokl ?}
   ";
   FAP.index('FAP');
   FAP.prefix(FAKS.ref);
   {? FAP.first
   ||
      {!
      |?
::       dokładność ilości
         exec('wysw_kor','faktury_poz');
         _dokl:=+form((frac(FKOR.IL)))-2;
         {? ddokl<_dokl || ddokl:=_dokl ?};
         _dokl:=+form((frac(FKOR.BIL)))-2;
         {? ddokl<_dokl || ddokl:=_dokl ?};
::       dokładność ceny
         {? ~walutowa
         || _dokl_c()
         || {? PARAM_W.WALH='T'
            || _dokl_c(); _dokl_cw()
            |? PARAM_W.WALH='H'
            || _dokl_cw()
            || _dokl_c()
            ?}
         ?};

         FAP.next
      !}
   ?};
   {? exec('czy_rab','faktury_wspolne','FAKS') || rdokl_c:=ddokl_c ?};

   print1:="
      VAR_DEL.delete('PRN1');
      PRN1:=obj_new(@.CLASS.PRINT,obj_len(k),__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN1.framecol(color_frame_g);
      _w10:='{? form(w[10])*''%''|| gsub(form(w[10]),'' '','''')||form(w[10])?}';
      {? k[1] || PRN1.add($('form(w[1])'),         k[1], n[1],1) ?};
      {? k[2] || PRN1.add($('form(w[2])'),         k[2], n[2]) ?};
      {? k[3] || PRN1.add($('form(w[3])'),         k[3], n[3]) ?};
      {? k[4] || PRN1.add($('form(w[4],,ddokl)'),  k[4], n[4],1) ?};
      {? k[5] || PRN1.add($('form(w[5],,2)'),      k[5], n[5]) ?};
      {? k[6] || PRN1.add($('form(w[6],,ddokl_c)'),k[6], n[6],1) ?};
      {? k[7] || PRN1.add($('form(w[7],,rdokl_c)'),k[7], n[7],1) ?};
      {? k[8] || PRN1.add($('form(w[8],,ddokl_c)'),k[8], n[8],1) ?};
      {? k[16] || PRN1.add($('form(w[16])'),       k[16], n[16]) ?};
      {? k[14] || PRN1.add($('form(w[14],,2)'),    k[14], n[14],1) ?};
      {? k[15] || PRN1.add($('form(w[15],,2)'),    k[15], n[15],1) ?};
      {? k[9] | k[12]
      || {? vatzpoz=1 | odbrutto=0
         ||      PRN1.add($('form(w[9],,2)'),      k[9], n[9],1)
         |? odbrutto=1
         ||      PRN1.add($('form(w[12],,2)'),     k[12],n[12],1)
         ?}
      ?};
      {? k[10] & ~('TO'*ndvat)
      ||         PRN1.add($_w10,                   k[10],n[10],1)
      ?};
      {? k[11] & ~('TO'*ndvat) & vatzpoz=1
      ||         PRN1.add($('form(w[11],,2)'),     k[11],n[11],1)
      ?};
      {? k[12] & ~('TO'*ndvat) & vatzpoz=1
      ||         PRN1.add($('form(w[12],,2)'),     k[12],n[12],1)
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      ||         PRN1.add($('form(w[13],,2)'),     k[13],n[13])
      ?}
   ";
   print2:="
      {? var_pres('_a')<>type_of(0) || _a:=0 ?};
      {? var_pres('_b')<>type_of(0) || _b:=0 ?};
      VAR_DEL.delete('PRN2');
      PRN2:=obj_new(@.CLASS.PRINT,6,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN2.framecol(color_frame_w);
      _white:='\\'%1\\''[color_bw];
      _w10:='{? form(w[10])*''%''|| gsub(form(w[10]),'' '','''')||form(w[10])?}';
      {? k[1]
      || PRN2.add($('form(w[1])'),
            _ret:=0; {!_ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13)|| k[_ii]+__odsram || 0 ?})!};_ret-__odsram,n[1],1,,$_white)
      ?};
      {? ~('TO'*ndvat)
      ||
         {? _b=0
         ||
            {? vatzpoz
            ||
                  PRN2.add($('form(w[9],,2)')   ,k1[1],n[9] ,1);
                  PRN2.add($_w10                ,k[10],n[10],1);
                  PRN2.add($('form(w[11],,2)')  ,k1[2],n[11],1);
                  PRN2.add($('form(w[12],,2)')  ,k1[3],n[12],1)
            ||
               {? odbrutto
               ||
                  PRN2.add($('form(w[12],,2)')  ,k[12],n[12], 1  );
                  PRN2.add($_w10                ,k[10],n[10], 1  )
               ||
                  PRN2.add($('form(w[9],,2)')   ,k[9] ,n[9] , 1  );
                  PRN2.add($_w10                ,k[10],n[10], 1  )
               ?}
            ?}
         |? _b=1
         ||
            {? k[6] & k[8]=0 || PRN2.add($('form(w[6])') , _wyn:=0;{! _ii:=5..8 |! _wyn+=({? k[_ii]<>0 || k[_ii]+__odsram || 0 ?}) !};_wyn-__odsram,,1,,$_white,$_white) ?};
            {? k[8]  || PRN2.add($('form(w[8])')         ,_wyn:=0;{! _ii:=5..8 |! _wyn+=({? k[_ii]<>0 || k[_ii]+__odsram || 0 ?}) !};_wyn-__odsram,,1,,$_white,$_white) ?};
            PRN2.add($('form(w[9],,2)')                  ,k1[1],n[9],1);
            PRN2.add($_w10                               ,k[10],n[10], 1  );
            PRN2.add($('form(w[11],,2)')                 ,k1[2],n[11], 1  );
            PRN2.add($('form(w[12],,2)')                 ,k1[3],n[12], 1  )
         ?}
      ||
         PRN2.add($('form(w[9],,2)'),  k[9] ,n[9] , 1  )
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & k[13] || PRN2.add($('form(w[13],,2)'),  k[13],n[13]) ?}
   ";
   print3:="
      VAR_DEL.delete('PRN3');
      PRN3:=obj_new(@.CLASS.PRINT,6,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN3.framecol(color_frame_g);
      {? k[1]
      || PRN3.add($('form(prz[1])'),
            _ret:=0; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13) || k[_ii]+__odsram || 0 ?}) !};_ret-__odsram,n[1])
      ?};
      {? ~('TO'*ndvat) & vatzpoz=0 & odbrutto=1
      ||
         PRN3.add($('form(prz[5],,2)'), k[12],n[12] , 1  );
         PRN3.add($('form(prz[3])'),    k[10],n[10] , 1  )
      |? ~('TO'*ndvat) & vatzpoz=0 & odbrutto=0
      ||
         PRN3.add($('form(prz[2],,2)'),  k[9] ,n[9] , 1  );
         PRN3.add($('form(prz[3])'),     k[10],n[10], 1  )
      |? ~('TO'*ndvat)
      ||
         PRN3.add($('form(prz[2],,2)'),  k1[1],n[9] , 1  );
         PRN3.add($('form(prz[3])'),     k[10],n[10], 1  );
         PRN3.add($('form(prz[4],,2)'),     k1[2],n[11], 1  );
         PRN3.add($('form(prz[5],,2)'),  k1[3],n[12], 1  )
      ||
         PRN3.add($('form(prz[2],,2)'),  k[9] ,n[9] , 1  )
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || PRN3.add($('form(prz[6],,2)'),  k[13],n[13]) ?}
   ";
   print4:="
      prn1:='PRN4';
      op_1:=op_2:='';
      VAR_DEL.delete('PRN4');
      PRN4:=obj_new(@.CLASS.PRINT,2,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      {? _a || PRN4.add($('form(op_1)'),_a,'') ?};
      {? _b || PRN4.add($('form(op_2)'),_b,'',1) ?}
   ";
   print6:="
      VAR_DEL.delete('PRN6');
      PRN6:=obj_new(@.CLASS.PRINT,2,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN6.framecol(color_frame_w);
      _blank:=\"
      \'\'
      \";
      _value:=\"
      exec('tr','tlumaczenia','2000215')
      \";
      _grey:=\"
      color_wg
      \";
      PRN6.add(_blank,_ret:=0; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<8 | _ii>16) || k[_ii]+__odsram || 0 ?}) !};_ret-__odsram);
      PRN6.add(_value,_ret:=0; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii>=8 & _ii<=16) || k[_ii]+__odsram || 0 ?}) !};_ret-__odsram,,,,_grey,,,,1)
   ";
::informacje dodatkowe
   VAR_DEL.delete('inf_head_beg','inf_val_beg'); inf_head_beg:=inf_val_beg:='';
:: zamowienia
   zam:='';
   {? -PARAM_W.ZAM='t'
   ||
::      FAKS.cntx_psh();
::      FAKS.index('FAK_SYM1'); FAKS.prefix(FAKS.KOR);
::      _ref_dok_spr:={? FAKS.first() || FAKS.ref() || null ?};
::      FAKS.cntx_pop();
      _ref_dok_spr:=$(FAKS.FKSQL);

      {? var_pres('X_Xp')>0 || obj_del(X_Xp) ?};
      ___zam_kl:=exec('ask_zam_klienta','qjoko');
      {? FAKS.WHERE='L'
      ||
      {? ___zam_kl
      ||
        _qq:=$"select FAPOW.ZK_N_SYM SYM from FAPOW where FAPOW.FAKS=':_a' and FAPOW.ZK_N<>'' group by FAPOW.ZK_N_SYM order by 1"
      ||
        _qq:=$"select FAPOW.ZK_N SYM from FAPOW where FAPOW.FAKS=':_a' and FAPOW.ZK_N<>'' group by FAPOW.ZK_N order by 1"
      ?};
::         X_Xp:=sql(_qq,$FAKS.ref())
         X_Xp:=sql(_qq,$_ref_dok_spr)
    |? FAKS.SZ='S' & $_ref_dok_spr<>''
    ||
      {? ___zam_kl
      ||
      X_Xp:=sql('select ZK_N.ZAM_KL SYM  from @ZK_RN join @ZK_N using (ZK_RN.N, ZK_N.REFERENCE)
                where ZK_RN.FAKS=\':_a\' group by ZK_N.ZAM_KL order by 1',$_ref_dok_spr)
::    where ZK_RN.FAKS=\':_a\' group by ZK_N.ZAM_KL order by 1',$FAKS.ref)
      ||
         X_Xp:=sql('select ZK_N.SYM SYM  from @ZK_RN join @ZK_N using (ZK_RN.N, ZK_N.REFERENCE)
                      where ZK_RN.FAKS=\':_a\' group by ZK_N.SYM order by 1',$_ref_dok_spr)
::                     where ZK_RN.FAKS=\':_a\' group by ZK_N.SYM order by 1',$FAKS.ref)
     ?}
     |? FAKS.SZ='Z' & $_ref_dok_spr<>''
     ||
      {? ___zam_kl
      ||
         X_Xp:=sql('select ZD_NAG.ZAM_DST SYM  from @ZD_RN join @ZD_NAG using (ZD_RN.ZD_NAG, ZD_NAG.REFERENCE)
               where ZD_RN.FAKS=\':_a\' group by ZD_NAG.SYM_DST order by 1',$_ref_dok_spr)
::                  where ZD_RN.FAKS=\':_a\' group by ZD_NAG.ZAM_DST order by 1',$FAKS.ref)
      ||
         X_Xp:=sql('select ZD_NAG.SYM SYM  from @ZD_RN join @ZD_NAG using (ZD_RN.ZD_NAG, ZD_NAG.REFERENCE)
                     where ZD_RN.FAKS=\':_a\' group by ZD_NAG.SYM order by 1',$_ref_dok_spr)
::                     where ZD_RN.FAKS=\':_a\' group by ZD_NAG.SYM order by 1',$FAKS.ref)
     ?}
      ?};
      {? $_ref_dok_spr<>'' & X_Xp.first
      || inf_head_beg+=exec('tr','tlumaczenia','1000148')+';';
         _sep:=''; {! |? inf_val_beg+=_sep+X_Xp.SYM; _sep:=', ';X_Xp.next !};inf_val_beg+=';'
      ?}
   ?};

:: dokumenty magazynowe
   mag:='';
   {? -PARAM_W.WZ='t'
   ||
      exec('dok_maga','powdok',0); __tt.prefix();
      {? __tt.first || _sep:=''; {! |? inf_val_beg+=_sep+'%1 %3 %2'@[__tt.SYM,$__tt.DATA,exec('tr','tlumaczenia','1000219')];_sep:=', ';__tt.next !};inf_val_beg+=';' ?};
      {? __tt.first || inf_head_beg+=exec('tr','tlumaczenia','1000149')+';'  ?}
   ?};
:: pozycje przed korekta
   pozycjaa:="
      {? var_pres('_a')<>type_of(null()) || _a:=FAP.ref() ?};

      FAKS.cntx_psh();
      FAP.cntx_psh();

      {? ref_name(FAP.FAKS)<>FAKS.name() || FAKS.use(ref_name(FAP.FAKS)) ?};
      FAP.FAKS().SYM;
      exec('wysw_kor','faktury_poz');

      {? ($_a)<>($FAP.ref())
      ||
         FAP.use('fakpo'+(5-form(8+FAKS.LKSQL)));
         FAKS.use(form(8+FAKS.LKSQL));
         FAP.prefix();
         FAP.seek(_a);
         FAP.FAKS().SYM
      ?};

      xktm.clear; xktm.find_key(FAP.POZ);
      _spp:=walutowa=0 & __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & FAKS.T().SPP='T';

      w[1]:='';
      {? mpp
      ||
         {? walutowa=0 | PARAM_W.WALH='H'
         ||
            _mpp:='';
            {? FAKS.SP_WYM='T' & FAKS.SZ='S'
            ||
               _mpp:=' ';
               {? FAP.SP_WYM='T' || _mpp:='*' ?}
            ?};
            w[1]:=form(FAP.POZ,,0,'99')+_mpp
         ?}
      ||
         w[1]:={? walutowa=0 | PARAM_W.WALH='H' || form(FAP.POZ,,0,'99') || '' ?}
      ?};
      w[2]:='';

      {? PARAM_W.JEZYK<>null
      || _language:=PARAM_W.JEZYK;
         {? PARAM_W.JEZYK().KOD='OBA'
         || _slu:=exec('FindInSet','#table','SLU','NAZ','Języki obce',,,1);
            _language:=exec('FindInSet','#table','SLO','SL','ANG',_slu,,1)
         ?};
         _tlumacz:=exec('trans_m','tlumaczenia',exec('FindInSet','#table','M','MATKTM',xktm.KTM,xktm.KTM),_language)
::      || _tlumacz:=exec('trans_m','tlumaczenia',exec('FindInSet','#table','M','MATKTM',xktm.KTM,xktm.KTM),PARAM_W.JEZYK)
      ?};

      {? PARAM_W.INDEKS='T'
      ||
         {? +xktm.KTX
         || w[2]+=xktm.KTX+' '
         || w[2]+={? xktm.KTM<>'' || xktm.KTM+' ' || xktm.UWA+' ' ?}
         ?}
      ?};
::      {? PARAM_W.NAZWA='T' & (PARAM_W.JEZYK=null | _tlumacz='')
      {? PARAM_W.NAZWA='T'
      ||
         {? +xktm.NAX
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+xktm.NAX+' '
         |? +xktm.NAZ
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+{? xktm.NAZ<>'' || xktm.NAZ+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.JEZYK<>null & _tlumacz<>''
      ||
         {? +xktm.KTX
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_tlumacz+' '
         || w[2]+={? w[2]<>''||' - ' || '' ?}+{? FAP.M<>null || _tlumacz+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.UWAGI='T' & FAP.memo_lin('UWAGI')<>'\n' & (FAP.M<>null | +FAP.NX)
      || FAP.memo_get(,'UWAGI');
         w[2]+={? w[2]<>'' || ' - ' || '' ?}+gsub(FAP.memo_txt(0,1,'UWAGI'),'\n',' ')
      ?};
      {? PARAM_W.KODOBCY='T' & xktm.KTM<>''  & FAKS.KH<>null
      ||  _kod_obcy:=exec('kod_obcy','kody_kresk',exec('FindInSet','#table','M','MATKTM',xktm.KTM,xktm.KTM),FAKS.KH);
         {? _kod_obcy<>''
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+_kod_obcy
         ?}
      ?};
      {? PARAM_W.DK_C='T' & xktm.DKC<>''
      || _dk_c:=xktm.DKC;
         {? _dk_c<>''
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_dk_c
         ?}
      ?};
      {? PARAM_W.KP='T' & xktm.KP<>''
      || _txt01:=exec('tr','tlumaczenia','1000230');
         _txt02:=exec('FindInSet','#table','ISTDEFSL','SL',xktm.KP,'007',\"ISTDEFSL.O\",1,'P',xktm.KP);
         w[2]+={? w[2]<>''
               || ' - %1 %2'[_txt01,_txt02]
               || '%1 %2'[_txt01,_txt02]
               ?}
      ?};
      w[2]:={? walutowa=0 | PARAM_W.WALH='H' || w[2] || '' ?};

      w[3]:={? walutowa=0 | PARAM_W.WALH='H' || FAP.M().PKWIU().KOD; xktm.PKW || '' ?};
      w[4]:={? walutowa=0 | PARAM_W.WALH='H' || FKOR.IL || '' ?};
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_jm','tlumaczenia',FAP.JM,PARAM_W.JEZYK)
      ?};
      w[5]:={? walutowa=0 | PARAM_W.WALH='H' || {? PARAM_W.JEZYK<>null & _tlumacz<>'' || _tlumacz || FAP.JM().KOD ?} || '' ?};
      w[6]:={? _spp || '' || {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.CPR || FKOR.CWAL ?} ?};
      _cdokl:={? FAKS.SZ='S'
              || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
              || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
              ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      || w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP)
               || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
               || FAP.RABK+(FAKS.RAB*FKOR.CWAL/100)$_cdokl
               ?}
      || w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP)
               || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
               || FKOR.CPR-{? odbrutto=1 || FKOR.CB || FKOR.CN ?}
               ?}
      ?};
      _doklFix:=FAKS.name()+2<>'hs' | exec('get','#params',300215,2)='T';
      _dokl_c:=
         {? _doklFix
         || {? FAKS.SZ='S' || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
            |? FAKS.SZ='Z' || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
            || 2
            ?}
         || _dokl_c:=+(2-$fabs(frac({? __walh<>__walp || FKOR.CWAL || FKOR.CPR ?})));
            {? _dokl_c<2 || 2 || _dokl_c ?}
         ?};
      w[8]:=
         {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
         || {? odbrutto=1 || FKOR.CB || FKOR.CN ?}
         || {? exec('czyrabp','ceny',FAKS.RAB_TYP)
            || _rabat:=1-w[7]/100;
               FKOR.CWAL*_rabat $_dokl_c
            || FKOR.CWAL-FAP.RABK-(FKOR.CWAL*FAKS.RAB/100)$_dokl_c
            ?}
         ?};
      {? walutowa=0 || $!s[1] || $!s[4] ?}()+=(w[9]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.WWAL_P || FKOR.WWAL ?});
      w[10]:=FAP.SV().KOD;
      {? walutowa=0 || $!s[2] || $!s[5] ?}()+=(w[11]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.VWAL_P || FKOR.VWAL ?});
      {? walutowa=0 || $!s[3] || $!s[6] ?}()+=(w[12]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BWAL_P || FKOR.BWAL ?});
      w[13]:={? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || __walh || __walp ?};

::    edit: sitek - poprawienie wyliczania pozycji po korekcie jesli rabat dla naglowka (wymuszone przez poprawianie wartosci faktury dla cosnova)
      {?  FAKS.T().T='KORWDT' | FAKS.T().T='KORWDTS' | FAKS.T().T='WDTS' | FAKS.T().T='WDT'
      ||  w[14]:=
            {? _spp
            ||  ''
            || {? FKOR.FRAB=0
               || {? odbrutto=1
                  || w[12]
                  || w[9]
                  ?}
               || (FKOR.IL*w[6])$2
               ?}
            ?}
      || w[14]:={? _spp || '' || (FKOR.IL*w[6])$2 ?}
      ?};
      w[15]:=w[14]-{? odbrutto=1 || w[12] || w[9] ?};
      w[16]:={? FAP.D<>date(0,0,0) & (walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='H')
             || gsub(form(FAP.D,,3),'/','-')
             || ''
             ?};

::    szacowanie ilosci lini pozycji
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+1;

      FAP.cntx_pop();
      FAKS.cntx_pop()
   ";
:: pozycje po korekcie
   pozycjab:="
      exec('wysw_kor','faktury_poz');
      _spp:=walutowa=0 & __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & FAKS.T().SPP='T';

      w[1]:='';
      {? mpp
      ||
         {? walutowa=0 | PARAM_W.WALH='H'
         ||
            _mpp:='';
            {? FAKS.SP_WYM='T' & FAKS.SZ='S'
            ||
               _mpp:=' ';
               {? FAP.SP_WYM='T' || _mpp:='*' ?}
            ?};
            w[1]:=form(FAP.POZ,,0,'99')+_mpp
         ?}
      ||
         w[1]:={? walutowa=0 | PARAM_W.WALH='H' || form(FAP.POZ,,0,'99') || '' ?}
      ?};
      w[2]:='';

      {? PARAM_W.JEZYK<>null
      || _language:=PARAM_W.JEZYK;
         {? PARAM_W.JEZYK().KOD='OBA'
         || _slu:=exec('FindInSet','#table','SLU','NAZ','Języki obce',,,1);
            _language:=exec('FindInSet','#table','SLO','SL','ANG',_slu,,1)
         ?};
         _tlumacz:=exec('trans_m','tlumaczenia',FAP.M,_language)
::      || _tlumacz:=exec('trans_m','tlumaczenia',FAP.M,PARAM_W.JEZYK)
      ?};

      {? PARAM_W.INDEKS='T'
      ||
         {? +FAP.MX
         || w[2]+=FAP.MX+' '
         || w[2]+={? FAP.M<>null || FAP.M().KTM+' ' || xktm.UWA+' ' ?}
         ?}
      ?};
::      {? PARAM_W.NAZWA='T' & (PARAM_W.JEZYK=null | _tlumacz='')
      {? PARAM_W.NAZWA='T'
      ||
         {? +FAP.NX
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+FAP.NX+' '
         |? FAP.M
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+{? FAP.M<>null || FAP.M().N+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.JEZYK<>null & _tlumacz<>''
      ||
         {? +FAP.MX
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_tlumacz+' '
         || w[2]+={? w[2]<>''||' - ' || '' ?}+{? FAP.M<>null || _tlumacz+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.UWAGI='T' & FAP.memo_lin('UWAGI')<>'\n' & (FAP.M<>null | +FAP.NX)
      || FAP.memo_get(,'UWAGI');
         w[2]+={? w[2]<>'' || ' - ' || '' ?}+gsub(FAP.memo_txt(0,1,'UWAGI'),'\n',' ')
      ?};
      {? PARAM_W.KODOBCY='T' & FAP.M<>null & FAKS.KH<>null
      ||  _kod_obcy:=exec('kod_obcy','kody_kresk',FAP.M,FAKS.KH);
         {? _kod_obcy<>''
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+_kod_obcy
         ?}
      ?};
      {? PARAM_W.DK_C='T' & FAP.DK_C
      || _dk_c:=form(exec('sym_dk_c','mat_atr',FAP.DK_C));
         {? _dk_c<>''
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_dk_c
         ?}
      ?};
      {? PARAM_W.KP='T' & FAP.KP<>''
      || _txt01:=exec('tr','tlumaczenia','1000230');
         _txt02:=FAP.KP;
         w[2]+={? w[2]<>''
               || ' - %1 %2'[_txt01,_txt02]
               || '%1 %2'[_txt01,_txt02]
               ?}
      ?};
      w[2]:={? walutowa=0 | PARAM_W.WALH='H' || w[2] || '' ?};

      w[3]:={? walutowa=0 | PARAM_W.WALH='H' || exec('pkwiu','material',FAKS.DW,FAP.M,1,FAP.PKWIU_CN) || '' ?};
      w[4]:={? walutowa=0 | PARAM_W.WALH='H' || FKOR.BIL || '' ?};
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_jm','tlumaczenia',FAP.JM,PARAM_W.JEZYK)
      ?};
      w[5]:={? walutowa=0 | PARAM_W.WALH='H' || {? PARAM_W.JEZYK<>null & _tlumacz<>'' || _tlumacz || FAP.JM().KOD ?} || '' ?};
      w[6]:={? _spp || '' || {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BCPR || FKOR.BCWAL ?} ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      || w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP) || FAP.RABK+(FAKS.RAB*FKOR.BCWAL/100)$2 ?}
      || w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP) || FKOR.BCPR-{? odbrutto=1 || FKOR.BCB || FKOR.BCN ?} ?}
      ?};
      w[8]:=
         {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
         || {? odbrutto=1 || FKOR.BCB || FKOR.BCN ?}
         || {? exec('czyrabp','ceny',FAKS.RAB_TYP)
            || _rabat:=1-w[7]/100;
               FKOR.BCWAL*_rabat ${? FAKS.SZ='S'
                                  || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
                                  || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
                                  ?}
            || FKOR.BCWAL-FAP.RABK-(FKOR.BCWAL*FAKS.RAB/100)${? FAKS.SZ='S'
                                                             || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
                                                             || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
                                                             ?}
            ?}
         ?};
      {? walutowa=0 || $!s[1] || $!s[4] ?}()+=w[9]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BWWAL_P || FKOR.BWWAL ?};
      w[10]:=FAP.SV().KOD;
      {? walutowa=0 || $!s[2] || $!s[5] ?}()+=w[11]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BVWAL_P || FKOR.BVWAL?};
      {? walutowa=0 || $!s[3] || $!s[6] ?}()+=w[12]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BBWAL_P || FKOR.BBWAL ?};
      w[13]:={? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || __walh || __walp ?};

::    edit: sitek - poprawienie wyliczania pozycji po korekcie jesli rabat dla naglowka (wymuszone przez poprawianie wartosci faktury dla cosnova).
      {? FAKS.T().T='KORWDT' | FAKS.T().T='KORWDTS' | FAKS.T().T='WDTS' | FAKS.T().T='WDT'
      || w[14]:=
            {? _spp
            || ''
            || {? FKOR.FRAB=0
               || {? odbrutto=1
                  || {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
                     || FKOR.BWAL_P
                     || FKOR.BWAL
                     ?}
                  || {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
                     || FKOR.WWAL_P || FKOR.WWAL
                     ?}
                  ?}
               || (FKOR.BIL*w[6])$2
               ?}
            ?}
      || w[14]:={? _spp || '' || (FKOR.BIL*w[6])$2 ?}
      ?};
      w[15]:=w[14]-
         {? odbrutto=1
         || {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
            || FKOR.BBWAL_P
            || FKOR.BBWAL
            ?}
         || {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
            || FKOR.BBWAL_P
            || FKOR.BWWAL
            ?}
         ?};
      w[16]:={? FAP.D<>date(0,0,0) & (walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='H')
             || gsub(form(FAP.D,,3),'/','-')
             || ''
             ?};
::    szacowanie ilosci lini pozycji
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+1
   ";

:: agregowanie wartosci do przeniesienia
   do_przep:="
      {? przed_po='przed'
      ||
         do_p[1]+=FKOR.WWAL_P;
         do_p[2]+=FKOR.VWAL_P;
         do_p[3]+=FKOR.BWAL_P
      ||
         do_p[1]+=FKOR.BWWAL_P;
         do_p[2]+=FKOR.BVWAL_P;
         do_p[3]+=FKOR.BBWAL_P
      ?}";
   do_przeh:="
      {? przed_po='przed'
      ||
         do_p[4]+=FKOR.WWAL;
         do_p[5]+=FKOR.VWAL;
         do_p[6]+=FKOR.BWAL
      ||
         do_p[4]+=FKOR.BWWAL;
         do_p[5]+=FKOR.BVWAL;
         do_p[6]+=FKOR.BBWAL
      ?}";

   sk:=obj_new(obj_len(s));
   vatk:=obj_new(3);
   kw_zal:=0;
   zal_wal:='';
   slownie:='';
   VAR_DEL.delete('xktm');
   xktm:=tab_tmp(1,
      'POZ','INTEGER','',
      'KTM','STRING[50]','',
      'NAZ','STRING[200]','',
      'KTX','STRING[50]','',
      'NAX','STRING[200]','',
      'UWA','STRING[255]','',
      'JEM','STRING[20]','',
      'PKW','STRING[50]','',
      'DKC','STRING[255]','',
      'KP','STRING[2]','');
   kw_fak:=0;
   faks:=BB.refsql(FAKS.ref)
?}



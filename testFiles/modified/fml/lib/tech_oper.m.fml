:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tech_oper.fml
:: Utworzony: 03.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi operacji kart technologicznych, wzorców, technologii zlecenia
::            (biblioteka wspólna dla TTE_TEC, TTE_WTE, TTE_PZL)
::            Obsługa tebel:
::            - TOPER - operacje technologiczne
::            - TECHZAMS - zamienniki stanowisk operacji
::            - NASTOPER - następniki operacji
::            - ZGP - pozycje przewodników (kilka pól zamiennych VAR, VAR1 w zakładce Tpz)
::======================================================================================================================
\tree
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.70]
:: OPIS: Struktura - drzewo operacji
::       !!! Działa w kontekście TKTL
::   WE: [_a] - sposób wywołania funkcji czy z poziomu zakładak czy z menu:
::              0 - zakładki zlecenia - technologia źródłowa [TTREE]
::              1 - menu okna wertowania TKTL albo lista ToDo (domyślnie) [TREE]
::              2 - przepis planistyczny [XTTREE]
::              3 - zakładki zlecenia - technologia zlecenia [ZTREE]
::              4 - lista ToDo dla surowców, gdy surowce do operacji [STREE]
::              5 - lista ToDo dla NPU, gdy NPU do operacji [NTREE]
::              6 - kartoteka materiałowa, zakładka Technologia domyślna
::              7 - akcja Przenieś lub F3 w oknie redakcji (tworzone okno selekcji) [SELTREE]
::       [_b] - dostępne redagowanie (1 - domyślnie), nie dostępne redagowanie (0)
::  OLD: \tree/tex_ope1.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

_zakl:={? var_pres('_a')=type_of(0) || _a || 1 ?};
_edit:={? var_pres('_b')=type_of(0) || _b || 1 ?};

{? _zakl=3 || _torw:='Z' |? _zakl=0 || _torw:='T' || _torw:=exec('FindAndGet','#table',TKTL,VAR.A_KTL,,"TORW",'') ?};
_plrelwyr:=exec('FindAndGet','#table',TKTL,VAR.A_KTL,,"PLRELWYR",'');

_locked:=0;
_result:=~~;
{? ~_edit
|| _locked:=0;
   _used:=1
|| {? TKTL.ref()<>null()
   || _locked:=exec('tktl_lock','tech_common',,'O')
   ?};
   _used:=~_locked
?};

:: Ustawienie okien dla słownika kontrahentów
exec('ustaw_okna','kontrahent');

_dia:=exec('dot_obj','#graph','toper');

{? _zakl=1 | _zakl=4 | _zakl=5 | _zakl=6 | _zakl=7
||
   TKTL.cntx_psh();
   {? _zakl=1
   || VAR_DEL.delete('TREE','Objt')
   |? _zakl=4
   || VAR_DEL.delete('STREE','Objt')
   |? _zakl=5
   || VAR_DEL.delete('NTREE','Objt')
   |? _zakl=6
   || VAR_DEL.delete('TTREE','Objt')
   |? _zakl=7
   || VAR_DEL.delete('SELTREE','Objt')
   ?};
   XINFO.SLWYDZIA();
   VAR.A_TORW:=_torw;

   _hide:=':';
:: Ukrycie akcji wynikające z kontekstu programu (UWAGA: w zakładkach zlecenia dodatkowo \technologia_load/zl_view.fml)
   {? _zakl=4
   || _hide:='DPUZE(ZNAD)F(NZUWER):DZF(WE)'
   |? _zakl=5
   || _hide:='DPUZE(ZSAD)F(NZUWER):DZF(WE)'
   |? _zakl=7
   || _hide:='DPUZEF:DZF'
   || {? TKTL.ARCH='T' | _used | (VAR.A_ZLEC<>null() & _torw='T') | (_torw='Z' & _plrelwyr<>null())
      || _hide:='DPUF(RZ):D'
      |? TKTL.STAN='T' & _torw='T' & exec('can_modify','tech_common')
      || _hide:=':'
      |? TKTL.STAT_O='T' | TKTL.STAT_N='N' & TKTL.TORW<>'Z'
      || _hide:='DPUF(ZR):D'
      || _hide:=':'
      ?}
   ?};
   {? TKTL.STAT_O='T' | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.ARCH='T' | _used
   || _hide:='Z'+_hide+'Z'
   ?};
   {? _dia.dot_cmd='' || _hide:='F(D)'+_hide ?};
   {? (TKTL.STAT_O='T' | _used | TKTL.ARCH='T' | TKTL.STAT_N='N' & TKTL.TORW<>'Z')
   || _hide:='F(N)'+_hide
   ?};
   {? (TKTL.STAT_O='T' | TKTL.TORW='W' | _used | TKTL.ARCH='T' | TKTL.STAT_N='N' & TKTL.TORW<>'Z')
   || _hide:='F(WE)'+_hide+'F(WE)'
   ?};
::   {? TKTL.TYP().SUR='K' || _hide:='E(S)'+_hide ?};
   {? TKTL.TYP().UTIL='N' || _hide:='E(N)'+_hide ?};
   {? ~(TKTL.STAN='T' & TKTL.ARCH='N' & ~_used) || _hide:='Ó'+_hide ?};
   {? TKTL.TORW='W' || _hide:='F(U)'+_hide ?};
   {? TKTL.TORW='W' | TKTL.TORW='T' || _hide:='F(Z)'+_hide ?};

:: Ukrycie akcji wynikające z uprawnień do czynności
   {? ~(exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTO') & _torw='T' |
        exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTO') & _torw='W' |
        exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC') & _torw='Z')
   || _hide:='DPUÓZF(NWER)'+_hide+'DZF(WE)'
   ?};

   TWRKZBR.actions('WER','H');

   PFAZ.clear();
   PFAZ.f_set('KOD', '',
                'PFAZ.WYD='':_a'' or '':_a''=''''',
               $TKTL.JORG);
   FILTER.PFAZ:=0;

   _debug:=0;
   {? _zakl=1
   || TREE:=obj_new(@.CLASS.TREEVIEW,_hide,_debug,'tree_oper'+(-TKTL.STAT_O)+$_zakl+$_used);
      TREE.SZEROK:=77;
      Objt:=TREE
   |? _zakl=4
   || STREE:=obj_new(@.CLASS.TREEVIEW,_hide,_debug,'tree_oper'+(-TKTL.STAT_O)+$_zakl+$_used);
      STREE.SZEROK:=77;
      Objt:=STREE
   |? _zakl=5
   || NTREE:=obj_new(@.CLASS.TREEVIEW,_hide,_debug,'tree_oper'+(-TKTL.STAT_O)+$_zakl+$_used);
      NTREE.SZEROK:=77;
      Objt:=NTREE
   |? _zakl=6
   || TTREE:=obj_new(@.CLASS.TREEVIEW,_hide,_debug,'tree_oper'+(-TKTL.STAT_O)+$_zakl+$_used);
      TTREE.SZEROK:=105;
      Objt:=TTREE
   |? _zakl=7
   || SELTREE:=obj_new(@.CLASS.TREEVIEW,_hide,_debug,'tree_oper'+(-TKTL.STAT_O)+$_zakl+$_used);
      SELTREE.SZEROK:=105;
      Objt:=SELTREE
   ?}
|? _zakl=0
||
   VAR_DEL.delete('TTREE','Objt');
   _hide:='';
   _debug:=0;
   TTREE:=obj_new(@.CLASS.TREEVIEW,_hide,_debug);
   Objt:=TTREE
|? _zakl=3
||
   VAR_DEL.delete('ZTREE','Objt');
   _hide:='';
   _debug:=0;
   ZTREE:=obj_new(@.CLASS.TREEVIEW,_hide,_debug);
   Objt:=ZTREE
|? _zakl=2
||
   VAR_DEL.delete('XTTREE','Objt');
   _hide:='DPUZF(NZWE):D';
   _debug:=0;
   XTTREE:=obj_new(@.CLASS.TREEVIEW,_hide,_debug);
   Objt:=XTTREE
?};
_timedok:=exec('get','#params',500604,1);
Objt.fld('NTIME','REAL','Norma czasowa'@,14,_timedok,0,1);
Objt.fld_fml('NTIME','DISPLAY_FORMAT','{? fld()=0 || \'empty=1\' || \'empty=0\' ?}');
Objt.fld('MTIME','REAL','Czas maszynowy'@,14,_timedok,0,1);
Objt.fld_fml('MTIME','DISPLAY_FORMAT','{? fld()=0 || \'empty=1\' || \'empty=0\' ?}');
Objt.fld('CENA','REAL','Kooperacja'@,10,2,0,1,'Wartość usług kooperacyjnych'@);
Objt.fld_fml('CENA','DISPLAY_FORMAT','{? fld()=0 || \'empty=1\' || \'empty=0\' ?}');
Objt.fld('WEW','STRING[1]','Wewnętrzna/kooperacja'@,2,0,1,0,'Operacja wewnętrzna/kooperacja'@);
Objt.fld('PZ','STRING[1]','Prosta/złożona'@,2,0,1,0,'Operacja prosta/złożona'@);
Objt.fld('PLACE','STRING[10]','Stanowisko'@,10,0,1,0,'Symbol stanowiska'@);
Objt.fld('PLACE_N','STRING[60]','Nazwa stanowiska'@,10,0,1,0);
Objt.fld('GRUPA','STRING[20]','Gniazdo'@,10,0,1,0);
Objt.fld('GRUPA_N','STRING[40]','Nazwa gniazda'@,10,0,1,0);
Objt.fld('OPER','STRING[10]','Operacja'@,10,0,1,0,'Kod operacji'@);
Objt.fld('OPER_N','STRING[50]','Nazwa operacji'@,10,0,1,0);
Objt.fld('ZAWOD','STRING[7]','Zawód'@,7,0,1,0);
Objt.fld('ZAWOD_N','STRING[30]','Opis zawodu'@,10,0,1,0);
Objt.fld('TPZ','STRING[1]','TPZ'@,2,0,1,0);
Objt.fld('TPZ_TIME','REAL','Czas TPZ'@,14,_timedok,1,1);
Objt.fld_fml('TPZ_TIME','DISPLAY_FORMAT','{? fld()=0 || \'empty=1\' || \'empty=0\' ?}');
Objt.fld('TPZ_ZAW','STRING[7]','Zawód TPZ'@,7,0,1,0);
Objt.fld('TPZ_ZAWN','STRING[30]','Opis zawodu TPZ'@,10,0,1,0);
Objt.fld('PFAZ','STRING[10]','Faza prod.'@,10,0,1,0);
Objt.fld('WYD','STRING[8]','Wydział'@,8,0,1,0);
Objt.fld('DOK','STRING[8]','Dokument'@,8,0,1,0);
Objt.fld('NAST','STRING[255]','Następne'@,20,0,1,0);
Objt.fld('ETAP','STRING[60]','Powiązany etap w planie strategicznym'@,20,0,1,0);
Objt.fld('DAYS_K','INTEGER','Szacunkowa ilość dni na kooperację'@,5,0,1,0);
Objt.fld('PX_KONT','STRING[60]','Zasób kooperacyjny'@,20,0,1,0);
Objt.fld('KONTRAH','STRING[8]','Kooperant'@,8,0,1,0);
Objt.fld('PL_GRP','STRING[1]','Grupowe planowanie'@,2,0,1,0);
Objt.fld('KONTROLA','STRING[1]','Operacja typu kontrola jakości'@,2,0,1,0);
Objt.fld('KJ_BAD','STRING[1]','Kontrola jakości - badania'@,2,0,1,0);
Objt.fld('OPIS','STRING[255]','Opis'@,20,0,1,0);
Objt.fld('FIX_NORM','STRING[1]','Stała norma czasowa'@,2,0,1,0);

{? _zakl=4 | _zakl=5 | _zakl=6 | _zakl=7
||
:: Dla okna do redagowania surowców albo NPU - zawsze bez bocznego panela
   ~~
|? ((TKTL.TORW='T' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTO')) |
    (TKTL.TORW='W' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTO')) |
    (TKTL.TORW='Z' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC'))
   )
      &
   TKTL.STAT_O='N' & (TKTL.STAT_N='T' | TKTL.TORW='Z') & TKTL.ARCH='N' & _zakl<>2
||
:: Możliwość wyłączenia dołączania z innych tabel
   _dnd:=~_used;
   {? _dnd;0
   ||
      _ttoper_wer:=TTOPER.mk_sel('Taryfikator operacji'@,'P',0,'ttoper_wer');
      TTOPER.win_fld(_ttoper_wer,,'KOD',,,10,,,'Kod'@);
      TTOPER.win_fld(_ttoper_wer,,'NA',,,20,,,'Nazwa operacji'@);
      TTOPER.win_act(_ttoper_wer,,'Kolejność');
      Objt.add_tab(!TTOPER,_ttoper_wer,,,,,11,,,0,'right','right1','maximized_with_title');

      _ttout_wer:=TTOUT.mk_sel('Taryfikator usług (w kooperacji)'@,'P',0,'ttout_wer');
      TTOUT.win_fld(_ttout_wer,,'KOD',,,10,,,'Kod'@);
      TTOUT.win_fld(_ttout_wer,,'NA',,,20,,,'Nazwa'@);
      TTOUT.win_act(_ttout_wer,,'Kolejność');
      Objt.add_tab(!TTOUT,_ttout_wer,,,,,11,,,0,'right1','right2','maximized_with_title');

      VAR_DEL.delete('__Fun');
      __Fun:=tab_tmp(1,'NAZ','STRING[30]','Nazwa');
      __Fun.NAZ:='1. <- '+'wstaw operację złożoną'@; __Fun.add();
::      __Fun.NAZ:='2. -> '+'usuń operację'@; __Fun.add();
      __Fun.first();
      _fun_wer:=__Fun.mk_sel('Funkcje dostępu'@,,0,'fun_wer_t');
      __Fun.dnd_sel(_fun_wer,,'records.#tree_wert',"
         FUN.info('Rekordy z: %1\nupuszczam na rekord: %2'@[dnd_info('table_name'),$dnd_info('dest_record')])
      ");
      __Fun.win_fld(_fun_wer,,'NAZ',,,31,,,'Nazwa'@);
      Objt.add_tab(!__Fun,_fun_wer,,,,,1,,,0,'right2',,'maximized_with_title')
   ?}
||
   {? _dia.dot_cmd<>'' || Objt.add_dia(TKTL,'DIAGRAM') ?}
?};

{? _zakl=2 | _zakl=6 | _zakl=7
|| ~~
|| Objt.add_tab(!NASTOPER,'SLOPOP',,,,0,3,'
      NASTOPER.index(''NASTOP'');
      NASTOPER.prefix(''T'',VAR.A_OP);
      NASTOPER.first();
      ~~','
      ~~',,'bottom','bottom1','maximized_with_title')
?};

{? _zakl=2 | _zakl=6 | _zakl=7
|| ~~
|| Objt.add_tab(!NASTOPER,{? _zakl=4 | _zakl=5 || 'SLO' || 'WER_'+_torw ?},,,,0,3,'
      NASTOPER.index(''OPNAST'');
      NASTOPER.prefix(''T'',VAR.A_OP);
      NASTOPER.first();
      {? VAR.A_OP=null() || NASTOPER.actions(''WER_'+_torw+''',''DPUG:DG'','''',1) ?};
      ~~','
      ~~',,'bottom1',,'maximized_with_title')
?};

set_virt(Objt,'f_record',$("
   _torw:='"+_torw+"';
   _used:=params_get().env_oper.used;
   _wer:='WER_'+_torw;
   TOPER.clear();
   {? TOPER.seek(cur_tab(1,1).REF,)
   || VAR.A_OP:=TOPER.ref();
      _res:=Color.rekprzed('TOPER#01#01');
      VAR.A_KTL();
::    Akcje dla NASTOPER
      _keys:=':'; _default:='D:D';
      {? TKTL.ARCH='T'
            |
         _used
            |
         _torw='Z' & TKTL.PLRELWYR<>null()
            |
         _torw='T' & VAR.A_ZLEC<>null()
            |
         TKTL.STAN='T' & _torw='T' & ~exec('can_modify','tech_common') & TOPER.ACT='T'
            |
         TKTL.STAN='T' & _torw='W'
            |
         TKTL.STAN='T' & _torw='Z'
      || _keys:='DPUG'+_keys+'DG'
      ?};
      {? TOPER.PZ='Z' || _keys:='D'+_keys+'D' ?};
      NASTOPER.actions(_wer,_keys,_default,1);
::    Akcje dla "TOPER" - wyszarzone w zależności od rekordu
      _keys:=':';
      {? TOPER.ACT='T' || _keys:='Ó'+_keys || {? .TAB.sel_size()>0 || _keys:='U'+_keys ||  _keys:='UPF(R)'+_keys ?} ?};
      {? TOPER.PZ='Z' || _keys:='BE(ZSN)F(R)'+_keys ?};
      {? TOPER.WEW='N' || _keys:='E(ZN)'+_keys ?};
      .TAB.actions_grayed(.WERT,_keys)
   || VAR.A_OP:=null();
      _res:=~~;
      NASTOPER.actions(_wer,'DPU:D','',1)
   ?};
   TKTL.index('NRK');
   TKTL.prefix(VAR.A_KTL().TORW,TKTL.NRK,TKTL.WER);
   TOPER.index('NNN');
   TOPER.prefix(TOPER.NRK,TOPER.NRNOP,'N');
   _res
"));

set_virt(Objt,'f_legend',"
   exec('legenda','tech_oper')
");

{? _zakl=0 | _zakl=3 | _zakl=6
|| set_virt(Objt,'title',"''")
|? _zakl=4
|| set_virt(Objt,'title',"
      'Operacje: %1 wersja: %2'@[TKTL.NRK,TKTL.WER]+
      {? .USED
      || ' — '+'PODGLĄD SUROWCÓW'@
      |? TKTL.ARCH='T'
      || ' — '+'ARCHIWALNE'@+' — '+'PODGLĄD SUROWCÓW'@
      |? TKTL.STAT_O='T'
      || ' — '+'ZATWIERDZONE'@+' — '+'REDAGOWANIE SUROWCÓW'@
      || ' — '+'REDAGOWANIE SUROWCÓW'@
      ?}
   ")
|? _zakl=5
|| set_virt(Objt,'title',"
      'Operacje: %1 wersja: %2'@[TKTL.NRK,TKTL.WER]+
      {? .USED
      || ' — '+'PODGLĄD N-P-U'@
      |? TKTL.ARCH='T'
      || ' — '+'ARCHIWALNE'@+' — '+'PODGLĄD N-P-U'@
      |? TKTL.STAT_O='T'
      || ' — '+'ZATWIERDZONE'@+' — '+'REDAGOWANIE N-P-U'@
      || ' — '+'REDAGOWANIE N-P-U'@
      ?}
   ")
|? _zakl=7
|| set_virt(Objt,'title',"
      'Operacje: %1 wersja: %2'@[TKTL.NRK,TKTL.WER]
   ")
|| set_virt(Objt,'title',"
      'Operacje: %1 wersja: %2'@[TKTL.NRK,TKTL.WER]+
      {? .USED
      || ' — '+'PODGLĄD'@
      |? TKTL.ARCH='T'
      || ' — '+'ARCHIWALNE'@
      |? TKTL.STAT_O='T'
      || ' — '+'ZATWIERDZONE'@
      || ''
      ?}
   ")
?};

set_virt(Objt,'hdr_1st',"
   'Operacja'
");

set_virt(Objt,'f_add',"
::----------------------------------------------------------------------------------------------------------------------
:: WE: _a - sposób dodawania operacji; 0: normalnie, 1: pusta dziedzina, 2: d'n'd
::     _b - PARAMETR NIEUŻYWANY
::     [_c] - jezeli _a=2, tabela źródłowa
::     [_d] - jezeli _a=2, ref zaznaczonego rekordu
::     [_e] - jezeli _a=2, ref rekordu na który zrzucamy
::----------------------------------------------------------------------------------------------------------------------
   params_set(params_get());

   {? _<1 || _a:=0 ?};
   VAR.KOR:=TKTL.STAT_O='T';

   {? _a=2
   || {? Objt.TAB.seek(_e)
      || Objt.f_record()
      || TOPER.blank(1)
      ?}
   ?};

   VAR.A_WYD:=TKTL.JORG;
   {? VAR.GRP_MOD='' ||  VAR.GRP_MOD:='N' ?};
   exec('toper_be','tech_oper');
   _bryg:='N';
   {? TOPER.PZ='Z' & (_a=0 | _a=2)
   || _ask:=choice('Dodawana operacja jest:'@,FUN.TYT,'ASK',1,0,,
         'Kolejna na tym poziomie'@,
         'Podrzędna do bieżącej'@,
         'Anuluj'@);
      {? _ask=2
      || return(0)
      |? _ask=1
      || {? TOPER.ACT='T'
         || _nrnop:=TOPER.UNROP;
            _bryg:=TOPER.BRYG;
            .expand(2)
         || FUN.info('Bieżąca operacja złożona jest nieaktywna — nie można do niej dodawać składowych.'@);
            return(0)
         ?}
      |? _ask=0
      || _nrnop:=TOPER.NRNOP
      ?}
   || _nrnop:=TOPER.NRNOP
   ?};

   VAR.A_UNROP:=_nrnop;

   {? _a=2
   || {? _c=TTOPER
      || _wariant:=0
      |? _c=TOPER
      || _wariant:=1
      |? _c=TTOUT
      || _wariant:=2
      ?}
   || _wariant:=3
   ?};

   {? VAR.KOR
   || TTXCAUSE.win_sel('WER');
      TTXCAUSE.actions('WER','T','d:d')
   ?};

   {? exec('toperp_d','tech_oper',_wariant)
   || _nrk:=TKTL.ref();
      TOPER.index('NNN');
      TOPER.prefix(_nrk,_nrnop,'N');
      TOPER.blank();
      {? _wariant=0
      || TOPER.OPER:=_d;
         exec('ae_ttoper','tech_oper',1)
      |? _wariant=2
      || TOPER.TTOUT:=_d;
         exec('ttout_ae','tech_oper',1)
      ?};
      TOPER.BRYG:=_bryg;
      TOPER.NRNOP:=_nrnop;
      TOPER.NRK:=_nrk;
      {? VAR.KOR
      || TOPER.OD:=date();
         TOPER.CRE_TIME:=TOPER.tm_stamp();
         TOPER.USER:=OPERATOR.USER;
         TOPER.CAUSE:=exec('ttxcause_default','tech_common')
      ?};
      {? TOPER.TTOUT<>null
      || TOPER.SRW:=TOPER.TTOUT().SRW
      ?};
      exec('efld_opt','tech_oper',TOPER);
      {? TOPER.memo_set('','OPISMEMO') & TOPER.edit(\"exec('topearec','tech_oper')\")
      || TOPER.clear();
         {? TOPER.add()
         || TOPER.memo_put(,'OPISMEMO');
:: NUCO - uruchomienie aktualizacji karty technologicznej projektowej po zmianie w kartotece surowca
            {? TOPER.NRK().TYP().TYP='PRJ' & VAR.KOR  & TOPER.NRK().TORW='T' & TOPER.NRK().STAN='T' & TOPER.NRK().ARCH='N'
            || exec('akt_px','qtpp',TOPER.NRK,'TOPER', $TOPER.ref())
            |? TOPER.NRK().TYP().TYP='PRD' & VAR.KOR  & TOPER.NRK().TORW='T' & TOPER.NRK().STAN='T' & TOPER.NRK().ARCH='N'
            || exec('akt_px_pro','qtpp',TOPER.NRK,'TOPER', $TOPER.ref())
            ?};

            {? VAR.KOR
            || NASTOPER.index('OPER');
               NASTOPER.prefix('T',TOPER.NRK);
               {? NASTOPER.first()
               || {? FUN.choice('Należy zmodyfikować strukturę operacji.\nStrukturę można wygenerować automatycznie.'@,,
                                'Generuj'@)
                  || exec('er_tab','tech_oper',VAR.A_KTL);
                     exec('gen_pow','tech_oper',VAR.A_KTL);
                     exec('po_nast','tech_oper')
                  ?}
               || exec('er_tab','tech_oper',VAR.A_KTL);
                  exec('gen_pow','tech_oper',VAR.A_KTL);
                  exec('po_nast','tech_oper')
               ?}
            ?}
         ?};
         exec('toperd_a','tech_oper');
         _ref:=TOPER.ref();
         .TAB.cntx_psh();
         exec('level','tech_oper',.,0);
         .replica();
         .load();
         .update();
         .TAB.clear();
         {? .TAB.first()
         || {!
            |?
               .TAB1.blank();
               .TAB1.REF:=.TAB.REF;
               {? .TAB1.find_rec()
               || .TAB.HIDDEN:=.TAB1.HIDDEN;
                  .TAB.EXPANDED:=.TAB1.EXPANDED;
                  .tab_txt();
                  .TAB.put()
               ?};
               .TAB.next()
            !}
         ?};
         .TAB.cntx_pop();
         .TAB.blank();
         .TAB.REF:=_ref;
         .TAB.find_rec()
      ?}
   ?};
   VAR.KOR:=0;
   ~~
");

set_virt(Objt,'f_add_e',"
   .f_add(1)
");

set_virt(Objt,'f_delete',"
   params_set(params_get());
   {? TKTL.STAT_O='N'
   || .f_record();
      _ref:=TOPER.ref();
      TOPER.cntx_psh();
      TOPER.index('NNN');
      TOPER.prefix(TOPER.NRK,TOPER.NRNOP);
      {? TOPER.next()
      || _ref1:=#TOPER.ref()
      || {? TOPER.prev()
         || _ref1:=#TOPER.ref()
         || _ref1:=0
         ?}
      ?};
      TOPER.cntx_pop();
      exec('TOPER_TREE_kasuj','tech_oper');
      {? ~TOPER.seek(_ref)
      || .delete(.TAB.NODE);
         .update();
         .TAB.blank();
         .TAB.REF:=_ref1;
         .TAB.find_rec()
      ?}
   || .f_record();
      exec('zm_usun','tech_oper',1)
   ?};
   ~~
");

set_virt(Objt,'f_del_bg',"
   sel_nchk();
   {? TKTL.STAN='T'
   || FUN.info('Na zaakceptowanej karcie usuwać (dezaktywować) można tylko pojedyncze pozycje.'@);
      0
   |? FUN.ask('Ilość zaznaczonych operacji: %1. Czy usuwać?'@[form(.TAB.sel_size())])
   || VAR.GRUPA:='T';
      KOMM.init(,,'Usuwanie operacji'@,'');
      1
   || 0
   ?}
");

set_virt(Objt,'f_del_ag',"
   VAR.GRUPA:='N';
   KOMM.select();
   ~~
");

set_virt(Objt,'f_modify',"
   params_exec('toper_modify','tech_oper',,.)
");

set_virt(Objt,'f_displ',"
   exec('prep_ch','tech_oper');
   exec('efld_opt','tech_oper',TOPER);
   TOPER.display();
   ~~
");

{? _zakl<>4 & _zakl<>5 & _zakl<>7
|| set_virt(Objt,'chk_sel',"
      {? params_get().env_oper.used
      || 1
      || _torw:=VAR.A_KTL().TORW;
         _stan:=TKTL.STAN;
         {? (exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTO') & _torw='T' |
             exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTO') & _torw='W')
         || exec('valid_op','tech_oper',VAR.A_KTL,0,0)
             &
            (_torw='W' | _stan<>'T' | exec('spr_pdok','magdok_prod','TOPER','DOK',VAR.A_KTL,'',1,1,1))
         || 1
         ?}
      ?}
   ")
?};

set_virt(Objt,'win_fml',"
  \"{? cur_tab(1,1).WEW='N'
    || 'xwin16.png:90'
    |? cur_tab(1,1).PZ='Z'
    || {? cur_tab(1,1).tr_state=0
       || 'xwin16.png:32'
       || 'xwin16.png:32'
       ?}
    |? cur_tab(1,1).PZ='P'
    || {? cur_tab(1,1).KJ_BAD='B'
       || 'xwin16.png:93'
       |? cur_tab(1,1).KJ_BAD='P'
       || 'xwin16.png:94'
       |? cur_tab(1,1).KONTROLA='T'
       || 'xwin16.png:95'
       || 'xwin16.png:33'
       ?}
    || ''
    ?}\"
");

:: Takie czary, żeby zadziałały tłumaczenia akcji poniżej (teksty MUSZĄ być identyczne)
'Przywróć&'@;
'D&efinicje'@;'Zamienniki'@;'Surowce'@;'Wszystkie surowce'@;'N-P-U'@;'Atrybuty operacji'@;'Dokumentacja'@;
'Operacje'@;'Generuj'@;
'Zakończ'@;
'Funkcje'@;'Diagram'@;'Numeruj'@;'Zamień'@;'Utwórz wzorzec'@;'Wstaw wzorzec'@;'Zmian&y'@;

set_virt(Objt,'f_akcje',"
:: Przywróć
   _akcja:=$(\"
      params_set(params_get());
      {? TKTL.STAT_O='T'
      || \"+!Objt+\".f_record();
         exec('zm_usun','tech_oper',3)
      ?};
      ~~
   \");
   .TAB.win_act(.WERT,,'Formuła','Przywróć&'@@,,,_akcja,,,1,,,'Ó');

:: Operacje
   {? .ZAKL=3
   || _akcja:=\"
         params_set(params_get());
         _env:=params_get().env;
         _used:=params_get().env_oper.used;

         {? _env.TKTL_ZL=null()
         || FUN.info('Brak technologii zlecenia.'@)
         || VAR.OPER:=exec('FindAndGet','#table',TKTL,_env.TKTL_ZL,,\"\"TYP().OPER\"\",'');
            {? VAR.OPER='T'
            || params_exec('tree','tech_oper',,~_used);
               ZTREE.reload()
            || FUN.info('Typ karty bez operacji technologicznych.'@)
            ?}
         ?}
      \";
      .TAB.win_act(.WERT,0,'Formuła','Operacje'@@,,,_akcja,,,,,,'O');
      .TAB.win_act(.WERT,1,'Formuła','Operacje'@@,,,_akcja,,,,,,'O')
   |? .ZAKL=0
   || _akcja:=\"
         params_set(params_get());
         _env:=params_get().env;

         _stan:=exec('FindAndGet','#table',ZL,_env.ZL,,\"\"STAN\"\",'');
         TKTL.cntx_psh();
         TKTL.prefix();
         {? TKTL.seek(_env.TKTL_SRC)
         ||
            VAR.OPER:=TKTL.TYP().OPER;
            {? VAR.OPER='T'
            || params_exec('tree','tech_oper',,0)
            || FUN.info('Typ karty bez operacji technologicznych.'@)
            ?}
         ?};
         TKTL.cntx_pop()
      \";
      .TAB.win_act(.WERT,0,'Formuła','Operacje'@@,,,_akcja,,,,,,'O');
      .TAB.win_act(.WERT,1,'Formuła','Operacje'@@,,,_akcja,,,,,,'O')
   ?};

:: Generuj
   {? .ZAKL=3
   || _akcja:=\"
         params_set(params_get());
         _env:=params_get().env;

         ZL.cntx_psh();
         ZL.prefix();
         {? ZL.seek(_env.ZL)
         ||
            _wp:=ZL.TYP().WP;
            _tech:=ZL.TYP().TECH;

            _wynik:={? _wp='P' & _tech='T'
                    || {? ZL.RODZAJ<>'P'
                       || 0
                       || 1
                       ?}
                    || 0
                    ?} & {? ZL.STAN<>'N' || ZL.KTLZ<>null() || 1 ?};
            {? _wynik=1
            || {? exec('action_gen','zl_view')
               || _env.TKTL_ZL:=VAR.A_KTL;
                  exec('start_tpar','tech_param',ZL.KTM,_env.TKTL_ZL);
                  ZTREE.reload()
               ?}
            || FUN.info('Funkcja niedostępna.'@)
            ?}
         ?};
         ZL.cntx_pop();
         ZTREE.ZRODLO:='';
         grp_disp(ZTREE.TAB,ZTREE.WERT,,1)
      \";
      .TAB.win_act(.WERT,1,'Formuła','Generuj'@@,,,_akcja,,,,,,'G')
   ?};

:: Definicje
   .TAB.win_act(.WERT,,'Menu','D&efinicje'@@,,,,,,,,,'E');

   _akcja:=\"params_exec('tech_zam','tech_oper')\";
   .TAB.win_act(.WERT,0,'Formuła','Zamienniki'@@,'#E','Selekcja zamienników stanowiska przypisanego do operacji',_akcja,,,,,,'Z');

   .TAB.win_act(.WERT,,'Formuła','--Y','#E');

   _akcja:=\"params_exec('action_oper','tech_mater',params_get().env_oper.used,params_get().env_oper.zakl)\";
   .TAB.win_act(.WERT,,'Formuła','Surowce'@@,'#E',,_akcja,,,,,,'S');

   {? .ZAKL=4
   || _akcja:=\"params_exec('action_all_mater','tech_mater')\";
      .TAB.win_act(.WERT,,'Formuła','Wszystkie surowce'@@,'#E',,_akcja,,,,,,'W')
   ?};

   _akcja:=\"exec('action_tools','tech_tool',0,'',params_get().env_oper.used)\";
   .TAB.win_act(.WERT,,'Formuła','N-P-U'@@,'#E',,_akcja,,,,,,'N');

   .TAB.win_act(.WERT,,'Formuła','--X','#E');

   _akcja:=\"params_exec('TOPER','tech_atr')\";
   .TAB.win_act(.WERT,,'Formuła','Atrybuty operacji'@@,'#E',,_akcja,,,,,,'A');

   _akcja:=\"params_exec('TOPER','tech_doc')\";
   .TAB.win_act(.WERT,,'Formuła','Dokumentacja'@@,'#E',,_akcja,,,,,,'D');

   _akcja:=\"exec('toper_dalej','tech_oper')\";
   {? TKTL.TORW<>'Z'
   || .TAB.win_act(.WERT,0,'Formuła','Zakończ'@@,,,_akcja,,,,,,'Z')
   ?};
   {? TKTL.TORW='W'
   || .TAB.win_act(.WERT,1,'Formuła','Zakończ'@@,,,_akcja,,,,,,'Z')
   ?};

:: Funkcje
   .TAB.win_act(.WERT,0,'Menu','Funkcje'@,,,,,,,,,'F');
   .TAB.win_act(.WERT,1,'Menu','Funkcje'@,,,,,,,,,'F');

   _akcja:=\"exec('graph_oper','tech_oper')\";
   .TAB.win_act(.WERT,0,'Formuła','Diagram'@@,'#F',,_akcja,,,,,,'D');

   _akcja:=$(\"{? ~exec('topernum','tech_oper') || \"+!Objt+\".reload() ?}\");
   .TAB.win_act(.WERT,0,'Formuła','Numeruj'@@,'#F','Przenumerowanie operacji'@,_akcja,,,,,,'N');

   _akcja:=$(\"exec('placechg','tech_oper'); \"+!Objt+\".reload()\");
   .TAB.win_act(.WERT,0,'Formuła','Zamień'@@,'#F','Użycie zamiennika stanowiska operacji'@,_akcja,,,,,,'Z');

   _akcja:=$(\"{? exec('toper_wew_change','tech_oper',,\"+!Objt+\") || \"+!Objt+\".reload() ?}\");
   .TAB.win_act(.WERT,0,'Formuła','Zmiana &rodzaju operacji'@@,'#F','Zmiana rodzaju operacji - wewnętrzna/zewnętrzna'@
      ,_akcja,,,,,,'R');

   .TAB.win_act(.WERT,,'Formuła','--X','#F');

   {? exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTE')
   || _akcja:=\"exec('action_pattern_4oper','tech_oper')\";
      .TAB.win_act(.WERT,0,'Formuła','Utwórz wzorzec'@@,'#F','Tworzenie wzorca operacji'@,_akcja,,,,,,'U')
   ?};

   _akcja:=$(\"{? ~exec('insert_tbox_o','tech_arch') || \"+!Objt+\".reload() ?}\");
   .TAB.win_act(.WERT,0,'Formuła','Wstaw wzorzec'@@,'#F','Wstawianie wzorca operacji'@,_akcja,,,,,,'W');
   .TAB.win_act(.WERT,1,'Formuła','Wstaw wzorzec'@@,'#F','Wstawianie wzorca operacji'@,_akcja,,,,,,'W');

   _akcja:=$(\"{? ~exec('insert_from_tktl','tech_arch',0) || \"+!Objt+\".reload() ?}\");
   .TAB.win_act(.WERT,0,'Formuła','Wstaw z t&echnologii'@@,'#F','Kopiuje operacje z innej technologii'@
      ,_akcja,,,,,,'E');
   .TAB.win_act(.WERT,1,'Formuła','Wstaw z t&echnologii'@@,'#F','Kopiuje operacje z innej technologii'@
      ,_akcja,,,,,,'E');

   .TAB.win_act(.WERT,,'Formuła','--Ć','#F');

   _akcja:=\"exec('zmiany','#syslog',TOPER)\";
   .TAB.win_act(.WERT,0,'Formuła','Zmian&y'@@,'#F','Historia modyfikacji'@,_akcja,,,,,,'Y');

   _akcja:=\"
      TOPER.cntx_psh();
      TOPER.index('TPZ');
      TOPER.prefix('T',TOPER.UNROP);
      {? TOPER.first()
      || exec('zmiany','#syslog',TOPER)
      || FUN.info('Brak operacji Tpz.'@)
      ?};
      TOPER.cntx_pop()
   \";
   .TAB.win_act(.WERT,0,'Formuła','Zmiany &Tpz'@@,'#F','Historia modyfikacji'@,_akcja,,,,,,'T');
   ~~
");

:: Takie czary, żeby zadziałały tłumaczenia przycisków poniżej (teksty MUSZĄ być identyczne)
'N-P-U'@;'Surowce'@;'Wszystkie surowce'@;'Operacje'@;'Generuj'@;
'Zam&ienniki'@;'Atrybuty'@;'Doku&mentacja'@;'Dia&gram'@;'Zakończ'@;

set_virt(Objt,'f_buttons',"
:: Okno dla NPU do operacji
   {? .ZAKL=5
   || .BTN:=obj_new('NPU');

      _btn:=.TAB.win_btn(.WERT,'text=%1,panel=right,align=begin'['N-P-U'@],'menu:EN',,,,,,'noempty');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Narzędzia/Przyrządy/Urządzenia - do wykonania operacji'@);
      .BTN.NPU:=_btn

:: Okno dla surowców do operacji
   |? .ZAKL=4
   || .BTN:=obj_new('SUROWCE','SUROWCE_ALL');

      _btn:=.TAB.win_btn(.WERT,'text=%1,panel=right,align=begin'['Surowce'@],'menu:ES',,,,,,'noempty');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Surowce potrzebne do wykonania operacji'@);
      .TAB.btn_sopt(.WERT,_btn,'default=1');
      .BTN.SUROWCE:=_btn;

      _btn:=.TAB.win_btn(.WERT,'text=%1,panel=bottom,align=begin'['Wszystkie surowce'@],'menu:EW',,,,,,'noempty');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Surowce potrzebne do wykonania produktu'@);
      .BTN.SUROWCE_ALL:=_btn

:: Okno dla technologii zlecenia (zakładka w widoku zleceń)
   |? .ZAKL=3
   || .BTN:=obj_new('OPERACJE','GENERUJ');

      .BTN.OPERACJE:=.TAB.win_btn(.WERT,'text='+'Operacje'@,'menu:O',,,,,,'');
      .TAB.btn_opt(.BTN.OPERACJE,'tooltip='+'Panel operacji dla zlecenia'@);

      .BTN.GENERUJ:=.TAB.win_btn(.WERT,'text='+'Generuj'@,'menu:G',,,,,,'empty');
      .TAB.btn_opt(.BTN.GENERUJ,'tooltip='+'Wygenerowanie technologii zlecenia'@)

:: Okno dla operacji karty technologicznej/ technologii zlecenia
   |? .ZAKL=1
   || .BTN:=obj_new('TECH_ZAM','SUROWCE','NPU','ATRYBUTY','DOK','DIAGRAM','DALEJ');

      _btn:=.TAB.win_btn(.WERT,'text=%1,panel=right,align=begin'['Zam&ienniki'@],'menu:EZ',,,,,,'noempty');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Selekcja zamienników stanowiska przypisanego do operacji'@);
      .BTN.TECH_ZAM:=_btn;

      _btn:=.TAB.win_btn(.WERT,'text=%1,panel=right,align=begin'['Surowce'@],'menu:ES',,,,,,'noempty');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Surowce potrzebne do wykonania operacji'@);
      .BTN.SUROWCE:=_btn;

      _btn:=.TAB.win_btn(.WERT,'text=%1,panel=right,align=begin'['N-P-U'@],'menu:EN',,,,,,'noempty');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Narzędzia/Przyrządy/Urządzenia - do wykonania operacji'@);
      .BTN.NPU:=_btn;

      _name:='text=%1,panel=right,align=begin'['Atrybuty'@];
      _btn:=.TAB.win_btn(.WERT,_name,'menu:EA',,,,,,'noempty');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Dodatkowe dane dotyczące operacji'@);
      .BTN.ATRYBUTY:=_btn;

      _name:='text=%1,panel=right,align=begin'['Doku&mentacja'@];
      _btn:=.TAB.win_btn(.WERT,_name,'menu:ED',,,,,,'noempty');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Dokumentacja elektroniczna przechowywana w bazie'@);
      .BTN.DOK:=_btn;

      _name:='text=%1,panel=bottom,align=begin'['Dia&gram'@];
      _btn:=.TAB.win_btn(.WERT,_name,'menu:FD',,,,,,'noempty');
      .BTN.DIAGRAM:=_btn;

      _name:='text=%1,panel=bottom,align=end'['Zakończ'@];
      _btn:=.TAB.win_btn(.WERT,_name,'menu:Z');
      .TAB.btn_sopt(.WERT,_btn,'tooltip='+'Zakończenie rejestracji operacji'@);
      .BTN.DALEJ:=_btn

:: Okno dla technologii wzorcowej (zakładka w widoku zleceń)
   |? .ZAKL=0
   || .BTN:=obj_new('OPERACJE');

      _btn:=TTREE.TAB.win_btn(TTREE.WERT,'text='+'Operacje'@,'menu:O',,,,,,'noempty');
      .TAB.btn_opt(_btn,'tooltip='+'Panel operacji dla zlecenia'@);
      .BTN.OPERACJE:=_btn

   ?};
   ~~
");

set_virt(Objt,'load',"
  .TAB.erase();
  exec('next_lev','tech_oper',.,0,0);
  ~~
");

Objt.USED:=_used;
Objt.ZAKL:=_zakl;
Objt.init();
Objt.update();

:: Renumeracja i dołączanie rekordów za pomocą d'n'd
_dnd:=~_used;
{? ~(exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTO') & _torw='T' |
     exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTO') & _torw='W' |
     exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC') & _torw='Z')
|| _dnd:=0
?};

{? (_torw='Z' | (TKTL.STAT_N='T' & TKTL.STAT_O='N' & ~_used)) & _dnd
||
   Objt.TAB.dnd_sel(Objt.WERT,,'records.#tree_wert',$("params_exec('dnd_renum','tech_oper',"+!Objt+")"));

   Objt.TAB.dnd_sel(Objt.WERT,,'records.TTOPER',"
::    Formuła na dodanie nowej operacji wewnętrznej prostej (analogicznie do 'Dołącz' z podpowiedzią pola 'Operacja')
      params_set(params_get());
      _records:=dnd_info('dropped_records');
      {? _records.size()>1
      || FUN.info('Dołączać można tylko pojedynczą pozycję.'@)
      || TTOPER.clear();
         TTOPER.seek(_records.REF,);
         {? Objt.TAB.seek(dnd_info('dest_record')) || TOPER.seek(Objt.TAB.REF,) ?};
         Objt.f_add(2,0,TTOPER,TTOPER.ref(),dnd_info('dest_record'))
      ?}

   ");
   Objt.TAB.dnd_sel(Objt.WERT,,'records.TTOUT',"
::    Formuła na dodanie nowej operacji zewnętrznej prostej (analogicznie do 'Dołącz' z podpowiedzią pola 'Operacja')
      params_set(params_get());
      _records:=dnd_info('dropped_records');
      {? _records.size()>1
      || FUN.info('Dołączać można tylko pojedynczą pozycję.'@)
      || TTOUT.clear();
         TTOUT.seek(_records.REF,);
         {? Objt.TAB.seek(dnd_info('dest_record')) || TOPER.seek(Objt.TAB.REF,) ?};
         Objt.f_add(2,0,TTOUT,TTOUT.ref(),dnd_info('dest_record'))
      ?}
   ");
   Objt.TAB.dnd_sel(Objt.WERT,,'records.fun_wer_t',"
::    Formuła na dodanie nowej operacji złożonej (analogicznie do 'Dołącz')
      params_set(params_get());
      _records:=dnd_info('dropped_records');
      {? _records.size()>1
      || FUN.info('Dołączać można tylko pojedynczą pozycję.'@)
      || {? Objt.TAB.seek(dnd_info('dest_record')) || TOPER.seek(Objt.TAB.REF,) ?};
         Objt.f_add(2,0,TOPER,null(),dnd_info('dest_record'))
      ?}
   ")

|| Objt.TAB.dnd_sel(Objt.WERT,,'records.#tree_wert',"");
   Objt.TAB.dnd_sel(Objt.WERT,,'records.TTOPER',"");
   Objt.TAB.dnd_sel(Objt.WERT,,'records.TTOUT',"");
   Objt.TAB.dnd_sel(Objt.WERT,,'records.fun_wer_t',"")
?};

:: widok wołany z menu technologii, z menu zlecenia
{? _zakl=1 | _zakl=4 | _zakl=5 | _zakl=7
||
   TTOPER.clear(); TTOPER.first();
   TTOUT.clear(); TTOUT.first();

   {? _zakl=7
   || _result:=Objt.select()
   || Objt.select()
   ?};
::   VAR_DEL.delete('TREE','__Fun','Objt');
::   {? var_pres('TTREE')>0
::   || Objt:=TTREE
::   ?};

   TWRKZBR.actions('WER','');

   PFAZ.f_clear();

   TKTL.cntx_pop()
|? _zakl=6
|| TKTL.cntx_pop()
?};

VAR_DEL.delete('Objt');
{? _locked || exec('tktl_unlock','tech_common',,'O') ?};
_result


\zm_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Zmiana - usunięcie (dezaktualizacja)/przywrócenie operacji na zatwierdzonej technologii
::   WE: _a - _a - usuwanie-czy z dialogiem (1/0), przywracanie-czy z dialogiem (3/2)
::      [_b] - data
::      [_c] - przyczyna TTXCAUSE.ref()
::      [_d] - znacznik czasowy
::  OLD: \zm_usun/tex_ope1.fml
::----------------------------------------------------------------------------------------------------------------------
:: czy tryb przywracania
_przywr:=_a>1;
:: czy z oknem dialogowym
_bDialog:=_a-2*_przywr;

{? var_pres('_b')=type_of(date()) || _dData:=_b || _dData:=date() ?};
{? var_pres('_c')=type_of(null()) || _rCause:=_c || _rCause:=null() ?};
{? var_pres('_d')=type_of(0) || _tmstamp:=_d || _tmstamp:=TOPER.tm_stamp() ?};
{? var_pres('_e')=type_of(0) || _oldstamp:=_e || _oldstamp:=TOPER.MOD_TIME ?};

{? _przywr
|| {? TOPER.ACT='T'
   || {? _bDialog || FUN.emsg('Element aktywny — nie można go przywracać.'@) ?};
      return()
   ?}
|| {? TOPER.ACT='N'
   || {? _bDialog || FUN.emsg('Element nieaktywny — nie można modyfikować.'@) ?};
      return()
   ?}
?};
VAR.KOR:=1;
_win_edit:=TOPER.win_edit('?');

{? _przywr
|| TOPER.win_edit('PRZYWR')
|| TOPER.win_edit('USUN')
?};

{? ~_bDialog
      |
   FUN.ask(
      'Modyfikacja zatwierdzonej karty technologicznej.'@+'\n\n'+
      {? _przywr || 'Czy na pewno przywrócić element?'@ || 'Czy na pewno usunąć (dezaktywować) element?'@ ?}
   )
|| TTXCAUSE.win_sel('WER');
   TTXCAUSE.actions('WER','','T:d');
   TOPER.OD:=_dData;
   {? _rCause<>null()
   || TOPER.CAUSE:=_rCause
   || TOPER.CAUSE:=exec('ttxcause_default','tech_common')
   ?};
   {? ~_bDialog | TOPER.edit("
         {? TOPER.CAUSE=null()
         || FUN.info('Podaj przyczynę zmiany.'@);
            'CAUSE'
         || ''
         ?}
        ")
   ||
      {? _bDialog | ~_przywr | TOPER.MOD_TIME=_oldstamp
      || _dData:=TOPER.OD;
         TOPER.ACT:={? _przywr || 'T' || 'N' ?};
         TOPER.USER:=OPERATOR.USER;
         TOPER.MOD_TIME:=_tmstamp;
         {? TOPER.put()
         || _rCause:=TTXCAUSE.ref();
            TMAT.index('NNL');
            TMAT.prefix(VAR.A_KTL,TOPER.ref());
            {? TMAT.first()
            || {!
               |?
                  exec('zm_m_usun','tech_mater',{? _przywr || 2 || 0 ?},_dData,_rCause,_tmstamp,_oldstamp);
                  TMAT.next()
               !}
            ?};
            TACTTLS.index('KNROP');
            TACTTLS.prefix(VAR.A_KTL,TOPER.ref());
            {? TACTTLS.first()
            || {!
               |?
                  exec('zm_usun','tech_tool',{? _przywr || 2 || 0 ?},_dData,_rCause,_tmstamp,_oldstamp);
                  TACTTLS.next()
               !}
            ?};
            _unrop:=TOPER.UNROP;
            TOPER.cntx_psh();
            TOPER.index('TPZ');
            TOPER.prefix('T',_unrop);
            {? TOPER.first()
            || TOPER.ACT:={? _przywr || 'T' || 'N' ?};
               TOPER.OD:=_dData;
               {? _rCause<>null()
               || TOPER.CAUSE:=_rCause
               || TOPER.CAUSE:=exec('ttxcause_default','tech_common')
               ?};
               TOPER.USER:=OPERATOR.USER;
               TOPER.MOD_TIME:=_tmstamp;
               TOPER.put()
            ?};
            TOPER.cntx_pop();
            TOPER.cntx_psh();
            TOPER.index('NNN');
            TOPER.prefix(VAR.A_KTL,_unrop);
            {? TOPER.first()
            || {!
               |? exec('zm_usun','tech_oper',{? _przywr || 2 || 0 ?},_dData,_rCause,_tmstamp,_oldstamp);
                  TOPER.next()
               !}
            ?};
            TOPER.cntx_pop()
         ?};
         {? _bDialog
         || {? FUN.choice(
                  'Należy zmodyfikować strukturę operacji.\nStrukturę można wygenerować automatycznie.'@,,'Generuj'@
               )
            || exec('er_tab','tech_oper',VAR.A_KTL);
               exec('gen_pow','tech_oper',VAR.A_KTL);
               exec('po_nast','tech_oper')
            ?}
         ?};
:: NUCO - uruchomienie aktualizacji karty technologicznej projektowej po zmianie w kartotece surowca
         {? TOPER.NRK().TYP().TYP='PRJ'  & TOPER.NRK().TORW='T' & TOPER.NRK().STAN='T' & TOPER.NRK().ARCH='N'
         || exec('akt_px','qtpp',TOPER.NRK,'TOPER',$TOPER.ref())
         |? TOPER.NRK().TYP().TYP='PRD'  & TOPER.NRK().TORW='T' & TOPER.NRK().STAN='T' & TOPER.NRK().ARCH='N'
         || exec('akt_px_pro','qtpp',TOPER.NRK,'TOPER',$TOPER.ref())
         ?}
      ?}
   ?}
?};
TOPER.win_edit(_win_edit);
VAR.KOR:=0;
~~


\toper_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Akcja Popraw dla operacji technologicznej
::   WE: [_a] - miejsce wywołania: [0] - wewnątrz obiektu - struktrura drzewiasta
::                                  1  - okno wertowania ALL - lista wszystkich operacji technologicznych
::       [_b] - obiekt, wewnątrz którego uruchamiana jest formuła (_gdy _a=0)
::   WY: 0/1 - czy zaktualizowano rekord - wynik TOPER.put()
::----------------------------------------------------------------------------------------------------------------------
_where:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_obj:={? var_pres('_b')>100 & _where=0 || _b || ~~ ?};
{? VAR.GRP_MOD='' ||  VAR.GRP_MOD:='N' ?};
_p500369:=exec('get','#params',500369,type_of(''));
_res:=0;
{? TOPER.ACT='N'
|| _msg:='Element nieaktywny — nie można modyfikować.'@;
   {? VAR.GRUPA='T'
   || KOMM.add(exec('grp_mod_msg','tech_oper')+_msg)
   || FUN.info(_msg)
   ?};
   ~~
|? params_exec('can_modify','tech_oper')=0
|| _msg:='Stan technologii lub operacji nie pozwala na modyfikację elementu.'@;
   {? VAR.GRUPA='T'
   || KOMM.add(exec('grp_mod_msg','tech_oper')+_msg)
   || FUN.info(_msg)
   ?};
   _ok:=0
|? TKTL.STAT_O='N'
||
   _ok:={? VAR.ZM_ROPER='T'
        ||
::         Akcja Zmiana rodzaju operacji - ustalenie wartości pól dla TOPER
           exec('wew_change','tech_oper')
        || 1
        ?};
   _plnh_bef:=TOPER.PLNH;
   _cena_bef:=TOPER.CENA;
   _ref:={? _where=0 || _obj.TAB.REF || TOPER.ref() ?};
   VAR.A_WYD:={? TOPER.PFAZ<>null() || TOPER.PFAZ().WYD || TKTL.JORG ?};
   exec('toper_be','tech_oper');
   exec('efld_opt','tech_oper',TOPER);
   {? _ok>0 & TOPER.memo_get(,'OPISMEMO') & (VAR.GRP_MOD='T' | TOPER.edit("exec('topearec','tech_oper')"))
   || _ok:=1;
      {? VAR.GRP_MOD='T'
      || _ok:=exec('toper_modify_frombfr','tech_oper',__BUFF_GRP)
      ?};
      {? _ok>0 & TOPER.put()
      || _res:=1;
         TOPER.memo_put(,'OPISMEMO');
         exec('pfazred','tech_common');

         NASTOPER.cntx_psh();
         NASTOPER.index('NASTOP');
         NASTOPER.prefix('T',TOPER.ref());
         {? NASTOPER.first()
         || _sciezka:=exec('get_oper_nr','tech_oper',TOPER.UNROP);
            {!
            |? NASTOPER.SCIEZKA:=_sciezka;
               NASTOPER.put();
               NASTOPER.next()
            !}
         ?};
         NASTOPER.cntx_pop();

         {? TOPER.WEW='T' & _plnh_bef<>TOPER.PLNH
         || exec('warn_toper_plnh','tech_oper')
         ?};

         {? TOPER.WEW='N' & _cena_bef<>TOPER.CENA
         || exec('warn_toper_cena','tech_oper')
         ?};
::       Aktualizacja przepisu planistycznego technologii (jeżeli istnieje)
         {? _p500369='T' & TOPER.NRK<>null() & exec('get_tex_tktl','px_tex',$TOPER.NRK)
         || exec('tktl_tex_update','px_tex',TOPER.NRK,0,1,exec('get','#params',500367)='T',,1,1)
         ?};
         {? _where=0
         || _obj.TAB.cntx_psh();
            _obj.replica();
            _obj.load();
            _obj.update();
            _obj.TAB.clear();
            {? _obj.TAB.first()
            || {!
               |?
                 _obj.TAB1.blank();
                 _obj.TAB1.REF:=_obj.TAB.REF;
                 {? _obj.TAB1.find_rec()
                 || _obj.TAB.HIDDEN:=_obj.TAB1.HIDDEN;
                    _obj.TAB.EXPANDED:=_obj.TAB1.EXPANDED;
                    _obj.tab_txt();
                    _obj.TAB.put()
                 ?};
                 _obj.TAB.next()
                !}
            ?};
            _obj.TAB.cntx_pop();
            _obj.TAB.blank();
            _obj.TAB.REF:=_ref;
            _obj.TAB.find_rec()
         ?}
      ?}
   ?};
   ~~
||
   TOPER.hdr_edit(' — '+'MODYFIKACJA ZATWIERDZONEJ KARTY'@);
   VAR.KOR:=1;
   _plnh_bef:=TOPER.PLNH;
   _cena_bef:=TOPER.CENA;
   _ok:={? VAR.ZM_ROPER='T'
        ||
::         Akcja Zmiana rodzaju operacji - ustalenie wartości pól dla TOPER
           exec('wew_change','tech_oper')
        || 1
        ?};
   VAR.A_WYD:={? TOPER.PFAZ<>null() || TOPER.PFAZ().WYD || TKTL.JORG ?};
   exec('toper_be','tech_oper');
   _ref:={? _where=0 || _obj.TAB.REF || TOPER.ref() ?};
   {? VAR.GRP_MOD<>'T'
   || _buffer:=exec('buffer','tech_oper');
      _buffer.get();
      TOPER.OD:=date();
      TOPER.USER:=OPERATOR.USER;
      TOPER.CAUSE:=exec('ttxcause_default','tech_common');
      TOPER.MOD_TIME:=TOPER.tm_stamp();
      _POLA:=exec('pola_tpz','tech_oper');
      exec('ust_pola_tpz','tech_oper',_POLA);
      params_set('buffer',_buffer,'POLA',_POLA)
   ?};
   exec('efld_opt','tech_oper',TOPER);
   {? _ok>0 & TOPER.memo_get(,'OPISMEMO') & (VAR.GRP_MOD='T' |
      TOPER.edit("
         _buffer:=exec('buffer','tech_oper');
         _buffer.get();
         {? exec('compare','#table',_buffer,params_get().buffer,1,'OD','USER','CAUSE','MOD_TIME')
            & exec('comp_tpz','tech_oper',params_get().POLA)
         || FUN.info('Nic nie zostało zmienione.\nNie można zatwierdzić zmian.'@);
            0
         || exec('topearec','tech_oper')
         ?}
      "))
   || _ok:=1;
      {? VAR.GRP_MOD='T'
      || _ok:=exec('toper_modify_frombfr','tech_oper',__BUFF_GRP)
      ?};
      {? _ok>0
      || {? TOPER.put()
         || _res:=1;
            TOPER.memo_put(,'OPISMEMO');
            exec('pfazred','tech_common');
:: NUCO - uruchomienie aktualizacji karty technologicznej projektowej po zmianie w kartotece surowca
         {? (TOPER.NRK().TYP().TYP='PRJ'|TOPER.NRK().TYP().TYP='PRD') & VAR.KOR & TOPER.NRK().TORW='T' & TOPER.NRK().STAN='T' & TOPER.NRK().ARCH='N'
         ||
:: NUCO - dodanie aktualizacji struktury operacji
            exec('er_tab','tech_oper',TOPER.NRK);
            exec('gen_pow','tech_oper',TOPER.NRK);
            exec('akt_px','qtpp',TOPER.NRK,'TOPER', $TOPER.ref())
         ?};
:: KONIEC ZMIANY
            NASTOPER.cntx_psh();
            NASTOPER.index('NASTOP');
            NASTOPER.prefix('T',TOPER.ref());
            {? NASTOPER.first()
            || _sciezka:=exec('get_oper_nr','tech_oper',TOPER.UNROP);
               {!
               |? NASTOPER.SCIEZKA:=_sciezka;
                  NASTOPER.put();
                  NASTOPER.next()
               !}
            ?};
            NASTOPER.cntx_pop()
         ?};

         {? TOPER.WEW='T' & _plnh_bef<>TOPER.PLNH
         || exec('warn_toper_plnh','tech_oper')
         ?};

         {? TOPER.WEW='N' & _cena_bef<>TOPER.CENA
         || exec('warn_toper_cena','tech_oper')
         ?};
::       Aktualizacja przepisu planistycznego technologii (jeżeli istnieje)
         {? _p500369='T' & TOPER.NRK<>null() & exec('get_tex_tktl','px_tex',$TOPER.NRK)
         || exec('tktl_tex_update','px_tex',TOPER.NRK,0,1,exec('get','#params',500367)='T',,1,1)
         ?};
         {? _where=0
         || _obj.TAB.cntx_psh();
            _obj.replica();
            _obj.load();
            _obj.update();
            _obj.TAB.clear();
            {? _obj.TAB.first()
            || {!
               |?
                  _obj.TAB1.blank();
                  _obj.TAB1.REF:=_obj.TAB.REF;
                  {? _obj.TAB1.find_rec()
                  || _obj.TAB.HIDDEN:=_obj.TAB1.HIDDEN;
                     _obj.TAB.EXPANDED:=_obj.TAB1.EXPANDED;
                     _obj.tab_txt();
                     _obj.TAB.put()
                  ?};
                  _obj.TAB.next()
               !}
            ?};
            _obj.TAB.cntx_pop();
            _obj.TAB.blank();
            _obj.TAB.REF:=_ref;
            _obj.TAB.find_rec()
         ?}
      ?}
   ?};
   TOPER.hdr_edit();
   VAR.KOR:=0;
   ~~
?};
_res


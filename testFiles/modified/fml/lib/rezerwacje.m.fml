:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: rezerwacje.fml
:: Utworzony: 2015/04/03 [17.00]
:: Autor: [rr]
::======================================================================================================================
:: Zawartosc: formuły wspólne do obsługi rezerwacji zamówień, rezerwacji tymczasowych, ilości w wydaniu
::======================================================================================================================


\rek_rez1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: wlaczenie/wylaczenie akcji
::  OLD: \rek_rez1/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
_msk:=SC.name()-2;
BEER.IL_DNI:=REZ.DODT-REZ.ODDT;
{? REZ.SC<>'' & 'dokma'=(5+REZ.SC)
|| SC.cntx_psh();
   SC.use(_msk+(form(8+REZ.SC)+2));
   SC.index('SRDK');
   SC.prefix(REZ.SC,REZ.SC);
   {? SC.first()
   || BEER.DOSTAWA:=SC.D;
      exec('sc_info','magazyn_stan');
:: NUCO - dodatkowe napelnienie zmiennej atrybutami dostawy
      DK_C.cntx_psh;
      {? SC.DK_C().M_ATR<>null & DISP.DOST<>''
      || {! _i:=1..10
         |! _naz:=($('SC.DK_C().M_ATR().SL_'+form(_i,-2,,'99')+'().NA'))();
            _war:=($('SC.DK_C().WAR'+form(_i,-2,,'99')))();
            {? _naz<>'' & _war<>'' || DISP.DOST+=' >'+_naz+': '+_war ?}
         !}
      ?};
      DK_C.cntx_pop
   ?};
   SC.cntx_pop()
|| BEER.DOSTAWA:=date(0,0,0);
   DISP.DOST:=''
?};
''


\is_rp_rez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: sprawdza RP_REZ
::   WE: _a - ZL.ref()
::       _b - M.ref()
::   WY: 'N' (domyślna wartość) lub 'T'
::----------------------------------------------------------------------------------------------------------------------
 _res:='N';
  _zl:=_a;
 _mat:=exec('FindAndGet','#table',M,_b,,,null());
_nrnzl:=exec('FindAndGet','#table',ZL,_zl,,"NRNZL",0);

{? _mat<>null() & _nrnzl<>0
|| _pod:=0;
   ZL.cntx_psh();
   ZL.index('NRNZL');
   ZL.prefix(_nrnzl);
   {? ZL.first() & ZL.find_tab('first','KTM',,'=',_mat)
   || {!
      |?
:: NUCO - uwzględniono również zlecenia produkcyjne proste ze zkeceń niezależnych
         _pod:=~(ZL.RODZAJ='N' | ZL.RODZAJ='P' | ZL.RP<>'T');
         ~_pod & ZL.find_tab('next','KTM',,'=',_mat)
      !}
   ?};
   ZL.cntx_pop();
   {? _pod
   || _res:=exec('FindAndGet','#table',ZL,_zl,,"{? RP_REZ='N' || TYP().RP_REZ |? RP_REZ='Z' || 'T' || 'N' ?}",'N')
   ?}
?};
_res



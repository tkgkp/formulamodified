:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zamdst_nag.fml
:: Utworzony: 09.09.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Zamówienia dostaw - nagłówek
::======================================================================================================================


\be_zd_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed edycja dla ZD_NAG.KH
::   WY: 1/0
::  OLD: \be_zd_kh/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZD_NAG.PROJEKTY
   & ZD_NAG.KH
   & ZD_NAG.T().PROJZKH='T'
|| return(0)
?};

:: NUCO - poprawianie kontrahenta mimo uzupełnionych dat potwierdzeń dostaw
{? exec('czy_ptw','zamdst_ptw')=0
|| FUN.ask('Dla pozycji dokumentu wprowadzono już daty potwierdzenia dostaw.\n Czy napewno chcesz zmienić kontrahenta ?')
|| 1
?}


\zdnag_pop_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Obsługa popraw po zakończeniu edycji (ZD_NAG.edit)
::   WE: [_a] - 0 standard, 1 - nie zwracaj uwagi na stan nagłówka - popraw
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::NUCO - Poprawianie zaakceptowanego nagłówka
_force:={? _<1 || 0 || _a ?};

_var_zdnagpop:=params_get().var_zdnagpop;
_projekty:=_var_zdnagpop.PROJEKTY;
_kh:=_var_zdnagpop.KH;
_wal:=_var_zdnagpop.WAL;
_krs:=_var_zdnagpop.KRS;
_kh_msc:=_var_zdnagpop.KH_MSC;
_po_first:=_var_zdnagpop.PO_FIRST;

{? _po_first=1
:: Dodyczy wywołania dla Dołącz - wtedy jako wartości przd ustawia zmienne z nagłówka
|| _projekty:=ZD_NAG.PROJEKTY;
   _kh:=ZD_NAG.KH;
   _war:=ZD_NAG.WAL;
   _krs:=ZD_NAG.KRS;
   _kh_msc:=ZD_NAG.KH_MSC
?};

{? ZD_NAG.STAN='N' | _force
||
   ZD_NAG.BL_STAT:=exec('zdnag_blstat_bl','zamdst_nag');
   {? ZAKR.KH<>null & ZD_NAG.KH<>ZAKR.KH
   || FUN.info('Aktualnie zredagowane zamówienie nie jest dostępne w ustawionym zakresie widoku zamówień.'@)
   |? ZAKR.MAG<>null & ZD_NAG.MG<>ZAKR.MAG
   || FUN.info('Aktualnie zredagowane zamówienie nie jest dostępne w ustawionym zakresie widoku zamówień.'@)
   ?};
   ZD_NAG.cntx_psh();
   ZD_NAG.clear();

   {? ZD_NAG.put()
    & ZD_NAG.memo_put() & (ZD_NAG.KH<>_kh | ZD_NAG.WAL<>_wal | ZD_NAG.KRS<>BEER.KRS
                        | ZD_NAG.PROJEKTY<>_projekty
                        | ZD_NAG.KH_MSC<>_kh_msc)
   ||

      ZD_POZ.index('POZ');
      ZD_POZ.prefix(ZD_NAG.ref);
      {? ZD_POZ.first()
      ||
         {!
         |?
            {? ZD_NAG.KH<>_kh || ZD_POZ.KH:=ZD_NAG.KH ?};
            {? ZD_NAG.KH_MSC<>_kh_msc || ZD_POZ.KH_MSC:=ZD_NAG.KH_MSC ?};
            {? ZD_NAG.WAL<>_wal
            ||
               {? ZD_NAG.WAL<>ZD_POZ.WAL || ZD_POZ.CWAL:=0; ZD_POZ.CENA2:=0 ?};
               ZD_POZ.WAL:=ZD_NAG.WAL
            ?};
            {? ZD_NAG.KRS<>BEER.KRS
            ||
               ZD_POZ.KRS:=ZD_NAG.KRS;
               {? ZD_POZ.KRS>0 & ZD_POZ.CWAL=0
               ||
                  ZD_POZ.CWAL:=(ZD_POZ.CENA/ZD_POZ.KRS)$ZD_POZ.M().DOKL_Z;
                  ZD_POZ.CENA2=0
               ?};
               exec('zdp_obl','zamdst_poz')
            ?};
            {? ZD_NAG.PROJEKTY || ZD_POZ.PROJEKTY:=ZD_NAG.PROJEKTY ?};

            ZD_POZ.put();
            ZD_POZ.next
         !};
         exec('war_zam','zamdst_wspolne',ZD_NAG.ref)
      ?}
   ?};

   ZD_NAG.cntx_pop()

|? ZD_NAG.STAN='A' | ZD_NAG.STAN='C'
||
   ZD_NAG.RR:=ZD_NAG.DTPREAL~1;
   ZD_NAG.RM:=ZD_NAG.DTPREAL~2;
   ZD_NAG.RT:=exec('NumberOfWeek','#datetime',ZD_NAG.DTPREAL);
   ZD_NAG.put()
?}



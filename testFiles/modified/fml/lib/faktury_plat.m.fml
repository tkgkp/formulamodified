:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: faktury_plat.m.fml
:: Utworzony: 10-3-2021
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Obsługa płatności na dokumentach sprzedaży
::======================================================================================================================


\term_plat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: formula wyznacza termin platnosci
::   WE: [_a] - akronim pola
::       [_b] - czy wyswietlac msg z informacja (T/N) - domyslnie T
::       _c - FAKS.ref lub ZK_N.ref lub ZD_NAG.ref
::   WY: _tz - termin platosci
::  OLD: \term_plat/plat.fml
::----------------------------------------------------------------------------------------------------------------------
{? FZ.PL=null() || return(date(0,0,0)) ?};

_Tab:=ref_tab(_c);
_czyFaks:=_Tab=FAKS;
_czyZdNag:=_Tab=ZD_NAG;

{? _>=1 || {? type_of(_a)<>2 || _a:=''?} || _a:='' ?};
{? _>=2 || {? type_of(_b)<>2 || _b:='T'?} || _b:='T' ?};
{? _czyFaks & FAKS.T().PAR='T' || _b:='N' ?};

:: NUCO - zmiana
_dw:=
   {? _czyFaks
   || FAKS.DW
   |? _czyZdNag
   || ZD_NAG.DTPREAL
   || {? ZK_N.DT<>date(0,0,0)
      || ZK_N.DT
      || ZK_N.DP
      ?}
   ?};
_akc:={? _czyFaks || FAKS.AKC |? _czyZdNag || {? 'NAC'*ZD_NAG.STAN>0  || 'N' || 'T' ?} || ZK_N.AKC ?};
_fld_akr:={? _czyFaks || 'DW' |? _czyZdNag || 'DTPREAL' || 'DP' ?};

:: _tz - dotychczasowy termin zapłaty
_tz:=FZ.TERMPLAT;
_ldni:=#exec('FindInSet','#table','ZR_SLO','SLO_NR',2,FZ.PL,"ZR_SLO.WAR",,,'');
:: _tn - nowy termin zapłaty
_tn:=
   {? _a='Y'
   || _tz
   || {? _dw=date(0,0,0) || date(ST.AR,ST.AM,1) || _dw ?}+_ldni
   ?};
::{? _czyFaks & _tn<FAKS.D || _tn:=FAKS.D ?};

{? exec('get','#params',300202,2)='T'
||
:: przesunięcie terminu płatności zgodnie z kalendarzem
   {!
   |? exec('spr_kal','faktury_plat',_tn,~('XY'*_a))=1
   |!
      _tn+=1
   !}
?};

_txt:=
   {? {? _a<>'' || _a || cur_afld() ?}=_fld_akr
   || {? _czyZdNag
      || 'Zmieniono datę realizacji'@
      || 'Zmieniono datę wystawienia'@
      ?}
   || 'Zmieniono liczbę dni płatności na '@+form(_ldni,,0,'99')
   ?};
{? #_dw<>0 & _akc<>'T'
   &  (
      #FZ.TERMPLAT=0
      |  (
         FZ.TERMPLAT<>_tn
         &
         {? _a='X' || 1
         |? _b='T' || FUN.ask(_txt+'.\nCzy zaktualizować termin płatności?'@)
         || 1
         ?}
         )
      )
||
:: aktualizacja terminu zapłaty
   _tz:=_tn
?};
_tz

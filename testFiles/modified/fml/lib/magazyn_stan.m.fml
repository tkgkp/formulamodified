:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: magazyn_stan.fml
:: Utworzony: 25.06.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły obliczające stan magazynowy, stan dostaw
::======================================================================================================================


\obl_rsc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: rezerwacje danej dostawy
::   WE:  _a  - ref dostawy DK
::        _b  - name dostawy DK
::        _c  - ref magazynu
::        _d  - ref materialu
::       [_e] - czy obliczac rezerwacje z faktur 0-nie(domyslnie) 1-tak
::       [_f] - ref SQL ZK_N do pominiecia
::   WY: ilosc zarezerwowana
::  OLD: \obl_rsc/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};

{? _=0
|| _rep:=SC.RDK;  _namp:=SC.NDK;
   _mag:=SC.MAG;  _mat:=SC.M
|| _rep:=_a;      _namp:=_b;
   _mag:=_c;      _mat:=_d
?};
_ilr:=0; _ild:=0;
_ilr2:=0; _ild2:=0;
_jmg:=exec('FindAndGet','#table',M,_mat,,"J2<>null()",0);

HELP.DIL2:=0;
_scsql:=exec('MaskInt2RefSql','#convert',_namp,_rep);
_scean:={? _scsql<>'' || exec('FindInSet','#table','SC','SRDK',_scsql,_scsql,"SC.SCEAN",,,'') || '' ?};
REZ.cntx_psh();
REZ.index('SC');
REZ.prefix(_mat,_scsql,_scsql);
{? REZ.first()
|| {!
   |? {? REZ.TYP<>'W'
      || {? _e & REZ.TYP='F'
         || _ilr+=REZ.ILR;
            {? _jmg || _ilr2+=REZ.IL2 ?}
         |? REZ.TYP<>'F' & (_f='' | _f<>($REZ.ZK_N))
         || _ilr+=REZ.ILR;
            _ild+=REZ.ILR;
            {? _jmg
            || _ilr2+=REZ.IL2;
               _ild2+=REZ.IL2
            ?}
         ?}
      ?};
      REZ.next()
   !}
?};
REZ.cntx_pop();
_kpl:=0;
{? _scean<>''
|| EANN.cntx_psh();
   EANP.cntx_psh();
   EANP.index('SCEAN');
   EANP.prefix(_scean,_scean,'Z');
   {? EANP.first()
   || {!
      |?
:: NUCO - dodatkowe zabezpieczenie jeśli EANP.LOKZ jest null nie sprawdzamy magazynu - powoduje błąd
      {? EANP.EANN<>null & EANP.EANN().AKT='T' & EANP.EANN().STAN<>'!' & EANP.LOKZ<>null() & EANP.LOKZ().MG=_mag
         || _ilrr:=0;
            _ilrr2:=0;
            {? EANP.M().IDMOB='D' & EANP.RSQL<>''
            || ZK_P.cntx_psh();
               REZ.cntx_psh();
               _zkp:=exec('FindAndGet','#table',ZK_P,EANP.RSQL,,,null());
               REZ.index('ZK_P');
               REZ.prefix(_zkp,'B');
               {? REZ.first()
               || {!
                  |? {? REZ.SC=_scsql || _ilrr+=REZ.ILR; {? _jmg || _ilrr2+=REZ.IL2 ?} ?};
                     REZ.next()
                  !}
               ?};
               ZK_P.cntx_pop();
               REZ.cntx_pop()
            ?};
            _ile:={? EANP.EANN().STAN<>'Z' || EANP.IL || EANP.ILS ?};
            _kpl+={? _ile>=_ilrr || _ile-_ilrr || 0 ?}
         ?};
         EANP.next()
      !}
   ?};
   EANN.cntx_pop();
   EANP.cntx_pop()
?};
DISP.ILD:=_ild;
EANX.ILW:=_kpl;
HELP.DIL2:=_ilr2;
_ilr


\oblsldat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: Oblicza stan wg DK_L-zaakceptowane dokumenty i reorganizacje - powyzej danej daty
::   WE: _a - material
::       _b - lokalizacja
::       _c - termin waznosci
::       _d - data do porownania
::       [_e] - paleta
::       [_f] - kod identyfikacyjny SCEAN
::       [_g] - 1-kontrola tylko terminu ważności - pomijamy lokalizację 0-nie (domyślnie)
::       [_h] - wskazanie na dostawę lub pusta wartość (domyślnie)
::       [_i] - aktualna ilość na stanie - wymagane wypełnienie parametru _h
::   WY: stan powyzej danej daty
::  OLD: \oblsldat/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=5 || {? type_of(_e)<>7 || _e:=null() ?} || _e:=null() ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
_onlytw:={? var_pres('_g')=type_of(0) || _g || 0 ?};
_scsql:={? var_pres('_h')=type_of('') & +(_h)=16 || _h || '' ?};

_wyn:={? var_pres('_i')=type_of(0) & _scsql<>'' || _i || 0 ?};
_dokl_m:=exec('jaka_dok_m','jm',_a);
_rok:=form(_d~1,,0,'99')+2;
_akt:=ND.name()+3;
_maski:=ND.names();
_mg:=exec('FindAndGet','#table',EANL,_b,,"MG",null());
_ispal:=exec('FindAndGet','#table',MG,_mg,,"PAL='T'",0);

ND.cntx_psh();
DK.cntx_psh();
DK_L.cntx_psh();
SC.cntx_psh();
:: NUCO - Błąd STD
DK_LN.cntx_psh();
_maski.clear();
{? _maski.last()
|| {!
   |? {? (_maski.NAME+2)>=_rok
      || exec('mag_open','open_tab',1+(_maski.NAME+3),_maski.NAME+2);
         {? ~_onlytw
         || DK_L.index('MLOKTW');
            DK_L.prefix(_a,_b,_e,_c)
         || DK_L.index('MTW');
            DK_L.prefix(_a,_e,_c)
         ?};
         {? _f=''
         || {? DK_L.first() & DK_L.find_tab('first','DK',,'<>',null(),'Z',,'=','T','MG',,'=',_mg)
            || {!
               |? _dksql:=DK_L.SCSQL;
                  _ppsql:={? _ispal & DK_L.DK<>null() || DK_L.DK().PRDK || '' ?};
                  {? DK_L.DK().NDK<>''
                  || _msksc:='stc__'+(DK_L.DK().NDK+3);
                     SC.cntx_psh();
                     {? _msksc<>SC.name() || SC.use(_msksc) ?};
                     SC.index('SN');
                     SC.prefix(_mg,DK.M,DK.RDK,DK.NDK);
                     {? SC.first() & SC.SRDK<>'' & SC.SRDK<>_dksql || _dksql:=SC.SRDK ?};
                     SC.cntx_pop()
                  ?};
                  _wsp:={? _scsql<>'' & (_dksql=_scsql | _ppsql=_scsql) || -1 || 0 ?}+
                        {? DK_L.DK().N().D>_d || 1 || 0 ?};
                  _wyn+=_wsp*DK_L.IL $ _dokl_m;
                  DK_L.find_tab('next','DK',,'<>',null(),'Z',,'=','T','MG',,'=',_mg)
               !}
            ?};
            {? DK_L.first() & DK_L.find_tab('first','DK_LN',,'<>',null(),'DK_LN','AKC','=','T','MG',,'=',_mg)
            || {!
               |? _wsp:={? _scsql<>'' & DK_L.SCSQL=_scsql || -1 || 0 ?}+
                        {? {? DK_L.DK_LN().DT>date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,0)
                           || date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,0)>_d
                           |? DK_L.DK_LN().DT<date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,1)
                           || date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,1)>_d
                           || DK_L.DK_LN().DT>_d
                           ?}
                        || 1
                        || 0
                        ?};
                  _wyn+=_wsp*DK_L.IL $ _dokl_m;
                  DK_L.find_tab('next','DK_LN',,'<>',null(),'DK_LN','AKC','=','T')
               !}
            ?}
         |? _f<>''
         || {? DK_L.first() & DK_L.find_tab('first','DK',,'<>',null(),'DK','SCEAN','=',_f,'Z',,'=','T','MG',,'=',_mg)
            || {!
               |? _dksql:=DK_L.SCSQL;
                  _ppsql:={? _ispal & DK_L.DK<>null() || DK_L.DK().PRDK || '' ?};
                  {? DK_L.DK().NDK<>''
                  || _msksc:='stc__'+(DK_L.DK().NDK+3);
                     SC.cntx_psh();
                     {? _msksc<>SC.name() || SC.use(_msksc) ?};
                     SC.index('SN');
                     SC.prefix(_mg,DK.M,DK.RDK,DK.NDK);
                     {? SC.first() & SC.SRDK<>'' & SC.SRDK<>_dksql & SC.SCEAN=_f || _dksql:=SC.SRDK ?};
                     SC.cntx_pop()
                  ?};
                  _wsp:={? _scsql<>'' & (_dksql=_scsql | _ppsql=_scsql) || -1 || 0 ?}+
                        {? DK_L.DK().N().D>_d || 1 || 0 ?};
                  _wyn+=_wsp*DK_L.IL $ _dokl_m;
                  DK_L.find_tab('next','DK',,'<>',null(),'DK','SCEAN','=',_f,'Z',,'=','T','MG',,'=',_mg)
               !}
            ?};
            {? DK_L.first()
             & DK_L.find_tab('first','DK_LN',,'<>',null(),'DK_LN','AKC','=','T','SCEAN',,'=',_f,'MG',,'=',_mg)
            || {!
               |? _wsp:={? _scsql<>'' & DK_L.SCSQL=_scsql || -1 || 0 ?}+
                        {? {? DK_L.DK_LN().DT>date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,0)
                           || date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,0)>_d
                           |? DK_L.DK_LN().DT<date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,1)
                           || date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,1)>_d
                           || DK_L.DK_LN().DT>_d
                           ?}
                        || 1
                        || 0
                        ?};
                  _wyn+=_wsp*DK_L.IL $ _dokl_m;
                  DK_L.find_tab('next','DK_LN',,'<>',null(),'DK_LN','AKC','=','T','SCEAN',,'=',_f,'MG',,'=',_mg)
               !}
            ?}
         ?}
      ?};
      _maski.prev()
   !}
?};
exec('mag_open','open_tab',1+_akt,_akt+2);
ND.cntx_pop();
DK.cntx_pop();
DK_L.cntx_pop();
SC.cntx_pop();
:: NUCO - Błąd STD
DK_LN.cntx_pop();
_wyn


\sm_dokal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyswietla wszystkie dokumenty z okienka stanow
::  OLD: \sm_dokal/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
_sel:=DK.grp_make('Pozycje dokumentów magazynowych'@,,'msaewdkvusopoly');
_atrmjs:=ATR.MJS;
ATR.MJS:='DK';
:: NUCO PeKa - zmienna do wyliczenia stanu po operacji (zmiany w defie) w oknie wszystkie dokumenty od strony stanow magazynowych
:: ponizej kod przeniesiony z Xpertisa
QNUCO.SMIL:=0;
QNUCO.fld_fml('SMIL','DISPLAY_FORMAT',"'out_prec='+$ST.DOKL");
DK.fld_fml('C','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $DK.M().DOKL_C || $ST.DOKL_C ?}+','+{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
DK.fld_fml('WAR','DISPLAY_FORMAT',"{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
ND.cntx_psh();
DK.cntx_psh();
DK_L.cntx_psh();
SM.cntx_psh();
{? var_pres('bie_r')>100 || obj_del(bie_r) ?};
{? var_pres('bie_stan')>100 || obj_del(bie_stan) ?};
{? var_pres('bie_mat')>100 || obj_del(bie_mat) ?};
{? var_pres('ind_tmp')>100 || obj_del(ind_tmp) ?};
{? var_pres('dk_tmp')>100 || obj_del(dk_tmp) ?};
dk_tmp:=tab_tmp(2,'ROK','INTEGER','Rok'
                   ,'REF','INTEGER','Ref'
                   ,'STAN','REAL','Stan');
ind_tmp:=dk_tmp.ndx_tmp(,1,'ROK',,0,'REF',,0);
dk_tmp.index(ind_tmp);
_il:=0;
czy_dsp:=0;
bie_stan:=bie_wart:=0;
bie_mat:=SM.M;
OKR.cntx_psh();
{? OKR.last()
||
   {!
   |?
      {? OKR.FIRMA=REF.FIRMA & OKR.MC=1
      ||
         DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
         ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
         DK_L.use('doklk'+ST.ODDZ+($OKR.ROK+2));
         DK.index('DK_SM');
         DK.prefix(ST.MAG,bie_mat);
         {? DK.first()
         ||
            _il+=1;
            _form:=$('czy_dsp:=1;bie_r:='+$OKR.ROK+';
            bie_stan:=bie_wart:=0;
            SM.use(\'stm__\'+ST.ODDZ+($bie_r+2));
            SM.index(\'SM\');
            SM.prefix();
            {? SM.find_key(ST.MAG,bie_mat)
            ||
               exec(\'stan_zest\',\'magazyn_stan\',ST.MAG,bie_mat,date(bie_r,12,31),0,\"bie_stan\",\"bie_wart\")
            ?};
            ND.use(\'nagdo\'+ST.ODDZ+($bie_r+2));
            DK.use(\'dokma\'+ST.ODDZ+($bie_r+2));
            DK_L.use(\'doklk\'+ST.ODDZ+($bie_r+2));
            DK.index(\'DK_SM\');
            DK.prefix(ST.MAG,bie_mat);
            {? DK.last()
            || _bie_dk:=0;
               {!
               |? dk_tmp.blank();
                  dk_tmp.ROK:=DK.AR;
                  dk_tmp.REF:=#DK.ref();
                  {? DK.N().Z = \'T\'
                  || {? DK.PLUS=\'T\'
                     || _bie_dk:=-DK.IL
                     || _bie_dk:=DK.IL
                     ?};
                     {? bie_stan>=0
                     || dk_tmp.STAN:=bie_stan
                     || dk_tmp.STAN:=0
                     ?};
                     dk_tmp.add();
                     bie_stan+=_bie_dk
                  ?};
                  DK.prev()
               !}
            ?};
            DK.first()');
            DK.grp_sel(_sel,DK,'WER_SM',$OKR.ROK,"{? czy_dsp=1 || grp_disp(DK,'WER_SM',0,1) ?};czy_dsp:=0",,,,_form)
         ?}
      ?};
      OKR.prev() & _il<16
   !}
?};
OKR.cntx_pop();
{? _il=0
||
   _form:=$('czy_dsp:=1;bie_r:='+$ST.AR+';
   bie_stan:=bie_wart:=0;
   SM.use(\'stm__\'+ST.ODDZ+($bie_r+2));
   SM.index(\'SM\');
   SM.prefix();
   {? SM.find_key(ST.MAG,bie_mat)
   ||
      exec(\'stan_zest\',\'magazyn_stan\',ST.MAG,bie_mat,date(bie_r,12,31),0,\"bie_stan\",\"bie_wart\")
   ?};
   ND.use(\'nagdo\'+ST.ODDZ+($bie_r+2));
   DK.use(\'dokma\'+ST.ODDZ+($bie_r+2));
   DK_L.use(\'doklk\'+ST.ODDZ+($bie_r+2));
   DK.index(\'DK_SM\');
   DK.prefix(ST.MAG,bie_mat);
   {? DK.last()
   || _bie_dk:=0;
      {!
      |? dk_tmp.blank();
         dk_tmp.ROK:=DK.AR;
         dk_tmp.REF:=#DK.ref();
         {? DK.N().Z = \'T\'
         || {? DK.PLUS=\'T\'
            || _bie_dk:=-DK.IL
            || _bie_dk:=DK.IL
            ?};
            {? bie_stan>=0
            || dk_tmp.STAN:=bie_stan
            || dk_tmp.STAN:=0
            ?};
            dk_tmp.add();
            bie_stan+=_bie_dk
         ?};
         DK.prev()
      !}
   ?};
   DK.first()');
   DK.grp_sel(_sel,DK,'WER_SM',$ST.AR,"{? czy_dsp=1 || grp_disp(DK,'WER_SM',0,1) ?};czy_dsp:=0",,,,_form)
?};

exec('dk_p_ib','magdok_poz','WER_SM');
DK.win_sel(_sel);
DK.win_fml('WER_SM',ND,'SYM',,'ICON_BEFORE',exec('nd_sym_ib','magdok_nag'));
{? _il>0 || DK.select() || FUN.info('Brak dokumentów magazynowych dla indeksu %1.'@[SM.M().KTM]) ?};
DK.cntx_pop();
ND.cntx_pop();
DK_L.cntx_pop();
SM.cntx_pop();
dk_tmp.ndx_drop(ind_tmp);
&bie_wart;
&bie_mat;
&ind_tmp;
VAR_DEL.delete('czy_dsp','bie_r');
ATR.MJS:=_atrmjs;
DK.fld_fml('C','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $DK.M().DOKL_C || $ST.DOKL_C ?}+','+'empty=0'");
DK.fld_fml('WAR','DISPLAY_FORMAT',"'empty=0'");
''



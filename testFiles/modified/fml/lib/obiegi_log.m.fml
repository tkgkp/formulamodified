:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obiegi_log.fml
:: Utworzony: 24.07.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa powiązania obiegów z logistyką
::======================================================================================================================

\edokum_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Tworzenie dokumentu w obiegu dla zamówienia
::   WE: _a - 'D' - zamówienie dostaw
::            'S' - zamówienie sprzedaży
::            'W' - zamówienie wewnętrzne
::       _b - typ obiegu (ETYPY.ref())
::       _c - 1/0 - czy pokazywać komunikaty
::       _d - wskazanie na jednostkę księgową
::   WY: ref EDOKUM lub null
::----------------------------------------------------------------------------------------------------------------------
:WW: DOSTAWCA przypisana jest do osoby rejestrujacej zamowienie dostawy
:WW: EDOKUM.UW_OPDL:=ZD_NAG.UW (TYP MEMO)
:WW: Do pola EDOKUM.ZD_NAG przypisano ref ZD_NAG
_args:=params_get();
_we:=_args.in;
_service:=params_get().mp.isService();
_zwrot:=null;
OBIEGI.DEL_ETAT:=''; SKID.SL_KH:='';
EDOKUM.blank();
EDOKUM.TYP:=_b;
EDOKUM.TYP();
EDOKUM.TYPOBIEG:=ETYPY.TYPOBIEG;
EDOKUM.DOSTAWCA:=ZD_NAG.US().OSOBA;
EDOKUM.USERS:={? _a='D' || ZD_NAG.US |? _a='S' | _a='W' || ZK_N.US || null ?};
EDOKUM.UNIK_ID:=tm_stamp();
EDOKUM.ODD:=_d;
EDOKUM.FIRMA:=REF.FIRMA;
EDOKUM.ROK_F:=SSTALE.AR;
EDOKUM.ALERTY:='T';
EDOKUM.AUTOMAT:=2;
EDOKUM.DATAW:=date();
{? _a='D'
|| EDOKUM.ID:=EDOKUM.SYM:=ZD_NAG.SYM;
   EDOKUM.ZD_NAG:=$ZD_NAG.ref;
   EDOKUM.DOP:=ZD_NAG.DATA;
   EDOKUM.KHKH:=ZD_NAG.KH;
   {? ETYPY.CZY_DATY
   || EDOKUM.DATA_OD:=ZD_NAG.DATA;
      EDOKUM.DATA_DO:=ZD_NAG.DTPREAL
   ?};
   {? ETYPY.CZY_WAR=3 | ETYPY.CZY_WAR=4
   || EDOKUM.WAL:=FINFO.NAROD;
      EDOKUM.NETTO:=EDOKUM.WART:=ZD_NAG.WAR
   ?};
   EDOKUM.TR:='Dot.zam.dostaw nr: '+ZD_NAG.SYM+ {? +ZD_NAG.UWAGI || ' UWAGI: '+ZD_NAG.UWAGI  || '' ?};
   {? EDOKUM.TR='' || EDOKUM.TR:='Uwagi zamówienia dostaw' ?}
|? _a='S' | _a='W'
|| EDOKUM.ID:=EDOKUM.SYM:=ZK_N.SYM;
   EDOKUM.DOP:=ZK_N.DP;
   EDOKUM.KHKH:=ZK_N.KH;
   {? ETYPY.CZY_DATY
   || EDOKUM.DATA_OD:=ZK_N.DT;
      EDOKUM.DATA_DO:=ZK_N.PL_DATA
   ?};
   EDOKUM.WAL:=FINFO.NAROD;
   {? ETYPY.CZY_WAR=1 | ETYPY.CZY_WAR=3
   || EDOKUM.WART:=ZK_N.BRUTTO
   ?};
   {? ETYPY.CZY_WAR=3 | ETYPY.CZY_WAR=4
   || EDOKUM.NETTO:=ZK_N.NETTO
   ?};
   EDOKUM.TR:=ZK_N.OP;
   {? _a='S'
   || {? EDOKUM.TR='' || EDOKUM.TR:='Dot.zam.sprzedaży nr: '+ZK_N.SYM ?}
   || {? EDOKUM.TR='' || EDOKUM.TR:='Dot.zam.wewnętrznego nr: '+ZK_N.SYM ?}
   ?}
?};
{? ETYPY.CZY_KH & ETYPY.DOM_SL & {? _a='D' || ZD_NAG.KH || ZK_N.KH ?}
|| _kod:={? _a='D' || ZD_NAG.KH().KOD || ZK_N.KH().KOD ?};
   SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(EDOKUM.TYP().DOM_SL().SLU,_kod,);
   {? SLO.first()
   || EDOKUM.KH:=SLO.ref(); EDOKUM.KHKH:=KH.ref()
   |? SLU.DL=+form(KH.KOD)
   || EDOKUM.TYP().DOM_SL().SLU();
      SLO.SLU:=SLU.ref();
      SLO.KOD:=KH.KOD;
      SLO.TR:=KH.NAZ;
      {? SLO.add(1)
      || EDOKUM.KH:=SLO.ref(); EDOKUM.KHKH:=KH.ref()
      ?}
   ?};
   SLO.cntx_pop()
?};
EDOKUM.bl_file('EDOKUM',1); EDOKUM.bl_file('EPODPIS',1);
_txt:=ZD_NAG.memo_txt(0,1,'UW');
EDOKUM.memo_set(_txt,'UW_OPDL');
no_us:=null;
{? EDOKUM.add()
|| _zwrot:=EDOKUM.ref();
   {? ETYPY.AUT_ID='T' & EDOKUM.ID=''
   || VAR_DEL.delete('id_edok');
      exec('ustal_numer','obiegi',1,SSTALE.AR,1,0);
      EDOKUM.ID:={? var_pres('id_edok')>0 || id_edok || '' ?};
      EDOKUM.put()
   ?};
   EDOKUM.memo_put(,'UW_OPDL');
   {? (EDOKUM.TYP().ATR_ONE>0) & (EDOKUM.TYP().ATR_G1R>0)
   || exec('edk_atr_dolacz','obiegi',0)
   ?};
   {? _a='D'
   || ZD_NAG.cntx_psh(); ZD_NAG.prefix(); ZD_NAG.OBI_POW:=EDOKUM.uidref(); ZD_NAG.put(); ZD_NAG.cntx_pop();
      EDOKUM.DOK_POW:=ZD_NAG.uidref(); EDOKUM.LOG_PRZ:='T'; EDOKUM.put();
      exec('copy_zalacz','obiegi_log',ZD_NAG)
   || ZK_N.cntx_psh(); ZK_N.prefix(); ZK_N.OBI_POW:=EDOKUM.uidref(); ZK_N.put(); ZK_N.cntx_pop();
      EDOKUM.DOK_POW:=ZK_N.uidref(); EDOKUM.LOG_PRZ:='T'; EDOKUM.put();
      exec('copy_zalacz','obiegi_log',ZK_N)
   ?}
?};
VAR_DEL.delete('no_us');
_zwrot



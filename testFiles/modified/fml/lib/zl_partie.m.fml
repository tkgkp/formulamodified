:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zl_partie.fml
:: Utworzony: 08.07.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa parti produkcyjnych do zlecenia
::======================================================================================================================


\add
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [12.30]
:: OPIS: Dodaje rekord do tabeli ZPARN na podstawie tablicy nazwanej bedaca buforem
::   WE: _a - _args - exec('buffer','zparn')
::   WY: 0 - porazka
::       1 - sukces
::  OLD: \add/zparn.fml
::----------------------------------------------------------------------------------------------------------------------
_args:={? var_pres('_a')>100 || _a || exec('buffer','zl_partie') ?};

_result:=0;
_can_continue:=0;

ZPARN.cntx_psh(); ZPARN.clear();
ATR.ZL:=null();
ATR.ZGH:=null();
ATR.KTM:=null();
ZPARN.blank();
:: przepisujemy wartosci z tablicy buforowej do bufora tabeli
_args.set();

:: Przed dodaniem partii podmieniam zlecenie, jeśli zostało przekazane niewłaściwe (ważne w przypadku zleceń złożonych)
ZPARN.ZL:=exec('party_first','zl_link',_args.ZL);

:: przepisujemy wartosci z rekordu z powrotem do bufora (bo byc moze ilosci zostaly zaokraglone na rekordzie)
_args.get();

ZPARN.SCEAN:=exec('blpp_sce','kody_kresk');
ZPARN.ZPEAN:=exec('blpp2sce','kody_kresk');
{? ZPARN.KTM=null()
|| ZPARN.KTM:=ZPARN.ZL().KTM
?};
{? ZPARN.KTM().TWD>0 || ZPARN.TW:=ZPARN.ZL().DTR+ZPARN.KTM().TWD ?};

:: NUCO - zmiana - ustalenie domyślnej daty ważnośći
{? PROD_REJ.STARTD<>date(0,0,0) & ZPARN.TW=date(0,0,0)
|| ZPARN.TW:=exec('add_months','#interval',PROD_REJ.STARTD,36)
|? ZGH.STARTD<>date(0,0,0) & ZPARN.TW=date(0,0,0)
|| ZPARN.TW:=exec('add_months','#interval',ZGH.STARTD,36)
?};
_result:=ZPARN.add();

{? _result>0
|| {? ZPARN.SCEAN<>'' || exec('mkodkadd','kody_kresk',ZPARN.ZL().KTM,ZPARN.SCEAN,$ZPARN.ref()) ?};
   {? ZPARN.ZPEAN<>'' & ZPARN.ZPEAN<>ZPARN.SCEAN
   || exec('mkodkadd','kody_kresk',ZPARN.ZL().KTM,ZPARN.ZPEAN,$ZPARN.ref())
   ?};
:: Mechanizm numeracji partii
   _can_continue:=exec('partsym','zl_partie')
?};
ZPARN.cntx_pop();
_result



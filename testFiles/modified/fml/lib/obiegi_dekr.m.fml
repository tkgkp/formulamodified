:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obiegi_dekr.fml
:: Utworzony: 21.04.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa obiegów dokumentów - dekretacja
::======================================================================================================================


\dob_dek1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Dekretacja dokumentu w obiegu
::   WE: _a - opcja - dekretacja z TODO
::  OLD: \dob_dek/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
_open:=0; _dalej:=0;
rok:=__PARSES.getVal('OkresRok').NAZWA;
VAR_DEL.delete('g_dokkh1','g_dokkh2','g_dokkh3');
g_dokkh1:=obj_new(2); g_dokkh1[1]:=g_dokkh1[2]:=0;
g_dokkh2:=obj_new(2); g_dokkh2[1]:=g_dokkh2[2]:='';
g_dokkh3:=obj_new(2); g_dokkh3[1]:=g_dokkh3[2]:='';

:: NUCO - dekretacja z poziomu obszaru - wybranie okresu
_zmiana:=0;
{? typobi=1 &
   (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON)
|| FUN.info('Data SPRZEDAŻY faktury (%2) jest spoza bieżącego okresu (%1).\n\n'
            'Wybierz okres dekretacji faktury.'@[SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ,$EDOKUM.DOP]);
   _zmiana:=1;
   {? ~_
   || _a:=0
   ?}
?};
:: KONIEC

{? _ | _zmiana
|| exec('szuk_okro','okresy',EDOKUM.DOP);
   {? ~_a
   || ROK_F.cntx_psh(); OKRO_F.cntx_psh(); OKRO_F.win_sel('WER');
      {? EDOKUM.ODD = null
      || OKRO_F.actions_grayed('WER','W')
      || OKRO_F.actions_grayed('WER','')
      ?};
      OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
      ROZNE.UT_OKROD();
      {? OKRO_F.select(,1)
      || {? OKRO_F.POCZ=date(0,0,0)
         || exec('blad_okr','obiegi_dekr')
         || _ar:=SSTALE.AR; _ao:=SSTALE.AO; _open:=1; _dalej:=1;
            SSTALE.AR:=OKRO_F.ROK; SSTALE.AO:=OKRO_F.ref()
         ?}
      ?};
      ROK_F.cntx_pop(); OKRO_F.cntx_pop()
   || _ar:=SSTALE.AR; _ao:=SSTALE.AO; _open:=1; _dalej:=1;
      SSTALE.AR:=ROZNE.UT_OKROD().ROK; SSTALE.AO:=ROZNE.UT_OKROD
   ?}
|| _dalej:=1
?};
_grvat:=null; _r:=_kk:=_k2:=''; POMOC.AUT_DEK:=0;
_ksdot:=0;
{? ~_dalej
|| return(0)
|? exec('czy_okr_zam_con','konsolidacja',1)
|| return(0)
|? (EDOKUM.f_active() & EDOKUM.f_get()) |
   (~EDOKUM.f_active() & EDOKUM.get())
|| {? EDOKUM.r_lock(1,1,1)
   || {? EDOKUM.ODD=null & ((var_press('zawin')>0 & zawin=8) | var_press('_a')=-1)
      || _tmpTab:=tab_tmp(1,
            'KOD','STRING[8]','Skrót'@,
            'OPIS','STRING[40]','Pełna nazwa'@,
            'REF','INTEGER',
         );
         ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
         {? ODD.first()
         || {!
            |? _tmpTab.KOD:=ODD.OD;
               _tmpTab.OPIS:=ODD.N;
               _tmpTab.REF:=#ODD.ref();
               _tmpTab.add();
               ODD.next()
            !}
         ?};
         _win:=_tmpTab.mk_sel('Jednostki księgowe'@,'P',,'#oddusr',,,,,'U');
         _tmpTab.win_fld(_win,,'KOD');
         _tmpTab.win_fld(_win,,'OPIS');
         _tmpTab.win_act(_win,0,'Formuła','Wybierz'@@,,'Wybór bieżącej pozycji'@,"sel_exit()",,1,,,,'W');
         _tmpTab.win_sel(_win);
         {? _tmpTab.select
         || ODD.seek(_tmpTab.REF,);
            EDOKUM.ODD:=ODD.ref();
            EDOKUM.put()
         ?}
      ?};
      EDOKUM.cntx_psh();
      _zwrot:=1; EDOKUM.TYP();
      dob_dek:=1;
      DOK.cntx_psh(); VPOZ.cntx_psh();
      {? EDOKUM.TYP().DEKR='N'
      || {? typobi=1
         || FUN.info('Faktura nie podlega dekretacji.'@)
         || FUN.info('Wniosek nie podlega dekretacji.'@)
         ?};
         _zwrot:=0
      ?};
      {? _zwrot & EDOKUM.STDEKRD
      || {? typobi=1
         || FUN.info('Faktura jest już po dekretacji.'@)
         || FUN.info('Wniosek jest już po dekretacji.'@)
         ?}; _zwrot:=0
      ?};
      {? _zwrot
      || EDOKOS.cntx_psh(); EDOKOS.index('SZUK5'); EDOKOS.prefix(EDOKUM.ref(),'D','T');
         _zwrot:=EDOKOS.first();
         {? ~_zwrot
         || {? typobi=1
            || FUN.info('Faktura nie jest jeszcze w czynności dekretacji.'@)
            || FUN.info('Wniosek nie jest jeszcze w czynności dekretacji.'@)
            ?}
         ?};
         EDOKOS.cntx_pop()
      ?};
      {? _zwrot
      || ETYPYFIR.index('UNIK'); ETYPYFIR.prefix(ETYPY.ref(),REF.FIRMA,SSTALE.AR,EDOKUM.ODD);
         {? ~ETYPYFIR.first()
         || _zwrot:=0; _rok:=SSTALE.AR().NAZ; _odd:=EDOKUM.ODD().OD;
            FUN.info('Nie znaleziono parametrów dekretacji dla typu %1 roku %2'
                     ' i jednostki księgowej %3.'@[ETYPY.NAZWA,_rok,_odd])
         ?}
      ?};
      {? _zwrot & SSTALE.AO().POCZ=date(0,0,0)
      || exec('blad_okr','obiegi_dekr'); _zwrot:=0
      ?};
      {? _zwrot & SSTALE.AO().ZAM='T'
      || FUN.info('Bieżący okres jest zamknięty.'@); _zwrot:=0
      ?};
      {? _zwrot & exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER) || _zwrot:=0 ?};
      {? _zwrot & typobi=2 & (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON)
      || FUN.info('Data operacji wniosku pochodzi z innego okresu obrachunkowego.'@); _zwrot:=0
      ?};
      {? _zwrot & EDOKUM.KH=null & (typobi=1 | (typobi=2 & ETYPY.CZY_KH))
      || FUN.info('Nie wybrano kontrahenta.'@); _zwrot:=0
      ?};
      {? _zwrot || _zwrot:=exec('spr_podz_dek','obiegi_dekr') ?};
      {? _zwrot & typobi=1
      || brutdob:=netdob:=0;
         _zwrot:=exec('spr_pvat','obiegi2',0,0);
         VAR_DEL.delete('brutdob','netdob')
      ?};
      SSTALE.AO();
      {? _
      || {? EDOKUM.DOP>=OKRO_F.POCZ & EDOKUM.DOP<=OKRO_F.KON
         || _ksdot:=0
         |? EDOKUM.DO>=OKRO_F.POCZ & EDOKUM.DO<=OKRO_F.KON
         || _ksdot:=1
         || _ksdot:=2; WYDRUKIN.DATAOD:=OKRO_F.KON
         ?}
      || {? _zwrot & typobi=1 &
            (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON) &
            (EDOKUM.DO>=OKRO_F.POCZ & EDOKUM.DO<=OKRO_F.KON)
         || _zwrot:=FUN.ask('Data sprzedaży faktury (%2) jest spoza bieżącego okresu (%1).\n'
                            'Data otrzymania faktury (%3) zawiera się w bieżącym okresie.\n'
                            'Zadekretować dokument w okresie zgodnie z datą '
                            'otrzymania faktury?'@[SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ,$EDOKUM.DOP,$EDOKUM.DO]);
            {? _zwrot || _ksdot:=1 ?}
         ?};
         {? _zwrot & typobi=1 & (EDOKUM.DO<OKRO_F.POCZ | EDOKUM.DO>OKRO_F.KON)
         || _zwrot:=FUN.ask('Data otrzymania faktury (%1) jest spoza bieżącego okresu (%2).\n'
                            'Zadekretować dokument w bieżącym okresie?'@[$EDOKUM.DO,SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ])
         ?};
         {? _zwrot & ~_ksdot & typobi=1 &
            (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON) & (EDOKUM.DO<OKRO_F.POCZ | EDOKUM.DO>OKRO_F.KON)
         || WYDRUKIN.DATAOD:=OKRO_F.KON;
            WYDRUKIN.win_edit('DATA'); WYDRUKIN.hdr_edit(); WYDRUKIN.hdr_edit('Data zapisu'@);
            {? WYDRUKIN.edit("{? WYDRUKIN.DATAOD<OKRO_F.POCZ | WYDRUKIN.DATAOD>OKRO_F.KON
                              || FUN.info('Wybrana data nie zawiera się w bieżącym okresie obrachunkowym.'@); 0
                              || 1
                              ?}"
                            )
            || _ksdot:=2
            || _zwrot:=0
            ?};
            WYDRUKIN.hdr_edit()
         ?}
      ?};
      {? _zwrot
      || VAT7.KK:=VAT7.K2:='N';
         POMOC.AKC:='E'; _addvpoz:=0; VPOZ.blank(1);
         DOK.use('doku'+SSTALE.AR().KOD+form(OKRO_F.NR,-2));
         DOK.index('REJ'); DOK.prefix(); DOK.blank();
         DOK.ODD:=EDOKUM.ODD;
         DOK.REJ:=ETYPYFIR.DOM_REJ;
         {? ~ETYPY.TYPVATPR & typobi=1
         || {? EDOKUM.WAL=FINFO.NAROD || DOK.SUMWAL:=EDOKUM.WART ?};
            DOK.RVAT:=ETYPYFIR.DOM_RVAT;
            VPOZ.GRVAT:=ETYPYFIR.DOMGRVAT
         ?};
         {? EDOKUM.KOREKTA='T'
         || DOK.DOK_REJ:=ETYPYFIR.DOM_RKOR;
            DOK.KOR:=DOK.KOR_ZEW:=EDOKUM.SYM_KOR;
            DOK.DWKOR:=EDOKUM.DWKOR
         || DOK.DOK_REJ:=ETYPYFIR.DOM_RDOK
         ?};
         POMOC.DISP_POZ:=EDOKUM.WAL<>FINFO.NAROD & (var_pres('KURS',EDOKUM)<=0 | EDOKUM.KURS=0);
         _edi:=exec('read_edi','obiegi_dekr') & EDI_Z.DOK<>'';
         {? _edi || {? ~DOK.seek(EDI_Z.DOK) || _edi:=0 || EDI_Z.DOK:=''; DOK.r_lock(1,1,1) ?} ?};
         _dodane:=0;
         DOK.EDI:=EDOKUM.EDI;
         {? ~ETYPY.TYPVATPR & typobi=1
         || {? EDOKUM.WAL=FINFO.NAROD || DOK.SUMWAL:=EDOKUM.WART ?};
            DOK.HAN:=EDOKUM.HAN;
            DOK.KH_FULL:=EDOKUM.KH_FULL;
            DOK.UL:=EDOKUM.UL;
            DOK.MIASTO:=EDOKUM.MIASTO;
            DOK.POCZ:=EDOKUM.POCZ;
            DOK.DOM:=EDOKUM.DOM;
            DOK.LOKAL:=EDOKUM.LOKAL;
            DOK.KPOCZ:=EDOKUM.KPOCZ
         ?};
         {? typobi=1 & ~ETYPY.TYPVATPR || exec('tagger_tip_nag_of','tagger',EDOKUM.KH,EDOKUM.KOREKTA) ?};
         DOK.SP_WYM:=EDOKUM.SP_WYM;
         DOK.NRKSEF:=EDOKUM.NRKSEF;
         {? DOK.SP_WYM='T' & 'VAT,SAD'*DOK.DOK_REJ().SLO().KOD=0
         || DOK.SP_WYM:='N'
         ?};
         {? typobi=1 & ~ETYPY.TYPVATPR
         || DOK.win_edit('KSDKOB02');
            POMOC.AUT_DEK:=EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS;
            POMOC.AUT_SCH:=0;
            DOK.efld_opt('KSDKOB02','enable='+$(ETYPYFIR.AUTOKSIE<>null),POMOC,'AUT_SCH')
         || DOK.win_edit('KSDKOB01');
            POMOC.AUT_SCH:=1
         ?};
         {? _edi |
            (var_pres('zoknzedi')<=0 & DOK.edit("exec('sprdokks_do1','obiegi_dekr')"))
         || {? typobi=1 & ~ETYPY.TYPVATPR
            || _grvat:=VPOZ.GRVAT; _r:=VPOZ.R; _kk:=VPOZ.KK; _k2:=VPOZ.K2
            ?};
            {? DOK.DOK_REJ().KOR='T'
            || DOK.KOR_PRZY:=EDOKUM.KOR_PRZY;
               DOK.KOR_OKR:=EDOKUM.KOR_OKR;
               DOK.DWKOR:=EDOKUM.DWKOR
            ?};
            DOK.NRKSEF:=EDOKUM.NRKSEF;
            DOK.memo_set(EDOKUM.NRKSEFK,'NRKSEFDK');
            DOK.WYS:=EDOKUM.KH;
            DOK.TR:=EDOKUM.TR;
            {? var_pres('RB',EDOKUM)>0
            || DOK.RB:=EDOKUM.RB
            ?};
            exec('edokum_kh','obiegi_dekr');
            DOK.WAL:={? EDOKUM.WAL=FINFO.NAROD || null || EDOKUM.WAL ?};
            {? typobi=1
            || DOK.NK:=DOK.SYM_ZEW:=EDOKUM.SYM;
               DOK.D4:=EDOKUM.DO;
               DOK.DTO:=EDOKUM.DW;
               _tp:=EDOKUM.TP;
               {? PAR_SKID.get(38)='N' & _tp<DOK.DTO || _tp:=DOK.DTO ?};
               DOK.D3:=_tp
            || DOK.DTO:=EDOKUM.DOP;
               DOK.NK:=DOK.SYM_ZEW:=EDOKUM.ID
            ?};
            DOK.DTW:={? _ksdot=1 || EDOKUM.DO
                     |? _ksdot=2 || WYDRUKIN.DATAOD
                     || EDOKUM.DOP
                     ?};
            DOK.DOP:=EDOKUM.DOP;
            DOK.E_DOC:=EDOKUM.EDOKUM;
            DOK.E_DOCP:=EDOKUM.EPODPIS;
            {? typobi=1 || DOK.SP_PL:=EDOKUM.PLATNOSC ?};
            DOK.EDOKUM:=$EDOKUM.ref();
            {? DOK.REJ().WAL='N' || DOK.BANK:=DOK.TKRS:=DOK.TRKS_V:=null; DOK.TYPKRS:='' ?};
            DOK.cntx_psh; DOK.index('REJ'); DOK.prefix(DOK.REJ);
            _numer:={? DOK.last() || DOK.NR+1 || 1 ?};
            DOK.cntx_pop();
            {? ~_edi
            || DOK.NR:=_numer
            ?};
            POMOC.V:=DOK.RVAT;
            exec('dok_edit','obiegi_dekr',exec('okna_r','dok_fks',DOK.DOK_REJ().SLO().KOD));
            {? ~ETYPY.TYPVATPR & typobi=1
            || DOK.RVAT().RVAT();
               DOK.KRAJ:={? RVAT.KRAJ=null || exec('kod_pl','slo_slu') || RVAT.KRAJ ?};
               DOK.JORG:=exec('ust_vwal','kraje',DOK.KRAJ().KOD);
               exec('be_okrvt','dok_fks')
            ?};
            DOK.JPK_V_T:=exec('typ_dok','obiegi2');
            {? ~_edi || exec('edit','dok_ksef',0) || exec('edit','dok_ksef',1); DOK.DOKZRODL:='' ?};
            {? _edi || VAR_DEL.delete('obdekredi'); obdekredi:=1 ?};
            {? DOK.edit("exec('dok_spr','dok_fks')")
            || {! |?
                  {? ~_edi
                  || _numer:=DOK.NR; _i:=exec('numer','dok_fks');
                     {? _i=1
                     || FUN.info('Dokument o numerze '+$_numer+' już istnieje. Proszę zmienić numer dokumentu.'@);
                        DOK.edit("exec('dok_spr','dok_fks')")
                     || 0
                     ?}
                  || 0
                  ?}
               !};
               {? _edi || DOK.r_unlock() ?};
               {? DOK.NR<>0
               || {! |?
                     {? {? ~_edi || DOK.add(1) || DOK.put(1) ?}
                     || {? DOK.r_lock(1,1)
                        || DOK.memo_put(,'NRKSEFDK');
                           exec('edit','dok_ksef',2);
                           {? var_pres('zak_dek')>0 || zak_dek:=1 ?};
                           exec('putbloby','dok_fks');
                           _refdok:=dok_ref:=DOK.ref();
                           POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix(DOK.ref());
                           exec('pop_poz','dok_fks','DOK',0);
                           ROZRACH.TABELA:='DOK';
                           {? DOK.DOK_REJ().PR='T' || exec('dokpradd','dok_fks') ?};
                           EDOKUM.REJ:=DOK.REJ;
                           EDOKUM.DOK_KS:=$DOK.ref();
                           EDOKUM.NREJ:=DOK.NR;
                           EDOKUM.DATDOK:=DOK.DTW;
                           {? OBIEGI.EDOKOS
                           || OBIEGI.EDOKOS();
                              EDOKOS.STATUS:='T';
                              EDOKOS.DATA:=date();
                              EDOKOS.TIME:=time();
                              EDOKOS.USERS:=OPERATOR.USER;
                              EDOKOS.cntx_psh(); EDOKOS.prefix();
                              EDOKOS.put();
                              EDOKOS.cntx_pop();
                              {? typobi=1
                              || exec('add_elog','obiegi_akc','Dekretacja faktury',0)
                              || exec('add_elog','obiegi_akc','Dekretacja wniosku',0)
                              ?}
                           ?};
                           EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
                           EDOKUM.prefix();
                           {? EDOKUM.put()
                           || EDOKUM.STDEKRD:=exec('zn_deleg','obiegi_dekr',EDOKUM.ref());
                              EDOKUM.put()
                           ?};
                           {? (~_edi | (var_pres('edipozfv')>0 & ~edipozfv))
                           || exec('gen_pozf','obiegi_dekr',_grvat)
                           ?};
                           exec('gen_podz','obiegi_dekr',_refdok);
                           _addvpoz:={? (~_edi | (var_pres('edipozfv')>0 & ~edipozfv)) & (~ETYPY.TYPVATPR &
                                        (EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS) &
                                        typobi=1)
                                     || {? DOK.EDI='N'
                                        || exec('gen_vpoz','obiegi_dekr',_refdok,_grvat,_r,_kk,_k2)
                                        || POZF.index('DOK'); POZF.prefix(DOK.ref);
                                           exec('pozf2vpoz','dok_fks1')
                                        ?}
                                     || 0
                                     ?};
                           exec('copy_zalacz','obiegi_dekr',EDOKUM.ref(),DOK.ref());
                           {? (typobi=1 | typobi=2) & ETYPYFIR.AUTOKSIE<>null & POMOC.AUT_SCH
                           || exec('oblicz_f','dok_fks',ETYPYFIR.AUTOKSIE().NAZ)
                           ?};
                           {? (ETYPY.TYPVATPR | typobi=2) & EDOKUM.WAL=FINFO.NAROD
                           || EDOKUM.prefix();
                              {? EDOKUM.put()
                              || EDOKUM.NETTONAR:=EDOKUM.NETTO;
                                 EDOKUM.put()
                              ?}
                           ?};
                           POZ.cntx_pop();
                           _dodane:=1;
                           DOK.r_unlock()
                        || FUN.info('Dokument obsługuje inny operator.'@)
                        ?};
                        _dalej:=0
                     || _numer:=DOK.NR; _i:=exec('numer','dok_fks');
                        {? _i=1
                        || FUN.info('Dokument o numerze %1 już istnieje.\nProszę zmienić numer dokumentu.'@[$_numer]);
                           DOK.edit("exec('dok_spr','dok_fks')")
                        ?};
                        _dalej:=1
                     ?};
                     _dalej
                  !}
               ?};
               {? DOK.r_lock(1,1)
               || {? _edi & POMOC.AUT_DEK | _addvpoz & POMOC.AUT_DEK
               || exec('dek_vpoz','obiegi_dekr',_refdok)
                  ?}
               ?};
               DOK.r_unlock();
               {? POMOC.DISP_POZ || exec('poz_dok','dok_fks') ?}
            || {? _edi || DOK.r_unlock() ?}
            ?};
            VAR_DEL.delete('obdekredi')
         ?};
         {? ~_dodane & _edi || exec('del_edi_dok','obiegi_dekr',DOK.ref()) ?}
      ?};
      DOK.cntx_pop(); VPOZ.cntx_pop();
      VAR_DEL.delete('dob_dek','zoknzedi','edipozfv');
      VAR_DEL.delete('__tagvpztip');
      EDOKUM.cntx_pop(); EDOKUM.f_rfresh();
      EDOKUM.r_unlock()
   || exec('err_kom','obiegi')
   ?}
|| exec('errnodok','obiegi')
?};
{? _open
|| SSTALE.AR:=_ar; SSTALE.AO:=_ao;
   exec('open_tabele','open_tab')
?}


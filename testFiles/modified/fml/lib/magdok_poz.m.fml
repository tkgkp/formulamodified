:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: magdok_poz.fml
:: Utworzony: 24.06.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa dokumentów magazynowych - pozycje dokumentów
::======================================================================================================================


\ae_dost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: po redakcji pola dostawa w DK.DOST
::  OLD: \ae_dost/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_iledk:={? HELP.POP || DK.IL ?};
_ewi:=DK.N().MAG().TYP*'EWI';

{? DK.N().TYP().P='T'
|| _atr_flag:=1;
   {? ~(DK.DOST<>date(0,0,0) & DK.RDK<>0 & ~exec('FindAndGet','#table',TYPYDOK,DK.N().TYP,,"TYPYDOK.T=\'BO\'",0))
   || DK.RDK:=0;
      DK.NDK:='';
      DK.SRDK:='';
      DK.PRDK:='';
      DK.SCEAN:=''
   |? DK.DOST<>date(0,0,0) & DK.RDK & (~HELP.POP | DK.RDK<>#DK.ref()) & DK.NDK<>''
   || _cecha:=null();
      _tw:=date(0,0,0);
      _return:=DK.PLUS='T' & DK.N().TYP().DN='T';
      _scean:='';
      _kp:='';
      DK.cntx_psh();
      DK.use(DK.NDK);
      DK.index('DOST3');
      DK.prefix(ST.MAG,DK.M,DK.RDK,DK.NDK,'T');
      {? DK.first()
      || _kp:=DK.KP;
         _tw:=DK.TW;
         _scean:=DK.SCEAN;
         {? DK.RDKC<>'' & _return
         || {? ATR.DKCZKP<>null() || exec('arch_atr','mat_atr',1,ATR.DKCZKP,''); ATR.DKCZKP:=null() ?};
            exec('arch_atr','mat_atr',0,null,DK.RDKC);
            DK.get();
            ATR.DKCZKP:=DK.DK_C
         ?};
         {? DK.DK_C<>null || _cecha:=DK.DK_C ?}
      ?};
      DK.cntx_pop();
      {? _kp<>'' & DK.KP<>_kp || DK.KP:=_kp ?};
      DK.DK_C:=_cecha;
      {? DK.N().TYP().DN='T'
      || EANX.TW:=DK.TW:=_tw;
         DK.SCEAN:=_scean;
         {? HELP.POP & DK.DOST<>date(0,0,0)
         || DK_L.cntx_psh();
            DK_L.index('DK');
            DK_L.prefix(DK.ref(),null());
            {? DK_L.first()
            || {!
               |? DK_L.SCEAN:=DK.SCEAN;
                  DK_L.TW:=DK.TW;
                  DK_L.SCSQL:=DK.SRDK;
                  DK_L.put(1);
                  DK_L.next()
               !}
            ?};
            DK_L.cntx_pop()
         ?}
      ?};
      {? DK.DK_C().M_ATR<>null()
      || {! _i..10 |! ($('ATR.WAR'+form(_i,-2,0,'99')))():=($('DK.DK_C().WAR'+form(_i,-2,0,'99')))() !};
         DK.DK_C().M_ATR().SYM
      || {! _i..10 |! ($('ATR.WAR'+form(_i,-2,0,'99')))():='' !};
         M_ATR.blank(0)
      ?};
      ATR.FLAG:=0;
      ATR.FLAG_ED:=0;
      _atr_flag:=0
   ?};
   {? _atr_flag
   || ATR.M_ATR:=DK.M().M_ATR;
      _czy_ed:=ATR.FLAG_ED:=ATR.CZY_ATR & (1+ND.MAG().TYP)='D' & ATR.M_ATR().EDIT;
      ATR.FLAG:={? ATR.FLAG_ED & _czy_ed & DK.M().M_ATR<>null() || 2 || 0 ?};
      {? _czy_ed & ATR.FLAG_ED || {? DK.M().M_ATR || ATR.FLAG_ED:=2 ?} ?};
      {? DK.DK_C<>null() || DK.DK_C().M_ATR().SYM || DK.M().M_ATR().SYM ?}
   ?};
   _wyn:=1
||
   __sc.cntx_psh();
   {? DK.RDK<>0
   ||
      _ind:=__sc.ndx_tmp('',,'D',,,'RDK',,,'NDK',,);
      __sc.index(_ind);
      __sc.prefix(DK.DOST,DK.RDK,DK.NDK);
      _jest:=__sc.first();
      {? ~_jest
      || DK.RDK:=0;
         DK.NDK:='';
         DK.SRDK:='';
         DK.PRDK:='';
         __sc.ndx_drop(_ind);
         _ind:=__sc.ndx_tmp('',,'D',,);
         __sc.index(_ind);
         __sc.prefix(DK.DOST);
         _jest:=__sc.first();
         {? _jest
         || {!
            |? {? {? __sc.S>0 || exec('sc_tmp_info','magazyn_stan'); DISP.SD>0 || 0 ?}
               || 0
               || __sc.next()
               ?}
            !}
         ?}
      ?}
   ||
::    szukanie gdy nie wybrano przez F3
      _ind:=__sc.ndx_tmp('',,'D',,);
      __sc.index(_ind);
      __sc.prefix(DK.DOST);
      _jest:=__sc.first();
      {? _jest
      || {!
         |? {? {? __sc.S>0 || exec('sc_tmp_info','magazyn_stan'); DISP.SD>0 || 0 ?}
            || 0
            || __sc.next()
            ?}
         !}
      ?}
   ?};
   {? _jest
   || {? DK.N().MAG().SKL='T' & __sc.WAL<>DK.N().SCWAL().KOD
      || FUN.info(
            'Waluta dostawy różna od waluty wybranej na dokumencie.'
            '\nWybierz dostawę zgodną z walutą dokumentu: %1.'@[DK.N().SCWAL().KOD]
         )
::         NUCO - Zmiana SRDK (dostawa lokalna) na PRDK dostawa pierwotna bo nie działa z przesunięciem
      |? DK.N().TYP().WYR='T' & TYPYDOK.P='N' & exec('FindAndGet','#table',DK,__sc.PRDK,,"ZL",null())<>DK.ZL
         & __sc.WAR01*DK.ZL().SYM=0
      || FUN.info(
            'Zlecenie dostawy różne od zlecenia wybranego na dokumencie.'
            '\nWybierz dostawę zgodną ze zleceniem: %1.'@[DK.ZL().SYM]
         )
      || _wyn:=1;
         DK.C:={? _ewi || exec('biez_cen','ceny_dok',DK.M,ST.MAG,DK.N().D) || __sc.C ?};
         DK.TW:=EANX.TW:=__sc.TW;
         DK.WAR:=(DK.C*DK.IL)$2;
         DK.RDK:=__sc.RDK;
         DK.NDK:=__sc.NDK;
         DK.SRDK:=__sc.SRDK;
         DK.PRDK:=__sc.PRDK;
         DK.SCEAN:=__sc.SCEAN;
         DK.STATS:=exec('FindAndGet','#table','STATS',__sc.STATS,,,null());
         {? ST.MAG().COS='T'
         || DK.CN:=__sc.CN;
            DK.CB:=__sc.CB;
            DK.RAB:=__sc.RAB
         ?};
         {? DK.N().MAG().SKL<>'T'
         || DK.SCC:=DK.C;
            DK.SCWAR:=DK.WAR
         || DK.SCC:=__sc.SCC;
            DK.SCWAR:=(DK.SCC*DK.IL)$2
         ?};
         _cecha:=null();
         _kp:='';
         exec('bd_dpoz_sckrs','magdok_poz');
         DK.cntx_psh();
         DK.use(__sc.NDK);
         DK.index('DOST3');
         DK.prefix(ST.MAG,DK.M,__sc.RDK,__sc.NDK,'T');
         {? DK.first()
         || _kp:=DK.KP;
            {? DK.DK_C<>null() || _cecha:=DK.DK_C ?}
         ?};
         DK.cntx_pop();
         DK.KP:=_kp;
         BEER.KP:=_kp;
         DK.DK_C:=_cecha;
         {? DK.DK_C().M_ATR<>null()
         || {! _i..10 |! ($('ATR.WAR'+form(_i,-2,0,'99')))():=($('DK.DK_C().WAR'+form(_i,-2,0,'99')))() !};
            DK.DK_C().M_ATR().SYM
         || {! _i..10 |! ($('ATR.WAR'+form(_i,-2,0,'99')))():='' !};
            M_ATR.blank(0)
         ?};
         {? DK.DOST<>date(0,0,0) & DK.RDK<>0 & HELP.POP & (__sc.S<_iledk | (DK.PLUS='N' & DK.M().SETW='P'))
         || DK.IL:={? __sc.S<_iledk | ~_iledk || __sc.S || _iledk ?};
            {? DK.M().J2<>null() || DK.IL2:=__sc.S2 ?};
            _ile:=DK.IL;
            DK_L.cntx_psh();
            DK_L.index('DK');
            DK_L.prefix(DK.ref(),null());
            {? HELP.POP & DK_L.first()
            || _sum:=0;
               _del:=0;
               {!
               |? _sum+=DK_L.IL;
                  {? _del
                  || DK_L.del()
                  |? _sum>_ile
                  || DK_L.IL-=(_sum-_ile);
                     DK_L.SCEAN:=DK.SCEAN;
                     DK_L.TW:=DK.TW;
                     DK_L.SCSQL:=DK.SRDK;
                     exec('reoIL2','magdok_wymiary');
                     DK_L.put(1);
                     _del:=1;
                     {? DK_L.IL<=0 || DK_L.del() ?}
                  || DK_L.SCEAN:=DK.SCEAN;
                     DK_L.TW:=DK.TW;
                     DK_L.SCSQL:=DK.SRDK;
                     DK_L.put(1);
                     DK_L.next()
                  ?}
               !}
            ?};
            DK_L.cntx_pop()
         ?}
      ?}
   || {? DK.DOST=date(0,0,0) || _wyn:=1 || FUN.info('Nie ma dostawy w tym dniu.'@) ?}
   ?};
   {? DK.DOST=date(0,0,0) & (DK.N().MAG().PAL<>'T' | DK.ROZPAK)
   || DK.AFIFO:=DK.N().TYP().AFIFO;
      {? DK.N().MAG().IL='T' & DK.AFIFO='' || DK.AFIFO:='F' ?}
   || DK.AFIFO:=''
   ?};
   __sc.ndx_drop(_ind);
   __sc.cntx_pop();
   {? DK.PLUS='N' & DK.DOST=date(0,0,0) & DK.M().SETW='P' & (';FL'*DK.N().TYP().AFIFO)>1
   || EANX.LOKP:='';
      EANX.EANL:=null();
      EANX.TW:=date(0,0,0);
      EANX.PAL:=null()
   ?};
:: ewentualne uzupełnienie danych dla rozpakowania do wydań
   {? EANX.PALR=null() || exec('pobPALsl','magdok_palety') ?}
?};
_wyn


\poprzpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF, MZ [2008]
:: OPIS: poprawienie ceny/ilosci/ktmu na zaakceptowanym dokumencie przychodowym
::   WE: [_a] $FAP.ref jeśli automatyczna wycena na podstawie dokumentu zakupu
::       [_b] 1 - poprawianie zaakceptowanego dokumentu powiazanego z robocizna (tylko cena), 0 - wpp
::       [_c] 1-tylko korekta atrybutów dostawy 0(domyślnie)-nie
::       [_d] 1-blokada edycji ceny 0-domyślnie
::       [_e] (auto) nowa cena>0-automatyczna wycena na podstawie podanej ceny 0(domyślnie) - kontekst DK
::   WY: dla (auto) 1-ok 0-nie
::  OLD: \poprzpoz/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};
{? var_pres('_b')=type_of(0) || _akord:=_b || _akord:=0 ?};
_zmatr:={? var_pres('_c')=type_of(0) || _c || 0 ?};
_blkcn:={? var_pres('_d')=type_of(0) || _d || 0 ?};

:: automatyczne uruchomienie
_res:=1;
_autwyc:=0;
_newprice:={? var_pres('_e')=type_of(0) || _autwyc:=1; _e || 0 ?};

VAR_DEL.delete('__dk_l','__odkc','__ndkc','__blkcen');

__blkcen:=_blkcn;
_oldkp:=DK.KP;
_change:=0;
_changem:=DK.M;

_ok:=1;
:: obsluga wprowadzonych wymiarow
DK_L.cntx_psh();
DK_L.index('DK');
DK_L.prefix(DK.ref(),null);
{? DK_L.first() & DK_L.size()=1
|| __onedk_l:=1;
   ZMIENNE.EANL:=DK_L.LOK().KOD
|| __onedk_l:=0;
   ZMIENNE.EANL:=''
?};
SC.cntx_psh();
SM.cntx_psh();
:: otwieram maske roczna tabeli stanow (dla SC jest otwarta zgodnie z parametrami pracy)
SM.use('stm__'+(5-DK.name));

SM.use('stm__'+ST.ODDZ+'zb');

exec('set_zmie','magdok_poz');

SC.index('SN');
SC.prefix(DK.N().MAG,DK.M,DK.RDK,DK.NDK);
SC.first();
SM.index('SM');
SM.prefix(DK.N().MAG,DK.M);
SM.first();
{? DK.WAL<>INFO.NAROD
|| ZMIENNE.WAL:=ND.WAL;
   ZMIENNE.SWAL:=ND.SWAL;
   ZMIENNE.DTK:=DK.N().DTK;
   ZMIENNE.RTK:=ND.RTK;
   ZMIENNE.NTK:=ND.NTK
?};
__odkc:=__ndkc:=DK.DK_C;
ATR.MOD:=~_autwyc & (DK.N().MAG().TYP*'DOST') & exec('isMOD','mat_atr',DK.M().M_ATR);
{? ATR.MOD
|| _ll:=exec('rozm_atr','mat_atr',DK.M().M_ATR);
   _olddkc:=obj_new(_ll);
   _newdkc:=obj_new(_ll);
   {! _ii.._ll
   |! _olddkc[_ii]:=($('DK.DK_C().WAR'+form(_ii,-2,0,'99')))();
      _newdkc[_ii]:=($('DK.DK_C().WAR'+form(_ii,-2,0,'99')))()
   !}
?};
ZMIENNE.JM:=DK.M().J;
ZMIENNE.J2:=DK.M().J2;
ZMIENNE.IL2:={? ZMIENNE.J2<>null() || DK.IL2 || 0 ?};
ZMIENNE.WS2:={? ZMIENNE.J2<>null() || DK.WS2 || 0 ?};

exec('zmienne_dk_win','magdok_poz',_zmatr);
:: buforuje DK_L-e
exec('buf__dkl','magdok_poz',DK.ref());
_auto:=0;
{? _a<>''
||
  FAP.cntx_psh();
  FAP.use(8+_a);
  FAP.prefix();
  {? FAP.seek(_a,8+_a) & (FAP.CN>0 | FAP.CWAL>0)
  ||
     _auto:=1;
     ZMIENNE.C:=FAP.CN$FAP.M().DOKL_C;
     ZMIENNE.CWAL:=FAP.CWAL$FAP.M().DOKL_C;
     {? ZMIENNE.CWAL & DK.KURS
     || ZMIENNE.C:=(ZMIENNE.CWAL*DK.KURS)$DK.M().DOKL_C
     ?};
     exec('ae_zm_ck','magdok_poz',_auto)
  ?};
  FAP.cntx_pop()
|? _autwyc
|| _auto:=1;
   ZMIENNE.C:=_newprice;
   exec('ae_zm_ck','magdok_poz',_auto)
?};

{? _auto | ZMIENNE.edit("exec('chk_zmie','magdok_poz')")
||
:: aktualizacja pozycji w tabeli pozycji dostaw magazynowych
   exec('dkdost_trig_on','magdok_poz','put');

   do();
   echo('Trwa poprawianie dostawy ...'@);
   {? DK.C<>ZMIENNE.C & ND.MAG().PAL='T' || exec('cen2pal','ceny_dok',DK.C,ZMIENNE.C) ?};
   _zmdkc:=0;
   {? ATR.MOD
   || {! _ii:=1.._ll |! _newdkc[_ii]:=($('ATR.WAR'+form(_ii,-2,0,'99')))() !};
      __ndkc:=exec('aktDK_C','magdok_wspolne',DK.M().M_ATR,_olddkc,_newdkc,__odkc);
      {? __ndkc<>null()
      || _zmdkc:=1;
         {? __ndkc=__odkc
         ||
::          musimy stwierdzić czy zmieniamy daną cechę, czy jednak tworzymy zupełnie nową
            {? ~exec('ctrlDK_C','mat_atr',__ndkc,DK.PRDK)
            ||
::             korygujemy tylko wartości atrybutów
               exec('FindAndGet','#table',DK_C,__odkc,,
                "_ll:=exec('rozm_atr','mat_atr',DK_C.M_ATR);
                 {! _ii:=1.._ll |! ($('DK_C.WAR'+form(_ii,-2,0,'99')))():=($('ATR.WAR'+form(_ii,-2,0,'99')))() !};
                 put(1)
                ",0);
               __ndkc:=null()
            ||
::             zakładamy nową cechę
               __ndkc:=exec('m_atr_sprdod','mat_atr',ATR.WAR01,ATR.WAR02,ATR.WAR03,ATR.WAR04,ATR.WAR05
                        ,ATR.WAR06,ATR.WAR07,ATR.WAR08,ATR.WAR09,ATR.WAR10
                        ,DK.DK_C().M_ATR,DK.DK_C().ZPARN);
               {? 'AZP'*DK.M().IDMOB
                 & (1+DK.N().MAG().TYP)='D'
               || _scean:=exec('newscean','magdok_poz',DK.M,DK.M().IDMOB
                           ,__ndkc,DK.DK_C().ZPARN,DK.ZL,'')
               ?};
               _zmdkc:=2
            ?}
         || _zmdkc:=2
         ?};
         {? DK.M().IDMOB='D' || exec('mkodkadd','kody_kresk',DK.M,DK.SCEAN,$DK.ref(),0)
         |? DK.M().IDMOB='A' || exec('mkodkadd','kody_kresk',DK.M,DK.SCEAN,$DK.DK_C,0)
         |? DK.M().IDMOB='P' || exec('mkodkadd','kody_kresk',DK.M,DK.SCEAN,$DK.ZPARN,0)
         |? DK.M().IDMOB='Z' || exec('mkodkadd','kody_kresk',DK.M,DK.SCEAN,$DK.ZL,0)
         ?}
      ?}
   ?};

   _czyil:=0;

:: poprawienie ilosci
   {? DK.IL<>ZMIENNE.IL | DK.FAP<>''
   || {? exec('FindInSet','#table','SM','SM',DK.M,DK.N().MAG,"SM.SD",,,0)+(ZMIENNE.IL-DK.IL)>=0
      || _czyil:=1;
         SC.cntx_psh();
         SM.cntx_psh();
         SC.use('stc__'+ST.ODDZ+ST.ROK);
         SM.use('stm__'+ST.ODDZ+ST.ROK);
         SC.index('SN');
         SC.prefix(DK.N().MAG,DK.M,DK.RDK,DK.NDK);
         SM.index('SM');
         SM.prefix(DK.N().MAG,DK.M);
         {? SC.first() & SM.first()
         ||
            SC.S+=ZMIENNE.IL-DK.IL;
            {? SC.S>=0
            || SC.A:='T';
               SC.put()
            || undo();
               FUN.info('Pomniejszenie ilości dostawy dla indeksu %1'
                  '\nspowoduje wystąpienie stanu ujemnego w magazynie %2'
                  '.\n\nOperacja została anulowana.'@[SC.M().KTM,SC.MAG().SYM]);
               _ok:=0
            ?};
            {? _ok=1
            ||
::             w rocznej masce SM
               SM.S+=ZMIENNE.IL-DK.IL;
               {? DK.STATS().KIND='O'
               || SM.S_O+=ZMIENNE.IL-DK.IL2;
                  SM.S_O2+={? ZMIENNE.M().J2<>null() || ZMIENNE.IL2-DK.IL2 || 0 ?}
               |? DK.STATS().KIND='N'
               || SM.S_N+=ZMIENNE.IL-DK.IL;
                  SM.S_N2+={? ZMIENNE.M().J2<>null() || ZMIENNE.IL2-DK.IL2 || 0 ?}
               ?};
               {? SM.S>=0
               || SM.put();
::                uzupelnianie ilosci w zbiorczej masce tabeli stanow
                  exec('stan_zb','magazyn_stan',SM.MAG,SM.M,ZMIENNE.IL-DK.IL,0,DK.N().AR
                   ,{? SM.M().J2<>null() || ZMIENNE.IL2-DK.IL2 || 0 ?})
               || undo();
                  FUN.info('Pomniejszenie ilości dostawy dla indeksu %1'
                     '\nspowoduje wystąpienie stanu ujemnego w magazynie %2'
                     '.\n\nOperacja została anulowana.'@[SM.M().KTM,SM.MAG().SYM]);
                  _ok:=0
               ?}
            ?};
            {? DK.N().TYP().P='T' & DK.N().TYP().Z='T'
            || _par:=exec('get','#params',600803,2);
               _rp:=exec('dk2zdnag','zamdst_rea',_par,DK.N,DK.ref(),DK.ZAM_RN,DK.ZAM_RP,DK.M,ZMIENNE.IL,DK.M,DK.IL);
               {? DK.ZAM_RP='' & _rp<>''
               || DK.ZAM_RP:=_rp;
                  DK.ZAM_RN:=exec('FindAndGet','#table',ZD_RP,_rp,,"$ZD_RN",'');
                  _change+=DK.put(1)
               ?}
            ?}
         || _ok:=0;
            undo();
            FUN.info('Nie znaleziono stanu magazynowego dla indeksu %1'
               '\ w magazynie %2.\n\nOperacja została anulowana.'@[SC.M().KTM,SC.MAG().SYM])
         ?};
         SM.cntx_pop();
         SC.cntx_pop();
::       Poprawianie ilosci wykonanej na zleceniu
         {? DK.N().TYP().ZLEC='T' & TYPYDOK.WYR='T'
         || ZL.cntx_psh();
            ZL.prefix();
            {? ZL.seek(DK.ZL)
            || ZL.ILDOK+=ZMIENNE.IL-DK.IL;
               {? ZL.ILDOK<0
               || undo();
                  FUN.info('Pomniejszenie ilości dostawy dla indeksu %1'
                    '\nspowoduje ujemny stan zlecenia %2'
                    '.\n\nOperacja została anulowana.'@[DK.M().KTM,ZL.SYM]);
                  _ok:=0
               || ZL.put()
               ?}
            ?};
            ZL.cntx_pop()
         ?}
      || undo();
         FUN.info('Pomniejszenie ilości dostawy dla indeksu %1'
           '\nspowoduje wystąpienie stanu dostępnego ujemnego w magazynie %2'
           '.\n\nOperacja została anulowana.'@[SM.M().KTM,SM.MAG().SYM]);
         _ok:=0
      ?}
   ?};

:: poprawienie cen
   {? _ok
   ||
      _m_old:=DK.M;
      _oldc:=DK.C;
      DK.M:=ZMIENNE.M;
      DK.OPAK_GEN:='N';
      _change+=DK.put();
      ZMIENNE.M:=_m_old;
      _dkref:=DK.ref;
      _zmtw:={? ZMIENNE.TW<>DK.TW || ZMIENNE.TW || ~~ ?};

      {? ZMIENNE.C<>DK.C | ZMIENNE.CWAL<>DK.CWAL | _zmtw<>~~ | _zmdkc
      || _oldc:=DK.C;
         {? _akord
         || _war:=(DK.IL*DK.C) $2;
            DK.C:=ZMIENNE.C;
            {? _zmtw<>~~ || DK.TW:=ZMIENNE.TW ?};
            DK.WAR:=(DK.IL*DK.C) $2;
            {? {? DK.put(1) || _change+=1; 1 || 0 ?} & DK.N().Z='T'
            || _war_diff:={? DK.M<>ZMIENNE.M || DK.WAR || (DK.WAR-_war) ?};
               exec('stan_zb','magazyn_stan',ND.MAG,DK.M,0,{? DK.PLUS='T' || 1 || -1 ?}*_war_diff,ND.AR,0)
            ?}
         |? _zmtw<>~~
         || DK.TW:=ZMIENNE.TW;
            _change+=DK.put(1)
         |? _zmdkc=2 & __ndkc<>null()
         || DK.DK_C:=__ndkc;
            {? DK.M().IDMOB='A' | (DK.M().IDMOB='D' & DK.M().IDZP)
            || DK.SCEAN:=exec('zwrdkc_m','material',$DK.DK_C,DK.M)
            ?};
            _change+=DK.put(1)
         ?};
         {? exec('zmcene','magdok_poz',DK.ref(),ZMIENNE.C,ZMIENNE.CWAL,,_oldc,_zmtw,_zmdkc,__ndkc)=0
         || undo();
            FUN.info('Zmiana ceny dostawy lub terminu ważności dla indeksu %1 nie powiodła się.\n\n'
                     'Operacja została anulowana.'@[DK.M().KTM]);
            _ok:=0
         |? _akord
         || exec('dk_sum','magdok_wspolne',ND.ref(),1)
         ?};
::       zmiana znacznika wyceny z kosztami
         {? _ok & ZMIENNE.C<>_oldc || exec('delWYCdk','faktury_wspolne',$DK.ref()) ?}
      ?};
      {? ~_ok || _res:=0 ?};
::    poprawa wszystkich pozycji jesli zmiana kursu

:: nuco poprawianie wszystkich pozycji zostało zablkowane
      {? (ZMIENNE.KRS<>DK.KURS | ZMIENNE.DTK<>DK.N().DTK) & DK.N().WAL<>INFO.NAROD;0
      ||
         ND.SWAL:=ND.NSWAL:=ZMIENNE.SWAL;
         ND.KRS:=ND.NKRS:=ZMIENNE.KRS;
         ND.DTK:=ND.NDTK:=ZMIENNE.DTK;
         ND.RTK:=ND.NRTK:=ZMIENNE.RTK;
         ND.NTK:=ND.NTK:=ZMIENNE.NTK;
         {? ND.put & ZMIENNE.KRS<>DK.KURS
         ||
            DK.cntx_psh;
            {? DK.first
            ||
               {!
               |?
                  {? DK.ref=_dkref
                  ||
                     DK.KURS:=ZMIENNE.KRS;
                     _change+=DK.put
                  ||
                     ZMIENNE.M:=DK.M;
                     DK.KURS:=ZMIENNE.KRS;
                     _ncen:=(DK.CWAL*DK.KURS)$DK.M().DOKL_C;
                     {? {? DK.put || _change+=1; 1 || 0 ?} & exec('zmcene','magdok_poz',DK.ref(),_ncen,DK.CWAL,,DK.C)=0
                     || undo();
                        FUN.info('Zmiana ceny dostawy dla indeksu %1'
                                 ' nie powiodła się.\n\nOperacja została anulowana.'@[DK.M().KTM]);
                        _ok:=0
                     ?};
::                   zmiana znacznika wyceny z kosztami
                     {? _ok & _ncen<>DK.C || exec('delWYCdk','faktury_wspolne',$DK.ref()) ?}
                  ?};
                  DK.next
               !}
            ?};
            DK.cntx_pop;
            DK.get
         ?}
      ?};
::    poprawa pozycji jesli zmiana kursu na dokumencie niewalutowym
:: nuco dodanie zmiany ceny na pozycji dokumentu z walutą dla
      {? ZMIENNE.KRS<>DK.KURS & (DK.N().WAL=INFO.NAROD | DK.N().WAL<>INFO.NAROD)
      ||
         ZMIENNE.M:=DK.M;
         DK.KURS:=ZMIENNE.KRS;
         _ncen:=(DK.CWAL*DK.KURS)$DK.M().DOKL_C;
         {? {? DK.put || _change+=1; 1 || 0 ?} & exec('zmcene','magdok_poz',DK.ref(),_ncen,DK.CWAL,,DK.C)=0
         || undo();
            FUN.info('Zmiana ceny dostawy dla indeksu %1 nie powiodła się.\n\nOperacja została anulowana.'@[DK.M().KTM]);
            _ok:=0
         ?};
::       zmiana znacznika wyceny z kosztami
         {? _ok & _ncen<>DK.C || exec('delWYCdk','faktury_wspolne',$DK.ref()) ?}
      ?};
      {? DK.IL<>ZMIENNE.IL
      || _rozn:=ZMIENNE.IL-DK.IL;
         _war:=DK.WAR;
         DK.IL:=ZMIENNE.IL;
         DK.WAR:=(DK.IL*DK.C) $2;
::       koryguje ilosc dodatkowa
         exec('aktmil2','magdok_poz');
         _change+=DK.put();
         exec('DK_HS_new4DK','statexam',DK.ref,'A','Poprawa ilości',,DK.STATS);
         _war_diff:=ND.WAR;
         exec('dk_sum','magdok_wspolne',ND.ref(),1);ND.get();
         _war_diff:=ND.WAR-_war_diff;
::       uzupelnianie wartosci w zbiorczej masce tabeli SM
         exec('stan_zb','magazyn_stan',ND.MAG,DK.M,0,_war_diff,ND.AR,0);
         {? _rozn>0
         || exec('akt_wgdk','zamsiw_wspolne',DK.N,DK.ref(),_rozn)
         || exec('aktu_rez','rezerwacje',DK.M,0,-_rozn)
         ?}
      ?};
::    uzupelnienie pol dla magazynow celnych
      {? DK.N().MAG().SKL<>'T'
      || DK.SCC:=DK.C;
         DK.SCWAR:=DK.WAR
      ?};
      _change+=DK.put();
      _ndlock:=ND_Z.R_LOCK;
      ND_Z.R_LOCK:='P';
      exec('obl_stan','magazyn_stan',DK.M,1,DK.N().MAG);
      ND_Z.R_LOCK:=_ndlock
   ?};

:: poprawianie wymiarow
:: poprawiamy DK_L-a i SL-a
   {? _ok & _czyil & DK.N().MAG().SL='T'
   ||
::    najpierw sciagamy stara rozpiske
      {? __dk_l.first()
      || {!
         |? _lok:=exec('FindAndGet','#table','EANL',__dk_l.EANL,,,null());
            _pal:=exec('FindAndGet','#table','PAL',__dk_l.PAL,,,null());
            exec('adddelsl','magazyn_stan',DK.N().MAG,DK.M,_lok,__dk_l.TW,__dk_l.IL,-1,_pal,DK.SCEAN,__dk_l.IL2);
            __dk_l.next()
         !}
      ?};
::    teraz wnosimy nowa rozpiske
      DK_L.index('DK');
      DK_L.prefix(DK.ref(),null);
      {? DK_L.first()
      || {!
         |? {? DK_L.IL=0
            || DK_L.del()
            || DK_L.Z:=DK.N().Z;
               DK_L.DT:={? DK.N().Z='T' || DK.N().DA || DK.N().D ?};
               DK_L.TM:={? DK.N().Z='T' || DK.N().TA || time() ?};
               DK_L.US:=DK.N().US;
               DK_L.put(1);
               exec('adddelsl','magazyn_stan',DK.N().MAG,DK.M,DK_L.LOK,DK_L.TW,DK_L.IL,1,DK_L.PAL,DK.SCEAN,DK_L.IL2);
               DK_L.next()
            ?}
         !}
      ?}
   ?};
:: kontroling
:: DRO:[rr] schematy.fml
::   exec('aktualizacja_mp','schematy',DK.N,DK.ref(),'U');
::   exec('aktualizacja_mp','schematy',DK.N,DK.ref(),'D');

:: naliczenie opłat dodatkowych
   {? _change || exec('actTAXS','oplaty_dod',DK.N().uidref(),DK.uidref(),DK.M,_changem) ?};
   end();
   {? _ok & _oldkp<>DK.KP || exec('act_kp','magdok_wspolne',DK.PRDK,DK.KP,_oldkp) ?};

   exec('dkdost_trig_off','magdok_poz');

:: przy nieudanej wycenie poprzednie DK_L
   {? ~_ok & DK.N().MAG().SL='T'
   || exec('unbufdkl','magdok_poz',DK.ref())
   ?};

:: rozpisanie rezerwacji
   {? _ok=1 & _czyil=1
   || exec('aktu_stu','zamsiw_wspolne',DK.M)
   ?};
   exec('obl_stan','magazyn_stan',DK.M,1,DK.N().MAG);
   echo()
||
:: przy rezygnacji przywraca poprzednie DK_L
   exec('unbufdkl','magdok_poz',DK.ref())
?};
undefine();
ATR.MOD:=0;
SC.cntx_pop();
SM.cntx_pop();
DK_L.cntx_pop();
VAR_DEL.delete('__dk_l','__odkc','__ndkc','__blkcen');
_res


\pos_dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: rekord przed dla DK w okienkach wertowania
::  OLD: \pos_dk/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? DK.M<>null
|| POMOC.RODZ:=DK.M().RODZ
?};
_no_et:={? ~exec('isDEFetyk','magazyn_mobi','DK') || 'N(T)' || '' ?};

exec('obl_mwb','magdok_poz');
POMOC.OPAKOW:='N';
HELP.POS:=DK.P;
{? (1+DK.N().MAG().TYP)='D' & DK.PLUS='N' & DK.N().Z='N' & DK.RDK & DK.NDK<>''
|| _dost:=date(0,0,0);
   ND.cntx_psh(); DK.cntx_psh();
   DK.use(DK.NDK); DK.clear(); {? DK.seek(DK.RDK,) || _dost:=DK.DOST ?};
   ND.cntx_pop(); DK.cntx_pop();
   {? _dost<>date(0,0,0) & _dost<>DK.DOST || DK.DOST:=_dost; DK.put(1) ?}
?};
_wer:=DK.win_sel('?');
{? ';WER_DOK;WER_GDOK;WERL;'*(';'+_wer+';')
|| _czy_akc:=ND.Z='T' | (';ZT'*ND.STAT_REJ)>1;
   {? DPOZ.POZ | _wer='WERL'
   ||
::    wyłączone
      _act:=':';
      {? DK.PLUS='T' || _act:='G'+_act+'G' ?};
      {? DK.N().FAKS<>null | (DK.N().INN='T' & DK.N().TYP().INW<>'N') || _act:='W'+_act ?};
      {? DK.N().MAG().U<>'T' | DK.PLUS='T' | (DK.PLUS='N' & DK.N().TYP().TD<>'') || _act:='G'+_act+'G' ?};
      {? ~(DK.N().MAG().TYP*'DOST') | DK.N().TYP().P<>'T' || _act:='R'+_act ?};
      {? DK.N().MAG().SL<>'T' || _act:='M'+_act ?};
      {? BEER.MENU_PTH<>'ZZRRD' & BEER.MENU_PTH<>'ZZTRRD' & BEER.MENU_PTH<>'ZRD'
       & 'ŚE'*(1+DK.N().MAG().TYP)>0 & _czy_akc
      || _act:='P'+_act
      ?};
      {? BEER.MENU_PTH+1='D'
      || _act:='DR'+_act+'D';
         {? DK.N().Z='T' | (';ZT'*ND.STAT_REJ)>1
         || {? exec('get','#params',1003,2,OPERATOR.USER)<>'T' | DK.PLUS='N'
            || _act:='P'+_act
            ?}
         ?}
      ?};
      {? ND.ZAK='T' || _act:='N(P)WP'+_act ?};
      {? DPOZ.NAG=0 || _act:='TN(WP)'+_act ?};
      {? DK.N().MAG().PAL='T' || _act:='D'+_act+'D' || _act:='T'+_act+'T' ?};
      {? DK.N().TYP().P='N'
      || _act:='F'+_act
      || {? exec('get','#params',540101,2,null())<>'T'
          | ~exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_DWYK')
         || _act:='F(B)'+_act
         ?};
         {? ~exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_EAKC')
         || _act:='F(M)'+_act
         ?}
      ?};
      {? DK.win_sel('?')='WERL'
      || _wer:='WER';
         DK.actions('WER',_act,,1)
      || _wer:='WER_DOK';
         DK.actions('WER_DOK',_act,,1)
      ?};

::    wyszarzone
      _act:=_no_et+
            {? DK.N().Z='T' | (';ZT'*ND.STAT_REJ)>1
            || {? exec('get','#params',1003,2,OPERATOR.USER)='T' & DK.PLUS='T'
               || 'D'+'U'+   +'T'+'C'+'R'+'G'+':'+'D'+'G'
               || 'D'+'U'+'P'+'T'+'C'+'R'+'G'+':'+'D'+'G'
               ?}
            || 'W'
            ?};
      {? DK.N().STAT_REJ='N' || _act:='A'+_act
      |? DK.N().STAT_REJ='Z' || _act:='ZN(P)'+_act
      |? DK.N().STAT_REJ='T' || _act:='ZAN(P)'+_act
      ?};
::----------------------------------------------------------------------------------------------------------------------
::  MOD: PeKa [28.02.2021]
:: OPIS: Wylaczenie akcji w dokumencie MP - brak popraw/usun/dodaj - przeniesiono z Xpertisa
      {? ND.NDSQL<>'' & ND.TYP().P='T' & exec('nie_zdok','magdok_wspolne',ND.NDSQL)<>1
      ||
         _act+='REN(KCYSRE)N(YSC)WAOGCIF(MB)DPU'
      ?};
::----------------------------------------------------------------------------------------------------------------------
      DK.actions_grayed(_wer,_act)
   ||
::    wyłączone
      _act:='';
      {? DPOZ.NAG=0 || _act:='ZTN(WP)'+_act ?};
      _act:=_act
         +{? DK.N().MAG().SL<>'T' & {? DK.N().MD=null || 1 || DK.N().MD().PAL<>'T' ?} || 'M' || '' ?}
         +'DUPACGWRHFE:DAGW';
      DK.actions('WER_DOK',_act,,1)
   ?}

?};
exec('dkr_okna','magdok_poz',DK.M().RODZ='U');
_prod:=exec('tte_lic','tte');
{? DISP.DISP
|| _allact:=exec('allActions','#string','wsSL');
   DK.actions_grayed('WER_DOK',_allact)
?};
{? DK.PLUS='N' & DK.N().TYP().DK='W'
|| _allact:=exec('allActions','#string','wsSLH');
   DK.actions_grayed('WER_POZ',_allact)
|? (';KR+;KR-;'*DK.N().TYP().T)>1
|| _allact:=exec('allActions','#string','wsSLH');
   DK.actions('WERU',_allact)
?};
:: okienko dokumentu z poziomu realizacji
{? ~DISP.DISP & ';WER_DOK;WER_POZ;WER_REA;WER_GREA;WERR;WERGR;WERUR;'*_wer
|| _koop:=AreaTitle.area='TTE_KOO';
   _cact:={? ~_koop & DK.PLUS='T' || 'LMG_MAG_DAPZ'
          |? ~_koop & DK.PLUS='N' || 'LMG_MAG_DWYD'
          |?  _koop & DK.PLUS='T' || 'TTE_WYK_DKOW'
          |?  _koop & DK.PLUS='N' || 'TTE_WYK_DKOP'
          || 'LMG_MAG_DAPZ'
          ?};
   _edit:=exec('chk_role','#b__box',OPERATOR.USER,_cact);
   _wyco:=exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_EWMG');
   _stat:=DK.N().STAT_REJ;

   _aoff:=':';
   _gray:='';

   {? DK.N().MAG().PAL='T'  || _aoff:='RDGO'+_aoff+'DO' || _aoff:='T'+_aoff+'T' ?};
   {? DK.N().MAG().SL<>'T'  || _aoff:='M'+_aoff ?};
   {? DK.N().MAG().U<>'T'
    | DK.N().TYP().ZLEC='T' || _aoff:='G'+_aoff+'G' ?};
   {? DK.PLUS='T'           || _aoff:='GO'+_aoff+'GO' ?};
   {? ~_edit                || _aoff:='DTGPUO'+_aoff+'DTG' ?};
   {? (1+DK.N().MAG().TYP)<>'D' | DK.N().TYP().P<>'T' || _aoff:='R'+_aoff ?};
   {? (+_aoff)=1            || _aoff:='' ?};

   {? ~_edit | _stat<>'N'   || _gray:='ZUDTGPUO'+_gray ?};
   {? _stat='N'
     | (~_edit & ~_wyco)    || _gray:='AN(C)'+_gray
   |? _stat='T'             || _gray:='RA'+{? ~_wyco || 'N(C)' || '' ?}+_gray
   |? _stat='Z'             || _gray:='R'+{? ~_edit || 'N(C)' || '' ?}+_gray
   ?};
:: dokument powiązany z wyposażeniem
   {? exec('FindInSet','#table','KARO','GENDK',$DK.ref(),,,1) || _gray:='P'+_gray ?};
   DK.actions('WER_REA',_aoff,,1);
   DK.actions_grayed('WER_REA',_gray)
?};
{? DK.win_sel('?')='WERGR' | DK.win_sel('?')='WER'
||
   DISP.C:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C);
   exec('rekprzed','color','DKK#01')
|? _prod='T'
 & ((' '+exec('get','#params',500701,2,null()))*(' '+TYPYDOK.T+' ')>0)
 & DK.win_sel('?')='WER_DOK'
 & ((' '+exec('get','#params',500704,2,null()))*(' '+ST.TYPYDOK().T+' ')>0)
|| exec('rekprzed','color','DK#02')
?}


\dks_okna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: ustawienie okien select dla pozycji dokumentu magazynowego
::   WY: akronim niegrupowego okna wertowaia tabeli DK
::  OLD: \dks_okna/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_usl:='';_act:='';_d_act:='';

_czy_inp:=(ND.INN='T' & ND.TYP().INW<>'N');
:: czy badania
_badan:={? ND.MAG().SKL<>'T' & exec('get','#params',540101,2,null())='T' & ND.TYP().KOOP<>'T' & ND.TYP().WYR<>'U'
        || ~(_czy_inp)
        || 0
        ?};

_czy_akc:=ND.Z='T' | (';ZT'*ND.STAT_REJ)>1;
_odakczz:=~_czy_akc & ND.TYP().P='T' & ND.TYP().Z='T' & ND.TYP().DN<>'N' & ND.F='T' & exec('spr_zz','opakow');

{? _odakczz
|| _act:='PMENDAGUTOZRCFC:DAEGOCNP'
|? HELP.DKRED='N' | exec('czy_z_ok','okresy',1,0)=0 | exec('czy_z_ok','okresy',1,0,ND.AR,ND.AM,ND.MAG)=0 |
   ~exec('spr_inwe','magdok_wspolne',0)
||
   _act:=
      {? ND.MAG().SL<>'T' || 'M' || '' ?}+
      {? (ND.MAG().TYP*'EWI') & (ND.INN='T' & ND.TYP().INW='I') || '' || 'P' ?}+
      {? (ND.INN='T' & ND.TYP().INW='I' & ND.TYP().P='T') & ND.MAG().TYP*'DOST' || '' || 'F' ?}+
      'WDAGUTOZRC:DAGOC'
||
   {? ((ND.TYP().P='N' & ND.MAG().U='T' & ND.TYP().ZLEC<>'T')
         |
      ((TYPYDOK.UPRW='T' ) & ND.MAG().TYP*'DOST')) & ND.TYP().KOOP<>'T'
   || _usl:='U'
   |? ND.TYP().KOOP='T'
   || _usl:='_KOO'
   ?};

   {? ND.MAG().SKL='T'
   || _usl:='SC'
   ?};
:: ustawianie akcji domyslnej
   _d_act:='D:D';
   _p_act:='';

:: wylaczanie akcji dolacz
   {? _czy_akc
    | (ND.MAG().PAL='T' & ~exec('isB_ROZP','magdok_palety'))
    | BEER.REORG
   || _act+='DR'+{? _usl='U' & ~_czy_akc || '' || 'G' ?}; _p_act+='D'+{? _usl='U' & ~_czy_akc || '' || 'G' ?}
   ?};
:: wylaczenie akcji z palet
   {? _czy_akc | ND.MAG().PAL='N' | BEER.REORG || _act+='A'; _p_act+='A' ?};
:: wylaczenie akcji usun
   {? _czy_akc | BEER.REORG || _act+='U'?};

:: wylaczanie akcji popraw
   _tmg:=1+ND.MAG().TYP;
   _ewi:=ND.MAG().TYP*'EWI';
   _czyzero:=exec('get','#params',600104,2)='T';
   _param:=exec('get','#params',1003,2,OPERATOR.USER);
   _kor:=exec('czy_kor','magdok_wspolne',$ND.ref());
   {? _param='T' & _czy_akc & ~exec('chk_wyc','magdok_wspolne',ND.D,0,,ND.T) || _param:='N' ?};
   {? ND.ZAK='T' | _kor | ND.INTRA='T' |
      (_czy_akc & (ND.TYP().P='N' | ND.MAG().SKL='T' | ('Ś'*_tmg)>0 | (~_czyzero & _ewi) | _param<>'T'
        | {? ND.TYP().P='T' || exec('nie_zdok','magdok_wspolne',ND.NDSQL,1)=0 || 0 ?}))
   || _act+='P'
   ?};

   {? ~(ND.MAG().TYP*'DOST') | ND.TYP().P<>'T' | ND.Z<>'N' || _act+='R' ?};

:: wylaczenie akcji dolaczenia z rezerwacji tymczasowych
   {? ND.MAG().PAL='T' | ND.MAG().SKL='T' | ND.TYP().P='T' | ND.TYP().ZLEC='T' | ND.TYP().TD<>'' | _czy_akc | ND.ZAK='T'
    | _kor | ND.TYP().Z='N' | BEER.REORG
   || _act+='O'; _p_act+='O'
   ?};
   {? ND.MAG().U<>'T' | ND.TYP().Z<>'T' || _act+='G';_p_act+='G' ?};
:   {? ND.TYP().UE<>'T' || _act+='C' ?};

:: wylaczenie kontrolingu
   _act:='W'+_act;

:: wylaczenie akcji Popraw dla zamknietego magazynu
   {? ~exec('czy_z_ok','okresy',1,0,ST.AR,ST.AM,ST.MAG) || _act+='p' ?};

:: wylaczenie kosztow dla korekt wartosciowych
   {? ND.TYP().DK='W' || _act+='Z' ?};

:: wylacznie naliczania gratisow, szczegoly cennikow, przeliczanie wg cennikow
   {? ND.TYP().P='T' || _act:='N(YSC)'+_act
   |? ND.TYP().P='N' & _czy_akc || _act:='N(CYR)'+_act
   ?};

:: dla korekt
   {? ND.TYP().DK='T' || _act+='P' ?};

:: wylaczenie obslugi zlecen
   _prod:=exec('tte_lic','tte');
   {? ND.TYP().ZLEC<>'T' | ND.TYP().WYR='T' | _prod='N' | _czy_akc |  ND.TYP().P<>'N'
   || _act+='C'; _p_act+='C'
   ?};
   {? ND.TYP().ZLEC<>'T' | ND.TYP().WYR='N' | _prod='N' |  ND.TYP().P<>'T'
   || _act+='I'; _p_act+='I'
   ?};
:: dla wymiarow
   {? ND.MAG().SL<>'T' & {? ND.MD=null || 1 || ND.MD().PAL<>'T' ?}
    & ~(ND.MAG().PAL='T' & (ND.INN='T' & ND.TYP().INW='I'))
   || _act+='M'
   ?};

:: wylaczenie kwalifikacji dostaw
   {? (1+ND.MAG().TYP)<>'D'
    | ND.TYP().P<>'T'
    | {? ND.MAG().DEFSTATS=null() || FUN.info('Proszę ustalić domyślny status dostawy dla magazynu.'@); 1 ?}
   || _act+='F'
   ?};

   {? exec('get','#params',540101,2,null())<>'T'
    | ~exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_DWYK')
   || _act+='F(B)'
   ?};

   {? ~exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_EAKC')
   || _act+='F(M)'
   ?};

:: wylaczenie funkcji [przelicz wg Cennikow], [nalicz gratisY], [udziel Rabatu], [Szczegoly cennika/gratisu]
   {? ND.TYP().CS='N'
   || _act:='N(CYSR)'+_act
   |? ~(ND.TYP().P='N' & ND.TYP().DS='T')
   || _act:='N(CYR)'+_act
   ?};
   {? _p_act<>'' || _act:=_act+':'+_p_act ?}

?};
:: akcja przypisz zlecenie
{? ~exec('czyAddZL','magdok_poz') || _act:='E'+_act ?};
:: Dane do Intrastat
_dane_INTR:={? ND.IST_TYP='' || ~((exec('get','#params',100205,2,null())*('%1 '[ND.TYP().T]))>0) || 0 ?};
{? _dane_INTR || _act:='N(E)'+_act ?};

DK_C.win_dict('WER');
KK.win_dict('SLO');

_cos:=ND.MAG().COS='T';
_win:='WER';
_result:=_win;
DK_L.actions('WER',{? _czy_akc || 'dup:d' || {? ND.MAG().PAL='T' || 'd:d' || '' ?} ?});

{? ND.MAG().PAL='T' || _d_act:='A:A' ?};

{? (ND.TYP().P='N' & ND.TYP().DK='W') | ND.TYP().DK='T'
|| _result:='WER_POZ';
   DK.win_sel(_result)

|? ND.TYP().CS='T' | _cos
||
   _win:='WER'+_usl;
   _result:=_win;
:: dokumenty powiazane ze sprzedaza
   _sel:=DK.grp_make('Pozycje dokumentu magazynowego'@,,'_dks_okna_rcs_');
:: wartosci magazynowe
   DK.actions(_win,_act,_d_act);
:: wersja branżowa
   {? exec('get_par','#parametr',141)='T' & _badan
    & ND.MAG().SKL='T' & ND.MAG().MG_OPAK=''
   || DK.grp_sel(_sel,DK,_win,'Wartości magazynowe'@,"exec('badp_sel','statexam')",,,
                 ,"exec('po_weru','magdok_poz')",,,,,'maximized');
      DK.tab_splt(_sel,,'vertical','panel1');
      DK.grp_sel(_sel,BADP,'WERDKR',,,,,,"exec('po_weru','magdok_poz')")
   || DK.grp_sel(_sel,DK,_win,'Wartości magazynowe'@,"",,,,"exec('po_weru','magdok_poz')",,,,'maximized')
   ?};
:: wartosci sprzedazy (pomijane dla skladu - brak okienka 'WESSC')
   {? ND.MAG().SKL<>'T'
   ||
      DK.actions('WES'+_usl,_act+{? _cos || 'DP' || '' ?},_d_act);
      DK.grp_sel(_sel,DK,'WES'+_usl,'Wartości sprzedaży'@,,,,,"exec('po_wesu','magdok_poz')",,,,'maximized')
   ?};
:: opakowania
   {? ND.MAG().MG_OPAK<>''
   || DK.grp_sel(_sel,OPAK_P,'WER','Opakowania'@,,,,,"exec('opak_wer','opakow')"
                 ,"exec('opak_out','opakow')",,,'maximized')
   ?};
   DK.win_sel(_sel)
||
   _win:='WER'+_usl;
   _result:=_win;
:: rejestracja realizacji usług
   {? ND.TYP().WYR='U'
   || _act:='DPU'+_act
   ?};

   {? ND.MAG().MG_OPAK<>'' & (ND.TYP().Z='T' | ND.TYP().T='BO')
   ||
::    okno grupowe z dodatkowa zakladka dot. opakowan
      DK.win_sel('WERGR'+_usl);
      {? _odakczz || tab_sel(2) ?}
   || {? (_win='WER' | (ND.INN='T' & ND.TYP().INW<>'N') | ND.MAG().SL<>'T')
       & {? ND.MD=null || 1 || ND.MD().PAL<>'T' ?} & ~(ND.MAG().PAL='T' & (ND.INN='T' & ND.TYP().INW='I'))
      || _act:='M'+_act
      ?};
      DK.win_sel(_win+{? _win='WER' & ~(ND.INN='T' & ND.TYP().INW<>'N')
                       & (ND.MAG().SL='T' | {? ND.MD=null || 0 || ND.MD().PAL='T' ?}) || 'L' || '' ?}
                       +{? _badan || 'R' || ''?})
   ?};
:: pozostale dokumenty
   DK.actions(_win,_act,_d_act,1)
?};
:: drag&drop - zmiana pozycji tylko dla niezaakceptowanych przychodowych
{? ~_czy_akc & ND.TYP().P='T'
||
   _dnd_fml:="exec('renumdk','magdok_wspolne')"
||
:: nalezy wylaczyc d&d dla okienka
   _dnd_fml:=""
?};
::----------------------------------------------------------------------------------------------------------------------
::  MOD: PeKa [28.02.2021]
:: OPIS: Wylaczenie drag&drop w dokumencie MP - przeniesiono z Xpertisa
{? _dnd_fml<>"" & ND.NDSQL<>'' & ND.TYP().P='T' & exec('nie_zdok','magdok_wspolne',ND.NDSQL)<>1
||
   _dnd_fml:="";
   ~~
?};
::----------------------------------------------------------------------------------------------------------------------
DK.dnd_sel(_win,,'records.DK',_dnd_fml);
_formikon:="{? ATR.INDATR='X'
            || ATR.INDATR:=''; 'xwin16.png:15'
            |? ATR.INDATR='N'
            || ATR.INDATR:=''; 'xwin16.png:91'
            |? ATR.INDATR='1'
            || ATR.INDATR:=''; 'xwin16.png:83'
            |? ATR.INDATR='2'
            || ATR.INDATR:=''; 'xwin16.png:184'
            |? ATR.INDATR='3'
            || ATR.INDATR:=''; 'xwin16.png:120'
            |? ATR.INDATR='4'
            || ATR.INDATR:=''; 'xwin16.png:49'
            |? ATR.INDATR='5'
            || ATR.INDATR:=''; 'xwin16.png:17'
            || ATR.INDATR:=''; ''
            ?}";
DK.win_fml('WER',ATR,'INDATR',,'ICON_BEFORE',_formikon);
DK.win_fml('WER_DOK',ATR,'INDATR',,'ICON_BEFORE',_formikon);
DK.win_fml('WER_KOO',ATR,'INDATR',,'ICON_BEFORE',_formikon);
_formikon:="{? exec('FindInSet','#table','FAP_K','TAXS',DK.uidref())<>null()
            || 'xwin16.png:10'
            || exec('pusta','#icon')
            ?}";
DK.win_fml('WER',DK,'WAR',,'ICON_BEFORE',_formikon);
DK.win_fml('WER_DOK',DK,'WAR',,'ICON_BEFORE',_formikon);
DK.win_fml('WER_REA',DK,'WAR',,'ICON_BEFORE',_formikon);
exec('dk_p_ib','magdok_poz',_result);
_result


\ae_m_dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji zmiennej material/usluga w pozycjach dok. magazynowego
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_prod:=exec('tte_lic','tte');
_pomoc_mgr:=POMOC.MGR;
ATR.MJS:='DK';

{? fld()=''
|| D_HELP.MR:=null;
   {? POMOC.RODZ='U'
   || exec('find_sql','#table','M','KTM;N;KODK','M.A=\'\'T\'\' and M.RODZ=\'\'U\'\'','D_HELP.MR'
            ,'20@Kod materiału;40@Nazwa materiału;20@Kod kreskowy;','REDU','','')
   ||
      {? DK.N().TYP().P='T'
      || exec('find_sql','#table','M','KTM;N;KODK;*(R)IL','M.A=\'\'T\'\' and M.RODZ=\'\'T\'\'','D_HELP.MR'
            ,'20@Kod materiału;40@Nazwa materiału;20@Kod kreskowy;12@Dostępne;','RED','obl_stm#magazyn_stan'
            ,{? DK.N().TYP().P='T' || '' || '*IL' ?})
      || exec('find_sql','#table','M','KTM;N;KODK;*(R)IL','','D_HELP.MR'
            ,'20@Kod materiału;40@Nazwa materiału;20@Kod kreskowy;12@Dostępne;'
            ,'RED','obl_stm#magazyn_stan',{? DK.N().TYP().P='T' || '' || '*IL' ?})
      ?}
   ?};
   D_HELP.M:=D_HELP.MR().KTM; {? D_HELP.MR<>null || POMOC.MGR:=D_HELP.MR().MGR ?}
?};

_idmob:=exec('FindInSet','#table','M','MATKTM',fld(),,"M.IDMOB",,,'');
_old_ind:=DK.M().KTM;
{? fld()<>'' & (1+ND.MAG().TYP)='D' & ND.TYP().P='T' & ND.TYP().DN<>'T'
 & (ND.TYP().ZLEC<>'T' & _idmob<>'' & ';ZP'*_idmob)
|| FUN.info('Wybrany indeks materiałowy wymaga identyfikacji wg zlecenia lub parti produkcyjnej.\n'
    'Ponieważ typ dokumentu nie pozwala na podanie zlecenia.\nWybranie indeksu niemożliwe.'@);
   _wyn:=0
|? fld()<>'' & (ND.TYP().WYR<>'T' & DK.ZL=null  | _prod='N')
||
   _wyn:=0;
   DK.cntx_psh();
   M.cntx_psh();
   {? DK.PLUS='T'
   || M.index('ARODZ');
      M.prefix('T',POMOC.RODZ)
   || M.index('RODZ');
      M.prefix(POMOC.RODZ)
   ?};
   _mat:=null;_gru:=null;_jm:=null; _inw:=0; _jmg:=null();

   {? M.find_key(fld)
   || {? ((ND.TYP().P='T' & ND.TYP().DN='K') | (ND.TYP().P='N' & ND.TYP().DN='D'))
       & (POM.SKLAD:=0; exec('m_sklad_','material',ND.D); ~POM.SKLAD)
      || FUN.info('Wybrany indeks materiałowy nie posiada składowych.\n'
          'Wybranie indeksu dla dokumentu typu przychód kompletu niemożliwe.'@);
         _wyn:=0
      || _inw:=exec('open_inw','magdok_wspolne',ND.MAG,M.ref,ND.TYP().P='T');
         {? _inw & ND.MD<>null || _inw:=exec('open_inw','magdok_wspolne',ND.MD,M.ref,ND.TYP().P='T') ?};

         _prz:=exec('sprprzec','magazyn_przec',ND.MAG,ND.D,M.ref(),ND.T);
         {? _prz<3 & ND.MD<>null || _prz:=exec('sprprzec','magazyn_przec',ND.MD,ND.D,M.ref,ND.T) ?};

         {? _inw>0 & _prz<3
         || _gru:=M.MGR; _grun:=M.MGR().KOD; _ok:=1;
            POMOC.MGR:=M.MGR;
            _ok:=exec('mx_add','magdok_wspolne');
            {? _ok
            ||
               _mat:=M.ref();
               _jm:=M.J;
               _jmg:=M.J2;
               _wyn:=1;
               POMOC.MGR:=M.MGR;
               D_HELP.M:=M.KTM
            ?}
         || {? _prz=3
            || FUN.info('Podany indeks materiałowy znajduję się na otwartym arkuszu przecen\n'
                '(data dokumentu jest mniejsza lub równa dacie przeceny).\n'
                'Dołączenie do dokumentu niemożliwe.'@)
            |? _prz=4
            || FUN.info('Podany indeks materiałowy znajduję się na zamkniętym arkuszu przecen\n'
                '(data dokumentu jest mniejsza lub równa dacie przeceny).\n'
                'Dołączenie do dokumentu niemożliwe.'@)
            ?};
            POMOC.MGR:=null;
            D_HELP.M:='';
            _wyn:=0
         ?}
      ?}
   || FUN.info('Brak pozycji w słowniku.'@);
      POMOC.MGR:=null;
      D_HELP.M:='';
      _wyn:=0
   ?};
   M.cntx_pop();
   DK.cntx_pop();

   DK.M:=_mat; DK.M().KTM;
   DK.JM:=_jm;
   {? _jmg<>null() || DK.J2:=_jmg ?};

   {? _old_ind<>D_HELP.M
   || {? DK.PLUS='N' & DK.IL>0
      || exec('obl_stan','magazyn_stan',DK.M,1,ST.MAG);
         {? DK.IL>BEER.SD || HELP.IL:=0; DK.IL:=BEER.SD ?}
      ?};
      DK.T2:={? DK.M().J2<>null() || 'M' || 1+(1-BEER.MMJM) ?};
      DK.J2:={? DK.M().J2<>null() || DK.M().J2 || null() ?};
      DK.WS2:={? DK.M().J2<>null() || exec('oblWSP','jm',DK.M) || 0 ?};
      DK.IL2:=0;
      EANX.TW:=date(0,0,0)
   ?};
   exec('ustawprz','magdok_poz',_mat,_jm);
:: dla uproszczonego wystawiania te same jednostki
   {? ND.TYP().UPRW='T' & ~HELP.POP || DK.J2:=DK.JM; DK.WS2:=1 ?};
   {? (DK.M<>null & DK.SV=null | _old_ind<>D_HELP.M  ) & (DK.PLUS='N' | DK.N().TYP().DN='T')
   || exec('dk_sv','magdok_poz')
   ?};
   {? _old_ind<>D_HELP.M & (DK.PLUS='N' | DK.N().TYP().DN='T')
   || DK.DOST:=date(0,0,0);
      DK.RDK:=0;
      DK.NDK:='';
      DK.SRDK:='';
      DK.PRDK:='';
      DK.C:=0;
      DK.WAR:=0
   ?};

:: sprawdzenie zmiany indeksu przy magazynie ewidencyjnym
   {? (_old_ind<>D_HELP.M | DK.PLUS='N') & (DK.N().MAG().TYP*'EWI')
   || DK.C:=exec('biez_cen','ceny_dok',DK.M,ST.MAG,DK.N().D);
      DPOZ.OCZ:=DK.C:=DK.C $DK.M().DOKL_C;
      DK.WAR:=(DK.IL*DK.C)$2
:: NUCO - dodanie blokady na likwidowanie ceny przy wykonywaniu KRP z kopii KRR
   |? _old_ind<>D_HELP.M & DK.PLUS='T' & DK.N().TYP().T<>'KRP'
   || DK.C:=DK.WAR:=0
   ?};

:: sprawdzenie inwentury
   {? _wyn=1 & exec('sprd_inp','magazyn_inw',DK.M,DK.N().D,,,DK.N().T)
    & {? DK.N().MD<>null || exec('sprd_inp','magazyn_inw',DK.M,DK.N().D,DK.N().MD,,DK.N().T) || 1 ?}
   || DPOZ.M:=HELP.KTM:=DK.M;
      {? DK.N().TYP().DS='T'
         || {? DK.SV=null
            || exec('dk_sv','magdok_poz')
            ?}
      ?};
      {? DK.N().TYP().P='N' || _wyn:=exec('prop_zam','magdok_poz') ?};
      {? _wyn=1 || _wyn:=exec('mx_add','magdok_wspolne') ?}
   || POMOC.MGR:=null();
      _pomoc_mgr:=null();
      _wyn:=0
   ?};

   _windisp:=0;

   {? _wyn & HELP.MOD_IND<>DK.M & DK.N().TYP().DS='T'
   ||
::    zerowanie wartosci sprzedazowych
      DK.CWAL:=DK.CN:=DK.CB:=DK.RAB:=DK.RABK:=0;
      exec('clr_promo_tap','ceny');
      _windisp:=_wyn:=exec('obl_wars','magdok_poz',,DK.N().RAB)
   ?};
:: intrastat
   {? _wyn=1 & DK.M<>null & D_HELP.M<>_old_ind
   ||
      DK.KP:='';
      exec('istatr_nd2dk','magdok_poz',1);
      exec('ist_wart_dk','magdok_poz',1);
      _windisp:=1
   ?};
   exec('obl_mwb','magdok_poz');
   {? _windisp
   ||
      exec('set_efld_opt','magdok_poz');
      win_disp
   ?}
|? fld()<>'' & ND.TYP().WYR='T' & _prod='T'
|| _czy_wyr:=exec('nd_is_rp','magdok_wspolne',DK.N)>0 & exec('zl_czy_wyr','zl_wyr',DK.ZL)>0;
   {? (_czy_wyr=0 & fld()<>DK.ZL().KTM().KTM) | (_czy_wyr>0 & fld()<>DK.M().KTM)
   || M.cntx_psh();
      M.index('MATKTM');
      M.prefix();
      {? M.find_key(fld,fld)
      || {? ((_czy_wyr=0 & DK.ZL().TYP().WP<>'P') | (_czy_wyr>0 & exec('zl_chk_ktm','zl_wyr',DK.ZL,M.ref())))
            & exec('spr_mat','magdok_poz')
         || DK.M:=M.ref();
            DK.JM:=M.J;
            {? _czy_wyr>0 & exec('party_req','zl_wyk',DK.ZL)>0
            ||
::             Sprawdzenie czy materiał na partii zgodny z nowym materiałem
               {? ATR.ZPARN_DK<>null()
               || _ktm_zparn:=exec('FindAndGet','#table',ZPARN,ATR.ZPARN_DK,,"ZPARN.KTM",null());
                  {? M.ref()<>_ktm_zparn
                  || ZPARN.cntx_psh();
                     ZPARN.index('ZGH3');
                     ZPARN.prefix(DK.ZL,ATR.ZPAR_ZGH,M.ref());
                     {? ZPARN.first()
                     || ATR.ZPARN_DK:=ZPARN.ref()
                     || ATR.ZPARN_DK:=null()
                     ?};
                     ZPARN.cntx_pop();
                     exec('zparn_var_ae','magdok_poz')
                  ?}
               ?}
            ?};
            _wyn:=1
         || FUN.info('Materiał nie jest produktem zlecenia.'@);
            _wyn:=0
         ?}
      || FUN.info('Brak materiału w kartotece materiałowej.'@);
         _wyn:=0
      ?};
      M.cntx_pop()
   || {? DK.PLUS='T'
      || M.index('ARODZ');
         M.prefix('T',POMOC.RODZ)
      || M.index('RODZ');
         M.prefix(POMOC.RODZ)
      ?};
      {? M.find_key(fld) & exec('spr_mat','magdok_poz')
      || DK.M:=M.ref;
         DK.JM:=M.J;
         _wyn:=1
      || _wyn:=0
      ?}
   ?}
|? fld()<>'' & ND.TYP().WYR<>'T' & DK.ZL<>null & _prod='T' & ND.TYP().KOOP<>'T'
||
:: Jesli DK.ZLIM nie wypelnione to szukam pierwszego limitu na zleceniu zgodnego z wypelnionym przez usera stringiem
   {? DK.ZLIM=null()
   || _zlim:=exec('dk_zlim_suggest','zl_limit',DK.ZL,D_HELP.M,ATR.ZPAR_ZGH);
      {? _zlim<>null()
      || DK.ZLIM:=_zlim;
         DK.M_ZLIM:=ref_name(_zlim)
      || FUN.emsg('Nie znaleziono limitu o podanym symbolu.'@);
         _wyn:=0
      ?}
   ||
::    Obsluga dla popraw - jesli zlim wypelniony i D_HELP.M rozny od KTM zlima to zmieniam
      _ktm_ref:=exec('FindAndGet','#table',ZLIM,#DK.ZLIM,ref_name(DK.ZLIM),"KTM",null());
      _ktm:=exec('FindAndGet','#table',M,#_ktm_ref,ref_name(_ktm_ref),"KTM",null());
      {? _ktm<>D_HELP.M
      || _zlim:=exec('dk_zlim_suggest','zl_limit',DK.ZL,D_HELP.M,ATR.ZPAR_ZGH);
         {? _zlim<>null()
         || DK.ZLIM:=_zlim;
            DK.M_ZLIM:=ref_name(_zlim)
         || FUN.emsg('Nie znaleziono limitu o podanym symbolu.'@);
            _wyn:=0
         ?}
      ?}
   ?};
   {? _wyn<>0
   ||
      {? DK.ZLIM<>null()
      || exec('openmask','zl_common',DK.ZL);

::       Sprawdzam czy typ magazynu pozwala na wydanie takiego limitu
         _wyn:=exec('mag_allowed_rw','zl_limit',DK.ZL,ND.MAG,DK.ZLIM,1);

         {? _wyn>0
         ||
            _il_zlim:=exec('sum_il','zl_limit',DK.ZLIM);
            _pob:=exec('ilosc_dk','zl_limit',DK.ZLIM,1);

            _ktm_ref:=exec('FindAndGet','#table',ZLIM,#DK.ZLIM,ref_name(DK.ZLIM),"KTM",null());
            M.clear();
            {? M.seek(_ktm_ref) & exec('spr_mat','magdok_poz')
            || {? DK.PLUS='T' | exec('FindAndGet','#table',ZLIM,DK.ZLIM,,"ZLIM.LIMIT='N'",0) |
                  exec('spr_rez','zamsiw_limit')

               ||
::                Jezeli zlecenie jest zleceniem na ktorym powstaje wyrob i zostala wskazana partia
::                to sprawdzam czy wybrany limit ma ten sam przewodnik co partia, w przeciwnym razie
::                nie ma sensu taka konfiguracja danych bo surowiec na limicie zostanie powiazany z partia z innego
::                przewodnika
                  _can_continue:=1;

                  {? ATR.ZPARN_RW<>null()
                  ||
                     _zl_top:=exec('top_level','zl_link',DK.ZL);
                     _top_rodz:=exec('FindAndGet','#table',ZL,#_zl_top,ref_name(_zl_top),"RODZAJ",'');
                     _zl_prod:=null();
                     {? _top_rodz='N'
                     || _zl_prod:=DK.ZL
                     || _zl_prod:=exec('main_podzlec','zl_link',_zl_top)
                     ?};
                     {? _zl_prod<>null() & _zl_prod=DK.ZL
                     ||
::                      Sprawdzam czy przewodniki sie zgadzaja
                        _zgh_part:=ATR.ZPARN_RW().ZGH;
                        _zgh_lim:=DK.ZLIM().ZGP().NRZLP;
                        {? _zgh_part<>_zgh_lim
                        ||
                           _zgh_s1:=exec('FindAndGet','#table',ZGH,#_zgh_part,ref_name(_zgh_part),"NRPRZ",'');
                           _zgh_s2:=exec('FindAndGet','#table',ZGH,#_zgh_lim,ref_name(_zgh_lim),"NRPRZ",'');
                           _msg0:='';
                           _msg1:='Przewodnik limitu i partii są różne.'@;
                           _msg2:='Takie przypisanie jest niedozwolone, należy wybrać '
                                  'limit powiązany z innym przewodnikiem.'@;
                           _msg4:='Przewodnik partii: %1'@[_zgh_s1];
                           _msg5:='Przewodnik limitu: %1'@[_zgh_s2];
                           _msg_glued:=exec('form','#string','L',_msg1
                                                                ,_msg2
                                                                ,_msg0
                                                                ,_msg4
                                                                ,_msg5);
                           FUN.emsg(_msg_glued);
                           _wyn:=0;
                           _can_continue:=0
                        ?}
                     ?}
                  ?};

                  {? _can_continue>0
                  ||
                     DK.M:=M.ref;
                     DK.JM:=M.J;
                     BEER.M:=M.ref;
                     POMOC.MGR:=M.MGR;
                     D_HELP.M:=M.KTM;

::                   Podpowiadam ilosc
                     _il:=exec('dk_il_suggest','zl_limit',DK.ZLIM,DK.ZL);
                     {? _il>0 & DK.IL=0
                     || DK.IL:=_il
                     ?};
                     _wyn:=1
                  ?}
               || FUN.info(
                     'Na jeden z materiałów zlecenia zostały założone rezerwacje.\n\n'
                     'Pobranie surowca może być wykonane poprzez realizację zamówienia wewnętrznego\n'
                     'lub przy użyciu funkcji \'Wydanie\' w obszarze zleceń — w oknie limitów.'@
                  );
                  _wyn:=0
               ?}
            || FUN.emsg('Nieprawidłowy indeks materiałowy.'@);
               _wyn:=0
            ?}
         ?}
      || FUN.emsg('Brak przypisanego limitu do pozycji dokumentu.'@);
         _wyn:=0
      ?}
   ?}
|| FUN.info('Niewypełnione pole.'@); _wyn:=0
?};
exec('ost_cena','ceny_dok',DK.M,DK.N().KH);
{? _wyn || exec('juz_jest','material','M',DK.N,DK.M,{? HELP.POP || DK.ref() || null ?}) ?};
{? _wyn & DK.PLUS='T' & (DK.SV=null() | _old_ind<>D_HELP.M)
||
   _sv:=DK.SV;
   exec('dk_sv','magdok_poz');
   {? _sv<>DK.SV || exec('obl_mwb','magdok_poz') ?}
?};
:: usunięcie wskazania na cechę po zmianie indeksu na bez wzorca
{? HELP.MOD_IND<>DK.M & DK.M().M_ATR=null() & DK.DK_C<>null()
 & (HELP.MOD_IND().M_ATR<>null() | FUN.ask('Zmieniono Indeks.\nCzy usunąć cechę dostawy?'@))
|| DK.DK_C:=null()
?};
:: ustawienie slownika okienka cech
{? DK.M().M_ATR<>null & #DK.M().M_ATR<>#DK.DK_C().M_ATR & HELP.MOD_IND<>DK.M
|| {! _i..10 |! ($('ATR.WAR'+form(_i,-2,0,'99')))():='' !};
   DK.DK_C:=null
?};
exec('set_efld_btn','magdok_poz');
{? HELP.MOD_IND<>DK.M | DK.DK_C<>null() || exec('dk_atr_dict','mat_atr') ?};
{? POMOC.MGR=null & _pomoc_mgr <> null
|| POMOC.MGR:=_pomoc_mgr
?};
{? _wyn & (1+ND.MAG().TYP)='D' & DK.PLUS='T' & (-menu_txt<>'popraw' | DK.STATS=null)
|| DK.STATS:=exec('FindInSet','#table','MX','M_MG',DK.M,ST.MAG,"MX.DEFSTATS",,,ST.MAG().DEFSTATS);
   _statsM:=exec('statsM','statexam',DK.M);
   {? _statsM<>null() || DK.STATS:=_statsM ?}
?};
{? DK.SCEAN<>'' & exec('FindInSet','#table','MKODK','KK',DK.SCEAN,DK.SCEAN,"MKODK.M",,,null())<>DK.M
|| {? DK.N().TYP().P='T' & exec('FindInSet','#table','MKODK','KK',DK.SCEAN,DK.SCEAN,"MKODK.IDMOB",,,'')='D'
   || exec('mkodkadd','kody_kresk',null,DK.SCEAN,'',1)
   ?};
   _mob:=exec('czyurmob','magdok_wspolne',1);
   {? DK.PLUS='T' & ~_mob & ~((';PZ'*DK.M().IDMOB)>1) || DK.SCEAN:='' ?}
?};
ATR.M_ATR:=DK.M().M_ATR;
ATR.FLAG_ED:=ND.TYP().KOOP='N' & ATR.CZY_ATR & (1+ND.MAG().TYP)='D'
           & ATR.M_ATR().EDIT & (DK.PLUS='T' | DK.M().RODZ='U');
ATR.FLAG:={? _wyn & ATR.FLAG_ED & DK.M().M_ATR<>null() || 2 || 0 ?};
{? ATR.FLAG_ED || {? DK.M().M_ATR || ATR.FLAG_ED:=2 ?} ?};
exec('set_efld_opt','mat_atr',ATR.MJS);
{? DK.DK_C<>null() || DK.DK_C().M_ATR().SYM || DK.M().M_ATR().SYM ?};
{? ND.TYP().UPRW='T' & ~HELP.POP || POMOC.MGR:=null() ?};
{? DK.DK_C=null()& DK.M().M_ATR<>null() || DK_C.blank(); DK_C.M_ATR:=DK.M().M_ATR ?};
{? _wyn || exec('set_efld_dk_tw','magdok_poz') ?};
{? _wyn & DK.IL=0 || BEER.ORD:=exec('ordIL','jm',DK.M,DK.PLUS='T') ?};
{? _wyn & DK.PLUS='T' & EANX.TW=date(0,0,0) & DK.M().TWD>0
|| EANX.TW:=DK.N().D+DK.M().TWD;
   {? DK.M().SETW='P' || DK.TW:=EANX.TW ?}
?};
{? _wyn
|| {? DK.M<>HELP.MOD_IND
   || DK.KK:=exec('m_kk','material',DK.M);
      Plugin.run('POZLOG_M_001','DK')
   ?}
?};
{? _wyn & DK.ROZPAK & DK.DOST=date(0,0,0) & DK.M().RODZ='T'
|| _wyn:=exec('po_eanxpalr','magdok_palety',1);
   {? ~_wyn || FUN.info('Brak podanego indeksu materiałowego: %1,\nna palecie: %2.'@[DK.M().KTM,EANX.PALR().KODK]) ?}
?};
_wyn



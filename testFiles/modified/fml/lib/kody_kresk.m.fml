:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kody_kresk.fml
:: Utworzony: 03.03.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa kodów kreskowych w logistyce
::======================================================================================================================


\kod_obcy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2008]
:: OPIS: Formula wyszukuje kod obcy kontrahenta
::   WE: _a - M.ref ; _b - KH.ref
::   WY: kod obcy
::  OLD: \kod_obcy/tkod.fml
::----------------------------------------------------------------------------------------------------------------------
_kod:='';
MKODK.cntx_psh();
MKODK.index('UNIK');
MKODK.prefix(_b,_a,'N');
:: NUCO PeKa - Dodatokowo drukowana nazwa indeksu kontrahenta
:: 01.12.2023 - dla zamiennikow nie drukujemy KTM-u
{? MKODK.first()
|| {? MKODK.KTM=MKODK.KODK
   || _kod:={? +MKODK.N || MKODK.N || '' ?}
   || _kod:=MKODK.KTM+{? +MKODK.N || ' - '+MKODK.N || '' ?}
   ?}
?};
MKODK.cntx_pop();
_kod


\druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2009+]
:: OPIS: Formula drukuje etykiete na drukarce kodow kreskowych
::   WE: _a - nazwa drukarki z CUPS (Linux)
::  OLD: \druk/skid_uz.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
{? VAR.DEBUG=1 | VAR.DEBUG=2 | VAR.DEBUG=3
|| exec('ddruk','param_wydr')
|? PAR_WYDR.TYPDR().TYPPODL='S'
|| _txt:=exec('lbl_gen','param_wydr');
   {? +_txt
   || _printer:={? _a<>'' || _a || PAR_WYDR.TYPDR().DRIVER ?};
      {? sys_name(0)='WINDOWS'
      || _wy:=myDPRINT.PrintText(_txt,_printer,'Druk etykiety %1'@[PAR_WYDR.URZ_LAB().KOD])
      || _out:=fopen('etyk.txt','a',1);
         {? _out
         || _sysname0:=sys_name(0);
            _sysname1:=sys_name(1);
            _sysname2:=sys_name(2);
            fwrite(_out,_sysname1+''+_sysname1+''+_sysname2);
            fclose(_out)
         ?};
         _wy:=exec('druk_plik','qmom',_txt,_printer)
      ?}
   || _wy:=0
   ?};
   {? _wy=0
   || FUN.emsg('Wydruk nieudany. Należy sprawdzić konfigurację sterownika\ndrukarki '
               'lub definicję etykiety.'@)
   ?};
   _wy
|? PAR_WYDR.TYPDR().TYPPODL='B'
|| _adres:=PAR_WYDR.TYPDR().PORT;
   {? PAR_WYDR.TYPDR().RODZPODL='L' & 1+_adres<>'@' || _adres:='@'+_adres ?};
   _tryb:={? -_adres*'lpt'<>0||'dpm'||'db' ?};
   on_error(2);
   _out:=fopen(_adres,_tryb);
   on_error(0);
   {? _out
   || _txt:=exec('lbl_gen','param_wydr');
      {? +_txt
      || fwrite(_out,_txt);
         _wy:=1
      || _wy:=0
      ?};
      delay(1);
      fclose(_out);
      _wy
   || FUN.emsg('Błąd otwarcia portu: %1.\nPort jest niedostępny lub zajęty przez inny program.'@[_adres]);
      0
   ?}
?}



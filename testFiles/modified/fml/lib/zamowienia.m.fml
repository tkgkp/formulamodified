:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zamowienia.fml
:: Utworzony: 2015-01-16
:: Autor: Mario
::======================================================================================================================
:: Zawartość: Formuły do obsługi zamówień. Uniwersalne do wykorzystania w czynnościach
::======================================================================================================================


\zd_nag_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: dodanie naglowka zamowienia dostawy (ZD_NAG)
::   WE: tablica zwraca przez exec('zd_nag_add_a','zamowienia')
::  OLD: \zd_nag_add/pd_zam.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

_params:=_a;

BEER.TYPYZAM:=null();
POM.TAB:='ZD_NAG';
POM.TYPDOK:='';
TYPYZAM.cntx_psh();
TYPYZAM.prefix();
{? TYPYZAM.seek(_params.TYPYZAM)
||
   POM.TYPDOK:=TYPYZAM.KOD;
   BEER.TYPYZAM:=TYPYZAM.ref()
?};
TYPYZAM.cntx_pop();
{? POM.TAB='' | POM.TYPDOK='' || return(0) ?};

ZD_NAG.blank();
ZD_NAG.KH:=_params.KH;
ZD_NAG.T:=_params.TYPYZAM;
ZD_NAG.DTPREAL:=_params.DD;
ZD_NAG.DATA:=_params.DZ;
ZD_NAG.WAL:=_params.WAL;
ZD_NAG.KRS:=_params.KRS;
::----------------------------------------------------------------------------------------------------------------------
::  MOD: PeKa [06.04.2023]
:: OPIS: Data tabeli kursowej zgodna z biezaca data
ZD_NAG.DTK:=date();
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.prefix();
{? ZD_NAG.add()
||
::----------------------------------------------------------------------------------------------------------------------
::  MOD: PeKa [05.06.2023]
:: OPIS: Dodanie sposobu platnosci jesli przypisany do kontrahenta
   _poz_pl:=0;
   ZD_NAG.PL:=
      {? XINFO.SLP<>null
      || _plat:=exec('sp_plat','kontrahent',ZD_NAG.KH);
         {? _plat=null & ZD_NAG.PL<>null
            & FUN.ask('Kontrahent %1 - %2 \nnie ma przypisanej domyślnej formy płatności.\n'
                  'Czy pozostawić aktualnie podaną?'@[ZD_NAG.KH().KOD, ZD_NAG.KH().NAZ])
         || _poz_pl:=-1;
            ZD_NAG.PL
         || _plat
         ?}
      || null
      ?};
   {? _poz_pl<>-1
   || FZ.FORMAPLA:=ZD_NAG.PL().KOD;
      FZ.PL:=ZD_NAG.PL;
      FZ.TERMPLAT:={? ~_poz_pl || exec('term_plat','faktury_plat','PL','N',ZD_NAG.ref()) || FZ.TERMPLAT ?};
      ZD_NAG.TZ:=FZ.TERMPLAT
   ?};
::----------------------------------------------------------------------------------------------------------------------
   {? ZD_NAG.r_lock(1,1,1)
   || _var:=obj_new('MAXN');
      {? exec('wol_nr','numery',POM.TAB,'N',0,1,_var)=-1
      || _red:=ZD_NAG.mk_edit('Zamówienie do dostawcy'@,,'zd_nag_nr');
         ZD_NAG.win_efld(_red,,'NR');
         ZD_NAG.win_efld(_red,,'SYM',,,,,1);
         ZD_NAG.win_edit(_red);
         _cfg:=',btn_label_align=center,panel=bottom,align=end';
         ZD_NAG.win_ebtn(_red,'text='+'&Zapisz'@+_cfg,'key:F2');
         ZD_NAG.win_ebtn(_red,'text='+'&Anuluj'@+_cfg,'key:Esc');
         {? ZD_NAG.edit("chk_rec('NR')")
         || _wyn:=1
         || _wyn:=exec('znak','numery',POM.TAB)
         ?};
         ZD_NAG.r_unlock()
      || _wyn:={? _var.MAXN=1 || 1 || exec('znak','numery',POM.TAB) ?};
         ZD_NAG.r_unlock()
      ?};
      obj_del(_var)
   ?}
?};
_wyn


\zd_poz_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: dodanie pozycji zamowienia dostaw
::   WE: _a - tabela zwracana przez exec('zd_poz_add_a','zamowienia')
::       _b - 1-grupowac pozycje ilościowo, 2-grupować pozycje wartościowo, 3-bez grupowania
::  OLD: \zd_poz_add/pd_zam.fml
::----------------------------------------------------------------------------------------------------------------------
_paramsa:=_a;
_wariant:=_b;
_wyn:=obj_new('REF','POZ','IL');

_zdn:=null();
ZD_NAG.cntx_psh(); ZD_POZ.cntx_psh();
ZD_NAG.use(8+_paramsa.ZAM_REF);
ZD_NAG.prefix();
{? ZD_NAG.seek(_paramsa.ZAM_REF)
|| _zdn:=ZD_NAG.ref();
   _put:=0;
   ZD_POZ.use('zdpoz'+(8+_paramsa.ZAM_REF+3));
   ZD_POZ.index('DAM');
   ZD_POZ.prefix(_zdn,_paramsa.M);
   {? _wariant=1 | _wariant=2
   ||
      _loop:=ZD_POZ.first();
      {!
      |? _loop
      |!
         {? _wariant=1
         ||
            _put:=
               ZD_POZ.JM=_paramsa.JM
               & ZD_POZ.J2=_paramsa.J2
               & ZD_POZ.WS2=_paramsa.WS2
               & ZD_POZ.T2=_paramsa.T2
               & ZD_POZ.WAL=_paramsa.WAL
               & ZD_POZ.DK_C=_paramsa.DK_C

         |? _wariant=2
         ||
            _put:=
               ZD_POZ.JM=_paramsa.JM
               & ZD_POZ.J2=_paramsa.J2
               & ZD_POZ.WS2=_paramsa.WS2
               & ZD_POZ.T2=_paramsa.T2
               & ZD_POZ.WAL=_paramsa.WAL
               & ZD_POZ.CENA=_paramsa.CENA
               & ZD_POZ.CWAL=_paramsa.CWAL
               & {? ZD_NAG.WAL<>INFO.NAROD || 1 || ZD_POZ.KRS=_paramsa.KRS ?}
               & ZD_POZ.DK_C=_paramsa.DK_C
         ?};
         _loop:=_put=0 & ZD_POZ.next()
      !}
   ?};
   _continue:=0;
   {? _put=1
   || ZD_POZ.IL_ZAM+=_paramsa.IL;
      ZD_POZ.IL+=_paramsa.IL;
      ZD_POZ.IL_KOR:=ZD_POZ.IL-ZD_POZ.IL_ZAM;
      ZD_POZ.IL_POZ:=ZD_POZ.IL;
      {? ZD_POZ.WS2=1 || ZD_POZ.IL2:=ZD_POZ.IL ?};
      exec('aktdil2','zamdst_poz');
      exec('zdp_obl','zamdst_poz');
      _continue:=ZD_POZ.put()
   || ZD_POZ.index('POZ');
      ZD_POZ.prefix(_zdn);
      ZD_POZ.blank();
      ZD_POZ.ZD_NAG:=_zdn;
      ZD_POZ.M:=_paramsa.M;
      ZD_POZ.JM:=_paramsa.JM;
      ZD_POZ.J2:=_paramsa.J2;
      ZD_POZ.WS2:=_paramsa.WS2;
      ZD_POZ.T2:=_paramsa.T2;
      ZD_POZ.IL_ZAM:=_paramsa.IL_ZAM;
      ZD_POZ.IL:=_paramsa.IL;
      ZD_POZ.IL2:=_paramsa.IL2;
      ZD_POZ.WAL:=_paramsa.WAL;
      ZD_POZ.CENA:=_paramsa.CENA;
      ZD_POZ.CWAL:=_paramsa.CWAL;
      ZD_POZ.KRS:=_paramsa.KRS;
      exec('zdp_obl','zamdst_poz');
      ZD_POZ.MG:=_paramsa.MG;
      ZD_POZ.IL_POZ:=_paramsa.IL_POZ;
      ZD_POZ.DK_C:=_paramsa.DK_C;
      ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH;
      ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC;
      ZD_POZ.TRN:=ZD_POZ.ZD_NAG().TRN;
      ZD_POZ.POJAZD:=ZD_POZ.ZD_NAG().POJAZD;
::  PK - uzupelnienie magazynu w naglowku wedlug magazynu z pozycji (zalozenie ustalone z NUCO - jeden magazyn na naglowku)
::   Pole magazyn (domyślnie uzupełnienie pola magazyn według ustalonych reguł)
::   Grupa 04 – magazyn MAT
::   Grupa 03 – magazyn ZAO
::   Grupa 07 – magazyn SUR
::   Grupa 05 – magazyn MAP
      {? ZD_NAG.MG=null()
      || _mgr:=2+ZD_POZ.M().KTM;
         _mg:={? _mgr='03' || 'ZAO' |? _mgr='04' || 'MAT' |? _mgr='05' || 'MAP' |? _mgr='07' || 'SUR' ?};
         ZD_NAG.MG:=exec('FindInSet','#table','MG','MAGAZYNY',_mg,_mg,,1);
         ZD_NAG.put;
         ZD_POZ.MG:=ZD_NAG.MG
      || ZD_POZ.MG:=ZD_NAG.MG
      ?};
      _continue:=ZD_POZ.add()
   ?};
   {? _continue
   ||
      exec('inf_dod','fakso',0,'zdpoz');
      {? ZD_POZ.MG<>null
      || exec('obl_stan','magazyn_stan',ZD_POZ.M,1,ZD_POZ.MG)
      ?};
      _wyn.REF:=ZD_POZ.ref();
      _wyn.POZ:=ZD_POZ.POZ;
      _wyn.IL:=_paramsa.IL
   ?}
?};
ZD_NAG.cntx_pop(); ZD_POZ.cntx_pop();
_wyn



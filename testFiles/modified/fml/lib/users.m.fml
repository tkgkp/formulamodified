:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: users.fml [17.00]
:: Utworzony: 12.11.2014
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli USERS i SYSUSERS (synchronizacja)
::======================================================================================================================


\others_action
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PRIVATE
::  UTW: WH [17.00]
:: OPIS: Akcja uzupełniania innych danych użytkownika w oknie wertowania USERS
::----------------------------------------------------------------------------------------------------------------------
:: Deklaruję potrzebne obiekty
{? var_pres('PAR_SKID')<100
|| exec('par_decl','#parametr');
   PAR_SKID:=obj_new(@.CLASS.PAR_CL)
?};
exec('load_par','#parametr');
__PAR188:=0;

:: Ustawiam okna
KH.win_sel('WER');
KH.actions('WER','W');
OSOBA.win_sel('SLO');
HAN.win_sel('SLO');
:: NUCO - dostęp do słownika handlowców
:: HAN.actions('SLO','dpu:dpu');
HAN.actions('SLO');

{? USERS.AKT<>'N'
||
   USERS.memo_get(,'DESC',0);
   USERS.memo_get(,'FOOT',0);

   _et_ref:=null();
:: urządzenie do wydruku etykiet
   EANX.IDURZ:=USERS.uidref();
   URZ_URZ.index('IDURZ');
   URZ_URZ.prefix(EANX.IDURZ);
   {? URZ_URZ.first() || _et_ref:=URZ_URZ.ref(); URZ_URZ.get() || URZ_URZ.blank() ?};

:: Tworzę okno redagowania
   _red:=exec('mk_edit_others','users');
   USERS.win_edit(_red);
   _valid:="
      _result:='';
      {? USERS.OSOBA<>null() & exec('_osoba_unique','users',USERS.OSOBA,USERS.ref())=0
      || _msg:='Wskazana osoba [%1] jest już przypisana do innego użytkownika.'@
            [exec('record','#to_string',USERS.OSOBA)];
         FUN.emsg(_msg);
         _result:='OSOBA'
      |? URZ_URZ.URZ<>null() & URZ_URZ.URZ_LAB=null()
      || _msg:='Wskazano urządzenie drukujące.\nWymagane podanie wzorca wydruku etykiety.'@;
         FUN.emsg(_msg);
         _result:='URZ_LAB'
      ?};
      _result
   ";
   {? USERS.edit(_valid)
   || {? exec('users_put','#users')
      || USERS.memo_put(,'DESC');
         USERS.memo_put(,'FOOT');
         _new_et:=_et_ref=null();
         {? URZ_URZ.URZ<>null() & URZ_URZ.URZ_LAB<>null()
         || URZ_URZ.DOM:='T';{? _new_et || URZ_URZ.add(1) || URZ_URZ.put(1) ?}
         |? _et_ref<>null() & URZ_URZ.seek(_et_ref)
         || URZ_URZ.del()
         ?}
      ?}
   ?}
|| FUN.emsg('Akcja niedostępna dla nieaktywnego użytkownika.'@)
?};
KH.actions('WER');
HAN.actions('SLO');
~~



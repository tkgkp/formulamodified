:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: powdok.fml [12.30]
:: Utworzony: 2012-06-01
:: Autor: [rr]
::======================================================================================================================
:: Zawartosc: Formuły do podglądu powiązanych dokumentów oraz do wiązania dokumentów
::
::          INFORMACJA O ZAKŁADKACH
::     __powdok.X=1 - faktury sprzedazy
::     __powdok.X=2 - korekty sprzedazy
::     __powdok.X=3 - dokumenty magazynowe
::     __powdok.X=4 - zamowienia
::     __powdok.X=5 - stawki VAT
::     __powdok.X=6 - zaliczki
::     __powdok.X=7 - zgloszenia
::     __powdok.X=8 - dekrety ksiegowego
::     __powdok.X=9 - dokumenty w obiegu
::     __powdok.X=10 - reorganizacje
::     __powdok.X=11 - oferty
::     __powdok.X=12 - zlecenia fakturowania
::     __powdok.X=13 - operacje mobilne
::     __powdok.X=14 - operacje webservice
::     __powdok.X=15 - potwierdzenia zamówień dostaw
::======================================================================================================================


\pow_dokt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: powiazania dokumentow sprzedazy -> przygotowanie tabeli z lista powiazan
::   WE: _a - miejsce wywolania:
::            1-faktury
::            2-dokumenty magazynowe
::            3-zamowienia sprzedazy/wewnetrzne
::            4-zamowienia dostaw
::            5-oferty
::            6-zlecenia fakturowania
::            7-potwierdzenia zamówień dostaw
::            8-zgłoszenie jednorazowe
::            9-dyspozycje transportu
::           10-zgłoszenia remontowe
::   WY: tabela (__powdok) z lista powiazan
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__powdok','__infdok');

__powdok:=tab_tmp(4,'D','TREE_REF',''
           ,'X','INTEGER',''
           ,'C','STRING[255]',''
           ,'REF','STRING[16]',''
           ,'PLUS','INTEGER',''
           ,'CANCEL','INTEGER','');
__infdok:=tab_tmp(1,'LP','INTEGER',''
           ,'ET','STRING[40]',''
           ,'OP','STRING[255]','');

_refdok:={? _a=1 || $FAKS.ref()
         |? _a=2 || $ND.ref()
         |? _a=3 || $ZK_N.ref()
         |? _a=4 || $ZD_NAG.ref()
         |? _a=5 || $OFE.ref()
         |? _a=6 || $FAKS.ref()
         |? _a=7 || $ZDP_NAG.ref()
         |? _a=8 || $ZLP.ref()
         |? _a=9 || $TR_NZL.ref()
         |? _a=10|| $REM_ZGL.ref()
         || ''
         ?};
_cancel:={? _a=1 || FAKS.STAT_REJ='A'
         |? _a=2 || ND.STAT_REJ='A'
         |? _a=3 || ZK_N.STAT_REJ='A'
         |? _a=4 || ZD_NAG.STAT_REJ='A'
         |? _a=5 || OFE.STAT_REJ='A'
         |? _a=6 || FAKS.STAT_REJ='A'
         |? _a=8 || ZLP.STAT_REJ='A'
         |? _a=9 || TR_NZL.STAT_REJ='A'
         || 0
         ?};
{? _a=6
|| __powdok.blank();
   __powdok.C:='Dokumenty sprzedaży'@;
   __powdok.X:=1;
   __powdok.PLUS:=1;
   __powdok.REF:='';
   __powdok.add();
   _ref:=__powdok.ref;
   __powdok.D:=_ref;
   __powdok.C:=FAKS.SYM;
   __powdok.REF:=$FAKS.ref();
   __powdok.CANCEL:=FAKS.STAT_REJ='A';
   __powdok.add();
   _a:=1
?};
:: tab 1 - dokumenty sprzedazy/zakupu
:: tab 2 - korekty sprzedazy/zakupu
{? _a<5 | _a=8
|| __powdok.blank();
   __powdok.C:={? _a=1 || {? FAKS.SZ='S' || 'Dokumenty sprzedaży'@ || 'Dokumenty zakupu'@ ?}
               |? _a=2 || {? ND.TYP().P='N' | ND.TYP().DN='T' || 'Dokumenty sprzedaży'@ || 'Dokumenty zakupu, wewnętrzne'@ ?}
               |? _a=3 || 'Dokumenty sprzedaży'@
               |? _a=4 || 'Dokumenty zakupu'@
               |? _a=8 || 'Dokumenty sprzedaży'@
               || ''
               ?};
   __powdok.X:=1;
   __powdok.PLUS:=1;
   __powdok.REF:=_refdok;
   __powdok.CANCEL:=_cancel;
   __powdok.add();
   {? _a=1 & ~exec('dok_spr','powdok') || __powdok.del()
   |? _a=2 & ND.TYP().P='T' & ND.TYP().DN='T' || __powdok.del()
   |? _a=2 & ND.TYP().P='T' & ND.TYP().DK='T' || __powdok.del()
   |? _a=2 & exec('FindAndGet','#table',FAKS,ND.FAKS,,"FAKS.WHERE",'')='N' || __powdok.del()
   |? _a=2 & ~exec('dok_sp','powdok') || __powdok.del()
   |? _a=3 & ~exec('dok_pzam','powdok',2) || __powdok.del()
   |? _a=4 & ~exec('dok_pzad','powdok',2) || __powdok.del()
   |? _a=8 & ~exec('dok_pzlp','powdok',1) || __powdok.del()
   ?};

   {? (_a=1 & FAKS.T().KOR<>'Z')
      | (_a=2 & ND.TYP().P='T' & ND.TYP().DN='T')
      | (_a=2 & ND.TYP().P='T' & ND.TYP().DK='T')
      | (_a=2 & (ND.TYP().P='T' & ND.TYP().DN='T' | exec('FindAndGet','#table',FAKS,ND.FAKS,,"FAKS.WHERE",'')='N'))
      | _a=8
   || __powdok.blank();
      __powdok.C:='Dokumenty korygujące'@;
      __powdok.X:=2;
      __powdok.PLUS:=1;
      __powdok.REF:=_refdok;
      __powdok.CANCEL:=_cancel;
      __powdok.add();
      {? _a=1 & ~exec('dok_spr','powdok') || __powdok.del()
      |? _a=2 & ~exec('dok_sp','powdok') || __powdok.del()
      |? _a=8 & ~exec('dok_pzlp','powdok',2) || __powdok.del()
      ?}
   ?}
?};
:: tab 3 - dokumenty magazynowe
{? _a<5 | _a=10
|| __powdok.blank();
   __powdok.C:='Dokumenty magazynowe'@;
   __powdok.X:=3;
   __powdok.PLUS:=1;
   __powdok.REF:=_refdok;
   __powdok.CANCEL:=_cancel;
   __powdok.add();
   {? _a=1 & ~exec('dok_maga','powdok') || __powdok.del()
   |? _a=2  & exec('FindAndGet','#table',FAKS,ND.FAKS,,"FAKS.WHERE",'')='N' || __powdok.del()
   |? _a=2 & ~exec('dok_mag','powdok') || __powdok.del()
   |? _a=3 & ~exec('dok_pzam','powdok',1) || __powdok.del()
   |? _a=4 & ~exec('dok_pzad','powdok',1) || __powdok.del()
   |? _a=10 & ~exec('dok_mag_r','powdok') || __powdok.del()
   ?}
?};

:: zamowienia klienta - __list_zk ustawiane w formule dok_mag
{? _a=1 & FAKS.WHERE='L'
||
   VAR_DEL.delete('__list_zk');
   _qq:=$"
      select
         FAPOW.ZK_N REF,
         'X' A,
         FAPOW.ZK_N_SYM SYM
      from
         FAPOW
      where
         FAPOW.FAKS=':_a'
         and FAPOW.ZK_N<>''
      group by
         FAPOW.ZK_N,
         'X',
         FAPOW.ZK_N_SYM
      order by
         1
   ";
   __list_zk:=sql(_qq,$FAKS.ref());
   {? __list_zk.first()
   ||
      ZK_N.cntx_psh();
      {!
      |?
         ZK_N.use(8+__list_zk.REF);
         ZK_N.prefix();
         {? ZK_N.seek(__list_zk.REF,8+__list_zk.REF)
         || __list_zk.A:=ZK_N.A;
            __list_zk.put()
         ?};
         __list_zk.next()
      !};
      ZK_N.cntx_pop()
   ?}
?};
:WW:NUCO Modyfikacja wiążąca fakturę proformę z zamówieniem sprzedaży
{? _a=1 & FAKS.WHERE='P' & FAKS.T().KOR='N' & FAKS.TZP<>''
||
   __powdok.blank();
   __powdok.C:='Zamówienia sprzedaży'@;
   __powdok.X:=4;
   __powdok.PLUS:=1;
   __powdok.REF:=_refdok;
   __powdok.CANCEL:=_cancel;
   __powdok.add();
   _my_ref:=__powdok.ref();
   _ref3:=exec('FindAndGet','#table',ZK_N,FAKS.TZP,,"uidref",null);
   {? ZK_N.seek(_ref3)
   ||
     __powdok.blank;
     __powdok.D:=_my_ref;
     __powdok.C:=ZK_N.SYM;
     __powdok.X:=4;
     __powdok.PLUS:=0;
     __powdok.REF:=$ZK_N.ref;
     __powdok.add
   ?}
?};



:: tab 4 - zamowienia sprzedazy/wewnetrzne/dostaw
{? _a<3
|| _typzam:={? _a=1 || FAKS.SZ='Z'
            |? _a=2 & ND.TYP().P='T' || {? ND.NDSQL<>'' & ND.MAG().COS='T'  || 0
                                        |? ND.NDSQL<>'' & ND.MAG().COS<>'T' || 2
                                        || 1
                                        ?}
            |? _a=2 & ND.TYP().Z<>'T' & ND.MD().COS<>'T' || 2
            || 0
            ?};
   {? var_pres('__list_zk')>100
   || {? __list_zk.size()>0
      || __powdok.blank();
         {? _typzam=1 || __powdok.C:='Zamówienia dostaw'@
         |? _typzam=0 || __powdok.C:='Zamówienia sprzedaży'@
         |? _typzam=2 || __powdok.C:='Zamówienia wewnętrzne'@
         ?};
         __powdok.X:=4;
         __powdok.PLUS:=1;
         __powdok.REF:=_refdok;
         __powdok.CANCEL:=_cancel;
         __powdok.add();
         _my_ref:=__powdok.ref();
         __list_zk.clear();
         {? __list_zk.first()
         || {!
            |? __powdok.D:=_my_ref;
               __powdok.C:=__list_zk.SYM;
               __powdok.X:=4;
               __powdok.PLUS:=0;
               __powdok.REF:=__list_zk.REF;
               __powdok.CANCEL:={? (2+__list_zk.REF)='zd'
                                || exec('FindAndGet','#table',ZD_NAG,__list_zk.REF,,"STAT_REJ='A'",0)
                                || exec('FindAndGet','#table',ZK_N,__list_zk.REF,,"STAT_REJ='A'",0)
                                ?};
               __powdok.add();
               {? ~_typzam || exec('find_ofe','powdok',__list_zk.REF,_refdok) ?};
               __list_zk.next()
            !}
         ?}
      ?}
   ?}
?};
{? _a=5
|| _zam:=sql('select ZK_N.REFERENCE REF, ZK_N.SYM SYM from @ZK_N where ZK_N.OFE=:_a',OFE.ref());
   _zam.clear();
   {? _zam.first()
   || __powdok.C:='Zamówienia sprzedaży'@;
      __powdok.X:=4;
      __powdok.PLUS:=1;
      __powdok.REF:=_refdok;
      __powdok.CANCEL:=_cancel;
      __powdok.add();
      _my_ref:=__powdok.ref();
      {!
      |? __powdok.blank();
         __powdok.D:=_my_ref;
         __powdok.C:=_zam.SYM;
         __powdok.X:=4;
         __powdok.PLUS:=0;
         __powdok.REF:=_zam.REF;
         __powdok.CANCEL:=exec('FindAndGet','#table',ZK_N,_zam.REF,,"STAT_REJ='A'",0);
         __powdok.add();
         exec('dok_pzam','powdok',2,_zam.REF,_refdok);
         exec('dok_pzam','powdok',1,_zam.REF,_refdok);
         _zam.next()
      !}
   ?}
?};

{? _a=7
|| _zam:=tab_tmp(1,'REF','STRING[16]','','SYM','STRING[20]','');
   ZDP_POZ.cntx_psh();
   ZDP_POZ.index('ZDP_NAG');
   ZDP_POZ.prefix(ZDP_NAG.ref());
   {? ZDP_POZ.first()
   || {!
      |? _msk:=form(8+ZDP_POZ.ZD_POZ)+3;
         ZD_NAG.cntx_psh();
         ZD_POZ.cntx_psh();
         ZD_NAG.use('zdnag'+_msk);
         ZD_POZ.use('zdpoz'+_msk);
         ZD_POZ.prefix();
         {? ZD_POZ.seek(ZDP_POZ.ZD_POZ)
         || _zam.clear();
            _zam.prefix($ZD_POZ.ZD_NAG,);
            {? ~_zam.first()
            || _zam.blank();
               _zam.REF:=$ZD_POZ.ZD_NAG;
               _zam.SYM:=ZD_POZ.ZD_NAG().SYM;
               _zam.add(1)
            ?}
         ?};
         ZD_NAG.cntx_pop();
         ZD_POZ.cntx_pop();
         ZDP_POZ.next()
      !}
   ?};
   ZDP_POZ.cntx_pop();
   _rzak:=0;
   _rmag:=0;
   _zam.clear();
   {? _zam.first()
   || __powdok.C:='Zamówienia dostaw'@;
      __powdok.X:=4;
      __powdok.PLUS:=1;
      __powdok.REF:=_refdok;
      __powdok.CANCEL:=_cancel;
      __powdok.add();
      _my_ref:=__powdok.ref();
      {!
      |? __powdok.blank();
         __powdok.D:=_my_ref;
         __powdok.C:=_zam.SYM;
         __powdok.X:=4;
         __powdok.PLUS:=0;
         __powdok.REF:=_zam.REF;
         __powdok.CANCEL:=exec('FindAndGet','#table',ZD_NAG,_zam.REF,,"STAT_REJ='A'",0);
         __powdok.add();
         {? ~(_res:=exec('dok_pzad','powdok',2,_zam.REF,_refdok,_rzak))
         || __powdok.del()
         || {? _res=1 || _rzak:=__powdok.D ?}
         ?};
         {? ~(_res:=exec('dok_pzad','powdok',1,_zam.REF,_refdok,_rmag))
         || __powdok.del()
         || {? _res=1 || _rmag:=__powdok.D ?}
         ?};
         _zam.next()
      !}
   ?}
?};
:: tab 5 - tabela stawek VAT
{? _a=1
||
   __powdok.blank();
   __powdok.C:='Stawki VAT'@;
   __powdok.X:=5;
   __powdok.REF:=_refdok;
   __powdok.CANCEL:=_cancel;
   __powdok.add()
?};
:: tab 6 - faktury zaliczkowe
{? _a=1 & FAKS.WHERE='L'
||
   __powdok.blank();
   __powdok.C:='Zaliczki'@;
   __powdok.X:=6;
   __powdok.REF:=$FAKS.ref;
   __powdok.CANCEL:=FAKS.STAT_REJ='A';
   __powdok.add();
   __my_ref:=__powdok.ref();
   FAPOW.index('FAKS_K');
   FAPOW.prefix($FAKS.ref,'N');
   {? FAPOW.first()
   ||
::       szuka powiazanych
      VAR_DEL.delete('X_FAPOW');
      _qq:="
         select distinct
            FAPOW.FAKS_SYM as FAKS_SYM,
            to_string(FAPOW.DW) as DW,
            FAPOW.KW as KW,
            FAPOW.WAL as WAL,
            FAPOW.FAKS as FAKS_REF
         from
            FAPOW
         where
            FAPOW.F=':_a'
            and FAPOW.FAKS_SYM<>':_b'
            and FAPOW.KH=:_c
            and FAPOW.WAL=':_d'
            and FAPOW.KOREKTA='N'
            and FAPOW.AKC='T'
            and FAPOW.POZ<:_e
      ";
      X_FAPOW:=sql($_qq,FAPOW.F,FAPOW.FAKS_SYM,FAPOW.KH,FAPOW.WAL,FAPOW.POZ);
      X_FAPOW.for_each("__powdok.D:=__my_ref;__powdok.C:='Powiązane zaliczki: '@+X_FAPOW.FAKS_SYM;__powdok.X:=6;
         __powdok.PLUS:=0;__powdok.REF:=X_FAPOW.FAKS_REF;
         __powdok.CANCEL:=exec('FindAndGet','#table',FAKS,X_FAPOW.FAKS_REF,,\"STAT_REJ='A'\",0);__powdok.add");
      VAR_DEL.delete('X_FAPOW')
   ?}
|? _a=1
||
   FAPOW.index('END_K');
   FAPOW.prefix($FAKS.ref,'N');
   {? FAPOW.first()
   ||
      __powdok.blank();
      __powdok.C:='Rozliczone zaliczki'@;
      __powdok.X:=6;
      __powdok.PLUS:=0;
      __powdok.REF:=_refdok;
      __powdok.CANCEL:=_cancel;
      __powdok.add
   ?}
|? _a=3 & ZK_N.T().R='Z'
|| FAPOW.index('ZK_N');
   FAPOW.prefix($ZK_N.ref);
   {? FAPOW.first()
   || __powdok.blank();
      __powdok.C:='Rozliczone zaliczki'@;
      __powdok.X:=6;
      __powdok.PLUS:=0;
      __powdok.REF:=_refdok;
      __powdok.CANCEL:=_cancel;
      __powdok.add
   ?}

|? _a=8
||
   _zlp:=ZLP.uidref();
   FAPOW.index('ZLP');
   FAPOW.prefix(_zlp);
   {? FAPOW.first()
   ||
      __powdok.blank();
      __powdok.C:='Rozliczone zaliczki'@;
      __powdok.X:=6;
      __powdok.PLUS:=0;
      __powdok.REF:=_refdok;
      __powdok.CANCEL:=_cancel;
      __powdok.add
   ?}
?};
:: tab 7 - zgloszenia
{? _a=1 & 'JA'*FAKS.WHERE
      |
   _a=9 & +TR_NZL.DOK_REF=48 & ref_tab(TR_NZL.DOK_REF)=ZLP
||
   __powdok.blank();
   __powdok.C:='Zgłoszenia'@;
   __powdok.X:=7;
   __powdok.REF:=_refdok;
   __powdok.CANCEL:=_cancel;
   __powdok.add()
?};
:: tab 8 - dekrety ksiegowe
{? _a<=2
|| __powdok.blank();
   {? __ksg='T'
   || _maska:=exec('maska','dok_fks_aut_dok',{? _a=1 || {? FAKS.T().KOR<>'Z' || 'F' || 'K' ?} || 'M' ?})
             +{? _a=1 & FAKS.T().KOR='Z' || form(#FAKS.ref,,,'99') || '' ?};
      {? var_pres('X_Xa')>0 || obj_del(X_Xa) ?};
      X_Xa:=sql('select DOK.REFERENCE as REF from @DOK join DOK_REJ
                 where DOK.DOKZRODL = \':_a\' ',_maska);
      {? X_Xa.first()
      ||
         _mask:=4+(4-X_Xa.REF);
         DOK.use('doku'+_mask);
         POZ.use('pozy'+_mask);
         POW.use('pow_'+_mask);
         P_V.use('p_v_'+_mask);
         P_W.use('p_w_'+_mask);
         VPOZ.use('pozv'+_mask);
         DOK.clear();
         {? DOK.seek(X_Xa.REF,DOK.name)
         || {? DOK.DOK_REJ().SLO().KOD='VAT' || __ksg:='V' ?}
         ?}
      ?};

      {? {? _a=1 || exec('upr_cs','ceny') || exec('upr_cm','ceny') ?}
      ||
         __powdok.C:='Zapisy księgowe'@;
         __powdok.X:=8;
         __powdok.REF:=_refdok;
         __powdok.CANCEL:=_cancel;
         __powdok.add()
      ?}
   ?}
?};

:: tab 9 dokumenty w obiegu
{? _a=1 & (FAKS.OBI_POW<>'' | exec('z_edokum','faktury_nag'))
   | _a=3 & ZK_N.OBI_POW<>''
   | _a=4 & ZD_NAG.OBI_POW<>''
|| __powdok.blank();
   __powdok.C:='Dokumenty w obiegu'@;
   __powdok.X:=9;
   __powdok.REF:=_refdok;
   __powdok.CANCEL:=_cancel;
   __powdok.add()
?};

:: tab 10 - reorganizacje
{? _a=2 & ND.MAG().MWS='T'
|| __powdok.blank();
   __powdok.C:='Reorganizacja magazynu'@;
   __powdok.X:=10;
   __powdok.REF:=_refdok;
   __powdok.CANCEL:=_cancel;
   __powdok.add();
   {? ~exec('reo_mag','powdok') || __powdok.del() ?}
?};
:: tab 11 oferty
{? _a=3
|| exec('find_ofe','powdok',_refdok,_refdok)
?};

:: tab 12 zlecenia fakturowania
{? _a=1
|| exec('find_fakz','powdok',_refdok,_refdok,_cancel)
?};
:: tab 13 i 14 operacje mobilne/webservice
{? _a=2
|| _msk1:=EANP.name();
   _msk2:=form((ND.D~1)-2000,-2,0,'99');
   EANN.cntx_psh();
   EANP.cntx_psh();
   exec('openean','open_tab',((_msk1-2)+1)+_msk2);
   EANP.clear();
   {? (_eanp:=$exec('FindInSet','#table','EANP','RNAG',$ND.ref(),$ND.ref()))<>''
   || _mob:=exec('FindAndGet','#table',EANP,_eanp,,"EANN().ZLEC='N'",1);
      {? _mob
      || __powdok.blank();
         __powdok.C:='Operacje mobilne'@;
         __powdok.X:=13;
         __powdok.REF:=_refdok;
         __powdok.CANCEL:=_cancel;
         __powdok.add()
      || __powdok.blank();
         __powdok.C:='Operacje webservice'@;
         __powdok.X:=14;
         __powdok.REF:=_refdok;
         __powdok.CANCEL:=_cancel;
         __powdok.add()
      ?}
   || ''
   ?};
   EANN.cntx_pop();
   EANP.cntx_pop()
?};
:: tab 15 potwierdzenia zamówień dostaw
{? _a=4
|| exec('find_zdp','powdok',_refdok,_refdok)
?};
:: tab 16 zlecenia transportowe
{? _a=9 & (';ZK_N;ZD_NAG;ND;TR_NZL;ZDP_NAG;'*TR_NZL.WHERE)>1
|| TR_NZL.cntx_psh();
   TR_ZL.cntx_psh();
   TR_ZL.index('POZ');
   TR_ZL.prefix(TR_NZL.ref());
   {? TR_ZL.first()
   || _Tab:={? TR_NZL.WHERE='TR_NZL' & TR_NZL.DOK_REF<>''
            || ref_tab(TR_ZL.DOK_REF)
            || exec('where2TAB','transport_wspolne',TR_NZL.WHERE)
            ?};
      _nrgal:=0;
      __powdok.blank();
      {? _Tab=ND
      || __powdok.C:='Dokumenty magazynowe'@;
         _nrgal:=3
      || _rodzzam:={? TR_NZL.WHERE='ZDP_NAG' | TR_ZL.DOK_REF*'zdptn'
                   || 'P'
                   || exec('FindAndGet','#table',_Tab,TR_ZL.DOK_REF,,"T().R",'')
                   ?};
         __powdok.C:={? _rodzzam='Z' || 'Zamówienia sprzedaży'@
                     |? _rodzzam='W' || 'Zamówienia wewnętrzne'@
                     |? _rodzzam='D' || 'Zamówienia dostaw'@
                     |? _rodzzam='P' || 'Potwiedzenia zamówień'@
                     || 'Zamówienia'@
                     ?};
         _nrgal:={? _rodzzam='P' || 15 || 4 ?}
      ?};
      __powdok.X:=_nrgal;
      __powdok.REF:=_refdok;
      __powdok.CANCEL:=_cancel;
      _pd_ref:={? __powdok.add(1) || __powdok.ref() || null() ?};
      {? _pd_ref<>null()
      || _buf:=tab_tmp(1,'SQL','STRING[16]','');
         {!
         |? {? (_buf.clear(); _buf.prefix(TR_ZL.DOK_REF+16); ~_buf.first())
            || __powdok.D:=_pd_ref;
               __powdok.C:=exec('record','#to_string',exec('FindAndGet','#table',_Tab,TR_ZL.DOK_REF));
               __powdok.X:=_nrgal;
               __powdok.PLUS:=0;
               __powdok.REF:=TR_ZL.DOK_REF+16;
               __powdok.CANCEL:=exec('FindAndGet','#table',_Tab,TR_ZL.DOK_REF,,"STAT_REJ='A'",0);
               {? __powdok.add(1)
               || _buf.clear();
                  _buf.blank();
                  _buf.SQL:=TR_ZL.DOK_REF+16;
                  _buf.add(1)
               ?}
            ?};
            TR_ZL.next()
         !};
         obj_del(_buf)
      ?}
   ?};
   TR_NZL.cntx_pop();
   TR_ZL.cntx_pop()
?};
{? _a=8 | (_a=9 & ~((';ZK_N;ZD_NAG;ND;TR_NZL;ZDP_NAG;REK_N;ZGP;'*TR_NZL.WHERE)>1)) | _a=2 | _a=3 | _a=4 | _a=7
||
   _dok:='';
   {? _a=8 || _dok:=ZLP.uidref()
   |? _a=9 || _dok:=TR_NZL.DOK_REF
   |? _refdok<>'' & (_a=2 | _a=3 | _a=4 | _a=7)
           || _dok:=exec('FindAndGet','#table',ref_tab(_refdok),_refdok,,"uidref()",'')
   ?};
   _Tr_nzl:=exec('get_tr_nzl','transport_zlec',_dok);
   _loop:=_Tr_nzl.first();
   {? _loop
   || {!
      |? {? _refdok<>_Tr_nzl.REF
         || __powdok.blank();
            __powdok.C:='Dyspozycje transportu'@;
            __powdok.X:=16;
            __powdok.REF:=_refdok;
            __powdok.CANCEL:=_cancel;
            {? __powdok.add()
            ||
               _pd_ref:=__powdok.ref();
               {!
               |? _loop
               |! {? _refdok<>_Tr_nzl.REF
                  || __powdok.D:=_pd_ref;
                     __powdok.C:=exec('record','#to_string',_Tr_nzl.REF);
                     __powdok.X:=16;
                     __powdok.PLUS:=0;
                     __powdok.REF:=_Tr_nzl.REF;
                     __powdok.CANCEL:=exec('FindAndGet','#table',TR_NZL,_Tr_nzl.REF,,"TR_NZL.STAT_REJ='A'",0);
                     _loop:=__powdok.add() & _Tr_nzl.next()
                  || _loop:=_Tr_nzl.next()
                  ?}
               !}
            ?};
            0
         || _loop:=_Tr_nzl.next()
         ?}
      !}
   ?};
   obj_del(_Tr_nzl)
?};
:: tab 17 zgrupowane dyspozycje transportu
{? _a=8 | _a=9
||
   _dok:=tab_tmp(,'UIDREF','STRING[48]','');
   {? _a=8
   ||
      _Tr_nzl:=exec('get_tr_nzl','transport_zlec',ZLP.uidref());
      _loop:=_Tr_nzl.first();
      {!
      |? _loop
      |!
         _uidref:=exec('FindAndGet','#table',TR_NZL,_Tr_nzl.REF,,"TR_NZL.GRP",'');
         {? +_uidref=48 & ~_dok.find_key(_uidref)
         ||
            _dok.UIDREF:=_uidref;
            _dok.add()
         ?};
         _loop:=_Tr_nzl.next()
      !};
      obj_del(_Tr_nzl)

   |? _a=9
   ||
      _dok.UIDREF:=TR_NZL.GRP;
      _dok.add()
   ?};
   _loop:=_dok.first();
   {? _loop
   || __powdok.blank();
      __powdok.C:='Zgrupowane dyspozycje transportu'@;
      __powdok.X:=17;
      __powdok.REF:=_refdok;
      __powdok.CANCEL:=_cancel;
      {!
      |? _loop
      |!
         _Tr_nzl:=exec('get_tr_nzl','transport_zlec',_dok.UIDREF,'GRP');
         _loop:=_Tr_nzl.first();
         {!
         |? {? _refdok<>_Tr_nzl.REF
            || {? _loop & __powdok.add()
               ||
                  _pd_ref:=__powdok.ref();
                  {!
                  |? _loop
                  |!
                     {? _refdok<>_Tr_nzl.REF
                     || __powdok.D:=_pd_ref;
                        __powdok.C:=exec('record','#to_string',_Tr_nzl.REF);
                        __powdok.X:=17;
                        __powdok.PLUS:=0;
                        __powdok.REF:=_Tr_nzl.REF;
                        __powdok.CANCEL:=exec('FindAndGet','#table',TR_NZL,_Tr_nzl.REF,,"TR_NZL.STAT_REJ='A'",0);
                        _loop:=__powdok.add() & _Tr_nzl.next()
                     || _loop:=_Tr_nzl.next()
                     ?}
                  !}
               ?};
               obj_del(_Tr_nzl);
               0
            || _loop:=_Tr_nzl.next()
            ?}
         !};
         _loop:=_dok.next()
      !}
   ?}
?};
:: reklamacje
{? (_a>=1 & _a<=5) | _a=9
|| _uid:=exec('FindAndGet','#table',ref_tab(_refdok),_refdok,,"uidref()",'');
   _ref:=null();
   {? _uid<>''
   || _msk:=REK_D.names();
      exec('rek_psh','open_tab');
      _msk.clear();
      {? _msk.first()
      || _buf:=tab_tmp(1,'SQL','STRING[16]','');
         {!
         |? exec('rek_open','open_tab',1+(_msk.NAME+3),_msk.NAME+2);
            REK_D.index('DK_REF');
            REK_D.prefix(_uid,);
            {? REK_D.first()
            || {!
               |? {? _ref=null()
                  || __powdok.blank();
                     __powdok.C:='Reklamacje'@;
                     __powdok.X:=18;
                     __powdok.PLUS:=1;
                     __powdok.REF:='';
                     __powdok.add();
                     _ref:=__powdok.ref
                  ?};
                  {? (_buf.clear(); _buf.prefix($REK_D.REK_N,); ~_buf.first())
                  || __powdok.D:=_ref;
                     __powdok.C:=REK_D.REK_N().SYM;
                     __powdok.REF:=$REK_D.REK_N;
                     __powdok.CANCEL:=0;
                     {? __powdok.add()
                     || _buf.blank();
                        _buf.SQL:=$REK_D.REK_N;
                        _buf.add(1)
                     ?}
                  ?};
                  REK_D.next()
               !}
            ?};
            _msk.next()
         !};
         obj_del(_buf)
      ?};
      exec('rek_pop','open_tab')
   ?}
?};
:: rozrachunek
{? _a<=2
|| __powdok.blank();
   {? __ksg='T' | __ksg='V'
   || _zp:='';
      _maska:=exec('maska','dok_fks_aut_dok',{? _a=1 || {? FAKS.T().KOR<>'Z' || 'F' || 'K' ?} || 'M' ?})
             +{? _a=1 & FAKS.T().KOR='Z' || form(#FAKS.ref,,,'99') || '' ?};
      {? var_pres('X_Xa')>0 || obj_del(X_Xa) ?};
      X_Xa:=sql('select DOK.REFERENCE as REF from @DOK join DOK_REJ
                 where DOK.DOKZRODL = \':_a\' ',_maska);
      {? X_Xa.first()
      ||
         _mask:=4+(4-X_Xa.REF);
         DOK.use('doku'+_mask);
         POZ.use('pozy'+_mask);
         POW.use('pow_'+_mask);
         P_V.use('p_v_'+_mask);
         P_W.use('p_w_'+_mask);
         VPOZ.use('pozv'+_mask);
         DOK.clear();
         {? DOK.seek(X_Xa.REF,DOK.name)
         || _zp:=DOK.ZP
         ?}
      ?};

      {? {? _a=1 || exec('upr_cs','ceny') || exec('upr_cm','ceny') ?} & _zp='T'
      ||
         __powdok.C:='Rozrachunki'@;
         __powdok.X:=19;
         __powdok.REF:=_refdok;
         __powdok.CANCEL:=_cancel;
         __powdok.add()
      ?}
   ?}
?};
:: Zgłoszenia remnontowe
{? _a=2
|| __powdok.blank();
   __powdok.C:='Zgłoszenia remontowe'@;
   __powdok.X:=21;
   __powdok.PLUS:=1;
   __powdok.REF:=_refdok;
   __powdok.CANCEL:=_cancel;
   __powdok.add();
   {? _a=2 & ~exec('zgl_rem','powdok') || __powdok.del() ?}
?};
''


\dok_pzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: dodaje informacje o dokumencie magzynowym lub dokumencie sprzedazy
::   WE: _a - 1-dokument magazynowy, 2-dokument sprzedazy
::       [_b] - ref SQL ZK_N
::       [_c] - ref nadrzednej galezi
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>2 || _b:='' ?} || _b:='' ?};
{? _>=3 || {? type_of(_c)<>2 || _c:='' ?} || _c:='' ?};

_refzkn:={? _b='' || ZK_N.ref() || exec('FindAndGet','#table',ZK_N,_b,,,null()) ?};
_wyn:=0;
:WW:NUCO otwieram maskę tabeli FAKS, pobieram wskazanie do tabeli FAKS. Wykorzystano pole TZP, w którym znajduje się ref FAKS
_faks:=null;
_txt:='';
_ref1:=sql('SELECT REFERENCE, SYM FROM @FAKS WHERE FAKS.TZP like \':_a%\'',ZK_N.uidref);
{? _ref1.first() || _msk1:=8+_ref1.REFERENCE; FAKS.use(_msk1) || _msk:=6+FAKS.name();_rok:=2-$ST.AR; FAKS.use($_msk+''+_rok) ?};
{? _ref1.first()
||
   _faks:=obj_new(_ref1.size);
   {! _loop:=1 .. _ref1.size
   |!
      _faks[_loop]:=exec('FindAndGet','#table',FAKS,_ref1.REFERENCE,,"ref",null());
      _ref1.next()
   !}
?};
{? _b<>'' & _c<>''
|| __powdok.blank();
   __powdok.C:={? _a=2 || 'Dokumenty sprzedaży'@ || 'Dokumenty magazynowe'@ ?};
   __powdok.X:={? _a=2 || 1 || 3 ?};
   __powdok.PLUS:=1;
   __powdok.REF:=_c;
   __powdok.add()
?};
:WW:NUCO tworzę drzewko do Funkcje->Powiązane dokumenty
{? _ref1.first()
|| {? _a=2 & _faks[1] || _txt:='Faktura PROFORMA'@ ?}
?};
{? _txt <> ''
||
   __powdok.del();
   __powdok.blank();
   __powdok.C:=_txt;
   __powdok.X:={? _a=2 || 1 || 3 ?};
   __powdok.PLUS:=1;
   __powdok.REF:=_c;
   __powdok.add()
?};
_ref:=__powdok.ref;
ZK_RN.cntx_psh();
ZK_N.cntx_psh();
ZK_N.use(ref_name(_refzkn));
ZK_RN.use('zkhin'+(ref_name(_refzkn)+3));
ZK_RN.index('ZAM');
ZK_RN.prefix(_refzkn);
{? ZK_RN.first()
|| {!
   |? {? _a=1 & ZK_RN.ND<>''
      || __powdok.D:=_ref;
         __powdok.C:=ZK_RN.SWZ;
         __powdok.REF:=ZK_RN.ND;
         __powdok.CANCEL:=exec('FindAndGet','#table',ND,ZK_RN.ND,,"STAT_REJ='A'",0);
         _wyn:=__powdok.add()
      |? _a=2 & ZK_RN.FAKS<>''
      || VAR_DEL.delete('__powr_f','__powr_s','__powa_d');
         __powdok.cntx_psh();
         __powr_f:=_ref;
         __powr_s:=ZK_RN.FAKS;
         __powa_d:=1;
         __powdok.clear();
         __powdok.for_each("{? __powdok.D=__powr_f & __powdok.REF=__powr_s || __powa_d:=0 ?}",0);
         __powdok.cntx_pop();
         {? __powa_d
         || __powdok.D:=_ref;
            __powdok.C:=exec('FindAndGet','#table',FAKS,ZK_RN.FAKS,,"SYM",ZK_RN.SFK);
            __powdok.REF:=ZK_RN.FAKS;
             __powdok.CANCEL:=exec('FindAndGet','#table',FAKS,ZK_RN.FAKS,,"STAT_REJ='A'",0);
            _wyn:=__powdok.add()
         || _wyn:=1
         ?};
         VAR_DEL.delete('__powr_f','__powr_s','__powa_d')
      ?};
      ZK_RN.next()
   !}
?};
ZK_RN.cntx_pop();
ZK_N.cntx_pop();
:WW:NUCO formuła wiąże zamówienie sprzedaży z fakturą proforma
FAKS.cntx_psh();
{? _ref1.first()
||
   {? _txt='Faktura PROFORMA'
   ||
      __powdok.blank();
      __powdok.cntx_psh();
      {! _loop:=1.._ref1.size
      |!
         {? FAKS.seek(_faks[_loop])
         ||
         __powdok.X:=1;
         __powdok.D:=_ref;
         __powdok.C:=FAKS.SYM;
         __powdok.REF:=$FAKS.ref;
         __powdok.CANCEL:=exec('FindAndGet','#table',FAKS,FAKS.ref,,"STAT_REJ='A'",0);
         _wyn:=__powdok.add()
         ?}
      !};
      __powdok.cntx_pop()
   ?}
?};
FAKS.cntx_pop();
{? _b<>'' & _c<>'' & ~_wyn || __powdok.del() ?};
_wyn



:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tech_prod.fml
:: Utworzony: 23.02.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa produktów kart technologicznych - tabela TKTLW
::            Plik biblioteczny - wspólna obsługa dla TTE_TEC
::======================================================================================================================
\tktlw_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: Zaznacza kartę produktu jako domyślną
::       Kontekst wywołania - rekord tabeli TKTLW
::   WE: [_a] - czy wyświetlać pytanie (domyślnie - 1), czy bez pytania (0 - zawsze ustawia domyślność kart)
::   WY: 0/1
::  OLD: \tktlw_default/tech.fml
::----------------------------------------------------------------------------------------------------------------------
_can_continue:=1;
_result:=0;

{? var_pres('_a')=type_of(0) || _ask:=_a || _ask:=1 ?};

{? _ask
|| {? TKTLW.DEFAULT='T'
   || {? FUN.ask(
            'Ta karta jest już domyślną dla produktu %1.\n\n'
            'Czy zmienić ustawienie?'@[TKTLW.KTM().KTM]
         )
      || TKTLW.DEFAULT:='N';
         TKTLW.put()
      ?};
      return()
   || {? ~FUN.ask('Czy ustawić tę kartę jako domyślną dla produktu %1?'@[TKTLW.KTM().KTM])
      || return()
      ?}
   ?}
?};

_ktm:=TKTLW.KTM;
TKTLW.cntx_psh();
_tab:=tab_tmp(1,'REF','INTEGER','Ref');
TKTLW.index('PT');
TKTLW.prefix(_ktm);
{? TKTLW.first()
|| {!
   |? TKTLW.DEFAULT:='N';
      _tab.prefix(TKTLW.TKTL);
      {? ~_tab.first()
      || _tab.REF:=TKTLW.TKTL;
         _tab.add()
      ?};
      _can_continue:=TKTLW.put();
      TKTLW.next() & _can_continue>0
   !}
?};
{? _can_continue>0
||
   TKTLW.cntx_pop();
   TKTLW.DEFAULT:='T';
   _can_continue:=TKTLW.put();
   TKTLW.cntx_psh()
?};

{? _can_continue>0
||
   _tab.clear();
   TKTL.cntx_psh();
   TKTL.clear();
   TKTLW.index('REF');
   {? _tab.first()
   || {!
      |? {? TKTL.seek(_tab.REF,)
         || TKTLW.prefix(TKTL.ref());
            _t:=0; _n:=0;
            {? TKTLW.first()
            || {!
               |? {? TKTLW.DEFAULT='T'
                  || _t+=1
                  || _n+=1
                  ?};
                  TKTLW.next()
               !}
            ?};
            {? _t>0 & _n=0
            || TKTL.DEFAULT:='T'
            |? _t=0 & _n>0
            || TKTL.DEFAULT:='N'
            || TKTL.DEFAULT:='P'
            ?};
            _can_continue:=TKTL.put()
         ?};
         _tab.next() & _can_continue>0
      !}
   ?};
   TKTL.cntx_pop()
?};
TKTLW.cntx_pop();
{? _can_continue>0
||
:: NUCO - dodanie aktualizacji przepisów planistycznych po zmianie karty domyślnej.
   {? TKTLW.TKTL().TYP().TYP='PRD' & TKTLW.TKTL().ARCH='N' & TKTLW.DEFAULT='T' &  TKTLW.TKTL().STAN='T'
   || exec('akt_px_pro','qtpp',TKTLW.TKTL,'DEFAULT','')
   ?};
:: KONIEC ZMIANY
   _result:=1
?};
_result

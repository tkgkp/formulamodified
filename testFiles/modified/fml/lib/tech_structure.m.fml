:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tech_structure.fml
:: Utworzony: 05.03.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi struktury produktów
::======================================================================================================================


\env_create
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Tworzy obiekt środowiska obsługującego strukturę produktu
::   WE: _a - dostępność przeliczania i pól z datami
::----------------------------------------------------------------------------------------------------------------------
exec('env_delete','tech_structure');
__ENV_STR:=_env:=obj_new('CFG','STRUCT','ELEMENT','CUMULATED','BUF','PARAMS','KOMM','SELECTED','CHECK');
_env.STRUCT:=obj_new('TAB','WERS','WERE','NDX');
_env.ELEMENT:=obj_new('TAB','WER','NDX');
_env.CUMULATED:=obj_new('TAB','WER','NDX','NDXW');
_env.BUF:=obj_new('TAB','NDX');
_env.PARAMS:=obj_new('IL','DW','DRILL');
_env.KOMM:=obj_new(@.CLASS.JCQ); _env.KOMM.init(,,'Analiza surowców'@);
_env.SELECTED:=obj_new('TAB','NDX');
_env.CHECK:=obj_new('TAB','TAB_ZAM','WERZ','WERA','REDA2','REDA3','REDZ2','REDZ3','NDX');

:: Zmienne konfiguracyjne
_env.CFG:=obj_new('EARLIER','T_PRIMARY','T_STRUCT','CALC_KIND','COUNT');
:: ile dni wcześniej zakończyć produkcję półfabrykatów (ujęcie uproszczone)
_env.CFG.EARLIER:=0;
:: czy pokazywać zakładkę surowców podstawowych
_env.CFG.T_PRIMARY:=1;
:: czy pokazywac zakładkę struktury produktów
_env.CFG.T_STRUCT:=1;
:: sposób przeliczania: 1 - prosty, 2 - wg czasu produkcji
_env.CFG.CALC_KIND:=1;
:: dostępność przeliczania i pól z datami
_env.CFG.COUNT:=_a;

:: tabela zawierająca elementy struktury produktów
_env.STRUCT.TAB:=tab_tmp(4
   ,'PARENT','TREE_REF',''
   ,'M_KTM','STRING[50]','Indeks'@
   ,'POW','STRING[1]','Powierzony'@
   ,'SO','STRING[1]','Odpad'@
   ,'M_N','STRING[100]','Nazwa'@
   ,'M','STRING[16]','$M.ref()'
   ,'IL','REAL','Ilość'@
   ,'JM','STRING[10]','jm'@
   ,'DW','DATE','Data wymagana'@
   ,'TW','TIME','Godzina wymagana'@
   ,'XW','REAL','Znacznik czasowy'@
   ,'T_NRK','STRING[50]','Numer karty'@
   ,'T_WER','STRING[6]','Wersja karty'@
   ,'TKTL','STRING[16]','$TKTL.ref()'
   ,'MG_SYM','STRING[8]','Magazyn'@
   ,'MG','STRING[16]','$MG.ref()'
   ,'SRC_REF','STRING[16]','$ZK_P.ref(), $ZL.ref(), $TKTL.ref(): ref() pozycji źródłowej'
   ,'SRC','STRING[255]','Pozycja źródłowa'@
   ,'NORM_REF','STRING[16]','$TMAT.ref(), $PX_SUR.ref() lub inny ref() źródła normy'
   ,'SD','REAL','Stan dostępny'
   ,'WDR','REAL','W drodze'
   ,'OMIT','STRING[1]','Pomijać przy analizie zapotrzebowania (element rozwinięty)'@
   ,'STRUCT','STRING[1]','Pole, na podstawie którego wiem, że jest to tabela STRUCT'
);

_env.CUMULATED.TAB:=tab_tmp(4
   ,'PARENT','TREE_REF',''
   ,'M_KTM','STRING[50]','Indeks'@
   ,'POW','STRING[1]','Powierzony'@
   ,'SO','STRING[1]','Odpad'@
   ,'M_N','STRING[100]','Nazwa'@
   ,'M','STRING[16]','$M.ref()'
   ,'IL','REAL','Ilość'@
   ,'JM','STRING[10]','jm'@
   ,'DW','DATE','Data wymagana'@
   ,'TW','TIME','Godzina wymagana'@
   ,'XW','REAL','Znacznik czasowy'@
   ,'T_NRK','STRING[50]','Numer karty'@
   ,'T_WER','STRING[6]','Wersja karty'@
   ,'TKTL','STRING[16]','$TKTL.ref()'
   ,'MG_SYM','STRING[8]','Magazyn'@
   ,'MG','STRING[16]','$MG.ref()'
   ,'SRC_REF','STRING[16]','$ZK_P.ref(), $ZL.ref(), $TKTL.ref(): ref() pozycji źródłowej'
   ,'SRC','STRING[255]','Pozycja źródłowa'@
   ,'NORM_REF','STRING[16]','$TMAT.ref(), $PX_SUR.ref() lub inny ref() źródła normy'
   ,'SD','REAL','Stan dostępny'
   ,'WDR','REAL','W drodze'
   ,'OMIT','STRING[1]','Pomijać przy analizie zapotrzebowania (element rozwinięty)'@
   ,'STRUCT','STRING[1]','Pole, na podstawie którego wiem, że jest to tabela STRUCT'
);

_env.STRUCT.TAB.fld_fml('POW','DISPLAY_FORMAT',"'empty=1'");
_env.STRUCT.TAB.fld_fml('SO','DISPLAY_FORMAT',"'empty=1'");

:: indeksy STRUCT.TAB
_env.STRUCT.NDX:=obj_new('TREE','OMIT');
_env.STRUCT.NDX.TREE:=_env.STRUCT.TAB.index('?');
_env.STRUCT.NDX.OMIT:=_env.STRUCT.TAB.ndx_tmp(,,'OMIT',,,'M_KTM',,);

:: okna STRUCT.TAB
_env.STRUCT.WERE:=exec('mk_sel_struct','tech_structure',1);
_env.STRUCT.WERS:=exec('mk_sel_struct','tech_structure',2);

_env.CUMULATED.NDX:=_env.CUMULATED.TAB.ndx_tmp(,,'M',,,'POW',,,'SO',,,'OMIT',,);
_env.CUMULATED.NDXW:=_env.CUMULATED.TAB.ndx_tmp(,,'OMIT',,,'M_KTM',,);
_env.CUMULATED.WER:=exec('mk_sel_struct','tech_structure',3);
_env.CUMULATED.TAB.fld_fml('POW','DISPLAY_FORMAT',"'empty=1'");
_env.CUMULATED.TAB.fld_fml('SO','DISPLAY_FORMAT',"'empty=1'");

:: tabela zawierająca pogrupowane Surowce wg zapotrzebowań
_env.ELEMENT.TAB:=tab_tmp(4
   ,'PARENT','TREE_REF',''
   ,'LABEL','STRING[255]','Indeks / pozycja źródłowa'@
   ,'POW','STRING[1]','Powierzony'@
   ,'SO','STRING[1]','Odpad'@
   ,'M_KTM','STRING[50]','Indeks'@
   ,'M_N','STRING[100]','Nazwa'@
   ,'M','STRING[16]','$M.ref()'
   ,'IL','REAL','Ilość'@
   ,'JM','STRING[10]','jm'@
   ,'DW','DATE','Data wymagana'@
   ,'TW','TIME','Godzina wymagana'@
   ,'XW','REAL','Znacznik czasowy'@
   ,'SRC_REF','STRING[16]','$ZK_P.ref(), $ZL.ref(), $TKTL.ref(): ref() pozycji źródłowej'
   ,'SRC','STRING[255]','Pozycja źródłowa'@
   ,'NORM_REF','STRING[16]','$TMAT.ref(), $PX_SUR.ref() lub inny ref() źródła normy'
   ,'SD','REAL','Stan dostępny'
   ,'WDR','REAL','W drodze'
   ,'OMIT','STRING[1]','Pomijać przy analizie zapotrzebowania (element źródłowy)'@
   ,'ELEMENT','STRING[1]','Pole, na podstawie którego wiem, że jest to tabela ELEMENT'
);
_env.ELEMENT.TAB.fld_fml('POW','DISPLAY_FORMAT',"'empty=1'");
_env.ELEMENT.TAB.fld_fml('SO','DISPLAY_FORMAT',"'empty=1'");
_env.ELEMENT.TAB.fld_fml('SD','DISPLAY_FORMAT',"{? cur_tab(1,1).SRC_REF<>'' || 'empty=1' || 'empty=0' ?}");
_env.ELEMENT.TAB.fld_fml('WDR','DISPLAY_FORMAT',"{? cur_tab(1,1).SRC_REF<>'' || 'empty=1' || 'empty=0' ?}");

:: indeksy ELEMENT.TAB
_env.ELEMENT.NDX:=obj_new('TREE','MOD');
_env.ELEMENT.NDX.TREE:=_env.ELEMENT.TAB.index('?');
_env.ELEMENT.NDX.MOD:=_env.ELEMENT.TAB.ndx_tmp(,,'M',,,'POW',,,'SO',,,'OMIT',,,'DW',,,'TW',,);

:: okna ELEMENT.TAB
_env.ELEMENT.WER:=exec('mk_sel_element','tech_structure');

:: tabela zawierająca zaznaczone rekordy do analizy - musi być jako element pośredni
:: (sortowana wg terminu zapotrzebowania)
_env.BUF.TAB:=tab_tmp(1,
   'XW','REAL','Znacznik czasowy'@,
   'REF','INTEGER','#ref() tabeli'
);

:: indeks tabeli BUF.TAB
_env.BUF.NDX:=_env.BUF.TAB.index('?');

:: parametry
_env.PARAMS.IL:=0;
_env.PARAMS.DW:=date(0,0,0);
_env.PARAMS.DRILL:=1;

:: pomocnicza tabela zawierająca zaznaczone rekordy źródłowe
:: (używana np. dla zleceń w celu eliminacji nadmiarowych elementów)
_env.SELECTED.TAB:=tab_tmp(1,
   'REF','INTEGER','#ZL.ref()',
   'OMIT','STRING[1]','Czy pomijać w przetwarzaniu'
);
_env.SELECTED.NDX:=_env.SELECTED.TAB.index('?');
::----------------------------------------------------------------------------------------------------------------------
::  MOD: PeKa [17.10.2022]
:: OPIS: Dodanie do tabeli problemow informacji o indeksie
::----------------------------------------------------------------------------------------------------------------------
::   Tabela z informajcą o napotkanych problemach
_env.CHECK.TAB:=tab_tmp(5,
   'NRK','STRING[30]','Numer karty',
   'WER','STRING[6]','Wersja',
   'TMAT','STRING[16]','REF TMAT',
   'KTLZ','STRING[16]','REF KTL',
   'KTM','STRING[16]','REF KTM',
   'POW','STRING[30]','Powód',
   'AKCJA','STRING[25]','Akcja',
   'A','STRING[1]','Znak akcji',
   'KTLW','STRING[16]','REF do wskazanego KTL',
   'KTLW_NRK','STRING[30]','Numer karty wskazanego KTL',
   'KTM_NAZ','STRING[100]','Nazwa materiału',
   'KTM_IND','STRING[50]','Indeks materiału',
   'KTM_SRC','STRING[50]','Źródło indeksu',
   'ARCH','STRING[1]','Czy archiwalana',
   'KTLW_WER','STRING[6]','Wersja karty wskazanej'
);

::   Tabela z informajcą o zamiennikach
_env.CHECK.TAB_ZAM:=tab_tmp(2,
   'TMAT','STRING[16]','REF TMAT',
   'KTLW','STRING[16]','REF do wskazanego KTL'
);

:: okna tabeli CHECK.TAB
_env.CHECK.WERA:=exec('tktl_chck_mk_sel','tech_structure',1);
_env.CHECK.WERZ:=exec('tktl_chck_mk_sel','tech_structure',0);
_env.CHECK.REDZ2:=exec('tktl_chck_mk_edit','tech_structure',0,0);
_env.CHECK.REDZ3:=exec('tktl_chck_mk_edit','tech_structure',0,1);
_env.CHECK.REDA2:=exec('tktl_chck_mk_edit','tech_structure',1,0);
_env.CHECK.REDA3:=exec('tktl_chck_mk_edit','tech_structure',1,1);

_env


\expand
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Rozwija strukturę elementu o jeden poziom (dla węzła będącego pozycją zamówienia albo wynikającą z rozwinięcia)
::       Uwzględniane są tylko surowce (bez zamienników) wg domyślnych wartości parametrów technologii
::       Dla grup technologicznych zwracane są indeksty domyślne
::       Karta półfabrykatu domyślna albo jawnie wskazana (wg wpisu w TMAT)
::       Termin wyliczany jest w oparciu o zmienną konfiguracyjną __ENV_STR.CFG.EARLIER
::   WE: _a - __ENV_STR.STRUCT.TAB.ref()
::----------------------------------------------------------------------------------------------------------------------
_tab_ref:=_a;
_env:=__ENV_STR;
_tab:=_env.STRUCT.TAB;

_tab.cntx_psh();
_tab.prefix();
{? _tab.seek(_tab_ref)
||
:: Pozycja z planu strategicznego
   {? _tab.SRC_REF<>'' & ref_tab(_tab.SRC_REF)=PX_OBJ
   ||
::      do();
      _tab.OMIT:='T'; _tab.put();

      _args:=exec('add_a','tech_structure');
      _args.PARENT:=#_tab.ref();
      _ver:=exec('get_mainversion','px_ver');
      PX_POZ.cntx_psh(); PX_SUR.cntx_psh();
      PX_POZ.index('TM_VOBJ');
      PX_POZ.prefix(_ver,exec('FindAndGet','#table',PX_OBJ,_tab.SRC_REF,,"ref()",null()));
      PX_SUR.prefix();
      PX_SUR.f_set('STARTD,STARTT,M(KTM)',,'PX_SUR.PX_POZ in (select REFERENCE from prefixed_table(:_a))',PX_POZ);
      {? PX_SUR.f_first()
      || {!
         |? _args.M:=PX_SUR.M;
            _args.IL:=PX_SUR.IL;
            _args.DW:={? _env.PARAMS.DW<>date(0,0,0) || _env.PARAMS.DW || PX_SUR.STARTD ?};
            _args.TW:={? _env.PARAMS.DW<>date(0,0,0) || time(6,0,0) || PX_SUR.STARTT ?};
            _args.NORM_REF:=$PX_SUR.ref();
            exec('add','tech_structure',_args);
            PX_SUR.f_next()
         !}
      ?};
      PX_SUR.f_clear();
      PX_POZ.cntx_pop(); PX_SUR.cntx_pop();
::      end();
      ~~

:: Wg wpisanej technologii
   |? _tab.TKTL<>''
   ||
::      do();
      _tab.OMIT:='T'; _tab.put();

      _args:=exec('add_a','tech_structure');
      _args.PARENT:=#_tab.ref();
      _il:=_tab.IL;
      exec('tktl_cntx_psh','tech_common');
      exec('tktl_use','tech_common',ref_name(_tab.TKTL)+3);
::    Dla uproszczenia surowce wykazywane są w kolejności od najwyższego numeru operacji
::    (nie są analizowane następniki operacji)
      TMAT.index('ANNL');
      TMAT.prefix('T',exec('FindAndGet','#table',TKTL,_tab.TKTL));
      {? TMAT.last()
      || {!
         |? _args.M:={? TMAT.GRKTM='G' || TMAT.TGDFLT().PT || TMAT.PT ?};
            _args.IL:=TMAT.WARB*_il/TMAT.NRK().XJM;
            _ktl:=null();
            _added:=0;
            _arch:='N';
            {? var_pres('__ENV_STR')>0 & (var_pres('TAB_ZAM',__ENV_STR.CHECK)) >0 & __ENV_STR.CHECK.TAB_ZAM.find_tab(,'TMAT',,'=',$TMAT.ref)
            || _ktl:=exec('FindAndGet','#table',TKTL,__ENV_STR.CHECK.TAB_ZAM.KTLW,,,null())
            ||
               {? TMAT.DFLT_KTL='T'
               || _ktl:=exec('dflt_ktl','tech_prod',_args.M,,1);
                  {? _ktl=null()
                  || _txt:='Brak karty domyślnej'@;
                     _added:=1
                  |? ~exec('tktl_act','tech_head',,$_ktl)
                  || _txt:='Karta domyślna nieaktualna'@;
                     _added:=1
                  ?}
               || _ktl:=exec('FindAndGet','#table',TKTL,TMAT.RKTL,,,null);
                  {? _ktl<> null
                  || _arch:=exec('FindAndGet','#table',TKTL,_ktl,,"ARCH",'N');
                     {? _arch<>'N'
                     || _txt:='Karta archiwalna'@;
                        _added:=1
                     |? ~exec('tktl_act','tech_head',4)
                     || _txt:='Karta nieaktualna'@;
                        _added:=1
                     ?}
                  ?}
               ?}
            ?};
            {? _added=1
            || exec('tktl_chck_add','tech_structure',$_ktl,$TMAT.ref,_txt,TMAT.PT().N,
                     exec('FindAndGet','#table',TKTL,_ktl,,'NRK',''),exec('FindAndGet','#table',TKTL,_ktl,,'WER',''),
                     $TMAT.PT,{? _arch<>'N' || 'T' || 'N' ?})
            ?};
            _args.TKTL:=_ktl;
            _args.DW:={? _env.PARAMS.DW<>date(0,0,0) || _env.PARAMS.DW || _tab.DW ?};
            _args.TW:={? _env.PARAMS.DW<>date(0,0,0) || time(6,0,0) || _tab.TW ?};
            {? _env.CFG.CALC_KIND=1
            || _args.DW-=_env.CFG.EARLIER
            |? _env.CFG.CALC_KIND=2
            || _args.DW-=exec('tmat_time','tech_structure',TMAT.ref(),_il/TMAT.NRK().XJM).DAYS
            ?};
            _args.NORM_REF:=$TMAT.ref();
            exec('add','tech_structure',_args);
            {? _added=0 || {? _env.PARAMS.DRILL || exec('expand','tech_structure',_args.ref) ?} ?};
            TMAT.prev()
         !}
      ?};
      exec('tktl_cntx_pop','tech_common');
::      end();
      ~~
   ?}
?};

_tab.cntx_pop();
~~


\icon_src
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Ikona źródła pozycji
::----------------------------------------------------------------------------------------------------------------------
::  MOD: PeKa [30.03.2023]
:: OPIS: dodanie obslugi wyswietlania pozycji zrodlowej dla ofer w analize surowca
_tab:=cur_tab(1,1);
{? _tab.SRC_REF<>'' &
   (ref_tab(_tab.SRC_REF)=ZK_P | ref_tab(_tab.SRC_REF)=ZL | ref_tab(_tab.SRC_REF)=TKTL | ref_tab(_tab.SRC_REF)=TKTLW
   | ref_tab(_tab.SRC_REF)=OFP)
|| 'xwin16.png:185'
|? _tab.SRC_REF<>'' & ref_tab(_tab.SRC_REF)=PX_OBJ
|| exec('px_plan','icon')
|| ''
?}


\edit_params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Redagowanie parametrów
::   WE: _a - wariant redagowania: dostępna ilość
::       _b - wariant redagowania: dostępna data
::       _c - wariant redagowania: dostępny drill
::  MOD: SITEK - wyswietlanie dla jakiego KTMu ustawiamy ilosc i dw
::       [_d] - opcjonalny - opis dla jakiego ktmu ustawiamy ilosc i dw
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _v_il:=_a || _v_il:=0 ?};
{? var_pres('_b')=type_of(0) || _v_dw:=_b || _v_dw:=0 ?};
{? var_pres('_c')=type_of(0) || _v_drill:=_c || _v_drill:=0 ?};
{? var_pres('_d')>0 || _v_label:=_d || _v_label:='' ?};
params_set('v_il',_v_il,'v_dw',_v_dw,'v_drill',_v_drill,'v_label',_v_label);

_env:=__ENV_STR;

_tab:=tab_tmp(
   ,'IL','REAL','Ilość'@
   ,'DW','DATE','Termin wymagany'@
   ,'DRILL','INTEGER','Zakres analizy'@
   ,'TAB','STRING[8]','Miejsce wywołania'@
);

_red:=_tab.mk_edit('Analiza surowców'@,,'#editparams'+$_v_il+$_v_dw+$_v_drill);
_tab.win_esep(_red,'Parametry analizy'@);
{? _v_label <> '' || _tab.win_esep(_red,'%1'[_v_label]) ?};
{? _v_il || _tab.win_efld(_red,,'IL',,,15,ST.DOKL); _tab.efld_opt(_red,'mark=1',,'IL') ?};
{? _v_dw || _tab.win_efld(_red,,'DW',,,12); _tab.efld_opt(_red,'mark=1',,'DW') ?};
{? _v_drill
|| _tab.win_efld(_red,,'DRILL',,,,,,,,,'radio-buttons',
      ,'Tylko surowce na pierwszym poziomie'@,"0"
      ,'Surowce na wszystkich poziomach'@,"1"
   )
?};
exec('ok_esc','#window',_tab,_red);
:: PK - funkcja po przerobce dostepna rowniez od strony zlecen
_tab.TAB:=2-!cur_tab;
_tab.win_edit(_red);
_chk:="
   _tab:=cur_tab(1,1);
   _args:=params_get();
   _chk:={? _args.v_il || {? exec('itsPositive','#field',1,,_tab.IL) || '' || 'IL' ?} || '' ?};
   _chk:=
      {? _chk='' & _args.v_dw
      || {?  _tab.TAB='ZL'
         || {? _tab.DW<>date(0,0,0) & _tab.DW<date() || FUN.info('Data nie może być wcześniejsza niż dzisiejsza.'@); _chk:='DW' || _chk ?}
         || {? _tab.DW<date() || FUN.info('Data nie może być wcześniejsza niż dzisiejsza.'@); _chk:='DW' || _chk ?}
         ?}
      || _chk
      ?};
   _chk
";

_tab.IL:=_env.PARAMS.IL;
_tab.DW:=_env.PARAMS.DW;
_tab.DRILL:=_env.PARAMS.DRILL;
_edited:=_tab.edit(_chk);
{? _edited
|| {? _v_il || _env.PARAMS.IL:=_tab.IL || _env.PARAMS.IL:=0 ?};
   {? _v_dw || _env.PARAMS.DW:=_tab.DW || _env.PARAMS.DW:=date() ?};
   {? _v_drill || _env.PARAMS.DRILL:=_tab.DRILL || _env.PARAMS.DRILL:=1 ?}
?};
_edited


\tktl_chck_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [19.42]
:: OPIS: Dodaje rekord do tabeli napotkanych błędów
::  WE: _a STRING $TKTL.ref()
::      _b STRING $TMAT.ref()
::      _c STRING Przyczyna błędu
::      _d STRING Nazwa materiału
::      _e STRING Nr karty technologicznej
::      _f STRING Wersja karty
::      _g STRING $KTM.ref
::      _h STRING Czy archiwalna
::----------------------------------------------------------------------------------------------------------------------
::  MOD: PeKa [17.10.2022]
:: OPIS: Dodanie dodatkowej informacji o indeksie
::      _i STRING Indeks
::      _j STRING Źródło indeksu (np. pozycja zamówienia/półprodukt indeksu)
::----------------------------------------------------------------------------------------------------------------------
::----------------------------------------------------------------------------------------------------------------------
{? var_press('__ENV_STR') > 0
||
   _env:=__ENV_STR;
   _tab:=_env.CHECK.TAB;

   {? var_press('_a')=type_of('a') || _ktl:=_a || _ktl:='' ?};
   {? var_press('_b')=type_of('a') || _tmat:=_b || _tmat:='' ?};
   {? var_press('_c')=type_of('a') || _pow:=_c || _pow:='' ?};
   {? var_press('_d')=type_of('a') || _naz:=_d || _naz:='' ?};
   {? var_press('_e')=type_of('a') || _nrk:=_e || _nrk:='' ?};
   {? var_press('_f')=type_of('a') || _wer:=_f || _wer:='' ?};
   {? var_press('_g')=type_of('a') || _ktm:=_g || _ktm:='' ?};
   {? var_press('_h')=type_of('a') || _arch:=_h || _arch:='N' ?};
   {? var_press('_i')=type_of('a') || _ind:=_i || _ind:='' ?};
   {? var_press('_j')=type_of('a') || _src:=_j || _src:='' ?};

   {? ~_tab.find_tab(,'TMAT',,'=',_tmat,'KTLZ',,'=',_ktl)
   ||
      _tab.TMAT:=_tmat;
      _tab.KTM_NAZ:=_naz;
      _tab.KTLZ:=_ktl;
      _tab.KTLW:='';
      _tab.KTLW_NRK:='';
      _tab.POW:=_pow;
      _tab.AKCJA:='Nie analizuj'@;
      _tab.A:='N';
      _tab.NRK:=_nrk;
      _tab.WER:=_wer;
      _tab.KTM:=_ktm;
      _tab.ARCH:=_arch;
      _tab.KTM_IND:=_ind;
      _tab.KTM_SRC:=_src;
      _tab.add()
   ?}
?};
~~


\tktl_chck_mk_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [19.42]
:: OPIS: Tworzy okno selekcji dla tabeli napotkanych błędów
::  WE: _a - NUMBER - kontekst wywolania:
::           0(domyślnie) -  generowanie zleceń
::           1 - analizy
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _choice:=_a || _choice:=0 ?};
_env:=__ENV_STR;
_tab:=_env.CHECK.TAB;

_sel:=_tab.mk_sel(,,,,,,,,'U','U',,,,);
_tab.win_fld(_sel,,'KTM_SRC',,,-20,,1,'Źródło indeksu'@);
_tab.win_fld(_sel,,'KTM_NAZ',,,-35,,1,'Nazwa materiału'@);
_tab.win_fld(_sel,,'KTM_IND',,,-25,,1,'Indeks materiału'@);
_tab.win_fld(_sel,,'NRK',,,-25,,1,'Numer karty technologicznej'@);
_tab.win_fld(_sel,,'WER',,,,,1,'Wersja'@);
_tab.win_fld(_sel,,'POW',,,-25,,1,'Powód'@);
_tab.win_fld(_sel,,'AKCJA',,,-25,,,'Akcja'@);
_tab.win_fld(_sel,,'KTLW_NRK',,,-20,,,'Wskazana karta technologiczna'@);
_tab.win_fld(_sel,,'KTLW_WER',,,-6,,,'Wersja karty wskazanej'@);

{? _choice=0
|| _tab.win_act(_sel,,'Popraw',,,,"exec('tktl_chck_pop','tech_structure',0)",,1);
   _close:="
      {? _a='sel_exit'
      || return(1)
      || {? FUN.ask('Czy chcesz przerwać generowanie technologii? Dane w oknie nie zostaną zapamiętane.'@)
         || return(1)
         || return(0)
         ?}
      ?}
   "
|| _tab.win_act(_sel,,'Popraw',,,,"exec('tktl_chck_pop','tech_structure',1)",,1);
   _close:="
      {? _a='sel_exit'
      || return(1)
      || {? FUN.ask('Czy chcesz przerwać analizę? Dane w oknie nie zostaną zapamiętane.'@)
         || return(1)
         || return(0)
         ?}
      ?}
   "
?};
_tab.win_act(_sel,,'Formuła','Akceptuj'@@,,,"sel_exit()",,,,,,'A');
_tab.win_btn(_sel,'text=%1,panel=bottom,align=begin'['Akceptuj'@@],'menu:A');

_tab.fld_attr(,2);

_grp:=_tab.grp_make('Lista napotkanych problemów'@,,'probgrp',,,_close,,'normal');
_tab.grp_sel(_grp,,_sel,,,,,,,,,,'maximized');

_grp



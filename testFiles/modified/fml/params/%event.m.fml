:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %event.fml
:: Utworzony: 07.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły dla zdarzeń warunkowych
::======================================================================================================================


\poczekaj_na_robocizne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Oczekiwanie na zarejestrowaną robociznę - obsługa zdarzenia warunkowego pośredniego,
::       po którym uruchomiona zostanie rejestracja dokumentu magazynowego
::       Formuła do zastosowania w zdarzeniu warunkowym - przykładowe wywołanie (p01 jest portem typu _ZL):
::          exec('poczekaj_na_robocizne','%event',_a.p01)
::   WE: _a - ZL.ref()
::   WY: 1 - są rekordy ZLGD bez wygenerowanych ilości na dokumentach
::       0 - żaden rekord ZLGD nie wymaga generowania dokumentu
::       STRING - komunikat błędu
::----------------------------------------------------------------------------------------------------------------------
_zl:=null();
{? var_pres('_a')=type_of(null())
|| _zl:=_a
?};

_result:=0;
:: NUCO
{? _zl<>null() & exec('FindAndGet','#table',ZL,_zl,,"STAN<>'Z' & STAN<>'N'",1)
|| ST.ODDZ:=exec('FindAndGet','#table',ZL,_zl,,"ODDZ",'_');
   ST.AR:=date()~1;
   ST.AM:=date()~2;
   exec('open','open_tab',ST.ODDZ,($ST.AR)+2);

   ZLGD.cntx_psh();
   _names:=ZLGD.names();
   {? _names.first()
   || {!
      |? ZLGD.use(_names.NAME);
         ZLGD.index('ZLECENIE');
         ZLGD.prefix(_zl);
         {? ZLGD.first()
         || {!
            |? _ilegen:=exec('ilegen','magdok_prod',ZLGD.ZPARN).ilegen;
               {? _ilegen>0
               || _result:=1
               ?};
               _result=0 & ZLGD.next()
            !}
         ?};
         _result=0 & _names.next()
      !}
   ?};
   ZLGD.cntx_pop()
?};

_result


\jest_do_wykonania
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Oczekiwanie na możliwość rejestrowania wykonań do zlecenia
::       Przechodzimy, gdy ilość wykonana na zleceniu produkcyjnym jest mniejsza niż planowana
::       albo jest to zlecenie warsztatowe otwarte
::       Formuła do zastosowania w zdarzeniu warunkowym - przykładowe wywołanie (p01 jest portem typu _ZL):
::          exec('jest_do_wykonania','%event',_a.p01)
::   WE: _a - ZL.ref()
::   WY: 1 - można rejestrować wykonania
::       0 - nie można rejestrować wykonania
::----------------------------------------------------------------------------------------------------------------------
_zl:=null();
{? var_pres('_a')=type_of(null())
|| _zl:=_a
?};

_result:=0;
:: NUCO
_zl_tab:=sql('select WP from ZL join ZTP using (ZL.TYP,ZTP.REFERENCE) where ZL.REFERENCE=:_a and ZL.STAN=\'O\'',_zl);

{? _zl_tab.first()
|| {? _zl_tab.WP='P'
   || {? sql('select ZGP.PLANUJ from ZGP where ZGP.ZL=:_a and ZGP.STATUS<>\'W\'',_zl).first()
       || _result:=1
       ?}
   || _result:=1
   ?}
?};

_result

::
::ZL.cntx_psh();
::ZL.prefix();
::{? ZL.seek(_zl)
::|| {? ZL.TYP().WP='P'
::   || ZGP.cntx_psh();
::      ZGP.clear();
::      ZGP.f_clear();
::      {? ZGP.find_tab(,'ZL',,'=',_zl,'STATUS',,'<>',exec('status_end','zl_guide'))
::      ||
::         _result:=ZL.STAN='O'
::         _result:=ZL.ILWYK_D<ZL.IL
::      ?};
::      ZGP.cntx_pop()
::   || _result:=ZL.STAN='O'
::   ?}
::?};
::ZL.cntx_pop();
::
::_result












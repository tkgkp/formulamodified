:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: qlokalizacje.fml
:: Utworzony: 26.04.2021
:: Autor: TP
:: Systemy:
::======================================================================================================================
:: Zawartość: Funkcje do dodatkowej obsługi lokalizacji 1
::======================================================================================================================

\zmien_lokalizacje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_TEST]
:: OPIS: Funkcja wyświetala okno z możliwością zmiany przypisania lokalizacji do innfego magazynu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

VAR_DEL.delete('sWinEANL','sWinMG','sWinSL','sEdtEANL','sEdtMG','sEdtSL');

EANL.cntx_psh();
MG.cntx_psh();
SL.cntx_psh();

MG.index('MAGS');
MG.prefix('T');
MG.first();

EANL.index('MG');
EANL.prefix('T',MG.ref());
EANL.first();

SL.use('stl__wzb');
SL.index('EANL');
SL.prefix(EANL.MG,EANL.ref);
SL.first();


:: Okienka dla EANL (lokalizacji)

sWinEANL:=EANL.mk_sel('Lista lokalizacji','P',,'#eanl_swineanl',,,,,'U','T');
EANL.win_sel(sWinEANL);
EANL.win_fld(sWinEANL,,'KOD'     ,          ,          , 15,  , ,'Kod lokalizacji',,'Kod lokalizacji',,1);
:: EANL.win_fld(sWinEANL,,'EAN'     ,          ,          , 15,  , ,'Kod kreskowy',,'Kod kreskowy lokalizacji',,1);
EANL.win_fld(sWinEANL,,'OP'      ,          ,          , 40,  , ,'Opis lokalizacji',,'Opis lokalizacji',,1);
EANL.win_act(sWinEANL, ,'Szukaj');

sEdtEANL:=EANL.mk_edit('Lokalizacja',,'#eanl_swineanl');
EANL.win_edit(sEdtEANL);
EANL.win_efld(sEdtEANL,,'KOD'     ,          ,          , 20,  , ,'Kod lokalizacji',,'Kod lokalizacji',,);
EANL.win_efld(sEdtEANL,,'EAN'     ,          ,          , 30,  , ,'Kod kreskowy',,'Kod kreskowy lokalizacji',,);
EANL.win_efld(sEdtEANL,,'OP'      ,          ,          ,120,  , ,'Opis lokalizacji',,'Opis lokalizacji',,);

:: Okienka dla MG

sWinMG:=MG.mk_sel('Lista magazynów','P',,'#mg_swinmg',,,,,'U','T');
MG.win_sel(sWinMG);
MG.win_fld(sWinMG,,'SYM'     ,          ,          ,  8,  , ,'Symbol magazynu',,'Symbol magazynu',,1);
MG.win_fld(sWinMG,,'NAZ'     ,          ,          , 20,  , ,'Nazwa magazynu',,'Nazwa magazynu',,1);
MG.win_fld(sWinMG,,'EANL'    ,'KOD'     ,          , 10,  , ,'Domyślna lokalizacja',,'Domyślna lokalizacja dla magazynu',,1);
MG.win_act(sWinMG, ,'Szukaj');

sEdtMG:=MG.mk_edit('Magazyn',,'#mg_swinmg');
MG.win_edit(sEdtMG);
MG.win_efld(sEdtMG,,'SYM'     ,          ,          ,  8,  , ,'Symbol magazynu',,'Symbol magazynu',,);
MG.win_efld(sEdtMG,,'NAZ'     ,          ,          , 20,  , ,'Nazwa magazynu',,'Nazwa magazynu',,);
MG.win_efld(sEdtMG,,'EANL'    ,'KOD'     ,          , 10,  , ,'Domyślna lokalizacja',,'Domyślna lokalizacja dla magazynu',,);

:: DnD - przeniesienie lokalizacji do innego magazynu
MG.dnd_sel(sWinMG,,'records.EANL',"
           {? dnd_info('dest_record')=null()
           || FUN.error('Lokalizacje można przenieść tylko na inny magazyn, nie możesz przenosić na wolne pole !!!')
           || _na:=sql('select * from MG where MG.REFERENCE = :_a',dnd_info('dest_record'));
              _co:=dnd_info('dropped_records');
              {? type_of(_co) = type_of(~~) || return(0) ?};
              _loop:=_co.first;
              {!|? _loop |!
                 EANL.seek(_co.REF,);
                 {? EANL.AKT='N'
                 || FUN.error('Lokalizacja '+EANL.KOD+' jest nieaktywna, można przenieść tylko aktywną lokalizacje')
                 || {? EANL.ref()=EANL.MG().EANL
                    || FUN.error('Nie można przenosić lokalizacji domyślnej dla magazynu')
                    || SL.clear();
                       SL.prefix(EANL.MG,EANL.ref);
                       {? SL.first()
                       || FUN.error('Lokalizacja '+EANL.KOD+' aktualnie posiada stan, przeniesienei nie jest możliwe')
                       || {? FUN.ask('Czy przesunąć lokalizację ' + EANL.KOD +' do magazynu '+ _na.SYM)
                          ||  exec('przenies_lok','qlokalizacje',EANL.ref(),dnd_info('dest_record'))
                          ?}
                       ?}
                    ?}
                 ?};
                 _loop:=_co.next
              !}
           ?};
           1
           ");

:: Okienko dla SL
sWinSL:=SL.mk_sel('Stan lokalizacji','P',,'#sl_swinsl',,,,,'U','T');
SL.win_sel(sWinSL);
SL.win_fld(sWinSL,,'M'       ,'KTM'     ,          , 15,  , ,'Materiał',,'Indeks materiałowy',,1);
SL.win_fld(sWinSL,,'M'       ,'N'       ,          , 25,  , ,'Materiał',,'Nazwa materiału',,1);
SL.win_fld(sWinSL,,'TW'      ,          ,          , 10,  , ,'Termin ważności',,'Termin ważności',,1);
SL.win_fld(sWinSL,,'IL'      ,          ,          , 10, 4, ,'Ilość',,'Ilość na stanie wg wymiarów',,1);
SL.win_fld(sWinSL,,'JM'      ,'KOD'     ,          ,  5,  , ,'Jednostka miary',,'Jednostka miary',,1);
SL.win_act(sWinSL, ,'Szukaj');

:: Okienko grupowe
_lokalizacje:=MG.grp_make();
MG.grp_sel(_lokalizacje,,sWinMG,,"EANL.prefix();
                                  EANL.prefix('T',MG.ref());
                                  EANL.first();
                                  grp_disp(EANL,sWinEANL)
                                 ",,,,,,,,'maximized_with_title','qsWinMG');
MG.grp_splt(_lokalizacje,'panel0','vertical','prawy');
MG.grp_sel(_lokalizacje,EANL,sWinEANL,,"SL.prefix();
                                        SL.prefix(EANL.MG,EANL.ref);
                                        SL.first();
                                        grp_disp(SL,sWinSL)",,,,,,,,'maximized_with_title');
MG.grp_splt(_lokalizacje,'prawy','vertical','prawy2');
MG.grp_sel(_lokalizacje,SL,sWinSL,,,,,,,,,,'maximized_with_title');

MG.win_sel(_lokalizacje);
MG.select();

EANL.cntx_pop();
MG.cntx_pop();
SL.cntx_pop();
1


\przenies_lok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [12.51]
:: OPIS: Weryfikuje i przenosi jedną lokalizację z miejsca do miejsca
::   WE: _a - lokalizacja do przeniesienie EANL.ref
::       _b - magazyn na jaki należy przenieść lokalizację
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_zmiana:=0;
{? EANL.seek(_a)
|| _eanl_kod:=exec('rb_nosp','#string',exec('repl_substr','#string',EANL.KOD,(' - '+EANL.MG().SYM),' '));
   {? _eanl_kod<>''
   || EANL.index('MG');
      MG.seek(_b);
      EANL.prefix('N',_b,_eanl_kod+' - '+MG.SYM, _eanl_kod+' - '+MG.SYM);
      {? EANL.first()
      || EANL.prefix();
         EANL.AKT:='T';
         EANL.EAN:=_eanl_kod;
         {? EANL.put(1)
         || _zmiana:=1
         ?}
      || EANL.prefix('T',_b,_eanl_kod+' - '+MG.SYM,_eanl_kod+' - '+MG.SYM);
         {? EANL.first()
         || FUN.info('Lokalizacja którą próbujesz przenieść jest już powiązana z tym magazynem')
         || EANL.blank();
            EANL.MG:=MG.ref();
            EANL.OP:=EANL.KOD:=_eanl_kod+' - '+MG.SYM;
            EANL.EAN:=_eanl_kod;
            EANL.AKT:='T';
            {? EANL.add(1)
            || _zmiana:=1
            ?}
         ?}

      ?};
      {? _zmiana
      || EANL.prefix();
         EANL.seek(_a);
         EANL.EAN:=_eanl_kod+' - '+EANL.MG().SYM;
         EANL.AKT:='N';
         EANL.put(1)
      ?}
   || FUN.info('Błąd przy przeniesieniu - błędny kod kreskowy lokalizacji')
   ?}
|| FUN.info('Błąd przy przeniesieniu - błędny parametr EANL.ref')
?};
_zmiana

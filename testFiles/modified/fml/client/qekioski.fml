:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: qekioski.fml
:: Utworzony: 28.07.2021
:: Autor: PeKa
:: Systemy:
::======================================================================================================================
:: Zawartość: Uszlachetnienie standardowego dzialania ekioskow
::======================================================================================================================


\ilosc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [21.14]
:: OPIS: podpowiedz ilosci surowca z normy brutto minus pobrane
::----------------------------------------------------------------------------------------------------------------------
:: Pole z kodem kreskowym w okienku (z czytnika kodów kreskowych)
_kkod:=exec('get_value','#desktop','','ctr_menu_sur','fld_dostawa@menu_sur');
{? _kkod<>''
|| _refktm:=sql('select M as MREF from @SC where SC.SCEAN=\':_a\'',_kkod).MREF;
   {? _refktm<>''
   || _tab2:=__ENV_REJ.TAB_N;
      _tab2.index(__ENV_REJ.NDX_N);
      {? _tab2.find_key(_refktm)
      || grp_disp(__ENV_REJ.TAB_N,__ENV_REJ.WER_N,1)
      ?};
      _limit:=sql('select IL, LIL from :_a where :_a.MREF=\':_b\'',__ENV_REJ.TAB_N,_refktm);
      {? _limit.size>0
      ||
:: jest material na liscie limitow
         _ilosc:=_limit.LIL-_limit.IL;
         {? _ilosc>0
         || exec('set_value','#desktop','','ctr_menu_sur','fld_il_sur@menu_sur',$_ilosc);
            exec('grab_focus','#desktop','','ctr_menu_sur','fld_il_sur@menu_sur')
         ?}
      || exec('grab_focus','#desktop','','ctr_menu_sur','fld_il_sur@menu_sur')
      ?}
:: kod dostawy materialu, ktory nie wystepuje na liscie limitow, ustawiam focus w polu ilosc
   || exec('grab_focus','#desktop','','ctr_menu_sur','fld_il_sur@menu_sur')
   ?}
?};
~~

\kolor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [21.14]
:: OPIS: kolorowanie wartosci pola pobrane (niepelna ilosc zolty, pobrane >= zielony), niebieski - limit spoza listy
::----------------------------------------------------------------------------------------------------------------------
__ENV_REJ.TAB_N.fld_fml('IL','BEFORE_DISPLAY',
                      "{? __ENV_REJ.TAB_N.ZLIMREF<>''& exec('FindAndGet','#table',ZLIM,__ENV_REJ.TAB_N.ZLIMREF,,\"ZLIM.AUTO\",'')='N'
                       || ',0:162:255'
                       |? __ENV_REJ.TAB_N.IL >= __ENV_REJ.TAB_N.LIL
                       || ',192:240:192'
                       |? __ENV_REJ.TAB_N.IL>0 & __ENV_REJ.TAB_N.IL < __ENV_REJ.TAB_N.LIL
                       || ',240:240:0'
                       || ''
                       ?}");
~~


\info_pobran
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [20.42]
:: OPIS: Dodatkowe informacje w gornej lini (ile pobrano z ilu), ile wagi pobrano z normy brutto oraz kolorowanie zielone/czerwone
::----------------------------------------------------------------------------------------------------------------------
_pobrano:=sql('select COUNT(*) AS POBRANO from :_a where :_a.IL<>0 ',__ENV_REJ.TAB).POBRANO;
_ilsum:=sql('select sum(IL) AS SUMP, sum(LIL) as SUMN  from :_a ',__ENV_REJ.TAB);
_opis:='POBRANO: '+$_pobrano+' z '+$__ENV_REJ.TAB_N.size()+'    ZWAŻONO kg: '+$_ilsum.SUMP+' z '+$_ilsum.SUMN;
exec('set_value','#desktop','','ctr_user_info','fld_info@user_info',_opis);
{? _pobrano>=__ENV_REJ.TAB_N.size()
|| exec('set_foreground','#desktop','','ctr_user_info','fld_info@user_info','21:176:32',1)
|| exec('set_foreground','#desktop','','ctr_user_info','fld_info@user_info','255:0:0',1)
?};
~~


\surowce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [20.42]
:: OPIS: Weryfikacja czy pobrano wszystkie surowce przed zaraportowaniem wyrobu (sprawdzane nielimity do operacji)
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? _nielimity:=sql('select COUNT(*) AS POBRANO from @ZLIM where LIMIT=\'N\' and LIL<>0 and ZLIM.ZGP=\':_a\'',$PROD_REJ.ZGP).POBRANO;
   _nielimity
||
   _pobrano:=sql('select COUNT(*) AS POBRANO from REJ_MAT where IL<>0 and REJ_MAT.ZGP=\':_a\'',$PROD_REJ.ZGP).POBRANO;
   {? _pobrano<_nielimity
   || _txt:='UWAGA - nie pobrano wszystkich surowców!\n'
            'Pobrano %1 z %2 \n'
            'Czy mimo rozliczenia brakujących surowców zarejestrować wykonaną ilość ?'@
            [$_pobrano,$_nielimity];
      {? FUN.ask(_txt)
      || _wyn:=1
      || _wyn:=0
      ?}
   ?}
?};
_wyn


\data_minus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa
:: OPIS: minus dzien
::----------------------------------------------------------------------------------------------------------------------
_data:=__dp-=1;
exec('set_value','#desktop','','ctr_menu_end','data_prod@menu_sur',form(_data,,1));
~~


\data_plus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa
:: OPIS: plus dzien
::----------------------------------------------------------------------------------------------------------------------
_data:=__dp+=1;
exec('set_value','#desktop','','ctr_menu_end','data_prod@menu_sur',form(_data,,1));
~~


\etykieta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa
:: OPIS: Ustawienie daty produkcji w polu etykiety
::----------------------------------------------------------------------------------------------------------------------
__dp:=date();
exec('set_value','#desktop','','ctr_menu_end','data_prod@menu_sur',form(date(),,1));
~~
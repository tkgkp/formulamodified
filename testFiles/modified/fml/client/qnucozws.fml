:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: qnucozws
:: Utworzony:
:: Autor:
::======================================================================================================================
:: Zawartość: Formuły obszaru ZWS
::======================================================================================================================


\kod_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jp
:: OPIS: wyznaczanie kodu kh
::   WY: kod kh [STRING]
::----------------------------------------------------------------------------------------------------------------------
KH.cntx_psh();
KH.clear();
KH.index('KOD1');
{? KH.find_key('Z')
|| KH.prev();
   _kod_k:=KH.KOD
|| _kod_k:=''
?};
{? KH.last()
|| {!|? _kod_z:=KH.KOD;
        {? KH.KOD+1='.'
        || KH.prev
        || 0
        ?}
   !}
?};
KH.cntx_pop();
_pyt:=FUN.choice('Kontrahent'@,1,'krajowy'@,'zagraniczny'@);
{? _pyt=1
|| _kod:=#(_kod_k-1);
   _nr:={? +$(_kod+1)>4
        || ''
        || form(((_kod)+1),-4,0,'9.')+'.'
        ?}
|? _pyt=2
|| _kod:=#(1-_kod_z);
   _nr:={? +$(_kod+1)>4
        || ''
        || 'Z'+form(((_kod)+1),-4,0,'9.')
        ?}
|| _nr:=''
?};
_nr


\get_new_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2011]
:: OPIS: Formula generuje nastepny wolny numer inwentarzowy
::   WY: numer inwentarzowy [STRING]
::  OLD: \get_new_nr/numerinw.fml
::----------------------------------------------------------------------------------------------------------------------
_nri_max:=0; _nri:='';_len:=0;
:: czy osobna numeracja dla środków niskocennych
_p207:=PAR_SKID.get(207);
:: czy osobna numeracja dla każdej jednostki księgowej
_p200:=PAR_SKID.get(200);
{? SRSR.NRI<>''
|| _nri:=SRSR.NRI;
   {? SRSR.NRI+1='.' || _tmp:=SRSR.NRI-1 || _tmp:=SRSR.NRI ?};
   {? _p207='T' || _tmp:=1-_tmp ?};
   _nri_max:=#(_tmp)
|| _nri:='';
   _nri_max:=0
?};
SRSR.cntx_psh();
{? _p207='T'
|| {? _p200='T'
   || _ndx:=SRSR.ndx_tmp(,1,'R',,,'ODD',,,'KIND',,,'DE',,,'NRI',,);
      SRSR.index(_ndx);
::      SRSR.index('RNRI_O');
      SRSR.prefix(SRSR.R,SRSR.ODD,'T')
   || _ndx:=SRSR.ndx_tmp(,1,'R',,,'KIND',,,'DE',,,'NRI',,);
      SRSR.index(_ndx);
::    SRSR.index('RNRI');
      SRSR.prefix(SRSR.R,'T')
   ?}
|| {? _p200='T'
   || _ndx:=SRSR.ndx_tmp(,1,'ODD',,,'KIND',,,'DE',,,'NRI',,);
::      SRSR.index('NRI_O');
      SRSR.index(_ndx);
      SRSR.prefix(SRSR.ODD,'T')
   || _ndx:=SRSR.ndx_tmp(,1,'KIND',,,'DE',,,'NRI',,);
      SRSR.index(_ndx);
::      SRSR.index('NRI');
      SRSR.prefix('T')
   ?}
?};
{? SRSR.last()
|| {? SRSR.NRI+1='.' || _tmp:=SRSR.NRI-1 || _tmp:=SRSR.NRI ?};
   _tmp:=3+_tmp;
   {? _p207='T' || _tmp:=1-_tmp ?};
   {? #(_tmp)>=_nri_max
   || _nri_max:=#(_tmp);
      _nri:=SRSR.NRI;
      _len:=+_tmp;
      {? +($(#_tmp))>_len || _len:=+($(#_tmp)) ?}
   ?}
?};

:: analiza zmian NRI dokumentami zmiany właściwości
SRDO.cntx_psh();
SRDO.index('NRI');
{? _p207='T'
|| SRDO.prefix('L',SRSR.R)
|| SRDO.prefix('L')
?};
{? SRDO.last()
|| _find:=0;
   {! |?
      {? SRDO.NRI<>''
      || _find:=1;
         {? SRDO.NRI+1='.' || _tmpd:=SRDO.NRI-1 || _tmpd:=SRDO.NRI ?};
         {? _p207='T' || _tmpd:=1-_tmpd ?};
         _t_max:=#(_tmpd);
         {? _t_max>_nri_max
         || _nri:=SRDO.NRI;
            _nri_max:=_t_max;
            _tmp:=_tmpd;
            {? +($(#_tmp))>_len || _len:=+($(#_tmp)) ?}

         ?}
      ?};
      ~_find & SRDO.prev()
   !}
?};
SRDO.cntx_pop();

{? _nri<>''
|| _nri_max+=1;
   {? _len<=0 || _len:={? _p207='T' || 12 || 13 ?} ?};

   _nri:={? _p207='T';0 || SRSR.R || '' ?}+form(_nri_max,-3,0,'9.')+'/'+form(SSTALE.AO().KON~1,-4,,'9.')+'.'
|| _nri:={? _p207='T';0 || SRSR.R || '' ?}+'0000000000001.'
?};
SRSR.cntx_pop();
_nri


\wyb_kh_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW:
:: OPIS: Wybór kontrahentów
::   WE: _a - obj_new - obiekt umożliwiający zapis parametru
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_par:={? var_pres('_a')=type_of('') || _start:=_a || _start:='' ?};
_wyn:=_par;

VAR_DEL.delete('__zatw');
__zatw:=0;

_tab:=tab_tmp(1,'ACCESS','STRING[1]','Uprawnienie'
      ,'REF','STRING[16]','Ref SQL'
      ,'KOD','STRING[10]','Kod'
      ,'NAZ','STRING[30]','Nazwa');
_ndx1:=_tab.ndx_tmp(,,'KOD',,);
_tab.index(_ndx1);

KH.cntx_psh();
KH.index('KOD');
KH.prefix(2);
{? KH.first()
|| {!
   |?
         _tab.blank();
         _tab.KOD:=KH.KOD;
         _tab.NAZ:=KH.NAZ;
         _tab.ACCESS:={? (_par+',')*(KH.KOD+',') || 'T' || 'N' ?};
         _tab.add(1);
         KH.next()
   !}
?};
KH.cntx_pop();

_sel:=_tab.mk_sel('Wybór kontrahentów'@,'P',,'fo_khroz_list',,,,,'U');
_tab.win_fld(_sel,,'ACCESS',,,,,0,,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
_tab.win_fld(_sel,,'KOD');
_tab.win_fld(_sel,,'NAZ');

_formula:="_tab:=cur_tab();_tab.ACCESS:='T'; _tab.put()";
_tab.win_act(_sel,,'Formuła','&Wybierz'@@,,,_formula,,1,1,,,'W');
_tab.win_btn(_sel,'text='+'&Wybierz'@+',panel=right,align=begin','menu:W',,,,,,'noempty');


_formula:="_tab:=cur_tab(); _tab.ACCESS:='N'; _tab.put()";
_tab.win_act(_sel,,'Formuła','&Odbierz'@@,,,_formula,,,1,,,'O');
_tab.win_btn(_sel,'text='+'&Odbierz'@+',panel=right,align=begin','menu:O',,,,,,'noempty');

_formula:="__zatw:=1; sel_exit()";
_tab.win_act(_sel,,'Formuła','&Akceptuj'@@,,,_formula,,,,,,'A');
_tab.win_btn(_sel,'text='+'&Akceptuj'@+',btn_label_align=center,panel=bottom,align=end','menu:A',,,,,,'noempty');
_tab.win_btn(_sel,'text='+'A&nuluj'@+',btn_label_align=center,panel=bottom,align=end','key:Esc');

_tab.win_act(_sel,,'Kolejność',);

'Czy zakończyć wybór kontrahentów?'@; 'Uwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@;
_tab.win_act(_sel,,'Okienko',,,,,"
   {? ~__zatw
   || FUN.ask('Czy zakończyć wybór kontrahentów?'@+'\n'+'Uwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@)
   || 1
   ?}");

_tab.win_sel(_sel);
{? _tab.select()
||
   _wyn:='';
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? _tab.ACCESS='T'
         || _wyn+=_tab.KOD+','
         ?};
         _tab.next()
      !}
   ?}
?};

_wyn


\auto_wycena_dmag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_2226]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

:: automatyczna wycena przeniesienie z xp
:: wycena dla poprzedniego miesiąca
::debug();
_mc:=(date(date()~1,date()~2,1)-1)~2;
_rok:=(date(date()~1,date()~2,1)-1)~1;
exec('auto_wycena_mc','qnucozws',_rok,_mc);
:: wycena dla bieżącego miesiąca
_mc:=date()~2;
_rok:=date()~1;
exec('auto_wycena_mc','qnucozws',_rok,_mc);
1


\auto_wycena_mc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_2226]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: wycena dla mc
ST.AR:=_a;ST.AM:=_b;ST.ODDZ:='w';
exec('open','open_tab',ST.ODDZ,$ST.AR+2);
_ok:=exec('czy_z_ok','okresy',1,1,ST.AR,ST.AM,ST.MAG);

_mod:=0;
MG.cntx_psh();
MG.index('MAGAZYNY');
MG.prefix();
{? MG.first()
|| {!
   |?
      _ok:=exec('czy_z_ok','okresy',1,1,ST.AR,ST.AM,MG.ref());
      {? _ok=1
      || MG.cntx_psh();
         ST.MAG:=MG.ref();
         _mod+=exec('wycen_pw_mag','qnucozws',ST.AR,ST.AM,ST.MAG,'PW');
         _mod+=exec('wycen_pw_mag','qnucozws',ST.AR,ST.AM,ST.MAG,'KRP');
         _mod+=exec('wycen_pw_mag','qnucozws',ST.AR,ST.AM,ST.MAG,'RP');
         MG.cntx_pop()
      ?};
      MG.next()
   !}
?};
MG.cntx_pop();
1


\wycen_pw_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: xpertis
:: OPIS: Wycena dokumentow PW
:: EXEC: exec('wycen_pw_mag','qmod01',ST.AR,ST.AM,ST.MAG)
::   WE: _a - ST.AR
::       _b - ST.AM
::       _c - ST.MAG
::       _d - [STRING] typ dokumentu
::   WY: string
::----------------------------------------------------------------------------------------------------------------------
_ar:=_a;
_am:=_b;
_mag:=_c;
_typ:=_d;

:: _par:='PW';
_par:=_typ;
_all:=_mod:=0;

TYPYDOK.cntx_psh();
TYPYDOK.index('TYP');
TYPYDOK.prefix(_par,);
{? TYPYDOK.first()
|| {!
   |?
      TYPYDOK.cntx_psh();
      {? TYPYDOK.T=_par & TYPYDOK.P='T'
      ||
         _typ:=TYPYDOK.ref();
         ND.cntx_psh();
         ND.index('NAGDOK');
         ND.prefix(_ar,_am,_mag,_typ);
         _all+=ND.size();
         {? ND.first()
         || {!
            |?
               ND.cntx_psh();
               _mod+=exec('wycen_pw_one','qnucozws',ND.ref());
               ND.cntx_pop();
               ND.next()
            !}
         ?};
         ND.cntx_pop();
         ~~
      ?};
      TYPYDOK.cntx_pop();
      TYPYDOK.next()
   !}
?};
TYPYDOK.cntx_pop();
$_mod+'/'+$_all;
_mod


\wycen_pw_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: xpertis
:: OPIS: Wycena dokumentow PW (zalozenia wszystko dzieje sie w biezacej masce)
::   WE: _a - ND.ref()
::   WY: ile razy wykonano put - tylko dla statystyk
::----------------------------------------------------------------------------------------------------------------------
_nd_ref:=_a;

_put:=0;
ND.cntx_psh();
ND.index('NAGDOK');
ND.prefix();
{? ND.seek(_nd_ref) & $ND.ref()=$_nd_ref
||
   {? ND.Z='T' & ND.ZAK='N' & ND.TYP().P='T'
   ||
      DK.cntx_psh();
      DK.index('DOKMAG');
      DK.prefix(ND.ref());
      {? DK.first()
      || {!
         |?
            _wycena:=sql('select DK.REFERENCE from @DK where DK.PLUS=\'T\' and DK.RDK=:_a and DK.NDK=\':_b\' and DK.Z=\'N\'',DK.RDK,DK.NDK).size();
            {? _wycena=0 & DK.C=0
            ||
               do();
               _cena:=0;
               _ile_cen:=0;
               TAP.cntx_psh();
               TAP.index('M');
               TAP.prefix(ND.ODDZ,'D',DK.M);
               {? TAP.first()
               || {!
                  |?
                     {? (TAP.OD<=ND.D & ND.D<=TAP.DO)
                        |
                        (TAP.OD<=ND.D & TAP.DO=date(0,0,0))
                     ||
                        _cena:=TAP.CEN; _ile_cen+=1; ~~
                     ?};
                     TAP.next()
                  !}
               ?};
               TAP.cntx_pop();
               {? _ile_cen>=2
               ||
                  {? 0
                  || FUN.info('Cen w cenniku jest więcej niż jedna w wybranym zakresie dat. Indeks: '+DK.M().KTM)
                  ?};
                  ~~
               |? _ile_cen=1
               ||
                  _cena:=_cena$exec('jaka_dokl_c','qnucozws',DK.M);
                  {? _cena<0 || _cena:=0 ?};
                  {? (_cena>=0 & _cena<>DK.C)
                     &
                     DK.C=0
                  ||
                     _stara_cena:=DK.C;
                     DK.C:=_cena;
                     DK.WAR:=(DK.C*DK.IL)$2;
                     {? DK.put()
                     ||
                        {? exec('zmcene','magdok_poz',DK.ref(),_cena,_cena,0,_stara_cena)=0
                        || undo();
                           {? 0
                           || FUN.info('Zmiana ceny dostawy dla indeksu '+DK.M().KTM+' nie powiodła się.\n\nOperacja została anulowana.')
                           ?};
                           _ok:=0
                        ||
                           _put+=1
                        ?};
                        ~~
                     ?};
                     ~~
                  ?};
                  ~~
               ||
                  {? 0 & Qpomoc.cgi<>'T'
                  || FUN.info('Brak cen w cenniku w wybranym zakresie dat. Indeks: '+DK.M().KTM)
                  ?};
                  ~~
               ?};
               _result:=end();
               ~~
            ?};
            DK.next()
         !}
      ?};
      DK.cntx_pop();
      ~~
   ?};
   ~~
?};
ND.cntx_pop();
_put


\jaka_dokl_c
::----------------------------------------------------------------------------------------------------------------------
::  UTW: xpertis
:: OPIS: Jaka dokladnosc ceny na materiale - na podstawie \jaka_dok z magazyn1.fml
::   WE: _a - M.ref()
::----------------------------------------------------------------------------------------------------------------------
M.cntx_psh();
M.prefix();
_wyn:={? M.seek(_a) || M.DOKL_C || 0 ?};
M.cntx_pop();
_wyn

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: qobiegi.fml
:: Utworzony: 10.02.2021
:: Autor: PeKa
:: Systemy:
::======================================================================================================================
:: Zawartość: Funkcje dla obslugi wnioskow w obiegu
::======================================================================================================================


\zobacz_wniosek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [20.42]
:: OPIS: Funkcja prezentuje wniosek w obiegu
::   WE: _a - powiazanie do obiegu (mozliwe warianty)
::             ZK_N.OBI_POW, ZD_NAG.OBI_POW,FAKS.OBI_POW,FAKS.EDOKUM
::----------------------------------------------------------------------------------------------------------------------
_edokum:=_a;
{? _edokum<>''
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(_edokum));
   EDOKUM.index('ID'); EDOKUM.prefix();
   {? EDOKUM.seek(_edokum,,1,1)
   || TYPOBIEG.cntx_psh(); TYPOBIEG.prefix();
      EDOKUM.TYPOBIEG();
      {? TYPOBIEG.NAZWA='Obieg faktur'
      || _okn:=EDOKUM.mk_edit('Faktura w obiegu'@);
         EDOKUM.win_ewin(_okn,,'WPR'); typobi:=1
      |? TYPOBIEG.NAZWA='Obieg wniosków'
      || _okn:=EDOKUM.mk_edit('Wniosek w obiegu'@);
         EDOKUM.win_ewin(_okn,,'KSW'); typobi:=2
      || _okn:=EDOKUM.mk_edit('Delegacja w obiegu'@);
         EDOKUM.win_ewin(_okn,,'KSD'); typobi:=3
      ?};
      EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Historia obiegu'@],
                      "exec('okn_opis','obiegi'); ''"
                      );
      {? typobi=1
      ||
         EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje VAT'@],
                         "exec('pozvdob','obiegi_log'); ''"
                         )
      ?};
      EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['P&odziały'@],
                      "exec('podz_dob','obiegi_log'); ''"
                      );
      {? typobi=1
      ||
         EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Pozycje &faktury'@],
                         "exec('pozf_edokum','obiegi_log'); ''"
                         )
      ?};
      EDOKUM.win_ebtn(_okn,'text=%1,icon=xwin16.png:14,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],'key:Esc');
      EDOKUM.win_edit(_okn);
      EDOKUM.display();
      TYPOBIEG.cntx_pop();
      VAR_DEL.delete('typobi')
   || FUN.info('Nie znaleziono powiązanego dokumentu.'@)
   ?};
   EDOKUM.cntx_pop()
|| FUN.info('Dla wybranego dokumentu nie uruchomiono obiegu.'@)
?}


\obg_sub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [20.42][11.02.2021]
:: OPIS: Formuła zwraca zawartość tytułu wiadmości e-mail o dokumencie w obiegu do akceptacji/przekazania/odrzucenia
::   WE: _a - EDOKUM.ref()
::       _b - 'P' - Przekazanie, 'A' - Akceptacja, 'O' - Odrzucenie, 'Z' - Zaakceptowano
:;       _c - 'ZS' - zamówienie sprzedaży, 'ZD' - zamówienie dostawy
::   WY: temat wiadomości
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || return('') ?};
_edokum:=_a;
_msg:=_b;
_zam:=_c;
_result:='';
EDOKUM.cntx_psh();
EDOKUM.use(8+$_edokum); EDOKUM.index('ODD'); EDOKUM.prefix();
{? EDOKUM.seek(_edokum)
|| _result:={? _msg='P'
            || 'Wniosek do przekazania: '
            |? _msg='A'
            || 'Wniosek do akceptacji zamówienia '+{? _c='ZS' || 'sprzedaży: ' |? _c='ZD' || 'dostawy: ' || '' ?}
            |? _msg='O'
            || 'Odrzucono wniosek dotyczący zamówienia '+{? _c='ZS' || 'sprzedaży: ' |? _c='ZD' || 'dostawy: ' || '' ?}
            |? _msg='Z'
            || 'Zaakceptowano wniosek dotyczący zamówienia '+{? _c='ZS' || 'sprzedaży: ' |? _c='ZD' || 'dostawy: ' || '' ?}
            ?}+EDOKUM.ID
?};
EDOKUM.cntx_pop();
_result


\obg_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Formuła zwraca zawartość wiadmości e-mail dokumentu w obiegu do akceptacji/przekazania.
::       Sposób użycia - wpisanie do pola 'Treść': $exec('obg_txt','%mail',_a.p01,'P')
::       albo: $exec('obg_txt','%mail',params_get().in.p01,'A')
::   WE: _a - EDOKUM.ref()
::       _b - 'P' - Przekazanie, 'A' - Akceptacja, 'O' - Odrzucenie, 'Z' - Zaakceptowano
:;       _c - 'ZS' - zamówienie sprzedaży, 'ZD' - zamówienie dostawy
::   WY: treść wiadomości
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || return('') ?};
_edokum:=_a;
_msg:=_b;
_tekst:='';
:: LINK
_uidref:=exec('FindAndGet','#table',{? _c='ZS' || ZK_N || ZD_NAG ?},EDOKUM.DOK_POW,,"uidref()",'');
_zam:=exec('FindAndGet','#table',EDOKUM,_a,,"SYM",'');
_uri:=exec('link_uri','#system',exec('obj_ntab_set','#array',,'LINK',_uidref));
_link:='<a href="%1">Link do zamówienia %2</a>'[_uri,_zam];


EDOKUM.cntx_psh();
EDOKUM.use(8+$_edokum); EDOKUM.index('ODD'); EDOKUM.prefix();
{? EDOKUM.seek(_edokum)
||
   {? _msg='P' | _msg='A'
   || _uwagi:=sql('select UW_KR from EDOKOS where EDOKUM=:_a',EDOKUM.ref).UW_KR
   || _uwagi:=sql('select UW_KR from EDOKLOG where EDOKUM=:_a',EDOKUM.ref).UW_KR
   ?};
   _tekst+='<BR>';
   _tekst+='<CENTER><H3><font color="black">'+_link+'</font></H3></CENTER>';
   {? _msg='P' | _msg='A'
   || _tekst+='<CENTER><H3>Wniosek do '+{? _msg='P' || 'przekazania' || 'akceptacji' ?}+'</CENTER><BR>'
   || _tekst+='<CENTER><H3>Wniosek '+{? _msg='O' || 'odrzucono' || 'zaakceptowano' ?}+'</CENTER><BR>'
   ?};
   {? +_uwagi
   || _tekst+='<CENTER><H4>Uwagi: '+_uwagi+'</CENTER><BR>'
   ?};
   {? _c<>'ZS'
   ||
      _tekst+='<table align=\"center\" valign=\"top\" border=\"2\" cellspacing=\"0\" cellpadding=\"0\">';
      _tekst+='<tr  class=\"head3\"><td colspan=\"2\">Dane wniosku</td></tr>';

      _tekst+='<tr><td>Typ wniosku</td>'+
              '<td class=\"wiersz\">'+EDOKUM.TYP().NAZWA+'</td></tr>'+
              '<tr><td>Operacja z dnia</td>'+
              '<td class=\"wiersz\">'+$EDOKUM.DOP+'</td></tr>'+
              '<tr><td>Identyfikator</td>'+
              '<td class=\"wiersz\">'+EDOKUM.ID+'</td></tr>';

      _tekst+='<tr class=\"head3\"><td colspan=\"2\">Kontrahent</td></tr>'+
              '<tr><td>NIP</td>'+
              '<td class=\"wiersz\">'+EDOKUM.KHKH().SNIP+'</td></tr>'+
              '<tr><td>Kod</td>'+
              '<td class=\"wiersz\">'+EDOKUM.KHKH().KOD+'</td></tr>'+
              '<tr><td>Nazwa</td>'+
              '<td class=\"wiersz\">'+EDOKUM.KHKH().SKR+'</td></tr>';

      _tekst+='<tr class=\"head3\"><td colspan=\"2\">Opis</td></tr>'+
              '<tr><td>Temat</td>'+
              '<td class=\"wiersz\">'+EDOKUM.TR+'</td></tr>';
      _tekst+='</table><BR>'
   ?};
   ZK_N.cntx_psh();
   ZK_P.cntx_psh();
   HAN.cntx_psh();
   KH.cntx_psh();
   {? _c='ZS' & (5+(EDOKUM.DOK_POW+16))='zknag' & ZK_N.seek(EDOKUM.DOK_POW,ref_name(EDOKUM.DOK_POW),1,1)
   ||
      _tekst+=
::       '<p class="danger">Zaakceptowano zamówienie sprzedaży (przekazano do obiegu): '+ZK_N.SYM+'</p>'+
       '<table width="100%" border="1" cellspacing="0" cellpading="0">'+
        '<tr>'+
        '<td class="wiersz">Symbol zamówienia</td>'+
        '<td class="wiersz">'+ZK_N.SYM+'</td>'+
      '</tr>'+
      '<tr>'+
        '<td class="wiersz">Termin realizacji</td>'+
        '<td class="wiersz">'+
        $ZK_N.DT+
        '</td>'+
      '</tr>'+
      '<tr>'+
        '<td class="wiersz">Kod kontrahent</td>'+
        '<td class="wiersz">'+ZK_N.KH().KOD+'</td>'+
      '</tr>'+
      '<tr>'+
        '<td class="wiersz">Nazwa kontrahent</td>'+
        '<td class="wiersz">'+ZK_N.KH().NAZ+'</td>'+
      '</tr>'+
      '<tr>'+
        '<td class="wiersz">Opiekun</td>'+
        '<td class="wiersz">'+ZK_N.HAN().NAZ+'</td>'+
      '</tr>'+
   '</table>';
      ZK_P.prefix(ZK_N.A,'Z',ZK_N.ref());
      {? ZK_P.first()
      || _tekst+='<p></p>'+
                  'Pozycje zamówienia sprzedaży:'+
                  '<p></p>'+
                  '<table width="100%" border="1" cellspacing="0" cellpading="0">'+
                  '<tr><th>Kod towaru</th><th>Nazwa towaru</th><th>Ilość</th><th>Jednostka</th></tr>';
         _lp:=0;
         {!
         |?
::    Dodawanie wierszy do tabelki
            _lp+=1;
            _tekst+='<tr>'+
                    '<td class="wiersz">'+ZK_P.M().KTM+'</td>'+
                    '<td class="wiersz">'+ZK_P.M().N+'</td>'+
                    '<td class="wiersz">'+form(ZK_P.ILZ,,2)+'</td>'+
                    '<td class="wiersz">'+ZK_P.JM().KOD+'</td>'+
                    '</tr>';
            ZK_P.next()
         !};
         _tekst+='</table>'
      ?}
   ?};
   ZK_N.cntx_pop();
   ZK_P.cntx_pop();
   HAN.cntx_pop();
   KH.cntx_pop()
?};
EDOKUM.cntx_pop();
_tekst


\auto_obieg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_TEST]
:: OPIS: Automatyczne uruchomienie obiegu z obszaru zamówień
::   WE: _a - _EDOKUM identyfikator dokumentu w obiegu
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='OBE_FAW_DARP';
_params.UIDREF:=_a;
_params.AKCJA:='Dalej';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',_a);
exec('mp_run','#b__box',_params)


\czy_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_2226]
:: OPIS: Sprawdzenie czy edokum istnieje i czy nie jest zakończony lub wycofany
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_edokum:=null();
_result:=0;

{? var_pres('_a')=type_of(null())
|| _edokum:=_a
|| return(1)
?};

_result:=exec('FindAndGet','#table',EDOKUM,_edokum,,"@.EDOKUM.ZAM='T' | @.EDOKUM.WYCOFAJ<>''",1);

_result


\status_q
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_2226]
:: OPIS: Funkcja dla procesu obiegu faktury zakupu, ustala status Q przy odrzuceniu w warjanice akceptacji "jeden z"
::   WE: p01 - EDOKUM, p02 - B_PREL
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

_edokum:=null();
_b_prel:='';

:: w p01 powinien być podpięty argument z zawartością EDOKUM
{? var_pres('p01',_in)=type_of(null())
|| _edokum:=_in.p01
?};
{? var_pres('p02',_in)=type_of('')
|| _b_prel:=_in.p02
?};

{? _edokum<>null() & _b_prel<>''
|| EDOKOS.cntx_psh();
   EDOKOS.use('skid_y'+(EDOKUM.name()+2));
   EDOKOS.index('EDOKUM'); EDOKOS.prefix(_edokum,_b_prel,);
   {? EDOKOS.first()
   || {!
      |? {? EDOKOS.OPERACJA='A' & EDOKOS.STATUS='O' ||  EDOKOS.STATUS:='Q'; EDOKOS.put() ?};
         EDOKOS.next()
      !}
   ?}
?};
1


\akcept_jeden_z
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_2226]
:: OPIS: Wyzerowojue dane obiegu po akceptacji merytorycznej, aby popraenie zadziałało określenie statusu
::   WE: p01 - EDOKUM.ref
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

_edokum:=null();
:: w p01 powinien być podpięty argument z zawartością EDOKUM
{? var_pres('p01',_in)=type_of(null())
|| _edokum:=_in.p01
?};
{? _edokum<>null()
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(_edokum));
   {? EDOKUM.seek(_edokum)
   || exec('null_bprel','obiegi_akc')
   ?};
   EDOKUM.cntx_pop()
?};
1

\addsprdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Dodawanie do tabelki tymczasowej kontrola dokumentow w obiegu - zadanie
::  OLD: \addsprdob/skid_dob.fml
::  [_c] - EDOKUM.ref, domyślnie brak
::----------------------------------------------------------------------------------------------------------------------
{? __bool_ed=1
|| __tab:=tab_tmp(6,
   'TYP_WP','INTEGER','Typ wpisu',
   'ROK','STRING[20]','Rok',
   'ODD','STRING[8]','Jednostka księgowa',
   'KH','STRING[100]','Kontrahent',
   'SYM','STRING[20]','Symbol lub id',
   'WAL','STRING[6]','Waluta',
   'NETTO','REAL','Wartość netto',
   'WART','REAL','Wartość brutto',
   'DELEG','STRING[50]','Delegowany',
   'DEL_MIE','STRING[80]','Miejsce docelowe',
   'PLAN','STRING[1]','Czy plan',
   'REFEDOK','STRING[16]','REF EDOKUM',
   'REFROK','STRING[16]','REF_ROK',
   'TYP_OB','INTEGER','Typ obiegu',
   'CZY_WAR','INTEGER','Wnioski - czy wartości',
   'TYP','STRING[50]','Typ dokumentu',
   'DOD','STRING[20]','Od dnia',
   'DDO','STRING[20]','Do dnia',
   'TP','STRING[20]','Od dnia',
   'DATAW','STRING[20]','Do dnia',
   'TR','STRING[100]','Tresc',
   'DS','STRING[20]','Data sprzedaży',
   'CEL_DEL','STRING[80]','Cel delegacji'
);
   __bool_ed:=0
|? __bool_ed=0
|| {? type_of(_c)=type_of(1)
   || {? EDOKUM.seek(EdokTab.E_REF,ref_name(EdokTab.E_REF),1)
      || USERS.cntx_psh();
         _status:={? EdokTab.OPERACJA='W' & EdokTab.STATUS='O'
                  || 1
                  |? EdokTab.OPERACJA='W' & EdokTab.STATUS='N'
                  || 2
                  |? EdokTab.OPERACJA='P' & EdokTab.STATUS='O'
                  || 3
                  |? EdokTab.OPERACJA='P' & EdokTab.STATUS='N'
                  || 4
                  |? EdokTab.OPERACJA='A' & EdokTab.STATUS='O'
                  || 5
                  |? EdokTab.OPERACJA='A' & EdokTab.STATUS='N'
                  || 6
                  |? EdokTab.OPERACJA='D' & EdokTab.STATUS='N'
                  || 7
                  || 0
                  ?};
         EDOKUM.TYPOBIEG();
         _typ:=1;
         {? ( _status=1 | _status=3 | _status=5 ) & EDOKUM.TYPOBIEG().NAZWA='Obieg faktur'
         || __tab.TYP_WP:=8;
            __tab.ODD:=EDOKUM.ODD().OD;
            __tab.KH:=EDOKUM.KHKH().SKR;
            __tab.SYM:={? _typ=1 || EDOKUM.SYM || EDOKUM.ID ?};
            __tab.WAL:=EDOKUM.WAL().KOD;
            __tab.NETTO:=EDOKUM.NETTO;
            __tab.WART:=EDOKUM.WART;
            __tab.ROK:=EDOKUM.ROK_F().NAZ;
            __tab.DELEG:=EDOKUM.DOSTAWCA().NAZWISKO + ' '+EDOKUM.DOSTAWCA().PIERWSZE;
            __tab.DEL_MIE:=EDOKUM.DEL_MIE;
            __tab.REFEDOK:=$EDOKUM.ref();
            __tab.REFROK:=$EDOKUM.ROK_F;
            __tab.TYP_OB:=_typ;
            __tab.CZY_WAR:=EDOKUM.TYP().CZY_WAR;
            __tab.TYP:=EDOKUM.TYP().NAZWA;
            __tab.DOD:=$EDOKUM.DATA_OD;
            __tab.DDO:=$EDOKUM.DATA_DO;
            __tab.TP:=$EDOKUM.TP;
            __tab.DATAW:=$EDOKUM.DATAW;
            __tab.DS:=$EDOKUM.DOP;
            __tab.TR:=EDOKUM.TR;
            __tab.CEL_DEL:=EDOKUM.DEL_CEL().TR;
            __tab.add()
         ?};
         USERS.cntx_pop()
      ?}
   || {? EDOKUM.seek(_c,ref_name(_c),1)
      || USERS.cntx_psh();
         _typ:=1;
         __tab.TYP_WP:=8;
         __tab.ODD:=EDOKUM.ODD().OD;
         __tab.KH:=EDOKUM.KHKH().SKR;
         __tab.SYM:={? _typ=1 || EDOKUM.SYM || EDOKUM.ID ?};
         __tab.WAL:=EDOKUM.WAL().KOD;
         __tab.NETTO:=EDOKUM.NETTO;
         __tab.WART:=EDOKUM.WART;
         __tab.ROK:=EDOKUM.ROK_F().NAZ;
         __tab.DELEG:=EDOKUM.DOSTAWCA().NAZWISKO + ' '+EDOKUM.DOSTAWCA().PIERWSZE;
         __tab.DEL_MIE:=EDOKUM.DEL_MIE;
         __tab.REFEDOK:=$EDOKUM.ref();
         __tab.REFROK:=$EDOKUM.ROK_F;
         __tab.TYP_OB:=_typ;
         __tab.CZY_WAR:=EDOKUM.TYP().CZY_WAR;
         __tab.TYP:=EDOKUM.TYP().NAZWA;
         __tab.DOD:=$EDOKUM.DATA_OD;
         __tab.DDO:=$EDOKUM.DATA_DO;
         __tab.TP:=$EDOKUM.TP;
         __tab.DATAW:=$EDOKUM.DATAW;
         __tab.DS:=$EDOKUM.DOP;
         __tab.TR:=EDOKUM.TR;
         __tab.CEL_DEL:=EDOKUM.DEL_CEL().TR;
         __tab.add();
         USERS.cntx_pop()
      ?}
   ?}
|| _typ:=1;
   TABSPDOK.TYP_WP:=__tab.TYP_WP;
   TABSPDOK.ODD:=__tab.ODD;
   TABSPDOK.KH:=__tab.KH;
   TABSPDOK.SYM:={? _typ=1 || __tab.SYM ?};
   TABSPDOK.WAL:=__tab.WAL;
   TABSPDOK.NETTO:=__tab.NETTO;
   TABSPDOK.WART:=__tab.WART;
   TABSPDOK.ROK:=__tab.ROK;
   TABSPDOK.DELEG:=__tab.DELEG;
   TABSPDOK.DEL_MIE:=__tab.DEL_MIE;
   TABSPDOK.REFEDOK:=__tab.REFEDOK;
   TABSPDOK.REFROK:=__tab.REFROK;
   TABSPDOK.TYP_OB:=__tab.TYP_OB;
   TABSPDOK.CZY_WAR:=__tab.CZY_WAR;
   TABSPDOK.TYP:=__tab.TYP;
   TABSPDOK.DOD:=__tab.DOD;
   TABSPDOK.DDO:=__tab.DDO;
   TABSPDOK.TP:=__tab.TP;
   TABSPDOK.DATAW:=__tab.DATAW;
   TABSPDOK.DS:=__tab.DS;
   TABSPDOK.TR:=__tab.TR;
   TABSPDOK.CEL_DEL:=__tab.CEL_DEL;
   TABSPDOK.add()
?}
:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: qprojekty.fml
:: Utworzony: 03.12.2020
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły obsługujące procesy
::======================================================================================================================


\obieg_wnioski
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [20.42 NUCO]
:: OPIS: Uruchomienie wniosków w obiegu do wybranego projektu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

{? 1 ||
UD_TYP.cntx_psh();
UD_TYP.prefix();
_typ:=exec('FindInSet','#table','UD_TYP','SYMBOL','POZ_BUD',,,1);

UD_SCH.cntx_psh();
UD_SCH.prefix();
UD_DEF.cntx_psh();
UD_DEF.index('SCHSYM');
UD_DEF.prefix(exec('FindInSet','#table','UD_SCH','SYMBOL','PRJ_ZAD',_typ,,1));

_ws:=UD_DEF.mk_sel('Etapy projektu',,0,'etapyprojektu',,,,,'U');
UD_DEF.win_fld(_ws,,'SYMBOL',,,20,,1,'Symbol',,'Symbol wniosku');
UD_DEF.win_fld(_ws,,'OPIS',,,60,,1,'Opis',,'Opis wniosku');
UD_DEF.win_act(_ws,,'Formuła','Uruchom',,'Uruchom wniosek'@,
   "  exec('obieg_uruchom','qprojekty',UD_DEF.SYMBOL,UD_DEF.OPIS)
   ",,,,,,'U'
);
UD_DEF.win_btn(_ws,'text=%1'['Uruchom'@],'menu:U',,,0);
UD_DEF.win_sel(_ws);
UD_DEF.select();

UD_DEF.cntx_pop();
UD_SCH.cntx_pop();
UD_TYP.cntx_pop()
?};

:: uruchamianie z procesów

::dek_fakt:=0;
::typobi:=1;
::__ofz:='T';
::exec('okn_fakt','obiegi2',dek_fakt);
::EPODZ.win_edit('RED');
::EDOKUM.cntx_psh();
::EDOKUM.prefix();
::EDOKUM.f_clear();
::DOK.cntx_psh();
::__edoref:='';
::
::EDOKUM.select()

::_params:=exec('mp_run_a','#b__box');
::_params.ACT_UID:='OBE_FAW_DARP';
::_params.AKCJA:='Dołącz';
::_params.PROC_START:='T';
::exec('mp_run','#b__box',_params);
::exec('akc_f_upd','obe_faw');

::DOK.cntx_pop();
::EDOKUM.cntx_pop();

VAR_DEL.delete('dek_fakt','__ofz');

:__CTR.BTAB:='DOKUM';

~~


\obieg_historia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [20.42 NUCO]
:: OPIS: Historia wniosków w obiegu wybranego projektu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_TAB:=sql('
   select
      EDOKLOG.OPIS, EDOKLOG.UW_KR, EDOKLOG.DATA, EDOKLOG.TIME
   from
      @EDOKLOG
      left join @EDOKUM using (EDOKLOG.EDOKUM,EDOKUM.reference)
   where
      EDOKUM.REFERENCE in
      (  select
            EDOKUM.REFERENCE
         from
            @EDOKUM
            left join EDOKUMPR using (EDOKUMPR.EDOKUM,EDOKUM.reference)
            left join PROJEKTY using (EDOKUMPR.PROJEKT,PROJEKTY.reference)
         where
            PROJEKTY.REFERENCE=:_a
      )',
   PROJEKTY.ref()
);

exec('select','#table',_TAB);
~~


\obieg_uruchom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [20.42 NUCO]
:: OPIS: Historia wniosków w obiegu wybranego projektu.
::   WE: _a - [STRING] Identyfikator obiegu
::       _b - [STRING] Opis
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_sygnal:=_a;
_opis:=_b;
zakres:=1;

{? FUN.ask('%1 - %2'['Czy napewno uruchomić obieg wniosku'@,_opis])
|| _par:=obj_new(1);
   _arg:=obj_new(1);
   _arg[1]:=obj_new('TYP_DOK','FORM_ADD');
   _arg[1].TYP_DOK:='Zadanie projektowe';
   _arg[1].FORM_ADD:='exec(\'edokumpr\',\'qprojekty\')';

   _ref:=exec('add','obe_faw',_arg[1]);

   {? _ref
   || _par[1]:=obj_new('PARAMETR','VALUE');
      _par[1].PARAMETR:='EDOKUM';
      _par[1].VALUE:=_ref;
      exec('force_signal','#b__box',_sygnal,,_par)
   ?}
?};
~~


\edokumpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_2226]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a:=UD_DEF.SYMBOL;
EDOKPAR.blank();
EDOKUMPR.PROJEKT:=PROJEKTY.ref();
EDOKUMPR.EDOKUM:=EDOKUM.ref();
EDOKUMPR.ETYPY:=EDOKUM.TYP;
EDOKUMPR.USERS:=OPERATOR.USER;
EDOKUMPR.RODZWART:=exec('ud_skl_ref','qprojekty','POZ_BUD','PRJ_ZAD',_a);
EDOKUMPR.add()


\ud_skl_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jacteb [2042]
:: OPIS: Dla podanego typu, schematu i kodu znajduje UD_SKL.ref
:: _a - symbol typu danych
:: _b - symbol schematu danych
:: _c - kod elementu
:: WY - ref elementu
::--------------------------------------------------------------------------------------------------
_ref:=null;
UD_TYP.cntx_psh();
UD_TYP.index('SYMBOL');
UD_TYP.prefix(_a,_a);
{? UD_TYP.first()
|| UD_SKL.cntx_psh();
   UD_SKL.index('SYMBOL');
   UD_SKL.prefix(UD_TYP.ref(),_c,_c);
   {? UD_SKL.first()
   || UD_SCH.cntx_psh();
      UD_SCH.index('SYMBOL');
      UD_SCH.prefix(UD_TYP.ref(),_b,_b);
      {? UD_SCH.first()
      || UD_DEF.cntx_psh();
         UD_DEF.index('SCHSYM');
         UD_DEF.prefix(UD_SCH.ref(),_c,_c);
         {? UD_DEF.first()
         || _ref:=UD_SKL.ref()
         ?};
         UD_DEF.cntx_pop()
      ?};
      UD_SCH.cntx_pop()
   ?};
   UD_SKL.cntx_pop()
?};
UD_TYP.cntx_pop();
_ref
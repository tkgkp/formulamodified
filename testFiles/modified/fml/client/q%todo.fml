:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: q%todo.fml
:: Utworzony: 19.10.2020
:: Autor: TP
:: Systemy:
::======================================================================================================================
:: Zawartość: formuły na opis dla listy TODO (NUCO)
::======================================================================================================================

\dsc_zlec_z_zkp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_DEV]
:: OPIS: Opis na listę TO_DO dla tworzenia zleceń z planu strategicznego (złożone niezależne)
::   WE: p01 - parametr ZK_P ścieżka z sygnału, p02 - parametr ZK_P - ścieżka z TODO
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_zk_p:=null();

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('p01',_in)<>type_of(~~)
|| _zk_p:=_in.p01
|? var_pres('p02',_in)<>type_of(~~)
|| _zk_p:=_in.p02
|? var_pres('p03',_in)<>type_of(~~)
|| _ref_zkp:=sql('select PX_OBJ.ZK_P from @PX_CONN join PX_OBJ where PX_CONN.REFERENCE=\':_a\'',$_in.p03).ZK_P;
   {? _ref_zkp<>''
   || _zk_p:=exec('FindAndGet','#table',ZK_P,_ref_zkp)
   ?}
|? var_pres('p04',_in)<>type_of(~~)
|| _zk_p:=_in.p04
?};

{? var_press('_zk_p')=7 & _zk_p<>null()
|| _desc_dod:=' - '+exec('FindAndGet','#table',ZK_P,_zk_p,,"
                                \'KTM: \'+ZK_P.M().KTM +\', dla: \'+ZK_P.KH().SKR
                                "
                           ,'');
   {? exec('get','#bulk',ZK_P,'GEN_ZLEC',$_zk_p + $_zk_p)
   || BULK.cntx_psh();
      BULK.clear();
      BULK.index('UNIQUE');
      BULK.prefix('ZK_P','GEN_ZLEC',$_zk_p);
      _dod_zkp:='';
      {? BULK.first()&BULK.size>1
      || {!
         |? {? (BULK.WARTOSC+16)<>$_zk_p
            || _dod_zkp+=','+exec('FindAndGet','#table',ZK_N,exec('FindAndGet','#table',ZK_P,(BULK.WARTOSC+16),,"ZK_P.N",null),,"ZK_N.SYM",'')
                            +', Poz.: '+$exec('FindAndGet','#table',ZK_P,(BULK.WARTOSC+16),,"ZK_P.POZ",0)
            ?};
            BULK.next()
         !}
      ?};
      BULK.cntx_pop()
   ||
     _dod_zkp:=''
   ?};
   _desc_pod:=exec('record','#to_string',_zk_p);
   _desc:='Utwórz zlecenia do - %1%2%3'@@[_desc_pod,_dod_zkp,_desc_dod]
?};

_desc


\display_zkp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [NUCO]
:: OPIS: Formula na wyswietl na Todo
::   WE: _a:=ZK_P.ref
::----------------------------------------------------------------------------------------------------------------------
:: szukam rekordu kluczowego

_class:={? __develop || @.Class.cProMan || @.CLASS.cProMan ?};
_mp:=obj_new(_class);
_zk_p:=null();

{? _mp.setCntx(BI_TODO.BI_PREL)>0
|| _in:=_mp.load(exec('kind_in','#b_port'));
   {? var_pres('p01',_in)<>type_of(~~)
   || _zk_p:=_in.p01
   |? var_pres('p02',_in)<>type_of(~~)
   || _zk_p:=_in.p02
   |? var_pres('p03',_in)<>type_of(~~)
   || _ref_zkp:=sql('select PX_OBJ.ZK_P from @PX_CONN join PX_OBJ where PX_CONN.REFERENCE=\':_a\'',$_in.p03).ZK_P;
      {? _ref_zkp<>''
      || _zk_p:=exec('FindAndGet','#table',ZK_P,_ref_zkp)
      ?}
   ?}
?};

ZK_P.cntx_psh();
ZK_N.cntx_psh();
ZK_P.clear();
{? _zk_p<>null & ZK_P.seek(_zk_p,ref_name(_zk_p)) & ZK_N.seek(ZK_P.N,ref_name(ZK_P.N))
|| ZK_P.N().SYM;
   ATR.MJS:='ZK_P';
   __fz_formapla_tab:=ZK_N;
   ZK_P.win_edit('DISPZ');
   ZK_N.memo_get(,'UW');
   exec('set_efld_opt','mat_atr','ZK_P');
   exec('set_efld_opt','zamsiw_poz','RED'+ZK_P.RODZ);
   exec('dispWithNag','okienka','ZK_P','DISPZ','ZK_N','N','RED'+{? ZK_P.RODZ='Z' || '' || 'W' ?},'Podgląd zamówienia'@);
   VAR_DEL.delete('__fz_formapla_tab')
?};
ZK_N.cntx_pop();
ZK_P.cntx_pop();
~~


\dsc_zdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_DEV]
:: OPIS: Opis na listę TO_DO dla czynności LZK_ZDS_REAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_zdp_nag:=null();

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZDP_NAG',_in)<>type_of(~~)
|| _tab:=sql('select ZDP_NAG.SYM,
                        KH.SKR,
                        ZDP_NAG.D_WYS,
                        ZDP_NAG.D_REA
                   from @ZDP_NAG join KH
                  where ZDP_NAG.REFERENCE=\':_a\'',
                  _in.ZDP_NAG);
   _desc_dod:=' - Sym.: '+_tab.SYM+', dla: '+_tab.SKR+', data wys.: '+($_tab.D_WYS)+', data real.: '+($_tab.D_REA);
   _desc:='Zrealizuj potwierdzenie - %1'@@[_desc_dod]
?};

_desc


\dsc_eann
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_DEV]
:: OPIS: Opis na listę TO_DO dla operacji mobilnych
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_eann:=null();

_desc:='';

_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('EANN',_in)<>type_of(~~)
|| _eann:=_in.EANN;
   _tab:=sql('select MG.SYM,
                     USERS.DANE as USR,
                     EANN.NRC,
                     EANN.SYM as SYMD
                from @EANN join MG join USERS
               where EANN.REFERENCE=:_a',
               _eann);
   {? _tab.first()
   || _desc:='Zrealizuj wydanie na podstawie kompletacji %1, wykonanej przez %2, na czytniku %3 w magazynie %4'@@[_tab.SYMD, _tab.USR, $_tab.NRC, _tab.SYM]
   || _desc:='brak eann'@@
   ?}
|| _desc:='Zrealizuj kompletacje wysyłki na podstawie operacji kompletacji'@@
?};
_desc


\dsc_RWZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_DEV]
:: OPIS: Opis na listę TO_DO dla pobrania próbek z dokumentu RP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_nag:=null();

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('p01',_in)<>type_of(~~)
|| _tab:=sql('select ND.SYM from @ND where ND.REFERENCE=:_a',
                  _in.p01);
   _desc_dod:=' raportu produkcji: '+_tab.SYM;
   _desc:='Zrealizuj pobranie próbek do %1'@@[_desc_dod]
?};

_desc


\sprUprMag2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Sprawdzenie czy podany operator ma uprawnienia do danego magazynu
::   WE: pobieramy za pomocą params_get();
::   WY: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_param:=params_get();
:: parametry wejściowe czynności
_in:=_param.in;
:: użytkownik
_user:=_param.user;

_wyn:=0;

{? var_pres('ND',_in)=type_of(null()) & _in.ND<>null()
|| _mag:=exec('FindAndGet','#table',ND,_in.ND,,"MAG",null())
|? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N<>null()
|| _mag:=exec('FindAndGet','#table',ZK_N,_in.ZK_N,,"ZK_N.MG",null())
::   Dodatkowo dla czynności realizacji zamówienia wewnętrznego (zmienna nazywa się ZAM)
|? var_pres('ZAM',_in)=type_of(null()) & _in.ZAM<>null()
|| _mag:=exec('FindAndGet','#table',ZK_N,_in.ZAM,,"ZK_N.MG",null())
|| _mag:=null()
?};

{? _mag=null()
|| _wyn:=1
|| USERS_UP.cntx_psh();
   USERS_UP.index('MAG');
   USERS_UP.prefix(_user,'MG',_mag);
   _wyn:=USERS_UP.first();
   USERS_UP.cntx_pop()
?};
_wyn


\sprUprZamWewBP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Sprawdzenie czy podany operator ma uprawnienia do danego zamówień wewnętrznych Bilans produkcji
::   WE: pobieramy za pomocą params_get();
::   WY: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_param:=params_get();
:: parametry wejściowe czynności
_in:=_param.in;
:: użytkownik
_user:=_param.user;

_wyn:=0;
{? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N<>null()
|| _mag:=exec('FindAndGet','#table',ZK_N,_in.ZK_N,,"ZK_N.MD",null())
|| _mag:=null()
?};

{? _mag=null()
|| _wyn:=1
|| USERS_UP.cntx_psh();
   USERS_UP.index('MAG');
   USERS_UP.prefix(_user,'MG',_mag);
   _wyn:=USERS_UP.first();
   USERS_UP.cntx_pop()
?};
_wyn


\opis_zam_wew
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_TEST]
:: OPIS: Opis na todo do rejestracji zamówienia wewnętrznego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_zk_n:=null();
_sym:='';

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('ZK_N',_in)<>type_of(~~)
|| _sym:=exec('FindAndGet','#table',ZK_N,_in.ZK_N,,"ZK_N.SYM",'')
?};

_desc:='Zakończ redakcję zamówienia wewnętrznego  %1'@@[_sym];

_desc


\wer_daty_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_TEST]
:: OPIS: Formuła na opis - Weryfikacja daty dla pozycji zamówienia
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_zk_p:=null();

_desc:='Zweryfikuj termin realizacji zamówienia'@@;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('p01',_in)<>type_of(~~)
|| _zk_p:=_in.p01
?};

{? var_press('_zk_p')=7 & _zk_p<>null()
|| _desc_dod:=' - '+exec('FindAndGet','#table',ZK_P,_zk_p,,"
                                \'KTM: \'+ZK_P.M().KTM +\', dla: \'+ZK_P.KH().SKR
                                "
                           ,'');
   _desc_pod:=exec('record','#to_string',_zk_p);
   _desc:='Zweryfikuj termin realizacji dla - %1%2'@@[_desc_pod,_desc_dod]
?};

_desc


\dsc_plans_usun_zl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO_TEST]
:: OPIS: Opis na todo do usunięcia zlecenia z planu strategicznego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_sym:='';

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('p01',_in)<>type_of(~~)
|| _sym:=exec('FindAndGet','#table',ZL,_in.p01,,"ZL.SYM",'')
?};

_desc:='Usuń zlecenie %1 z planu strategicznego, aby zakończyć jego modyfikacje.'@@[_sym];

_desc


\dsc_dalej_zk_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TP [NUCO]
:: OPIS: Opis na todo generowanie zleceń z zamuwienia sprzedaży
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_sym:='';

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('p01',_in)<>type_of(~~)
|| _sym:=exec('FindAndGet','#table',ZK_N,_in.p01,,"ZK_N.SYM+' z dnia: '+$ZK_N.DP+', dla '+ ZK_N.KH().NAZ",'')
?};

_desc:='Wygeneruj zlecenia dla pozycji zamówienia: %1.'@@[_sym];

_desc

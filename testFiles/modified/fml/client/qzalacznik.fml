:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: qzalacznik.fml
:: Utworzony: 08.12.2022
:: Autor: WW
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================

\zalacznik

::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyswietla dokumenty powiazane z rekordem
::   WE:  _a  - akronim tabeli
::       [_b] - 0 - wyswietla select, 1 - nie wyswietla
::       [_c] - czy dostepne funkcje edycyjne, gdy wyswietlony select (0- tak, 1 -nie 2-nie, ale dostępne wysyłanie)
::       [_d] - refsql rekordu
::       [_e] - 0 - domyslnie, 1 - dodatkowy panel załączników z DOKUMZ
::----------------------------------------------------------------------------------------------------------------------

{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0  ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=0 ?} || _c:=0  ?};
{? _>=4 || {? type_of(_d)<>2 || _d:='' ?} || _d:='' ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0  ?};
_d:=EDOKUM.ZD_NAG;
{? _d=''
|| _d:=32-EDOKUM.DOK_POW
?};
{? ~_b & _e

|| VAR_DEL.delete('__CTR');
   __CTR:=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','BLOKUJ','PODGLAD','WIN');
   __CTR.BTAB:='DOKUMZ';
   __CTR.TAB:='DOKUM';
   __CTR.BBLOB:='DOKUM';
   __CTR.WIN_ACR:=exec('mk_ctr','bloby','dokum_dokz',':');
   __CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
   __CTR.DIR_SEP:=exec('dir_sep','bloby');
   __CTR.DND:=1;
   __CTR.BLOKUJ:=0;
   {? _c
   || __CTR.PODGLAD:=1
   || __CTR.PODGLAD:=0
   ?};
   __CTR.WIN:='WER';

   _okno:=DOKUM.grp_make('Załączniki'@,,'dokum',,,,,'normal');
   _fr:=$('exec(\'gr_dok\',\'dokum\');exec(\'dokum_rfr\',\'bloby\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
   DOKUM.grp_sel(_okno,,'WER',,_fr);

   DOKUM.grp_splt(_okno,,'vertical','nlook1');
   DOKUM.grp_sel(_okno,DOKTYP,'CECH',,,,,,,,,,'maximized_with_title','doktyp');

   DOKUM.grp_splt(_okno,'panel0','horizontal','nlook2',16);
   _fb:=$("
      {? grp_empty(DOKUM,__CTR.WIN)
      || __CTR.BLOKUJ:=1
      || __CTR.BLOKUJ:=0
      ?}
   "+
   {? _c
   || ""
   || ";
         {? DOKUM.BL_STAT='' | DOKUM.BL_STAT=exec('before_send','zbl_stat')
         || __CTR.PODGLAD:=0
         || __CTR.PODGLAD:=1
         ?};
         exec('bobs_dokumz','bloby')
      "
   ?});
   _fa:="";
   DOKUM.grp_ctr(_okno,DOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_fb,_fa,,'maximized')

|| _okno:= 'GR'

?};

($(_a+'.cntx_psh()'))();
KHVZ.EDIT:=_c;
DOKUM.index('DOKUM');
DOKUM.win_sel(_okno);
DOKUM.timer('sel',_okno,10,"win_disp()");
exec('icon','dokum');
POLA.REFSQL:={? _d<>'' || _d || $(($(_a+'.ref'))()) ?};
__REFSQL:=POLA.REFSQL;
DOKUM.prefix(REF.FIRMA,POLA.REFSQL);
_par:='N';
{? _b=0
|| _blink_hide:='W(BIU)';
   {? exec('is_active','businesslink3')
   || {? ref_tab(POLA.REFSQL)=FAKS
      || {? exec('FindAndGet','#table',FAKS,POLA.REFSQL,,"SZ",'')='S'
         || _blink_hide:=''
         ?}
      ?}
   ?};

   {? _c>0
   || DOKUM.actions('WER',_blink_hide+{? _c=1 || 'Ww' || '' ?}+'GSsDdPpUuEeIiL:LDd');
      DOKUM.select()
   || _act:={? _par<>'T' || 'S' || '' ?};
      DOKUM.actions('WER',_blink_hide+'G:'+_act);
      DOKUM.select()
   ?};
   DOKUM.actions('WER')
?};
KHVZ.EDIT:=0;
&__REFSQL;
($(_a+'.cntx_pop()'))();
''
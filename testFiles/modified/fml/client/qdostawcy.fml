:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: Nowy1.fml
:: Utworzony: 28.12.2022
:: Autor: WW
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================

\dostawcy
{? MKODK.index('?')<>'MDOST'
||
   _wyn:='';
   MDOST.M:=MKODK.M;
   TAZ.SD:='D';
   MDOST.KH:=MKODK.KH;
   _tab:=sql('SELECT REFERENCE, KOD, TR, SLU FROM SLO WHERE KOD LIKE \'PLN\'');
   _ref:=exec('FindAndGet','#table',SLO,_tab.REFERENCE,,"ref",null());
   _tab1:=sql('SELECT D FROM MDOST WHERE MDOST.M = :_a AND D = \'T\'',MKODK.M);
   {? _tab1.size>0
   ||
      MDOST.D:='N'
   ||
      MDOST.D:='T'
   ?};
   MDOST.WAL:=_ref;
   MDOST.WAL().KOD:=_tab.KOD;
   MDOST.MKODK:=MKODK.ref;
   MDOST.MKODK().KTM:=MKODK.KTM;
   MDOST.MKODK().N:=MKODK.N;
   MDOST.SD:='D';
   MDOST.win_edit('RED');
   {? MDOST.edit("params_exec('chk_mdst','!zws_par_kmtr')")
   || MDOST.add()
   ?}
||
   FUN.info('Funkcja przypisania dostawcy jest dostępna od strony kartoteki kontrahenta')
?}


\wybierz_dostawce

MDOST.cntx_psh();
_ndx:=MDOST.ndx_tmp(,,'SD',,,'M',,,'ILDNI',,);
exec('tab','qdostawcy');
MDOST.index(_ndx);
MDOST.prefix('D',PD_P.M,);
_win:=MDOST.mk_sel('Dostawcy materiału'@,'P',0,'mdostqwer',1,8,17,0,'U','T',0,0,0,'','','on');
MDOST.win_fld(_win,MDOST,'D',,,3,0,1,'',0,'',2,,"mb_exec('''T''')","mb_exec('''N''')","");
MDOST.win_fld(_win,MDOST,'KH','KOD','KOD',8,0,0,'Kod'@@,0,'',,);
MDOST.win_fld(_win,MDOST,'KH','NAZ','NAZ',20,0,1,'Nazwa'@@,0,'',,);
MDOST.win_fld(_win,MDOST,'ILDNI',,,10,0,0,'Lead time'@@,0,'Lead time'@,,);
MDOST.win_fld(_win,MDOST,'EWZ',,,10,2,0,'EWZ'@@,0,'',,);
MDOST.win_fld(_win,tmp,'CENA',,,10,2,0,'Cena'@@,0,'',,);
MDOST.win_fld(_win,MDOST,'MKODK','IL',,10,2,0,'Ilość zbiorcza'@@,0,'',,);
MDOST.win_fld(_win,MDOST,'MKODK','KTM','*',15,0,1,'Indeks dostawcy'@@,0,'',,);
MDOST.win_fld(_win,MDOST,'MKODK','N','*',15,0,1,'Nazwa dostawcy'@@,0,'',,);
MDOST.win_act(_win,0,'Formuła','WYBIERZ'@@,'','',"","PD_P.KH:=MDOST.KH;PD_P.put();params_exec('walidacja','qdostawcy');exec('sel_exit_','#window')",1,0,"","",'',0);
tmp.fld_fml('CENA','BEFORE_DISPLAY',"exec('minimalna_wartosc_zamowienia','qdostawcy',tmp)");
MDOST.win_sel(_win);
MDOST.select();
obj_del(tmp);
MDOST.ndx_drop(_ndx);
MDOST.cntx_pop()

\icon
MDOST.index('M');
MDOST.prefix('D',PD_P.M);
{? MDOST.first()
|| {? MDOST.size()>1
   ||
      '__sysusr.png:10'
   ||
      ''
   ?}
||
''
?}


\walidacja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Ustawienie domyślności dla pojedynczego rekordu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? MDOST.sel_size()=0
|| MDOST.cntx_psh();
   _mat:=MDOST.M;
   MDOST.cntx_psh();
   MDOST.index('DM');
   MDOST.prefix('D','T',_mat);
   MDOST.first();
   _ref:=MDOST.ref;
   MDOST.cntx_pop();

   _old_kh:='';
   MDOST.cntx_psh();
   MDOST.prefix();
   {? MDOST.seek(_ref) || _old_kh:=MDOST.KH().SKR ?};
   MDOST.cntx_pop();

   {? FUN.ask('Czy chcesz dla materiału %1 zmienić domyślnego dostawcę z %2 na %3.'[MDOST.M().KTM,_old_kh,MDOST.KH().SKR])
   || MDOST.D:='T';
      MDOST.put();
      {? _old_kh<>'' & _old_kh<>MDOST.KH().SKR
      || MDOST.cntx_psh();
         MDOST.prefix();
         {? MDOST.seek(_ref)
         || MDOST.D:='N';
            MDOST.put
         ?};
         MDOST.cntx_pop();
         FUN.emsg('Dla materiału %1 zmieniono domyślnego dostawcę z %2 na %3.'[MDOST.M().KTM,_old_kh,MDOST.KH().SKR])
      ?}
   ?};
   MDOST.cntx_pop()
?};
~~

\lead_time
:WW: zmiana koloru w sytuacji, gdy mamy niższy lead time w stosunku do domyślnego dostawcy
MDOST.cntx_psh();
MDOST.index('M');
MDOST.prefix('D',PD_P.M,PD_P.KH,);
_size:=MDOST.size;
{? MDOST.first()
|| _ildd:=MDOST.ILDNI
?};
MDOST.clear();
_ndx:=MDOST.ndx_tmp(,,'SD',,,'M',,,'ILDNI',,);
MDOST.index(_ndx);
MDOST.prefix('D',PD_P.M);
_color:='';
{? MDOST.first()
|| {! |?
   {? MDOST.ILDNI>0
   || {? _ildd>MDOST.ILDNI
      || _color:=',248:242:131';
         MDOST.ndx_drop(_ndx);
         MDOST.cntx_pop();
         return(_color)
      ?}
   ?};
   MDOST.next()
   !}
|| _color:=''
?};
MDOST.ndx_drop(_ndx);
MDOST.cntx_pop();
_color

\cennik
MDOST.cntx_psh();
MKODK.cntx_psh();
TAP.cntx_psh();
MDOST.index('M');
MDOST.prefix('D',PD_P.M,PD_P.KH,);
TAP.index('ODWAL_M');
_color:='';
{? MDOST.first()
|| _tab:=sql('SELECT TAP.REFERENCE,TAP.CEN,TAP.CENB, TAR.REFERENCE,TAR.KOD FROM @TAP JOIN @TAR USING(TAP.TAR,TAR.REFERENCE)
         WHERE TAP.WAL=\':_a\' AND TAP.M=\':_b\' AND TAR.KH=\':_c\' AND TAR.SD=\'D\' ORDER BY TAP.CEN ASC'
         ,$MDOST.WAL,$MDOST.M,$MDOST.KH);
   {? _tab.first()
   || _cenaDD:=_tab.CEN;
      MDOST.prefix('D',PD_P.M);
      _color:='';
      obj_del(_tab);
      {? _cenaDD=0
      || _color:=',248:242:131'
      || {? MDOST.first()
         || {! |?
            _tab:=sql('SELECT TAP.REFERENCE,TAP.CEN,TAP.CENB, TAR.REFERENCE,TAR.KOD FROM @TAP JOIN @TAR USING(TAP.TAR,TAR.REFERENCE)
            WHERE TAP.WAL=\':_a\' AND TAP.M=\':_b\' AND TAR.KH=\':_c\' AND TAR.SD=\'D\' ORDER BY TAP.CEN ASC'
            ,$MDOST.WAL,$MDOST.M,$MDOST.KH);
            {? _tab.first()
            ||
               {? _tab.CEN>0
               || {? _cenaDD > _tab.CEN
                  || _color:=',248:242:131'
                  ?}
                  || _color:=''
               ?}
            ?};
            obj_del(_tab);
            MDOST.next()
            !}
         || _color:=''
         ?}
      ?}
   || _color:=''
   ?}
?};
MDOST.cntx_pop();
MKODK.cntx_pop();
TAP.cntx_pop();
_color


\kod_dostawcy

{? QNUCOZ.KTM<>'' & MDOST.MKODK().KTM=''
|| {? FUN.ask('Czy chcesz dodać zamiennik i automatycznie pobrać dane z kartoteki materiałowej?',,'Pytanie')
   ||
      _ndx:=MKODK.ndx_tmp(,,'KH',,,'M',,,'KTM',,);
      _tmp:=QNUCOZ.KTM;
      _mat:=M.ref;
      MKODK.index(_ndx);
      MKODK.prefix(MDOST.KH,MDOST.M,MDOST.MKODK().KTM);
:WW: jeśli MKODK istnieje w indeksie w połączeniu z tym kh to wtedy przypisz MDOST.MKODK:=MKODK.ref
      {? MKODK.first()
      || {? MDOST.MKODK().KTM=MKODK.KTM
         || MDOST.MKODK:=MKODK.ref;
            MDOST.MKODK().N:=MKODK.N;
            MDOST.MKODK().KODK:=MKODK.KODK;
            return(1)
         ?}
      || _ndx:=M.ndx_tmp(,,'KTM',,);
         M.index(_ndx);
         {? _tmp<>''
         || M.prefix(_tmp);
            {? M.first()
            || MKODK.clear();
               MKODK.KH:=MDOST.KH;
               MKODK.M:=_mat;
               MKODK.KTM:=M.KTM;
               MKODK.N:=M.N;
               MKODK.D:='N';
               MKODK.IDMOB:='N';
               MKODK.AKT:='T';
               MKODK.OLD:='N';
               MKODK.KODK:=M.KODK;
               MKODK.add();
               win_disp();
               MDOST.MKODK:=MKODK.ref;
               MDOST.MKODK().KTM:=MKODK.KTM;
               MDOST.MKODK().N:=MKODK.N;
               MDOST.MKODK().KODK:=MKODK.KODK;
::               _ndx:=MKODK.ndx_tmp(,,'KH',,,'M',,,'KTM',,);
::               MKODK.index(_ndx);
::               MKODK.prefix(MKODK.KH,_mat,MKODK.KTM);
               return(1)
            || FUN.info('Nie znaleziono zamiennika w kartotece materiałowej.')
            ?}
         ?}
      ?}
   ?}
|| 1
?}

\szukaj_wg_nazwy

::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed szukaj wg kodu okna WER_ZAM tabeli M
::   WY: 1
::  OLD: \m_kod_szuk/tkod.fml
::----------------------------------------------------------------------------------------------------------------------
_kh_ref:={? exec('get','#params',100100,2)='T' || BEER.KH || null ?};
_m_rodz:=POMOC.RODZ;

undefine();
define('NAZ','','Nazwa materiału'@,'Nazwa materiału wg ktorego szukany jest material'@,100,80);
def_btn('text='+'&OK'@+',icon=xwin16.png:13',"'key:F2'");
def_btn('text='+'&Anuluj'@+',icon=xwin16.png:14',"'key:Esc'");

{? def_edit("chk_rec()",'Szukana nazwa materiału'@)
||
:: wyszukiwanie

   _old_m:=M.win_dict('?');
   _old_ma:=M.actions('SEL');
   _old_z:=ZAKR.MATU;
   _old_kh:=KH.win_dict('?');
   _old_kha:=KH.actions('WER');
   ZAKR.MATU:='T';
   M.win_dict('SEL');
   M.actions('SEL','T');
   KH.win_dict('WER');
   KH.actions('WER','T');

::   _tab:=sql('SELECT MKODK.KTM, MKODK.KODK FROM MKODK JOIN M USING(MKODK.M,M.REFERENCE)
::            WHERE MKODK.N like \'%:_a%\' or M.N like \'%:_a%\'',DEFINE.NAZ);
   {? MKODK.first()
   || MKODK.cntx_psh;
      MKODK.clear();
      M.f_clear(0);
      MKODK.f_clear(0);
      _sort:='KTM,KODK';
      _from:={? _m_rodz='' || '' || ' join M using("MKODK".M,M.reference)'?};
      _where:=
         '((lower("MKODK".N) like ''%:_a%'' or lower("M".N) like ''%:_b%'') AND ("MKODK".AKT=''T'' or "M".A=''T'') )'+
         ' and MKODK.RSQL=\'\''+
         {? _kh_ref=null || '' || ' and "MKODK".KH=:_c' ?}+
         {? POMOC.MGR=null || '' || ' and "M".MGR=:_d' ?}+
         {? _m_rodz=''   || '' || ' and M.RODZ='':_e''' ?};
      MKODK.f_set(_sort,_from,_where,-DEFINE.NAZ,-DEFINE.NAZ,_kh_ref,POMOC.MGR,_m_rodz);

      {? MKODK.f_first()=0
      ||
         FUN.info('Brak materiału o nazwie %1.'@[DEFINE.NAZ])
      ||
         {? MKODK.f_get()
         ||
            _mref:=MKODK.M;
            _cont:=_one:=1;
            {!
            |? {? _cont:=MKODK.f_get() || _one:=MKODK.M=_mref ?};
               _cont=1 &_one=1 & MKODK.f_next()
            !};
            {? _cont=1
            ||
               {? _one=1
               ||
::          przypadek jesli znaleziono tylko jeden material lub usluge
                  {? ~
                     {? M.f_active
                     || M.f_seek(MKODK.M)
                     || M.seek(MKODK.M)
                     ?}
                  ||
                     FUN.info('Indeks nie występuje w bieżącym zakresie.'@)
                  ?}
               ||
::          przypadek jesli znaleziono wiele materialow lub uslug
                  MKODK.win_edit('REDKH');
                  MKODK.win_sel('WERSZUK');
                  MKODK.hdr_sel();
                  M.cntx_psh();
                  _sel:=MKODK.select();
                  M.cntx_pop();
                  {? _sel<>0
                  ||
                     {? ~
                        {? M.f_active
                        || M.f_seek(MKODK.M)
                        || M.seek(MKODK.M)
                        ?}
                     ||
                        FUN.info('Indeks nie występuje w bieżącym zakresie.'@)
                     ?}
                  ?}
               ?}
            ?}
         ?}
      ?};
      M.f_clear(0);
      MKODK.f_clear(0);
      MKODK.cntx_pop;
      M.win_dict(_old_m);
      M.actions('SEL',_old_ma);
      ZAKR.MATU:=_old_z;
      KH.win_dict(_old_kh);
      KH.actions('WER',_old_kha)
   ?}
?};
undefine();
1


\tab

tmp:=tab_tmp(1,'CENA','REAL','MOQ');
   {? MDOST.first()
   || {! |?
   _tab:=sql('SELECT TAP.REFERENCE,TAP.CEN,TAP.CENB, TAR.REFERENCE,TAR.KOD FROM @TAP JOIN @TAR USING(TAP.TAR,TAR.REFERENCE)
   WHERE TAP.WAL=\':_a\' AND TAP.M=\':_b\' AND TAR.KH=\':_c\' AND TAR.SD=\'D\' ORDER BY TAP.CEN ASC'
   ,$MDOST.WAL,$MDOST.M,$MDOST.KH);
   {? _tab.first()
   || tmp.CENA:=_tab.CEN;
      tmp.add()
   || tmp.CENA:=0;
      tmp.add()
   ?};
   obj_del(_tab);
   MDOST.next()
   !}
?};
tmp


\minimalna_wartosc_zamowienia
_tab:=sql('SELECT TAP.REFERENCE,TAP.CEN,TAP.CENB, TAR.REFERENCE,TAR.KOD FROM @TAP JOIN @TAR USING(TAP.TAR,TAR.REFERENCE)
WHERE TAP.WAL=\':_a\' AND TAP.M=\':_b\' AND TAR.KH=\':_c\' AND TAR.SD=\'D\' ORDER BY TAP.CEN ASC'
,$MDOST.WAL,$MDOST.M,$MDOST.KH);
{? _tab.first()
|| tmp.CENA:=_tab.CEN;
   return(tmp.CENA)
|| tmp.CENA:=0;
   '0'
?}

\analiza_TKTL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [22.26]
:: OPIS: Analiza struktury produktu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_res:=exec('tmat_all_tktl','qtechniczny',TKTL.ref,0);
KOMM.init(250,,'Wyniki kalkulacji dla karty'@,'',,,0);
_tab:=tab_tmp(3,'LP','INTEGER','Poziom',
               'POZ','STRING[30]','NODE',
               'KTM_P','STRING[50]','KTM produktu karty',
               'NAZ_P','STRING[100]','Nazwa produktu',
               'NAZ_K','STRING[50]','Nazwa karty',
               'WER_K','STRING[10]','Wersja karty',
               'KS_K','STRING[20]','Koszt surowców/1kg',
               'CENA','STRING[20]','Cena surowca',
               'KOD_M','STRING[50]','Kod',
               'NAZ_M','STRING[100]','Nazwa',
               'LEAD_T','STRING[10]','Horyzont dostawy',
               'K_TECH','STRING[50]','Karta technologiczna',
               'WER_M','STRING[10]','Wersja',
               'OPIS_M','SYS_MEMO','Opis'
);
_res.cntx_psh();
MDOST.cntx_psh();
TKTLW.cntx_psh();
MDOST.index('DM');
KKTL.cntx_psh();
KKTL.index('KNR');

TKTLW.index('REF');
TKTLW.prefix(TKTL.ref);
{? TKTLW.first()
|| KKTL.prefix(TKTL.ref,TKTLW.ref);
   {? KKTL.last()
   || {? exec('timeDiff','#datetime',KKTL.DT+30,time(0,0,0),date(),time(0,0,0))<time(0,0,0)
      || _qq:=sql('SELECT WART FROM @KPOZK JOIN KRUB using(KPOZK.RUBR,KRUB.REFERENCE) WHERE KALK=\':_a\' and KRUB.NR=200',$KKTL.ref)
      ||
         {? exec('kalk4ktl','!tte_tec_ktkw',TKTL.ref,1,TKTLW.ref,0,'N')=1
         || _qq:=sql('SELECT WART FROM @KPOZK JOIN KRUB using(KPOZK.RUBR,KRUB.REFERENCE) WHERE KALK=\':_a\' and KRUB.NR=200',$KKTL.ref)
         ?}
      ?}
   || {? exec('kalk4ktl','!tte_tec_ktkw',TKTL.ref,1,TKTLW.ref,0,'N')=1
      || _qq:=sql('SELECT WART FROM @KPOZK JOIN KRUB using(KPOZK.RUBR,KRUB.REFERENCE) WHERE KALK=\':_a\' and KRUB.NR=200',$KKTL.ref)
      ?}
   ?}
?};
KKTL.cntx_pop();
TKTLW.cntx_pop();
{? _qq.first()
|| _ksk:=form(_qq.WART,,2);
   {? _res.first()
   || {!
      |?
      _tab.LP:=_res.LP;
      _tab.KTM_P:=TKTL.KTM().KTM;
      _tab.NAZ_P:=TKTL.KTM().N;
      _tab.NAZ_K:=TKTL.NRK;
      _tab.WER_K:=TKTL.WER;
      _tab.KS_K:=_ksk;
      _tab.POZ:=_res.POZ;
      {? _tab.LP=0
      || _tab.KOD_M:=_res.M_KTM
      |? _tab.LP=1
      || _tab.KOD_M:=' '+_res.M_KTM
      |? _tab.LP=2
      || _tab.KOD_M:='  '+_res.M_KTM
      |? _tab.LP=3
      || _tab.KOD_M:='   '+_res.M_KTM
      |? _tab.LP=4
      || _tab.KOD_M:='    '+_res.M_KTM
      |? _tab.LP=5
      || _tab.KOD_M:='     '+_res.M_KTM
      || _tab.KOD_M:='      '+_res.M_KTM
      ?};
      _tab.NAZ_M:=_res.M_N;
      _ff:=sql('SELECT REFERENCE FROM M WHERE M.KTM=\':_a\'',_res.M_KTM);
      {? _res.NRK_KTL<>''
      || _nrk:=exec('FindAndGet','#table',TKTL,_res.NRK_KTL,,"TKTL.NRK",'');
         _wer:=exec('FindAndGet','#table',TKTL,_res.NRK_KTL,,"TKTL.WER",'');
         _tab.K_TECH:=_nrk;
         _tab.WER_M:=_wer
      || _tab.K_TECH:='';
         _tab.WER_M:=''
      ?};
      {? _ff.first()
      || _refm:=exec('FindAndGet','#table',M,_ff.REFERENCE,,"ref",null);
         MDOST.prefix('D','T',_refm);
         {? MDOST.first() & _res.NRK_KTL=''
         || _tab.LEAD_T:=$MDOST.ILDNI
         || _tab.LEAD_T:='-'
         ?}
      || _tab.LEAD_T:='-'
      ?};
      _arr:=sql('select TOP 1 SC.D, SC.C as CENA
      from @SC join @DK using(SC.SRDK,DK.REFERENCE)
                        join @ND using (DK.N,ND.REFERENCE)
                        join TYPYDOK using (ND.TYP, TYPYDOK.REFERENCE)
      where SC.M=:_a and TYPYDOK.Z=\'T\'
      order by SC.D DESC
        ',_refm);

      {? _arr.first()
      || _tab.CENA:=form(_arr.CENA,,2);
         &_arr
      || &_arr;
         _arr:=sql('select TOP 1 SC.D, SC.C as CENA
         from @SC where SC.M=:_a
         order by SC.D DESC
           ',_refm);
         {? _arr.first()
         || _tab.CENA:=form(_arr.CENA,,2)
         ?};
         &_arr
      ?};
      &_ff;
      _tab.add();
:: Dodanie informacji OPIS (memo) do bazy surowców
      _memo:=sql('select OPIS from M where M.REFERENCE=:_a',_refm);
      _memo.memo_get();
      _tab.memo_set(_memo.memo_txt(),'OPIS_M');
      _tab.memo_put();
      _tab.put();
      &_memo;
      _res.next()
      !}
   ?}
?};
_tab.cntx_psh();
_ndx:=_tab.ndx_tmp(,,'POZ',,1,'KTM_P',,,'NAZ_P',,,'WER_K',,,'KS_K',,,'LEAD_T',,,'K_TECH',,,'WER_M',,,'KOD_M',,);
_tab.index(_ndx);
_tab.prefix();
{? _tab.first()
|| 1
?};
:: Tabela dla display
_sEdt:=_tab.mk_edit('',,'#tmat_swin');
_tab.win_edit(_sEdt);
_tab.win_efld(_sEdt,,'KOD_M'    ,          ,          , 25,, ,'Kod surowca',,'Kod surowca',,);
_tab.win_efld(_sEdt,,'NAZ_M'    ,          ,          , 25,, ,'Nazwa surowca',,'Nazwa surowca',,);
_tab.win_efld(_sEdt,,'OPIS_M'    ,          ,          ,60,,1 ,'Opis surowca',,'Opis surowca',,);

_win:=_tab.mk_sel('Analiza struktury produktu','P',0,'mdostqwer',1,8,17,0,'U','T',0,0,,'maximized');

:_tab.win_fld(_win,_tab,'POZ',,,17,0,0,'Poziom'@@,0,'',,);
_tab.fld_attr('POZ',2);
_tab.win_fld(_win,_tab,'KTM_P',,,15,0,0,'KTM produktu karty'@@,0,'',,);
_tab.win_fld(_win,_tab,'NAZ_P',,,20,0,0,'Nazwa produktu'@@,0,'',,);
_tab.win_fld(_win,_tab,'NAZ_K',,,22,0,0,'Nazwa karty'@@,0,'',,);
_tab.win_fld(_win,_tab,'WER_K',,,10,0,0,'Wersja karty'@@,0,'',,);
_tab.win_fld(_win,_tab,'KS_K',,,8,0,0,'Koszt surowców/1kg'@@,0,'',,);
_tab.win_fld(_win,_tab,'CENA',,,8,0,0,'Cena surowca'@@,0,'',,);
_tab.win_fld(_win,_tab,'KOD_M',,,15,0,0,'Kod'@@,0,'',,);
_tab.win_fld(_win,_tab,'NAZ_M',,,22,0,0,'Nazwa'@@,0,'',,);
_tab.win_fld(_win,_tab,'LEAD_T',,,8,0,0,'Horyzont dostawy'@@,0,'',,);
_tab.win_fld(_win,_tab,'K_TECH',,,20,0,0,'Karta technologiczna'@@,0,'',,);
_tab.win_fld(_win,_tab,'WER_M',,,8,0,0,'Wersja'@@,0,'',,);
_tab.win_act(_win,0,'Kolejność','','','',"","",0,0,"","",'',0);
_tab.win_sel(_win);
_tab.select();
_tab.ndx_drop(_ndx);
_tab.cntx_pop();
MDOST.cntx_pop();
_res.cntx_pop()


\szukaj_zamiennik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa
:: OPIS: Wykorzystanie w Funkcje Nuco pozycje planu dostaw
::----------------------------------------------------------------------------------------------------------------------
_m:=PD_P.M().KTM;
M.cntx_psh;
_tab:=sql('select * from MKODK left join M using(MKODK.M,M.REFERENCE)
         where MKODK.KTM=\':_a\' and MKODK.RSQL=\'\' and M.A=\'T\' and MKODK.KH is not null',_m);
{? _tab.first()
|| _sWin:=_tab.mk_sel('Użyty w indeksach','P',,'#zamien_swin',,,,,'U','T');
   _tab.win_sel(_sWin);
   _tab.win_fld(_sWin,,'KTM0002',,,30,,,'Indeks',,,,1);
   _tab.win_fld(_sWin,,'N0002',,,60,,,'Nazwa',,,,1);
   _tab.win_act(_sWin, ,'Kolejność' );
   _tab.win_act(_sWin, ,'Szukaj');
   _tab.select()
|| FUN.info('Brak powiązania.')
?};
M.cntx_pop
\rozchod_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [22.26]
:: OPIS: Rozchód na ostatnie 30, 90, 180, 360
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_tab:=tab_tmp(,'R30','REAL','Rozchód ostatnie 30 dni'
              ,'R90','REAL','Rozchód ostatnie 90 dni'
              ,'R180','REAL','Rozchód ostatnie 180 dni'
              ,'R360','REAL','Rozchód ostatnie 360 dni');

_tab.R30:=sql('select sum(DK.IL) as S_IL from @DK join @ND where DK.M=:_a and DK.PLUS=\'N\' and DK.ZL is not null and ND.D>to_date(:_b)',PD_P.M,date()-30).S_IL;
_tab.R90:=sql('select sum(DK.IL) as S_IL from @DK join @ND where DK.M=:_a and DK.PLUS=\'N\' and DK.ZL is not null and ND.D>to_date(:_b)',PD_P.M,date()-90).S_IL;
_tab.R180:=sql('select sum(DK.IL) as S_IL from @DK join @ND where DK.M=:_a and DK.PLUS=\'N\' and DK.ZL is not null and ND.D>to_date(:_b)',PD_P.M,date()-180).S_IL;
_tab.R360:=sql('select sum(DK.IL) as S_IL from @DK join @ND where DK.M=:_a and DK.PLUS=\'N\' and DK.ZL is not null and ND.D>to_date(:_b)',PD_P.M,date()-360).S_IL;
_tab.add();
_win:=_tab.mk_sel('Rozchód w okresach','P',0,'mr90qwer',1,8,17,0,'U','T',0,0,,'maximized');

_tab.fld_fml('R30','DISPLAY_FORMAT',"{? cur_tab.R30=0 || 'out_prec='+$0 || 'out_prec='+$3 ?}");
_tab.fld_fml('R90','DISPLAY_FORMAT',"{? cur_tab.R90=0 || 'out_prec='+$0 || 'out_prec='+$3 ?}");
_tab.fld_fml('R180','DISPLAY_FORMAT',"{? cur_tab.R180=0 || 'out_prec='+$0 || 'out_prec='+$3 ?}");
_tab.fld_fml('R360','DISPLAY_FORMAT',"{? cur_tab.R360=0 || 'out_prec='+$0 || 'out_prec='+$3 ?}");

_tab.win_fld(_win,_tab,'R30',,,12,0,0,'Rozchód w okresie 30 dni'@@,0,'',,);
_tab.win_fld(_win,_tab,'R90',,,12,0,0,'Rozchód w okresie 90 dni'@@,0,'',,);
_tab.win_fld(_win,_tab,'R180',,,12,0,0,'Rozchód w okresie 180 dni'@@,0,'',,);
_tab.win_fld(_win,_tab,'R360',,,12,0,0,'Rozchód w okresie 360 dni'@@,0,'',,);

_tab.win_sel(_win);
_tab.select()

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: Nowy1.fml
:: Utworzony: 23.08.2020
:: Autor: NN
:: Systemy:
::======================================================================================================================
:: Zawartość: Generowanie danyc klienta do prezentacji MarżyIV w Qlik Sense
::======================================================================================================================


\dane_kh_ktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: NN [20.xx]
:: OPIS: Przygotowanie danych
::   WE: _a - data początkowa
::       _b - data końcowa
::   WY:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TAB_REL','TAB_REL2','sql01','pocz','kon','info','generuj');
pocz:=_a;
kon:=_b;
sql01:='select
      :_d
      substr(cast(FAKS.D as string_type),6,2)||\'.\'||substr(cast(FAKS.D as string_type),1,4) Okres,
      :_c||\'_\'||:_e as K_P,
      :_c Klient,
      GRKH.KOD Grupa,
      MGRP.KOD PodGrupa,
      :_e PROD,
      MGR.KOD,
      MGR.NAZ,
      M.N,
      :_f MPK,
      \'Wykonanie\' Wersja,
      \'N\' CZY_BUD,
      0 Tech,
      FAP.WN Wartosc,
      case when M.RODZ=\'U\' then FAP.WN else 0 end PRZ_USL,
      case when M.RODZ<>\'U\' and M.R<>\'W\' then FAP.WN else 0 end PRZ_TOW,
      case when M.RODZ<>\'U\' and M.R=\'W\' then FAP.WN else 0 end PRZ_WYR,
      cast (0 as real_type) WARMAG,
      cast (0 as real_type) MARZA,
      FAP.reference FAPREF
      from @FAP
      join @FAKS using (FAP.FAKS,FAKS.reference)
      left join KH using(FAKS.KH,KH.reference)
      join M left join HAN
      left join MGR using (M.MGR,MGR.reference)
      left join MGRP using (M.MGRP,MGRP.reference)
      left join GRKH using (KH.GRKH,GRKH.reference)
      left join STS using (FAKS.STS,STS.reference)
      where FAKS.D <to_date(:_b) and FAKS.D >=to_date(:_a) and FAKS.SZ=\'S\' :_g
      order by WARTOSC DESC';

sql02:='select :_b
      :_c,
      sum(WARTOSC) Wartosc
      from :_a
      group by :_c
      order by WARTOSC DESC';

rel_size:="sql('select distinct K_P from :_a',cur_tab).size()";
prod_size:="sql('select distinct PROD from :_a',cur_tab).size()";
klient_size:="sql('select distinct KLIENT from :_a',cur_tab).size()";

info:="FUN.info('Liczba relacji: '+$rel_size()+'\n'+
       'Liczba produktów: '+$prod_size()+'\n'+
       'Liczba kontrahentów: '+$klient_size())
";

generuj:="
         _lrel:=sql('select distinct K_P from :_a',TAB_REL).size;
         undefine;
         define('header',~~,'        Parametry generowania danych:');
         define('REL_LICZ',_lrel,'Max liczba relacji Klient-Produkt',,30);
         define('KH_LICZ',20,'Max liczba Klientów',,6,7);
         define('P_LICZ',10,'MaxlLiczba Produktów',,6,7);
         define('PROD','KTM','Wymiar Produkt: KTM - indeks / MGRP - podgrupa / MGR - grupa ',,4,5);
         define('KH','KH','Wymiar Klient: GR - grupa kontrah. / KH - kontrahent ',,3,4);
         define('MPK','HAN','Wymiar MPK: HAN - handlowiec / STS - staowisko sprzedaży ',,3,4);
         def_edit();
         VAR_DEL.delete('TAB_REL2');
         TAB_REL2:=sql(sql01,
                       pocz,
                       kon,
                       {? DEFINE.KH='GR'|| 'GRKH.KOD' || 'KH.KOD' ?},
                       'top '+$DEFINE.REL_LICZ,
                       {? DEFINE.PROD='MGR'
                          || 'MGR.KOD'
                          |? DEFINE.PROD='MGRP'
                          || 'case when MGRP.KOD is null then MGR.KOD else MGRP.KOD end'
                          || 'M.KTM'
                       ?},
                       {? DEFINE.MPK='HAN' || 'HAN.NAZ' || 'STS.NAZ' ?},
                       ''
                       );
        _sql_prod:=sql(sql02,TAB_REL2,'top '+$DEFINE.P_LICZ,'PROD');
        _sql_klient:=sql(sql02,TAB_REL2,'top '+$DEFINE.KH_LICZ,'KLIENT');
        _sql_rel:=sql(sql02,TAB_REL2,'top '+$DEFINE.REL_LICZ,'K_P');
         {? TAB_REL2.first()
         || _sql_prod.index(_sql_prod.ndx_tmp(,1,'PROD',,));
            _sql_klient.index(_sql_klient.ndx_tmp(,1,'KLIENT',,));
            _sql_rel.index(_sql_rel.ndx_tmp(,1,'K_P',,));
            {! |? _sql_prod.prefix(TAB_REL2.PROD);
                  _sql_klient.prefix(TAB_REL2.KLIENT);
                  _sql_rel.prefix(TAB_REL2.K_P);
                  {? _sql_prod.first() & _sql_klient.first() & _sql_rel.first()
                  || TAB_REL2.next()
::                  || TAB_REL2.del()
                  || TAB_REL2.KLIENT:='POZ';
                     TAB_REL2.PROD:='POZ';
                     TAB_REL2.K_P:=TAB_REL2.KLIENT+'_'+TAB_REL2.PROD;
                     TAB_REL2.put();
                     TAB_REL2.next()

                  ?}
            !}
         ?};
          _rel:=TAB_REL2.mk_sel('Dane do Marży IV',,,'tab_rel2',,,,,'U');
         TAB_REL2.win_fld(_rel,,'OKRES',,,8,,,'Okres');
         TAB_REL2.win_fld(_rel,,'K_P',,,8,,,'Klient_Produkt');
         TAB_REL2.win_fld(_rel,,'KLIENT',,,8,,,'Klient');
         TAB_REL2.win_fld(_rel,,'PROD',,,8,,,'Produkt');
         TAB_REL2.win_fld(_rel,,'MPK',,,6,,,'MPK');
         TAB_REL2.win_fld(_rel,,'WERSJA',,,6,,,'Wersja');
         TAB_REL2.win_fld(_rel,,'CZY_BUD',,,3,,,'Czy Budżet?');
         TAB_REL2.win_fld(_rel,,'WARTOSC',,,16,,,'Przychody');
         TAB_REL2.win_fld(_rel,,'PRZ_USL',,,16,,,'Przychody ze spr. usl.');
         TAB_REL2.win_fld(_rel,,'PRZ_TOW',,,16,,,'Przychody ze spr. tow.');
         TAB_REL2.win_fld(_rel,,'PRZ_WYR',,,16,,,'Przychody ze spr. wyr.');
         TAB_REL2.win_fld(_rel,,'WARMAG',,,16,,,'Koszty towarów.');
         TAB_REL2.win_fld(_rel,,'MARZA',,,16,,,'Marża I');
         TAB_REL2.win_sel(_rel);
         TAB_REL2.win_act(_rel,,'Kolejność');

         TAB_REL2.win_act(_rel,,'Formuła','Info',,,
         \"info()
         \",,1);
         exec('war_mag','qmiv',TAB_REL2);
         TAB_REL2.select()
         ";

TAB_REL:=sql(sql01,pocz,kon,'KH.KOD','','M.KTM','HAN.NAZ','');
_rel:=TAB_REL.mk_sel('Dane do Marży IV',,,'tab_rel',,,,,'U');
TAB_REL.win_fld(_rel,,'OKRES',,,8,,,'Okres');
TAB_REL.win_fld(_rel,,'K_P',,,8,,,'Klient_Produkt');
TAB_REL.win_fld(_rel,,'KLIENT',,,8,,,'Klient');
TAB_REL.win_fld(_rel,,'PROD',,,8,,,'Produkt');
TAB_REL.win_fld(_rel,,'MPK',,,6,,,'MPK');
TAB_REL.win_fld(_rel,,'WERSJA',,,6,,,'Wersja');
TAB_REL.win_fld(_rel,,'CZY_BUD',,,3,,,'Czy Budżet?');
TAB_REL.win_fld(_rel,,'WARTOSC',,,16,,,'Przychody');
TAB_REL.win_fld(_rel,,'PRZ_USL',,,16,,,'Przychody ze spr. usl.');
TAB_REL.win_fld(_rel,,'PRZ_TOW',,,16,,,'Przychody ze spr. tow.');
TAB_REL.win_fld(_rel,,'PRZ_WYR',,,16,,,'Przychody ze spr. wyr.');
TAB_REL.win_fld(_rel,,'WARMAG',,,16,,,'Koszty towarów.');
TAB_REL.win_fld(_rel,,'MARZA',,,16,,,'Marża I');
TAB_REL.win_sel(_rel);

TAB_REL.win_act(_rel,,'Formuła','Generuj dane',,,
      "
       generuj()
      ",,1);
TAB_REL.win_act(_rel,,'Kolejność');
TAB_REL.win_act(_rel,,'Formuła','Info',,,
      "info()
      ",,1);
exec('war_mag','qmiv',TAB_REL);
TAB_REL.select();

VAR_DEL.delete('TAB_REL','TAB_REL2','sql01','pocz','kon','info','generuj')


\war_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: wartosc magazynowa dla pozycji faktury
::  OLD: \war_mag/sql_rpt.fml
::   WE: _nazwa tabeli
::----------------------------------------------------------------------------------------------------------------------
{? _a.first()
||
   _war_mag:="
      ND.cntx_psh(); DK.cntx_psh(); MG.cntx_psh();
      ND.use('nagdo'+(DK.name()+3));
      _wyn:=0;
      DK.index('FAP');
      DK.prefix(_a.FAPREF,_a.FAPREF);
      {? DK.first()
      ||
         {!
         |? _war:=DK.WAR;
            {? 1+DK.N().MAG().TYP='D'
            || BO.CEN:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C);
               {? BO.CEN<>DK.C || _war:=(BO.CEN*DK.IL)$2 ?}
            ?};
            {? DK.PLUS='T'
            || _wyn-=_war
            || _wyn+=_war
            ?};
            DK.next()
         !}
      ?};
      ND.cntx_pop(); DK.cntx_pop(); MG.cntx_pop();
      _wyn
   ";

   {!
   |?
      _a.WARMAG:=0;
      _a.MARZA:=0;
::    szuka w roku poprzedzajacym fakture
      _oddz:=1+(5-_a.FAPREF);
      _ar:=(8+_a.FAPREF)+2;
      {? #_ar>0
      || DK.cntx_psh();
         DK.use('dokma'+_oddz+form(#_ar-1,-2));
         _a.WARMAG+=_war_mag(_a);
         DK.cntx_pop()
      ?};
::    szuka w roku faktury
      DK.cntx_psh();
      DK.use('dokma'+_oddz+_ar);
      _a.WARMAG+=_war_mag(_a);
      DK.cntx_pop();
      _a.MARZA:=(_a.WARTOSC-_a.WARMAG)$2;
      _a.put();
      _a.next()
   !};
   _a.first()
?};
1
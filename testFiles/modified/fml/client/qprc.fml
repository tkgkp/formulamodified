:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: qprc.fml
:: Utworzony: 22.02.2024
:: Autor: areKc
:: Systemy: PRC
::======================================================================================================================
:: Zawartość: Formuły dedykowane do wywołania w obszarze PRC
::======================================================================================================================


\liczba_nadgodzin_w_tygodniu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc
:: OPIS: Funkcja ustalająca liczbę nadgodzin wypracowanych w bieżącym tygodniu pracy.
::     : WE _a [TABLE] - tabela z kwalifikacjami.
::     : WY
::----------------------------------------------------------------------------------------------------------------------
:: Deklaracja zmiennych
_result:=0;
_d0:=date(0,0,0);
{? var_pres('_a')=type_of(SYSLOG) || _KWAL:=_a || return(_result) ?};
_llngt:=8;       'Limit przekroczonych nadgodzin w tygodniu - zmiana z 50% na 100%';
_dt:=_KWAL.DT;   'Data biżąca do sprawdzenia czy w bieżącym tygodniu przekroczono liczbę nadgodzin';
_lng:=0;         'Obliczona liczba nagdodzin';

_KWAL.cntx_psh();
{!
|? _lng+=(*(_KWAL.G55+_KWAL.G56))/60;
   _result:=_lng>_llngt;
   _KWAL.prev() & _KWAL.TYP='R' & _result=0
!};
_KWAL.cntx_pop();
_result


\plan_a_wykonanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc
:: OPIS: Funkcja ustalająca liczbę nadgodzin wypracowanych w bieżącym tygodniu pracy.
::     : WE _a [TABLE] - tabela z kwalifikacjami.
::     : WY
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(2,
   'PRAC','STRING[50]','Pracownik',
   'DT','DATE','Data',
   'P_WE','TIME','Godzina wejścia wg planu',
   'W_WE','TIME','Godzina wejścia wg wykonania',
   'OPIS','STRING[255]','Opis'
);

_od:=date(,,1);   'Pierwszy dzień bieżącego miesiąca w bieżącym roku';
_do:=date();      'Bieżąca data';

:: Ustalenie parametrów do analizy planów i wykonania czasu pracy.
undefine();
define('OD',_od,'Data od','Początek okresu do weryfikacji planu i wykonania',10,11);
define('DO',_do,'Data do','Koniec okresu do weryfikacji planu i wykonania',10,11);
def_btn('text=%1,icon=xwin16.png:13'['OK'@],'key:F2');
def_btn('text=%1,icon=xwin16.png:14'['Anuluj'@],'key:Esc');
_ok:=def_edit(
   "  {? DEFINE.OD>DEFINE.DO
      || FUN.emsg(\'Wartość w polu Data od nie może być większa od wartości w polu Data do.\');
         'OD'
      |? DEFINE.OD=date(0,0,0)
      || FUN.emsg(\'Wartość w polu Data od nie może mieć wartości zerowej.\');
         'OD'
      |? DEFINE.OD~1<>DEFINE.DO~1
      || FUN.emsg(\'Data od i Data do muszą być z tego samego roku.\');
         'OD'
      |? DEFINE.OD~2<>DEFINE.DO~2
      || FUN.emsg(\'Data od i Data do muszą być z tego samego miesiąca.\');
         'OD'
      || 1
      ?}
   "
);
{? ~_ok
|| FUN.emsg('Zrezygnowano z przygotowania zestawienia.');
   return(~~)
?};
_rok:=DEFINE.OD~1;      'rok wg wybranych parametrów';
_msc:=DEFINE.OD~2;      'miesiąc wg wybranych parametrów';
_start:=DEFINE.OD~3;    'pierwszy dzień z okresu wg wybranych parametrów';
_stop:=DEFINE.DO~3;     'ostatni dzień z okresu wg wybranych parametrów';
undefine();


P.cntx_psh();
P.index(P.ndx_tmp(,1,'GRAFIK',,));
P.prefix('T');
{? P.first()
|| OSOBA.cntx_psh();
   KAL_BUFF.cntx_psh();
   KAL_BUFF.index(KAL_BUFF.ndx_tmp(,,'P',,,'GPW',,,'DATA',,));
   {!
   |? {! _dz:=_start.._stop
      |! _dt:=date(_rok,_msc,_dz);     'data do analizy zapisów w planie i wykonaniu';
         _bgd:=time(99,0,0);           'oznaczenie braku zapisu w planie lub wykonaniu';
         _p_we:=_w_we:=_bgd;           'domyślna godzina jeżeli nie zostanie znaleziony zapis w planie i wykonaniu';
         _ogd:=time(24,0,0);           'ostatnia godzina w dobie';
:: Odszukanie zapisów w planie i wykonanu czasu pracy.
         KAL_BUFF.prefix(P.ref(),'P',_dt);
         {? KAL_BUFF.first()
         || _p_we:=KAL_BUFF.POCZATEK
         ?};
         KAL_BUFF.prefix(P.ref(),'Z',_dt);
         {? KAL_BUFF.first()
         || _w_we:=KAL_BUFF.POCZATEK
         ?};
:: Analiza danych i zapisanie różnic do zestawienia.
         {? _p_we<>_w_we
         || {? _p_we=_bgd
            || _opis:='Brak zapisu w planie pacy na dzień %1.'[$_dt]
            |? _w_we=_bgd
            || _opis:='Brak zapisu w wykonaniu czasu pracy na dzień %1.'[$_dt]
            |? _p_we>_w_we
            || _opis:='Godzina rozpoczęcia pacy przed zaplanowaną godziną rozpoczęcia w dniu %1.'[$_dt]
            |? _p_we>_w_we
            || _opis:='Godzina rozpoczęcia pacy po zaplanowanej godzinie rozpoczęcia w dniu %1.'[$_dt]
            || _opis:=''
            ?};
            _TAB.PRAC:='%1 %2, nr teczki %3.'[P.OSOBA().NAZWISKO,OSOBA.PIERWSZE,P.T];
            _TAB.DT:=_dt;
            _TAB.P_WE:=_p_we;
            _TAB.W_WE:=_w_we;
            _TAB.OPIS:=_opis;
            _TAB.add()
         ?}
      !};
      P.next()
   !};
   KAL_BUFF.ndx_drop();
   KAL_BUFF.cntx_pop();
   OSOBA.cntx_pop()
?};
P.cntx_pop();

:: Wyświetlenie danych.
exec('select','#table',_TAB);
~~
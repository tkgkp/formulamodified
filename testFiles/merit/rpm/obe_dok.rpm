:!UTF-8
{TITLE:Dokument w obiegu lub wniosek}
{PRINTER: 'hp3v',,,,,'Q','C',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   {? app_info('web_sesid')<>''
   ||
      exec('set_parw','param_wydr');
      EDOKUM.web_cntx_load()
   ?};
   exec('czytaj','#stalesys');
   {? var_pres('PRN')>0 || obj_del(PRN) ?};
   PRN:=obj_new(@.CLASS.PRINT,10); PRN.sl_frame();
   {? var_pres('PRNV')>0 || obj_del(PRNV) ?};
   PRNV:=obj_new(@.CLASS.PRINT,4); PRNV.sl_frame();
   {? var_pres('PRNS')>0 || obj_del(PRNS) ?};
   PRNS:=obj_new(@.CLASS.PRINT,6); PRNS.sl_frame();
   {? var_pres('PRNATR')>0 || obj_del(PRNATR) ?};
   PRNATR:=obj_new(@.CLASS.PRINT,15); PRNATR.s_frame();
   {? var_pres('STR2',@)<=0 || STR2:=obj_new(@.CLASS.STRING) ?};
   {? var_pres('w')>0 || obj_del(w) ?};
   w:=obj_new(10);
   {? var_pres('v')>0 || obj_del(v) ?};
   v:=obj_new(4);
   {? var_pres('szc')>0 || obj_del(szc) ?};
   szc:=obj_new(6);
   {? var_pres('f')>0 || obj_del(f) ?};
   f:=obj_new(15);
   {! _i:=1..15 |! f[_i] := '' !};
   color:=prn1:=prn2:='PRN'; lm:=ll:=vp:=lmt:=lporz:=druk:=0; rsep:=PAR_WYDR.RSEP;
   head:=PAR_WYDR.HEAD; vp:=1; NOMAXP:=0; __szer1:=__szer2:=0; tekst:='';
   part_a:=0;
   EDOKUMP.use('skidpu'+(EDOKUM.name()+2));
   EDOKUMD.use('bdiety'+(EDOKUM.name()+2));
   EDOK_ZAL.use('edokzl'+(EDOKUM.name()+2));
   EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
   EVAT.use('skid_a'+(EDOKUM.name()+2));
   EPODZ.use('skid_j'+(EDOKUM.name()+2));
   EDOKLOG.use('skid_d'+(EDOKUM.name()+2));
   czy_dob:=(EDOKUM.TYPOBIEG().NAZWA='Obieg faktur') | (EDOKUM.TYPOBIEG().NAZWA='Obieg delegacji');
   czy_vat:=0;
   {? czy_dob
   || EVAT.cntx_psh();
      EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
      czy_vat:=EVAT.first();
      EVAT.cntx_pop()
   ?};
   czy_podz:=0;
   _par:=params_get();
   {? type_of(_par)<>0
   ||
      _showDialog:={? var_pres('show',_par)>0 || _par.show || 1 ?}
   ||
      _showDialog:= 1
   ?};
   {? cli_ver = 'mbasic'
   || dobieg:=1
   || dobieg:=
         {? _showDialog
         || {? czy_dob
            || FUN.ask('Drukować szczegóły obiegu '+
                  {? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur' || 'dokumentu?' || 'delegacji?' ?}
               )
            || FUN.ask('Drukować szczegóły obiegu wniosku?')
            ?}
         ?}
   ?}
}
{END:
   VAR_DEL.delete('PRN','PRNV','PRNS','PRNATR','__T_ATR','w','v','f','NOMAXP','szc','czy_podz');
   VAR_DEL.delete('color','prn1','prn2','lporz','head','tekst','lm','ll','vp','lmt','rsep','__szer1','__szer2','druk');
   VAR_DEL.delete('dobieg','czy_dob','czy_vat','part_a');
   exec('end_msg','dok_fks_zest')
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: obe_dok.rpm
Utworzony:    2009-12-01 [PB]
==================================================
Zawartosc: Wydruk dokumentow w obiegu i wnioskow
{COMMENT-}
{COMMENT}------------------------------MACRO INIT-------------------{COMMENT-}
{MACRO: 'INIT'}
{ EPODZ.cntx_psh();
  EPODZ.index('EDOKUMDS'); EPODZ.prefix(EDOKUM.ref());
  _ok:=0;
  {? EPODZ.first()
  || czy_podz:=1;
     _ost:=EPODZ.SKID_MB;
     {!
     |? _ok:=_ost=EPODZ.SKID_MB;
        _ok & EPODZ.next()
     !}
  ?};
  PRN.add("w[1]",5,'Lp.',1);
  PRN.add("EPODZ.SKID_MB().KOD",16,'Model');
  {? _ok
  || SKID_MBP.index('LP'); SKID_MBP.prefix(EPODZ.SKID_MB);
     {? SKID_MBP.first()
     || {!
        |? PRN.add(($('w['+$(SKID_MBP.LP+1)+']')),16,SKID_MBP.NAZ);
           SKID_MBP.next()
        !}
     ?}
  || PRN.add("w[2]",16,'Pozycja budżetowa'@);
     PRN.add("w[3]",16,'Wymiar 2'@);
     PRN.add("w[4]",16,'Wymiar 3'@);
     PRN.add("w[5]",16,'Wymiar 4'@);
     PRN.add("w[6]",16,'Wymiar 5'@)
  ?};
  EPODZ.cntx_pop();
  PRN.add("w[7]",4,'Wal.'@);
  PRN.add("w[8]",16,'Wartość'@,1);
  PRN.add("w[9]",5,'Jedn.'@);

  PRNV.add("v[1]",16,'Kwota netto'@,1);
  PRNV.add("v[2]",16,'st.VAT'@,1);
  PRNV.add("v[3]",16,'Kwota VAT'@,1);
  PRNV.add("v[4]",16,'Kwota brutto'@,1);

  PRNS.add("szc[1]",10,'Data'@);
  PRNS.add("szc[2]",8,'Godzina'@);
  PRNS.add("szc[3]",20,'Operator'@);
  PRNS.add("szc[4]",15,'Etap'@);
  PRNS.add("szc[5]",35,'Opis'@);
  PRNS.add("szc[6]",45,'Uwagi'@);

  __T_ATR:=proc_exe('edk_atr_sel@obe',$EDOKUM.ref());
  {? __T_ATR.first() || part_a:=1 ?};

  PRNATR.add("f[4]",7,'Rekord'@,1,' ');
  PRNATR.add("f[5]",57,'Opis'@,,' ');
  PRNATR.add("f[6]",68,'Wartość'@,,' ');

  ~~}
{MACRO-}
{COMMENT}---------------------------END MACRO INIT------------------{COMMENT-}
{COMMENT}------------------------------MACRO START------------------{COMMENT-}
{MACRO: 'START'}
{ VAR_DEL.delete('PRN'); PRN:=obj_new(@.CLASS.PRINT,10); PRN.sl_frame();
  VAR_DEL.delete('PRNV'); PRNV:=obj_new(@.CLASS.PRINT,4); PRNV.sl_frame();
  VAR_DEL.delete('PRNS'); PRNS:=obj_new(@.CLASS.PRINT,6); PRNS.sl_frame();
  VAR_DEL.delete('PRNATR'); PRNATR:=obj_new(@.CLASS.PRINT,15); PRNATR.s_frame();
  VAR_DEL.delete('__T_ATR');
  ~~}
{SUBSTITUTE: 'INIT'}
{MACRO-}
{COMMENT}---------------------------END MACRO START-----------------{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_pw2.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}------------------------------MACRO WYDRUK-----------------{COMMENT-}
{MACRO: 'WYDRUK'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:={? czy_dob || {? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur' || 'DOKUMENT' || 'DELEGACJA' ?}+' W OBIEGU - ID: ' || 'WNIOSEK - ID: ' ?}+EDOKUM.ID;~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=0; prn1:=prn2:='PRNV'; ~~}
{<2 ''}
{IF: czy_dob}{SUBSTITUTE: 'DANE'}{ELSE}{SUBSTITUTE: 'DANEW'}{FI}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{LINE}
{FONT: 'T8GB'}
{IF: czy_vat}{SUBSTITUTE: 'TABHEAD'}{FI}{FONT: 'T8G'}{ vp:=1; ~~}
{IF: czy_vat}{SUBSTITUTE: 'VAT'}
{IF:lineleft()<>0&lineleft<=7}{PAGE}{HEADER}{HEADER-}{SUBSTITUTE: 'HEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ELSE}{SUBSTITUTE: 'LINIAF'}{FI}
{FI}
{IF: EDOKUM.TYP().ED_PODZ='T' & czy_podz}
{FONT: 'T8GB'}
{LINE}
{<2 'Podziały dla controllingu:'}
{ lmt:=0; prn1:=prn2:='PRN'; ~~}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{SUBSTITUTE: 'PODZIALY'}
{IF:lineleft()<>0&lineleft()<=9}{PAGE}{HEADER}{HEADER-}{SUBSTITUTE: 'HEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ELSE}{SUBSTITUTE: 'LINIAF'}{LINE}{FI}
{FI}
{IF: lineleft() <> 0 & lineleft()>8}{LINE}{FI}
{IF: EDOKUM.TYPOBIEG().NAZWA<>'Obieg delegacji' & part_a=1}
{<1 'Inne dane:'}{FONT: 'T8G'}
{SUBSTITUTE: 'ATRYBUTY'}
{IF:lineleft()<>0&lineleft()<=9}{PAGE}{HEADER}{HEADER-}{SUBSTITUTE: 'HEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ELSE}{SUBSTITUTE: 'LINIAF'}{LINE}{FI}
{FI}
{ lmt:=0; prn1:=prn2:='PRNS'; ~~}
{IF: dobieg}{SUBSTITUTE: 'STATUS'}{FI}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: dobieg}{SUBSTITUTE: 'LINIAF'}{FI}
{~~}
{FOOTER-}
{FONT: 'T8G'}
{MACRO-}
{COMMENT}--------------------------END MACRO WYDRUK-----------------{COMMENT-}
{COMMENT}------------------------------MACRO STATUS-----------------{COMMENT-}
{MACRO: 'STATUS'}
{FONT: 'T8GB'}
{<2 {? czy_dob || 'Szczegóły obiegu '+{? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur' || 'dokumentu:' || 'delegacji:' ?} || 'Szczegóły wniosku:' ?}}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; prn1:=prn2:='PRNS';~~}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FOR: 'EDOKLOG'}
{INDEX: 'DISP'}
{PREFIX: EDOKUM.ref()}
{DO}
{ szc[1]:=$EDOKLOG.DATA;
  szc[2]:=$EDOKLOG.TIME;
  USERS.cntx_psh(); OSOBA.cntx_psh();
  szc[3]:=EDOKLOG.USERS().OSOBA().NAZWISKO+' '+EDOKLOG.USERS().OSOBA().PIERWSZE+' ('+EDOKLOG.USERS().KOD+')';
  USERS.cntx_pop(); OSOBA.cntx_pop();
  szc[4]:=EDOKLOG.B_PRELS;
  szc[5]:=EDOKLOG.OPIS;
  szc[6]:=EDOKLOG.UW_KR;
  ~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{MACRO-}
{COMMENT}--------------------------END MACRO STATUS-----------------{COMMENT-}
{COMMENT}------------------------------MACRO DANE-------------------{COMMENT-}
{MACRO: 'DANE'}
{LINE}
{FONT: 'T8GB'}
{<2 'Dane '+{? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur' || 'dokumentu' || 'delegacji' ?}+' w obiegu:'}
{FONT: 'T8G'}
{ __szer1:=5; __szer2:=__szer1+{? EDOKUM.SYM_KOR<>'' || 31 |? EDOKUM.OPIS_PRZ<>null || 26 || 21 ?};~~}
{<{__szer1} 'Jednostka księgowa: '}{<{__szer2} EDOKUM.ODD().OD}
{<{__szer1} 'Typ '+{? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur' || 'dokumentu' || 'delegacji' ?}+': '} {<{__szer2} EDOKUM.TYP().NAZWA}
{IF: EDOKUM.TYPOBIEG().NAZWA='Obieg delegacji'}
{<{__szer1} 'Identyfikator: '}{FONT: 'T8GB'}{<{__szer2} EDOKUM.ID}{FONT: 'T8G'}
{ELSE}
{<{__szer1} 'Symbol dokumentu: '}{FONT: 'T8GB'}{<{__szer2} EDOKUM.SYM}{FONT: 'T8G'}
{FI}
{IF: EDOKUM.SYM_KOR<>''}
{<{__szer1} 'Symbol dokumentu korygowanego: '}{<{__szer2} EDOKUM.SYM_KOR}
{FI}
{IF: EDOKUM.TYPOBIEG().NAZWA='Obieg delegacji'}
{<{__szer1} 'Data polecenia wyj.: '}{<{__szer2} $EDOKUM.DW}
{<{__szer1} 'Data od: '}{<{__szer2} $EDOKUM.DATA_OD}
{<{__szer1} 'Data do: '}{<{__szer2} $EDOKUM.DATA_DO}
{ELSE}
{<{__szer1} 'Data sprzedaży: '}{<{__szer2} $EDOKUM.DOP}
{<{__szer1} 'Data wystawienia: '}{<{__szer2} $EDOKUM.DW}
{<{__szer1} 'Data otrzymania: '}{<{__szer2} $EDOKUM.DO}
{FI}
{LINE}
{IF: EDOKUM.TYPOBIEG().NAZWA='Obieg delegacji'}
{<{__szer1} 'Miejsce delegacji: '}{<{__szer2} EDOKUM.DEL_MIE}
{<{__szer1} 'Cel delegacji: '}{<{__szer2} EDOKUM.DEL_CEL().TR}
{ELSE}
{<{__szer1} 'Waluta: '}{<{__szer2} EDOKUM.WAL().KOD}
{<{__szer1} 'Netto: '}{>{__szer2+18} form(EDOKUM.NETTO,,2,' ,')}
{<{__szer1} 'Brutto: '}{>{__szer2+18} form(EDOKUM.WART,,2,' ,')}
{FI}
{LINE}
{<{__szer1} 'Kontrahent: '}{<{__szer2} EDOKUM.KH().KOD+' - '+EDOKUM.KH().TR}
{IF: EDOKUM.HAN<>null}
{<{__szer1} 'Handlowiec: '}{<{__szer2} EDOKUM.HAN().KOD+' - '+EDOKUM.HAN().NAZ}
{FI}
{IF: EDOKUM.TYPOBIEG().NAZWA='Obieg delegacji'}
{<{__szer1} 'Delegowany: '}{<{__szer2} EDOKUM.DOSTAWCA().NAZWISKO+' '+EDOKUM.DOSTAWCA().PIERWSZE}
{<{__szer1} 'Opis: '}{<{__szer2} EDOKUM.TR}
{ELSE}
{<{__szer1} 'Forma płatności: '}{<{__szer2} EDOKUM.PLATNOSC().KOD+' - '+EDOKUM.PLATNOSC().TR}
{<{__szer1} 'Termin płatności: '}{<{__szer2} $EDOKUM.TP}
{<{__szer1} 'Dostarczył: '}{<{__szer2} EDOKUM.DOSTAWCA().NAZWISKO+' '+EDOKUM.DOSTAWCA().PIERWSZE}
{<{__szer1} 'Opis operacji: '}{<{__szer2} EDOKUM.TR}
{FI}
{IF: EDOKUM.PRZEKAZ='T'}
{<{__szer1} 'Dokument papierowy został przekazany działowi księgowości.'}
{IF: EDOKUM.OPIS_PRZ<>null}
{<{__szer1} 'Opis przekazania działowi księgowości: '+EDOKUM.OPIS_PRZ().OPIS}
{FI}
{FI}
{IF: EDOKUM.EDOKUM}
{<{__szer1} 'E-dokument dostępny.'}
{ELSE}
{IF:  EDOKUM.TYPOBIEG().NAZWA<>'Obieg delegacji'}
{<{__szer1} 'Brak e-dokumentu.'}
{FI}
{FI}
{MACRO-}
{COMMENT}--------------------------END MACRO DANE-------------------{COMMENT-}
{COMMENT}------------------------------MACRO DANEW-------------------{COMMENT-}
{MACRO: 'DANEW'}
{LINE}
{FONT: 'T8GB'}
{<2 'Dane wniosku:'}
{FONT: 'T8G'}
{ __szer1:=5; __szer2:=__szer1+21;~~}
{<{__szer1} 'Jednostka księgowa: '}{<{__szer2} EDOKUM.ODD().OD}
{<{__szer1} 'Typ wniosku: '} {<{__szer2} EDOKUM.TYP().NAZWA}
{FONT: 'T8G'}
{<{__szer1} 'Data operacji: '}{<{__szer2} $EDOKUM.DOP}
{IF: EDOKUM.TYP().CZY_WAR}
{<{__szer1} 'Waluta: '}{<{__szer2} EDOKUM.WAL().KOD}
{IF: czy_dob}{<{__szer1} 'Netto: '}{>{__szer2+18} form(EDOKUM.NETTO,,2,' ,')}{FI}
{<{__szer1} 'Brutto: '}{>{__szer2+18} form(EDOKUM.WART,,2,' ,')}
{FI}
{IF: EDOKUM.TYP().CZY_KH}
{<{__szer1} 'Kontrahent: '}{<{__szer2} EDOKUM.KH().KOD+' - '+EDOKUM.KH().TR}
{FI}
{<{__szer1} 'Pracownik: '}{<{__szer2} EDOKUM.DOSTAWCA().NAZWISKO+' '+EDOKUM.DOSTAWCA().PIERWSZE}
{<{__szer1} 'Temat: '}{<{__szer2} EDOKUM.TR}
{ _temp:=EDOKUM.memo_txt(0,1,'UW_OPDL'); STR.split(_temp,'\n'); ~~}
{WHILE: STR.next()}
{DO}
{<{__szer1}}{STR.get_word()}
{OD}
{LINE: 1}
{IF: EDOKUM.EDOKUM}
{<{__szer1} 'E-dokument dostępny.'}
{ELSE}
{<{__szer1} 'Brak e-dokumentu.'}
{FI}
{MACRO-}
{COMMENT}--------------------------END MACRO DANE-------------------{COMMENT-}
{COMMENT}------------------------------MACRO VAT--------------------{COMMENT-}
{MACRO: 'VAT'}
{IF: czy_dob}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; prn1:=prn2:='PRNV';~~}
{FOR: 'EVAT'}
{INDEX: 'EDOKUM'}
{PREFIX: EDOKUM.ref()}
{DO}
{ v[1]:=form(EVAT.NETTO,,2,' ,');
  v[2]:=EVAT.PR().KOD;
  v[3]:=form(EVAT.VAT,,2,' ,');
  v[4]:=form(EVAT.BRUTTO,,2,' ,');
  ~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{FI}
{MACRO-}
{COMMENT}--------------------------END MACRO VAT--------------------{COMMENT-}
{COMMENT}------------------------------MACRO PODZIALY---------------{COMMENT-}
{MACRO: 'PODZIALY'}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; lporz:=0; prn1:=prn2:='PRN';~~}
{FOR: 'EPODZ'}
{INDEX: 'EDOKUMDS'}
{PREFIX: EDOKUM.ref()}
{DO}
{ lporz+=1;
  w[1]:=lporz;
  w[2]:=EPODZ.PBUD().SYMBOL;
  w[3]:=EPODZ.JORG().SYMBOL;
  w[4]:=EPODZ.OKOSZ().SYMBOL;
  w[5]:=EPODZ.WYM4().SYMBOL;
  w[6]:=EPODZ.WYM5().SYMBOL;
  w[7]:={? EPODZ.WAL<>null || EPODZ.WAL().KOD || '' ?};
  w[8]:=form(EPODZ.WART,,{? EPODZ.JM<>null || 6 || 2 ?},' ,');
  w[9]:=EPODZ.JM().KOD;
  ~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{MACRO-}
{COMMENT} ---------------------------- atrybuty -------------------- {COMMENT-}
{MACRO: 'ATRYBUTY'}
{IF: part_a=1}
{ prn1:=prn2:='PRNATR';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: lineleft() <> 0 & lineleft()<=6}{PAGE}
{ELSE}{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{ rsep:=1; par5:=''; ~~}
{FOR: __T_ATR}
 {DO}
{IF: __T_ATR.WARTOSC<>'' | __T_ATR.TAT_OPIS<>'' }
   {
         STR.split(__T_ATR.WARTOSC,'\n');
         STR2.split(__T_ATR.TAT_OPIS);
         {? par5<>$__T_ATR.REKORD || f[4]:=$__T_ATR.REKORD || f[4]:='' ?};
         par5:=$__T_ATR.REKORD;
         f[5]:=STR2.line(57);
         f[6]:=STR.get_word();
         {? +f[6]>68 || STR.split(__T_ATR.WARTOSC); f[6]:=STR.line(68)  ?};
         STR.split((+f[6]+1)-__T_ATR.WARTOSC,'\n')
   ;~~}
   {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
   {SUBSTITUTE: 'LINIA0'}
{f[4]:='';   f[5]:=STR2.line(57);
 druk:=1;~~}
{IF: druk}
{WHILE: druk}
{DO}
 {f[6]:=STR.get_word();~~}
{SUBSTITUTE: 'LINIA03'}
{druk:=STR.next(); f[5]:='';~~}
{OD}
{ELSE}
{IF: f[5]<>''} {SUBSTITUTE: 'LINIA03'} {FI}
{FI}
 {FI}
 {OD}
{FI}
{MACRO-}
{COMMENT} ------------------------- END atrybuty ---------------------- {COMMENT-}
{COMMENT}--------------------------END MACRO PODZIALY-------------------{COMMENT-}
{IF: var_pres('__EDOKUM')>100}
   {SUBSTITUTE: 'START'}
   {NOMAXP:=1;~~}
   {FOR: '__EDOKUM'}
   {DO}
      {IF: EDOKUM.seek(__EDOKUM.REF,EDOKUM.name())}
         {SUBSTITUTE: 'START'}
         {SUBSTITUTE: 'WYDRUK'}{l_druk+=1;~~}
         {IF: __EDOKUM.next()}{HEADER}{HEADER-}{IF: lineleft()<>0}{PAGE}{FI}{page(1);~~}{FI}
      {FI}
   {OD}
   {czy_druk:=1;~~}
{ELSE}
   {SUBSTITUTE: 'INIT'}
   {SUBSTITUTE: 'WYDRUK'}
{FI}
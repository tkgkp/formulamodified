:!UTF-8
{TITLE:B. Stan magazynu wg cen}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('sm_02');
   sm_02:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('dokl','dokl_c');
      VAR_DEL.delete('k','w','s','sg','PRN','kgr');
      VAR_DEL.delete('wyjscie','X_Xa','X_Xb','X_Xc')
   ";
   sm_02();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   k:=obj_new(7); "--szerokosci kolumn--";
   w:=obj_new(7); "--wartosci kolumn--";
   s:=0; "--razem--";
   sg:=0; "--razem grupa--";
   dokl:=0; "--dokladnosc ilosci--";
   dokl_c:=2; "--dokladnosc ceny--";
   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   tryb:=rep_export();

   k[1]:= 20;
   k[2]:= 35;
   k[3]:= 5;
   k[4]:= 10;
   k[5]:=k[6] :=k[7] := 18;
   w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:='';
   kgr:='';
   {? ST.MAG().IL='T'
   || FUN.info('Zestawienie niedostępne w magazynie z ewidencją wyłącznie ilościową.'@);
      wyjscie:=1
   ||
      {? (_wyb:=FUN.choice('Czy filtrować kartotekę materiałową?'@,,'Tak'@,'Nie'@))>0
      ||
         wyjscie:=0;
         {? _wyb=1 || HELP.FM:='T' || HELP.FM:='N' ?}
      ||
         wyjscie:=1
      ?};
      {? wyjscie=0 || wyjscie:=exec('filtr_m','material',HELP.FM,'N','T',1) ?}
   ?}
}
{END:
   sm_02(); VAR_DEL.delete('sm_02')
}
{IF: wyjscie=1}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
PRN.add("w[1]",k[1],'Indeks'@);
PRN.add("w[2]",k[2],'Nazwa materiału'@);
PRN.add("w[3]",k[3],'jm'@);
PRN.add("w[4]",k[4],'Data'@);
{? exec('upr_cm','ceny') || PRN.add("w[5]",k[5],'Cena'@,1) ?};
PRN.add("w[6]",k[6],'Stan ilościowy'@,1);
{? exec('upr_cm','ceny') || PRN.add("w[7]",k[7],'Stan wartościowy'@,1) ?};

VAR_DEL.delete('X_Xc');
X_Xc:=tab_tmp(1,
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'N'      ,'STRING[100]'  ,'Nazwa',
   'JM'     ,'STRING[5]'   ,'jm',
   'D'      ,'DATE'        ,'Data',
   'C'      ,'REAL'        ,'Cena',
   'S'      ,'REAL'        ,'Stan',
   'W'      ,'REAL'        ,'Stan wartościowy',
   'MGR'    ,'STRING[8]'   ,'Grupa materiałowa');

"--obliczenia--";
_sr:='Ś'=(1+ST.MAG().TYP);
{? X_Xa.first()
||
   X_Xb:=sql('
      select SC.M M, SC.S S, SC.C C, SC.D D, SC.RDK RDK, SC.NDK NDK
      from @SC join M using(SC.M,M.REFERENCE)
      where SC.A=\'T\' and SC.MAG=:_a',ST.MAG);
   X_Xb.index(X_Xb.ndx_tmp(,,'M',,));
   M.cntx_psh();
   M.prefix();
   {!
   |? M.seek(BB.sqlint(X_Xa.REF),);
      echo('Trwają obliczenia dla materiału: '@+M.KTM+' '+M.N);
      X_Xb.prefix(X_Xa.REF);
      {? X_Xb.first()
      ||
         {!
         |? X_Xc.KTM:=M.KTM;
            X_Xc.N:=M.N;
            X_Xc.JM:=M.J().KOD;
            X_Xc.D:=X_Xb.D;
            X_Xc.MGR:=M.MGR().KOD;
            {? _sr=0
            || _cena:=exec('cena_mag','ceny',M.ref,X_Xb.RDK,X_Xb.NDK,ST.MAG,X_Xb.C)
            || SM.cntx_psh();
               SM.index('SA'); SM.prefix(ST.MAG,X_Xa.KTM,X_Xa.KTM);
               _cena:={? SM.first() || {? SM.ST_IL>0 || (SM.ST_WAR/SM.ST_IL)$2 || 0 ?} || 0 ?};
               SM.cntx_pop()
            ?};
            X_Xc.C:=_cena;
            X_Xc.S:=X_Xb.S;
            X_Xc.W:=(X_Xc.C*X_Xb.S)$2;
            X_Xc.add();
            X_Xb.next()
         !}
      ?};
      X_Xa.next()
   !};
   M.cntx_pop();
   echo()
?};

PAR_WYDR.TITLE:='ZESTAWIENIE STANÓW MAGAZYNOWYCH WEDŁUG CEN'@;
prn1:='PRN'
;~~}
{IF: X_Xc.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział '@+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{<{lmt+1}}{'Dla magazynu: '@+ST.MAG().SYM}
{IF: HELP.FM='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
{<{lmt+1}}{'Stan na: '@+$date()+', '+$time()}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xc}
{DO}
   {FIRST}
   {
   dokl:=exec('fld_prec','#field',X_Xc,'S');
   dokl_c:=exec('fld_prec','#field',X_Xc,'C');
   kolejny:=0;~~}
   {FIRST-}
   {IF: kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {
   w[1]:=form(X_Xc.KTM);
   w[2]:=form(X_Xc.N);
   w[3]:=form(X_Xc.JM);
   w[4]:=form(X_Xc.D);
   w[5]:=form(X_Xc.C,,dokl_c,'.,');
   w[6]:=form(X_Xc.S,,dokl,'.,');
   w[7]:=form(X_Xc.W,,2,'.,');
   kgr :=form(X_Xc.MGR);
   sg+=X_Xc.W;
   s+=X_Xc.W;
   kolejny:=0
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
   {rsep:=PAR_WYDR.RSEP;~~}
   {TOTAL: X_Xc.KTM}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
      {IF: tryb<>1}
      {
      w[1] :=form(X_Xc.KTM);
      w[2] :=form(X_Xc.N);
      w[3] :=form(X_Xc.JM);
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+3
      ;~~}
      {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {
      kolejny:=1; rsep:=0;
      PRN.n_frame();
      w[1]:='';
      w[2]:='Razem '@+grp_fld()+':'; PRN.FORM[2]:=0;
      w[3]:=w[4] :=w[5] :=w[6] :='';
      w[7]:=form(sg,,2,'.,')
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {FONT: 'T8G'}{SPACE: 'C'}
      {
      sg:=0;
      PRN.s_frame();
      PRN.FORM[2]:=1
      ;~~}
      {FI}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{IF: tryb<>1}
{IF: exec('upr_cm','ceny')}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {FOOTER}{FOOTER-}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {
   PRN.n_frame();
   w[1]:='';
   w[2]:='Razem:'@; PRN.FORM[2]:=0;
   w[3]:=w[4] :=w[5] :=w[6] :='';
   w[7]:=form(s,,2,'.,')
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{ELSE}
   {FOOTER}{FOOTER-}
{FI}
{FI}
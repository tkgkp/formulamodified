:!UTF-8
{TITLE:B. Dziennik zbiorczy}
{PRINTER: FINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:  i:=1;
   par_okr:=par_odd:=par_wal:=mdc:='';
   dz:=obj_new(8);
   dzz:=obj_new(4);
   z:=obj_new(12); suma:=obj_new(6); razem:=obj_new(6);
   prn1:='PRN';rokBilNr:='';okresNr:='';rejRef:=1;a:=0;   j:=lm:=0;
   okresOd:=okresDo:=0;
   PRN:=obj_new(@.CLASS.PRINT,8);
   PRN.sl_frame();
   PRN1:=obj_new(@.CLASS.PRINT,8);
   PRN1.sl_frame(); vp:=1;
   UNPAR.P7:=1;
   UNPAR.P7_BE:='';
   UNPAR.P7_AE:='';
   ROZNE.UT_OKROD:=SSTALE.AO;
   ROZNE.UT_OKRDO:=SSTALE.AO;
   ROZNE.win_edit('DZIENNI2'@)
}
{END:
   obj_del(PRN); obj_del(PRN1); obj_del(dz);  obj_del(z); obj_del(suma);
   obj_del(razem);
   obj_del(dzz);
   &par_okr; &par_odd; &par_wal; &mdc; &lm; &prn1; &okresOd; &okresDo;
   &i; &rokBilNr; &okresNr; &rejRef; &a;  &j; &vp
}
{EXIT: ~ROZNE.edit("exec('spr_par_wydruk','dok_fks1')")}
{  {? OPERATOR.DEPT=null
   || {? rep_export
      || PRN.add("dz[8]",10,'Jednostka księgowa'@)
      || PRN.add("dz[8]",10,'Jednostka# księgowa'@,,'#')
      ?}
   ?};
   PRN.add("dz[1]",8,'  Rejestr'@);
   PRN.add("dz[2]",18,'     Winien'@,1);
   PRN.add("dz[3]",18,'        Ma'@,1);
   PRN.add("dz[4]",18,'     Winien'@,1);
   PRN.add("dz[5]",18,'        Ma'@,1);
   PRN.add("dz[6]",18,'     Winien'@,1);
   PRN.add("dz[7]",18,'        Ma'@,1);
   _brakJedKsi:=OPERATOR.DEPT=null;
   {? rep_export
   || {? _brakJedKsi || PRN1.add("",37,'') ?};
      PRN1.add("dzz[1]",8+11*_brakJedKsi,'');
      PRN1.add("dzz[2]",37,'     Obroty poprzednich okresów'@);
      PRN1.add("",37,'');
      {? ROZNE.UT_OKROD<>ROZNE.UT_OKRDO
      || PRN1.add("dzz[3]",37,'      Obroty wybranych okresów'@)
      || PRN1.add("dzz[3]",37,'      Obroty wybranego okresu'@)
      ?};
      PRN1.add("",37,'');
      PRN1.add("dzz[4]",37,' Obroty narastająco od początku roku'@);
      PRN1.add("",37,'')
   ||
      PRN1.add("dzz[1]",8+11*_brakJedKsi,'');
      PRN1.add("dzz[2]",37,'     Obroty poprzednich okresów'@);
      {? ROZNE.UT_OKROD<>ROZNE.UT_OKRDO
      || PRN1.add("dzz[3]",37,'      Obroty wybranych okresów'@)
      || PRN1.add("dzz[3]",37,'      Obroty wybranego okresu'@)
      ?};
      PRN1.add("dzz[4]",37,' Obroty narastająco od początku roku'@)
   ?};
   {? ROZNE.UT_OKROD<>ROZNE.UT_OKRDO
   || par_okr:='Okresy obrachunkowe: %1 - %2 %3'@[ROZNE.UT_OKROD().NAZ,ROZNE.UT_OKRDO().NAZ,SSTALE.AO().ROK().NAZ]
   || par_okr:='Okres obrachunkowy: %1 %2'@[ROZNE.UT_OKROD().NAZ,SSTALE.AO().ROK().NAZ];
      {? SSTALE.AO().ZAM='T'
         || par_okr+=' - okres zamknięty'@
         || par_okr+=' - okres otwarty'@
      ?}
   ?};
   {? OPERATOR.DEPT
   || par_odd:='Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
   || par_odd:='Wszystkie jednostki księgowe'@
   ?};
   par_wal:='Waluta: %1 - %2'@[FINFO.NAROD().KOD,FINFO.NAROD().TR]; ~~}
{INCLUDE: wsp_pw3.rpi}
{INCLUDE: wsp_pw.rpi}
{COMMENT}-----------------------------------------------------------------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'czolo'}
{FONT: 'T8GB'}{SPACE: 'C'}
{PRN.setcolor(color); PRN1.setcolor(color);~~}
{REP: PRN1.fhbar()}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}{DO}
{REP: PRN1.h_fmline()}
{OD}
{PRN.ini_head()}
{REP: PRN1.fjbar(PRN)}
{WHILE: PRN.h_next()}
{DO}
{REP: PRN.h_fmline()}
{OD}
{REP: PRN.fbar()}
{FONT: 'T8G'}{SPACE: 'C'}
{PRN.setcolor('255:255:255'); PRN1.setcolor('255:255:255');~~}
{HEADER-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}-----------------------------------------------------------------------------{COMMENT-}
{MACRO:'czolo'}
{PAR_WYDR.TITLE:='DZIENNIK ZBIORCZY '@;~~}
{SUBSTITUTE: 'HEAD'}
{IF: page=1}
{FONT: 'T8G'}{SPACE: 'C'}
{LINE: 1}
{<1 'PARAMETRY WYDRUKU:'@}
{<1 par_okr}
{<1 par_wal}
{<1 'Zakres danych: Obroty na kontach bilansowych/wynikowych'@ }
{<1 par_odd}
{<1
   {? UNPAR.P7
      || 'Bilans otwarcia - liczone saldo: tak'@
      || 'Bilans otwarcia - liczone saldo: nie'@
   ?}
}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}
{MACRO-}
{COMMENT}----------------------------------------------------------------------------{COMMENT-}
{MACRO: 'linia'}
{FONT: 'T8G'}{SPACE: 'C'}
{ENTIRE}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}
{IF: vp & lineleft()<>1}
{REP: PRN.fbar()}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}-----------------------------MACRO-obrpop-----------------------------------{COMMENT-}
{MACRO: 'obrpop'}
{ DZIENNIK.OBWN:=DZIENNIK.OBMA:=DZIENNIK.OBWNPB:=DZIENNIK.OBMAPB:=0;
  okres:=ROZNE.UT_OKROD().NR;
  OKRO_F.cntx_psh();
  OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
  {? OKRO_F.first() & OKRO_F.NR<okres
  || {! |?
     rokBilNr:=OKRO_F.ROK().KOD;
     okresNr:=form(OKRO_F.NR,-2);
     {? ~OPERATOR.DEPT
     || ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
        {? ODD.first()
        || {!
           |? exec('obr_mc','!fks_ksr_zddz',rokBilNr,okresNr,rejRef,ODD.ref());
              ODD.next()
           !}
        ?};
        ODD.cntx_pop()
     || exec('obr_mc','!fks_ksr_zddz',rokBilNr,okresNr,rejRef,OPERATOR.DEPT)
     ?};
     OKRO_F.next();
     {? OKRO_F.NR=okres || _a:=0 || _a:=1 ?};
     _a
     !}
  ?};
  OKRO_F.cntx_pop();
  z[1]:=DZIENNIK.OBWN; suma[1]+=z[1];
  z[2]:=DZIENNIK.OBMA; suma[2]+=z[2];
  z[3]:=DZIENNIK.OBWNPB;
  z[4]:=DZIENNIK.OBMAPB; ~~}
{MACRO-}
{COMMENT}----------------------------------------------------------------------------{COMMENT-}
{MACRO: 'obrakt'}
{ DZIENNIK.OBWN:=DZIENNIK.OBMA:=DZIENNIK.OBWNPB:=DZIENNIK.OBMAPB:=0;
  rokBilNr:=OKRO_F.ROK().KOD;
  okresOd:=ROZNE.UT_OKROD().NR;
  okresDo:=ROZNE.UT_OKRDO().NR;
  {! okres:=okresOd .. okresDo
  |! okresNr:=form(okres,-2);
     {? ~OPERATOR.DEPT
     || ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
        {? ODD.first()
        || {! |?
              exec('obr_mc','!fks_ksr_zddz',rokBilNr,okresNr,rejRef,ODD.ref());
              ODD.next()
           !}
        ?};
        ODD.cntx_pop()
    || exec('obr_mc','!fks_ksr_zddz',rokBilNr,okresNr,rejRef,OPERATOR.DEPT)
    ?}
  !};
 z[5]:=DZIENNIK.OBWN; suma[3]+=z[5];
 z[6]:=DZIENNIK.OBMA; suma[4]+=z[6];
 z[7]:=DZIENNIK.OBWNPB;
 z[8]:=DZIENNIK.OBMAPB;~~}
{MACRO-}
{COMMENT}----------------------------------------------------------------------------{COMMENT-}
{MACRO: 'obrrok'}
{ DZIENNIK.OBWN:=DZIENNIK.OBMA:=DZIENNIK.OBWNPB:=DZIENNIK.OBMAPB:=0;
  {? ~OPERATOR.DEPT
  || ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
     {? ODD.first()
     || {! |?
           exec('obr_rok','!fks_ksr_zddz',rejRef,ODD.ref());
           ODD.next()
        !}
     ?};
     ODD.cntx_pop()
  || exec('obr_rok','!fks_ksr_zddz',rejRef,OPERATOR.DEPT)
  ?};
  z[9]:=DZIENNIK.OBWN;  suma[5]+=z[9];
  z[10]:=DZIENNIK.OBMA; suma[6]+=z[10];
  z[11]:=DZIENNIK.OBWNPB;
  z[12]:=DZIENNIK.OBMAPB;~~}
{MACRO-}
{COMMENT}----------------------------------------------------------------------------{COMMENT-}
{MACRO: 'obroty'}
{ dz[1]:=REJ.KOD;  {! i:=1..12|! z[i]:=0 !};~~}
{IF: ROZNE.UT_OKROD().NR<>0}
{SUBSTITUTE: 'obrpop'}
{FI}
{SUBSTITUTE: 'obrakt'}
{SUBSTITUTE: 'obrrok'}
{ dz[2]:=form(z[1],18,2,' ,');dz[3]:=form(z[2],18,2,' ,');
  dz[4]:=form(z[5],18,2,' ,');dz[5]:=form(z[6],18,2,' ,');
  dz[6]:=form(z[9],18,2,' ,');dz[7]:=form(z[10],18,2,' ,');
  dz[8]:=REJ.ODD().OD;
~~}
{SUBSTITUTE: 'linia'}
{MACRO-}
{COMMENT}----------------------------------------------------------------------------{COMMENT-}
{MACRO: 'razem'}
{ dz[8]:=''; {! i:=2..7|! razem[i-1]+=suma[i-1]; dz[i]:=form(suma[i-1],15,2,' ,'); suma[i-1]:=0!};
  dz[1]:='RAZEM';  {! i:=2..7|! dz[i]:=form(razem[i-1],18,2,' ,') !}; vp:=0;~~}
{SUBSTITUTE: 'linia'}
{MACRO-}
{COMMENT}----------------------------------------------------------------------------{COMMENT-}
{FOOTER:0}
{REP: PRN.flbar()}
{SUBSTITUTE: 'CHARFOOT'}
{FONT: 'T8G'}{SPACE: 'C'}{  prn:=1; ~~}
{FOOTER-}
{COMMENT}----------------------------------------------------------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ {! i:=1..7 |! dz[i]:=''!};j:=0;~~}
{ {! i:=1..6 |! suma[i]:=razem[i]:=0 !};~~}
{IF: OPERATOR.DEPT}
   {FOR: 'REJ'}{INDEX:'KOD'}{PREFIX: SSTALE.AR,OPERATOR.DEPT}
   {DO}
      { rejRef:=REJ.ref();~~}
      {SUBSTITUTE: 'obroty'}
   {OD}
{ELSE}
   {FOR: 'REJ'}{INDEX:'KOD'}{PREFIX: SSTALE.AR}
   {DO}
      { rejRef:=REJ.ref();~~}
      {SUBSTITUTE: 'obroty'}
   {OD}
{FI}
{IF: lineleft()<4 & lineleft()<>0}{PAGE}{FI}
{IF: ~rep_export}
{SUBSTITUTE: 'razem'}
{REP: PRN.flbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU '@}
{ELSE}
{REP: PRN.flbar()}
{FI}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{FONT: 'Q'}{SPACE: 'C'}{  prn:=1; ~~}
{FOOTER-}

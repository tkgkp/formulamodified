:!UTF-8
{TITLE:Pełne zestawienie składników - okres}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','b_rat','h_rat','__edit','__param','__okres','__tab');
   PAR_WYDR.TITLE:='Pełne zestawienie składników'@;
   P.cntx_psh();
   par:=obj_new(19);
   par[7]:=0;
   par[15]:=0;
   par[16]:=0;
   par[17]:=0;
   par[18]:=0;
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'TYP_ROZ','INTEGER','Typ rozliczenia'@,
         'ROK_OD','INTEGER','Rok od'@,
         'MIES_OD','INTEGER','Miesiąc od'@,
         'ROK_DO','INTEGER','Rok do'@,
         'MIES_DO','INTEGER','Miesiąc do'@,
         'DRUP','INTEGER','Drukować parametry sprawozdania'@,
         'RMIN','INTEGER' ,'',
         'RMAX','INTEGER' ,''
      );
      __param.ROK_OD:=O.R;
      __param.MIES_OD:=O.M;
      __param.ROK_DO:=O.R;
      __param.MIES_DO:=O.M;
      __param.DRUP:=0;
      __param.TYP_ROZ:=0;
      __param.RMIN:=0;
      __param.RMAX:=0;

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_plistrokzak');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'TYP_ROZ',,,,,,,,,'radio-buttons',,
         'Kosztowy'@,"0",
         'Podatkowy'@,"1",
         'Ubezpieczeniowy'@,"2"
      );
      __param.win_efld(__edit,,'ROK_OD',,,4,,,,,'Rok od'@);
      __param.win_efld(__edit,,'MIES_OD',,,2,,,,,'Miesiąc od'@);
      __param.win_efld(__edit,,'ROK_DO',,,4,,,,,'Rok do'@);
      __param.win_efld(__edit,,'MIES_DO',,,2,,,,,'Miesiąc do'@);
      __param.win_efld(__edit,,'DRUP',,,,,,,,,'radio-buttons',,
         'na pierwszej stronie'@,"0",
         'na każdej stronie'@,"1"
      );
      __param.efld_opt(__edit,'mark=1',,'TYP_ROZ');
      __param.efld_opt(__edit,'mark=1',,'ROK_OD');
      __param.efld_opt(__edit,'mark=1',,'MIES_OD');
      __param.efld_opt(__edit,'mark=1',,'ROK_DO');
      __param.efld_opt(__edit,'mark=1',,'MIES_DO');
      __param.efld_opt(__edit,'enable='+$(~rep_export()),,'DRUP');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[7]:=__param.edit(
            "  _znak:='A';
               {? __param.TYP_ROZ=1
               || _znak:='P'
               |? __param.TYP_ROZ=2
               || _znak:='U'
               ?};
               O.cntx_psh();
               O.index('LISTYPL'+_znak);
               O.prefix(exec('ref_firma','ustawienia'),__F_ZATR.O,);
               __param.RMIN:={? O.first()
                             || {? __param.TYP_ROZ=0
                                || O.R
                                |? __param.TYP_ROZ=1
                                || O.RP
                                || O.RU
                                ?}
                             ?};
               __param.RMAX:={? O.last()
                             || {? __param.TYP_ROZ=0
                                || O.R
                                |? __param.TYP_ROZ=1
                                || O.RP
                                || O.RU
                                ?}
                             ?};
               O.cntx_pop();
               {? (__param.ROK_OD<__param.RMIN | __param.RMAX<__param.ROK_OD)
               || __CHK.err_fld(
                     __param,'ROK_OD',1,'Dozwolone wartości z przedziału %1-%2.'@ [$__param.RMIN,$__param.RMAX]
                  )
               |? __param.ROK_DO>__param.RMAX | __param.ROK_DO<__param.ROK_OD
               || __CHK.err_fld(
                     __param,'ROK_DO',1,'Dozwolone wartości z przedziału %1-%2.'@ [$__param.RMIN,$__param.RMAX]
                  )
               |? __param.ROK_OD=__param.ROK_DO & __param.MIES_DO<__param.MIES_OD
               ||  __CHK.err_fld(__param,'MIES_DO',1,'Wybrana data do nie może być mniejsza niż data od.'@)
               |? __param.MIES_OD<1 | __param.MIES_OD>12
               || __CHK.err_fld(__param,'MIES_OD',1,'Dozwolone wartości z przedziału 1-12.'@)
               |? __param.MIES_DO<1 | __param.MIES_DO>12
               || __CHK.err_fld(__param,'MIES_DO',1,'Dozwolone wartości z przedziału 1-12.'@)
               || {? ~rep_export()
                  || _tab:=exec('tab_month','ppl',__param.ROK_OD,__param.MIES_OD,__param.ROK_DO,__param.MIES_DO);
                     {? _tab.size()>12
                     || FUN.emsg('Dla wydruku papierowego podany okres nie może być większy niż 12 miesięcy.'@)
                     || ''
                     ?}
                  || ''
                  ?}
               ?}

            "
         )
      || __tab:=exec('tab_month','ppl',__param.ROK_OD,__param.MIES_OD,__param.ROK_DO,__param.MIES_DO);
         par[15]:=__param.ROK_OD;
         par[16]:=__param.MIES_OD;
         par[17]:=__param.ROK_DO;
         par[18]:=__param.MIES_DO;
         par[1]:=obj_new(@.CLASS.PRINT,{? ~rep_export() || 14 || 2+__tab.size() ?});
         {? rep_export()
         || par[1].add(
               "  '%1 %2 %3 %4'
                     [  P.OSOBA().NAZWISKO,
                        OSOBA.PIERWSZE,
                        {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                        '(Nr teczki %1)'@[|P.T]
                     ]
               ",40,'Pracownik'@
            )
         ?};
         par[1].add("__RUB.RT[par[3].RN]",20,'Nazwa składnika'@);
         _loop:=__tab.first();
         {!
         |? _loop
         |! STR.split(date(,__tab.MIESIAC,1)$8);
            _m:=__tab.LP;
            par[1].add($('par[8]['+$_m+']'),12,STR.get_word()+' '+$__tab.ROK,1,,,,,'12,2');
            _loop:=__tab.next()
         !};
         {? ~rep_export() & __tab.size()>1
         || par[1].add("par[3].KW",12,'RAZEM'@,1,,,,,'12,2')
         ?};
         par[1].s_frame();
         par[6]:=par[11]:=par[12]:=0;
         par[5]:={? __param.DRUP || 2 || 1 ?};
         par[8]:=obj_new(__tab.size());
         {! _ni:=1..obj_len(par[8]) |! par[8][_ni]:=0 !};
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?};
      prn1:='par[1]';
      lmt:=rsep:=0;
      vp:=b_rat:=1
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','b_rat','h_rat','__edit','__param','__okres','__tab')
}
{EXIT: ~par[7]}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { {? par[5]=1 || par[5]:=0 ?}; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Typ rozliczenia: '@+
      {? __param.TYP_ROZ=0
      || 'kosztowy'
      |? __param.TYP_ROZ=1
      || 'podatkowy'
      |? __param.TYP_ROZ=2
      || 'ubezpieczeniowy'
      ?}
   }
   {<{lmt+1}'Okres: '@+
      {? __tab.size()>1
      || $par[16]+'/'+$par[15]+' - '+$par[18]+'/'+$par[17]
      || STR.split(date(,par[16],1)$8);
         STR.get_word()+' '+$par[15]
      ?}
   }
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {FOOTER:0}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {REP: par[1].flbar(lmt)}
   {FOOTER-}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W10'}
{SPACE: 'B'}
{<{ceil(lmt*b_rat)+1}}{form(par[6]+=(~par[12]),,,'9.')} {INCLUDE: p_prac.rpi}
{FONT: 'T8G'}
{SPACE: 'C'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W10'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'C'}
{
   echo('Trwa pobieranie danych z list płac...'@);
   _lsref:='';
   _znak:='';
   O.cntx_psh();
   _loop:=__tab.first();
   {!
   |? _loop
   |! {? __param.TYP_ROZ=0
      || _lsref+='LS.REFERENCE like \'l%1%2%%\' or ' [($__tab.ROK)+2,('0'+$__tab.MIESIAC)+2]
      || {? __param.TYP_ROZ=1
         || _znak:='P'
         |? __param.TYP_ROZ=2
         || _znak:='U'
         ?};
         O.index('LISTYPL'+_znak);
         O.prefix(exec('ref_firma','ustawienia'),__F_ZATR.O,__tab.ROK,__tab.MIESIAC);
         {? O.first()
         || {!|?
               _lsref+='LS.REFERENCE like \''+O.LT+'%\' or ';
               O.next()
            !}
         ?}
      ?};
      _loop:=__tab.next()
   !};
   O.cntx_pop();
   _lsref:='(%1)' [_lsref-4];
   par[2]:=sql(
      'select '
         'P.IP as IP, '
         'R.LP as LP, '
         'R.RN as RN, '
         'R.RT as RT, '
         'O.R'+_znak+' as R_L, '
         'O.M'+_znak+' as M_L, '
         'sum(LS.KW) as KW '
      'from '
         'P join @LS using(LS.P,P.REFERENCE) join O join R '
      'where '
         'P.REFERENCE in (select :_a.SQL from :_a) and '+_lsref+' '+
      'group by '
         'P.IP, R.LP, R.RN, R.RT, O.R'+_znak+', O.M'+_znak+' '
      'order by '
         'IP, LP, RN, R_L, M_L',
         params_get().P
   );

   echo('Trwa przetwarzanie pobranych danych...'@);
   par[3]:=sql('
      select
         IP,
         LP,
         RN,
         sum(KW) KW
      from
         :_a
      group by IP, LP, RN
      order by IP, LP, RN',
      par[2]
   );
   echo(); ~~
}
{EXIT: type_of(par[2])<>type_of(SYSLOG)}
{FOR: params_get().P}
{DO}
{IF: P.seek(params_get().P.SQL,,1)}
   { par[4]:=0; ~~ }
   {FOR: par[3]}{PREFIX: P.IP}{DO}
   {FIRST}
      {IF: ~rep_export()}
         {SUBSTITUTE: 'pracownik'}
      {FI}
      {IF: {? rep_export() || ~par[4] || 1 ?}}
         {REP: par[1].fhbar(lmt)}
      {FI}
      { par[4]:=1; ~~ }
   {FIRST-}
      {IF: ~rep_export() & par[12]}
      {SUBSTITUTE: 'pracownik'}
      {REP: par[1].fhbar(lmt)}
      {FI}
      {IF: ~__RUB.sys_attr(par[3].RN,611,date())}
      {  {? lineleft()<2 || par[12]:=1 || par[12]:=0 ?};
         _nj:=0;
         _loop:=__tab.first();
         {!
         |? _loop
         |! par[2].prefix(P.IP,par[3].LP,par[3].RN,#($__tab.ROK));
            {? par[2].find_key(__tab.MIESIAC)
            || _nj+=1;
               par[8][_nj]:=par[2].KW
            || _nj+=1;
               {? __tab.size()>1
               || par[8][_nj]:=0
               || par[8]:=0
               ?}
            ?};
            _loop:=__tab.next()
         !};
         par[1].ini_line(); ~~
      }
      {REP: par[1].fline(lmt)}
      {FI}
   {OD}
   {IF: rep_export()} {REP: par[1].flbar(lmt)} {FI}
   {IF: par[4] & ~par[12]}
      {PAGE}
   {FI}
   { par[12]:=0; ~~ }
{FI}
{OD}
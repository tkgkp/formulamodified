:!UTF-8
{TITLE:G. Należności / zobowiązania w podziale na kwoty }
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,10,PAR_WYDR.MTOP,10,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:  i:=0;           "pomocnicza do pętli";
         max_per:=6;     "maksymalna liczba przedziałów + 1";
         il:=0;          "ostatnia kolumna w wierszu";
         tab_kol:=0;     "liczba kolumn tabeli";
         stan_na:=date;  "stan na wybrany dzień; drukowane są tylko rozrachunki ";
                         "z datą otwarcia wcześniejszą od podanej";
         pr:=0;
         pierwszy:=licznik:=0;
                         "zmienne pomocnicze - pierwszy rozrachunek, licznik rozrachunków ";
         suma:=0;        "saldo aktualnego rozrachunku";
         q:=obj_new(max_per+3); "wartości dla pojedynczego rozrachunku";
         s:=obj_new(max_per+3); "suma kolumn dla kontrahenta ";
         t:=obj_new(max_per+3); "totale kolumn dla całego wydruku";
         {! i:=1..max_per+3 |! q[i]:=s[i]:=t[i]:=0 !};
         per:=obj_new(max_per+1); "szerokości przedziałów";
         szer_kol:=obj_new(20); "szerokości kolumn";
         w:=obj_new(max_per+4); w[3]:=''; "wartosci dla macra linia i suma";
         petla:=vk:=kanc:=g1:=0;  PAR_WYDR.OMASKA:=1;
         own:=0; oma:=0; druk:=0; p_odd:=indtym:=kan:='';
         x:=obj_new(14); {! _a:=1..14 |! x[_a]:='' !};
         PRN:=obj_new(@.CLASS.PRINT,14); PRN.sl_frame();
         PRNH:=obj_new(@.CLASS.PRINT,6); PRNH.sl_frame();
         color:=prn1:=prn2:=''; KH.win_sel('WER_RPM');
         lm:=ll:=vp:=lmt:=rsep:=h_rsep:=0; pom_w:=''; sumall:=0
      }
{END:    &il; &tab_kol; &stan_na; &pr; &i; &max_per; &suma; &petla;
         &pierwszy; &licznik; &vk; &kan; &kanc; &g1;
         obj_del(szer_kol); obj_del(w); obj_del(s); obj_del(q);
         obj_del(t); obj_del(per);
         &own; &oma; &indtym; OP.ndx_drop; &druk; &p_odd;
         obj_del(PRNH); obj_del(PRN); obj_del(x);
         &color; &prn1; &prn2;
         &lm; &ll; &vp; &lmt; &rsep; &h_rsep; &pom_w; &sumall
      }
{  UNPAR.P1:=1;
   KONTO.K1:=KONTO.K2:='';
   KONTO.K1_WYM:=KONTO.K2_WYM:=0;
   KONTO.K1_SYNT:=KONTO.K2_SYNT:=0;
   KONTO.K1_RODZ:=2; KONTO.K2_RODZ:=3;
   KONTO.K1_BE:='{? UNPAR.P1=1 || 1 || 0 ?}';
   KONTO.K2_BE:='{? UNPAR.P1=2 || 1 || 0 ?}';
   KONTO.K1_AE:=KONTO.K2_AE:='';
   UNPAR.P1_BE:='';
   UNPAR.P1_AE:='{? UNPAR.P1=1 || KONTO.K2:=\'\''
               +'|? UNPAR.P1=2 || KONTO.K1:=\'\''
               +'?}';
   PAR_WYDR.win_edit('RWKWOTY');
   PAR_WYDR.PAR1:=max_per;
   PAR_WYDR.PAR2:=500;
   PAR_WYDR.PAR3:=1000;
   PAR_WYDR.PAR4:=2000;
   PAR_WYDR.PAR5:=3000;
   PAR_WYDR.PAR6:=4000;
   PAR_WYDR.PAR7:=5000;
   PAR_WYDR.S1PAR:='+';
   PAR_WYDR.DPAR1:=date();
   PAR_WYDR.S1PAR1:='N';
   PAR_WYDR.PAR8:=1;
   PAR_WYDR.PAR9:=2; ~~}
  {EXIT: ~PAR_WYDR.edit("
    PAR_WYDR.S1PAR1:=~-PAR_WYDR.S1PAR1;
    {? PAR_WYDR.PAR1>max_per | PAR_WYDR.PAR1<1
    || FUN.info('Liczba przedziałów nie jest z zakresu 1 - 6.'@); 'PAR1'
    |? PAR_WYDR.PAR2>=PAR_WYDR.PAR3 & PAR_WYDR.PAR1>1
    || FUN.info('Nie prawidłowa wartości przedziałów.'@); 'PAR2'
    |? PAR_WYDR.PAR3>=PAR_WYDR.PAR4 & PAR_WYDR.PAR1>2
    || FUN.info('Nie prawidłowa wartości przedziałów.'@); 'PAR3'
    |? PAR_WYDR.PAR4>=PAR_WYDR.PAR5 & PAR_WYDR.PAR1>3
    || FUN.info('Nie prawidłowa wartości przedziałów.'@); 'PAR4'
    |? PAR_WYDR.PAR5>=PAR_WYDR.PAR6 & PAR_WYDR.PAR1>4
    || FUN.info('Nie prawidłowa wartości przedziałów.'@); 'PAR5'
    |? PAR_WYDR.PAR6>=PAR_WYDR.PAR7 & PAR_WYDR.PAR1>5
    || FUN.info('Nie prawidłowa wartości przedziałów.'@); 'PAR6'
    || {? PAR_WYDR.S1PAR1='' || PAR_WYDR.S1PAR1:='N' ?}; 1
    ?}")}
{  _prefix:=exec('maska2prefix','konto',KONTO.K1);
   {? _prefix<>'?'
   || UNPAR.P1:=2;
      KONTO.K2:=_prefix;
      KONTO.K1:=''
   ?};
   stan_na:=PAR_WYDR.DPAR1;
   PAR_WYDR.TITLE:={? PAR_WYDR.S1PAR1<>'Z' || 'NALEŻNOŚCI'@ || 'ZOBOWIĄZANIA'@?}+' W PODZIALE NA KWOTY'@+'  '+'STAN NA DZIEŃ: %1'@[$stan_na];
   WYDRUKIN.DLKON:=35;
   WYDRUKI.MASKA:=KONTO.K1;
   {? WYDRUKI.MASKA=''
   || WYDRUKI.MASKA:=35*'?'
   || {? +WYDRUKI.MASKA<WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?}
   ?};
   {? OPERATOR.DEPT
   || indtym:=OP.ndx_tmp('',1,'WAL',,0,'KH',,0,'ODD',,0,'AN',,0,'TZ',,)
   || indtym:=OP.ndx_tmp('',1,'WAL',,0,'KH',,0,'AN',,0,'TZ',,)
   ?};
    w[4]:=w[5]:=0;
    PAR_WYDR.PAR1+=1;
    tab_kol:=PAR_WYDR.PAR1+6;
    szer_kol[1]:=40;
    szer_kol[2]:={? PAR_WYDR.PAR9=2 || 20 || 0 ?};
    szer_kol[3]:={? PAR_WYDR.PAR9=2 || 10 || 0 ?};
    szer_kol[4]:={? PAR_WYDR.PAR9=2 || 10 || 0 ?};
    {! _a:=5..tab_kol
    |! szer_kol[_a]:=13
    !};
    {! _a:=1..tab_kol
    |! il+=szer_kol[_a]+1
    !};
    per[1]:=-999999999999;
    per[2]:=PAR_WYDR.PAR2;
    per[3]:=PAR_WYDR.PAR3;
    per[4]:=PAR_WYDR.PAR4;
    per[5]:=PAR_WYDR.PAR5;
    per[6]:=PAR_WYDR.PAR6;
    per[7]:=PAR_WYDR.PAR7;
    WYDRUKIN.REJSYM:='';
   {? OPERATOR.DEPT
   || p_odd:='Jednostka księgowa: '+OPERATOR.DEPT().OD
   || p_odd:='Wszystkie jednostki księgowe'
   ?};
   _k:=0; {! _a:=1 .. 6 |! _k+=szer_kol[_a] !};
   PRNH.add("''",_k+12,'',,,,,,,,2+(PAR_WYDR.PAR9=2)*3);
   _k:=0; {! _a:=1 .. PAR_WYDR.PAR1+1 |! _k+=szer_kol[_a+5] !};
   PRNH.add("''",_k+PAR_WYDR.PAR1,{? PAR_WYDR.S1PAR1<>'Z' || 'Należności'@ || 'Zobowiązania'@?}+' wg kwot '+{?PAR_WYDR.S1PAR='-'||'przed terminem'@||'po terminie'@?}+' płatności'@,,,,,,,,3+{? PAR_WYDR.PAR1>2 || PAR_WYDR.PAR1-2 ?});
   PRN.add({? ~rep_export() || "x[1]"
           |? PAR_WYDR.PAR8=1 & PAR_WYDR.PAR15=0 || "{? sumall || x[1] || OP.AN ?}"
           || "{? sumall || x[1] || OP.KH().KOD+' - '+OP.KH().NAZ ?}"
           ?},szer_kol[1],{? PAR_WYDR.PAR8=1 & PAR_WYDR.PAR15=0 || 'Konto'@ || 'Kontrahent'@ ?});
   PRN.add("x[14]",szer_kol[2],'Skrót');
   {? PAR_WYDR.PAR9=2 ||
      PRN.add("x[2]",szer_kol[2],'Dokument'@);
      PRN.add("x[3]",szer_kol[3],'Data otwarcia'@);
      PRN.add("x[4]",szer_kol[4],'Termin zapłaty'@)
   ?};
   PRN.add("x[5]",szer_kol[5],{?PAR_WYDR.S1PAR='+'||'Przed terminem'@||'Po terminie'@?}+' płatności'@,1);
   PRN.add("x[6]",szer_kol[6],'Do '+$per[2],1);
   {? PAR_WYDR.PAR1-1>1
   || {! _a:=1 .. PAR_WYDR.PAR1-2
      |! PRN.add($('x['+$(_a+6)+']'),szer_kol[_a+6],'Od '@+$per[_a+1]+' '+{? rep_export() || ' ' || ' #' ?}+' Do '@+$per[_a+2],1,'#')
      !}
   || _a:=0
   ?};
   PRN.add($('x['+$(_a+7)+']'),szer_kol[_a+7],'Od '@+$per[_a+2],1);
   PRN.add($('x['+$(_a+8)+']'),szer_kol[_a+8],'Razem'@,1);
   prn1:=prn2:='PRN';
   ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}-------------------------------LINIA_D------------------------------------{COMMENT-}
{MACRO: 'LINIA_D'}
{IF: pom_w<>''}{x[1]:=pom_w; {! _a:=2 .. PAR_WYDR.PAR1+6 |! x[_a]:='' !}; h_rsep:=rsep; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}{pom_w:=''; rsep:=h_rsep;~~}
{FI}
{x[1]:=STR.line_pri(szer_kol[1]);
 {? PAR_WYDR.PAR9=2 || x[2]:=w[1];x[3]:=$w[2];x[4]:=$OP.TZ ?};
 x[5]:=form(q[1],,2,' ,'); {! _a:=1 .. PAR_WYDR.PAR1+1 |! x[_a+5]:=form(q[_a+1],,2,' ,') !}; ~~}
{SUBSTITUTE: 'LINIA0'}{rsep:=0;~~}
{MACRO-}
{COMMENT}-------------------------------------SUMA---------------------------------{COMMENT-}
{MACRO: 'SUMA'}
{IF: pom_w<>'' & pom_w<>w[3]}{x[1]:=pom_w; {! _a:=2 .. PAR_WYDR.PAR1+6 |! x[_a]:='' !}; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}{pom_w:=''; ~~}
{FI}
{x[1]:=w[3]; {? PAR_WYDR.PAR9=2 || x[2]:=x[3]:=x[4]:='' ?};
{! _a:=0 .. PAR_WYDR.PAR1+1 |! x[_a+5]:=form(s[_a+1],,2,' ,') !}; ~~}
{rsep:=1;~~}{SUBSTITUTE: 'LINIA0'}{rsep:=0; w[3]:=pom_w:='';~~}
{MACRO-}
{COMMENT}-----------------------------------HEADTAB--------------------------------{COMMENT-}
{MACRO: 'HEADTAB'}
{PRNH.setcolor(color);~~}
{PRNH.ini_head()}
{REP: PRNH.fhbar()}
{WHILE: PRNH.h_next()}{DO}
{REP: PRNH.h_fmline()}
{OD}
{REP: PRNH.fjbar(PRN)}
{PRN.setcolor(color);~~}
{PRN.ini_head()}
{WHILE: PRN.h_next()}{DO}
{REP: PRN.h_fmline()}
{OD}
{MACRO-}
{COMMENT}---------------------------------CZOLO1------------------------------------{COMMENT-}
{MACRO: 'CZOLO1'}
{SUBSTITUTE: 'HEAD'}{FONT: 'T8G'}{SPACE: 'C'}
{<1 'PARAMETRY WYDRUKU:'@}
{IF: PAR_WYDR.PAR8=1}{<1 'Dla wszystkich kontrahentów'@}{ELSE}{<1 'Dla wybranych kontrahentów'@}{FI}
{<1 {? UNPAR.P1=1 || 'Maska konta: %1'@[KONTO.K1] || 'Maska konta: %1'@[35+(KONTO.K2+35*'?')] ?}}
{<1'Waluta:  %1 %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<1 p_odd}
{IF: rep_export()}
{<1 {? PAR_WYDR.PAR15 || 'Sumowanie: według kontrahentów'@ || 'Sumowanie: według konta'@ ?} }
{<40 'Dla granicznych wartości przedziału zakres \'od\' nie należy do przedziału, zakres \'do\' należy'@}
{ELSE}
{<1 {? PAR_WYDR.PAR15 || 'Sumowanie: według kontrahentów'@ || 'Sumowanie: według konta'@ ?} }{<40 'Dla granicznych wartości przedziału zakres \'od\' nie należy do przedziału, zakres \'do\' należy'@}
{FI}
{FONT: 'T8GB'}{SUBSTITUTE: 'HEADTAB'}{FONT: 'T8G'}{vp:=1;~~}
{MACRO-}
{COMMENT}----------------------------------CZOLO------------------------------------{COMMENT-}
{MACRO: 'CZOLO'}
{SUBSTITUTE: 'HEAD'}{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'HEADTAB'}{FONT: 'T8G'}{vp:=1;~~}
{MACRO-}
{COMMENT}----------------------------------BODY_------------------------------------{COMMENT-}
{MACRO:'BODY_'}{ 'WYWOLYWANE W PETLI KH'@;~~}
{  {? |w[3]<>'' & STR.next() & licznik>0 || pom_w:=STR.line_pri(255) ?};
   rsep:=1;
   w[3]:=KH.KOD+' - '+KH.NAZ; STR.split(w[3]); licznik:=0;
   OP.index(indtym);
   {? OPERATOR.DEPT
   || OP.prefix(SSTALE.WAL,KH.ref,OPERATOR.DEPT,KONTO.K2)
   || OP.prefix(SSTALE.WAL,KH.ref,KONTO.K2)
   ?};
   petla:=OP.first(); ~~}
{WHILE: petla}
{DO}
   {  {? UNPAR.P1=1 & KONTO.K1<>'' & KONTO.K1<>35*'?' || vk:=exec('mask','konto',OP.AN) || vk:=1 ?}; ~~}
      {IF: vk & (1+OP.TYP)=PAR_WYDR.S1PAR1 }
     { druk:=0;
       ZAP_OP.cntx_psh;
       ZAP_OP.index('OP');
       ZAP_OP.prefix(OP.ref);
       {? ZAP_OP.first
       ||  {! |?
              {? ZAP_OP.DATA<=PAR_WYDR.DPAR1 ||
                 druk:=1
              ?};
              ZAP_OP.next & druk=0
           !}
       ?};
       ZAP_OP.cntx_pop;~~}
     {IF: druk=1 }
        { 'obroty na dzien podany w parametrach wejsciowych'@;
           own:=F.ROw(OP.ref, PAR_WYDR.DPAR1); oma:=F.ROm(OP.ref, PAR_WYDR.DPAR1);
           ~~}
       {IF: ((-(1+OP.TYP))='n' & own$2<>oma$2) | ((-(1+OP.TYP))='z' & own$2<>oma$2)}
{'--------------------------------------------------------------------';~~}
           { w[1]:=OP.SYM;
             x[14]:=OP.KH().SKR;
             w[2]:=OP.DO;  pr:=1;
             suma:={? PAR_WYDR.S1PAR1='N' || own-oma || oma-own ?};
             licznik+=1;  pierwszy:=1;
             _b:={? PAR_SKID.get(47)='N' & ( 1+OP.TYP='N' & oma>own | 1+OP.TYP='Z' & own>oma)
                 || {? PAR_WYDR.S1PAR='+' || -1 || 1 ?}
                 || {? PAR_WYDR.S1PAR='+' || PAR_WYDR.DPAR1-OP.TZ || OP.TZ - PAR_WYDR.DPAR1 ?}
                 ?};
             {? {? PAR_WYDR.S1PAR='+' || -_b>=0 || -_b>0 ?}
             || q[1]+=suma
             |? suma>per[PAR_WYDR.PAR1]
             || q[PAR_WYDR.PAR1+1]+=suma
             ||{! i:=1..PAR_WYDR.PAR1-1
               |! {? suma>per[i]
                           & suma<=per[i+1]
                  || q[i+1]+=suma
                  ?}
               !}
             ?};
             q[PAR_WYDR.PAR1+2]+=suma; ~~
           }
           {IF: PAR_WYDR.PAR9=2}
           {SUBSTITUTE: 'LINIA_D'}
           {FI}
           { {! i:=1..(PAR_WYDR.PAR1+2)
             |! s[i]+=q[i]; q[i]:=0
             !};~~
           }
            {FI}
           {FI}
         {FI}
   {petla:=OP.next(); ~~}
{OD}{' Do  OP';~~}
{IF: (PAR_WYDR.PAR9<>2 | licznik>1) & ~rep_export()}
   { {? w[3]<>'' & STR.next() || pom_w:=STR.line_pri(255) ?};
     w[3]:={?PAR_WYDR.PAR9=2||'Suma '||''?}+{? PAR_WYDR.PAR9=2|| KH.SKR||KH.NAZ?};~~}
   {IF: s[PAR_WYDR.PAR1+2]}
      {SUBSTITUTE: 'SUMA'}
   {FI}
{FI}
{ {! i:=1..(PAR_WYDR.PAR1+2) |! t[i]+=s[i]; s[i]:=0; q[i]:=0 !}; ~~}
{MACRO-}
{COMMENT}------------------------------MACRO-BODY-------------------{COMMENT-}
{MACRO:'BODY'}
{  licznik:=0;
   {? OPERATOR.DEPT
   || OP.index('PRZEG_O'); OP.prefix(SSTALE.WAL,OPERATOR.DEPT,KONTO.K2)
   || OP.index('PRZEG'); OP.prefix(SSTALE.WAL,KONTO.K2)
   ?};
   petla:=OP.first(); ~~}
{WHILE: petla}
{DO}
   { {? UNPAR.P1=1 || vk:=exec('mask','konto',OP.AN) || vk:=1 ?}; ~~}
   {IF: vk & (1+OP.TYP)=PAR_WYDR.S1PAR1}
      {  druk:=0;
         ZAP_OP.cntx_psh;
         ZAP_OP.index('OP');
         ZAP_OP.prefix(OP.ref);
         {? ZAP_OP.first
         || {! |?
               {? ZAP_OP.DATA<=PAR_WYDR.DPAR1 || druk:=1 ?};
               ZAP_OP.next & druk=0
            !}
         ?};
         ZAP_OP.cntx_pop; ~~}
      {IF: druk=1 }
         {  'obroty na dzien podany w parametrach wejsciowych'@;
            own:=F.ROw(OP.ref,PAR_WYDR.DPAR1); oma:=F.ROm(OP.ref,PAR_WYDR.DPAR1); ~~}
         {IF: ((-(1+OP.TYP))='n' & own$2<>oma$2) | ((-(1+OP.TYP))='z' & own$2<>oma$2)}
            {  w[1]:=OP.SYM;
               x[14]:=OP.KH().SKR;
               w[2]:=OP.DO;
               suma:={? PAR_WYDR.S1PAR1='N' || own-oma || oma-own ?};
               {? licznik=1 || {? PAR_WYDR.PAR8<>1 || w[3]:='' ?} ?};
               licznik+=1; pierwszy:=1;
               _b:={? PAR_SKID.get(47)='N' & ( 1+OP.TYP='N' & oma>own | 1+OP.TYP='Z' & own>oma)
                   || {? PAR_WYDR.S1PAR='+' || -1 || 1 ?}
                   || {? PAR_WYDR.S1PAR='+' || PAR_WYDR.DPAR1-OP.TZ || OP.TZ - PAR_WYDR.DPAR1 ?}
                   ?};
               {? {? PAR_WYDR.S1PAR='+' || -_b>=0 || -_b>0 ?}
               || q[1]+=suma
               |? suma>per[PAR_WYDR.PAR1]
               || q[PAR_WYDR.PAR1+1]+=suma
               || {! i:=1..PAR_WYDR.PAR1-1 |!
                     {? suma>per[i] & suma<=per[i+1] || q[i+1]+=suma ?}
                  !}
               ?};
               q[PAR_WYDR.PAR1+2]+=suma; ~~}
            {IF: PAR_WYDR.PAR9=2}
               {IF: OP.AN<>kan | rep_export()}
                  {IF: g1>1 & ~rep_export()}
                     { w[3]:='Suma '+kan; rsep:=1;~~}
                     {SUBSTITUTE: 'SUMA'}
                  {FI}
                  {  w[3]:=OP.AN; kan:=OP.AN; kanc:=1; g1:=0; STR.split(w[3]); rsep:=1;
                     {! i:=1..(PAR_WYDR.PAR1+2) |! t[i]+=s[i]; s[i]:=0 !}; ~~}
               {ELSE}
                  { kanc:=0;~~}
               {FI}
               {SUBSTITUTE: 'LINIA_D'}
               { licznik+=1; g1+=1; pr:=1; ~~}
            {ELIF: PAR_WYDR.PAR9=1 & q[PAR_WYDR.PAR1+2]$2<>0}
               {IF: OP.AN<>kan & kan<>''}
                  { w[3]:=kan; g1:=1; rsep:=1;~~}
                  {SUBSTITUTE: 'SUMA'}
                  {  {! i:=1..(PAR_WYDR.PAR1+2) |! t[i]+=s[i]; s[i]:=0 !};
                     pr:=1; ~~}
               {FI}
            {FI}
            {IF: PAR_WYDR.PAR9=1 & kan<>OP.AN}
               { kan:=OP.AN; ~~}
            {FI}
            { {! i:=1..(PAR_WYDR.PAR1+2) |! s[i]+=q[i]; q[i]:=0 !}; ~~}
         {FI}
      {FI}
   {FI}
   {petla:=OP.next(); ~~}
{OD}{' Do  OP';~~}
{IF: PAR_WYDR.PAR9=1}
   {  w[3]:=kan; rsep:=1;~~}
   {ENTIRE}
   {IF: s[PAR_WYDR.PAR1+2]}
      {SUBSTITUTE: 'SUMA'}
   {FI}
   {ENTIRE-}
   { {! i:=1..(PAR_WYDR.PAR1+2) |! t[i]+=s[i]; s[i]:=0; q[i]:=0 !}; ~~}
{ELIF: PAR_WYDR.PAR9=2}
   {IF: g1>1}
      { w[3]:='Suma '+kan; ~~}
      {SUBSTITUTE: 'SUMA'}
   {FI}
   {  w[3]:=OP.AN; kan:=OP.AN; kanc:=1; g1:=0; STR.split(w[3]); ~~}
   { {! i:=1..(PAR_WYDR.PAR1+2) |! t[i]+=s[i]; s[i]:=0; q[i]:=0 !}; ~~}
{FI}
{MACRO-}
{FOOTER:0}

{FOOTER-}
{'--------------------------------------------------------------------';~~}
{SUBSTITUTE: 'CZOLO1'}
{HEADER}
{SUBSTITUTE: 'CZOLO'}
{HEADER-}
{'--------------------------------------------------------------------';~~}
{FOOTER:0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{'--------------------------------------------------------------------';~~}
{IF: PAR_WYDR.PAR8=1}
{IF: PAR_WYDR.PAR15=1}
{FOR: 'KH'}
{INDEX: 'KOD'}
{PREFIX: 2, WYDRUKIN.REJSYM}
{DO}
{SUBSTITUTE:'BODY_'}
{OD}{'  Do  KH';~~}
{ELSE}
{SUBSTITUTE: 'BODY'}
{FI}
{ELIF: PAR_WYDR.PAR8=2}
{KH.win_sel('WER_RPM');~~}
{FOR: 'KH'}
{INDEX: 'KOD'}
{PREFIX: 2, WYDRUKIN.REJSYM}
{SELECT:'#EXIT'}
{DO}
{SUBSTITUTE: 'BODY_'}
{OD}{'  Do  KH';~~}
{FI}
{  {? pom_w=w[3] || pom_w:='' ?};
   sumall:=1;
   {? PAR_WYDR.PAR15
   || w[3]:='Ogółem wszyscy kontrahenci'
   || w[3]:='Ogółem'
   ?};
   {! i:=1..(PAR_WYDR.PAR1+2) |! s[i]:=t[i] !}; ~~}
{SUBSTITUTE: 'SUMA'}
{IF: lineleft()<2 & lineleft()<>0}{PAGE}{FI}
{FOOTER}
{FOOTER-}
{SUBSTITUTE: 'LINIAF'}
{IF: ~rep_export()}
{<1>{il}'KONIEC WYDRUKU'@}
{FI}
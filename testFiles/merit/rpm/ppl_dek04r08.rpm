:!UTF-8
{TITLE:PIT-4R(8) formularz obowiązujący od roku 2018}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,5,5,5,5,1,1,'A4'}
{BEGIN:
   {? var_pres('pit')>100 || obj_del(pit) ?};
   pit:=obj_new(9);
   {! _nx:=1..obj_len(pit) |! pit[_nx]:='' !};
   USERS.cntx_psh();
   pit[4]:=4+VAT_DEK.OKRES;
   exec('czytaj','#stalesys',date(),KST);

   pit[1]:=KST.NIP;
   pit[5]:=KST.US().NU+
           {? +KST.US().UU || ', '+KST.US().UU || '' ?}+
           {? +KST.US().UK & +KST.US().MU ||
              ', '+(6+KST.US().UK)+' '+KST.US().MU || ''
           ?};
   pit[7]:='N';
   {? VAT_DEK.POD=1 || pit[7]:='T' ?};
   {? pit[7]='N'
   || pit[8]:=KST.NAZWA+', '+KST.REG;    'Platnik niebedacy osoba fizyczna'
   || pit[8]:=KST.PODNAZ+', '+KST.PODIME+', '+STR.gsub(KST.PODURDAT$4,'.','-'); 'Platnik bedacy osoba fizyczna'
   ?};

   pit[6]:=VAT_DEK.KOR;
   pit[9]:=VAT_DEK.IMIE+' '+VAT_DEK.NAZ+{? VAT_DEK.STN<>'' || ', '+VAT_DEK.STN || '' ?};

   {! _nx:=1..obj_len(pit) |! pit[_nx]:=~(-pit[_nx]) !};
   pit[2]:=exec('upo_id','xml');

   {? var_pres('tab')>100 || obj_del(tab) ?};
   tab:=tab_tmp(2,
      'NR','INTEGER','Nr',
      'NRK','INTEGER','Nrk',
      'K1','INTEGER','Kwota',
      'K2','INTEGER','Kwota',
      'K3','INTEGER','Kwota',
      'K4','INTEGER','Kwota',
      'K5','INTEGER','Kwota',
      'K6','INTEGER','Kwota'
   );
   ind:=0; Podatek:=0; czy_raport:=0;
   DEKL_POD.use('pod_'+pit[4]);
   DEKL_POD.index('DEKL_PIT');
   DEKL_POD.prefix();

   {? VAT_DEK.KOR='T'
   || {? var_pres('__ord')>100 || obj_del(__ord) ?};
      __ord:=obj_new(13);
      {! _nx:=1..obj_len(__ord) |! __ord[_nx]:='' !};
      {? pit[7]='T'
      || __ord[1]:=KST.NIP;
         __ord[5]:=KST.PODNAZ;
         __ord[6]:=KST.PODIME;
         __ord[7]:=STR.gsub(KST.PODURDAT$4,'.','-')
      || __ord[1]:=KST.NIP;
         __ord[5]:=KST.NAZWA;
         __ord[6]:=KST.SKROT;
         __ord[8]:=KST.REG
      ?}
   ?};
   VAT_POZ.cntx_psh();
   exec('rpm_as_pdf_ini','rpm_pdf');
   VAR_DEL.delete('x','w','y');
   y:=0;
   x:=obj_new(6);
   w:=obj_new(6);
   VAR_DEL.delete('__OpWpla','__iter');
   __OpWpla:={? var_pres('OPISWPLA',VAT_DEK)>0
             || ~-exec('opis_korekty','edeklar','OPISWPLA')
             || ''
             ?}
}
{END:
   exec('rpm_as_pdf_end','rpm_pdf');
   USERS.cntx_pop();
   VAT_POZ.cntx_pop;
   {? VAT_DEK.KOR='T' || obj_del(__ord); &__ord ?};
   VAR_DEL.delete('x','w','y','ind','Podatek','czy_raport','pit','tab','__OpWpla','__iter');
   exec('stalesys','#stalesys')
}
{COMMENT}OLD: dek04r08.rpm{COMMENT-}
{COMMENT}OLD: dkw4r08g.rpi{COMMENT-}
{IF: DEKL_POD.find_key('P','PIT-4R','PIT-4R')}
{EXIT:
   USERS.cntx_psh();
   USERS.prefix();
   _kod:=DEKL_POD.USER().KOD;
   USERS.cntx_pop();
   ~FUN.ask(
      'Za %1 rok wydrukowano deklarację podatkową %2\n'
      '(data: %3, operator: %4).\n'
      'Czy kontynuować wydruk?'@
      [pit[4],DEKL_POD.PIT,DEKL_POD.DATA$7,_kod]
   )
}{FI}
{  tab.prefix();
   VAT_POZ.index('NR_NRK');
   VAT_POZ.prefix(VAT_DEK.ref);
   {? VAT_POZ.first()
   || {!
      |? tab.NR:=VAT_POZ.NR;
         tab.NRK:=VAT_POZ.NRK;
         {! _ind:=1..6
         |! _kw:=($('VAT_POZ.K'+$_ind))();
            ($('tab.K'+$_ind))():=_kw;
            {? tab.NR>=1 || czy_raport+=_kw ?};
            {? tab.NR=11 || Podatek+=_kw ?}
         !};
         tab.add();
         VAT_POZ.next()
      !}
   ?};
   {? VAT_DEK.KOR='T'
   || __ord[13]:=exec('opis_korekty','edeklar');
      {! _nx:=1..obj_len(__ord) |! __ord[_nx]:=~(-__ord[_nx]) !};
      __ord[3]:=pit[2]
   ?};

   tab.prefix();
   ~~
}
{EXIT:
   {? ~(czy_raport>0)
   || FUN.info('Brak informacji do wykazania na deklaracji.'@+'\n'+'(zerowe kwoty podatku)'@);
      1
   || 0
   ?}
}
{REP: '{INCLUDE: ppl_dkw4r08g.rpi}'}
{IF: VAT_DEK.KOR='T' & __ord[13]<>''}
{REP: '{INCLUDE: ppl_ord_zu3g.rpi}'}
{FI}
{ DEKL_POD.clear(); DEKL_POD.blank();
  DEKL_POD.RODZAJ:='P'; DEKL_POD.MSC:=0;
  DEKL_POD.DATA:=date(); DEKL_POD.PIT:='PIT-4R';
  DEKL_POD.S1:=0; DEKL_POD.S2:=0; DEKL_POD.S3:=Podatek;
  DEKL_POD.USER:=OPERATOR.USER;
  DEKL_POD.add();
  ~~
}
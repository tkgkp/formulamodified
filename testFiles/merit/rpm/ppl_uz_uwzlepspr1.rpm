:!UTF-8
{TITLE:Zleceniobiorcy z naliczoną blokadą KU}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','tresc','lmt','opis','Text','Edit_ok','__edit','__param','upr');
   PAR_WYDR.TITLE:='Zleceniobiorcy z naliczoną blokadą KU'@;
   tresc:='zlepspr';
   par:=obj_new(12);
   par[6]:=par[7]:=0;
   par[8]:=par[9]:=#0;
   par[10]:=1;
   par[11]:=0;
   upr:=_upr:=exec('uprawnieniawrap','pkd','Rejestracja rachunków'@,'PPL_ZLC_RRAC','PPL_ZLC_PRAC');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,'ROK','INTEGER','Rok');
      __param.ROK:=date~1;
      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'uz_uwzlepsprc');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok do składników wypłaty'@);
      __param.efld_opt(__edit,'mark=1',,'ROK');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? Edit_ok:=__param.edit("{?  __param.ROK<=1900
                                || FUN.emsg('Błędna wartość parametru - Rok.'@); 'ROK'
                                || 1
                                ?}")
      || par[8]:=date(__param.ROK,1,1);
         par[9]:=date(__param.ROK,12,0);
         par[11]:=exec('kwota_koszty50_zlec_rh','lista_licz',par[8]~1,0);
         par[6]:=1
      ?};
      par[1]:=obj_new(3); 'obiekty print';
      par[2]:='wykaz osób';
      par[3]:='zbiorówka';
      par[4]:=''; 'opis uwzglednianych umow';
      par[5]:=''; 'rodzaje uwzględnianych umów';

      OSOBA.prefix;
      lmt:=0;
      opis:=0;
      Text:=''
   ?};
   OS_PAB.cntx_psh();
   OS_PAB.index('OSOBAOD');
   ~~
}
{END:
   OS_PAB.cntx_pop();
   VAR_DEL.delete('par','tresc','lmt','opis','Text','Edit_ok','__edit','__param','upr')
}
{COMMENT}OLD: zlepspr1.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzialz.rpi{COMMENT-}
{EXIT: +upr}
{EXIT: ~Edit_ok}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}
{SPACE: 'C'}
{IF: par[10]}
   {LINE}
   { par[10]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Zakres dat'@}: {'od'@} {par[8]$1} {'do'@} {par[9]$1}
   {<{lmt+1}'Rodzaj umowy'@}: { STR.split(par[4]);opis:=1;STR.line(charleft) }
   {IF: opis}
   {WHILE: +(Text:=STR.line(charleft-lmt-15))}{DO}{<{lmt+15}}{REP: Text}{OD}
   {FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{REP: par[1][1].fhbar(lmt)}
{REP: par[1][1].ini_head(); par[1][1].h_fline(lmt)}
{REP: par[1][2].ini_head(); par[1][2].h_fline(lmt)}
{REP: par[1][3].ini_head(); par[1][3].h_fline(lmt)}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1][3].flbar(lmt)}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   D E F I N I C J E   -   P O B R A N I E   D A N Y C H

{COMMENT-}
{MACRO: tresc}
{MACRO-}
{MACRO: 'tabela'}
{ENTIRE}
   {REP: par[1][1].fbar(lmt)}
   {REP: par[1][1].ini_line(); par[1][1].fline(lmt)}
   {REP: par[1][2].ini_line(); par[1][2].fline(lmt)}
   {REP: par[1][3].ini_line(); par[1][3].fline(lmt)}
{ENTIRE-}
{MACRO-}
{EXIT: ~par[6]}
{EXIT: ~exec('slo_typ','ext_slo','UMZLEC')}
{IF: par[6]=1}
   {FOR: RU}{INDEX: 'K'}{PREFIX: SLO_TYP.ref}
   {DO}
   {IF: RU.K='2' | RU.K='3'}
   {
   {? par[5]*RU.K=0 ||
      par[5]+={? par[5]<>'' || ',' || '' ?}+
              '\''+RU.K+'\'';
      par[4]+={? par[4]<>'' || ', ' || '' ?}+
                     ''+RU.O+'('+RU.K+')'
   ?};
   ~~
   }
   {FI}
   {OD}
{FI}{
   _rh:=sql(
      'select '
      '  O.LT as LT, '
      '  RH.REFERENCE as RH, '
      '  ZC.OSOBA as OSOBA, '
      '  RU.K as K '
      'from '
      '  RH join '
      '  ZC using(RH.ZLE,ZC.REFERENCE) join '
      '  RU using(ZC.RU,RU.REFERENCE) join '
      '  :_c using(ZC.P,:_c.REF) join '
      '  O using(RH.O,O.REFERENCE) '
      'where to_date(:_a)<=RH.DWY and RH.DWY<=to_date(:_b) '+
      {? par[5]<>'' || ' and RU.K in ('+par[5]+')' || '' ?}+
      'order by LT, RH',
      par[8],
      par[9],
      exec('dostepne_p','schemat','PPL',__F_ZATR.P,'*')
   );

   _o:=sql('select distinct :_a.LT from :_a',_rh);

   _txt:=
      'select '
      '  :_a.OSOBA, '
      '  :_a.K, '
      '  case when LS.RB=\''+$__RUB.ref(500)+'\' then LS.KW else 0 end KW, '
      '  case when LS.RB=\''+$__RUB.ref(650)+'\' then LS.KW else 0 end ZAS, '
      '  case when LS.RB=\''+$__RUB.ref(765)+'\' then LS.KW else 0 end FEP, '
      '  case when LS.RB=\''+$__RUB.ref(766)+'\' then LS.KW else 0 end FRP, '
      '  case when LS.RB=\''+$__RUB.ref(767)+'\' then LS.KW else 0 end FC, '
      '  case when LS.RB=\''+$__RUB.ref(769)+'\' then LS.KW else 0 end ZUP, '
      '  case when LS.RB=\''+$__RUB.ref(784)+'\' then LS.KW else 0 end KU, '
      '  case when LS.RB=\''+$__RUB.ref(792)+'\' then LS.KW else 0 end KC, '
      '  case when LS.RB=\''+$__RUB.ref(793)+'\' then LS.KW else 0 end KO, '
      '  case when LS.RB=\''+$__RUB.ref(797)+'\' or LS.RB=\''+$__RUB.ref(798)+'\' then LS.KW else 0 end PD, '
      '  case when LS.RB=\''+$__RUB.ref(950)+'\' then LS.KW else 0 end POTR, '
      '  case when LS.RB=\''+$__RUB.ref(958)+'\' then LS.KW else 0 end FEZ, '
      '  case when LS.RB=\''+$__RUB.ref(959)+'\' then LS.KW else 0 end FRZ, '
      '  case when LS.RB=\''+$__RUB.ref(960)+'\' then LS.KW else 0 end FW, '
      '  case when LS.RB=\''+$__RUB.ref(963)+'\' then LS.KW else 0 end ZUF, '
      '  case when LS.RB=\''+$__RUB.ref(982)+'\' then LS.KW else 0 end FP, '
      '  case when LS.RB=\''+$__RUB.ref(983)+'\' then LS.KW else 0 end FG, '
      '  case when LS.RB=\''+$__RUB.ref(962)+'\' then LS.KW else 0 end FEPOM, '
      '  case when LS.RB=\''+$__RUB.ref(990)+'\' then LS.KW else 0 end DW, '
      '  case when LS.RB=\''+$__RUB.ref(994)+'\' then LS.KW else 0 end PR, '
      '  case when LS.RB=\''+$__RUB.ref(999)+'\' then LS.KW else 0 end GT '
      'from '
      '  LS join '
      '  :_a using(LS.RH,:_a.RH)'
      'where '
      '  LS.RB in ('
      '     select R.REFERENCE '
      '     from R '
      '     where R.RN in ('
      '        500,650,765,766,767,769,784,792,793,797,798,'
      '        950,958,959,960,962,963,982,983,990,994,999'
      '     ) '
      '     order by R.REFERENCE'
      '  ) ';

   _sum:=
      'sum(KW) KW, sum(ZAS) ZAS, sum(ZUP) ZUP, sum(ZUF) ZUF,'
      'sum(FEP) FEP, sum(FRP) FRP, sum(FC) FC,'
      'sum(FEZ) FEZ, sum(FRZ) FRZ, sum(FW) FW,'
      'sum(FP) FP, sum(FG) FG, sum(FEPOM) FEPOM, sum(KC) KC, sum(KO) KO,'
      'sum(KU) KU, sum(PD) PD, sum(DW) DW, '
      'sum(PR) PR, sum(GT) GT, sum(POTR) POTR ';

   LS.cntx_psh();
   LS.use('l_______');
   _buf:=sql(_txt,sql('select * from :_a where :_a.LT=\':_b\' order by :_a.RH',_rh,8*'*'));

   {? type_of(_buf)<>type_of(SYSLOG)
   || LS.cntx_pop();
      return(~~)
   ?};

   _fml:='';
   {! _ndx:=1.._buf.fld_num()
   |! _fml+=('_a[%1]:=_b[%1];'[$_ndx])
   !};
   _fml+='_a.add()';
   _add:=$_fml;

   _loop:=_o.first();
   {!
   |? _loop
   |! LS.use(_o.LT);
      _tmp:=sql(_txt,sql('select * from :_a where :_a.LT=\':_b\' order by :_a.RH',_rh,_o.LT));
      _loop:=_tmp.first();
      {!
      |? _loop
      |! _add(_buf,_tmp);
         _loop:=_tmp.next()
      !};
      obj_del(_tmp);
      _loop:=_o.next()
   !};
   LS.cntx_pop();

   _tmp:=sql('select :_a.OSOBA, :_a.K, %1 from :_a group by OSOBA, K'[_sum],_buf);
   obj_del(_buf);

   par[2]:=sql(
      'select '
      '  OSOBA.NAZWISKO, '
      '  OSOBA.PIERWSZE, '
      '  OSOBA.PESEL, '
      '  :_a.* '
      'from '
      '  :_a join '
      '  OSOBA using(:_a.OSOBA,OSOBA.REFERENCE) '
      'order by '
      '  OSOBA.NAZWISKO, '
      '  OSOBA.PIERWSZE, '
      '  OSOBA.PESEL, '
      '  :_a.K',
      _tmp
   );
   obj_del(_tmp);

   {? type_of(par[2])<>type_of(SYSLOG)
   || return(~~)
   ?};

   _blokada:="
      _blokada:=0;
      {? OSOBA.seek(BIT.sqlint(_a),)
      || OS_PAB.prefix(OSOBA.ref());
         {? OS_PAB.find_ge(par[8])
         || {!
            |? {? OS_PAB.OD<=par[9]
               || {? OS_PAB.TYP='B'
                  || _blokada:=1; 0
                  || OS_PAB.next()
                  ?}
               ?}
            !}
         ?}
      ?};
      _blokada";
   {? par[2].first
   || {!
      |? {? par[2].KU<par[11] & ~_blokada(par[2].OSOBA)
         || par[2].del()
         || par[2].next()
         ?}
      !}
   ?};

   par[3]:=sql('select '+_sum+' from :_a',par[2]);
   ~~
}
{EXIT: type_of(par[3])<>type_of(SYSLOG)}
{
   _k:={? par[8]<date(1999,1,1) || 7 || 9 ?};
   (par[1][1]:=obj_new(@.CLASS.PRINT,_k)).s_frame();
   (par[1][2]:=obj_new(@.CLASS.PRINT,_k)).s_frame();
   (par[1][3]:=obj_new(@.CLASS.PRINT,_k)).s_frame();

   par[1][1].add("{? par[6] || ''      || par[2].NAZWISKO ?}",20,'Nazwisko'@);
   par[1][2].add("{? par[6] || 'RAZEM'@ || par[2].PIERWSZE ?}",20,'Imię'@);
   par[1][3].add("{? par[6] || ''      || par[2].PESEL    ?}",20,'PESEL'@);

   _l:=13; _f:=$_l+',2,\'9,\'';

   par[1][1].add("''",_l,'');
   par[1][2].add("par[2+par[6]].KW              ",_l,'Brutto'@,1,,,,,_f);
   par[1][3].add("par[2+par[6]].ZAS              ",_l,'Zasiłki ZUS'@,1,,,,,_f);
   {? par[8]<date(1999,1,1)
      ||
         par[1][1].add("par[2+par[6]].ZUP",_l,'ZUS'@,1,,,,,_f);
         par[1][2].add("''               ",_l,'ubezpieczony'@);
         par[1][3].add("''               ",_l,'');

         par[1][1].add("par[2+par[6]].ZUF",_l,'ZUS płatnik'@,1,,,,,_f);
         par[1][2].add("par[2+par[6]].FP ",_l,'F.pracy'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].FG ",_l,'F.G.Ś.P.'@,1,,,,,_f)
      ||
         par[1][1].add("par[2+par[6]].FEP",_l,'F.emerytalny'@,1,,,,,_f);
         par[1][2].add("par[2+par[6]].FRP",_l,'F.rentowy'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].FC ",_l,'F.chorobowy'@,1,,,,,_f);

         par[1][1].add("par[2+par[6]].FEZ",_l,'F.emerytalny'@,1,,,,,_f);
         par[1][2].add("par[2+par[6]].FRZ",_l,'F.rentowy'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].FW ",_l,'F.wypadkowy'@,1,,,,,_f);

         par[1][1].add("par[2+par[6]].FP ",_l,'F.pracy'@,1,,,,,_f);
         par[1][2].add("par[2+par[6]].FG ",_l,'F.G.Ś.P.'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].FEPOM",_l,'F.E.P'@,1,,,,,_f);

         par[1][1].add("''               ",_l,'Ub.zdrowotne'@);
         par[1][2].add("par[2+par[6]].KC ",_l,'pobrane'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].KO ",_l,'odliczone'@,1,,,,,_f)
   ?};

   par[1][1].add("''              ",_l,'Koszty'@);
   par[1][2].add("par[2+par[6]].KU",_l,'uzyskania'@,1,,,,,_f);
   par[1][3].add("''              ",_l,'przychodu'@);

   par[1][1].add("''              ",_l,'Podatek'@);
   par[1][2].add("par[2+par[6]].PD",_l,'dochodowy'@,1,,,,,_f);
   par[1][3].add("par[2+par[6]].POTR",_l,'Potrącenia'@,1,,,,,_f);

   par[1][1].add("par[2+par[6]].DW",_l,' Netto'@,1,,,,,_f);
   par[1][2].add("par[2+par[6]].PR",_l,' Przelew'@,1,,,,,_f);
   par[1][3].add("par[2+par[6]].GT",_l,' Gotówka'@,1,,,,,_f);
   par[6]:=0;
   ~~
}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft-par[1][1].width())/2); ~~ }
{FOR: par[2]}{DO}
   {SUBSTITUTE: 'tabela'}
{OD}
{IF: {? var_pres('[2]',par)=type_of(SYSLOG)
        || par[2].first
        || 1
     ?}}
   { par[6]:=1; ~~ }
   {SUBSTITUTE: 'tabela'}
{FI}
:!UTF-8
{TITLE:C. Stan magazynów oddziału wg atrybutów}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('sm_03');
   sm_03:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('dokl','dokl_c');
      VAR_DEL.delete('k','w','s','sg','PRN','PRN2','k2','w2');
      VAR_DEL.delete('wyjscie','X_Xa','X_Xb','X_Xc','X_Xd','pom_tab')
   ";
   sm_03();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   k:=obj_new(5); "--szerokosci kolumn--";
   w:=obj_new(5); "--wartosci kolumn--";
   s:=0; "--razem--";
   sg:=0; "--razem grupa--";
   dokl:=0; "--dokladnosc ilosci--";
   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   tryb:=rep_export();
   k2:=obj_new(12); "--szerokosci kolumn--";
   w2:=obj_new(12); "--wartosci kolumn--";
   PRN2:=obj_new(@.CLASS.PRINT,12); PRN2.s_frame();

   k[1]:= 21;
   k[2]:= 77;
   k[3]:= 5;
   k[4]:= 18;
   k[5]:= 12;
   w[1]:=w[2]:=w[3]:=w[4]:=w[5]:='';
   k2[1]:=k2[2]:=k2[3]:=k2[4]:=k2[5]:=k2[6]:=k2[7]:=k2[8]:=k2[9]:=k2[10]:=9;
   k2[11]:=10;k2[12]:=12;
   w2[1]:=w2[2]:=w2[3]:=w2[4]:=w2[5]:=w2[6]:=w2[7]:=w2[8]:=w2[9]:=w2[10]:=w2[11]:='';
   {? (_wyb:=FUN.choice('Czy filtrować kartotekę materiałową?'@,,'Tak'@,'Nie'@))>0
   ||
      wyjscie:=0;
      {? _wyb=1 || HELP.FM:='T' || HELP.FM:='N' ?}
   ||
      wyjscie:=1
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_m','material',HELP.FM,'N','T',1) ?}
}
{END:
   sm_03(); VAR_DEL.delete('sm_03')
}
{IF: wyjscie=1}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{
PRN.add("w[1]",k[1],'Indeks'@);
PRN.add("w[2]",k[2],'Nazwa materiału'@);
PRN.add("w[3]",k[3],'jm'@);
PRN.add("w[4]",k[4],'Atrybuty'@);
PRN.add("w[5]",k[5],'Stan ilościowy'@,1);

PRN2.add("w2[1]",k2[1],'Atrybut 1'@);
PRN2.add("w2[2]",k2[2],'Atrybut 2'@);
PRN2.add("w2[3]",k2[3],'Atrybut 3'@);
PRN2.add("w2[4]",k2[4],'Atrybut 4'@);
PRN2.add("w2[5]",k2[5],'Atrybut 5'@);
PRN2.add("w2[6]",k2[6],'Atrybut 6'@);
PRN2.add("w2[7]",k2[7],'Atrybut 7'@);
PRN2.add("w2[8]",k2[8],'Atrybut 8'@);
PRN2.add("w2[9]",k2[9],'Atrybut 9'@);
PRN2.add("w2[10]",k2[10],'Atrybut 10'@);
PRN2.add("w2[11]",k2[11],'Magazyn'@,1);
PRN2.add("w2[12]",k2[12],'Stan ilościowy'@,1);

VAR_DEL.delete('X_Xc');
X_Xc:=tab_tmp(1,
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'N'      ,'STRING[100]'  ,'Nazwa',
   'JM'     ,'STRING[5]'   ,'jm',
   'ATR'    ,'STRING[50]'  ,'Atrybut',
   'S'      ,'REAL'        ,'Stan',
   'MGR'    ,'STRING[8]'   ,'Grupa materiałowa',
   'A01'    ,'STRING[50]'  ,'Atrybut 01',
   'A02'    ,'STRING[50]'  ,'Atrybut 02',
   'A03'    ,'STRING[50]'  ,'Atrybut 03',
   'A04'    ,'STRING[50]'  ,'Atrybut 04',
   'A05'    ,'STRING[50]'  ,'Atrybut 05',
   'A06'    ,'STRING[50]'  ,'Atrybut 06',
   'A07'    ,'STRING[50]'  ,'Atrybut 07',
   'A08'    ,'STRING[50]'  ,'Atrybut 08',
   'A09'    ,'STRING[50]'  ,'Atrybut 09',
   'A10'    ,'STRING[50]'  ,'Atrybut 10');

X_Xd:=tab_tmp(1,
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'MAG'    ,'STRING[20]'  ,'Magazyn',
   'S'      ,'REAL'        ,'Stan',
   'WAR01','STRING[25]','Wartość 01',
   'WAR02','STRING[25]','Wartość 02',
   'WAR03','STRING[25]','Wartość 03',
   'WAR04','STRING[25]','Wartość 04',
   'WAR05','STRING[25]','Wartość 05',
   'WAR06','STRING[25]','Wartość 06',
   'WAR07','STRING[25]','Wartość 07',
   'WAR08','STRING[25]','Wartość 08',
   'WAR09','STRING[25]','Wartość 09',
   'WAR10','STRING[25]','Wartość 10');
X_Xd.index(X_Xd.ndx_tmp(,,'KTM',,,'WAR01',,,'WAR02',,,'WAR03',,,'WAR04',,,'WAR05',,,'WAR06',,,'WAR07',,,'WAR08',,,
     'WAR09',,,'WAR10',,,'MAG',,));

_max_a:=0;

"--obliczenia--";
{? X_Xa.first()
||
   X_Xb:=sql('
      select SM.M M, sum(SM.S) as S
      from SM join M using(SM.M,M.REFERENCE) join MG using(SM.MAG,MG.REFERENCE)
      where MG.ODDZ=\':_a\' and SM.S<>0 group by SM.M',ST.ODDZ);
   X_Xb.index(X_Xb.ndx_tmp(,,'M',,));
   M.cntx_psh();
   M.prefix();
   {!
   |? M.seek(BB.sqlint(X_Xa.REF),);
      echo('Trwają obliczenia dla materiału: '@+M.KTM+' '+M.N);
      X_Xb.prefix(X_Xa.REF);
      {? X_Xb.first()
      ||
         {!
         |? X_Xc.blank();
            X_Xc.KTM:=M.KTM;
            X_Xc.N:=M.N;
            X_Xc.JM:=M.J().KOD;
            X_Xc.MGR:=M.MGR().KOD;
            X_Xc.S:=X_Xb.S;
            {? M.M_ATR<>null()
            || X_Xc.ATR:=M.M_ATR().SYM;
               {! _ii:=1..10
               |! {? ($('M.M_ATR().SL_'+form(_ii,-2,0,'99')))()<>null
                  || _fml:=$('X_Xc.A'+form(_ii,-2,0,'99')+':=M.M_ATR().SL_'+form(_ii,-2,0,'99')+'().NA');
                     _fml();
                     {? _max_a<_ii
                     || _max_a:=_ii
                     ?}
                  ?}
               !}
            ?};
            X_Xc.add();
            {? M.M_ATR<>null()
            || MG.cntx_psh();
               MG.index('MAG');
               MG.prefix(ST.ODDZ);
               {? MG.first()
               || {!
                  |? MG.cntx_psh();
                     exec('sc_tymczas','magazyn_stan',MG.ref(),M.ref());
                     pom_tab:=sql('select sum(s) as s, war01, war02, war03, war04, war05, war06, war07, war08, '+
                        'war09, war10'+
                        ' from :_a group by war01, war02, war03, war04, war05, war06, war07, war08, war09, war10',
                        __sc);
                     {? pom_tab.first()
                     || {!
                        |? {? pom_tab.S<>0
                           || X_Xd.KTM:=M.KTM;
                              X_Xd.S:=pom_tab.S;
                              X_Xd.MAG:=MG.SYM;
                              {! _ii:=1..10
                              |! _fml:=$('X_Xd.WAR'+form(_ii,-2,0,'99')+':=pom_tab.WAR'+form(_ii,-2,0,'99'));
                                 _fml()
                              !};
                              X_Xd.add()
                           ?};
                           pom_tab.next()
                        !}
                     ?};
                     &pom_tab;
                     MG.cntx_pop();
                     MG.next()
                  !}
               ?};
               MG.cntx_pop()
            ?};
            X_Xb.next()
         !}
      ?};
      X_Xa.next()
   !};
   M.cntx_pop();
   echo()
?};

PAR_WYDR.TITLE:='ZESTAWIENIE STANÓW MAGAZYNOWYCH WEDŁUG ATRYBUTÓW'@;
prn1:='PRN';
prn2:='PRN2'
;~~}
{IF: X_Xc.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział '@+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: HELP.FM='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
{<{lmt+1}}{'Stan na: '@+$date()+', '+$time()}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {IF: prn1='PRN2'}
   {SUBSTITUTE: 'LINIAF'}
   {FI}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xc}
{DO}
   {FIRST}
   {
   dokl:=exec('fld_prec','#field',X_Xc,'S');
   kolejny:=0;~~}
   {FIRST-}
   {
   w[1]:=form(X_Xc.KTM);
   w[2]:=form(X_Xc.N);
   w[3]:=form(X_Xc.JM);
   w[4]:=form(X_Xc.ATR);
   w[5]:=form(X_Xc.S,,dokl,'.,');
   sg+=X_Xc.S;
   s+=X_Xc.S;
   ~~}
   {IF: kolejny=1}
     {kolejny:=0;~~}
     {_f:=prn1+'.length()';il_lin:=($(_f))()+3;~~}
     {{? X_Xc.ATR<>''|| il_lin+=2 ?};~~}
     {IF: lineleft()<il_lin}
       {PAGE}
       {vp:=1;~~}
     {ELSE}
       {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
       {rsep:=0;~~}
     {FI}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
   { rsep:=PAR_WYDR.RSEP;vp:=0;~~}
   {IF: X_Xc.ATR<>''}
     {
     w2[1]:=form(X_Xc.A01);
     w2[2]:=form(X_Xc.A02);
     w2[3]:=form(X_Xc.A03);
     w2[4]:=form(X_Xc.A04);
     w2[5]:=form(X_Xc.A05);
     w2[6]:=form(X_Xc.A06);
     w2[7]:=form(X_Xc.A07);
     w2[8]:=form(X_Xc.A08);
     w2[9]:=form(X_Xc.A09);
     w2[10]:=form(X_Xc.A10);
     w2[11]:='Magazyn';
     w2[12]:=''
     ;~~}
     {SUBSTITUTE: 'LINIA02'}
     {prn1:='PRN2';~~}
     {FOR: X_Xd}
     {PREFIX: X_Xc.KTM,}
     {DO}
       {_f:=prn1+'.length()';il_lin:=($(_f))()+3;~~}
       {IF: lineleft()>0 & lineleft()<il_lin}
         {PAGE}
         {prn1:='PRN';rsep:=PAR_WYDR.RSEP;~~}
         {
         w2[1]:=form(X_Xc.A01);
         w2[2]:=form(X_Xc.A02);
         w2[3]:=form(X_Xc.A03);
         w2[4]:=form(X_Xc.A04);
         w2[5]:=form(X_Xc.A05);
         w2[6]:=form(X_Xc.A06);
         w2[7]:=form(X_Xc.A07);
         w2[8]:=form(X_Xc.A08);
         w2[9]:=form(X_Xc.A09);
         w2[10]:=form(X_Xc.A10);
         w2[11]:='Magazyn';
         w2[12]:=''
         ;~~}
         {SUBSTITUTE: 'LINIA02'}
         {prn1:='PRN2';~~}
       {FI}
       {
       w2[1]:=form(X_Xd.WAR01);
       w2[2]:=form(X_Xd.WAR02);
       w2[3]:=form(X_Xd.WAR03);
       w2[4]:=form(X_Xd.WAR04);
       w2[5]:=form(X_Xd.WAR05);
       w2[6]:=form(X_Xd.WAR06);
       w2[7]:=form(X_Xd.WAR07);
       w2[8]:=form(X_Xd.WAR08);
       w2[9]:=form(X_Xd.WAR09);
       w2[10]:=form(X_Xd.WAR10);
       w2[11]:=form(X_Xd.MAG);
       w2[12]:=form(X_Xd.S,,dokl,'.,')
       ;~~}
       {SUBSTITUTE: 'LINIA0'}
       {rsep:=0;~~}
     {OD}
     {SUBSTITUTE: 'LINIAF'}
     {prn1:='PRN';rsep:=PAR_WYDR.RSEP;vp:=0;~~}
   {ELSE}
     {SUBSTITUTE: 'LINIAF'}
     {vp:=0;~~}
   {FI}
   {kolejny:=1;~~}
{OD}
{IF: tryb<>1}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {FOOTER}{FOOTER-}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {
   PRN.n_frame();
   w[1]:='';
   w[2]:='Razem:'@; PRN.FORM[2]:=0;
   w[3]:=w[4]:='';
   w[5]:=form(s,,2,'.,')
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
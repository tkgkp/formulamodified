:!UTF-8
{TITLE:A. Wydruk etykiet towarowych - kody kreskowe}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   materean:="VAR_DEL.delete('temp','font','wybierz','dalej','wyjscie','etykieta')";
   materean();
   temp:='';
   font:='';
   wybierz:='';
   dalej:=0;
   M.win_patt('SZUK');
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=exec('barcode_height','kody_kresk');
   etykieta:=PAR_WYDR.URZ_LAB;
   PAR_WYDR.URZ_LAB:=exec('wyb_etykiety','kody_kresk');
   wyjscie:={? PAR_WYDR.TYPDR<>null || PAR_WYDR.URZ_LAB=null ?};
   {? wyjscie=0 & PAR_WYDR.TYPDR=null & PAR_WYDR.KODKRESK=''
   || FUN.info('Brak parametryzacji drukowania kodów kreskowych.'@);
      wyjscie:=1
   ?};
   exec('ini_kom','#message','Wydruk etykiet towarowych - kody kreskowe'@);
   M.cntx_psh
}
{END:
   M.cntx_pop;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   exec('end_kom','#message');
   PAR_WYDR.URZ_LAB:=etykieta;
   materean(); VAR_DEL.delete('materean')
}
{EXIT: wyjscie=1}
{ wyjscie:=1;~~}
{DEFINEFONT: 'courier=TexGyreCursor,,,8'}
{FONT: 'courier'}
{MACRO:'stan'}
   {ENTIRE}
   {M.N}

    {IF: M.KODK<>''}
     {BARCODE: M.KODK;50;BARCODE_H;BARCODE_TYPE}


     {FONT: 'courier'}{SPACE: 'B'}
      {M.KODK}
   {FI}
  ─────────────────────────────────────
   {ENTIRE-}
{MACRO-}
{MACRO:'etykiety'}
{IF: M.KODK=''}
   { exec('add_kom','#message','Indeks: '+M.KTM,7,'Brak kodu kreskowego'@);~~}
{ELSE}
   { wyjscie:=0;~~}
   {IF: PAR_WYDR.TYPDR<>null}
      { wyjscie:=exec('druk','kody_kresk')=0;~~}
   {ELSE}
      {SUBSTITUTE: 'stan'}
   {FI}
{FI}
{MACRO-}
{EXIT: wybierz:=FUN.choice('Wydruk etykiet dla towarów'@,,' Bieżącego'@, 'Wszystkich'@, 'W&ybranych'@, 'Wg w&zorca'@);
 {? wybierz <> 0
 || {? wybierz = 1
    || wybierz := 'B'
    |? wybierz = 2
    || wybierz := 'W'
    |? wybierz = 3
    || wybierz := 'Y'
    |? wybierz = 4
    || wybierz := 'Z'
    ?};0
  || "# koniec #"; 1
 ?}
}
{IF:wybierz='B'}
   {SUBSTITUTE: 'etykiety'}
{ELIF:wybierz='Y'}
   {
   _wer:=M.mk_sel('Materiały'@,,0,'lldddppppsdsadf',,,,,'U');
   M.win_fld(_wer,,'KTM',,,20,,,'Indeks'@);
   M.win_fld(_wer,,'N',,,20,,,'Nazwa'@);
   M.win_fld(_wer,,'KODK',,,14,,,'Kod kreskowy'@);
   _fbg:="
      VAR_DEL.delete('__Sel');
      __Sel:=M.sel_aget; M.sel_adel;
      {? __Sel.first=0
      || __Sel.REF:=#M.ref;
         __Sel.add
      ?};
      sel_exit
   ";
   M.win_act(_wer,,'Formuła','Akceptuj'@,,'Akceptacja wybranych pozycji'@,"",,1,1,_fbg);

   M.index('RODZ'); M.prefix('T');
   M.win_sel(_wer);

   {? M.select=0
   || VAR_DEL.delete('__Sel');
      __Sel:=M.sel_aget; M.sel_adel
   ?};
   M.cntx_psh;
   M.prefix;
   ~~}
   {FOR: __Sel}
   {DO}
      {IF: M.seek(__Sel.REF,)}
         {SUBSTITUTE: 'etykiety'}
      {FI}
   {OD}
   {
   M.cntx_pop;
   VAR_DEL.delete('__Sel');
   ~~}
{ELIF:wybierz='W'}
   {FOR: 'M'}
   {INDEX: 'RODZ'}
   {PREFIX: 'T'}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{ELIF:wybierz='Z'}
   {FOR: 'M'}
   {INDEX: 'RODZ'}
   {PREFIX: 'T'}
   {FILTER}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{FI}
{EXIT: {? wybierz='W' | wybierz='Z' || {? wyjscie=1 || FUN.info('Brak danych spełniających kryteria.'@); 1 ?} ?}}
{EXIT: wyjscie=1}
{EXIT: PAR_WYDR.TYPDR<>null}

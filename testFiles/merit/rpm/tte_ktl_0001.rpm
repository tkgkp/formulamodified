:!UTF-8
{TITLE:1. Karta technologiczna}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  dalej:=dalej1:=dalej2:=druk:=druk1:=0;
  VAR_DEL.delete('PRNMAT','PRNOPER','PRNTCH','PRNTLS','PRNPAR','PRNATR','PRNSUM','__TMP');
  "---Obiekt wydruku pozycji kalkulacji karty technologicznej.";
  PRNMAT := obj_new(@.CLASS.PRINT,16); PRNMAT.sl_frame();
  "---Obiekt wydruku operacji karty technologicznej.";
  PRNOPER := obj_new(@.CLASS.PRINT,18); PRNOPER.sl_frame();
  "Obiekt wydruku zamiennikow dla surowcow.";
  PRNTCH := obj_new(@.CLASS.PRINT,16); PRNTCH.sl_frame();
  "---Obiekt wydruku narzedzi karty technologicznej.";
  PRNTLS := obj_new(@.CLASS.PRINT,13); PRNTLS.sl_frame();
  "---Obiekt wydruku parametrow karty technologicznej.";
  PRNPAR := obj_new(@.CLASS.PRINT,7); PRNPAR.sl_frame();
  "---Obiekt wydruku atrybutow karty technologicznej.";
  PRNATR := obj_new(@.CLASS.PRINT,10); PRNATR.sl_frame();
  PRNSUM:= obj_new(@.CLASS.PRINT,7); PRNSUM.sl_frame();
  TKTL.cntx_psh();TKTLW.cntx_psh();
  VAR.A_KTL:={? VAR.ACTION='3' || TKTLW.TKTL || TKTL.ref() ?};
  VAR.A_T:={? VAR.ACTION='3' || TKTLW.KTM || VAR.A_KTL().KTM ?};
  __TMP:=tab_tmp(1,'UNROP','REAL','Unik.nr operacji',
                   'PLNX','REAL','koszt na XJM',
                   'PLNH','REAL','koszt na H',
                   'NTIME','REAL','norma czasowa',
                   'MTIME','REAL','Czas maszynowy');
  "---Inicjalizacja obiektu 'tpar' klasy 'TPAr', sluzacego do operacji na     ";
  "---parametrach karty technologicznej - produkt glowny karty";
  exec('start_tpar','tech_param',VAR.A_T);
  i:=0; ctrl_s:=ctrl_t:=0; "pomocnicze";
  nrop:='';wew:='T';poziom:=0;
  pierwszy:=0;
  rodz:=obj_new(4);
  rodz[1]:='N'; rodz[2]:='P'; rodz[3]:='U'; rodz[4]:='I';
  sumplnx:=0;
  "---Suma z pol 'Placa na godz'.";
  sumplnh:=0;
  "---Suma z pol 'Norma czasowa'.";
  sumntime:=0;
  atryb:=' ';
  "---Suma z pol 'Czas maszynowy'.";
  sumnst:=0;
   pop:='';
  nas:='';
  "---'TACTTLS' - drukowane sa narzedzia, 'TOPER' - drukowane sa operacje.";
  sumpzew:=0;
  FAZY:=tab_tmp(1,'KOD','STRING[10]','Nazwa Fazy','NR','INTEGER','Ref do fazy');
  fazy:=FAZY.ndx_tmp(,1,'KOD','',0,'KOD',,0);
  __NDX:=TOPER.ndx_tmp(,1,'NRK',,,'NRNOP',,,'NROP',,,'TPZ',,);
  FAZY.index(fazy);
  numer:='';
  prns2o:=0; "1 - drukować surowce do operacji, 0 - nie drukować.";
  prnt2o:=0; "1 - drukować narzędzia do operacji, 0 - nie drukować.";
  buf_unrop:=VAR.A_UNROP;
  ktlnr := TKTL.NRK;
  f:=0;
  "---Jednostka miary.";
  jm:=djm:='';
  "---Okresla sposob drukowania norm materialowych, czasowych oraz zuzycia:   ";
  "---V - wartosci, A - algorytmy.";
  paramprt:='V';
  "---Okresla taka liczbe parametrow karty technologicznej, przy ktorej       ";
  "---wydruk tabelki tych parametrow bedzie podzielony na dwie tabele,        ";
  "---lezace obok siebie.";
  pwrap:=5;
  "---Liczba parametrow karty technologicznej jako zmienna.";
  pc:=0;
  "---Zmienna pomocnicza do przechowywania liczby rekordow tabeli.";
  tsize:=0;
  head_var:=0;kreska := '─';
  prn1:='';prn2:='PRNSUM';
  color:='';pchange:="";poziom:="";pcount:="";
  pwrapcount:="";
  lm:=ll:=vp:=lmt:=rsep:=lk:=0;
  pkpar:=pkalmat:=pkaloper:=1;
  norma:='W';zam:='T';jedn:='P'
}
{END:
 TKTL.cntx_pop();TKTLW.cntx_pop();
  "---Sekcja zwalniania obiektow.";
  VAR_DEL.delete('PRNMAT','PRNOPER','PRNTCH','PRNTLS','PRNPAR','PRNATR','PRNSUM','__TMP');
  FAZY.ndx_drop(fazy);
  obj_del(FAZY);
  obj_del(rodz);
"---Sekcja zwalniania funkcji.";
  &pchange;
  &atryb;
  &pop;
  &nas;
  &pcount; &poziom;
  &pwrapcount; &kreska;
  "---Sekcja zwalniania pozostalych zmiennych.";
  "---Usuniecie obiektu sluzacego do operacji na parametrach karty            ";
  "---technologicznej.";
  exec('stop_tpar','tech_param');
  TOPER.ndx_drop(__NDX);
  &__NDX;
  &ctrl_s; &ctrl_t;
  &nrop;
  &prns2o; &prnt2o;
  &sumplnx;
  &sumplnh;
  &sumntime;
  &f;
  &sumnst;
  &sumpzew;&wew;
  VAR.A_UNROP:=&buf_unrop;
  &ktlnr;
  &jm;&djm;&i;
  &numer;
  &paramprt;
  &pwrap;
  &norma;
  &zam;
  &jedn;
  &dalej1;
  &dalej2;
  &dalej;
  &pierwszy;
  &pc;
  &tsize;
  &head_var;
  &prn1;&prn2;
  &color;
  &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
  Wydruk karty technologicznej.
  [SS] & [TS] - 2000/07/12 - [7.22]
  [SS] - 2001/03/15 - [7.41] - uaktualnienie zwiazane z parametryzacja
    norm materialowych surowcow, norm czasowych operacji oraz zuzycia narzedzi.
  [MKO] - 2001/11/08 - [7.53] uaktualnienie związane z dodaniem
               fazy produkcji do operacji, surowców i narzędzi OWwPR013/5.1
               nowa wersja graficzna wydruku OWwSK003_Aneks1
  [MKO] - 2001/11/19 - [7.53] OWwPR007/5.2
  [MKO] - 2001/11/29 - [7.53] Uwaga_30
  [MKO] - 2002/01/03 - [7.53] OWwPR024/5.1
  [MKO] - 2002/06/28 - [7.60] atrybuty do operacji
  [TS] - 2004/03/17 - [8.50] (233: Surowce powierzone)
                               PR/WRT/PRODUKCJ/7.53/0002
  Wywołanie: Karty technologiczne -> Drukuj
{COMMENT-}
{ {? TKTL.TYP().PAR='T'
  || WYDRUK.win_edit('RED1'); _wydr_naz:='ktl_0001'
  || WYDRUK.win_edit('RED7'); _wydr_naz:='ktl_001a'
  ?};
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.NWART:=WYDRUKIL.SORTUJ;
     WYDRUK.JM:=WYDRUKIL.SR;
     WYDRUK.FAZY:=WYDRUKIL.FAK;
     WYDRUK.ZAM:=WYDRUKIL.RODZFAK
  ?};
~~}
{IF: WYDRUK.edit()}
{ WYDRUKIL.SORTUJ:=WYDRUK.NWART;
  WYDRUKIL.SR:=WYDRUK.JM;
  WYDRUKIL.FAK:=WYDRUK.FAZY;
  WYDRUKIL.RODZFAK:=WYDRUK.ZAM;
  WYDRUKIL.put(1);
  zam:=WYDRUK.ZAM;
  jedn:=WYDRUK.JM;
  f:=WYDRUK.FAZY='T';
  {? TKTL.TYP().PAR='T'
  || norma:=WYDRUK.NWART
  || norma:='W'
  ?};
~~}
{ELSE}
{EXIT}
{FI}
{INCLUDE: tte_ktx_0001.rpi}
{IF: f=1}
{SUBSTITUTE: 'utw_fazy'}
{FI}
{"---Zbior funkcji wykorzystywanych w biezacej definicji wydruku.         "; ~~}
{MACRO: 'functions'}
  {"--------------------------------------------------------------------------";
   "---Wyswietla okienko z pytaniem, czy drukowac wartosci czy algorytmy      ";
   "---na surowcach, operacjach i narzedziach (odpowiednio - normy            ";
   "---materialowe, normy czasowe oraz zuzycie).                              ";
   "---Jezeli zostalo wybrane drukowanie wartosci, to wyswietla okienko       ";
   "---redakcyjne, aby umozliwic redakcje domyslnych parametrow karty.        ";
   "---Zwraca 2, jezeli beda drukowane algorytmy - 1, jezeli wartosci,        ";
   "---natomiast 0, jezeli operator przerwal dzialanie wydruku.";
   pchange:="
     _result:=0;
     _result:={? norma='W' || 1 || 2 ?};
     {? _result=1
     || _result:=tpar.change()
     ?};
     _result
   ";
   "--------------------------------------------------------------------------";
   "---Zwraca liczbe parametrow do biezacej karty technologicznej.";
   pcount:="
     TPAR.index('NN');
     TPAR.prefix(TKTL.ref());
     TPAR.size()
   ";
   "--------------------------------------------------------------------------";
   "---Zwraca liczbe pozycji w tabelce parametrow karty technologicznej       ";
   "---w zaleznosci od tego, czy tabelka bedzie zalamana, czy tez nie.        ";
   "---Parametry:                                                             ";
   "--- _a:NUMBER - liczba wszystkich parametrow karty technologicznej        ";
   "--- _b:NUMBER - liczba parametrow, przy ktorej tabelka zostanie zalamana. ";
   "---Jezeli wystapi blad, zwraca -1.";
    pwrapcount:="
     _result:=0;
     {? _<2
     || _result:=-1;
        FUN.emsg('Funkcja PWRAPCOUNT wydruku KTL_0001.RPM:\nniewłaściwa liczba parametrów.')
     ?};
     {? _result<>-1
     || {? _a>=_b
        || _result:=_a%2 + 1*(_a%*2<>0)
        || _result:=_a
        ?}
     ?};
     _result
   ";
prns2o:=(TKTL.TYP().SUR='O');
prnt2o:=(TKTL.TYP().UTIL='O');
"------Drukowane wszystkie surowce (bez i z przypisaną operacją)";
prns2o:=prnt2o:=0;
   "--------------------------------------------------------------------------";
  ~~}
{MACRO-}
{SUBSTITUTE: 'functions'}
{"------------------------------------------------------------------------"; ~~}
{"---Okresl sposob drukowania norm wg parametrow.                         "; ~~}
{IF: pcount()>0}
  {IF: (paramprt:=pchange())=0}
    {EXIT}
  {ELSE}
    {IF: paramprt=1}
      {paramprt:='V'; ~~}
    {ELIF: paramprt=2}
      {paramprt:='A'; ~~}
    {ELSE}
      {EXIT}
    {FI}
  {FI}
{ELSE}
  {paramprt:='V'; ~~}
{FI}
{"---Definicja wiersza wydruku surowców TMAT.                             "; ~~}
{PRNMAT.add("form(TMAT.LP,,0,'9')",9,'Lp. Surowca',1); ~~}
{{? prns2o;1 || PRNMAT.add("numer",9,'OPERACJA') ?};~~}
{PRNMAT.add("  {? ~(-TMAT.GRKTM)='G'
               || ' '+TMAT.TGDFLT().PT().R
               || ' '+TMAT.PT().R
               ?}
             ",3,'WMP'); ~~}
{"Grupa, czy samodzielny KTM."; ~~}
{PRNMAT.add("  {? ~(-TMAT.GRKTM)='G'
               || TMAT.TGRP().GR+' - '+TMAT.TGRP().OPIS
               || '-'
               ?}
             ",20,'Grupa technologiczna'); ~~}
{PRNMAT.add("  {? ~(-TMAT.GRKTM)='G'
               || {? jedn='P' || jm:=TMAT.TGDFLT().PT().J().KOD || djm:=TMAT.DJM().KOD ?};
                  TMAT.TGDFLT().PT().KTM+' - '+(40+TMAT.TGDFLT().PT().N)
               || {? jedn='P' || jm:=TMAT.PT().J().KOD|| djm:=TMAT.DJM().KOD ?};
                  TMAT.PT().KTM+' - '+(40+TMAT.PT().N)
               ?}
             ",30,'Domyślny KTM'); ~~}
{PRNMAT.add("  {? paramprt='V'
               || {? jedn='P' ||
                  {? form(TMAT.FORMN)<>''
                  || form(tpar.calc(TMAT.FORMN),,4)
                  || form(TMAT.WARN,,4)
                  ?}
                  ||
                  {? form(TMAT.DFORMN)<>''
                  || form(tpar.calc(TMAT.DFORMN),,4)
                  || form(TMAT.DWARN,,4)
                  ?}
                  ?}
               || {? jedn='P' || TMAT.FORMN || TMAT.DFORMN ?}
               ?}
             ",{? paramprt='V' || 15 || 20 ?},'Norma materiałowa netto',paramprt='V'); ~~}
{PRNMAT.add("  {? paramprt='V'
               || {? jedn='P' ||
                  {? form(TMAT.FORMB)<>''
                  || form(tpar.calc(TMAT.FORMB),,4)
                  || form(TMAT.WARB,,4)
                  ?}
                  ||
                  {? form(TMAT.DFORMB)<>''
                  || form(tpar.calc(TMAT.DFORMB),,4)
                  || form(TMAT.DWARB,,4)
                  ?}
                  ?}
               || {? jedn='P' || TMAT.FORMB || TMAT.DFORMB ?}
               ?}
             ",{? paramprt='V' || 15 || 20 ?},'Norma materiałowa brutto',paramprt='V'); ~~}
{ PRNMAT.add("{? jedn='P' || jm || djm ?}",5,{? jedn='P'||'JM'||'DJM'?});
  PRNMAT.add(" {? TMAT.RKTL<>''
               || _what:=exec('FindAndGet','#table',TKTL,TMAT.RKTL,,\"NRK+' / '+WER\",'');
                  _what
               || ''
               ?}
              ",20,'Karta technologiczna (nr/wersja)');
  PRNMAT.add("'  '+TMAT.PODST",6,'Podst.');
  PRNMAT.add(" {? paramprt='V'
               || {? tpar.calc(TMAT.EXIST) || ' T' || ' N' ?}
               || TMAT.EXIST
               ?}
              ",{? paramprt='V' || 3 || 10 ?},'Akt');
  PRNMAT.add("'  '+TMAT.PARTIA",5,'Part.');
  {? f=0 || PRNMAT.add("TMAT.PFAZ().KOD",10,'Faza produkcji') ?};
  PRNMAT.add("TMAT.LIMIT",3,'Lim');
  PRNMAT.add("TMAT.SO",3,'S/O');
  PRNMAT.add("TMAT.POW",6,'Powie- rzony'); ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Definicja wiersza wydruku zamienników dla surowców TCHMAT.           "; ~~}
{PRNTCH.add("'Zamiennik '+form(TMAT.LP,,0,'9')+'.'+form(TCHMAT.LP,,0,'9')",9,'Lp. Surowca',1); ~~}
{{? prns2o;1 || PRNTCH.add("numer",9,'OPERACJA') ?};~~}
{PRNTCH.add("  {? ~(-TCHMAT.GRKTM)='G'
               || ' '+TCHMAT.TGDFLT().PT().R
               || ' '+TCHMAT.PT().R
               ?}
             ",3,'WMP'); ~~}
{"Grupa, czy samodzielny KTM."; ~~}
{PRNTCH.add("  {? ~(-TCHMAT.GRKTM)='G'
               || TCHMAT.TGRP().GR+' - '+TMAT.TGRP().OPIS
               || '-'
               ?}
            ",20,'Grupa technologiczna'); ~~}
{PRNTCH.add("  {? ~(-TCHMAT.GRKTM)='G'
               ||{? jedn='P' || jm:=TCHMAT.TGDFLT().PT().J().KOD || djm:=TCHMAT.DJM().KOD ?};
                  TCHMAT.TGDFLT().PT().KTM+' - '+(40+TCHMAT.TGDFLT().PT().N)
               ||{? jedn='P' || jm:=TCHMAT.PT().J().KOD || djm:=TCHMAT.DJM().KOD ?};
                  TCHMAT.PT().KTM+' - '+(40+TCHMAT.PT().N)
               ?}
            ",30,'Domyślny KTM'); ~~}
{PRNTCH.add("  {? paramprt='V'
               || {? jedn='P' ||
                    {? form(TCHMAT.FORMN)<>''
                    || form(tpar.calc(TCHMAT.FORMN),,4)
                    || form(TCHMAT.WARN,,4)
                    ?}
                  ||
                    {? form(TCHMAT.DFORMN)<>''
                    || form(tpar.calc(TCHMAT.DFORMN),,4)
                    || form(TCHMAT.DWARN,,4)
                    ?}
                  ?}
               || {? jedn='P' || TCHMAT.FORMN || TCHMAT.DFORMN ?}
               ?}
             ",{? paramprt='V' || 15 || 20 ?},'Norma materiałowa netto',paramprt='V'); ~~}
{PRNTCH.add("  {? paramprt='V'
               || {? jedn='P' ||
                    {? form(TCHMAT.FORMB)<>''
                    || form(tpar.calc(TCHMAT.FORMB),,4)
                    || form(TCHMAT.WARB,,4)
                    ?}
                  ||
                    {? form(TCHMAT.DFORMB)<>''
                    || form(tpar.calc(TCHMAT.DFORMB),,4)
                    || form(TCHMAT.DWARB,,4)
                    ?}
                  ?}
               || {? jedn='P' || TCHMAT.FORMB || TCHMAT.DFORMB  ?}
               ?}
             ",{? paramprt='V' || 15 || 20 ?},'Norma materiałowa brutto',paramprt='V'); ~~}
{ PRNTCH.add("{? jedn='P' || jm || djm ?}",5,{? jedn='P'||'JM'||'DJM'?});
  PRNTCH.add("
   _result:=exec('FindAndGet','#table',TKTL,TCHMAT.RKTL,,\"NRK\",'');
   _result
  ",20,'Karta technologiczna (nr/wersja)');
  PRNTCH.add("''",6,'Podst.');
  PRNTCH.add(" {? paramprt='V'
               || {? tpar.calc(TCHMAT.EXIST) || ' T' || ' N' ?}
               || TCHMAT.EXIST
               ?}
              ",{? paramprt='V' || 3 || 10 ?},'Akt');
  PRNTCH.add("TCHMAT.PARTIA",5,'Part.');
  {? f=0 || PRNTCH.add("TCHMAT.SRC().PFAZ().KOD",10,'Faza produkcji')?};
  PRNTCH.add("''",3,'Lim');
  PRNTCH.add("''",3,'S/O');
  PRNTCH.add("TCHMAT.POW",6,'Powie- rzony'); ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Definicja wiersza wydruku operacji TOPER.                            "; ~~}
{PRNOPER.add("nrop+$TOPER.NROP",5,'Nr'); ~~}
{PRNOPER.add("form(pop-2,14)+'#'+ 14*kreska+ '#'+ form(nas-2,14)",15,'Poprzedniki'+'#'+ 14*kreska+ '#'+'Następniki',1,'#');~~}
{PRNOPER.add("'   '+{? ~(-form(TOPER.PZ))='P' || 'T' || 'N' ?}",2,'Pr'); ~~}
{PRNOPER.add(" {? ~(-form(TOPER.PZ))='P' & wew='T'
               || TOPER.OPER().NA || TOPER.NA +{? wew='N' || ' - zewn.' || '' ?}
               ?}
              ",17,'Nazwa'); ~~}
{PRNOPER.add("{? wew='T' || TOPER.PLACE().KOD+' - '+TOPER.PLACE().NA || '-' ?}",14,'Stanowisko'); ~~}
{PRNOPER.add("{? wew='T' || '  '+TOPER.ALL || '-' ?}",4,'Równ'); ~~}
{PRNOPER.add("{? wew='T' || '  '+TOPER.BRYG || '-'?}",4,'Bryg'); ~~}
{PRNOPER.add("{? wew='T' || TOPER.OPER().ZAWOD().ZD || '-' ?}",14,'Zawód prac.'); ~~}
{PRNOPER.add("{? wew='T' || form(TOPER.OPER().TKAT().KAT,,0,'9') || '-' ?}",6,'Kat. zaszer',1); ~~}
{PRNOPER.add("{? wew='T'
              || form(__TMP.PLNX,,4)+'#'+ 16*kreska+ '#'+'-'
              || '-'+'#'+16*kreska+'#'+ form(TOPER.CENA,15,4)
              ?} ",17,'Pł.na '+form(TKTL.XJM,,2)+' '+TKTL.JM().KOD + '#'+ 17*kreska+ '#'
+ 'Cena na '+form(TKTL.XJM,,2)+TKTL.JM().KOD ,1,'#'); ~~}
{PRNOPER.add("{? wew='T' & TOPER.PZ='P'
              || form(__TMP.PLNH,,4)+'#'+ 14*kreska+ '#' + form(TOPER.KH,,4)
              || '-'+'#'+ 14*kreska+ '#'+'-'
              ?}",15,'Płaca na godz'+'#'+ 14*kreska+ '#'+'Koszt st.na    godz',1,'#'); ~~}
{PRNOPER.add("{? wew='T' & TOPER.PZ='P'
              || {? TOPER.FMTIME<>''
                 || {? TOPER.MTIME<>0 & TOPER.KX<>0
                    || form(tpar.calc(TOPER.FMTIME)/TOPER.MTIME*TOPER.KX,,4)
                    || 0
                    ?}
                 || form(TOPER.KX,,4)
                 ?}
              || '-'
              ?}",15,'Koszt st.na     '+form(TKTL.XJM,,2)+TKTL.JM().KOD,1,'#'); ~~}
{PRNOPER.add("{? wew='T'
              || {? paramprt='V'
                 || form(__TMP.NTIME,14,exec('get','#params',500604,1))+'#'+ 14*kreska+ '#'+ form(__TMP.MTIME,14,exec('get','#params',500604,1))
                 || {? TOPER.PZ='P'
                    || form(TOPER.FNTIME,14,exec('get','#params',500604,1)) +'#'+ 14*kreska+ '#'+ form(TOPER.FMTIME,14,exec('get','#params',500604,1))
                    || '-' +'#'+ 14*kreska+ '#'+ '-'
                    ?}
                 ?}
              || '-'+'#'+ 14*kreska+ '#'+'-'
              ?}",15,'Norma czasowa'+'#'+ 14*kreska+ '#'+'Czas maszynowy',paramprt='V','#'); ~~}
{PRNOPER.add("{? wew='T' & TOPER.PZ='P'
              || {? paramprt='V'
                 || {? TOPER.FCOEF<>'' || form(tpar.calc(TOPER.FCOEF),,4) || form(TOPER.COEF,,4) ?}
                 || {? TOPER.PZ='P' || TOPER.FCOEF ||'-' ?}
                 ?}
              || '-'
              ?}",{? paramprt='V' || 10 || 15 ?},'Wsp. PLN/h',paramprt='V','#'); ~~}
{PRNOPER.add("{? wew='T' || ' '+TOPER.TPZ || '-' ?}",3,'TPZ'); ~~}
{PRNOPER.add("{? wew='T'
              || {? paramprt='V'
                 || {? tpar.calc(TOPER.EXIST) || ' T' || ' N' ?}
                 || TOPER.EXIST
                 ?}
              || '-'?}",{? paramprt='V' || 3 || 10 ?},'Akt'); ~~}
{{? f=0 || PRNOPER.add("TOPER.PFAZ().KOD",7,'Faza prod.') ?}; ~~}
{PRNOPER.add("form(atryb-1)",9,'Atryb.'+' '+'#'+ 'Naz-War',,'#');~~}
{"------------------------------------------------------------------------"; ~~}
{"---Definicja wiersza wydruku narzędzi TACTTLS.                          "; ~~}
{{? prnt2o;1 || PRNTLS.add("numer",9,'OPERACJA') ?};~~}
{PRNTLS.add("  TACTTLS.M().KTM+' - '+TACTTLS.M().N",40,'Narzędzie'); ~~}
{PRNTLS.add("  {? paramprt='V'
               || {? TACTTLS.FILE<>'' || form(tpar.calc(TACTTLS.FILE),,0) || form(TACTTLS.ILE,,0) ?}
               || TACTTLS.FILE
               ?}
             ",{? paramprt='V' || 6 || 30 ?},'Ilość',paramprt='V');~~}
{PRNTLS.add("  {? paramprt='V'
               || {? TACTTLS.FZXJM<>'' || form(tpar.calc(TACTTLS.FZXJM),,4) || form(TACTTLS.ZXJM,,4) ?}
               || TACTTLS.FZXJM
               ?}
              ",{? paramprt='V' || 18 || 30 ?},'Zużycie na JM',paramprt='V'); ~~}
{PRNTLS.add("{? paramprt='V'
             || {? TACTTLS.FZH<>'' || form(tpar.calc(TACTTLS.FZH),,4) || form(TACTTLS.ZH,,4) ?}
             || TACTTLS.FZH
             ?}
             ",{? paramprt='V' || 18 || 30 ?},'Zużycie na godz.',paramprt='V'); ~~}
{PRNTLS.add("  {? paramprt='V'
               || {? tpar.calc(TACTTLS.EXIST) || ' T' || ' N' ?}
               || TACTTLS.EXIST
               ?}
            ",{? paramprt='V' || 3 || 30 ?},'Akt'); ~~}
{{? f=0 || PRNTLS.add("TACTTLS.PFAZ().KOD",10,'Faza prod.')?}; ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Definicja wiersza wydruku parametrow karty technologicznej.          "; ~~}
{PRNPAR.add("'p('+$(tpar.P[1][i])+')'",10,'Symbol'); ~~}
{PRNPAR.add("tpar.P[3][i]",40,'Opis'); ~~}
{PRNPAR.add("form(tpar.P[2][i],25,10)",25,'Wartość',1); ~~}
{pc:=pcount(); ~~}
{"---Tabela bedzie zalamana tylko wowczas, gdy liczba parametrow osiagnie "; ~~}
{"---wartosc 'pwrap'.                                                     "; ~~}
{IF: pwrap<=pcount()}
  {PRNPAR.add("''",1,''); ~~}
  {PRNPAR.add("{? i+pc%2+1*(pc%*2<>0)<=pc
               || 'p('+$(tpar.P[1][ i+pc%2+1*(pc%*2<>0) ])+')'
               || ''
               ?}",10,'Symbol'); ~~}
  {PRNPAR.add("{? i+pc%2+1*(pc%*2<>0)<=pc
               || tpar.P[3][ i+pc%2+1*(pc%*2<>0) ]
               || ''
               ?}",40,'Opis'); ~~}
  {PRNPAR.add("{? i+pc%2+1*(pc%*2<>0)<=pc
               || form(tpar.P[2][ i+pc%2+1*(pc%*2<>0) ],25,10)
               || ''
               ?}",25,'Wartość',1); ~~}
{FI}
{"---Definicja wiersza wydruku atrybutow karty technologicznej.           "; ~~}
{"------------------------------------------------------------------------"; ~~}
{ PRNATR.add("UAT.TYP().NA",20,'Typ');
  PRNATR.add("exec('decode','tech_atr'); ATR.TWAR",58,'Wartość');
  PRNSUM.add("'Podsumowanie dla operacji pierwszego poziomu:'",89,'Tytul podsumowania');
  PRNSUM.add(" form(sumplnx,14,4)+ '#'+16*kreska+ '#' +form(sumpzew,14,4)",17,'Płaca na XJM',1,'#');
  PRNSUM.add("''",31,'',1);
  PRNSUM.add(" {? paramprt='V'|| form(sumntime,14,exec('get','#params',500604,1))+'#'+15*kreska+'#'+form(sumnst,14,exec('get','#params',500604,1))|| '-'+'#'+15*kreska+'#'+'-'?}",15,'',1,'#');
 {? f=0
 || PRNSUM.add("' '",36+{?paramprt='V'||0||12?},'')
 || PRNSUM.add("' '",28+{?paramprt='V'||0||12?},'')
 ?}; ~~}
{"------------------------------------------------------------------------"; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{ prn1:='PRNOPER'; ~~}
{PAR_WYDR.TITLE:='KARTA TECHNOLOGICZNA NR: '+TKTL.NRK+' WERSJA '+TKTL.WER; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRNOPER.width())/2); ~~}
{<{lmt+1} 'Produkt:  '+ VAR.A_T().KTM+' - '+M.N}
{IF:TKTL.DEFAULT='T'}
 {<{lmt+1} 'Technologia domyślna'}
{FI}
{<{lmt+1} 'Karta kalkulowana na:  '+ form(TKTL.XJM,,2)+' '+  TKTL.JM().KOD }
{<{lmt+1} 'Domyślny wariant kalkulacji:  ' + {? form(TKTL.DEF_OPCK().NAZ)<>''
                                              || TKTL.DEF_OPCK().NAZ
                                              || '<brak>'
                                              ?}}
{IF:TKTL.GKTL<>null()}
 {<{lmt+1} 'Grupa technologii:  '+ TKTL.GKTL().GR+' - '+TKTL.GKTL().OPIS }
{FI}
{<{lmt+1} 'Opis: '}{<{lmt+5} TKTL.OPIS}
{<{lmt+1} {? TKTL.TYP().PAR='T' || 'Karta parametryzowana' || 'Karta nieparametryzowana'?}}
{<{lmt+1} 'Zatwierdzona końcowo: ' + {? -TKTL.STAN='t' || 'TAK' || 'NIE' ?}}
{<{lmt+1} 'Surowce karty drukowane według '+{?jedn='P' || 'podstawowych' ||'dodatkowych'?}+' jednostek miar'}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'B'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Czy drukować zamienniki dla surowców.                                "; ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Pomocnicze makra            "; ~~}
{"---Makro wypisujace operacje podrzedne wraz z odpowiadajacymi numerami            "; ~~}
{MACRO: 'operacje'}
{TOPER.cntx_psh();~~}
{TOPER.index(__NDX);TOPER.prefix(TKTL.ref(),VAR.A_UNROP); ~~}
{IF: TOPER.first()}{ dalej:=1;~~}
{FI}
{WHILE: dalej}
 {DO}
 {IF: TOPER.ACT='T'  & tpar.calc(TOPER.EXIST)=1}
 {IF: f=0 | (f=1 & TOPER.PFAZ().KOD=FAZY.KOD)}
 { nas:='';
   pop:='';
   NASTOPER.cntx_psh();
   TOPER.cntx_psh();
   NASTOPER.index('OPER');
   NASTOPER.prefix('T',TKTL.ref(),TOPER.ref());
   {? NASTOPER.first()
   || {!|?
        nas+=NASTOPER.SCIEZKA+', ';
        NASTOPER.next()
      !}
   ?};
   NASTOPER.index('UNROP');
   NASTOPER.prefix('T',TKTL.ref(),TOPER.ref());
   {? NASTOPER.first()
   || {!|?
        pop+=exec('get_oper_nr','tech_oper',NASTOPER.OPER().UNROP)+', ';
        NASTOPER.next()
      !}
   ?};
   NASTOPER.cntx_pop();
   TOPER.cntx_pop();
   __TMP.prefix(TOPER.UNROP);
   __TMP.first();~~}
 {IF: TOPER.NRNOP=0}
 {sumplnx+=__TMP.PLNX; ~~}
 {sumplnh+=__TMP.PLNH; ~~}
 {sumntime+=__TMP.NTIME; ~~}
 {sumnst+=__TMP.MTIME; ~~}
 {FI}
 {wew:=~-(TOPER.WEW);atryb:=''; ~~}
  {UAT.clear();
       UAT.index('TR');
       UAT.prefix('TOPER*',#TOPER.ref);
       {? UAT.first()
       ||
         {! |?
          exec('decode','tech_atr');
            _naz:=UAT.TYP().NA+'-'+ATR.TWAR;
            atryb+={? +_naz>15
                    || form(_naz,(ceil((+_naz)/15)*15))+'#'
                    || form(_naz,15)+'#'
                    ?};
            UAT.next
          !}
       ?}
      ;~~}
   {IF: wew='N'}{sumpzew+=TOPER.CENA;~~}{FI}
   {IF: lineleft > 9} {SUBSTITUTE: 'LINIA0'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE:'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {FI}
   {FI}
   {VAR.A_UNROP:=TOPER.UNROP; ~~}
   {nrop+=$TOPER.NROP+'\\';dalej:=0; ~~}
    {SUBSTITUTE: 'operacje'}
   {ELSE}
   {VAR.A_UNROP:=TOPER.UNROP; ~~}
   {nrop+=$TOPER.NROP+'\\';dalej:=0; ~~}
   {FI}
   {ctrl:=+($TOPER.NROP+'\\');nrop:=nrop-ctrl; ~~}
   {dalej:=TOPER.next();~~}
  {OD}
{TOPER.cntx_pop();~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Makro wypisujace surowce przypis. do operacji wraz z odpowiadajacymi im numerami            "; ~~}
{MACRO: 'oper'}
{TOPER.cntx_psh();~~}
{TOPER.index(__NDX); TOPER.prefix(VAR.A_KTL,VAR.A_UNROP); ~~}
{IF: TOPER.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
  {DO}
    {IF: TOPER.ACT='T'  & tpar.calc(TOPER.EXIST)=1}
    {VAR.A_UNROP:=TOPER.UNROP; ~~}
    {numer+=$TOPER.NROP+'\\';dalej:=0; ~~}
    {TMAT.index('NNL'); TMAT.prefix(VAR.A_KTL,TOPER.ref);dalej2:=0;~~}
    {IF: TMAT.first()}{ dalej2:=1; prn1:='PRNMAT';druk:=TMAT.size; ~~}{FI}
    {WHILE: dalej2}
    {DO}
    {IF: TMAT.ACT='T'  & tpar.calc(TMAT.EXIST)=1 & {? TMAT.NROP<>null ||  tpar.calc(TMAT.NROP().EXIST)=1 ||1 ?}}
    {IF: f=0 | (f=1 & (TMAT.PFAZ().KOD=FAZY.KOD))}
     {IF: lineleft > 7} {SUBSTITUTE: 'LINIA0'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE:'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
     {FI}
    {FI}
     {IF: zam='T'}
     {TCHMAT.index('NSL'); TCHMAT.prefix(VAR.A_KTL,TMAT.ref()); ~~}
     {IF: TCHMAT.first()}{ dalej1:=1; prn1:='PRNTCH'; ~~}{FI}
     {WHILE: dalej1}
     {DO}
     {IF: TCHMAT.ACT='T'  & tpar.calc(TCHMAT.EXIST)=1 & {? TCHMAT.NROP<>null ||  tpar.calc(TCHMAT.NROP().EXIST)=1 ||1 ?}}
     {IF: f=0 | (f=1 & (TCHMAT.SRC().PFAZ().KOD=FAZY.KOD))}
       {IF: lineleft > 7} {SUBSTITUTE: 'LINIA0'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE:'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
      {FI}
     {FI}
      {dalej1:=TCHMAT.next();~~}
     {OD}
     {FI}
    {FI}
    {prn1:='PRNMAT';~~}
    {dalej2:=TMAT.next();~~}
  {OD}
{SUBSTITUTE: 'oper'}
{FI}
{ ctrl:=+($TOPER.NROP+'\\');numer:=numer-ctrl; ~~}
{dalej:=TOPER.next(); ~~}
{OD}
{TOPER.cntx_pop();~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Makro wypisujace narzedzia przypis. do operacji wraz z odpowiadajacymi im numerami"; ~~}
{MACRO: 'opernarz'}
{TOPER.cntx_psh();~~}
{TOPER.index('NNN'); TOPER.prefix(TKTL.ref(),VAR.A_UNROP); ~~}
{IF: TOPER.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
    {IF: TOPER.ACT='T' & tpar.calc(TOPER.EXIST)=1}
    {VAR.A_UNROP:=TOPER.UNROP; ~~}
    {numer+=$TOPER.NROP+'\\';dalej:=0;poziom+=1; ~~}
    {TACTTLS.index('KNROPR'); TACTTLS.prefix(TKTL.ref(),TOPER.ref,exec('rodz2mgr','tech_tool',rodz[iter]));dalej2:=0;~~}
    {IF: TACTTLS.first()}{ dalej2:=1; prn1:='PRNTLS';pierwszy:=0; ~~}
    {WHILE: dalej2}
    {DO}
    {IF: TACTTLS.ACT='T' & tpar.calc(TACTTLS.EXIST)=1 & {? TACTTLS.NROP<>null || tpar.calc(TACTTLS.NROP().EXIST)=1 || 1 ?} }
     {IF: f=0 | (f=1 & (TACTTLS.PFAZ().KOD=FAZY.KOD))}
      {IF: pierwszy=0}
         {IF: (poziom=1 & bylo=0) | ( poziom>1 & bylo=0)}
         {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
         {IF: lineleft<14}{PAGE}{FI}
         {IF: rodz[iter]='N'}{<1>{lk} 'NARZĘDZIA KARTY TECHNOLOGICZNEJ'}{FI}
         {IF: rodz[iter]='P'}{<1>{lk} 'PRZYRZĄDY KARTY TECHNOLOGICZNEJ'}{FI}
         {IF: rodz[iter]='U'}{<1>{lk} 'URZĄDZENIA KARTY TECHNOLOGICZNEJ'}{FI}
         {IF: rodz[iter]='I'}{<1>{lk} 'INNE N-P-U KARTY TECHNOLOGICZNEJ'}{FI}
         {IF: lineleft < 7}{PAGE}{FI}
          {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;bylo:=1;pierwszy:=1;ok:=1; ~~}
        {FI}
      {FI}
     {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
     {FI}
     {FI}
     {FI}
     {dalej2:=TACTTLS.next(); ~~}
    {OD}
    {FI}
    {SUBSTITUTE: 'opernarz'}
    {FI}
    { ctrl:=+($TOPER.NROP+'\\');numer:=numer-ctrl;poziom-=1;~~}
    {dalej:=TOPER.next();~~}
{OD}
{IF: ok & poziom=0} {SUBSTITUTE: 'LINIAF'}{FI}
{TOPER.cntx_pop();~~}
{MACRO-}
{IF: f=0}
{"------------------------------------------------------------------------"; ~~}
{"---Tabelka operacji do wybranej karty technologicznej.";~~}
{TOPER.index(__NDX); TOPER.prefix(TKTL.ref(),VAR.A_UNROP); ~~}
{IF: TOPER.first()}
  {FONT: 'W12'}{SPACE: 'A'}
{<1 lk:=charleft(); ~~}
  {IF: lineleft < 14}{PAGE}{FI}
  {<1>{lk} 'OPERACJE KARTY TECHNOLOGICZNEJ'}
  {FONT: 'T8GB'}{SPACE: 'B'}
  {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
  {SUBSTITUTE: 'sumop'}
  {SUBSTITUTE: 'operacje'}
   {IF: lineleft > 9} {SUBSTITUTE: 'LINIAFS'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE:'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIAFS'}
   {FI}
{FI}
{"------------------------------------------------------------------------"; ~~}
{"---Gdy istnieją surowce do karty technologicznej, to wydrukuj.          "; ~~}
{TKTL.cntx_psh(); TOPER.cntx_psh();~~}
{"---Tabelka surowcow do wybranej karty technologicznej.                "; ~~}
{TMAT.index('NNL'); TMAT.prefix(TKTL.ref()); ~~}
{IF: TMAT.first()}
  { dalej:=1; prn1:='PRNMAT';~~}
{<1 lmt:=int((charleft()-PRNMAT.width())/2); ~~}
  {IF: ~prns2o}
  {IF: lineleft < 14}{PAGE}{FI}
  {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
  {<1>{lk} 'SUROWCE KARTY TECHNOLOGICZNEJ'}
  {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
  {WHILE: dalej}
    {DO}
    {IF: TMAT.ACT='T' & tpar.calc(TMAT.EXIST)=1 & {? TMAT.NROP<>null ||  tpar.calc(TMAT.NROP().EXIST)=1 ||1 ?}}
    {numer:=exec('get_oper_nr','tech_oper',TMAT.NROP().UNROP,'\\','n'); ~~}
    {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
    {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
    {FI}
    {IF: zam='T'}
      {TCHMAT.index('NSL'); TCHMAT.prefix(VAR.A_KTL,TMAT.ref()); ~~}
      {IF: TCHMAT.first()}{ dalej1:=1; prn1:='PRNTCH';~~}{FI}
      {WHILE: dalej1}
       {DO}
       {IF: TCHMAT.ACT='T' & tpar.calc(TCHMAT.EXIST)=1 & {? TCHMAT.NROP<>null ||  tpar.calc(TCHMAT.NROP().EXIST)=1 ||1 ?}}
       {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
       {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
       {FI}
       {FI}
       {dalej1:=TCHMAT.next();~~}
       {OD}
     {FI}
     {prn1:='PRNMAT';~~}
     {FI}
     { dalej:=TMAT.next();druk-=1;~~}
    {OD}
    {ELSE}
     {IF: lineleft < 14}{PAGE}{FI}
     {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
     {<1>{lk} 'SUROWCE PRZYPISANE DO OPERACJI'}
     {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
     {VAR.A_UNROP:=0;~~}
     {SUBSTITUTE: 'oper'}
   {FI}
 {SUBSTITUTE: 'LINIAF'}
{FI}
{TKTL.cntx_pop(); TOPER.cntx_pop(); ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Gdy istnieją narzędzia do karty technologicznej, to wydrukuj.        "; ~~}
{TOPER.cntx_psh(); iter:=1;~~}
{WHILE: iter<=4}
{DO}
{TACTTLS.index('KNROPR2'); TACTTLS.prefix(TKTL.ref()); ~~}
{IF: TACTTLS.first()}
  {dalej:=1; prn1:='PRNTLS';~~}
{<1 lmt:=int((charleft()-PRNTLS.width())/2); ~~}
  {TOPER.index('NNN'); TOPER.prefix(TKTL.ref(),VAR.A_UNROP); ~~}
  {IF: ~prnt2o}
   {TACTTLS.prefix(TKTL.ref(),exec('rodz2mgr','tech_tool',rodz[iter]))}
    {IF: TACTTLS.first()}
    {IF: lineleft < 14}{PAGE}{FI}
    {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
    {IF: rodz[iter]='N'}{<1>{lk} 'NARZĘDZIA KARTY TECHNOLOGICZNEJ'}{FI}
    {IF: rodz[iter]='P'}{<1>{lk} 'PRZYRZADY KARTY TECHNOLOGICZNEJ'}{FI}
    {IF: rodz[iter]='U'}{<1>{lk} 'URZĄDZENIA KARTY TECHNOLOGICZNEJ'}{FI}
    {IF: rodz[iter]='I'}{<1>{lk} 'INNE N-P-U KARTY TECHNOLOGICZNEJ'}{FI}
    {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
    {WHILE: dalej}
    {DO}
      {IF: TACTTLS.ACT='T'  & tpar.calc(TACTTLS.EXIST)=1 & {? TACTTLS.NROP<>null ||  tpar.calc(TACTTLS.NROP().EXIST)=1 ||1 ?}}
      {numer:=exec('get_oper_nr','tech_oper',TACTTLS.NROP().UNROP,'\\','n'); ~~}
      {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
      {FI}
      {dalej:=TACTTLS.next();~~}
    {OD}
    {TACTTLS.prefix(TKTL.ref);~~}
    {SUBSTITUTE: 'LINIAF'}
    {FI}
   {ELSE}
    {VAR.A_UNROP:=0;poziom:=0;ok:=0;bylo:=0;~~}
 {SUBSTITUTE: 'opernarz'}
 {FI}
{FI}
{iter+=1;~~}
{OD}
{TOPER.cntx_pop(); ~~}
{ELSE}
 {SUBSTITUTE: 'sumop'}
{FOR: FAZY}
{DO}
 {FONT: 'W12'}{SPACE: 'A'}
{<1 lk:=charleft();~~}
  {PFAZ.seek(FAZY.NR,'pfazypr');~~}
  {IF: lineleft < 14}{PAGE}{FI}
  {IF: FAZY.KOD<>''}
  {<1>{lk} 'FAZA '+ FAZY.KOD+'  -  '+PFAZ.OPIS}
  {FI}

  {FONT: 'T8G'}{SPACE: 'B'}
  {VAR.A_UNROP:=0;prn1:='PRNOPER';
   sumplnx:=0;sumplnh:=0;
  sumntime:=0;sumnst:=0;sumpzew:=0;~~}
{<1 lmt:=int((charleft()-PRNOPER.width())/2); ~~}
  {"---Tabelka operacji do wybranej karty technologicznej.";~~}
  {TOPER.index(__NDX); TOPER.prefix(TKTL.ref(),VAR.A_UNROP);~~}
  {IF: TOPER.first()}
    {FONT: 'W12'}{SPACE: 'A'}
{<1 lk:=charleft(); ~~}
    {IF: lineleft < 14}{PAGE}{FI}
    {<1>{lk} 'OPERACJE KARTY TECHNOLOGICZNEJ'}
    {FONT: 'T8GB'}{SPACE: 'B'}
    {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
   {SUBSTITUTE: 'operacje'}
     {IF: lineleft > 9} {SUBSTITUTE: 'LINIAFS'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE:'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIAFS'}
   {FI}
  {FI}
{"------------------------------------------------------------------------"; ~~}
{"---Gdy istnieją surowce do karty technologicznej, to wydrukuj.          "; ~~}
{TKTL.cntx_psh(); TOPER.cntx_psh(); ~~}
{"---Tabelka surowcow do wybranej karty technologicznej.                "; ~~}
{TMAT.index('NNL'); TMAT.prefix(TKTL.ref()); ~~}
{IF: TMAT.first()}
  { dalej:=1; prn1:='PRNMAT';pierwszy:=0;~~}
{<1 lmt:=int((charleft()-PRNMAT.width())/2); ~~}
  {IF: ~prns2o}
  {WHILE: dalej}
    {DO}
    {IF: TMAT.ACT='T' & tpar.calc(TMAT.EXIST)=1 & {? TMAT.NROP<>null ||  tpar.calc(TMAT.NROP().EXIST)=1 ||1 ?}}
    {numer:=exec('get_oper_nr','tech_oper',TMAT.NROP().UNROP,'\\','n'); ~~}
    {IF: f=0 | (f=1 & (TMAT.PFAZ().KOD=FAZY.KOD))}
     {IF: pierwszy=0}
      {IF: lineleft < 14}{PAGE}{FI}
      {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
      {<1>{lk} 'SUROWCE KARTY TECHNOLOGICZNEJ'}
      {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; pierwszy:=1;~~}
     {FI}
    {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
    {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
    {FI}
    {IF: zam='T'}
      {TCHMAT.index('NSL'); TCHMAT.prefix(VAR.A_KTL,TMAT.ref()); ~~}
      {IF: TCHMAT.first()}{ dalej1:=1; prn1:='PRNTCH';~~}{FI}
      {WHILE: dalej1}
       {DO}
       {IF: TCHMAT.ACT='T' & tpar.calc(TCHMAT.EXIST)=1 & {? TCHMAT.NROP<>null ||  tpar.calc(TCHMAT.NROP().EXIST)=1 ||1 ?}}
       {IF: f=0 | (f=1 & (TCHMAT.SRC().PFAZ().KOD=FAZY.KOD))}
       {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
       {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
       {FI}
       {FI}
       {FI}
       {dalej1:=TCHMAT.next();~~}
       {OD}
     {FI}
    {FI}
     {prn1:='PRNMAT';~~}
     {FI}
     { dalej:=TMAT.next();druk-=1;~~}
    {OD}
    {ELSE}
     {IF: lineleft < 14}{PAGE}{FI}
     {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
     {<1>{lk} 'SUROWCE PRZYPISANE DO OPERACJI'}
     {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
     {VAR.A_UNROP:=0;pierwszy:=1;~~}
     {SUBSTITUTE: 'oper'}
   {FI}
    {IF: pierwszy=1}
      {SUBSTITUTE: 'LINIAF'}
    {FI}
 {FI}
{TKTL.cntx_pop(); TOPER.cntx_pop(); ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Gdy istnieją narzędzia do karty technologicznej, to wydrukuj.        "; ~~}
{TOPER.cntx_psh(); iter:=1;~~}
{WHILE: iter<=4}
{DO}
{TACTTLS.index('KNROPR2'); TACTTLS.prefix(TKTL.ref()); ~~}
{IF: TACTTLS.first()}
  {dalej:=1; prn1:='PRNTLS';pierwszy:=0;~~}
{<1 lmt:=int((charleft()-PRNTLS.width())/2); ~~}
  {TOPER.index('NNN'); TOPER.prefix(TKTL.ref(),VAR.A_UNROP); ~~}
  {IF: ~prnt2o}
   {TACTTLS.prefix(TKTL.ref(),exec('rodz2mgr','tech_tool',rodz[iter]))}
    {IF: TACTTLS.first()}
    {WHILE: dalej}
    {DO}
    {IF: TACTTLS.ACT='T'  & tpar.calc(TACTTLS.EXIST)=1 & {? TACTTLS.NROP<>null ||  tpar.calc(TACTTLS.NROP().EXIST)=1 ||1 ?}}
    {numer:=exec('get_oper_nr','tech_oper',TACTTLS.NROP().UNROP,'\\','n'); ~~}
    {IF: f=0 | (f=1 & (TACTTLS.PFAZ().KOD=FAZY.KOD))}
      {IF: pierwszy=0}
       {IF: lineleft < 14}{PAGE}{FI}
       {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
       {IF: rodz[iter]='N'}{<1>{lk} 'NARZĘDZIA KARTY TECHNOLOGICZNEJ'}{FI}
       {IF: rodz[iter]='P'}{<1>{lk} 'PRZYRZADY KARTY TECHNOLOGICZNEJ'}{FI}
       {IF: rodz[iter]='U'}{<1>{lk} 'URZĄDZENIA KARTY TECHNOLOGICZNEJ'}{FI}
       {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; pierwszy:=1;~~}
      {FI}
      {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
      {FI}
      {FI}
      {dalej:=TACTTLS.next();~~}
    {OD}
    {TACTTLS.prefix(TKTL.ref);~~}
    {IF: pierwszy=1}
      {SUBSTITUTE: 'LINIAF'}
    {FI}
    {FI}
   {ELSE}
    {VAR.A_UNROP:=0;poziom:=0;ok:=0;bylo:=0;pierwszy:=0;~~}
 {SUBSTITUTE: 'opernarz'}
 {FI}
{FI}
{iter+=1;~~}
{OD}
{TOPER.cntx_pop(); ~~}
{OD}
{FI}
{"------------------------------------------------------------------------"; ~~}
{"---Wydruk wiersza tabelki atrybutow.                                    ";~~}
{ ATR.TACR:='TKTL*';ATR.TREF:=#TKTL.ref(); ATR.TMSK:=ref_name(TKTL.ref())+'*';~~}
{ UAT.index('TR'); UAT.prefix(ATR.TACR,ATR.TREF,ATR.TMSK); ~~}
{IF: UAT.first()}{ dalej:=1; prn1:='PRNATR';~~}
{<1 lmt:=int((charleft()-PRNATR.width())/2); ~~}
  {IF: lineleft < 14}{PAGE}{FI}
  {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
  {<1>{lk} 'ATRYBUTY KARTY TECHNOLOGICZNEJ'}
  {FONT: 'T8GB'}{SPACE: 'B'}
  {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
  {WHILE: dalej}
  {DO}
    {IF: lineleft > 8} {SUBSTITUTE: 'LINIA0'}
    {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
    {FI}
    {dalej:=UAT.next();~~}
  {OD}
  {SUBSTITUTE: 'LINIAF'}
{FI}
{"------------------------------------------------------------------------"; ~~}
{"---Tutaj wydruk parametrow karty technologicznej.                       "; ~~}
{IF: pcount()>0}
  {TPAR.index('NN'); TPAR.prefix(TKTL.ref()); ~~}
  {IF: TPAR.first()}{ dalej:=1; prn1:='PRNPAR';~~}
{<1 lmt:=int((charleft()-PRNPAR.width())/2); ~~}
    {IF: lineleft < 14}{PAGE}{FI}
    {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
    {<1>{lk} 'SPIS PARAMETRÓW KARTY TECHNOLOGICZNEJ'}
    {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
    {i:=1; ~~}
    {WHILE: i<=pwrapcount(pc,pwrap)}
    {DO}
     {IF: lineleft > 8} {SUBSTITUTE: 'LINIA0'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
     {FI}
     {i+=1; ~~}
     { dalej:=TPAR.next();~~}
   {OD}
   {SUBSTITUTE: 'LINIAF'}
  {FI}
{FI}



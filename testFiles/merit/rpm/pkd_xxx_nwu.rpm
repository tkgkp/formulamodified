:!UTF-8
{TITLE:PDF. Wniosek o urlop}
{PRINTER: exec('prt','param_wydr','v'),,,,,'I','B',,,,,,1,5,5,5,5,0,1}
{BEGIN:
   VAR_DEL.delete('cil','szerl','par','cfg','__TAB');
   cil:=obj_new('HEAD','TITLE','T8','T8G','T10B','W12');
   szerl:=50;
   par:=obj_new(4);

   cfg:=obj_new('attr','tryb');
   'Zakładamy, że parametryzacja jest prawidłowa, tzn. nieobecność należy tylko do jednego z ww. typów.';
   cfg.attr:='';
   _arr:=json_parse('[1111,1112,1113,1121,1161,1162,1242]');
   {! _lp:=1 .. obj_len(_arr)
   |? cfg.attr=''
   |! {? __RUB.sys_attr(NWU.R,_arr[_lp],NWU.OD)
      || cfg.attr:=$_arr[_lp]
      ?}
   !};
   {? cfg.attr<>''
   || _par:=params_get();
      {? obj_ntab(_par) & var_pres('cntx',_par)>100 & obj_ntab(_par.cntx) & var_pres('SLO_NAZ',_par.cntx)>=0
      || _par.cntx.SLO_NAZ:=
            {? ',1111,1112,1113,'*',%1,' [cfg.attr]
            || 'Wniosek urlopowy'
            |? cfg.attr='1121'
            || 'Wniosek o urlop bezpłatny'
            |? cfg.attr='1161'
            || 'Wniosek o urlop okolicznościowy'
            |? cfg.attr='1162'
            || 'Wniosek o zwolnienie od pracy (Art. 188 Kp)'
            |? cfg.attr='1242'
            || 'Wniosek o opiekę bezpłatną'
            || ''
            ?}
      ?}
   ?};

   OSOBA.cntx_psh();
   OSOBA.prefix();
   P.cntx_psh();
   P.prefix();
   H.cntx_psh();
   H.index('_HISTKOD');
   H.prefix(P.ref(),'Z');
   __TAB:={? H.find_le(NWU.D) || H || P ?};
   STN.cntx_psh();
   UD_SKL.cntx_psh();
   ~~
}
{END:
   UD_SKL.cntx_pop();
   STN.cntx_pop();
   H.cntx_pop();
   P.cntx_pop();
   OSOBA.cntx_pop();
   VAR_DEL.delete('cil','szerl','par','cfg','__TAB');
   ~~
}
{COMMENT}---------------------------------------------------------------------------------------------------------------
Raport jest elementem czynności PKD_ZES_ZNWU - Załączniki wniosków urlopowych.
Jeżeli bieżący rekord tabeli NWU spełnia odpowiednie warunki, to drukowany jest wniosek i ewentualnie jego wycofanie.
W czynności wydruk zapisywany jest jako dokument PDF.
Jeżeli do wydruku przekazzany został parametr (params_set()) 'SLO_NAZ', to zostanie on uzupełniony typem załącznika,
związanym z daną nieobecnością.

Obsługiwane nieobecności:
   1111 - Urlop wypoczynkowy "na żądanie" [6pkt2]  Wniosek urlopowy
   1112 - Urlop wypoczynkowy              [6pkt2]  Wniosek urlopowy
   1113 - Dodatkowy urlop wypoczynkowy    [6pkt2]  Wniosek urlopowy
   1121 - Urlop bezpłatny                 [B]      Wniosek o urlop bezpłatny
   1161 - Urlop okolicznościowy           [B]      Wniosek o urlop okolicznościowy
   1162 - Opieka - Art. 188               [6pkt1]  Wniosek o zwolnienie od pracy (Art. 188 Kp)
   1242 - Opieka bezpłatna                ??????   Wniosek o opiekę bezpłatną

Podstawa prawna:
   * Kodeks pracy (Dz. U. 1974 Nr 24 poz. 141):
      1111 <- Art. 167^2.
      1112 <- Art. 152.
      1121 <- Art. 174. § 1
      1162 <- Art. 188. § 1
   * Ustawa o rehabilitacji zawodowej i społecznej oraz zatrudnianiu osób niepełnosprawnych
     (Dz. U. 1997 Nr 123 poz. 776)
      1113 <- Art. 19. 1
   * Rozporządzenie w sprawie sposobu usprawiedliwiania nieobecności w pracy oraz udzielania pracownikom
     zwolnień od pracy (Dz. U. 1996 Nr 60 poz. 281)
      1161 <- § 15.
      1242 <- § 3. 1)
   * Ustawa o świadczeniach pieniężnych z ubezpieczenia społecznego w razie choroby i macierzyństwa
      1242 <- Art. 55. 1, Art. 32
--------------------------------------------------------------------------------------------------------------{COMMENT-}
{EXIT: ~(cfg.attr<>'' & (NWU.AZ='T' | NWU.AZ='N' | NWU.AZ='W'))}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{FONT: 'HEAD'}{SPACE: 'B'}{ cil.HEAD:=charleft(); ~~}
{FONT: 'TITLE'}{SPACE: 'A'}{ cil.TITLE:=charleft(); ~~}
{FONT: 'T8'}{SPACE: 'B'}{ cil.T8:=charleft(); ~~}
{FONT: 'T8G'}{SPACE: 'C'}{ cil.T8G:=charleft(); ~~}
{FONT: 'T10B'}{SPACE: 'B'}{ cil.T10B:=charleft(); ~~}
{FONT: 'W12'}{SPACE: 'B'}{ cil.W12:=charleft(); ~~}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'text_out'}
{COMMENT}
   par[1] - lewy margines w pierwszym wierszu
   par[2] - lewy margines w kolejnych wierszach
   par[3] - prawy margines
   par[4] - tekst
{COMMENT-}
{JUSTIFY}
{ STR.split(par[4]); ~~ }
{WHILE: +(par[4]:=STR.line(par[3]-par[1]))}
{DO}
{REP:
   _rep:=
      {? +STR.spl_str
      || '{<'+$par[1]+'>'+$par[3]
      || '{CENTER}{<'+$par[1]
      ?}+' gsub(par[4],%255,%32)}';
   par[1]:=par[2];
   _rep
}
{OD}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'nag'}
{COMMENT}
   par[1] - treść
   par[2] - podpowiedź
{COMMENT-}
{LINE: 2}
{FONT: 'T10B'}{SPACE: 'B'}
{CENTER}
{ STR.split(par[1]); ~~}
{WHILE: +(par[1]:=STR.line(szerl))}
{DO}
   {<1>{szerl} par[1]}
{OD}
{FONT: 'W12'}{SPACE: 'C'}{<1 (szerl*1.7)*'.'}
{FONT: 'T8G'}{SPACE: 'C'}{<1>{szerl*cil.T8G/cil.T10B} par[2]}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{HEADER}
{IF: +PAR_WYDR.MIEJSC}{FONT: 'HEAD'}{SPACE: 'B'}
   {>{cil.HEAD} PAR_WYDR.MIEJSC+{? cfg.tryb='wniosek' || ', ' + NWU.OD$1 || '' ?}}
{FI}
{SUBSTITUTE:
   par[1]:=NWU.P().OSOBA().PIERWSZE+' '+OSOBA.NAZWISKO;
   par[2]:='Imię i nazwisko pracownika';
   'nag'
}
{SUBSTITUTE:
   par[1]:=__TAB.WYDZIAL().SYMBOL+' - '+__TAB.WYDZIAL().OPIS;
   par[2]:='Jednostka organizacyjna';
   'nag'
}
{SUBSTITUTE:
   par[1]:=__TAB.ST().ST;
   par[2]:='Stanowisko';
   'nag'
}
{LINE: 5}
{HEADER-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{LINE: 1}
{IF: NWU.AZ<>'W' | cfg.tryb<>'wniosek'}
   {REP:
      _txt:=NWU.AD$1+
         {? NWU.KTO_WER
         || ', '+NWU.KTO_WER().NAZWISKO+' '+OSOBA.PIERWSZE
         |? NWU.AO
         || ', '+NWU.AO().NAZWISKO+' '+OSOBA.PIERWSZE
         || ''
         ?};
      _dl:=+_txt;
      _lm:=int(0.75*cil.T8);
      {? cil.T8-_dl<_lm
      || _lm:=cil.T8-_dl
      ?};
      '{<'+$_lm+'>'+$cil.T8+' \''+_txt+'\'}'
   }
{FI}
{LINE: 1}
{LINE: 1}
{LINE: 1}
{LINE: 1}
{LINE: 1}
{LINE: 1}
{FONT: 'T8G'}{SPACE: 'C'}
{>{cil.T8G} 'Dokument sporządzony elektronicznie - podpisy niewymagane.'}
{FOOTER-}
{COMMENT}---------------------------------------------------------------------------------------------------------------
                                                Wniosek
--------------------------------------------------------------------------------------------------------------{COMMENT-}
{FONT: 'TITLE'}{SPACE: 'A'}{<1>{charleft()}
   cfg.tryb:='wniosek';
   {? ',1111,1112,1113,1121,'*',%1,' [cfg.attr]
   || 'Wniosek o udzielenie urlopu'
   || 'Wniosek o zwolnienie od pracy'
   ?}
}
{LINE: 1}
{FONT: 'T8'}{SPACE: 'B'}
{SUBSTITUTE:
   par[1]:=10;
   par[2]:=1;
   par[3]:=cil.T8;
   par[4]:=
      'Zwracam się z uprzejmą prośbą o udzielenie mi '+
      {? NWU.OD=NWU.DO
      || 'w dniu '+NWU.OD$6+%255+'roku '
      || 'w dniach od '+NWU.OD$6+%255+'roku do '+NWU.DO$6+%255+'roku '
      ?}+
      {? cfg.attr='1111'
      || 'urlopu wypoczynkowego w trybie "na żądanie"'
      |? cfg.attr='1112'
      || 'urlopu wypoczynkowego'
      |? cfg.attr='1113'
      || 'dodatkowego urlopu wypoczynkowego'
      |? cfg.attr='1121'
      || 'urlopu bezpłatnego'
      |? cfg.attr='1161'
      || 'zwolnienia od pracy (%1)' [{? NWU.KOMENT='' || 'szczególne okoliczności' || NWU.KOMENT ?}]
      |? cfg.attr='1162'
      || 'zwolnienia od pracy przysługującego z mocy art. 188 Kodeksu pracy'
      |? cfg.attr='1242'
      || 'zwolnienia od pracy z powodu konieczności osobistego sprawowania opieki nad dzieckiem / członkiem rodziny'
      || 'zwolnienia od pracy'
      ?}+'.';
   'text_out'
}
{LINE: 6}
{FONT: 'T8'}{SPACE: 'B'}
{SUBSTITUTE:
   par[1]:=10;
   par[2]:=1;
   par[3]:=cil.T8;
   par[4]:=
      {? NWU.AZ='N' || 'Nie wyrażam zgody' || 'Wyrażam zgodę' ?}+' na udzielenie '+
      {? ',1111,1112,1113,1121,'*',%1,' [cfg.attr] || 'urlopu' || 'zwolnienia od pracy' ?}+
      ' w wymiarze i terminie wskazanym we wniosku.';
   'text_out'
}
{IF: NWU.AZ='W'}{PAGE}{ELSE}{EXIT}{FI}
{COMMENT}---------------------------------------------------------------------------------------------------------------
                                                Wycofanie wniosku
--------------------------------------------------------------------------------------------------------------{COMMENT-}
{FONT: 'TITLE'}{SPACE: 'A'}{<1>{charleft()}
   cfg.tryb:='wycofanie';
   {? ',1111,1112,1113,1121,'*',%1,' [cfg.attr]
   || 'Wycofanie wniosku o udzielenie urlopu'
   || 'wycofanie wniosku o zwolnienie od pracy'
   ?}
}
{LINE: 1}
{FONT: 'T8'}{SPACE: 'B'}
{SUBSTITUTE:
   par[1]:=10;
   par[2]:=1;
   par[3]:=cil.T8;
   par[4]:=
      'Zwracam się z uprzejmą prośbą o wycofanie mojego wniosku o udzielenie mi '+
      {? NWU.OD=NWU.DO
      || 'w dniu '+NWU.OD$6+%255+'roku '
      || 'w dniach od '+NWU.OD$6+%255+'roku do '+NWU.DO$6+%255+'roku '
      ?}+
      {? cfg.attr='1111'
      || 'urlopu wypoczynkowego w trybie "na żądanie"'
      |? cfg.attr='1112'
      || 'urlopu wypoczynkowego'
      |? cfg.attr='1113'
      || 'dodatkowego urlopu wypoczynkowego'
      |? cfg.attr='1121'
      || 'urlopu bezpłatnego'
      |? cfg.attr='1161'
      || 'zwolnienia od pracy (%1)' [{? NWU.KOMENT='' || 'szczególne okoliczności' || NWU.KOMENT ?}]
      |? cfg.attr='1162'
      || 'zwolnienia od pracy przysługującego z mocy art. 188 Kodeksu pracy'
      |? cfg.attr='1242'
      || 'zwolnienia od pracy z powodu konieczności osobistego sprawowania opieki nad dzieckiem / członkiem rodziny'
      || 'zwolnienia od pracy'
      ?}+'.';
   'text_out'
}
{LINE: 6}
{FONT: 'T8'}{SPACE: 'B'}
{SUBSTITUTE:
   par[1]:=10;
   par[2]:=1;
   par[3]:=cil.T8;
   par[4]:=
      {? NWU.AZ='N' || 'Nie wyrażam zgody' || 'Wyrażam zgodę'?}+' na wycofanie wniosku o udzielenie '+
      {? ',1111,1112,1113,1121,'*',%1,' [cfg.attr] || 'urlopu' || 'zwolnienia od pracy' ?}+
      ' w wymiarze i terminie wskazanym we wniosku.';
   'text_out'
}
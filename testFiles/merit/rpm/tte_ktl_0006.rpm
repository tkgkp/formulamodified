:!UTF-8
{TITLE:3. Operacje procesu technologicznego}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  TKTL.cntx_psh();
  "---Obiekt wydruku operacji karty technologicznej.";
  PRNOPER := obj_new(@.CLASS.PRINT,17);PRNOPER.sl_frame();
  PRNSUM := obj_new(@.CLASS.PRINT,16);PRNSUM.sl_frame();
  "---Obiekt wydruku parametrow karty technologicznej.";
  PRNPAR := obj_new(@.CLASS.PRINT,7); PRNPAR.sl_frame();
  "---Inicjalizacja obiektu 'tpar' klasy 'TPAr', sluzacego do operacji na     ";
  "---parametrach karty technologicznej.";
  VAR.A_KTL:={? VAR.ACTION='3' || TKTLW.TKTL || TKTL.ref() ?};
  VAR.A_T:={? VAR.ACTION='3' || TKTLW.KTM || VAR.A_KTL().KTM ?};
  exec('start_tpar','tech_param',VAR.A_T);
  nrop:='';wew:='T';
  buf_unrop:=VAR.A_UNROP; kreska := '─';
  prnzam:=0; "1 - drukować zamienniki, 0 - nie drukować zamienników.";
  ktlnr := TKTL.NRK;
   __TMP:=tab_tmp(1,'UNROP','REAL','Unik.nr operacji',
                   'PLNX','REAL','koszt na XJM',
                   'PLNH','REAL','koszt na H',
                   'NTIME','REAL','norma czasowa',
                   'MTIME','REAL','Czas maszynowy');
  FAZY:=tab_tmp(1,'KOD','STRING[10]','Nazwa Fazy','NR','INTEGER','Ref do fazy');
  fazy:=FAZY.ndx_tmp(,1,'KOD','',0,'KOD',,0);
  __NDX:=TOPER.ndx_tmp(,1,'NRK',,,'NRNOP',,,'NROP',,,'TPZ',,);
  FAZY.index(fazy);
  i:=0; f:=0;
  data:=date();
  "---Suma z pol 'Placa na XJM'.";
  sumplnx:=0;
  "---Suma z pol 'Placa na godz'.";
  sumplnh:=0;
  "---Suma z pol 'Norma czasowa'.";
  sumntime:=0;
  "---Suma z pol 'Czas maszynowy'.";
  sumnst:=0;
  "---'TACTTLS' - drukowane sa narzedzia, 'TOPER' - drukowane sa operacje.";
  sumpzew:=0;
  tlsexist:=0;
  pop:='';
  nas:='';
  prn1:='PRNOPER'; prn2:='PRNSUM';
  waslbar:=0;
  dalej:=0;
  color:='';
  f:=FUN.ask('Rozbicie na fazy produkcji?'@);
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  TKTL.cntx_pop();
  obj_del(__TMP);
  obj_del(PRNOPER);
  obj_del(PRNSUM);
  obj_del(PRNPAR);
  FAZY.ndx_drop(fazy);
  obj_del(FAZY);
  "---Usuniecie obiektu sluzacego do operacji na parametrach karty            ";
  "---technologicznej.";
  exec('stop_tpar','tech_param');
  &nrop;
  VAR.A_UNROP:=&buf_unrop;
  TOPER.ndx_drop(__NDX);
  &__NDX;
  &prnzam;
  &i; &f;
  &data;
  &ktlnr;
  &sumplnx;
  &kreska;
  &pop;
  &nas;
  &wew;
  &sumplnh;
  &sumntime;
  &sumnst;
  &sumpzew;
  &tlsexist;
  &dalej;
  &prn1;&prn2;
  &color;
  &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
  Wydruk operacji i narzedzi procesu technologicznego.
  [SS] - 2000/09/01 - [7.22]
  [SS] - 2001/03/14 - [7.41] - uaktualnienie zwiazane z parametryzacja
    norm czasowych operacji oraz zuzycia narzedzi.
  [MKO] - 2001/11/13 - [7.53] OWwSK003_Aneks1
                       dodatkowo: rozdzielenie wydruku operacji i narzedzi na dwa rozne
  [MKO] - 2001/11/19 - [7.53] OWwPR007/5.2
  [MKO] - 2002/06/28 - [7.60] atrybuty o operacji
  Wywołanie: Karty technologiczne -> Drukuj
{COMMENT-}
{MACRO: 'utw_fazy'}
{ TOPER.clear();TOPER.index('NNN');TOPER.prefix(TKTL.ref,0);
{? TOPER.first()
   || {!|?
        FAZY.prefix(TOPER.PFAZ().KOD,TOPER.PFAZ().KOD);
         {? ~FAZY.first()
         ||  FAZY.KOD:=TOPER.PFAZ().KOD;
             FAZY.NR:=#TOPER.PFAZ;
             FAZY.add()
         ?};
         TOPER.next()
      !}
 ?};~~}
{MACRO-}
{MACRO: 'sumop'}
{TOPER.cntx_psh();
 TOPER.index('NNN');
 TOPER.prefix(TKTL.ref());
 {? TOPER.first()
 || {! |?
      {| ($'TOPER')() |!
           __TMP.prefix(UNROP);
          {? ~__TMP.first
          || __TMP.UNROP:=UNROP;
             {? PZ='P'
             ||__TMP.PLNX:={? FNTIME<>''
                           || {? NTIME<>0
                              || tpar.calc(FNTIME)/NTIME*{? FCOEF<>''||{? COEF<>0
                                                                       ||tpar.calc(FCOEF)/COEF
                                                                       ||0
                                                                       ?}
                                                                      ||1?}*PLNX
                               || 0
                               ?}
                            || {? FCOEF<>''
                               || {? COEF<>0
                                  || tpar.calc(FCOEF)/COEF
                                  || 0
                                  ?}
                                || 1
                                ?}*PLNX
                             ?};
                 __TMP.PLNH:={? FCOEF<>''
                             || {? COEF<>0 || tpar.calc(FCOEF)/COEF*PLNH || 0 ?}
                             || PLNH
                              ?};
                __TMP.NTIME:={? FNTIME<>''
                             || tpar.calc(FNTIME)
                             || NTIME
                             ?};
                __TMP.MTIME:={?FMTIME<>''
                             ||tpar.calc(FMTIME)
                             ||MTIME
                             ?}
               || __TMP.PLNX:=__TMP.PLNH:=__TMP.NTIME:=__TMP.MTIME:=0
               ?};
             __TMP.add
          ?};
          {? NRNOP<>0
          || __TMP.prefix(NRNOP);
            {? __TMP.first
            || __TMP.PLNX+={? FNTIME<>''
                           || tpar.calc(FNTIME)/NTIME*{? FCOEF<>''||tpar.calc(FCOEF)/COEF||1?}*PLNX
                           || {? FCOEF<>''||tpar.calc(FCOEF)/COEF||1?}*PLNX
                           ?};
               __TMP.PLNH+={? FCOEF<>''
                           || tpar.calc(FCOEF)/COEF*PLNH
                           || PLNH
                           ?};
               __TMP.NTIME+={? FNTIME<>''
                            || tpar.calc(FNTIME)
                            || NTIME
                            ?};
               __TMP.MTIME+={? FMTIME<>''
                            || tpar.calc(FMTIME)
                            || MTIME
                            ?};
              __TMP.put()
            ?}
         ?}
      |};
    TOPER.next()
  !}
?};
TOPER.cntx_pop()
;~~}
{MACRO-}
{IF: f=1}
{SUBSTITUTE: 'utw_fazy'}
{FI}
{"------------------------------------------------------------------------"; ~~}
{"---Zbior funkcji wykorzystywanych w biezacej definicji wydruku.         "; ~~}
{MACRO: 'functions'}
  {"--------------------------------------------------------------------------";
   "---Wyswietla okienko z pytaniem, czy drukowac wartosci czy algorytmy      ";
   "---na surowcach, operacjach i narzedziach (odpowiednio - normy            ";
   "---materialowe, normy czasowe oraz zuzycie).                              ";
   "---Jezeli zostalo wybrane drukowanie wartosci, to wyswietla okienko       ";
   "---redakcyjne, aby umozliwic redakcje domyslnych parametrow karty.        ";
   "---Zwraca 2, jezeli beda drukowane algorytmy - 1, jezeli wartosci,        ";
   "---natomiast 0, jezeli operator przerwal dzialanie wydruku.";
   pchange:="
     _result:=tpar.change();
     _result
   ";
   "--------------------------------------------------------------------------";
   "---Zwraca liczbe parametrow do biezacej karty technologicznej.";
   pcount:="
     TPAR.index('NN');
     TPAR.prefix(TKTL.ref());
     TPAR.size()
   ";
  ~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Sekcja akcji inicjalizujacych.                                       "; ~~}
{MACRO: 'miscelaneous'}
  {"--- B R A K   A K C J I   I N I C J A L I Z U J A C Y C H.            "; ~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Deklaracja funkcji wydruku.                                          "; ~~}
{SUBSTITUTE: 'functions'}
{"------------------------------------------------------------------------"; ~~}
{"---Wykonanie akcji inicjalizujacych.                                    "; ~~}
{SUBSTITUTE: 'miscelaneous'}
{"---Definicja wiersza wydruku operacji TOPER.";~~}
{ PRNOPER.add("nrop+$TOPER.NROP",5,'Nr',1);
  PRNOPER.add(" form(pop-2,14)+'#'+ 14*kreska+ '#'+ form(nas-2,14)",15,'Poprzedniki'+ '        ' + '#'+ 14*kreska+ '#'+'Następniki',1,'#');
  PRNOPER.add("'   '+{? ~(-form(TOPER.PZ))='P' || 'T' || 'N' ?}",2,'Pr');
  PRNOPER.add("{? ~(-form(TOPER.PZ))='P' & wew='T'
               || TOPER.OPER().NA
               |? wew='N'
               || TOPER.NA+'-zewn'
               || TOPER.NA
               ?}",20,'Nazwa');
  PRNOPER.add("{? wew ='T'
               || TOPER.PLACE().KOD+' - '+TOPER.PLACE().NA
               || '-'
               ?} ",22,'Stanowisko');
  PRNOPER.add("{? wew ='T'||'  '+TOPER.ALL || '-' ?}",3,'Rów');
  PRNOPER.add("{? wew='T' ||'  '+TOPER.BRYG || '-' ?}",3,'Bry');
  PRNOPER.add("{? wew='T'
               || {? ~(-form(TOPER.PZ))='P' || TOPER.OPER().ZAWOD().ZD || '  -'?}
               || '  -'
               ?}",19,'Zawód prac.');
  PRNOPER.add("{? wew='T' || form(TOPER.OPER().TKAT().KAT,,0,'9') || '-'?}",5,'Kat. zasz.');
  PRNOPER.add("{? wew='T'
               || form(TOPER.PLNX,14,4)+'#'+ 14*kreska+ '#' + '-'
               || '-'+'#'+14*kreska+'#'+ form(TOPER.CENA,14,4)
               ?} ",15,'Pł.na #'
                  +form(TKTL.XJM,,2)+' '+TKTL.JM().KOD + '#'+ 14*kreska+ '#'
                 + 'Cena na    #'+ form(TKTL.XJM,,2)+ ' ' +TKTL.JM().KOD ,1,'#');
  PRNOPER.add("{? wew='T' & TOPER.PZ='P'
               || form(TOPER.PLNH,,4)+'#'+ 14*kreska+ '#' + form(TOPER.KH,,4)
               || '-'+'#'+ 14*kreska+ '#'+'-'
               ?}",15,'Płaca na godz'+ '        ' + '#'+ 14*kreska+ '#'+'Koszt st.     # na godz',1,'#');
  PRNOPER.add("{? wew='T' & TOPER.PZ='P' || form(TOPER.KX,,4) || '-' ?}",15,'Koszt st. na #'+form(TKTL.XJM,,2)+ ' ' +TKTL.JM().KOD,1,'#');
  PRNOPER.add("{? wew='T'
               || form(__TMP.NTIME,14,exec('get','#params',500604,1))+'#'+ 14*kreska+ '#'+ form(__TMP.MTIME,14,exec('get','#params',500604,1))
               || '-'+'#'+ 14*kreska+ '#'+'-' ?}",15,'Norma czasowa' + '        ' + '#'+ 14*kreska+ '#'+'Czas maszynowy',1,'#');
  PRNOPER.add("{? wew='T'
               || {? ~(-form(TOPER.PZ))='P'
                  || {? TOPER.FCOEF<>'' || form(tpar.calc(TOPER.FCOEF),10,4) || form(TOPER.COEF,10,4) ?}
                  || '  -'
                  ?}
               || '-'
               ?}",10,'Wsp. PLN/h',1);
  PRNOPER.add("{? wew='T'|| ' '+TOPER.TPZ || '-' ?}",3,'TPZ');
  {? f=0 ||
  PRNOPER.add("TOPER.PFAZ().KOD",7,'Faza pr')
  ?};
  PRNOPER.add("atryb-1",17,'Atrybuty'+9*' '+'#'+ 'Nazwa - Wartość',,'#');~~}
{"------------------------------------------------------------------------";~~}
{"---Definicja wiersza podsumowania tabelki operacji.                     "; ~~}
{ PRNSUM.add("'Podsumowanie dla operacji pierwszego poziomu:'",102,'Tytul podsumowania');
  PRNSUM.add(" form(sumplnx,14,4)+ '#'+14*kreska+ '#' +form(sumpzew,14,4)",15,'Płaca na XJM',1,'#');
  PRNSUM.add("''",31,'',1);
  PRNSUM.add("form(sumntime,14,exec('get','#params',500604,1))+'#'+15*kreska+'#'+form(sumnst,14,exec('get','#params',500604,1))",15,'',1,'#');
  {? f=0
  || PRNSUM.add("' '",40,'')
  || PRNSUM.add("' '",32,'')
  ?};~~}
{"---Definicja wiersza wydruku parametrow karty technologicznej.          "; ~~}
{ PRNPAR.add("'p('+$(tpar.P[1][i])+')'",10,'Symbol');
  PRNPAR.add("tpar.P[3][i]",40,'Opis');
  PRNPAR.add("form(tpar.P[2][i],25,10)",25,'Wartość',1);
  PAR_WYDR.TITLE:='OPERACJE PROCESU TECHNOLOGICZNEGO';~~}
{INCLUDE: wsp_pw.rpi}
{"------------------------------------------------------------------------"; ~~}
{"---Wydruk tabeli operacji, wraz z operacjami podrzednymi (recursive).   "; ~~}
{MACRO: 'printopr'}
  {FOR: 'TOPER'}
    {INDEX: __NDX}
    {PREFIX: TKTL.ref,VAR.A_UNROP}
  {DO}
  {IF: TOPER.ACT='T' & tpar.calc(TOPER.EXIST)=1}
  {nas:='';
   pop:='';

   NASTOPER.cntx_psh();
   TOPER.cntx_psh();
   {? data=date()
   || NASTOPER.index('OPER');
      NASTOPER.prefix('T',TKTL.ref(),TOPER.ref())
   || NASTOPER.index('KTLIDOD');
      NASTOPER.prefix(TKTL.ref);
      {? NASTOPER.find_le(data)
      || _idstr:=NASTOPER.IDSTR;
         NASTOPER.index('ODOPNAST');
         NASTOPER.prefix(TOPER.ref,_idstr)
      || _idstr:=0;
         NASTOPER.index('OPER');
         NASTOPER.prefix('T',TKTL.ref(),TOPER.ref())
      ?}
   ?};
   {? NASTOPER.first()
   || {!|?
        nas+=NASTOPER.SCIEZKA+', ';
        NASTOPER.next()
      !}
   ?};
   {? data=date() | _idstr=0
   || NASTOPER.index('UNROP');
      NASTOPER.prefix('T',TKTL.ref(),TOPER.ref())
   || NASTOPER.index('ODNASTOP');
      NASTOPER.prefix(TOPER.ref,_idstr)
   ?};
   {? NASTOPER.first()
   || {!|?
        pop+=exec('get_oper_nr','tech_oper',NASTOPER.OPER().UNROP)+', ';
        NASTOPER.next()
      !}
   ?};
   NASTOPER.cntx_pop();
   TOPER.cntx_pop();
   __TMP.prefix(TOPER.UNROP);
   __TMP.first;~~}
      {IF: f=0 | (f=1 & TOPER.PFAZ().KOD=FAZY.KOD)}
      {IF: TOPER.NRNOP=0}
      {sumplnx+=TOPER.PLNX; ~~}
      {sumplnh+=TOPER.PLNH; ~~}
      {sumntime+={? TOPER.FNTIME<>'' || tpar.calc(TOPER.FNTIME) || TOPER.NTIME ?}; ~~}
      {FI}
      {sumnst+={? TOPER.FMTIME<>'' || tpar.calc(TOPER.FMTIME) || TOPER.MTIME ?}; ~~}
      {wew:=~-(TOPER.WEW); atryb:=''; ~~}
      {UAT.clear();
       UAT.index('TR');
       UAT.prefix('TOPER*',#TOPER.ref(),ref_name(TOPER.ref())+'*');
       {? UAT.first()
       ||
         {! |?
          exec('decode','tech_atr');
            _naz:=UAT.TYP().NA+'-'+ATR.TWAR;
            atryb+={? +_naz>17
                    || form(_naz,(ceil((+_naz)/17)*17))+'#'
                    || form(_naz,17)+'#'
                    ?};
            UAT.next
          !}
       ?}
      ;~~}
      {IF: wew='N'}{sumpzew+=TOPER.CENA;~~}
       {"---Operacje topowe zostana wyboldowane."; ~~}
       {IF: lineleft > 10} {SUBSTITUTE: 'LINIA0'}
        {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
       {FI}
    {ELSE}
      {IF: lineleft > 10} {SUBSTITUTE: 'LINIA0'}
       {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
    {FI}
    {FI}
    {VAR.A_UNROP:=TOPER.UNROP; ~~}
    {nrop+=$TOPER.NROP+'\\'; ~~}
    {SUBSTITUTE: 'printopr'}
    { ctrl:=+($TOPER.NROP+'\\');nrop:=nrop-ctrl; ~~}
    {ELSE}
    {VAR.A_UNROP:=TOPER.UNROP; ~~}
    {FI}
  {OD}
{dalej:=1;~~}
{MACRO-}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{SUBSTITUTE: 'functions'}
{IF: pcount()>0}
  {IF: (paramprt:=pchange())=0}
    {EXIT}
  {FI}
{FI}
{SUBSTITUTE: 'sumop'}
{IF: f=0}
    {FONT: 'T8G'}{SPACE: 'B'}
{   lmt:=int((charleft()-PRNOPER.width())/2); ~~}
    {<{lmt} 'Karta technologiczna: '+ktlnr + ' wersja '+TKTL.WER}
    {<{lmt} 'Produkt: '+VAR.A_T().KTM+' - '+M.N}
    {FONT: 'T8GB'}
    {IF: lineleft <14}{PAGE}{FI}
    {SUBSTITUTE: 'TABHEAD'}
    {FONT: 'T8G'}{ vp:=1; ~~}
    {prn1:='PRNOPER';~~}
    {"---Tabelka operacji do wybranej karty technologicznej.                  "; ~~}
    {SUBSTITUTE: 'printopr'}
    {printing:=''; ~~}
    {waslbar2:=1; ~~}
    {"------------------------------------------------------------------------"; ~~}
   {SUBSTITUTE: 'LINIAFS'}
  {ELSE}
   {FOR: FAZY}
   {DO}
     {FONT: 'W12'}{SPACE: 'A'}
{     lk:=charleft(); ~~}
     {PFAZ.seek(FAZY.NR,'pfazypr');~~}
     {IF: lineleft <14}{PAGE}{FI}
     {IF: FAZY.KOD<>''}
     {<1>{lk} 'FAZA '+ FAZY.KOD+'  -  '+PFAZ.OPIS}
     {FI}
    {FIRST}
    {FONT: 'T8G'}{SPACE: 'B'}
{    lmt:=int((charleft()-PRNOPER.width())/2); ~~}
    {<{lmt} 'Karta technologiczna: '+ktlnr + ' wersja '+TKTL.WER}
    {<{lmt} 'Produkt: '+VAR.A_T().KTM+' - '+M.N}
    {FIRST-}
    {FONT: 'T8GB'}{SPACE: 'B'}
    {IF: lineleft <14}{PAGE}{FI}
    {SUBSTITUTE: 'TABHEAD'}
    {FONT: 'T8G'}{ vp:=1; ~~}
    {prn1:='PRNOPER';~~}
    {"---Tabelka operacji do wybranej karty technologicznej.                  "; ~~}
    {SUBSTITUTE: 'printopr'}
    {printing:=''; ~~}
    {waslbar2:=1; ~~}
      {SUBSTITUTE: 'LINIAFS'}
    {VAR.A_UNROP:=0;
     sumplnx:=0; sumplnh:=0;
     sumntime:=0;sumnst:=0;sumpzew:=0;~~}
    {OD}
{FI}
{"------------------------------------------------------------------------"; ~~}
{"---Tutaj wydruk parametrow karty technologicznej.                       "; ~~}
{IF: pcount()>0}
  {TPAR.index('NN'); TPAR.prefix(TKTL.ref()); ~~}
  {IF: TPAR.first()}{ dalej:=1; prn1:='PRNPAR';~~}
{<1  lmt:=int((charleft()-PRNPAR.width())/2); ~~}
    {IF: lineleft < 14}{PAGE}{FI}
    {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
    {<1>{lk} 'SPIS PARAMETRÓW KARTY TECHNOLOGICZNEJ'}
    {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
    {i:=1; ~~}
    {WHILE: dalej}
    {DO}
     {IF: lineleft > 8} {SUBSTITUTE: 'LINIA0'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
     {FI}
     { i+=1; dalej:=TPAR.next();~~}
   {OD}
   {SUBSTITUTE: 'LINIAF'}
  {FI}
{FI}

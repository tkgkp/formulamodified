:!UTF-8
{TITLE: Lista współpracowników, którym nie wznowiono odprowadzania wpłat}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','__TAB','lp');
   tresc:='ppklnwzn';
   PAR_WYDR.TITLE:='Lista współpracowników, którym nie wznowiono odprowadzania wpłat'@;
   par:=obj_new(5);
   par[1]:=1;
   par[4]:=exec('par2wydr','ppk_rep',2);
   par[5]:=rep_export();
   {? par[1]
   || VAR_EDIT.DATA:=date(par[4].ROK_WZN,KST_PPK.WZN_MS-1,KST_PPK.WZN_DN);
      {? KST_PPK.PPK_UMO<>null() | exec('init','ppk_umo',VAR_EDIT.DATA)<>null()
      || PPK_UZA.cntx_psh();
         KST_PPK.PPK_UMO().PPK_UZA();
         _PARRAP:=exec('parrap_b','ppk_uza',1);
         _mc:={? _PARRAP.AWW_MC || _PARRAP.AWW_MC || KST_PPK.WZN_MS-1 ?};
         _dz:={? _PARRAP.AWW_DZ || _PARRAP.AWW_DZ || KST_PPK.WZN_DN ?};
         VAR_EDIT.DATA:=date(par[4].ROK_WZN,_mc,_dz);
         obj_del(_PARRAP);
         PPK_UZA.cntx_pop()
      ?};

      __TAB:=exec('n_wznow','ppk_rep',exec('info_wzn','ppk_ucz',par[4].ROK_WZN,1,1,1),par[4].ROK_WZN);

      {? __TAB.first()
      || par[3]:=obj_new(@.CLASS.PRINT,11);
         {? ~par[5] || par[3].add("lp+=1",7,'Lp.'@,1,,,,,',,\'99\'') ?};
         par[3].add("__TAB.NAZWISKO+' '+__TAB.PIERWSZE+' '+__TAB.DRUGIE",30,'Współpracownik'@);
         par[3].add("__TAB.PESEL",11,'PESEL'@);
         par[3].add("__TAB.UR_DATA$1",10,'Data urodzenia'@);
         par[3].add("__TAB.RODZAJ",9,'Rodzaj odbiorcy'@);
         par[3].add("__TAB.AKT",5,'Aktywny'@);
         par[3].add("__TAB.TYPDOK",6,'Rodzaj dokumentu'@);
         par[3].add("__TAB.SYMDOK",15,'Symbol dokumentu'@);
         par[3].add("__TAB.ADRES",40,'Adres'@);
         par[3].add("__TAB.EMAIL",40,'Adres e-mail'@);
         par[3].s_frame();

         prn1:='par[3]';
         lmt:=rsep:=lp:=0;
         par[2]:=vp:=h_rat:=1
      || par[1]:=0;
         _rok:=par[4].ROK_WZN;
         _info:='Brak współpracowników, którym należy wznowić odprowadzanie wpłat do PPK w %1 roku.'@[$_rok];
         {? _rok<=2019 | (_rok-2019)%*KST_PPK.WZN_OKR<>0
         || _info+='\nPodany rok nie jest rokiem wznowienia'@
         ?};
         FUN.info(_info)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','__TAB','lp')
}
{EXIT: ~par[1]}
{COMMENT}OLD: ppklnwzn.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[2]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? par[5] || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[2]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Data wznowienia: %1'[VAR_EDIT.DATA$1]}
{FI}

{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
   {SUBSTITUTE: 'LINIA0'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[3].width())/2); ~~ }
{FOR: __TAB}
{DO}
   {SUBSTITUTE: tresc}
{OD}
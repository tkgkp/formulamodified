:!UTF-8
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: rapkas.rpi
Utworzony: [MB]
==================================================
Zawartosc: Wydruk raportu kasowego - zawartosc
{COMMENT-}
{ USRCONST.STANKAS(); KUSERUPR.STANKAS(); STANKAS.F_NUMER();
  _tytul:='Raport '@+{? STANKAS.CZY_KASA='T' || 'kasowy'@ || 'bankowy'@ ?}+
          ' nr: '@+$RAPORT.NUM_RAP+' za '@+
          {? RAPORT.DATA_OD=RAPORT.DATA_DO
          || 'dzień: '@+$RAPORT.DATA_OD
          || 'okres: '@+$RAPORT.DATA_OD+' - '+$RAPORT.DATA_DO
          ?};
  PAR_WYDR.TITLE:=~-_tytul;
  {? STANKAS.WALUTA=exec('wal_nar','kasa_wspolne') | (STANKAS.WALUTA().SYM<>'' & WYDRUKI.PAR2='N')
  || r_macr:=0
  |? (STANKAS.WALUTA().SYM<>'' & WYDRUKI.PAR2='T')
  || r_macr:=1
  || r_macr:=2
  ?};~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Stanowisko raportu: %1 - %2'@[$STANKAS.KOD,STANKAS.NAZWA]}
{<5 'Waluta: '@+{? STANKAS.WALUTA<>null || STANKAS.WALUTA().SYM || 'wszystkie waluty (raport w stanowisku wielowalutowym)'@ ?}}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{IF: (r_macr=0 | r_macr=1) & page>1 & do_przen}
{ w[17]:='   Z przeniesienia  (obroty narastająco)'@; w[18]:=sw; w[19]:=sm; w[20]:=STANKAS.WALUTA().SYM;~~}
{ prn1:='PRN'; prn2:='PRN1'; separf:=rsep; rsep:=1; ~~}
{REP: PRN.fjbar(PRN1)}
{ PRN1.setcolor('255:255:255');~~}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}
{IF: r_macr=1}
{ rsep:=0; w[17]:=''; w[18]:=sw1; w[19]:=sm1; w[20]:=KINFO.NAROD().KOD;~~}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}
{FI}
{ rsep:=separf; prn1:=prn2:='PRN';~~}
{FI}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{IF: r_macr=0}
{FOOTER: 0}{IF: do_przen}{ prn1:='PRN'; prn2:='PRN1'; separf:=rsep; rsep:=1; w[17]:='   Do przeniesienia  (obroty narastająco)'@; w[18]:=sw; w[19]:=sm;~~}{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA02'}{FONT: 'T8G'}{SPACE: 'C'}
{PRN1.lbar()}{ rsep:=separf;~~}{ELSE}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}{FI}{ vp:=1;~~}
{FOOTER-}
{ELIF: r_macr=1}
{FOOTER: 0}{IF: do_przen}{ prn1:='PRN'; prn2:='PRN1'; separf:=rsep; rsep:=1; w[17]:='   Do przeniesienia  (obroty narastająco)'@; w[18]:=sw; w[19]:=sm;~~}{FONT: 'T8GB'}{SPACE: 'C'}
{ w[20]:=STANKAS.WALUTA().SYM;~~}{SUBSTITUTE: 'LINIA02'}{ prn1:=prn2:='PRN1'; w[17]:=''; w[18]:=sw1; w[19]:=sm1; rsep:=0;~~}
{ w[20]:=KINFO.NAROD().KOD;~~}{IF: WYDRUKI.PAR2='T'}{SUBSTITUTE: 'LINIA0'}{FI}{FONT: 'T8G'}{SPACE: 'C'}
{PRN1.lbar()}{ w[17]:='';~~}{ prn1:=prn2:='PRN';~~}
{ rsep:=separf;~~}{ELSE}{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}{FI}{ vp:=1;~~}
{FOOTER-}
{ELSE}
{FOOTER: 0}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}{ vp:=1;~~}
{FOOTER-}
{FI}
{INCLUDE: wsp_pw.rpi}
{ rsep:=PAR_WYDR.RSEP; ~~}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{  rsep:=PAR_WYDR.RSEP; ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{ _szer_kw:={? WYDRUKI.PAR3='T' | WYDRUKI.PAR4='T' || 13 || 16 ?};
  PRN.add("w[1]",4,'Poz.'@,1);
  PRN.add("w[2]",10,'Data'@);
  PRN.add("w[3]",12,'Nr dokumentu'@);
  PRN.add("w[4]",32,'Od kogo/komu'@);
  PRN.add("w[5]",{? r_macr & WYDRUKI.PAR3='T' & WYDRUKI.PAR4='T' || 21 || 25 ?},'Treść'@);
  {? r_macr || PRN.add("w[11]",4,'Wal.'@) ?};
  PRN.add("w[6]",_szer_kw,'Przychód'@,1);
  PRN.add("w[7]",_szer_kw,'Rozchód'@,1);
  {? WYDRUKI.PAR3='T' || PRN.add("w[8]",35,'Konta Wn'@,,','); PRN.add("w[9]",35,'Konta Ma'@,,',') ?};
  {? WYDRUKI.PAR4='T' || PRN.add("w[10]",20,'Identyfikator rozrachunku'@) ?};
  PRN1.add("w[17]",87,'');
  {? r_macr || PRN1.add("w[20]",4,'') ?};
  PRN1.add("form(w[18],,2,' ,')",_szer_kw,'',1);
  PRN1.add("form(w[19],,2,' ,')",_szer_kw,'',1);
  {? WYDRUKI.PAR3='T' || PRN1.add("''",71,'') ?};
  {? WYDRUKI.PAR4='T' || PRN1.add("''",20,'') ?};
  {? r_macr=0 | r_macr=1
  || PRN2.add("w[12]",{? r_macr & WYDRUKI.PAR3='T' & WYDRUKI.PAR4='T' || 83 || 87 ?});
     {? r_macr || PRN2.add("w[20]",4,'') ?};
     PRN2.add("w[13]",_szer_kw,,1);
     PRN2.add("w[14]",_szer_kw,,1)
  || PRN2.add("''",32,'Podsumowanie raportu'@);
     PRN2.add("w[22]",_szer_kw,'Stan początkowy'@,1);
     PRN2.add("w[23]",_szer_kw,'Przychody'@,1);
     PRN2.add("w[24]",_szer_kw,'Rozchody'@,1);
     PRN2.add("w[25]",_szer_kw,'Stan końcowy'@,1);
     PRN2.add("w[21]",4,'Wal.'@);
     PRN2.add("w[16]",33,form('Sporz.'@,16)+'Spraw.'@)
  ?};
  PRN3.add("w[16]",{? r_macr & WYDRUKI.PAR3='T' & WYDRUKI.PAR4='T' || 57 || 61 ?},'Podpisy'@);
  PRN3.add("w[15]",25,'Obroty'@);
  {? r_macr || PRN3.add("w[20]",4,'Wal.'@) ?};
  PRN3.add("w[13]",_szer_kw,'Przychód'@,1);
  PRN3.add("w[14]",_szer_kw,'Rozchód'@,1);
  exec('bo_rap','kasa_wspolne',STANKAS.WALUTA().SYM);
  {? STANKAS.WALUTA<>null & STANKAS.WALUTA<>exec('wal_nar','kasa_wspolne') || r_wal:=1 || r_wal:=0 ?};
  {? ~r_wal || sp:=BO.PLN; sp1:=BO.WAL || sp:=BO.WAL; sp1:=BO.PLN ?};~~
}
{COMMENT}-------------------------------------------MACRO LINE-----------------------------------{COMMENT-}
{MACRO: 'LINE'}
{ separ:=rsep; {? separat=0 || rsep:=separat ?};~~}
{IF: rsep & lineleft=1}{PAGE}{FI}
{IF: page=1 | (page>1 & vp=0)}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
{REP: PRN1.fjbar(PRN)}{ rsep:=vp:=0; PRN.setcolor('255:255:255');~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ rsep:=separ;~~}
{MACRO-}
{COMMENT}-------------------------------------------MACRO LINE1-----------------------------------{COMMENT-}
{MACRO: 'LINE1'}
{ separ:=rsep; {? separat=0 || rsep:=separat ?};~~}
{IF: r_macr=2 | page=1 | (page>1 & vp=0)}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
{REP: PRN1.fjbar(PRN)}{ rsep:=vp:=0; PRN.setcolor('255:255:255');~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ rsep:=separ;~~}
{MACRO-}
{COMMENT}-------------------------------------------MACRO-ONEWAL----------------------------------{COMMENT-}
{MACRO: 'ONEWAL'}
{IF:anul=1}
{ {! _a:=1..11 |! w[_a]:='' !};  w[5]:='----dokument anulowany!----'@;~~}
{SUBSTITUTE:'LINE'}
{FI}
{ {! _a:=1..11 |! w[_a]:='' !};
  w[1]:=DOKUMENT.LP; w[2]:=$DOKUMENT.DATA; w[3]:=DOKUMENT.TYP_DOK+'  '+$DOKUMENT.DOK_NUM;
  w[4]:=DOKUMENT.NAZWA; w[5]:=DOKUMENT.OPIS;
  {? DOKUMENT.PLUS_MIN='+'
  || w[6]:=form({? ~r_wal || DOKUMENT.KW_PLN || DOKUMENT.KW_WAL ?},,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form({? ~r_wal || DOKUMENT.KW_PLN || DOKUMENT.KW_WAL ?},,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  w[6]+=' '; w[7]+=' ';
  POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref()); POZDOK.first();
  {? POZDOK.size()=1
  || {? WYDRUKI.PAR3='T'
     || {? var_pres('konto')>0 || obj_del(konto) ?};
        _k:=exec('konto','dok_fks_aut_dok');
        {? var_pres('konto')<=0
        || {? POZDOK.PLUS_MIN='-' || w[8]:=_k  || w[9]:=_k ?}
        || {? POZDOK.PLUS_MIN='-' || w[8]:=konto[1]  || w[9]:=konto[1] ?}
        ?}
     ?};
     {? WYDRUKI.PAR4='T' || w[10]:=POZDOK.MF_FIKS ?}
  ?};~~}
{IF: lineleft()=1 | lineleft()=2 | lineleft()=3}{PAGE}{FI}
{ {? anul=0 || separat:=1 || separat:=0 ?};~~}{SUBSTITUTE: 'LINE'}
{ {? DOKUMENT.PLUS_MIN='+'
  || sw+={? ~r_wal || DOKUMENT.KW_PLN$2 || DOKUMENT.KW_WAL$2 ?}
  || sm+={? ~r_wal || DOKUMENT.KW_PLN$2 || DOKUMENT.KW_WAL$2 ?}
  ?}; licznik:=2;~~}
{IF: POZDOK.size()=1 & WYDRUKI.PAR3='T' & ile_l_k>1}
{WHILE: licznik<=ile_l_k}
{DO}
{ {! _a:=1..11 |! w[_a]:='' !};
  {? POZDOK.PLUS_MIN='-' || w[8]:=konto[licznik]  || w[9]:=konto[licznik] ?};  licznik+=1;~~}
{ separat:=0;~~}{SUBSTITUTE: 'LINE'}
{OD}
{FI}
{IF: WYDRUKI.PAR1='T' & (POZDOK.size>1 | POZDOK.size()=1 & POZDOK.POZOPER().ZM_OBR='N')}
{ {! _a:=1..11 |! w[_a]:='' !};~~}
{FOR: 'POZDOK'}
{INDEX: 'DOKUMENT'}
{PREFIX: DOKUMENT.ref}
{DO}
{ w[4]:=POZDOK.SLO().TR; w[5]:=POZDOK.POZOPER().NAZWA;
  {? DOKUMENT.PLUS_MIN='+'
  || w[6]:=form({? ~r_wal || POZDOK.KW_PLN || POZDOK.KW_WAL ?}*{? POZDOK.PLUS_MIN='-'||-1||1?},,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form({? ~r_wal || POZDOK.KW_PLN || POZDOK.KW_WAL ?}*{? POZDOK.PLUS_MIN='+'||-1||1?},,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  {? POZDOK.POZOPER().ZM_OBR='N' || w[6]:='['+w[6]+']'; w[7]:='['+w[7]+']' || w[6]+=' '; w[7]+=' ' ?};
  {? WYDRUKI.PAR3='T'
  || {? var_pres('konto')>0 || obj_del(konto) ?};
     _k:=exec('konto','dok_fks_aut_dok');
     {? var_pres('konto')<=0
     || {? POZDOK.PLUS_MIN='-' || w[8]:=_k  || w[9]:=_k ?}
     || {? POZDOK.PLUS_MIN='-' || w[8]:=konto[1]  || w[9]:=konto[1] ?}
     ?}
  ?};
  {? WYDRUKI.PAR4='T' || w[10]:=POZDOK.MF_FIKS ?};~~}
{ separat:=0; licznik:=2;~~}{SUBSTITUTE: 'LINE'}
{IF: WYDRUKI.PAR3='T' & ile_l_k>1}
{WHILE: licznik<=ile_l_k}
{DO}
{ {! _a:=1..11 |! w[_a]:='' !};
  {? POZDOK.PLUS_MIN='-' || w[8]:=konto[licznik]  || w[9]:=konto[licznik] ?}; licznik+=1;~~}
{ separat:=0;~~}{SUBSTITUTE: 'LINE'}
{OD}
{FI}
{OD}
{FI}
{MACRO-}
{COMMENT}-------------------------------------------MACRO-MULTIWAL----------------------------------{COMMENT-}
{MACRO: 'MULTIWAL'}
{IF:anul=1}
{ {! _a:=1..11 |! w[_a]:='' !};  w[5]:='----dokument anulowany!----'@;~~}
{SUBSTITUTE:'LINE1'}
{FI}
{ {! _a:=1..11 |! w[_a]:='' !};
  w[1]:=DOKUMENT.LP; w[2]:=$DOKUMENT.DATA; w[3]:=DOKUMENT.TYP_DOK+'  '+$DOKUMENT.DOK_NUM;
  w[4]:=DOKUMENT.NAZWA; w[5]:=DOKUMENT.OPIS;
  {? DOKUMENT.PLUS_MIN='+'
  || w[6]:=form(DOKUMENT.KW_WAL,,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form(DOKUMENT.KW_WAL,,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  w[6]+=' '; w[7]+=' ';
  w[11]:=DOKUMENT.WALUTA().SYM;
  POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref()); POZDOK.first();
  {? POZDOK.size()=1
  || {? WYDRUKI.PAR3='T'
     || {? var_pres('konto')>0 || obj_del(konto) ?};
        _k:=exec('konto','dok_fks_aut_dok');
        {? var_pres('konto')<=0
        || {? POZDOK.PLUS_MIN='-' || w[8]:=_k  || w[9]:=_k ?}
        || {? POZDOK.PLUS_MIN='-' || w[8]:=konto[1]  || w[9]:=konto[1] ?}
        ?}
     ?};
     {? WYDRUKI.PAR4='T' || w[10]:=POZDOK.MF_FIKS ?}
  ?};~~}
{IF: _l:=lineleft; _l1:=2; _l1+=rsep; {? WYDRUKI.PAR2='T' || _l1+=1 ?}; _l<>0 & _l<_l1}{PAGE}{FI}
{ {? anul=0 || separat:=1 || separat:=0 ?};~~}{SUBSTITUTE: 'LINE1'}
{ {? DOKUMENT.PLUS_MIN='+'
  || sw+=DOKUMENT.KW_WAL$2; sw1+=DOKUMENT.KW_PLN$2
  || sm+=DOKUMENT.KW_WAL$2; sm1+=DOKUMENT.KW_PLN$2
  ?};~~}
{IF: WYDRUKI.PAR2='T'}
{ {! _a:=1..11 |! w[_a]:='' !};
  {? DOKUMENT.PLUS_MIN='+'
  || w[6]:=form(DOKUMENT.KW_PLN,,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form(DOKUMENT.KW_PLN,,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  w[6]+=' '; w[7]+=' ';
  w[11]:=KINFO.NAROD().KOD;
  {? var_pres('konto')>0 & ile_l_k>1 || {? POZDOK.PLUS_MIN='-' || w[8]:=konto[2]  || w[9]:=konto[2] ?} ?};
  ~~}
{ separat:=0;~~}{SUBSTITUTE: 'LINE1'}
{FI}
{IF: POZDOK.size()=1 & WYDRUKI.PAR3='T' &
     {? WYDRUKI.PAR2='N' || ile_l_k>1 || ile_l_k>2 ?}}
{ licznik:={? WYDRUKI.PAR2='N' || 2 || 3 ?};~~}
{WHILE: licznik<=ile_l_k}
{DO}
{ {! _a:=1..11 |! w[_a]:='' !};
  {? POZDOK.PLUS_MIN='-' || w[8]:=konto[licznik]  || w[9]:=konto[licznik] ?};
  licznik+=1;~~}
{ separat:=0;~~}{SUBSTITUTE: 'LINE1'}
{OD}
{FI}
{IF: WYDRUKI.PAR1='T' & (POZDOK.size>1 | POZDOK.size()=1 & POZDOK.POZOPER().ZM_OBR='N')}
{ {! _a:=1..11 |! w[_a]:='' !};~~}
{FOR: 'POZDOK'}
{INDEX: 'DOKUMENT'}
{PREFIX: DOKUMENT.ref}
{DO}
{ w[4]:=POZDOK.SLO().TR; w[5]:=POZDOK.POZOPER().NAZWA;
  {? DOKUMENT.PLUS_MIN='+'
  || w[6]:=form(POZDOK.KW_WAL*{?POZDOK.PLUS_MIN='-'||-1||1?},,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form(POZDOK.KW_WAL*{?POZDOK.PLUS_MIN='+'||-1||1?},,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  {? POZDOK.POZOPER().ZM_OBR='N' || w[6]:='['+w[6]+']'; w[7]:='['+w[7]+']' || w[6]+=' '; w[7]+=' ' ?};
  {? WYDRUKI.PAR3='T'
  || {? var_pres('konto')>0 || obj_del(konto) ?};
     _k:=exec('konto','dok_fks_aut_dok');
     {? var_pres('konto')<=0
     || {? POZDOK.PLUS_MIN='-' || w[8]:=_k  || w[9]:=_k ?}
     || {? POZDOK.PLUS_MIN='-' || w[8]:=konto[1]  || w[9]:=konto[1] ?}
     ?}
  ?};
  {? WYDRUKI.PAR4='T' || w[10]:=POZDOK.MF_FIKS ?}; w[11]:=DOKUMENT.WALUTA().SYM;~~}
{IF: _l:=lineleft; _l1:=1; _l1+=rsep; {? WYDRUKI.PAR2='T' || _l1+=1 ?}; _l<>0 & _l<_l1}{PAGE}{FI}
{ separat:=0;~~}{SUBSTITUTE: 'LINE1'}
{IF: WYDRUKI.PAR2='T'}
{ {! _a:=1..11 |! w[_a]:='' !}; w[11]:=KINFO.NAROD().KOD;
  {? DOKUMENT.PLUS_MIN='+'
  || w[6]:=form(POZDOK.KW_PLN*{?POZDOK.PLUS_MIN='-'||-1||1?},,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form(POZDOK.KW_PLN*{?POZDOK.PLUS_MIN='+'||-1||1?},,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  {? POZDOK.POZOPER().ZM_OBR='N' || w[6]:='['+w[6]+']'; w[7]:='['+w[7]+']' || w[6]+=' '; w[7]+=' ' ?};
  {? var_pres('konto')>0 & ile_l_k>1 || {? POZDOK.PLUS_MIN='-' || w[8]:=konto[2]  || w[9]:=konto[2] ?} ?};~~}
{ separat:=0;~~}{SUBSTITUTE: 'LINE1'}
{FI}
{IF: WYDRUKI.PAR3='T' &
     {? WYDRUKI.PAR2='N' || ile_l_k>1 || ile_l_k>2 ?}}
{ licznik:={? WYDRUKI.PAR2='N' || 2 || 3 ?};~~}
{WHILE: licznik<=ile_l_k}
{DO}
{ {! _a:=1..11 |! w[_a]:='' !};
  {? POZDOK.PLUS_MIN='-' || w[8]:=konto[licznik]  || w[9]:=konto[licznik] ?}; licznik+=1;~~}
{ separat:=0;~~}{SUBSTITUTE: 'LINE'}
{OD}
{FI}
{OD}
{FI}
{MACRO-}
{COMMENT}-------------------------------------------WYDRUK-----------------------------------{COMMENT-}
{IF: MLEX.RAP_ZB='T'}
{ TAB_SQL:=sql('select
                 OPER.KOD KOD_OP,
                 OPER.NAZWA NAZ_OP,
                 POZOPER.NAZWA NAZ_P,
                 POZOPER.KOD KOD_P,
                 POZDOK.PLUS_MIN,
                 DOKUMENT.DATA,
                 POZDOK.ZAKS,
                 sum(case when POZDOK.ZAKS=''A'' then 0 else POZDOK.KW_PLN end) as SUMA
                from DOKUMENT
                  join POZDOK
                  join POZOPER
                  join OPER using(DOKUMENT.OPER,OPER.REFERENCE)
                 where DOKUMENT.RAPORT='':_a'' and OPER.RAP_ZB=''T''
                group by OPER.KOD,OPER.NAZWA,POZOPER.NAZWA,POZOPER.KOD,POZDOK.PLUS_MIN,DOKUMENT.DATA, POZDOK.ZAKS
                order by DOKUMENT.DATA,KOD_OP,KOD_P
                 ',BB.refsql(RAPORT.ref));
  ~~
}
{FOR: TAB_SQL}
{DO}
  {{? TAB_SQL.ZAKS='A' || anul:=1 || anul:=0 ?};~~}
  {IF:anul=1}
  { {! _a:=1..11 |! w[_a]:='' !};  w[5]:='----dokument anulowany!----'@;~~}
  {SUBSTITUTE:'LINE1'}
  {FI}
  {  w[1]:=w[2]:=w[3]:=w[4]:=w[5]:='';
     w[6]:=w[7]:=form(0,,2);
     w[2]:=form(TAB_SQL.DATA);
     w[4]:=TAB_SQL.NAZ_OP;
     w[5]:=TAB_SQL.NAZ_P;
     {? TAB_SQL.PLUS_MIN='+' || w[6]:=form(TAB_SQL.SUMA,,2) || w[7]:=form(TAB_SQL.SUMA,,2) ?};
     ~~
  }
  {IF: lineleft()=1 | lineleft()=2 | lineleft()=3}{PAGE}{FI}
{ {? anul=0 || separat:=1 || separat:=0 ?};~~}
  {SUBSTITUTE: 'LINE'}
  { {? TAB_SQL.PLUS_MIN='+'
    || sw+=TAB_SQL.SUMA$2
    || sm+=TAB_SQL.SUMA$2
    ?};
    ~~
  }
{OD}
{FI}
{FOR: 'DOKUMENT'}
{INDEX: 'RAPORT'}
{PREFIX: RAPORT.ref}
{DO}
{IF: MLEX.RAP_ZB<>'T' | DOKUMENT.OPER().RAP_ZB<>'T'}
{ POZDOK.cntx_psh; POZDOK.index( 'DOKUMENT');POZDOK.prefix( DOKUMENT.ref);
  {? POZDOK.first & POZDOK.ZAKS='A' || anul:=1 || anul:=0 ?}; POZDOK.cntx_pop;
~~}
{IF: r_macr=0}{SUBSTITUTE: 'ONEWAL'}{ELSE}{SUBSTITUTE: 'MULTIWAL'}{FI}
{FI}
{OD}
{COMMENT}----------------------------------------FOOTER LAST-------------------------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: r_macr=2}
{REP: PRN2.fhbar()}{ vp:=1;~~}
{ PRN2.ini_head();~~}
{WHILE: PRN2.h_next()}
{DO}
   {REP: PRN2.h_fmline(lmt)}
{OD}
{ELSE}
{ prn1:=prn2:='PRN'; PRN2.setcolor('255:255:255');~~}
{SUBSTITUTE: 'TABHEAD'}
{REP: PRN.fjbar(PRN2)}{ vp:=0; ~~}
{FI}
{HEADER-}
{ do_przen:=1;~~}
{IF: lineleft()<5 & lineleft<>0}{PAGE}{FI}
{IF: lineleft()<>0}
{IF: r_macr=2}
{FONT: 'T8GB'}{SPACE: 'C'}
{REP: PRN.flbar()}
{ PRN2.setcolor(color);~~}
{ PRN2.ini_head();~~}
{REP: PRN2.fhbar(lmt)}
{WHILE: PRN2.h_next()}
{DO}
   {REP: PRN2.h_fmline(lmt)}
{OD}
{ELSE}
{REP: PRN.fjbar(PRN2)}
{FI}
{FI}
{FOOTER: 0}
{PRN2.lbar()}{ vp:=0;~~}
{FOOTER-}
{ vp:=0;~~}
{ prn1:=prn2:='PRN2'; PRN2.setcolor('255:255:255');~~}
{FONT: 'T8GB'}{SPACE: 'C'}{ {! _a:=1..25 |! w[_a]:='' !};~~}
{IF: r_macr=0 | r_macr=1}
{ w[12]:='   Podsumowanie raportu (przychody i rozchody)'@; w[13]:=form(sw,,2,' ,'); w[14]:=form(sm,,2,' ,');
  rsep:=0; w[20]:=STANKAS.WALUTA().SYM;~~}
{SUBSTITUTE: 'LINIA0'}
{IF: r_macr & WYDRUKI.PAR2='T'}
{ w[13]:=form(sw1,,2,' ,'); w[14]:=form(sm1,,2,' ,');  w[20]:=KINFO.NAROD().KOD; w[16]:=w[12]:=w[15]:=''; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{REP: PRN3.fbar()}{PRN3.setcolor('255:255:255');~~}
{HEADER-}
{FOOTER: 0}
{PRN3.lbar()}{vp:=0;~~}
{FOOTER-}
{ prn1:=prn2:='PRN3'; PRN3.setcolor(color);~~}
{REP: PRN2.flbar()}
{ PRN3.ini_head();~~}
{REP: PRN3.fhbar(lmt)}
{WHILE: PRN3.h_next()}
{DO}
   {REP: PRN3.h_fmline(lmt)}
{OD}
{REP: PRN3.fbar()}
{PRN3.setcolor('255:255:255');~~}
{ w[12]:=''; w[13]:=form(sp,,2,' ,'); w[14]:='';  w[20]:=STANKAS.WALUTA().SYM;
  w[15]:='Stan na początek'@; w[16]:=form('Sporz.'@,50)+'Spraw.'@; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{IF: r_macr & WYDRUKI.PAR2='T'}
{ w[13]:=form(sp1,,2,' ,'); w[14]:='';  w[20]:=KINFO.NAROD().KOD; w[12]:=w[15]:=w[16]:=''; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ w[13]:=''; w[14]:=form(sp-sm+sw,,2,' ,'); w[15]:='Stan na koniec'@; w[16]:=''; w[20]:=STANKAS.WALUTA().SYM; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{IF: r_macr & WYDRUKI.PAR2='T'}
{ w[14]:=form(sp1-sm1+sw1,,2,' ,'); w[13]:=w[15]:=w[16]:=''; w[20]:=KINFO.NAROD().KOD; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ w[13]:=form(sw+sp,,2,' ,');    w[14]:=form(sp+sw,,2,' ,'); w[15]:='Suma'@; w[20]:=STANKAS.WALUTA().SYM;  ~~}
{SUBSTITUTE: 'LINIA0'}
{IF: r_macr & WYDRUKI.PAR2='T'}
{ w[13]:=form(sw1+sp1,,2,' ,'); w[14]:=form(sp1+sw1,,2,' ,'); w[15]:=''; w[20]:=KINFO.NAROD().KOD;  ~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ELSE}{ rsep:=1; vp:=1;~~}
{FOR: 'WAL'}
{INDEX: 'WAL_SYM'}
{DO}
{ DOKUMENT.index('WALUTA'); DOKUMENT.prefix(RAPORT.ref,WAL.SYM);
  exec('bo_rap','kasa_wspolne',WAL.SYM); exec('dok_suma','kasa_wspolne',WAL.SYM); w[21]:=WAL.SYM;
  w[22]:=form(BO.WAL,,2,' ,');  w[23]:=form(SUMA.WAL_PLUS,,2,' ,');
  w[24]:=form(SUMA.WAL_MIN,,2,' ,'); w[25]:=form(SALDO.WAL,,2,' ,'); ~~}
{IF: BO.WAL<>0 | SUMA.WAL_PLUS<>0 | SUMA.WAL_MIN<>0 | SALDO.WAL<>0}
{IF: lineleft()=1}{PAGE}{ vp:=1;~~}{FI}{SUBSTITUTE: 'LINIA0'}{ rsep:=0; vp:=0;~~}{ PRN2.setcolor('255:255:255');~~}
{FI}
{OD}
{IF: WYDRUKI.PAR2='T'}
 { exec('bo_rap','kasa_wspolne');
   DOKUMENT.index('WALUTA'); DOKUMENT.prefix(RAPORT.ref);
   exec('dok_suma','kasa_wspolne');
   w[21]:=KINFO.NAROD().KOD;
   w[22]:=form(BO.PLN,,2,' ,'); w[23]:=form(sw1,,2,' ,'); w[24]:=form(sm1,,2,' ,'); w[25]:=form(SALDO.PLN,,2,' ,'); ~~}
{IF: lineleft()=1}{PAGE}{ vp:=1;~~}{FI}{SUBSTITUTE: 'LINIA0'}
{FI}
{FI}

:!UTF-8
{TITLE:Kredyty}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='pkd_wkredyt';
   PAR_WYDR.TITLE:='Kredyty pracownicze'@;
   P.cntx_psh();
   par:=obj_new(6);
   par[3]:=0;
   _upr:=exec('uprawnieniawrap','pkd','pożyczkach'@,'PPL_PLL_PPOZ','PPL_PLL_RPOZ');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[2]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,
         "  {? cur_tab().OD<>#0 & cur_tab().DO<>#0
            || {? cur_tab().OD<=cur_tab().DO
               || 1
               || FUN.emsg('Data początku okresu nie może być większa od daty końcowej'@)
               ?}
            || 1
            ?}
         ",
         date(,,1),'Początek okresu'@,,
         date(,,0),'Koniec okresu'@,,
         'Kredyty z wybranego okresu'@,'Wszystkie'@,'Z okresu'@,0,1
      );

      {? par[3]:=par[2].size()
      || par[1]:=obj_new(@.CLASS.PRINT,{? ~rep_export() || 8 || 11 ?});
         {? rep_export()
         || par[1].add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("P.OSOBA().PIERWSZE +' '+ P.OSOBA().DRUGIE",41,'Imiona');
            par[1].add("P.T",9,'Nr teczki'@)
         ?};
         par[1].add("K.PO().RT",20,'Rodzaj kredytu'@);
         par[1].add("K.DU$1",10,'Przyznano'@);
         par[1].add("K.DR$1",10,'Spłata od'@);
         par[1].add("K.MR",4,'Raty'@,1);
         par[1].add("K.PR",5,'(%)',1,,,,,'5,2');
         par[1].add("K.KP",16,'Kwota początkowa'@,1,,,,,'16,2');
         par[1].add("K.KS",15,'Dług / wkład'@,1,,,,,'15,2');
         par[1].add("K.KR",15,'Rata / składka'@,1,,,,,'15,2');
         par[1].s_frame();

         prn1:='par[1]';
         lmt:=rsep:=par[5]:=par[4]:=0;
         vp:=b_rat:=h_rat:=par[6]:=1;
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat')
}
{COMMENT}OLD: wkredyt.rpm{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[3]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[6]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[6]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Kredyty udzielone: od %1 do %2'@[par[2].OD$1,par[2].DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}
{<{ceil(lmt*b_rat)+1}}{form(par[4]+=1,,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{REP: par[1].fhbar(lmt)}
{ par[5]:=1; par[3]+=1; ~~ }
{MACRO-}
{MACRO: 'kred_dane'}
{IF: ~par[5]}
   {SUBSTITUTE: 'pracownik'}
{FI}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{COMMENT}

   Przeglądaj kartotekę kredytów w podanym lub pełnym zakresie dat.
   Pełny zakres - wszystkie kredytów, ograniczony zakres - od daty
   do daty, od 'początku' kartoteki do daty lub od daty do 'końca'.

{COMMENT-}
{MACRO: tresc}
{ par[5]:=0; ~~ }
{ENTIRE}
{REP: '{FOR: K}{INDEX: \'KREDYTY\'}'+
      {? par[2].ASK
      || '{FROM: P.ref()'+{? par[2].OD=#0 || '' || ',par[2].OD' ?}+'}'+
         '{TO: P.ref()'+{? par[2].DO=#0 || '' || ',par[2].DO' ?}+'}'
      || '{PREFIX: P.ref()}'
      ?}+'{DO}{SUBSTITUTE: \'kred_dane\'}{OD}'}
{IF: par[5]}
   {REP: par[1].flbar(lmt)}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2);
  par[3]:=0; ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
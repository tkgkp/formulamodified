:!UTF-8
{TITLE:M. Fabrycznie nowe środki amortyzowane jednorazowo w bieżącym roku}
{PRINTER: FINFO.W_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,15); PRN.s_frame();
   PRNF:=obj_new(@.CLASS.PRINT,15); PRNF.s_frame();
   c:=obj_new(15);
   {! _i:=1..14 |! c[_i] := '' !};
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0; par5:=0;
   __njp:=__zmadok:=__war100:=0;
   sum_zal:=sum_100:=sum_razm:=0
}
{END:
   obj_del(PRN); obj_del(PRNF);
   obj_del(c);
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3;&par4;&par5;
   &__njp; &__zmadok; &__war100;
   &sum_zal; &sum_100; &sum_razm
}
{EXIT:
   {? OPERATOR.DEPT<>null
   || PAR_WYDR.ODD:= OPERATOR.DEPT
   || PAR_WYDR.ODD:=0
   ?};
   0
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_o_njp.rpm
Utworzony  : 2017-11-23
Zawartość  : Fabrycznie nowe środki amortyzowane jednorazowo w bieżącym roku
==================================================
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("c[1]",18,'Nr inwentarzowy',,' ');
      PRN.add("c[2]",25,'Nazwa',,' ');
      PRN.add("c[3]",12,'Data zakupu',,' ');
      PRN.add("c[4]",12,'Data eksploatacji',,' ');
      PRN.add("c[5]",8,'Grupa',,' ');
      PRN.add("{? type_of(c[6])=1 || form(c[6],,2,' ,') || c[6] ?}",12,'Zaliczka',1,' ');
      PRN.add("{? type_of(c[7])=1 || form(c[7],,2,' ,') || c[7] ?}",12,'Amortyzacja jednorazowa',1,' ');
      PRN.add("{? type_of(c[8])=1 || form(c[8],,2,' ,') || c[8] ?}",12,'Razem',1,' ');

      PRNF.add("c[1]", 87, 'Środki');
      PRNF.add("{? type_of(c[6]) = 1 || form(c[6],,2,' ,') || c[6] ?}",12,'Zaliczka',1,' ');
      PRNF.add("{? type_of(c[7]) = 1 || form(c[7],,2,' ,') || c[7] ?}",12,'Amortyzacja jednorazowa',1,' ');
      PRNF.add("{? type_of(c[8]) = 1 || form(c[8],,2,' ,') || c[8] ?}",12,'Razem',1,' ');

      PAR_WYDR.TITLE:='Fabrycznie nowe środki amortyzowane jednorazowo w roku '+SSTALE.AR().NAZ; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
 {FOR: SRSR}{INDEX: 'NRI'}
 {DO}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
  {
   {? (1+SRSR.AMOR100='T') & SSTALE.AO().RES=SRSR.DE~1 & (PAR_WYDR.ODD=null | SRSR.ODD=PAR_WYDR.ODD)
   || __njp:=1
   || __njp:=0
   ?}
  ;~~}
  {IF: __njp=1}
  { c[1]:=SRSR.NRI;
    c[2]:=SRSR.NST;
    c[3]:=$SRSR.DZ;
    c[4]:=$SRSR.DE;
    c[5]:=SRSR.GR().GR;
    c[6]:=SRSR.ZALP100;
    _d_amop:=0;
    SRDO.cntx_psh();
    SRDO.index('PODAT');
    SRDO.prefix(SRSR.ref(),SSTALE.AO().RES,'W');
    {? SRDO.first()
    || {! |?
         _d_amop+=SRDO.WARP100;
         SRDO.next()
       !}
    ?};
    SRDO.cntx_pop();
    c[7]:=SRSR.WARP100+_d_amop;
    c[8]:=SRSR.WARP100+_d_amop+SRSR.ZALP100;

    sum_zal+=SRSR.ZALP100;
    sum_100+=(SRSR.WARP100+_d_amop);
    sum_razm+=SRSR.WARP100+_d_amop+SRSR.ZALP100

   ;~~}
  {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=PAR_WYDR.RSEP;~~}
  {FI}
 {OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}{FOOTER-}
 {COMMENT} ---- podsumowanie nie występuje dla excela ---- {COMMENT-}
{IF: ~rep_export()}
{FONT: 'T8GB'}{SPACE: 'C'}
{
   c[1]:='Ogółem zaliczki i amortyzacja jednorazowa:';
   c[6]:=sum_zal;
   c[7]:=sum_100;
   c[8]:=sum_razm;
   prn1:='PRN'; prn2:='PRNF';
   rsep:=1
;~~}
{SUBSTITUTE: 'LINIA02'}
{FI}
{ vp:=0; ~~}
{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
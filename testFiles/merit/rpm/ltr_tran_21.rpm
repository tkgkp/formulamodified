:!UTF-8
{TITLE:G. Rozliczenie pracy pojazdu + zbiornik dodatkowy}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,5,
   5,5,5,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('w','s','k','PRN','PRNS','s_tran','s_trand','tmp_licz','PRN2','k_count','tryb','samr','samt','samm');
   k :=obj_new(17); "--szerokosci kolumn--";
   w :=obj_new(17); "--wartosci kolumn--";
   s_tran:=obj_new(17); "--podsumowania--";
   s_trand:=obj_new(17); "--podsumowania - zb. dodatkowy--";
   dokl :=2; "--dokladnosc--";

   {! _i:=1..17 |! s_tran[_i]:=s_trand[_i]:=0 !};

   PRN:=obj_new(@.CLASS.PRINT,18); PRN.s_frame();
   PRN2:=obj_new(@.CLASS.PRINT,16); PRN2.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,16); PRNS.s_frame();
   tryb:=rep_export();
   samr:=samt:=samm:='';

   k_count:=0;
   color:=prn1:='';
   lm:=ll:=vp:=lmt:=0;rsep:=1;

   k[1]  :=4;
   k[2]  :=10;
   k[3]  :=5;
   k[4]  :=25;
   k[5]  :=8;
   k[6]  :=8;
   k[7]  :=10;
   k[8]  :=8;
   k[9]  :=8;
   k[10] :=8;
   k[11] :=8;
   k[12] :=12;
   k[13] :=12;
   k[14] :=12;
   k[15] :=12;
   k[16] :=12;
   k[17] :=12;

   POJAZDY.win_sel('SLO_J');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;
   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0
   || wyjscie:=1
   ?}
}
{END:
   VAR_DEL.delete('w','s','k','PRN','PRNS','s_tran','s_trand','tmp_licz','PRN2','k_count','tryb','samr','samt','samm');
   VAR_DEL.delete('wyjscie','dokl','oddz');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1
|| PRN.add("form(w[1])",k[1],'Lp.',1)
|| PRN.add("form(samr)",15,'Samochód (Rejestracja)');
   PRN.add("form(samt)",15,'Samochód (Nazwa)');
   PRN.add("form(samm)",15,'Samochód (Marka)')
?};
PRN.add("form(w[2])",k[2],'Data');
PRN.add("form(w[3])",k[3],'Karta');
PRN.add("form(w[4])",k[4],'Nazwisko i imię kierowcy',,' ');
PRN.add("form(w[5])",k[5],'Licznik pocz.',1);
PRN.add("form(w[6])",k[6],'Licznik kon.',1);
PRN.add("form(w[7])",k[7],'Zbiornik');
PRN.add("form(w[8],,dokl,' ,')",k[8],'Zał.',1);
PRN.add("form(w[9],,dokl,' ,')",k[9],'Jazdy',1);
PRN.add("form(w[10],,dokl,' ,')",k[10],'Stan pal. początek',1);
PRN.add("form(w[11],,dokl,' ,')",k[11],'Stan pal. koniec',1);
PRN.add("form(w[12],,dokl,' ,')",k[12],'Pobrano',1);
PRN.add("form(w[13],,dokl,' ,')",k[13],'Zużycie wg normy',1);
PRN.add("form(w[14],,dokl,' ,')",k[14],'Zużycie rzeczywiste',1);
PRN.add("form(w[15],,dokl,' ,')",k[15],'Przepał',1);
PRN.add("form(w[16],,dokl,' ,')",k[16],'Oszczędność',1);
PRN.add("form(w[17],,dokl,' ,')",k[17],'Motogodziny',1);

PRN2.add("form(w[1])",75,'',1);
PRN2.add("form(w[7])",k[7],'Zbiornik');
PRN2.add("form(w[8],,dokl,' ,')",k[8],'Zał.',1);
PRN2.add("form(w[9],,dokl,' ,')",k[9],'Jazdy',1);
PRN2.add("form(w[10],,dokl,' ,')",k[10],'Stan pal. początek',1);
PRN2.add("form(w[11],,dokl,' ,')",k[11],'Stan pal. koniec',1);
PRN2.add("form(w[12],,dokl,' ,')",k[12],'Pobrano',1);
PRN2.add("form(w[13],,dokl,' ,')",k[13],'Zużycie wg normy',1);
PRN2.add("form(w[14],,dokl,' ,')",k[14],'Zużycie rzeczywiste',1);
PRN2.add("form(w[15],,dokl,' ,')",k[15],'Przepał',1);
PRN2.add("form(w[16],,dokl,' ,')",k[16],'Oszczędność',1);
PRN2.add("form(w[17],,dokl,' ,')",k[17],'Motogodziny',1);

PRNS.add("form(s_tran[1])",75,'Razem');
PRNS.add("form(s_tran[7])",k[7],'Zbiornik',1);
PRNS.add("form(s_tran[8])",k[8],'Zał.',1);
PRNS.add("form(s_tran[9])",k[9],'Jazdy',1);
PRNS.add("form(s_tran[10],,dokl,' ,')",k[10],'Stan pal. początek',1);
PRNS.add("form(s_tran[11],,dokl,' ,')",k[11],'Stan pal. koniec',1);
PRNS.add("form(s_tran[12],,dokl,' ,')",k[12],'Pobrano',1);
PRNS.add("form(s_tran[13],,dokl,' ,')",k[13],'Zużycie wg normy',1);
PRNS.add("form(s_tran[14],,dokl,' ,')",k[14],'Zużycie rzeczywiste',1);
PRNS.add("form(s_tran[15],,dokl,' ,')",k[15],'Przepał',1);
PRNS.add("form(s_tran[16],,dokl,' ,')",k[16],'Oszczędność',1);
PRNS.add("form(s_tran[17],,dokl,' ,')",k[17],'Motogodziny',1);

{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu (+ zbiornik dodatkowy)\nza okres od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu (+ zbiornik dodatkowy)\nza okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?};
   PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu (+ zbiornik dodatkowy)\nza okres od: '+$_od+' do: '+$_do
?};
prn1:='PRN';  vp:=1
;~~}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{<{lmt+1}}{'Parametry wydruku:'}
{IF: tryb<>1}
{<{lmt+1}POJAZDY.NRREJ+' '+POJAZDY.NAZ+' '+POJAZDY.MAR().NAZ}{<90}{'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{ELSE}
{<{lmt+1}'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{FI}
{FONT: 'T8GB'}
{prn1:='PRN';prn2:='PRN';~~}
{SUBSTITUTE: 'TABHEAD'}
{prn1:='PRN';prn2:='PRN';~~}
{FONT: 'T8G'}{ vp:=1;~~}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR:'POJAZDY'}
{INDEX:'NRREJ'}
{PREFIX: REF.FIRMA}
{SELECT}
{DO}
   { w[1]:=0; {! _i:=1..17 |! s_tran[_i]:=s_trand[_i]:=0 !};
   samr:=POJAZDY.NRREJ;
   samt:=POJAZDY.NAZ;
   samm:=POJAZDY.MAR().NAZ;
   {? KARTDROG.WYBKART='M'
   || KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
      KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0)
   |? KARTDROG.WYBKART='W'
   || KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
      KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
      SAMK.index('D2');
      SAMK.prefix();
      {? SAMK.first()
      || KARTDROG.KARTY_OD:=SAMK.D;
         SAMK.last();
         KARTDROG.KARTY_DO:=SAMK.D
      ?}
   ?}
   ; ~~}
   {prn1:='PRN';prn2:='PRN'; ~~}
   {FOR:'SAMK'}
   {INDEX:'D'}
   {PREFIX:POJAZDY.ref()}
   {DO}
      {IF: (SAMK.D>=KARTDROG.KARTY_OD & SAMK.D<=KARTDROG.KARTY_DO) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT)}
         {
         k_count+=1;
         w[1]+=1;
         w[2]:=SAMK.D;
         w[3]:=SAMK.SYM;

         P_KAP.index('K');
         P_KAP.prefix(SAMK.ref(),'T');
         {? P_KAP.first()
         || {? P_KAP.size()>1
            || P_KAP.cntx_psh();
               w[4]:='';
               {! |?
                  {? w[4]<>'' || w[4]+=', ' ?};
                  w[4]+=P_KAP.OSOBA().NAZWISKO+' '+P_KAP.OSOBA().PIERWSZE;
                  P_KAP.next()
               !};
               P_KAP.cntx_pop()
            || w[4]:=P_KAP.OSOBA().NAZWISKO+' '+P_KAP.OSOBA().PIERWSZE
            ?}
         || w[4]:=''
         ?};

         w[5]:=SAMK.SP;
         w[6]:=SAMK.SK;
         w[7]:='Podstawowy';
         w[8]:=SAMK.ILZM+SAMK.ILZP;
         w[9]:=SAMK.PRZ;
         w[10]:=SAMK.SPP;
         w[11]:=SAMK.SKP;
         w[12]:=exec('i_palz','karty_drog',SAMK.ref(),'P');
         w[13]:=SAMK.Z_NORMA;
         w[14]:=(w[10]-w[11])+w[12];
         _zu:=w[14]-w[13];
         w[15]:=w[16]:=0;
         {? _zu>0 || w[15]:=|(_zu)
         |? _zu<0 || w[16]:=|(_zu)
         ?};
         w[17]:=SAMK.MOTGD;
         s_tran[9]+=w[9];
         s_tran[12]+=w[12];
         s_tran[13]+=w[13];
         s_tran[14]+=w[14];
         s_tran[15]+=w[15];
         s_tran[16]+=w[16];
         s_tran[17]+=w[17]
         ;~~}
         {SUBSTITUTE:'LINIA02'}
         {tmp_licz:=w[1];~~}
         {
         {? tryb<>1
         || w[1]:='';
            w[2]:='';
            w[3]:='';
            w[4]:='';
            w[5]:='';
            w[6]:=''
         ?};
         w[7]:='Dodatkowy';
         w[8]:=SAMK.ILZM2+SAMK.ILZP2;
         w[9]:=SAMK.PRZ2;
         w[10]:=SAMK.SPP2;
         w[11]:=SAMK.SKP2;
         w[12]:=exec('i_palz','karty_drog',SAMK.ref(),'D');
         w[13]:=SAMK.ZD_NORMA;
         w[14]:=(w[10]-w[11])+w[12];
         _zu:=w[14]-w[13];
         w[15]:=w[16]:=0;
         {? _zu>0 || w[15]:=|(_zu)
         |? _zu<0 || w[16]:=|(_zu)
         ?};
         w[17]:='';
         s_trand[9]+=w[9];
         s_trand[12]+=w[12];
         s_trand[13]+=w[13];
         s_trand[14]+=w[14];
         s_trand[15]+=w[15];
         s_trand[16]+=w[16]
         ;~~}
         {IF: tryb<>1}{prn1:='PRN';prn2:='PRN2'; ~~}{FI}
         {SUBSTITUTE:'LINIA02'}
         {prn1:='PRN2';prn2:='PRN'; ~~}
         {w[1]:=tmp_licz;~~}
      {FI}
      {IF: lineleft()<>0 & lineleft()<=5}{PAGE}{prn1:='PRN';prn2:='PRN'; ~~}{FI}
   {OD}
   {IF: tryb<>1}
   {IF: k_count>0}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {prn1:='PRN2';prn2:='PRNS'; k_count:=0; vp:=0;
      s_tran[1]:='Razem:';
      s_tran[7]:='Podstawowy';
      s_tran[8]:=s_tran[10]:=s_tran[11]:=''
      ; ~~}
      {SUBSTITUTE: 'LINIA02'}
      {prn1:='PRNS';prn2:='PRNS'; ~~}
      {
      s_tran[1]:='';
      s_tran[7]:='Dodatkowy';
      s_tran[8]:=s_tran[10]:=s_tran[11]:='';
      s_tran[9]:=s_trand[9];
      {! _i:=12..16 |! s_tran[_i]:=s_trand[_i] !};
      s_tran[17]:=''
      ; ~~}
      {SUBSTITUTE: 'LINIA0'}
      {prn1:='PRNS';prn2:='PRN'; ~~}
      {FONT: 'T8G'}{SPACE: 'C'}
      {IF: lineleft()>0}{PAGE}{FI}
   {FI}
   {FI}
{OD}
:!UTF-8
{TITLE:Rozdzielnik płac na konta kosztowe z wyborem składników}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__edit','__param');
   PAR_WYDR.TITLE:='Rozdzielnik płac na konta kosztowe'@;
   par:=obj_new(8);
   _upr:=exec('uprawnieniawrap','pkd',
      'Podział kosztów'@,
      'PPL_PLL_PPKO',
      'PPL_PLL_RPKO',
      'PPL_PLL_RPPK',
      'PPL_GRP_APKO'
   );
   par[5]:=rep_export();
   tresc:='rlskkwyb';

   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'RODZAJ','STRING[1]',,
         'WYBOR','STRING[1]',,
         'ATRUSE_L','STRING[1]',
      );
      __param.RODZAJ:='S';
      __param.WYBOR:='T';
      __param.ATRUSE_L:='T';

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pr_rozkk');
      __param.win_esep(__edit,'Parametry wydruku'@);
      {? ~par[5]
      || __param.win_efld(__edit,,'RODZAJ',,,,,,'Prezentacja na raporcie '@,,,'radio-buttons',,
              'Symbol konta kosztów'@,"'S'",
              'Nazwa  konta kosztów'@,"'N'");
         __param.win_efld(__edit,AH,'H',,,,,1)
      ?};
      __param.win_efld(__edit,,'WYBOR',,,,,,'Wybór składników '@,,,'radio-buttons',,
              'Predefiniowane'@,"'N'",
              'Wybór własny użytkownika'@,"'T'");
      __param.win_efld(__edit,AH,'H',,,,,1);
      __param.win_efld(__edit,,'ATRUSE_L',,,,,,'Czy uwzględniać podział kosztów?'@,,,'radio-buttons',,
              'NIE, nie uwzględniaj'@,"'N'",
              'TAK, uwzględniaj'@,"'T'");
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      __param.edit();

      par[8]:=__param.ATRUSE_L='T';

      _sql1:='select   LS.P P, KK.SYM SYM, KK.NAZWA, R.RN, R.LP, sum(LS.KW) KW, 1 RODZAJ
              from     LS left join KK join R join P using (LS.P,P.REFERENCE)
              where    P.FIRMA=:_b
              group by LS.P, KK.SYM, KK.NAZWA , R.RN, R.LP
              having   R.RN in (:_a)';
      {? __param.ATRUSE_L='T'
      || ATRUSE_L.use('kak'+'l'+($O.R+2)+('0'+$O.M+2));
         _sql1+='union all
                 select   ATRUSE_L.P P, ATRUSE_L.KON SYM, \'                                                 \' as NAZWA,
                          R.RN, R.LP, sum(ATRUSE_L.WARR) KW, 0 RODZAJ
                 from     ATRUSE_L join R join P using (ATRUSE_L.P,P.REFERENCE)
                 where    R.RN in (:_a) and ATRUSE_L.LT=\''+(~-O.LT)+'\' and P.FIRMA=:_b
                 group by ATRUSE_L.P, ATRUSE_L.KON, R.RN, R.LP, \'                                                 \' '
      ?};

      {? __param.WYBOR='N' | (_rub:=exec('code_grp','profile',,tresc,'',{? ~par[5] || 11 || 99 ?}))=''
      || FUN.emsg('Przyjęto do sprawozdania standardowy zestaw rubryk:\n'+
                  '500,797,798,792,982,983,765,958,766,959,767,960,962.');
         _rub:=',500,797,798,792,982,983,765,958,766,959,767,960,962,';
         __param.WYBOR:='N'
      ?};
      _rub:=1-_rub-1;

      par[6]:=sql('select   R.LP, R.RN, R.RT, \'   \' as ES
                   from     R where R.RN in (:_a)
                   order by R.LP, R.RN
                  ',_rub
      );

      par[7]:=_rub;
      &_rub;

      _rub:='';
      {? par[6].first()
      || {!
         |? _rub+={? +_rub|| ',' || '' ?}+$par[6].RN;
            par[6].next()
         !}
      ?};

      _BUF:=sql(_sql1,_rub,exec('ref_firma','ustawienia'));

      {? __param.ATRUSE_L='T'
      || _ndx:=_BUF.ndx_tmp(,1,'P',,0,'RN',,0,'RODZAJ',,0);
         _BUF.index(_ndx);
         {? _BUF.first()
         || {!
            |? _BUF.prefix(_BUF.P,_BUF.RN);
               {? _BUF.first()
               || {? _BUF.RODZAJ=0
                     || _BUF.last();
                        {? _BUF.RODZAJ=1
                        || _BUF.del()
                        || {! |? _BUF.del() !}
                        ?}
                     || _BUF.last()
                  ?}
               ?};
               _BUF.prefix();
               _BUF.next()
            !}
         ?}
      ?};

      {? var_pres('_BUF')=type_of(SYSLOG)
      || {? __param.WYBOR='N'
         || par[1]:=tab_tmp(1,
            'SYM','STRING['+$MS.fld_len('KK','SYM')+']','Konto - Symbol'@,
            'NAZ','STRING[50]','Konto - Nazwa'@,
            'S01','REAL','Brutto'@,
            'S02','REAL','Fundusz emerytalny'@,
            'S03','REAL','Fundusz rentowy'@,
            'S04','REAL','Fundusz chorobowy'@,
            'S05','REAL','Fundusz wypadkowy'@,
            'S06','REAL','Fundusz pracy'@,
            'S07','REAL','Fundusz gwarancyjny'@,
            'S08','REAL','Ubezpieczenie zdrowotne'@,
            'S09','REAL','Podatek'@,
            'S10','REAL','FEP'@
            )

         || _fm:='par[1]:=tab_tmp(1,'+
            '\'SYM\',\'STRING['+$MS.fld_len('KK','SYM')+']\',\'Konto\',';
            _fm+='\'NAZ\',\'STRING[40]\',\'Konto - nazwa\',';
            {? par[6].first()
             || _i:=0;
               {!
               |? _i+=1;
                  _fm+='\'S'+form(_i,-2)+'\',\'REAL\','+'\''+par[6].RT+'\',';
                  par[6].ES:='S'+form(_i,-2);
                  par[6].put();
                  par[6].next()
               !};
               _fm:=_fm-1;
               _fm+=')'
            ?};
            &_i;

            ($_fm)()
         ?};

         par[1].index(par[1].ndx_tmp('Symbol',0,'SYM',,0,'SYM',,0));

         {? _BUF.first()
         || {? __param.WYBOR='N'
            || _s:=" {? _a=500 || 'S01'
                  |? _a=797 || 'S09'
                  |? _a=792 || 'S08'
                  |? _a=982 || 'S06'
                  |? _a=983 || 'S07'
                  |? _a=765 || 'S02'
                  |? _a=958 || 'S02'
                  |? _a=766 || 'S03'
                  |? _a=959 || 'S03'
                  |? _a=767 || 'S04'
                  |? _a=960 || 'S05'
                  |? _a=962 || 'S10'
                  ?}"
            || _s:="{? par[6].find_key(_a) || par[6].ES || '' ?}"
            ?};
            {!
            |? _acr:=_s({? __param.WYBOR='N' || _BUF.RN || _BUF.LP?});
               {? +_acr=3
               || _f:=$('par[1].'+_acr+'+=_a');
                  {? par[1].find_key(_BUF.SYM,_BUF.SYM)
                     || _f(_BUF.KW);
                        par[1].put()
                     || par[1].blank();
                        par[1].SYM:=_BUF.SYM;
                        par[1].NAZ:=_BUF.NAZWA;
                        _f(_BUF.KW);
                        par[1].add()
                  ?}
               ?};
               _BUF.next()
            !}
         ?};
         obj_del(_BUF);

         _agreguj:='';
         par[2]:=obj_new(@.CLASS.PRINT,par[1].fld_num()-(~par[5]));

         {? par[5]
         || par[2].add("par[1].SYM",MS.fld_len('KK','SYM'),'Symbol konta'@);
            par[2].add("par[1].NAZ",50,'Nazwa konta'@)
         |? __param.RODZAJ='S'
         || par[2].add("par[1].SYM",35,'Symbol konta'@)
         || par[2].add("par[1].NAZ",40-{? par[1].fld_num()>10 || 5 || 0 ?},'Nazwa konta'@)
         ?};
         {! _s:=3..(par[1].fld_num())
         |! _agreguj+=',sum(S'+form(_n:=_s-(2),-2)+') S'+form(_n,-2);
            par[2].add($("par[{? par[1].SYM='RAZEM' || 3 || 1 ?}].S"+form(_n,-2)),12,par[1].fld_name(_s),1,,,,,'12,2,\'9,\'')
         !};
         par[2].s_frame();

         par[3]:=sql('select :_a from :_b',1-_agreguj,par[1])
      ?};

      prn1:='par[2]';
      lmt:=0;
      rsep:=1;
      vp:=par[4]:=h_rat:=1
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__edit','__param')
}
{COMMENT}OLD: pkonta.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: var_pres('[1]',par)<>type_of(SYSLOG) | ~par[3].first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ {? rep_export() || h_rat:=charleft() || h_rat:=charleft()/h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Miesiąc kalendarzowy: '@+date(O.R,O.M,1)$8}
   {<{ceil(lmt*h_rat)+1}'Typ listy: '@+O.T().T}
   {<{ceil(lmt*h_rat)+1}{? par[8] || 'Uwzględniono podział kosztów'@ || 'Nie uwzględniono podziału kosztów'@?}}
   {<{ceil(lmt*h_rat)+1}'Składniki płacowe: '@+par[7]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[2].width())/2); ~~ }
{FOR: par[1]}{DO}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: ~rep_export()}
   { par[1].SYM:='RAZEM'@;
     par[1].NAZ:='RAZEM'@;
     rsep:=0;
     ~~ }
   {IF: lineleft()>=2}
      {REP: par[2].fbar(lmt)}
   {ELSE}
      {PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
{FI}
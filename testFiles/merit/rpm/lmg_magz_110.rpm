:!UTF-8
{TITLE:J. Odchylenia od stanów minimalnych lub maksymalnych}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_110:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('k','w','PRN','X_Xb','kgr');
      VAR_DEL.delete('dokls','doklmin','doklmax','__iskom')";
   mag_110();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   VAR_DEL.delete('k','w','PRN');
   k:=obj_new(6); "--szerokosci kolumn--";
   w:=obj_new(6); "--wartosci kolumn--";
   dokls:=doklmin:=doklmax:=0; "--ilosc miejsc dziesietnych--";
   PRN:=obj_new(@.CLASS.PRINT,7); PRN.s_frame();

   k[1]:=20;
   k[2]:=40;
   k[3]:=8;
   k[4]:=20;
   k[5]:=20;
   k[6]:=20;
   w[1]:=w[2]:=w[3]:='';
   w[4]:=w[5]:=w[6]:=0;
   tryb:=rep_export();
   kgr:='';

   __iskom:=0
}
{END:
   mag_110(); VAR_DEL.delete('mag_110')
}
{
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
PRN.add("w[1]",k[1],'Indeks'@);
PRN.add("w[2]",k[2],'Nazwa materiału'@);
PRN.add("w[3]",k[3],'jm'@);
PRN.add("w[4]",k[4],'Stan ilościowy'@,1);
PRN.add("w[5]",k[5],'Stan minimalny'@,1);
PRN.add("w[6]",k[6],'Stan maksymalny'@,1);
"--obliczenia--";
VAR_DEL.delete('X_Xb');
X_Xb:=tab_tmp(1,
   'KTM' ,'STRING[50]'  ,'Indeks',
   'N'   ,'STRING[100]' ,'Nazwa materiału',
   'JM'  ,'STRING[10]'  ,'jm',
   'S'   ,'REAL'        ,'Stan ilościowy',
   'MIN' ,'REAL'        ,'Stan minimalny',
   'MAX' ,'REAL'        ,'Stan maksymalny',
   'MGR' ,'STRING[8]'   ,'Grupa materiałowa');

M.cntx_psh();
M.index('ARODZ'); M.prefix('T','T');
{? M.first()
||

   exec('ini_kom','#message','Stany kontrolne'@,'Brak stanów kontrolnych dla:'@,M.size);
   SM.cntx_psh();
   SM.index('SA');
   {!
   |? echo('Trwają obliczenia dla materiału: %1 %2'@[M.KTM,M.N]);
      SM.prefix(ST.MAG,M.KTM,M.KTM);
      _stan:={? SM.first() || SM.S || 0 ?};

      MST.cntx_psh();
      MST.index('MG');
      MST.prefix(M.ref(),ST.MAG);
      {? MST.first()
      ||
         X_Xb.MIN:=MST.MIN;
         X_Xb.MAX:=MST.MAX
      || MST.prefix(M.ref(),null);
         {? MST.first
         ||
            X_Xb.MIN:=MST.MIN;
            X_Xb.MAX:=MST.MAX
         ||
            X_Xb.MIN:=X_Xb.MAX:=0
         ?}
      ?};
      MST.cntx_pop();
      {? X_Xb.MIN<>0 & X_Xb.MIN>_stan
         | X_Xb.MAX<>0 & X_Xb.MAX<_stan
      ||
         X_Xb.KTM:=M.KTM;
         X_Xb.MGR:=M.MGR().KOD;
         X_Xb.N:=M.N;
         X_Xb.JM:=M.J().KOD;
         X_Xb.S:=_stan;
         X_Xb.add()
      |? X_Xb.MIN=0 & X_Xb.MAX=0
      || __iskom:=1;
         exec('add_kom','#message',M.KTM,7)
      ?};
      M.next()
   !};
   SM.cntx_pop();
   echo();
   {? __iskom || exec('end_kom','#message') ?}
?};
M.cntx_pop();

PAR_WYDR.TITLE:='ZESTAWIENIE ODCHYLEŃ OD STANÓW MINIMALNYCH LUB MAKSYMALNYCH'@;
prn1:='PRN'
;~~}
{IF: X_Xb.first()=0}{ {? __iskom=0 || FUN.info('Brak danych spełniających kryteria.'@) ?};~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Na dzień: %1'@[(date()$6)]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}
   {LINE}
   {<{lmt+1}}{'Parametry wydruku:'@}
   {<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
   {<{lmt+1}}{'Na dzień: %1'@[(date()$6)]}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xb}
{DO}
   {FIRST}
   {
   dokls:=exec('fld_prec','#field',X_Xb,'S');
   doklmin:=exec('fld_prec','#field',X_Xb,'MIN');
   doklmax:=exec('fld_prec','#field',X_Xb,'MAX');
   ~~}
   {FIRST-}
   {
   w[1]:=form(X_Xb.KTM);
   w[2]:=form(X_Xb.N);
   w[3]:=form(X_Xb.JM);
   w[4]:=form(X_Xb.S,,dokls,'.,');
   w[5]:=form(X_Xb.MIN,,doklmin,'.,');
   w[6]:=form(X_Xb.MAX,,doklmax,'.,');
   kgr:=form(X_Xb.MGR)
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
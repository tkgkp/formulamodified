:!UTF-8
{TITLE:Zatrudnienie wg stanowisk}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PAR_WYDR.TITLE:='%1\n(%2)'['Zatrudnienie wg stanowisk'@,'liczba zatrudnionych'@];
   VAR_DEL.delete('par','prn1','vp','lmt','rsep');

   par:=obj_new(7);
   par[5]:=1;
   par[6]:=exec('par_wydr_ddp','pkd',
      PAR_WYDR.TITLE,
      "  {? cur_tab().OD=#0
         || FUN.emsg('Proszę podać datę kontroli'@); 0
         || 1
         ?}
      ",
      date(),'Data kontroli'@,"1"
   );
   par[7]:=0;

   {? par[6].size()
   || _in:=sql('
         select
            UD_SKL.SYMBOL || \' \' || UD_SKL.OPIS WYDZIAL,
            STN.ST STANOWIS,
            case when OSOBA.PLEC=\'K\' then 1 else 0 end ZOK,
            case when OSOBA.PLEC=\'M\' then 1 else 0 end ZOM,
            1 ZOS,
            case when OSOBA.PLEC=\'K\' and H.RWY=1 then 1 else 0 end ZPK,
            case when OSOBA.PLEC=\'M\' and H.RWY=1 then 1 else 0 end ZPM,
            case when H.RWY=1 then 1 else 0 end ZPS,
            case when OSOBA.PLEC=\'K\' and H.RWY<1 then 1 else 0 end ZNK,
            case when OSOBA.PLEC=\'M\' and H.RWY<1 then 1 else 0 end ZNM,
            case when H.RWY<1 then 1 else 0 end ZNS
         from
            P join H using(P.REFERENCE,H.P) join UD_SKL using(H.WYDZIAL,UD_SKL.REFERENCE) join
            STN using(H.ST,STN.REFERENCE) join OSOBA join H_ZM
         where
            P.REFERENCE in (select :_a.REF from :_a) and H_ZM.KZ=\'Z\' and H.OD<=to_date(:_b) and
            (H.DO is null or to_date(:_b)<=H.DO)',
         exec('dostepne_p','schemat','PKD',__F_ZATR.P,'*'),par[6].OD
      );
      _sel_sum:='
         sum(ZOK) ZOK, sum(ZOM) ZOM, sum(ZOS) ZOS,
         sum(ZPK) ZPK, sum(ZPM) ZPM, sum(ZPS) ZPS,
         sum(ZNK) ZNK, sum(ZNM) ZNM, sum(ZNS) ZNS
      ';
      {? type_of(_in)=type_of(SYSLOG)
      || par[3]:=sql('select WYDZIAL, STANOWIS,'+_sel_sum+' from :_a group by WYDZIAL, STANOWIS',_in);
         obj_del(_in)
      ?};
      {? var_pres('[3]',par)=type_of(SYSLOG)
      || _ext:=sql('select WYDZIAL,'+_sel_sum+' from :_a group by WYDZIAL',par[3]);
         {? type_of(_ext)=type_of(SYSLOG) & _ext.first()
         || par[3].STANOWIS:=%255;
            {!
            |? par[3].WYDZIAL:=_ext.WYDZIAL;
               {! _ii:=2..10 |! par[3][_ii+1]:=_ext[_ii] !};
               par[3].add();
               _ext.next()
            !};
            _sum:=sql('select '+_sel_sum+' from :_a',_ext);
            {? type_of(_sum)=type_of(SYSLOG) & _sum.first()
            || par[3].WYDZIAL:=par[3].STANOWIS:=%255;
               {! _ii:=1..9 |! par[3][_ii+2]:=_sum[_ii] !};
               {? ~rep_export()
               || par[3].add()
               ?};
               par[7]:=1;
               obj_del(_sum)
            ?};
            obj_del(_ext)
         ?};
         par[1]:=obj_new(@.CLASS.PRINT,{? ~rep_export() || 13 || 11 ?});
         par[1].s_frame();
         {? ~rep_export()
         || par[1].add(
               "  {? par[3].STANOWIS<>%255 || par[3].STANOWIS
                  |? par[3].WYDZIAL<>%255 || par[3].WYDZIAL
                  || 'R a z e m'@
                  ?}
               ",60,'Stanowisko / Jednostka organizacyjna'@
            )
         || par[1].add("par[3].STANOWIS",40,'Stanowisko'@);
            par[1].add("par[3].WYDZIAL",40,'Jednostka org.'@)
         ?};
         {! _ii:=3..11
         |! {? ~rep_export()
            || _label:=2-par[3].fld_acr(_ii)
            || {? _ii=3
               || _label:='K - liczba etatów ogółem'
               |? _ii=4
               || _label:='M - liczba etatów ogółem'
               |? _ii=6
               || _label:='K - pełne etaty'
               |? _ii=7
               || _label:='M - pełne etaty'
               |? _ii=9
               || _label:='K - niepełne etaty'
               |? _ii=10
               || _label:='M - niepełne etaty'
               ?}
            ?};
            {? _label='S'
            || {? ~rep_export()
               || _label:='Razem'@;
                  par[1].add($('par[3]['+$_ii+']'),5,_label,1,,,,,',0,\'1,\'')
               ?}
            || {? _ii=3 | _ii=4 | _ii=6 | _ii=7 | _ii=9 | _ii=10
               || par[1].add($('par[3]['+$_ii+']'),5,_label,1,,,,,',0,\'1,\'')
               ?}
            ?}
         !};

         par[4]:=obj_new(@.CLASS.PRINT,3);
         {? ~rep_export()
         || par[4].s_frame();
            par[4].add("''",21,'Liczba etatów ogółem'@);
            par[4].add("''",21,'Pełne etaty'@);
            par[4].add("''",21,'Niepełne etaty'@)
         ?}
      ?}
   ?};

   prn1:='par[1]';
   lmt:=rsep:=0;
   vp:=1
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep')
}
{EXIT: ~par[6].size()}
{EXIT: ~par[7]}
{COMMENT}OLD: zk_stz.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   { par[5]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Data kontroli:'@} {par[6].OD}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{IF: ~rep_export()}
{  par[1].setcolor(color); par[1].ini_head();
   par[4].setcolor(color); par[4].ini_head()
}
{REP: par[4].fhbar(lmt+63)}
{WHILE: par[4].h_next()}{DO}
   {REP: par[4].h_fmline(lmt+63)}
{OD}
{<{lmt+1}'┌'+(62*'─')
}┼{COLOR: ','+color}{(7*'─')+'┬'+(7*'─')+'┬'+(7*'─')
}{(2*('┼'+(7*'─')+'┬'+(7*'─')+'┬'+(7*'─')))+'┤'}{COLOR: ',255:255:255'}
{WHILE: par[1].h_next()}{DO}
   {REP: par[1].h_fmline(lmt)}
{OD}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{ELSE}
{SUBSTITUTE: 'TABHEAD'}
{FI}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{MACRO: 'DATA'}
{par[1].ini_line()}
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: par[3]}{INDEX: par[3].ndx_tmp(,,'WYDZIAL',,,'STANOWIS',,)}{DO}
   {FONT: 'T8G'}
   {IF: par[3].STANOWIS=%255}
      {FONT: 'T8GB'}
   {FI}
   {IF: par[3].WYDZIAL<>%255 & par[3].STANOWIS=%255}
      {IF: ~rep_export()}{SUBSTITUTE: 'DATA'}
      {IF: lineleft()<=1}
         {PAGE}
      {ELSE}
         {REP: par[1].fbar(lmt)}
      {FI}
      {FI}
   {ELSE}
      {SUBSTITUTE: 'DATA'}
   {FI}
{OD}

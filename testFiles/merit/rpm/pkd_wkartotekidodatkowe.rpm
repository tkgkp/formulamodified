:!UTF-8
{TITLE:Kartoteki dodatkowe}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('tresc','par','prn1','vp','lmt','rsep');
   tresc:='wkartdod';
   par:=obj_new(16);
   par[4]:=par[7]:=0;
   P.cntx_psh();
   KART_DEF.cntx_psh();
   KART_DOD.cntx_psh();
   KART_DOD.index('KART_DOD');
   par[6]:=exec('uprawnieniawrap','pkd','kartotekach dodatkowych'@,'PKD_DOS_PRKD','PKD_DOS_PPKD');
   {? +par[6]
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[par[6]])
   || par[5]:=tab_tmp(1,'REF','INTEGER','Ref');
      par[3]:=exec('pkd_kart_dod','!pkd_zes_orka');
      {? par[4]:=par[3].size()
      || KART_DEF.prefix(par[3].SYMBOL,par[3].SYMBOL);
         {? KART_DEF.first()
         || _kol:={? +KART_DEF.CZYWART || 1 || 0 ?};
            {? KART_DEF.WER='KART'
            || par[1]:=obj_new(@.CLASS.PRINT,6+_kol);
               par[1].add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
               par[1].add("P.OSOBA().PIERWSZE",20,'Imię'@);
               par[1].add("$KART_DOD.OD",10,KART_DEF.OD,1);
               par[1].add("$KART_DOD.DO",10,KART_DEF.DO,1);
               par[1].add("KART_DOD.KT",8,KART_DEF.KT);
               par[1].add("KART_DOD.OPIS",40,KART_DEF.OPIS)
            |? KART_DEF.WER='KTOD'
            || par[1]:=obj_new(@.CLASS.PRINT,5+_kol);
               par[1].add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
               par[1].add("P.OSOBA().PIERWSZE",20,'Imię'@);
               par[1].add("KART_DOD.KT",8,KART_DEF.KT);
               par[1].add("$KART_DOD.OD",10,KART_DEF.OD,1);
               par[1].add("KART_DOD.OPIS",40,KART_DEF.OPIS)
            |? KART_DEF.WER='ODDO'
            || par[1]:=obj_new(@.CLASS.PRINT,5+_kol);
               par[1].add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
               par[1].add("P.OSOBA().PIERWSZE",20,'Imię'@);
               par[1].add("$KART_DOD.OD",10,KART_DEF.OD,1);
               par[1].add("$KART_DOD.DO",10,KART_DEF.DO,1);
               par[1].add("KART_DOD.OPIS",40,KART_DEF.OPIS)
            ?};
            {? _kol
               & (KART_DEF.WER='KART' | KART_DEF.WER='KTOD' | KART_DEF.WER='ODDO')
            || par[1].add("form(KART_DOD.WARTOSC,16,2,'9,')",16,KART_DEF.WARTOSC,1)
            ?};
            par[1].s_frame();
            par[2]:=1;
            PAR_WYDR.TITLE:='%1 - %2'['Kartoteka dodatkowa'@,par[3].NAZWA]
         ?};
         prn1:='par[1]';
         lmt:=rsep:=0;
         vp:=1;
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('tresc','par','prn1','vp','lmt','rsep');
   P.cntx_pop();
   KART_DOD.cntx_pop();
   KART_DEF.cntx_pop()
}
{COMMENT}OLD: wkartdod.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: +par[6]}
{EXIT: ~par[4]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[2]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[2]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Zakres dat: od %1 do %2'@[par[3].OD$1,par[3].DO$1]}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: ~par[5].find_key(#P.OSOBA)}
   {par[5].REF:=#P.OSOBA; par[5].add(); ~~}
   {FOR: KART_DOD}
   {FROM: exec('ref_firma','ustawienia'),KART_DEF.ref(),P.OSOBA,par[3].OD}
   {TO: exec('ref_firma','ustawienia'),KART_DEF.ref(),P.OSOBA,par[3].DO}
   {DO}
      { par[7]+=1; ~~}
      {IF: par[3].KT<>' '}
         {IF: KART_DOD.KT=par[3].KT}
            {SUBSTITUTE: 'LINIA0'}
         {FI}
      {ELSE}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
   {OD}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
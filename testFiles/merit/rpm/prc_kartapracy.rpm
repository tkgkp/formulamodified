:!UTF-8
{TITLE: Karta pracy}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'E','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   _TAB:=exec('tab_prac','!prc_czp_wydr');
   {? _TAB.first()
   || VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','pb','End','Temp','Data','Czas','Tekst');
      VAR_DEL.delete('NrCzyt','Cnt');
      tresc:='kartprac';
      PAR_WYDR.TITLE:='Karta pracy'@;

      PRN:=obj_new(@.CLASS.PRINT,6);
      {? rep_export()
      || PRN.add(
            "  '%1 %2%3, %4 %5'
                  [  P.OSOBA().NAZWISKO,
                     OSOBA.PIERWSZE,
                     {? +OSOBA.DRUGIE || ' '+OSOBA.DRUGIE || '' ?},
                     'Nr teczki'@,
                     |P.T
                  ]
            ",40,'Współpracownik'@
         );
         PRN.add("P.WYDZIAL().SYMBOL+' '+|UD_SKL.OPIS",40,'Jednostka organizacyjna'@)
      ?};
      PRN.add("Data",12,'Dzień'@);
      PRN.add("Czas",7,'Godzina'@);
      PRN.add("Tekst",40,'Wejście(wyjście) - Normalne(służbowe)'@);
      PRN.add("NrCzyt",8,'Czytnik'@);
      PRN.s_frame();

      prn1:='PRN';
      rsep:=PAR_WYDR.SEP;
      vp:=h_rat:=1;
      Temp:=Cnt:=End:=lmt:=Lp:=pb:=0;
      Data:=date();
      Czas:=time();
      Tekst:=NrCzyt:=''
   ?};
   params_set('P',_TAB)
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','pb','End','Temp','Data','Czas','Tekst');
   VAR_DEL.delete('NrCzyt','Cnt');
   exec('czytaj','#stalesys',date(),KST,'R_TZ')
}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: kartprac.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{  h_rat:=charleft; ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'Zakres dat od: '}{VAR_EDIT.D_OD}{' do: '}{VAR_EDIT.D_DO}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: ~rep_export()}
   {<{lmt+1}}{INCLUDE: p_prac.rpi}
   {<{lmt+1}}{P.WYDZIAL().SYMBOL}{IF: +(|UD_SKL.OPIS)} ({UD_SKL.OPIS}){FI}
{FI}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{  Cnt:=VAR_EDIT.D_OD;
   End:=0; ~~
}
{WHILE: Cnt<=VAR_EDIT.D_DO}
{DO}
   { {? Cnt=date(Cnt~1,Cnt~2,0)&~End
     || End:=1;
        MASK.Use('R_REJ_WW',Cnt~1,Cnt~2)
     || MASK.Use('R_REJ_WW',(Cnt+1)~1,(Cnt+1)~2);
        End:=0
     ?}; ~~
   }
   {FOR: R_REJ_WW}
   {INDEX:'R_REJ_WX'}
   {PREFIX: P.ref(),Cnt}
   {DO}
      {  Data:={? rep_export() || R_REJ_WW.DZ$1 || R_REJ_WW.DZ$1 ?};
         Czas:=form(R_REJ_WW.GD$1,-5);
         Tekst:=form(R_REJ_WW.TP+' '+R_REJ_WW.RD,37);
         NrCzyt:=form(R_REJ_WW.CZ,7); ~~
      }
      {IF: {? type_of(Temp)=4 || Temp<>R_REJ_WW.DZ || 1 ?}}
         {SUBSTITUTE: 'LINIA0'}
         {  Temp:=R_REJ_WW.DZ; ~~ }
      {ELSE}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
   {OD}
   {  {? ~End || Cnt+=1 ?}; ~~}
{OD}
{IF: type_of(Temp)=4}
{PAGE}
{FI}
{  Temp:=0; ~~ }
{MACRO-}
{IF:  VAR_EDIT.win_edit('ZAKR_DAT');
      VAR_EDIT.D_OD:=A_OKR.OD;
      VAR_EDIT.D_DO:=A_OKR.DO;
      VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_OD');
      VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_DO');
      ~VAR_EDIT.edit(
         "  _result:=__CHK.record(VAR_EDIT,,'D_OD','D_DO');
            {? _result='' & VAR_EDIT.D_OD>=VAR_EDIT.D_DO
            || FUN.emsg('Wartość w polu \"Data od\" nie może być większa niż w polu \" Data do\".');
               _result:=D_OD
            ?};
            _result
         "
      )}
   {EXIT}
{FI}
{exec('czytaj','#stalesys',VAR_EDIT.D_DO,KST,'R_TZ');~~}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
   {SUBSTITUTE: tresc}
   {FI}
{OD}
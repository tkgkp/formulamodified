:!UTF-8
{TITLE:Brakujące wyróżniki}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN: POZ.cntx_psh(); mdc:=''; lm:=0;
        PRN:=obj_new(@.CLASS.PRINT,11); PRN.sl_frame();
        exec('tab_s','wyrozniki');
        TAB_S.first();
        POZY:=tab_tmp(4,'REJ_KOD','STRING[8]','',
                        'DOK_NR','INTEGER','',
                        'DOK_NK','STRING[20]','',
                        'POZ_KON','STRING[35]','',
                        'POZ_NR','INTEGER','',
                        'DOK_DOP','DATE','',
                        'DOK_DTW','DATE','',
                        'POZ_STR','STRING[2]','',
                        'POZ_SUM','REAL','',
                        'SUM_POW','REAL','',
                        'SLU_NAZ','STRING[20]',''
                     );
       ind1:=POZY.index('?');
       ind2:=POZY.ndx_tmp('',1,'POZ_KON',,0,'REJ_KOD',,0,'DOK_NR',,0,'DOK_NK',,0)
}
{END: POZ.cntx_pop(); obj_del(PRN); obj_del(TAB_S); obj_del(POZY); &ind1; &ind2; &mdc; &lm}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: poz_wyr.rpm
Utworzony:   2003-06-16 [AMK] Artur Makos
==================================================
Zawartosc: Brakujace wyrozniki.
{COMMENT-}
{ WYROZN.win_edit('ZAK_NWP'); WYROZN.MASKA1:=35*'?';
  WYROZN.WYBOR_SL:=0; WYROZN.SLOWYR2:=''; WYROZN.OKRESDO:=0;~~}
{EXIT: ~WYROZN.edit("exec('ae_par','!fks_dks_dawr')")}
{ WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=WYROZN.MASKA1;
  {? +WYDRUKI.MASKA < WYDRUKIN.DLKON
  || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
  ?};~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='BRAKUJĄCE WYRÓŻNIKI'@;~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}
{LINE: 1}
{SPACE: 'B'}{IF: page=1}
{<1 'PARAMETRY WYDRUKU:'@}
{<1  'Maska kont: %1'@[WYDRUKI.MASKA]}{>{PRN.width()} 'Okres obrotowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AO().ROK().NAZ]}
{<1
   {? WYROZN.WYBOR_SL
      || 'Słownik wyróżnika: %1'@[TAB_S.NAZ]
      || 'Wszystkie słowniki'@
   ?}
}{>{PRN.width()}
 {? OPERATOR.DEPT
 || 'Jednostka księgowa %1'@[OPERATOR.DEPT().OD]
 || 'Wszystkie jednostki księgowe'@
 ?}}
{<1
   {? WYROZN.OKRESDO=0 ||  'Sortowanie według: kont'@ || 'Sortowanie według: dokumentów'@ ?}
}
{FI}
{FONT: 'R'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{PRN.bar()}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'Q'}{SPACE: 'C'}
{PRN.lbar()}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{IF: WYROZN.OKRESDO=0}
{PRN.add("POZY.POZ_KON",35,'Konto'@);~~}
{PRN.add("POZY.REJ_KOD",8,'Rejestr'@);~~}
{PRN.add("$POZY.DOK_NR",9,'Nr w rej.'@,1);~~}
{PRN.add("POZY.DOK_NK",20,'Symbol dokumentu'@);~~}
{PRN.add("POZY.POZ_NR",9,'Pozycja'@,1);~~}
{ POZY.index(ind2);~~}
{ELSE}
{PRN.add("POZY.REJ_KOD",8,'Rejestr'@);~~}
{PRN.add("$POZY.DOK_NR",9,'Nr w rej.'@,1);~~}
{PRN.add("POZY.DOK_NK",20,'  Symbol dokumentu'@);~~}
{PRN.add("POZY.POZ_NR",9,' Pozycja'@,1);~~}
{PRN.add("POZY.POZ_KON",35,'Konto'@);~~}
{ POZY.index(ind1);~~}
{FI}
{PRN.add("$POZY.DOK_DOP",10,'Data oper.'@,1);~~}
{PRN.add("$POZY.DOK_DTW",10,'Data zap.'@,1);~~}
{PRN.add("POZY.POZ_STR",4,'Str.');~~}
{PRN.add("form(POZY.POZ_SUM,,2,' ,')",18,'  Kwota z pozycji'@,1);~~}
{PRN.add("form(POZY.SUM_POW,,2,' ,')",18,' Suma z wyróżników'@ ,1);~~}
{IF: ~WYROZN.WYBOR_SL}{PRN.add("POZY.SLU_NAZ",20,'  Słownik wyróżnika'@);~~}{FI}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------PRZYGOTOWANIE WYDRUKU-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ POZ.cntx_psh(); _slu_ref:=null;
  {? OPERATOR.DEPT
  || POZ.index('OKWAL_O1'); POZ.prefix('T','T',OPERATOR.DEPT)
  || POZ.index('OKWAL1'); POZ.prefix('T','T')
  ?};
  KS_W.index('KSSLU'); POW.index('POZ');
  {? WYROZN.WYBOR_SL
  ||  SLU.cntx_psh();SLUAPPL.cntx_psh();
     SLU.index('NAZ'); SLU.prefix(WYROZN.SLOWYR2);
     SLUAPPL.index('NAZ'); SLUAPPL.prefix('F');
     {? SLU.first() & SLU.NAZ=WYROZN.SLOWYR2 & SLUAPPL.find_key(SLU.NAZ)
     || _slu_ref:=SLUAPPL.ref()
     ?};
    SLU.cntx_pop(); SLUAPPL.cntx_pop()
  ?};
  {? POZ.first()
  ||  {! |?
        {? exec('mask','konto',POZ.KON)
        ||  POZ.KOM().KS();
            {? WYROZN.WYBOR_SL || KS_W.prefix(KS.ref(),_slu_ref) || KS_W.prefix(KS.ref()) ?};
            {? KS_W.first()
            || {! |?
                _sum_pow:=0;   POW.prefix(POZ.ref());
                {? POW.first()
                ||  {! |? {? POW.SLU=KS_W.SLU || _sum_pow+=POW.KW ?};  POW.next() !}
                ?};
                {? _sum_pow$2<>POZ.SUM$2
                ||  POZ.DOK();
                    POZY.REJ_KOD:=DOK.REJ().KOD;
                    POZY.DOK_NR:=DOK.NR;
                    POZY.DOK_NK:=DOK.NK;
                    POZY.POZ_KON:=POZ.KON;
                    POZY.POZ_NR:=POZ.POZ;
                    POZY.DOK_DOP:=DOK.DOP;
                    POZY.DOK_DTW:=DOK.DTW;
                    POZY.POZ_STR:=POZ.STR;
                    POZY.POZ_SUM:=POZ.SUM;
                    POZY.SUM_POW:=_sum_pow;
                    POZY.SLU_NAZ:=KS_W.SLU().SLU().NAZ;
                    POZY.add()
                ?};
                KS_W.next()
               !}
            ?}
        ?};
        POZ.next()
     !}
  ?};
  POZ.cntx_pop();~~}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FOR: 'POZY'}{DO}{SUBSTITUTE: 'LINIA'}{OD}
{COMMENT}------------------------------FOOTER-LAST----------------------{COMMENT-}
{FOOTER}{FOOTER-}
{ENTIRE}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}

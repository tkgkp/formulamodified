:!UTF-8
{TITLE:Wykaz pracowników na korygowanych listach w danym roku}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__edit','__param');
   PAR_WYDR.TITLE:='Wykaz pracowników na korygowanych listach'@;
   tresc:='pkor_zus';
   par:=obj_new(7);
   par[7]:=0;
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','danych płacowych'@,'PPL_PLL_PKOR','PPL_PLL_RKOR','PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'ROK','INTEGER','Rok',
         'RODZAJ','INTEGER','Sposób prezentacji danych'
      );
      __param.ROK:=date()~1;
      __param.add();

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pkor_zus');
      __param.win_esep(__edit,'Pracownicy - Korygowane listy'@);
      __param.win_efld(
         __edit,,
         'RODZAJ',,,,,,
         'Sposób prezentacji danych'@,,,
         'radio-buttons',,
         'Pracownicy'@,"0",
         'Miesiące'@,"1"
      );
      __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok - Korygowane listy'@);
      __param.efld_opt(__edit,'mark=1',,'ROK');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[7]:=__param.edit(
                     "  {? __param.ROK<=1900
                        || FUN.emsg('Błędna wartość parametru wydruku - Raport za rok.'@); 'ROK'
                        || par[3]:=__param.ROK; 1
                        ?}
                     "
                 )
      || par[1]:=obj_new(@.CLASS.PRINT,7);
         {? par[6]:= __param.RODZAJ
            || {? ~rep_export() || par[1].add("Lp+=1; $Lp",5,'Lp.'@,1) ?};
               par[1].add("$par[2].R",4,'Rok'@,1);
               par[1].add("par[2].M",8,'Miesiąc'@,1);
               par[1].add("par[2].N",30,'Nazwisko'@);
               par[1].add("par[2].I",25,'Imię'@);
               par[1].add("par[2].P",13,'Pesel'@);
               par[1].add("par[2].T",13,'Teczka'@)
            || {? ~rep_export() || par[1].add("Lp+=1; $Lp",5,'Lp.'@,1) ?};
               par[1].add("par[2].N",30,'Nazwisko'@);
               par[1].add("par[2].I",25,'Imię'@);
               par[1].add("par[2].P",13,'Pesel'@);
               par[1].add("par[2].T",13,'Teczka'@);
               par[1].add("$par[2].R",4,'Rok'@,1);
               par[1].add("par[2].M",8,'Miesiąc'@,1)
         ?};
         par[1].s_frame();
         par[5]:=params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?};
      prn1:='par[1]';
      lmt:=Lp:=rsep:=0;
      par[4]:=vp:=1
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__edit','__param')
}
{COMMENT}OLD: pkor_zus.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~par[7]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Rok kalendarzowy: '@+$par[3]}
{FI}
{LINE}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}{
   _sql:='
      select KORN.ROK R,
             KORN.MC M,
             OSOBA.NAZWISKO N,
             OSOBA.PIERWSZE I,
             OSOBA.PESEL P,
             P.T T,
             P.REFERENCE REF
      from   KORN join P using(KORN.P,P.REFERENCE) join OSOBA
      where  KORN.ROK=:_a and P.REFERENCE in (select :_b.REF from :_b)
      order by '+{? par[6] || '1,2,3,4' || '3,4,1,2' ?};
   par[2]:=sql(_sql,par[3],
      exec('dostepne_p','schemat','PPL',__F_ZATR.P,'*'));
   ~~
}
{FOR: par[2]}
{DO}
{ P.seek(par[2].REF,,1); ~~ }
{SUBSTITUTE: 'LINIA0'}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{SUBSTITUTE: tresc}
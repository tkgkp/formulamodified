:!UTF-8
{TITLE: Lista nierozliczonych przychodów PPK}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','__TABPAR','koniec','__TABPOD');

   koniec:=0;
   PAR_WYDR.TITLE:='Lista nierozliczonych przychodów PPK';
   _d0:=date(0,0,0);
   'Okienko edycji parametrów wydruku:';
   __TABPAR:=tab_tmp(,
      'ROK','INTEGER','Rok raportu'
   );
   _ed:=__TABPAR.mk_edit(PAR_WYDR.TITLE);
   __TABPAR.win_esep(_ed,'Parametry wydruku'@);
   __TABPAR.ROK:=date()~1;
   __TABPAR.win_efld(_ed,,'ROK',,,11,,,'Data raportu'@);
   __TABPAR.efld_opt(_ed,'mark=1',,'ROK');
   exec('ok_esc','#window',__TABPAR,_ed);
   __TABPAR.win_edit(_ed);
   {? __TABPAR.edit("_rok:=cur_tab().ROK;
                     {? _rok<=0 | _rok>exec('max_rok','#datetime')
                     || FUN.emsg('Niepoprawny rok.'@);
                        'ROK'
                     || ''
                     ?}
                    ")
   || __TABPAR.add()
   ?};
   {? __TABPAR.first()
   || 'Przygotowanie tabeli z danymi:';
      __TABPOD:=exec('list_przych_pit','ppk_rep',__TABPAR.ROK);

      'Jeżeli nie znaleziono uczestników spełniających kryteria:';
      {? ~__TABPOD.size()
      || koniec:=1;
         FUN.info('Brak uczestników spełniających kryteria.'@);
         return(0)
      ?};

      PRN:=obj_new(@.CLASS.PRINT,12);
      {? ~rep_export() || PRN.add("__TABPOD.LP",2,'Lp') ?};
      PRN.add("__TABPOD.NAZWISKO",MS.fld_len(OSOBA,'NAZWISKO'),'Nazwisko');
      PRN.add("__TABPOD.PIERWSZE",MS.fld_len(OSOBA,'PIERWSZE'),'Imię');
      PRN.add("__TABPOD.T",MS.fld_len(P,'T'),'Nr teczki',1);
      PRN.add("__TABPOD.JEDN",MS.fld_len(UD_SKL,'SYMBOL'),'Jednostka organizacyjna');
      PRN.add("__TABPOD.CP",MS.fld_len(CP,'CP'),'Charakter pracy');
      PRN.add("__TABPOD.FZ",10,'Forma współpracy');
      PRN.add("__TABPOD.WYL_R",7,'Rok wyliczenia',,,,,,',,\'4.\'');
      PRN.add("__TABPOD.WYL_M",7,'Miesiąc wyliczenia');
      PRN.add("__TABPOD.WYL_LT",10,'Lista wyliczenia');
      PRN.add("__TABPOD.WYL_KW",10,'Wyliczona kwota',1,,,,,',2');
      PRN.add("__TABPOD.UWAGI",15,'Uwagi');
      PRN.s_frame();

      prn1:='PRN';
      lmt:=0;
      rsep:=vp:=1
   || koniec:=1
   ?}
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','__TABPAR','koniec','__TABPOD')
}
{EXIT: koniec}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{COMMENT} Parametry sprawozdania: {COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{<{ceil(lmt)+3}'Parametry sprawozdania'}
{<{ceil(lmt)+3}'Rok raportu:'} { __TABPAR.ROK }
{FONT: 'T8B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: __TABPOD}
{DO}
   { {? ~rep_export()
     || __TABPOD.cntx_psh();
        _osRefPr:={? __TABPOD.prev() || __TABPOD.OSOBAREF || '' ?};
        __TABPOD.cntx_pop();
        {? __TABPOD.OSOBAREF=_osRefPr
        || __TABPOD.NAZWISKO:=__TABPOD.PIERWSZE:=''
        ?}
     ?};
     ~~ }
   {FONT: 'T8'}
   {SPACE: 'B'}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
========================================================================================================================
Nazwa pliku: ppk_listaprzychodypit.rpm []
Utworzony: 05.09.2022
========================================================================================================================
Zawartość: Lista nierozliczonych przychodów PPK
{COMMENT-}
:!UTF-8
{TITLE:Zaświadczenie do zasiłku rodzinnego}
{VERSION: PEP}
{PRINTER: dziedzina:='PKD';
   exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PXX_ZASWRODZ')='T',,,,'I','B',,,,,,1,
   PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='';
   VAR_DEL.delete('par','Text','cnt','__CALL','czy_szablon','czy_paperless','czy_pep');
   _par:=params_get();
   czy_szablon:=exec('rep_set','rap_zew','PXX_ZASWRODZ',1)='T';
   czy_pep:=var_pres('_par')>100 & var_pres('PEP',_par)>0;
   O.cntx_psh();
   P.cntx_psh();
   H.cntx_psh();
   H.index('_HISTKOD');
   par:=obj_new('SKL','KW','ROK','DALEJ');
   par.DALEJ:=exec('uprawnieniawrap','pkd',
      'Naliczonych składnikach płacowych'@,
      'PPL_PLL_PSKP',
      'PPL_PLL_RSKP'
   );
   {? +par.DALEJ
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@ [par.DALEJ])
   || _paperless:=czy_pep & var_pres('active',_par.PEP)>0 & _par.PEP.active;
      {? _paperless
      || czy_paperless:=0;
         {? ~czy_szablon & exec('rep_set','rap_zew','PXX_ZASWRODZ')<>'T' & _par.PEP.fml_after=""
         || czy_paperless:=1
         ?}
      || czy_paperless:=1
      ?};
      {? _paperless
      || {? _par.PEP.P
         || params_set(
               'P',exec('zas_rodzinny','!pkd_zes_orza',_paperless,_par.PEP.P,czy_paperless),
               'paperless',_paperless,
               'par',_par,
               'CALL',null()
            )
         || params_set(
               'P',exec('zas_rodzinny','!pkd_zes_orza',_paperless,null(),czy_paperless),
               'paperless',_paperless,
               'par',_par,
               'CALL',null()
            )
         ?}
      || params_set(
            'P',exec('zas_rodzinny','!pkd_zes_orza',_paperless,null(),czy_paperless),
            'paperless',_paperless,
            'par',_par,
            'CALL',null()
         )
      ?};
      par.SKL:=obj_new(5);
      par.SKL[1]:=780; 'przychód';
      par.SKL[2]:=520; 'zasiłek wychowawczy';
      par.SKL[3]:=521; 'wyrównanie zasiłku wychowczego';
      par.SKL[4]:=784; 'koszty uzyskania';
      par.SKL[5]:=824; 'alimenty';
      Text:='';
      par.KW:=cnt:=0;
      __xr_ins:=exec('new_ins','rap_zew','PXX_ZASWRODZ')
   ?}
}
{END:
   VAR_DEL.delete('par','Text','cnt','__CALL','czy_szablon','czy_paperless','dziedzina','czy_pep');
   O.cntx_pop();
   P.cntx_pop();
   H.cntx_pop();
   FUNKCJE.OTWOLIST();
   {? var_pres('__xr_ins')=type_of(null()) || exec('del_ins','rap_zew',&__xr_ins) ?}
}
{COMMENT}OLD: zaswrodz.rpm{COMMENT-}
{COMMENT}OLD: zaswrodz.rpi{COMMENT-}
{EXIT: +par.DALEJ}
{EXIT: ~params_get().P.first()}
{EXIT: ~__xr_ins}
{FOR: params_get().P}
{DO}
   {IF: (czy_szablon | exec('rep_set','rap_zew','zaswrodz')='T' | ~(params_get().paperless & ~czy_paperless))
      & P.seek(params_get().P.P,,1)}
      {
      P.OSOBA();
      par.KW:=0;
      cnt+=1;
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'F_NAZWA',KST.NAZWA,
         'MIEJSCE',PAR_WYDR.MIEJSC,
         'DATA',date()$4+' r.',
         'F_ULICA',exec('ulica','stalesys'),
         'F_POCZTA',exec('poczta','stalesys'),
         'F_REGON',KST.REG,
         'F_NIP',KST.NIP,
         'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?},
         'ROK',par.ROK
      );
      _umowa:='';
      _adres:='';
      H.prefix(P.ref());
      {? H.last()
      || _ru:=H.RU().K;
         _umowa:=
            {? _ru='A'
            || 'na okres próbny, do dnia %1' [H.DO$6]
            |? _ru='B' & H.UMOWA().OKR_UZAS().KOD='1'
            || 'na umowę na zastępstwo'
            |? _ru='B'
            || 'na czas określony, do dnia %1' [H.DO$6]
            |? _ru='C'
            || 'na czas nieokreślony'
            |? _ru='D'
            || 'na umowę na zastępstwo'
            |? _ru='E'
            || 'na czas określony, do dnia porodu'
            || 'na %1' [RU.O]
            ?};
         {? H.OS_ADRES<>null()
         || _adres:=exec('adres','osoba',H.OS_ADRES().RODZAJ,H.OS_ADRES().OD)
         ?}
      ?};
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'NAZWISKO',OSOBA.NAZWISKO,
         'IMIE',OSOBA.PIERWSZE,
         'DRUGIE',OSOBA.DRUGIE,
         'PLEC',OSOBA.PLEC,
         'OJCIEC',OSOBA.OJCIEC,
         'UR_DATA',{? OSOBA.UR_DATA<>#0 || OSOBA.UR_DATA$6+' r.' || '' ?},
         'ADRES',_adres
      );
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'JESTZATR',P.ZA,
         'DATAZATR',P.DZA$6+' r.',
         'UMOWA',_umowa,
         'ZWOLDATA',{? P.DZ<>#0 || P.DZ$6+' r.' || '' ?},
         'ZWOLTRYB',exec('uspkod','personel',P)
      );
      ~~}
      {FOR: O}{INDEX: 'LISTYPLA'}{PREFIX: exec('ref_firma','ustawienia'),__F_ZATR.P,par.ROK}
      {DO}
         {
         FUNKCJE.OTWOLIST();
         _Prac:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE;
         par.KW+=FUNKCJE.L(par.SKL[1]);
         par.KW+=FUNKCJE.L(par.SKL[2])+FUNKCJE.L(par.SKL[3]);
         par.KW-=FUNKCJE.L(par.SKL[4])+FUNKCJE.L(par.SKL[5]); ~~
         }
      {OD}
      {
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'KWOTA',form(par.KW,,2),
         'SLOWNIE',STR.kwota_sł(par.KW)
      );
      ~~}
   {FI}
   {exec('ins_par','rap_zew',__xr_ins,cnt,'CZY_ZAP',{? params_get().paperless || '1' || ' ' ?});~~}
{OD}
{EXIT: ~exec('ready','rap_zew',__xr_ins) & ~(params_get().paperless & ~czy_paperless)}
{IF: czy_szablon}
   {FOR: params_get().P}{DO}
      {IF: P.seek(params_get().P.P,,1)}{
         _rok:=par.ROK;
         params_set('ROK',_rok);
         P.OSOBA();
         _paperless:=params_get().paperless;
         _exe:={? _paperless || 2 || 3 ?};
         _file:=exec('wypelnij','szablon','PXX_ZASWRODZ',,,_exe,,_paperless);
         {? _paperless & var_pres('_file')>0 & var_pres('INFO',_file)>0 & var_pres('STATUS',_file)>0 & _file.STATUS=1
         || exec('add_file_and_run','paperless_grp',
               P.ref(),_file.INFO,'Inne','Zaświadczenie do zasiłku rodzinnego')
         ?};
         ~~ }
      {FI}
   {OD}
   {EXIT}
{FI}
{IF: exec('rep_set','rap_zew','zaswrodz')='T'}
   {
   exec('use_tmp','rap_zew','zaswrodz');
   {? params_get().paperless
   || exec('add_file_and_run','paperless_grp',
         P.ref(),
         '@!Tmp\\macrotmp.doc',
         'Inne',
         'Zaświadczenie do zasiłku rodzinnego'
      )
   ?};
   ~~}
   {EXIT}
{ELSE}
{
   params_set(_par:=params_get());
   {? _par.paperless & ~czy_paperless
   || __CALL:=_par.par.PEP.CALL
   || __CALL:=proc_exec('rodz@zasw',#__xr_ins);
      {? czy_pep || _par.par.PEP.CALL:=__CALL ?}
   ?};
~~ }
   {IF: params_get().paperless & params_get().par.PEP.fml_after=""}
      {
      params_get().par.PEP.P:=P.ref();
      params_get().par.PEP.fml_after:="
         _pep:=obj_new('active','fml_after','P','CALL');
         _pep.active:=1;
         _pep.fml_after:=\"1\";
         _pep.P:=_a;
         _pep.CALL:=_b;
         {? var_pres('_params')>100
         || exec('obj_ntab_set','#array',_params,'PEP',_pep)
         || _params:=obj_new('PEP');
            _params.PEP:=_pep
         ?};
         params_set(_params);
         _name:='@'+tmp_dir()+exec('sep','#file')+'pkd_zaswdozasilkurodzinnego_%1'[$P.tm_stamp()]+'.pdf';
         rep_exec('pkd_zaswdozasilkurodzinnego',,0,_name,1,,1);
         exec('add_file_and_run','paperless_grp',
         _pep.P,_name,'Inne','Zaświadczenie do zasiłku rodzinnego')
      ";
      ~~}
      {EXIT}
   {ELSE}
      {INCLUDE: p_zaswrodz.rpi}
   {FI}
{FI}
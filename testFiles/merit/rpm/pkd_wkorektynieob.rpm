:!UTF-8
{TITLE:Korygowane i wycofane nieobecności}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','b_rat','tresc','h_rat','lmt','rsep','vp','__param','__okredit','__edit','__chk');
   tresc:='pkd_wkorektynieob';
   PAR_WYDR.TITLE:='Korygowane i wycofane nieobecności'@;
   P.cntx_psh();
   par:=obj_new(7);
   par[3]:=0;
   O.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','nieobecnościach'@,'PKD_EZK_OPNN','PKD_EZK_ORNN');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,
         "{? cur_tab.OD<>#0 & cur_tab().DO<>#0 || cur_tab().OD<=cur_tab().DO || 1 ?}",
         {? VAR.JESTLIST || date(O.R,O.M,1) || date(date()~1,date()~2,1) ?},'Początek okresu'@,,
         {? VAR.JESTLIST || date(O.R,O.M,0) || date(date()~1,date()~2,0) ?},'Koniec okresu'@,,
         'Nieobecności z wybranego okresu'@,'Wszystkie'@,'Z okresu'@,0,1
      );

      {? par[3]:=__param.size()
      || par[1]:=obj_new(@.CLASS.PRINT,{?  rep_export() || 14 || 11 ?});
         {? rep_export()
         || par[1].add("N.P().OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("N.P().OSOBA().PIERWSZE +' '+ N.P().OSOBA().DRUGIE",41,'Imiona'@);
            par[1].add("N.P().T",9,'Nr teczki'@)
         ?};
         par[1].add("N.NB().RN",5,'Kod'@,1);
         par[1].add("R.RT",20,'Nazwa'@);
         par[1].add("N.OD$1",10,'Od dnia'@);
         par[1].add("N.DO$1",10,'Do dnia'@);
         par[1].add("N.NK",5,'Dni kal'@,1);
         par[1].add("N.NR",5,'Dni rob'@,1);
         par[1].add("N.NG",10,'Godziny robocze'@,1,,,,,'8,2');
         par[1].add("N.ST",5,'Nr stat'@);
         par[1].add("N.LT",10,'Rozliczone listą'@);
         par[1].add("N.POD",10,'Podstawa'@,1,,,,,'10,2');
         par[1].add("{? N.RODZAJ='R' || 'Korygowana'@ || 'Wycofana'@ ?}",10,'Rodzaj'@);
         par[1].s_frame();

         par[2]:=obj_new(@.CLASS.PRINT,{? rep_export() || 14 || 11 ?});
         {? rep_export()
         || par[2].add("N.P().OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[2].add("N.P().OSOBA().PIERWSZE +' '+ N.P().OSOBA().DRUGIE",41,'Imiona'@);
            par[2].add("N.P().T",9,'Nr Teczki'@)
         ?};
         par[2].add("N.NB().RN",5,'Kod'@,1);
         par[2].add("R.RT",20,'Nazwa'@);
         par[2].add("N.OD$1",10,'Od dnia'@);
         par[2].add("N.DO$1",10,'Do dnia'@);
         par[2].add("N.NK",5,'Dni kal'@,1);
         par[2].add("N.NR",5,'Dni rob'@,1);
         par[2].add("N.NG",10,'Godziny robocze'@,1,,,,,'8,2');
         par[2].add("N.ST",5,'Nr stat'@);
         par[2].add("{? par[5] || N.LT || ~N.O().LT ?}",10,'Rozliczone listą'@);
         par[2].add("N.POD",10,'Podstawa'@,1,,,,,'10,2');
         par[2].add("{? par[5] || 'Korekta'@ || 'Wycofanie'@ ?}",10,'Rodzaj'@);
         par[2].s_frame();

         lmt:=rsep:=par[6]:=par[7]:=par[5]:=0;
         par[4]:=null();
         vp:=b_rat:=h_rat:=1;
         prn1:='par[1]';

         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','b_rat','tresc','h_rat','lmt','rsep','vp','__param','__okredit','__edit','__chk');
   P.cntx_pop();
   O.cntx_pop()
}
{COMMENT}OLD: wnieokor.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[3]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~par[6]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   { lmt:=int((charleft()-par[1].width())/2); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[6]:=1; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: od %1 do %2'@[__param.OD$1,__param.DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{  par[1].setcolor(color);
   par[2].setcolor('255:255:255'); ~~}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{ENTIRE}
{REP:'{FOR: N}{INDEX: \'NIEOBECN\'}'+
      {? ~__param.ASK
         || '{PREFIX: \'T\',P.ref()}'
         || '{FROM: \'T\',P.ref()'+{? __param.OD=#0 || '' || ',__param.OD' ?}+'}'+
            '{TO: \'T\',P.ref()'+{? __param.DO=#0 || '' || ',__param.DO' ?}+'}'
      ?}+
 '{DO}
  {FIRST}
{FONT: \'W12\'}
{SPACE: \'A\'}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{par[7]+=1}. {INCLUDE: p_prac.rpi}
{FI}
{FIRST-}
{FONT: \'T8B\'}
{SPACE: \'B\'}
{REP: par[1].fhbar(lmt)}
{ par[1].ini_line(); par[4]:=N.ref(); par[3]+=1; ~~}
{REP: par[1].fline(lmt)}
{REP: par[1].fbar(lmt)}
{FONT: \'T8\'}
{SPACE: \'B\'}
{FOR: N}{INDEX:\'NIEOTREE\'}{PREFIX: par[4]}
{DO}
{ par[5]:=1; par[2].ini_line(); ~~}
{REP: par[2].fline(lmt)}
{OD}
{IF: ~par[5]}
{ par[2].ini_line(); ~~}
{REP: par[2].fline(lmt)}
{FI}
{ par[5]:=0; ~~}
{REP: par[2].flbar(lmt)}
{OD}'}
{ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ par[3]:=0; ~~}
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
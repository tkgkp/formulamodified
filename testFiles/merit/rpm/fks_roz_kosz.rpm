:!UTF-8
{TITLE:  Definicja rozliczenia kosztów}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:  PRN:=obj_new(@.CLASS.PRINT,11); PRN.sl_frame();
         w:=obj_new(11); wyr:=PAR_SKID.get(35)='T';
         s_sign:=color:=''; prn1:=prn2:='PRN';
         lm:=ll:=lmt:=rsep:=0; vp:=1; rsep:=PAR_WYDR.RSEP; fun:='';
         co:='DEFINICJE KONT ŹRÓDŁOWYCH'@; ref:=null; separat:=separat1:=0;
         zrod:=docel:=i:=j:=0; last_ref:=obj_new(6); {! _i:=1..6 |! last_ref[_i]:=null !}
}
{END:    obj_del(PRN); &PRN; obj_del(w); &w; &wyr; &co; &ref; &separat; &separat1; &s_sign;
         &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &fun; &zrod; &docel; &i;
         obj_del(last_ref); &last_ref; &j}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: roz_kosz.rpm
==================================================
Zawartosc: Wydruk definicji rozliczenia kosztow
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{ ROZ_KON.index('ROZ_KON'); ROZ_KON.prefix(ROZ_NAZ.ref(),'Z'); zrod:=ROZ_KON.size();
  {? ROZ_NAZ.TYP<>'R'
  || ROZ_KON.index('ROZ_KON'); ROZ_KON.prefix(ROZ_NAZ.ref(),ROZ_NAZ.TYP);
     docel:=ROZ_KON.size()
  ?};
~~}
{IF: ROZ_NAZ.TYP='R' & zrod=0}
  { FUN.info('Nie zdefiniowano kont źródłowych.'@);~~}{EXIT}
{ELIF: ROZ_NAZ.TYP<>'R' & zrod=0 & docel=0}
  { FUN.info('Nie zdefiniowano kont źródłowych i kont docelowych.'@);~~}{EXIT}
{FI}
{ PRN.add("$ROZ_KON.LP",4,'Lp.'@,1);
  PRN.add("ROZ_KON.MASKA",35,'Maska konta'@);
  PRN.add("ROZ_KON.FUN().TRESC",20,'      Funkcja       #'@,,'#');
  PRN.add("ROZ_KON.STR",4,'Str.'@);
  PRN.add("form(ROZ_KON.WSP,10,2,' ,')",10,' Współcz. '@,1);
  {? wyr
  || PRN.add("w[1]",20,'     Wyróżnik1:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[2]",20,'     Wyróżnik2:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[3]",20,'     Wyróżnik3:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[4]",20,'     Wyróżnik4:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[5]",20,'     Wyróżnik5:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[6]",20,'     Wyróżnik6:     #      Słownik      #Maska kodu     ile %%'@,,'#')
  ?};~~}
{COMMENT}------------------------------HEADER i FOOTER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}{ PAR_WYDR.TITLE:='DEFINICJA ROZLICZENIA KOSZTÓW "%1" - %2'@[ROZ_NAZ.KOD,ROZ_NAZ.NAZWA];~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}{LINE: 1}
{<3 'Parametry wydruku:'@}

{IF: ROZ_NAZ.TYP='P'}
{<3 'Wartości narzutów wyznaczane poprzez: Procentowo/formuły'@ }
{ELIF: ROZ_NAZ.TYP='K'}
{<3 'Wartości narzutów wyznaczane poprzez: Klucz podziałowy'@ }
{ELSE}
{<3 'Wartości narzutów wyznaczane poprzez: \'Ręczny\' rozdzielnik'@ }
{FI}
{<3 {? ROZ_NAZ.AKC='T' || 'Status definicji rozliczenia: zaakceptowana w dniu: %1'@[ROZ_NAZ.DATA_A$3] || 'Status definicji rozliczenia: niezaakceptowana'@ ?}}
{FI}
{IF: zrod | (~zrod & page>1)}
{LINE: 1}
{<3 co}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{ vp:=1;~~}
{FI}
{HEADER-}
{FOOTER: 0}
{PRN.lbar()}{ vp:=1;~~}
{FOOTER-}
{COMMENT}--------------------------------------MACRO WYROZN---------------------------------------{COMMENT-}
{MACRO: 'WYROZN'}
{ i:=1;~~}
{WHILE: i<=6}
{DO}
{ ROZ_WYR.index('ROZ_WYR'); ROZ_WYR.prefix(ROZ_KON.ref(),i); w[i]:='';~~}
{IF: ROZ_WYR.first()}
{ w[i]:=ROZ_WYR.SLUAPPL().SLU().NAZ;~~}
{FI}
{ i+=1;~~}
{OD}
{MACRO-}
{COMMENT}--------------------------------------MACRO WYROZN1---------------------------------------{COMMENT-}
{MACRO: 'WYROZN1'}
{ i:=1; j:=1; {! _i:=1..11 |! w[_i]:='' !}; separat1:=rsep; rsep:=0;~~}
{WHILE: j=1 | last_ref[1]<>null | last_ref[2]<>null | last_ref[3]<>null |
        last_ref[4]<>null | last_ref[5]<>null | last_ref[6]<>null}
{DO}
{ i:=1; {! _i:=1..6 |! w[_i]:='' !};~~}
{WHILE: i<=6}
{DO}
{ ROZ_WYR.index('ROZ_WYR'); ROZ_WYR.prefix(ROZ_KON.ref(),i); w[i]:='';~~}
{IF: (j=1 & ROZ_WYR.first()) | (j=0 & last_ref[i]<>null & ROZ_WYR.seek(last_ref[i]))}
{ w[i]:=form(ROZ_WYR.MASKA,8)+form(ROZ_WYR.PROC,12,2,' ,');
  last_ref[i]:={? ROZ_WYR.next() || ROZ_WYR.ref() || null ?};~~}
{FI}
{ i+=1;~~}
{OD}
{IF: w[1]<>'' | w[2]<>'' | w[3]<>'' | w[4]<>'' | w[6]<>''}{SUBSTITUTE: 'LINIA0'}{FI}
{ j:=0;~~}
{OD} { rsep:=separat1;~~}
{MACRO-}
{COMMENT}-------------------------------------PRZYGOTOWANIE WYDRUKU------------------------------------{COMMENT-}
{COMMENT}----------------------------------------KONTA ZRODLOWE----------------------------------------{COMMENT-}
{IF: zrod}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR:    'ROZ_KON'}
{INDEX:  'ROZ_KON'}
{PREFIX: ROZ_NAZ.ref(),'Z'}
{DO}
{SUBSTITUTE: 'WYROZN'}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'WYROZN1'}
{OD}
{FOOTER}{FOOTER-}
{IF: lineleft()<>0}{PRN.lbar()}{FI}{ co:='';~~}
{FI}
{IF: docel}
{COMMENT}------------------------------------KONTA DOCELOWE, KLUCZE------------------------------------{COMMENT-}
{COMMENT}------------------------------------ROZLICZENIE PROCENTOWE------------------------------------{COMMENT-}
{IF: ROZ_NAZ.TYP='P'}
{ obj_del(PRN); PRN:=obj_new(@.CLASS.PRINT,11); PRN.sl_frame();
  PRN.add("w[7]",4,'Lp.'@,1);
  PRN.add("w[8]",35,'Maska konta'@);
  PRN.add("w[9]",4,'Str.'@);
  PRN.add("w[10]",6,'  %   #',1,'#');
  PRN.add("w[11]",24,'        Formuła         #'@,,'#');
  {? wyr
  || PRN.add("w[1]",20,'     Wyróżnik1:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[2]",20,'     Wyróżnik2:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[3]",20,'     Wyróżnik3:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[4]",20,'     Wyróżnik4:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[5]",20,'     Wyróżnik5:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[6]",20,'     Wyróżnik6:     #      Słownik      #Maska kodu     ile %%'@,,'#')
  ?};
  co:='DEFINICJE KONT DOCELOWYCH'@;~~}
{IF: lineleft()<10 & lineleft<>0}
{PAGE}
{FOOTER: 0}
{PRN.lbar()}{ vp:=1;~~}
{FOOTER-}
{ELSE}
{FOOTER: 0}
{PRN.lbar()}{ vp:=1;~~}
{FOOTER-}
{LINE: 1}
{FONT: 'W8'}{SPACE: 'C'}
{<3 co}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; PRN.setcolor('255:255:255');~~}
{FOR:   'ROZ_KON'}
{INDEX: 'ROZ_KON'}
{PREFIX: ROZ_NAZ.ref(),'P'}
{DO}
{ w[7]:=$ROZ_KON.LP; w[8]:=ROZ_KON.MASKA; w[9]:=ROZ_KON.STR; w[10]:=form(ROZ_KON.WSP,6,2,' ,'); w[11]:=ROZ_KON.FORM;~~}
{SUBSTITUTE: 'WYROZN'}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'WYROZN1'}
{OD}
{FOOTER}{FOOTER-}
{IF: lineleft()<>0}{PRN.lbar()}{FI}
{ELIF: ROZ_NAZ.TYP='K'}
{COMMENT}---------------------------------ROZLICZENIE Z KLUCZAMI------------------------------------{COMMENT-}
{ obj_del(PRN);
  PRN:=obj_new(@.CLASS.PRINT,20);
  PRN.sl_frame();
  PRN.add("w[7]",4,'Lp.'@,1);
  PRN.add("w[8]",14,' Rodzaj konta #'@,,'#');
  PRN.add("w[9]",35,'Maska konta'@);
  PRN.add("w[10]",16,'     Funkcja    #'@,,'#');
  PRN.add("w[11]",4,'Str.');
  {? wyr
  || PRN.add("w[1]",20,'     Wyróżnik1:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[2]",20,'     Wyróżnik2:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[3]",20,'     Wyróżnik3:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[4]",20,'     Wyróżnik4:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[5]",20,'     Wyróżnik5:     #      Słownik      #Maska kodu     ile %%'@,,'#');
     PRN.add("w[6]",20,'     Wyróżnik6:     #      Słownik      #Maska kodu     ile %%'@,,'#')
  ?};
  co:='DEFINICJE KONT KLUCZY I KONT DOCELOWYCH'@;~~}
{IF: lineleft()<11 & lineleft<>0}
{PAGE}
{FOOTER: 0}
{PRN.lbar()}{ vp:=1;~~}
{FOOTER-}
{ELSE}
{FOOTER: 0}
{PRN.lbar()}{ vp:=1;~~}
{FOOTER-}
{LINE: 1}
{FONT: 'W8'}{SPACE: 'C'}
{<3 co}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; PRN.setcolor('255:255:255');~~}
{FOR:    'ROZ_KON'}
{INDEX:  'ROZ_KON'}
{PREFIX: ROZ_NAZ.ref(),'K'}
{DO}
{ w[7]:=$ROZ_KON.LP; w[8]:='Konto klucz'@; w[9]:=ROZ_KON.MASKA; w[11]:=ROZ_KON.STR;
  w[10]:=ROZ_KON.FUN().TRESC; ref:=#ROZ_KON.ref();~~}
{SUBSTITUTE: 'WYROZN'}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'WYROZN1'}
{ separat:=rsep; rsep:=0;~~}
{FOR:    'ROZ_KON'}
{INDEX:  'ROZ_KLU'}
{PREFIX: ROZ_NAZ.ref(),'D',ref}
{DO}
{ w[7]:=''; w[8]:='Konto docelowe'@; w[9]:=ROZ_KON.MASKA; w[11]:=ROZ_KON.STR;
  w[10]:=ROZ_KON.FUN().TRESC; ref:=#ROZ_KON.ref();~~}
{SUBSTITUTE: 'WYROZN'}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'WYROZN1'}
{OD}
{ rsep:=separat;~~}
{OD}
{FOOTER}{FOOTER-}
{IF: lineleft()<>0}{PRN.lbar()}{FI}
{FI}
{FI}
:!UTF-8
{TITLE:Arkusz pomocniczy spisu z natury}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,12); PRN.s_frame();
   c:=obj_new(12);
   {! _i:=1..12 |! c[_i] := '' !};
   i:=0; __std:=null; slicznik:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0; par5:=''; par6:='';
   sum_wart:=0; licznik:=0; std_index:=''; std_prefix:=0;
   podpisy:=0
}
{END:
   obj_del(PRN);
   obj_del(c);
   &i; &color; &prn1; &prn2; &__std; &slicznik;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3; &par4; &par5; &par6;
   &sum_wart; &licznik; &std_index; &std_prefix;
   &podpisy
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_inp_p.rpm
Utworzony  : 2016-04-19
Zawartość  : Arkusz pomocniczy spisu z natury
==================================================
{COMMENT-}
{PAR_WYDR.cntx_psh();~~}
{INCLUDE: wsp_pw.rpi}
{PAR_WYDR.cntx_pop();~~}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("c[1]",7,'Lp.',,'#');
      PRN.add("c[2]",20,'Nr inwentarz.',,'#');
      PRN.add("c[3]",40,'Nazwa środka',,'#');
      PRN.add("c[4]",25,'Nr fabryczny',,'#');
      PRN.add("c[5]",11,'Data zakupu',,'#');
      PRN.add("{? type_of(c[6]) = 1 || form(c[6],,2,' ,') || c[6] ?}",14,'Cena',1,'#');
      PRN.add("c[8]",25,'Użytkownik',,'#');
      PRN.add("c[9]",35,'Uwagi',,'#');

      PAR_WYDR.TITLE:='Arkusz pomocniczy spisu z natury. Nr ' + $SRXA.NR; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{<1 'PARAMETRY ARKUSZA:'}
{COMMENT} ---- Arkusz, Jedn. ks., Jedn. org., Pomieszczenie ---- {COMMENT-}
{
   par1:='Jedn. ks.: '+SRXA.ODD().OD;
   par2:='Jedn. org.: '+SRXA.JORG().SYMBOL;
   par3:='Pomieszczenie: '+SRXA.POM().S;
   par5:='Inwentaryzację wykonano: '+{? SRXI.DZ<>date(0,0,0) || SRXI.DZ$6 || $SRXI.DZ ?};
   par6:='Stan na dzień: '+{? SRXI.DS<>date(0,0,0) || SRXI.DS$6 || $SRXI.DS ?}
;~~}
{IF: SRXA.ODD().OD<>''}{<1 par1}{ELSE}{<1 'Wszystkie jedn. ks.'}{FI}{<40 par5}
{IF: SRXA.JORG().SYMBOL<>''}{<1 par2}{<40 par6}{ELSE}{<1 par6}{FI}
{IF: SRXA.POM().S<>''}{<1 par3}{FI}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: podpisy = 0}{SUBSTITUTE: 'TABHEAD'}{FI}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{FOR: SRXD}{INDEX: 'NRI'}{PREFIX: SRXA.ref() }
 {DO}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
 {
      licznik+=1;
      slicznik:=0;
      c[1]:=licznik;
      c[2]:=SRXD.NRI;
      c[3]:=SRXD.NST;
      c[4]:=SRXD.NF;
      c[5]:=form(SRXD.DZ,,1);
      c[6]:=SRXD.WARP;
      c[8]:=SRXD.OSOBA().NAZWISKO + ' ' + SRXD.OSOBA().PIERWSZE;
      c[9]:='';
      {? SRXD.GRP<>'E' || sum_wart+=SRXD.WARP ?};
      __std:=SRXD.ref()
 ;~~}
  {IF: lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
   {rsep:=0;~~}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
   c[1] := '';
   c[2] := '';
   c[3] := 'Ogółem ARKUSZ:';
   c[4] := '';
   c[5] := '';
   c[6] := sum_wart;
   c[8] := '';
   c[9] := ''
;~~}
{SUBSTITUTE: 'LINIA0'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{FONT: 'W8'}{SPACE: 'C'}
{podpisy:=1;~~}



{ENTIRE}
{<20 'Skład komisji inwentaryzacyjnej:'}{<75 'Podpis'}{<120 'Inne osoby obecne przy spisie:'}

{<1 'Przewodniczący:'}{<20 50*'.'}{<75 30*'.'}{<120 '1. '+ 45*'.'}

{<1 'Członkowie:'}{<20 '1. '+ 50*'.'}{<75 30*'.'}{<120 '2. '+ 45*'.'}

{<20 '2. '+ 50*'.'}{<75 30*'.'}{<120 '3. '+ 45*'.'}

{<20 '3. '+ 50*'.'}{<75 30*'.'}{<120 '4. '+ 45*'.'}


{<1 'Imię i nazwisko osoby odpowiedzialnej materialnie: '}{<75 'Podpis:'}

{<20  50*'.'}{<75 30*'.'}

{<1 'Wycenił(a): '}{<75 'Podpis:'}

{<20  50*'.'}{<75 30*'.'}
{ENTIRE-}
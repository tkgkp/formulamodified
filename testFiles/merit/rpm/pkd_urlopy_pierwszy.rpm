:!UTF-8
{TITLE: Pierwszy urlop z podziałem na miesiące}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par1','par2','PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','rok');
   PAR_WYDR.TITLE:='Dni pierwszego urlopu z podziałem na miesiące'@;
   P.cntx_psh();
   rok:=exec('par_wydr_ddp','pkd',
     PAR_WYDR.TITLE,,,,,,,,,,,,,
     date()~1,
     "{? cur_tab().ROK<2004 || FUN.emsg('Wydruk niedostępny dla lat wcześniejszych niż 2004.'@); 0 || 1 ?}"
   );
   {? rok.size()
   || tresc:='url_pie';
      Lp:=0;
      par1:=obj_new(12);
      par2:=obj_new(12);
      {! _lp:=1..12
      |! _mc:=date(,_lp,0)$8;
         par2[_lp]:=(_mc*' ')+_mc
      !};
      PRN:=obj_new(@.CLASS.PRINT,18);
      {? ~rep_export()
      || PRN.add("Lp+=1",4,'Lp.'@,1,,,,,',,\'9.\'');
         PRN.add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE",30,'Nazwisko i imię'@)
      ||
         PRN.add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
         PRN.add("P.OSOBA().PIERWSZE",20,'Imię'@)
      ?};
      PRN.add("KART_URL.LIM_AKT",7,'Urlop bieżący'@,1,,,,,',2,\'9,\'');
      PRN.add("P.NAB_URL$1",10,'Data nabycia'@);
      {! _lp:=1..12 |! PRN.add($('par1['+$_lp+']'),5,3+($('par2[_a]'))(_lp),1,,,,,',2,\'9,\'') !};
      PRN.add("KART_URL.URL_WYK",12,'Wykorzystany'@,1,,,,,',2,\'9,\'');
      PRN.add("KART_URL.URL_POZ",9,'Pozostało'@,1,,,,,',2,\'9,\'');
      PRN.s_frame();
      prn1:='PRN';
      lmt:=rsep:=0;
      vp:=h_rat:=1;
      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?}
}
{END:
   VAR_DEL.delete('par1','par2','PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','rok');
   P.cntx_pop()
}
{COMMENT}OLD: url_pie.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~rok.size()}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: vp}
{FONT: 'T8'}{ h_rat:=charleft(); ~~}
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Rok kalendarzowy: %1'@[$rok.ROK]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{FOR: KART_URL}{INDEX: 'PRAC_ROK'}{PREFIX: P.ref(),rok.ROK}{DO}
{IF: KART_URL.PIERWSZY='T' & exec('czy_nowy','kart_url') & KART_URL.LIM_AKT}
  { _tmp:=exec('pierwszy','kart_url',1);
    {! _ii:=1..12 |! par1[_ii]:=0 !};
    {? _tmp.first()
    || {!
       |? par1[_tmp.N]:=_tmp.W;
          _tmp.next()
      !}
    ?};
    obj_del(_tmp); ~~
  }
{SUBSTITUTE: 'LINIA0'}
{FI}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: params_get().P}{DO}
{IF: P.seek(params_get().P.SQL,,1)}
{SUBSTITUTE: tresc}
{FI}
{OD}
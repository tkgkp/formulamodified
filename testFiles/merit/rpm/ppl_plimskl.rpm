:!UTF-8
{TITLE:Limit składników list płac}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','__param','__okredit','__edit');
   tresc:='limskl';
   PAR_WYDR.TITLE:='Limit składników rozliczonych na listach płac'@;
   par:=obj_new(12);
   par[12]:=rep_export();
   P.cntx_psh();
   {? par[12]
   || R.cntx_psh();
      R.prefix();
      par[1]:=obj_new(@.CLASS.PRINT,R.size()+4);
      R.cntx_pop()
   || par[1]:=obj_new(@.CLASS.PRINT,14)
   ?};

   {? ~par[12] || par[1].add("par[9]",5,'Lp.'@,1,,,,,',,\'9.\'') ?};
   par[1].add("par[4]:=0;P.T",10,'Nr teczki'@,1);
   par[1].add("P.OSOBA().NAZWISKO",20,'Nazwisko'@);
   par[1].add("OSOBA.PIERWSZE",20,'Imię'@);
   par[4]:='';
   par[11]:=0;
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'TYP','STRING[1]','Typ składników'@,
         'LOD','INTEGER','Miesiąc listy od',
         'LDO','INTEGER','Miesiąc listy do',
         'RODZ','INTEGER','Wybór miesiąca wstecz'
      );
      __param.RODZ:=__param.LOD:=__param.LDO:=0;
      __param.add();

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_plimskl');
      __param.win_esep(__edit,'Wybór składników list płac typu '@+O.T().T);
      __param.win_efld(__edit,,'TYP',,,,,,,,,
         'radio-buttons',,
         'Nieobecności pracowników '@,"'N'",
         'Przepracowane godziny '@,"'R'",
         'Kredyty '@,"'K'",
         'Składniki typu T '@,"'T'",
         'Składniki typu # '@,"'#'",
         'Stałe składniki listy '@,"'S'",
         'Wszystkie składniki '@,"''"
      );
      __param.win_esep(__edit,'Zakres miesięcy list płac typu '@+O.T().T);
      __param.win_efld(__edit,,'LOD',,,3,,,'Miesiąc listy od'@);
      __param.win_efld(__edit,,'LDO',,,3,,,'Miesiąc listy do'@);
      __param.win_efld(__edit,,'RODZ',,,,,,'Rodzaj sprawozdania'@,,,
         'radio-buttons',,
         'Przekroczone limity'@,"0",
         'Pełna kartoteka'@,"1"
      );
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[11]:=__param.edit(
            "  {? __param.LOD<0 | __param.LOD>127
               || FUN.emsg('Miesiąc listy od musi być poprawnie wypełniony (0-127).'@); 0
               |? __param.LOD>__param.LDO
               || FUN.emsg('Miesiąc listy od nie może być większy od wartości pola Miesiąc listy do. '@+
                          '\n Dozwolone wartości z przedziału (0-127).\n'@); 0
               |? __param.LDO<0 | __param.LDO>127
               || FUN.emsg('Miesiąc listy od nie może być większy od wartości pola Miesiąc listy do. '@+
                          '\n Dozwolone wartości z przedziału (0-127).\n'@); 0
               |? __param.LOD>__param.LDO
               || FUN.emsg('Miesiąc listy od nie może być większy od wartości pola Miesiąc listy do.'@); 0
               || 1
               ?}
            "
         )
      || par[7]:=_od:=__param.LOD;
         par[8]:=_do:=__param.LDO;
         par[4]:=__param.TYP;

         {? (_rub:=exec('code_grp','profile',,tresc+par[4],par[4],{? ~par[12] || 10 ?}))<>''
         || par[2]:=sql('select   R.LP, R.RN, R.RT, 0.01-0.01 LIMIT
                         from     R where R.RN in (:_a)
                         order by R.LP, R.RN
                        ',1-_rub-1
            );

            {? var_pres('[2]',par)=type_of(SYSLOG) & par[2].first()
            || par[2].ndx_tmp('Kod',0,'RN',,0);
               _ndx:=par[2].ndx_tmp('Numer',0,'LP',,0);
               par[2].index(_ndx);
               {? ~(par[6]:=__param.RODZ)
               || _wnd:=par[2].mk_sel('Limity'@,'N',0,'ppl_plimskl1',,,,,'U');
                  par[2].win_fld(_wnd,,'LP',,,5,,1,'Numer'@);
                  par[2].win_fld(_wnd,,'RN',,,5,,1,'Kod'@);
                  par[2].win_fld(_wnd,,'RT',,,20,,1,'Nazwa'@);
                  par[2].win_fld(_wnd,,'LIMIT',,,15,,,'Limit'@);
                  par[2].win_act(_wnd,0,'Popraw','Zmień'@@,,,,,1);
                  par[2].win_sel(_wnd);
                  par[2].select();
                  par[2].index(_ndx)
               ?};
               par[3]:='';
               {! _ii:=1..par[2].size()
               |? _ff:='_a:=FUNKCJE.L('+$par[2].RN+
                     {? _od | _do || ',,'+$_od+','+$_do || '' ?}+
                     ')-'+form(par[2].LIMIT,,,'9.')+
                     {? par[2].LIMIT || ';{?_a<0||_a:=0?}' || '' ?}+
                     ';par[4]+=(_a<>0);_a';
                  par[1].add($_ff,12,par[2].RT,1,,,,,'12,2,\'9,\'');
                  par[3]+=', '+$par[2].RN;
                  par[2].next()
               !}
            ?}
         ?};

         par[1].s_frame();
         prn1:='par[1]';
         lmt:=rsep:=par[4]:=0;
         vp:=par[5]:=par[9]:=1;
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','__param','__okredit','__edit')
}
{COMMENT}OLD: plimskl.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~par[11]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Rodzaj raportu: '@+{? par[6] || 'pełna kartoteka'@ || 'przekroczone limity'@ ?}}
   {<{lmt+1}'Uwzględniane składniki: '@+(1-par[3])}
   {<{lmt+1}'Miesiąc kalendarzowy: '@+date(O.R,O.M,1)$8}
   {<{lmt+1}'Typ listy: '@+O.T().T}
   {<{lmt+1}'Zakres list:'@}{' od '@} {par[7]} {' do '@} {par[8]}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~par[12]}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: par[1].ini_line(); par[4]}
   { par[9]+=1; ~~ }
   {WHILE: par[1].next()}{DO}
      {REP: par[1].fline(lmt)}
   {OD}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
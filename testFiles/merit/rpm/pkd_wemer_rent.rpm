:!UTF-8
{TITLE:Wykaz wprowadzonych rent i emerytur}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='pkd_wemer_rent';
   PAR_WYDR.TITLE:='Wprowadzone renty i emerytury'@;
   P.cntx_psh();
   ZC.cntx_psh();
   par:=obj_new(8);
   par[7]:=exec('uprawnieniawrap','pkd','rentach i emeryturach'@,'PKD_DOS_PPER','PKD_DOS_PRER');
   {? +par[7]
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[par[7]])
   || par[1]:=obj_new(@.CLASS.PRINT,9);
      {? ~rep_export()
      || par[1].add("$(par[6]+=1)",5,'Lp'@,1,,,,,',,\'9.\'')
      ?};
      par[1].add("OSOBA.NAZWISKO",20,'Nazwisko'@);
      par[1].add("OSOBA.PIERWSZE",20,'Imię'@);
      par[1].add("OSOBA.UR_DATA$1",10,'Data urodzenia'@);
      par[1].add("{? EMER_REN.E_R='E' || 'Emerytura' || 'Renta' ?}",11,'Renta Emerytura'@);
      par[1].add("EMER_REN.NRE",20,'Nr emerytury/renty'@);
      par[1].add("{? EMER_REN.OD<>date(0,0,0) || EMER_REN.OD$1 || '' ?}",11,'Początek świadczenia'@);
      par[1].add("{? EMER_REN.DO<>date(0,0,0) || EMER_REN.DO$1 || '' ?}",11,'Koniec świadczenia'@);
      par[1].add("par[8]",80,'Inspektorat ZUS'@);

      par[1].s_frame();

      par[3]:=exec('pkd_emer_rent','personel');

      par[2]:=tab_tmp(1,'REF','INTEGER','Osoba');
      {? par[3].first
      || {? par[3].WYBOR='0'
         || _tab:=sql('select F_ZATR.KOD as KOD, OSOBA.REFERENCE as OSOBA '+
            'from EMER_REN join OSOBA join P join F_ZATR '+
            'where P.REFERENCE in (select :_a.REF from :_a) ',
            exec('dostepne_p','schemat','PKD',__F_ZATR.P,'*'))
         || _tab:=sql('select F_ZATR.KOD as KOD, OSOBA.REFERENCE as OSOBA '+
            'from EMER_REN join OSOBA join P join F_ZATR '+
            'where P.REFERENCE in (select :_a.REF from :_a) ',
            exec('dostepne_p','schemat','PKD','P','*'));
            _tab1:=sql('select F_ZATR.KOD as KOD, OSOBA.REFERENCE as OSOBA '+
            'from EMER_REN join OSOBA join P join F_ZATR '+
            'where P.REFERENCE in (select :_a.REF from :_a) ',
            exec('dostepne_p','schemat','PKD','Z','*'))
         ?};

         P.index('OSOBA');
         ZC.index('ZLECDAT');
         _akt:="{? par[3].OD=date(0,0,0)
                || return(1)
                ?};
                _akt:=0;
                {? _c='P'
                || P.prefix(_a,_b);
                   {? P.last()
                   || {! |?
                       {? P.F_ZATR().KOD='P' & P.DZA<=par[3].DO & (P.DZ=date(0,0,0) | P.DZ>=par[3].OD)
                       || _akt:=1
                       ?};
                       ~_akt & P.prev()
                      !}
                   ?}
                |? _c='Z'
                || ZC.prefix(_b,_a);
                   {? ZC.last()
                   || {!
                      |? {? ZC.DU<=par[3].DO & ZC.DW>=par[3].OD
                         || _akt:=1
                         ?};
                         ~_akt & ZC.prev()
                      !}
                   ?}
                ?};
                _akt";
         {? var_pres('_tab')=type_of(SYSLOG) & _tab.first()
         || {!
            |? _ref:=_tab.OSOBA;
               {? OSOBA.seek(_ref,,1) & ~par[2].find_key(#OSOBA.ref) & _akt(OSOBA.ref,exec('ref_firma','ustawienia'),_tab.KOD)
               || par[2].REF:=#OSOBA.ref();
                  par[2].add()
               ?};
               _tab.next()
            !}
         ?};
         {? var_pres('_tab1')=type_of(SYSLOG) & _tab1.first()
         || {!
            |? _ref:=_tab1.OSOBA;
               {? OSOBA.seek(_ref,,1) & ~par[2].find_key(#OSOBA.ref) & _akt(OSOBA.ref,exec('ref_firma','ustawienia'),_tab1.KOD)
               || par[2].REF:=#OSOBA.ref();
                  par[2].add()
               ?};
               _tab1.next()
            !}
         ?};

         prn1:='par[1]';
         par[4]:=par[6]:=lmt:=rsep:=0;
         par[5]:=vp:=b_rat:=h_rat:=1

      || par[7]:='Nie podano parametrów!'
      ?}
   ?}
}
{END:
   P.cntx_pop();
   ZC.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat')
}
{EXIT: +par[7]}
{EXIT: ~par[2].size}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   { par[5]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
{IF:par[3].OD=date(0,0,0)}
   {<{lmt+1}'Zakres dat: bez ograniczeń'@}
{ELSE}
   {<{lmt+1}'Zakres dat: od %1 do %2'@[par[3].OD$1,par[3].DO$1]}
{FI}
{IF: par[3].WYBOR='0'}
   {IF: __F_ZATR.P='P'}
   {<{lmt+1}'Forma zatrudnienia: pracownicy'@}
   {ELSE}
   {<{lmt+1}'Forma zatrudnienia: zleceniobiorcy'@}
   {FI}
   {ELSE}
   {<{lmt+1}'Forma zatrudnienia: pracownicy i zleceniobiorcy'@}
{FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{  par[1].setcolor(color);
   par[1].ini_head()
}
{REP: par[1].fhbar(lmt)}
{WHILE: par[1].h_next()}{DO}
{REP: par[1].h_fmline(lmt)}
{OD}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'T8G'}
{SPACE: 'C'}
{ par[4]:=0; ~~ }
{MACRO-}
{MACRO: tresc}
   {ENTIRE}
   {FOR: EMER_REN}{INDEX: 'EMER_REN'}{PREFIX: exec('ref_firma','ustawienia'),OSOBA.ref}{DO}
   {IF:par[4]}
   {REP: par[1].fbar(lmt)}
   {FI}
      {par[8]:='';
      {? EMER_REN.ZUS<>null()
      || EMER_REN.ZUS();
         _ad:=ADRES.NAZWA+',';
         _ad+={? |ADRES.ULICA='' | (3+(-ADRES.ULICA)='pl.') | (5+(-ADRES.ULICA)='plac') | 3+(-ADRES.ULICA)='ul.'
              || ''
              || 'ul.'
              ?};
         _ad+=ADRES.ULICA;
         {? +ADRES.DOM || _ad+=' '+ADRES.DOM ?};
         {? +ADRES.LOKAL || _ad+=' m.'+ADRES.LOKAL ?};
         {? +ADRES.KOD_POCZ || _ad+={? +_ad ||'; '||''?}+ADRES.KOD_POCZ ?};
         {? +ADRES.POCZTA
         || _ad+={? +_ad ||' '||''?}+ADRES.POCZTA
         |? +ADRES.MIASTO
         || _ad+={? +_ad ||' '||''?}+ADRES.MIASTO
         ?};
         par[8]:=_ad
      ?};~~}
      { par[1].ini_line(); ~~ }
      {WHILE: par[1].next()}{DO}
         {REP: par[1].fline(lmt)}
      {OD}
     {{? lineleft() || par[4]:=1 || par[4]:=0 ?};~~}
   {OD}
   {ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U
{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8G'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: OSOBA}{INDEX:'OSOBA'}{DO}
   {IF: par[2].find_key(#OSOBA.ref())}
      {SUBSTITUTE: tresc}
      {{? lineleft() || par[4]:=1?};~~}
   {FI}
{OD}

:!UTF-8
{TITLE: D. Tabela roczna zmian stanu środka}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,12); PRN.s_frame();
   c:=obj_new(12);
   {! _i:=1..12 |! c[_i] := '' !};
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   __std:=null; slicznik:=0;
   par0:=0; par1:=0; par2:=0; par3:=0; par4:=0; par5:=0;
   wspocz:=wsprze:=wszwi:=wsskr:=wskon:=0;
   uspocz:=usprze:=uswc:=uszwi:=usskr:=uskon:=0;
   zmtabpoc:=date(0,0,0)
}
{END:
   obj_del(PRN);
   obj_del(c);
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par0; &par1; &par2; &par3;&par4;&par5;
   &opcja; &zmtabpoc;
   &wspocz; &wsprze; &wszwi; &wsskr; &wskon;
   &uspocz; &usprze; &uswc; &uszwi; &usskr; &uskon
}
{EXIT:
   _exit:=0;

   _res:=SSTALE.AO().RES;
   OKRO_F.cntx_psh();
   OKRO_F.index('SRODKI');
   OKRO_F.prefix(REF.FIRMA,_res);
   {? OKRO_F.first()
   || {! |? OKRO_F.OES=0 & OKRO_F.next() !};
      zmtabpoc:=OKRO_F.POCZ
   ?};
   OKRO_F.cntx_pop();

   {? FINFO.TOR_D='T'
   || opcja:=FUN.choice('Wybierz rodzaj zestawienia zmian'@,,'PODATKOWE'@, 'BILANSOWE'@,'DODATKOWE'@)
   || opcja:=FUN.choice('Wybierz rodzaj zestawienia zmian'@,,'PODATKOWE'@, 'BILANSOWE'@)
   ?};
   {? opcja<>0
   || {? opcja=1 || opcja:='P' |? opcja=2 || opcja:='F' |? opcja=3 || opcja:='D' ?};
      _exit:=0
   || _exit:=1
   ?};

   _exit
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_ztabelzm.rpm
Utworzony  : 2016-11-15
Zawartość  : Tabela roczna zmian stanu środka
==================================================
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("form(c[1],,2,' ,')",18,'Wartość#na początek okresu',1,'#');
      PRN.add("form(c[2],,2,' ,')",18,'Wartość#ze zwiększenia',1,'#');
      PRN.add("form(c[3],,2,' ,')",23,'Wartość#skreślonego z ewidencji',1,'#');
      PRN.add("form(c[4],,2,' ,')",18,'Wartość#na koniec okresu',1,'#');
      PRN.add("form(c[5],,2,' ,')",18,'Umorzenie#na początek okresu',1,'#');
      PRN.add("form(c[6],,2,' ,')",18,'Umorzenie#w ciągu roku',1,'#');
      PRN.add("form(c[7],,2,' ,')",18,'Umorzenie#ze zwiększenia',1,'#');
      PRN.add("form(c[8],,2,' ,')",23,'Umorzenie#skreślonego z ewidencji',1,'#');
      PRN.add("form(c[9],,2,' ,')",18,'Umorzenie#na koniec okresu',1,'#');

      PAR_WYDR.TITLE:='Tabela roczna zmian stanu środka: '+SRST.NRI+'\n'
                      'od '+~(zmtabpoc$6) + ' roku   do '+~(SSTALE.AO().KON$6) + ' roku';
      ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{<1 'Dane środka:'}
{COMMENT} ---- Arkusz, Jedn. ks., Jedn. org., Pomieszczenie ---- {COMMENT-}
{
  par1:='J. księgowa.: '+SRST.ODD().OD;
  par2:='J. organizacyjna: '+SRST.JORG().SYMBOL;
  par3:='Pomieszczenie:'+SRST.POM().S;
  par4:='Tor amortyzacji: '+{? opcja='P' || 'podatkowy' |? opcja='F' || 'bilansowy' |? opcja='D' || 'dodatkowy' ?}
;~~}
{IF: SRST.ODD().OD<>''}{<1 par1}{FI}
{IF: SRST.JORG().SYMBOL<>''}{<1 par2}{FI}
{IF: SRST.POM().S<>''}{<1 par3}{FI}
{<1 par4}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{COMMENT} ---- wiersz z danymi ---- {COMMENT-}
{
   _od:=zmtabpoc;
   _do:=SSTALE.AO().KON;

   {? SRSR.DE<_od
   || wspocz:=SRD.getValue(SRSR.ref(),SSTALE.AO().RES,1,opcja,'W');
      uspocz:=SRD.getValue(SRSR.ref(),SSTALE.AO().RES,1,opcja,'U')
   |? SRSR.DE<=_do
   || wszwi:=SRD.getValue(SRSR.ref(),SSTALE.AO().RES,1,opcja,'W');
      uszwi:=SRD.getValue(SRSR.ref(),SSTALE.AO().RES,1,opcja,'U')
   ?};

   SRDO.cntx_psh();
   SRDO.index('PODAT');
   SRDO.prefix(SRSR.ref(),SSTALE.AO().RES,'W');
   {? SRDO.first()
   || {! |?
         {? SRDO.DW<=_do
         || {? SRDO.Z='T'
            || wsskr+={? opcja='P'|| SRDO.WARP |? opcja='F' || SRDO.WARF |? opcja='D' || SRDO.WARD ?};
               usskr+={? opcja='P'|| SRDO.UMOP+exec('sum_ulga','fst',SRDO.SRSR) |? opcja='F' || SRDO.UMOF |? opcja='D' || SRDO.UMOD ?}
            || wszwi+={? opcja='P' || SRDO.WARP |? opcja='F' || SRDO.WARF |? opcja='D' || SRDO.WARD ?};
               uszwi+={? opcja='P' || SRDO.UMOP+exec('sum_ulga','fst',SRDO.SRSR) |? opcja='F' || SRDO.UMOF |? opcja='D' || SRDO.UMOD ?}
            ?}
         ?};
         SRDO.next()
      !}
   ?};
   SRDO.cntx_pop();

   {! _i:=1..SSTALE.AO().OES |! uswc+=SRD.getValue(SRSR.ref(),SSTALE.AO().RES,_i,opcja,'A') !};

   wskon:=wspocz + wszwi + wsskr;
   uskon:=uspocz + uswc + uszwi + usskr;

   wspocz:=wspocz$FINFO.DOKL; uspocz:=uspocz$FINFO.DOKL;
   wszwi:=wszwi$FINFO.DOKL; uszwi:=uszwi$FINFO.DOKL;
   wsskr:=wsskr$FINFO.DOKL; usskr:=usskr$FINFO.DOKL;
   wskon:=wskon$FINFO.DOKL; uskon:=uskon$FINFO.DOKL;
   uswc:=uswc$FINFO.DOKL;

   c[1]:=wspocz;
   c[2]:=wszwi;
   c[3]:=wsskr;
   c[4]:=wskon;
   c[5]:=uspocz;
   c[6]:=uswc;
   c[7]:=uszwi;
   c[8]:=usskr;
   c[9]:=uskon

;~~}
{IF: lineleft() <=4}{PAGE}{FI}
{SUBSTITUTE: 'LINIA0'}
{rsep:=PAR_WYDR.RSEP;~~}
{COMMENT}------------------------------ZAKONCZENiE-----------------------{COMMENT-}
:!UTF-8
{TITLE:1. Limity zlecenia}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   ZLIM.cntx_psh();
   zlec:=0;
   {? VAR.A_ZLEC().RODZAJ='P'
   || zlec:=0
   || zlec:=1
   ?};
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=1;
   dalej:=0;
   ramka:='│';
   FAZY:=tab_tmp(1,'KOD','STRING[10]','Nazwa Fazy','NR','INTEGER','Ref do fazy');
   fazy:=FAZY.ndx_tmp(,1,'KOD','',0,'KOD',,0);
   FAZY.index(fazy);
   f:=0;
   temp:='';
   PRN:=obj_new(@.CLASS.PRINT,12);
   PRN.sl_frame();
   prn1:='PRN';
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   "---Zaakceptowane, czy wszystkie.";
   akc_only:=0;
   "---Tymczasowy indeks pomocniczy:";
   "---Gdy tylko zaakceptowane, to: ZLEC+KOR+AKC+NR";
   "---Gdy wszystkie, to: ZLEC+KOR+NR";
   "---Zawiera ilosc z korekt dla biezacej pozycji.";
   il_kor:=0;
   "---REFERENCE zlecenia.";
   zl_ref:=null();
   etyk:='';
   kor:='W';
   "---Numer limitu";
   lim_nr:=0
}
{END:
   ZLIM.cntx_pop();
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   &dalej;
   &temp;
   obj_del(PRN);
   FAZY.ndx_drop(fazy);
   obj_del(FAZY);
   &f;
   &kor;
   &etyk;
   &prn1;
   &zlec;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   '--------------------------';
   {? var_pres('ndx_tmp')>0
   || ZLIM.ndx_drop(ndx_tmp);
      &ndx_tmp
   ?};
   &il_kor;
   &zl_ref;
   &lim_nr;
   &akc_only
}
{DEFINEFONT: 'kod=TexGyreCursor,160,x,8'}
{COMMENT}
  Wydruk limitow zlecenia.
  [SS] - 2000/09/06 - [7.22]
  [TS] - 2001/10/10 - [7.53] OWwSK003_Aneks1
  [MKO] - 2001/10/12 - [7.53] warianty wydruku - Uwaga 72
  [MKO] - 2001/11/21 - [7.53] OWwPR013/5.1
  [TS] [8.60] usuniecie pola 'Limit wartosciowy'
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Definicje -> Zapotrzebowania - limity -> Drukuj
{COMMENT-}
{ _wydr_naz:='zlim_001';
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.KOR:=WYDRUKIL.SORTUJ;
     WYDRUK.AKC:=WYDRUKIL.SR;
     WYDRUK.FAZY:=WYDRUKIL.FAK;
     WYDRUK.KODY:=WYDRUKIL.RODZFAK
  ?};
  WYDRUK.win_edit('ZLIM_001');~~}
{IF: WYDRUK.edit()}
{  WYDRUKIL.SORTUJ:=WYDRUK.KOR;
   WYDRUKIL.SR:=WYDRUK.AKC;
   WYDRUKIL.FAK:=WYDRUK.FAZY;
   WYDRUKIL.RODZFAK:=WYDRUK.KODY;
   WYDRUKIL.put(1);
   akc_only:=(WYDRUK.AKC='T');
   f:=(WYDRUK.FAZY='T');
   kor:=WYDRUK.KOR;
   etyk:=WYDRUK.KODY;
   {? akc_only
   || {? zlec=0
      || {? f
         || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'LIMIT',,0,'KOR','',0,'AKC','',0,'PFAZ','KOD',0,'PFAZ','KOD',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
         || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'LIMIT',,0,'KOR','',0,'AKC','',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
         ?}
      || {? f
         || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLDOD','',0,'LIMIT',,0,'KOR','',0,'AKC','',0,'PFAZ','KOD',0,'PFAZ','KOD',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
         || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLDOD','',0,'LIMIT',,0,'KOR','',0,'AKC','',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
         ?}
      ?}
   || {? zlec=0
      || {? f
         || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'LIMIT',,0,'KOR','',0,'PFAZ','KOD',0,'PFAZ','KOD',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
         || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'LIMIT',,0,'KOR','',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
         ?}
      || {? f
         || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLDOD','',0,'LIMIT',,0,'KOR','',0,'PFAZ','KOD',0,'PFAZ','KOD',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
         || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLDOD','',0,'LIMIT',,0,'KOR','',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
         ?}
      ?}
   ?};
~~}
{ELSE}
{EXIT}
{FI}
{MACRO: 'miscelaneous'}
  {COMMENT}---Query.------------------------------------------------{COMMENT-}
{MACRO-}
 {MACRO: 'utw_fazy'}
{  ZLIM.clear();
   {? VAR.A_ZLEC().RODZAJ='P'
   || ZLIM.index('ZN')
   || ZLIM.index('ZPN')
   ?};
   ZLIM.prefix(VAR.A_ZLEC,'T');
   {? ZLIM.first()
   || {!
      |? FAZY.prefix(ZLIM.PFAZ().KOD,ZLIM.PFAZ().KOD);
         {? ~FAZY.first()
         || FAZY.KOD:=ZLIM.PFAZ().KOD;
            FAZY.NR:=#ZLIM.PFAZ;
            FAZY.add()
         ?};
         ZLIM.next()
      !}
   ?};
~~}
{MACRO-}
{COMMENT}--Definicja wiersza wydruku pozycji anzlizy zlecenia ANZP.---{COMMENT-}
{SUBSTITUTE: 'miscelaneous'}
{IF: f=1}
{SUBSTITUTE: 'utw_fazy'}
{FI}
{  {? etyk='T'
   || PRN.add("''",50,'Kod kreskowy'@,1)
   ?};
   PRN.add("ZLIM.KTM().KTM",25,'Indeks'@);
   PRN.add("ZLIM.KTM().N",40,'Nazwa'@);
   PRN.add("ZLIM.KTM().J().KOD",10,'jm'@);
   PRN.add("ZLIM.ZGP().NRZLP().NRPRZ+' poz. '+$ZGP.NRP",30,'Przewodnik'@);
   PRN.add("form({? ZLIM.SO='O' || -1 || 1 ?}*ZLIM.LIL,,4)",15,'Limit ilościowy'@,1);
   {? kor='W' || PRN.add("form({? ZLIM.SO='O' || -1 || 1 ?}*il_kor,,4)",15,'Limit ilościowy po korekcie'@,1)?};
   {? f=0 || PRN.add("ZLIM.PFAZ().KOD",10,'Faza produkcji'@) ?};
   PAR_WYDR.TITLE:='LIMITY ZLECENIA %1'@[ZL.SYM]; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
  ---Wpisuje do zmiennej il_kor ilosc
  ---korekt dla biezacego limitu zlecenia. Jezeli biezaca pozycja jest
  ---korekta, to w ww. zmiennych znajda sie wartosci 0.
{COMMENT-}
{MACRO: 'obl_kor'}
{ il_kor:=exec('sum_il','zl_limit',ZLIM.ref(),akc_only); ~~}
{MACRO-}
{MACRO:'stang'}
   {FONT: 'T8G'
   }{SPACE: 'B'
   }{temp:=form(ZLIM.KTM().KODK);~~
   }{<{lmt+1} ramka
   }{<{(lmt+3)}
   }{BARCODE: temp;47;BARCODE_H;BARCODE_TYPE
   }{>{lmt+52}ramka
   }{>{lmt+78}ramka
   }{>{lmt+119}ramka
   }{>{lmt+130}ramka
   }{>{lmt+161}ramka
   }{IF: kor='W' & f=0}{>{lmt+177}ramka}{>{lmt+193}ramka}{>{lmt+204}ramka
   }{FI}{IF: kor='W' & f=1}{>{lmt+177}ramka}{>{lmt+193}ramka
   }{FI}{IF: kor<>'W' & f=0}{>{lmt+177}ramka}{>{lmt+188}ramka
   }{FI}{IF: kor<>'W' & f=1}{>{lmt+177}ramka
   }{FI}
{MACRO-}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Rok obrotowy: %1'@[$ST.AR]}
{<{lmt+1} 'Okres: %1'@[$ST.AM]}{IF: etyk='T'}{<{lmt+70}}{BARCODE: form(ZL.SYM);50;2;BARCODE_TYPE}{FONT: 'T8G'}{SPACE: 'B'}{FI}
{<{lmt+1} 'Opis zlecenia: %1'@[ZLIM.ZLEC().OPIS]}
{<{lmt+1} 'Data otwarcia zlecenia: %1'@[form(ZLIM.ZLEC().OD)]}
{<{lmt+1} {? akc_only || 'Zaakceptowane'@ || 'Zaakceptowane i niezaakceptowane'@ ?}+{? kor='K' || ', '+'bez drukowania korekt'@ || '' ?}}
{IF: f=0}
{FONT: 'T8GB'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FI}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: f=0}
{FONT: 'T8GB'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{FI}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: f=0}
{SUBSTITUTE: 'LINIAF'}
{FI}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{IF: f=0}
{  {? akc_only
   || ZLIM.index(ndx_tmp); ZLIM.prefix(VAR.A_ZLEC,'T',0,'T')
   || ZLIM.index(ndx_tmp); ZLIM.prefix(VAR.A_ZLEC,'T',0)
   ?};
   rsep:=PAR_WYDR.RSEP; ~~}
{IF: ZLIM.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
   {COMMENT}---Oblicz ilosc i wartosc z korekt.---{COMMENT-}
   {SUBSTITUTE: 'obl_kor'}
   {COMMENT}---Wydrukuj pojedynczy wiersz.--------{COMMENT-}
   {SUBSTITUTE: 'LINIA0'}
   {IF:etyk='T'}
     {SUBSTITUTE: 'stang'}
   {FI}
   { dalej:=ZLIM.next(); ~~}
{OD}
{ELSE}
{FOR: FAZY}
{DO}
     {FONT: 'W12'}{SPACE: 'A'}
{     lk:=charleft();~~}
     {PFAZ.seek(FAZY.NR,'pfazypr');~~}
     {<1>{lk} 'FAZA '+ FAZY.KOD+'  -  '+PFAZ.OPIS}
     {FONT: 'T8G'}{SPACE: 'B'}
{  {? akc_only
   || ZLIM.index(ndx_tmp); ZLIM.prefix(VAR.A_ZLEC,'T',0,'T',FAZY.KOD,FAZY.KOD)
   || ZLIM.index(ndx_tmp); ZLIM.prefix(VAR.A_ZLEC,'T',0,FAZY.KOD,FAZY.KOD)
   ?};
   rsep:=PAR_WYDR.RSEP; ~~}
{IF: ZLIM.first()}{ dalej:=1; ~~}
{IF: lineleft<10}
{PAGE}
{FI}
{FONT: 'T8GB'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;il_kor:=0;~~}
{FONT: 'T8G'}{SPACE: 'B'}
{WHILE: dalej}
{DO}
   {COMMENT}---Oblicz ilosc i wartosc z korekt.---{COMMENT-}
   {SUBSTITUTE: 'obl_kor'}
   {COMMENT}---Wydrukuj pojedynczy wiersz.--------{COMMENT-}
   {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {FI}
   {IF:etyk='T'}
      {SUBSTITUTE: 'stang'}
   {FI}
   { dalej:=ZLIM.next(); ~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI}
{OD}
{FI}

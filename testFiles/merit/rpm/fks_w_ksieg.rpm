:!UTF-8
{TITLE:A.  Księgowania}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
          PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN: PRN:=obj_new(@.CLASS.PRINT,20); PRN.sl_frame();
        PRN1:=obj_new(@.CLASS.PRINT,11); PRN1.sl_frame();
        color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=0;
        head:=PAR_WYDR.HEAD; vp:=1; rsep:=PAR_WYDR.RSEP;
        PAR_WYDR.TITLE:={? var_press('SymKon')>0 & SymKon.KONTO<>''
                        || {? SymKon.TYP='P' || 'ZAPISY KSIĘGOWE DLA KONT O PREFIKSIE "'+SymKon.KONTO+'"'
                           |? SymKon.TYP='M' || 'ZAPISY KSIĘGOWE DLA KONT O MASCE "'+SymKon.KONTO+'"'
                           || 'ZAPISY KSIĘGOWE DLA WSZYSTKICH KONT'
                           ?}
                        || 'ZAPISY KSIĘGOWE DLA WSZYSTKICH KONT'
                        ?};
        liczn:=0; sstr6:=sstr7:=sstr8:=sstr9:=0; suma6:=suma7:=suma8:=suma9:=0; wal:=0;
        w:=obj_new(20); ww:=obj_new(10); szer:=obj_new(11);
        wal:={? SSTALE.WAL=FINFO.NAROD || 0 || 1 ?};
        {? PAR_SKID.get(39)='T' || czy_w:=1
        |? PAR_SKID.get(39)='N' || czy_w:=0 || czy_w:=2 ?};
        odd:=OPERATOR.DEPT=null;
        {? var_press('hlp')<=0 || hlp:=exec('obj_hlp','fks_ksr') ?};
        POZ.cntx_psh; DOK.cntx_psh();
        akt_wal:=SSTALE.WAL; sumwnpln:=summapln:=sumwnwal:=summawal:=jbar:=0
}
{END: &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &head; &jbar;
      POZ.cntx_pop; DOK.cntx_pop();
      &liczn; &sstr6; &sstr7; &sstr8; &sstr9; &suma6; &suma7; &suma8; &suma9;
      &wal; &czy_w; VAR_DEL.delete('szer','w','ww','PRN','PRN1'); &odd; &hlp;
      SSTALE.WAL:=akt_wal; &akt_wal; &sumwnpln; &summapln; &sumwnwal; &summawal
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: w_ksieg.rpm
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Wydruk ksiegowan
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_export()}{SUBSTITUTE: 'FOOT'}{SPACE: 'C'}{FI}
{  {? czy_w=2
   || czy_w:=FUN.ask('Prezentować dane związane z walutą obcą?')
   ?};
   szer[1]:=11+odd*6; szer[2]:=10; szer[3]:=35; szer[4]:=szer[5]:=szer[7]:=szer[8]:=14;
   szer[6]:=8; szer[9]:=50; szer[10]:=10; szer[11]:=48+{? ~czy_w || szer[6]+szer[7]+szer[8]+3 ?}-odd*6;
   {? rep_export()
   || PRN.add("w[1]", 9,'Rejestr'@);
      {? odd || PRN.add("w[12]",15,'Jedn. księgowa'@) ?};
      PRN.add("w[13]",9,'Nr',1);
      PRN.add("w[14]",9,'Poz.'@,1);
      PRN.add("w[2]", szer[2],'Data operacji'@);
      PRN.add("w[15]",szer[2],'Data zapisu'@);
      PRN.add("w[3]",szer[3],'Konto');
      PRN.add("w[4]",szer[4],'Wn ('+(3+FINFO.NAROD().KOD)+')',1);
      PRN.add("w[5]",szer[5],'Ma ('+(3+FINFO.NAROD().KOD)+')',1);
      {? czy_w
      || PRN.add("w[6]",szer[6],'Waluta'@);
         PRN.add("w[16]",szer[6],'Kurs'@,1);
         PRN.add("w[7]",szer[7],'Wn '+{? ~wal ||'(waluta)'||'('+(3+SSTALE.WAL().KOD)+')'?},1);
         PRN.add("w[8]",szer[8],'Ma '+{? ~wal ||'(waluta)'||'('+(3+SSTALE.WAL().KOD)+')'?},1)
      ?};
      PRN.add("w[9]",5,'Typ'@);
      PRN.add("w[17]",szer[9],'Symbol rozrachunku'@);
      PRN.add("w[10]",szer[10],'Data otw. rozrachunku'@);
      PRN.add("w[18]",szer[10],'Termin płatności'@);
      PRN.add("w[11]",szer[11],'Opis'@)
   ||
      PRN.add("w[1]",szer[1],'Rejestr'+{? odd || '/Jedn. ks.' || '' ?}+'#nr/ pozycja',,'#');
      PRN.add("w[2]",szer[2],'Data oper.#/zapisu',,'#');  PRN.add("w[3]",szer[3],'Konto');
      PRN.add("w[4]",szer[4],'Wn ('+(3+FINFO.NAROD().KOD)+')',1);
      PRN.add("w[5]",szer[5],'Ma ('+(3+FINFO.NAROD().KOD)+')',1);
      {? czy_w
      || PRN.add("w[6]",szer[6],'Waluta/ kurs');
         PRN.add("w[7]",szer[7],'Wn '+{? ~wal ||'(waluta)'||'('+(3+SSTALE.WAL().KOD)+')'?},1);
         PRN.add("w[8]",szer[8],'Ma '+{? ~wal ||'(waluta)'||'('+(3+SSTALE.WAL().KOD)+')'?},1)
      ?};
      PRN.add("w[9]",szer[9],'Typ/ symbol rozr.'); PRN.add("w[10]",szer[10],'Data otw./ t. płatn.');
      PRN.add("w[11]",szer[11],'Opis');
      PRN1.add("ww[1]",szer[1]+szer[2]+szer[3]+2,'');
      PRN1.add("ww[2]",szer[4],'',1);  PRN1.add("ww[3]",szer[5],'',1);
      {? czy_w
      || PRN1.add("ww[4]",szer[6],''); PRN1.add("ww[5]",szer[7],'',1);  PRN1.add("ww[6]",szer[8],'',1)
      ?};
      PRN1.add("ww[7]",szer[9]+szer[10]+szer[11]+2,'',1); ww[1]:=ww[4]:=ww[5]:=ww[6]:=ww[7]:=''
   ?};~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}{ prn1:=prn2:='PRN';~~}
{SUBSTITUTE: 'HEAD'}{FONT: 'T8G'}{SPACE: 'C'}
{IF: page()=1}
{IF: rep_export()}
{'Parametry wydruku:'}
{'Jednostka księgowa: '+{? OPERATOR.DEPT || OPERATOR.DEPT().OD || 'wszystkie jednostki księgowe' ?}}
{'Waluta: '+SSTALE.WAL().KOD}
{'Okres obrachunkowy: '+hlp.okres()}
{IF: hlp.aget<>2}
{'Rodzaj okresu: '+{? SSTALE.AO().ZAM='T' || 'zamknięty' || 'otwarty' ?}}
{FI}
{{? hlp.aget=1 || 'Tylko zaznaczone wiersze ('+$TT_AGET.size()+')' || 'Wszystkie wiersze' ?} }
{LINE: 1}
{ELSE}
{<3 'Parametry wydruku:'}
{<6 'Jednostka księgowa: '+{? OPERATOR.DEPT || OPERATOR.DEPT().OD || 'wszystkie jednostki księgowe' ?}}
{<6 'Waluta: '+SSTALE.WAL().KOD}
{<6 'Okres obrachunkowy: '+hlp.okres()}
{IF: hlp.aget<>2}
{<6 'Rodzaj okresu: '+{? SSTALE.AO().ZAM='T' || 'zamknięty' || 'otwarty' ?}}
{FI}
{<6 {? hlp.aget=1 || 'Tylko zaznaczone wiersze ('+$TT_AGET.size()+')' || 'Wszystkie wiersze' ?} }
{FI}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8GB'}{SPACE: 'C'}{prn2:='PRN1';~~}
{ ww[1]:='SUMA STRONY';ww[2]:=form(sstr6,,2,' ,'); ww[3]:=form(sstr7,,2,' ,'); {? wal || ww[5]:=form(sstr8,,2,' ,'); ww[6]:=form(sstr9,,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA02'}
{IF: page>1}{ww[1]:='Z PRZENIESIENIA';ww[2]:=form(suma6-sstr6,,2,' ,'); ww[3]:=form(suma7-sstr7,,2,' ,'); {?wal||ww[5]:=form(suma8-sstr8,,2,' ,'); ww[6]:=form(suma9-sstr9,,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{FI}{ ww[1]:='DO PRZENIESIENIA'; ww[2]:=form(suma6,,2,' ,'); ww[3]:=form(suma7,,2,' ,'); {? wal || ww[5]:=form(suma8,,2,' ,'); ww[6]:=form(suma9,,2,' ,')?};~~}
{SUBSTITUTE: 'LINIA1'}{prn1:='PRN1';~~}
{SUBSTITUTE: 'LINIAF'}{ sstr6:=sstr7:=sstr8:=sstr9:=0; vp:=1; prn1:=prn2:='PRN';~~}
{FOOTER-}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{  ll:=lineleft(); ~~}
{IF:  PRN1.ini_line(); ll<(rsep+ PRN1.length()) & ll<>0}{PAGE}{FI}
{IF: vp}
 { PRN1.setcolor(color); PRN.setcolor(color);~~}
{IF: jbar}{REP: PRN.fjbar(PRN1)}{ELSE}{REP: PRN1.fbar(lmt)}{FI}
 { PRN1.setcolor('255:255:255'); PRN.setcolor('255:255:255'); vp:=0; ~~}
{ELIF: rsep}
{IF: jbar}{REP: PRN.fjbar(PRN1)}{ELSE}{REP: PRN1.fbar(lmt)}{FI}
{FI}
{WHILE: PRN1.next()}
{DO}
   {REP: PRN1.fline(lmt)}
{OD}
{MACRO-}
{COMMENT}----------------------WYDRUK-------------------------------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
    { liczn:=hlp.first();  ~~}
    {IF: rep_export()}
    {WHILE: liczn}
    {DO}
      {IF: hlp.seek()}
        {
           w[1]:=POZ.DOK().REJ().KOD;
           {? odd
           || w[12]:=(szer[1]-+REJ.ODD().OD-+REJ.KOD)*' '+ODD.OD
           || w[12]:=''
           ?};
           w[13]:=$POZ.DOK().NR+(6-(+$POZ.DOK().NR))*' ';
           w[14]:=form(POZ.POZ,5+6*odd,,'99');
           w[2]:=POZ.DOK().DOP$1;
           w[15]:=POZ.DOK().DTW$1;
           w[3]:=POZ.KON;
           {? -POZ.STR='wn'
           || w[4]:=form(POZ.SUM,,2,' ,');  w[5]:=form(0,,2,' ,')
           || w[5]:=form(POZ.SUM,,2,' ,');  w[4]:=form(0,,2,' ,')
           ?};
           {? (~wal & POZ.WAL<>null) | wal
           || w[6]:=(3+POZ.WAL().KOD);
              {? -POZ.STR='wn'
              || w[7]:=form(POZ.SUMW,,2,' ,'); w[8]:=form(0,,2,' ,')
              || w[8]:=form(POZ.SUMW,,2,' ,'); w[7]:=form(0,,2,' ,')
              ?}
           || w[6]:=w[16]:=w[7]:=w[8]:=''
           ?};
           w[9]:=POZ.TID;
           w[11]:=POZ.OP;
           {? POZ.TID<>''  ||  w[10]:=POZ.DO$1 || w[10]:='' ?};
           ~~
        }
            { w[7]:=w[8]:=''; {? POZ.ID<>''  ||  w[18]:=POZ.TP$1 || w[18]:=''?};
              {? (~wal & POZ.WAL<>null) | wal
              || w[16]:=form(POZ.KURS,8,4,' ,')
              ?};
              w[17]:=POZ.ID; ~~}
         {SUBSTITUTE: 'LINIA'}{ vp:=0; {? lineleft()=0 || vp:=1 ?};~~}
      {FI}
      { liczn:=hlp.next();  ~~}
   {OD}
   {ELSE}
   {WHILE: liczn}
   {DO}
      {IF: hlp.seek()}
         {IF: lineleft()<>0 & lineleft()<3}{PAGE}{FI}
         { w[1]:=POZ.DOK().REJ().KOD+{?odd||(szer[1]-+REJ.ODD().OD-+REJ.KOD)*' '+ODD.OD||''?};
           w[2]:=POZ.DOK().DOP$1;  w[3]:=POZ.KON;
           {? -POZ.STR='wn'
           || w[4]:=form(POZ.SUM,,2,' ,');  w[5]:=form(0,,2,' ,')
           || w[5]:=form(POZ.SUM,,2,' ,');  w[4]:=form(0,,2,' ,')
           ?};
           {? (~wal & POZ.WAL<>null) | wal
           || w[6]:=(3+POZ.WAL().KOD);
              {? -POZ.STR='wn'
              || w[7]:=form(POZ.SUMW,,2,' ,'); w[8]:=form(0,,2,' ,')
              || w[8]:=form(POZ.SUMW,,2,' ,'); w[7]:=form(0,,2,' ,')
              ?}
           || w[6]:=w[7]:=w[8]:=''
           ?};
           w[9]:=POZ.TID; STR.split(POZ.OP);w[11]:=STR.line(szer[11]);
           {? POZ.TID<>''  ||  w[10]:=POZ.DO$1 || w[10]:='' ?};
           ~~
         }
         {SUBSTITUTE: 'LINIA0'}{ vp:=0; {? lineleft()=0 || vp:=1 ?};~~}
            { w[2]:=w[3]:=w[4]:=w[5]:=w[7]:=w[8]:=''; {? POZ.ID<>''  ||  w[10]:=POZ.TP$1 || w[10]:=''?};
              {? (~wal & POZ.WAL<>null) | wal || w[6]:=form(POZ.KURS,8,4,' ,') ?};  w[9]:=POZ.ID;
              w[1]:=$POZ.DOK().NR+(6-(+$POZ.DOK().NR))*' '+form(POZ.POZ,5+6*odd,,'99');
              w[2]:=POZ.DOK().DTW$1;
              w[11]:=STR.line(szer[11]);
              {? -POZ.STR='wn'
              || sstr6+=POZ.SUM;  suma6+=POZ.SUM; {? wal || sstr8+=POZ.SUMW; suma8+=POZ.SUMW ?}
              || sstr7+=POZ.SUM;  suma7+=POZ.SUM; {? wal || sstr9+=POZ.SUMW; suma9+=POZ.SUMW ?}
              ?};~~}
         {SUBSTITUTE: 'LINIA'}{ vp:=0; {? lineleft()=0 || vp:=1 ?};~~}
      {FI}
      { liczn:=hlp.next();  ~~}
  {OD}
{FI}
{IF: ~rep_export()}
{IF: lineleft()<>0 & page>1 & lineleft()<5}{PAGE}{FI}
{FOOTER}{FOOTER-}
{ENTIRE}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: page>1}
{ ww[1]:='Z PRZENIESIENIA'; ww[2]:=form(suma6-sstr6,,2,' ,'); ww[3]:=form(suma7-sstr7,,2,' ,');
  {? wal || ww[5]:=form(suma8-sstr8,,2,' ,'); ww[6]:=form(suma9-sstr9,,2,' ,')?}; jbar:=1;~~}
{SUBSTITUTE: 'LINIA1'}
{ ww[1]:='SUMA STRONY'; ww[2]:=form(sstr6,,2,' ,'); ww[3]:=form(sstr7,,2,' ,');
  {? wal || ww[5]:=form(sstr8,,2,' ,'); ww[6]:=form(sstr9,,2,' ,')?}; jbar:=0;~~}
{SUBSTITUTE: 'LINIA1'}
{ ww[1]:='OGÓŁEM'; ww[2]:=form(suma6,,2,' ,'); ww[3]:=form(suma7,,2,' ,');
  {? wal || ww[5]:=form(suma8,,2,' ,'); ww[6]:=form(suma9,,2,' ,') ?};~~}
{SUBSTITUTE: 'LINIA1'}
{ELSE}
{ ww[1]:='OGÓŁEM'; ww[2]:=form(suma6,,2,' ,');ww[3]:=form(suma7,,2,' ,');
  {? wal || ww[5]:=form(suma8,,2,' ,'); ww[6]:=form(suma9,,2,' ,')?}; jbar:=1;~~}
{SUBSTITUTE: 'LINIA1'}
{FI}
{ {? var_pres('zakladka')>0 & zakladka=10 || _okres:=ROZNE.UT_OKRDO().NR || _okres:=SSTALE.AO().NR ?}; _rok:=OKRO_F.ROK().NAZ;
  SSTALE.WAL:=FINFO.NAROD; F.ODDZIAL:=OPERATOR.DEPT; sumwnpln:=F.Kw(SymKon.KONTO,0,_okres); summapln:=F.Km(SymKon.KONTO,0,_okres);
  ww[1]:='OBROTY NARASTAJĄCO DO '+$_okres+'/'+_rok; ww[2]:=form(sumwnpln,,2,' ,'); ww[3]:=form(summapln,,2,' ,');
  {? wal
  || SSTALE.WAL:=akt_wal; sumwnwal:=F.Kw(SymKon.KONTO,0,_okres); summawal:=F.Km(SymKon.KONTO,0,_okres);
     ww[5]:=form(sumwnwal,,2,' ,'); ww[6]:=form(summawal,,2,' ,')
  ?}; jbar:=0;~~}
{SUBSTITUTE: 'LINIA1'}
{ ww[1]:='SALDO'; ww[2]:=form(F.SWn(sumwnpln,summapln),,2,' ,'); ww[3]:=form(F.SMa(sumwnpln,summapln),,2,' ,');
  {? wal || ww[5]:=form(F.SWn(sumwnwal,summawal),,2,' ,'); ww[6]:=form(F.SMa(sumwnwal,summawal),,2,' ,')?}; prn2:='PRN1';~~}
{SUBSTITUTE: 'LINIA1'}
{PRN1.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'}
{ENTIRE-}
{FI}
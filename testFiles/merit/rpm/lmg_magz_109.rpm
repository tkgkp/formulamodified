:!UTF-8
{TITLE:I. Szczegółowe zestawienie obrotów na kontach}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_109:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('tmp1','tmp2');
      VAR_DEL.delete('k','w','s','sg','PRN');
      VAR_DEL.delete('wyjscie','X_Xa','X_Xb')";
   mag_109();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   tmp1:=tmp2:='';

   VAR_DEL.delete('k','w','s','sg','PRN');
   k:=obj_new(6); "--szerokosci kolumn--";
   w:=obj_new(6); "--wartosci kolumn--";
   s:=0; "--razem--";
   sg:=0; "--razem grupa--";
   PRN:= obj_new(@.CLASS.PRINT,6); PRN.s_frame();
   k[1]:=20;
   k[2]:=9;
   k[3]:=5;
   k[4]:=10;
   k[5]:=15;
   k[6]:=30;
   w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:='';
   tryb:=rep_export();

   wyjscie:=1;

   _od:=date(ST.AR,ST.AM,1);
   _do:=date(ST.AR,ST.AM,0);

   _wydr_naz:='mag_109';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ||
      _od:=WYDRUKIL.OD_DATY;
      _do:=WYDRUKIL.DO_DATY
   ?};

   D_HELP.OD:=_od;
   D_HELP.DO:=_do;
   D_HELP.win_edit('ODDO');
   {? ST.MAG().IL='T'
   || FUN.info('Zestawienie niedostępne w magazynie z ewidencją wyłącznie ilościową.'@)
   |? exec('upr_cm','ceny')=0
   || FUN.info('Brak uprawnień do wartości magazynowych.'@)
   |? D_HELP.edit("
         {? D_HELP.DO>=D_HELP.OD
         || ''
         || FUN.info('Błędny zakres dat.'@); 'DO'
         ?}")
   ||
      WYDRUKIL.index('WYDRUKI');
      WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
      {? WYDRUKIL.first()
      ||
         WYDRUKIL.OD_DATY:=D_HELP.OD;
         WYDRUKIL.DO_DATY:=D_HELP.DO;
         WYDRUKIL.put();
         wyjscie:=0
      ?}
   ?}
}
{END:
   mag_109(); VAR_DEL.delete('mag_109')
}
{IF: exec('upr_cm','ceny')=0}{ FUN.info('Brak uprawnień do wartości magazynowych.'@);~~}{EXIT}{FI}
{IF: wyjscie=1}{EXIT}{FI}
{
PRN.add("w[1]",k[1],'Konto'@);
PRN.add("w[2]",k[2],'Typ dokumentu'@);
PRN.add("w[3]",k[3],'Nr'@,1);
PRN.add("w[6]",k[6],'Symbol'@);
PRN.add("w[4]",k[4],'Data'@);
PRN.add("w[5]",k[5],'Wartość'@,1);

"--obliczenia--";
VAR_DEL.delete('X_Xa','X_Xb');
X_Xa:=sql('
   select
      case when KK2.SYM is null then KK1.SYM else KK2.SYM end KONTO,
      TYPYDOK.T TYP, ND.NR NR, ND.SYM SYM, ND.D D,
      case when DK.PLUS=\'T\' then DK.WAR else -DK.WAR end W
   from @DK
      join @ND using(DK.N,ND.REFERENCE)
      join TYPYDOK using(ND.TYP,TYPYDOK.REFERENCE)
      left join KK KK1 using(ND.KK,KK1.REFERENCE)
      left join KK KK2 using(DK.KK,KK2.REFERENCE)
   where
      ND.Z=\'T\'
      and ND.MAG=:_e
      and ND.D>=to_date(:_a) and ND.D<=to_date(:_b)
      and ND.REFERENCE between \'nagdo:_c\' and \'nagdo:_dz\'
      and DK.REFERENCE between \'dokma:_c\' and \'dokma:_dz\''
    ,D_HELP.OD,D_HELP.DO,ST.ODDZ+($(D_HELP.OD~1)+2),ST.ODDZ+($(D_HELP.DO~1)+2),ST.MAG);
X_Xb:=sql('
   select KONTO KONTO, TYP TYP, NR NR, SYM SYM, D D, sum(W) W
   from :_a
   group by KONTO, TYP, NR, SYM, D
   order by KONTO'
   ,X_Xa);

PAR_WYDR.TITLE:='SZCZEGÓŁOWE ZESTAWIENIE OBROTÓW NA KONTACH MAGAZYNOWYCH'@;
prn1:='PRN'
;~~}
{IF: X_Xb.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{IF: ODDZ.cntx_psh();ODDZ.index('KOD2');ODDZ.prefix();_wyn:=ODDZ.find_key(ST.ODDZ,ST.ODDZ);ODDZ.cntx_pop();_wyn}
{<{lmt+1}}{'Oddział: %1 - %2'@[(ODDZ.KOD),(ODDZ.NAZ)]}
{FI}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Za okres: %1 - %2'@[(D_HELP.OD$1),(D_HELP.DO$1)]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}
   {LINE}
   {<{lmt+1}}{'Parametry wydruku:'@}
   {IF: ODDZ.cntx_psh();ODDZ.index('KOD2');ODDZ.prefix();_wyn:=ODDZ.find_key(ST.ODDZ,ST.ODDZ);ODDZ.cntx_pop();_wyn}
   {<{lmt+1}}{'Oddział: %1 - %2'@[(ODDZ.KOD),(ODDZ.NAZ)]}
   {FI}
   {<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
   {<{lmt+1}}{'Za okres: %1 - %2'@[(D_HELP.OD$1),(D_HELP.DO$1)]}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xb}
{DO}
   {FIRST}{ kolejny:=0;~~}{FIRST-}
   {IF: tryb<>1 & kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {
    w[1]:=form(X_Xb.KONTO);
    w[2]:=form(X_Xb.TYP);
    w[3]:=form(X_Xb.NR,,,'99');
    w[4]:=form(X_Xb.D);
    w[5]:=form(X_Xb.W,,2,'.,');
    w[6]:=form(X_Xb.SYM);
    s+=X_Xb.W;
    sg+=X_Xb.W;~~}
   {SUBSTITUTE: 'LINIA0'}
   { kolejny:=0; rsep:=PAR_WYDR.RSEP;~~}
   {TOTAL: X_Xb.KONTO}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
      {IF: tryb<>1}
      {
      w[1]:=form(X_Xb.KONTO);
      w[2]:=form(X_Xb.TYP);
      w[3]:=form(X_Xb.NR);
      w[4]:=form(X_Xb.D);
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+3
      ;~~}
      {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {
      kolejny:=1; rsep:=0;
      PRN.n_frame();
      w[1]:=w[2]:=w[3]:=w[4]:='';
      w[6]:={? grp_fld()='' || 'Razem zapisy bez konta:'@ || 'Razem %1:'@[grp_fld()] ?}; PRN.FORM[4]:=0;
      w[5]:=form(sg,,2,'.,')
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {FONT: 'T8G'}{SPACE: 'C'}
      {
      sg:=0;
      PRN.s_frame();
      PRN.FORM[4]:=1
      ;~~}
      {FI}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{
PRN.n_frame();
w[1]:=w[2]:=w[3]:=w[4]:='';
w[6]:='Razem:'@; PRN.FORM[4]:=0;
w[5]:=form(s,,2,'.,')
;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
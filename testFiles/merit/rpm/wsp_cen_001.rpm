:!UTF-8
{TITLE:A. Cennik}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   cen_001:="
      VAR_DEL.delete('color','prn1','prn2');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');

      VAR_DEL.delete('k','w','dokl','doklw','PRN');
      VAR_DEL.delete('cncb','bzcb');
      VAR_DEL.delete('wydr_naz','wydr_edi');
      VAR_DEL.delete('wyjscie','X_Xd')";
   cen_001();

   k:=obj_new(20); "--szerokosci kolumn--";
   w:=obj_new(20); "--wartosci kolumn--";

   dokl:=3; "--dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";

   PRN:=obj_new(@.CLASS.PRINT,20); PRN.sl_frame();

   k[1]:=5;
   k[2]:=8;
   k[3]:=8;
   k[4]:=30;
   k[5]:=10;
   k[6]:=3;
   k[7]:=9;
   k[8]:=10;
   k[9]:=10;
   k[10]:=10;
   k[11]:=10;
   k[12]:=10;
   k[13]:=10;
   k[14]:=20;
   k[15]:=10;
   k[16]:=10;
   k[17]:=3;
   k[18]:=7;
   k[19]:=10;
   k[20]:=20;

   wydr_naz:='cen_001';
   wydr_edi:='CEN_001';

   cncb:=1;
   bzcb:=1;

"--parametry okreslane przez uzytkownika--";
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OZ.USER,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OZ.USER;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit()
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         cncb:=WYDRUKIL.RADIOB_1;
         bzcb:=WYDRUKIL.RADIOB_2
      ?}
   ?};

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=0;
   rsep:=PAR_WYDR.RSEP
}
{END:
   cen_001(); VAR_DEL.delete('cen_001')
}
{IF: wyjscie}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{
PRN.add("w[1]",k[1],'Lp.'@,1);
PRN.add("w[2]",k[2],'Grupa'@);
PRN.add("w[3]",k[3],'Podgrupa'@);
PRN.add("w[4]",k[4],'Indeks'@);
PRN.add("w[5]",k[5],'Jm'@);
PRN.add("w[6]",k[6],'Waluta'@);
PRN.add("w[7]",k[7],'Sposób płatności'@);
PRN.add("w[8]",k[8],'Ilść od'@,1);
PRN.add("w[9]",k[9],'Ilść do'@,1);
PRN.add("w[10]",k[10],'Wartość od'@,1);
PRN.add("w[11]",k[11],'Wartość do'@,1);
PRN.add("w[12]",k[12],'Ważny od'@);
PRN.add("w[13]",k[13],'Ważny do'@);
PRN.add("w[14]",k[14],'Cennik bazowy'@);
PRN.add("w[15]",k[15],'Cena od'@,1);
PRN.add("w[16]",k[16],'Cena do'@,1);
PRN.add("w[17]",k[17],'BER'@,1);
PRN.add("w[18]",k[18],'Rabat %%'@,1);
PRN.add("w[19]",k[19],'Rabat kwota'@,1);
PRN.add("w[20]",k[20],'Promocja'@,1);

"--obliczenia--";
X_Xd:=tab_tmp(5
   ,'TOP'      ,'INTEGER'     ,'0-poz.nadrz.,1-podrzędna'@
   ,'MAT'      ,'STRING[50]'  ,'Indeks'@
   ,'MGR'      ,'STRING[8]'   ,'Grupa'@
   ,'MGRP'     ,'STRING[8]'   ,'Podrupa'@
   ,'JM'       ,'STRING[10]'  ,'Jm'@
   ,'WAL'      ,'STRING[3]'   ,'Waluta'@
   ,'PL'       ,'STRING[8]'   ,'Sposób płatności'@
   ,'IL'       ,'REAL'        ,'Ilość'@
   ,'IL_DO'    ,'REAL'        ,'Ilość do'@
   ,'WAR'      ,'REAL'        ,'Wartość'@
   ,'WAR_DO'   ,'REAL'        ,'Wartość do'@
   ,'OD'       ,'STRING[20]'  ,'Ważny od'@
   ,'DO'       ,'STRING[20]'  ,'Ważny do'@
   ,'TARB'     ,'STRING[41]'  ,'Cennik bazowy'@
   ,'CEN_ZAKR' ,'STRING[1]'   ,'Zakres cenowy'@
   ,'FO_CEN'   ,'STRING[255]' ,'Cena formuła'@
   ,'C_OD'     ,'REAL'        ,'Cena od'@
   ,'C'        ,'REAL'        ,'Cena do'@
   ,'BER'      ,'STRING[1]'   ,'Blokada edycji rabatu'@
   ,'FO_RAB'   ,'STRING[255]' ,'Rabat formuła'@
   ,'PRC'      ,'REAL'        ,'Rabat %%'@
   ,'RAB'      ,'REAL'        ,'Rabat kwota'@
   ,'PROMO'    ,'STRING[50]'  ,'Promocja'@
   ,'ID'       ,'STRING[16]'  ,'ID poz.nadrzędnej'@
   ,'LP'       ,'INTEGER'     ,'Lp.'@);
X_Xdndx:=X_Xd.ndx_tmp(,,'TOP',,,'ID',,);

TAP.cntx_psh();
TAP.index('TM');
TAP.prefix(DISP.TAR);
DISP.TAR();
_loop:=TAP.first();
{!
|? _loop
|!
   _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
   _mgr:={? TAP.M || TAP.M().MGR || TAP.MGR ?};
   _mgrp:={? TAP.M || TAP.M().MGRP || TAP.MGRP ?};
   {? exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)

   ||
      X_Xd.TOP:=0;
      X_Xd.MAT:=TAP.M().KTM;
      X_Xd.MGR:=TAP.MGR().KOD;
      X_Xd.MGRP:=TAP.MGRP().KOD;
      X_Xd.JM:=TAP.JM().KOD;
      X_Xd.WAL:=TAP.WAL().KOD;
      X_Xd.PL:=TAP.PL().KOD;
      X_Xd.IL:=TAP.IL;
      X_Xd.IL_DO:=TAP.IL_DO;
      X_Xd.WAR:=TAP.WAR;
      X_Xd.WAR_DO:=TAP.WAR_DO;
      X_Xd.OD:=form(TAP.OD)+{? TAZ.SD='S' & TAP.T_OD<>time(0,0,0) || ' '+form(TAP.T_OD) || '' ?};
      X_Xd.DO:={? TAP.DO=date(0,0,0) || '' || form(TAP.DO)+{? TAZ.SD='S' & TAP.T_DO<>time(0,0,0) || ' '+form(TAP.T_DO) || '' ?} ?};
      X_Xd.TARB:=
         {? TAP.TARB=''
         || ''
         || TAR.cntx_psh();
            TAR.prefix();
            _tarb:={? TAR.seek(BB.sqlint(TAP.TARB),) || TAR.KOD+' '+TAR.NAZ || '' ?};
            TAR.cntx_pop();
            _tarb
         ?};
      X_Xd.FO_CEN:=TAP.FO_CEN().FORMULA;
      X_Xd.CEN_ZAKR:=TAP.CEN_ZAKR;
      X_Xd.C_OD:={? cncb=1 || TAP.CEN_OD || TAP.CENB_OD ?};
      X_Xd.C:={? cncb=1 || TAP.CEN || TAP.CENB ?};
      X_Xd.BER:=TAP.RAB_B;
      X_Xd.FO_RAB:=TAP.FO_RAB().FORMULA;
      X_Xd.PRC:=TAP.PRC;
      X_Xd.RAB:={? cncb=1 || TAP.RABNET || TAP.RABBRT ?};
      X_Xd.PROMO:=TAP.PROMO().KOD;
      X_Xd.ID:=$TAP.ref();
      X_Xd.add();
      _id:=X_Xd.ID;
      _cod:=TAP.OD; _cdo:=TAP.DO;
      _ctod:=TAP.T_OD; _ctdo:=TAP.T_DO;
      _cmat:=TAP.M;
      _cmgr:={? TAP.MGR || TAP.MGR || TAP.M().MGR ?};
      _cmgrp:={? TAP.MGRP || TAP.MGRP || TAP.M().MGRP ?};
      _cber:=X_Xd.BER; _cforab:=X_Xd.FO_RAB; _cprc:=X_Xd.PRC; _crab:=X_Xd.RAB;
      _cpromo:=X_Xd.PROMO;
      {? bzcb=2
         & {? cncb=1 || TAP.CEN_OD=0 & TAP.CEN=0 || TAP.CENB_OD=0 & TAP.CENB=0 ?}
         & TAP.TARB<>''
      || TAR.cntx_psh(); TAP.cntx_psh();
         TAP.prefix(BB.sqlint(TAP.TARB),);
         _loop:=TAP.first();
         {? _loop || TAP.TAR() ?};
         {!
         |? _loop
         |!
            _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
            _mgr:={? TAP.M || TAP.M().MGR || TAP.MGR ?};
            _mgrp:={? TAP.M || TAP.M().MGRP || TAP.MGRP ?};
            {? exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)

            ||
               {? _cmgr<>null & (TAP.MGR=_cmgr | TAP.M().MGR=_cmgr)
                  | _cmgrp<>null & (TAP.MGRP=_cmgrp | TAP.M().MGRP=_cmgrp)
                  | _cmat<>null & TAP.M=_cmat
               || X_Xd.TOP:=1;
                  X_Xd.MAT:=TAP.M().KTM;
                  X_Xd.MGR:=TAP.MGR().KOD;
                  X_Xd.MGRP:=TAP.MGRP().KOD;
                  X_Xd.JM:=TAP.JM().KOD;
                  X_Xd.WAL:=TAP.WAL().KOD;
                  X_Xd.PL:=TAP.PL().KOD;
                  X_Xd.IL:=TAP.IL;
                  X_Xd.IL_DO:=TAP.IL_DO;
                  X_Xd.WAR:=TAP.WAR;
                  X_Xd.WAR_DO:=TAP.WAR_DO;
                  X_Xd.OD:=
                     {? TAP.OD>_cod | TAP.OD=_cod & (TAP.T_OD=time(0,0,0) | _ctod<>time(0,0,0) & TAP.T_OD>_ctod)
                     || form(TAP.OD)+{? TAZ.SD='S' & TAP.T_OD<>time(0,0,0) || ' '+form(TAP.T_OD) || '' ?}
                     || form(_cod)+{? TAZ.SD='S' & _ctod<>time(0,0,0) || ' '+form(_ctod) || '' ?}
                     ?};
                  X_Xd.DO:=
                     {? _cdo<TAP.DO | _cdo=TAP.DO & (TAP.T_DO=time(0,0,0) | _ctdo<>time(0,0,0) & _ctdo<TAP.T_DO)
                     || {? TAP.DO=date(0,0,0) || '' || form(TAP.DO)+{? TAZ.SD='S' & TAP.T_DO<>time(0,0,0) || ' '+form(TAP.T_DO) || '' ?} ?}
                     || {? _cdo=date(0,0,0) || '' || form(_cdo)+{? TAZ.SD='S' & _ctdo<>time(0,0,0) || ' '+form(_ctdo) || '' ?} ?}
                     ?};
                  X_Xd.TARB:='';
                  X_Xd.FO_CEN:=TAP.FO_CEN().FORMULA;
                  X_Xd.CEN_ZAKR:=TAP.CEN_ZAKR;
                  X_Xd.C_OD:={? cncb=1 || TAP.CEN_OD || TAP.CENB_OD ?};
                  X_Xd.C:={? cncb=1 || TAP.CEN || TAP.CENB ?};
                  X_Xd.BER:=_cber;
                  X_Xd.FO_RAB:=_cforab;
                  X_Xd.PRC:=_cprc;
                  X_Xd.RAB:=_crab;
                  X_Xd.PROMO:=_cpromo;
                  X_Xd.ID:=_id;
                  X_Xd.add()
               ?}
            ?};
            _loop:=TAP.next()
         !};
         TAR.cntx_pop();TAP.cntx_pop()
      ?}
   ?};
   _loop:=TAP.next()
!};
"--dokladnosc wartosci--";
p_il:=exec('fld_prec','#field',X_Xd,'IL');
p_ildo:=exec('fld_prec','#field',X_Xd,'IL_DO');
p_war:=exec('fld_prec','#field',X_Xd,'WAR');
p_wardo:=exec('fld_prec','#field',X_Xd,'WAR_DO');
p_cod:=exec('fld_prec','#field',X_Xd,'C_OD');
p_c:=exec('fld_prec','#field',X_Xd,'C');
p_prc:=exec('fld_prec','#field',X_Xd,'PRC');
p_rab:=exec('fld_prec','#field',X_Xd,'RAB');
TAP.cntx_pop();

PAR_WYDR.TITLE:='CENNIK '+TAR.KOD+' '+TAR.NAZ;
prn1:='PRN';
~~}
{ {? X_Xd.first()=0 || wyjscie:=1; FUN.info('Brak danych spełniających kryteria.'@) ?};~~}
{EXIT: wyjscie=1}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{IF: cncb=1}{'Cennik netto'@}{ELSE}{'Cennik brutto'@}{FI}
{<{lmt+1}}{IF: bzcb=1}{'Bez cennika bazowego'@}{ELSE}{'Z cennikiem bazowym'@}{FI}
{LINE}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   drukowanie pozycji                                       {COMMENT-}
{IF: bzcb=1}{FONT: 'T8G'}{SPACE: 'C'}{ELSE}{FONT: 'T8GB'}{SPACE: 'C'}{FI}
{FOR: X_Xd}
{PREFIX: 0}
{DO}
   {FIRST}{ rsep:=PAR_WYDR.RSEP; lp:=1;~~}{FIRST-}
   {
   w[1]:=form(lp,,0,'99');
   w[2]:=X_Xd.MGR;
   w[3]:=X_Xd.MGRP;
   w[4]:=X_Xd.MAT;
   w[5]:=X_Xd.JM;
   w[6]:=X_Xd.WAL;
   w[7]:=X_Xd.PL;
   w[8]:={? TAP.IL | TAP.IL_DO || form(TAP.IL,,p_il,'.,') || '' ?};
   w[9]:={? TAP.IL_DO || form(TAP.IL_DO,,p_ildo,'.,') || '' ?};
   w[10]:={? TAP.WAR | TAP.WAR_DO || form(TAP.WAR,,p_war,'.,') || '' ?};
   w[11]:={? TAP.WAR_DO || form(TAP.WAR_DO,,p_wardo,'.,') || '' ?};
   w[12]:=form(X_Xd.OD);
   w[13]:=form(X_Xd.DO);
   w[14]:=X_Xd.TARB;
   w[15]:={? X_Xd.FO_CEN='' || {? X_Xd.CEN_ZAKR='T' || form(X_Xd.C_OD,,p_cod,'.,') || '' ?} || 'Formuła' ?};
   w[16]:={? X_Xd.FO_CEN='' || form(X_Xd.C,,p_c,'.,') || 'Formuła' ?};
   w[17]:=X_Xd.BER;
   w[18]:={? X_Xd.FO_RAB='' || {? X_Xd.PRC || form(X_Xd.PRC,,p_prc,'.,') || '' ?} || 'Formuła' ?};
   w[19]:={? X_Xd.FO_RAB='' || {? X_Xd.RAB || form(X_Xd.RAB,,p_rab,'.,') || '' ?} || 'Formuła' ?};
   w[20]:=X_Xd.PROMO;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
   { X_Xd.cntx_psh();~~}
   {FOR: X_Xd}
   {INDEX: X_Xdndx}
   {PREFIX: 1,X_Xd.ID}
   {DO}
      {FIRST}{FONT: 'T8G'}{SPACE: 'C'}{FIRST-}
      {
      w[1]:=form(lp,,0,'99');
      w[2]:=X_Xd.MGR;
      w[3]:=X_Xd.MGRP;
      w[4]:=X_Xd.MAT;
      w[5]:=X_Xd.JM;
      w[6]:=X_Xd.WAL;
      w[7]:=X_Xd.PL;
      w[8]:={? TAP.IL | TAP.IL_DO || form(TAP.IL,,p_il,'.,') || '' ?};
      w[9]:={? TAP.IL_DO || form(TAP.IL_DO,,p_ildo,'.,') || '' ?};
      w[10]:={? TAP.WAR | TAP.WAR_DO || form(TAP.WAR,,p_war,'.,') || '' ?};
      w[11]:={? TAP.WAR_DO || form(TAP.WAR_DO,,p_wardo,'.,') || '' ?};
      w[12]:=form(X_Xd.OD);
      w[13]:=form(X_Xd.DO);
      w[14]:=X_Xd.TARB;
      w[15]:={? X_Xd.FO_CEN='' || {? X_Xd.CEN_ZAKR='T' || form(X_Xd.C_OD,,p_cod,'.,') || '' ?} || 'Formuła'@ ?};
      w[16]:={? X_Xd.FO_CEN='' || form(X_Xd.C,,p_c,'.,') || 'Formuła'@ ?};
      w[17]:=X_Xd.BER;
      w[18]:={? X_Xd.FO_RAB='' || {? X_Xd.PRC || form(X_Xd.PRC,,p_prc,'.,') || '' ?} || 'Formuła'@ ?};
      w[19]:={? X_Xd.FO_RAB='' || {? X_Xd.RAB || form(X_Xd.RAB,,p_rab,'.,') || '' ?} || 'Formuła'@ ?};
      w[20]:=X_Xd.PROMO;
      ~~}
      {SUBSTITUTE: 'LINIA0'}
      {TOTAL}
         {IF: bzcb=1}{FONT: 'T8G'}{SPACE: 'C'}{ELSE}{FONT: 'T8GB'}{SPACE: 'C'}{FI}
   {OD}
   { X_Xd.cntx_pop(); lp+=1;~~}
   {TOTAL}
      { VAR_DEL.delete('lp');~~}
{OD}
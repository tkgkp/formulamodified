:!UTF-8
{TITLE:Kontrola poprawności informacji podatkowych}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('__prep','__TB','__skul','par','__par1','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__Lp');
   VAR_DEL.delete('__ok','__param');
   tresc:='wkontro1';
   __prep:=__F_ZATR.P;
   PAR_WYDR.TITLE:='Wykaz '@+{? __prep='P' || 'pracowników'@ || 'zleceniobiorców'@ ?}+
                   ' z wpisami o urzędach skarbowych\n'@+
                   'bez wskazanego adresu do celów rozliczeń podatkowych'@;
   OSOBA.cntx_psh();
   P.cntx_psh();
   OSOBA.prefix();
   P.prefix();
   OS_US.cntx_psh();
   __ok:=0;
   _upr:=exec('uprawnieniawrap','pkd','danych osobowych'@,'ZWS_DOS_PPDO','ZWS_DOS_PRDO')+
    exec('uprawnieniawrap','pkd','urzędach skarbowych'@,'PKD_DOS_PPUS','PKD_DOS_PRUS');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień pozwalających na przeglądanie informacji o: %1'@[_upr])
   || __TB:=tab_tmp(2,
         'OSOBA','STRING[51]','Nazwisko, imię',
         'PESEL','STRING[11]','Pesel pracownika/zleceniobiorcy',
         'TECZKA','STRING[11]','Nr teczki',
         'OS_US','INTEGER','Urząd skarbowy - ref',
         'US_NAZ','STRING[52]','Nazwa urzędu skarbowego',
         'US_OD','DATE','US data od',
         'US_DO','DATE','US data do',
         'ERR','INTEGER','Błąd - 1, poprawnie - 0',
         'OPIS','STRING[100]','Opis błędu'
      );
      __par1:=obj_new(@.CLASS.PRINT,3);
      {? rep_export() || __par1.add("__TB.OSOBA+', PESEL: '+__TB.PESEL",40,'Zleceniobiorca'@) ?};
      __par1.add("__TB.US_NAZ",70,'Urząd skarbowy'@);
      __par1.add(
       "  {? __TB.US_OD~1 || __TB.US_OD$2 || ''?}+{? __TB.US_OD~1 & __TB.US_DO~1<>9999 || ' - ' || '' ?}+
          {? __TB.US_DO~1<>9999 || __TB.US_DO$2 || ''?}
       ",28,'Zakres obowiązywania urzędu'@
      );
      __par1.s_frame();

      __param:=exec('par_wydr_ddp','pkd',
          'Kontrola poprawności informacji podatkowych'@,
          "  {? ~(cur_tab().DO~1)
             || FUN.emsg('Data końcowa musi być podana.'@); 'DO'
             |? cur_tab().DO<cur_tab().OD
             || FUN.emsg('Podano niespójne daty.'@); 'DO'
             || {? cur_tab().DO>=date() || cur_tab().DO:=date(9999,12,31) ?};
                1
             ?}
          ",
          date(0,0,0),'Data początkowa'@,,
          date(),'Data końcowa'@,"1"
      );

      {? __param.size()
      || b_rat:=h_rat:=vp:=__ok:=1;
       __Lp:=lmt:=rsep:=0;
       prn1:='__par1';
       params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('__prep','__TB','__skul','par','__par1','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__Lp');
   VAR_DEL.delete('__ok','__param');
   OSOBA.cntx_pop();
   P.cntx_pop();
   OS_US.cntx_pop()
}
{EXIT: ~__ok}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: wkontro1.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: __TB.size()}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania:'@}
   {<{ceil(lmt*h_rat)+1}'Zakres: '@+{? __prep='P' || 'pracownicy'@ || 'zleceniobiorcy'@ ?}+' wskazani przez operatora'@}
   {<{ceil(lmt*h_rat)+1}'%1: '['Kontrolowany zakres czasowy'@]+
                           {? ~(__param.OD~1) & __param.DO>=date()
                           || 'wszystkie'@
                           |? __param.OD~1 & __param.DO>=date()
                           || 'od %1'[__param.OD$6]
                           |? ~(__param.OD~1) & __param.DO<date()
                           || 'do %1'[__param.DO$6]
                           || 'od %1 do %2'[__param.OD$6,__param.DO$6]
                           ?}+'.'}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{FONT: 'T8'}
{SPACE: 'B'}
{  P.OSOBA();
   {? (exec('nieobcy','osoba') | +OSOBA.PESEL) & ~__TB.find_tab(0,'PESEL',,'=',OSOBA.PESEL)
   || __TB.blank();
      __TB.PESEL:=OSOBA.PESEL;
      __TB.OSOBA:=OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE;
      __TB.TECZKA:={? __prep='P' || P.T || OSOBA.PESEL ?};
      OS_US.index('OD');
      OS_US.prefix(OSOBA.ref());
      {? OS_US.first()
      || {!
         |? {? ~OS_US.OS_ADRES
            || _next:=0;
               __TB.US_OD:=__TB.US_DO:=date(0,0,0);
               __TB.OS_US:=#OS_US.ref();
               __TB.US_NAZ:=OS_US.US().NU;
               __TB.US_OD:=OS_US.OD;
               {? OS_US.next()
               || __TB.US_DO:=OS_US.OD-1;
                  _next:=1
               ?};
               {? ~(__TB.US_DO~1) || __TB.US_DO:=date(9999,12,31) ?};
               __TB.ERR:=1;
               __TB.OPIS:='Nie wskazano adresu do celów rozliczeń podatkowych'@;
               __TB.add(1);
               _next
            || OS_US.next()
            ?}
         !}
      ?}
   ?};
~~}
{MACRO-}
{MACRO: 'ogranicz'}
{  {? __TB.first()
   ||
      {!
      |? {? (~(__param.OD~1) & __param.DO~1=9999)
          | (__TB.US_OD<=__param.DO & __TB.US_OD>=__param.OD)
          | (__TB.US_DO>=__param.OD & __TB.US_DO<=__param.DO)
         || __TB.next()
         || __TB.del()
         ?}
      !}
   ?};
~~}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-__par1.width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{EXIT: ~__TB.first() }
{IF: __TB.first()}
{SUBSTITUTE: 'ogranicz'}
{__skul:=sql('select distinct PESEL, OSOBA, TECZKA from :_a order by 2,1',__TB); ~~}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{FOR: __skul}
{DO}
   {ENTIRE}
   {IF: ~rep_export()}
   {<{ceil(lmt*h_rat)+1} __Lp+=1;
      $__Lp+') '+{? __F_ZATR.P='P' || 'Pracownik: '@ || 'Zleceniobiorca: '@ ?}+__skul.OSOBA+' ('+form(__skul.TECZKA)+')'
   }
   {FI}
   {REP: __par1.fhbar(lmt)}
   {FOR: __TB}
   {PREFIX: __skul.OSOBA}
   {DO}
      { __par1.ini_line(); ~~ }
      {REP: __par1.fline(lmt)}
   {OD}
   {REP: __par1.flbar(lmt)}
   {ENTIRE-}
{OD}
{FI}
:!UTF-8
{TITLE:A. Arkusz spisu z natury}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   color:=prn1:='';
   lm:=ll:=lmt:=0;rsep:=1;vp:=1;

   VAR_DEL.delete('w','h','k','PRN','__only','komisja');
   w:=obj_new(9);
   h:=obj_new(9);
   k:=obj_new(9);
   dokl:=0; "--dokladnosc ilosc--";
   dokc:=0; "--dokladnosc ceny--";
   lp:=0; ile:=0; wsk:=0; bufk:=''; sumse:=0;
   PRN:=obj_new(@.CLASS.PRINT,10); PRN.s_frame();
   ii:=0;
   __only:=INN.MG().IL='T';

   h[1] := 'Poz.'@;
   h[2] := 'Kod towaru'@;
   h[3] := 'Nazwa towaru'@;
   h[4] := 'Data dostawy'@;
   h[5] := 'Cena dostawy'@;
   h[6] := 'jm'@;
   h[7] := 'Ilość wg ewidencji'@;
   h[8] := 'Ilość stwierdzona'@;
   h[9] := 'Uwagi'@;

   lpod:=lpdo:=0;

   wydr_naz:='inwe0001';
   wydr_edi:='INWE0001';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OZ.USER,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OZ.USER;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("
      {? WYDRUKIL.CHKBOX05<0
      ||
         FUN.info('Ilość stron arkusza spisu z natury nie może być ujemna.'@); 'CHKBOX05'
      ||
         {? WYDRUKIL.CHKBOX01=0
         || {? WYDRUKIL.CHKBOX05=0
            || FUN.info('Należy zanzaczyć opcję Drukować\n'+
                  'lub wypełnić pole Ilość stron.'@);'CHKBOX01'
            || ~~
            ?}
         || ~~
         ?}
      ?}")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         ZAKR.TA:=WYDRUKIL.CHKBOX01;
         ZAKR.ZD:=WYDRUKIL.CHKBOX02;
         ZAKR.OR:=WYDRUKIL.CHKBOX03;
         ZAKR.IS:=WYDRUKIL.CHKBOX04;

         ZAKR.IP:=WYDRUKIL.CHKBOX05
      ?}
   ?};

   k[1] := 4;
   k[2] :=20;
   k[3] :={? ZAKR.ZD || 31 || 51 ?};
   k[4] :=10;
   k[5] :=10;
   k[6] := 5;
   k[7] :=12;
   k[8] :=12;
   k[9] :={? __only || 40 |? ZAKR.IS || 10 || 25 ?}+{? ZAKR.ZD || 0 || 6 ?};

   acr:={? ZAKR.OR=1 || INP.ndx_tmp('',0,'IN',,0,'M','N',0,'M','KTM',0,'D',,0) || 'INP' ?};
   sapoz:=0;
   {? wyjscie=0
   ||
      INP.cntx_psh;
      INP.index('INP'); INP.prefix(INN.ref);
      {? INP.first
      ||
         sapoz:=1;
         {!
         |? _dokl:=+form(frac(INP.SE))-2; {? _dokl>dokl || dokl:=_dokl ?};
            _dokc:=+form(frac(INP.C))-2; {? _dokc>dokc || dokc:=_dokc ?};
            INP.next
         !}
      ?};
      INP.cntx_pop
   ?};
   INN.cntx_psh();
   komisja:=''
}
{END:
   INN.cntx_pop();
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep');
   VAR_DEL.delete('w','h','k','PRN','dokl','dokc','__only');
   VAR_DEL.delete('lp','ile','wsk','bufk','sumse');
   VAR_DEL.delete('lpod','lpdo');
   VAR_DEL.delete('ii','wyjscie','sapoz','komisja');
   {? ZAKR.OR=1 || INP.ndx_drop(acr) ?}; VAR_DEL.delete('acr')
}
{IF: wyjscie=1}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   MACRO danepoz                                            {COMMENT-}
{MACRO: 'danepoz'}
{
w[1] :=lp;
w[2] :=INP.M().KTM;
w[3] :=INP.M().N;
w[4] :={? (1+ST.MAG().TYP)='E' || INP.IN().D || INP.D ?};
w[5] :=INP.C;
w[6] :=INP.M().J().KOD;
w[7] :={? ZAKR.ZD || INP.SE || sumse ?};
w[8] :=' .......... ';
w[9] :=''
;~~}
{MACRO-}
{
pusta:=' ';
PRN.add("w[1]",k[1],h[1],1);
PRN.add("w[2]",k[2],h[2]);
PRN.add("w[3]",k[3],h[3]);
{? ZAKR.ZD
|| PRN.add("form(w[4],,2)",k[4],h[4]);
   {? ~__only || PRN.add("form(w[5],,dokc)",k[5],h[5],1) ?}
?};
PRN.add("w[6]",k[6],h[6],1);
{? ZAKR.IS
|| PRN.add("form(w[7],,dokl)",k[7],h[7],1)
?};
PRN.add("w[8]",k[8],h[8],1);
PRN.add("w[9]",k[9],h[9]);
prn1:='PRN';
rsep:=PAR_WYDR.RSEP
;~~}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{IF: ZAKR.TA=1}
{COMMENT}   wypelniony arkusz                                        {COMMENT-}
{COMMENT}   HEADER                                                   {COMMENT-}
   {HEADER}
   {FONT: 'T8GB'}{SPACE: 'C'}
   { PAR_WYDR.TITLE:='Arkusz spisu z natury %1 z dnia %2\nMagazyn: %3'@[INN.SYM,form(INN.DI),INN.MG().SYM+' - '+INN.MG().NAZ];~~}
   {SUBSTITUTE: 'HEAD'}
   {IF: page()=1}
      {FONT: 'T10'}{SPACE: 'C'}

{<25'Rodzaj inwentaryzacji'@}{<50}________________________

{<25'Sposób przeprowadzenia'@}{<50}________________________

{<1'Skład komisji inwentaryzacyjnej:'@}{<50'Inne osoby obecne przy spisie:'@}

{IF: exec('FindInSet','#table','INNKOM','INN',INN.ref(),,,,,null())=null()}
{<1}_________________________________{<50}________________________________

{<1}_________________________________{<50}________________________________

{<1}_________________________________{<50}________________________________
{ELSE}
   {FOR: INNKOM}
   {INDEX: 'INN'}
   {PREFIX: INN.ref()}
   {DO}
      {_przew:={? INNKOM.P='T' || 'Przewodniczący-'@ || '' ?};
       _osoba:='%1 %2 %3'[INNKOM.OSOBA().NAZWISKO,INNKOM.OSOBA().PIERWSZE,INNKOM.OSOBA().DRUGIE];
       komisja:='%1%2'[_przew,_osoba];~~}
      {<1 komisja}{<50}________________________________

   {OD}
{FI}

{<1'Spis rozpoczęto'@}{<18'dn.____________'@}{<38'o godz. __________'@}

{<1'zakończono'@}{<18'dn.____________'@}{<38'o godz. __________'@}
   {FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {IF: sapoz}{SUBSTITUTE:'TABHEAD'}{FI}
   {HEADER-}
   {FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FOOTER: 0}
   {IF: sapoz}{SUBSTITUTE: 'LINIAF'}{FI}
   {FOOTER-}
{COMMENT}   pozycje arkusza                                          {COMMENT-}
   { bufk:=''; ~~}
   {FOR: INP}
   {INDEX: acr}
   {PREFIX: INN.ref}
   {DO}
      {IF: ~ZAKR.ZD & bufk<>INP.M().KTM}
         {
         sumse:=0; bufk:=INP.M().KTM;
         INP.cntx_psh;
         INP.clear;
         INP.index('INP');
         INP.prefix(INN.ref,bufk,bufk);
         {? INP.first() || {! |? sumse+=INP.SE; INP.next() !} ?};
         INP.cntx_pop;
         lp+=1
         ;~~}
         {SUBSTITUTE: 'danepoz'}
         {SUBSTITUTE: 'LINIA0'}
      {ELIF: ZAKR.ZD}
         { lp+=1;~~}
         {SUBSTITUTE: 'danepoz'}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
      {TOTAL}
      {IF: lineleft>0}
         {HEADER}{HEADER-}
         {FOOTER}{FOOTER-}
         {SUBSTITUTE: 'LINIAF'}
      {FI}
   {OD}
   {IF: bufk=''}{PAGE}{FI}
   {HEADER}{HEADER-}
   {FOOTER}{FOOTER-}
{COMMENT}   podpisy                                               {COMMENT-}
   {ENTIRE}
   {FONT: 'T10'}{SPACE: 'C'}

{<1'Spis z natury zakończono na pozycji'@}{<42}_______

{<1'Podpis osoby materialnie odpowiedzialnej'@}{<42}_____________________________

{<1'Wycena'@}{<10}__________________________________
{<14'imię nazwisko'@}{<35'podpis'@}


{<40'Skład komisji inwentaryzacyjnej'@}

{<40'przewodniczący:'@}

{<40}___________________________________
{<40'członkowie:'@}
{IF: exec('FindInSet','#table','INNKOM','INN',INN.ref(),,,,,null())=null()}

{<40}___________________________________

{<40}___________________________________
{ELSE}
   {FOR: INNKOM}
   {INDEX: 'INN'}
   {PREFIX: INN.ref(),'N'}
   {DO}

{<40}___________________________________
   {OD}
{FI}
{<40'Sprawdził'@}

{<40}___________________________________
   {ENTIRE-}
{FI}
{IF: ZAKR.IP>0}
{COMMENT}   pusty arkusz                                             {COMMENT-}
{COMMENT}   HEADER                                                   {COMMENT-}
   {HEADER}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {<1 'Arkusz spisu z natury '@+form(INN.DI,,2)+', Magazyn: '@+INN.MG().SYM}
   {SUBSTITUTE: 'TABHEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}{ vp:=1;~~}
   {HEADER-}
   {IF: ZAKR.TA=1}{PAGE}{FI}
   {FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {FOOTER-}
{COMMENT}   pozycje arkusza                                          {COMMENT-}
   { wsk:=lp:=0; vp:=1;
   w[1] :=100; {! _ii:=2..9 |! w[_ii]:='' !}; w[8] :=' .......... ';
   _f:=prn1+'.ini_line()'; _v:=($(_f))();
   _f:=prn1+'.length()'; il_lin:=($(_f))()+2
   ;~~}
   {WHILE: wsk<ZAKR.IP}
   {DO}
      { lp+=1; w[1]:=lp;~~}
      {IF: _ll:=lineleft(); (_ll>0 & _ll<il_lin)}
         { lp:=0; wsk+=1;~~}
         {PAGE}
      {ELSE}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
   {OD}
{FI}
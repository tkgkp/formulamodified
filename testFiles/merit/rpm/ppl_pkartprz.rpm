:!UTF-8
{TITLE:Karta przychodów}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','__par','__b_rat','__ILE','__JUZ','__true');
   VAR_DEL.delete('__nline','__all','__param','__edit','__iter');
   tresc:='pkartprz';
   P.cntx_psh();
   __par:=obj_new(11);
   __all:=__par[7]:=0;
   {? ~exec('no_limit','schemat','PPL')
   || __all:=0
   || lmt:=rsep:=__nline:=__true:=0;
      __par[11]:=__b_rat:=vp:=1;
      prn1:='__par[1]';
      _upr:=exec('uprawnieniawrap','pkd','karty pracownika'@,'PPL_PLL_PKPR','PPL_PLL_RSKP','PPL_PLL_PSKP');
      {? +_upr
      || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
      || __param:=tab_tmp(1,
            'ASK','INTEGER','Przychody',
            'ROK','INTEGER','Rok'
         );
         __param.ROK:=O.RP;
         __param.add();

         __edit:=__param.mk_edit('Karta przychodów pracownika'@,0,'ppl_pkartprz');
         __param.win_esep(__edit,'Parametry wydruku'@);
         __param.win_efld(__edit,,'ASK',,,,,,'Uwzględniać przychody?'@,,,'check-box',
            'check_label="%1"' ['ze wszystkich etatów oraz zleceń'@],"1","0"
         );
         __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok podatkowy'@);
         __param.efld_opt(__edit,'mark=1',,'ROK');
         exec('ok_esc','#window',__param,__edit);
         __param.win_edit(__edit);
         {? __par[7]:=__param.edit(
               "  {? __param.ROK<=1900
                  || FUN.emsg('Błędna wartość parametru wydruku'@); 0
                  || 1
                  ?}
               "
            )
         || __all:=__param.ASK;
            PAR_WYDR.TITLE:='Karta przychodów pracownika za rok %1'@[$__param.ROK];
            __par[1]:=obj_new(@.CLASS.PRINT,18);
            {? rep_export()
            || __par[1].add(
                  "  OSOBA.seek(__par[2][__nline][15],,1,1);
                     OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE+' '+'(Nr teczki %1)'@[|P.T]
                  ",40,'Pracownik'@
               );
               __par[1].add("OSOBA.seek(__par[2][__nline][15],,1,1); OSOBA.NIP",13,'NIP'@);
               __par[1].add("OSOBA.seek(__par[2][__nline][15],,1,1); OSOBA.PESEL",13,'PESEL'@);
               __par[1].add(
                  "  OSOBA.seek(__par[2][__nline][15],,1,1);
                     _dza:=date(9999,12,0);
                     P.cntx_psh();
                     P.index('PRACOSOB');
                     P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,P.OSOBA);
                     {? P.first()
                     || {!
                        |? _dza:=P.DZA;
                           _dza~1=9999 & P.next()
                        !}
                     ?};
                     P.cntx_pop();
                     {? _dza~1<>9999 || _dza$1 || 'brak danych' ?}
                  ",10,'Data zatrudnienia'@,1
               );
               __par[1].add(
                  "  {? OSOBA.seek(__par[2][__nline][15],,1)
                     || {? (1+exec('adresdous','osoba',__param.ROK))='1'
                        || {? +OS_ADRES.ULICA || OS_ADRES.ULICA || OS_ADRES.MIASTO ?}+' '+OS_ADRES.DOM+
                           {? +OS_ADRES.LOKAL || ' lok. '+OS_ADRES.LOKAL || '' ?}+', '+OS_ADRES.KOD+' '+
                           {? +OS_ADRES.POCZTA || OS_ADRES.POCZTA || OS_ADRES.MIASTO ?}+', '+OS_ADRES.KRAJ().NAZ+'.'
                        || ''
                        ?}
                     || ''
                     ?}
                  ",40,'Adres'@
               )
            ?};
            __par[1].add("__par[2][__nline][14]",40,'Nazwa składnika'@);
            {! _nn:=1..12
            |! STR.split(date(date()~1,_nn,1)$8);
               __par[1].add($('__par[2][__nline]['+$_nn+']'),12,STR.get_word(),1,,,,,'12,2')
            !};
            {? ~rep_export()
            || __par[1].add($('__par[2][__nline][13]'),12,'RAZEM'@,1,,,,,'12,2')
            ?};
            __par[1].s_frame();

            __par[2]:=obj_new(10);
            {! _nn:=1..obj_len(__par[2]) |! __par[2][_nn]:=obj_new(15) !};

            __par[2][1][14]:='Przychód'@;
            __par[2][2][14]:='Koszty uzyskania przychodu'@;
            __par[2][3][14]:='Potrącone składki na ubezpieczenia społeczne'@;
            __par[2][4][14]:='Dochód w danym miesiącu'@;
            __par[2][5][14]:='Dochód narastająco'@;
            __par[2][6][14]:='Podatek nominalny'@;
            __par[2][7][14]:='Ubezpieczenie zdrowotne'@;
            __par[2][8][14]:='Ubezpieczenie zdrowotne odliczone od podatku'@;
            __par[2][9][14]:='Należna zaliczka na podatek dochodowy'@;
            __par[2][10][14]:='Data przekazania zaliczki'@;

            __ILE:=
               "  _sum:=0;
                  LS.use(O.LT);
                  {? __all
                  || LS.index('OSOBAKOD');
                     LS.prefix(exec('ref_firma','ustawienia'),P.OSOBA,_a)
                  || LS.index('PRACNRRU');
                     LS.prefix(P.ref(),_a)
                  ?};
                  {? LS.first()
                  || {!
                     |? _sum+=LS.KW$2;
                        LS.next()
                     !}
                  ?};
                  _sum
               ";
            __JUZ:=tab_tmp(,'O','INTEGER','OSOBA');
            params_set('P',exec('wybierz_parses','pracownik','PPL').P)
         ?}
      ?}
   ?};
   O.cntx_psh();
   {? __all
   || O.index('LISTYPOD')
   || O.index('LISTYPLP')
   ?};
   LS.cntx_psh()
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','__par','__b_rat','__ILE','__JUZ','__true');
   VAR_DEL.delete('__nline','__all','__param','__edit','__iter');
   P.cntx_pop();
   O.cntx_pop();
   LS.cntx_pop()
}
{COMMENT}OLD: pkartprz.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~__par[7]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{IF: ~rep_export}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{IF: ~rep_export()}
   {FONT: 'W10'}
   {SPACE: 'B'}
   {<{ceil(lmt*__b_rat)+1}}{'Pracownik:'@}{<{ceil(lmt*__b_rat)+30}}{P.OSOBA().NAZWISKO+' '+P.OSOBA().PIERWSZE+
      {? ~__all || ' ('+form(P.T)+')' || '' ?}
   }
   {<{ceil(lmt*__b_rat)+1}}{'NIP:'@}{<{ceil(lmt*__b_rat)+30}}{P.OSOBA().NIP}
   {<{ceil(lmt*__b_rat)+1}}{'PESEL:'@}{<{ceil(lmt*__b_rat)+30}}{P.OSOBA().PESEL}
   {<{ceil(lmt*__b_rat)+1}}{'Data zatrudnienia:'@}{<{ceil(lmt*__b_rat)+30}}{
      _dza:=date(9999,12,0);
      P.cntx_psh();
      P.index('PRACOSOB');
      P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,P.OSOBA);
      {? P.first()
      || {!
         |? _dza:=P.DZA;
            _dza~1=9999 & P.next()
         !}
      ?};
      P.cntx_pop();
      {? _dza~1<>9999 || _dza$1 || 'brak danych' ?}
   }
   {IF: (1+exec('adresdous','osoba',__param.ROK))='1'}{
      _adres:={? +OS_ADRES.ULICA || OS_ADRES.ULICA || OS_ADRES.MIASTO ?}+' '+
             OS_ADRES.DOM+
             {? +OS_ADRES.LOKAL || ' lok. '+OS_ADRES.LOKAL || '' ?}+', '+
             OS_ADRES.KOD+' '+{? +OS_ADRES.POCZTA || OS_ADRES.POCZTA || OS_ADRES.MIASTO ?}+', '+
             OS_ADRES.KRAJ().NAZ+'.';
      STR.split(_adres);
      __iter:=0; ~~
   }
   {<{ceil(lmt*__b_rat)+1}}{'Adres:'@}{<{ceil(lmt*__b_rat)+30}}{STR.line(175)}
   {WHILE: __iter+=1; __iter<=3 & STR.next()}
   {DO}
      {<{ceil(lmt*__b_rat)+1}}{<{ceil(lmt*__b_rat)+30}}{STR.line(175)}
   {OD}
   {FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{IF: {? rep_export() || __par[11] || 1 ?}}
{LINE}
{  __par[11]:=0; ~~ }
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{MACRO-}
{MACRO: tresc}
{IF: ~__JUZ.find_key(#P.OSOBA)}
   {  __JUZ.O:=#P.OSOBA;
      __JUZ.add(); ~~
   }
   {ENTIRE}
   {  __true:=__nline:=0;
      {! _nn:=1..obj_len(__par[2])
      |! {! _mm:=1..obj_len(__par[2][_nn])-2 |! __par[2][_nn][_mm]:=0 !}
      !};
      __par[2][10][13]:='';
      {! _mies:=1..12
      |! __par[2][10][_mies]:='';
         {? __all
         || O.prefix(exec('ref_firma','ustawienia'),__param.ROK,_mies)
         || O.prefix(exec('ref_firma','ustawienia'),__F_ZATR.O,__param.ROK,_mies)
         ?};
         {? O.first()
         || {!
            |? {? O.Z='T'
               || {? O.T().T='SUM'
                  || __par[2][10][_mies]:={? O.DUS~1 || O.DUS$1 || '' ?}
                  ?};
                  _rub784:=__ILE(784);
                  _rub780:=__ILE(780);
                  _ubspol:=__ILE(765)+__ILE(766)+__ILE(767);
                  __par[2][1][_mies]+=_rub780+_ubspol;
                  __par[2][2][_mies]+=_rub784;
                  __par[2][3][_mies]+=_ubspol;
                  __par[2][4][_mies]+=_rub780+_ubspol-_rub784;
                  __par[2][6][_mies]+=__ILE(790);
                  __par[2][7][_mies]+=__ILE(789)+__ILE(792);
                  __par[2][8][_mies]+=__ILE(793)+__ILE(794);
                  __par[2][9][_mies]+=__ILE(797)+__ILE(798)
               ?};
               O.next()
            !}
         ?};
         {? ~rep_export()
         || {! _par:=1..9
            |! __par[2][_par][13]+=__par[2][_par][_mies]
            !}
         ?}
      !};

      _kw:=0;
      {! _mies:=1..12
      |! {!  _par:=1..9
         |? __true=0
         |! {? __par[2][_par][_mies]>0 || __true:=1 ?}
         !}
      !};
      {? rep_export()
      || {! _par:=1..obj_len(__par[2])
         |! __par[2][_par][15]:=P.OSOBA
         !}
      ?};
      {? __true
      || {! _mm:=1..12
         |! _kw+=__par[2][4][_mm];
            __par[2][5][_mm]:=_kw
         !};
         {? ~rep_export()
         || __par[2][5][13]:=_kw
         ?}
      ?};
      ~~
   }
   {IF: __true}
      {SUBSTITUTE: 'pracownik'}
      {REP: __par[1].fhbar(lmt)}
      {WHILE: (__nline+=1)<=10}{DO}
         { __par[1].ini_line(); ~~ }
         {WHILE: __par[1].next()}{DO}
            {REP: __par[1].fline(lmt)}
         {OD}
      {OD}
      {REP: __par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
   {IF: __true}{PAGE}{FI}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W10'}{ __b_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ __b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'C'}
{ lmt:=int((charleft()-__par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
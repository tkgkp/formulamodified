:!UTF-8
{TITLE: B. Szczegółowy wydruk pierwotnych danych środka}
{PRINTER: FINFO.W_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   SRST.cntx_psh();
   SRSR.cntx_psh();
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0;
   amop:=obj_new(12);
   amof:=obj_new(12);
   {! _i:=1..12 |! amop[_i]:=amof[_i]:=0 !};
   amp:=amf:=anp:=anf:=uin:=wip:=wif:=wup:=wuf:=wnp:=wnf:=0;
   ind:=''
}
{END:
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3;&par4;
   obj_del(amop); obj_del(amof);
   &amp; &amf; &anp; &anf; &uin; &wip; &wif; &wup; &wuf; &wnp; &wnf;
   &ind;
   SRST.cntx_pop();
   SRSR.cntx_pop()
}
{EXIT:
   exec('set_parw','param_wydr');
   {? OPERATOR.DEPT<>null
   || PAR_WYDR.ODD:=OPERATOR.DEPT
   || PAR_WYDR.ODD:=0
   ?};
   PAR_WYDR.STAN_NA:=SRSR.DE;
   PAR_WYDR.WART_PN:=2;
   PAR_WYDR.ELEMENTY:=1;
   PAR_WYDR.RODZAJ:=1;
   PAR_WYDR.RODZAJ2:=1;
   PAR_WYDR.DRUKCECH:=1;
   PAR_WYDR.JORG:=null;
   UD_POM.JORG:='';
   PAR_WYDR.SRODEK:=$SRSR.ref();
   0
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_zzestawp.rpm
Utworzony  : 2015-11-26
Zawartość  : Szczególowe zestawienie pierwotnych danych srodka trwalego
==================================================
{COMMENT-}
{COMMENT}zmienne pomocnicze do wydrukow graficznych:
i - licznik, color, prn1, lm, ll, vp, lmt, rsep
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}--------------- OPIS PÓL ZMIENNEJ PAR_WYDR ------------{COMMENT-}
{MACRO: 'SRODEK'}
{ENTIRE}
{LINE: 1}
{FONT: 'T10B'}
{<1>95 'Z E S T A W I E N I E   P I E R W O T N Y C H  D A N Y C H   Ś R O D K A' }

{FONT: 'T10'}
{>42 'Nazwa środka' } :{FONT: 'T10B'}{ SRSR.NST }{FONT: 'T10'}
{>42 'Kod grupy' } : { SRSR.GR().GR }
{>42 'Stawka amortyzacji bilansowej' } : {,2 (SRSR.STAF * SRST.WKF)$FINFO.DOKL}
{>42 'Stawka amortyzacji podatkowej' } : {,2 (SRSR.STAP * SRST.WKP)$FINFO.DOKL}
{IF: FINFO.TOR_D='T'}
{>42 'Stawka amortyzacji dodatkowej' } : {,2 (SRSR.STAD * SRST.WKD)$FINFO.DOKL}
{FI}
{>42 'Jednostka księgowa' } : { SRSR.ODD().OD }
{>42 'Jednostka organizacyjna' } : { SRSR.JORG().SYMBOL }
{>42 'Na dzień' } : { PAR_WYDR.STAN_NA$6 }
{>42 'Rodzaj' } : { {? SRSR.R='T' || 'trwały' || 'niskocenny' ?} }

{>42 'Numer inwentarzowy' } :{FONT: 'T10B'}{ SRSR.NRI }{FONT: 'T10'}
{>42 'Numer dowodu OT' } : { {? SRSR.DOKPRZ<>null || SRSR.DOKPRZ().SYMBOL || '' ?} }
{>42 'Rodzaj przychodu' } : { SRSR.PRZYCHOD().R }
{>42 'Numer fabryczny' } : { SRSR.NF }
{>42 'Data zakupu' } : { SRSR.DZ$6 }
{>42 'Data eksploatacji' } : { SRSR.DE$6 }
{>42 'Użytkownik' } : { {? SRSR.OSOBA<>null || SRSR.OSOBA().PIERWSZE + ' ' + SRSR.OSOBA().NAZWISKO || '' ?} }
{ENTIRE-}
{ENTIRE}
{>42 'Zestaw kont bilansowych kosztów' } : { SRSR.KONFIN().KOD }{wiersz:=45;~~}
{FOR: KKOSZ } {INDEX: 'KONTO' } {PREFIX: SRSR.KONFIN }
{DO}
{FIRST}
{>{wiersz+4}}Składniki zestawu kont
{FIRST-}
{>{wiersz+4}}{KKOSZ.KONTO().SYM}  : {form(KKOSZ.PR,,2,'9')}%
{OD}
{ENTIRE-}
{ENTIRE}
{>42 'Zestaw kont podatkowych kosztów' } : { SRSR.KONPOD().KOD }{wiersz:=45;~~}
{FOR: KKOSZ } {INDEX: 'KONTO' } {PREFIX: SRSR.KONPOD }
{DO}
{FIRST}
{>{wiersz+4}}Składniki zestawu kont
{FIRST-}
{>{wiersz+4}}{KKOSZ.KONTO().SYM}  : {form(KKOSZ.PR,,2,'9')}%
{OD}
{ENTIRE-}
{IF: FINFO.TOR_D='T'}
{ENTIRE}
{>42 'Zestaw kont dodatkowych kosztów' } : { SRSR.KONDOD().KOD }{wiersz:=45;~~}
{FOR: KKOSZ } {INDEX: 'KONTO' } {PREFIX: SRSR.KONDOD }
{DO}
{FIRST}
{>{wiersz+4}}Składniki zestawu kont
{FIRST-}
{>{wiersz+4}}{KKOSZ.KONTO().SYM}  : {form(KKOSZ.PR,,2,'9')}%
{OD}
{ENTIRE-}
{FI}
{ENTIRE}
{>42 'Metoda amortyzacji podatkowej' } : { {? SRSR.MP().K='UJP'
                                           || 'UMORZENIE JEDNORAZOWE'
                                           || SRSR.MP().T
                                           ?}}
{>42 'Metoda amortyzacji bilansowej' } : { {? SRSR.MF().K='UJF'
                                           || 'UMORZENIE JEDNORAZOWE'
                                           || SRSR.MF().T
                                           ?}}
{IF: FINFO.TOR_D='T'}
{>42 'Metoda amortyzacji dodatkowej' } : { {? SRSR.MD().K='UJD'
                                           || 'UMORZENIE JEDNORAZOWE'
                                           || SRSR.MD().T
                                           ?}}
{FI}
{>42 'Sezonowy?' } : { {? SRSR.S='T' || 'TAK' + ', wg. schematu: ' + SRSR.SCH_SEZ().KOD || 'NIE' ?} }

{>42 'Skreślony?' } : { {? SRSR.Z='T' || 'TAK' || 'NIE' ?} }
{IF: SRSR.ROZ<>null}
{>42 'Rodzaj rozchodu' } : { SRSR.ROZ().R }
{FI}
{IF: SRSR.DES<>date(0,0,0)}
{>42 'Data skreślenia z ewidencji' } : { SRSR.DES$6 }
{FI}
{IF: SRSR.DOKSKR<>null}
{>42 'Numer dowodu skreślającego' } : { SRSR.DOKSKR().SYMBOL }
{FI}
{ENTIRE-}

{ENTIRE}
{>42 'Uwagi' } : { SRSR.U }

{ENTIRE-}
{ENTIRE}
{FONT: 'T10B'}
{<1>88 'P I E R W O T N E   W A R T O Ś C I   Ś R O D K A' }
{FONT: 'T10'}
{>42 'Wartość podatkowa' } : {>51 form(SRSR.WARP,15,2,' ,') }
{>42 'Umorzenie podatkowe' } : {>51 form(SRSR.UMOP,15,2,' ,') }
{>42 'Netto podatkowe' } : {>51 form(SRSR.NETP,15,2,' ,') }
{IF: SRSR.AMOR100='T'}
{>42 'Amortyzacji jednorazowej'@ } : {>51 form(SRST.SRSR().WARP100,15,2,' ,') }
{>42 'Zaliczki na zakup środka'@ } : {>51 form(SRST.SRSR().ZALP100,15,2,' ,') }
{>42 'Rozliczenie jednorazowe'@ } : {>51 SRST.SRSR().FREQ100().OPIS }
{FI}

{>42 'Wartość bilansowa' } : {>51 form(SRSR.WARF,15,2,' ,')}
{>42 'Umorzenie bilansowe' } : {>51 form(SRSR.UMOF,15,2,' ,')}
{>42 'Netto bilansowe' } : {>51 form(SRSR.NETF,15,2,' ,')}

{IF: FINFO.TOR_D='T'}
{>42 'Wartość dodatkowa' } : {>51 form(SRSR.WARD,15,2,' ,')}
{>42 'Umorzenie dodatkowe' } : {>51 form(SRSR.UMOD,15,2,' ,')}
{>42 'Netto dodatkowo' } : {>51 form(SRSR.NETD,15,2,' ,')}
{FI}

{ENTIRE-}
{MACRO-}

{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{ PAR_WYDR.TITLE:=''; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{COMMENT}{SUBSTITUTE: 'LINIAF'}{COMMENT-}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
 {SUBSTITUTE: 'SRODEK'}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
{FOOTER}{FOOTER-}
{ vp:=0; ~~}
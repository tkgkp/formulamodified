:!UTF-8
{TITLE:Lista obecności}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('kody','cfg','licznik','kol','prn','par','ok','prn1','color','lmt','rsep','vp');
   kody:=obj_new(3);
   kody[1]:=obj_new(3); kody[1][2]:=2;
   kody[2]:=obj_new(3); kody[2][2]:=15;
   kody[3]:=obj_new(3); kody[3][2]:=3;
   cfg:=obj_new('pdm','odm','szerD','szerP','lkol','tydz','P','LO');
   cfg.szerD:=5;
   cfg.szerP:=29;
   ok:=0;
   par:=obj_new('PAR','JORG','SCH');
   licznik:=obj_new('str','maxstr','dz','maxdz');
   kol:=obj_new(20);
   prn:=obj_new(@.CLASS.PRINT,20);

   P.cntx_psh();
   P.prefix();
   H.cntx_psh();
   H.index('_HISTKOD');
   H_ZM.cntx_psh();
   H_ZM.index('ZMIANY');
   R.cntx_psh();
   R.index('RUBKOD');
   N.cntx_psh();
   N.index('NIEOBECN');
   UD_DEF.cntx_psh();
   UD_SCH.cntx_psh();
   UD_SKL.cntx_psh();
   UD_POZ.cntx_psh();

   exec('lista_obecnosci','!pkd_zes_orze',par);
   prn1:='prn';
   color:='';
   lmt:=0;
   rsep:=vp:=1
}
{END:
   UD_SKL.cntx_pop();
   UD_POZ.cntx_pop();
   UD_SCH.cntx_pop();
   UD_DEF.cntx_pop();
   N.cntx_pop();
   R.cntx_pop();
   H_ZM.cntx_pop();
   H.cntx_pop();
   P.cntx_pop();
   VAR_DEL.delete('kody','cfg','licznik','kol','prn','par','ok','prn1','color','lmt','rsep','vp')
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{EXIT: ~UD_DEF.first()}
{EXIT: ~par.JORG.size()}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{EXIT:
   {! _lp:=1..obj_len(kody)
   |! kody[_lp][3]:={? R.find_key(kody[_lp][2]) || R.RT ?}
   !};

   cfg.pdm:=date(par.PAR.ROK,par.PAR.MC,1);
   cfg.odm:=date(par.PAR.ROK,par.PAR.MC,0);

   licznik.maxdz:=cfg.odm~3;

   _sql:=
      'select'+
      ' 0 WYDZIAL, OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.DRUGIE, KAL_NAZW.NAZWA KALENDAR,'+
      ' from_hex(substr(P.REFERENCE,9,8)) PREF '+
      'from P join OSOBA join KAL_NAZW '+
      'where P.FIRMA=:_d and P.F_ZATR=\':_a\' and P.DZA<=to_date(:_c) and (P.DZ is null or P.DZ>=to_date(:_b)) '+
      'order by PREF';
   cfg.P:=sql(_sql,__F_ZATR.REF,cfg.pdm,cfg.odm,exec('ref_firma','ustawienia'));

   {? var_press('P',cfg)=type_of(SYSLOG)
   || {? cfg.P.first()
      || {!
         |? H.prefix(cfg.P.PREF,'Z');
            {? H.find_le(cfg.odm)
            || cfg.P.WYDZIAL:=H.WYDZIAL;
               cfg.P.put()
            ?};
            cfg.P.next()
         !}
      ?};
      _ndx:=cfg.P.ndx_tmp(,,'WYDZIAL',,,'NAZWISKO',,,'PIERWSZE',,,'DRUGIE',,);
      cfg.P.index(_ndx);
      0
   || FUN.emsg(
         '%1\n'['Przygotowanie listy pracowników nie powiodło się.'@]+
         '%1\n'['Wydruk nie może zostać sporządzony w tej chwili.'@]+
         'Spróbuj ponownie za chwilę.'@);
      1
   ?}
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{COMMENT}OLD: zk_lobec.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{FONT: 'T8G'}
{SPACE: 'C'}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{EXIT:
   'Budowa wydruku:';
   'Dzień|Pracownik1|...|PracownikN|Kierownik';
   'Szerokość I-szej kolumny jest określona przez cfg.szerD, pozostałych - cfg.szerP.';
   'Przyjmując, że tabela będzie z ramkami bez odstępów -';
   'policzmy ile kolumn na podpisy zmieści się na jednej stronie.';
   'Pamiętajmy, że ostatnia kolumna jest na podpis kierownika.';
   cfg.lkol:=(charleft()-(cfg.szerD+2))%(cfg.szerP+1);
   {? rep_export() || cfg.lkol:=6 ?};
   {? cfg.lkol<2 || FUN.emsg('Bieżący układ strony nie pozwala na wydruk.'@); 1 || 0 ?}
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{
   _stab:='tab_tmp(2,\'STR\',\'INTEGER\',\'Nr strony\',\'DZ\',\'INTEGER\',\'Dzień\'';
   {! _kol:=1..cfg.lkol
   |! _stab+=',\'K'+$_kol+'\',\'STRING[80]\',\'K'+$_kol+'\''
   !};
   _stab+=')';
   cfg.LO:=($_stab)();

   cfg.tydz:=obj_new(7);
   cfg.tydz[1]:='Pn'@;
   cfg.tydz[2]:='Wt'@;
   cfg.tydz[3]:='Śr'@;
   cfg.tydz[4]:='Cz'@;
   cfg.tydz[5]:='Pt'@;
   cfg.tydz[6]:='Sb'@;
   cfg.tydz[7]:='Nd'@;

   prn.sl_frame();
   prn.add("kol[1]",cfg.szerD,'Dzień'@,1,,,,,',0,\'9.\'');
   {! _kol:=1..cfg.lkol |! prn.add($('kol['+$(1+_kol)+']'),cfg.szerP,'Podpis przełożonego'@) !};

   lmt:=int((charleft()-prn.width())/2);
   ~~
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'head'}
   {FONT: 'HEAD'}{SPACE: 'B'}{<1 lm:=charleft(); STR.split(KST.NAZWA); ~~}
   {IF: ~rep_export()}
   {WHILE: STR.next()}{DO}{<1 STR.line(lm)}{OD}
   {FI}
   {SPACE: 'A'}{FONT: 'TITLE'}{<1 lm:=charleft(); ~~}
   {<1>{lm} 'Lista obecności'@}
   {IF: ~rep_export()}
   {FONT: 'HEAD'}{SPACE: 'B'}{<1 lm:=charleft(); ~~}
   {<1 'Miesiąc:'@} {<10 cfg.odm$8+'r.'} {>{lm} 'Strona '@+$licznik.str+' z '@+$licznik.maxstr}
   {<1 'Komórka:'@} {<10 UD_DEF.SYMBOL+' - '+UD_DEF.OPIS}
   {FI}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'tabheadname'}
   {FONT: 'HEAD'}{SPACE: 'B'}{<1 lm:=charleft(); STR.split(KST.NAZWA); ~~}
   {FONT: 'HEAD'}{SPACE: 'B'}{<1 lm:=charleft(); ~~}
   {<1 'Miesiąc:'@} {<10 cfg.odm$8+'r.'}
   {<1 'Komórka:'@} {<10 UD_DEF.SYMBOL+' - '+UD_DEF.OPIS}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO:'tabhead'}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn1+'.ini_head()'; ($(_f))()}
   {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
   {WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
   {DO}
      {<1}{REP: _f:=prn1+'.h_fline(lmt)'; ($(_f))()}
   {OD}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{HEADER}
   {SUBSTITUTE: 'head'}
   {IF: ~rep_export()}
      {FONT: 'T8G'}{SPACE: 'C'}
      {SUBSTITUTE: 'tabhead'}
   {FI}
{HEADER-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FOR: UD_DEF}
{INDEX: 'SYMBOL'}
{PREFIX: par.SCH}
{DO}
 {IF: par.JORG.find_key($UD_DEF.ref())}
   {IF:
      'Przygotowanie danych do wydruku dla bieżącego wydziału.';
      cfg.LO.prefix();
      {? cfg.LO.first() || cfg.LO.erase() ?};
      cfg.P.prefix(#UD_DEF.UD_SKL);
      {? cfg.P.first()
      || _dni:=obj_new(licznik.maxdz);
         _str:=0;
         _kol:=0;
         {!
         |? _lw:=0;
            {? par.PAR.ZHN | par.PAR.ZK
            || _lw:=0;
               {! _lp:=1..licznik.maxdz |! _dni[_lp]:='' !};

               {? par.PAR.ZHN
               || 'Analiza nieobecności';
                  N.prefix('N',cfg.P.PREF);
                  {? N.find_le(cfg.odm)
                  || {!
                     |? {? N.OD<=cfg.odm & cfg.pdm<=N.DO
                        || _dp:={? N.OD<=cfg.pdm || cfg.pdm || N.OD ?};
                           _dk:={? cfg.odm<=N.DO || cfg.odm || N.DO ?};
                           {! _ld:=_dp~3.._dk~3 |! _dni[_ld]:=N.NB().RT; _lw+=1 !}
                        ?};
                        N.prev() & cfg.pdm<=N.DO
                     !}
                  ?}
               ?};

               {? par.PAR.ZK
               || 'Analiza kalendarza (dni wolne/świąteczne)';
                  __KAL.set_cal(cfg.P.KALENDAR,par.PAR.ROK);
                  {! _ld:=1..licznik.maxdz
                  |! {? _dni[_ld]=''
                     || _dt:=date(par.PAR.ROK,par.PAR.MC,_ld);
                        _dni[_ld]:=
                           {? __KAL.f_days(_dt,_dt) || _lw+=1; 'Dzień wolny'
                           |? __KAL.h_days(_dt,_dt) || _lw+=1; 'Dzień świąteczny'
                           || ''
                           ?}
                     ?}
                  !}
               ?}
            ?};

            {? ~par.PAR.POMIN | _lw<licznik.maxdz
            || {? _kol=0
               || cfg.LO.blank();
                  cfg.LO.STR:=_str+1;
                  cfg.LO.DZ:=0
               || cfg.LO.find_key(_str,0)
               ?};
               ($('cfg.LO.K'+$(_kol+1)+':=_a'))(form(cfg.P.NAZWISKO+' '+cfg.P.PIERWSZE+' '+cfg.P.DRUGIE));
               {? _kol=0 || _str+=cfg.LO.add() || cfg.LO.put() ?};

               {? _lw | par.PAR.ZK
               || {! _ld:=1..licznik.maxdz
                  |! {? _dni[_ld]<>''
                     || {? cfg.LO.find_key(_str,_ld)
                        || _nowy:=0
                        || _nowy:=1;
                           cfg.LO.blank();
                           cfg.LO.STR:=_str;
                           cfg.LO.DZ:=_ld
                        ?};
                        ($('cfg.LO.K'+$(_kol+1)+':=_a'))(_dni[_ld]);
                        {? _nowy || cfg.LO.add() || cfg.LO.put() ?}
                     ?}
                  !}
               ?};

               _kol+=1;
               {? _kol=cfg.lkol-1 || _kol:=0 ?}
            ?};
            cfg.P.next()
         !};
         'Kolumny "zapasowe"';
         _brakuje:=par.PAR.ZAPAS-(cfg.lkol-1-_kol);
         {!
         |? _brakuje>0
         |! cfg.LO.blank();
            cfg.LO.STR:=_str+1;
            _str+=cfg.LO.add();
            _brakuje-=cfg.lkol-1
         !}
      ?};
      {? cfg.LO.first()
      || licznik.str:=1;
         licznik.maxstr:=_str;
         1
      ?}
   }
      {IF: lineleft()>0}{PAGE}{FI}
      {WHILE: licznik.str<=licznik.maxstr}
      {DO}
         {IF:
            'Przygotowanie nagłówków';
            cfg.LO.prefix(licznik.str,0);
            {? cfg.LO.first()
            || {! _kol:=1..cfg.lkol-1
               |! _na:=($('cfg.LO.K'+$_kol))();
                  prn.HEAD[1+_kol]:={? _na='' || '' || $((licznik.str-1)*(cfg.lkol-1)+_kol)+'. '+_na ?}
               !};
               licznik.dz:=1
            ?}
         }
         {IF: rep_export()}
            {SUBSTITUTE: 'LINIAF'}
            {SUBSTITUTE: 'tabheadname'}
            {FONT: 'T8G'}{SPACE: 'C'}
            {SUBSTITUTE: 'tabhead'}
         {FI}
            {IF: licznik.str>1}{PAGE}{FI}
            {WHILE: licznik.dz<=licznik.maxdz}
            {DO}
               { cfg.LO.prefix(licznik.str,licznik.dz);
                 _fval:={? cfg.LO.first() || $('\'cfg.LO.K\'+$_a') || "'\\\'\\\''" ?};
                 kol[1]:=form(licznik.dz,2,0,'9.')+' '+cfg.tydz[date(par.PAR.ROK,par.PAR.MC,licznik.dz)~4];
                 {! _kol:=1..cfg.lkol |! kol[1+_kol]:=($(_fval(_kol)))() !}; ~~
               }
               {SUBSTITUTE: 'LINIA0'}
               { licznik.dz+=1; ~~}
            {OD}
            { licznik.str+=1; ~~}
         {FI}
      {OD}
      {ELSE}
         { FUN.emsg( '%1\n'['Lista obecności dla wybranej jednostki organizacyjnej'@]+
              'nie zawiera żadnego pracownika i nie będzie drukowana.'@ ); ~~ }
   {FI}
 {FI}
{OD}
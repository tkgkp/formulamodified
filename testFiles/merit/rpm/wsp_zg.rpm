:!UTF-8
{TITLE: Dokumenty}
{PRINTER: exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,9); PRN.sl_frame();
   color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=0;
   head:=PAR_WYDR.HEAD; vp:=1; rsep:=PAR_WYDR.RSEP;
   PAR_WYDR.TITLE:='DOKUMENTY Z POZDZIAŁAMI CONTROLLINGOWYMI NIEZGODNYMI Z POZYCJAMI'@;
  w:=obj_new(9); {! _i:=1..9 |! w[_i]:='' !}; exec('tab_dok','dok_fks',1); rodz_dok:=0
}
{END: VAR_DEL.delete('PRN','w'); &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &head; exec('tab_dokd','dok_fks'); &rodz_dok}
{EXIT: ~(rodz_dok:=FUN.choice('Rodzaj dokumentów'@,,'Zaakceptowane'@,'Niezaakceptowane'@,'Wszystkie'@))}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: skid_zg.rpm
Utworzony:    2006-09-20 [AMK]
==================================================
Zawartosc: Wydruk dokumentow z niezgodnymi pozycjami z podzialami
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{ PRN.add("w[1]",8,'Jed. ks.'@);
  PRN.add("w[2]",8,'Rejestr'@);
  PRN.add("w[3]",6,'Numer'@,1);
  PRN.add("w[4]",20,'Symbol dokumentu'@);
  PRN.add("w[9]",8,'Model'@);
  PRN.add("w[5]",50,'Rodzaj kosztu'@);
  PRN.add("w[6]",17,'Suma z pozycji'@,1);
  PRN.add("w[7]",17,'Suma z podziałów'@,1);
  PRN.add("w[8]",17,'Różnica'@,1);
  ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}{FONT: 'T8G'}{SPACE: 'C'}{IF: page()=1}
{FI}
{FONT: 'T8B'}{SPACE: 'C'}
{LINE: 1}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8B'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}{ vp:=1;~~}
{FOOTER-}
{COMMENT}------------------------------SRODEK-----------------------{COMMENT-}
{MACRO: 'SRODEK'}
{ KONT_ZG.erase();~~}
{IF: ~exec('spr_dok','dok_fks',0)}
{FOR: KONT_ZG}
{DO}
{FIRST}
{ w[1]:=DOK.ODD().OD; w[2]:=DOK.REJ().KOD; w[3]:=$DOK.NR; w[4]:=DOK.NK;~~}
{FIRST-}
{ w[5]:=KONT_ZG.RODZKOSZ;
  w[9]:=KONT_ZG.MODEL;
  w[6]:=form(KONT_ZG.SUM_POZ,17,2,' ,');
  w[7]:=form(KONT_ZG.SUM_KON,17,2,' ,');
  w[8]:=form(KONT_ZG.ROZNICA,17,2,' ,');~~}
{SUBSTITUTE: 'LINIA02'}
{ {! _i:=1..9 |! w[_i]:='' !}; ~~}
{OD}
{FI}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'C'}
{IF: rodz_dok=3}
{IF: OPERATOR.DEPT}
{FOR: 'DOK'}{INDEX: 'O_XD'}{PREFIX: OPERATOR.DEPT}
{DO}{SUBSTITUTE: 'SRODEK'}{OD}
{ELSE}
{FOR: 'DOK'}{INDEX: 'XD'}
{DO}{SUBSTITUTE: 'SRODEK'}{OD}
{FI}
{ELIF: rodz_dok=2}
{IF: OPERATOR.DEPT}
{FOR: 'DOK'}{INDEX: 'CON1_O'}{PREFIX: OPERATOR.DEPT,'N'}
{DO}{SUBSTITUTE: 'SRODEK'}{OD}
{ELSE}
{FOR: 'DOK'}{INDEX: 'CON1'}{PREFIX: 'N'}
{DO}{SUBSTITUTE: 'SRODEK'}{OD}
{FI}
{ELSE}
{IF: OPERATOR.DEPT}
{FOR: 'DOK'}{INDEX: 'CON1_O'}{PREFIX: OPERATOR.DEPT,'T'}
{DO}{SUBSTITUTE: 'SRODEK'}{OD}
{ELSE}
{FOR: 'DOK'}{INDEX: 'CON1'}{PREFIX: 'T'}
{DO}{SUBSTITUTE: 'SRODEK'}{OD}
{FI}
{FI}

:!UTF-8
{TITLE: Zestawienie dokumentów w obiegu}
{PRINTER: 'land_std',,,,,'Q','C',,,,,,1,15,15,15,15,0,1,,'enable'}
{BEGIN:
  EDOKUM.cntx_psh;
  exec('czytaj','#stalesys');
  PRN:=obj_new(@.CLASS.PRINT, 10);
  PRN.sl_frame();
  prn1:='PRN';
  dalej:=typobi:=0;data_od:=data_do:=date();
  lm:=ll:=vp:=lmt:=rsep:=0;
  rekord:=EDOKUM.ref;exit:=0;
  D_HELP.win_edit('ODDO');
  {? D_HELP.edit("
  _txt:='';
  {? EDOKUM.name()+2<>$(D_HELP.OD~1)+2 | EDOKUM.name()+2<>$(D_HELP.DO~1)+2
  || _txt:='Należy wybrać daty w aktywnym roku bilansowym'
  |? D_HELP.OD>D_HELP.DO
  || _txt:='Data OD musi być wcześniejsza od daty DO'
  ?};
  {? _txt<>''
  || FUN.info(_txt); 0
  || 1
  ?}
  ")
  ||
     data_od:=D_HELP.OD;
     data_do:=D_HELP.DO
  ||
     data_od:=date(0,0,0);
     data_do:=date(0,0,0);
     exit:=1
  ?}
}
{END:
  EDOKUM.f_seek(rekord);
  EDOKUM.cntx_pop;
  obj_del(PRN);
  &dalej;
  &prn1;
  &lm; &ll; &vp; &lmt; &rsep;&rekord;&exit;
  VAR_DEL.delete('data_od','data_do','rodzaj','status','uzytk','typ','par')
}
{EXIT: exit=1}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: obe_zestj.rpm
Utworzony:    2018-03-08 [BZ]
==================================================
Zawartosc: Zestawienie dokumentow w obiegu i wnioskow
{COMMENT-}

{COMMENT}--Definicja wiersza wydruku.------------------------{COMMENT-}
{  EDOKUM.TYPOBIEG();
      {? TYPOBIEG.NAZWA='Obieg faktur'
      || typobi:=1
      |? TYPOBIEG.NAZWA='Obieg wniosków'
      || typobi:=2
      || typobi:=3
      ?};
  {? typobi=3
  || PRN.add("$(EDOKUM.DW)",12,'Data wyjazdu')
  || PRN.add("$(EDOKUM.DATAW)",12,'Rejestrowano')
  ?};
  PRN.add("EDOKUM.USERS().DANE",15,'Rejestrujący');
  {? typobi=3
  || PRN.add("EDOKUM.DOSTAWCA().NAZWISKO",15,'Delegowany')
  |? typobi=2
  || PRN.add("EDOKUM.DOSTAWCA().NAZWISKO",15,'Osoba')
  |? typobi=1
  || PRN.add("$(EDOKUM.TP)",10,'Termin płatności')
  ?};
  {? typobi=3
  || PRN.add("$(EDOKUM.DATA_OD)",10,'Od dnia')
  |? typobi=2
  || PRN.add("EDOKUM.TR",40,'Temat')
  |? typobi=1
  || PRN.add("EDOKUM.KH().KOD",11,'Kod kontrahenta')
  ?};
  {? typobi=3
  || PRN.add("$(EDOKUM.DATA_DO)",10,'Do dnia')
  |? typobi=2
  || PRN.add("form(EDOKUM.NETTO,,2)",11,'Kwota netto',1)
  |? typobi=1
  || PRN.add("EDOKUM.KH().TR",30,'Nazwa kontrahenta')
  ?};
  {? typobi=3
  || PRN.add("EDOKUM.DEL_MIE",17,'Miejsce delegacji')
  |? typobi=1
  || PRN.add("EDOKUM.SYM",15,'Symbol dokumentu')
  ?};
  {? typobi=3
  || PRN.add("EDOKUM.DEL_CEL().TR",25,'Cel delegacji')
  |? typobi=2 | typobi=1
  || PRN.add("form(EDOKUM.WART,,2)",11,'Kwota brutto',1)
  ?};
  {? typobi=1 || PRN.add("{? EDOKUM.EDOKUM ||EDOKUM.bl_info('EDOKUM','NAME') || '' ?}",10,'E-dokument',1)?};
  PRN.add("EDOKUM.B_PRELS",14,'Bieżaca czynność');
  PRN.add("{? EDOKUM.ZAM<> 'T' || 'N' || EDOKUM.ZAM ?}",4,'Zam.');
  PAR_WYDR.TITLE:='Dokumenty w obiegu'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
 {FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}

{<2 'Zakres dat: od '+$data_od +' do ' +$data_do}{<80 'Rodzaj dokumentów: '+ {? typobi=1 ||'Faktury' |? typobi=2 || 'Wnioski' || 'Delegacje' ?}}
{<2 'Status: '+ hdr_obg }{<80 'Wybrany typ dokumentów: '+ {? OBIEGI.CZY_TYP <>'W' || OBIEGI.TYP().NAZWA || 'Wszystkie' ?}}

{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T10B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{IF: EDOKUM.f_first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
    {IF: (data_od<=EDOKUM.DATAW & EDOKUM.DATAW<=data_do) | (data_od=date(0,0,0) & data_do=date(0,0,0))}
    {SUBSTITUTE: 'LINIA0'}
    {FI}
   { dalej:=EDOKUM.f_next(); ~~}
{OD}
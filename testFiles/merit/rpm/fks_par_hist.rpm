:!UTF-8
{TITLE:B.  Zestawienie zbiorcze rozliczeń rozrachunków}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN:
   w:=obj_new(20);  ww1:=''; szer:=obj_new(15); indtym:=''; petla:=0; rozr_1:=rozr_2:=''; pierw:=0;
   PRN:=obj_new(@.CLASS.PRINT,15); PRN.sl_frame();
   PRN2:=obj_new(@.CLASS.PRINT,2); PRN2.sl_frame();
   szer[1]:=35; szer[3]:=10; szer[4]:=3; szer[5]:=10; szer[6]:=40; szer[7]:=14; szer[8]:=3;
   szer[9]:=50; szer[10]:=szer[11]:=szer[12]:=szer[13]:=13; szer[14]:=1;
   {! _i:=1..15 |! w[_i]:='' !}; ROZNE.win_edit('ROZL_HIS'); ROZNE.ROZL_ZAK:=0; ROZNE.ROZL_STA:=0;
   ROZNE.ROZL_KOD:=ROZNE.ROZL_KDO:=null; ROZNE.ROZL_SLU:=null; ROZNE.SZUK:=1;
   ROZNE.ROZL_SL1:=''; ROZNE.ROZL_POZ:=null; sumka:=obj_new(10); {! _i:=1..10 |! sumka[_i]:=0 !};
   wal:=wal1:=SymOp:=''; SLO.win_sel('ONE'); SLO.win_dict('ONE'); mdc:=''; lm:=0}
{END:
   PAR_NAG.ndx_drop();
   VAR_DEL.delete('w','PRN','PRN2','ww1','szer','indtym','petla','rozr_1','rozr_2');
   VAR_DEL.delete('pierw','sumka','wal','wal1','mdc','lm','SymOp')
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_par_hist.rpm
Utworzony:   2001-12-14 [AMK] Artur Makos
==================================================
Zawartosc:  Zestawienie zbiorcze parowan
{COMMENT-}
{EXIT: ~ROZNE.edit("{? ROZNE.ROZL_ZAK=3 & ROZNE.ROZL_KDO().SYM<ROZNE.ROZL_KOD().SYM &
                       |ROZNE.ROZL_KDO().SYM<>''
                    || FUN.info('Należy podać poprawnie zakres kont.'@); 'ROZL_KOD'
                    |? ROZNE.ROZL_ZAK=1 & (|ROZNE.ROZL_SL1='' | ROZNE.ROZL_POZ=null)
                    || FUN.info('Podaj poprawnie kontrahenta.'@); 'ROZL_SL1'
                    || 1
                    ?}")}
{ {? ROZNE.ROZL_ZAK=3
  || {? ROZNE.ROZL_KOD().SYM='' || AN.index('SYM'); AN.first(); ROZNE.ROZL_KOD:=AN.ref() ?};
     {? ROZNE.ROZL_KDO().SYM='' || AN.index('SYM'); AN.last(); ROZNE.ROZL_KDO:=AN.ref() ?}
  ?};~~}
{ PRN.add("w[1]",szer[1],'              Konto/#        Rozrachunek główny'@,,'#');
  PRN.add("w[3]",szer[3],'Data otw./ T. płatn.'@);
  PRN.add("w[4]",szer[4],'Typ'@);
  {? ROZNE.ROZL_ZAK<>1 || PRN.add("w[6]",szer[6],'           Wystawca'@) ?};
  {? ROZNE.ROZL_STA=0 || PRN.add("w[14]",szer[14],'S') ?};
  {? ROZNE.ROZL_STA=0 | ROZNE.ROZL_STA=1 || PRN.add("w[5]",szer[5],'D. akcept.'@) ?};
  PRN.add("w[8]",szer[8],'Str'@);  PRN.add("w[15]",szer[8],'Wal'@);
  PRN.add("w[7]",szer[7],' Do rozliczenia'@,1);
  PRN.add("w[9]",szer[9],'Rozrach. rozliczany/ Typ       T. płatn.'@);
  PRN.add("w[10]",szer[10],'  Saldo Winien'@,1); PRN.add("w[11]",szer[11],'    Saldo Ma'@,1);
  PRN.add("w[12]",szer[12],'Kw. rozliczenia'@,1); PRN.add("w[13]",szer[13],' Saldo po rozl.'@,1);
  PRN2.add("",szer[1]+szer[3]+szer[4]+szer[7]+szer[8]+szer[8]+5+
              {? ROZNE.ROZL_ZAK<>1 || szer[6]+1 || 0 ?}+
              {? ROZNE.ROZL_STA=0 || szer[14]+1 || 0 ?}+
              {? ROZNE.ROZL_STA=0 | ROZNE.ROZL_STA=1 || szer[5]+1 || 0 ?},
              (25+{? ROZNE.ROZL_ZAK<>1 || 15 || 0 ?}+
                     {? ROZNE.ROZL_STA=0 | ROZNE.ROZL_STA=1 || 6 || 0 ?})*' '+
              'Rozliczenia rozrachunków'@);
  PRN2.add("",szer[9]+szer[10]+szer[11]+szer[12]+szer[13]+4,'%1 Pozycje rozliczeń rozrachunków'@[34*' ']);~~}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{<1 PRN1.line()}
{OD}
{MACRO-}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ZESTAWIENIE ZBIORCZE ROZLICZEŃ ROZRACHUNKÓW'@;~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Rok obrotowy: %1'@[SSTALE.AR().NAZ]}
{<5 {? OPERATOR.DEPT
    || 'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
    || 'Jednostka księgowa: wszystkie jednostki księgowe'@
    ?}
}
{<5 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<5
    {? ~ROZNE.ROZL_ZAK
    || 'Zakres rozliczeń: wszystkie'@
    |? ROZNE.ROZL_ZAK=1
    ||'Zakres rozliczeń: dla wystawcy - %1'@[ROZNE.ROZL_POZ().TR]
    ||'Zakres rozliczeń: przedział kont od: %1 do: %2'@[ROZNE.ROZL_KOD().SYM,ROZNE.ROZL_KDO().SYM]
    ?}}
{<5
   {? ~ROZNE.ROZL_STA
    || 'Status rozliczeń:wszystkie'@
    |? ROZNE.ROZL_STA=1
    || 'Status rozliczeń:zaakceptowane'@
    |? ROZNE.ROZL_STA=2
    || 'Status rozliczeń:niezaakceptowane'@
    || 'Status rozliczeń:wycofane'@
    ?}
}
{FI}{FONT: 'R'}{SPACE: 'C'}
{PRN2.hbar()}
{PRN2.ini_head()}
{WHILE: PRN2.h_next()}
{DO}
{PRN2.h_line()}
{OD}
{PRN2.jbar(PRN)}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{PRN.lbar()}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}------------------------------MACRO-POZYCJE------------------{COMMENT-}
{MACRO: 'POZYCJE'}{ pierw:=0; ~~}
{FOR: 'PAR_POZ'}
{INDEX: 'PAR_POZ'}
{PREFIX: PAR_NAG.ref}
{DO}{ rozr_1:=''; wal:=PAR_NAG.WAL().KOD; wal1:=PAR_NAG.WAL2().KOD;~~}
{FIRST}
{ {! _i:=1..15 |! w[_i]:='' !}; w[1]:=PAR_NAG.KONTO;
   OP.cntx_psh();  OP.index('KON_O');
   OP.prefix(SSTALE.WAL,PAR_NAG.ODD,PAR_NAG.KONTO,PAR_NAG.OP,PAR_NAG.OP,PAR_NAG.SYM_ROK);
   SymOp:=PAR_NAG.OP;
   w[2]:=35+PAR_NAG.OP; {? OP.first() || w[3]:=OP.DO$3; rozr_1:=OP.TZ$3; w[4]:=OP.TYP ?};  OP.cntx_pop();
   w[6]:=40+PAR_NAG.POCH().TR; w[8]:=PAR_NAG.STR;
   w[7]:=form(PAR_NAG.KW_ROZ,,2,' ,'); sumka[1]+=PAR_NAG.KW_ROZ$2;
   w[14]:=PAR_NAG.ZN; w[5]:=PAR_NAG.DATA_AKC$3; w[15]:=PAR_NAG.WAL().KOD;
~~}
{FIRST-}
{ w[9]:=PAR_POZ.OP;
   OP.cntx_psh();  OP.index('KON_O'); rozr_2:='';
   OP.prefix(SSTALE.WAL,PAR_NAG.ODD,PAR_NAG.KONTO,PAR_POZ.OP,PAR_POZ.OP,PAR_POZ.SYM_ROK);
   {? OP.first() || rozr_2:=form('   '+OP.TYP,32)+OP.TZ$3; w[9]:=form(PAR_POZ.OP,50) ?};
   OP.cntx_pop();
   w[10]:={? -PAR_NAG.STR='ma'
                || sumka[2]+=PAR_POZ.SAL$2; form(PAR_POZ.SAL,,2,' ,')
                || form(0,,2,' ,')
                ?};
   w[11]:={? -PAR_NAG.STR='wn'
                || sumka[3]+=PAR_POZ.SAL$2; form(PAR_POZ.SAL,,2,' ,')
                || form(0,,2,' ,')
                ?};
   w[12]:=form(PAR_POZ.KWOTA,,2,' ,'); sumka[4]+=PAR_POZ.KWOTA$2;
   w[13]:=form((PAR_POZ.SAL-PAR_POZ.KWOTA),,2,' ,'); sumka[5]+=(PAR_POZ.SAL-PAR_POZ.KWOTA)$2;
~~}
{ENTIRE}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{ _w2:=w[2]; {! _i:=1..15 |! w[_i]:='' !}; w[3]:=rozr_1; w[9]:=rozr_2; w[1]:=_w2;~~}
{ {? PAR_NAG.WAL2<>null
    ||  PAR_NAG.cntx_psh(); PAR_POZ.cntx_psh(); _rozr:=PAR_POZ.OP; _usym:=PAR_POZ.SYM_ROK;
       PAR_NAG.index('UNIK');
       PAR_NAG.prefix(PAR_NAG.WAL2,'P',PAR_NAG.UNIK);
       {? PAR_NAG.first()
        ||  PAR_POZ.index('POZ_ROZ'); PAR_POZ.prefix(PAR_NAG.ref,_rozr,_rozr,_usym);
           {? PAR_POZ.first
            ||  PAR_POZ.PAR_NAG();
               w[7]:=form(PAR_NAG.KW_ROZ,,2,' ,'); sumka[6]+=PAR_NAG.KW_ROZ$2;
               w[10]:={? -PAR_NAG.STR='ma'
                            || sumka[7]+=PAR_POZ.SAL$2; form(PAR_POZ.SAL,,2,' ,')
                            || form(0,,2,' ,')
                            ?};
               w[11]:={? -PAR_NAG.STR='wn'
                            || sumka[8]+=PAR_POZ.SAL$2; form(PAR_POZ.SAL,,2,' ,')
                            || form(0,,2,' ,')
                            ?};
               w[12]:=form(PAR_POZ.KWOTA,,2,' ,'); sumka[9]+=PAR_POZ.KWOTA$2;
               w[13]:=form((PAR_POZ.SAL-PAR_POZ.KWOTA),,2,' ,'); sumka[10]+=(PAR_POZ.SAL-PAR_POZ.KWOTA)$2
           ?}
       ?};
       PAR_NAG.cntx_pop();  PAR_POZ.cntx_pop()
    ?};
    {? ~pierw
    || pierw:=1; w[6]:=(40-PAR_NAG.POCH().TR); w[15]:=PAR_NAG.WAL2().KOD
    || w[7]:=''
    ?};~~}
{SUBSTITUTE: 'LINIA'}
{ w[1]:=35-SymOp; {! _i:=2..15 |! w[_i]:='' !}; ~~}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{ {! _i:=1..15 |! w[_i]:='' !};~~}
{OD}
{MACRO-}
{COMMENT}-----------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{IF: ROZNE.ROZL_ZAK=0 | ROZNE.ROZL_ZAK=3}
  {IF: OPERATOR.DEPT}
  {FOR: 'PAR_NAG'}
  {INDEX: 'KONTO_O'}
  {FROM: SSTALE.WAL, 'P', OPERATOR.DEPT, ROZNE.ROZL_KOD().SYM}
  {TO: SSTALE.WAL, 'P', OPERATOR.DEPT, ROZNE.ROZL_KDO().SYM}
  {DO}
  {IF: ~ROZNE.ROZL_STA | (ROZNE.ROZL_STA=1 & -PAR_NAG.ZN='t') |
       (ROZNE.ROZL_STA=2 & -PAR_NAG.ZN='n') | (ROZNE.ROZL_STA=3 & -PAR_NAG.ZN='w')}
  {SUBSTITUTE: 'POZYCJE'}
  {FI}
  {OD}
  {ELSE}
  {FOR: 'PAR_NAG'}
  {INDEX: 'KONTO'}
  {FROM: SSTALE.WAL, 'P', ROZNE.ROZL_KOD().SYM}
  {TO: SSTALE.WAL, 'P', ROZNE.ROZL_KDO().SYM}
  {DO}
  {IF: ~ROZNE.ROZL_STA | (ROZNE.ROZL_STA=1 & -PAR_NAG.ZN='t') |
       (ROZNE.ROZL_STA=2 & -PAR_NAG.ZN='n') | (ROZNE.ROZL_STA=3 & -PAR_NAG.ZN='w')}
  {SUBSTITUTE: 'POZYCJE'}
  {FI}
  {OD}
  {FI}
{ELSE}
 { {? OPERATOR.DEPT
     || indtym:=PAR_NAG.ndx_tmp('',1,'WAL',,0,'TYP',,0,'ODD',,0,'POCH',,0,'KONTO',,0,'DATA_AKC',,0,'UNIK',,0);
        PAR_NAG.index(indtym);  PAR_NAG.prefix(SSTALE.WAL,'P',OPERATOR.DEPT,ROZNE.ROZL_POZ)
     || indtym:=PAR_NAG.ndx_tmp('',1,'WAL',,0,'TYP',,0,'POCH',,0,'KONTO',,0,'DATA_AKC',,0,'UNIK',,0);
        PAR_NAG.index(indtym);  PAR_NAG.prefix(SSTALE.WAL,'P',ROZNE.ROZL_POZ)
     ?}; petla:=PAR_NAG.first;~~}
{FI}
{WHILE: petla}
{DO}
{IF: ~ROZNE.ROZL_STA | (ROZNE.ROZL_STA=1 & -PAR_NAG.ZN='t') |
       (ROZNE.ROZL_STA=2 & -PAR_NAG.ZN='n') | (ROZNE.ROZL_STA=3 & -PAR_NAG.ZN='w')}
{SUBSTITUTE: 'POZYCJE'}
{FI}
{ petla:=PAR_NAG.next;~~}
{OD}
{IF: (SSTALE.WAL<>FINFO.NAROD & lineleft=5) | (SSTALE.WAL=FINFO.NAROD & lineleft=4)}
{PAGE}
{FI}
{ENTIRE}
{FONT: 'R'}{SPACE: 'C'}
{ {! _i:=1..15 |! w[_i]:='' !}; w[7]:=form(sumka[1],,2,' ,');
   w[10]:=form(sumka[2],,2,' ,');  w[11]:=form(sumka[3],,2,' ,');
   w[12]:=form(sumka[4],,2,' ,'); w[13]:=form(sumka[5],,2,' ,');
   w[15]:=wal; w[1]:='   Suma:'@;~~}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{IF: SSTALE.WAL<>FINFO.NAROD}
{ {! _i:=1..15 |! w[_i]:='' !}; w[7]:=form(sumka[6],,2,' ,');
   w[10]:=form(sumka[7],,2,' ,');  w[11]:=form(sumka[8],,2,' ,');
   w[12]:=form(sumka[9],,2,' ,'); w[13]:=form(sumka[10],,2,' ,');
   w[15]:=wal1;~~}
{SUBSTITUTE: 'LINIA'}
{FI}
{FONT: 'Q'}{SPACE: 'C'}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
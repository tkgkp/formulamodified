:!UTF-8
{TITLE: Wydruk przelewów oczekujących}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame(); w:=obj_new(6);
   PRNF:=obj_new(@.CLASS.PRINT,2); PRNF.s_frame();
   l:=1; vsum:=0;
   PB.cntx_psh();
   pb_ref:=PB.ref();
   par1:='';
   WAL.prefix(); gtwals:=WAL.size(); twal:=obj_new(gtwals); nwal:=0;
   {! _i:=1..gtwals |! twal[_i]:=obj_new(2) !};
   {! _i:=1..gtwals |! twal[_i,1]:=''; twal[_i,2]:=0 !};
   color:=prn1:=prn2:=''; lm:=ll:=vp:=lmt:=rsep:=0;
   fa:=PB.f_active()
}
{END:
   obj_del(PRN); obj_del(PRNF); obj_del(w);
   &l; &vsum;
   {? fa || PB.f_seek(pb_ref) ?};
   PB.cntx_pop();
   &par1;
   &gtwals; obj_del(twal); &twal; &nwal; &pb_ref;
   &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &fa
}
{EXIT:
   {? fa & ~PB.f_first() | ~fa & ~PB.first()
   || FUN.info('Brak danych spełniających założone warunki.'@);
      _exit:=1
   || _exit:=0
   ?};
   {? _exit=0
   || PAR_WYDR.STAN_PRZ:='W';
      {? PB.name()*'xxx'
      || PAR_WYDR.win_edit('PRZEL_DR');
         {? PAR_WYDR.edit() || _exit:=0 || _exit:=1 ?}
      ?}
   ?};
   {? _exit=0 & PAR_WYDR.STAN_PRZ<>'W'
   || _next:=1;
      {! |?
         {? PB.ZT=PAR_WYDR.STAN_PRZ || _next:=0 ?};
         _next & (fa & PB.f_next() | ~fa & PB.next())
      !};
      {? _next=1
      || FUN.info('Brak danych spełniających założone warunki.'@);
         _exit:=1
      ?}
   ?};
   _exit
}
{
   PRN.add("w[1]",13,'Data przelewu'@);
   PRN.add("w[2]",35,'Przelew z rachunku'@,,'#');
   PRN.add("w[3]",60,'Wierzyciel / rachunek bankowy'@,,'#');
   PRN.add("w[4]",50,'Tytuł przelewu'@);
   PRN.add("w[6]",5,'Zatw.'@);
   PRN.add("w[5]",20, 'Waluta / Kwota'@,1,'#');
   PRNF.add("'Razem'@",175);
   PRNF.add("w[1]",20,,1,'#');
   {? PB.name()+4='xxxx'
   || PAR_WYDR.TITLE:='Przelewy oczekujące'@
   || PAR_WYDR.TITLE:='Przelewy archiwalne, rok: %1'@[TT_ROK.ROK]
   ?};
   prn1:='PRN'; prn2:=prn3:='PRNF'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'W8'}{SPACE: 'C'}
{IF: PB.name()*'xxx'}
{<1 'PARAMETRY WYDRUKU:'}
{COMMENT} ---- Stan na, Grupa, Rodzaj zestawienia ---- {COMMENT-}
{
  {? PAR_WYDR.STAN_PRZ='W'
  || par1:='wszystkie'@
  |? PAR_WYDR.STAN_PRZ='T'
  || par1:='zatwierdzone'@
  |? PAR_WYDR.STAN_PRZ='N'
  || par1:='niezatwierdzone'@
  ?}
;~~}
{<1 'Status przelewów: '+par1}
{FI}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; ~~}
{WHILE: l}
{DO}
   {IF: PAR_WYDR.STAN_PRZ='W' | PB.ZT=PAR_WYDR.STAN_PRZ}
      {  w[1]:=$PB.DP;
         w[2]:=PB.RD;
         w[3]:=form(PB.W,60)+'#'+RB.get_rbtx(3,PB.RW,PB.KRAJ_BAN().KODISO);
         w[4]:=PB.TYT;
         w[5]:=form(PB.WAL().KOD,20)+'#'+form(PB.KW,,2,' ,');
         w[6]:={? PB.ZT='T' || 'Tak' || 'Nie' ?};
         _v:=0;
         {! _i:=1..gtwals |! {? twal[_i,1]=PB.WAL().KOD || _v:=1 ?} !};
         {? ~_v
         || _i:=0; {! |? _i+=1; twal[_i,1]<>'' !};
            twal[_i,1]:=PB.WAL().KOD; twal[_i,2]:=PB.KW
         || _i:=0; {! |? _i+=1; twal[_i,1]<>PB.WAL().KOD !};
            twal[_i,2]+=PB.KW
         ?};
         vsum+=PB.KW; ~~}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
   { l:={? fa || PB.f_next() || PB.next() ?}; ~~}
{OD}
{COMMENT}------------------------------REPORT-FOOTER----------------{COMMENT-}
{  w[1]:='';
   {! _i:=1..gtwals |!
      {? twal[_i,1]<>'' || w[1]+=form(twal[_i,1],20)+'#'+form(twal[_i,2],,2,' ,')+'#'; nwal+=1 ?}
   !};
   w[1]:=w[1]-1; ~~}
{IF: lineleft()<>0 & lineleft()<(2+nwal*2)}{PAGE}{FI}
{FOOTER}{FOOTER-}
{SUBSTITUTE: 'LINIAFS'}
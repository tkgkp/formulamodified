:!UTF-8
{TITLE:B. Wydruk oferty II (bmp)}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   POLA.NAZ_WYDR:='Oferta '+exec('str_to_pth','#string',OFE.SYMW)+'.pdf';
   ofer_002:="
      VAR_DEL.delete('PRN','w','na','head');
      VAR_DEL.delete('rozdzial');
      VAR_DEL.delete('ck','ddd');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('oddzial','__wal');
      VAR_DEL.delete('max_T8');
      VAR_DEL.delete('__wal','__cen','__vat');
      VAR_DEL.delete('wydr_naz','wyjscie','wydr_edi','plat','cenab','czywal')";
   ofer_002();

   exec('czytaj','#stalesys',,XINFO); KSTE.get();
   _ok:=exec('ofe_wydr','!lsp_ofe_dofe','ofe');
   VAR_DEL.delete('PRN','w','na');
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   w:=obj_new(1);
   na:=obj_new(2);

   rozdzial:='';
   ck:=ddd:=0;
   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   oddzial:=KSTE.NAZ<>'';

   wyjscie:=1;

   wydr_naz:='ofer_002';
   wydr_edi:={? OFE.WAL=INFO.NAROD || 'OFERA001' || 'OFERB001' ?};

   plat:=cenab:=czywal:=0;

   WYDRUKIL.CHKBOX01:=0;
   WYDRUKIL.RADIOB_1:=OFE.CB='T';
   WYDRUKIL.CHKBOX02:=OFE.WAL<>INFO.NAROD;
   PARAM_W.JEZYK:=null;

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ||
      PARAM_W.JEZYK:=WYDRUKIL.JEZYK
   ?};
   _jezyk_:=PARAM_W.JEZYK;
   _djezyk:=exec('kh_jezyk','tlumaczenia',OFE.KH);
   PARAM_W.JEZYK:={? _djezyk || _djezyk || _jezyk_ ?};
   PARAM_W.JEZYK_KH:={? _djezyk & _djezyk=PARAM_W.JEZYK || 'T' || 'N' ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? _ok=1 & WYDRUKIL.edit()
   ||
      WYDRUKIL.JEZYK:={? _djezyk & PARAM_W.JEZYK=_djezyk || _jezyk_ || PARAM_W.JEZYK ?};
      {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
      {? WYDRUKIL.put()
      || wyjscie:=0;
         plat:=WYDRUKIL.CHKBOX01;
         cenab:=WYDRUKIL.RADIOB_1;
         czywal:=WYDRUKIL.CHKBOX02
      ?}
   ?};
   {? OFE.WAL=INFO.NAROD || czywal:=0 ?};
   {? czywal=1
   || __wal:=OFE.WAL().KOD
   || SLO.cntx_psh();
      _wyn:=exec('bl_nwal','ustawienia'); SLO.prefix(); __wal:={? SLO.seek(_wyn) || SLO.KOD || '' ?};
      SLO.cntx_pop()
   ?};
   head:=PAR_WYDR.HEAD;
   PAR_WYDR.HEAD:=0;
   exec('st_licz_wz','ceny','OFE')
;~~}
{END:
   PAR_WYDR.HEAD:=head;
   ofer_002(); VAR_DEL.delete('ofer_002')
;~~}
{EXIT: wyjscie=1}
{EXIT:
   OFP.cntx_psh();
   OFP.index('OFE_P');
   OFP.prefix(OFE.ref());
   {? ~OFP.first()
   ||
      FUN.info('Oferta nie ma pozycji.'@);
      wyjscie:=1
   ?};
   OFP.cntx_pop();
   wyjscie=1
}
{DEFINEFONT: 'TM=Inter,,M,,0,0'}
{DEFINEFONT: 'TMB=Inter,,N,,0,0'}
{DEFINEFONT: 'TF=Inter,,F,,0,0'}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
   _nag_wc1:=charleft()/2;
   _nag_wc2:=charleft()-_nag_wc1;
   PRN.add("na[1]",_nag_wc1,'');
   PRN.add("na[2]",_nag_wc2,'');
':: tr: Oferta/Nr/z dnia';
   PAR_WYDR.TITLE:=exec('tr','tlumaczenia','4000101')+'\n'+exec('tr','tlumaczenia','1000102')+' '+OFE.SYMW+'\n'+exec('tr','tlumaczenia','1000127')+' '+$OFE.DU;
   prn1:='PRN'
;~~}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_lic.rpi}
{INCLUDE: wsp_strony.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   miejsce i daty wystawienia                               {COMMENT-}
{FONT: exec('font','param_wydr','daty')}{SPACE: exec('space','param_wydr')}
{LINE}
{>{charleft()} PAR_WYDR.MIEJSC+', '+OFE.DU$1}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{FONT: exec('font','param_wydr','head')}{SPACE: exec('space','param_wydr')}
{SUBSTITUTE: 'HEADF'}
{LINE}
{COMMENT}   strony transakcji                                        {COMMENT-}
{ __STRTAB:='OFE'; __STRODB:='OFE.ODB'; __STRLIN:=''; __STRDAT:=OFE.DU;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{COMMENT}   termin waznosci                                          {COMMENT-}
{<{lmt+1}
   }{FONT: exec('font','param_wydr','tw_tr')}{
':: tr: Termin ważności oferty'; exec('tr','tlumaczenia','4000103')+':'
   }{FONT: exec('font','param_wydr','tw_war')}{' '}{$OFE.TW}
{COMMENT}   forma platnosci                                          {COMMENT-}
{IF: OFE.PL<>null & plat=1}
   {
':: tr: Forma płatności:';
   _tlumacz:={? PARAM_W.JEZYK=null || '' || exec('trans_slo','tlumaczenia',OFE.PL,PARAM_W.JEZYK) ?};
   fp_tr:=exec('tr','tlumaczenia','1000121')+' ';
   fp_war:=
      {? PARAM_W.JEZYK<>null & _tlumacz<>''
      || _tlumacz
      || OFE.PL().TR
      ?};
   tp_tr:=tp_war:=wp_tr:=wp_war:=wp_wal:='';
   odstep:=charleft()-(+fp_tr)-(+fp_war)-(+tp_tr)-(+tp_war)-(+wp_tr)-(+wp_war)-(+wp_wal);
   odstep:={? odstep>0 || ' '*odstep || ' ' ?};
   ~~}
   {<{1}
      }{FONT: exec('font','param_wydr','fp_tr')}{fp_tr
      }{FONT: exec('font','param_wydr','fp_war')}{fp_war
      }{FONT: exec('font','param_wydr','fp_ods')}{odstep
      }{FONT: exec('font','param_wydr','tp_tr')}{tp_tr
      }{FONT: exec('font','param_wydr','tp_war')}{tp_war
      }{FONT: exec('font','param_wydr','wp_tr')}{wp_tr
      }{FONT: exec('font','param_wydr','wp_war')}{wp_war
      }{FONT: exec('font','param_wydr','wp_wal')}{wp_wal}
{FI}
{FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}   TABHEAD                                                  {COMMENT-}
{FONT: exec('font','param_wydr','tabhead_poz')}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{FONT: exec('font','param_wydr','header')}{SPACE: exec('space','param_wydr')}
{HEADER}
{SUBSTITUTE: 'HEADF'}
{FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{IF: _a:=exec('get_par','#parametr',34); _a='Z' | _a='A'}
   {FOOTER}
   {SUBSTITUTE: 'FOOT_LIC'}
   {~~}
   {~~}
   {~~}
   {FOOTER-}
{ELSE}
   {FOOTER}{FOOTER-}
{FI}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{ rsep:=1;w[1]:=0;~~}
{FOR:'OFP'}{INDEX: 'OFE_P'}{PREFIX: OFE.ref}
{DO}
{
ck:=0;
_szer_bez_sep:=charleft()-2-3-2;
kol1_szer:=_szer_bez_sep%2;
kol1_end:=2+kol1_szer+1;
kol2_szer:=_szer_bez_sep-kol1_szer;
kol2_end:=charleft()-1;
APARAM.OKROVAT1:='';
~~}
{ENTIRE}
{'┌─'}{'─'*kol1_szer}{'─┬─'}{'─'*kol2_szer}{'─┐'}
{'│ '}{w[1]+=1}{'.'}{<{kol1_end}}{' │ '}{IF: OFP.M().RYS<>null}{IMAGE: OFP.M().RYS;kol2_szer;18}{FI}{<{kol2_end}}{' │'}
{'│ '}{
':: tr: Nazwa materiału lub usługi'; _txt:=exec('tr','tlumaczenia','1000181')+':'; ident:=+_txt+1; _txt}{<{ident}}{
':: tr: materiał';
_tlumacz:='';
{? PARAM_W.JEZYK<>null
|| _tlumacz:=exec('trans_m','tlumaczenia',OFP.M,PARAM_W.JEZYK)
?};
{? _tlumacz='' || _tlumacz:=OFP.M().N ?};
STR.split(_tlumacz);STR.line(25)}{<{kol1_end}}{' │ '}{<{kol2_end}}{' │'}
{ck:=0;~~}
{WHILE: STR.next()<>0 | ck<13}
{DO}
   {IF: lineleft = 1}
      {SUBSTITUTE: 'LINIAF'}
   {FI}
{'│ '}{<{ident} STR.line(24)}{<{kol1_end}}{' │ '}{<{kol2_end}}{' │'}
{ck+=1;~~}
{TOTAL}
   {   VAR_DEL.delete('ident'); ~~}
{OD}
{'│ '}{<3
':: tr: Ilość';
_tr_il:=exec('tr','tlumaczenia','1000183');
':: tr: jm';
_tr_jm:='';
{? PARAM_W.JEZYK<>null
|| _tr_jm:=exec('trans_jm','tlumaczenia',OFP.M().J,PARAM_W.JEZYK)
?};
{? _tr_jm='' || _tr_jm:=OFP.M().J().KOD ?};
_tr_il+': '+form(OFP.IL,,OFP.M().DOKL)+' '+_tr_jm}{<{kol1_end}}{' │ '}{<{kol2_end}}{' │'}
{
__cen:={? czywal=1 || OFP.CWAL || OFP.C ?};
__vat:=#((OFP.SV().KOD*'%')+OFP.SV().KOD-1)/100;
{? OFE.CB='T' || __cen:=(__cen/(1+__vat))$OFP.M().DOKL_S ?};
{? cenab=1 || __cen:=__cen+(__cen*__vat) ?}
;~~}
{IF: exec('czyrabp','ceny',TAZ.RAB_TYP)}
   {IF: cenab=1}
   {<1}{'│ '
      }{<3
':: tr: Cena brutto'; exec('tr','tlumaczenia','1000193')+': '
      +form(__cen-(__cen*exec('rab_proc','ceny',OFP.RAB,TAZ.RAB,TAZ.RAB_TYP))/100,,OFP.M().DOKL_S)+
      ' '+__wal}{<{kol1_end}}{' │ '}{<{kol2_end}}{' │'}
   {ELSE}
   {<1}{'│ '
      }{<3
':: tr: Cena netto'; exec('tr','tlumaczenia','1000188')+': '
      +form(__cen-(__cen*exec('rab_proc','ceny',OFP.RAB,TAZ.RAB,TAZ.RAB_TYP))/100,,OFP.M().DOKL_S)+
      ' '+__wal}{<{kol1_end}}{' │ '}{<{kol2_end}}{' │'}
   {FI}
{ELSE}
   {
      _cena:=__cen;
      _dokl_c:=OFP.M().DOKL_S;
      _kwil:=OFP.IL;
      _nagwsp:=-TAZ.RAB/100;
      _kwpoz:=-
         {? cenab & OFE.CB='N' || (OFP.RABK*(1+__vat))$2
         |? cenab=0 & OFE.CB='T' || (OFP.RABK/(1+__vat))$2
         || OFP.RABK
         ?};
      kwrabj:=
         {? TAZ.RAB_TYP='W'
         || {? _kwil || (_kwpoz/_kwil)$_dokl_c ?}+(_nagwsp*_cena)$_dokl_c
         || _kwpoz+(_nagwsp*_cena)$_dokl_c
         ?}
   ;~~}
   {IF: cenab=1}
   {<1}{'│ '}{<3
':: tr: Cena brutto'; exec('tr','tlumaczenia','1000193')+': '
      +form(__cen+kwrabj,,OFP.M().DOKL_S)+' '+__wal}{<{kol1_end}}{' │ '}{<{kol2_end}}{' │'}
   {ELSE}
   {<1}{'│ '}{<3
':: tr: Cena netto'; exec('tr','tlumaczenia','1000188')+': '
      +form(__cen+kwrabj,,OFP.M().DOKL_S)+' '+__wal}{<{kol1_end}}{' │ '}{<{kol2_end}}{' │'}
   {FI}
{FI}
{'│ '}{<{kol1_end}}{' │ '}{<{kol2_end}}{' │'}
{'└─'}{'─'*kol1_szer}{'─┴─'}{'─'*kol2_szer}{'─┘'}
{ENTIRE-}
{  ddd:=lineleft; ~~}
{IF: lineleft<8}
{LINE: ddd}
{FI}
{OD}
{COMMENT}   uwagi                                                    {COMMENT-}
{FONT: exec('font','param_wydr','uwagi')}
{ __memo:=OFE.memo_get('r','OPIS'); text:='';~~}
{IF: __memo.is_open}
   {WHILE: text:=fread(__memo); text<>'\n'}
   {DO}
      {<{lmt+1}}{
      max_T8:=charleft;
      STR.split(text);
      Text:='';
      sep:=0
      ;~~}
      {WHILE: STR.next()}
      {DO}
         {FIRST}{LINE}{FIRST-}
         { word:=STR.get_word(); sep+=1;~~}
         {IF: +Text+(+word)>=(max_T8-4-8)}
            {<{lmt+1}}{JUSTIFY}{<{lmt+1}>{max_T8-8}Text}
            { Text:=word; sep:=1;~~}
         {ELSE}
            { Text+={? sep>1 || ' ' || '' ?}+word;~~}
         {FI}
      {OD}
      {IF: +Text}
         {<{lmt+1}}{Text}
      {FI}
   {OD}
   {CENTER}
{FI}
{ VAR_DEL.delete('__memo','text','Text');~~}
{COMMENT}   podpisy                                                  {COMMENT-}
{LINE: 3}
{FONT: exec('font','param_wydr','podpisy')}{SPACE: exec('space','param_wydr')}
{IF: OFE.HAN<>null}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,OFE.HAN().NAZ);
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{FI}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=30*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:='Ofertę sporządził';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}

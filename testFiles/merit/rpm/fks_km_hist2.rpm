:!UTF-8
{TITLE:B. Zestawienie zbiorcze planów umów}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0}
{BEGIN: w:=obj_new(11); {! _i:=1..11 |! w[_i]:='' !}; petla:=0;
        PRN:=obj_new(@.CLASS.PRINT,15); PRN.sl_frame();
        ROZNE.STAT_KOM:=0;
        {? ROZRACH.KONTR=null
        || ROZNE.win_edit('KOMP_HIS');
           ROZNE.KOMP_ZAK:=0; ROZNE.KOMP_KH:=null
        || ROZNE.win_edit('KOMP_HI2');
           ROZNE.KOMP_ZAK:=1; ROZNE.KOMP_KH:=ROZRACH.KONTR
        ?};
        ROZNE.KOMP_DOD:=ROZNE.KOMP_DDO:=date(0,0,0);
        ROZNE.UMOWA:=null; ROZNE.KOMP_OKR:=0;
        KH.win_dict('WERHOME2');
        sum_1:=sum_2:=sum_3:=sum_4:=lm:=0; mdc:=''; font:='J'; pier_poz:=ilelinii:=0
      }
{END: VAR_DEL.delete('w','PRN','petla','sum_1','sum_2','sum_3','sum_4','lm','mdc','font','pier_poz','ilelinii')}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_km_hist2.rpm
Utworzony:   2001-12-14 [AMK] Artur Makos
==================================================
Zawartosc:  Zestawienie zbiorcze planow umow
{COMMENT-}
{EXIT: ~ROZNE.edit("{? ROZRACH.KONTR=null & ROZNE.KOMP_ZAK=1 & ROZNE.KOMP_KH=null
                    || FUN.info('Proszę wypełnić pole kontrahent.'@); 'KOMP_KH'
                    || {? ROZNE.KOMP_OKR & ROZNE.KOMP_DDO<ROZNE.KOMP_DOD
                       || FUN.info('Wpisz poprawnie zakres dat.'@); 'KOMP_DOD'
                       || 1
                       ?}
                    ?}")}
{ PRN.add("w[1]",20,'     Symbol umowy'@);
  PRN.add("w[10]",32,13*' '+'Nabywca'@);
  PRN.add("w[2]",10,'Data planu'@);
  {? ROZNE.KOMP_ZAK<>1 || PRN.add("w[3]",35,11*' '+'Kontrahent'@) ?};
  {? FINFO.NAROD<>SSTALE.WAL || PRN.add("w[11]",10,'Kurs wal.'@) ?};
  PRN.add("w[5]",22,'  Symbol rozrachunku'@);
  PRN.add("w[6]",10,'T. płatn.'@);
  PRN.add("w[7]",14,'   Saldo Wn'@,1);
  {? SER_KOR.TYP='S'
     || PRN.add("w[8]",14,'Cena sprzedaży'@,1)
     || PRN.add("w[8]",14,'Finansowanie '@,1)
  ?};
  PRN.add("w[9]",14,'    Koszt'@,1);
  {? SER_KOR.TYP='F'|| PRN.add("w[11]",15,'Wartość regresu'@,1) ?};
~~}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_pw3.rpi}
{INCLUDE: wsp_lic.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{<1 PRN1.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{ {? PAR_NAG.TYP='F'
  || PAR_WYDR.TITLE:='ZESTAWIENIE ZBIORCZE PLANÓW UMÓW FAKTORINGU'@
  || PAR_WYDR.TITLE:='ZESTAWIENIE ZBIORCZE PLANÓW UMÓW SPRZEDAŻY WIERZYTELNOŚCI'@
?};~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Rok obrotowy: %1'@[SSTALE.AR().NAZ]
}{<60 {? ~ROZNE.KOMP_ZAK || 'Zakres umów: dla wszystkich kontrahentów'@ || 'Zakres umów: dla kontrahenta - %1  %2'@[ROZNE.KOMP_KH().KOD,ROZNE.KOMP_KH().NAZ] ?}}
{<5{? OPERATOR.DEPT ||  'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD] ||  'Jednostka księgowa: wszystkie jednostki księgowe'@ ?}
}{IF: ROZNE.UMOWA<>null}{<60'Wybrana umowa: %1'@[ROZNE.UMOWA().SYM]}{FI}
{<5 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]
}{<60 {? ROZNE.STAT_KOM=0 || 'Status umów: wszystkie'@
      |?  ROZNE.STAT_KOM=1 || 'Status umów: niezaakceptowane'@
      |?  ROZNE.STAT_KOM=2 || 'Status umów: zaakceptowane'@
      || 'Status umów: zadekretowane'@
      ?}}
{IF: ROZNE.KOMP_OKR}{<60 'Przedział dat: od %1 do %2'@[$ROZNE.KOMP_DOD,$ROZNE.KOMP_DDO]}{FI}
{FI}{FONT: 'R'}{SPACE: 'C'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{IF: _a:=PAR_SKID.get(34); _a='Z' | _a='A'}
{FOOTER}
{SUBSTITUTE: 'FOOT_LIC'}
{~~}
{~~}
{~~}
{FOOTER-}
{FI}
{COMMENT}-----------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ {? ROZNE.STAT_KOM=0
  || {? OPERATOR.DEPT
     || PAR_NAG.index('KONTO_O');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,OPERATOR.DEPT,'')
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,OPERATOR.DEPT,'',ROZNE.KOMP_KH().KOD)
        ?}
     || PAR_NAG.index('KONTO');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,'')
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,'',ROZNE.KOMP_KH().KOD)
        ?}
     ?}
  |? ROZNE.STAT_KOM=1 | ROZNE.STAT_KOM=2
  || _stat:={? ROZNE.STAT_KOM=1 || 'N' || 'T' ?};
     {? OPERATOR.DEPT<>null
     || PAR_NAG.index('ZNACZ_O3');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,OPERATOR.DEPT,_stat,'')
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,OPERATOR.DEPT,_stat,'',ROZNE.KOMP_KH().KOD)
        ?}
     || PAR_NAG.index('ZNACZ3');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,_stat,'')
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,_stat,'',ROZNE.KOMP_KH().KOD)
        ?}
     ?}
  || {? OPERATOR.DEPT<>null
     || PAR_NAG.index('ZNACZ_O1');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,OPERATOR.DEPT,'T','T','T','')
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,OPERATOR.DEPT,'T','T','T','',ROZNE.KOMP_KH().KOD)
        ?}
     || PAR_NAG.index('ZNACZ1');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,'T','T','T','')
        || PAR_NAG.prefix(SSTALE.WAL,SER_KOR.TYP,'T','T','T','',ROZNE.KOMP_KH().KOD)
        ?}
     ?}
  ?};
  petla:=PAR_NAG.first();~~}
{WHILE: petla}
{DO}
{IF: (~ROZNE.KOMP_OKR |
       (ROZNE.KOMP_OKR & PAR_NAG.DATA>=ROZNE.KOMP_DOD & PAR_NAG.DATA<=ROZNE.KOMP_DDO)) &
     (ROZNE.UMOWA=null | PAR_NAG.PAR_UM=ROZNE.UMOWA)}
{FOR: 'PAR_POZ'}
{INDEX: 'PAR_POZ'}
{PREFIX: PAR_NAG.ref}
{DO}
{FIRST}
{ {! _i:=1..11 |! w[_i]:='' !}; sum_1:=sum_2:=sum_3:=sum_4:=0; pier_poz:=1;
  w[1]:=PAR_NAG.PAR_UM().SYM;  w[2]:=$PAR_NAG.DATA;
  w[10]:=PAR_NAG.PAR_UM().SLO().KOD+' '+PAR_NAG.PAR_UM().SLO().TR;
  ile_linii:=exec('ilelinii','rozrach',w[10],32);
  w[3]:=PAR_NAG.KH1().SKR+'  '+KH.NAZ;
  _ile_l:=exec('ilelinii','rozrach',w[3],35);
  {? _ile_l>ilelinii || ilelinii:=_ile_l ?};
  w[4]:=PAR_NAG.SER_DEF().KOD;  w[11]:=form(PAR_NAG.KURS,10,6,' ,');
~~}
{FIRST-}
{ w[5]:=PAR_POZ.OP;  w[6]:=$PAR_POZ.TZ;
  w[11]:=form(PAR_POZ.REGRES,,2,' ,'); sum_1+=PAR_POZ.REGRES$2;
  w[7]:=form(PAR_POZ.SAL,,2,' ,');     sum_2+=PAR_POZ.SAL$2;
  w[8]:=form(PAR_POZ.KWOTA,,2,' ,');   sum_3+=PAR_POZ.KWOTA$2;
  w[9]:=form((PAR_POZ.SAL-PAR_POZ.KWOTA)$2,,2,' ,'); sum_4+=(PAR_POZ.SAL-PAR_POZ.KWOTA)$2;
~~}
{IF: ((~pier_poz | ROZNE.KOMP_ZAK=1 | ilelinii=1) & (lineleft()=1 | lineleft()=2)) |
     (pier_poz=1 & lineleft<>0 & (lineleft()<(2+ile_linii)))}
{PRN.lbar()}{IF: lineleft()<>0}{PAGE}{FI}
{FI}
{ENTIRE}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{ {! _i:=1..11 |! w[_i]:='' !}; pier_poz:=1;~~}
{OD}
{ w[11]:=form(sum_1,,2,' ,');
  w[7]:=form(sum_2,,2,' ,');
  w[8]:=form(sum_3,,2,' ,');
  w[9]:=form(sum_4,,2,' ,'); w[1]:='RAZEM UMOWA'@;~~}
{IF: (lineleft()=1 | lineleft()=2)}{PRN.lbar()}{IF: lineleft()<>0}{PAGE}{FI}{FI}
{ENTIRE}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{ {! _i:=1..11 |! w[_i]:='' !};~~}
{FI}
{ petla:=PAR_NAG.next();~~}
{OD}
{PRN.lbar()}
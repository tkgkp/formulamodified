:!UTF-8
{TITLE:Wyróżnienia i kary}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   P.cntx_psh();
   KART_DEF.cntx_psh();
   tresc:='pkd_wyroznieniakary';
   PAR_WYDR.TITLE:='Wyróżnienia i kary'@;
   par:=obj_new(6);
   par[5]:=0;
   _upr:=exec('uprawnieniawrap','pkd','wyróżnieniach i karach'@,'PKD_DOS_PPWK','PKD_DOS_PRWK');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[3]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,
         "  {? cur_tab().OD<>#0 & cur_tab().DO<>#0
            || {? cur_tab().OD>cur_tab().DO
               || FUN.emsg('Data końca okresu nie może być mniejsza od daty początkowej'@); 'OD'
               || 1
               ?}
            || 1
            ?}
         ",
         date(,,1),'Od daty:'@,,
         date(,,0),'Do daty:'@,,
         'Które z wyróżnień i kar uwzględniać na wydruku?'@,'Wszystkie'@,'Z wybranego okresu'@,0,1
      );

      {? par[3].size()
      || par[2]:=tab_tmp(,'REF','INTEGER','Osoba');

         par[1]:=obj_new(@.CLASS.PRINT,{? ~rep_export() || 4 || 7 ?});
         {? rep_export()
         || par[1].add("KART_DOD.OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("KART_DOD.OSOBA().PIERWSZE+' '+KART_DOD.OSOBA().DRUGIE",41,'Imiona'@);
            par[1].add("KART_DOD.OSOBA().PESEL",11,'PESEL'@)
         ?};
         par[1].add("KART_DOD.KT",9,'Kategoria'@);
         par[1].add("KART_DOD.OD$1",10,'Data'@);
         par[1].add("KART_DOD.OPIS",40,'Opis'@);
         par[1].add("exec('memo2print','#string',KART_DOD,'UWAGI')",60,'Adnotacje i uwagi'@);
         par[1].s_frame();

         prn1:='par[1]';
         par[4]:=par[6]:=lmt:=rsep:=0;
         par[5]:=vp:=1;

         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   KART_DEF.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc')
}
{COMMENT}OLD: wodznacz.rpm{COMMENT-}
{COMMENT}OLD: std_osop.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[5]}
{EXIT: ~params_get().P.first()}
{EXIT: KART_DEF.index('SYMBOL'); KART_DEF.prefix(); ~KART_DEF.find_key('WYR_KAR')}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Data udzielenia: od %1 do %2'@[par[3].OD$1,par[3].DO$1]}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W10'}
{SPACE: 'B'}
{IF: ~rep_export()}
   {<{int((136/160)*lmt)+1}}  {form(par[6]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{ par[4]:=1; ~~ }
{MACRO-}
{MACRO: 'odz_dane'}
{IF: ~par[4]}
   {SUBSTITUTE: 'pracownik'}
   {REP: par[1].fhbar(lmt)}
{FI}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{COMMENT}

   Przeglądaj kartotekę wyróżnień i kar w podanym lub pełnym zakresie dat.
   Pełny zakres - wszystkie wyróżnienia i kary, ograniczony zakres - od daty
   do daty, od 'początku' kartoteki do daty lub od daty do 'końca'.

{COMMENT-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[2].add();
      par[4]:=0; ~~
   }
   {ENTIRE}
   {REP: '{FOR: KART_DOD}{INDEX: \'KART_DOD\'}'+
         {? ~par[3].ASK
         || '{PREFIX: exec(\'ref_firma\',\'ustawienia\'),KART_DEF.ref(),P.OSOBA}'
         || '{FROM: exec(\'ref_firma\',\'ustawienia\'),KART_DEF.ref(),P.OSOBA'+
               {? par[3].OD=#0 || '' || ',par[3].OD' ?}+'}'+
            '{TO: exec(\'ref_firma\',\'ustawienia\'),KART_DEF.ref(),P.OSOBA'+
               {? par[3].DO=#0 || '' || ',par[3].DO' ?}+'}'
         ?}+'{DO}{SUBSTITUTE: \'odz_dane\'}{OD}'}
   {IF: par[4]}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}

:!UTF-8
{  'W E R S J A  A N A L I T Y C Z N A';
   sql_war:='';
   {? FINFO.NAROD<>SSTALE.WAL || sql_war:='SLOW.KOD=\''+SSTALE.WAL().KOD+'\' ' ?};
   {? UNPAR.PSLO || sql_war+='and OP_PROJ.SLO_PROJ=\''+$UNPAR.PSLO+'\' ' ?};
   {? ROZNE.KH_WYB<>null || sql_war+='and OP.KH=:_a ' ?};
   {? OPERATOR.DEPT || sql_war+='and ODD.OD=\':_b\' ' ?};
   {? UNPAR.P3=3 || sql_war+='and OP.TZ<to_date(:_c) ' ?};
   {? UNPAR.P15
   || sql_war+='and OP.DO between to_date(:_d) and '+
      {? UNPAR.P34 & UNPAR.P14>UNPAR.P42
      || 'to_date(:_f) '
      || 'to_date(:_e) '
      ?}
   |? UNPAR.P34
   || sql_war+='and OP.DO<=to_date(:_f) '
   ?};
   {? KONTO.K1+1='?'
   || {! |? KONTO.K1:=KONTO.K1-1; KONTO.K1+1='?' !}; {? +KONTO.K1<35 || KONTO.K1+='%' ?}
   ?};
   {! _i:=1..2 |!
      _f:='{? KONTO.K'+$_i+'<>\'\' || {? KONTO.K'+$_i+'=35*\'?\' || KONTO.K'+$_i+':=\'%\'?};'
         +'KONTO.K'+$_i+':=STR.gsub(KONTO.K'+$_i+',\'?\',\'_\');'
         +'KONTO.K'+$_i+':=STR.gsub(KONTO.K'+$_i+',\'*\',\'%\');'
         +'sql_war+=\'and OP.AN like \\\'\'+KONTO.K'+$_i+'+\'%\\\'\' ?}';
      ($(_f))()
   !};
   {? UNPAR.P1=3
   || STR.gsub(KONTO.K3,'?','_'); STR.gsub(KONTO.K3,'*','%');
      STR.gsub(KONTO.K4,'?','_'); STR.gsub(KONTO.K4,'*','%');
      sql_war+='and OP.AN between \''+KONTO.K3+'\' and \''+KONTO.K4+%255+'\' '
   |? UNPAR.P1=4
   || _okn:=KS.win_sel('?');
      TMPSIK.cntx_psh();
      exec('initmpks','dok_fks_zest');
      KS.select();
      KS.win_sel(_okn);
      {? TMPSIK.first()
      || sql_war+='and (OP.AN like \'' + TMPSIK.T2 + '%\' ';
         {!|? TMPSIK.next()
         |! sql_war+='or OP.AN like \'' + TMPSIK.T2 + '%\' '
         !};
         sql_war+=') '
      || sql_war+='and (OP.AN like \'%\')';ext_spr:=1
      ?};
      TMPSIK.cntx_pop()
   |? UNPAR.P1=5
   || _okn:=AN.win_sel('?');
      TMPSIK.cntx_psh();
      exec('initmpan','dok_fks_zest');
      AN.select();
      AN.win_sel(_okn);
      {? TMPSIK.first()
      || sql_war+='and (OP.AN like \'' + TMPSIK.T2 + '%\' ';
         {!|? TMPSIK.next()
         |! sql_war+='or OP.AN like \'' + TMPSIK.T2 + '%\' '
         !};
         sql_war+=') '
      || sql_war+='and (OP.AN like \'%\')';ext_spr:=1
      ?};
      TMPSIK.cntx_pop()
   |? UNPAR.P1=6
   || KON_SCH.cntx_psh();
      KON_SCH.index('IND_LP');
      KON_SCH.prefix(ROZNE.KON_WYDR);
      {? KON_SCH.first()
      || {? KON_SCH.MZ = 'M'
         || {! |?
               {? KON_SCH.MASKA+1='?' || KON_SCH.MASKA:=KON_SCH.MASKA-1 ?};
               KON_SCH.MASKA+1='?'
            !};
            KON_SCH.MASKA:=KON_SCH.MASKA+'%';
            KON_SCH.MASKA:=STR.gsub( KON_SCH.MASKA,'?','_');
            sql_war+='and (OP.AN like \''+KON_SCH.MASKA+'\''
         || sql_war+='and (OP.AN between \''+KON_SCH.ZOD+'\' and \''+KON_SCH.ZDO+%255+'\''
         ?};
         {!|? KON_SCH.next()
         |! {? KON_SCH.MZ = 'M'
            || {! |?
                  {? KON_SCH.MASKA+1='?' || KON_SCH.MASKA:=KON_SCH.MASKA-1 ?};
                  KON_SCH.MASKA+1='?'
               !};
               KON_SCH.MASKA:=KON_SCH.MASKA+'%';
               KON_SCH.MASKA:=STR.gsub( KON_SCH.MASKA,'?','_');
               sql_war+=' or OP.AN like \''+KON_SCH.MASKA+'\''
            || sql_war+=' or OP.AN between \''+KON_SCH.ZOD+'\' and \''+KON_SCH.ZDO+%255+'\''
            ?}
         !};
         sql_war+=') '
      || sql_war+='and (OP.AN like \'%\')'
      ?};
      KON_SCH.cntx_pop()
   ?};
   {? PAR_WYDR.T_ROZ<>'' || sql_war+='and OP.TYP like \''+PAR_WYDR.T_ROZ+'%\' ' ?};
   {? PAR_WYDR.SP<>'W'
   || sql_war+='and OP.SP=\''+PAR_WYDR.SP+'\' '
   ?};
   {? +sql_war & 3+sql_war='and' || sql_war:=4-sql_war ?};
   {? +sql_war & sql_war+1<>' ' || sql_war+=' ' ?};
   {? +sql_war || sql_war:='where '+sql_war ?};
   TAB_SQL:=sql(
      {? UNPAR.PSLO
      || 'select /*+MASK_FILTER(ZAP_PROJ,\'rozprz'+SSTALE.AR().KOD+'\')*/ '
      || 'select /*+MASK_FILTER(ZAP_OP,\'rozzap'+SSTALE.AR().KOD+'\')*/ '
      ?}
      +'OP.AN,OP.SYM,OP.SYM_ROK,SLOW.KOD as WAL,'+
      {? UNPAR.PSLO
      ||  'ZAP_PROJ.WN,ZAP_PROJ.MA,ZAP_PROJ.DATA,REJ.KOD as REJ_KOD,ZAP_PROJ.NR,ZAP_PROJ.NR_POZ as POZ,'
         +'ZAP_PROJ.DKS,ZAP_PROJ.NK,ZAP_PROJ.KURS,OP.TYP,'
         +'PAR_NAG.TYP as TYPP,ZAP_PROJ.POZ as POZDOK,OP.TZ,OP.DO,SLO.KOD, ZAP_PROJ.WAL2 as WALR,'
      ||  'ZAP_OP.WN,ZAP_OP.MA,ZAP_OP.DATA,REJ.KOD as REJ_KOD,ZAP_OP.NR,ZAP_OP.POZ,'
         +'ZAP_OP.DKS,ZAP_OP.NK,ZAP_OP.KURS,ZAP_OP.PAR_POZ,OP.TYP,'
         +'PAR_NAG.TYP as TYPP,ZAP_OP.POZDOK,OP.TZ,OP.DO,SLO.KOD, ZAP_OP.WAL2 as WALR,'
      ?}
      +'OP.KH,KH.KOD as KONTR,ODD.OD,ODDR.OD as ODR,'
      +'KH.NAZ as KONTNAZ '+
      {? PAR_WYDR.NO_SPVAT<>'W'
      || {? UNPAR.PSLO
         || ',ZAP_PROJ.SP_V_WN as VWN, ZAP_PROJ.SP_V_MA as VMA '
         || ',ZAP_OP.SP_V_WN as VWN, ZAP_OP.SP_V_MA as VMA '
         ?}
      || ''
      ?}+
      {? gzwal=3
      || {? UNPAR.PSLO
         || ',ZAP_PROJ.SP_V_WN as WN2, ZAP_PROJ.SP_V_MA as MA2 '
         || ',ZAP_OP.SP_V_WN as WN2, ZAP_OP.SP_V_MA as MA2 '
         ?}
      |? UNPAR.PSLO
      || ', ZAP_PROJ.WN2,ZAP_PROJ.MA2 '
      || ', ZAP_OP.WN2,ZAP_OP.MA2 '
      ?}+
      {? UNPAR.PSLO
      || 'from @ZAP_PROJ join OP_PROJ using(ZAP_PROJ.OP_PROJ,OP_PROJ.REFERENCE) join OP using(OP_PROJ.OP,OP.REFERENCE) '
      || 'from @ZAP_OP join OP using(ZAP_OP.OP,OP.REFERENCE) '
      ?}
      +'join ODD using(OP.ODD,ODD.REFERENCE) '
      +{? UNPAR.PSLO || 'join REJ using(ZAP_PROJ.REJ,REJ.REFERENCE) ' || 'join REJ using(ZAP_OP.REJ,REJ.REFERENCE) ' ?}
      +'join ODD as ODDR using(REJ.ODD,ODDR.REFERENCE) '
      +'left join SLO using('+{? UNPAR.PSLO || 'ZAP_PROJ' || 'ZAP_OP' ?}+'.WAL2,SLO.REFERENCE) '
      +'join SLO as SLOW using(OP.WAL,SLOW.REFERENCE) '+
      {? UNPAR.PSLO
      || 'left join @PAR_POZ using(ZAP_PROJ.PAR_POZ,PAR_POZ.REFERENCE) '
         +'left join @PAR_NAG using(PAR_POZ.PAR_NAG,PAR_NAG.REFERENCE) '
      || 'left join @PAR_POZ using(ZAP_OP.PAR_POZ,PAR_POZ.REFERENCE) '
         +'left join @PAR_NAG using(PAR_POZ.PAR_NAG,PAR_NAG.REFERENCE) '
      ?}
      +'left join KH using(OP.KH,KH.REFERENCE) '
      +sql_war
      +'order by WAL,'+{? UNPAR.P9=1 || '' || 'KONTR,' ?}+'OP.AN,OP.TZ,OP.SYM,OP.SYM_ROK,ODD.OD',
      ROZNE.KH_WYB,OPERATOR.DEPT().OD,gdzap,UNPAR.P13,UNPAR.P14,UNPAR.P42); ~~}
{EXIT: type_of(TAB_SQL)<100 | ext_spr}
{  {? UNPAR.P3=4 | UNPAR.P3=1 | UNPAR.P2=1 | PAR_WYDR.NO_SPVAT<>'W' || exec('rwsynt_0','!fks_roz_zazr') ?};
   ind1:=TAB_SQL.ndx_tmp(,1,'WAL',,,'AN',,,'SYM',,,'SYM',,,'SYM_ROK',,,'OD',,,'DATA',,);
   {? ~OPERATOR.DEPT || _vo:=9 || _vo:=0 ?};
   _zwl:={? UNPAR.P3=2 | UNPAR.P3=3 || 0 || 9 ?};
   {? gzwal=3
   || {? PAR_WYDR.T_ROZ='' | +(|PAR_WYDR.T_ROZ)=1 || PRN21.add("w[1]",171+40+_vo-_zwl) || PRN21.add("w[1]",166+40+_vo-_zwl) ?}
   |? gzwal
   || {? PAR_WYDR.T_ROZ='' | +(|PAR_WYDR.T_ROZ)=1 || PRN21.add("w[1]",186+40+_vo-_zwl) || PRN21.add("w[1]",181+40+_vo-_zwl) ?}
   || {? PAR_WYDR.T_ROZ='' | +(|PAR_WYDR.T_ROZ)=1 || PRN21.add("w[1]",141+40+_vo-_zwl) || PRN21.add("w[1]",136+40+_vo-_zwl) ?}
   ?};
   {? ~OPERATOR.DEPT
   || PRN22.add("w[18]",8,'Jed. ks.')
   ?};
   {? rep_export()
   || PRN22.add("TAB_SQL.AN",35,'Konto')
   ?};
   PRN22.add("w[1]",35,'Rozrachunek');
   PRN22.add("w[2]",10,'Termin płatności');
   {? _zwl=0
   || PRN22.add("w[17]",8,'Dni po'+{? rep_export() || ' ' || ' #' ?}+'terminie',1,'#')
   ?};
{? PAR_WYDR.T_ROZ='' | +(|PAR_WYDR.T_ROZ)=1 || PRN22.add("w[15]",4,'Typ') ?};
   PRN22.add("w[3]",10,'Data zapisu');
   PRN22.add("w[4]",17,'Jedn. ks./Rejestr');
   PRN22.add("w[5]",6,'Nr',1);
   PRN22.add("w[6]",5,'Poz.',1);
   PRN22.add("w[7]",10,'Data księg.');
   PRN22.add("w[8]",35,'Dowód księgowy');
   PRN22.add("w[16]",1,'P');
   PRN22.add("w[9]",14,'Kwota Wn',1);
   PRN22.add("w[10]",14,'Kwota Ma',1);
   {? gzwal=3
   || PRN22.add("w[13]",14,'Kwota Wn VAT',1,'#');
      PRN22.add("w[14]",14,'Kwota Ma VAT',1,'#')
   |? gzwal
   || PRN22.add("w[11]",4,'Wal.');
      PRN22.add("w[12]",9,'Kurs',1);
      PRN22.add("w[13]",14,'Kwota Wn'+{? rep_export() || ' ' || ' #' ?}+'w wal.',1,'#');
      PRN22.add("w[14]",14,'Kwota Ma'+{? rep_export() || ' ' || ' #' ?}+'w wal.',1,'#')
   ?};
   {? PAR_WYDR.T_ROZ='' | +(|PAR_WYDR.T_ROZ)=1
   || PRN23.add("w[1]",111+40+_vo-_zwl)
   || PRN23.add("w[1]",106+40+_vo-_zwl)
   ?};
   PRN23.add("w[2]",14,,1);
   PRN23.add("w[3]",14,,1);
   {? gzwal=3
   || PRN23.add("w[6]",14,,1);
      PRN23.add("w[7]",14,,1)
   |? gzwal
   || PRN23.add("w[4]",4);
      PRN23.add("w[5]",9);
      PRN23.add("w[6]",14,,1);
      PRN23.add("w[7]",14,,1)
   ?};
   {? PAR_WYDR.T_ROZ='' | +(|PAR_WYDR.T_ROZ)=1
   || PRN2F.add("wf[1]",111+40+_vo-_zwl)
   || PRN2F.add("wf[1]",106+40+_vo-_zwl)
   ?};
   PRN2F.add("wf[2]",14,,1);
   PRN2F.add("wf[3]",14,,1);
   {? gzwal=3
   || PRN2F.add("wf[6]",14,,1);
      PRN2F.add("wf[7]",14,,1)
   |? gzwal
   || PRN2F.add("wf[4]",4);
      PRN2F.add("wf[5]",9);
      PRN2F.add("wf[6]",14,,1);
      PRN2F.add("wf[7]",14,,1)
   ?};
   {! _a:=1..16 |! w[_a]:='' !}; {! _a:=1..7 |! wf[_a]:='' !};
   PAR_WYDR.TITLE:=
   {? UNPAR.P34
   || 'Zestawienie '+{? 1+PAR_WYDR.T_ROZ='N'||'należności'||'zobowiązań'?}+' z tyt. dostaw, robót i usług na dzień bilansowy '+
      UNPAR.P42$3+'\nnie zapłacone do dnia: '+gdzap$3
   || 'ZESTAWIENIE ROZRACHUNKÓW: '+
      {? UNPAR.P2=1 || 'STAN NA '+$gdzap || {? UNPAR.P3=1 || 'ROZLICZONE' ||
      'Z ZAPISAMI' ?}+' OD '+$UNPAR.P5+' DO '+$UNPAR.P6 ?}
   ?};
   prn1:='PRN22'; prn2:='PRN23'; ~~}
{COMMENT}-----------------------------------SIGNATURE------------------------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}-----------------------------------MACRO-LINIA23--------------------------------{COMMENT-}
{MACRO: 'LINIA23'}
{PRN23.ini_line()}
{WHILE: PRN23.next()}
{DO}
{REP: PRN23.fline(lmt)}
{OD}
{MACRO-}
{COMMENT}-----------------------------------MACRO-LINIA2F--------------------------------{COMMENT-}
{MACRO: 'LINIA2F'}
{PRN2F.ini_line()}
{WHILE: PRN2F.next()}
{DO}
{REP: PRN2F.fline(lmt)}
{OD}
{MACRO-}
{COMMENT}-----------------------------------MACRO-G1-------------------------------------{COMMENT-}
{MACRO: 'G1'}
{IF: lineleft()<3 & lineleft()<>0}{PAGE}{  vp:=prn:=1; ~~}{FI}
{IF: prn | s=2}
{ prn1:='PRN22'; prn2:='PRN21'; ~~}
{ELIF: s=3}
{ prn1:='PRN23'; prn2:='PRN21'; ~~}
{FI}
{ rsep:=1; fun:="prn:=0; s:=1; rsep:=PAR_WYDR.RSEP"; ~~}
{SUBSTITUTE: 'LINIA02'}
{MACRO-}
{COMMENT}-----------------------------------MACRO-G2-------------------------------------{COMMENT-}
{MACRO: 'G2'}
{IF: lineleft()<3 & lineleft()<>0}{PAGE}{  vp:=prn:=1; ~~}{FI}
{IF: prn | s=2}
{ prn1:='PRN22'; prn2:='PRN21'; ~~}
{ELIF: s=3}
{ prn1:='PRN23'; prn2:='PRN21'; ~~}
{ELIF: s=1}
{ prn1:=prn2:='PRN21'; ~~}
{FI}
{  rsep:=1;
   fun:="prn:=0; s:=1; rsep:=PAR_WYDR.RSEP"; ~~}
{SUBSTITUTE: 'LINIA02'}
{MACRO-}
{COMMENT}-----------------------------------MACRO-G3-------------------------------------{COMMENT-}
{MACRO: 'G3'}
{COMMENT}
Makro do drukowania wiersza rekordu
{COMMENT-}
{IF: lineleft()<3 & lineleft()<>0}{PAGE}{  vp:=prn:=1; ~~}{FI}
{IF: prn}
{ prn1:=prn2:='PRN22'; ~~}
{ELIF: s=1}
{ prn1:='PRN21'; prn2:='PRN22'; ~~}
{ELIF: s=3}
{ prn1:='PRN23'; prn2:='PRN22'; ~~}
{ELIF: s=2}
{ prn1:=prn2:='PRN22'; ~~}
{FI}
{  pkwn+=vpkwn;
   pkma+=vpkma;
   pkwwn+=vpkwwn;
   pkwma+=vpkwma;
   vpkwn:=vpkma:=vpkwwn:=vpkwma:=0;
   {? gnrzap>1 || rsep:=0 || {? s<>2 || rsep:=1 || rsep:=0 ?} ?};
   fun:="prn:=0; s:=2; rsep:=PAR_WYDR.RSEP"; ~~}
{SUBSTITUTE: 'LINIA02'}
{MACRO-}
{COMMENT}-----------------------------------MACRO-SG2------------------------------------{COMMENT-}
{MACRO: 'SG2'}
{IF: lineleft()<3 & lineleft()<>0}{PAGE}{  vp:=prn:=1; ~~}{FI}
{IF: prn | s=2}
{ prn1:='PRN22'; prn2:='PRN23'; ~~}
{ELIF: s=3}
{ prn1:=prn2:='PRN23'; ~~}
{FI}
{ fun:="prn:=0; s:=3; rsep:=PAR_WYDR.RSEP"; ~~}
{SUBSTITUTE: 'LINIA02'}
{MACRO-}
{COMMENT}-----------------------------------MACRO-SG3------------------------------------{COMMENT-}
{MACRO: 'SG3'}
{IF: lineleft()<3 & lineleft()<>0}{PAGE}{  vp:=prn:=1; ~~}{FI}
{ prn1:='PRN22'; prn2:='PRN23'; ~~}
{ rsep:=1; ~~}
{ fun:="prn:=0; s:=3; PAR_WYDR.RSEP"; ~~}
{SUBSTITUTE: 'LINIA02'}
{MACRO-}
{COMMENT}-----------------------------------REPORT-HEADER--------------------------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{  {! _i:=2..4 |!
      _f:='{? KONTO.K'+$_i+'=\'%\' || KONTO.K'+$_i+':=35*\'?\' ?};'
         +'KONTO.K'+$_i+':=STR.gsub(KONTO.K'+$_i+',\'_\',\'?\');'
         +'KONTO.K'+$_i+':=STR.gsub(KONTO.K'+$_i+',\'%\',\'*\')';
      ($(_f))()
   !}; ~~}
{ lmt:=int((charleft()-PRN22.width())/2); wl:=75; ~~}
{IF: rep_export()}
{'PARAMETRY WYDRUKU:'}
{'Okres obrachunkowy: '+SSTALE.AO().NAZ+' '+SSTALE.AO().ROK().NAZ+' - okres '
   +{? SSTALE.AO().ZAM='T' || 'zamknięty' || 'otwarty' ?}}
{'Zakres danych: analitycznie wraz z zapisami księgowymi'}
{'Waluta: '+SSTALE.WAL().KOD+' - '+SSTALE.WAL().TR}
{'Zakres danych: rozrachunki '+rozl
   +{? +PAR_WYDR.T_ROZ || ' typu: '+PAR_WYDR.T_ROZ || '' ?}
   +' dla kont o masce: '+{? UNPAR.P1=1
   || ' dla kont o masce: '+{? KONTO.K1='' || 35*'?' || PAR_WYDR.MASKA ?}
   |? UNPAR.P1=2
   || ' dla kont o prefiksie '+KONTO.K2
   |? UNPAR.P1=3
   || ' dla kont od: '+KONTO.K3+ ' do: '+KONTO.K4
   |? UNPAR.P1=4
   || ' dla wybranych kont syntetycznych'
   |? UNPAR.P1=5
   || ' dla wybranych kont analitycznych'
   |? UNPAR.P1=6
   || ' dla zestawu kont'
   ?}}
{<1 {? OPERATOR.DEPT || 'Jednostka księgowa: '+OPERATOR.DEPT().OD || 'Wszystkie jednostki księgowe' ?}}{<{wl} 'Zakres danych: '
+{? PAR_WYDR.SP='T' || ' tylko ze split payment' |? PAR_WYDR.SP='N' || 'bez split payment' || 'z i bez split payment' ?}}
{IF: ROZNE.KH_WYB<>null}{'Kontrahent: '+ROZNE.KH_WYB().KOD+' - '+ROZNE.KH_WYB().SKR }{FI}
{'Sortowanie: wg '+{? UNPAR.P9=2 || 'kontrahenta' || 'symbolu konta' ?}}
{IF: UNPAR.PSLO<>null}{<{wl} 'Projekt: '+UNPAR.PSLO().KOD+' - '+UNPAR.PSLO().TR}{FI}
{IF: UNPAR.P15}{'Data otwarcia od: '+$UNPAR.P13+' do: '+$UNPAR.P14}{FI}
{ELSE}
{<1 'PARAMETRY WYDRUKU:'}
{<1 'Okres obrachunkowy: '+SSTALE.AO().NAZ+' '+SSTALE.AO().ROK().NAZ+' - okres '
   +{? SSTALE.AO().ZAM='T' || 'zamknięty' || 'otwarty' ?}}{<{wl} 'Zakres danych: analitycznie wraz'
   +' z zapisami księgowymi'}
{<1 'Waluta: '+SSTALE.WAL().KOD+' - '+SSTALE.WAL().TR}{<{wl} 'Zakres danych: rozrachunki '+rozl
   +{? +PAR_WYDR.T_ROZ || ' typu: '+PAR_WYDR.T_ROZ || '' ?}
   +{? UNPAR.P1=1
   || ' dla kont o masce '+{? KONTO.K1='' || 35*'?' || PAR_WYDR.MASKA ?}
   |? UNPAR.P1=2
   || ' dla kont o prefiksie '+KONTO.K2
   |? UNPAR.P1=3
   || ' dla kont od: '+KONTO.K3+ ' do: '+KONTO.K4
   |? UNPAR.P1=4
   || ' dla wybranych kont syntetycznych'
   |? UNPAR.P1=5
   || ' dla wybranych kont analitycznych'
   |? UNPAR.P1=6
   || ' dla zestawu kont'
   ?}}
{<1 {? OPERATOR.DEPT || 'Jednostka księgowa: '+OPERATOR.DEPT().OD || 'Wszystkie jednostki księgowe' ?}}{<{wl} 'Zakres danych: '
+{? PAR_WYDR.SP='T' || ' tylko ze split payment' |? PAR_WYDR.SP='N' || 'bez split payment' || 'z i bez split payment' ?}}
{IF: ROZNE.KH_WYB<>null}{<{wl} 'Kontrahent: '+ROZNE.KH_WYB().KOD+' - '+ROZNE.KH_WYB().SKR }{FI}
{<1 'Sortowanie: wg '+{? UNPAR.P9=2 || 'kontrahenta' || 'symbolu konta' ?}
}{IF: UNPAR.PSLO<>null}{<{wl} 'Projekt: '+UNPAR.PSLO().KOD+' - '+UNPAR.PSLO().TR}{FI}
{IF: UNPAR.P15}{<1 'Data otwarcia od: '+$UNPAR.P13+' do: '+$UNPAR.P14}{FI}
{FI}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=prn:=1; ~~}
{COMMENT}-----------------------------------HEADER---------------------------------------{COMMENT-}
{HEADER}
{ prn1:='PRN22'; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}-----------------------------------FOOTER---------------------------------------{COMMENT-}
{IF: PAR_WYDR.PAR5}
{FOOTER: 0}
{IF: s=1}{<{lmt+1} PRN21.jbar(PRN23)}{ELIF: s=2}{<{lmt+1}PRN22.jbar(PRN23)}{ELIF: s=3
}{<{lmt+1} PRN23.bar()}{FI}
{  wf[1]:='Z przeniesienia'; wf[2]:=form(rkwn,,2,' ,'); wf[3]:=form(rkma,,2,' ,'); wf[5]:='';
   {? SSTALE.WAL<>FINFO.NAROD | gzwal=3
   || wf[4]:=twal[1,1]; wf[6]:=form(rkwwn,,2,' ,'); wf[7]:=form(rkwma,,2,' ,')
   || wf[4]:=wf[6]:=wf[7]:=''
   ?}; ~~}
{IF: page>1}{SUBSTITUTE: 'LINIA2F'}{<{lmt+1} PRN23.bar()}{FI}{  wf[1]:='Suma strony';
   wf[2]:=form(pkwn,,2,' ,'); wf[3]:=form(pkma,,2,' ,'); wf[5]:='';
   {? SSTALE.WAL<>FINFO.NAROD | gzwal=3
   || wf[4]:=twal[1,1]; wf[6]:=form(pkwwn,,2,' ,'); wf[7]:=form(pkwma,,2,' ,')
   || wf[4]:=wf[6]:=wf[7]:=''
   ?}; ~~}
{SUBSTITUTE: 'LINIA2F'}
{<{lmt+1} PRN2F.bar()}{  rkwn+=pkwn; rkma+=pkma; rkwwn+=pkwwn; rkwma+=pkwma;
   wf[1]:='Do przeniesienia'; wf[2]:=form(rkwn,,2,' ,'); wf[3]:=form(rkma,,2,' ,'); wf[5]:='';
   {? SSTALE.WAL<>FINFO.NAROD | gzwal=3
   || wf[4]:=twal[1,1]; wf[6]:=form(rkwwn,,2,' ,'); wf[7]:=form(rkwma,,2,' ,')
   || wf[4]:=wf[6]:=wf[7]:=''
   ?}; ~~}
{SUBSTITUTE: 'LINIA2F'}
{<{lmt+1} PRN2F.lbar()}{ prn:=1; pkwn:=pkma:=pkwwn:=pkwma:=vp:=0; ~~}
{FOOTER-}
{ELSE}
{FOOTER: 0}
{<{lmt+1}}{REP: _f:=prn2+'.lbar()'; ($(_f))()}{ prn:=1;  rkwn+=pkwn; rkma+=pkma; rkwwn+=pkwwn; rkwma+=pkwma; pkwn:=pkma:=pkwwn:=pkwma:=vp:=0; ~~}
{FOOTER-}
{FI}
{COMMENT}-----------------------------------WYDRUK---------------------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; KS.index('SYM'); KS.prefix(BILANSP.AROK);~~}
{FOR: TAB_SQL}
{PREFIX: SSTALE.WAL().KOD}
{DO}
   {IF: TAB_SQL.SYM_ROK<>gsym | TAB_SQL.OD<>godd | TAB_SQL.AN<>gan}
{COMMENT}
Sprawdzenie, czy zmienilo sie konto syntetyczne, analityczne lub kontrahent i drukowanie stosownych
naglowkow
{COMMENT-}
      {  gsym:=TAB_SQL.SYM_ROK; godd:=TAB_SQL.OD; gsbud:=0;
         {? KS.find_key(BILANSP.ASYNT+TAB_SQL.AN)
         || {? KS.SYM<>gks || gks:=KS.SYM; gcks:=1 ?};
            BUD.cntx_psh(); exec('rpm_clear','dok_fks'); synt:=TAB_SQL.AN;
            KOM.index('KS'); KOM.prefix(); {? KOM.find_key(KS.ref) || exec('rpm_display','dok_fks') ?};
            gsbud:=BUD.size(); BUD.cntx_pop()
         ?};
         {? (UNPAR.P9=1 & TAB_SQL.AN=gan) | (UNPAR.P9=2 & TAB_SQL.KONTR=gkontr)
         || gnrroz+=1
         || gnrroz:=1
         ?}; ~~}
      {IF: (gcks & UNPAR.P9=1) | (~gsbud & PAR_WYDR.PAR1=1 & UNPAR.P9=2)}
         {  w[1]:=KS.SYM+' - '+KS.NAZ; {! _a:=2..16 |!  w[_a]:='' !}; gcks:=0; ~~}
         {IF: ~rep_export()}{SUBSTITUTE: 'G1'}{FI}
      {FI}
      {IF: TAB_SQL.AN<>gan}
         {  gan:=TAB_SQL.AN; {! _a:=2..16 |! w[_a]:='' !};
            {? UNPAR.P9=1
            || {! _i:=1..gtwals |! twal[_i,1]:=''; {! _j:=2..3 |! twal[_i,_j]:=0 !} !}; gwal:='';
               swng2:=smag2:=swwng2:=swmag2:=0
            ?}; ~~}
         {IF: gsbud}
            {  w[1]:=($('KA.P'+$(gsbud)+'().TR'))(); w[1]:=TAB_SQL.AN+' - '+w[1];
               RS.index('RS'); RS.prefix();
               {! _i:=1..gsbud |!
                  {? RS.find_key(($('KA.P'+$(_i)+'().SLU().WZ'))())
                  || {? RS.TAB='KH'
                     || KH.index('KOD'); KH.prefix(2);
                        {? KH.find_key(($('KA.P'+$(_i)+'().KOD'))())
                        || w[1]:=TAB_SQL.AN+' - '+KH.SKR+' - '+KH.NAZ
                        ?}
                     ?}
                  ?}
               !}; ~~}
            {IF: ~rep_export()}{SUBSTITUTE: 'G2'}{FI}
         {FI}
      {FI}
      {IF: TAB_SQL.KONTR<>gkontr}
         {  gckontr:=1; gkontr:=TAB_SQL.KONTR;
            {! _i:=1..gtwals |! twal[_i,1]:=''; {! _j:=2..3 |! twal[_i,_j]:=0 !} !}; gwal:='';
            swng2:=smag2:=swwng2:=swmag2:=0; ~~}
      {FI}
      {  TAB_SQL.cntx_psh(); ~~}
{COMMENT}
Drukowanie zapisow
{COMMENT-}
      {FOR: TAB_SQL}
      {INDEX: ind1}
      {PREFIX: SSTALE.WAL().KOD,TAB_SQL.AN,TAB_SQL.SYM,TAB_SQL.SYM,TAB_SQL.SYM_ROK,TAB_SQL.OD}
      {DO}
         {  {? TAB_SQL.SYM_ROK<>gzsym | TAB_SQL.OD<>gzodd | TAB_SQL.AN<>gzan
            || gzsym:=TAB_SQL.SYM_ROK; gzodd:=TAB_SQL.OD; gzan:=TAB_SQL.AN;
               _wn:=_ma:=0;
               TAB_SQL.cntx_psh;
               {! |? _wn+=TAB_SQL.WN; _ma+=TAB_SQL.MA; TAB_SQL.next !};
               TAB_SQL.cntx_pop;
               _v:={? PAR_SKID.get(47)='N' & (1+TAB_SQL.TYP='N' & _ma>_wn | 1+TAB_SQL.TYP='Z' & _wn>_ma)
                   || -1
                   || gdzap-TAB_SQL.TZ
                   ?};
               w[17]:={? _v<0 || '-' || _v ?};
               w[1]:=TAB_SQL.SYM; w[2]:=$TAB_SQL.TZ; w[15]:=TAB_SQL.TYP;

               w[18]:=TAB_SQL.OD
            |? ~rep_export()
            || w[1]:=w[2]:=w[15]:=w[17]:=w[18]:=''
            ?};
            w[3]:=$TAB_SQL.DATA;
            w[4]:=TAB_SQL.ODR+'/'+TAB_SQL.REJ_KOD;
            w[5]:=$TAB_SQL.NR; w[6]:=$TAB_SQL.POZ;
            w[7]:=$TAB_SQL.DKS; w[8]:=TAB_SQL.NK; w[16]:=TAB_SQL.TYPP; w[11]:=TAB_SQL.KOD;
            w[9]:=form(TAB_SQL.WN,,2,' ,'); w[10]:=form(TAB_SQL.MA,,2,' ,');
            {? TAB_SQL.WALR<>'' | gzwal=3
            || w[12]:=form(TAB_SQL.KURS,,6,' ,');
               w[13]:=form(TAB_SQL.WN2,,2,' ,'); w[14]:=form(TAB_SQL.MA2,,2,' ,');
               _v:=0;
               {! _i:=1..gtwals |! {? twal[_i,1]=w[11] || _v:=1 ?} !};
               {? ~_v
               || _i:=0; {! |? _i+=1; twal[_i,1]<>'' !};
                  twal[_i,1]:=w[11]; twal[_i,2]:=TAB_SQL.WN2; twal[_i,3]:=TAB_SQL.MA2
               || _i:=0; {! |? _i+=1; twal[_i,1]<>w[11] !};
                  twal[_i,2]+=TAB_SQL.WN2; twal[_i,3]+=TAB_SQL.MA2
               ?};
               {? w[11]<>'' || gwal:=w[11] ?}
            || w[12]:=w[13]:=w[14]:=''
            ?};
            swng3+=TAB_SQL.WN; smag3+=TAB_SQL.MA; swwng3+=TAB_SQL.WN2; swmag3+=TAB_SQL.MA2;
            swng2+=TAB_SQL.WN; smag2+=TAB_SQL.MA; swwng2+=TAB_SQL.WN2; swmag2+=TAB_SQL.MA2;
            vpkwn+=TAB_SQL.WN; vpkma+=TAB_SQL.MA; vpkwwn+=TAB_SQL.WN2; vpkwma+=TAB_SQL.MA2;
            gnrzap+=1; ~~}
         {SUBSTITUTE: 'G3'}
         {  TAB_SQL.cntx_psh(); gg3:={? TAB_SQL.next() || 0 || 1 ?}; TAB_SQL.cntx_pop(); ~~}
         {IF: gg3 & gnrzap>1}
            {  w[1]:='Suma';
               w[2]:=form(swng3,,2,' ,');
               w[3]:=form(smag3,,2,' ,');
               w[5]:='';
               {? (gwal<>'' | gzwal=3 ) & (swwng3<>0 | swmag3<>0)
               || w[4]:=gwal; w[6]:=form(swwng3,,2,' ,'); w[7]:=form(swmag3,,2,' ,')
               || w[4]:=w[6]:=w[7]:=''
               ?}; ~~}
            {IF:~rep_export()}{SUBSTITUTE: 'SG3'}{FI}
         {FI}
      {OD}
      {  swng3:=smag3:=swwng3:=swmag3:=0;
         TAB_SQL.cntx_pop(); ~~}
   {FI}
{COMMENT}
Sprawdzenie czy drukowac wiersz Razem
{COMMENT-}
   {  gg2:=0;
      TAB_SQL.cntx_psh();
      {? UNPAR.P9=1
      || {? TAB_SQL.next()
         || {? TAB_SQL.AN<>gan || gg2:=1 ?}
         || gg2:=1
         ?}
      || {? TAB_SQL.next()
         || {? TAB_SQL.KONTR<>gkontr || gg2:=1 ?}
         || gg2:=1
         ?}
      ?};
      TAB_SQL.cntx_pop(); ~~}
{COMMENT}
Drukowanie wiersza Razem
{COMMENT-}
   {FONT: 'T8GB'}
   {IF: gg2 & gnrroz>1 & ~rep_export()}
      {  i:=gv:=1; ~~}
      {WHILE: twal[i,1]<>'' | i=1}
      {DO}
         {  {? gv=1
            || rsep:=1;
               {? UNPAR.P9=2
               || {? TAB_SQL.KONTNAZ=''
                  || w[1]:='Razem'
                  || w[1]:='Razem ('+TAB_SQL.KONTNAZ+')'
                  ?}
               || w[1]:='Razem '+TAB_SQL.AN
               ?};
               w[2]:=form(swng2,,2,' ,');
               w[3]:=form(smag2,,2,' ,')
            || rsep:=0; w[1]:=w[2]:=w[3]:=''
            ?};
            w[5]:='';
            {? gwal<>'' | gzwal=3 & i=1
            || w[4]:=twal[i,1]; w[6]:=form(twal[i,2],,2,' ,'); w[7]:=form(twal[i,3],,2,' ,')
            || w[4]:=w[6]:=w[7]:=''
            ?};
            i+=1; gv+=1; ~~}
         {SUBSTITUTE: 'SG2'}
      {OD}
   {FI}
{COMMENT}
Drukowanie wiersza Saldo
{COMMENT-}
   {IF: gg2 & (UNPAR.P7=1 | UNPAR.P9=2) & ~rep_export()}
      {  i:=gv:=1; ~~}
      {WHILE: twal[i,1]<>'' | i=1}
      {DO}
         {  {? gv=1
            || rsep:={? gnrroz>1 || PAR_WYDR.RSEP || 1 ?};
               w[1]:='Saldo';
               w[2]:=form(F.SWn(swng2,smag2),,2,' ,');
               w[3]:=form(F.SMa(swng2,smag2),,2,' ,')
            || rsep:=0; w[1]:=w[2]:=w[3]:=''
            ?};
            w[5]:='';
            {? gwal<>'' | gzwal=3 & i=1
            || w[4]:=twal[i,1];
               w[6]:=form(F.SWn(twal[i,2],twal[i,3]),,2,' ,');
               w[7]:=form(F.SMa(twal[i,2],twal[i,3]),,2,' ,')
            || w[4]:=w[6]:=w[7]:=''
            ?};
            i+=1; gv+=1; ~~}
         {SUBSTITUTE: 'SG2'}
      {OD}
   {FI}
   {IF: gg2 & gnrroz>1}
      {  swng2:=smag2:=swwng2:=swmag2:=0; ~~}
   {FI}
   {FONT: 'T8G'}
   {  gnrzap:=0; ~~}
{OD}
{COMMENT}-----------------------------------FOOTER-LAST----------------------------------{COMMENT-}
{IF: ~PAR_WYDR.PAR5}
{FOOTER: 0}
{IF: s=1}{REP: PRN21.flbar(lmt)}{ELIF: s=2}{REP: PRN22.flbar(lmt)}{ELIF: s=3
}{REP: PRN23.flbar(lmt)}{FI}{ vp:=1; ~~}
{FOOTER-}
{FI}
{IF: PAR_WYDR.PAR5}
{IF: ((page()=1 & lineleft()<5) | (page()>1 & lineleft()<8)) & lineleft()<>0}{PAGE}{ vp:=1; ~~}{FI}
{ELSE}
{IF: ((page()=1 & lineleft()<2) | (page()>1 & lineleft()<6)) & lineleft()<>0}{PAGE}{ vp:=1; ~~}{FI}
{FI}
{FOOTER}{FOOTER-}
{ENTIRE}
{IF: vp | lineleft()=0}
   {IF: s=1}{ PRN21.setcolor(color); PRN22.setcolor(color); ~~}
   {ELIF: s=2}{ PRN22.setcolor(color); ~~}
   {ELIF: s=3}{ PRN22.setcolor(color); PRN23.setcolor(color); ~~}{FI}
{FI}
{IF: vp | s=2 | s=0}{REP: PRN22.fjbar(PRN23,lmt)}
{ELIF: s=1}{REP: PRN21.fjbar(PRN23,lmt)}
{ELIF: s=3}{REP: PRN23.fbar(lmt)}
{FI}
{IF: vp}{ PRN23.setcolor('255:255:255'); ~~}{FI}
{IF: PAR_WYDR.PAR5 & ~rep_export()}
   {  w[1]:='Z przeniesienia'; w[2]:=form(rkwn,,2,' ,'); w[3]:=form(rkma,,2,' ,'); w[5]:='';
      {? SSTALE.WAL<>FINFO.NAROD | gzwal=3
      || w[4]:=twal[1,1]; w[6]:=form(rkwwn,,2,' ,'); w[7]:=form(rkwma,,2,' ,')
      || w[4]:=w[6]:=w[7]:=''
      ?}; {! _a:=8..14 |! w[_a]:='' !}; ~~}
   {IF: page>1}
   {SUBSTITUTE: 'LINIA23'}
   {<{lmt+1} PRN23.bar()}{  w[1]:='Suma strony'; w[2]:=form(pkwn,,2,' ,');
      w[3]:=form(pkma,,2,' ,'); w[5]:='';
      {? SSTALE.WAL<>FINFO.NAROD | gzwal=3
      || w[4]:=twal[1,1]; w[6]:=form(pkwwn,,2,' ,'); w[7]:=form(pkwma,,2,' ,')
      || w[4]:=w[6]:=w[7]:=''
      ?};
      {! _a:=8..14 |! w[_a]:='' !}; ~~}
   {SUBSTITUTE: 'LINIA23'}
   {<{lmt+1} PRN23.bar()}{FI}
{FI}
{IF: ~rep_export()}
{  rkwn+=pkwn; rkma+=pkma; rkwwn+=pkwwn; rkwma+=pkwma; w[1]:='Razem'; w[2]:=form(rkwn,,2,' ,');
   w[3]:=form(rkma,,2,' ,'); w[5]:='';
   {? SSTALE.WAL<>FINFO.NAROD | gzwal=3
   || w[4]:=twal[1,1]; w[6]:=form(rkwwn,,2,' ,'); w[7]:=form(rkwma,,2,' ,')
   || w[4]:=w[6]:=w[7]:=''
   ?}; ~~}
{SUBSTITUTE: 'LINIA23'}
{FI}
{IF: UNPAR.P7 & ~rep_export()}
{  w[1]:='Saldo'; w[2]:=form(F.SWn(rkwn,rkma),,2,' ,');
   w[3]:=form(F.SMa(rkwn,rkma),,2,' ,'); w[5]:='';
   {? SSTALE.WAL<>FINFO.NAROD | gzwal=3
   || w[4]:=twal[1,1]; w[6]:=form(F.SWn(rkwwn,rkwma),,2,' ,'); w[7]:=form(F.SMa(rkwwn,rkwma),,2,' ,')
   || w[4]:=w[6]:=w[7]:=''
   ?}; ~~}
{SUBSTITUTE: 'LINIA23'}
{FI}
{IF: ~rep_export()}
{<{lmt+1} PRN23.lbar()}
{<{lmt+1}>{PRN23.width()+lmt} 'KONIEC WYDRUKU'}
{FI}
{ENTIRE-}
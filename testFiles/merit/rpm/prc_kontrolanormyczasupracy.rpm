:!UTF-8
{TITLE: Kontrola norm czasu pracy}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','pb','n40','n48','n150','ref_okr');

   tresc:='okrnorma';
   PAR_WYDR.TITLE:='Norma czasu pracy'@;

   PRN:=obj_new(@.CLASS.PRINT, 7);
   {? ~rep_export()
   || PRN.add("Lp+=1",5,'Lp.'@,1,,,,,',,\'9.\'')
   ?};
   PRN.add("P.T",10,'Nr teczki'@);
   PRN.add("P.OSOBA().NAZWISKO",25,'Nazwisko'@);
   PRN.add("P.OSOBA().PIERWSZE",25,'Imię'@);
   PRN.s_frame();

   prn1:='PRN';
   lmt:=rsep:=Lp:=pb:=0;
   vp:=h_rat:=1;
   n40:=n48:=n150:=time(0,0,0);
   A_OKR.cntx_psh();
   A_OKR.index('A_OKR');
   A_OKR.win_sel('SLO');
   A_OKR.seek(__HARM.OKR_REF,,1);
   {? A_OKR.select(,1,5,'dpu:d')
   || ref_okr:=A_OKR.ref();
      exec('czytaj','#stalesys',A_OKR.DO,KST,'R_NTYG','R_NNTYG','R_NLIM');
      {? P.f_active() || P.f_clear() ?};

      _args:=exec('wybierz_args','pracownik');
      _args.DOMAIN:='PRC';
      _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
      _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
      {? +P_FILTER.STATUS
      || _args.F_ZATR:={? +P_FILTER.F_ZATR().KOD || P_FILTER.F_ZATR().KOD || '*T' ?};
         _args.VIEW:=P_FILTER.STATUS
      || _args.F_ZATR:=__PARSES.getVal('F_ZATR');
         _args.VIEW:=__PARSES.getVal('ZakresDanych')
      ?};
      _args.SQL_FROM:='';
      _args.SQL_WHERE:='P.REFERENCE in (select A_OKRP.P from A_OKRP where A_OKRP.OKR=\''+$ref_okr+'\' order by 1)';
      _TAB:=exec('wybierz','pracownik',_args).P;
      _NDX:=_TAB.ndx_tmp(,1,'SQL',,);

      _n:='Elementy wydruku';
      _o:=exec('get','#profile',,tresc,_n);
      _tab:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,,,,,,,,
         'Uwzględniać na wydruku?'@,'Tylko pracowników z przekroczonymi normami'@,'Wszystkich wybranych'@,#_o
      );
      _all:={? _tab.first()
            || _o:=$_tab.ASK;
               exec('set','#profile',,tresc,_n,_o);
               _tab.ASK
            || -1
            ?};
      params_set('P',_TAB,'NDX',_NDX,'ALL',_all)
   || ref_okr:=null()
   ?}
}
{END:
   {? P.f_active() || P.f_clear() ?};
   A_OKR.cntx_pop();
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','pb','n40','n48','n150','ref_okr');
   exec('czytaj','#stalesys',date(),KST,'R_NTYG','R_NNTYG','R_NLIM')
}
{EXIT: ref_okr=null() | ~params_get().P.first() | params_get().ALL=-1}
{COMMENT}OLD: okrnorma.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
{FONT: 'T8'}{  h_rat:=charleft(); ~~}
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~}
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'%1 %2'['Nazwa okresu:'@,A_OKR.NAZ().NAZ]}
   {<{ceil(lmt*h_rat)+1}'%1 %2 %3 %4'['Od daty:'@,A_OKR.OD$1,'do daty:',A_OKR.DO$1]}
   {<{ceil(lmt*h_rat)+1}'Kontrola %1 nadgodzin od daty: %2 do daty: %3'@
      [time(#(3+KST.R_NLIM),#(KST.R_NLIM+2))$1,date(A_OKR.DO~1,1,1)$1,A_OKR.DO$1]}
   {<{ceil(lmt*h_rat)+1}'Pracownicy uwzględnieni na wydruku: %1'@
   [{? params_get().ALL || 'Wszyscy wybrani'@ || 'Tylko z przekroczonymi normami'@ ?}]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{  PRN.add("n40$1",10,'%1h średnio w tygodniu'@[time(#(3+KST.R_NTYG),#(KST.R_NTYG+2))$1],1);
   PRN.add("n48$1",10,'%1h średnio z nadgodzin'@[time(#(3+KST.R_NNTYG),#(KST.R_NNTYG+2))$1],1);
   PRN.add("n150$1",10,'%1h nadgodzin w roku'@[time(#(3+KST.R_NLIM),#(KST.R_NLIM+2))$1],1);
   lmt:=int((charleft()-PRN.width())/2); ~~
}
{FOR: A_OKRP}
{INDEX: 'A_OKRNI'}
{PREFIX: A_OKR.ref()}
   {DO}
   {  A_OKRP.P(); ~~}
      {IF: _cfg:=params_get(); _cfg.P.index(_cfg.NDX); _cfg.P.find_key($A_OKRP.P) & (A_OKRP.PRZEP<>*0 | _cfg.ALL)}
         {  _od:={? P.DZA>A_OKR.OD || P.DZA || A_OKR.OD ?};
            _do:={? P.DZ<>#0 & P.DZ<A_OKR.DO || P.DZ || A_OKR.DO ?};
'nadgodziny';
            _rub:=',55,56,63,66,7011,';
'nadgodziny w okresie';
            _nadgodz:=exec('godzWOkresie','prc_plan',P.ref(),A_OKR.OD,A_OKR.DO,0,_rub);
'limit nadgodzin w roku';
            n150:=*(60*exec('godzWOkresie','prc_plan',P.ref(),date(A_OKR.DO~1,1,1),A_OKR.DO,0,_rub));
'wyliczenia';
            _oWymiar:=exec('wymiar_od_do','okres',_od,_do);
            _tyg:=_oWymiar.tyg;
            _czas:=A_OKRP.PRZEP~1+(A_OKRP.PRZEP~2/60)-(_oWymiar.godz_poz_rob);
            {? _czas<0 || _czas:=0 ?};
            n48:={? _tyg>0 || time(_czas%_tyg,(_czas/_tyg-(_czas%_tyg))*60,0) || *0 ?};
            _czas-=_nadgodz;
            {? _czas<0 || _czas:=0 ?};
            n40:={? _tyg>0 || time(_czas%_tyg,(_czas/_tyg-(_czas%_tyg))*60,0) || *0 ?};
            ~~
         }
         {IF:  _cfg:=params_get();
               {? _cfg.ALL
               || 1
               || n40>time(#(3+KST.R_NTYG),#(KST.R_NTYG+2),0)
                  |
                  n48>time(#(3+KST.R_NNTYG),#(KST.R_NNTYG+2),0)
                  |
                  n150>time(#(3+KST.R_NLIM),#(KST.R_NLIM+2),0)
               ?}
         }
            {SUBSTITUTE: 'LINIA0'}
         {FI}
      {FI}
   {OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: tresc}
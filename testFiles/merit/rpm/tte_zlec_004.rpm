:!UTF-8
{TITLE:D. Porównanie ilości przepracowanych godzin z normami}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  ZLGD.cntx_psh;
  ZL.cntx_psh;
  ZGH.cntx_psh;
  ZTP.cntx_psh;
  ZGP.cntx_psh;
  {? var_pres('PRN')>0 || obj_del(PRN) ?};
  PRN:= obj_new(@.CLASS.PRINT, 5);
  PRN.s_frame();
  {? var_pres('TAB1')>0 || obj_del(TAB1) ?};
  TAB1:=tab_tmp(3,'ZL','INTEGER','Nr zlecenia',
                  'SYM','STRING[20]','Symbol',
                  'FAZA','STRING[10]','Faza.pr',
                  'NORMA','REAL','Norma',
                  'CZAS','REAL','Czas');
  {? var_pres('TAB2')>0 || obj_del(TAB2) ?};
  TAB2:=tab_tmp(2,'ZL','INTEGER','Zlecenie',
                  'SYM','STRING[20]','Symbol',
                  'NORMA','REAL','Norma',
                  'CZAS','REAL','Czas');
  prn1:='PRN';
  tryb:=rep_export();
  zlec:=0;
  waslbar:=0;rez:=0;tab1norma:=0;
  dalej:=dalej1:=dalej2:=dalej3:=0;
  color:='';ntime:=0;fazy:='';nag:='N';
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  ZLGD.cntx_pop;
  ZL.cntx_pop;
  ZGH.cntx_pop;
  ZTP.cntx_pop;
  ZGP.cntx_pop;
  obj_del(PRN);
  obj_del(TAB1);
  obj_del(TAB2);
  &dalej;&rez;&fazy;
  &dalej1;
  &dalej2;
  &zlec;
  &nag;
  &tab1norma;
  &dalej3;
  &prn1;
  &ntime;
  &color;
  &lm; &ll; &vp; &lmt; &rsep; &tryb
}
{COMMENT}
  Wydruk porównania ilości przepracowanych godzin z normami technologicznymi.
  [MKO] - 2001/01/30 - [7.60] OWwPRO27/5.3
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{ _wydr_naz:={? ~tryb || 'zlec_004' || 'zlec_04a' ?};
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.ZLECOZ:=WYDRUKIL.SORTUJ;
     WYDRUK.RODZAJ:=WYDRUKIL.SR;
     WYDRUK.TYPZL:=WYDRUKIL.FAK;
     WYDRUK.FAZY:=WYDRUKIL.RODZFAK
  ?};
  WYDRUK.win_edit();
  {? ~tryb
  || WYDRUK.win_edit('ZLEC_004')
  || WYDRUK.win_edit('ZLEC_04A')
  ?}; ~~}
{IF: WYDRUK.edit()}
 {WYDRUKIL.SORTUJ:=WYDRUK.ZLECOZ;
  WYDRUKIL.SR:=WYDRUK.RODZAJ;
  WYDRUKIL.FAK:=WYDRUK.TYPZL;
  WYDRUKIL.RODZFAK:=WYDRUK.FAZY;
  WYDRUKIL.put(1);
  typ:=WYDRUK.TYPZL;
  rodzaj:=WYDRUK.ZLECOZ;
  {? ~tryb
  || fazy:=WYDRUK.FAZY
  || fazy:='N'
  ?};
  nag:=WYDRUK.RODZAJ;~~}
{ELSE}
{EXIT}
{FI}
  {COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{ {? fazy='T'||
  PRN.add("form(sym)",20,'Symbol zlecenia'@);
  PRN.add("f",12,'Faza'@,,'#');
  PRN.add("n",13,'Norma czasowa'@,1,'#');
  PRN.add("c",18,'Czas przepracowany'@,1,'#');
  PRN.add("p",6,'%% wyk.'@,1,'#')
 ||
  PRN.add("form(TAB2.SYM)",20,'Symbol zlecenia'@);
  PRN.add("form(TAB2.NORMA,,exec('get','#params',500604,1))",20,'Norma czasowa'@,1);
  PRN.add("form(TAB2.CZAS,,exec('get','#params',500604,1))",20,'Czas przepracowany'@,1);
  PRN.add("form({? TAB2.NORMA<>0 || TAB2.CZAS/TAB2.NORMA*100 || 0 ?},,2)",6,'%% wyk.'@,1)
  ?};
  PAR_WYDR.TITLE:='PORÓWNANIE ILOŚCI PRZEPRACOWANYCH GODZIN Z NORMAMI'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Typ: %1'@[{? typ='P' || 'Zlecenie produkcyjne'@ || 'Zlecenie warsztatowe'@ ?}]}
{<{lmt+1} {? rodzaj='O' || 'Zlecenia otwarte'@ |? rodzaj='Z' || 'Zlecenia zamknięte'@ || 'Zlecenia wszystkie'@ ?}}
{<{lmt+1} {? fazy='T' || 'W rozbiciu na fazy'@ || 'Bez rozbicia na fazy'@ ?}}
{<{lmt+1} {? nag='T' || 'Tylko zlecenia nagłówkowe'@ || '' ?}}
{FONT: 'T10B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}-----------------------------PRZYGOTOWANIE DANYCH-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{IF: typ='P'}
{ ZTP.clear(); ZTP.index('RDTP'); ZTP.prefix('P');~~}
{ELSE}
{ ZTP.clear(); ZTP.index('RDTP'); ZTP.prefix('W');~~}
{FI}
{IF: ZTP.first}{ dalej:=1;~~}{FI}
  {WHILE: dalej}
  {DO}
  {IF: nag='N'}
   {IF: rodzaj='Z'}
     {ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref,'N','Z'); ~~}
   {ELIF: rodzaj='O'}
     {ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref,'N','O'); ~~}
   {ELSE}
     {ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref,'N'); ~~}
   {FI}
  {ELSE}
   {IF: rodzaj='Z'}
     {ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref,'N','Z',0); ~~}
   {ELIF: rodzaj='O'}
     {ZL.clear(); ZL.index('TSSA'); ZL.prefix(ZTP.ref,'N','O',0); ~~}
   {ELSE}
     {ZL.clear(); ZL.index('NRNZL'); ZL.prefix(0,0,ZTP.ref); ~~}
   {FI}
  {FI}
     {IF: ZL.first()}{ dalej1:=1; ~~}{FI}
     {WHILE: dalej1}
     {DO}
        {zlec:=ZL.NRNZL;ZGH.clear(); ZGH.index('ZLNR'); ZGH.prefix(ZL.ref); ~~}
        {IF: ZGH.first()}{ dalej2:=1; ~~}{FI}
        {WHILE: dalej2}
        {DO}
          {IF: zlec=0}
           {ZGP.clear(); ZGP.index('NRPP'); ZGP.prefix(ZGH.ref); ~~}
           {ELSE}
           {ZGP.clear(); ZGP.index('PNRPP'); ZGP.prefix(ZGH.ref); ~~}
           {FI}
           {IF: ZGP.first()}{ dalej3:=1; ~~}{FI}
           {WHILE: dalej3}
           {DO}
             {IF: fazy='T'}
             {IF: TAB1.find_key(#ZL.ref,ZL.SYM,ZGP.PFAZ().KOD)}
              {TAB1.NORMA+=ZGP.NTIME;
               TAB1.put;~~}
             {ELSE}
               {TAB1.CZAS:=0;
                TAB1.ZL:=#ZL.ref;
                TAB1.FAZA:=ZGP.PFAZ().KOD;
                TAB1.SYM:=ZL.SYM;
                TAB1.NORMA:=ZGP.NTIME;
                TAB1.add;~~}
             {FI}
             {FI}
             { ntime+=ZGP.NTIME; ~~}
             { dalej3:=ZGP.next(); ~~}
           {OD}
           { dalej2:=ZGH.next(); dalej3:=0; ~~}
        {OD}
        {ZLGD.cntx_psh;~~}
        {d1:=ZL.OD;{? ZL.DO<>date(0,0,0) || d2:=ZL.DO || d2:=date(ST.AR,ST.AM,0) ?};~~}
        {_datap:=date(ST.AR,ST.AM,1);
    OKR.cntx_psh;
    OKR.clear();
    OKR.index('OKR');
    OKR.find_key(ST.AR,ST.AM);
    _rok:=OKR.ROK;
    _lastrok:=0;
    _nr:=OKR.MC;
    {!
    |? d2>=date(OKR.ROK,OKR.MC,0)
    |!
       {! |? d1<_datap & ~rez
       |! {? ~OKR.prev() || rez:=1 ?};
          _rok:=OKR.ROK;
          _nr:=OKR.MC;
          _datap:=date(OKR.ROK,OKR.MC,1)
       !};
       dalej2:=0;
       {? _lastrok<>_rok
       || _lastrok:=_rok;
          {? ZLGD.use('zlgd_'+ST.ODDZ+(2-$OKR.ROK))
          ||
            {? ZL.RODZAJ='Z'
            || ZL.cntx_psh();
               ZL.index('NRNZL');
               ZL.prefix(ZL.UNRZL);
               {? ZL.first()
               || {! |?
                  ZLGD.clear;ZLGD.index('ZLECENIE');ZLGD.prefix(ZL.ref);
                  {? ZLGD.first || dalej2:=1 ?};
                  {! |? dalej2 |!
                     {? fazy<>'T'
                     || TAB2.CZAS+=ZLGD.TIME
                     || {? TAB1.find_key(#ZL.ref,ZL.SYM,ZLGD.ZGP().PFAZ().KOD)
                        || TAB1.CZAS+=ZLGD.TIME;
                           TAB1.put
                        ?}
                     ?};
                     dalej2:=ZLGD.next
                  !};
                  ZL.next
                  !}
               ?};
               ZL.cntx_pop()
            || ZLGD.clear;ZLGD.index('ZLECENIE');ZLGD.prefix(ZL.ref);
               {? ZLGD.first || dalej2:=1 ?};
               {! |? dalej2 |!
                  {? fazy<>'T'
                  || TAB2.CZAS+=ZLGD.TIME
                  || {? TAB1.find_key(#ZL.ref,ZL.SYM,ZLGD.ZGP().PFAZ().KOD)
                     || TAB1.CZAS+=ZLGD.TIME;TAB1.put
                     ?}
                  ?};
                  dalej2:=ZLGD.next
               !}
            ?}
          ?}
       ?};
       {? _nr=12
       || {? ~OKR.next || rez:=1 ?};
          _rok:=OKR.ROK;
          _nr:=OKR.MC
       || OKR.next;
         _rok:=OKR.ROK;
         _nr:=OKR.MC
       ?};
       TAB2.SYM:=''
    !};
    OKR.cntx_pop;
    ZLGD.cntx_pop;~~}
    {IF: fazy<>'T'}
       {{? ntime>0
        || TAB2.ZL:=#ZL.ref;
           TAB2.SYM:=ZL.SYM;
           TAB2.NORMA:=ntime;
           TAB2.add
        ?};~~}
    {FI}
    {dalej1:=ZL.next;ntime:=dalej2:=dalej3:=0;TAB2.ZL:=0;TAB2.SYM:='';TAB2.CZAS:=0;TAB2.NORMA:=0;rez:=0;~~}
    {OD}
   {dalej:=ZTP.next;dalej1:=dalej2:=dalej3:=0;~~}
  {OD}
{ dalej:=0;~~}
{COMMENT}--------------------WŁAŚCIWY WYDRUK-----------------------{COMMENT-}
{IF: fazy<>'T'}
    {IF: TAB2.first()}
        { dalej:=1; ~~}
    {FI}
    {WHILE: dalej}
    {DO}
       {SUBSTITUTE: 'LINIA0'}
       { dalej:=TAB2.next(); ~~}
    {OD}
{ELSE}
    {IF: TAB1.first()}
        {dalej:=1;sym:=TAB1.SYM;f:=n:=c:=p:='';suman:=sumac:=sumap:=0; ~~}
     {FI}
     {FOR: TAB1}
     {DO}
        {f+=form(TAB1.FAZA,12)+'#';
        n+=form(TAB1.NORMA,13,exec('get','#params',500604,1))+'#';
        c+=form(TAB1.CZAS,18,exec('get','#params',500604,1))+'#';
        p+=form(({?TAB1.NORMA<>0||TAB1.CZAS/TAB1.NORMA||0?})*100,6,2)+'#';
        suman+=TAB1.NORMA;sumac+=TAB1.CZAS;tab1norma:=TAB1.NORMA;
        ~~}
     {TOTAL: TAB1.SYM}
          {IF: suman>tab1norma}
              {n+=form(suman,13,exec('get','#params',500604,1));
               c+=form(sumac,18,exec('get','#params',500604,1));~~}
          {FI}
          {SUBSTITUTE: 'LINIA0'}
          {f:=n:=c:=p:='';sym:=TAB1.SYM;suman:=sumac:=sumap:=0;~~}
      {OD}
{FI}

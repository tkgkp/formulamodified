:!UTF-8
{TITLE:E.  Suma obrotów kont analitycznych w rejestrze}
{PRINTER: exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:  kolu:=obj_new(20); p:=obj_new(20); kl:=0; il:=0;
         rejkod:=''; sumawn:=0; sumama:=0; druk:=0;
         saldown:=0; saldoma:=0; summw:=0; summm:=0; sumswn:=0; sumsma:=0;
         druksynt:=0; licznik:=0; podsum1:=podsum2:=podsum3:=podsum4:=kurs:=0;
         exec('ini_wal','konto');
         wc:=kk:=0; tt:=obj_new(12); a:=p1:=p2:=mdc:=''; lm:=0;
         drul:=0;ROZNE.PAR1:=2; ROZNE.KODREJ:=''; ROZNE.win_edit('KS_3');
         pref1:=pref2:=''; ROZNE.KS_OD:=ROZNE.KS_DO:=null; ROZNE.blank();
         ROZNE.PAR1:=3; ROZNE.PAR4:=0; ROZNE.UT_DOK:=1; param:='';
         KS.win_sel('SLO_TEN');
         ROK_F.clear(); SSTALE.AR(); dept:=OPERATOR.DEPT
       }
{END:  obj_del(kolu); obj_del(p); &kl; &il; &rejkod; &mdc; &lm; &kurs;
       &sumawn; &sumama; &druk; &saldown; &saldoma; &summw; &summm;
       &sumswn; &sumsma; &druksynt; &licznik; &podsum1; &podsum2; &podsum3;
       &podsum4; &wc; &kk; obj_del(tt); &p1; &p2; &drul;
       {? var_pres('a')>0 ||  &a ?};  &pref1; &pref2; &param;
       OPERATOR.DEPT:=dept; &dept
     }
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: ks_analr.rpm
Utworzony:   ????-??-??
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Wydruk zestawienia obrotow i sald kont analitycznych w rejestrze
{COMMENT-}
{ kl:=6;
  kolu[1]:=35;
  kolu[2]:=80;
  kolu[3]:=20;
  kolu[4]:=20;
  kolu[5]:=20;
  kolu[6]:=20;
  il:=kolu[1]+kolu[2]+kolu[3]+kolu[4]+kolu[5]+kolu[6]+kl+1;
  p[1]:=2+kolu[1];
  {!_a:=2 ..kl
  |! p[_a]:=1+p[_a-1]+kolu[_a]
  !};
  wal:='Waluta aktywna:  '+SSTALE.WAL().TR+' - '+SSTALE.WAL().KOD;
        {? WYDRUKI.ODD=0
           || OPERATOR.DEPT:=null;   odd:='Jednostka księgowa: wszystkie jednostki księgowe'@
           ||   odd:='Jednostka księgowa: %1'@[WYDRUKI.ODD().OD]
        ?};
~~}
{EXIT: ~ROZNE.edit("_a:=__CHK.record(ROZNE,,'KODREJ');
                    {? _a='' & ROZNE.PAR4=4
                    || _a:=__CHK.record(ROZNE,,'KS_OD','KS_DO');
                       {? _a=''
                       || {? ROZNE.KS_OD().SYM> ROZNE.KS_DO().SYM
                          || FUN.info('Podaj poprawnie zakres kont.'@); _a:='KS_OD'
                          ?}
                       ?}
                    ?}; _a")}
{   rejkod:=ROZNE.KODREJ;
    {? ROZNE.PAR1=1 || konto:='Konta: pozabilansowe'@
    |? ROZNE.PAR1=2 || konto:='Konta: bilansowe'@
    |? ROZNE.PAR1=3 || konto:='Konta: bilansowe i pozabilansowe'@
    || konto:='Konta: wynikowe'@
    ?};
    kurs:=exec('kurs','konto',UNPAR.PSLO().KOD,UNPAR.P25);~~
}
{'------------------------------------------------------------------';~~}
{'           DEFINICJE "MACRO", "HEADER" I "FOOTER"                 ';~~}
{'------------------------------------------------------------------';~~}
{MACRO: 'LINIA1'}
┌{kolu[1]*'─'}┬{kolu[2]*'─'}┬{(kolu[3]+kolu[4]+1)*'─'}┬{(kolu[5]+kolu[6]+1)*'─'}┐
{MACRO-}
{MACRO: 'LINIA2'}
├{kolu[1]*'─'}┼{kolu[2]*'─'}┼{kolu[3]*'─'}┼{kolu[4]*'─'}┼{kolu[5]*'─'}┼{kolu[6]*'─'}┤
{MACRO-}
{MACRO: 'LINIA3'}
└{kolu[1]*'─'}┴{kolu[2]*'─'}┴{kolu[3]*'─'}┴{kolu[4]*'─'}┴{kolu[5]*'─'}┴{kolu[6]*'─'}┘
{MACRO-}
{MACRO: 'LINIA4'}
├{kolu[1]*'─'}┴{kolu[2]*'─'}┼{kolu[3]*'─'}┼{kolu[4]*'─'}┼{kolu[5]*'─'}┼{kolu[6]*'─'}┤
{MACRO-}
{MACRO: 'LINIA5'}
└{kolu[1]*'─'}─{kolu[2]*'─'}┴{kolu[3]*'─'}┴{kolu[4]*'─'}┴{kolu[5]*'─'}┴{kolu[6]*'─'}┘
{MACRO-}
{MACRO: 'LINIA6'}
├{kolu[1]*'─'}┼{kolu[2]*'─'}┼{kolu[3]*'─'}┼{kolu[4]*'─'}┼{kolu[5]*'─'}┼{kolu[6]*'─'}┤
{MACRO-}
{MACRO: 'LINIA7'}
├{kolu[1]*'─'}─{kolu[2]*'─'}┼{kolu[3]*'─'}┼{kolu[4]*'─'}┼{kolu[5]*'─'}┼{kolu[6]*'─'}┤
{MACRO-}
{MACRO: 'LINIA8'}
├{kolu[1]*'─'}┼{kolu[2]*'─'}┼{kolu[3]*'─'}┼{kolu[4]*'─'}┼{kolu[5]*'─'}┼{kolu[6]*'─'}┤
{MACRO-}
{ {? ROZNE.PAR4=2
   || WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=ROZNE.MASKA;
       WYDRUKI.MASKA:=exec('usostsep','konto',WYDRUKI.MASKA);
     {? +WYDRUKI.MASKA < WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?}
   |? ROZNE.PAR4=0
   || WYDRUKI.MASKA:=ROZNE.PREFIX
   || WYDRUKI.MASKA:=''
   ?};~~
 }
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ZESTAWIENIE OBROTÓW I SALD KONT ANALITYCZNYCH ZA OKRES %1 %2 REJESTR %3'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ,rejkod];~~}
{LINE: 1}

{FONT: 'HEAD'}{SPACE: 'B'}{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}

{IF: page=1}

{ KS.cntx_psh();
  {? ROZNE.PAR4=2
  || param:='Dla kont o masce: %1'@[ROZNE.MASKA]
  |? ROZNE.PAR4=0
  || param:='Wydruk dla kont : %1'@[ROZNE.PREFIX+(35-+ROZNE.PREFIX)*'?']
  |? ROZNE.PAR4=3
  || param:='Wydruk wybranych kont'@
  |? ROZNE.PAR4=4
  || param:='Wydruk dla zakresu kont od: %1 do: %2'@[ROZNE.KS_OD().SYM,ROZNE.KS_DO().SYM]
  || param:='Wydruk dla wszystkich kont'@
  ?};
  KS.cntx_pop();~~}
{IF: rep_export()}
{'Parametry wydruku:'}
{{? ROZNE.UT_DOK || 'Drukowanie pozycji z zerowymi sumami: tak'@ || 'Drukowanie pozycji z zerowymi sumami: nie'@ ?}}
{wal}
{{? SSTALE.AO().ZAM='T'||'Rodzaj okresu: zamknięty'@||'Rodzaj okresu: otwarty'@ ?}}
{param}
{{? UNPAR.PSLO || 'Waluta wydruku:  %1 - %2'@[UNPAR.PSLO().TR,SLO.KOD] || 'Waluta wydruku:  %1 - %2'@[SSTALE.WAL().TR,SLO.KOD] ?}}
{{? UNPAR.P25<>1 | (UNPAR.PSLO<>null & UNPAR.PSLO<>SSTALE.WAL) || 'Kurs: %1'@[$UNPAR.P25] || '' ?}}
{odd}
{konto}
{ELSE}
{<3 'Parametry wydruku:'}
{<6 {? ROZNE.UT_DOK || 'Drukowanie pozycji z zerowymi sumami: tak'@ || 'Drukowanie pozycji z zerowymi sumami: nie'@ ?}}
{<66 wal}{<116 {? SSTALE.AO().ZAM='T'||'Rodzaj okresu: zamknięty'@||'Rodzaj okresu: otwarty'@ ?}}
{<6 param}{<66 {? UNPAR.PSLO || 'Waluta wydruku:  %1 - %2'@[UNPAR.PSLO().TR,SLO.KOD] || 'Waluta wydruku:  %1 - %2'@[SSTALE.WAL().TR,SLO.KOD] ?}}{<116 {? UNPAR.P25<>1 | (UNPAR.PSLO<>null & UNPAR.PSLO<>SSTALE.WAL) || 'Kurs: %1'@[$UNPAR.P25] || '' ?}}
{<6 odd}{<66 konto}
{FI}
{FI}
{IF: rep_export()}
{TABLE}
{TR}{TH:2}{TH:2}{'Obroty'@}{TH:2}{'Saldo'@}
{TR}{TH}{'Konto'@}{TH}{'Nazwa'@}{TH}{'Winien'@}{TH}{'Ma'@}{TH}{'Winien'@}{TH}{'Ma'@}
{ELSE}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA1'}
│{<{p[1]}'│'}{<{p[2]}'│'}{<{p[2]}>{p[4]}'Obroty'@}{<{p[4]}'│'}{<{p[4]}>{p[6]}'Saldo'@}{<{p[6]}'│'}
│{<1>{p[1]}'Konto'@}{<{p[1]}'│'}{<{p[1]}>{p[2]}'Nazwa'@}{<{p[2]}'├'}{kolu[3]*'─'}┬{kolu[4]*'─'}{<{p[4]}'┼'}{kolu[5]*'─'}┬{kolu[6]*'─'}{<{p[6]}'┤'}
│{<{p[1]}'│'}{<{p[2]}'│'}{<{p[2]}>{p[3]}'Winien'@}{<{p[3]}'│'}{<{p[3]}>{p[4]}'Ma'@}{<{p[4]}'│'}{<{p[4]}>{p[5]}'Winien'@}{<{p[5]}'│'}{<{p[5]}>{p[6]}'Ma'@}{<{p[6]}'│'}
{SUBSTITUTE: 'LINIA2'}{FONT: 'Q'}{SPACE: 'C'}
{FI}
{HEADER-}
{'------------------------------------------------------------------';~~}
{'                   FOOTER                                         ';~~}
{'------------------------------------------------------------------';~~}
{FOOTER: 0}
{FONT:'T8G'}{SPACE:'C'}
{SUBSTITUTE: 'LINIA3'}
{SUBSTITUTE: 'FOOT'}
{~~}
{~~}
{FOOTER-}
{COMMENT}------------------------------MACRO- SRODEK-----------------------{COMMENT-}
{MACRO: 'SRODEK'}
{FONT:'T8G'}{SPACE:'C'}
  { podsum1:=podsum2:=podsum3:=podsum4:=0;~~}
  {IF: (ROZNE.PAR1=1 & -(1+KS.TYP)='p') | (ROZNE.PAR1=2 & -(KS.TYP)='bl') | (ROZNE.PAR1=3) | (ROZNE.PAR1=4 & -KS.TYP='bw') | (ROZNE.PAR1=5 & -(1+KS.TYP)<>'p')}
  { druksynt:=0; licznik:=0; BUD.cntx_psh();  BUD.index('KS'); BUD.prefix(KS.ref); _poz:=BUD.size();
    {! _i:=1.._poz |! tt[_i]:='' !}; BUD.cntx_pop(); ~~}
  {FOR: 'AN'}
  {INDEX: 'WALSYM'}
  {PREFIX: SSTALE.WAL,pref2}
  {DO}
    {IF: (ROZNE.PAR4=2 & exec('mask','konto',AN.SYM)) | ROZNE.PAR4<>2}
    { sumawn:=0; sumama:=0; saldown:=0; saldoma:=0; druk:=0;
      wc:=BILANSP.ASYNT; {? BILANSP.ASEP=',' || wc:=wc+2 || wc:=wc+3 ?};~~}
       {IF: WYDRUKI.ODD}
        {IF: SSTALE.WAL<>FINFO.NAROD}
           {FOR: POZ}
           {INDEX: 'OKWAL_O'}
           {PREFIX: 'T','T',SSTALE.WAL,WYDRUKI.ODD,AN.SYM}
           {DO}
           {IF: POZ.DOK().REJ().KOD=rejkod & REJ.ODD=WYDRUKI.ODD}
             { druk:=1; {? -POZ.STR='wn' || sumawn+=POZ.SUMW ||  sumama+=POZ.SUMW ?};~~}
           {FI}
           {OD}
         {ELSE}
            {FOR: POZ}
            {INDEX: 'OKWAL_O1'}
            {PREFIX: 'T','T',WYDRUKI.ODD,AN.SYM}
            {DO}
            {IF: POZ.DOK().REJ().KOD=rejkod & REJ.ODD=WYDRUKI.ODD}
               { druk:=1; {? -POZ.STR='wn' || sumawn+=POZ.SUM ||  sumama+=POZ.SUM ?};~~}
            {FI}
            {OD}
          {FI}
    {ELSE}
           {IF: SSTALE.WAL<>FINFO.NAROD}
           {FOR: POZ}
           {INDEX: 'OKWAL'}
           {PREFIX: 'T','T',SSTALE.WAL,AN.SYM}
           {DO}
           {IF: POZ.DOK().REJ().KOD=rejkod}
             { druk:=1; {? -POZ.STR='wn' || sumawn+=POZ.SUMW ||  sumama+=POZ.SUMW ?};~~}
           {FI}
           {OD}
         {ELSE}
            {FOR: POZ}
            {INDEX: 'OKWAL1'}
            {PREFIX: 'T','T',AN.SYM}
            {DO}
            {IF: POZ.DOK().REJ().KOD=rejkod}
               { druk:=1; {? -POZ.STR='wn' || sumawn+=POZ.SUM ||  sumama+=POZ.SUM ?};~~}
            {FI}
            {OD}
          {FI}
    {FI}
  {IF: ROZNE.UT_DOK | (~ROZNE.UT_DOK & druk=1)}
   { sumawn:=(sumawn/kurs)$2; sumama:=(sumama/kurs)$2;
     saldown:=F.SWn(sumawn,sumama); saldoma:=F.SMa(sumawn,sumama); podsum1+=sumawn; podsum2+=sumama;
     podsum3+=saldown; podsum4+=saldoma; summw+=sumawn; summm+=sumama; sumswn+=saldown; sumsma+=saldoma;~~}
   {IF: druksynt=0 & KS.SYM<>AN.SYM & ~rep_export()}
{<1'│'}{KS.SYM}{<{p[1]}'│'}{KS.NAZ}{<{p[2]}'│'}{<{p[3]}'│'}{<{p[4]}'│'}{<{p[5]}'│'}{<{p[6]}'│'}
   { druksynt:=1;~~}
   {FI}
   { SLO.cntx_psh; BUD.cntx_psh;_a:=AN.SYM;  a:=BILANSP.ASYNT-_a; {? BILANSP.ASEP=',' || a || a:=1-a ?}; kk:=0;~~}
          {FOR: BUD}
          {INDEX: 'KS'}
          {PREFIX: KS.ref}
          {DO}
             { BUD.SLU().SLU(); SLO.index('SL'); SLO.prefix(SLU.ref()); _poziom:=SLU.DL+a;
               {? SLO.find_key(_poziom)
               || kk:=0; _c:=BUD.POZ; _d:=_c+1;
                  {? tt[BUD.POZ]<>_poziom
                  || _a:=BUD.POZ; _b:=_a+1; _poz:=BUD.size; _d:=_poz-_a;
                     {! _e:=_b.._d |! tt[_e]:='' !};  tt[BUD.POZ]:=_poziom; {? BUD.next || kk:=1; BUD.prev ?}
                  || kk:=0
                  ?}
               ?}; a:=SLU.DL-a; {? BILANSP.ASEP=',' || a || a:=1-a ?};~~}
               {IF: kk=1}
               { p1:=SLO.KOD; p2:=SLO.TR; ~~}
               {IF: ~rep_export()}
               {<1'│'}{<{wc} p1}{<{p[1]}'│'}{p2}{<{p[2]}'│'}{<{p[3]}'│'}{<{p[4]}'│'}{<{p[5]}'│'}{<{p[6]}'│'}
               {FI}
               {FI}
               { wc:=wc+SLU.DL+{? BILANSP.ASEP=','  || 0 || 1 ?};~~}
          {OD}
         { SLO.cntx_pop; BUD.cntx_pop; exec('pw_opan','dok_fks_zest');licznik+=1;~~}
{IF: rep_export()}
{TR}{TD}{AN.SYM}{TD}
{_tab:=exec('kon_fun','konto',AN.SYM);
 _opis:='';
 {! _i:=1..obj_len(_tab[2])
 |! _opis+=_tab[2][_i]+' / '
 !};
 &_tab;
 _opis-3
}
{TD}{form(sumawn,,2,' ,')}{TD}{form(sumama,,2,' ,')}{TD}{form(saldown,,2,' ,')}{TD}{form(saldoma,,2,' ,')}
{ELSE}
{<1'│'}{AN.SYM}{<{p[1]}'│'}{form(KOMPEN.OPIS,80)}{<{p[2]}'│'}{>{p[3]-2}(form(sumawn,,2,' ,'))}{<{p[3]}'│'}{>{p[4]-2}(form(sumama,,2,' ,'))
}{<{p[4]}'│'}{>{p[5]-2}(form(saldown,,2,' ,'))}{<{p[5]}'│'}{>{p[6]-2}(form(saldoma,,2,' ,'))}{<{p[6]}'│'}
{FI}
  {FI}
 {FI}
  {OD}  {'Do AN';~~}
   {WYDRUKIN.DLKON:=ROK_F.SYNT;~~}
   {IF: ~rep_export()}
   {IF: druksynt=1 & licznik>1 & (ROZNE.PAR4=2 & exec('mask','konto',KS.SYM) | ROZNE.PAR4<>2)}
    {ENTIRE}
     {SUBSTITUTE: 'LINIA8'}
     {<1'│'}Suma {KS.SYM}{<{p[1]}'│'}{<{p[2]}'│'}{>{p[3]-2}(form(podsum1,,2,' ,'))}{<{p[3]}'│'}{>{p[4]-2}(form(podsum2,,2,' ,'))}{<{p[4]}'│'
}{>{p[5]-2}(form(podsum3,,2,' ,'))}{<{p[5]}'│'}{>{p[6]-2}(form(podsum4,,2,' ,'))}{<{p[6]}'│'}
     {SUBSTITUTE: 'LINIA6'}
    {ENTIRE-}
   {ELIF: licznik=1}
  {SUBSTITUTE: 'LINIA2'}
   {FI}
  {FI}
  {FI}  {'Do ROZNE.PAR1';~~}
{ WYDRUKIN.DLKON:=35; ~~}
{MACRO-}
{'------------------------------------------------------------------';~~}
{'                    CIAŁO  WYDRUKU                                ';~~}
{'------------------------------------------------------------------';~~}
{IF: ROZNE.PAR4<>0}{ pref1:='';~~}{ELSE}{ pref1:=BILANSP.ASYNT+WYDRUKI.MASKA; ~~}{FI}
{IF: ROZNE.PAR4=0 | ROZNE.PAR4=1 | ROZNE.PAR4=2}
{FOR: 'KS'}
{INDEX: 'SYM'}
{PREFIX: SSTALE.AO().ROK,pref1}
{DO}
{IF: ROZNE.PAR4<>0}{ pref2:=KS.SYM;~~}{ELSE}{ pref2:=KS.SYM+(BILANSP.ASYNT-WYDRUKI.MASKA);~~}{FI}
{SUBSTITUTE: 'SRODEK'}
{OD}
{ELIF: ROZNE.PAR4=3}
{FOR: 'KS'}
{INDEX: 'SYM'}
{PREFIX: SSTALE.AO().ROK,pref1}
{SELECT}
{DO}
{ pref2:=KS.SYM;~~}
{SUBSTITUTE: 'SRODEK'}
{OD}
{ELIF: ROZNE.PAR4=4}
{FOR: 'KS'}
{INDEX: 'SYM'}
{FROM: SSTALE.AO().ROK,ROZNE.KS_OD().SYM}
{TO: SSTALE.AO().ROK,ROZNE.KS_DO().SYM}
{DO}
{ pref2:=KS.SYM;~~}
{SUBSTITUTE: 'SRODEK'}
{OD}
{FI}
{COMMENT}------------------------------FOOTER-LAST-----------------------{COMMENT-}
{FOOTER}
{SUBSTITUTE: 'FOOT'}
{~~}
{FOOTER-}
{DIRECT}
{IF: rep_export()}
{TR}{TD:2}Suma{TD}{form(summw,,2,' ,')}{TD}{form(summm,,2,' ,')}{TD}{form(sumswn,,2,' ,')}{TD}{form(sumsma,,2,' ,')}
{TABLE-}
{ELSE}
{SUBSTITUTE: 'LINIA4'}
   {<1'│'}       Suma{<{p[2]}'│'}{>{p[3]-2}(form(summw,,2,' ,'))}{<{p[3]}'│'}{>{p[4]-2}(form(summm,,2,' ,'))}{<{p[4]}'│'}{>{p[5]-2}(form(sumswn,,2,' ,'))
}{<{p[5]}'│'}{>{p[6]-2}(form(sumsma,,2,' ,'))}{<{p[6]}'│'}
{SUBSTITUTE: 'LINIA5'}
{<1>{il} 'KONIEC WYDRUKU'@}
{FI}
{DIRECT-}
:!UTF-8
{TITLE:E.  Dokumenty bez spełnionego kręgu kosztowego}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN: petla:=0; odd:=0;
        PRN:=obj_new(@.CLASS.PRINT,17); PRN.sl_frame();
        ROZNE.REJ:=null;
        WYDRUKIN.win_edit('WYBDATA3');
        WYDRUKIN.DATAOD:=SSTALE.AO().POCZ;
        WYDRUKIN.DATADO:=SSTALE.AO().KON;
        {? var_pres('KOMUNIKAT'@)>0 || &KOMUNIKAT ?};
        CFPROC:=0.68;
        ~~
}
{END: &petla; &odd; obj_del(PRN);
      WSK.index('RODZ');
      WSK.prefix('G');
      {? WSK.first()
      || {!|? {? ($('var_pres(''k'+WSK.WART+''')'))()>0 || ($('&k'+WSK.WART+''))() ?}; WSK.next() !}
      ?};
      VAR_DEL.delete('CFPROC')
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: k_krag.rpm
Utworzony:   2003-06-10
==================================================
Zawartosc: Wydruk dokumentow bez kregu kosztowego
{COMMENT-}
{EXIT: WSK.index('RODZ');
       WSK.prefix('G');
       {? ~WSK.first() || FUN.info('Brak definicji rodzajów kont do kręgu kosztowego.'@); 1  || 0 ?}
}
{EXIT: {? SSTALE.WAL<>FINFO.NAROD
       || FUN.info('Zestawienie może być wykonywane tylko\n dla parametrów pracy z ustawioną \n walutą narodową jako walutą aktywną.'@); 1
       || ~(WYDRUKIN.edit("{? SSTALE.AO().POCZ=date(0,0,0) &
                            (~exec('dat_not','dok_fks',WYDRUKIN.DATAOD) | ~exec('dat_not','dok_fks',WYDRUKIN.DATADO))
                           || FUN.info('Podane daty muszą być z bieżącego roku bilansowego.'@);0
                           |? ((WYDRUKIN.DATAOD<SSTALE.AO().POCZ) |
                              (WYDRUKIN.DATADO>SSTALE.AO().KON) |
                              (WYDRUKIN.DATAOD>WYDRUKIN.DATADO)) & SSTALE.AO().POCZ<>date(0,0,0)
                           || FUN.info('Daty muszą się mieścić w bieżącym okresie obrachunkowym.'@);
                              {? (WYDRUKIN.DATAOD<SSTALE.AO().POCZ) | (WYDRUKIN.DATAOD>WYDRUKIN.DATADO)
                              || 'DATAOD'
                              || 'DATADO'
                              ?}
                           || 1
                           ?}
                         "
         ))
      ?}
}
{INCLUDE: wsp_pw3.rpi}
{DEFINEFONT: 'HEAD='+PAR_WYDR.FONTW+',100,M,8'}
{DEFINEFONT: 'T8G='+PAR_WYDR.FONTTAB+',100,Q,8'}
{DEFINEFONT: 'T8GB='+PAR_WYDR.FONTTAB+',100,R,8'}
{DEFINEFONT: 'TITLE='+PAR_WYDR.FONTW+',100,F,8'}
{
   PRN.add("{? DOK.ZK='T'||'!'||''?}",2,'ZK');
   {? ROZNE.REJ | OPERATOR.DEPT
   || PRN.add("(9+(DOK.REJ().KOD+(9*' ')))+(((9*' ')+$DOK.NR)+9)",9,'Rejestr#Nr w rej'@,,'#')
   || PRN.add("(9+(DOK.REJ().KOD+(9*' ')))+(((8*' ')+$DOK.NR)+8)+REJ.ODD().OD",17,'Rejestr/ Nr w rej#Jedn. księgowa'@,,'#')
   ?};
   PRN.add("$DOK.DTW+' '+$DOK.DTO",11,'D. zapisu  D. dokum.'@);
   PRN.add("(51+(DOK.NK+(51*' ')))+{? DOK.TR<>''||DOK.TR||''?}",51,'                Symbol dokumentu      #                 Opis dokumentu'@,,'#');
   ~~}
{FOR: WSK}
{INDEX:'TR'}
{PREFIX:'G'}
{DO}
{IF: WSK.WART<>'86'}
{PRN.add("_x:=($('form(k'+WSK.WART+',,2,'' ,'')'))();WSK.next();_x",14,WSK.TRESC,1);~~}
{FI}
{OD}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{ENTIRE}
{REP: PRN.fbar()}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}
{ENTIRE-}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}{ll:=charleft();~~}
{<1 XINFO.NAZ}{>{ll} XINFO.MIA+', '+date$3}
{LINE: 1}
{FONT: 'TITLE'}{SPACE: 'A'}{ll:=charleft();~~}
{<1>{ll} 'DOKUMENTY BEZ SPEŁNIONEGO KRĘGU KOSZTOWEGO OD %1 DO %2'@[$WYDRUKIN.DATAOD,$WYDRUKIN.DATADO]}
{FONT: 'HEAD'}{SPACE: 'C'}{ll:=charleft();~~}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Rejestr: '@}{IF: ROZNE.REJ<>null}{ROZNE.REJ().KOD+' - '+REJ.ODD().OD}{ELSE}{'wszystkie rejestry'@}{FI}{>{ll} {? OPERATOR.DEPT || 'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD] || 'Jednostka księgowa: wszystkie jednostki księgowe'@ ?}}
{<5 {? STALE.AO().ZAM='T'
    ||'Okres obrachunkowy: %1 %2 - zamknięty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]
    ||'Okres obrachunkowy: %1 %2 - otwarty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]
    ?}
}{>{ll} 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{FI}{FONT: 'T8GB'}{SPACE: 'C'}
{REP: PRN.fhbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{REP: PRN.h_fline()}
{OD}
{FONT: 'T8G'}{SPACE: 'C'}
{HEADER-}
{FONT: 'T8G'}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{REP: PRN.flbar()}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  {? ROZNE.REJ
   || DOK.index('PR1'); DOK.prefix('T','T',ROZNE.REJ)
   || {? OPERATOR.DEPT
      || DOK.index('PR'); DOK.prefix('T','T',OPERATOR.DEPT)
      || DOK.index('PR1'); DOK.prefix('T','T')
      ?}
   ?};
   petla:=DOK.first(); ~~
}
{WHILE: petla}
{DO}
  {IF: (DOK.DTW>=WYDRUKIN.DATAOD)&(DOK.DTW<=WYDRUKIN.DATADO)}
        { exec('kr_pocz','dok_fks');~~}
        {FOR: 'POZ'}
        {INDEX: 'DOK'}
        {PREFIX: DOK.ref}
        {DO}
          { exec('kr_poz','dok_fks'); ~~}
        {OD}
        {IF: ~exec('kr_kon','dok_fks',0,1)}
{ WSK.index('TR');WSK.prefix('G');WSK.first();~~}
{SUBSTITUTE: 'LINIA'}
        {FI}
  {FI} {'...Koniec sprawdzenia zakresu dat';~~}
  {petla:=DOK.next(); ~~}
{OD}
{FOOTER:0}
{FOOTER-}
{REP: PRN.flbar()}
{ENTIRE}
{IF: rep_export=0}{<1>{PRN.width()}'KONIEC WYDRUKU'@}
{ELSE}

{FI}
{ENTIRE-}
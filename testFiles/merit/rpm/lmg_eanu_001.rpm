:!UTF-8
{TITLE:A. Operatorzy urządzeń mobilnych - kody kreskowe}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   materean:="VAR_DEL.delete('temp','font','wybierz','dalej','wyjscie','etykieta')";
   materean();
   temp:='';
   font:='';
   wybierz:='';
   dalej:=0;
   etykieta:=PAR_WYDR.URZ_LAB;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=exec('barcode_height','kody_kresk');
   PAR_WYDR.URZ_LAB:=exec('wyb_etykiety','kody_kresk');
   wyjscie:={? PAR_WYDR.TYPDR<>null || PAR_WYDR.URZ_LAB=null ?};
   {? wyjscie=0 & PAR_WYDR.TYPDR=null & PAR_WYDR.KODKRESK=''
   || FUN.info('Brak parametryzacji drukowania kodów kreskowych.'@);
      wyjscie:=1
   ?};
   exec('ini_kom','#message','Wydruk etykiet towarowych - kody kreskowe'@);
   EANU.cntx_psh;
   VAR_DEL.delete('__zaz','__zatw');

   __zaz:=tab_tmp(1,'REF','STRING[16]','');
   __zatw:=0
}
{END:
   EANU.cntx_pop;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   exec('end_kom','#message');
   PAR_WYDR.URZ_LAB:=etykieta;
   materean(); VAR_DEL.delete('materean');
   VAR_DEL.delete('__zaz','__zatw')
}
{EXIT: wyjscie=1}
{ wyjscie:=1;~~}
{DEFINEFONT: 'courier=TexGyreCursor,,,8'}
{FONT: 'courier'}
{MACRO:'stan'}
   {ENTIRE}
   {'LOGIN: '+EANU.USER}
   {'DANE:  '+EANU.DANE}

    {IF: EANU.USER<>''}
     {BARCODE: EANU.USER;50;BARCODE_H;BARCODE_TYPE}


     {FONT: 'courier'}{SPACE: 'B'}
   {FI}
  ─────────────────────────────────────
   {ENTIRE-}
{MACRO-}
{MACRO:'etykiety'}
{IF: EANU.USER=''}
   { exec('add_kom','#message','Operator: '+EANU.DANE,7,'Brak loginu'@);~~}
{ELSE}
   { wyjscie:=0;~~}
   {IF: PAR_WYDR.TYPDR<>null}
      { wyjscie:=exec('druk','kody_kresk')=0;~~}
   {ELSE}
      {SUBSTITUTE: 'stan'}
   {FI}
{FI}
{MACRO-}
{EXIT: wybierz:=FUN.choice('Wydruk etykiet dla operatorów'@,,' Bieżącego'@, 'Wszystkich'@,'W&ybranych'@);
 {? wybierz <> 0
 || {? wybierz=1
    || wybierz:='B'
    |? wybierz=2
    || wybierz:='W'
    |? wybierz=3
    || wybierz:='Y'
    ?};0
  || "# koniec #"; 1
 ?}
}
{IF:wybierz='B'}
   {SUBSTITUTE: 'etykiety'}
{ELIF:wybierz='Y'}
   {
   _wer:=EANU.mk_sel('Operatorzy urządzeń'@,,0,'eanu_eanabcde',,,,,'U','T');
   EANU.win_fld(_wer,BPMN,'ACCESS',,,,,,,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
   EANU.win_fld(_wer,,'USER',,,20,,,'Login'@);
   EANU.win_fld(_wer,,'DANE',,,20,,,'Dane'@);
   _formula:="__zaz.clear(); {? ~__zaz.find_key($EANU.ref()) || __zaz.blank(); __zaz.REF:=$EANU.ref(); __zaz.add(1) ?}";
   EANU.win_act(_wer,,'Formuła','Wybierz'@@,,,_formula,,1,1,,,'W');
   EANU.win_btn(_wer,'text=%1,panel=right,align=begin'['Wybierz'@],'menu:W',,,,,,'noempty');
   _formula:="__zaz.clear(); {? __zaz.find_key($EANU.ref()) || __zaz.del() ?}";
   EANU.win_act(_wer,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
   EANU.win_btn(_wer,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O',,,,,,'noempty');
   _formula:="__zatw:=1; sel_exit()";
   EANU.win_act(_wer,,'Formuła','&Akceptuj'@@,,,_formula,,,,,,'A');
   EANU.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Akceptuj'@],'menu:A',,,,,,'noempty');
   EANU.win_act(_wer,,'Szukaj'@);
   EANU.win_act(_wer,,'Kolejność'@);
   _formula:="__zaz.clear();
             {? __zaz.find_key($EANU.ref())
             || BPMN.ACCESS:='T';
                EANU.actions(EANU.win_sel('?'),,'O',1);
                {? ~EANU.sel_size
                || EANU.actions_grayed(EANU.win_sel('?'),'W')
                || EANU.actions_grayed(EANU.win_sel('?'),'')
                ?}
             || BPMN.ACCESS:='N';
                {? EANU.sel_size
                || EANU.actions(EANU.win_sel('?'),,'O',1);
                   EANU.actions_grayed(EANU.win_sel('?'),'')
                || EANU.actions(EANU.win_sel('?'),,'W',1);
                   EANU.actions_grayed(EANU.win_sel('?'),'O')
                ?}
             ?}";
   EANU.win_act(_wer,,'Rekord',,,,_formula);
   EANU.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   EANU.win_act(_wer,,'Okienko',,,,,"
     {? ~__zatw
     || FUN.ask('Czy zakończyć wybór operatorów?\nUwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@)
     || 1
     ?}");

   EANU.index('AKT');
   EANU.prefix('T');
   EANU.win_sel(_wer);

   {? ~EANU.select() || __zaz.erase() ?};
   EANU.cntx_psh;
   EANU.prefix;
   ~~}
   {FOR: __zaz}
   {DO}
      {IF: EANU.seek(__zaz.REF,)}
         {SUBSTITUTE: 'etykiety'}
      {FI}
   {OD}
   {
   EANU.cntx_pop;
   ~~}
{ELIF:wybierz='W'}
   {FOR: 'EANU'}
   {INDEX: 'AKT'}
   {PREFIX: 'T'}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{FI}
{EXIT: {? wybierz='W' | wybierz='Z' || {? wyjscie=1 || FUN.info('Brak danych spełniających kryteria.'@); 1 ?} ?}}
{EXIT: wyjscie=1}
{EXIT: PAR_WYDR.TYPDR<>null}
:!UTF-8
{TITLE:J. Wydruk zbiorczy analiz zleceń}
{PRINTER:  exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   dalej:=0;
   PRN:=obj_new(@.CLASS.PRINT,12);
   PRN.s_frame();
   prn1:='PRN';
   tryb:=rep_export();
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   typ:='1';
   "----------------------------";
   "---Zawiera REF typu zlecenia, ktorym jest filtrowana tabela ANZH, jezeli   ";
   "---tabela nie jest niczym filtrowana, to zawiera null.";
   tzlecr:=null();
   "---Okienko do wybrania typu zlecenia.";
   ZTP.win_dict('SLO'); ZTP.f_clear();
   WYDRUK.fld_fml('TYPZL','AFTER_EDIT',"{? WYDRUK.TYPZL='T'
                                     || WYDRUK.efld_opt('RED10', 'mark=1',, 'ZTP','TYP');
                                        WYDRUK.efld_opt('RED10', 'enable=1',, 'ZTP','TYP');
                                        WYDRUK.efld_opt('RED10', 'enable=1',, 'ZTP','OPIS')
                                     || WYDRUK.efld_opt('RED10', 'mark=0',, 'ZTP','TYP');
                                        WYDRUK.efld_opt('RED10', 'enable=0',, 'ZTP','TYP');
                                        WYDRUK.efld_opt('RED10', 'enable=0',, 'ZTP','OPIS')
                                     ?}
                                    ");
   WYDRUK.fld_fml('ZTP','AFTER_EDIT',"1");
   WYDRUK.fld_fml('ODP','AFTER_EDIT',"{? WYDRUK.ODP='W'
                                      || WYDRUK.efld_opt('RED10', 'enable=0',, 'ODDNIA');
                                         WYDRUK.efld_opt('RED10', 'enable=0',, 'DODNIA')
                                      || WYDRUK.efld_opt('RED10', 'enable=1',, 'ODDNIA');
                                         WYDRUK.efld_opt('RED10', 'enable=1',, 'DODNIA')
                                      ?}
                                     ");
   WYDRUK.TYPZL:='N';
   WYDRUK.ZTP:=null();
   WYDRUK.ODP:='W';
   WYDRUK.ODDNIA:=date(0,0,0);
   WYDRUK.DODNIA:=date(0,0,0);
   WYDRUK.ZLEC:='W';
   query:=0;
   query1:=0;
   d1:=date(); d2:=date()
}
{END:
   &dalej;
   WYDRUK.fld_fml('TYPZL','AFTER_EDIT',"*");
   WYDRUK.fld_fml('ODP','AFTER_EDIT',"*");
   WYDRUK.fld_fml('ZTP','AFTER_EDIT',"*");
   WYDRUK.TYPZL:='';
   WYDRUK.ZTP:=null();
   WYDRUK.ODP:='';
   WYDRUK.ODDNIA:=date(0,0,0);
   WYDRUK.DODNIA:=date(0,0,0);
   WYDRUK.ZLEC:='';
   obj_del(PRN);
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   "----------------------------";
   &tzlecr;
   &query;
   &typ;
   &query1;
   &d1; &d2; &tryb
}
{COMMENT}
  Wydruk zbiorczy analiz zlecen.
  [SS] - 2001/03/23 - [7.41]
  [TS] - 2001/10/08 - [7.53] OWwSK003_Aneks1
  [MKO] - 2001/10/12 - [7.53] warianty wydruku - Uwaga 71
  [TS] - 2003/05/05 - [8.10] dodanie kolumn 'aktywna' i 'zaksiegowana'
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{MACRO: 'miscelaneous'}
  {COMMENT}---Query.------------------------------------------------{COMMENT-}
{
_wydr_naz:='zlec_010';
WYDRUKIL.index('WYDRUKI');
WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
{? ~WYDRUKIL.first()
|| WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add();
   WYDRUK.blank();
   WYDRUK.TYPZL:='N';
   WYDRUK.ODP:='W'
|| WYDRUK.TYPZL:=WYDRUKIL.SR;
   WYDRUK.ZTP:={? WYDRUK.TYPZL='N' || null() || WYDRUKIL.ZTP ?};
   WYDRUK.ODP:=WYDRUKIL.RODZFAK;
   WYDRUK.ODDNIA:=WYDRUKIL.OD_DATY;
   WYDRUK.DODNIA:=WYDRUKIL.DO_DATY;
   WYDRUK.ZLEC:=WYDRUKIL.SORTUJ
?};


WYDRUK.win_edit('RED10');
WYDRUK.efld_opt('RED10',{? WYDRUK.ZTP=null() || 'mark=0' || 'mark=1' ?},, 'ZTP','TYP');
WYDRUK.efld_opt('RED10',{? WYDRUK.ZTP=null() || 'enable=0' || 'enable=1' ?},, 'ZTP','TYP');
WYDRUK.efld_opt('RED10',{? WYDRUK.ZTP=null() || 'enable=0' || 'enable=1' ?},, 'ZTP','OPIS');
WYDRUK.efld_opt('RED10',{? WYDRUK.ODP<>'O' || 'enable=0' || 'enable=1' ?},, 'ODDNIA');
WYDRUK.efld_opt('RED10',{? WYDRUK.ODP<>'O' ||'enable=0' || 'enable=1' ?},,'DODNIA');
{? WYDRUK.edit("{? WYDRUK.TYPZL='T'
                || __CHK.record(WYDRUK,,'ZTP')
                || ''
                ?}")
|| {? WYDRUK.TYPZL='N'
   || ndx_tmp:=ANZH.ndx_tmp(,1,'A01','',0,'ZLEC','',0,'NRA','',0)
   || ndx_tmp:=ANZH.ndx_tmp(,1,'A01','',0,'ZLEC','TYP',0,'ZLEC','',0,'NRA','',0);
      tzlecr:=WYDRUK.ZTP
   ?};
   {? WYDRUK.ODP='W'
   || query:=1
   || query:=2
   ?};
   {? WYDRUK.ZLEC='Z'
   || query1:=13
   |? WYDRUK.ZLEC='O'
   || query1:=12
   || query1:=11
   ?};
   d1:=WYDRUK.ODDNIA;
   d2:=WYDRUK.DODNIA;
   WYDRUKIL.SR:=WYDRUK.TYPZL;
   WYDRUKIL.ZTP:={? WYDRUKIL.SR='N' || null() || WYDRUK.ZTP ?};
   WYDRUKIL.RODZFAK:=WYDRUK.ODP;
   WYDRUKIL.OD_DATY:=WYDRUK.ODDNIA;
   WYDRUKIL.DO_DATY:=WYDRUK.DODNIA;
   WYDRUKIL.SORTUJ:=WYDRUK.ZLEC;
   WYDRUKIL.put(1)
|| query=0
?};
~~}
{IF: query=0}
   {EXIT}
{FI}
{MACRO-}
{COMMENT}
   ---Zwraca wartosc z pozycji biezacej analizy zlecenia, odpowiadajaca
   ---rubryce o numerze okreslonym przez parametr _a. Jezeli w analizie
   ---zlecenia brakuje rubryki o numerze _a, to funkcja zwraca 0.
   ---Parametry:
   ---_a:NUMBER - numer rubryki, z ktorej bedzie odczytana wartosc
{COMMENT-}
  {anzhval:="
     ANZP.cntx_psh();
     _result:=0;
     {? _<1
     || _result:=-1;
        FUN.emsg('Funkcja ANZHVAL wydruku ANZZ_001.RPM:\nniewłaściwa liczba parametrów!')
     ?};
     {? _result=0
     || ANZP.index('NRARUB');
        ANZP.prefix(ANZH.ref(),_a);
        {? ANZP.first()
        || _result:=ANZP.WAR
        || _result:=0
        ?}
     || _result:=0
     ?};
     ANZP.cntx_pop();
     _result
   ";~~}
{COMMENT}---Filtr na typ zlecenia.------------------------------{COMMENT-}
  {COMMENT}-----Definicja wiersza wydruku pozycji anzlizy zlecenia ANZP.---{COMMENT-}
{SUBSTITUTE: 'miscelaneous'}
{ PRN.add("ANZH.ZLEC().SYM",20,'Zlecenie'@);
  {? query1=11 || PRN.add("ANZH.ZLEC().STAN",8,'Stan zlecenia'@) ?};
   PRN.add("form(ANZH.NRA,,0,,'9')",7,'Nr analizy'@,1);
   PRN.add("form(ANZH.DT)",10,'Data'@);
   "---Kolumna z wariantem analitycznym jest drukowany tylko wowczas, gdy   ";
   "---analizy nie sa filtrowane wariantem.                                 ";
   PRN.add("ANZH.OPC().NA",25,'Wariant'@);
   PRN.add("ANZH.MADE().KOD",10,'Wykonał'@);
   PRN.add("ANZH.AKT",7,'Aktywna'@);
   PRN.add("form(anzhval(ANZH.ZLEC().TYP().CRUBALL),,4)",14,'Całkowity koszt zlecenia'@,1);
   PRN.add("form(anzhval(ANZH.ZLEC().TYP().CRUBMAT),,4)",14,'Koszt materiałów'@,1);
   PRN.add("form(anzhval(ANZH.ZLEC().TYP().CRUBROB),,4)",14,'Koszt robocizny'@,1);
   PRN.add("form(anzhval(ANZH.ZLEC().TYP().CRUBJEDN),,4)",12,'Cena jednostkowa'@,1);
   PAR_WYDR.TITLE:='WYDRUK ZBIORCZY ANALIZ ZLECEŃ'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} form('Zakres:'@,10)+{? tzlecr=null()
                               || 'Wszystkie zlecenia'@
                               || 'Zlecenia typu %1'@[{? ZTP.seek(tzlecr)
                                                    || ZTP.TYP+' - '+ZTP.OPIS
                                                    || '<błąd>'
                                                   ?}]
                              ?} +
{? query=1 & query1=11 || ''
|? query=1 & query1=12 || ' '+'otwarte'@
|? query=1 & query1=13 || ' '+'zamknięte'@
|? query=2 & query1=11 || ' '+'w okresie od %1 do %2'@[form(d1),form(d2)]
|? query=2 & query1=12 || ' '+'otwarte w okresie od %1 do %2'@[form(d1),form(d2)]
|? query=2 & query1=13 || ' '+'zamknięte w okresie od %1 do %2'@[form(d1),form(d2)]
|? query=2 & query1=14 || ' '+'przygotowaniu w okresie od %1 do %2'@[form(d1),form(d2)]
?}}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{IF: tzlecr=null()}
  { ANZH.index(ndx_tmp); ANZH.prefix(typ); ~~}
{ELSE}
  { ANZH.index(ndx_tmp); ANZH.prefix(typ,tzlecr); ~~}
{FI}
{IF: ANZH.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
{IF: query=1}
       {IF: query1=11 | (query1=12 & ~(-ANZH.ZLEC().STAN) = 'O')| (query1=13 & ~(-ANZH.ZLEC().STAN) = 'Z')}
       {SUBSTITUTE: 'LINIA0'}
       {FI}
{ELIF: query=2 }
    {IF: ANZH.DT>=d1 & ANZH.DT<=d2}
      {COMMENT}------Drukowac wszystkie z okresu, czy tylko otwarte.{COMMENT-}
      {IF: query1=11 | (query1=12 & ~(-ANZH.ZLEC().STAN) = 'O')| (query1=13 & ~(-ANZH.ZLEC().STAN) = 'Z')}
       {SUBSTITUTE: 'LINIA0'}
      {FI}
    {FI}
{FI}
   { dalej:=ANZH.next(); ~~}
{OD}
{ANZH.ndx_drop(ndx_tmp);~~}

:!UTF-8
{TITLE:Zatrudnienie wg adresu zamieszkania}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   tresc:='pkd_zkwggminy';
   PAR_WYDR.TITLE:='Zatrudnienie według adresu zamieszkania'@;
   P.cntx_psh();
   GMINY.cntx_psh();
   WOJEWODZ.cntx_psh();
   POWIATY.cntx_psh();
   GMINY.index('G');
   POWIATY.index('P');
   OSOBA.cntx_psh();
   OSOBA.prefix();
   par:=obj_new(5);
   par[4]:=1;
   par[5]:=0;
   par[3]:=exec('par_wydr_ddp','pkd',
      PAR_WYDR.TITLE,,
      date(),'Data kontroli'@,
      "  {? cur_tab().OD=#0
         || FUN.emsg('Proszę podać datę kontroli.'@); 0
         || 1
         ?}
      ",,,,
      'Poziom szczegółowości'@,
      'Uproszczony - Województwo'@,
      'Pełny - Województwo, Powiat, Gmina'@,
      0
   );
   {? par[3].size()
   || par[1]:=obj_new(@.CLASS.PRINT,{? ~par[3].ASK || 3 || 5 ?});
      par[1].add("par[2].WOJ",25,'Województwo'@);
      {? par[3].ASK || par[1].add("par[2].POWIAT",25,'Powiat'@) ?};
      {? par[3].ASK || par[1].add("par[2].GM",25,'Gmina'@) ?};
      par[1].add("par[2].OS",8,'Liczba osób'@,1);
      par[1].add("par[2].ET",10,'Liczba etatów'@,1);
      par[1].s_frame();

      _in:=sql('
         select
            OSOBA.REFERENCE as REF,
            1 as OS,
            sum(H.RWY) as ET,
            \'                               \' as GM,
            \'                               \' as WOJ,
            \'                               \' as POWIAT
         from
            P join H using(P.REFERENCE,H.P) join OSOBA join H_ZM
         where
            P.REFERENCE in (select :_a.REF from :_a) and H_ZM.KZ=\'Z\' and H.OD<=to_date(:_b) and
            (H.DO is null or to_date(:_b)<=H.DO)
         group by
            OSOBA.REFERENCE,
            1,
            \'                               \' ,
            \'                               \' ,
            \'                               \' ',
         exec('dostepne_p','schemat','PKD',__F_ZATR.P,'*'),par[3].OD
      );
      {? _in.first()
      || {!
         |? {? OSOBA.seek(_in.REF)
            || {? exec('szukaj','osoba','C',par[3].OD) | exec('szukaj','osoba','S',par[3].OD)
               || _in.GM:=OS_ADRES.GMINA;
                  {? +_in.GM & (GMINY.prefix(_in.GM,_in.GM);GMINY.first())
                  || _in.POWIAT:=GMINY.POWIAT().NAZWA
                  ?};
                  {? +_in.POWIAT & (POWIATY.prefix(_in.POWIAT,_in.POWIAT);POWIATY.first())
                  || _in.WOJ:=POWIATY.WOJEWODZ().NAZWA
                  ?};
                  {? |_in.GM=''
                  || _in.GM:='<brak>'
                  ?};
                  {? |_in.WOJ=''
                  || _in.WOJ:='<brak>'
                  ?};
                  {? |_in.POWIAT=''
                  || _in.POWIAT:='<brak>'
                  ?};
                  _in.put()
               || _in.GM:='<brak>';
                  _in.WOJ:='<brak>';
                  _in.POWIAT:='<brak>';
                  _in.put
               ?}
            ?};
            _in.next()
         !}
      ?};
      {? ~par[3].ASK
      || par[2]:=sql('
            select
               WOJ,
               sum(OS) as OS,
               sum(ET) as ET
             from :_a
             group by
               WOJ
             order by
               WOJ
          ',_in)
      || par[2]:=sql('
            select
               GM,
               POWIAT,
               WOJ,
               sum(OS) as OS,
               sum(ET) as ET
             from :_a
             group by
               GM,
               POWIAT,
               WOJ
             order by
               WOJ,
               POWIAT,
               GM
          ',_in)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   POWIATY.cntx_pop();
   WOJEWODZ.cntx_pop();
   GMINY.cntx_pop();
   OSOBA.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc')
}
{EXIT: ~par[3].size}
{EXIT: ~par[2].size}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   { par[4]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Data kontroli:'@} {par[3].OD}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{  par[1].setcolor(color);
   par[1].ini_head()
}
{REP: par[1].fhbar(lmt)}
{WHILE: par[1].h_next()}{DO}
{REP: par[1].h_fmline(lmt)}
{OD}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: par[2]}
{DO}
{par[1].ini_line()}
{IF:par[5]}
{REP:par[1].fbar(lmt)}
{FI}
{par[5]:=1;~~}
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
   {OD}
{OD}

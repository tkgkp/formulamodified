:!UTF-8
{TITLE:A.  Kartoteka zapisów księgowych w bieżącym okresie}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   sstrwn:=sstrma:=sumawn:=sumama:=sstrwn1:=sstrma1:=sumawn1:=sumama1:=lm:=ll:=rsep:=vp:=lmt:=0;
   w:=obj_new(25); ww:=obj_new(10);  petla:=0; dane:=dane1:=mdc:='';font:='J';
   w9:=w10:=w91:=w92:=0; dl:=dl1:=0;  poziom:=poziom2:=0; prn1:='PRN'; prn2:=''; color:='';
   PRN:=obj_new(@.CLASS.PRINT,25); PRN.sl_frame();
   PRN1:=obj_new(@.CLASS.PRINT,15); PRN1.sl_frame();
   PRNF:=obj_new(@.CLASS.PRINT,15); PRNF.sl_frame();
   szer:=obj_new(15); lp:=1;
   {? PAR_SKID.get(39)='T' || czy_w:=1
   |? PAR_SKID.get(39)='N' || czy_w:=0
   || czy_w:=2
   ?};
   wal:={? SSTALE.WAL=FINFO.NAROD || 0 || 1 ?};
   szer[1]:=5; szer[2]:=30; szer[3]:=10; szer[4]:=11;
   {? czy_w || szer[5]:=szer[6]:=15 || szer[5]:=szer[6]:=22 ?};
   szer[9]:=szer[10]:=15; szer[7]:=8; szer[8]:=20; szer[11]:=20; szer[12]:=10; szer[13]:=0;
   {? PARWYD.REDANTRE & var_pres('KONTO',TT_STKON)=27
   || an_sym:=TT_STKON.AN;
      KS.cntx_psh();
      KS.index('SYM'); KS.prefix();
      {? KS.find_key(SSTALE.AR,TT_STKON.KS)
      || ks_ref:=KS.ref; ks_naz:=KS.NAZ
      || ks_ref:=null; ks_naz:=KS.NAZ
      ?};
      KS.cntx_pop()
   || an_sym:=AN.SYM; ks_ref:=AN.KS; ks_naz:=AN.KS().NAZ
   ?};
   czy_odd:=OPERATOR.DEPT=null
}
{END:
   VAR_DEL.delete('PRNF','PRN1');
   &sstrwn; &sstrma; &petla; &sumawn; &sumama; obj_del(w); obj_del(PRN); &lp; &rsep; &ll; &vp;
   obj_del(ww); &w9; &w10; &w91; &w92; obj_del(szer); &dl; &dl1; &wal; &dane; &dane1; &lmt; &prn1;
   &an_sym; &ks_ref; &ks_naz; &color;
   &poziom; &poziom2; &czy_w;  &sstrma1; &sstrwn1; &sumama1; &sumawn1; &czy_odd; &mdc; &lm; &font
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: kk_mies.rpm
Utworzony:   ????-??-??
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Kartoteka zapisow dla konta w biezacym okresie
{COMMENT-}
{ {? czy_w=2
  || WYDRUKIN.win_edit('WYBDATA5');
     PAR_WYDR.WSK_WAL:='T'
  || WYDRUKIN.win_edit('WYBDATA4')
  ?};
  WYDRUKIN.DATAOD:=SSTALE.AO().POCZ;
  WYDRUKIN.DATADO:=SSTALE.AO().KON; ~~}
{EXIT: ~(WYDRUKIN.edit("{? SSTALE.AO().POCZ=date(0,0,0) &
                           (~exec('dat_not','dok_fks',WYDRUKIN.DATAOD) | ~exec('dat_not','dok_fks',WYDRUKIN.DATADO))
                        ||  FUN.info('Podane daty muszą być z bieżącego roku bilansowego.'@);0
                        |? ((WYDRUKIN.DATAOD<SSTALE.AO().POCZ) |
                            (WYDRUKIN.DATADO>SSTALE.AO().KON) |
                            (WYDRUKIN.DATAOD>WYDRUKIN.DATADO)) & SSTALE.AO().POCZ<>date(0,0,0)
                        || FUN.info('Daty muszą się mieścić w bieżącym okresie obrachunkowym.'@);
                           WYDRUKIN.DATAOD:=SSTALE.AO().POCZ; WYDRUKIN.DATADO:=SSTALE.AO().KON; 0
                        || 1
                        ?}"))}
{ {? czy_w=2 || {? PAR_WYDR.WSK_WAL='T' || czy_w:=1 || czy_w:=0 ?} ?};
  {? czy_w || szer[2]:=53 || szer[2]:=46 ?};
  {? czy_odd || szer[13]:=8; szer[2]-=szer[13]+1 ?};
  {? ~rep_export()
  ||
  PRN.add("w[1]",szer[1],' Lp.'@,1); PRN.add("w[2]",szer[2],'%1 Symbol dokumentu / opis operacji'@[(szer[2]%2-16)*' ']);
  PRN.add("w[3]",szer[3],'D. zap./ nr w dz.'@);
  {? czy_odd || PRN.add("w[13]",szer[13],'Jed. ks.'@) ?};
  PRN.add("w[4]",szer[4],'  Rejestr#nr   / poz.'@,,'#');
  PRN.add("w[5]",szer[5],'   Wn (%1)'[(3+FINFO.NAROD().KOD)],1);
  PRN.add("w[6]",szer[6],'   Ma (%1)'[(3+FINFO.NAROD().KOD)],1);
  {? czy_w
  || PRN.add("w[7]",szer[7],'Waluta/ kurs'@); PRN.add("w[8]",szer[8],'Bank/ tabela'@);
     PRN.add("w[9]",szer[9],{? ~wal ||'  Wn (waluta)'@||'  Wn (%1)'[(3+SSTALE.WAL().KOD)]?},1);
     PRN.add("w[10]",szer[10],{? ~wal ||'  Ma (waluta)'@||'  Ma (%1)'[(3+SSTALE.WAL().KOD)]?},1)
  ?};
  PRN.add("w[11]",szer[11],'Typ/ symbol rozr.'@); PRN.add("w[12]",szer[12],'Data otw./ t. płatn.'@);
  PRNF.add("ww[1]",szer[1]+szer[2]+szer[3]+szer[4]+szer[13]+3+czy_odd,''); PRNF.add("ww[2]",szer[5],'',1);
  PRNF.add("ww[3]",szer[6],'',1);
  {? czy_w
  || PRNF.add("ww[4]",szer[7]+szer[8]+1,''); PRNF.add("ww[5]",szer[9],'',1);
     PRNF.add("ww[6]",szer[10],'',1)
  ?};
  PRNF.add("ww[7]",szer[11]+szer[12]+1,'')
  ||
     PRN.add("w[1]",szer[1],'Lp.');
     PRN.add("w[2]",20,'Symbol dokumentu');
     PRN.add("w[3]",22,'Opis operacji');
     PRN.add("w[4]",10,'Data zapisu');
     PRN.add("w[5]",5,'Nr w dzienniku',1);
     {? czy_odd || PRN.add("w[13]",szer[13],'Jedn. księgowa'@) ?};
     PRN.add("w[6]",9,'Rejestr'@);
     PRN.add("w[7]",9,'Nr'@,1);
     PRN.add("w[8]",9,'Poz'@,1);
     PRN.add("w[9]",szer[5],'Wn (%1)'[(3+FINFO.NAROD().KOD)],1);
     PRN.add("w[10]",szer[6],'Ma (%1)'[(3+FINFO.NAROD().KOD)],1);
     {? czy_w
     || PRN.add("w[11]",szer[7],'Waluta'@);
        PRN.add("w[12]",szer[7],'Kurs'@,1);
        PRN.add("w[14]",szer[8],'Bank'@);
        PRN.add("w[15]",szer[8],'Tabela'@);
        PRN.add("w[16]",szer[9],{? ~wal ||'  Wn (waluta)'@||'  Wn (%1)'[(3+SSTALE.WAL().KOD)]?},1);
        PRN.add("w[17]",szer[10],{? ~wal ||'  Ma (waluta)'@||'  Ma (%1)'[(3+SSTALE.WAL().KOD)]?},1);
        PRN.add("w[18]",szer[11],'Typ'@);
        PRN.add("w[19]",szer[11],'Symbol rozrachunku'@);
        PRN.add("w[20]",szer[12],'Data otwarcia rozrachunku'@);
        PRN.add("w[21]",szer[12],'Termin płatości.'@)
     ?}
  ?};
  ww[1]:=ww[2]:=ww[3]:=ww[4]:=ww[5]:=ww[6]:=ww[7]:='';
  w[1]:=an_sym; poziom:=poziom2:=0; KS.prefix();
  {? KS.seek(ks_ref)
  || BUD.cntx_psh(); exec('rpm_clear','dok_fks');
     synt:=an_sym; KOM.index('KS'); KOM.prefix();
     {? KOM.find_key(KS.ref) || exec('rpm_display','dok_fks') ?};
     poziom:=poziom2:=BUD.size();
     BUD.cntx_pop()
  || FUN.info('Nie znaleziono konta syntetycznego dla konta %1.'@[an_sym])
  ?};~~}
{INCLUDE: wsp_pw3.rpi}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{ PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{PRNF.ini_line()}
{WHILE: PRNF.next()}
{DO}
{REP: PRNF.fline()}
{OD}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='KARTOTEKA ZAPISÓW KSIĘGOWYCH NA KONTACH O PREFIKSIE %1'@[an_sym];~~}
{FONT: 'T8G'}{SPACE: 'C'}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{LINE: 1}
{IF: page=1}
{IF: rep_export()}
{'Parametry wydruku:'@}
{IF: STALE.AO().ZAM='T'}
{'Okres obrachunkowy: %1 %2 - zamknięty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{ELSE}
{'Okres obrachunkowy: %1 %2 - otwarty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{FI}
{'Od dnia: %1 do dnia: %2'@[$WYDRUKIN.DATAOD,$WYDRUKIN.DATADO]}
{'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{'Jednostka księgowa: %1'@[{? OPERATOR.DEPT || OPERATOR.DEPT().OD || 'wszystkie jednostki księgowe'@ ?}]}
{ SSTALE.AO().ROK().SYNT+KA.KS}{<{7+(+an_sym)} ks_naz}
{WHILE: poziom>0}
{DO}
  { dane:=($('  '+'KA.P'+$(poziom2-poziom+1)+'().TR'))();
    dane1:=$('KA.P'+$(poziom2-poziom+1)+'().KOD');
    dl1:=5+SSTALE.AO().ROK().SYNT+dl+(poziom2-poziom+1)*{?SSTALE.AO().ROK().SEP<>','||1||0?};
    dl+=(+dane1());~~}
{IF: dane<>''}{<{dl1} dane1()}{<{7+(+an_sym)} dane}{FI}
  { poziom-=1; ~~}
{OD}
{LINE: 1}
{ELSE}
{<3 'Parametry wydruku:'@}
{IF: STALE.AO().ZAM='T'}
{<5 'Okres obrachunkowy: %1 %2 - zamknięty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{ELSE}
{<5 'Okres obrachunkowy: %1 %2 - otwarty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{FI}
{<5 'Od dnia: %1 do dnia: %2'@[$WYDRUKIN.DATAOD,$WYDRUKIN.DATADO]}
{<5 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<5 'Jednostka księgowa: %1'@[{? OPERATOR.DEPT || OPERATOR.DEPT().OD || 'wszystkie jednostki księgowe'@ ?}]}
{<5 SSTALE.AO().ROK().SYNT+KA.KS}{<{7+(+an_sym)} ks_naz}
{WHILE: poziom>0}
{DO}
  { dane:=($('  '+'KA.P'+$(poziom2-poziom+1)+'().TR'))();
    dane1:=$('KA.P'+$(poziom2-poziom+1)+'().KOD');
    dl1:=5+SSTALE.AO().ROK().SYNT+dl+(poziom2-poziom+1)*{?SSTALE.AO().ROK().SEP<>','||1||0?};
    dl+=(+dane1());~~}
{IF: dane<>''}{<{dl1} dane1()}{<{7+(+an_sym)} dane}{FI}
  { poziom-=1; ~~}
{OD}
{FI}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}{ PRN.setcolor(color); prn1:='PRN';~~}
{SUBSTITUTE: 'TABHEAD'}
{REP: PRN.fbar()}
{FONT: 'T8G'}{SPACE: 'C'}{ PRN.setcolor('255:255:255');~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{FONT: 'T8GB'}{SPACE: 'C'}
{REP: PRN.fjbar(PRNF)}
{ ww[1]:='   SUMA STRONY'@; ww[2]:=form(sstrwn,,2,' ,'); ww[3]:=form(sstrma,,2,' ,');
   {? wal || ww[5]:=form(sstrwn1,,2,' ,'); ww[6]:=form(sstrma1,,2,' ,') ?}; ~~}
{SUBSTITUTE: 'LINIA1'}{REP: PRNF.fbar()}
{prn1:='PRNF'; ~~}
{IF: page>1}{ ww[1]:='   Z PRZENIESIENIA'@; ww[2]:=form((sumawn-sstrwn),,2,' ,'); ww[3]:=form((sumama-sstrma),,2,' ,');
  {? wal || ww[5]:=form((sumawn1-sstrwn1),,2,' ,'); ww[6]:=form((sumama1-sstrma1),,2,' ,') ?};~~}
{SUBSTITUTE: 'LINIA1'}{REP: PRNF.fbar()}
{FI}
{ ww[1]:='   DO PRZENIESIENIA'@; ww[2]:=form(sumawn,,2,' ,'); ww[3]:=form(sumama,,2,' ,');
  {? wal || ww[5]:=form(sumawn1,,2,' ,'); ww[6]:=form(sumama1,,2,' ,')?};~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRNF.flbar()}
{SUBSTITUTE: 'CHARFOOT'}{FONT: 'T8G'}{SPACE: 'C'}{ sstrwn:=sstrma:=sstrwn1:=sstrma1:=0; ~~}
{FOOTER-}
{COMMENT}----------------------WYDRUK-------------------------------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ {? OPERATOR.DEPT
  || {? SSTALE.WAL<>FINFO.NAROD
     || POZ.index('OKWAL_O'); POZ.prefix('T','T',SSTALE.WAL,OPERATOR.DEPT,an_sym)
     || POZ.index('OKWAL_O1'); POZ.prefix('T','T',OPERATOR.DEPT,an_sym)
     ?}
  || {? SSTALE.WAL<>FINFO.NAROD
     || POZ.index('OKWAL'); POZ.prefix('T','T',SSTALE.WAL,an_sym)
     ||  POZ.index('OKWAL1'); POZ.prefix('T','T',an_sym)
     ?}
  ?}; petla:=POZ.first(); ~~
}
{IF: rep_export()}
{WHILE: petla}
{DO}
{IF: (POZ.DOK().DTW>=WYDRUKIN.DATAOD)&(POZ.DOK().DTW<=WYDRUKIN.DATADO)}
    { w[1]:=$lp; lp+=1;
      w[2]:=POZ.DOK().NK;
      w[3]:=POZ.OP;
      w[4]:=POZ.DOK().DTW$3;
      w[5]:=$POZ.DOK().NRDZ;
      w[13]:=POZ.DOK().REJ().ODD().OD;
      w[6]:=POZ.DOK().REJ().KOD;
      w[7]:=$POZ.DOK().NR+(6-(+$POZ.DOK().NR))*' ';
      w[8]:=form(POZ.POZ,5,,'99');
      {? -POZ.STR='wn'
      || w[9]:=form(POZ.SUM,,2,' ,'); w[10]:=form(0,,2,' ,')
      || w[10]:=form(POZ.SUM ,,2,' ,'); w[9]:=form(0,,2,' ,')
      ?};
      {? (~wal & POZ.WAL<>null) | wal
      || {? -POZ.STR='wn'
         || w[16]:=form(POZ.SUMW,,2,' ,'); w[17]:=form(0,,2,' ,')
         || w[17]:=form(POZ.SUMW,,2,' ,'); w[16]:=form(0,,2,' ,')
         ?};
         w[11]:=(3+POZ.WAL().KOD); w[14]:=POZ.DOK().BANK().KOD
      || w[16]:=w[17]:=w[11]:=w[14]:=w[12]:=w[15]:=''
      ?};
      w[18]:=POZ.TID; {? POZ.ID<>'' || w[20]:=POZ.DO$3 || w[20]:=''?};
      {? (~wal & POZ.WAL<>null) | wal
      || w[12]:=form(POZ.KURS,8,4,' ,');
         {? POZ.DOK().BANK().KOD<>''
         || w[15]:=POZ.DOK().TKRS().SYM
         || w[15]:=''
         ?}
      ?};
      w[19]:=POZ.ID;
      {? POZ.ID<>'' || w[21]:=POZ.TP$3 || w[21]:=''?};~~}
      {SUBSTITUTE: 'LINIA'}
      { petla:=POZ.next();~~}
{FI}
{OD}
{ELSE}
{WHILE: petla}
{DO}
{IF: (POZ.DOK().DTW>=WYDRUKIN.DATAOD)&(POZ.DOK().DTW<=WYDRUKIN.DATADO)}
    {IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
    { w[1]:=$lp; lp+=1; w[2]:=POZ.DOK().NK;  w[3]:=POZ.DOK().DTW$3;
      w[4]:=POZ.DOK().REJ().KOD; w[13]:=POZ.DOK().REJ().ODD().OD;
     {? (~wal & POZ.WAL<>null) | wal
     || {? -POZ.STR='wn'
        || w[9]:=form(POZ.SUMW,,2,' ,'); w[10]:=form(0,,2,' ,')
        || w[10]:=form(POZ.SUMW,,2,' ,'); w[9]:=form(0,,2,' ,')
        ?};
        w[7]:=(3+POZ.WAL().KOD); w[8]:=POZ.DOK().BANK().KOD
     || w[7]:=w[8]:=w[9]:=w[10]:=''
     ?};
     w[11]:=POZ.TID; {? POZ.ID<>'' || w[12]:=POZ.DO$3 || w[12]:=''?};
     {? -POZ.STR='wn'
     || w[5]:=form(POZ.SUM,,2,' ,'); w[6]:=form(0,,2,' ,')
     || w[6]:=form(POZ.SUM ,,2,' ,'); w[5]:=form(0,,2,' ,')
     ?}; ~~}
{ENTIRE}
{SUBSTITUTE: 'LINIA'}
{ w[1]:=w[5]:=w[6]:=w[9]:=w[10]:=w[13]:=''; w[2]:=POZ.OP; w[3]:=$POZ.DOK().NRDZ;
  w[4]:=$POZ.DOK().NR+(6-(+$POZ.DOK().NR))*' '+form(POZ.POZ,5,,'99');
  {? (~wal & POZ.WAL<>null) | wal
  || w[7]:=form(POZ.KURS,8,4,' ,');
     {? POZ.DOK().BANK().KOD<>''
     || w[8]:=POZ.DOK().TKRS().SYM
     || w[8]:=''
     ?}
  ?};  w[11]:=POZ.ID; {? POZ.ID<>'' || w[12]:=POZ.TP$3 || w[12]:=''?};~~}
{SUBSTITUTE: 'LINIA'}
{ {? -POZ.STR='wn'
  || sstrwn+=POZ.SUM;  sumawn+=POZ.SUM;
     {? wal || sstrwn1+=POZ.SUMW; sumawn1+=POZ.SUMW ?}
  || sstrma+=POZ.SUM;  sumama+=POZ.SUM;
     {? wal || sstrma1+=POZ.SUMW; sumama1+=POZ.SUMW ?}
  ?};
~~}
{ petla:=POZ.next();~~}
{IF: petla & lineleft()>6}
{REP: PRN.fbar() }
{FI}
{ENTIRE-}
{FI}
{OD}
{FI}
{FOOTER}{FOOTER-}
{ENTIRE}
{FONT: 'T8GB'}{SPACE: 'C'}
{REP: PRN.fjbar(PRNF)}
{prn1:='PRNF'; ~~}
{IF: page>1}
{ ww[1]:='   SUMA STRONY'@; ww[2]:=form(sstrwn,,2,' ,'); ww[3]:=form(sstrma,,2,' ,');
  {? wal || ww[5]:=form(sstrwn1,,2,' ,'); ww[6]:=form(sstrma1,,2,' ,') ?}; ~~}
{SUBSTITUTE: 'LINIA1'}{REP: PRNF.fbar()}
{ ww[1]:='   Z PRZENIESIENIA'@; ww[2]:=form((sumawn-sstrwn),,2,' ,'); ww[3]:=form((sumama-sstrma),,2,' ,');
  {? wal || ww[5]:=form((sumawn1-sstrwn1),,2,' ,'); ww[6]:=form((sumama1-sstrma1),,2,' ,') ?}; ~~}
{SUBSTITUTE: 'LINIA1'}{REP: PRNF.fbar()}{FI}
{ ww[1]:='   RAZEM'@; ww[2]:=form(sumawn,,2,' ,'); ww[3]:=form(sumama,,2,' ,');
  {? wal || ww[5]:=form(sumawn1,,2,' ,'); ww[6]:=form(sumama1,,2,' ,') ?}; ~~}
{SUBSTITUTE: 'LINIA1'}{REP: PRNF.fbar()}
{ ww[1]:='   SALDO'@; ww[2]:=form((F.SWn(sumawn,sumama)),,2,' ,'); ww[3]:=form((F.SMa(sumawn,sumama)),,2,' ,');
  {? wal || ww[5]:=form((F.SWn(sumawn1,sumama1)),,2,' ,'); ww[6]:=form((F.SMa(sumawn1,sumama1)),,2,' ,') ?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRNF.flbar()}
{IF: ~rep_export()}{<1>{PRN.width()}'KONIEC WYDRUKU'@}{FI}
{ENTIRE-}

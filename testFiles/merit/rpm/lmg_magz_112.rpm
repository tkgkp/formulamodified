:!UTF-8
{TITLE:M. Stany dostaw w przedziale czasowym}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_107:="
      VAR_DEL.delete('color','prn1','prn2','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');

      VAR_DEL.delete('k','w','s','sg','fm','oddnia','nadzien','iw','dokl','kin','kgr','j2','dok2');
      VAR_DEL.delete('PRN','PRNF');

      VAR_DEL.delete('wyjscie','X_Xa','X_Xd')";
   mag_107();

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   ilk:=10;
   k :=obj_new(ilk); "--szerokosci kolumn--";
   w :=obj_new(ilk); "--wartosci kolumn--";
   s :=obj_new(2); "--razem--";
   sg:=obj_new(2); "--razem grupa--";
   dokl:=0; "--dokladnosc ilosci--";
   dok2:=0;
   j2:='N';

   k[1]:=12;
   k[2]:=55;
   k[3]:=5;
   k[4]:=16;
   k[5]:=16;
   k[6]:=16;
   k[7]:=16;
   k[8]:=5;
   k[9]:=16;
   k[10]:=16;
   w[1]:=w[2]:=w[3]:=w[6]:=w[8]:='';
   w[4]:=w[5]:=w[6]:=w[7]:=w[9]:=w[10]:=0;
   s[1]:=s[2]:=0;
   sg[1]:=sg[2]:=0;
   tryb:=rep_export();
   kin:=kgr:='';

   PRN:=obj_new(@.CLASS.PRINT,ilk+2); PRN.s_frame();

   fm:='N';
   oddnia:=date(ST.AR,ST.AM,1);
   nadzien:=date(ST.AR,ST.AM,0);
   iw:='N';

   _wydr_naz:='mag_112';
   _wydr_edi:='MAG_112';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ?};
   {? WYDRUKIL.RADIOB_1=2 & (exec('upr_cm','ceny')=0 | ST.MAG().IL='T') || WYDRUKIL.RADIOB_1:=1 ?};

   WYDRUKIL.win_edit(_wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("
         _noupr:=exec('upr_cm','ceny')=0;
         {? ~_noupr & ST.MAG().IL='T' & WYDRUKIL.RADIOB_1=2
         || FUN.info('Magazyn tylko ilościowy zmieniono parametr Rodzaj na wydruk ilościowy.'@);
            WYDRUKIL.RADIOB_1:=1
         ?};
         {? chk_rec('D')<>''
         || 'D'
         |? WYDRUKIL.RADIOB_1=2 & _noupr
         || FUN.info('Brak uprawnień do wartości magazynowych.'@); 'RADIOB_1'
         || ''
         ?}")
   || WYDRUKIL.put();
      wyjscie:=0;
      fm:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
      oddnia:=WYDRUKIL.OD_DATY;
      nadzien:=WYDRUKIL.DO_DATY;
      iw:={? WYDRUKIL.RADIOB_1=1 || 'N' || 'T' ?};
      j2:={? WYDRUKIL.CHKBOX02=1 || 'T' || 'N' ?};
      wyjscie:=exec('filtr_m','material',fm,'N','T',1)
   ?}
}
{END:
   mag_107(); VAR_DEL.delete(mag_107)
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1
|| PRN.add("form(kgr)",8,'Grupa materiałowa'@);
   PRN.add("form(kin)",20,'Indeks'@)
?};
PRN.add("form(w[1])",k[1],'Data dostawy'@);
PRN.add("form(w[2])",k[2],'Typ dokumentu'@+'/'+'Nr dostawy'@+'/'+'Pozycja dostawy'@);
PRN.add("form(w[3])",k[3],'jm'@);
PRN.add("form(w[6],,dokl)",k[6],'Stan ilościowy na dzień %1'@[$oddnia],1);
PRN.add("form(w[4],,dokl)",k[4],'Stan ilościowy na dzień %1'@[$nadzien],1);
{? j2='T' & tryb=1
|| PRN.add("form(w[8])",k[8],'jm'@);
   PRN.add("form(w[10],,dok2)",k[10],'Stan ilościowy 2 na dzień %1'@[$oddnia],1);
   PRN.add("form(w[9],,dok2)",k[9],'Stan ilościowy 2 na dzień %1'@[$nadzien],1)
?};
{? iw='T' || PRN.add("form(w[7],,2)",k[7],'Stan wartościowy na dzień %1'@[$oddnia],1) ?};
{? iw='T' || PRN.add("form(w[5],,2)",k[5],'Stan wartościowy na dzień %1'@[$nadzien],1) ?};

"--obliczenia--";
X_Xa.index(X_Xa.ndx_tmp(,,'KTM',,));
{? X_Xa.first()
||
   X_Xd:=tab_tmp(3
      ,'GR' ,'STRING[153]' ,'Grupowanie'
      ,'RDK','INTEGER'     ,'Ref dok. dostawy'
      ,'NDK','STRING[20]'  ,'Name dok. dostawy'
      ,'SYM','STRING[20]'  ,'Symbol dostawy'
      ,'D'  ,'DATE'        ,'Data dostawy'
      ,'JM' ,'STRING[10]'  ,'jm'
      ,'PSI','REAL'        ,'Początkowy stan ilościowy'
      ,'PSW','REAL'        ,'Początkowy stan wartościowy'
      ,'SI' ,'REAL'        ,'Stan ilościowy'
      ,'SW' ,'REAL'        ,'Stan wartościowy'
      ,'KTM','STRING[50]'  ,'Indeks'
      ,'MGR','STRING[8]'   ,'Grupa materiałowa'
      ,'J2' ,'STRING[10]'  ,'jm'
      ,'PS2','REAL'        ,'Początkowy stan ilościowy'
      ,'S2' ,'REAL'        ,'Stan ilościowy');
   _il:=$X_Xa.size();
   _ii:=0;
   M.cntx_psh();
   M.prefix();
   {!
   |? M.seek(BB.sqlint(X_Xa.REF),);
      _ii+=1; echo('Trwają obliczenia dla materiału: %1 %2'@[M.KTM,M.N],'%1 z %2'@[$_ii,_il]);
      _j2:=M.J2<>null();
      OKR.cntx_psh();
      OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      || _cont:=1;
         ND.cntx_psh();DK.cntx_psh();
         {!
         |? ND.use('nagdo'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
            DK.use('dokma'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
            DK.index('DOKMAT');
            DK.prefix('T',M.ref(),ST.MAG,OKR.ROK);
            {? DK.first()
            || {!
               |? {? DK.M().RODZ='T' & DK.N().D<=nadzien
                  || _grupa:=M.KTM+' - '+M.N;
                     {? X_Xd.find_key(_grupa,DK.RDK,DK.NDK)
                     || {? ND.D<oddnia | ND.D=oddnia & #DK.ref=DK.RDK & DK.name=DK.NDK
                        || X_Xd.PSI+={? DK.PLUS='T' || DK.IL || -1*DK.IL ?};
                           X_Xd.PSW+={? DK.PLUS='T' || DK.WAR || -1*DK.WAR ?};
                           X_Xd.PS2+={? _j2 || {? DK.PLUS='T' || DK.IL2 || -1*DK.IL2 ?} || 0 ?}
                        ?};
                        X_Xd.SI+={? DK.PLUS='T' || DK.IL || -1*DK.IL ?};
                        X_Xd.SW+={? DK.PLUS='T' || DK.WAR || -1*DK.WAR ?};
                        X_Xd.S2+={? _j2 || {? DK.PLUS='T' || DK.IL2 || -1*DK.IL2 ?} || 0 ?};
                        X_Xd.put()
                     || X_Xd.blank(1);
                        X_Xd.GR:=_grupa;
                        X_Xd.RDK:=DK.RDK;
                        X_Xd.NDK:=DK.NDK;
                        X_Xd.KTM:=M.KTM;
                        X_Xd.MGR:=M.MGR().KOD;
                        ND.cntx_psh(); DK.cntx_psh();
                        DK.use(DK.NDK);
                        DK.prefix();
                        {? DK.seek(DK.RDK,)
                        || ND.use(ref_name(DK.N));
                           X_Xd.SYM:=DK.N().TYP().T+'/'+form(ND.NR,,,'99')+'/'+form(DK.P,,,'99');
                           X_Xd.D:=ND.D
                        ?};
                        ND.cntx_pop(); DK.cntx_pop();
                        X_Xd.JM:=M.J().KOD;
                        X_Xd.J2:={? _j2 || M.J2().KOD || '' ?};
                        {? ND.D<oddnia | ND.D=oddnia & #DK.ref=DK.RDK & DK.name=DK.NDK
                        || X_Xd.PSI:={? DK.PLUS='T' || DK.IL || -1*DK.IL ?};
                           X_Xd.PSW:={? DK.PLUS='T' || DK.WAR || -1*DK.WAR ?};
                           X_Xd.PS2:={? _j2 || {? DK.PLUS='T' || DK.IL2 || -1*DK.IL2 ?} || 0 ?}
                        ?};
                        X_Xd.SI:={? DK.PLUS='T' || DK.IL || -1*DK.IL ?};
                        X_Xd.SW:={? DK.PLUS='T' || DK.WAR || -1*DK.WAR ?};
                        X_Xd.S2:={? _j2 || {? DK.PLUS='T' || DK.IL2 || -1*DK.IL2 ?} || 0 ?};
                        X_Xd.add()
                     ?}
                  |? DK.N().D>nadzien
                  || _cont:=0
                  ?};
                  _cont=1 & DK.next()
               !}
            ?};
            _cont=1 & OKR.next()
         !};
         ND.cntx_pop(); DK.cntx_pop()
      ?};
      OKR.cntx_pop();
      X_Xa.next()
   !};
   M.cntx_pop();
   "-- usuniecie zapisow z zerowymi stanami --";
   {? X_Xd.first() || {! |? {? X_Xd.SI=0 & X_Xd.PSI=0 || X_Xd.del() || X_Xd.next() ?} !} ?};
   echo()
?};

PAR_WYDR.TITLE:=
   {? iw='T'
   || 'ILOŚCIOWO - WARTOŚCIOWE ZESTAWIENIE STANÓW DOSTAW W PRZEDZIALE CZASOWYM'@
   || 'ILOŚCIOWE ZESTAWIENIE STANÓW DOSTAW W PRZEDZIALE CZASOWYM'@
   ?};
prn1:='PRN'; prn2:='PRNF';
~~}
{IF: X_Xa.first()=0 | X_Xd.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Od dnia: %1'@[(oddnia$6)]}
{<{lmt+1}}{'Do dnia: %1'@[(nadzien$6)]}
{IF:fm='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
{IF:j2='T'}{<{lmt+1}}{'Druga jednostka ewidencyjna'@}{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{ prn1:='PRN';~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Od dnia: %1'@[(oddnia$6)]}
{<{lmt+1}}{'Do dnia: %1'@[(nadzien$6)]}
{IF:fm='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
{IF:j2='T'}{<{lmt+1}}{'Druga jednostka ewidencyjna'@}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE}
{FOOTER-}
{COMMENT}   Pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{
rsep:=PAR_WYDR.RSEP;
w[1]:=0;
M.index('RODZ'); M.prefix('T');
~~}
{COMMENT}   wydruk pozycji                                           {COMMENT-}
{FOR: 'X_Xd'}
{INDEX: X_Xd.ndx_tmp(,,'GR',,,'D',,)}
{DO}
   {FIRST}
   {
   dokl:=exec('fld_prec','#field',X_Xd,'SI');
   dok2:=exec('fld_prec','#field',X_Xd,'S2');
   kolejny:=0;
   ~~}
   {FIRST-}
   {IF: tryb<>1 & kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {
   w[1]:=X_Xd.D;
   w[2]:=X_Xd.SYM;
   w[3]:=X_Xd.JM;
   w[6]:=X_Xd.GR;
   w[4]:=X_Xd.SI;
   w[5]:=X_Xd.SW;
   w[6]:=X_Xd.PSI;
   w[7]:=X_Xd.PSW;
   w[8]:=X_Xd.J2;
   w[9]:={? X_Xd.J2<>'' || X_Xd.S2|| '' ?};
   w[10]:={? X_Xd.J2<>'' || X_Xd.PS2 || '' ?};
   kin:=X_Xd.KTM;
   kgr:=X_Xd.MGR;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
   {
   sg[1]+=w[5]; s[1]+=w[5];
   sg[2]+=w[7]; s[2]+=w[7];
   ~~}
   {IF: j2='T' & X_Xd.J2<>'' & tryb<>1}
      {
      rsep:=0;
      w[1]:='';
      w[2]:='';
      w[3]:=X_Xd.J2;
      w[4]:=X_Xd.S2;
      w[5]:='';
      w[6]:=X_Xd.PS2;
      w[7]:='';
      ~~}
      {SUBSTITUTE: 'LINIA0'}
      { rsep:=PAR_WYDR.RSEP;~~}
   {FI}
   {
   kolejny:=0; rsep:=PAR_WYDR.RSEP;
   ~~}
   {TOTAL: X_Xd.GR}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
      {IF: tryb<>1}
      {
      w[1]:=X_Xd.D;
      w[2]:=X_Xd.SYM;
      w[3]:=X_Xd.JM;
      w[6]:=X_Xd.GR;
      w[4]:=X_Xd.SI;
      w[5]:=X_Xd.SW;
      w[6]:=X_Xd.PSI;
      w[7]:=X_Xd.PSW;
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+2+3;
      ~~}
      {IF: lineleft>0 & lineleft<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {
      kolejny:=1; rsep:=0;
      PRN.n_frame();
      w[1]:='';
      w[2]:={? iw='T'
            || {? grp_fld()='' || 'Razem Bez lokalizacji'@ || 'Razem %1'@[grp_fld()] ?}
            || {? grp_fld()='' || 'Bez lokalizacji'@ || grp_fld() ?}
            ?};
      PRN.FORM[2]:=0;
      w[3]:=w[4]:='';
      w[5]:=form(sg[1],,2,'.,');
      w[6]:='';
      w[7]:=form(sg[2],,2,'.,');
      ~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {FONT: 'T8G'}{SPACE: 'C'}
      {
      sg[1]:=sg[2]:=0;
      PRN.s_frame();
      PRN.FORM[2]:=1
      ;~~}
      {FI}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{ PRN.n_frame();~~}
{IF: tryb<>1 & iw='T'}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {
   w[1]:='';
   w[2]:='Razem'@; PRN.FORM[2]:=0;
   w[3]:=w[4]:='';
   w[5]:=form(s[1],,2,'.,');
   w[6]:='';
   w[7]:=form(s[2],,2,'.,');
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
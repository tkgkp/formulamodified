:!UTF-8
{TITLE:A. Zapotrzebowanie na aktywnej liście}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   ODZAM.cntx_psh();
   ODZAMPT.cntx_psh();
   VAR_DEL.delete('PRN','PRNSUM','__TAB');
   PRN:=obj_new(@.CLASS.PRINT,10); PRN.s_frame();
   PRNSUM:=obj_new(@.CLASS.PRINT,10); PRNSUM.s_frame();
   prn1:='PRN';
   prn2:='PRNSUM';
   waslbar:=0;
   color:='';
   tryb:=rep_export();
   lm:=ll:=vp:=lmt:=rsep:=0;
   parametr:=FUN.ask('Czy umieścić na wydruku atrybuty wyposażenia?'@);
   razem:='RAZEM'@;
   bylo:=0; total:=''
}
{END:
   ODZAM.cntx_pop();
   ODZAMPT.cntx_pop();
   VAR_DEL.delete('PRN','PRNSUM','__TAB');
   &prn2;
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   &parametr;
   &razem;
   &bylo; &total; &tryb
}
{IF: parametr=~~}{EXIT}{FI}
{COMMENT}
  Wydruk zestawienia wyposażenia na aktualnej liście zapotrzebowania
  GZ [18.02]
{COMMENT-}
{COMMENT}--Definicja wiersza zestawienia.--------------------{COMMENT-}
{ {? parametr=1
  || PRN.add("__TAB.MGRP",20,'Podgrupa materiałowa'@);
     PRN.add("__TAB.KTM",20,'Indeks materiałowy'@);
     PRN.add("__TAB.ATR",40,'Atrybuty'@);
     PRN.add("form(__TAB.IL,,0)",15,'Liczba'@,1);
     PRNSUM.add("razem",86,'Opis'@,1,,,,,,,3);
     PRNSUM.add("form(sum('ilosc'),,0)",15,'Liczba'@,1)
  || PRN.add("__TAB.MGRP",20,'Podgrupa materiałowa'@);
     PRN.add("__TAB.KTM",20,'Indeks materiałowy'@);
     PRN.add("__TAB.N",30,'Nazwa materiału'@);
     PRN.add("form(__TAB.IL,,0)",15,'Liczba'@,1);
     PRNSUM.add("razem",76,'Opis'@,1,,,,,,,3);
     PRNSUM.add("form(sum('ilosc'),,0)",15,'Liczba'@,1)
  ?};
  PAR_WYDR.TITLE:='ZAPOTRZEBOWANIE NA AKTYWNEJ LIŚCIE'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~tryb}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------MACRO-----------------------{COMMENT-}
{MACRO: 'usttab'}
{  {? parametr=0
   || __TAB:=sql('
         select
            ODZAM.DATA_W,
            ODZAM.SYM,
            MGRP.KOD as MGRP,
            M.KTM,
            M.N,
            ODZAMPT.IL,
            MGRP.KOD as TOTAL
         from ODZAMPT
            join ODZAM using (ODZAMPT.ODZAM, ODZAM.REFERENCE)
            join MGRP using(ODZAMPT.MGRP, MGRP.REFERENCE)
            left join M using (ODZAMPT.M, M.REFERENCE)
         where ODZAM.STAN=\'T\'
         order by TOTAL, MGRP, KTM
      ')
   || __TAB:=sql('
         select
            ODZAM.DATA_W,
            ODZAM.SYM,
            MGRP.KOD as MGRP,
            M.KTM,
            M.N,
            ODZAMPT.IL,
            MGRP.KOD as TOTAL,
            \'                                        \' as ATR,
            DK_C.REFERENCE as DK_C
         from ODZAMPT
            join ODZAM using (ODZAMPT.ODZAM, ODZAM.REFERENCE)
            join MGRP using(ODZAMPT.MGRP, MGRP.REFERENCE)
            left join M using (ODZAMPT.M, M.REFERENCE)
            left join DK_C using (ODZAMPT.DK_C, DK_C.REFERENCE)
         where ODZAM.STAN=\'T\'
         order by TOTAL, MGRP, KTM
      ');
      DK_C.cntx_psh();
      DK_C.prefix();
      {? __TAB.first()
      || {!
         |? {? __TAB.DK_C<>'' & DK_C.seek(__TAB.DK_C)
            || __TAB.ATR:='';
               {! _i:=1.. exec('il_atr','mat_atr')
               |! _war:=($('DK_C.WAR'+form(_i,-2,0,'99')))() ;
                  {? _war<>''
                  || __TAB.ATR+=(_war + '; ')
                  ?}
               !};
               __TAB.ATR:=__TAB.ATR-1;
               __TAB.put()
            ?};
            __TAB.next()
         !}
      ?};
      DK_C.cntx_pop()
   ?};
   ~~
}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{SUBSTITUTE: 'usttab'}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: __TAB.first}
{<{lmt+1} 'Lista zaopatrzenia: %1, Planowana data dostawy: %2'@[__TAB.SYM,$__TAB.DATA_W]}
{<{lmt+1} ' '}
{FI}
{FOR: __TAB}
{DO}
   {FIRST}
      {IF: tryb<>1}
         {IF: lineleft<7}{PAGE}
         {FI}
         {<{lmt+1} 'Podgrupa materiałowa: %1'@[__TAB.MGRP]}
      {FI}
      {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1; total:=__TAB.TOTAL; ~~}
   {FIRST-}
   {IF: tryb | lineleft > 5}{SUBSTITUTE: 'LINIA0'}{ADD: ilosc;#;__TAB.IL}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {FI}
{TOTAL: __TAB.TOTAL}
   {IF: tryb<>1}
      {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIAFS'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIAFS'}
      {FI}
      {IF: total<>__TAB.TOTAL}
         {IF: tryb=0 & lineleft<7}{PAGE}
         {ELIF: tryb=2}{<{lmt+1} ''}
         {FI}
         {<{lmt+1} 'Podgrupa materiałowa: %1'@[__TAB.MGRP]}
         {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1; total:=__TAB.TOTAL; ~~}
      {FI}
    {FI}
{OD}

:!UTF-8
{TITLE:Przebieg zatrudnienia}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='historia';
   PAR_WYDR.TITLE:='Przebieg zatrudnienia'@;
   par:=obj_new(5);
   P.cntx_psh();
   par[3]:=0;
   _upr:=exec('uprawnieniawrap','pkd',
              'Przebiegu zatrudnienia'@,
              'PKD_EZK_PPZA',
              'PKD_EZK_RPZA'
             );
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || _plac:=FUN.ask('Czy uwzględniać dane płacowe?'@);

      par[1]:=obj_new(@.CLASS.PRINT,6+(8*_plac));
      par[1].add("H.OD$1",10,'Od dnia'@);
      par[1].add("H.DO$1",10,'Do dnia'@);
      par[1].add("H.ST().ST",30,'Stanowisko'@);
      par[1].add("H.KZ().KZ",3,'Kod'@);
      par[1].add("H.RU().K",6,'Rodzaj'@);
      par[1].add("form(H.WY,5,3)",6,'Wymiar'@,1);
      {? _plac ||
         par[1].add("H.S1",20,MS.name('H','S1'),1,,,,,'20,2');
         par[1].add("{? H.CZYWAL='T' || H.WAL().KOD || 'PLN' ?}",3,'Wal'@);
         par[1].add("H.S2",15,MS.name('H','S2'),1,,,,,'15,2');
         par[1].add("{? H.S2T='P' || '%' |? H.CZYWAL2='T' || H.WAL().KOD || 'PLN' ?}",3,'Wal'@);
         par[1].add("H.S3",15,MS.name('H','S3'),1,,,,,'15,2');
         par[1].add("{? H.S3T='P' || '%' |?  H.CZYWAL3='T' || H.WAL().KOD || 'PLN' ?}",3,'Wal'@);
         par[1].add("H.S4",15,MS.name('H','S4'),1,,,,,'15,2');
         par[1].add("{? H.CZYWAL4='T' || H.WAL().KOD || 'PLN' ?}",3,'Wal'@)
      ?};
      par[1].s_frame();

      prn1:='par[1]';
      lmt:=rsep:=0;
      vp:=1;

      par[2]:=par[4]:=par[5]:=0;
      par[3]:=1;
      b_rat:=h_rat:=1;
      params_set('P',exec('wybierz_parses','pracownik','PPL').P)
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat')
}
{COMMENT}OLD: phist.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}NEW: p_osoba.rpi{COMMENT-}
{EXIT: ~par[3]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: par[5]<>P.ref}{ par[4]+=1; par[5]:=P.ref; ~~ }{FI}
{<{ceil(lmt*b_rat)+1}}{form(par[4],,,'9.')} {INCLUDE: p_prac.rpi}
{FONT: 'T8'}
{SPACE: 'B'}
{MACRO-}
{MACRO: 'hist'}
{IF: ~par[2]}
   { vp:=~lineleft; ~~ }
   {IF: lineleft & lineleft<=6}
      {SUBSTITUTE: 'LINIAF'}
      {PAGE}
   {FI}
   {SUBSTITUTE: 'LINIAF'}
   {SUBSTITUTE: 'pracownik'}
   {REP: par[1].fhbar(lmt)}
   { par[2]:=1; ~~ }
{FI}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{IF: lineleft<=2}
   { par[2]:=0; ~~ }
{FI}
{MACRO-}
{MACRO: tresc}
{ par[2]:=0; ~~ }
{FOR: H}{INDEX: '_HISTDAT'}{PREFIX: P.ref}{DO}
   {SUBSTITUTE: 'hist'}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft; ~~ }
{FONT: 'T8'}{ b_rat/=charleft; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft-par[1].width())/2); ~~ }
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{FOOTER: 0}
   { vp:=0; ~~ }
   {SUBSTITUTE: 'LINIAF'}
{FOOTER-}
:!UTF-8
{TITLE:Okresy nieskładkowe}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   tresc:='wokrnskl';
   PAR_WYDR.TITLE:='Okresy nieskładkowe'@;
   par:=obj_new(7);
   par[3]:=0;
   P.cntx_psh();
   P_INFO.cntx_psh();
   P_INFO.index('OD');
   _upr:=exec('uprawnieniawrap','pkd','danych ubezpieczeniowych'@,'PKD_DOS_PPDU','PKD_DOS_PRDU');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || par[4]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,
         "  {? cur_tab().DO<>#0
            || {? cur_tab().OD<=cur_tab().DO
               || 1
               || FUN.emsg('Data początku okresu nie może być większa od daty końcowej'@)
               ?}
            || 1
            ?}
         ",
         date(0,0,0),'Od dnia'@,,
         date(0,0,0),'Do dnia'@
      );
      {? par[3]:=par[4].size()
      || par[1]:=obj_new(@.CLASS.PRINT,{? rep_export() || 8 || 5 ?});
         {? rep_export()
         || par[1].add("P_NSKL.OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("P_NSKL.OSOBA().PIERWSZE+' '+P_NSKL.OSOBA().DRUGIE",41,'Imiona'@);
            par[1].add("P.T",9,'Nr teczki'@)
         ?};
         par[1].add("P_NSKL.OD$1",10,'Od dnia'@);
         par[1].add("P_NSKL.DO$1",10,'Do dnia'@);
         par[1].add("P_INFO.prefix(exec('ref_firma','ustawienia'),@.P.OSOBA);
                     {? P_INFO.find_le(@.P_NSKL.OD)
                     || (4+|P_INFO.TTUB().KOD)+' '+(1+|P_INFO.PREM().KOD)+' '+(1+|P_INFO.STNP().KOD)
                     || ''
                     ?}",20,'Tytuł ubezpieczenia'@);
         par[1].add("P_NSKL.TTUB().KOD",6,'Symbol'@);
         par[1].add("P_NSKL.LS",10,'Lista płac'@);
         par[1].s_frame();

         par[2]:=tab_tmp(,'REF','INTEGER','Osoba');

         prn1:='par[1]';
         lmt:=rsep:=0;
         vp:=1;

         par[7]:=1;
         par[5]:=par[6]:=0;
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   P.cntx_pop();
   P_INFO.cntx_pop()
}
{COMMENT}OLD: wokrnskl.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par[3]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[7]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[7]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Zakres dat: od %1 do %2'@[par[4].OD$1,par[4].DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{FOOTER: 0}
{FOOTER-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W10'}
{SPACE: 'B'}
{IF: par[6]<>P.ref()}{ par[5]+=1; par[6]:=P.ref(); ~~ }{FI}
{IF: ~rep_export()}
   {<{int((136/160)*lmt)+1}}{form(par[5],,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{MACRO-}
{MACRO: 'okr_nskl'}
{IF: ~par[3]}
   { vp:=~lineleft(); ~~ }
   {IF: lineleft() & lineleft()<=6}
      {SUBSTITUTE: 'LINIAF'}
      {PAGE}
   {FI}
   {SUBSTITUTE: 'LINIAF'}
   {SUBSTITUTE: 'pracownik'}
   {REP: par[1].fhbar(lmt)}
   { par[3]:=1; ~~ }
{FI}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{IF: lineleft()<=2}
   { par[3]:=0; ~~ }
{FI}
{MACRO-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[2].add();
      par[3]:=0;
      ~~
   }
   {REP: '{FOR: P_NSKL}{INDEX: \'OD\'}'+
         {? par[4].OD=#0 & par[4].DO=#0
            || '{PREFIX: exec(\'ref_firma\',\'ustawienia\'),P.OSOBA}'
            || '{FROM: exec(\'ref_firma\',\'ustawienia\'),P.OSOBA,par[4].OD}'+
               '{TO: exec(\'ref_firma\',\'ustawienia\'),P.OSOBA'+{? par[4].DO=#0 || '' || ',par[4].DO' ?}+'}'
         ?}+'{DO}{SUBSTITUTE: \'okr_nskl\'}{OD}'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{FOOTER: 0}
   { vp:=0; ~~ }
   {SUBSTITUTE: 'LINIAF'}
{FOOTER-}
:!UTF-8
{TITLE:D. Analiza sprzedaży/zakupu według materiałów i usług}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   fir_03:="
      VAR_DEL.delete('k','w','o1','s1','z1','o2','s2','z2','PRN','XXX');
      VAR_DEL.delete('wyjscie','kod','naz');
      VAR_DEL.delete('grkh','kh','kh_odb','ktm','rok','a_o','a_s','a_z','a_rok0','gr','dokl','jakadata');
      VAR_DEL.delete('wydr_naz');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep')
   ";
   exec('upr_set','ceny');
   fir_03();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   k :=obj_new(8);
   w :=obj_new(14);
   o1 :=obj_new(14); s1 :=obj_new(14); z1 :=obj_new(14);
   o2 :=obj_new(14); s2 :=obj_new(14); z2 :=obj_new(14);
   dokl:=obj_new(14);
   PRN := obj_new(@.CLASS.PRINT,16); PRN.s_frame();

   k[1] :=20;
   k[2] :=15;
   k[3] :=10;
   k[4] :=20;
   k[5] :=25;
   k[6] :=15;
   k[7] :=15;
   k[8] :=15;
   "--wartosci linii--";
   {! _ii:=1 .. 14 |! w[_ii]:=0 !};
   'sumy posrednie'@;
   {! _ii:=1 .. 14 |! o1[_ii]:=0 !};
   {! _ii:=1 .. 14 |! s1[_ii]:=0 !};
   {! _ii:=1 .. 14 |! z1[_ii]:=0 !};
   "--sumy koncowe--";
   {! _ii:=1 .. 14 |! o2[_ii]:=0 !};
   {! _ii:=1 .. 14 |! s2[_ii]:=0 !};
   {! _ii:=1 .. 14 |! z2[_ii]:=0 !};

   kh :=grkh :=kh_odb :=ktm :=null;
   rok :=a_o :=a_s :=a_z :=a_rok0 := gr:=0;

   jakadata:=WYDRUKIL.RADIOB_1:=0;

   wydr_naz:='FIR_03';
   wydr_edi:={? var_pres('wydr_edi')=2 & wydr_edi='FIRA02' || 'FIRA02' || 'FIR_02' ?};

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OZ.USER,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OZ.USER;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   {? WYDRUKIL.CHKBOX08=2 & exec('upr_cm','ceny')=0 || WYDRUKIL.CHKBOX08:=0 ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("
         {? WYDRUKIL.CHKBOX01=1 & WYDRUKIL.CHKBOX02=1
            | WYDRUKIL.CHKBOX01=1 & WYDRUKIL.CHKBOX03=1
            | WYDRUKIL.CHKBOX02=1 & WYDRUKIL.CHKBOX03=1
         ||
            FUN.info('Można wybrać tylko jedną opcję z:\n\n'
               'Czy wybierać kontrahenta?\n'
               'Czy wybierać grupę kontrahentów?\n'
               'Czy wybierać odbiorcę?'@); 'CHKBOX07'
         |? WYDRUKIL.CHKBOX06=0 & WYDRUKIL.CHKBOX07=0 & WYDRUKIL.CHKBOX08=0
         ||
            FUN.info('Należy wybrać co najmniej jedną z opcji:\n\n'
               'Czy obrót ilościowy?\n'
               'Czy wartość sprzedaży?\n'
               'Czy wartość zakupu?'@); 'CHKBOX07'
         |? WYDRUKIL.CHKBOX07=1 & exec('upr_cs','ceny')=0
         ||
            FUN.info('Brak uprawnień do wartości sprzedaży.'@); 'CHKBOX07'
         |? WYDRUKIL.CHKBOX08=1 & exec('upr_cm','ceny')=0
         ||
            FUN.info('Brak uprawnień do wartości magazynowych.'@); 'CHKBOX08'
         ||
            ~~
         ?}
         ")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         {? wydr_edi='FIRA02' || kh :=KH.ref(); grkh :=KH.GRKH ?};
         {? WYDRUKIL.CHKBOX01=1 & wydr_edi='FIR_02'
         || KH.cntx_psh(); KH.index('KOD'); KH.prefix(2); KH.win_sel('SEL');
            {? KH.select() || kh :=KH.ref(); grkh :=KH.GRKH || wyjscie :=1 ?};
            KH.cntx_pop()
         ?};
         {? wyjscie=0
         || {? WYDRUKIL.CHKBOX02=1 & wydr_edi='FIR_02'
            || GRKH.cntx_psh(); GRKH.index('KOD'); GRKH.prefix(); GRKH.win_sel('SEL');
               {? GRKH.select() || grkh :=GRKH.ref() || wyjscie :=1 ?};
               GRKH.cntx_pop()
            ?}
         ?};
         {? wyjscie=0
         || {? WYDRUKIL.CHKBOX03=1
            || KH_ODB.cntx_psh(); KH_ODB.index('KH_ODB');
               {? wydr_edi='FIR_02' || KH_ODB.prefix() || KH_ODB.prefix(kh) ?};
               _acr_sel:=KH_ODB.mk_sel('Grupy kontrahentów'@,'T',0,'fir_03_kh_odb');
               KH_ODB.win_fld(_acr_sel,,'KH','KOD',,8,,0,'Kod'@);
               KH_ODB.win_fld(_acr_sel,,'KH','NAZ',,30,,0,'Nazwa kontrahenta'@);
               KH_ODB.win_fld(_acr_sel,,'KOD',,,8,,0,'Kod'@);
               KH_ODB.win_fld(_acr_sel,,'NAZ',,,30,,0,'Nazwa odbiorcy'@);
               KH_ODB.win_fld(_acr_sel,,'MIASTO',,,15,,0,'Miasto'@);
               KH_ODB.win_fld(_acr_sel,,'MIASTO',,,15,,0,'Ulica'@);
               KH_ODB.win_act(_acr_sel,0,'Formuła','Wybierz'@,,'Zatwierdzenie wybranj pozycji'@,"sel_exit()",,1);
               KH_ODB.win_act(_acr_sel,0,'Kolejność');
               KH_ODB.win_sel(_acr_sel);
               {? KH_ODB.select() || kh_odb :=KH_ODB.ref(); kh :=KH_ODB.KH; grkh :=KH_ODB.KH().GRKH || wyjscie :=1 ?};
               KH_ODB.cntx_pop()
            ?}
         ?};
         {? wyjscie=0
         || {? WYDRUKIL.CHKBOX04=1
            || M.cntx_psh(); M.index('MATKTM'); M.prefix(); M.win_sel('SEL');
               {? M.select() || ktm :=M.ref() || wyjscie :=1 ?};
               M.cntx_pop()
            ?}
         ?};
         {? wyjscie=0
         || {? WYDRUKIL.CHKBOX05=1
            || OKR.cntx_psh(); OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
               _acr_sel:=OKR.mk_sel('Lata obrachunkowe'@,'T',0,'fir_03_rok',,,10);
               OKR.win_fld(_acr_sel,,'ROK',,,30,,0,'Rok'@);
               OKR.win_act(_acr_sel,0,'Formuła','Wybierz'@,,'Zatwierdzenie wybranj pozycji'@,"sel_exit()",,1);
               OKR.win_act(_acr_sel,0,'Kolejność');
               OKR.win_sel(_acr_sel);
               OKR.find_key(ST.AR);
               {? OKR.select(,1,2) || rok :=OKR.ROK || wyjscie :=1 ?};
               OKR.cntx_pop()
            || rok :=ST.AR
            ?}
         ?};
         jakadata:=WYDRUKIL.RADIOB_1;
         a_o    :=WYDRUKIL.CHKBOX06;
         a_s    :=WYDRUKIL.CHKBOX07;
         a_z    :=WYDRUKIL.CHKBOX08;
         a_rok0 :=WYDRUKIL.CHKBOX09;
         gr     :={? wydr_edi='FIRA02' || 0 || WYDRUKIL.CHKBOX10 ?}
      ?}
   ?}
}
{END:
   "--wydr_edi wyjatkowo usuwane tutaj a nie w fir_03 poniewaz moze byc powolywane przed wydrukiem--";
   fir_03(); VAR_DEL.delete('fir_03','wydr_edi')
}
{IF: __PARSES.setEnv('LSP_CEN')=0}{EXIT}{FI}
{IF: wyjscie=1}{EXIT}{FI}
{COMMENT}   MACRO wiersze                                            {COMMENT-}
{MACRO: 'wiersze'}
{
x:=0;
kod:=naz:='';
M.index('MATKTM');
M.prefix(XXX.KTM, XXX.KTM);
{? M.first()
||
   kod:=M.KTM+{? a_o || ' ['+XXX.JM+']' || '' ?};
   naz:=M.N
?}
;~~}
{IF: suma>0}{FONT: 'T8GB'}{SPACE: 'C'}{FI}
{IF: a_o}
   {
   typ:='O';
   {? suma=0
   ||
      {? x=0
      || kod+=' O'
      || kod:='O';
         naz:=''
      ?};
      x+=1;
      w[1]:=XXX.ROKP_O;
      w[14]:=XXX.ROK_O;
      {! _i:=2..13 |! ($('w['+$_i+']:='+'XXX.M'+$(_i-1)+'_O'))() !};
      {! _i:=1..14 |! ( $('o1['+$_i+']+='+'w['+$(_i)+']') )() !};
      {! _i:=1..14 |! ( $('o2['+$_i+']+='+'w['+$(_i)+']') )() !}
   |? suma=1
   ||
      kod:='Razem O';
      naz:='';
      {! _i:=1..14 |! ( $('w['+$_i+']:='+'o1['+$(_i)+']') )() !}
   ||
      kod:='Razem O';
      naz:='';
      {! _i:=1..14 |! ( $('w['+$_i+']:='+'o2['+$(_i)+']') )() !}
   ?}
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{IF: a_s}
   {
   typ:='S';
   {? suma=0
   ||
      {? x=0
      || kod+=' S'
      || kod:='S';
         naz:=''
      ?};
      x+=1;
      w[1]:=XXX.ROKP_S;
      w[14]:=XXX.ROK_S;
      {! _i:=2..13 |! ( $('w['+$_i+']:='+'XXX.M'+$(_i-1)+'_S') )() !};
      {! _i:=1..14 |! ( $('s1['+$_i+']+='+'w['+$(_i)+']') )() !};
      {! _i:=1..14 |! ( $('s2['+$_i+']+='+'w['+$(_i)+']') )() !}
   |? suma=1
   ||
      kod:='Razem S';
      naz:='';
      {! _i:=1..14 |! ( $('w['+$_i+']:='+'s1['+$(_i)+']') )() !}
   ||
      kod:='Razem S';
      naz:='';
      {! _i:=1..14 |! ( $('w['+$_i+']:='+'s2['+$(_i)+']') )() !}
   ?}
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{IF: a_z}
   {
   typ:='Z';
   {? suma=0
   ||
      {? x=0
      || kod+=' Z'
      || kod:='Z';
         naz:=''
      ?};
      x+=1;
      w[1]:=XXX.ROKP_Z;
      w[14]:=XXX.ROK_Z;
      {! _i:=2..13 |! ( $('w['+$_i+']:='+'XXX.M'+$(_i-1)+'_Z') )() !};
      {! _i:=1..14 |! ( $('z1['+$_i+']+='+'w['+$(_i)+']') )() !};
      {! _i:=1..14 |! ( $('z2['+$_i+']+='+'w['+$(_i)+']') )() !}
   |? suma=1
   ||
      kod:='Razem Z';
      naz:='';
      {! _i:=1..14 |! ( $('w['+$_i+']:='+'z1['+$(_i)+']') )() !}
   ||
      kod:='Razem Z';
      naz:='';
      {! _i:=1..14 |! ( $('w['+$_i+']:='+'z2['+$(_i)+']') )() !}
   ?}
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{IF: suma>0}{FONT: 'T8G'}{SPACE: 'C'}{FI}
{MACRO-}
{
PRN.add("form(kod)",k[1],'Kod materiału/usługi',1);
{? a_rok0 || PRN.add("form(w[1],,{? typ='O' || dokl[1] || 2 ?},' 9')",k[2], $(rok-1),1) ?};
PRN.add("form(w[2],,{? typ='O' || dokl[2] || 2 ?},' 9')",k[3], '01/'+$(rok),1);
PRN.add("form(w[3],,{? typ='O' || dokl[3] || 2 ?},' 9')",k[3], '02/'+$(rok),1);
PRN.add("form(w[4],,{? typ='O' || dokl[4] || 2 ?},' 9')",k[3], '03/'+$(rok),1);
PRN.add("form(w[5],,{? typ='O' || dokl[5] || 2 ?},' 9')",k[3], '04/'+$(rok),1);
PRN.add("form(w[6],,{? typ='O' || dokl[6] || 2 ?},' 9')",k[3], '05/'+$(rok),1);
PRN.add("form(w[7],,{? typ='O' || dokl[7] || 2 ?},' 9')",k[3], '06/'+$(rok),1);
PRN.add("form(w[8],,{? typ='O' || dokl[8] || 2 ?},' 9')",k[3], '07/'+$(rok),1);
PRN.add("form(w[9],,{? typ='O' || dokl[9] || 2 ?},' 9')",k[3], '08/'+$(rok),1);
PRN.add("form(w[10],,{? typ='O' || dokl[10] || 2 ?},' 9')",k[3], '09/'+$(rok),1);
PRN.add("form(w[11],,{? typ='O' || dokl[11] || 2 ?},' 9')",k[3], '10/'+$(rok),1);
PRN.add("form(w[12],,{? typ='O' || dokl[12] || 2 ?},' 9')",k[3], '11/'+$(rok),1);
PRN.add("form(w[13],,{? typ='O' || dokl[13] || 2 ?},' 9')",k[3], '12/'+$(rok),1);
PRN.add("form(w[14],,{? typ='O' || dokl[14] || 2 ?},' 9')",k[2], $(rok),1);
PRN.add("form(naz,k[4])",k[4],'Opis');

"--obliczenia--";
exec('dane','faktury_wydruk',grkh,kh,kh_odb,ktm,date(rok,1,1),date(rok,12,0),a_o,a_s,a_z,a_rok0,null,0,gr,jakadata);

PAR_WYDR.TITLE:='Analiza sprzedaży/zakupu według materiałów i usług'@;
prn1:='PRN'
;~~}
{IF: XXX.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{IF: wydr_edi='FIR_02'}{<{lmt+1}}{'Grupowanie: '@}{IF:gr}{'wg grup kontrahentów'@}{ELSE}{' bez ograniczeń'@}{FI}{FI}
{<{lmt+1}}{'Grupa kontrahentów: '@}{IF:grkh<>null}{<{lmt+1}}{(WYDRUKIL.GRKH_OD :=grkh; WYDRUKIL.GRKH_OD().KOD+' '+GRKH.NAZ)}{ELSE}{'bez ograniczeń'@}{FI}
{<{lmt+1}}{'Kontrahent: '@}{IF:kh<>null}{(WYDRUKIL.KH_OD :=kh; WYDRUKIL.KH_OD().KOD+' '+KH.NAZ)}{ELSE}{'bez ograniczeń'@}{FI}
{<{lmt+1}}{'Odbiorca: '@}{IF:kh_odb<>null}{(FAKS.KH_ODB :=kh_odb; FAKS.KH_ODB().KOD+' '+KH_ODB.NAZ+' '+KH_ODB.MIASTO)}{ELSE}{'bez ograniczeń'@}{FI}
{<{lmt+1}}{'Materiał/Usługa: '@}{IF:ktm<>null}{(WYDRUKIL.KTM_OD :=ktm; WYDRUKIL.KTM_OD().KTM+' '+M.N)}{ELSE}{'bez ograniczeń'@}{FI}
{<{lmt+1}}{'Rok: '+$rok}
{<{lmt+1}}{{? a_rok0 || 'Analizować rok poprzedni?: T'@ || 'Analizować rok poprzedni?: N'@ ?}}{
   {? a_o || ', [O] - Obrót ilościowy?: T'@ || ', [O] - Obrót ilościowy?: N'@ ?}}{
   {? a_s || ', [S] - Wartość sprzedaży?: T'@ || ', [S] - Wartość sprzedaży?: N'@ ?}}{
   {? a_z || ', [Z] - Wartość zakupu?: T'@ || ', [Z] - Wartość zakupu?: N'@ ?}}
{FONT: 'T8G'}{ vp:=1;~~}
{LINE}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: 'XXX'}{INDEX: XXX.ndx_tmp(,,'GRKH',,,'KTM',,,'JM',,)}
{DO}
   { {! _ii:=1..14 |! w[_ii]:=0 !};~~}
   {FIRST}
      {
      dokl[1]:=exec('fld_prec','#field',XXX,'ROKP_O');
      {! _ii:=2..13
      |!
         dokl[_ii]:=exec('fld_prec','#field',XXX,'M'+$(_ii-1)+'_O')
      !};
      dokl[14]:=exec('fld_prec','#field',XXX,'ROK_O');
      ~~}
      {FONT: 'T8GB'}
      {IF: gr}
         {<{lmt+1}}{grkh:=XXX.GRKH;'Grupa:'+grkh}
      {ELSE}
         { grkh:='';~~}
      {FI}
      {ENTIRE}
      {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}
      {ENTIRE-}
   {FIRST-}
   {IF: XXX.GRKH<>grkh & gr}
      { suma:=1;~~}
      {SUBSTITUTE: 'wiersze'}
      {IF: lineleft()>0 & lineleft()>=2}
         {FOOTER}{FOOTER-}{SUBSTITUTE: 'LINIAF'}
      {ELIF: lineleft()>0}
         {HEADER}{HEADER-}
         {PAGE}
      {ELSE}
         {HEADER}{HEADER-}
      {FI}
      {FONT: 'T8GB'}
      {<{lmt+1}}{grkh:=XXX.GRKH;'Grupa:'+grkh}
      {FONT: 'T8G'}
      {
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+2
      ;~~}
      {IF: lineleft()>0 & lineleft-il_lin>=4}
         {ENTIRE}
         {FONT: 'T8GB'}
         {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}
         {ENTIRE-}
      {ELIF: lineleft()>0}
         {PAGE}
      {FI}
      {HEADER}
      {SUBSTITUTE: 'HEAD'}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'TABHEAD'}
      {HEADER-}
      {FOOTER: 0}
      {SUBSTITUTE: 'LINIAF'}
      {FOOTER-}
      {
      {! _ii:=1 .. 14 |! o1[_ii]:=0 !};
      {! _ii:=1 .. 14 |! s1[_ii]:=0 !};
      {! _ii:=1 .. 14 |! z1[_ii]:=0 !}
      ;~~}
   {FI}
   { suma:=0;~~}
   {SUBSTITUTE: 'wiersze'}
{OD}
{ suma:=1;~~}
{SUBSTITUTE: 'wiersze'}
{IF: lineleft()>0 & lineleft()>=2}{FOOTER}{FOOTER-}{SUBSTITUTE: 'LINIAF'}{ELIF: lineleft()>0}{PAGE}{FI}
{IF: gr<>0 & wydr_edi<>'FIRA02'}
   {FONT: 'T8GB'}
   {<{lmt+1}}{'Podsumowanie analizy'@}
   {IF: lineleft()>0 & lineleft()-1>=4}
      {ENTIRE}
      {FONT: 'T8GB'}
      {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}
      {ENTIRE-}
   {ELIF: lineleft()>0}
      {PAGE}
   {FI}
   {FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {FOOTER-}
   { suma:=2;~~}
   {SUBSTITUTE: 'wiersze'}
{FI}
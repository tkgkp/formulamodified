:!UTF-8
{TITLE:Potrącenia pracownicze}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='potrprac';
   PAR_WYDR.TITLE:='Potrącenia pracownicze'@;

   par:=obj_new(9);
   par[8]:="1";
   par[9]:=0;
   _upr:=exec('uprawnieniawrap','pkd','Dane płacowe - potrącenia'@, 'PPL_PLL_PPOT','PPL_PLL_RPOT');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[2]:=tab_tmp(1,'OKR','INTEGER','Czy z okresu','OD','DATE','Początek okresu','DO','DATE','Koniec okresu');
      par[2].OKR:=0;
      par[2].OD:=par[2].DO:=#0;
      par[2].add();

      par[3]:=
         "  {? _a
            || par[2].fld_fml('OD','BEFORE_EDIT',\"1\");
               par[2].fld_fml('DO','BEFORE_EDIT',\"1\");
               par[2].OD:={? VAR.JESTLIST || date(O.R,O.M,1) || date(date()~1,date()~2,1) ?};
               par[2].DO:={? VAR.JESTLIST || date(O.R,O.M,0) || date(date()~1,date()~2,0) ?}
            || par[2].fld_fml('OD','BEFORE_EDIT',\"0\");
               par[2].fld_fml('DO','BEFORE_EDIT',\"0\");
               par[2].OD:=par[2].DO:=#0
            ?};
            par[2].efld_opt(par[4],'enable='+$par[2].OKR,,'OD');
            par[2].efld_opt(par[4],'enable='+$par[2].OKR,,'DO')
         ";
      par[4]:=par[2].mk_edit(PAR_WYDR.TITLE,0,'ppl_potrprac');
      par[2].win_esep(par[4],'Parametry wydruku'@);
      par[2].win_efld(par[4],,'OKR',,,,,,
         'Potrącenia z wybranego okresu'@,,,
         'radio-buttons',,'Wszystkie'@,"0",'Z okresu'@,"1"
      );
      par[3](0);
      par[2].fld_fml('OKR','AFTER_EDIT',"par[3](par[2].OKR)");
      par[2].win_efld(par[4],,'OD',,,11,,,'Początek okresu'@);
      par[2].win_efld(par[4],,'DO',,,11,,,'Koniec okresu'@);
      par[2].efld_opt(par[4],'enable='+$par[2].OKR,,'OD');
      par[2].efld_opt(par[4],'enable='+$par[2].OKR,,'DO');
      par[2].efld_opt(par[4],'mark=1',,'OD');
      par[2].efld_opt(par[4],'mark=1',,'DO');
      exec('ok_esc','#window',par[2],par[4]);
      par[2].win_edit(par[4]);
      {? par[9]:=par[2].edit(
            " {? par[2].OKR=0
              || ''
              |? (_chk:=__CHK.record(par[2],,'OD','DO'))<>''
              || return(_chk)
              |? par[2].DO<par[2].OD
              || FUN.emsg(
                    '\"%1\" nie może być wcześniejszy niż \"%2\".'@
                    [MS.name(par[2],'DO'),MS.name(par[2],'OD')]
                 );
                 return('OD')
              || 1
              ?}
            "
         )
      || par[8]:="(KOM_OS.OD<=par[2].DO | par[2].DO=#0) & (KOM_OS.DO>=par[2].OD | KOM_OS.DO=#0)";
         par[1]:=obj_new(@.CLASS.PRINT,8);
         {? rep_export()
         || par[1].add(
               "  '%1 %2 %3 %4'
                     [  KOM_OS.OSOBA().NAZWISKO,
                        OSOBA.PIERWSZE,
                        {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                        '(Nr teczki %1)'@[|P.T]
                     ]
               ",40,'Pracownik'@)
         ?};
         par[1].add("KOM_OS.KOM_RP().N",30,'Nazwa potrącenia'@);
         par[1].add("KOM_OS.A",7,'Aktywne'@);
         par[1].add("KOM_OS.OD$1",10,'Spłata od'@);
         par[1].add("KOM_OS.WART",15,'Wartość'@,1,,,,,'12,2');
         par[1].add("KOM_OS.RATA",15,'Rata'@,1,,,,,'12,2');
         par[1].add("exec('licz_splate_komornik','lista_licz')",15,'Spłacono'@,1,,,,,'12,2');
         par[1].add("KOM_OS.SYG",20,'Sygnatura'@);
         par[1].s_frame();

         prn1:='par[1]';
         lmt:=rsep:=par[5]:=par[7]:=0;
         vp:=b_rat:=h_rat:=par[6]:=1;
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?};
   P.cntx_psh();
   KOM_SP.cntx_psh()
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   P.cntx_pop();
   KOM_SP.cntx_pop()
}
{COMMENT}OLD: potrprac.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~par[9]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[6]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[6]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {IF: par[8]="1"}
      {<{ceil(lmt*h_rat)+1}'Wszystkie potrącenia'@}
   {ELSE}
     {<{ceil(lmt*h_rat)+1}'Potrącenia z okresu: od %1 do %2'@[par[2].OD$1,par[2].DO$1]}
   {FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par[7]+=1,,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{REP: par[1].fhbar(lmt)}
{  par[5]:=1; ~~ }
{MACRO-}
{MACRO: 'potr_dane'}
{IF: ~par[5]}
   {SUBSTITUTE: 'pracownik'}
{FI}
{  par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{COMMENT}

   Przeglądaj kartotekę potrąceń w podanym lub pełnym zakresie dat.
   Pełny zakres - wszystkie potrącenia, ograniczony zakres - od daty
   do daty.

{COMMENT-}
{MACRO: tresc}
{ par[5]:=0; ~~ }
{ENTIRE}
{REP: '{FOR: KOM_OS}{INDEX: \'KOM_OS\'}{PREFIX: exec(\'ref_firma\',\'ustawienia\'),P.OSOBA}'+
      '{DO}{IF: par[8]()}{SUBSTITUTE: \'potr_dane\'}{FI}{OD}'}
{IF: par[5]}
   {REP: par[1].flbar(lmt)}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{  lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
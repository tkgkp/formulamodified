:!UTF-8
{TITLE:Arkusz spisu z natury}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,12); PRN.s_frame();
   c:=obj_new(12);
   VAR_DEL.delete('TMP_KOM','TMP_KOMS');
   {! _i:=1..12 |! c[_i] := '' !};
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   __std:=null; slicznik:=0;
   par1:=0; par2:=0; par3:=0; par4:=0; par5:=0; par6:=''; par7:=''; dod_podp:=0;
   sum_wart:=0; licznik:=0; inw_index:=''; inw_prefix:=0;
   podpisy:=0
}
{END:
   obj_del(PRN);
   obj_del(c);
   VAR_DEL.delete('TMP_KOM','TMP_KOMS');
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3; &par4; &par5; &par6; &par7; &dod_podp;
   &sum_wart; &licznik; &inw_index; &inw_prefix
   &podpisy; &__std; &slicznik
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_inp_arkusz.rpm
Utworzony  : 2016-04-19
Zawartość  : Arkusz spisu z natury
==================================================
{COMMENT-}
{PAR_WYDR.cntx_psh();~~}
{INCLUDE: wsp_pw.rpi}
{PAR_WYDR.cntx_pop();~~}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("c[1]",5,'Lp.',,'#');
      PRN.add("c[2]",20,'Nr inwentarz.',,'#');
      PRN.add("c[3]",30,'Nazwa środka',,'#');
      PRN.add("c[4]",25,'Nr fabryczny',,'#');
      PRN.add("c[5]",11,'Data zakupu',,'#');
      PRN.add("{? type_of(c[6]) = 1 || form(c[6],,2,' ,') || c[6] ?}",14,'Wartość',1,'#');
      PRN.add("c[7]",10,'Pomieszcz.',,'#');
      PRN.add("c[8]",20,'Użytkownik',,'#');
      PRN.add("c[9]",34,'Uwagi',,'#');

      PAR_WYDR.TITLE:='Arkusz spisu z natury: '+SRXN.NAZWA; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{<1 'PARAMETRY ARKUSZA:'}
{COMMENT} ---- Arkusz, Jedn. ks., Jedn. org., Pomieszczenie ---- {COMMENT-}
{
  par1:='J. księgowa.: '+SRXN.ODD().OD;
  par2:='J. organizacyjna: '+SRXN.JORG().SYMBOL;
  par3:='Pomieszczenie:'+SRXN.POM().S;
  par6:='Inwentaryzację wykonano: '+{? SRXI.DZ<>date(0,0,0) || SRXI.DZ$6 || $SRXI.DZ ?};
  par7:='Stan na dzień: '+{? SRXI.DS<>date(0,0,0) || SRXI.DS$6 || $SRXI.DS ?}
;~~}
{IF: SRXN.ODD().OD<>''}{<1 par1}{ELSE}{<1 'Wszystkie jedn. ks.'}{FI}{<40 par6}
{IF: SRXN.JORG().SYMBOL<>''}{<1 par2}{<40 par7}{ELSE}{<1 par7}{FI}
{IF: SRXN.POM().S<>''}{<1 par3}{FI}
{LINE: 1}
{<1 'Imię i nazwisko osoby odpowiedzialnej materialnie: '+'.'*100}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: podpisy=0}{SUBSTITUTE: 'TABHEAD'}{FI}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{FOR: SRXP}{INDEX: 'SRXP'}{PREFIX: SRXN.ref()}
 {DO}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
 {
      SRXD.cntx_psh();
      SRXD.index('SRXI');
      SRXD.prefix(SRXI.ref(),SRXP.ref());
      {? SRXD.first() || _wczytany:=1 || _wczytany:=0 ?};
      licznik+=1;
      slicznik:=0;
      c[1]:=licznik;
      c[2]:=SRXP.NRI;
      c[3]:=SRXP.NST;
      {? _wczytany=1
      || c[4]:=SRXD.NF;
         c[5]:=form(SRXD.DZ,,1);
         c[6]:=SRXD.WARP
      || c[4]:='';
         c[5]:='';
         c[6]:=''
      ?};
      c[7]:=SRXP.POM().S;
      c[8]:=SRXP.OSOBA().NAZWISKO+' '+SRXP.OSOBA().PIERWSZE;
      {? _wczytany=1
      || _tmp:=SRXD.memo_txt(,1,'UWAGI');
         {? +_tmp>255 || _tmp:=255+_tmp ?};
         _tmp:=gsub(_tmp,'\n',' ')
      || _tmp:=''
      ?};
      c[9]:=_tmp;
      sum_wart+={? _wczytany & SRXD.GRP<>'E' || SRXD.WARP || 0 ?};
      SRXD.cntx_pop()

 ;~~}
  {IF: lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
   c[1]:='';
   c[2]:='';
   c[3]:='Ogółem ARKUSZ:';
   c[4]:='';
   c[5]:='';
   c[6]:=sum_wart;
   c[7]:='';
   c[8]:='';
   c[9]:=''
;~~}
{SUBSTITUTE: 'LINIA0'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{FONT: 'W8'}{SPACE: 'C'}
{podpisy:=1;~~}

{FONT: 'T8G'}
{ENTIRE}
{<12 'Skład komisji inwentaryzacyjnej:'}{<65 'Podpis'}{<120 'Inne osoby obecne przy spisie:'}

{
   TMP_KOM:=tab_tmp(1,'REF','INTEGER','Ref komisji','KOD','STRING[20]', 'Kod komisji');
   TMP_KOMS:=tab_tmp(1,'CZLON','STRING[50]', 'Członek komisji');

   {? SRXN.SRXK<>null
   || _kod:=SRXN.SRXK().KOD;
      {? TMP_KOM.first()
      || _add:=1;
         {! |?
            {? TMP_KOM.KOD=_kod || _add:=0 ?};
            _add & TMP_KOM.next()
         !}
      || _add:=1
      ?};
      {? _add
      || TMP_KOM.REF:=#SRXN.SRXK;
         TMP_KOM.KOD:=SRXN.SRXK().KOD;
         TMP_KOM.add()
      ?}
   ?};

   {? TMP_KOM.first()
   || {! |?
         SRXL.index('SRXL');
         SRXL.prefix(TMP_KOM.REF);
         {? SRXL.first()
         || {! |?
               {? SRXL.P='T' || _czlon:='(Przew.) ' || _czlon:='' ?};
               _czlon+=SRXL.OSOBA().PIERWSZE+' '+SRXL.OSOBA().NAZWISKO;
               {? TMP_KOMS.first()
               || _add:=1;
                  {! |?
                     {? TMP_KOMS.CZLON=_czlon || _add:=0 ?};
                     _add & TMP_KOMS.next()
                  !}
               || _add:=1
               ?};
               {? _add
               || TMP_KOMS.CZLON:=_czlon;
                  TMP_KOMS.add()
               ?};
               SRXL.next()
            !}
         ?};
         TMP_KOM.next()
      !}
   ?}
;~~}
{IF: TMP_KOMS.size=0}
{<12 '(Przew.)..........................................'}{<120 '..................................................'}

{<12 '..................................................'}{<120 '..................................................'}

{<12 '..................................................'}{<120 '..................................................'}
{ELSE}
{FOR: TMP_KOMS}
   {DO}
{<12 TMP_KOMS.CZLON}{<65 '.........................'}{<120 '..................................................'}
   {OD}
   {IF: TMP_KOMS.size()<3}
      {dod_podp:=3-TMP_KOMS.size();~~}
      {WHILE: dod_podp>0}
      {DO}
         {<12 '..................................................'}{<120 '..................................................'}
         {dod_podp:=dod_podp-1; ~~}
      {OD}
   {FI}
{FI}
{ENTIRE-}
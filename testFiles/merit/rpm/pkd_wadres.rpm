:!UTF-8
{TITLE:Dane adresowe}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat');
   tresc:='pkd_wadres';
   PAR_WYDR.TITLE:='Dane adresowe'@;
   P.cntx_psh();
   par:=obj_new(8);
   par[5]:=0;
   _upr:=exec('uprawnieniawrap','pkd','danych adresowych'@,'ZWS_DOS_PPDA','ZWS_DOS_PRDA');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || par[7]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,,
         date(),'Na dzień'@,,
         ,,,
         'Rodzaj wydruku'@,'Uproszczony'@,'Pełny'@,0
      );
      {? par[5]:=par[7].size()
      || par[1]:=obj_new(@.CLASS.PRINT,{? ~rep_export() || 10 || 15 ?});
         par[1].add("{? par[6] || OSOBA.NAZWISKO || {? rep_export() || OSOBA.NAZWISKO || '' ?} ?}",17,'Nazwisko'@);
         par[1].add("{? par[6] || OSOBA.PIERWSZE || {? rep_export() || OSOBA.PIERWSZE || '' ?} ?}",17,'Imię'@);
         par[1].add("{? par[6] || par[6]:=0; OSOBA.PESEL || {? rep_export() || OSOBA.PESEL || '' ?} ?}",11,'PESEL'@);
         par[1].add("
            {? par[3]=1 | ~par[7].ASK
            || OS_ADRES.RODZAJ
            || {? rep_export()
               || OS_ADRES.RODZAJ
               || ''
               ?}
            ?}
         ",6,'Rodzaj'@);
         par[1].add("
            {? par[3]=1 | ~par[7].ASK
            || OS_ADRES.OD$1
            || {? rep_export()
               || OS_ADRES.OD$1
               || ''
               ?}
            ?}
         ",10,'Od dnia'@);
         par[1].add("
            {? par[3]=1 | ~par[7].ASK
            || OS_ADRES.DO$1
            || {? rep_export()
               || OS_ADRES.DO$1
               || ''
               ?}
            ?}
         ",10,'Do dnia'@);
         {? par[7].ASK
         || {? ~rep_export()
            || par[1].add(
                  "  {? par[3]=1 || exec('ulica','osoba')
                     |? par[3]=2 || exec('poczta','osoba')
                     |? par[3]=3 || OS_ADRES.MIASTO
                     || ''
                     ?}
                  ",43,'Ulica/poczta/miejscowość'@
               );
               par[1].add(
                  "  {? par[3]=1 || OS_ADRES.TEL
                     |? par[3]=2 || 18+OS_ADRES.EMAIL
                     |? par[3]=3 || 18-OS_ADRES.EMAIL
                     || ''
                     ?}
                  ",18,'Telefon/e-mail'@
               );
               par[1].add(
                  "  {? par[3]=1 || OS_ADRES.WOJEWODZ
                     |? par[3]=2 || OS_ADRES.POWIAT
                     |? par[3]=3 || OS_ADRES.GMINA
                     || ''
                     ?}
                  ",25,'Województwo/powiat/gmina'@
                  );
               par[1].add(
                  "  {? par[3]=1 || OS_ADRES.KRAJ().NAZ
                     || ''
                     ?}
                  ",15,'Kraj'@
               )
            || par[1].add("exec('ulica','osoba')",25,'Ulica'@);
               par[1].add("exec('poczta','osoba')",20,'Poczta'@);
               par[1].add("OS_ADRES.MIASTO",20,'Miejscowość'@);
               par[1].add("OS_ADRES.TEL",15,'Telefon'@);
               par[1].add("OS_ADRES.EMAIL",18,'Email'@);
               par[1].add("OS_ADRES.WOJEWODZ",25,'Województwo'@);
               par[1].add("OS_ADRES.POWIAT",25,'Powiat'@);
               par[1].add("OS_ADRES.GMINA",25,'Gmina'@);
               par[1].add("OS_ADRES.KRAJ().NAZ",15,'Kraj'@)
            ?}
         || par[1].add("exec('ulica','osoba')",41,'Ulica'@);
            par[1].add("exec('poczta','osoba')",27,'Poczta'@);
            par[1].add("OS_ADRES.MIASTO",18,'Miejscowość'@);
            par[1].add("OS_ADRES.KRAJ().NAZ",15,'Kraj'@)
         ?};
         par[1].s_frame();
         par[2]:=tab_tmp(,'REF','INTEGER','Osoba');
         prn1:='par[1]';
         lmt:=rsep:=par[3]:=0;
         vp:=par[4]:=par[6]:=h_rat:=1;
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat')
}
{COMMENT}OLD: wadres.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[5]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}{? par[7].ASK || 'Wydruk pełny'@ || 'Wydruk uproszczony'@ ?}}
   {<{ceil(lmt*h_rat)+1}'Data'@}: {par[7].OD$1}
   {<{ceil(lmt*h_rat)+1
   }'Rodzaj adresu: S - adres zameldowania, C - adres zamieszkania, K - adres korespondencyjny, I - inny.'@}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[6]:=1;
      par[2].add();
      ~~ }
   {ENTIRE}
   {FOR: OS_ADRES}{INDEX: 'OD'}{PREFIX: P.OSOBA}{DO}
      {IF: {? par[7].OD<>#0
           || {? OS_ADRES.OD<=par[7].OD
              || {? OS_ADRES.DO<>#0 || par[7].OD<=OS_ADRES.DO || 1 ?}
              ?}
           || 1
           ?}}
         {  P.OSOBA().NAZWISKO;
            par[3]:={? ~par[7].ASK || 2 ?};
            ~~}
         {WHILE: {? rep_export() & par[7].ASK || (par[3]+=1)<=1 || (par[3]+=1)<=3 ?}}{DO}
            { par[1].ini_line(); ~~ }
            {WHILE: par[1].next()}{DO}
               {REP: par[1].fline(lmt)}
            {OD}
         {OD}
      {FI}
   {OD}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
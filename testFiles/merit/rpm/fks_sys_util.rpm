:!UTF-8
{TITLE:A.  Lista wykrytych usterek}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN:  w:=obj_new(4); petla:=wybor:=lm:=0; mdc:='';  SYS_UTIL.cntx_psh();
         PRN:=obj_new(@.CLASS.PRINT,3); PRN.s_frame()
}
{END:  VAR_DEL.delete('w','PRN','petla','wybor','lm','mdc'); SYS_UTIL.cntx_pop()}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_sys_util.rpm
Utworzony:   2002-07-16 [AMK] Artur Makos
==================================================
Zawartosc:  Lista wykrytych usterek przy odtwarzaniu kartotek
{COMMENT-}
{EXIT:
wybor:=0;
popup(0,'Wybierz wariant zakresu:',
               '1 - Automatycznie naprawialne',,"wybor:=1",
               '2 - Istotne automatycznie naprawialne',,"wybor:=2",
               '3 - Nienaprawialne',,"wybor:=3",
               '4 - Wszystkie istotne',,"wybor:=4",
               '5 - Wszystkie',,"wybor:=5");
~wybor }
{ PRN.add("w[1]",7,'Numer');
  PRN.add("w[2]",80,38*' '+'Opis');
  PRN.add("w[4]",120,55*' '+'Szczegóły');
  {? wybor=3 || PRN.add("w[3]",12,'Aut. naprawa') ?};~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='LISTA WYKRYTYCH USTEREK';~~}{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'}
{<5 'Przedział okresów: '+ROZNE.UT_OKROD().NAZ+' '+ROZNE.UT_ROKOD().NAZ+' - '+ROZNE.UT_OKRDO().NAZ+' '+ROZNE.UT_ROKDO().NAZ}
{<5 'Zakres: '+{? wybor=1
               || 'automatycznie naprawialne'
               |? wybor=2
               || 'istotne automatycznie naprawialne'
               |? wybor=3
               || 'nienaprawialne'
               |? wybor=4
               || 'wszystkie istotne'
               || 'wszystkie' ?}}
{FI}{FONT: 'R'}{SPACE: 'C'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{PRN.lbar()}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}-----------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ {? wybor=1
   || SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('A')
   |? wybor=2
   || SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('A','I')
   |? wybor=3
   || SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('N')
   |? wybor=4
   || SYS_UTIL.index(ind_tym3); SYS_UTIL.prefix('I')
   || SYS_UTIL.index(ind_tym); SYS_UTIL.prefix()
  ?};~~}
{ petla:=SYS_UTIL.first;~~}
{WHILE: petla}
{DO}
{ENTIRE}
{ w[1]:=SYS_UTIL.LP; w[2]:=SYS_UTIL.OPIS; w[3]:=6*' '+{? -SYS_UTIL.AUT_NAP='a' || 'T' || 'N' ?};
  w[4]:=SYS_UTIL.SZCZEG;~~}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{ petla:=SYS_UTIL.next();~~}
{OD}
{COMMENT}----------------------OSTATNI FOOTER-----------------------{COMMENT-}
{FOOTER}{FOOTER-}
{ENTIRE}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'}
{ENTIRE-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}

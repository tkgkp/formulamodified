:!UTF-8
{TITLE:Historia spłaty kredytów (wybranych)}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='Rozliczenie kredytów'@;

   Cntx.psh(P,OSOBA,R,K,KRE_HIS);
   KRE_HIS.index('KRE_HIS');

   VAR_DEL.delete('par','b_rat');
   par:=obj_new(6);
   par[4]:=par[5]:=par[6]:=0;
   b_rat:=1;
   _upr:=
      exec('uprawnieniawrap','pkd','Pożyczkach'@,'PPL_PLL_PPOZ','PPL_PLL_RPOZ')+
      exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');

   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[1]:=obj_new(@.CLASS.PRINT,8);
      par[1].add("K.PO().RT",26,'Kredyt'@);
      par[1].add("K.DU$1",12,'Udzielono'@);
      par[1].add("K.DR$1",12,'Spłata od'@);
      par[1].add("K.KP",15,'Wartość'@,1,,,,,'15,2');
      par[1].add("K.MR",8,'Rat'@,1);
      par[1].add("K.PR",5,'(%%)'@,1,,,,,'5,2');
      par[1].add("K.KS",15,'Dług / wkład'@,1,,,,,'15,2');
      par[1].add("K.KR",15,'Rata / składka'@,1,,,,,'15,2');
      par[1].s_frame();

      par[3]:=3;
      par[2]:=obj_new(@.CLASS.PRINT,par[3]*3);
      {! _ii:=0..par[3]-1
      |! par[2].add($("{? par[4]="+$_ii+" || KRE_HIS.DATA$1 || '' ?}"),17,'Dzień rozliczenia'@);
         par[2].add($("{? par[4]="+$_ii+" || KRE_HIS.KWOTA || '' ?}"),10,'Kwota'@,1,,,,,'10,2');
         par[2].add($(
            "  {? par[4]="+$_ii+"
               || _opis:=KRE_HIS.OPIS;
                  par[4]+=KRE_HIS.next();
                  _opis
               || ''
               ?}
            "),8,'Opis'@
         )
      !};
      par[2].s_frame();

      _SKL:=sql('select R.RN as RN, R.RT as RT, \'N\' as WB from R where R.RK=\'K\' order by 1');
      _where:="'P.REFERENCE in (select K.P from K join R where R.RN in ('+_a+'))'";
      _P:=exec('historia_splaty','!ppl_zes_wkar',_SKL,'Kredyty i wkłady'@,_where);
      _P.index(_P.ndx_tmp(,1,'LP',,));
      params_set('P',_P,'SKL',_SKL);
      _P.ndx_drop()
   ?}
}
{END:
   VAR_DEL.delete('par','b_rat');
   Cntx.pop(P,OSOBA,R,K,KRE_HIS)
}
{EXIT: ~params_get().P.size()}
{COMMENT}OLD: pkrehis.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{  par[1].setcolor(color); par[1].ini_head();
   par[2].setcolor(color); par[2].ini_head()
}
{REP: par[1].fhbar(lmt)}
{REP: par[1].h_fline(lmt)}
{REP: '{<{lmt+1}}'+par[1].fjbar(par[2])}
{REP: par[2].h_fline(lmt)}
{REP: par[2].flbar(lmt)}
{  par[1].setcolor('255:255:255');
   par[2].setcolor('255:255:255')
}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/=charleft(); ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-par[2].width())/2); ~~ }
{FOR: params_get().P}
{DO}
   {par[6]:=0; ~~ }
   {FOR: params_get().SKL}
   {DO}
   {IF: params_get().SKL.WB='T'}
      {  P.prefix();
         P.seek(params_get().P.REF,);
         R.index('RUBKOD');
         R.prefix(params_get().SKL.RN);
         R.first(); ~~
      }
      {FOR: K}{INDEX: 'PRACPOZ'}{PREFIX: P.ref(),R.ref()}
      {DO}
         {ENTIRE}
         {FIRST}
         {IF: ~par[6]}
{FONT: 'W12'}
{SPACE: 'A'}
{<{ceil(lmt*b_rat)+1}}{form(par[5]+=1,,,'9.')} {INCLUDE: p_prac.rpi}
{FONT: 'T8'}
{SPACE: 'B'}
         { par[6]:=1; ~~ }
         {FI}
         {FIRST-}
         {REP: par[1].fhbar(lmt)}
         {REP: par[1].ini_line(); par[1].fline(lmt)}
         {IF: KRE_HIS.prefix(K.ref()); KRE_HIS.first()}
            {REP: '{<{lmt+1}}'+par[1].fjbar(par[2])}
            {WHILE: par[2].ini_line(); par[4]=par[3]}
            {DO}
               {REP: par[4]:=0; par[2].fline(lmt)}
            {OD}
            {IF: par[4]<par[3]}
               {REP: par[4]:=0; par[2].fline(lmt)}
            {FI}
            {REP: par[2].flbar(lmt)}
         {ELSE}
            {REP: par[1].flbar(lmt)}
            {<{ceil(lmt)+1} 'Brak rozliczenia'@}
         {FI}
         {ENTIRE-}
      {OD}
   {FI}
   {OD}
{OD}
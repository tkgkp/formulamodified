:!UTF-8
{TITLE:E. Szczegółowe zestawienie sprzedaży w podziale na grupy kontrahentów}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   fakz_011:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','sg','dokl','doklw','PRN','kgr','kgk');
      VAR_DEL.delete('od_datyw','do_datyw');
      VAR_DEL.delete('od_daty','do_daty');
      VAR_DEL.delete('od_platnosc','do_platnosc');
      VAR_DEL.delete('filtr_kh','filtr_m');
      VAR_DEL.delete('wydr_naz','wydr_edi','wyjscie','X_Xa','X_Xb','X_Xw1')";
   fakz_011();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   VAR_DEL.delete('n','k','w','s','sg','PRN');
   n :=obj_new(7); "--nazwy kolumn--";
   k :=obj_new(7); "--szerokosci kolumn--";
   w :=obj_new(7); "--wartosci kolumn--";
   s :=obj_new(4); "--razem--";
   sg:=obj_new(4); "--razem grupa--";
   dokl  :=3; "--dokladnosc ilosci--";
   doklw :=2; "--dokladnosc wartosc--";
   PRN := obj_new(@.CLASS.PRINT,9); PRN.s_frame();

   k[1] :=20;     n[1] :='Indeks'@;
   k[2] :=60;     n[2] :='Nazwa materiału lub usługi'@;
   k[3] :=5;      n[3] :='jm'@;
   k[4] :=13;     n[4] :='Ilość'@;
   k[5] :=13;     n[5] :='Wartość netto'@;
   k[6] :=13;     n[6] :='Wartość VAT'@;
   k[7] :=13;     n[7] :='Wartość brutto'@;

   {! _ii..7 |! w[_ii] :='' !};
   {! _ii..4 |! s[_ii] :=0 !};
   {! _ii..4 |! sg[_ii] :=0 !};
   tryb:=rep_export();
   kgr:=kgk:='';

   od_datyw:=do_datyw:=date(0,0,0);
   od_daty:=do_daty:=date(0,0,0);
   od_platnosc:=do_platnosc:=date(0,0,0);

   filtr_kh :=filtr_m :='N';

   wydr_naz:='fakz_011';
   wydr_edi:='FAKZ_011';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit()
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         filtr_kh:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         filtr_m :={? WYDRUKIL.CHKBOX02=1 || 'T' || 'N' ?};
         od_datyw:=WYDRUKIL.OD_DATY2;
         do_datyw:=WYDRUKIL.DO_DATY2;
         od_daty:=WYDRUKIL.OD_DATY;
         do_daty:=WYDRUKIL.DO_DATY;
         od_platnosc:=WYDRUKIL.OD_DATY1;
         do_platnosc:=WYDRUKIL.DO_DATY1
      ?}
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_kh','kontrahent',filtr_kh,'N','G') ?};
   {? wyjscie=0 || X_Xb.index(X_Xb.ndx_tmp(,,'REFI',,)) ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_m','material',filtr_m,'N','') ?}
}
{END:
   fakz_011(); VAR_DEL.delete('fakz_011')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1
|| PRN.add("kgk",20,'Grupa kontrahenta'@);
   PRN.add("kgr",8,'Grupa materiałowa'@)
?};
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
PRN.add("w[3]",k[3],n[3]);
PRN.add("w[4]",k[4],n[4],1);
{? exec('upr_cs','ceny')
||
   PRN.add("w[5]",k[5],n[5],1);
   PRN.add("w[6]",k[6],n[6],1);
   PRN.add("w[7]",k[7],n[7],1)
?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'KOD'    ,'STRING[20]'  ,'Kod',
   'NAZ'    ,'STRING[40]'  ,'Nazwa',
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'N'      ,'STRING[100]' ,'Nazwa materiału lub usługi',
   'JM'     ,'STRING[10]'  ,'jm',
   'IL'     ,'REAL'        ,'Ilość',
   'WN'     ,'REAL'        ,'Netto',
   'WV'     ,'REAL'        ,'VAT',
   'WB'     ,'REAL'        ,'Brutto',
   'MGR'    ,'STRING[8]'   ,'Grupa materiałowa');
X_Xw1.index(X_Xw1.ndx_tmp(,,'KOD',,,'KTM',,,'KTM',,,'JM',,,'JM',,));

_lp:=0;
OKR.cntx_psh; FAKS.cntx_psh; FAP.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
_min:="{? _a & _a<_b || _a || _b ?}";
_min:=_min(_min(od_datyw~1,od_daty~1),od_platnosc~1);
_max:="{? _a & _a>_b || _a || _b ?}";
_max:=_max(_max(do_datyw~1,do_daty~1),do_platnosc~1);
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
{? OKR.first & (_min=0 | OKR.find_ge(_min)) & X_Xb.first() & X_Xa.first
||
   _stsupr:=exec('get','#params',2102,2,OPERATOR.USER)='T';
     _stss:="
                _wyn:=exec('usr_upr','b_perm','STS',_a,OPERATOR.USER);
                _wyn
";
   {!
   |?
      FAKS.use('faktu'+ST.ODDZ+(($OKR.ROK)+2));
      FAP.use('fakpo'+ST.ODDZ+(($OKR.ROK)+2));
      FAP.index('REPORT');
      {? X_Xa.first()
      ||
         {!
         |?
            FAP.prefix('S','T',X_Xa.REFI);
            {? FAP.first()
            ||
               {!
               |?
                  {? FAP.FAKS
                     & FAP.FAKS().STAT_REJ<>'A'
                     & FAP.FAKS().ZEST='T' & FAKS.ZEST_AKC='T'
                     & (_stsupr=0 | _stsupr=1 & FAKS.STS<>null & _stss(FAKS.STS))
                     & X_Xb.find_key(#FAKS.KH)
                     & ((od_datyw<=FAKS.DW | od_datyw=date(0,0,0)) & (FAKS.DW<=do_datyw | do_datyw=date(0,0,0)))
                     & ((od_daty<=FAKS.D | od_daty=date(0,0,0)) & (FAKS.D<=do_daty | do_daty=date(0,0,0)))
                     & ((od_platnosc<=FAKS.TZ | od_platnosc=date(0,0,0) ) & (FAKS.TZ<=do_platnosc
                        | do_platnosc=date(0,0,0)))
                  ||
                     _kod:=FAKS.KH().GRKH().KOD;
                     _ktm:=FAP.M().KTM;
                     _jm:=FAP.JM().KOD;
                     {? X_Xw1.find_key(_kod,_ktm,_ktm,_jm,_jm)
                     ||
                        X_Xw1.IL  +=FAP.IL;
                        X_Xw1.WN  +=FAP.WWAL_P;
                        X_Xw1.WV  +=FAP.VWAL_P;
                        X_Xw1.WB  +=FAP.BWAL_P;
                        X_Xw1.put()
                     ||
                        X_Xw1.KOD :=_kod;
                        X_Xw1.NAZ :=GRKH.NAZ;
                        X_Xw1.KTM :=_ktm;
                        X_Xw1.N   :=FAP.M().N;
                        X_Xw1.MGR :=FAP.M().MGR().KOD;
                        X_Xw1.JM  :=_jm;
                        X_Xw1.IL  :=FAP.IL;
                        X_Xw1.WN  :=FAP.WWAL_P;
                        X_Xw1.WV  :=FAP.VWAL_P;
                        X_Xw1.WB  :=FAP.BWAL_P;
                        X_Xw1.add()
                     ?}
                  ?};
                  FAP.next()
               !}
            ?};
            X_Xa.next()
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max)
   !}
?};
OKR.cntx_pop; FAKS.cntx_pop; FAP.cntx_pop;

PAR_WYDR.TITLE:='SZCZEGÓŁOWE ZESTAWIENIE SPRZEDAŻY W PODZIALE NA GRUPY KONTRAHENTÓW';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: filtr_kh='T'}{<{lmt+1}}{'Z wyborem grup kontrahentów'}{FI}
{IF: filtr_m='T'}{<{lmt+1}}{'Z wyborem materiałów i usług'}{FI}
{<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
{<{lmt+1}}{'Data wystawienia'}{<{lmt+1+20}od_datyw}{<{lmt+1+40}do_datyw}
{<{lmt+1}}{'Data sprzedaży'}{<{lmt+1+20}od_daty}{<{lmt+1+40}do_daty}
{<{lmt+1}}{'Termin płatności'}{<{lmt+1+20}od_platnosc}{<{lmt+1+40}do_platnosc}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozcycje wydruku                                         {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;  ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xw1}
{DO}
   {FIRST}{ dokl:=exec('fld_prec','#field',X_Xw1,'IL'); kolejny:=0;~~}{FIRST-}
   {IF: kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {
   w[1] :=form(X_Xw1.KTM);
   w[2] :=form(X_Xw1.N);
   w[3] :=form(X_Xw1.JM);
   w[4] :=form(X_Xw1.IL,,dokl,'.,');
   w[5] :=form(X_Xw1.WN,,doklw,'.,');
   w[6] :=form(X_Xw1.WV,,doklw,'.,');
   w[7] :=form(X_Xw1.WB,,doklw,'.,');
   kgr  :=form(X_Xw1.MGR);
   kgk  :=form(X_Xw1.KOD);
   s[1] +=X_Xw1.IL; sg[1] +=X_Xw1.IL;
   s[2] +=X_Xw1.WN; sg[2] +=X_Xw1.WN;
   s[3] +=X_Xw1.WV; sg[3] +=X_Xw1.WV;
   s[4] +=X_Xw1.WB; sg[4] +=X_Xw1.WB
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
   { kolejny:=0; rsep:=PAR_WYDR.RSEP;~~}
   {TOTAL: X_Xw1.KOD}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
      {IF: tryb<>1}
      {
      w[1] :=form(X_Xw1.KTM);
      w[2] :=form(X_Xw1.N);
      w[3] :=form(X_Xw1.JM);
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+3
      ;~~}
      {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {
      kolejny:=1; rsep:=0;
      PRN.n_frame();
      w[1] :='';
      w[2] :='Razem '+{? grp_fld()='' || 'bez grupy' || grp_fld() ?}+':'; PRN.FORM[2]:=0;
      w[3] :='';
      w[4]:=form(sg[1],,dokl,'.,');
      w[5]:=form(sg[2],,doklw,'.,');
      w[6]:=form(sg[3],,doklw,'.,');
      w[7]:=form(sg[4],,doklw,'.,')
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {FONT: 'T8G'}{SPACE: 'C'}
      {
      {! _ii..4 |! sg[_ii]:=0 !};
      PRN.s_frame();
      PRN.FORM[2]:=1
      ;~~}
      {FI}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1] :='';
w[2] :='Razem:'; PRN.FORM[2] :=0;
w[3] :='';
w[4] :=form(s[1],,dokl,'.,');
w[5] :=form(s[2],,doklw,'.,');
w[6] :=form(s[3],,doklw,'.,');
w[7] :=form(s[4],,doklw,'.,');
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
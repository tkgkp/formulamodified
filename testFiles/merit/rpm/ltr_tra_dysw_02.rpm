:!UTF-8
{TITLE:B. Zbiorcze dowody dostaw}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   tra_dysp:="
      VAR_DEL.delete('ww','ww1','kk','kk1','ss','doklw','lp','PRN','PRN1','tryb');
      VAR_DEL.delete('wydr_naz','wydr_edi');
      VAR_DEL.delete('wyjscie','X_Xw1','X_Xw2');
      VAR_DEL.delete('lmt','vp','rsep');
      VAR_DEL.delete('wb','il');
      VAR_DEL.delete('rul','lul','STRR','STRL')
   ";
   tra_dysp();

   "--tabela dla pozycji dokumentu--";
      _ilk:=7;
      PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();
      kk:=obj_new(_ilk);   "--szerokosc kolumn--";
      ww:=obj_new(_ilk);   "--wartosci kolumn--";
      doklw:=3;            "--dokladnosc wartosci--";
      max_dokl:=0;         "--maksymalna dokładność--";
      ss:=obj_new(4);      "--sumator--";
      {! _i:=1..4 |! ss[_i]:=0 !};

      kk[1]:=3;          "--Lp.--";
      kk[2]:=88;         "--Opis1--";
      kk[3]:=8;          "--Ilość op.--";
      kk[5]:=9;          "--Jedn. miary--";
      kk[4]:=9;          "--Całkowita ilość--";
      kk[6]:=10;         "--Waga netto (t)--";
      kk[7]:=11;         "--Waga brutto (t)--";
   "--koniec tabeli--";

   "--tabela dla danych kontrahentów--";
      _ilk1:=2;
      PRN1:=obj_new(@.CLASS.PRINT,_ilk1); PRN1.s_frame();
      kk1:=obj_new(_ilk1); "--szerokosc kolumn--";
      ww1:=obj_new(_ilk1); "--wartosci kolumn--";

      kk1[1]:=76;          "--Adresat--";
      kk1[2]:=76;          "--Odbiorca--";
   "--koniec tabeli--";

   tryb:=rep_export();

   rul:=lul:='';
   STRR:=obj_new(@.CLASS.STRING);
   STRL:=obj_new(@.CLASS.STRING);
   lmt:=0;
   vp:=0;
   wyjscie:=0;
   lp:=poz:=0
}
{END:
   tra_dysp(); VAR_DEL.delete('tra_dysp')
}
{IF: wyjscie=1}{EXIT}{FI}
{
   {? tryb<>1 || PRN.add("ww[1]",kk[1],'Lp.'@,1) ?};
   PRN.add("ww[2]",kk[2],'Opis'@);
   PRN.add("ww[3]",kk[3],'Ilość opakowań'@,1);
   PRN.add("ww[4]",kk[4],'Ilość całkowita'@,1);
   PRN.add("ww[5]",kk[5],'jm'@);
   PRN.add("ww[6]",kk[6],'Waga netto (t)'@,1);
   PRN.add("ww[7]",kk[7],'Waga brutto (t)'@,1);

   PRN1.add("ww1[1]",kk1[1],'Dostawa od'@);
   PRN1.add("ww1[2]",kk1[2],'Dostawa do / Kupujący'@);

   VAR_DEL.delete('X_Xw1');
   X_Xw1:=tab_tmp(1,
      'POZ'   ,'INTEGER'     ,'Nr pozycji',
      'IND'   ,'STRING[50]'  ,'Indeks',
      'NAZ'   ,'STRING[100]' ,'Nazwa',
      'IL'    ,'REAL'        ,'Ilość opakowań',
      'IC'    ,'REAL'        ,'Ilość całkowita',
      'JM'    ,'STRING[10]'  ,'Jednostka miary',
      'WN'    ,'REAL'        ,'Waga netto (t)',
      'WB'    ,'REAL'        ,'Waga brutto (t)'
   );

   X_Xw1.index(X_Xw1.ndx_tmp(,,{? 1=1 || 'POZ' ?},,));

   VAR_DEL.delete('X_Xw2');
   X_Xw2:=tab_tmp(1,
      'ZNAZ'    ,'STRING[150]'       ,'Nazwa z',
      'ZPAN'    ,'STRING[20]'        ,'Panstwo z',
      'ZKPO'    ,'STRING[6]'         ,'Kod pocztowy z',
      'ZMIA'    ,'STRING[30]'        ,'Miasto z',
      'ZULI'    ,'STRING[150]'       ,'Ulica z',
      'ZNUM'    ,'STRING[40]'        ,'Numer posesji z',

      'DNAZ'    ,'STRING[150]'       ,'Nazwa do',
      'DPAN'    ,'STRING[20]'        ,'Panstwo do',
      'DKPO'    ,'STRING[6]'         ,'Kod pocztowy do',
      'DMIA'    ,'STRING[30]'        ,'Miasto do',
      'DULI'    ,'STRING[150]'       ,'Ulica do',
      'DNUM'    ,'STRING[40]'        ,'Numer posesji do',

      'KNAZ'    ,'STRING[150]'       ,'Nazwa kupującego',
      'KPAN'    ,'STRING[20]'        ,'Panstwo kupującego',
      'KKPO'    ,'STRING[6]'         ,'Kod pocztowy kupującego',
      'KMIA'    ,'STRING[30]'        ,'Miasto kupującego',
      'KULI'    ,'STRING[150]'       ,'Ulica kupującego',
      'KNUM'    ,'STRING[40]'        ,'Numer posesji kupującego',

      'ZNIP'    ,'STRING[30]'        ,'NIP z',
      'ZREG'    ,'STRING[45]'        ,'REGON z',
      'ZKRS'    ,'STRING[40]'        ,'KRS z',

      'DODE'    ,'DATE'              ,'Data odebrania',
      'DDOS'    ,'DATE'              ,'Data dostarczenia'
   );
   ~~
}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: ltr_zle_dys.rpi}
{i:=1;~~}
{FOR: TR_NZL}
{INDEX: 'TR_NAG'}
{PREFIX: TR_NAG.ref()}
{DO}
{
   max_dokl:=0;
   i+=1;lp:=0;
   {! _i:=1..4 |! ss[_i]:=0 !};
   "--tabela dla pozycji zamówienia--";
      X_Xw1.erase();

      wb:=il:=0;

      TR_ZL.index('POZ'); TR_ZL.prefix(TR_NZL.ref,);
      {? TR_ZL.first ||
         {! |?
            X_Xw1.IND      :=TR_ZL.M().KTM;
            X_Xw1.NAZ      :=TR_ZL.M().N;
            X_Xw1.IL       :=TR_ZL.IL_OP;
            X_Xw1.IC       :=TR_ZL.IL;
            X_Xw1.JM       :=TR_ZL.M().J().KOD;
            X_Xw1.WN       :=exec('tr_zl_waga','transport_zlec',0);
            X_Xw1.WB       :=exec('tr_zl_waga','transport_zlec',1);
            X_Xw1.add();
            wb+=X_Xw1.WB;
            il+=X_Xw1.IL;

            _dokl:=+$frac(X_Xw1.IC)-2;
            {? _dokl>max_dokl || max_dokl:=_dokl ?};
            TR_ZL.next
         !}
      ?};
   "--koniec--";

   "--tabela dla danych nagłówka--";
      X_Xw2.erase();

      KSTE.get(); exec('czytaj','#stalesys',,XINFO);

      "----dane sprzedawcy----";
         X_Xw2.ZNAZ      :=XINFO.NAZ;
         X_Xw2.ZPAN      :=XINFO.KRAJ().NAZ;
         X_Xw2.ZKPO      :=XINFO.KOD_P;
         X_Xw2.ZMIA      :=XINFO.MIA;
         X_Xw2.ZULI      :=XINFO.UL+' '+XINFO.NR_D;
         X_Xw2.ZNUM      :=XINFO.NR_L;
         X_Xw2.ZNIP      :=XINFO.NIP;
         X_Xw2.ZREG      :=XINFO.REGON;
         X_Xw2.ZKRS      :=XINFO.NREJ;

      "----miejsce docelowe----";
         {? TR_NZL.ADDR_CHO().N='Kontrahenta'          || X_Xw2.DNAZ      :=TR_NZL.KH().NAZ_P
         |? TR_NZL.ADDR_CHO().N='Odbiorcy kontrahenta' || X_Xw2.DNAZ      :=TR_NZL.KH_ODB().NAZ
         |? TR_NZL.ADDR_CHO().N='Posesji'              || X_Xw2.DNAZ      :=TR_NZL.POS().KH().NAZ_P
         |? TR_NZL.ADDR_CHO().N='Miejsca dostawy'      || X_Xw2.DNAZ      :=TR_NZL.KH_MSC().NAZ
         |? TR_NZL.ADDR_CHO().N='Magazynu'             || X_Xw2.DNAZ      :=TR_NZL.MG().NAZ
         |? TR_NZL.ADDR_CHO().N='Składowiska'          || X_Xw2.DNAZ      :=TR_NZL.WYS().NAZ
         ?};
         X_Xw2.DPAN      :=TR_NZL.KRAJ;
         X_Xw2.DKPO      :=TR_NZL.KPOCZ;
         X_Xw2.DMIA      :=TR_NZL.MIASTO;
         X_Xw2.DULI      :=TR_NZL.UL;
         X_Xw2.DNUM      :=exec('trnzl_dom_lokal','transport_wspolne');

      "----dane kupującego----";
         {? TR_NZL.KH<>null ||
            X_Xw2.KNAZ   :=TR_NZL.KH().NAZ_P;
            X_Xw2.KPAN   :=TR_NZL.KH().KRAJ;
            X_Xw2.KKPO   :=TR_NZL.KH().KPOCZ;
            X_Xw2.KMIA   :=TR_NZL.KH().MIASTO;
            X_Xw2.KULI   :=exec('adr','faktury_wydruk',TR_NZL.KH().UL,TR_NZL.KH().DOM,TR_NZL.KH().LOKAL,
                           TR_NZL.KH().MIASTO,TR_NZL.KH().POCZ)
         ||
            X_Xw2.KPAN   :=TR_NZL.KRAJ;
            X_Xw2.KKPO   :=TR_NZL.KPOCZ;
            X_Xw2.KMIA   :=TR_NZL.MIASTO;
            X_Xw2.KULI   :=TR_NZL.UL;
            X_Xw2.KNUM   :=exec('trnzl_dom_lokal','transport_wspolne')
         ?};

      "----daty----";
         X_Xw2.DODE      :=TR_NZL.DW;
         X_Xw2.DDOS      :=date(TR_NZL.RR,TR_NZL.RM,TR_NZL.RD);
         X_Xw2.add();

   "--koniec--";

   PAR_WYDR.TITLE:='DOWÓD DOSTAWY'@;
   prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}{INCLUDE: wsp_pw.rpi}{COMMENT-}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}{LINE:2}
{<{lmt+1}}{'Data wyjazdu: '@}{<{lmt+25}}{form(X_Xw2.DODE,,4)}
{<{lmt+1}}{'Data dokumentu: '@}{<{lmt+25}}{form(date(),,4)}
{<{lmt+1}}{'Miejsce: '@}{<{lmt+25}}{X_Xw2.ZMIA}{LINE:2}
{COMMENT} ----             DANE ADRESOWE (Z/DO)             ----                                              {COMMENT-}
{<{lmt+3}}{FONT: 'T8GB'}{SPACE: 'C'}{'Dostawa z:'@}{<{lmt+84}}{'Dostawa do:'@}{FONT: 'T8G'}{LINE:2}
{LC:=X_Xw2.ZNAZ;
 RC:=X_Xw2.DNAZ;
 lul:=form(X_Xw2.ZULI +' '+ X_Xw2.ZNUM);
 rul:=form(X_Xw2.DULI +' '+ X_Xw2.DNUM);
 STRL.split(lul); STRR.split(rul);~~}
{SUBSTITUTE: 'LONGLINE'}
{IF: X_Xw2.ZPAN<>''}{<{lmt+3}}{X_Xw2.ZPAN}{FI}{IF: X_Xw2.DPAN<>''}{<{lmt+84}}{X_Xw2.DPAN}{FI}
{<{lmt+3}}{IF: X_Xw2.ZKPO<>''}{X_Xw2.ZKPO+' '}{FI}{X_Xw2.ZMIA+' '}{<{lmt+84}}{IF: X_Xw2.DKPO<>''}{X_Xw2.DKPO+' '}{FI}{X_Xw2.DMIA+' '}
{<{lmt+3}}{STRL.line(75)}{<{lmt+84}}{STRR.line(75)}
{WHILE: +(lul:=STRL.line(75)) | +(rul:=STRR.line(75))}
{DO}
   {<{lmt+3}}{lul}{<{lmt+84}}{rul}
{OD}{LINE:2}
{COMMENT} ----             DANE ADRESOWE (KUPUJĄCY)             ---- {COMMENT-}
{<{lmt+84}}{FONT: 'T8GB'}{SPACE: 'C'}{'Kupujący:'@}{FONT: 'T8G'}{LINE:2}
{LC:='';
 RC:=X_Xw2.KNAZ;
 rul:=form(X_Xw2.KULI +' '+ X_Xw2.KNUM);
 STRR.split(rul);~~}
{SUBSTITUTE: 'LONGLINE'}
{IF: X_Xw2.KPAN<>''}{<{lmt+84}}{X_Xw2.KPAN}{FI}
{<{lmt+84}}{IF: X_Xw2.KKPO<>''}{X_Xw2.KKPO+' '}{FI}{X_Xw2.KMIA}
{<{lmt+84}}{STRR.line(75)}
{WHILE: +(rul:=STRR.line(75))}
{DO}
   {<{lmt+84}}{rul}
{OD}{LINE:2}
{COMMENT}      PODSUMOWANIE NAGŁÓWKA                                 {COMMENT-}
{<{lmt+1}}{'Waga brutto: '}{<{lmt+25}}{$wb}
{<{lmt+1}}{'Całkowita ilość paczek: '}{<{lmt+25}}{$il}
{<{lmt+1}}{'Data dostarczenia: '}{<{lmt+25}}{form(X_Xw2.DDOS,,4)}{LINE:2}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{IF: lp>0}{SUBSTITUTE: 'HEAD'}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: rsep}{SUBSTITUTE: 'TABHEAD'}{FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xw1}
{DO}
   {
      lp+=1;
      ww[1]:=form(lp);
      ww[2]:=form(X_Xw1.IND+' - '+X_Xw1.NAZ);
      ww[3]:=form(X_Xw1.IL);
      ww[4]:=form(X_Xw1.IC,,max_dokl,'.,');
      ww[5]:=form(X_Xw1.JM);
      ww[6]:=form(X_Xw1.WN,,doklw,'.,');
      ww[7]:=form(X_Xw1.WB,,doklw,'.,');
      ss[1]+=X_Xw1.IL;
      ss[2]+=X_Xw1.IC;
      ss[3]+=X_Xw1.WN;
      ss[4]+=X_Xw1.WB;
      poz+=1;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{  rsep:=1; ~~}
{IF: tryb<>1}
   {IF: exec('upr_cm','ceny')}
      {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
      {FOOTER}{FOOTER-}
      {
         PRN.s_frame();
         ww[1]:='';
         ww[2]:='Razem:'@;
         ww[3]:=form(ss[1]);
         ww[5]:='';
         ww[4]:='';
         ww[6]:=form(ss[3],,doklw,'.,');
         ww[7]:=form(ss[4],,doklw,'.,');
         PRN.FORM[2]:=1;
         poz+=1;
      ~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {SUBSTITUTE: 'LINIAF'}
      { PRN.s_frame(); PRN.FORM[2]:=1; ~~}
   {FI}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
{  rsep:=0; ~~}
{LINE:2}
{FONT: 'T8G'}
{>{lmt+2}}{'Data i pieczęć: '}{61*'.'}{LINE:2}
{<{lmt+2}}{'NIP: '+ X_Xw2.ZNIP}{'  REGON: '+ X_Xw2.ZREG}{'  KRAJOWY REJESTR SĄDOWY - REJESTR PRZEDSIĘBIORCÓW NR KRS: '+ X_Xw2.ZKRS}
{PAGE}
{OD}
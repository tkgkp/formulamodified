:!UTF-8
{
   max_dokl:=0;
   lp:=0;
   {! _i:=1..4 |! ss[_i]:=0 !};

   "--tabela dla pozycji zamówienia--";
      X_Xw1.erase();

      wb:=il:=0;

      TR_ZL.index('POZ'); TR_ZL.prefix(TR_NZL.ref,);
      {? TR_ZL.first ||
         {! |?
            X_Xw1.IND      :=TR_ZL.M().KTM;
            X_Xw1.NAZ      :=TR_ZL.M().N;
            X_Xw1.IL       :=TR_ZL.IL_OP;
            X_Xw1.IC       :=TR_ZL.IL;
            X_Xw1.JM       :=TR_ZL.M().J().KOD;
            X_Xw1.WN       :=exec('tr_zl_waga','transport_zlec',0);
            X_Xw1.WB       :=exec('tr_zl_waga','transport_zlec',1);
            X_Xw1.add();
            wb+=X_Xw1.WB;
            il+=X_Xw1.IL;

            _dokl:=+$frac(X_Xw1.IC)-2;
            {? _dokl>max_dokl || max_dokl:=_dokl ?};
            TR_ZL.next
         !}
      ?};
   "--koniec--";

   "--tabela dla danych nagłówka--";
      X_Xw2.erase();

      KSTE.get(); exec('czytaj','#stalesys',,XINFO);

      "----dane sprzedawcy----";
         X_Xw2.ZNAZ      :=XINFO.NAZ;
         X_Xw2.ZPAN      :=XINFO.KRAJ().NAZ;
         X_Xw2.ZKPO      :=XINFO.KOD_P;
         X_Xw2.ZMIA      :=XINFO.MIA;
         X_Xw2.ZULI      :=XINFO.UL+' '+XINFO.NR_D;
         X_Xw2.ZNUM      :=XINFO.NR_L;
         X_Xw2.ZNIP      :=XINFO.NIP;
         X_Xw2.ZREG      :=XINFO.REGON;
         X_Xw2.ZKRS      :=XINFO.NREJ;

      "----miejsce docelowe----";
    X_Xw2.DNAZ     :='';
         {? TR_NZL.ADDR_CHO().N='Kontrahenta'          || X_Xw2.DNAZ      :=TR_NZL.KH().NAZ_P
         |? TR_NZL.ADDR_CHO().N='Odbiorcy kontrahenta' || X_Xw2.DNAZ      :=TR_NZL.KH_ODB().NAZ
         |? TR_NZL.ADDR_CHO().N='Posesji'              || X_Xw2.DNAZ      :=TR_NZL.POS().KH().NAZ_P
         |? TR_NZL.ADDR_CHO().N='Miejsca dostawy'      || X_Xw2.DNAZ      :=TR_NZL.KH_MSC().NAZ
         |? TR_NZL.ADDR_CHO().N='Magazynu'             || X_Xw2.DNAZ      :=TR_NZL.MG().NAZ
         |? TR_NZL.ADDR_CHO().N='Składowiska'          || X_Xw2.DNAZ      :=TR_NZL.WYS().NAZ
         ?};
         X_Xw2.DPAN      :=TR_NZL.KRAJ;
         X_Xw2.DKPO      :=TR_NZL.KPOCZ;
         X_Xw2.DMIA      :=TR_NZL.MIASTO;
         X_Xw2.DULI      :=TR_NZL.UL;
         X_Xw2.DNUM      :=exec('trnzl_dom_lokal','transport_wspolne');

      "----dane kupującego----";
    X_Xw2.KNAZ    :='';
         {? TR_NZL.KH<>null ||
            X_Xw2.KNAZ      :=TR_NZL.KH().NAZ_P;
            X_Xw2.KPAN      :=TR_NZL.KH().KRAJ;
            X_Xw2.KKPO      :=TR_NZL.KH().KPOCZ;
            X_Xw2.KMIA      :=TR_NZL.KH().MIASTO;
            X_Xw2.KULI      :=exec('adr','faktury_wydruk',TR_NZL.KH().UL,TR_NZL.KH().DOM,TR_NZL.KH().LOKAL,
                              TR_NZL.KH().MIASTO,TR_NZL.KH().POCZ)
         ||
            X_Xw2.KPAN      :=TR_NZL.KRAJ;
            X_Xw2.KKPO      :=TR_NZL.KPOCZ;
            X_Xw2.KMIA      :=TR_NZL.MIASTO;
            X_Xw2.KULI      :=TR_NZL.UL;
            X_Xw2.KNUM      :=exec('trnzl_dom_lokal','transport_wspolne')
         ?};

      "----daty----";
         X_Xw2.DODE      :=TR_NZL.DW;
         X_Xw2.DDOS      :=date(TR_NZL.RR,TR_NZL.RM,TR_NZL.RD);
         X_Xw2.add();

   "--koniec--";

   PAR_WYDR.TITLE:='LISTA PAKUNKOWA%1'@[{? TR_NZL.STAT_REJ='A' || ' (anulowana)'@ || '' ?}];
   prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}{INCLUDE: wsp_pw.rpi}
{INCLUDE: ltr_zle_dys.rpi}{COMMENT-}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{LINE:2}
{<{lmt+3}}{FONT: 'T8GB'}{SPACE: 'C'}{'Dostawa z:'@}{<{lmt+84}}{'Dostawa do:'@}{FONT: 'T8G'}{LINE:2}
{LC:=X_Xw2.ZNAZ;
 RC:=X_Xw2.DNAZ;
 lul:=form(X_Xw2.ZULI +' '+ X_Xw2.ZNUM);
 rul:=form(X_Xw2.DULI +' '+ X_Xw2.DNUM);
 STRL.split(lul); STRR.split(rul);~~}
{SUBSTITUTE: 'LONGLINE'}
{IF: X_Xw2.ZPAN<>''}{<{lmt+3}}{X_Xw2.ZPAN}{FI}{IF: X_Xw2.DPAN<>''}{<{lmt+84}}{X_Xw2.DPAN}{FI}
{<{lmt+3}}{IF: X_Xw2.ZKPO<>''}{X_Xw2.ZKPO+' '}{FI}{X_Xw2.ZMIA+' '}{<{lmt+84}}{IF: X_Xw2.DKPO<>''}{X_Xw2.DKPO+' '}{FI}{X_Xw2.DMIA+' '}
{<{lmt+3}}{STRL.line(75)}{<{lmt+84}}{STRR.line(75)}
{WHILE: +(lul:=STRL.line(75)) | +(rul:=STRR.line(75))}
{DO}
   {<{lmt+3}}{lul}{<{lmt+84}}{rul}
{OD}
{LINE}
{<{lmt+84}}{FONT: 'T8GB'}{SPACE: 'C'}{'Kupujący:'@}{FONT: 'T8G'}{LINE:2}
{LC:='';
 RC:=X_Xw2.KNAZ;
 rul:=form(X_Xw2.KULI +' '+ X_Xw2.KNUM);
 STRR.split(rul);~~}
{SUBSTITUTE: 'LONGLINE'}
{IF: X_Xw2.KPAN<>''}{<{lmt+84}}{X_Xw2.KPAN}{FI}
{<{lmt+84}}{IF: X_Xw2.KKPO<>''}{X_Xw2.KKPO+' '}{FI}{X_Xw2.KMIA}
{<{lmt+84}}{STRR.line(75)}
{WHILE: +(rul:=STRR.line(75))}
{DO}
   {<{lmt+84}}{rul}
{OD}
{LINE}
{<{lmt+1}}{'Waga brutto: '@}{<{lmt+25}}{$wb}
{<{lmt+1}}{'Całkowita ilość paczek: '@}{<{lmt+25}}{$il}
{<{lmt+1}}{'Data dostarczenia: '@}{<{lmt+25}}{form(X_Xw2.DDOS,,4)}{LINE:2}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{IF: lp>0}{SUBSTITUTE: 'HEAD'}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: rsep}{SUBSTITUTE: 'TABHEAD'}{FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xw1}
{DO}
   {
      lp+=1;
      ww[1]:=form(lp);
      ww[2]:=form(X_Xw1.IND+' - '+X_Xw1.NAZ);
      ww[3]:=form(X_Xw1.IL);
      ww[4]:=form(X_Xw1.IC,,max_dokl,'.,');
      ww[5]:=form(X_Xw1.JM);
      ww[6]:=form(X_Xw1.WN,,doklw,'.,');
      ww[7]:=form(X_Xw1.WB,,doklw,'.,');
      ss[1]+=X_Xw1.IL;
      ss[2]+=X_Xw1.IC;
      ss[3]+=X_Xw1.WN;
      ss[4]+=X_Xw1.WB;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
   {IF: exec('upr_cm','ceny')}
      {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
      {FOOTER}{FOOTER-}
      {
         PRN.s_frame();
         ww[1]:='';
         ww[2]:='Razem:'@;
         ww[3]:=form(ss[1]);
         ww[5]:='';
         ww[4]:='';
         ww[6]:=form(ss[3],,doklw,'.,');
         ww[7]:=form(ss[4],,doklw,'.,');
         PRN.FORM[2]:=1;
      ~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {SUBSTITUTE: 'LINIAF'}
      { PRN.s_frame(); PRN.FORM[2]:=1; ~~}
   {FI}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
{  rsep:=0; ~~}
{ENTIRE}
{LINE:2}{FONT: 'T8G'}
{<{lmt+3}}{'Towar wydał: '}{>{lmt+32}}{48*'.'}{<{lmt+84}}{'Towar odebrał: '}{>{lmt+113}}{48*'.'}{LINE:2}
{<{lmt+84}}{'Transport: '}{>{lmt+113}}{48*'.'}{LINE:2}
{<{lmt+84}}{'Imię i nazwisko kierowcy: '}{>{lmt+113}}{48*'.'}{LINE:3}
{<{lmt+3}}{'Data i pieczęć: '}{>{lmt+32}}{48*'.'}{<{lmt+84}}{'Numer rejestracyjny pojazdu: '}{>{lmt+113}}{48*'.'}{LINE:2}
{<{lmt+84}}{'Marka pojazdu: '}{>{lmt+113}}{48*'.'}{LINE:2}
{<{lmt+84}}{'Data i pieczęć: '}{>{lmt+113}}{48*'.'}{LINE:3}
{ENTIRE-}
{<{lmt+1}}{'NIP: '+ X_Xw2.ZNIP}
{<{lmt+1}}{'REGON: '+ X_Xw2.ZREG}
{<{lmt+1}}{'KRAJOWY REJESTR SĄDOWY - REJESTR PRZEDSIĘBIORCÓW NR KRS: '+ X_Xw2.ZKRS}

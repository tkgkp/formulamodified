:!UTF-8
{TITLE:Porównanie kalkulacji zlecenia}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   params_set(params_get());
   ANZH.cntx_psh();
   ANZTTO.cntx_psh();
   ANZMAT.cntx_psh();
   dalej:=0;
   PRN:=obj_new(@.CLASS.PRINT,5);
   PRN.s_frame();
   PRNNAG:=obj_new(@.CLASS.PRINT,6); PRNNAG.s_frame();
   PRNMAT:=obj_new(@.CLASS.PRINT,8);
   PRNMAT.s_frame();
   __PRNMAT:=tab_tmp(2,'KOD','STRING[15]','Kod surowca',
                       'NAZ','STRING[30]','Opis suowca',
                       'CENA','REAL','Cena surowca',
                       'CENA1','REAL','Cena surowca',
                       'POCH','STRING[11]','Pochodzenie',
                       'POCH1','STRING[11]','Poch1',
                       'TKTLNRK','STRING[20]','tech1',
                       'TKTL1NRK','STRING[20]','tech2',
                       'TKTLWER','STRING[6]','wer1',
                       'TKTL1WER','STRING[6]','wer2'
                       );
   prn1:='PRNNAG';
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   ndx_tmp:=ANZP.ndx_tmp(,1,'NRA',,0,'RUBR','NR',0)
}
{END:
   ANZH.cntx_pop();
   ANZTTO.cntx_pop();
   ANZMAT.cntx_pop();
   &dalej;
   obj_del(PRN);
   obj_del(PRNMAT);
   obj_del(PRNNAG);
   obj_del(__PRNMAT);
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   ANZP.ndx_drop(ndx_tmp)
}
 {IF: FUN.ask('Czy drukować cennik pobranych surowców?'@)}
 {pkalmat:='t'; ~~}
 {ELSE}
 {pkalmat:='n'; ~~}
 {FI}
{COMMENT}
  Wydruk porownania kalkulacji zlecenia.
  [MKO] - 2003/02/28 - [8.10]
  Wywolanie: Produkcja -> Zlecenia -> Kalkulacje -> TEN
             -> Porownanie->TEN  -> drukuJ
{COMMENT-}
{COMMENT}---Definicja wiersza wydruku pozycji porownania kalkulacji.{COMMENT-}
{  PRNNAG.add("nr",5,'Nr kalk.'@,1);
   PRNNAG.add("dk",10,'Data kalkulacji'@);
   PRNNAG.add("akt",7,'Aktywna'@);
   PRNNAG.add("war",40,'Wariant kalkulacji'@);
   PRN.add("form(ANZP.RUBR().NR,,0,'9')",5,'Nr rubr.'@,1);
   PRN.add("ANZP.RUBR().OPIS",30,'Opis rubryki'@);
   PRN.add("form(ANZP.WAR,,2)",12,'Wartość'@,1);
   PRN.add("form(params_get().anzp.WARTOSC,,2)",12,'Wart. por.'@,1);
   PRN.add("form(ANZP.WAR-params_get().anzp.WARTOSC,,2)",12,'Różnica'@,1);
   PRNMAT.add("__PRNMAT.KOD",10,'Kod surowca'@);
   PRNMAT.add("__PRNMAT.NAZ",15,'Nazwa surowca'@);
   PRNMAT.add("form(__PRNMAT.CENA,,2)",6,'Cena'@,1);
   PRNMAT.add("form(__PRNMAT.CENA1,,2)",6,'Cena por.'@,1);
   PRNMAT.add("__PRNMAT.POCH",4,'Poch'@);
   PRNMAT.add("__PRNMAT.POCH1",4,'Poch por.'@);
   PRNMAT.add("__PRNMAT.TKTLNRK+' '+__PRNMAT.TKTLWER",17,'Technologia'@);
   PRNMAT.add("__PRNMAT.TKTL1NRK+' '+__PRNMAT.TKTL1WER",17,'Techn. por.'@);
   PAR_WYDR.TITLE:='PORÓWNANIE KALKULACJI ZLECENIA'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRNNAG.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Zlecenie:'@}   {<{lmt+15} VAR.A_ANZH().ZLEC().SYM}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------MACRO-----------------------{COMMENT-}
{MACRO: 'prnmat'}
{ ANZMAT.index('ASN');
  ANZMAT.prefix(VAR.A_ANZH);
  {? ANZMAT.first
  || {! |?
      __PRNMAT.KOD:=ANZMAT.T().KTM;
      __PRNMAT.NAZ:=ANZMAT.T().N;
      __PRNMAT.CENA:=ANZMAT.PRICE;
      __PRNMAT.CENA1:=0;
      __PRNMAT.POCH:=ANZMAT.POCH;
      __PRNMAT.POCH1:='';
      __PRNMAT.TKTLNRK:={? ANZMAT.TKTL<>null || ANZMAT.TKTL().NRK||''?};
      __PRNMAT.TKTL1NRK:='';
      __PRNMAT.TKTLWER:={? ANZMAT.TKTL<>null || ANZMAT.TKTL().WER||''?};
      __PRNMAT.TKTL1WER:='';
      __PRNMAT.add;
      ANZMAT.next
     !}
  ?};
  ANZMAT.prefix(VAR.A_PANZH);
  {? ANZMAT.first
  || {! |?
      __PRNMAT.prefix(ANZMAT.T().KTM);
      {? __PRNMAT.first
      || __PRNMAT.CENA1:=ANZMAT.PRICE;
         __PRNMAT.POCH1:=ANZMAT.POCH;
         __PRNMAT.TKTL1NRK:={? ANZMAT.TKTL<>null || ANZMAT.TKTL().NRK ||''?};
         __PRNMAT.TKTL1WER:={? ANZMAT.TKTL<>null || ANZMAT.TKTL().WER || ''?};
         __PRNMAT.put
      || __PRNMAT.KOD:=ANZMAT.T().KTM;
         __PRNMAT.NAZ:=ANZMAT.T().N;
         __PRNMAT.CENA1:=ANZMAT.PRICE;
         __PRNMAT.CENA:=0;
         __PRNMAT.POCH1:=ANZMAT.POCH;
         __PRNMAT.POCH:='';
         __PRNMAT.TKTL1NRK:={? ANZMAT.TKTL<>null || ANZMAT.TKTL().NRK||''?};
         __PRNMAT.TKTLNRK:='';
         __PRNMAT.TKTL1WER:={? ANZMAT.TKTL<>null || ANZMAT.TKTL().WER||''?};
         __PRNMAT.TKTLWER:='';
         __PRNMAT.add
      ?};
      ANZMAT.next
     !}
  ?}
 ;~~}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{nr:=VAR.A_ANZH().NRA;
 dk:=form(VAR.A_ANZH().DT);
 akt:=VAR.A_ANZH().AKT;
 war:=VAR.A_ANZH().OPC().NA;~~}
{SUBSTITUTE: 'LINIA0'}
{nr:=VAR.A_PANZH().NRA;
 dk:=form(VAR.A_PANZH().DT);
 akt:=VAR.A_PANZH().AKT;
 war:=VAR.A_PANZH().OPC().NA;~~}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'LINIAF'}

{ prn1:='PRN'; lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{ ANZP.index(ndx_tmp); ANZP.prefix(VAR.A_ANZH); ~~}
{IF: ANZP.first()}{ dalej:=1; ~~}
{WHILE: dalej}
{DO}
   {params_get().anzp.prefix(ANZP.RUBR().NR); params_get().anzp.first();~~}
   {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {FI}
   { dalej:=ANZP.next(); ~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI}
{COMMENT}
Ceny surowców pobranych do kalkulacji
{COMMENT-}

{IF: pkalmat='t'}
{SUBSTITUTE: 'prnmat'}
{__PRNMAT.prefix();~~}
{IF: __PRNMAT.first()}{dalej:=1;prn1:='PRNMAT'; lmt:=int((charleft()-PRNMAT.width())/2); ~~}
{FONT: 'W12'}{SPACE: 'A'}
{lk:=charleft(); ~~}
{IF: lineleft < 7}{PAGE}{FI}
{<1>{lk} 'LISTA CEN SUROWCÓW POBRANYCH DO KALKULACJI ZLECENIA'@}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{WHILE: dalej}
{DO}
   {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
    {FI}
 { dalej:=__PRNMAT.next(); ~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI}
{FI}

:!UTF-8
{COMMENT}   MACRO - LINIA0                                              {COMMENT-}
{COMMENT}
Makro używane do drukowania rekordu z linią poprzedzającą
dla wydruków z obiektem PRINTER o nazwie przekazywanej przez zmienną prn1.
Bez ENTIRE
{COMMENT-}
{DEFINEFONT: 'TM=Inter,75,Q,,0,0'}
{DEFINEFONT: 'TMB=Inter,75,R,,0,0'}
{DEFINEFONT: 'TF=Inter,75,F,,0,0'}
{DEFINEFONT: 'IE=Inter,150,E,,0,0'}
{DEFINEFONT: 'IF=Inter,150,F,,0,0'}
{MACRO: 'MY_LINIA0'}
{  ll:=lineleft(); ~~}
{IF:  _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; _v:=($(_f))();
      ll<(rsep+_v) & ll<>0}{PAGE}{FI}
{IF: vp}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   {ELIF: rsep}
      {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   {FI}
{FI}
{WHILE: _f:=prn1+'.next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
{OD}
{IF: var_pres('fun')>=0 & fun<>''}
{  $fun(); ~~}
{FI}
{MACRO-}
{COMMENT}   MACRO - druk                                             {COMMENT-}
{MACRO: 'druk'}
{IF: wyjscie=1}{EXIT}{FI}
{WHILE: ilkop<=PARAM_W.KOPIA}
{DO}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}
   {IF: exec('czy_grp_dr','faktury_wydruk') | PARAM_W.KOPIA }
   {strona:=1;~~}
   {FOOTER}
   {<1}{charleft_foot:=charleft();~~}{>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {<1}{IF: exec('get_footer','img_blob')}{IMAGE: exec('get_footer','img_blob');;1}{FI}{>{charleft_foot-3}}{$strona}{' '}{COLOR: color_oo}{'  '}{COLOR: color_bw}{strona+=1;~~}
   {>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {FOOTER-}
   {ELSE}
   {FOOTER}
   {<1}{charleft_foot:=charleft();~~}{>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {<1}{IF: exec('get_footer','img_blob')}{IMAGE: exec('get_footer','img_blob');;1}{FI}{MAXPAGE: >{charleft_foot-3};$page+' / '; 3L;' '}{' '}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
   {FOOTER-}
   {FI}
   {
   PAR_WYDR.TITLE:=exec('trans_typysp','tlumaczenia',TYPYSP.ref(),PARAM_W.JEZYK);
   PAR_WYDR.SYM:=FAKS.SYM;
   TYPYSP.cntx_psh();
   PAR_WYDR.TYPE:=FAKS.T().T;
   TYPYSP.cntx_pop();
   rsep:=0
   ;~~}
{COMMENT}   REPORT - HEADER                                          {COMMENT-}
   {
   head:=PAR_WYDR.HEAD; PAR_WYDR.HEAD:=0;
   __STRTAB:='FAKS';
   {? -PARAM_W.DR_DUPL='t'
   ||
      dup_head:=exec('tr','tlumaczenia','1000105');
      dup_val:=gsub(form(PARAM_W.DUPLIKAT,,3),'/','-');
      dup_sym:=1
   ?};
   _dw:=exec('FindAndGet','#table',FAKS,FAKS.FKSQL,,"FAKS.DW",date(0,0,0));
   podtytul:=exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','2000102'))+exec('get','#params',100225,3)()+
      ' '+FAKS.KOR+' ' + exec('tr','tlumaczenia','2000103')+' '+gsub(form(_dw,,3),'/','-');
   ~~}
   {SUBSTITUTE: 'HEADF'}
   {
   podtytul:='';
   ~~}
{COMMENT}   HEDER obowiazujacy przed wydrukiem pozycji               {COMMENT-}
   {HEADER}
   {FONT: exec('font','param_wydr','head')}{SPACE: exec('space','param_wydr')}
   {SUBSTITUTE: 'HEADF'}
   {HEADER-}
{COMMENT}   oryginal / kopia, tryb probny                            {COMMENT-}
   {FONT: exec('font','param_wydr','ory_kop')}{SPACE: exec('space','param_wydr')}
   {IF: (';NZA'*FAKS.STAT_REJ)>1}{<1}{>{charleft}exec('tr','tlumaczenia','100021'+{? FAKS.STAT_REJ='A' || '5' || '0' ?})}{FI}
   {<1}{>{charleft}{? ~skladanka
      || {? ilkop=0 || exec('tr','tlumaczenia','1000103') || exec('tr','tlumaczenia','1000104') ?}
      || '' ?}}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}
   {IF: exec('czy_grp_dr','faktury_wydruk') | PARAM_W.KOPIA }
      {strona:=1;~~}
      {FOOTER}
      {<1}{charleft_foot:=charleft();~~}{>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
      {<1}{IF: exec('get_footer','img_blob')}{IMAGE: exec('get_footer','img_blob');;1}{FI}{>{charleft_foot-3}}{$strona}{' '}{COLOR: color_oo}{'  '}{COLOR: color_bw}{strona+=1;~~}
      {>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
      {FOOTER-}
      {ELSE}
      {FOOTER}
      {<1}{charleft_foot:=charleft();~~}{>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
      {<1}{IF: exec('get_footer','img_blob')}{IMAGE: exec('get_footer','img_blob');;1}{FI}{MAXPAGE: >{charleft_foot-3};$page+' / '; 3L;' '}{' '}{COLOR: color_oo}{'  '}{COLOR: color_bw}
      {>{charleft_foot-1}}{COLOR: color_oo}{'  '}{COLOR: color_bw}
      {FOOTER-}
   {FI}
   { ilkop+=1;~~}
   {LINE: 1}
{COMMENT}   strony transakcji                                        {COMMENT-}
   { FAKS.cntx_psh(); ii:=1;~~}
   {FOR: FAKS_KZF}
   {INDEX: FAKS_KZF.ndx_tmp(,,'FAKS_UID',,,'NETTO',,1) }
   {PREFIX: exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'')}
   {DO}
      { __STRTAB:='FAKS'; __STRODB:='FAKS.KH_ODB'; __STRLIN:='KOR'; __STRDAT:=FAKS.DW;~~}
      {IF: ii=1}
   {COMMENT}   napis po korekcie                                        {COMMENT-}
         {<1}{SUBSTITUTE: 'stronyf'}
         {<1}{FONT: exec('font','param_wydr','kor_po')}{SPACE: exec('space','param_wydr')}
         {<3}{COLOR: color_wg}{'   '+exec('tr','tlumaczenia','2000106')}{COLOR: color_dgg}{'...'}{COLOR: color_bw}
         {SPACE: 'E'}
         {<3}{COLOR: color_dgg}{'   '+exec('tr','tlumaczenia','2000106')}{COLOR: color_dgg}{'...'}{COLOR: color_bw}
         {SPACE: exec('space','param_wydr')}
      {ELSE}
         {<1}{LINE}
   {COMMENT}   napis przed korekta                                      {COMMENT-}
         {<1}{FONT: exec('font','param_wydr','kor_przed')}{SPACE: exec('space','param_wydr')}
         {<3}{COLOR: color_wg}{'   '+exec('tr','tlumaczenia','2000104')}{COLOR: color_dgg}{'...'}{COLOR: color_bw}
         {SPACE: 'E'}
         {<3}{COLOR: color_dgg}{'   '+exec('tr','tlumaczenia','2000104')}{COLOR: color_dgg}{'...'}{COLOR: color_bw}
         {SPACE: exec('space','param_wydr')}
      {FI}
      {
      ii+=1;
      FAKS.prefix(); FAKS.seek(FAKS_KZF.KOR,ref_name(FAKS_KZF.KOR),1);
      ~~}
      {LINE}
   {COMMENT}   Data dostawy towarów/wykonania usługi{COMMENT-}
      {IF: FAKS.WHERE<>'K'}
           { VAR_DEL.delete('date_head_kz','date_val_kz');
            {? FAKS.T().HIS='T'
            || _date_head:=exec('tr','tlumaczenia','1000112')
            || _txt_ds:=
                        {? FAKS.WHERE='L'
                        || {? FAKS.DO_NA_DR<>date(0,0,0) || '1000111' || '' ?}
                        |? PAR_SKID.get(308)='T' & FAKS.DW>=date(2014,1,1)
                        || {? FAKS.DO_NA_DR<>date(0,0,0)
                           || exec('kod_aktualny','tlumaczenia','1000112')
                           |? FAKS.T().KOR='T' & exec('FindAndGet','#table',FAKS,FAKS.FKSQL,,"D",date(2014,1,1))<date(2014,1,1)
                           || '1000112'
                           || ''
                           ?}
                        || exec('kod_aktualny','tlumaczenia','1000112')
                        ?};
                        _date_head:=exec('tr','tlumaczenia',_txt_ds)
            ?};
            {? FAKS.WHERE<>'L'
                  |
               FAKS.T().HIS<>'T' & PAR_SKID.get(308)='T' & FAKS.DW>=date(2014,1,1)
            || {? FAKS.DO_NA_DR<>date(0,0,0)
               || {? PARAM_W.MC='T'
                  || {? PARAM_W.JEZYK=null || _date_val:=form(FAKS.DO_NA_DR,,8)|| _date_val:=(7+form(FAKS.DO_NA_DR,,1)) ?}
                  || _date_val:=gsub(form(FAKS.DO_NA_DR,,3),'/','-')
                  ?}
               |? FAKS.T().KOR='T' & exec('FindAndGet','#table',FAKS,FAKS.FKSQL,,"D",date(2014,1,1))<date(2014,1,1)
               || {? PARAM_W.MC='T'
                  || {? PARAM_W.JEZYK=null || _date_val:=form(FAKS.D,,8) || _date_val:=(7+form(FAKS.D,,1)) ?}
                  || _date_val:=gsub(form(FAKS.D,,3),'/','-')
                  ?}
               || ''
               ?}
            || {? PARAM_W.MC='T'
               || {? PARAM_W.JEZYK=null || _date_val:=form(FAKS.D,,8) || _date_val:=(7+form(FAKS.D,,1)) ?}
               || _date_val:=gsub(form(FAKS.D,,3),'/','-')
               ?}
            ?};
            {? (var_press('_date_head')=type_of('')) & (var_press('_date_val')=type_of(''))
            ||
               date_head_kz:=_date_head;
               date_val_kz:=_date_val
            ?};~~}
      {FI}
      {IF: (var_press('date_head_kz')=type_of('')) & (var_press('date_val_kz')=type_of(''))}
         {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 (date_head_kz+':')}{COLOR: color_bw}{<41 date_val_kz}
         {VAR_DEL.delete('date_head_kz','date_val_kz');~~}
      {FI}
   {COMMENT}   Rachunek bankowy {COMMENT-}
      {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 (exec('tr','tlumaczenia','1000220')+':')}{COLOR: color_bw}{<41 FAKS.NB}
      {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 (exec('tr','tlumaczenia','1000221')+':')}{COLOR: color_bw}{<41 exec('rb_prefix','faktury_wydruk')+RB.get_rbtx(3,FAKS.RBK,FAKS.SKID_RBK().KRAJ().KODISO)}
      {IF: FAKS.VSKIDRBK}
         {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 (exec('tr','tlumaczenia','1000220')+':')}{COLOR: color_bw}{<41 FAKS.VNB}
         {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 (exec('tr','tlumaczenia','1000221')+':')}{COLOR: color_bw}{<41 exec('rb_prefix','faktury_wydruk')+RB.get_rbtx(3,FAKS.VRBK,FAKS.VSKIDRBK().KRAJ().KODISO)}
      {FI}
   {COMMENT}   Sprzedawca {COMMENT-}
      {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 _sprz:=(exec('tr','tlumaczenia','1000107')+':');((1+_sprz))+~(1-_sprz)}{COLOR: color_bw}{<41
      KH.cntx_psh();
      KH.prefix();
      FAKSKH.cntx_psh();
      FAKSKH.index('UNIK');
      FAKSKH.prefix(FAKS.ref(),exec('rola_grupa_vat','edi_fo_ksef'));
      _fkh_grvat:=FAKSKH.first();
      _kh_grvat:=0;
      {? ~_fkh_grvat
      || _kgv:=exec('kh_grvat_lic','kontrahent',FAKS.DW);
         {? _kgv<>null() & KH.seek(_kgv)
         || _kh_grvat:=1
         ?}
      ?};
      KSTE.index('KST'); KSTE.prefix();
      {? ~_fkh_grvat & ~_kh_grvat & KSTE.first() & +KSTE.NAZ || _oddzial:=1 || _oddzial:=0 ?};
      _wyn:='';
      {? _oddzial
      || _wyn:=KSTE.NAZ+', ';
         _wyn+=exec('kpocz','faktury_wydruk',KSTE.KOD_P,KSTE.POCZ,KSTE.MIA,KSTE.UL)+', ';
         _wyn+=exec('adr','faktury_wydruk',KSTE.UL,KSTE.NR_D,KSTE.NR_L,KSTE.MIA,KSTE.POCZ)
      |? _fkh_grvat
      || _wyn:=FAKSKH.NAZ+', ';
         _wyn+=exec('kpocz','faktury_wydruk',FAKSKH.KPOCZ,FAKSKH.POCZ,FAKSKH.MIASTO,FAKSKH.UL)+', ';
         _wyn+=exec('adr','faktury_wydruk',FAKSKH.UL,FAKSKH.DOM,FAKSKH.LOKAL,FAKSKH.MIASTO,FAKSKH.POCZ)
      |? _kh_grvat
      || _wyn:=KH.NAZ+', ';
         _wyn+=exec('kpocz','faktury_wydruk',KH.KPOCZ,KH.POCZ,KH.MIASTO,KH.UL)+', ';
         _wyn+=exec('adr','faktury_wydruk',KH.UL,KH.DOM,KH.LOKAL,KH.MIASTO,KH.POCZ)
      || _wyn:=XINFO.NAZ+', ';
         _wyn+=exec('kpocz','faktury_wydruk',XINFO.KOD_P,XINFO.POCZ,XINFO.MIA,XINFO.UL)+', ';
         _wyn+=exec('adr','faktury_wydruk',XINFO.UL,XINFO.NR_D,XINFO.NR_L,XINFO.MIA,XINFO.POCZ)
      ?};
      KH.cntx_pop();
      FAKSKH.cntx_pop();
      _wyn}
   {COMMENT}   Nabywca {COMMENT-}
      {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 (exec('tr','tlumaczenia','1000120')+'')}{COLOR: color_bw}{<41
       _wyn:=exec('kontrahent_naz_lotusuid','abstore_fun',FAKS.NAZ,KH.LOTUSUID)+', ';
       _wyn+=exec('kpocz','faktury_wydruk',FAKS.KPOCZ,FAKS.POCZ,FAKS.MIASTO,FAKS.UL)+', ';
       _wyn+=exec('adr','faktury_wydruk',FAKS.UL,FAKS.DOM,FAKS.LOKAL,FAKS.MIASTO,FAKS.POCZ);
       _wyn}
   {COMMENT}   Odbiorca {COMMENT-}
      {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 exec('tr','tlumaczenia','6000115')}{COLOR: color_bw}{<41
       _wyn:=FAKS.O_NAZ+', ';
       _wyn+=exec('kpocz','faktury_wydruk',FAKS.O_KPOCZ,FAKS.O_POCZ,FAKS.O_MIASTO,FAKS.O_UL)+', ';
       _wyn+=exec('adr','faktury_wydruk',FAKS.O_UL,FAKS.O_DOM,FAKS.O_LOKAL,FAKS.O_MIASTO,FAKS.O_POCZ);
       _wyn}
   {COMMENT}   Podmioty {COMMENT-}
      {FOR: FAKSKH}
      {INDEX: 'UNIK'}
      {PREFIX: FAKS.ref()}
      {DO}
         {IF: ~exec('rola_pomin_lsp','edi_fo_ksef2',FAKSKH.ROLA,2) }
            {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 exec('tr_podmiot','faktury_wydruk_new',FAKSKH.ROLA)}{COLOR: color_bw}{<41
             _wyn:=FAKSKH.NAZ+', ';
             _wyn+=exec('kpocz','faktury_wydruk',FAKSKH.KPOCZ,FAKSKH.POCZ,FAKSKH.MIASTO,FAKSKH.UL)+', ';
             _wyn+=exec('adr','faktury_wydruk',FAKSKH.UL,FAKSKH.DOM,FAKSKH.LOKAL,FAKSKH.MIASTO,FAKSKH.POCZ);
             _wyn}
         {FI}
      {OD}
   {COMMENT}   Uwagi {COMMENT-}
      {COLOR: color_gw}{FONT: exec('font','param_wydr','dane')}{>38 _uw:=(exec('tr','tlumaczenia','6000111')+':');((1+_uw))+~(1-_uw)}{COLOR: color_bw}{<41}{
       charleft_korn:=charleft();
       Text:=FAKS.memo_txt(,1,'UWAGI');
       Text:=gsub(Text,'\n',' ');
       STR.split(Text);~~}{WHILE: +(Text:=STR.line(charleft_korn))}{DO}{<41 Text}{LINE}{OD}
   {COMMENT}   mechanizm podzielonej płatności                          {COMMENT-}
      {FONT: 'T8B'}{SPACE: 'B'}
      {  SLU.cntx_psh(); SLO.cntx_psh(); ~~}
      {IF: exec('sp_active','faktury_wspolne',FAKS.DW) & FAKS.SP_WYM='T'}
         {
         Text:='';
         FAKS.cntx_psh();
         FAKS.prefix();
         FAKS_KZF.cntx_psh();
         FAKS_KZF.index('KZ_UID');
         FAKS_KZF.prefix(exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",''));
         _loop:=FAKS_KZF.first();
         {!
         |? _loop
         |!
            {? FAKS.seek(FAKS_KZF.KOR)
                  &
               FAKS.NETTO>0
                  &
               FAKS.SP_WAR<>FAKS.BRUTTO
                  &
               FAKS.T().VATZPOZ='T'
            ||
               SLO.cntx_psh();
               _waln:=FAKS.NWAL().KOD;
               SLO.cntx_pop();
               Text:=
                  ' ('+
                  exec('tr','tlumaczenia','1000189')+': '+form(FAKS.SP_NET,,2,'.,')+' '+_waln+'; '+
                  exec('tr','tlumaczenia','1000191')+': '+form(FAKS.SP_VAT,,2,'.,')+' '+_waln+'; '+
                  exec('tr','tlumaczenia','1000192')+': '+form(FAKS.SP_WAR,,2,'.,')+' '+_waln+
                  ')'
            ?};
            _loop:=FAKS_KZF.next()
         !};
         FAKS_KZF.cntx_pop();
         FAKS.cntx_pop();
         ~~}
         {<1}{LINE}
         {<1}{<{lmt}}{'* - '+exec('tr','tlumaczenia','1000170')}{FONT: 'T8G'}{Text}
      {FI}
      {  SLU.cntx_pop(); SLO.cntx_pop(); ~~}
   {OD}
   { VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT','ii'); FAKS.cntx_pop();~~}
{COMMENT}   przyczyna korekty                                        {COMMENT-}
   {FONT: exec('font','param_wydr','kor_przyczyna')}
   {IF: FAKS.TZP<>''}
      {LINE}
      {<{lmt+1}}{exec('tr','tlumaczenia','3000111')}{' '+FAKS.TZP}
   {FI}
{COMMENT}   podpisujacy                                              {COMMENT-}
   {ENTIRE}
   {FONT: exec('font','param_wydr','podpisy')}
   {<1}{
   max_T8:=charleft;
   _p100230:=exec('get','#params',100230,2);
   {? var_pres('STR1')>=100 || obj_del(STR1) ?};
   STR1:=obj_new(@.CLASS.STRING);
   Text:={? _p100230='O' | _p100230='N' || exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000145')) || '' ?};
   Text1:={? _p100230='O' | _p100230='S' || exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000146')) || '' ?};
   STR.split(Text);
   STR1.split(Text1);
   ~~}
   {WHILE: _a:=+(Text:=STR.line(((max_T8-4)/2)$0-30)); _b:=+(Text1:=STR1.line(((max_T8-4)/2)$0-30)); _a | _b}
   {DO}
      {FIRST}
         {IF: lineleft()<6 & lineleft()>0}{PAGE}{FI}{LINE: 1}
      {FIRST-}
      {IF: +Text | +Text1}
         {IF: +Text}{<{lmt+1}Text}{FI}{IF: +Text1}{<{((max_T8-4)/2)$0+20}Text1}{FI}
      {FI}
   {OD}
{COMMENT}   upowaznienie                                             {COMMENT-}
   {FONT: exec('font','param_wydr','upowaznienie')}
   {_p100230:=exec('get','#params',100230,2);
   _os:=form(exec('kh_osob_default','kontrahent',KH.ref));
   USERS.cntx_psh;
   USERS.index('USR_KKOD');
   USERS.prefix;
   Text:={? _p100230='O' | _p100230='N' || {? +_os || _os || 20*'.' ?} || '' ?};
   Text1:={? _p100230='O' | _p100230='S' || {? USERS.find_key(FAKS.USER,FAKS.USER) & +USERS.DANE || USERS.DANE || 20*'.' ?} || '' ?};
   USERS.cntx_pop;
   STR.split(Text);
   STR1.split(Text1)
   ;~~}
   {WHILE: _a:=+(Text:=STR.line(((max_T8-4)/2)$0-30)); _b:=+(Text1:=STR1.line(((max_T8-4)/2)$0-30)); _a | _b}
   {DO}
      {FIRST}
         {LINE: 4}
      {FIRST-}
      {IF: +Text | +Text1}
         {IF: +Text}{<{lmt+1}Text}{FI}{IF: +Text1}{<{((max_T8-4)/2)$0+20}Text1}{FI}
      {FI}
   {OD}
   {ENTIRE-}
   {ENTIRE-}
{COMMENT}   KONIEC FAKTURY                                           {COMMENT-}
   {HEADER}{HEADER-}
   { duplex:=page%*2=1;~~}
   {IF: lineleft()>0}{PAGE}{FI}
   {IF: BEER.SZ='S' & exec('get','#params',2124,2,OPERATOR.USER)='T' & duplex=1}
      {duplex:=2;~~}
      {PAGE}
      {duplex:=0;~~}
   {FI}
{OD}
{MACRO-}
:!UTF-8
{TITLE:Raport - ankieta}
{PRINTER: exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:=ZA_ZEST.NAZWA;

   __INFO:=obj_new(4);__INFOO:=obj_new(4);
   {! _n:=1..obj_len(__INFO)
   |! __INFO[_n]:=__INFOO[_n]:=''
   !};

   head:=0;
   lmt:=0;
   max:=0;

   nag:=ZA_ZEST.memo_txt(,,'NAG');
   ZZ_KOMPK.cntx_psh;
   ZZ_KOMPK.index('WARTOSC');

   ZZ_KOMPS.cntx_psh;
   ZZ_KOMPS.index('WARTOSC');

   ZZ_KOMPZ.cntx_psh;
   ZZ_KOMPZ.index('UNIQUE');

   ~~
}
{END:
   obj_del(__INFO);obj_del(__INFOO);

   &head;
   &max;
   &lmt;
   &nag;

   ZZ_KOMPK.cntx_pop;
   ZZ_KOMPS.cntx_pop;
   ZZ_KOMPZ.cntx_pop;

   ~~
}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
==================================================================

   K O N F I G U R A C J A   -   N A G L O W E K,   S T O P K A

==================================================================
{COMMENT-}
{HEADER}
{IF: ~head}
{ head:=1; ~~ }
{SUBSTITUTE: 'HEAD'}
{LINE}
{FI}
{HEADER-}
{FOOTER: 0}
{SPACE: 'C'}
{FONT: 'W8'}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
=======================================

   Z A W A R T O S C   W Y D R U K U

=======================================
{COMMENT-}
{DEFINEFONT: 'W8I='+PAR_WYDR.FONTW+',100,O,8'}
{  {? ZA_ZEST.ZZ_LINK().KLASA=2-!SZK_OPIS
   || __INFO[1]:=SZK_OPIS.OD$4;
      __INFO[2]:=SZK_OPIS.ORG().NAZWA;
      __INFO[3]:=SZK_OPIS.TEMAT().TEMAT;
      __INFO[4]:=
         {? SZK_OPIS.TRENER
         || SZK_OPIS.TRENER().PIERWSZE+' '+ZZ_OSOBA.NAZWISKO
         || 40*'_'
         ?};
      __INFOO[1]:='Data szkolenia:';
      __INFOO[2]:='Organizator szkolenia:';
      __INFOO[3]:='Temat szkolenia:';
      __INFOO[4]:='Imię i nazwisko trenera:'
   |? ZA_ZEST.ZZ_LINK().KLASA=2-!RP_PROC
   || RP_PROC.RP_OFE();
      __INFO[1]:='';
      __INFO[2]:=RP_OFE.SYM;
      __INFO[3]:=RP_OFE.TYTUL;
      __INFO[4]:='';
      __INFOO[1]:='Data wypełnienia:';
      __INFOO[2]:='Numer referencyjny:';
      __INFOO[3]:='Tytuł:';
      __INFOO[4]:='Imię i nazwisko kandydata:'
   ?};
   ~~
}
{SPACE: 'C'}
{FONT: 'W8'}
{  max:=charleft;
   ~~
}
{IF: ZA_ZEST.ZZ_LINK().KLASA=2-!SZK_OPIS | ZA_ZEST.ZZ_LINK().KLASA=2-!RP_PROC}
{LINE}
{FONT: 'W8B'}{__INFOO[1]}{FONT: 'W8'} {__INFO[1]}
{LINE}
{FONT: 'W8B'}{__INFOO[2]}{FONT: 'W8'} {__INFO[2]}
{LINE}
{FONT: 'W8B'}{__INFOO[3]}{FONT: 'W8'} {__INFO[3]}
{LINE}
{FONT: 'W8B'}{__INFOO[4]}{FONT: 'W8'} {__INFO[4]}
{FI}
{LINE}
{REP: exec('memo2rep','phr_druk',ZA_ZEST,'NAG',ZA_ZEST.ref(),,130)}
{LINE}
{FOR: ZA_TEST}{INDEX: 'INDEKS'}{PREFIX: ZA_ZEST.ref}{DO}
{ENTIRE}
{FONT: 'W8'+{? ~ZA_TEST.ZA_TEST || 'B' || 'I' ?}}
{REP:
   _txt:={? ~ZA_TEST.ZA_TEST || ZA_TEST.INDEKS+' ' || '' ?}+ZA_TEST.TRESC;
    exec('string2rep','phr_druk',_txt,,130)
}
{LINE}
{FONT: 'W8'}
{IF: ZA_TEST.ZZ_MET().KOD='SP'}
{FOR: ZZ_KOMPS}
{INDEX: 'WARTOSC'}
{PREFIX: ZA_TEST.NP_DOK,ZA_TEST.ZZ_DOK,null}
{DO}
{REP:
   $ZZ_KOMPS.WARTOSC+'  -  '+ZZ_KOMPS.OPIS
}
{LINE}
{OD}
{ELIF: ZA_TEST.ZZ_MET().KOD='KW'}
{FOR: ZZ_KOMPK}
{INDEX: 'WARTOSC'}
{PREFIX: ZA_TEST.NP_DOK,ZA_TEST.ZZ_DOK,null}
{DO}
{REP:
   $ZZ_KOMPK.KOD+'  -  '+ZZ_KOMPK.OPIS
}
{LINE}
{OD}
{ELIF: ZA_TEST.ZZ_MET().KOD='WB'}
{REP:
   _line:='';
   ZZ_KOMPZ.prefix(ZA_TEST.NP_DOK,ZA_TEST.ZZ_DOK,null);
   {? ZZ_KOMPZ.first
   || _off:=int(charleft/(ZZ_KOMPZ.MAKS-ZZ_KOMPZ.MIN+1));
      {! _n:=ZZ_KOMPZ.MIN..ZZ_KOMPZ.MAKS
      |! _col:=(_n-1)*_off+int(_off/2);
         _line+='{<'+$_col+'}'+$_n
      !}
   ?};
   _line
}
{ELIF: ZA_TEST.RODZ<>null() & ZA_TEST.RODZ<>exec('kod','ext_slo','ZA_TEST','O')}
{FOR: ZA_NOTAO}{INDEX: 'NUMER'}{PREFIX: ZA_TEST.ref()}{DO}
{REP:
   _litera:=exec('get_abc','phr_dane',ZA_NOTAO.NUMER);
   _txt:=_litera+')'+' '+ZA_NOTAO.TRESC;
   exec('string2rep','phr_druk',_txt,,130)
}
{OD}
{FI}
{IF: ZA_TEST.ZZ_MET().KOD='OO' | ZA_TEST.RODZ=exec('kod','ext_slo','ZA_TEST','O')}
{SPACE: 'A'}
{int(charleft)*'_'}
{int(charleft)*'_'}
{int(charleft)*'_'}
{SPACE: 'C'}
{FI}
{LINE}
{ENTIRE-}
{OD}
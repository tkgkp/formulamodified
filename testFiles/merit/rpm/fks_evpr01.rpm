:!UTF-8
{TITLE:A. Ewidencja nabyć do korekty podatku VAT}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:   {? var_pres('TTMP')>100  || obj_del(TTMP) ?};
          {? var_pres('TTMP1')>100  || obj_del(TTMP1) ?};
          PRN:=obj_new(@.CLASS.PRINT, 11); PRN.s_frame();
          PRNH:=obj_new(@.CLASS.PRINT, 3); PRNH.s_frame();
          PRNH1:=obj_new(@.CLASS.PRINT, 2); PRNH1.s_frame();
          PRNR:=obj_new(@.CLASS.PRINT, 6); PRNR.s_frame();
          lp:=j:=0;w:=obj_new(11);
          prn1:=prn2:='PRN';
          lm:=ll:=lmt:=0;rsep:=1; vp:=1; dl:=1; bl:=lf:=0;
          TTMP:=tab_tmp(2,'KROK','STRING[2]' ,'KROK',
                          'ROK' ,'STRING[20]','ROK',
                          'KO'  ,'REAL'      ,'KO',
                          'KK'  ,'REAL'      ,'KK',
                          'ONR' ,'INTEGER'   ,'ONR',
                          'OKR' ,'STRING[41]','OKR',
                          'KOO' ,'REAL'      ,'KOO',
                          'KOK' ,'REAL'      ,'KOK',
                          'DOL','STRING[1]'  ,'DOL');
          IND:=TTMP.ndx_tmp('',0,'KROK',,0,'KROK',,0,'ONR',,0);TTMP.index(IND);
          TTMP1:=tab_tmp(2,'KROK','STRING[2]' ,'KROK',
                           'ROK' ,'STRING[20]','ROK',
                           'ONR' ,'INTEGER'   ,'ONR',
                           'OKR' ,'STRING[41]','OKR',
                           'KOO' ,'REAL'      ,'KOO',
                           'KOK' ,'REAL'      ,'KOK');
          IND1:=TTMP1.ndx_tmp('',0,'KROK',,0,'KROK',,0,'ONR',,0);TTMP1.index(IND1);
          ROZNE.win_edit('KOR_VAT');
          OKRO_F.win_dict('WER');
          VAT7.ROK:=null;
          ROZNE.UT_ROKOD:=ROZNE.UT_ROKDO:=SSTALE.AR;
          ROZNE.UT_OKROD:=ROZNE.UT_OKRDO:=SSTALE.AO;
          ROZNE.KV_ROKOD:=ROZNE.KV_ROKDO:=ROZNE.KV_OKROD:=ROZNE.KV_OKRDO:=null;
          edycja:=ROZNE.edit("exec('kor_vat','dok_fks')")
}
{END:     obj_del(PRN); obj_del(PRNH); obj_del(PRNH1); obj_del(PRNR); obj_del(TTMP); obj_del(TTMP1);
          &PRN; &PRNH; &PRNH1; &PRNR; &TTMP; &TTMP1; &IND;&IND1;
          {? var_pres('color')>0 || &color ?}; {? var_pres('s_sign')>0 || &s_sign ?};
          obj_del(w);&w;&lp; &edycja; &j;&prn1;&prn2;&rsep;&vp;&lm;&ll;&lmt;&dl;&lf;&bl;
          VAT7.ROK:=null
}
{EXIT: ~edycja}
{ PRN.add("w[1]",4,'LP',1);
  PRN.add("w[2]",40,'Oznaczenie środka     #Opis              ',,'#');
  PRN.add("w[3]",22,'Okres#Symbol dokumentu   ',,'#');
  PRN.add("w[4]",25,'Rej. ks. - Jedn. ks. - Nr#Rej. VAT',,'#');
  PRN.add("w[5]",9,'% prewsk.#% str.#Ile lat',1,'#');
  PRN.add("w[6]",15,'Podatek nalicz.#Do odliczenia',1,'#',,,,',2,\' ,\'');
  PRN.add("w[7]",18,'za rok');
  PRN.add("w[11]",9,'% prewsk.',1,,,,,',2,\' ,\'');
  PRN.add("w[8]",6,'% str.',1,,,,,',2,\' ,\'');
  PRN.add("w[9]",14,'kwota odliczenia',1,,,,,',2,\' ,\'');
  PRN.add("w[10]",14,'kwota korekty',1,,,,,',2,\' ,\'');
  PRNH.add("",100,'Opis dokumentu');
  PRNH.add("",27,'Nabycie');
  PRNH.add("",73,'Kwoty korekt podatku VAT');
  PRNH1.add("",58,'Kwoty odliczeń rocznych (nabycie)');
  PRNH1.add("",79,'Kwoty odliczeń miesięcznych (zbycie)');
  PRNR.add("w[1]",20,'Rok');
  PRNR.add("w[2]",16,'Kwota odliczeń',1,,,,'SKO',',2,\' ,\'');
  PRNR.add("w[3]",16,'Kwota korekt',1,,,,'SKK',',2,\' ,\'');
  PRNR.add("w[4]",41,'Okres');
  PRNR.add("w[5]",16,'Kwota odliczeń',1,,,,'SKOO',',2,\' ,\'');
  PRNR.add("w[6]",16,'Kwota korekt',1,,,,'SKOK',',2,\' ,\'');
  PAR_WYDR.TITLE:='Ewidencja nabyć do korekty podatku VAT';
  {!_i:=1..11 |! w[_i]:=''!};~~}
{COMMENT}------------------------------------T A B H E A D 0------------------------------------{COMMENT-}
{MACRO: 'TABHEAD0'}
{  PRNH.setcolor(color);
   PRNH.ini_head(); ~~}
{REP: PRNH.fhbar()}
{WHILE: PRNH.h_next()}
{DO}
   {REP: PRNH.h_fmline()}
{OD}
{REP: PRNH.fjbar(PRN)}
{  PRN.setcolor(color);
   PRN.ini_head();~~}
{WHILE: PRN.h_next()}
{DO}
   {REP: PRN.h_fmline()}
{OD}
{MACRO-}
{COMMENT}------------------------------------T A B H E A D 1------------------------------------{COMMENT-}
{MACRO: 'TABHEAD1'}
{  PRNH1.setcolor(color);
   PRNH1.ini_head(); ~~}
{REP: PRNH1.fhbar()}
{WHILE: PRNH1.h_next()}
{DO}
   {REP: PRNH1.h_fmline()}
{OD}
{REP: PRNH1.fjbar(PRNR)}
{  PRNR.setcolor(color);
   PRNR.ini_head();~~}
{WHILE: PRNR.h_next()}
{DO}
   {REP: PRNR.h_fmline()}
{OD}
{REP: PRNR.fbar()}
{PRNR.setcolor('255:255:255');~~}
{MACRO-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}-------------------------------------H E A D E R---------------------------------------{COMMENT-}
{HEADER}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{FONT: 'HEAD'}{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}
{<3 'Parametry wydruku'}
{<6 'Zakres nabyć:'} {{?VAT7.ZAKRES='N'||'nierozliczone'|?VAT7.ZAKRES='T'||'rozliczone'||'wszystkie'?}}
{<6 'Okres nabycia:'} {ROZNE.UT_ROKOD().NAZ} {ROZNE.UT_OKROD().NAZ} - {ROZNE.UT_ROKDO().NAZ} {ROZNE.UT_OKRDO().NAZ}
{<6 'Korekty za rok:'} {{?VAT7.ROK<>null||VAT7.ROK().NAZ||''?}}
{<6 'Okres zbycia:'} {IF: ROZNE.KV_OKROD<>null}{ROZNE.KV_ROKOD().NAZ} {ROZNE.KV_OKROD().NAZ} - {ROZNE.KV_ROKDO().NAZ} {ROZNE.KV_OKRDO().NAZ}{FI}
{FI}
{LINE: 1}{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD0'}
{REP: PRN.fbar()}{ vp:=0;~~}
{HEADER-}
{COMMENT}-------------------------------------F O O T E R---------------------------------------{COMMENT-}
{FOOTER:0}
{REP: PRN.lbar()}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}-------------------------------------L I N I A X 0-------------------------------------{COMMENT-}
{MACRO: 'LINIAX0'}
{FONT: 'T8G'}{SPACE: 'C'}
 {IF: PRN.setcolor('255:255:255'); lf:=lineleft(); PRN.ini_line(); lf<PRN.length() & lf<>0}
  {PAGE}
 {FI}
 {ENTIRE}
{IF: lf<>0 & dl}
{REP: PRN.fbar()}
{FI}
 {WHILE: PRN.next()}
  {DO}
{IF: bl}{FONT: 'T8GB'}{SPACE: 'C'}{FI}
{REP: PRN.fline()}
{FONT: 'T8G'}{SPACE: 'C'}
  {OD}
 {ENTIRE-}
{MACRO-}
{COMMENT}-------------------------------------L I N I A X R-------------------------------------{COMMENT-}
{MACRO: 'LINIAXR'}
{PRNR.setcolor('255:255:255');~~}
 {IF: lf:=lineleft(); PRNR.ini_line(); lf<PRNR.length() & lf<>0}
  {PAGE}
 {FI}
 {ENTIRE}
{IF: lf<>0 & lf<>1 & dl}
{REP: PRNR.fbar()}
{FI}
 {WHILE: PRNR.next()}
  {DO}
{REP: PRNR.fline()}
  {OD}
 {ENTIRE-}
{MACRO-}
{COMMENT}---------------------------------------W Y D R U K--------------------------------------{COMMENT-}
{FOR: 'VAT_SR'}
 {INDEX: 'OK'}
 {FROM: REF.FIRMA,ROZNE.UT_OKROD}
 {TO:   REF.FIRMA,ROZNE.UT_OKRDO}
 {DO}
   {IF:  ((VAT7.ZAKRES<>'' & VAT_SR.ROZL=VAT7.ZAKRES) | VAT7.ZAKRES='')
         &( VAT_SR.OKRES().ROK().POCZ_ROK>=ROZNE.UT_ROKOD().POCZ_ROK & VAT_SR.OKRES().ROK().POCZ_ROK<=ROZNE.UT_ROKDO().POCZ_ROK
            & {? ROZNE.UT_ROKOD().POCZ_ROK=VAT_SR.OKRES().ROK().POCZ_ROK
              || VAT_SR.OKRES().NR>=ROZNE.UT_OKROD().NR
              || 1
              ?}
            & {? ROZNE.UT_ROKDO().POCZ_ROK=VAT_SR.OKRES().ROK().POCZ_ROK
              || VAT_SR.OKRES().NR<=ROZNE.UT_OKRDO().NR
              || 1
              ?}
            )
         & {? ROZNE.KV_ROKOD=null
           ||1
           || VAT_SR.OKRES_S().ROK().POCZ_ROK>=ROZNE.KV_ROKOD().POCZ_ROK & VAT_SR.OKRES_S().ROK().POCZ_ROK<=ROZNE.KV_ROKDO().POCZ_ROK
            & {? ROZNE.KV_ROKOD().POCZ_ROK=VAT_SR.OKRES_S().ROK().POCZ_ROK
              || VAT_SR.OKRES_S().NR>=ROZNE.KV_OKROD().NR
              || 1
              ?}
            & {? ROZNE.KV_ROKDO().POCZ_ROK=VAT_SR.OKRES_S().ROK().POCZ_ROK
              || VAT_SR.OKRES_S().NR<=ROZNE.KV_OKRDO().NR
              || 1
              ?}
           ?}}
     {j:=0;dl:=1;{!_i:=1..11 |! w[_i]:=''!};bl:=0;
      w[2]:=form(VAT_SR.INR,40);
      w[3]:=form(VAT_SR.OKRES().ROK().NAZ+' '+VAT_SR.OKRES().NAZ,22);
      exec('ust_vats','dok_fks');w[4]:=form(VAT7.REJ_N().KOD+'-'+VAT7.REJ_N().ODD().OD+'-'+$VAT7.NR_N,25);
      w[5]:=form(VAT_SR.PROC_PR,,2,' ,');w[6]:=VAT_SR.VAT;~~}
     {FOR: 'VAT_KW'}
      {INDEX: 'VAT_KW'}
      {PREFIX: VAT_SR.ref()}
      {DO}
       {IF:  ((VAT7.ROK<>null & VAT_KW.ROK().POCZ_ROK=VAT7.ROK().POCZ_ROK) | VAT7.ROK=null)
              & (j<3 | (VAT_KW.OKRES=null | VAT_KW.OKRES=VAT7.ROK))}
         {j+=1;w[7]:=VAT_KW.ROK().NAZ; w[8]:=VAT_KW.PROC; w[11]:=VAT_KW.PROC_PR;
          {? j=1 || lp+=1; w[1]:=$lp ?};
          {? j=2 || w[1]:=''; w[2]:=VAT_SR.NST; w[3]:=VAT7.SYM_N;
                    w[4]:=VAT7.RVAT_N().SYM; w[5]:=form(VAT_SR.PROC,,2,' ,');
                    w[6]:=VAT_SR.ODLICZ
          |? j=3 || {!_i:=1..4|!w[_i]:=''!}; w[5]:=form(VAT_SR.ILE_LAT,,0); w[6]:=''
          |? j>3 || {!_i:=1..6|!w[_i]:=''!} ?};
          {? VAT_KW.OKRES=null | VAT_KW.OKRES=VAT7.ROK
          || w[9]:=VAT_KW.ODLICZ; w[10]:=VAT_KW.KOREKTA
          || w[9]:=w[10]:=0
          ?}; lo:=1;
          {?j>1|| dl:=0 ?};
          {? TTMP.prefix();TTMP.find_key(VAT_KW.ROK().KOD,VAT_KW.ROK().KOD)
          || TTMP.KO+=w[9]; TTMP.KK+=w[10]; TTMP.put()
          || TTMP.blank();TTMP.KROK:=VAT_KW.ROK().KOD; TTMP.ROK:=VAT_KW.ROK().NAZ; TTMP.KO:=w[9]; TTMP.KK:=w[10];TTMP.add()
          ?};~~}
{SUBSTITUTE: 'LINIAX0'}
       {FI}
      {OD}
      {IF: VAT7.ROK=null & j=0}
       {j:=1;lp+=1; w[1]:=$lp;~~}
{SUBSTITUTE: 'LINIAX0'}
      {FI}
      {IF: j=1}
       {w[1]:='';w[2]:=VAT_SR.NST;w[3]:=VAT7.SYM_N;w[4]:=VAT7.RVAT_N().SYM;
        w[5]:=form(VAT_SR.PROC,,2,' ,');w[6]:=VAT_SR.ODLICZ;dl:=0;
        {!_i:=7..11|!w[_i]:=''!};~~}
{SUBSTITUTE: 'LINIAX0'}
       {{!_i:=1..4|!w[_i]:=''!};w[5]:=form(VAT_SR.ILE_LAT,,0); w[6]:='';~~}
{SUBSTITUTE: 'LINIAX0'}
      {FI}
      {IF: j=2}
       {{!_i:=1..4|!w[_i]:=''!};w[5]:=form(VAT_SR.ILE_LAT,,0); w[6]:='';
        {!_i:=7..11|!w[_i]:=''!};~~}
{SUBSTITUTE: 'LINIAX0'}
      {FI}
      {IF: j>0 & VAT_SR.OKRES_S<>null & VAT7.ROK=null | VAT7.ROK=VAT_SR.OKRES_S().ROK & (ROZNE.KV_ROKOD=null |
                                   (VAT_SR.OKRES_S().ROK().POCZ_ROK>=ROZNE.KV_ROKOD().POCZ_ROK & VAT_SR.OKRES_S().ROK().POCZ_ROK<=ROZNE.KV_ROKDO().POCZ_ROK
                                    & {? ROZNE.KV_ROKOD().POCZ_ROK=VAT_SR.OKRES_S().ROK().POCZ_ROK
                                      || VAT_SR.OKRES_S().NR>=ROZNE.KV_OKROD().NR
                                      || 1
                                      ?}
                                    & {? ROZNE.KV_ROKDO().POCZ_ROK=VAT_SR.OKRES_S().ROK().POCZ_ROK
                                      || VAT_SR.OKRES_S().NR<=ROZNE.KV_OKRDO().NR
                                      || 1
                                      ?}))}
       {j:=0;w[1]:='';w[2]:='zbycie środka';w[3]:=form(VAT_SR.OKRES_S().ROK().NAZ+' '+VAT_SR.OKRES_S().NAZ,22);
        w[4]:={? VAT7.REJ_S().KOD<>''|| form(VAT7.REJ_S().KOD+'-'+$VAT7.NR_S,15)||''?};w[5]:='';w[6]:='';bl:=1;~~}
       {FOR: 'VAT_KW'}
        {INDEX: 'VAT_KW'}
        {PREFIX: VAT_SR.ref()}
         {DO}
          {IF: VAT_KW.OKRES<>null}
           {j+=1;w[7]:=VAT_KW.OKRES().ROK().NAZ;w[8]:=VAT_KW.PROC;w[11]:=VAT_KW.PROC_PR;
            w[9]:=VAT_KW.ODLICZ; w[10]:=VAT_KW.KOREKTA;
            TTMP.KO:=TTMP.KK:=0;
            'dodanie podsumowan do zbycia';
            TTMP1.prefix(VAT_KW.OKRES().ROK().KOD, VAT_KW.OKRES().ROK().KOD);
            {? TTMP1.first() & TTMP1.find_key(VAT_KW.OKRES().NR)
            || TTMP1.KOO+=VAT_KW.ODLICZ; TTMP1.KOK+=VAT_KW.KOREKTA; TTMP1.put()
            || TTMP1.KROK:=VAT_KW.OKRES().ROK().KOD;
               TTMP1.ROK:=VAT_KW.OKRES().ROK().NAZ;
               TTMP1.OKR:=VAT_KW.OKRES().ROK().NAZ+' '+VAT_KW.OKRES().NAZ;
               TTMP1.KOO:=VAT_KW.ODLICZ; TTMP1.KOK:=VAT_KW.KOREKTA;
               TTMP1.ONR:=VAT_KW.OKRES().NR;
               TTMP1.add()
            ?};~~}
{SUBSTITUTE: 'LINIAX0'}
          {FI}
         {OD}
       {IF: j=1}
        {w[2]:='';w[3]:=VAT7.SYM_S;w[4]:='';w[7]:=w[8]:=w[9]:=w[10]:=w[11]:='';~~}
{SUBSTITUTE: 'LINIAX0'}
       {FI}
      {FI}
   {FI}
 {OD}
{'przerzucenie danych z TTMP1 do TTMP';
 {? TTMP.prefix();TTMP1.prefix(); TTMP1.first()
 || {!|? {? TTMP.find_key(TTMP1.KROK,TTMP1.KROK,TTMP1.ONR)
         || TTMP.KOO+=TTMP1.KOO; TTMP.KOK+=TTMP1.KOK; TTMP1.put()
         || {? TTMP.find_key(TTMP1.KROK,TTMP1.KROK)
            || _p:=0; TTMP.prefix(TTMP1.KROK,TTMP1.KROK);
               {? TTMP.first() || {!|? {? TTMP.OKR=''|| _p:=1 ?};~_p & TTMP.next() !} ?};
               {? _p
               || TTMP.ONR:=TTMP1.ONR;TTMP.OKR:=TTMP1.OKR;
                  TTMP.KOO:=TTMP1.KOO;TTMP.KOK:=TTMP1.KOK;
                  TTMP.DOL:=''; TTMP.put()
               || TTMP.KROK:=TTMP1.KROK;TTMP.DOL:='D';
                  TTMP.ONR:=TTMP1.ONR;TTMP.OKR:=TTMP1.OKR;
                  TTMP.KOO:=TTMP1.KOO;TTMP.KOK:=TTMP1.KOK;
                  TTMP.add()
               ?}; TTMP.prefix()
            || TTMP.KROK:=TTMP1.KROK;TTMP.ROK:=TTMP1.ROK;
               TTMP.DOL:='N';
               TTMP.KO:=TTMP.KK:=0;
               TTMP.ONR:=TTMP1.ONR;TTMP.OKR:=TTMP1.OKR;
               TTMP.KOO:=TTMP1.KOO;TTMP.KOK:=TTMP1.KOK;
               TTMP.add()
            ?}
         ?};
         TTMP1.next() !}
 ?};~~}
{IF: TTMP.first()}
{prn1:=prn2:='PRNR';dl:=0;~~}
{IF: lineleft()>7}
{FOOTER:0}
{REP: PRNR.flbar()}
{FOOTER-}
{REP: PRN.lbar()}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD1'}
{FONT: 'T8G'}{SPACE: 'C'}
{ELSE}
{IF: lineleft()>0}
{PAGE}
{FI}
{FI}
{HEADER}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{FONT: 'HEAD'}{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}

{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD1'}
{FONT: 'T8G'}{SPACE: 'C'}
{HEADER-}
{FOOTER:0}
{REP: PRNR.flbar()}

{<1>{PRN.width()} 'KONIEC WYDRUKU'}
{FOOTER-}
{FOR: 'TTMP'}
{INDEX: IND}
{DO}
 {w[1]:={?TTMP.DOL='D'||''||TTMP.ROK?};
  w[2]:={?TTMP.DOL='D'||''||TTMP.KO?};
  w[3]:={?TTMP.DOL='D'||''||TTMP.KK?};w[4]:=TTMP.OKR;w[5]:=TTMP.KOO;w[6]:=TTMP.KOK;~~}
 {SUBSTITUTE: 'LINIAXR'}
 {dl:=0;~~}
{OD}
{w[1]:='Suma:';w[2]:=PRNR.sum('SKO');w[3]:=PRNR.sum('SKK');w[4]:='';w[5]:=PRNR.sum('SKOO');w[6]:=PRNR.sum('SKOK');dl:=1;~~}
{SUBSTITUTE: 'LINIAXR'}
{ELSE}
{FOOTER:0}
{FOOTER-}
{DIRECT}
{REP: PRN.flbar()}

{<1>{PRN.width()} 'KONIEC WYDRUKU'}
{DIRECT-}
{FI}
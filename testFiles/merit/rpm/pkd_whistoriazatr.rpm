:!UTF-8
{TITLE:Przebieg pracy zawodowej}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   tresc:='pkd_whistoriazatr';
   PAR_WYDR.TITLE:='Przebieg pracy zawodowej'@;
   VAR_DEL.delete('par','__hum');
   par:=obj_new(11);
   P.cntx_psh();
   H.cntx_psh();
   H.index('HISTUM');
   par[11]:=0;

   _upr:=exec('uprawnieniawrap','pkd','przebiegu zatrudnienia'@,'PKD_EZK_PPZA','PKD_EZK_RPZA');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[3]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,,,,,,,,
         'Zakres prezentowanych danych o zatrudnieniu'@,'Umowy'@,'Pełny przebieg'@,0
      );
      {? par[11]:=par[3].size()
      || par[1]:=obj_new(@.CLASS.PRINT,{? rep_export()|| 24 || 15 ?});
         {? rep_export()
         || par[1].add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE+' '+'(Nr teczki: %1)'@ [P.T]",150,
               'Pracownik'@);
            par[1].add("H_UM.RU().K",3,'Typ'@);
            par[1].add("H_UM.NUMER",20,'Nr umowy'@);
            par[1].add("par[9]",48,'Przedstawiciel zakładu'@);
            par[1].add("H_UM.OD$1",10,'Od dnia'@);
            par[1].add("H_UM.DO$1",10,'Do dnia'@);
            par[1].add("par[10]",42,'Miejsce pracy z umowy'@);
            par[1].add("exec('uspkod','personel')",24,'Przyczyna ustania'@);
            par[1].add("H_UM.EKW+H_UM.EKW_NSP",20,'Ekwiwalent'@,1,,,,,'15,2')
         ?};
         par[1].add("H.KZ().KZ",3,'Kod'@);
         par[1].add("H.WYDZIAL().SYMBOL",16,'Jednostka organizacyjna.'@);
         par[1].add("H.ST().ST",23,'Stanowisko'@);
         par[1].add("H.WY",6,'Wymiar'@,1,,,,,'6,3');
         par[1].add("H.OD$1",10,'Od dnia'@);
         par[1].add("H.DO$1",10,'Do dnia'@);
         par[1].add("H.S1",10,MS.name('H','S1'),1,,,,,'12,2');
         par[1].add("{? H.CZYWAL='T' || H.WAL().KOD || 'PLN' ?}",6,'Waluta'@);
         par[1].add("H.S2",10,MS.name('H','S2'),1,,,,,'7,2');
         par[1].add("{? H.S2T='P' || '%' |? H.CZYWAL2='T' || H.WAL().KOD || 'PLN' ?}",6,'Waluta'@);
         par[1].add("H.S3",10,MS.name('H','S3'),1,,,,,'7,2');
         par[1].add("{? H.S3T='P' || '%' |? H.CZYWAL3='T' || H.WAL().KOD || 'PLN' ?}",6,'Waluta'@);
         par[1].add("H.S4",10,MS.name('H','S4'),1,,,,,'12,2');
         par[1].add("{? H.CZYWAL4='T' || H.WAL().KOD || 'PLN' ?}",10,'Waluta'@);
         par[1].add("H.RU().O",19,'Rodzaj umowy'@);
         par[1].s_frame();

         par[7]:=obj_new(@.CLASS.PRINT,{? rep_export() || 9 || 8 ?});
         {? par[3].ASK & rep_export()
         || ~~
         || {? rep_export()
            || par[7].add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' (Nr teczki: %1)'@[P.T]",150,'Pracownik'@)
            ?};
            par[7].add("H_UM.RU().K",3,'Typ'@);
            par[7].add("H_UM.NUMER",20,'Nr umowy'@);
            par[7].add("par[9]",48,'Przedstawiciel zakładu'@);
            par[7].add("H_UM.OD$1",10,'Od dnia'@);
            par[7].add("H_UM.DO$1",10,'Do dnia'@);
            par[7].add("par[10]",43,'Miejsce pracy z umowy'@);
            par[7].add("exec('uspkod','personel')",23,'Przyczyna ustania'@);
            par[7].add("H_UM.EKW+H_UM.EKW_NSP",19,'Ekwiwalent'@,1,,,,,'15,2');
            par[7].s_frame()
         ?};

         lmt:=rsep:=ileh:=petla:=par[5]:=par[6]:=par[8]:=0;
         par[9]:=par[10]:='';
         vp:=par[4]:=b_rat:=h_rat:=1;

         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   H.cntx_pop();
   VAR_DEL.delete('par','vp','lmt','rsep','tresc','b_rat','h_rat','ileh','petla','__hum')
}
{COMMENT}OLD: wsthist5.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT:  ~par[11]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
{SPACE: 'C'}
{LINE}
{ par[4]:=0; ~~ }
{<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{IF: par[3].ASK}
{<{ceil(lmt*h_rat)+1}'Wydruk pełnego przebiegu zatrudnienia'@}
{ELSE}
{<{ceil(lmt*h_rat)+1}'Wydruk umów o pracę'@}
{FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{  par[7].setcolor(color); par[7].ini_head();
   par[1].setcolor(color); par[1].ini_head(); ~~
}
{REP: par[7].fhbar(lmt)}
{REP: par[7].h_fline(lmt)}
{IF: par[3].ASK}
{REP: '{<{lmt+1}}'+par[7].fjbar(par[1])}
{REP: par[1].h_fline(lmt)}
{REP: par[1].flbar(lmt)}
{ par[1].setcolor('255:255:255'); ~~ }
{ELSE}
{REP: par[7].flbar(lmt)}
{ par[7].setcolor('255:255:255'); ~~ }
{FI}
{HEADER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'full'}
{FOR: H_UM}{INDEX: 'OD'}{PREFIX: P.ref()}{DO}
{IF: lineleft() & lineleft()<=7}
{PAGE}
{FI}
{FIRST}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: par[6]<>P.ref()}{ par[5]+=1; par[6]:=P.ref(); ~~ }{FI}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par[5],,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FIRST-}
{ H.prefix(H_UM.ref());
  {? H.first()
  || par[9]:=H.PRZEDST;
     par[10]:=H.MIEJSCE
  || par[9]:=par[10]:=''
  ?}; ~~
}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[8]:=0; ~~ }
{REP: par[7].fhbar(lmt)}
{IF: ~rep_export()}{REP: par[7].ini_line(); par[7].fline(lmt)}{FI}
{WHILE: par[7].next()}
{DO}
{REP: par[7].fline(lmt)}
{OD}
{FOR: H}{INDEX: 'HISTUM'}{PREFIX: H_UM.ref()}{DO}
{FIRST}
{IF: lineleft() & lineleft()<=4}
{REP: par[7].flbar(lmt)}
{PAGE}
{REP: par[1].fhbar(lmt)}
{ELSE}
{REP: '{<{lmt+1}}'+par[7].fjbar(par[1])}
{FI}
{ par[8]:=1; ileh:=H.size(); petla:=0; ~~ }
{FIRST-}
{REP: par[1].ini_line(); par[1].fline(lmt)}
{WHILE: par[1].next()}
{DO}
{REP: par[1].fline(lmt)}
{OD}
{ petla+=1; ~~ }
{IF: lineleft() & lineleft()<=4 & ileh<>petla}
{REP: par[1].flbar(lmt)}
{PAGE}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par[5],,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[8]:=1; ~~ }
{REP: par[7].fhbar(lmt)}
{REP: par[7].ini_line(); par[7].fline(lmt)}
{WHILE: par[7].next()}
{DO}
{REP: par[7].fline(lmt)}
{OD}
{REP: '{<{lmt+1}}'+par[7].fjbar(par[1])}
{FI}
{OD}
{IF: par[8]}
{REP: par[1].flbar(lmt)}
{ELSE}
{REP: par[7].flbar(lmt)}
{FI}
{OD}
{MACRO-}
{MACRO: 'hum'}
{ENTIRE}
{FOR: H_UM}{INDEX: 'OD'}{PREFIX: P.ref()}{DO}
{FIRST}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: par[6]<>P.ref()}{ par[5]+=1; par[6]:=P.ref(); ~~ }{FI}
{IF: ~rep_export()}{<{ceil(lmt*b_rat)+1}}{form(par[5],,,'9.')} {INCLUDE: p_prac.rpi}{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{REP: par[7].fhbar(lmt)}
{FIRST-}
{ H.prefix(H_UM.ref());
  {? H.first()
  || par[9]:=H.PRZEDST;
     par[10]:=H.MIEJSCE
  || par[9]:=par[10]:=''
  ?}; ~~
}
{REP: par[7].ini_line(); par[7].fline(lmt)}
{WHILE: par[7].next()}
{DO}
{REP: par[7].fline(lmt)}
{OD}
{OD}
{REP: par[7].flbar(lmt)}
{ENTIRE-}
{MACRO-}
{MACRO: tresc}
{IF: par[3].ASK}
{SUBSTITUTE: 'full'}
{ELSE}
{SUBSTITUTE: 'hum'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{FOOTER: 0}
{ vp:=0; ~~ }
{FOOTER-}
:!UTF-8
{TITLE:A. Zapisy księgowe dla wybranego rozrachunku}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN:
    OP.cntx_psh(); ZAP_OP.cntx_psh(); KS.cntx_psh();
    PRN:=obj_new(@.CLASS.PRINT,15); PRN.sl_frame();
    w:=obj_new(15); nazwa:=obj_new(11); kody:=obj_new(11);
    {! _i:=1..11 |! nazwa[_i]:=kody[_i]:='' !};
    PRNT:=obj_new(@.CLASS.PRINT,4); PRNT.sl_frame();
    ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref);
    czy_par:=0; 'czy rozrachunek posiada zapisy z rozliczenia';
    czy_w:=0; 'czy rozrachunek posiada zapisy walutowe';
    czy_rmk:={? -OP.TYP='rmk' || 1 |? -OP.TYP='rmp' || 2 ?};
    {? czy_rmk
    || RMK.index('NAZ'); RMK.prefix();
       RMK.find_key(OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK);
       PRNTRMK:=obj_new(@.CLASS.PRINT,5); PRNTRMK.s_frame();
       RRMK.prefix(RMK.ref());
       czy_rrmk:=RRMK.first()
    ?};
    sumow_w:=1; wal_poz:=null;
    {? ZAP_OP.first()
    || {! |?
          {? ZAP_OP.PAR_POZ<>null || czy_par:=1 ?};
          {? ZAP_OP.WAL2<>null & SSTALE.WAL=FINFO.NAROD
          || czy_w:=1;
             {? wal_poz=null || wal_poz:=ZAP_OP.WAL2
             |? wal_poz<>ZAP_OP.WAL2 || sumow_w:=0
             ?}
          ?};
          ZAP_OP.next()
       !}
    ?};
    {? SSTALE.WAL<>FINFO.NAROD || sumow_w:=czy_w:=1 ?};
    sumawn:=sumama:=kon:=sumwwal:=summwal:=wn:=ma:=wnw:=maw:=0; konto:='';
    color:=prn1:=prn2:=mdc:='';
    lm:=ll:=vp:=lmt:=rsep:=lline:=0;
    synt:=SSTALE.AO().ROK().SYNT; separ:=SSTALE.AO().ROK().SEP;
    pozycja:=(synt+{? separ<>',' || 1 || 0 ?})+12
}
{END:
    KS.cntx_pop(); OP.cntx_pop(); ZAP_OP.cntx_pop();
    _a:=SSTALE.AO().ROK().KOD+form(OKRO_F.NR,-2);
    DOK.use('doku'+_a); POZ.use('pozy'+_a); POW.use('pow_'+_a);
    _a:=ROK_F.KOD;
    AN.use('koan__'+_a); PAR_NAG.use('parnag'+_a); PAR_POZ.use('parpoz'+_a);
    VAR_DEL.delete('w','PRN','nazwa','kody','sumow_w','wal_poz','PRNT','PRNTRMK','czy_rrmk','czy_rmk','p');
    &sumawn; &sumama; &czy_w; &czy_par; &sumwwal; &summwal; &konto; &kon; &wnw; &maw;
    &color; &prn1; &prn2; &mdc;
    &lm; &ll; &vp; &lmt; &rsep; &lline; &synt; &separ; &pozycja
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: op_zap1.rpm
Utworzony:    2000-12-03 [JS] Jolanta Socha
==================================================
Zawartosc: Wydruk zapisów księgowych dla wybranego rozrachunku.
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_pw3.rpi}
{ KS.index('SYM');
  KS.prefix(BILANSP.AROK,BILANSP.ASYNT+OP.AN);
  {? KS.first()
  || exec('zer_anal','konto');
     exec('ust_anal1','konto',OP.AN,KS.ref());
     {! p:=1..11
     |! nazwa[p]:=($('KA.O'+$(p)))();
        {? p<>1 || kody[p]:=($('KA.K'+$(p-1)))() ?}
     !}
  ?};
~~}
{  PRN.add("w[1]",10,'   Data#  zapisu'@,,'#');
   PRN.add("w[2]",8,'Rejestr'@);
   PRN.add("w[3]",5,' Nr'@,1);
   PRN.add("w[4]",5,'Poz.',1);
   PRN.add("w[5]",11,'   Data#księgowania'@,,'#');
   PRN.add("w[6]",50,'   Dowód księgowy'@);
   PRN.add("w[7]",16,'    Kwota Wn'@,1);
   PRN.add("w[8]",16,'    Kwota Ma'@,1);
   {? czy_w
   || PRN.add("w[9]",4,'Wal.'@);
      PRN.add("w[10]",7,' Kurs'@,1);
      PRN.add("w[11]",16,'    Kwota Wn#   w walucie'@,1,'#');
      PRN.add("w[12]",16,'    Kwota Ma #   w walucie'@,1,'#');
      {? czy_par
      || PRN.add("w[13]",20,'    Rozliczenie#  z rozrachunkiem'@,,'#');
         PRN.add("w[14]",11,'   Data# wykonania#rozliczenia'@,,'#');
         PRN.add("w[15]",20,'Wykonał rozliczenie'@)
      ?}
   || {? czy_par
      || PRN.add("w[9]",20,'    Rozliczenie#  z rozrachunkiem'@,,'#');
         PRN.add("w[10]",11,'   Data# wykonania#rozliczenia'@,,'#');
         PRN.add("w[11]",20,'Wykonał rozliczenie'@)
      ?}
   ?};
~~}
{COMMENT}------------------------------HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='ZAPISY KSIĘGOWE DLA ROZRACHUNKU: %1'@[OP.SYM];~~}
{SUBSTITUTE: 'CHARHEAD'}
{LINE: 1}
{IF: page=1}
{FONT: 'Q'}{SPACE: 'B'}
{<1 'PARAMETRY WYDRUKU:'@}
{<5 'Rok obrotowy: %1'@[SSTALE.AR().NAZ]}
{<5 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SLO.TR]}
{<5 'Jednostka księgowa: %1'@[OP.ODD().N]}
{<5 'Symbol zewnętrzny: %1'@[OP.SYM_ZEW]}
{<5 'Konto: %1  %2'@[OP.AN,KS.NAZ]}{ dl:=14+(+OP.AN);~~}
{ p:=1;~~}
{WHILE: p<=11}
{DO}
{IF: |kody[p]<>'' | |nazwa[p]<>''}{<{pozycja} kody[p]}{<{dl} nazwa[p]}{FI}{ p+=1;~~}
{OD}
{<5 'Data otwarcia: %1  Termin płatności: %2'@[OP.DO$3,OP.TZ$3]}
   {IF: czy_rmk}
      {LINE: 1}
      {IF: czy_rmk=1}
         {<1 'Rozliczenie Międzyokresowe Kosztów:'@}
      {ELSE}
         {IF: RMK.S='M'}
            {<1 'Miesięczne Rozliczenie Międzyokresowe Przychodów:'@}
         {ELSE}
            {<1 'Dzienne (wg dni %1) Rozliczenie Międzyokresowe Przychodów:'@[{? RMK.D='T' || 'kalendarzowych'@ || 'roboczych'@ ?}]}
         {FI}
      {FI}
      {IF: RMK.GRUPA<>null}{<5 'Grupa: %1-%2'@[RMK.GRUPA().KOD,SLO.TR]}{FI}
      {IF: -RMK.BL='t'}{<5 'Blokada: od %1 do %2'@[$RMK.BLOD,$RMK.BLDO]}{FI}
   {FI}
{FI}
{FONT: 'R'}{SPACE: 'C'}
{IF: czy_rmk}
   {COMMENT}------------------------------HEADER RMK-----------------------{COMMENT-}
   {HEADER}
   {PAR_WYDR.TITLE:='ZAPISY KSIĘGOWE DLA ROZRACHUNKU: %1'@[OP.SYM];~~}
   {SUBSTITUTE: 'CHARHEAD'}
   {LINE: 1}
   {FONT: 'R'}{SPACE: 'C'}
   {prn1:=prn2:='PRNTRMK'; vp:=1; ~~}
   {SUBSTITUTE: 'TABHEAD'}
   {HEADER-}
   {COMMENT}------------------------------FOOTER RMK-----------------------{COMMENT-}
   {FOOTER: 0}{IF: ~lline}
   {SUBSTITUTE: 'LINIAF'}{FI}
   {SUBSTITUTE: 'CHARFOOT'}
   {~~}
   {FOOTER-}
   {COMMENT}---------------------------------RMK---------------------------{COMMENT-}
{  {? czy_rmk=1
   || PRNTRMK.add("w[1]",23,'Data pierwszej operacji'@);
      PRNTRMK.add("w[2]",6,'Co mc.'@);
      PRNTRMK.add("w[3]",13,'Strona księg.'@);
      PRNTRMK.add("w[4]",38,'Konto księg. zmniejszeń'@);
      PRNTRMK.add("w[5]",15,'Wartość'@,1);
      w[1]:=$RMK.DATAOP;
      w[2]:=$RMK.ILEMIES;
      w[3]:={? RMK.STRZ='Wn' || 'Winien'@ || 'Ma'@ ?}
   || PRNTRMK.add("w[1]",20,'Pierwsze rozliczenie'@);
      {? RMK.S='M'
      || PRNTRMK.add("w[2]",6,'Co mc.'@)
      || PRNTRMK.add("w[2]",20,'Ostatnie rozliczenie'@)
      ?};
      PRNTRMK.add("w[3]",13,'Strona księg.'@);
      PRNTRMK.add("w[4]",38,'Konto księg. zmniejszeń'@);
      PRNTRMK.add("w[5]",20,'Kwota przeksięgowań'@,1);
      w[1]:=$RMK.DATAOP;
      w[2]:={? RMK.S='M' || $RMK.ILEMIES || $RMK.DZ ?};
      w[3]:={? RMK.STRZ='Wn' || 'Winien'@ || 'Ma'@ ?}
   ?};
   prn1:=prn2:='PRNTRMK'; vp:=1; ~~}
   {SUBSTITUTE: 'TABHEAD'}
   {FONT: 'Q'}
   {IF: czy_rrmk}
      {rmksum:=0;~~}
      {FOR: RRMK}
      {INDEX: 'RRMKRK'}
      {PREFIX: RMK.ref()}
      {DO}
         {  w[4]:=RRMK.KONTO;
            w[5]:=form(RRMK.WAR,,2,' ,')+{? -RRMK.RODZ='p' || ' %' || ''?};
            rmksum:=rmksum+RRMK.WAR;~~}
            {SUBSTITUTE: 'LINIA0'}
            {w[1]:=w[2]:=w[3]:='';~~}
      {OD}
      {lline:=0;~~}
      {<1 PRNTRMK.bar()}
      {lline:=1;~~}
      {  w[4]:='Suma'; w[5]:=form(rmksum,,2,' ,')+{? -RRMK.RODZ='p' || ' %' || ''?}; &rmksum;~~}
      {SUBSTITUTE: 'LINIA0'}
   {ELSE}
      { w[4]:=RMK.KONZ; w[5]:={? czy_rmk=1 || '100,00 %' || form(RMK.SUMZ,,2,' ,') ?}; ~~}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
   {lline:=0;~~}
   {IF: lineleft()<>0 & lineleft()>6}
      {SUBSTITUTE: 'LINIAF'}
      {lline:=1;~~}
   {FI}
   {IF: lineleft() & lineleft()<6}
      {PAGE}
   {FI}
{FI}
{FONT: 'R'}{SPACE: 'C'}
{IF: exec('czyLista','raty',OP)}
{COMMENT}-----------------------HEADER-PŁATNOŚCI-RATALNYCH--------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ZAPISY KSIĘGOWE DLA ROZRACHUNKU: %1'@[OP.SYM];~~}
{SUBSTITUTE: 'CHARHEAD'}
{FONT: 'R'}{SPACE: 'C'}
{prn1:=prn2:='PRNT'; vp:=0; ~~}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------FOOTER-PŁATNOŚCI-RATALNYCH--------------{COMMENT-}
{FOOTER: 0}{IF: ~lline}
{SUBSTITUTE: 'LINIAF'}{FI}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{  PRNT.add("w[1]",12,'Termin płat.'@);
   PRNT.add("form(w[2],,2,' ,')",12,'Kwota'@,1);
   PRNT.add("w[3]",40,'Sposób płatności'@);
   prn1:=prn2:='PRNT'; vp:=1; ~~}
{<5 'LISTA PŁATNOŚCI:'@}
{SUBSTITUTE: 'TABHEAD'}
{FONT: 'Q'}
{FOR: D_RB}
{INDEX: 'OP'}
{PREFIX: OP.ref()}
{DO}
{  w[1]:=$D_RB.TERMPLAT;
   w[2]:=D_RB.WAR;
   w[3]:=D_RB.FORMAPLA().TR; w[4]:=''; ~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{lline:=1;~~}
{SUBSTITUTE: 'LINIAF'}
{IF: _l:=lineleft(); _l<=3 & _l<>0}{PAGE}{FI}
{FI}
{COMMENT}------------------------------HEADER----------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ZAPISY KSIĘGOWE DLA ROZRACHUNKU: %1'@[OP.SYM];~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'R'}{SPACE: 'C'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------TAB HEAD---------------------{COMMENT-}
{ENTIRE}
{IF: lineleft()>6}
{FONT: 'R'}{SPACE: 'C'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}{SPACE: 'C'}
{ELSE}{IF: lineleft()>0}{PAGE}{FI}
{FI}
{ENTIRE-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{PRN.lbar()}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-PLN------------------{COMMENT-}
{MACRO: 'PLN'}
{ {? SSTALE.WAL=FINFO.NAROD
  || wn:=ZAP_OP.WN;  ma:=ZAP_OP.MA
  || wn:=ZAP_OP.WN2; ma:=ZAP_OP.MA2
  ?};~~}
{MACRO-}
{COMMENT}------------------------------MACRO-WAL------------------{COMMENT-}
{MACRO: 'WAL'}
{ {? SSTALE.WAL=FINFO.NAROD
  || wnw:=ZAP_OP.WN2; maw:=ZAP_OP.MA2
  || wnw:=ZAP_OP.WN;  maw:=ZAP_OP.MA
  ?};~~}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{FOR: 'ZAP_OP'}
{INDEX: 'OP'}
{PREFIX: OP.ref}
{DO}
{ENTIRE}
{PRN.bar()}
{  _a:=ZAP_OP.OKRO().ROK().KOD+form(OKRO_F.NR,-2);
   DOK.use('doku'+_a); POZ.use('pozy'+_a); POW.use('pow_'+_a);
   AN.use('koan__'+ROK_F.KOD);
   _a:=ROK_F.KOD;
   PAR_NAG.use('parnag'+_a); PAR_POZ.use('parpoz'+_a);
   w[1]:=$ZAP_OP.DATA; w[2]:=ZAP_OP.REJ().KOD; w[3]:=$ZAP_OP.NR;
   w[4]:=$ZAP_OP.POZ;  w[5]:=$ZAP_OP.DKS;      w[6]:=ZAP_OP.NK;
   wal:=0;~~}
   {SUBSTITUTE: 'PLN'}
{  sumawn+=wn;
   sumama+=ma; w[7]:=form(wn,,2,' ,'); w[8]:=form(ma,,2,' ,');
   {? ZAP_OP.ROK_ROZL
   || ZAP_OP.ROK_ROZL();
      PAR_NAG.use('parnag'+ROK_F.KOD);
      PAR_POZ.use('parpoz'+ROK_F.KOD)
   ?};
   PAR_NAG.index('UNIK'); PAR_NAG.prefix();
   PAR_POZ.index('PAR_POZ'); PAR_POZ.prefix();
~~}
{IF: czy_w}
  {IF: ZAP_OP.WAL2<>null}
    { w[9]:={? SSTALE.WAL=FINFO.NAROD || ZAP_OP.WAL2().KOD || OP.WAL().KOD ?}; w[10]:=form(ZAP_OP.KURS,,4,' ,'); ~~}
    {SUBSTITUTE: 'WAL'}
    { w[11]:=form(wnw,,2,' ,'); w[12]:=form(maw,,2,' ,');
      {? sumow_w
      || {? wn<>0 || sumwwal+=wnw || summwal+=maw ?}
      ?};~~}
  {ELSE}
    { w[9]:=w[10]:=w[11]:=w[12]:='';~~}
  {FI}
  { {? czy_par
    || w[13]:={? ZAP_OP.PAR_POZ().PAR_NAG().OP=OP.SYM
              || PAR_POZ.OP
              || PAR_NAG.OP
              ?};
       w[14]:={? PAR_NAG.DATA_AKC<>date(0,0,0)
              || PAR_NAG.DATA_AKC$3
              || ''
              ?};
       w[15]:=PAR_NAG.USER
    ?};~~}
{ELSE}
  { {? czy_par
    || w[9]:={? ZAP_OP.PAR_POZ().PAR_NAG().OP=OP.SYM
             || PAR_POZ.OP
             || PAR_NAG.OP
             ?};
       w[10]:={? PAR_NAG.DATA_AKC<>date(0,0,0)
              || PAR_NAG.DATA_AKC$3
              || ''
              ?};
       w[11]:=PAR_NAG.USER
    ?};~~}
{FI}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{OD}
{ w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=''; w[6]:='RAZEM:';
  w[7]:=form(sumawn, , 2, ' ,');
  w[8]:=form(sumama, , 2, ' ,');
  w[9]:=w[10]:=w[13]:=w[14]:=w[15]:='';
  {? czy_w & sumow_w
  || w[11]:=form(sumwwal, , 2, ' ,');
     w[12]:=form(summwal, , 2, ' ,')
  || w[11]:=w[12]:=''
  ?};
~~}
{ENTIRE}
{FONT: 'R'}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{PRN.bar()}
{ w[6]:='SALDO:'; w[7]:=form(F.SWn(sumawn$2,sumama$2), , 2, ' ,');  w[8]:=form(F.SMa(sumawn$2,sumama$2), , 2, ' ,');
  {? czy_w & sumow_w
  || w[11]:=form(F.SWn(sumwwal$2,summwal$2), , 2, ' ,');
     w[12]:=form(F.SMa(sumwwal$2,summwal$2), , 2, ' ,')
  || w[11]:=w[12]:=''
  ?};~~}
{SUBSTITUTE: 'LINIA'}
{FONT: 'Q'}
{PRN.lbar()}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}-----------------------------------------------------------{COMMENT-}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
:!UTF-8
{TITLE:A. Historia zdarzeń dla kontrahentów}
{PRINTER: FINFO.W_PRN,,,,,'Q','B',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRNKH0:=obj_new(@.CLASS.PRINT,1); PRNKH0.sl_frame();
   PRNKH1:=obj_new(@.CLASS.PRINT,3); PRNKH1.sl_frame();
   PRNKH2:=obj_new(@.CLASS.PRINT,4); PRNKH2.sl_frame();
   PRNPOZ1:=obj_new(@.CLASS.PRINT,6); PRNPOZ1.sl_frame();
   PRNPOZ2:=obj_new(@.CLASS.PRINT,11); PRNPOZ2.sl_frame();
   kolum:=obj_new(13); war:=obj_new(13); tyt:=obj_new(13); head:=obj_new(13);
   color:=prn1:=prn2:=prn3:='';
   lm:=ll:=vp:=lmt:=rsep:=0; wybor:=maxkol:=il:=0; akt_wydr:=0; ramka:='';
   t_zdarz:=tytul:=''; dateod:=datedo:=date(0,0,0); xpage:=0; zdarz:=1; lp:=lpp:=0;
   format:=",2,'' ,''";
   PAR_WYDR.PAR1:=PAR_WYDR.PAR2:=0; PAR_WYDR.PAR3:=1;
   WYDRUKI.P11DDO:=WYDRUKI.P21DDO:=WYDRUKI.P31DDO:=WYDRUKI.P41DDO:=date(0,0,0);
   WYDRUKI.P11DOD:=WYDRUKI.P21DOD:=WYDRUKI.P31DOD:=WYDRUKI.P41DOD:=date(0,0,0);
   WYDRUKI.P1:=WYDRUKI.P2:=WYDRUKI.P22:=WYDRUKI.P23:=WYDRUKI.P3:=WYDRUKI.P4:=0;
   WYDRUKI.P21:=WYDRUKI.P31:=WYDRUKI.P41:=2; WYDRUKI.P22S:='';
   typ_pola:=obj_new(13); strona:=0;
   SER_NAG.cntx_psh(); SER_NAG.prefix(); ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);  oddzial:=OPERATOR.DEPT().OD
}
{END:
   VAR_DEL.delete('PRNKH0','PRNKH1','PRNKH2','PRNPOZ1','PRNPOZ2','PRN','PRNH');
   VAR_DEL.delete('kolum','war','tyt','head','typ_pola');
   &oddzial; &strona; &color; &prn1; &prn2; &prn3; &maxkol; &wybor; &il; &akt_wydr; &ramka; &format;
   &lm; &ll; &vp; &lmt; &rsep; &t_zdarz; &tytul; &dateod; &datedo; &xpage; &zdarz; &lp; &lpp;
   SER_NAG.cntx_pop(); ODD.cntx_pop()
}
{  {! _i:=1..13 |! war[_i]:=tyt[_i]:='' !};
   PRNKH0.add("war[1]",115,'Dane kontrahenta'@);
   PRNKH1.add("war[1]",14,'');
   PRNKH1.add("war[2]",17,'');
   PRNKH1.add("war[3]",82,'');
   PRNKH2.add("war[1]",32,'');
   PRNKH2.add("war[2]",20,'');
   PRNKH2.add("war[3]",25,'');
   PRNKH2.add("war[4]",35,'');
   PRNPOZ1.add("war[1]",4,'Lp.'@,1,,,,,format);
   PRNPOZ1.add("war[2]",10,'Data'@);
   PRNPOZ1.add("war[11]",99,'Pozycje'@);
   PRNPOZ2.add("''",1,'',1,,,,,format);
   PRNPOZ2.add("war[3]",4,'Poz.'@,1);
   PRNPOZ2.add("war[13]",8,'Jed. ks.'@);
   PRNPOZ2.add("war[4]",20,'Dokument'@);
   PRNPOZ2.add("war[5]",10,'Data (lub Data wpł.)'@);
   PRNPOZ2.add("war[6]",10,'Termin płatności'@);
   PRNPOZ2.add("war[12]",4,'Wal.'@,1);
   PRNPOZ2.add("war[7]",15,'Kwota faktury'@,1,,,,,format);
   PRNPOZ2.add("war[8]",15,'Wpłata'@,1,,,,,format);
   PRNPOZ2.add("war[9]",4,'Zwł.'@);
   PRNPOZ2.add("war[10]",14,'Odsetki'@,1,,,,,format);
   PAR_WYDR.TITLE:=~-('Historia zdarzeń dla kontrahentów '@);
   prn1:='PRNKH1'; prn2:='PRNKH2'; ~~}
{INCLUDE: wsp_pw.rpi}
{ OKRO_F.cntx_psh();
  OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
  WYDRUKI.P11DOD:=WYDRUKI.P21DOD:=WYDRUKI.P31DOD:=WYDRUKI.P41DOD:=WYDRUKI.P11DDO:=
     {? SSTALE.AO().POCZ=date(0,0,0) &  OKRO_F.first() & OKRO_F.next()
     || OKRO_F.POCZ
     |? SSTALE.AO().POCZ<>date(0,0,0)
     || {? date()<=OKRO_F.KON & date()>=SSTALE.AO().POCZ
        || date()
        || OKRO_F.KON
        ?}
     || date(0,0,0)
     ?};
  SSTALE.AO(); OKRO_F.prev(); OKRO_F.next();
  WYDRUKI.P21DDO:=WYDRUKI.P31DDO:=WYDRUKI.P41DDO:=OKRO_F.KON;
  OKRO_F.cntx_pop();
  WYDRUKI.win_edit('ZDARZ_KH'); ~~}
{EXIT: ~WYDRUKI.edit("exec('kh_zdarz_spr','rozrach_kor')")}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'W10'}
{ lmt:=int((charleft()-PRNKH0.width())/2);~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}{FONT: 'W10'}{SPACE: 'B'}
{IF: +ramka}{SUBSTITUTE: 'TABHEAD'}{FI}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{SPACE: 'B'}
{IF: +ramka}{SUBSTITUTE: 'LINIAF'}{FI}{SPACE: 'B'}

{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}{SPACE: 'B'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{MACRO: 'ADDPRN'}
{  PRN:=obj_new(@.CLASS.PRINT,maxkol); PRN.sl_frame();
   _f:=''; {! _i:=1..maxkol |! _f+='PRN.add("war['+$_i+']",kolum['+$_i+'],tyt['+$_i+'],typ_pola['+$_i+'],,,,,'+"',2,'' ,'''"+');' !}; _f+='1';
   ($(_f))();
   il:=0;  {! _i:=1..maxkol  |! il+=kolum[_i] !}; il+=(maxkol-1);
   PRNH:=obj_new(@.CLASS.PRINT,1); PRNH.sl_frame(); PRNH.add("",il,head[1]);
~~}
{MACRO-}
{MACRO: 'DELPRN'}
{ obj_del(PRN); obj_del(PRNH); prn1:='PRNKH1'; prn2:=''; ~~}
{MACRO-}
{COMMENT}------------------------------MACRO DRUKUJĄCE TAB KH-----------------------{COMMENT-}
{MACRO: 'KHTAB'}
{SUBSTITUTE: 'PRZEDKH'}
{FONT: 'W10'}
{  prn1:=prn2:='PRNKH0'; rsep:=vp:=0;
   PRNKH0.setcolor(color); PRNKH0.ini_head();~~}
{REP: PRNKH0.fhbar(lmt)}
{WHILE: PRNKH0.h_next()}
{DO}
   {REP: PRNKH0.h_fmline(lmt)}
{OD}
{FONT: 'W10'}
{IF: lineleft<>0 & lineleft<>1}{ prn1:=prn2:='PRNKH1';~~}{REP: PRNKH0.fjbar(PRNKH1,lmt)}{FI}
{ xpage:=1; {! _i:=1..6 |! war[_i]:=tyt[_i]:='' !};
  war[1]:='Kod: '+KH.KOD; war[2]:='Skrót: '+KH.SKR;  war[3]:='Nazwa: '+KH.NAZ_P;
~~}
{IF: PRNKH1.ini_line(); (PRNKH1.length()+1)>=lineleft() & lineleft<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
   {<{lmt+1}}{PRNKH1.hbar()}{ vp:=0; rsep:=0;~~}
    { prn1:='PRNKH1'; prn2:='PRNKH2';~~}
    { PRNKH1.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
{ELSE}
    { prn1:='PRNKH1'; prn2:='PRNKH2';~~}
    { {? lineleft()=0 || rsep:=0 ?}; PRNKH1.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
{FI}
{IF: PAR_WYDR.PAR2}
{ rsep:=1; war[1]:='Typ: '+exec('pw_typkh','!zws_par_kkhr');   war[2]:='Nip: %1'@[KH.NIP];  war[3]:='Regon: %1'@[KH.REG];
  war[4]:='Nazwa skrócona: %1'@[KH.NAZ];
~~}
{IF: PRNKH2.ini_line(); (PRNKH2.length()+1)>=lineleft() & lineleft<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
   {<{lmt+1}}{PRNKH2.hbar()}{ prn1:='PRNKH2'; vp:=0; rsep:=0;~~}
    {SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
{ELSE}
   { PRNKH2.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA02'}
{FI}{prn1:='PRNKH2'; ~~}
{IF: KH.TYP='U'}
{ prn1:='PRNKH2';  prn2:='PRNKH0'; war[1]:='';
  NIPY.index('SNIP'); NIPY.prefix(KH.ref());
  {? NIPY.first()
  || {!|?  war[1]+=({? +war[1] ||', ' || '' ?}+NIPY.KRAJ().KOD+' '+NIPY.NIP); NIPY.next()!};
     {? +war[1] || war[1]:='Symbole NIP w krajach UE:  %1'@[war[1]] ?}
  ?}; PRNKH0.setcolor('255:255:255');
~~}
{IF: PRNKH0.ini_line(); (PRNKH0.length()+1)>=lineleft() & lineleft<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
   {<{lmt+1}}{PRNKH0.hbar()}{ prn1:='PRNKH0'; vp:=0; rsep:=0;~~}
    {SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
{ELSE}
   { PRNKH0.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA02'}
{FI}
{prn1:='PRNKH0'; ~~}
{FI}
{ prn2:='PRNKH0'; tyt[1]:='';
  war[1]:='Adres siedziby: '+{? KH.POCZ*KH.MIASTO & +KH.POCZ & |KH.POCZ=|KH.MIASTO
                             || {? +KH.KPOCZ || KH.KPOCZ+' ' || '' ?}+KH.POCZ+', '
                             || {? ~+KH.POCZ & +KH.KPOCZ || KH.KPOCZ+' ' || '' ?}+KH.MIASTO+
                                {? +KH.POCZ
                                || ', '+{? +KH.KPOCZ || KH.KPOCZ+' ' || '' ?}+KH.POCZ+', '
                                |? +KH.MIASTO
                                || ', '
                                || ''
                                ?}
                             ?}+
                             {? +KH.UL || KH.UL || '' ?};
~~}
{IF: PRNKH0.ini_line(); (PRNKH0.length()+1)>=lineleft() & lineleft<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
   {<{lmt+1}}{PRNKH0.hbar()}{ prn1:='PRNKH0'; vp:=0; rsep:=0; PRNKH0.setcolor('255:255:255'); ~~}
   {SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
{ELSE}
   { PRNKH0.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA02'}
{FI}
{IF: OS_ADRES.index('RODZAJ1'); OS_ADRES.prefix(KH.ref(),'H'); OS_ADRES.last()}
{ prn2:=prn1:='PRNKH0'; tyt[1]:='';
  war[1]:='Adres do korespondencji: '+{? +OS_ADRES.MIASTO & |OS_ADRES.MIASTO<>|OS_ADRES.POCZTA
                                     || {? (~(+OS_ADRES.POCZTA)) & +OS_ADRES.KOD || OS_ADRES.KOD+' ' || '' ?}+
                                        OS_ADRES.MIASTO+',  '
                                     || ''
                                     ?}+
                                     {? +OS_ADRES.POCZTA
                                     || {? +OS_ADRES.KOD || OS_ADRES.KOD+' ' || '' ?}+OS_ADRES.POCZTA+',  '
                                     || ''
                                     ?}+
                                     {? +OS_ADRES.ULICA
                                     || OS_ADRES.ULICA+{? +OS_ADRES.DOM || ' '+OS_ADRES.DOM || '' ?}+
                                        {? +OS_ADRES.LOKAL || '/'+OS_ADRES.LOKAL || '' ?}
                                     || ''
                                     ?}; ~~}
{IF: PRNKH0.ini_line(); (PRNKH0.length()+1)>=lineleft() & lineleft<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
   {<{lmt+1}}{PRNKH0.hbar()}{ prn1:='PRNKH0'; vp:=0; rsep:=0; PRNKH0.setcolor('255:255:255'); ~~}
    {SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
{ELSE}
   { rsep:=0; PRNKH0.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA02'}{ rsep:=1;~~}
{FI}
{FI}
{ prn1:='PRNKH0'; prn2:='PRNKH2'; ~~}
{ rsep:=1; war[1]:='Grupa: %1'@[KH.GRKH().NAZ];   war[2]:='Obszar: %1'@[KH.OBS().KOD];  war[3]:='Kraj: %1'@[KH.KRAJ];
  exec('pw_kldni','!zws_par_kkhr');  war[4]:='Forma płatności: %1'@[KH.PLATNOSC().TR];
~~}
{IF: PRNKH2.ini_line(); (PRNKH2.length()+1)>=lineleft() & lineleft<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
   {<{lmt+1}}{PRNKH2.hbar()}{ prn1:='PRNKH2'; vp:=0; rsep:=0;~~}
    {SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
{ELSE}
   { PRNKH2.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA02'}
{FI}
{ prn1:='PRNKH2'; ~~}
{ rsep:=1; war[1]:='Telefon: %1'@[KH.TEL];   war[2]:='Telefax: %1'@[KH.FAX];  war[3]:='Teleks: %1'@[KH.TX];
  war[4]:='Osoba: %1'@[exec('kh_osob_default','kontrahent',KH.ref)];
~~}
{IF: PRNKH2.ini_line(); (PRNKH2.length()+1)>=lineleft() & lineleft<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
   {<{lmt+1}}{PRNKH2.hbar()}{ vp:=0; rsep:=0;~~}
    {SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
{ELSE}
    { PRNKH2.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA0'}
{FI}
{ROZNE.KKBAN:=1; PAR_WYDR.TABAKR:='KH'; PAR_WYDR.REF:=#KH.ref();~~}
{FOR: 'SKID_RBK'}
{INDEX: 'TAB'}
{PREFIX: REF.FIRMA,'KH','KH',#@.KH.ref()}
{DO}
  {FIRST}
    { rsep:=1; prn1:='PRNKH2';  prn2:='PRNKH1'; ~~}
  {FIRST-}
  { war[1]:='Bank: '+SKID_RBK.BANK().NUMER; war[2]:=SKID_RBK.BANK().NB;
    war[3]:='Nr rachunku:  '+RB.get_rbtx(2,SKID_RBK.N,KH.KRAJISO().KODISO);
~~}
  {IF: PRNKH1.ini_line(); (PRNKH1.length()+1)>=lineleft() & lineleft<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
   {<{lmt+1}}{PRNKH1.hbar()}{ prn1:='PRNKH1'; vp:=0; rsep:=0;~~}
{IF: -SKID_RBK.RD='t'}{FONT: 'T8B'}{FI}
    {SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}{FONT: 'W10'}
  {ELSE}
   {IF: -SKID_RBK.RD='t'}{FONT: 'T8B'}{FI}
   { PRNKH1.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA02'}{FONT: 'W10'}
  {FI}
  {prn1:='PRNKH1'; ~~}
{OD}
{FI}
{IF: WYDRUKI.P1 & WYDRUKI.P11=1}
{ SER_KOR.DATA:=WYDRUKI.P11DDO;  exec('jakisch','rozrach'); ~~}
  {IF: SER_KOR.SER_SCH}
  { war[1]:='Obowiązujący schemat windykacyjny na dzień:  %1   kod:  %2  -  %3'@[WYDRUKI.P11DDO$1,SER_WOW.SER_SCH().KOD,SER_WOW.SER_SCH().OPIS]+
    {? SER_WOW.KH=null & SER_WOW.GRKH=null || '   (Domyślny)'@ |? SER_WOW.GRKH<>null || '   (dla Grupy)'@ || '   (dla Kontrahenta)'@ ?};
    prn2:='PRNKH0'; rsep:=1;
  ~~}{ PRNKH0.setcolor('255:255:255');~~}
  {IF: PRNKH0.ini_line(); (PRNKH0.length()+1)>=lineleft() & lineleft<>0}
    { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
    {<{lmt+1}}{PRNKH0.hbar()}{ prn1:='PRNKH0'; vp:=0; rsep:=0;~~}
    {SUBSTITUTE: 'LINIA0'}{ rsep:=1;~~}
  {ELSE}
    {SUBSTITUTE: 'LINIA02'}
  {FI}{prn1:='PRNKH0'; ~~}
  {FI}
{FI}
{IF: lineleft}{ramka:=''; ~~}{SUBSTITUTE: 'LINIAF'}{FI}
{MACRO-}
{MACRO: 'TABHEADJ'}
{COMMENT}
Makro używane do drukowania nagłówka tabeli (np. w nagłówku strony) dla tabeli obiektu PRINTER
o nazwie przekazywanej przez zmienne prn1 i prn2 (oraz ewentualnie prn3).
{COMMENT-}
{ENTIRE}
{ strona:={? lineleft()<>0 || page() || 0 ?}; ~~}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn1+'.ini_head()'; ($(_f))()}
{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
{WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
{ _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn2+'.ini_head()'; ($(_f))()}
{WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{IF: +prn3}
{REP: _f:=prn2+'.fjbar('+prn3+',lmt)'; ($(_f))()}
{ _f:=prn3+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn3+'.ini_head()'; ($(_f))()}
{WHILE: _f:=prn3+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn3+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{IF: (lineleft()=0 & strona=page())}{ prn1:=prn3; ramka:='x';~~}{FI}
{IF: lineleft()=1}{REP: _f:=prn3+'.flbar(lmt)'; ($(_f))()}{FI}
{ELSE}
{IF: (lineleft()=0 & strona=page())}{ prn1:=prn2; ramka:='x';~~}{FI}
{IF: lineleft()=1}{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}{FI}
{FI}
{ENTIRE-}
{IF: (lineleft()=0 & strona=page())}{PAGE}{ ramka:='';~~}{FI}
{MACRO-}
{COMMENT}------------------------------MACRO DRUKUJACE TAB ZDARZ-----------------------{COMMENT-}
{MACRO: 'WAR_ZDAR'}
{IF: t_zdarz='H'}
{ war[1]:=$lp;  war[2]:=DOKUM.DATA$1;  war[3]:=DOKUM.CZAS$1; war[4]:=DOKUM.DOT().ZDARZ;
  war[5]:=DOKUM.OSOBA;  war[6]:=DOKUM.OPER; war[7]:=DOKUM.KR_OP; ~~}
{ELSE}
{ war[1]:=$lp;  war[2]:=ZDARZ.DATA$1;  war[3]:=ZDARZ.CZAS$1; war[4]:=ZDARZ.DOT().ZDARZ;
  war[5]:=ZDARZ.OPER; war[6]:=ZDARZ.KR_OP; ~~}
{FI}
{MACRO-}
{MACRO: 'DEK_ZDAR'}
{SUBSTITUTE: 'WAR_ZDAR'}
{prn1:='PRNH'; prn2:='PRN'; prn3:=''; lp:=0; ~~}
{IF: t_zdarz='H'}
{ maxkol:=7; kolum[1]:=3; kolum[2]:=10; kolum[3]:=5; kolum[4]:=15; kolum[5]:=20; kolum[6]:=18; kolum[7]:=38; ~~}
{ head[1]:={? WYDRUKI.P31=1 || 'Kontakty kontrahenta od dnia %1 do %2'@[WYDRUKI.P31DOD$1,WYDRUKI.P31DDO$1] || 'Kontakty kontrahenta'@ ?};
  tyt[1]:='Lp.'; tyt[2]:="Data"; tyt[3]:='Czas'@; tyt[4]:='Zdarzenie'@; tyt[5]:='Osoba kontaktowa'@; tyt[6]:='Operator'@; tyt[7]:='Opis'@; ~~}
{ELSE}
{ maxkol:=6; kolum[1]:=3; kolum[2]:=10; kolum[3]:=5; kolum[4]:=15; kolum[5]:=20; kolum[6]:=57;~~}
{ head[1]:={? WYDRUKI.P41=1 || 'Blokady kontrahenta od dnia %1 do %2'@[WYDRUKI.P41DOD$1,WYDRUKI.P41DDO$1] || 'Blokady kontrahenta '@ ?};
  tyt[1]:='Lp.'; tyt[2]:="Data"; tyt[3]:='Czas'@; tyt[4]:='Zdarzenie'@; tyt[5]:='Operator'@; tyt[6]:='Opis'@; ~~}
{FI}
{ {! _i:=1..maxkol |!  typ_pola[_i]:=0 !}; typ_pola[1]:=1; ~~}
{MACRO-}
{MACRO: 'ZDARZH'}
{t_zdarz:='H'; tytul:='Kontakty'@; wybor:=WYDRUKI.P31; dateod:=WYDRUKI.P31DOD; datedo:=WYDRUKI.P31DDO; ~~}
{IF: WYDRUKI.P3}
{SUBSTITUTE: 'DEK_ZDAR'}{SUBSTITUTE: 'ADDPRN'}{SUBSTITUTE: 'ZDARZHH'} {SUBSTITUTE: 'DELPRN'}
{FI}
{MACRO-}
{MACRO: 'ZDARZHH'}
  {IF: wybor=2}
    {FOR: 'DOKUM'}
    {INDEX: 'KH'}
    {PREFIX: KH.ref()}
    {DO}
     {FIRST}
       { ramka:='';~~}{SUBSTITUTE: 'TABHEADJ'}
       {prn1:='PRN'; prn3:=''; ramka:='zdarzh'; ~~}
     {FIRST-}
     {lp+=1; ~~}
     {SUBSTITUTE: 'WAR_ZDAR'}
     { PRN.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA0'}
   {OD}
  {ELSE}
    {FOR: 'DOKUM'}
      {INDEX: 'KHDC'}
       {FROM: KH.ref(), dateod}
       {TO: KH.ref(), datedo}
       {DO}
       {FIRST}
         { ramka:='';~~}{SUBSTITUTE: 'TABHEADJ'}
         {prn1:='PRN'; prn3:=''; ramka:='zdarzh'; ~~}
       {FIRST-}
       {lp+=1; ~~}
       {SUBSTITUTE: 'WAR_ZDAR'}
       { PRN.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA0'}
       {OD}
  {FI}
{IF: lp & lineleft<>0}
{ ramka:='';~~}{SUBSTITUTE: 'LINIAF'}
{IF: lineleft<>0}{SUBSTITUTE: 'ODSTEP'}{FI}
{FI}
{MACRO-}
{COMMENT}------------------------------MACRO DRUKUJACE TAB ZDARZ-----------------------{COMMENT-}
{MACRO: 'ZDARZB'}
{t_zdarz:='B'; tytul:='Blokady'@; wybor:=WYDRUKI.P41; dateod:=WYDRUKI.P41DOD; datedo:=WYDRUKI.P41DDO; ~~}
{IF: WYDRUKI.P4}
{SUBSTITUTE: 'DEK_ZDAR'}{SUBSTITUTE: 'ADDPRN'}{SUBSTITUTE: 'ZDARZBB'} {SUBSTITUTE: 'DELPRN'}
{FI}
{MACRO-}
{MACRO: 'ZDARZBB'}
  {IF: wybor=2}
    {FOR: 'ZDARZ'}
    {INDEX: 'ZDTKH'}
    {PREFIX: t_zdarz,KH.ref()}
    {DO}
     {FIRST}
       { ramka:='';~~}{SUBSTITUTE: 'TABHEADJ'}
       {prn1:='PRN'; prn3:=''; ramka:='zdarzb'; ~~}
     {FIRST-}
     {lp+=1; ~~}
     {SUBSTITUTE: 'WAR_ZDAR'}
     { PRN.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA0'}
   {OD}
  {ELSE}
    {FOR: 'ZDARZ'}
      {INDEX: 'ZDTKH'}
       {FROM: t_zdarz, KH.ref(), dateod}
       {TO: t_zdarz, KH.ref(), datedo}
       {DO}
       {FIRST}
         { ramka:='';~~}{SUBSTITUTE: 'TABHEADJ'}
         {prn1:='PRN'; prn3:=''; ramka:='zdarzb'; ~~}
       {FIRST-}
       {lp+=1; ~~}
       {SUBSTITUTE: 'WAR_ZDAR'}
       { PRN.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA0'}
       {OD}
  {FI}
{IF: lp & lineleft<>0}
{ ramka:='';~~}{SUBSTITUTE: 'LINIAF'}
{IF: lineleft<>0}{SUBSTITUTE: 'ODSTEP'}{FI}
{FI}
{MACRO-}
{COMMENT}------------------------------MACRO DRUKUJĄCE TAB SER_NAG-----------------------{COMMENT-}
{MACRO: 'WAR_SNAG'}
{IF: WYDRUKI.P23}
{ war[1]:=$lp; war[2]:=SER_NAG.DATA$1;
  war[3]:=$lpp; war[4]:=SER_POZ.DOK; war[13]:=SER_POZ.ODD().OD;
  war[5]:={? SER_NAG.T='W' || SER_POZ.DATA$1 || SER_POZ.DATW$1 ?};
  war[6]:=SER_POZ.TERM$1; war[7]:=form(SER_POZ.KW,,2,' ,'); war[8]:=form(SER_POZ.KWP,,2,' ,'); war[9]:=$SER_POZ.ZWL; war[10]:=form(SER_POZ.ODS,,2,' ,');
  war[11]:={? SER_NAG.T='W' || 'Wezwanie do zapłaty'@
           |? SER_NAG.T='N' || 'Nota odsetkowa'@
           |? SER_NAG.T='P' || 'Potwierdzenie sald'@
           |? SER_NAG.T='I' || 'Inne'@
           ?}+{? SER_NAG.ODD().OD<>'' || ' ('+ODD.OD+')' || '' ?}+' - '+SER_NAG.KOD().KOD+' - '+SER_NAG.KOD().OP;
  war[12]:=SER_NAG.WAL().KOD;
  {? SER_NAG.T='P' & SER_NAG.WAL<>null
  || SER_POZ.cntx_psh(); SER_POZ.index('SER_POZW'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref);
     exec('sum_potw','rozrach_kor');
     SER_POZ.cntx_pop();
     war[11]:=war[11]+' - '+SER_NAG.WAL().KOD+
              ' (nasze: '+form(SER_KOR.SAL_NASZ,,2,' ,')+' wasze: '+form(SER_KOR.SAL_WASZ,,2,' ,')+')'
  ?};
~~}
{ELSE}
{ war[1]:=$lp;
  war[2]:={? SER_NAG.ODD().OD<>'' || ODD.OD || '' ?};
  war[3]:={? SER_NAG.T='W' || 'Wezwanie do zapłaty'@
          |? SER_NAG.T='N' || 'Nota odsetkowa'@
          |? SER_NAG.T='P' || 'Potwierdzenie sald'@
          |? SER_NAG.T='I' || 'Inne'@
          ?};
 war[4]:=SER_NAG.DATA$1;  war[5]:=form(SER_NAG.KOD().KOD,3)+' '+SER_NAG.KOD().OP;
 war[6]:=SER_NAG.TTAB_ODS().TR; war[7]:=SER_NAG.WAL().KOD; war[8]:=SER_NAG.JEZYK().TR;~~}
{FI}
{MACRO-}
{MACRO: 'DEK_SNAG'}
{SUBSTITUTE: 'WAR_SNAG'}
{IF: WYDRUKI.P23}
{ maxkol:=9;
  kolum[1]:=14; kolum[2]:=4;
  kolum[3]:=20; kolum[4]:=10; kolum[5]:=10; kolum[6]:=16; kolum[7]:=16; kolum[8]:=4; kolum[9]:=13;
  prn1:='PRNH'; prn2:='PRNPOZ1'; prn3:='PRNPOZ2'; lp:=0;
  head[1]:={? WYDRUKI.P21=1 || 'Korespondencja seryjna kontrahenta od dnia %1 do %2'@[WYDRUKI.P21DOD$1,WYDRUKI.P21DDO$1] || 'Korespondencja seryjna kontrahenta'@ ?};
  tyt[1]:='Lp.'@; tyt[2]:='Typ'@; tyt[3]:='Kod'@; tyt[4]:='Sporządzono'@; tyt[5]:='Rod. ods.'@; tyt[6]:=''; tyt[7]:='';
~~}
{ELSE}
{ maxkol:=8; kolum[1]:=3; kolum[2]:=8; kolum[3]:=19; kolum[4]:=11; kolum[5]:=24; kolum[6]:=22; kolum[7]:=4; kolum[8]:=17;~~}
{ prn1:='PRNH'; prn2:='PRN'; lp:=0;
  head[1]:={? WYDRUKI.P21=1 || 'Korespondencja seryjna kontrahenta od dnia %1 do %2'@[WYDRUKI.P21DOD$1,WYDRUKI.P21DDO$1] || 'Korespondencja seryjna kontrahenta '@ ?};
  tyt[1]:='Lp.'@; tyt[2]:='Jed. ks.'@; tyt[3]:='Typ'@; tyt[4]:='Sporządzono'@; tyt[5]:='Kod / Opis korespondencji'@;
  tyt[6]:='Rodzaj odsetek'@; tyt[7]:='Wal.'@; tyt[8]:='Język'@;~~}
{FI}
{ {! _i:=1..maxkol |!  typ_pola[_i]:=0 !}; typ_pola[1]:=1;~~}
{MACRO-}
{MACRO: 'SER_NAG'}
{IF: WYDRUKI.P2}
{ prn1:='PRNPOZ1';~~}
{SUBSTITUTE: 'DEK_SNAG'}
{SUBSTITUTE: 'ADDPRN'}
  {IF: ~WYDRUKI.P22}
      {IF: WYDRUKI.P21=2}
        {FOR: 'SER_NAGS'}
        {INDEX: 'NAG_KODK'}
        {PREFIX: REF.FIRMA,OPERATOR.DEPT().OD,KH.KOD,KH.KOD}
        {DO}
          { SER_NAGS.SER_NAG();~~}
          {IF: WYDRUKI.P23}
           {SUBSTITUTE: 'SER_NAG1'}
          {ELSE}
           {SUBSTITUTE: 'SER_NAG0'}
          {FI}
        {OD}
      {ELSE}
        {FOR: 'SER_NAGS'}
        {INDEX: 'NAG_KODK'}
        {FROM: REF.FIRMA,OPERATOR.DEPT().OD, KH.KOD, KH.KOD, WYDRUKI.P21DOD}
        {TO: REF.FIRMA,OPERATOR.DEPT().OD, KH.KOD, KH.KOD, WYDRUKI.P21DDO}
        {DO}
          { SER_NAGS.SER_NAG();~~}
          {IF: WYDRUKI.P23}
           {SUBSTITUTE: 'SER_NAG1'}
          {ELSE}
           {SUBSTITUTE: 'SER_NAG0'}
          {FI}
        {OD}
      {FI}
  {ELSE}
      {IF: WYDRUKI.P21=2}
        {FOR: 'SER_NAGS'}
        {INDEX: 'TYPKH'}
        {PREFIX: REF.FIRMA,OPERATOR.DEPT().OD,(1+WYDRUKI.P22S),KH.KOD,KH.KOD}
        {DO}
          { SER_NAGS.SER_NAG();~~}
          {IF: WYDRUKI.P23}
           {SUBSTITUTE: 'SER_NAG1'}
          {ELSE}
           {SUBSTITUTE: 'SER_NAG0'}
          {FI}
        {OD}
      {ELSE}
        {FOR: 'SER_NAGS'}
        {INDEX: 'TYPKH'}
        {FROM: REF.FIRMA,OPERATOR.DEPT().OD,(1+WYDRUKI.P22S),KH.KOD,KH.KOD,WYDRUKI.P21DOD}
        {TO: REF.FIRMA,OPERATOR.DEPT().OD,(1+WYDRUKI.P22S),KH.KOD,KH.KOD,WYDRUKI.P21DDO}
        {DO}
          { SER_NAGS.SER_NAG();~~}
          {IF: WYDRUKI.P23}
           {SUBSTITUTE: 'SER_NAG1'}
          {ELSE}
           {SUBSTITUTE: 'SER_NAG0'}
          {FI}
        {OD}
      {FI}
  {FI}
{IF: lp & lineleft<>0}
{ ramka:='';~~}{SUBSTITUTE: 'LINIAF'}
{IF: lineleft<>0}{SUBSTITUTE: 'ODSTEP'}{FI}
{FI}
{SUBSTITUTE: 'DELPRN'}
{FI}
{MACRO-}
{MACRO: 'SER_NAG0'}
{FIRST}
  { ramka:='';~~}{SUBSTITUTE: 'TABHEADJ'}{prn1:='PRN'; prn3:=''; ramka:='ser_nag'; ~~}
{FIRST-}
  {lp+=1;~~}
  {SUBSTITUTE: 'WAR_SNAG'}
  { PRN.setcolor('255:255:255');~~}
  {SUBSTITUTE: 'LINIA0'}
{MACRO-}
{MACRO: 'SER_NAG1'}
{FIRST}
  { ramka:='';~~}{SUBSTITUTE: 'TABHEADJ'}{prn1:='PRNPOZ2'; prn2:=prn3:=''; lp:=lpp:=0;  ramka:='ser_nag'; ~~}
{FIRST-}
  {lp+=1; ~~}
  {SUBSTITUTE: 'WAR_SNAG'}
  {IF: (SER_NAG.T='W' | SER_NAG.T='N')}
  { prn2:='PRNPOZ1'; prn3:=''; rsep:=1; ~~}
  { PRNPOZ1.setcolor('255:255:255'); PRNPOZ2.setcolor('255:255:255');~~}
{IF:  _f:=prn2+'.ini_line()'; _v:=($(_f))(); _f:=prn2+'.length()'; _v:=($(_f))(); _v>=(lineleft-1) & lineleft()<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
{REP: PRNPOZ1.fhbar(lmt)}{ prn1:='PRNPOZ1'; vp:=0; rsep:=0;~~}
    {SUBSTITUTE: 'LINIA0'}
{ELSE}
    {SUBSTITUTE: 'LINIA02'}
{FI}{ prn1:='PRNPOZ1';prn2:=prn3:=''; rsep:=1; vp:=0; ramka:='ser_poz'; ~~}
   {FOR: 'SER_POZ'}
   {INDEX: 'SER_POZO'}
   {PREFIX: REF.FIRMA,SER_NAG.ref(),oddzial}
   {DO}
     {lpp+=1; ~~}
     {SUBSTITUTE: 'SER_POZ1'}
   {OD}
   {lpp:=0; ~~}
  {ELSE}
    {lpp:=0; prn2:='PRNPOZ1'; prn3:=''; rsep:=1; ~~}
    { PRNPOZ1.setcolor('255:255:255'); PRNPOZ2.setcolor('255:255:255'); ~~}
{IF:  _f:=prn2+'.ini_line()'; _v:=($(_f))(); _f:=prn2+'.length()'; _v:=($(_f))(); _v>=(lineleft-1) & lineleft()<>0}
   { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}
{REP: PRNPOZ1.fhbar(lmt)}{ prn1:='PRNPOZ1'; vp:=0; rsep:=0;~~}
    {SUBSTITUTE: 'LINIA0'}
{ELSE}
    { PRNPOZ2.setcolor('255:255:255');~~}{SUBSTITUTE: 'LINIA02'}
{FI}
    {prn1:='PRNPOZ1'; vp:=0; rsep:=1; ~~}
  {FI}
{MACRO-}
{COMMENT}------------------------------MACRO DRUKUJĄCE TAB SER_POZ-----------------------{COMMENT-}
{MACRO: 'SER_POZ1'}
{SUBSTITUTE: 'WAR_SNAG'}
{IF: lineleft()<>0}{ PRNPOZ1.setcolor('255:255:255'); PRNPOZ2.setcolor('255:255:255'); ~~}{FI}
{IF: lpp=1}
    {prn2:='PRNPOZ2'; prn3:='';~~}
    {IF: PRNPOZ2.ini_line(); (PRNPOZ2.length()+1)>=lineleft() & lineleft()<>0}{PAGE}{ vp:=1;~~}{FI}
    {SUBSTITUTE: 'LINIA02'} {prn1:='PRNPOZ2'; prn2:=prn3:=''; vp:=0; ~~}
{ELSE}
    {SUBSTITUTE: 'LINIA0'}
{FI}
{MACRO-}
{COMMENT}------------------------------MACRO DRUKUJĄCE TAB SER_WOW-----------------------{COMMENT-}
{MACRO: 'WAR_SWOW'}
{ war[1]:=$lp; war[2]:=$SER_WOW.DATA_OD; war[3]:=$SER_WOW.DATA_DO;  war[4]:=SER_WOW.SER_SCH().KOD;  war[5]:=SER_WOW.SER_SCH().OPIS;
  war[6]:={? SER_WOW.KH=null & SER_WOW.GRKH=null || 'Domyślny' |? SER_WOW.GRKH<>null || 'dla Grupy' || 'dla Kontrahenta' ?}; ~~}
{MACRO-}
{MACRO: 'DEK_SWOW'}
{SUBSTITUTE: 'WAR_SWOW'}
{ kolum[1]:=3; kolum[2]:=10; kolum[3]:=10; kolum[4]:=8; kolum[5]:=50; maxkol:=5;
  prn1:='PRNH'; prn2:='PRN'; prn3:=''; lp:=0;
  head[1]:={? WYDRUKI.P11=1
           || 'Schemat windykacyjny na dzień  %1'@[WYDRUKI.P11DDO$1]
           || 'Schematy windykacyjne kontrahenta '@
           ?};
 tyt[1]:='Lp.'@; tyt[2]:='Od dnia'@; tyt[3]:='Do dnia'@; tyt[4]:='Schemat'@; tyt[5]:='Opis'@; tyt[6]:='Rodzaj'@;
 {! _i:=1..maxkol |!  typ_pola[_i]:=0 !}; typ_pola[1]:=1; ~~}
{MACRO-}
{MACRO: 'SER_WOW'}
{IF: WYDRUKI.P1}
{SUBSTITUTE: 'DEK_SWOW'}
{SUBSTITUTE: 'ADDPRN'}
  {IF: WYDRUKI.P11=2}
    {FOR: 'SER_WOW'}
    {INDEX: 'KH'}
    {PREFIX: KH.ref()}
    {DO}
      {FIRST}
       { ramka:='';~~}{SUBSTITUTE: 'TABHEADJ'}{prn1:='PRN'; prn3:=''; ramka:='ser_wow'; ~~}
      {FIRST-}
      {lp+=1; ~~}
      {SUBSTITUTE: 'WAR_SWOW'}
      {SUBSTITUTE: 'LINIA0'}
    {OD}
    {IF: lp & lineleft<>0}
      { ramka:='';~~}{SUBSTITUTE: 'LINIAF'}
      {IF: lineleft<>0}{SUBSTITUTE: 'ODSTEP'}{FI}
    {FI}
  {FI}
{SUBSTITUTE: 'DELPRN'}
{FI}
{MACRO-}
{MACRO: 'PRZEDKH'}
{ramka:='khtab'; ~~}
{IF: lineleft()>3}
  {LINE: 1}
{ELSE}
  {ramka:=''; ~~}
  {IF: ~PAR_WYDR.PAR3 & lineleft<>0}{PAGE}{FI}
{FI}
{MACRO-}
{MACRO: 'ODSTEP'}
  {ramka:=''; ~~}
  {IF: lineleft()>3}
    {IF: PAR_WYDR.PAR3}{LINE: 1}{FI}
  {ELSE}
    {PAGE}
  {FI}
{MACRO-}
{MACRO: 'WYDRUK'}
{SUBSTITUTE: 'KHTAB'}
{IF: WYDRUKI.P11=2}{SUBSTITUTE: 'SER_WOW'}{FI}
{SUBSTITUTE: 'SER_NAG'}
{SUBSTITUTE: 'ZDARZH'}
{SUBSTITUTE: 'ZDARZB'}
{zdarz:=1; ~~}
{MACRO-}
{FONT: 'W10'}
{COMMENT}------------------------------WŁAŚCIWY WYDRUK-----------------------{COMMENT-}
{IF: ~PAR_WYDR.PAR1}
      {SUBSTITUTE: 'WYDRUK'}
   {FOOTER}{FOOTER-}
{ELIF: PAR_WYDR.PAR1=2}
      {FOR: 'KH'}
      {INDEX: 'KOD'}
      {PREFIX: 2}
      {SELECT}
      {DO}
        {IF: PAR_WYDR.PAR3 & xpage & lineleft<>0}{PAGE}{FI}
        {SUBSTITUTE: 'WYDRUK'}
      {OD}
{ELSE}
      {FOR: 'KH'}
      {INDEX: 'KOD'}
      {PREFIX: 2}
      {DO}
        {IF: PAR_WYDR.PAR3 & xpage & lineleft<>0}{PAGE}{FI}
        {SUBSTITUTE: 'WYDRUK'}
      {OD}
{FI}
{FOOTER}{FOOTER-}
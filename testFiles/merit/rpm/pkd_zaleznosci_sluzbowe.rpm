:!UTF-8
{TITLE:Zależności służbowe}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('__P','PODWL','par','tresc','h_rat','lmt','vp','size','loop','pageEnd');
   tresc:='zalez_sl';
   PAR_WYDR.TITLE:='Zależności służbowe'@;

   par:=obj_new(9);
   par[6]:='NIEZNANY';
   par[7]:='NIEZNANY';
   par[8]:='Domyślny';
   EDIT_VAR.win_edit('ZAL_SLUZ');
   EDIT_VAR.fld_fml('PAR_I2','AFTER_EDIT',"
      EDIT_VAR.efld_opt('ZAL_SLUZ','mark=%1,enable=%1'[$(EDIT_VAR.PAR_I2=2)],,'KOD_Z','NAZWA')
   ");
   {? par[5]:=EDIT_VAR.edit("
         {? EDIT_VAR.PAR_I2=2 & EDIT_VAR.KOD_Z=null
         || 'KOD_Z'
         || 1
         ?}
      ")
   || par[4]:=EDIT_VAR.PAR_I1;
      {? EDIT_VAR.PAR_I2=2
      || EDIT_VAR.KOD_Z();
         par[6]:='TYPPOZ';
         par[7]:=SLO_KOD.KOD;
         par[8]:=SLO_KOD.NAZWA
      || EDIT_VAR.KOD_Z:=null
      ?};
      __P:=exec('wybierz_parses','pracownik','PKD').P;

      par[1]:=obj_new(@.CLASS.PRINT,8);
      {? par[4]=2
      || par[1].add("PODWL.POZIOM",6,'Poziom'@,1)
      || par[1].add("PODWL.L",6,'Poziom'@,1)
      ?};
      par[1].add("PODWL.T",9,'Nr teczki'@,1);
      par[1].add("PODWL.NAZWISKO",20,'Nazwisko'@);
      par[1].add("PODWL.IMIE",16,'Imię'@);
      par[1].add("PODWL.WYDZIAL",9,'Jednostka organizacyjna'@);
      par[1].add("PODWL.STN",30,'Stanowisko'@);
      par[1].add("PODWL.J",15,'Jednostka w zależności służbowej'@);
      par[1].add("PODWL.PATH",6,{? par[4]=2 || 'Przełożony'@ || 'Podwładny'@ ?});
      par[1].s_frame();

      par[2]:=obj_new(@.CLASS.PRINT,1);
      par[2].add("
         {? par[4]=2 || '%1: '['Podwładny'@] || '%1: '['Przełożony'@] ?}+
         P.OSOBA().NAZWISKO+' '+
         OSOBA.PIERWSZE+', %1: '['Jednostka organizacyjna'@]+P.WYDZIAL().OPIS+
         ', %1: '['Stanowisko'@]+P.ST().ST",
         par[1].width()-4
      );
      par[2].s_frame();

      PODWL:=0;
      h_rat:=vp:=1; lmt:=size:=loop:=pageEnd:=0;
      par[9]:='';
      {? par[4]=2
      || PODWL:=tab_tmp(4,
            'POZIOM','INTEGER','Poziom',
            'NAZWISKO','STRING[30]','Nazwisko',
            'IMIE','STRING[20]','Imię',
            'T','STRING[9]','Nr teczki',
            'WYDZIAL','STRING[16]','Jednostka org.',
            'STN','STRING[80]','Stanowisko',
            'PATH','STRING[1]','Ścieżka',
            'J','STRING[16]','Jedn. w zal. sł.'
         )
      ?}
   ?}
}
{END:
   VAR_DEL.delete('__P','PODWL','par','tresc','h_rat','lmt','vp','size','loop','pageEnd')
}
{EXIT: par[5]=0}
{COMMENT}OLD: zalez_sl.rpm{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: vp}
{FONT: 'T8'}{ h_rat:=charleft(); vp:=0; ~~ }
{FONT: 'T8G'}{ h_rat:=charleft/h_rat; ~~ }
{SPACE: 'C'}
{LINE}
{<{ceil(lmt*h_rat)+1}'Parametry sprawozdania:'@}
{<{ceil(lmt*h_rat)+1}'Podział wydruku według: '@+{? par[4]=2 || 'Podwładnych'@ || 'Kierowników'@ ?}}
{<{ceil(lmt*h_rat)+1}'Typ obowiązków: '@+par[8]}
{IF: par[4]=2}
   {<{ceil(lmt*h_rat)+1}'Przełożony: B - bezpośredni, P - przypisany, Z - wynikający z zastępstwa, O - wynikający z pełnionych obowiązków '@}
{ELSE}
   {<{ceil(lmt*h_rat)+1}'Podwładny: B - bezpośredni, P - przypisany, Z - wynikający z zastępstwa, O - wynikający z pełnionych obowiązków '@}
{FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'tresc'}
{  {? par[4]=1 & exec('czyKier_Zal_Ob','stp',P.ref(),par[6],par[7])='T'
   || {? var_pres('PODWL')>100 || obj_del(PODWL) ?};
      PODWL:=exec('prac_pod','stanprac',P.OSOBA,par[6],par[7]);
      par[9]:=PODWL.ndx_tmp(,,'L',,,'NAZWISKO',,,'IMIE',,,'T',,);
      PODWL.index(par[9])
   |? par[4]=2
   || PODWL.erase();
      _TMP:=exec('prac_nad','stanprac',P.ref,,par[6],par[7]);
      {? _TMP.first()
      || P.cntx_psh();
         P.clear();
         {!
         |? {? P.seek(_TMP.P,)
            || PODWL.POZIOM:=_TMP.L;
               PODWL.NAZWISKO:=P.OSOBA().NAZWISKO;
               PODWL.IMIE:=P.OSOBA().PIERWSZE;
               PODWL.T:=P.T;
               PODWL.WYDZIAL:=P.WYDZIAL().SYMBOL;
               PODWL.STN:=P.ST().ST;
               PODWL.J:=_TMP.J;
               PODWL.PATH:=_TMP.PATH;
               PODWL.add()
            ?};
            _TMP.next()
         !};
         P.cntx_pop()
      ?}
   || {? var_pres('PODWL')>100 || PODWL.erase() ?}
   ?};
   ~~
}
{IF: var_pres('PODWL')>100 & PODWL.first()}
   {LINE}
   {IF: lineleft()>0 & lineleft()<=6}
      {PAGE}
   {FI}
   {FONT: 'T8B'}
   {SPACE: 'B'}
   {REP: par[2].fhbar(lmt)}
   {par[2].ini_line()}
   {WHILE: par[2].next()}{DO}
      {REP: par[2].fline(lmt)}
   {OD}
   {FOR: PODWL}{DO}
      {pageEnd:=0;~~}
      {FIRST}
         {size:=PODWL.size();loop:=0;~~}
         {REP: '{<{lmt+1}}'+par[2].fjbar(par[1])}
         {FONT: 'T8'}
         {SPACE: 'B'}
         {par[1].ini_head()}
         {WHILE: par[1].h_next()}{DO}
            {REP: par[1].h_fmline(lmt)}
         {OD}
         {REP: par[1].fbar(lmt)}
      {FIRST-}
      {loop+=1;~~}
      {par[1].ini_line()}
      {WHILE: par[1].next()}{DO}
         {REP: par[1].fline(lmt)}
         {IF: lineleft()<=3}
            {IF: par[1].next()}
               {REP: par[1].flbar(lmt)}
            {FI}
            {IF: lineleft()<>0}
               {IF: ~par[1].next()}
                  {pageEnd:=1;~~}
                  {REP: par[1].flbar(lmt)}
               {FI}
               {IF: lineleft()<>0}
                  {PAGE}
               {FI}
            {FI}
            {IF: loop<size}
               {FONT: 'T8B'}
               {SPACE: 'B'}
               {REP: par[2].fhbar(lmt)}
               {par[2].ini_line()}
               {WHILE: par[2].next()}{DO}
                  {REP: par[2].fline(lmt)}
               {OD}
               {REP: '{<{lmt+1}}'+par[2].fjbar(par[1])}
               {FONT: 'T8'}
               {SPACE: 'B'}
               {par[1].ini_head()}
               {WHILE: par[1].h_next()}{DO}
                  {REP: par[1].h_fmline(lmt)}
               {OD}
               {REP: par[1].fbar(lmt)}
            {FI}
         {FI}
      {OD}
   {OD}
   {IF: pageEnd=0 & lineleft()<>0}
      {REP: par[1].flbar(lmt)}
   {FI}
{FI}
{IF: var_pres('PODWL')>100 & par[9]<>''}
   {PODWL.ndx_drop(par[9]);~~}
{FI}
{MACRO-}
{COMMENT}

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{  lmt:=int((charleft()-par[1].width())/2);
   par[2].setcolor(color);
   ~~
}
{IF: par[4]=2}
   {FOR: __P}
   {DO}
      {IF: P.seek(__P.SQL)}
         {SUBSTITUTE: 'tresc'}
      {FI}
   {OD}
{ELSE}
   {  P.cntx_psh();
      P.prefix();
      {? __P.first()
      || {!
         |? {? P.seek(__P.SQL) & (exec('czyKier_Zal_Ob','stp',P.ref(),par[6],par[7])='T')
            || __P.next()
            || __P.del()
            ?}
         !}
      ?};
      P.cntx_pop();
      ~~
   }
   {IF: __P.first()}
      {IF: P.seek(__P.SQL)}
         {SUBSTITUTE: 'tresc'}
      {FI}
      {WHILE: __P.next()}
      {DO}
         {IF: P.seek(__P.SQL)}
            {SUBSTITUTE: 'tresc'}
         {FI}
      {OD}
   {FI}
{FI}
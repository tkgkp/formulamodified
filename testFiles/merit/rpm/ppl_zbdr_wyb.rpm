:!UTF-8
{TITLE: Wybrane składniki zestawienia}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PAR_WYDR.TITLE:='Wybrane składniki zestawienia'@;

   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','test','Lp','fp','PRN');
   {? rep_export()
   || R.cntx_psh();
      R.prefix();
      _maxcol:=R.size();
      R.cntx_pop()
   || _maxcol:=10
   ?};
   test:=0;
   Lp:=1;
   PRN:=obj_new(@.CLASS.PRINT,_maxcol+2);
   {? ~rep_export() || PRN.add("Lp",5,'Lp.',1,,,,,',,\'9.\'') ?};
   PRN.add(
      "  ZB_WART.index('SKLADNIK');
         ZB_WART.prefix(ZB_DEF.ref());
         test:=0;
         (1+ZB_DEF.WARTOSC=^65533)-ZB_DEF.WARTOSC
      ",
      55,'Kryterium zestawienia'@
   );
   {? (_rub:=exec('code_grp','profile',,'listatab',,_maxcol))<>''
   || R.cntx_psh();
      R.index('RUBKOD');
      R.prefix();
      STR.split(1-_rub-1,',');
      {! _cnt:=1.._maxcol
      |? _kod:=#STR.get_word()
      |! {? R.find_key(_kod)
         || PRN.add($('_a:=0;'+
                      '{? ZB_WART.find_key('+$R.RN+') ||'+
                      ' {? _a:=ZB_WART.KWOTA || test+={? rep_export() || ~(ZB_DEF.WARTOSC*\'RAZEM\') || 1 ?} ?}'+
                      '?}; {? _a || _a || \'0,00\' ?}'),
                    12,R.RT,1,,,,,'12,2,\'9,\'')
         ?}
      !};
      R.cntx_pop()
   ?};
   PRN.s_frame();

   exec('otworz_zb_def','zbiorowk',ZB_OPIS.LISTA);
   exec('otworz_zb_wart','zbiorowk',ZB_OPIS.LISTA);

   prn1:='PRN';
   lmt:=rsep:=0;
   vp:=fp:=1
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','test','Lp','fp','PRN')
}
{COMMENT}OLD: zbdrwybr.rpm{COMMENT-}
{EXIT: ~exec('no_limit','schemat','PPL')}
{INCLUDE: wsp_pw.rpi}
{COMMENT}

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: fp}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {IF: rep_export()}
      {LINE}
   {FI}
   {<{lmt}'Parametry sprawozdania'@}
{     _max:=charleft()-2*lmt;
      _lp:='Listy płac:'@;
      fp:='';
      ZB_LISTY.cntx_psh();
      ZB_LISTY.index('SYMBOL');
      ZB_LISTY.prefix(ZB_OPIS.ref());
      {? ZB_LISTY.first()
      || {!
         |? _lp+=' '+ZB_LISTY.SYMBOL;
            ZB_LISTY.next()
         !}
      || _lp+=' '+VAR.NAZWALIS
      ?};
      ZB_LISTY.cntx_pop();
      STR.split(_lp);
      {!
      |? (_linia:=STR.line(_max))<>''
      |! fp+='{<{lmt}}'+_linia;
         {? STR.next()
         || fp+='{LINE}'
         ?}
      !};
      ~~
   }{REP: fp}
   { fp:=''; ~~ }
   {<{lmt}'Nazwa zestawienia:'@} {ZB_OPIS.TYP().NAZWA}
   {<{lmt}'Klucz zestawienia:'@}{FOR: ZB_KLUCZ}{INDEX: 'NUMER'}{PREFIX: ZB_OPIS.TYP}
   {DO}{
      _zl:=ZB_KLUCZ.NAZWA_ZL;
      fp+=', '+ZB_KLUCZ.NAZWA+
          {? +_zl || ' -> '+_zl || '' ?};
      ~~
   }{OD} {2-fp}
   { fp:=0; ~~ }
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{  PRN.ini_head();
   PRN.setcolor(color);
   ~~
}
{REP: PRN.fhbar(lmt)}
{WHILE: PRN.h_next()}{DO}
{REP: PRN.h_fline(lmt)}
{OD}
{REP: PRN.fbar(lmt)}
{ PRN.setcolor('255:255:255'); ~~ }
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: PRN.flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: ZB_DEF}{INDEX: 'OPIS'}{PREFIX: ZB_OPIS.ref()}{DO}
   {IF: PRN.ini_line(); test}
      {ENTIRE}
      {WHILE: PRN.next()}{DO}
         {REP: PRN.fline(lmt)}
      {OD}
      {ENTIRE-}
      { Lp+=1; ~~ }
   {FI}
{OD}
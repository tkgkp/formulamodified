:!UTF-8
{TITLE:Aneks do umowy o pracę}
{PRINTER: exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PXX_ZASWUMAN')='T',,,,'I','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='';
   VAR_DEL.delete('PARCZAS','Text','lmt','indent','cnt','num','ppkt','ppkt_inne','wydtmp','__CALL','par','aneks');
   VAR_DEL.delete('czy_pep','czy_szablon','czy_paperless','__ppsf');
   __ppsf:=obj_new('czyPPSF','obj');
   _test:=params_get();
   __ppsf.obj:=
      {? var_pres('_test')>100 & var_pres('obj',_test)>100
      || _test.obj
      || obj_new('jestH','jestA','brakH','brakA','pobierz','PPSF_H','PPSF_ADR','drukH','drukA','exit_code','CFG')
      ?};
   __ppsf.czyPPSF:=exec('is_pzd02','ppsf');
   PARCZAS:=PAR_WYDR.CZAS;
   PAR_WYDR.CZAS:=0;
   Cntx.psh(P,H_UM,H);
   H.index('HISTUM');
   lmt:=cnt:=num:=indent:=0;
   Text:=ppkt:=ppkt_inne:=wydtmp:='';
   aneks:=obj_new('OZA','PESEL','STN','WYD','WYM','SW1','SW2','SW3','SW4','SW5','PAT','PAW','IW1','IW2','IW3','IW4',
      'KW_SL1','KW_SL2','KW_SL3','KW_SL4',
      'PPSF_OD','PPSF_DO','PPSF_A','PPSF_R','PPSF_I'
      );
   par:=params_get();
   czy_szablon:=exec('rep_set','rap_zew','PXX_ZASWUMAN',1)='T';
   czy_pep:=var_pres('par')>100 & var_pres('PEP',par)>0;
   _paperless:=czy_pep & var_pres('active',par.PEP)>0 & par.PEP.active;
   {? _paperless
   || czy_paperless:=0;
      {? ~czy_szablon & exec('rep_set','rap_zew','PXX_ZASWUMAN')<>'T' & par.PEP.fml_after=""
      || czy_paperless:=1
      ?}
   || czy_paperless:=1
   ?};
   {? _paperless & var_pres('H',par)<0
   || _tab:=tab_tmp(1,'H','STRING[16]','ref');
      _tab.H:=par.PEP.CALL.H_REF;
      _tab.add();
      _new:=exec('obj_ntab_set','#array',par,'H',_tab,'ZAL','0','DALEJ','');
      obj_del(par); &par;
      par:=_new; obj_del(_new); &_new
   || {? type_of(par)=type_of(~~)
      || par:=obj_new('H','ZAL','DALEJ')
      ?};
      {? var_pres('H',par)<0
      || _par:=exec('obj_ntab_set','#array',par,'H',~~);
         obj_del(par);par:=_par; obj_del(_par)
      ?};
      {? var_pres('ZAL',par)<0
      || _par:=exec('obj_ntab_set','#array',par,'ZAL','');
         obj_del(par);par:=_par; obj_del(_par)
      ?};
      {? var_pres('DALEJ',par)<=0
      || _par:=exec('obj_ntab_set','#array',par,'DALEJ','');
         obj_del(par);par:=_par; obj_del(_par);
         par.DALEJ:=exec('uprawnieniawrap','pkd',
            'Przebiegu zatrudnienia'@,
            'PKD_EZK_PPZA',
            'PKD_EZK_RPZA'
         );
         {? par.DALEJ=''
         || par.H:=exec('aneks_do_umowy','!pkd_zes_orza');
            par.ZAL:='0'
         || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[par.DALEJ])
         ?}
      ?}
   ?};
   {? _paperless
   || {? par.PEP.P
      || params_set(
            'P',exec('wybierz_silent','pracownik',par.PEP.P).P,
            'paperless',_paperless,
            'par',par,
            'CALL',null()
         )
      || params_set(
            'P',{? H.seek(par.H.H,,1) || exec('wybierz_silent','pracownik',H.P).P || null() ?},
            'paperless',_paperless,
            'par',par,
            'CALL',null()
         )
      ?};
      par.DALEJ:={? ~params_get(1).P.first() || 'Nie wybrano pracownika' || '' ?}
   || params_set(
         'P',null(),
         'paperless',_paperless,
         'par',par,
         'CALL',null()
      )
   ?};

   __xr_ins:=exec('new_ins','rap_zew','PXX_ZASWUMAN')
}
{END:
   PAR_WYDR.CZAS:=PARCZAS;
   VAR_DEL.delete('PARCZAS','Text','lmt','indent','cnt','num','ppkt','ppkt_inne','wydtmp','__CALL','par','aneks');
   VAR_DEL.delete('czy_pep','czy_szablon','czy_paperless','__ppsf');
   exec('del_ins','rap_zew',&__xr_ins);
   Cntx.pop(P,H_UM,H)
}
{COMMENT}OLD: zaswuman.rpm{COMMENT-}
{COMMENT}OLD: zaswuma2.rpi{COMMENT-}
{COMMENT}OLD: zaswuman.rpi{COMMENT-}
{EXIT: +par.DALEJ}
{EXIT: ~par.H.first()}
{EXIT: ~__xr_ins}
{FOR: par.H}
 {DO}
    {IF: H.seek(par.H.H,,1)}
       {H.prefix(H.UMOWA); ~~}
       {INCLUDE: p_zaswuma2.rpi}
    {FI}
 {OD}
{EXIT: ~exec('ready','rap_zew',__xr_ins) & ~(params_get().paperless & ~czy_paperless)}
{
   params_set(_par:=params_get());
   {? _par.paperless & _par.par.PEP.fml_after<>""
   || __CALL:=_par.par.PEP.CALL
   || __CALL:=proc_exec('uman@zasw',#__xr_ins);
      {? czy_pep || _par.par.PEP.CALL:=__CALL ?}
   ?};
~~ }
{IF: (cli_ver()='jterm' | exec('cli_ver','#system')='interm') & (
     czy_szablon |
     exec('rep_set','rap_zew','PXX_ZASWUMAN_PRAUT',1)='T')}
   {FOR: par.H}{DO}
      {IF: H.seek(par.H.H,,1) & __CALL.find_tab(,'H_REF',,'=',$H.ref())}{
         H.prefix(H.UMOWA);
         H.P().OSOBA();
         _usun:=0;
         _zaswuman:=obj_new('ppkt','ppkt_inne','praca_zd','praca_zw','praca_od','praca_do');
         _zaswuman.ppkt:=__CALL.PPKT;
         _zaswuman.ppkt_inne:=__CALL.GODZNADL<>'' | __CALL.INNEWAR<>'   ';
         _zaswuman.praca_zd:=__CALL.PRACA_ZD;
         _zaswuman.praca_zw:=__CALL.PRACA_ZW;
         _zaswuman.praca_od:=__CALL.PRACA_OD;
         _zaswuman.praca_do:=__CALL.PRACA_DO;
         _exe:={? var_pres('SERIA',par)=type_of('') & par.SERIA='T' || -1 || 2 ?};
         _szab:={? _exe<0 || par.SZABLON || ~~ ?};
         params_set('zaswuman',_zaswuman);
         _paperless:=params_get().paperless;
         _ret:=~~;
         {? (__CALL.PAT='' | __CALL.PAT='N') & czy_szablon
         || _ret:=exec('wypelnij','szablon','PXX_ZASWUMAN',_szab,,_exe,,_paperless);
            _usun:=1
         |? +__CALL.PAT & __CALL.PAT<>'N' & exec('rep_set','rap_zew','PXX_ZASWUMAN_PRAUT',1)='T'
         || _ret:=exec('wypelnij','szablon','PXX_ZASWUMAN_PRAUT',_szab,,_exe,,_paperless);
            _usun:=1
         ?};
         {? _exe<0 || par.SERIA_RET.STATUS:=_ret.STATUS; par.SERIA_RET.INFO:=_ret.INFO ?};
         {? _usun<>0
         || __CALL.del()
         ?};
         {? par.ZAL='1' & type_of(_ret)>100 & _ret.STATUS=1 & _exe>=0
         || {? _paperless
            || params_exec('dodaj_dokument','paperless',_ret.INFO,H.uidref(),par)
            || exec('add_zal','zal','an',,_ret.INFO)
            ?}
         ?}; ~~
      }{FI}
   {OD}
   {EXIT: __CALL.size()=0}
{FI}
{IF: cli_ver()='jterm' & exec('rep_set','rap_zew','zaswuman')='T'}
   {  exec('use_tmp','rap_zew','zaswuman');
      {? par.ZAL='1'
      || {? params_get().paperless
         || params_exec('dodaj_dokument','paperless','@!Tmp\\macrotmp.doc',H.uidref(),par)
         || exec('add_zal','zal','an')
         ?}
      ?}; ~~
   }
   {EXIT}
{ELSE}
   {IF: params_get().paperless & params_get().par.PEP.fml_after=""}
      {
      params_get().par.PEP.P:=P.ref();
      params_get().par.PEP.fml_after:="
         _pep:=obj_new('active','fml_after','P','CALL');
         _pep.active:=1;
         _pep.fml_after:=\"1\";
         _pep.P:=_a;
         _pep.CALL:=_b;
         {? var_pres('_params')>100
         || exec('obj_ntab_set','#array',_params,'PEP',_pep)
         || _params:=obj_new('PEP');
            _params.PEP:=_pep
         ?};
         params_set(_params);
         _name:='@'+tmp_dir()+exec('sep','#file')+'pkd_zaswaneksdoumowy_%1'[$P.tm_stamp()]+'.pdf';
         rep_exec('pkd_zaswaneksdoumowy',,0,_name,1,,1);
         params_exec('dodaj_dokument','paperless',_name,H.uidref(),params_get())
      ";
      ~~}
      {EXIT}
   {ELSE}
      {INCLUDE: p_zaswuman.rpi}
   {FI}
{FI}
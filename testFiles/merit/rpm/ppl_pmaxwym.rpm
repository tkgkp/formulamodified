:!UTF-8
{TITLE:Prognozowane przekroczenie maksymalnego wymiaru podstawy składek}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__param','__edit');
   PAR_WYDR.TITLE:=
      'Prognozowane przekroczenie maksymalnego wymiaru podstawy składek'@+'\n'+'na ubezpieczenia.'@;
   P.cntx_psh();
   tresc:='pmaxwym'; par:=obj_new(9);
   par[2]:=par[3]:=par[4]:=par[5]:=par[7]:=par[9]:=0;
   _upr:=exec('uprawnieniawrap','pkd',
              'Naliczonych składnikach płacowych'@,
              'PPL_PLL_RSKP',
              'PPL_PLL_PSKP'
             );
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
               'OD','INTEGER','Miesiąc od',
               'DO','INTEGER','Miesiąc do');
      __param.OD:=date~2;
      __param.DO:=12;
      __param.add();
      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pmaxwym');
      __param.win_esep(__edit,'Przekroczenie podstawy składek w miesiącach'@);
      __param.win_efld(__edit,,'OD',,,2,,,'Miesiąc od'@);
      __param.win_efld(__edit,,'DO',,,2,,,'Miesiąc do'@);
      __param.efld_opt(__edit,'mark=1',,'OD');
      __param.efld_opt(__edit,'mark=1',,'DO');
      exec('ok_esc','#window',__param,__edit);
      __param.fld_fml('OD','AFTER_EDIT',"{? __param.OD<1 | __param.OD>12
                                         || FUN.emsg('Miesiąc początku okresu musi być poprawnie wypełniony.'@);
                                            'OD'
                                         |? __param.OD>__param.DO
                                         || FUN.emsg('Miesiąc początku okresu nie może być większy od końcowego.'@); 0
                                         || 1
                                         ?}");
      __param.fld_fml('DO','AFTER_EDIT',"{? __param.DO<1 | __param.DO>12
                                         || FUN.emsg('Miesiąc końca okresu musi być poprawnie wypełniony.'@);
                                            'DO'
                                         |? __param.OD>__param.DO
                                         || FUN.emsg('Miesiąc początku okresu nie może być większy od końcowego.'@); 0
                                         || 1
                                         ?}");

      __param.win_edit(__edit);
      {? par[9]:=__param.edit("{? __param.OD<1 | __param.OD>12
                               || FUN.emsg('Błędna wartość parametru wydruku.'@); 0
                               || par[3]:=__param.OD; par[4]:=__param.DO; 1
                               ?}")
      || par[1]:=obj_new(@.CLASS.PRINT,6);
         par[1].add("par[7]+=1",5,'Lp.'@,1,,,,,',,\'9.\'');
         par[1].add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE",55,'Nazwisko i imiona'@);
         par[1].add("par[6]$8",20,'Prognozowany miesiąc przekroczenia maksymalnego wymiaru składek'@);
         par[1].add("par[2]",20,'Łączna wyskość podstawy skł. odczytana z kartotek KU i UZ'@,1,,,,,'15,2');
         par[1].add("par[3]",20,'Prognozowana kwota do końca roku na podstawie zatrudnienia'@,1,,,,,'15,2');
         par[1].s_frame();

         prn1:='par[1]';
         lmt:=rsep:=0;
         vp:=1;

         par[6]:=date(0,0,0);
         params_set('P',exec('wybierz_parses','pracownik','PPL').P);
         par[8]:=1; h_rat:=1
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__param','__edit')
}
{COMMENT}OLD: pmaxwym.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[9]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[8]}
   {FONT: 'T8'}{ h_rat:=charleft; ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft/h_rat; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[8]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Maksymalna podstawa wymiaru składek'@}: {form(KST.SK,,2)}
   {<{ceil(lmt*h_rat)+1}'Zakres analizy'@}: {date(date~1,__param.OD,1)$8} - {date(date~1,__param.DO,1)$8}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: P.ZA='T'}
{  echo(P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE);
   par[4]:=par[5]:=par[2]:=par[3]:=0;
   par[6]:=date(0,0,0);
   ~~
}
   {WHILE: (par[5]+=1)<=12}{DO}
      {FOR: KU}
      {INDEX: 'KARTAUB'}
      {PREFIX: exec('ref_firma','ustawienia'),P.OSOBA,date~1,par[5]}
      {DO}
         { par[4]:=KU.M; par[2]+=KU.S1; ~~ }
      {OD}
      {FOR: RH}
      {INDEX: 'RACHDATA'}
      {FROM: exec('ref_firma','ustawienia'),P.OSOBA,date~1,par[5]}
      {TO: exec('ref_firma','ustawienia'),P.OSOBA,date~1,par[5]}
      {DO}
         { {? RH.ZC_INFO().ZUS='T' || par[2]+=FUNKCJE.Z(200) ?}; ~~ }
      {OD}
      {  {? par[2]>=KST.SK & par[6]=date(0,0,0) ||
            par[6]:=date(date~1,par[5],1)
         ?};
         ~~
      }
   {OD}
   {IF: par[4]<12}
   {
     H.cntx_psh;
     H.index('_HISTKOD');
     H.prefix(P.ref,'Z');
     {? H.first
     ||  {? H.find_le(date(date~1,par[4]+1,1))
         || _hsx:=exec('hsx','lista_licz',date(date~1,par[4]+1,1));
            {! _n:=par[4]+1..12
            |! par[3]+={? 1+H.CP().S='U' || 1 || 168 ?}*_hsx.S1+_hsx.S2+_hsx.S3;
               {? (par[2]+par[3])>=KST.SK & par[6]=date(0,0,0) || par[6]:=date(date~1,_n,1) ?}
            !};
            obj_del(_hsx); &_hsx
         ?}
     ?};
     H.cntx_pop;
     ~~
   }
   {FI}
   {IF: __param.OD<=(par[6]~2) & (par[6]~2)<=__param.DO}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft-par[1].width())/2); ~~ }
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
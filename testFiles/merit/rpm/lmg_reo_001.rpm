:!UTF-8
{TITLE:Reorganizacja}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   reo_001:="
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('nn','kk','ww','nn2','kk2','ww2','doklw','PRN','PRN2');
      VAR_DEL.delete('wydr_naz','wydr_edi','X_Xw1','X_Xw2','poznalok')";
   reo_001();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   _ilk2:=2;
   nn2:=obj_new(_ilk2); "--nazwy kolumn--";
   kk2:=obj_new(_ilk2); "--szerokosci kolumn--";
   ww2:=obj_new(_ilk2); "--wartosci kolumn--";
   _ilk:=9;
   nn:=obj_new(_ilk); "--nazwy kolumn--";
   kk:=obj_new(_ilk); "--szerokosci kolumn--";
   ww:=obj_new(_ilk); "--wartosci kolumn--";

   poznalok:=3;
   dokl:=3; "--dokladnosc wartosc--";

   PRN2:=obj_new(@.CLASS.PRINT,2); PRN2.s_frame();
   kk2[1] :=20;   nn2[1] :='Lokalizacja'@;
   kk2[2] :=20;   nn2[2] :='Uwagi'@;

   PRN:=obj_new(@.CLASS.PRINT,10); PRN.s_frame();
   kk[1] :=20;    nn[1] :='Materiał'@;
   kk[2] :=25;    nn[2] :='Nazwa'@;
   kk[3] :=16;    nn[3] :='Lokal.Z'@;
   kk[4] :=16;    nn[4] :='Lokal.DO'@;
   kk[5] :=10;    nn[5] :='T.ważn.Z'@;
   kk[6] :=10;    nn[6] :='T.ważn.DO'@;
   kk[7] :=12;    nn[7] :='Ilość'@;
   kk[8] :=10;    nn[8] :='Paleta Z'@;
   kk[9] :=10;    nn[9] :='Paleta DO'@;

   {! _ii.._ilk2 |! ww2[_ii] :='' !};
   {! _ii.._ilk |! ww[_ii] :='' !}
}
{END:
   reo_001(); VAR_DEL.delete('reo_001')
}
{
PRN2.add("ww2[1]",kk2[1],nn2[1]);
PRN2.add("ww2[2]",kk2[2],nn2[2]);

PRN.add("ww[1]",kk[1],nn[1]);
PRN.add("ww[2]",kk[2],nn[2]);
PRN.add("ww[3]",kk[3],nn[3]);
{? DK_LN.RODZ<>'B' || PRN.add("ww[4]",kk[4],nn[4]) ?};
PRN.add("ww[5]",kk[5],nn[5]);
{? DK_LN.RODZ<>'B' || PRN.add("ww[6]",kk[6],nn[6]) ?};
{? DK_LN.MG().PAL='T' || PRN.add("ww[8]",kk[8],nn[8]) ?};
{? DK_LN.MG().PAL='T' & DK_LN.RODZ<>'B' || PRN.add("ww[9]",kk[9],nn[9]) ?};
PRN.add("ww[7]",kk[7],nn[7],1);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'NAZ'    ,'STRING[100]' ,'Nazwa',
   'LOK'    ,'STRING[20]'  ,'Lokaliz.Z',
   'LOKDO'  ,'STRING[20]'  ,'Lokaliz.DO',
   'TW'     ,'DATE'        ,'T.ważno.Z',
   'TWDO'   ,'DATE'        ,'T.ważno.DO',
   'IL'     ,'REAL'        ,'Ilość',
   'PAL'    ,'STRING[30]'  ,'Paleta Z',
   'PALDO'  ,'STRING[30]'  ,'Paleta DO');
X_Xw1.index(X_Xw1.ndx_tmp(,,'KTM',,,'LOK',,,'TW',,));

DK_LN.cntx_psh; DK_L.cntx_psh; DK_LI.cntx_psh;
DK_LN.prefix;
DK_L.index('DK_LN');
DK_LI.index('DK_LN');
_lp:=0;
{? __Sel.first
|| {!
   |?
      {? DK_LN.seek(__Sel.REF,)
      ||
         _znak:={? DK_LN.AKC='T' & DK_LN.RODZ<>'B' || -1 || 1 ?};
         DK_L.prefix('Z',DK_LN.ref);
         {? DK_L.first
         || {!
            |?
               X_Xw1.KTM:=DK_L.M().KTM;
               X_Xw1.NAZ:=M.N;
               X_Xw1.LOK:=DK_L.LOK().KOD;
               X_Xw1.LOKDO:=DK_L.LOKDO().KOD;
               X_Xw1.TW:=DK_L.TW;
               X_Xw1.TWDO:=DK_L.TWDO;
               X_Xw1.PAL:=DK_L.PAL().KODK;
               X_Xw1.PALDO:=DK_L.PALDO().KODK;
               X_Xw1.IL:=_znak*DK_L.IL;
               X_Xw1.add() & DK_L.next
            !}
         ?}
      ?};
      __Sel.next
   !}
?};

X_Xw2:=tab_tmp(1,
   'LOK'    ,'STRING[20]'  ,'Lokaliz.Z');
X_Xw2.index(X_Xw2.ndx_tmp(,,'LOK',,,'LOK',,));
{? ~X_Xw1.first & DK_LN.RODZ='I'
||
   {? __Sel.first
   ||
      {!
      |?
         {? DK_LN.seek(__Sel.REF,)
         ||
            DK_LI.prefix(DK_LN.ref);
            {? DK_LI.first
            ||
               {!
               |?
                  _lok:=DK_LI.EANL().KOD;
                  {? ~X_Xw2.find_key(_lok,_lok)
                  ||
                     X_Xw2.LOK:=_lok;
                     X_Xw2.add
                  ?};
                  DK_LI.next
               !}
            ?}
         ?};
         __Sel.next
      !}
   ?};
   {? X_Xw2.first
   ||
      X_Xw1.blank(1);
      {! ..X_Xw2.size*poznalok |! X_Xw1.add !}
   ?}
?};
DK_LN.cntx_pop; DK_L.cntx_pop; DK_LI.cntx_pop;

PAR_WYDR.TITLE:='REORGANIZACJA';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0 & X_Xw2.first=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{FOR: __Sel}
{DO}
   {IF: DK_LN.seek(__Sel.REF,)}
      {<{lmt+1}}{'Rejestracja (data, czas, kto): '+form(DK_LN.DT)+', '+form(DK_LN.TM)+', '+form(DK_LN.US().DANE)}
   {FI}
{OD}
{COMMENT}   >> pozycje lokalizacji do inwentaryzacji <<              {COMMENT-}
{IF: X_Xw2.first}
   { prn1:='PRN2';~~}
   {FONT: 'T8GB'}
   {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
   {HEADER}
      {SUBSTITUTE: 'HEAD'}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'TABHEAD'}
   {HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FOOTER: 0}
      {SUBSTITUTE: 'LINIAF'}
      {LINE: 1}
   {FOOTER-}
{COMMENT}   pozcycje wydruku                                         {COMMENT-}
   { rsep:=PAR_WYDR.RSEP;  ~~}
   {FONT: 'T8G'}{SPACE: 'C'}
   {FOR: X_Xw2}
   {DO}
      {
      ww2[1]:=form(X_Xw2.LOK);
      ww2[2]:=''
      ;~~}
      {SUBSTITUTE: 'LINIA0'}
   {OD}
   {IF: lineleft>0}{SUBSTITUTE: 'LINIAF'}{FI}
{FI}
{COMMENT}   >> pozycje standardowe <<                                {COMMENT-}
{ prn1:='PRN';~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozcycje wydruku                                         {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;  ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xw1}
{DO}
   { ww[1]:=form(X_Xw1.KTM);
     ww[2]:=form(X_Xw1.NAZ);
     ww[3]:=form(X_Xw1.LOK);
     ww[4]:={? X_Xw1.LOK=X_Xw1.LOKDO || '' || form(X_Xw1.LOKDO) ?};
     ww[5]:={? X_Xw1.KTM='' || '' || form(X_Xw1.TW) ?};
     ww[6]:={? X_Xw1.TW=X_Xw1.TWDO || '' || form(X_Xw1.TWDO) ?};
     ww[7]:={? X_Xw1.KTM='' || ' ' || form(X_Xw1.IL,,dokl,'.,') ?};
     ww[8]:=form(X_Xw1.PAL);
     ww[9]:={? X_Xw1.PAL=X_Xw1.PALDO || '' || form(X_Xw1.PALDO) ?}; ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
:!UTF-8
{TITLE:Kalendarz do planu urlopów}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','col');
   PAR_WYDR.TITLE:='';
   tresc:='pkd_wplanyurlopowe';
   par:=obj_new(10);
   col:=obj_new(37);
   par[1]:=0;
   par[2]:=date()~1;
   par[10]:=exec('uprawnieniawrap','pkd','kartach urlopowych'@,'PKD_EZK_OPKU','PKD_EZK_ORKU');

   P.cntx_psh();
   KAL_ROK.cntx_psh();
   KAL_ROK.index('KAL_ROK');
   KAL_ROK.clear();
   {? +par[10]
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[par[10]])
   || params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?}
}
{END:
   P.cntx_pop();
   KAL_ROK.cntx_pop();
   VAR_DEL.delete('par','col')
}
{COMMENT}OLD: wplanurl.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: +par[10]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}{
   PAR_WYDR.TITLE:=
      'Kalendarz do planu urlopowego na rok '@+$par[2]+'\n'+
      P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' (%1 '['nr akt'@]+(|P.T)+')';
   par[5]:=par[6]:=0;
   __KAL.set_cal(P.KAL);
   ~~
}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
┌{13*'─'}{37*('┬'+(3*'─'))}┐
│ Miesiąc {>15'│'}{WHILE: (par[6]+=1)<=37}{DO} {
  _n:=par[6]%*7;
  {? _n=0 || 'Nd'@
  |? _n=1 || 'Pn'@
  |? _n=2 || 'Wt'@
  |? _n=3 || 'Śr'@
  |? _n=4 || 'Cz'@
  |? _n=5 || 'Pt'@
  || 'So'@
  ?}
}│{OD}
├{13*'─'}{37*('┼'+(3*'─'))}┤
{WHILE: (par[5]+=1)<=12}{DO}
│ {_m:=date(,par[5],1)$8;_m:=(_m*' ')+_m;_m}{>15'│'}{
   par[4]:=date(par[2],par[5],1)~4;
   par[7]:=date(par[2],par[5],0)~3;
   par[6]:=0;
   ~~
}{WHILE: (par[6]+=1)<=37}{DO}{
   par[8]:=par[6]-par[4]+1;
   col[par[6]]:='0:0:0,255:255:255';
   {? par[8]<=0 || par[8]:=''
   |? par[7]<par[8] || par[8]:=''
   || _dm:=date(par[2],par[5],par[8]);
      {? __KAL.get_day(_dm) ||
         {? KAL_DEF.TYP<>'R' ||
            col[par[6]]:='0:0:0,'+color
   ?} ?} ?};
   ~~
}{COLOR: col[par[6]]}{>{18+((par[6]-1)*4)}par[8]
}{>{19+((par[6]-1)*4)}'│'}{OD}{COLOR: '0:0:0,255:255:255'}
{IF: par[5]<12}
{  par[6]:=0; ~~ }
├{13*'─'}{WHILE: (par[6]+=1)<=37}{DO}{
}┼{COLOR: col[par[6]]}{3*'─'}{OD}┤{COLOR: '0:0:0,255:255:255'}
{FI}{OD}
{  par[6]:=0; ~~ }
└{13*'─'}{WHILE: (par[6]+=1)<=37}{DO}{
}┴{COLOR: col[par[6]]}{3*'─'}{OD}┘{COLOR: '0:0:0,255:255:255'}
{FONT: 'W12'}
{SPACE: 'A'}
{FOR: KART_URL}{INDEX: 'PRAC_ROK'}{PREFIX: P.ref(),par[2]}{DO}
{exec('kart_url_sumuj','kart_url'); ~~}
{IF: OSOBA.PLEC='K'}
{'W roku %1 powinna Pani wykorzystać %2 dni urlopu wypoczynkowego.'@[$par[2],$EDIT_VAR.RAZEM]}
{ELSE}
{'W roku %1 powinien Pan wykorzystać %2 dni urlopu wypoczynkowego.'@[$par[2],$EDIT_VAR.RAZEM]}
{FI}
{OD}
{PAGE}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=0; ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1) & (P.DZ>=date() | P.DZ=#0)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
:!UTF-8
{TITLE:A. Dokument źródłowy}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,14); PRN.sl_frame();
   PRNH:=obj_new(@.CLASS.PRINT,5); PRNH.sl_frame();
   PRNF:=obj_new(@.CLASS.PRINT,4); PRNF.sl_frame();
   w:=obj_new(14);
   czy_w:=sumawn:=sumama:=sumawnb:=sumamab:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=NOMAXP:=0
}
{END:
   VAR_DEL.delete('PRN','PRNH','PRNF','w','NOMAXP');
   &czy_w; &sumawn; &sumama; &sumawnb; &sumamab;
   &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep; exec('end_msg','dok_fks_zest')
}
{IF: var_pres('TT_D_DOK')<100}
   {  POZ.index('DOK'); POZ.prefix(DOK.ref);
      {? ~POZ.first()
      || FUN.info('\nBrak pozycji w dokumencie!\n'@)
      ?};
   ~~}
   {EXIT: ~POZ.first()}
{FI}
{  {? PAR_SKID.get(39)='T' || czy_w:=1
   |? PAR_SKID.get(39)='N' || czy_w:=0
   || czy_w:=2
   ?};
   {? czy_w=2
   || czy_w:=FUN.ask('Prezentować dane związane z walutą obcą?'@)
   ?}; ~~}
{MACRO: 'INIT'}
{  _o:=34;
   PRN.add("w[1]",5,'Lp.'@,1);
   PRN.add("w[2]",35,'Konto'@);
   PRN.add("form(w[3],,2,' ,')",14,'Winien'@,1);
   PRN.add("form(w[4],,2,' ,')",14,'Ma'@,1);
   {? czy_w
   || PRN.add("w[5]",4,'Wal.'@);
      PRN.add("form(w[6],,4,' ,')",8,'Kurs'@,1);
      PRN.add("form(w[7],,2,' ,')",14,'Kwota w walucie'@,1)
   || _o+=28
   ?};
   {? DOK.DOK_REJ().SLO().KOD<>'BO'
   || PRN.add("w[8]",50,'Symbol'@);
      PRN.add("w[9]",3,'Typ'@);
      PRN.add("w[10]",10,'Data otw.'@);
      PRN.add("w[11]",10,'T. płatn.'@);
      {? DOK.DOK_REJ().CZY_ROZL='T'|| PRN.add("w[12]",10,'Data rozl.'@) || _o+=10 ?};
      {? DOK.DOK_REJ().ODD_ROZR='T'|| PRN.add("w[13]",8,'Jed. ks.'@) || _o+=8 ?}
   || _o+=41
   ?};
   PRN.add("w[14]",_o,'Opis'@);
   PRNH.add("w[1]",41,,,,,,,,,2);
   PRNH.add("w[2]",29,'Kwoty w walucie narodowej'@,,,,,,,,2);
   {? czy_w || PRNH.add("w[3]",28,'Dane o zapisie walutowym'@,,,,,,,,3) ?};
   {? DOK.DOK_REJ().SLO().KOD<>'BO'
   || _v:=76;
      {? DOK.DOK_REJ().CZY_ROZL='T' || _v+=11 ?};
      {? DOK.DOK_REJ().ODD_ROZR='T' || _v+=9 ?};
      PRNH.add("w[4]",_v,'Rozrachunek'@,,,,,,,,5)
   ?};
   PRNH.add("w[5]",_o);
   PRNF.add("w[1]",41,,,,,,,,,2);
   PRNF.add("form(w[2],,2,' ,')",14,,1);
   PRNF.add("form(w[3],,2,' ,')",14,,1);
   ~~}
{MACRO-}
{MACRO: 'START'}
{  obj_del(PRN); obj_del(PRNH); obj_del(PRNF);
   PRN:=obj_new(@.CLASS.PRINT,14); PRN.sl_frame();
   PRNH:=obj_new(@.CLASS.PRINT,5); PRNH.sl_frame();
   PRNF:=obj_new(@.CLASS.PRINT,4); PRNF.sl_frame();
   sumawn:=sumama:=sumawnb:=sumamab:=0;
   ~~}
   {SUBSTITUTE: 'INIT'}
{MACRO-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}------------------------------MACRO-TABHEADX---------------{COMMENT-}
{MACRO: 'TABHEADX'}
{  PRNH.setcolor(color);
   PRNH.ini_head(); ~~}
{REP: PRNH.fhbar(lmt)}
{WHILE: PRNH.h_next()}
{DO}
   {REP: PRNH.h_fmline(lmt)}
{OD}
{REP: PRNH.fjbar(PRN,lmt)}
{  PRN.setcolor(color);
   PRN.ini_head(); ~~}
{WHILE: PRN.h_next()}
{DO}
   {REP: PRN.h_fmline(lmt)}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO WYDRUK-----------------{COMMENT-}
{MACRO: 'WYDRUK'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='DOKUMENT ŹRÓDŁOWY '@+DOK.NK;~~}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: rep_export()}
{'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}
{'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}{IF: DOK.NRKSEF<>''}{'Nr KSeF: '+DOK.NRKSEF}{FI}
{'Data operacji: %1'@[$DOK.DOP]}
{'Data zapisu: %1'@[$DOK.DTW]}
{'Data wystawienia: %1'@[$DOK.DTO]}
{IF: DOK.KSEFCDAT<>date(0,0,0)}{'Data KSeF: '+$DOK.KSEFCDAT}{FI}
{IF: DOK.D4<>date(0,0,0)}{'Data otrzymania: %1'@[$DOK.D4]}{FI}
{IF: DOK.D3<>date(0,0,0)}{'Termin płatności: %1'@[$DOK.D3]}{FI}
{'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}
{'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}
{'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}
{'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}{FI}
{LINE: 1}
{ELSE}
{<2 'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{<2 'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}{<40 'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{<2 'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}{IF: DOK.NRKSEF<>''}{<66 'Nr KSeF: '+DOK.NRKSEF}{FI}
{<2 'Data operacji: %1'@[$DOK.DOP]}{<34 'Data zapisu: %1'@[$DOK.DTW]}{<66 'Data wystawienia: %1'@[$DOK.DTO]}{IF: DOK.KSEFCDAT<>date(0,0,0)}{<106 'Data KSeF: '+$DOK.KSEFCDAT}{FI}{IF: DOK.D4<>date(0,0,0)}{<130 'Data otrzymania: %1'@[$DOK.D4]}{FI}{IF: DOK.D3<>date(0,0,0)}{<162 'Termin płatności: %1'@[$DOK.D3]}{FI}
{<2 'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{<2 'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}{<2 'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}{<66 'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}{<98 'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}{FI}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEADX'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEADX'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; prn1:='PRN'; prn2:='PRNF'; ~~}
{FOR: 'POZ'}
{INDEX: 'DOK'}
{PREFIX: DOK.ref()}
{DO}
{IF: DOK.DOK_REJ().M_ODD='N' | (DOK.DOK_REJ().M_ODD='T' & POZ.ODD_KS=DOK.ODD)}
{ w[1]:={? DOK.DOK_REJ().M_ODD='T' || POZ.POZ_MOD || POZ.POZ ?};
  w[2]:=POZ.KON;
  {? -POZ.STR='wn' || w[3]:=POZ.SUM; w[4]:=0 || w[4]:=POZ.SUM; w[3]:=0 ?};
  sumawn+=w[3]; sumama+=w[4];
  _v:=(BILANSP.ASYNT)+POZ.KON;
  KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR,_v);
  {? KS.first() & -(1+KS.TYP)='p'
  || {? -POZ.STR='wn' || sumawnb+=POZ.SUM || sumamab+=POZ.SUM ?}
  ?};
  KS.cntx_pop();
  w[5]:={? POZ.WAL<>0 || POZ.WAL().KOD || '' ?};
  w[6]:={? POZ.WAL<>0 || POZ.KURS || '' ?};
  w[7]:={? POZ.WAL<>0 || POZ.SUMW || '' ?};
  w[8]:=POZ.ID;
  {? +|POZ.ID>0
  || w[9]:=POZ.TID; w[10]:=$POZ.DO; w[11]:=$POZ.TP; w[12]:=$POZ.DATA_R
  || w[9]:=w[10]:=w[11]:=w[12]:=''
  ?};
  w[13]:=POZ.ODD().OD;
  w[14]:=POZ.OP; ~~}
  {SUBSTITUTE: 'LINIA0'}
{FI}
{OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft<10}{PAGE}{FI}
{IF: ~rep_export()}
{FOOTER}{FOOTER-}
{  rsep:=1;
   w[1]:='Razem:'; w[2]:=sumawn; w[3]:=sumama; w[4]:=''; ~~}
{SUBSTITUTE: 'LINIA02'}
{  prn1:=prn2:='PRNF';
   w[1]:='w tym konta pozabilansowe:'@; w[2]:=sumawnb; w[3]:=sumamab;
   w[4]:=''; ~~}
{SUBSTITUTE: 'LINIAFS'}
{~~}
{LINE: 1}

{ lmt:=charleft(); ~~}
{<{lmt/3} 'Sporządził dnia %1'@[$DOK.DR]}{>{lmt/3*2} 'Zaakceptował dnia %1'@[$DOK.DAKC]}
{LINE: 1}
{LINE: 1}
{<{lmt/3}>{lmt/3+26} 20*'.'}{<{lmt/3*2-28}>{lmt/3*2} 20*'.'}
{FI}
{MACRO-}
{COMMENT}--------------------------END MACRO WYDRUK-----------------{COMMENT-}
{IF: var_pres('TT_D_DOK')>100}
   {NOMAXP:=1;~~}
   {FOR: 'TT_D_DOK'}
   {DO}
      {IF: DOK.seek(TT_D_DOK.REF,DOK.name()) & (POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first()) }
         {SUBSTITUTE: 'START'}
         {SUBSTITUTE: 'WYDRUK'}{l_druk+=1;~~}
         {IF: TT_D_DOK.next()}{HEADER}{HEADER-}{PAGE}{page(1);~~}{FI}
      {FI}
   {OD}
   {czy_druk:=1;~~}
{ELSE}
   {SUBSTITUTE: 'INIT'}
   {SUBSTITUTE: 'WYDRUK'}
{FI}

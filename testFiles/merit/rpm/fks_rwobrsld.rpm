:!UTF-8
{TITLE:C. Zestawienie obrotów i sald}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,10,15,10,15,,,,'enable'}
{BEGIN:
   PRN1:=obj_new(@.CLASS.PRINT,13); PRN1.sl_frame(); w:=obj_new(13);
   PRN2:=obj_new(@.CLASS.PRINT,9); PRN2.sl_frame();
   opwn:=opma:=own:=oma:=kanc:=nop:=vr:=vp:=vl:=vk:=l:=lm:=0;
   opwng:=opmag:=owng:=omag:=swng:=smag:=0;
   opwnt:=opmat:=ownt:=omat:=swnt:=smat:=0;
   kan:=roz:=pokr:=p_odd:=mdc:=skrot:='';
   OKRO_F.index('ROK');
   OKRO_F.prefix(SSTALE.AO().ROK);
   {? OKRO_F.NR=0 || OKRO_F.next; datap:=OKRO_F.POCZ; datak:=OKRO_F.KON
   |? (OKRO_F.POCZ=date(0,0,0)) & (OKRO_F.NR<>0)
      || OKRO_F.prev; datap:=OKRO_F.POCZ; datak:=OKRO_F.KON
      || datap:=OKRO_F.POCZ; datak:=OKRO_F.KON
   ?}; prefroz:=1
}
{END:
   obj_del(PRN1); obj_del(w); obj_del(PRN2);
   &opwn; &opma; &own; &oma; &kanc; &nop; &vr; &vp; &vl; &vk; &lm; &mdc;
   &opwng; &opmag; &owng; &omag; &swng; &smag; &l;
   &opwnt; &opmat; &ownt; &omat; &swnt; &smat;
   &kan; &roz; &pokr; &p_odd;
   &datap; &datak; &skrot;
   DOK.use('doku'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
   POZ.use('pozy'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
   POW.use('pow_'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
   AN.use('koan__'+SSTALE.AR().KOD); &prefroz
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: rwobrsld.rpm
Utworzony:    2001-06-18 [AMA]
==================================================
Zawartosc: Zestawienie obrotow i sald.
{COMMENT-}
{COMMENT}
vr - znacznik raportu (czy są rekordy do drukowania)
vp - znacznik nowej strony
vl - znacznik ostatnio wydrukowanej linii (jaki obiekt)
vk - jeśli rekord spełnia kryterium to ma wartość 1
{COMMENT-}
{  PRN1.add("w[1]",35,'Konto'@);
   PRN1.add("w[2]",16,'Skrót Kontrahenta'@,1);
   PRN1.add("w[3]",20,'Identyfikator'@);
   PRN1.add("w[13]",10,'Data otw.'@);
   PRN1.add("w[4]",10,'Termin pł.'@);
   PRN1.add("w[5]",16,'Okres poprzedni'+{? rep_export() || ' ' || ' #' ?}+'Obroty Wn'@,1,'#');
   PRN1.add("w[6]",16,'Okres poprzedni'+{? rep_export() || ' ' || ' #' ?}+'Obroty Ma'@,1,'#');
   PRN1.add("w[7]",16,'Okres bieżący'+{? rep_export() || ' ' || ' #' ?}+'Obroty Wn'@,1,'#');
   PRN1.add("w[8]",16,'Okres bieżący'+{? rep_export() || ' ' || ' #' ?}+'Obroty Ma'@,1,'#');
   PRN1.add("w[9]",16,'Obroty narastaj.'+{? rep_export() || ' ' || ' #' ?}+'Wn'@,1,'#');
   PRN1.add("w[10]",16,'Obroty narastaj.'+{? rep_export() || ' ' || ' #' ?}+'Ma'@,1,'#');
   PRN1.add("w[11]",16,'Saldo Wn'@,1);
   PRN1.add("w[12]",16,'Saldo Ma'@,1);
   PRN2.add("w[1]",95,'');
   PRN2.add("w[2]",16,'',1);
   PRN2.add("w[3]",16,'',1);
   PRN2.add("w[4]",16,'',1);
   PRN2.add("w[5]",16,'',1);
   PRN2.add("w[6]",16,'',1);
   PRN2.add("w[7]",16,'',1);
   PRN2.add("w[8]",16,'',1);
   PRN2.add("w[9]",16,'',1);
   {! _a:=1..12 |! w[_a]:='' !};
   UNPAR.P1:=1;
   KONTO.K1:=KONTO.K2:='';
   KONTO.K1_WYM:=KONTO.K2_WYM:=0;
   KONTO.K1_SYNT:=KONTO.K2_SYNT:=0;
   KONTO.K1_RODZ:=2; KONTO.K2_RODZ:=3;
   KONTO.K1_BE:='{? UNPAR.P1=1 || 1 || 0 ?}';
   KONTO.K2_BE:='{? UNPAR.P1=2 || 1 || 0 ?}';
   KONTO.K1_AE:=KONTO.K2_AE:='';
   UNPAR.P1_BE:='';
   UNPAR.P1_AE:='{? UNPAR.P1=1 || KONTO.K2:=\'\''
               +'|? UNPAR.P1=2 || KONTO.K1:=\'\''
               +'?}';
   PAR_WYDR.win_edit('RWOBRSLD');
   WYDRUKIN.DLKON:=35;
   PAR_WYDR.ROZR:=1;
   PAR_WYDR.T_ROZ:='';
   exec('ar_rozr','!fks_roz_zazr');
   PAR_WYDR.STANDZ:=date(); ~~}
{EXIT: ~PAR_WYDR.edit()}
{  exec('prefix_maska','!fks_roz_zazr');
   WYDRUKI.MASKA:=KONTO.K1;
   {? WYDRUKI.MASKA=''
   || WYDRUKI.MASKA:=35*'?'
   || {? +WYDRUKI.MASKA<WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?}
   ?};
   {? PAR_WYDR.ROZR=1 || roz:='niezbilansowane'@
   |? PAR_WYDR.ROZR=2 || roz:='po terminie płatności - stan na %1'@[$PAR_WYDR.STANDZ]

   |? PAR_WYDR.ROZR=3 || roz:='wszystkie'@
   ?};
   pokr:=SSTALE.AO().NAZ+' '+SSTALE.AO().ROK().NAZ;
   {? SSTALE.AO().ZAM='T'
      || pokr+=' - okres zamknięty'@
      || pokr+=' - okres otwarty'@
   ?};
   {? OPERATOR.DEPT
   || p_odd:='Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
   || p_odd:='Wszystkie jednostki księgowe'@
   ?}; ~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------MACRO-LINE1------------------{COMMENT-}
{MACRO: 'LINE1'}
{IF: lineleft()<2 & lineleft()<>0}{PAGE}{FI}
{ENTIRE}
{IF: vp | (vl=1 & nop=0)}
{REP: PRN1.fbar()}
{ELIF: vl=2}
{REP: PRN2.fjbar(PRN1)}
{FI}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}{ vp:=0; vl:=1; ~~}
{ENTIRE-}
{MACRO-}
{COMMENT}------------------------------MACRO-LINE2------------------{COMMENT-}
{MACRO: 'LINE2'}
{IF: lineleft()<2 & lineleft()<>0}{PAGE}{FI}
{ENTIRE}
{IF: vp | vl=1}
{REP: PRN1.fjbar(PRN2)}
{ELIF: vl=2}
{REP: PRN2.fbar()}
{FI}
{PRN2.ini_line()}
{WHILE: PRN2.next()}
{DO}
{REP: PRN2.fline()}
{OD}{ vp:=0; vl:=2; ~~}
{ENTIRE-}
{MACRO-}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='ZESTAWIENIE OBROTÓW I SALD ROZRACHUNKÓW - STAN NA DZIEŃ: %1'@[$SSTALE.AO().KON];~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}
{LINE: 1}{SPACE: 'C'}
{<1 'PARAMETRY WYDRUKU:'@}
{<1 'Okres obrachunkowy: %1'@[pokr]}
{<1 'Waluta: %1 - %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<1 {? UNPAR.P1=1
    || 'Maska konta: %1'@[KONTO.K1 ]
    || 'Maska konta: %1'@[35+(KONTO.K2+35*'?')]
    ?}
    }
{<1{? PAR_WYDR.T_ROZ<>''
   || {? PAR_WYDR.T_ROZ='NAL' || 'Rozrachunki: %1 typu: NAL - należność'@[roz]
      |? PAR_WYDR.T_ROZ='ZOB' || 'Rozrachunki: %1 typu: ZOB - zobowiązanie'@[roz]
      |? PAR_WYDR.T_ROZ='NOD' || 'Rozrachunki: %1 typu: NOD - odsetki od należności'@[roz]
      |? PAR_WYDR.T_ROZ='ZOD' || 'Rozrachunki: %1 typu: ZOD - odsetki od zobowiązań'@[roz]
      |? PAR_WYDR.T_ROZ='RMK' || 'Rozrachunki: %1 typu: RMK - rozliczenie międzyokresowe kosztów'@[roz]
      |? PAR_WYDR.T_ROZ='RMP' || 'Rozrachunki: %1 typu: RMP - rozliczenie międzyokresowe przychodów'@[roz]
      |? PAR_WYDR.T_ROZ='N'   || 'Rozrachunki: %1 typu: N - wszystkie należności'@[roz]
      |? PAR_WYDR.T_ROZ='Z'   || 'Rozrachunki: %1 typu: Z - wszystkie zobowiązania'@[roz]
                              || 'Rozrachunki: %1 typu: INN - techniczny'@[roz]
      ?}
   || 'Rozrachunki: %1'@[roz]
   ?}
}
{<1 p_odd}
{FONT: 'R'}
{REP: PRN1.fhbar()}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}
{DO}
{REP: PRN1.h_fline()}
{OD}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ZESTAWIENIE OBROTÓW I SALD ROZRACHUNKÓW - STAN NA DZIEŃ: %1'@[$SSTALE.AO().KON];~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'R'}
{LINE: 1}{SPACE: 'C'}
{PRN1.hbar()}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}
{DO}
{REP: PRN1.h_fline()}
{OD}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: vl=1}{REP: PRN1.flbar()}{ELIF: vl=2}{REP: PRN2.flbar()}{FI}
{SUBSTITUTE: 'CHARFOOT'}
{  vp:=1; ~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{  KS.index('SYM'); KS.prefix(BILANSP.AROK);
   _v:=KONTO.K2;
   {? KONTO.K1<>'' & (KONTO.K1*'?'>1)
   || _v:=((KONTO.K1*'?')-1)+KONTO.K1
   |? KONTO.K1<>'' & KONTO.K1*'?'=0
   || _v:=KONTO.K1
   ?};
   {? OPERATOR.DEPT
      || OP.index('PRZEG_O'); OP.prefix(SSTALE.WAL,OPERATOR.DEPT,_v)
      || OP.index('PRZEG'); OP.prefix(SSTALE.WAL,_v)
   ?};
   l:=OP.first(); ~~}
{WHILE: l}
{DO}
   {  {? (PAR_WYDR.T_ROZ<>'' &
          {? PAR_WYDR.T_ROZ='N' | PAR_WYDR.T_ROZ='Z'
          || (1+PAR_WYDR.T_ROZ)=(1+OP.TYP)
          || PAR_WYDR.T_ROZ=OP.TYP
          ?}) | PAR_WYDR.T_ROZ=''
      || vk:=exec('mask','konto',OP.AN);
         {? PAR_WYDR.ROZR=2 & OP.TZ>=PAR_WYDR.STANDZ || vk:=0 ?}
      || vk:=0
      ?}; ~~}
   {IF: vk}
      {FOR: 'ZAP_OP'}
      {INDEX: 'OP'}
      {PREFIX: OP.ref}
      {DO}
         {  {? ZAP_OP.DATA<datap || opwn+=ZAP_OP.WN; opma+=ZAP_OP.MA
            |? ZAP_OP.DATA>=datap & ZAP_OP.DATA<=datak || own+=ZAP_OP.WN; oma+=ZAP_OP.MA
            ?}; ~~}
      {OD}
      {IF: (((PAR_WYDR.ROZR=1 | PAR_WYDR.ROZR=2) & F.SWn(opwn+own,opma+oma)
            <>F.SMa(opwn+own,opma+oma)) | (PAR_WYDR.ROZR=3 & ((opwn+own)<>0 | (opma+oma)<>0)))}
         {  vr:=1;
            w[1]:=OP.AN;
            w[3]:=OP.SYM;
            w[4]:=$OP.TZ;
            w[13]:=$OP.DO;
            w[2]:=OP.KH().SKR;
            w[5]:=form(opwn,,2,' ,');
            w[6]:=form(opma,,2,' ,');
            w[7]:=form(own,,2,' ,');
            w[8]:=form(oma,,2,' ,');
            w[9]:=form(opwn+own,,2,' ,');
            w[10]:=form(opma+oma,,2,' ,');
            w[11]:=form(F.SWn(opwn+own,opma+oma),,2,' ,');
            w[12]:=form(F.SMa(opwn+own,opma+oma),,2,' ,');
            opwng+=opwn; opmag+=opma; owng+=own; omag+=oma;
            swng+=F.SWn(opwn+own,opma+oma);
            smag+=F.SMa(opwn+own,opma+oma);
            opwnt+=opwn; opmat+=opma;
            ownt+=own; omat+=oma;
            swnt+=F.SWn(opwn+own,opma+oma);
            smat+=F.SMa(opwn+own,opma+oma); ~~}
         {SUBSTITUTE: 'LINE1'}
      {  kan:=OP.AN;
         skrot:=OP.KH().SKR;
         nop+=1; ~~}
      {FI}
      {  opwn:=opma:=own:=oma:=0; ~~}
   {FI}
   {  OP.cntx_psh();
      {? OP.next()
      || _v:=1;
         {! |?
            {? PAR_WYDR.ROZR=2 & OP.TZ<PAR_WYDR.STANDZ
            || {? OP.AN<>kan || kanc:=1 || kanc:=0 ?}; _v:=0
            |? PAR_WYDR.ROZR=1 & (F.RObr(OP.ref,datak); F.rwn<>F.rma)
            || {? OP.AN<>kan || kanc:=1 || kanc:=0 ?}; _v:=0
            |? PAR_WYDR.ROZR=3
            || {? OP.AN<>kan || kanc:=1 || kanc:=0 ?}; _v:=0
            ?};
            _v & OP.next()
         !}
      || kanc:=1
      ?};
      _ref:=OP.ref();
      OP.cntx_pop(); {? _ref<>OP.ref() || OP.seek(_ref); OP.prev() ?}; ~~}
   {IF: kanc & nop>1 & ~rep_export()}
      {  w[1]:='Razem: '+kan+'-'+skrot;
         w[2]:=form(opwng,,2,' ,');
         w[3]:=form(opmag,,2,' ,');
         w[4]:=form(owng,,2,' ,');
         w[5]:=form(omag,,2,' ,');
         w[6]:=form(opwng+owng,,2,' ,');
         w[7]:=form(opmag+omag,,2,' ,');
         w[8]:=form(swng,,2,' ,');
         w[9]:=form(smag,,2,' ,'); ~~}
      {SUBSTITUTE: 'LINE2'}
   {FI}
   {  {? kanc
      || nop:=0;
         opwng:=opmag:=owng:=omag:=swng:=smag:=0
      ?}; ~~}
   {  l:=OP.next(); ~~}
{OD}
{COMMENT}------------------------------FOOTER_LAST------------------{COMMENT-}
{IF: vr & ~rep_export()}
{  w[1]:='Suma';
   w[2]:=form(opwnt,,2,' ,');
   w[3]:=form(opmat,,2,' ,');
   w[4]:=form(ownt,,2,' ,');
   w[5]:=form(omat,,2,' ,');
   w[6]:=form(opwnt+ownt,,2,' ,');
   w[7]:=form(opmat+omat,,2,' ,');
   w[8]:=form(swnt,,2,' ,');
   w[9]:=form(smat,,2,' ,'); ~~}
{IF: lineleft()<2  & lineleft()<>0}{PAGE}{FI}
{ENTIRE}
{IF: vp | vl=1}
{REP: PRN1.fjbar(PRN2)}
{ELIF: vl=2}
{REP: PRN2.fbar()}
{FI}
{PRN2.ini_line()}
{WHILE: PRN2.next()}
{DO}
{REP: PRN2.fline()}
{OD}{ vp:=0; vl:=2; ~~}
{REP: PRN2.flbar()}
{ELSE}
{REP: PRN1.flbar()}
{FI}
{IF: ~rep_export()}
{<1>{PRN1.width()} 'KONIEC WYDRUKU'@}
{FI}
{ENTIRE-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}

:!UTF-8
{TITLE: Zestawienie dla zapytania SQL}
{PRINTER: exec('prt','param_wydr','h',{? BPMN.SYM_DOM='PPL' | BPMN.SYM_DOM='PKD' || KST || FINFO ?}),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,20); PRN.s_frame(); w:=obj_new(30);
   PRNS:=obj_new(@.CLASS.PRINT,20); PRNS.s_frame();
   s:=obj_new(TAB_SQLR.fld_num()-1);
   indp:=TAB_SQLP.ndx_tmp(,1,'SQL',,,'NR_KOL',,);
   indps:=TAB_SQLP.ndx_tmp(,1,'SQL',,,'SUM',,,'NR_KOL',,);
   v_lkmax:=TAB_SQLR.fld_num()-2;
   v_lk:=1; v_wid:=0;
   v_sk:=i:=v_lkn:=v_par:=v_sum:=0;
   color:=prn1:=prn2:=v_vpar:='';
   lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
   obj_del(PRN); obj_del(PRNS); obj_del(w); obj_del(s);
   TAB_SQLP.ndx_drop(&indp); TAB_SQLP.ndx_drop(&indps);
   &v_lkmax; &v_lk; &v_wid; &v_lkn; &v_par; &v_sk; &i;
   &color; &prn1; &prn2; &v_vpar; &v_sum;
   &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
v_lkmax - maksymalna liczba kolumn (pola tabeli - 1 pole techniczne)
v_lk - liczba kolumn na stronie (bez kolumny Lp.)
v_lkn - liczba kolumn narastajaco
v_sk - szerokosc kolumn na stronie
v_wid - szerokość strony (w znakach)
i - licznik
color, prn1, lm, ll, vp, lmt, rsep - zmienne pomocnicze do wydrukow graficzn.
{COMMENT-}
{  PAR_WYDR.TITLE:=TAB_SQL.NAZ; prn1:='PRN'; prn2:='PRNS';
   {! _i:=1..TAB_SQLR.fld_num()-1 |! s[_i]:='' !};
   TAB_SQLP.index(indps); TAB_SQLP.prefix(TAB_SQL.FILENAME,'T');
   {? TAB_SQLP.first()
   || {! |?
         {? TAB_SQLP.NR_KOL<=TAB_SQLR.fld_num()-1
            & type_of(($('TAB_SQLR.'+($('TAB_SQLR.fld_acr('
            +$TAB_SQLP.NR_KOL+')'))()))())=1
         || v_sum:=1; s[TAB_SQLP.NR_KOL]:=0
         ?};
         TAB_SQLP.next()
      !}
   ?}; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------MACRO-COL--------------------{COMMENT-}
{MACRO: 'COL'}
{HEADER}{HEADER-}
{  obj_del(PRN); PRN:=obj_new(@.CLASS.PRINT,30); PRN.s_frame();
   obj_del(PRNS); PRNS:=obj_new(@.CLASS.PRINT,30); PRNS.s_frame();
   i:=2; v_lk:=v_par:=0; v_sk:=10;
   _v:=+$TAB_SQLR.size(); {? _v<4 || _v:=4 ?};
   PRN.add("w[1]",_v,'LP.',1);
   PRNS.add("w[1]",_v,,1);
   {! |?
      TAB_SQLP.index(indp);
      ($('TAB_SQLP.prefix(\''+$TAB_SQL.FILENAME+'\','+$(v_lkn+v_lk+2)+')'))();
      {? TAB_SQLP.first() & TAB_SQLP.SZER<>0
      || _v:=TAB_SQLP.SZER
      || _v:=exec('fld_len','#sql','TAB_SQLR',v_lkn+v_lk+2)
      ?};
      {? _v>40 || _v:=40 ?};
      {? v_wid-v_sk-3>=_v
      || v_lk+=1;
         _v_fld:=($('TAB_SQLR.'+TAB_SQLR.fld_acr(v_lkn+v_lk+1)))();
         ($('TAB_SQLP.prefix(\''+$TAB_SQL.FILENAME+'\','+$(v_lkn+v_lk+1)+')'))();
         _hid:=TAB_SQLP.first();
         {? _hid=0 | (TAB_SQLP.HID='' | ($TAB_SQLP.HID)())
         ||
            {? type_of(_v_fld)=1
            || ($('TAB_SQLP.prefix(\''+$TAB_SQL.FILENAME+'\','+$(v_lkn+v_lk+1)+')'))();
               {? TAB_SQLP.first()
               || _v_p:=$TAB_SQLP.PREC
               || {? ($('MS.fld_real(TAB_SQLR,\''+TAB_SQLR.fld_acr(v_lkn+v_lk+1)
                     +'\')'))()
                  || _v_p:='2'
                  || _v_p:=''
                  ?}
               ?};
               {? _v_p='0' || _v_p:='$0,,,\' ,\'' || _v_p:=',,'+_v_p+',\' ,\'' ?};
               PRN.add($('form(w['+$i+']'+_v_p+')'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1),1);
               TAB_SQLP.index(indps); TAB_SQLP.prefix(TAB_SQL.FILENAME,'T');
               {? TAB_SQLP.first & TAB_SQLP.find_key(v_lkn+v_lk+1)
               || PRNS.add($('form(w['+$i+']'+_v_p+')'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1),1)
               || PRNS.add($('w['+$i+']'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1))
               ?}
            |? type_of(_v_fld)=4
            || PRN.add($('$w['+$i+']'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1));
               PRNS.add($('w['+$i+']'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1))
            |? type_of(_v_fld)=5
            || PRN.add($('(w['+$i+']$3)'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1));
               PRNS.add($('w['+$i+']'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1))
            || PRN.add($('w['+$i+']'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1));
               PRNS.add($('w['+$i+']'),_v,TAB_SQLR.fld_name(v_lkn+v_lk+1))
            ?};
            v_sk+=_v+3
         ?}
      || v_sk:=v_wid
      ?};
      i+=1;
      v_lk+v_lkn<v_lkmax & v_sk<v_wid
   !};
   v_lkn+=v_lk;
   {! i:=1..v_lk+1 |! w[i]:='' !}; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{  i:=1; ~~}
{FOR: TAB_SQLK}
{PREFIX: TAB_SQL.FILENAME,TAB_SQL.FILENAME}
{DO}
{IF: TAB_SQLK.RODZ<>'F' & TAB_SQLK.RODZ<>'Q'}
{IF: ~v_par}{<{lmt+1} 'PARAMETRY WYDRUKU:'@}{v_par:=1; ~~}{FI}
{  v_vpar:=($('PAR_SQL.PAR'+$TAB_SQLK.NR+TAB_SQLK.RODZ))();
   {? type_of(v_vpar)<>2 || v_vpar:=$v_vpar ?}; ~~}
{<{lmt+1} TAB_SQLK.NAZ+': '+v_vpar}
{FI}
{OD}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{MACRO-}
{ v_wid:=charleft(); ~~}
{WHILE: v_lkn<v_lkmax}
{DO}
{SUBSTITUTE: 'COL'}
{FOR: TAB_SQLR}
{DO}
   {IF: TAB_SQLR.F0000001<>-1}
      {  w[1]:=$TAB_SQLR.LP;
         {! i:=2..v_lk+1 |!
            ($('w['+$i+']:=TAB_SQLR.'+TAB_SQLR.fld_acr(v_lkn-v_lk+i)))();
            {? type_of(w[i])=2 & w[i]*(%10) ||  w[i]:=gsub(w[i],(%10),' ') ?};
            {? type_of(s[i+v_lkn-v_lk])=1 || s[i+v_lkn-v_lk]+=w[i] ?}
         !}; ~~}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
{OD}
{IF: TAB_SQLP.index(indps); TAB_SQLP.prefix(TAB_SQL.FILENAME,'T'); TAB_SQLP.first() & v_sum}
{  w[1]:='Suma';
   {! _i:=2..v_lk+1 |!
      {? type_of(s[_i+v_lkn-v_lk])=1 || w[_i]:=s[_i+v_lkn-v_lk] || w[_i]:='' ?}
   !};
   rsep:=1; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA02'}
{  rsep:=PAR_WYDR.RSEP; ~~}
{FI}
{IF: lineleft()}{PAGE}{FI}
{OD}

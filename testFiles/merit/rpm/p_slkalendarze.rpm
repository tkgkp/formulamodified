:!UTF-8
{TITLE:Kalendarze}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='';
   VAR_DEL.delete('par','col','lmt','tresc','week','opis','kolor','kal','week_short');
   tresc:='sl_kalen';
   KAL_NAZW.cntx_psh();
   KAL_ROK.cntx_psh();
   KAL_DEF.cntx_psh();
   kal:=exec('kalendarze','!pkd_zes_orsl');
   col:=obj_new(37);
   lmt:=0
}
{END:
   KAL_DEF.cntx_pop;
   KAL_ROK.cntx_pop();
   KAL_NAZW.cntx_pop();
   VAR_DEL.delete('par','col','lmt','tresc','week','opis','kolor','kal','week_short')
}
{COMMENT}OLD: sl_kalen.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~kal.size()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}{
   par[2]:=KAL_ROK.ROK;
   PAR_WYDR.TITLE:='Kalendarz '@+KAL_DEF.ROK().NAZWA().NAZWA+' '+$par[2];
   par[5]:=par[6]:=0;
   par[3]:=1;  ~~
}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
┌{13*'─'}{37*('┬'+(3*'─'))}┐
│ Miesiąc {>15'│'}{WHILE: (par[6]+=1)<=37}{DO} {
  _n:=par[6]%*7;
  {? _n=0 || 'Nd'@
  |? _n=1 || 'Pn'@
  |? _n=2 || 'Wt'@
  |? _n=3 || 'Śr'@
  |? _n=4 || 'Cz'@
  |? _n=5 || 'Pt'@
  || 'So'@
  ?}
}│{OD}
├{13*'─'}{37*('┼'+(3*'─'))}┤
{WHILE: (par[5]+=1)<=12}{DO}
│ {_m:=date(,par[5],1)$8;_m:=(_m*' ')+_m;_m}{>15'│'}{
   par[4]:=date(par[2],par[5],1)~4;
   par[7]:=date(par[2],par[5],0)~3;
   par[6]:=0;
   ~~
}{WHILE: (par[6]+=1)<=37}{DO}{
   par[8]:=par[6]-par[4]+1;
   col[par[6]]:='0:0:0,255:255:255';
   {? par[8]<=0 || par[8]:=''
   |? par[7]<par[8] || par[8]:=''
   || _dm:=date(par[2],par[5],par[8]);
      {? par[3] & KAL_DEF.find_key(_dm)
      || {? KAL_DEF.TYP<>'R' ||
            col[par[6]]:='0:0:0,'+color
         || par[10].find_key('R',KAL_DEF.POCZATEK$3,KAL_DEF.KONIEC$3);
            col[par[6]]:=kolor(par[10][1])
         ?}
      ?}
   ?};
   ~~
}{COLOR: col[par[6]]}{>{18+((par[6]-1)*4)}par[8]
}{>{19+((par[6]-1)*4)}'│'}{OD}{COLOR: '0:0:0,255:255:255'}
{IF: par[5]<12}
{  par[6]:=0; ~~ }
├{13*'─'}{WHILE: (par[6]+=1)<=37}{DO}{
}┼{COLOR: col[par[6]]}{3*'─'}{OD}┤{COLOR: '0:0:0,255:255:255'}
{FI}{OD}
{  par[6]:=0; ~~ }
└{13*'─'}{WHILE: (par[6]+=1)<=37}{DO}{
}┴{COLOR: col[par[6]]}{3*'─'}{OD}┘{COLOR: '0:0:0,255:255:255'}

   Legenda:
{FOR: par[10]}
{DO}
   {IF: par[10].C='R'}
      {<6}{COLOR: kolor(par[10][1])}{'   '}{COLOR: '0:0:0,255:255:255'}{>20$par[10].A}{<21' - '}{>30$par[10].B}
   {FI}
{OD}
{<6}{COLOR: '0:0:0,' +color}{'   '}{COLOR: '0:0:0,255:255:255'}{<13'Dni wolne'@}
{PAGE}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{FOR: kal}
{DO}
   {IF: KAL_ROK.seek(kal.ROK,,1)}
      {  exec('init','edit_kal');
         KAL_DEF.index('KAL_DEF');
         KAL_DEF.prefix(KAL_ROK.ref);
         KAL_DEF.first();
         {? var_pres('par[10]')>=100 || obj_del(par[10]) ?};
         par[10]:=sql_exec('!sql_info_kal',KAL_ROK.ROK,KAL_ROK.NAZWA().NAZWA);
         {? par[10].first()
         || _ii:=1;
            {!
            |? {? par[10][4]='R'
               || par[10][1]:=_ii;
                  par[10].put();
                  _ii+=1
               ?};
               par[10].next()
            !}
         ?};
         par[10].index(par[10].ndx_tmp('',,'C',,,'A',,,'B',,)); ~~
      }
{SUBSTITUTE: tresc}
    {FI}
{OD}

:!UTF-8
{TITLE:3. Przewodnik produkcyjny}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  ZGH.cntx_psh;
  ZGP.cntx_psh();
  ZL.cntx_psh();
  exec('tktl_cntx_psh','tech_common');
  PRN:= obj_new(@.CLASS.PRINT, 10);
  PRN.s_frame();
  TABx:=tab_tmp(2,
     'PZ','STRING[1]','Wyr/polf',
     'ZLEC','INTEGER','Ref.do zlec',
     'NORMA','REAL','Norma czasowa',
     'NAZWA','STRING[61]','Nazwa',
     'ILOSC','REAL','Il.do wyk.',
     'OPER','STRING[40]','Operacja',
     'ZLOZ','STRING[40]','Op.złożone',
     'NARZ','STRING[210]','Narzędzia',
     'STAN','STRING[10]','Stanowisko',
     'NAS','STRING[50]','Nast',
     'POP','STRING[50]','Pop'
  );
  prn1:='PRN';
  waslbar:=0;
  dalej:=0;
  opcja:=0;
  pop:='';
  nas:='';
  color:='';
  first:=1;
  lm:=ll:=vp:=lmt:=rsep:=pierwszy:=0
}
{END:
  ZGH.cntx_pop;
  ZGP.cntx_pop();
  ZL.cntx_pop;
  exec('tktl_cntx_pop','tech_common');
  obj_del(PRN);
  obj_del(TABx);
  &dalej;
  &first;
  &prn1;
  &pop;
  &nas;
  &opcja;
  &color;
  &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
  Wydruk przewodnika produkcyjnego.
   [MK] - 2002/12/12 - [8.40]
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Definicje -> Przewodniki -> Drukuj
{COMMENT-}
{COMMENT}--Definicja wiersza wydruku przewodnika produkcyjnego.--------------------{COMMENT-}
{
PRN.add("operacja",41,'Operacja'@,,'#');
PRN.add("'Data'@",7,'Data'@);
PRN.add("'Nr prac'@",7,'Nr prac'@);
PRN.add("'Ilość'@",7,'Ilość'@);
PRN.add("'Podpis'@",7,'Podpis'@);
PRN.add("'Data'@",7,'Data'@);
PRN.add("'Nr prac'@",7,'Nr prac'@);
PRN.add("'Ilość'@",7,'Ilość'@);
PRN.add("'Podpis'@",7,'Podpis'@);
PRN.add("'SUMA'@",7,'SUMA'@);
PAR_WYDR.TITLE:='';~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{<1 lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'Zlecenie nr: %1'@[ZL.SYM]}{<{lmt+50} 'Przewodnik %1'@[ZGH.NRPRZ]}
{IF: ZGH.GKTL<>null()}
{<{lmt+1} 'Grupa technologii:  '+ ZGH.GKTL().GR+' - '+ZGH.GKTL().OPIS }
{FI}
{<{lmt+1} 'Nazwa wyrobu: %1'@[ZL.KTM().N]}{<{lmt+105} 'Data uruchomienia: %1'@[form(ZL.OD)]}
{<{lmt+1} 'Ilość uruchomiona: %1'@[form(ZGH.ILNPRZ)]}{<{lmt+105} 'Data plan.zakoń: %1'@[form(ZL.DTR)]}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------MACRO-----------------------{COMMENT-}
{MACRO: 'utwtab'}
{  ZGP.clear();
   {? VAR.A_ZLEC().NRNZL=0
   || ZGP.index('NRPP')
   || ZGP.index('PNRPP')
   ?};
   ZGP.prefix(ZGH.ref());
   {? ZGP.first()
   || {!
      |?
         _rktl:={? ZGP.NRZLP().ZLEC().TYP().TECH='T' || ZGP.NRZLP().ZLEC().RTKTL || ZGP.NRZLP().ZLEC().RKTL ?};
         _tktl:=exec('FindAndGet','#table',TKTL,_rktl,,"ref()",null());
         _msk:=ref_name(_tktl)+3;
         exec('tktl_use','tech_common',_msk);

         TABx.blank();
         TABx.PZ:={? ZGP.NRZLP().ZLEC().LEVEL=0 |
                     (ZGP.NRZLP().ZLEC().LEVEL=1 & ZGP.NRZLP().ZLEC().NR=1)
                  || 'Z'
                  |?  ZGP.NRZLP().ZLEC().LEVEL=1
                  || 'P'
                  || 'D'
                  ?};
         TABx.NORMA:=ZGP.NTIME;
         TABx.NAZWA:=ZGP.NRZLP().ZLEC().KTM().N+'-'+ZGP.NRZLP().ZLEC().KTM().KTM;
         TABx.ILOSC:=ZGP.NRZLP().ILNPRZ;
         TABx.OPER:=form(ZGP.NRP,-2)+'. '+ZGP.OPIS;
         TABx.STAN:=ZGP.PLACE().KOD;
         TABx.ZLEC:=#ZGP.NRZLP().ZLEC;
         TACTTLS.index('KNROP');
         TACTTLS.prefix(_tktl,ZGP.TOPER);
         {? TACTTLS.first()
         || {!
            |? TABx.NARZ+=TACTTLS.M().N;
               TACTTLS.next()
            !}
         || TABx.NARZ:=''
         ?};
         VAR.STRING:='';
         {? ZGP.TOPER().NRNOP<>0
         || TOPER.index('UNROP');
            TOPER.prefix(ZGP.TOPER().NRNOP);
            {? TOPER.first()
            || _nrop:=ZGP.TOPER().NRNOP;
               {!
               |? TOPER.prefix(_nrop);
                  {? TOPER.first()
                  || VAR.STRING:=form(TOPER.NROP)+'.'+TOPER.NA+'/'+VAR.STRING;
                     _nrop:=TOPER.NRNOP;
                     TOPER.NRNOP<>0
                  || 0
                  ?}
               !}
            ?}
         ?};
         {? +VAR.STRING>35
         || {!
            |? _str:=VAR.STRING;
               _nr:=0;
               {!
               |? _nr+=_str*'/';
                  _str:=_str*'/'-_str;
                  _str*'/'>0
               !};
               VAR.STRING:=_nr+VAR.STRING;
               VAR.STRING:=VAR.STRING-1;
               _str:=VAR.STRING;
               +VAR.STRING>35
            !};
            VAR.STRING+='/...'
         ?};
         ZGP.cntx_psh();
         NASZGP.cntx_psh();
         NASZGP.index('OPNAST');
         NASZGP.prefix(ZGP.ref());
         {? NASZGP.first()
         || {!
            |? TABx.NAS+=NASZGP.SCIEZKA+',';
               NASZGP.next()
            !}
         ?};
         NASZGP.cntx_pop();
         NASZGP.index('NASTOP');
         NASZGP.prefix(ZGP.ref());
         {? NASZGP.first()
         || {!
            |? TABx.POP+=$NASZGP.OPER().NRP+',';
               NASZGP.next()
            !}
         ?};
         TABx.NAS:=TABx.NAS-1;
         TABx.POP:=TABx.POP-1;
         ZGP.cntx_pop();
         TABx.ZLOZ:=VAR.STRING;
         TABx.add();
         ZGP.next()
      !}
   ?};
   ~~
}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{SUBSTITUTE: 'utwtab'}
{FOR: TABx}
{DO}
{FIRST}

{<{lmt+1} {? TABx.PZ='D' || 'Detal: %1'@[TABx.NAZWA] |? TABx.PZ='P' || 'Podzespół: %1'@[TABx.NAZWA] || 'Zespół: %1'@[TABx.NAZWA] ?}+', Ilość do wykonania '+ form(TABx.ILOSC)}
{pierwszy:=1;zlec:=TABx.ZLEC;~~}
{FIRST-}
{narz:='Narzędzie: %1'@[form(TABx.NARZ)];~~}
{operacja:=form(TABx.ZLOZ)+' '*40-((+TABx.ZLOZ)%*41)+'#'+form(TABx.OPER)+form(' ',40-((+TABx.OPER)%*41))+'#'+'Następniki: %1'@[form(TABx.NAS)+form(' ',28-((+TABx.NAS)%*41))]+'#'+'Poprzedniki: %1'@[form(TABx.POP)+form(' ',27-((+TABx.POP)%*41))]+'#'+'Norma: %1'@[form(TABx.NORMA,15,exec('get','#params',500604,1))]+'#'+narz+form(' ',40-((+narz)%*41))+'#'+'Stanowisko: %1'@[form(TABx.STAN)];~~}
{IF: lineleft > 10}
  {IF: pierwszy}
     {SUBSTITUTE: 'LINIA01'}
     {pierwszy:=0;~~}
  {ELSE}
     {SUBSTITUTE: 'LINIA0'}
  {FI}
{ELSE}
  {SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8G'}{SPACE:'B'}
  {SUBSTITUTE: 'LINIA01'}
{FI}
{TOTAL: TABx.ZLEC}
{SUBSTITUTE: 'LINIAF'}

{IF: zlec<>TABx.ZLEC}
{IF: lineleft<10}
{PAGE}
{FI}
{<{lmt+1} {? TABx.PZ='D' || 'Detal: %1'@[TABx.NAZWA] |? TABx.PZ='P' || 'Podzespół: %1'@[TABx.NAZWA] || 'Zespół: %1'@[TABx.NAZWA] ?}+', Ilość do wykonania %1'@[form(TABx.ILOSC)]}
{pierwszy:=1;~~}
{zlec:=TABx.ZLEC;~~}
{FI}
{OD}

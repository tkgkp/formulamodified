:!UTF-8
{TITLE:C. Zbiorcze zestawienie pobrań i zwrotów surowców do zleceń}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   ZL.cntx_psh();
   dalej:=0;
   {? var_pres('PRN')>0 || obj_del(PRN) ?};
   PRN:=obj_new(@.CLASS.PRINT,16);
   PRN.s_frame();
   {? var_pres('TABx')>0 || obj_del(TABx) ?};
   TABx:=tab_tmp(3,'WYD','STRING[8]','Ref.do wydz.',
                  'ZLREF','INTEGER','Ref.do zlec',
                  'NRR','INTEGER','Nr rubryki',
                  'WART','REAL','Wartosc',
                  'SYM','STRING[20]','Sym do zlec');
   akczl:=ND.ndx_tmp(,1,'Z',,0,'ZL',,0);
   prn1:='PRN';
   tryb:=rep_export();
   sym:='';
   wyd:='';
   color:='';rodz:='N';
   lm:=ll:=vp:=lmt:=rsep:=0;
   zlec:='O'; okrkw:='O';
   ilo:=6;wyd:='N';odd:='T';
   wartosc:=0
}
{END:
   ZL.cntx_pop();
   &dalej;
   obj_del(PRN);
   obj_del(TABx);
   ND.ndx_drop(akczl);
   &prn1;
   &color;
   &sym;
   &rodz;
   {? var_pres('tabok')>=0 ||  &tabok ?};
   {? var_pres('druk')>=0 || &druk ?};
   &lm; &ll; &vp; &lmt; &rsep;
   &ilo;&wyd;&odd;&zlec;&okrkw;
   &wartosc;&tryb;
   VAR_DEL.delete('ZLST')
}
{COMMENT}
  Wydruk wartosci surowcow pobranych i zwroconych do zlecen
  [TS] - 2002/05/24 - [7.60]
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{ _wydr_naz:='zlec_003';
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.ZLECOZ:=WYDRUKIL.SORTUJ;
     WYDRUK.OKRKW:=WYDRUKIL.FAKUM;
     WYDRUK.ILO:=WYDRUKIL.TYP_ZEST;
     WYDRUK.WYD:=WYDRUKIL.FAK;
     WYDRUK.ODP:=WYDRUKIL.RODZFAK;
     WYDRUK.RODZAJ:=WYDRUKIL.SR
  ?};
  WYDRUK.win_edit(); WYDRUK.win_edit('ZLEC_003');~~}
{IF: WYDRUK.edit()}
{ WYDRUKIL.SORTUJ:=WYDRUK.ZLECOZ;
  WYDRUKIL.FAKUM:=WYDRUK.OKRKW;
  WYDRUKIL.TYP_ZEST:=WYDRUK.ILO;
  WYDRUKIL.FAK:=WYDRUK.WYD;
  WYDRUKIL.RODZFAK:=WYDRUK.ODP;
  WYDRUKIL.SR:=WYDRUK.RODZAJ;
  WYDRUKIL.put(1);
  zlec:=WYDRUK.ZLECOZ;
  okrkw:=WYDRUK.OKRKW;
  ilo:=WYDRUK.ILO;
  wyd:=WYDRUK.WYD;
  odd:=WYDRUK.ODP;
  rodz:=WYDRUK.RODZAJ;
  {? ~tryb
  || {? wyd='T' & odd='T'
     || _o:=int((charleft()-39)/12)
     |? (wyd='T' & odd='N') | (wyd='N' & odd='T')
     || _o:=int((charleft()-27)/12)
     || _o:=int((charleft()-15)/12)
     ?}
  || _o:=12
  ?};
  {? ilo>_o
  || FUN.info('Podana liczba okresów nie mieści się na wydruku.\nIlość została zmniejszona do %1'@[form(_o)]);
     ilo:=_o
  ?};
  tabok:=obj_new(ilo);
  druk:=obj_new(ilo+1);
  {! _i:=1..ilo+1
  |! druk[_i]:=0
  !};
  {! _i:=1..ilo
  |! tabok[_i]:=obj_new(2);
     tabok[_i][1]:='';
     tabok[_i][2]:=date(0,0,0)
  !};
  {? okrkw='O'
  || OKR.cntx_psh();
     OKR.clear();
     OKR.index('OKR');
     OKR.find_key(REF.FIRMA,ST.AR,ST.AM);
     _i:=1;
     {!|?
         tabok[_i][1]:=(OKR.NAZ-5)+'/'+(12-(+(OKR.NAZ-5)))*' '+$OKR.ROK;
         tabok[_i][2]:=date(OKR.ROK,OKR.MC,0);
         _i+=1;
         _i<=ilo & OKR.prev
     !};
     pocz:=date(OKR.ROK,OKR.MC,1);
     OKR.cntx_pop()
  ||
     OKR.cntx_psh();
     OKR.clear();
     OKR.index('OKR');
     OKR.find_key(REF.FIRMA,ST.AR,ST.AM);
     _msc_start:=0;
     {? OKR.MC<4
     || _zm:=1;
        _msc_start:=1
     |? OKR.MC<7
     || _zm:=2;
        _msc_start:=4
     |? OKR.MC<10
     || _zm:=3;
        _msc_start:=7
     || _zm:=4;
        _msc_start:=10
     ?};
     _ok:=OKR.find_key(REF.FIRMA,OKR.ROK,_msc_start);
     {! _i:=1..ilo
     |? _ok>0
     |! tabok[_i][1]:=$_zm+'/'+$OKR.ROK;
        tabok[_i][2]:=
           {? _zm=1 || date(OKR.ROK,3,31)
           |? _zm=2 || date(OKR.ROK,6,30)
           |? _zm=3 || date(OKR.ROK,9,30)
           || date(OKR.ROK,12,31)
           ?};
        _zm-=1;
        {? _zm=0
        || _zm:=4
        ?};
        {! _it:=1..3
        |? _ok:=OKR.prev();
           _ok
        !}
     !};
     _zm+=1;
     pocz:={? _zm=1 || date(OKR.ROK,1,1)
           |? _zm=2 || date(OKR.ROK,4,1)
           |? _zm=3 || date(OKR.ROK,7,1)
           || date(OKR.ROK,10,1)
           ?};
     OKR.cntx_pop()
  ?};
  ~~}
{ELSE}
{EXIT}
{FI}
{COMMENT}
Co ma robic wydruk:
===================
- mozliwosc wyboru wszystkich zlecen lub tylko otwartych
  lub tylko zamknietych.
- wydruk od poczatku zlecenia, dla aktywnego okresu, tygodnia(?), dnia
- jezeli od poczatku, to zobaczyc najdawniej otwarte zlecenie z przetwarzanych
  i iterowac po okresach wrzucajac do tabeli tymczasowej dane dla
  kazdego zlecenia - potem drukowac
- jezeli "od poczatku", to drukowac od n-tego miesiaca (okresu) wstecz
  w kolumnach co miesiac do biezacego miesiaca, a dla miesiecy
  dawniejszych niz n sume w pierwszej kolumnie.
- sumowana wartosciowo robocizna akordowa dla kazdego zlecenia ogolem
  (czy robic opcjonalnie jakies rozbicie wartosci surowcow np.
  wg kodow? - ale tak ma wydruk nr 2).
- Czy sumowac kazda kolumne? Chyba nie calkiem sens, chyba ze
  beda drukowane wszystkie zlecenia wystepujace w danym okresie,
  bo inaczej suma kolumny nie bedzie jednoznaczna z faktyczna suma
  akordu w danym okresie (albo trzeba o tym poprostu pamietac).
- obliczenia odbywaja sie z zawartosci tabeli ZLST
- dac rozbicie na wydzialy - wtedy musi zbierac po dok. magazynowych?

- narazie wariant razem z wszystkich okresow.
- Uwzglednic raporty brakow surowca
{COMMENT-}
{COMMENT}-Definicja wiersza wydruku -{COMMENT-}
{PRN.add("sym",20,'Zlecenie'@,0);
{? wyd='T' || PRN.add("wyd",5,'Wydział'@,0) ?};
{? odd='T' || PRN.add("form(druk[1],,2)",7,'Pobrania wcześn.'@,1) ?};
{! _i:=1..ilo |!
   PRN.add($('form(druk['+$(ilo+2-_i)+'],,2)'),12,tabok[ilo+1-_i][1],1)
!};
PAR_WYDR.TITLE:='ZBIORCZE ZESTAWIENIE POBRAŃ I ZWROTÓW SUROWCÓW DO ZLECEŃ'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} {? zlec='O' || 'Zlecenia otwarte'@ |? zlec='Z' || 'Zlecenia zamknięte'@ || 'Zlecenia wszystkie'@ ?}}
{<{lmt+1} {? okrkw='O' || 'Podział na okresy'@ || 'Podział na kwartały'@ ?}}
{<{lmt+1} {? okrkw='O' || 'Szczegółowe zestawienie dla %1 okresów'@[form(ilo)] || 'Szczegółowe zestawienie dla %1 kwartałów'@[form(ilo)] ?}}
{<{lmt+1} {? wyd='T' || 'Rozbicie na wydziały'@ || 'Brak rozbicia na wydziały'@ ?} }
{<{lmt+1} {? odd='T' || 'Zestawienie od początku zlecenia'@ || '' ?} }
{FONT: 'T10B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{MACRO: 'utwzl'}
{
   {? odd='T'
   || ZL.index('DATAO');
      {? ZL.first()
      || _data:=ZL.OD
      ?}
   || _data:=pocz
   ?};
   OKR.clear();
   OKR.index('OKR');
   OKR.find_key(REF.FIRMA,pocz~1,pocz~2);
   _ok:=1;
   OKR.prev();
   {!
   |? 'ustala nr okresu';
      OKR.next();
      {? date(OKR.ROK,OKR.MC,1)<pocz
      || _i:=0
      || _i:=1;_ok:=1;
         {? okrkw='O'
         || {!
            |? {? tabok[_i][2]=date(OKR.ROK,OKR.MC,0)
               || _ok:=0
               || _i+=1
               ?};
               _i<=ilo & _ok
            !}
         || {!
            |? {? tabok[ilo+1-_i][2]>=date(OKR.ROK,OKR.MC,0)
               || _ok:=0
               || _i+=1
               ?};
               _i<=ilo & _ok
            !};
            _i:=ilo+1-_i
         ?}
      ?};
      ZL.index('STAN');
      {? zlec= 'O' || ZL.prefix('N','O')
      |? zlec= 'Z' || ZL.prefix('N','Z')
      || ZL.prefix()
      ?};
      {? ZL.first()
      || {!
         |? VAR_DEL.delete('ZLST');
            ZLST:=exec('zlst','zl_common',ZL.ref());
            _zlstndx:=ZLST.ndx_tmp(,,'ZL',,,'OKRES',,);
            ZLST.index(_zlstndx);
            ZLST.prefix($ZL.ref,$OKR.ref);
            {? ZLST.first()
            || {!
               |? wartosc+=ZLST.WAR_P-ZLST.WAR_Z;
                  {? ZLST.IL_P<>0 || _cena:=ZLST.WAR_P/ZLST.IL_P || _cena:=0 ?};
                  ZLST.next()
               !}
            ?};
            {? wartosc>0
            || {? ZL.NRNZL=0 | rodz='T'
               || TABx.prefix('',#ZL.ref,_i);
                  {? TABx.first()
                  || TABx.WART+=wartosc;
                     TABx.put()
                  || TABx.WYD:='';
                     TABx.ZLREF:=#ZL.ref();
                     TABx.NRR:=_i;
                     TABx.WART:=wartosc;
                     TABx.SYM:=ZL.SYM;
                     TABx.add()
                  ?}
               || ZL.cntx_psh();
                  ZL.index('UNRZL');
                  ZL.prefix(ZL.NRNZL);
                  ZL.first();
                  _zl:=#ZL.ref();
                  _zlsym:=ZL.SYM;
                  ZL.cntx_pop();
                  TABx.prefix('',_zl,_i);
                  {? TABx.first()
                  || TABx.WART+=wartosc;
                     TABx.put()
                  || TABx.WYD:='';
                     TABx.ZLREF:=_zl;
                     TABx.NRR:=_i;
                     TABx.WART:=wartosc;
                     TABx.SYM:=_zlsym;
                     TABx.add()
                  ?}
               ?}
            ?};
            wartosc:=0;
            ZL.next()
         !}
      ?};
      date(OKR.ROK,OKR.MC,1)<>date(ST.AR,ST.AM,1)
   !}
;~~}
{MACRO-}
{COMMENT}
Funkcja okreslajaca cene zbrakowanego towaru. Pobierana jest ostatnia
aktulana cena z tabeli stanów, gdy brak stanow, to cena pobierana jest
z tabeli PTC, gdy brak ceny w tabeli PTC to pobierana jest usredniona
cena z tabeli ZLST.
{COMMENT-}
{MACRO: 'utwzlwyd'}
{  {? odd='T'
   || ZL.index('DATAO');
      {? ZL.first()
      || _data:=ZL.OD
      ?}
   || _data:=pocz
   ?};
   OKR.clear();
   OKR.index('OKR');
   OKR.find_key(REF.FIRMA,pocz~1,pocz~2);
   _ok:=1;
   OKR.prev();
   {!
   |? 'ustala nr okresu';
      OKR.next();
      ND.use('nagdo'+ST.ODDZ+(2-$OKR.ROK));
      DK.use('dokma'+ST.ODDZ+(2-$OKR.ROK));
      {? date(OKR.ROK,OKR.MC,1)<pocz
      || _i:=0
      || _i:=1;_ok:=1;
         {? okrkw='O'
         || {!
            |? {? tabok[_i][2]=date(OKR.ROK,OKR.MC,0)
               || _ok:=0
               || _i+=1
               ?};
               _i<=ilo & _ok
            !}
         || {!
            |? {? tabok[ilo+1-_i][2]>=date(OKR.ROK,OKR.MC,0)
               || _ok:=0
               || _i+=1
               ?};
               _i<=ilo & _ok
            !};
            _i:=ilo+1-_i
         ?}
      ?};
      ZL.index('STAN');
      {? zlec= 'O' || ZL.prefix('N','O')
      |? zlec= 'Z' || ZL.prefix('N','Z')
      || ZL.prefix()
      ?};
      {? ZL.first()
      || {!
         |? ND.clear();
            ND.index(akczl);
            ND.prefix('T',ZL.ref());
            wartosc:=0;
            {? ND.first()
            || {!
               |? {? ND.TYP().WYR='N' & ND.TYP().ZLEC='T' & ND.TYP().P='N' & ND.TYP().DN='N'
                  || DK.clear();
                     DK.index('DOKMAG');
                     DK.prefix(ND.ref());
                     _ilosc:=0;
                     {? DK.first()
                     || {!
                        |? wartosc+=DK.WAR;
                           DK.next()
                        !}
                     ?}
                  ?};
                  {? ND.TYP().WYR='N' & ND.TYP().ZLEC='T' & ND.TYP().P='N' & ND.TYP().DN='T'
                  || DK.clear();
                     DK.index('DOKMAG');
                     DK.prefix(ND.ref());
                     {? DK.first()
                     || {!
                        |? wartosc-=DK.WAR;
                           DK.next()
                        !}
                     ?}
                  ?};
                  {? ZL.NRNZL=0 | rodz='T'
                  || TABx.prefix(ND.WYD().KOD,#ZL.ref(),_i);
                     {? TABx.first()
                     || TABx.WART+=wartosc;
                        TABx.put()
                     || TABx.WYD:=ND.WYD().KOD;
                        TABx.ZLREF:=#ZL.ref();
                        TABx.NRR:=_i;
                        TABx.WART:=wartosc;
                        TABx.SYM:=ZL.SYM;
                        TABx.add()
                     ?}
                  || ZL.cntx_psh();
                     ZL.index('UNRZL');
                     ZL.prefix(ZL.NRNZL);
                     ZL.first();
                     _zl:=#ZL.ref();
                     _zlsym:=ZL.SYM;
                     ZL.cntx_pop();
                     TABx.prefix(ND.WYD().KOD,_zl,_i);
                     {? TABx.first()
                     || TABx.WART+=wartosc;
                        TABx.put()
                     || TABx.WYD:=ND.WYD().KOD;
                        TABx.ZLREF:=_zl;
                        TABx.NRR:=_i;
                        TABx.WART:=wartosc;
                        TABx.SYM:=_zlsym;
                        TABx.add()
                     ?}
                  ?};
                  wartosc:=0;
                  ND.next()
               !}
            ?};
            wartosc:=0;
            ZL.next()
         !}
      ?};
      date(OKR.ROK,OKR.MC,1)<>date(ST.AR,ST.AM,1)
   !}
;~~}
{MACRO-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{IF: wyd='N'}
{SUBSTITUTE: 'utwzl'}
{ELSE}
{SUBSTITUTE: 'utwzlwyd'}
{FI}
{TABx.clear();~~}
{IF: TABx.first()}{ dalej:=1;zl:=TABx.ZLREF;i:=1;sym:=TABx.SYM;wyd:=TABx.WYD;~~}{FI}
{WHILE: dalej}
{DO}
{WHILE: i<=ilo+1}
   {DO}
     {IF: TABx.NRR<>i-1}
      {druk[i]:=0;i+=1;~~}
     {ELIF: sym=TABx.SYM & wyd=TABx.WYD}
      {druk[i]:=TABx.WART$3;i+=1;~~}
     {dalej:=TABx.next();~~}
     {ELSE}
      {druk[i]:=0;i+=1;~~}
     {FI}
   {OD}
   {IF: TABx.NRR<ilo & dalej}
   {TABx.prev;~~}
   {SUBSTITUTE: 'LINIA0'}
   {TABx.next;~~}
   {ELSE}
   {SUBSTITUTE: 'LINIA0'}
   {FI}
   {i:=1;zl:=TABx.ZLREF;sym:=TABx.SYM;wyd:=TABx.WYD;~~}
{OD}

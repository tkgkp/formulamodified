:!UTF-8
{TITLE:B. Zestawienie zbiorcze wg towaru/usługi}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   rek_001:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','doklw','PRN','lp','kgr','sr','med','med2');
      VAR_DEL.delete('od_daty','do_daty','filtr_kh','koszty');
      VAR_DEL.delete('wydr_naz','wydr_edi','wyjscie','X_Xb','X_Xw1','X_Med','X_Med2')";
   rek_001();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   tryb:=rep_export();

   filtr_kh:='N';
   od_daty:=do_daty:=date(0,0,0);
   koszty:=0;
   kgr:='';
   lp:=0;
   sr:=0;
   med:=0;
   med2:=0;
   doklw:=2; "--dokladnosc wartosc--";

   wydr_naz:='rek_001';
   wydr_edi:='REK_001';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit()
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         filtr_kh:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         koszty:={? WYDRUKIL.CHKBOX02=1 || 1 || 0 ?};
         od_daty:=WYDRUKIL.OD_DATY;
         do_daty:=WYDRUKIL.DO_DATY
      ?}
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_kh','kontrahent',filtr_kh,'N') ?};
   {? wyjscie=0
   ||
      _ilk:=12+koszty;
      PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();
      k:=obj_new(_ilk); "--szerokosci kolumn--";
      n:=obj_new(_ilk); "--nazwy kolumn--";
      w:=obj_new(_ilk); "--wartosci kolumn--";
      k[1]:=5;    n[1]:='Lp.'@;
      k[2]:=20;   n[2]:='Indeks'@;
      k[3]:=30;   n[3]:='Nazwa'@;
      k[4]:=15;   n[4]:='Sumaryczna ilość'@;
      k[5]:=10;   n[5]:='jm'@;
      k[6]:=10;   n[6]:='Ilość reklamacji'@;
      k[7]:=12;   n[7]:='Czas rozpatrzenia - łącznie (dni)'@;
      k[8]:=12;   n[8]:='Średni czas rozpatrzenia (dni)'@;
      k[9]:=12;   n[9]:='Czas rozpatrzenia - mediana (dni)'@;
      k[10]:=12;   n[10]:='Czas realizacji - łącznie (dni)'@;
      k[11]:=12;   n[11]:='Średni czas realizacji (dni)'@;
      k[12]:=12;   n[12]:='Czas realizacji - mediana (dni)'@;
      {? koszty
      || k[13]:=15;    n[13]:='Koszty'
      ?};
      {! _ii.._ilk |! w[_ii] :='' !};
      _ilks:=3+koszty;
      s:=obj_new(_ilks); "--razem--";
      {! _ii.._ilks |! s[_ii] :=0 !}
   ?}
}
{END:
   rek_001(); VAR_DEL.delete('rek_001')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
PRN.add("w[3]",k[3],n[3]);
PRN.add("w[4]",k[4],n[4],1);
PRN.add("w[5]",k[5],n[5]);
PRN.add("w[6]",k[6],n[6],1);
PRN.add("w[7]",k[7],n[7],1);
PRN.add("w[8]",k[8],n[8],1);
PRN.add("w[9]",k[9],n[9],1);
PRN.add("w[10]",k[10],n[10],1);
PRN.add("w[11]",k[11],n[11],1);
PRN.add("w[12]",k[12],n[12],1);
{? koszty
|| PRN.add("w[13]",k[13],n[13],1)
?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'N'      ,'STRING[100]' ,'Nazwa',
   'JM'     ,'STRING[10]'  ,'jm',
   'IL'     ,'REAL'        ,'Sumaryczna ilość',
   'KOSZT'  ,'REAL'        ,'Koszty',
   'MGR'    ,'STRING[8]'   ,'Grupa materiałowa',
   'ILR'    ,'INTEGER'     ,'Ilość reklamacji',
   'TR'     ,'INTEGER'     ,'Czas rozpatrzenia - łącznie',
   'TREA'   ,'INTEGER'     ,'Czas realizacji - łącznie'
   );
X_Med:=tab_tmp(2,
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'TR'     ,'INTEGER'     ,'Czas rozpatrzenia'
);
X_Med2:=tab_tmp(2,
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'TREA'   ,'INTEGER'     ,'Czas realizacji'
);
OKR.cntx_psh(); REK_N.cntx_psh();
_min:=0;
_max:=0;
_arch:={? REK_N.name()+2='__' || 1 || 0 ?};
{? _arch=0
|| _min:=od_daty~1;
   _max:=do_daty~1;
   {? _min || _min-=1 ?};
   {? _max || _max+=1 ?}
?};
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? OKR.first & (_min=0 | OKR.find_ge(_min)) & X_Xb.first()
||
   KH.cntx_psh();
   M.cntx_psh();
   REK_R.cntx_psh();
   {!
   |?
      {? _arch>0
      || REK_N.use('rekn'+ST.ODDZ+'__')
      || REK_N.use('rekn'+ST.ODDZ+(($OKR.ROK)+2))
      ?};
      REK_N.index('KH');
      {? X_Xb.first()
      ||
         {!
         |?
            REK_N.prefix('S',X_Xb.KOD);
            {? REK_N.first()
            ||
               {!
               |?
                  {? ((od_daty<=REK_N.DP | od_daty=date(0,0,0)) & (REK_N.DP<=do_daty | do_daty=date(0,0,0)))
                  || REK_N.M();
                     {? ~X_Xw1.find_tab(,'KTM',,'=',M.KTM)
                     || X_Xw1.blank(1);
                        X_Xw1.IL:=REK_N.IL;
                        X_Xw1.KTM:=M.KTM;
                        X_Xw1.N:=M.N;
                        X_Xw1.JM:=M.J().KOD;
                        X_Xw1.MGR:=M.MGR().KOD;
                        X_Xw1.ILR:=1;
                        X_Xw1.TR:={? REK_N.DZ<>date(0,0,0) || REK_N.DZ || date() ?}-REK_N.DP+1;
                        X_Xw1.TREA:={? REK_N.DZ<>date(0,0,0)
                                    || {? REK_N.DX<>date(0,0,0) || REK_N.DX || date() ?}-REK_N.DZ+1
                                    || 0
                                    ?};
                        X_Med.KTM:=X_Xw1.KTM;
                        X_Med.TR:=X_Xw1.TR;
                        X_Med.add();
                        {? X_Xw1.TREA>0
                        || X_Med2.KTM:=X_Xw1.KTM;
                           X_Med2.TREA:=X_Xw1.TREA;
                           X_Med2.add()
                        ?};
                        {? koszty
                        || X_Xw1.KOSZT:=REK_N.KOSZT
                        ?};
                        X_Xw1.add()
                     || X_Xw1.IL+=REK_N.IL;
                        X_Xw1.ILR+=1;
                        _tr:={? REK_N.DZ<>date(0,0,0) || REK_N.DZ || date() ?}-REK_N.DP+1;
                        _trea:={? REK_N.DZ<>date(0,0,0)
                               || {? REK_N.DX<>date(0,0,0) || REK_N.DX || date() ?}-REK_N.DZ+1
                               || 0
                               ?};
                        X_Xw1.TR+=_tr;
                        X_Xw1.TREA+=_trea;
                        X_Med.KTM:=X_Xw1.KTM;
                        X_Med.TR:=_tr;
                        X_Med.add();
                        {? _trea>0
                        || X_Med2.KTM:=X_Xw1.KTM;
                           X_Med2.TREA:=_trea;
                           X_Med2.add()
                        ?};
                        {? koszty
                        || X_Xw1.KOSZT+=REK_N.KOSZT
                        ?};
                        X_Xw1.put()
                     ?}
                  ?};
                  REK_N.next()
               !}
            ?};
            X_Xb.next()
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max) & _arch=0
   !};
   M.cntx_pop();
   KH.cntx_pop();
   REK_R.cntx_pop()
?};
OKR.cntx_pop(); REK_N.cntx_pop();

PAR_WYDR.TITLE:='ZESTAWIENIE ZBIORCZE REKLAMACJI KLIENTÓW WG TOWARU/USŁUGI';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: filtr_kh='T'}{<{lmt+1}}{'Z wyborem kontrahentów'}{FI}
{<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
{<{lmt+1}}{'Data przyjęcia'}{<{lmt+1+20}od_daty}{<{lmt+1+40}do_daty}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; lp:=0;~~}
{FOR: X_Xw1}
{INDEX: X_Xw1.ndx_tmp(,,'ILR',,1)}
{DO}
   {
   w[1]:=form(lp+=1,,,'99');
   w[2]:=form(X_Xw1.KTM);
   w[3]:=form(X_Xw1.N);
   w[4]:=form(X_Xw1.IL,,doklw,'.,');
   w[5]:=form(X_Xw1.JM);
   w[6]:=form(X_Xw1.ILR);
   w[7]:=form(X_Xw1.TR);
   w[8]:=form(X_Xw1.TR/X_Xw1.ILR,,doklw,'.,');
   X_Med.prefix(X_Xw1.KTM,);
   {? X_Med.first()
   || _ile:=X_Med.size();
      {? (_ile%*2)
      || _nr1:=_nr2:=(_ile%2)+1;
         _ile:=1
      || _nr1:=(_ile%2);
         _nr2:=_nr1+1;
         _ile:=2
      ?};
      _sum:=0;
      _i:=0;
      {!
      |? _i+=1;
         {? (_i=_nr1 | _i=_nr2) || _sum+=X_Med.TR ?};
         X_Med.next()
      !};
      med:={? _ile>0 || _sum/_ile || 0 ?}
   ?};
   w[9]:=form(med,,doklw,'.,');
   w[10]:=form(X_Xw1.TREA);
   X_Med2.prefix(X_Xw1.KTM,);
   w[11]:=form({? X_Med2.size()>0 || X_Xw1.TREA/X_Med2.size() || 0 ?},,doklw,'.,');
   {? X_Med2.first()
   || _ile:=X_Med2.size();
      {? (_ile%*2)
      || _nr1:=_nr2:=(_ile%2)+1;
         _ile:=1
      || _nr1:=(_ile%2);
         _nr2:=_nr1+1;
         _ile:=2
      ?};
      _sum:=0;_sum2:=0;
      _i:=0;
      {!
      |? _i+=1;
         {? (_i=_nr1 | _i=_nr2) || _sum+=X_Med2.TREA ?};
         X_Med2.next()
      !};
      med2:={? _ile>0 || _sum/_ile || 0 ?}
   ?};
   w[12]:=form(med2,,doklw,'.,');
   kgr:=form(X_Xw1.MGR);
   {? koszty
   || w[13]:=form(X_Xw1.KOSZT,,doklw,'.,')
   ?};
   s[1]+=X_Xw1.ILR;
   s[2]+=X_Xw1.TR;
   s[3]+=X_Xw1.TREA;
   {? koszty
   || s[4]+=X_Xw1.KOSZT
   ?}
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[8]:=w[9]:=w[11]:=w[12]:='';
w[5]:='Razem:'; PRN.FORM[5]:=0;
w[6]:=s[1];
w[7]:=s[2];
w[10]:=s[3];
{? koszty
|| w[13]:=form(s[4],,doklw,'.,')
?};
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}

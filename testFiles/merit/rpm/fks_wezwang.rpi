:!UTF-8
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: wezwang.rpi
Utworzony:   2003-11-26 [AMK] Artur Makos
==================================================
Zawartosc: Wezwanie do zaplaty - jezyk angielski
{COMMENT-}
{ {? var_pres('PRN')>0 || obj_del(PRN) ?}; naz_str:='Page ';
  PRN:=obj_new(@.CLASS.PRINT,11); PRN.sl_frame();
  {! _i:=1..12 |! w[_i]:='' !};
  PRN.add("w[12]",8,'Agency');
  PRN.add("w[1]",{? PAR_WYDR.PAR15 || 35 || 50 ?},'Account');
  PRN.add("w[2]",10,'Open date#Due date',,'#');
  {? SER_NAG.WAL=null || PRN.add("w[10]",3,'Cur') ?};
  PRN.add("form(w[4],,2,' ,')",14,'Invoice amount',1);
  PRN.add("w[5]",10,'Payment date');
  PRN.add("form(w[6],,2,' ,')",14,'Your payment',1);
  PRN.add("form(w[7],,,'1')",5,'Delay in days',1);
  PRN.add("form(w[8],,2,' ,')",14,'Unpaid',1,'#');
  {? ROZNE.UT_DOK & SER_NAG.TTAB_ODS<>null
  || PRN.add("form(w[9],,2,' ,')",14,'Interest amount (after due date)',1);
     {? PAR_WYDR.PAR15
     || PRN.add("form(w[11],,2,' ,')",14,'Interest amount (berore due date)',1)
     ?}
  ?};
  prn1:=prn2:='PRN'; PAR_WYDR.HEAD:=0;
  {! _i:=1..licz_wal  |! sumy[_i][2]:=sumy[_i][3]:=sumy[_i][4]:=0 !};
  tab_adr1[1]:='RECEIVER:'; tab_adr1[2]:='SENDER:'; tab_adr1[3]:='ADDRESS:'; tab_adr1[4]:='PAYER\'S CODE:';
 ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}------------------------------WYDRUK----------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ sumafakt:=sumawpl:=sumaods:=0;~~}
{ dokument:=oddzial:=''; tp:=date(0,0,0); kontosp:=''; sumapf:=0; sumaods1:=sumaods2:=sumaods3:=0; ~~}
{FOR: 'SER_POZ'}
{INDEX: ind_spoz}
{PREFIX: REF.FIRMA,SER_NAG.ref(),OPERATOR.DEPT().OD}
{DO}
{IF: (SER_POZ.PRZEDPO & PAR_WYDR.PAR15 & ROZNE.UT_DOK) | ~SER_POZ.PRZEDPO}
{COMMENT}------------------------------MACRO-HEADX------------------{COMMENT-}
{IF: lineleft=0}
{IF: ~new_poz}{ new_poz:=1;~~}
{  PAR_WYDR.TITLE:='Payment call'+{? SER_NAG.ID<>'' || ' number '+SER_NAG.ID || '' ?}+' from day '+$SER_NAG.DATA; ~~}
{IF: ROZNE.UT_KOM}{SUBSTITUTE: 'KORHEADP'}{ELSE}{SUBSTITUTE: 'KORHEADL'}{FI}
{IF: SER_NAG.WAL<>null}{<3 'CURRENCY:'}{<20 SER_NAG.WAL().KOD+' '+SER_NAG.WAL().TR}{FI}
{ SKID_RBK.index('WEKTOR');
  RACHBANK.KB_1R:=RB.get_rbel(2,RACHBANK.KB_1R);
  {? RACHBANK.KB_1R<>'' & SKID_RBK.find_key(REF.INFO,RACHBANK.KB_1R,RACHBANK.KB_1R)
  || kontbank:=RB.get_rbtx(1,SKID_RBK.ref(),SKID_RBK.KRAJ().KODISO)
  ?};
  {? kontbank='' ||  kontbank:=exec('get_rb','rozrach_kor') ?};
~~}
{<3 'Form of payment - bank transfer: '+SKID_RBK.BANK().NB+{? SKID_RBK.BANK().KODISO<>XINFO.KRAJ || ', SWIFT (BIC): '+SKID_RBK.BANK().SWIFT || '' ?}}
{<28 'in account: '+kontbank}
{exec('kh_dod_ini','kontrahent',SER_NAG.KH);~~}
{IF: |KH_DOD.NASZ_KOD<>''}{<3 exec('symbol','rozrach',2)+'  '+|KH_DOD.NASZ_KOD}{FI}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{LINE: 1}
{SUBSTITUTE: 'TABHEAD'}
{ vp:=1;~~}
{FI}
{COMMENT}------------------------------MACRO-HEADX-KONIEC-----------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
   {  nast:=(SER_POZ.KWP$2<>0 & SER_POZ.DATW<>SER_NAG.DATA);
      {? ~nast
      || SER_POZ.cntx_psh();
         {? PAR_WYDR.PAR15 & ROZNE.UT_DOK
         || SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),SER_POZ.ODD().OD,SER_POZ.WAL().KOD,SER_POZ.DATA,SER_POZ.SYM_ROK,SER_POZ.KON)
         || SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),SER_POZ.ODD().OD,SER_POZ.WAL().KOD,SER_POZ.DATA,SER_POZ.SYM_ROK,SER_POZ.KON,0)
         ?};
         nast:=(SER_POZ.size()>1);
         SER_POZ.cntx_pop()
      ?};
      w[1]:=w[2]:=w[4]:=w[8]:=w[10]:=w[12]:='';
      w[5]:={? SER_POZ.DATW<>date(0,0,0) & SER_POZ.KWP$2<>0 || $SER_POZ.DATW || '' ?};
      w[6]:=SER_POZ.KWP; w[7]:=SER_POZ.ZWL;
      kwp+=w[6];
      {? SER_POZ.PRZEDPO || w[9]:=0;  w[11]:=SER_POZ.ODS || w[9]:=SER_POZ.ODS;  w[11]:=0 ?};
      {? SER_NAG.WAL<>null
      || {? SER_POZ.PRZEDPO || sumaods2+=SER_POZ.ODS || sumaods+=SER_POZ.ODS ?}
      || _znal:=0;
         {! _i:=1..licz_wal
         |? _znal=0
         |! {? sumy[_i][1]=SER_POZ.WAL().KOD
            || _znal:=1;
               {? SER_POZ.PRZEDPO || sumy[_i][5]+=SER_POZ.ODS || sumy[_i][4]+=SER_POZ.ODS ?}
            ?}
         !}
      ?};
      {? SER_POZ.PRZEDPO || sumaods3+=SER_POZ.ODS || sumaods1+=SER_POZ.ODS ?};
      {? SER_POZ.SYM_ROK<>dokument | SER_POZ.TERM<>tp | SER_POZ.KON<>kontosp | SER_POZ.ODD().OD<>oddzial
      || {? SER_NAG.WAL<>null
         || sumafakt+=SER_POZ.KW
         || _znal:=0;
            {! _i:=1..licz_wal
            |? _znal=0
            |! {? sumy[_i][1]=SER_POZ.WAL().KOD || _znal:=1; sumy[_i][2]+=SER_POZ.KW ?}
            !}
         ?};
         w[4]:=SER_POZ.KW; w[2]:=$SER_POZ.DATA+$SER_POZ.TERM;
         w[1]:=SER_POZ.DOK+{? SER_POZ.SYM_ZEW<>'' & SER_POZ.SYM_ZEW<>SER_POZ.DOK
                           || ' ('+SER_POZ.SYM_ZEW+')'
                           || ''
                           ?};
         w[10]:=SER_POZ.WAL().KOD; sumapf:=0; w[12]:=SER_POZ.ODD().OD
      ?};
      sumapf+=SER_POZ.KWP; w[8]:=(SER_POZ.KW-sumapf);
      dokument:=SER_POZ.SYM_ROK; tp:=SER_POZ.TERM; kontosp:=SER_POZ.KON; oddzial:=SER_POZ.ODD().OD;
      {? SER_NAG.WAL<>null
      || sumawpl+=SER_POZ.KWP
      || _znal:=0;
         {! _i:=1..licz_wal
         |? _znal=0
         |! {? sumy[_i][1]=SER_POZ.WAL().KOD || _znal:=1; sumy[_i][3]+=SER_POZ.KWP ?}
         !}
      ?};~~}
   {SUBSTITUTE: 'LINIA0'}
{COMMENT}------------------------------LINIA KONCZACA + NOWY HEADER-----------------{COMMENT-}
      {IF: lineleft<=3}{FONT: 'T8G'}
      {SPACE: 'C'}{SUBSTITUTE: 'LINIAF'}
      {IF: lineleft<>0}{PAGE}{FI}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
      {FI}
{COMMENT}------------------------------KONIEC NOWEGO HEADERA-----------------{COMMENT-}
      { w[3]:=SER_POZ.TERM; SER_POZ.cntx_psh();~~}
    {IF: nast & {? SER_POZ.next()
                || SER_POZ.SYM_ROK<>dokument | SER_POZ.TERM<>tp | SER_POZ.KON<>kontosp | SER_POZ.ODD().OD<>oddzial
                || 1
                ?}}
      { w[1]:='TOTAL FOR ACCOUNT';
        w[3]:={? exec('get_par','#parametr',26)='T'
              || exec('dzienRob','kalendarz',w[3],1,1)
              || w[3]
              ?};
        w[7]:=$(SER_NAG.DATA-w[3]);
        w[2]:=w[4]:=w[5]:=w[10]:=w[12]:='';
        w[6]:=kwp; kwp:=0; w[9]:=sumaods1; w[11]:=sumaods3; ~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}{ sumaods1:=sumaods3:=0;~~}
      {FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}------------------------------LINIA KONCZACA + NOWY HEADER-----------------{COMMENT-}
      {IF: lineleft<=3}{FONT: 'T8G'}
      {SPACE: 'C'}{SUBSTITUTE: 'LINIAF'}
      {IF: lineleft<>0}{PAGE}{FI}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
      {FI}
{COMMENT}------------------------------KONIEC NOWEGO HEADERA-----------------{COMMENT-}
   {ELIF: ~nast}
      { kwp:=sumaods1:=sumaods3:=0;~~}
   {FI}
  { SER_POZ.cntx_pop(); ~~}
{FI}
{OD}
{IF: SER_NAG.WAL<>null}
{IF: lineleft<3}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{PAGE}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{  w[1]:='TOTAL'; w[4]:=sumafakt; w[6]:=sumawpl; w[8]:=sumafakt-sumawpl;
   w[2]:=w[5]:=w[7]:=w[10]:=w[12]:=''; w[9]:=sumaods; w[11]:=sumaods2; ~~}
{SUBSTITUTE: 'LINIAFS'}
{ELSE}
{ liczwal:=0; {! _i:=1..licz_wal  |! {? sumy[_i][2]<>0 || liczwal+=1 ?} !};~~}
{IF: lineleft<=(2+liczwal)}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{PAGE}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{  w[1]:='TOTAL'; rsep:=1; w[2]:=w[5]:=w[7]:=w[12]:=''; licznik:=1; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{WHILE: licznik<=licz_wal}
{DO}
   {IF: sumy[licznik][2]<>0}
    {  w[4]:=sumy[licznik][2]; w[6]:=sumy[licznik][3]; w[8]:=sumy[licznik][2]-sumy[licznik][3];
       w[10]:=sumy[licznik][1]; w[9]:=sumy[licznik][4]; w[11]:=sumy[licznik][5];~~}
    {SUBSTITUTE: 'LINIA0'} { w[1]:=''; rsep:=0;~~}
   {FI}
   { licznik+=1;~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI} { rsep:=1;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}------------------------------STOPKA-----------------{COMMENT-}
{ENTIRE}
{LINE: 1}
{IF: SER_NAG.WAL<>null}
{FONT: 'W8'}{SPACE: 'C'}
{<1 'Due amount: '+form((sumafakt-sumawpl),,2,' ,')+' '+SER_NAG.WAL().KOD}
{LINE: 1}
{FI}
{ ref_defw:=null;
  SLO.cntx_psh();
  SER_DEFW.index('SERDEFW1'); SER_DEFW.prefix(SER_NAG.KOD,SER_KOR.JEZYK);
  {? SER_DEFW.first() || ref_defw:=SER_DEFW.ref() ?};
  SLO.cntx_pop();~~}
{IF: SER_DEFW.first()}
{ _temp:=SER_DEFW.memo_txt(0,1,'TEKST'); STR.split(_temp,'\n'); ~~}
{WHILE: STR.next()}
{DO}
   {<1 STR.get_word()}
{OD}
{FI}
{LINE: 1}
{<1 'Please put your code ('+SER_NAG.KH().KOD+') on the transfer form'}
{LINE: 1}
{ wdt:=charleft(); ~~}
{<1>{wdt/2} 'MADE BY'}{<{wdt/2}>{wdt} 'CONFIRMED BY'}
{LINE: 2}
{<1>{wdt/2} 50*'.'}{<{wdt/2}>{wdt} 50*'.'}
{ENTIRE-}
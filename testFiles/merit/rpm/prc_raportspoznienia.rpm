:!UTF-8
{TITLE: Spóźnienia}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('__tabs','PRN','prn1','vp','lmt','rsep','h_rat','tresc','pb','Ok','Lp','Wydzial','Kalend','Karta','Pracownik');
   VAR_DEL.delete('Teczka','CzasPrac','GodzKal','GodzRozp','Spozn');

   _TAB:=exec('tab_prac','!prc_czp_wydr');
   _sql2:=''+" select * from P join UD_SKL join :_a using (P.REFERENCE, :_a.SQL) order by UD_SKL.SYMBOL";
   _TAB2:=sql(_sql2,_TAB);
   {? _TAB.first()
   || tresc:='w_spozn';
      PAR_WYDR.TITLE:='Lista pracowników spóźnionych'@;

      PRN:=obj_new(@.CLASS.PRINT,10);
      {? rep_export()
      || PRN.add("P.WYDZIAL().SYMBOL+' '+|UD_SKL.OPIS",40,'Jednostka organizacyjna'@)
      || PRN.add("Lp+=1",5,'Lp.'@,1,,,,,',,\'9.\'')
      ?};
      PRN.add("Karta",14,'Karta'@);
      PRN.add("Teczka",9,'Nr teczki'@);
      PRN.add("Pracownik",40,'Nazwisko i imię'@);
      {? ~rep_export() || PRN.add("P.WYDZIAL().SYMBOL",13,'Jednostka organizacyjna'@) ?};
      PRN.add("CzasPrac",13,'Planowany czas pracy'@,1);
      PRN.add("GodzKal",13,'Planowana godzina rozpoczęcia'@,1);
      PRN.add("GodzRozp",13,'Rzeczywista godzina rozpoczęcia'@,1);
      PRN.add("Spozn",13,'Czas spóźnienia'@,1);
      PRN.add("''",40,'Wyjaśnienia'@);
      PRN.s_frame();

      prn1:='PRN';
      rsep:=PAR_WYDR.SEP;
      lmt:=pb:=Ok:=0;
      vp:=h_rat:=all_prac:=1;
      Lp:=Wydzial:=Kalend:=0; Karta:=Pracownik:=Teczka:=CzasPrac:=GodzKal:=GodzRozp:=Spozn:=''
   ?};
  params_set('P',_TAB,'PT',_TAB2);

   R_WZCZ.cntx_psh();
   R_KARHIS.cntx_psh();
   KAL_DEF.cntx_psh();
   R_REJ_WW.cntx_psh()
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','pb','Ok','Lp','Wydzial','Kalend','Karta','Pracownik');
   VAR_DEL.delete('Teczka','CzasPrac','GodzKal','GodzRozp','Spozn');
   exec('czytaj','#stalesys',date(),KST,'R_TZ');
   R_REJ_WW.cntx_pop();
   KAL_DEF.cntx_pop();
   R_KARHIS.cntx_pop();
   R_WZCZ.cntx_pop()
}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: w_spozn.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~}
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~}
   {SPACE: 'C'}
   {LINE}
   { pb:=1; ~~}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
{<{ceil(lmt*h_rat)+1}'Pracownicy uwzględnieni na wydruku: %1'@
[{? all_prac || 'Wszyscy wybrani'@ || 'Tylko spóźnieni (z zarejestrowanym czasem pracy)'@ ?}]}
   {IF: VAR_EDIT.MSC=3}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Alfabetycznie w ramach jednostek organizacyjnych'@]}
   {ELIF: VAR_EDIT.MSC=1}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Grupowo w ramach jednostek organizacyjnych'@]}
   {ELIF: VAR_EDIT.MSC=2}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Alfabetycznie (bez związku z jednostką organizacyjną)'@]}
   {FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: ~rep_export()}
   {IF: VAR_EDIT.MSC=1}
   {<{lmt+1}'Jednostka organizacyjna:'@} {P.WYDZIAL().SYMBOL}{IF: +(|UD_SKL.OPIS)} ({UD_SKL.OPIS}){FI}
   {FI}
{FI}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{  R_WZCZ.index('R_WZCZ');
   R_WZCZ.prefix(P.name(),P.ref());
   R_KARHIS.index('PR_ZW_OD');
   R_KARHIS.prefix(P.ref(),'T');
   Karta:=
      {? R_KARHIS.find_le(VAR_EDIT.DATA)
      || R_KARHIS.K().N
      || ''
      ?};
   Kalend:=
      {? R_WZCZ.find_le(VAR_EDIT.DATA)
      || {? R_WZCZ.GRAFIK='T' || R_WZCZ.CZESC || R_WZCZ.KAL ?}
      || P.KAL
      ?};
   Pracownik:=form(P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,40);
   Karta:=form(Karta,-13);
   Teczka:=form(P.T,-9); ~~
}
{IF: Kalend<>0}
   {  KAL_ROK.index('KAL_ROK');
      KAL_ROK.prefix(Kalend,VAR_EDIT.DATA~1); ~~
   }
   {IF: KAL_ROK.first()}
      {  KAL_DEF.index('KAL_DEF');
         KAL_DEF.prefix(); ~~
      }
      {IF: KAL_DEF.find_key(KAL_ROK.ref(),VAR_EDIT.DATA)}
         {  CzasPrac:=form(KAL_DEF.CZAS,17);
            GodzKal:=form(KAL_DEF.POCZATEK,17);
            R_REJ_WW.index('R_REJ_WX');
            R_REJ_WW.prefix(P.ref(),VAR_EDIT.DATA);
            {? R_REJ_WW.first()
            || GodzRozp:=form(R_REJ_WW.GD,17);
               {? R_REJ_WW.GD>{? KAL_DEF.TYP='R' || KAL_DEF.POCZATEK || time(24,0,0) ?}
               || Spozn:=form(R_REJ_WW.GD-KAL_DEF.POCZATEK,17)
               || Spozn:=form('',17)
               ?}
            || {? KAL_DEF.TYP='R' & all_prac || Spozn:=form('NB',-17) || Spozn:=form('',17) ?}
            ?}; ~~
         }
         {IF: +Pracownik & +(|Spozn)}
            {IF: P.WYDZIAL<>Wydzial}
               {IF: VAR_EDIT.MSC=1}
                  {PAGE}
               {FI}
               {  Wydzial:=P.WYDZIAL; ~~}
            {FI}
            {SUBSTITUTE: 'LINIA0'}
         {FI}
      {FI}
   {FI}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{IF:  _n:='Elementy wydruku';
      _o:=exec('get','#profile',,tresc,_n);
      VAR_EDIT.DATA:=date();
      _ed:=VAR_EDIT.mk_edit('Zestawienie spóźnień'@,0,'prc_zs');
      VAR_EDIT.win_esep(_ed,'Parametry wydruku'@);
      VAR_EDIT.win_efld(_ed,,'FLAGA',,,,,,
         'Uwzględniać na wydruku?'@,,'Uwzględniać na wydruku?'@,
         'radio-buttons',,
         'Tylko spóźnionych (z zarejestrowanym czasem pracy)'@,"0",
         'Wszystkich wybranych'@,"1");
      VAR_EDIT.win_efld(_ed,,'DATA',,,8,,,'Na dzień'@,,'Data, której dotyczy raport'@);
      VAR_EDIT.win_efld(_ed,,'MSC',,,,,,
         'Rodzaj raportu?'@,,'Zestawienia są generowane dla wydziałów na osobnych stronach?'@,
         'radio-buttons',,
         'Alfabetycznie w ramach jednostek organizacyjnych'@,"3",
         'Grupowo w ramach jednostek organizacyjnych'@,"1",
         'Alfabetycznie (bez związku z jednostką organizacyjną)'@,"2");
      exec('ok_esc','#window',VAR_EDIT,_ed);
      {? rep_export() || VAR_EDIT.efld_opt(_ed,'enable=0',,'FLAGA') ?};
      VAR_EDIT.efld_opt(_ed,'enable=1, mark=1',,'DATA');
      VAR_EDIT.win_edit(_ed);
      _tmp:=~VAR_EDIT.edit("__CHK.record(VAR_EDIT,,'DATA')");
      all_prac:=VAR_EDIT.FLAGA;
      _o:=$(VAR_EDIT.FLAGA);
      exec('set','#profile',,tresc,_n,_o);
      _tmp
}
{FI}
{  exec('czytaj','#stalesys',VAR_EDIT.DATA,KST,'R_TZ');
   MASK.Use('R_PRACDN',VAR_EDIT.DATA~1,VAR_EDIT.DATA~2);
   MASK.Use('R_REJ_WW',VAR_EDIT.DATA~1,VAR_EDIT.DATA~2); ~~
}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: VAR_EDIT.MSC<>2}
{FOR: params_get().PT}
{DO}
   {IF: P.seek(params_get().PT.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
{ELIF: VAR_EDIT.MSC=2}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
{FI}
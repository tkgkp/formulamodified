:!UTF-8
{TITLE:Lista różnic dla inwentaryzacji}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,12); PRN.sl_frame();
   c:=obj_new(12);
   VAR_DEL.delete('TMP_KOM','TMP_KOMS');
   {! _i:=1..12 |! c[_i] := '' !};
   i:=0; __std:=null; slicznik:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0; par5:=0; par6:='';
   sum_wart:=0; licznik:=0; inw_index:=''; inw_prefix:=0;druk_pod:=0;
   roznica:=''
}
{END:
   obj_del(PRN);
   obj_del(c);
   VAR_DEL.delete('TMP_KOM','TMP_KOMS');
   &i; &color; &prn1; &prn2; &__std; &slicznik;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3; &par4; &par5; &par6;
   &sum_wart; &licznik; &inw_index; &inw_prefix;&druk_pod;
   &roznica
}
{EXIT:
   PAR_WYDR.ARKUSZ:=1;
   PAR_WYDR.win_edit('ARKUSZ');
   {? PAR_WYDR.edit() || _exit:=0 || _exit:=1 ?};
   _exit
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: i_arkusz.rpm
Utworzony  : 2005-01-06
Zawartość  : Arkusz spisu z natury
==================================================
{COMMENT-}
{PAR_WYDR.cntx_psh();~~}
{INCLUDE: wsp_pw.rpi}
{PAR_WYDR.cntx_pop();~~}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("c[1]",5,'Lp.',,'#');
      {? PAR_WYDR.ARKUSZ=1
      || PRN.add("c[9]",6,'Arkusz',1,'#')
      ?};
      PRN.add("c[2]",20,'Nr inwentarz.',,'#');
      PRN.add("c[3]",30,'Nazwa środka',,'#');
      PRN.add("c[4]",25,'Nr fabryczny',,'#');
      PRN.add("c[5]",11,'Data zakupu',,'#');
      PRN.add("c[6]",10,'Rodzaj',,' ');
      PRN.add("c[7]",45,'Opis różnicy',,' ');
      PRN.add("c[8]",40,'Uwagi',,' ');

      {? PAR_WYDR.ARKUSZ = 1
      || _title:='inwentaryzacji nr: '+$SRXI.NR+', na dzień: '+SRXI.DS$6
      || _title:='arkusza nr: '+$SRXA.NR+' z inwentaryzacji nr:'+$SRXI.NR+', na dzień: '+SRXI.DS$6
      ?};
      PAR_WYDR.TITLE:='Lista różnic dla '+_title; ~~}
{COMMENT} --- Makra --- {COMMENT-}
{MACRO: 'wiersz'}
{ roznica:='';
  czy_druk:=0;

     {? SRXD.GRP<>'E' & SRXD.STATUS='D'
     || czy_druk:=1;
        {? SRXD.ODD_N<>null || roznica+='w jedn. ks.: '+SRXD.ODD().OD ?};
        {? SRXD.JORG_N<>null
        || {? roznica<>'' || roznica+=', ' ?};
           roznica+='w jedn. org.: '+SRXD.JORG().SYMBOL
        ?};
        {? SRXD.POM_N<>null
        || {? roznica<>'' || roznica+=', ' ?};
           roznica+='w pomieszczeniu: '+SRXD.POM().S
        ?};
        {? SRXD.OSOBA_N<>null
        || {? roznica<>'' || roznica+=', ' ?};
           roznica+='użytkuje: '+SRXD.OSOBA().NAZWISKO+' '+SRXD.OSOBA().PIERWSZE
        ?};
        {? roznica<>''
        || _len:=(+roznica-1);
           _pri:=~(1+roznica);
           _sec:=roznica+_len;
           roznica:=_pri+_sec
        ?}
     |? SRXD.GRP<>'E' & SRXD.STATUS='Z'
     || czy_druk:=1;
        {? SRXD.ODD<>null || roznica+='w jedn. ks.: '+SRXD.ODD().OD ?};
        {? SRXD.JORG<>null
        || {? roznica<>'' || roznica+=', ' ?};
           roznica+='w jedn. org.: '+SRXD.JORG().SYMBOL
        ?};
        {? SRXD.POM<>null
        || {? roznica<>'' || roznica+=', ' ?};
           roznica+='w pomieszczeniu: '+SRXD.POM().S
        ?};
        {? SRXD.OSOBA<>null
        || {? roznica<>'' || roznica+=', ' ?};
           roznica+='użytkował(a): '+SRXD.OSOBA().NAZWISKO+' '+SRXD.OSOBA().PIERWSZE
        ?};
        {? roznica<>''
        || _len:=(+roznica-1);
           _pri:=~(1+roznica);
           _sec:=roznica+_len;
           roznica:=_pri+_sec
        ?}
     |? SRXD.GRP<>'E' & SRXD.STATUS='P'
     || {? SRXD.ODD<>SRXD.ODD_N
        || czy_druk:=1;
           {? SRXD.ODD().OD<>''
           || _bylo:=SRXD.ODD().OD
           || _bylo:='[brak]'
           ?};
           {? SRXD.ODD_N().OD<>''
           || _jest:=SRXD.ODD_N().OD
           || _jest:='[brak]'
           ?};
           roznica:='Jedn. ks. - była: '+_bylo+'- jest: '+_jest
        ?};

        {? SRXD.JORG<>SRXD.JORG_N
        || czy_druk:=1;
           {? SRXD.JORG().SYMBOL<>''
           || _bylo:=SRXD.JORG().SYMBOL
           || _bylo:='[brak]'
           ?};
           {? SRXD.JORG_N().SYMBOL<>''
           || _jest:=SRXD.JORG_N().SYMBOL
           || _jest:='[brak]'
           ?};
           {? roznica<>'' || roznica+=', ' ?};
           roznica+='Jedn. org. - była: '+_bylo+'- jest: '+_jest
        ?};

        {? SRXD.POM<>SRXD.POM_N
        || czy_druk:=1;
           {? SRXD.POM().S<>''
           || _bylo:=SRXD.POM().S
           || _bylo:='[brak]'
           ?};
           {? SRXD.POM_N().S<>''
           || _jest:=SRXD.POM_N().S
           || _jest:='[brak]'
           ?};
           {? roznica<>'' || roznica+=', ' ?};
           roznica+='Pomieszczenie - było: '+_bylo+'- jest: '+_jest
        ?};

        {? SRXD.OSOBA<>SRXD.OSOBA_N
        || czy_druk:=1;
           {? (SRXD.OSOBA().NAZWISKO+' '+SRXD.OSOBA().PIERWSZE)<>' '
           || _bylo:=SRXD.OSOBA().NAZWISKO+' '+SRXD.OSOBA().PIERWSZE
           || _bylo:='[brak]'
           ?};
           {? (SRXD.OSOBA_N().NAZWISKO+' '+SRXD.OSOBA().PIERWSZE)<>' '
           || _jest:=SRXD.OSOBA_N().NAZWISKO+' '+SRXD.OSOBA().PIERWSZE
           || _jest:='[brak]'
           ?};
           {? roznica<>'' || roznica+=', ' ?};
           roznica+='Użytkownik - był(a): '+_bylo+'- jest: '+_jest
        ?}
     ?};

     __std:=null;
     {? czy_druk=0 & SRXD.GRP='T'
     || SRXD.cntx_psh();
        SRXD.index('SRXD');
        SRXD.prefix(SRXD.ref());
        {? SRXD.first()
        || {! |?
               {? SRXD.STATUS='Z' | SRXD.STATUS='D' || czy_druk:=2 ?};
               czy_druk=0 & SRXD.next()
           !}
        ?};
        SRXD.cntx_pop()
     ?}
;~~}
{COMMENT} ---- zwykłe wiersze, jesli czy_druk > 0 ---- {COMMENT-}
 {IF: czy_druk>0}
  {IF: czy_druk=1}
     {
         licznik+=1;
         c[1]:=licznik;
         {? PAR_WYDR.ARKUSZ=1
         || c[9]:=$SRXD.SRXA().NR
         ?};
         c[2]:=SRXD.NRI;
         c[3]:=SRXD.NST;
         c[4]:=SRXD.NF;
         c[5]:=form(SRXD.DZ,,1);
         c[6]:={? SRXD.STATUS='P'
               || 'Różnica'
               |? SRXD.STATUS='D'
               || 'Nadmiar'
               |? SRXD.STATUS='Z'
               || 'Niedobór'
               ?};
         c[7]:=roznica;
         c[8]:=SRXD.memo_txt(,1,'UWAGI');
         {? +c[8]>255 || c[8]:=255+c[8] ?};
         c[8]:=gsub(c[8],'\n',' ')
     ;~~}
     {IF: lineleft() <>0 & lineleft() <=4}{PAGE}{FI}
     {SUBSTITUTE: 'LINIA0'}
  {FI}
  { {? czy_druk=2 || licznik+=1 ?} ;~~}
  {
      slicznik:=0;
      __std:=null;
      SRXD.cntx_psh();
      SRXD.index('SRXD');
      SRXD.prefix(SRXA.ref(),SRXD.ref());
      {? SRXD.first()
      || {! |?
            {? SRXD.STATUS='Z' | SRXD.STATUS='D' || __std:=SRXD.TREE ?};
            __std=null & SRXD.next()
         !}
      ?};
      SRXD.cntx_pop()
  ;~~}
  { {? czy_druk=1 || rsep:=0 ?}; ~~}
{COMMENT} --- elementy --- {COMMENT-}
  {IF: __std<>null}
     {FOR: SRXD}{INDEX: 'SRXD'}{PREFIX: __std}
     {DO}
     {IF: (SRXD.STATUS='Z' | SRXD.STATUS='D')}
        {
            slicznik+=1;
            c[1]:=$licznik+'/'+$slicznik;
            c[2]:=SRXD.NRI;
            c[3]:=SRXD.NST;
            c[4]:=SRXD.NF;
            c[5]:=form(SRXD.DZ,,1);
            c[6]:={? SRXD.STATUS='D' || 'Nadmiar' |? SRXD.STATUS='Z' || 'Niedobór' ?};
            c[7]:=' - ';
            c[8]:=' - ';
            c[9]:=' - '
        ;~~}
        {IF: lineleft() <=4}{PAGE}{FI}
        {SUBSTITUTE: 'LINIA0'}
        { {? czy_druk=2 || rsep:=0 ?}; ~~}
     {FI}
     {OD}
  {FI}
  {rsep:=PAR_WYDR.RSEP;~~}
 {FI}
{MACRO-}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{<1 'PARAMETRY INWENTARYZACJI:'}
{COMMENT} ---- Arkusz, Jedn. ks., Jedn. org., Pomieszczenie ---- {COMMENT-}
{
  par1:='Zakres środków: '+{? SRXI.R='T' || 'Trwałe' |? SRXI.R='N' || 'Niskocenne' || 'Wszystkie' ?};
  par2:='Inwentaryzację wykonano: '+{? SRXI.DZ<>date(0,0,0) || SRXI.DZ$6 || $SRXI.DZ ?};
  {? SRXI.ODD<>null || par3:='J. księgowa : '+SRXI.ODD().OD ?};
  par6:='Stan na dzień: '+{? SRXI.DS<>date(0,0,0) || SRXI.DS$6 || $SRXI.DS ?}
;~~}
{<1 par1}
{<1 par2}
{<1 par6}
{IF: SRXI.ODD<>null}{<1 par3}{FI}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: druk_pod=0}
{SUBSTITUTE: 'TABHEAD'}
{FI}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{IF: PAR_WYDR.ARKUSZ=1}
{FOR: SRXD}{INDEX: 'SRXINRI'}{PREFIX: SRXI.ref()}
 {DO}
 {SUBSTITUTE: 'wiersz'}
 {OD}
{ELSE}
 {FOR: SRXD}{INDEX: 'NRI'}{PREFIX: SRXA.ref()}
 {DO}
 {SUBSTITUTE: 'wiersz'}
 {OD}
{FI}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8G'}{SPACE: 'C'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{druk_pod:=1;~~}

{FONT: 'T8G'}
 {ENTIRE}
{<12 'Skład komisji inwentaryzacyjnej:'}{<65 'Podpis'}{<120 'Inne osoby obecne przy spisie:'}

{
   TMP_KOM:=tab_tmp(1,'REF','INTEGER','Ref komisji','KOD','STRING[20]', 'Kod komisji');
   TMP_KOMS:=tab_tmp(1,'CZLON','STRING[50]', 'Członek komisji');
   SRXD.cntx_psh();
   SRXD.index('STATUS');
   SRXD.prefix(SRXA.ref());
   {? SRXD.first()
   || {! |?
         {? SRXD.SRXP<>null
         || {? SRXD.SRXP().SRXN().SRXK<>null
            || _kod:=SRXD.SRXP().SRXN().SRXK().KOD;
               {? TMP_KOM.first()
               || _add:=1;
                  {! |?
                     {? TMP_KOM.KOD=_kod || _add:=0 ?};
                     _add & TMP_KOM.next()
                  !}
               || _add:=1
               ?};
               {? _add
               || TMP_KOM.REF:=#SRXD.SRXP().SRXN().SRXK;
                  TMP_KOM.KOD:=SRXD.SRXP().SRXN().SRXK().KOD;
                  TMP_KOM.add()
               ?}
            ?}
         ?};
         SRXD.next()
      !}
   ?};
   SRXD.cntx_pop();
   {? TMP_KOM.first()
   || {! |?
         SRXL.index('SRXL');
         SRXL.prefix(TMP_KOM.REF);
         {? SRXL.first()
         || {! |?
               {? SRXL.P='T' || _czlon:='(Przew.) ' || _czlon:='' ?};
               _czlon+=SRXL.OSOBA().PIERWSZE+' '+SRXL.OSOBA().NAZWISKO;
               {? TMP_KOMS.first()
               || _add:=1;
                  {! |?
                     {? TMP_KOMS.CZLON=_czlon || _add:=0 ?};
                     _add & TMP_KOMS.next()
                  !}
               || _add:=1
               ?};
               {? _add
               || TMP_KOMS.CZLON:=_czlon;
                  TMP_KOMS.add()
               ?};
               SRXL.next()
            !}
         ?};
         TMP_KOM.next()
      !}
   ?}
;~~}
{IF: TMP_KOMS.size=0}
{<12 '(Przew.)..........................................'}{<120 '..................................................'}

{<12 '..................................................'}{<120 '..................................................'}

{<12 '..................................................'}{<120 '..................................................'}
{ELSE}
{FOR: TMP_KOMS}
   {DO}
{<12 TMP_KOMS.CZLON}{<65 '.........................'}{<120 '..................................................'}
   {OD}
   {IF: TMP_KOMS.size()<3}
      {dod_podp:=3-TMP_KOMS.size();~~}
      {WHILE: dod_podp>0}
      {DO}
         {<12 '..................................................'}{<120 '..................................................'}
         {dod_podp:=dod_podp-1; ~~}
      {OD}
   {FI}
{FI}
{ENTIRE-}
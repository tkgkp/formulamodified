:!UTF-8
{MACRO: 'rachunek'}
{COMMENT}OLD: zlerach.rpi{COMMENT-}
{ENTIRE}
{  PAR_WYDR.TITLE:='';
   LS.use(RH.O().LT); ~~
}
{IF: PAR_WYDR.HEAD}
{FONT: 'HEAD'}
{SPACE: 'B'}
{  lmt:=charleft(); indent:=1; ~~ }
{>{lmt-13}PAR_WYDR.MIEJSC+', dn. '@+date()$4}
{<13 STR.split(KST.NAZWA); STR.line(lmt-36)}
{WHILE: STR.next()}{DO}{<13 STR.line(lmt-36)}{OD}
{  PAR_WYDR.HEAD:=0; ~~ }
{MARGIN: 0.6}
{SUBSTITUTE: 'HEAD'}
{MARGIN: 0}
{  PAR_WYDR.HEAD:=1; ~~ }
{FI}
{FONT: 'TITLE'}
{SPACE: 'A'}
{<1>{charleft()} 'RACHUNEK Z DNIA %1'@[RH.DRA$4]}
{<1>{charleft()} 'DO UMOWY NR %1'@[|ZC.NU]}

{FONT: 'W12'}
{'A. DANE OSOBOWE'@}

{SPACE: 'B'}
{FONT: 'W10'}{>5}1. {'Nazwisko i Imię'@}:{>40}{FONT: 'T10B'}{RH.ZLE().OSOBA().NAZWISKO} {OSOBA.PIERWSZE} {OSOBA.DRUGIE}
{FONT: 'W10'}{>5}2. {'Data urodzenia'@}:{>40}{FONT: 'T10B'}{OSOBA.UR_DATA$4}
{FONT: 'W10'}{>5}3. {'Nr PESEL'@}:{>40}{FONT: 'T10B'}{OSOBA.PESEL}
{FONT: 'W10'}{>5}4. {'Urząd Skarbowy'@}:{>40
}{FONT: 'T10B'}{{? exec('szukaj_us','osoba',RH.DWY) || OS_US.US().NU || ''?}}
{IF: {? ~exec('szukaj','osoba','C',RH.DWY)
     || {? ~exec('szukaj','osoba','C')
        || exec('szukaj','osoba','S')
        || 1
        ?}
     || 1
     ?}}
{FONT: 'W10'}{>5}5. {'Adres zamieszkania'@}:
{FONT: 'W10'}{>8'Kod/Miasto'@}:{>40
}{FONT: 'T10B'}{_ad:=exec('poczta','osoba'); {? |OS_ADRES.POCZTA<>'' & |OS_ADRES.POCZTA<>|OS_ADRES.MIASTO || _ad+=', '+|OS_ADRES.MIASTO ?}; |_ad}
{FONT: 'W10'}{>8'Województwo'@}:{>40}{FONT: 'T10B'}{OS_ADRES.WOJEWODZ}
{FONT: 'W10'}{>8'Gmina/Dzielnica'@}:{>40}{FONT: 'T10B'}{OS_ADRES.GMINA}{
STR.split(exec('ulica','osoba'));~~}
{FONT: 'W10'}{'Ulica, dom, lokal'@}:{>40}{FONT: 'T10B'}{STR.line(charleft())}
{WHILE: STR.next()}
{DO}
{FONT: 'W10'}{''}{>40}{FONT: 'T10B'}{STR.line(charleft())}
{OD}
{FI}
{ENTIRE-}
{ENTIRE}
{SPACE: 'A'}

{FONT: 'W12'}
{'B. KALKULACJA WYNAGRODZENIA'@}

{SPACE: 'B'}
{FONT: 'W10'}
{IF: ZC.CZYWAL='T' & RH.ZAS<>'T'}
{>5}{IF: ZC.TYP='G' | ZC.TYP='D'}{'Stawka rachunku w walucie'@}{ELSE}{'Kwota rachunku w walucie'@
}{FI}: {FONT: 'T10B'} {.2 RH.WAL} {RH.ZLE().WAL().KOD}{FONT: 'W10'}
{dzien:=RH.DWY-1; {! |? {? ~__KAL.w_days(dzien,dzien) || dzien-=1; 1 ?} !}; ~~}
{>5}{'Kurs z dnia %1'@[dzien$1]}: {FONT: 'T10B'}{ZC.WAL(); exec('waluta_miara','lista_licz',SLO.KOD)} {SLO.KOD
} = {exec('waluta_kurs','lista_licz',ZC.ZWAL,&dzien,ZC.WAL)} PLN{FONT: 'W10'}
{FI}
{FOR: LS}{INDEX: 'ZLECLPRU'}{PREFIX: RH.ref()}{DO}
{IF: LS.KW<>0 & LS.RB().KDR='T'}
{>5}{R.RT}{>60}{FONT: 'T10B'}{>70.2 LS.KW}{IF: R.RK='R'} {'godz.'@}{ELIF: R.RK='N'} {'dni'@}{ELIF: R.RN=71 | __RUB.sys_attr(R.RN,71)} %{ELIF: R.RN<100} {ELSE} zł{FI}
{FONT: 'W10'}
{FI}
{OD}

{>5}{ gt:=exec('licz_rhs','lista_licz',990);
      Text:=STR.split(STR.kwota_sł(gt)); ~~ }{'Słownie'@}:
{WHILE: +(Text:=STR.line(charleft()-5))}{DO}{>5}###{Text}###{'\n'}{OD}{ &Text; ~~ }
{LINE: 3}
{ lm:=charleft(); ~~ }
{<1>{lm/2}(25*'─')}{<{lm/2}>{lm}(25*'─')}
{FONT: 'W8'}
{SPACE: 'C'}
{ lm:=charleft(); ~~ }
{<1>{lm/2}'Obliczył'@}{<{lm/2}>{lm}'Zatwierdził'@}
{ &lm; ~~ }
{ENTIRE-}
{ENTIRE}
{SPACE: 'A'}

{FONT: 'W12'}
{'C. WYPŁATA'@}

{SPACE: 'B'}
{FONT: 'W10'}
{>5}{'Data wypłaty'@}{>40}{FONT: 'T10B'}{RH.DWY$4}{FONT: 'W10'}
{FOR:RHP}{INDEX:'RHP'}{PREFIX:RH.ref()}{DO}
{>5}{RHP.B().NB}
{>5}{RHP.N}{>70}{FONT: 'T10B'}{>70.2 exec('przelkw','zlec_rh')} {FONT: 'T10B'}zł{FONT: 'W10'}
{ gt-=exec('przelkw','zlec_rh'); ~~ }

{TOTAL}
{IF: gt}
{>5}{'Gotówka'@}:{>70}{FONT: 'T10B'}{>70.2 gt} zł{FONT: 'W10'}
{FI}{OD}
{IF: gt}
{  STR.split('Potwierdzam odbiór kwoty %1 zł (słownie: %2).'@[form(gt,,2,'9.'),STR.kwota_sł(gt)]); ~~ }
{WHILE: +(Text:=STR.line(charleft()-5))}
{DO}{REP: '{>5 Text}'}{OD}
{FI}
{LINE: 4}
{<{charleft()/2}>{charleft()}(25*'─')}
{FONT: 'W8'}
{SPACE: 'C'}
{<{charleft()/2}>{charleft()}'(podpis odbiorcy)'@}
{ENTIRE-}
{IF: {? var_pres('typ')=type_of('')
     || params_get().P.RMUA
     || FUN.ask('Czy drukować informacje ubezpieczeniowe (RMUA)?'@)
     ?}
}
{SPACE: 'A'}

{FONT: 'W12'}
{ENTIRE}
{'D. INFORMACJE UBEZPIECZENIOWE (RMUA)'@}

{SPACE: 'B'}
{FONT: 'W10'}
{INCLUDE: p_zlermua.rpi}
{ENTIRE-}
{FI}
{ {? var_pres('Text')>=0 || &Text ?};
  {? var_pres('gt')>=0 || &gt ?}; ~~
}
{PAGE}
{MACRO-}
:!UTF-8
{TITLE:Karta ubezpieczeniowa - zestawienie uproszczone}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__edit','__param');
   tresc:='kartaubr';
   PAR_WYDR.TITLE:='Karta ubezpieczeniowa - zestawienie uproszczone'@;
   par:=obj_new(11);
   par[11]:=rep_export();
   par[9]:=par[10]:=0;
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'ROK','INTEGER','Rok',
         'ZERA','INTEGER','Drukowanie wierszy z zerowymi wartościami'
      );
      __param.ROK:=O.RU;
      __param.add();

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pkartubr');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok ubezpieczeniowy'@);
      __param.win_efld(__edit,,'ZERA',,,,,,'Wiersze z zerowymi wartościami?'@,,,'check-box',
                                           'check_label="%1"' ['Składniki karty'@],"1","0"
      );
      __param.efld_opt(__edit,'mark=1',,'ROK');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[10]:=__param.edit(
            "  {? __param.ROK<=1900
               || FUN.emsg('Błędna wartość parametru wydruku.'@); 0
               || par[9]:=__param.ROK; 1
               ?}
            "
         )
      || par[1]:=obj_new(@.CLASS.PRINT,7);
         {? par[11]
         || par[1].add(
               "  '%1 %2 %3 %4'
                     [  P.OSOBA().NAZWISKO,
                        OSOBA.PIERWSZE,
                        {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                        '(PESEL %1)'@[OSOBA.PESEL]
                     ]
               ",40,'Pracownik'@)
         ?};
         par[1].add(
            "  {? par[6]<=12
               || STR.split(date(par[9],par[6],1)$8);
                  STR.get_word()
               || 'Razem'@
               ?}
            ",11,'Miesiąc'@);
         par[1].add("par[2][par[6]][1]",12,'Fundusz emerytalny'@,1,,,,,'12,2');
         par[1].add("par[2][par[6]][2]",12,'Fundusz rentowy'@,1,,,,,'12,2');
         par[1].add("par[2][par[6]][3]",12,'Fundusz chorobowy'@,1,,,,,'12,2');
         par[1].add("par[2][par[6]][4]",12,'Fundusz wypadkowy'@,1,,,,,'12,2');
         par[1].add("par[2][par[6]][5]",15,'Ubezpieczenie zdrowotne'@,1,,,,,'12,2');
         {? ~par[11] || par[1].add("par[2][par[6]][6]",12,'Razem'@,1,,,,,'12,2') ?};
         par[1].s_frame();

         par[2]:=obj_new(13);
         {! _im:=1..obj_len(par[2])
         |! par[2][_im]:=obj_new(6);
            {! _jk:=1..obj_len(par[2][_im])
            |! par[2][_im][_jk]:=0
            !}
         !};

         par[3]:=tab_tmp(,'REF','INTEGER','Osoba');
         par[4]:=__param.ZERA;
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?};
   prn1:='par[1]';
   lmt:=rsep:=par[5]:=par[6]:=par[8]:=0;
   vp:=par[7]:=b_rat:=h_rat:=1;

   KU.cntx_psh();
   KU.index('KARTAUB');
   RH.cntx_psh();
   RH.index('RACHDATA')
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__edit','__param');
   P.cntx_pop();
   KU.cntx_pop();
   RH.cntx_pop()
}
{COMMENT}

   UWAGA! wydruk wykorzystuje wartości przechowywane w karcie ubezpieczeniowej
   pracownika:
             2 - fundusz emerytalny pracownik
             4 - fundusz emerytalny zakład
             8 - fundusz emerytalny budżet państwa
             3 - fundusz rentowy pracownik
             5 - fundusz rentowy zakład
             9 - fundusz rentowy budżet państwa
            11 - fundusz chorobowy
            12 - fundusz wypadkowy
            14 - kasa chorych

   Jeżeli pracownik etatowy jest jednocześnie zatrudniany na umowy - zlecenia
   to na wydruku zostaną uwzględnione dane z rachunków od umów.

{COMMENT-}
{COMMENT}OLD: pkartubr.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par[10]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[7]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? par[11] || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[7]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Rok ubezpieczeniowy: '@+$par[9]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~par[11]}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{IF: ~par[11]}
   {FONT: 'W12'}
   {SPACE: 'A'}
   {<{ceil(lmt*b_rat)+1}}{form(par[8]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[5]:=1; ~~ }
{MACRO-}
{MACRO: tresc}
{IF: ~par[3].find_key(#P.OSOBA)}
   {  par[3].REF:=#P.OSOBA;
      par[3].add();

      par[5]:=par[6]:=0;
'Zerowanie pól w buforze';
      {! _im:=1..obj_len(par[2])
      |! {! _jk:=1..obj_len(par[2][_im])
         |! par[2][_im][_jk]:=0
         !}
      !};

'Odczytanie składników z karty ubezpieczeniowej';
         KU.prefix(P.FIRMA,P.OSOBA,par[9]);
         {? KU.first()
         || {!
            |? par[2][KU.M][1]:=KU.S2+KU.S4+KU.S8+KU.S26; 'fundusz emerytalny';
               par[2][KU.M][2]:=KU.S3+KU.S5+KU.S9+KU.S27; 'fundusz rentowy';
               par[2][KU.M][3]:=KU.S11;                   'fundusz chorobowy';
               par[2][KU.M][4]:=KU.S12;                   'fundusz wypadkowy';
               par[2][KU.M][5]:=KU.S14;                   'ubezp. zdrowotne';
               KU.next()
            !}
         ?};

'Odczytanie składników z rachunków do umów cywilno-prawnych';
         RH.prefix(exec('ref_firma','ustawienia'),P.OSOBA,par[9]);
         {? RH.first()
         || {!
            |? par[2][RH.M][1]+=exec('licza_rhs','lista_licz',57111,57121);
               par[2][RH.M][2]+=exec('licza_rhs','lista_licz',57112,57122);
               par[2][RH.M][3]+=exec('licza_rhs','lista_licz',5721);
               par[2][RH.M][4]+=exec('licza_rhs','lista_licz',5731);
               par[2][RH.M][5]+=exec('licza_rhs','lista_licz',57411);
               RH.next()
            !}
         ?};

'Suma pól bufora (kolumny i wiersze)';
      _lwierszy:=obj_len(par[2]);
      _lkolumn:=obj_len(par[2][1]);

      {! _im:=1.._lwierszy-1
      |! {! _jk:=1.._lkolumn-1
         |! _kw:=par[2][_im][_jk];
            par[2][_im][_lkolumn]+=_kw;
            par[2][_lwierszy][_jk]+=_kw;
            par[5]+=(_kw<>0)
         !}
      !};
      {? ~par[11]
      || {! _im:=1.._lwierszy-1
         |! par[2][_lwierszy][_lkolumn]+=par[2][_im][_lkolumn]
         !}
      ?}; ~~
   }
   {ENTIRE}
   {IF: par[5] | par[4]}
      {SUBSTITUTE: 'pracownik'}
      {REP: par[1].fhbar(lmt)}
      {WHILE: (par[6]+=1)<=(obj_len(par[2])-par[11])}{DO}
         {IF: (par[6]=(obj_len(par[2])) & par[5]) | par[2][par[6]][obj_len(par[2][par[6]])]<>0 | par[4]}
            {IF: par[6]=obj_len(par[2])}
               {REP: par[1].fbar(lmt)}
            {FI}
            { par[1].ini_line(); ~~ }
            {WHILE: par[1].next()}{DO}
               {REP: par[1].fline(lmt)}
            {OD}
         {FI}
      {OD}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? par[11] || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
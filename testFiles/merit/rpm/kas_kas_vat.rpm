:!UTF-8
{TITLE:B. Dokumenty VAT}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN: WYDRUKI.win_edit('VAT'); WYDRUKI.ZAKRES:=0;
        VAR_DEL.delete('w','PRN','tryb');
        WYDRUKI.RAP_OD:=WYDRUKI.RAP_DO:=WYDRUKI.DOK_OD:=WYDRUKI.DOK_DO:=WYDRUKI.RAP:=null;
        RAPORT.win_dict('RAP_DICT'); DOKUMENT.win_dict('DOK_PRZE');
        rap_od:=rap_do:=dok_od:=dok_do:=brak:=0; w:=obj_new(13); rap:=dok:=-1;
        PRN:=obj_new(@.CLASS.PRINT,13); PRN.sl_frame();
        sum_net:=sum_vat:=sum_bru:=0; czy_dr:=0;
        color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=rsep:=0; vp:=1;
        tryb:=rep_export();
        RAPORT.cntx_psh(); RAPORT.prefix()
}
{END:  &rap_od; &rap_do; &dok_od; &dok_do; &brak; &sum_net; &sum_vat; &sum_bru; &rap; &dok;
       VAR_DEL.delete('w','PRN','tryb'); &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &czy_dr;
       RAPORT.cntx_pop()}
{EXIT: {? ~RAPORT.size()
       || FUN.info('Brak raportów kasowych.\nWykonanie zestawienia niemożliwe.'@); 1
       || ~WYDRUKI.edit("exec('spr_vat','kasa_wydruki')")
       ?}}
{ {? WYDRUKI.ZAKRES=1 || PRN.add("w[1]",5,'Numer rap.'@,1) ?};
  PRN.add("w[2]",6,'Numer dokum.'@,1);
  PRN.add("w[3]",22,'      Operacja kasowa'@);
  PRN.add("w[4]",22,'          Pozycja'@);
  PRN.add("w[11]",20,'       Faktura'@);
  PRN.add("w[12]",20,'     Kontrahent'@);
  PRN.add("w[13]",15,'  NIP kontrah.'@);
  PRN.add("w[5]",16,'     Netto'@,1);
  PRN.add("w[6]",4,'Pr.# VAT'@,,'#');
  PRN.add("w[7]",14,'   Kwota VAT'@,1);
  PRN.add("w[8]",16,'     Brutto'@,1);
  PRN.add("w[9]",8,'  Grupa# podatk.'@,,'#');
  PRN.add("w[10]",35,'  Konto rozliczenia'@);
  {! _i:=1..13 |! w[_i]:='' !};
  ~~
}
{ {? WYDRUKI.ZAKRES=0 & (WYDRUKI.DOK_OD=null | WYDRUKI.DOK_DO=null)
  || DOKUMENT.cntx_psh();
     DOKUMENT.index('RAPORT'); DOKUMENT.prefix(WYDRUKI.RAP);
     {? WYDRUKI.DOK_OD=null & DOKUMENT.first() || WYDRUKI.DOK_OD:=DOKUMENT.ref() ?};
     {? WYDRUKI.DOK_DO=null & DOKUMENT.last() || WYDRUKI.DOK_DO:=DOKUMENT.ref() ?};
     DOKUMENT.cntx_pop()
  || RAPORT.cntx_psh();
     RAPORT.index('NUMER'); RAPORT.prefix();
     {? WYDRUKI.RAP_OD=null & RAPORT.first() || WYDRUKI.RAP_OD:=RAPORT.ref() ?};
     {? WYDRUKI.RAP_DO=null & RAPORT.last() || WYDRUKI.RAP_DO:=RAPORT.ref() ?};
     RAPORT.cntx_pop()
  ?}; ~~
}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{  PAR_WYDR.TITLE:= 'DOKUMENTY VAT'@; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Rok: '@+USRCONST.ROK().NAZ}
{<5 'Nazwa stanowiska kasowego: '@+USRCONST.STANKAS().STANKASN}{ RAPORT.cntx_psh();~~}
{IF: WYDRUKI.ZAKRES=0}{<5 'Raport numer: '@+$WYDRUKI.RAP().NUM_RAP+' od dokumentu: '@+$WYDRUKI.DOK_OD().LP+
' do dokumentu: '@+$WYDRUKI.DOK_DO().LP}
{ELSE}{<5 'Od raportu: '@+$WYDRUKI.RAP_OD().NUM_RAP+' do raportu: '@+$WYDRUKI.RAP_DO().NUM_RAP}
{FI}
{ RAPORT.cntx_pop();~~}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{FONT: 'Q'}{SPACE: 'C'}{IF: vp}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}{ vp:=1;~~}
{FOOTER-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{  rsep:=PAR_WYDR.RSEP; ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{ RAPORT.index('NUMER'); RAPORT.prefix();
  {? WYDRUKI.ZAKRES=0
  || rap_od:=rap_do:=WYDRUKI.RAP().NUM_RAP
  || rap_od:=WYDRUKI.RAP_OD().NUM_RAP; rap_do:=WYDRUKI.RAP_DO().NUM_RAP
  ?};
  DOKUMENT.index('RAPORT'); DOKUMENT.prefix();
  ~~}
{FOR: 'RAPORT'}
{INDEX: 'NUMER'}
{FROM: rap_od}
{TO: rap_do}
{DO}
{ dok_od:=dok_do:=0;
  {? WYDRUKI.ZAKRES=0
  || dok_od:=WYDRUKI.DOK_OD().LP; dok_do:=WYDRUKI.DOK_DO().LP
  || DOKUMENT.prefix(RAPORT.ref);
     dok_od:={? DOKUMENT.first() || DOKUMENT.LP ?}; dok_do:={? DOKUMENT.last() || DOKUMENT.LP ?}
  ?};~~
}
{FOR: 'DOKUMENT'}
{INDEX: 'RAPORT'}
{FROM: RAPORT.ref,dok_od}
{TO: RAPORT.ref,dok_do}
{DO}
{FOR: 'POZDOK'}
{INDEX: 'DOKUMENT'}
{PREFIX: DOKUMENT.ref()}
{DO}
{IF: POZOPER.cntx_psh(); _zwrot:=(-POZDOK.POZOPER().VAT='t'); POZOPER.cntx_pop(); _zwrot}
{ DOKUMENT.cntx_psh();~~}
{FOR: 'VPD'}
{INDEX: 'VPD'}
{PREFIX: POZDOK.ref()}
{DO}
{ w[1]:={? rap<>RAPORT.NUM_RAP | tryb=1|| rap:=RAPORT.NUM_RAP; dok:=-1; $RAPORT.NUM_RAP || '' ?};
  w[2]:={? dok<>DOKUMENT.LP | tryb=1 || dok:=DOKUMENT.LP; $DOKUMENT.LP || '' ?};
  w[3]:=DOKUMENT.OPER().NAZWA;
  w[4]:=POZDOK.POZOPER().NAZWA;
  w[11]:=POZDOK.MF_FIKS;
  w[12]:=POZDOK.KH;
  w[13]:=POZDOK.NIP;
  w[5]:=form(VPD.NETTO,,2,' ,');
  w[6]:=VPD.PR().KOD;
  w[7]:=form(VPD.VAT,,2,' ,');
  w[8]:=form(VPD.BRUTTO,,2,' ,');
  w[9]:=VPD.GRVAT().KOD;
  w[10]:=VPD.R;
  sum_net+=VPD.NETTO; sum_vat+=VPD.VAT; sum_bru+=VPD.BRUTTO; {? lineleft()=0 || vp:=1 ?};
~~}
{SUBSTITUTE: 'LINIA0'}{ vp:=0; czy_dr:=1; PRN.setcolor('255:255:255');~~}
{OD}
{ DOKUMENT.cntx_pop();~~}
{FI}
{OD}
{OD}
{OD}
{IF: tryb<>1}
{IF: lineleft()=1 | lineleft=2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{ENTIRE}{FONT: 'T8GB'}{SPACE: 'C'}
{ w[1]:=w[2]:=w[4]:=w[6]:=w[9]:=w[10]:=w[11]:=w[12]:=w[13]:=''; w[3]:='     RAZEM'@; rsep:=1;
   w[5]:=form(sum_net,,2,' ,'); w[7]:=form(sum_vat,,2,' ,'); w[8]:=form(sum_bru,,2,' ,'); ~~}
{IF: czy_dr}{ {? lineleft()=0 | lineleft()=1 || vp:=1 ?};~~}{SUBSTITUTE: 'LINIAFS'}{ELSE}{SUBSTITUTE: 'LINIAF'}{FI}
{<1>{PRN.width()}'KONIEC WYDRUKU'@}
{ENTIRE-}
{ELSE}
{SUBSTITUTE: 'LINIAF'}
{FI}
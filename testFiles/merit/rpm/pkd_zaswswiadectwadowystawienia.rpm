:!UTF-8
{TITLE:Świadectwa pracy do wystawienia}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('lm','color','lmt','vp','rsep','ll','prn1','tresc','__lp','__data','__TabP','__PRN','__cl','par');
   OSOBA.cntx_psh();
   P.cntx_psh();
   UD_SKL.cntx_psh();
   H_UM.cntx_psh();
   H.cntx_psh();
   RU.cntx_psh();
   par:=obj_new('P','DALEJ');
   par.DALEJ:=exec('uprawnieniawrap','pkd',
      'Przebiegu zatrudnienia'@,
      'PKD_EZK_PPZA',
      'PKD_EZK_RPZA'
   );
   {? +par.DALEJ
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[par.DALEJ])
   || __data:=obj_new('OD','DO');
      __TabP:=tab_tmp(3,
         'DATA','DATE','Data wystawienia',
         'WYD','STRING[16]','Jednostka organizacyjna',
         'T','STRING[9]','Nr teczki',
         'NIM','STRING[72]','Nazwisko i imiona',
         'NAZWISKO','STRING[40]','Nazwisko',
         'IMIONA','STRING[40]','Imiona',
         'UMOWY','SYS_MEMO','Umowy'
      );
      par.P:=exec('sw_do_wystaw','!pkd_zes_orza');
      __PRN:=obj_new(@.CLASS.PRINT,6);
      {? ~rep_export()
      || __PRN.add("__lp+=1",5,'Lp.'@,1,,,,,',,\'9.\'')
      ?};
      __PRN.add("__TabP.DATA$4",10,'Data'@);
      __PRN.add("__TabP.WYD",16,'Jednostka organizacyjna'@);
      __PRN.add("__TabP.T",9,'Nr teczki'@);
      {? rep_export()
      || __PRN.add("__TabP.NAZWISKO",30,'Nazwisko'@);
         __PRN.add("__TabP.IMIONA",41,'Imiona'@)
      || __PRN.add("__TabP.NIM",50,'Nazwisko i imiona'@)
      ?};
      __PRN.add("__TabP.memo_txt(,1,'UMOWY')",26,'Umowy'@,,'^');
      __PRN.s_frame();
      __cl:=obj_new('T8','T8G');
      __lp:=lmt:=lm:=0;
      tresc:='zaswswl';
      prn1:='__PRN';
      ll:=rsep:=vp:=1;
      color:='';
      OSOBA.prefix();
      UD_SKL.prefix();
      H_UM.index('OD');
      H.index('HISTUM');
      RU.prefix()
   ?}
}
{END:
   RU.cntx_pop();
   H.cntx_pop();
   H_UM.cntx_pop();
   UD_SKL.cntx_pop();
   P.cntx_pop();
   OSOBA.cntx_pop();
   VAR_DEL.delete('lm','color','lmt','vp','rsep','ll','prn1','tresc','__lp','__data','__TabP','__PRN','__cl','par')
}
{COMMENT}---------------------------------------------------------------------------------------------------------------
Raport ma za zadanie pokazać dla wybranych pracownikow terminy wystawienia swiadectw pracy w zwiazku z
Ustawa z dnia 5 stycznia 2011r. o zmianie ustawy - Kodeks pracy (Dz.U. z 2011r. Nr 36, poz. 181).
Nowelizacja stwarza mozliwosci wystawienia "zbiorczego swiadectwa pracy" z wielu nastepujacych po sobie umow
terminowych. Swiadectwo takie musi byc wystawione po 24 m-cach od PODPISANIA pierwszej z tych umow. Jezeli jednak w tym
dniu obowiazuje kolejna umowa terminowa - swiadectwo wystawiane jest w dniu rozwiazania lub wygasniecia tejze umowy.
W zwiazku z powyzszym, jako dobre praktyki nalezy przyjac:
- Podpisywanie umow w pierwszym dniu obowiazywania (H_UM.PODPIS=H_UM.OD);
- Wystawianie swiadectwa pracy przy zmianie rodzaju umowy z terminowej na czas nieokreslony.

Zalozenia:
- Umowa na czas okreslony NIE wystepuje po umowie na czas nieokreslony.
--------------------------------------------------------------------------------------------------------------{COMMENT-}
{COMMENT}OLD: zaswswl.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: +par.DALEJ}
{EXIT: ~par.P.first()}
{ PAR_WYDR.TITLE:='Świadectwa pracy\ndo wystawienia w okresie od %1r. do %2r.'@[__data.OD$4,__data.DO$4]; ~~ }
{INCLUDE: wsp_pw.rpi}
{FONT: 'T8G'}{ __cl.T8G:=charleft(); ~~ }
{FONT: 'T8'}{SPACE:'B'}{ __cl.T8:=charleft(); lmt:=int((__cl.T8-__PRN.width())/2); ~~ }
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO:tresc}
   {  __TabP.blank();
      __TabP.WYD:=P.WYDZIAL().SYMBOL;
      __TabP.T:=P.T;
      __TabP.NIM:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE;
      __TabP.NAZWISKO:=P.OSOBA().NAZWISKO;
      __TabP.IMIONA:=P.OSOBA().PIERWSZE+' '+P.OSOBA().DRUGIE;
      _par:=exec('czy_swiadectwo','!pkd_zes_orza');
      {? _par.DSWP<>#0
      || __TabP.DATA:=_par.DSWP;
         __TabP.memo_set(_par.UMOWY,'UMOWY');
         {? __TabP.add() || __TabP.memo_put() ?}
      ?};
      ~~
   }
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FOR: par.P}{DO}
   {IF: P.seek(par.P.P,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8B'}{SPACE: 'B'}
   {LINE}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FOOTER: 0}
   {FONT: 'T8'}{SPACE: 'B'}
   {SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FOR:__TabP}
{FROM:__data.OD}
{TO:__data.DO}
{DO}
   {SUBSTITUTE: 'LINIA0'}
{OD}
:!UTF-8
{TITLE:Okresy zatrudnienia}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='pkd_wstaz';
   PAR_WYDR.TITLE:='Okresy poprzedniego zatrudnienia'@;
   P.cntx_psh();
   par:=obj_new(6);
   par[6]:=exec('uprawnieniawrap','pkd','okresach poprzedniego zatrudnienia'@,'PKD_DOS_PPSW','PKD_DOS_PRSW');
   {? +par[6]
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[par[6]])
   || par[1]:=obj_new(@.CLASS.PRINT,{? ~rep_export() || 7 || 14 ?});
      {? rep_export()
      || par[1].add("STAZ.OSOBA().NAZWISKO",30,'Nazwisko'@);
         par[1].add("STAZ.OSOBA().PIERWSZE+' '+STAZ.OSOBA().DRUGIE",41,'Imiona'@);
         par[1].add("STAZ.OSOBA().PESEL",11,'PESEL'@)
      ?};
      par[1].add("STAZ.F",50,'Pracodawca'@);
      par[1].add("STAZ.STN().ST",30,'Stanowisko'@);
      par[1].add("STAZ.DR$1",10,'Od dnia'@);
      par[1].add("STAZ.DZ$1",10,'Do dnia'@);
      {? ~rep_export()
      || par[1].add("form(STAZ.PRY,-2)+'/'+form(STAZ.PRM,-2)+'/'+form(STAZ.PRD,-2)",8,'Do stażu'@);
         par[1].add("form(STAZ.PSY,-2)+'/'+form(STAZ.PSM,-2)+'/'+form(STAZ.PSD,-2)",8,'Poza stażem'@)
      || par[1].add("STAZ.PRY",8,'Do stażu - lata'@,1);
         par[1].add("STAZ.PRM",8,'Do stażu - miesiące'@,1);
         par[1].add("STAZ.PRD",8,'Do stażu - dni'@,1);
         par[1].add("STAZ.PSY",8,'Poza stażem - lata'@,1);
         par[1].add("STAZ.PSM",8,'Poza stażem - miesiące'@,1);
         par[1].add("STAZ.PSD",8,'Poza stażem - dni'@,1)
      ?};
      par[1].add("exec('uspkod','personel',STAZ)",38,'Rozwiązanie umowy'@);
      par[1].s_frame();

      par[2]:=tab_tmp(,'REF','INTEGER','Osoba');
      prn1:='par[1]';
      par[3]:=par[5]:=lmt:=rsep:=0;
      par[4]:=b_rat:=h_rat:=vp:=1;

      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat')
}
{COMMENT}OLD: wststaz5.rpm{COMMENT-}
{COMMENT}OLD: std_osop.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: +par[6]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
:: ---------------------------------------------------------------------------------------------------------------------
::
::   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A
::
:: ---------------------------------------------------------------------------------------------------------------------
{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
:: ---------------------------------------------------------------------------------------------------------------------
::
::   M A K R O D E F I N I C J E
::
:: ---------------------------------------------------------------------------------------------------------------------
{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par[5]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[3]:=1; ~~ }
{MACRO-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[2].add();
      par[3]:=0; ~~
   }
   {ENTIRE}
   {FOR: STAZ}{INDEX: '_STAZE'}{PREFIX: exec('ref_firma','ustawienia'),P.OSOBA}{DO}
   {FIRST}
      {SUBSTITUTE: 'pracownik'}
      {REP: par[1].fhbar(lmt)}
   {FIRST-}
      { par[1].ini_line(); ~~ }
      {WHILE: par[1].next()}{DO}
         {REP: par[1].fline(lmt)}
      {OD}
   {OD}
   {IF: par[3]}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
:: ---------------------------------------------------------------------------------------------------------------------
::
::   Z A W A R T O Ś Ć   W Y D R U K U
::
:: ---------------------------------------------------------------------------------------------------------------------
{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}

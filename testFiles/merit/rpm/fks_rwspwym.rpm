:!UTF-8
{TITLE:K. Zestawienie rozrachunków z wymaganym split payment}
{PRINTER: FINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   {? var_press('SP_WYM',DOK)<=0
   || FUN.info('Brak naniesionych zmian w definicji systemu\nzwiązanych z wymaganym split payment.');
      return()
   |? SSTALE.WAL<>FINFO.NAROD
   || FUN.info('Rozrachunki z wymaganym split payment dotyczą tylko waluty narodowej.');
      return()
   ?};
   VAR_DEL.delete('repexp','OpSp');repexp:=rep_export();
   PRN1:=obj_new(@.CLASS.PRINT,11); PRN1.sl_frame(); w:=obj_new(11);
   PRN2:=obj_new(@.CLASS.PRINT,1); PRN2.sl_frame(); s:=obj_new(5);
   PRN3:=obj_new(@.CLASS.PRINT,6); PRN3.sl_frame(); r:=obj_new(5);
   s_sign:=color:=''; prn1:=prn2:='PRN1'; lm:=ll:=lmt:=rsep:=0; vp:=1;
   rwn:=rma:=drukuj:=all:=one:=0; han:=null;
   {! _i:=1..obj_len(w) |! w[_i]:='' !};
   {! _i:=1..obj_len(r) |! r[_i]:=0 !};
   OpSp:=exec('obj_opsp','fks_sp');
   ~~
}
{END:
   VAR_DEL.delete('PRN1','PRN2','PRN3','w','s','r','DRB','repexp','OpSp');
   VAR_DEL.delete('s_sign','color','prn1','prn2','lm','ll','vp','lmt','rsep','han','rwn','rma');
   VAR_DEL.delete('drukuj','all','one','i');
   ~~
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: rwspwym.rpm
Utworzony:   13.06.2019 [MB]
==================================================
Zawartosc: Zestawienie rozrachunków z wymaganym split payment
{COMMENT-}
{  {? var_press('SP_WYM',DOK)>0
   || KONTO.K1:=KONTO.K2:=KONTO.K3:=KONTO.K4:='';
      KONTO.K1_AE:=KONTO.K2_AE:=KONTO.K3_AE:=KONTO.K4_AE:='';
      KONTO.K1_RODZ:=2; KONTO.K2_RODZ:=KONTO.K3_RODZ:=KONTO.K4_RODZ:=3;
      KONTO.K1_SYNT:=KONTO.K2_SYNT:=KONTO.K3_SYNT:=KONTO.K4_SYNT:=KONTO.K1_WYM:=KONTO.K2_WYM:=KONTO.K3_WYM:=KONTO.K4_WYM:=0;
      KONTO.K1_BE:='UNPAR.P1=1';
      KONTO.K2_BE:='UNPAR.P1=2';
      KONTO.K3_BE:=KONTO.K4_BE:='UNPAR.P1=3';
      UNPAR.P1:=1; UNPAR.P3:=1;
      UNPAR.P1_BV:=UNPAR.P1_BE:=UNPAR.P3_BE:='';
      UNPAR.P1_AE:='{? UNPAR.P1=1 || KONTO.K2:=KONTO.K3:=KONTO.K4:=\'\' |? UNPAR.P1=2 || KONTO.K1:=KONTO.K3:=KONTO.K4:=\'\''
         +'|? UNPAR.P1=3 || KONTO.K1:=KONTO.K2:=\'\' ?}';
      UNPAR.P3_AE:='';
      PAR_WYDR.win_edit('RWSPWYM');
      PAR_WYDR.KHKH:=null;
      UNPAR.P4_AE:=UNPAR.P5_AE:='{? chk_fld() || {? ~exec(\'dat_not\',\'dok_fks\',fld) || FUN.info(\'Data spoza aktualnego roku.\'); 0 || 1 ?} ?}';
      UNPAR.P4_BV:=UNPAR.P4_BE:=UNPAR.P5_BV:=UNPAR.P5_BE:='';
      UNPAR.P4:=SSTALE.AO().POCZ;
      UNPAR.P5:=SSTALE.AO().KON;
      PAR_WYDR.T_ROZ:='';
      KH.win_dict('WERHOME2')
   ?};
   ~~
}
{EXIT: var_press('SP_WYM',DOK)<=0 | SSTALE.WAL<>FINFO.NAROD |
       ~PAR_WYDR.edit("{? UNPAR.P4>UNPAR.P5 || FUN.info('Nieprawidłowy zakres dat.'); 'P4' || 1 ?}")
}
{INCLUDE: wsp_pw.rpi}
{  OpSp.getAll();
   {? repexp
   || PRN1.add("KH.SKR+' - '+KH.NAZ",8,'Kontrahent')
   ?};
   PRN1.add("w[9]",8,'Jed. ks.');
   PRN1.add("w[1]",51,'Rozrachunek');
   PRN1.add("w[2]",10,'Data otwarcia');
   PRN1.add("w[3]",10,'Termin płatności');
   PRN1.add("w[10]",3,'Typ');
   PRN1.add("w[4]",16,'Stan Wn na koniec zakresu',1);
   PRN1.add("w[5]",16,'Stan Ma na koniec zakresu',1);
   PRN1.add("w[6]",16,'Wymagany split payment',1);
   PRN1.add("w[7]",16,'Zapłacono split payment',1);
   PRN1.add("w[8]",16,'Zapłata standardowa',1);
   PRN2.add("w[1]",140,'');
   PRN3.add("w[1]",86,'',,,,,,,,6);
   PRN3.add("form(s[1],,2,' ,')",16,'',1);
   PRN3.add("form(s[2],,2,' ,')",16,'',1);
   PRN3.add("form(s[3],,2,' ,')",16,'',1);
   PRN3.add("form(s[4],,2,' ,')",16,'',1);
   PRN3.add("form(s[5],,2,' ,')",16,'',1);
   PAR_WYDR.TITLE:='Zestawienie rozrachunków z wymaganym split payment';

   ~~
}
{IF: ~repexp}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{HEADER}
{ prn1:='PRN1'; rsep:=1;~~}
{SUBSTITUTE: 'HEAD'}{SPACE: 'C'}

{IF: page()=1}
{FONT: 'T8G'}{SPACE: 'C'}
{<1 'PARAMETRY WYDRUKU:'}
{<3 'Zakres dat od '+$UNPAR.P4+' do '+$UNPAR.P5}
{<3 'Zakres kont '+{? UNPAR.P1=1 || ', maska: '+KONTO.K1
                   |? UNPAR.P1=2 || ', prefix: '+KONTO.K2
                   |? UNPAR.P1=3 || ', zakres od: '+KONTO.K3+' do: '+KONTO.K4
                   ?}
}
{<3 'Rozrachunki '+{? UNPAR.P3=1 || 'rozliczone'
                   |? UNPAR.P3=2 || 'nierozliczone'
                   |? UNPAR.P3=3 || 'przeterminowane'
                   || 'wszystkie' ?}+
    {? UNPAR.P1<4 || ' na dzień '+$UNPAR.P5 || '' ?}
}
{<3 {? OPERATOR.DEPT || 'Jednostka księgowa '+OPERATOR.DEPT().OD || 'Wszystkie jednostki księgowe' ?} }
{<3 {? PAR_WYDR.KHKH || 'Kontrahent '+PAR_WYDR.KHKH().SKR+' - '+PAR_WYDR.KHKH().NAZ || 'Kontrahenci wszyscy' ?} }
{<3 {? PAR_WYDR.T_ROZ='' || 'Wszystkie typy rozrachunków' || 'Rozrachunki typu: '+PAR_WYDR.T_ROZ ?} }
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
{HEADER-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8G'}{SPACE: 'C'}{prn1:=prn2;~~}
{SUBSTITUTE: 'LINIAF'}{prn1:='PRN1';~~}
{FOOTER-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{MACRO: 'STRONA'}
{IF: lineleft()<2 & lineleft()<>0}{PAGE}{prn1:='PRN1';~~}
{ELSE}
{IF: lineleft()=0}{prn1:='PRN1';~~}{ELSE}{prn1:=prn2;~~}{FI}
{FI}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{MACRO: 'OP'}
{  drukuj:=OpSp.get(OP.ref());
   {? drukuj
   || w[9]:=OP.ODD().OD;
      w[10]:=OP.TYP;
      w[1]:=OP.SYM;
      w[2]:=$OP.DO;
      w[3]:=$OP.TZ;
      w[4]:=form(F.rwn,,2,' ,'); s[1]+=F.rwn;
      w[5]:=form(F.rma,,2,' ,'); s[2]+=F.rma;
      w[6]:=form(OpSp.sp_wym,,2,' ,'); s[3]+=OpSp.sp_wym;
      w[7]:=form(OpSp.zap_sp,,2,' ,'); s[4]+=OpSp.zap_sp;
      w[8]:=form(OpSp.zap_std,,2,' ,'); s[4]+=OpSp.zap_std
   ?};
   ~~
}
{IF: drukuj}
{prn2:='PRN1';~~}
{SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
{FI}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{MACRO: 'KH'}
{  one:=0;
   {! _i:=1..obj_len(s) |! s[_i]:=0 !};
   {? OpSp.first()
   || {!
      |? drukuj:=OpSp.get();
         ~drukuj & OpSp.next()
      !}
   ?}; ~~
}
{IF: drukuj}
   { prn2:='PRN2'; w[1]:=KH.SKR+' - '+KH.NAZ; rsep:=1; one:=0; drukuj:=OpSp.first(); ~~}
   {IF: ~repexp}
   {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
   {FI}
   {WHILE: drukuj}
   {DO}
      {SUBSTITUTE: 'OP'}{one+=drukuj; drukuj:=OpSp.next(); ~~}
   {OD}
   { {! _i:=1..obj_len(s) |! r[_i]+=s[_i] !}; ~~}
   {IF: one>1 & ~repexp}
      {w[1]:='Suma'; prn2:='PRN3';~~}{FONT: 'T8GB'}
      {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}{FONT: 'T8G'}
   {FI}
{FI}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{IF: PAR_WYDR.KHKH}
   {PAR_WYDR.KHKH();~~}
   {SUBSTITUTE: 'KH'}{all+={? one || 1 ?};~~}
{ELSE}
   {FOR: 'KH'}
   {INDEX: 'SKR'}
   {PREFIX: 2}
   {DO}
      {SUBSTITUTE: 'KH'}{all+={? one || 1 ?};~~}
   {OD}
{FI}
{IF: all<>1}
{w[1]:='Razem'; prn2:='PRN3'; {! _i:=1..obj_len(s) |! s[_i]:=r[_i] !}; ~~}{FONT: 'T8GB'}
{SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}{FONT: 'T8G'}
{FI}
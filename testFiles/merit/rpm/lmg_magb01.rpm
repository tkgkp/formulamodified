:!UTF-8
{TITLE:A. Zbiorcze zestawienie pozycji dokumentów magazynowych zaakceptowanych}
{PRINTER: exec('prt','param_wydr','v'),exec('nopanel','param_wydr','M'),,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,exec('get','#params',1121,2,OPERATOR.USER)<>'T'}
{BEGIN:
   lmg_mag_02:="
      VAR_DEL.delete('PRN','PRP','PRNS','w','__poz','__nag','__tab_sel','dokl');
      VAR_DEL.delete('color','prn1','prn2');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('wyjscie')";
   lmg_mag_02();

   _graf:=exec('get','#params',1121,2,OPERATOR.USER)<>'T';

   PRN:=obj_new(@.CLASS.PRINT,6);   {? _graf || PRN.s_frame() || PRN.t_frame() ?};
   PRP:=obj_new(@.CLASS.PRINT,5);   {? _graf || PRP.s_frame() || PRP.t_frame() ?};
   PRNS:=obj_new(@.CLASS.PRINT,2);  {? _graf || PRNS.s_frame() || PRNS.t_frame() ?};

   w:=obj_new(2);
   dokl:=0; "--dokladnosc ilosci--";

   'zmienne pomocnicze do wydrukow graficznych';
   color:='';
   lm:=ll:=vp:=lmt:=0; rsep:=PAR_WYDR.RSEP;

   wyjscie:=0;

   VAR_DEL.delete('__poz','__nag','__tab_sel');

   PAR_WYDR.TITLE:='Zbiorcze zestawienie pozycji dokumentów magazynowych'
}
{END:
   lmg_mag_02(); VAR_DEL.delete('lmg_mag_02')
}
{INCLUDE: wsp_pw.rpi}
{
   w[1]:=w[2]:=0;

   __nag:=tab_tmp(1
      ,'SYM','STRING[20]','Symbol'
      ,'D','DATE','Data'
      ,'KH','STRING[20]','Kontrahent'
      ,'FAKTURA','STRING[255]','Faktura'
      ,'MG','STRING[8]','Magazyn'
   );
   __poz:=tab_tmp(3
      ,'INDEKS','STRING[50]','Indeks'
      ,'JM','STRING[8]','Jm'
      ,'NAZWA','STRING[100]','Nazwa'
      ,'IL','REAL','Ilość'
   );

   _gen_fml:="
         {? ND.STAT_REJ='T'
         ||
            __nag.blank();
            __nag.SYM:=ND.SYM;
            __nag.D:=ND.D;
            __nag.KH:=ND.KH().KOD+' - '+ND.KH().SKR;
            __nag.MG:=ND.MAG().SYM;
            {? ND.FAKS=null
            ||
               {? ND.FAKS_REF<>''
               ||
                  FAKS.cntx_psh();
                  FAKS.use(8+ND.FAKS_REF);
                  FAKS.prefix();
                  {? FAKS.seek(BB.sqlint(ND.FAKS_REF),8+ND.FAKS_REF)
                  || __nag.FAKTURA:=FAKS.SYM
                  ?};
                  FAKS.cntx_pop()
               ?};
               DK.index('DOKMAG'); DK.prefix(ND.ref());
               {? DK.first()
               || _faktury:='';
                  {!
                  |? FAP2DK.index('NDK'); FAP2DK.prefix($DK.ref());
                     {? FAP2DK.first()
                     || {!
                        |? _sep:={? __nag.FAKTURA='' || '' || ' ' ?};
                           FAKS.cntx_psh();
                           FAKS.use(8+FAP2DK.FAKS);
                           FAKS.prefix();
                           {? FAKS.seek(BB.sqlint(FAP2DK.FAKS),8+FAP2DK.FAKS)
                           ||
                              {? (_faktury*($FAKS.ref()))=0
                              || __nag.FAKTURA+=_sep+FAKS.SYM;
                                 _faktury+=$FAKS.ref()
                              ?}
                           ?};
                           FAKS.cntx_pop();
                           FAP2DK.next()
                        !}
                     ?};
                     DK.next()
                  !}
               ?}
            ||
               __nag.FAKTURA:=ND.FAKS().SYM
            ?};
            {? __nag.add()
            ||
               DK.index('DOKMAG');
               DK.prefix(ND.ref());
               {? DK.first()
               ||
                  {!
                  |?
                     __poz.prefix(DK.M().KTM,DK.JM().KOD,DK.M().N);
                     {? __poz.first()
                     ||
                        __poz.IL+=DK.IL;
                        __poz.put()
                     ||
                        __poz.blank();
                        __poz.INDEKS:=DK.M().KTM;
                        __poz.JM:=DK.JM().KOD;
                        __poz.NAZWA:=DK.M().N;
                        __poz.IL:=DK.IL;
                        __poz.add()
                     ?};

                     DK.next()
                  !}
               ?}
            ?}
         ?}
   ";

   _mix:='';

   DK.cntx_psh();
   ND.cntx_psh();
   FAKS.cntx_psh();
   __tab_sel:=ND.sel_aget();
   {? __tab_sel.first()
   ||
      {!
      |?
         {? ND.seek(__tab_sel.REF,)
         ||
            {? _mix*ND.TYP().P=0 || _mix+=ND.TYP().P ?};
            _gen_fml()
         ?};
         __tab_sel.next()
      !}
   ||
      ND.get();
      _gen_fml()
   ?};
   DK.cntx_pop();
   ND.cntx_pop();
   FAKS.cntx_pop();

   PRN.add("__nag.SYM",20,'Symbol'@);
   PRN.add("__nag.MG",9,'Magazyn'@);
   PRN.add("$__nag.D",10,'Data'@);
   PRN.add("__nag.KH",20,'Kontrahent'@);
   PRN.add("__nag.FAKTURA",20,'Faktura'@,,' ');

   PRP.add("w[1]",5,'Lp.'@,1);
   PRP.add("__poz.INDEKS+' - '+__poz.NAZWA",75,'Materiał lub usługa'@);
   PRP.add("__poz.JM",5,'jm'@);
   PRP.add("form(__poz.IL,,dokl)",12,'Ilość (suma)'@,1);

   PRNS.add("'Suma'",50);
   PRNS.add("form(w[2],,3)",12,,1);

   {? _mix='NT' | _mix='TN'
   || wyjscie:={? FUN.ask('Wybrano dokumenty przychodowe i rozchodowe.\nKontynuować?'@) || 0 || 1 ?}
   ?}
;~~}
{EXIT: wyjscie=1}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ prn1:='PRN'; lmt:=int((charleft()-PRP.width())/2); ~~}
{<{lmt+1}}{'Wybrane dokumenty (zaakceptowane):'@}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{FOR: '__nag'}
{DO}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: lineleft()}
   {FOOTER: 0}{FOOTER-}
   {SUBSTITUTE: 'LINIAF'}
{FI}
{
prn1:='PRP'; prn2:='PRNS';
il_lin:=1;
{? __poz.first()
|| _q:=PRP.ini_line();
   il_lin:=PRP.length()
?};
~~
}
{IF: lineleft()>5+il_lin}

   {FONT: 'T8GB'}
   {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{ELSE}
   {PAGE}
{FI}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: '__poz'}
{DO}
   {FIRST}
   { dokl:=exec('fld_prec','#field',__poz,'IL');~~}
   {FIRST-}
   { w[1]+=1; w[2]+=__poz.IL; ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
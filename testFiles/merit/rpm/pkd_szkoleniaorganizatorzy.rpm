:!UTF-8
{TITLE:Lista organizatorów szkoleń}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='Lista organizatorów szkoleń'@;
   VAR_DEL.delete('PRNS','PRNO','lmt','rsep','vp','fp','test','par');
   SZK_ORG.cntx_psh();
   SZK_OPIS.cntx_psh();
   SZK_OPIS.index('ORG_DATA');
   par:=obj_new('ORG','ZAK','INF','OD','DO');
   par.ORG:=exec('szkolenia_org','szkolenia',par);
   PRNO:=obj_new(@.CLASS.PRINT,5);
   PRNO.add("SZK_ORG.NAZWA",MS.fld_len('SZK_ORG','NAZWA'),'Nazwa'@);
   PRNO.add("SZK_ORG.MIASTO",MS.fld_len('SZK_ORG','MIASTO'),'Miasto'@);
   PRNO.add("SZK_ORG.ULICA",MS.fld_len('SZK_ORG','ULICA'),'Ulica'@);
   PRNO.add("SZK_ORG.TEL",MS.fld_len('SZK_ORG','TEL'),'Telefon'@);
   PRNO.add("SZK_ORG.FAX",MS.fld_len('SZK_ORG','FAX'),'Fax'@);
   PRNO.s_frame();
   {? par.INF>1
   || PRNS:=obj_new(@.CLASS.PRINT,4+{? par.INF=3 || 4 ?});
      PRNS.add("SZK_OPIS.TEMAT().TEMAT",MS.fld_len('SZK_TEM','TEMAT')/2,'Temat'@);
      PRNS.add("SZK_OPIS.OD$1",10,'Od dnia'@);
      PRNS.add("SZK_OPIS.DO$1",10,'Do dnia'@);
      PRNS.add("SZK_OPIS.SUMA",12,'Koszt'@,1,,,,,'12,2');
      {? par.INF=3
      || PRNS.add("SZK_OPIS.MIEJSCE",MS.fld_len('SZK_OPIS','MIEJSCE')/2,'Miejsce'@);
         PRNS.add("SZK_OPIS.MIASTO",MS.fld_len('SZK_OPIS','MIASTO')-1,'Miasto'@);
         PRNS.add("SZK_OPIS.ULICA",MS.fld_len('SZK_OPIS','ULICA')-1,'Ulica'@);
         PRNS.add("exec('memo2print','#string',SZK_OPIS,'OPIS')",38,'Uwagi'@)
      ?};
      PRNS.s_frame()
   ?};
   SZK_ORG.win_sel('SLO');
   lmt:=rsep:=test:=0;
   vp:=fp:=1
}
{END:
   SZK_OPIS.cntx_pop();
   SZK_ORG.cntx_pop();
   VAR_DEL.delete('PRNS','PRNO','lmt','rsep','vp','fp','test','par')
}
{COMMENT}OLD: szk_org.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par.ORG.size()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: fp}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { fp; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Informacje o szkoleniu:'@} {
      {? par.INF=2 || 'Skrócone'@
      |? par.INF=3 || 'Pełne'@
      || 'Brak'@
      ?}}
   {IF: par.INF>1}
      {<{lmt+1}'Szkolenia zorganizowane: %1 do %2'@[par.OD$1,par.DO$1]}
   {FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{ PRNO.setcolor(color); PRNO.ini_head()}
{REP: PRNO.fhbar(lmt)}
{WHILE: PRNO.h_next()}{DO}
   {REP: '{<{lmt+1}}'+PRNO.h_fline(0)}
{OD}
{IF: par.INF>1}
   {REP: '{<{lmt+1}}'+PRNO.fjbar(PRNS)}
   {PRNS.ini_head()}
   {WHILE: PRNS.h_next()}{DO}
      {REP: '{<{lmt+1}}'+PRNS.h_fline(0)}
   {OD}
{ELSE}
   {REP: PRNO.fbar(lmt)}
   {PRNO.setcolor('255:255:255')}
{FI}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: {? par.INF>1 || PRNS || PRNO ?}.flbar(lmt)}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'org_dane'}
{ test:=1; ~~ }
{IF: par.INF>1}
   {REP: '{<{lmt+1}}'+PRNS.fjbar(PRNO)}
{FI}
{ PRNO.ini_line(); ~~ }
{WHILE: PRNO.next()}{DO}
   {REP: PRNO.fline(lmt)}
{OD}
{IF: par.INF>1}
   {REP: '{<{lmt+1}}'+PRNO.fjbar(PRNS)}
{FI}
{MACRO-}
{MACRO: 'szk_dane'}
{ENTIRE}
{ test:=0; ~~ }
{FOR: SZK_OPIS}{INDEX: 'ORG_DATA'}{PREFIX: SZK_ORG.ref()}{DO}
   {IF: {? par.OD<=SZK_OPIS.OD  || {? par.DO<>#0 || SZK_OPIS.OD<=par.DO || 1 ?} ?} | ~par.ZAK}
      {IF: ~test}
         {SUBSTITUTE: 'org_dane'}
      {FI}
      {IF: par.INF>1}
         { PRNS.ini_line(); ~~ }
         {WHILE: PRNS.next()}{DO}
            {REP: PRNS.fline(lmt)}
         {OD}
      {FI}
   {FI}
{OD}
{ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{  lmt:=int((charleft()-PRNO.width())/2);
   {? var_press('PRNS')=type_of(PRNO)
   || _lmt:=int((charleft()-PRNS.width())/2);
      {? _lmt<lmt || lmt:=_lmt ?}
   ?}; ~~
}
{FOR: par.ORG}
 {DO}
  {IF: SZK_ORG.seek(par.ORG.REF,,1)}
    {SUBSTITUTE: 'szk_dane'}
  {FI}
 {OD}
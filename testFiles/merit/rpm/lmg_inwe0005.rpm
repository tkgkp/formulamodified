:!UTF-8
{TITLE:E. Arkusz spisu z natury (wg palet)}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   wyjscie:=0;
   {? ST.MAG().PAL<>'T'
   ||
      FUN.info('Magazyn bez obsługi palet.\nWydruk arkusza niedostępny.'@);
      wyjscie:=1
   |? INN.ARKPOM=0
   ||
      FUN.info('Inwentaryzacja bez arkuszy pomocniczych.\nWydruk niedostępny.'@);
      wyjscie:=1
   ||
      color:=prn1:='';
      lm:=ll:=lmt:=0;rsep:=1;vp:=1;

      VAR_DEL.delete('w','h','k','PRN','komisja');
      w:=obj_new(7);
      h:=obj_new(7);
      k:=obj_new(7);
      dokl:=0; "--dokladnosc ilosc--";
      lp:=0; ile:=0; wsk:=0; sumse:=0;
      PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
      ii:=0;

      h[1] := 'Poz.'@;
      h[2] := 'Kod palety'@;
      h[3] := 'Pozycje palety (Indeks [ilość])'@;
      h[4] := 'Kod dostawy'@;
      h[5] := 'Typ palety'@;
      h[6] := 'Czy jest [x]'@;
      h[7] := 'Uwagi'@;

      lpod:=lpdo:=0;

      wydr_naz:='inwe0005';
      wydr_edi:='INWE0005';

      WYDRUKIL.index('WYDRUKI');
      WYDRUKIL.prefix(OZ.USER,wydr_naz);
      {? ~WYDRUKIL.first()
      || WYDRUKIL.blank();
         WYDRUKIL.USERS:=OZ.USER;
         WYDRUKIL.WYDRUK:=wydr_naz;
         WYDRUKIL.add()
      ?};
      WYDRUKIL.win_edit(wydr_edi);
      wyjscie:=1;
      {? WYDRUKIL.edit("
         {? WYDRUKIL.CHKBOX05<0
         ||
            FUN.info('Ilość stron arkusza spisu z natury nie może być ujemna.'@); 'CHKBOX05'
         ||
            {? WYDRUKIL.CHKBOX01=0
            || {? WYDRUKIL.CHKBOX05=0
               || FUN.info('Należy zaznaczyć opcję Pozycje lub wypełnić pole Ilość pustych stron.'@);'CHKBOX01'
               || ~~
               ?}
            || ~~
            ?}
         ?}")
      ||
         {? WYDRUKIL.put()
         || wyjscie:=0;
            ZAKR.TA:=WYDRUKIL.CHKBOX01;
            ZAKR.ZD:=WYDRUKIL.CHKBOX02;
            ZAKR.OR:=WYDRUKIL.CHKBOX03;
            ZAKR.IS:=WYDRUKIL.CHKBOX04;
            ZAKR.IP:=WYDRUKIL.CHKBOX05
         ?}
      ?};

      k[1] := 4;
      k[2] :=20;
      k[3] :=50;
      k[4] :=20;
      k[5] := 6;
      k[6] := 5;
      k[7] :=30

   ?};
   INN.cntx_psh()
}
{END:
   INN.cntx_pop();
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep');
   VAR_DEL.delete('w','h','k','PRN','dokl','komisja');
   VAR_DEL.delete('lp','ile','wsk','sumse');
   VAR_DEL.delete('lpod','lpdo');
   VAR_DEL.delete('ii','wyjscie')
}
{EXIT: wyjscie}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   MACRO danepoz                                            {COMMENT-}
{MACRO: 'danepoz'}
{
w[1]:=lp;
w[2]:=SL.PAL().KODK;
w[3]:='';
{? PAL.AKT='N' || PAL_POZ.use('palet'+form(PAL.AR-2000,-2,0,'99')) || PAL_POZ.use('paletyp') ?};
PAL_POZ.index('PAL');
PAL_POZ.prefix(PAL.ref());
{? PAL_POZ.first()
|| {!
   |? w[3]+=PAL_POZ.M().KTM+' ['+form(PAL_POZ.ILP,,PAL_POZ.M().DOKL)+' '+PAL_POZ.M().J().KOD+'], ';
      PAL_POZ.next()
   !}
?};
w[4]:=SL.PAL().SCEAN;
w[5]:=SL.PAL().TPAL().TYP;
w[6]:='[   ]';
w[7]:='';
~~}
{MACRO-}
{
pusta:=' ';
PRN.add("w[1]",k[1],h[1],1);
PRN.add("w[2]",k[2],h[2]);
PRN.add("w[3]",k[3],h[3]);
PRN.add("w[4]",k[4],h[4]);
PRN.add("w[5]",k[5],h[5]);
PRN.add("w[6]",k[6],h[6]);
PRN.add("w[7]",k[7],h[7]);
prn1:='PRN';
rsep:=PAR_WYDR.RSEP
;~~}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{IF: ZAKR.TA=1}
{COMMENT}   wypelniony arkusz                                        {COMMENT-}
{COMMENT}   HEADER                                                   {COMMENT-}
   {HEADER}
   {FONT: 'T8GB'}{SPACE: 'C'}
   { PAR_WYDR.TITLE:='Arkusz spisu z natury (wg palet) '+INN.SYM+' z dnia '+form(INN.DI)+
      '\nMagazyn: '+INN.MG().SYM+' - '+INN.MG().NAZ;~~}
   {SUBSTITUTE: 'HEAD'}
   {IF: page()=1}
      {FONT: 'T10'}{SPACE: 'C'}

                                        Rodzaj inwentaryzacji  ________________________

                                        Sposób przeprowadzenia ________________________

{<1'Skład komisji inwentaryzacyjnej:'@}{<50'Inne osoby obecne przy spisie:'@}

{IF: exec('FindInSet','#table','INNKOM','INN',INN.ref(),,,,,null())=null()}
{<1}_________________________________{<50}________________________________

{<1}_________________________________{<50}________________________________

{<1}_________________________________{<50}________________________________
{ELSE}
   {FOR: INNKOM}
   {INDEX: 'INN'}
   {PREFIX: INN.ref()}
   {DO}
      {_przew:={? INNKOM.P='T' || 'Przewodniczący-'@ || '' ?};
       _osoba:='%1 %2 %3'[INNKOM.OSOBA().NAZWISKO,INNKOM.OSOBA().PIERWSZE,INNKOM.OSOBA().DRUGIE];
       komisja:='%1%2'[_przew,_osoba];~~}
      {<1 komisja}{<50}________________________________

   {OD}
{FI}

Spis rozpoczęto dn.____________ o godz. __________

zakończono       dn.____________ o godz. __________
   {FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE:'TABHEAD'}
   {HEADER-}
   {FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {FOOTER-}
{COMMENT}   pozycje arkusza                                          {COMMENT-}
   {FOR: SL}
   {INDEX: 'PAL'}
   {PREFIX: INN.MG}
   {DO}
      {IF: SL.PAL<>null}
         {lp+=1;~~}
         {SUBSTITUTE: 'danepoz'}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
   {OD}
   {HEADER}{HEADER-}
   {FOOTER}{FOOTER-}
   {IF: lineleft>0}
      {SUBSTITUTE: 'LINIAF'}
   {FI}
{COMMENT}   podpisy                                               {COMMENT-}
   {ENTIRE}
   {FONT: 'T10'}{SPACE: 'C'}

Spis z natury zakończono na pozycji           _______

Podpis osoby materialnie odpowiedzialnej   _____________________________

Wycena __________________________________
            imię nazwisko                          podpis


                                                          Skład komisji inwentaryzacyjnej

                                                          przewodniczący

                                                          ___________________________________
                                                          członkowie:
{IF: exec('FindInSet','#table','INNKOM','INN',INN.ref(),,,,,null())=null()}

                                                          ___________________________________

                                                          ___________________________________
{ELSE}
   {FOR: INNKOM}
   {INDEX: 'INN'}
   {PREFIX: INN.ref(),'N'}
   {DO}

                                                          ___________________________________
   {OD}
{FI}
                                                          Sprawdził

                                                          ___________________________________
   {ENTIRE-}
{FI}
{IF: ZAKR.IP>0}
{COMMENT}   pusty arkusz                                             {COMMENT-}
{COMMENT}   HEADER                                                   {COMMENT-}
   {HEADER}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {<1 'Arkusz spisu z natury '@+form(INN.DI,,2)+', Magazyn: '@+INN.MG().SYM}
   {SUBSTITUTE: 'TABHEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}{ vp:=1;~~}
   {HEADER-}
   {IF: ZAKR.TA=1}{PAGE}{FI}
   {FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {FOOTER-}
{COMMENT}   pozycje arkusza                                          {COMMENT-}
   { wsk:=lp:=0; vp:=1;
   w[1]:=20; {! _ii:=2..7 |! w[_ii]:='' !}; w[6]:='[   ]';
   _f:=prn1+'.ini_line()'; _v:=($(_f))();
   _f:=prn1+'.length()'; il_lin:=($(_f))()+2
   ;~~}
   {WHILE: wsk<ZAKR.IP}
   {DO}
      { lp+=1; w[1]:=lp;~~}
      {IF: _ll:=lineleft(); (_ll>0 & _ll<il_lin)}
         { lp:=0; wsk+=1;~~}
         {PAGE}
      {ELSE}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
   {OD}
{FI}
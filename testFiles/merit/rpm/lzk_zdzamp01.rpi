:!UTF-8
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
"--szerokosc kolumn--";
ddokl:=0;
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1
   ,'P','INTEGER',''
   ,'M','STRING[160]',''
   ,'JM','STRING[5]',''
   ,'IL','REAL',''
   ,'REF','STRING[16]',''
   ,'D_POTW','DATE','');

ZD_POZ.index('POZ'); ZD_POZ.prefix(ZD_NAG.ref());
{? ZD_POZ.first
|| {!
   |? _pop:=((+form((frac(ZD_POZ.IL))))-2);
      {? _pop>0 & _pop>ddokl || ddokl:=_pop ?};
      X_Xw1.clear();
      X_Xw1.blank();
      X_Xw1.P:=ZD_POZ.POZ;
      X_Xw1.M:=ZD_POZ.M().KTM+' - '+M.N;
      X_Xw1.JM:=ZD_POZ.JM().KOD;
      exec('obl_ptw','zamdst_ptw',$ZD_POZ.ref(),1);
      {? __zdp_sum.IL_PTW>0
      || X_Xw1.IL:=__zdp_sum.IL_PTW
      || X_Xw1.IL:={? ZD_POZ.D_POTW<>date(0,0,0) || ZD_POZ.IL || 0 ?}
      ?};
      X_Xw1.REF:=$ZD_POZ.ref();
      X_Xw1.D_POTW:=ZD_POZ.D_POTW;
      X_Xw1.add(1);
      ZD_POZ.next
   !}
?};

lmt:=charleft()-4;
k[1]:=10;                           p[1] :='form(X_Xw1.IL,,ddokl,\' ,\')';
k[4]:=-12;                          p[4] :='X_Xw1.P';
k[5]:=0;                            p[5] :='X_Xw1.M';
k[6]:=2;                            p[6] :='X_Xw1.JM';
k[2]:=10;                           p[2] :='X_Xw1.D_POTW';

_len:=obj_len(k);
_buf:=obj_new(_len); {! _i.._len |! _buf[_i]:=0 !};
_max:=obj_new(_len); {! _i.._len |! _max[_i]:=0 !};

X_Xw1.clear();
{? X_Xw1.first()
|| {!
   |? {! _i.._len
      |! _war:=($p[_i])();
         _typ:=type_of(_war);
         {? _typ=1 || _w:=(+form(_war));
                      {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
         |? _typ=2 || _w:=(+_war);
                      {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
         |? _typ=4 || _buf[_i]:=10
         |? _typ=5 || _buf[_i]:=5
         ?}
      !};
      X_Xw1.next()
   !}
?};

_ods:=-3; _all:=0;
{! _i.._len
|! {? k[_i]<0         || k[_i]:=-k[_i]
   |? k[_i]<=_buf[_i] || k[_i]:=_buf[_i]
   ?};
   _all+=k[_i]; _ods+=3
!};
k[5]:=1;
VAR_DEL.delete('_buf','_max');

{! _ii:=1..2
|!
   PRN.add("w[1]",k[1],'Lp.'@,1);
   PRN.add("form(w[3])",k[5],'Materiał/Usługa'@);
   PRN.add("form(w[2])",k[2],'Najbliższa data dostawy'@);
   PRN.add("form(w[7])",k[6],'jm'@);
   PRN.add("form(w[4],,dokl)",k[4],'Potwierdzono'@,1);
   {? _ii=1
   ||
      k[5]+=charleft-PRN.width()-36;
      VAR_DEL.delete('PRN');
      PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame()
   ?}
!};
~~}
{FONT: exec('font','param_wydr','header')}{SPACE: exec('space','param_wydr')}
{
   lmt:=int((charleft()-PRN.width())/2);

   PAR_WYDR.TITLE:='POTWIERDZENIE ZAMÓWIENIA NR '+$ZD_NAG.NR+' - '+ZD_NAG.SYM+ '\n z dnia '+$ZD_NAG.DATA;
   prn1:='PRN'
;~~}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEADF'}
{ns:=0;~~}
{LINE}
{COMMENT}   strony transakcji                                        {COMMENT-}
{ __STRTAB:='ZD_NAG'; __STRODB:=''; __STRLIN:=''; __STRDAT:=ZD_NAG.DATA;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{COMMENT}   forma platnosci                                          {COMMENT-}
{IF: ZD_NAG.KH().PLATNOSC<>null | ZD_NAG.PL<>null}
   {LINE}
   {
   fp_tr:='Forma płatności:';
   fp_war:=' '+{? ZD_NAG.PL=null() || ZD_NAG.KH().PLATNOSC().TR || ZD_NAG.PL().TR ?};
   tp_tr:={? ZD_NAG.TZ<>date(0,0,0) || 'Termin zapłaty:' || '' ?};
   tp_war:={? ZD_NAG.TZ<>date(0,0,0) || ' '+form(ZD_NAG.TZ) || '' ?};
   wp_tr:=wp_war:=wp_wal:='';
   odstep:=charleft()-(+fp_tr)-(+fp_war)-(+tp_tr)-(+tp_war)-(+wp_tr)-(+wp_war)-(+wp_wal);
   odstep:={? odstep>0 || ' '*odstep || ' ' ?};
   ~~}
   {<{1}
      }{FONT: exec('font','param_wydr','fp_tr')}{fp_tr
      }{FONT: exec('font','param_wydr','fp_war')}{fp_war
      }{FONT: exec('font','param_wydr','fp_ods')}{odstep
      }{FONT: exec('font','param_wydr','tp_tr')}{tp_tr
      }{FONT: exec('font','param_wydr','tp_war')}{tp_war
      }{FONT: exec('font','param_wydr','wp_tr')}{wp_tr
      }{FONT: exec('font','param_wydr','wp_war')}{wp_war
      }{FONT: exec('font','param_wydr','wp_wal')}{wp_wal}
{FI}
{COMMENT}   miejsce dostawy                                          {COMMENT-}
{FONT: exec('font','param_wydr','strony_po')}{SPACE: exec('space','param_wydr')}
{IF: ZD_NAG.ZAM_DST<>''}
   {LINE}
   {<{lmt+1}'Zamówienie dostawcy: '+ZD_NAG.ZAM_DST}
{FI}
{IF: ZD_NAG.KH_MSC<>null}
   {LINE}
   {<{lmt+1}exec('msc_dost','kontrahent','ZD_NAG')}
{FI}
{COMMENT}   planowana data realizacji                                {COMMENT-}
{IF: ZD_NAG.DTPREAL<>date(0,0,0)}
   {LINE}
   {<{lmt+1}
      }{FONT: exec('font','param_wydr','tr_tr')}{'Planowana data realizacji: '
      }{FONT: exec('font','param_wydr','tr_war')}{' '}{$ZD_NAG.DTPREAL}
{FI}
{LINE}
{COMMENT} Dane dodatkowe wynikajace z informacji dodatkowych zawartych do zamowienia {COMMENT-}
{
   'pobieram dane'; exec('pobgdnag','fakso','D',ZD_NAG.ref);
   VAR_DEL.delete('oo','OPIK')
;~~}
{FOR: S_GD}
{PREFIX: 'G'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{ prn1:='PRN';~~}
{FONT: exec('font','param_wydr','tabhead')}{SPACE: exec('space','param_wydr')}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {IF: ~ns}
   {SUBSTITUTE: 'HEADF'}
   {IF: zakoncz}
      {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
      {SUBSTITUTE: 'TABHEAD'}
   {FI}
   {FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{IF: _a:=exec('get_par','#parametr',34); _a='Z' | _a='A'}
   {FOOTER}
   {SUBSTITUTE: 'FOOT_LIC'}
   {~~}
   {~~}
   {~~}
   {FOOTER-}
{ELSE}
   {FOOTER}{FOOTER-}
{FI}
{COMMENT}   pozycje                                                  {COMMENT-}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{  rsep:=PAR_WYDR.RSEP; w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=0;s[1]:=s[2]:=s[3]:=s[4]:=0; ~~}
{FOR:'ZD_POZ'}{INDEX: 'POZ'}{PREFIX: ZD_NAG.ref}
{DO}
   {FIRST}
   {
   ZD_POZ.cntx_psh;
   {!
   |?
      _ilptw:={? ZD_POZ.D_POTW<>date(0,0,0) || ZD_POZ.IL || 0 ?};
      _dokl:=+form((frac(_ilptw)))-2;
      {? dokl<_dokl || dokl:=_dokl ?};
      ZD_POZ.next
   !};
   ZD_POZ.cntx_pop;
   ~~}
   {FIRST-}
   {
   rsep:=PAR_WYDR.RSEP;
   w[1]+=1;ok:=w[1];
   w[3]:=ZD_POZ.M().KTM+' '+ZD_POZ.M().N;
   w[2]:=ZD_POZ.D_POTW;
   exec('obl_ptw','zamdst_ptw',$ZD_POZ.ref(),1);
   {? __zdp_sum.IL_PTW>0
   || _ilpotw:=__zdp_sum.IL_PTW
   || _ilpotw:={? ZD_POZ.D_POTW<>date(0,0,0) || ZD_POZ.IL || 0 ?}
   ?};

   s[1]+=w[4]:=_ilpotw;
   w[7]:=ZD_POZ.JM().KOD;
   s[2]+=w[6]:=ZD_POZ.IL*ZD_POZ.CENA $2;

   _f:=prn1+'.ini_line()'; _v:=($(_f))();
   _f:=prn1+'.length()'; il_lin:=($(_f))() + 2
   ;~~}
   {IF: lineleft <= il_lin}
      {SUBSTITUTE: 'LINIAF'}{PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
   {FOR: FAKSO}{INDEX: 'ZD_POZ'}{PREFIX: ZD_POZ.ref}
   {DO}
      {
      {! _i..20 |! w[_i]:='' !};
      {? FAKSO.O<>'' || w[3]:=exec('printFAKSO','fakso',PARAM_W.JEZYK) ?};
      rsep:=0
      ;~~}
      {SUBSTITUTE: 'LINIA0'}
      { w[1]:=ok;~~}
   {OD}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FONT: exec('font','param_wydr','inf_dod')}{SPACE: exec('space','param_wydr')}
{COMMENT} Dane dodatkowe wynikajace z informacji dodatkowych zawartych do zamowienia {COMMENT-}
{ zakoncz:=0;~~}
{FOR: S_GD}
{PREFIX: 'D'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   podpisy                                                  {COMMENT-}
{FONT: exec('font','param_wydr','podpisy')}{SPACE: exec('space','param_wydr')}
{LINE: 3}
{IF: ZD_NAG.US<>null}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,ZD_NAG.US().DANE);
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{FI}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=30*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:='Potwierdzenie sporządził';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}


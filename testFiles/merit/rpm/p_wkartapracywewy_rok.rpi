:!UTF-8
{BEGIN:
   VAR_DEL.delete('tresc','dzien','SKLokr','NGodz','NDni','Cnt','lmt','lmtt8','lmtt10b','lmtt8g','lmttw8','lmttw8b');
   VAR_DEL.delete('suma','total','var','fca');
   VAR_DEL.delete('ok','__param');

   tresc:='pkd_wkartapracywewy_rok';

   PAR_WYDR.TITLE:='Roczna karta ewidencji czasu pracy';
   P.cntx_psh();
   R_PRACDN.cntx_psh();
   R_KWGODZ.cntx_psh();
   R_WZCZ.cntx_psh();
   KAL_ROK.cntx_psh();
   KAL_DEF.cntx_psh();
   N.cntx_psh();
   R_ODN.cntx_psh();
   R_ODP.cntx_psh();
   G.cntx_psh();

   dzien:=obj_new(32);
   {! _ii:=1..obj_len(dzien) |! dzien[_ii]:=obj_new(100) !};
   SKLokr:=obj_new(7);
   {! _n:=1..obj_len(SKLokr) |! SKLokr[_n]:=obj_new(2) !};

   _domain:={? +B_DOMAIN.SYMBOL || B_DOMAIN.SYMBOL || 3+AreaTitle.area ?};
   ok:={? _domain='PRC'
       || exec('uprawnieniawrap','pkd','wydrukach zestawień czasu pracy'@,'PRC_CZP_WYDR')
       || exec('uprawnieniawrap','pkd','nieobecnościach'@,'PKD_EZK_OPNN','PKD_EZK_ORNN')+
          exec('uprawnieniawrap','pkd','wzorcach czasu pracy'@,'PKD_EZK_OPKA','PKD_EZK_ORKA')
       ?};
   {? +ok
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[ok])
   || __param:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,
         "  {? cur_tab().ROK<1900
            || FUN.emsg('Wymagane poprawne wypełnienie pola \"Rok\"'@); 'ROK'
            || 1
            ?}
         ",
         ,,,
         ,,,
         ,,,,,
         date()~1,"1"
      );
      {? __param.size()
      || R_WZCZ.index('R_WZCZ');
         KAL_ROK.index('KAL_ROK');
         KAL_ROK.clear();
         KAL_DEF.index('KAL_DEF');
         N.index('NIEORM');
         R_ODP.index('PRAC');
         R_ODN.index('PRAC');

         NGodz:=time(0,0,0);
         NDni:=Cnt:=lmt:=lmtt8:=lmtt10b:=lmtt8g:=lmttw8:=lmttw8b:=0;
         suma:=obj_new(11+3);
         total:=obj_new( obj_len(suma));
         {! _n:=1..obj_len(suma) |! total[_n]:=suma[_n]:=0 !};
         var:=obj_new(17);
         ' nieobecności wykazywane w dniach kalendarzowych ';
         var[8]:=151;
         var[14]:='';

         fca:="_nic:='-----';
               {? _a>date(var[1],var[2],0)~3 | dzien[_a][1]=0
               || _nic
               || _ret:='';
                  {? dzien[_a][50] || _ret+='Uw' ?};
                  {? dzien[_a][51] || _ret+='Um' ?};
                  {? dzien[_a][52] || _ret+='Uc' ?};
                  {? dzien[_a][53] || _ret+='Ub' ?};
                  {? dzien[_a][54] || _ret+='Uo' ?};
                  {? dzien[_a][55] || _ret+='Zn' ?};
                  {? dzien[_a][56] || _ret+='Ch' ?};
                  {? dzien[_a][57] || _ret+='Op' ?};
                  {? dzien[_a][58] || _ret+='Nu' ?};
                  {? dzien[_a][59] || _ret+='Nn' ?};
                  {? dzien[_a][60] || _ret+='Pr' ?};
                  {? _ret=''
                  || _ret:={? *(_w:=dzien[_a][5])>0
                           || gsub(_w$1,':',':')
                           |? 1+dzien[_a][3]='W' | 1+dzien[_a][3]='S'
                           || dzien[_a][3]
                           || ''
                           ?}
                  ?};
                  _ret
               ?}
              ";
         params_set('P',exec('wybierz_parses','pracownik',_domain).P);

         var[1]:=__param.ROK;
         var[2]:=1;

         {? var_press('Tab')>100 || obj_del(Tab) ?};
         Tab:=tab_tmp(1,'LP','INTEGER','Lp');
         {! _i:=1..12
         |! Tab.LP:=_i;
            Tab.add()
         !};

         _wDni:=5;
         _wOG:=7;
         _wN:=2;
         {? var_pres('PRN')>0 || obj_del(PRN) ?};
         PRN:=obj_new(@.CLASS.PRINT,44+4);
         PRN.add("miesiąc(var[2])",2,'Mc');
         {! _i:=1..31 |! PRN.add(($('fca('+$_i+')')),_wDni,$_i,,,,,,,1) !};
         PRN.add("suma[12]$1" ,_wOG,'PG',1,,,,,',2,\'9.\'');
         PRN.add("suma[1]$1" ,_wOG,'OG',1,,,,,',2,\'9.\'');
         PRN.add("suma[2]$2" ,_wN,'Uw',1);
         PRN.add("suma[3]$2" ,_wN,'Um',1);
         PRN.add("suma[4]$2" ,_wN,'Uc',1);
         PRN.add("suma[5]$2" ,_wN,'Ub',1);
         PRN.add("suma[6]$2" ,_wN,'Uo',1);
         PRN.add("suma[7]$2" ,_wN,'Zn',1);
         PRN.add("suma[8]$2" ,_wN,'Ch',1);
         PRN.add("suma[9]$2" ,_wN,'Op',1);
         PRN.add("suma[10]$2",_wN,'Nu',1);
         PRN.add("suma[13]$2",_wN,'Pr',1);
         PRN.add("suma[11]$2",_wN,'Nn',1);
         PRN.sl_frame();

         {? var_pres('PRN2')>0 || obj_del(PRN2) ?};
         PRN2:=obj_new(@.CLASS.PRINT,4);
         PRN2.add("", 2+1+31*(_wDni+1)-1,'Miesiąc     /     Dzień miesiąca');
         PRN2.add("",              _wOG,'Plan');
         PRN2.add("",              _wOG,'Godz.');
         PRN2.add("",     11*(_wN+1)-1,'Czas nieobecności w pracy w dniach wg przyczyny');
         PRN2.sl_frame();

         {? var_pres('PRN1')>0 || obj_del(PRN1) ?};
         PRN1:=obj_new(@.CLASS.PRINT,12+3);
         PRN1.add("'Razem:'", 2+1+31*(_wDni+1)-1,'');
         PRN1.add("total[12]$1", _wOG,'',1,,,,,',2,\'9.\'');
         PRN1.add("total[1]$1" , _wOG,'',1,,,,,',2,\'9.\'');
         PRN1.add("total[2]$2" , _wN,'',1);
         PRN1.add("total[3]$2" , _wN,'',1);
         PRN1.add("total[4]$2" , _wN,'',1);
         PRN1.add("total[5]$2" , _wN,'',1);
         PRN1.add("total[6]$2" , _wN,'',1);
         PRN1.add("total[7]$2" , _wN,'',1);
         PRN1.add("total[8]$2" , _wN,'',1);
         PRN1.add("total[9]$2" , _wN,'',1);
         PRN1.add("total[10]$2", _wN,'',1);
         PRN1.add("total[13]$2", _wN,'',1);
         PRN1.add("total[11]$2", _wN,'',1);
         PRN1.sl_frame();

         prn1:='PRN';
         prn2:='PRN1';
         lmt:=rsep:=0;
         vp:=1;
         htcol:=36;
         span:='1;31;1;3';

         miesiąc:="
            {? _a=1  ||' 1'
            |? _a=2  ||' 2'
            |? _a=3  ||' 3'
            |? _a=4  ||' 4'
            |? _a=5  ||' 5'
            |? _a=6  ||' 6'
            |? _a=7  ||' 7'
            |? _a=8  ||' 8'
            |? _a=9  ||' 9'
            |? _a=10 ||'10'
            |? _a=11 ||'11'
            |? _a=12 ||'12'
            ?}"
      || ok:='nie'
      ?}
   ?}
}
{END:
   P.cntx_pop();
   R_PRACDN.cntx_pop();
   R_KWGODZ.cntx_pop();
   R_WZCZ.cntx_pop();
   KAL_ROK.cntx_pop();
   KAL_DEF.cntx_pop();
   N.cntx_pop();
   G.cntx_pop();
   R_ODP.cntx_pop();
   R_ODN.cntx_pop();
   {! _n:=1..obj_len(dzien) |! obj_del(dzien[_n]) !};
   VAR_DEL.delete('tresc','dzien','SKLokr','NGodz','NDni','Cnt','lmt','lmtt8','lmtt10b','lmtt8g','lmttw8','lmttw8b');
   VAR_DEL.delete('suma','total','var','fca','miesiąc','PRN','PRN1','PRN2','Tab');
   VAR_DEL.delete('ok','__param')
}
{EXIT: +ok}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}-------- fragment pliku skid_pw4.rpi MACRO-TABHEAD2 -------{COMMENT-}
{MACRO: 'TABHEAD2'}
{ _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn2+'.ini_head()'; ($(_f))()}
{REP: _f:=prn2+'.fhbar(lmt)'; ($(_f))()}
{WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn1+'.ini_head()'; ($(_f))()}
{REP: _f:=prn2+'.fjbar( ($prn1)(),lmt)'; ($(_f))()}
{WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{FONT:'T10B'}{SPACE: 'B'}
{CENTER}{<1>{charleft()}'E W I D E N C J A   R O C Z N A   C Z A S U   P R A C Y ('+$var[1]+')'}
{<{ceil(lmt)+20}}
{FONT: 'T8B'}
{SPACE: 'B'}
{CENTER}{<1>{charleft()}'(wg kartoteki WE/WY)'}
{<{ceil(lmt)+1}P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE}   (Nr teczki {|P.T}),   Jednostka org. {P.WYDZIAL().SYMBOL}
{FONT: 'T8GB'}
{SPACE: 'C'}
{SUBSTITUTE: rsep:=1; vp:=1; prn1:='PRN'; prn2:='PRN2'; 'TABHEAD2'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8GB'}
{SPACE: 'C'}
{REP: vp:=1; _f:='PRN.fjbar(PRN1,lmt)'; ($(_f))()}
{SUBSTITUTE: rsep:=0; vp:=0; prn1:='PRN1'; 'LINIA0'}
{SUBSTITUTE: rsep:=0; vp:=0; prn1:='PRN1'; 'LINIAF'}

    Legenda :
             PG - planowana liczba godzin    OG - liczba godzin ogółem    Uw - urlop wypoczynkowy    Um - urlop macierzyński     Uc - urlop wychowawczy    Ub - urlop bezpłatny    Uo - urlop okolicznościowy
             Zn - zwolnienie niepłatne    Ch - choroba pracownika    Op - opieka nad dzieckiem   Nu - nieobecność usprawiedliwiona    Nn - nieobecność nieusprawiedliwiona    Pr - przestój
             R  - roboczy     RN - roboczy w niedzielę      RS - roboczy w święto      WH - harmonogramowo wolny     W5 - wolny z tytułu pięciodniowego tyg. pracy
             WN - wolny za niedzielę      WS - wolny za święto    SN - święto niedzielne     SW - święto w inny dzień niż niedziela

{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
   {IF: P.DZA<= date(var[1],12,0) & (P.DZ>= date(var[1],1,1)|P.DZ=date(0,0,0))}
      {FOR: Tab}{DO}
            {var[2]:=__param.MC:=VAR_EDIT.MSC:=Tab.LP;
             var[3]:=date(var[1],var[2],1);
             var[4]:=date(var[1],var[2],0);~~}
            {SUBSTITUTE: 'miesiac'}
      {OD}
      {PAGE}
      { {! _n:=1..obj_len(suma) |! total[_n]:=0 !};~~}
   {FI}
{MACRO-}
{MACRO: 'miesiac'}
{IF: P.DZA<= date(var[1],var[2],0) & (P.DZ>= date(var[1],var[2],1)|P.DZ=date(0,0,0))}
{  echo('Pracownik: '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE);
   MASK.Use('R_PRACDN',__param.ROK,__param.MC);
   R_PRACDN.index('R_PRACDN');
   R_PRACDN.prefix();
   MASK.Use('R_KWGODZ',__param.ROK,__param.MC);
   R_KWGODZ.index('DATA_RUB');
   R_KWGODZ.prefix();
   exec('oblicz_wewy','wkartp','rok');
   ~~
}
{  suma[1]:=dzien[obj_len(dzien)][5];
   suma[2]:=dzien[obj_len(dzien)][50];
   suma[3]:=dzien[obj_len(dzien)][51];
   suma[4]:=dzien[obj_len(dzien)][52];
   suma[5]:=dzien[obj_len(dzien)][53];
   suma[6]:=dzien[obj_len(dzien)][54];
   suma[7]:=dzien[obj_len(dzien)][55];
   suma[8]:=dzien[obj_len(dzien)][56];
   suma[9]:=dzien[obj_len(dzien)][57];
   suma[10]:=dzien[obj_len(dzien)][58];
   suma[11]:=dzien[obj_len(dzien)][59];
   suma[12]:=dzien[obj_len(dzien)][17];
   suma[13]:=dzien[obj_len(dzien)][60];

   {! _n:=1..obj_len(suma) |! total[_n]+=suma[_n] !};
~~}
{SUBSTITUTE:  rsep:=1; prn1:='PRN'; 'LINIA0'}
{FI}
{MACRO-}
{MACRO: 'podsum'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8GB'}
{SPACE: 'C'}
{ lmt:=int((charleft-PRN.width())/2); ~~ }
{FOR:  params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
   {SUBSTITUTE: tresc}
   {FI}
{OD}
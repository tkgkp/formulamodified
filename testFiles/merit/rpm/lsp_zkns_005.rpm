:!UTF-8
{TITLE:E. Zestawienie zamówień sprzedaży}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   zkl_005:="
      VAR_DEL.delete('ww','kk','doklw','lp','PRN','tryb');
      VAR_DEL.delete('han','kh','stan','zakres','zam','sort1');
      VAR_DEL.delete('odd','dod');
      VAR_DEL.delete('wydr_naz','wydr_edi');
      VAR_DEL.delete('wyjscie','X_Xw1');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep')
   ";
   zkl_005();

   VAR_DEL.delete('w','PRN');
   _ilk:=9;
   kk:=obj_new(_ilk); "--szerokosc kolumn--";
   ww:=obj_new(_ilk); "--wartosci kolumn--";
   doklw:=2; "--dokladnosc wartosci--";
   ss:=0; "--sumator--";
   PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();
   lp:=0;
   tryb:=rep_export();

   kk[1]:=4;   "--Lp.--";
   kk[2]:=20;  "--Zamowienie--";
   kk[3]:=34;  "--Kontrahent--";
   kk[4]:=12;  "--Stan--";
   kk[5]:=10;  "--Data przyjecia--";
   kk[6]:=10;  "--Termin realizacji--";
   kk[7]:=20;  "--Handlowiec--";
   kk[8]:=14;  "--Wartosc--";
   kk[9]:=5;   "--Archiwalne--";

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   han:=kh:=stan:=zakres:=zam:=sort1:='';
   odd:=dod:=date(0,0,0);

   wydr_naz:='zkns_005';
   wydr_edi:='ZKL_005';

   KH.cntx_psh(); HAN.cntx_psh();
   KH.win_dict('WER_SLO'); KH.actions('WER_SLO','W');
   HAN.win_dict('SLO');

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   KH.win_dict('WERHOME2');
   HAN.win_dict('SLO');
   HAN.actions('SLO','dpu:d');
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("{? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || '' || FUN.info('Błędny zakres dat.'@);'DO_DATY' ?}")
   || exec('bufAndgetRec','#table','WYDRUKIL');
      {? WYDRUKIL.put(1)
      || wyjscie:=0;
         kh:=WYDRUKIL.KH_OD().KOD;
         han:=WYDRUKIL.HAN().KOD;
         odd:=WYDRUKIL.OD_DATY;
         dod:=WYDRUKIL.DO_DATY;
         stan:=WYDRUKIL.STANZAM;
         sort1:=WYDRUKIL.SORTUJ;
         zakres:={? WYDRUKIL.RADIOB_1=2 || 'Z' |? WYDRUKIL.RADIOB_1=1 || 'A' || 'W' ?}
      ?}
   ?};
   KH.actions('WER_SLO','');
   HAN.actions('SLO','');
   KH.cntx_pop(); HAN.cntx_pop()
}
{END:
   zkl_005(); VAR_DEL.delete('zkl_005')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("ww[1]",kk[1],'Lp.'@,1) ?};
PRN.add("ww[2]",kk[2],'Zamówienie'@);
PRN.add("ww[3]",kk[3],'Kontrahent'@);
PRN.add("ww[4]",kk[4],'Stan'@);
PRN.add("ww[9]",kk[9],'Arch.'@);
PRN.add("ww[5]",kk[5],'Data przyjęcia'@);
PRN.add("ww[6]",kk[6],'Termin realizacji'@);
PRN.add("ww[7]",kk[7],'Handlowiec'@);
{? exec('upr_cs','ceny') || PRN.add("ww[8]",kk[8],'Wartość'@,1) ?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'ZK'     ,'STRING[20]'  ,'Symbol zamówienia',
   'KH'     ,'STRING[70]'  ,'Kontrahent',
   'E'      ,'STRING[11]'  ,'Stan zamówienia',
   'DP'     ,'DATE'        ,'Data przyjęcia',
   'DT'     ,'DATE'        ,'Termin realizacji',
   'HAN'    ,'STRING[50]'  ,'Handlowiec',
   'NET'    ,'REAL'        ,'Wartość netto',
   'ARCH'   ,'STRING[1]'   ,'Archiwalne');
X_Xw1.index(X_Xw1.ndx_tmp(,,{? sort1='W' || 'NET' |? sort1='S' || 'E' || 'ZK' ?},,));

"--obliczenia w masce zbiorczej--";
ZK_N.index('ZKON'); ZK_N.prefix();
{? (zakres='W' | zakres='A') & ZK_N.first()
||
   {!
   |? {? ZK_N.STAT_REJ<>'A'
         & ZK_N.T().R='Z' & (ZK_N.DP>=odd | odd=date(0,0,0)) & (ZK_N.DP<=dod | dod=date(0,0,0))
         & (kh=ZK_N.KH().KOD | kh='')
         & (han=ZK_N.HAN().KOD | han='')
         & (stan=exec('zwr_etap','zamsiw_nag',#ZK_N.E) | stan='')
      ||
         X_Xw1.ZK:=ZK_N.SYM;
         X_Xw1.KH:=ZK_N.KH().KOD+' '+KH.NAZ;
         X_Xw1.E:=exec('zwr_etap','zamsiw_nag',#ZK_N.E);
         X_Xw1.DP:=ZK_N.DP;
         X_Xw1.DT:=ZK_N.DT;
         X_Xw1.HAN:=ZK_N.HAN().NAZ;
         X_Xw1.NET:=ZK_N.NETTO;
         X_Xw1.ARCH:='';
         X_Xw1.add()
      ?};
      ZK_N.next()
   !}
?};
"--obliczenia w maskach archiwalnych--";
OKR.cntx_psh();
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? (zakres='W' | zakres='Z') & OKR.first()
||
   ZK_N.cntx_psh(); ZK_RN.cntx_psh();
   {!
   |? _maska:=ST.ODDZ+form(form(OKR.ROK,,,'99'),-2);
      ZK_N.use('zknag'+_maska);
      ZK_RN.use('zkhin'+_maska);
      ZK_N.index('ZKON'); ZK_N.prefix();
      {? ZK_N.first()
      ||
         {!
         |? {? ZK_N.STAT_REJ<>'A'
               & ZK_N.T().R='Z' & (ZK_N.DP>=odd | odd=date(0,0,0)) & (ZK_N.DP<=dod | dod=date(0,0,0))
               & (kh=ZK_N.KH().KOD | kh='')
               & (han=ZK_N.HAN().KOD | han='')
               & (stan=exec('zwr_etap','zamsiw_nag',#ZK_N.E) | stan='')
            ||
               X_Xw1.ZK   :=ZK_N.SYM;
               X_Xw1.KH   :=ZK_N.KH().KOD+' '+KH.NAZ;
               X_Xw1.E    :=exec('zwr_etap','zamsiw_nag',#ZK_N.E);
               X_Xw1.DP   :=ZK_N.DP;
               X_Xw1.DT   :=ZK_N.DT;
               X_Xw1.HAN  :=ZK_N.HAN().NAZ;
               X_Xw1.NET  :=ZK_N.NET;
               X_Xw1.ARCH :='T';
               X_Xw1.add()
            ?};
            ZK_N.next()
         !}
      ?};
      OKR.next()
   !};
   ZK_N.cntx_pop(); ZK_RN.cntx_pop()
?};
OKR.cntx_pop();

PAR_WYDR.TITLE:='Zestawienie zamówień sprzedaży'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział: %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1}}{IF: (kh<>'')}{'Kontrahent: %1'@[kh]}{ELSE}{'Kontrahent: bez ograniczeń'@}{FI}
{<{lmt+1}}{IF: (han<>'')}{'Handlowiec: %1'@[han]}{ELSE}{'Handlowiec: bez ograniczeń'@}{FI}
{IF: odd<>date(0,0,0) | dod<>date(0,0,0)}{<{lmt+1}}{'Zakres dat: od %1 do %2'@[$odd,$dod]}{FI}
{<{lmt+1}}{IF: stan<>''}{'Stan zamówienia: %1'@[stan]}{ELSE}{'Stan zamówienia: wszystkie'@}{FI}
{<{lmt+1}}{{? sort1='W' || 'Sortowanie wg: wartość'@ |? sort1='S' || 'Sortowanie wg: stan zamówienia'@ || 'Sortowanie wg: symbol zamówienia'@ ?}}
{<{lmt+1}}{{? zakres='Z' || 'Zakres zamówień: zarchiwizowane'@ |? zakres='A' || 'Zakres zamówień: aktywne'@ || 'Zakres zamówień: wszystkie'@ ?}}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xw1}
{DO}
   {
   lp +=1;
   ww[1] :=form(lp);
   ww[2] :=form(X_Xw1.ZK);
   ww[3] :=form(X_Xw1.KH);
   ww[4] :=form(X_Xw1.E);
   ww[5] :=form(X_Xw1.DP);
   ww[6] :=form(X_Xw1.DT);
   ww[7] :=form(X_Xw1.HAN);
   ww[8] :=form(X_Xw1.NET,,doklw,'.,');
   ww[9] :=form(X_Xw1.ARCH);
   ss+=X_Xw1.NET;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{SUBSTITUTE: 'LINIAF'}
{
PRN.n_frame();
ww[1]:='';
ww[2]:='';
ww[3]:='';
ww[4]:='';
ww[5]:='';
ww[6]:='';
ww[7]:='Razem:'@; PRN.FORM[7]:=0;
ww[8]:=form(ss,,2,'.,');
ww[9]:='';
~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
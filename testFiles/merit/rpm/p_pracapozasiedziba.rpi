:!UTF-8
{BEGIN:
   VAR_DEL.delete('PRN','PRH','PR','prn1','prn2','vp','lmt','rsep','h_rat','pb','Nr');
   VAR_DEL.delete('LIST','exit','sumy');

   'Pobranie parametrów:';
   _PARWYDR:=tab_tmp(1,'OD','DATE','Data od','DO','DATE','Data do');
   _PARWYDR.OD:=date(,,1);
   _PARWYDR.DO:=date(,,0);
   _ed:=_PARWYDR.mk_edit('Parametry wydruku',0,'#ppsf_wydr');
   _PARWYDR.win_efld(_ed,,'OD');
   _PARWYDR.win_efld(_ed,,'DO');
   _PARWYDR.efld_opt(_ed,'mark=1',,'OD');
   _PARWYDR.efld_opt(_ed,'mark=1',,'DO');
   _PARWYDR.win_edit(_ed);
   exec('ok_esc','#window',_PARWYDR,_ed);
   exit:=~_PARWYDR.edit("__CHK.record(cur_tab(,1),,'OD','DO')");

   {? ~exit
   || LIST:=exec('ppsf_p_wyd_tab','ppsf',_PARWYDR.OD,_PARWYDR.DO);
      {? LIST.first()
      || params_set('OD',_PARWYDR.OD,'DO',_PARWYDR.DO);
         PAR_WYDR.TITLE:='Praca poza siedzibą firmy'@;
         sumy:=obj_new('DRZ','DWZ','DWN','DRE','GRZ','GWZ','GWN','GRE');
         sumy.DRZ:=sumy.DWZ:=sumy.DWN:=sumy.DRE:=0;
         sumy.GRZ:=sumy.GWZ:=sumy.GWN:=sumy.GRE:=time(0,0,0);
         {? rep_export()
         || PRH:=obj_new(@.CLASS.PRINT,6)
         || PRH:=obj_new(@.CLASS.PRINT,7);
            PRH.add("Nr+=1",3,'Lp.'@,1,,,,,',,\'9.\'')
         ?};
         PRH.add("''",6,'Nr teczki'@);
         PRH.add("''",30,'Nazwisko i imię'@);

         PRH.add("''",17,'Stanowisko'@);
         PRH.add("''",13,'Jednostka organizacyjna'@);
         PRH.add("''",53,'Całodzienne (ilość dni roboczych)'@,,,,,,,,5);
         PRH.add("''",57,'Część dnia (ilość godzin)'@,,,,,,,,5);
         PRH.s_frame();

         {? rep_export()
         || PRN:=obj_new(@.CLASS.PRINT,14)
         || PRN:=obj_new(@.CLASS.PRINT,15);
            PRN.add("Nr+=1",3,'',1,,,,,',,\'9.\'')
         ?};
         PRN.add("LIST.NRT",6,'',1);
         PRN.add("'%1 %2'[LIST.NAZ, LIST.PIE]",30,'');

         PRN.add("LIST.STN",17,'');
         PRN.add("LIST.WYD",13,'');

         PRN.add("{? _sum:=LIST.DWZ+LIST.DWN+LIST.DRE || _sum || '' ?}",5,'Razem'@,1);
         PRN.add("{? LIST.DWZ || LIST.DWZ || '' ?}",4,'Wz'@,1);
         PRN.add("{? LIST.DWN || LIST.DWN || '' ?}",4,'Wn'@,1);
         PRN.add("{? LIST.DRE || LIST.DRE || '' ?}",4,'Wr'@,1);
         PRN.add("LIST.DTY",24,'Typy'@);

         PRN.add("{? (_sum:=LIST.GWZ+LIST.GWN+LIST.GRE)<>*0 || $_sum || '' ?}",6,'Razem'@,1);
         PRN.add("{? LIST.GWZ<>*0 || $LIST.GWZ || '' ?}",5,'Wz'@,1);
         PRN.add("{? LIST.GWN<>*0 || $LIST.GWN || '' ?}",5,'Wn'@,1);
         PRN.add("{? LIST.GRE<>*0 || $LIST.GRE || '' ?}",5,'Wr'@,1);
         PRN.add("LIST.GTY",24,'Typy'@);

         PRN.s_frame();

         {? ~rep_export()
         || PR:=obj_new(@.CLASS.PRINT,11);

            PR.add("'S U M A'@",81,'',1);

            PR.add("{? sumy.DRZ || sumy.DRZ || '' ?}",5,'',1);
            PR.add("{? sumy.DWZ || sumy.DWZ || '' ?}",4,'',1);
            PR.add("{? sumy.DWN || sumy.DWN || '' ?}",4,'',1);
            PR.add("{? sumy.DRE || sumy.DRE || '' ?}",4,'',1);
            PR.add("''",24,'');

            PR.add("{? sumy.GRZ<>*0 || $sumy.GRZ || '' ?}",6,'',1);
            PR.add("{? sumy.GWZ<>*0 || $sumy.GWZ || '' ?}",5,'',1);
            PR.add("{? sumy.GWN<>*0 || $sumy.GWN || '' ?}",5,'',1);
            PR.add("{? sumy.GRE<>*0 || $sumy.GRE || '' ?}",5,'',1);
            PR.add("''",24,'');

            PR.s_frame()
         ?};

         prn1:='PRH';
         prn2:='PRN';
         rsep:=PAR_WYDR.SEP;
         lmt:=pb:=Nr:=lmt:=0;
         vp:=h_rat:=1
      ?}
   ?}
}
{END:
   VAR_DEL.delete('PRN','PRH','PR','prn1','prn2','vp','lmt','rsep','h_rat','pb','Nr');
   VAR_DEL.delete('LIST','exit','sumy')
}
{EXIT: exit | ~LIST.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{FONT: 'M'}
{SPACE: 'B'}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~}
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~}
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'%1 %2 - %3'['Za okres:',$params_get().OD,$params_get().DO]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{  prn1:='PRH'; prn2:='PRN'; ~~}
{SUBSTITUTE: 'TABHEAD2'}
{  prn1:='PRN'; prn2:='PRH'; ~~}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'TABHEAD2'}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn1+'.ini_head()'; ($(_f))()}
   {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
   {WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
   {DO}
      {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {OD}
   { _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn2+'.ini_head()'; ($(_f))()}
   {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   {WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
   {DO}
      {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
   {OD}
{MACRO-}
{MACRO: 'tresc'}
   { prn1:='PRN'; prn2:='PRH'; ~~}
   {SPACE: 'C'}
   {SUBSTITUTE: 'LINIA0'}
   {SPACE: 'B'}
   {IF: ~rep_export() & lineleft()<=8}
      {SUBSTITUTE: 'LINIAF'}
      {PAGE}
   {FI}
{MACRO-}
{MACRO: 'wsum'}
   { prn2:='PR'; rsep:=1; ~~ }
   {FONT: 'T8B'}
   {SPACE: 'B'}
   {SUBSTITUTE: 'LINIAFS'}
   {FONT: 'T8'}
   {SPACE: 'B'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRH.width())/2); ~~}
{FOR: LIST}
{DO}
   { sumy.DRZ+=(LIST.DWZ+LIST.DWN+LIST.DRE);
     sumy.DWZ+=LIST.DWZ;
     sumy.DWN+=LIST.DWN;
     sumy.DRE+=LIST.DRE;
     sumy.GRZ+=(LIST.GWZ+LIST.GWN+LIST.GRE);
     sumy.GWZ+=LIST.GWZ;
     sumy.GWN+=LIST.GWN;
     sumy.GRE+=LIST.GRE; ~~}
   {SUBSTITUTE: 'tresc'}
{OD}
{IF: rep_export()}
   {TABLE-}
{ELSE}
   {SUBSTITUTE: 'wsum'}
{FI}
{FONT: 'W8'}{SPACE:'C'}{<{lmt}'Legenda:'}
{<{lmt}}{FONT: 'W8B'
   }Wz{FONT: 'W8'} - Wnioski zaakceptowane  {FONT: 'W8B'
   }Wn{FONT: 'W8'} - Wnioski niezaakceptowane  {FONT: 'W8B'
   }Wr{FONT: 'W8'} - Wprowadzone ręcznie
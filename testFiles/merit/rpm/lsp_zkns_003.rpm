:!UTF-8
{TITLE:C. Zamówienia sprzedaży - zysk}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('w','s','PRN','tryb');
   w:=obj_new(7); "--wartosci kolumn--";
   s:=obj_new(4); {! _ii..4 |! s[_ii]  :=0 !}; "--razem--";
   doklw:=2; "--dokladnosc wartosci--";
   lp:=0;
   PRN:=obj_new(@.CLASS.PRINT,7); PRN.s_frame();
   tryb:=rep_export();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   wyjscie:=1;

   _odd:=date(ST.AR,ST.AM,1);
   _dod:=date(ST.AR,ST.AM,0);
   _wydr_naz:='zkns_003';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ||
      _odd:=WYDRUKIL.OD_DATY;
      _dod:=WYDRUKIL.DO_DATY
   ?};

   D_HELP.OD:=_odd;
   D_HELP.DO:=_dod;
   D_HELP.win_edit('ODDO');
   {? exec('upr_cm','ceny')=0
   || FUN.info('Brak uprawnień do wartości magazynowych.'@)
   |? D_HELP.edit("
         {? D_HELP.DO>=D_HELP.OD
         || ''
         || FUN.info('Błędny zakres dat.'@); 'DO'
         ?}")
   ||
      WYDRUKIL.index('WYDRUKI');
      WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
      {? WYDRUKIL.first()
      ||
         WYDRUKIL.OD_DATY:=D_HELP.OD;
         WYDRUKIL.DO_DATY:=D_HELP.DO;
         WYDRUKIL.put();
         wyjscie:=0
      ?}
   ?}
}
{END:
   VAR_DEL.delete('w','s','PRN','tryb');
   VAR_DEL.delete('wyjscie','X_Xw1','lp','doklw');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: exec('upr_cm','ceny')=0}{ FUN.info('Brak uprawnień do wartości magazynowych.'@);~~}{EXIT}
{ELIF: exec('upr_cs','ceny')=0}{ FUN.info('Brak uprawnień do wartości sprzedaży.'@);~~}{EXIT}
{FI}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("w[1]",5,'Lp.'@,1) ?};
PRN.add("w[2]",20,'Zamówienie'@);
PRN.add("w[3]",30,'Kontrahent'@);
PRN.add("w[4]",15,'Wartość zakupu'@,1);
PRN.add("w[5]",15,'Koszty dodatkowe'@,1);
PRN.add("w[6]",15,'Wartość sprzedaży'@,1);
PRN.add("w[7]",15,'Zysk'@,1);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'ZK'     ,'STRING[20]'  ,'Symbol zamówienia',
   'KH'     ,'STRING[70]'  ,'Kontrahent',
   'WZ'     ,'REAL'        ,'Wartość zakupu',
   'KD'     ,'REAL'        ,'Koszty dodatkowe',
   'WS'     ,'REAL'        ,'Wartość sprzedaży',
   'Z'      ,'REAL'        ,'Zysk');
X_Xw1.index(X_Xw1.ndx_tmp(,,'ZK',,));

"--obliczenia w masce zbiorczej--";
ZK_N.index('ZAM_KL'); ZK_N.prefix('Z');
{? ZK_N.first()
||
   {!
   |? {? ZK_N.STAT_REJ<>'A'
         & (ZK_N.DP>=D_HELP.OD | D_HELP.OD=date(0,0,0)) & (ZK_N.DP<=D_HELP.DO | D_HELP.DO=date(0,0,0))
      ||
         BEER.ZK_N:=ZK_N.ref();
         exec('s_dokmag','zamsiw_rea');
         X_Xw1.ZK:=ZK_N.SYM;
         X_Xw1.KH:=ZK_N.KH().KOD+' '+KH.NAZ;
         X_Xw1.WZ:=all_wm;
         X_Xw1.KD:=DISP.Z_S;
         X_Xw1.WS:=all_ws;
         X_Xw1.Z:=X_Xw1.WS-X_Xw1.WZ-X_Xw1.KD;
         X_Xw1.add()
      ?};
      ZK_N.next()
   !}
?};
"--obliczenia w maskach archiwalnych--";
OKR.cntx_psh();
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   ZK_N.cntx_psh(); ZK_RN.cntx_psh();
   {!
   |? _maska:=ST.ODDZ+form(form(OKR.ROK,,,'99'),-2);
      ZK_N.use('zknag'+_maska);
      ZK_RN.use('zkhin'+_maska);
      ZK_N.index('ZAM_KL'); ZK_N.prefix('Z');
      {? ZK_N.first()
      ||
         {!
         |? {? ZK_N.STAT_REJ<>'A'
               & (ZK_N.DP>=D_HELP.OD | D_HELP.OD=date(0,0,0)) & (ZK_N.DP<=D_HELP.DO | D_HELP.DO=date(0,0,0))
            ||
               BEER.ZK_N:=ZK_N.ref();
               exec('s_dokmag','zamsiw_rea');
               X_Xw1.ZK:=ZK_N.SYM;
               X_Xw1.KH:=ZK_N.KH().KOD+' '+KH.NAZ;
               X_Xw1.WZ:=all_wm;
               X_Xw1.KD:=DISP.Z_S;
               X_Xw1.WS:=all_ws;
               X_Xw1.Z:=X_Xw1.WS-X_Xw1.WZ-X_Xw1.KD;
               X_Xw1.add()
            ?};
            ZK_N.next()
         !}
      ?};
      OKR.next()
   !};
   ZK_N.cntx_pop(); ZK_RN.cntx_pop()
?};
OKR.cntx_pop();

PAR_WYDR.TITLE:='Zamówienia sprzedaży - zysk'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział: %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1}}{'od daty: %1 do daty: %2'@[$D_HELP.OD,$D_HELP.DO]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xw1}
{DO}
   {
   lp+=1;
   w[1]:=form(lp,,,'99');
   w[2]:=form(X_Xw1.ZK);
   w[3]:=form(X_Xw1.KH);
   w[4]:=form(X_Xw1.WZ,,doklw,'.,');
   w[5]:=form(X_Xw1.KD,,doklw,'.,');
   w[6]:=form(X_Xw1.WS,,doklw,'.,');
   w[7]:=form(X_Xw1.Z,,doklw,'.,');
   s[1]+=X_Xw1.WZ;
   s[2]+=X_Xw1.KD;
   s[3]+=X_Xw1.WS;
   s[4]+=X_Xw1.Z
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}{SPACE: 'C'}
{
PRN.n_frame();
w[1]:=w[2]:='';
w[3]:='Razem:'@; PRN.FORM[3]:=0;
w[4]:=form(s[1],,doklw,'.,');
w[5]:=form(s[2],,doklw,'.,');
w[6]:=form(s[3],,doklw,'.,');
w[7]:=form(s[4],,doklw,'.,')
;~~}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
:!UTF-8
{TITLE:Lista osób bez naliczonej listy płac}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','Lp','rsep','tresc','b_rat','h_rat','__edit','__param','__okredit');
   tresc:='pbezwyn';
   PAR_WYDR.TITLE:='Lista osób bez naliczonej listy płac'@;
   P.cntx_psh();
   H.cntx_psh();
   LS.cntx_psh();
   LS.index('PRACNRRU');
   par:=obj_new(10); par[7]:=par[10]:=0;
   par[9]:=par[8]:=O.R;
   O.cntx_psh();
   O.index('LISTYPLA');
   O.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P);
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_PSKP','PPL_PLL_RSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   |? ~ O.first
   || FUN.emsg('Nie zostały zdefiniowane listy płac.'@+'\n'+'Brak danych do wykazania na raporcie.'@);
      par[8]:=par[9]:=0
   || params_set('P',exec('wybierz_parses','pracownik','PPL').P);

      SEEK.O:=__PARSES.getVal('ListaPłac').REF;
      SEEK.O();

      __param:=tab_tmp(1,
         'RODZAJ','INTEGER','Należy określić rodzaj wydruku',
         'ROK','INTEGER','Rok',
         'MC','INTEGER','Miesiąc'
      );
      __param.fld_fml('RODZAJ','AFTER_EDIT',"__okredit(~__param.RODZAJ)");
      __param.RODZAJ:=1;
      __param.ROK:=O.R;
      __param.MC:=O.M;

      __okredit:=
         "  __param.efld_opt(__edit,'enable='+$_a,,'ROK');
            __param.efld_opt(__edit,'enable='+$_a,,'MC')
         ";
      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pbezwyn');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(
         __edit,,
         'RODZAJ',,,,,,
         'Należy określić rodzaj wydruku'@,,,
         'radio-buttons',,
         'bieżąca Lista'@,"1",
         'wybrany Miesiąc'@,"0"
      );
      __param.win_efld(__edit,,'ROK',,,4,,,'Rok'@,,'Rok ('+$par[8]+'-'+$par[9]+')'@);
      __param.win_efld(__edit,,'MC',,,2,,,'Miesiąc'@,,'Miesiąc (1-12)'@);
      __param.efld_opt(__edit,'mark=1',,'ROK');
      __param.efld_opt(__edit,'mark=1',,'MC');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      __okredit(0);
      {? par[10]:=__param.edit(
            "  {?  __param.ROK<=1900
               || FUN.emsg('Błędna wartość parametru - Rok.'@);
                  'ROK'
               |?  __param.MC<1 | __param.MC>12
               || FUN.emsg('Błędna wartość parametru - Miesiąc.'@);
                  'MC'
               || 1
               ?}
            "
         )
      || par[1]:=obj_new(@.CLASS.PRINT,6);
         par[2]:=__param.ROK;
         par[3]:=__param.MC;
         par[7]:=__param.RODZAJ;
         {? ~par[7]
         || O.prefix(exec('ref_firma','ustawienia'),__F_ZATR.O,par[2],par[3]);
            par[8]:=O.R;
            O.last();
            par[9]:=O.R
         ?};

         {? ~rep_export() || par[1].add("Lp+=1",5,'Lp.'@,1,,,,,',,\'9.\'') ?};
         par[1].add("P.T",10,'Teczka'@);
         par[1].add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
         par[1].add("OSOBA.PIERWSZE",20,'Imię'@);
         par[1].add("H.WYDZIAL().SYMBOL",16,'Jednostka organizacyjna'@);
         par[1].add("H.CP().CP",12,'Charakter pracy'@);
         par[1].s_frame()
      ?};
      prn1:='par[1]';
      lmt:=Lp:=rsep:=0;
      vp:=par[4]:=1
   ?}
}
{END:
   P.cntx_pop();
   O.cntx_pop();
   H.cntx_pop();
   LS.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','Lp','rsep','tresc','b_rat','h_rat','__edit','__param','__okredit')
}
{COMMENT}OLD: pbezwyn.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~par[8] | ~par[9]}
{EXIT: ~par[10]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {IF: par[7]}
      {<{lmt+1}'Lista płac: '@+~(-O.LT)}
   {ELSE}
      {<{lmt+1}'Rok kalendarzowy: '@+$par[2]}
      {<{lmt+1}'Miesiąc: '@+$par[3]}
   {FI}
{FI}
{LINE}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: H.index('_HISTKOD');
     H.prefix(P.ref(),'Z');
     {? H.find_le(date(par[2],par[3],0))
     || {? H.DO=#0 | H.DO>=date(par[2],par[3],1)
        || {? ~par[7]
           || _test:=0;
              {? O.first()
              || {!
                 |? {? FUNKCJE.TESTKRYT(0)
                    || LS.use(-O.LT);
                       LS.index('PRACNRRU');
                       LS.prefix(P.ref());
                       _test:=LS.first()
                    ?};
                    ~_test & O.next()
                 !}
              ?};
              ~_test
           |? FUNKCJE.TESTKRYT(0)
           || LS.prefix(P.ref());
              ~LS.first()
           ?}
        ?}
     ?}}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
   {SUBSTITUTE: tresc}
   {FI}
{OD}
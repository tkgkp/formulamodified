:!UTF-8
{TITLE: Dokument zbiorczy}
{PRINTER: FINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN: PRN_GR:=obj_new(@.CLASS.PRINT,6); PRN_GR.sl_frame();
        P_DOK:=obj_new(@.CLASS.PRINT,10); P_DOK.sl_frame();
        P_POZ:=obj_new(@.CLASS.PRINT,6); P_POZ.sl_frame();
        P_SUM:=obj_new(@.CLASS.PRINT,4); P_SUM.sl_frame();
        color:=''; prn1:='PRN_GR';prn2:='PRN_GR'; lm:=ll:=lmt:=rsep:=0; vp:=1;
        s_dok_wn:=s_dok_ma:=s_gr_wn:=s_gr_ma:=l_dok:=l_gr:=0;opis:='';
        DOK.cntx_psh(); DOK.prefix(); POZ.cntx_psh(); GR_DOK.cntx_psh()
}
{END: VAR_DEL.delete('PRN_GR','P_DOK','P_POZ','P_SUM');
      &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep;
      &s_dok_wn; &s_dok_ma; &s_gr_wn; &s_gr_ma; &l_dok; &l_gr; &opis;
      DOK.cntx_pop(); POZ.cntx_pop(); GR_DOK.cntx_pop()
}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}
{LINE: 1}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}{prn1:='PRN_GR';~~}
{SUBSTITUTE: 'TABHEAD'}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8G'}{SPACE: 'C'}
{prn1:=prn2;~~}{SUBSTITUTE: 'LINIAF'}{prn1:='PRN_GR';~~}
{FOOTER-}
{COMMENT}------------------------------STRONA-----------------------{COMMENT-}
{MACRO: 'STRONA'}
{IF: lineleft<2 & lineleft<>0}{PAGE}{prn1:='PRN_GR';~~}
{ELSE}
{IF: lineleft()=0}{prn1:='PRN_GR';~~}{ELSE}{prn1:=prn2;~~}{FI}
{FI}
{MACRO-}
{COMMENT}------------------------------DOK_SUMA-----------------------{COMMENT-}
{MACRO: 'DOK_SUMA'}
{  s_dok_wn:=s_dok_ma:=0;
   POZ.index('DOK'); POZ.prefix(DOK.ref());
   {? POZ.first()
   || {!
      |? {? 1+POZ.STR='W' || s_dok_wn+=POZ.SUM || s_dok_ma+=POZ.SUM ?};
         POZ.next()
      !}
   ?};
~~}
{MACRO-}
{COMMENT}-------------------------------GRUPA------------------------{COMMENT-}
{MACRO: 'GRUPA'}{FONT: 'T8GB'}
{l_gr:=0;s_gr_wn:=s_gr_ma:=0; prn2:='PRN_GR';rsep:=1;~~}{SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}{FONT: 'T8G'}
{FOR: DOK}
{INDEX: 'DZ'}
{PREFIX: GR_DOK.ref()}
{DO}
  {l_gr+=1;l_dok:=0;prn2:='P_DOK';rsep:=1; ~~}
  {SUBSTITUTE: 'DOK_SUMA'}{s_gr_wn+=s_dok_wn; s_gr_ma+=s_dok_ma;~~}
  {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
    {rsep:=1; ~~}
    {FOR: POZ}
    {INDEX: 'DOK'}
    {PREFIX: DOK.ref()}
    {DO}
      {prn2:='P_POZ';~~}
      {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}{l_dok+=1;prn2:='P_POZ';rsep:=0;~~}
    {OD}
{OD}
{IF: l_gr>1}
  {opis:='SUMA GRUPY DOKUMENTÓW: %1'@[GR_DOK.SYM];s_dok_wn:=s_gr_wn; s_dok_ma:=s_gr_ma; prn2:='P_SUM'; rsep:=1;~~}
  {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
  {opis:='SALDO GRUPY DOKUMENTÓW: %1'@[GR_DOK.SYM];s_dok_wn:=s_dok_ma:=0; {? s_gr_wn>s_gr_ma || s_dok_wn:=s_gr_wn-s_gr_ma || s_dok_ma:=s_gr_ma-s_gr_wn ?}; rsep:=1;~~}
  {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
{FI}
{MACRO-}
{INCLUDE: wsp_pw.rpi}
{SUBSTITUTE: 'FOOT'}
{FONT: 'T8G'}{SPACE: 'C'}
{  PAR_WYDR.TITLE:={? UNPAR.P1 || 'Dokumenty zbiorcze rejestru %1 - %2'@[REJ.ODD().OD,REJ.KOD] || 'Dokument zbiorczy \' %1 \' '@[GR_DOK.SYM] ?};
   PRN_GR.add("GR_DOK.SYM",44,'Symbol dokumentu zbiorczego #Jedn. ks. / Rejestr - Numer # Pozycja / Konto'@,,'#');
   PRN_GR.add("GR_DOK.OPIS",64,'Opis dokumentu zbiorczego#Symbol dokumentu / Data oper. / Data zap. / Data wys.#Opis pozycji'@,,'#');
   PRN_GR.add("''",14,'Wn');
   PRN_GR.add("''",14,'Ma');
   P_DOK.add("DOK.ODD().OD",8,'Oddział'@);
   P_DOK.add("DOK.REJ().KOD+' - '+$DOK.NR",35,'Rejestr'@);
   P_DOK.add("DOK.NK",31,'Symbol'@);
   P_DOK.add("$DOK.DOP",10,'Data oper.'@);
   P_DOK.add("$DOK.DTW",10,'Data zap.'@);
   P_DOK.add("$DOK.DTO",10,'Data wyst.'@);
   P_DOK.add("form(s_dok_wn,,2,' ,')",14,'Wn',1);
   P_DOK.add("form(s_dok_ma,,2,' ,')",14,'Ma',1);
   P_POZ.add("$POZ.POZ",8,'Pozycja'@,1);
   P_POZ.add("POZ.KON",35,'Konto'@);
   P_POZ.add("POZ.OP",64,'Opis'@);
   P_POZ.add("form({? 1+POZ.STR='W' || POZ.SUM || 0 ?},,2,' ,')",14,'Wn',1);
   P_POZ.add("form({? 1+POZ.STR='M' || POZ.SUM || 0 ?},,2,' ,')",14,'Ma',1);
   P_SUM.add("opis",109,'');
   P_SUM.add("form(s_dok_wn,,2,' ,')",14,'Wn',1);
   P_SUM.add("form(s_dok_ma,,2,' ,')",14,'Ma',1);
~~}
{IF: UNPAR.P1}
{FOR: GR_DOK}
{INDEX: 'REJ'}
{PREFIX: REF.FIRMA,'T',SSTALE.AO,REJ.ref()}
{DO}
{SUBSTITUTE: 'GRUPA'}
{OD}
{ELSE}
{SUBSTITUTE: 'GRUPA'}
{FI}

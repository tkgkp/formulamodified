:!UTF-8
{DEFINEFONT: 'TM=Inter,,M,,0,0'}
{DEFINEFONT: 'TMB=Inter,,N,,0,0'}
{DEFINEFONT: 'TF=Inter,,F,,0,0'}
{COMMENT}   MACRO - druk                                             {COMMENT-}
{MACRO: 'druk'}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}
{FOOTER}
{{? ilkop=1 || stron:=page-pages ?};~~}{FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}{<1>{charleft}
   exec('tr','tlumaczenia','1000211')+' '+$(page-pages-(ilkop-1)*stron)}
{FOOTER-}
{WHILE: ilkop<=PARAM_W.KOPIA}
{DO}
   {
   _doksym:=_dokdt:='';
   {? REK_N.FAKS<>''
   || _doksym:=REK_N.FAKS;
      _dokdt:=REK_N.FAKSD
   |? REK_N.ZAM<>''
   || _doksym:=REK_N.ZAM;
      _dokdt:=REK_N.ZAMD
   ?};
   PAR_WYDR.TITLE:={? BEER.SZ='S' || 'Reklamacja klienta ' || 'Reklamacja do dostawcy ' ?}
                    +exec('tr','tlumaczenia','1000102')+' '+REK_N.SYM+'\n'+
                    {? _doksym<>''
                    || 'Dotyczy dokumentu nr: '+_doksym+' z dnia '+form(_dokdt)
                    || ''
                    ?};
   prn1:='PRN1';
   rsep:=0
   ;~~}
{COMMENT}   REPORT - HEADER                                          {COMMENT-}
   { head:=PAR_WYDR.HEAD;PAR_WYDR.HEAD:=0;~~}
   {SUBSTITUTE: 'HEADF'}
{COMMENT}   HEDER obowiazujacy przed wydrukiem pozycji               {COMMENT-}
   {HEADER}
   {SUBSTITUTE: 'HEADF'}
   {HEADER-}
{COMMENT}   oryginal / kopia, tryb probny                            {COMMENT-}
   {FONT: exec('font','param_wydr','ory_kop')}{SPACE: exec('space','param_wydr')}
   {<1}{>{charleft}{? ilkop=0 || exec('tr','tlumaczenia','1000103') || exec('tr','tlumaczenia','1000104') ?}
         +{? -PARAM_W.DR_DUPL='t' ||' - '+exec('tr','tlumaczenia','1000105')+' '+PARAM_W.DUPLIKAT$1||''?}}
{ ilkop+=1;~~}
{COMMENT}   DANE NAGLOWKOWE                                          {COMMENT-}
{ __STRTAB:='REK_N'; __STRODB:='REK_N.KH_ODB'; __STRLIN:=''; __STRDAT:=REK_N.DP;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{COMMENT}   DANE  KONTAKTOWE                                         {COMMENT-}
{
max_T10B:=charleft;
VAR_DEL.delete('szer');szer:=obj_new(5);
szer[1]:=1;
szer[3]:=3;
szer[4]:=1;
szer[5]:=1;
szer[2]:=(max_T10B-(szer[1]+szer[3]+szer[5]+16))/2$0;
print3(szer[1],szer[2],szer[3],szer[4],szer[5]);
szer[4]+=charleft-PRN3.width();
print3(szer[1],szer[2],szer[3],szer[4],szer[5]);
vp:=0;
lmt:=0;
seller_first:={? exec('get','#params',100229)='T' || 1 || 0?};
op_1:=op_2:=op_3:=op_4:='';
fill_dane();
~~}
{COMMENT}   DANE KONTAKTOWE   {COMMENT-}
{IF: +__dk1[5] | +__dk2[5] | +__dk1[2] | +__dk2[2] | +__dk1[3] | +__dk2[3] | +__dk1[4] | +__dk2[4]}
   {IF: REK_N.KH_ODB<>null() | exec('get','#params',100222)='T'}{LINE}{FI}
   {COMMENT}   LINIA 1: __dk1,__dk2;   ETYKIETA DANYCH KONTAKTOWYCH  {COMMENT-}
   {FONT: exec('font','param_wydr','strony_s1_l07')}
   {IF: +__dk1[1] | +__dk2[1]} {{? seller_first || op_1:=__dk2[1]; op_3:=__dk1[1] || op_1:=__dk1[1]; op_3:=__dk2[1] ?};~~} {SUBSTITUTE: 'LINIA0'}{FI}
   {LINE}
   {COMMENT}   LINIA 2: __dk1,__dk2;   KOD,MIASTO                    {COMMENT-}
   {FONT: exec('font','param_wydr','strony_s1_l03')}
   {IF: +__dk1[2] | +__dk2[2]} {{? seller_first || op_1:=__dk2[2]; op_3:=__dk1[2] || op_1:=__dk1[2]; op_3:=__dk2[2] ?};~~} {SUBSTITUTE: 'LINIA0'}{FI}
   {COMMENT}   LINIA 3: __dk1,__dk2;   UL .KDP                       {COMMENT-}
   {FONT: exec('font','param_wydr','strony_s1_l04')}
   {IF: +__dk1[3] | +__dk2[3]} {{? seller_first || op_1:=__dk2[3]; op_3:=__dk1[3] || op_1:=__dk1[3]; op_3:=__dk2[3] ?};~~} {SUBSTITUTE: 'LINIA0'}{FI}
   {COMMENT}   LINIA 4: __dk1,__dk2;   TEL                           {COMMENT-}
   {FONT: exec('font','param_wydr','strony_s1_l12')}
   {IF: +__dk1[4] | +__dk2[4]} {{? seller_first || op_1:=__dk2[4]; op_3:=__dk1[4] || op_1:=__dk1[4]; op_3:=__dk2[4] ?};~~} {SUBSTITUTE: 'LINIA0'}{FI}
   {COMMENT}   LINIA 5: __dk1,__dk2;   EMAIL                         {COMMENT-}
   {FONT: exec('font','param_wydr','strony_s1_l12')}
   {IF: +__dk1[5] | +__dk2[5]} {{? seller_first || op_1:=__dk2[5]; op_3:=__dk1[5] || op_1:=__dk1[5]; op_3:=__dk2[5] ?};~~} {SUBSTITUTE: 'LINIA0'}{FI}
{FI}
{COMMENT}   odkreslenie                                              {COMMENT-}
{IF: lineleft()-62>0}{LINE: lineleft()-62}{FI}
{LINE: 2}
{ op1:=op3:='';~~}
{COMMENT}   wyliczenie szerokosci kolumn                             {COMMENT-}
   {FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
   {<1}{
   prn1:='PRN1';
   {? ilkop=1
   || {! _i:=1..obj_len(szer1) |! szer1[_i]:=0 !};
      szer1[1]:={? (+(REK_N.M().KTM+' - '+M.N))>szer1[1] || +(M.KTM+' - '+M.N) || szer1[1] ?};
      szer1[2]:={? (+form(REK_N.IL,,ddokl))>szer1[2] || +form(REK_N.IL,,ddokl) || szer1[2] ?};
      _tlumacz:=exec('trans_jm','tlumaczenia',REK_N.JM,PARAM_W.JEZYK);
      _tlumacz:={? PARAM_W.JEZYK<>null || _tlumacz || REK_N.JM().KOD ?};
      szer1[3]:={? (+_tlumacz)>szer1[3] || +_tlumacz || szer1[3] ?};
      {! _i:=1..obj_len(k) |! {? szer1[_i]=0 || k[_i]:=0 ?} !};
      {! _i:=2..obj_len(k)
      |!
         {? szer1[_i]>k[_i] || k[_i]:=szer1[_i] ?}
      !};
      k[1]:=1; print1();
      k[1]+=charleft-PRN1.width();
      print1()
   ?}
   ;~~}
{COMMENT}   Dane reklamowanego towaru/usługi   {COMMENT-}
   {IF: REK_N.M<>null()}
      {<{lmt+1}
      }{FONT: exec('font','param_wydr','dane_bold')}{'Reklamacja dotyczy:'@}
{COMMENT}   odrysowanie naglowka tabeli   {COMMENT-}
      {FONT: exec('font','param_wydr','tabhead_poz')}
      {prn1:='PRN1';  pozycja(); ~~}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{COMMENT}   drukowanie danych dot. towaru/usługi   {COMMENT-}
      {FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
      {
      rsep:=PAR_WYDR.RSEP;
      prn1:='PRN1'
      ;~~}
      {SUBSTITUTE: 'LINIA0'}
      {SUBSTITUTE: 'LINIAF'}
   {FI}
{COMMENT}   opis   {COMMENT-}
   {IF: +REK_N.memo_txt(,1,'OPIS')}
      {VAR_DEL.delete('__memo','text','Text');~~}
      { __memo:=REK_N.memo_get('r','OPIS'); text:='';~~}
      {IF: __memo.is_open}
         {LINE}
         {<1}{FONT: exec('font','param_wydr','dane_bold')}{'Opis reklamacji:'@}
         {FONT: exec('font','param_wydr','opis')}
         {WHILE: text:=fread(__memo); text<>'\n'}
         {DO}
            {<{lmt+1}}
            {
            max_T8:=charleft;
            STR.split(text);
            Text:='';
            sep:=0
            ;~~}
            {WHILE: STR.next()}
            {DO}
               {FIRST}{LINE}{FIRST-}
               { word:=STR.get_word(); sep+=1;~~}
               {IF: +Text+(+word)>=(max_T8)}
                  {<{lmt+1}}{JUSTIFY}{<{lmt+1}>{max_T8}Text}
                  { Text:=word; sep:=1;~~}
               {ELSE}
                  { Text+={? sep>1 || ' ' || '' ?}+word;~~}
               {FI}
            {OD}
            {IF: +Text}
               {<{lmt+1}}{Text}
            {FI}
         {OD}
         {CENTER}
      {FI}
      {VAR_DEL.delete('__memo','text','Text');~~}
   {FI}
{COMMENT}   wyliczenie szerokosci kolumn                             {COMMENT-}
   {FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
   {<1}{
   prn1:='PRN2';
   {? ilkop=1
   || {! _i:=1..obj_len(szer2) |! szer2[_i]:=0 !};
      szer2[1]:={? (+REK_N.ST)>szer2[1] || +(REK_N.ST) || szer2[1] ?};
      szer2[2]:={? (+REK_N.ESR().DANE)>szer2[2] || +form(REK_N.ESR().DANE) || szer2[2] ?};
      szer2[3]:={? (+form(REK_N.DZ))>szer2[3] || +form(REK_N.DZ) || szer2[3] ?};
      _opis:={? REK_N.memo_lin('RES_OPIS')<>'\n' || gsub(REK_N.memo_lin('RES_OPIS'),'\n',' ') || '' ?};
      szer2[4]:={? (+_opis)>szer2[4] || +(_opis) || szer2[4] ?};
      {! _i:=1..obj_len(k2)
      |!
         {? szer2[_i]>k2[_i] || k2[_i]:=szer2[_i] ?}
      !};
      k2[4]:=1; print2();
      k2[4]+=charleft-PRN2.width();
      print2()
   ?}
   ;~~}
{COMMENT}   Ocena reklamacji   {COMMENT-}
   {LINE}
   {<1
   }{FONT: exec('font','param_wydr','dane_bold')}{'Ocena reklamacji:'@}
   {IF: REK_N.ST<>''}
{COMMENT}   odrysowanie naglowka tabeli   {COMMENT-}
      {FONT: exec('font','param_wydr','tabhead_poz')}
      {prn1:='PRN2';  pozycja2(); ~~}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{COMMENT}   drukowanie danych  {COMMENT-}
      {FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
      {
      rsep:=PAR_WYDR.RSEP;
      prn1:='PRN2'
      ;~~}
      {SUBSTITUTE: 'LINIA0'}
      {SUBSTITUTE: 'LINIAF'}
   {ELSE}
      {LINE}
      {<{lmt+1}}{FONT: exec('font','param_wydr','dane')}{'Nie rozpatrzono'@}
   {FI}
{COMMENT}   podpisy                                                  {COMMENT-}
{FONT: exec('font','param_wydr','podpisy')}{SPACE: exec('space','param_wydr')}
{LINE: 3}
{IF: REK_N.USR<>null}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,REK_N.USR().DANE);
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {
   _szer:=20;
   PRN.HEAD[1]:='';
   PRN.HEAD[2]:=30*'─';
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {
   _szer:=20;
   PRN.HEAD[1]:='';
   PRN.HEAD[2]:='Reklamację zarejestrował';
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{FI}
   {CENTER}
   {PAR_WYDR.HEAD:=head;~~}
   {HEADER}{HEADER-}
   {IF: lineleft()>0}{PAGE}{FI}
{OD}
{MACRO-}
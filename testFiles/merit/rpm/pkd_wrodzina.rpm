:!UTF-8
{TITLE:Stan rodzinny}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='pkd_wrodzina';
   PAR_WYDR.TITLE:='Stan rodzinny'@;
   P.cntx_psh();
   par:=obj_new(7);
   par[7]:=exec('uprawnieniawrap','pkd','członkach rodziny'@,'PKD_DOS_PPRD','PKD_DOS_PRRO');
   {? +par[7]
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[par[7]])
   || par[1]:=obj_new(@.CLASS.PRINT,{? rep_export() || 11 || 6 ?});
      {? rep_export()
      ||   par[1].add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
           par[1].add("P.OSOBA().PIERWSZE+' '+P.OSOBA().DRUGIE",41,'Imiona'@);
           par[1].add("P.OSOBA().PESEL",11,'PESEL'@)
      ?};
      par[1].add("RD.NA",20,'Nazwisko członka rodziny'@);
      par[1].add("RD.IM",20,'Imię członka rodziny'@);
      par[1].add("RD.DA$1",10,'Data urodzenia członka rodziny'@);
      par[1].add("RD.PESEL",11,'PESEL członka rodziny'@);
      {? ~rep_export()
      || par[1].add(
            "  {? RD.KW_OD<>par[3] || RD.KW_OD$1 || 10*'_' ?}+' '+{? RD.KW_DO<>par[3] || RD.KW_DO$1 || 10*'_' ?}
            ",21,'Zasiłek rodzinny'@
         );
         par[1].add(
            "  {? RD.ZP_OD<>par[3] || RD.ZP_OD$1 || 10*'_' ?}+' '+{? RD.ZP_DO<>par[3] || RD.ZP_DO$1 || 10*'_' ?}
            ",21,'Zasiłek pielęgnacyjny'@
         )
      || par[1].add("{? RD.KW_OD<>par[3] || RD.KW_OD$1 || '' ?}",21,'Zasiłek rodzinny - data od'@);
         par[1].add("{? RD.KW_DO<>par[3] || RD.KW_DO$1 || '' ?}",21,'Zasiłek rodzinny - data do'@);
         par[1].add("{? RD.ZP_OD<>par[3] || RD.ZP_OD$1 || '' ?}",21,'Zasiłek pielęgnacyjny - data od'@);
         par[1].add("{? RD.ZP_DO<>par[3] || RD.ZP_DO$1 || '' ?}",21,'Zasiłek pielęgnacyjny - data do'@)
      ?};
      par[1].s_frame();

      par[2]:=tab_tmp(,'REF','INTEGER','Osoba');

      prn1:='par[1]';
      par[4]:=par[6]:=lmt:=rsep:=0;
      par[5]:=vp:=b_rat:=h_rat:=1;
      par[3]:=date(0,0,0);

      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat')
}
{COMMENT}OLD: wrodz.rpm{COMMENT-}
{COMMENT}OLD: std_osop.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: +par[7]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par[6]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[4]:=1; ~~ }
{MACRO-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[2].add();
      par[4]:=0; ~~
   }
   {ENTIRE}
   {FOR: RD}{INDEX: '_RODZINA'}{PREFIX: P.OSOBA}{DO}
   {FIRST}
      {SUBSTITUTE: 'pracownik'}
      {REP: par[1].fhbar(lmt)}
   {FIRST-}
      { par[1].ini_line(); ~~ }
      {WHILE: par[1].next()}{DO}
         {REP: par[1].fline(lmt)}
      {OD}
   {OD}
   {IF: par[4]}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
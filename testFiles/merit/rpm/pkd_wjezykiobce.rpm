:!UTF-8
{TITLE:Znajomość języków}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','vp','lmt','rsep','tresc');
   tresc:='pkd_wjezykiobce';
   PAR_WYDR.TITLE:='Znajomość języków'@;
   par:=obj_new(10);
   P.cntx_psh();
   par[3]:='';
   _upr:=exec('uprawnieniawrap','pkd','danych osobowych'@,'ZWS_DOS_PPDO','ZWS_DOS_PRDO')+
         exec('uprawnieniawrap','pkd','znajomości języków obcych'@,'PKD_DOS_PPJO','PKD_DOS_PRJO');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[2]:=obj_new(@.CLASS.PRINT,{? ~rep_export() || 4 || 6 ?});
      par[4]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,,,,,,,,
         'Jaki rodzaj sprawozdania sporządzić?'@,'wg Pracowników'@,'wg Języków'@
      );
      {? par[4].size()
      || par[3]:='ZNJEZ';
         {? par[4].ASK
         || {? rep_export()
            || par[2].add("ZJ.JO().NAZWA",40,'Język'@);
               par[2].add("ZJ.OSOBA().NAZWISKO",30,'Nazwisko'@);
               par[2].add("ZJ.OSOBA().PIERWSZE",20,'Imię'@)
            || par[2].add("ZJ.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE",40,'Osoba'@)
            ?};
            par[3]:=ZJ.ndx_tmp('Język',0,'JO',,0,'OSOBA','NAZWISKO',0,'OSOBA','PIERWSZE',0,'OSOBA','PESEL',0)
         || {? rep_export()
            || par[2].add("ZJ.OSOBA().NAZWISKO",30,'Nazwisko'@);
               par[2].add("ZJ.OSOBA().PIERWSZE",20,'Imię'@)
            ?};
            par[2].add("ZJ.JO().NAZWA",40,'Język'@)
         ?};
         par[2].add("ZJ.OC",5,'Ocena'@,1,,,,,'5,2');
         par[2].add("ZJ.SZ().NAZWA",20,'Stopień znajomości'@);
         par[2].add("ZJ.OD",35,'Określenie dokumentacji'@);
         par[2].s_frame();

         par[1]:=obj_new(@.CLASS.PRINT,2);
         {? ~rep_export()
         || par[1].add("par[6]",5,'Lp.'@,1,,,,,',,\'9.\'');
            {? par[4].ASK
            || par[1].add("ZJ.JO().NAZWA",101,'Język'@)
            || par[1].add("ZJ.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE",101,'Osoba'@)
            ?};
            par[1].s_frame()
         ?};

         par[8]:=tab_tmp(1,'REF','INTEGER','Rekord');

         lmt:=rsep:=0;
         vp:=0;

         par[5]:=1;
         par[6]:=par[7]:=0;
         par[9]:=null();
         {? par[4].ASK
         || params_set('P',exec('dostepne_p','schemat','PKD'
                                                      ,__PARSES.getVal('F_ZATR').KOD
                                                      ,__PARSES.getVal('ZakresDanych')))
         || params_set('P',exec('wybierz_parses','pracownik','PKD').P)
         ?}
      ?}
   ?}
}
{END:
   P.cntx_pop();
   {? par[3]<>'ZNJEZ' || ZJ.ndx_drop(par[3]) ?};
   VAR_DEL.delete('par','vp','lmt','rsep','tresc')
}
{COMMENT}OLD: wznjez.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: par[3]=''}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4].first() & par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{  par[1].setcolor(color);
   par[2].setcolor(color)
}
{par[1].ini_head()}
{REP: par[1].fhbar(lmt)}
{WHILE: par[1].h_next()}{DO}
   {REP: '{<{lmt+1}}'+par[1].h_fline(0)}
{OD}
{REP: '{<{lmt+1}}'+par[1].fjbar(par[2])}
{par[2].ini_head()}
{WHILE: par[2].h_next()}{DO}
   {REP: '{<{lmt+1}}'+par[2].h_fline(0)}
{OD}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{  par[7]:=0;
   par[10]:={? par[4].ASK || SLO_NAZ.ref() || P.OSOBA ?}; ~~
}
{FOR: ZJ}{INDEX: par[3]}{PREFIX: par[10]}{DO}
   {IF: ~par[8].find_key(#ZJ.ref) & {? par[4].ASK || params_get().P.find_tab(,'OSOBA',,'=',$ZJ.OSOBA) || 1 ?}}
      {  par[8].REF:=#ZJ.ref();
         par[8].add();
         {? par[9]<>par[10]
         || par[9]:=par[10];
            par[6]+=1
         ?}; ~~
      }
      {IF: ~par[7]}
         {IF: lineleft() & lineleft()<=6}
            {REP: par[2].flbar(lmt)}
            {PAGE}
         {FI}
         {IF: ~lineleft()}
            {  par[1].setcolor(color);
               par[2].setcolor(color); ~~
            }
         {FI}
         {REP: '{<{lmt+1}}'+par[2].fjbar(par[1])}
         {  par[1].setcolor('255:255:255');
            par[2].setcolor('255:255:255')
         }
         {par[1].ini_line()}
         {WHILE: par[1].next()}{DO}
            {REP: par[1].fline(lmt)}
         {OD}
         {REP: '{<{lmt+1}}'+par[1].fjbar(par[2])}
         { par[7]:=1; ~~ }
      {FI}
      {par[2].ini_line()}
      {WHILE: par[2].next()}{DO}
         {REP: par[2].fline(lmt)}
      {OD}
      {IF: lineleft()<=2}
         { par[7]:=0; ~~ }
      {FI}
   {FI}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{IF: par[4].ASK}
   {EXIT: ~exec('slo_typ','ext_slo','JEZOB')}
   {FOR: SLO_NAZ}{INDEX: 'NAZWA'}{PREFIX: SLO_TYP.ref()}{SELECT}{DO}
      {SUBSTITUTE: tresc}
   {OD}
{ELSE}
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{FI}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{REP: par[2].flbar(lmt)}
{FOOTER-}

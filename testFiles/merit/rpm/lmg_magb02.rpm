:!UTF-8
{TITLE:B. Etykiety dla dokumentu - kody kreskowe}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   VAR_DEL.delete('__kkresk');
   __kkresk:=tab_tmp(2,'ORD','STRING[10]',''
              ,'KODK','STRING[128]',''
              ,'SCEAN','STRING[128]',''
              ,'KOD','STRING[60]',''
              ,'OPIS','STRING[100]',''
              ,'OPIS2','STRING[255]','');
   _pal:=tab_tmp(1,'SQL','STRING[64]','');
   _lp:=0;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=exec('barcode_height','kody_kresk');
   ND.cntx_psh();
   DK.cntx_psh();
   DK_L.cntx_psh();
   PAL.cntx_psh();
   PAL_POZ.cntx_psh();
   DK.index('DOKMAG');
   DK.prefix(ND.ref());
   {? DK.first()
   || _czypal:=DK.N().MAG().PAL='T';
      {!
      |? {? _czypal
         || DK_L.index('DK');
            DK_L.prefix(DK.ref(),null);
            {? DK_L.first()
            || {!
               |? {? DK_L.PAL<>null
                  || _sql:=($DK_L.PAL)+form(0,-16,0,'99');
                     _pal.clear();
                     {? ~_pal.find_key(_sql)
                     || _lp+=1;
                        _pal.blank(); _pal.SQL:=_sql; _pal.add(1);
                        __kkresk.clear();
                        __kkresk.blank();
                        __kkresk.ORD:=form(_lp,-5,0,'99')+'00000';
                        __kkresk.KODK:=DK_L.PAL().KODK;
                        __kkresk.KOD:=DK_L.PAL().KODK;
                        __kkresk.OPIS:='paleta '+DK_L.PAL().TPAL().TYP;
                        {? __kkresk.add(1)
                        || {? DK_L.PAL().AKT='N'
                           || PAL_POZ.use('palet'+form(DK_L.PAL().AR-2000,-2,0,'99'))
                           || PAL_POZ.use('paletyp')
                           ?};
                           PAL_POZ.index('PAL');
                           PAL_POZ.prefix(DK_L.PAL);
                           {? PAL_POZ.first()
                           || {!
                              |? _sql:=($PAL_POZ.PAL)+($PAL_POZ.M);
                                 _pal.clear();
                                 {? ~_pal.find_key(_sql) & PAL_POZ.M().KODK<>''
                                 || _pal.blank(); _pal.SQL:=_sql; _pal.add(1);
                                    __kkresk.clear();
                                    __kkresk.blank();
                                    __kkresk.ORD:=form(_lp,-5,0,'99')+'00001';
                                    __kkresk.KODK:=PAL_POZ.M().KODK;
                                    __kkresk.KOD:='    '+PAL_POZ.M().KTM;
                                    __kkresk.OPIS:=PAL_POZ.M().N;
                                    __kkresk.add(1)
                                 ?};
                                 PAL_POZ.next()
                              !}
                           ?}
                        ?}
                     ?}
                  ?};
                  DK_L.next()
               !}
            ?}
         || _sql:=($DK.M)+form(0,-16,0,'99')+{? DK.SCEAN<>'' || DK.SCEAN || 'xxxx' ?};
            _pal.clear();
            {? ~_pal.find_key(_sql) & DK.M().KODK<>''
            || _lp+=1;
               _pal.blank(); _pal.SQL:=_sql; _pal.add(1);
               __kkresk.clear();
               __kkresk.blank();
               __kkresk.ORD:=form(_lp,-5,0,'99');
               __kkresk.KODK:=DK.M().KODK;
               __kkresk.KOD:=DK.M().KTM;
               __kkresk.OPIS:=DK.M().N;
               __kkresk.SCEAN:=DK.SCEAN;
               _opis:='';
               {? DK.SCEAN<>''
               || MKODK.cntx_psh();
                  MKODK.index('KK');
                  MKODK.prefix(DK.SCEAN,DK.SCEAN);
                  {? MKODK.first() & MKODK.M<>null
                  || _opis:={? MKODK.M().FOMOB<>null
                            || ($MKODK.M().FOMOB().FORMULA)()
                            || _op:=exec('formopis','kody_kresk',MKODK.IDMOB,MKODK.RSQL,0,,MKODK.M().IDZP);
                               _bop:=_op[1]; obj_del(_op); _bop
                            ?}
                  ?};
                  MKODK.cntx_pop()
               ?};
               __kkresk.OPIS2:=_opis;
               __kkresk.add(1)
            ?}
         ?};
         DK.next()
      !}
   ?};
   ND.cntx_pop();
   DK.cntx_pop();
   DK_L.cntx_pop();
   PAL.cntx_pop();
   PAL_POZ.cntx_pop();
   obj_del(_pal);

   etykieta:=PAR_WYDR.URZ_LAB;
   PAR_WYDR.URZ_LAB:=exec('wyb_etykiety','kody_kresk');
   wyjscie:={? PAR_WYDR.TYPDR<>null || PAR_WYDR.URZ_LAB=null ?};
   {? wyjscie=0 & PAR_WYDR.TYPDR=null & PAR_WYDR.KODKRESK=''
   || FUN.info('Brak parametryzacji drukowania kodów kreskowych.'@);
      wyjscie:=1
   |? __kkresk.clear(); ~__kkresk.first()
   || FUN.info('Brak pozycji z kodami kreskowymi.'@);
      wyjscie:=1
   ?}
}
{END:
   PAR_WYDR.URZ_LAB:=etykieta;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   VAR_DEL.delete('__kkresk','etykieta','wyjscie')
}
{EXIT: wyjscie=1}
{DEFINEFONT: 'courier=TexGyreCursor,,,8'}
{FONT: 'courier'}
{MACRO:'stan'}
   {ENTIRE}

      { temp:=__kkresk.KODK;~~}
      { temp2:={? __kkresk.SCEAN<>'' || __kkresk.SCEAN || '' ?};~~}

      {BARCODE: temp;3*(+temp)+1;1;BARCODE_TYPE}{IF: __kkresk.SCEAN<>''}

      {BARCODE: temp2;3*(+temp2)+1;1;BARCODE_TYPE}{FI}


   {FONT: 'courier'}{SPACE: 'B'}
   {__kkresk.KOD+' - '+__kkresk.OPIS}
   {IF: __kkresk.SCEAN<>''}{__kkresk.SCEAN+' - '+__kkresk.OPIS2}{FI}
  ─────────────────────────────────────
   {ENTIRE-}
{MACRO-}
{MACRO:'etykiety'}
   { wyjscie:=0;~~}
   {IF: PAR_WYDR.TYPDR<>null}
      { wyjscie:=exec('druk','kody_kresk')=0;~~}
   {ELSE}
      {SUBSTITUTE: 'stan'}
   {FI}
{MACRO-}
{IF: FUN.ask('Czy chesz wybrać kody do wydruku?'@)}
   { _win:=__kkresk.mk_sel('Wybór kodów kreskowych'@,,,'wyb_kodk',,,,,'U',,,,,,'auto');
     __kkresk.win_fld(_win,,'KOD',,,20,,,'Indeks'@);
     __kkresk.win_fld(_win,,'KODK',,,20,,,'Kod kreskowy'@);
     __kkresk.win_fld(_win,,'SCEAN',,,20,,,'Kod identyfikacyjny'@);
     __kkresk.win_fld(_win,,'OPIS',,,30,,,'Opis'@);
     __kkresk.win_act(_win,,'Szukaj');
     __kkresk.win_sel(_win);
   ~~}
   {FOR: __kkresk}
   {SELECT}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{ELSE}
   {FOR: __kkresk}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{FI}
:!UTF-8
{TITLE: Wykaz powiązań pozycji deklaracji z dokumentami VAT}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN: PRN1:=obj_new(@.CLASS.PRINT,13); PRN1.sl_frame(); w:=obj_new(13);
        PRN2:=obj_new(@.CLASS.PRINT,4); PRN2.sl_frame(); s:=obj_new(3);
        color:=''; prn1:='PRN1'; prn2:='PRN2'; lm:=ll:=lmt:=0; vp:=1; warunek:=0; rsep:=1; ost:=''; pierwszy:=1; tryb:=0;
        VAT_POZ.cntx_psh();
        VAT_PS.cntx_psh();
        VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
        PAR_WYDR.TITLE:='Wykaz powiązań pozycji deklaracji z dokumentami VAT'@
}
{END: VAR_DEL.delete('PRN1','PRN2','w','s'); &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &warunek; &ost;
      &pierwszy; &tryb;
      VAT_POZ.cntx_pop();
      VAT_PS.cntx_pop()
}
{EXIT: tryb:={? cur_tab()<>cur_tab(1,1) || 1 |? 6+cur_tab.name='vat_ps' || 2 || FUN.choice('W wydruku uwzględnić'@,1,'Wszystkie pozycje'@,'Bieżącą pozycję'@) ?}; ~tryb}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_ps_vat.rpm
Utworzony:   08-07-2009 [PB] Piotr Biendzio
==================================================
Zawartosc: Wydruk powiazan pozycji deklaracji VAT z dokumentami VAT
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_pw2.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{  PRN1.add("w[1]",10,'Rejestr VAT'@);
   PRN1.add("w[2]",8,'Nr w rej. VAT'@,1);
   PRN1.add("w[3]",12,'Rejestr księgowy'@);
   PRN1.add("w[4]",8,'Nr w rej. ks.'@,1);
   PRN1.add("w[5]",22,'Kontrahent'@);
   PRN1.add("w[6]",13,'NIP'@);
   PRN1.add("w[7]",10,'Data wyst.'@);
   PRN1.add("w[8]",10,'Termin płatności'@);
   PRN1.add("w[9]",6,'Zwł.'@,1);
   PRN1.add("w[10]",5,'%% VAT'@,1);
   PRN1.add("w[11]",14,'Netto'@,1);
   PRN1.add("w[12]",12,'VAT'@,1);
   PRN1.add("w[13]",14,'Brutto'@,1);
   PRN2.add("s[1]",113,'');
   PRN2.add("s[2]",14,'',1);
   PRN2.add("s[3]",12,'',1);
   PRN2.add("''",14,'',1);
~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{MACRO: 'SRODEK'}
{IF: VAT_POZ.TYP='A'}
{ s[1]:=VAT_POZ.LP+' '+VAT_POZ.OP;
  s[2]:=form(VAT_POZ.NETTO,,0,' ,');
  s[3]:=form(VAT_POZ.VAT,,0,' ,');
  {! _i:=1 .. 13 |! w[_i]:='' !};
~~}
{IF: lineleft<=2&lineleft>0}{PAGE}{FI}
{IF: vp=1}
{SUBSTITUTE: 'LINIA02'}
{ELIF: pierwszy=1 }
{prn1:='PRN2'; ~~}
{SUBSTITUTE: 'LINIA0'}
{prn1:='PRN1'; ~~}
{ELSE}
{SUBSTITUTE: 'LINIA02'}
{FI}
{pierwszy:=1;last:=2;~~}
{FOR: 'VAT_PS'}
{INDEX: 'VAT_PS'}
{PREFIX: VAT_DEK.ref(),VAT_POZ.ref()}
{DO}
{  w[1]:=VAT_PS.REJ_VAT;
   w[2]:={? VAT_PS.NR_VAT || $VAT_PS.NR_VAT || '' ?};
   w[3]:=VAT_PS.REJ_KS().KOD;
   w[4]:={? VAT_PS.NR_KS || $VAT_PS.NR_KS || ''?};
   w[5]:=VAT_PS.KH;
   w[6]:=VAT_PS.NIP;
   w[7]:=$VAT_PS.DATA;
   w[8]:=$VAT_PS.TP;
   w[9]:={? VAT_PS.REJ_VAT<>'' | VAT_PS.ZWL=-1 || ' - ' || VAT_PS.ZWL ?};
   w[10]:=VAT_PS.SVAT;
   w[11]:=form(VAT_PS.NETTO,,2,' ,');
   w[12]:=form(VAT_PS.VAT,,2,' ,');
   w[13]:=form(VAT_PS.BRUTTO,,2,' ,');
 ~~}
{IF: lineleft<=2&lineleft>0}{PAGE}{FI}
{IF: vp=1}
{SUBSTITUTE: 'LINIA0'}
{ELIF: pierwszy=1}
{prn1:='PRN2';prn2:='PRN1'; ~~}
{SUBSTITUTE: 'LINIA02'}
{prn1:='PRN1';prn2:='PRN2'; ~~}
{ELSE}
{SUBSTITUTE: 'LINIA03'}
{FI}
{pierwszy:=0;last:=1;~~}
{OD}
{ELSE}
{ {! _i:=1 .. 3 |! s[_i]:='' !};
  {! _i:=1 .. 11 |! w[_i]:='' !};
~~}
{FI}
{MACRO-}
{HEADER}
{SUBSTITUTE: 'HEAD'}{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}
   {<1 'PARAMETRY WYDRUKU:'@}
   {<3 'Okres deklaracji: %1             Typ deklaracji: %2             Kolejny wariant: %3             Data wypełnienia: %4'@[VAT_DEK.OKRES,VAT_DEK.TYP,$VAT_DEK.WER,$VAT_DEK.DATA]}
   {<3 {? VAT_DEK.ZN_PROC='T'
       || 'Naliczanie wg. %% struktury: Tak           %% sprzedaży opodatkowanej: %1'@[form(VAT_DEK.PROC,,2,' ,')]
       || 'Naliczanie wg. %% struktury: Nie'@
       ?}}
   {<3 {? tryb=2 || 'Wydruk dla wybranej pozycji.'@ || ~~ ?}}
{FI}
{FONT: 'T8B'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8'}{SPACE: 'C'}
{IF:last=1}{prn1:='PRN1';~~}{ELSE}{prn1:='PRN2';~~}{FI}{SUBSTITUTE: 'LINIAF'}{ prn1:='PRN1';vp:=1;~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'C'}
{IF: tryb=1}
{FOR: 'VAT_POZ'}
{INDEX: 'VAT_POZ'}
{PREFIX: VAT_DEK.ref()}
{DO}
{SUBSTITUTE: 'SRODEK'}
{OD}
{ELSE}
{FOR: 'VAT_POZ'}
{INDEX: 'VAT_POZ'}
{PREFIX: VAT_DEK.ref(),VAT_POZ.LP }
{DO}
{SUBSTITUTE: 'SRODEK'}
{OD}
{FI}
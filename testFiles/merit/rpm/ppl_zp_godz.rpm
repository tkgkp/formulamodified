:!UTF-8
{TITLE:Godziny pracy}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','upr');
   PAR_WYDR.TITLE:='Godziny pracy'@;
   R.cntx_psh();
   R.index('RUBLP');
   R.prefix();
   par:=obj_new(8);
   par[8]:=rep_export();
   par[2]:=par[4]:='';
   upr:=exec('uprawnieniawrap','pkd','godzinach pracy'@,'PPL_PLL_PGOD','PPL_PLL_RGOD');
   {? +upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[upr])
   || {? (par[4]:=1-exec('code_grp','profile',,'godzprac','R',{? par[8] || 50 || 9 ?})-1)<>''
      || echo('Trwa odczyt informacji o przepracowanych godzinach...'@);
         G.cntx_psh();
         G.use('godz'+('0000'+$O.R+4));

         par[3]:=
            sql('select OSOBA.NAZWISKO, OSOBA.PIERWSZE, P.T, R.LP, G.G
                 from   P join G using(P.REFERENCE,G.P) join OSOBA using(P.OSOBA,OSOBA.REFERENCE) join R
                 where  P.REFERENCE in (select :_a.SQL from :_a) and R.RN in (:_b) and G.R=\'G\'',
                 exec('wybierz_parses','pracownik','PPL').P,
                 par[4]
            );

         echo('Trwa przetwarzanie danych...'@);
         exec('object','#pivot');
         (par[2]:=obj_new(@.CLASS.PIVOT,par[3],'LP','sum','G')).
         grpfld('NAZWISKO','PIERWSZE','T').total(1).dosum(1).
         make();

         echo();

         {? par[2].valid()
         || par[1]:=obj_new(@.CLASS.PRINT,par[2].fld_num());
            {? ~par[8]
            || par[7]:=obj_new(@.CLASS.PRINT,par[2].sfld_num());
               par[7].add("'Razem'@",64)
            ?};

            {? ~par[8] || par[1].add("par[5]+=1",5,'Lp.'@,1,,,,,',,\'9.\'') ?};
            par[1].add("par[2].OUT.NAZWISKO",20,'Nazwisko'@);
            par[1].add("par[2].OUT.PIERWSZE",20,'Imię'@);
            par[1].add("par[2].OUT.T",10,'Teczka'@);
            {? ~par[8] || par[1].add("par[2].fld_val(4)",14,'Razem'@,1,,,,,'14,2') ?};

            {! _ii:=5..par[2].fld_num()-1
            |! _name:=par[2].fld_name(_ii);
               {? var_pres('_name')=type_of('')
               || _name:={? R.find_key(#_name) || R.RT || '' ?};
                  par[1].add($('par[2].fld_val('+$_ii+')'),14,_name,1,,,,,'14,2')
               ?}
            !};

            {? ~par[8]
            || {! _ii:=1..par[2].sfld_num()-1
               |! par[7].add($('par[2].sfld_val('+$_ii+')'),14,'',1,,,,,'14,2')
               !};
               par[7].s_frame()
            ?};

            par[1].s_frame()
         ?};
         G.cntx_pop()
      ?}
   ?};
   prn1:='par[1]';
   par[5]:=lmt:=rsep:=0;
   vp:=par[6]:=1
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','upr');
   R.cntx_pop();
   echo()
}
{EXIT: +upr}
{EXIT: par[4]='' | ~par[2].valid()}
{COMMENT}OLD: zp_godz.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[6]}
   { par[6]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Uwzględniane składniki:'@} {gsub(par[4],',',', ')}
   {<{lmt+1}'Rok kalendarzowy:'@} {$O.R}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~par[8]}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: par[2].OUT}{DO}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: ~par[8] & par[5]}
   { prn1:='par[7]'; ~~ }
   {REP: '{<{lmt+1}}'+par[1].jbar(par[7])}
   {SUBSTITUTE: 'LINIA0'}
{FI}
:!UTF-8
{TITLE:C.  Dokumenty z rejestru księgowego}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
       sstr7:=0; suma7:=0; rejestr:=''; petla:=0; w:=obj_new(7); szer:=obj_new(7); ww:=obj_new(7); suma:=0;
       PRN:=obj_new(@.CLASS.PRINT,7); PRN.sl_frame();
       PRN1:=obj_new(@.CLASS.PRINT,7); PRN1.sl_frame();
       ROZNE.REJ:=null; mdc:=''; lm:=0; font:='J'
 }
{END: &petla; &sstr7; &suma7; obj_del(w); obj_del(szer); obj_del(ww); obj_del(PRN); obj_del(PRN1); &suma;
      &mdc; &lm; &font
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: k_rejks.rpm
Utworzony:   ????-??-??
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Wydruk dokumentow z rejestru ksiegowego
{COMMENT-}
   { WYDRUKIN.win_edit('WYBDATA3');
      WYDRUKIN.DATAOD:=SSTALE.AO().POCZ;
      WYDRUKIN.DATADO:=SSTALE.AO().KON;  ~~
  }
    {EXIT: {? SSTALE.WAL<>FINFO.NAROD
           || FUN.info('Wydruk nie jest dostępny dla waluty innej niż narodowa.'@);1
           || ~(WYDRUKIN.edit("
                _chk:=__CHK.record3(
                     WYDRUKIN,'DATAOD','Od dnia'@,
                     WYDRUKIN,'DATADO','Do dnia'@,
                     ROZNE,'REJ','Symbol rejestru'@
                );
                {? _chk=''
                || {? SSTALE.AO().POCZ=date(0,0,0)  &
                      (~exec('dat_not','dok_fks',WYDRUKIN.DATAOD) | ~exec('dat_not','dok_fks',WYDRUKIN.DATADO))
                   || FUN.info('Podane daty muszą być z \nbieżącego roku bilansowego.'@);0
                   |? ((WYDRUKIN.DATAOD<SSTALE.AO().POCZ) |
                       (WYDRUKIN.DATADO>SSTALE.AO().KON) |
                       (WYDRUKIN.DATAOD>WYDRUKIN.DATADO)) & SSTALE.AO().POCZ<>date(0,0,0)
                   || FUN.info('Daty muszą się zawierać w bieżącym okresie obrachunkowym.'@);
                      {? (WYDRUKIN.DATAOD<SSTALE.AO().POCZ) | (WYDRUKIN.DATAOD>WYDRUKIN.DATADO) || 'DATAOD' || 'DATADO' ?}
                   || 1
                   ?}
               || _chk
               ?}"
              ))
        ?}
}
{      szer[1]:=9; szer[2]:=szer[3]:=10; szer[4]:=8; szer[5]:=50; szer[6]:=58; szer[7]:=18;
        PRN.add("w[1]",szer[1],'Nr w rej.'@,1); PRN.add("w[2]",szer[2],'D. zapisu'@);
        PRN.add("w[3]",szer[3],'D. dokum.'@); PRN.add("w[4]",szer[4],'Nr w dz.'@,1);
        PRN.add("w[5]",szer[5],'  Symbol dokumentu'@); PRN.add("w[6]",szer[6],'        Opis dokumentu'@);
        PRN.add("w[7]",szer[7],'      Kwota'@,1);
        PRN1.add("ww[1]",szer[1]+szer[2]+szer[3]+szer[4]+szer[5]+szer[6]+5,'',,,,,,,,6); PRN1.add("ww[2]",szer[7],'',1);
        ww[1]:=ww[2]:=w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:='';
~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='DOKUMENTY Z REJESTRU %1 - %2 OD %3 DO %4'@[ROZNE.REJ().KOD,ROZNE.REJ().ODD().OD,$WYDRUKIN.DATAOD,$WYDRUKIN.DATADO];~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Sumowane konta: bilansowe'@}
{<5 'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{<5
{? STALE.AO().ZAM='T'
   ||  'Okres obrachunkowy: %1 %2 - zamknięty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]
   ||  'Okres obrachunkowy: %1 %2 - otwarty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]
?}
}
{<5 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{FI}{FONT: 'R'}{SPACE: 'C'}
{PRN.ini_head()}
{REP: PRN.fhbar()}
{WHILE: PRN.h_next()}
{DO}
{REP: PRN.h_fline()}
{OD}
{REP: PRN.fbar()}{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{IF: ~rep_export()}
{FOOTER:0}
{REP: PRN.fjbar(PRN1,-1)}{ ww[1]:='   SUMA STRONY'@;ww[2]:=form(sstr7,,2,' ,');~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.fbar()}
{IF: page>1}{ ww[1]:='   Z PRZENIESIENIA'@;ww[2]:=form((suma7-sstr7),,2,' ,');~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.fbar()}
{FI}{ ww[1]:='   DO PRZENIESIENIA'@; ww[2]:=form(suma7,,2,' ,');~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.flbar()}
{SUBSTITUTE: 'CHARFOOT'}
{ sstr7:=0; ~~}
{FOOTER-}
{FI}
{COMMENT}----------------------WYDRUK-------------------------------------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ {? OPERATOR.DEPT
  || DOK.index('PR');   DOK.prefix('T','T',OPERATOR.DEPT,ROZNE.REJ)
  || DOK.index('PR1'); DOK.prefix('T','T',ROZNE.REJ)
  ?};
  petla:=DOK.first(); ~~
}
{WHILE: petla}
{DO}
  {IF: (DOK.DTW>=WYDRUKIN.DATAOD)&(DOK.DTW<=WYDRUKIN.DATADO)}
    { w[1]:=$DOK.NR;  w[2]:=DOK.DTW$3; w[3]:=DOK.DTO$3;
      {?  DOK.NRDZ || w[4]:=$DOK.NRDZ || w[4]:='' ?};
      w[5]:=DOK.NK; w[6]:=DOK.TR; w[7]:=''; suma:=ok:=0;~~}
      {FOR: 'POZ'}
      {INDEX: 'DOK'}
      {PREFIX: DOK.ref}
      {DO}
      { {?  -(1+POZ.KOM().KS().TYP)<>'p' || ok:=1 ?};
        {?  -POZ.STR='wn' & -(1+POZ.KOM().KS().TYP)<>'p' || suma+=POZ.SUM  ?}; ~~}
      {OD}
      {IF: ok}
       { w[7]:=form(suma,,2,' ,'); sstr7+=suma; suma7+=suma; ~~}
{SUBSTITUTE: 'LINIA'}
      {FI}
  {FI} {'...Koniec sprawdzenia zakresu dat'@;~~}
  {petla:=DOK.next(); ~~}
{OD}
{IF: ~rep_export()}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{FI}
{DIRECT}
{IF: ~rep_export()}
 {REP: PRN.fjbar(PRN1,0)}{ ww[1]:='   SUMA STRONY'@;ww[2]:=form(sstr7,,2,' ,');~~}
 {SUBSTITUTE: 'LINIA1'}
 {REP: PRN1.fcbar()}
 {IF: page>1}{ ww[1]:='   Z PRZENIESIENIA'@;ww[2]:=form((suma7-sstr7),,2,' ,');~~}
 {SUBSTITUTE: 'LINIA1'}
 {REP: PRN1.fcbar()}
 {FI}
{FI}
{ ww[1]:='   RAZEM'@; ww[2]:=form(suma7,,2,' ,');~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.flbar()}
{IF: ~rep_export()}{<1>{PRN.width()}'KONIEC WYDRUKU'@}{FI}
{DIRECT-}

:!UTF-8
{TITLE:D2. Zbiorcze podsumowanie pracy pojazdów wg kierowców}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('k','s','PRN','TMP_KIER','IDX_KIER','kier_licz','tryb');
   k :=obj_new(8); "'--szerokosci kolumn--'";
   dokl:=2;        "'--dokladnosc--'";
   kier_licz:=0;   "'--licznik kierowców--'";
   PRN :=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   tryb:=rep_export();

   TMP_KIER:=tab_tmp(1,'REF_KIER','INTEGER','Ref kierowcy',
                      'NAZW','STRING[50]','Imię i nazwisko',
                      'JAZDY','REAL','Jazdy',
                      'POBRANO','REAL','Pobrano',
                      'WG_NORMY','REAL','Wg normy',
                      'RZECZYW','REAL','Rzeczywiste',
                      'PRZEPAL','REAL','Przepal',
                      'OSZCZ','REAL','Oszczędność');
   IDX_KIER:=TMP_KIER.ndx_tmp(,,'NAZW',,);

   oddz :=KARTDROG.ODDZ;

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   k[1]  :=4;
   k[2]  :=40;
   k[3]  :=10;
   k[4]  :=16;
   k[5] :=16;
   k[6] :=16;
   k[7] :=16;
   k[8] :=16
}
{END:
   VAR_DEL.delete('k','s','PRN','TMP_KIER','IDX_KIER','kier_licz','tryb');
   VAR_DEL.delete('dokl','wyjscie','oddz','SXS');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{EXIT:

   POJAZDY.win_sel('WER');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;

   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || _exit:=0
   || _exit:=1
   ?};
   {? _exit=0
   || {? KARTDROG.WYBKART='Z'
      || _od:=KARTDROG.KARTY_OD;
         _do:=KARTDROG.KARTY_DO
      |? KARTDROG.WYBKART='M'
      || _od:=date(KARTDROG.AR,KARTDROG.AM,1);
         _do:=date(KARTDROG.AR,KARTDROG.AM,0)
      || _od:=date(KARTDROG.AR,KARTDROG.AM,1);
         _do:=date(KARTDROG.AR,KARTDROG.AM,0);
         SAMK.index('D2');
         SAMK.prefix();
         {? SAMK.first()
         || _od:=SAMK.D;
            SAMK.last();
            _do:=SAMK.D
         ?}
      ?};
      POJAZDY.index('NRREJ');
      POJAZDY.prefix(REF.FIRMA);
      {? POJAZDY.first()
      || {! |?
            SAMK.cntx_psh();
            SAMK.index('D');
            SAMK.prefix(POJAZDY.ref());
            P_KAP.index('K');
            {? SAMK.first()
            || {! |?
                  {? SAMK.D>=_od & SAMK.D<=_do & (KARTDROG.WYB_STAT='W' | KARTDROG.WYB_STAT=SAMK.STATUS) & SAMK.STATUS<>'S'
                  || P_KAP.prefix(SAMK.ref(),'T');
                     {? P_KAP.first()
                     || _temp:=0;
                        {! |?
                           _temp+=P_KAP.PRZEBIEG;
                           P_KAP.next()
                        !};
                        P_KAP.first();
                        {! |?
                           {? ~TMP_KIER.find_key(#P_KAP.OSOBA)
                           || _new:=1;
                              TMP_KIER.blank();
                              TMP_KIER.REF_KIER:=#P_KAP.OSOBA;
                              TMP_KIER.NAZW:='   '+P_KAP.OSOBA().NAZWISKO+' '+P_KAP.OSOBA().PIERWSZE
                           || _new:=0
                           ?};
                           'jesli podano przebiegi dla kierowcow to wg nich jesli nie to proporcjonalnie do liczby kierowcow';
                           {? _temp<>0
                           || {? (SAMK.SK-SAMK.SP)>0
                              || _udzial:=P_KAP.PRZEBIEG/(SAMK.SK-SAMK.SP)
                              || _udzial:=0
                              ?}
                           || _udzial:=1/P_KAP.size()
                           ?};
                           TMP_KIER.JAZDY+=((SAMK.PRZ)*_udzial);
                           _pobrano:=exec('i_palz','karty_drog',SAMK.ref(),'P');
                           TMP_KIER.POBRANO+=_pobrano*_udzial;
                           TMP_KIER.WG_NORMY+=SAMK.Z_NORMA*_udzial;
                           TMP_KIER.RZECZYW+=SAMK.Z_RZECZ*_udzial;
                           {? TMP_KIER.RZECZYW>TMP_KIER.WG_NORMY
                           || TMP_KIER.PRZEPAL:=TMP_KIER.RZECZYW-TMP_KIER.WG_NORMY;
                              TMP_KIER.OSZCZ:=0
                           || TMP_KIER.OSZCZ:=TMP_KIER.RZECZYW-TMP_KIER.WG_NORMY;
                              TMP_KIER.PRZEPAL:=0
                           ?};
                           {? _new || TMP_KIER.add() || TMP_KIER.put() ?};
                           P_KAP.next()
                        !}
                     ?}
                  ?};
                  SAMK.next()
               !}
            ?};
            SAMK.cntx_pop();
            POJAZDY.next()
         !}
      ?}
   ?};
   {? TMP_KIER.size()=0
   || FUN.info('Brak danych do wyświetlenia.');
      _exit:=1
   ?};
   _exit
}
{
{? tryb<>1 || PRN.add("form(kier_licz)",k[1],'Lp.') ?};
PRN.add("TMP_KIER.NAZW",k[2],'Kierowca');
PRN.add("form(TMP_KIER.JAZDY,,dokl)",k[3],'Jazdy',1);
PRN.add("form(TMP_KIER.POBRANO,,dokl)",k[4],'Pobrano',1);
PRN.add("form(TMP_KIER.WG_NORMY,,dokl,' ,')",k[5],'Wg normy',1);
PRN.add("form(TMP_KIER.RZECZYW,,dokl,' ,')",k[6],'Rzeczywiste',1);
PRN.add("form(TMP_KIER.PRZEPAL,,dokl,' ,')",k[7],'Przepał',1);
PRN.add("form(TMP_KIER.OSZCZ,,dokl,' ,')",k[8],'Oszczędność',1);

{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Zbiorcze podsumowanie pracy pojazdów\nza okres od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO+' (kierowcy)'
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Zbiorcze podsumowanie pracy pojazdów\nza okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8+' (kierowcy)'
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?};
   PAR_WYDR.TITLE:='Zbiorcze podsumowanie pracy pojazdów\nza okres od: '+$_od+' do: '+$_do+' (kierowcy)'
?};
prn1:='PRN'
;~~}
{IF: POJAZDY.index('NRREJ'); POJAZDY.prefix(REF.FIRMA); POJAZDY.first()=0}{ FUN.info('Brak danych spełniających kryteria.');~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}{<70}{'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE:'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{rsep:=PAR_WYDR.RSEP; ~~}
{prn1:='PRN';prn2:='PRN';~~}
{FOR: 'TMP_KIER'}{INDEX: IDX_KIER}
{DO}
   {kier_licz+=1; ~~}
   {SUBSTITUTE:'LINIA0'}
{OD}
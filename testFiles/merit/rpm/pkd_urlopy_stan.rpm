:!UTF-8
{TITLE: Stan urlopów na dzień}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('tresc','par','PRN','PR1','PR','prn1','prn2','vp','lmt','rsep','h_rat','Wydzial','WydOpis');
   VAR_DEL.delete('SumaWD','SumaD','SumaWU','SumaU','SumaWC','SumaC','SumaWP','SumaP','SumaWW','SumaW','SumaWX');
   VAR_DEL.delete('SumaX','SumaWB','SumaB','SumaWZ','SumaZ','SumaE','SumaWE','Lp','prac');
   PAR_WYDR.TITLE:='Stan wykorzystania urlopów na dzień'@;
   tresc:='url_stan';
   P.cntx_psh();
   H_UM.cntx_psh();
   par:=obj_new(8);
   par[7]:=exec('par_wydr_ddp','pkd',
      PAR_WYDR.TITLE,
      "{? cur_tab().OD=#0 || FUN.emsg('Proszę wprowadzić datę kontroli stanu urlopów.'@); 0 || 1 ?}",
      date(,,0),
      'Data kontroli',
      "{? cur_tab().OD=#0 || FUN.emsg('Proszę wprowadzić datę kontroli stanu urlopów.'@); 0 || 1 ?}",,,,
      'Typ zestawienia'@,'Całość'@,'Wg jednostek'@
   );
   {? par[7].size()
   || par[5]:=date(par[7].OD~1,1,1);
      Lp:=par[2]:=par[3]:=par[8]:=0;
      par[4]:=
         " {? P.KAL || __KAL.set_cal(P.KAL,par[7].OD~1) || __KAL.set_cal('standard',par[7].OD~1) ?};
            N.prefix('N',P.ref(),KST_PAR.URLOP().RN);
            par[2]:=par[3]:=par[6]:=0;
            {? N.last()
            || {!
               |? {? N.OD<=par[7].OD & N.DO>=par[5]
                  || _od:={? N.OD<par[5] || par[5] || N.OD ?};
                     _do:={? N.DO>par[7].OD || par[7].OD || N.DO ?};
                     {? _od=N.OD & _do=N.DO
                     || par[2]+=N.NG/exec('norma','godziny',par[7].OD)
                     || par[2]+=exec('nominal','godziny',_od,_do)/exec('norma','godziny',par[7].OD)
                     ?}
                  ?};
                  N.prev() & N.DO>=par[5]
               !}
            ?};
            N.prefix('N',P.ref(),KST_PAR.URL_CHOR().RN);
            'Zliczanie urlopu na żądanie w godzinach celem odjęcia od limitu wypoczynkowego';
            _gUz:=0;
            {? N.last()
            || {!
               |? {? N.OD<=par[7].OD & N.DO>=par[5]
                  || _od:={? N.OD<par[5] || par[5] || N.OD ?};
                     _do:={? N.DO>par[7].OD || par[7].OD || N.DO ?};
                     {? _od=N.OD & _do=N.DO
                     || _gUz+=N.NG/exec('norma','godziny',par[7].OD);
                        par[6]+=N.NR
                     || _gUz+=exec('nominal','godziny',_od,_do)/exec('norma','godziny',par[7].OD);
                        par[6]+=__KAL.w_days(_od,_do)
                     ?}
                  ?};
                  N.prev() & N.DO>=par[5]
               !}
            ?};
            _rn:=__RUB.sys_kod(1113,,1);
            {? _rn<>0
            || N.prefix('N',P.ref(),_rn);
               {? N.last()
               || {!
                  |? {? N.OD<=par[7].OD & N.DO>=par[5]
                     || _od:={? N.OD<par[5] || par[5] || N.OD ?};
                        _do:={? N.DO>par[7].OD || par[7].OD || N.DO ?};
                        {? _od=N.OD & _do=N.DO
                        || par[2]+=N.NG/exec('norma','godziny',par[7].OD)
                        || par[2]+=exec('nominal','godziny',_od,_do)/exec('norma','godziny',par[7].OD)
                        ?}
                     ?};
                     N.prev() & N.DO>=par[5]
                  !}
               ?}
            ?};
            'Urlopy OZ - oddelegowanie może być kilka w atrybut 1114';
            {? var_pres('_tab_rub')>100 || obj_del(_tab_rub) ?};
            _tab_rub:=__RUB.sys_rub(1114);
            {? _tab_rub.first()
            || {!
               |? N.prefix('N',P.ref(),_tab_rub.RN);
                  {? N.last()
                  || {!
                     |? {? N.OD<=par[7].OD & N.DO>=par[5]
                        || _od:={? N.OD<par[5] || par[5] || N.OD ?};
                           _do:={? N.DO>par[7].OD || par[7].OD || N.DO ?};
                           {? _od=N.OD & _do=N.DO
                           || par[2]+=N.NG/exec('norma','godziny',par[7].OD)
                           || par[2]+=exec('nominal','godziny',_od,_do)/exec('norma','godziny',par[7].OD)
                           ?}
                        ?};
                        N.prev() & N.DO>=par[5]
                     !}
                  ?};
                  _tab_rub.next()
               !}
            ?};
            obj_del(_tab_rub);
            par[2]+=_gUz;
            'Zlicz ekwiwalenty';
            H_UM.index('OD');
            H_UM.prefix(P.ref());
            {? H_UM.find_le(par[7].OD)
            || {!
               |? {? H_UM.DO~1=par[7].OD~1 & H_UM.DO<=par[7].OD & (H_UM.EKW | H_UM.EKW_NSP)
                  || par[2]+=(H_UM.EKW+H_UM.EKW_NSP)/exec('norma','godziny',H_UM.DO)
                  ?};
                  H_UM.prev() & H_UM.DO~1=par[7].OD~1
               !}
            ?};
            par[3]:=KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.NSP_ZAL+KART_URL.URL_NSP-par[2]
         ";
      PRN:=obj_new(@.CLASS.PRINT,{? rep_export() & par[7].ASK || 16 || 14 ?});
      {? ~rep_export()
      || PRN.add("Lp+=1",5,'Lp.'@,1,,,,,',,\'99\'');
         PRN.add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE",37,'Nazwisko i imię'@)
      ?};
      {? rep_export() & par[7].ASK
      || PRN.add("P.WYDZIAL().SYMBOL",10,'Jednostka org. - symbol'@);
         PRN.add("P.WYDZIAL().OPIS",50,'Jednostka org. - opis'@);
         PRN.add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
         PRN.add("P.OSOBA().PIERWSZE",20,'Imię'@)
      |? rep_export()
      || PRN.add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
         PRN.add("P.OSOBA().PIERWSZE",20,'Imię'@)
      ?};
      PRN.add("KART_URL.LIM_ZAL",9,'Urlop zaległy'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("KART_URL.LIM_AKT",7,'Urlop bieżący'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("P.NAB_URL$1",10,'Data nabycia'@);
      PRN.add("{? _u:=KART_URL.URL_DOD || _u || '' ?}",13,'Urlop uzupełniający'@,1
         ,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("{? (_d:=KART_URL.DATA_DOD)<>#0 || _d$1 || ''?}",10,'Data nabycia'@);
      PRN.add("{? _u:=KART_URL.NSP_ZAL || _u || '' ?}",9,'Urlop dodatkowy zaległy'@,1
         ,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("{? _u:=KART_URL.URL_NSP || _u || '' ?}",9,'Urlop dodatkowy'@,1
         ,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("{? (_d:=KART_URL.DATA_NSP)<>#0 || _d$1 || ''?}",10,'Data nabycia'@);
      PRN.add(
         "  KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.NSP_ZAL+KART_URL.URL_NSP
         ",9,'Całkowity urlop'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("par[4]();par[2]",12,'Wykorzystano'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("par[6]",12,'W tym na żądanie'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("par[3]",9,'Pozostało'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.s_frame();
      {? par[7].ASK
      || PR:=obj_new(@.CLASS.PRINT,13);
         {? ~rep_export()
         || PR.add("'R A Z E M'@",45);
            PR.add("SumaZ",9,,1,,,,,',2,\'9.\'');
            PR.add("SumaB",7,,1,,,,,',2,\'9.\'');
            PR.add("''",10);
            PR.add("{? SumaU=0 || '' || SumaU ?}",13,,1,,,,,',2,\'9.\'');
            PR.add("''",10);
            PR.add("{? SumaE=0 || '' || SumaE ?}",9,,1,,,,,',2,\'9.\'');
            PR.add("{? SumaD=0 || '' || SumaD ?}",9,,1,,,,,',2,\'9.\'');
            PR.add("''",10);
            PR.add("SumaC",9,,1,,,,,',2,\'9.\'');
            PR.add("SumaW",12,,1,,,,,',2,\'9.\'');
            PR.add("SumaX",12,,1,,,,,',2,\'9.\'');
            PR.add("SumaP",9,,1,,,,,',2,\'9.\'');
            PR.s_frame()
         ?}
      ?};
      PR1:=obj_new(@.CLASS.PRINT,13);
      {? ~rep_export()
      || PR1.add("{? par[7].ASK || 'S U M A  F I R M A'@ || 'R A Z E M'@?}",45);
         PR1.add("SumaWZ",9,,1,,,,,',2,\'9.\'');
         PR1.add("SumaWB",7,,1,,,,,',2,\'9.\'');
         PR1.add("''",10);
         PR1.add("{? SumaWU=0 ||''|| SumaWU ?}",13,,1,,,,,',2,\'9.\'');
         PR1.add("''",10);
         PR1.add("{? SumaWE=0 || '' || SumaWE ?}",9,,1,,,,,',2,\'9.\'');
         PR1.add("{? SumaWD=0 || '' || SumaWD ?}",9,,1,,,,,',2,\'9.\'');
         PR1.add("''",10);
         PR1.add("SumaWC",9,,1,,,,,',2,\'9.\'');
         PR1.add("SumaWW",12,,1,,,,,',2,\'9.\'');
         PR1.add("SumaWX",12,,1,,,,,',2,\'9.\'');
         PR1.add("SumaWP",9,,1,,,,,',2,\'9.\'');
         PR1.s_frame()
      ?};
      prn1:='PRN';
      prn2:='PR';
      vp:=rsep:=h_rat:=1;
      lmt:=0;
      Wydzial:=WydOpis:='';
      SumaWD:=SumaD:=SumaWU:=SumaU:=SumaWC:=SumaC:=SumaWP:=SumaP:=SumaWW:=SumaW:=SumaWB:=SumaB:=SumaWZ:=SumaZ:=0;
      SumaWX:=SumaX:=SumaE:=SumaWE:=0;
      N.index('NIPRACNB');
      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?}
}
{END:
   VAR_DEL.delete('tresc','par','PRN','PR1','PR','prn1','prn2','vp','lmt','rsep','h_rat','Wydzial','WydOpis');
   VAR_DEL.delete('SumaWD','SumaD','SumaWU','SumaU','SumaWC','SumaC','SumaWP','SumaP','SumaWW','SumaW','SumaWX');
   VAR_DEL.delete('SumaX','SumaWB','SumaB','SumaWZ','SumaZ','SumaE','SumaWE','Lp','prac');
   H_UM.cntx_pop();
   P.cntx_pop()
}
{COMMENT}OLD: url_stan.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[7].size()}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: vp}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Rok kalendarzowy'@}: {$(par[7].OD~1)}
   {<{ceil(lmt*h_rat)+1}'Dzień badania'@}: {par[7].OD$6}
   {<{ceil(lmt*h_rat)+1}'Urlop w dniach'@}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: par[7].ASK & ~rep_export()}
{<{lmt+1}'Jednostka organizacyjna'@} - {Wydzial}{IF: +(|WydOpis)} ({WydOpis}){FI}
{FI}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'fsum'}
{LINE}
{FONT: 'T8B'}
{SPACE: 'B'}
{IF: ~rep_export()}
{REP: PR1.fhbar(lmt)}
{FI}
{  PR1.ini_line(); ~~ }
{WHILE: PR1.next()}{DO}
   {REP: PR1.fline(lmt)}
{OD}
{IF: ~rep_export()}
{REP: PR1.flbar(lmt)}
{FI}
{MACRO-}
{MACRO: 'wsum'}
{{? ~par[7].ASK || prn2:='PR1'?}; rsep:=1; ~~ }
{FONT: 'T8B'}
{SPACE: 'B'}
{IF: ~rep_export()}
{SUBSTITUTE: 'LINIAFS'}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{MACRO-}
{MACRO: tresc}
{FOR: KART_URL}{INDEX:'PRAC_ROK'}{PREFIX:P.ref(),par[7].OD~1}{DO}
{SUBSTITUTE: 'LINIA0'}
{ par[8]:=1; ~~}
{SUBSTITUTE: 'sumw'}
{IF: ~rep_export() & lineleft()<4}
     {SUBSTITUTE: 'LINIAF'}
     {PAGE}
{FI}
{OD}
{MACRO-}
{MACRO: 'sumw'}
{  {? par[7].ASK
   || SumaD+=KART_URL.URL_NSP;
      SumaE+=+KART_URL.NSP_ZAL;
      SumaU+=KART_URL.URL_DOD;
      SumaC+=KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.NSP_ZAL+KART_URL.URL_NSP;
      SumaP+=par[3];
      SumaW+=par[2];
      SumaX+=par[6];
      SumaB+=KART_URL.LIM_AKT;
      SumaZ+=KART_URL.LIM_ZAL
  ?};
  SumaWD+=KART_URL.URL_NSP;
  SumaWE+=+KART_URL.NSP_ZAL;
  SumaWU+=KART_URL.URL_DOD;
  SumaWC+=KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.NSP_ZAL+KART_URL.URL_NSP;
  SumaWP+=par[3];
  SumaWW+=par[2];
  SumaWX+=par[6];
  SumaWB+=KART_URL.LIM_AKT;
  SumaWZ+=KART_URL.LIM_ZAL; ~~
}
{MACRO-}
{MACRO: 'wydzialy'}
{ {? params_get().P.first()
  || prac:=tab_tmp(3,
        'WYDZIAL','STRING[16]','Wydzial',
        'NAZWISKO','STRING[30]','Nazwisko',
        'PIERWSZE','STRING[20]','Imię',
        'PREF','STRING[16]','Ref pracownika'
     );
     {!
     |? {? P.seek(params_get().P.SQL,,1)
        || prac.NAZWISKO:=P.OSOBA().NAZWISKO;
           prac.PIERWSZE:=OSOBA.PIERWSZE;
           prac.PREF:=$P.ref();
           prac.WYDZIAL:=P.WYDZIAL().SYMBOL;
           prac.add()
        ?};
        params_get().P.next()
     !}
  ?}; ~~
}
{FOR: prac}{DO}
   {IF: P.seek(prac.PREF,,1)}
      {FOR: KART_URL}{INDEX:'PRAC_ROK'}{PREFIX:P.ref(),par[7].OD~1}{DO}
          {IF: ~par[8]}{Wydzial:=P.WYDZIAL().SYMBOL; WydOpis:=P.WYDZIAL().OPIS; ~~ }{FI}
            {IF: P.WYDZIAL().SYMBOL<>Wydzial}
{SUBSTITUTE: 'wsum'}
               { SumaD:=SumaE:=SumaU:=SumaC:=SumaP:=SumaW:=SumaB:=SumaZ:=SumaX:=0; ~~ }
             {PAGE}
             {Wydzial:=P.WYDZIAL().SYMBOL; WydOpis:=P.WYDZIAL().OPIS; Lp:=0; ~~ }
          {FI}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'sumw'}
          {IF: ~rep_export() & lineleft()<4}
{SUBSTITUTE: 'LINIAF'}
             {PAGE}
          {FI}
          {par[8]:=1; ~~ }
      {OD}
   {FI}
{OD}
{IF: par[8]=1}
{SUBSTITUTE:'wsum'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{IF: ~par[7].ASK }
{FOR: params_get().P}{DO}
{IF: P.seek(params_get().P.SQL,,1)}
{SUBSTITUTE: tresc}
{FI}
{OD}
{IF: par[8]}
{SUBSTITUTE: 'wsum'}
{FI}
{ELSE}
{SUBSTITUTE: 'wydzialy'}
{IF: par[8]}
{ENTIRE}{SUBSTITUTE: 'fsum'}{ENTIRE-}
{FI}
{FI}
{IF: rep_export()}
{SUBSTITUTE: 'LINIAF'}
{FI}
:!UTF-8
{TITLE:4. N-P-U procesu technologicznego}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  TKTL.cntx_psh();
  "---Obiekt wydruku narzędzi karty technologicznej.";
  PRNTLS := obj_new(@.CLASS.PRINT,9);PRNTLS.s_frame();
  "---Obiekt wydruku parametrow karty technologicznej.";
  PRNPAR := obj_new(@.CLASS.PRINT,7); PRNPAR.sl_frame();
  FAZY:=tab_tmp(1,'KOD','STRING[10]','Nazwa Fazy','NR','INTEGER','Ref do fazy');
  fazy:=FAZY.ndx_tmp(,1,'KOD','',0,'KOD',,0);
  FAZY.index(fazy);
  o:=f:=0;
  data:=date();
  kreska := '─';
  "---Inicjalizacja obiektu 'tpar' klasy 'TPAr', sluzacego do operacji na     ";
  "---parametrach karty technologicznej.";
  VAR.A_KTL:={? VAR.ACTION='3' || TKTLW.TKTL || TKTL.ref() ?};
  VAR.A_T:={? VAR.ACTION='3' || TKTLW.KTM || VAR.A_KTL().KTM ?};
  exec('start_tpar','tech_param',VAR.A_T);
  nrop:='';
  buf_unrop:=VAR.A_UNROP;
  prnzam:=0; "1 - drukować zamienniki, 0 - nie drukować zamienników.";
  ktlnr := TKTL.NRK;
  "---Suma z pol 'Placa na XJM'.";
  sumplnx:=0;
  "---Suma z pol 'Placa na godz'.";
  sumplnh:=0;
  "---Suma z pol 'Norma czasowa'.";
  sumntime:=0;
  "---'TACTTLS' - drukowane sa narzedzia, 'TOPER' - drukowane sa operacje.";
  printing:='';
  pierwszy:=0;
  "---1 - narzedzia istnieja.";
  tlsexist:=0;
  prn1:='PRNTLS';
  rodz:=obj_new(4);
  rodz[1]:='N'; rodz[2]:='P'; rodz[3]:='U'; rodz[4]:='I';
  waslbar:=0;
  dalej:=0;
  color:='';
  f:=FUN.ask('Rozbicie na fazy produkcji?'@);
  i:=lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  TKTL.cntx_pop();
  obj_del(PRNTLS);
  obj_del(PRNPAR);
  FAZY.ndx_drop(fazy);
  obj_del(FAZY);
  &i; &f;
  &pierwszy;
  obj_del(rodz);
  &kreska;
  "---Usuniecie obiektu sluzacego do operacji na parametrach karty            ";
  "---technologicznej.";
  exec('stop_tpar','tech_param');
  &nrop;
  VAR.A_UNROP:=&buf_unrop;
  &prnzam;
  &ktlnr;
  &sumplnx;
  &sumplnh;
  &sumntime;
  &printing;
  &tlsexist;
  &dalej;
  &prn1;
  &data;
  &color;
  &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
  Wydruk narzędzi procesu technologicznego.
  [SS] - 2000/09/01 - [7.22]
  [SS] - 2001/03/14 - [7.41] uaktualnienie zwiazane z parametryzacja
    norm czasowych operacji oraz zuzycia narzedzi.
  [MKO] - 2001/11/13 - [7.53] OWwSK003_Aneks1
                       dodatkowo: rozdzielenie wydruku narzędzi i operacji na dwa różne
  [MKO] - 2001/11/29 - [7.53] Uwaga_30
  Wywołanie: Karty technologiczne -> Drukuj
{COMMENT-}
{MACRO: 'functions'}
  {"--------------------------------------------------------------------------";
   "---Wyswietla okienko z pytaniem, czy drukowac wartosci czy algorytmy      ";
   "---na surowcach, operacjach i narzedziach (odpowiednio - normy            ";
   "---materialowe, normy czasowe oraz zuzycie).                              ";
   "---Jezeli zostalo wybrane drukowanie wartosci, to wyswietla okienko       ";
   "---redakcyjne, aby umozliwic redakcje domyslnych parametrow karty.        ";
   "---Zwraca 2, jezeli beda drukowane algorytmy - 1, jezeli wartosci,        ";
   "---natomiast 0, jezeli operator przerwal dzialanie wydruku.";
   pchange:="
     _result:=tpar.change();
     _result
   ";
   "--------------------------------------------------------------------------";
   "---Zwraca liczbe parametrow do biezacej karty technologicznej.";
   pcount:="
     TPAR.index('NN');
     TPAR.prefix(TKTL.ref());
     TPAR.size()
   ";
  ~~}
{MACRO-}
{MACRO: 'utw_fazy'}
{  TOPER.clear(); TOPER.index('NNN'); TOPER.prefix(TKTL.ref(),0);
   {? TKTL.TYP().UTIL='O';0
   || {? TOPER.first()
      || {!
         |? FAZY.prefix(TOPER.PFAZ().KOD,TOPER.PFAZ().KOD);
            {? ~FAZY.first()
            || FAZY.KOD:=TOPER.PFAZ().KOD;
               FAZY.NR:=#TOPER.PFAZ;
               FAZY.add()
            ?};
            TOPER.next()
         !}
      ?}
   || TACTTLS.clear(); TACTTLS.index('KNROP'); TACTTLS.prefix(TKTL.ref());
      {? TACTTLS.first()
      || {!
         |? FAZY.prefix(TACTTLS.PFAZ().KOD,TACTTLS.PFAZ().KOD);
            {? ~FAZY.first()
            || FAZY.KOD:=TACTTLS.PFAZ().KOD;
               FAZY.NR:=#TACTTLS.PFAZ;
               FAZY.add()
            ?};
            TACTTLS.next()
         !}
      ?}
   ?};
   ~~
}
{MACRO-}
{IF: f=1}
{SUBSTITUTE: 'utw_fazy'}
{FI}
{"---Definicja wiersza wydruku narzędzi TACTTLS."; ~~}
{ PRNTLS.add("TACTTLS.M().KTM+' - '+TACTTLS.M().N",49,'Narzędzie');
  PRNTLS.add("form(TACTTLS.ILE)",6,'Ilość',1);
  PRNTLS.add("{? TACTTLS.FZXJM<>''
              || form(tpar.calc(TACTTLS.FZXJM),18,4)
              || form(TACTTLS.ZXJM,,4)
              ?}", 18,'Zużycie na JM',1);
  PRNTLS.add("{? TACTTLS.FZH<>''
              || form(tpar.calc(TACTTLS.FZH),18,4)
              || form(TACTTLS.ZH,,4)
              ?}", 18,'Zużycie na godz.',1);
  {? f=0 || PRNTLS.add("TACTTLS.PFAZ().KOD",7,'Faza') ?};
  PRNTLS.add("{? tpar.calc(TACTTLS.EXIST) || ' T' || ' N' ?}",3,'Akt');~~}
{"---Definicja wiersza wydruku parametrow karty technologicznej.          "; ~~}
{ PRNPAR.add("'p('+$(tpar.P[1][i])+')'",10,'Symbol');
  PRNPAR.add("tpar.P[3][i]",40,'Opis');
  PRNPAR.add("form(tpar.P[2][i],25,10)",25,'Wartość',1);
  PAR_WYDR.TITLE:='N-P-U KARTY TECHNOLOGICZNEJ';~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRNTLS.width())/2);~~}
{<{lmt} 'Karta technologiczna: '+ktlnr+'  wer. '+TKTL.WER}
{<{lmt} 'Produkt: '+VAR.A_T().KTM+' - '+M.N}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{SUBSTITUTE: 'functions'}
{IF: pcount()>0}
  {IF: (paramprt:=pchange())=0}
    {EXIT}
  {FI}
{FI}
{IF: f=0}
{iter:=1; bylo:=0; ok:=0; ~~}
{WHILE: iter<=4}
{DO}
{FOR: 'TACTTLS'}
  {INDEX: 'KNROPR'}
  {PREFIX: TKTL.ref()}
{DO}
{IF: TACTTLS.ACT='T'  & tpar.calc(TACTTLS.EXIST)=1 & {? TACTTLS.NROP<>null || tpar.calc(TACTTLS.NROP().EXIST)=1 || 1 ?}}
{IF: exec('rodz_decode','tech_tool',TACTTLS.M().MGR().KOD)=rodz[iter]}
{IF: ~bylo}
    {IF: lineleft < 14}{PAGE}{FI}
    {FONT: 'W12'}{SPACE: 'A'}
{    lk:=charleft(); ~~}
    {IF: rodz[iter]='N'}{<1>{lk} 'NARZĘDZIA'}{FI}
    {IF: rodz[iter]='P'}{<1>{lk} 'PRZYRZĄDY'}{FI}
    {IF: rodz[iter]='U'}{<1>{lk} 'URZĄDZENIA'}{FI}
    {IF: rodz[iter]='I'}{<1>{lk} 'INNE'}{FI}
    {FONT: 'T8GB'}{SPACE: 'B'}
    {SUBSTITUTE: 'TABHEAD'}
    {FONT: 'T8G'}{ vp:=1; ok:=1; bylo:=1; ~~}
{FI}
   {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {FI}
{FI}
{FI}
{OD}
{IF: ok}{SUBSTITUTE: 'LINIAF'}{FI}
{iter+=1;ok:=0;bylo:=0;~~}
{OD}
{ELSE}
  {FOR: FAZY}
  {DO}
     {FONT: 'W12'}{SPACE: 'A'}
{     lk:=charleft();pierwszy:=0;~~}
     {PFAZ.seek(FAZY.NR,'pfazypr');~~}

    {FONT: 'T8GB'}{SPACE: 'B'}
    {iter:=1; bylo:=0; ok:=0;~~}
    {WHILE: iter<=4}
    {DO}
       {FOR: 'TACTTLS'}
       {INDEX: 'KNROPR'}
       {PREFIX: TKTL.ref()}
       {DO}
       {IF: TACTTLS.ACT='T' & tpar.calc(TACTTLS.EXIST)=1 & {? TACTTLS.NROP<>null ||  tpar.calc(TACTTLS.NROP().EXIST)=1 || 1 ?} }
       {IF: exec('rodz_decode','tech_tool',TACTTLS.M().MGR().KOD)=rodz[iter] & TACTTLS.PFAZ().KOD=FAZY.KOD}
       {IF: pierwszy=0}
          {IF: lineleft < 14}{PAGE}{FI}
           {IF: FAZY.KOD<>''}
           {<1>{lk} 'FAZA '+ FAZY.KOD+'  -  '+PFAZ.OPIS}
           {FI}
           {pierwszy:=1;~~}
          {FI}
          {IF: ~bylo}
              {IF: lineleft < 14}{PAGE}{FI}
              {FONT: 'W12'}{SPACE: 'A'}
{              lk:=charleft(); ~~}
              {IF: rodz[iter]='N'}{<1>{lk} 'NARZĘDZIA'}{FI}
              {IF: rodz[iter]='P'}{<1>{lk} 'PRZYRZĄDY'}{FI}
              {IF: rodz[iter]='U'}{<1>{lk} 'URZĄDZENIA'}{FI}
              {IF: rodz[iter]='I'}{<1>{lk} 'INNE'}{FI}
              {FONT: 'T8GB'}{SPACE: 'B'}
              {SUBSTITUTE: 'TABHEAD'}
              {FONT: 'T8G'}{ vp:=1;ok:=1;bylo:=1; ~~}
         {FI}
         {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
         {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
         {FI}
      {FI}
     {FI}
   {OD}
   {IF: ok}{SUBSTITUTE: 'LINIAF'}{FI}
   {iter+=1;ok:=0;bylo:=0;~~}
   {OD}
  {OD}
{FI}
{"------------------------------------------------------------------------"; ~~}
{"---Tutaj wydruk parametrow karty technologicznej.                       "; ~~}
{IF: pcount()>0}
  {TPAR.index('NN'); TPAR.prefix(TKTL.ref()); ~~}
  {IF: TPAR.first()}{ dalej:=1; prn1:='PRNPAR';~~}
{<1  lmt:=int((charleft()-PRNPAR.width())/2); ~~}
    {IF: lineleft < 14}{PAGE}{FI}
    {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
    {<1>{lk} 'SPIS PARAMETRÓW KARTY TECHNOLOGICZNEJ'}
    {FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
    {i:=1; ~~}
    {WHILE: dalej}
    {DO}
     {IF: lineleft > 8} {SUBSTITUTE: 'LINIA0'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
     {FI}
     { i+=1; dalej:=TPAR.next();~~}
   {OD}
   {SUBSTITUTE: 'LINIAF'}
  {FI}
{FI}

:!UTF-8
{TITLE:PIT-4R(4) (2013 r.)}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,5,5,5,5,1,
   exec('tryb_pdf','pit','PIT-4R')='T',exec('tryb_druku','pit','PIT-4R')}
{BEGIN:
   {? var_pres('pit')>100 || obj_del(pit) ?};
   pit:=obj_new(8);
   {! _n:=1..8 |! pit[_n]:='' !};
   USERS.cntx_psh();
   pit[1]:=KST.NIP;
   pit[4]:=4+VAT_DEK.OKRES;
   pit[5]:=KST.US().NU+
           {? +KST.US().UU || ', '+KST.US().UU || '' ?}+
           {? +KST.US().UK & +KST.US().MU ||
              ', '+(6+KST.US().UK)+' '+KST.US().MU || ''
           ?};
   pit[8]:=KST.NAZWA+', '+KST.REG;

   pit[6]:=VAT_DEK.KOR;
   pit[2]:=VAT_DEK.IMIE;
   pit[3]:=VAT_DEK.NAZ;

   {! _n:=1..8 |! pit[_n]:=~(-pit[_n]) !};

   {? var_pres('tab')>100 || obj_del(tab) ?};
   tab:=tab_tmp(2,
      'NR','INTEGER','Nr',
      'NRK','INTEGER','Nrk',
      'K1','INTEGER','Kwota',
      'K2','INTEGER','Kwota',
      'K3','INTEGER','Kwota',
      'K4','INTEGER','Kwota',
      'K5','INTEGER','Kwota',
      'K6','INTEGER','Kwota'
   );
   ind:=0; Podatek:=0; czy_raport:=0;
   DEKL_POD.use('pod_'+pit[4]);
   DEKL_POD.index('DEKL_PIT');
   DEKL_POD.prefix;
   CNT:="_c:=(_b-(+_a))%2; form(_c*' '+_a,_b)";

   {? VAT_DEK.KOR='T'
   || {? var_pres('__ord')>100 || obj_del(__ord) ?};
      __ord:=obj_new(13);
      {! _n:=1..obj_len(__ord) |! __ord[_n]:='' !};
      __ord[1]:=KST.NIP;
      __ord[5]:=KST.NAZWA;
      __ord[6]:=KST.SKROT;
      __ord[8]:=KST.REG
   ?};
   VAT_POZ.cntx_psh;
   pdf:=exec('tryb_pdf','pit','PIT-4R')='T';
   {? pdf
   || exec('rpm_as_pdf_ini','rpm_pdf');
      y:=0;
      VAR_DEL.delete('x','w');
      x:=obj_new(6);
      w:=obj_new(6)
   ?};
   VAR_DEL.delete('__OpWpla','__iter');
   __OpWpla:={? var_pres('OPISWPLA',VAT_DEK)>0
             || exec('opis_korekty','edeklar','OPISWPLA')
             || ''
             ?}
}
{END:
   {? pdf
   || VAR_DEL.delete('x','w');
      &y;
      exec('rpm_as_pdf_end','rpm_pdf')
   ?};
   &pdf;
   &CNT; &ind; &Podatek; &czy_raport;
   obj_del(pit); &pit; obj_del(tab); &tab;
   USERS.cntx_pop();
   VAT_POZ.cntx_pop;
   {? VAT_DEK.KOR='T' || obj_del(__ord); &__ord ?};
   VAR_DEL.delete('__OpWpla','__iter')
}
{COMMENT}OLD: dek04r04.rpm{COMMENT-}
{COMMENT}OLD: dkw4r041.rpi{COMMENT-}
{COMMENT}OLD: dkw4r042.rpi{COMMENT-}
{COMMENT}OLD: dkw4r043.rpi{COMMENT-}
{COMMENT}OLD: dkw4r044.rpi{COMMENT-}
{COMMENT}OLD: ord_zu2.rpi{COMMENT-}
{COMMENT}OLD: dkw4r04g.rpi{COMMENT-}
{COMMENT}OLD: ord_zu2g.rpi{COMMENT-}
{IF: DEKL_POD.find_key('P','PIT-4R','PIT-4R')}
{EXIT:
   USERS.cntx_psh();
   USERS.prefix();
   _kod:=DEKL_POD.USER().KOD;
   USERS.cntx_pop();
   ~FUN.ask(
      'Za %1 rok wydrukowano deklarację podatkową %2\n'
      '(data: %3, operator: %4).\n'
      'Czy kontynuować wydruk?'@
      [pit[4],DEKL_POD.PIT,DEKL_POD.DATA$7,_kod]
   )
}{FI}
{  tab.prefix;
   VAT_POZ.index('NR_NRK');
   VAT_POZ.prefix(VAT_DEK.ref);
   {? VAT_POZ.first
   || {! |? tab.NR:=VAT_POZ.NR;
            tab.NRK:=VAT_POZ.NRK;
            {! _ind:=1..6
            |! _kw:=($('VAT_POZ.K'+$_ind))();
               ($('tab.K'+$_ind))():=_kw;
               {? tab.NR=11 || czy_raport+=_kw ?};
               {? tab.NR=13 || Podatek+=_kw ?}
            !};
            tab.add;
            VAT_POZ.next
      !}
   ?};
   {? VAT_DEK.KOR='T'
   || __ord[13]:=exec('opis_korekty','edeklar');
      {! _n:=1..obj_len(__ord) |! __ord[_n]:=~(-__ord[_n]) !}
   ?};

   tab.prefix;
   ~~
}
{EXIT:
   {? ~(czy_raport>0)
      || FUN.info('Brak informacji do wykazania na deklaracji.'@+'\n'+'(zerowe kwoty podatku)'@);
         1
      || 0
   ?}
}
{IF:pdf}
   {REP: '{INCLUDE: ppl_dkw4r04g.rpi}'}
   {IF: VAT_DEK.KOR='T'}
      {REP: '{INCLUDE: ppl_ord_zu2g.rpi}'}
   {FI}
{ELSE}
{FONT: 'q'}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_dkw4r041.rpi}'}
{DIRECT-}
{PAGE}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_dkw4r042.rpi}'}
{DIRECT-}
{PAGE}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_dkw4r043.rpi}'}
{DIRECT-}
{PAGE}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_dkw4r044.rpi}'}
{DIRECT-}
{PAGE}
{IF: VAT_DEK.KOR='T'}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_ord_zu2.rpi}'}
{DIRECT-}
{PAGE}
{FI}
{FI}
{ DEKL_POD.clear; DEKL_POD.blank;
  DEKL_POD.RODZAJ:='P'; DEKL_POD.MSC:=0;
  DEKL_POD.DATA:=date; DEKL_POD.PIT:='PIT-4R';
  DEKL_POD.S1:=0; DEKL_POD.S2:=0; DEKL_POD.S3:=Podatek;
  DEKL_POD.USER:=OPERATOR.USER;
  DEKL_POD.add;
   ~~
}
:!UTF-8
{TITLE:Nieobecności pracowników}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('tresc','par','suma1','suma2','suma3','suma4','czy_psum','czy_prac','wpis','ile_znak','d_pocz');
   VAR_DEL.delete('d_kon','ind','ind2','ind_aut','info','rozmiar','ii','pierwszy','zam','tmp','prn1','vp','lmt','rsep');
   VAR_DEL.delete('b_rat','h_rat');

   tresc:='nieobecn';
   PAR_WYDR.TITLE:='Nieobecności pracowników'@;
   P.cntx_psh();
   par:=obj_new(16);
   par[12]:=0;
   _upr:=exec('uprawnieniawrap','pkd','nieobecnościach'@,'PKD_EZK_OPNN','PKD_EZK_ORNN');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || d_pocz:=d_kon:=date(0,0,0);
      par[2]:='';

      par[3]:=exec('pkd_wnieobecn','personel',,tresc,PAR_WYDR.TITLE,{? rep_export() || 0 || 1 ?});
      {? par[12]:=par[3].size()
      || {? par[3].KOD || par[2]:=par[3].memo_txt(,1,'LISTA') ?};
         par[10]:=tab_tmp(1,
            'TYP','STRING[5]',,
            'NAZWA','STRING[20]',,
            'IMIE','STRING[50]',,
            'NAZWISKO','STRING[50]',,
            'TECZKA','STRING[50]',,
            'OD','DATE',,
            'DO','DATE',,
            'NK','INTEGER',,
            'NR','INTEGER',,
            'NG','REAL',,
            'ST','STRING[5]',,
            'LT','STRING[8]',,
            'POD','REAL',
         );
         par[11]:=par[10].ndx_tmp(,1,'OD',,0);
         par[10].index(par[11]);

         par[1]:=obj_new(@.CLASS.PRINT,{? rep_export() || 13 || 10 ?});
         {? rep_export()
         || par[1].add("par[10].NAZWISKO",30,'Nazwisko'@);
            par[1].add("par[10].IMIE",41,'Imiona'@);
            par[1].add("par[10].TECZKA",9,'Nr teczki'@)
         ?};
         par[1].add("par[10].TYP",5,'Kod'@,1);
         par[1].add("par[10].NAZWA",20,'Nazwa'@);
         par[1].add("{?par[15]||par[10].OD$1||''?}",10,'Od dnia'@);
         par[1].add("{?par[15]||par[10].DO$1||''?}",10,'Do dnia'@);
         par[1].add("par[10].NK",5,'Dni kal'@,1);
         par[1].add("par[10].NR",5,'Dni rob'@,1);
         par[1].add("par[10].NG",10,'Godziny robocze'@,1,,,,,'8,2');
         par[1].add("par[10].ST",5,'Nr stat'@);
         par[1].add("par[10].LT",8,'Rozlicz. listą'@);
         {? N.fld_read('POD') || par[1].add("{?par[15]||par[10].POD||''?}",12,'Podstawa'@,1,,,,,'12,2') ?};
         par[1].s_frame();

         suma1:=suma2:=suma3:=suma4:=czy_psum:=czy_prac:=wpis:=ile_znak:=ind:=ind2:=ind_aut:=info:=rozmiar:=ii:=tmp:=0;
         lmt:=rsep:=par[8]:=par[9]:=0;
         pierwszy:=zam:=par[15]:=vp:=b_rat:=h_rat:=par[7]:=1;
         prn1:='par[1]';

         {? d_kon=#0 || d_kon:=date() ?};
         {? par[3].REAL
         || par[16]:=tab_tmp(1,
               'TYP','STRING[5]',,
               'NAZWA','STRING[20]',,
               'NK','REAL',,
               'SORT','INTEGER',
            );
            ind_aut:=par[16].ndx_tmp(,1,'TYP',,,'TYP',,);
            ind:=par[16].ndx_tmp(,1,'SORT',,0);
            par[13]:=tab_tmp(1,
               'TYP','STRING[5]',,
               'NAZWA','STRING[20]',,
               'NK','REAL',,
               'SORT','INTEGER',
            );
            ind2:=par[13].ndx_tmp(,1,'SORT',,);
            par[13].index(ind2)
         ?};

         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   {? par[12]
   || {? par[3].REAL
      || par[16].ndx_drop(ind);
         par[16].ndx_drop(ind_aut);
         par[13].ndx_drop(ind2)
      ?}
   ?};
   P.cntx_pop();
   VAR_DEL.delete('tresc','par','suma1','suma2','suma3','suma4','czy_psum','czy_prac','wpis','ile_znak','d_pocz');
   VAR_DEL.delete('d_kon','ind','ind2','ind_aut','info','rozmiar','ii','pierwszy','zam','tmp','prn1','vp','lmt','rsep');
   VAR_DEL.delete('b_rat','h_rat')
}
{COMMENT}OLD: wnieobeq.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par[12]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[7]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[7]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Uwzględniane kody'@}: {
      {? +par[2] || STR.gsub(1-par[2]-1,',',', ') || 'brak ograniczeń'@ ?}}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: od %1 do %2'@[par[3].OD$1,par[3].DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'podsum'}
{ czy_psum:=1;
  par[16].index(ind);
  par[16].prefix(2);
  {? par[16].first() & par[16].next()
  || par[16].first();
     tmp:=par[16].NK;
     par[16].del();
     par[16].TYP:='2(B)';
     par[16].NK:=par[16].NK+tmp;
     par[16].put()
  ?};
  par[16].clear();
  {? par[16].first ()
  || {!
     |? par[16].NK:=(par[16].NK/((d_kon-d_pocz)+1)$4)*100;
        par[16].put();
        par[16].next()
     !}
  ?}; ~~
}
{MACRO-}
{MACRO: 'podsum_w'}
{FONT: 'T8'}
{SPACE: 'B'}
{ czy_psum:=0;
  ii:=1;
  rozmiar:=par[16].size();
  par[16].index(ind);
  par[13].erase();
  rozmiar:=((rozmiar-1)/2)$0; ~~
}
{IF: par[16].first()}
  {par[13].TYP:=par[16].TYP;
   par[13].NAZWA:=par[16].NAZWA;
   par[13].NK:=par[16].NK;
   par[13].add(); ~~
  }
  {WHILE: ii<rozmiar}{DO}
       {ii:=ii+1;
        {? par[16].next()
        || par[13].TYP:=par[16].TYP;
           par[13].NAZWA:=par[16].NAZWA;
           par[13].NK:=par[16].NK;
           par[13].add()
        ?}; ~~
       }
  {OD}
  {<{ceil(lmt*h_rat)}} {info}
  {JUSTIFY}
  {<1}{>{ceil(lmt*h_rat)+6}
        ile_znak:=charleft();
        {? par[13].first() || par[13].TYP ?}} {<18
        par[13].NAZWA} {>50
        form(par[13].NK,,2,'9,')}% {<1}{>{ceil(ile_znak/2)+13}
        {? par[16].next() || par[16].TYP ?}} {<{ceil(ile_znak/2)+15}
        par[16].NAZWA} {>{ceil(ile_znak/2)+52}
        form(par[16].NK,,2,'9,')}%
  {WHILE: par[16].next() }{DO}
  {IF: par[13].next() }
     {<1}{>{ceil(lmt*h_rat)+6}
     ile_znak:=charleft();par[13].TYP} {<18
     par[13].NAZWA} {>50
     form(par[13].NK,,2,'9,')}% {FI} {<1}{>{ceil(ile_znak/2)+13}
     par[16].TYP} {<{ceil(ile_znak/2)+15}
     par[16].NAZWA} {>{ceil(ile_znak/2)+52}
     form(par[16].NK,,2,'9,')}%
  {OD}
{FI}
{MACRO-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{czy_prac:=1; ~~ }
{IF: par[9]<>P.ref()}
{ par[8]+=1; par[9]:=P.ref();
  info:='Udział poszczególnych nieobecności pracownika %1 %2 (Nr teczki %3) w dniach kalendarzowych: '@
   [P.OSOBA().NAZWISKO,OSOBA.PIERWSZE,|P.T];
  ~~
}
{FI}
{IF: ~rep_export()}
{<{ceil(lmt*b_rat)+1}}{form(par[8],,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[6]:=1; ~~ }
{MACRO-}
{MACRO: 'nieo_dane'}
   {IF: ~rep_export() & ~par[6]}
      { vp:=~lineleft(); ~~ }
      {IF: lineleft() & lineleft()<=6}
         {IF: zam} {SUBSTITUTE: 'LINIAF'} {FI}
         {PAGE}
         {vp:=1; ~~ }
         {SUBSTITUTE: 'LINIAF'}
      {FI}
      {zam:=1; ~~ }
      {SUBSTITUTE: 'pracownik'}
      {REP: par[1].fhbar(lmt)}
      { par[6]:=1; ~~ }
   {FI}
   { par[1].ini_line(); ~~ }
   {WHILE: par[1].next()}{DO}
      {REP: par[1].fline(lmt)}
   {OD}
   {IF: lineleft()<=2}
      { par[6]:=0; ~~ }
   {FI}
{MACRO-}
{COMMENT}

   Przeglądaj kartotekę nieobecności w podanym lub pełnym zakresie dat.
   Pełny zakres - wszystkie nieobecności, ograniczony zakres - od daty
   do daty, od 'początku' kartoteki do daty lub od daty do 'końca'.

{COMMENT-}
{MACRO: tresc}
{{? par[3].REAL
 || d_pocz:=par[3].OD;d_kon:=par[3].DO;
     {? par[3].OD=#0 || d_pocz:=P.DZA ?};
     {? par[3].DO=#0
     || {? P.DZ<>#0
        || d_kon:=P.DZ
        || d_kon:=date()
        ?}
     ?}
 ?};
 par[6]:=0;
 par[10].erase();
 {? par[3].REAL || par[16].erase(); par[16].index(ind_aut) ?}; ~~
}
{
_d0:=#0;
_sql:='select N.REFERENCE as REF, N.OD from N where N.KOR=\'N\' and N.P=:_a'+
{? par[3].DO=_d0 || '' || ' and N.OD<=to_date(:_c)'?}+
{? par[3].OD=_d0 || '' || ' and N.DO>=to_date(:_b)'?}+
' order by N.OD';
_tab:=sql(_sql,P.ref(),par[3].OD,par[3].DO);
{? _tab.first()
|| __KAL.set_cal({? P.KAL || P.KAL || 'standard' ?},{? par[3].DO=_d0 || date() || par[3].DO ?}~1);
   par[14]:=1;
   N.cntx_psh();
   N.prefix();
   {!
   |? {? N.seek(_tab.REF,) & {? +par[2] || par[2]*(','+$N.NB().RN+',') || 1 ?}
      || {? rep_export()
         || par[10].TECZKA:=P.T;
            par[10].IMIE:=P.OSOBA().PIERWSZE + ' ' + P.OSOBA().DRUGIE;
            par[10].NAZWISKO:=P.OSOBA().NAZWISKO
         || par[10].IMIE:='';
            par[10].NAZWISKO:='';
            par[10].TECZKA:=''
         ?};
         par[10].TYP:=$N.NB().RN;
         par[10].NAZWA:=R.RT;
         _bz:=0;
         par[10].OD:={? par[3].OD=_d0 | par[3].OD<=N.OD || _bz+=1; N.OD || par[3].OD ?};
         par[10].DO:={? par[3].DO=_d0 | N.DO<=par[3].DO || _bz+=1; N.DO || par[3].DO ?};
         {? _bz=2
         || par[10].NK:=N.NK;
            par[10].NR:=N.NR;
            par[10].NG:=N.NG
         || par[10].NK:=par[10].DO-par[10].OD+1;
            par[10].NR:=__KAL.w_days(par[10].OD,par[10].DO);
            par[10].NG:=__KAL.w_hours(par[10].OD,par[10].DO)
         ?};
         par[10].ST:=N.ST;
         par[10].LT:=N.LT;
         par[10].POD:=N.POD;
         par[10].add();
         {? ~rep_export() & par[3].REAL
         || par[16].prefix(par[10].TYP,par[10].TYP);
            {? par[16].first()
            || _new:=0
            || _new:=1;
               par[16].blank();
               par[16].TYP:=par[10].TYP;
               par[16].NAZWA:=par[10].NAZWA;
               par[16].SORT:=#par[16].TYP
            ?};
            par[16].NK+=par[10].NK;
            {? _new || par[16].add() || par[16].put() ?}
         ?}
      ?};
      _tab.next()
   !};
   N.cntx_pop()
|| par[14]:=0
?}; ~~
}
{IF: ~rep_export() & pierwszy & par[14]} {pierwszy:=0; ~~ }{SUBSTITUTE: 'LINIAF'}{FI}
{suma1:=suma2:=suma3:=suma4:=0;
 par[15]:=1; ~~ }
{FOR: par[10]}{INDEX: par[11]}{DO}
      {SUBSTITUTE: 'nieo_dane'}
      {suma1+=par[10].NK;
       suma2+=par[10].NR;
       suma3+=par[10].NG; ~~
      }
{OD}
{IF: ~rep_export() & par[3].REAL}
      {FOR: par[16]}{DO}
      {suma4+=par[16].NK; ~~ }
{OD}
{FI}
{IF: ~rep_export() & par[10].first()}
   {par[15]:=0;
    par[10].TYP:=' ';
    par[10].NAZWA:='Razem';
    par[10].NK:=suma1;
    par[10].NR:=suma2;
    par[10].NG:=suma3;
    par[10].ST:='';
    par[10].LT:='';
    par[10].add();
    {? par[3].REAL
    || par[16].clear();
       par[16].TYP:=par[10].TYP;
       par[16].NAZWA:=par[10].NAZWA;
       par[16].NK:=suma4;
       par[16].SORT:=100;
       par[16].add()
    ?}; ~~
   }
   {SUBSTITUTE: 'nieo_dane'}
   {REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}{zam:=0; ~~ }
   {IF: par[3].REAL}
      {SUBSTITUTE: 'podsum'}
      {IF: lineleft() & lineleft()<=6}
         {PAGE}
         {vp:=1; ~~ }
         {SUBSTITUTE: 'LINIAF'}
      {FI}
      {SUBSTITUTE: 'podsum_w'}
   {FI}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{IF: rep_export()}
   {SUBSTITUTE: 'LINIAF'}
{FI}
{FOOTER: 0}
   { vp:=0; ~~ }
{FOOTER-}
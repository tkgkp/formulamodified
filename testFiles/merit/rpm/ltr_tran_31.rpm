:!UTF-8
{TITLE:I. Kilometry w podziale na zlecenia}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('tryb');
   VAR_DEL.delete('w','s','t','PRN');
   w  :=obj_new(5); "--wartosci kolumn--";
   s  :=obj_new(20); "--razem--";
   PRN :=obj_new(@.CLASS.PRINT,20); PRN.s_frame();
   tryb:=rep_export();

   s[5] :=s[15] :=0;
   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   POJAZDY.win_sel('WER');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;
   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0;
      {? KARTDROG.WYBKART='M'
      || KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
         KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0)
      |? KARTDROG.WYBKART='W'
      || KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
         KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
         SAMK.index('D2');
         SAMK.prefix();
         {? SAMK.first()
         || KARTDROG.KARTY_OD:=SAMK.D;
            SAMK.last();
            KARTDROG.KARTY_DO:=SAMK.D
         ?}
      ?}
   || wyjscie:=1
   ?}
}
{END:
   undefine();
   VAR_DEL.delete('tryb');
   VAR_DEL.delete('w','s','t','PRN');
   VAR_DEL.delete('wyjscie','oddz');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("form(w[1])",5,'Lp.',1) ?};
PRN.add("w[2]",20,'Nr rejestracyjny');
PRN.add("w[3]",20,'Nr ewidencyjny');
PRN.add("w[4]",20,'Konto');
PRN.add("form(w[5])",15,'Ilość km',1);

PAR_WYDR.TITLE:='Kilometry w podziale na zlecenia';
prn1:='PRN'
;~~}
{IF: POJAZDY.index('NRREJ'); POJAZDY.prefix(REF.FIRMA); POJAZDY.first()=0}{ FUN.info('Brak danych spełniających kryteria.');~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}
{<{lmt+1}}{'Od daty: '+$KARTDROG.KARTY_OD}
{<{lmt+1}}{'Do daty: '+$KARTDROG.KARTY_DO}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; w[1]:=0; s[2]:=0;~~}
{FOR:'POJAZDY'}
{INDEX: 'NRREJ'}
{PREFIX: REF.FIRMA}
{DO}
   {FIRST}{ kolejny:=0;~~}{FIRST-}
   {FONT: 'T8G'}{SPACE: 'C'}
   {FOR:'NWYR'}
   {DO}
      {FIRST}
      { s[15] :=0; w[1] :=0;~~}
      {FIRST-}
      {
      {? s[15]=0 || w[2]:=POJAZDY.NRREJ; w[3]:=POJAZDY.NREW || w[2]:='';w[3]:='' ?};
      w[4]:=NWYR.NAZ;
      w[5]:=exec('zle_kosz','karty_drog',POJAZDY.ref(),NWYR.ref(),KARTDROG.KARTY_OD,KARTDROG.KARTY_DO,1);
      s[5]  +=w[5];
      s[15] +=w[5]
      ;~~}
      {IF: w[5]<>0}
         {IF: kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
         {w[1]+=1;~~}
         {SUBSTITUTE: 'LINIA0'}
         { rsep:=PAR_WYDR.RSEP; kolejny:=0;~~}
      {FI}
   {OD}
   {IF: tryb<>1 & s[15]<>0}
   {FONT: 'T8GB'}{SPACE: 'C'}
      {
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+3
      ;~~}
      {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {
      kolejny:=1; rsep:=0;
      w[1] :='';
      w[2] :='';
      w[3] :='';
      w[4] :='Razem '+POJAZDY.NRREJ+':'; PRN.FORM[4]:=0;
      w[5] :=s[15]; s[15] :=0;
      PRN.n_frame()
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {FONT: 'T8G'}{SPACE: 'C'}
      {
      PRN.s_frame();
      PRN.FORM[2]:=1
      ;~~}
   {FI}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1 & s[5]<>0}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {
   PRN.n_frame();
   w[1] :=w[2] :=w[3] :='';
   w[4] :='Razem:'; PRN.FORM[4] :=0;
   w[5] :=s[5]
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
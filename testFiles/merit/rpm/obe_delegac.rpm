:!UTF-8
{TITLE:Delegacja}
{PRINTER: 'hp3v',,,,,'M','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   exec('czytaj','#stalesys');
   {? app_info('web_sesid')<>''
   ||
      exec('set_parw','param_wydr');
      EDOKUM.web_cntx_load()
   ?};

   PRNP:=obj_new(@.CLASS.PRINT,15); PRNP.s_frame();
   PRNPF:=obj_new(@.CLASS.PRINT,15); PRNPF.s_frame();
   PRND:=obj_new(@.CLASS.PRINT,15); PRND.s_frame();
   PRNDF:=obj_new(@.CLASS.PRINT,15); PRNDF.s_frame();
   PRNW:=obj_new(@.CLASS.PRINT,15); PRNW.s_frame();
   PRNWF:=obj_new(@.CLASS.PRINT,15); PRNWF.s_frame();
   PRNZ:=obj_new(@.CLASS.PRINT,15); PRNZ.s_frame();
   PRNZF:=obj_new(@.CLASS.PRINT,15); PRNZF.s_frame();
   PRNSUM:=obj_new(@.CLASS.PRINT,15); PRNSUM.s_frame();
   PRNATR:=obj_new(@.CLASS.PRINT,15); PRNATR.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,15); PRNS.sl_frame();
   c:=obj_new(15);
   d:=obj_new(16);
   e:=obj_new(15);
   f:=obj_new(15);
   szc:=obj_new(15);
   {! _i:=1..15 |! c[_i] := '' !};
   {! _i:=1..16 |! d[_i] := '' !};
   {! _i:=1..15 |! e[_i] := '' !};
   {! _i:=1..15 |! f[_i] := '' !};
   {! _i:=1..15 |! szc[_i] := '' !};
   __T_DSUM:=tab_tmp(2,'RODZAJ','STRING[10]','Rodzaj'@,
                       'WAL','STRING[3]','Waluta'@,
                       'KWOTA_W','REAL','Kwota'@,
                       'KWOTA_WP','REAL','Kwota do rozliczenia z pracownikiem'@,
                       'RYCZ_NOC','REAL','Ryczałt za nocleg'@,
                       'RYCZ_DOJ','REAL','Ryczałt za dojazd'@,
                       'KWOTA_L','REAL','Lotnisko Port'@,
                       'STRONA','STRING[1]','Strona'@);
   i:=0;
   color:=prn1:=prn2:='';
   licznik:=0;ewidpsw:=0;ewidpsp:=0;ewidpsk:=0;sekcja:='STOP';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0; par5:=0;
   part_p:=part_d:=part_w:=part_z:=part_a:=0;
   EDOKUMP.use('skidpu'+(EDOKUM.name()+2));
   EDOKUMD.use('bdiety'+(EDOKUM.name()+2));
   EDOK_ZAL.use('edokzl'+(EDOKUM.name()+2));
   EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
   EVAT.use('skid_a'+(EDOKUM.name()+2));
   EDOKUMP.index('CHRONO');
   EDOKUMP.prefix(EDOKUM.ref());
   {? EDOKUMP.first() || part_p:=1 ?};
   EDOKUMD.index('DELEG');
   EDOKUMD.prefix(EDOKUM.ref());
   {? EDOKUMD.first() || part_d:=1 ?};
   EDOK_ZAL.index('WAL');
   EDOK_ZAL.prefix(EDOKUM.ref(),'W');
   {? EDOK_ZAL.first() || part_w:=1 ?};
   EDOK_ZAL.index('WAL');
   EDOK_ZAL.prefix(EDOKUM.ref(),'Z');
   {? EDOK_ZAL.first() || part_z:=1 ?};
   __T_ATR:=proc_exe('edk_atr_sel@obe',$EDOKUM.ref());
   {? __T_ATR.first() || part_a:=1 ?};
   czy_dob:=(EDOKUM.TYPOBIEG().NAZWA='Obieg faktur') | (EDOKUM.TYPOBIEG().NAZWA='Obieg delegacji');
   czy_vat:=0;
   {? czy_dob
   || EVAT.cntx_psh();
      EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
      czy_vat:=EVAT.first();
      EVAT.cntx_pop()
   ?};
   czy_podz:=0;
   _par:=params_get();
   {? type_of(_par)<>0
   ||
      _showDialog:={? var_pres('show',_par)>0 || _par.show || 1 ?}
   ||
      _showDialog:= 1
   ?};
   {? cli_ver = 'mbasic'
   || dobieg:=1
   || dobieg:=
         {? _showDialog
         || {? czy_dob
            || FUN.ask('Drukować szczegóły obiegu '+
                  {? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur' || 'dokumentu?' || 'delegacji?' ?}
               )
            || FUN.ask('Drukować szczegóły obiegu wniosku?')
            ?}
         ?}
   ?}
}
{END:
   obj_del(PRNP); obj_del(PRNPF);
   obj_del(PRND); obj_del(PRNDF);
   obj_del(PRNW); obj_del(PRNWF);
   obj_del(PRNZ); obj_del(PRNZF);
   obj_del(PRNSUM); obj_del(PRNATR);
   obj_del(PRNS);
   obj_del(c); obj_del(d); obj_del(e); obj_del(f); obj_del(szc);
   VAR_DEL.delete('__T_DSUM','__T_SUM','__T_ATR','czy_dob','dobieg','czy_vat','czy_podz');
   &i; &color; &prn1; &prn2; &licznik; &ewidpsw; &ewidpsp; &ewidpsk; &sekcja;
   &part_p; &part_d; &part_w; &part_z; &part_a;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3;&par4;&par5
}
{EXIT:
   0
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: delegac.rpm
Utworzony  : 2012-07-19
Zawartość  : Delegacja
==================================================
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRNP'; prn2:='PRNP'; ~~}
{
      PRNP.add("c[1]",20,'Miejsce wyjazdu'@,1,'#');
      PRNP.add("c[2]",10,'Data wyjazdu'@,,' ');
      PRNP.add("c[3]",8,'Godzina wyjazdu'@,,' ');
      PRNP.add("c[4]",20,'Miejsce przyjazdu'@,,' ');
      PRNP.add("c[5]",10,'Data przyjazdu'@,,' ');
      PRNP.add("c[6]",10,'Godzina przyjazdu'@,,' ');
      PRNP.add("c[7]",16,'Środek lokomocji'@,,' ');
      PRNP.add("form(c[8],,2,' ,')",10,'Liczba km'@,1,'#');
      PRNP.add("form(c[9],,2,' ,')",10,'Wartość'@,1,'#');

      PRNPF.add("c[10]", 112, 'Razem'@);
      PRNPF.add("form(c[11],,2,' ,')", 10, 'Liczba km'@,1,'#');
      PRNPF.add("form(c[12],,2,' ,')", 10, 'Wartość'@,1,'#');

      PRND.add("d[1]",16,'Kraj'@,,' ');
      PRND.add("d[2]",6,'Waluta'@,,' ');
      PRND.add("d[3]",10,'Data od'@,,' ');
      PRND.add("d[4]",5,'G. od'@,,' ');
      PRND.add("d[5]",10,'Data do'@,,' ');
      PRND.add("d[6]",5,'G. do'@,,' ');
      PRND.add("d[16]",8,'Pomniej. (Ś/O/K)'@,,' ');
      PRND.add("form(d[15],,2,' ,')",6,'L. godz.'@,1,' ');
      PRND.add("form(d[7],,2,' ,')",10,'Kwota diety'@,1,' ');
      PRND.add("form(d[8],,2,' ,')",10,'Ryczałt za nocleg'@,1,' ');
      PRND.add("form(d[9],,2,' ,')",10,'Ryczałt za dojazd'@,1,' ');
      PRND.add("form(d[14],,2,' ,')",9,'Lotnisko /Port'@,1,' ');


      PRNDF.add("d[10]", 87, 'Razem');
      PRNDF.add("form(d[11],,2,' ,')", 10, 'Kwota diety'@,1,' ');
      PRNDF.add("form(d[12],,2,' ,')", 10, 'Ryczałt za nocleg'@,1,' ');
      PRNDF.add("form(d[13],,2,' ,')", 10, 'Ryczałt za dojazd'@,1,' ');
      PRNDF.add("form(d[14],,2,' ,')", 9, 'Lotnisko /Port'@,1,' ');

      PRNW.add("e[1]",37,'Rodzaj'@,,' ');
      PRNW.add("e[2]",72,'Opis'@,,' ');
      PRNW.add("e[3]",6,'Waluta'@,,' ');
      PRNW.add("form(e[4],,2,' ,')",14,'Kwota wydatku'@,1,' ');

      PRNWF.add("e[10]", 121, 'Razem'@);
      PRNWF.add("form(e[11],,2,' ,')", 14, 'Kwota wydatku'@,1,' ');

      PRNZ.add("f[1]",8,'Przelew?'@,,' ');
      PRNZ.add("f[2]",30,'Rachunek bankowy'@,,' ');
      PRNZ.add("f[3]",16,'Dotyczy wynagr.?'@,,' ');
      PRNZ.add("f[4]",49,'Opis'@,,' ');
      PRNZ.add("f[5]",6,'Waluta'@,,' ');
      PRNZ.add("form(f[6],,2,' ,')",14,'Kwota zaliczki'@,1,' ');

      PRNZF.add("f[10]", 121, 'Razem'@);
      PRNZF.add("form(f[11],,2,' ,')", 14, 'Kwota'@,1,' ');

      PRNSUM.add("f[1]", 112, 'Opis'@);
      PRNSUM.add("f[2]",6,'Waluta'@,,' ');
      PRNSUM.add("form(f[3],,2,' ,')", 14, 'Kwota'@,1,' ');

      PRNATR.add("f[4]",7,'Rekord'@,1,' ');
      PRNATR.add("f[5]",57,'Opis'@,,' ');
      PRNATR.add("f[6]",68,'Wartość'@,,' ');

      PRNS.add("szc[1]",10,'Data'@);
      PRNS.add("szc[2]",8,'Godzina'@);
      PRNS.add("szc[3]",20,'Operator'@);
      PRNS.add("szc[4]",15,'Etap'@);
      PRNS.add("szc[5]",35,'Opis'@);
      PRNS.add("szc[6]",45,'Uwagi'@);

      PAR_WYDR.TITLE:='Delegacja'@

; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRNP.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{FONT: 'T10'}{SPACE: 'B'}
{
  exec('pw_del_etat','obe');
  par3:=OBIEGI.DEL_ETAT
;~~}
{LINE: 1}
{<1 'Identyfikator:'@}{FONT: 'T10B'}{<27 EDOKUM.ID}{FONT: 'T10'}
{<1 'Delegowany: '@}{FONT: 'T10B'}{<27 EDOKUM.DOSTAWCA().NAZWISKO+' '+EDOKUM.DOSTAWCA().PIERWSZE}{FONT: 'T10'}{{? par3<>'' || ', stanowisko: '@ || ~~ ?} }{FONT: 'T10B'}{ {? par3<>'' || par3 || ~~ ?} }{FONT: 'T10'}
{<1 'Data polecenia wyjazdu: '@}{FONT: 'T10B'}{<27 EDOKUM.DW$1}{FONT: 'T10'}
{<1 'Data od: '@}{FONT: 'T10B'}{<27 EDOKUM.DATA_OD$1}{FONT: 'T10'}{' godz. '}{FONT: 'T10B'}{EDOKUM.GODZ_OD$1}{FONT: 'T10'}{' - do: '@}{FONT: 'T10B'}{EDOKUM.DATA_DO$1}{FONT: 'T10'}{' godz. '@}{FONT: 'T10B'}{EDOKUM.GODZ_DO$1}{FONT: 'T10'}
{IF: EDOKUM.TYP().DEL_ZAGR}
{<1 'Data prz. gran. (wyjazd): '@}{FONT: 'T10B'}{<27 EDOKUM.GRAN_DAT$1}{FONT: 'T10'}{' godz. '@}{FONT: 'T10B'}{EDOKUM.GRAN_GDZ$1}{FONT: 'T10'}
{<1 'Data prz. gran. (powrót): '@}{FONT: 'T10B'}{<27 EDOKUM.GRAN_DPW$1}{FONT: 'T10'}{' godz. '@}{FONT: 'T10B'}{EDOKUM.GRAN_GPW$1}{FONT: 'T10'}
{FI}
{<1 'Cel: '}{FONT: 'T10B'}{<27 EDOKUM.DEL_CEL().TR}{FONT: 'T10'}
{<1 'Miejsce delegacji: '@}{FONT: 'T10B'}{<27 EDOKUM.DEL_MIE}{FONT: 'T10'}
{<1 'Opis: '@}{FONT: 'T10B'}{<27 EDOKUM.TR}{FONT: 'T10'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: sekcja='START'}{SUBSTITUTE: 'TABHEAD'}{FI}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: sekcja='START'}{SUBSTITUTE: 'TABHEAD'}{FI}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: sekcja='START'}{SUBSTITUTE: 'LINIAF'}{FI}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOTCMS'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{COMMENT} ---- przejazdy ---- {COMMENT-}
{IF: part_p=1}
{IF: lineleft() <> 0 & lineleft() <=8}{PAGE}{ sekcja:='STOP';~~}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}{<1 'PRZEJAZDY:'@}{FONT: 'T8G'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: lineleft() <> 0 & lineleft()<=6}{PAGE}{ sekcja:='START';~~}
{ELSE}{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{ rsep:=1; ~~}
{prn1:='PRNP';~~}
{FOR: EDOKUMP}
 {INDEX: 'CHRONO'}
 {PREFIX: EDOKUM.ref()}
 {DO}
 { sekcja:='START';~~}
 {
      c[1]:=EDOKUMP.MIE_WYJ;
      c[2]:=form(EDOKUMP.DATA_WYJ,,1);
      c[3]:=form(EDOKUMP.GODZ_WYJ,,1);
      c[4]:=EDOKUMP.MIE_PRZ;
      c[5]:=form(EDOKUMP.DATA_PRZ,,1);
      c[6]:=form(EDOKUMP.GODZ_PRZ,,1);
      {? EDOKUMP.SRODTRAN='S' || _sl:='Sam. służbowy'@
      |? EDOKUMP.SRODTRAN='P' || _sl:='Sam. prywatny'@
      |? EDOKUMP.SRODTRAN='I' || _sl:='Inny:%1'@[EDOKUMP.INNESRTR().NAZWA]
      ?};
      c[7]:=_sl;
      c[8]:=EDOKUMP.LICZ_KM;
      c[9]:=EDOKUMP.WARTOSC;

      ewidpsk+=EDOKUMP.LICZ_KM;
      ewidpsw+=EDOKUMP.WARTOSC;
      {? EDOKUMP.SRODTRAN='P' || ewidpsp+=EDOKUMP.WARTOSC ?}

 ;~~}
  {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRNP';prn2:='PRNPF'; rsep:=1;
 c[10]:='Razem w PLN: '@;
 c[11]:=ewidpsk;
 c[12]:=ewidpsw;
 __T_DSUM.WAL:='PLN';
 __T_DSUM.RODZAJ:='EDOKUMP';
 __T_DSUM.KWOTA_W:=ewidpsw;
 __T_DSUM.KWOTA_WP:=ewidpsp;
 __T_DSUM.STRONA:='+';
 __T_DSUM.KWOTA_L:=0;
 __T_DSUM.add()
;~~}
{SUBSTITUTE: 'LINIA02'}
{FONT: 'T8G'}{SPACE: 'C'}
{ sekcja:='STOP';~~}
{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
{LINE: 1}
{ELSE}
{FONT: 'T8GB'}{SPACE: 'C'}{<1 'BRAK PRZEJAZDÓW'@}{FONT: 'T8G'}
{FI}
{COMMENT} ---- END przejazdy ---- {COMMENT-}
{COMMENT} ---- diety ---- {COMMENT-}
{IF: part_d=1}
{IF: lineleft() <> 0 & lineleft() <=8}{PAGE}{ sekcja:='STOP';~~}{FI}
{FONT: 'T8GB'}{<1 'DIETY:'}{FONT: 'T8G'}
{LINE: 1}
{ prn1:=prn2:='PRND';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: lineleft() <> 0 & lineleft()<=6}{PAGE}{ sekcja:='START';~~}
{ELSE}{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{ rsep:=1; ~~}
{FOR: EDOKUMD}
 {INDEX: 'DELEG'}
 {PREFIX: EDOKUM.ref()}
 {DO}
 { sekcja:='START';~~}
 {
      d[1]:=EDOKUMD.KRAJ().NAZ;
      d[2]:=EDOKUMD.WAL().SYM;
      d[3]:=form(EDOKUMD.DATA_OD,,1);
      d[4]:=form(EDOKUMD.GODZ_OD,,1);
      d[5]:=form(EDOKUMD.DATA_DO,,1);
      d[6]:=form(EDOKUMD.GODZ_DO,,1);
      d[15]:={? EDOKUMD.Z_CZY_G='G'
             || $EDOKUMD.CZAS_GDZ+':'+form(EDOKUMD.CZAS_MIN,-2)
             || ''
             ?};
      d[16]:={? EDOKUMD.SNIAD || $EDOKUMD.LICZ_SNI || '0' ?}+'/'
                +{? EDOKUMD.OBIAD || $EDOKUMD.LICZ_OBI || '0' ?}+'/'
                +{? EDOKUMD.KOLACJA || $EDOKUMD.LICZ_KOL || '0' ?};
      d[7]:=EDOKUMD.KWOTA_W;
      d[8]:=EDOKUMD.RYCZ_NOC;
      d[9]:=EDOKUMD.RYCZ_DOJ;
      d[14]:=EDOKUMD.KWOTA_L;

      {? __T_DSUM.find_key('EDOKUMD',EDOKUMD.WAL().SYM)
      || __T_DSUM.KWOTA_W+=EDOKUMD.KWOTA_W;
         __T_DSUM.KWOTA_WP+=EDOKUMD.KWOTA_W;
         __T_DSUM.RYCZ_NOC+=EDOKUMD.RYCZ_NOC;
         __T_DSUM.RYCZ_DOJ+=EDOKUMD.RYCZ_DOJ;
         __T_DSUM.KWOTA_L+=EDOKUMD.KWOTA_L;
         __T_DSUM.put()
      || __T_DSUM.WAL:=EDOKUMD.WAL().SYM;
         __T_DSUM.RODZAJ:='EDOKUMD';
         __T_DSUM.KWOTA_W:=EDOKUMD.KWOTA_W;
         __T_DSUM.KWOTA_WP:=EDOKUMD.KWOTA_W;
         __T_DSUM.RYCZ_NOC:=EDOKUMD.RYCZ_NOC;
         __T_DSUM.RYCZ_DOJ:=EDOKUMD.RYCZ_DOJ;
         __T_DSUM.KWOTA_L+=EDOKUMD.KWOTA_L;
         __T_DSUM.STRONA:='+';
         __T_DSUM.add()
      ?}
 ;~~}
  {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRND'; prn2:='PRNDF'; rsep:=1; licznik:=0; ~~}
{FOR: __T_DSUM}
 {PREFIX: 'EDOKUMD'}
 {DO}
 {
   licznik+=1;
   d[10]:='Razem w '+__T_DSUM.WAL+': ';
   d[11]:=__T_DSUM.KWOTA_W;
   d[12]:=__T_DSUM.RYCZ_NOC;
   d[13]:=__T_DSUM.RYCZ_DOJ;
   d[14]:=__T_DSUM.KWOTA_L
 ;~~}
 {IF: licznik=1}
   {SUBSTITUTE: 'LINIA02'}
 {ELSE}
   {prn1:='PRNDF'; prn2:='PRNDF'; ~~}
   {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
   {SUBSTITUTE: 'LINIA0'}
 {FI}
 {OD}
{FONT: 'T8G'}{SPACE: 'C'}
{ sekcja:='STOP';~~}
{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
{LINE: 1}
{ELSE}
{FONT: 'T8GB'}{SPACE: 'C'}{<1 'BRAK DIET'}{FONT: 'T8G'}
{FI}
{COMMENT} ---- END diety ---- {COMMENT-}
{COMMENT} ---- wydatki ---- {COMMENT-}
{IF: part_w=1}
{IF: lineleft() <> 0 & lineleft() <=8}{PAGE}{ sekcja:='STOP';~~}{FI}
{FONT: 'T8GB'}{<1 'WYDATKI:'}{FONT: 'T8G'}
{LINE: 1}
{ prn1:=prn2:='PRNW';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: lineleft() <> 0 & lineleft()<=6}{PAGE}{ sekcja:='START';~~}
{ELSE}{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{ rsep:=1; ~~}
{FOR: EDOK_ZAL}
 {INDEX: 'WAL'}
 {PREFIX: EDOKUM.ref(),'W'}
 {DO}
 { sekcja:='START';~~}
 {
      e[1]:=EDOK_ZAL.RWYD().TR;
      e[2]:=EDOK_ZAL.OPIS;
      e[3]:=EDOK_ZAL.WAL().KOD;
      e[4]:={? EDOK_ZAL.BRUTTO<>0 || EDOK_ZAL.BRUTTO || EDOK_ZAL.NETTO ?};

      {? __T_DSUM.find_key('EDOK_ZAL_W',EDOK_ZAL.WAL().KOD)
      || __T_DSUM.KWOTA_W+={? EDOK_ZAL.BRUTTO<>0 || EDOK_ZAL.BRUTTO || EDOK_ZAL.NETTO ?};
         {? EDOK_ZAL.ROZLPRAC
         || __T_DSUM.KWOTA_WP+={? EDOK_ZAL.BRUTTO<>0 || EDOK_ZAL.BRUTTO || EDOK_ZAL.NETTO ?}
         ?};
         __T_DSUM.put()
      || __T_DSUM.WAL:=EDOK_ZAL.WAL().KOD;
         __T_DSUM.RODZAJ:='EDOK_ZAL_W';
         __T_DSUM.KWOTA_W:={? EDOK_ZAL.BRUTTO<>0 || EDOK_ZAL.BRUTTO || EDOK_ZAL.NETTO ?};
         __T_DSUM.RYCZ_NOC:=0;
         __T_DSUM.RYCZ_DOJ:=0;
         __T_DSUM.STRONA:='+';
         __T_DSUM.KWOTA_L:=0;
         __T_DSUM.KWOTA_WP:={? EDOK_ZAL.ROZLPRAC
                            || {? EDOK_ZAL.BRUTTO<>0 || EDOK_ZAL.BRUTTO || EDOK_ZAL.NETTO ?}
                            || 0
                            ?};
         __T_DSUM.add()
      ?}
 ;~~}
  {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRNW'; prn2:='PRNWF'; rsep:=1; licznik:=0; ~~}
{FOR: __T_DSUM}
 {PREFIX: 'EDOK_ZAL_W'}
 {DO}
 {
   licznik+=1;
   e[10]:='Razem w '+__T_DSUM.WAL+': ';
   e[11]:=__T_DSUM.KWOTA_W
 ;~~}
 {IF: licznik=1}
   {SUBSTITUTE: 'LINIA02'}
 {ELSE}
   {prn1:='PRNWF'; prn2:='PRNWF'; ~~}
   {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
   {SUBSTITUTE: 'LINIA0'}
 {FI}
 {OD}
{FONT: 'T8G'}{SPACE: 'C'}
{ sekcja:='STOP';~~}
{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
{LINE: 1}
{ELSE}
{FONT: 'T8GB'}{SPACE: 'C'}{<1 'BRAK WYDATKÓW'@}{FONT: 'T8G'}
{FI}
{COMMENT} ---- END wydatki ---- {COMMENT-}
{COMMENT} ---- zaliczki ---- {COMMENT-}
{IF: part_z=1}
{IF: lineleft() <> 0 & lineleft() <=8}{PAGE}{ sekcja:='STOP';~~}{FI}
{FONT: 'T8GB'}{<1 'ZALICZKI:'@}{FONT: 'T8G'}
{LINE: 1}
{ prn1:=prn2:='PRNZ';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: lineleft() <> 0 & lineleft()<=6}{PAGE}{ sekcja:='START';~~}
{ELSE}{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{ rsep:=1; ~~}
{FOR: EDOK_ZAL}
 {INDEX: 'WAL'}
 {PREFIX: EDOKUM.ref(),'Z'}
 {DO}
 { sekcja:='START';~~}
 {
      f[1]:={? EDOK_ZAL.KASPRZEL='K' || 'Kasa'@ |? EDOK_ZAL.KASPRZEL='P' || 'Przelew'@ || 'Inny'@ ?};
      f[2]:=EDOK_ZAL.SKID_RBK().N;
      f[3]:=EDOK_ZAL.DOT_WYN;
      f[4]:=EDOK_ZAL.OPIS;
      f[5]:=EDOK_ZAL.WAL().KOD;
      f[6]:=EDOK_ZAL.KWOTA;
      {? __T_DSUM.find_key('EDOK_ZAL_Z',EDOK_ZAL.WAL().KOD)
      || __T_DSUM.KWOTA_W+=EDOK_ZAL.KWOTA;
         EZAL.cntx_psh(); EZAL.prefix();
         {? EDOK_ZAL.EZAL<>null()
         || EDOK_ZAL.EZAL();
            EZALPOZ.cntx_psh(); EZALPOZ.index('ZALICZKA'); EZALPOZ.prefix(EZAL.ref());
            {? EZALPOZ.first()
            || {! |?
                  __T_DSUM.KWOTA_WP+=EZALPOZ.ROZCHOD;
                  EZALPOZ.next()
               !}
            ?};
            EZALPOZ.cntx_pop()
         ?};
         EZAL.cntx_pop();
         __T_DSUM.put()
      || __T_DSUM.WAL:=EDOK_ZAL.WAL().KOD;
         __T_DSUM.RODZAJ:='EDOK_ZAL_Z';
         __T_DSUM.KWOTA_W:=EDOK_ZAL.KWOTA;
         __T_DSUM.KWOTA_WP:=0;
         EZAL.cntx_psh(); EZAL.prefix();
         {? EDOK_ZAL.EZAL<>null()
         || EDOK_ZAL.EZAL();
            EZALPOZ.cntx_psh(); EZALPOZ.index('ZALICZKA'); EZALPOZ.prefix(EZAL.ref());
            {? EZALPOZ.first()
            || {! |?
                  __T_DSUM.KWOTA_WP+=EZALPOZ.ROZCHOD;
                  EZALPOZ.next()
               !}
            ?};
            EZALPOZ.cntx_pop()
         ?};
         EZAL.cntx_pop();
         __T_DSUM.RYCZ_NOC:=0;
         __T_DSUM.RYCZ_DOJ:=0;
         __T_DSUM.STRONA:='-';
         __T_DSUM.KWOTA_L:=0;
         __T_DSUM.add()
      ?}
 ;~~}
  {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRNZ'; prn2:='PRNZF'; rsep:=1; licznik:=0; ~~}
{FOR: __T_DSUM}
 {PREFIX: 'EDOK_ZAL_Z'}
 {DO}
 {
   licznik+=1;
   f[10]:='Razem w %1:'@[__T_DSUM.WAL];
   f[11]:=__T_DSUM.KWOTA_W
 ;~~}
 {IF: licznik=1}
   {SUBSTITUTE: 'LINIA02'}
 {ELSE}
   {prn1:='PRNZF'; prn2:='PRNZF'; ~~}
   {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
   {SUBSTITUTE: 'LINIA0'}
 {FI}
 {OD}
{FONT: 'T8G'}{SPACE: 'C'}
{ sekcja:='STOP';~~}
{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
{LINE: 1}
{ELSE}
{FONT: 'T8GB'}{SPACE: 'C'}{<1 'BRAK ZALICZEK'@}{FONT: 'T8G'}
{FI}
{COMMENT} ---- END zaliczki ---- {COMMENT-}
{COMMENT} ---- atrybuty ---- {COMMENT-}
{IF: part_a=1}
{IF: lineleft() <> 0 & lineleft() <=8}{PAGE}{ sekcja:='STOP';~~}{FI}
{FONT: 'T8GB'}{<1 'INNE DANE:'}{FONT: 'T8G'}
{LINE: 1}
{ prn1:=prn2:='PRNATR';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: lineleft() <> 0 & lineleft()<=6}{PAGE}{ sekcja:='START';~~}
{ELSE}{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{ rsep:=1; par5:=''; ~~}
{FOR: __T_ATR}
 {DO}
 { sekcja:='START';~~}
 {IF: __T_ATR.WARTOSC<>''}
   {
         {? par5<>$__T_ATR.REKORD || f[4]:=$__T_ATR.REKORD || f[4]:='' ?};
         par5:=$__T_ATR.REKORD;
         f[5]:=__T_ATR.TAT_OPIS;
         f[6]:=__T_ATR.WARTOSC
   ;~~}
   {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
   {SUBSTITUTE: 'LINIA0'}
   {rsep:=PAR_WYDR.RSEP;~~}
 {FI}
 {OD}
{FONT: 'T8G'}{SPACE: 'C'}
{ sekcja:='STOP';~~}
{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
{LINE: 1}
{ELSE}
{FONT: 'T8GB'}{SPACE: 'C'}{<1 'BRAK INNYCH DANYCH'}{FONT: 'T8G'}
{FI}
{COMMENT} ---- END atrybuty ---- {COMMENT-}
{COMMENT} ---- podsumowanie ---- {COMMENT-}
{IF: part_p=1 | part_d=1 | part_w=1 | part_z=1}
{IF: lineleft() <> 0 & lineleft() <=7}{PAGE}{ sekcja:='STOP';~~}{FI}
{FONT: 'T8GB'}{<1 'PODSUMOWANIE:'@}{FONT: 'T8G'}
{LINE: 1}
{ prn1:=prn2:='PRNSUM';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: lineleft() <> 0 & lineleft()<=6}{PAGE}
{ELSE}{ sekcja:='START';~~}{SUBSTITUTE: 'TABHEAD'}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{ rsep:=1; ~~}
{ _sql:='select STRONA, '+
        '(sum(KWOTA_W)+sum(RYCZ_DOJ)+sum(RYCZ_NOC)+sum(KWOTA_L)) as KWOTA, '+
        '(sum(KWOTA_WP)+sum(RYCZ_DOJ)+sum(RYCZ_NOC)+sum(KWOTA_L)) as KWOTAP, '+
        'WAL from :_a group by WAL, STRONA order by WAL,STRONA';
  __T_SUM:=sql(_sql,__T_DSUM);
  _tmp:=sql('select distinct WAL from :_a',__T_DSUM);
  {? _tmp.first()
  || {! |?
        __T_SUM.prefix(_tmp.WAL);
        _sum:=_sump:=0;
        {? __T_SUM.first()
        || {! |?
              {? __T_SUM.STRONA='+'
              || _sum+=__T_SUM.KWOTA; _sump+=__T_SUM.KWOTAP
              |? __T_SUM.STRONA='-'
              || _sum-=__T_SUM.KWOTA; _sump-=__T_SUM.KWOTAP
              ?};
              __T_SUM.next()
           !}
        ?};
        __T_SUM.WAL:=_tmp.WAL;
        __T_SUM.STRONA:='X';
        __T_SUM.KWOTA:=_sum;
        __T_SUM.KWOTAP:=_sump;
        __T_SUM.add();
        _tmp.next()
     !}
  ?};
  __T_SUM.prefix()
;~~}
{FOR: __T_SUM}
 {DO}
 { {? __T_SUM.STRONA='+'
   || f[1]:='Ogólem wydatki, diety i ryczałty:'@
   |? __T_SUM.STRONA='-'
   || f[1]:='Ogółem zaliczki:'@
   |? __T_SUM.STRONA='X'
   || f[1]:='Razem:'@
   ?};
   f[2]:=__T_SUM.WAL;
   f[3]:=__T_SUM.KWOTA;~~}
   {IF: __T_SUM.STRONA<>'X'}{FONT: 'T8G'}{SPACE: 'C'}
   {IF: lineleft() <> 0 & lineleft()<=4}{PAGE}{FI}
   {SUBSTITUTE: 'LINIA0'}{FI}
   {IF: __T_SUM.STRONA='+'}
    { f[1]:='   w tym wydatki do rozliczenia z pracownikiem, diety i ryczałty:'@;
      f[3]:=__T_SUM.KWOTAP;~~}
    {FONT: 'T8G'}{SPACE: 'C'}
    {IF: lineleft() <> 0 & lineleft()<=4}{PAGE}{FI}
    {SUBSTITUTE: 'LINIA0'}
   {ELIF: __T_SUM.STRONA='X'}
    { f[1]:='Razem - do wypłaty/zwrotu:'@; f[3]:=__T_SUM.KWOTAP; ~~}
    {FONT: 'T8GB'}{SPACE: 'C'}
    {IF: lineleft() <> 0 & lineleft()<=4}{PAGE}{FI}
    {SUBSTITUTE: 'LINIA0'}
   {ELIF: __T_SUM.STRONA='-'}
    { f[1]:='   w tym wypłacone zaliczki:'@; f[3]:=__T_SUM.KWOTAP; ~~}
    {FONT: 'T8G'}{SPACE: 'C'}
    {IF: lineleft() <> 0 & lineleft()<=4}{PAGE}{FI}
    {SUBSTITUTE: 'LINIA0'}
   {FI}
 {OD}
{FI}
{ sekcja:='STOP';~~}
{~~}
{COMMENT}--------------------------END MACRO WYDRUK-----------------{COMMENT-}
{COMMENT}------------------------------MACRO STATUS-----------------{COMMENT-}
{IF: dobieg=1}
{LINE: 1}
{FONT: 'T8GB'}
{<1 {? czy_dob || 'SZCZEGÓŁY OBIEGU '+{? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur' || 'DOKUMENTU:' || 'DELEGACJI:' ?} || 'Szczegóły wniosku:' ?}}
{LINE: 1}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; prn1:=prn2:='PRNS';~~}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FOR: 'EDOKLOG'}
{INDEX: 'DISP'}
{PREFIX: EDOKUM.ref()}
{DO}
{ szc[1]:=$EDOKLOG.DATA;
  szc[2]:=$EDOKLOG.TIME;
  USERS.cntx_psh(); OSOBA.cntx_psh();
  szc[3]:=EDOKLOG.USERS().OSOBA().NAZWISKO+' '+EDOKLOG.USERS().OSOBA().PIERWSZE+' ('+EDOKLOG.USERS().KOD+')';
  USERS.cntx_pop(); OSOBA.cntx_pop();
  szc[4]:=EDOKLOG.B_PRELS;
  szc[5]:=EDOKLOG.OPIS;
  szc[6]:=EDOKLOG.UW_KR;
  ~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI}
{COMMENT} ---- END podsumowanie ---- {COMMENT-}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}{FOOTER-}
{FONT: 'T8G'}{SPACE: 'C'}
{IF: part_p=1 | part_d=1 | part_w=1 | part_z=1}
{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
{FI}
{IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
{ENTIRE}
{LINE:4}
{<10 '..................................'} {<110 '...............................'}
{<10 'Nazwisko imię i podpis'@} {<110 'Nazwisko imię i podpis'@}
{<10 'delegowanego'@} {<110 'osoby sprawdzającej'@}
{ENTIRE-}




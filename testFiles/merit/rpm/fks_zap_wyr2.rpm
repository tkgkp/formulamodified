:!UTF-8
{TITLE:B. Obroty dla wybranych wyróżników}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN: SLO.cntx_psh(); SLO.win_dict('ONE'); SLO.win_sel('ONE');
        KS.cntx_psh(); KS.index('NAZ'); KS.prefix(SSTALE.AR);
        BUD.cntx_psh(); BUD.index('KS'); SLUAPPL.cntx_psh(); SLUAPPL.prefix();
        SLU.cntx_psh(); SLU.prefix();
        PRN:=obj_new(@.CLASS.PRINT,9); PRN.sl_frame();
        w:=obj_new(4);  vp:=1; w3:=w4:=0; w1:=w11:=w12:=w13:=w14:=mdc:='';wh:='';
        sum1:=sum2:=licznik:=0; fun1:=fun2:=funs:=''; konto:=''; lm:=0;
        wyrod:=wyrdo:=slowyr:=indtym:=''; is_zap:=0; dat_od:=dat_do:=date(0,0,0);
        synt:=SSTALE.AO().ROK().SYNT; separ:=SSTALE.AO().ROK().SEP;
        pozycja:=(synt+{? separ<>',' || 1 || 0 ?});
        WYROZN.OPBUDKON:=0; ks_naz:=ks_sym:=wyr_kon:=''
}
{END: SLO.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop(); KS.cntx_pop(); BUD.cntx_pop();
      &ks_naz; &ks_sym; &wyr_kon; &mdc; &lm; &wh; &pozycja;
      obj_del(w); obj_del(PRN); &wyrod; &wyrdo; &slowyr; &indtym; &is_zap; &w1;
      &dat_od; &dat_do; WYR.ndx_drop(); &vp; &sum1; &sum2; &w3; &w4; &licznik;
      &fun1; &fun2; &funs; &konto; &w11; &w12; &w13; &w14; &synt; &separ}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_zap_wyr2.rpm
Utworzony:    2003-06-10 [AMK] Artur Makos
==================================================
Zawartosc: Obroty dla wybranych wyroznikow.
{COMMENT-}
{ WYROZN.MASKA:=WYROZN.ANALSYNT:='A'; WYROZN.MASKA1:=35*'?';
  WYROZN.WYROD:=SLO.ref();
  SLO.cntx_psh(); SLO.last(); WYROZN.WYRDO:=SLO.ref(); SLO.cntx_pop();
  OKRO_F.cntx_psh();
  WYROZN.DATA_OD:=WYROZN.DATA_DO:=date(0,0,0);
  OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
  {? OKRO_F.first() & OKRO_F.next() || WYROZN.DATA_OD:=OKRO_F.POCZ ?};
  SSTALE.AO(); {? OKRO_F.KON=date(0,0,0) || OKRO_F.last(); OKRO_F.prev() ?};
  WYROZN.DATA_DO:=OKRO_F.KON;
  OKRO_F.cntx_pop();
  WYROZN.win_edit('ZAKR_WYR');
  dat_od:=WYROZN.DATA_OD; dat_do:=WYROZN.DATA_DO;~~}
{EXIT: ~WYROZN.edit("_spr:=exec('spr_zwyr','!fks_dks_zwro');
                    {? _spr='' || _spr:=exec('spr_zdat','!fks_dks_zwro') ?}; _spr")}
{ SLO.cntx_psh(); SLU.cntx_psh(); SLUAPPL.cntx_psh();
  SLU.index('NAZ'); SLU.prefix(TAB_S.NAZ);
  SLUAPPL.index('NAZ'); SLUAPPL.prefix('F');
  {? SLU.first() & SLU.NAZ=TAB_S.NAZ & SLUAPPL.find_key(SLU.NAZ)
  || SLO.index('SL'); SLO.prefix(SLU.ref);
     {? WYROZN.WYROD=null & SLO.first() || WYROZN.WYROD:=SLO.ref ?};
     {? WYROZN.WYRDO=null & SLO.last() || WYROZN.WYRDO:=SLO.ref ?};
     {? WYROZN.WYROD<>null || wyrod:=WYROZN.WYROD().KOD+' - '+WYROZN.WYROD().TR ?};
     {? WYROZN.WYRDO<>null || wyrdo:=WYROZN.WYRDO().KOD+' - '+WYROZN.WYRDO().TR ?};
     slowyr:=SLU.NAZ
  ?};
  SLO.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop();
  WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=WYROZN.MASKA1;
  {? +WYDRUKI.MASKA < WYDRUKIN.DLKON
  || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
  ?};
  fun1:="{? -WYROZN.MASKA='a'
         || _tr:=WYR.KOD+' - '+WYR.TR;
            {? _ & WYROZN.OPBUDKON || STR.split(_tr); STR.line(80) || _tr ?}
         || {? -WYROZN.ANALSYNT='a' || WYR.KON+' '+WYR.KS_NAZ || WYR.KS_SYM+' '+WYR.KS_NAZ ?}
         ?}
         ";
  fun2:="{? -WYROZN.MASKA<>'a'
         || _tr:=WYR.KOD+' - '+WYR.TR;
            {? _ & WYROZN.OPBUDKON || STR.split(_tr); STR.line(80) || _tr ?}
         || {? -WYROZN.ANALSYNT='a' || WYR.KON+' '+WYR.KS_NAZ || WYR.KS_SYM+' '+WYR.KS_NAZ ?}
         ?}";
  funs:="{? -WYROZN.MASKA='a'
         || WYR.KOD
         || {? -WYROZN.ANALSYNT='a' || WYR.KON || WYR.KS_SYM ?}
         ?}";
  {? -WYROZN.MASKA='a'
  || PRN.add("w[1]",86,35*' '+'Wyróżnik'@);
     PRN.add("w[2]",81,38*' '+'Konto'@);
     {? -WYROZN.ANALSYNT='a'
     || indtym:=WYR.ndx_tmp('',1,'NAZ',,0,'NAZ',,0,'KOD',,0,'KON',,0)
     || indtym:=WYR.ndx_tmp('',1,'NAZ',,0,'NAZ',,0,'KOD',,0,'KS_SYM',,0)
     ?}
  || PRN.add("w[1]",97,36*' '+'Konto'@);
     PRN.add("w[2]",75,32*' '+'Wyróżnik'@);
     {? -WYROZN.ANALSYNT='a'
     || indtym:=WYR.ndx_tmp('',1,'NAZ',,0,'NAZ',,0,'KON',,0,'KOD',,0)
     || indtym:=WYR.ndx_tmp('',1,'NAZ',,0,'NAZ',,0,'KS_SYM',,0,'KOD',,0)
     ?}
  ?};
  PRN.add("w[3]",16,'    Kwota Wn',1);
  PRN.add("w[4]",16,'    Kwota Ma',1);
  w[1]:=w[2]:=w[3]:=w[4]:='';~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='OBROTY DLA WYBRANYCH WYRÓŻNIKÓW'@;~~}{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}
{LINE: 1}
{SPACE: 'B'}{IF: page=1}
{<1 'PARAMETRY WYDRUKU:'@}
{<1 'Słownik wyróżnika: %1'@[slowyr]}{>{PRN.width()} 'Okres obrotowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AO().ROK().NAZ]}
{<1 'Od wyróżnika: %1'@[wyrod]}{>{PRN.width()}
 {? OPERATOR.DEPT
    || 'Jednostka księgowa %1'@[OPERATOR.DEPT().OD]
    || 'Wszystkie jednostki księgowe'@
 ?}}
{<1 'Do wyróżnika: %1'@[wyrdo]}{>{PRN.width()} 'Od daty: %1 do daty: %2'@[$WYROZN.DATA_OD,$WYROZN.DATA_DO]}
{<1 {? WYROZN.ANALSYNT='A' || 'Konta: analityczne'@ || 'Konta: syntetyczne'@ ?}}{>{PRN.width()}  'Maska kont: %1'@[WYDRUKI.MASKA]}
{FI}
{FONT: 'R'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{PRN.bar()}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{PRN.lbar()}{ vp:=1;~~}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-BUDOWA------------------{COMMENT-}
{MACRO: 'BUDOWA'}
{IF: KS.find_key(ks_naz,ks_sym)}
{ w11:=w[1]; w12:=w[2]; w13:=w[3]; w14:=w[4]; w[1]:=w[2]:=w[3]:=w[4]:='';
  BUD.prefix(KS.ref());
  konto:=pozycja-wyr_kon;~~}
{IF: BUD.first()}
{FOR: 'BUD'}
{INDEX: 'KS'}
{PREFIX: KS.ref()}
{DO}
{ BUD.SLU().SLU();
  SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(SLU.ref(),(BUD.SLU().SLU().DL+konto));~~}
{IF: SLO.first()}
{ {? -WYROZN.MASKA='a'
  || w[2]:=pozycja*' '+form(SLO.KOD,8)+' '+SLO.TR;
     w[1]:=STR.line(80); {? wh=w[1] || w[1]:='' || wh:=w[1] ?}
  || w[1]:=pozycja*' '+form(SLO.KOD,8)+' '+SLO.TR;
     w[2]:=STR.line(80)
  ?};
~~}
{SUBSTITUTE: 'LINIA'}{ vp:=0;~~}
{FI}
{ SLO.cntx_pop();
  konto:=(BUD.SLU().SLU().DL+{? separ<>',' || 1 || 0 ?})-konto;~~}
{OD}
{FI}
{ w[1]:=w11; w[2]:=w12; w[3]:=w13; w[4]:=w14;~~}
{FI}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{FOR: 'WYR'}
{INDEX: indtym}
{PREFIX: SLU.NAZ, SLU.NAZ}
{DO}
{ w[1]:=fun1(1); w[2]:=fun2(1); ~~}
{ ks_naz:=WYR.KS_NAZ; ks_sym:=WYR.KS_SYM; wyr_kon:=WYR.KON;~~}
{IF: WYR.DTW>=WYROZN.DATA_OD & WYR.DTW<=WYROZN.DATA_DO &
     WYR.KOD>=WYROZN.WYROD().KOD & WYR.KOD<=WYROZN.WYRDO().KOD & exec('mask','konto',WYR.KON)}
{ is_zap:=1; sum1+=WYR.WN$2; sum2+=WYR.MA$2;~~}
{ADD: w3;#;WYR.WN}{ADD: w4;#;WYR.MA}
{FI}
{TOTAL: fun1()+fun2()}
{IF: sum('w3')$2<>0 | sum('w4')$2<>0}
{IF: lineleft=1}{PAGE}{FI}
{ENTIRE}
{ {? |w[1]=|w1 || w[1]:=''; licznik:=1 || w1:=|w[1] ?};
  w[3]:=form(sum('w3'),,2,' ,'); w[4]:=form(sum('w4'),,2,' ,'); ~~}
{IF: vp=0 & |w[1]<>''}{PRN.bar()}{FI}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}{ vp:=0;~~}
{IF: {? -WYROZN.MASKA='a' || |w[2]<>'' || |w[1]<>'' ?} & -WYROZN.ANALSYNT='a' & WYROZN.OPBUDKON}{SUBSTITUTE: 'BUDOWA'}{FI}
{FI}
{TOTAL: funs()}
{IF: (sum('w3')$2<>0 | sum('w4')$2<>0) & licznik=1}
{ENTIRE}
{ w[1]:='Suma: '+grp_fld(); w[2]:=''; w[3]:=form(sum('w3'),,2,' ,'); w[4]:=form(sum('w4'),,2,' ,'); licznik:=0;~~}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}{ vp:=0;~~}
{FI}
{OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft=1 | lineleft=2}{PAGE}{FI}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{IF: is_zap}
{ w[1]:='SUMA:'; w[3]:=form(sum1, , 2, ' ,'); w[4]:=form(sum2, , 2, ' ,'); w[2]:='';~~}
{ENTIRE}
{IF: vp=0}{PRN.bar()}{FI}
{SUBSTITUTE: 'LINIA'}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{ELSE}
{PRN.lbar()}
{<1>{PRN.width()} 'BRAK REKORDÓW SPEŁNIAJĄCYCH KRYTERIA!'@}
{FI}
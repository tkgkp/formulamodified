:!UTF-8
{TITLE: Raport miesięczny pracowniczy}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','PRH','prn1','prn2','vp','lmt','rsep','h_rat','tresc','pb','Format','Ok','Nr','Data');
   VAR_DEL.delete('NormWej','NormWyj','NormPrac','PracWej','PracWyj','PracPrac','PracPrzer','Nadmiar','Niedobor');
   VAR_DEL.delete('Wydzial','Uwagi','Ppsf','Nieob','nieob','var');

   _TAB:=exec('tab_prac','!prc_czp_wydr');
   {? _TAB.first()
   || tresc:='w_rapmpr';
      PAR_WYDR.TITLE:='Raport miesięczny pracowniczy'@;

      {? rep_export()
      || PRH:=obj_new(@.CLASS.PRINT,11);
         PRH.add("''",40,'Jednostka organizacyjna'@);
         PRH.add("''",40,'Współpracownik'@);
         PRH.add("''",14,'Karta'@)
      || PRH:=obj_new(@.CLASS.PRINT,9);
         PRH.add("Nr+=1",3,'Lp.'@,1,,,,,',,\'9.\'')
      ?};
      PRH.add("''",10,'Data'@);
      PRH.add("''",21,'Norma'@,,,,,,,,3);
      PRH.add("''",31,'Pracował'@,,,,,,,,4);
      PRH.add("''",8,'Niedobór'@);
      PRH.add("''",7,'Nadmiar'@);
      PRH.add("''",15,'Wyjaśnienia'@);
      PRH.add("''",16,'Praca poza siedzibą firmy'@);
      PRH.s_frame();

      {? rep_export()
      || PRN:=obj_new(@.CLASS.PRINT,16);
         PRN.add("P.WYDZIAL().SYMBOL+' '+|UD_SKL.OPIS",40,'');
         PRN.add("'%1 %2, %3 %4'[P.OSOBA().NAZWISKO,OSOBA.PIERWSZE,'Nr teczki:'@,P.T]",40,'');
         PRN.add(
            "  R_KARHIS.index('PR_DATA');
               R_KARHIS.prefix(P.ref());
               {? R_KARHIS.last() || R_KARHIS.K().N || '' ?}
            ",40,''
         )
      || PRN:=obj_new(@.CLASS.PRINT,14);
         PRN.add("Nr+=1",3,'',1,,,,,',,\'9.\'')
      ?};
      PRN.add("Data$1",10,'',1);
      PRN.add("Format_godz(NormWej,NormPrac)",5,'od'@);
      PRN.add("Format_godz(NormWyj,NormPrac)",5,'do'@);
      PRN.add("Format(NormPrac)",5,'praca'@);
      PRN.add("Format_godz(PracWej,PracPrac)",5,'od'@);
      PRN.add("Format_godz(PracWyj,PracPrac)",5,'do'@);
      PRN.add("Format(PracPrac)",5,'praca'@);
      PRN.add("Format(PracPrzer)",7,'przerwy'@,1);
      PRN.add("Format(Niedobor)",8,'',1);
      PRN.add("Format(Nadmiar)",7,'',1);
      PRN.add("Uwagi",15,'');
      PRN.add("{? Format(NormPrac)<>'' || form(Ppsf) || '' ?}",16,'',1);
      PRN.s_frame();

      prn1:='PRH';
      prn2:='PRN';
      rsep:=PAR_WYDR.SEP;
      Wydzial:=lmt:=pb:=Ok:=Nr:=lmt:=0;
      vp:=h_rat:=1;
      Data:=date(0,0,0);
      Format_godz:=
         "  {? _a='00:00' & _b='00:00' || '' || _a ?}
         ";
      Format:=
         "  {? _a='00:00' || '' || _a ?}
         ";
      PracWej:=PracWyj:=PracPrac:=PracPrzer:=NormWej:=NormWyj:=NormPrac:=Nadmiar:=Niedobor:=Uwagi:=Ppsf:=Nieob:=''
   ?};
   params_set('P',_TAB);
   nieob:=" _naz_n:='';
           N.cntx_psh;
           N.index('NIEOBECN');
           N.prefix('N',P.ref);
           {? N.first()
           || {!|?
                   {? _a>=N.OD & _a<=N.DO
                       || _naz_n+=N.NB().RT
                   ?};
                   N.next
              !}
           ?};
          N.cntx_pop();_naz_n";
   var:=exec('uprawnieniawrap','pkd','nieobecnościach'@,'PKD_EZK_OPNN','PKD_EZK_ORNN');

   R_PRACDN.cntx_psh();
   R_WZCZ.cntx_psh();
   R_WZCZ.index('R_WZCZ');
   R_REJ_WW.cntx_psh()
}
{END:
   VAR_DEL.delete('PRN','PRH','prn1','prn2','vp','lmt','rsep','h_rat','tresc','pb','Format','Ok','Nr','Data');
   VAR_DEL.delete('NormWej','NormWyj','NormPrac','PracWej','PracWyj','PracPrac','PracPrzer','Nadmiar','Niedobor');
   VAR_DEL.delete('Wydzial','Uwagi','Ppsf','Nieob','nieob','var');
   R_PRACDN.cntx_pop();
   R_WZCZ.cntx_pop();
   R_REJ_WW.cntx_pop()
}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: w_rapmpr.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{FONT: 'M'}
{SPACE: 'B'}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~}
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~}
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'%1 %2'['Za okres:',date(VAR_EDIT.ROK,VAR_EDIT.MSC,1)$8]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: ~rep_export()}
   {<{lmt+1}'Współpracownik:'} {P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE}         {'Nr teczki:'@} {|P.T}
   {<{lmt+1}'Karta:'@} {R_KARHIS.index('PR_DATA'); R_KARHIS.prefix(P.ref); {? R_KARHIS.last || R_KARHIS.K().N || '' ?}}
   {<{lmt+1}'Jednostka organizacyjna:'} {P.WYDZIAL().SYMBOL}{IF: +(|P.WYDZIAL().OPIS)} ({P.WYDZIAL().OPIS}){FI}
{FI}
{  prn1:='PRH'; prn2:='PRN'; ~~}
{SUBSTITUTE: 'TABHEAD2'}
{  prn1:='PRN'; prn2:='PRH'; ~~}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'TABHEAD2'}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn1+'.ini_head()'; ($(_f))()}
   {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
   {WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
   {DO}
      {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {OD}
   { _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn2+'.ini_head()'; ($(_f))()}
   {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   {WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
   {DO}
      {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
   {OD}
{MACRO-}
{MACRO: tresc}
{  Nr:=0;
   Data:=date(VAR_EDIT.ROK,VAR_EDIT.MSC,1); ~~
}
{WHILE: Data<=date(VAR_EDIT.ROK,VAR_EDIT.MSC,0)}
{DO}
   {  R_KARHIS.index('PR_DATA');
      R_KARHIS.prefix(P.ref());
      Karta:=form({? R_KARHIS.last() || R_KARHIS.K().N || '' ?}, -13);
      Pracownik:=form(P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,40);
      Teczka:=form(P.T,-9);
      Uwagi:='';
      R_WZCZ.prefix(P.name(),P.ref());
      NormWej:=NormWyj:=NormPrac:=5*' ';
      _Kalend:=__KAL.set_cal(P.KAL,Data~1);
      {? _Kalend
      || {? __KAL.get_day(Data)
         || NormWej:=form(KAL_DEF.POCZATEK,5);
            NormWyj:=form(KAL_DEF.KONIEC,5);
            NormPrac:=form(KAL_DEF.CZAS,5)
         || Uwagi:='brak definicji kalendarza'@
         ?}
      || Uwagi:='nieokreślony kalendarz'@
      ?};
      {? +var=0 || Nieob:=(18+nieob(Data)) || Nieob:='' ?};
      Uwagi+=Nieob;
      R_PRACDN.prefix(P.ref());
      R_REJ_WW.index('R_REJ_WX');
      R_REJ_WW.prefix(P.ref(),Data);
      PracWej:=PracWyj:=PracPrac:=PracPrzer:=Nadmiar:=Niedobor:=5*' ';
      {? R_REJ_WW.first() || PracWej:=form(R_REJ_WW.GD,5) ?};
      {? R_REJ_WW.last() || PracWyj:=form(R_REJ_WW.GD,5) ?};
      {? R_PRACDN.find_key(Data)
      || PracPrzer:=form(R_PRACDN.WP,5);
         PracPrac:=form(R_PRACDN.CP,5);
         {? Nieob<>'' || Niedobor:='' || Niedobor:=form(R_PRACDN.NB,5)?};
         Nadmiar:=form(R_PRACDN.CN,5)
      || {? Nieob<>'' || Niedobor:='' || Niedobor:=NormPrac ?}
      ?};
      Ppsf:=exec('wydr_p_dt','ppsf',P.ref(),Data); ~~
   }
   {  prn1:='PRN'; prn2:='PRH'; ~~}
   {SPACE: 'C'}
   {SUBSTITUTE: 'LINIA0'}
   {SPACE: 'B'}
   {Data+=1; ~~}
{OD}
{PAGE}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{IF:  VAR_EDIT.win_edit('PRDN_DAT');
      VAR_EDIT.ROK:=date()~1;
      VAR_EDIT.MSC:=date()~2;
      {? AREATIT.AREA='PRC_MCR'
      || VAR_EDIT.ROK:=A_OKRM.R;
         VAR_EDIT.MSC:=A_OKRM.M
      ?};
      VAR_EDIT.efld_opt('PRDN_DAT','mark=1',,'ROK');
      VAR_EDIT.efld_opt('PRDN_DAT','mark=1',,'MSC');
      ~VAR_EDIT.edit(
         "  _result:=__CHK.record(VAR_EDIT,,'ROK','MSC');
            {? _result='' & (VAR_EDIT.MSC<1 | VAR_EDIT.MSC>12)
            || FUN.emsg('Proszę podać porawną wartość w polu \"Miesiąc\".'@);
               _result:='MSC'
            ?};
            _result
         "
      )
}
   {EXIT}
{FI}
{  MASK.Use('R_PRACDN',VAR_EDIT.ROK,VAR_EDIT.MSC);
   MASK.Use('R_REJ_WW',VAR_EDIT.ROK,VAR_EDIT.MSC); ~~
}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRH.width())/2); ~~}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
:!UTF-8
{TITLE:3. Historia kalkulacji dla bieżącej karty}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   TKTL.cntx_psh();
   KKTL.cntx_psh();
   dalej:=0;
   PRN:=obj_new(@.CLASS.PRINT,12);
   PRN.s_frame();
   prn1:='PRN';
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=robocizna:=materialy:=cena:=0;
   "---Indeks tymczasowy na historie kalkulacji karty technologicznej.";
   ndx_tmp:=KKTL.ndx_tmp(,1,'NRK',,0,'DT',,0,'KNR',,0)
}
{END:
   TKTL.cntx_pop();
   KKTL.cntx_pop();
   &dalej;
   obj_del(PRN);
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;&robocizna;&materialy;&cena;
   KKTL.ndx_drop(ndx_tmp)
}
{COMMENT}
  Historia kalkulacji dla bieżącej karty.
  [SS] - 2001/03/19 - [7.41]
  [TS] - 2001/10/09 - [7.53] OWwSK003_Aneks1
  [MKO] - 2001/10/10 - [7.53] dodanie kolumn 'koszt rob.' i 'koszt mat' (Uwaga_61)
  [TS] - 2003/01/16 - [7.62] kolumna: Data waznosci
  Wywołanie: Karty technologiczne -> Kalkulacje TKW -> Drukuj
{COMMENT-}
{MACRO: 'functions'}
{COMMENT}
Zwraca wartosc numer rubryki zawierajacej informacje okreslone parametrm _a
{COMMENT-}
{  wart:="
   TKTL.cntx_psh();
   TKTL.clear();
   _res:=0;
   {? _<1
   || _res:=-1;
      FUN.error('Funkcja ''wart'' wydruku kkt_0003.RPM:\nniewłaściwa liczba parametrów.')
   ?};
   {? _res=0
   || KPOZK.index('KR');
      KPOZK.prefix(KKTL.ref(),_a);
      {? KPOZK.first()
      || _res:=KPOZK.WART
      || _res:=-1
      ?}
   || _res:=0
   ?};
   TKTL.cntx_pop();
   _res
";
~~}
{MACRO-}
{SUBSTITUTE: 'functions'}
{MACRO: 'przygotuj'}
{ robocizna:=wart(KKTL.NRK().TYP().CRUBROB);
  materialy:=wart(KKTL.NRK().TYP().CRUBMAT);
  cena:=wart(KKTL.NRK().TYP().CRUB); ~~}
{MACRO-}
{COMMENT}--Definicja wiersza wydruku kalkulacji.---------------{COMMENT-}
{  PRN.add("form(KKTL.KNR)",5,'Numer kalk.'@,1);
   PRN.add("KKTL.TKTLW().KTM().KTM+' - '+M.N",20,'Produkt'@);
   PRN.add("form(KKTL.DT)",10,'Data kalkulacji'@);
   PRN.add("form(KKTL.DT_D)",10,'Ceny na dzień'@);
   PRN.add("form(KKTL.DW)",10,'Data ważności'@);
   PRN.add("KKTL.OPC().NAZ",15,'Wariant'@);
   PRN.add("KKTL.MADE().KOD",10,'Sporządził'@);
   PRN.add("KKTL.ACCEPT().KOD",10,'Zatwierdz.'@);
   PRN.add("({? materialy<>-1
              || form(materialy,,2)
              || form('<brak>'@)
              ?})",12,'Koszt materiałów'@,1);
   PRN.add("({? robocizna<>-1
              || form(robocizna,,2)
              || form('<brak>'@)
              ?})",12,'Koszt robocizny'@,1);
   PRN.add("({? cena<>-1
              || form(cena,,2)
              || form('<brak>'@,-15)
              ?})",12,'Cena jednostkowa'@,1);
   PRN.add("exec('stan_opis','!tte_tec_ktkw',KKTL.STAN)",17,'Stan kalkulacji'@);
   PAR_WYDR.TITLE:='HISTORIA KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Karta technologiczna:'@} {<{lmt+25} TKTL.NRK}
{<{lmt+1} 'Wersja:'@} {<{lmt+25} TKTL.WER}
{<{lmt+1} 'Kalkulowana na:'@} {<{lmt+25} form(TKTL.XJM,,2)+' '+TKTL.JM().KOD}
{COMMENT}
{<{lmt+1} 'Produkt:'} {<{lmt+25} TKTL.KTM().KTM+' - '+TKTL.KTM().N}
{COMMENT-}
{<{lmt+1} 'Domyślny wariant kalk.:'@} {<{lmt+25} {? form(TKTL.DEF_OPCK().NAZ)<>''
                                                 || TKTL.DEF_OPCK().NAZ
                                                 || '<brak>'@
                                                 ?}
                                               }
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ KKTL.index(ndx_tmp); KKTL.prefix(TKTL.ref()); ~~}
{IF: KKTL.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
   {SUBSTITUTE:  'przygotuj'}
   {SUBSTITUTE: 'LINIA0'}
   { dalej:=KKTL.next(); ~~}
{OD}

:!UTF-8
{TITLE: Wydruk dokumentu kasowego}
{PRINTER: KINFO.W_PRN,,,,,'Q','B',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN: exec('initWARL','dok_fks_aut_dok',USRCONST.ROK,'KASA','S',0);
        exec('init','dok_fks_aut_dok','Informacje o błędach dekretacji raportów kasowych');
        EKSG.REF_ODD:=USRCONST.STANKAS().STANKAS().ODD;
        EKSG.STANKAS:=USRCONST.STANKAS;
        exec('set_fld','dok_fks_aut_dok','O','S'); exec('set_fld','dok_fks_aut_dok','N','S');
        pierwszy:=1;
        DEK_NAG.index('DEK_NAG'); DEK_POZ.index('DEK_POZ');
        adres1:=adres2:=''; dane:=0;
        PRN:=obj_new(@.CLASS.PRINT,1); PRN.sl_frame();
        PRN1:=obj_new(@.CLASS.PRINT,5); PRN1.sl_frame();
        PRN2:=obj_new(@.CLASS.PRINT,5); PRN2.sl_frame();
        PRN3:=obj_new(@.CLASS.PRINT,5); PRN3.sl_frame();
        w:=obj_new(11); {! _a:=1..11 |! w[_a]:='' !};
        color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=rsep:=0; vp:=1; separ:=ile_l_k:=licznik:=licznik1:=0;anul:=0;
        WYDRUKI.win_edit('KPKW'); WYDRUKI.PAR1:='N'; WYDRUKI.PAR5:=DOKUMENT.OPER().L_KOPII; WYDRUKI.PAR3:='N'; ob:='';
        obca:=DOKUMENT.WALUTA<>exec('wal_nar','kasa_wspolne'); countl1:=0; countl2:=1; lp:=1}
{END: exec('done','dok_fks_aut_dok');
      VAR_DEL.delete('SD_OB','PRN','PRN1','PRN2','PRN3','w','konto','kwoty','kwotyw','pierwszy'); &adres1; &adres2; &dane;&anul;
      &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &separ; &ile_l_k; &licznik; &licznik1; &ob; &countl1; &countl2; &lp
      }
{INCLUDE: wsp_pw.rpi}
{MACRO: 'DANE'}
{ adres1:=adres2:='';
  RS.index('RS'); RS.prefix();
"dla kwitariusza dane adresowe z pola ADR";
  {? DOKUMENT.TYP_DOK='KWT'
  || adres1:='ul. '+{? STANKAS.CZY_ZDEP='N' | STANKAS.SYM='DKW' || DOKUMENT.ADR || DOKUMENT.WPL().UL+', '+DOKUMENT.WPL().KP+' '+DOKUMENT.WPL().M ?};
     adres2:=''
  ||
     {? RS.find_key(DOKUMENT.DOTYCZY().WZ) & (_a:=RS.TAB; _a='SLO_OSOB' | _a='OS' | _a='KH')
     || _pre:=REF.WFIRM & exec('czy_tab_glob','#table',_a)=0;
        {? _a='KH'
        || KH.cntx_psh(); KH.index('KOD'); KH.prefix(2,DOKUMENT.DLA().KOD);
           {? KH.first()
           || {? var_press('DOM',KH)>0
               || _adr:={? +KH.DOM || ' '+KH.DOM || '' ?};
                  _adr+={? +KH.LOKAL || ' Lok. '+KH.LOKAL || '' ?}
               || _adr:=''
              ?};
              adres1:='ul. '+KH.UL+_adr;
              adres2:={? KH.POCZ*KH.MIASTO
                      || KH.POCZ
                      || KH.MIASTO+{? +KH.POCZ || ', '+KH.POCZ  || '' ?}
                      ?}
           ?};
           KH.cntx_pop()
        || _znal_os:=0;
           {? _a='OS'
           || OS.cntx_psh();
              OS.index('KOD'); OS.prefix(DOKUMENT.DLA().KOD);
              {? OS.first() || OS.OSOBA(); _znal_os:=1 ?};
              OS.cntx_pop()
           |? _a='SLO_OSOB'
           || SLO_OSOB.cntx_psh();
              SLO_OSOB.index('ID'); SLO_OSOB.prefix(RS.F_ZATR,{? ~_pre || #DOKUMENT.DLA().KOD || #(3-DOKUMENT.DLA().KOD) ?});
              {? SLO_OSOB.first() || SLO_OSOB.OSOBA(); _znal_os:=1 ?};
              SLO_OSOB.cntx_pop()
           ?};
           {? _znal_os
           || OS_ADRES.cntx_psh();
              OS_ADRES.index('RODZAJ'); OS_ADRES.prefix(OSOBA.ref(),'S');
              {? OS_ADRES.last()
              || adres1:='ul. '+OS_ADRES.ULICA+' '+OS_ADRES.DOM+
                         {? +OS_ADRES.LOKAL || ' m. '+OS_ADRES.LOKAL || '' ?};
                 adres2:={? OS_ADRES.POCZTA*OS_ADRES.MIASTO
                         || OS_ADRES.KOD+' '+OS_ADRES.POCZTA
                         || OS_ADRES.MIASTO+{? +OS_ADRES.POCZTA | +OS_ADRES.KOD
                                            || ', '+OS_ADRES.KOD+' '+OS_ADRES.POCZTA
                                            || ''
                                            ?}
                         ?}
              ?};
              OS_ADRES.cntx_pop()
           ?}
        ?}
     ?}
  ?};
  adres2:=|adres2;~~}
{MACRO-}
{EXIT: ~WYDRUKI.edit("{? WYDRUKI.PAR5<0
                      || FUN.info('Liczba kopii nie może być liczbą ujemną.'@); 'PAR5'
                      || ''
                      ?}")}
{ rsep:=PAR_WYDR.RSEP; ~~}
{COMMENT}------------------------------TAB BEGIN-----------------------{COMMENT-}
{MACRO: 'TABBEGIN'}
{  POZDOK.cntx_psh; POZDOK.index( 'DOKUMENT');POZDOK.prefix( DOKUMENT.ref);
   {? POZDOK.first & POZDOK.ZAKS='A' || anul:=1?}; POZDOK.cntx_pop;
   ob:=w[1]; w[1]:='Dowód '+{? DOKUMENT.PLUS_MIN='-' || 'wypłaty ' || 'wpłaty ' ?}
   +DOKUMENT.TYP_DOK+' nr '+$DOKUMENT.DOK_NUM+' z dnia '+$DOKUMENT.DATA;
   +{? anul=1 || ' - dokument anulowany!' || '' ?};
   w[1]:=form(w[1],-95);PRN.setcolor(color);~~}
{REP: PRN.fhbar()}
{PRN.ini_line()}
{REP: PRN.fline()}
{w[1]:=ob;PRN.ini_line();~~}
{MACRO-}
{COMMENT}--------------------------------HEADER------------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{ PAR_WYDR.TITLE:=''; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABBEGIN'}
{HEADER-}
{ PRN.add("w[1]",115,'Dowód '+{? DOKUMENT.PLUS_MIN='-' || 'wypłaty '@ || 'wpłaty '@ ?}+DOKUMENT.TYP_DOK+' nr '@+$DOKUMENT.DOK_NUM+' z dnia '@+$DOKUMENT.DATA);
  _dl:=98;
  {? WYDRUKI.PAR3='T' || _dl-=36 ?};
  PRN1.add("w[2]",_dl,{? DOKUMENT.PLUS_MIN='-' || 'Wypłata'@ || 'Wpłata'@ ?}+' tytułem'@);
  PRN1.add("w[3]",16,{? DOKUMENT.PLUS_MIN='-' || 'Ma'@ || 'Winien'@ ?}+' kasa ('@+KINFO.NAROD().KOD+')',1);
  PRN3.add("w[2]",_dl-17,{? DOKUMENT.PLUS_MIN='-' || 'Wypłata'@ || 'Wpłata'@ ?}+' tytułem'@);
  PRN3.add("w[3]",16,{? DOKUMENT.PLUS_MIN='-' || 'Ma'@ || 'Winien'@ ?}+' kasa ('@+KINFO.NAROD().KOD+')',1);
  PRN3.add("w[5]",16,{? DOKUMENT.PLUS_MIN='-' || 'Ma'@ || 'Winien'@ ?}+' kasa w walucie ('@+DOKUMENT.WALUTA().SYM+')',1,'#');
  {? WYDRUKI.PAR3='T'
  || PRN1.add("w[6]",35,{? DOKUMENT.PLUS_MIN='-' || 'Winien'@ || 'Ma'@ ?}+' konto'@);
     PRN3.add("w[6]",35,{? DOKUMENT.PLUS_MIN='-' || 'Winien'@ || 'Ma'@ ?}+' konto'@)
  ?};
  PRN2.add("w[7]",18,'',,'#');
  PRN2.add("w[8]",18,'',,'#');
  PRN2.add("w[9]",18,'',,'#');
  PRN2.add("w[10]",18,'',,'#');
  PRN2.add("w[11]",39,'',,'#');
~~}
{FONT: 'T8'}{SPACE: 'B'}
{COMMENT}==================================MACRO WYDRUK=================================={COMMENT-}
{MACRO: 'WYDRUK'}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8'}{SPACE: 'B'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}{ vp:=1;~~}
{FOOTER-}
{ countl2:=page();~~}
{COMMENT}----------------------------DANE ADRESOWE-----------------------{COMMENT-}
{SUBSTITUTE: 'DANE'}
{IF: lineleft()=1}{PAGE}{FI}
{ prn1:=prn2:='PRN'; vp:=1;~~}
{ w[1]:={? DOKUMENT.PLUS_MIN='-' || 'Komu: ' || 'Od kogo: '?}+DOKUMENT.DLA().KOD+'  '+DOKUMENT.NAZWA;
  separ:=rsep; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{ countl1:=lineleft();~~}
{IF: +adres1}
{IF: ((+adres1)+(+adres2)+2)<=115}
{ w[1]:=adres1+'  '+adres2;~~}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
{ w[1]:=adres1;~~}
{SUBSTITUTE: 'LINIA0'}
{ w[1]:=adres2;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{FI}
{IF: +DOKUMENT.OPIS}{w[1]:='Opis: '+DOKUMENT.OPIS;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ rsep:=separ;~~}
{COMMENT}----------------------------KWOTY I KONTA-----------------------{COMMENT-}
{ENTIRE}
{IF: (obca & (lineleft()=1 | lineleft()=2)) | (~obca & lineleft()=1)}{ vp:=1;~~}{PAGE}{FI}
{IF: vp}{ PRN1.setcolor(color);PRN.setcolor(color);~~}{REP: PRN.fjbar({?obca||PRN3||PRN1?})}{ELSE}{PRN.jbar({?obca||PRN3||PRN1?})}{FI}{ PRN1.setcolor('255:255:255'); PRN.setcolor('255:255:255'); ~~}
{ _min:=DOKUMENT.PLUS_MIN='-';
  w[2]:=((82-obca*19+(WYDRUKI.PAR3='N')*35)%2)*' '+{? _min || 'Wypłata' || 'Wpłata' ?}+' tytułem';
  w[3]:={? _min || 'Ma' || 'Winien' ?}+' kasa ('+KINFO.NAROD().KOD+')'+(_min*2)*' ';
  w[5]:={? _min || 'Ma kasa w     ' || 'Winien kasa w  ' ?}+'# walucie ('+DOKUMENT.WALUTA().SYM+')  ';
  w[6]:=5*' '+{? _min || 'Winien' || '   Ma' ?}+' konto';~~}
{_f:='PRN'+{?obca||'3'||'1'?}+'.ini_line()'; ($_f)();~~}
{WHILE: _f:='PRN'+{?obca||'3'||'1'?}+'.next()'; ($_f)()}
{DO}
   {REP: _f:='PRN'+{?obca||'3'||'1'?}+'.fline()'; ($_f)()}
{OD}
{ENTIRE-}{ prn1:=prn2:='PRN'+{?obca||'3'||'1'?};~~}
{IF: lineleft()=1}{ vp:=1;~~}{PAGE}{ELSE}{ vp:=0;~~}{FI}
{IF: rsep=0 & vp=0 & lineleft()<>0}{{?obca||PRN3.bar()||PRN1.bar()?}}{FI}
{FOR: 'POZDOK'}
{INDEX: 'DOKUMENT'}
{PREFIX: DOKUMENT.ref()}
{DO}
{ _k:=exec('konto_z_dek','dok_fks_aut_dok');
  w[2]:=POZDOK.POZOPER().NAZWA+' '+POZDOK.MF_FIKS;
  w[3]:=form({? DOKUMENT.PLUS_MIN=POZDOK.PLUS_MIN || POZDOK.KW_PLN || -1*POZDOK.KW_PLN ?},,2,' ,');
  w[4]:=DOKUMENT.WALUTA().SYM;
  w[5]:=form({? DOKUMENT.PLUS_MIN=POZDOK.PLUS_MIN || POZDOK.KW_WAL || -1*POZDOK.KW_WAL ?},,2,' ,');
  w[6]:={? var_pres('konto')<=0 || w[6]:=_k || '' ?};
~~}{ {? lineleft()=0 || vp:=1 ?};~~}
{IF: rsep & lineleft()=1}{ vp:=1;~~}{PAGE}{FI}
{IF: (rsep & (lineleft()=0 | lineleft()=1)) | ~rsep & lineleft()=0}{ prn1:='PRN'; prn2:='PRN'+{?obca||'3'||'1'?};~~}{FI}
{SUBSTITUTE: 'LINIA02'}{ licznik:=1; separ:=rsep; rsep:=0; prn1:='PRN'+{?obca||'3'||'1'?}; vp:=0;~~}
{IF: ile_l_k>=1 & var_pres('konto')=type_of(obj_new(2))}
{WHILE: licznik<=ile_l_k}
{DO}
{ {! _a:=2..6 |! w[_a]:='' !};
  w[6]:=konto[licznik];
  w[3]:='';
  w[5]:='';
  licznik+=1; ~~}
{IF: lineleft()=0}{ prn1:='PRN'; prn2:='PRN'+{?obca||'3'||'1'?};~~}{FI}
{SUBSTITUTE: 'LINIA02'}{ prn1:='PRN1';~~}
{OD}
{FI}
{ rsep:=separ;~~}
{OD}
{IF: lineleft()=1}{PAGE}{FI}{IF: lineleft()=0}{ vp:=1;~~}{FI}
{ separ:=rsep; rsep:=1; {! _a:=2..6 |! w[_a]:='' !};
  w[2]:='   RAZEM';
  w[3]:=form(DOKUMENT.KW_PLN,,2,' ,');
  w[5]:=form(DOKUMENT.KW_WAL,,2,' ,');
~~}
{IF: (lineleft()=0 | lineleft()=1)}{ prn1:='PRN'; prn2:='PRN'+{?obca||'3'||'1'?};~~}{FI}
{SUBSTITUTE: 'LINIA02'}
{COMMENT}----------------------------KWOTA SLOWNIE-----------------------{COMMENT-}
{ _kwota:={? DOKUMENT.KW_WAL<>0 || DOKUMENT.KW_WAL || DOKUMENT.KW_PLN ?};
  _slownie:=STR.kwota_sł(_kwota);
  _gr:='';
  {? frac(_kwota)>0
  ||  _gr:=$frac(_kwota); {? +_gr=3 || _gr:=_gr+'0' || _gr ?};
      _gr:=2-_gr
  ?};
  _slownie:={? _slownie*'złotych'>0
            || (((_slownie*'złotych')-1)+_slownie)+' '
            |? _slownie*'złoty'>0
            || (((_slownie*'złoty')-1)+_slownie)+' '
            |? _slownie*'złote'>0
            || (((_slownie*'złote')-1)+_slownie)+' '
            || ''
            ?}+
            {? +_gr || _gr+'/100' || '' ?};
  STR.split(_slownie);
  w[1]:='Słownie ('+DOKUMENT.WALUTA().SYM+'):  '+STR.line(97);
  prn2:='PRN'; ~~}
{IF: lineleft()=1  | lineleft()=2}{ vp:=1;~~}{PAGE}{FI}{ prn1:={? lineleft=0 | lineleft()=1 || 'PRN' || 'PRN'+{?obca||'3'||'1'?} ?}; ~~}
{SUBSTITUTE: 'LINIA02'}{  rsep:=0; prn1:='PRN';~~}
{WHILE: STR.next()<>0}
{DO}
{  prn1:=prn2:='PRN';~~}
{ w[1]:=STR.line(115);~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{ rsep:=separ;~~}
{IF: lineleft<5 & lineleft()<>0}{ vp:=1;~~}{PAGE}{FI}
{FOOTER}{FOOTER-}
{ separ:=rsep; rsep:=1; prn1:='PRN'; prn2:='PRN2';~~}
{ w[7]:='       Wystawił'; w[8]:='       Sprawdził'; w[9]:='      Zatwierdził'; w[10]:='     Raport kasowy'; w[11]:=16*' '+'Kwotę powyższą';~~}
{SUBSTITUTE: 'LINIA02'}
{ prn1:='PRN2';w[7]:=w[8]:=w[9]:=''; rsep:=0; w[10]:='     Nr: '+$DOKUMENT.RAPORT().NUM_RAP;
  w[11]:={? DOKUMENT.PLUS_MIN='+' || 20*' '+'otrzymałem'@ || '       wypłaciłem            otrzymałem'@ ?};~~}
{SUBSTITUTE: 'LINIA0'}{ w[10]:='     Poz.: '@+$DOKUMENT.LP; w[11]:='';~~}
{SUBSTITUTE: 'LINIA0'}{ w[10]:='   ';~~}
{SUBSTITUTE: 'LINIA0'}
{PRN2.lbar()}{ rsep:=separ;~~}
{MACRO-}
{COMMENT}--------------------------------SRODEK------------------------------------{COMMENT-}
{MACRO: 'SRODEK'}
{ obca:=DOKUMENT.WALUTA<>exec('wal_nar','kasa_wspolne'); licznik1:=0;~~}
{WHILE: licznik1<=WYDRUKI.PAR5}
{DO}
{SUBSTITUTE: 'WYDRUK'}
{IF: WYDRUKI.PAR1='T'}
{IF: lineleft<>0 & licznik1<>WYDRUKI.PAR5}{PAGE}{FI}
{ELSE}
{IF: licznik1<>WYDRUKI.PAR5}
{IF: lineleft>=4}
{ vp:=1;~~}
{IF: {? countl2=page() || countl1:=countl1-lineleft()+5; countl1>lineleft() & lineleft()<>0 || 0  ?}}
{PAGE}
{ELSE}
{LINE: 1}
{FONT: 'T8B'}{SPACE: 'B'}{ prn1:='PRN';~~}
{SUBSTITUTE: 'TABBEGIN'}{FONT: 'T8'}{SPACE: 'B'}
{FI}
{ELIF: lineleft<>0}{PAGE}
{FI}
{FI}
{FI}
{ licznik1+=1;~~}
{OD}
{MACRO-}
{COMMENT}==================================WYDRUK=================================={COMMENT-}
{IF: var_pres('TT_DOK')>100 }
   {FOR: TT_DOK}
   {DO}
      {DOKUMENT.seek(TT_DOK.REF,DOKUMENT.name());~~}
      {SUBSTITUTE: 'SRODEK'}
      {PAGE}
   {OD}
{ELSE}
   {SUBSTITUTE: 'SRODEK'}
{FI}

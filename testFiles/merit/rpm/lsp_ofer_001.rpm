:!UTF-8
{TITLE:A. Wydruk oferty}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   10,10,10,10,0,1}
{BEGIN:
   POLA.NAZ_WYDR:='Oferta '+exec('str_to_pth','#string',OFE.SYMW)+'.pdf';
   ofer_001:="
      VAR_DEL.delete('PRN','PRN2','PRNS','PRNS2','NAG','w','p1','s','k','p','na');
      VAR_DEL.delete('rozdzial');
      VAR_DEL.delete('grofe','grofe1');
      VAR_DEL.delete('color','prn1','prn2','prn_tmp');
      VAR_DEL.delete('prn1a','head');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('il_lin','lacz');
      VAR_DEL.delete('zakoncz');
      VAR_DEL.delete('oddzial');
      VAR_DEL.delete('__wal');
      VAR_DEL.delete('wydr_naz','wyjscie','wydr_edi','plat','cenab','czywal','dr_rab');
      VAR_DEL.delete('color_oo','color_bw','color_bg','color_gw','color_wo','color_ww','color_frame_g','color_frame_w','color_gg','color_glg','color_g','color_w','color_wg','color_dgg');
      VAR_DEL.delete('ofe_date_head','ofe_date_val');
      VAR_DEL.delete('__empty_char')
      ";
   ofer_001();
   exec('czytaj','#stalesys',,XINFO); KSTE.get();
   _ok:=exec('ofe_wydr','!lsp_ofe_dofe','ofe');
   VAR_DEL.delete('PRN','PRN2','PRNS','PRNS2','NAG','w','p1','s','k','p','na','szer');

   color_oo:='245:120:10,245:120:10';
   color_bw:='0:0:0,255:255:255';
   color_bg:='0:0:0,247:247:247';
   color_gw:='102:102:102,255:255:255';
   color_wo:='255:255:255,245:120:10';
   color_ww:='255:255:255,255:255:255';
   color_frame:='\'204:204:204\'';
   color_frame_g:='204:204:204';
   color_frame_w:='255:255:255';
   color_gg:='102:102:102,247:247:247';
   color_glg:='196:196:196,247:247:247';
   color_g:='247:247:247';
   color_w:='255:255:255';
   color_wg:='255:255:255,114:114:114';
   color_dgg:='114:114:114,114:114:114';

   __empty_char:={? exec('get','#params', 100236, 2)='N' || ^160 || ' ' ?};

   PRNS:=obj_new(@.CLASS.PRINT,10,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   PRNS.framecol(color_frame_w);
   PRNS2:=obj_new(@.CLASS.PRINT,10,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   PRNS2.framecol(color_frame_w);
   NAG:=obj_new(@.CLASS.PRINT,2,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   NAG.framecol(color_frame_g);

   VAR_DEL.delete('w','p','s','s2','na');
   w:=obj_new(10);
   p1:=obj_new(8);
   s:=obj_new(4);
   s2:=obj_new(4);
   k:=obj_new(9);  {! _i..9 |! k[_i]:=0  !}; "--szerokosc kolumn--";
   p:=obj_new(9);  {! _i..9 |! p[_i]:='' !}; "--tymczasowa do szacowania szerokosci kolumn--";
   na:=obj_new(10);
   dokl:=3; "--dokladnosc ilosci--";
   ddokl:=0; "--najwieksza drukowana dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";
   cdokl:=0; "--najwieksza drukowana dokladnosc ceny--";
   rdokl:=2; "--najwieksza drukowana dokladnosc ceny--";

   rozdzial:='';
   OFP.index('OFE_P'); OFP.prefix(OFE.ref);
   grofe:={? OFP.first() || OFP.OFEGR || null ?};
   grofe1:=0;
   color:=prn1:=prn2:=prn_tmp:='';
   prn1a:='PRN';
   lm:=ll:=vp:=lmt:=rsep:=0;
   il_lin:=lacz:=0;
   zakoncz:=1;
   oddzial:=KSTE.NAZ<>'';

   wyjscie:=1;

   wydr_naz:='ofer_001';
   wydr_edi:={? OFE.WAL=INFO.NAROD || 'OFERA001' || 'OFERB001' ?};

   plat:=cenab:=czywal:=0;
   TAZ.RAB_TYP:=OFE.RAB_TYP;

   WYDRUKIL.CHKBOX01:=0;
   WYDRUKIL.RADIOB_1:=OFE.CB='T';
   WYDRUKIL.CHKBOX02:=OFE.WAL<>INFO.NAROD;
   PARAM_W.JEZYK:=null;
   WYDRUKIL.CHKBOX03:=1;
   dr_rab:=TAZ.RAB_TYP='W';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ||
      PARAM_W.JEZYK:=WYDRUKIL.JEZYK
   ?};
   _jezyk_:=PARAM_W.JEZYK;
   _djezyk:=exec('kh_jezyk','tlumaczenia',OFE.KH);
   PARAM_W.JEZYK:={? _djezyk || _djezyk || _jezyk_ ?};
   PARAM_W.JEZYK_KH:={? _djezyk & _djezyk=PARAM_W.JEZYK || 'T' || 'N' ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? wydr_edi='OFERA001'
   || {? TAZ.RAB_TYP<>'W'
      || WYDRUKIL.efld_opt(wydr_edi,'enable=1',,'CHKBOX03')
      || WYDRUKIL.efld_opt(wydr_edi,'enable=0',,'CHKBOX03')
      ?}
   ?};
   {? _ok=1 & WYDRUKIL.edit()
   ||
      WYDRUKIL.JEZYK:={? _djezyk & PARAM_W.JEZYK=_djezyk || _jezyk_ || PARAM_W.JEZYK ?};
      {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
      {? WYDRUKIL.put()
      || wyjscie:=0;
         plat:=WYDRUKIL.CHKBOX01;
         cenab:=WYDRUKIL.RADIOB_1;
         czywal:=WYDRUKIL.CHKBOX02;
         dr_rab:={? WYDRUKIL.CHKBOX03 |TAZ.RAB_TYP='W' || 1 || WYDRUKIL.CHKBOX03 ?}
      ?}
   ?};
   {? OFE.WAL=INFO.NAROD || czywal:=0 ?};
   {? czywal=1
   || __wal:=OFE.WAL().KOD
   || SLO.cntx_psh();
      _wyn:=exec('bl_nwal','ustawienia'); SLO.prefix(); __wal:={? SLO.seek(_wyn) || SLO.KOD || '' ?};
      SLO.cntx_pop()
   ?};
   head:=PAR_WYDR.HEAD;
   PAR_WYDR.HEAD:=0;
   exec('st_licz_wz','ceny','OFE')
;~~}
{END:
   PAR_WYDR.HEAD:=head;
   undefine();
   ofer_001(); VAR_DEL.delete('ofer_001')
;~~}
{EXIT: wyjscie=1}
{EXIT:
   OFP.cntx_psh();
   OFP.index('OFE_P');
   OFP.prefix(OFE.ref());
   {? ~OFP.first()
   ||
      FUN.info('Oferta nie ma pozycji.'@);
      wyjscie:=1
   ?};
   OFP.cntx_pop();
   wyjscie=1
}
{DEFINEFONT: 'TM=Inter,75,Q,,0,0'}
{DEFINEFONT: 'TMB=Inter,75,R,,0,0'}
{DEFINEFONT: 'TF=Inter,75,F,,0,0'}
{DEFINEFONT: 'IE=Inter,150,E,,0,0'}
{DEFINEFONT: 'IF=Inter,150,F,,0,0'}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_pw2.rpi}
{INCLUDE: wsp_lic.rpi}
{INCLUDE: wsp_strony_new.rpi}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
"--szerokosc kolumn--";
   ddokl:=0;
   VAR_DEL.delete('X_Xw1');
   X_Xw1:=tab_tmp(1
      ,'POZ','INTEGER',''
      ,'M','STRING[160]',''
      ,'IL','REAL',''
      ,'JM','STRING[10]',''
      ,'C','REAL',''
      ,'RAB','REAL',''
      ,'CR','REAL',''
      ,'WAR','REAL',''
      ,'WB','REAL',''
      ,'REF','STRING[16]','');

   _dr_rab:=0;
   _war:=_warb:=0;
   OFP.index('OFE_P'); OFP.prefix(OFE.ref());
   {? OFP.first
   || {!
      |? _pop:=OFP.M().DOKL;
         {? _pop>0 & _pop>ddokl || ddokl:=_pop ?};
         _dokl_c:=OFP.M().DOKL_S;
         _pop:=_dokl_c;
         {? exec('get','#params', 100235, 2)='T' & _pop>0 & _pop>cdokl || cdokl:=_pop ?};
         {? ~exec('czyrabp','ceny',TAZ.RAB_TYP) || rdokl:=cdokl ?};
         X_Xw1.clear();
         X_Xw1.blank();
         X_Xw1.POZ:=OFP.POZ;
         X_Xw1.M:=OFP.M().KTM+' - '+M.N;
         X_Xw1.IL:=OFP.IL;
         X_Xw1.JM:=M.J().KOD;
         _vat:=#((OFP.SV().KOD*'%')+OFP.SV().KOD-1)/100;
         _cena:={? czywal=1 || OFP.CWAL || OFP.C ?};
         {? OFE.CB='T' || _cena:=(_cena/(1+_vat))$_dokl_c ?};
         {? cenab || _cena:=_cena+(_cena*_vat)$_dokl_c ?};
         X_Xw1.C:=_cena;
         {? exec('czyrabp','ceny',TAZ.RAB_TYP)
         || _rabat:=exec('rab_proc','ceny',OFP.RAB,TAZ.RAB,TAZ.RAB_TYP);
            _rabat1:=1-(_rabat)/100
         ||
            _kwil:=OFP.IL;
            _nagwsp:=-TAZ.RAB/100;
            _kwpoz:=-
               {? cenab & OFE.CB='N' || (OFP.RABK*(1+_vat))$_dokl_c
               |? cenab=0 & OFE.CB='T' || (OFP.RABK/(1+_vat))$_dokl_c
               || OFP.RABK
               ?};
            _kwrab:=
               {? TAZ.RAB_TYP='W'
               || _kwpoz+(_nagwsp*((_cena*_kwil)$2))$_dokl_c
               || ((_kwpoz+(_nagwsp*_cena)$_dokl_c)*_kwil)$_dokl_c
               ?};
            _kwrabj:=
               {? TAZ.RAB_TYP='W'
               || {? _kwil || (_kwpoz/_kwil)$_dokl_c ?}+(_nagwsp*_cena)$_dokl_c
               || _kwpoz+(_nagwsp*_cena)$_dokl_c
               ?}
         ?};
         X_Xw1.RAB:={? 'PS'*TAZ.RAB_TYP || _rabat |? TAZ.RAB_TYP='W' || _kwrab || _kwrabj ?};
         _dr_rab+=X_Xw1.RAB;
         {? exec('czyrabp','ceny',TAZ.RAB_TYP)
         || X_Xw1.CR:=(_cena-(_cena*_rabat)/100) $_dokl_c
         ?};
         X_Xw1.WAR:=(X_Xw1.C*X_Xw1.IL)$2;
         X_Xw1.WB:=
            {? cenab=1
            || X_Xw1.WAR
            || (X_Xw1.WAR*(1+_vat))$2
            ?};
         X_Xw1.REF:=$OFP.ref();
         X_Xw1.add(1);
         _war+=X_Xw1.WAR;
         _warb+=X_Xw1.WB;
         OFP.next
      !}
   ?};
   dr_rab:=dr_rab & _dr_rab;

   _cen_dl:=_cenr_dl:=5;
   X_Xw1.clear();
   {? X_Xw1.first()
   || {!
      |?
         {? _cen_dl<+form(X_Xw1.C,,cdokl,' ,') || _cen_dl:=+form(X_Xw1.C,,cdokl,' ,') ?};
         {? _cenr_dl<+form(X_Xw1.CR,,cdokl,' ,') || _cenr_dl:=+form(X_Xw1.CR,,cdokl,' ,') ?};
         X_Xw1.next()
      !}
   ?};

   lmt:=charleft;
   k[1]:=5;                                     p[1]:='form(X_Xw1.IL,,ddokl,\' ,\')';
   k[2]:=_cen_dl;                               p[2]:='{? dr_rab || form(X_Xw1.C,,cdokl,\' ,\') || \'\' ?}';
   _dl:=+form(_war,,2,' ,');
   k[3]:={? _dl<=7 || 7 || _dl ?};              p[3]:='form(X_Xw1.WAR,,2,\' ,\')';
   k[4]:=4;                                     p[4]:='X_Xw1.POZ';
   k[5]:=0;                                     p[5]:='X_Xw1.M';
   k[6]:=6;                                     p[6]:='{? dr_rab || form(X_Xw1.RAB,,rdokl,\' ,\') || \'\' ?}';
   k[7]:=_cenr_dl;                              p[7]:='form(X_Xw1.CR,,cdokl,\' ,\')';
   k[8]:=2;                                     p[8]:='X_Xw1.JM';
   {? PARAM_W.JEZYK().KOD='ANG'
   ||
      k[1]:=8;    'Ilość';
      k[2]:=9;    'Cena przed rabatem';
      k[6]:=8;    'Rabat';
      k[8]:=4;    'jm'
   ?};
   _dl:=+form(_warb,,2,' ,');
   {? TAZ.RAB_TYP='W' || k[7]:=0 ?};
   {? cenab=1
   || k[9]:=0; p[9] :=''
   || k[9]:={? _dl<=7 || 7 || _dl ?};           p[9] :='form(X_Xw1.WB,,2,\' ,\')'
   ?};

   _len:=obj_len(k);
   _buf:=obj_new(_len); {! _i.._len |! _buf[_i]:=0 !};
   _max:=obj_new(_len); {! _i.._len |! _max[_i]:=0 !};

   X_Xw1.clear();
   {? X_Xw1.first()
   || {!
      |? {! _i.._len
         |! _war:=($p[_i])();
            _typ:=type_of(_war);
            {? _typ=1 || _w:=(+form(_war));
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=2 || _w:=(+_war);
                         {? _max[_i]<_w || _max[_i]:=_w; _buf[_i]:=_w ?}
            |? _typ=4 || _buf[_i]:=10
            |? _typ=5 || _buf[_i]:=5
            ?}
         !};
         X_Xw1.next()
      !}
   ?};

   _ods:=-3; _all:=0;
   {! _i.._len
   |! {? k[_i]<0         || k[_i]:=-k[_i]
      |? k[_i]<=_buf[_i] || k[_i]:=_buf[_i]
      ?};
      _all+=k[_i]; _ods+=3
   !};
   _print:="
      VAR_DEL.delete('PRN');
      PRN:=obj_new(@.CLASS.PRINT,10,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
      PRN.framecol(color_frame_g);
':: tr: L.p.';
      PRN.add(\"w[1]\",k[4],exec('tr','tlumaczenia','1000180'),1);
':: tr: Materiał/Usługa';
      PRN.add(\"form(w[3])\",k[5],exec('tr','tlumaczenia','1000181'));
':: tr: Ilość';
      PRN.add(\"form(w[4],,ddokl)\",k[1],exec('tr','tlumaczenia','1000183'),1);
':: tr: jm';
      PRN.add(\"form(w[5])\",k[8],exec('tr','tlumaczenia','1000184'),1);
      {? dr_rab
      ||
':: tr: Cena netto przed rabatem/Cena brutto przed rabatem';
         PRN.add(\"form(w[9],,cdokl)\",k[2],{? cenab=1 || exec('tr','tlumaczenia','1000185') || exec('tr','tlumaczenia','1000186') ?},1)
      ?};
      {? TAZ.RAB_TYP='W'
      ||
':: tr: Wartość netto przed rabatem/Wartość brutto przed rabatem';
         PRN.add(\"form(w[6],,2)\",k[3],{? cenab=1 || exec('tr','tlumaczenia','1000198') || exec('tr','tlumaczenia','1000197') ?},1);
':: tr: Kwota rabatu';
         PRN.add(\"form(w[10],,rdokl)\",k[6],exec('tr','tlumaczenia','1000199'),1)
      ||
         {? dr_rab
         ||
':: tr: % rab./Kwota rabatu';
            PRN.add(\"form(w[10],,rdokl)\",k[6],{? exec('czyrabp','ceny',TAZ.RAB_TYP) || exec('tr','tlumaczenia','1000187') || exec('tr','tlumaczenia','1000199') ?},1);
':: tr: Cena netto po rabacie/Cena brutto po rabacie';
            PRN.add(\"form(w[6],,cdokl)\",k[7],{? cenab=1 || exec('tr','tlumaczenia','1000193') || exec('tr','tlumaczenia','1000188') ?},1)
         ||
':: tr: Cena netto po rabacie/Cena brutto po rabacie';
            PRN.add(\"form(w[6],,cdokl)\",k[2],{? cenab=1 || exec('tr','tlumaczenia','1000193') || exec('tr','tlumaczenia','1000188') ?},1)
         ?}
      ?};
':: tr: Wartość netto/Wartość brutto';
      PRN.add(\"form(w[7],,2)\",k[3],{? cenab=1 || exec('tr','tlumaczenia','1000192') || exec('tr','tlumaczenia','1000189') ?},1);
      {? cenab=0 || PRN.add(\"form(w[8],,2)\",k[9],exec('tr','tlumaczenia','1000192'),1) ?}
   ";

   k[5]:=1; _print();
   k[5]+=charleft-PRN.width();

   obj_del(_buf); obj_del(_max);

   _white:='\'%1\''[color_bw];
   _print();
':: tr: Razem w/Oferta';
   PRNS.add("exec('tr','tlumaczenia','1000115')+' '+exec('tr','tlumaczenia','1000131')+' '+__wal+': '+exec('tr','tlumaczenia','4000101')",k[5]+k[4]+k[1]+k[8]+k[2]+(4*3),,1,,$_white);
   {? TAZ.RAB_TYP='W'
   || PRNS.add("''",k[3]);
      PRNS.add("''",k[6])
   |? dr_rab
   || PRNS.add("''",k[6]);
      PRNS.add("''",k[7])
   ?};
   PRNS.add("form(s[2],,2)",k[3],,1);
   {? cenab=0 || PRNS.add("form(s[3],,2)",k[9],,1) ?};

':: tr: Razem w/Bez rozdziału';
   PRNS2.add("exec('tr','tlumaczenia','1000115')+' '+exec('tr','tlumaczenia','1000131')+' '+__wal+': '+{? rozdzial='' || exec('tr','tlumaczenia','4000102') || rozdzial ?}",k[5]+k[4]+k[1]+k[8]+k[2]+(4*3),,1,,$_white);
   {? TAZ.RAB_TYP='W'
   || PRNS2.add("''",k[3]);
      PRNS2.add("''",k[6])
   |? dr_rab
   || PRNS2.add("''",k[6]);
      PRNS2.add("''",k[7])
   ?};
   PRNS2.add("form(s2[2],,2)",k[3],,1);
   {? cenab=0 || PRNS2.add("form(s2[3],,2)",k[9],,1) ?}
;~~}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
   _nag_wc1:=charleft()/2;
   _nag_wc2:=charleft()-_nag_wc1;
   NAG.add("na[1]",_nag_wc1,'');
   NAG.add("na[2]",_nag_wc2,'');

   lmt:=int((charleft()-PRN.width())/2);

   PAR_WYDR.SYM:=OFE.SYMW;
   PAR_WYDR.TITLE:=exec('tr','tlumaczenia','4000101');
   PAR_WYDR.TYPE:='OFE';

   ofe_date_head:=exec('tr','tlumaczenia','4000106')+';'+exec('tr','tlumaczenia','4000103')+';';
   ofe_date_val:=(gsub(form(OFE.DU,,3),'/','-')+';')+(gsub(form(OFE.TW,,3),'/','-')+';');

   prn1:='PRN'; prn2:='PRNS'
;~~}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'clean_footer'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEADF'}
{LINE}
{COMMENT}   strony transakcji                                        {COMMENT-}
{ __STRTAB:='OFE'; __STRODB:='OFE.ODB'; __STRLIN:=''; __STRDAT:=OFE.DU;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{COMMENT}   forma platnosci                                          {COMMENT-}
{IF: OFE.PL<>null & plat=1}
   {
':: tr: Forma płatności:';
   _tlumacz:={? PARAM_W.JEZYK=null || '' || exec('trans_slo','tlumaczenia',OFE.PL,PARAM_W.JEZYK) ?};
   fp_tr:=exec('tr','tlumaczenia','1000121')+' ';
   fp_war:=
      {? PARAM_W.JEZYK<>null & _tlumacz<>''
      || _tlumacz
      || OFE.PL().TR
      ?};
   inf_head:=fp_tr+';';
   inf_val:=fp_war+';';
   ~~}
{COMMENT}informacje dodatkowe: formy platnosci {COMMENT-}
   {ENTIRE}
   {SUBSTITUTE: 'panel_informacji'}
   {ENTIRE-}
{FI}
{COMMENT}   Nagłówek tabeli                                          {COMMENT-}
{  prn1:='PRN';~~}
{FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEADF'}
   {IF: zakoncz}
      {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
      { prn1a == prn1; ~~}
      {SUBSTITUTE: 'TABHEAD'}
      { prn1a == prn1; ~~}
   {FI}
{HEADER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
   rsep:=PAR_WYDR.RSEP;
   w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:=w[8]:=0;
   p1[1]:=p1[2]:=p1[3]:=p1[4]:=p1[5]:=p1[6]:=p1[7]:=p1[8]:='';
   s[1]:=s[2]:=s[3]:=s[4]:=s2[1]:=s2[2]:=s2[3]:=s2[4]:=0
;~~}
{ lacz:=0; ~~}
{FOR:'OFP'}{INDEX: 'OFE_P'}{PREFIX: OFE.ref}
{DO}
   {IF: grofe<>OFP.OFEGR}
{COMMENT}   podsumowanie rozdzialu                                   {COMMENT-}
      {FONT: exec('font','param_wydr','poz1')}{SPACE: exec('space','param_wydr')}
      {
         grofe1:=1;
         rsep:=1;
         grofe:=OFP.OFEGR;
         prn2:='PRNS2';

         _f:=prn2+'.ini_line()'; _v:=($(_f))();
         _f:=prn2+'.length()'; il_lin:=($(_f))() + 2
      ;~~}
      {IF: lineleft <= il_lin}
         { prn_tmp:=prn1; prn1:=prn2; ~~}
         {SUBSTITUTE: 'LINIAF'}{PAGE}
         { prn1:=prn_tmp; ~~}
      {FI}
      {FONT: exec('font','param_wydr','razem')}
      {PRNS2.setcolor(color_g);~~}
      {SUBSTITUTE: 'LINIA02'}
      {REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
      {FONT: exec('font','param_wydr','poz')}
      {PRNS2.setcolor(color_w);~~}
      {
         s2[1]:=s2[2]:=s2[3]:=0;
         lacz:=1
      ;~~}
   {FI}
   {FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
   {
      prn1:='PRN';
      rsep:=PAR_WYDR.RSEP;
      _dokl_c:=OFP.M().DOKL_S;
      _vat:=#((OFP.SV().KOD*'%')+OFP.SV().KOD-1)/100;
      _cena:={? czywal=1 || OFP.CWAL || OFP.C ?};
      {? cenab || _cena:=_cena+(_cena*_vat) ?};
      {? OFE.CB='T' || _cena:=(_cena/(1+_vat))$_dokl_c ?};
      {? exec('czyrabp','ceny',TAZ.RAB_TYP)
      || _rabat:=exec('rab_proc','ceny',OFP.RAB,TAZ.RAB,TAZ.RAB_TYP);
         _rabat1:=1-(_rabat)/100
      ||
         _kwil:=OFP.IL;
         _nagwsp:=-TAZ.RAB/100;
         _kwpoz:=-
            {? cenab & OFE.CB='N' || (OFP.RABK*(1+_vat))$_dokl_c
            |? cenab=0 & OFE.CB='T' || (OFP.RABK/(1+_vat))$_dokl_c
            || OFP.RABK
            ?};
         _kwrab:=
            {? TAZ.RAB_TYP='W'
            || _kwpoz+(_nagwsp*((_cena*_kwil)$2))$_dokl_c
            || ((_kwpoz+(_nagwsp*_cena)$_dokl_c)*_kwil)$_dokl_c
            ?};
         _kwrabj:=
            {? TAZ.RAB_TYP='W'
            || {? _kwil || (_kwpoz/_kwil)$_dokl_c ?}+(_nagwsp*_cena)$_dokl_c
            || _kwpoz+(_nagwsp*_cena)$_dokl_c
            ?}
      ?};
      w[1]+=1;
      w[2]:=OFP.OFEGR().KOD;
':: tr: materiał';
      _tlumacz:='';
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_m','tlumaczenia',OFP.M,PARAM_W.JEZYK)
      ?};
      {? _tlumacz='' || _tlumacz:=OFP.M().N ?};
      w[3]:=OFP.M().KTM+' '+_tlumacz;
      w[4]:=OFP.IL;
':: tr: jm';
      _tlumacz:='';
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_jm','tlumaczenia',OFP.M().J,PARAM_W.JEZYK)
      ?};
      {? _tlumacz='' || _tlumacz:=OFP.M().J().KOD ?};
      w[5]:=_tlumacz;

      w[6]:=
         {? TAZ.RAB_TYP='W'
         ||
            (_cena*OFP.IL)$2
         ||
            {? exec('czyrabp','ceny',TAZ.RAB_TYP)
            ||
               _cena*_rabat1$OFP.M().DOKL_S
            ||
               _cena+_kwrabj$OFP.M().DOKL_S
            ?}
         ?};

      exec('ae_ofpww','ceny_dok',,czywal);
      s[2]+=w[7]:={? cenab=1 || OFP.WB || OFP.WN ?};
      s2[2]+=w[7]:={? cenab=1 || OFP.WB || OFP.WN ?};
      s[3]+=w[8]:={? cenab=0 || OFP.WB ?};
      s2[3]+=w[8]:={? cenab=0 || OFP.WB ?};
      w[9]:=_cena;
      w[10]:=
         {? TAZ.RAB_TYP='W'
         ||
            -_kwrab
         ||
            {? exec('czyrabp','ceny',TAZ.RAB_TYP) || _rabat || w[9]-w[6] ?}
         ?};
      rozdzial:=OFP.OFEGR().KOD;
      rozdzial+={? rozdzial='' | OFP.OFEGR().NAZ='' || '' || ' - '+OFP.OFEGR().NAZ ?};

      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))() + 2
   ;~~}
   {IF: lineleft <= il_lin}
      {SUBSTITUTE: 'LINIAF'}{PAGE}
   {FI}
   {IF: lacz}
      { prn1:='PRNS2'; prn2:='PRN'; ~~}
      {SUBSTITUTE: 'LINIA02'}
      { lacz:=0; ~~}
   {ELSE}
      {SUBSTITUTE: 'LINIA03'}
   {FI}
{OD}
{IF: lineleft < 3}
   {SUBSTITUTE: 'LINIAF'}
   {IF: lineleft > 0}{PAGE}{FI}
{FI}
{FONT: exec('font','param_wydr','poz1')}{SPACE: exec('space','param_wydr')}
{COMMENT}   podsumowanie ostatniego rozdzialu                        {COMMENT-}
{IF: grofe1=0}
   {  lacz:=1; ~~}
{ELSE}
   {
      rsep:=1;
      grofe:=OFP.OFEGR;
      prn1:='PRN'; prn2:='PRNS2'
   ;~~}
   {FONT: exec('font','param_wydr','razem')}
   {PRNS2.setcolor(color_g);~~}
   {SUBSTITUTE: 'LINIA02'}
   {REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
   {FONT: exec('font','param_wydr','poz')}
   {PRNS2.setcolor(color_w);~~}
   { lacz:=0; ~~}
   {IF: lineleft < 3}
      { prn1:='PRNS2'; ~~}
      {SUBSTITUTE: 'LINIAF'}
      {IF: lineleft > 0}{PAGE}{FI}
      { lacz:=1; ~~}
   {FI}
{FI}
{COMMENT}   podsumowanie oferty                                      {COMMENT-}
{FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
{
   {? lacz = 1
   ||
      rsep:=1; prn1:='PRN'; prn2:='PRNS'
   ||
      rsep:=1; prn1:='PRNS2'; prn2:='PRNS'
   ?}
;~~}
{FONT: exec('font','param_wydr','razem')}
{PRNS.setcolor(color_g);~~}
{SUBSTITUTE: 'LINIA02'}
{REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
{FONT: exec('font','param_wydr','poz')}
{PRNS.setcolor(color_w);~~}
{ prn1:=prn2; ~~}

{ zakoncz:=0; ~~}
{LINE}
{COMMENT}   uwagi                                                    {COMMENT-}
{FONT: exec('font','param_wydr','uwagi')}
{<1}{
max_T8:=charleft;
VAR_DEL.delete('Text');
Text:=STR.split(form(OFE.U))
;~~}
{WHILE: +(Text:=STR.line(max_T8-4-2))}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {IF: +Text>=(max_T8-4-8)}
      {JUSTIFY}{<2>{max_T8-5}Text}
   {ELSE}
      {<2Text}
   {FI}
{OD}
{COMMENT}   opis                                                     {COMMENT-}
{FONT: exec('font','param_wydr','opis')}{SPACE: exec('space','param_wydr')}
{ __memo:=OFE.memo_get('r','OPIS'); text:='';~~}
{IF: __memo.is_open}
   {WHILE: text:=fread(__memo); text<>'\n'}
   {DO}
      {FIRST}
        {COLOR: color_gw}
        {<3}{exec('tr','tlumaczenia','4000107')}
        {<3}{charleft()*'─'}
        {COLOR: color_bw}
      {FIRST-}
      {<{lmt+1}}{
      max_T8:=charleft;
      STR.split(text);
      Text:='';
      sep:=0
      ;~~}
      {WHILE: STR.next()}
      {DO}
         { word:=STR.get_word(); sep+=1;~~}
         {IF: +Text+(+word)>=(max_T8-4-8)}
            {<{lmt+3}}{JUSTIFY}{<{lmt+3}>{max_T8-8}Text}
            { Text:=word; sep:=1;~~}
         {ELSE}
            { Text+={? sep>1 || ' ' || '' ?}+word;~~}
         {FI}
      {OD}
      {IF: +Text}
         {<{lmt+3}}{Text}
      {FI}
   {OD}
   {CENTER}
{FI}
{ VAR_DEL.delete('__memo','text','Text');~~}
{COMMENT}   podpisy                                                  {COMMENT-}
{FONT: exec('font','param_wydr','podpisy')}{SPACE: exec('space','param_wydr')}
{LINE: 3}
{
VAR_DEL.delete('PRN');
prn1:='PRN';
PRN:=obj_new(@.CLASS.PRINT,2,__empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
PRN.framecol(color_frame_g);
PRN.add("",charleft-50-3,'');
PRN.add("",50,OFE.HAN().NAZ);
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=30*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
':: tr: Ofertę sporządził';
PRN.HEAD[2]:=exec('tr','tlumaczenia','4000104');
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
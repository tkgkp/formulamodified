:!UTF-8
{TITLE:Dane ubezpieczeniowe}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__param','__edit');
   tresc:='pdk_wubezpieczenia';
   PAR_WYDR.TITLE:='Dane ubezpieczeniowe'@;
   par:=obj_new(7);
   par[6]:=0;
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','danych ubezpieczeniowych'@,'PKD_DOS_PPDU','PKD_DOS_PRDU');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || __param:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,
         "  {? cur_tab().OD>cur_tab().DO & cur_tab().DO<>#0
            || FUN.emsg('Data początku okresu nie może być większa od daty końcowej'@); 0
            || 1
            ?}
         ",
         date(0,0,0),'Data od'@,,
         date(0,0,0),'Data do'@
      );
      {? par[6]:=__param.size()
      || par[1]:=obj_new(@.CLASS.PRINT,{? rep_export() || 11 || 7 ?});
         {? rep_export()
         || par[1].add("P_INFO.OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("P_INFO.OSOBA().PIERWSZE+' '+P_INFO.OSOBA().DRUGIE",41,'Imiona'@);
            par[1].add("P_INFO.OSOBA().PESEL",11,'PESEL'@)
         ?};
         par[1].add("P_INFO.OD$1",10,'Od dnia'@);
         par[1].add("P_INFO.KC().NAZWA",29,'Fundusz Ochrony Zdrowia'@);
         par[1].add("{? (_d:=P_INFO.DTUM)<>#0 || _d$1 || '' ?}",10,'Data umowy'@);
         par[1].add(
            "  {? (_k:=4+P_INFO.TTUB().KOD)='' || '????' || _k ?}+' '+
               {? (_k:=1+P_INFO.PREM().KOD)='' || '?' || _k ?}+' '+
               {? (_k:=1+P_INFO.STNP().KOD)='' || '?' || _k ?}+' '+
               {? (_k:=2+P_INFO.NZPR().KOD)='' || '??' || _k ?}
            ",13,'Tytuł ubezpieczenia'@
         );
         par[1].add(
            "  _d:=P_INFO.BLOKADA;
               _k:=1+P_INFO.BLWN().KOD;
               {? _d<>#0 | _k<>''
               || {? rep_export()
                  || {? _k='' || '?' || _k ?}
                  || {? _k='' || '?' || _k ?}+' '+{? _d<>#0 || _d$1 || '' ?}
                  ?}
               || ''
               ?}
            ",15,'Blokada składek'@,1
         );
         {? rep_export()
         || par[1].add(
               " _d:=P_INFO.BLOKADA;
                  {? _d<>#0
                  || _d$1
                  || ''
                  ?}
            ",15,'Data blokady składek'@
            )
         ?};
         par[1].add(
            "  _k:=P_INFO.KW_WSK;
               _r:=P_INFO.ROK_WSK;
               {? _k | _r
               || {? _k || form(_k,,2,'9,') || '?,??' ?}
               || ''
               ?}
            ",17,'Kwota zwiększenia podstawy'@,1
         );
         par[1].add(
            "  _k:=P_INFO.KW_WSK;
               _r:=P_INFO.ROK_WSK;
               {? _k | _r
               || {? _r || form(_r,-4,,'99') || '????' ?}
               || ''
               ?}
            ",15,'Zwiększenie podstawy w roku'@,1
         );
         par[1].s_frame();

         par[2]:=tab_tmp(,'REF','INTEGER','Osoba');
         prn1:='par[1]';
         lmt:=rsep:=par[3]:=0;
         vp:=b_rat:=h_rat:=par[4]:=1;
         par[7]:=exec('ref_firma','ustawienia');
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__param','__edit')
}
{COMMENT}OLD: wubezp.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_osop.rpi{COMMENT-}
{EXIT: ~par[6]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: od %1 do %2 '@[__param.OD$1,__param.DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}{<{ceil(lmt*b_rat)+1}}{form(par[3]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[6]:=1; ~~ }
{MACRO-}
{MACRO: 'dane_ub'}
{IF: ~par[6]}
   {SUBSTITUTE: 'pracownik'}
   {REP: par[1].fhbar(lmt)}
{FI}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[2].add();
      par[6]:=0;
      ~~
   }
   {ENTIRE}
   {REP: '{FOR: P_INFO}{INDEX: \'OD\'}'+
          {? __param.OD=#0 & __param.DO=#0
             || '{PREFIX: par[7],P.OSOBA}'
             || '{FROM: par[7],P.OSOBA,__param.OD}'+
                '{TO: par[7],P.OSOBA'+{? __param.DO=#0 || '' || ',__param.DO' ?}+'}'
          ?}+'{DO}{SUBSTITUTE: \'dane_ub\'}{OD}'}
   {IF: par[6]}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
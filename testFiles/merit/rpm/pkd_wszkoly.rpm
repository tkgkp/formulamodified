:!UTF-8
{TITLE:Ukończone szkoły}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='pkd_wszkoly';
   PAR_WYDR.TITLE:='Ukończone szkoły'@;
   P.cntx_psh();
   par:=obj_new(7);
   par[6]:=exec('uprawnieniawrap','pkd','wykształceniu'@,'PKD_DOS_PPWY','PKD_DOS_PRWY');
   {? +par[6]
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[par[6]])
   || par[1]:=obj_new(@.CLASS.PRINT,{? rep_export() || 11 || 3 ?});
      {? rep_export()
      ||   par[1].add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
           par[1].add("P.OSOBA().PIERWSZE+' '+P.OSOBA().DRUGIE",41,'Imiona'@);
           par[1].add("P.OSOBA().PESEL",11,'PESEL'@)
      ?};
      par[1].add("OS_SZKOL.OD$1",10,'Od dnia'@);
      par[1].add("OS_SZKOL.DO$1",10,'Do dnia'@);
      {? ~rep_export()
      || par[1].add("par[7]()",96,'Nazwa i rodzaj szkoły'@)
      || par[1].add("OS_SZKOL.SZKOLA().NAZWA",50,'Nazwa szkoły'@);
         par[1].add("OS_SZKOL.RODZAJ().NAZWA",50,'Rodzaj szkoły'@);
         par[1].add("OS_SZKOL.WYDZIAL",50,'Wydział'@);
         par[1].add("OS_SZKOL.KIERUNEK",50,'Kierunek'@);
         par[1].add("OS_SZKOL.SPEC",50,'Specjalizacja'@)
      ?};
      par[1].s_frame();
      par[7]:=
         "  _szkola:=OS_SZKOL.SZKOLA().NAZWA+'; '+OS_SZKOL.RODZAJ().NAZWA+'; '+OS_SZKOL.WYDZIAL+'; '+
            OS_SZKOL.KIERUNEK+'; '+OS_SZKOL.SPEC+'; ';
            {! |? _szkola*'; ; ' |! _szkola:=STR.gsub(_szkola,'; ; ','; ') !};
            _szkola-2
         ";

      par[2]:=tab_tmp(,'REF','INTEGER','Osoba');

      prn1:='par[1]';
      par[3]:=par[5]:=lmt:=0;
      par[4]:=vp:=rsep:=b_rat:=h_rat:=1;

      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat')
}
{COMMENT}OLD: wszkoly.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_osop.rpi{COMMENT-}
{EXIT: +par[6]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par[5]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[3]:=1; ~~ }
{MACRO-}
{MACRO: 'szk_dane'}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[2].add();
      par[3]:=0; ~~
   }
   {ENTIRE}
   {FOR: OS_SZKOL}{INDEX: 'OD'}{PREFIX: P.OSOBA}{DO}
   {FIRST}
      {SUBSTITUTE: 'pracownik'}
      {REP: par[1].fhbar(lmt)}
   {FIRST-}
      {SUBSTITUTE: 'szk_dane'}
   {OD}
   {IF: par[3]}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}

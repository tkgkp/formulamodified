:!UTF-8
{TITLE:Nieobecności pracowników}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PAR_WYDR.TITLE:='Nieobecności pracowników'@;
   R.cntx_psh();
   R.index('RUBLP');
   R.prefix();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep');
   par:=obj_new(9);
   par[4]:='';
   par[7]:=exec('pkd_wnieobecn','personel',,'nieobecn',PAR_WYDR.TITLE,3);
   {? par[7].size()
   || {? (par[4]:=1-par[7].memo_txt(,1,'LISTA')-1)<>''
      || _rok:='';
         {? par[7].REAL || _rok:='substr(to_string(N.OD),1,4) ROK, ' ?};

         echo('Trwa odczyt informacji o nieobecnościach...');
         _tab:=sql('select '+_rok+'OSOBA.NAZWISKO, OSOBA.PIERWSZE, P.T, R.LP, N.NK, N.OD, N.DO '+
            'from P join N using(P.REFERENCE,N.P) join OSOBA join R '+
            'where P.REFERENCE in (select :_a.SQL from :_a) and N.KOR=\'N\' and  R.RN in (:_b)'+
            {? par[7].OD<>#0 || ' and to_date(:_c)<=N.DO' || '' ?}+
            {? par[7].DO<>#0 || ' and N.OD<=to_date(:_d)' || '' ?},
            exec('wybierz_parses','pracownik','PKD').P,par[4],par[7].OD,par[7].DO);
         {? _tab.first & par[7].DO>#0
         || {!
            |? {? _tab.OD<par[7].OD & _tab.DO>par[7].DO
               || _tab.NK:=par[7].DO-par[7].OD+1
               |? _tab.OD<par[7].OD
               || _tab.NK:=_tab.DO-par[7].OD+1
               |? _tab.DO>par[7].DO
               || _tab.NK:=par[7].DO-_tab.OD+1
               ?};
               _tab.put;
               _tab.next
            !}
         ?};

         par[3]:=_tab;

         echo('Trwa przetwarzanie danych...');

         exec('object','#pivot');
         (par[2]:=obj_new(@.CLASS.PIVOT,par[3],'LP','sum','NK')).total(1).count(1).dosum(1);
         {? par[7].REAL
         || par[2].grpfld('NAZWISKO','PIERWSZE','T','ROK')
         || par[2].grpfld('NAZWISKO','PIERWSZE','T')
         ?};
         {? par[2].make().valid()
         || par[1]:=obj_new(@.CLASS.PRINT,par[2].fld_num());
            par[8]:=obj_new(@.CLASS.PRINT,par[2].sfld_num());

            {? ~rep_export()
            || par[1].add("par[5]+=1",5,'Lp.'@,1,,,,,',,\'9.\'')
            ?};
            par[1].add("par[2].OUT.NAZWISKO",30,'Nazwisko'@);
            par[1].add("par[2].OUT.PIERWSZE",20,'Imię'@);
            par[1].add("par[2].OUT.T",9,'Nr teczki'@);
            {? par[7].REAL || par[1].add("par[2].OUT.ROK",4,'Rok'@,1) ?};
            par[1].add("par[2].OUT.PQ_TOTAL",5,'Razem'@,1);
            par[1].add("par[2].OUT.PQ_COUNT",6,'Liczba'@,1);

            {? ~rep_export()
            || par[8].add("'Razem'@",{? par[7].REAL || 80 || 73 ?});
               par[8].add("par[2].SUM.PQ_TOTAL",5,'',1,,,,,',,\'9,\'');
               par[8].add("par[2].SUM.PQ_COUNT",6,'',1)
            ?};

            {! _ii:=6+par[7].REAL..par[2].fld_num()-1
            |! _name:=par[2].fld_name(_ii);
               _name:={? R.find_key(#_name) || R.RT || '' ?};
               par[1].add($('par[2].fld_val('+$_ii+')'),12,_name,1,,,,,'12,0,\'9,\'')
            !};

            {? ~rep_export()
            || {! _ii:=3..par[2].sfld_num()-1 |! par[8].add($('par[2].sfld_val('+$_ii+')'),12,'',1,,,,,',,\'9,\'') !};
               par[8].s_frame()
            ?};
            par[1].s_frame()

         ?}
      ?};

      echo;

      prn1:='par[1]';
      par[5]:=lmt:=rsep:=0;
      par[6]:=par[9]:=vp:=1
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep');
   R.cntx_pop();
   echo
}
{COMMENT}OLD: zk_nieo.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[7].size()}
{EXIT: par[4]='' | ~par[2].valid()}
 {INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[6]}
   { par[6]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Uwzględniane składniki:'@} {STR.gsub(par[4],',',', ')}
   {<{lmt+1}'Zakres dat: od %1 do %2'@[par[7].OD$1,par[7].DO$1]}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{  par[1].setcolor(color);
   par[1].ini_head()
}
{REP: par[1].fhbar(lmt)}
{WHILE: par[1].h_next()}{DO}
{REP: par[1].h_fmline(lmt)}
{OD}
{IF: par[9]}
   {REP: par[1].fbar(lmt)}
{FI}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: par[2].OUT}{DO}
   {par[1].ini_line()}
   {WHILE: par[1].next()}{DO}
      {REP: par[1].fline(lmt)}
   {OD}
{OD}
{IF: par[5]}
   {IF: ~rep_export()}
      {FOOTER: 0}
      {REP: par[8].flbar(lmt)}
      {FOOTER-}
   {FI}
   {  par[9]:=0;
      par[8].ini_line()
   }
   {REP: '{<1}'+par[1].jbar(par[8],lmt)}
   {WHILE: par[8].next()}{DO}
      {REP: par[8].fline(lmt)}
   {OD}
{FI}
:!UTF-8
{TITLE:Oddziały ZUS, Oddziały NFZ, ...}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','Typ','Lp');
   Typ:=null();
   _TAB:=tab_tmp(1,
      'LP','INTEGER','Kolejność',
      'NAZ','STRING[25]','Nazwa słownika',
      'TYP','STRING[6]','Typ słownika'
   );
   _TAB.LP:=1; _TAB.NAZ:='Oddziały ZUS'; _TAB.TYP:='ODDZUS'; _TAB.add();
   _TAB.LP:=2; _TAB.NAZ:='Fundusze Emerytalne'; _TAB.TYP:='FUNDEM'; _TAB.add();
   _TAB.LP:=3; _TAB.NAZ:='Oddziały NFZ'; _TAB.TYP:='KASACH'; _TAB.add();
   _wer:=_TAB.mk_sel('Wybierz rodzaj słownika'@,,,,,,,,'U',,,,,'maximized');
   _TAB.win_fld(_wer,,'NAZ',,,30,,1,'Rodzaj słownika'@,,'Rodzaj słownika do wydruku'@);
   _TAB.win_act(_wer,,'Formuła','Drukuj'@@,,'Wybór słownika'@,"sel_exit()",,,,,,'D',,'icon=print');
   _TAB.win_btn(_wer,'text=%1,panel=right,align=begin'['Drukuj'@],'menu:D');
   _TAB.win_btn(_wer,'text=%1,panel=right,align=end'['Anuluj'@],'key:Esc');
   _TAB.win_sel(_wer);
   {? _TAB.select()
   || Typ:={? +_TAB.TYP || exec('slo_typ','ext_slo',_TAB.TYP) ?};

      PAR_WYDR.TITLE:=SLO_TYP.OPIS;
      PRN:=obj_new(@.CLASS.PRINT,4);
      {? ~rep_export() || PRN.add("Lp+=1",3,'Lp.'@,1,,,,,',,\'9.\'') ?};
      PRN.add("ADRES.NAZWA",40,'Nazwa'@);
      PRN.add(
      "  ADRES.KOD_POCZ+' '+ADRES.POCZTA+' '+ADRES.MIASTO+' '+ADRES.ULICA+' '+ADRES.DOM+
         {? +ADRES.LOKAL || ' m. '+ADRES.LOKAL || '' ?}
      ",50,'Adres'@);
      PRN.add("{? ADRES.BANK || ADRES.BANK().NB+' '+ADRES.KONTO || '' ?}",50,'Bank i konto'@);
      PRN.s_frame();

      prn1:='PRN';
      lmt:=rsep:=vp:=1;
      Lp:=0
   ?}
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','Typ','Lp')
}
{EXIT: Typ=null()}
{COMMENT}OLD: sl_adres.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: ADRES}{INDEX: 'ADRES'}{PREFIX: Typ}{DO}
{SUBSTITUTE: 'LINIA0'}
{OD}

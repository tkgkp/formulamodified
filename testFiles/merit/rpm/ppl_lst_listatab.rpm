:!UTF-8
{TITLE:Lista płac}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep');
   PAR_WYDR.TITLE:='Lista płac'@;

   UD_SKL.cntx_psh();
   UD_SKL.clear();
   R.cntx_psh();
   R.index('RUBLP');
   R.prefix();
   P.cntx_psh();
   P.prefix();
   par:=obj_new(13);
   par[11]:=0;
   par[12]:=exec('plaski_ud_def','schemat',,'PPL','PODZORG',exec('ref_firma','ustawienia'));
   par[13]:=rep_export();
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr]);
      par[11]:=1
   || {? (par[8]:=1-exec('code_grp','profile',,'listatab',,{? par[13] || 50 || 10 ?})-1)<>''
      || par[4]:=
            sql('
               select   P.WYDZIAL, OSOBA.NAZWISKO, OSOBA.PIERWSZE, P.T, R.LP, LS.KW, P.REFERENCE P
               from     P join LS using(P.REFERENCE,LS.P) join OSOBA join R
               where    P.REFERENCE in (select O_P.P from O_P where O_P.O=\''+$VAR.O+'\' and O_P.LS=\'T\' order by 1)
               and R.RN in (:_a)
               order by 2,3,4',
               par[8]);

         H.cntx_psh();
         H.index('_HISTKOD');
         _data:=date(O.R,O.M,0);
         _test:=
            {? exec('is_active','gp_api')
            || "exec('has_access_p_h','gp_api',,_a,'PPL')"
            || "1"
            ?};
         _loop:=par[4].first();
         {!
         |? _loop
         |! {? P.seek(par[4].P) & _test(_data)
            || H.prefix(P.ref(),'Z');
               {? H.find_le(_data) & par[4].WYDZIAL<>$H.WYDZIAL
               || par[4].WYDZIAL:=$H.WYDZIAL;
                  par[4].put()
               ?};
               _loop:=par[4].next()
            || _loop:=par[4].del()
            ?}
         !};
         H.cntx_pop();
         par[4].first();

         exec('object','#pivot');
         (par[1]:=obj_new(@.CLASS.PIVOT,par[4],'LP','sum','KW')).
         grpfld('WYDZIAL','NAZWISKO','PIERWSZE','T').dosum(1).
         make();

         obj_del(par[4]);
         {? par[1].valid()
         || par[1].OUT.index(par[1].OUT.ndx_tmp('Pracownik',0,'WYDZIAL',,0,'NAZWISKO',,0,'PIERWSZE',,0,'T',,0));

            par[3]:=obj_new(@.CLASS.PRINT,par[1].fld_num()+par[13]);
            {? ~par[13]
            || par[4]:=obj_new(@.CLASS.PRINT,par[1].sfld_num());
               par[5]:=obj_new(@.CLASS.PRINT,par[1].sfld_num())
            ?};

            {? par[13]
            || par[3].add("UD_SKL.SYMBOL+' - '+UD_SKL.OPIS",30,'Jednostka organizacyjna'@)
            || par[3].add("par[9]+=1",5,'Lp.'@,1,,,,,',,\'9.\'')
            ?};
            par[3].add("par[1].OUT.NAZWISKO",30,'Nazwisko'@);
            par[3].add("par[1].OUT.PIERWSZE",20,'Imię'@);
            par[3].add("par[1].OUT.T",10,'Teczka'@);

            {? ~par[13]
            || _name:='Przedsiębiorstwo/Jednostka organizacyjna'@;
               par[4].add("UD_SKL.SYMBOL+' - '+UD_SKL.OPIS",74,_name);
               par[5].add("'Jednostki razem'",74,_name);
               &_name;
               _sum:='select WYDZIAL'
            ?};

            {! _n:=5..par[1].fld_num()-1
            |! _name:=par[1].fld_name(_n);
               {? var_pres('_name')=type_of('')
               || _name:={? R.find_key(#_name) || R.RT || '' ?};
                  par[3].add($('par[1].fld_val('+$_n+')'),12,_name,1,,,,,'12,2,\'9,\'')
               ?};

               {? ~par[13]
               || _fld:='S'+$_n;
                  _sum+=', sum('+par[1].fld_acr(_n)+') '+_fld;
                  par[4].add($('par[2].'+&_fld),12,_name,1,,,,,'12,2,\'9,\'');
                  par[5].add($('par[1].sfld_val('+$(_n-4)+')'),12,_name,1,,,,,'12,2,\'9,\'')
               ?}
            !};

            {? ~par[13]
            || par[2]:=sql(':_b from :_a group by WYDZIAL',par[1].OUT,&_sum);
               par[2].index(par[2].ndx_tmp('Nazwa',0,'WYDZIAL',,0))
            ?};

            par[3].s_frame();
            {? ~par[13]
            || par[4].s_frame();
               par[5].s_frame()
            ?}
         ?}
      ?}
   ?};
   prn1:='par[3]';
   lmt:=rsep:=0;
   vp:=1;

   par[6]:=0;
   par[7]:=1
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep');
   UD_SKL.cntx_pop();
   R.cntx_pop();
   P.cntx_pop()
}
{COMMENT}OLD: listatab.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: par[11]}
{EXIT: par[8]='' | ~par[1].valid()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[7]}
   { par[7]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Uwzględniane składniki:'@} {STR.gsub(par[8],',',', ')}
   {<{lmt+1}'Miesiąc kalendarzowy:'@} {date(O.R,O.M,1)$8}
   {<{lmt+1}'Typ listy:'@} {O.T().T}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{IF: ~par[13]}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[3].width())/2); ~~ }
{FOR: par[12]}{DO}
   {  prn1:='par[3]';
      par[10]:=par[12].SQL_REF;
      UD_SKL.seek(par[10]);
      par[9]:=0;
      ~~
   }
   {FOR: par[1].OUT}{PREFIX: par[10]}{DO}
      {FIRST}
         { par[6]:=par[1].OUT.size(); ~~ }
      {FIRST-}
      {IF: ~(par[6]-=1) & lineleft() & lineleft()<=4}
         {SUBSTITUTE: 'LINIAF'}
         {PAGE}
      {FI}
      {IF: par[6] & lineleft() & lineleft()=2}
         {SUBSTITUTE: 'LINIAF'}
      {FI}
      {SUBSTITUTE: 'LINIA0'}
   {OD}
   {IF: ~par[13]}
      {FOR: par[2]}{PREFIX: par[10]}{DO}
         {FIRST}
            { prn1:='par[4]'; ~~ }
            {REP: '{<{lmt+1}}'+par[3].fjbar(par[4])}
            {SUBSTITUTE: 'LINIA0'}
            {SUBSTITUTE: 'LINIAF'}
            {IF: lineleft>0}
               {PAGE}
            {FI}
         {FIRST-}
      {OD}
   {FI}
{OD}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{  PAR_WYDR.TITLE+='\n(zestawienie zbiorcze)'@;
   ~~
}
{IF: ~par[13]}
   {FOR: par[12]}{DO}
      {FIRST}
         { prn1:='par[5]'; ~~ }
         {SUBSTITUTE: 'LINIA0'}
         {REP: par[5].fbar(lmt)}
         { prn1:='par[4]'; ~~ }
      {FIRST-}{
         par[10]:=par[12].SQL_REF;
         UD_SKL.seek(par[10]);
         ~~
      }
      {FOR: par[2]}{PREFIX: par[10]}{DO}
         {SUBSTITUTE: 'LINIA0'}
      {OD}
   {OD}
{FI}
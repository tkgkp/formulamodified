:!UTF-8
{TITLE:E. Zestawienie pracy pojazdów + zbiornik dodatkowy}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,5,
   5,5,5,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('tryb');
   VAR_DEL.delete('w','s','k','PRN','PRN2','PRNS','s_tran');
   k :=obj_new(24); "--szerokosci kolumn--";
   w :=obj_new(24); "--wartosci kolumn--";
   s_tran:=obj_new(24); "--podsumowania--";

   {! _i:=1..24 |! s_tran[_i]:=0 !};

   dokl :=2; "--dokladnosc--";
   PRN := obj_new(@.CLASS.PRINT,24); PRN.s_frame();
   PRN2 := obj_new(@.CLASS.PRINT,24); PRN2.s_frame();
   PRNS := obj_new(@.CLASS.PRINT,24); PRNS.s_frame();
   tryb:=rep_export();

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=0;rsep:=1;

   k[1]  :=3;
   k[2]  :=9;
   k[3]  :=7;
   k[4]  :=7;
   k[5]  :=5;
   k[6]  :=4;
   k[7]  :=5;
   k[8]  :=5;
   k[9]  :=7;
   k[10] :=9;
   k[11] :=9;
   k[12] :=7;
   k[13] :=8;
   k[14] :=5;
   k[15] :=5;
   k[16] :=5;
   k[17] :=6;
   k[18] :=6;
   k[19] :=8;
   k[20] :=7;
   k[21] :=8;
   k[22] :=6;
   k[23] :=5;
   k[24] :=5;
   w[22] :=0;
   w[23] :=0;

   POJAZDY.win_sel('WER');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;
   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0
   || wyjscie:=1
   ?}
}
{END:
   VAR_DEL.delete('tryb');
   VAR_DEL.delete('w','s','k','PRN','PRN2','PRNS','s_tran');
   VAR_DEL.delete('wyjscie','dokl','oddz');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{MACRO: 'TABHEAD2'}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn1+'.ini_head()'; ($(_f))()}
{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
{WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
{ _f:=prn2+'.ini_head()'; ($(_f))()}
{ _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
{WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{MACRO-}
{
{? tryb<>1 || PRN.add("form(w[1])",k[1],'Lp.',1) ?};
PRN.add("form(w[2])",k[2],'Samochód');
PRN.add("form(w[3])",k[3],'Licz. pocz.',1);
PRN.add("form(w[4])",k[4],'Licz. kon.',1);
PRN.add("form(w[5])",k[5],'Jazdy',1);
PRN.add("form(w[6])",k[6],'Ład.',1);
PRN.add("form(w[7])",k[7],{? tryb=1 || 'ZP: ' || '' ?}+'SP pocz.',1);
PRN.add("form(w[8])",k[8],{? tryb=1 || 'ZP: ' || '' ?}+'SP kon.',1);
PRN.add("form(w[9])",k[9],{? tryb=1 || 'ZP: ' || '' ?}+'Pobrano',1);
PRN.add("form(w[10],,dokl,'.,')",k[10],{? tryb=1 || 'ZP: ' || '' ?}+'Wg normy',1);
PRN.add("form(w[11],,dokl,'.,')",k[11],{? tryb=1 || 'ZP: ' || '' ?}+'Rzeczywiste',1);
PRN.add("form(w[12],,dokl,'.,')",k[12],{? tryb=1 || 'ZP: ' || '' ?}+'Przepał',1);
PRN.add("form(w[13],,dokl,'.,')",k[13],{? tryb=1 || 'ZP: ' || '' ?}+'Oszczędność',1);
PRN.add("form(w[14])",k[14],{? tryb=1 || 'ZD: ' || '' ?}+'Godz. pracy');
PRN.add("form(w[15])",k[15],{? tryb=1 || 'ZD: ' || '' ?}+'SP pocz.',1);
PRN.add("form(w[16])",k[16],{? tryb=1 || 'ZD: ' || '' ?}+'SP kon.',1);
PRN.add("form(w[17])",k[17],{? tryb=1 || 'ZD: ' || '' ?}+'Pobrano',1);
PRN.add("form(w[18],,dokl,'.,')",k[18],{? tryb=1 || 'ZD: ' || '' ?}+'Wg normy',1);
PRN.add("form(w[19],,dokl,'.,')",k[19],{? tryb=1 || 'ZD: ' || '' ?}+'Rzeczywiste',1);
PRN.add("form(w[20],,dokl,'.,')",k[20],{? tryb=1 || 'ZD: ' || '' ?}+'Przepał',1);
PRN.add("form(w[21],,dokl,'.,')",k[21],{? tryb=1 || 'ZD: ' || '' ?}+'Oszczędność',1);
PRN.add("form(w[22])",k[22],'LP. pocz.',1);
PRN.add("form(w[23])",k[23],'LP. kon.',1);
PRN.add("form(w[24])",k[24],'Pob.-LP',1);

PRN2.add("", 50, '');
PRN2.add("", 68, 'Zbiornik podstawowy');
PRN2.add("", 71, 'Zbiornik dodatkowy');
PRN2.add("", 22, '');

PRNS.add("form(s_tran[1])",66,'Razem:');
PRNS.add("form(s_tran[9])",k[9],'Pobrano',1);
PRNS.add("form(s_tran[10],,dokl,'.,')",k[10],'Wg normy',1);
PRNS.add("form(s_tran[11],,dokl,'.,')",k[11],'Rzeczywiste',1);
PRNS.add("form(s_tran[12],,dokl,'.,')",k[12],'Przepał',1);
PRNS.add("form(s_tran[13],,dokl,'.,')",k[13],'Oszczędność',1);
PRNS.add("form(s_tran[14])",k[14],'Godz. pracy');
PRNS.add("form(s_tran[15])",k[15],'SP pocz.',1);
PRNS.add("form(s_tran[16])",k[16],'SP kon.',1);
PRNS.add("form(s_tran[17])",k[17],'Pobrano',1);
PRNS.add("form(s_tran[18],,dokl,'.,')",k[18],'Wg normy',1);
PRNS.add("form(s_tran[19],,dokl,'.,')",k[19],'Rzeczywiste',1);
PRNS.add("form(s_tran[20],,dokl,'.,')",k[20],'Przepał',1);
PRNS.add("form(s_tran[21],,dokl,'.,')",k[21],'Oszczędność',1);
PRNS.add("form(s_tran[22])",k[22],'LP. pocz.',1);
PRNS.add("form(s_tran[23])",k[23],'LP. kon.',1);
PRNS.add("form(s_tran[24])",k[24],'Pob.-LP',1);
{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Zestawienie pracy pojazdów\nza okres od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Zestawienie pracy pojazdów\nza okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?};
   PAR_WYDR.TITLE:='Zestawienie pracy pojazdów\nza okres od: '+$_od+' do: '+$_do
?};
prn1:='PRN';prn2:='PRN'
;~~}
{IF: POJAZDY.index('NRREJ'); POJAZDY.prefix(REF.FIRMA); POJAZDY.first()=0}{ FUN.info('Brak danych spełniających kryteria.');~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}{<70}{'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{IF: tryb=1}
{<{lmt+1}}{'ZP - Zbiornik podstawowy'}
{<{lmt+1}}{'ZD - Zbiornik dodatkowy'}
{FI}
{IF: tryb<>1}
{FONT: 'T8GB'}
{prn1:='PRN2';prn2:='PRN';~~}
{SUBSTITUTE: 'TABHEAD2'}
{prn1:='PRN';prn2:='PRN';~~}
{FONT: 'T8G'}{ vp:=1; ~~}
{ELSE}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FI}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: tryb<>1}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRN2';prn2:='PRN';~~}
{SUBSTITUTE: 'TABHEAD2'}
{prn1:='PRN';prn2:='PRN';~~}
{ELSE}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{
rsep:=PAR_WYDR.RSEP;
POJAZDY.win_edit('RED');
w[1]:=0;~~}
{FOR:'POJAZDY'}
{INDEX:'NRREJ'}
{PREFIX: REF.FIRMA}
{DO}
   {
   {? KARTDROG.WYBKART='Z'
   || _od:=KARTDROG.KARTY_OD;
      _do:=KARTDROG.KARTY_DO
   |? KARTDROG.WYBKART='M'
   || _od:=date(KARTDROG.AR,KARTDROG.AM,1);
      _do:=date(KARTDROG.AR,KARTDROG.AM,0)
   || _od:=date(KARTDROG.AR,KARTDROG.AM,1);
      _do:=date(KARTDROG.AR,KARTDROG.AM,0);
      SAMK.index('D2');
      SAMK.prefix();
      {? SAMK.first()
      || _od:=SAMK.D;
         SAMK.last();
        _do:=SAMK.D
      ?}
   ?};
   w[1] +=1;
   w[2] :=POJAZDY.NRREJ+'  '+POJAZDY.MAR().NAZ;
   SAMK.index('D');
   SAMK.prefix(POJAZDY.ref());
   _ref_f:=_ref_l:=null;
   _licznik:=_licznip:=0;
   _data:=_datap:=date(0,0,0);
   {? SAMK.first()
   || {! |?
         {? _ref_f=null
         || {? (SAMK.D>=_od & SAMK.D<=_do) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT ) & SAMK.STATUS<>'S'
            || _ref_f:=SAMK.ref();
               _licznip:=SAMK.SP;
               _datap:=SAMK.D
            ?}
         || {? (SAMK.D>=_od & SAMK.D<=_do) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT ) & SAMK.STATUS<>'S'
            || {? SAMK.D=_datap & SAMK.SP<_licznip
               || _ref_f:=SAMK.ref()
               ?}
            ?}
         ?};
         {? (SAMK.D>=_od & SAMK.D<=_do) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT ) & (SAMK.D>_data | (SAMK.D=_data & SAMK.SK>=_licznik)) & SAMK.STATUS<>'S'
         || _ref_l:=SAMK.ref();
            _licznik:=SAMK.SK;
            _data:=SAMK.D
         ?};
         SAMK.next()
      !};
      {? _ref_l=null & _ref_f<>null || _ref_l:=_ref_f ?};
      {? _ref_f=null & _ref_l<>null || _ref_l:=null ?}
   ?};
   w[3] :=w[4]:=w[7]:=w[8]:=w[22]:=w[23]:=0;
   {? _ref_f<>null & SAMK.seek(_ref_f) || w[3]:=SAMK.SP; w[7]:=SAMK.SPP; w[22]:=SAMK.LPP ?};
   {? _ref_l<>null & SAMK.seek(_ref_l) || w[4]:=SAMK.SK; w[8]:=SAMK.SKP; w[23]:=SAMK.LPK ?};
   w[5] :=exec('oblicz_prz','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT,'P');
   w[6] :=exec('oblicz_zal','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT,'P');
   w[9] :=exec('i_palz_m','karty_drog',POJAZDY.ref(),'P',_od,_do,KARTDROG.WYB_STAT);
   w[10]:=exec('i_paln_m','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT,'P');
   w[11] :=exec('oblicz_rzecz','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT,'P');
   {? w[23]-w[22]<>0 || w[24]:=w[11]-(w[23]-w[22]) || w[24]:=0 ?};
   _zu:=w[11]-w[10];
   w[12]:=w[13]:=0;
   {? _zu>0 || w[12]:=|(_zu)
   |? _zu<0 || w[13]:=|(_zu)
   ?};
   {? POJAZDY.ND='T'
   ||
      w[14]:=exec('i_prac_m','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT);
      w[15]:=w[16]:=0;
      {? _ref_f<>null & SAMK.seek(_ref_f) || w[15]:=SAMK.SPP2 ?};
      {? _ref_l<>null & SAMK.seek(_ref_l)  || w[16]:=SAMK.SKP2 ?};
      w[17]:=exec('i_palz_m','karty_drog',POJAZDY.ref(),'D',_od,_do,KARTDROG.WYB_STAT);
      w[18]:=exec('i_paln_m','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT,'D');
      w[19]:=w[17]+(w[15]-w[16]);
      w[20]:=w[21]:=0;
      _zu:=w[19]-w[18];
      {? _zu>0 || w[20]:=|(_zu)
      |? _zu<0 || w[21]:=|(_zu)
      ?}
   ||
      w[14]:=time(0,0,0);
      {! _i:=15..21 |! w[_i]:=0 !}
   ?};
   s_tran[9]+=w[9];
   s_tran[11]+=w[11];
   s_tran[12]+=w[12];
   s_tran[13]+=w[13];
   s_tran[14]+=w[14];
   s_tran[17]+=w[17];
   s_tran[19]+=w[19];
   s_tran[20]+=w[20];
   s_tran[21]+=w[21];
   s_tran[24]+=w[24]
   ;~~}
   {SUBSTITUTE:'LINIA0'}
{OD}
{COMMENT}------------------------------Podsumowanie------------------{COMMENT-}
{IF: tryb<>1}
{IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRN';prn2:='PRNS'; vp:=0;
 s_tran[1]:='Razem:';
 s_tran[10]:=s_tran[15]:=s_tran[16]:=s_tran[18]:=s_tran[22]:=s_tran[23]:=''
; ~~}
{SUBSTITUTE: 'LINIA02'}
{prn1:='PRNS';prn2:='PRN'; vp:=0; ~~}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{FI}
:!UTF-8
{TITLE: Lista obecności}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'I','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('__tab','PRN','prn1','vp','lmt','rsep','h_rat','tresc','pb','TMP1','Ok','Lp','Wydzial','Kalend');
   VAR_DEL.delete('Teczka','DataWej','DataWyj','GodzWej','GodzWyj','CzasPrac','Count','Temp','Chk','Karta','Pracownik');
   VAR_DEL.delete('Ppsf');

   _tab:=exec('tab_prac','!prc_czp_wydr');
   _sql:=''+" select * from P join UD_SKL join :_a using (P.REFERENCE, :_a.SQL) order by UD_SKL.SYMBOL";
   _sql2:=''+" select * from P join UD_SKL join :_a using (P.REFERENCE, :_a.SQL) order by :_a.LP";
   __tab:=sql(_sql2,_tab);
   _TAB:=sql(_sql,_tab);

   {? _TAB.first()
   || tresc:='w_listob';
      PAR_WYDR.TITLE:='Lista pracowników obecnych'@;

      PRN:=obj_new(@.CLASS.PRINT,10);
      {? rep_export()
      || PRN.add("P.WYDZIAL().SYMBOL+' '+|UD_SKL.OPIS",40,'Jednostka organizacyjna'@)
      || PRN.add("Lp",5,'Lp.'@,1,,,,,',,\'9.\'')
      ?};
      PRN.add("Karta",-8,'Karta'@,1);
      PRN.add("Teczka",8,'Nr teczki'@);
      PRN.add("Pracownik",17,'Nazwisko i imię'@);
      {? ~rep_export() ||PRN.add("P.WYDZIAL().SYMBOL",13,'Jednostka organizacyjna'@,1) ?};
      PRN.add("form(GodzWej)",7,'Wejście'@,1);
      PRN.add("form(GodzWyj)",7,'Wyjście'@,1);
      PRN.add("form(CzasPrac)",6,'Czas pracy'@,1);
      PRN.add("form(Ppsf)",17,'Praca poza siedzibą firmy'@,1);
      PRN.add("form(Nieob)",17,'Uwagi'@,1);
      PRN.s_frame();
      prn1:='PRN';
      rsep:=PAR_WYDR.SEP;
      lmt:=pb:=Ok:=Count:=Temp:=Chk:=0;
      TMP1:=0;
      vp:=h_rat:=1;
      Lp:=Kalend:=0;
      Karta:=Pracownik:=Teczka:=DataWej:=DataWyj:=GodzWej:=GodzWyj:=CzasPrac:=Ppsf:=Nieob:=nieob:=jest:='';
      Wydzial:=0
   ?};
   params_set('P',_tab,'PT',_TAB);
   zatr:="{? P.DZA>_a || 0
          |? P.DZ=date(0,0,0) || 1
          |? P.DZ<_a || 0
          || 1
          ?}";
   nieob:=" _naz_n:='';
           N.cntx_psh;
           N.index('NIEOBECN');
           N.prefix('N',P.ref);
           {? N.first()
           || {!|?
                   {? _a>=N.OD & _a<=N.DO
                       || _naz_n+=N.NB().RT
                   ?};
                   N.next
              !}
           ?};
          N.cntx_pop();_naz_n";
   var:=exec('uprawnieniawrap','pkd','nieobecnościach'@,'PKD_EZK_OPNN','PKD_EZK_ORNN');
   R_WZCZ.cntx_psh();
   R_KARHIS.cntx_psh();
   KAL_ROK.cntx_psh();
   KAL_DEF.cntx_psh();
   R_REJ_WW.cntx_psh()
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','pb','Ok','Lp','Wydzial','Kalend','Karta','Pracownik');
   VAR_DEL.delete('Teczka','DataWej','DataWyj','Identyfikator','GodzWej','GodzWyj','CzasPrac','Count','Temp','Chk');
   VAR_DEL.delete('Ppsf','Nieob','nieob','jest','zatr','var');
   exec('czytaj','#stalesys',date(),KST,'R_TZ');
   KAL_DEF.cntx_pop();
   KAL_ROK.cntx_pop();
   R_KARHIS.cntx_pop();
   R_WZCZ.cntx_pop();
   R_REJ_WW.cntx_pop()
}
{EXIT: ~params_get().P.first()}
{EXIT: ~__tab.first()}
{COMMENT}OLD: w_listob.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{  h_rat:=charleft(); ~~}
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() ||1 || h_rat ?}; ~~}
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'%1 %2'['W dniu:'@,$VAR_EDIT.DATA]}
   {IF: VAR_EDIT.FLAGA=0}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Alfabetycznie w ramach jednostek organizacyjnych'@]}
   {ELIF: VAR_EDIT.FLAGA=1}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Grupowo w ramach jednostek organizacyjnych'@]}
   {ELIF: VAR_EDIT.FLAGA=2}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Alfabetycznie (bez związku z jednostką organizacyjną)'@]}
   {FI}
   {IF: +var=0}
   {IF: VAR_EDIT.DL=1}{<{ceil(lmt*h_rat)+1}'%1'['Uwzględniono współpracowników, którym wprowadzono nieobecność: TAK'@]}
   {ELSE}{<{ceil(lmt*h_rat)+1}'%1'['Uwzględniono współpracowników, którym wprowadzono nieobecność: NIE'@]}
   {FI}
   {FI}
   {IF: VAR_EDIT.AKT=1}{<{ceil(lmt*h_rat)+1}'%1'['Uwzględniono współpracowników bez wejść/wyjść: TAK'@]}
   {ELSE}{<{ceil(lmt*h_rat)+1}'%1'['Uwzględniono współpracowników bez wejść/wyjść: NIE'@]}
   {FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: ~rep_export()}
   {IF: VAR_EDIT.FLAGA=1}
      {<{lmt+1}'Jednostka organizacyjna:'@} {P.WYDZIAL().SYMBOL}{IF: +(|UD_SKL.OPIS)} ({UD_SKL.OPIS}){FI}
   {FI}
{FI}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF:zatr(VAR_EDIT.DATA)}
{  R_WZCZ.index('R_WZCZ');
   R_WZCZ.prefix(P.name(),P.ref());
   R_KARHIS.index('PR_ZW_OD');
   R_KARHIS.prefix(P.ref(),'T');
   Karta:={? R_KARHIS.find_le(VAR_EDIT.DATA) || R_KARHIS.K().N || '' ?};
   Kalend:=
      {? R_WZCZ.find_le(VAR_EDIT.DATA)
      || {? R_WZCZ.GRAFIK='T' || R_WZCZ.CZESC || R_WZCZ.KAL ?}
      || P.KAL
      ?};
   Pracownik:=form(P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,40);
   Karta:=form(Karta,-13);
   Teczka:=form(P.T,-9);
   {? +var=0 || Nieob:=18+nieob(VAR_EDIT.DATA) || Nieob:='' ?};
   jest:=0;
   ~~
}
{IF: Kalend<>0}
   {  KAL_ROK.index('KAL_ROK');
      KAL_ROK.prefix(Kalend, VAR_EDIT.DATA~1);
      Ppsf:=exec('wydr_p_dt','ppsf',P.ref(),VAR_EDIT.DATA); ~~
   }
   {IF: KAL_ROK.first()}
      {  KAL_DEF.index('KAL_DEF');
         KAL_DEF.prefix(); ~~
      }
      {IF: KAL_DEF.find_key(KAL_ROK.ref(),VAR_EDIT.DATA)}
         {IF: R_REJ_WW.index('R_REJ_WX'); R_REJ_WW.prefix(P.ref,VAR_EDIT.DATA); R_REJ_WW.first}
            {FOR: R_REJ_WW}
            {INDEX: 'R_REJ_WX'}
            {PREFIX: P.ref(),VAR_EDIT.DATA}
            {DO}
               {FIRST}
               {  Count:=0;
                  Chk:=1;
                  GodzWyj:=GodzWej:=time(-1,0,0);
                  DataWej:=DataWyj:=#0; ~~
               }
               {FIRST-}
               {{? Temp:=(R_REJ_WW.RD='normalne') ||
                  Count:={? R_REJ_WW.TP='WEJŚCIE'
                              || GodzWej:=R_REJ_WW.GD; DataWej:=R_REJ_WW.DZ; 0
                              || GodzWyj:=R_REJ_WW.GD; DataWyj:=R_REJ_WW.DZ; 1
                        ?}
                ?}; ~~
               }
               {IF: (Temp & Count)}
               {IF:+Pracownik}
                  {  {? GodzWej=time(-1,0,0)
                     || CzasPrac:=form(''@,5);
                        GodzWej:=''; Nieob:='Brak wejścia'@
                     |? GodzWyj=time(-1,0,0)
                     || CzasPrac:=form(''@,5);
                        GodzWyj:=''; Nieob:='Brak wyjścia'@
                        || _time:=exec('UstGodz','prc_wewy',DataWej,DataWyj,GodzWyj);
                           CzasPrac:=form(_time-GodzWej,5)
                        ?};
                        {? Chk
                        || Lp+=1
                        || Karta:=Teczka:=Pracownik:=''
                        ?}; ~~
                     }
                     {IF: P.WYDZIAL<>Wydzial & TMP1}
                        {IF: VAR_EDIT.FLAGA=1}
                           { Lp:=1; ~~}
                           {PAGE}
                        {FI}
                        {  Wydzial:=P.WYDZIAL; ~~}
                     {FI}
                     {  TMP1:=1; ~~}
                     {SUBSTITUTE: 'LINIA0'}
                     {  Ok:=1; jest+=1;
                        {? type_of(GodzWej)=2 || GodzWej:=time(-1,0,0) ?}; {? type_of(GodzWyj)=2 || GodzWyj:=time(-1,0,0) ?}; ~~
                     }
                     { Chk:=0; ~~}
                  {FI}
               {FI}
            {OD}
            {IF:jest=0}
             {IF: KAL_DEF.CZAS>time(0,0,0)}
             { {? type_of(GodzWej)=type_of(time) &  GodzWej=time(-1,0,0) || GodzWej:=''?};
               {? type_of(GodzWyj)=type_of(time) &  GodzWyj=time(-1,0,0) || GodzWyj:=''?};
               Lp+=1; CzasPrac:='';
               {? Nieob=''|| Nieob:={? GodzWyj='' || 'Brak wyjścia' || 'Brak wejścia' ?} ?}; ~~ }
                  {IF: P.WYDZIAL<>Wydzial & TMP1}
                     {IF: VAR_EDIT.FLAGA=1}
                        { Lp:=1; ~~}
                        {PAGE}
                     {FI}
                     {  Wydzial:=P.WYDZIAL; ~~}
                  {FI}
                  {  TMP1:=1; ~~}
             {SUBSTITUTE: 'LINIA0'}
             {Ok:=1; Chk:=0; ~~ }
             {FI}
            {FI}
         {ELSE}
            {IF: KAL_DEF.CZAS>time(0,0,0)}
             {IF: Nieob<>'' & VAR_EDIT.DL=1}
             { Lp+=1; GodzWej:=''; GodzWyj:=''; CzasPrac:=''; ~~ }
                    {IF: P.WYDZIAL<>Wydzial & TMP1}
                      {IF: VAR_EDIT.FLAGA=1}
                         { Lp:=1; ~~}
                         {PAGE}
                      {FI}
                      {  Wydzial:=P.WYDZIAL; ~~}
                   {FI}
                   {  TMP1:=1; ~~}
             {SUBSTITUTE: 'LINIA0'}
             {Ok:=1; Chk:=0; ~~ }
             {ELIF: Nieob='' & VAR_EDIT.AKT=1}
             { Lp+=1; GodzWej:=''; GodzWyj:=''; CzasPrac:=''; {? +var=0 || Nieob:='Brak we/wy oraz nieobec.' || Nieob:='Brak we/wy'?};~~ }
                  {IF: P.WYDZIAL<>Wydzial & TMP1}
                      {IF: VAR_EDIT.FLAGA=1}
                         { Lp:=1; ~~}
                         {PAGE}
                      {FI}
                      {  Wydzial:=P.WYDZIAL; ~~}
                  {FI}
                   {  TMP1:=1; ~~}
             {SUBSTITUTE: 'LINIA0'}
             {Ok:=1; Chk:=0; ~~ }
             {FI}
            {FI}
         {FI}
      {FI}
   {FI}
{FI}
{FI}
{MACRO-}
{IF:  VAR_EDIT.DATA:=date();
      _ed:=VAR_EDIT.mk_edit('Lista obecności'@,0,'prc_l_ob');
      VAR_EDIT.win_esep(_ed,'Parametry wydruku'@);
      VAR_EDIT.win_efld(_ed,,'DATA',,,8,,,'Na dzień'@,,'Data, której dotyczy raport'@);
      VAR_EDIT.win_efld(_ed,,'FLAGA',,,,,,
         'Rodzaj raportu'@,,'Zestawienia są generowane dla wydziałów na osobnych stronach'@,
         'radio-buttons',,
         'Alfabetycznie w ramach jednostek organizacyjnych'@,"0",
         'Grupowo w ramach jednostek organizacyjnych'@,"1",
         'Alfabetycznie (bez związku z jednostką organizacyjną)'@,"2");
      exec('ok_esc','#window',VAR_EDIT,_ed);
      {? rep_export() || VAR_EDIT.efld_opt(_ed,'enable=0',,'FLAGA') ?};
      {? +var=0 || VAR_EDIT.win_efld(_ed,,'DL',,,,,{?+var||0?},'Drukować współpracowników z nieobecnością?'@,,{?+var|| 'Pole dostępne dla użytkowników z dostępem do przeglądania lub rejestracji nieobecności.' || '' ?},'check-box',,"1","0") ?};
      VAR_EDIT.win_efld(_ed,,'AKT',,,,,,'Drukować współpracowników bez wejść/wyjść?'@,,,'check-box',,"1","0");
      VAR_EDIT.efld_opt(_ed,'enable=1, mark=1',,'DATA');
      VAR_EDIT.win_edit(_ed);
      ~VAR_EDIT.edit("__CHK.record(VAR_EDIT,,'DATA')")
}
   {EXIT}
{FI}
{  exec('czytaj','#stalesys',VAR_EDIT.DATA,KST,'R_TZ');
   MASK.Use('R_PRACDN',VAR_EDIT.DATA~1,VAR_EDIT.DATA~2);
   MASK.Use('R_REJ_WW',VAR_EDIT.DATA~1,VAR_EDIT.DATA~2); ~~
}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{IF: VAR_EDIT.FLAGA=2}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
{ELIF: VAR_EDIT.FLAGA<2}
{FOR: params_get().PT}
{DO}
   {IF: P.seek(params_get().PT.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
{FI}
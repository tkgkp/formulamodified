:!UTF-8
{TITLE:2. Limity zlecenia z pobraniami}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   ZLIM.cntx_psh();
   zlec:=0;
   {? VAR.A_ZLEC().RODZAJ='P'
   || zlec:=0
   || zlec:=1
   ?};
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=1;
   dalej:=0;
   ramka:='│';
   FAZY:=tab_tmp(1,'KOD','STRING[10]','Nazwa Fazy','NR','INTEGER','Ref do fazy');
   fazy:=FAZY.ndx_tmp(,1,'KOD','',0,'KOD',,0);
   FAZY.index(fazy);
   f:=0;
   PRN:=obj_new(@.CLASS.PRINT,14);
   PRN.sl_frame();
  "---Obiekt wydruku pobran asortymentow do zlecen.";
   PRNZST:=obj_new(@.CLASS.PRINT, 13); PRNZST.sl_frame();
   prn1:='PRN';
   color:='';ok:=0;
   refzl:=null();
   lm:=ll:=vp:=lmt:=rsep:=0;
   "---Zaakceptowane, czy wszystkie.";
   akc_only:=0;
   f:=0;
   etyk:='';
   temp:='';
   "---Tymczasowy indeks pomocniczy:";
   "---Gdy tylko zaakceptowane, to: ZLEC+KOR+AKC+NR";
   "---Gdy wszystkie, to: ZLEC+KOR+NR";
   "---Zawiera ilosc z korekt dla biezacej pozycji.";
   il_kor:=il_rw:=il_zw:=il_il:=0;
   "---REFERENCE zlecenia.";
   zl_ref:=null();
   query:=0;
   "---Numer limitu";
   lim_nr:=0;
   wyd:=0
}
{END:
   ZLIM.cntx_pop();
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   obj_del(PRNZST);
   FAZY.ndx_drop(fazy);
   obj_del(FAZY);
   &f;
   &dalej;&ok;
   &etyk;
   &ramka;
   &temp;
   &zlec;
   obj_del(PRN);
   &refzl;
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   '--------------------------';
   ZLIM.ndx_drop();
   &il_kor; &il_rw; &il_zw; &il_il;
   &zl_ref;
   &query;
   &lim_nr;
   &akc_only;
   &wyd
}
{COMMENT}
  Wydruk limitow zlecenia z pobraniami
  [MKO] - 2001/11/21 - [7.53] Uwaga 81
  [TS] [8.60] Usuniecie pola 'Limit wartosciowy'
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Definicje -> Zapotrzebowania - limity -> Drukuj
{COMMENT-}
{DEFINEFONT: 'kod=TexGyreCursor,160,x,8'}
{ _wydr_naz:='zlim_002';
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.AKC:=WYDRUKIL.SR;
     WYDRUK.FAZY:=WYDRUKIL.FAK;
     WYDRUK.KODY:=WYDRUKIL.RODZFAK
  ?};
  WYDRUK.win_edit('ZLIM_002');~~}
{IF: WYDRUK.edit()}
{ WYDRUKIL.SR:=WYDRUK.AKC;
  WYDRUKIL.FAK:=WYDRUK.FAZY;
  WYDRUKIL.RODZFAK:=WYDRUK.KODY;
  WYDRUKIL.put(1);
  akc_only:=WYDRUK.AKC='T';
  f:=WYDRUK.FAZY='T';
  etyk:=WYDRUK.KODY;
   {? akc_only
    || {? zlec=0
       || {? f
          || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'LIMIT',,0,'KOR','',0,'AKC','',0,'PFAZ','KOD',0,'PFAZ','KOD',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
          || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'LIMIT',,0,'KOR','',0,'AKC','',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
          ?}
       || {? f
          || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLDOD','',0,'LIMIT',,0,'KOR','',0, 'AKC','',0,'PFAZ','KOD',0,'PFAZ','KOD',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
          || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLDOD','',0,'LIMIT',,0,'KOR','',0, 'AKC','',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
          ?}
       ?}
    || {? zlec=0
       || {? f
          || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'LIMIT',,0,'KOR','',0,'PFAZ','KOD',0,'PFAZ','KOD',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
          || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'LIMIT',,0,'KOR','',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
          ?}
       || {? f
          || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLDOD','',0,'LIMIT',,0,'KOR','',0,'PFAZ','KOD',0,'PFAZ','KOD',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
          || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLDOD','',0,'LIMIT',,0,'KOR','',0,'KTM','KTM',0,'ZGP','NRZLP',0,'ZGP','NRP',0)
          ?}
       ?}
   ?};~~}
{ELSE}
{EXIT}
{FI}
{MACRO: 'utw_fazy'}
{  ZLIM.clear();
   {? zlec=0
   ||  ZLIM.index('ZN')
   ||  ZLIM.index('ZPN')
   ?};
   ZLIM.prefix(VAR.A_ZLEC,'T');
   {? ZLIM.first()
   || {!
      |? FAZY.prefix(ZLIM.PFAZ().KOD,ZLIM.PFAZ().KOD);
         {? ~FAZY.first()
         || FAZY.KOD:=ZLIM.PFAZ().KOD;
            FAZY.NR:=#ZLIM.PFAZ;
            FAZY.add()
         ?};
         ZLIM.next()
      !}
   ?};
~~}
{MACRO-}
{IF: f=1}
{SUBSTITUTE: 'utw_fazy'}
{FI}
{MACRO: 'miscelaneous'}
  {COMMENT}---Query.------------------------------------------------{COMMENT-}
{MACRO-}
{COMMENT}--Definicja wiersza wydruku -------------------------------{COMMENT-}
{SUBSTITUTE: 'miscelaneous'}
{  {? etyk='T'
   ||  PRN.add("''",50,'Kod kreskowy'@)
   ?};
   PRN.add("ZLIM.KTM().KTM",21,'Indeks'@);
   PRN.add("ZLIM.KTM().N",30,'Nazwa'@);
   PRN.add("ZLIM.KTM().J().KOD",10,'jm'@);
   PRN.add("ZLIM.ZGP().NRZLP().NRPRZ+' poz. '+$ZLIM.ZGP().NRP",25,'Przewodnik'@);
   {? f=0 || PRN.add("ZLIM.PFAZ().KOD",10,'Faza produkcji'@) ?};
   PRN.add("form({? ZLIM.SO='O' || -1 || 1 ?}*il_kor,,ST.DOKL)",14,'Limit ilościowy (po korektach)'@,1);
   PRN.add("{? il_rw=0 || '' || form(il_rw,,ST.DOKL) ?}",14,'Ilość pobrana'@,1);
   PRN.add("{? il_zw=0 || '' || form(il_zw,,ST.DOKL) ?}",14,'Ilość zwrócona'@,1);
   PRN.add("{? il_il=0 || '' || form(il_il,,ST.DOKL) ?}",14,'Ilość aktualna (pobrano - zwrócono)'@,1);
   PAR_WYDR.TITLE:='LIMITY ZLECENIA %1 Z POBRANIAMI'@[ZL.SYM]; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
  ---Wpisuje do zmiennej il_kor ilosc
  ---korekt dla biezacego limitu zlecenia. Jezeli biezaca pozycja jest
  ---korekta, to w ww. zmiennych znajda sie wartosci 0.
{COMMENT-}
{MACRO: 'obl_kor'}
{ il_kor:=exec('sum_il','zl_limit',ZLIM.ref(),akc_only); ~~}
{MACRO-}
{MACRO:'stang'}
{IF: f=0}
   {FONT: 'T8G'
   }{SPACE: 'B'
   }{temp:=form(ZLIM.KTM().KODK);~~
   }{<{lmt+1} ramka
   }{<{(lmt+3)}
   }{BARCODE: temp;47;BARCODE_H;BARCODE_TYPE
   }{>{lmt+52}ramka
   }{>{lmt+74}ramka
   }{>{lmt+105}ramka
   }{>{lmt+116}ramka
   }{>{lmt+142}ramka
   }{>{lmt+153}ramka
   }{>{lmt+168}ramka
   }{>{lmt+183}ramka
   }{>{lmt+198}ramka
   }{>{lmt+213}ramka}
{ELSE}
   {FONT: 'T8G'
   }{SPACE: 'B'
   }{temp:=form(ZLIM.KTM().KODK);~~
   }{<{lmt+1} ramka
   }{<{(lmt+3)}
   }{BARCODE: temp;47;BARCODE_H;BARCODE_TYPE
   }{>{lmt+52}ramka
   }{>{lmt+74}ramka
   }{>{lmt+105}ramka
   }{>{lmt+116}ramka
   }{>{lmt+142}ramka
   }{>{lmt+157}ramka
   }{>{lmt+172}ramka
   }{>{lmt+187}ramka
   }{>{lmt+202}ramka}
{FI}
{MACRO-}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Rok obrotowy: %1'@[$ST.AR]}
{<{lmt+1} 'Okres: %1'@[$ST.AM]}{IF: etyk='T'}{<{lmt+70}}{BARCODE: form(ZL.SYM);50;2;BARCODE_TYPE}{FONT: 'T8G'}{SPACE: 'B'}{FI}
{<{lmt+1} 'Opis zlecenia: %1'@[ZLIM.ZLEC().OPIS]}
{<{lmt+1} 'Data otwarcia zlecenia: %1'@[form(ZLIM.ZLEC().OD)]}
{<{lmt+1} {? akc_only || 'Zaakceptowane'@ || 'Zaakceptowane i niezaakceptowane'@ ?}}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{IF: f=0}
{ {? akc_only
   || ZLIM.index(ndx_tmp); ZLIM.prefix(VAR.A_ZLEC,'T',0,'T')
   || ZLIM.index(ndx_tmp); ZLIM.prefix(VAR.A_ZLEC,'T',0)
  ?};
  rsep:=PAR_WYDR.RSEP; ~~}
{IF: ZLIM.first()}{ dalej:=1;ok:=1; ~~}
 {FONT: 'T8GB'}{SPACE: 'B'}
{lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: lineleft<8}{PAGE}{FI}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;ok:=0; ~~}
{WHILE: dalej}
{DO}
   {COMMENT}---Oblicz ilosc i wartosc z korekt.---{COMMENT-}
   {SUBSTITUTE: 'obl_kor'}
   {COMMENT}---Wyznacz wartosc po korekcie.-------{COMMENT-}
   { il_il:=ZLIM.IL_RW-ZLIM.IL_ZW; il_rw:=ZLIM.IL_RW; il_zw:=ZLIM.IL_ZW; ~~}
   {COMMENT}---Wydrukuj pojedynczy wiersz.--------{COMMENT-}
   { wyd:={? ZLIM.PFAZ<>null
           || ZLIM.PFAZ().WYD
           || VAR.A_ZLEC().JORG
          ?};~~}
   {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {FI}
     {IF:etyk='T'}
     {SUBSTITUTE: 'stang'}
     {FI}
  {dalej:=ZLIM.next(); ~~}
  {OD}
  {SUBSTITUTE: 'LINIAF'}
{FI}
{ELSE}
{FOR: FAZY}
{DO}
   {FONT: 'W12'}{SPACE: 'A'}
{     lk:=charleft(); ~~}
     {PFAZ.seek(FAZY.NR,'pfazypr');~~}
     {<1>{lk} 'FAZA '+ FAZY.KOD+'  -  '+PFAZ.OPIS}
     {FONT: 'T8G'}{SPACE: 'B'}
{  {? akc_only
   || ZLIM.index(ndx_tmp); ZLIM.prefix(VAR.A_ZLEC,'T',0,'T',FAZY.KOD,FAZY.KOD)
   || ZLIM.index(ndx_tmp); ZLIM.prefix(VAR.A_ZLEC,'T',0,FAZY.KOD,FAZY.KOD)
   ?};
   rsep:=PAR_WYDR.RSEP; ~~}
{IF: ZLIM.first()}{ dalej:=1;ok:=1; ~~}
 {FONT: 'T8GB'}{SPACE: 'B'}
{lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: lineleft<8}{PAGE}{FI}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;ok:=0;il_kor:=0; ~~}
{WHILE: dalej}
{DO}
   {COMMENT}---Oblicz ilosc i wartosc z korekt.---{COMMENT-}
   {SUBSTITUTE: 'obl_kor'}
   {COMMENT}---Wyznacz wartosc po korekcie.-------{COMMENT-}
   { il_il:=ZLIM.IL_RW-ZLIM.IL_ZW; il_rw:=ZLIM.IL_RW; il_zw:=ZLIM.IL_ZW; ~~}
   {COMMENT}---Wydrukuj pojedynczy wiersz.--------{COMMENT-}
   { wyd:={? ZLIM.PFAZ<>null()
           || ZLIM.PFAZ().WYD
           || VAR.A_ZLEC().JORG
          ?};~~}
   {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {FI}
     {IF:etyk='T'}
     {SUBSTITUTE: 'stang'}
     {FI}
  {dalej:=ZLIM.next(); ~~}
  {OD}
  {SUBSTITUTE: 'LINIAF'}
{FI}
{OD}
{FI}

:!UTF-8
{TITLE:6. Struktura technologiczna wyrobu}
{PRINTER: INFO.W_PRN,,,,,'i','b',,,,1}
{BEGIN:
  TKTL.cntx_psh();
  kreska := '─';
  width:=92;
  dalej:=0;
  "---Ramki tekstowe czy graficzne do rysowania drzewa.";
  bcross := '├─';
  lbcross := '└─';
  sep := '│';
  "---Odleglosc pomiedzy poszczegolnymi poziomami drzewa (pozioma).";
  levelspace:=6;
  VAR.A_KTL:={? VAR.ACTION='3' || TKTLW.TKTL || TKTL.ref() ?};
  VAR.A_T:={? VAR.ACTION='3' || TKTLW.KTM || VAR.A_KTL().KTM ?};
  exec('start_tpar','tech_param',VAR.A_T);
  "---Biezacy poziom drzewa (numeracja od 0).";
  level:=0;
  "---REF biezacego surowca.";
  mat:=null();
  "---REF karty technologicznej surowca.";
  matktl:=null();
  "---Maksymalny poziom zagniezdzenia drzewa.";
  maxlevel:=10;
  ktl:=null();
  nrk:='';
  wer:='';
  xjm:=0;
  "---Norma materialowa brutto dla elementu nadrzednego w drzewie. Informacja ";
  "---jest przechowywana dla kazdego poziomu.";
  warbmast:=obj_new(maxlevel);
  {! _iter:=1..maxlevel |! warbmast[_iter]:=0 !};
  "---Wartosci XJM technologio elementu nadrzednego w drzewie. Informacja     ";
  "---jest przechowywana dla kazdego poziomu.";
  techmast:=obj_new(maxlevel);
  {! _iter:=1..maxlevel |! techmast[_iter]:=0 !};
  poziomy:=obj_new(maxlevel);
  {! _iter:=1..maxlevel |! poziomy[_iter]:=''!}
 }
{END:
  TKTL.cntx_pop();
  &kreska;&dalej;
  obj_del(warbmast);
  obj_del(techmast);
  obj_del(poziomy);
"---Sekcja zwalniania funkcji.";
  &getgaprow;
   exec('stop_tpar','tech_param');
  "---";
  &bcross;
  &wer;
  &lbcross;
  &sep;
   &levelspace;
  &level;
  &mat;
  &ktl;
  &nrk;
  &xjm;
  &matktl;
  &maxlevel
}
{COMMENT}
  Struktura technologiczna wyrobu.
  [SS] - 2001/04/03 - [7.41]
  [MKO] - 2001/11/16 - [7.53]
  Wywołanie: Karty technologiczne -> Drukuj
{COMMENT-}
{COMMENT}

  Schemat wydruku:

    0   1   2       M                   0   1   2       M
    │   │   │  ...  │                   |   |   |  ...  |
  Wyrob                               Wyrob
    │                                   |
    ├─ Surowiec1                        +- Surowiec1
    │   │                               |   |
    │   ├─ Surowiec1                    |   +- Surowiec1
    │   .   │                           |   .   |
    │   .   ├─ Surowiec1                |   .   +- Surowiec1
    │   .   .                           |   .   .
    │   │                               |   |
    │   └─ SurowiecN                    |   +- SurowiecN
    │                                   |
    │                                   |
    ├─ Surowiec2                        +- Surowiec2
    .                                   .
    .                                   .
    .                                   .
    └─ SurowiecN                        +- SurowiecN

{COMMENT-}
{"------------------------------------------------------------------------"; ~~}
{PAR_WYDR.TITLE:='STRUKTURA TECHNOLOGICZNA WYROBU'}
{"------------------------------------------------------------------------"; ~~}
{"---Zbior funkcji wykorzystywanych w biezacej definicji wydruku.         "; ~~}
{MACRO: 'functions'}
  {"--------------------------------------------------------------------------";
   "---Zwraca wiersz wydruku dla poziomu okreslonego przez _a, w tej czesci,  ";
   "---gdzie NIE wystepuje opis surowca, np. '│    │    │'.                   ";
   "---Parametry:                                                             ";
   "--- _a:NUMBER - poziom zaglebienia.";
   getgaprow:="
     _result:='';sep:='│';
     {? _<1
     || _result:='error';
        FUN.error('Funkcja GETGAPROW wydruku KTLE0005.RPM:\n'+
            'niewłaściwa liczba parametrów.')
     ?};
     {? _result<>'error'
     || _result:='';
        {? _a>0
        ||  {? _a>1 || _result:=poziomy[1];
           {! _iter:=2.._a-1
           |! _result:=_result + ' '*levelspace +poziomy[_iter]
           !};
         {? _b || sep:='   ' || sep:='│' ?};
         _result:=_result + ' '*levelspace  +sep
           ||_result:=poziomy[1]?}
        ?}
     ?};
     _result
   ";
   "--------------------------------------------------------------------------";
   "---Zwraca wiersz wydruku dla poziomu okreslonego przez _a, w tej czesci,  ";
   "---gdzie wystepuje opis surowca, np. '│    │    ├'.                   ";
   "---Parametry:                                                             ";
   "--- _a:NUMBER - poziom zaglebienia.                                       ";
   "--- _b:NUMBER - 1 - koniec glalezi ('└'), 0 - srodek galezi ('├').";
   getmatrow:="
     _result:='';_sum:='';
     {? _<2
     || _result:='error';
        FUN.error('Funkcja GETMATROW wydruku KTLE0005.RPM:\n'+
            'niewłaściwa liczba parametrów.')
     ?};
     {? _result<>'error'
     || {? _a>0
        || {? _b
           || _result:=lbcross
           || _result:=bcross
           ?};
            {? _a> 1 ||
             {! _iter:=1.._a-1
             |! _sum:=_sum+poziomy[_iter]+' '*levelspace
             !}; _result:=_sum +_result ?}
        ?}
     ?};
     _result
   ";
   "--------------------------------------------------------------------------";
  ~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Sekcja akcji inicjalizujacych.                                       "; ~~}
{MACRO: 'miscelaneous'}
  {"--- B R A K   A K C J I   I N I C J A L I Z U J A C Y C H.            "; ~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Deklaracja funkcji wydruku.                                          "; ~~}
{SUBSTITUTE: 'functions'}
{"------------------------------------------------------------------------"; ~~}
{"---Wykonanie akcji inicjalizujacych.                                    "; ~~}
{SUBSTITUTE: 'miscelaneous'}
{INCLUDE: wsp_pw.rpi}
{"------------------------------------------------------------------------"; ~~}
{"---Drukuje cale drzewo stanowiace strukture technologiczna wyrobu.      "; ~~}
{MACRO: 'prntree'}
  {"-----------------------------------"; ~~}
  {"---Na zerowym poziomie biezemy technologie bezposrednio z TKTL, ale   "; ~~}
  {"---w innych przypadkach biezemy ja juz z surowca (jezeli oczywiscie)  "; ~~}
  {"---surowiec jest wyrobem.                                             "; ~~}
  {IF: level=0}
    {matktl:=TKTL.ref(); ~~}
    {warbmast[level+1]:=TKTL.XJM; ~~}
    {techmast[level+1]:=TKTL.XJM; ~~}
  {ELSE}
    { ktl:=null();
      xjm:=1;
      nrk:='';
      wer:='';
      {? TMAT.DFLT_KTL='T'
      || ktl:=exec('dflt_ktl','tech_prod',{? TMAT.GRKTM='G' || TMAT.TGDFLT().PT || TMAT.PT ?});
         {? ktl<>null()
         || TKTL.cntx_psh();
            TKTL.use(ref_name(ktl));
            TKTL.clear();
            {? TKTL.seek(ktl)
            || xjm:=TKTL.XJM;
               nrk:=TKTL.NRK;
               wer:=TKTL.WER
            ?};
            TKTL.cntx_pop()
         ?}
      || {? TMAT.RKTL<>''
         ||
            TKTL.cntx_psh();
            TKTL.use(form(8+TMAT.RKTL));
            TKTL.clear();
            {? TKTL.seek(BIT.sqlint(TMAT.RKTL),)
            || ktl:=TKTL.ref();
               xjm:=TKTL.XJM;
               nrk:=TKTL.NRK;
               wer:=TKTL.WER
            ?};
            TKTL.cntx_pop()
         ?}
      ?};
      matktl:=ktl; ~~}
    {IF: level+1<=maxlevel}
      {warbmast[level+1]:=TMAT.WARB; ~~}
      {techmast[level+1]:={? ktl<>null() || xjm || 0 ?}; ~~}
    {FI}
  {FI}
  {"-----------------------------------"; ~~}
  {"---Skok na nastepny poziom drzewa.                                    "; ~~}
  {level+=1; ~~}
  {"---Sprawdz, czy nie zostal przekroczony maksymalny poziom             "; ~~}
  {"---zagniezdzenia drzewa.                                              "; ~~}
  {IF: level>maxlevel-2}
    {FUN.emsg('Został przekroczony maksymalny poziom zagnieżdżenia\n'+
         'drzewa ('+form(maxlevel)+'). Zwiększ maksymalną wartość poziomu\n'+
         'zagnieżdżenia, używając zmiennej "maxlevel"'); ~~}
    {EXIT}
  {FI}
  {"-----------------------------------"; ~~}
  {TMAT.cntx_psh();
   _msk_tktl:=ref_name(matktl);
   _msk_tmat:=form((TMAT.name()-3)+(_msk_tktl+3));
   TMAT.use(_msk_tmat);
  ~~}
  {FOR: 'TMAT'}
    {INDEX: 'NL'}
    {PREFIX: matktl}
  {DO}
   {IF: TMAT.ACT='T' & tpar.calc(TMAT.EXIST)=1}
    { TMAT.cntx_psh(); dalej:=~TMAT.next(); TMAT.cntx_pop(); ~~}
    {"---Przeswit.                                                        "; ~~}
    {<2 getgaprow(level,0)}
    {{? ~dalej || poziomy[level]:='│' || poziomy[level]:=' ' ?};~~}
   {"---Linia z opisem KTM surowca.                                      "; ~~}
    {<2 getmatrow(level,dalej)
  }{{? ~(-TMAT.GRKTM)='G'
                  || TMAT.TGDFLT().PT().KTM+' - '+TMAT.SO
                  || TMAT.PT().KTM+' - '+TMAT.SO
                  ?}}
       {"---Zapotrzebowanie.                                                 "; ~~}
   {IF: lineleft<8}{<2 getgaprow(level,dalej)+'  '+'...'}{PAGE}{FI}
   {<2 getgaprow(level,dalej) + '  '+form('Ilość:',9)+form({? techmast[level]<>0
                                                      || warbmast[level]*(TMAT.WARB)/techmast[level]
                                                      || 0
                                                      ?})}
    {"---Technologia zwiazana z surowcem, jezeli jest zlozony.            "; ~~}
    { ktl:=null();
      xjm:=1;
      nrk:='';
      wer:='';
      {? TMAT.MAG='T'
      || ktl:=null()
      |? TMAT.DFLT_KTL='T'
      || ktl:=exec('dflt_ktl','tech_prod',{? TMAT.GRKTM='G' || TMAT.TGDFLT().PT || TMAT.PT ?});
         {? ktl<>null()
         || TKTL.cntx_psh();
            TKTL.use(ref_name(ktl));
            TKTL.clear();
            {? TKTL.seek(ktl)
            || xjm:=TKTL.XJM;
               nrk:=TKTL.NRK;
               wer:=TKTL.WER
            ?};
            TKTL.cntx_pop()
         ?}
      || {? TMAT.RKTL<>''
         || TKTL.cntx_psh();
            TKTL.use(form(8+TMAT.RKTL));
            TKTL.clear();
            {? TKTL.seek(BIT.sqlint(TMAT.RKTL),)
            || ktl:=TKTL.ref();
               xjm:=TKTL.XJM;
               nrk:=TKTL.NRK;
               wer:=TKTL.WER
            ?};
            TKTL.cntx_pop()
         ?}
      ?};~~}
    {IF: ktl<>null()}
    {<2 getgaprow(level,dalej) + '  '+form('KTL:',9)+nrk+'  wer:  '+ wer +' (XJM: '+form(xjm,,0,,'9')+')'}
    {FI}
    {"---Norma materialowa brutto.                                        "; ~~}
    {<2 getgaprow(level,dalej)+'  '+form('Norma b:',9)+form(TMAT.WARB,,0,,'9')}
    {"---Recurse.                                                         "; ~~}
    {IF: ktl<>null()}
      {SUBSTITUTE: 'prntree'}
    {FI}
   {FI}
  {OD}
  {TMAT.cntx_pop(); ~~}
  {"-----------------------------------"; ~~}
  {"---powrot do poprzedniego poziomu drzewa.                             "; ~~}
  {level-=1; ~~}
{MACRO-}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{"========================================================================"; ~~}
{"=========================== POCZATEK WYDRUKU ==========================="; ~~}
{"========================================================================"; ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Informacje naglowkowe.                                               "; ~~}
{<1 form('Karta technologiczna:')}{<25  TKTL.NRK}{'   wer:  '}{TKTL.WER}
{<1 form('Kalkulowana na:')}{<25 form(TKTL.XJM,,2)+' '+TKTL.JM().KOD}
{<1 form('Wyrób:')}{<25 TKTL.KTM().KTM+' - '+TKTL.KTM().N}
{<1 form('Domyślny wariant kalk.:')}{<25  {? form(TKTL.DEF_OPCK().NAZ)<>''
                                              || TKTL.DEF_OPCK().NAZ
                                              || '<brak>'
                                              ?}}
{<1 form('Opis:')}{<25  TKTL.OPIS}
{<1 form('Zatwierdzona końcowo:')}{<25  {? -TKTL.STAN='t' || 'TAK' || 'NIE' ?}}

{level:=0; ~~}
{<1 TKTL.KTM().KTM}
{<1 'Ilość: '+form(TKTL.XJM)}
{<1 'KTL: '+TKTL.NRK}
{SUBSTITUTE: 'prntree'}

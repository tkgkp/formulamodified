:!UTF-8
{TITLE: Lista uczestników}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','__TAB');
   tresc:='ppklucze';
   PAR_WYDR.TITLE:='Lista uczestników PPK'@;
   par:=obj_new(5);
   par[1]:=0;
   par[5]:=rep_export();
   _dt:=date();
   par[4]:=FUN.choice('Drukowanie listy uczestników'@,1,'Na podany dzień'@,'Zgłoszonych w okresie'@);
   undefine();
   define('HD',~~,'Drukowanie informacji'@);
   {? par[4]=1
   || VAR_EDIT.D_OD:={? PPK_UMO.OD>_dt || PPK_UMO.OD || _dt ?};
      define('OD',VAR_EDIT.D_OD,'Na dzień'@);
      _valid:=
         "  {? DEFINE.OD=date(0,0,0)
            || FUN.emsg('Data w polu \"Na dzień\" musi być różna od daty zerowej.'@);
               DEFINE.OD:=VAR_EDIT.D_OD;
               'OD'
            |? DEFINE.OD<PPK_UMO.OD
            || FUN.emsg('Data w polu \"Na dzień\" nie może być wcześniejsza niż data zawarcia umowy.'@);
               DEFINE.OD:=VAR_EDIT.D_OD;
               'OD'
            || 1
            ?}
         "
   |? par[4]=2
   || VAR_EDIT.D_OD:={? PPK_UMO.OD>_dt || PPK_UMO.OD || date(_dt~1,_dt~2,1) ?};
      VAR_EDIT.D_DO:=date(VAR_EDIT.D_OD~1,VAR_EDIT.D_OD~2,0);
      define('OD',VAR_EDIT.D_OD,'Od daty'@);
      define('DO',VAR_EDIT.D_DO,'Do daty'@);
      _valid:=
         "  {? DEFINE.OD=date(0,0,0)
            || FUN.emsg('Data w polu \"Od daty\" musi być różna od daty zerowej.'@);
               DEFINE.OD:=VAR_EDIT.D_OD;
               'OD'
            |? DEFINE.OD<PPK_UMO.OD
            || FUN.emsg('Data w polu \"Od daty\" nie może być wcześniejsza niż data zawarcia umowy.'@);
               DEFINE.OD:=VAR_EDIT.D_OD;
               'OD'
            |? DEFINE.DO<DEFINE.OD
            || FUN.emsg('Data w polu \"Do daty\" nie może być wcześniejsza niż data w polu \"Od daty\".'@);
               DEFINE.DO:={? VAR_EDIT.D_DO<DEFINE.OD || DEFINE.OD || VAR_EDIT.D_DO ?};
               'DO'
            || 1
            ?}
         "
   ?};
   {? par[4]
   || def_btn('text=%1,icon=xwin16.png:13'['OK'@],'key:F2');
      def_btn('text=%1,icon=xwin16.png:14'['Anuluj'@],'key:Esc');
      par[1]:=def_edit(_valid,'Lista uczestników PPK'@)
   ?};

   {? par[1]
   || {? par[4]=1
      || VAR_EDIT.D_OD:=DEFINE.OD;
         __TAB:=exec('list_ucz','ppk_rep',VAR_EDIT.D_OD)
      || VAR_EDIT.D_OD:=DEFINE.OD;
         VAR_EDIT.D_DO:=DEFINE.DO;
         __TAB:=exec('list_ucz','ppk_rep',VAR_EDIT.D_OD,VAR_EDIT.D_DO)
      ?};

      {? __TAB.first()
      || par[3]:=obj_new(@.CLASS.PRINT,9);
         {? ~par[5] || par[3].add("__TAB.LP",7,'Lp.',1,,,,,',,\'99\'') ?};
         par[3].add("__TAB.NAZWISKO+' '+__TAB.PIERWSZE+' '+__TAB.DRUGIE",30,'Uczestnik'@);
         par[3].add("__TAB.PESEL",11,'PESEL'@);
         par[3].add("__TAB.UR_DATA$1",10,'Data urodzenia'@);
         par[3].add("__TAB.TYPDOK",9,'Rodzaj dokumentu'@);
         par[3].add("__TAB.SYMDOK",15,'Symbol dokumentu'@);
         par[3].add("__TAB.ADRES",40,'Adres'@);
         par[3].add("__TAB.EMAIL",40,'e-mail'@);
         par[3].add("__TAB.DATA$1",10,'Data zgłoszenia'@);
         par[3].s_frame();

         prn1:='par[3]';
         lmt:=rsep:=0;
         par[2]:=vp:=h_rat:=1
      || par[1]:=0
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','__TAB');
   undefine()
}
{EXIT: ~par[1]}
{COMMENT}OLD: ppklucze.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[2]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? par[5] || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[2]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Lista uczestników '@}
   {IF: par[4]=1}
   {<{ceil(lmt*h_rat)+1}'w dniu: %1'@[VAR_EDIT.D_OD$1]}
   {ELSE}
   {<{ceil(lmt*h_rat)+1}'zgłoszonych w okresie: %1 - %2'@[VAR_EDIT.D_OD$1,VAR_EDIT.D_DO$1]}
   {FI}
{FI}

{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
   {SUBSTITUTE: 'LINIA0'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[3].width())/2); ~~ }
{FOR: __TAB}
{DO}
   {SUBSTITUTE: tresc}
{OD}
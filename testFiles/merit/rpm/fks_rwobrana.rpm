:!UTF-8
{TITLE:E. Zestawienie obrotów i sald kont rozrachunkowych}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,
   PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   _exp:=rep_export()>0;
   PRN1:=obj_new(@.CLASS.PRINT,13+_exp); PRN1.sl_frame(); w:=obj_new(13);
   PRN1A:=obj_new(@.CLASS.PRINT,8+_exp); PRN1A.sl_frame();
   PRN2:=obj_new(@.CLASS.PRINT,11+_exp); PRN2.sl_frame();
   PRN3:=obj_new(@.CLASS.PRINT,1); PRN3.sl_frame();
   bown:=boma:=okwn:=okma:=onwn:=onma:=sbown:=sboma:=sokwn:=sokma:=sonwn:=sonma:=0;
   kokwn:=kokma:=konwn:=konma:=skokwn:=skokma:=skonwn:=skonma:=0; zeskon_anasyn:=0;
   tbown:=tboma:=tokwn:=tokma:=tonwn:=tonma:=tkokwn:=tkokma:=tkonwn:=tkonma:=0;
   leng_tab_synanzest:=0;
   kanc:=nop:=vr:=vp:=vl:=l:=0;tuser:=tm_form(TMPVAT.tm_stamp);
   kan:=pokr:=p_odd:='';
   OKRO_F.index('ROK');
   OKRO_F.prefix(SSTALE.AO().ROK);
   {? OKRO_F.NR=0 || OKRO_F.next; datap:=OKRO_F.POCZ; datak:=OKRO_F.KON
   |? (OKRO_F.POCZ=date(0,0,0)) & (OKRO_F.NR<>0)
   || OKRO_F.prev; datap:=OKRO_F.POCZ; datak:=OKRO_F.KON
   || datap:=OKRO_F.POCZ; datak:=OKRO_F.KON
   ?};
   s_sign:=color:=prn1:=prn2:=''; lm:=ll:=vp:=lmt:=rsep:=0;
   fun:=""
}
{END:
  VAR_DEL.delete('PRN1','w','PRN1A','PRN2','PRN3','bown','boma','okwn','okma','onwn',
                 'onma','sbown','sboma','sokwn','sokma','sonwn','sonma','kokwn','kokma',
                 'konwn','konma','skokwn','skokma','skonwn','skonma','zeskon_anasyn');
  VAR_DEL.delete('tbown','tboma','tokwn','tokma','tonwn','tonma','tkokwn','tkokma','tkonwn',
                 'tkonma','kanc','nop','vr','vl','kan','pokr','p_odd','datap','datak','s_sign',
                 'color','prn1','prn2','lm','ll','tab_synanzest');
  VAR_DEL.delete('vp','lmt','rsep','fun','prn','l','s','wl','tuser','leng_tab_synanzest');
  exec('use_dok','fiksobi',SSTALE.AR().KOD,SSTALE.AR().KOD+form(SSTALE.AO().NR,-2))
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
===============================================================================
Nazwa pliku: rwobrana.rpm
Utworzony:    2005-07-13 [AMA]
===============================================================================
Zawartosc: Zestawienie obrotow i sald kont rozrachunkowych
{COMMENT-}
{COMMENT}
vr - znacznik raportu (czy są rekordy do drukowania)
vp - znacznik nowej strony
vl - znacznik ostatnio wydrukowanej linii (jaki obiekt)
{COMMENT-}
{  {? rep_export()
   || PRN1.add("OP.AN",35);
      PRN1A.add("w[1]",35,'Konto');
      PRN2.add("w[1]",35)
   ?};
   PRN1.add("w[1]",50);
   PRN1.add("w[2]",10);
   PRN1.add("w[3]",10);
   PRN1.add("w[4]",16,'Obroty Wn'@,1,'#');
   PRN1.add("w[5]",16,'Obroty Ma'@,1,'#');
   PRN1.add("w[6]",16,'Obroty Wn'@,1,'#');
   PRN1.add("w[7]",16,'Obroty Ma'@,1,'#');
   PRN1.add("w[8]",16,'Obroty Wn'@,1,'#');
   PRN1.add("w[9]",16,'Obroty Ma'@,1,'#');
   PRN1.add("w[10]",16,'Obroty Wn'@,1,'#');
   PRN1.add("w[11]",16,'Obroty Ma'@,1,'#');
   PRN1.add("w[12]",14,'Wn'@,1);
   PRN1.add("w[13]",14,'Ma'@,1);
   PRN1A.add("w[1]",50,'Rozrachunek'@);
   PRN1A.add("w[2]",10,{? rep_export() ||'Data otwarcia'@ || 'Data'@ ?});
   PRN1A.add("w[3]",10,{? rep_export() || 'Termin płatności'@ || 'Termin'@ ?});
   PRN1A.add("w[4]",33,'Bilans otwarcia'@,,,,,,,,2);
   PRN1A.add("w[5]",33,'Wybrane okresy'@,,,,,,,,2);
   PRN1A.add("w[6]",33,'Korekty'@,,,,,,,,2);
   PRN1A.add("w[7]",33,'Narastająco'@,,,,,,,,2);
   PRN1A.add("w[8]",29,'Saldo'@,,,,,,,,2);
   PRN2.add("w[1]",72,'');
   PRN2.add("w[2]",16,'',1);
   PRN2.add("w[3]",16,'',1);
   PRN2.add("w[4]",16,'',1);
   PRN2.add("w[5]",16,'',1);
   PRN2.add("w[6]",16,'',1);
   PRN2.add("w[7]",16,'',1);
   PRN2.add("w[8]",16,'',1);
   PRN2.add("w[9]",16,'',1);
   PRN2.add("w[10]",14,'',1);
   PRN2.add("w[11]",14,'',1);
   PRN3.add("w[1]",238);
   {! _a:=1..13 |! w[_a]:='' !};
   UNPAR.P1:=1;
   exec('p1_enable','!fks_roz_zazr');
   UNPAR.P1_BV:=UNPAR.P1_BE:='';
   UNPAR.P1_AE:='exec(\'p1_ae\',\'!fks_roz_zazr\')';
   KONTO.K1:=KONTO.K2:=KONTO.K3:=KONTO.K4:='';
   KONTO.K1_WYM:=KONTO.K2_WYM:=KONTO.K3_WYM:=KONTO.K4_WYM:=KONTO.K1_SYNT:=KONTO.K2_SYNT:=KONTO.K3_SYNT:=KONTO.K4_SYNT:=0;
   KONTO.K1_RODZ:=2; KONTO.K2_RODZ:=KONTO.K3_RODZ:=KONTO.K4_RODZ:=3;
   KONTO.K1_BE:='{? UNPAR.P1=1 || 1 || 0 ?}';
   KONTO.K2_BE:='{? UNPAR.P1=2 || 1 || 0 ?}';
   KONTO.K3_BE:=KONTO.K4_BE:='{? UNPAR.P1=3 || 1 || 0 ?}';
   KONTO.K1_AE:=KONTO.K2_AE:=KONTO.K3_AE:=KONTO.K4_AE:='';
   ROZNE.UT_OKROD:=ROZNE.UT_OKRDO:=SSTALE.AO;
   PAR_WYDR.win_edit('OBR_ANOP');
   WYDRUKIN.DLKON:=35; ~~}
{EXIT: ~PAR_WYDR.edit("
   {? ~ROZNE.UT_OKROD | ~ROZNE.UT_OKRDO | ROZNE.UT_OKROD().NR>ROZNE.UT_OKRDO().NR
   || FUN.info('Błędny zakres okresów obrachunkowych.'@); {? ~ROZNE.UT_OKRDO || 'UT_OKRDO' || 'UT_OKROD' ?}
   |? UNPAR.P1=6
   || _a:=__CHK.record2(ROZNE,'KON_WYDR','Zestaw kont');
      {? _a=''
      || KON_SCH.prefix(KON_WYDR.ref);
         _a:={? KON_SCH.first()
             || ''
             || FUN.info('Wybrany zestaw kont nie ma zdefiniowanej definicji kont.'@); 'KON_WYDR'
             ?}
      ?}
   || _t:='Błędny zakres kont!'@;
      {? UNPAR.P1=3 & (KONTO.K3='' | (KONTO.K4<>'' & KONTO.K3>KONTO.K4)) || FUN.info(_t); 'K3'
      |? UNPAR.P1=3 & KONTO.K4='' || FUN.info(_t); 'K4'
      || ''
      ?}
   ?}
   ")}
   {WYDRUKI.MASKA:=KONTO.K1;
   {? WYDRUKI.MASKA=''
   || WYDRUKI.MASKA:=35*'?'
   || {? +WYDRUKI.MASKA<WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?}
   ?};
   {? ROZNE.UT_OKROD=ROZNE.UT_OKRDO
   || pokr:='Okres obrachunkowy: '+ROZNE.UT_OKROD().NAZ+' '+SSTALE.AR().NAZ;
      {? ROZNE.UT_OKROD().ZAM='T'
      || pokr+=' - okres zamknięty'@
      || pokr+=' - okres otwarty'@
      ?}
   || pokr:='Okresy obrachunkowe: '+ROZNE.UT_OKROD().NAZ+' - '+ROZNE.UT_OKRDO().NAZ+' '+SSTALE.AR().NAZ
   ?};
   {? OPERATOR.DEPT
   || p_odd:='Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
   || p_odd:='Wszystkie jednostki księgowe'@
   ?};
   PAR_WYDR.TITLE:='Zestawienie obrotów i sald kont rozrachunkowych'@;
   prn1:='PRN1'; prn2:='PRN2'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}-----------------------------------MACRO-TABHEADM-------------------------------{COMMENT-}
{COMMENT}
Makro używane do drukowania nagłówka tabeli
{COMMENT-}
{MACRO: 'TABHEADM'}
{ PRN1A.setcolor(color); ~~}
{ PRN1A.ini_head()}
{REP: PRN1A.fhbar(lmt)}
{WHILE: PRN1A.h_next()}
{DO}
   {REP: PRN1A.h_fmline(lmt)}
{OD}
{IF: ~rep_export()}
{<{lmt+1}'│'}{COLOR: '0:0:0,'+color}{50*' '}{'│'}{'  otwarcia'@}{'│'}{' płatności'@}{'├'}{16*'─'}{
'┬'}{16*'─'}{'┼'}{16*'─'}{'┬'}{16*'─'}{'┼'}{16*'─'}{'┬'}{16*'─'}{'┼'}{16*'─'}{'┬'}{16*'─'}{'┼'}{
14*'─'}{'┬'}{14*'─'}{'┤'}{COLOR: '255:255:255,255:255:255'}
{ELSE}
{REP: PRN1A.fjbar(PRN1)}
{FI}
{ PRN1.setcolor(color); ~~}
{ PRN1.ini_head()}
{WHILE: PRN1.h_next()}
{DO}
   {REP: PRN1.h_fmline(lmt)}
{OD}
{MACRO-}
{COMMENT}-----------------------------------MACRO-LINE1----------------------------------{COMMENT-}
{MACRO: 'LINE1'}
{COMMENT}
Makro do drukowania wiersza rekordu
{COMMENT-}
{IF: lineleft()<3 & lineleft()<>0}{PAGE}{  vp:=prn:=1; ~~}{FI}
{IF: prn}
{ prn1:=prn2:='PRN1'; ~~}
{ELIF: s=1}
{ prn1:=prn2:='PRN1'; ~~}
{ELIF: s=3}
{ prn1:='PRN3'; prn2:='PRN1'; ~~}
{FI}
{  fun:="prn:=0; s:=1; rsep:=PAR_WYDR.RSEP"; ~~}
{SUBSTITUTE: 'LINIA02'}
{MACRO-}
{COMMENT}-----------------------------------MACRO-LINE2----------------------------------{COMMENT-}
{MACRO: 'LINE2'}
{COMMENT}
Makro do drukowania wiersza podsumowania
{COMMENT-}
{IF: lineleft()<3 & lineleft()<>0}{PAGE}{  vp:=prn:=1; ~~}{FI}
{IF: prn | s=1}
{ prn1:='PRN1'; prn2:='PRN2'; ~~}
{ELIF: s=2}
{ prn1:=prn2:='PRN2'; ~~}
{ELIF: s=3}
{ prn1:='PRN3'; prn2:='PRN2'; ~~}
{FI}
{  fun:="prn:=0; s:=2; rsep:=PAR_WYDR.RSEP"; ~~}
{SUBSTITUTE: 'LINIA02'}
{MACRO-}
{COMMENT}-----------------------------------MACRO-LINE3----------------------------------{COMMENT-}
{MACRO: 'LINE3'}
{COMMENT}
Makro do drukowania wiersza naglowka konta analitycznego
{COMMENT-}
{IF: lineleft()<3 & lineleft()<>0}{PAGE}{  vp:=prn:=1; ~~}{FI}
{IF: prn | s=1}
{ prn1:='PRN1'; prn2:='PRN3'; ~~}
{ELIF: s=2}
{ prn1:='PRN2'; prn2:='PRN3'; ~~}
{FI}
{  fun:="prn:=0; s:=3; rsep:=PAR_WYDR.RSEP"; ~~}
{SUBSTITUTE: 'LINIA02'}
{MACRO-}
{COMMENT}-----------------------------------SIGNATURE------------------------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}-----------------------------------REPORT-HEADER--------------------------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN1.width())/2); wl:=(charleft()/2)$0; ~~}
{<1 'PARAMETRY WYDRUKU:'@}
{<1 pokr}
{<1 'Waluta: %1 - %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{IF: UNPAR.P1=1}
{<1 'Maska konta: %1'@[WYDRUKI.MASKA]}
{ELIF: UNPAR.P1=2}
{<1 'Prefix konta: %1'@[KONTO.K2]}
{ELIF: UNPAR.P1=3}
{<1 'Zakres kont: od %1 do %2'[KONTO.K3,KONTO.K4]}
{ELIF: UNPAR.P1=4}
{<1 'Wybrane konta syntetyczne'}{_okn:=KS.win_sel('?');
                                 TMPSIK.cntx_psh();
                                 exec('initmpks','dok_fks_zest');
                                 KS.select();
                                 KS.win_sel(_okn);
                                 {? TMPSIK.first()
                                 || _sql_war3:='select distinct OP.AN AN from OP where OP.AN like \'' + TMPSIK.T2 + '%\' ';
                                    {!|? TMPSIK.next()
                                      |! _sql_war3+='or OP.AN like \'' + TMPSIK.T2 + '%\' '
                                    !};
                                    _tab_synanzest:=sql(_sql_war3);
                                    leng_tab_synanzest:=_tab_synanzest.size();
                                    tab_synanzest:=obj_new(leng_tab_synanzest);
                                    _tab_synanzest.first();
                                    {! _ii:=1// 1 .. leng_tab_synanzest
                                    |! tab_synanzest[_ii] := _tab_synanzest.AN;
                                       _tab_synanzest.next()
                                    !}
                                 ?};
                                 TMPSIK.cntx_pop();~~}
{ELIF: UNPAR.P1=5}
{<1 'Wybrane konta analityczne'} {_okn:=AN.win_sel('?');
                                 TMPSIK.cntx_psh();
                                 exec('initmpan','dok_fks_zest');
                                 AN.select();
                                 AN.win_sel(_okn);
                                 {? TMPSIK.first()
                                 || leng_tab_synanzest:=TMPSIK.size();
                                    tab_synanzest:=obj_new(leng_tab_synanzest);
                                    {! _ii:=1// 1 .. leng_tab_synanzest
                                    |! tab_synanzest[_ii] := TMPSIK.T2;
                                       TMPSIK.next()
                                    !}
                                 ?};
                                 TMPSIK.cntx_pop();~~}
{ELIF: UNPAR.P1=6}
{<1 'Zestaw kont: %1'@[ROZNE.KON_WYDR().KOD]}{ KON_SCH.cntx_psh();
                                               KON_SCH.index('IND_LP');
                                               KON_SCH.prefix(ROZNE.KON_WYDR);
                                               {? KON_SCH.first()
                                               || _sql_war3:='';
                                                  {? KON_SCH.MZ = 'M'
                                                  || {! |?
                                                     {? KON_SCH.MASKA+1='?' || KON_SCH.MASKA:=KON_SCH.MASKA-1 ?};
                                                     KON_SCH.MASKA+1='?'
                                                     !};
                                                     KON_SCH.MASKA:=KON_SCH.MASKA+'%';
                                                     KON_SCH.MASKA:=STR.gsub( KON_SCH.MASKA,'?','_');
                                                     _sql_war3+='select distinct OP.AN AN from OP where OP.AN like \''+KON_SCH.MASKA+'\''
                                                  || _sql_war3+='select distinct OP.AN AN from OP where (OP.AN between \''+KON_SCH.ZOD+'\' and \''+KON_SCH.ZDO+%255+'\')'
                                                  ?};
                                                  {!|? KON_SCH.next()
                                                  |! {? KON_SCH.MZ = 'M'
                                                     || {!|?
                                                        {? KON_SCH.MASKA+1='?' || KON_SCH.MASKA:=KON_SCH.MASKA-1 ?};
                                                           KON_SCH.MASKA+1='?'
                                                        !};
                                                        KON_SCH.MASKA:=KON_SCH.MASKA+'%';
                                                        KON_SCH.MASKA:=STR.gsub( KON_SCH.MASKA,'?','_');
                                                        _sql_war3+=' or OP.AN like \''+KON_SCH.MASKA+'\''
                                                     || _sql_war3+=' or OP.AN between (\''+KON_SCH.ZOD+'\' and \''+KON_SCH.ZDO+%255+'\')'
                                                     ?}
                                                  !};
                                                  _tab_synanzest:=sql(_sql_war3);
                                                  leng_tab_synanzest:=_tab_synanzest.size();
                                                  tab_synanzest:=obj_new(leng_tab_synanzest);
                                                  _tab_synanzest.first();
                                                  {! _ii:=1// 1 .. leng_tab_synanzest
                                                  |! tab_synanzest[_ii] := _tab_synanzest.AN;
                                                     _tab_synanzest.next()
                                                  !}
                                               ?};
                                               KON_SCH.cntx_pop();~~}
{FI}
{<1 p_odd}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEADM'}{FONT: 'T8G'}{ vp:=prn:=1; ~~}
{COMMENT}-----------------------------------HEADER---------------------------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEADM'}
{HEADER-}
{COMMENT}-----------------------------------FOOTER---------------------------------------{COMMENT-}
{FOOTER: 0}
{IF: s=1}{<{lmt+1}}{PRN1.lbar()}{ELIF: s=2}{<{lmt+1}}{PRN2.lbar()}{ELIF: s=3}{<{lmt+1}}{PRN3.lbar()}{FI}{  vp:=1; ~~}
{FOOTER-}
{COMMENT}-----------------------------------WYDRUK---------------------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
   KS.index('SYM'); KS.prefix(BILANSP.AROK);
   _v:=KONTO.K2;
   {? KONTO.K1<>'' & (KONTO.K1*'?'>1)
   || _v:=((KONTO.K1*'?')-1)+KONTO.K1
   |? KONTO.K1<>'' & KONTO.K1*'?'=0
   || _v:=KONTO.K1
   ?};
   {? OPERATOR.DEPT
   || OP.index('PRZEG_O'); OP.prefix(SSTALE.WAL,OPERATOR.DEPT,_v)
   || OP.index('PRZEG'); OP.prefix(SSTALE.WAL,_v)
   ?};
   l:=OP.first(); ~~}
{WHILE: l}
{DO}
   {IF: (UNPAR.P1=1 & exec('mask','konto',OP.AN)) | UNPAR.P1=2 | (UNPAR.P1=3 & OP.AN>=KONTO.K3 & OP.AN<=KONTO.K4) |
     ((UNPAR.P1=4 | UNPAR.P1=5 | UNPAR.P1=6) & ({? leng_tab_synanzest > 0 || {! _ii:=1// 1 .. leng_tab_synanzest |! {? tab_synanzest[_ii] = OP.AN || return(1) ?} !}; return(0) ?}))}
      {  _od:=ROZNE.UT_OKROD().NR; _do:=ROZNE.UT_OKRDO().NR;
         F.RObr3(OP.ref(),_od,_do);
         bown:=F.rtwn; boma:=F.rtma;
         okwn:=F.rwn; okma:=F.rma;
         onwn:=F.wn; onma:=F.ma;
         kokwn:=F.rwwn+F.dwn; kokma:=F.rwma+F.dma;
         konwn:=F.dwn; konma:=F.dma; ~~}
      {IF: bown | boma | okwn | okma | kokwn | kokma | onwn | onma}
         {IF: OP.AN<>kan}
            {  rsep:=1;
               {? KS.find_key(BILANSP.ASYNT+OP.AN)
               || BUD.cntx_psh(); exec('rpm_clear','dok_fks'); synt:=OP.AN;
                  KOM.index('KS'); KOM.prefix(); {? KOM.find_key(KS.ref) || exec('rpm_display','dok_fks') ?};
                  _sbud:=BUD.size(); BUD.cntx_pop();
                  {? _sbud
                  || w[1]:=($('KA.P'+$(_sbud)+'().TR'))(); w[1]:=OP.AN+' - '+w[1];
                     RS.index('RS'); RS.prefix();
                     {! |?
                        {? RS.find_key(($('KA.P'+$(_sbud)+'().SLU().WZ'))())
                        || {? RS.TAB='KH'
                           || KH.index('KOD'); KH.prefix(2);
                              {? KH.find_key(($('KA.P'+$(_sbud)+'().KOD'))())
                              || w[1]:=OP.AN+' - '+KH.SKR+' - '+KH.NAZ; _sbud:=0
                              || _sbud-=1
                              ?}
                           || _sbud-=1
                           ?}
                        || _sbud-=1
                        ?};
                        _sbud
                     !}
                  || w[1]:=OP.AN+' - '+KS.NAZ
                  ?}
               ?}; ~~}
            {IF: ~rep_export()}{SUBSTITUTE: 'LINE3'}{FI}
            { rsep:=1; ~~}
         {FI}
         {  vr:=1;
            w[1]:=OP.SYM;
            w[2]:=$OP.DO;
            w[3]:=$OP.TZ;
            w[4]:=form(bown,,2,' ,'); w[5]:=form(boma,,2,' ,');
            w[6]:=form(okwn,,2,' ,'); w[7]:=form(okma,,2,' ,');
            w[8]:=form(kokwn,,2,' ,'); w[9]:=form(kokma,,2,' ,');
            w[10]:=form(onwn,,2,' ,'); w[11]:=form(onma,,2,' ,');
            w[12]:=form(F.SWn(onwn,onma),,2,' ,'); w[13]:=form(F.SMa(onwn,onma),,2,' ,');
            sbown+=bown; sboma+=boma; sokwn+=okwn; sokma+=okma; sonwn+=onwn; sonma+=onma;
            skokwn+=kokwn; skokma+=kokma; skonwn+=konwn; skonma+=konma;
            tbown+=bown; tboma+=boma; tokwn+=okwn; tokma+=okma; tonwn+=onwn; tonma+=onma;
            tkokwn+=kokwn; tkokma+=kokma; tkonwn+=konwn; tkonma+=konma; ~~}
         {SUBSTITUTE: 'LINE1'}
         {  kan:=OP.AN; nop+=1; ~~}
         {  bown:=boma:=okwn:=okma:=onwn:=onma:=kokwn:=kokma:=konwn:=konma:=0; ~~}
      {FI}
   {FI}
   {  {? OP.next() || {? OP.AN=kan || kanc:=0 || kanc:=1 ?}; OP.prev() || kanc:=1 ?}; ~~}
   {IF: kanc & nop & ~rep_export()}
      {  rsep:=1;
         w[1]:='Razem: '+kan;
         w[2]:=form(sbown,,2,' ,'); w[3]:=form(sboma,,2,' ,');
         w[4]:=form(sokwn,,2,' ,'); w[5]:=form(sokma,,2,' ,');
         w[6]:=form(skokwn,,2,' ,'); w[7]:=form(skokma,,2,' ,');
         w[8]:=form(sonwn,,2,' ,'); w[9]:=form(sonma,,2,' ,');
         w[10]:=form(F.SWn(sonwn,sonma),,2,' ,'); w[11]:=form(F.SMa(sonwn,sonma),,2,' ,'); ~~}
      {SUBSTITUTE: 'LINE2'}
      {  w[1]:='Razem wg rozrachunkowych zap. księgowych'@;
         w[2]:=form(sbown,,2,' ,'); w[3]:=form(sboma,,2,' ,');
         w[4]:=form(sokwn+skokwn,,2,' ,'); w[5]:=form(sokma+skokma,,2,' ,');
         w[6]:=w[7]:='';
         w[8]:=form(sonwn+skonwn,,2,' ,'); w[9]:=form(sonma+skonma,,2,' ,');
         w[10]:=form(F.SWn(sonwn+skonwn,sonma+skonma),,2,' ,');
         w[11]:=form(F.SMa(sonwn+skonwn,sonma+skonma),,2,' ,'); rsep:=0; ~~}
      {SUBSTITUTE: 'LINE2'}
   {FI}
   {  {? kanc
      || nop:=0; sbown:=sboma:=sokwn:=sokma:=sonwn:=sonma:=skokwn:=skokma:=skonwn:=skonma:=0
      ?}; ~~}
   {  l:=OP.next(); ~~}
{OD}
{COMMENT}-----------------------------------FOOTER_LAST------------------------------------------------------{COMMENT-}
{IF: lineleft()<5 & lineleft()<>0}{PAGE}{ prn1:='PRN1'; vp:=1; ~~}{FI}
{FOOTER}{FOOTER-}
{IF: vr & ~rep_export()}
{  w[1]:='Suma';
   w[2]:=form(tbown,,2,' ,');
   w[3]:=form(tboma,,2,' ,');
   w[4]:=form(tokwn,,2,' ,');
   w[5]:=form(tokma,,2,' ,');
   w[6]:=form(tkokwn,,2,' ,');
   w[7]:=form(tkokma,,2,' ,');
   w[8]:=form(tonwn,,2,' ,');
   w[9]:=form(tonma,,2,' ,');
   w[10]:=form(F.SWn(tonwn,tonma),,2,' ,');
   w[11]:=form(F.SMa(tonwn,tonma),,2,' ,'); ~~}
{ENTIRE}
{IF: vp}{ PRN1.setcolor(color); ~~}{FI}
{IF: vp | s=1}{REP: PRN1.fjbar(PRN2,lmt)}{ELIF: s=2}{REP: PRN2.fbar(lmt)}{FI}
{ PRN2.setcolor('255:255:255'); ~~}
{PRN2.ini_line()}
{WHILE: PRN2.next()}
{DO}
{REP: PRN2.fline(lmt)}
{OD}
{  w[1]:='Razem wg rozrachunkowych zap. księgowych'@;
   w[2]:=form(tbown,,2,' ,');
   w[3]:=form(tboma,,2,' ,');
   w[4]:=form(tokwn+tkokwn,,2,' ,');
   w[5]:=form(tokma+tkokma,,2,' ,');
   w[6]:=w[7]:='';
   w[8]:=form(tonwn+tkonwn,,2,' ,');
   w[9]:=form(tonma+tkonma,,2,' ,');
   w[10]:=form(F.SWn(tonwn+tkonwn,tonma+tkonma),,2,' ,');
   w[11]:=form(F.SMa(tonwn+tkonwn,tonma+tkonma),,2,' ,'); ~~}
{PRN2.ini_line()}
{WHILE: PRN2.next()}
{DO}
{REP: PRN2.fline(lmt)}
{OD}{ vp:=0; ~~}
{FI}
{IF: ~rep_export()}
{IF: vp}{PRN1.setcolor(color); ~~}{REP: PRN1.flbar(lmt)}{ELSE}{<{lmt+1} PRN2.lbar()}{FI}
{<{lmt+1}>{PRN2.width()+lmt} 'KONIEC WYDRUKU'@}
{FI}
{ENTIRE-}
{IF: rep_export()}{TABLE-}{FI}
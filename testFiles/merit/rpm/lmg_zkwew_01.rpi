:!UTF-8
{FONT: 'T8'}{SPACE: 'B'}
{
"--szerokosc kolumn--";
   ddokl:=0;
   VAR_DEL.delete('X_Xw1');
   X_Xw1:=tab_tmp(1
      ,'P'  ,'INTEGER'     ,''
      ,'M'  ,'STRING[160]' ,''
      ,'IL' ,'REAL'        ,''
      ,'J'  ,'STRING[10]'  ,''
      ,'ZL' ,'STRING[20]'  ,''
      ,'DT' ,'DATE'        ,''
      ,'REF','STRING[16]'  ,'');

   ZK_P.index('NAG'); ZK_P.prefix(ZK_N.ref());
   {? ZK_P.first
   || {!
      |? _pop:=((+form((frac(ZK_P.IL2))))-2);
         {? _pop>0 & _pop>ddokl || ddokl:=_pop ?};
         X_Xw1.clear();
         X_Xw1.blank();
         X_Xw1.P:=ZK_P.POZ;
         X_Xw1.M:=ZK_P.M().KTM+' - '+M.N;
         X_Xw1.IL:=ZK_P.IL2;
         X_Xw1.J:=ZK_P.J2().KOD;
         X_Xw1.REF:=$ZK_P.ref();
         X_Xw1.ZL:=ZK_P.ZL().SYM;
         X_Xw1.DT:=ZK_P.DT;
         X_Xw1.add(1);
         ZK_P.next
      !}
   ?};

   lmt:=charleft()-4;
   kk[1]:=3;                           pp[1] :='X_Xw1.P';
   kk[2]:=5;                           pp[2] :='form(X_Xw1.IL,,ddokl,\' ,\')';
   kk[3]:=2;                           pp[3] :='X_Xw1.J';
   kk[4]:=0;                           pp[4] :='X_Xw1.M';
   kk[5]:=0;                           pp[5] :='X_Xw1.ZL';
   kk[6]:=10;                          pp[6]:='form(X_Xw1.DT)';

   _len:=obj_len(kk);
   _buf:=obj_new(_len); {! _ii.._len |! _buf[_ii]:=0 !};
   _max:=obj_new(_len); {! _ii.._len |! _max[_ii]:=0 !};

   X_Xw1.clear();
   {? X_Xw1.first()
   || {!
      |? {! _ii.._len
         |! _war:=($pp[_ii])();
            _typ:=type_of(_war);
            {? _typ=1 || _w:=(+form(_war));
                         {? _max[_ii]<_w || _max[_ii]:=_w; _buf[_ii]:=_w ?}
            |? _typ=2 || _w:=(+_war);
                         {? _max[_ii]<_w || _max[_ii]:=_w; _buf[_ii]:=_w ?}
            |? _typ=4 || _buf[_ii]:=10
            |? _typ=5 || _buf[_ii]:=5
            ?}
         !};
         X_Xw1.next()
      !}
   ?};

   _ods:=-3; _all:=0;
   {! _ii.._len
   |! {? kk[_ii]<0            || kk[_ii]:=-kk[_ii]
      |? kk[_ii]<=_buf[_ii]   || kk[_ii]:=_buf[_ii]
      ?};
      _all+=kk[_ii]; {? kk[_ii]>0 || _ods+=3 ?}
   !};
   kk[4]:=(lmt-(_all-kk[4])-_ods); {? kk[4]<0 || FUN.error('Błąd'@) ?};
   obj_del(_buf); obj_del(_max);

   PRN.add("ww[1]",kk[1],'Lp.'@,1);
   PRN.add("ww[4]",kk[4],'Materiał/Usługa'@);
   PRN.add("ww[6]",kk[6],'Termin realizacji'@);
   PRN.add("ww[2]",kk[2],'Ilość'@,1);
   PRN.add("ww[3]",kk[3],'jm'@,1);
   {? kk[5] || PRN.add("ww[5]",kk[5],'Zlecenie'@,1) ?}
;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{
   lmt:=int((charleft()-PRN.width())/2);
   PAR_WYDR.TITLE:='Zamówienie Nr '+ZK_N.SYM+'\n z dnia '+$ZK_N.DP;
   prn1:='PRN'
;~~}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{ns:=0;~~}
{COMMENT}   dokument anulowany                                       {COMMENT-}
{FONT: 'T8'}{SPACE: 'C'}
{IF: ZK_N.STAT_REJ='A'}{<1}{>{charleft}exec('tr','tlumaczenia','1000215')}{FI}
{FONT: 'T10'}{SPACE: 'C'}
{LINE: 2}
{COMMENT}   termin realizacji                                        {COMMENT-}
{<{lmt+1}'Termin realizacji: '+$ZK_N.DT}
{COMMENT}   forma platnosci                                          {COMMENT-}
{IF: ZK_N.PL<>null}
   {LINE}
   {<{lmt+1}'Forma płatności: '+ZK_N.PL().TR}
{FI}
{COMMENT}   warunki dostawy                                          {COMMENT-}
{IF: ZK_N.ZDSWD<>null}
   {LINE}
   {<{lmt+1}'Warunki dostawy: '+ZK_N.ZDSWD().KOD+', '+ZDSWD.OPIS}
{FI}
{COMMENT}   informacje dodatkowe dla naglowka dokumentu              {COMMENT-}
{
   prn1:='PRN';
   'pobieram dane'; exec('pobgdnag','fakso','Z',ZK_N.ref);
   VAR_DEL.delete('oo','OPIK')
;~~}
{FOR: S_GD}
{PREFIX: 'G'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   naglowek tabeli                                          {COMMENT-}
{  prn1:='PRN';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {IF: ~ns}
   {SUBSTITUTE: 'HEAD'}
   {IF: zakoncz}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
   {FI}
   {FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{
rsep:=PAR_WYDR.RSEP;
FAKSO.cntx_psh();
FAKSO.use('fakto' + (ZK_N.name() + 3));
lp:=0
;~~}
{FOR:'ZK_P'}{INDEX: 'TYPN'}{PREFIX: ZK_N.A,'Z',ZK_N.ref,1}
{DO}
   {
      lp+=1;
      rsep:=PAR_WYDR.RSEP;
      ww[1]:=form(lp);
      ww[4]:=form(ZK_P.M().KTM+' '+ZK_P.M().N);
      ww[2]:=form(ZK_P.IL2,,ddokl,'.,');
      ww[3]:=ZK_P.J2().KOD;
      ww[5]:=ZK_P.ZL().SYM;
      ww[6]:=form(ZK_P.DT);

      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))() + 2
   ;~~}
   {IF: lineleft<=il_lin}
      {SUBSTITUTE: 'LINIAF'}{PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
{COMMENT}   informacje dodatkowe dla pozycji dokumentu               {COMMENT-}
   {FOR: FAKSO}{INDEX: 'ZK_P'}{PREFIX: ZK_P.ref}
   {DO}
      {FIRST}
         {
         VAR_DEL.delete('w_tmp');
         w_tmp:=obj_new(obj_len(ww));
         {! _ii..obj_len(ww) |! w_tmp[_ii]:=ww[_ii] !}
         ;~~}
      {FIRST-}
      { {! _ii..obj_len(ww) |! ww[_ii]:='' !};
        ww[4]:=exec('printFAKSO','fakso',PARAM_W.JEZYK);
        rsep:=0; _f:=prn1+'.ini_line()'; ($(_f))();~~}
      {WHILE: _f:=prn1+'.next()'; ($(_f))()}
      {DO}
         {IF: lineleft()>0 & lineleft()<4}
            { bpage:=page(); {! _ii..obj_len(ww) |! ww[_ii]:=w_tmp[_ii] !};~~}
            {SUBSTITUTE: 'LINIAF'}
            {IF: bpage=page}{PAGE}{FI}
            { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
            {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
            { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
         {FI}
         {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
      {OD}
      {TOTAL}
         { VAR_DEL.delete('w_tmp'); vp:=0;~~}
   {OD}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{
FAKSO.cntx_pop();
rsep:=1
;~~}
{SUBSTITUTE: 'LINIAF'}
{  zakoncz:=0; ~~}
{COMMENT}   informacje dodatkowe pod dokumentem              {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: S_GD}
{PREFIX: 'D'}
{DO}
   {FIRST}
      {LINE}
   {FIRST-}
   {SUBSTITUTE: 'inf_dod'}
{OD}
{COMMENT}   podpisy                                                  {COMMENT-}
{LINE: 3}
{FONT: 'HEAD'}{SPACE: 'B'}
{IF: ZK_N.US<>null}
   {
   VAR_DEL.delete('PRN');
   prn1:='PRN';
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.n_frame();
   PRN.add("",charleft-50-3,'');
   PRN.add("",50,ZK_N.US().DANE);
   _f:=prn1+'.ini_head()'; ($(_f))();
   _f:=prn1+'.h_next()'; ($(_f))()
   ;~~}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{FI}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:=28*'─';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{
_szer:=20;
PRN.HEAD[1]:='';
PRN.HEAD[2]:='Zamówienie przyjął';
_f:=prn1+'.ini_head()'; ($(_f))();
_f:=prn1+'.h_next()'; ($(_f))()
;~~}
{REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
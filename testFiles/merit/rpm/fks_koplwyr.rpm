:!UTF-8
{TITLE:C.  Konta syntetyczne z wyróżnikami}
{PRINTER: FINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN:   PRN:=obj_new(@.CLASS.PRINT,7); PRN.sl_frame();
          w:=obj_new(7); s:=0; KS.cntx_psh();mdc:=''; lm:=0}
{END: obj_del(w); obj_del(PRN); &s; KS.cntx_pop(); &mdc; &lm}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: koplwyr.rpm
Utworzony:    2000-10-27 [AMA] Arkadiusz Maczka
==================================================
Zawartosc: Konta syntetyczne z wyroznikami.
{COMMENT-}
{PRN.add("w[1]", 15, '     Konto'@);
 PRN.add("w[2]", 60, 27*' '+'Nazwa'@);
 PRN.add("w[3]", 20, ' Słownik wyróżnika'@);
 PRN.add("w[4]", 7, 'Analit.'@);
 PRN.add("w[5]", 5, 'Uzup.'@);
 PRN.add("w[6]", 8, 'Wartość'@);
 PRN.add("w[7]", 6, 'Rozdz.'@);
 w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:='0';~~}
{IF: KS.index('SYM'); KS.prefix(ROK_F.ref); ~KS.first}
{ FUN.info('Brak kont syntetycznych.'@);  ~~}
{EXIT}
{FI}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='KONTA SYNTETYCZNE Z WYRÓŻNIKAMI'@;~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}
{<1 'PARAMETRY WYDRUKU:'@}
{<1 'Rok obrotowy: %1'@[BILANSP.AROK().NAZ]}
{FONT: 'R'}{SPACE: 'B'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FONT: 'Q'}{SPACE: 'C'}
{PRN.lbar()}
{SUBSTITUTE: 'CHARFOOT'}
{FONT: 'Q'}{SPACE: 'C'}
{FOOTER-}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{ENTIRE}
{PRN.bar()}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{ENTIRE-}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{FOR: 'KS'}
{INDEX: 'SYM'}
{PREFIX: ROK_F.ref()}
{DO}
   {FOR: 'KS_W'}
   {INDEX: 'KSSLU'}
   {PREFIX: KS.ref()}
   {DO}
   { s:=1;
     w[1]:=KS_W.S().SYM;
     w[2]:=KS_W.S().NAZ;
     w[3]:=KS_W.SLU().SLU().NAZ;
     w[4]:=KS_W.AN;
     w[5]:=KS_W.WYM;
     w[6]:=KS_W.ST().KOD;
     w[7]:=KS_W.ROZDZ;
     ~~}
   {SUBSTITUTE: 'LINIA'}
   {OD}
{OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{IF: s}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ELSE}
{PRN.lbar()}
{<1>{PRN.width()} 'BRAK REKORDÓW SPEŁNIAJĄCYCH KRYTERIA!'@}
{FI}

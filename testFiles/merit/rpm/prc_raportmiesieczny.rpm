:!UTF-8
{TITLE: Raport miesięczny}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','pb','Format','Nr','Karta','Teczka','Pracownik');
   VAR_DEL.delete('NormPrac','PracPrac','GodzSw','GodzNadm','Wydzial','Uwagi','Ok','Ppsf');

   _TAB:=exec('tab_prac','!prc_czp_wydr');
   _sql2:=''+" select * from P join UD_SKL join :_a using (P.REFERENCE, :_a.SQL) order by UD_SKL.SYMBOL";
   _TAB2:=sql(_sql2,_TAB);
   {? _TAB.first()
   || tresc:='w_rapms';
      PAR_WYDR.TITLE:='Raport miesięczny'@;

      PRN:=obj_new(@.CLASS.PRINT,11);
      {? rep_export()
      || PRN.add("P.WYDZIAL().SYMBOL+' '+|UD_SKL.OPIS",40,'Jednostka organizacyjna'@)
      || PRN.add("Nr+=1",5,'Lp.'@,1,,,,,',,\'9.\'')
      ?};
      PRN.add("Karta",-8,'Karta'@);
      PRN.add("Teczka",9,'Nr teczki'@,1);
      {? ~rep_export() || PRN.add("P.WYDZIAL().SYMBOL",13,'Jednostka organizacyjna'@) ?};
      PRN.add("Pracownik",28,'Nazwisko i imię'@);
      PRN.add("Format(NormPrac)",12,'Norma'@,1);
      PRN.add("Format(PracPrac)",12,'Przepracował'@,1);
      PRN.add("Format(GodzSw)",12,'Święta'@,1);
      PRN.add("Format(GodzNadm)",12,'Godziny nadmiaru'@,1);
      PRN.add("Uwagi",30,'Wyjaśnienia'@,1);
      PRN.add("form(Ppsf)",29,'Praca poza siedzibą firmy'@,1);
      PRN.s_frame();

      prn1:='PRN';
      rsep:=PAR_WYDR.SEP;

      lmt:=Nr:=pb:=Wydzial:=Wydzial:=Ok:=0;
      vp:=h_rat:=1;
      Karta:=Teczka:=Pracownik:=NormPrac:=PracPrac:=GodzSw:=GodzNadm:=Uwagi:=Ppsf:='';
      Format:=
         "  {? _a=' 00:00'
            || ''
            || _a
            ?}
         "
   ?};
   params_set('P',_TAB,'PT',_TAB2);

   R_PRACDN.cntx_psh();
   R_KARHIS.cntx_psh();
   R_KARHIS.index('PR_DATA');
   KAL_DEF.cntx_psh();
   KAL_DEF.index('KAL_DATA');
   R_WZCZ.cntx_psh();
   R_WZCZ.index('R_WZCZ')
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','pb','Format','Nr','Karta','Teczka','Pracownik');
   VAR_DEL.delete('NormPrac','PracPrac','GodzSw','GodzNadm','Wydzial','Uwagi','Ok','Ppsf');
   R_WZCZ.cntx_pop();
   KAL_DEF.cntx_pop();
   R_KARHIS.cntx_pop();
   R_PRACDN.cntx_pop()
}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: w_rapms.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{  h_rat:=charleft(); ~~}
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~}
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'%1 %2'['Za miesiąc:'@,date(VAR_EDIT.ROK,VAR_EDIT.MSC,1)$8]}
   {IF: VAR_EDIT.FLAGA=0}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Alfabetycznie w ramach jednostek organizacyjnych'@]}
   {ELIF: VAR_EDIT.FLAGA=1}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Grupowo w ramach jednostek organizacyjnych'@]}
   {ELIF: VAR_EDIT.FLAGA=2}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Alfabetycznie (bez związku z jednostką organizacyjną)'@]}
   {FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: ~rep_export()}
   {IF: VAR_EDIT.FLAGA=1}
   {<{lmt+1}}{'Jednostka organizacyjna:'} {P.WYDZIAL().SYMBOL}{IF: +(|P.WYDZIAL().OPIS)} ({P.WYDZIAL().OPIS}){FI}
   {FI}
{FI}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{  _d_pocz:=date(VAR_EDIT.ROK,VAR_EDIT.MSC,1);
   _Data:=date(VAR_EDIT.ROK,VAR_EDIT.MSC,0);
   R_KARHIS.prefix(P.ref());
   Karta:={? R_KARHIS.last() || R_KARHIS.K().N || '' ?};
   Pracownik:=form(P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,40);
   Teczka:=form(P.T,-9);
   Uwagi:='';
   R_WZCZ.prefix(P.name(),P.ref());
   NormPrac:=PracPrac:=GodzNadm:=GodzSw:=time(0,0,0);

   {? (P.DZ<_d_pocz & P.DZ<>#0) | P.DZA>_Data || _ok:=0 || _ok:=1 ?};
   {? _ok
   || {? (P.DZ<_Data & P.DZ<>#0) || _Data:=P.DZ ?};
      {? P.DZA>_d_pocz || _d_pocz:=P.DZA ?};
      {? __KAL.set_cal({? P.KAL || P.KAL().NAZWA || 'standard' ?},VAR_EDIT.ROK)
      || NormPrac:=*(__KAL.w_hours(_d_pocz,_Data,,1)*60);
         _ok:=1
      || Uwagi:='brak definicji kalendarza'@;
         _ok:=0
      ?}
   || Uwagi:='pracownik niezatrudniony'@
   ?};
   {? _ok ||
      {!
      |? _Kalend:=
            {? R_WZCZ.find_le(_d_pocz)
            || {? R_WZCZ.GRAFIK='T' || R_WZCZ.CZESC || R_WZCZ.KAL ?}
            || P.KAL
            ?};
         {? _Kalend
         || KAL_DEF.prefix(_Kalend,VAR_EDIT.ROK);
            {? KAL_DEF.find_key(_d_pocz)
            || {? KAL_DEF.TYP='S'
               || R_PRACDN.prefix(P.ref());
                  {? R_PRACDN.find_key(KAL_DEF.DATA) || GodzSw+=R_PRACDN.CP ?}
               ?};
               _d_pocz+=1;
               _d_pocz<=_Data
            || Uwagi:='brak definicji kalendarza'@; 0
            ?}
         || Uwagi:='nieokreślony kalendarz'@; 0
         ?}
      !}
   ?};
   NormPrac:=form(NormPrac,6);
   GodzSw:=form(GodzSw,6); ~~
}
{FOR: R_PRACDN}
{INDEX: 'R_PRACDN'}
{FROM: P.ref(),date(VAR_EDIT.ROK,VAR_EDIT.MSC,1)}
{TO: P.ref,date(VAR_EDIT.ROK,VAR_EDIT.MSC,0)}
{DO}
   {  PracPrac+=R_PRACDN.CP;
      GodzNadm+=R_PRACDN.CN; ~~
   }
{OD}
{  PracPrac:=form(PracPrac,6);
   GodzNadm:=form(GodzNadm,6);
   Ppsf:=exec('wydr_p_msc','ppsf',P.ref(),VAR_EDIT.ROK,VAR_EDIT.MSC); ~~
}
{IF: P.WYDZIAL<>Wydzial}
   {IF: VAR_EDIT.FLAGA=1}
   {PAGE}
   {Nr:=0; ~~}
   {FI}
   {  Wydzial:=P.WYDZIAL;
       ~~
   }
{FI}
{IF: +Pracownik}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{IF:  VAR_EDIT.ROK:=date()~1;
      VAR_EDIT.MSC:=date()~2;
      _ed:=VAR_EDIT.mk_edit('Raport dzienny'@,0,'prc_rap_dz');
      VAR_EDIT.win_esep(_ed,'Parametry wydruku'@);
      VAR_EDIT.win_efld(_ed,,'ROK',,,8,,,'Rok'@,,'Wybrany rok'@);
      VAR_EDIT.win_efld(_ed,,'MSC',,,8,,,'Miesiąc'@,,'Wybrany miesiąc'@);
      VAR_EDIT.win_efld(_ed,,'FLAGA',,,,,,
         'Rodzaj raportu'@,,'Zestawienia są generowane dla wydziałów na osobnych stronach'@,
         'radio-buttons',,
         'Alfabetycznie w ramach jednostek organizacyjnych'@,"0",
         'Grupowo w ramach jednostek organizacyjnych'@,"1",
         'Alfabetycznie (bez związku z jednostką organizacyjną)'@,"2");
      exec('ok_esc','#window',VAR_EDIT,_ed);
      {? rep_export() || VAR_EDIT.efld_opt(_ed,'enable=0',,'FLAGA') ?};
      VAR_EDIT.efld_opt(_ed,'enable=1, mark=1',,'DATA');
      VAR_EDIT.win_edit(_ed);
      ~VAR_EDIT.edit("__CHK.record(VAR_EDIT,,'ROK')")
}
   {EXIT}
{FI}
{  MASK.Use('R_PRACDN',VAR_EDIT.ROK,VAR_EDIT.MSC);
   R_PRACDN.index('R_PRACDN'); ~~
}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft-PRN.width())/2); ~~ }
{IF: VAR_EDIT.FLAGA =2}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
{ELIF: VAR_EDIT.FLAGA<2}
{FOR: params_get().PT}
{DO}
   {IF: P.seek(params_get().PT.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
{FI}
:!UTF-8
{TITLE:Wypowiedzenie warunków umowy o pracę}
{VERSION: PEP}
{PRINTER: dziedzina:='PKD';
   exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PKD_ZASWZMUM')='T',,,,'I','B',,,,,,1,
   PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='';
   VAR_DEL.delete('Text','cnt','__CALL','__PRAC','oblipwy','dwylock','czy_szablon','czy_paperless','tytul','czy_pep');
   VAR_DEL.delete('edit','wyb_prac');
   edit:=0;
   _par:=params_get();
   tytul:='Wypowiedzenie warunków umowy o pracę';
   czy_szablon:=exec('rep_set','rap_zew','PKD_ZASWZMUM',1)='T';
   czy_pep:=var_pres('_par')>100 & var_pres('PEP',_par)>0;
   wyb_prac:=0;
   P.cntx_psh();
   H.cntx_psh();
   H_UM.cntx_psh();
   Text:=exec('uprawnieniawrap','pkd',
      'Przebiegu zatrudnienia'@,
      'PKD_EZK_PPZA',
      'PKD_EZK_RPZA'
   );
   {? +Text
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@ [Text])
   || _paperless:=czy_pep & var_pres('active',_par.PEP)>0 & _par.PEP.active;
      {? _paperless
      || czy_paperless:=0;
         {? ~czy_szablon & exec('rep_set','rap_zew','PKD_ZASWZMUM')<>'T' & _par.PEP.fml_after=""
         || czy_paperless:=1
         ?}
      || czy_paperless:=1
      ?};

      cnt:=0;
      H.index('HISTUM');
      H_UM.index('OD');
      _tab:=tab_tmp(1,
         'DUM','DATE','Data zawarcia umowy o pracę',
         'DWY','DATE','Koniec okresu wypowiedzenia',
         'OWY','INTEGER','Długość okresu wypowiedzenia',
         'PWY','DATE','Połowa okresu wypowiedzenia',
         'PRZ','STRING[150]','Przyczyna wypowiedzenia',
         'ZUM','STRING[120]','Część umowy podlegająca zmianie',
         'NUM','STRING[120]','Nowe warunki umowy o pracę',
         'SAD','STRING[120]','Odwołanie do Sądu Pracy',
         'KOM','STRING[120]','Siedziba Komisji Pojednawczej'
      );
      _red:=_tab.mk_edit(tytul,,'wypowiedzwar',,,'html_maximized');
      _tab.win_esep(_red,'Parametry wydruku'@);
      _tab.win_efld(_red,,'DUM',,,11);
      _tab.win_efld(_red,,'DWY',,,11);
      _tab.win_efld(_red,,'OWY',,,,,,'Długość okresu wypowiedzenia'@,,'Długość okresu wypowiedzenia według kp'@,
         'radio-buttons',,'Trzy dni robocze',"3",'Tydzień',"4",'Dwa tygodnie',"0",'Miesiąc',"1",'Trzy miesiące',"2");
      _tab.win_efld(_red,,'PWY',,,11,,,,,'Połowa okresu wypowiedzenia według kp'@);
      _tab.win_efld(_red,,'PRZ',,,70,,,,,'Przyczyną jest %1'@['...']);
      _tab.win_efld(_red,,'ZUM',,,70,,,,,'Wypowiadam [...] w części dotyczącej ...');
      _tab.win_efld(_red,,'NUM',,,70,,,,,'[...] proponuję następujące, nowe warunki:');
      _tab.win_efld(_red,,'SAD',,,70,,,,,'[...] odwołania do Sądu Rejonowego - Sądu Pracy w...');
      _tab.win_esep(_red,'');
      _tab.win_efld(_red,,'KOM',,,70,,,,,'[...] przed Komisją Pojednawczą...');
      exec('ok_esc','#window',_tab,_red);
      _tab.efld_opt(_red,'mark=1',,'DUM');
      _tab.efld_opt(_red,'mark=1',,'DWY');
      _tab.efld_opt(_red,'mark=1',,'OWY');
      _tab.efld_opt(_red,'mark=1',,'PWY');
      _tab.efld_opt(_red,'mark=1',,'ZUM');
      _tab.efld_opt(_red,'mark=1',,'NUM');
      _tab.efld_opt(_red,'mark=1',,'SAD');
      dwylock:=obj_new('lck','val');
      dwylock.lck:=0;
      oblipwy:="_datakon:=date();
                {? cur_tab().OWY=1
                || {? _datakon~2>11
                   || _datakon:=date((_datakon~1)+1,(_datakon~2)-11,0)
                   || _datakon:=date(_datakon~1,(_datakon~2)+1,0)
                   ?}
                |? cur_tab().OWY=2
                || {? _datakon~2>9
                   || _datakon:=date((_datakon~1)+1,(_datakon~2)-9,0)
                   || _datakon:=date(_datakon~1,(_datakon~2)+3,0)
                   ?}
                |? cur_tab().OWY=3
                || _dnirob:={? date()~4=4 | date()~4=5 | date()~4=6 || 4 || 3 ?};
                   _datakon:=_datakon+_dnirob
                |? cur_tab().OWY=4
                || _dnirob:=13-date()~4;
                   _datakon:=_datakon+_dnirob
                || _dnirob:=20-date()~4;
                   _datakon:=_datakon+_dnirob
                ?};
                {? _a&~dwylock.lck || cur_tab().DWY:=_datakon ?};
                {? dwylock.lck || _datakon:=cur_tab().DWY ?};
                _okrldni:=_datakon-date();
                cur_tab().PWY:=_datakon-int(_okrldni/2)
               ";
      _tab.fld_fml('DWY','BEFORE_EDIT',"dwylock.val:=cur_tab().DWY");
      _tab.fld_fml('DWY','AFTER_EDIT',"{? dwylock.val<>cur_tab().DWY || dwylock.lck:=1 ?}; oblipwy(0)");
      _tab.fld_fml('OWY','AFTER_EDIT',"oblipwy(1)");
      {? _paperless
      || {? _par.PEP.P
         || edit+=1;
            params_set(
               'P',exec('wybierz_silent','pracownik',_par.PEP.P).P,
               'paperless',_paperless,
               'par',_par,
               'CALL',null(),
               'tab',_tab,
               'red',_red
            )
         || _args:=exec('wybierz_args','pracownik');
            _args.DOMAIN:=dziedzina;
            _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
            _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
            _args.F_ZATR:=__PARSES.getVal('F_ZATR').KOD;
            _args.VIEW:=__PARSES.getVal('ZakresDanych');
            _args.SQL_FROM:=" JOIN OSOBA using(P.OSOBA, OSOBA.REFERENCE) "+
                            " JOIN USERS using(USERS.OSOBA, OSOBA.REFERENCE)"+
                            " JOIN USERSF using(USERSF.USERS, USERS.REFERENCE)";
            _args.SQL_WHERE:=''+"P.PORTAL='T' and USERSF.PORTAL2='T'";
            _args.WIELU:=~_paperless;
            params_set(
               'P',exec('wybierz_parses','pracownik',dziedzina,_args).P,
               'paperless',_paperless,
               'par',_par,
               'CALL',null(),
               'tab',_tab,
               'red',_red
            )
         ?};
         Text:={? ~params_get(1).P.first() || 'Nie wybrano pracownika' || '' ?}
      || params_set(
            'P',null(),
            'paperless',_paperless,
            'par',_par,
            'CALL',null(),
            'tab',_tab,
            'red',_red
         )
      ?};
      __xr_ins:=exec('new_ins','rap_zew','PKD_ZASWZMUM')
   ?}
}
{END:
   VAR_DEL.delete('Text','cnt','__CALL','__PRAC','oblipwy','dwylock','wyb_prac');
   VAR_DEL.delete('dziedzina','czy_szablon','czy_paperless','tytul','czy_pep','edit');
   H_UM.cntx_pop();
   H.cntx_pop();
   P.cntx_pop();
   {? var_pres('__xr_ins')=type_of(null()) || exec('del_ins','rap_zew',&__xr_ins) ?}
}
{EXIT: +Text}
{EXIT: ~__xr_ins}
{COMMENT}OLD: zaswzmum.rpm{COMMENT-}
{COMMENT}OLD: zaswzmum.rpi{COMMENT-}
{WHILE: (~params_get().paperless &
   (_wyb:=exec('wybierz','!pkd_zes_orza');
    wyb_prac+=_wyb;
    _wyb)
   )
   | (params_get().paperless & params_get().par.PEP.fml_after="" & P.seek(params_get().P.UID,))}
{DO}
   {EXIT:
   _word:=(exec('rep_set','rap_zew','PKD_ZASWZMUM',1)='T');
   H_UM.prefix(P.ref());
   H_UM.last();
   H.prefix(H_UM.ref());
   {? H.first() & H.PODPIS<>#0 || _data:=H.PODPIS || _data:=H_UM.OD ?};
   params_set(_par:=params_get());
   _par.tab.win_edit(_par.red);
   {? H_UM.RU().K<>'C'
   || _par.tab.efld_opt(_par.red,'enable=0',,'PRZ')
   || _par.tab.efld_opt(_par.red,'mark=1,enable=1',,'PRZ')
   ?};
   _par.tab.DUM:=_data;
   _par.tab.OWY:=1;
   _par.tab.DWY:={? date~2<>12 || date(date~1,1+date~2,0) || date(1+date~1,1,0) ?};
   _par.tab.PWY:=_par.tab.DWY-int((_par.tab.DWY-date)/2);
   _stale:=exec('zasw_pob','profile');
   _par.tab.SAD:=_stale.sad;
   _par.tab.KOM:=_stale.komisja;
   _par.tab.hdr_edit();
   _par.tab.hdr_edit(' - %1 %2'[P.OSOBA().NAZWISKO,OSOBA.PIERWSZE]);
   {? _par.tab.edit(
         "  _tab:=params_get().tab;
            {? (_chk:=__CHK.record(_tab,,'DUM','DWY','PWY'))<>''
               |
               (H_UM.RU().K='C' & (_chk:=__CHK.record(_tab,,'PRZ'))<>'')
               |
               (_chk:=__CHK.record(_tab,,'ZUM','NUM','SAD'))<>''
            || _chk
            |? _tab.DUM>_tab.DWY
            || __CHK.err_fld(_tab,'DWY',1,'Data nie może być wcześniejsza niż \"%1\" [%2].'
                  [MS.name(_tab,'DUM'),$_tab.DUM])
            || ''
            ?}
         "
      )
   || _stale.sad:=_par.tab.SAD;
      _stale.komisja:=_par.tab.KOM;
      exec('zasw_zap','profile',_stale);
      exec('ins_par','rap_zew',__xr_ins,cnt+=1,
         'F_NAZWA',KST.NAZWA,
         'MIEJSCE',PAR_WYDR.MIEJSC,
         'DATA',date()$4,
         'F_ULICA',exec('ulica','stalesys'),
         'F_POCZTA',exec('poczta','stalesys'),
         'F_REGON',KST.REG,
         'F_PKD',KST.PKD,
         'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?}
      );
      {? _par.tab.OWY=1 || _okres:='miesięcznego'
      |? _par.tab.OWY=2 || _okres:='trzymiesięcznego'
      |? _par.tab.OWY=3 || _okres:='trzydniowego'
      |? _par.tab.OWY=4 || _okres:='tygodniowego'
      || _okres:='dwutygodniowego'
      ?};

      {? _word<>0
      || _zasw:=obj_new(
            'DATAUM','DATAWYP','OKRWYP','POWOD','ZMIANA','NOWAWAR',
            'SAD','KOMISJA','DATAZM','UMTYP','DATAPOLW'
         );
         _zasw.DATAUM:=_par.tab.DUM$6;
         _zasw.DATAWYP:=_par.tab.DWY$6;
         _zasw.OKRWYP:=_okres;
         _zasw.POWOD:=
            {? H_UM.RU().K<>'C'
            || 'wygaśnięcie umowy'
            || _par.tab.PRZ
            ?};
         _zasw.ZMIANA:=_par.tab.ZUM;
         _zasw.NOWAWAR:=_par.tab.NUM;
         _zasw.SAD:=_par.tab.SAD;
         _zasw.KOMISJA:=_par.tab.KOM;
         _zasw.DATAZM:=((_par.tab.DWY+1)$6);
         _zasw.UMTYP:=H_UM.RU().K;
         _zasw.DATAPOLW:=_par.tab.PWY$6;
         _paperless:=params_get().paperless;
         _exe:={? _paperless || 2 || 3 ?};
         params_set('ZASWZMUM',_zasw);
         _file:=exec('wypelnij','szablon','PKD_ZASWZMUM',,,_exe,,_paperless);
         {? _paperless & var_pres('_file')>0 & var_pres('_file')>100 & var_pres('INFO',_file)>0
         || exec('add_file_and_run','paperless_grp',
               P.ref(),_file.INFO,'Inne',tytul)
         ?};
         obj_del(_zasw)

      || exec('ins_par','rap_zew',__xr_ins,cnt,
            'NAZWISKO',OSOBA.NAZWISKO,
            'IMIE',OSOBA.PIERWSZE,
            'DRUGIEIM',OSOBA.DRUGIE,
            'PLEC',OSOBA.PLEC,
            'DATAUM',_par.tab.DUM$6,
            'DATAWYP',_par.tab.DWY$6,
            'OKRWYP',_okres,
            'POWOD',_par.tab.PRZ,
            'ZMIANA',_par.tab.ZUM,
            'NOWAWAR',_par.tab.NUM,
            'SAD',_par.tab.SAD,
            'KOMISJA',_par.tab.KOM
         );
         exec('ins_par','rap_zew',__xr_ins,cnt,
            'DATAZM',((_par.tab.DWY+1)$6),
            'UMTYP',H_UM.RU().K,
            'DATAPOLW',(_par.tab.PWY$6)
         )
      ?};
      exec('ins_par','rap_zew',__xr_ins,cnt,'CZY_ZAP',{? params_get().paperless || '1' || ' ' ?});
      dwylock.lck:=0;
      edit+=1;
      0
   || 0
   ?} | params_get().paperless
}{OD}
{EXIT: params_get().paperless & ~edit}
{EXIT: czy_szablon}
{EXIT: ~(exec('ready','rap_zew',__xr_ins) | czy_pep)}
{EXIT: ~(wyb_prac & edit>0) & ~params_get().paperless}
{IF: exec('rep_set','rap_zew','zaswzmum')='T'}
   {
   exec('use_tmp','rap_zew','zaswzmum');
   {? params_get().paperless
   || exec('add_file_and_run','paperless_grp',P.ref(),'@!Tmp\\macrotmp.doc','Inne',tytul)
   ?};
   ~~}
   {EXIT}
{ELSE}
{
   params_set(_par:=params_get());
   {? _par.paperless & _par.par.PEP.fml_after<>""
   || __CALL:=_par.par.PEP.CALL
   || __CALL:=proc_exec('zmum@zasw',#__xr_ins);
      {? czy_pep || _par.par.PEP.CALL:=__CALL ?}
   ?};
~~ }
   {IF: params_get().paperless & params_get().par.PEP.fml_after=""}
      {
      params_get().par.PEP.P:=P.ref();
      params_get().par.PEP.fml_after:="
         _pep:=obj_new('active','fml_after','P','CALL');
         _pep.active:=1;
         _pep.fml_after:=\"1\";
         _pep.P:=_a;
         _pep.CALL:=_b;
         {? var_pres('_params')>100
         || exec('obj_ntab_set','#array',_params,'PEP',_pep)
         || _params:=obj_new('PEP');
            _params.PEP:=_pep
         ?};
         params_set(_params);
         _name:='@'+tmp_dir()+exec('sep','#file')+'pkd_zaswwypowiedzeniewarunkow_%1'[$P.tm_stamp()]+'.pdf';
         rep_exec('pkd_zaswwypowiedzeniewarunkow',,0,_name,1,,1);
         exec('add_file_and_run','paperless_grp',
         _pep.P,_name,'Inne','Wypowiedzenie warunków umowy o pracę')
      ";
      ~~}
      {EXIT}
   {ELSE}
      {INCLUDE: p_zaswzmum.rpi}
   {FI}
{FI}
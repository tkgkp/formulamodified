:!UTF-8
{TITLE:Wydruk etykiet lokalizacji - kody kreskowe}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   lmg_eanl:="VAR_DEL.delete('temp','font','wybierz','dalej','wyjscie','etykieta')";
   lmg_eanl();
   temp:='';
   font:='';
   wybierz:='';
   dalej:=0;
   etykieta:=PAR_WYDR.URZ_LAB;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=exec('barcode_height','kody_kresk');
   PAR_WYDR.URZ_LAB:=exec('wyb_etykiety','kody_kresk');
   wyjscie:={? PAR_WYDR.TYPDR<>null || PAR_WYDR.URZ_LAB=null ?};
   {? wyjscie=0 & PAR_WYDR.TYPDR=null & PAR_WYDR.KODKRESK=''
   || FUN.info('Brak parametryzacji drukowania kodów kreskowych.'@);
      wyjscie:=1
   ?};
   exec('ini_kom','#message','Wydruk etykiet lokalizacji - kody kreskowe'@);
   EANL.cntx_psh
}
{END:
   EANL.cntx_pop;
   exec('end_kom','#message');
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   PAR_WYDR.URZ_LAB:=etykieta;
   lmg_eanl(); VAR_DEL.delete('lmg_eanl')
}
{EXIT: wyjscie=1}
{DEFINEFONT: 'courier=TexGyreCursor,,,8'}
{FONT: 'courier'}
{MACRO:'stan'}
   {ENTIRE}
   {EANL.KOD+{? +EANL.OP || ' - '+EANL.OP || '' ?}}

   {IF: EANL.EAN<>''}
      {BARCODE: EANL.EAN;50;BARCODE_H;BARCODE_TYPE}


      {FONT: 'courier'}{SPACE: 'B'}
      {EANL.EAN}
   {FI}
  ─────────────────────────────────────
   {ENTIRE-}
{MACRO-}
{MACRO:'etykiety'}
{IF: EANL.EAN=''}
   { exec('add_kom','#message','Lokalizacja: '+EANL.KOD,7,'Brak kodu kreskowego'@);~~}
{ELSE}
   { wyjscie:=0;~~}
   {IF: PAR_WYDR.TYPDR<>null}
      { wyjscie:=exec('druk','kody_kresk')=0;~~}
   {ELSE}
      {SUBSTITUTE: 'stan'}
   {FI}
{FI}
{MACRO-}
{EXIT:
   wybierz:=FUN.choice('Wydruk etykiet dla lokalizacji'@,,' Bieżącego'@, 'Wszystkich'@, 'W&ybranych'@);
   {? wybierz <> 0
   || {? wybierz=1
      || wybierz:='B'
      |? wybierz=2
      || wybierz:='W'
      |? wybierz=3
      || wybierz:='Y'
      |? wybierz=4
      || wybierz:='Z'
      ?};0
   || "# koniec #"; 1
   ?}
}
{ wyjscie:=1;~~}
{IF:wybierz='B'}
   {SUBSTITUTE: 'etykiety'}
{ELIF:wybierz='Y'}
   {
   _wer:=EANL.mk_sel('Lokalizacje'@,,0,'wwdddppppswsadf');
   EANL.win_fld(_wer,,'KOD',,,20,,,'Kod lokalizacji'@);
   EANL.win_fld(_wer,,'EAN',,,20,,,'Kod kreskowy'@);
   EANL.win_fld(_wer,,'OP',,,20,,,'Opis'@);
   _fb:="
      VAR_DEL.delete('__Sel');
      __Sel:=EANL.sel_aget; EANL.sel_adel;
      {? __Sel.first=0
      || __Sel.REF:=#EANL.ref;
         __Sel.add
      ?};
      sel_exit
   ";
   EANL.win_act(_wer,,'Formuła','Akceptuj'@@,,'Akceptacja wybranych pozycji'@,_fb,,1,1,_fb);

   EANL.index('MG'); EANL.prefix('T',MG.ref);
   EANL.win_sel(_wer);

   {? EANL.select=0
   || VAR_DEL.delete('__Sel');
      __Sel:=EANL.sel_aget; EANL.sel_adel
   ?};
   EANL.cntx_psh;
   EANL.prefix;
   ~~}
   {FOR: __Sel}
   {DO}
      {IF: EANL.seek(__Sel.REF,)}
         {SUBSTITUTE: 'etykiety'}
      {FI}
   {OD}
   {
   EANL.cntx_pop;
   VAR_DEL.delete('__Sel');
   ~~}
{ELIF:wybierz='W'}
   {FOR: 'EANL'}
   {INDEX: 'MG'}
   {PREFIX: 'T',MG.ref()}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{FI}
{EXIT: {? wybierz='W' | wybierz='Z' || {? wyjscie=1 || FUN.info('Brak danych spełniających kryteria.'@); 1 ?} ?}}
{EXIT: wyjscie}
{EXIT: PAR_WYDR.TYPDR<>null}
:!UTF-8
{COMMENT}OLD: zaswuman.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{MACRO: 'ls_item'}
   {JUSTIFY}
   {  STR.split(gsub(Text,'..','.'));
      korekta:=
         {? type_of(num)=type_of(0) & num
         || 4
         |? type_of(num)=type_of('') || 1+(+num)
         |? 1+Text=%255 || 3
         || 0
         ?};
   ~~}
   {WHILE: +(Text:=STR.line(charleft()-indent-korekta-4))}
   {DO}
{REP:
         _cl:=charleft();
         {? type_of(num)=type_of(0) & num
         || _odp:={? indent=0 || '{<1 \''+$num+'.\'}' || '{<'+$indent+' \''+$num+')\'}' ?}; indent+=4; num:=0; _odp
         |? type_of(num)=type_of('') || _odp:='{<'+$indent+' \''+num+'\'}'; indent+=1+(+num); num:=0; _odp
         |? 1+Text=%255 || Text:=1-Text; _odp:='{<'+$indent+' \'-\'}'; indent+=3; num:=0; _odp
         || ''
         ?}+
         {? +STR.spl_str || '{JUSTIFY}{<'+$(indent)+'>'+$_cl || '{CENTER}{<'+$(indent) ?}+' num:=0; Text }'
      }
   {OD}
{MACRO-}
{FOR: __CALL}{DO}
   {
      ppkt_inne:=__CALL.GODZNADL<>'' | __CALL.INNEWAR<>'   ';
      ppkt:=(__CALL.PPKT*'pkt.1 ppkt.')<>0;
   ~~}
   {IF: PAR_WYDR.HEAD}
      {FONT: 'HEAD'}{SPACE: 'B'}
      { lmt:=charleft(); indent:=1; ~~ }
      {>{lmt}'%1, dn. %2'[__CALL.MIEJSCE,__CALL.DATA]}
      {<1 STR.split(__CALL.F_NAZWA); STR.line(lmt)}
      {WHILE: STR.next()}{DO}{<1 STR.line(lmt)}{OD}
      { PAR_WYDR.HEAD:=0; ~~ }
      {SUBSTITUTE: 'HEAD'}
      { PAR_WYDR.HEAD:=1; ~~ }
      {FONT: 'HEAD'}{SPACE: 'B'}
      {<1}{__CALL.F_ULICA}
      {<1}{__CALL.F_POCZTA}
      {<1}REGON {__CALL.F_REGON}
      {<1}NIP {__CALL.F_NIP}
   {ELSE}
      {FONT: 'HEAD'}{SPACE: 'B'}
      {LINE: 6}
   {FI}
   {FONT: 'TITLE'}{SPACE: 'A'}
   {<1>{charleft()}'ANEKS DO UMOWY O PRACĘ O NUMERZE %1' [__CALL.NR_UM]}
   {<1>{charleft()}' Z DNIA %1' [__CALL.DATA_UM]}
   {LINE: 1}
   {FONT: 'W12'}{SPACE: 'A'}
   {lmt:=charleft(); ~~}
   {SUBSTITUTE: indent:=4; num:=0; Text:='podpisany w dniu %1, w którym stronami są:' [__CALL.DATA_ZAW]; 'ls_item'}
   {SUBSTITUTE: indent:=6; num:='-'; Text:=__CALL.F_OSREP+','; 'ls_item' }
   {SUBSTITUTE: indent:=6; num:='-'; Text:=__CALL.OSOBA+'.'; 'ls_item' }

   {SUBSTITUTE:
      indent:=0;
      num:=1;
      Text:=
         'Strony zmieniają umowę w ten sposób%1%2' [{? ppkt || ', iż dla ' || '' ?},__CALL.PPKT];
      'ls_item' }
   {IF: __CALL.STANOW<>''}
      {SUBSTITUTE: indent:=4; num:=1; Text:='rodzaj umówionej pracy: %1' [__CALL.STANOW]; 'ls_item'}{FI}
   {IF: __CALL.WYDZIAL<>''}
      {SUBSTITUTE: indent:=4; num:=2; Text:='miejsce/miejsca wykonywania pracy: %1' [__CALL.WYDZIAL]; 'ls_item'}{FI}
   {IF: __CALL.WYMIAR<>''}
      {SUBSTITUTE: indent:=4; num:=3; Text:='wymiar czasu pracy: %1' [__CALL.WYMIAR]; 'ls_item'}{FI}
   {IF: __CALL.SW1<>'' | __CALL.SW5<>''}
      {SUBSTITUTE: indent:=4; num:=4; Text:='wynagrodzenie:' ; 'ls_item'}
      {IF: __CALL.SW1<>''}
         {SUBSTITUTE:
            indent:=7; num:=0;
            Text:=%255+__CALL.SW1+{? __CALL.KW_SL1<>'' || ' (słownie: %1)'[__CALL.KW_SL1] || '' ?};
            'ls_item'
       }{FI}
      {IF: __CALL.SW2<>''}{SUBSTITUTE:
         indent:=7; num:=0;
         Text:=%255+__CALL.SW2+{? __CALL.KW_SL2<>'' || ' (słownie: %1)'[__CALL.KW_SL2] || '' ?};
         'ls_item'
      }{FI}
      {IF: __CALL.SW3<>''}{SUBSTITUTE:
         indent:=7; num:=0;
         Text:=%255+__CALL.SW3+{? __CALL.KW_SL3<>'' || ' (słownie: %1)'[__CALL.KW_SL3] || '' ?};
         'ls_item'
      }{FI}
      {IF: __CALL.SW5<>''}{SUBSTITUTE:
         indent:=7; num:=0;
         Text:=%255+__CALL.SW5+{? __CALL.KW_SL4<>'' || ' (słownie: %1)'[__CALL.KW_SL4] || '' ?};
         'ls_item'
      }{FI}
      {IF: __CALL.SW4<>''}{SUBSTITUTE: indent:=7; num:=0; Text:=%255+__CALL.SW4; 'ls_item'}{FI}
   {FI}
   {IF: +__CALL.PRACA_ZD}{
      {? var_pres('Temp')=type_of(SYSLOG) || obj_del(Temp) ?};
      Temp:=proc_exe('praca_zd@zasw',__CALL.PRACA_ZD,__CALL.PRACA_ZW);
      ~~ }
      {IF: +|__CALL.PRACA_OD}
         {SUBSTITUTE: indent:=4; num:=5;
            Text:='świadczenie pracy w trybie pracy zdalnej zgodnie z art. 67(18) Kodeksu pracy od %1 r.%2'
               [__CALL.PRACA_OD,{? +__CALL.PRACA_DO || ' do '+__CALL.PRACA_DO+' r.' || '' ?}];
            'ls_item'
         }
         {FOR: Temp}
         {DO}
            {IF: +|Temp.TEKST}
               {SUBSTITUTE: indent:=7; num:=0; Text:=%255+Temp.TEKST; 'ls_item'}
            {FI}
         {OD}
      {FI}
      { {? var_pres('Temp')=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
   {FI}

   {SUBSTITUTE:
      indent:=0;
      num:=2;
      _txt:=
         {? __CALL.ZAKPRAC=''
         || 'od dnia %1.' [__CALL.ROZPRAC]
         || 'w okresie od dnia %1 do dnia %2.' [__CALL.ROZPRAC,__CALL.ZAKPRAC]
         ?};
      Text:='Nowe warunki zatrudnienia będą obowiązywać %1'[_txt];
      'ls_item' }
   {IF: ppkt_inne}

      {SUBSTITUTE:
         indent:=0;
         num:=3;
         Text:='Zmianie podlegają %1inne warunki pracy z pkt. 3 na następującą treść:'[{? ppkt || 'też ' || '' ?}];
         'ls_item' }
      {IF: __CALL.GODZNADL<>''}
         {SUBSTITUTE: indent:=4; num:=1; Text:=__CALL.GODZNADL; 'ls_item'}{FI}
      {IF: __CALL.INNEWAR<>'   '}
         {SUBSTITUTE: indent:=4; num:=2; Text:='inne warunki zatrudnienia %1'[__CALL.INNEWAR]; 'ls_item'}{FI}
   {FI}
   {IF: __CALL.PAT='N'}

      {SUBSTITUTE:
         indent:=0;
         num:=3+ppkt_inne;
         Text:=
            'Obowiązki ze stosunku pracy nie będą już obejmować działalności twórczej, której rezultaty spełniają '
            'przesłanki utworu, o którym mowa w Art. 1 ust. 1 Ustawy o prawie autorskim i prawach pokrewnych.';
         'ls_item'}
   {ELIF: __CALL.PAT<>''}

      {SUBSTITUTE:
         indent:=0;
         num:=3+ppkt_inne;
         Text:=
            'Obowiązki ze stosunku pracy obejmować także będą działalność twórczą, której rezultaty '
            'spełniają przesłanki utworu, o którym mowa w Art. 1 ust. 1 Ustawy o prawie autorskim i prawach pokrewnych.'
            ' W związku z tym wynagrodzenie za pracę ustalone w w pkt 1. ust. 4) obejmuje:';
         'ls_item'}
      {SUBSTITUTE:
         indent:=4;
         num:='a) ';
         Text:=
            'Wynagrodzenie autorskie z tytułu nabycia przez Pracodawcę, w trybie Ustawy o prawie autorskim i prawach '
            'pokrewnych, majątkowych praw autorskich do utworów stworzonych przez Pracownika w wyniku wykonywania '
            'obowiązków służbowych;';
         'ls_item'}
      {SUBSTITUTE:
         indent:=4;
         num:='b) ';
         Text:=
            'Wynagrodzenie za pracę obejmujące wykonywanie innych obowiązków pracowniczych, niezwiązanych z tworzeniem '
            'utworów w rozumieniu Ustawy o prawie autorskim i prawach pokrewnych.';
         'ls_item'}
      {SUBSTITUTE:
         indent:=4;
         num:=~~;
         _txt:=
            {? __CALL.PAT='K'
            || 'stanowić będzie kwotę %1'[__CALL.PAW]
            |? __CALL.PAT='P'
            || 'stanowić będzie %1 płacy zasadniczej pomniejszonej za dni nieprzepracowane'[__CALL.PAW]
            || 'ustalane będzie każdorazowo za dany miesiąc na podstawie godzinowej ewidencji pracy twórczej'
            ?};
         Text:='Wynagrodzenie autorskie %1.'[_txt];
         'ls_item'}
   {FI}

   {SUBSTITUTE:
      indent:=0;
      num:=3+(__CALL.PAT<>'')+ppkt_inne;
      Text:='Aneks sporządzono w dwóch jednobrzmiących egzemplarzach, po jednym dla każdej ze stron.';
      'ls_item' }
   {CENTER}
   {LINE: 3}
   {FONT: 'W10'}{SPACE: 'C'}
   { lmt:=charleft(); ~~ }
   {<1>{lmt/2}(35*'─')}{<{lmt/2}>{lmt}(60*'─')}
   {<1>{lmt/2}'(data i podpis pracownika)'}{<{lmt/2}>{lmt}'(podpis pracodawcy lub osoby reprezentującej pracodawcę'}
   {<{lmt/2}>{lmt}'albo osoby upoważnionej do składania oświadczeń'}
   {<{lmt/2}>{lmt}'w imieniu pracodawcy)'}
   {PAGE}
{OD}
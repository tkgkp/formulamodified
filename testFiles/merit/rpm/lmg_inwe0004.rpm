:!UTF-8
{TITLE:D. Inwentaryzacja wg wymiarów}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   reo_004:="
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('nn','kk','ww','nn2','kk2','ww2','doklw','PRN','PRN2');
      VAR_DEL.delete('wydr_naz','wydr_edi','X_Xw1','X_Xw2','poznalok','wyjscie')";
   reo_004();

   wyjscie:=0;
   {? ST.MAG().SL<>'T'
   ||
      FUN.info('Magazyn bez obsługi wymiarów.\nWydruk arkusza niedostępny.'@);
      wyjscie:=1
   |? INN.ARKPOM=0
   ||
      FUN.info('Inwentaryzacja bez arkuszy pomocniczych.\nWydruk niedostępny.'@);
      wyjscie:=1
   ||
      color:=prn1:='';
      lm:=ll:=vp:=lmt:=rsep:=0;

      _ilk:=6;
      nn:=obj_new(_ilk); "--nazwy kolumn--";
      kk:=obj_new(_ilk); "--szerokosci kolumn--";
      ww:=obj_new(_ilk); "--wartosci kolumn--";

      poznalok:=3;
      dokl:=0; "--dokladnosc wartosc--";

      PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
      kk[1] :=20;    nn[1] :='Materiał'@;
      kk[2] :=40;    nn[2] :='Nazwa'@;
      kk[3] :=15;    nn[3] :='Lokalizacja'@;
      kk[4] :=10;    nn[4] :='Termin ważności'@;
      kk[5] :=15;    nn[5] :='Ilość wg dokumentów'@;
      kk[6] :=15;    nn[6] :='Ilość wg spisu'@;

      {! _ii.._ilk |! ww[_ii] :='' !}
   ?}
}
{END:
   reo_004(); VAR_DEL.delete('reo_004')
}
{EXIT: wyjscie}
{
PRN.add("ww[1]",kk[1],nn[1]);
PRN.add("ww[2]",kk[2],nn[2]);
PRN.add("ww[3]",kk[3],nn[3]);
PRN.add("ww[4]",kk[4],nn[4]);
{? INN.D<>date(0,0,0) || PRN.add("ww[5]",kk[5],nn[5],1) ?};
PRN.add("ww[6]",kk[6],nn[6],1);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(3,
   'M'      ,'STRING[16]'  ,'$M.ref',
   'EANL'   ,'STRING[16]'  ,'$EANL.ref',
   'TW'     ,'DATE'        ,'T.ważno.Z',
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'NAZ'    ,'STRING[100]' ,'Nazwa',
   'LOK'    ,'STRING[20]'  ,'Lokaliz.Z',
   'ILD'    ,'REAL'        ,'Ilość wg dok.',
   'ILS'    ,'REAL'        ,'Ilość wg spisu');

"--wg stanow--";
SL.cntx_psh; INP.cntx_psh;
{? SL.first & (INN.DK_LN_R<>'' | 1)
||
   _mg:=INN.MG;
   _dat:=INN.D;
   INP.index('INP');
   INP.prefix(INN.ref);
   SL.index('EANL');
   SL.prefix(INN.MG);
   {? SL.first()
   || {!
      |?
         {? INP.find_key(SL.M().KTM,SL.M().KTM)
         ||
            X_Xw1.blank(1);
            X_Xw1.M:=$SL.M;
            X_Xw1.EANL:=$SL.EANL;
            X_Xw1.TW:=SL.TW;
            X_Xw1.KTM:=SL.M().KTM;
            X_Xw1.NAZ:=SL.M().N;
            X_Xw1.LOK:=SL.EANL().KOD;
            X_Xw1.ILD:=SL.IL-exec('oblsldat','magazyn_stan',SL.M,SL.EANL,SL.TW,_dat);
            X_Xw1.add
         ?};
         SL.next
      !}
   ?}
?};
SL.cntx_pop; INP.cntx_pop;

"--wylaczenie ilosci z reorganizacji inwentaryzacyjnej z ilosci wg dokumentow--";
DK_LN.cntx_psh; DK_L.cntx_psh;
DK_LN.prefix;
DK_L.index('DK_LNLOK');
{? DK_LN.seek(BB.sqlint(INN.DK_LN_R),) & DK_LN.AKC='T' & DK_LN.DT<=INN.D
||
   DK_L.prefix(DK_LN.ref);
   {? DK_L.first
   ||
      {!
      |?
         {? X_Xw1.find_key($DK_L.M,$DK_L.LOK,DK_L.TW)
         ||
            X_Xw1.ILD-=DK_L.IL;
            X_Xw1.put
         ||
            X_Xw1.blank(1);
            X_Xw1.M:=$DK_L.M;
            X_Xw1.EANL:=$DK_L.LOK;
            X_Xw1.TW:=DK_L.TW;
            X_Xw1.KTM:=DK_L.M().KTM;
            X_Xw1.NAZ:=DK_L.M().N;
            X_Xw1.LOK:=DK_L.LOK().KOD;
            X_Xw1.ILD:=-DK_L.IL;
            X_Xw1.add
         ?};
         DK_L.next
      !}
   ?}
?};
DK_LN.cntx_pop; DK_L.cntx_pop;

"--wylaczenie DK_L dokumentow INW-,INW+--";
{! _ii:=1..2
|!
   _nd:={? _ii=1 || INN.INP_R || INN.INR_R ?};
   {? _nd<>''
   ||
      ND.cntx_psh; DK.cntx_psh; DK_L.cntx_psh;
      ND.prefix;
      DK_L.index('DK_LNLOK');
      {? ND.seek(BB.sqlint(_nd),) & ND.Z='T'
      ||
         DK.index('DOKMAG'); DK.prefix(ND.ref);
         {? DK.first
         ||
            DK_L.index('DK');
            {!
            |?
               DK_L.prefix(DK.ref);
               {? DK_L.first
               ||
                  {!
                  |?
                     {? X_Xw1.find_key($DK_L.M,$DK_L.LOK,DK_L.TW)
                     ||
                        X_Xw1.ILD-=DK_L.IL;
                        X_Xw1.put
                     ||
                        X_Xw1.blank(1);
                        X_Xw1.M:=$DK_L.M;
                        X_Xw1.EANL:=$DK_L.LOK;
                        X_Xw1.TW:=DK_L.TW;
                        X_Xw1.KTM:=DK_L.M().KTM;
                        X_Xw1.NAZ:=DK_L.M().N;
                        X_Xw1.LOK:=DK_L.LOK().KOD;
                        X_Xw1.ILD:=-DK_L.IL;
                        X_Xw1.add
                     ?};
                     DK_L.next
                  !}
               ?};
               DK.next
            !}
         ?}
      ?};
      ND.cntx_pop; DK.cntx_pop; DK_L.cntx_pop
   ?}
!};

"--ilosc wg spisu--";
INX.cntx_psh; INY.cntx_psh;
INX.index('INN'); INX.prefix(INN.ref);
INY.index('INX');
{? INX.first
||
   {!
   |?
      INY.prefix(INX.ref);
      {? INY.first
      ||
         {!
         |?
            {? X_Xw1.find_key($INY.M,$INY.EANL,INY.TW)
            ||
               X_Xw1.ILS+=INY.IL;
               X_Xw1.put
            ||
               X_Xw1.blank(1);
               X_Xw1.M:=$INY.M;
               X_Xw1.EANL:=$INY.EANL;
               X_Xw1.TW:=INY.TW;
               X_Xw1.KTM:=INY.M().KTM;
               X_Xw1.NAZ:=INY.M().N;
               X_Xw1.LOK:=INY.EANL().KOD;
               X_Xw1.ILS:=INY.IL;
               X_Xw1.add
            ?};
            INY.next
         !}
      ?};
      INX.next
   !}
?};
INX.cntx_pop; INY.cntx_pop;

PAR_WYDR.TITLE:='INWENTARYZACJA WG WYMIARÓW\n%1 z dnia %2'@[INN.SYM,form(INN.DI)];
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{COMMENT}   pozycje                                                  {COMMENT-}
{ prn1:='PRN';~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                         {COMMENT-}
{ X_Xw1.clear();
  {? X_Xw1.first()
  || {!
     |? _dokl:=+form(frac(X_Xw1.ILD))-2; {? _dokl>dokl || dokl:=_dokl ?};
        _dokl:=+form(frac(X_Xw1.ILS))-2; {? _dokl>dokl || dokl:=_dokl ?};
        X_Xw1.next()
     !}
  ?};
~~}
{ rsep:=PAR_WYDR.RSEP;  ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xw1}
{DO}
   {
   ww[1]:=form(X_Xw1.KTM);
   ww[2]:=form(X_Xw1.NAZ);
   ww[3]:=form(X_Xw1.LOK);
   ww[4]:=form(X_Xw1.TW);
   ww[5]:=form(X_Xw1.ILD,,dokl,'.,');
   ww[6]:=form(X_Xw1.ILS,,dokl,'.,')
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
:!UTF-8
{TITLE:Karta drogowa}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'I','B',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
}
{END:
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: historia.rpm
Utworzony  : 2010-01-07
Zawartość  : Karta drogowa
==================================================
{COMMENT-}
{MACRO: 'one'}
{<1}╔════════════════════════════════════════════════════════════════════════════════╗
{<1}║                        K A R T A    D R O G O W A                              ║
{<1}╠═══════════════╤════════════════════════════════════════════════════════════════╣
{<1}║ Pojazd        │{__SAMK.NAZ}{<82 '║'}
{<1}╟───────────────┼───────────────────────┬────────────────┬───────────────────────{<82 '╢'}
{<1}║ Symbol karty  │{__SAMK.SYM}        {<41 '│'}{<43}Nr rej.     {<58 '│'} {__SAMK.NRREJ}{<82 '║'}
{<1}╟───────────────┼───────────────────────┼────────────────┼───────────────────────{<80 '╢'}
{<1}║ Data pocz.    │{$__SAMK.D}           {<41 '│'}{<43}Data końcowa {<58 '│'} {$__SAMK.DK}{<82 '║'}
{<1}╟───────────────┴──────┬────────────────┼────────────────┼───────────────────────╢
{<1}║ Stan licznika pocz.  │ {>39 $__SAMK.SP} {<41}│ {<43}końcowy      {<58}│ {>78 $__SAMK.SK}{<82 '║'}
{<1}╟──────────────────────┼────────────────┼────────────────┼───────────────────────╢
{<1}║ Przebieg             │ {>39 $(__SAMK.SK-__SAMK.SP)}{<41}│ {<43}Godz. pracy {<58}│{>78 $__SAMK.GP}{<82 '║'}
{<1}╟──────────────────────┼────────────────┼────────────────┼───────────────────────╢
{<1}║ Zużycie wg normy     │ {>39 form(__SAMK.Z_NORMA,,2,' ,')}{<41}│{<43}Zużycie rzecz. {<58}│{>78 form(__SAMK.Z_RZECZ,,2,' ,')}{<82 '║'}
{<1}╟──────────────────────┼────────────────┼────────────────┼───────────────────────╢
{<1}║ Wynik                │ {>39 form(__SAMK.ZU_KART,,2,' ,')}{<41}│{<43}Opis {<58}│{>78 __SAMK.PO}{<82 '║'}
{<1}╟──────────────────────┼────────────────┼────────────────┼───────────────────────╢
{<1}║ Status karty         │ {>39 {? __SAMK.STATUS='T' || 'Zaakceptowana' || 'Niezaakceptowana' ?}}{<41}│{<43}Data akc. {<58}│{>78 $__SAMK.DATA_AKC}{<82 '║'}
{<1}╟──────────────────────┼────────────────┼────────────────┼───────────────────────╢
{FOR: P_KAP}{INDEX:'K'}{PREFIX:__SAMK.REF,'T'}
{DO}
{<1}║ Kierowca             │ {>39 P_KAP.OSOBA().NAZWISKO}{<41}│{<43}Podpis {<58}│{>78 '.............'}{<82 '║'}
{OD}
{P_KAP.index('K');~~}
{P_KAP.prefix(SAMK.ref(),'T');~~}
{IF: P_KAP.size()=0}
{<1}║ Kierowca             │ {>39}{<41}│{<43}Podpis {<58}│{>78 '.............'}{<82 '║'}
{FI}
{<1}╚══════════════════════╧════════════════╧════════════════╧═══════════════════════╝
{MACRO-}
{MACRO: 'licznik'}
{<1}╔════════════════════════════════════════════════════════════════════════════════╗
{<1}║                        K A R T A   Z M I A N Y  L I C Z N I K A  {<82 '║'}
{<1}╠═══════════════╤════════════════════════════════════════════════════════════════╣
{<1}║ Pojazd        │{__SAMK.NAZ}{<82 '║'}
{<1}╟───────────────┼───────────────────────┬────────────────┬───────────────────────{<82 '╢'}
{<1}║ Symbol karty  │{__SAMK.SYM}        {<41 '│'}{<43}Nr rej.     {<58 '│'} {__SAMK.NRREJ}{<82 '║'}
{<1}╟───────────────┼───────────────────────┴────────────────┴───────────────────────{<80 '╢'}
{<1}║ Data zmiany   │{$__SAMK.D}           {<82 '║'}
{<1}╟───────────────┼────────────────────────────────────────────────────────────────╢
{<1}║ Stan licznika │{$__SAMK.SP} {<41}{<82 '║'}
{<1}╟───────────────┼────────────────────────────────────────────────────────────────╢
{{? +__SAMK.OPIS
 || STR.spl_word();
    STR.split(__SAMK.OPIS,' ')
 ?}
;~~}
{<1}║ Opis          │{>35 STR.line(40)}{<82 '║'}
{IF: +__SAMK.OPIS}
{IF: STR.next()}
{<1}╟───────────────┘ {>40 STR.line(40)}{<82 '║'}
{FI}
{WHILE: STR.next()}
{DO}
{<1}║ {STR.line(75)}{<82 '║'}
{OD}
{FI}
{IF: +__SAMK.OPIS>40}
{<1}╚════════════════════════════════════════════════════════════════════════════════╝
{ELSE}
{<1}╚═══════════════╧════════════════════════════════════════════════════════════════╝
{FI}
{MACRO-}
{FOR: __SAMK}
{DO}
{IF: SAMK.STATUS='S'}
{SUBSTITUTE: 'licznik'}
{ELSE}
{SUBSTITUTE: 'one'}
{FI}
{PAGE}
{OD}
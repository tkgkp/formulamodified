:!UTF-8
{TITLE:lista płac (paski)}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','C',,,,,,1,5,5,5,5,0,0}
{BEGIN:
   'prolog';
   FUNKCJE.TBEGIN();
   Banknoty:=" form(Nominal[_a],10,2)+':'+
               form(Tbank[_a],10)+'  '+(10*'─')+
               form(Nominal[_a]*Tbank[_a],13,2)
             ";
   LISTA.Update();
   max:=FUNKCJE.TCOLUMNS()*37;
   nom:=FUNKCJE.NOMINAL(200);
   FUNKCJE.TFREE();
   col:=row:=0;
   kwota:=0;
   LPrac:=LWydz:=0;
   _data:=date(O.R,O.M,0);

   P.cntx_psh;
   P.clear;
   H.cntx_psh;
   H.index('_HISTKOD');
   UD_SKL.cntx_psh();
   UD_SKL.clear();

   {? var_pres('__SKL')>100 || obj_del(__SKL) ?};
   __SKL:=exec('plaski_ud_def','schemat',,'PPL','PODZORG',exec('ref_firma','ustawienia'));

   _key:=__SKL.index('?');
   __SKL.index(__SKL.ndx_tmp(,,'INT_REF',,));

   {? var_pres('__P')>100 || obj_del(__P) ?};
   __P:=sql(
      'select   0 WYDZIAL, '
      '         case '
      '         when P.DZA<=to_date(:_a) then '
      '            case '
      '            when P.DZ is null then \'T\' '
      '            when P.DZ<to_date(:_a) then \'N\' '
      '            else \'T\' '
      '            end '
      '         else \'N\' '
      '         end Z, '
      '         OSOBA.NAZWISKO, '
      '         OSOBA.PIERWSZE, '
      '         P.REFERENCE as P_REF, '
      '         0 MBREF '
      'from     P join OSOBA '
      'where    P.FIRMA=:_c and P.F_ZATR=:_b '
      'order by P_REF',
      _data,
      O.F_ZATR,
      exec('ref_firma','ustawienia')
   );

   {? var_press('__P')=type_of(SYSLOG)
   || _test:=
         {? exec('is_active','gp_api')
         || "exec('has_access_p_h','gp_api',,_a,'PPL')"
         || "1"
         ?};
      {? __P.first()
      || {!
         |? {? P.seek(__P.P_REF) & _test(_data)
            || H.prefix(P.ref(),'Z');
               __P.MBREF:=P.ref();
               {? H.find_le(_data) &
                  __SKL.find_key(#H.WYDZIAL)
               || __P.WYDZIAL:=H.WYDZIAL
               || __P.WYDZIAL:=P.WYDZIAL
               ?};
               __P.put()
            ?};
            __P.next()
         !};
         __NDX:=__P.ndx_tmp(,,
            'WYDZIAL',,0,
            'Z',,0,
            'NAZWISKO',,0,
            'PIERWSZE',,0,
            'MBREF',,0
         );
         __P.index(__NDX)
      ?}
   ?};

   __SKL.index(_key)
}
{END:
   'epilog';
   {? var_pres('__P')=type_of(SYSLOG)
   || obj_del(__P);
      &__P
   ?};
   {? var_pres('__SKL')=type_of(SYSLOG)
   || obj_del(__SKL);
      &__SKL
   ?};
   P.cntx_pop;
   H.cntx_pop;
   UD_SKL.cntx_pop();

   FUNKCJE.TEND();
   FUNKCJE.NOMINAL();
   &max; &col; &row;
   &kwota;
   &nom;
   &Banknoty;
   &LPrac;
   &LWydz
}
{EXIT: var_press('__P')<>type_of(SYSLOG)}
{EXIT: LISTA.__Fatal}
{HEADER}
  {>{max}KST.MIASTO+', dn. '+$date}
  {KST.NAZWA}
  {<3>{max}'W Y D R U K   L I S T Y   P Ł A C'}
  {<3>{max}'Lista płac '+VAR.NAZWALIS+' ('+date(O.R,O.M,1)$8+') nr '+$O.N}
{IF: +O.O}
  {<3>{max}O.O}
{FI}

{HEADER-}
{FOOTER:0 }
{max*'─'}
{>{max}'str. '+$page}
{FOOTER-}
{COMMENT}

   Makrodefinicje do formatowania składników wypłaty pracownika i zbiorówek
   jednostek i zakładu. __nazwa(20 znaków)__kwota(13 znaków)

{COMMENT-}
{MACRO: 'prac'}{
   '  '+(_a:=form(FUNKCJE.TPRAC(col,row,2),20))+
   '  '+{? +(|_a) & FUNKCJE.TPRAC(col,row,1)
           || form(FUNKCJE.TPRAC(col,row,3),13,2)
           || 13*' '
        ?}
}{MACRO-}
{MACRO: 'wydz'}{
   '  '+(_a:=form(FUNKCJE.TWYDZ(col,row,2),20))+
   '  '+{? +(|_a) & FUNKCJE.TWYDZ(col,row,1)
           || form(FUNKCJE.TWYDZ(col,row,3),13,2)
           || 13*' '
        ?}
}{MACRO-}
{MACRO: 'zakl'}{
   '  '+(_a:=form(FUNKCJE.TZAKL(col,row,2),20))+
   '  '+{? +(|_a) & FUNKCJE.TZAKL(col,row,1)
           || form(FUNKCJE.TZAKL(col,row,3),13,2)
           || 13*' '
        ?}
}{MACRO-}
{COMMENT}

   Przeglądaj wszystkie jednostki i dla każdej z nich przeglądaj kartotekę
   osobową. Dla każdego z nich pobieraj składniki wypłaty. Jeżeli są jakieś,
   to drukuj pasek "wypłaty" pracownika.

{COMMENT-}
{FOR: __SKL}{DO}
{  UD_SKL.seek(__SKL.SQL_REF);
   FUNKCJE.TWYDZ();
   LWydz:=0;
   ~~
}
{FOR: __P}{INDEX: __NDX}{PREFIX: __SKL.INT_REF}{DO}
{IF: P.seek(__P.MBREF,P.name)}
{  echo('Lista: '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE);
   FUNKCJE.TPRAC();
   col:=row:=1;
   ~~
}
{COMMENT}

   S K Ł A D N I K I   W Y P Ł A T Y   P R A C O W N I K A

{COMMENT-}
{ENTIRE}
{IF: FUNKCJE.TROWS('Prac')}
{ LPrac+=1; LWydz+=1; max*'─'}

{form(LPrac,-5,,'9.')} ({form(LWydz,-5,,'9.')})  {P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE}   nr akt {|P.T}
               Lista płac za {date(O.R,O.M,1)$8}
{IF: O.D<>date(0,0,0)}Wypłacona dnia {O.D$1}{FI}{IF: {? O.DKURS<>date(0,0,0) || exec('h_find','pracownik'); H.CZYWAL='T' | H.CZYWAL2='T' | H.CZYWAL3='T'  ?}
}      Kurs z dnia {O.DKURS}: {H.WAL(); exec('waluta_miara','lista_licz',SLO.KOD)} {SLO.KOD} = {exec('waluta_kurs','lista_licz',H.ZWAL,O.DKURS,H.WAL)} PLN  {FI}

{WHILE: row<=FUNKCJE.TROWS('Prac')}{DO}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'prac'}{ col+=1; ~~ }{OD}
{ col:=1; row+=1; ~~ }
{OD}
{ col:=1; row:=0; ~~ }
{FUNKCJE.TCOLUMNS()*('  '+(35*'═'))}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'prac'}{ col+=1; ~~ }{OD}
{  FUNKCJE.TBANK(kwota:=FUNKCJE.TPRAC(FUNKCJE.TCOLUMNS(),0,3)); ~~ }
{FI}
{ENTIRE-}
{FI}
{OD}
{COMMENT}

   S K Ł A D N I K I   Z B I O R Ó W K I   W Y D Z I A Ł U

{COMMENT-}
{ col:=row:=1; ~~ }
{IF: FUNKCJE.TROWS('Wydz')}
{IF: PAR_SKID.get(193)='T'}{IF: lineleft}{PAGE}{FI}{FI}
{ENTIRE}
{max*'─'}

  Podsumowanie cząstkowe dla jednostki: {UD_SKL.SYMBOL}  {UD_SKL.OPIS}
  Lista płac {VAR.NAZWALIS+' ('+date(O.R,O.M,1)$8+')'}
  Razem pracowników: {LWydz}

{WHILE: row<=FUNKCJE.TROWS('Wydz')}{DO}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'wydz'}{ col+=1; ~~ }{OD}
{ col:=1; row+=1; ~~ }
{OD}
{ col:=1; row:=0; ~~ }
{FUNKCJE.TCOLUMNS()*('  '+(35*'═'))}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'wydz'}{ col+=1; ~~ }{OD}
{ENTIRE-}
{IF: PAR_SKID.get(193)='T'}{IF: lineleft}{PAGE}{FI}{FI}
{FI}
{OD}
{COMMENT}

   S K Ł A D N I K I   Z B I O R Ó W K I   Z A K Ł A D U

{COMMENT-}
{ENTIRE}
{ col:=row:=1; ~~ }
{IF: FUNKCJE.TROWS('Zakl')}
{max*'─'}

  Podsumowanie końcowe dla uwzględnionych jednostek
  Razem pracowników: {LPrac}

{WHILE: row<=FUNKCJE.TROWS('Zakl')}{DO}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'zakl'}{ col+=1; ~~ }{OD}
{ col:=1; row+=1; ~~ }
{OD}
{ col:=1; row:=0; ~~ }
{FUNKCJE.TCOLUMNS()*('  '+(35*'═'))}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'zakl'}{ col+=1; ~~ }{OD}
{ kwota:=FUNKCJE.TZAKL(FUNKCJE.TCOLUMNS(),0,3); ~~ }

{SPACE:'c'}
{>{max-50}'Nominały:'}
{WHILE: nom>0}{DO}
{IF: Tbank[nom]}{>{max}Banknoty(nom)}{FI}
{ nom-=1; ~~ }
{OD}
{>{max}(13*'═')}
{>{max}form(kwota,13,2)}
{SPACE:'@'}


{SPACE:'c'}
{>{max}'Sprawdzono pod względem merytorycznym: '+(20*'_')}
{SPACE:'@'}
Zatwierdzono do wypłaty: {,2kwota} zł
słownie: #{STR.kwota_sł(kwota)}#

{SPACE:'c'}
{<1>{max}'Główny księgowy: '+(20*'_')}{>{max}'Osoba zatwierdzająca: '+(20*'_')}
{SPACE:'@'}
{FI}
{ENTIRE-}
{COMMENT}

   Koniec definicji sprawozdania

{COMMENT-}
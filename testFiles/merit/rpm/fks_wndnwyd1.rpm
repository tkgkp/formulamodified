:!UTF-8
{TITLE:B. Wydruk analizy należności i zobowiązań - skrócony}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN: WIND_KH.cntx_psh();
        w:=obj_new(11); ww:=obj_new(11); i:=0; dru_sum:=0;
        PRN:=obj_new(@.CLASS.PRINT,3); PRN.sl_frame();
        PRN1:=obj_new(@.CLASS.PRINT,20); PRN1.sl_frame();
        {! _i:=1..11 |! w[_i]:=''; ww[_i]:=0 !};
        nag1:=nag2:=nag3:=nag4:=nag5:=nag6:=nag7:=nag8:='';
        {? WIND_N.PRZED_TP<>0 || nag1:=form('< '+$WIND_N.PRZED_TP+' przed TP',-15); _i:=1 || _i:=0 ?};
        prz:='';
        {! _j:=1+_i..max_prz+1 |!
           prz:=($('WIND_N.PRZ'+$({? _j>=max_prz+1-_i || max_prz-_i || _j-_i ?})))();
                ($('nag'+$(_j)+':='+
                {? _j=max_prz+1
                || '''> ''+$prz'
                || '''do '''+'+$'+$prz
                ?}
                ))()
        !};
        ile_kol:=1;
        color:=''; prn1:=prn2:='PRN1';
        lm:=ll:=lmt:=rsep:=0; vp:=1; rsep:=PAR_WYDR.RSEP; PAR_WYDR.RSEP:=1; fun:=''
      }
{END:  WIND_KH.cntx_pop();
       obj_del(w); obj_del(ww); obj_del(PRN); obj_del(PRN1); &i; &ile_kol;
       &color; &prn1; &prn2; &prz; &dru_sum;
       &lm; &ll; &vp; &lmt; PAR_WYDR.RSEP:=rsep; &rsep; &fun;
       &nag1; &nag2; &nag3; &nag4; &nag5; &nag6; &nag7; &nag8
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_wndnwyd1.rpm
Utworzony:   2005-06-17 [AMK] Artur Makos
==================================================
Zawartosc:  Wydruk analizy naleznosci i zobowiazan - skrocony
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{  PRN1.add("w[1]",46,'Kontrahent'); ~~}
{  PRN1.add("w[2]",15,'Przed terminem',1); ~~}
{IF: nag1<>''}{ PRN1.add("w[3]",15,nag1,1,'#'); ile_kol+=1; ~~}{FI}
{IF: nag2<>''}{ PRN1.add("w[4]",15,nag2,1,'#'); ile_kol+=1; ~~}{FI}
{IF: nag3<>''}{ PRN1.add("w[5]",15,nag3,1,'#'); ile_kol+=1; ~~}{FI}
{IF: nag4<>''}{ PRN1.add("w[6]",15,nag4,1,'#'); ile_kol+=1; ~~}{FI}
{IF: nag5<>''}{ PRN1.add("w[7]",15,nag5,1,'#'); ile_kol+=1; ~~}{FI}
{IF: nag6<>''}{ PRN1.add("w[8]",15,nag6,1,'#'); ile_kol+=1; ~~}{FI}
{IF: nag7<>''}{ PRN1.add("w[9]",15,nag7,1,'#'); ile_kol+=1; ~~}{FI}
{IF: nag8<>''}{ PRN1.add("w[10]",15,nag8,1,'#'); ile_kol+=1; ~~}{FI}
{ PRN1.add("w[11]",15,'Ogółem',1); ile_kol+=1; ~~}
{ PRN.add("",46,''); ~~}
{ PRN.add("",ile_kol*15+ile_kol-1,'Kwoty');~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{PAR_WYDR.TITLE:='WYDRUK ANALIZY '+
                 {? WIND_N.TYP='N' || 'NALEŻNOŚCI' || 'ZOBOWIĄZAŃ' ?}+' NA DZIEŃ '+WIND_N.DATA$1;~~}
{SUBSTITUTE: 'HEAD'}{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}
{<3 'Parametry wydruku:'}
{<6 'Waluta: '+WIND_N.WAL().TR+' - '+WIND_N.WAL().KOD}{<60
 {? WIND_N.ZAK_KON='M' || 'Maska kont: '+WIND_N.KON_MAS || 'Prefix kont: '+WIND_N.KON_PRE?}}{<115
 'Generować pozycje rozr. po terminie: '+{? WIND_N.Z='T' || 'tak' || 'nie' ?}}
{<6 'Jednostka księgowa: '+{? WIND_N.ODD=0 || 'wszystkie jednostki księgowe' || WIND_N.ODD().OD ?}}{<60
 'Rozrachunki '+{?WIND_N.ZAK_ROZ='T' || 'przed terminem poniżej '+$WIND_N.PRZED_TP ||  'przed terminem powyżej '+$WIND_N.PO_TP ?}+' dni'}
{<6 'Operator: '+WIND_N.USER}{<60'Ostatnie płatności '+$WIND_N.OST_PL+' dni wstecz'}{<115
 'Tylko oznaczone do monitowania: '+{? WIND_N.OZNACZ='T' || 'tak' || 'nie' ?}}
{<6 'Kontrahenci: '+{? -WIND_N.ZAK_KH='b' || 'wybrani' || 'wszyscy' ?}}{<60
 'Kontrahenci powyżej wartości: '+form(WIND_N.OD_KW,,2,' ,')}
{<6 'Dla granicznych wartości przedziału zakres \'od\' nie należy do przedziału, zakres \'do\' należy'}
{<6 'Opis: '+WIND_N.OPIS}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{ PRN1.setcolor(color); PRN.setcolor(color);~~}
{REP: PRN.fhbar(lmt)}
{PRN.ini_head()}
{WHILE: PRN.h_next()}{DO}
{REP: PRN.h_fmline(lmt)}{OD}
{REP: PRN.fjbar(PRN1)}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}{DO}
{REP: PRN1.h_fmline(lmt)}{OD}{ vp:=1;~~}
{HEADER-}
{FOOTER:0}
{IF: vp}{REP: PRN1.lbar()}{ELSE}{PRN1.lbar()}{FI}{ vp:=1;~~}

{FOOTER-}
{COMMENT}-----------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ _m:=$(#WIND_N.ref()); _m:=('000'+_m)+4; WIND_KH.use('wndk'+_m);
  WIND_KH.index({? WIND_N.KHKODSKR=0 || 'SKROT' || 'KOD' ?}); i:=WIND_KH.first();~~}
{WHILE: i}
{DO}
{ {! _i:=1..11 |! w[_i]:='' !};
  WIND_KH.KH();
  w[1]:={? WIND_N.KHKODSKR=0 || KH.SKR || KH.KOD ?}+{? +KH.NAZ || ' - '+KH.NAZ ?};
  w[2]:=form(WIND_KH.PRZEDTER,,2,' ,');  ww[2]+=WIND_KH.PRZEDTER$2;
  w[3]:=form(WIND_KH.PRZ1,,2,' ,');      ww[3]+=WIND_KH.PRZ1$2;
  w[4]:=form(WIND_KH.PRZ2,,2,' ,');      ww[4]+=WIND_KH.PRZ2$2;
  w[5]:=form(WIND_KH.PRZ3,,2,' ,');      ww[5]+=WIND_KH.PRZ3$2;
  w[6]:=form(WIND_KH.PRZ4,,2,' ,');      ww[6]+=WIND_KH.PRZ4$2;
  w[7]:=form(WIND_KH.PRZ5,,2,' ,');      ww[7]+=WIND_KH.PRZ5$2;
  w[8]:=form(WIND_KH.PRZ6,,2,' ,');      ww[8]+=WIND_KH.PRZ6$2;
  w[9]:=form(WIND_KH.PRZ7,,2,' ,');      ww[9]+=WIND_KH.PRZ7$2;
  w[10]:=form(WIND_KH.PRZ8,,2,' ,');     ww[10]+=WIND_KH.PRZ8$2;
  w[11]:=form(WIND_KH.RAZEM,,2,' ,');    ww[11]+=WIND_KH.RAZEM$2;
  ~~}
{SUBSTITUTE: 'LINIA0'}
{ i:=WIND_KH.next();~~}
{OD}
{FONT: 'T8GB'}{SPACE: 'C'}
{ w[1]:='RAZEM';
  w[2]:=form(ww[2],,2,' ,'); w[3]:=form(ww[3],,2,' ,'); w[4]:=form(ww[4],,2,' ,');
  w[5]:=form(ww[5],,2,' ,'); w[6]:=form(ww[6],,2,' ,'); w[7]:=form(ww[7],,2,' ,');
  w[8]:=form(ww[8],,2,' ,'); w[9]:=form(ww[9],,2,' ,'); w[10]:=form(ww[10],,2,' ,');
  w[11]:=form(ww[11],,2,' ,');~~}
{SUBSTITUTE: 'LINIA0'}
{COMMENT}------------------------------FOOTER-LAST----------------------{COMMENT-}
{FOOTER}{FOOTER-}
{ENTIRE}
{IF: vp}{REP: PRN1.flbar()}{ELSE}{PRN1.lbar()}{FI}
{<1>{PRN1.width()} 'KONIEC WYDRUKU'}
{ENTIRE-}
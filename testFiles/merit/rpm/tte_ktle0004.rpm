:!UTF-8
{TITLE:5. Zapotrzebowanie surowców wg procesu technologicznego}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  TKTL.cntx_psh();
  dalej:=0;
  PRN:=obj_new(@.CLASS.PRINT,8);
  PRN.s_frame();
  "---Obiekt wydruku parametrow karty technologicznej.";
  PRNPAR:=obj_new(@.CLASS.PRINT,7);
  PRNPAR.sl_frame();
  prn1:='PRN';
  color:='';
  lm:=ll:=vp:=lmt:=rsep:=0;
  "--------------------------";
  "---Inicjalizacja obiektu 'tpar' klasy 'TPAr', sluzacego do operacji na     ";
  "---parametrach karty technologicznej.";
  VAR.A_KTL:={? VAR.ACTION='3' || TKTLW.TKTL || TKTL.ref() ?};
  VAR.A_T:={? VAR.ACTION='3' || TKTLW.KTM || VAR.A_KTL().KTM ?};
  exec('start_tpar','tech_param',VAR.A_T);
  "---Numer karty.";
  "---String zawierajacy XJM i jednostke, drukowane w naglowku.";
  ktlxjmjm := form(TKTL.XJM,,2)+' '+TKTL.JM().KOD;
  "---Jednostka miary.";
  jm:='';
  lp:=0;
  undefine();
  grp:=0;
  fnt:='10'
}
{END:
  TKTL.cntx_pop();
  &dalej;
  obj_del(PRN);
  obj_del(PRNPAR);
  &prn1;
  &color;
  &lm; &ll; &vp; &lmt; &rsep;
  "--------------------------";
  "---Usuniecie obiektu sluzacego do operacji na parametrach karty            ";
  "---technologicznej.";
  exec('stop_tpar','tech_param');
  &ktlxjmjm;
  &jm;
  &lp;
  &grp;&fnt;
  undefine()
}
{COMMENT}
  Wydruk surowcow procesu technologicznego.
  [SS] - 2000/08/28 - [7.22]
  [SS] - 2001/03/14 - [7.41] - uaktualnienie zwiazane z parametryzacja
         norm materialowych surowcow.
  [TS] - 2001/10/09 - [7.53] OWwSK003_Aneks1
  [MKO] - 2001/10/12 - [7.53] Uwaga 70
  [TS] - 2004/03/17 - [8.50] (233: Surowce powierzone)
  Wywołanie: Karty technologiczne -> Drukuj
{COMMENT-}
{ lp:=1; TMAT.index('NNL'); TMAT.prefix(TKTL.ref()); ~~}
{IF: TMAT.first()}{ dalej:=1; ~~}
{WHILE: dalej}
{DO}
{IF: TMAT.GRKTM='G'}
{grp:=1;fnt:='8';~~}
{FI}
{dalej:=TMAT.next();~~}
{OD}
{FI}
{MACRO: 'functions'}
  {"--------------------------------------------------------------------------";
   "---Wyswietla okienko z pytaniem, czy drukowac wartosci czy algorytmy      ";
   "---na surowcach, operacjach i narzedziach (odpowiednio - normy            ";
   "---materialowe, normy czasowe oraz zuzycie).                              ";
   "---Jezeli zostalo wybrane drukowanie wartosci, to wyswietla okienko       ";
   "---redakcyjne, aby umozliwic redakcje domyslnych parametrow karty.        ";
   "---Zwraca 2, jezeli beda drukowane algorytmy - 1, jezeli wartosci,        ";
   "---natomiast 0, jezeli operator przerwal dzialanie wydruku.";
   pchange:="
     _result:=tpar.change();
     _result
   ";
   "--------------------------------------------------------------------------";
   "---Zwraca liczbe parametrow do biezacej karty technologicznej.";
   pcount:="
     TPAR.index('NN');
     TPAR.prefix(TKTL.ref());
     TPAR.size()
   ";
  ~~}
{MACRO-}
{COMMENT}---Definicja wiersza wydruku surowców TMAT.------------------{COMMENT-}
{  PRN.add("form(lp,,0,'9')+'.'",6,'Lp.'@,1);
   PRN.add("{? ~(-TMAT.GRKTM)='G'
             || ' '+TMAT.TGDFLT().PT().R
             || ' '+TMAT.PT().R
            ?}",3,'WMP'@);
   "Grupa, czy samodzielny KTM.";
    {? grp=1 ||
  PRN.add("{? ~(-TMAT.GRKTM)='G'
             || TMAT.TGRP().GR+' - '+TMAT.TGRP().OPIS
             || '-'
            ?}",35,'Grupa technologiczna'@)
 ?};
 {? grp=1 ||
 PRN.add("{? ~(-TMAT.GRKTM)='G'
             || jm:=TMAT.TGDFLT().PT().J().KOD;
                TMAT.TGDFLT().PT().KTM+' - '+TMAT.TGDFLT().PT().N
             || jm:=TMAT.PT().J().KOD;
                TMAT.PT().KTM+' - '+TMAT.PT().N
            ?}",40,'Domyślny KTM'@)
 ||
     PRN.add("jm:=TMAT.PT().J().KOD; TMAT.PT().KTM+' - '+TMAT.PT().N
              ",38,'Surowiec KTM'@)
  ?};
   PRN.add("form( ({? form(TMAT.FORMB)<>''
                    || tpar.calc(TMAT.FORMB)
                    || TMAT.WARB
                    ?}) *DEFINE.NA_ILE/TKTL.XJM,,4)",15,'Zapotrzebowanie wg normy mat. brutto'@,1);
   PRN.add("jm",5,'JM'@);
   PRN.add("TMAT.SO",3,'S/O'@);
   PRN.add("TMAT.POW",6,'Powie- rzony'@);

   PRNPAR.add("'p('+$(tpar.P[1][i])+')'",10,'Symbol'@);
   PRNPAR.add("tpar.P[3][i]",40,'Opis'@);
   PRNPAR.add("form(tpar.P[2][i],25,10)",25,'Wartość'@,1);

   PAR_WYDR.TITLE:='ZAPOTRZEBOWANIE SUROWCÓW WG PROCESU TECHNOLOGICZNEGO'@; ~~}
{INCLUDE: wsp_pw.rpi}
{SUBSTITUTE: 'functions'}
{IF: pcount()>0}
  {IF: (paramprt:=pchange())=0}
    {EXIT}
  {FI}
{FI}
{EXIT:
   define('NA_ILE',TKTL.XJM,'wielkość produkcji'@,'Na jaką wielkość produkcji sporządzasz wydruk'@);
   def_btn('text=%1'['Zapisz'@],'key:F2');
   def_btn('text=%1'['Anuluj'@],'key:Esc');
   ~def_edit("{? DEFINE.NA_ILE>0 || 1 || FUN.emsg('Podaj liczbę większą od zera.'@);0 ?}",FUN.TYT)
}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T'+fnt}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Karta technologiczna: %1'@[TKTL.NRK]}
{<{lmt+1} 'Produkt:  %1 - %2'@[VAR.A_T().KTM,M.N]}
{<{lmt+1} {? TKTL.TYP().PAR='T' || 'Karta parametryzowana'@ || 'Karta nieparametryzowana'@ ?}}
{<{lmt+1} 'Zapotrzebowanie na: %1 %2'@[form(DEFINE.NA_ILE,,4),TKTL.JM().KOD]}
{FONT: 'T'+fnt+'B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T'+fnt+'B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T'+fnt}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ lp:=1; TMAT.index('NNL'); TMAT.prefix(TKTL.ref()); ~~}
{IF: TMAT.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
   {IF: TMAT.ACT='T'}
    {SUBSTITUTE: 'LINIA0'}
    {lp+=1; ~~}
   {FI}
    {dalej:=TMAT.next(); ~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{"------------------------------------------------------------------------"; ~~}
{"---Tutaj wydruk parametrow karty technologicznej.                       "; ~~}
{IF: pcount()>0}
  {TPAR.index('NN'); TPAR.prefix(VAR.A_KTL); ~~}
  {IF: TPAR.first()}{ dalej:=1; prn1:='PRNPAR';~~}
    {IF: lineleft < 14}{PAGE}{FI}
    {FONT: 'W12'}{SPACE: 'A'}
{<1  lk:=charleft(); ~~}
    {<1>{lk} 'SPIS PARAMETRÓW KARTY TECHNOLOGICZNEJ'@}
    {FONT: 'T10B'}
{<1  lmt:=int((charleft()-PRNPAR.width())/2); ~~}
    {SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
    {i:=1; ~~}
    {WHILE: dalej}
    {DO}
     {IF: lineleft > 8} {SUBSTITUTE: 'LINIA0'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T10B'}{SPACE: 'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
     {FI}
     { i+=1; dalej:=TPAR.next();~~}
   {OD}
   {SUBSTITUTE: 'LINIAF'}
  {FI}
{FI}

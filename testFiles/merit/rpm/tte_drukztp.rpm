:!UTF-8
{TITLE:Typy zleceń}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  ZTP.cntx_psh();
  PRNZTP := obj_new(@.CLASS.PRINT, 11);
  PRNZTP.s_frame();
  kreska := '─';
    canprint:=1;
  "---Pomocnicza do iteracji.";
  iter:=1;
  "---Liczba wariantow kalkulacji dla danego typu zlecenia.";
  opccount:=0;
  prn1:='PRNZTP';
  waslbar:=0;
  row:='';
  row1:='';
  dalej:=0;
  color:='';
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  ZTP.cntx_pop;
  obj_del(PRNZTP);
  &dalej;&kreska;
  &opccount;
  &iter;&row1;
  &prn1;&row;
  &color;
  &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
   Wydruk tabeli typow zlecen.
  [TS] & [SS] - 2000/07/13 - [7.22]
  [SS] - 2001/03/29 - [7.41] - dadanie listy wariantow analitycznych
  [MKO] - 2001/11/12 - [7.53] OWwSK003_Aneks1
  Wywołanie: Ustawienia i parametryzacja -> Produkcja -> Typy zleceń -> Typy zleceń -> Drukuj
{COMMENT-}
{COMMENT}--Definicja wiersza wydruku typów zleceń.--------------------{COMMENT-}
{ PRNZTP.add("{? canprint || ZTP.TYP || '' ?}",3,'Typ'@); ~~}
{"---W tej kolumnie pod opisem bedzie drukowany spis wariantow            "; ~~}
{"---analitycznych. Drukowany wiersz nalezy umiescic w zmiennej 'row'.    "; ~~}
{ PRNZTP.add("row",47,'Opis'@+' / '+'Wariant'@); ~~}
{COMMENT}
  {PRNZTP.add("ZTP.OPIS",31,'Opis'); ~~}
{COMMENT-}
{ PRNZTP.add("{? canprint || ZTP.KOD_ZLC || '' ?}",9,'Kod numeracji zleceń'@);
  PRNZTP.add("{? canprint || ZTP.KOD_ZLP || '' ?}",9,'Kod numeracji podzleceń'@);
  PRNZTP.add("{? canprint || ZTP.ILE_PRZ || '' ?}",8,'Ilość znaków'@);
  PRNZTP.add("{? canprint || ZTP.WP || '' ?}",3,'W/P');
  PRNZTP.add("{? canprint || form(ZTP.CRUBALL) || '' ?}",3,'KC');
  PRNZTP.add("{? canprint || form(ZTP.CRUBMAT) || '' ?}",3,'KM');
  PRNZTP.add("{? canprint || form(ZTP.CRUBROB) || '' ?}",3,'KR');
  PRNZTP.add("{? canprint || form(ZTP.CRUBJEDN) || '' ?}",3,'CJ');
  PAR_WYDR.TITLE:='WYKAZ TYPÓW ZLECEŃ'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRNZTP.width())/2); ~~}
{<{lmt+1} 'Legenda:'@}
{<{lmt+1} 'W/P - '+'Warsztatowe/Produkcyjne'@}
{<{lmt+1} 'KC  - '+'Rubryka kosztu całkowitego'@}
{<{lmt+1} 'KM  - '+'Rubryka kosztu materiału'@}
{<{lmt+1} 'KR  - '+'Rubryka kosztu robocizny'@}
{<{lmt+1} 'CJ  - '+'Rubryka ceny jednostkowej'@}
{<{lmt+1} '*   - '+'Domyślny wariant analityczny'@}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{"---Wydruk wiersza tabeli typow zlecen ZTP.                              "; ~~}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ lp:=1; ZTP.index('TP'); ~~}
{IF: ZTP.first()}{ dalej:=1; ~~}
{WHILE: dalej}
{DO}
    {canprint:=1; ~~}
    {row:=ZTP.OPIS; ~~}
    {SUBSTITUTE: 'LINIA0'}
    {"---Teraz juz nie drukujemy zawartosci kolumn - oprocz kolumny Opis. "; ~~}
    {canprint:=0;rsep:=0;~~}
    {"---Oddziel opis od wariantow pozioma kreska.                        "; ~~}
    {row:=kreska*27; ~~}
     {SUBSTITUTE: 'LINIA0'}
     {"---Dodaj opis, ze sa to warianty kalkulacji.                        "; ~~}
    {row:=' '+'Warianty analityczne:'@; ~~}
        {SUBSTITUTE: 'LINIA0'}
    {"---Teraz drukuj warianty analityczne.                               "; ~~}
    {ZTPF.index('TO'); ~~}
    {ZTPF.prefix(ZTP.ref()); ~~}
    {opccount:=ZTPF.size(); ~~}
    {iter:=1; ~~}
    {FOR: 'ZTPF'}
      {INDEX: 'TO'}
      {PREFIX: ZTP.ref()}
    {DO}
      {row:={? opccount > 1
            || form(iter,2)+'. '+ZTPF.OPC().NA+': '+form(ZTPF.OPC().WST,,,'9')
            || ' '+ZTPF.OPC().NA+': '+form(ZTPF.OPC().WST,,,'9')
            ?}; ~~}
      {"---Domyslny wariant analityczny zaznacz gwiazdka.                 "; ~~}
      {IF: ZTP.DEF_OPCA=ZTPF.OPC}
        {row:='*'+(1-row); ~~}
      {FI}
      {iter+=1; ~~}
     {SUBSTITUTE: 'LINIA0'}
  {OD}
  {dalej:=ZTP.next(); rsep:=PAR_WYDR.RSEP;row:=''; ~~}
{OD}
{FI}

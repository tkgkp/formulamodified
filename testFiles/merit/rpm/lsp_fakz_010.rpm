:!UTF-8
{TITLE:D. Zestawienie sprzedaży w podziale na grupy kontrahentów}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   fakz_010:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','doklw','PRN');
      VAR_DEL.delete('od_datyw','do_datyw');
      VAR_DEL.delete('od_daty','do_daty');
      VAR_DEL.delete('od_platnosc','do_platnosc');
      VAR_DEL.delete('filtr_kh');
      VAR_DEL.delete('wydr_naz','wydr_edi','wyjscie','X_Xb','X_Xw1')";
   fakz_010();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   n :=obj_new(5); "--nazwy kolumn--";
   k :=obj_new(5); "--szerokosci kolumn--";
   w :=obj_new(5); "--wartosci kolumn--";
   s :=obj_new(3); "--razem--";
   doklw :=2; "--dokladnosc wartosc--";
   PRN := obj_new(@.CLASS.PRINT,8); PRN.s_frame();

   k[1] :=20;     n[1] :='Kod'@;
   k[2] :=40;     n[2] :='Nazwa'@;
   k[3] :=17;     n[3] :='Wartość netto'@;
   k[4] :=17;     n[4] :='Wartość VAT'@;
   k[5] :=17;     n[5] :='Wartość brutto'@;

   {! _ii..5 |! w[_ii] :='' !};
   {! _ii..3 |! s[_ii] :=0 !};
   tryb:=rep_export();

   od_datyw:=do_datyw:=date(0,0,0);
   od_daty:=do_daty:=date(0,0,0);
   od_platnosc:=do_platnosc:=date(0,0,0);

   filtr_kh :='N';

   wydr_naz:='fakz_010';
   wydr_edi:='FAKZ_010';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? exec('upr_cs','ceny') & WYDRUKIL.edit()
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         filtr_kh:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         od_datyw:=WYDRUKIL.OD_DATY2;
         do_datyw:=WYDRUKIL.DO_DATY2;
         od_daty:=WYDRUKIL.OD_DATY;
         do_daty:=WYDRUKIL.DO_DATY;
         od_platnosc :=WYDRUKIL.OD_DATY1;
         do_platnosc :=WYDRUKIL.DO_DATY1
      ?}
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_kh','kontrahent',filtr_kh,'N','G') ?}
}
{END:
   fakz_010(); VAR_DEL.delete('fakz_010')
}
{IF: exec('upr_cs','ceny')=0}{ FUN.info('Brak uprawnień do wartości sprzedaży.'@);~~}{EXIT}{FI}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
PRN.add("w[3]",k[3],n[3],1);
PRN.add("w[4]",k[4],n[4],1);
PRN.add("w[5]",k[5],n[5],1);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'KOD'    ,'STRING[50]'  ,'Kod',
   'NAZ'    ,'STRING[100]'  ,'Nazwa',
   'NETTO'  ,'REAL'        ,'Netto',
   'VAT'    ,'REAL'        ,'VAT',
   'BRUTTO' ,'REAL'        ,'Brutto');
X_Xw1.index(X_Xw1.ndx_tmp(,,'KOD',,,'KOD',,));

_lp:=0;
OKR.cntx_psh; FAKS.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
_min:="{? _a & _a<_b || _a || _b ?}";
_min:=_min(_min(od_datyw~1,od_daty~1),od_platnosc~1);
_max:="{? _a & _a>_b || _a || _b ?}";
_max:=_max(_max(do_datyw~1,do_daty~1),do_platnosc~1);
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
{? OKR.first() & (_min=0 | OKR.find_ge(_min)) & X_Xb.first()
||
   _stsupr:=exec('get','#params',2102,2,OPERATOR.USER)='T';
  _stss:="
                _wyn:=exec('usr_upr','b_perm','STS',_a,OPERATOR.USER);
                _wyn
";
   {!
   |?
      FAKS.use('faktu'+ST.ODDZ+(($OKR.ROK)+2));
      FAKS.index('FAK_KH');
      {? X_Xb.first()
      ||
         {!
         |?
            FAKS.prefix('S',X_Xb.KOD);
            {? FAKS.first()
            ||
               {!
               |?
                  {? FAKS.STAT_REJ<>'A'
                     & (_stsupr=0 | _stsupr=1 & FAKS.STS<>null & _stss(FAKS.STS))
                     & FAKS.KH().KOD=X_Xb.KOD
                     & ((od_datyw<=FAKS.DW | od_datyw=date(0,0,0)) & (FAKS.DW<=do_datyw | do_datyw=date(0,0,0)))
                     & ((od_daty<=FAKS.D | od_daty=date(0,0,0)) & (FAKS.D<=do_daty | do_daty=date(0,0,0)))
                     & ((od_platnosc<=FAKS.TZ | od_platnosc=date(0,0,0) ) & (FAKS.TZ<=do_platnosc
                        | do_platnosc=date(0,0,0)))
                     & FAKS.ZEST='T' & FAKS.KZ=''
                     & FAKS.AKC='T'
                  ||
                     _kod:=FAKS.KH().GRKH().KOD;
                     _naz:=FAKS.KH().GRKH().NAZ;
                     {? X_Xw1.find_key(_kod,_kod)
                     ||
                        X_Xw1.NETTO  +=FAKS.NETTO;
                        X_Xw1.VAT    +=FAKS.VAT;
                        X_Xw1.BRUTTO +=FAKS.BRUTTO;
                        X_Xw1.put()
                     ||
                        X_Xw1.KOD    :={? _kod<>'' || _kod || 'brak' ?};
                        X_Xw1.NAZ    :={? _naz<>'' || _naz || 'kontrahenci bez przypisanej grupy' ?};
                        X_Xw1.NETTO  :=FAKS.NETTO;
                        X_Xw1.VAT    :=FAKS.VAT;
                        X_Xw1.BRUTTO :=FAKS.BRUTTO;
                        X_Xw1.add()
                     ?}
                  ?};
                  FAKS.next()
               !}
            ?};
            X_Xb.next()
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max)
   !}
?};
OKR.cntx_pop; FAKS.cntx_pop;

PAR_WYDR.TITLE:='ZESTAWIENIE SPRZEDAŻY W PODZIALE NA GRUPY KONTRAHENTÓW';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: filtr_kh='T'}{<{lmt+1}}{'Z wyborem grup kontrahentów'}{FI}
{<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
{<{lmt+1}}{'Data wystawienia'}{<{lmt+1+20}od_datyw}{<{lmt+1+40}do_datyw}
{<{lmt+1}}{'Data sprzedaży'}{<{lmt+1+20}od_daty}{<{lmt+1+40}do_daty}
{<{lmt+1}}{'Termin płatności'}{<{lmt+1+20}od_platnosc}{<{lmt+1+40}do_platnosc}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozcycje wydruku                                         {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;  ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xw1}
{DO}
   {
   w[1] :=form(X_Xw1.KOD);
   w[2] :=form(X_Xw1.NAZ);
   w[3] :=form(X_Xw1.NETTO,,doklw,'.,');
   w[4] :=form(X_Xw1.VAT,,doklw,'.,');
   w[5] :=form(X_Xw1.BRUTTO,,doklw,'.,');
   s[1] +=X_Xw1.NETTO;
   s[2] +=X_Xw1.VAT;
   s[3] +=X_Xw1.BRUTTO
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1] :='';
w[2] :='Razem:'; PRN.FORM[2] :=0;
w[3] :=form(s[1],,doklw,'.,');
w[4] :=form(s[2],,doklw,'.,');
w[5] :=form(s[3],,doklw,'.,');
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
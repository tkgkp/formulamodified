:!UTF-8
{TITLE:Rozliczone świadczenia}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','__param','__okredit','__edit');
   tresc:='prozsw';
   PAR_WYDR.TITLE:='Rozliczone świadczenia';
   par:=obj_new(21);
   par[21]:=0;
   par[4]:=obj_new(2);
   par[4][2]:='';
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','Świadczeń grupowych'@,'PPL_GRP_ASWI');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
             'KOD','INTEGER','Wybór kodu',
             'OKR','INTEGER','Czy z okresu',
             'OD','DATE','Początek okresu',
             'DO','DATE','Koniec okresu');
      __param.KOD:=__param.OKR:=0;
      __param.OD:=date(,1,1);
      __param.DO:=date(,12,0);
      __param.add();

      __okredit:="{? _a
                  || __param.efld_opt(__edit,'enable='+$_a,,'OKR')
                  || __param.efld_opt(__edit,'enable='+$_a,,'OKR')
                  ?}";

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_prozsw');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'KOD',,,,,,'Sposób podziału danych'@,,,'radio-buttons',,'wg pracowników'@,"0",
                                                                                       'wg programów'@,"1");
      __param.win_efld(__edit,,'OKR',,,,,,'Zakres danych do drukowania'@,,,'radio-buttons',,'Wszystkie'@,"0",
                                                                                            'Wybrane'@,"1");
      __okredit(0);
      __param.fld_fml('KOD','AFTER_EDIT',"__okredit(__param.KOD)");
      __param.win_efld(__edit,,'OD',,,11,,,'Początek okresu'@);
      __param.win_efld(__edit,,'DO',,,11,,,'Koniec okresu'@);
      __param.efld_opt(__edit,'mark=1',,'OD');
      __param.efld_opt(__edit,'mark=1',,'DO');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);

      {? par[21]:=__param.edit("{? __param.DO=#0 | __param.OD=#0 | __param.OD>__param.DO
                                || FUN.emsg('\nDaty muszą być wypełnione.\n'+
                                            '\nData początku okresu nie może być większa od daty końcowej.\n'@);
                                   {? __param.OD=#0 || 'OD' || 'DO' ?}
                                || par[11]:=__param.OD; par[12]:=__param.DO; 1
                                ?}")
      || par[2]:=~__param.KOD;
         par[3]:=__param.OKR;

         par[1]:=obj_new(@.CLASS.PRINT,14);
         {? par[2]
            || par[1].add("par[4][1]",40,'Program'@)
            || par[1].add("par[4][1]",50,'Pracownik'@)
         ?};
         par[1].add("{? par[18] || IS_WYPL.IS_DEF().OPIS || '' ?}",60,'Opis świadczenia'@);
         par[1].add("{? par[18] || par[9] || '' ?}",10,'Lista'@,1);
         par[1].add("par[10]",12,'Kwota'@,1,,,,,'12,2');
         par[1].s_frame();

         par[13]:=tab_tmp(,'NR','INTEGER','Numer');
         par[15]:=tab_tmp(,'REF','INTEGER','Numer');
         par[17]:=par[19]:=0;
         par[18]:=1;
         par[8]:='';
         par[14]:=0;
         par[5]:=1;
         par[7]:=0;
         prn1:='par[1]';
         lmt:=rsep:=0;
         vp:=1;
         _ret:=exec('wybierz_parses','pracownik','PPL');
         params_set('P',_ret.P,'ndx',_ret.ndx);
         Lp:=0;
         b_rat:=h_rat:=1
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','__param','__okredit','__edit')
}
{COMMENT}OLD: prozsw.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~par[21]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8'}{ h_rat:=charleft; ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft/h_rat; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: '@}{par[11]} - {par[12]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'opis'}
{FONT: 'W12'}
{SPACE: 'A'}
{<{ceil(lmt*b_rat)+1}}{IF:par[2]}{Lp+=1;form(Lp,,,'9.')}{FI} {par[6]}
{FONT: 'T8'}
{SPACE: 'B'}
{MACRO-}
{MACRO: 'drukuj'}
{ {? par[18] || par[17]+=par[10] ?}; ~~ }
{IF: lineleft & lineleft<5}
   {IF: par[7]}
     {REP: par[1].flbar(lmt)}
     {  par[7]:=0;
        Lp-=1;
        {? par[8] & par[18] ||
           par[4][1]==par[4][2];
           par[8]:=0
        ?};
        ~~
     }
   {FI}
   {IF: lineleft}
      {PAGE}
   {FI}
{FI}
{IF: par[7] = 0}
   {SUBSTITUTE: 'opis'}
   {REP: par[1].fhbar(lmt)}
   { par[7]:=1; ~~ }
{FI}
{ par[1].ini_line(); ~~ }
{REP: par[1].fline(lmt)}
{IF: par[8]=0 & par[18]}{
   par[4][1]==par[4][2];
   par[8]:=1;
   ~~
}{FI}
{MACRO-}
{MACRO: 'wydruk'}
{IF: IS_WYPL.MIES='T'}
   {FOR: IS_ROZL}{INDEX: 'DATA'}{PREFIX: IS_WYPL.ref}{DO}
      {IF: {? par[12]=date(0,0,0)
              || {? par[11]<>date(0,0,0)
                    || par[11]<=IS_ROZL.D
                    || 1
                 ?}
              || par[11]<=IS_ROZL.D & IS_ROZL.D<=par[12]
           ?}}
         { par[9]:=IS_ROZL.LT; par[10]:=IS_ROZL.KW; ~~ }
         {IF: par[9]=exec('nie_lista_isplac','lista_licz') & IS_WYPL.IS_DEF().OSOBA='T'}
            { par[9]:=IS_ROZL.D$1; ~~ }
            {IF: ~par[13].find_key(#P.OSOBA)}
               {SUBSTITUTE: 'drukuj'}
            {FI}
         {ELIF: par[9]=exec('nie_lista_isplac','lista_licz')}
            {IF: IS_ROZL.P=P.ref}
               {par[9]:=IS_ROZL.D$1;~~}
               {SUBSTITUTE: 'drukuj'}
            {FI}
         {ELSE}
            {IF: par[9]<>''}
               {  LS.cntx_psh; LS.use(~par[9]); ~~ }
               {FOR: LS}
               {INDEX: 'PRACNRRU'}
               {PREFIX: P.ref,IS_ROZL.IS_WYPL().IS_DEF().R().RN}{DO}
                  {IF: IS_WYPL.IS_DEF().OSOBA='T'}
                     {SUBSTITUTE: 'drukuj'}
                  {ELIF: IS_ROZL.P = P.ref}
                     {SUBSTITUTE: 'drukuj'}
                  {FI}
               {OD}
               { LS.cntx_pop; ~~ }
            {FI}
         {FI}
      {FI}
   {OD}
{ELSE}
   {IF: {? par[12]=date(0,0,0)
           || {? par[11]<>date(0,0,0)
                 || par[11]<=IS_WYPL.OD
                 || 1
              ?}
           || par[11]<=IS_WYPL.OD & IS_WYPL.DO<=par[12]
        ?}}
      { par[9]:=IS_WYPL.LT; par[10]:=IS_WYPL.KW; ~~ }
      {IF: par[9]=exec('nie_lista_isplac','lista_licz') & IS_WYPL.IS_DEF().OSOBA='T'}
         { par[9]:=''; ~~ }
         {IF: ~par[13].find_key(#P.OSOBA)}
            {SUBSTITUTE: 'drukuj'}
         {FI}
      {ELIF: par[9]=exec('nie_lista_isplac','lista_licz')}
         { par[9]:=''; ~~ }
         {SUBSTITUTE: 'drukuj'}
      {ELSE}
         {IF: par[9]<>''}
            { LS.cntx_psh; LS.use(~par[9]); ~~ }
            {FOR: LS}
            {INDEX: 'PRACNRRU'}
            {PREFIX: P.ref,IS_WYPL.IS_DEF().R().RN}{DO}
               {SUBSTITUTE: 'drukuj'}
            {OD}
            { LS.cntx_pop; ~~ }
         {FI}
      {FI}
   {FI}
{FI}
{MACRO-}
{MACRO: tresc}
{FONT: 'W10'}
{SPACE: 'B'}
{IF: par[2]}
   {FOR: IS_WYPL}{INDEX: 'ROZL'}{PREFIX: P.OSOBA}{DO}
      {  par[8]:=par[17]:=0;
         par[18]:=1;
         par[6]:=IS_WYPL.OSOBA().NAZWISKO+' '+
                 OSOBA.PIERWSZE+
                 ' (Nr teczki '@+|P.T+')';
         par[4][2]:='';
         par[4][1]:=IS_WYPL.IS_OPIS().SLO_NAZ().NAZWA;
         ~~
      }
      {SUBSTITUTE: 'wydruk'}
      {IF: par[17]}
         {  par[4][1]:='Razem'@;
            par[10]:=par[17];
            par[19]+=par[17];
            par[18]:=0;
            ~~
         }
         {SUBSTITUTE: 'drukuj'}
         {IF: lineleft & lineleft=5}
            {IF: par[7]}
               {REP: par[1].flbar(lmt)}
               {  par[7]:=0;
                  Lp-=1;
                  {? par[8] & par[18] ||
                     par[4][1]==par[4][2];
                     par[8]:=0
                  ?};
                  ~~
               }
               {FI}
               {PAGE}
            {ELIF: lineleft & lineleft>5}
               {REP: par[1].fbar(lmt)}
            {FI}
         {FI}
   {OD}
   {  par[13].NR:=#P.OSOBA;
      par[13].add;
      ~~
   }
{ELSE}
   { params_get().P.index(params_get().ndx.SQL); params_get().P.prefix();~~ }
   {FOR: IS_WYPL}{INDEX: 'OD'}{PREFIX: IS_OPIS.ref}{DO}
      {  par[8]:=0;
         par[6]:=IS_WYPL.IS_OPIS().SLO_NAZ().NAZWA;
         par[4][2]:='';
         {? par[13].find_key(#IS_WYPL.OSOBA) || par[13].del ?};
         ~~
      }
      {FOR: P}{INDEX: 'PRACOSOB'}{PREFIX: exec('ref_firma','ustawienia'),__PARSES.getVal('F_ZATR').KOD,IS_WYPL.OSOBA}
      {DO}
      {IF: params_get().P.find_key($P.ref(),)}
      {  par[4][1]:=IS_WYPL.OSOBA().NAZWISKO+' '+
                    OSOBA.PIERWSZE+' ('+form(P.T)+')';
         par[8]:=par[17]:=0;
         par[4][2]:='';
         par[18]:=1;
         ~~
      }
      {SUBSTITUTE: 'wydruk'}
      {IF: par[17]}
         {  par[4][1]:='Razem'@;
            par[10]:=par[17];
            par[19]+=par[17];
            par[18]:=0;
            ~~
         }
         {SUBSTITUTE: 'drukuj'}
         {IF: lineleft & lineleft=5}
            {IF: par[7]}
               {REP: par[1].flbar(lmt)}
               {  par[7]:=0;
                  Lp-=1;
                  {? par[8] & par[18] ||
                     par[4][1]==par[4][2];
                     par[8]:=0
                  ?};
                  ~~
               }
            {FI}
            {PAGE}
            {ELIF: lineleft & lineleft > 5}
               {REP: par[1].fbar(lmt)}
            {FI}
         {FI}
         {  {? ~par[13].find_key(#IS_WYPL.OSOBA) ||
               par[13].NR:=#IS_WYPL.OSOBA;
               par[13].add
            ?};
            ~~
         }
         {FI}
      {OD}
   {OD}
{FI}
{IF: par[19]}
   { par[4][1]:='Ogółem'@;
     par[10]:=par[19];
     par[18]:=0;
     ~~
   }
   {SUBSTITUTE: 'drukuj'}
   { par[19]:=0; par[18]:=1; ~~ }
{FI}
{IF: par[7]}
   {REP: par[1].flbar(lmt)}
   { par[7]:=0; ~~ }
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft; ~~ }
{FONT: 'T8'}{ b_rat/=charleft; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft-par[1].width())/2); ~~ }
{IF: par[2]}
   {FOR: params_get().P}{DO}
      {IF: P.seek(params_get().P.SQL,,1)}
         {IF: ~par[15].find_key(#P.ref)}
            {SUBSTITUTE: tresc}
            {  par[15].REF:=#P.ref;
               par[15].add;
               ~~
            }
         {FI}
      {FI}
   {OD}
{ELSE}
   {IF: par[3]}
      {FOR: IS_OPIS}{INDEX: 'OPIS'}{SELECT: 'k'}{DO}
          {IF: ~par[15].find_key(#IS_OPIS.ref)}
             {SUBSTITUTE: tresc}
             {  par[15].REF:=#IS_OPIS.ref;
                par[15].add;
                ~~
               }
          {FI}
      {OD}
   {ELSE}
      {FOR: IS_OPIS}{INDEX: 'OPIS'}{DO}
         {SUBSTITUTE: tresc}
      {OD}
   {FI}
{FI}
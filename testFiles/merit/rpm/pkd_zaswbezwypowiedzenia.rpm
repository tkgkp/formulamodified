:!UTF-8
{TITLE:Zwolnienie bez wypowiedzenia}
{VERSION: PEP}
{PRINTER: dziedzina:='PKD';
   exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PKD_ZASWBWYP')='T',,,,'I','B',,,,,,1,
   PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='';
   VAR_DEL.delete('Text','cnt','__CALL','__PRAC','__xr_ins');
   VAR_DEL.delete('czy_szablon','czy_paperless','tytul','czy_pep','wyjdz','wyb_prac','edit');
   _par:=params_get();
   tytul:='Zwolnienie bez wypowiedzenia';
   czy_szablon:=exec('rep_set','rap_zew','PKD_ZASWBWYP',1)='T';
   czy_pep:=var_pres('_par')>100 & var_pres('PEP',_par)>0;
   wyjdz:=0;
   edit:=0;
   wyb_prac:=0;
   P.cntx_psh();
   H.cntx_psh();
   H_UM.cntx_psh();
   Text:=exec('uprawnieniawrap','pkd',
      'Przebiegu zatrudnienia'@,
      'PKD_EZK_PPZA',
      'PKD_EZK_RPZA'
   );
   {? +Text
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@ [Text])
   || _paperless:=czy_pep & var_pres('active',_par.PEP)>0 & _par.PEP.active;
      {? _paperless
      || czy_paperless:=0;
         {? ~czy_szablon & exec('rep_set','rap_zew','PKD_ZASWZMUM')<>'T' & _par.PEP.fml_after=""
         || czy_paperless:=1
         ?}
      || czy_paperless:=1
      ?};
      cnt:=0;
      H.index('HISTUM');
      H_UM.index('OD');
      _tab:=tab_tmp(1,
         'DUM','DATE','Data zawarcia umowy o pracę',
         'DWY','DATE','Data rozwiązania umowy o pracę',
         'PRZ','STRING[150]','Przyczyna i podstawa prawna wypowiedzenia',
         'SAD','STRING[120]','Sąd Pracy',
         'KOM','STRING[120]','Siedziba Komisji Pojednawczej'
      );
      _red:=_tab.mk_edit('Zwolnienie bez wypowiedzenia',,'zasbwypowiedz');
      _tab.win_esep(_red,'Parametry wydruku');
      _tab.win_efld(_red,,'DUM',,,11);
      _tab.win_efld(_red,,'DWY',,,11);
      _tab.win_efld(_red,,'PRZ',,,70,,,,,'z powodu...');
      _tab.win_efld(_red,,'SAD',,,70,,,,,'[...] odszkodowania do Sądu Rejonowego - Sądu Pracy w ...');
      _tab.win_esep(_red,'');
      _tab.win_efld(_red,,'KOM',,,70,,,,,'[...] przed Komisją Pojednawczą...');
      exec('ok_esc','#window',_tab,_red);
      _tab.efld_opt(_red,'mark=1',,'DUM');
      _tab.efld_opt(_red,'mark=1',,'DWY');
      _tab.efld_opt(_red,'mark=1',,'PRZ');
      _tab.efld_opt(_red,'mark=1',,'SAD');
      {? _paperless
      || {? _par.PEP.P
         || edit+=1;
            params_set(
               'P',exec('wybierz_silent','pracownik',_par.PEP.P).P,
               'paperless',_paperless,
               'par',_par,
               'CALL',null(),
               'tab',_tab,
               'red',_red
            )
         || _args:=exec('wybierz_args','pracownik');
            _args.DOMAIN:=dziedzina;
            _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
            _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
            _args.F_ZATR:=__PARSES.getVal('F_ZATR').KOD;
            _args.VIEW:=__PARSES.getVal('ZakresDanych');
            _args.SQL_FROM:=" JOIN OSOBA using(P.OSOBA, OSOBA.REFERENCE)"+
                            " JOIN USERS using(USERS.OSOBA, OSOBA.REFERENCE)"+
                            " JOIN USERSF using(USERSF.USERS, USERS.REFERENCE)";
            _args.SQL_WHERE:=''+"P.PORTAL='T' and USERSF.PORTAL2='T'";
            _args.WIELU:=~_paperless;
            params_set(
               'P',exec('wybierz_parses','pracownik',dziedzina,_args).P,
               'paperless',_paperless,
               'par',_par,
               'CALL',null(),
               'tab',_tab,
               'red',_red
            )
         ?};
         Text:={? ~params_get(1).P.first() || 'Nie wybrano pracownika' || '' ?}
      || params_set(
            'P',null(),
            'paperless',_paperless,
            'par',_par,
            'CALL',null(),
            'tab',_tab,
            'red',_red
         )
      ?};
      __xr_ins:=exec('new_ins','rap_zew','PKD_ZASWBWYP')
   ?}
}
{END:
   H_UM.cntx_pop();
   H.cntx_pop();
   P.cntx_pop();
   {? var_pres('__xr_ins')=type_of(null()) || exec('del_ins','rap_zew',&__xr_ins) ?};
   VAR_DEL.delete('Text','cnt','__CALL','__PRAC','__xr_ins');
   VAR_DEL.delete('dziedzina','czy_szablon','czy_paperless','tytul','czy_pep','wyjdz','wyb_prac','edit')
}
{COMMENT}OLD: zaswbwyp.rpm{COMMENT-}
{COMMENT}OLD: zaswbwyp.rpi{COMMENT-}
{EXIT: +Text}
{EXIT: ~__xr_ins}
{WHILE: (~params_get().paperless &
   (_wyb:=exec('wybierz','!pkd_zes_orza');
    wyb_prac+=_wyb;
    _wyb)
   )
   | (params_get().paperless & params_get().par.PEP.fml_after="" & ~wyjdz & P.seek(params_get().P.UID,))}
{DO}
{  H_UM.prefix(P.ref());
   H_UM.last();
   H.prefix(H_UM.ref());
   {? H.first() & H.PODPIS<>#0 || _data:=H.PODPIS || _data:=H_UM.OD ?};
   params_set(_par:=params_get());
   {? _par.paperless || wyjdz:=1 ?};
   _par.tab.win_edit(_par.red);
   _par.tab.DUM:=_data;
   _par.tab.DWY:={? P.DZ<>date(0,0,0)&P.DZ<date || P.DZ || date ?};
   _par.tab.PRZ:=exec('uspkod','personel',P);
   _stale:=exec('zasw_pob','profile');
   _par.tab.SAD:=_stale.sad;
   _par.tab.KOM:=_stale.komisja;
   _par.tab.hdr_edit();
   _par.tab.hdr_edit(' - %1 %2'[P.OSOBA().NAZWISKO,OSOBA.PIERWSZE]);
   {? czy_szablon | _par.tab.edit(
         "  _tab:=params_get().tab;
            {? (_chk:=__CHK.record(_tab,,'DUM','DWY','PRZ','SAD'))<>''
            || _chk
            |? _tab.DUM>_tab.DWY
            || __CHK.err_fld(_tab,'DWY',1,'Data nie może być wcześniejsza niż \"%1\" [%2].'
                  [MS.name(_tab,'DUM'),$_tab.DUM])
            || ''
            ?}
         ")
   || edit+=1;
      _stale.sad:=_par.tab.SAD;
      _stale.komisja:=_par.tab.KOM;
      exec('zasw_zap','profile',_stale);
      exec('ins_par','rap_zew',__xr_ins,cnt+=1,
         'H_UM_REF',$H_UM.ref(),
         'F_NAZWA',KST.NAZWA,
         'MIEJSCE',PAR_WYDR.MIEJSC,
         'DATA', date()$4,
         'F_ULICA',exec('ulica','stalesys'),
         'F_POCZTA',exec('poczta','stalesys'),
         'F_REGON',KST.REG,
         'F_PKD',KST.PKD,
         'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?}
      );
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'NAZWISKO',P.OSOBA().NAZWISKO,
         'IMIE',OSOBA.PIERWSZE,
         'DRUGIEIM',OSOBA.DRUGIE,
         'PLEC',OSOBA.PLEC,
         'DATAUM',{? _par.tab.DUM<>#0 || _par.tab.DUM$6 || '' ?},
         'DATAWYP',{? _par.tab.DWY<>#0 || _par.tab.DWY$6 || '' ?},
         'POWOD',_par.tab.PRZ,
         'SAD',_par.tab.SAD,
         'KOMISJA',_par.tab.KOM
      );
      exec('ins_par','rap_zew',__xr_ins,cnt,'CZY_ZAP',{? params_get().paperless || '1' || ' ' ?})
   ?};
   ~~
}{OD}
{EXIT: params_get().paperless & ~edit}
{EXIT: ~exec('ready','rap_zew',__xr_ins) & ~(params_get().paperless & ~czy_paperless)}
{EXIT: ~(wyb_prac & edit>0) & ~params_get().paperless}
{
   params_set(_par:=params_get());
   {? _par.paperless & _par.par.PEP.fml_after<>""
   || __CALL:=_par.par.PEP.CALL
   || __CALL:=proc_exec('bwyp@zasw',#__xr_ins);
      {? czy_pep || _par.par.PEP.CALL:=__CALL ?}
   ?};
~~ }
{IF: czy_szablon}
   {FOR: __CALL}{DO}
      {IF: H_UM.seek(__CALL.H_UM_REF,,1)}
         {
         H_UM.P().OSOBA();
         _paperless:=params_get().paperless;
         _exe:={? _paperless || 2 || 3 ?};
         _file:=exec('wypelnij','szablon','PKD_ZASWBWYP',,,_exe,,_paperless);
         {? _paperless & var_pres('_file')>0 & var_pres('_file')>0 & var_pres('INFO',_file)>0
         || exec('add_file_and_run','paperless_grp',
               P.ref(),_file.INFO,'Inne',tytul)
         ?};
         ~~}
      {FI}
   {OD}
   {EXIT}
{FI}
{IF: exec('rep_set','rap_zew','PKD_ZASWBWYP')='T'}
   {
   exec('use_tmp','rap_zew','PKD_ZASWBWYP');
   {? params_get().paperless
   || exec('add_file_and_run','paperless_grp',
         P.ref(),
         '@!Tmp\\macrotmp.doc',
         'Inne',
         tytul
      )
   ?};
   ~~}
   {EXIT}
{ELSE}
   {IF: params_get().paperless & params_get().par.PEP.fml_after=""}
      {
      params_get().par.PEP.P:=P.ref();
      params_get().par.PEP.fml_after:="
         _pep:=obj_new('active','fml_after','P','CALL');
         _pep.active:=1;
         _pep.fml_after:=\"1\";
         _pep.P:=_a;
         _pep.CALL:=_b;
         {? var_pres('_params')>100
         || exec('obj_ntab_set','#array',_params,'PEP',_pep)
         || _params:=obj_new('PEP');
            _params.PEP:=_pep
         ?};
         params_set(_params);
         _name:='@'+tmp_dir()+exec('sep','#file')+'pkd_zaswbezwypowiedzenia_%1'[$P.tm_stamp()]+'.pdf';
         rep_exec('pkd_zaswbezwypowiedzenia',,0,_name,1,,1);
         exec('add_file_and_run','paperless_grp',
         _pep.P,_name,'Inne','Zwolnienie bez wypowiedzenia')
      ";
      ~~}
      {EXIT}
   {ELSE}
      {INCLUDE: p_zaswbwyp.rpi}
   {FI}
{FI}
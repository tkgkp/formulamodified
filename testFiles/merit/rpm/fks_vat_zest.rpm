:!UTF-8
{TITLE:E.  Zestawienie zbiorcze}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  poziom_1:=poziom_2:=poziom_3:=''; il_poz:=0; fun1:=fun2:=fun3:=fun4:=fun5:=fun6:='';
  PRN:=obj_new(@.CLASS.PRINT,9); PRN.sl_frame();
  w:=obj_new(10); ile:=0; VAT7.win_edit('VAT_ZEST'); VAT7.hdr_edit();
  {? var_pres('hdr_vat')<0
  || hdr_vat:='ZESTAWIENIE ZBIORCZE'
  || hdr_vat:=~(-hdr_vat)
  ?};
  VAT7.hdr_edit(hdr_vat);
  VAT7.POZIOM1:=VAT7.POZIOM2:=VAT7.POZIOM3:='';
  VAT7.POZ1OD:=VAT7.POZ2OD:=VAT7.POZ3OD:='';
  VAT7.POZ1DO:=VAT7.POZ2DO:=VAT7.POZ3DO:='';
  VAT7.TYP_VAT:='';
  VAT7.POMIN:='T';
  TYM_VAT:=tab_tmp(3,'POZ1','STRING[10]','Poziom1',
                     'POZ2','STRING[10]','Poziom2',
                     'POZ3','STRING[10]','Poziom3',
                     'POZ4','STRING[60]','Poziom1a',
                     'POZ5','STRING[60]','Poziom2a',
                     'POZ6','STRING[60]','Poziom2b',
                     'BRUTTO','REAL','Brutto',
                     'NETTO','REAL','Netto',
                     'VAT','REAL','Vat'
                  );
 w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:=cosum:=w1:=w2:=poz_1:=poz_2:=tmp1:='';
 sum31:=sum32:=sum33:=sum21:=sum22:=sum23:=drukuj:=dru3poz:=0;
 sum3_b:=sum3_v:=sum3_n:=ssum1:=ssum2:=ssum3:=0;
 color:=''; prn1:=prn2:='PRN';
 lm:=ll:=lmt:=rsep:=0; vp:=1; rsep:=PAR_WYDR.RSEP; PAR_WYDR.RSEP:=1;
 {? var_press('WAL',VAT7)>0 || VAT7.WAL:=exec('kod_pln','slo_slu') ?}; wal_op:='';
 VAT7.KRAJ:=exec('kod_pl','slo_slu');
 kraj:=''; SLO.win_dict('ONE_SEL');
 OKRO_F.win_dict('SLO'); VAT7.WYSTROK:=VAT7.WYSTOKRO:=null; ROK_F.win_dict('WER')
 }
{END:
   &hdr_vat; &ile;
   obj_del(w); obj_del(PRN); &il_poz; &fun1; &fun2; &fun3; &fun4; &fun5; &fun6;
   _a:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2); &poziom_1; &poziom_2; &poziom_3;
   DOK.use('doku'+_a);  POZ.use('pozy'+_a);POW.use('pow_'+_a);
   obj_del(TYM_VAT); &sum31; &sum32; &sum33; &sum21; &sum22; &sum23;
   &drukuj; &cosum; &w1; &w2; &poz_1; &poz_2; &dru3poz;
   &sum3_b; &sum3_v; &sum3_n; &tmp1; &ssum1; &ssum2; &ssum3;
   &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; PAR_WYDR.RSEP:=rsep; &rsep;
   &wal_op; &kraj; VAR_DEL.delete('dokvpoch','okrVat','okrVatNr')
 }
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: vat_zest.rpm
Utworzony:   ???? - ??- ?? [?????] ???????????
Modyfikacje: 2001-11-08 [AMK] Artur Makos
==================================================
Zawartosc: Wydruk zestawienia zbiorczego VAT
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_expo()}{SUBSTITUTE: 'FOOT'}{FI}
{EXIT: ~VAT7.edit("{? VAT7.POZIOM1=''
                   || FUN.info('Proszę wybrać wartość\npoziomu pierwszego ze słownika.'); 'POZIOM1'
                   || ''
                   ?}")}
{ PAR_WYDR.TITLE:=hdr_vat+exec('naz_okr_vat','!fks_vat_zrej');
  okrVat:=exec('okr_vat_wydr','!fks_vat_zrej');
  dokvpoch:=exec('dokvpoch','!fks_vat_zrej');
  kraj:=VAT7.KRAJ().KOD+' '+VAT7.KRAJ().TR;
  {? var_press('WAL',VAT7)<=0
  || DVAT.cntx_psh();
     DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO);
     {? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?};
     DVAT.cntx_pop()
  || wal_op:=VAT7.WAL().KOD+' '+VAT7.WAL().TR
  ?};
~~}
{  {? VAT7.POZIOM2='' || il_poz:=1
   |? VAT7.POZIOM3='' || il_poz:=2
   || il_poz:=3 ?};
   {? VAT7.POZIOM1<>''
   || {? VAT7.POZ1OD=''  || exec('wyp_zakr','!fks_vat_zrej',1,0) ?};
      {? VAT7.POZ1DO='' || exec('wyp_zakr','!fks_vat_zrej',1,1) ?}
   ?};
   {? VAT7.POZIOM2<>''
   || {? VAT7.POZ2OD=''  || exec('wyp_zakr','!fks_vat_zrej',2,0) ?};
      {? VAT7.POZ2DO='' || exec('wyp_zakr','!fks_vat_zrej',2,1) ?}
   ?};
   {? VAT7.POZIOM3<>''
   || {? VAT7.POZ3OD=''  || exec('wyp_zakr','!fks_vat_zrej',3,0) ?};
      {? VAT7.POZ3DO='' || exec('wyp_zakr','!fks_vat_zrej',3,1) ?}
   ?};
   _nk:=VAT7.POZIOM1;
   {? _nk*' '|| _ns:=1;{!|? _nk:={? +((_nk*' '-1)+_nk)<10 || form(((_nk*' '-1)+_nk)+10*'$',10) || ((_nk*' '-1)+_nk) ?}
                                 +'#'+((_nk*' ')-_nk);_nk*' '
                       !}; {? _nk*'$' || {!|? _nk:=((_nk*'$'-1)+_nk)+' '+((_nk*'$')-_nk); _nk*'$'!} ?}
   ?};
   PRN.add("w[1]",10,(int((10-(+_nk))/2))*' '+_nk,,'#');
   fun1:=exec('kol','!fks_vat_zrej',1,0);
   _k:=0;
   {? -VAT7.POZIOM1='rejestry vat'
   || PRN.add("w[7]",60,'      Nazwa Rejestru VAT');_k:=1
   |? -VAT7.POZIOM1='klasy ewidencji'
   || PRN.add("w[7]",34,'     Nazwa Klasy ewidencji');_k:=1
   |? -VAT7.POZIOM1='grupy podatkowe'
   || PRN.add("w[7]",38,'             Grupy podatkowe');_k:=1
   |? -VAT7.POZIOM1='rodzaje ewidencji vat'
   || PRN.add("w[7]",39,'          Rodzaje ewidencji VAT');_k:=1
   ?};
   {? _k || fun4:=exec('kol','!fks_vat_zrej',1,1) ?};
   {? il_poz>=2
   || _nk:=VAT7.POZIOM2;
      {? _nk*' '|| _ns:=1;{!|? _nk:={? +((_nk*' '-1)+_nk)<10 || form(((_nk*' '-1)+_nk)+10*'$',10) || ((_nk*' '-1)+_nk) ?}
                                    +'#'+((_nk*' ')-_nk);_nk*' '
                          !}; {? _nk*'$' || {!|? _nk:=((_nk*'$'-1)+_nk)+' '+((_nk*'$')-_nk); _nk*'$'!} ?}
      ?};
      PRN.add("w[2]",10,(int((10-(+_nk))/2))*' '+_nk,,'#');
      fun2:=exec('kol','!fks_vat_zrej',2,0);
      _k:=0;
      {? -VAT7.POZIOM2='rejestry vat'
      || PRN.add("w[8]",60,'      Nazwa Rejestru VAT');_k:=1
      |? -VAT7.POZIOM2='klasy ewidencji'
      || PRN.add("w[8]",34,'     Nazwa Klasy ewidencji');_k:=1
      |? -VAT7.POZIOM2='grupy podatkowe'
      || PRN.add("w[8]",38,'             Grupy podatkowe');_k:=1
      |? -VAT7.POZIOM2='rodzaje ewidencji vat'
      || PRN.add("w[8]",39,'          Rodzaje ewidencji VAT');_k:=1
      ?};
      {? _k || fun5:=exec('kol','!fks_vat_zrej',2,1) ?}
   ?};
   {? il_poz=3
   || _nk:=VAT7.POZIOM3;
      {? _nk*' '|| _ns:=1;{!|? _nk:={? +((_nk*' '-1)+_nk)<10 || form(((_nk*' '-1)+_nk)+10*'$',10) || ((_nk*' '-1)+_nk) ?}
                                    +'#'+((_nk*' ')-_nk);_nk*' '
                          !}; {? _nk*'$' || {!|? _nk:=((_nk*'$'-1)+_nk)+' '+((_nk*'$')-_nk); _nk*'$'!} ?}
      ?};
      PRN.add("w[3]",10,(int((10-(+_nk))/2))*' '+_nk,,'#');
      fun3:=exec('kol','!fks_vat_zrej',3,0);
      _k:=0;
      {? -VAT7.POZIOM3='rejestry vat'
      || PRN.add("w[9]",60,'      Nazwa Rejestru VAT');_k:=1
      |? -VAT7.POZIOM3='klasy ewidencji'
      || PRN.add("w[9]",34,'     Nazwa Klasy ewidencji');_k:=1
      |? -VAT7.POZIOM3='grupy podatkowe'
      || PRN.add("w[9]",38,'             Grupy podatkowe');_k:=1
      |? -VAT7.POZIOM3='rodzaje ewidencji vat'
      || PRN.add("w[9]",39,'          Rodzaje ewidencji VAT');_k:=1
      ?};
      {? _k || fun6:=exec('kol','!fks_vat_zrej',3,1) ?}
   ?};
   PRN.add("w[4]",20,'   Kwota Brutto',1);
   PRN.add("w[5]",20,'    Kwota Netto',1);
   PRN.add("w[6]",19,'   Kwota Podatku',1);~~
}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}
{<3 'Parametry wydruku:'}
{ w[10]:={? SSTALE.AO().ZAM='T' || 'zamknięty' || 'otwarty' ?};~~}
{IF: dokvpoch<>''}{<6 dokvpoch}{FI}
{<6 'Okres: '+SSTALE.AO().NAZ+' '+OKRO_F.ROK().NAZ+' ('+w[10]+')'}{<42 'Jednostka księgowa: '+{? OPERATOR.DEPT || OPERATOR.DEPT().OD || 'wszystkie jednostki księgowe' ?}}{<95 exec('typ_vat_str','!fks_vat_zrej',0)}
{<6 'Kraj opodatkowania: '+kraj}{<42 {? wal_op<>'' || 'Waluta opodatkowania: '+wal_op || '' ?}}
{<6 'Poziomy sumowania:'}{<42 '1) '+VAT7.POZIOM1+' od: '+VAT7.POZ1OD+' do: '+VAT7.POZ1DO}
{IF: JpkV7.DATA<>date(0,0,0) & SSTALE.AO().POCZ>=exec('start','jpk_v')}
{<6 {? VAT7.POMIN='T' || 'Zestawienie zawiera pozycje pominięte w JPK_V7' || 'Zestawienie nie zawiera pozycji pominiętych w JPK_V7' ?}}
{FI}
{IF: il_poz>=2}{<42 '2) '+VAT7.POZIOM2+' od: '+VAT7.POZ2OD+' do: '+VAT7.POZ2DO}{FI}
{IF: il_poz=3}{<42 '3) '+VAT7.POZIOM3+' od: '+VAT7.POZ3OD+' do: '+VAT7.POZ3DO}{FI}
{FI}{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FONT: 'T8G'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{ vp:=1;~~}
{FOOTER-}
{COMMENT}------------------------------MACRO-WART------------------{COMMENT-}
{MACRO: 'WART'}
{ {? w1<>'' & w1=w[1]
  ||  w[1]:=w[7]:=''; {? il_poz=3 || {? w2<>'' & w2=w[2] ||  w[2]:=w[8]:='' || w2:=w[2] ?} ?}
  || w1:=w[1]; w2:=w[2]
  ?};~~}
{MACRO-}
{COMMENT}------------------------------MACRO-PETLA------------------{COMMENT-}
{MACRO: 'PETLA'}
{  _a:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2); DOK.use('doku'+_a); ~~}
   {FOR: 'PVAT'}
   {INDEX: 'DVAT_ST'}
   {PREFIX: DVAT.ref}
   {DO}
      {IF: exec('typ_okr_vat','!fks_vat_zrej',2)}
      { ile:=0;
       {!|? TYM_VAT.POZ1:=fun1();
            TYM_VAT.POZ2:=TYM_VAT.POZ3:=TYM_VAT.POZ4:=TYM_VAT.POZ5:=TYM_VAT.POZ6:='';
            {? -VAT7.POZIOM1='rejestry vat' | -VAT7.POZIOM1='klasy ewidencji'
               | -VAT7.POZIOM1='grupy podatkowe' | -VAT7.POZIOM1='rodzaje ewidencji vat'
            || TYM_VAT.POZ4:=fun4()
            ?};
            {? il_poz>=2
            || TYM_VAT.POZ2:=fun2();
               {?  -VAT7.POZIOM2='rejestry vat' | -VAT7.POZIOM2='klasy ewidencji'
                   | -VAT7.POZIOM2='grupy podatkowe' | -VAT7.POZIOM2='rodzaje ewidencji vat'
               || TYM_VAT.POZ5:=fun5()
               ?}
            ?};
            {? il_poz=3
            || TYM_VAT.POZ3:=fun3();
               {? -VAT7.POZIOM3='rejestry vat' | -VAT7.POZIOM3='klasy ewidencji'
                  | -VAT7.POZIOM3='grupy podatkowe' | -VAT7.POZIOM3='rodzaje ewidencji vat'
               || TYM_VAT.POZ6:=fun6()
               ?}
            ?};
            TYM_VAT.BRUTTO:=PVAT.SUM1;
            TYM_VAT.NETTO:=PVAT.SUM2;
            TYM_VAT.VAT:=PVAT.VAT;
            {? (il_poz=1 & (TYM_VAT.POZ1>=VAT7.POZ1OD & TYM_VAT.POZ1<=VAT7.POZ1DO) &
               (-VAT7.POZIOM1<>'rodzaje podatku vat' | (exec('typ_vat_str','!fks_vat_zrej',0)*(-TYM_VAT.POZ1))) |
               (il_poz=2 & (TYM_VAT.POZ1>=VAT7.POZ1OD & TYM_VAT.POZ1<=VAT7.POZ1DO)  &
               (TYM_VAT.POZ2>=VAT7.POZ2OD & TYM_VAT.POZ2<=VAT7.POZ2DO) &
               (-VAT7.POZIOM2<>'rodzaje podatku vat' | (exec('typ_vat_str','!fks_vat_zrej',0)*(-TYM_VAT.POZ2)))) |
               (il_poz=3 & (TYM_VAT.POZ1>=VAT7.POZ1OD & TYM_VAT.POZ1<=VAT7.POZ1DO)  &
               (TYM_VAT.POZ2>=VAT7.POZ2OD & TYM_VAT.POZ2<=VAT7.POZ2DO) &
               (TYM_VAT.POZ3>=VAT7.POZ3OD & TYM_VAT.POZ3<=VAT7.POZ3DO)
               & (-VAT7.POZIOM3<>'rodzaje podatku vat' | (exec('typ_vat_str','!fks_vat_zrej',0)*(-TYM_VAT.POZ3)))))
            || TYM_VAT.add
            ?}; ile=1
        !};
        ~~}
      {FI}
   {OD}
{MACRO-}
{COMMENT}-------WPROWADZANIE DANYCH DO TABELI TYMCZASOWEJ--------{COMMENT-}
{IF: OPERATOR.DEPT}
   {okrVatNr:=1;~~}
   {WHILE: okrVatNr<=obj_len(okrVat)}
   {DO}
      {IF: var_press('WAL',VAT7)<=0}
         {FOR: 'DVAT'}
         {INDEX: 'NR_O' }
         {PREFIX: OPERATOR.DEPT,VAT7.KRAJ,okrVat[okrVatNr]}
         {DO}
            {IF: DVAT.USUNIETY=0 & exec('wystrokokr','!fks_vat_zrej') & exec('typ_okr_vat','!fks_vat_zrej',1) & (VAT7.POMIN='T' | DVAT.POMIN=0)}{SUBSTITUTE: 'PETLA'}{FI}
         {OD}
      {ELSE}
         {FOR: 'DVAT'}
         {INDEX: 'NR_OW' }
         {PREFIX: OPERATOR.DEPT,VAT7.KRAJ,VAT7.WAL,okrVat[okrVatNr]}
         {DO}
            {IF: DVAT.USUNIETY=0 & exec('wystrokokr','!fks_vat_zrej') & exec('typ_okr_vat','!fks_vat_zrej',1) & (VAT7.POMIN='T' | DVAT.POMIN=0)}{SUBSTITUTE: 'PETLA'}{FI}
         {OD}
      {FI}
      {okrVatNr+=1;~~}
   {OD}
{ELSE}
   {okrVatNr:=1;~~}
   {WHILE: okrVatNr<=obj_len(okrVat)}
   {DO}
      {IF: var_press('WAL',VAT7)<=0}
         {FOR: 'DVAT'}
         {INDEX: 'NR' }
         {PREFIX: VAT7.KRAJ,okrVat[okrVatNr]}
         {DO}
            {IF: DVAT.USUNIETY=0 & exec('wystrokokr','!fks_vat_zrej') & exec('typ_okr_vat','!fks_vat_zrej',1) & (VAT7.POMIN='T' | DVAT.POMIN=0)}{SUBSTITUTE: 'PETLA'}{FI}
         {OD}
      {ELSE}
         {FOR: 'DVAT'}
         {INDEX: 'NRW'}
         {PREFIX: VAT7.KRAJ,VAT7.WAL,okrVat[okrVatNr]}
         {DO}
            {IF: DVAT.USUNIETY=0 & exec('wystrokokr','!fks_vat_zrej') & exec('typ_okr_vat','!fks_vat_zrej',1) & (VAT7.POMIN='T' | DVAT.POMIN=0)}{SUBSTITUTE: 'PETLA'}{FI}
         {OD}
      {FI}
      {okrVatNr+=1;~~}
   {OD}
{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: TYM_VAT}
{DO}
{  w[1]:=TYM_VAT.POZ1; w[2]:=TYM_VAT.POZ2; w[3]:=TYM_VAT.POZ3;
   w[7]:=TYM_VAT.POZ4; w[8]:=TYM_VAT.POZ5; w[9]:=TYM_VAT.POZ6;
   TYM_VAT.cntx_psh();
   {? TYM_VAT.next &
       {? il_poz=3
       || TYM_VAT.POZ3=w[3] & |TYM_VAT.POZ2=|w[2]
       |? il_poz=2
       || |TYM_VAT.POZ2=|w[2]
       || |TYM_VAT.POZ1=|w[1]
       ?} &
       |TYM_VAT.POZ1=|w[1]
    || drukuj:=0
    || drukuj:=1
    ?};
   TYM_VAT.cntx_pop();
~~}
{ sum31+=TYM_VAT.BRUTTO; sum32+=TYM_VAT.NETTO; sum33+=TYM_VAT.VAT;~~}
{ ssum1+=TYM_VAT.BRUTTO; ssum2+=TYM_VAT.NETTO; ssum3+=TYM_VAT.VAT;~~}
{IF: drukuj}
    {IF: il_poz>1}
{ {? TYM_VAT.POZ1<>tmp1 || tmp1:=TYM_VAT.POZ1; cosum:='' ?};~~}
      { {? cosum=({? il_poz=3 || TYM_VAT.POZ2 |? il_poz=2 || TYM_VAT.POZ1 ?})
         || sum21+=sum31; sum22+=sum32; sum23+=sum33
         || sum21:=sum31; sum22:=sum32; sum23:=sum33
         ?};~~}
    {FI}
{ w[4]:=form(sum31,,2,' ,'); w[5]:=form(sum32,,2,' ,'); w[6]:=form(sum33,,2,' ,');~~}
{SUBSTITUTE: 'WART'}
{ {? il_poz=3
   || dru3poz:=0;
      {? TYM_VAT.POZ1=poz_1
       || sum3_b+=sum31; sum3_v+=sum33; sum3_n+=sum32
       || sum3_b:=sum31; sum3_v:=sum33; sum3_n:=sum32
      ?};
     {? TYM_VAT.POZ1=poz_1 & TYM_VAT.POZ2<>poz_2
      ||  TYM_VAT.cntx_psh();
         {? (TYM_VAT.next & TYM_VAT.POZ1<>poz_1) | ~TYM_VAT.next || dru3poz:=1 ?};
         TYM_VAT.cntx_pop()
     |? TYM_VAT.POZ1<>poz_1
     ||  poz_1:=TYM_VAT.POZ1; poz_2:=TYM_VAT.POZ2
     ?}
   ?};~~
}
{FONT: 'T8G'}
{SUBSTITUTE: 'LINIA0'}
{ TYM_VAT.cntx_psh();
  _next:=TYM_VAT.next(); _poz_1:=TYM_VAT.POZ1; _poz_2:=TYM_VAT.POZ2;
  TYM_VAT.cntx_pop();
  _tmp_1:=TYM_VAT.POZ1;
   {?  il_poz<>1 & (cosum={? il_poz=3 || TYM_VAT.POZ2 |? il_poz=2 || TYM_VAT.POZ1 ?}) & cosum<>'' &
        (_next & ((cosum<>{? il_poz=3 || _poz_2 |? il_poz=2 || _poz_1 ?}) |
         ({? il_poz=3 & _tmp_1<>_poz_1 || 1 || 0 ?}))
        | ~_next)
   ||  drukuj:=1
   ||  drukuj:=0
   ?}; _tmp_2:=TYM_VAT.POZ1;
   cosum:={? il_poz=3 || TYM_VAT.POZ2 |? il_poz=2 || TYM_VAT.POZ1 ?};~~}
{ sum31:=sum32:=sum33:=0;~~}
{FI}
{IF: il_poz>1 & drukuj}
{ w[1]:=w[2]:=w[3]:=w[7]:=w[8]:=w[9]:='';
  {? il_poz=2 || w[1]:='SUMA:'+{? +TYM_VAT.POZ1<6 || '' || '#'?}+TYM_VAT.POZ1
              || w[2]:='SUMA:'+{? +TYM_VAT.POZ2<6 || '' || '#'?}+TYM_VAT.POZ2
  ?}; w[4]:=form(sum21,,2,' ,'); w[5]:=form(sum22,,2,' ,'); w[6]:=form(sum23,,2,' ,');~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'LINIA0'}
{FONT: 'T8G'}
{ sum21:=sum22:=sum23:=0;~~}
{FI}
{IF: dru3poz}
{ w[1]:='Suma:'+{? +TYM_VAT.POZ1<6 || '' || '#'?}+TYM_VAT.POZ1;
  w[2]:=w[3]:=w[7]:=w[8]:=w[9]:=''; dru3poz:=0;
  w[4]:=form(sum3_b,,2,' ,'); w[5]:=form(sum3_n,,2,' ,'); w[6]:=form(sum3_v,,2,' ,');~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'LINIA0'}
{FONT: 'T8G'}
{FI}
{OD}
{IF: ssum1 | ssum2 | ssum3}
{ w[1]:='     RAZEM '; w[2]:=w[3]:=w[7]:=w[8]:=w[9]:='';
   w[4]:=form(ssum1,,2,' ,'); w[5]:=form(ssum2,,2,' ,'); w[6]:=form(ssum3,,2,' ,');~~}
{IF: lineleft<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{ENTIRE}
{FONT: 'T8GB'}
{IF: ~rep_expo()}
{SUBSTITUTE: 'LINIAFS'}
{ELSE}
{TABLE-}
{FI}
{FONT: 'T8G'}
{ENTIRE-}
{ELSE}
{ FUN.info('Brak danych spełniających\nzałożone kryteria.');~~}{EXIT}
{FI}
{IF: ~rep_expo()}{<1>{PRN.width()} 'KONIEC WYDRUKU'}{FI}
:!UTF-8
{TITLE: Obroty na kontach}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,6); PRN.sl_frame();
   PRN1:=obj_new(@.CLASS.PRINT,6); PRN1.sl_frame();
   color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=0;
   head:=PAR_WYDR.HEAD; vp:=1; rsep:=PAR_WYDR.RSEP;
   PAR_WYDR.TITLE:='OBROTY NA KONTACH'@; w:=obj_new(6); ww:=obj_new(6);
   dalej:=0; {! _i:=1..6 |! w[_i]:=ww[_i]:='' !};
   sstr1:=sstr2:=sstr3:=sstr4:=suma1:=suma2:=suma3:=suma4:=0;
   aget:=var_pres('TT_AGET')>100;
   tab_ana:={? var_pres('TAB_WID')>0 || TAB_WID || {? dwiewalo || TAB_OBRW || TAB_OBR  ?} ?};
   tab_ana.cntx_psh()
}
{END: obj_del(PRN); obj_del(PRN1); &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &head;
      obj_del(w); obj_del(ww); &dalej; tab_ana.cntx_pop(); &tab_ana;
      &sstr1; &sstr2; &suma1; &suma2; &sstr3; &sstr4; &suma3; &suma4; &aget}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: obroty.rpm
Utworzony:    2005-01-07 [AMK]
==================================================
Zawartosc: Obroty na wybranych kontach analitycznych (dowolne poziomy)
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{ PRN.add("w[1]",35,'Konto'@);
  PRN.add("w[2]",60,'Opis');
  PRN.add("w[3]",17,k_1,1);
  PRN.add("w[4]",17,k_2,2);
  PRN.add("w[5]",17,k_3,1);
  PRN.add("w[6]",17,k_4,2);
  PRN1.add("ww[1]",35,'');
  PRN1.add("ww[2]",60,'');
  PRN1.add("ww[3]",17,'',1);
  PRN1.add("ww[4]",17,'',2);
  PRN1.add("ww[5]",17,'',1);
  PRN1.add("ww[6]",17,'',2);
  ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}{ prn1:=prn2:='PRN';~~}
{SUBSTITUTE: 'HEAD'}{FONT: 'T8G'}{SPACE: 'C'}{IF: page()=1}
{<3 'Parametry wydruku:'@}
{<6 {? ROZNE.UT_OKROD<>ROZNE.UT_OKRDO
    || 'Okres: %1 - %2 %3'@[ROZNE.UT_OKROD().NAZ,ROZNE.UT_OKRDO().NAZ,SSTALE.AR().NAZ]
    || 'Okres: %1  %2'@[ROZNE.UT_OKROD().NAZ,SSTALE.AR().NAZ]
    ?}}
{<6 {? OPERATOR.DEPT || 'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD] || 'Wszystkie jednostki księgowe'@ ?}}
{<6 'Waluta aktywna:  '+SSTALE.WAL().TR+' - '+SSTALE.WAL().KOD+
'       Waluta wydruku:  '+{? UNPAR.PSLO || UNPAR.PSLO().TR+' - '+SLO.KOD || SSTALE.WAL().TR+' - '+SLO.KOD ?}+{? ~dwiewalo & (UNPAR.P25<>1 | (UNPAR.PSLO<>null & UNPAR.PSLO<>SSTALE.WAL)) || '       Kurs: '+$UNPAR.P25 || '' ?}}
{IF: aget}{<6 'Tylko zaznaczone wiersze (%1)'@[$TT_AGET.size()]}{FI}{FI}
{FONT: 'T8B'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8B'}{SPACE: 'C'}{ prn1:=prn2:='PRN1'; ww[5]:=form(sstr3,,2,' ,'); ww[6]:=form(sstr4,,2,' ,');~~}
{ ww[1]:='SUMA STRONY (KONTA SYNTETYCZNE)'@; ww[2]:=''; ww[3]:=form(sstr1,,2,' ,'); ww[4]:=form(sstr2,,2,' ,');~~}
{SUBSTITUTE: 'LINIA0'}{ ww[5]:=form(suma3-sstr3,,2,' ,'); ww[6]:=form(suma4-sstr4,,2,' ,');~~}
{IF: page>1}{ ww[1]:='Z PRZENIESIENIA'@; ww[3]:=form(suma1-sstr1,,2,' ,'); ww[4]:=form(suma2-sstr2,,2,' ,');~~}
{SUBSTITUTE: 'LINIA0'}{FI}{ ww[5]:=form(suma3,,2,' ,'); ww[6]:=form(suma4,,2,' ,');~~}
{ ww[1]:='DO PRZENIESIENIA'@; ww[3]:=form(suma1,,2,' ,'); ww[4]:=form(suma2,,2,' ,');~~}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'LINIAF'}{ sstr1:=sstr2:=sstr3:=sstr4:=0; vp:=1; prn1:=prn2:='PRN';~~}
{FOOTER-}
{COMMENT}------------------------------LINIA-----------------------{COMMENT-}
{MACRO: 'LINIA1'}
{  ll:=lineleft(); ~~}
{IF:  PRN1.ini_line(); ll<(rsep+ PRN1.length()) & ll<>0}{PAGE}{FI}
{IF: vp}
 { PRN1.setcolor(color);~~}
 {REP: PRN1.fbar(lmt)}
 { PRN1.setcolor('255:255:255'); vp:=0; ~~}
{ELIF: rsep}
 {REP: PRN1.fbar(lmt)}
{FI}
{WHILE: PRN1.next()}
{DO}
   {REP: PRN1.fline(lmt)}
{OD}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'C'}
{ dalej:={? aget || TT_AGET.first() || tab_ana.first() ?}; ~~}
{WHILE: dalej}
{DO}
   {IF: ~aget | tab_ana.seek(TT_AGET.REF,tab_ana.name())}
   {IF: tab_ana.ANALIT=-1}{FONT: 'T8B'}{FI}
      { w[1]:=tab_ana.SYM_KON;
        w[2]:={? tab_ana.ANALIT=-1
              || ''
              || POZ.KOM:=null;
                 exec('opis','konto',tab_ana.SYM_KON); exec('ka_opis','konto');
                 {? KA.O11<>'' || KA.O11
                 |? KA.O10<>'' || KA.O10
                 |? KA.O9<>'' || KA.O9
                 |? KA.O8<>'' || KA.O8
                 |? KA.O7<>'' || KA.O7
                 |? KA.O6<>'' || KA.O6
                 |? KA.O5<>'' || KA.O5
                 |? KA.O4<>'' || KA.O4
                 |? KA.O3<>'' || KA.O3
                 |? KA.O2<>'' || KA.O2
                 || KS.NAZ
                 ?}
              ?};
        w[3]:=form(tab_ana.WINIEN,,2,' ,'); w[4]:=form(tab_ana.MA,,2,' ,');
        w[5]:=form(tab_ana.SWINIEN,,2,' ,'); w[6]:=form(tab_ana.SMA,,2,' ,');
        {? tab_ana.POZIOM=0 & tab_ana.ANALIT<>-1
        || sstr1+=tab_ana.WINIEN$2;  sstr2+=tab_ana.MA$2;
           suma1+=tab_ana.WINIEN$2;  suma2+=tab_ana.MA$2;
           sstr3+=tab_ana.SWINIEN$2; sstr4+=tab_ana.SMA$2;
           suma3+=tab_ana.SWINIEN$2; suma4+=tab_ana.SMA$2
        ?};
      ~~}
      {SUBSTITUTE: 'LINIA0'}{ vp:=0; {? lineleft=0 || vp:=1 ?};~~}
      {FONT: 'T8'}
      {IF: PAR_WYDR.RSEP=1 & lineleft()=1}{PAGE}{FI}
   {FI}
   { dalej:={? aget || TT_AGET.next() || tab_ana.next() ?}; ~~}
{OD}
{FOOTER}{FOOTER-}
{ENTIRE}
{FONT: 'T8B'}{SPACE: 'C'}{ ww[5]:=form(sstr3,,2,' ,'); ww[6]:=form(sstr4,,2,' ,');~~}
{ ww[1]:='SUMA STRONY (KONTA SYNTETYCZNE)'@; ww[2]:=''; ww[3]:=form(sstr1,,2,' ,'); ww[4]:=form(sstr2,,2,' ,');~~}
{SUBSTITUTE: 'LINIA1'}{ ww[5]:=form(suma3-sstr3,,2,' ,'); ww[6]:=form(suma4-sstr4,,2,' ,');~~}
{IF: page>1}{ ww[1]:='Z PRZENIESIENIA'@; ww[3]:=form(suma1-sstr1,,2,' ,'); ww[4]:=form(suma2-sstr2,,2,' ,');~~}
{SUBSTITUTE: 'LINIA1'}{FI}{ PRN.setcolor('255:255:255');~~}{ ww[5]:=form(suma3,,2,' ,'); ww[6]:=form(suma4,,2,' ,');~~}
{ ww[1]:='RAZEM'@; ww[3]:=form(suma1,,2,' ,'); ww[4]:=form(suma2,,2,' ,');~~}
{SUBSTITUTE: 'LINIA1'}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8'}{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}

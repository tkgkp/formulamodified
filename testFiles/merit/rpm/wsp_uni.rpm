:!UTF-8
{TITLE:  Wydruk uniwersalny dla zapytań SQL}
{PRINTER: exec('prt','param_wydr','h',{? BPMN.SYM_DOM='PPL' | BPMN.SYM_DOM='PKD' || KST || FINFO ?}),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
        {? var_pres('MISC',@)<=100 || MISC:=obj_new(@.CLASS.MISC) ?};
        {? var_pres('TAB_TMP',PARAM_W)>0
        || il:=($("PARAM_W.TAB_TMP"()+".fld_num()"))()+1
        || il:=($("t_tmp"()+".fld_num()"))()+1
        ?};
        PRN := obj_new(@.CLASS.PRINT, il);
        PRN.s_frame();
        PRX := obj_new(@.CLASS.PRINT, il);
        PRX.s_frame();
        sum_g  := obj_new(il);
        wart   := obj_new(il);
        pola   := obj_new(il);
        kolumny:= obj_new(il);
        pom    := obj_new(20);
        {! lp:=1..il |! sum_g[lp]:=0 !};
        {! lp:=1..il |! pola[lp]:=0 !};
        {! lp:=1..il |! kolumny[lp]:=0 !};
        lp:=0;v_wid:=0;tr_info:='Ze wględu na szerokość nie dodano następujących kolumn : \n'@;
        tab_ok:=0; v_kadd:=1;
        len:=0;naz:='';ind:='';
        typ_pola:=0;
        real:=0;
        line_r:=2;
        {? var_pres('t_sumk')>1
        || {? t_sumk<>''
           || kol_sum:=t_sumk;aut_sum:=0
           || kol_sum:='';aut_sum:=1
           ?}
        || kol_sum:='';aut_sum:=1
        ?};
        {? var_pres('TAB_SUM',PARAM_W)>0
        || kol_psum:=PARAM_W.TAB_SUM
        || kol_psum:=t_sum
        ?};
        kol1:=kol2:=0;
        kol_bie:=0;
        sum_p:= obj_new(il);
        {! _i:=1 .. obj_len(sum_p) |! sum_p[_i]:=obj_new(il) !};
        {! lp:=1 .. il |! {! len:= 1 .. il |! sum_p[lp][len]:=0 !} !};

        color:=prn1:=prn2:='';
        lm:=ll:=vp:=lmt:=rsep:=0;
        dokl:=0; i:=0;
        v_par:=0; v_vpar:='';
~~}
{END:
        obj_del(PRN);
        obj_del(PRX);
        obj_del(sum_g);
        obj_del(sum_p);
        obj_del(wart);
        obj_del(pola);
        obj_del(kolumny);
        obj_del(pom);
        &il;&lp;
        &tab_ok; &v_wid; &tr_info; &v_kadd;
        &len;&naz;&ind;
        &typ_pola;
        &line_r;
        &real;
        &kol_sum;&kol_psum;
        &aut_sum;
        &kol1;&kol2;
        &kol_bie;
        &color; &prn1; &prn2;
        &lm; &ll; &vp; &lmt; &rsep;
        &dokl; &i;
        &v_par; &v_vpar;
~~}
{COMMENT}
 created by [Mariusz Krumpholz] - Macrologic Katowice
 date 2002.01.09

 wydruk uniwersalny drukuje zawartosc tabeli tymczasowej
 parametry:

 PARAM_W.TAB_TMP lub t_tmp - akronim tabeli tymczasowej
 PARAM_W.TAB_POL lub t_pol - numery pol, ktore nie beda drukowane
                   np. '1;15;' - nie wydrukuja sie pola 1 i 15
 PARAM_W.TAB_TYT lub t_tyt - tytul zestawienia
 PARAM_W.TAB_SUM lub t_sum - numery kolumn do sum czesciowych np. kolumna z kodem kontrahenta
                   np. '1;'
 PARAM_W.TAB_SUM lub t_sumk - numery kolumn sumowania (brak - wszystkie pola typu real)
                   np. '1;'
{COMMENT-}
{
   {? var_pres('TAB_TYT',PARAM_W)>0
   || PAR_WYDR.TITLE:=PARAM_W.TAB_TYT
   || PAR_WYDR.TITLE:=t_tyt
   ?};
   prn1:='PRN'; prn2:='PRX'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------MACRA-----------------------{COMMENT-}
{COMMENT}------------------------------MACRO-LINIA2-----------------{COMMENT-}
{COMMENT}
Makro uzywane do drukowania rekordu z linia poprzedzajaca
dla wydruków z obiektem PRINTER o nazwie przekazywanej przez zmienną prn2.
{COMMENT-}
{MACRO: 'LINIA2'}
{  ll:=lineleft(); ~~}
{IF:  _f:=prn2+'.length()'; _v:=($(_f))();
      ll<(2+_v) & ll<>0}{PAGE}{FI}
{ENTIRE}
{IF: vp}
   { _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn2+'.fbar(lmt)'; ($(_f))()}
   { _f:=prn2+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: _f:=prn2+'.fbar(lmt)'; ($(_f))()}
   {ELIF: rsep}
      {REP: _f:=prn2+'.fbar(lmt)'; ($(_f))()}
   {FI}
{FI}
{ _f:=prn2+'.ini_line()'; ($(_f))()}
{WHILE: _f:=prn2+'.next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.fline(lmt)'; ($(_f))()}
{OD}
{IF: var_pres('fun')>=0 & fun<>''}
{  $fun(); ~~}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA03----------------{COMMENT-}
{COMMENT}
Makro uzywane do drukowania rekordu z linia poprzedzajaca
dla wydruków z obiektem PRINTER o nazwie przekazywanej przez zmienną prn2
i drugiego o nazwie przekazywanej przez zmienna prn1. Linia separatora
drukowana jest jako linia przejsciowa miedzy obiektem prn2 i prn1.
{COMMENT-}
{MACRO: 'LINIA03'}
{  ll:=lineleft(); ~~}
{IF:  _f:=prn1+'.length()'; _v:=($(_f))();
      ll<(2+_v) & ll<>0}{PAGE}{FI}
{ENTIRE}
{IF: vp}
   { _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn2+'.fjbar('+prn1+',lmt)'; ($(_f))()}
   { _f:=prn2+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
   { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: _f:=prn2+'.fjbar('+prn1+',lmt)'; ($(_f))()}
   {ELIF: rsep}
      {REP: _f:=prn2+'.fjbar('+prn1+',lmt)'; ($(_f))()}
   {FI}
{FI}
{ _f:=prn1+'.ini_line()'; ($(_f))()}
{WHILE: _f:=prn1+'.next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
{OD}
{IF: var_pres('fun')>=0 & fun<>''}
{  $fun(); ~~}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIAF2-----------------{COMMENT-}
{COMMENT}
Makro uzywane do drukowania stopki z linia domykajaca tabele
dla wydrukow z dwoma obiektami PRINTER.
{COMMENT-}
{MACRO: 'LINIAF2'}
{IF: vp}
   { _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
   { _f:=prn2+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
{ELSE}
   {REP: _f:=prn2+'.flbar(lmt)'; ($(_f))()}
{FI}
{ vp:=1; ~~}
{MACRO-}
{COMMENT}------------------------------MACRA - line-----------------------{COMMENT-}
{MACRO:'line'}
    {lp+=1;~~}
    {kol2:=kol_sum;
     {!|?
        kol1:=#(((kol2*';')-1)+kol2);
        {? type_of(kol1)=1
        || {? kol1<>0
           || {? var_pres('TAB_TMP',PARAM_W)>0
              || {? type_of(($("PARAM_W.TAB_TMP"()+"."+($("PARAM_W.TAB_TMP"()+".fld_acr(kol1-1)"))()))()) = 1
                 || ($("sum_g[kol1]+="+("PARAM_W.TAB_TMP")()+"[kol1-1]"))()
                 ?}
              || {? type_of(($("t_tmp"()+"."+($("t_tmp"()+".fld_acr(kol1-1)"))()))()) = 1
                 || ($("sum_g[kol1]+="+("t_tmp")()+"[kol1-1]"))()
                 ?}
              ?}
           ?}
        ?};
        kol2:=(kol2*';')-kol2;
        kol2<>''
     !};~~}
    {
     {? kol_psum<>''
     ||
     kol2:=kol_sum;
     {!|?
        kol1:=#(((kol2*';')-1)+kol2);
        {? type_of(kol1)=1
        || {? kol1<>0
           ||
              kol3:=kol_psum;
              {!|?
                 kol4:=#(((kol3*';')-1)+kol3);
                 {? type_of(kol4)=1
                 || {? kol4<>0
                    || {? var_pres('TAB_TMP',PARAM_W)>0
                       || {? type_of(($("PARAM_W.TAB_TMP"()+"."+($("PARAM_W.TAB_TMP"()+".fld_acr(kol1-1)"))()))()) = 1
                          || ($("sum_p[kol1][kol4]+="+("PARAM_W.TAB_TMP")()+"[kol1-1]"))()
                          ?}
                       || {? type_of(($("t_tmp"()+"."+($("t_tmp"()+".fld_acr(kol1-1)"))()))()) = 1
                          || ($("sum_p[kol1][kol4]+="+("t_tmp")()+"[kol1-1]"))()
                          ?}
                       ?}
                    ?}
                 ?};
                 kol3:=(kol3*';')-kol3;
                 kol3<>''
              !}
           ?}
        ?};
        kol2:=(kol2*';')-kol2;
        kol2<>''
     !}
     ?};pom[11]:=0;~~}
    {FONT: 'T8G'}{SPACE: 'C'}
    {IF: line_r=1 }{SUBSTITUTE: 'LINIA03'}{ELSE}{SUBSTITUTE: 'LINIA0'}{FI}
    {line_r:=0;~~}
{MACRO-}
{MACRO: 'line_p'}
{kol2:=';'+kol_psum;pom[7]:=il;~~}
{WHILE: pom[7]>0 }
{DO}
   {IF: kol2*(';'+$pom[7]+';')>0}
   {kol1:=pom[7];~~}
   {IF: {? var_pres('TAB_TMP',PARAM_W)>0 || pola[kol1]<>($("PARAM_W.TAB_TMP"()+"[kol1]"))()|| pola[kol1]<>($("t_tmp"()+"[kol1]"))() ?} }
       {wart[1]:='SUMA : '+pola[kol1];lp:=0;
       {! i:=2..il |! wart[i]:=sum_p[i][kol1] !};~~}
       {pom[11]:=0;~~}
       {ENTIRE}
       {IF: line_r }{<1PRX.bar();pom[11]+=1;~~}{ELSE}{<1PRN.jbar(PRX);pom[11]+=1;~~}{FI}
       {PRX.ini_line();~~}
       {WHILE: PRX.next()}
       {DO}
       {<1 PRX.line();pom[11]+=1;~~}
       {OD}
       {ENTIRE-}
       {{? lineleft < pom[11] || line_r:=3 ?};~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: line_r=1 }{SUBSTITUTE: 'LINIA2'}{ELSE}{SUBSTITUTE: 'LINIA02'}{FI}
       {line_r:=1;
        {! i:=2..il |! sum_p[i][kol1]:=0 !};
        {? var_pres('TAB_TMP',PARAM_W)>0
        || pola[kol1]:=($("PARAM_W.TAB_TMP"()+"[kol1]"))()
        || pola[kol1]:=($("t_tmp"()+"[kol1]"))()
        ?}
        ;~~}
   {FI}
   {FI}
   {pom[7]-=1;~~}
{OD}
{MACRO-}
{MACRO: 'line_po'}
{
kol2:=';'+kol_psum;pom[7]:=il;
rsep:=1;
~~}
{WHILE: pom[7]>0 }
{DO}
   {IF: kol2*(';'+$pom[7]+';')>0}
   {kol1:=pom[7];~~}
       {wart[1]:='SUMA : %1'@[pola[kol1]];lp:=0;
       {! i:=2..il |! wart[i]:=sum_p[i][kol1] !};~~}
       {pom[11]:=0;~~}
       {ENTIRE}
       {IF: line_r }{<1PRX.bar();pom[11]+=1;~~}{ELSE}{<1PRN.jbar(PRX);pom[11]+=1;~~}{FI}
       {PRX.ini_line();~~}
       {WHILE: PRX.next()}
       {DO}
       {<1 PRX.line();pom[11]+=1;~~}
       {OD}
       {ENTIRE-}
       {{? lineleft < pom[11] || line_r:=3 ?};~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: line_r=1 }{SUBSTITUTE: 'LINIA2'}{ELSE}{SUBSTITUTE: 'LINIA02'}{FI}
       {line_r:=1;
        {! i:=2..il |! sum_p[i][kol1]:=0 !};
        {? var_pres('TAB_TMP',PARAM_W)>0
        || pola[kol1]:=($("PARAM_W.TAB_TMP"()+"[kol1]"))()
        || pola[kol1]:=($("t_tmp"()+"[kol1]"))()
        ?};~~}
   {FI}
   {pom[7]-=1;~~}
{OD}
{MACRO-}
{MACRO:'sum_all'}
  {IF: kol_sum<>''}
      {
      wart[1]:='SUMA'@;
      {! i:=2..il |! wart[i]:=sum_g[i] !};
      pom[11]:=0;
      {? line_r
      || PRX.bar(); pom[11]+=1
      || PRN.jbar(PRX); pom[11]+=1
      ?};
      PRX.ini_line();
      {!
      |? PRX.next()
      |!
         PRX.line(); pom[11]+=1
      !};
      ~~}
      {IF: lineleft & lineleft<pom[11]}{PAGE}{FI}
      {FOOTER: 0}
      {<1}{ prn1:='PRX';~~}{SUBSTITUTE: 'LINIAF'}
      {<1}{LINE: 1}
      {FOOTER-}
      { prn1:='PRN'; prn2:='PRX';~~}
      {SUBSTITUTE: 'LINIA02'}
  {FI}
{MACRO-}
{MACRO: 'ind'}
    {
     kol2:=kol_psum;pom[2]:='ndx_tmp('''',';
     {!|?
        kol1:=#(((kol2*';')-1)+kol2);
        {? var_pres('TAB_TMP',PARAM_W)>0
        || pom[2]+=','''+($("PARAM_W.TAB_TMP"()+".fld_acr(kol1)"))()+''',,'
        || pom[2]+=','''+($("t_tmp"()+".fld_acr(kol1)"))()+''',,'
        ?};
        kol2:=(kol2*';')-kol2;
        kol2<>''
     !};
     pom[2]+=')';
     {? var_pres('TAB_TMP',PARAM_W)>0
     || pom[1]:=($(PARAM_W.TAB_TMP+"."+pom[2]))()
     || pom[1]:=($(t_tmp+"."+pom[2]))()
     ?};~~}
{MACRO-}
{COMMENT}------------------------------MACRO-COL--------------------{COMMENT-}
{HEADER}{HEADER-}
{ v_wid:=charleft();v_kadd:=1;
 {? var_pres('TAB_POL',PARAM_W)>0
 || {? PARAM_W.TAB_POL*';'<>1 || PARAM_W.TAB_POL:=';'+PARAM_W.TAB_POL ?}
 || {? t_pol*';'<>1 || t_pol:=';'+t_pol ?}
 ?};
 {? var_pres('TAB_TMP',PARAM_W)>0
 || tab_ok:=($("MISC.table("+("PARAM_W.TAB_TMP")()+")"))()
 || tab_ok:=($("MISC.table("+("t_tmp")()+")"))()
 ?};
 len:=5;
 PRN.add("form(lp,,,'99')",len,'Lp.',1);
 kolumny[1]:=len+4;
 v_wid-= kolumny[1];
 {! i:=1..il-1 |!
  {? var_pres('TAB_TMP',PARAM_W)>0 || naz:=($("PARAM_W.TAB_TMP"()+".fld_name(i)"))() || naz:=($("t_tmp"()+".fld_name(i)"))() ?};
  {? v_kadd
  ||
    {? {? var_pres('TAB_POL',PARAM_W)>0 || PARAM_W.TAB_POL*(';'+$i+';')=0 || t_pol*(';'+$i+';')=0?}
    ||
    {? var_pres('UNIMARIO',@)>100
    || UNIMARIO.ndx_drop();
       {? var_pres('__uniw_l')>0 || ind:=UNIMARIO.ndx_tmp('',,'ACR',,) || ind:=UNIMARIO.ndx_tmp('',,'NAZ',,) ?};
       UNIMARIO.index(ind);
       UNIMARIO.prefix(naz)
    ?};
    {? var_pres('TAB_POL',PARAM_W)>0
    || typ_pola:=type_of(($("PARAM_W.TAB_TMP"()+"."+($("PARAM_W.TAB_TMP"()+".fld_acr(i)"))()))())
    || typ_pola:=type_of(($("t_tmp"()+"."+($("t_tmp"()+".fld_acr(i)"))()))())
    ?};
    {? exec('hid_fld','#sql',($("PARAM_W.TAB_TMP"()+".fld_acr(i)"))())
    ||
       {? typ_pola = 1
       ||
          {? var_pres('TAB_POL',PARAM_W)>0
          || real:=($("MISC.fld_real('"+("PARAM_W.TAB_TMP")()+"','"+($("PARAM_W.TAB_TMP"()+".fld_acr(i)"))()+"')"))()
          || real:=($("MISC.fld_real('"+("t_tmp")()+"','"+($("t_tmp"()+".fld_acr(i)"))()+"')"))()
          ?};
          {? real=1
          || len:=15;
             {? ind<>'' || {? UNIMARIO.first() || len:=UNIMARIO.DLO;naz:=UNIMARIO.NAZ;dokl:=UNIMARIO.DOKL || dokl:=2 ?}|| dokl:=2 ?};
             {? len=0 || len:=15 ?};
          {? (v_wid-len)-3>0
          ||
             {? aut_sum || kol_sum+=$((i+1))+';' ?};
             {? var_pres('TAB_TMP',PARAM_W)>0
             || ($("PRN.add("+'"form('+"PARAM_W.TAB_TMP"()+"."+($("PARAM_W.TAB_TMP"()+".fld_acr(i)"))()+'\$'+$dokl+',,'+$dokl+','' ,'')"'+",len,naz,1)"))()
             || ($("PRN.add("+'"form('+"t_tmp"()+"."+($("t_tmp"()+".fld_acr(i)"))()+'\$'+$dokl+',,'+$dokl+','' ,'')"'+",len,naz,1)"))()
             ?}
          || tr_info+=naz+'\n';
             v_kadd:=0
          ?}
          || len:=11;
             {? ind<>'' || {? UNIMARIO.first() || len:=UNIMARIO.DLO;naz:=UNIMARIO.NAZ ?} ?};
             {? len=0 || len:=11 ?};
          {? (v_wid-len)-3>0
          ||
             {? var_pres('TAB_TMP',PARAM_W)>0
             || ($("PRN.add("+'"form('+"PARAM_W.TAB_TMP"()+"."+($("PARAM_W.TAB_TMP"()+".fld_acr(i)"))()+',,,''99'')"'+",len,naz,1)"))()
             || ($("PRN.add("+'"form('+"t_tmp"()+"."+($("t_tmp"()+".fld_acr(i)"))()+',,,''99'')"'+",len,naz,1)"))()
             ?}
          || tr_info+=naz+'\n';
              v_kadd:=0
          ?}
       ?}
       |? typ_pola = 2
       ||
          {? var_pres('TAB_TMP',PARAM_W)>0
          || len:=($("MISC.fld_len('"+("PARAM_W.TAB_TMP")()+"','"+($("PARAM_W.TAB_TMP"()+".fld_acr(i)"))()+"')"))()
          || len:=($("MISC.fld_len('"+("t_tmp")()+"','"+($("t_tmp"()+".fld_acr(i)"))()+"')"))()
          ?};
          {? ind<>'' || {? UNIMARIO.first() || {? UNIMARIO.DLO>0 || len:=UNIMARIO.DLO ?};naz:=UNIMARIO.NAZ ?} ?};
       {? (v_wid-len)-3>0
       ||
          {? var_pres('TAB_TMP',PARAM_W)>0
          || ($("PRN.add("+'"'+"PARAM_W.TAB_TMP"()+"."+($("PARAM_W.TAB_TMP"()+".fld_acr(i)"))()+'"'+",len,naz)"))()
          ||  ($("PRN.add("+'"'+"t_tmp"()+"."+($("t_tmp"()+".fld_acr(i)"))()+'"'+",len,naz)"))()
          ?}
       ||
          tr_info+=naz+'\n';
          v_kadd:=0
       ?}
       |? typ_pola = 4 | typ_pola = 5
       || len:=10;
          {? ind<>'' || {? UNIMARIO.first() || len:=UNIMARIO.DLO;naz:=UNIMARIO.NAZ ?} ?};
          {? len=0 || len:=10 ?};
          {? (v_wid-len)-3>0
          ||
          {? var_pres('TAB_TMP',PARAM_W)>0
          || ($("PRN.add("+'"form('+"PARAM_W.TAB_TMP"()+"."+($("PARAM_W.TAB_TMP"()+".fld_acr(i)"))()+')"'+",len,naz)"))()
          || ($("PRN.add("+'"form('+"t_tmp"()+"."+($("t_tmp"()+".fld_acr(i)"))()+')"'+",len,naz)"))()
          ?}
         || tr_info+=naz+'\n';
             v_kadd:=0
       ?}
    ?};
    kolumny[i+1]:=len+3;
    v_wid-=kolumny[i+1]
    ?}
    ?}
  ||

    {? {? var_pres('TAB_POL',PARAM_W)>0 || PARAM_W.TAB_POL*(';'+$i+';')=0 || t_pol*(';'+$i+';')=0 ?}
    ||
     tr_info+=naz+'\n'
    ?};
     v_kadd:=0
    ?}
 !}
;~~}
{
 {? kol_sum<>''
 || kol_bie:=0;
    kol2:=kol_sum;
    {!|?
        kol1:=#(((kol2*';')-1)+kol2);
        {? kol_bie=0
        || len:=0;{! i:=1..(kol1-1) |! len+=kolumny[i] !};
           PRX.add("wart[1]",(len-4),'')
        |? (kol_bie+1)<kol1
        || len:=0;{! i:=(kol_bie+1)..(kol1-1) |! len+=kolumny[i] !};
           PRX.add("''",(len-3),'')
        ?};
        {? var_pres('UNIMARIO',@)>100
        || UNIMARIO.ndx_drop();
           ind:=UNIMARIO.ndx_tmp('',,'NUM',,);
           UNIMARIO.index(ind);
           UNIMARIO.prefix(kol1)
        ?};
        {? ind<>'' ||
           {? UNIMARIO.first()
           || dokl:=UNIMARIO.DOKL
           || dokl:=2
           ?}
        || dokl:=2
        ?};
        ($("PRX.add("""+$"form(wart["+$kol1+"]\$"+$dokl+",,"+$dokl+", ' ,')"""+",(kolumny[kol1]-3),'',1)"))();
        kol2:=(kol2*';')-kol2;
        kol_bie:=kol1;
        kol2<>''
    !}
 ?};
{? +tr_info>58 || v_kadd:=FUN.ask(tr_info+'\n Chcesz wydrukować bez tych kolumn'@) ?};
 ~~}
{EXIT: ~v_kadd}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: page()=1 & var_pres('DD_OGR',@.UNI_M)>100 }
  {IF: UNI_M.DD_OGR.size>0 }
  {FOR: UNI_M.DD_OGR}
  {DO}
    {FIRST}
    {<{lmt+1}'PARAMETRY WYDRUKU:'@}
    {FIRST-}
    {<{lmt+1} UNI_M.DD_OGR[8]} {UNI_M.DD_OGR[9]} {UNI_M.DD_OGR[2]} {UNI_M.DD_OGR[3]}
  {OD}
  {FI}
{FI}
{FOR: TAB_SQLK}
{PREFIX: TAB_SQL.FILENAME}
{DO}
{IF: TAB_SQLK.RODZ<>'F' & TAB_SQLK.RODZ<>'Q'}
{IF: ~v_par}{<{lmt+1} 'PARAMETRY WYDRUKU:'@}{v_par:=1; ~~}{FI}
{  v_vpar:=($('PAR_SQL.PAR'+$TAB_SQLK.NR+TAB_SQLK.RODZ))();
   {? type_of(v_vpar)<>2 || v_vpar:=$v_vpar ?}; ~~}
{<{lmt+1} TAB_SQLK.NAZ+': '+v_vpar}
{FI}
{OD}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: line_r=1 | line_r=3}{SUBSTITUTE: 'LINIAF2'}{ELSE}{SUBSTITUTE: 'LINIAF'}{FI}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{IF: tab_ok}
  {lp:=0;~~}
  {IF: kol_psum=''}
    {IF: var_pres('TAB_TMP',PARAM_W)>0 }
       {FOR: PARAM_W.TAB_TMP}
       {DO}
          {SUBSTITUTE:'line'}
       {OD}
    {ELSE}
       {FOR: t_tmp}
       {DO}
          {SUBSTITUTE:'line'}
       {OD}
    {FI}
  {ELSE}
    {SUBSTITUTE: 'ind'}
    {IF: var_pres('TAB_TMP',PARAM_W)>0 }
            {FOR: PARAM_W.TAB_TMP}
            {INDEX: pom[1]}
            {DO}
               {FIRST}
               { kol2:=kol_psum;
                 {!|?
                    kol1:=#(((kol2*';')-1)+kol2);
                    pola[kol1]:=($("PARAM_W.TAB_TMP"()+"[kol1]"))();
                    kol2:=(kol2*';')-kol2;
                    kol2<>''
                 !};
                 pom[4]:="kol2:=kol_psum;pom[5]:=0;
                          {!|?
                             kol1:=#(((kol2*';')-1)+kol2);
                             {? pola[kol1]<>($(\"PARAM_W.TAB_TMP\"()+\"[kol1]\"))() || pom[5]:=1 ?};
                             kol2:=(kol2*';')-kol2;
                             kol2<>'' & pom[5]=0
                          !};
                          pom[5]"
                 ;~~}
               {FIRST-}
               {IF: pom[4]()}
                  {SUBSTITUTE:'line_p'}
               {FI}
               {SUBSTITUTE:'line'}
            {OD}
            {($("PARAM_W.TAB_TMP"()+".ndx_drop(pom[1])"))();~~}
    {ELSE}
            {FOR: t_tmp}
            {INDEX: pom[1]}
            {DO}
               {FIRST}
               { kol2:=kol_psum;
                 {!|?
                    kol1:=#(((kol2*';')-1)+kol2);
                    pola[kol1]:=($("t_tmp"()+"[kol1]"))();
                    kol2:=(kol2*';')-kol2;
                    kol2<>''
                 !};
                 pom[4]:="kol2:=kol_psum;pom[5]:=0;
                          {!|?
                             kol1:=#(((kol2*';')-1)+kol2);
                             {? pola[kol1]<>($(\"t_tmp\"()+\"[kol1]\"))() || pom[5]:=1 ?};
                             kol2:=(kol2*';')-kol2;
                             kol2<>'' & pom[5]=0
                          !};
                          pom[5]"
                 ;~~}
               {FIRST-}
               {IF: pom[4]()}
                  {SUBSTITUTE:'line_p'}
               {FI}
               {SUBSTITUTE:'line'}
            {OD}
            {($("t_tmp"()+".ndx_drop(pom[1])"))();~~}
    {FI}
  {FI}
  {SUBSTITUTE:'line_po'}
  {SUBSTITUTE:'sum_all'}
{ELSE}
  {FUN.info('Nie znaleziono tabeli w systemie'@);~~}
{FI}

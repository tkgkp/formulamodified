:!UTF-8
{TITLE:B. Dokument źródłowy z wyróżnikami}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   PRNF:=obj_new(@.CLASS.PRINT,4); PRNF.s_frame();
   PRNH:=obj_new(@.CLASS.PRINT,4); PRNH.s_frame();
   w:=obj_new(8);
   sumawn:=sumama:=sumawnb:=sumamab:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=NOMAXP:=0
}
{END:
   VAR_DEL.delete('PRN','PRNF','PRNH','w','NOMAXP');
   &sumawn; &sumama; &sumawnb; &sumamab;
   &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep; exec('end_msg','dok_fks_zest')
}
{IF: var_pres('TT_D_DOK')<100}
   {  POZ.index('DOK'); POZ.prefix(DOK.ref);
      {? ~POZ.first() || FUN.info('\nBrak pozycji w dokumencie!\n'@) ?}; ~~}
   {EXIT: ~POZ.first()}
{FI}
{  PRN.add("w[1]",3,'Lp.'@,1);
   PRN.add("w[2]",35,'Konto'@);
   PRN.add("form(w[3],,2,' ,')",16,'Wn'@,1);
   PRN.add("form(w[4],,2,' ,')",16,'Ma'@,1);
   PRN.add("w[5]",20,'Słownik'@);
   PRN.add("w[6]",8,'Kod'@);
   PRN.add("w[7]",20,'Nazwa wyróżnika'@);
   PRN.add("w[8]",16,'Kwota wyróżnika'@,1);
   PRNH.add("w[1]",3);
   PRNH.add("w[2]",35);
   PRNH.add("w[3]",35,'Kwoty w walucie narodowej'@,,,,,,,,2);
   PRNH.add("w[4]",73,'Wyróżniki'@,,,,,,,,4);
   PRNF.add("w[1]",41,,,,,,,,,2);
   PRNF.add("form(w[2],,2,' ,')",16,,1);
   PRNF.add("form(w[3],,2,' ,')",16,,1);
   ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}------------------------------MACRO-TABHEADX---------------{COMMENT-}
{MACRO: 'TABHEADX'}
{  PRNH.setcolor(color);
   PRNH.ini_head(); ~~}
{REP: PRNH.fhbar(lmt)}
{WHILE: PRNH.h_next()}
{DO}
   {REP: PRNH.h_fmline(lmt)}
{OD}
{REP: PRNH.fjbar(PRN,lmt)}
{  PRN.setcolor(color);
   PRN.ini_head(); ~~}
{WHILE: PRN.h_next()}
{DO}
   {REP: PRN.h_fmline(lmt)}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO WYDRUK-----------------{COMMENT-}
{MACRO: 'WYDRUK'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='DOKUMENT ŹRÓDŁOWY %1 Z WYRÓŻNIKAMI'@[DOK.NK]; ~~}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: rep_export()}
{'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}
{'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}
{IF: DOK.NRKSEF<>''}
{'Nr KSeF: '+DOK.NRKSEF}
{FI}
{'Data operacji: %1'@[$DOK.DOP]}
{'Data zapisu: %1'@[$DOK.DTW]}
{'Data wystawienia: %1'@[$DOK.DTO]}
{IF: DOK.KSEFCDAT<>date(0,0,0)}
{'Data KSeF: '+$DOK.KSEFCDAT}
{FI}
{IF: DOK.D4<>date(0,0,0)}
{'Data otrzymania: %1'@[$DOK.D4]}
{FI}
{IF: DOK.D3<>date(0,0,0)}
{'Termin płatności: %1'@[$DOK.D3]}
{FI}
{'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}
{'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}
{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}
{'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}
{'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}
{'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}
{FI}
{LINE: 1}
{ELSE}
{<2 'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{<2 'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}{<40 'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{<2 'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}{IF: DOK.NRKSEF<>''}{<66 'Nr KSeF: '+DOK.NRKSEF}{FI}
{<2 'Data operacji: %1'@[$DOK.DOP]}{<34 'Data zapisu: %1'@[$DOK.DTW]}{<66 'Data wystawienia: %1'@[$DOK.DTO]}{IF: DOK.KSEFCDAT<>date(0,0,0)}{<106 'Data KSeF: '+$DOK.KSEFCDAT}{FI}{IF: DOK.D4<>date(0,0,0)}{<130 'Data otrzymania: %1'@[$DOK.D4]}{FI}{IF: DOK.D3<>date(0,0,0)}{<162 'Termin płatności: %1'@[$DOK.D3]}{FI}
{<2 'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{<2 'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}{<2 'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}{<66 'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}{<98 'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}{FI}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEADX'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEADX'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; prn1:='PRN'; prn2:='PRNF'; sumawn:=sumama:=sumawnb:=sumamab:=0; ~~}
{FOR: 'POZ'}
{INDEX: 'DOK'}
{PREFIX: DOK.ref}
{DO}
{IF: DOK.DOK_REJ().M_ODD='N' | (DOK.DOK_REJ().M_ODD='T' & POZ.ODD_KS=DOK.ODD)}
  { w[1]:={? DOK.DOK_REJ().M_ODD='T' || POZ.POZ_MOD || POZ.POZ ?};
    w[2]:=POZ.KON;
    {? -POZ.STR='wn' || w[3]:=POZ.SUM; w[4]:=0 || w[4]:=POZ.SUM; w[3]:=0 ?};
    sumawn+=w[3]; sumama+=w[4];
    _v:=(BILANSP.ASYNT)+POZ.KON;
    KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR,_v);
    {? KS.first() & -(1+KS.TYP)='p'
    || {? -POZ.STR='wn' || sumawnb+=POZ.SUM || sumamab+=POZ.SUM ?}
    ?};
    KS.cntx_pop();
    POW.index('POZ'); POW.prefix(POZ.ref);
    {? POW.first()
    || w[5]:=POW.SLU().SLU().NAZ;
       w[6]:=POW.SLO().KOD;
       w[7]:=POW.SLO().TR;
       w[8]:=form(POW.KW,16,2,' ,')
    || w[5]:=w[6]:=w[7]:=w[8]:=''
    ?}; ~~ }
  {SUBSTITUTE: 'LINIA0'}
  {FOR: 'POW'}{INDEX: 'POZ'}{PREFIX: POZ.ref}
  {DO}
     { {? POW.next()
       || w[1]:=w[2]:=w[3]:=w[4]:='';
          w[5]:=POW.SLU().SLU().NAZ;
          w[6]:=POW.SLO().KOD;
          w[7]:=POW.SLO().TR;
          w[8]:=form(POW.KW,16,2,' ,')
       || {! _i:=1..8 |! w[_i]:='' !}
       ?}; ~~}
     {IF: w[5]<>'' & w[6]<>'' & w[7]<>'' & w[8]<>''}
        {  rsep:=0; ~~}
        {SUBSTITUTE: 'LINIA0'}
        {  rsep:=PAR_WYDR.RSEP; ~~}
     {FI}
   {OD}
{FI}
{OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<10}{PAGE}{FI}
{IF: ~rep_export()}
{FOOTER}{FOOTER-}
{ENTIRE}
{  rsep:=1;
   w[1]:='Razem:'; w[2]:=sumawn; w[3]:=sumama; w[4]:=''; ~~}
{IF: vp}
   { PRN.setcolor(color); ~~}
   {REP: PRN.fjbar(PRNF,lmt)}
   { PRN.setcolor('255:255:255'); vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: PRN.fjbar(PRNF,lmt)}
   {ELIF: rsep}
      {REP: PRN.fjbar(PRNF,lmt)}
   {FI}
{FI}
{ PRNF.ini_line()}
{WHILE: PRNF.next()}
{DO}
   {REP: PRNF.fline(lmt)}
{OD}
{  prn1:=prn2:='PRNF';
   w[1]:='w tym konta pozabilansowe:'@; w[2]:=sumawnb; w[3]:=sumamab;
   w[4]:=''; ~~}
{SUBSTITUTE: 'LINIAFS'}
{~~}
{LINE: 1}
{<{int(PRN.width()/4)} 'Sporządził dnia %1'@[$DOK.DR]}{<{int(PRN.width()/3*2)} 'Zaakceptował dnia %1'@[$DOK.DAKC]}
{LINE: 1}
{LINE: 1}
{<{int(PRN.width()/4)}>{int(PRN.width()/4)+26} '...........................'}{<{int(PRN.width()/3*2)}>{int((PRN.width()/3*2))+28} '...........................'}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}--------------------------END MACRO WYDRUK-----------------{COMMENT-}
{IF: var_pres('TT_D_DOK')>100}
   {NOMAXP:=1;~~}
   {FOR: 'TT_D_DOK'}
   {DO}
      {IF: DOK.seek(TT_D_DOK.REF,DOK.name()) & (POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first()) }
         {SUBSTITUTE: 'WYDRUK'}{l_druk+=1;~~}
         {IF: TT_D_DOK.next()}{HEADER}{HEADER-}{PAGE}{page(1);~~}{FI}
      {FI}
   {OD}
   {czy_druk:=1;~~}
{ELSE}
   {SUBSTITUTE: 'WYDRUK'}
{FI}

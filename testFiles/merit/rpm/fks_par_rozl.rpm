:!UTF-8
{TITLE:A.  Rozrachunki do rozliczenia}
{PRINTER: FINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN: w:=obj_new(20); petla:=0; konto:=''; zn_kon:=0; tmp_kon:=''; ok:=0;
        PRN:=obj_new(@.CLASS.PRINT,15); PRN.sl_frame();
        PRN1:=obj_new(@.CLASS.PRINT,15); PRN1.sl_frame();
        PRN.add("w[1]",52,' Symbol rozrachunku'@); PRN.add("w[2]",10,'Data otw.'@);
        PRN.add("w[3]",10,'T. płatn.'@);   PRN.add("w[4]",20,'    Saldo Winien'@,1);
        PRN.add("w[5]",20,'      Saldo Ma'@,1);
        PRN1.add("ww1",116,''); ww1:=''; sum_1:=sum_2:=0; ROZNE.PREF_ROZ:=mdc:='';
        ROZNE.ZER_SALD:=ROZNE.ZAKR_ROZ:=lm:=0; ROZNE.win_edit('ROZL_ROZ');
        ROZNE.KON_OD:=ROZNE.KON_DO:=null; ROZNE.KH_WYB:=null;
        czy_druk:=0; kh_kont:=0; sumkh1:=sumkh2:=0; ROZNE.SZUK:=1;
        KH.win_dict('WERHOME2');
        OP_PROJ.index('WN'); OP_PROJ.prefix()
}
{END:  VAR_DEL.delete('petla','w','PRN','PRN1','konto','ww1','mdc','lm','zn_kon','tmp_kon');
       VAR_DEL.delete('ok','sum_1','sum_2','czy_druk','kh_kont','sumkh1','sumkh2')
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_par_rozl.rpm
Utworzony:   2001-11-27 [AMK] Artur Makos
==================================================
Zawartosc:  Rozrachunki do rozliczenia
{COMMENT-}
{IF: ROZRACH.KONTR=null}
{EXIT: ~ROZNE.edit("
  {? ROZNE.ZAKR_ROZ=3 & ROZNE.KON_DO().SYM<ROZNE.KON_OD().SYM & ROZNE.KON_DO<>null
  || FUN.info('Podaj poprawnie zakres kont.'@); 'KON_OD'
  |? ROZNE.ZAKR_ROZ=2&ROZNE.KH_WYB=null
  || FUN.info('Wybierz kontrahenta.'@); 'KH_WYB'
  || 1
  ?}")}
{ELSE}
{ ROZNE.KH_WYB:=ROZRACH.KONTR; ROZNE.ZAKR_ROZ:=2;~~}
{FI}
{ {? ROZNE.ZAKR_ROZ=3
  || {? ROZNE.KON_OD().SYM='' || AN.index('SYM'); AN.prefix(); AN.first(); ROZNE.KON_OD:=AN.ref ?};
     {? ROZNE.KON_DO().SYM='' || AN.index('SYM'); AN.prefix(); AN.last(); ROZNE.KON_DO:=AN.ref ?}
  ?};~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{<1 PRN1.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------OPIS KONTA------------------{COMMENT-}
{MACRO: 'OPIS_KON'}
{  konto:=OP.AN;
   AN.cntx_psh(); AN.index('SYM'); AN.prefix(OP.AN);
   {? AN.first
    || exec('pw_opan','dok_fks_zest'); ww1:=OP.AN+'   '+KOMPEN.OPIS
    || ww1:=OP.AN
    ?};
   AN.cntx_pop();~~}
{ zn_kon:=1;~~}{IF:  lineleft()<2 & lineleft<>0}{ zn_kon:=0;~~}{PAGE}{ zn_kon:=1;~~}{FI}
{ENTIRE}
{PRN.jbar(PRN1)}
{SUBSTITUTE: 'LINIA1'}
{ENTIRE-}
{MACRO-}
{COMMENT}------------SPRAWDZANIE ROZRACHUNKOW------------------{COMMENT-}
{MACRO: 'SPR_ROZR'}
{ OP.cntx_psh(); ok:=0; _przerw:=0; _ref:=null;
   _s1:=F.SWn(OP.WN,OP.MA); _s2:=F.SMa(OP.WN,OP.MA); _sum1:=_s1; _sum2:=_s2;
   {? _s1>_s2 || _wn:=1 || _wn:=0 ?};
   {? OP.next
   || {? OP.AN=tmp_kon
      || {! |?
            {? OP.AN<>tmp_kon || _przerw:=1 ?};
            {? ~_przerw || _sum1+=F.SWn(OP.WN,OP.MA); _sum2+=F.SMa(OP.WN,OP.MA) ?};
            {? ~_przerw &
               ((_wn & F.SWn(OP.WN,OP.MA)<F.SMa(OP.WN,OP.MA)) |
               (~_wn & F.SWn(OP.WN,OP.MA)>F.SMa(OP.WN,OP.MA)))
            || {? ~ROZNE.ZER_SALD || ok:=1 ?}
            ?};
            {? ~ROZNE.ZER_SALD || ~ok || 1 ?} & ~_przerw & OP.next()
         !};
         {? _przerw & _sum1$2<>_sum2$2 || OP.prev(); _ref:=OP.ref() ?};
         {? ROZNE.ZER_SALD &  _sum1$2=_sum2$2 || ok:=1 ?}
      || OP.prev(); _ref:=OP.ref
      ?}
   ?};
   OP.cntx_pop();
   {? _ref<>null || OP.seek(_ref) ?};~~}
{MACRO-}
{COMMENT}------------SUMOWANIE ROZRACHUNKOW------------------{COMMENT-}
{MACRO: 'SUM_ROZR'}
{ OP.cntx_psh();~~}
{IF: _a:=OP.next; (_a & OP.AN<>tmp_kon) | ~_a}
{ w[2]:=w[3]:=''; w[1]:='Suma  '+tmp_kon; w[4]:=form(sum_1,,2,' ,'); w[5]:=form(sum_2,,2,' ,');~~}
{IF:  lineleft()<2 & lineleft<>0}{PAGE}{FI}
{ENTIRE}
{IF: zn_kon}{PRN1.jbar(PRN)}{ zn_kon:=0;~~}{ELSE}{PRN.bar()}{FI}{FONT: 'R'}
{SUBSTITUTE: 'LINIA'}{ kh_kont+=1; sumkh1+=sum_1; sumkh2+=sum_2; ~~}
{ENTIRE-}{FONT: 'Q'}
{IF: ROZNE.ZER_SALD<>1}
{ENTIRE}
{ w[1]:='Saldo'; w[4]:=form(F.SWn(sum_1$2,sum_2$2),,2,' ,'); w[5]:=form(F.SMa(sum_1$2,sum_2$2),,2,' ,');~~}
{PRN.bar()}{FONT: 'R'}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}{FONT: 'Q'}{FI}
{FI}
{ OP.cntx_pop();~~}
{MACRO-}
{COMMENT}------------------------------SRODEK-----------------------------{COMMENT-}
{MACRO: 'SRODEK'}
{IF: ~OP_PROJ.find_key(OP.ref())}
{IF: tmp_kon<>OP.AN}{ sum_1:=sum_2:=0; tmp_kon:=OP.AN;~~}{SUBSTITUTE: 'SPR_ROZR'}{FI}
{IF: tmp_kon=OP.AN & ok}
{IF: konto<>OP.AN}{SUBSTITUTE: 'OPIS_KON'}{FI}
{ w[1]:=' '+OP.SYM; w[2]:=OP.DO$3; w[3]:=OP.TZ$3;
   _a:=F.SWn(OP.WN,OP.MA);  _b:=F.SMa(OP.WN,OP.MA);
   w[4]:=form(_a,,2,' ,'); w[5]:=form(_b,,2,' ,'); sum_1+=_a; sum_2+=_b;~~}
{IF:  lineleft()<2 & lineleft<>0}{PAGE}{FI}
{ENTIRE}
{IF: zn_kon}{PRN1.jbar(PRN)}{ zn_kon:=0;~~}{ELSE}{PRN.bar()}{FI}{ czy_druk:=1;~~}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{SUBSTITUTE: 'SUM_ROZR'}
{FI}
{FI}
{ petla:=OP.next();~~}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ROZRACHUNKI DO ROZLICZENIA';~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Rok obrotowy: %1   Waluta: %2  %3'@[SSTALE.AR().NAZ,SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<5 {? OPERATOR.DEPT
    || 'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
    || 'Jednostka księgowa: wszystkie jednostki księgowe'@
    ?}
}
{<5
    {? ROZNE.ZAKR_ROZ=0
    || 'Zakres rozrachunków: wszystkie'@
    |? ROZNE.ZAKR_ROZ=1
    ||'Zakres rozrachunków: dla kont o początkowych znakach "%1"'@[ROZNE.PREF_ROZ]
    |? ROZNE.ZAKR_ROZ=2
    ||'Zakres rozrachunków: dla kontrahenta - %1'@[(50+ROZNE.KH_WYB().NAZ)]
    ||'Zakres rozrachunków: przedział kont od: %1 do: %2'@[ROZNE.KON_OD().SYM,ROZNE.KON_DO().SYM]
    ?}
}
{<5 {? ROZNE.ZER_SALD=1
    ||'Wyłącznie konta z zerowym saldem: tak'@
    ||'Wyłącznie konta z zerowym saldem: nie'@
    ?}
}
{FI}{FONT: 'R'}{SPACE: 'C'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{IF: zn_kon}{PRN1.lbar()}{ELSE}{PRN.lbar()}{FI}
{ zn_kon:=0;~~}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}-----------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ {? ROZNE.ZAKR_ROZ=0 | ROZNE.ZAKR_ROZ=1
    || {? OPERATOR.DEPT
       || OP.index('STAN_O'); OP.prefix(SSTALE.WAL,OPERATOR.DEPT,'N',ROZNE.PREF_ROZ)
       || OP.index('STAN'); OP.prefix(SSTALE.WAL,'N',ROZNE.PREF_ROZ)
       ?}
    |?ROZNE.ZAKR_ROZ=2
    || {? OPERATOR.DEPT
       || OP.index('KH');
          OP.prefix(SSTALE.WAL,ROZNE.KH_WYB,'N',OPERATOR.DEPT)
       || OP.index('KH');
          OP.prefix(SSTALE.WAL,ROZNE.KH_WYB,'N')
       ?}
    ?};
    petla:=OP.first(); ~~
}
{IF: ROZNE.ZAKR_ROZ<>3}
{WHILE: petla}
{DO}
{SUBSTITUTE: 'SRODEK'}
{OD}
{ELIF: OPERATOR.DEPT}
{FOR: OP}
{INDEX: 'STAN_O'}
{FROM: SSTALE.WAL,OPERATOR.DEPT,'N',ROZNE.KON_OD().SYM}
{TO: SSTALE.WAL,OPERATOR.DEPT,'N',ROZNE.KON_DO().SYM}
{DO}
{SUBSTITUTE: 'SRODEK'}
{OD}
{ELSE}
{FOR: OP}
{INDEX: 'STAN'}
{FROM: SSTALE.WAL,'N',ROZNE.KON_OD().SYM}
{TO: SSTALE.WAL,'N',ROZNE.KON_DO().SYM}
{DO}
{SUBSTITUTE: 'SRODEK'}
{OD}
{FI}
{IF: ~czy_druk}
{ FUN.info('Brak danych spełniających założone kryteria'@);~~}{EXIT}{FI}
{COMMENT}----------------------OSTATNI FOOTER-----------------------{COMMENT-}
{FOOTER}{FOOTER-}
{ENTIRE}
{IF: ROZNE.ZAKR_ROZ=2 & kh_kont>1}
{ w[2]:=w[3]:=''; w[1]:='      RAZEM'; w[4]:=form(sumkh1,,2,' ,'); w[5]:=form(sumkh2,,2,' ,');~~}
{IF: zn_kon}{PRN1.jbar(PRN)}{ zn_kon:=0;~~}{ELSE}{PRN.bar()}{FI}{FONT: 'R'}
{SUBSTITUTE: 'LINIA'}{FONT: 'Q'}
{FI}
{IF: zn_kon}{PRN1.lbar()}{ELSE}{PRN.lbar()}{FI}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}

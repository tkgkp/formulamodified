:!UTF-8
{TITLE: Wszystkie składniki zestawienia}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'m','c',,,,,,1,5,5,5,5,0,0,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','test','fp');
   PAR_WYDR.TITLE:='Wszystkie składniki zestawienia'@;

   {? rep_export()
   || _ref:=null();
      test:=0;
      
      R.cntx_psh();
      R.index('RUBLP');
      R.prefix();

      PRN:=obj_new(@.CLASS.PRINT,R.size()+2);
      PRN.add(
         "  ZB_WART.index('SKLADNIK');
            ZB_WART.prefix(ZB_DEF.ref());
            test:=0;
            (1+ZB_DEF.WARTOSC=^65533)-ZB_DEF.WARTOSC
         ",
         55,'Kryterium zestawienia'@
      );

      ZB_DEF.cntx_psh();
      ZB_DEF.index('OPIS');
      ZB_DEF.prefix(ZB_OPIS.ref(),'�{RAZEM}',);
      {? ZB_DEF.first() || _ref:=ZB_DEF.ref() ?};
      ZB_DEF.cntx_pop();

      ZB_WART.cntx_psh();
      ZB_WART.index('SKLADNIK');
      ZB_WART.prefix(_ref);
      
      _loop:=R.first();
      {!
      |? _loop
      |! {? ZB_WART.find_key(R.RN) & R.KDZ='T'
         || PRN.add($('_a:=0;'+
               '{? ZB_WART.find_key('+$R.RN+') ||'+
               ' {? _a:=ZB_WART.KWOTA || test+={? rep_export() || ~(ZB_DEF.WARTOSC*\'RAZEM\') || 1 ?} ?}'+
               '?}; {? _a || _a || \'0,00\' ?}'),
            12,R.RT,1,,,,,'12,2,\'9,\'')
         ?};
         _loop:=R.next()
      !};
      ZB_WART.cntx_pop();
      R.cntx_pop();
      PRN.s_frame()
   ?};

   exec('otworz_zb_def','zbiorowk',ZB_OPIS.LISTA);
   exec('otworz_zb_wart','zbiorowk',ZB_OPIS.LISTA);

   {? rep_export() || lmt:=rsep:=0; prn1:='PRN' || lmt:=3 ?};
   vp:=fp:=1;
   ~~
}
{END:
  VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','test','fp')
}
{COMMENT}OLD: zbdr_wsz.rpm{COMMENT-}
{EXIT: ~exec('no_limit','schemat','PPL')}
{IF: rep_export()}
   {INCLUDE: wsp_pw.rpi}
{FI}
{HEADER}
{IF: ~rep_export()}
{<3 KST.NAZWA}{>200 (KST.MIASTO+', dn. '+(date$2)+', str. '+$page)}
{<3>200 'Zestawienie zbiorcze'@}
{FI}
{IF: rep_export()}
   {SUBSTITUTE: 'HEAD'}
{FI}
{IF: rep_export()}
   {FONT: 'T8G'}
   {SPACE: 'C'}
      {LINE}
   {FI}
{IF: fp}
{IF: rep_export()}
{'Parametry sprawozdania'@}
{ELSE}
   {<3 'Parametry sprawozdania'@}
{FI}
{     _max:=charleft()-2*lmt;
      _lp:='Listy płac:'@;
      fp:='';
      ZB_LISTY.cntx_psh();
      ZB_LISTY.index('SYMBOL');
      ZB_LISTY.prefix(ZB_OPIS.ref());
      {? ZB_LISTY.first()
      || {!
         |? _lp+=' '+ZB_LISTY.SYMBOL;
            ZB_LISTY.next()
         !}
      || _lp+=' '+VAR.NAZWALIS
      ?};
      ZB_LISTY.cntx_pop();
      STR.split(_lp);
      {!
      |? (_linia:=STR.line(_max))<>''
      |! fp+='{<{lmt}}'+_linia;
         {? STR.next()
         || fp+='{LINE}'
         ?}
      !};
      ~~
   }
   {REP: fp}
   { fp:=''; ~~ }
{FOR: ZB_KLUCZ}{INDEX: 'NUMER'}{PREFIX: ZB_OPIS.TYP}
   {DO}{
      _zl:=ZB_KLUCZ.NAZWA_ZL;
      fp+=', '+ZB_KLUCZ.NAZWA+
          {? +_zl || ' -> '+_zl || '' ?};
      ~~
   }{OD}
{IF: ~rep_export()}
{<3 'Nazwa zestawienia: '@}{ZB_OPIS.TYP().NAZWA}
{<3 'Klucz zestawienia: '@}{2-fp}
{ELSE}
{'Nazwa zestawienia: '@}{ZB_OPIS.TYP().NAZWA}
{'Klucz zestawienia: '@}{2-fp}
{FI}
   { fp:=0; ~~ }
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{IF: rep_export()}
{  PRN.ini_head();
   PRN.setcolor(color);
   ~~
}
{REP: PRN.fhbar(lmt)}
{WHILE: PRN.h_next()}{DO}
{REP: PRN.h_fline(lmt)}
{OD}
{REP: PRN.fbar(lmt)}
{ PRN.setcolor('255:255:255'); ~~ }
{FI}
{HEADER-}
{IF: rep_export()}
{FOOTER}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: PRN.flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: ZB_DEF}{INDEX: 'OPIS'}{PREFIX: ZB_OPIS.ref()}{DO}
   {IF: PRN.ini_line(); test}
      {ENTIRE}
      {WHILE: PRN.next()}{DO}
         {REP: PRN.fline(lmt)}
      {OD}
      {ENTIRE-}
   {FI}
{OD}
{ELSE}
{FOR: ZB_DEF}{INDEX: 'OPIS'}{PREFIX: ZB_OPIS.ref()}{DO}
{ ZB_DRUK.Czytaj(); ~~ }
{ENTIRE}
{IF: 1+ZB_DEF.WARTOSC<>^65533}
{<3 ZB_DEF.WARTOSC}
{ELSE}
{<3 (1-ZB_DEF.WARTOSC)}
{ ZB_DRUK.LiniaSep() }
{FI}
{WHILE: ZB_DRUK.Wiersz}{DO}
{ ZB_DRUK.Linia() }
{OD}
{ENTIRE-}
{LINE}
{OD}
{FI}
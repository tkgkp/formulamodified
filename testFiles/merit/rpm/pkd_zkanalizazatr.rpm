:!UTF-8
{TITLE:Analiza zatrudnienia na dzień}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','lp','osoby','etaty','prn1','lmt','rsep','vp','dt');
   PAR_WYDR.TITLE:='Analiza zatrudnienia na dzień'@;

   dt:=date();
   exec('create','zk_uni');
   par:=obj_new(6);
   par[6]:=rep_export();
   _kolumny:=
      {? par[6]
      || {?  var_pres('__ZK_UNI')>100
         || __ZK_UNI[2].size()
         || 0
         ?}
      || 2
      ?}+2;
   par[1]:=obj_new(@.CLASS.PRINT,_kolumny);
   par[1].s_frame();
   lp:=osoby:=etaty:=par[4]:=par[5]:=lmt:=0;
   rsep:=vp:=par[3]:=1;
   prn1:='par[1]'
}
{END:
   exec('kill','zk_uni');
   VAR_DEL.delete('par','lp','osoby','etaty','prn1','lmt','rsep','vp','dt')
}
{EXIT: var_pres('__ZK_UNI')<100}
{  PAR_WYDR.TITLE+='\n('+__ZK_UNI[1].NAZWA+')';

   {? par[6]
   || {! _ii:=1 .. __ZK_UNI[2].size()
      |! __ZK_UNI[2].prefix(__ZK_UNI[1].ID);
         _name:={? __ZK_UNI[2].find_key(_ii) || __ZK_UNI[2].NAME || '' ?};
         _acr:='G'+$_ii;
         _typ:=var_pres(_acr,__ZK_UNI[5]);
         _fml:='form(__ZK_UNI[5].'+_acr;
         _fml+={? _typ=20 || ',,2,\'9,1\''
               |? _typ=21 || ',1'
               || ''
               ?}+')';

         par[1].add($_fml,1,_name,_typ<23)
      !}
   || _fml:='{? par[5]=1 || grp_fld |? par[5]=2 || \'Wszystkie zapisy\' || ';
      {! _ii:=1..__ZK_UNI[5].fld_num()
      |? 1+__ZK_UNI[5].fld_acr(_ii)='G'
      |! _fml+='form(__ZK_UNI[5]['+$_ii+'])+\'; \'+'
      !};
      _fml:=(_fml-6)+' ?}';

      _fld:='';
      {? __ZK_UNI[2].first()
      || {! |? _fld+=' '+__ZK_UNI[2].NAME+';'; __ZK_UNI[2].next() !}
      ?};

      par[1].add("{? par[5]=0 || lp+=1|? par[5]=2 || 'Suma' || '' ?}",4,'Lp.'@,1);
      par[1].add($_fml,charleft()-32,_fld-1)
   ?};


   par[1].add("{? par[5] || sum('osoby') || __ZK_UNI[5].OSOBY ?}",6,'Liczba pracowników'@,1);
   par[1].add("{? par[5] || sum('etaty') || __ZK_UNI[5].ETATY ?}",9,'Suma etatów'@,1,,,,,',3,\'9,\'');

   par[2]:=
      'Pracownicy'@+
      {? __ZK_UNI[1].POM_BEZP='T' || ' na urlopach bezpłatnych,'@ || '' ?}+
      {? __ZK_UNI[1].POM_WYCH='T' || ' na urlopach wychowawczych,'@ || '' ?}+
      {? __ZK_UNI[1].POM_WOJS='T' || ' odbywający służbę wojskową,'@ || '' ?}-1+
      ' zostali pominięci.'; ~~
}
{COMMENT}OLD: zk_uni2.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[3]}
   { par[3]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Data analizy:'@} {dt$1}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8B'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~par[6]}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{FOR: __ZK_UNI[5]}{DO}
   { par[5]:=0; ~~ }
   {SUBSTITUTE: 'LINIA0'}
   {ADD: osoby; #; __ZK_UNI[5].OSOBY}
   {ADD: etaty; #; __ZK_UNI[5].ETATY}
   {TOTAL: __ZK_UNI[5].G1}
   {IF: ~par[6]}
   {IF: __ZK_UNI[1].SUM='T'}
      { par[5]:=1; ~~ }
      {SUBSTITUTE: 'LINIA0'}
   {FI}
   {FI}
   {TOTAL}
      { par[5]:=2; ~~ }
      {IF: ~par[6]}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
   {ENTIRE-}
{OD}
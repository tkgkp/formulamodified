:!UTF-8
{TITLE:K. Wydruk etykiet środków i elementów - arkusz A4}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,10,10,10,10,0,1}
{BEGIN:
        SRSR.cntx_psh(); SRST.cntx_psh();
        bar_nri:='';
        bar_nst:='';
        bar_nri2:='';
        bar_nst2:='';
        licznik:=0;
        exec('odd_filtr','fst');
        VAR_DEL.delete('bar_form', 'BAR_SROD', 'BAR_ELEM', 'BAR_IND');
        BAR_SROD:=tab_tmp(1, 'REF', 'INTEGER'    , 'Nr ref-a rekordu tabeli SRST'@,
                             'NRI', 'STRING[15]' , 'Nr inwentarzowy'@,
                             'NST', 'STRING[100]', 'Nazwa'@,
                             'ODD', 'STRING[8]', 'Jedn. ksieg.'@);
        BAR_ELEM:=tab_tmp(1, 'REF','INTEGER','Nr ref-a rekordu tabeli SRST'@,
                             'NRI', 'STRING[18]', 'Nr inwentarzowy elem.'@,
                             'NST', 'STRING[40]', 'Nazwa elementu'@,
                             'NRI_SROD', 'STRING[15]', 'Nr inw. srodka'@,
                             'ODD_SROD', 'STRING[8]', 'Jedn. ksieg. srodka'@);
        BAR_IND:=BAR_ELEM.ndx_tmp('',0,'ODD_SROD',,0,
                                       'NRI_SROD',,0,
                                       'NRI_SROD',,0,
                                       'NRI',,0)
}
{END:
        SRSR.cntx_pop(); SRST.cntx_pop();
        &bar_nri;
        &bar_nst;
        &bar_nri2;
        &bar_nst2;
        &licznik;
        VAR_DEL.delete('bar_form', 'BAR_SROD', 'BAR_ELEM', 'BAR_IND')
}
{EXIT:
    _exit:=0;
    {? ~_exit
    || {? SRSR.name()<>'srsrr'+REF.FIRMA().SYMBOL
       || exec('init','fst');
          SRD.maski('r')
       ?};
       PAR_WYDR.JORG:=null; EDIT_ES.JORG:='';
       exec('odd_filtr','fst');
       {? OPERATOR.DEPT<>null || PAR_WYDR.ODD:=OPERATOR.DEPT || PAR_WYDR.ODD:=null ?};
       PAR_WYDR.DRUK_ELE:=0;
       PAR_WYDR.OD_NRI:='';
       PAR_WYDR.DO_NRI:='';
       PAR_WYDR.ETY_WYB:=2;
       PAR_WYDR.win_edit('ETYKIETY');
       PAR_WYDR.hdr_edit(': K. Wydruk etykiet środków i elementów - arkusz A4'@);
       {? PAR_WYDR.edit("exec('check_etykiety','fst')") || _exit:=0 || _exit:=1 ?}
    ?};
    {? ~_exit
    || exec('barcode','fst',1);
       {? BAR_SROD.size()=0 & BAR_ELEM.size()=0
       || _exit:=1
       ?}
    ?};
    _exit
}
{DEFINEFONT: 'courier=Courier New,,m,8'}
{FONT: 'courier'}
{COMMENT}
(c) 2002 Macrologic
==============================================================================
    system: E S T R A
      plik: O_KODKRE.RPM
            +----------------------------------------------------------------+
  Zawartość | Drukowanie etykiet Środków trwałych                            |
            +----------------------------------------------------------------+
==============================================================================
{COMMENT-}
{MACRO:'etykieta'}
{ENTIRE}
╔═══                                                 {>60 '═══╗'}{<65 '╔═══'}{>125 '═══╗'}
║ {XINFO.SKROT}                                      {>60 '   ║'}{<65 '║ '}{XINFO.SKROT}{>125 '   ║'}

{ temp:=bar_nri; temp2:=bar_nri2;~~}
{SPACE: 'A'}{<4}{BARCODE:temp; +temp*3; 1;PAR_WYDR.KODKRESK}{<68}{BARCODE:temp2; +temp2*3; 1;PAR_WYDR.KODKRESK}{FONT: 'courier'}{SPACE: 'E'}


{SPACE: 'B'}
║ {bar_nri } {40+bar_nst }                           {>60 '   ║'}{<65 '║ '}{bar_nri2 } {40+bar_nst2 }{>125 '   ║'}
╚═══                                                 {>60 '═══╝'}{<65 '╚═══'}{>125 '═══╝'}
{ENTIRE-}
{MACRO-}
{MACRO:'etykiet2'}
{ENTIRE}
╔═══                                                 {>60 '═══╗'}
║ {XINFO.SKROT}                                      {>60 '   ║'}

{ temp:=bar_nri; ~~}
{SPACE: 'A'}{<4}{BARCODE:temp; +temp*3; 1;PAR_WYDR.KODKRESK}{FONT: 'courier'}{SPACE: 'E'}


{SPACE: 'B'}
║ {bar_nri } {40+bar_nst }                           {>60 '   ║'}
╚═══                                                 {>60 '═══╝'}
{ENTIRE-}
{MACRO-}
{FOR: 'BAR_SROD'}
   {DO}
      {licznik+=1;
       {? licznik %*2 = 1
       || bar_nri:=BAR_SROD.NRI; bar_nst:=BAR_SROD.NST
       || bar_nri2:=BAR_SROD.NRI; bar_nst2:=BAR_SROD.NST
       ?}
      ; ~~}
      {IF: licznik %* 2 = 0}
        {SUBSTITUTE: 'etykieta'}
        {bar_nri:=''; bar_nst:=''; bar_nri2:=''; bar_nst2:=''; ~~}
      {FI}
      {FOR: 'BAR_ELEM'}{INDEX: BAR_IND}{PREFIX: BAR_SROD.ODD, BAR_SROD.NRI, BAR_SROD.NRI}
         {DO}
            { licznik+=1;
              {? licznik %* 2=1
              || bar_nri:=BAR_ELEM.NRI; bar_nst:=BAR_ELEM.NST
              || bar_nri2:=BAR_ELEM.NRI; bar_nst2:=BAR_ELEM.NST
              ?}
            ; ~~}
            {IF: licznik %* 2=0}
              {SUBSTITUTE: 'etykieta'}
              {bar_nri:=''; bar_nst:=''; bar_nri2:=''; bar_nst2:=''; ~~}
            {FI}
         {OD}
   {OD}
 {IF: bar_nri <> ''}
     {SUBSTITUTE: 'etykiet2'}
     {bar_nri:=''; bar_nst:=''; bar_nri2:=''; bar_nst2:=''; ~~}
{FI}
{COMMENT}
==============================================================================
      koniec pliku
==============================================================================
{COMMENT-}

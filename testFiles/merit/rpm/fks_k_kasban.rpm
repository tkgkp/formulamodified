:!UTF-8
{TITLE:D.  Raport kasowo - bankowy}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:  sstr7:=sstr8:=suma7:=suma8:=0; wal:=''; kont:='';
         poprz7:=poprz8:=0; kl:=0; il:=0; petla:=0;
         pop_prz:=pop_roz:=obec_prz:=obec_roz:=0; odd:=OPERATOR.DEPT=null;
         kolu:=obj_new(20); w:=obj_new(20); p:=obj_new(20); s:=obj_new(3); ROZNE.PAR4:=0;
         lm:=ll:=vp:=lmt:=rsep:=0;
         PRN:=obj_new(@.CLASS.PRINT,8); PRN.sl_frame();
         PRNF:=obj_new(@.CLASS.PRINT,3); PRNF.sl_frame();
         prn1:='PRN'; prn2:='PRNF'
       }
{END:
       &sstr7; &sstr8; &suma7; &suma8; &wal; &kont;
       &poprz7; &poprz8; &kl; &il; &petla;
       &pop_prz; &pop_roz; &obec_prz; &obec_roz; &odd;
       obj_del(kolu); obj_del(w); obj_del(p); obj_del(s);
       &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep;
       obj_del(PRN); obj_del(PRNF)
      }
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: k_kasban.rpm
Utworzony:   ????-??-??
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Raport kasowo-bankowy
{COMMENT-}
{ w[1]:=0;
  kl:=8;
  kolu[1]:=5;
  kolu[2]:=10;
  kolu[3]:=50;
  kolu[4]:=22+8*odd;
  kolu[5]:=35;
  kolu[6]:=76-8*odd;
  kolu[7]:=15;
  kolu[8]:=16;
  il:=kolu[1]+kolu[2]+kolu[3]+kolu[4]+kolu[5]+kolu[6]+kolu[7]+kolu[8]+kl+1;
  p[1]:=2+kolu[1];
   {!_a:=2 ..kl
   |! p[_a]:=1+p[_a-1]+kolu[_a]
   !};
   wal:='Waluta aktywna:  %1 - %2'@[SSTALE.WAL().TR,SSTALE.WAL().KOD];
   PRN.add("w[1]",kolu[1],'Lp.'@,1);
   PRN.add("w[2]",kolu[2],'Data zapisu'@);
   PRN.add("w[3]",kolu[3],'Symbol dokumentu'@);
   {? odd
   || PRN.add("w[4]",kolu[4],' Rej./ Jedn. ks./ Nr./ Poz.'@)
   || PRN.add("w[4]",kolu[4],'Rej./ Nr./ Poz.'@)
   ?};
   PRN.add("w[5]",kolu[5],'Konto'@);
   PRN.add("w[6]",kolu[6],'Treść'@);
   PRN.add("w[7]",kolu[7],'Przychód'@,1);
   PRN.add("w[8]",kolu[8],'Rozchód'@,1);
   PRNF.add("s[1]",kolu[1]+kolu[2]+kolu[3]+kolu[4]+kolu[5]+kolu[6]+5,'',,,,,,,,6);
   PRNF.add("s[2]",kolu[7],,1);
   PRNF.add("s[3]",kolu[8],,1);
   WYDRUKIN.win_edit('WYBDATA1'@);
   WYDRUKIN.DATAOD:=SSTALE.AO().POCZ;
   WYDRUKIN.DATADO:=SSTALE.AO().KON;
   ROZNE.PREFIX:=''; ~~
}
  {EXIT: ~WYDRUKIN.edit("{? SSTALE.AO().POCZ=date(0,0,0)  &
                           (~exec('dat_not','dok_fks',WYDRUKIN.DATAOD) | ~exec('dat_not','dok_fks',WYDRUKIN.DATADO))
                         || FUN.info('Podane daty muszą być z bieżącego roku bilansowego.'@);0
                         |? ((WYDRUKIN.DATAOD<SSTALE.AO().POCZ)|
                             (WYDRUKIN.DATADO>SSTALE.AO().KON)|
                             (WYDRUKIN.DATAOD>WYDRUKIN.DATADO)) & SSTALE.AO().POCZ<>date(0,0,0)
                         || FUN.info('Daty muszą się mieścić w bieżącym okresie obrachunkowym.'@); 0
                         || 1
                         ?}
                     ")}
  {IF: ROZNE.PREFIX=''}
    {kont:='Konta: wszystkie'@; ~~}
  {ELSE}
    {kont:='Dla kont o początkowych znakach: %1'@[ROZNE.PREFIX]; ~~}
  {FI}
{PAR_WYDR.TITLE:='RAPORT KASOWO - BANKOWY ZA OKRES OD %1 DO %2'@[$WYDRUKIN.DATAOD,$WYDRUKIN.DATADO];~~}
{INCLUDE: wsp_pw.rpi}
{IF: rep_export=0}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{LINE: 1}{IF: page=1}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<1 'PARAMETRY WYDRUKU:'@}
{<2 'Jednostka księgowa: %1'@[{? OPERATOR.DEPT || OPERATOR.DEPT().OD || 'Wszystkie jednostki księgowe'@ ?}]}
{<2 'Waluta: %1'@[SSTALE.WAL().KOD]}
{<2 'Okres: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AO().ROK().NAZ]}
{IF: SSTALE.AO().ZAM='T'}
{<2 'Rodzaj okresu: zamknięty'@}
{ELSE}
{<2 'Rodzaj okresu: otwarty'@}
{FI}
{<2 kont}
{LINE: 1}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{IF: rep_export=1}{TR}{FI}
{FONT: 'T8G'}{vp:=1; ~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{FONT: 'T8G'}{SPACE: 'C'}{vp:=0;rsep:=1;~~}{ s[1]:='OBROTY'; s[2]:=form(sstr7 ,,2,' ,'); s[3]:=form(sstr8 ,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA02'}{prn1:='PRNF';~~}
{IF: page>1}
{ s[1]:='Z PRZENIESIENIA'; s[2]:=form((suma7-sstr7) ,,2,' ,'); s[3]:=form((suma8-sstr8) ,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA0'}
{FI}  {'Do IF: page>1';~~}
{ s[1]:='DO PRZENIESIENIA'; s[2]:=form(suma7 ,,2,' ,'); s[3]:=form(suma8 ,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA0'}
{REP: PRNF.lbar(lmt)}{prn1:='PRN';~~}{FONT: 'Q'}{SPACE: 'C'}{sstr7:=sstr8:=0; ~~}{vp:=1;rsep:=0; ~~}
{FOOTER-}
{'------------------------------------------------------------------';~~}
{'                    CIAŁO  WYDRUKU                                ';~~}
{'------------------------------------------------------------------';~~}
{FONT: 'T8G'}{SPACE: 'C'}
{lmt:=int((charleft()-PRN.width())/2); vp:=1;~~}
{ {? OPERATOR.DEPT
  || {? SSTALE.WAL<>FINFO.NAROD
     || POZ.index('OKWAL_O'); POZ.prefix('T','T',SSTALE.WAL,OPERATOR.DEPT,ROZNE.PREFIX)
     || POZ.index('OKWAL_O1'); POZ.prefix('T','T',OPERATOR.DEPT,ROZNE.PREFIX)
     ?}
  || {? SSTALE.WAL<>FINFO.NAROD
      || POZ.index('OKWAL'); POZ.prefix('T','T',SSTALE.WAL,ROZNE.PREFIX)
      ||  POZ.index('OKWAL1'); POZ.prefix('T','T',ROZNE.PREFIX)
     ?}
  ?};
  petla:=POZ.first(); ~~}
{WHILE: petla}
{DO}
        {IF: (POZ.DOK().DTW>=WYDRUKIN.DATAOD)&(POZ.DOK().DTW<=WYDRUKIN.DATADO)}
{'------------------------------------------------------------------';~~}
{'        Obliczenie kolejnego wyrażenia dla linii                  ';~~}
{'------------------------------------------------------------------';~~}
           { w[1]+=1;
             w[2]:=$POZ.DOK().DTW;
             w[3]:=POZ.DOK().NK;
             w[4]:=POZ.DOK().REJ().KOD+(8-(+POZ.DOK().REJ().KOD))*' '+'/'+{? odd || REJ.ODD().OD+(8-(+ODD.OD))*' '+'/' || '' ?}+form(DOK.NR,5,,'9')+'/'+form(POZ.POZ,5,,'9');
             w[5]:=POZ.KON;
             w[6]:=POZ.DOK().TR;
             w[7]:={? -POZ.STR='wn'
                         || {? SSTALE.WAL=FINFO.NAROD
                             ||  sstr7+=POZ.SUM; suma7+=POZ.SUM; form(POZ.SUM ,,2,' ,')
                             || sstr7+=POZ.SUMW; suma7+=POZ.SUMW; form(POZ.SUMW ,,2,' ,')
                             ?}
                         || form(0 ,,2,' ,')
                         ?};
             w[8]:={? -POZ.STR='ma'
                         || {? SSTALE.WAL=FINFO.NAROD
                             ||  sstr8+=POZ.SUM; suma8+=POZ.SUM; form(POZ.SUM ,,2,' ,')
                             || sstr8+=POZ.SUMW; suma8+=POZ.SUMW; form(POZ.SUMW ,,2,' ,')
                             ?}
                         || form(0 ,,2,' ,')
                         ?}; ~~
           }
{'------------------------------------------------------------------';~~}
{'            Wydruk obliczonego wyrażenia                          ';~~}
{'------------------------------------------------------------------';~~}
              {SUBSTITUTE: 'LINIA0'}
              { w[7]:=w[8]:=0; ~~}
        {FI} {'  Do IF: zakres DAT';~~}
   {petla:=POZ.next(); ~~}
{OD}  {' Do POZ';~~}
{'------------------------------------------------------------------';~~}
{'            Obliczenie ostatniej stopki                           ';~~}
{'------------------------------------------------------------------';~~}
{IF: SSTALE.AO().NR>0}
  { F.Obr(ROZNE.PREFIX,0,(SSTALE.AO().NR-1));
    poprz7:=F.wn;
    poprz8:=F.ma; ~~}
{FI}
{ {? OPERATOR.DEPT
  || {? SSTALE.WAL<>FINFO.NAROD
     || POZ.index('OKWAL_O'); POZ.prefix('T','T',SSTALE.WAL,OPERATOR.DEPT,ROZNE.PREFIX)
     || POZ.index('OKWAL_O1'); POZ.prefix('T','T',OPERATOR.DEPT,ROZNE.PREFIX)
     ?}
  || {? SSTALE.WAL<>FINFO.NAROD
      || POZ.index('OKWAL'); POZ.prefix('T','T',SSTALE.WAL,ROZNE.PREFIX)
      ||  POZ.index('OKWAL1'); POZ.prefix('T','T',ROZNE.PREFIX)
     ?}
  ?};
  petla:=POZ.first(); ~~}
{WHILE: petla}
{DO}
         {IF: (POZ.DOK().DTW<WYDRUKIN.DATAOD)}
            { {? -POZ.STR='wn'
                || poprz7+={? SSTALE.WAL=FINFO.NAROD || POZ.SUM$2 || POZ.SUMW$2 ?}
                || poprz8+={? SSTALE.WAL=FINFO.NAROD || POZ.SUM$2 || POZ.SUMW$2 ?}
                ?}; ~~}
         {FI} {'  Do IF: zakres DAT';~~}
   {petla:=POZ.next(); ~~}
{OD}  {' Do POZ';~~}
{'------------------------------------------------------------------';~~}
{'            KONIEC  WYDRUKU                                       ';~~}
{'------------------------------------------------------------------';~~}
{ pop_prz:=F.SWn(poprz7,poprz8);
  pop_roz:=F.SMa(poprz7,poprz8);
  obec_prz:=F.SWn(pop_prz+F.SWn(suma7,suma8),pop_roz+F.SMa(suma7,suma8));
  obec_roz:=F.SMa(pop_prz+F.SWn(suma7,suma8),pop_roz+F.SMa(suma7,suma8));
  ~~
}
{FONT: 'T8G'}{SPACE: 'C'}{IF: lineleft()<14 & lineleft()<>0}{PAGE}{FI}
{FOOTER:0}
{FOOTER-}
{vp:=lineleft()=0;rsep:=1; s[1]:='OBROTY'@; s[2]:=form(sstr7 ,,2,' ,'); s[3]:=form(sstr8 ,,2,' ,');~~}
{SUBSTITUTE: 'LINIA02'}{prn1:='PRNF';~~}{IF: page>1}{ s[1]:='Z PRZENIESIENIA'@; s[2]:=form((suma7-sstr7) ,,2,' ,'); s[3]:=form((suma8-sstr8) ,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA0'}{FI}{ s[1]:='SUMA'@; s[2]:=form(suma7 ,,2,' ,'); s[3]:=form(suma8 ,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA0'}{ s[1]:='SALDO'@; s[2]:=form(F.SWn(suma7,suma8) ,,2,' ,'); s[3]:=form(F.SMa(suma7,suma8) ,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA0'}{ s[1]:='POPRZEDNI STAN'@; s[2]:=form(pop_prz ,,2,' ,'); s[3]:=form(pop_roz ,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA0'}{ s[1]:='OBECNY STAN'@; s[2]:=form(obec_prz ,,2,' ,'); s[3]:=form(obec_roz ,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA0'}
{IF: rep_export=0}
{REP: PRNF.lbar(lmt)}
{<1>{il}'KONIEC WYDRUKU '@}
{ELSE}{TABLE-}
{FI}

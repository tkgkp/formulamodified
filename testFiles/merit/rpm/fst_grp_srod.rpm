:!UTF-8
{TITLE:Środki zbyte w obrębie grupy kapitałowej}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,15); PRN.s_frame();
   c:=obj_new(15);
   {! _i:=1..14 |! c[_i] := '' !};
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0; par5:=0
}
{END:
   obj_del(PRN);
   obj_del(c);
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3;&par4;&par5
}
{EXIT:
   exec('set_var','param_wydr');
   {? OPERATOR.DEPT<>null
      || PAR_WYDR.ODD:=OPERATOR.DEPT
      || PAR_WYDR.ODD:=0
   ?};
   0
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_grp_srod.rpm
Utworzony  : 2017-09-15
Zawartość  : Środki zbyte w obrębie grupy kapitałowej
==================================================
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("c[1]",10,'Data zbycia',,' ');
      PRN.add("c[2]",15,'Firma zbywająca',,' ');
      PRN.add("c[3]",11,'Dokument zbycia',,' ');
      PRN.add("c[4]",15,'Nr pierwotny',,' ');
      PRN.add("c[5]",18,'Nazwa pierwotna',,' ');
      PRN.add("c[6]",10,'Data nabycia',,' ');
      PRN.add("c[7]",15,'Firma docelowa',,' ');
      PRN.add("c[8]",15,'Nr docelowy',,' ');
      PRN.add("c[9]",18,'Nazwa docelowa',,' ');

      PAR_WYDR.TITLE:='Środki zbyte w obrębie grupy kapitałowej'; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{FOR: SRSFIRMA}{INDEX: 'DATA'}
 {DO}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
 {
      SRSR.cntx_psh(); SRDO.cntx_psh();
      SRSR.prefix(); SRDO.prefix();

      {? SRDO.seek(SRSFIRMA.SRDO_REF,ref_name(SRSFIRMA.SRDO_REF))
      || _dokument:=SRDO.SYMBOL
      || _dokument:=''
      ?};
      {? SRSR.seek(SRSFIRMA.SRSR_Z,ref_name(SRSFIRMA.SRSR_Z))
      || _nri_z:=SRSR.NRI;
         _nst_z:=SRSR.NST
      || _nri_z:=_nst_z:=''
      ?};
      {? SRSR.seek(SRSFIRMA.SRSR_D,ref_name(SRSFIRMA.SRSR_D))
      || _nri_d:=SRSR.NRI;
         _nst_d:=SRSR.NST
      || _nri_d:=_nst_d:=''
      ?};


      c[1]:=$SRSFIRMA.DATA_Z;
      c[2]:=SRSFIRMA.FIRMA_Z().SYMBOL+' - '+SRSFIRMA.FIRMA_Z().OPIS;
      c[3]:=_dokument;
      c[4]:=_nri_z;
      c[5]:=_nst_z;
      c[6]:=$SRSFIRMA.DATA_D;
      c[7]:=SRSFIRMA.FIRMA_D().SYMBOL+' - '+SRSFIRMA.FIRMA_D().OPIS;
      c[8]:=_nri_d;
      c[9]:=_nst_d;

      SRSR.cntx_pop(); SRDO.cntx_pop()

 ;~~}
  {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}{FOOTER-}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
:!UTF-8
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','dane');
   tresc:='pgodziny';
   PAR_WYDR.TITLE:='Wykaz godzin przepracowanych'@;
   par:=obj_new(18);
   par[13]:='';
   par[18]:=rep_export();
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','godzinach pracy'@,'PPL_PLL_PGOD','PPL_PLL_RGOD');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[14]:=tab_tmp(3,
         'PRAC','INTEGER','Pracownik',
         'LP','INTEGER','Numer',
         'RN','INTEGER','Kod',
         'RT','STRING[20]','Nazwa','GODZ',
         'REAL','Suma godzin'
      );
      par[16]:=tab_tmp(2,
         'LP','INTEGER','Numer',
         'RN','INTEGER','Kod',
         'RT','STRING[20]','Nazwa',
         'GODZ','REAL','Suma godzin'
      );

      {? (par[13]:=1-exec('code_grp','profile',,'godzprac','R',{? ~par[18] || 10 ?})-1)<>''
      || par[1]:=obj_new(@.CLASS.PRINT,{? par[18] || 8 || 5 ?});
         {? par[18]
         || par[1].add("OSOBA.NAZWISKO",30,'Nazwisko'@);
            par[1].add("OSOBA.PIERWSZE",20,'Imię'@);
            par[1].add("P.T",9,'Nr teczki'@)
         ?};
         par[1].add("par[2].DATA$1",10,'Data'@);
         par[1].add("par[2].RT",20,'Nazwa składnika'@);
         par[1].add("par[2].KONTO",20,'Konto kosztów'@);
         par[1].add("par[2].LT",10,'Lista'@,1);
         par[1].add("par[2].GODZ",15,'Liczba godzin'@,1,,,,,'12,2');
         par[1].s_frame();

         par[15]:=obj_new(@.CLASS.PRINT,3);
         par[15].add("''",10);
         par[15].add("par[14].RT",56,'Nazwa składnika'@);
         par[15].add("par[14].GODZ",15,'',1,,,,,'12,2');
         par[15].s_frame();

         par[17]:=obj_new(@.CLASS.PRINT,3);
         par[17].add("{?dane=1||'R A Z E M'@||''?}",10);
         par[17].add("par[16].RT",56,'Nazwa składnika'@);
         par[17].add("par[16].GODZ",15,'',1,,,,,'12,2');
         par[17].s_frame();

         R.cntx_psh();
         R.index('RUBKOD');
         R.prefix();
         {? +(|par[13])
         || STR.split(par[13],',');
            {!
            |? {? (_kod:=|STR.get_word())<>''
               || {? R.find_key(#_kod) || par[16].LP:=R.LP; par[16].RN:=R.RN; par[16].RT:=R.RT; par[16].add() ?}
               ?}
            !}
         ?};
         R.cntx_pop();

         _spr:=
            "  _tab:=params_get().tab;
               {? _tab.DO=#0 | _tab.OD=#0 | _tab.OD>_tab.DO
               || FUN.emsg('Daty muszą być wypełnione. Data początku okresu nie może być większa od daty końcowej.'@);
                  {? _tab.OD=#0 || 'OD' || 'DO' ?}
               || 1
               ?}
            ";
         par[3]:=exec('par_wydr_ddp','pkd',PAR_WYDR.TITLE,_spr,date(,1,1),'Od daty'@,"1",date(,12,0),'Do daty'@,"1");
         {? par[3].size()
         || b_rat:=h_rat:=vp:=par[9]:=1;
            par[12]:=tab_tmp(,'REF','INTEGER','Numer');
            Lp:=lmt:=rsep:=dane:=0;
            prn1:='par[1]';
            params_set('P',exec('wybierz_parses','pracownik',dziedzina).P)
         ?}
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','dane','dziedzina')
}
{COMMENT}OLD: wgodziny.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: godzprac.rpi{COMMENT-}
{EXIT: par[13]=''}
{EXIT: ~par[3].size()}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[9]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? par[18] || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[9]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'%1: od %2 do %3'['Zakres dat'@,par[3].OD$1,par[3].DO$1]}
   {<{ceil(lmt*h_rat)+1}'Uwzględniane składniki:'@} {STR.gsub(par[13],',',', ')}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~par[18]}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'podsum'}
{ENTIRE}
{FONT: 'T8B'}
{SPACE: 'B'}
{  dane:=0; ~~ }
{FOR: par[14]}{PREFIX: P.ref()}{DO}
{  dane+=1;
   {? par[16].find_key(par[14].LP)
   || par[16].GODZ+=par[14].GODZ;
      par[16].put()
   ?}; ~~
}
{IF: dane=1}
{<{lmt+1}} {'Suma pracownik: '@}{par[4]}
{REP: par[15].fhbar(lmt)}
{FI}
   {par[15].ini_line(); ~~ }
   {REP: par[15].fline(lmt)}
   {WHILE: par[15].next()}
   {DO}
      {REP: par[15].fline(lmt)}
   {OD}
{OD}
{IF: dane}
{REP: par[15].flbar(lmt)}
{FI}
{ENTIRE-}
{MACRO-}
{MACRO: 'sumfirm'}
{ENTIRE}
{FONT: 'T8B'}
{SPACE: 'B'}
{  dane:=0; ~~ }
{FOR: par[16]}{DO}
{IF: par[16].GODZ}
{  dane+=1; ~~ }
{IF: dane=1}
{REP: par[17].fhbar(lmt)}
{FI}
   {  par[17].ini_line(); ~~ }
   {REP: par[17].fline(lmt)}
   {WHILE: par[17].next()}
   {DO}
      {REP: par[17].fline(lmt)}
   {OD}
{FI}
{OD}
{IF: dane}
{REP: par[17].flbar(lmt)}
{FI}
{ENTIRE-}
{MACRO-}
{MACRO: 'opis'}
{IF: par[7]<>''}
   {IF: ~par[18]}
      {FONT: 'W12'}
      {SPACE: 'A'}
      {<{ceil(lmt*b_rat)+1}}{Lp+=1; form(Lp,,,'9.')} {par[7]}
   {FI}
   {FONT: 'T8'}
   {SPACE: 'B'}
   {REP: par[1].fhbar(lmt)}
   {  par[7]:='';
      par[11]:=1; ~~
   }
{FI}
{MACRO-}
{MACRO: tresc}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[7]:=par[4]:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' ('+form(P.T)+')'; ~~ }
{FOR: par[2]}{PREFIX: $P.ref()}{DO}
   {IF: lineleft() & lineleft()<=4}
      {IF: par[11]}
         {REP: par[1].flbar(lmt)}
         {  par[11]:=0;
            Lp-=1;
            par[7]:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' ('+form(P.T)+')'; ~~
         }
      {FI}
      {IF: lineleft()}
         {PAGE}
      {FI}
   {FI}
   {SUBSTITUTE: 'opis'}
   {  par[1].ini_line(); ~~ }
   {REP: par[1].fline(lmt)}
   {WHILE: par[1].next()}
   {DO}
      {REP: par[1].fline(lmt)}
   {OD}
   {  {? par[14].find_key(#P.ref(),par[2].LP,par[2].RN)
      || par[14].GODZ+=par[2].GODZ;
         par[14].put()
      || par[14].prefix();
         par[14].PRAC:=#P.ref();
         par[14].LP:=par[2].LP;
         par[14].RN:=par[2].RN;
         par[14].RT:=par[2].RT;
         par[14].GODZ:=par[2].GODZ;
         par[14].add()
      ?}; ~~
   }
{OD}
{IF: par[7]=''}
   {REP: par[1].flbar(lmt)}
   {  par[11]:=0; ~~ }
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{MACRO-}
{MACRO: 'godziny'}
{  par[10]:=par[3].OD~1;
   _sql:='select G.P PRAC, G.G GODZ, G.D DATA, R.RT RT, KK.SYM KONTO, G.LT LT, R.LP, R.RN RN '+
         'from @G join R left join KK '+
         'where (G.REFERENCE like \'godz'+$par[10]+'%\'';
   par[10]:=par[10]+1;
   {!
   |? par[10]<=(par[3].DO~1+1)
   |! _sql+=' or G.REFERENCE like \'godz'+$par[10]+'%\'';
      par[10]+=1
   !};
   _sql+=') and G.P=:_d ';
   _sql+=' and G.R=\'G\' and G.D>=to_date(:_a) and G.D<=to_date(:_b) and R.RN in (:_c)';
   _sql+=' order by PRAC, DATA, LP, RN';
   par[2]:=sql(_sql,par[3].OD,par[3].DO,par[13],P.ref()); ~~
}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? par[18] || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {IF: ~par[12].find_key(#P.ref())}
         {SUBSTITUTE: 'godziny'}
         {SUBSTITUTE: tresc}
         {IF: ~par[18]}
            {SUBSTITUTE: 'podsum'}
         {FI}
         {  par[12].REF:=#P.ref();
            par[12].add();
           {? type_of(par[2])=type_of(SYSLOG) || obj_del(par[2]) ?}; ~~
         }
      {FI}
   {FI}
{OD}
{IF: ~par[18]}
{SUBSTITUTE: 'sumfirm'}
{FI}

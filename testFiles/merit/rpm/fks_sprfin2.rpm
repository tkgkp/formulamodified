:!UTF-8
{TITLE: Wydruk dla sprawozdań finansowych - wartości algorytmów}
{PRINTER: SIK.PRT,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   okres:=okres1:=null; wart_alg:=0;
   odd:={? PAR_SKID.get(12)='T' || OPERATOR.DEPT || null ?};
   PRN:=obj_new(@.CLASS.PRINT,5); PRN.sl_frame();
   vp:=0; w:=obj_new(5);
   color:=prn1:=prn2:=''; lm:=ll:=lmt:=rsep:=0;
   exec('ini_wal','konto',0);
   __KURS:=1; LANG_SLO:=SKID.LANG_SLO
}
{END: &color; &wart_alg; &vp; &prn1; &prn2; SKID.LANG_SLO:=LANG_SLO;
      VAR_DEL.delete('w','PRN','__KURS','TAB_WAR','LANG_SLO','PAGELANG');
      &lm; &ll; &lmt; &rsep; &okres; &okres1; &odd
}
{ PAR_WYDR.TITLE:=~-exec('get_lang','lang',GR_SIK.ref()); prn1:=prn2:='PRN';
  exec('set_par_win2','!fks_ksp_zawd');
  WYRAZ.GRUPA:=SIK.GR_SIK:=GR_SIK.ref();
  GR_KOL.win_dict('WER2');
  GR_KOL.cntx_psh();
  GR_KOL.index('NAZWA'); GR_KOL.prefix(GR_SIK.ref());
  SIK.KOLUMNA:={? GR_KOL.first() & GR_KOL.size()=1 || GR_KOL.ref() || null ?};
  GR_KOL.cntx_pop();
  SIK.PARDRUK:=0;
  SIK.PARWPOD:=1; SIK.PARWINN:=0;
  ~~}
{EXIT: ~SIK.edit("chk_rec('KOLUMNA')")}
{ {? REF.FIRMA=REF.GRUPA
  || REF.WYKON:=exec('wykon_firmy','konsolidacja',REF.FIRMA,REF.W_SCH)
  || REF.WYKON:=exec('wykon_firmy','konsolidacja',REF.FIRMA)
  ?};
  {? SKID.LANG_SLO & SKID.LANG_SLO().KOD<>'POL' || PAGELANG:=1 ?};
  SIK.KOLUMNA();
  __KURS:=exec('kurs','konto',UNPAR.PSLO().KOD,UNPAR.P25);
  {? REF.S_FIRMA=REF.GRUPA & REF.FIRMA<>REF.GRUPA
  || _okr:=exec('okro_f_f','konsolidacja',SSTALE.AO,REF.FIRMA);
     {? _okr
     || OKRO_F.prefix();
        OKRO_F.seek(_okr);
        okres:=OKRO_F.ref();
        OKRO_F.ROK()
     || SSTALE.AO().ROK(); okres:=SSTALE.AO
     ?}
  || SSTALE.AO().ROK(); okres:=SSTALE.AO
  ?};
  _nazwa:=ROK_F.NAZ+'-'+form(OKRO_F.NR,2,0)+' ('+OKRO_F.NAZ+')';
  OKRESY.OKR_RAP:=null;
  {? exec('dod_ospr','okresy',ROK_F.ref(),OKRO_F.NR,OKRO_F.NR,_nazwa,'M')
  || OKRESY.OKR_RAP:=OSPR.ref
  ?};
  WARTOSCI.use('wsik_'+ROK_F.KOD);
  W_ALGPAR.use('walg_'+ROK_F.KOD);
  GrSikPrn:=1;
  ~~}
{INCLUDE: wsp_pw.rpi}
{PRN.add("w[1]",5,exec('tran_sik','lang',14),1);
 PRN.add("w[2]",15,exec('tran_sik','lang',15));
 PRN.add("w[3]",78,exec('tran_sik','lang',16));
 PRN.add("w[4]",18,exec('get_lang','lang',GR_KOL.ref()),1);~~}
{IF: SIK.PARWINN}{PRN.add("w[5]",17,exec('tran_sik','lang',11),1);~~}{FI}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); OKRO_F.prefix(); OKRO_F.seek(okres); OKRO_F.ROK(); ~~}
{<1 exec('tran_sik','lang',1)+':'}
{<1 exec('tran_sik','lang',2)+': '+{? PAR_SKID.get(12)='T' & OPERATOR.DEPT || OPERATOR.DEPT().OD || exec('tran_sik','lang',3) ?}}
{<1 exec('tran_sik','lang',4)+': '+exec('get_lang','lang',OKRO_F.ref())+' '+ROK_F.NAZ+'   '+exec('tran_sik','lang',5)+': '+{? OKRO_F.ZAM='T'||exec('tran_sik','lang',6)||exec('tran_sik','lang',7) ?}}
{IF: var_press('Konsolid')>0}
{<1 'Wartości: %1'@[{? SIK.WYL='T' || 'jednostkowe'@ |? SIK.WYL='W' || 'wyłączeniowe'  || 'skonsolidowane'@ ?}]}
{FI}
{IF: UNPAR.PSLO}
{<1 exec('tran_sik','lang',12)+': '+UNPAR.PSLO().KOD+'     '+exec('tran_sik','lang',13)+': '+form(UNPAR.P25,,6,' ,') }
{ELSE}
{LINE: 1}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{ {? __KURS<>1 || exec('tab_pom','!fks_ksp_zawd',0) ?};~~}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; W_ALGPAR.index('W_ALGPAR'); ~~}
{FOR: 'DEFW'}{INDEX:'LP'}{PREFIX: GR_SIK.ref}
{DO}
{COMMENT}------------------------------WSKAZNIKI SPRAWOZDANIA-----------------------{COMMENT-}
{  w[1]:=$DEFW.LP; w[2]:=DEFW.KOD; w[3]:=exec('get_lang','lang',DEFW.ref()); w[4]:=w[5]:='';
   {? -DEFW.ALGORYTM<>'p'
   || WARTOSCI.index('KOD'); WARTOSCI.prefix(REF.WYKON,DEFW.ref,OKRESY.OKR_RAP,odd,'N','',GR_KOL.ref());
      {? WARTOSCI.first()
      || w[4]:={? __KURS=1
               || form(WARTOSCI.WARTOSC,18,2,' ,')
               |? TAB_WAR.find_key(#DEFW.ref())
               || form(TAB_WAR.WAR,18,2,' ,')
               || ''
               ?};
         w[5]:=$WARTOSCI.DATA+'  '+$WARTOSCI.CZAS
      || w[4]:=form(0,18,2,' ,')
      ?}
   || w[4]:=w[5]:=''
   ?};
~~}
{SUBSTITUTE: 'LINIA0'}
{COMMENT}--------------------------ALGORYTMY WSKAZNIKOW PODSTAWOWYCH-----------------------{COMMENT-}
{IF: -DEFW.ALGORYTM='f' & SIK.PARWPOD}
{ {? REF.FIRMA=REF.GRUPA
  || ALG_PAR.prefix();
     _firmy:=sql('select FIRMA from W_STR where W_SCH=:_a and FIRMA!=:_b',REF.W_SCH,REF.GRUPA);

     ALG_PAR.f_set(
        'FIRMA(SYMBOL),ROK(NAZ),LP',
        'join ROK_F using(ALG_PAR.ROK,ROK_F.REFERENCE) join FIRMA using(ALG_PAR.FIRMA,FIRMA.REFERENCE)',
        'DEFW=:_a and GR_KOL=:_b '+
        {? SIK.WYL<>'S' || 'and WYL=\':_c\' ' || '' ?}+
        {? SIK.ROK1<>null || 'and ROK_F.POCZ_ROK=TO_DATE(\':_d\') ' || '' ?}+
        'and ALG_PAR.FIRMA in (select FIRMA from :_e)',
        DEFW.ref(),GR_KOL.ref(),{? SIK.WYL='T' || 'N' || 'T' ?},$OKRESY.OKR_RAP().OKRES_OD().ROK().POCZ_ROK,_firmy
     )

  || ALG_PAR.f_clear();
     {? SIK.WYL<>'S'
     || ALG_PAR.index('ALG_PAR7');
        ALG_PAR.prefix(REF.FIRMA,DEFW.ref(),GR_KOL.ref(),{? SIK.WYL='T' || 'N' || 'T' ?},OKRESY.OKR_RAP().OKRES_OD().ROK().NAZ)
     || ALG_PAR.index('ALG_PAR1');
        ALG_PAR.prefix(REF.FIRMA,DEFW.ref(),GR_KOL.ref(),OKRESY.OKR_RAP().OKRES_OD().ROK().NAZ)
     ?}
  ?};
  next:=ALG_PAR.f_active() & ALG_PAR.f_first() | ~ALG_PAR.f_active() & ALG_PAR.first();~~
}
{WHILE: next}
{DO}
{ _okr:=OKRESY.OKR_RAP;
  {? ALG_PAR.FIRMA<>OKRESY.OKR_RAP().FIRMA
  || OKRESY.OKR_RAP:=exec('ospr_fg','konsolidacja',OKRESY.OKR_RAP,'F',ALG_PAR.FIRMA);
     W_ALGPAR.use('walg_'+OKRESY.OKR_RAP().ROK().KOD)
  ?};
  _mno:={? SIK.WYL<>'S' || 1 |? ALG_PAR.WYL='N' || 1 || -1 ?};
  {? SIK.WYL='W'
  || W_ALGPAR.prefix(ALG_PAR.ref(),OKRESY.OKR_RAP,odd,REF.FIRMAWYL)
  || W_ALGPAR.prefix(ALG_PAR.ref(),OKRESY.OKR_RAP,odd)
  ?};
  wart_alg:={? W_ALGPAR.first() || _mno*W_ALGPAR.WAR/__KURS || 0 ?};
  OKRESY.OKR_RAP:=_okr;
~~}
{IF: (SIK.PARDRUK | (~SIK.PARDRUK & wart_alg$2<>0))}
{ w[1]:=w[2]:=w[5]:='';
  WYRAZ.KOLUMNA:=ALG_PAR.GR_KOL;
  exec('parametr','sprfin');
  w[3]:='  '+exec('tran_sik','lang',24)+': '+ALG_PAR.FORMULA().SKROT+'  ';
  {! _i:=1..8
 |! {? ($('|ALG_PAR.P'+$_i))()<>'' || w[3]+=($('|SIK.NP'+$_i))()+' '+($('|ALG_PAR.P'+$_i))()+'  ' ?}
  !};
  w[4]:=form(wart_alg,18,2,' ,');~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{next:=ALG_PAR.f_active() & ALG_PAR.f_next() | ~ALG_PAR.f_active() & ALG_PAR.next();~~}
{OD}
{FI}
{COMMENT}--------------------------ALGORYTMY POZOSTALYCH WSKAZNIKOW-----------------------{COMMENT-}
{IF: SIK.PARWINN}
{ DEFW.cntx_psh(); GR_KOL.cntx_psh(); WYRAZ.WIERSZ:=DEFW.ref(); DEFW.prefix();~~}
{FOR: 'DEFA'}
{INDEX: 'LP'}
{PREFIX: WYRAZ.WIERSZ,SIK.KOLUMNA}
{DO}
{ w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=''; wart_alg:=0;
  WARTOSCI.index('KOD'); WARTOSCI.prefix(REF.WYKON,DEFA.ARGUMENT,OKRESY.OKR_RAP,odd,'N','',DEFA.ARG_KOL);
  {? WARTOSCI.first()
  || wart_alg:={? __KURS=1 | ~TAB_WAR.find_key(#DEFA.ARGUMENT) || WARTOSCI.WARTOSC || TAB_WAR.WAR ?}; w[5]:=$WARTOSCI.DATA+'  '+$WARTOSCI.CZAS
  || wart_alg:=0
  ?};~~}
{IF: (SIK.PARDRUK | (~SIK.PARDRUK & wart_alg$2<>0))}
{ DEFW.cntx_psh(); DEFW.prefix(); DEFA.ARGUMENT();
  w[3]:='  '+exec('get_lang','lang',DEFW.ref());
  DEFW.cntx_pop();
  w[4]:=form(wart_alg,18,2,' ,');~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{OD}
{ DEFW.cntx_pop(); GR_KOL.cntx_pop();~~}
{FI}
{OD}

:!UTF-8
{TITLE:A. Raport wykorzystania dokumentów Businesslink}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   zbl_del:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','doklw','p','PRN');
      VAR_DEL.delete('od_daty','do_daty');
      VAR_DEL.delete('wydr_naz','wydr_edi','wyjscie','X_Xb','X_Xw1')
   ";
   zbl_del();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   tryb:=rep_export();

   od_daty:=do_daty:=date(0,0,0);

   lp:=0;
   doklw :=2; "--dokladnosc wartosc--";

   wydr_naz:='zbl_001';
   wydr_edi:='ZBL_001';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank(1);
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.OD_DATY:=od_daty;
      WYDRUKIL.DO_DATY:=do_daty;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("
         {? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || _wyn:='' || FUN.info('Błędny zakres dat.'@);_wyn:='DO_DATY' ?};
         _wyn")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         od_daty:=WYDRUKIL.OD_DATY;
         do_daty:=WYDRUKIL.DO_DATY
      ?}
   ?};
   {? wyjscie=0
   ||
      _ilk:=4;
      PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();
      n:=obj_new(_ilk); "--nazwy kolumn--";
      k:=obj_new(_ilk); "--szerokosci kolumn--";
      w:=obj_new(_ilk); "--wartosci kolumn--";
      k[1]:=40;   n[1]:='Dokumenty';
      k[2]:=14;   n[2]:='Businesslink';
      k[3]:=14;   n[3]:='Wszystkie';
      k[4]:=14;   n[4]:='Procent';
      {! _ii.._ilk |! w[_ii] :='' !}
   ?}
}
{END:
   zbl_del(); VAR_DEL.delete('zbl_del')
}
{IF: wyjscie=1}{EXIT}{FI}
{
PRN.add("w[1]",k[1],n[1]);
PRN.add("w[2]",k[2],n[2],1);
PRN.add("w[3]",k[3],n[3],1);
PRN.add("w[4]",k[4],n[4],1);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'RODZ'   ,'STRING[40]','Dokumenty',
   'IL_BL'  ,'INTEGER'   ,'Businesslink',
   'IL'     ,'INTEGER'   ,'Wszystkie',
   'PROCENT','REAL'      ,'Procent');

_il_s:=0;
_il_z:=0;
_ilb_s:=0;
_ilb_z:=0;

OKR.cntx_psh; FAKS.cntx_psh;DOKUM.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
_min:=od_daty~1;
_max:=do_daty~1;
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
{? OKR.first & (_min=0 | OKR.find_ge(_min))
|| _fnames:=tab_tmp(2,
                    'ROK','STRING[2]','Rok',
                    'ODDZ','STRING[1]','Oddział',
                    'NAZ','STRING[8]','Nazwa');
   _fn0:=FAKS.names();
   {? _fn0.first()
   || {!
      |? _fnames.NAZ:=_fn0.NAME;
         _fnames.ROK:=_fn0.NAME+2;
         _fnames.ODDZ:=1+(_fn0.NAME+3);
         _fnames.add();
         _fn0.next()
      !}
   ?};
   {!
   |? _fnames.prefix(($OKR.ROK)+2);
      {? _fnames.first()
      || {!
         |? FAKS.use(_fnames.NAZ);
            FAKS.index('FAK_KH');
            FAKS.prefix();
            {? FAKS.first()
            || DOKUM.index('TYP');
               {!
               |? {? FAKS.STAT_REJ<>'A' & FAKS.WHERE<>'E'
                     & (od_daty<={? FAKS.SZ='Z' || FAKS.DO || FAKS.DW ?} | od_daty=date(0,0,0))
                     & ({? FAKS.SZ='Z' || FAKS.DO || FAKS.DW ?}<=do_daty | do_daty=date(0,0,0))
                     & FAKS.ZEST='T' & FAKS.ZEST_AKC='T'
                  || _bl_czy:=0;
                     DOKUM.prefix($FAKS.ref(),'D');
                     {? DOKUM.first()
                     || {!
                        |? {? DOKUM.BL='T'
                           || _bl_czy:=1
                           ?};
                           ~_bl_czy & DOKUM.next()
                        !}
                     ?};
                     {? FAKS.SZ='Z'
                     || _il_z+=1;
                        {? _bl_czy
                        || _ilb_z+=1
                        ?}
                     || _il_s+=1;
                        {? _bl_czy
                        || _ilb_s+=1
                        ?}
                     ?}
                  ?};
                  FAKS.next()
               !}
            ?};
            _fnames.next()
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max)
   !}
?};
OKR.cntx_pop; FAKS.cntx_pop; DOKUM.cntx_pop;

ROK_F.cntx_psh; EDOKUM.cntx_psh; DOKUM.cntx_psh;
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
_min:=od_daty~1;
_max:=do_daty~1;
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
{? ROK_F.first & (_min=0 | ROK_F.find_le(od_daty))
|| _fnames2:=tab_tmp(2,
                    'ROK','STRING[2]','Rok',
                    'NAZ','STRING[8]','Nazwa');
   _fn2:=EDOKUM.names();
   {? _fn2.first()
   || {!
      |? _fnames2.NAZ:=_fn2.NAME;
         _fnames2.ROK:=_fn2.NAME+2;
         _fnames2.add();
         _fn2.next()
      !}
   ?};
   {!
   |? _fnames2.prefix($(ROK_F.POCZ_ROK~1)+2,);
      {? _fnames2.first()
      || {!
         |? EDOKUM.use(_fnames2.NAZ);
            EDOKUM.index('DISP');
            EDOKUM.prefix();
            {? EDOKUM.first()
            || DOKUM.index('TYP');
               {!
               |? {? EDOKUM.ZAM<>'T'
                     & (od_daty<=EDOKUM.DOP | od_daty=date(0,0,0))
                     & (EDOKUM.DOP<=do_daty | do_daty=date(0,0,0))
                  || _bl_czy:=0;
                     DOKUM.prefix($EDOKUM.ref(),'D');
                     {? DOKUM.first()
                     || {!
                        |? {? DOKUM.BL='T'
                           || _bl_czy:=1
                           ?};
                           ~_bl_czy & DOKUM.next()
                        !}
                     ?};
                     _il_z+=1;
                     {? _bl_czy
                     || _ilb_z+=1
                     ?}
                  ?};
                  EDOKUM.next()
               !}
            ?};
            _fnames2.next()
         !}
      ?};
      ROK_F.next() & (_max=0 | ROK_F.POCZ_ROK<=do_daty)
   !}
?};
ROK_F.cntx_pop; EDOKUM.cntx_pop; DOKUM.cntx_pop;

ROK_F.cntx_psh; DOK.cntx_psh; DOKUM.cntx_psh;
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
_min:=od_daty~1;
_max:=do_daty~1;
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
{? ROK_F.first & (_min=0 | ROK_F.find_le(od_daty))
|| _fnames3:=tab_tmp(2,
                    'ROK','STRING[2]','Rok',
                    'NAZ','STRING[8]','Nazwa');
   _fn3:=DOK.names();
   {? _fn3.first()
   || {!
      |? _fnames3.NAZ:=_fn3.NAME;
         _fnames3.ROK:=2+(_fn3.NAME+4);
         _fnames3.add();
         _fn3.next()
      !}
   ?};
   {!
   |? _fnames3.prefix($(ROK_F.POCZ_ROK~1)+2,);
      {? _fnames3.first()
      || {!
         |? DOK.use(_fnames3.NAZ);
            DOK.index('O_REJ');
            DOK.prefix();
            {? DOK.first()
            || DOKUM.index('TYP');
               {!
               |? {? (od_daty<=DOK.DOP | od_daty=date(0,0,0))
                     & (DOK.DOP<=do_daty | do_daty=date(0,0,0))
                  || _bl_czy:=0;
                     DOKUM.prefix($DOK.ref(),'D');
                     {? DOKUM.first()
                     || {!
                        |? {? DOKUM.BL='T'
                           || _bl_czy:=1
                           ?};
                           ~_bl_czy & DOKUM.next()
                        !}
                     ?};
                     _il_z+=1;
                     {? _bl_czy
                     || _ilb_z+=1
                     ?}
                  ?};
                  DOK.next()
               !}
            ?};
            _fnames3.next()
         !}
      ?};
      ROK_F.next() & (_max=0 | ROK_F.POCZ_ROK<=do_daty)
   !}
?};
ROK_F.cntx_pop; DOK.cntx_pop; DOKUM.cntx_pop;

X_Xw1.blank(1);
X_Xw1.RODZ:='Sprzedaż - wychodzące';
X_Xw1.IL_BL:=_ilb_s;
X_Xw1.IL:=_il_s;
{? _il_s=0
|| X_Xw1.PROCENT:=0
|| X_Xw1.PROCENT:=100*_ilb_s/_il_s
?};
X_Xw1.add();
X_Xw1.blank(1);
X_Xw1.RODZ:='Zakup - przychodzące';
X_Xw1.IL_BL:=_ilb_z;
X_Xw1.IL:=_il_z;
{? _il_z=0
|| X_Xw1.PROCENT:=0
|| X_Xw1.PROCENT:=100*_ilb_z/_il_z
?};
X_Xw1.add();

PAR_WYDR.TITLE:='RAPORT WYKORZYSTANIA DOKUMENTÓW BUSINESSLINK';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{IF: od_daty$1+do_daty$1<>''}
   {<{lmt+1+40}}{'Od:'}{<{lmt+1+60}'Do:'}
   {IF: od_daty$1+do_daty$1<>''}{<{lmt+1}}{'Data wystawienia / otrzymania'}{<{lmt+1+40}od_daty}{<{lmt+1+60}do_daty}{FI}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; lp:=0;~~}
{FOR: X_Xw1}
{INDEX: X_Xw1.ndx_tmp(,,'RODZ',,)}
{DO}
   {
   w[1] :=form(X_Xw1.RODZ);
   w[2] :=form(X_Xw1.IL_BL);
   w[3] :=form(X_Xw1.IL);
   w[4] :=form(X_Xw1.PROCENT,,doklw,'.,')
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}  bez podsumowania                                             {COMMENT-}

:!UTF-8
{TITLE: Wydruk dokumentu kasowego - wpłata gotówkowa}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN: pierwszy:=1;
        adres1:=adres2:=''; dane:=0;
        PRN:=obj_new(@.CLASS.PRINT,1); PRN.sl_frame();
        PRN1:=obj_new(@.CLASS.PRINT,5); PRN1.sl_frame();
        PRN2:=obj_new(@.CLASS.PRINT,5); PRN2.sl_frame();
        PRN3:=obj_new(@.CLASS.PRINT,5); PRN3.sl_frame();
        w:=obj_new(11); {! _a:=1..11 |! w[_a]:='' !};
        color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=rsep:=0; vp:=1; separ:=ile_l_k:=licznik:=licznik1:=0;anul:=0;
        WYDRUKI.PAR1:='N'; WYDRUKI.PAR5:=0; WYDRUKI.PAR3:='N'; ob:='';
        obca:=0; countl1:=0; countl2:=1; lp:=1;
        {? MBWPL.RSQL<>''
        || DOKUMENT.use(form(8+MBWPL.RSQL));
           RAPORT.use('krp'+(form(8+MBWPL.RSQL)+5))
         ?}
      }
{END: VAR_DEL.delete('SD_OB','PRN','PRN1','PRN2','PRN3','w','konto','kwoty','kwotyw','pierwszy'); &adres1; &adres2; &dane;&anul;
      &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &separ; &ile_l_k; &licznik; &licznik1; &ob; &countl1; &countl2; &lp;
      DOKUMENT.use('kdo'+(5*'_'));
      RAPORT.use('krp'+(5*'_'))
      }
{INCLUDE: wsp_pw.rpi}
{MACRO: 'DANE'}
{ adres1:=adres2:='';
  adres1:='ul. '+MBWPL.KH().UL;
  adres2:={? MBWPL.KH().POCZ*MBWPL.KH().MIASTO
          || MBWPL.KH().POCZ
          || MBWPL.KH().MIASTO+{? +MBWPL.KH().POCZ || ', '+MBWPL.KH().POCZ  || '' ?}
          ?};
  adres2:=|adres2;~~}
{MACRO-}
{ rsep:=PAR_WYDR.RSEP; ~~}
{COMMENT}------------------------------TAB BEGIN-----------------------{COMMENT-}
{MACRO: 'TABBEGIN'}
{  ob:=w[1]; w[1]:='Potwierdzenie wpłaty gotówkowej nr %1/%2 z dnia %3'@[$MBWPL.NRPTW,$MBWPL.ROKPTW,$MBWPL.DATA];
   w[1]:=form(w[1],-95);PRN.setcolor(color);~~}
{REP: PRN.fhbar()}
{PRN.ini_line()}
{REP: PRN.fline()}
{w[1]:=ob;PRN.ini_line();~~}
{MACRO-}
{COMMENT}--------------------------------HEADER------------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{ PAR_WYDR.TITLE:=''; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABBEGIN'}
{HEADER-}
{ PRN.add("w[1]",115,'Potwierdzenie wpłaty gotówkowej nr %1/%2 z dnia %3'@[$MBWPL.NRPTW,$MBWPL.ROKPTW,$MBWPL.DATA]);
  _dl:=98;
  {? WYDRUKI.PAR3='T' || _dl-=36 ?};
  PRN1.add("w[2]",_dl,'Wpłata gotówkowa'@);
  PRN1.add("w[3]",16,'Winien kasa (%1)'@[KINFO.NAROD().KOD],1);
  PRN3.add("w[2]",_dl-17,'Wpłata tytułem'@);
  PRN3.add("w[3]",16,'Winien kasa (%1)'@[KINFO.NAROD().KOD],1);
  PRN3.add("w[5]",16,'Winien kasa w walucie (PLN)'@,1,'#');
  PRN2.add("w[7]",18,'',,'#');
  PRN2.add("w[8]",18,'',,'#');
  PRN2.add("w[9]",18,'',,'#');
  PRN2.add("w[10]",18,'',,'#');
  PRN2.add("w[11]",39,'',,'#');
~~}
{FONT: 'T8'}{SPACE: 'B'}
{COMMENT}==================================MACRO WYDRUK=================================={COMMENT-}
{MACRO: 'WYDRUK'}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8'}{SPACE: 'B'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}{ vp:=1;~~}
{FOOTER-}
{ countl2:=page();~~}
{COMMENT}----------------------------DANE ADRESOWE-----------------------{COMMENT-}
{SUBSTITUTE: 'DANE'}
{IF: lineleft()=1}{PAGE}{FI}
{ prn1:=prn2:='PRN'; vp:=1;~~}
{ w[1]:='Od kogo: %1 %2'@[MBWPL.KH().KOD,MBWPL.KH().NAZ];
  separ:=rsep; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{ countl1:=lineleft();~~}
{IF: +adres1}
{IF: ((+adres1)+(+adres2)+2)<=115}
{ w[1]:=adres1+'  '+adres2;~~}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
{ w[1]:=adres1;~~}
{SUBSTITUTE: 'LINIA0'}
{ w[1]:=adres2;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{FI}
{IF: +MBWPL.OPIS}{w[1]:='Opis: %1'@[MBWPL.OPIS];~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ rsep:=separ;~~}
{COMMENT}----------------------------KWOTY I KONTA-----------------------{COMMENT-}
{ENTIRE}
{IF: (obca & (lineleft()=1 | lineleft()=2)) | (~obca & lineleft()=1)}{ vp:=1;~~}{PAGE}{FI}
{IF: vp}{ PRN1.setcolor(color);PRN.setcolor(color);~~}{REP: PRN.fjbar({?obca||PRN3||PRN1?})}{ELSE}{PRN.jbar({?obca||PRN3||PRN1?})}{FI}{ PRN1.setcolor('255:255:255'); PRN.setcolor('255:255:255'); ~~}
{ _min:=0;
  w[2]:=((82-obca*19+(WYDRUKI.PAR3='N')*35)%2)*' '+'Wpłata tytułem';
  w[3]:='Winien kasa (%1)'@[INFO.NAROD().KOD]+(_min*2)*' ';
  w[5]:='Winien kasa w  # walucie (PLN)  '@;
  w[6]:=5*' '+'   Ma konto'@;~~}
{_f:='PRN'+{?obca||'3'||'1'?}+'.ini_line()'; ($_f)();~~}
{WHILE: _f:='PRN'+{?obca||'3'||'1'?}+'.next()'; ($_f)()}
{DO}
   {REP: _f:='PRN'+{?obca||'3'||'1'?}+'.fline()'; ($_f)()}
{OD}
{ENTIRE-}{ prn1:=prn2:='PRN'+{?obca||'3'||'1'?};~~}
{IF: lineleft()=1}{ vp:=1;~~}{PAGE}{ELSE}{ vp:=0;~~}{FI}
{IF: rsep=0 & vp=0 & lineleft()<>0}{{?obca||PRN3.bar()||PRN1.bar()?}}{FI}
{ _k:=''; w[2]:='Wpłata gotówkowa pobrana przez: '@+MBWPL.HAN().NAZ+{? MBWPL.SYM<>'' || ', zamówienie '@+MBWPL.SYM+'.' || '.' ?};
  w[3]:=form(MBWPL.WAR,,2,' ,'); w[4]:='PLN';
  w[5]:=form(MBWPL.WAR,,2,' ,'); w[6]:={? var_pres('konto')<=0 || w[6]:=_k || '' ?};
~~}{ {? lineleft()=0 || vp:=1 ?};~~}
{IF: rsep & lineleft()=1}{ vp:=1;~~}{PAGE}{FI}
{IF: (rsep & (lineleft()=0 | lineleft()=1)) | ~rsep & lineleft()=0}{ prn1:='PRN'; prn2:='PRN'+{?obca||'3'||'1'?};~~}{FI}
{SUBSTITUTE: 'LINIA02'}{ licznik:=1; separ:=rsep; rsep:=0; prn1:='PRN'+{?obca||'3'||'1'?}; vp:=0;~~}
{IF: ile_l_k>=1 & var_pres('konto')=type_of(obj_new(2))}
{WHILE: licznik<=ile_l_k}
{DO}
{ {! _a:=2..6 |! w[_a]:='' !};
  w[6]:=konto[licznik];
  w[3]:='';
  w[5]:='';
  licznik+=1; ~~}
{IF: lineleft()=0}{ prn1:='PRN'; prn2:='PRN'+{?obca||'3'||'1'?};~~}{FI}
{SUBSTITUTE: 'LINIA02'}{ prn1:='PRN1';~~}
{OD}
{FI}
{ rsep:=separ;~~}
{IF: lineleft()=1}{PAGE}{FI}{IF: lineleft()=0}{ vp:=1;~~}{FI}
{ separ:=rsep; rsep:=1; {! _a:=2..6 |! w[_a]:='' !};
  w[2]:='   RAZEM';
  w[3]:=form(MBWPL.WAR,,2,' ,');
  w[5]:=form(MBWPL.WAR,,2,' ,');
~~}
{IF: (lineleft()=0 | lineleft()=1)}{ prn1:='PRN'; prn2:='PRN'+{?obca||'3'||'1'?};~~}{FI}
{SUBSTITUTE: 'LINIA02'}
{COMMENT}----------------------------KWOTA SLOWNIE-----------------------{COMMENT-}
{ _slownie:=STR.kwota_sł(MBWPL.WAR);
  _gr:='';
  {? frac(MBWPL.WAR)>0
  ||  _gr:=$frac(MBWPL.WAR); {? +_gr=3 || _gr:=_gr+'0' || _gr ?};
      _gr:=2-_gr
  ?};
  _slownie:={? _slownie*'złotych'>0
            || (((_slownie*'złotych')-1)+_slownie)+' '
            |? _slownie*'złoty'>0
            || (((_slownie*'złoty')-1)+_slownie)+' '
            |? _slownie*'złote'>0
            || (((_slownie*'złote')-1)+_slownie)+' '
            || ''
            ?}+
            {? +_gr || _gr+'/100' || '' ?};
  STR.split(_slownie);
  w[1]:='Słownie (PLN):  '+STR.line(97);
  prn2:='PRN'; ~~}
{IF: lineleft()=1  | lineleft()=2}{ vp:=1;~~}{PAGE}{FI}{ prn1:={? lineleft=0 | lineleft()=1 || 'PRN' || 'PRN'+{?obca||'3'||'1'?} ?}; ~~}
{SUBSTITUTE: 'LINIA02'}{  rsep:=0; prn1:='PRN';~~}
{WHILE: STR.next()<>0}
{DO}
{  prn1:=prn2:='PRN';~~}
{ w[1]:=STR.line(115);~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{ rsep:=separ;~~}
{IF: lineleft<5 & lineleft()<>0}{ vp:=1;~~}{PAGE}{FI}
{FOOTER}{FOOTER-}
{ separ:=rsep; rsep:=1; prn1:='PRN'; prn2:='PRN2';~~}
{ w[7]:='       Wystawił'@; w[8]:='       Sprawdził'@; w[9]:='      Zatwierdził'@; w[10]:='     Raport kasowy'@; w[11]:=16*' '+'Kwotę powyższą'@;~~}
{SUBSTITUTE: 'LINIA02'}
{ prn1:='PRN2';w[7]:=w[8]:=w[9]:=''; rsep:=0;
  w[10]:={? MBWPL.RSQL<>''
         || '     Nr: '@+$exec('FindAndGet','#table','DOKUMENT',MBWPL.RSQL,,"RAPORT().NUM_RAP",0)
         || ''
         ?};
  w[11]:=20*' '+'otrzymałem'@;~~}
{SUBSTITUTE: 'LINIA0'}{ w[10]:={? MBWPL.RSQL<>''
                               || '     Poz.: '@+$exec('FindAndGet','#table','DOKUMENT',MBWPL.RSQL,,"LP",0)
                               || ''
                               ?};
                               w[11]:='';~~}
{SUBSTITUTE: 'LINIA0'}{ w[10]:='   ';~~}
{SUBSTITUTE: 'LINIA0'}
{PRN2.lbar()}{ rsep:=separ;~~}
{MACRO-}
{COMMENT}--------------------------------SRODEK------------------------------------{COMMENT-}
{MACRO: 'SRODEK'}
{ obca:=0; licznik1:=0;~~}
{WHILE: licznik1<=WYDRUKI.PAR5}
{DO}
{SUBSTITUTE: 'WYDRUK'}
{IF: WYDRUKI.PAR1='T'}
{IF: lineleft<>0 & licznik1<>WYDRUKI.PAR5}{PAGE}{FI}
{ELSE}
{IF: licznik1<>WYDRUKI.PAR5}
{IF: lineleft>=4}
{ vp:=1;~~}
{IF: {? countl2=page() || countl1:=countl1-lineleft()+5; countl1>lineleft() & lineleft()<>0 || 0  ?}}
{PAGE}
{ELSE}
{LINE: 1}
{FONT: 'T8B'}{SPACE: 'B'}{ prn1:='PRN';~~}
{SUBSTITUTE: 'TABBEGIN'}{FONT: 'T8'}{SPACE: 'B'}
{FI}
{ELIF: lineleft<>0}{PAGE}
{FI}
{FI}
{FI}
{ licznik1+=1;~~}
{OD}
{MACRO-}
{COMMENT}==================================WYDRUK=================================={COMMENT-}
{IF: var_pres('TT_DOK')>100 }
   {FOR: TT_DOK}
   {DO}
      {DOKUMENT.seek(TT_DOK.REF,DOKUMENT.name());~~}
      {SUBSTITUTE: 'SRODEK'}
      {PAGE}
   {OD}
{ELSE}
   {SUBSTITUTE: 'SRODEK'}
{FI}

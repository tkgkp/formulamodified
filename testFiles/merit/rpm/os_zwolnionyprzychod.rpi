:!UTF-8
{BEGIN:
   OS_ZWPOD.cntx_psh();
   VAR_DEL.delete('tresc','b_rat','lmt','color','prn1','repexp','cfg','dane','prn','tos','exit','lp');
   _perm:=exec('uprawnieniawrap','pkd','urzędach skarbowych'@,'PKD_DOS_PPUS','PKD_DOS_PRUS');
   {? +_perm
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_perm]);
      exit:=1
   || tresc:='os_zwpod';
      lmt:=lp:=0;
      b_rat:=1;
      color:=prn1:='';
      repexp:=rep_export();
      tos:=tab_tmp(1,'REF','INTEGER','#OSOBA.ref()');
      {? var_pres('OS_ZWPOD')>100
      || exit:=0;
         OS_ZWPOD.index('OS_ZWPOD');
         _P:=exec('wybierz_parses','pracownik','PKD').P;
         params_set('P',_P);
         exit:=(_P.size()=0)
      || FUN.emsg('Wydruk dostępny tylko w wersji z pełną aktualizacją.'@);
         exit:=1
      ?}
   ?};
   ~~
}
{END:
   OS_ZWPOD.cntx_pop();
   VAR_DEL.delete('tresc','b_rat','lmt','color','prn1','repexp','cfg','dane','prn','tos','exit','lp');
   ~~
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{EXIT:
   {? ~exit & params_get().P.first()
   || cfg:=tab_tmp(1,
         'ROK','INTEGER','Rok',
         'UWZ','STRING[1]','Uwzględniać przychód z wypłat'
      );
      cfg.blank();
      cfg.ROK:=date~1;
      cfg.UWZ:='T';

      _we:=cfg.mk_edit('Parametry wydruku'@,,'red_'+tresc);
      cfg.win_esep(_we,'Dane podstawowe');
      cfg.win_efld(_we,,'ROK',,,4,0,,'Rok'@,,'Rok wybrany do wydruku'@);
      cfg.win_efld(_we,,'UWZ',,,,,,'Przychód zwolniony'@,,
         'Uwzględniony przychód zwolniony od podatku z list płac i rachunków'@,
         'check-box','left_label=1,check_label="%1"'['Uwzględnij przychód z list płac i rachunków'@],"'T'","'N'"
      );
      cfg.efld_opt(_we,'mark=1',,'ROK');
      exec('ok_esc','#window',cfg,_we);
      cfg.win_edit(_we);
      ~cfg.edit("
         _tab:=cur_tab();
         {? ~_tab.ROK
         || __CHK.err_fld(_tab,'ROK')
         |? _tab.ROK<2019
         || FUN.emsg('Wydruk dostępny dla danych od 2019 roku.'@);
            'ROK'
         |? _tab.ROK>3000
         || FUN.emsg('Błędna wartość w polu \"Rok\".'@);
            'ROK'
         || ''
         ?}
      ")
   ?};
   exit
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? repexp || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {IF: page()=1}
      {<{lmt+1} 'Parametry sprawozdania'@}
      {<{lmt+1} 'Forma współpracy: %1'@[{? __F_ZATR.P='P' || 'Pracownicy'@ |? __F_ZATR.P='Z' || 'Zleceniobiorcy'@ ?}]}
      {<{lmt+1} 'Rok: %1'@[$cfg.ROK]}
      {LINE}
   {FI}
   {FONT: 'T8B'}
   {SPACE: 'B'}
   {SUBSTITUTE: 'TABHEAD'}
   {<1} {REP: prn.fbar(lmt)}
   { prn.setcolor('255:255:255'); ~~}
{HEADER-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FOOTER: 0}
   {<1} {REP: prn.flbar(lmt)}
{FOOTER-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{IF: ~repexp}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'linia'}
   {($(prn1+'.ini_line()'))(); ~~}
   {WHILE: ($(prn1+'.next()'))()}
   {DO}
      {<1}{REP: ($(prn1+'.fline(lmt)'))()}
   {OD}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{  PAR_WYDR.TITLE:='Osoby uprawnione do skorzystania ze zwolnienia przychodu od podatku'@;

   dane:=tab_tmp(5,
      'NAZWISKO','STRING[30]','Nazwisko',
      'PIERWSZE','STRING[20]','Imię',
      'PESEL','STRING[11]','PESEL',
      'RODZ','STRING[1]','Rodzaj',
      'D_ZO','DATE','Data złożenia oświadczenia',
      'D_OB','DATE','Data od kiedy obowiązuje oświadczenie',
      'OD','DATE','Data od kiedy obowiązuje zwolnienie',
      'DO','DATE','Data do kiedy obowiązuje zwolnienie',
      'LIMIT','REAL','Limit przychodu do zwolnienia od podatku',
      'PRZYCH','REAL','Przychód zwolniony od podatku u poprzednich pracodawców',
      'DEKL','STRING[26]','Złożone oświadczenie'
   );

   prn:=obj_new(@.CLASS.PRINT,{? repexp || 10 || 9 ?});
   prn.s_frame();
   {? repexp
   || prn.add("dane.NAZWISKO",30,'Nazwisko'@);
      prn.add("dane.PIERWSZE",20,'Imię'@);
      prn.add("dane.PESEL",11,'PESEL'@)
   || prn.add("lp+=1",5,'Lp.'@,1,,,,,',,\'9.\'');
      prn.add("dane.NAZWISKO+' '+dane.PIERWSZE+' '+dane.PESEL",50,'Osoba'@)
   ?};
   prn.add("dane.DEKL",26,'Oświadczenie'@);
   prn.add("{? dane.D_ZO=#0 || '' || dane.D_ZO$1 ?}",12,'Data złożenia oświadczenia'@);
   prn.add("{? dane.D_OB=#0 || '' || dane.D_OB$1 ?}",12,'Oświadczenie obowiąuje od'@);
   prn.add("{? dane.OD=#0 || '' || dane.OD$1 ?}",12,'Zwolnienie od'@);
   prn.add("{? dane.DO=#0 || '' || dane.DO$1 ?}",12,'Zwolnienie do'@);
   prn.add("dane.LIMIT",12,'Limit przychodu'@,1,,,,,',2,\'9,\'');
   prn.add("dane.PRZYCH",12,'Przychód wykorzystany'@,1,,,,,',2,\'9,\'');

   prn1:='prn';
   lmt:=int((charleft()-prn.width())/2);
   ~~
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: tresc}
   {P.OSOBA(); ~~}
   {IF:
      _ref:=#OSOBA.ref();
      {? tos.find_key(_ref)
      || 1
      || tos.REF:=_ref;
         tos.add();
         0
      ?}
   }
   {ELIF:
      exec('czytaj','#stalesys',date(cfg.ROK,12,0),KST,'ZWPOD','ZWPODW');
      dane.erase();

      _firma:=exec('ref_firma','ustawienia');
      _mc:=OSOBA.UR_DATA~2;
      _dn:=OSOBA.UR_DATA~3;
      _d_26:=date((OSOBA.UR_DATA~1)+KST.ZWPODW,_mc,{? _mc=2 & _dn=29 || 0 || _dn ?});
      _d0:=_d_zo:=_d_ob:=date(0,0,0);
      _przych:=0;
      _rezygn:='N';

      _przychod:=
         "  {? _e='N'|| return(0) ?};
            _przychod:=0;
            _rok:=_a~1;
            _start:=_a;
            _stop:=_b;
            _firma:=_c;
            _osoba:=_d;
            _RB:=__RUB.sys_rub(9022);

            LS.cntx_psh();
            O.cntx_psh();
            O.index('LISTYPLP');
            O.prefix(_firma,'P',_rok);
            {? O.find_ge(_start~2)
            || {!
               |? {? ~('S,W,Z'*O.T().T)
                  || LS.use(O.LT);
                     LS.index('OSOBAKOD');
                     {? _RB.first()
                     || {!
                        |? LS.prefix(_firma,_osoba,_RB.RN);
                           {? LS.first()
                           || {!
                              |? _przychod+=LS.KW;
                                 LS.next()
                              !}
                           ?};
                           _RB.next()
                        !}
                     ?}
                  ?};
                  O.next() & O.MP<=_stop~2
               !}
            ?};

            RH.cntx_psh();
            RH.index('RACHWYPL');
            RH.prefix(_firma,_osoba);
            {? RH.find_ge(_start)
            || {!
               |? {? RH.ZLE().RU().ZWPOD='T'
                  || LS.use(RH.O().LT);
                     LS.index('ZLEC');
                     {? _RB.first()
                     || {!
                        |? LS.prefix(RH.ref(),_RB.RN);
                           {? LS.first()
                           || {!
                              |? _przychod+=LS.KW;
                                 LS.next()
                              !}
                           ?};
                           _RB.next()
                        !}
                     ?}
                  ?};
                  RH.next() & RH.DWY<=_stop
               !}
            ?};
            RH.cntx_pop();
            O.cntx_pop();
            LS.cntx_pop();

            _przychod
         ";

      OS_ZWPOD.prefix(_firma,OSOBA.ref(),cfg.ROK);
      {? OS_ZWPOD.first()
      || _d_zo:=OS_ZWPOD.D_ZO;
         _przych:=OS_ZWPOD.PRZYCHOD;
         _d_ob:=OS_ZWPOD.D_OB;
         _rezygn:=OS_ZWPOD.REZYGN
      ?};

      dane.NAZWISKO:=OSOBA.NAZWISKO;
      dane.PIERWSZE:=OSOBA.PIERWSZE;
      dane.PESEL:=OSOBA.PESEL;
      dane.DEKL:=
         {? _d_zo=_d0
         || {? cfg.ROK<2020
            || 'Brak oświadczenia'@
            || 'Bez oświadczenia'@
            ?}
         || {? _rezygn='T'
            || 'Rezygnacja'@
            || 'Oświadczenie o przychodzie'@
            ?}
         ?};
      dane.D_ZO:={? _d_zo=_d0 || _d0 || _d_zo ?};
      dane.D_OB:={? _d_ob=_d0 || _d0 || _d_ob ?};
      {? _d_26>date(cfg.ROK,1,1)
      || {? cfg.ROK<2020
         || {? _d_ob=_d0
            || dane.OD:=dane.DO:=_d0;
               dane.LIMIT:=dane.PRZYCH:=0
            || dane.OD:=_d_ob;
               dane.DO:={? _d_26<date(cfg.ROK,12,0) || _d_26 || date(cfg.ROK,12,0) ?};
               dane.LIMIT:=KST.ZWPOD;
               dane.PRZYCH:=_przych+_przychod(dane.OD,dane.DO,_firma,OSOBA.ref(),cfg.UWZ)
            ?}
         || dane.OD:=date(cfg.ROK,1,1);
            dane.LIMIT:=KST.ZWPOD;
            {? _d_ob=_d0 | (_d_ob>_d0 & _rezygn='N')
            || dane.DO:={? _d_26<date(cfg.ROK,12,0) || _d_26 || date(cfg.ROK,12,0) ?};
               dane.PRZYCH:=_przych+_przychod(dane.OD,dane.DO,_firma,OSOBA.ref(),cfg.UWZ)
            || {? _d_ob>date(cfg.ROK,1,1)
               || dane.DO:=_d_ob-1;
                  dane.PRZYCH:=_przych+_przychod(dane.OD,dane.DO,_firma,OSOBA.ref(),cfg.UWZ)
               || dane.OD:=dane.DO:=_d0;
                  dane.PRZYCH:=dane.LIMIT:=_przych
               ?}
            ?}
         ?};
         dane.add()
      ?}
   }
      {SUBSTITUTE: 'linia'}
   {FI}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? repexp || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-prn.width())/2); ~~ }
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
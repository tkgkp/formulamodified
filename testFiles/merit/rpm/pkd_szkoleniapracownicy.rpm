:!UTF-8
{TITLE:Lista szkoleń pracowników}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='PracSzkol';
   PAR_WYDR.TITLE:='Szkolenia pracowników'@;
   P.cntx_psh();
   par:=obj_new(7);
   par[2]:=exec('par_wydr_ddp','pkd',
      PAR_WYDR.TITLE,
      "  {? cur_tab().OD<>#0 & cur_tab().DO<>#0
         || {? cur_tab().OD>cur_tab().DO || FUN.emsg('Niepoprawny zakres dat'); 'OD' || 1 ?}
         || 1
         ?}
      ",
      date(0,0,0),'Od daty'@,,
      date(0,0,0),'Do daty'@,,
      'Które szkolenia uwzględniać na wydruku?'@,'Wszystkie'@,'Z wybranego okresu'@,0,1
   );
   par[1]:=obj_new(@.CLASS.PRINT,6);
   par[1].add("SZK_PRAC.SZKOL().TEMAT().TEMAT",60,'Temat'@);
   par[1].add("SZK_OPIS.OD$1",10,'Od dnia'@);
   par[1].add("SZK_OPIS.DO$1",10,'Do dnia'@);
   par[1].add("SZK_PRAC.KOSZT* {? SZK_PRAC.KOSZT<=1 || SZK_PRAC.SZKOL().SUMA || 1 ?}",12,'Potrącenie'@,1,,,,,'12,2');
   par[1].add(
      "  SZK_PRAC.SZKOL();
         {? SZK_OPIS.ZAKW_U='T' || SZK_OPIS.ZAKW || {? SZK_OPIS.LU || SZK_OPIS.ZAKW/SZK_OPIS.LU ?} ?}+
         {? SZK_OPIS.DYD_U='T'  || SZK_OPIS.DYD  || {? SZK_OPIS.LU || SZK_OPIS.DYD/SZK_OPIS.LU ?} ?}+
         {? SZK_OPIS.WYZ_U='T'  || SZK_OPIS.WYZ  || {? SZK_OPIS.LU || SZK_OPIS.WYZ/SZK_OPIS.LU ?} ?}+
         {? SZK_OPIS.DEL_U='T'  || SZK_OPIS.DEL  || {? SZK_OPIS.LU || SZK_OPIS.DEL/SZK_OPIS.LU ?} ?}
      ",12,'Koszt'@,1,,,,,'12,2'
   );
   par[1].s_frame();
   prn1:='par[1]';
   lmt:=rsep:=par[5]:=par[7]:=0;
   vp:=par[6]:=b_rat:=h_rat:=1;
   params_set('P',exec('wybierz_parses','pracownik','PKD').P)
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   P.cntx_pop()
}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: szk_prac.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[6]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/h_rat; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[6]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: od %1 do %2'@[par[2].OD$1,par[2].DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{<{ceil(lmt*b_rat)+1}}{form(par[7]+=1,,,'9.')} {INCLUDE: p_prac.rpi}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[5]:=1; ~~ }
{MACRO-}
{MACRO: 'szk_dane'}
{IF: ~par[5]}
   {SUBSTITUTE: 'pracownik'}
   {REP: par[1].fhbar(lmt)}
{FI}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{MACRO: tresc}
{ENTIRE}
{ par[5]:=0; ~~ }
{FOR: SZK_PRAC}{INDEX: 'PRAC_DAT'}{PREFIX: P.ref}{DO}
   {IF: {? par[2].OD<>#0 || par[2].OD<=SZK_PRAC.SZKOL().OD || 1 ?}
        &
        {? par[2].DO<>#0 || SZK_PRAC.SZKOL().DO<=par[2].DO || 1 ?}}
      {SUBSTITUTE: 'szk_dane'}
   {FI}
{OD}
{IF: par[5]}
   {REP: par[1].flbar(lmt)}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/=charleft(); ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
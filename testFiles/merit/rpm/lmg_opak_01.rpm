:!UTF-8
{TITLE:A. Niezwrócone kaucje pobrane od kontrahentów}
{PRINTER: exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
  PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,,,'enable'}
{BEGIN:

   {? var_pres('PRN')>100  || obj_del(PRN)  ?}; PRN:=obj_new(@.CLASS.PRINT,5);  PRN.s_frame();
   {? var_pres('PRNS')>100 || obj_del(PRNS) ?}; PRNS:=obj_new(@.CLASS.PRINT,2); PRNS.s_frame();

   {? var_pres('w')>100    || obj_del(w)    ?}; w:=obj_new(2);

   'zmienne pomocnicze do wydrukow graficznych';
   color:='';
   prn1:='PRN'; prn2:='PRNS';
   lm:=ll:=vp:=lmt:=0; rsep:=PAR_WYDR.RSEP;
   tryb:=rep_export();

   VAR_DEL.delete('__kh');

   PAR_WYDR.TITLE:='Lista kontrahentów'@
;~~}
{END:
   VAR_DEL.delete('PRN','PRNS','w','__kh');

   &color; &prn1; &prn2; &tryb;
   &lm; &ll; &vp; &lmt; &rsep
;~~}
{
   {? tryb<>1 || PRN.add("w[1]",3,'Lp.'@,1) ?};
   PRN.add("__kh.KOD",5,'Kod'@);
   PRN.add("__kh.SKR",10,'Skrót'@);
   PRN.add("__kh.NAZ",50,'Nazwa'@);
   PRN.add("form(__kh.WAR,,2)",14,'Wartość kaucji'@,1);

   PRNS.add("w[1]",77,,1);
   PRNS.add("form(w[2],,2)",14,,1)
;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ w[1]:=w[2]:=0; ~~}
{
   _q_kh:=$"
      select
         KH.KOD as KOD,
         KH.SKR as SKR,
         KH.NAZ as NAZ,
         OPAK_P.WAR as WAR,
         OPAK_P.reference as OPKP_REF
      from
         @OPAK_P
         join @OPAK_N using (OPAK_P.OPAK_N,OPAK_N.reference)
         join KH using (OPAK_N.KH,KH.reference)
      where
         OPAK_N.AKC='T'
         and OPAK_N.PLUS='N'
         and OPAK_P.CZY_KAUC='T'
         and OPAK_P.ZWROT='N'
      order by
         1
   ";
   _kh:=sql(_q_kh);
   {? _kh.first()
   ||
      OPAK_N.cntx_psh();
      OPAK_P.cntx_psh();
      OPAK_P.clear();
      {!
      |?
         _il_zwr:=0;
         OPAK_N.use('opakn'+((8+_kh.OPKP_REF)+3));
         OPAK_P.use(8+_kh.OPKP_REF);
         {? OPAK_P.seek(_kh.OPKP_REF)
         || _pow:=exec('opow_mk','opakow',0,0); _il_zwr:=_pow[1]; &_pow
         ?};
         _kh.WAR-=_il_zwr;
         _kh.put();
         _kh.next()
      !};
      OPAK_P.cntx_pop();
      OPAK_N.cntx_pop();

      _q__kh:=$"
         select
            :_a.KOD as KOD,
            :_a.SKR as SKR,
            :_a.NAZ as NAZ,
            sum(:_a.WAR) as WAR
         from
            :_a
         group by
            :_a.KOD,
            :_a.SKR,
            :_a.NAZ
         order by
            1
      ";
      __kh:=sql(_q__kh,_kh);
      {? __kh.first()
      ||
         {!
         |?
            {? __kh.WAR<=0
            ||
               __kh.del()
            ||
               w[2]+=__kh.WAR;
               __kh.next()
            ?}
         !}
      ?}
   ?};
   ~~
}
{IF: var_pres('__kh')>100}
{FOR: '__kh'}
{DO}
{  w[1]+=1; ~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{FI}
{IF: tryb<>1}
{  w[1]:='Suma'@; rsep:=1; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA02'}
{FOOTER: 0}
{prn1:='PRNS';~~}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{FI}
:!UTF-8
{TITLE:Karta zasiłkowa}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('NextPage','DataCiag','Okres','miesiac','Rodzaj','__okredit','__edit','__param');
   VAR_DEL.delete('Choroba','formula','ZasMies','NrListy','DataWypl','PozListy','__kol_16');
   VAR_DEL.delete('ZasOkr','SumZar','tz_ndx','not_all','ok','tresc','__KLP','__tab_o','__KDCH');
   VAR_DEL.delete('n_wynikN','n_wynikL','n_wynikM','wynik','__ndx_kl','__ndx_kp','__Ed','__Con','__N_ind');
   tresc:='kartzas';
   __Ed:=0;
   __Con:=1;
   PAR_WYDR.TITLE:='Karta zasiłkowa'@;
   _upr:=exec('uprawnieniawrap','pkd',
              'Przeglądanie nieobecności'@,
              'PKD_EZK_OPNN',
              'PKD_EZK_ORNN'
             )+
         exec('uprawnieniawrap','pkd',
              'Naliczonych składnikach płacowych'@,
              'PPL_PLL_PSKP',
              'PPL_PLL_RSKP'
             );
   {? +_upr
   || __Con:=0;
      FUN.emsg('Brak wymaganych uprawnień pozwalających na przeglądane informacji o: %1'@[_upr])
   || __param:=tab_tmp(1,
             'ALL','INTEGER','Wykaz zasiłków rozliczonych listami tylko z wybranego roku',
             'ROK','INTEGER','Rok');
      __param.ROK:=O.R; __param.ALL:=0;
      __param.add();
      __okredit:="{? _a
                  || __param.fld_fml('ROK','BEFORE_EDIT',\"1\");
                     __param.efld_opt(__edit,'mark=1,enable='+$_a,,'ROK');
                     __param.ROK:=date()~1
                  || __param.fld_fml('ROK','BEFORE_EDIT',\"0\");
                     __param.efld_opt(__edit,'enable='+$_a,,'ROK'); 1
                  ?}";

      __edit:=__param.mk_edit('%1'[PAR_WYDR.TITLE],0,'ppl_pkartzas');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'ALL',,,,,,'Zasiłki rozliczone listami'@,,,'radio-buttons',,'Wszystkie lata'@,"0",
                                                                                           'Tylko z wybranego roku'@,"1");
      __okredit(0);
      __param.fld_fml('ALL','AFTER_EDIT',"__okredit(__param.ALL)");

      __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok karty zasiłkowej'@);
      __param.efld_opt(__edit,'enable='+$__param.ALL,,'ROK');
      __param.efld_opt(__edit,'mark=1',,'ROK');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? __Ed:=__param.edit("{? __param.ALL
                             || {? __param.ROK<=1900
                                || FUN.emsg('Błędna wartość parametru wydruku.'@); return('ROK')
                                || 1
                                ?}
                             ?};1
                            ")
      || NextPage:=0;
         NrListy:=PozListy:='';
         DataWypl:=date(0,0,0);

         Rodzaj:="{? __RUB.sys_attr(_a,12211,_b)
               || ' Chorobowy  '
               |? __RUB.sys_attr(_a,12212,_b)
               || ' Szpitalny  '
               |? __RUB.sys_attr(_a,12213,_b)
               || ' Wypadkowy  '
               |? __RUB.sys_attr(_a,1222,_b)
               || 'Rehabilitac.'
               |? __RUB.sys_attr(_a,12251,_b)
               || 'Opieka < 14 '
               |? __RUB.sys_attr(_a,12253,_b)
               || 'Opieka < 8 '
               |? __RUB.sys_attr(_a,12252,_b)
               || 'Opieka inna '
               |? __RUB.sys_attr(_a,1227,_b)
               || 'Macierzyński'
               || ''
               ?}";

         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?};
      ZasOkr:=SumZar:=ZasMies:='';
      DataCiag:=date(0,0,0);
      Okres:=0;
      VAR_DEL.delete('__kol_16');
      __kol_16:=obj_new(4);
      {! _ind:=1..4 |! __kol_16[_ind]:=obj_new(2) !};
      not_all:=__param.ALL;
      N.cntx_psh();
      __N_ind:=N.ndx_tmp('',,'P',,,'KOR',,1,'OD',,);
      Choroba:='__RUB.sys_attr(_a,1222,_b) | __RUB.sys_attr(_a,12212,_b) | __RUB.sys_attr(_a,1227,_b) | '+
               '__RUB.sys_attr(_a,1225,_b) | (\',';
        KST_CHOR.cntx_psh;
        KST_CHOR.index('KST_CHOR');
        KST_CHOR.clear;
        {? KST_CHOR.first ||
           {! |? Choroba+=$KST_CHOR.WYN().RN+','+
                          $KST_CHOR.ZAS().RN+',';
                 KST_CHOR.next
           !}
        ?};
      Choroba+='\'*(\',\'+$_a+\',\'))>0';
      Choroba:=$Choroba;
      miesiac:="
      {? _a=1  ||'I,'
      |? _a=2  ||'II,'
      |? _a=3  ||'III,'
      |? _a=4  ||'IV,'
      |? _a=5  ||'V,'
      |? _a=6  ||'VI,'
      |? _a=7  ||'VII,'
      |? _a=8  ||'VIII,'
      |? _a=9  ||'IX,'
      |? _a=10 ||'X,'
      |? _a=11 ||'XI,'
         ||'XII,'
      ?}
      ";
      NW.cntx_psh;
      NW.index('CHW');
      {? var_pres('__KLP')>100 || obj_del(__KLP) ?};
      __KLP:=tab_tmp(6,
        'LISTA','STRING['+$MS.fld_len('N','LT')+']','Lista płac',
        'WYDZIAL','STRING['+$MS.fld_len('UD_SKL','SYMBOL')+']','Jednostka org.',
        'ZA','STRING['+$MS.fld_len('P','ZA')+']','Zatrudniony?',
        'NAZWISKO','STRING['+$MS.fld_len('OSOBA','NAZWISKO')+']','Nazwisko',
        'PIERWSZE','STRING['+$MS.fld_len('OSOBA','PIERWSZE')+']','Imię',
        'PESEL','STRING['+$MS.fld_len('OSOBA','PESEL')+']','PESEL',
        'NR','INTEGER','Numer',
        'D','DATE','Data wypłaty',
        'RU','INTEGER','Rok ubezpieczeniowy',
        'POZ','INTEGER','Pozycja',
        'REFP','INTEGER','Pracownik'
      );
      __ndx_kl:=__KLP.index('?');
      __ndx_kp:=__KLP.ndx_tmp('Prac',,'REFP',,0,'LISTA',,0);
      VAR_DEL.delete('__tab_o');
      {? not_all
      || __tab_o:=sql(
            'select O.REFERENCE O_REF, upper(O.LT) LT, O.RU RU '
            'from O '
            'where O.FIRMA=:_a and O.F_ZATR=\':_b\' and O.RU=:_c',
            exec('ref_firma','ustawienia'),
            __F_ZATR.REF,
            __param.ROK
         )
      || __tab_o:=sql(
            'select O.REFERENCE O_REF, upper(O.LT) LT, O.RU RU '
            'from O '
            'where O.FIRMA=:_a and O.F_ZATR=\':_b\' ',
            exec('ref_firma','ustawienia'),
            __F_ZATR.REF
         )
      ?};
      LS.cntx_psh;
      O.cntx_psh; O.prefix();
      P.cntx_psh;
      H.cntx_psh; H.index('_HISTKOD');
      formula:="
       __KLP.prefix();
       {? __KLP.find_key(~-O.LT) || return ?};
       LS.use(O.LT);
       LS.index('PRACNRRU');
       LS.prefix();
       {? ~LS.first || return ?};
       _tab_p:=sql(
             'select distinct LS.P REF '
             'from LS join P using (LS.P,P.REFERENCE) '
             'where P.FIRMA=:_a',
             exec('ref_firma','ustawienia'));
       {? ~_tab_p.first() || return ?};
       P.cntx_psh;
       H.cntx_psh;
        __KLP.blank;
        __KLP.NR:=O.N;
        __KLP.D:=O.D;
        __KLP.RU:=O.RU;
        __KLP.LISTA:=~-O.LT;
        P.prefix();
        H.index('_HISTKOD');
        {!
        |? {? P.seek(_tab_p.REF)
           || LS.prefix(P.ref);
              {? LS.first ||
                 H.prefix(P.ref,'Z');
                 {? H.find_le(date(O.R,O.M,0))
                    || __KLP.WYDZIAL:=H.WYDZIAL().SYMBOL;
                       __KLP.ZA:={? H.DO=date(0,0,0) || 'T'
                                 |? date(O.R,O.M,0)<=H.DO || 'T'
                                 || 'N'
                                 ?}
                    || __KLP.WYDZIAL:=P.WYDZIAL().SYMBOL;
                       __KLP.ZA:=P.ZA
                 ?};
                 __KLP.NAZWISKO:=P.OSOBA().NAZWISKO;
                 __KLP.PIERWSZE:=OSOBA.PIERWSZE;
                 __KLP.PESEL:=OSOBA.PESEL;
                 __KLP.REFP:=P.ref;
                 __KLP.add
              ?}
           ?};
           _tab_p.next
        !};
        __KLP.prefix(__KLP.LISTA);
        {? __KLP.first ||
           _poz:=0;
           {! |? _poz+=1;
                 __KLP.POZ:=_poz;
                 __KLP.put;
                 __KLP.next
           !}
        ?};
        __KLP.clear;
        obj_del(_tab_p);
        P.cntx_pop();
        H.cntx_pop()
      ";

      ok:=1;
      LSW.index('ZLT');
      {? var_pres('wynik')>100 || obj_del(wynik) ?};
      wynik:=tab_tmp(1,
         'OD','DATE','OD',
         'DT','DATE','DT',
         'DO','DATE','DO',
         'MAC','INTEGER','Macierzyński',
         'OKRES','INTEGER','Okres',
         'NR','STRING[3]','NR',
         'S3','STRING[4]','S3',
         'S5','STRING[10]','S5',
         'S6','STRING[3]','S6',
         'S7','STRING[15]','S7',
         'S8','STRING[15]','S8',
         'S12','STRING[12]','S12',
         'S13','STRING[9]','S13',
         'S14','STRING[21]','S14',
         'S15','STRING[15]','S15',
         'S16','STRING[14]','S16',
         'S17','STRING[30]','S17',
         'S18','STRING[21]','S18',
         'S19','STRING[12]','S19',
         'S20','STRING[12]','S20'
      );
      tz_ndx:=TZ.ndx_tmp(,0,'N',,,'M',,);
      n_wynikN:=wynik.index('?');
      n_wynikL:=wynik.ndx_tmp(,0,'DT',,,'OD',,);
      n_wynikM:=wynik.ndx_tmp(,0,'MAC',,,'DO',,)
   ?}
}
{END:
   prgs_clr;
   {? __Con
   || H.cntx_pop; P.cntx_pop; LS.cntx_pop; KST_CHOR.cntx_pop; O.cntx_pop; NW.cntx_pop;TZ.ndx_drop(tz_ndx);
      N.ndx_drop(__N_ind); N.cntx_pop()
   ?};
   VAR_DEL.delete('NextPage','DataCiag','Okres','miesiac','Rodzaj','__okredit','__edit','__param');
   VAR_DEL.delete('Choroba','formula','ZasMies','NrListy','DataWypl','PozListy','__kol_16');
   VAR_DEL.delete('ZasOkr','SumZar','not_all','ok','tresc','__Ed','__Con','__KDCH');
   VAR_DEL.delete('n_wynikN','n_wynikL','n_wynikM','wynik','tz_ndx','__ndx_kl','__ndx_kp','__KLP','__tab_o','__N_ind')
}
{EXIT: ~__Con}
{EXIT: ~__Ed}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: pkartzas.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{HEADER}
{SPACE: 'c'}
┌───────────────────────────────────────────────────────────────────────────────────────────┐
│ K A R T A   Z A S I Ł K O W A                              Poprzednie ubezp. chor. ustało │
│                                                            dnia ......................... │
│                                                                                           │
│ {form(P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE,46)} {OSOBA.UR_DATA}  .............................. │
│ Nazwisko i imię ubezp.                     Data urodzenia  Data                    podpis │
│ PESEL {form(OSOBA.PESEL,11)}  NIP {form(OSOBA.NIP,13)} {>93}│
│ Wym. zatrudniony - objęty ubezpieczeniem chorobowym od {P.DZA} zwolniony - wyłączony z │
│ ubezp. dn. {{? P.DZ<>date(0,0,0) || P.DZ || '' ?}}{>93}│
│ Uwagi ................................................................................... │
│ ......................................................................................... │
└───────────────────────────────────────────────────────────────────────────────────────────┘

┌───────────────────────┬───────────────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬────────────┬───────────┬─────────────────────────────────────────┬────────────────┐
│      Lista wypłat     │Okres niezdolności do pracy│                 │                 │                 │      Kwota      │                 │            │           │        Wynagrodzenie lub przychód       │                │
│                       │                           │ Dzienny zasiłek │     Zasiłek     │     Przypis     │   potrąconej    │     Zasiłek     │  Rodzaj    │    Kod    │      przyjęty do obliczenia zasiłku     │ Z okresu zasił-│
│                       │                           │      zł,gr      │     (brutto)    │     podatku     │   zaliczki na   │     (netto)     │   (i%)     │ literowy  │                                         │  kowego wypła- │
├─────┬──────────┬──────┼──────────┬──────────┬─────┤                 │      zł,gr      │      zł,gr      │     podatek     │      zł,gr      │  zasiłku   │           ├───────────────────────┬─────────────────┤   cono za dni  │
│  Nr │   Data   │ Poz. │    od    │    do    │ dni │                 │                 │                 │      zł,gr      │                 │            │           │      za miesiące      │    przeciętny   │                │
├─────┼──────────┼──────┼──────────┼──────────┼─────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼────────────┼───────────┼───────────────────────┼─────────────────┼────────────────┤
│  1  │     2    │   3  │     4    │     5    │  6  │        7        │        8        │        9        │        10       │        11       │     12     │     13    │          14           │        15       │       16       │
{HEADER-}
{FOOTER: 0}
└─────┴──────────┴──────┴──────────┴──────────┴─────┴─────────────────┴─────────────────┴─────────────────┴─────────────────┴─────────────────┴────────────┴───────────┴───────────────────────┴─────────────────┴────────────────┘
{FOOTER-}
{MARGIN: 0}
{FOR: params_get().P}
{DO}
{IF: P.seek(params_get().P.SQL,,1)}
{ DataCiag:=date(0,0,0);
  {! _ind:=1..4 |! __kol_16[_ind][1]:=0; __kol_16[_ind][2]:=date(0,0,0) !};
  Okres:=0; wynik.index(n_wynikN);

  _tab_no:=sql('select :_a.O_REF REF, :_a.RU
      from N join :_a using (:_a.LT, N.LT) join R
      where N.P=\':_b\' and R.RN in (:_c)
      order by 2
      union all
      select :_a.O_REF REF, :_a.RU
      from LSW join :_a using (:_a.LT, LSW.LT)
      where LSW.P=\':_b\' and LSW.Z in(\'N\',\'H\')
      order by 2',
      __tab_o,$P.ref,__RUB.sys_sql(121)+','+__RUB.sys_sql(122));
  __KLP.index(__ndx_kl);
  progress(,'Trwa realizacja raportu...'@,'Proszę czekać'@,1);
  {? not_all
  || _tab_no.prefix(__param.ROK);
     {? _tab_no.first
     || {!|? {? O.seek(BIT.sqlint(_tab_no.REF),) || formula() ?}; _tab_no.next !}
     ?}
  || {? _tab_no.first
     || {! |? {? O.seek(BIT.sqlint(_tab_no.REF),) || formula() ?}; _tab_no.next() !}
     ?}
  ?};
  __KLP.index(__ndx_kp);~~
}
{FOR: 'N'}{INDEX: __N_ind}{PREFIX: P.ref()}
{DO}
{FIRST}{ __KLP.prefix(P.ref); __KDCH:=N.KDCH().KOD; ~~ }{FIRST-}
{IF: __RUB.sys_attr(N.NB,121,N.OD) | __RUB.sys_attr(N.NB,122,N.OD)}
{  NrListy:=3*' ';
   PozListy:=4*' ';
   DataWypl:=date(0,0,0);
   _ru:=0;
   {? +N.LT & __KLP.find_key(N.LT) ||
      NrListy:=form(__KLP.NR,3);
      {? __KLP.D<>date(0,0,0) ||
         DataWypl:=__KLP.D
      ?};
      _ru:=__KLP.RU;
      PozListy:=form(__KLP.POZ,4,,'99')
   ?};
   _mac:=0;
   N.cntx_psh;
   NW.prefix(N.ref);
   _nwsk:={? NW.first || TZ.use('tabz'+(4+$NW.CHW().OD)); N.ref || null ?};
   N.cntx_pop;
   TZ.index(tz_ndx);
   TZ.prefix(_nwsk);
   {? TZ.first()
      || ZasOkr:='   '+($(TZ.M$2))-3;
         SumZar:=0;
         ZasMies:='';
         _m_rob:=date(0,0,0);
         {!
         |? {? TZ.W<>'N'
            || {? _m_rob<>TZ.M || ZasMies+=miesiac(TZ.M~2) ?};
               _m_rob:=TZ.M
            ?};
            TZ.next()
         !};
         ZasOkr+=('-'+$(TZ.M$2))-3;
         ZasOkr+='   ';
         SumZar:=N.POD+N.POZ;
         SumZar:=form(SumZar,15,2)
      || ZasOkr:=21*' '; ZasMies:=''; SumZar:=15*' '
   ?};
   {? not_all
      || {? _ru=__param.ROK | (DataWypl=date(0,0,0) & N.OD~1=__param.ROK & N.LT='') || ok:=1 || ok:=0 ?}
      || ok:=1
   ?};
   {? __RUB.sys_attr(N.NB,121,N.OD) | __RUB.sys_attr(N.NB,1221,N.OD)
   || {? {? N.OD<date(2022,1,1) & exec('sprawdz_2022','nieobecnosc',N.ref())
         || N.OD-DataCiag=1 | (N.OD-DataCiag<62 & N.KDCH().KOD*'A')
         || N.OD-DataCiag<62 & ~(N.KDCH().KOD*'B' & __KDCH<>N.KDCH().KOD)
         ?}
         | DataCiag=date(0,0,0)
      || Okres+=N.NK
      || Okres:=N.NK
      ?}
   |? __RUB.sys_attr(N.NB,12251,N.OD)
   || {? N.OD~1=__kol_16[1][2]~1
      || __kol_16[1][1]+=N.NK
      || __kol_16[1][1]:=N.NK
      ?}
   |? __RUB.sys_attr(N.NB,12252,N.OD)
   || {? N.OD~1=__kol_16[2][2]~1
      || __kol_16[2][1]+=N.NK
      || __kol_16[2][1]:=N.NK
      ?}
   |? __RUB.sys_attr(N.NB,1227,N.OD)
   || {? N.KOR='N' || _mac:=1 ?};
      {? N.OD-1=__kol_16[3][2]
      || __kol_16[3][1]+=N.NK
      || __kol_16[3][1]:=N.NK
      ?}
   |? __RUB.sys_attr(N.NB,12253,N.OD)
   || {? N.OD~1=__kol_16[4][2]~1
      || __kol_16[4][1]+=N.NK
      || __kol_16[4][1]:=N.NK
      ?}
   ?};
   __KDCH:=N.KDCH().KOD;
   {? N.KOR='N'
   || {? __RUB.sys_attr(N.NB,12251,N.OD)
      || __kol_16[1][2]:=N.DO
      |? __RUB.sys_attr(N.NB,12252,N.OD)
      || __kol_16[2][2]:=N.DO
      |? __RUB.sys_attr(N.NB,1227,N.OD)
      || __kol_16[3][2]:=N.DO
      |? __RUB.sys_attr(N.NB,12253,N.OD)
      || __kol_16[4][2]:=N.DO
      || DataCiag:=N.DO
      ?}
   ?};
   {? ok
   || wynik.blank;
      wynik.MAC:=_mac;
      {? Choroba(N.NB().RN,N.OD)
      || wynik.S20:={? N.RODZAJ='R' || 'Skorygowany ' |? N.RODZAJ='W' || wynik.S20:='  Wycofany  '|| '' ?};
         wynik.NR:=NrListy; wynik.DT:=DataWypl; wynik.S3:=PozListy;
         wynik.OD:=N.OD; wynik.DO:=N.DO; wynik.S5:=N.DO$2; wynik.S6:=form(N.NK,3); wynik.S7:=form(N.STD,15,2);
         wynik.S8:=form((N.NK-N.DN-N.DK-N.OP*0.25)*N.STD,15,2); wynik.S12:=(('    ')+$N.PROC+'    ')+12;
         wynik.S13:=form(N.KDCH().KOD,9); wynik.S19:=Rodzaj(N.NB().RN,N.OD);
         {? ZasMies<>'' ||
            {! |?
               {? +wynik.S17<=21
                  || _czesc:=(ZasMies*','); _czesc_wyn:=_czesc+ZasMies; wynik.S17+=_czesc_wyn;
                     ZasMies:=_czesc-ZasMies; +ZasMies
                  || wynik.S17-=_czesc; ZasMies:=_czesc_wyn+ZasMies; 0
               ?}
            !};
            {? +ZasMies
               || wynik.S18:=ZasMies-1
               || wynik.S17:=wynik.S17-1; wynik.S18:=ZasMies-1
            ?}
         ?};
         wynik.S14:=ZasOkr; wynik.S15:=SumZar;
         wynik.S16:=
            {? __RUB.sys_attr(N.NB,1222,N.OD)
            || Okres:=0; 14*' '
            || {? __RUB.sys_attr(N.NB,12251,N.OD)
               || form(__kol_16[1][1],14)
               |? __RUB.sys_attr(N.NB,12252,N.OD)
               || form(__kol_16[2][1],14)
               |? __RUB.sys_attr(N.NB,12253,N.OD)
               || form(__kol_16[4][1],14)
               |? __RUB.sys_attr(N.NB,1227,N.OD)
               || {? _mac || wynik.OKRES:=__kol_16[3][1] ?};
                  form(__kol_16[3][1],14)
               || form(Okres,14)
               ?}
            ?};
         {? wynik.DT=date(0,0,0) || wynik.DT:=date(9999,12,31) ?};
         wynik.add();
         {? N.RODZAJ='W' | N.RODZAJ='R'
         || wynik.S8:=form('-'+|wynik.S8,-15);
            wynik.S15:=form('-'+|wynik.S15,-15); wynik.S16:=form(N.NK*-1,14);
            {? N.RODZAJ='W'
            || {? __KLP.find_key(~-N.O().LT)
               || wynik.NR:=form(__KLP.NR,3);
                  {? __KLP.D<>date(0,0,0) || wynik.DT:=__KLP.D ?};
                  wynik.S3:=form(__KLP.POZ,4,,'99')
               ?};
               wynik.S20:='  Wycofanie '
            || N.cntx_psh();
               N.index('NIEOTREE');
               N.prefix(#N.ref);
               {? N.first & N.LT<>'' & __KLP.find_key(N.LT)
               || wynik.NR:=form(__KLP.NR,3);
                  {? __KLP.D<>date(0,0,0) || wynik.DT:=__KLP.D ?};
                  wynik.S3:=form(__KLP.POZ,4,,'99')
               ?};
               N.cntx_pop();
               wynik.S20:='  Korekta   '
            ?};
            {? __RUB.sys_attr(N.NB,12251,N.OD)
            || {? __kol_16[1][1]>0 || __kol_16[1][1]-=N.NK ?}
            |? __RUB.sys_attr(N.NB,12252,N.OD)
            || {? __kol_16[2][1]>0 || __kol_16[2][1]-=N.NK ?}
            |? __RUB.sys_attr(N.NB,12253,N.OD)
            || {? __kol_16[4][1]>0 || __kol_16[4][1]-=N.NK ?}
            |? __RUB.sys_attr(N.NB,1227,N.OD)
            || {? __kol_16[3][1]>0 || __kol_16[3][1]-=N.NK ?}
            || {? Okres>0 || Okres-=N.NK ?}
            ?};

            {? wynik.DT=date(0,0,0) || wynik.DT:=date(9999,12,31) ?};
            wynik.add()
         ?}
      || wynik.NR:=NrListy; wynik.DT:=DataWypl; wynik.S3:=PozListy; wynik.OD:=N.OD;
         wynik.DO:=N.DO; wynik.S5:=N.DO$2; wynik.S6:=form(N.NK,3); wynik.S16:=form(Okres,14);
         wynik.MAC:=_mac;
         {? _mac || wynik.OKRES:=Okres ?};
         {? wynik.DT=date(0,0,0) || wynik.DT:=date(9999,12,31) ?};
         wynik.add()
      ?}
   ?};
~~}
{FI}
{OD}
{  LSW.prefix('N',P.ref);
   __KLP.prefix(P.ref);
   {? LSW.first ||
       _nieob:="_s12:=_s13:=_s14:=_s19:=''; _wynik:=wynik.ref;
            {!|?{? wynik.prev
                   || {? _a=wynik.OD$2
                      || _s12:=wynik.S12; _s13:=wynik.S13; _s14:=wynik.S14; _s19:=wynik.S19; 0
                      || 1
                      ?}
                ?}
            !};
            {? wynik.seek(_wynik) || wynik.S12:=_s12; wynik.S13:=_s13; wynik.S14:=_s14; wynik.S19:=_s19; wynik.put ?}";
      {!|?
         {? LSW.LT<>'' ||
            wynik.blank; wynik.S3:=4*' '; wynik.DT:=date(0,0,0); wynik.NR:=3*' ';
            {? __KLP.find_key(LSW.LT) ||
               wynik.NR:=form(__KLP.NR,3);
               {? __KLP.D<>date(0,0,0) ||
                  wynik.DT:=__KLP.D
               ?};
               wynik.S3:=form(__KLP.POZ,4,,'99')
            ?};
            wynik.OD:=LSW.OD; wynik.DO:=LSW.DO; wynik.S5:=LSW.DO$2; wynik.S6:=form(LSW.DO-LSW.OD+1,3);
            wynik.S7:='      WYR      '; wynik.S8:=form(LSW.KW,15,2);
            wynik.S16:=14*' '; wynik.S20:=' Wyrównanie ';
            wynik.S17:=wynik.S18:='';
            {? not_all
               ||{? wynik.DT~1=__param.ROK
                    || {? wynik.DT=date(0,0,0) || wynik.DT:=date(9999,12,31) ?};
                       wynik.add; _nieob(wynik.OD$2)
                 ?}
               || {? wynik.DT=date(0,0,0) || wynik.DT:=date(9999,12,31) ?};
                  wynik.add; _nieob(wynik.OD$2)
            ?}
         ?}; LSW.next
      !}
   ?};
   LSW.prefix('H',P.ref);
   {? LSW.first
   || {!
      |? {? LSW.LT<>''
         || wynik.index(n_wynikM);
            wynik.prefix(1);
            {? wynik.find_key(LSW.OD-1)
            || _okres:=wynik.OKRES
            || _okres:=0
            ?};
            wynik.blank; wynik.S3:=4*' '; wynik.DT:=date(0,0,0); wynik.NR:=3*' ';
            wynik.MAC:=1;
            {? __KLP.find_key(LSW.LT)
            || wynik.NR:=form(__KLP.NR,3);
               {? __KLP.D<>date(0,0,0) || wynik.DT:=__KLP.D ?};
               wynik.S3:=form(__KLP.POZ,4,,'99')
            ?};
            _kw_wyp:=(LSW.KW/(LSW.DO-LSW.OD+1));
            _kw_100:=(LSW.POD/30);
            _proc:=(_kw_wyp/_kw_100)$2;
            wynik.OD:=LSW.OD;  wynik.DO:=LSW.DO; wynik.S5:=LSW.DO$2; wynik.S6:=form(LSW.DO-LSW.OD+1,3);
            wynik.S7:=form(LSW.POD/30*_proc$2,15,2);
            wynik.S8:=form(LSW.KW,15,2);
            wynik.S15:=form(LSW.POD,15,2);
            _okres+=LSW.DO-LSW.OD+1;
            wynik.S16:=form(_okres,14); wynik.OKRES:=_okres; wynik.S19:='Macierzyński';
            wynik.S20:=(('    ')+$(_proc*100$2)+'    ')+12;
            wynik.S17:=wynik.S18:='';
            {? not_all
               ||{? wynik.DT~1=__param.ROK
                    || {? wynik.DT=date(0,0,0) || wynik.DT:=date(9999,12,31) ?};
                       wynik.add
                 ?}
               || {? wynik.DT=date(0,0,0) || wynik.DT:=date(9999,12,31) ?};
                  wynik.add
            ?}
         ?}; LSW.next
      !}
   ?};
~~}
{FOR: wynik}{INDEX: n_wynikL}
{DO}
{ENTIRE}
{ NextPage+=1; ~~}
├─────┼──────────┼──────┼──────────┼──────────┼─────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼────────────┼───────────┼───────────────────────┼─────────────────┼────────────────┤
│ {wynik.NR} │{{? wynik.DT=date(9999,12,31) || 10*' ' || wynik.DT$2 ?}}│ {wynik.S3} │{wynik.OD$2}│{wynik.S5}│ {wynik.S6
} │ {{?wynik.S7<>''||wynik.S7|| form('',15)?}} │ {{?wynik.S8<>''||wynik.S8||form('',15)?}} │                 │                 │                 │{form(wynik.S19,12)}│ {{?wynik.S13<>''||wynik.S13||form('',9)?}
} │ {{?wynik.S14<>''||wynik.S14||21*' '?}} │ {{?wynik.S15<>''||wynik.S15||15*' '?}} │ {{?wynik.S16<>''||wynik.S16||14*' '?}} │
{IF: wynik.S17<>'' | wynik.S19<>''}
│     │          │      │          │          │     │                 │                 │                 │                 │                 │{{?wynik.S19<>''||form(wynik.S12,12)||form('',12)?}}│           │ {form(wynik.S17,21)} │                 │                │
{FI}
{IF: wynik.S18<>'' | wynik.S20<>''}
│     │          │      │          │          │     │                 │                 │                 │                 │                 │{{?wynik.S20<>''||form(wynik.S20,12)||form('',12)?}}│           │ {form(wynik.S18,21)} │                 │                │
{FI}
{ENTIRE-}
{OD}
{IF: NextPage}{ NextPage:=0; wynik.erase; ~~ }{PAGE}
{FI}
{FI}
{OD}
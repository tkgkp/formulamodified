:!UTF-8
{TITLE:Amortyzacja środka zbytego w obrębie grupy kapitałowej}
{PRINTER: FINFO.W_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,15); PRN.s_frame();
   c:=obj_new(15);
   {! _i:=1..14 |! c[_i] := '' !};
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0; par5:=0
}
{END:
   obj_del(PRN);
   obj_del(c);
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3;&par4;&par5
}
{EXIT:
   exec('set_var','param_wydr');
   {? OPERATOR.DEPT<>null
      || PAR_WYDR.ODD:=OPERATOR.DEPT
      || PAR_WYDR.ODD:=0
   ?};
   0
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_grp_srod.rpm
Utworzony  : 2017-09-15
Zawartość  : Amortyzacja środka zbytego w obrębie grupy kapitałowej
==================================================
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("c[1]",6,'Rok',1,' ');
      PRN.add("c[2]",6,'Okres',1,' ');
      PRN.add("{? type_of(c[3]) = 1 || form(c[3],,2,' ,') || c[3] ?}",21,'Amortyzacja podatkowa pierwotna',1,' ');
      PRN.add("{? type_of(c[4]) = 1 || form(c[4],,2,' ,') || c[4] ?}",21,'Amortyzacja podatkowa bieżąca',1,' ');
      PRN.add("{? type_of(c[7]) = 1 || form(c[7],,2,' ,') || c[7] ?}",18,'Różnica',1,' ');
      PRN.add("{? type_of(c[5]) = 1 || form(c[5],,2,' ,') || c[5] ?}",21,'Amortyzacja bilansowa pierwotna',1,' ');
      PRN.add("{? type_of(c[6]) = 1 || form(c[6],,2,' ,') || c[6] ?}",21,'Amortyzacja podatkowa bieżąca',1,' ');
      PRN.add("{? type_of(c[8]) = 1 || form(c[8],,2,' ,') || c[8] ?}",18,'Różnica',1,' ');

      PAR_WYDR.TITLE:='Amortyzacja środka zbytego w obrębie grupy kapitałowej'; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
 {LINE: 1}
{<1 'PARAMETRY WYDRUKU:'@}
{FONT: 'W8B'}{<1 'Nr inwentarzowy bieżący: '@}{FONT: 'W8'}{__T_SRSF.NRI_D}
{FONT: 'W8B'}{<1 'Nazwa bieżąca: '@}{FONT: 'W8'}{__T_SRSF.NST_D}
{FONT: 'W8B'}{<1 'Firma bieżąca: '@}{FONT: 'W8'}{__T_SRSF.FIRMA_D}
{FONT: 'W8B'}{<1 'Firma pierwotna: '@}{FONT: 'W8'}{__T_SRSF.FIRMA_Z}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{FOR: __T_AMOR}
 {DO}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
 {
      c[1]:=$__T_AMOR.ROK;
      c[2]:=$__T_AMOR.OKRES;
      c[3]:=__T_AMOR.AMOP_P;
      {? __T_AMOR.P='T' || c[4]:='-' || c[4]:=__T_AMOR.AMOP_B ?};
      c[5]:=__T_AMOR.AMOF_P;
      {? __T_AMOR.P='T' || c[6]:='-' || c[6]:=__T_AMOR.AMOF_B ?};
      {? __T_AMOR.P='T' || c[7]:='-' || c[7]:=__T_AMOR.AMOP_B-__T_AMOR.AMOP_P ?};
      {? __T_AMOR.P='T' || c[8]:='-' || c[8]:=__T_AMOR.AMOF_B-__T_AMOR.AMOF_P ?}

 ;~~}
  {IF: lineleft() <> 0 & lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}{FOOTER-}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
:!UTF-8
{TITLE:Dodatkowe kwalifikacje}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   tresc:='pkd_wkwalifikacje';
   PAR_WYDR.TITLE:='Dodatkowe kwalifikacje'@;
   P.cntx_psh();
   KART_DEF.cntx_psh();
   par:=obj_new(7);
   par[3]:=0;
   _upr:=exec('uprawnieniawrap','pkd','dodatkowych kwalifikacjach'@,'PKD_DOS_PPKW','PKD_DOS_PRKW');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || par[7]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,
         "  {? cur_tab().OD<>#0 & cur_tab().DO<>#0
            || {? cur_tab().OD>cur_tab().DO
               || FUN.emsg('Data początku okresu nie może być większa od daty końcowej'@); 'OD'
               || 1
               ?}
            || 1
            ?}
         ",
         date(,,1),'Początek okresu'@,,
         date(,,0),'Koniec okresu'@,,
         'Okres kontroli daty uzyskania'@,'Bez ograniczeń'@,'Zakres dat'@,0,1
      );
      {? par[3]:=par[7].size()
      || par[2]:=tab_tmp(,'REF','INTEGER','Osoba');
         par[1]:=obj_new(@.CLASS.PRINT,{? rep_export() || 7 || 4?});
         {? rep_export()
         || par[1].add("KART_DOD.OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("KART_DOD.OSOBA().PIERWSZE+' '+KART_DOD.OSOBA().DRUGIE",41,'Imiona'@);
            par[1].add("KART_DOD.OSOBA().PESEL",11,'PESEL'@)
         ?};
         par[1].add("KART_DOD.OPIS",40,'Opis kwalifikacji'@);
         par[1].add("KART_DOD.OD$1",10,'Uzyskano'@);
         par[1].add("{?KART_DOD.DO<>date(0,0,0)||KART_DOD.DO$1||'bezterminowo'@?}",12,'Ważne do'@);
         par[1].add("exec('memo2print','#string',KART_DOD,'UWAGI')",60,'Adnotacje i uwagi'@);
         par[1].s_frame();

         prn1:='par[1]';
         lmt:=rsep:=par[6]:=par[5]:=0;
         vp:=par[4]:=1;
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   KART_DEF.cntx_pop();
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc')
}
{COMMENT}OLD: wkwal.rpm{COMMENT-}
{COMMENT}OLD: std_osop.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[3]}
{EXIT: ~params_get().P.first()}
{EXIT: KART_DEF.index('SYMBOL'); KART_DEF.prefix(); ~KART_DEF.find_key('DOD_KWAL')}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
 {IF: par[7].OD<>#0 & par[7].DO<>#0}
   {<{lmt+1}'Data uzyskania: od %1 do %2'@[par[7].OD$1,par[7].DO$1]}
 {ELSE}
   {<{lmt+1}'Bez ograniczeń'@}
 {FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W10'}
{SPACE: 'B'}
{IF: ~rep_export()}
   {<{int((136/160)*lmt)+1}}{form(par[5]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{ par[6]:=1; ~~ }
{MACRO-}
{MACRO: 'kwa_dane'}
{IF: ~par[6]}
   {SUBSTITUTE: 'pracownik'}
   {REP: par[1].fhbar(lmt)}
{FI}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{COMMENT}

   Przeglądaj kartotekę dodatkowych kwalifikacji w podanym lub pełnym
   zakresie dat. Pełny zakres - wszystkie dodatkowe kwalifikacje,
   ograniczony zakres - od daty do daty, od 'początku' kartoteki do daty
   lub od daty do 'końca'.

{COMMENT-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[2].add();
      par[6]:=0;
      ~~
   }
   {ENTIRE}
   {FOR: KART_DOD}{INDEX: 'KART_DOD'}{PREFIX: exec('ref_firma','ustawienia'),KART_DEF.ref(),P.OSOBA}{DO}
      {IF: {? par[7].OD<>#0 || par[7].OD<=KART_DOD.OD || 1 ?} &
           {? par[7].DO<>#0 || KART_DOD.OD<=par[7].DO || 1 ?}}
         {SUBSTITUTE: 'kwa_dane'}
      {FI}
   {OD}
   {IF: par[6]}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
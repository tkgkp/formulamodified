:!UTF-8
{TITLE:Decyzje o niepełnosprawności tracące ważność}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='wniepeln';
   PAR_WYDR.TITLE:='Decyzje o niepełnosprawności tracące ważność'@;
   par:=obj_new(9);
   par[6]:=0;
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','danych ubezpieczeniowych'@,'PKD_DOS_PPDU','PKD_DOS_PRDU');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || par[9]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,
         "  {? cur_tab().OD=#0
            || FUN.info('Podanie daty początkowej jest obowiązkowe.'@); 'OD'
            |? cur_tab().DO=#0
            || FUN.info('Podanie daty końcowej jest obowiązkowe.'@); 'DO'
            |? cur_tab().DO<cur_tab().OD
            || FUN.info('Podano niespójne daty.'@); 'OD'
            || 1
            ?}
         ",
         date(date~1,date~2,1),'Data od'@,"1",
         date(date~1,date~2,0),'Data do'@,"1"
      );

      {? par[6]:=par[9].size()
      || par[1]:=obj_new(@.CLASS.PRINT,{?  rep_export() || 8 || 5 ?});
         {? rep_export()
         || par[1].add("P_INFO.OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("P_INFO.OSOBA().PIERWSZE +' '+ P_INFO.OSOBA().DRUGIE",41,'Imiona'@);
            par[1].add("P_INFO.OSOBA().PESEL",11,'PESEL'@)
         ?};
         par[1].add("P_INFO.STNP_OD$1",10,'Od dnia'@);
         par[1].add("P_INFO.STNP_DO$1",10,'Do dnia'@);
         par[1].add("P_INFO.ORZECZ",50,'Orzecznik niepełnosprawności'@);
         par[1].add("P_INFO.STNP().KOD",18,'Kod stopnia niepełnosprawności'@,1);
         par[1].add("{? P_INFO.SZCZNIEP='N' || 'Nie'@ || 'Tak'@ ?}",15,'Szczególne schorzenia'@);
         par[1].s_frame();
         par[2]:=tab_tmp(,'REF','INTEGER','Osoba');
         prn1:='par[1]';
         lmt:=rsep:=par[8]:=0;
         vp:=b_rat:=h_rat:=par[7]:=1;
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat')
}
{COMMENT}OLD: wniepeln.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_osop.rpi{COMMENT-}
{EXIT: ~par[6]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[7]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[7]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: od %1 do %2'@[par[9].OD$1,par[9].DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par[8]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[6]:=1; ~~ }
{MACRO-}
{MACRO: 'dane_ub'}
{IF: ~par[6]}
   {SUBSTITUTE: 'pracownik'}
   {REP: par[1].fhbar(lmt)}
{FI}
{ par[1].ini_line(); ~~ }
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{MACRO-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[2].add();
      par[6]:=0;
      ~~
   }
   {ENTIRE}
   {REP: '{FOR: P_INFO}{INDEX: \'OD\'}{PREFIX: exec(\'ref_firma\',\'ustawienia\'),P.OSOBA}'+
         '{DO}{IF: P_INFO.STNP_DO>=par[9].OD & P_INFO.STNP_DO<=par[9].DO}'+
                 '{SUBSTITUTE: \'dane_ub\'}'+
              '{FI}'+
         '{OD}'}
   {IF: par[6]}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
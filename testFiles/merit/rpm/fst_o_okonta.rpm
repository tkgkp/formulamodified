:!UTF-8
{TITLE:J. Wartość amortyzacji w podziale na konta kosztowe}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,9+{? rep_export() || 3 ?}); PRN.s_frame();
   PRN2:=obj_new(@.CLASS.PRINT,9); PRN2.s_frame();
   PRNF:=obj_new(@.CLASS.PRINT,9); PRNF.s_frame();
   c:=obj_new(9+{? rep_export() || 3 ?});
   {! _i:=1..9+{? rep_export() || 3 ?} |! c[_i] := '' !};
   SRST.cntx_psh(); SRSR.cntx_psh(); SRSW.cntx_psh();
   SRSR.prefix(); SRST.prefix(); SRSW.prefix();
   exec('init','fst');
   SRD.maski('r');
   exec('odd_filtr','fst');
   VAR_DEL.delete('TAB_SQL');
   TAB_SQL:=tab_tmp(1, 'KONTO','STRING[35]','Konto'@,
                       'ODDZIAL','STRING[10]','J. księgowa'@,
                       'NRI','STRING[20]','Numer'@,
                       'NST','STRING[40]','Nazwa'@,
                       'JORG','STRING[16]','J. organizacyjna'@,
                       'GRUPA','STRING[10]','Grupa'@,
                       'AMORT','REAL','Amortyzacja'@,
                       'AMORTN','REAL','Amort_narasta'@,
                       'NETTO','REAL','Netto'@,
                       'PODSTAWA','REAL','Podstawa'@,
                       'UMORZENI','REAL','Umorzenie'@,
                       'PROCENT','REAL','Procent'@,
                       'DE','DATE','Data eksploatacji'@);
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0;
   first_line:=1; fun:=''; temp_p:='';
   licz_podsum:=0;
   g_amort:=0; sum_amort:=0; first_rec:=1;
   sum_konta:=''; test_konta:='';
   srsw_dr:=0
}
{END:
   obj_del(PRN);
   obj_del(PRNF);
   obj_del(PRN2);
   obj_del(c);
   SRST.cntx_pop(); SRSR.cntx_pop(); SRSW.cntx_pop();
   VAR_DEL.delete('TAB_SQL');
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3;&par4;
   &first_line; &fun; &temp_p;
   &licz_podsum;
   &g_amort; &sum_amort; &first_rec;
   &sum_konta; &test_konta;
   &srsw_dr
}
{EXIT:
   exec('clear_par','fst');
   exec('set_parw','param_wydr');
   {? OPERATOR.DEPT <> null
   || PAR_WYDR.ODD:=OPERATOR.DEPT
   || PAR_WYDR.ODD:=0
   ?};
   {? SSTALE.AO().KON<>date(0,0,0)
   || PAR_WYDR.STAN_NA:=SSTALE.AO().KON
   |? SSTALE.AO().OES=0
   || PAR_WYDR.STAN_NA:=SSTALE.AR().POCZ_ROK
   |? SSTALE.AO().OES>0
   || PAR_WYDR.STAN_NA:=SSTALE.AR().KON_ROK
   ?};
   PAR_WYDR.RODZAJ:=1;
   PAR_WYDR.DANE:=1;
   PAR_WYDR.KONTA:=1;
   PAR_WYDR.MO_DO:=1;
   SRD.PAR_WYDR:='T';
   PAR_WYDR.JORG:=null; EDIT_ES.JORG:='';
   exec('odd_filtr','fst');
   {? FINFO.TOR_D<>'T'
   || PAR_WYDR.win_edit('AMKONTO')
   || PAR_WYDR.win_edit('AMKONTOD')
   ?};
   PAR_WYDR.hdr_edit(': J. Wartość amortyzacji w podziale na konta kosztowe'@);
   {? PAR_WYDR.edit("exec('check_amk','fst')") || _exit:=0 || _exit:=1 ?};
   {? _exit=0 || exec('prepare_okonta','fst2') ?};
   _exit
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_o_okonta.rpm
Utworzony  : 2004-09-10
Zawartość  : Wartość amortyzacji w podziale na konta kosztowe
==================================================
------------------------ OPIS PÓL ZMIENNEJ PAR_WYDR ------------{COMMENT-}
{COMMENT}
Dane pobierane są w sekcji Exit z wykorzystaniem formuł z pliku query.fml
RODZAJ : 1 = analityczne, 2 = syntetyczne,
DANE   : 1 = podatkowo, 2 = syntetyczne,
KONTA  : 1 = zestawy kont, 2 = faktyczne konta
ODD    : jedn. ksiegowa
{COMMENT-}
{COMMENT}zmienne pomocnicze do wydrukow graficznych:
i - licznik, color, prn1, lm, ll, vp, lmt, rsep
{COMMENT-}
{PAR_WYDR.cntx_psh();~~}
{INCLUDE: wsp_pw.rpi}
{PAR_WYDR.cntx_pop();~~}
{MACRO: 'TABHEAD2'}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn1+'.ini_head()'; ($(_f))()}
{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
{WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
{ _f:=prn2+'.ini_head()'; ($(_f))()}
{ _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
{WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{ _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
{ _f:=prn2+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
{ vp:=0; ~~}
{MACRO-}
{MACRO: 'LINIA02A'}
{  ll:=lineleft(); ~~}
{IF:  _f:=prn2+'.ini_line()'; _v:=($(_f))();
      _f:=prn2+'.length()'; _v:=($(_f))();
      ll<(rsep+_v) & ll<>0}{PAGE}{FI}
{ENTIRE}
{IF: vp}
   { {? PAR_WYDR.KONTA=3 || prn1:='PRN2' || prn1:='PRN' ?}; _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   {ELIF: rsep}
      {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   {FI}
{FI}
{WHILE: _f:=prn2+'.next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.fline(lmt)'; ($(_f))()}
{OD}
{IF: var_pres('fun')>=0 & fun<>''}
{  $fun(); ~~}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      {? PAR_WYDR.RODZAJ = 1
      ||
         {? PAR_WYDR.KONTA <=2
         || {? PAR_WYDR.KONTA = 1
            || PRN.add("c[1]",20,'Zestaw kont'@,,'#')
            |? PAR_WYDR.KONTA = 2
            || PRN.add("c[1]",20,'Konto'@,,'#')
            ?};
            PRN.add("c[6]",10,'Jedn. ks.'@);
            PRN.add("c[7]",16,'Jedn. organiz.'@);
            PRN.add("c[2]",20,'Nr inwentarz.'@);
            PRN.add("c[3]",40,'Nazwa'@,,'#');
            PRN.add("c[4]",8,'Grupa'@,,'#');
            PRN.add("{? type_of(c[5]) = 1 || form(c[5],,2,' ,') || c[5] ?}",14,'Amortyzacja'@,1,'#');

            PRNF.add("c[1]",129,'Konto'@,,'#');
            PRNF.add("{? type_of(c[5]) = 1 || form(c[5],,2,' ,') || c[5] ?}",14,'',1,'#')

         || PRN.add("c[1]",20,'Nr inw.'@,,'#');
            PRN.add("c[2]",40,'Nazwa środka'@);
            PRN.add("c[3]",10,'Jedn. ks.'@);
            PRN.add("c[4]",16,'Jedn. organiz.'@);
            PRN.add("c[5]",8,'Grupa'@);
            PRN.add("{? type_of(c[6]) = 1 || form(c[6],,2,' ,') || c[6] ?}",14,'Netto'@,1,'#');
            PRN.add("{? type_of(c[7]) = 1 || form(c[7],,2,' ,') || c[7] ?}",14,'Wartość'@,1,'#');
            PRN.add("{? type_of(c[8]) = 1 || form(c[8],,2,' ,') || c[8] ?}",14,'Umorzenie'@,1,'#');
            PRN.add("{? type_of(c[9]) = 1 || form(c[9],,2,' ,') || c[9] ?}",14,'Amort. narast.'@,1,'#');
         {? ~rep_export()
         || PRN2.add("''",116,'',,'#');
            PRN2.add("c[1]",21,'Konto'@,,'#');
            PRN2.add("{? type_of(c[2]) = 1 || form(c[2],,2,' ,') || c[2] ?}",14,'Procent'@,1,'#');
            PRN2.add("{? type_of(c[3]) = 1 || form(c[3],,2,' ,') || c[3] ?}",14,'Amortyzacja'@,1,'#');

            PRNF.add("c[1]",157,'Razem'@,,'#');
            PRNF.add("{? type_of(c[2]) = 1 || form(c[2],,2,' ,') || c[2] ?}",14,'',1,'#')
         || PRN.add("c[10]",21,'Konto'@,,'#');
            PRN.add("{? type_of(c[11]) = 1 || form(c[11],,2,' ,') || c[11] ?}",14,'Procent'@,1,'#');
            PRN.add("{? type_of(c[12]) = 1 || form(c[12],,2,' ,') || c[12] ?}",14,'Amortyzacja'@,1,'#') ?} ?}

      |? PAR_WYDR.RODZAJ = 2
      || {? PAR_WYDR.KONTA = 1
         || PRN.add("c[1]",25,'Zestaw kont'@,,'#')
         |? PAR_WYDR.KONTA = 2
         || PRN.add("c[1]",25,'Konto'@,,'#')
         ?};
         PRN.add("{? type_of(c[5]) = 1 || form(c[5],,2,' ,') || c[5] ?}",20,'Amortyzacja'@,1,'#');

         PRNF.add("c[1]",25,'Konto',,'#');
         PRNF.add("{? type_of(c[5]) = 1 || form(c[5],,2,' ,') || c[5] ?}",20,'Amortyzacja'@,1,'#')
      ?};

      {? SSTALE.AO().KON<>date(0,0,0)
      || _okres:=(PAR_WYDR.STAN_NA)$8
      |? SSTALE.AO().OES=0
      || _okres:='Bilans otwarcia'
      |? SSTALE.AO().OES>0
      || _okres:='Bilans zamknięcia'
      ?};

      PAR_WYDR.TITLE:='Wartość amortyzacji w podziale na konta kosztowe - za okres %1'@[_okres]; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{<1 'PARAMETRY WYDRUKU:'}
{COMMENT} ---- Stan na, Grupa, Rodzaj zestawienia ---- {COMMENT-}
{COMMENT}----------NIE EXCEL-------------{COMMENT-}
{IF: ~rep_export()}
{
  {? PAR_WYDR.RODZAJ = 1
  || par2:='analityczne'@ || par2:='syntetyczne'@
  ?};

  {? PAR_WYDR.ODD<>null || par3:=PAR_WYDR.ODD().OD || par3:='wszystkie'@ ?};

  {? PAR_WYDR.ZAKRESDR=0 || par4:='wszystkie'@
  |? PAR_WYDR.ZAKRESDR=1 || par4:='trwałe'@
  |? PAR_WYDR.ZAKRESDR=2 || par4:='niskocenne'@
  ?}
;~~}
{FONT: 'W8B'}{<1 'Rodzaj zestawienia: '@}{FONT: 'W8'}{par2}{FONT: 'W8B'}{<45 'J. księgowa: '@}{FONT: 'W8'}{par3}{FONT: 'W8B'}{<85 'Zakres środków: '@}{FONT: 'W8'}{par4}
{COMMENT} ---- Dane, Jedn. ks., Zestawienie według ---- {COMMENT-}
{
  {? PAR_WYDR.DANE=1 || par1:='podatkowa'@
  |? PAR_WYDR.DANE=2 || par1:='bilansowa'@
  |? PAR_WYDR.DANE=3 || par1:='Dodatkowa'@
  ?};

  {? PAR_WYDR.KONTA=1 || par2:='zestawów kont'@
  |? PAR_WYDR.KONTA=2 || par2:='faktycznych kont'@
  |? PAR_WYDR.KONTA=3 || par2:='środków i kont'@
  ?}

;~~}
{FONT: 'W8B'}{<1 'Amortyzacja: '@}{FONT: 'W8'}{par1}{FONT: 'W8B'}{<45 'Zestawienie według: '@}{FONT: 'W8'}{par2}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: PAR_WYDR.KONTA = 3}
{prn1:='PRN';prn2:='PRN2';~~}
{SUBSTITUTE: 'TABHEAD2'}
{ELIF: PAR_WYDR.KONTA <= 2}
{prn1:='PRN';prn2:='PRN';~~}
{SUBSTITUTE: 'TABHEAD'}
{FI}
{FI}
{COMMENT}----------EXCEL-------------{COMMENT-}
{IF: rep_export()}
{
  {? PAR_WYDR.RODZAJ = 1
  || par2:='analityczne'@ || par2:='syntetyczne'@
  ?};

  {? PAR_WYDR.ODD<>null || par3:=PAR_WYDR.ODD().OD || par3:='wszystkie'@ ?};

  {? PAR_WYDR.ZAKRESDR=0 || par4:='wszystkie'@
  |? PAR_WYDR.ZAKRESDR=1 || par4:='trwałe'@
  |? PAR_WYDR.ZAKRESDR=2 || par4:='niskocenne'@
  ?}
;~~}
{FONT: 'W8B'}{ 'Rodzaj zestawienia: '@}{FONT: 'W8'}{par2}
{FONT: 'W8B'}{ 'J. księgowa: '@}{FONT: 'W8'}{par3}
{FONT: 'W8B'}{ 'Zakres środków: '@}{FONT: 'W8'}{par4}
{COMMENT} ---- Dane, Jedn. ks., Zestawienie według ---- {COMMENT-}
{
  {? PAR_WYDR.DANE=1 || par1:='podatkowa'@
  |? PAR_WYDR.DANE=2 || par1:='bilansowa'@
  |? PAR_WYDR.DANE=3 || par1:='Dodatkowa'@
  ?};

  {? PAR_WYDR.KONTA=1 || par2:='zestawów kont'@
  |? PAR_WYDR.KONTA=2 || par2:='faktycznych kont'@
  |? PAR_WYDR.KONTA=3 || par2:='środków i kont'@
  ?}

;~~}
{FONT: 'W8B'}{ 'Amortyzacja: '@}{FONT: 'W8'}{par1}
{FONT: 'W8B'}{ 'Zestawienie według: '@}{FONT: 'W8'}{par2}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: PAR_WYDR.KONTA = 3}
{prn1:='PRN';prn2:='PRN2';~~}
{SUBSTITUTE: 'TABHEAD2'}
{ELIF: PAR_WYDR.KONTA <= 2}
{prn1:='PRN';prn2:='PRN';~~}
{SUBSTITUTE: 'TABHEAD'}
{FI}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}{vp:=1;~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: PAR_WYDR.KONTA = 3}
{prn1:='PRN';prn2:='PRN2';~~}
{SUBSTITUTE: 'TABHEAD2'}
{ELIF: PAR_WYDR.KONTA <= 2}
{prn1:='PRN';prn2:='PRN';~~}
{SUBSTITUTE: 'TABHEAD'}
{FI}
{
  {? PAR_WYDR.KONTA=3
  || {? first_line = 1 || prn2:='PRN'
     |? first_line = 2 || prn2:='PRN2'
     |? first_line = 3 || prn2:='PRNF'
     |? first_line = 4 || prn2:='PRNF'
     ?}
  |? PAR_WYDR.KONTA <= 3
  || {? first_line = 4 || prn2:='PRNF'
     |? first_line = 3 || prn2:='PRNF'
     ?}
  ?}
;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; "PAR_WYDR.RSEP"; ~~}
{FOR: TAB_SQL}
 {DO}
 {COMMENT} ---- Podsumowanie cząstkowe według grup lub jedn. ksiegowych ---- {COMMENT-}
    {COMMENT} ---- podsumowanie nie występuje dla excela ---- {COMMENT-}
  {IF: ~rep_export()}
  {
   {? PAR_WYDR.RODZAJ=1
   ||
      {? PAR_WYDR.KONTA<>3 || test_konta:=TAB_SQL.KONTO || test_konta:=TAB_SQL.NRI ?};
      {? sum_konta='' || test_konta:=''?}
   ?}
  ;~~}
  {IF: PAR_WYDR.RODZAJ=1 & sum_konta<>test_konta}
  {
   first_line:=3; licz_podsum+=1;
   {? PAR_WYDR.KONTA=1 || _konta:='Zestaw '@; c[1]:=_konta+sum_konta+' razem:'@
   |? PAR_WYDR.KONTA=2 || _konta:='Konto '@; c[1]:=_konta+sum_konta+' razem:'@
   |? PAR_WYDR.KONTA=3 || _konta:='Środek '@; c[1]:=_konta+' razem:'@
   ?};
   {? PAR_WYDR.KONTA<>3
   || c[2]:=''; c[3]:=''; c[4]:=''; c[5]:=g_amort;
      prn1:='PRN'; prn2:='PRNF'
   |? PAR_WYDR.KONTA=3
   || c[2]:=g_amort; c[3]:=''; c[4]:=''; c[5]:='';
      c[6]:=''; c[7]:=''; c[8]:= '';
      prn1:='PRN2'; prn2:='PRNF'
   ?};
   rsep:=1
  ;~~}
  {FONT: 'T8GB'}{SPACE: 'C'}{IF: lineleft()<=4 & lineleft()<>0}{PAGE}{FI}{SUBSTITUTE: 'LINIA02A'}{first_line:=1 ;~~}
  {
  {? PAR_WYDR.KONTA<=2 || prn1:='PRNF'; prn2:='PRN' ?};
  rsep:=1
  ;~~}
   {FI}
  {FONT: 'T8G'}{SPACE: 'C'}
{COMMENT} ---- wyzerowanie podsumowań cząstkowych---- {COMMENT-}
  {
     {? PAR_WYDR.RODZAJ=1 & sum_konta<>test_konta
     || g_amort:=0
     ?}
  ;~~}
 {FI}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
{COMMENT} Konta i zestawy {COMMENT-}
{COMMENT}----------NIE EXCEL-------------{COMMENT-}
{IF: ~rep_export()}
 {IF: PAR_WYDR.KONTA<>3}
 {
   {? PAR_WYDR.RODZAJ=1
   ||
      {? PAR_WYDR.KONTA<>3 || _temp:=TAB_SQL.KONTO || _temp:=TAB_SQL.NRI ?};
      {? (c[1]=_temp | c[1]='') & first_rec=0 || c[1]:='' || c[1]:=_temp ?};
      first_rec:=0;
      c[2]:={? PAR_WYDR.KONTA<>3 || TAB_SQL.NRI || TAB_SQL.KONTO ?};
      c[3]:={? PAR_WYDR.KONTA<>3 || TAB_SQL.NST || TAB_SQL.DE ?};
      c[4]:=TAB_SQL.GRUPA;
      c[5]:=TAB_SQL.AMORT;
      c[6]:=TAB_SQL.ODDZIAL;
      c[7]:=TAB_SQL.JORG;

      sum_amort+=TAB_SQL.AMORT$2;
      g_amort+=TAB_SQL.AMORT$2
   ||
      sum_amort+=TAB_SQL.AMORT$2;
      c[1]:=TAB_SQL.KONTO;
      c[5]:=TAB_SQL.AMORT
   ?}
 ;~~}
 {SUBSTITUTE: 'LINIA02A'}
 {rsep:=1;"PAR_WYDR.RSEP"; ~~}
 {FI}
{COMMENT} Konta i zestawy {COMMENT-}
{COMMENT} Srodki i konta {COMMENT-}
 {IF: PAR_WYDR.KONTA=3 & first_line=1}
  {
       c[1]:=TAB_SQL.NRI;
       c[2]:=TAB_SQL.NST;
       c[3]:=TAB_SQL.ODDZIAL;
       c[4]:=TAB_SQL.JORG;
       c[5]:=TAB_SQL.GRUPA;
       c[6]:=TAB_SQL.NETTO;
       c[7]:=TAB_SQL.PODSTAWA;
       c[8]:=TAB_SQL.UMORZENI;
       c[9]:=TAB_SQL.AMORTN;

       {? first_rec=1
       || prn1:='PRN2'; first_rec:=0
       || prn1:='PRNF'
       ?};
       prn2:='PRN'; rsep:=1
 ;~~}
 {IF: lineleft()<=4 & lineleft()<>0}{PAGE}{FI}{SUBSTITUTE: 'LINIA02A'}
 {
       c[1]:=TAB_SQL.KONTO;
       c[2]:=TAB_SQL.PROCENT;
       c[3]:=TAB_SQL.AMORT;
       rsep:=1;

       sum_amort+=TAB_SQL.AMORT;
       g_amort+=TAB_SQL.AMORT;
       first_line:=2;
       prn1:='PRN'; prn2:='PRN2';
       rsep:=1
 ;~~}
 {SUBSTITUTE: 'LINIA02A'}
 {prn1:='PRN2';prn2:='PRN2';~~}
 {ELIF: PAR_WYDR.KONTA=3 & first_line=2}
 {
       c[1]:=TAB_SQL.KONTO;
       c[2]:=TAB_SQL.PROCENT;
       c[3]:=TAB_SQL.AMORT;
       rsep:=1; "PAR_WYDR.RSEP";

       sum_amort+=TAB_SQL.AMORT;
       g_amort+=TAB_SQL.AMORT;
       prn1:='PRN2'; prn2:='PRN2'
 ;~~}
 {SUBSTITUTE: 'LINIA02A'}
 {FI}
 {prn1:='PRN'; prn2:='PRN'; first_rec:=0;~~}
 {
   {? PAR_WYDR.RODZAJ=1
   || {? PAR_WYDR.KONTA<=2
      || sum_konta:=TAB_SQL.KONTO
      || sum_konta:=TAB_SQL.NRI
      ?}
   ?}
 ;~~}
 {FI}
 {COMMENT}----------EXCEL-------------{COMMENT-}
{IF: rep_export()}
{IF: PAR_WYDR.KONTA<>3}
 {
   {? PAR_WYDR.RODZAJ=1
   ||
      {? PAR_WYDR.KONTA<>3 || _temp:=TAB_SQL.KONTO || _temp:=TAB_SQL.NRI ?};
      c[1]:=_temp;
      c[2]:={? PAR_WYDR.KONTA<>3 || TAB_SQL.NRI || TAB_SQL.KONTO ?};
      c[3]:={? PAR_WYDR.KONTA<>3 || TAB_SQL.NST || TAB_SQL.DE ?};
      c[4]:=TAB_SQL.GRUPA;
      c[5]:=TAB_SQL.AMORT;
      c[6]:=TAB_SQL.ODDZIAL;
      c[7]:=TAB_SQL.JORG;

      sum_amort+=TAB_SQL.AMORT$2;
      g_amort+=TAB_SQL.AMORT$2
   ||
      sum_amort+=TAB_SQL.AMORT$2;
      c[1]:=TAB_SQL.KONTO;
      c[5]:=TAB_SQL.AMORT
   ?}
 ;~~}
 {rsep:=1;"PAR_WYDR.RSEP"; ~~}
 {FI}
{COMMENT} Konta i zestawy {COMMENT-}
{COMMENT} Srodki i konta {COMMENT-}
 {IF: PAR_WYDR.KONTA=3 & first_line=1}
  {
       c[1]:=TAB_SQL.NRI;
       c[2]:=TAB_SQL.NST;
       c[3]:=TAB_SQL.ODDZIAL;
       c[4]:=TAB_SQL.JORG;
       c[5]:=TAB_SQL.GRUPA;
       c[6]:=TAB_SQL.NETTO;
       c[7]:=TAB_SQL.PODSTAWA;
       c[8]:=TAB_SQL.UMORZENI;
       c[9]:=TAB_SQL.AMORTN;

       {? first_rec=1
       || prn1:='PRN2'; first_rec:=0
       || prn1:='PRNF'
       ?};
       prn2:='PRN'; rsep:=1
 ;~~}
 {
       c[10]:=TAB_SQL.KONTO;
       c[11]:=TAB_SQL.PROCENT;
       c[12]:=TAB_SQL.AMORT;
       rsep:=1;

       sum_amort+=TAB_SQL.AMORT;
       g_amort+=TAB_SQL.AMORT;
       first_line:=2;
       prn1:='PRN'; prn2:='PRN2';
       rsep:=1
 ;~~}
 {prn1:='PRN2';prn2:='PRN2';~~}
 {ELIF: PAR_WYDR.KONTA=3 & first_line=2}
 {     c[1]:=TAB_SQL.NRI;
       c[2]:=TAB_SQL.NST;
       c[3]:=TAB_SQL.ODDZIAL;
       c[4]:=TAB_SQL.JORG;
       c[5]:=TAB_SQL.GRUPA;
       c[6]:=TAB_SQL.NETTO;
       c[7]:=TAB_SQL.PODSTAWA;
       c[8]:=TAB_SQL.UMORZENI;
       c[9]:=TAB_SQL.AMORTN;
       c[10]:=TAB_SQL.KONTO;
       c[11]:=TAB_SQL.PROCENT;
       c[12]:=TAB_SQL.AMORT;
       rsep:=1; "PAR_WYDR.RSEP";

       sum_amort+=TAB_SQL.AMORT;
       g_amort+=TAB_SQL.AMORT;
       prn1:='PRN'; prn2:='PRN'
 ;~~}
 {FI}
 {prn1:='PRN'; prn2:='PRN'; first_rec:=0;~~}
 {
   {? PAR_WYDR.RODZAJ=1
   || {? PAR_WYDR.KONTA<=2
      || sum_konta:=TAB_SQL.KONTO
      || sum_konta:=TAB_SQL.NRI
      ?}
   ?}
 ;~~}
 {SUBSTITUTE: 'LINIA02A'}
 {FI}
 {OD}
{COMMENT} ---- podsumowanie czastkowe za ostatnim rekordem tabeli TAB_SQL ---- {COMMENT-}
 {COMMENT} ---- podsumowanie nie występuje dla excela ---- {COMMENT-}
{IF: ~rep_export()}
  {
   first_line:=3;
   {? PAR_WYDR.RODZAJ=1
   ||
      {? PAR_WYDR.KONTA<>3 || test_konta:=TAB_SQL.KONTO || test_konta:=TAB_SQL.NRI ?};
      {? sum_konta='' || test_konta:=''?}
   ?}
  ;~~}
  {IF: PAR_WYDR.RODZAJ=1 }
  {
   {? PAR_WYDR.KONTA=1 || _konta:='Zestaw '@; c[1]:=_konta+sum_konta+' razem:'@
   |? PAR_WYDR.KONTA=2 || _konta:='Konto '@; c[1]:=_konta+sum_konta+' razem:'@
   |? PAR_WYDR.KONTA=3 || _konta:='Środek '@; c[1]:=_konta+' razem:'@
   ?};
   {? PAR_WYDR.KONTA<>3
   || c[2]:=''; c[3]:=''; c[4]:=''; c[5]:=g_amort;
      prn1:='PRN'; prn2:='PRNF'
   |? PAR_WYDR.KONTA=3
   || c[2]:=g_amort; c[3]:=''; c[4]:=''; c[5]:='';
      c[6]:=''; c[7]:=''; c[8]:='';
      prn1:='PRN2'; prn2:='PRNF'
   ?};
   rsep:=1
  ;~~}
  {FONT: 'T8GB'}{SPACE: 'C'}{IF: lineleft()<=4 & lineleft()<>0}{PAGE}{FI}{SUBSTITUTE: 'LINIA02A'}
  {
  rsep:=1; "PAR_WYDR.RSEP";
  prn1:='PRNF'; prn2:='PRNF'
  ;~~}
  {FONT: 'T8G'}{SPACE: 'C'}
 {FI}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<=4}{PAGE}{FI}
{prn1:='PRNF';prn2:='PRNF';~~}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
   c[1]:='Ogółem: ';
   {? PAR_WYDR.KONTA<>3 || c[5]:=sum_amort || c[2]:=sum_amort ?};
   rsep:=1; first_line:=4
;~~}
{{? vp=1 & PAR_WYDR.KONTA<>3 || prn1:='PRN' ?};~~}{SUBSTITUTE: 'LINIA02A'}
{prn1:='PRNF';prn2:='PRNF';~~}
{FI}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
:!UTF-8
{TITLE:G. Zestawienie produkcji w toku}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  VAR_DEL.delete('PRN','PRNSUM');
  ZL.cntx_psh;
  ZLIM.cntx_psh();
  TMAT.cntx_psh();
  PRN:=obj_new(@.CLASS.PRINT,6);
  PRN.s_frame();
  PRNSUM:=obj_new(@.CLASS.PRINT,6);
  PRNSUM.s_frame();
  VAR_DEL.delete('__TAB');
  __TAB:=tab_tmp(1,
     'KK','INTEGER','Konto kosztowe',
     'SYMKK','STRING[20]','Symbol KK',
     'SYM','STRING[20]','Symbol zlecenia',
     'DATA','DATE','Data otwarcia',
     'PROD','REAL','Wart.prod.w tok'
  );
  prn1:='PRN';
  prn2:='PRNSUM';
  tryb:=rep_export();
  waslbar:=0;
  dalej:=0;
  sumakw:=0;
  color:='';
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  ZL.cntx_pop;
  ZLIM.cntx_pop();
  TMAT.cntx_pop();
  obj_del(PRN);
  obj_del(PRNSUM);
  obj_del(__TAB);
  &dalej;
  &prn1;
  &sumakw;
  &color;
  &lm; &ll; &vp; &lmt; &rsep; &tryb;
  VAR_DEL.delete('ZLST')
}
{COMMENT}
  Wydruk Zestawienie produkcji w toku.
   [MK] - 2002/12/05 -
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{COMMENT}--Definicja wiersza zestawienia produkcji w toku.--------------------{COMMENT-}
{
{? tryb=1
|| PRN.add("__TAB.SYMKK",15,'Konto'@)
?};
PRN.add("__TAB.SYM",20,'Symbol zlecenia'@);
PRN.add("form(__TAB.DATA)",10,'Data otwarcia'@);
PRN.add("form(__TAB.PROD,,2)",20,'Produkcja w toku'@,1);
{? tryb=1
|| PRNSUM.add("'SUMA:'",28,'Symbol zlecenia'@,1,,,,,,,3)
|| PRNSUM.add("'SUMA:'",28,'Symbol zlecenia'@,1,,,,,,,2)
?};
PRNSUM.add("form(sumakw,,2)",20,'Symbol zlecenia'@,1);
PAR_WYDR.TITLE:='ZESTAWIENIE PRODUKCJI W TOKU'@;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}  'Rok: %1 Okres: %2'@[$ST.AR,form(ST.AM,-2)]}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{MACRO: 'utwtab'}
{  ZL.cntx_psh();
   ZL.index('STAN');
   ZL.prefix('N','O');
   {? ZL.first()
   ||
      {!
      |?
         VAR_DEL.delete('ZLST');
         ZLST:=exec('zlst','zl_common',ZL.ref());
         _zlstndx:=ZLST.ndx_tmp(,,'ZL',,,'WYD',,,'KOD',,);
         {? ZL.TYP().WP='P' & ZL.IL<>0
         ||
            exec('openmask','zl_common',ZL.ref());
            _kk:={? ZL.KK<>null()
                 || _ref:=#ZL.KK; ZL.KK().SYM
                 || _ref:=0; ''
                 ?};
            __TAB.KK:=_ref;
            __TAB.SYMKK:=_kk;
            __TAB.SYM:=ZL.SYM;
            __TAB.DATA:=ZL.OD;
            _coef_nwyk:={? ZL.IL<>0 || (ZL.IL-ZL.ILWYK)/ZL.IL || 0 ?};
            {? _coef_nwyk<0 || _coef_nwyk:=0 ?};
            _kwota:=0;
            ZLIM.cntx_psh();
            ZLIM.index('ZKN');
            ZLIM.prefix(ZL.ref());
            'Obsługują surowce limitowane i nielimitowane';
            {? ZLIM.first()
            || _wyd:={? ZLIM.PFAZ<>null()
                     || ZLIM.PFAZ().WYD
                     || ZL.JORG
                     ?};
               {!
               |? ZLST.index(_zlstndx);
                  ZLST.prefix($ZL.ref(),$_wyd,$ZLIM.KTM);
                  {? ZLST.last()
                  || _pobr:=ZLST.IL;
                     {? ZLST.IL=0 || _cena:=0 || _cena:=ZLST.WAR/ZLST.IL ?}
                  || _pobr:=0; _cena:=0
                  ?};
                  {? _pobr<>0
                  || _kwota+=_pobr*_coef_nwyk*_cena
                  ?};
                  ZLIM.next()
               !}
            ?};
            ZLIM.cntx_pop();
            __TAB.PROD:=_kwota;
            __TAB.add();
            _kwota:=0
         ?};
         ZL.next()
      !}
   ?};
   ZL.cntx_pop();
~~}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{SUBSTITUTE: 'utwtab'}
{FOR: __TAB}
{DO}
 {FIRST}
     {IF: ~tryb & lineleft<7}
     {PAGE}
     {FI}
     {IF: tryb<>1}{<{lmt+1} 'Konto: %1'@[__TAB.SYMKK]}{FI}
     {FONT: 'T10B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; bylo:=1;~~}
     {kk:=__TAB.KK;~~}
 {FIRST-}
 {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIA0'}
 {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T10B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
 {FI}
 {sumakw+=__TAB.PROD;~~}
{TOTAL: __TAB.KK}
  {IF: tryb<>1}{SUBSTITUTE: 'LINIAFS'}{FI}
  {IF: tryb=2}{<{lmt+1} ''}{FI}
  {IF: kk<>__TAB.KK}
     {IF: ~tryb & lineleft<7}
     {PAGE}
     {FI}
     {IF: tryb<>1}{<{lmt+1} 'Konto: %1'@[__TAB.SYMKK]}{FI}
     {IF: tryb<>1}{FONT: 'T10B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; bylo:=1;~~}{FI}
     {kk:=__TAB.KK;sumakw:=0;~~}
  {FI}
{OD}
{IF: tryb=1 & __TAB.size()}{SUBSTITUTE: 'LINIAF'}{FI}


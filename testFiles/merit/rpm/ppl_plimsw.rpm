:!UTF-8
{TITLE:Limity świadczeń}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__param','__edit');
   tresc:='plimsw';
   PAR_WYDR.TITLE:='Limity świadczeń'@;

   par:=obj_new(6); par[4]:=1; par[6]:=0;
   _upr:=exec('uprawnieniawrap','pkd','limitach świadczeń'@,'ZWS_PAR_PSWI','PPL_GRP_ASWI');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
               'OD','DATE','Data od',
               'DO','DATE','Data do');
      __param.OD:=date(,1,1);
      __param.DO:=date(,12,0);
      __param.add();
      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_plimsw');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'OD',,,11,,,'Data od'@);
      __param.win_efld(__edit,,'DO',,,11,,,'Data do'@);
      __param.efld_opt(__edit,'mark=1',,'OD');
      __param.efld_opt(__edit,'mark=1',,'DO');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[6]:=__param.edit(
         " {? (_chk:=__CHK.record(__param,,'OD','DO'))<>''
           || return(_chk)
           |? __param.DO<__param.OD
           || FUN.emsg(
                 '\"%1\" nie może być wcześniejsza niż \"%2\".'@
                 [MS.name(__param,'DO'),MS.name(__param,'OD')]
              );
              return('OD')
           || 1
           ?}
         ")
      || par[1]:=obj_new(@.CLASS.PRINT,7);
         par[1].add("IS_OGR.IS_DEF().OPIS",60,'Nazwa świadczenia'@);
         par[1].add("par[2]",10,'Typ'@);
         par[1].add("par[3]",18,'Potrącenie'@);
         par[1].s_frame()
      ?};
      prn1:='par[1]';
      lmt:=rsep:=0;
      vp:=1;
      par[2]:=par[3]:=''
   ?};
   b_rat:=h_rat:=1
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__param','__edit')
}
{COMMENT}OLD: plimsw.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[6]}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   {FONT: 'T8'}{ h_rat:=charleft; ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft/h_rat; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: od %1 do %2 '@[__param.OD$1,__param.DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'swiad'}
{FONT: 'W12'}
{SPACE: 'A'}
{<{ceil(lmt*b_rat)+1}}{IS_LIM.OD} - {IS_LIM.DO}
{<{ceil(lmt*b_rat)+1}}{' Kwota zwolnienia: '@} {IS_LIM.KW} {' zł'@}
{FONT: 'T8'}
{SPACE: 'B'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft; ~~ }
{FONT: 'T8'}{ b_rat/=charleft; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft-par[1].width())/2); ~~ }
{FOR: IS_LIM}{INDEX: 'OD'}
{DO}
  {ENTIRE}
  {IF: __param.OD<=IS_LIM.OD & IS_LIM.DO<=__param.DO}
    {SUBSTITUTE: 'swiad'}
    {REP: par[1].fhbar(lmt)}
    {FOR: IS_OGR}{INDEX: 'OPIS'}{PREFIX: IS_LIM.ref}{DO}
        { {? IS_LIM.MIES = 'T'
             || par[2]:='Miesięczne'@
             || par[2]:='Okresowe'@
          ?};
          {? IS_LIM.RODZAJ = 'U'
             || par[3]:='Ubezpieczenie'@
             || par[3]:='Podatek'@
          ?};~~
        }
        {par[1].ini_line();~~}
        {REP: par[1].fline(lmt)}
    {OD}
    {REP: par[1].flbar(lmt)}
  {FI}
  {ENTIRE-}
{OD}
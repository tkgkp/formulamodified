:!UTF-8
{TITLE:Wydruk składnika wypłaty}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   tresc:='pskllp';
   PAR_WYDR.TITLE:='Wykaz wybranych składników wypłaty'@;
   par:=obj_new(9);
   par[9]:=rep_export();
   P.cntx_psh();
   vp:=0;
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || {? par[9]
      || R.cntx_psh();
         R.prefix();
         _maxcol:=R.size();
         R.cntx_pop()
      || _maxcol:=10
      ?};
      par[1]:=obj_new(@.CLASS.PRINT,_maxcol+3);
      {? ~par[9]
      || par[6]:=obj_new(@.CLASS.PRINT,_maxcol+1);
         par[6].add("'Razem'",51,'Razem'@);
         par[1].add("par[4]",5,'Lp.'@,1,,,,,',,\'9.\'')
      ?};
      par[1].add("par[3]:=0; P.OSOBA().NAZWISKO",20,'Nazwisko'@);
      par[1].add("OSOBA.PIERWSZE",20,'Imię'@);
      params_set('P',exec('wybierz_parses','pracownik','PPL').P);
      {? (_rub:=exec('code_grp','profile',,'listatab',,_maxcol))<>''
      || par[7]:=obj_new(_maxcol);
         {! _n:=1.._maxcol
         |! par[7][_n]:=0
         !};
         R.cntx_psh();
         R.index('RUBKOD');
         R.prefix();
         par[2]:='';
         STR.split(1-_rub-1,',');
         {! _cnt:=1.._maxcol
         |? _kod:=#STR.get_word()
         |! {? R.find_key(_kod)
            || _s:='par[7]['+$_cnt+']';
               _f:='_a:=FUNKCJE.L('+$R.RN+');'+
                   'par[3]+=(_a<>0);'+_s+'+=_a;_a';
               par[1].add($_f,12,R.RT,1,,,,,'12,2,\'9,\'');
               {? ~par[9] || par[6].add($_s,12,R.RT,1,,,,,'12,2,\'9,\'') ?};
               par[2]+=', '+$R.RN
            ?}
         !};
         R.cntx_pop()
      ?};

      par[1].s_frame();
      {? ~par[9] || par[6].s_frame() ?};
      prn1:='par[1]';
      rsep:=PAR_WYDR.RSEP;
      lmt:=par[3]:=par[8]:=0;
      vp:=par[4]:=par[5]:=1
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc')
}
{COMMENT}OLD: pskllp.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~vp}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Uwzględniane składniki: '@+(1-par[2])}
   {<{lmt+1}'Miesiąc kalendarzowy: '@+date(O.R,O.M,1)$8}
   {<{lmt+1}'Typ listy: '@+O.T().T}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{ par[8]:=0; ~~}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~par[9]} {SUBSTITUTE: 'FOOT'} {FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: par[1].ini_line(); par[3]}
   {IF: rsep & par[8]}
   {IF: lineleft()<3}
   {PAGE}
   {ELSE}
   {REP: _f:=prn1+'.fcbar(lmt)'; ($(_f))()}
   {FI}
   {FI}
   { par[4]+=1; ~~ }
   {WHILE: par[1].next()}{DO}
   {REP: par[8]:=1; par[1].fline(lmt)}
   {OD}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{IF: ~par[9] & par[4]>1}
   {IF: lineleft() & lineleft()<=2}
      {PAGE}
   {FI}
   {FOOTER: 0}
   {FOOTER-}
   {REP: '{<{lmt+1}}'+par[1].fjbar(par[6])}
   { par[6].ini_line(); ~~ }
   {WHILE: par[6].next()}{DO}
      {REP: par[6].fline(lmt)}
   {OD}
   {REP: par[6].flbar(lmt)}
{FI}
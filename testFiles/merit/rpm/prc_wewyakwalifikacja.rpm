:!UTF-8
{TITLE: Różnice pomiędzy zaewidencjonowanym czasem pracy a kwalifikacją}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'E','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   _par:=params_get();
   _www:=0;

   {? type_of(_par)>100 &
      var_pres('P',_par)=type_of(SYSLOG) &
      var_pres('KOL',_par)=type_of('') &
      var_pres('OD',_par)=type_of(date) &
      var_pres('DO',_par)=type_of(date)

   || _www:=1;
      _P:=_par.P;
      _kol:=_par.KOL;
      _od:=VAR_EDIT.D_OD:=_par.OD;
      _do:=VAR_EDIT.D_DO:=_par.DO;
      _bs:=(var_pres('BEZ_SZEF',_par)=type_of(0) & _par.BEZ_SZEF<>0);
      _bez_or:=(var_pres('BEZ_OR',_par)=type_of(0) & _par.BEZ_OR<>0)

   || _P:=exec('wybierz_parses','pracownik','PRC').P;
      params_set('P',_P);
      {? _P.size()=0
      || return()
      ?};
      _kol:='SQL';
      VAR_EDIT.D_OD:=date(,,1);
      VAR_EDIT.D_DO:=date(,,0);
      VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_OD');
      VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_DO');
      VAR_EDIT.win_edit('ZAKR_DAT');
      _ok:=VAR_EDIT.edit("__CHK.record(VAR_EDIT,,'D_OD','D_DO')");
      VAR_EDIT.efld_opt('ZAKR_DAT','mark=0',,'D_OD');
      VAR_EDIT.efld_opt('ZAKR_DAT','mark=0',,'D_DO');
      {? _ok<>0
      || _od:=VAR_EDIT.D_OD;
         _do:=VAR_EDIT.D_DO
      || return()
      ?};
      _bs:=_bez_or:=0
   ?};

   _TAB:=exec('spr_wewy_kwal','prc_dane',_P,_kol,_od,_do);
   {? _bs<>0 & _www<>0
   || _COL:=sql('select count(*) from :_a group by :_a.SZEF_NAZ, :_a.SZEF_IM',_TAB);
      {? _COL.size()=1
      || _bs:=1
      ?}
   ?};

   params_set('P',_TAB);

   {? _TAB.first()
   || VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','pb','bez_or');
      PAR_WYDR.TITLE:='Różnice pomiędzy zaewidencjonowanym czasem pracy a kwalifikacją'@;
      exec('czytaj','#stalesys',,XINFO);

      PRN:=obj_new(@.CLASS.PRINT,14-_bs);
      PRN.add("params_get().P.T",9,MS.name(_TAB,'T'));
      PRN.add("_P:=params_get().P; _P.NAZWISKO+' '+_P.PIERWSZE",30,'Nazwisko i imię współpracownika'@);
      PRN.add("params_get().P.SYMBOL",16,'Symbol jednostki organizacyjnej'@);
      PRN.add("params_get().P.OPIS",30,'Opis jednostki organizacyjnej'@);
      PRN.add("params_get().P.DATA$1",10,MS.name(_TAB,'DATA'));
      PRN.add("params_get().P.PLAN_OD$1",5,MS.name(_TAB,'PLAN_OD'));
      PRN.add("params_get().P.PLAN_DO$1",5,MS.name(_TAB,'PLAN_DO'));
      PRN.add("params_get().P.MARG_WE$1",9,'Margines wejścia'@);
      PRN.add("params_get().P.MARG_WY$1",9,'Margines wyjścia'@);
      PRN.add("params_get().P.WE$1",7,'Godzina wejścia'@);
      PRN.add("params_get().P.WY$1",7,'Godzina wyjścia'@);
      PRN.add("params_get().P.NADMIAR$1",8,MS.name(_TAB,'NADMIAR'));
      PRN.add("params_get().P.WNIO_NAD$1",11,'Wnioskowane nadgodziny'@);
      {? _bs=0
      || PRN.add("_P:=params_get().P; _P.SZEF_NAZ+' '+_P.SZEF_IM",30,'Nazwisko i imię przełożonego'@)
      ?};
      PRN.s_frame();

      prn1:='PRN';
      rsep:=PAR_WYDR.SEP;
      vp:=h_rat:=1;
      bez_or:=_bez_or;
      pb:=0
   ?}
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','pb','bez_or')
}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{  h_rat:=charleft; ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{IF: ~bez_or}
   {INCLUDE: p_obszarprc.rpi}
{FI}
   {<{ceil(lmt*h_rat)+1}'Zakres dat od: '}{VAR_EDIT.D_OD}{' do: '}{VAR_EDIT.D_DO}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: params_get().P}
{DO}
   {SUBSTITUTE: 'LINIA0'}
{OD}
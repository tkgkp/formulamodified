:!UTF-8
{TITLE: Przeterminowane rozrachunki}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
          PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN: PRN:=obj_new(@.CLASS.PRINT,10); PRN.sl_frame(); w:=obj_new(10); licz:=0;
        color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=0;
        head:=PAR_WYDR.HEAD; vp:=1; rsep:=PAR_WYDR.RSEP;
        PAR_WYDR.TITLE:='PRZETERMINOWANE ROZRACHUNKI'@;
        tab_ana:={? var_pres('TAB_WID')>0 || TAB_WID || TAB_WIN ?};
        tab_ana.cntx_psh(); {! _i:=1..10 |! w[_i]:='' !}; dalej:=0; s_sign:=''
}
{END: tab_ana.cntx_pop(); VAR_DEL.delete('tab_ana','PRN','w'); &licz; &dalej;
      &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &head; &s_sign}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_tmptree.rpm
Utworzony:   2005-06-13 [AMK] Artur Makos
==================================================
Zawartosc: Szczegoly analizy rozrachunkow (pulpit windykatora)
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{ PRN.add("w[1]",45,'Opis pozycji'@);
  PRN.add("w[2]",10,'Data otw.'@);
  PRN.add("w[3]",10,'Termin pł.'@);
  {! licz:=1..ilekolr  |? ($('PRN.add("w['+$(licz+3)+']",14,co_w_kol[licz],1)'))() !};~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}
{<3 'Parametry analizy:'@}
{<6 'Waluta: %1 - %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}{<60
 {? WIND_N.ZAK_KON='M' || 'Maska kont: %1'@[WIND_N.KON_MAS] || 'Prefix kont: %1'@[WIND_N.KON_PRE]?}}{<115
{? WIND_N.Z='T' ||  'Generować pozycje rozr. po terminie: tak'@ ||  'Generować pozycje rozr. po terminie: nie'@ ?}}
{<6 {? WIND_N.ODD=0 || 'Jednostka księgowa: wszystkie jednostki księgowe'@ || WIND_N.ODD().OD ?}}{<60
{?WIND_N.ZAK_ROZ='T' ||  'Rozrachunki przed terminem poniżej %1 dni'@[$WIND_N.PRZED_TP] ||   'Rozrachunki przed terminem powyżej %1 dni'@[$WIND_N.PO_TP] ?}}{<115
 'Opis: %1'@[WIND_N.OPIS]}
{<6 'Operator: %1'@[WIND_N.USER]}{<60'Ostatnie płatności %1 dni wstecz'@[$WIND_N.OST_PL]}{<115
{? WIND_N.OZNACZ='T' ||  'Tylko oznaczone do monitowania: tak'@ ||  'Tylko oznaczone do monitowania: nie'@ ?}}
{<6 {? -WIND_N.ZAK_KH='b' || 'Kontrahenci: wybrani'@ || 'Kontrahenci: wszyscy'@ ?}}{<60
 'Kontrahenci powyżej wartości: %1'@[form(WIND_N.OD_KW,,2,' ,')]}
{FI}
{FONT: 'T8B'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}{ vp:=1;~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'C'}{ dalej:=tab_ana.first();~~}
{WHILE: dalej}
{DO}
{IF: lineleft()=1 | lineleft()=2}{PAGE}{FI}
{ {? tab_ana.POZIOM=4
  || w[1]:=tab_ana.WARTTRE1; w[2]:=$tab_ana.DO; w[3]:=$tab_ana.TZ
  || w[1]:=tab_ana.WART; w[2]:=w[3]:=''
  ?};
  {! licz:=1..ilekolr |! w[licz+3]:=form(($('tab_ana.P'+$licz))(),,2,' ,') !};
~~}
{SUBSTITUTE: 'LINIA0'}
{IF: tab_ana.SYM_ZEW<>''}
{ {! _i:=1..10 |! w[_i]:='' !}; w[1]:=tab_ana.SYM_ZEW; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{ rsep:=1;~~}
{FI}
{ dalej:=tab_ana.next();~~}
{OD}
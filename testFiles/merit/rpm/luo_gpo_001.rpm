:!UTF-8
{TITLE:A. Zestawienie odebranych odpadów}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','PRNS','w','s','tryb','kkh');
   PRN:=obj_new(@.CLASS.PRINT,20); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,20); PRNS.s_frame();
   tryb:=rep_export();

   w:=obj_new(20);
   s:=obj_new(20);

   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   'parametry z tabeli WYDRUKI';
   _wydr_naz:='gpo_001';
   WYDRUKIL.win_edit('GPO_001');
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz,);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ?};
   {? WYDRUKIL.GPOD=null() & GPOD.size()>0
   || WYDRUKIL.GPOD:=GPOD.ref()
   ?};
   dat_min:=dat_max:=date(0,0,0)
;~~}
{END:
   VAR_DEL.delete('PRN','PRNS','w','s','tryb','kkh');
   exec('zle_open','open_tab',ST.ODDZ,2-$ST.AR,form(ST.AM,-2));
   &i;
   &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &dat_min;&dat_max;
   &odp_il
;~~}
{EXIT: ~WYDRUKIL.edit("{? WYDRUKIL.OD_DATY>WYDRUKIL.DO_DATY & WYDRUKIL.DO_DATY<>date(0,0,0)
                       || FUN.info('Niepoprawny zakres dat.'@);
                          'OD_DATY'
                       |? WYDRUKIL.GPOD=null()
                       ||   FUN.info('Nie wybrano gminnego punktu odbioru odpadów.');
                          'GPOD'
                       || ''
                       ?}")}
{  WYDRUKIL.put(1);
   {? tryb<>1
   || PRN.add("w[1]",5,'Lp.',1)
   ?};
   PRN.add("w[2]",10,'Kod odpadu'@);
   PRN.add("w[3]",40,'Nazwa odpadu'@);
   PRN.add("form(w[4],,3)",12,'Ilość'@,1);
   PRN.add("w[5]",8,'jm'@);

   PRNS.add("s[1]",61);
   PRNS.add("form(s[2],,3)",12,'Ilość'@,1);
   PRNS.add("s[3]",8,'jm'@)
;~~}
{COMMENT}
color, prn1, lm, ll, vp, lmt, rsep - zmienne pomocnicze do wydrukow graficzn.
{COMMENT-}
{  PAR_WYDR.TITLE:='Zestawienie odebranych odpadów'@;
   prn1:='PRN'; prn2:='PRNS'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Data odbioru odpadów: od %1 do %2'@[$WYDRUKIL.OD_DATY,$WYDRUKIL.DO_DATY]}
{<{lmt+1}}{'Punkt odbioru odpadów: %1 %2'@[WYDRUKIL.GPOD().KOD,WYDRUKIL.GPOD().NAZ]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'T8G'}{SPACE: 'C'}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: tryb<>1}
{LINE: 1}
{ELSE}
{SUBSTITUTE: 'LINIAF'}
{FI}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; w[1]:=0;
  dat_min:=WYDRUKIL.OD_DATY; dat_max:=WYDRUKIL.DO_DATY;
~~}
{s[1]:='Razem';s[2]:=0;s[3]:='kg';~~}
{FOR: 'ODP'}
{INDEX: 'KOD'}
{DO}
  {odp_il:=0;~~}
  {FOR: 'OKR'}
  {INDEX: 'OKR'}
  {DO}
    {IF: (dat_min=date(0,0,0) | dat_min~1 <= OKR.ROK) & (dat_max=date(0,0,0) | OKR.ROK <= dat_max~1) & OKR.MC=1}
      {GPODPOZ.use('gpodb'+ST.ODDZ+(2-$OKR.ROK));~~}
      {FOR: 'GPODPOZ'}
      {INDEX: 'D'}
      {PREFIX: WYDRUKIL.GPOD}
      {DO}
        {IF: ((WYDRUKIL.OD_DATY<=GPODPOZ.D | WYDRUKIL.OD_DATY=date(0,0,0))
             & (WYDRUKIL.DO_DATY>=GPODPOZ.D | WYDRUKIL.DO_DATY=date(0,0,0)))
             & GPODPOZ.ODP=ODP.ref()}
          {odp_il+=GPODPOZ.WAGA;~~}
        {FI}
      {OD}
    {FI}
  {OD}
  {IF: odp_il<>0}       
   {
    w[1]+=1;
    w[2]:=ODP.KOD;
    w[3]:=ODP.NAZ;
    w[4]:=odp_il;
    w[5]:='kg';
    s[2]+=odp_il;
    ~~}
   {SUBSTITUTE: 'LINIA0'}
  {FI}
{OD}
{IF: tryb<>1}
{SUBSTITUTE: 'LINIA02'}
{prn1:='PRNS';prn2:='PRN'; vp:=0; ~~}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{FI}
{PAGE}
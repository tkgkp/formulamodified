:!UTF-8
{TITLE:D. Zamówienia sprzedaży - zysk (z dokumentami magazynowymi)}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('w','s','sg','PRN','tryb','kza');
   w:=obj_new(7); "--wartosci kolumn--";
   s:=obj_new(4); {! _ii..4 |! s[_ii]  :=0 !}; "--razem--";
   sg:=obj_new(4); {! _ii..4 |! sg[_ii] :=0 !}; "--razem grupa--";
   doklw:=2; "--dokladnosc wartosci--";
   lp:=0;
   PRN:=obj_new(@.CLASS.PRINT,7); PRN.s_frame();
   tryb:=rep_export();
   kza:='';

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   wyjscie:=1;

   _odd:=date(ST.AR,ST.AM,1);
   _dod:=date(ST.AR,ST.AM,0);
   _wydr_naz:='zkns_004';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ||
      _odd:=WYDRUKIL.OD_DATY;
      _dod:=WYDRUKIL.DO_DATY
   ?};

   D_HELP.OD:=_odd;
   D_HELP.DO:=_dod;
   D_HELP.win_edit('ODDO');
   {? exec('upr_cm','ceny')=0
   || FUN.info('Brak uprawnień do wartości magazynowych.'@)
   |? D_HELP.edit("
         {? D_HELP.DO>=D_HELP.OD
         || ''
         || FUN.info('Błędny zakres dat.'@); 'DO'
         ?}")
   ||
      WYDRUKIL.index('WYDRUKI');
      WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
      {? WYDRUKIL.first()
      ||
         WYDRUKIL.OD_DATY:=D_HELP.OD;
         WYDRUKIL.DO_DATY:=D_HELP.DO;
         WYDRUKIL.put();
         wyjscie:=0
      ?}
   ?}
}
{END:
   VAR_DEL.delete('w','s','sg','PRN','tryb','kza');
   VAR_DEL.delete('wyjscie','X_Xw1','lp','doklw');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: exec('upr_cm','ceny')=0}{ FUN.info('Brak uprawnień do wartości magazynowych.'@);~~}{EXIT}
{ELIF: exec('upr_cs','ceny')=0}{ FUN.info('Brak uprawnień do wartości sprzedaży.'@);~~}{EXIT}
{FI}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1 || PRN.add("kza",20,'Zamówienie'@) ?};
{? tryb<>1 || PRN.add("w[1]",5,'Lp.'@,1) ?};
PRN.add("w[2]",20,'Dokument'@);
PRN.add("w[3]",30,'Kontrahent'@);
PRN.add("w[4]",15,'Wartość zakupu'@,1);
PRN.add("w[5]",15,'Koszty dodatkowe'@,1);
PRN.add("w[6]",15,'Wartość sprzedaży'@,1);
PRN.add("w[7]",15,'Zysk'@,1);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'ZK'     ,'STRING[20]'  ,'Symbol zamówienia',
   'ND'     ,'STRING[20]'  ,'Symbol dok. magazynowego',
   'KH'     ,'STRING[70]'  ,'Kontrahent',
   'WZ'     ,'REAL'        ,'Wartość zakupu',
   'KD'     ,'REAL'        ,'Koszty dodatkowe',
   'WS'     ,'REAL'        ,'Wartość sprzedaży',
   'Z'      ,'REAL'        ,'Zysk');
X_Xw1.index(X_Xw1.ndx_tmp(,,'ZK',,));

"--obliczenia w masce zbiorczej--";
ZK_N.index('ZAM_KL'); ZK_N.prefix('Z');
{? ZK_N.first()
||
   {!
   |? {? ZK_N.STAT_REJ<>'A'
         & (ZK_N.DP>=D_HELP.OD | D_HELP.OD=date(0,0,0)) & (ZK_N.DP<=D_HELP.DO | D_HELP.DO=date(0,0,0))
      ||
         ZK_RN.index('ZAM'); ZK_RN.prefix(ZK_N.ref());
         {? ZK_RN.first()
         ||
            ND.cntx_psh();
            {!
            |? ND.use('nagdo'+((8+ZK_RN.ND)+3));
               ND.prefix();
               {? ND.seek(BB.sqlint(ZK_RN.ND), 8+ZK_RN.ND)
               ||
                  exec('koszt_n','magdok_koszty',ND.ref(),$ZK_RN.ref());
                  X_Xw1.ZK:=ZK_N.SYM;
                  X_Xw1.ND:=ND.SYM;
                  X_Xw1.KH:=ZK_N.KH().KOD+' '+KH.NAZ;
                  X_Xw1.WZ:=wart_m;
                  X_Xw1.KD:=DISP.N_S;
                  X_Xw1.WS:=wart_s;
                  X_Xw1.Z:=X_Xw1.WS-X_Xw1.WZ-X_Xw1.KD;
                  X_Xw1.add()
               ?};
               ZK_RN.next()
            !};
            ND.cntx_pop()
         ?}
      ?};
      ZK_N.next()
   !}
?};
"--obliczenia w maskach archiwalnych--";
OKR.cntx_psh();
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   ZK_N.cntx_psh(); ZK_RN.cntx_psh();
   {!
   |? _maska:=ST.ODDZ+form(form(OKR.ROK,,,'99'),-2);
      ZK_N.use('zknag'+_maska);
      ZK_RN.use('zkhin'+_maska);
      ZK_N.index('ZAM_KL'); ZK_N.prefix('Z');
      {? ZK_N.first()
      ||
         {!
         |? {? ZK_N.STAT_REJ<>'A'
               & (ZK_N.DP>=D_HELP.OD | D_HELP.OD=date(0,0,0)) & (ZK_N.DP<=D_HELP.DO | D_HELP.DO=date(0,0,0))
            ||
               ZK_RN.index('ZAM'); ZK_RN.prefix(ZK_N.ref());
               {? ZK_RN.first()
               ||
                  ND.cntx_psh();
                  {!
                  |? ND.use('nagdo'+((8+ZK_RN.ND)+3));
                     ND.prefix();
                     {? ND.seek(BB.sqlint(ZK_RN.ND), 8+ZK_RN.ND)
                     ||
                        exec('koszt_n','magdok_koszty',ND.ref(),$ZK_RN.ref);
                        X_Xw1.ZK:=ZK_N.SYM;
                        X_Xw1.ND:=ND.SYM;
                        X_Xw1.KH:=ZK_N.KH().KOD+' '+KH.NAZ;
                        X_Xw1.WZ:=wart_m;
                        X_Xw1.KD:=DISP.N_S;
                        X_Xw1.WS:=wart_s;
                        X_Xw1.Z:=X_Xw1.WS-X_Xw1.WZ-X_Xw1.KD;
                        X_Xw1.add()
                     ?};
                     ZK_RN.next()
                  !};
                  ND.cntx_pop()
               ?}
            ?};
            ZK_N.next()
         !}
      ?};
      OKR.next()
   !};
   ZK_N.cntx_pop(); ZK_RN.cntx_pop()
?};
OKR.cntx_pop();

PAR_WYDR.TITLE:='Zamówienia sprzedaży - zysk (z dokumentami magazynowymi)'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział: %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1}}{'od daty: %1 do daty: %2'@[$D_HELP.OD,$D_HELP.OD]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozcycje wydruku                                         {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;  ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xw1}
{DO}
   {FIRST}{ kolejny:=0;~~}{FIRST-}
   {IF: tryb<>1 & kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {
   lp+=1;
   w[1]:=form(lp,,,'99');
   w[2]:=form(X_Xw1.ND);
   w[3]:=form(X_Xw1.KH);
   w[4]:=form(X_Xw1.WZ,,doklw,'.,');
   w[5]:=form(X_Xw1.KD,,doklw,'.,');
   w[6]:=form(X_Xw1.WS,,doklw,'.,');
   w[7]:=form(X_Xw1.Z,,doklw,'.,');
   kza :=form(X_Xw1.ZK);
   s[1]+=X_Xw1.WZ; sg[1]+=X_Xw1.WZ;
   s[2]+=X_Xw1.KD; sg[2]+=X_Xw1.KD;
   s[3]+=X_Xw1.WS; sg[3]+=X_Xw1.WS;
   s[4]+=X_Xw1.Z;  sg[4]+=X_Xw1.Z
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
   { kolejny:=0; rsep:=PAR_WYDR.RSEP;~~}
   {TOTAL: X_Xw1.ZK}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
      {IF: tryb<>1}
      {
      w[1] :=form(X_Xw1.ND);
      w[2] :=form(X_Xw1.KH);
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+3
      ;~~}
      {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {
      kolejny:=1; rsep:=0;
      PRN.n_frame();
      w[1]:=w[2]:='';
      w[3]:='Razem %1:'@[grp_fld()]; PRN.FORM[3]:=0;
      w[4]:=form(sg[1],,doklw,'.,');
      w[5]:=form(sg[2],,doklw,'.,');
      w[6]:=form(sg[3],,doklw,'.,');
      w[7]:=form(sg[4],,doklw,'.,')
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {FONT: 'T8G'}{SPACE: 'C'}
      {
      {! _ii..4 |! sg[_ii]:=0 !};
      PRN.s_frame();
      PRN.FORM[3]:=1
      ;~~}
      {FI}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1]:=w[2]:='';
w[3]:='Razem:'@; PRN.FORM[3]:=0;
w[4]:=form(s[1],,doklw,'.,');
w[5]:=form(s[2],,doklw,'.,');
w[6]:=form(s[3],,doklw,'.,');
w[7]:=form(s[4],,doklw,'.,');
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
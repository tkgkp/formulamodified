:!UTF-8
{TITLE: Sprawozdanie Z-06}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('tresc','par','__TPAR');
   tresc:='gus_z06';

   par:=obj_new(6);

   PAR_WYDR.TITLE:='Dane do sprawozdania Z-06'@;

   par[4]:=exec('gus_rok_mce','!pkd_roz_zsst','Z-06',,,,
      "  {? __TPAR.ROK<=2013
         || __CHK.err_fld(__TPAR,'ROK',1,'Sprawozdanie Z-06 dostępne jest od roku 2014.'@)
         || ''
         ?}
      "
   );
   par[3]:=tab_tmp(1,
      'DZIAL','REAL','Dział@',
      'TYT','STRING[110]','Tytuł'@,
      'KOL1','STRING[40]','Kolumna 1'@,
      'KOL2','STRING[40]','Kolumna 2'@
      );

   par[2]:=tab_tmp(2,
      'DZIAL','REAL','Dział'@,
      'IND','INTEGER','Index'@,
      'KOD','STRING[5]','Kod'@,
      'NAZWA','STRING[255]','Nazwa'@,
      'WAR1','REAL','Wartość kolumny 1'@,
      'WAR2','REAL','Wartość kolumny 2'@);

   {? par[4]
   || {? __TPAR.ROK>=2014
      || exec('spr_Z06_2014','!pkd_roz_zsst',__TPAR.ROK,__TPAR.MOD,__TPAR.MDO)
      ?}
   ?};

   par[1]:=obj_new(@.CLASS.PRINT,4);
   par[1].add("par[2].KOD",5,'Kod');
   par[1].add("par[2].NAZWA",100,'Nazwa');
   par[1].add("par[2].WAR1",20,par[3].KOL1,1,,,,,'12,2');
   par[1].add("par[2].WAR2",20,par[3].KOL2,2,,,,,'12,2');
   par[1].s_frame();

   par[6]:=1;
   prn1:='par[1]';
   lmt:=0;
   vp:=rsep:=1;
   h_rat:=1

}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','__TPAR')
}
{EXIT:~par[4]}
{COMMENT}OLD: gus_z06.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{FONT: 'T8'}{ h_rat:=charleft; ~~ }
{SUBSTITUTE: 'HEAD'}
{ h_rat:=charleft/h_rat; ~~ }
{<{ceil(lmt*h_rat)-9} 'DZIAŁ '+$int(par[3].DZIAL)+'. '+par[3].TYT}
{IF: par[6]}
{ par[6]:=0; ~~}
{FONT: 'T8'}{ h_rat:=charleft; ~~ }
{FONT: 'T8G'}{ h_rat:=charleft/h_rat; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres danych: %1 - %2'[date(__TPAR.ROK,__TPAR.MOD,1)$8,date(__TPAR.ROK,__TPAR.MDO,0)$8]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{FOR: par[2]}{PREFIX:par[3].DZIAL}
{DO}
   {IF: par[2].KOD='---'}
      {REP: par[1].flbar(lmt)}
      {IF: par[2].NAZWA<>''}
         {FONT: 'T8B'}{<{lmt+1} par[2].NAZWA}{FONT: 'T8'}
      {FI}
      {REP: par[1].fhbar(lmt)}
      { rsep:=0; ~~}
   {ELSE}
      {SUBSTITUTE: 'LINIA0'}
      { rsep:=1; ~~}
   {FI}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft-par[1].width())/2); ~~ }
{FOR: par[3]}
{DO}
   {IF:par[3].DZIAL>1}{PAGE}{FI}
   {SUBSTITUTE:
      obj_del(par[1]);
      par[1]:=obj_new(@.CLASS.PRINT,4);
      par[1].add("par[2].KOD",5,'Kod'@);
      par[1].add("par[2].NAZWA",100,'Nazwa'@);
      par[1].add("par[2].WAR1",20,par[3].KOL1,1,,,,,'12,2');
      {? +par[3].KOL2 || par[1].add("par[2].WAR2",20,par[3].KOL2,2,,,,,'12,2') ?};
      par[1].s_frame();
      tresc
   }
{OD}
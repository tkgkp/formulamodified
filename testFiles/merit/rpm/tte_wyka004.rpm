:!UTF-8
{TITLE:B. Zestawienie operacji wykonanych w miesiącu wg pracowników}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  ZLGB.cntx_psh();
  ZLGD.cntx_psh();
  ZGP.cntx_psh();
  ZL.cntx_psh();
  ndxzlgb1:=ZLGB.ndx_tmp(,1,
     'ZLGD','O',0,
     'ZLGD',,0,
     'P',,0,
     'GAT',,0
  );
  PRN:=obj_new(@.CLASS.PRINT,12);
  PRN.s_frame();
  PRNSUM:=obj_new(@.CLASS.PRINT,5);
  PRNSUM.s_frame();

  prn1:='PRN';
  prn2:='PRNSUM';
  tryb:=rep_export();
  __TAB:=tab_tmp(2,'OREF','INTEGER','Ref.do pracownika',
                   'DATA','DATE','DATA',
                   'IMIE','STRING[20]','Imię',
                   'NAZ','STRING[30]','Nazwisko',
                   'ZLEC','STRING[20]','Symb.zlecenia',
                   'PRZEW','STRING[20]','Przewodnik',
                   'KOD','STRING[50]','Wyrób',
                   'OPERACJA','STRING[50]','Operacja',
                   'CZPL','REAL','Czas planowany',
                   'CZRZECZ','REAL','Czas rzeczywisty',
                   'ILOSC','INTEGER','Ilość wyrobu',
                   'KWOTA','REAL','Kwota'
                   );
  waslbar:=0;
  sczrz:=0;
  sczpl:=0;
  bylo:=0;
  prac:=0;
  sumakw:=0;
  color:='';
  nazwisko:=pierwsze:=pesel:='';
  timedok:=exec('get','#params',500604,1);
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  ZLGB.cntx_pop();
  ZLGD.cntx_pop();
  ZGP.cntx_pop();
  ZL.cntx_pop();
  ZLGB.ndx_drop(ndxzlgb1);
  obj_del(PRN);
  obj_del(PRNSUM);
  obj_del(__TAB);
  &bylo;
  &prac;
  &sumakw;
  &sczrz;
  &sczpl;
  &prn2;
  &prn1;
  &color;
  &nazwisko;
  &pierwsze;
  &pesel;
  &timedok;
  &lm; &ll; &vp; &lmt; &rsep;
  &tryb
}
{COMMENT}
  Wydruk zestawienia operacji w miesiacu
   [MK] - 2002/11/29 - [7.62]
  Wywołanie: Wykonania produkcji -> Raporty -> Zestawienia
{COMMENT-}
 {INCLUDE: tte_wykx001.rpi}
{COMMENT}--Definicja wiersza zestawienia wykonanych operacji w miesiacu wg pracownika.--------------------{COMMENT-}
{
{? tryb=1
|| PRN.add("form(__TAB.NAZ)",30,'Nazwisko'@);
   PRN.add("form(__TAB.IMIE)",20,'Imię'@)
?};
PRN.add("form(__TAB.DATA)",10,'Data'@);
PRN.add("__TAB.ZLEC",17,'Zlecenie'@);
PRN.add("__TAB.PRZEW",19,'Przewodnik'@);
PRN.add("__TAB.KOD",10,'Wyrób'@);
PRN.add("__TAB.OPERACJA",40,'Operacja'@);
PRN.add("form(__TAB.ILOSC,,ST.DOKL)",15,'Ilość'@,1);
PRN.add("form(__TAB.CZRZECZ,,timedok)",11,'Czas rzeczywisty'@,1);
PRN.add("form(__TAB.CZPL,,timedok)",11,'Czas planowany'@,1);
PRN.add("form({? __TAB.CZPL=0 || 0 || __TAB.CZRZECZ/__TAB.CZPL ?},,2)",11,'Wyrobienie normy'@,1);
PRN.add("form(__TAB.KWOTA,,2)",15,'Kwota'@,1);
PRNSUM.add("''",126,'Opis'@,1);
PRNSUM.add("form(sczrz,,timedok)",11,'Opis'@,1);
PRNSUM.add("form(sczpl,,timedok)",11,'Opis'@,1);
PRNSUM.add("'SUMA'@",11,'Opis'@,1);
PRNSUM.add("form(sumakw,,2)",15,'Kod wydziału'@,1);
PAR_WYDR.TITLE:='ZESTAWIENIE OPERACJI WYKONANYCH W MIESIĄCU WG PRACOWNIKÓW'@;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1}}{'Oddział %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1} 'Rok: %1 Okres: %2'@[$ST.AR,form(ST.AM,-2)]}

{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------MACRO-----------------------{COMMENT-}
{MACRO: 'usttab'}
{  ZLGB.clear();
   ZLGB.index(ndxzlgb1);
   ZLGB.prefix(ST.OKR_REF);
   {? ZLGB.first()
   || {!
      |?
         {? ZLGB.P<>null()
         || nazwisko:=ZLGB.P().OSOBA().NAZWISKO;pierwsze:=OSOBA.PIERWSZE;pesel:=OSOBA.PESEL;
            {? P.f_find(nazwisko,pierwsze,pesel)
            ||
               __TAB.OREF:=#ZLGB.P;
               __TAB.DATA:=ZLGB.ZLGD().DT;
               __TAB.IMIE:=P.OSOBA().PIERWSZE;
               __TAB.NAZ:=P.OSOBA().NAZWISKO;
               __TAB.ZLEC:=ZLGB.ZLGD().ZL().SYM;
               __TAB.PRZEW:=ZLGB.ZLGD().ZGH().NRPRZ;
               __TAB.KOD:=ZLGB.ZLGD().ZL().KTM().KTM;
               __TAB.OPERACJA:=ZLGB.ZLGD().ZGP().OPIS;
               __TAB.CZPL:=ZLGB.TIME_P;
               __TAB.CZRZECZ:=ZLGB.TIME;
               __TAB.ILOSC:=ZLGB.IL;
               __TAB.KWOTA:=ZLGB.KW;
               __TAB.add()
            ?}
         ?};
         ZLGB.next()
      !}
  ?};
  ~~
}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{SUBSTITUTE: 'filtrprac'}
{SUBSTITUTE: 'usttab'}
{FOR: __TAB}
{DO}
 {FIRST}
   {IF: ~tryb & lineleft<7}
   {PAGE}
   {FI}
   {IF: tryb<>1}{<{lmt+1} 'Pracownik: %1 %2'@[__TAB.IMIE,__TAB.NAZ]}{FI}
   {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1;~~}
   {prac:=__TAB.OREF;~~}
 {FIRST-}
 {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIA0'}
 {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
 {FI}
 {sumakw+=__TAB.KWOTA;sczrz+=__TAB.CZRZECZ;sczpl+=__TAB.CZPL;~~}
{TOTAL: __TAB.OREF}
  {IF: tryb<>1}
     {IF: lineleft > 5} {SUBSTITUTE: 'LINIAFS'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIAFS'}
     {FI}
     {IF: prac<>__TAB.OREF}
        {IF: lineleft<7}
        {PAGE}
        {FI}
        {<{lmt+1} 'Pracownik: %1 %2'@[__TAB.IMIE,__TAB.NAZ]}
        {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1;~~}
        {prac:=__TAB.OREF;sumakw:=0;sczpl:=0;sczrz:=0;~~}
     {FI}
  {FI}
{OD}
{IF: tryb=1 & __TAB.size()}{SUBSTITUTE: 'LINIAF'}{FI}
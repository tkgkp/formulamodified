:!UTF-8
{TITLE:PIT-4R(11) formularz obowiązujący od roku 2020}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,5,5,5,5,1,1,'A4'}
{BEGIN:
   {? var_pres('pit')>100 || obj_del(pit) ?};
   pit:=obj_new(10);
   {! _nx:=1..obj_len(pit) |! pit[_nx]:='' !};
   USERS.cntx_psh();
   exec('czytaj','#stalesys',date(),KST);

   pit[1]:=KST.NIP;
   pit[4]:=4+VAT_DEK.OKRES;
   pit[5]:=KST.US().NU+
           {? +KST.US().UU || ', '+KST.US().UU || '' ?}+
           {? +KST.US().UK & +KST.US().MU ||
              ', '+(6+KST.US().UK)+' '+KST.US().MU || ''
           ?};
   pit[6]:=VAT_DEK.KOR;
  'nowe pole w PIT-4R od 2019 roku';
   pit[7]:='1';
   {? var_pres('KOR_RODZ',VAT_DEK)>0 || pit[7]:=VAT_DEK.KOR_RODZ ?};
   pit[8]:='N';
   {? VAT_DEK.POD=1 || pit[8]:='T' ?};
   {? pit[8]='N'
   || pit[9]:=KST.NAZWA;    'Platnik niebedacy osoba fizyczna'
   || pit[9]:=KST.PODNAZ+', '+KST.PODIME+', '+STR.gsub(KST.PODURDAT$4,'.','-'); 'Platnik bedacy osoba fizyczna'
   ?};
   pit[10]:=VAT_DEK.IMIE+' '+VAT_DEK.NAZ+{? VAT_DEK.STN<>'' || ', '+VAT_DEK.STN || '' ?};

   {! _nx:=1..obj_len(pit) |! pit[_nx]:=~(-pit[_nx]) !};
   pit[2]:=exec('upo_id','xml');

   {? var_pres('tab')>100 || obj_del(tab) ?};
   tab:=tab_tmp(2,
      'NR','INTEGER','Nr',
      'NRK','INTEGER','Nrk',
      'K1','INTEGER','Kwota',
      'K2','INTEGER','Kwota',
      'K3','INTEGER','Kwota',
      'K4','INTEGER','Kwota',
      'K5','INTEGER','Kwota',
      'K6','INTEGER','Kwota'
   );
   ind:=0; Podatek:=0; czy_raport:=0;
   DEKL_POD.use('pod_'+pit[4]);
   DEKL_POD.index('DEKL_PIT');
   DEKL_POD.prefix();

   {? VAT_DEK.KOR='T'
   || {? var_pres('__ord')>100 || obj_del(__ord) ?};
      __ord:=obj_new(13);
      {! _nx:=1..obj_len(__ord) |! __ord[_nx]:='' !};
      {? pit[8]='T'
      || __ord[1]:=KST.NIP;
         __ord[5]:=KST.PODNAZ;
         __ord[6]:=KST.PODIME;
         __ord[7]:=STR.gsub(KST.PODURDAT$4,'.','-')
      || __ord[1]:=KST.NIP;
         __ord[5]:=KST.NAZWA;
         __ord[6]:=KST.SKROT
      ?}
   ?};
   VAT_POZ.cntx_psh();
   exec('rpm_as_pdf_ini','rpm_pdf');
   VAR_DEL.delete('x','w','y');
   y:=0;
   x:=obj_new(6);
   w:=obj_new(6);
   VAR_DEL.delete('__OpWpla','__iter','__dodinf');
   __OpWpla:={? var_pres('OPISWPLA',VAT_DEK)>0
             || ~-exec('opis_korekty','edeklar','OPISWPLA')
             || ''
             ?};
  'nowe pola P_159, P_160, P_161 w PIT-4R od 2020 wersja 10 od 18.11.2020 roku';
  'nowe pola P_162, P_163, P_164 w PIT-4R od 2020 wersja 11 od 09.12.2020 roku';
  __dodinf:=obj_new('P_159','P_160','P_161','P_162','P_163','P_164');
  __dodinf.P_159:=exec('pole_covid','edeklar',1);
  __dodinf.P_160:=exec('pole_covid','edeklar',2);
  __dodinf.P_161:=exec('pole_covid','edeklar',3);
  __dodinf.P_162:=exec('pole_covid','edeklar',4);
  __dodinf.P_163:=exec('pole_covid','edeklar',5);
  __dodinf.P_164:=exec('pole_covid','edeklar',6)
}
{END:
   exec('rpm_as_pdf_end','rpm_pdf');
   USERS.cntx_pop();
   VAT_POZ.cntx_pop();
   {? VAT_DEK.KOR='T' || obj_del(__ord); &__ord ?};
   VAR_DEL.delete('x','w','y','ind','Podatek','czy_raport','pit','tab','__OpWpla','__iter','__dodinf');
   exec('stalesys','#stalesys')
}
{COMMENT}OLD: dek04r11.rpm{COMMENT-}
{COMMENT}OLD: dkw4r11g.rpi{COMMENT-}
{COMMENT}OLD: ord_zu3g.rpi{COMMENT-}
{IF: DEKL_POD.find_key('P','PIT-4R','PIT-4R')}
{EXIT:
   USERS.cntx_psh();
   USERS.prefix();
   _kod:=DEKL_POD.USER().KOD;
   USERS.cntx_pop();
   ~FUN.ask(
      'Za %1 rok wydrukowano deklarację podatkową %2\n'
      '(data: %3, operator: %4).\n'
      'Czy kontynuować wydruk?'@
      [pit[4],DEKL_POD.PIT,DEKL_POD.DATA$7,_kod]
   )
}{FI}
{  tab.prefix();
   VAT_POZ.index('NR_NRK');
   VAT_POZ.prefix(VAT_DEK.ref);
   {? VAT_POZ.first()
   || {!
      |? tab.NR:=VAT_POZ.NR;
         tab.NRK:=VAT_POZ.NRK;
         {! _ind:=1..6
         |! _kw:=($('VAT_POZ.K'+$_ind))();
            ($('tab.K'+$_ind))():=_kw;
            {? tab.NR>=1 || czy_raport+=_kw ?};
            {? tab.NR=10 || Podatek+=_kw ?}
         !};
         tab.add();
         VAT_POZ.next()
      !}
   ?};
   {? VAT_DEK.KOR='T'
   || __ord[13]:=exec('opis_korekty','edeklar');
      {! _nx:=1..obj_len(__ord) |! __ord[_nx]:=~(-__ord[_nx]) !};
      __ord[3]:=pit[2]
   ?};
   tab.prefix();
   ~~
}
{EXIT:
   {? ~(czy_raport>0)
   || FUN.info('Brak informacji do wykazania na deklaracji.'@+'\n'+'(zerowe kwoty podatku)'@);
      1
   || 0
   ?}
}
{REP: '{INCLUDE: ppl_dkw4r11g.rpi}'}
{IF: VAT_DEK.KOR='T' & __ord[13]<>''}
{REP: '{INCLUDE: ppl_ord_zu3g.rpi}'}
{FI}
{ DEKL_POD.clear(); DEKL_POD.blank();
  DEKL_POD.RODZAJ:='P'; DEKL_POD.MSC:=0;
  DEKL_POD.DATA:=date(); DEKL_POD.PIT:='PIT-4R';
  DEKL_POD.S1:=0; DEKL_POD.S2:=0; DEKL_POD.S3:=Podatek;
  DEKL_POD.USER:=OPERATOR.USER;
  DEKL_POD.add();
  ~~
}
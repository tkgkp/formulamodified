:!UTF-8
{TITLE:2. Wydruk kartek produkcyjnych przewodnika}
{PRINTER: INFO.W_PRN,,,,,'i','b',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  ZGH.cntx_psh();
  ZGP.cntx_psh();
  VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
  BARCODE_TYPE:=exec('barcode_type','kody_kresk');
  BARCODE_H:=1;
  PRN := obj_new(@.CLASS.PRINT, 6);
  PRN.s_frame();
  kreska :='─';
  ramka:='│';
  opcja:=0;
  temp:='';
  waslbar:=0;
  "---Pozioma wspolrzedna lewej czesci tabeli. Jest uzalezniona od sposobu";
  "---przyciagania tabeli na stronie. Jest wyliczana przez formule 'prleft'";
  "---umieszczona za definicja wiersza wydruku.";
  left:=0;
  width:=115;
  pop:='';
  nas:='';
  "-----------------------------------------------";
  "U W A G A!   TUTAJ KONFIGURUJ SZEROKOSC KOLUMN.";
  "- - - - - - - - - - - - - - - - - - - - - - - -";
  "---Szerokosc wydruku.";
  prnwidth:=82;
  "---Szerokosci poszczegolnych kolumn.";
  lpwd:=3;      "Lp.";
  inwd:=30;     "Imie i nazwisko";
  ilwd:=5;      "Ilosc wykonana";
  sg1wd:=8;     "Podpis 1";
  dtwd:=9;      "Data";
  sg2wd:=10;    "Podpis 2";
  "---Liczba pozycji kartki produkcyjnej.";
  pozcount:=2
}
{END:
  VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
  ZGH.cntx_pop();
  ZGP.cntx_pop();
  obj_del(PRN);
  &kreska;
  &opcja;
  &temp;
  &waslbar;
  &left;
  &width;
  &nas;
  &pop;
  &prnwidth;
  &ramka;
  &lpwd;
  &inwd;
  &ilwd;
  &sg1wd;
  &dtwd;
  &sg2wd;
  &pozcount
}
{COMMENT}
  Wyglad:
  ┌────────────────────────────────────────────────────────────────────┐
  │              Kartka produkcyjna nr {ZGH.NRPRZ-ZGP.NRP}             │
  ├────────────────────────────────────────────────────────────────────┤
  │ Zlecenie: {ZGH.ZLEC().SYM}       │ Wyrob:                          │
  │ XJM do wyk.: {ZGH.ILNPRZ}        │ {KOD - NAZ}                     │
  ├────────────────────────────────────────────────────────────────────┤
  │ Opis czynnosci:                                                    │
  ├────┬─────────────────────────────┬──────┬────────┬─────────────────┤
  │    │                             │ Il.  │        │Potwierdzenie odb│
  │ Lp.│ Imie i nazwisko             │ wyk. │ Podpis ├────────┬────────┤
  │    │                             │      │        │   Data │ Podpis │
  ├────┼─────────────────────────────┼──────┼────────┼────────┼────────┤
  │    │                             │      │        │        │        │
  └────┴─────────────────────────────┴──────┴────────┴────────┴────────┘
  Przewodnik do zlecenia.
  [SS] - 2000/09/15 - [7.22]
  [TS] - 2000/10/12 - [7.53] OWwSK003_Aneks1
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Definicje -> Przewodniki -> Drukuj
{COMMENT-}
{opcja:=~choice('Czy drukować kody kreskowe?'@,FUN.TYT,'ASK',1,1,,'Tak'@,'Nie'@);~~}
{DEFINEFONT: 'fa=TexGyreCursor,120,m,8'}
{FONT: 'fa'}
{"------------------------------------------------------------------------"; ~~}
{"---Zbior funkcji wykorzystywanych w biezacej definicji wydruku.         "; ~~}
{MACRO: 'functions'}
  {"--- B R A K   F U N K C J I   W Y D R U K U.                          "; ~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Sekcja akcji inicjalizujacych.                                       "; ~~}
{MACRO: 'miscelaneous'}
  {"--- B R A K   A K C J I   I N I C J A L I Z U J A C Y C H.            "; ~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Deklaracja funkcji wydruku.                                          "; ~~}
{SUBSTITUTE: 'functions'}
{"------------------------------------------------------------------------"; ~~}
{"---Wykonanie akcji inicjalizujacych.                                    "; ~~}
{SUBSTITUTE: 'miscelaneous'}
{"------------------------------------------------------------------------"; ~~}
{"---Okresl przyciaganie: 1 - do lewej, 2 - wysrodkowanie, 3 - do prawej. "; ~~}
{left:=exec('prleft','#print',2,width,prnwidth+4); ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Drukuje kartke produkcyjna dla biezacej pozycji przewodnika zlecenia."; ~~}
{"---Do wydruku ramek zostal wykorzystany obiekt PRN klasy PRINT."; ~~}
{MACRO: 'prnkart'}
{ENTIRE}

{ZGH.cntx_psh();~~}
{"┌────────────────────────────────────────────────────────────────────┐"; ~~}
  {<{left+1} PRN.hbcross + PRN.bartab*(prnwidth-2)
     }{<{left+prnwidth-1} PRN.hecross}
  {"│                            Kartka produkcyjna                      │"; ~~}
  {<{left} PRN.sep
     }{<{left}>{left+prnwidth} 'Kartka produkcyjna nr %1-%2'@[ZGP.NRZLP().NRPRZ,form(ZGP.NRP,,0,'9')]
     }{<{left+prnwidth+1} PRN.sep}
     {IF: opcja}
       {<{left} PRN.sep}{<{left+prnwidth+1} PRN.sep}
       {temp:={? ZGP.IDEAN<>'' || ZGP.IDEAN || form(#ZGP.ref(),-7,,'99') ?};~~}
       {<{left} PRN.sep}{<10>50}{BARCODE: temp;(+temp)*2;BARCODE_H;BARCODE_TYPE}{FONT: 'fa'}{<{left+prnwidth+1} PRN.sep}
     {FI}
  {"├────────────────────────────────────────────────────────────────────┤"; ~~}
  {<{left+1} PRN.bcross + PRN.bartab*(prnwidth-2)
     }{<{left+prnwidth-1} PRN.ecross}
  {"│ Zlecenie:                        │ Wyrob:                          │"; ~~}
  {<{left} PRN.sep+ 'Zlecenie: %1'@[ZGP.NRZLP().ZLEC().SYM]
     }{<{left+prnwidth%2} PRN.sep + 'Produkt:'@
     }{<{left+prnwidth+1} PRN.sep}
  {"│ XJM do wykonania                 │ <kod materialu - nazwa mat.>    │"; ~~}
  {<{left} PRN.sep + 'XJM do wyk: %1 %2'@[form(ZGH.ILNPRZ,,ZL.KTM().DOKL),M.J().KOD]
     }{<{left+prnwidth%2} PRN.sep + ZGH.ZLEC().KTM().KTM + ' - ' + ((prnwidth%2-(+(ZL.KTM().KTM)+5))+M.N)
     }{<{left+prnwidth+1} PRN.sep}
  {"├────────────────────────────────────────────────────────────────────┤"; ~~}
  {<{left+1} PRN.bcross + PRN.bartab*prnwidth-2
     }{<{left+prnwidth+1} PRN.ecross}
  {"│ Operacja:                                                          │"; ~~}
  {<{left} PRN.sep + 'Opis czynności: %1'@[ZGP.OPIS]
     }{<{left+prnwidth+1} PRN.sep}
       {<{left} PRN.sep + 'Operacje złożone: %1'@[VAR.STRING]
     }{<{left+prnwidth+1} PRN.sep}
       {<{left} PRN.sep + 'Następniki: %1'@[nas-1]
     }{<{left+prnwidth+1} PRN.sep}
       {<{left} PRN.sep + 'Poprzedniki: %1'@[pop-1]
     }{<{left+prnwidth+1} PRN.sep}
  {"├────┬─────────────────────────────┬──────┬────────┬─────────────────┤"; ~~}
  {<{left+1} PRN.bcross + PRN.bartab*lpwd
     }{<{left+lpwd+1} PRN.hcross + PRN.bartab*inwd
     }{<{left+lpwd+inwd+1} PRN.hcross + PRN.bartab*ilwd
     }{<{left+lpwd+inwd+ilwd+1} PRN.hcross + PRN.bartab*sg1wd
     }{<{left+lpwd+inwd+ilwd+sg1wd+1} PRN.hcross + PRN.bartab*(dtwd+sg2wd+3)
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+sg2wd+1} PRN.ecross}
  {"│    │                             │ Il.  │        │Potwierdzenie odb│"; ~~}
  {<{left} PRN.sep
     }{<{left+lpwd+3} PRN.sep
     }{<{left+lpwd+inwd+6} PRN.sep + 'Ilość'@
     }{<{left+lpwd+inwd+ilwd+9} PRN.sep
     }{<{left+lpwd+inwd+ilwd+sg1wd+12} PRN.sep + 'Potwierdzenie odbioru'@
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+sg2wd+18} PRN.sep}{COLOR: '0:0:0,255:255:255'}
  {"│ Lp.│ Imie i nazwisko             │ wyk. │ Podpis ├────────┬────────┤"; ~~}
  {<{left} PRN.sep + 'Lp.'@
     }{<{left+lpwd+3} PRN.sep + 'Imię i nazwisko'@
     }{<{left+lpwd+inwd+6} PRN.sep + 'wyk.'@
     }{<{left+lpwd+inwd+ilwd+9} PRN.sep + 'Podpis'@
     }{<{left+lpwd+inwd+ilwd+sg1wd+13} PRN.bcross + PRN.bartab*dtwd
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+1} PRN.hcross + PRN.bartab*sg2wd
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+sg2wd+1} PRN.ecross}{COLOR: '0:0:0,255:255:255'}
  {"│    │                             │      │        │   Data │ Podpis │"; ~~}
  {<{left} PRN.sep
     }{<{left+lpwd+3} PRN.sep
     }{<{left+lpwd+inwd+6} PRN.sep
     }{<{left+lpwd+inwd+ilwd+9} PRN.sep
     }{<{left+lpwd+inwd+ilwd+sg1wd+12} PRN.sep + 'Data'@
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+15} PRN.sep + 'Podpis'@
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+sg2wd+18} PRN.sep}{COLOR: '0:0:0,255:255:255'}
  {"├────┼─────────────────────────────┼──────┼────────┼────────┼────────┤"; ~~}
  {<{left+1} PRN.bcross + PRN.bartab*lpwd
     }{<{left+lpwd+1} PRN.cross + PRN.bartab*inwd
     }{<{left+lpwd+inwd+1} PRN.cross + PRN.bartab*ilwd
     }{<{left+lpwd+inwd+ilwd+1} PRN.cross + PRN.bartab*sg1wd
     }{<{left+lpwd+inwd+ilwd+sg1wd+1} PRN.cross + PRN.bartab*(dtwd)
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+1} PRN.cross + PRN.bartab*(sg2wd)
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+sg2wd+1} PRN.ecross}{COLOR: '0:0:0,255:255:255'}
  {"Tutaj petla drukujaca pozycje kartki produkcyjnej w ilosci okreslonej "; ~~}
  {"przez zmienna 'pozcount'.                                             "; ~~}
  {"│    │                             │      │        │        │        │"; ~~}
  {i:=1; ~~}
  {WHILE: i<=pozcount}
  {DO}
    {<{left} PRN.sep + {? pozcount>1 || form(i,lpwd,0,'9') || '' ?}
       }{<{left+lpwd+3} PRN.sep
       }{<{left+lpwd+inwd+6} PRN.sep
       }{<{left+lpwd+inwd+ilwd+9} PRN.sep
       }{<{left+lpwd+inwd+ilwd+sg1wd+12} PRN.sep
       }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+15} PRN.sep
       }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+sg2wd+18} PRN.sep}
    {IF: i<pozcount}
    {"├────┼─────────────────────────────┼──────┼────────┼────────┼────────┤"; ~~}
      {<{left+1} PRN.bcross + PRN.bartab*lpwd
         }{<{left+lpwd+1} PRN.cross + PRN.bartab*inwd
         }{<{left+lpwd+inwd+1} PRN.cross + PRN.bartab*ilwd
         }{<{left+lpwd+inwd+ilwd+1} PRN.cross + PRN.bartab*sg1wd
         }{<{left+lpwd+inwd+ilwd+sg1wd+1} PRN.cross + PRN.bartab*(dtwd)
         }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+1} PRN.cross + PRN.bartab*(sg2wd)
         }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+sg2wd+1} PRN.ecross}
    {FI}
    {i+=1; ~~}
  {OD}
  {"└────┴─────────────────────────────┴──────┴────────┴────────┴────────┘"; ~~}
  {<{left+1} PRN.lbcross + PRN.bartab*lpwd
     }{<{left+lpwd+1} PRN.lcross + PRN.bartab*inwd
     }{<{left+lpwd+inwd+1} PRN.lcross + PRN.bartab*ilwd
     }{<{left+lpwd+inwd+ilwd+1} PRN.lcross + PRN.bartab*sg1wd
     }{<{left+lpwd+inwd+ilwd+sg1wd+1} PRN.lcross + PRN.bartab*(dtwd)
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+1} PRN.lcross + PRN.bartab*(sg2wd)
     }{<{left+lpwd+inwd+ilwd+sg1wd+dtwd+sg2wd+1} PRN.lecross}
  {<1 '─ '*(width%2)}
{ZGH.cntx_pop();~~}
{ENTIRE-}
{MACRO-}
{"========================================================================"; ~~}
{"=========================== POCZATEK WYDRUKU ==========================="; ~~}
{"========================================================================"; ~~}
{"---Pytanie o liczbe pozycji na kartce produkcyjnej.                     "; ~~}
{undefine(); ~~}
{define('lpc',1,'Liczba pozycji'@,,,3,0); ~~}
{def_btn('text=%1,btn_label_align=center,panel=bottom,align=end'['&Zapisz'@],'key:F2');~~}
{def_btn('text=%1,btn_label_align=center,panel=bottom,align=end'['&Anuluj'@],'key:Esc');~~}
{IF: def_edit("{? DEFINE.lpc>10 || FUN.info('Należy wpisać liczbę nie większą niż %1.'@[$10]);0 || 1 ?}",'Podaj liczbę pozycji'@)}
  {pozcount:=DEFINE.lpc; ~~}
  {undefine(); ~~}
{ELSE}
  {EXIT}
{FI}
{"---Glowna petla.                                                        "; ~~}
{IF: VAR.A_ZLEC().NRNZL=0}
{FOR: 'ZGP'}
  {INDEX: 'NRPP'}
  {PREFIX: ZGH.ref()}
{DO}
   {VAR.STRING:='';
    ZOPER.cntx_psh();
    {? ZGP.ZOPER().NRNOP<>0
    ||
       ZOPER.index('UNROP');
       ZOPER.prefix(ZGP.ZOPER().NRNOP);
       {? ZOPER.first()
       || _nrop:=ZGP.ZOPER().NRNOP;
         {! |?
          ZOPER.prefix(_nrop);
          {? ZOPER.first()
          ||  VAR.STRING:=form(ZOPER.NROP)+'.'+ZOPER.NA+'/'+VAR.STRING;
             _nrop:=ZOPER.NRNOP;
             ZOPER.NRNOP<>0
          || 0
          ?}
        !}
     ?}
   ?};
   {? VAR.STRING<>''
   ||  VAR.STRING:=VAR.STRING-1;
       {? +VAR.STRING>39
       || {! |?
            _str:=VAR.STRING;
            _nr:=0;
            {! |?
               _nr+=_str*'/';
               _str:=_str*'/'-_str;
               _str*'/'>0
            !};
            VAR.STRING:=_nr+VAR.STRING;
            VAR.STRING:=VAR.STRING-1;
            _str:=VAR.STRING;
            +VAR.STRING>39
            !};
            VAR.STRING+='/...'
       ?}
   || VAR.STRING:='Brak'@
   ?};
   pop:='';
   nas:='';
   ZGP.cntx_psh();
   NASZGP.cntx_psh();
   NASZGP.index('OPNAST');
   NASZGP.prefix(ZGP.ref());
   {? NASZGP.first()
   || {!|?
        nas+=NASZGP.SCIEZKA+',';
        NASZGP.next()
      !}
   ?};
   NASZGP.cntx_pop();
   NASZGP.index('NASTOP');
   NASZGP.prefix(ZGP.ref());
   {? NASZGP.first()
   || {!|?
        pop+=$NASZGP.OPER().NRP+',';
        NASZGP.next()
      !}
   ?};
   ZGP.cntx_pop();
   ZOPER.cntx_pop();~~}
  {SUBSTITUTE: 'prnkart'}
{OD}
{ELSE}
{FOR: 'ZGP'}
  {INDEX: 'PNRPP'}
  {PREFIX: ZGH.ref()}
{DO}
   {VAR.STRING:='';
    ZOPER.cntx_psh();
    {? ZGP.ZOPER().NRNOP<>0
    ||
       ZOPER.index('UNROP');
       ZOPER.prefix(ZGP.ZOPER().NRNOP);
       {? ZOPER.first()
       || _nrop:=ZGP.ZOPER().NRNOP;
         {! |?
          ZOPER.prefix(_nrop);
          {? ZOPER.first()
          ||  VAR.STRING:=form(ZOPER.NROP)+'.'+ZOPER.NA+'/'+VAR.STRING;
             _nrop:=ZOPER.NRNOP;
             ZOPER.NRNOP<>0
          || 0
          ?}
        !}
     ?}
   ?};
   {? VAR.STRING<>''
   ||  VAR.STRING:=VAR.STRING-1;
       {? +VAR.STRING>39
       || {! |?
            _str:=VAR.STRING;
            _nr:=0;
            {! |?
               _nr+=_str*'/';
               _str:=_str*'/'-_str;
               _str*'/'>0
            !};
            VAR.STRING:=_nr+VAR.STRING;
            VAR.STRING:=VAR.STRING-1;
            _str:=VAR.STRING;
            +VAR.STRING>39
            !};
            VAR.STRING+='/...'
       ?}
   || VAR.STRING:='Brak'@
   ?};
   ZOPER.cntx_pop();~~}
  {SUBSTITUTE: 'prnkart'}
  {<1 '─ '*(width%2)}
{OD}
{FI}

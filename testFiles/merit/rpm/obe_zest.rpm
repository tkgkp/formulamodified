:!UTF-8
{TITLE: Zestawienie dokumentów w obiegu}
{PRINTER: 'land_std',,,,,'Q','C',,,,,,1,15,15,15,15,0,1}
{BEGIN:
  {? app_info('web_sesid')<>''
  || exec('czytaj','#stalesys');
     exec('set_parw','param_wydr');
     EDOKUMW.web_cntx_load()
  ?};
  EDOKUMW.cntx_psh;
  EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
  PRN:=obj_new(@.CLASS.PRINT, 10);
  PRN.sl_frame();
  prn1:='PRN';
  par:=web_global_params_get();
  {? type_of(par)>100 & var_pres('zakrdr1',par)>0 & var_pres('rodzaj',par)>0
  || {? par.rodzaj='R'
     || _d:=par.zakrdr1; _c:=par.zakrdr2; _tyb_obi:=par.zakrdr3
     |? par.rodzaj='P'
     || _d:=par.zakrdp1; _c:=par.zakrdp2; _tyb_obi:=par.zakrdp3
     || _d:=par.zakrda1; _c:=par.zakrda2; _tyb_obi:=par.zakrda3
     ?}
  || _d:=_c:=_tyb_obi:=''
  ?};
  status:={? _d='N' || 'Niezakończone'
          |? _d='Z' || 'Zakończone'
          |? _d='O' || 'Odrzucone'
          |? _d='W' || 'Wszystkie'
          || ' - '
          ?};
  uzytk:={? _c='U' || 'użytkownika'
         |? _c='W' || 'wszystkich użytkowników'
         || ' - '
         ?};
  {? _tyb_obi<>''
  || ETYPY.prefix;
     typ:={? ETYPY.seek(_tyb_obi) || ETYPY.NAZWA || ' - ' ?}
  || typ:=' - '
  ?};
  {? type_of(par)>100 & var_pres('typobi',par)>0
  || rodzaj:={? par.typobi=2 || 'Wnioski w obiegu'
             |? par.typobi=1 || 'Faktury w obiegu'
             |? par.typobi=3 || 'Delegacje w obiegu'
             || ''
             ?}
  ?};
  data_od:=web_params_get.data_od;
  data_do:=web_params_get.data_do;
  dalej:=0;
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  EDOKUMW.cntx_pop;
  obj_del(PRN);
  &dalej;
  &prn1;
  &lm; &ll; &vp; &lmt; &rsep;
  VAR_DEL.delete('data_od','data_do','rodzaj','status','uzytk','typ','par')
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: obe_zest.rpm
Utworzony:    2017-11-17 [BZ]
==================================================
Zawartosc: Zestawienie dokumentow w obiegu i wnioskow
{COMMENT-}

{COMMENT}--Definicja wiersza wydruku.------------------------{COMMENT-}
{ {? par.typobi=3
  || PRN.add("$(EDOKUMW.EDOKUM().DW)",12,'Data wyjazdu')
  || PRN.add("$(EDOKUMW.EDOKUM().DATAW)",12,'Rejestrowano')
  ?};
  PRN.add("EDOKUMW.USERS().DANE",15,'Rejestrujący');
  {? par.typobi=3
  || PRN.add("EDOKUMW.DOSTAWCA().NAZWISKO",15,'Delegowany')
  |? par.typobi=2
  || PRN.add("EDOKUMW.DOSTAWCA().NAZWISKO",15,'Osoba')
  |? par.typobi=1
  || PRN.add("$(EDOKUMW.EDOKUM().TP)",10,'Termin płatności')
  ?};
  {? par.typobi=3
  || PRN.add("$(EDOKUMW.EDOKUM().DATA_OD)",10,'Od dnia')
  |? par.typobi=2
  || PRN.add("EDOKUMW.EDOKUM().TR",40,'Temat')
  |? par.typobi=1
  || PRN.add("EDOKUMW.KH().KOD",11,'Kod kontrahenta')
  ?};
  {? par.typobi=3
  || PRN.add("$(EDOKUMW.EDOKUM().DATA_DO)",10,'Do dnia')
  |? par.typobi=2
  || PRN.add("form(EDOKUMW.EDOKUM().NETTO,,2)",11,'Kwota netto',1)
  |? par.typobi=1
  || PRN.add("EDOKUMW.KH().TR",30,'Nazwa kontrahenta')
  ?};
  {? par.typobi=3
  || PRN.add("EDOKUMW.EDOKUM().DEL_MIE",17,'Miejsce delegacji')
  |? par.typobi=1
  || PRN.add("EDOKUMW.EDOKUM().SYM",15,'Symbol dokumentu')
  ?};
  {? par.typobi=3
  || PRN.add("EDOKUMW.DEL_CEL().TR",25,'Cel delegacji')
  |? par.typobi=2 | par.typobi=1
  || PRN.add("form(EDOKUMW.EDOKUM().WART,,2)",11,'Kwota brutto',1)
  ?};
  {? par.typobi=1 || PRN.add("{? EDOKUMW.EDOKUM().EDOKUM || EDOKUMW.EDOKUM(); EDOKUM.bl_info('EDOKUM','NAME') || '' ?}",10,'E-dokument',1) ?};
  PRN.add("EDOKUMW.EDOKUM().B_PRELS",14,'Bieżaca czynność');
  PRN.add("EDOKUMW.ZAM",4,'Zam.');
  PAR_WYDR.TITLE:='Dokumenty w obiegu'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
 {FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}

{<2 'Zakres dat: od %1 do %2'@[$data_od,$data_do]}{<80 'Rodzaj dokumentów: %1'@[rodzaj]}
{<2 'Status: %1 %2'@[status,uzytk]}{<80 'Wybrany typ dokumentów: %1'@[typ]}

{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T10B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{IF: EDOKUMW.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
   {IF: data_od<=EDOKUMW.EDOKUM().DATAW & EDOKUMW.EDOKUM().DATAW<=data_do}
    {SUBSTITUTE: 'LINIA0'}
   {FI}
   { dalej:=EDOKUMW.next(); ~~}
{OD}

:!UTF-8
{BEGIN:
   VAR_DEL.delete('par','prn1','prn2','lp','color','vp','lmt','ll','rsep','tresc','b_rat','h_rat','__edit');
   VAR_DEL.delete('cfg','dane','prn','tos');
   tresc:='paku';
   b_rat:=1;
   lp:=lmt:=ll:=rsep:=__edit:=vp:=0;
   color:='';
   prn:=prn1:=prn2:='';
   _upr:=exec('uprawnieniawrap','pkd',
               'Naliczonych składnikach płacowych'@,
               'PPL_PLL_RSKP',
               'PPL_PLL_PSKP'
             )+
         exec('uprawnieniawrap','pkd',
               'Rejestracji rachunków'@,
               'PPL_ZLC_RRAC',
               'PPL_ZLC_PRAC'
             );
   P.cntx_psh();
   O.cntx_psh();
   LS.cntx_psh();
   RU.cntx_psh();
   ZC.cntx_psh();
   RH.cntx_psh();
   OS_PAB.cntx_psh();
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień pozwalających na przeglądane informacji o: %1'@[_upr])
   || tos:=tab_tmp(1,'REF','INTEGER','#OSOBA.ref()');
      OS_PAB.index('OSOBAOD');
      P.index('PRACOSOB');
      O.index('LISTYPLP');
      RU.prefix();
      ZC.prefix();
      RH.index('RACHDATA');

      cfg:=tab_tmp(1,
         'ROK','INTEGER','Rok',
         'LIMIT','REAL','Limit kosztów',
         'RODZ','STRING[1]','Rodzaj raportu'
      );

      cfg.blank();
      cfg.ROK:=exec('o_rp','blank');
      cfg.RODZ:='S';

      _we:=cfg.mk_edit('Parametry wydruku'@,,'red_'+tresc);
      cfg.win_efld(_we,,'ROK',,,4,,,,,'Rok kosztów uzyskania od przychodów');
      cfg.win_efld(_we,,'RODZ',,,,,,,,'Sposób prezentacji raportu','radio-buttons',,
         'Szczegółowy (każda kwota osobno)'@,"'S'",
         'Zbiorczy (podatnik i sumy roczne)'@,"'Z'"
      );
      cfg.efld_opt(_we,'mark=1',,'ROK');
      exec('ok_esc','#window',cfg,_we);
      cfg.win_edit(_we);

      dane:=tab_tmp(5,
         'DATA','DATE','Data',
         'F_ZATR','STRING[1]','Forma zatrudnienia',
         'T','STRING[9]','Nr teczki',
         'SRC','STRING[42]','Źródło informacji',
         'KWO','REAL','Koszty odliczone',
         'KWN','REAL','Koszty narastająco',
         'KWP','REAL','Koszty pozostałe'
      );
      {? __edit:=cfg.edit("_tab:=cur_tab();
                  {? _tab.ROK=0
                  || __CHK.err_fld(_tab,'ROK')
                  |? _tab.ROK<2013
                  || FUN.emsg('Wydruk dostępny dla danych od 2013 roku.'@); 'ROK'
                  |? _tab.LIMIT:=exec('kwota_koszty50_zlec_rh','lista_licz',_tab.ROK,1); _tab.LIMIT=0
                  || FUN.emsg('Brak uzupełnionych wszystkich progów podatkowych podanego roku.'@);'ROK'
                  || ''
                  ?}
                  ")
      || PAR_WYDR.TITLE:=
         'Koszty uzyskania od przychodów z tytułu praw autorskich\n'@+
         '('+{? cfg.RODZ='S' || 'szczegółowo'@ || 'zbiorczo'@ ?}+')';
         {? cfg.RODZ='S'
         || prn:=obj_new(@.CLASS.PRINT,8);
            prn.s_frame();
            {? rep_export() || prn.add("OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE",40,'Osoba'@) ?};
            prn.add("dane.DATA$1",10,'Data'@);
            prn.add("dane.F_ZATR",6,'Forma współpracy'@);
            prn.add("{? dane.F_ZATR='' || '' || dane.T ?}",9,'Nr teczki'@);
            prn.add("dane.SRC",42,'Źródło informacji'@);
            prn.add("dane.KWO",14,'Koszty odliczone'@,1,,,,,',2,\'9,\'');
            prn.add("dane.KWN",14,'Koszty narastająco'@,1,,,,,',2,\'9,\'');
            prn.add("dane.KWP",14,'Koszty pozostałe'@,1,,,,,',2,\'9,\'')
         || prn:=obj_new(@.CLASS.PRINT,5);
            prn.s_frame();
            prn.add("OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE",40,'Osoba'@);
            prn.add("P.T",9,'Nr teczki'@);
            prn.add("OSOBA.PESEL",11,'PESEL'@);
            prn.add("dane.KWN",18,'Koszty narastająco'@,1,,,,,',2,\'9,\'');
            prn.add("dane.KWP",18,'Koszty pozostałe'@,1,,,,,',2,\'9,\'')
         ?}
      ?};
      params_set('P',exec('wybierz_parses','pracownik','PPL').P)
   ?};
   ~~
}
{END:
   RH.cntx_pop();
   ZC.cntx_pop();
   RU.cntx_pop();
   LS.cntx_pop();
   O.cntx_pop();
   P.cntx_pop();
   OS_PAB.cntx_pop();
   VAR_DEL.delete('par','prn1','prn2','lp','color','vp','lmt','ll','rsep','tresc','b_rat','h_rat','__edit');
   VAR_DEL.delete('cfg','dane','prn','tos');
   ~~
}
{COMMENT}OLD: zp_paku.rpm{COMMENT-}
{COMMENT}OLD: paku.rpi{COMMENT-}
{COMMENT}NEW: p_paku.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{COMMENT}NEW: p_prac.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~__edit}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ {? rep_export() || b_rat:=charleft() || b_rat/=charleft() ?}; ~~ }
{SPACE: 'B'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K
{COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {IF: page()=1}
      {<{lmt+1} 'Parametry sprawozdania'@}
      {<{lmt+1} 'Rok: '@+$cfg.ROK}
   {FI}
   {FONT: 'T8B'}
   {SPACE: 'B'}
   {LINE}
   {SUBSTITUTE: 'TABHEAD'}
   {IF: cfg.RODZ='S'}
      {<1} {REP: prn.flbar(lmt)}
   {ELSE}
      {<1} {REP: prn.fbar(lmt)}
   {FI}
   { prn.setcolor('255:255:255'); ~~}
{HEADER-}
{IF: cfg.RODZ='Z'}
   {FOOTER: 0}
      {<1} {REP: prn.flbar(lmt)}
   {FOOTER-}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   S T O P K A
{COMMENT-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{MACRO: 'linia'}
   {($(prn1+'.ini_line()'))(); ~~}
   {WHILE: ($(prn1+'.next()'))()}
   {DO}
      {<1}{REP: ($(prn1+'.fline(lmt)'))()}
   {OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════
{COMMENT-}
{ prn1:='prn';
  lmt:=int((charleft()-prn.width())/2);
  ~~
}
{MACRO: tresc}
   {IF:
      _ref:=#P.OSOBA;
      {? tos.find_key(_ref)
      || 1
      || tos.REF:=_ref;
         tos.add();
         0
      ?}
   }
   {ELIF:
      _dp:=date(cfg.ROK,1,1);
      _dk:=date(cfg.ROK,12,31);

      dane.erase();

      OS_PAB.prefix(OSOBA.ref());
      {? OS_PAB.find_ge(_dp)
      || {!
         |? {? OS_PAB.OD<=_dk
            || {? OS_PAB.TYP='P'
               || dane.blank();
                  dane.DATA:=OS_PAB.OD;
                  dane.SRC:='Oświadczenie'@;
                  dane.KWO:=OS_PAB.KW;
                  dane.add()
               ?};
               OS_PAB.next()
            ?}
         !}
      ?};

      P.prefix(exec('ref_firma','ustawienia'),'P',OSOBA.ref());
      {? P.first()
      || O.prefix(exec('ref_firma','ustawienia'),'P',cfg.ROK);
         {? O.first()
         || {!
            |? _dl:=date(O.RP,O.MP,1);
               _src:='Lista: '@+(~-O.LT);
               LS.use(-O.LT);
               LS.index('OSOBAKOD');
               LS.prefix(exec('ref_firma','ustawienia'),OSOBA.ref(),7004);
               {? LS.first()
               || {!
                  |? {? dane.find_key(_dl,'P',LS.P().T,_src)
                     || dane.KWO+=LS.KW;
                        dane.put()
                     || dane.blank();
                        dane.DATA:=_dl;
                        dane.F_ZATR:='P';
                        dane.T:=LS.P().T;
                        dane.SRC:=_src;
                        dane.KWO:=LS.KW;
                        dane.add()
                     ?};
                     LS.next()
                  !}
               ?};
               O.next()
            !}
         ?}
      ?};

      RH.prefix(exec('ref_firma','ustawienia'),OSOBA.ref(),cfg.ROK);
      {? RH.first()
      || {!
         |? _um:=RH.ZLE().RU().K;
            {? _um='2' | _um='3'
            || dane.blank();
               dane.DATA:=RH.DWY;
               dane.F_ZATR:='Z';
               dane.T:=ZC.P().T;
               dane.SRC:='Lista: '@+(~-RH.O().LT)+' (UZ: '+ZC.NU+')';
               dane.KWO:=exec('licz_rhs','lista_licz',784);
               dane.add()
            ?};
            RH.next()
         !}
      ?};

      {? dane.first()
      || _sn:=0;
         {!
         |? _sn+=dane.KWO;
            dane.KWN:=_sn;
            dane.KWP:=cfg.LIMIT-_sn;
            dane.put();
            dane.next()
         !}
      ?};

      dane.last()
   }
      {IF: cfg.RODZ='S'}
         {ENTIRE}
         {FONT: 'W12'}{SPACE: 'A'}
         {IF: ~rep_export()}
            {<{ceil(lmt*b_rat)+1} form(lp+=1,,,'9.')} {INCLUDE: p_prac.rpi}
         {FI}
         {FONT: 'T8'}{SPACE: 'B'}
         {<1} {REP: prn.fhbar(lmt)}
         {FOR: dane}
         {DO}
            {SUBSTITUTE: 'linia'}
         {OD}
         {<1} {REP: prn.flbar(lmt)}
         {ENTIRE-}
      {ELSE}
         {SUBSTITUTE: 'linia'}
      {FI}
   {FI}
{MACRO-}
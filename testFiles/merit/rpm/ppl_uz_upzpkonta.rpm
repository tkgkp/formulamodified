:!UTF-8
{TITLE:Rozdzielnik na konta kosztowe z wyborem składników}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PAR_WYDR.TITLE:='Rozdzielnik na konta kosztowe rachunków od umów zleceń'@;
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','h_rat','__ok','__edit','__param');
   par:=obj_new(9);
   par[5]:=date()~1;
   par[6]:=date()~2;
   par[7]:=rep_export();
   __ok:=0;
   tresc:='r_kk_wyb';
   _upr:=exec('uprawnieniawrap','pkd','Rachunkach'@,'PPL_ZLC_RRAC','PPL_ZLC_PRAC');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'ROK','INTEGER','Rok',
         'MC','INTEGER','Miesiąc',
         'RODZAJ','STRING[1]',,
         'WYBOR','STRING[1]',
      );
      __param.ROK:=date()~1;
      __param.MC:=date()~2;
      __param.RODZAJ:='S';
      __param.WYBOR:='T';

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'uz_uwzlepsprc');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok do składników wypłaty'@);
      __param.win_efld(__edit,,'MC',,,2,,,'Miesiąc'@,,'Miesiąc (1-12)'@);
      {? ~par[7]
      || __param.win_efld(__edit,AH,'H',,,,,1);
         __param.win_efld(__edit,,'RODZAJ',,,,,,'Prezentacja na raporcie '@,,,'radio-buttons',,
              'Symbol konta kosztów'@,"'S'",
              'Nazwa  konta kosztów'@,"'N'")
      ?};
      __param.win_efld(__edit,AH,'H',,,,,1);
      __param.win_efld(__edit,,'WYBOR',,,,,,'Wybór składników '@,,,'radio-buttons',,
              'Predefiniowane'@,"'N'",
              'Wybór własny użytkownika'@,"'T'");
      __param.efld_opt(__edit,'mark=1',,'ROK');
      __param.efld_opt(__edit,'mark=1',,'MC');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? __ok:=__param.edit(
            "  {? __param.ROK<1999
               || FUN.emsg('Sprawozdanie dostępne dla danych od 1999 roku.'@);
                  'ROK'
               |? __param.MC<1 | __param.MC>12
               || FUN.emsg('Miesiąc musi zawierać się w przedziale od 1 do 12.'@);
                  'MC'
               || 1
               ?}
            "
         )
      || par[5]:=__param.ROK;
         par[6]:=__param.MC;

         {? __param.WYBOR='N' | (_rub:=exec('code_grp','profile',,tresc,'',{? ~par[7] || 11 || 99 ?}))=''
         || FUN.emsg('Przyjęto do sprawozdania standardowy zestaw rubryk:\n'+
                     '500,797,798,792,982,983,765,958,766,959,767,960,962.');
            _rub:=',500,797,798,792,982,983,765,958,766,959,767,960,962,';
            __param.WYBOR:='N'
         ?};
         _rub:=1-_rub-1;
         par[8]:=sql('select   R.LP, R.RN, R.RT, \'   \' as ES
                      from     R where R.RN in (:_a)
                      order by R.LP, R.RN
                     ',_rub
         );

         par[9]:=_rub;
         &_rub;
         _rub:='';
         {? par[8].first()
         || {!
            |? _rub+={? +_rub|| ',' || '' ?}+$par[8].RN;
               par[8].next()
            !}
         ?};

         _skul:='select KK.SYM, KK.NAZWA,  R.LP, R.RN, sum(LS.KW) KW '+
                'from @LS left join KK join R join RH using(LS.RH,RH.reference) join O using (LS.O,O.REFERENCE) '+
                'where LS.REFERENCE like \'l'+(($par[5])+2)+(('0'+$par[6])+2)+'z%\' and '+
                      'RH.R=:_b and RH.M=:_c and O.FIRMA=:_d'+
                'group by KK.SYM, KK.NAZWA, R.LP, R.RN '+
                'having   R.RN in (:_a) '+
                'order by RN ';
         _BUF:=sql(
            _skul,
            _rub,
            par[5],
            par[6],
            exec('ref_firma','ustawienia')
         );
         {? ~(var_pres('_BUF')=type_of(SYSLOG) & _BUF.size())
         || FUN.info('Brak danych do prezentacji za '@+date(par[5],par[6],1)$8+'.');
            __ok:=0
         ?};
         {? __param.WYBOR='N'
         || par[1]:=tab_tmp(1,
            'SYM','STRING['+$MS.fld_len('KK','SYM')+']','Konto - Symbol'@,
            'NAZ','STRING[50]','Konto - Nazwa'@,
            'S01','REAL','Brutto'@,
            'S02','REAL','Fundusz emerytalny'@,
            'S03','REAL','Fundusz rentowy'@,
            'S04','REAL','Fundusz chorobowy'@,
            'S05','REAL','Fundusz wypadkowy'@,
            'S06','REAL','Fundusz pracy'@,
            'S07','REAL','Fundusz gwarancyjny'@,
            'S08','REAL','Ubezpieczenie zdrowotne'@,
            'S09','REAL','Podatek'@,
            'S10','REAL','FEP'@
            )

         || _fm:='par[1]:=tab_tmp(1,'+
              '\'SYM\',\'STRING[30]\',\'Konto\',';
            _fm+='\'NAZ\',\'STRING[40]\',\'Konto - nazwa\',';
            {? par[8].first()
            || _i:=0;
               {!
               |? _i+=1;
                  _fm+='\'S'+form(_i,-2)+'\',\'REAL\','+'\''+par[8].RT+'\',';
                  par[8].ES:='S'+form(_i,-2);
                  par[8].put();
                  par[8].next()
               !};
               _fm:=_fm-1;
               _fm+=')'
            ?};
            &_i;

           ($_fm)()
         ?};

         par[1].index(par[1].ndx_tmp('Symbol',0,'SYM',,0,'SYM',,0));

         {? _BUF.first()
         || {? __param.WYBOR='N'
            || _es:=
               "  {? _a=500 || 'S01'
                  |? _a=797 | _a=798 || 'S09'
                  |? _a=792 || 'S08'
                  |? _a=982 || 'S06'
                  |? _a=983 || 'S07'
                  |? _a=765 || 'S02'
                  |? _a=958 || 'S02'
                  |? _a=766 || 'S03'
                  |? _a=959 || 'S03'
                  |? _a=767 || 'S04'
                  |? _a=960 || 'S05'
                  |? _a=962 || 'S10'
                  ?}
               "
            || _es:="{? par[8].find_key(_a) || par[8].ES || '' ?}"
            ?};

            {!
            |? _acr:=_es({? __param.WYBOR='N' || _BUF.RN ||  _BUF.LP ?});
               {? +_acr=3
               || _ef:=$('par[1].'+_acr+'+=_a');
                  {? par[1].find_key(_BUF.SYM,_BUF.SYM)
                  || _ef(_BUF.KW);
                     par[1].put()
                  || par[1].blank();
                     par[1].SYM:=_BUF.SYM;
                     par[1].NAZ:=_BUF.NAZWA;
                     _ef(_BUF.KW);
                     par[1].add()
                  ?}
               ?};
               _BUF.next()
            !}
         ?};
         obj_del(_BUF);

         _agreguj:='';
         par[2]:=obj_new(@.CLASS.PRINT,par[1].fld_num());
         {? par[7]
         || par[2].add("par[1].SYM",MS.fld_len('KK','SYM'),'Symbol konta'@);
            par[2].add("par[1].NAZ",50,'Nazwa konta'@)
         |? __param.RODZAJ='S'
         || par[2].add("par[1].SYM",35,'Symbol konta'@)
         || par[2].add("par[1].NAZ",40-{? par[1].fld_num()>10 || 5 || 0 ?},'Nazwa konta'@)
         ?};
         {! _es:=3..(par[1].fld_num())
         |! _agreguj+=',sum(S'+form(_n:=_es-(2),-2)+') S'+form(_n,-2);
            par[2].add($("par[{? par[1].SYM='RAZEM' || 3 || 1 ?}].S"+form(_n,-2)),12,par[1].fld_name(_es),1,,,,,'12,2,\'9,\'')
         !};
         par[2].s_frame();

         par[3]:=sql('select :_a from :_b',1-_agreguj,par[1]);
         prn1:='par[2]';
         lmt:=0;rsep:=1;
         vp:=par[4]:=h_rat:=1
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','h_rat','__ok','__edit','__param','tresc')
}
{EXIT: ~__ok}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: obszarzlec.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Miesiąc kalendarzowy: '@+date(par[5],par[6],1)$8}
   {<{ceil(lmt*h_rat)+1}'Składniki płacowe: '@+par[9]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[2].width())/2); ~~ }
{FOR: par[1]}{DO}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: ~rep_export()}
   { par[1].SYM:='RAZEM'@;
     par[1].NAZ:='RAZEM'@;
     rsep:=0; ~~ }
   {IF: lineleft()>=2}
      {REP: par[2].fbar(lmt)}
   {ELSE}
      {PAGE}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
{FI}
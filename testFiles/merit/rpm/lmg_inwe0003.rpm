:!UTF-8
{TITLE:C. Różnice poinwentaryzacyjne}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   inwe003:="
      VAR_DEL.delete('color','prn1','prn2');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');

      VAR_DEL.delete('k','w','su','PRN','__only');
      VAR_DEL.delete('dokl','doklw');
      VAR_DEL.delete('wyjscie','X_Xd')";
   inwe003();

   VAR_DEL.delete('k','w','s','PRN');
   k:=obj_new(8); "--szerokosci kolumn--";
   w:=obj_new(8); "--wartosci kolumn--";
   su:=0; "--razem--";

   dokl:=3; "--dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";

   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   __only:=INN.MG().IL='T';

   k[1]:=20;
   k[2]:=5;
   k[3]:=20;
   k[4]:=20+{? __only || 18 || 0 ?};
   k[5]:=10;
   k[6]:=0;
   k[7]:=15;
   k[8]:=15;

   w[1]:='';
   w[2]:=0;
   w[3]:=w[4]:=w[5]:=w[6]:='';
   w[7]:=w[8]:=0;

   wyjscie:=0;

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=0;
   rsep:=PAR_WYDR.RSEP
}
{END:
   inwe003(); VAR_DEL.delete('inwe003')
}
{INCLUDE: wsp_pw.rpi}
{
PRN.add("w[1]",k[1],'Symbol dokumentu'@);
PRN.add("w[2]",k[2],'Poz.'@);
PRN.add("w[3]",k[3],'Indeks'@);
PRN.add("w[4]",k[4],'Nazwa'@);
PRN.add("w[5]",k[5],'jm'@);
PRN.add("w[7]",k[7],'Ilość'@,1);
{? ~__only || PRN.add("w[8]",k[8],'Wartość'@,1) ?};

"--obliczenia--";
X_Xd:=sql('
   select
      ND.SYM as SYM,
      DK.P as P,
      M.KTM as KTM,
      M.N as NAZ,
      JM.KOD as JM,
      case when DK.PLUS=''T'' then DK.IL else -DK.IL end as IL,
      case when DK.PLUS=''T'' then DK.WAR else -DK.WAR end as WAR
   from
      @DK
      join @ND using (DK.N,ND.reference)
      join M using (DK.M,M.reference)
      join JM using (DK.JM,JM.reference)
   where
      ND.REFERENCE in ('':_a'','':_b'')
      and ND.REFERENCE like '':_c%''
   order by
      SYM, P
   ',INN.INP_R,INN.INR_R,{? INN.INP_R<>'' || 8+INN.INP_R || 8+INN.INR_R ?});

{? X_Xd.first()=0
|| wyjscie:=1; FUN.info('Brak danych spełniających kryteria.'@)
||
   {!
   |?
     M.cntx_psh();
     M.index('MATKTM');
     M.prefix(X_Xd.KTM,X_Xd.KTM);
     {? M.first()
     || {? M.DOKL>dokl || dokl:=M.DOKL ?}
     ?};
     M.cntx_pop();
      X_Xd.next()
   !};
   X_Xd.first()
?};
PAR_WYDR.TITLE:='RÓŻNICE POINWENTARYZACYJNE\n%1 z dnia %2'@[INN.SYM,form(INN.DI)];
prn1:='PRN';
~~}
{EXIT: wyjscie=1}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{LINE}
{<{lmt+1}}{'Magazyn: %1'@[INN.MG().SYM]}
{<{lmt+1}}{{? INN.TI<>time(0,0,0)
           || 'Data rozpoczęcia inwentaryzacji: %1, godzina: %2'@[INN.DI$1,INN.TI$3]
           || 'Data rozpoczęcia inwentaryzacji: %1'@[INN.DI$1]
           ?}}
{<{lmt+1}}{{? INN.T<>time(0,0,0)
           || 'Data zakończenia inwentaryzacji: %1, godzina: %2'@[INN.D$1,INN.T$3]
           || 'Data zakończenia inwentaryzacji: %1'@[INN.D$1]
           ?}}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   drukowanie pozycji wydruku                               {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xd}
{DO}
   {FIRST}{ rsep:=PAR_WYDR.RSEP;~~}{FIRST-}
   {
   w[1]:=X_Xd.SYM;
   w[2]:=form(X_Xd.P,,,'99');
   w[3]:=X_Xd.KTM;
   w[4]:=X_Xd.NAZ;
   w[5]:=X_Xd.JM;
   w[7]:=form(X_Xd.IL,,dokl,'.,');
   w[8]:=form(X_Xd.WAR,,doklw,'.,');
   su+=X_Xd.WAR;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie jesli wydruk wartosciowy                    {COMMENT-}
{IF: lineleft()>0 & lineleft<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{SUBSTITUTE: 'LINIAF'}
{
PRN.n_frame();
w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:='';
w[7]:='Razem:'; PRN.FORM[7]:=0;
w[8]:=form(su,,doklw,'.,');
~~}
{IF: ~__only}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA0'}
{FI}
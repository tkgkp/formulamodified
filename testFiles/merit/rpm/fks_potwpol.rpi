:!UTF-8
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: potwpol.rpi
Utworzony:   2003-11-26 [AMK] Artur Makos
==================================================
Zawartosc: Potwierdzenie salda - jezyk polski
{COMMENT-}
{ {? var_pres('PRN')>0 || obj_del(PRN) ?};
  {? var_pres('PRN1')>0 || obj_del(PRN1) ?};
  naz_str:='Strona ';
  {! _i:=1..6 |! w[_i]:='' !};
  PRN:=obj_new(@.CLASS.PRINT,6); PRN.sl_frame();
  PRN.add("w[6]",8,'Jed. ks.'@);
  PRN.add("w[1]",62,'Dokument'@);
  PRN.add("w[2]",10,'Data dokumentu'@);
  {? SER_NAG.WAL=null || PRN.add("w[5]",8,'Waluta'@) ?};
  PRN.add("form(w[3],,2,' ,')",18,'Na dobro NASZE'@,1);
  PRN.add("form(w[4],,2,' ,')",18,'Na dobro WASZE'@,1);
  PRN1:=obj_new(@.CLASS.PRINT,6); PRN1.sl_frame();
  {? ROZNE.UT_VAT
  || PRN1.add("w[6]",8,'Jed. ks.'@);
     PRN1.add("w[1]",62,'Dokument'@);
     PRN1.add("w[2]",10,'Data dokumentu'@)
  || PRN1.add("w[1]",35)
  ?};
  {? SER_NAG.WAL=null || PRN1.add("w[5]",8,'Waluta'@) ?};
  PRN1.add("form(w[3],,2,' ,')",20,'Na dobro WASZE'@,1);
  PRN1.add("form(w[4],,2,' ,')",20,'Na dobro NASZE'@,1);
  {! _i:=1..licz_wal  |! sumy[_i][2]:=sumy[_i][3]:=sumy[_i][4]:=0 !};
  PAR_WYDR.HEAD:=0;
 ~~}
{COMMENT}---------------------CZY DRUKOWAC PROSBE O ZAPLATE-----------------{COMMENT-}
{ petla:=1; druk1:=obrn:=obrw:=0;
  SLUAPPL.cntx_psh(); SLU.cntx_psh(); SLO.cntx_psh();  SER_POZ.cntx_psh();
  SLUAPPL.prefix(); SLU.prefix(); FINFO.SLWAL().SLU();
  SLO.index('SL'); SLO.prefix(SLU.ref());
  {? SLO.first()
  || {! |?
        {? OPERATOR.DEPT=null
        || SER_POZ.index('SER_POZW'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),SLO.KOD)
        || SER_POZ.index('SER_POZO'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),OPERATOR.DEPT().OD,SLO.KOD)
        ?};
        {? SER_POZ.first()
        || {! |? obrn+=SER_POZ.DNASZ;  obrw+=SER_POZ.DWASZ; SER_POZ.next() !}
        ?};
        druk1:=(obrn$2>obrw$2);
        druk1=0 & SLO.next()
      !}
  ?};
  SLUAPPL.cntx_pop(); SLU.cntx_pop(); SLO.cntx_pop(); SER_POZ.cntx_pop();
~~}
{COMMENT}------------------------------HEADXA-----------------{COMMENT-}
{ PAR_WYDR.LOGO:=logo; prn1:=prn2:='PRN'; nr_str:=0;~~}
{IF: lineleft=0}
{IF: ~new_poz}{ new_poz:=1;~~}
{  PAR_WYDR.TITLE:='ODCINEK A'@;  tab_adr1[1]:='ADRESAT:'@; tab_adr1[2]:='NADAWCA:'@;
   tab_adr1[3]:='ADRES:'@;  tab_adr1[4]:='KOD PŁATNIKA:'@;~~}
{LINE: 1}
{IF: ROZNE.UT_KOM}{SUBSTITUTE: 'KORHEADP'}{ELSE}{SUBSTITUTE: 'KORHEADL'}{FI}
{LINE: 1}
{IF: SER_NAG.WAL<>null}{<3 'WALUTA:'@}{<20 SER_NAG.WAL().KOD+' '+SER_NAG.WAL().TR}
{exec('kh_dod_ini','kontrahent',SER_NAG.KH);~~}
{IF: |KH_DOD.NASZ_KOD<>''}{<3 exec('symbol','rozrach',1)+'  '+|KH_DOD.NASZ_KOD}{FI}
{LINE: 1}{FI}
{<1 'Na podstawie art. 26 Ustawy o rachunkowości z dnia 29.09.1994 r. (Dz. U. z 2023 r. poz. 120 ze zm.) wzywamy Was'@}
{<1 'do potwierdzenia w terminie 10 dni na ODCINKU B zgodności następujących sald, figurujących'@}
{<1 'w naszych księgach na dzień: '@}{FONT: 'T8GB'}{$SER_NAG.DATA}{FONT: 'W8'}{'.'}
{LINE: 1}
{<3 'Zgodnie z przepisami o rachunkowości do potwierdzenia sald nie można stosować tzw. akceptu milczącego.'@}
{LINE: 1}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{LINE: 1}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FI}
{COMMENT}------------------------------STRONAA----------------{COMMENT-}
{  suma3:=suma4:=0; pierwszy:=1;~~}
{FOR: 'SER_POZ'}
{INDEX: 'SER_POZO'}
{PREFIX: REF.FIRMA,SER_NAG.ref(),OPERATOR.DEPT().OD}
{DO}
   { w[1]:=SER_POZ.DOK+{? SER_POZ.SYM_ZEW<>'' & SER_POZ.SYM_ZEW<>SER_POZ.DOK
                       || ' ('+SER_POZ.SYM_ZEW+')'
                       || ''
                       ?};
     w[6]:=SER_POZ.ODD().OD; w[2]:=$SER_POZ.DATA; w[5]:=SER_POZ.WAL().KOD;
     _v_n:=SER_POZ.DNASZ$2; _v_w:=SER_POZ.DWASZ$2;
     w[3]:=F.SWn(_v_n,_v_w); w[4]:=F.SMa(_v_n,_v_w);
     {? SER_NAG.WAL<>null
     || suma3+=w[3]; suma4+=w[4]
     || _znal:=0;
        {! _i:=1..licz_wal
        |? _znal=0
        |! {? sumy[_i][1]=SER_POZ.WAL().KOD
           || _znal:=1; sumy[_i][2]+=w[3]; sumy[_i][3]+=w[4]; sumy[_i][4]:=1
           ?}
        !}
     ?}; ~~}
{COMMENT}------------------------------LINIA KONCZACA + NOWY HEADER-----------------{COMMENT-}
   {IF: lineleft<3}{FONT: 'T8G'}
   {SPACE: 'C'}{SUBSTITUTE: 'LINIAF'}
   {IF: lineleft<>0}{PAGE}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {IF: lineleft=0}{SUBSTITUTE: 'TABHEAD'}{FI}{ vp:=1;~~}
   {FONT: 'T8G'}{SPACE: 'C'}
   {FI}
{COMMENT}------------------------------KONIEC NOWEGO HEADERA-----------------{COMMENT-}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{ separat:=rsep; ~~}
{IF: SER_NAG.WAL<>null}
{IF: lineleft<3}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{  w[1]:='SALDO'; w[2]:=w[6]:=''; w[3]:=F.SWn(suma3,suma4);  w[4]:=F.SMa(suma3,suma4); ~~}
{SUBSTITUTE: 'LINIAFS'}
{ELSE}
{ liczwal:=0; {! _i:=1..licz_wal  |! {? sumy[_i][4]=1 || liczwal+=1 ?} !};~~}
{IF: lineleft<=(2+liczwal)}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{  w[1]:='SALDO'; rsep:=1; w[2]:=w[3]:=w[4]:=w[6]:=''; licznik:=1; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{WHILE: licznik<=licz_wal}
{DO}
   {IF: sumy[licznik][4]<>0}
    {  w[3]:=sumy[licznik][2]; w[4]:=sumy[licznik][3]; w[5]:=sumy[licznik][1];~~}
    {SUBSTITUTE: 'LINIA0'} { w[1]:=''; rsep:=0;~~}
   {FI}
   { licznik+=1;~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI} { rsep:=separat;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{ENTIRE}
{LINE: 1}
{FONT: 'W8'}{SPACE: 'C'}
{IF: druk1}
{ ref_defw:=null;
  SLO.cntx_psh();
  SER_DEFW.index('SERDEFW1'); SER_DEFW.prefix(SER_NAG.KOD,SER_KOR.JEZYK);
  {? SER_DEFW.first() || ref_defw:=SER_DEFW.ref() ?};
  SLO.cntx_pop();~~}
{ _tmp:=SER_DEFW.memo_txt(0,1,'TEKST');STR.spl_word();STR.split(_tmp,'\n');~~}
   {WHILE: STR.next()}
   {DO}
      {tekst:=STR.get_word();~~}
      {<1 tekst}
   {OD}{ kontbank:=exec('get_rb','rozrach_kor');~~}
   {<1 'Sposób zapłaty - przelew do banku: '@}
   {<3 SKID_RBK.BANK().NB+{? SKID_RBK.BANK().KODISO<>XINFO.KRAJ || ', SWIFT (BIC): '+SKID_RBK.BANK().SWIFT || '' ?}}
   {<1 'konto: '@}
   {<3 kontbank}
{FI}
{LINE: 1}
{ wdt:=charleft(); ~~}
{<1>{wdt/2} 'Data'}{<{wdt/2}>{wdt} 'Pieczęć'}
{LINE: 2}
{<1>{wdt/2} 50*'.'}{<{wdt/2}>{wdt} 50*'.'}
{ENTIRE-}
{ pagebef:=page();~~ }
{IF: lineleft()<>0 & ROZNE.ONE_PAGE=0}{PAGE}{FI}
{IF: ROZNE.ONE_PAGE=1}{IF: lineleft()>2}{LINE: 2}{FI}{ENTIRE}{FI}
{COMMENT}------------------------------HEADXB-----------------{COMMENT-}
{IF: ROZNE.ONE_PAGE=0}{ nr_str:=0;~~}{FI}
{IF: lineleft=0 | ROZNE.ONE_PAGE}
{IF: ~new_poz1}{ new_poz1:=1;~~}
{  PAR_WYDR.TITLE:='ODCINEK B'; prn1:=prn2:='PRN1';
   tab_adr1[2]:='ADRESAT:'@; tab_adr1[1]:='NADAWCA:'@; tab_adr1[3]:='ADRES:'@; tab_adr1[4]:='KOD PŁATNIKA:'@;~~}
{IF: ROZNE.ONE_PAGE}
{FONT: 'HEAD'}{SPACE: 'B'}
{druktyt:=0;~~}
{>{charleft()} XINFO.MIA+', '+$SER_NAG.DATA}
{SUBSTITUTE: 'HEAD'}{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{FI}
{LINE: 1}
{IF: ROZNE.UT_KOM}{SUBSTITUTE: 'KORHEADL'}{ELSE}{SUBSTITUTE: 'KORHEADP'}{FI}
{LINE: 1}
{IF: SER_NAG.WAL<>null}{<3 'WALUTA:'@}{<20 SER_NAG.WAL().KOD+' '+SER_NAG.WAL().TR}
{LINE: 1}{FI}
{<1 'Potwierdzamy saldo figurujące w Waszych księgach na dzień '@
   +$SER_NAG.DATA+'.'}
{LINE: 1}
{FONT: 'T8G'}{SPACE: 'C'}
   { lmt:=int((charleft()-PRN.width())/2); ~~}
{FI}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{ pageaf:=page();{? pageaf<>pagebef & ROZNE.ONE_PAGE || nr_str:=0;firstloopone:=0 ?}; ~~ }
{COMMENT}------------------------------MACRO-STRONAB----------------{COMMENT-}
{IF: ROZNE.UT_VAT}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: 'SER_POZ'}
{INDEX: 'SER_POZO'}
{PREFIX: REF.FIRMA,SER_NAG.ref(),OPERATOR.DEPT().OD}
{DO}
   {IF: lineleft<>0 & ROZNE.ONE_PAGE & ~firstloopone & pageaf=pagebef & ~nrdone}{ nr_str:=0;nrdone:=1;~~}{FI}
   { w[1]:=SER_POZ.DOK+{? SER_POZ.SYM_ZEW<>'' & SER_POZ.SYM_ZEW<>SER_POZ.DOK
                       || ' ('+SER_POZ.SYM_ZEW+')'
                       || ''
                       ?};
     w[6]:=SER_POZ.ODD().OD; w[2]:=$SER_POZ.DATA; w[5]:=SER_POZ.WAL().KOD;
     _v_n:=SER_POZ.DNASZ$2; _v_w:=SER_POZ.DWASZ$2;
     w[3]:=F.SWn(_v_n,_v_w); w[4]:=F.SMa(_v_n,_v_w); ~~}
{COMMENT}------------------------------LINIA KONCZACA + NOWY HEADER-----------------{COMMENT-}
   {IF: lineleft<3 & ~firstloopone}{FONT: 'T8G'}
   {SPACE: 'C'}{SUBSTITUTE: 'LINIAF'}
   {IF: lineleft<>0}{PAGE}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {IF: lineleft=0}{SUBSTITUTE: 'TABHEAD'}{FI}{ vp:=1;~~}
   {FONT: 'T8G'}{SPACE: 'C'}
   {ELSE}
   {IF: lineleft<2 & ROZNE.ONE_PAGE}{firstloopone:=0;~~}{FI}
   {FI}
{COMMENT}------------------------------KONIEC NOWEGO HEADERA-----------------{COMMENT-}
   {SUBSTITUTE: 'LINIA0I'}
{OD}
{FI}
{IF: firstloopone}{ nr_str:=0;~~}{FI}
{ w[2]:=w[5]:=w[6]:='';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: SER_NAG.WAL<>null}
{ w[1]:='SALDO'; w[3]:=F.SWn(suma3,suma4); w[4]:=F.SMa(suma3,suma4); ~~}
{IF: lineleft()<3}
{SUBSTITUTE: 'LINIAF'}{IF: lineleft()<>0}{PAGE}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{SUBSTITUTE: 'LINIA0I'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{ELSE}
{IF: lineleft<=(2+liczwal)}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{ w[1]:='SALDO'; rsep:=separat; w[3]:=w[4]:=''; licznik:=1; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{WHILE: licznik<=licz_wal}
{DO}
   {IF: sumy[licznik][4]<>0}
    {  w[3]:=sumy[licznik][2]; w[4]:=sumy[licznik][3]; w[5]:=sumy[licznik][1];~~}
    {SUBSTITUTE: 'LINIA0I'} { w[1]:=''; rsep:=0;~~}
   {FI}
   { licznik+=1;~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI} { rsep:=separat;~~}
{IF: ROZNE.ONE_PAGE=0}{ENTIRE}{FI}
{LINE: 1}
{FONT: 'W8'}{SPACE: 'C'}
{ wdt:=charleft(); ~~}
{<1>{wdt/2} 'Data'@}{<{wdt/2}>{wdt} 'Pieczęć'@}
{LINE: 2}
{<1>{wdt/2} 50*'.'}{<{wdt/2}>{wdt} 50*'.'}
{ENTIRE-}
{IF: lineleft()<>0}{PAGE}{FI}
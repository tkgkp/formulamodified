:!UTF-8
{TITLE:A. Zestawienie dokumentów sprzedaży}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   fakz_000:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','doklw','PRN');
      VAR_DEL.delete('od_datyw','do_datyw');
      VAR_DEL.delete('od_daty','do_daty');
      VAR_DEL.delete('od_platnosc','do_platnosc','lp');
      VAR_DEL.delete('filtr_kh','drukuj_zal','mpp');
      VAR_DEL.delete('wydr_naz','wydr_edi','wyjscie','X_Xb','X_Xw1')";
   fakz_000();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   tryb:=rep_export();

   filtr_kh :='N';
   mpp:=0;
   od_datyw:=do_datyw:=date(0,0,0);
   od_daty:=do_daty:=date(0,0,0);
   od_platnosc:=do_platnosc:=date(0,0,0);

   lp:=0;
   doklw:=2; "--dokladnosc wartosc--";

   wydr_naz:='fakz_000';
   wydr_edi:='FAKZ_000';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? exec('upr_cs','ceny') & WYDRUKIL.edit()
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         filtr_kh:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         drukuj_zal:={? WYDRUKIL.CHKBOX02=1 || 'T' || 'N' ?};
         mpp:={? exec('sp_active','faktury_wspolne') || WYDRUKIL.RADIOB_3 || 0 ?};
         od_datyw:=WYDRUKIL.OD_DATY2;
         do_datyw:=WYDRUKIL.DO_DATY2;
         od_daty:=WYDRUKIL.OD_DATY;
         do_daty:=WYDRUKIL.DO_DATY;
         od_platnosc:=WYDRUKIL.OD_DATY1;
         do_platnosc:=WYDRUKIL.DO_DATY1
      ?}
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_kh','kontrahent',filtr_kh,'N') ?};
   {? wyjscie=0
   ||
      _ilk:=9+mpp*2;
      PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();
      k:=obj_new(_ilk); "--szerokosci kolumn--";
      n:=obj_new(_ilk); "--nazwy kolumn--";
      w:=obj_new(_ilk); "--wartosci kolumn--";
      k[1] :=5;    n[1] :='Lp.'@;
      k[2] :=20;   n[2] :='Symbol dokumentu'@;
      k[3] :=10;   n[3] :='Kontrahent'@;
      k[9] :=8;    n[9] :='Odbiorca'@;
      k[4] :=10;   n[4] :='Data sprzedaży'@;
      k[5] :=10;   n[5] :='Termin płatności'@;
      k[6] :=14;   n[6] :='Wartość netto'@;
      k[7] :=13;   n[7] :='Wartość VAT'@;
      k[8] :=14;   n[8] :='Wartość brutto'@;
      {? mpp
      ||
         k[10]:=14;   n[10]:='Wartość split payment';
         k[11]:=8;    n[11]:='Wymagany split payment'
      ?};
      {! _ii.._ilk |! w[_ii] :='' !};

      _ilks:=3+mpp;
      s:=obj_new(_ilks); "--razem--";
      {! _ii.._ilks |! s[_ii] :=0 !}
   ?}
}
{END:
   fakz_000(); VAR_DEL.delete('fakz_000')
}
{IF: exec('upr_cs','ceny')=0}{ FUN.info('Brak uprawnień do wartości sprzedaży.'@);~~}{EXIT}{FI}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
PRN.add("w[3]",k[3],n[3]);
PRN.add("w[9]",k[9],n[9]);
PRN.add("w[4]",k[4],n[4]);
PRN.add("w[5]",k[5],n[5]);
PRN.add("w[6]",k[6],n[6],1);
PRN.add("w[7]",k[7],n[7],1);
PRN.add("w[8]",k[8],n[8],1);
{? mpp
||
   PRN.add("w[10]",k[10],n[10],1);
   PRN.add("w[11]",k[11],n[11])
?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'SYM'    ,'STRING[20]'  ,'Symbol faktury',
   'KH'     ,'STRING[10]'  ,'Skrót kontrahenta',
   'KH_ODB' ,'STRING[8]'   ,'Kod odbiorcy',
   'D'      ,'DATE'        ,'Data sprzedaży',
   'TZ'     ,'DATE'        ,'Termin płatności',
   'NETTO'  ,'REAL'        ,'Netto',
   'VAT'    ,'REAL'        ,'VAT',
   'BRUTTO' ,'REAL'        ,'Brutto',
   'SP_WAR' ,'REAL'        ,'Wartość split payment',
   'SP_WYM' ,'STRING[1]'   ,'Wymagany split payment');

OKR.cntx_psh(); FAPOW.cntx_psh(); FAKS.cntx_psh(); FAKSV.cntx_psh();
_min:="{? _a & _a<_b || _a || _b ?}";
_min:=_min(_min(od_datyw~1,od_daty~1),od_platnosc~1);
_max:="{? _a & _a>_b || _a || _b ?}";
_max:=_max(_max(do_datyw~1,do_daty~1),do_platnosc~1);
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? OKR.first & (_min=0 | OKR.find_ge(_min)) & X_Xb.first()
||
   _stsupr:=exec('get','#params',2102,2,OPERATOR.USER)='T';
_stss:="
_wyn:=exec('usr_upr','b_perm','STS',_a,OPERATOR.USER);
_wyn
";
   {!
   |?
      FAKS.use('faktu'+ST.ODDZ+(($OKR.ROK)+2));
      FAKSV.use('faksv'+ST.ODDZ+(($OKR.ROK)+2));
      FAKS.index('FAK_KH');
      FAKSV.index('FF_SV');
      FAPOW.index('END_K');
      {? X_Xb.first()
      ||
         {!
         |?
            FAKS.prefix('S',X_Xb.KOD);
            {? FAKS.first()
            ||
               {!
               |?
                  {? FAKS.STAT_REJ<>'A'
                     & (_stsupr=0 | _stsupr=1 & FAKS.STS<>null & _stss(FAKS.STS))
                     & FAKS.KH().KOD=X_Xb.KOD
                     & ((od_datyw<=FAKS.DW | od_datyw=date(0,0,0)) & (FAKS.DW<=do_datyw | do_datyw=date(0,0,0)))
                     & ((od_daty<=FAKS.D | od_daty=date(0,0,0)) & (FAKS.D<=do_daty | do_daty=date(0,0,0)))
                     & ((od_platnosc<=FAKS.TZ | od_platnosc=date(0,0,0) ) & (FAKS.TZ<=do_platnosc
                        | do_platnosc=date(0,0,0)))
                     & (FAKS.ZEST='T' | drukuj_zal='T' & FAKS.WHERE='L')
                     & (FAKS.KZ='' | exec('FindAndGet','#table',FAKS,FAKS.KZ,,"WHERE",'')='N')
                     & FAKS.AKC='T'
                     & (mpp=0 | mpp=1 & FAKS.SP_WYM='T' | mpp=2 & FAKS.SP_WAR<>0)
                  ||
                     _faksv_n:=_faksv_v:=_faksv_b:=0;
                     {? FAKS.WHERE='L'
                     ||
                        FAKSV.prefix('',$FAKS.ref());
                        {? FAKSV.first()
                        || {!
                           |? _faksv_n+=FAKSV.WN_Z;
                              _faksv_v+=FAKSV.WV_Z;
                              _faksv_b+=FAKSV.WB_Z;
                              FAKSV.next()
                           !}
                        ?}
                     ||
                        _seek:=1;
                        _fakzpar:=FAKS.SYMF<>'' & FAKS.T().PAR='N';
                        {? _fakzpar=1
                        ||
                           FAKS.cntx_psh();
                           FAKS.use(8+FAKS.SYMF);
                           FAKS.prefix();
                           _seek:=FAKS.seek(BB.sqlint(FAKS.SYMF),)
                        ?};
                        {? _seek=1
                        ||
                           FAPOW.prefix($FAKS.ref(),'N');
                           {? FAPOW.first()
                           || {!
                              |? FAKS.cntx_psh(); TYPYSP.cntx_psh();
                                 FAKS.use(8+FAPOW.FAKS);
                                 FAKS.prefix();
                                 _zest:=FAKS.seek(BB.sqlint(FAPOW.FAKS),);
                                 FAKS.cntx_pop(); TYPYSP.cntx_pop();
                                 {? _zest=1
                                 || FAKSV.cntx_psh;
                                    FAKSV.use('faksv'+(8+FAPOW.FAKS+3));
                                    FAKSV.index('FAPOW_SV'); FAKSV.prefix($FAPOW.ref());
                                    {? FAKSV.first()
                                    || {!
                                       |? _faksv_n+=FAKSV.WN_Z;
                                          _faksv_v+=FAKSV.WV_Z;
                                          _faksv_b+=FAKSV.WB_Z;
                                          FAKSV.next()
                                       !}
                                    ?};
                                    FAKSV.cntx_pop
                                 ?};
                                 FAPOW.next()
                              !}
                           ?}
                        ?};
                        {? _fakzpar=1 || FAKS.cntx_pop() ?}
                     ?};
                     X_Xw1.blank(1);
                     X_Xw1.SYM    :=FAKS.SYM;
                     X_Xw1.KH     :=KH.SKR;
                     X_Xw1.KH_ODB :=FAKS.KH_ODB().KOD;
                     X_Xw1.D      :=FAKS.D;
                     X_Xw1.TZ     :=FAKS.TZ;
                     {? FAKS.WHERE='L'
                     ||
                        X_Xw1.NETTO  :=_faksv_n;
                        X_Xw1.VAT    :=_faksv_v;
                        X_Xw1.BRUTTO :=_faksv_b

                     |? drukuj_zal='T'
                     ||
                        X_Xw1.NETTO  :=FAKS.NETTO-_faksv_n;
                        X_Xw1.VAT    :=FAKS.VAT-_faksv_v;
                        X_Xw1.BRUTTO :=FAKS.BRUTTO-_faksv_b
                     ||
                        X_Xw1.NETTO  :=FAKS.NETTO;
                        X_Xw1.VAT    :=FAKS.VAT;
                        X_Xw1.BRUTTO :=FAKS.BRUTTO
                     ?};
                     {? mpp
                     ||
                        X_Xw1.SP_WAR:=FAKS.SP_WAR;
                        X_Xw1.SP_WYM:=FAKS.SP_WYM
                     ?};
                     X_Xw1.add()
                  ?};
                  FAKS.next()
               !}
            ?};
            X_Xb.next()
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max)
   !}
?};
OKR.cntx_pop(); FAPOW.cntx_pop(); FAKS.cntx_pop(); FAKSV.cntx_pop();

PAR_WYDR.TITLE:='ZESTAWIENIE DOKUMENTÓW SPRZEDAŻY';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: filtr_kh='T'}{<{lmt+1}}{'Z wyborem kontrahentów'}{FI}
{IF: drukuj_zal='T'}{<{lmt+1}}{'W zestawieniu są dokumenty zaliczkowe'
   ' i wartość dokumentów rozliczających uwzględnia zaliczki'}{FI}
{IF: mpp=1}{<{lmt+1}}{'Tylko dokumenty z wymaganym split payment'}{FI}
{IF: mpp=2}{<{lmt+1}}{'Tylko dokumenty z wartością split payment'}{FI}
{<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
{<{lmt+1}}{'Data wystawienia'}{<{lmt+1+20}od_datyw}{<{lmt+1+40}do_datyw}
{<{lmt+1}}{'Data sprzedaży'}{<{lmt+1+20}od_daty}{<{lmt+1+40}do_daty}
{<{lmt+1}}{'Termin płatności'}{<{lmt+1+20}od_platnosc}{<{lmt+1+40}do_platnosc}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; lp:=0;~~}
{FOR: X_Xw1}
{INDEX: X_Xw1.ndx_tmp(,,'D',,)}
{DO}
   {
   w[1] :=form(lp+=1,,,'99');
   w[2] :=form(X_Xw1.SYM);
   w[3] :=form(X_Xw1.KH);
   w[9] :=form(X_Xw1.KH_ODB);
   w[4] :=form(X_Xw1.D);
   w[5] :=form(X_Xw1.TZ);
   w[6] :=form(X_Xw1.NETTO,,doklw,'.,');
   w[7] :=form(X_Xw1.VAT,,doklw,'.,');
   w[8] :=form(X_Xw1.BRUTTO,,doklw,'.,');
   {? mpp
   ||
      w[10]:=form(X_Xw1.SP_WAR,,doklw,'.,');
      w[11]:=form(X_Xw1.SP_WYM)
   ?};
   s[1] +=X_Xw1.NETTO;
   s[2] +=X_Xw1.VAT;
   s[3] +=X_Xw1.BRUTTO;
   {? mpp
   ||
      s[4]+=X_Xw1.SP_WAR
   ?}
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1] :=w[2] :=w[3] :=w[4] :=w[9] :='';
w[5] :='Razem:'; PRN.FORM[5] :=0;
w[6] :=form(s[1],,doklw,'.,');
w[7] :=form(s[2],,doklw,'.,');
w[8] :=form(s[3],,doklw,'.,');
{? mpp
||
   w[10]:=form(s[4],,doklw,'.,');
   w[11]:=''
?};
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
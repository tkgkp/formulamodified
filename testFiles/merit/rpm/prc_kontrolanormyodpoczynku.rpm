:!UTF-8
{TITLE: Kontrola norm odpoczynku}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','pb','n11','n35','n24','opis','ref_okr');

   tresc:='okrodpoc';
   PAR_WYDR.TITLE:='Norma odpoczynku'@;

   PRN:=obj_new(@.CLASS.PRINT, 9);
   {? ~rep_export() || PRN.add("Lp+=1",5,'Lp.'@,1,,,,,',,\'9.\'') ?};
   PRN.add("P.T",10,'Nr teczki'@);
   PRN.add("P.OSOBA().NAZWISKO",25,'Nazwisko'@);
   PRN.add("P.OSOBA().PIERWSZE",25,'Imię'@);
   PRN.add("P.ST().ST",20,'Stanowisko'@);
   PRN.add("opis",50,'Opis (przekroczenie)'@);
   PRN.s_frame();

   _POM:=tab_tmp(1,
      'DATA','DATE','DATA WYJ',
      'WYJ','DATE','DATA WEJ',
      'DOBA','TIME','DOBA OD',
      'WY','TIME','WYJSCIE',
      'WE','TIME','WEJSCIE',
      'T','REAL','T',
      'P','INTEGER','P',
      'NR','INTEGER','NR',
      'ZAP','INTEGER','ZAP',
      'N','INTEGER','NIEO',
      'CZAS','REAL','CZAS'
   );

   prn1:='PRN';
   lmt:=rsep:=Lp:=pb:=0;
   vp:=h_rat:=1;
   n11:=n35:=n24:=time(0,0,0);
   opis:='';

   P.cntx_psh();
   N.cntx_psh();
   N.index('NIEOBECN');
   N.prefix();
   H.cntx_psh();
   H.index('_HISTDAT');
   R_WZCZ.cntx_psh();
   R_REJ_WW.cntx_psh();
   A_OKR.cntx_psh();
   A_OKR.index('A_OKRNN');
   A_OKR.win_sel('SLO');
   A_OKR.seek(__HARM.OKR_REF,,1);
   {? A_OKR.select(,1,5,'dpu:d')
   || ref_okr:=A_OKR.ref();
      exec('czytaj','#stalesys',A_OKR.DO,KST,'R_NTYG','R_NNTYG','R_NLIM');
      {? P.f_active() || P.f_clear() ?};
      _args:=exec('wybierz_args','pracownik');
      _args.DOMAIN:='PRC';
      _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
      _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
      {? +P_FILTER.STATUS
      || _args.F_ZATR:={? +P_FILTER.F_ZATR().KOD || P_FILTER.F_ZATR().KOD || '*T' ?};
         _args.VIEW:=P_FILTER.STATUS
      || _args.F_ZATR:=__PARSES.getVal('F_ZATR');
         _args.VIEW:=__PARSES.getVal('ZakresDanych')
      ?};
      _args.SQL_FROM:='';
      _args.SQL_WHERE:='P.REFERENCE in (select A_OKRP.P from A_OKRP where A_OKRP.OKR=\''+$ref_okr+'\' order by 1)';
      _TAB:=exec('wybierz','pracownik',_args).P;
      _NDX:=_TAB.ndx_tmp(,1,'SQL',,);
      params_set('P',_TAB,'NDX',_NDX,'POM',_POM)
   || ref_okr:=null()
   ?}
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','pb','n11','n35','n24','opis','ref_okr');
   exec('czytaj','#stalesys',date(),KST,'R_ODOB','R_OTYG','R_OTYGZ');
   R_REJ_WW.cntx_pop();
   R_WZCZ.cntx_pop();
   A_OKR.cntx_pop();
   H.cntx_pop();
   N.cntx_pop();
   P.cntx_pop()
}
{EXIT: ref_okr=null() | ~params_get().P.first()}
{COMMENT}OLD: okrodpoc.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{  h_rat:=charleft(); ~~}
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export || 1 || h_rat ?}; ~~}
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'%1 %2'['Nazwa okresu:'@,A_OKR.NAZ().NAZ]}
   {<{ceil(lmt*h_rat)+1}'%1 %2 %3 %4'['Od daty:'@,A_OKR.OD$1,'do daty:'@,A_OKR.DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
════════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{  PRN.add("n11$1",12,'%1h odpoczynku dobowego'@[time(#(3+KST.R_ODOB),#(KST.R_ODOB+2))$1],1);
   PRN.add("n35$1",12,'%1h odpoczynku tygodniowego'@[time(#(3+KST.R_OTYG),#(KST.R_OTYG+2))$1],1);
   PRN.add("n24$1",12,'%1h odpoczynku tygodniowego zmiana'@[time(#(3+KST.R_OTYGZ),#(KST.R_OTYGZ+2))$1],1);
   lmt:=int((charleft()-PRN.width())/2); ~~
}
{FOR: A_OKRP}
{INDEX: 'A_OKRNI'}
{PREFIX: A_OKR.ref()}
   {DO}
      {  A_OKRP.P(); ~~}
      {IF: _cfg:=params_get(); _cfg.P.index(_cfg.NDX); _cfg.P.find_key($A_OKRP.P) & A_OKRP.PRZEP<>*0}
         {  _POM:=params_get().POM;
            _POM.erase();
            R_WZCZ.index('R_WZCZ');
            R_WZCZ.prefix(P.name(),P.ref());
            _od:={? P.DZA>A_OKR.OD || P.DZA || A_OKR.OD ?};
            _do:={? P.DZ<>#0 & P.DZ<A_OKR.DO || P.DZ || A_OKR.DO ?};
            _dni:=A_OKR.DO-A_OKR.OD+1;
            {! _ii:=1.._dni
            |! _POM.blank();
               _POM.DATA:=A_OKR.OD+_ii-1;
               _POM.NR:=(_ii+6)%7;
               {? _POM.DATA>=_od & _POM.DATA<=_do
               || _Kalend:=
                     {? R_WZCZ.find_le(_od+_ii-1)
                     || {? R_WZCZ.GRAFIK='T' || R_WZCZ.CZESC || R_WZCZ.KAL ?}
                     || P.KAL
                     ?};
                  __KAL.set_cal(_Kalend,(_od+_ii-1)~1);
                  _POM.P:=1;
                  _POM.ZAP:=0;
                  _POM.N:=0;
                  _POM.N:=(N.find_le('N',P.ref(),_POM.DATA) & N.DO>=_POM.DATA);
                  {? KAL_DEF.CZAS<>*0
                  || _POM.DOBA:=KAL_DEF.POCZATEK;
                     _POM.CZAS:=KAL_DEF.CZAS~1+KAL_DEF.CZAS~2/60
                  ?}
               || _POM.P:=0
               ?};
               _POM.add()
            !};
            _t_wy:=time(0,0,0);
            _dt:=date(0,0,0);
            _rok:=A_OKR.OD~1;
            _mc:=A_OKR.OD~2;
            {!
            |? MASK.Use('R_REJ_WW',_rok,_mc);
               _Ndx_R:=R_REJ_WW.ndx_tmp('',,'P',,,'RD',,,'DZK',,,'DZ',,,'GD',,);
               R_REJ_WW.clear();
               R_REJ_WW.index(_Ndx_R);
               R_REJ_WW.prefix(P.ref(),'normalne');
               {? R_REJ_WW.first()
               || {!
                  |? {? _POM.find_key(R_REJ_WW.DZ)
                     || _POM.ZAP:=1;
                        _POM.put()
                     ?};
'analiza tylko [ARP] i odczytów w okresie';
                     {? R_REJ_WW.DZ>=A_OKR.OD & R_REJ_WW.DZ<=A_OKR.DO
                     || {? 2+R_REJ_WW.TP='WY'
                        || _t_wy:=R_REJ_WW.GD;
                           _dt:=R_REJ_WW.DZ;
                           _zm:=0;
                           _dzk:=R_REJ_WW.DZK;
                           {? (_dt+1)~3=1
                              || R_REJ_WW.ndx_drop();
'zmiana maski';
                                 {? (_mc+1)>12
                                 || _rok+=1;
                                    _mc:=1
                                 || _mc+=1
                                 ?};
                                 MASK.Use('R_REJ_WW',_rok,_mc);
                                 _Ndx_R:=R_REJ_WW.ndx_tmp('',,'P',,,'RD',,,'DZK',,,'DZ',,,'GD',,);
                                 R_REJ_WW.clear();
                                 R_REJ_WW.index(_Ndx_R);
                                 R_REJ_WW.prefix(P.ref(),'normalne');
                                 _zm:=1
                           ?};
                           {? {? _zm || R_REJ_WW.first() || R_REJ_WW.next() ?}
                           || {? _dzk<R_REJ_WW.DZK
                              || {? ~_zm
                                 || {? _POM.find_key(R_REJ_WW.DZ)
                                    || _POM.ZAP:=1;
'jest jakikolwiek odczyt';
                                       _POM.put()
                                    ?}
                                 ?};
                                 {? 2+R_REJ_WW.TP='WE'
                                 || {? _POM.find_key(_dt)
                                    || _POM.WY:=_t_wy;
                                       _POM.WE:=R_REJ_WW.GD;
                                       _db:=(R_REJ_WW.DZ-_dt)*24;
                                       {? _db
                                       || _db+=24-(_POM.WY~1+_POM.WY~2/60+_POM.WY~3/3600);
                                          _db+=_POM.WE~1+_POM.WE~2/60+_POM.WE~3/3600;
                                          _db-=24
                                       || _r:=_POM.WE-_POM.WY;
                                          _db:=_r~1+_r~2/60+_r~3/3600
                                       ?};
                                       _POM.T:=_db;
                                       _POM.WYJ:=R_REJ_WW.DZ;
                                       _POM.put()
                                    ?}
                                 || {? _zm || 1 || R_REJ_WW.prev() ?}
                                 ?}
                              ?}
                           ?}
                        ?}
                     ?};
                     R_REJ_WW.next()
                  !}
               ?};
               {? (_mc+1)>12
               || _rok+=1;
                  _mc:=1
               || _mc+=1
               ?};
               R_REJ_WW.ndx_drop();
               date(A_OKR.DO~1,A_OKR.DO~2,0)>=date(_rok, _mc, 0)
            !};
            n11:=time(#(3+KST.R_ODOB),#(KST.R_ODOB+2));
            n11:=n11~1+n11~2/60;
            n35:=time(#(3+KST.R_OTYG),#(KST.R_OTYG+2));
            n35:=n35~1+n35~2/60;
            n24:=time(#(3+KST.R_OTYGZ),#(KST.R_OTYGZ+2));
            n24:=n24~1+n24~2/60;
            _opis11:=_opis35:=_opis24:='';
            _tydzien:=_max:=0;
            _t11:=n11;
            _t35:=n35;
            _t24:=n24;
'normalizacja i zebranie danych';
            {? _POM.first()
            || {!
               |? _t:=_POM.T;
                  {? _t>24
                  || _POM.T:=24;
                     _t-=_POM.T;
                     _POM.put();
                     _POM.cntx_psh();
                     {? _POM.next()
                        || _POM.T+=fabs(_t-_POM.T);
                           _POM.put()
                     ?};
                     _POM.cntx_pop()
                  ?};
'norma dobowa';
                  {? _POM.WYJ<>date(0,0,0) & _POM.T<_t11 & (_POM.DATA+1=_POM.WYJ | _POM.DATA=_POM.WYJ)
                  || _opis11+=
                        {? ~(_opis11*'Odpoczynek dobowy:'@)
                        || 'Odpoczynek dobowy: '@
                        || ''
                        ?}+_POM.DATA$1+' - '+$(*(_POM.T$2*60))+', ';
                     {? _POM.T<n11 || n11:=_POM.T ?}
                  ?};
                  {? _tydzien <> _POM.NR
                  ||
'zerowanie zmiennych i zapisanie informacji';
                     {? _t35>_max & _tydzien
                        || n35:=_max;
                           _opis35+=
                              {? ~(_opis35*'Odpoczynek tygodniowy:'@)
                              || 'Odpoczynek tygodniowy: '@
                              || ''
                              ?}+$_tydzien+', '
                     ?};
                     {? _t24>_max & _tydzien
                     || n24:=_max;
                        _opis24+=
                           {? ~(_opis24*'Odpoczynek tygodniowy zmiana:'@)
                           || 'Odpoczynek tygodniowy zmiana: '@
                           || ''
                           ?}+$_tydzien+', '
                     ?};
                     _tydzien:=_POM.NR;
                     _licz:=0;
                     _max:=0
                  ?};
'analiza danych';
                  {? _POM.WYJ<>#0 || _licz:=_POM.T ?};
                  {? _POM.WYJ=#0
                  || _licz+=
                        {? _POM.N
                        || {? _POM.DOBA<>*0
                           || _POM.DOBA~1+_POM.DOBA~2/60
                           || 24
                           ?}
                        || {? _POM.T
                           || _POM.T
                           || {? _POM.CZAS
                              || 24-_POM.CZAS
                              || 24
                              ?}
                           ?}
                        ?}
                  ?};
                  {? _licz>_max || _max:=_licz ?};
                  _POM.next()
               !}
            ?};
            n11:=time(int(n11),(n11-int(n11))*60$0,0);
            n35:=time(int(n35),(n35-int(n35))*60$0,0);
            n24:=time(int(n24),(n24-int(n24))*60$0,0);
            opis:=
               {? +_opis11 || _opis11 || '' ?}+
               {? +_opis35
                  || {? +_opis11 || ';' || '' ?}+_opis35
                  || ''
               ?}+
               {? +_opis24
                  || {? +_opis11 | +_opis35 || ';' || '' ?}+_opis24
                  || ''
               ?}; ~~
         }
         {IF:  n11<time(#(3+KST.R_ODOB),#(KST.R_ODOB+2),0)
               |
               n35<time(#(3+KST.R_OTYG),#(KST.R_OTYG+2),0)
               |
               n24<time(#(3+KST.R_OTYGZ),#(KST.R_OTYGZ+2),0)
         }
            {SUBSTITUTE: 'LINIA0'}
         {FI}
      {FI}
   {OD}
{MACRO-}
{COMMENT}
════════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: tresc}
:!UTF-8
{TITLE:F.  Bilans przekształcenia}
{PRINTER: exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
     PRN:=obj_new(@.CLASS.PRINT,6); w1:=w2:=w3:=w6:=''; i:=0;
     PRN.sl_frame(); kolumna:=1;
     poz_1:=obj_new(11); poz_2:=obj_new(11); poz_11:=obj_new(11); poz_22:=obj_new(11);
     w:=obj_new(6); pyt:=exit:=0;  kon:=kon1:=synt:=konto1:=pozsum:='';
     sum1:=sum2:=sum3:=sum4:=sum5:=sum6:=sum7:=sum8:=0; separat:=''; rok:=obj_new(6);
     first:=druk:=sumsynt:=dlusynt:=0; w[2]:=w[6]:=''; pozkon:=obj_new(6); dalej:=0; rok_hed:='';
     _a:=SSTALE.AR().KOD; pozkon[2]:=pozkon[6]:=''; kon_1:=kon_2:='';
     _b:=form(0,-2);
     DOK.use('doku'+_a+_b);
     POZ.use('pozy'+_a+_b);
     POW.use('pow_'+_a+_b);
     indtym1:=POZ.ndx_tmp('',1,'DOK','ODD',0,'WAL',,0,'DOK','ZP',0,'KON',,0,'PKON',,0);
     indtym11:=POZ.ndx_tmp('',1,'DOK','ODD',0,'DOK','ZP',0,'KON',,0,'PKON',,0);
     indtym2:=POZ.ndx_tmp('',1,'WAL',,0,'DOK','ZP',0,'KON',,0,'PKON',,0);
     indtym22:=POZ.ndx_tmp('',1,'DOK','ZP',0,'KON',,0,'PKON',,0);
     indtym3:=POZ.ndx_tmp('',1,'DOK','ODD',0,'WAL',,0,'DOK','ZP',0,'PKON',,0,'KON',,0);
     indtym33:=POZ.ndx_tmp('',1,'DOK','ODD',0,'DOK','ZP',0,'PKON',,0,'KON',,0);
     indtym4:=POZ.ndx_tmp('',1,'WAL',,0,'DOK','ZP',0,'PKON',,0,'KON',,0);
     indtym44:=POZ.ndx_tmp('',1,'DOK','ZP',0,'PKON',,0,'KON',,0);
     ilesp:=5; opiskon1:=obj_new(11); opiskon2:=obj_new(11);  pozycja:=''; miejsce:=obj_new(11);
     {! _i:=1..11 |! opiskon1[_i]:=opiskon2[_i]:=''; miejsce[_i]:=0 !}; ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA);
     font_gr:=0; color:=''; prn1:=prn2:='PRN';
     lm:=ll:=lmt:=rsep:=lsep:=0; vp:=1; hrsep:=rsep:=PAR_WYDR.RSEP; PAR_WYDR.RSEP:=1;
     fun:="{? lsep>0 || lsep-=1; rsep:=1 || rsep:=hrsep ?}"
}
{END:
     obj_del(w); obj_del(poz_1); obj_del(poz_2);  obj_del(poz_11); obj_del(poz_22); obj_del(pozkon);
     obj_del(PRN); &w1; &w2; &w6; &w3; &i; &kon_1; &kon_2; VAR_DEL.delete('exit');
     _a:=SSTALE.AR().KOD; _b:=form(SSTALE.AO().NR,-2);
     DOK.use('doku'+_a+_b);
     POZ.use('pozy'+_a+_b);
     POW.use('pow_'+_a+_b); POZ.ndx_drop; obj_del(rok); &rok_hed; &kolumna;
     &pyt;  &kon; &kon1; &sum1; &sum2; &sum3; &sum4; &first; &druk; &separat;
     &sum5; &sum6; &sumsynt; &synt; &dlusynt; &sum7; &sum8; &konto1;&pozsum;
     &indtym1; &indtym2;  &indtym3; &indtym4; &indtym11; &indtym22;  &indtym33; &indtym44;
     &dalej; &ilesp; obj_del(opiskon1); obj_del(opiskon2); &pozycja; obj_del(miejsce);
     font_gr:=1; &color; &prn1; &prn2;
     &lm; &ll; &vp; &lmt; PAR_WYDR.RSEP:=rsep; &rsep; &lsep; &hrsep; &fun
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: ks_bp.rpm
Utworzony:   2000-10-30 [AMK] Artur Makos
==================================================
Zawartosc: Wydruk bilansu przeksztalcenia.
{COMMENT-}
{ pyt:=FUN.choice('Sortuj wg symboli kont roku:'@,,'Poprzedniego'@,'Obecnego'@);~~}
{EXIT: ~pyt}
{ OKRO_F.cntx_psh;
  OKRO_F.index('ROK');
  OKRO_F.prefix(SSTALE.AR);
  {? OKRO_F.first() || _kodokr:=form(OKRO_F.NR,-2) ?};
  OKRO_F.cntx_pop;
  DOK.use('doku'+SSTALE.AR().KOD+_kodokr);
  POZ.use('pozy'+SSTALE.AR().KOD+_kodokr);
  POW.use('pow_'+SSTALE.AR().KOD+_kodokr);~~
}
{IF: pyt=1}
   {PRN.add("w[1]",42,'Symbol konta w poprz. roku'@);~~}
   {PRN.add("w[2]",45,'Opis konta w poprzednim roku'@);~~}
   {PRN.add("w[3]",35,'Symbol konta w obecnym roku'@);~~}
   {PRN.add("w[6]",45,'Opis konta w obecnym roku'@);~~}
   { {? WYDRUKI.ODD
     || {? SSTALE.WAL<>FINFO.NAROD
        || POZ.index(indtym3);  POZ.prefix(WYDRUKI.ODD,SSTALE.WAL,'T')
        ||  POZ.index(indtym33);  POZ.prefix(WYDRUKI.ODD,'T')
        ?}
     || {? SSTALE.WAL<>FINFO.NAROD
        || POZ.index(indtym4);  POZ.prefix(SSTALE.WAL,'T')
        || POZ.index(indtym44);  POZ.prefix('T')
        ?}
     ?}; dalej:=POZ.first(); ~~}
{ELSE}
    {PRN.add("w[1]",42,'Symbol konta w obecnym roku'@);~~}
    {PRN.add("w[2]",45,'Opis konta w obecnym roku'@);~~}
    {PRN.add("w[3]",35,'Symbol konta w poprzednim roku'@);~~}
    {PRN.add("w[6]",45,'Opis konta w poprzednim roku'@);~~}
    { {? WYDRUKI.ODD
      || {? SSTALE.WAL<>FINFO.NAROD
         || POZ.index(indtym1);  POZ.prefix(WYDRUKI.ODD,SSTALE.WAL,'T')
         || POZ.index(indtym11);  POZ.prefix(WYDRUKI.ODD,'T')
         ?}
      || {? SSTALE.WAL<>FINFO.NAROD
         || POZ.index(indtym2);  POZ.prefix(SSTALE.WAL,'T')
         || POZ.index(indtym22);  POZ.prefix('T')
         ?}
      ?}; dalej:=POZ.first;~~}
{FI}
{PRN.add("w[4]",18,'Kwota Winien',1);~~}
{PRN.add("w[5]",18,'Kwota Ma',1);~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{ ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
  SSTALE.AR(); ROK_F.prev(); rok_hed:=ROK_F.NAZ;
  ROK_F.cntx_pop();
  PAR_WYDR.TITLE:='BILANS PRZEKSZTAŁCENIA  %1/%2'@[rok_hed,SSTALE.AO().ROK().NAZ]; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{IF: page=1}
{<1 'PARAMETRY WYDRUKU:'@}
{<1
{? WYDRUKI.ODD
   ||  'Jednostka księgowa: %1'@[WYDRUKI.ODD().OD]
   ||  'Jednostka księgowa: wszystkie jednostki księgowe'@
?}
}
{<1 'Waluta: %1'@[SSTALE.WAL().KOD]}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{IF: rep_export()}{TR}{FI}
{HEADER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{PRN.lbar()}{ vp:=1;~~}
{FOOTER-}
{COMMENT}*******************************************************{COMMENT-}
{MACRO: 'KS_BP1'}
 {IF: pozkon[kolumna]<>{? pyt=1
                       || {? kolumna=2 || POZ.PKON || POZ.KON ?}
                       || {? kolumna=2 || POZ.KON || POZ.PKON ?}
                       ?}}
   { pozkon[kolumna]:={? pyt=1
                      || {? kolumna=2 || POZ.PKON || POZ.KON ?}
                      || {? kolumna=2 || POZ.KON || POZ.PKON ?}
                      ?};  {? kolumna=2 || miejsce[1]:=ilesp || miejsce[1]:=0 ?};  poziom:=1;
     {? kolumna=2 || {! _i:=1..11 |! poz_1[_i]:=poz_11[_i]:='' !} || {! _i:=1..11 |! poz_2[_i]:= poz_22[_i]:='' !} ?};
     KS.cntx_psh;  KS.index('SYM'); KS.prefix(rok[kolumna],dlusynt+pozkon[kolumna]); pozycja:=pozkon[kolumna];
     pozycja:=dlusynt-pozycja; {? separat<>',' || pozycja:=1-pozycja ?}; ~~}
          {IF:  KS.first}
          { BUD.index('KS'); BUD.prefix(KS.ref());
             {? BUD.first
             || {! |?
                 BUD.SLU().SLU();
                 miejsce[BUD.POZ+1]:=miejsce[BUD.POZ]+{? BUD.POZ=1 || dlusynt || SLU.DL ?}+
                 {? separat<>',' || 1 || 0 ?}; BUD.next
                !}
             ?};~~}
             {IF: BUD.first}
               {IF: {? kolumna=2 || opiskon1[1] || opiskon2[1] ?}<>dlusynt+pozkon[kolumna]}
                  { {? kolumna=2
                    || poz_1[1]:=miejsce[1]*' '+KS.SYM; poz_11[1]:=KS.NAZ
                    || poz_2[1]:=KS.SYM; poz_22[1]:=KS.NAZ
                    ?};  poziom+=1;
                    {? kolumna=2 || opiskon1[1]:=KS.SYM || opiskon2[1]:=KS.SYM ?};
                    {! _i:=2..11 |!  {? kolumna=2 || opiskon1[_i]:='' || opiskon2[_i]:='' ?} !}; ~~}
               {FI}
                {FOR: BUD}
                {INDEX: 'KS'}
                {PREFIX: KS.ref()}
                {DO}
                 { BUD.SLU().SLU();
                   {? {? kolumna=2 || opiskon1[BUD.POZ+1] || opiskon2[BUD.POZ+1] ?}<>SLU.DL+pozycja
                   || {? kolumna=2 || opiskon1[BUD.POZ+1]:=SLU.DL+pozycja || opiskon2[BUD.POZ+1]:=SLU.DL+pozycja ?};
                      {! _i:=(BUD.POZ+2)..11 |! {? kolumna=2 || opiskon1[_i]:='' || opiskon2[_i]:='' ?} !};
                      SLO.cntx_psh; SLO.index('SL'); SLO.prefix(SLU.ref);
                      {? SLO.find_key(SLU.DL+pozycja)
                      || BUD.cntx_psh;
                          {? BUD.next()
                          || {? kolumna=2
                             || poz_1[poziom]:=miejsce[BUD.POZ]*' '+SLO.KOD; poz_11[poziom]:=SLO.TR
                             || poz_2[poziom]:=miejsce[BUD.POZ]*' '+SLO.KOD; poz_22[poziom]:=SLO.TR
                             ?}; poziom+=1
                          || {? kolumna=2 || w[2]:=SLO.TR || w[6]:=SLO.TR ?}
                          ?};  BUD.cntx_pop
                       ?};  SLO.cntx_pop()
                   ?}; pozycja:=SLU.DL-pozycja; {? separat<>',' || pozycja:=1-pozycja ?}; ~~}
                 {OD}
             {ELIF: {? kolumna=2 || opiskon1[1] || opiskon2[1] ?}<>dlusynt+pozkon[kolumna]}
                { {? kolumna=2 || w[2]:=KS.NAZ || w[6]:=KS.NAZ ?}; {? kolumna=2 || opiskon1[1]:=KS.SYM || opiskon2[1]:=KS.SYM ?};
                {! _i:=2..11 |! {? kolumna=2 || opiskon1[_i]:='' || opiskon2[_i]:='' ?} !};~~}
             {FI}
       {FI}{  KS.cntx_pop; ~~}
    {IF: rep_export()}
    {_tab:=exec('kon_fun','konto',pozkon[kolumna]);
     _opis:='';
     {! _i:=1..obj_len(_tab[2])
     |! _opis+=_tab[2][_i]+' / '
     !};
     &_tab;
     {? kolumna=2 || w[2]:=_opis-3 || w[6]:=_opis-3 ?};
     ~~}
    {FI}
    {FI}
{MACRO-}
{COMMENT}*******************************************************{COMMENT-}
{MACRO: 'WYDR_AN'}
{IF: ~rep_export()}
{ i:=1;~~}
{WHILE: i<12}
{DO}
{  w[1]:=poz_1[i]; w[2]:=poz_11[i]; w[3]:=poz_2[i]; w[6]:=poz_22[i]; w[4]:=w[5]:=''; ~~}
{IF: w[1]<>'' | w[2]<>'' | w[3]<>'' | w[6]<>''}
{SUBSTITUTE: 'LINIA0'}
{FI}{ i+=1;~~}
{OD}
{FI}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT:'T8G'}{SPACE:'C'}
{  exit:=0;
   ROK_F.cntx_psh(); SSTALE.AR();
   {? pyt=1
   || {? ~exec('first_rok_obsz','zam_okr_rok','FKS',ROK_F.ref()) & ROK_F.prev()
      || dlusynt:=ROK_F.SYNT; separat:=ROK_F.SEP; rok[2]:=ROK_F.ref(); ROK_F.next(); rok[6]:=ROK_F.ref()
      || FUN.info('Obecny rok jest pierwszy.'@); exit:=1
      ?}
   || {? exec('first_rok_obsz','zam_okr_rok','FKS',ROK_F.ref())
      || FUN.info('Obecny rok jest pierwszy.'@); exit:=1
      || dlusynt:=SSTALE.AO().ROK().SYNT; separat:=SSTALE.AO().ROK().SEP;
         rok[2]:=SSTALE.AR; SSTALE.AR(); ROK_F.prev(); rok[6]:=ROK_F.ref()
      ?}
   ?};
   ROK_F.cntx_pop();
   ~~
}
{IF: exit}{EXIT}{FI}
{ pozsum:=$('{? SSTALE.WAL=FINFO.NAROD || POZ.SUM || POZ.SUMW ?}');~~}
{WHILE: dalej}
{DO}
{IF: POZ.PKON<>''}
{IF: pyt=1}{ kon_1:='POZ.PKON'; kon_2:='POZ.KON';~~}{ELSE}{ kon_1:='POZ.KON'; kon_2:='POZ.PKON';~~}{FI}
{ {? -POZ.STR='wn' || sum5+=pozsum() ||  sum6+=pozsum() ?};
  {?  kon=($(kon_1))() & kon1<>($(kon_2))()
  || w[1]:='';  first+=1; w[3]:=($(kon_2))(); POZ.cntx_psh;
     {? POZ.next
     || {? kon<>($(kon_1))() | (kon=($(kon_1))() & kon1<>($(kon_2))()) || druk:=1 || druk:=0 ?}
     ||  druk:=1
     ?}; POZ.cntx_pop;
     {? -POZ.STR='wn' || sum1:=pozsum(); sum2:=0; sum3+=pozsum() ||  sum1:=0; sum2:=pozsum(); sum4+=pozsum() ?}
  |?  kon=($(kon_1))() & kon1=($(kon_2))()
  || w[3]:=($(kon_2))(); POZ.cntx_psh;
     {? POZ.next
     || {? kon<>($(kon_1))() | (kon=($(kon_1))() & kon1<>($(kon_2))()) ||  druk:=1 ||  druk:=0 ?}
     || druk:=1
     ?};
     POZ.cntx_pop;
     {? -POZ.STR='wn' || sum1+=pozsum(); sum3+=pozsum() ||  sum2+=pozsum(); sum4+=pozsum() ?}
  || kon:=($(kon_1))(); w[1]:=ilesp*' '+kon; w[3]:=kon1:=($(kon_2))(); first:=0; sum1:=sum2:=sum3:=sum4:=0; POZ.cntx_psh;
     {? POZ.next
     || {? kon<>($(kon_1))() | (kon=($(kon_1))() & kon1<>($(kon_2))()) ||  druk:=1 ||  druk:=0 ?}
     ||  druk:=1
     ?}; POZ.cntx_pop;
     {? -POZ.STR='wn' || sum1:=pozsum(); sum3:=pozsum() ||  sum2:=pozsum(); sum4:=pozsum() ?}
  ?};~~
}
{IF: druk}
{ w1:=w[1]; w3:=w[3];  {! _i:=1..11 |! poz_1[_i]:=poz_11[_i]:=poz_2[_i]:= poz_22[_i]:='' !};~~}
{ kolumna:=2;~~}{SUBSTITUTE: 'KS_BP1'}
{ kolumna:=6;~~}{SUBSTITUTE: 'KS_BP1'}{ w2:=w[2]; w6:=w[6]; ~~}
{SUBSTITUTE: 'WYDR_AN'}
{  w[1]:=w1; w[3]:=w3; w[2]:=w2; w[6]:=w6; w[4]:=form(sum1,,2,' ,'); w[5]:=form(sum2,,2,' ,');~~}
{SUBSTITUTE: 'LINIA0'}{ w[2]:=w[6]:='';~~}
{FI}
{IF: ~rep_export()}
{ POZ.cntx_psh; ~~}
{IF: POZ.next}
{IF: ($(kon_1))()<>kon & first}
{  w[1]:='Suma '+kon; w[3]:=''; w[4]:=form(sum3,,2,' ,');  w[5]:=form(sum4,,2,' ,'); rsep:=lsep:=1; ~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ELIF: first}
{  w[1]:='Suma '+kon; w[3]:=''; w[4]:=form(sum3,,2,' ,');  w[5]:=form(sum4,,2,' ,'); rsep:=lsep:=1;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ POZ.cntx_pop;~~}
{ {? synt<>dlusynt+($(kon_1))()
  ||  {? -POZ.STR='wn' || sum7:=pozsum(); sum8:=0 || sum8:=pozsum(); sum7:=0 ?}; synt:=dlusynt+($(kon_1))();
      konto1:=($(kon_1))()
  ||  {? -POZ.STR='wn' || sum7+=pozsum() || sum8+=pozsum() ?};
      {? konto1<>($(kon_1))()
      ||  POZ.cntx_psh;
         {? POZ.next ||  {? synt<>dlusynt+($(kon_1))() || sumsynt:=1 ?} ||  sumsynt:=1 ?};
         POZ.cntx_pop
      ?}
  ?};~~
}
{IF: sumsynt}
{ w[1]:='Suma '+synt;  w[3]:=''; w[4]:=form(sum7,,2,' ,');  w[5]:=form(sum8,,2,' ,'); rsep:=lsep:=1;~~}
{SUBSTITUTE: 'LINIA0'}{ sumsynt:=0; ~~}
{FI}
{FI}
{FI}{ dalej:=POZ.next;~~}
{OD}
{IF: sum5 | sum6}
{ w[1]:='RAZEM'; w[3]:=''; w[4]:=form(sum5,,2,' ,');  w[5]:=form(sum6,,2,' ,'); rsep:=1;~~}
{SUBSTITUTE: 'LINIA0'}{rsep:=hrsep;~~}
{ELSE}
{ FUN.info('Brak danych\n'+'lub nie zaksięgowano bilansu otwarcia.'@);~~}
{EXIT}
{FI}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}{FOOTER-}
{ENTIRE}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
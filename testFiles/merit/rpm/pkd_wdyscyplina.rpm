:!UTF-8
{TITLE:Dyscyplina pracy}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='wdyscypl';
   PAR_WYDR.TITLE:='Dyscyplina pracy'@;
   par:=obj_new(10);
   par[6]:=par[9]:=0;
   P.cntx_psh();
   N.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','dyscyplinie pracy'@,'PKD_EZK_OPDS','PKD_EZK_ORDS');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || par[10]:=exec('pkd_wdyscyplina','!pkd_zes_orka',,tresc,PAR_WYDR.TITLE);
      {? par[6]:=par[10].size()
      || par[9]:={? par[10].DATA<>#0 || par[10].RODZ:=1; 2 || 1 ?};
         par[2]:=
            "  {? par[9]>1
               || {? P.KAL
                  || __KAL.set_cal(P.KAL().NAZWA,par[10].ROK)
                  || __KAL.set_cal('standard',par[10].ROK)
                  ?};
                  par[7]:=0;

                  N.prefix('N',P.ref(),DS.NB().RN);
                  {? N.last()
                  || {!
                     |? {? N.OD<=par[10].DATA & N.DO>=date(par[10].DATA~1,1,1)
                        || _od:={? N.OD<date(par[10].DATA~1,1,1) || date(par[10].DATA~1,1,1) || N.OD ?};
                           _do:={? N.DO>par[10].DATA || par[10].DATA || N.DO ?};
                           {? DS.NR='K'
                           || par[7]+=_do-_od+1
                           |? DS.NR='G'
                           || {? _od=N.OD & _do=N.DO
                              || {? __RUB.sys_attr(N.NB,1162) || par[7]+=ceil(N.NG) || par[7]+=N.NG ?}
                              || par[7]+=exec('nominal','godziny',_od,_do)
                              ?}
                           || par[7]+=__KAL.w_days(_od,_do)
                           ?}
                        ?};
                        N.prev() & N.DO>=date(par[10].DATA~1,1,1)
                     !}
                  ?};
                  par[3]:=DS.NN+DS.NZ-par[7]
               || par[7]:=DS.NW; par[3]:=DS.NP
               ?}
            ";

         par[1]:=obj_new(@.CLASS.PRINT,{? ~rep_export() || 8 || 11 ?}+(par[10].ROK=0));
         {? rep_export()
         || par[1].add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("P.OSOBA().PIERWSZE+' '+P.OSOBA().DRUGIE",41,'Imiona'@);
            par[1].add("P.T",9,'Nr teczki'@)
         ?};
         {? ~par[10].ROK || par[1].add("DS.R",4,'Rok'@,1,,,,,'4,,\'99\'') ?};
         par[1].add("DS.NB().RT",20,'Nieobecność'@);
         par[1].add("DS.NR",3,'Dni'@);
         par[1].add("DS.NN",7,'Limit bieżący'@,1,,,,,'7,2');
         par[1].add("DS.NZ",7,'Limit zaległy'@,1,,,,,'7,2');
         par[1].add("par[2](); par[7]",12,'Limit wykorzystany'@,1,,,,,'7,2');
         par[1].add("par[3]",9,'Limit pozostały'@,1,,,,,'7,2');
         par[1].add("DS.NL",7,'Nowy limit'@,1,,,,,'7,2');
         par[1].add("{?DS.DNL<>date(0,0,0)||DS.DNL$1||''?}",12,'Data zmiany limitu'@);
         par[1].s_frame();

         N.index('NIPRACNB');
         prn1:='par[1]';
         lmt:=rsep:=par[4]:=par[8]:=0;
         vp:=b_rat:=h_rat:=par[5]:=1;

         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   P.cntx_pop();
   N.cntx_pop()
}
{COMMENT}OLD: wdyscypl.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par[6]|~par[9]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Wybrane kody'@}: {IF: par[10].memo_txt(,1,'LISTA')<>''
      }{STR.gsub(1-par[10].memo_txt(,1,'LISTA')-1,',',', ')}{ELSE}brak ograniczeń{FI}
   {<{ceil(lmt*h_rat)+1}'Kontrola przekroczenia limitu'@}: {{? par[10].RODZ || 'nie'@ || 'tak'@ ?}}
   {IF: par[10].ROK}{<{ceil(lmt*h_rat)+1}'Rok kalendarzowy'@}: {par[10].ROK}{FI}
   {IF: par[9]>1}{<{ceil(lmt*h_rat)+1}'Wykorzystanie na dzień'@}: {par[10].DATA}{FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par[8]+=1,,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par[4]:=1; ~~ }
{MACRO-}
{MACRO: 'dyscypli'}
{IF:
   _lista:=par[10].memo_txt(,1,'LISTA');
   (par[10].RODZ | DS.NP<0) & (_lista='' | _lista*(','+$DS.NB().RN+','))
}
   {IF: ~par[4]}
      {SUBSTITUTE: 'pracownik'}
      {REP: par[1].fhbar(lmt)}
   {FI}
   { par[1].ini_line(); ~~ }
   {WHILE: par[1].next()}{DO}
      {REP: par[1].fline(lmt)}
   {OD}
{FI}
{MACRO-}
{MACRO: tresc}
{ par[4]:=0; ~~ }
{ENTIRE}
{REP: '{FOR: DS}{INDEX: \'DYSCYPLI\'}'+
      '{PREFIX: P.ref()'+{? par[10].ROK || ',par[10].ROK' || '' ?}+'}'+
      '{DO}{SUBSTITUTE: \'dyscypli\'}{OD}'}
{IF: par[4]}
   {REP: par[1].flbar(lmt)}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{FOOTER: 0}
   { vp:=0; ~~ }
{FOOTER-}

:!UTF-8
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: potwang.rpi
Utworzony:   2019-06-03 [] Katarzyna Niziolek
==================================================
Zawartosc: Potwierdzenie salda - jezyk angielski
{COMMENT-}
{ {? var_pres('PRN')>0 || obj_del(PRN) ?};
  {? var_pres('PRN1')>0 || obj_del(PRN1) ?};
  naz_str:='Page ';
  {! _i:=1..6 |! w[_i]:='' !};
  PRN:=obj_new(@.CLASS.PRINT,6); PRN.sl_frame();
  PRN.add("w[6]",8,'Agency'@);
  PRN.add("w[1]",62,'Document'@);
  PRN.add("w[2]",10,'Date of the document'@);
  {? SER_NAG.WAL=null || PRN.add("w[5]",8,'Currency'@) ?};
  PRN.add("form(w[3],,2,' ,')",18,'To OUR account'@,1);
  PRN.add("form(w[4],,2,' ,')",18,'To YOUR account'@,1);
  PRN1:=obj_new(@.CLASS.PRINT,6); PRN1.sl_frame();
  {? ROZNE.UT_VAT
  || PRN1.add("w[6]",8,'Agency'@);
     PRN1.add("w[1]",62,'Document'@);
     PRN1.add("w[2]",10,'Date of the document'@)
  || PRN1.add("w[1]",35)
  ?};
  {? SER_NAG.WAL=null || PRN1.add("w[5]",8,'Currency'@) ?};
  PRN1.add("form(w[3],,2,' ,')",20,'To YOUR account'@,1);
  PRN1.add("form(w[4],,2,' ,')",20,'To OUR account'@,1);
  {! _i:=1..licz_wal  |! sumy[_i][2]:=sumy[_i][3]:=sumy[_i][4]:=0 !};
  PAR_WYDR.HEAD:=0;
 ~~}
{COMMENT}---------------------CZY DRUKOWAC PROSBE O ZAPLATE-----------------{COMMENT-}
{ petla:=1; druk1:=obrn:=obrw:=0;
  SLUAPPL.cntx_psh(); SLU.cntx_psh(); SLO.cntx_psh();  SER_POZ.cntx_psh();
  SLUAPPL.prefix(); SLU.prefix(); FINFO.SLWAL().SLU();
  SLO.index('SL'); SLO.prefix(SLU.ref());
  {? SLO.first()
  || {! |?
        {? OPERATOR.DEPT=null
        || SER_POZ.index('SER_POZW'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),SLO.KOD)
        || SER_POZ.index('SER_POZO'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),OPERATOR.DEPT().OD,SLO.KOD)
        ?};
        {? SER_POZ.first()
        || {! |? obrn+=SER_POZ.DNASZ;  obrw+=SER_POZ.DWASZ; SER_POZ.next() !}
        ?};
        druk1:=(obrn$2>obrw$2);
        druk1=0 & SLO.next()
      !}
  ?};
  SLUAPPL.cntx_pop(); SLU.cntx_pop(); SLO.cntx_pop(); SER_POZ.cntx_pop();
~~}
{COMMENT}------------------------------HEADXA-----------------{COMMENT-}
{ PAR_WYDR.LOGO:=logo; prn1:=prn2:='PRN'; nr_str:=0;~~}
{IF: lineleft=0}
{IF: ~new_poz}{ new_poz:=1;~~}
{  PAR_WYDR.TITLE:='PART A'@;  tab_adr1[1]:='RECEIVER:'@; tab_adr1[2]:='SENDER:'@;
   tab_adr1[3]:='ADDRESS:'@;  tab_adr1[4]:='PAYER\'S CODE:'@;~~}
{LINE: 1}
{IF: ROZNE.UT_KOM}{SUBSTITUTE: 'KORHEADP'}{ELSE}{SUBSTITUTE: 'KORHEADL'}{FI}
{LINE: 1}
{IF: SER_NAG.WAL<>null}{<3 'CURRENCY:'@}{<20 SER_NAG.WAL().KOD+' '+SER_NAG.WAL().TR}
{exec('kh_dod_ini','kontrahent',SER_NAG.KH);~~}
{IF: |KH_DOD.NASZ_KOD<>''}{<3 exec('symbol','rozrach',1)+'  '+|KH_DOD.NASZ_KOD}{FI}
{LINE: 1}{FI}
{<1 'Pursuant to Art. 26 of the Accounting Act dated 29 September 1994 (amended: J.L. of 2023, item 120) we would like to request'@}
{<1 'confirmation, within 10 days, of the compliance of the following balances on PART B, listed in'@}
{<1 'our books at the day of: '@}{FONT: 'T8GB'}{$SER_NAG.DATA}{FONT: 'W8'}{'.'}
{LINE: 1}
{<3 'According to the Accounting Act tacit acceptance can\'t be used to confirm balances.'@}
{LINE: 1}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{LINE: 1}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FI}
{COMMENT}------------------------------STRONAA----------------{COMMENT-}
{  suma3:=suma4:=0; pierwszy:=1;~~}
{FOR: 'SER_POZ'}
{INDEX: 'SER_POZO'}
{PREFIX: REF.FIRMA,SER_NAG.ref(),OPERATOR.DEPT().OD}
{DO}
   { w[1]:=SER_POZ.DOK+{? SER_POZ.SYM_ZEW<>'' & SER_POZ.SYM_ZEW<>SER_POZ.DOK
                       || ' ('+SER_POZ.SYM_ZEW+')'
                       || ''
                       ?};
     w[6]:=SER_POZ.ODD().OD; w[2]:=$SER_POZ.DATA; w[5]:=SER_POZ.WAL().KOD;
     _v_n:=SER_POZ.DNASZ$2; _v_w:=SER_POZ.DWASZ$2;
     w[3]:=F.SWn(_v_n,_v_w); w[4]:=F.SMa(_v_n,_v_w);
     {? SER_NAG.WAL<>null
     || suma3+=w[3]; suma4+=w[4]
     || _znal:=0;
        {! _i:=1..licz_wal
        |? _znal=0
        |! {? sumy[_i][1]=SER_POZ.WAL().KOD
           || _znal:=1; sumy[_i][2]+=w[3]; sumy[_i][3]+=w[4]; sumy[_i][4]:=1
           ?}
        !}
     ?}; ~~}
{COMMENT}------------------------------LINIA KONCZACA + NOWY HEADER-----------------{COMMENT-}
   {IF: lineleft<3}{FONT: 'T8G'}
   {SPACE: 'C'}{SUBSTITUTE: 'LINIAF'}
   {IF: lineleft<>0}{PAGE}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {IF: lineleft=0}{SUBSTITUTE: 'TABHEAD'}{FI}{ vp:=1;~~}
   {FONT: 'T8G'}{SPACE: 'C'}
   {FI}
{COMMENT}------------------------------KONIEC NOWEGO HEADERA-----------------{COMMENT-}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{ separat:=rsep; ~~}
{IF: SER_NAG.WAL<>null}
{IF: lineleft<3}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{  w[1]:='BALANCE'; w[2]:=w[6]:=''; w[3]:=F.SWn(suma3,suma4);  w[4]:=F.SMa(suma3,suma4); ~~}
{SUBSTITUTE: 'LINIAFS'}
{ELSE}
{ liczwal:=0; {! _i:=1..licz_wal  |! {? sumy[_i][4]=1 || liczwal+=1 ?} !};~~}
{IF: lineleft<=(2+liczwal)}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{  w[1]:='BALANCE'; rsep:=1; w[2]:=w[3]:=w[4]:=w[6]:=''; licznik:=1; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{WHILE: licznik<=licz_wal}
{DO}
   {IF: sumy[licznik][4]<>0}
    {  w[3]:=sumy[licznik][2]; w[4]:=sumy[licznik][3]; w[5]:=sumy[licznik][1];~~}
    {SUBSTITUTE: 'LINIA0'} { w[1]:=''; rsep:=0;~~}
   {FI}
   { licznik+=1;~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI} { rsep:=separat;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{ENTIRE}
{LINE: 1}
{FONT: 'W8'}{SPACE: 'C'}
{IF: druk1}
{ ref_defw:=null;
  SLO.cntx_psh();
  SER_DEFW.index('SERDEFW1'); SER_DEFW.prefix(SER_NAG.KOD,SER_KOR.JEZYK);
  {? SER_DEFW.first() || ref_defw:=SER_DEFW.ref() ?};
  SLO.cntx_pop();~~}
{ _tmp:=SER_DEFW.memo_txt(0,1,'TEKST');STR.spl_word();STR.split(_tmp,'\n');~~}
   {WHILE: STR.next()}
   {DO}
      {tekst:=STR.get_word();~~}
      {<1 tekst}
   {OD}{ kontbank:=exec('get_rb','rozrach_kor');~~}
   {<1 'Method of payment - bank transfer: '@}
   {<3 SKID_RBK.BANK().NB+{? SKID_RBK.BANK().KODISO<>XINFO.KRAJ || ', SWIFT (BIC): '+SKID_RBK.BANK().SWIFT || '' ?}}
   {<1 'bank account: '@}
   {<3 kontbank}
{FI}
{LINE: 1}
{ wdt:=charleft(); ~~}
{<1>{wdt/2} 'Date'}{<{wdt/2}>{wdt} 'Company stamp'}
{LINE: 2}
{<1>{wdt/2} 50*'.'}{<{wdt/2}>{wdt} 50*'.'}
{ENTIRE-}
{IF: lineleft()<>0}{PAGE}{FI}
{COMMENT}------------------------------HEADXB-----------------{COMMENT-}
{ nr_str:=0;~~}
{IF: lineleft=0}
{IF: ~new_poz1}{ new_poz1:=1;~~}
{  PAR_WYDR.TITLE:='PART B'; prn1:=prn2:='PRN1';
   tab_adr1[2]:='RECEIVER:'@; tab_adr1[1]:='SENDER:'@; tab_adr1[3]:='ADDRESS:'@; tab_adr1[4]:='PAYER\'S CODE:'@;~~}
{LINE: 1}
{IF: ROZNE.UT_KOM}{SUBSTITUTE: 'KORHEADL'}{ELSE}{SUBSTITUTE: 'KORHEADP'}{FI}
{LINE: 1}
{IF: SER_NAG.WAL<>null}{<3 'CURRENCY:'@}{<20 SER_NAG.WAL().KOD+' '+SER_NAG.WAL().TR}
{LINE: 1}{FI}
{<1 'We confirm balance that is listed in your books at the day of: '@
   +$SER_NAG.DATA+'.'}
{LINE: 1}
{FONT: 'T8G'}{SPACE: 'C'}
   { lmt:=int((charleft()-PRN.width())/2); ~~}
{FI}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{COMMENT}------------------------------MACRO-STRONAB----------------{COMMENT-}
{IF: ROZNE.UT_VAT}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: 'SER_POZ'}
{INDEX: 'SER_POZO'}
{PREFIX: REF.FIRMA,SER_NAG.ref(),OPERATOR.DEPT().OD}
{DO}
   { w[1]:=SER_POZ.DOK+{? SER_POZ.SYM_ZEW<>'' & SER_POZ.SYM_ZEW<>SER_POZ.DOK
                       || ' ('+SER_POZ.SYM_ZEW+')'
                       || ''
                       ?};
     w[6]:=SER_POZ.ODD().OD; w[2]:=$SER_POZ.DATA; w[5]:=SER_POZ.WAL().KOD;
     _v_n:=SER_POZ.DNASZ$2; _v_w:=SER_POZ.DWASZ$2;
     w[3]:=F.SWn(_v_n,_v_w); w[4]:=F.SMa(_v_n,_v_w); ~~}
{COMMENT}------------------------------LINIA KONCZACA + NOWY HEADER-----------------{COMMENT-}
   {IF: lineleft<3}{FONT: 'T8G'}
   {SPACE: 'C'}{SUBSTITUTE: 'LINIAF'}
   {IF: lineleft<>0}{PAGE}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {IF: lineleft=0}{SUBSTITUTE: 'TABHEAD'}{FI}{ vp:=1;~~}
   {FONT: 'T8G'}{SPACE: 'C'}
   {FI}
{COMMENT}------------------------------KONIEC NOWEGO HEADERA-----------------{COMMENT-}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{FI}
{ w[2]:=w[5]:=w[6]:='';~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: SER_NAG.WAL<>null}
{ w[1]:='BALANCE'; w[3]:=F.SWn(suma3,suma4); w[4]:=F.SMa(suma3,suma4); ~~}
{IF: lineleft()<2}
{SUBSTITUTE: 'LINIAF'}{IF: lineleft()<>0}{PAGE}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{SUBSTITUTE: 'LINIA0'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{ELSE}
{IF: lineleft<=(2+liczwal)}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{IF: lineleft<>0}{PAGE}{FI}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{ w[1]:='BALANCE'; rsep:=separat; w[3]:=w[4]:=''; licznik:=1; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{WHILE: licznik<=licz_wal}
{DO}
   {IF: sumy[licznik][4]<>0}
    {  w[3]:=sumy[licznik][2]; w[4]:=sumy[licznik][3]; w[5]:=sumy[licznik][1];~~}
    {SUBSTITUTE: 'LINIA0'} { w[1]:=''; rsep:=0;~~}
   {FI}
   { licznik+=1;~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI} { rsep:=separat;~~}
{ENTIRE}
{LINE: 1}
{FONT: 'W8'}{SPACE: 'C'}
{ wdt:=charleft(); ~~}
{<1>{wdt/2} 'Date'@}{<{wdt/2}>{wdt} 'Company stamp'@}
{LINE: 2}
{<1>{wdt/2} 50*'.'}{<{wdt/2}>{wdt} 50*'.'}
{ENTIRE-}
{IF: lineleft()<>0}{PAGE}{FI}
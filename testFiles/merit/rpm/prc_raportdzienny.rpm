:!UTF-8
{TITLE: Raport dzienny}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','PRH','prn1','prn2','vp','lmt','rsep','h_rat','tresc','pb','Format','Nr','Karta','Teczka');
   VAR_DEL.delete('Pracownik','NormWej','NormWyj','NormPrac','PracWej','PracWyj','PracPrac','PracPrzer','Nadmiar','var');
   VAR_DEL.delete('Niedobor','Wydzial','Uwagi','Ok');

   _TAB:=exec('tab_prac','!prc_czp_wydr');
   _sql2:=''+" select * from P join UD_SKL join :_a using (P.REFERENCE, :_a.SQL) order by UD_SKL.SYMBOL";
   _TAB2:=sql(_sql2,_TAB);
   {? _TAB.first()
   || tresc:='w_rapdz';
      PAR_WYDR.TITLE:='Raport dzienny'@;

      PRH:=obj_new(@.CLASS.PRINT,10);
      {? rep_export()
      || PRH.add("''",40,'Jednostka organizacyjna'@)
      || PRH.add("Nr+=1",5,'Lp.'@,1,,,,,',,\'9.\'')
      ?};
      PRH.add("''",14,'Karta'@);
      PRH.add("''",9,'Nr teczki'@,1);
      PRH.add("Pracownik",38,'Nazwisko i imię'@);

      {?~rep_export()||PRH.add("''",13,'Jednostka organizacyjna'@)?};
      PRH.add("''",21,'Norma'@,,,,,,,,3);
      PRH.add("''",31,'Przepracował'@,,,,,,,,4);
      PRH.add("''",8,'Niedobór'@);
      PRH.add("''",7,'Nadmiar'@);
      PRH.add("''",25,'Wyjaśnienia'@);
      PRH.s_frame();

      PRN:=obj_new(@.CLASS.PRINT,15);
      {? rep_export()
      || PRN.add("P.WYDZIAL().SYMBOL+' '+|UD_SKL.OPIS",40,'')
      || PRN.add("Nr+=1",5,'',1,,,,,',,\'9.\'')
      ?};
      PRN.add(
         "  R_KARHIS.index('PR_DATA');
            R_KARHIS.prefix(P.ref());
            {? R_KARHIS.last() || R_KARHIS.K().N || '' ?}
         ",14,''
      );
      PRN.add("Teczka",9,'');
      PRN.add("Pracownik",38,'');
      {? ~rep_export() || PRN.add("P.WYDZIAL().SYMBOL",13,'') ?};
      PRN.add("Format_godz(NormWej,NormPrac)",5,'od'@);
      PRN.add("Format_godz(NormWyj,NormPrac)",5,'do'@);
      PRN.add("Format(NormPrac)",5,'praca'@);
      PRN.add("Format_godz(PracWej,PracPrac)",5,'od'@);
      PRN.add("Format_godz(PracWyj,PracPrac)",5,'do'@);
      PRN.add("Format(PracPrac)",5,'praca'@);
      PRN.add("Format(PracPrzer)",7,'przerwy'@,1);
      PRN.add("Format(Niedobor)",8,'',1);
      PRN.add("Format(Nadmiar)",7,'',1);
      PRN.add("Uwagi",25,'');
      PRN.s_frame();

      prn1:='PRH';
      prn2:='PRN';
      rsep:=PAR_WYDR.SEP;
      vp:=h_rat:=1;
      lmt:=Nr:=pb:=Wydzial:=Ok:=0;
      Karta:=Teczka:=Pracownik:=PracWej:=PracWyj:=PracPrac:=PracPrzer:=NormWej:=NormWyj:=NormPrac:=var:='';
      Nadmiar:=Niedobor:=Uwagi:='';
      Format_godz:=
         "  {? _a='00:00' & _b='00:00' || '' || _a ?}
         ";
      Format:=
         "  {? _a='00:00' || '' || _a ?}
         "
   ?};
   var:=exec('uprawnieniawrap','pkd','nieobecnościach'@,'PKD_EZK_OPNN','PKD_EZK_ORNN');
   params_set('P',_TAB,'PT',_TAB2);
   R_PRACDN.cntx_psh();
   R_KARHIS.cntx_psh();
   R_WZCZ.cntx_psh();
   KAL_ROK.cntx_psh();
   KAL_DEF.cntx_psh();
   R_PRACDN.cntx_psh();
   R_REJ_WW.cntx_psh()
}
{END:
   VAR_DEL.delete('PRN','PRH','prn1','prn2','vp','lmt','rsep','h_rat','tresc','pb','Format','Nr','Karta','Teczka');
   VAR_DEL.delete('Pracownik','NormWej','NormWyj','NormPrac','PracWej','PracWyj','PracPrac','PracPrzer','Nadmiar','var');
   VAR_DEL.delete('Niedobor','Wydzial','Uwagi','Ok');
   R_REJ_WW.cntx_pop();
   R_PRACDN.cntx_pop();
   KAL_DEF.cntx_pop();
   KAL_ROK.cntx_pop();
   R_WZCZ.cntx_pop();
   R_KARHIS.cntx_pop();
   R_PRACDN.cntx_pop();
   exec('czytaj','#stalesys',date(),KST,'R_TZ')
}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: w_rapdz.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { pb:=1; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'Na dzień: '@}{$VAR_EDIT.DATA}
   {IF: VAR_EDIT.FLAGA=0}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Alfabetycznie w ramach jednostek organizacyjnych'@]}
   {ELIF: VAR_EDIT.FLAGA=1}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Grupowo w ramach jednostek organizacyjnych'@]}
   {ELIF: VAR_EDIT.FLAGA=2}{<{ceil(lmt*h_rat)+1}'Rodzaj wydruku: %1'['Alfabetycznie (bez związku z jednostką organizacyjną)'@]}
   {FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: ~rep_export()}
   {IF: VAR_EDIT.FLAGA=1}
      {<{lmt+1}}{'Jednostka organizacyjna:'@} {P.WYDZIAL().SYMBOL}{IF: +(|P.WYDZIAL().OPIS)} ({P.WYDZIAL().OPIS}){FI}
   {FI}
{FI}
{  prn1:='PRH';
   prn2:='PRN'; ~~
}
{SUBSTITUTE: 'TABHEAD2'}
{  prn1:='PRN';
   prn2:='PRH'; ~~
}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'TABHEAD2'}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn1+'.ini_head()'; ($(_f))()}
   {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
   {WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
   {DO}
      {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {OD}
   { _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn2+'.ini_head()'; ($(_f))()}
   {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   {WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
   {DO}
      {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
   {OD}
{MACRO-}
{MACRO: tresc}
{  R_KARHIS.index('PR_DATA');
   R_KARHIS.prefix(P.ref());
   Karta:={? R_KARHIS.last() || R_KARHIS.K().N || '' ?};
   Pracownik:=form(P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,40);
   Teczka:=form(P.T,-9);
   Uwagi:='';
   R_WZCZ.index('R_WZCZ');
   R_WZCZ.prefix(P.name(),P.ref());
   NormWej:=NormWyj:=NormPrac:=5*' ';
   _Kalend:=
      {? R_WZCZ.find_le(VAR_EDIT.DATA)
      || {? R_WZCZ.GRAFIK='T' || R_WZCZ.CZESC || R_WZCZ.KAL ?}
      || P.KAL
      ?};
   {? _Kalend
   || KAL_ROK.index('KAL_ROK');
      KAL_ROK.prefix(_Kalend);
      {? KAL_ROK.find_key(VAR_EDIT.DATA~1)
      || KAL_DEF.index('KAL_DEF');
         KAL_DEF.prefix(KAL_ROK.ref());
         {? KAL_DEF.find_key(VAR_EDIT.DATA)
         || NormWej:=form(KAL_DEF.POCZATEK,5);
            NormWyj:=form(KAL_DEF.KONIEC,5);
            NormPrac:=form(KAL_DEF.CZAS,5)
         || Uwagi:='błędna definicja kalendarza'@
        ?}
      || Uwagi:='brak definicji kalendarza'@
      ?}
   || Uwagi:='nieokreślony kalendarz'@
   ?};

   {? VAR_EDIT.DATA<P.DZA | (VAR_EDIT.DATA>P.DZ & P.DZ<>#0)
   || Uwagi:=Uwagi+{? +|Uwagi || ', ' || '' ?}+'poza zatrudnieniem'
   ?};

   R_PRACDN.index('R_PRACDN');
   R_PRACDN.prefix(P.ref());
   R_REJ_WW.index('R_REJ_WX');
   R_REJ_WW.prefix(P.ref(),VAR_EDIT.DATA);
   PracWej:=PracWyj:=PracPrac:=PracPrzer:=Nadmiar:=Niedobor:=5*' ';
   {? R_REJ_WW.first() & 2+R_REJ_WW.TP='WE'
   || PracWej:=form(R_REJ_WW.GD,5)
   || PracWej:=5*' '
   ?};
   {? R_REJ_WW.last() & 2+R_REJ_WW.TP='WY'
   || PracWyj:=form(R_REJ_WW.GD,5)
   || PracWyj:=5*' '
   ?};
   {? R_PRACDN.find_key(VAR_EDIT.DATA)
   || PracPrzer:=form(R_PRACDN.WP,5);
      PracPrac:=form(R_PRACDN.CP,5);
      Niedobor:=form(R_PRACDN.NB,5);
      Nadmiar:=form(R_PRACDN.CN,5)
   || Niedobor:=NormPrac
   ?};
   {? +var=0
   || N.cntx_psh();
      N.index('NIEOBECN');
      N.prefix('N',P.ref());
      {? N.find_le(VAR_EDIT.DATA)
      || {? N.DO>=VAR_EDIT.DATA || Uwagi:=N.NB().RT; Niedobor:='' ?}
      ?};
      N.cntx_pop()
   ?};
   ~~
}
{IF: P.WYDZIAL<>Wydzial}
   {IF: VAR_EDIT.FLAGA=1}
      {PAGE}
      {  Nr:=0;
         ~~
      }
   {FI}
   {  Wydzial:=P.WYDZIAL;
      ~~
   }
{FI}
{  prn1:='PRN';
   prn2:='PRH'; ~~
}
{IF: +Pracownik}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{IF:   VAR_EDIT.DATA:=date();
      _ed:=VAR_EDIT.mk_edit('Raport dzienny'@,0,'prc_rap_dz');
      VAR_EDIT.win_esep(_ed,'Parametry wydruku'@);
      VAR_EDIT.win_efld(_ed,,'DATA',,,8,,,'Na dzień'@,,'Data, której dotyczy raport'@);
      VAR_EDIT.win_efld(_ed,,'FLAGA',,,,,,
         'Rodzaj raportu'@,,'Zestawienia są generowane dla wydziałów na osobnych stronach'@,
         'radio-buttons',,
         'Alfabetycznie w ramach jednostek organizacyjnych'@,"0",
         'Grupowo w ramach jednostek organizacyjnych'@,"1",
         'Alfabetycznie (bez związku z jednostką organizacyjną)'@,"2");
      exec('ok_esc','#window',VAR_EDIT,_ed);
      {? rep_export() || VAR_EDIT.efld_opt(_ed,'enable=0',,'FLAGA') ?};
      VAR_EDIT.efld_opt(_ed,'enable=1, mark=1',,'DATA');
      VAR_EDIT.win_edit(_ed);
      ~VAR_EDIT.edit("__CHK.record(VAR_EDIT,,'DATA')")}
   {EXIT}
{FI}
{  exec('czytaj','#stalesys',VAR_EDIT.DATA,KST,'R_TZ');
   MASK.Use('R_PRACDN',VAR_EDIT.DATA~1,VAR_EDIT.DATA~2);
   MASK.Use('R_REJ_WW',VAR_EDIT.DATA~1,VAR_EDIT.DATA~2);
   ~~
}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRH.width())/2); ~~ }
{IF: VAR_EDIT.FLAGA=2}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
{ELIF: VAR_EDIT.FLAGA<2}
{FOR: params_get().PT}
{DO}
   {IF: P.seek(params_get().PT.SQL,,1)}
      {IF: ~Ok}
         {Wydzial:=P.WYDZIAL; ~~}
      {FI}
      {SUBSTITUTE: tresc}
      {Ok:=1; ~~}
   {FI}
{OD}
{FI}

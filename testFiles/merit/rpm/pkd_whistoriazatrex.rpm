:!UTF-8
{TITLE:Przebieg pracy zawodowej - rozszerzony}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','vp','lmt','rsep','tresc','b_rat','h_rat','ileHodd',
                  'petlaHodd','ileH','petlaH','__hum','__WIN','maxHead');
   tresc:='pkd_whistoriazatrex';
   PAR_WYDR.TITLE:='Przebieg pracy zawodowej'@;
   par:=obj_new(11);
   P.cntx_psh();
   H.cntx_psh();
   H_UM.cntx_psh();
   H_ODDE.cntx_psh();
   H.index('HISTUM');
   par[11]:=0;

   __WIN:=obj_new('VAL1','LAB2','MAX','MARG','VAL2','LINE','SLINE');
   __WIN.MARG:=50;
   __WIN.MAX:=240;
   __WIN.VAL2:=__WIN.MAX-__WIN.MARG-1;
   __WIN.VAL1:=117;
   __WIN.LAB2:=125;
   __WIN.LINE:="{? var_pres('_a')=type_of('') & var_pres('_b')=type_of('') & var_pres('_c')=type_of('') &
                var_pres('_d')=type_of('')
                ||
                  _maxLen1:=__WIN.VAL1-__WIN.MARG-1-(+_a);
                  _maxLen2:=__WIN.VAL2-__WIN.LAB2-(+_c);
                  {? _b='' || _b:='-' ?};
                  {? _d='' || _d:='-' ?};
                  _b:=_b+_maxLen1;
                  _d:=_d+_maxLen2;
                  '{<{__WIN.MARG} \\'│ \\'}{\\'%1\\'}{>{__WIN.VAL1} \\'%2\\'}'
                  '{<{__WIN.LAB2} \\'%3\\'}{>{__WIN.VAL2} \\'%4\\'}{\\' │\\'}'[_a,_b,_c,_d]
                || msg('Zły typ');
                   'Błąd'
                ?}";

   __WIN.SLINE:="{? var_pres('_a')=type_of('') & var_pres('_b')=type_of('')
                 ||
                   _result:='';
                   _maxLen:=__WIN.VAL2-__WIN.MARG-2-(+_a);
                   {? _a='' || _a:='-' ?};
                   {? _b='' || _b:='-' ?};
                   {? +_b<_maxLen
                   || _result+='{<{__WIN.MARG} \\'│ \\'}{\\'%1:   \\'}{\\'%2\\'}{>{__WIN.VAL2+2} \\' │\\'}'[_a,_b]
                   ||
                      _maxLen:=__WIN.VAL2-__WIN.MARG-2;
                      _result+='{<{__WIN.MARG} \\'│ \\'}{\\'%1: \\'}{>{__WIN.VAL2+2} \\' │\\'}\\n'[_a];
                      _words:=spli_str(_b,' ');
                      _ii:=1;
                      {! |? _ii<=obj_len(_words)
                      |! _text:='';
                         _pass:=1;
                         {! |? _ii<=obj_len(_words) & _pass
                         |! _pass:={? +(_text+' '+_words[_ii]) < _maxLen || _text+=' '+_words[_ii]; 1 || 0 ?};
                            _ii+=1
                         !};
                         _result+='{<{__WIN.MARG} \\'│ \\'}{\\'%1\\'}{>{__WIN.VAL2+2} \\' │\\'}\\n'[_text]
                      !}
                   ?};
                   return(_result)
                 || msg('Zły typ');
                    'Błąd'
                 ?}";

   _upr:=exec('uprawnieniawrap','pkd','przebiegu zatrudnienia'@,'PKD_EZK_PPZA','PKD_EZK_RPZA');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[3]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,,,,,,,,
         'Zakres prezentowanych danych o zatrudnieniu'@,'Umowy'@,'Pełny przebieg'@,0
      );
      {? par[11]:=par[3].size()
      || par[7]:=obj_new(@.CLASS.PRINT,{? rep_export() || 19+17+8 || 19 ?});
         {? rep_export()
         || par[2]:=obj_new(@.CLASS.PRINT,17);
            par[2].add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE+' '+'(Nr teczki: %1)'[P.T]",150,
               'Pracownik'@);
            par[2].add("'%1 (%2)'[H_UM.RU().O, H_UM.RU().K]",3,'Typ umowy'@);
            par[2].add("H_UM.NUMER",20,'Nr umowy'@);
            par[2].add("par[9]",48,'Przedstawiciel zakładu'@);
            par[2].add("H_UM.OD$1",10,MS.name('H_UM','OD'));
            par[2].add("H_UM.P_OD$1",10,MS.name('H_UM','P_OD'));
            par[2].add("H_UM.DO$1",10,MS.name('H_UM','DO'));
            par[2].add("H_UM.P_DO$1",10,MS.name('H_UM','P_DO'));
            par[2].add("par[10]",42,'Miejsce pracy z umowy'@);
            par[2].add("H_UM.EKW",20,MS.name('H_UM','EKW'),,,,,,'15,2');
            par[2].add("H_UM.EKW_NSP",34,MS.name('H_UM','EKW_NSP'),,,,,,'15,2');
            par[2].add("H_UM.ZAM_ZAW",34,MS.name('H_UM','ZAM_ZAW'));
            par[2].add("H_UM.ZAM_OKR",34,MS.name('H_UM','ZAM_OKR'));
            par[2].add("H_UM.PRZ_USPR",34,MS.name('H_UM','PRZ_USPR'));
            par[2].add("H_UM.USPKOD().LINIA",50,'Przyczyna ustania'@);
            par[2].add("H_UM.USPPPRAK().KOD",34,'Podstawa prawna ustania'@);
            par[2].add("H_UM.USPSTR().LINIA",34,MS.name('H_UM','USPSTR'));
            par[2].s_frame()
         ?};
         {? par[3].ASK & rep_export()
         ||
            par[7].add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE+' '+'(Nr teczki: %1)'[P.T]",150,
               'Pracownik'@);
            par[7].add("'%1 (%2)'[H_UM.RU().O, H_UM.RU().K]",3,'Typ umowy'@);
            par[7].add("H_UM.NUMER",20,'Nr umowy'@);
            par[7].add("par[9]",48,'Przedstawiciel zakładu'@);
            par[7].add("H_UM.OD$1",10,MS.name('H_UM','OD'));
            par[7].add("H_UM.P_OD$1",10,MS.name('H_UM','P_OD'));
            par[7].add("H_UM.DO$1",10,MS.name('H_UM','DO'));
            par[7].add("H_UM.P_DO$1",10,MS.name('H_UM','P_DO'));
            par[7].add("par[10]",42,'Miejsce pracy z umowy'@);
            par[7].add("H_UM.EKW",20,MS.name('H_UM','EKW'),,,,,,'15,2');
            par[7].add("H_UM.EKW_NSP",34,MS.name('H_UM','EKW_NSP'),,,,,,'15,2');
            par[7].add("H_UM.ZAM_ZAW",34,MS.name('H_UM','ZAM_ZAW'));
            par[7].add("H_UM.ZAM_OKR",34,MS.name('H_UM','ZAM_OKR'));
            par[7].add("H_UM.PRZ_USPR",34,MS.name('H_UM','PRZ_USPR'));
            par[7].add("H_UM.USPKOD().LINIA",50,'Przyczyna ustania'@);
            par[7].add("H_UM.USPPPRAK().KOD",34,'Podstawa prawna ustania'@);
            par[7].add("H_UM.USPSTR().LINIA",34,MS.name('H_UM','USPSTR'))
         ?};
         par[7].add("H.WYDZIAL().SYMBOL",16,'Jedostka organizacyjna'@);
         par[7].add("H.ST().ST",24,'Stanowisko'@);
         par[7].add("H.WY",6,'Wymiar'@,1,,,,,'12,3');
         par[7].add("H.OD$1",10,'Od dnia'@);
         par[7].add("H.DO$1",10,'Do dnia'@);
         par[7].add("H.S1",20,MS.name('H','S1'),1,,,,,'12,2');
         par[7].add("{? H.CZYWAL='T' || H.WAL().KOD || 'PLN' ?}",6,'Waluta'@);
         par[7].add("H.S2",20,MS.name('H','S2'),1,,,,,'12,2');
         par[7].add("{? H.S2T='P' || '%' |? H.CZYWAL2='T' || H.WAL().KOD || 'PLN' ?}",9,'Waluta/ Procent'@);
         par[7].add("H.S3",10,MS.name('H','S3'),1,,,,,'12,2');
         par[7].add("{? H.S3T='P' || '%' |? H.CZYWAL3='T' || H.WAL().KOD || 'PLN' ?}",9,'Waluta/ Procent'@);
         par[7].add("H.ODDEL",9,'Oddeleg.'@);
         par[7].add("H.S4",15,MS.name('H','S4'),1,,,,,'12,2');
         par[7].add("{? H.CZYWAL4='T' || H.WAL().KOD || 'PLN' ?}",6,'Waluta'@);
         par[7].add("H.KODDEL",10,'Praca w kilku krajach?'@);
         par[7].add("H.PAW",10,'Prawa autorskie'@,1,,,,,'12,2');
         par[7].add("{? H.PAT='P' || '%' || 'PLN' ?}",10,'Waluta/ Procent'@);
         par[7].add("H.DOP_NADG",10,'Nadgodziny dop. ponad wymiar'@,1,,,,,'12,2');
         par[7].add("H.DOP_ST",10,'Nadgodziny dop. śred.'@,1,,,,,'12,2');
         par[7].sl_frame();

         par[1]:=obj_new(@.CLASS.PRINT,8);

         {? par[3].ASK & rep_export()
         ||
            par[7].add("H_ODDE.O_OD$1",15,'Data od'@);
            par[7].add("H_ODDE.O_DO$1",15,'Data do'@);
            par[7].add("H_ODDE.KRAJ().NAZ",36,'Kraj'@);
            par[7].add("H_ODDE.POD",31,'Podatek rozliczany w Polsce?'@);
            par[7].add("H_ODDE.ZUS",35,'Składki ubezp. rozliczane w Polsce?'@);
            par[7].add("$H_ODDE.ST_NOC",20,'Stawka za nocleg'@,1);
            par[7].add("{? H_ODDE.WAL_NOC().SYM<>'' || H_ODDE.WAL_NOC().SYM || '-' ?}",6,'Waluta'@);
            par[7].add("{? H_ODDE.P_ZAST || '%1 %2 (Nr teczki: %3)'[H_ODDE.P_ZAST().OSOBA().PIERWSZE,
                        H_ODDE.P_ZAST().OSOBA().NAZWISKO,H_ODDE.P_ZAST().T]|| '-' ?}",73,'Na zastępstwo'@)
         ||
            par[1].add("H_ODDE.O_OD$1",15,'Data od'@);
            par[1].add("H_ODDE.O_DO$1",15,'Data do'@);
            par[1].add("H_ODDE.KRAJ().NAZ",36,'Kraj'@);
            par[1].add("H_ODDE.POD",31,'Podatek rozliczany w Polsce?'@);
            par[1].add("H_ODDE.ZUS",35,'Składki ubezp. rozliczane w Polsce?'@);
            par[1].add("$H_ODDE.ST_NOC",20,'Stawka za nocleg'@,1);
            par[1].add("{? H_ODDE.WAL_NOC().SYM<>'' || H_ODDE.WAL_NOC().SYM || '-' ?}",6,'Waluta'@);
            par[1].add("{? H_ODDE.P_ZAST || '%1 %2 (Nr teczki: %3)'[H_ODDE.P_ZAST().OSOBA().PIERWSZE,
                        H_ODDE.P_ZAST().OSOBA().NAZWISKO,H_ODDE.P_ZAST().T]|| '-' ?}",73,'Na zastępstwo'@);
            par[1].sl_frame()
         ?};

         lmt:=rsep:=ileHodd:=petlaHodd:=par[5]:=par[6]:=par[8]:=maxHead:=ileH:=petlaH:=0;
         par[9]:=par[10]:='';
         vp:=par[4]:=b_rat:=h_rat:=1;

         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   H.cntx_pop();
   H_ODDE.cntx_pop();
   H_UM.cntx_pop();
   VAR_DEL.delete('par','vp','lmt','rsep','tresc','b_rat','h_rat','ileHodd',
                  'petlaHodd','ileH','petlaH','__hum','__WIN','maxHead')
}
{EXIT:  ~par[11]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
{SPACE: 'C'}
{LINE}
{FONT: 'W12'}
{SPACE: 'A'}
{ par[4]:=0; ~~ }
{IF: par[3].ASK}
{maxHead:=charleft();~~}
{IF: ~rep_export()}{INCLUDE: p_prac.rpi}{>{maxHead} 'Wydruk pełnego przebiegu zatrudnienia'@}{FI}
{IF: rep_export()}
{ par[7].setcolor(color); par[7].ini_head(); ~~ }
{LINE}
{REP: par[7].fhbar(lmt)}
{WHILE: par[7].h_next()}{DO}
{REP: par[7].h_fmline()}
{OD}
{REP: par[7].flbar(lmt)}
{FI}
{ELSE}
{'Wydruk umów o pracę'@}
{IF: rep_export()}
{ par[2].setcolor(color); par[2].ini_head(); ~~ }
{LINE}
{REP: par[2].fhbar(lmt)}
{WHILE: par[2].h_next()}{DO}
{REP: par[2].h_fmline()}
{OD}
{REP: par[2].flbar(lmt)}
{FI}
{FI}
{HEADER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'h_um_window'}
{<{__WIN.MARG} '┌'+('─'*(charleft()-2*__WIN.MARG))+'┐'}
{REP: __WIN.LINE('Typ umowy','%1 (%2)'[H_UM.RU().O, H_UM.RU().K],MS.name('H_UM','EKW'),$H_UM.EKW)}
{REP: __WIN.LINE('Numer umowy',H_UM.NUMER,MS.name('H_UM','EKW_NSP'),$H_UM.EKW_NSP)}
{REP: __WIN.LINE(MS.name('H_UM','OD'),H_UM.OD$1,MS.name('H_UM','P_OD'),H_UM.P_OD$1)}
{REP: __WIN.LINE(MS.name('H_UM','DO'),H_UM.DO$1,MS.name('H_UM','P_DO'),H_UM.P_DO$1)}
{REP: __WIN.LINE(MS.name('H_UM','PRZ_USPR'),H_UM.PRZ_USPR,'Strona inicjująca rozwiązanie',H_UM.USPSTR().LINIA)}
{IF: +par[10] <= 40}
{REP: __WIN.LINE(MS.name('H_UM','ZAM_ZAW'),H_UM.ZAM_ZAW,'Miejsce pracy',par[10])}
{ELSE}
{REP: __WIN.LINE(MS.name('H_UM','ZAM_ZAW'),H_UM.ZAM_ZAW,' ',' ')}
{REP: __WIN.SLINE('Miejsce pracy',par[10])}
{FI}
{IF: +par[9] <= 40}
{REP: __WIN.LINE('Przestawiciel zakładu',par[9],' ',' ')}
{ELSE}
{REP: __WIN.SLINE('Przestawiciel zakładu',par[9])}
{FI}
{IF: H_UM.ZAM_ZAW='T'}
{REP: __WIN.SLINE(MS.name('H_UM','ZAM_OKR'),H_UM.ZAM_OKR)}
{FI}
{IF: +H_UM.USPPPRAK().LINIA}
{REP: __WIN.SLINE('Podstawa prawna ustania',H_UM.USPPPRAK().LINIA)}
{FI}
{IF: +H_UM.USPKOD().LINIA}
{REP: __WIN.SLINE(MS.name('H_UM','USPKOD'),H_UM.USPKOD().LINIA)}
{FI}
{<{__WIN.MARG} '└'+('─'*(charleft()-2*__WIN.MARG))+'┘'}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'tabhead'}
{  par[7].setcolor(color); par[7].ini_head();
   par[1].setcolor(color); par[1].ini_head(); ~~
}
{REP: par[7].fhbar(lmt)}
{WHILE: par[7].h_next()}{DO}
{REP: par[7].h_fmline()}
{OD}
{REP: '{<{lmt+1}}'+par[7].fjbar(par[1])}
{REP: par[1].h_fmline()}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'full'}
{FOR: H_UM}{INDEX: 'OD'}{PREFIX: P.ref()}{DO}
{FONT: 'T8G'}
{SPACE: 'C'}
{ H.prefix(H_UM.ref());
  {? H.first()
  || par[9]:=H.PRZEDST;
     par[10]:=H.MIEJSCE
  || par[9]:=par[10]:=''
  ?}; ~~
}
{ENTIRE}
{<1>{__WIN.MAX} 'Umowa o pracę'}
{SUBSTITUTE: 'h_um_window'}
{ENTIRE-}
{IF: lineleft() & lineleft()<=10}
{PAGE}
{FI}
{COMMENT} header tabeli {COMMENT-}
{SUBSTITUTE: 'tabhead'}
{REP: par[1].fjbar(par[7],lmt)}
{ par[1].setcolor('255:255:255'); par[7].setcolor('255:255:255'); ~~ }
{FOR: H}{INDEX: 'HISTUM'}{PREFIX: H_UM.ref()}{DO}
{FIRST}
{ ileH:=H.size(); petlaH:=0; ~~ }
{FIRST-}
{ par[8]:=0; ~~ }
{IF: 1}{REP: par[7].ini_line(); par[7].fline(lmt)}{FI}
{WHILE: par[7].next()}
{DO}
{REP: par[7].fline(lmt)}
{OD}
{FOR: H_ODDE}{INDEX: 'HOD'}{PREFIX: H.ref()}{DO}
{FIRST}
{ par[8]:=1; ileHodd:=H_ODDE.size(); petlaHodd:=0; ~~ }
{COMMENT}v złamanie tabeli v{COMMENT-}
{IF: lineleft() & lineleft()>4}
{REP: '{<{lmt+1}}'+par[7].fjbar(par[1])}
{ELSE}
{REP: par[7].flbar(lmt)}
{PAGE}
{SUBSTITUTE: 'tabhead'}
{REP: par[1].fbar(lmt)}
{ par[1].setcolor('255:255:255'); par[7].setcolor('255:255:255'); ~~ }
{FI}
{COMMENT}^ złamanie tabeli ^{COMMENT-}
{FIRST-}
{REP: par[1].ini_line(); par[1].fline(lmt)}
{WHILE: par[1].next()}
{DO}
{REP: par[1].fline(lmt)}
{OD}
{ petlaHodd+=1; ~~ }
{COMMENT}v złamanie tabeli v{COMMENT-}
{IF: lineleft() & lineleft()<=3 & ileHodd<>petlaHodd}
{REP: par[1].flbar(lmt)}
{PAGE}
{SUBSTITUTE: 'tabhead'}
{REP: par[1].fbar(lmt)}
{ par[1].setcolor('255:255:255'); par[7].setcolor('255:255:255'); ~~ }
{FI}
{COMMENT}^ złamanie tabeli ^{COMMENT-}
{OD}
{ petlaH+=1; ~~ }
{COMMENT} łamanie tabeli oraz drukowanie ostatniej ramki zależnie od typu ostatniego wiersza (par[8]) {COMMENT-}
{IF: lineleft() & lineleft()<=3 & ileH<>petlaH}
{IF: par[8]}
{REP: par[1].flbar(lmt)}
{ELSE}
{REP: par[7].flbar(lmt)}
{FI}
{PAGE}
{SUBSTITUTE: 'tabhead'}
{REP: par[1].fjbar(par[7],lmt)}
{ par[1].setcolor('255:255:255'); par[7].setcolor('255:255:255'); ~~ }
{ELSE}
{IF: par[8]}
{REP: par[1].fjbar(par[7],lmt)}
{ par[1].setcolor('255:255:255'); par[7].setcolor('255:255:255'); ~~ }
{ELSE}
{IF: ileH=petlaH}
{REP: par[7].flbar(lmt)}
{ELSE}
{REP: par[7].fbar(lmt)}
{FI}
{FI}
{FI}
{OD}
{OD}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'hum'}
{FOR: H_UM}{INDEX: 'OD'}{PREFIX: P.ref()}{DO}
{ENTIRE}
{FIRST}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: par[6]<>P.ref()}{ par[5]+=1; par[6]:=P.ref(); ~~ }{FI}
{IF: ~rep_export()}{<{ceil(lmt*b_rat)+1}}{form(par[5],,,'9.')} {INCLUDE: p_prac.rpi}
{ELSE}
{REP: par[2].fhbar(lmt)}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{FIRST-}
{ H.prefix(H_UM.ref());
  {? H.first()
  || par[9]:=H.PRZEDST;
     par[10]:=H.MIEJSCE
  || par[9]:=par[10]:=''
  ?}; ~~
}
{IF: rep_export()}
{REP: par[2].ini_line(); par[2].fline(lmt)}
{WHILE: par[2].next()}{DO}
{REP: par[2].fline(lmt)}
{OD}
{ELSE}
{SUBSTITUTE: 'h_um_window'}
{FI}
{ENTIRE-}
{OD}
{IF: rep_export()}{REP: par[2].flbar(lmt)}{FI}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'fullexport'}
{REP: par[7].fhbar(lmt)}
{FOR: H_UM}{INDEX: 'OD'}{PREFIX: P.ref()}{DO}
{ H.prefix(H_UM.ref());
  {? H.first()
  || par[9]:=H.PRZEDST;
     par[10]:=H.MIEJSCE
  || par[9]:=par[10]:=''
  ?}; ~~
}
{FOR: H}{INDEX: 'HISTUM'}{PREFIX: H_UM.ref()}{DO}
{IF: H.ODDEL='N'}
{REP: par[7].ini_line(); par[7].fline(lmt)}
{WHILE: par[7].next()}{DO}
{REP: par[7].fline(lmt)}
{OD}
{ELSE}
{FOR: H_ODDE}{INDEX: 'HOD'}{PREFIX: H.ref()}{DO}
{REP: par[7].ini_line(); par[7].fline(lmt)}
{WHILE: par[7].next()}{DO}
{REP: par[7].fline(lmt)}
{OD}
{OD}
{FI}
{OD}
{OD}
{REP: par[7].flbar(lmt)}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: tresc}
{IF: par[3].ASK}
{IF: ~rep_export()}
{SUBSTITUTE: 'full'}
{PAGE}
{ELSE}
{SUBSTITUTE: 'fullexport'}
{FI}
{ELSE}
{SUBSTITUTE: 'hum'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{FOOTER: 0}
{ vp:=0; ~~ }
{FOOTER-}
:!UTF-8
{TITLE:A. Stan magazynu}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('sm_01');
   sm_01:="
      VAR_DEL.delete('w','PRN','X_Xa','dokl','tryb','kgr');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep')
   ";
   sm_01();

   w:=obj_new(4); "--wartosci kolumn--";
   PRN:=obj_new(@.CLASS.PRINT,5); PRN.s_frame();
   dokl:=0;
   tryb:=rep_export();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
   sm_01(); VAR_DEL.delete('sm_01')
}
{
{? tryb=1 || PRN.add("form(kgr)",8,'Grupa materiałowa'@) ?};
PRN.add("form(w[1])",17,'Indeks'@);
PRN.add("form(w[2])",60,'Nazwa materiału'@);
PRN.add("form(w[3],,dokl,'.,')",20,'Stan'@,1);
PRN.add("form(w[4])",10,'jm'@);

PAR_WYDR.TITLE:='Stan materiałów w magazynie '@+ST.MAG().SYM;
prn1:='PRN'
;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział '@+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{<{lmt+1}}{'Stan na: '@+$date()+', '+$time()}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}   WYDRUK                                                   {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; w[1]:=0;~~}
{FOR:'SM'}
{INDEX:'SA'}
{PREFIX:ST.MAG}
{DO}
   {FIRST}
   { dokl:=exec('fld_prec','#field',SM,'S');~~}
   {FIRST-}
   {
   w[1]:=SM.M().KTM;
   w[2]:=SM.M().N;
   w[3]:=SM.S;
   w[4]:=SM.M().J().KOD;
   kgr :=SM.M().MGR().KOD
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: tryb<>1}
{FOOTER}{FOOTER-}
{IF: lineleft>6}
   {SUBSTITUTE: 'LINIAF'}
   {LINE}
{ELIF: lineleft}
   {SUBSTITUTE: 'LINIAF'}
   {PAGE}
{FI}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{
X_Xa:=sql('
   select JM.KOD JM, sum(SM.S) S
   from SM join M using (SM.M,M.reference) join JM using(M.J,JM.reference)
   where SM.MAG=:_a and SM.S<>0
   group by JM.KOD
   order by 1',ST.MAG);
   VAR_DEL.delete('PRN');
   PRN:=obj_new(@.CLASS.PRINT,2); PRN.s_frame();
   PRN.add("form(w[1])",10,'jm'@);
   PRN.add("form(w[2])",20,'Stan'@,1);
~~}
{IF: lineleft>5}
   {FONT: 'T8GB'}
   {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FI}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xa}
{DO}
   {FIRST}
   { dokl:=exec('fld_prec','#field',X_Xa,'S');~~}
   {FIRST-}
   {
   w[1]:=X_Xa.JM;
   w[2]:=form(X_Xa.S,,dokl,'.,');
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{FI}
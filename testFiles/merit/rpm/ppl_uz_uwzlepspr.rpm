:!UTF-8
{TITLE:Uproszczone zestawienie wypłat}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','tresc','lmt','opis','Text','__edit','__param','data_ppk');
   PAR_WYDR.TITLE:='Realizacja umów pozaetatowych'@;
   tresc:='zlepspr';

   data_ppk:=date(0,0,0);
   {? var_pres('KST_PPK')>0 & exec('czytaj','#stalesys',date(),KST_PPK,'UST_OD')
   || data_ppk:=KST_PPK.UST_OD
   ?};

   par:=obj_new(11);
   par[6]:=par[7]:=par[11]:=0;
   par[8]:=par[9]:=#0;
   _upr:=exec('uprawnieniawrap','pkd',
              'Rachunkach'@,
              'PPL_ZLC_RRAC',
              'PPL_ZLC_PRAC'
              );
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || exec('__RUB','object');
      __param:=tab_tmp(1,
             'WYKAZ','INTEGER','Sposób przygotowania wydruku',
             'UMOWY','INTEGER','Rodzaje umów do zestawienia',
             'OD','DATE','Początek okresu',
             'DO','DATE','Koniec okresu');
      __param.UMOWY:=__param.WYKAZ:=0;
      __param.OD:=date(,1,1);
      __param.DO:=date(,12,0);
      __param.add();
      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'uz_uwzlepspr');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'OD',,,11,,,'Początek okresu'@);
      __param.win_efld(__edit,,'DO',,,11,,,'Koniec okresu'@);
      __param.efld_opt(__edit,'mark=1',,'OD');
      __param.efld_opt(__edit,'mark=1',,'DO');
      __param.win_efld(__edit,,'WYKAZ',,,,,,'Sposób przygotowania wydruku'@,,,'radio-buttons',,'Wykaz osób'@,"1",
                                                                                               'Zbiorówka'@,"2");
      __param.win_efld(__edit,,'UMOWY',,,,,,'Rodzaje umów do zestawienia'@,,,'radio-buttons'
                             ,,'wybrany rodzaj Umów'@,"1",
                               'Wszystkie rodzaje'@,"2");
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);

      {? par[11]:=__param.edit("{? __param.DO=#0 | __param.OD=#0 | __param.OD>__param.DO
                                || FUN.emsg('\nDaty muszą być wypełnione.\n'+
                                            '\nData początku okresu nie może być większa od daty końcowej.\n'@);
                                   {? __param.OD=#0 || 'OD' || 'DO' ?}
                                |? __param.DO~1<1999 | __param.OD~1<1999
                                || FUN.emsg('Daty muszą być późniejsze niż z 1998 roku.'@);
                                   {? __param.OD~1<1999 || 'OD' || 'DO' ?}
                                || par[8]:=__param.OD; par[9]:=__param.DO; 1
                                ?}")
      || par[6]:=__param.UMOWY;
         par[7]:=__param.WYKAZ
      ?};
      par[1]:=obj_new(3); 'obiekty print';
      par[2]:='wykaz osób';
      par[3]:='zbiorówka';

      par[4]:=''; 'opis uwzglednianych umow';
      par[5]:=''; 'rodzaje uwzględnianych umów';

      par[10]:=1;
      lmt:=0;
      opis:=0;
      Text:=''
   ?};
   ~~
}
{END:
   VAR_DEL.delete('par','tresc','lmt','opis','Text','__edit','__param','data_ppk')
}
{COMMENT}OLD: zlepspr.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzialz.rpi{COMMENT-}
{EXIT: ~par[11]}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}
{SPACE: 'C'}
{IF: par[10]}
   {LINE}
   { par[10]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Zakres dat'@}: {'od'@} {par[8]$1} {'do'@} {par[9]$1}
   {<{lmt+1}'Rodzaj wydruku'@}: {{? par[7]=2 || 'Zbiorówka'@ || 'Wykaz'@ ?}}
   {<{lmt+1}'Rodzaj umowy'@}: {{? par[5]='' &  par[7]=2
                               || 'Wszystkie'@
                               || STR.split(par[4]);opis:=1;STR.line(charleft)
                              ?}}
   {IF: opis}
   {WHILE: +(Text:=STR.line(charleft-lmt-15))}{DO}{<{lmt+15}}{REP: Text}{OD}
   {FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{REP: par[1][1].fhbar(lmt)}
{REP: par[1][1].ini_head(); par[1][1].h_fline(lmt)}
{REP: par[1][2].ini_head(); par[1][2].h_fline(lmt)}
{REP: par[1][3].ini_head(); par[1][3].h_fline(lmt)}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1][3].flbar(lmt)}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   D E F I N I C J E   -   P O B R A N I E   D A N Y C H

{COMMENT-}
{MACRO: tresc}
{MACRO-}
{MACRO: 'tabela'}
{ENTIRE}
   {REP: par[1][1].fbar(lmt)}
   {REP: par[1][1].ini_line(); par[1][1].fline(lmt)}
   {REP: par[1][2].ini_line(); par[1][2].fline(lmt)}
   {REP: par[1][3].ini_line(); par[1][3].fline(lmt)}
{ENTIRE-}
{MACRO-}
{EXIT: ~par[6] | ~par[7]}
{EXIT: ~exec('slo_typ','ext_slo','UMZLEC')}
{IF: par[6]=1}
   {FOR: RU}{INDEX: 'K'}{PREFIX: SLO_TYP.ref}{SELECT}
   {DO}{
   {? par[5]*RU.K=0 ||
      par[5]+={? par[5]<>'' || ',' || '' ?}+'\''+RU.K+'\'';
      par[4]+={? par[4]<>'' || ', ' || '' ?}+'%1 (%2)'[RU.O,RU.K]
   ?};
   ~~
   }{OD}
{ELIF: par[7]<>2}
   {IF: RU.index('K'); RU.prefix(SLO_TYP.ref()); RU.first()}{
      par[4]+='Wszystkie - %1(%2)'@[RU.O,RU.K];
      ~~
   }
   {WHILE: RU.next}
   {DO}{
      par[4]+={? par[4]<>'' || ', ' || '' ?}+'%1 (%2)'[RU.O,RU.K];
      ~~
   }{OD}{FI}
{FI}{
   _rh:=sql(
      'select '
      '  O.LT as LT, '
      '  RH.REFERENCE as RH, '
      '  ZC.OSOBA as OSOBA, '
      '  RU.K as K '
      'from '
      '  RH join '
      '  ZC using(RH.ZLE,ZC.REFERENCE) join '
      '  RU using(ZC.RU,RU.REFERENCE) join '
      '  :_c using(ZC.P,:_c.SQL) join '
      '  O using(RH.O,O.REFERENCE) '
      'where to_date(:_a)<=RH.DWY and RH.DWY<=to_date(:_b) '+
      {? par[5]<>'' || ' and RU.K in ('+par[5]+')' || '' ?}+
      'order by LT, RH',
      par[8],
      par[9],
      exec('wybierz_parses','pracownik','PPL').P
   );

   _o:=sql('select distinct :_a.LT from :_a',_rh);

   _txt:=
      'select '
      '  :_a.OSOBA, '
      '  :_a.K, '
      '  case when LS.RB=\''+$__RUB.ref(500)+'\' then LS.KW else 0 end KW, '
      '  case when LS.RB=\''+$__RUB.ref(650)+'\' then LS.KW else 0 end ZAS, '
      '  case when LS.RB=\''+$__RUB.ref(765)+'\' then LS.KW else 0 end FEP, '
      '  case when LS.RB=\''+$__RUB.ref(766)+'\' then LS.KW else 0 end FRP, '
      '  case when LS.RB=\''+$__RUB.ref(767)+'\' then LS.KW else 0 end FC, '
      '  case when LS.RB=\''+$__RUB.ref(769)+'\' then LS.KW else 0 end ZUP, '
      '  case when LS.RB=\''+$__RUB.ref(784)+'\' then LS.KW else 0 end KU, '
      '  case when LS.RB=\''+$__RUB.ref(792)+'\' then LS.KW else 0 end KC, '
      '  case when LS.RB=\''+$__RUB.ref(793)+'\' then LS.KW else 0 end KO, '
      '  case when LS.RB=\''+$__RUB.ref(797)+'\' or LS.RB=\''+$__RUB.ref(798)+'\' then LS.KW else 0 end PD, '
      '  case when LS.RB=\''+$__RUB.ref(7088)+'\' then LS.KW else 0 end PPKWF, '
      '  case when LS.RB=\''+$__RUB.ref(7089)+'\' then LS.KW else 0 end PPKDF, '
      '  case when LS.RB=\''+$__RUB.ref(7090)+'\' then LS.KW else 0 end PPKW, '
      '  case when LS.RB=\''+$__RUB.ref(7091)+'\' then LS.KW else 0 end PPKD, '
      '  case when LS.RB=\''+$__RUB.ref(7092)+'\' then LS.KW else 0 end PPKU, '
      '  case when LS.RB=\''+$__RUB.ref(7093)+'\' then LS.KW else 0 end PPKF, '
      '  case when LS.RB=\''+$__RUB.ref(950)+'\' then LS.KW else 0 end POTR, '
      '  case when LS.RB=\''+$__RUB.ref(958)+'\' then LS.KW else 0 end FEZ, '
      '  case when LS.RB=\''+$__RUB.ref(959)+'\' then LS.KW else 0 end FRZ, '
      '  case when LS.RB=\''+$__RUB.ref(960)+'\' then LS.KW else 0 end FW, '
      '  case when LS.RB=\''+$__RUB.ref(963)+'\' then LS.KW else 0 end ZUF, '
      '  case when LS.RB=\''+$__RUB.ref(982)+'\' then LS.KW else 0 end FP, '
      '  case when LS.RB=\''+$__RUB.ref(983)+'\' then LS.KW else 0 end FG, '
      '  case when LS.RB=\''+$__RUB.ref(962)+'\' then LS.KW else 0 end FEPOM, '
      '  case when LS.RB=\''+$__RUB.ref(990)+'\' then LS.KW else 0 end DW, '
      '  case when LS.RB=\''+$__RUB.ref(994)+'\' then LS.KW else 0 end PR, '
      '  case when LS.RB=\''+$__RUB.ref(999)+'\' then LS.KW else 0 end GT '
      'from '
      '  LS join '
      '  :_a using(LS.RH,:_a.RH)'
      'where '
      '  LS.RB in ('
      '     select R.REFERENCE '
      '     from R '
      '     where R.RN in ('
      '        500,650,765,766,767,769,784,792,793,797,798,'
      '        7088,7089,7090,7091,7092,7093,'
      '        950,958,959,960,962,963,982,983,990,994,999'
      '     ) '
      '     order by R.REFERENCE'
      '  ) ';

   _sum:=
      'sum(KW) KW, sum(ZAS) ZAS, sum(ZUP) ZUP, sum(ZUF) ZUF,'
      'sum(FEP) FEP, sum(FRP) FRP, sum(FC) FC,'
      'sum(FEZ) FEZ, sum(FRZ) FRZ, sum(FW) FW,'
      'sum(FP) FP, sum(FG) FG, sum(FEPOM) FEPOM, sum(KC) KC, sum(KO) KO,'
      'sum(KU) KU, sum(PD) PD, sum(PPKWF) PPKWF, sum(PPKDF) PPKDF, sum(PPKW) PPKW, sum(PPKD) PPKD, sum(PPKU) PPKU, sum(PPKF) PPKF, sum(DW) DW, '
      'sum(PR) PR, sum(GT) GT, sum(POTR) POTR ';

   LS.cntx_psh();
   LS.use('l_______');
   _buf:=sql(_txt,sql('select * from :_a where :_a.LT=\':_b\' order by :_a.RH',_rh,8*'*'));

   {? type_of(_buf)<>type_of(SYSLOG)
   || LS.cntx_pop();
      return(~~)
   ?};

   _fml:='';
   {! _ndx:=1.._buf.fld_num()
   |! _fml+=('_a[%1]:=_b[%1];'[$_ndx])
   !};
   _fml+='_a.add()';
   _add:=$_fml;

   _loop:=_o.first();
   {!
   |? _loop
   |! LS.use(_o.LT);
      _tmp:=sql(_txt,sql('select * from :_a where :_a.LT=\':_b\' order by :_a.RH',_rh,_o.LT));
      _loop:=_tmp.first();
      {!
      |? _loop
      |! _add(_buf,_tmp);
         _loop:=_tmp.next()
      !};
      obj_del(_tmp);
      _loop:=_o.next()
   !};
   LS.cntx_pop();

   _tmp:=sql('select :_a.OSOBA, :_a.K, %1 from :_a group by OSOBA, K'[_sum],_buf);
   obj_del(_buf);

   {? par[7]=1
   || par[2]:=
         sql(
            'select '
            '  OSOBA.NAZWISKO, '
            '  OSOBA.PIERWSZE, '
            '  OSOBA.PESEL, '
            '  :_a.* '
            'from '
            '  :_a join '
            '  OSOBA using(:_a.OSOBA,OSOBA.REFERENCE) '
            'order by '
            '  OSOBA.NAZWISKO, '
            '  OSOBA.PIERWSZE, '
            '  OSOBA.PESEL, '
            '  :_a.K',
            _tmp
         );
      {? type_of(par[2])<>type_of(SYSLOG)
      || obj_del(_tmp);
         return(~~)
      ?}
   ?};

   par[3]:=sql('select '+_sum+' from :_a',{? par[7]=1 || par[2] || _tmp ?});
   obj_del(_tmp);
   ~~
}
{EXIT: type_of(par[3])<>type_of(SYSLOG)}
{
   _k:={? par[8]<date(1999,1,1) || 7 |? data_ppk<>date(0,0,0) & par[9]>=data_ppk || 11 || 9 ?};
   (par[1][1]:=obj_new(@.CLASS.PRINT,_k)).s_frame();
   (par[1][2]:=obj_new(@.CLASS.PRINT,_k)).s_frame();
   (par[1][3]:=obj_new(@.CLASS.PRINT,_k)).s_frame();

   par[1][1].add("{? par[6] || ''      || par[2].NAZWISKO ?}",20,
                  {? par[7]=1 || 'Nazwisko'@ || '' ?});
   par[1][2].add("{? par[6] || 'RAZEM'@ || par[2].PIERWSZE ?}",20,
                  {? par[7]=1 || 'Imię'@ || '' ?});
   par[1][3].add("{? par[6] || ''      || par[2].PESEL    ?}",20,
                  {? par[7]=1 || 'PESEL'@ || '' ?});

   _l:=13; _f:=$_l+',2,\'9,\'';

   par[1][1].add("{? par[6] || '' || par[2].K ?}",_l,
                  {? par[7]=1 || 'Rodzaj umowy'@ || '' ?});
   par[1][2].add("par[2+par[6]].KW              ",_l,'Brutto'@,1,,,,,_f);
   par[1][3].add("par[2+par[6]].ZAS              ",_l,'Zasiłki ZUS'@,1,,,,,_f);

   {? par[8]<date(1999,1,1)
      ||
         par[1][1].add("par[2+par[6]].ZUP",_l,'ZUS'@,1,,,,,_f);
         par[1][2].add("''               ",_l,'ubezpieczony'@);
         par[1][3].add("''               ",_l,'');

         par[1][1].add("par[2+par[6]].ZUF",_l,'ZUS płatnik'@,1,,,,,_f);
         par[1][2].add("par[2+par[6]].FP ",_l,'F.pracy'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].FG ",_l,'F.G.Ś.P.'@,1,,,,,_f)
      ||
         par[1][1].add("par[2+par[6]].FEP",_l,'F.emerytalny'@,1,,,,,_f);
         par[1][2].add("par[2+par[6]].FRP",_l,'F.rentowy'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].FC ",_l,'F.chorobowy'@,1,,,,,_f);

         par[1][1].add("par[2+par[6]].FEZ",_l,'F.emerytalny'@,1,,,,,_f);
         par[1][2].add("par[2+par[6]].FRZ",_l,'F.rentowy'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].FW ",_l,'F.wypadkowy'@,1,,,,,_f);

         par[1][1].add("par[2+par[6]].FP ",_l,'F.pracy'@,1,,,,,_f);
         par[1][2].add("par[2+par[6]].FG ",_l,'F.G.Ś.P.'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].FEPOM",_l,'F.E.P'@,1,,,,,_f);

         par[1][1].add("''               ",_l,'Ub.zdrowotne'@);
         par[1][2].add("par[2+par[6]].KC ",_l,'pobrane'@,1,,,,,_f);
         par[1][3].add("par[2+par[6]].KO ",_l,'odliczone'@,1,,,,,_f)
   ?};

   {? data_ppk<>date(0,0,0) & par[9]>=data_ppk
   ||
      par[1][1].add("par[2+par[6]].PPKW",_l,'Pod.wpł. PPK'@,1,,,,,_f);
      par[1][2].add("par[2+par[6]].PPKD",_l,'Dod.wpł. PPK'@,1,,,,,_f);
      par[1][3].add("par[2+par[6]].PPKU",_l,'Nadp.ucz.PPK'@,1,,,,,_f);

      par[1][1].add("par[2+par[6]].PPKWF",_l,'Pod.wpł. PPK'@,1,,,,,_f);
      par[1][2].add("par[2+par[6]].PPKDF",_l,'Dod.wpł. PPK'@,1,,,,,_f);
      par[1][3].add("par[2+par[6]].PPKF",_l,'Nadp.fir.PPK'@,1,,,,,_f)
   ?};

   par[1][1].add("''              ",_l,'Koszty'@);
   par[1][2].add("par[2+par[6]].KU",_l,'uzyskania'@,1,,,,,_f);
   par[1][3].add("''              ",_l,'przychodu'@);

   par[1][1].add("''              ",_l,'Podatek'@);
   par[1][2].add("par[2+par[6]].PD",_l,'dochodowy'@,1,,,,,_f);
   par[1][3].add("par[2+par[6]].POTR",_l,'Potrącenia'@,1,,,,,_f);

   par[1][1].add("par[2+par[6]].DW",_l,' Netto'@,1,,,,,_f);
   par[1][2].add("par[2+par[6]].PR",_l,' Przelew'@,1,,,,,_f);
   par[1][3].add("par[2+par[6]].GT",_l,' Gotówka'@,1,,,,,_f);

   par[6]:=0;
   ~~
}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft-par[1][1].width())/2); ~~ }
{IF: par[7]=1}
   {FOR: par[2]}{DO}
      {SUBSTITUTE: 'tabela'}
   {OD}
{FI}
{IF: {? var_pres('[2]',par)=type_of(SYSLOG)
        || par[2].first
        || 1
     ?}}
   { par[6]:=1; ~~ }
   {SUBSTITUTE: 'tabela'}
{FI}

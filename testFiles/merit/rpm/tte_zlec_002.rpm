:!UTF-8
{TITLE:B. Zestawienie pobrań i zwrotów wszystkich asortymentów do zleceń}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  ZL.cntx_psh();
  "---Obiekt wydruku glownej tabelki.";
  VAR_DEL.delete('PRNZST');
  PRNZST:=obj_new(@.CLASS.PRINT,13);PRNZST.sl_frame();
  "---Obiekt wydruku podsumowania.";
  VAR_DEL.delete('PRNSUM');
  PRNSUM:=obj_new(@.CLASS.PRINT,12);PRNSUM.sl_frame();
  kreska:='─';
  "---1 - pobrania tylko dla biezacego okresu, 0 - wszystkie.";
  curmonth:=0;
  "---Poprzedni KTM w tabeli ZLST.";
  befktm:='';
  "---Poprzednie zlecenie w tabeli ZLST.";
  befzl:='';
  "---1 - nalezy wydrukowac nastepna tabelke elementarna, poniewaz zostalo";
  "---osiagniete kolejne zlecenie.";
  nextzl:=0;
  "---1 - nalezy wydrukowac nastepna tabelke elementarna, poniewaz zostal";
  "---osiagniety kolejny KTM.";
  nextktm:=0;
  "---Podsumowanie poszczegolnych kolumn dla kontekstu zlecenie-KTM.";
  "---Tylko 4 kolumny.";
  il_p:=war_p:=il_z:=war_z:=0;
  "---1 - byly pozycje w ostatnio iterowanej tabeli stanow zlecen.";
  waspos:=0;
  "---Ostatnio drukowany symbol zlecenia.";
  lastzlec:='';
  prn1:='PRNZST';prn2:='PRNSUM';
  tryb:=rep_export();
  waslbar:=0;
  dalej:=0;
  color:='';
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  ZL.cntx_pop();
  obj_del(PRNZST);
  obj_del(PRNSUM);
  &kreska;
  &curmonth;
  &befktm;
  &befzl;
  &nextzl;
  &nextktm;
  &il_p; &war_p; &il_z; &war_z;
  &waspos;
  &waslbar;
  &lastzlec;
  &dalej;
  &prn1;
  &prn2;
  &color;
  &lm; &ll; &vp; &lmt; &rsep;&tryb;
  VAR_DEL.delete('ZLST','zlstndx')
}
{COMMENT}
 Zestawienie pobran i zwrotow wszystkich asortymentow do zlecenia
  [SS] - 2000/09/07 - [7.22]
  [MKO] - 2001/11/15 - [7.53] OWwSK003_Aneks1
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{"------------------------------------------------------------------------"; ~~}
{"---Wszystkie okresy, czy tylko biezacy.                                 "; ~~}
{curmonth:=FUN.ask('Dokumenty tylko z bieżącego okresu?'@); ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Definicja wiersza wydruku stanu zlecen zlecen ZLST.                  "; ~~}
{IF: tryb=1}
{PRNZST.add("exec('FindAndGet','#table',ZL,ZLST.ZL,,\"ZL.SYM\",'')",20,'Zlecenie'@); ~~}
{PRNZST.add("exec('FindAndGet','#table',M,ZLST.KOD,,\"M.KTM\",'')",20,'Indeks materiałowy'@); ~~}
{PRNZST.add("exec('FindAndGet','#table',M,ZLST.KOD,,\"M.N\",'')",30,'Nazwa materiału'@); ~~}
{FI}
{PRNZST.add("form(ZLST.ROK,4,0,'9')+'/'+form(ZLST.MC,2)",7,'Okres'@); ~~}
{PRNZST.add("form(ZLST.IL_00,11,3)",11,'Ilość na pocz. okr.'@,1); ~~}
{PRNZST.add("form(ZLST.WAR_00,11,2)",11,'Wartość na pocz. okr.'@,1); ~~}
{PRNZST.add("form(ZLST.IL_P,11,3)",11,'Ilość pobrana'@,1); ~~}
{PRNZST.add("form(ZLST.WAR_P,11,2)",11,'Wartość pobrana'@,1); ~~}
{PRNZST.add("form(ZLST.IL_Z,11,3)",11,'Ilość zwrócona'@,1); ~~}
{PRNZST.add("form(ZLST.WAR_Z,11,2)",11,'Wartość zwrócona'@,1); ~~}
{PRNZST.add("form(ZLST.IL,11,3)",11,'Ilość aktualna'@,1); ~~}
{PRNZST.add("form(ZLST.WAR,11,2)",11,'Wartość aktualna'@,1); ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Definicja wiersza podsumowania kolumn.                               "; ~~}
{PRNSUM.add("'Razem:'",31,'Okres'@,,,,,,,,3); ~~}
{PRNSUM.add("form(il_p,,3)",11,'Ilość pobrana'@,1); ~~}
{PRNSUM.add("form(war_p,,2)",11,'Wartość pobrana'@,1); ~~}
{PRNSUM.add("form(il_z,,3)",11,'Ilość zwrócona'@,1); ~~}
{PRNSUM.add("form(war_z,,2)",11,'Wartość zwrócona'@,1); ~~}
{PRNSUM.add("''",23,'',,,,,,,,2); ~~}
{PAR_WYDR.TITLE:='ZESTAWIENIE POBRAŃ I ZWROTÓW ASORTYMENTÓW DO ZLECEŃ'@;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRNZST.width())/2); ~~}
{IF: curmonth}
   {<{lmt+1} form('Wariant wydruku:'@,20) + 'Pobrania z okresu: %1/%2'@[$ST.AR,form(ST.AM,-2)]}
{ELSE}
   {<{lmt+1} form('Wariant wydruku:'@,20) + 'Wszystkie pobrania'@}
{FI}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------MACRO------------------------{COMMENT-}
{"---Zawiera wnetrze elementarnej petli wydruku.                          "; ~~}
{MACRO: 'petla1'}
  {il_p+=ZLST.IL_P; war_p+=ZLST.WAR_P; ~~}
  {il_z+=ZLST.IL_Z; war_z+=ZLST.WAR_Z; ~~}
    {FIRST}
      {IF: ~tryb & lineleft<10}{PAGE}{FI}
      {IF: lastzlec<>exec('FindAndGet','#table',ZL,ZLST.ZL,,"ZL.SYM",'')}
        {IF: tryb<>1}
           {FONT: 'W12'}
           {<{lmt+1} 'Zlecenie: %1'@[exec('FindAndGet','#table',ZL,ZLST.ZL,,"ZL.SYM",'')]}
           {FONT: 'T8'}
           {lastzlec:=exec('FindAndGet','#table',ZL,ZLST.ZL,,"ZL.SYM",''); ~~}
        {FI}
      {FI}
      {FIRST}
         {IF: tryb<>1}
            {<{lmt+1} 'KTM: %1 - %2, jm: %3'@[exec('FindAndGet','#table',M,ZLST.KOD,,"M.KTM",''),exec('FindAndGet','#table',M,ZLST.KOD,,"M.N",''),exec('FindAndGet','#table',M,ZLST.KOD,,"M.J().KOD",'')]}
         {FI}
      {FIRST-}
      {IF: tryb<>1}
         {FONT: 'T8B'}
         {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
      {FI}
      {FIRST-}
     {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIA0'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
     {MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Zawiera wnetrze glownej petli wydruku.                               "; ~~}
{MACRO: 'petla2'}
  {nextzl:=nextktm:=0; ~~}
  {IF: exec('FindAndGet','#table',ZL,ZLST.ZL,,"ZL.SYM",'')<>befzl}
    {befzl:=exec('FindAndGet','#table',ZL,ZLST.ZL,,"ZL.SYM",''); ~~}
    {nextzl:=1; ~~}
  {FI}
  {IF: exec('FindAndGet','#table',M,ZLST.KOD,,"M.KTM",'')<>befktm}
    {befktm:=exec('FindAndGet','#table',M,ZLST.KOD,,"M.KTM",''); ~~}
    {nextktm:=1; ~~}
  {FI}
  {IF: nextzl+nextktm<>0}
    {il_p:=war_p:=il_z:=war_z:=0; ~~}
    {waslbar:=1; ~~}
    {ZLST.cntx_psh(); ~~}
    {waspos:=0; ~~}
    {IF: curmonth}
      {FOR: ZLST}
        {INDEX: zlstndx}
        {PREFIX: ZLST.ZL,ZLST.KOD,ST.AR,ST.AM}
      {DO}
        {FIRST}
          {waspos:=1; ~~}
        {FIRST-}
        {SUBSTITUTE: 'petla1'}
      {OD}
    {ELSE}
      {FOR: ZLST}
        {INDEX: zlstndx}
        {PREFIX: ZLST.ZL,ZLST.KOD}
      {DO}
        {FIRST}
          {waspos:=1; ~~}
        {FIRST-}
        {SUBSTITUTE: 'petla1'}
       {OD}
    {FI}
  {IF: waspos=1}
   {IF: ~curmonth}
    {IF: tryb<>1}{SUBSTITUTE: 'LINIAFS'}{FI}
    {IF: tryb=2}{<{lmt+1} ''}{FI}
   {ELSE}
    {IF: tryb<>1}{SUBSTITUTE: 'LINIAF'}{FI}
   {FI}
  {FI}
   {ZLST.cntx_pop(); ~~}
    {FI}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{IF: tryb=1}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{FI}
{FOR: 'ZL'}
  {INDEX: 'HIDDEN'}
  {PREFIX: 'N'}
{DO}
  {
  exec('openmask','zl_common',ZL.ref());
  VAR_DEL.delete('ZLST','zlstndx');
  ZLST:=exec('zlst','zl_common',ZL.ref());
  zlstndx:=ZLST.ndx_tmp(,,'ZL',,,'KOD',,,'ROK',,,'MC',,,'OKRES',,);
  ~~}
  {FOR: ZLST}
    {INDEX: zlstndx}
    {PREFIX: $ZL.ref()}
  {DO}
    {SUBSTITUTE: 'petla2'}
  {OD}
{TOTAL}
{IF: tryb=1}{SUBSTITUTE: 'LINIAF'}{FI}
{OD}


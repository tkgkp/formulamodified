:!UTF-8
{TITLE:Porównanie analiz zlecenia}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   ANZH.cntx_psh();
   ANZTTO.cntx_psh();
   ANZMAT.cntx_psh();
   __ANZP.cntx_psh();
   dalej:=0;
   VAR_DEL.delete('PRN','PRNCONS','PRNFORM','PRNNAG','PRNMAT','__PRNMAT','TABEDIT');
   PRN:=obj_new(@.CLASS.PRINT,4);
   PRN.s_frame();
   PRNCONS:=obj_new(@.CLASS.PRINT, 6); PRNCONS.s_frame();
   PRNFORM:=obj_new(@.CLASS.PRINT, 6); PRNFORM.s_frame();
   PRNNAG:=obj_new(@.CLASS.PRINT, 6); PRNNAG.s_frame();
   PRNMAT:=obj_new(@.CLASS.PRINT,6);
   PRNMAT.s_frame();
   __PRNMAT:=tab_tmp(2,'KOD','STRING[15]','Kod surowca',
                       'NAZ','STRING[30]','Opis suowca',
                       'CENA','REAL','Cena surowca',
                       'CENA1','REAL','Cena surowca',
                       'POCH','STRING[11]','Pochodzenie',
                       'POCH1','STRING[11]','Poch1'
                       );
   prn1:='PRN';
   color:='';
   pkalmat:=pcons:=pform:=0;
   lm:=ll:=vp:=lmt:=rsep:=0;
   ndx_tmp:=ANZP.ndx_tmp(,1,'NRA',,0,'RUBR','NR',0)
}
{END:
   ANZH.cntx_pop();
   ANZTTO.cntx_pop();
   ANZMAT.cntx_pop();
   __ANZP.cntx_pop();
   VAR_DEL.delete('PRN','PRNCONS','PRNFORM','PRNNAG','PRNMAT','__PRNMAT','TABEDIT');
   &dalej;
   &pkalmat;
   &pcons;
   &pform;
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   ANZP.ndx_drop(ndx_tmp)
}
  {TABEDIT:=tab_tmp(
     ,'KCONS','STRING[1]','Stałe analizy'@
     ,'KFORM','STRING[1]','Formuły użyte w analizie'@
   );
   _wydr_naz:='drukanzz';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add();
      TABEDIT.KFORM:=TABEDIT.KCONS:='T'
   || TABEDIT.KFORM:=WYDRUKIL.OD_STR;
      TABEDIT.KCONS:=WYDRUKIL.DO_STR
   ?};
   _red:=TABEDIT.mk_edit('Wydruk kartotek stowarzyszonych'@,0,'tabedit1');
   TABEDIT.win_efld(_red,,'KCONS',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KFORM',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Zapisz'@],"'key:F2'");
   TABEDIT.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],"'key:Esc'");
   TABEDIT.win_edit(_red);
   ~~}
   {IF: TABEDIT.edit()}
    { WYDRUKIL.OD_STR:=TABEDIT.KFORM;
      WYDRUKIL.DO_STR:=TABEDIT.KCONS;
      WYDRUKIL.put(1);
      pcons:=(-TABEDIT.KCONS='t');
      pform:=(-TABEDIT.KFORM='t');~~}
   {ELSE}
      {EXIT}
   {FI}
{COMMENT}
  Wydruk porownania kalkulacji zlecenia.
  [MKO] - 2003/02/28 - [8.10]
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Koszty -> Analiza -> Porównanie -> Zmieniony algorytm -> Drukuj
{COMMENT-}
{COMMENT}---Definicja wiersza wydruku pozycji porownania analiz.{COMMENT-}
{  PRN.add("form(__ANZP.NR,,0,'9')",8,'Nr rubr.'@,1);
   PRN.add("__ANZP.OPIS",30,'Opis rubryki'@);
   PRN.add("form(__ANZP.WART1,,2)",15,'Wart. stara'@,1);
   PRN.add("form(__ANZP.WART,,2)",15,'Wartość nowa'@,1);
   PRNMAT.add("__PRNMAT.KOD",10,'Kod surowca'@);
   PRNMAT.add("__PRNMAT.NAZ",25,'Nazwa surowca'@);
   PRNMAT.add("form(__PRNMAT.CENA1,,2)",8,'Cena stara'@,1);
   PRNMAT.add("form(__PRNMAT.CENA,,2)",8,'Cena nowa'@,1);
   PRNMAT.add("__PRNMAT.POCH1",5,'Poch. stara'@);
   PRNMAT.add("__PRNMAT.POCH",5,'Poch. nowa'@);
   PRNCONS.add("form(__ANKON.NR,,0,'9')",5,'Rubr.'@,1);
   PRNCONS.add("__ANKON.OPIS",30,'Opis'@);
   PRNCONS.add("form(ANKON.WAR,,2)",12,'Wartość stara'@,1);
   PRNCONS.add("form(__ANKON.WAR,,2)",12,'Wartość nowa'@,1);
   PRNFORM.add("form(__AFORM.NR,,0,'9')",5,'Rubr.'@,1);
   PRNFORM.add("AFORM.FRM",30,'Formuła stara'@);
   PRNFORM.add("__AFORM.FORM",30,'Formuła nowa'@);
   PAR_WYDR.TITLE:='PORÓWNANIE ANALIZ ZLECENIA'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Nr analizy:'@} {<{lmt+15} form(ANZH.NRA)}
{<{lmt+1} 'Zlecenie:'@}   {<{lmt+15} ANZH.ZLEC().SYM}
{<{lmt+1} 'Wariant:'@}    {<{lmt+15} ANZH.OPC().NA}
{FONT: 'T10B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{lmt:=int((charleft()-PRN.width())/2); ~~}
{ANZP.prefix(); ~~}
{IF: __ANZP.first()}{ dalej:=1; ~~}
{WHILE: dalej}
{DO}
   {SUBSTITUTE: 'LINIA0'}
   { dalej:=__ANZP.next(); ~~}
{OD}
{FI}
{IF: pform}
  { PAR_WYDR.TITLE:='FORMUŁY ANALIZY ZLECENIA'@; ~~}
  {__AFORM.prefix(); ~~}
  {IF: __AFORM.first()}{PAGE}
{ dalej:=1; prn1:='PRNFORM';lmt:=int((charleft()-PRNFORM.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {AFORM.clear();{? ~AFORM.seek(__AFORM.REF,'aformsrv') || AFORM.FRM:=''?};~~}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=__AFORM.next(); ~~}
  {OD}
{FI}
{IF: pcons}
  { PAR_WYDR.TITLE:='STAŁE ANALIZY ZLECENIA'@; ~~}
  {__ANKON.prefix(); ~~}
  {IF: __ANKON.first()}{PAGE}
{ dalej:=1; prn1:='PRNCONS';lmt:=int((charleft()-PRNCONS.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {ANKON.clear();ANKON.seek(__ANKON.REF,'ankonst');~~}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=__ANKON.next(); ~~}
  {OD}
{FI}

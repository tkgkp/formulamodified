:!UTF-8
{TITLE:Realizacja planów urlopowych}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','Databad','PRT','prn2','Sumaplan');
   VAR_DEL.delete('Sumawyk','Plany','Wykorz','plan','Sumareal','Urlopy');
   tresc:='rplanrea';
   Lp:=0;
   PAR_WYDR.TITLE:='Realizacja planów urlopowych'@;
   P.cntx_psh();
   Databad:=exec('par_wydr_ddp','pkd',
      PAR_WYDR.TITLE,
      "  {? cur_tab().OD=#0
         || FUN.emsg('Podano zerową datę.'@); 0
         || 1
         ?}
      ",
      date(,,0),'Podaj datę wyliczenia:',"1"
   );
   {? Databad.size()
   || PRN:=obj_new(@.CLASS.PRINT,11);
      {? ~rep_export()
      || PRN.add("Lp+=1",5,'Lp.'@,1,,,,,',,\'99\'');
         PRN.add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE",51,'Nazwisko i imię'@)
      ||
         PRN.add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
         PRN.add("P.OSOBA().PIERWSZE",20,'Imię'@)
      ?};
      PRN.add("KART_URL.LIM_ZAL",7,'Urlop zaległy'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("KART_URL.LIM_AKT",7,'Urlop bieżący'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("KART_URL.URL_DOD",13,'Urlop uzupełniający'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("KART_URL.NSP_ZAL",9,'Urlop dodatkowy zaległy'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("KART_URL.URL_NSP",9,'Urlop dodatkowy'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add(
         "  KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.URL_NSP+KART_URL.NSP_ZAL
         ",9,'Urlop całkowity'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?}
      );
      PRN.add("Plany",15,'Planowany urlop'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("Rplan",15,'Wykorzystany zgodnie z planem'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.add("Wykorz",15,'Wykorzystany urlop'@,1,,,,,{? ~rep_export() || ',2,\'9.\'' || '12,2' ?});
      PRN.s_frame();
      prn1:='PRN';

      PRT:=obj_new(@.CLASS.PRINT,4);
      {? ~rep_export()
      || PRT.add("'             '+'R A Z E M '@",131);
         PRT.add("form(Sumaplan,,2,'9.,')",15,'',1);
         PRT.add("form(Sumareal,,2,'9.,')",15,'',1);
         PRT.add("form(Sumawyk,,2,'9.,')",15,'',1);
         PRT.s_frame();
         prn2:='PRT'
      ?};

      lmt:=rsep:=Sumaplan:=Sumawyk:=Sumareal:=Urlopy:=Plany:=Wykorz:=Rplan:=0;
      vp:=h_rat:=1;

      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?};
   URL_PLAN.cntx_psh();
   URL_PLAN.index('ROK');
   URL_POZ.cntx_psh();
   URL_POZ.index('URL_POZ');
   N.cntx_psh();
   N.index('NIPRACNB')
}
{END:
   VAR_DEL.delete('PRN','PRN','prn1','vp','lmt','rsep','h_rat','tresc','Lp','Databad','PRT','prn2','Sumaplan');
   VAR_DEL.delete('Sumawyk','Plany','Wykorz','plan','Sumareal','Urlopy');
   N.cntx_pop();
   URL_POZ.cntx_pop();
   URL_PLAN.cntx_pop();
   P.cntx_pop()
}
{EXIT: ~Databad.size() | ~params_get().P.size()}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: vp}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Data kontroli:'@} {Databad.OD$1}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{  Plany:=0;
   Wykorz:=0;
   Rplan:=0;
   Urlopy:=0; ~~
}
{IF: (P.DZ>=Databad.OD | P.DZ=#0) & P.DZA<=Databad.OD}
{FOR: KART_URL}{INDEX: 'PRAC_ROK'}{PREFIX: P.ref(),Databad.OD~1}
{DO}
{  URL_PLAN.prefix(P.ref(),'A',Databad.OD~1);
   Urlopy:=KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.NSP_ZAL+KART_URL.URL_NSP; ~~
}
{IF: Urlopy>0 & URL_PLAN.first() & URL_PLAN.AZ='T'}
{  URL_POZ.prefix(URL_PLAN.ref());
   {? URL_POZ.first()
   || __KAL.set_cal(P.KAL,Databad.OD~1);
      {!
      |? {? URL_POZ.OD<=Databad.OD
         || _od:=URL_POZ.OD;
            _do:=URL_POZ.DO;
            {? URL_POZ.DO>Databad.OD
            || _do:=Databad.OD;
               Plany+=exec('nominal','godziny',_od,_do)/exec('norma','godziny',Databad.OD)
            || Plany+=URL_POZ.NG/exec('norma','godziny',Databad.OD)
            ?};
            N.prefix('N',P.ref(),KST_PAR.URLOP().RN);
            {? N.find_le(_do) & _od<=N.DO
            || {!
               |? _start:={? N.OD<_od || _od || N.OD ?};
                  _stop:={? _do<N.DO || _do || N.DO ?};
                  {? _start=N.OD & _stop=N.DO
                 || Rplan+=N.NG/exec('norma','godziny',Databad.OD)
                 || Rplan+=exec('nominal','godziny',_start,_stop)/exec('norma','godziny',Databad.OD)
                 ?};
                 N.prev() & N.OD<=_do & _od<=N.DO
              !}
            ?};
            N.prefix('N',P.ref(),__RUB.sys_kod(1113,,1));
            {? N.find_le(_do) & _od<=N.DO
            || {!
               |? _start:={? N.OD<_od || _od || N.OD ?};
                  _stop:={? _do<N.DO || _do || N.DO ?};
                  {? _start=N.OD & _stop=N.DO
                 || Rplan+=N.NG/exec('norma','godziny',Databad.OD)
                 || Rplan+=exec('nominal','godziny',_start,_stop)/exec('norma','godziny',Databad.OD)
                 ?};
                 N.prev() & N.OD<=_do & _od<=N.DO
              !}
            ?}
        ?};
        URL_POZ.next() & URL_POZ.OD<=Databad.OD
     !}
  ?};
  Wykorz:=exec('wykorzystany','kart_url',Databad.OD,0,2);
  Plany:=Plany$2;
  Rplan:=Rplan$2;
  Sumaplan+=Plany;
  Sumawyk+=Wykorz;
  Sumareal+=Rplan; ~~
}
{SUBSTITUTE: 'LINIA0'}
{FI}
{OD}
{FI}
{MACRO-}
{MACRO: 'podsum'}
{ rsep:=1;~~ }
{SUBSTITUTE: 'LINIA02'}
{ prn1:=prn2;~~ }
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{IF: ~rep_export() & (Sumaplan | Sumawyk | Sumareal)}
   {SUBSTITUTE: 'podsum'}
{FI}
:!UTF-8
{COMMENT}
(c) MacroSoft S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: kas_kas.rpi
Utworzony: [MB]
==================================================
Zawartosc: Wydruk raportu kasowego - z podanego okresu
{COMMENT-}
{EXIT: ~exec('an_par','kasa_wydruki',0,0,1)}
{  bo_kasy:=tab_tmp(1
      ,'WAL'   ,'STRING[8]'   ,'Waluta'
      ,'KW'    ,'REAL'        ,'Kwota');
   bo_kasyl:="
      BO_KASY.cntx_psh();
      BO_KASY.index('STANKAS');
      BO_KASY.prefix(KUSERUPR.STANKAS);
      _loop:=BO_KASY.first();
      {!
      |? _loop
      |!
         {? bo_kasy.find_key(KINFO.NAROD().KOD)
         || bo_kasy.KW+=BO_KASY.BO_PLN;
            bo_kasy.put()
         || bo_kasy.WAL:=KINFO.NAROD().KOD;
            bo_kasy.KW:=BO_KASY.BO_PLN;
            bo_kasy.add()
         ?};
         {? BO_KASY.WAL().SYM<>KINFO.NAROD().KOD
         || {? bo_kasy.find_key(BO_KASY.WAL().SYM)
            || bo_kasy.KW+=BO_KASY.BO_WAL;
               bo_kasy.put()
            || bo_kasy.WAL:=BO_KASY.WAL().SYM;
               bo_kasy.KW:=BO_KASY.BO_WAL;
               bo_kasy.add()
            ?}
         ?};
         _loop:=BO_KASY.next()
      !};
      BO_KASY.cntx_pop()
   ";
   STANKAS.cntx_psh;
   {? rodz_mks='S'
   || {? STANKAS.seek(BB.sqlint(ref_mks),STANKAS.name)
      || KUSERUPR.index('KU_ST');
         KUSERUPR.prefix(USERS.ref,STANKAS.ref);
         {? KUSERUPR.first()
         || temp1:=STANKAS.SYM;
            temp2:=STANKAS.NAZWA;
            temp3:=$STANKAS.KOD;
            temp3:='and DOKUMENT.REFERENCE like \'kdo'+(('00'+temp3)+3)+'%\'';
            bo_kasyl()
         || temp1:='';
            temp2:='';
            temp3:=' and 0=1'
         ?}
      ?}
   || _ok:=0;
      {? rodz_mks='M' & MKASA.seek(BB.sqlint(ref_mks),MKASA.name)
      || temp1:=MKASA.NAZ;
         KUSERUPR.index('MKASAST');
         KUSERUPR.prefix(MKASA.ref);
         _ok:=1
      || temp1:='wszytskie uprawnione';
         KUSERUPR.index('KU');
         KUSERUPR.prefix(USERS.ref);
         _ok:=1
      ?};
      temp2:=temp3:='';
      {? KUSERUPR.first()
      || temp3+=' and (';
         {! |? {? rodz_mks='M'
               || KUSERUPR.cntx_psh;
                  KUSERUPR.index('KU_ST');
                  KUSERUPR.prefix(OPERATOR.USER,KUSERUPR.STANKAS);
                  {? KUSERUPR.first
                  || _upr:='T'
                  || _upr:='N'
                  ?};
                  KUSERUPR.cntx_pop;
                  {? _upr='T'
                  || temp3+=' DOKUMENT.REFERENCE like \'kdo'+(('00'+$KUSERUPR.STANKAS().KOD)+3)+'%\' or ';
                     bo_kasyl()
                  ?}
               || temp3+=' DOKUMENT.REFERENCE like \'kdo'+(('00'+$KUSERUPR.STANKAS().KOD)+3)+'%\' or ';
                  bo_kasyl()
               ?};
               KUSERUPR.next()
         !};
         {? temp3=' and ('
         || temp3:=' and 0=1'
         || temp3:=(temp3-3)+') '
         ?}
      || temp3:=' and 0=1'
      ?}
   ?};
   {? rodz_mks<>'S' | STANKAS.WALUTA=exec('wal_nar','kasa_wspolne') | (STANKAS.WALUTA().SYM<>'' & WYDRUKI.PAR2='N')
   || r_macr:=0
   |? (STANKAS.WALUTA().SYM<>'' & WYDRUKI.PAR2='T')
   || r_macr:=1
   || r_macr:=2
   ?};
   STANKAS.cntx_pop;
   ~~
}
{EXIT: {? temp3=' and 0=1'
       || FUN.info('Brak uprawnień do wybranych stanowisk lub multikas.'@);
          1
       || 0
       ?}
}
{ WYDRUKI.PAR1:=~(-WYDRUKI.PAR1);
  WYDRUKI.PAR2:=~(-WYDRUKI.PAR2);
  STANKAS.F_NUMER();
  _tytul:='Raport '@+{? rodz_mks<>'S' | STANKAS.CZY_KASA='T' || 'kasowy'@ || 'bankowy'@ ?};
  PAR_WYDR.TITLE:=~-_tytul;
  PAR_WYDR.TITLE+=' od '@ +$WYDRUKI.DAT_OD+' do '@ +$WYDRUKI.DAT_DO;
  PRN.add("w[1]",5,'Poz.'@,1);
  PRN.add("w[2]",10,'Data'@);
  {? WYDRUKI.PAR1='T' ||PRN.add("w[26]",12,{? KPARAM.CZY_B='T' || 'Typ/raport'@ || 'Kasa/raport'@ ?}) ?};
  PRN.add("w[3]",12,'Nr dokumentu'@);
  PRN.add("w[4]",30,'Od kogo/komu'@);
  PRN.add("w[5]",30,'Treść'@);
  {? r_macr || PRN.add("w[11]",4,'Wal.'@) ?};
  PRN.add("w[6]",16,'Przychód'@,1);
  PRN.add("w[7]",16,'Rozchód'@,1);
  {? WYDRUKI.PAR1='T' ||PRN1.add("w[17]",104,'')||PRN1.add("w[17]",91)?};
  {? r_macr || PRN1.add("w[20]",4,'') ?};
  PRN1.add("form(w[18],,2,' ,')",16,'',1);
  PRN1.add("form(w[19],,2,' ,')+' '",16,'',1);
  {? r_macr=0 | r_macr=1
  || {? WYDRUKI.PAR1='T' ||PRN2.add("w[12]",104)||PRN2.add("w[12]",91)?};
     {? r_macr || PRN2.add("w[20]",4,'') ?};
     PRN2.add("w[13]",16,,1);
     PRN2.add("w[14]",16,,1)
  || PRN2.add("''",32,'Podsumowanie raportu'@);
     PRN2.add("w[22]",16,'Stan początkowy'@,1);
     PRN2.add("w[23]",16,'Przychody'@,1);
     PRN2.add("w[24]",16,'Rozchody'@,1);
     PRN2.add("w[25]",16,'Stan końcowy'@,1);
     PRN2.add("w[21]",4,'Wal.'@);
     PRN2.add("w[16]",33,form('Sporz.'@,16)+'Spraw.'@)
  ?};
  {? WYDRUKI.PAR1='T' || PRN3.add("w[16]",73,'Podpisy'@)|| PRN3.add("w[16]",60,'Podpisy'@)?};
  PRN3.add("w[15]",30,'Obroty'@);
  {? r_macr || PRN3.add("w[20]",4,'Wal.'@) ?};
  PRN3.add("w[13]",16,'Przychód'@,1);
  PRN3.add("w[14]",16,'Rozchód'@,1);
  {? rodz_mks='S' & STANKAS.WALUTA<>null & STANKAS.WALUTA<>exec('wal_nar','kasa_wspolne') || r_wal:=1 || r_wal:=0 ?};
   _sql:='select 0 as LP, RAPORT.DATA_OD as DATA1, RAPORT.DATA_OD as DATA2, RAPORT.NUM_RAP as NUM, \'   \' as TYP,
             SUBSTR(DOKUMENT.REFERENCE, 4, 3) as TYP0, \'                                 \' as NAZWA,
             case when DOKUMENT.KW_PLN=0 and DOKUMENT.DOK_NUM<>0 then \'----dokument anulowany!----\' else DOKUMENT.OPIS end as OPIS,
             DOKUMENT.NAZWA as NAZWA, DOKUMENT.DATA as DATA, DOKUMENT.PLUS_MIN as PLUS_MIN,
             DOKUMENT.KW_PLN as KWOTA, DOKUMENT.KW_WAL as KWOTAW,
             DOKUMENT.KW_PLN as PRZYCH, DOKUMENT.KW_PLN as ROZCH, DOKUMENT.KW_WAL as PRZYCHW, DOKUMENT.KW_WAL as ROZCHW,
             DOKUMENT.DOK_NUM as NUM2, DOKUMENT.TYP_DOK as TYP2, WAL.SYM as WAL,
             DOKUMENT.KW_PLN as SALDO_PLUS, DOKUMENT.KW_PLN as SALDO_MIN, DOKUMENT.KW_WAL as SALDO_PLUSW, DOKUMENT.KW_WAL as SALDO_MINW
          from @DOKUMENT
             left join @RAPORT using (DOKUMENT.RAPORT, RAPORT.REFERENCE)
             left join WAL using (DOKUMENT.WALUTA, WAL.REFERENCE)
          where  DOKUMENT.DATA<=to_date(:_a) :_b
          order by DATA';
   RAPORT3:=sql(_sql,WYDRUKI.DAT_DO,temp3);
   STANKAS.index('KOD');
   RAPORT3.for_each("{? STANKAS.find_key(#RAPORT3.TYP0)
                  || RAPORT3.TYP:={? STANKAS.SYM='' || $STANKAS.KOD || STANKAS.SYM ?};
                     RAPORT3.NAZWA:=STANKAS.NAZWA;
                     RAPORT3.put
                  ?}");
   RAPORT3.win_edit(RAPORT3.mk_edit(,1));
   RAPORT3.win_sel(RAPORT3.mk_sel(,,1));
   _bo:=0;
   RAPORT3.blank;
   RAPORT3.KWOTA:=_bo; RAPORT3.TYP:='BO'; RAPORT3.PLUS_MIN:='+';
   RAPORT3.add;
   'znajdz sume BO';
   {? RAPORT3.first
   || _sal1:=0;_sal2:=_salw1:=0;_salw2:=0;
      {!
      |?
         _sal:=_sal1-_sal2+{? RAPORT3.PLUS_MIN='+' || 1 || -1 ?}*RAPORT3.KWOTA;
         _salw:=_salw1-_salw2+{? RAPORT3.PLUS_MIN='+' || 1 || -1 ?}*RAPORT3.KWOTAW;
         RAPORT3.PRZYCH:={? RAPORT3.PLUS_MIN='+' || 1 || 0 ?}*RAPORT3.KWOTA;
         RAPORT3.ROZCH:={? RAPORT3.PLUS_MIN='-' || 1 || 0 ?}*RAPORT3.KWOTA;
         RAPORT3.PRZYCHW:={? RAPORT3.PLUS_MIN='+' || 1 || 0 ?}*RAPORT3.KWOTAW;
         RAPORT3.ROZCHW:={? RAPORT3.PLUS_MIN='-' || 1 || 0 ?}*RAPORT3.KWOTAW;
         {? _sal>0
         || RAPORT3.SALDO_MIN:=0; RAPORT3.SALDO_PLUS:=_sal; _sal1:=_sal; _sal2:=0;
            RAPORT3.SALDO_MINW:=0; RAPORT3.SALDO_PLUSW:=_salw; _salw1:=_salw; _salw2:=0
         || RAPORT3.SALDO_PLUS:=0; RAPORT3.SALDO_MIN:=-_sal; _sal2:=-_sal; _sal1:=0;
            RAPORT3.SALDO_PLUSW:=0; RAPORT3.SALDO_MINW:=-_salw; _salw2:=-_salw; _salw1:=0
         ?};
         RAPORT3.put;
         RAPORT3.next
      !}
   ?};
   RAPORT3.first;
   BO.PLN:={? bo_kasy.find_key(KINFO.NAROD().KOD) || bo_kasy.KW || 0 ?};
   BO.WAL:=0;
   {? RAPORT3.find_ge(WYDRUKI.DAT_OD)
   || {? RAPORT3.prev
      || RAPORT3.get;
         BO.PLN+=RAPORT3.SALDO_PLUS-RAPORT3.SALDO_MIN;
         BO.WAL+=RAPORT3.SALDO_PLUSW-RAPORT3.SALDO_MINW
      ?}
   ?};
   {? ~r_wal || sp:=BO.PLN; sp1:=BO.WAL || sp:=BO.WAL; sp1:=BO.PLN ?};
~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{IF: page=1}
{<3 'Parametry wydruku:'@}
{IF: rodz_mks='M'}
{<5 'Multikasa raportu: '@+temp2}
{ELIF: rodz_mks='S' }
{<5 'Stanowisko raportu: '@+ temp1+' - '+temp2}
{ELSE}
{<5 'Wszystkie uprawnione stanowiska'@}
{FI}
{<5 'Waluta: '@+{? STANKAS.WALUTA<>null || STANKAS.WALUTA().SYM || 'wszystkie waluty (raport w stanowisku wielowalutowym)'@ ?}}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{IF: (r_macr=0 | r_macr=1) & page>1 & do_przen}
{ w[17]:='   Z przeniesienia  (obroty narastająco)'@; w[18]:=sw; w[19]:=sm; w[20]:=STANKAS.WALUTA().SYM;~~}
{ prn1:='PRN'; prn2:='PRN1'; separf:=rsep; rsep:=1; ~~}
{REP: PRN.fjbar(PRN1)}
{ PRN1.setcolor('255:255:255');~~}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}
{IF: r_macr=1}
{ rsep:=0; w[17]:=''; w[18]:=sw1; w[19]:=sm1; w[20]:=KINFO.NAROD().KOD;~~}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}
{FI}
{ rsep:=separf; prn1:=prn2:='PRN';~~}
{FI}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{IF: r_macr=0}
{FOOTER: 0}{IF: do_przen}{ prn1:='PRN'; prn2:='PRN1'; separf:=rsep; rsep:=1; w[17]:='   Do przeniesienia  (obroty narastająco)'@; w[18]:=sw; w[19]:=sm;~~}{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA02'}{FONT: 'T8G'}{SPACE: 'C'}
{PRN1.lbar()}{ rsep:=separf;~~}{ELSE}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}{FI}{ vp:=1;~~}
{FOOTER-}
{ELIF: r_macr=1}
{FOOTER: 0}{IF: do_przen}{ prn1:='PRN'; prn2:='PRN1'; separf:=rsep; rsep:=1; w[17]:='   Do przeniesienia  (obroty narastająco)'@; w[18]:=sw; w[19]:=sm;~~}{FONT: 'T8GB'}{SPACE: 'C'}
{ w[20]:=STANKAS.WALUTA().SYM;~~}{SUBSTITUTE: 'LINIA02'}{ prn1:=prn2:='PRN1'; w[17]:=''; w[18]:=sw1; w[19]:=sm1; rsep:=0;~~}
{ w[20]:=KINFO.NAROD().KOD;~~}{IF: WYDRUKI.PAR2='T'}{SUBSTITUTE: 'LINIA0'}{FI}{FONT: 'T8G'}{SPACE: 'C'}
{PRN1.lbar()}{ w[17]:='';~~}{ prn1:=prn2:='PRN';~~}
{ rsep:=separf;~~}{ELSE}{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}{FI}{ vp:=1;~~}
{FOOTER-}
{ELSE}
{FOOTER: 0}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}{ vp:=1;~~}
{FOOTER-}
{FI}
{INCLUDE: wsp_pw.rpi}
{ rsep:=PAR_WYDR.RSEP; ~~}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{  rsep:=PAR_WYDR.RSEP; ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}-------------------------------------------MACRO LINE-----------------------------------{COMMENT-}
{MACRO: 'LINE'}
{ separ:=rsep; {? separat=0 || rsep:=separat ?};~~}
{IF: rsep & lineleft=1}{PAGE}{FI}
{IF: page=1 | (page>1 & vp=0)}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
{REP: PRN1.fjbar(PRN)}{ rsep:=vp:=0; PRN.setcolor('255:255:255');~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ rsep:=separ;~~}
{MACRO-}
{COMMENT}-------------------------------------------MACRO LINE1-----------------------------------{COMMENT-}
{MACRO: 'LINE1'}
{ separ:=rsep; {? separat=0 || rsep:=separat ?};~~}
{IF: r_macr=2 | page=1 | (page>1 & vp=0)}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
{REP: PRN1.fjbar(PRN)}{ rsep:=vp:=0; PRN.setcolor('255:255:255');~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ rsep:=separ;~~}
{MACRO-}
{COMMENT}-------------------------------------------MACRO-ONEWAL----------------------------------{COMMENT-}
{MACRO: 'ONEWAL'}
{ {! _a:=1..11 |! w[_a]:='' !};
  w[1]:=form(lp,,0,'9'); w[2]:=$RAPORT3.DATA; w[3]:=RAPORT3.TYP2+'  '+$RAPORT3.NUM2;
  w[4]:=RAPORT3.NAZWA;
  w[5]:=RAPORT3.OPIS;
  w[26]:=RAPORT3.TYP+'/'+$RAPORT3.NUM;
  {? RAPORT3.PLUS_MIN='+'
  || w[6]:=form({? ~r_wal || RAPORT3.KWOTA || RAPORT3.KWOTAW ?},,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form({? ~r_wal || RAPORT3.KWOTA || RAPORT3.KWOTAW ?},,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  w[6]+=''; w[7]+={? RAPORT3.NUM2<>0 & RAPORT3.KWOTA=0 || '*'||' '?};
  ~~}
{IF: lineleft()=1 | lineleft()=2 | lineleft()=3}{PAGE}{FI}
{ separat:=1;~~}{SUBSTITUTE: 'LINE'}
{ {? RAPORT3.PLUS_MIN='+'
  || sw+={? ~r_wal || RAPORT3.KWOTA$2 || RAPORT3.KWOTAW$2 ?};
     sw_pln+=RAPORT3.KWOTA$2
  || sm+={? ~r_wal || RAPORT3.KWOTA$2 || RAPORT3.KWOTAW$2 ?};
     sm_pln+=RAPORT3.KWOTA$2
  ?}; licznik:=2;~~}
{MACRO-}
{COMMENT}-------------------------------------------MACRO-MULWAL----------------------------------{COMMENT-}
{MACRO: 'MULWAL'}
{ {! _a:=1..11 |! w[_a]:='' !};
  w[1]:=form(lp,,0,'9'); w[2]:=$RAPORT3.DATA; w[3]:=RAPORT3.TYP2+'  '+$RAPORT3.NUM2;
  w[4]:=RAPORT3.NAZWA;
  w[5]:=RAPORT3.OPIS;
  w[11]:=RAPORT3.WAL;
  w[26]:=RAPORT3.TYP+'/'+$RAPORT3.NUM;
  {? RAPORT3.PLUS_MIN='+'
  || w[6]:=form(RAPORT3.KWOTAW,,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form(RAPORT3.KWOTAW,,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  w[6]+=''; w[7]+={? RAPORT3.NUM2<>0 & RAPORT3.KWOTA=0 || '*'||' '?};
  ~~}
{IF: lineleft()=1 | lineleft()=2 | lineleft()=3}{PAGE}{FI}
{ separat:=1;~~}{SUBSTITUTE: 'LINE'}
{ w[1]:=''; w[2]:=''; w[3]:='';
  w[4]:='';
  w[5]:='';
  w[11]:=KINFO.NAROD().KOD;
  w[26]:='';
  {? RAPORT3.PLUS_MIN='+'
  || w[6]:=form(RAPORT3.KWOTA,,2,' ,'); w[7]:=form(0,,2,' ,')
  || w[7]:=form(RAPORT3.KWOTA,,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  w[6]+=''; w[7]+={? RAPORT3.NUM2<>0 & RAPORT3.KWOTA=0 || '*'||' '?};
  ~~}
{ separat:=0;~~}{SUBSTITUTE: 'LINE'}
{ {? RAPORT3.PLUS_MIN='+'
  || sw+=RAPORT3.KWOTAW$2;
     sw1+=RAPORT3.KWOTA$2;
     sw_pln+=RAPORT3.KWOTA$2
  || sm+=RAPORT3.KWOTAW$2;
     sm1+=RAPORT3.KWOTA$2;
     sm_pln+=RAPORT3.KWOTA$2
  ?}; licznik:=2;~~}
{MACRO-}
{COMMENT}-------------------------------------------WYDRUK-----------------------------------{COMMENT-}
{ lp:=0;~~}
{EXIT: ~(var_pres('RAPORT3')>5)}
{FOR: RAPORT3}
{DO}
{IF:RAPORT3.DATA>=WYDRUKI.DAT_OD}
{ lp+=1;~~}
{IF: r_macr=0}{SUBSTITUTE: 'ONEWAL'}{ELSE}{SUBSTITUTE: 'MULWAL'}{FI}
{FI}
{OD}
{COMMENT}----------------------------------------FOOTER LAST-------------------------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: r_macr=2}
{REP: PRN2.fhbar()}{ vp:=1;~~}
{ PRN2.ini_head();~~}
{WHILE: PRN2.h_next()}
{DO}
   {REP: PRN2.h_fmline(lmt)}
{OD}
{ELSE}
{ prn1:=prn2:='PRN'; PRN2.setcolor('255:255:255');~~}
{SUBSTITUTE: 'TABHEAD'}
{REP: PRN.fjbar(PRN2)}{ vp:=0; ~~}
{FI}
{HEADER-}
{ do_przen:=1;~~}
{IF: lineleft()<5 & lineleft<>0}{PAGE}{FI}
{IF: lineleft()<>0}
{IF: r_macr=2}
{FONT: 'T8GB'}{SPACE: 'C'}
{REP: PRN.flbar()}
{ PRN2.setcolor(color);~~}
{ PRN2.ini_head();~~}
{REP: PRN2.fhbar(lmt)}
{WHILE: PRN2.h_next()}
{DO}
   {REP: PRN2.h_fmline(lmt)}
{OD}
{ELSE}
{REP: PRN.fjbar(PRN2)}
{FI}
{FI}
{FOOTER: 0}
{PRN2.lbar()}{ vp:=0;~~}
{FOOTER-}
{ vp:=0;~~}
{ prn1:=prn2:='PRN2'; PRN2.setcolor('255:255:255');~~}
{FONT: 'T8GB'}{SPACE: 'C'}{ {! _a:=1..25 |! w[_a]:='' !};~~}
{IF: r_macr=0 | r_macr=1}
{ w[12]:='   Podsumowanie raportu (przychody i rozchody)'@; w[13]:=form(sw,,2,' ,'); w[14]:=form(sm,,2,' ,')+' ';
  rsep:=0; w[20]:=STANKAS.WALUTA().SYM;~~}
{SUBSTITUTE: 'LINIA0'}
{IF: r_macr & WYDRUKI.PAR2='T'}
{ w[13]:=form(sw1,,2,' ,'); w[14]:=form(sm1,,2,' ,');  w[20]:=KINFO.NAROD().KOD; w[16]:=w[12]:=w[15]:=''; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{COMMENT}   HEADER                                                      {COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{REP: PRN3.fbar()}{PRN3.setcolor('255:255:255');~~}
{HEADER-}
{COMMENT}   FOOTER                                                      {COMMENT-}
{FOOTER: 0}
{PRN3.lbar()}{vp:=0;~~}
{FOOTER-}
{COMMENT}   PODSUMOWANIE                                                {COMMENT-}
{ prn1:=prn2:='PRN3'; PRN3.setcolor(color);~~}
{REP: PRN2.flbar()}
{ PRN3.ini_head();~~}
{REP: PRN3.fhbar(lmt)}
{WHILE: PRN3.h_next()}
{DO}
   {REP: PRN3.h_fmline(lmt)}
{OD}
{REP: PRN3.fbar()}
{PRN3.setcolor('255:255:255');~~}
{ w[12]:=''; w[13]:=form(sp,,2,' ,'); w[14]:='';  w[20]:=STANKAS.WALUTA().SYM;
  w[15]:=12*' '+'Stan na początek'@; w[16]:=form('Sporz.'@,50)+'Spraw.'@; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{IF: r_macr & WYDRUKI.PAR2='T'}
{ w[13]:=form(sp1,,2,' ,'); w[14]:='';  w[20]:=KINFO.NAROD().KOD; w[12]:=w[15]:=w[16]:=''; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ w[13]:=''; w[14]:=form(sp-sm+sw,,2,' ,')+' '; w[15]:=14*' '+'Stan na koniec'@; w[16]:=''; w[20]:=STANKAS.WALUTA().SYM; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{IF: r_macr & WYDRUKI.PAR2='T'}
{ w[14]:=form(sp1-sm1+sw1,,2,' ,'); w[13]:=w[15]:=w[16]:=''; w[20]:=KINFO.NAROD().KOD; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ w[13]:=form(sw+sp,,2,' ,');    w[14]:=form(sp+sw,,2,' ,')+' '; w[15]:=20*' '+'Suma'; w[20]:=STANKAS.WALUTA().SYM;  ~~}
{SUBSTITUTE: 'LINIA0'}
{IF: r_macr & WYDRUKI.PAR2='T'}
{ w[13]:=form(sw1+sp1,,2,' ,'); w[14]:=form(sp1+sw1,,2,' ,')+' '; w[15]:=''; w[20]:=KINFO.NAROD().KOD;  ~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ELSE}{ rsep:=1; vp:=1;~~}
{ RAPORT3.index(RAPORT3.ndx_tmp(,,'WAL',,,'DATA',,));~~}
{FOR: 'WAL'}
{INDEX: 'WAL_SYM'}
{DO}
{ BO.WAL:=0;
  sw1:=sm1:=0;
  RAPORT3.prefix(WAL.SYM);
  {? RAPORT3.first
  || _sal1:=0;_sal2:=_salw1:=0;_salw2:=0;
     {!
     |?
        _salw:=_salw1-_salw2+{? RAPORT3.PLUS_MIN='+' || 1 || -1 ?}*RAPORT3.KWOTAW;
        RAPORT3.PRZYCHW:={? RAPORT3.PLUS_MIN='+' || 1 || 0 ?}*RAPORT3.KWOTAW;
        RAPORT3.ROZCHW:={? RAPORT3.PLUS_MIN='-' || 1 || 0 ?}*RAPORT3.KWOTAW;
        {? _salw>0
        || RAPORT3.SALDO_MINW:=0; RAPORT3.SALDO_PLUSW:=_salw; _salw1:=_salw; _salw2:=0
        || RAPORT3.SALDO_PLUSW:=0; RAPORT3.SALDO_MINW:=-_salw; _salw2:=-_salw; _salw1:=0
        ?};
        RAPORT3.put;
        RAPORT3.next
     !}
  ?};
  RAPORT3.first;
  BO.WAL:={? bo_kasy.find_key(WAL.SYM) || bo_kasy.KW || 0 ?};
  {? RAPORT3.find_ge(WYDRUKI.DAT_OD)
  || {? RAPORT3.prev
     || RAPORT3.get;
        BO.WAL+=RAPORT3.SALDO_PLUSW-RAPORT3.SALDO_MINW
     ?};
     RAPORT3.find_ge(WYDRUKI.DAT_OD);
     {!
     |?
        sw1+={? RAPORT3.PLUS_MIN='+' || 1 || 0 ?}*RAPORT3.KWOTAW;
        sm1+={? RAPORT3.PLUS_MIN='-' || 1 || 0 ?}*RAPORT3.KWOTAW;
        RAPORT3.next
     !}
  ?};
  w[21]:=WAL.SYM;
  w[22]:=form(BO.WAL,,2,' ,');  w[23]:=form(sw1,,2,' ,');
  w[24]:=form(sm1,,2,' ,'); w[25]:=form(BO.WAL+sw1-sm1,,2,' ,');
~~}
{IF: RAPORT3.first & (BO.WAL<>0 | sw1<>0 | sm1<>0 | BO.WAL+sw1-sm1<>0)}
{IF: lineleft()=1}{PAGE}{ vp:=1;~~}{FI}{SUBSTITUTE: 'LINIA0'}{ rsep:=0; vp:=0;~~}{ PRN2.setcolor('255:255:255');~~}
{FI}
{OD}
{IF: r_macr & WYDRUKI.PAR2='T'}
{  rsep:=separat:=1;
   w[21]:=KINFO.NAROD().KOD;
   w[22]:=form(BO.PLN,,2,' ,'); w[23]:=form(sw_pln,,2,' ,'); w[24]:=form(sm_pln,,2,' ,'); w[25]:=form(BO.PLN+sw_pln-sm_pln,,2,' ,'); ~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{ RAPORT3.ndx_drop();~~}
{FI}
:!UTF-8
{TITLE:Odprawy emerytalne}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('pb','h_rat','vp','rsep','lmt','prn2','prn1','tresc','par','kol','__EME','__PRAC','PRN','PRT');
   par:=exec('eme_par','!ppl_zes_wrem');
   {? ODP_EME.KP=null()
   || FUN.info('Nie wybrano metody wyliczenia podstawy do rezerwy emerytalnej. \nObliczenia nie są możliwe.'@)
   |? ODP_EME.KP().SYSTEM='N'
   || FUN.info('Wybrano niestandardową metodę wyliczenia podstawy do rezerwy emerytalnej. \nObliczenia nie są możliwe.'@)
   |? ODP_EME.KP().KOD='REZ_EKW'
   || {? ODP_EME.KP & ~(O.LT*'sum')
      || FUN.info('W celu obliczenia rezerwy emerytalnej należy wybrać listę płac typu SUM na %1.'@[ODP_EME.DATAW $ 8])
      |? ODP_EME.KP & ~(O.M=ODP_EME.DATAW~2) | ~(O.LT*($(ODP_EME.DATAW~1)+2))
      || FUN.info('Data badania nie jest zgodna z rokiem i miesiącem wybranej listy płac.'@)
      ?}
   ?};
   {? par.OK
   || kol:=obj_new(4);
      tresc:='odp_eme';
      PAR_WYDR.TITLE:='Rezerwy emerytalne na %1r.'@[ODP_EME.DATAB$6];
      __EME:=exec('eme_tab','!ppl_zes_wrem');

      PRN:=obj_new(@.CLASS.PRINT,13);
      PRN.add("form(__EME.NAZWISKO+' '+__EME.PIERWSZE+' '+__EME.DRUGIE)",35,'Nazwisko i imiona'@);
      PRN.add("__EME.PLEC",4,'Płeć'@);
      PRN.add("__EME.UR_DATA$1",10,'Data urodzenia'@);
      PRN.add("__EME.DZA$1",12,'Data zatrudnienia'@);
      PRN.add("form(__EME.WEL,2,0,'9.')+'/'+form(__EME.WEM,-2,0,'9.')",10,'Wiek emerytalny'@,1);
      PRN.add("__EME.DWE$1",10,'Data emerytury'@);
      PRN.add("__EME.LA",9,'Lata przepracowane'@,1,,,,,',0,\'9,\'');
      PRN.add("__EME.LB",9,'Lat do przepracowania'@,1,,
         "{? __EME.LB=0 || '255:0:0,255:255:255' || '0:0:0,255:255:255' ?}",,,',0,\'9,\'');
      PRN.add("__EME.P",7,'Prawdopodobieństwo wypłaty'@,1,,,,,',2,\'9,\'');
      PRN.add("__EME.PODSTAWA",12,'Podstawa odprawy'@,1,,,,,',2,\'9,\'');
      PRN.add("__EME.KWOTA",15,'Wysokość odprawy'@,1,,,,,',2,\'9,\'');
      {? par.BO
      || PRN.add("__EME.ODPISA",15,'Odpis za lata ubiegłe'@,1,,,,'ODPISA',',2,\'9,\'')
      ?};
      PRN.add("__EME.ODPISB",15,'Odpis za rok bieżący'@,1,,,,'ODPISB',',2,\'9,\'');
      PRN.s_frame();
      prn1:='PRN';

      _szer:=-3;
      {! _lp:=1..11 |! _szer+=PRN.LEN[_lp]+3 !};
      PRT:=obj_new(@.CLASS.PRINT,3);
      PRT.add("'             R A Z E M '@",_szer);
      {? par.BO
      || PRT.add("PRN.sum('ODPISA')",PRN.LEN[PRN.nstr-1],'',1,,,,,',2,\'9,\'')
      ?};
      PRT.add("PRN.sum('ODPISB')",PRN.LEN[PRN.nstr],'',1,,,,,',2,\'9,\'');
      PRT.s_frame();
      prn2:='PRT';

      lmt:=rsep:=pb:=0;
      vp:=h_rat:=1;

      __PRAC:=sql('select P.REFERENCE as REF, OSOBA.NAZWISKO, OSOBA.PIERWSZE '
                  'from P join F_ZATR join OSOBA using (P.OSOBA,OSOBA.REFERENCE) '
                  'where P.FIRMA=:_a and F_ZATR.KOD=\'P\' and (P.DZA<=to_date(:_b) and '
                        '(P.DZ<=to_date(:_b) or P.DZ is null)) '
                  'order by 2,3',
                  exec('ref_firma','ustawienia'),ODP_EME.DATAW
              )
   ?}
}
{END:
   {? par.OK
   || exec('pozycje_rezerwy','!ppl_zes_wrem');
      {? __PRAC.first()
      || ODP_EME.WR:={? par.BO || PRN.sum('ODPISA') ?}+PRN.sum('ODPISB')
      ?}
   ?};
   VAR_DEL.delete('pb','h_rat','vp','rsep','lmt','prn2','prn1','tresc','par','kol','__EME','__PRAC','PRN','PRT')
}
{COMMENT}OLD: odp_eme.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: skid_info.rpi{COMMENT-}
{EXIT: ~__PRAC.first()}
{EXIT: ~par.OK}
{EXIT: ODP_EME.KP().KOD='REZ_EKW' & ~(O.LT*'sum')}
{EXIT: ODP_EME.KP().KOD='REZ_EKW' & ~(O.MP=ODP_EME.DATAW~2) | ~(O.LT*($(ODP_EME.DATAW~1)+2))}
{INCLUDE: wsp_pw.rpi}
{COMMENT}---------------------------------------------------------------------------------------------------------------

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

--------------------------------------------------------------------------------------------------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb }
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:={? rep_export() || charleft() || charleft()/h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {
      pb:=1;
      kol[1]:=ceil(lmt*h_rat)+1;
      kol[2]:=kol[1]+32;
      kol[3]:=kol[2]+42;
      kol[4]:=kol[3]+7;
      ~~
   }
   {<{kol[1]}'Parametry sprawozdania'@}
   {IF: rep_export()}
      {<{kol[1]}'Pierwszy odpis na rezerwę: %1'@[{? par.BO || 'TAK'@ || 'NIE'@ ?}]}
      {<{kol[1]}'Zakładany wzrost wynagrodzeń: %1\%'@[form(par.WW,6,2)]}
      {<{kol[1]}'Stopa dyskontowa: %1\%'@[form(ODP_EME.DYSKONTO,6,2)]}
      {<{kol[1]}'Data badania: %1'@[ODP_EME.DATAB$1]}
      {<{kol[1]}'Data ustalania wynagrodzenia: %1'@[ODP_EME.DATAW$1]}
      {<{kol[1]}'Prawdopodobieństwo ciągłości zatrudnienia:'@}
      {<{kol[1]}'od 0 do 5:   %1'@[form(ODP_EME.GRUPA_1,,2)]}
      {<{kol[1]}'od 6 do 10:  %1'@[form(ODP_EME.GRUPA_2,,2)]}
      {<{kol[1]}'od 11 do 15: %1'@[form(ODP_EME.GRUPA_3,,2)]}
      {<{kol[1]}'od 16 do 20: %1'@[form(ODP_EME.GRUPA_4,,2)]}
      {<{kol[1]}'od 21 do 50: %1'@[form(ODP_EME.GRUPA_5,,2)]}
      {<{kol[1]}'Podstawa naliczana według: %1'@[ODP_EME.KP().NAZWA]}
   {ELSE}
      {<{kol[3]-20} 'Prawdopodobieństwo ciągłości zatrudnienia'@}
      {<{kol[1]} 'Pierwszy odpis na rezerwę?'@} {<{kol[2]} {? par.BO || 'TAK'@ || 'NIE'@ ?}
         } {>{kol[3]} 'od 0 do 5:'@} {>{kol[4]} form(ODP_EME.GRUPA_1,,2)}
      {<{kol[1]} 'Zakładany wzrost wynagrodzeń:'@} {>{kol[2]+6} form(par.WW,6,2)+'%'
         } {>{kol[3]} 'od 6 do 10:'@} {>{kol[4]} form(ODP_EME.GRUPA_2,,2)}
      {<{kol[1]} 'Stopa dyskontowa:'@} {>{kol[2]+6} form(ODP_EME.DYSKONTO,6,2)+'%'
         } {>{kol[3]} 'od 11 do 15:'@} {>{kol[4]} form(ODP_EME.GRUPA_3,,2)}
      {<{kol[1]} 'Data badania:'@} {<{kol[2]} ODP_EME.DATAB$1
         } {>{kol[3]} 'od 16 do 20:'@} {>{kol[4]} form(ODP_EME.GRUPA_4,,2)}
      {<{kol[1]} 'Data ustalania wynagrodzenia:'@} {<{kol[2]} ODP_EME.DATAW$1
         } {>{kol[3]} 'od 21 do 50:'@} {>{kol[4]} form(ODP_EME.GRUPA_5,,2)}
      {<{kol[1]}'Podstawa naliczana według:'@} {<{kol[2]} ODP_EME.KP().NAZWA}
   {FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}---------------------------------------------------------------------------------------------------------------

   M A K R O D E F I N I C J E

--------------------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: tresc}
   {IF: exec('eme_1p','!ppl_zes_wrem',__EME,par) }
      {SUBSTITUTE: 'LINIA0'}
   {FI}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'podsum'}
   { rsep:=1;~~ }
   {SUBSTITUTE: 'LINIA02'}
   { prn1:=prn2;~~ }
{MACRO-}
{COMMENT}---------------------------------------------------------------------------------------------------------------

   Z A W A R T O Ś Ć   W Y D R U K U

--------------------------------------------------------------------------------------------------------------{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: __PRAC}{DO}
   {IF: P.seek(__PRAC.REF,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{IF: ~rep_export()}
   {SUBSTITUTE: 'podsum'}
{FI}
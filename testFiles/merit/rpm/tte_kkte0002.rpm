:!UTF-8
{TITLE:2. Cennik surowców pobranych do kalkulacji}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   dalej:=0;
   PRN:=obj_new(@.CLASS.PRINT,7); PRN.s_frame();
   PRNSUM := obj_new(@.CLASS.PRINT,7); PRNSUM.s_frame();
   prn1:='PRN';
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   zaokr:=4;
   sumwart:=0;
   warsb:= VAR.ASB='B'
}
{END:
   &dalej;
   obj_del(PRN);
   obj_del(PRNSUM);
   &prn1;
   &color;
   &sumwart;
   &zaokr;
   &warsb;
   &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
  Wydruk cennika surowcow pobranych do kalkulacji.
  [SS] - 2001/03/19 - [7.41]
  [TS] - 2001/10/09 - [7.53] OWwSK003_Aneks1
  [TS] - 2003/01/16 - [7.62] Data waznosci
  Wywołanie: Karty technologiczne -> Kalkulacje TKW -> Drukuj
{COMMENT-}
{COMMENT}--Definicja wiersza wydruku cennika surowcow pobranych do kalk. KALMAT.--{COMMENT-}
{
   PRN.add("KALMAT.PT().KTM",20,'Kod surowca'@);
   PRN.add("(KALMAT.PT().N)",35,'Opis'@);
   PRN.add("form(KALMAT.PRICE,,2)",15,'Cena'@,1);
   {? warsb=1
   ||
      PRN.add("form(KALMAT.IL,,2)",15,'Ilość'@,1);
      PRN.add("
               _wart:=KALMAT.PRICE;
               _wart*=KALMAT.IL;
               sumwart+=_wart;
               form(_wart,,zaokr)
              ",20,'Wartość'@,1)
   ?};
   PRN.add("{? KALMAT.POCH='H' || 'Wprowadzone ręcznie'@
            |? KALMAT.POCH='S' || 'Stany magazynowe'@
            |? KALMAT.POCH='K' || 'Kalkulacja półfabrykatu'@
            |? KALMAT.POCH='T' || 'Cennik tow. produkcyjnych'@ || '' ?}",13,'Pochodzenie'@);
   PRN.add("{? KALMAT.TKTL<>null || KALMAT.TKTL().NRK+' '+KALMAT.TKTL().WER || '' ?}",20,'Technologia'@);
~~}
{COMMENT}--Definicja wiersza podsumowania cennika surowcow pobranych do kalk. KALMAT.--{COMMENT-}
{COMMENT}warsb=1 dla kalkulacji budzetowej{COMMENT-}
{
   {? warsb=1
   ||
      PRNSUM.add("'RAZEM:'@",94,'');
      PRNSUM.add("form(sumwart,,zaokr)",20,'',1)
   ?};
~~}
{  PAR_WYDR.TITLE:='CENNIK SUROWCÓW POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Nr kalkulacji:'@} {<{lmt+25} form(KKTL.KNR)}
{<{lmt+1} 'Data kalkulacji:'@} {<{lmt+25} form(KKTL.DT)}
{<{lmt+1} 'Data ważności:'@}{<{lmt+25} form(KKTL.DW)}
{<{lmt+1} 'Ceny surowców na dzień:'@} {<{lmt+25} {? KKTL.DT_D=date(0,0,0) || form(KKTL.DT) || form(KKTL.DT_D) ?}}
{<{lmt+1} 'Stan:'@} {<{lmt+25} exec('stan_opis','!tte_tec_ktkw',KKTL.STAN)}
{<{lmt+1} 'Wariant kalkulacji:'@} {<{lmt+25} KKTL.OPC().NAZ}
{<{lmt+1} 'Typ karty:'@}{<{lmt+25} KKTL.NRK().TYP().TYP+' - '+KKTL.NRK().TYP().OPIS}
{<{lmt+1} 'Karta:'@}{<{lmt+25} KKTL.NRK().NRK+' '+'wersja: %1'@[KKTL.NRK().WER]}
                    {<{lmt+25} KKTL.NRK().OPIS}
{<{lmt+1} 'Produkt:'@} {<{lmt+25} KKTL.TKTLW().KTM().KTM+' - '+M.N}
{<{lmt+1} 'Karta kalkulowana na:'@} {<{lmt+25} form(TKTL.XJM,,2)+' '+TKTL.JM().KOD}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ KALMAT.index('KSN'); KALMAT.prefix(KKTL.ref()); ~~}
{prn1:='PRN';~~}
{IF: KALMAT.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
   {SUBSTITUTE: 'LINIA0'}
   { dalej:=KALMAT.next(); ~~}
{OD}
{COMMENT}warsb=1 dla kalkulacji budzetowej{COMMENT-}
{IF: warsb=1}
  {SUBSTITUTE: prn2:='PRNSUM'; 'LINIA02'}
  {prn1:='PRNSUM';~~}
{FI}

:!UTF-8
{TITLE:Wydruk etykiet towarowych - kody kreskowe}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   mkodkm:="VAR_DEL.delete('temp','font','wybierz','dalej','wyjscie','etykieta')";
   mkodkm();
   domys:=var_pres('__domys')>0;
   temp:='';
   font:='';
   wybierz:='';
   dalej:=0;
   VAR.DEBUG:=0;
   etykieta:=PAR_WYDR.URZ_LAB;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=exec('barcode_height','kody_kresk');
   PAR_WYDR.URZ_LAB:=exec('wyb_etykiety','kody_kresk');
   wyjscie:={? PAR_WYDR.TYPDR<>null || PAR_WYDR.URZ_LAB=null ?};
   {? wyjscie=0 & PAR_WYDR.TYPDR=null & PAR_WYDR.KODKRESK=''
   || FUN.info('Brak parametryzacji drukowania kodów kreskowych.'@);
      wyjscie:=1
   ?};
   exec('ini_kom','#message','Wydruk etykiet towarowych - kody kreskowe'@);
   MKODK.cntx_psh
}
{END:
   MKODK.cntx_pop;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   exec('end_kom','#message');
   PAR_WYDR.URZ_LAB:=etykieta;
   mkodkm(); VAR_DEL.delete('mkodkm','domys')
}
{EXIT: wyjscie=1}
{ wyjscie:=1;~~}
{DEFINEFONT: 'courier=TexGyreCursor,,,8'}
{FONT: 'courier'}
{MACRO:'stan'}
   {ENTIRE}
   {MKODK.N}

   {IF: MKODK.KODK<>''}
      { temp:=MKODK.KODK;~~}
      {BARCODE: temp;50;BARCODE_H;BARCODE_TYPE}


      {FONT: 'courier'}{SPACE: 'B'}
      {IF: domys}{MKODK.M().KTM} - {MKODK.M().N}{FI}
      {MKODK.KODK}
   {FI}
  ─────────────────────────────────────
   {ENTIRE-}
{MACRO-}
{MACRO:'etykiety'}
{IF: MKODK.KODK=''}
   { exec('add_kom','#message','Kod: '+MKODK.KTM,7,'Brak kodu kreskowego'@);~~}
{ELSE}
   { wyjscie:=0;~~}
   {IF: PAR_WYDR.TYPDR<>null}
      { wyjscie:=exec('druk','kody_kresk')=0;~~}
   {ELSE}
      {SUBSTITUTE: 'stan'}
   {FI}
{FI}
{MACRO-}
{
_bez:=EANX.WHERE='Q';

{? ~_bez & ~domys
|| _wer:=MKODK.mk_sel('Kody dla materiału: %1'@[M.KTM],,0,'lldddppppswsadg');
   MKODK.win_fld(_wer,,'KTM',,,30,,,'Indeks'@);
   MKODK.win_fld(_wer,,'N',,,30,,,'Nazwa'@);
   MKODK.win_fld(_wer,,'KODK',,,30,,,'Kod kreskowy'@)
|? domys
|| _wer:=MKODK.mk_sel('Kody dla materiału: '+M.KTM,,0,'lldddppppswsadh');
   MKODK.win_fld(_wer,,'M','KTM',,30,,,'Indeks'@);
   MKODK.win_fld(_wer,,'M','N',,30,,,'Nazwa'@);
   MKODK.win_fld(_wer,,'KODK',,,30,,,'Kod kreskowy'@)
|| _wer:=MKODK.mk_sel('Nieprzypisane kody kreskowe'@,,0,'lldddppppswsadf');
   MKODK.win_fld(_wer,,'KODK',,,30,,,'Kod kreskowy'@)
?};
_fb:="
   VAR_DEL.delete('__Sel');
   __Sel:=MKODK.sel_aget; MKODK.sel_adel;
   {? __Sel.first=0
   || __Sel.REF:=#MKODK.ref;
      __Sel.add
   ?};
   sel_exit
";
MKODK.win_act(_wer,,'Formuła','Akceptuj'@,,'Akceptacja wybranych pozycji'@,_fb,,1,1,_fb);

{? _bez & ~domys
|| MKODK.index('BEZ');
   MKODK.prefix(null)
|? domys
|| MKODK.index('OLD');
   MKODK.prefix('T')
|| MKODK.index('M_KK');
   MKODK.prefix(null,M.ref,'N')
?};
MKODK.win_sel(_wer);

{? MKODK.select=0
|| VAR_DEL.delete('__Sel');
   __Sel:=MKODK.sel_aget; MKODK.sel_adel
?};
MKODK.cntx_psh;
MKODK.prefix;
~~}
{FOR: __Sel}
{DO}
   {IF: MKODK.seek(__Sel.REF,)}
      {SUBSTITUTE: 'etykiety'}
   {FI}
{OD}
{
MKODK.cntx_pop;
VAR_DEL.delete('__Sel');
~~}
{EXIT: wyjscie=1}
{EXIT: PAR_WYDR.TYPDR<>null}

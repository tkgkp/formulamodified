:!UTF-8
{BEGIN:
   VAR_DEL.delete('dz','tresc','__param');
   VAR_DEL.delete('suma','total','var','fca','miesiąc','PRN','PRN1','PRN2','Tab');

   tresc:='p_wkartapracykal_rok';
   PAR_WYDR.TITLE:='Roczna karta ewidencji czasu pracy';

   dz:=obj_new(32);
   {! _ii:=1..obj_len(dz) |! dz[_ii]:=obj_new(30) !};

   suma:=obj_new(12+3);
   total:=obj_new(obj_len(suma));
   {! _ii:=1..obj_len(suma) |! total[_ii]:=suma[_ii]:=0 !};
   var:=obj_new(17);

   ' nieobecności wykazywane w dniach kalendarzowych ';
   var[2]:=151;
   var[1]:=exec('uprawnieniawrap','pkd','nieobecnościach'@,'PKD_EZK_OPNN','PKD_EZK_ORNN')+
           exec('uprawnieniawrap','pkd','godzinach pracy'@,'PPL_PLL_PGOD','PPL_PLL_RGOD')+
           exec('uprawnieniawrap','pkd','wzorcach czasu pracy'@,'PKD_EZK_OPKA','PKD_EZK_ORKA');
   P.cntx_psh();
   H.cntx_psh();
   N.cntx_psh();
   R_WZCZ.cntx_psh();
   R_WYK.cntx_psh();
   G.cntx_psh();
   {? +var[1]
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[var[1]])
   || var[8]:=exec('par_wydr_ddp','pkd',
         'Karta czasu pracy'@,
         "  {? cur_tab().ROK<1900
            || FUN.emsg('Wymagane poprawne wypełnienie pola \"Rok\"'@); 'ROK'
            || 1
            ?}
         ",
         ,,,
         ,,,
         ,,,,,
         date()~1,"1"
      );
      {? var[7]:=var[8].size()
      || H.index('_HISTKOD');
         N.index('NIEOBECN');
         R_WZCZ.index('R_WZCZE');
         R_WYK.index('R_WYKDN');
         G.use('godz'+form(var[8].ROK,-4,,'9.'));
         G.index('MSCDKW');
         params_set('P',exec('wybierz_parses','pracownik','PKD').P);

         fca:="_nic:='-----';
            {? _a>date(var[8].ROK,var[8].MC,0)~3 | dz[_a][1]=0
               || _nic
               || _ret:='';
                  {? dz[_a][10] || _ret+='Uw' ?};
                  {? dz[_a][11] || _ret+='Um' ?};
                  {? dz[_a][12] || _ret+='Uc' ?};
                  {? dz[_a][13] || _ret+='Ub' ?};
                  {? dz[_a][14] || _ret+='Uo' ?};
                  {? dz[_a][15] || _ret+='Zn' ?};
                  {? dz[_a][16] || _ret+='Ch' ?};
                  {? dz[_a][17] || _ret+='Op' ?};
                  {? dz[_a][18] || _ret+='Nu' ?};
                  {? dz[_a][19] || _ret+='Nn' ?};
                  {? dz[_a][30] || _ret+='Pr' ?};
                  {? _ret=''
                  || _ret:={? (_w:=dz[_a][5])>0
                           || $_w
                           |? 1+dz[_a][3]='W' | 1+dz[_a][3]='S'
                           || dz[_a][3]
                           || ''
                           ?}
                  ?};
                  _ret
            ?}";

         {? var_press('Tab')>100 || obj_del(Tab) ?};
         Tab:=tab_tmp(1,'LP','INTEGER','Lp');
         {! _ii:=1..12
         |! Tab.LP:=_ii;
             Tab.add()
         !};

         _wDni:=5;
         _wOG:=7;
         _wN:=2;
         {? var_pres('PRN')>0 || obj_del(PRN) ?};
         PRN:=obj_new(@.CLASS.PRINT,41+4);
         PRN.add("miesiąc(var[8].MC)",2,'Mc');
         {! _i:=1..31 |! PRN.add(($('fca('+$_i+')')),_wDni,$_i,,,,,,,1) !};
         PRN.add("suma[12]",_wOG,'PG',1,,,,,',2,\'9.\'');
         PRN.add("suma[1]" ,_wOG,'OG',1,,,,,',2,\'9.\'');
         PRN.add("suma[2]" ,_wN,'Uw',1);
         PRN.add("suma[3]" ,_wN,'Um',1);
         PRN.add("suma[4]" ,_wN,'Uc',1);
         PRN.add("suma[5]" ,_wN,'Ub',1);
         PRN.add("suma[6]" ,_wN,'Uo',1);
         PRN.add("suma[7]" ,_wN,'Zn',1);
         PRN.add("suma[8]" ,_wN,'Ch',1);
         PRN.add("suma[9]" ,_wN,'Op',1);
         PRN.add("suma[10]",_wN,'Nu',1);
         PRN.add("suma[13]",_wN,'Pr',1);
         PRN.add("suma[11]",_wN,'Nn',1);
         PRN.sl_frame();

         {? var_pres('PRN2')>0 || obj_del(PRN2) ?};
         PRN2:=obj_new(@.CLASS.PRINT,4);
         PRN2.add("", 2+1+31*(_wDni+1)-1,'Miesiąc     /     Dzień miesiąca');
         PRN2.add("",              _wOG,'Plan');
         PRN2.add("",              _wOG,'Godziny');
         PRN2.add("",     11*(_wN+1)-1,'Czas nieobecności w pracy w dniach wg przyczyny');
         PRN2.sl_frame();

         {? var_pres('PRN1')>0 || obj_del(PRN1) ?};
         PRN1:=obj_new(@.CLASS.PRINT,12+4);
         PRN1.add("'Razem:'", 2+1+31*(_wDni+1)-1,'');
         PRN1.add("total[12]", _wOG,'',1,,,,,',2,\'9.\'');
         PRN1.add("total[1]" , _wOG,'',1,,,,,',2,\'9.\'');
         PRN1.add("total[2]" , _wN,'',1);
         PRN1.add("total[3]" , _wN,'',1);
         PRN1.add("total[4]" , _wN,'',1);
         PRN1.add("total[5]" , _wN,'',1);
         PRN1.add("total[6]" , _wN,'',1);
         PRN1.add("total[7]" , _wN,'',1);
         PRN1.add("total[8]" , _wN,'',1);
         PRN1.add("total[9]" , _wN,'',1);
         PRN1.add("total[10]", _wN,'',1);
         PRN1.add("total[13]", _wN,'',1);
         PRN1.add("total[11]", _wN,'',1);
         PRN1.sl_frame();

         prn1:='PRN';
         prn2:='PRN1';
         lmt:=rsep:=0;
         vp:=1;
         htcol:=36;
         span:='1;31;1;3';

         miesiąc:="
            {? _a=1  ||' 1'
            |? _a=2  ||' 2'
            |? _a=3  ||' 3'
            |? _a=4  ||' 4'
            |? _a=5  ||' 5'
            |? _a=6  ||' 6'
            |? _a=7  ||' 7'
            |? _a=8  ||' 8'
            |? _a=9  ||' 9'
            |? _a=10 ||'10'
            |? _a=11 ||'11'
            |? _a=12 ||'12'
            ?}"
      ?}
   ?}
}
{END:
   G.cntx_pop();
   R_WYK.cntx_pop();
   R_WZCZ.cntx_pop();
   N.cntx_pop();
   H.cntx_pop();
   P.cntx_pop();
   VAR_DEL.delete('dz','tresc','__param');
   VAR_DEL.delete('suma','total','var','fca','miesiąc','PRN','PRN1','PRN2','Tab')
}
{EXIT: +var[1]}
{EXIT: ~var[7]}
{INCLUDE: wsp_pw.rpi}
{COMMENT}-------- fragment pliku skid_pw4.rpi MACRO-TABHEAD2 -------{COMMENT-}
{MACRO: 'TABHEAD2'}
{ _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn2+'.ini_head()'; ($(_f))()}
{REP: _f:=prn2+'.fhbar(lmt)'; ($(_f))()}
{WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn1+'.ini_head()'; ($(_f))()}
{REP: _f:=prn2+'.fjbar( ($prn1)(),lmt)'; ($(_f))()}
{WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{FONT:'T10B'}{SPACE: 'B'}
{CENTER}{<1>{charleft()}'E W I D E N C J A   R O C Z N A   C Z A S U   P R A C Y ('+$var[8].ROK+')'}
{<{ceil(lmt)+20}}
{<{ceil(lmt)+20}}
{FONT: 'T8B'}
{SPACE: 'B'}
{CENTER}{<1>{charleft()}'(wg kartoteki godzin/kalendarzy)'}
{<{ceil(lmt)+1}P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE}   (Nr teczki {|P.T}),   Jednostka org. {P.WYDZIAL().SYMBOL}
{FONT: 'T8GB'}
{SPACE: 'C'}
{SUBSTITUTE: rsep:=1; vp:=1; prn1:='PRN'; prn2:='PRN2'; 'TABHEAD2'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8GB'}
{SPACE: 'C'}
{REP: vp:=1; _f:='PRN.fjbar(PRN1,lmt)'; ($(_f))()}
{SUBSTITUTE: rsep:=0; vp:=0; prn1:='PRN1'; 'LINIA0'}
{SUBSTITUTE: rsep:=0; vp:=0; prn1:='PRN1'; 'LINIAF'}

    Legenda :
             PG - planowana liczba godzin    OG - liczba godzin ogółem    Uw - urlop wypoczynkowy    Um - urlop macierzyński     Uc - urlop wychowawczy    Ub - urlop bezpłatny    Uo - urlop okolicznościowy
             Zn - zwolnienie niepłatne    Ch - choroba pracownika    Op - opieka nad dzieckiem   Nu - nieobecność usprawiedliwiona    Nn - nieobecność nieusprawiedliwiona    Pr - przestój
             R  - roboczy     RN - roboczy w niedzielę      RS - roboczy w święto      WH - harmonogramowo wolny     W5 - wolny z tytułu pięciodniowego tyg. pracy
             WN - wolny za niedzielę      WS - wolny za święto    SN - święto niedzielne     SW - święto w inny dzień niż niedziela
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
   {IF: P.DZA<=date(var[8].ROK,12,0) & (P.DZ>=date(var[8].ROK,1,1) | P.DZ=date(0,0,0))}
      {FOR: Tab}{DO}
            {var[8].MC:=Tab.LP;~~}
            {SUBSTITUTE: 'miesiac'}
      {OD}
      {PAGE}
      { {! _ii:=1..obj_len(suma) |! total[_ii]:=0 !};~~}
   {FI}
{MACRO-}
{MACRO: 'miesiac'}
{IF: P.DZA<=date(var[8].ROK,var[8].MC,0) & (P.DZ>=date(var[8].ROK,var[8].MC,1) | P.DZ=date(0,0,0))}
   { exec('oblicz_godz','wkartp','rok');

    suma[1]:=dz[obj_len(dz)][5];
    suma[2]:=dz[obj_len(dz)][10];
    suma[3]:=dz[obj_len(dz)][11];
    suma[4]:=dz[obj_len(dz)][12];
    suma[5]:=dz[obj_len(dz)][13];
    suma[6]:=dz[obj_len(dz)][14];
    suma[7]:=dz[obj_len(dz)][15];
    suma[8]:=dz[obj_len(dz)][16];
    suma[9]:=dz[obj_len(dz)][17];
    suma[10]:=dz[obj_len(dz)][18];
    suma[11]:=dz[obj_len(dz)][19];
    suma[12]:=dz[obj_len(dz)][27];
    suma[13]:=dz[obj_len(dz)][30];

    {! _ii:=1..obj_len(suma) |! total[_ii]+=suma[_ii] !};
    ~~
   }
   {SUBSTITUTE:  rsep:=1; prn1:='PRN'; 'LINIA0'}
{FI}
{MACRO-}
{MACRO: 'podsum'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft-PRN.width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
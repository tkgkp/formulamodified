:!UTF-8
{TITLE: Plan pracy lub Grafik - export do Excela}
{PRINTER: ,1,0,0}
{BEGIN:
   _cfg:=obj_new('exit','nazpli','myKatal','sep1','sep2','save','conv','plik','ilupra','pg','dod','ddo','all','line','dbad');
   _cfg.exit:=1;
   _cfg.nazpli:='';

   {? exec('interm','#system') 
   || _cfg.exit:=2;
      params_set('cfg',_cfg); 
      return() 
   ?};

   VAR_DEL.delete('tresc');
   P.cntx_psh();
   H.cntx_psh();
   KAL_BUFF.cntx_psh();
   KAL_BUFF.index('PRZNODT');
   H.index('_HISTKOD');
   exec('KAL_cpsh','grafik');
   N.cntx_psh();
   N.index('NIEOBECN');

   _PRAC:=exec('tab_prac','!prc_czp_wydr');
   {? _PRAC.first()
   || tresc:='rcp_plgr';
'Zapis utworzonego pliku jako XLS. Po wypelnieniu pliku danymi nalezy zapisac go jako CSV';

      _cfg.myKatal:=exec('filechooser','#file','Katalog do zapisu szablonów'@,0,,,,'DIRECTORIES_ONLY',0,exec('get','#profile',,'Harmonogram','katalog do zapisu'));
      {? +_cfg.myKatal
      || exec('set','#profile',,'Harmonogram','katalog do zapisu',_cfg.myKatal);
         _cfg.myKatal:='@'+_cfg.myKatal+'\\';
         _cfg.sep1:=(%9);
         _cfg.sep2:='';
         _cfg.save:=
            "  fwrite(_a,_b)
            ";
         _cfg.conv:=
            "  {? _a='NN' | _a='NU' || 'XX' || _a ?}
            ";
         _cfg.plik:='';
         _cfg.ilupra:=0;

         _TAB:=tab_tmp(,
            'A','STRING[3]','ETYK',
            'B','STRING[3]',,
            'C','STRING[3]',,
            'D','DATE','Data do',
            'I','INTEGER',,
            'F','STRING[3]',,
            'G','STRING[3]',,
            'M','STRING[3]',,
            'K','STRING[30]',,
            'L','STRING[30]',,
            'N','STRING[3]',,
            'O','DATE','Data od',
            'Q','STRING[3]',,
            'R','STRING[3]',,
            'ALL','INTEGER',
         );

'Wartości początkowe';
         _TAB.O:=date(,,1);
         _TAB.D:=date(,,0);
         _TAB.R='N';
         _TAB.ALL=0;

'Definicja okna';
         _TAB.win_edit(_edit:=_TAB.mk_edit('Parametry eksportu'@,0,'id20121111'));
         _TAB.win_esep(_edit,'Dane podstawowe'@);
         _TAB.win_efld(_edit,,'O',,,10,,,'Data od'@,,'Data początkowa'@);
         _TAB.win_efld(_edit,,'D',,,10,,,'Data do'@,,'Data końcowa'@);
         _TAB.win_efld(_edit,,'R',,,,,,'Rodzaj danych'@,,
            'Rodzaj danych','radio-buttons',,'Plan'@,"'T'",'Grafik'@,"'N'"
         );
         _TAB.win_efld(_edit,,'ALL',,,,,,'Zakres'@,,'Wświetleni współpracowników','radio-buttons',,
            'Tylko współpracownicy uwzględnieni w planowaniu'@,"0",
            'Wszyscy współpracownicy'@,"1"
         );
         _TAB.efld_opt(_edit,'mark=1',,'O');
         _TAB.efld_opt(_edit,'mark=1',,'D');

'Przyciski w oknie';
         exec('ok_esc','#window',_TAB,_edit);

         _cfg.exit:=~_TAB.edit(
            "  _result:=__CHK.record(cur_tab(),,'O','D');
               {? _result=''
               || {? cur_tab().O>cur_tab().D
                  || FUN.emsg('Wartość w polu \"Data od\" nie może być większa od wartości w polu \"Data do\".'@);
                     _result:='O'
                  |? cur_tab().D-cur_tab().O>242
                  || FUN.emsg('%1 %2'['Za duży zakres dat.'@,'Maksymalnie do 242 dni.'@]);
                     _result:='O'
                  ?}
               ?};
               _result
            ");
         _cfg.pg:={?_TAB.R='N'||'G'||'P'?};
         _cfg.dod:=_TAB.O;
         _cfg.ddo:=_TAB.D;
         _cfg.all:=_TAB.ALL;
         _cfg.nazpli:=STR.gsub(_cfg.dod$1,'/','')+'_'+STR.gsub(_cfg.ddo$1,'/','')
      ?}
   ?};
   params_set('P',_PRAC,'cfg',_cfg)
}
{END:
   {? ~exec('interm','#system') 
   || ferase('@!TMP\\'+params_get().cfg.nazpli+'.xls');
      ferase('@!TMP\\ustaw.txt');
      ferase('@!TMP\\dane.txt');
      ferase('@!TMP\\2harmgr2.vbs');
      VAR_DEL.delete('tresc');
      KAL_BUFF.cntx_pop();
      H.cntx_pop();
      P.cntx_pop();
      N.cntx_pop();
      exec('KAL_cpop','grafik')  
   ?}
}
{EXIT: {? params_get().cfg.exit=2 
       || FUN.info('Funkcjonalność niedostępna dla klienta inTerm.'@);1
       |? params_get().cfg.exit 
       || FUN.info('Anulowano tworzenie pliku harmonogramu.'@);
          1 
       || 0 
       ?}}
{MACRO: tresc}
{  _cfg:=params_get().cfg;
   _praca:=_uw:=_ub:=_op:=_uo:=_um:=_uc:=_ch:=_xx:=0;
   {? P.DZA<_cfg.ddo & (P.DZ=#0 | _cfg.dod<=P.DZ)
   || _pracow:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE;
      echo(_pracow);
      _cfg.line:=$P.IP+_cfg.sep1;
      _cfg.line+=_pracow+_cfg.sep1;
      H.index('_HISTKOD');
      H.prefix(P.ref(),'Z');
      _wyd:=
         {? H.find_le(_cfg.dod) || H.WYDZIAL().SYMBOL
         |? H.find_le(P.DZA) || H.WYDZIAL().SYMBOL
         || ''
         ?};
      _cfg.line+=_wyd+_cfg.sep1;
      _nad:=exec('prac_nad','stanprac',P.ref(),0);
      _przel:={? _nad.first() || _nad.NAZWISKO+' '+_nad.IMIE || '' ?};
      obj_del(_nad);
      _cfg.line+=_przel+_cfg.sep1;

      _cfg.dbad:=_cfg.dod;
      {!
      |? _cfg.dbad<=_cfg.ddo
      |! _war:='';
         _poz:=0;
         KAL_BUFF.prefix(P.ref(),'P','T',_cfg.dbad);
         {? KAL_BUFF.first()
         || {? KAL_BUFF.NB<>'' & _cfg.pg='G'
            || _war:={? KAL_BUFF.NB='W' || 'UC'
                     |? KAL_BUFF.NB='O' || 'XX'
                     |? KAL_BUFF.NB='B' || 'UB'
                     || _cfg.conv(exec('nieo_typ','grafik',#KAL_BUFF.NB))
                     ?}
            || _war:={? KAL_BUFF.CZAS<>*0
                     || form(KAL_BUFF.POCZATEK)+' - '+form(KAL_BUFF.KONIEC)
                     || ''
                     ?}
            ?};
            _poz:=*(KAL_BUFF.CZAS)/60$2
         ?};
         {? _cfg.pg='G'
         || KAL_BUFF.prefix(P.ref(),'G','T',_cfg.dbad);
            {? KAL_BUFF.first()
            || {? KAL_BUFF.NB=''
               || _war:={? KAL_BUFF.CZAS<>*0
                        || form(KAL_BUFF.POCZATEK)+' - '+form(KAL_BUFF.KONIEC)
                        || ''
                        ?}
               || _war:={? KAL_BUFF.NB='W' || 'UC'
                        |? KAL_BUFF.NB='O' || 'XX'
                        |? KAL_BUFF.NB='B' || 'UB'
                        || _cfg.conv(exec('nieo_typ','grafik',#KAL_BUFF.NB))
                        ?}
               ?};
               _poz:=*(KAL_BUFF.CZAS)/60$2
             ?}
         ?};
         _cfg.line+=_war+_cfg.sep1;
         {? _cfg.pg='G'
         || {? _war='UW' || _uw+=_poz
            |? _war='UB' || _ub+=_poz
            |? _war='UM' || _um+=_poz
            |? _war='OP' || _op+=_poz
            |? _war='UO' || _uo+=_poz
            |? _war='UC' || _uc+=_poz
            |? _war='CH' || _ch+=_poz
            |? _war='XX' || _xx+=_poz
            || _praca+=_poz
            ?}
         || _praca+=_poz
         ?};
         _cfg.dbad+=1
      !};

'tutaj są formuły sumujące';
      _cfg.line+=$form_sys(_praca,0)+_cfg.sep1; 'suma godz w miesiącu';
      {? _cfg.pg='G'
      || _cfg.line+=form(_uw,,,'9.')+_cfg.sep1;
         _cfg.line+=form(_ub,,,'9.')+_cfg.sep1;
         _cfg.line+=form(_um,,,'9.')+_cfg.sep1;
         _cfg.line+=form(_op,,,'9.')+_cfg.sep1;
         _cfg.line+=form(_uo,,,'9.')+_cfg.sep1;
         _cfg.line+=form(_uc,,,'9.')+_cfg.sep1;
         _cfg.line+=form(_ch,,,'9.')+_cfg.sep1;
         _cfg.line+=form(_xx,,,'9.')+_cfg.sep1
      ?};
      {? _praca>0 | _cfg.all
      || _cfg.ilupra+=1;
         _cfg.save(_cfg.plik,_cfg.line+_cfg.sep2)
      ?}
   ?};
   echo(); ~~
}
{MACRO-}
{  _cfg:=params_get().cfg;
   _liczp:=1;
   {!
   |? _liczp<=999 & fexists(_cfg.myKatal+_cfg.nazpli+'_'+(('000'+$_liczp)+3)+'.xls',0)
   |! _liczp+=1
   !};
   {? _liczp>999
   || FUN.emsg('%1\n%2'
         [  'Zbyt dużo plików zaczynajacych się od: %1.'@[_cfg.nazpli],
            'Tworzenie pliku harmonogramu zostanie przerwane.'@
         ]
      );
      return()
   ?};
   _cfg.nazpli:=_cfg.nazpli+'_'+(('000'+$_liczp)+3);
   {? ~(_cfg.plik:=fopen(_cfg.myKatal+_cfg.nazpli+'.xls','w',0))
   || FUN.emsg('%1\n%2'
         [  'Nie udało się otworzyć pliku: %1.'@[_cfg.nazpli],
            'Tworzenie pliku harmonogramu zostanie przerwane.'@
         ]
      );
      return()
   ?};
   fclose(_cfg.plik);

   {? _cfg.plik:=fopen('@!TMP\\ustaw.txt','Uw',0)
   || _cfg.save(_cfg.plik,'dane.txt'+'\r\n'+'dane'+'\r\n'+_cfg.nazpli+'.xls');
      fclose(_cfg.plik)
   || FUN.emsg('%1\n%2'
         [  'Zakładanie tymczasowego pliku wymiany zakończone niepowodzeniem.'@,
            'Tworzenie pliku harmonogramu zostanie przerwane.'@
         ]
      );
      return()
   ?};

   {? ~(_cfg.plik:=fopen('@!TMP\\dane.txt','Uw',0))
   || FUN.emsg('%1\n%2'
         [  'Zakładanie tymczasowego pliku wymiany zakończone niepowodzeniem.'@,
            'Tworzenie pliku harmonogramu zostanie przerwane.'@
         ]
      );
      return()
   ?};

'Wiersze nagłówkowe';
   _cfg.line:=_cfg.sep1+{? _cfg.pg='P' || 'PLAN PRACY'@ || 'GRAFIK'@ ?}+_cfg.sep1;
   _cfg.line+=' '+'za okres'@+' '+_cfg.dod$4+' - '+_cfg.ddo$4+_cfg.sep2;
   _cfg.save(_cfg.plik,_cfg.line);
   _cfg.line:=_cfg.sep2;
   _cfg.save(_cfg.plik,_cfg.line);

'Legenda';
   {? _cfg.pg='G'
   || _cfg.line:='Legenda:'+_cfg.sep1+'UW - u.płatny'+_cfg.sep1+'UB - u.bezpłatny'+_cfg.sep1+'UM - u.mac.'+_cfg.sep2;
      _cfg.save(_cfg.plik,_cfg.line);
      _cfg.line:=_cfg.sep1+'OP - opieka'+_cfg.sep1+'UO - u.okol.'+_cfg.sep1+'UC - u.wych.'+_cfg.sep2;
      _cfg.save(_cfg.plik,_cfg.line);
      _cfg.line:=_cfg.sep1+'CH - zwol.'+_cfg.sep1+'XX - inne'+(2*_cfg.sep1)
   ||
     _cfg.save(_cfg.plik,_cfg.line);
     _cfg.save(_cfg.plik,_cfg.line);
     _cfg.line:=4*_cfg.sep1
   ?};

'Wiersz opisu kolumn';
   {! _rok:=(_cfg.dod~1)..(_cfg.ddo~1)
   |! KAL_ROK.index('KAL_NAZ');
      {? KAL_ROK.find_key(exec('ref_firma','ustawienia'),'standard','standard',_rok)
      || KAL_DEF.index('KAL_DEF');
         KAL_DEF.prefix(KAL_ROK.ref());
         {? KAL_DEF.first()
         || {!
            |? {? _cfg.dod<=KAL_DEF.DATA & KAL_DEF.DATA<=_cfg.ddo
               || _dtyg:=(((KAL_DEF.DATA$7)*','-1)+(KAL_DEF.DATA$7));
                  _cfg.line+={? 'sobota,niedziela'*KAL_DEF.OPIS=0 || 'święto'@ || '' ?};
                  _cfg.line+=_cfg.sep1
               ?};
               KAL_DEF.next()
            !}
         ?}
      || FUN.emsg('%1\n%2'
            [  'Brak kalendarza Standard na rok: %1.'@[$_rok],
               'Tworzenie pliku harmonogramu zostanie przerwane.'@
            ]
         );
         return()
      ?}
   !};
   _cfg.line+=_cfg.sep2;
   _cfg.save(_cfg.plik,_cfg.line);

'Wiersz kalendarza';
   _cfg.line:=4*_cfg.sep1;
   {! _rok:=(_cfg.dod~1)..(_cfg.ddo~1)
   |! KAL_ROK.index('KAL_NAZ');
      {? KAL_ROK.find_key(exec('ref_firma','ustawienia'),'standard','standard',_rok)
      || KAL_DEF.index('KAL_DEF');
         KAL_DEF.prefix(KAL_ROK.ref());
         {? KAL_DEF.first()
         || {!
            |? {? _cfg.dod<=KAL_DEF.DATA & KAL_DEF.DATA<=_cfg.ddo
               || _dtyg:=(((KAL_DEF.DATA$7)*','-1)+(KAL_DEF.DATA$7));
                  _cfg.line+=_dtyg;
                  _cfg.line+=_cfg.sep1
               ?};
               KAL_DEF.next()
            !}
         ?}
      || FUN.emsg('%1\n%2'
            [  'Brak kalendarza Standard na rok: %1.'@[$_rok],
               'Tworzenie pliku harmonogramu zostanie przerwane.'@
            ]
         );
         return()
      ?}
   !};
   _cfg.line+=_cfg.sep2;
   _cfg.save(_cfg.plik,_cfg.line);

'Opis kolumn zestawienia';
   _cfg.line:='ID'@+_cfg.sep1;
   _cfg.line+='Pracownik'@+_cfg.sep1;
   _cfg.line+='Wydział'@+_cfg.sep1;
   _cfg.line+='Przełożony'@+_cfg.sep1;
   {! _rok:=(_cfg.dod~1)..(_cfg.ddo~1)
   |! KAL_ROK.index('KAL_NAZ');
      {? KAL_ROK.find_key(exec('ref_firma','ustawienia'),'standard','standard',_rok)
      || KAL_DEF.index('KAL_DEF');
         KAL_DEF.prefix(KAL_ROK.ref());
         {? KAL_DEF.first()
         || {!
            |? {? _cfg.dod<=KAL_DEF.DATA & KAL_DEF.DATA<=_cfg.ddo
               || _dtyg:=(((KAL_DEF.DATA$7)*','-1)+(KAL_DEF.DATA$7));
                  _cfg.line+=(KAL_DEF.DATA$4);
                  _cfg.line+=_cfg.sep1
               ?};
               KAL_DEF.next()
            !}
         ?}
      ?}
   !};
   _cfg.line+='Suma godzin'@+_cfg.sep1;
   {? _cfg.pg='G'
   || _cfg.line+='UW'+_cfg.sep1+'UB'+_cfg.sep1+'UM'+_cfg.sep1+'OP'+_cfg.sep1+'UO'+_cfg.sep1+'UC'+_cfg.sep1+'CH';
      _cfg.line+=_cfg.sep1+'XX'+_cfg.sep1
   ?};
   _cfg.save(_cfg.plik,_cfg.line+_cfg.sep2); ~~
}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
   {SUBSTITUTE: tresc}
   {FI}
{OD}
{  _cfg:=params_get().cfg;
   {? _cfg.plik || fclose(_cfg.plik) ?};
   _dirvbs:=pth_dir('2harmgr2.xls')+'/'+'2harmgr2.vbs';
   {? fexists(_dirvbs)<>1 | ~fcopy(_dirvbs, '@!TMP\\2harmgr2.vbs',,,1)
   || FUN.emsg('%1\n%2'['Brak pliku "2harmgr2.vbs".'@,'Utworzenie sprawozdania zakończone niepowodzeniem.'@])
   |? _cfg.ilupra
   || {? system('@wscript.exe "'+tmp_dir()+'\\2harmgr2.vbs"',1)
      || FUN.emsg('Błąd podczas tworzenia pliku harmonogramu.'@)
      || {? fcopy('@!TMP\\'+_cfg.nazpli+'.xls',_cfg.myKatal+_cfg.nazpli+'.xls',0,0,1)
         || cli_exec((1-_cfg.myKatal)+_cfg.nazpli+'.xls')
         ?}
      ?}
   || FUN.info('Brak pracowników spełniających kryteria.'@)
   ?}; ~~
}
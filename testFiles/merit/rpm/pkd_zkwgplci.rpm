:!UTF-8
{TITLE:Zatrudnienie wg płci}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PAR_WYDR.TITLE:='Zatrudnienie wg płci'@;
   VAR_DEL.delete('par','prn1','vp','lmt','rsep');

   par:=obj_new(6);
   par[5]:=1;
   par[6]:=exec('par_wydr_ddp','pkd',
      PAR_WYDR.TITLE,
      "  {? cur_tab().OD=#0
         || FUN.emsg('Proszę podać datę kontroli'@); 0
         || 1
         ?}
      ",
      date(),'Data kontroli'@,"1",,,,
      'Rodzaj analizowanej informacji.'@,'Wymiar zatrudnienia'@,'Liczba angaży'@,,,,,,,
      'Drukowanie','Dane dla jednostek organizacyjnych',1
   );
   par[4]:=0;

   {? par[6].size()
   || _val:={? par[6].ASK || '1' || 'H.RWY' ?};
      _lst:=sql('
         select
            UD_SKL.SYMBOL || \' \' || UD_SKL.OPIS WYDZIAL, OSOBA.PLEC, '+_val+' LICZBA
         from
            P join
            H using(P.REFERENCE,H.P) join
            UD_SKL using(H.WYDZIAL,UD_SKL.REFERENCE) join
            OSOBA join
            H_ZM
         where
            P.REFERENCE in (select :_a.SQL from :_a) and H_ZM.KZ=\'Z\' and
            H.OD<=to_date(:_b) and (H.DO is null or to_date(:_b)<=H.DO)',
         exec('wybierz_parses','pracownik','PKD').P,par[6].OD
      );
      {? type_of(_lst)=type_of(SYSLOG)
      || exec('object','#pivot');
         par[3]:=obj_new(@.CLASS.PIVOT,_lst,'PLEC','sum','LICZBA').grpfld('WYDZIAL').total(1).dosum(1);
         {? par[3].make().valid()
         || _len:=10+(~par[6].ASK)*5;
            _fmt:={? par[6].ASK || ',,\'99\'' || ',3,\'.,\'' ?};
            par[1]:=obj_new(@.CLASS.PRINT,par[3].fld_num());
            par[1].add("par[3].OUT.WYDZIAL",60,'Jednostka organizacyjna'@);
            {! _ii:=3..par[3].fld_num()-1
            |! par[1].add($('par[3].fld_val('+$_ii+')'),_len,par[3].fld_name(_ii),1,,,,,_fmt)
            !};
            {? ~rep_export()
            || par[1].add("par[3].OUT.PQ_TOTAL",_len,'Razem'@,1,,,,,_fmt)
            ?};

            par[2]:=obj_new(@.CLASS.PRINT,par[3].sfld_num());
            {? ~rep_export()
            || par[2].add("'Razem'@",60,'Razem'@);
               {! _ii:=2..par[3].sfld_num()-1
               |! par[2].add($('par[3].sfld_val('+$_ii+')'),_len,par[3].fld_name(_ii+1),1,,,,,_fmt)
               !};
               par[2].add("par[3].SUM.PQ_TOTAL",_len,'Razem'@,1,,,,,_fmt);
               par[2].s_frame()
            ?};
            par[1].s_frame();

            par[4]:=1
         ?}
      ?}
   ?};

   prn1:='par[1]';
   lmt:=rsep:=0;
   vp:=1
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep')
}
{EXIT: ~par[4]}
{COMMENT}OLD: zk_ztpl.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   { par[5]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Data kontroli:'@} {par[6].OD}
   {<{lmt+1}'Rodzaj wydruku:'@} {{? par[6].BOX || 'wykaz i suma'@ || 'tylko podsumowanie'@ ?}}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{  par[1].setcolor(color);
   par[1].ini_head()
}
{REP: par[1].fhbar(lmt)}
{WHILE: par[1].h_next()}{DO}
{REP: par[1].h_fmline(lmt)}
{OD}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{IF: par[6].BOX}
   {FOR: par[3].OUT}{DO}
      {par[1].ini_line()}
      {WHILE: par[1].next()}{DO}
         {REP: par[1].fline(lmt)}
      {OD}
   {OD}
{FI}
{IF: par[3].SUM.first()}
   {FOOTER: 0}
   {REP: par[2].flbar(lmt)}
   {FOOTER-}
   {IF: par[6].BOX}
      {IF: lineleft()>2}
         {REP: par[1].fbar(lmt)}
      {ELSE}
         {PAGE}
      {FI}
   {FI}
   { par[2].ini_line() }
   {WHILE: par[2].next()}{DO}
      {REP: par[2].fline(lmt)}
   {OD}
{FI}
:!UTF-8
{TITLE:3. Wydruk etykiet towarowych - kody kreskowe}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   materean:="VAR_DEL.delete('temp','font','wybierz','dalej','wyjscie','etykieta')";
   materean();
   temp:='';
   font:='';
   wybierz:='';
   dalej:=0;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=exec('barcode_height','kody_kresk');
   "ZL.win_patt('SZUK')";
   etykieta:=PAR_WYDR.URZ_LAB;
   PAR_WYDR.URZ_LAB:=exec('wyb_etykiety','kody_kresk');
   wyjscie:={? PAR_WYDR.TYPDR<>null || PAR_WYDR.URZ_LAB=null ?};
   {? wyjscie=0 & PAR_WYDR.TYPDR=null & PAR_WYDR.KODKRESK=''
   || FUN.info('Brak parametryzacji drukowania kodów kreskowych.'@);
      wyjscie:=1
   ?};
   exec('ini_kom','#message','Wydruk etykiet towarowych - kody kreskowe'@);
   ZL.cntx_psh
}
{END:
   ZL.cntx_pop;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   exec('end_kom','#message');
   PAR_WYDR.URZ_LAB:=etykieta;
   materean(); VAR_DEL.delete('materean')
}
{COMMENT}
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Drukuj
{COMMENT-}
{EXIT: wyjscie=1}
{ wyjscie:=1;~~}
{DEFINEFONT: 'courier=TexGyreCursor,,,8'}
{FONT: 'courier'}
{MACRO:'stan'}
   {ENTIRE}
   {_op:=exec('formopis','kody_kresk','Z',$ZL.ref(),0); _bop:=_op[1]; obj_del(_op); _bop}

   {IF: ZL.SCEAN<>''}
     {BARCODE: ZL.SCEAN;50;BARCODE_H;BARCODE_TYPE}


     {SPACE: 'B'}
     {ZL.SCEAN}
   {FI}
   {IF: ZL.ZLEAN<>'' & ZL.ZLEAN<>ZL.SCEAN}
     {BARCODE: ZL.ZLEAN;50;BARCODE_H;BARCODE_TYPE}


     {FONT: 'courier'}{SPACE: 'B'}
     {'KOD KRESKOWY ZLECENIA: %1'@[ZL.ZLEAN]}
   {FI}
  ─────────────────────────────────────
   {ENTIRE-}
{MACRO-}
{MACRO:'etykiety'}
{IF: ZL.SCEAN='' & ZL.ZLEAN=''}
   { exec('add_kom','#message','Zlecenie: %1'@[ZL.SYM],7,'Brak kodu kreskowego'@);~~}
{ELSE}
   { wyjscie:=0;~~}
   {IF: PAR_WYDR.TYPDR<>null}
      { wyjscie:=exec('druk','kody_kresk')=0;~~}
   {ELSE}
      {SUBSTITUTE: 'stan'}
   {FI}
{FI}
{MACRO-}
{EXIT: wybierz:=FUN.choice('Wydruk etykiet dla zleceń'@,,'Bieżącego'@,'Wszystkich'@,'W&ybranych'@,'Wg w&zorca'@);
 {? wybierz <> 0
 || {? wybierz=1
    || wybierz:='B'
    |? wybierz=2
    || wybierz:='W'
    |? wybierz=3
    || wybierz:='Y'
    |? wybierz=4
    || wybierz:='Z'
    ?};0
  || "# koniec #"; 1
 ?}
}
{IF:wybierz='B'}
   {SUBSTITUTE: 'etykiety'}
{ELIF:wybierz='Y'}
   {
   _wer:=ZL.mk_sel('Zlecenia'@,,0,'zlecfddppsdsadf');
   ZL.win_fld(_wer,,'SYM',,,20,,,'Symbol'@);
   ZL.win_fld(_wer,,'OPIS',,,20,,,'Opis'@);
   ZL.win_fld(_wer,,'SCEAN',,,20,,,'Kod kreskowy'@);
   ZL.win_fld(_wer,,'SCEAN',,,20,,,'Kod dla surowców'@);
   _fbg:="
      VAR_DEL.delete('__Sel');
      __Sel:=ZL.sel_aget; ZL.sel_adel;
      {? __Sel.first=0
      || __Sel.REF:=#ZL.ref;
         __Sel.add
      ?};
      sel_exit
   ";
   ZL.win_act(_wer,,'Formuła','Akceptuj'@@,,'Akceptacja wybranych pozycji'@,_fbg,,1,1,_fbg);

   ZL.win_sel(_wer);

   {? ZL.select=0
   || VAR_DEL.delete('__Sel');
      __Sel:=ZL.sel_aget; ZL.sel_adel
   ?};
   ZL.cntx_psh;
   ZL.prefix;
   ~~}
   {FOR: __Sel}
   {DO}
      {IF: ZL.seek(__Sel.REF,)}
         {SUBSTITUTE: 'etykiety'}
      {FI}
   {OD}
   {
   ZL.cntx_pop;
   VAR_DEL.delete('__Sel');
   ~~}
{ELIF:wybierz='W'}
   {FOR: 'ZL'}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{ELIF:wybierz='Z'}
   {FOR: 'ZL'}
   {FILTER}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{FI}
{EXIT: {? wybierz='W' | wybierz='Z' || {? wyjscie=1 || FUN.info('Brak danych spełniających kryteria.'@); 1 ?} ?}}
{EXIT: wyjscie=1}
{EXIT: PAR_WYDR.TYPDR<>null}

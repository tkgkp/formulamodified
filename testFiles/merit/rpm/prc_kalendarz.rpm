:!UTF-8
{TITLE: Kalendarz}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('Rok');
   Rok:=0
}
{END:
   VAR_DEL.delete('Rok')
}
{HEADER}
 {>90 '%1 dnia %2 strona %3'@[KST.MIASTO,date()$4,$page()]}
 {<1 STR.split(KST.NAZWA); STR.line(90)}
 {WHILE: STR.next()}{DO}{<1>92 STR.line(90)}{OD}
 {<1>92'W Y D R U K   K A L E N D A R Z A'@}
 ┌{25*'─'}┬{5*'─'}┬{19*'─'}┬{37*'─'}┐
 │ {form('Dzień'@,24)}{'│ '+'Typ'@+' │   '+'Od'@+'    '+'Do'@+'   '+'Czas'@+' │ '+'Opis'@}{>92'│'}
 ├{25*'─'}┴{5*'─'}┴{19*'─'}┴{37*'─'}┤
 { '│ Kalendarz: \''+KAL_NAZW.NAZWA+'\' za okres '+
   (VAR_EDIT.D_OD$4)+' - '+(VAR_EDIT.D_DO$4) }{>92'│'}
 ├{25*'─'}┬{5*'─'}┬{19*'─'}┬{37*'─'}┤
{HEADER-}
{FOOTER: 0}
 └{25*'─'}┴{5*'─'}┴{19*'─'}┴{37*'─'}┘
{FOOTER-}
{EXIT:   VAR_EDIT.win_edit('ZAKR_DAT');
         VAR_EDIT.D_OD:=date(,,1);
         VAR_EDIT.D_DO:=date(,,0);
         VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_OD');
         VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_DO');
         ~VAR_EDIT.edit(
            "  _result:=__CHK.record(VAR_EDIT,,'D_OD','D_DO');
               {? _result='' & VAR_EDIT.D_OD>=VAR_EDIT.D_DO
               || FUN.emsg('Wartość w polu \"Data od\" nie może być większa niż w polu \" Data do\".');
                  _result:='D_OD'
               ?};
               _result
            "
         )}
{FOR: KAL_NAZW}
{INDEX: 'KAL_CZES'}
{PREFIX: exec('ref_firma','ustawienia'),'N'}
{SELECT}
{DO}
   { Rok:=VAR_EDIT.D_OD~1; ~~ }
   {WHILE: Rok<=VAR_EDIT.D_DO~1}
   {DO}
      {FOR: KAL_ROK}
      {INDEX: 'KAL_ROK'}
      {PREFIX: KAL_NAZW.ref(),Rok}
      {DO}
         {FOR: KAL_DEF}
         {INDEX: 'KAL_DEF'}
         {FROM: KAL_ROK.ref(),VAR_EDIT.D_OD}
         {TO: KAL_ROK.ref(),VAR_EDIT.D_DO}
         {DO}
{  _a:=KAL_DEF.DATA$7;
' │'+form(KAL_DEF.DATA,11)+' '+form((_a*','-1)+_a,13)+'│  '+KAL_DEF.TYP+
'  │'+form(KAL_DEF.POCZATEK,6)+form(KAL_DEF.KONIEC,6)+form(KAL_DEF.CZAS,6)+
' │ '+form(KAL_DEF.OPIS,36)+'│'
}
         {OD}
      {OD}
      { Rok+=1; ~~}
   {OD}
   {PAGE}
{OD}
:!UTF-8
{TITLE:A. Zestawienie ofert}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('w','s','PRN');
   w :=obj_new(8); "--wartosci kolumn--";
   s :=obj_new(8); "--szerokosc kolumn--";
   doklw :=2; "--dokladnosc wartosci--";
   PRN :=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   lp :=0;

   s[1] :=5;
   s[2] :=20;
   s[3] :=30;
   s[4] :=10;
   s[5] :=10;
   s[6] :=20;
   s[7] :=16;
   s[8] :=16;

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   tryb:=rep_export();

   kh :=zakres :=status :=redds :=beer :='';
   odd :=dod :=date(0,0,0);

   wydr_naz:='zkl_ofe1';
   wydr_edi:='ZKL_OFE1';

   KH.cntx_psh();
   KH.win_dict('WER_SLO'); KH.actions('WER_SLO','W');

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("{? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || '' || FUN.info('Błędny zakres dat.'@);'DO_DATY' ?}")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         kh     :=WYDRUKIL.KH_OD().KOD;
         dataod :=WYDRUKIL.OD_DATY;
         datado :=WYDRUKIL.DO_DATY;
         zakres :={? WYDRUKIL.RADIOB_2=2 || 'N' |? WYDRUKIL.RADIOB_2=1 || 'T' || 'W' ?};
         status :={? WYDRUKIL.RADIOB_1=2 || 'Z' |? WYDRUKIL.RADIOB_1=1 || 'A' || 'W' ?}
      ?}
   ?};
   KH.actions('WER_SLO','');
   KH.cntx_pop()
}
{END:
   VAR_DEL.delete('w','s','PRN','tryb');
   VAR_DEL.delete('doklw','wyjscie','lp','X_Xw1');
   VAR_DEL.delete('kh','zakres','status','redds','beer');
   VAR_DEL.delete('odd','dod');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("w[1]",s[1],'Lp.'@,1) ?};
PRN.add("w[2]",s[2],'Symbol'@);
PRN.add("w[3]",s[3],'Kontrahent'@);
PRN.add("w[4]",s[4],'Data utworzenia'@);
PRN.add("w[5]",s[5],'Termin ważności'@);
PRN.add("w[6]",s[6],'Zamówienie z oferty'@);
{? exec('upr_cm','ceny') & exec('upr_cs','ceny')
||
   PRN.add("w[7]",s[7],'Wartość netto'@,1);
   PRN.add("w[8]",s[8],'Zysk'@,1)
?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'OFE'    ,'STRING[20]'  ,'Symbol oferty',
   'KH'     ,'STRING[70]'  ,'Kontrahent',
   'DU'     ,'DATE'        ,'Data utworzenia',
   'TW'     ,'DATE'        ,'Termin ważności',
   'ZK'     ,'STRING[20]'  ,'Symbol zamówienia',
   'NET'    ,'REAL'        ,'Wartość',
   'ZYSK'   ,'REAL'        ,'Zysk');

OFE.index('SYM'); OFE.prefix(ST.ODDZ);
{? OFE.first()
||
   _zam2ofe :=sql('select OFE.REFERENCE oferef, ZK_N.SYM ZK from @ZK_N join OFE order by 1');
   {!
   |? _findz2o :=_zam2ofe.find_key(BB.refsql(OFE.ref()));
      {? (OFE.DU>=dataod | dataod=date(0,0,0)) & (OFE.DU<=datado | datado=date(0,0,0))
         & (kh=OFE.KH().KOD | kh='')
         & (OFE.A=status | status='W')
         & (zakres='W'
            | zakres='T' & _findz2o=1
            | zakres='N' & _findz2o=0)
      ||
         X_Xw1.OFE  :=OFE.SYM;
         X_Xw1.KH   :=OFE.KH().KOD+' '+KH.NAZ;
         X_Xw1.DU   :=OFE.DU;
         X_Xw1.TW   :=OFE.TW;
         X_Xw1.ZK   :={? _findz2o=1 || _zam2ofe.ZK || '' ?};
         X_Xw1.NET  :=OFE.NET;
         X_Xw1.ZYSK :=OFE.ZYSK;
         X_Xw1.add()
      ?};
      OFE.next()
   !}
?};

{? zakres='W' || redds:='Oferty: Wszystkie'@ |? zakres='T' || redds:='Oferty: Z zamówieniami'@ |? zakres='N' || redds:='Oferty: Bez zamówień'@ ?};
{? status='W' || beer:='Status ofert: Wszystkie'@ |? status='A' || beer:='Status ofert: Aktywne'@ |? status='Z' || beer:='Status ofert: Zarchiwizowane'@ ?};

PAR_WYDR.TITLE:='Zestawienie ofert'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{IF: (kh<>'')}{<{lmt+1}}{'Kontrahent: %1'@[kh]}{FI}
{IF: dataod<>date(0,0,0) | datado<>date(0,0,0)}{<{lmt+1}}{'Zakres dat: od %1 do %2'@[$dataod,$datado]}{FI}
{IF: zakres<>''}{<{lmt+1}}{redds}{FI}
{IF: status<>''}{<{lmt+1}}{beer}{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xw1}
{DO}
   {
   lp +=1;
   w[1] :=form(lp);
   w[2] :=form(X_Xw1.OFE);
   w[3] :=form(X_Xw1.KH);
   w[4] :=form(X_Xw1.DU);
   w[5] :=form(X_Xw1.TW);
   w[6] :=form(X_Xw1.ZK);
   w[7] :=form(X_Xw1.NET,,doklw,'.,');
   w[8] :=form(X_Xw1.ZYSK,,doklw,'.,')
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: tryb=1}{SUBSTITUTE: 'LINIA0'}{FI}
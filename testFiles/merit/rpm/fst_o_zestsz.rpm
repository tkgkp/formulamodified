:!UTF-8
{TITLE:D. Szczegółowy wydruk danych środków}
{PRINTER: FINFO.W_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   SRSR.cntx_psh(); SRST.cntx_psh();
   SRSR.prefix(); SRST.prefix();
   exec('init','fst');
   SRD.maski('r');
   exec('odd_filtr','fst');
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0;
   amop:=obj_new(12);
   amof:=obj_new(12);
   amod:=obj_new(12);
   warp:=warf:=ward:=0;
   {! _i:=1..12 |! amop[_i]:=amof[_i]:=amod[_i]:= 0 !};
   amp:=amf:=anp:=anf:=uin:=wip:=wif:=wup:=wuf:=wnp:=wnf:=0;
   amd:=and:=wid:=wud:=wnd:=0;
   ind:='';
   {? FINFO.TOR_D='T'
   ||  PAR_WYDR.efld_opt('ZEST_SZ','enable=1',,'MAD');
       g_ward:=0; g_umod:=0; g_netd:=0;
       sum_ward:=0; sum_umod:=0; sum_netd:=0
   ||  PAR_WYDR.efld_opt('ZEST_SZ','enable=0',,'MAD')
   ?};
   __cur_mo:=null; __cur_r:=''; __cur_od:=null; __cur_rf:=null;
   srst_dr:=0
}
{END:
   SRSR.cntx_pop(); SRST.cntx_pop();
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3;&par4;
   obj_del(amop); obj_del(amof); obj_del(amod);
   &amp; &amf; &anp; &anf; &uin; &wip; &wif; &wup; &wuf; &wnp; &wnf;
   &ind; &amd; &and; &wid; &wud; &wnd;
   &__cur_mo; &__cur_r; &__cur_od; &__cur_rf;
   &warp; &warf; &ward;
   &srst_dr
}
{EXIT:
   exec('clear_par','fst');
   exec('set_parw','param_wydr');
   {? OPERATOR.DEPT<>null || PAR_WYDR.ODD:=OPERATOR.DEPT || PAR_WYDR.ODD:=0 ?};
   {? SSTALE.AO().KON<>date(0,0,0)
   || PAR_WYDR.STAN_NA:=SSTALE.AO().KON
   |? SSTALE.AO().OES=0
   || PAR_WYDR.STAN_NA:=SSTALE.AR().POCZ_ROK
   |? SSTALE.AO().OES>0
   || PAR_WYDR.STAN_NA:=SSTALE.AR().KON_ROK
   ?};
   PAR_WYDR.TO_DE:=SSTALE.AO().KON;
   PAR_WYDR.WART_PN:=2;
   PAR_WYDR.ELEMENTY:=1;
   PAR_WYDR.RODZAJ:=1;
   PAR_WYDR.RODZAJ2:=1;
   SRD.PAR_WYDR:='T';
   PAR_WYDR.JORG:=null; EDIT_ES.JORG:='';
   exec('odd_filtr','fst');
   exec('set_gr_list','fst');
   PAR_WYDR.win_edit('ZEST_SZ');
   PAR_WYDR.hdr_edit(': D. Szczegółowy wydruk danych środków'@);
   {? PAR_WYDR.edit("exec('check_zest_sz','fst')")
   || _exit:=0;
      exec('zakres_gr','fst')
   || _exit:=1
   ?};
   _exit
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_o_zestsz.rpm
Utworzony  : 2004-09-10
Zawartość  : Zestawienie środków trwałych - szczegółowe
==================================================
--------------- OPIS PÓL ZMIENNEJ PAR_WYDR ------------
Dane pobierane są w sekcji Exit z wykorzystaniem formuł z pliku query.fml
ELEMENTY: 1 = z elementami składowymi, 2 bez elementów
WART_PN: 1= wartosc srodkow poczatkowa, 2 = narastajaco
MAP    : kod metody podatkowej
MAF    : kod metody bilansowej
MAD    : kod metody dodatkowej
FROM_DE: data eksploatacji od
TO_DE  : data eksploatacji do
ODD    : jedn. ks.
GRUPA  : grupa środków trwałych np. 100, 1**
SKR_ONLY: tylko skreslone
UMO_ONLY: tylko umorzone
{COMMENT-}
{COMMENT}zmienne pomocnicze do wydrukow graficznych:
i - licznik, color, prn1, lm, ll, vp, lmt, rsep
{COMMENT-}
{PAR_WYDR.cntx_psh();~~}
{INCLUDE: wsp_pw1.rpi}
{PAR_WYDR.cntx_pop();~~}
{COMMENT}--------------- OPIS PÓL ZMIENNEJ PAR_WYDR ------------{COMMENT-}
{MACRO: 'SRODEK'}
{ENTIRE}
{LINE: 1}
{FONT: 'T10B'}
{<1>95 'Z E S T A W I E N I E   D A N Y C H   Ś R O D K A'@ }

{FONT: 'T10'}
{>42 'Nazwa środka'@ } :{FONT: 'T10B'}{ SRST.NST }{FONT: 'T10'}
{>42 'Kod grupy'@ } : { SRST.GR().GR }
{IF: SRST.R<>'N'}
{>42 'Stawka amortyzacji bilansowej'@ } : {,2 (SRST.STAF)$FINFO.DOKL}
{>42 'Korekta amortyzacji bilansowej'@ } : {,2 (SRST.WKF)$FINFO.DOKL}
{>42 'Stawka amortyzacji podatkowej'@ } : {,2 (SRST.STAP)$FINFO.DOKL}
{>42 'Korekta amortyzacji podatkowej'@ } : {,2 (SRST.WKP)$FINFO.DOKL}
{IF: FINFO.TOR_D='T'}
{>42 'Stawka amortyzacji dodatkowej'@ } : {,2 (SRST.STAD)$FINFO.DOKL}
{>42 'Korekta amortyzacji dodatkowej'@ } : {,2 (SRST.WKD)$FINFO.DOKL}
{FI}
{FI}
{>42 'Jednostka księgowa'@ } : { SRST.ODD().OD }
{>42 'Jednostka organizacyjna'@ } : { SRST.JORG().SYMBOL }
{>42 'Na dzień'@ } : { PAR_WYDR.STAN_NA$6 }
{>42 'Rodzaj'@ } : { {? SRST.R='T' || 'trwały'@ || 'niskocenny'@ ?} }

{>42 'Numer inwentarzowy'@ } :{FONT: 'T10B'}{ SRST.NRI }{FONT: 'T10'}
{>42 'Numer dowodu OT'@ } : { {? SRST.SRSR().DOKPRZ<>null || SRST.SRSR().DOKPRZ().SYMBOL || '' ?} }
{>42 'Rodzaj przychodu'@ } : { SRST.SRSR().PRZYCHOD().R }
{>42 'Numer fabryczny'@ } : { SRST.SRSR().NF }
{>42 'Data zakupu'@ } : { SRST.SRSR().DZ$6 }
{>42 'Data eksploatacji'@ } : { SRST.SRSR().DE$6 }
{>42 'Użytkownik'@ } : { SRST.OSOBA().PIERWSZE + ' ' + SRST.OSOBA().NAZWISKO }
{>42 'Zestaw kont podatkowych kosztów'@ } : { SRST.KONPOD().KOD }{wiersz:=45;~~}
{FOR: KKOSZ } {INDEX: 'KONTO' } {PREFIX: SRST.KONPOD }
{DO}
{FIRST}
{>{wiersz+4}}Składniki zestawu kont
{FIRST-}
{>{wiersz+4}}{KKOSZ.KONTO().SYM}  : {form(KKOSZ.PR,10,2)}%
{OD}
{COMMENT}{INCLUDE: konta.rpi}{COMMENT-}
{>42 'Zestaw kont bilansowych kosztów'@ } : { SRST.KONFIN().KOD }{wiersz:=45;~~}
{FOR: KKOSZ } {INDEX: 'KONTO' } {PREFIX: SRST.KONFIN }
{DO}
{FIRST}
{>{wiersz+4}}Składniki zestawu kont
{FIRST-}
{>{wiersz+4}}{KKOSZ.KONTO().SYM}  : {form(KKOSZ.PR,10,2)}%
{OD}
{COMMENT}{INCLUDE: konta.rpi}{COMMENT-}
{IF: FINFO.TOR_D='T'}
{>42 'Zestaw kont dodatkowych kosztów'@ } : { SRST.KONDOD().KOD }{wiersz:=45;~~}
{FOR: KKOSZ } {INDEX: 'KONTO' } {PREFIX: SRST.KONDOD }
{DO}
{FIRST}
{>{wiersz+4}}Składniki zestawu kont
{FIRST-}
{>{wiersz+4}}{KKOSZ.KONTO().SYM}  : {form(KKOSZ.PR,10,2)}%
{OD}
{COMMENT}{INCLUDE: konta.rpi}{COMMENT-}
{FI}
{>42 'Metoda amortyzacji podatkowej'@ } : {SRST.MP().N}
{>42 'Metoda amortyzacji bilansowej'@ } : {SRST.MF().N}
{IF: FINFO.TOR_D='T'}
{>42 'Metoda amortyzacji dodatkowej'@ } : {SRST.MD().N}
{FI}
{>42 'Sezonowy?'@ } : { {? SRST.S='T' || 'TAK'@ + ', wg. schematu: '@+SRST.SCH_SEZ().KOD || 'NIE'@ ?} }
{>42 'Skreślony?'@ } : { {? SRST.SRSR().Z='T' || 'TAK'@ || 'NIE'@ ?} }
{IF: SRST.SRSR().ROZ<>null}
{>42 'Rodzaj rozchodu'@ } : { SRST.SRSR().ROZ().R }
{FI}
{IF: SRST.SRSR().DES<>date(0,0,0)}
{>42 'Data skreślenia z ewidencji'@ } : { SRST.SRSR().DES$6 }
{FI}
{IF: SRST.SRSR().DOKSKR<>null}
{>42 'Numer dowodu skreślającego'@ } : { SRST.SRSR().DOKSKR().SYMBOL }
{FI}

{>42 'Uwagi'@ } : { SRST.SRSR().U }

{IF:PAR_WYDR.WART_PN = 1}
{FONT: 'T10B'}
{<1>88 'W A R T O Ś C I   P O C Z Ą T K O W E   Ś R O D K A'@ }
{FONT: 'T10'}

{>42 'Wartość podatkowa'@ } : {>51,2 SRST.SRSR().WARP }
{>42 'Umorzenie podatkowe'@ } : {>51,2 SRST.SRSR().UMOP+SRST.SRSR().ZALP100 }
{>42 'Netto podatkowe'@ } : {>51,2 SRST.SRSR().NETP-SRST.SRSR().ZALP100 }
{IF: SRST.SRSR().AMOR100='T'}
{>42 'Amortyzacja jednorazowa'@ } : {>51,2 SRST.SRSR().WARP100 }
{>42 'Zaliczka na zakup środka'@ } : {>51,2 SRST.SRSR().ZALP100 }
{>42 'Rozliczenie jednorazowe'@ } : {>51,2 SRST.SRSR().FREQ100().OPIS }
{FI}

{>42 'Wartość bilansowa'@ } : {>51,2 SRST.SRSR().WARF }
{>42 'Umorzenie bilansowe'@ } : {>51,2 SRST.SRSR().UMOF }
{>42 'Netto bilansowe'@ } : {>51,2 SRST.SRSR().NETF }
{IF: FINFO.TOR_D='T'}

{>42 'Wartość dodatkowa'@ } : {>51,2 SRST.SRSR().WARD }
{>42 'Umorzenie dodatkowe'@ } : {>51,2 SRST.SRSR().UMOD }
{>42 'Netto dodatkowe'@ } : {>51,2 SRST.SRSR().NETD }
{FI}
{ELSE}
{FONT: 'T10B'}
{<1>88 'A K T U A L N E   W A R T O Ś C I   Ś R O D K A'@ }
{FONT: 'T10'}
{IF: SRST.SRSR().DOKSKR().DW<>date(0,0,0) | (PAR_WYDR.MO_DO=2 & SRST.SRSR().DOKSKR().DO<>date(0,0,0) & SRST.SRSR().DOKSKR().DO<=PAR_WYDR.STAN_NA)}
{>42 'Wartość podatkowa'@ } : {>51 form(0,15,2,' ,') }
{>42 'Umorzenie podatkowe'@ } : {>51 form(0,15,2,' ,') }
{>42 'Netto podatkowe'@ } : {>51 form(0,15,2,' ,') }
{ELSE}
{IF: PAR_WYDR.MO_DO=2}
{ warp:=0; warf:=0; ward:=0;
  SRDO.cntx_psh();
  SRDO.index('SRODZAJ');
  SRDO.prefix(SRST.SRSR,'W');
  {? SRDO.first()
  || {!
     |? {? 1+SRDO.TYP().TYP='K' & (SRDO.DO<=PAR_WYDR.STAN_NA & SRDO.DO~2=PAR_WYDR.STAN_NA~2)
        || warp+=SRDO.WARP;
           warf+=SRDO.WARF;
           {? FINFO.TOR_D='T' || ward+=SRDO.WARD ?}
        ?};
        SRDO.next()
     !}
  ?};
  SRDO.cntx_pop() }
{>42 'Wartość podatkowa'@ } : {>51 form(SRST.WARP+warp,15,2,' ,') }
{>42 'Umorzenie podatkowe'@ } : {>51 form(SRST.UMOP+SRST.SRSR().ZALP100,15,2,' ,') }
{>42 'Netto podatkowe'@ } : {>51 form({? SRST.NETP-SRST.SRSR().ZALP100+warp<0 || 0 || SRST.NETP-SRST.SRSR().ZALP100+warp ?},15,2,' ,') }
{ELSE}
{>42 'Wartość podatkowa'@ } : {>51 form(SRST.WARP,15,2,' ,') }
{>42 'Umorzenie podatkowe'@ } : {>51 form(SRST.UMOP+SRST.SRSR().ZALP100,15,2,' ,') }
{>42 'Netto podatkowe'@ } : {>51 form(SRST.NETP-SRST.SRSR().ZALP100,15,2,' ,') }
{FI}
{FI}
{IF: SRST.SRSR().AMOR100='T'}
{>42 'Amortyzacja jednorazowa'@ } : {>51 form(SRST.SRSR().WARP100,15,2,' ,') }
{>42 'Zaliczka na zakup środka'@ } : {>51 form(SRST.SRSR().ZALP100,15,2,' ,') }
{>42 'Rozliczenie jednorazowe'@ } : {>51 SRST.SRSR().FREQ100().OPIS }
{FI}
{IF: SRST.SRSR().DOKSKR().DW<>date(0,0,0) | (PAR_WYDR.MO_DO=2 & SRST.SRSR().DOKSKR().DO<>date(0,0,0) & SRST.SRSR().DOKSKR().DO<=PAR_WYDR.STAN_NA)}
{>42 'Wartość bilansowa'@ } : {>51 form(0,15,2,' ,')}
{>42 'Umorzenie bilansowe'@ } : {>51 form(0,15,2,' ,')}
{>42 'Netto bilansowe'@ } : {>51 form(0,15,2,' ,')}
{ELSE}
{IF: PAR_WYDR.MO_DO=2}
{>42 'Wartość bilansowa'@ } : {>51 form(SRST.WARF+warf,15,2,' ,')}
{>42 'Umorzenie bilansowe'@ } : {>51 form(SRST.UMOF,15,2,' ,')}
{>42 'Netto bilansowe'@ } : {>51 form({? SRST.NETF+warf<0 || 0 || SRST.NETF+warf ?},15,2,' ,')}
{ELSE}
{>42 'Wartość bilansowa'@ } : {>51 form(SRST.WARF,15,2,' ,')}
{>42 'Umorzenie bilansowe'@ } : {>51 form(SRST.UMOF,15,2,' ,')}
{>42 'Netto bilansowe'@ } : {>51 form(SRST.NETF,15,2,' ,')}
{FI}
{FI}
{IF: FINFO.TOR_D='T'}
{IF: SRST.SRSR().DOKSKR().DW<>date(0,0,0) | (PAR_WYDR.MO_DO=2 & SRST.SRSR().DOKSKR().DO<>date(0,0,0) & SRST.SRSR().DOKSKR().DO<=PAR_WYDR.STAN_NA)}
{>42 'Wartość dodatkowa'@ } : {>51 form(0,15,2,' ,')}
{>42 'Umorzenie dodatkowe'@ } : {>51 form(0,15,2,' ,')}
{>42 'Netto dodatkowe'@ } : {>51 form(0,15,2,' ,')}
{ELSE}
{IF: PAR_WYDR.MO_DO=2}
{>42 'Wartość dodatkowa'@ } : {>51 form(SRST.WARD+ward,15,2,' ,')}
{>42 'Umorzenie dodatkowe'@ } : {>51 form(SRST.UMOD,15,2,' ,')}
{>42 'Netto dodatkowe'@ } : {>51 form({? SRST.NETD+ward<0 || 0 || SRST.NETD+ward ?},15,2,' ,')}
{ELSE}
{>42 'Wartość dodatkowa'@ } : {>51 form(SRST.WARD,15,2,' ,')}
{>42 'Umorzenie dodatkowe'@ } : {>51 form(SRST.UMOD,15,2,' ,')}
{>42 'Netto dodatkowe'@ } : {>51 form(SRST.NETD,15,2,' ,')}
{FI}
{FI}
{FI}
{FI}

{ENTIRE-}
{MACRO-}
{MACRO: 'ELEMENTY'}
{ __cur_mo:=SRST.OKRO_F; __cur_r:=SRST.R; __cur_od:=SRST.ODD; __cur_rf:=SRST.ref(); SRST.cntx_psh(); ~~}
{FOR: SRST }{INDEX: 'TREE' }{PREFIX: __cur_r, __cur_mo, __cur_od, __cur_rf}
{DO}
{FIRST}
{FONT: 'T10B'}
{<1>88 'E L E M E N T Y   Ś R O D K A'@ }
{FONT: 'T10'}
{<1 'Nr inwentarzowy'@}{<18 'Nazwa elementu'@}{>64 'Wart. podat.'@}{>79'Wart. bilan.'@}{IF: FINFO.TOR_D='T'}{>94'Wart. dodat.'@}{FI}
{FIRST-}
{ warp:=0; warf:=0; ward:=0;
  SRDO.cntx_psh();
  SRDO.index('SRODZAJE');
  SRDO.prefix(SRSR.ref,SRST.SRSR,'W');
  {? SRDO.first()
  || {!
     |? {? 1+SRDO.TYP().TYP='K' & (SRDO.DO<=PAR_WYDR.STAN_NA & SRDO.DO~2=PAR_WYDR.STAN_NA~2)
        || warp+=SRDO.WARP;
           warf+=SRDO.WARF;
           {? FINFO.TOR_D='T' || ward+=SRDO.WARD ?}
        ?};
        SRDO.next()
     !}
  ?};
  SRDO.cntx_pop(); ~~ }
{IF: PAR_WYDR.MO_DO=2}
{<1}{SRST.NRI} {<18 SRST.NST} {>64 form(SRST.WARP+warp,15,2,' ,')} {>79 form(SRST.WARF+warf,15,2,' ,')}{IF: FINFO.TOR_D='T'}{>94 form(SRST.WARD+ward,15,2,' ,')}{FI}
{ELSE}
{<1}{SRST.NRI} {<18 SRST.NST} {>64 form(SRST.WARP,15,2,' ,')} {>79 form(SRST.WARF,15,2,' ,')}{IF: FINFO.TOR_D='T'}{>94 form(SRST.WARD,15,2,' ,')}{FI}
{FI}
{OD}
 { SRST.cntx_pop(); ~~}
{MACRO-}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{ PAR_WYDR.TITLE:=''; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{COMMENT}{SUBSTITUTE: 'LINIAF'}{COMMENT-}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{FOR: SRST}{INDEX: 'ROK_G' }{PREFIX: SSTALE.AR, SSTALE.AO}
 {DO}
 {COMMENT}Warunek sprawdzajacy czy drukowac SRST{COMMENT-}
 { {? PAR_WYDR.WAR_GR=4
   || _grupa_ok:=1
   || _grupa_ok:=exec('gr_fit','fst',SRST.GR().GR)
   ?};
   srst_dr:=(PAR_WYDR.JORG=null | PAR_WYDR.JORG=SRST.JORG)
            & (PAR_WYDR.ODD=null | PAR_WYDR.ODD=SRST.ODD)
            & (PAR_WYDR.ZAKRESDR=0
              | (PAR_WYDR.ZAKRESDR=1 & SRST.R='T')
              | (PAR_WYDR.ZAKRESDR=2 & SRST.R='N'))
            & (PAR_WYDR.RODZAJ2=1 | (PAR_WYDR.RODZAJ2=2 & 1+SRST.MP().K='U' & 1+SRST.MF().K='U' & 1+SRST.MD().K='U')
                                  | (PAR_WYDR.RODZAJ2=3 & SRST.NETP=0 & SRST.NETF=0 & SRST.NETD=0)
                                  | (PAR_WYDR.RODZAJ2=4 & SRST.SRSR().Z='T')
                                  | (PAR_WYDR.RODZAJ2=5 & SRST.SRSR().OKRO_F=SSTALE.AO))
            & SRST.GR<>null
            & SRST.KIND='T'
            & _grupa_ok & SRST.R<>'S'
            & (PAR_WYDR.MAP=null | PAR_WYDR.MAP=SRST.MP)
            & (PAR_WYDR.MAF=null | PAR_WYDR.MAF=SRST.MF)
            & (PAR_WYDR.MAD=null | PAR_WYDR.MAD=SRST.MD)
            & (PAR_WYDR.FROM_DE=date(0,0,0) | SRST.SRSR().DE>=PAR_WYDR.FROM_DE)
            & (PAR_WYDR.TO_DE=date(0,0,0) | SRST.SRSR().DE<=PAR_WYDR.TO_DE)
            & (PAR_WYDR.PRZYCHOD=null | PAR_WYDR.PRZYCHOD=SRST.SRSR().PRZYCHOD)
            & (PAR_WYDR.ROZCHOD=null | PAR_WYDR.ROZCHOD=SRST.SRSR().ROZ)
            & (PAR_WYDR.UMO_ONLY=0 | (PAR_WYDR.UMO_ONLY=1 & SRST.SRSR().WARP<>0 & SRST.NETP=0
               & SRST.SRSR().WARF<>0 & SRST.NETF=0 & SRST.SRSR().WARD<>0 & SRST.NETD=0))
            & (PAR_WYDR.SKR_ONLY=0 | (PAR_WYDR.SKR_ONLY=1 & SRST.SRSR().Z='T'))
 ;~~}
 {IF: srst_dr}
   {SUBSTITUTE: 'SRODEK'}
   {IF: PAR_WYDR.ELEMENTY=1}
     {SUBSTITUTE:'ELEMENTY'}
   {FI}
 {FI}
 {OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
{FOOTER}{FOOTER-}
{ vp:=0; ~~}
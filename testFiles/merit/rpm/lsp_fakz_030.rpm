:!UTF-8
{TITLE:H. Zestawienie faktur zaliczkowych}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   fakz_030:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','PRN','what');
      VAR_DEL.delete('wydr_naz','wydr_edi');
      VAR_DEL.delete('doklw','bb','cc','wyjscie','X_Xw1');
      VAR_DEL.delete('dataodw','datadow');
      VAR_DEL.delete('dataod','datado','status','kh','start');
      VAR_DEL.delete('sym','lp')";
   fakz_030();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=0; rsep:=1;

   VAR_DEL.delete('n','k','w','s','PRN');
   n :=obj_new(11); "--nazwy kolumn--";
   k :=obj_new(11); "--szerokosci kolumn--";
   w :=obj_new(11); "--wartosci kolumn--";
   s :=0; "--razem--";
   doklw:=2; "--dokladnosc wartosci--";

   PRN:=obj_new(@.CLASS.PRINT,11); PRN.s_frame();

   n[1] :='Lp.'@;
   n[2] :='Faktura zaliczkowa'@;
   n[3] :='Kontrahent'@;
   n[4] :='Data wystawienia'@;
   n[5] :='Kwota zaliczki'@;
   n[6] :='Kwota po korekcie'@;
   n[7] :='Rozliczono'@;
   n[8] :='Do rozliczenia'@;
   n[9] :='Faktura końcowa'@;
   n[10]:='Waluta'@;
   n[11]:='Data otrzy. zaliczki'@;

   k[1] :=5;
   k[2] :=15;
   k[3] :=15;
   k[4] :=11;
   k[5] :=12;
   k[6] :=12;
   k[7] :=12;
   k[8] :=12;
   k[9] :=15;
   k[10]:=6;
   k[11]:=11;

   lp:=0;
   tryb:=rep_export();

   dataodw:=datadow:=date(0,0,0);
   dataod:=datado:=date(0,0,0);
   fakrozl :=kh :=status :='';
   KH.cntx_psh();
   KH.win_dict('WER_SLO'); KH.actions('WER_SLO','W');

   wydr_naz:='fakz_030';
   wydr_edi:='FAKZ_030';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? exec('upr_cs','ceny')
      & WYDRUKIL.edit("{? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || '' || FUN.info('Błędny zakres dat.'@);'DO_DATY' ?}")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         dataodw:=WYDRUKIL.OD_DATY2;
         datadow:=WYDRUKIL.DO_DATY2;
         dataod:=WYDRUKIL.OD_DATY;
         datado:=WYDRUKIL.DO_DATY;
         kh    :=WYDRUKIL.KH_OD().KOD;
         status:=WYDRUKIL.SORTUJ
      ?}
   ?};
   {? wyjscie=0
   ||
      SLO.cntx_psh();
      SLO.index('SL'); SLO.prefix(INFO.SLWAL().SLU);
      {? SLO.first()
      || s:=tab_tmp(1
            ,'WAL'      ,'STRING[8]'   ,'Waluta'
            ,'KW'       ,'REAL'        ,'Kwota zaliczki'
            ,'KW_PO_K'  ,'REAL'        ,'Kwota po korekcie'
            ,'KW_ROZ'   ,'REAL'        ,'Rozliczono'
            ,'KW_POZ'   ,'REAL'        ,'Do rozliczenia')
      || FUN.info('Słownik walut jest pusty.'@); wyjscie:=1
      ?};
      SLO.cntx_pop()
   ?};
   FAKS.cntx_psh(); FAP.cntx_psh()
}
{END:
   KH.cntx_pop();
   FAKS.cntx_pop(); FAP.cntx_pop();
   fakz_030(); VAR_DEL.delete('fakz_030')
}
{IF: exec('upr_cs','ceny')=0}{ FUN.info('Brak uprawnień do wartości sprzedaży.'@);~~}{EXIT}{FI}
{IF: wyjscie=1} {EXIT}{FI}
{
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
PRN.add("w[3]",k[3],n[3]);
PRN.add("w[4]",k[4],n[4]);
PRN.add("w[11]",k[11],n[11]);
PRN.add("w[10]",k[10],n[10]);
PRN.add("w[5]",k[5],n[5],1);
PRN.add("w[6]",k[6],n[6],1);
PRN.add("w[7]",k[7],n[7],1);
PRN.add("w[8]",k[8],n[8],1);
PRN.add("w[9]",k[9],n[9]);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'LP'     ,'INTEGER'     ,'Lp.',
   'FAKSZ'  ,'STRING[20]'  ,'Faktura zaliczkowa',
   'KH'     ,'STRING[60]'  ,'Kontrahent',
   'DW'     ,'DATE'        ,'Data wystawienia',
   'D'      ,'DATE'        ,'Data przyjęcia',
   'KW'     ,'REAL'        ,'Kwota zaliczki',
   'KW_PO_K','REAL'        ,'Kwota po korekcie',
   'KW_ROZ' ,'REAL'        ,'Rozliczono',
   'KW_POZ' ,'REAL'        ,'Do rozliczenia',
   'FAKSK'  ,'STRING[255]' ,'Faktura końcowa',
   'WAL'    ,'STRING[8]'   ,'Waluta');

OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? OKR.first()
|| _stsupr:=exec('get','#params',2102,2,OPERATOR.USER)='T';
   _stss:="
                _wyn:=exec('usr_upr','b_perm','STS',_a,OPERATOR.USER);
                _wyn
";
   _lp :=0;
   {!
   |?
      FAKS.use('faktu'+ST.ODDZ+(($OKR.ROK)+2));
      FAP.use('fakpo'+ST.ODDZ+(($OKR.ROK)+2));
      FAKS.index('FAK_WHE'); FAKS.prefix('S','L');
      {? FAKS.first()
      ||
         {!
         |?
            {? (_stsupr=0 | _stsupr=1 & FAKS.STS<>null & _stss(FAKS.STS))
               & FAKS.T().KOR='N'
               & FAKS.STAT_REJ<>'A'
            ||
               _fakrozl :=_bb :=_cc :='';
               _faks:=BB.refsql(FAKS.ref());
               FAPOW.index('FAKS_K');
               FAPOW.prefix(_faks);
               {? FAPOW.first()
               || exec('zal_sum','faktury_zalicz');
                  {? SUM.KW_POZ=0 || "--rozliczone--"; _bb:='T'
                  |? SUM.KW_POZ>0 & SUM.KW_POZ<FAPOW.KW_PO_K || "--czesciowo rozliczone--"; _bb:='C'
                  |? SUM.KW_POZ=FAPOW.KW_PO_K || "--nierozliczone--"; _bb:='N'
                  ?};
                  {? SUM.KW>=0 & SUM.KW<=FAPOW.KW || "--wszystkie--"; _cc:='W'?};
                  {? ((FAKS.DW>=dataodw | dataodw=date(0,0,0)) & (FAKS.DW<=datadow | datadow=date(0,0,0)))
                     & ((FAKS.D>=dataod | dataod=date(0,0,0)) & (FAKS.D<=datado | datado=date(0,0,0)))
                     & (kh=FAKS.KH().KOD | kh='')
                     & (status=_bb | status='' | status=_cc)
                  ||
                     _lp+=1;
                     X_Xw1.LP       :=_lp;
                     X_Xw1.FAKSZ    :=FAKS.SYM;
                     X_Xw1.KH       :=FAKS.KH().NAZ;
                     X_Xw1.DW       :=FAPOW.DW;
                     X_Xw1.D        :=FAPOW.D;
                     X_Xw1.KW       :=FAPOW.KW;
                     X_Xw1.KW_PO_K  :=FAPOW.KW_PO_K;
                     X_Xw1.KW_ROZ   :=0;
                     X_Xw1.KW_POZ   :=SUM.KW_POZ;
                     X_Xw1.FAKSK    :='';
                     X_Xw1.WAL      :=FAKS.WAL().KOD;
                     X_Xw1.add();
                     {!
                     |?
                        {? FAPOW.ROZ='T'
                        ||
                           _lp+=1;
                           X_Xw1.LP       :=_lp;
                           X_Xw1.FAKSZ    :=FAKS.SYM;
                           X_Xw1.KH       :=FAKS.KH().NAZ;
                           X_Xw1.DW       :=FAPOW.DW;
                           X_Xw1.D        :=FAPOW.D;
                           X_Xw1.KW       :=0;
                           X_Xw1.KW_PO_K  :=0;
                           X_Xw1.KW_ROZ   :=FAPOW.KW_ROZ;
                           X_Xw1.KW_POZ   :=0;
                           X_Xw1.FAKSK    :=FAPOW.END_SYM;
                           X_Xw1.add()
                        ?};
                        FAPOW.next()
                     !}
                  ?}
               ?}
            ?};
            FAKS.next()
         !}
      ?};
      OKR.next()
   !}
?};

PAR_WYDR.TITLE:='Zestawienie faktur zaliczkowych';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{
{? status='C' || start:='Częściowo rozliczone'
|? status='T' || start:='Rozliczone'
|? status='N' || start:='Nierozliczone'
|? status='W' || start:='Wszystkie'
              || start:=''
?}
;~~}
{<{lmt+1}}{'Parametry wydruku:'}
{IF: (kh<>'')}{<{lmt+1}}{'Kontrahent - '+kh}{FI}
{<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
{<{lmt+1}}{'Data wystawienia'}{<{lmt+1+20}dataodw}{<{lmt+1+40}datadow}
{<{lmt+1}}{'Data sprzedaży'}{<{lmt+1+20}dataod}{<{lmt+1+40}datado}
{IF: status<>''}{<{lmt+1}}{'Status: '+start}{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{IF: tryb<>1}{SUBSTITUTE: 'LINIAF'}{FI}
{LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{ sym:=''; lp:=0;~~}
{FOR: X_Xw1}
{DO}
   {
   {? sym<>X_Xw1.FAKSZ
   || what:=1;
      rsep:=PAR_WYDR.RSEP;
      lp+=1;
      w[1] :=form(lp,,,'99');
      w[2] :=form(X_Xw1.FAKSZ);
      w[3] :=form(X_Xw1.KH);
      w[4] :=form(X_Xw1.DW);
      w[5] :=form(X_Xw1.KW,,doklw,'.,');
      w[6] :=form(X_Xw1.KW_PO_K,,doklw,'.,');
      w[7] :='';
      w[8] :=form(X_Xw1.KW_POZ,,doklw,'.,');
      w[9] :='';
      w[10]:=form(X_Xw1.WAL);
      w[11]:=form(X_Xw1.D);
      {? s.find_key(X_Xw1.WAL)
      || s.KW     +=X_Xw1.KW;
         s.KW_PO_K+=X_Xw1.KW_PO_K;
         s.KW_POZ +=X_Xw1.KW_POZ;
         s.put()
      || s.WAL    :=X_Xw1.WAL;
         s.KW     :=X_Xw1.KW;
         s.KW_PO_K:=X_Xw1.KW_PO_K;
         s.KW_POZ :=X_Xw1.KW_POZ;
         s.add()
      ?};
      sym:=X_Xw1.FAKSZ
   || what:=0;
      rsep:=0;
      w[1] :='';
      w[2] :='';
      w[3] :='';
      w[4] :='';
      w[5] :='';
      w[6] :='';
      w[7] :=form(X_Xw1.KW_ROZ,,doklw,'.,');
      w[8] :='';
      w[9] :=form(X_Xw1.FAKSK);
      w[11]:='';
      {? s.find_key(X_Xw1.WAL)
      || s.KW_ROZ+=X_Xw1.KW_ROZ;
         s.put()
      || s.KW_ROZ+=X_Xw1.KW_ROZ;
         s.add()
      ?}
   ?}
   ;~~}
   { {? tryb=1
     || w[1] :=form(lp,,,'99');
        w[2] :=form(X_Xw1.FAKSZ);
        w[3] :=form(X_Xw1.KH);
        w[4] :=form(X_Xw1.DW);
        w[5] :=form(X_Xw1.KW,,doklw,'.,');
        w[6] :=form(X_Xw1.KW_PO_K,,doklw,'.,');
        w[8] :=form(X_Xw1.KW_POZ,,doklw,'.,');
        w[10]:=form(X_Xw1.WAL);
        w[11]:=form(X_Xw1.D);
        w[7] :=form(X_Xw1.KW_ROZ,,doklw,'.,');
        w[9] :=form(X_Xw1.FAKSK)
     ?};~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}{SPACE: 'C'}
{FOR: s}
{DO}
{
w[1] :=w[2] :=w[3] :='';
w[4] :='Razem:'; PRN.FORM[4] :=0;
w[10]:=form(s.WAL);
w[5] :=form(s.KW,,doklw,'.,');
w[6] :=form(s.KW_PO_K,,doklw,'.,');
w[7] :=form(s.KW_ROZ,,doklw,'.,');
w[8] :=form(s.KW_POZ,,doklw,'.,');
w[9] :='';
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
{OD}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
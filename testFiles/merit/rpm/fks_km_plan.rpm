:!UTF-8
{TITLE:A. Plan bieżącej kompensaty rozrachunków}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,10); PRN.sl_frame(); w:=obj_new(10);
   PRNH:=obj_new(@.CLASS.PRINT,4); PRNH.sl_frame();
   {? var_pres('ML',@)<=0 || ML:=obj_new(@.CLASS.MEMO) ?};
   color:=prn1:=prn2:=''; lm:=ll:=vp:=lmt:=rsep:=0;
   head:=PAR_WYDR.HEAD;
   SLO.cntx_psh(); SLU.prefix(); SLO.prefix(); SLUAPPL.prefix();
   XINFO.SLJEZYK();
   SER_KOR.JEZYK:={? SLO.find_key(SLU.ref(),'POL')
                  || SLO.ref()
                  || null
                  ?};
   SLO.cntx_pop();
   sum_1:=sum_2:=sum_3:=sum_4:=sum_5:=sum_6:=0; SER_KOR.ANAL:=1;
   memo:=''; SER_KOR.win_edit('JEZYK'); wz_wyd:=''; texty:=obj_new(20)
}
{END: VAR_DEL.delete('PRN','PRNH','w','ML','wz_wyd','color','prn1','prn2','lm','ll','vp','lmt','rsep');
      PAR_WYDR.HEAD:=head; &head;
      VAR_DEL.delete('sum_1','sum_2','sum_3','sum_4','sum_5','sum_6','memo','texty')
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_km_plan.rpm
Utworzony:   2001-11-27 [AMK] Artur Makos
==================================================
Zawartosc:  Plan kompensaty rozrachunkow
{COMMENT-}
{EXIT: ~exec('par_j','rozrach')}
{IF: var_press('ToPDF')>0}
{  SER_KOR.ANAL:=0;
   wz_wyd:=PAR_NAG.KH1().JEZYK().KOD;
   {? wz_wyd='' || wz_wyd:='POL' ?};
~~}
{EXIT: ~exec('sprwzwyd','rozrach','fks_km_p',wz_wyd,0)}
{REP: '{INCLUDE: '+wz_wyd+'.rpi}'}
{ELIF: PAR_NAG.SER_DEF<>null}
  {EXIT: ~SER_KOR.edit("{? ~exec('sprwzwyd','rozrach','fks_km_p',SER_KOR.JEZYK().KOD,1) || 'JEZYK'@ || '' ?}")}
  {REP: '{INCLUDE: '+wz_wyd+'.rpi}'}
{ELSE}
{ SER_KOR.ANAL:=FUN.ask('Drukować symbole kont analitycznych?'@);~~}
{INCLUDE: fks_km_ppol.rpi}
{FI}
{ prn1:=prn2:='PRN'; PAR_WYDR.HEAD:=0; ~~}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_lic.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SIGNATURE}
{FONT: 'SIGN'}{MAXPAGE: >{charleft()};'Strona '+$page()+' z ';5L;''}
{SIGNATURE-}
{COMMENT}------------------------------MACRO-TABHEADX---------------{COMMENT-}
{MACRO: 'TABHEADX'}
{  PRNH.setcolor(color);
   PRNH.ini_head(); ~~}
{REP: PRNH.fhbar(lmt)}
{WHILE: PRNH.h_next()}
{DO}
   {REP: PRNH.h_fmline(lmt)}
{OD}
{REP: PRNH.fjbar(PRN,lmt)}
{  PRN.setcolor(color);
   PRN.ini_head(); ~~}
{WHILE: PRN.h_next()}
{DO}
   {REP: PRN.h_fmline(lmt)}
{OD}
{MACRO-}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{FONT: 'HEAD'}{SPACE: 'B'}
{SUBSTITUTE: 'HEAD'}
{FONT: 'W8'}{SPACE: 'C'}
{<1 texty[1]}{<21 XINFO.NAZ}
{<21 XINFO.KOD_P+{? +XINFO.POCZ & XINFO.POCZ<>XINFO.MIA || ' '+XINFO.POCZ+', ' || ' ' ?}+XINFO.MIA}
{<21 texty[2]+XINFO.UL+' '+XINFO.NR_D+{? +XINFO.NR_L || '/'+XINFO.NR_L  || ''  ?}}
{<1 texty[3]}{<21 PAR_NAG.ODD().OD}
{LINE: 1}
{ PAR_WYDR.HEAD:=1; ~~}
{<1 texty[4]}{<21 PAR_NAG.PAR_UM().SYM}{<50 texty[5]}{<60 ' '+$PAR_NAG.DATA
}{<80 texty[6]}{<90 PAR_NAG.WAL().KOD}{IF: PAR_NAG.WAL<>FINFO.NAROD
}{<100 texty[7]}{<108 form(PAR_NAG.KURS,10,6,' ,')}{FI}
{<1 texty[8]}{<21 PAR_NAG.OPIS}
{LINE: 1}
{<1 texty[9]}{<21 PAR_NAG.KH1().KOD}
{<21 PAR_NAG.KH1().NAZ_P}
{IF: OS_ADRES.index('RODZAJ1'); OS_ADRES.prefix(KH.ref(),'H'); OS_ADRES.last()}
{<1 texty[12]}{<21 {? +OS_ADRES.MIASTO & |OS_ADRES.MIASTO<>|OS_ADRES.POCZTA
                  || {? (~(+OS_ADRES.POCZTA)) & +OS_ADRES.KOD || OS_ADRES.KOD+' ' || '' ?}+
                     OS_ADRES.MIASTO+',  '
                  || ''
                  ?}+
                  {? +OS_ADRES.POCZTA
                  || {? +OS_ADRES.KOD || OS_ADRES.KOD+' ' || '' ?}+OS_ADRES.POCZTA+',  '
                  || ''
                  ?}+
                  {? +OS_ADRES.ULICA
                  || OS_ADRES.ULICA+{? +OS_ADRES.DOM || ' '+OS_ADRES.DOM || '' ?}+
                     {? +OS_ADRES.LOKAL || '/'+OS_ADRES.LOKAL || '' ?}
                  || ''
                  ?}}
{ELSE}
{<1 texty[12]}{<21 {? KH.POCZ*KH.MIASTO & +KH.POCZ & |KH.POCZ=|KH.MIASTO
                   || {? +KH.KPOCZ || KH.KPOCZ+' ' || '' ?}+KH.POCZ+', '
                   || {? ~+KH.POCZ & +KH.KPOCZ || KH.KPOCZ+' ' || '' ?}+KH.MIASTO+
                      {? +KH.POCZ
                      || ', '+{? +KH.KPOCZ || KH.KPOCZ+' ' || '' ?}+KH.POCZ+', '
                      |? +KH.MIASTO
                      || ', '
                      || ''
                      ?}
                   ?}+KH.UL}
{FI}
{LINE: 1}
{exec('kh_dod_ini','kontrahent',PAR_NAG.KH1);~~}
{IF: |KH_DOD.NASZ_KOD<>''}{<1 texty[13]+'  '+|KH_DOD.NASZ_KOD}{FI}
{LINE: 1}
{IF: PAR_NAG.SER_DEF<>null}
{ PAR_NAG.SER_DEF(); SER_DEFW.index('SERDEFW1'); SER_DEFW.prefix(SER_DEF.ref(),SER_KOR.JEZYK); ~~}
{IF: SER_DEFW.first()}
   { _tmp:=SER_DEFW.memo_txt(0,1,'TEKST'); STR.spl_word(); STR.split(_tmp,'\n'); ~~}
   {WHILE: STR.next()}
   {DO}
      { memo:=STR.get_word(); ~~}
      {IF: |memo<>''}{<1 memo}{FI}
   {OD}
{FI}
{FI}
{LINE: 1}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEADX'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEADX'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{IF: _a:=PAR_SKID.get(34); _a='Z' | _a='A'}
{FOOTER}
{SUBSTITUTE: 'FOOT_LIC'}
{~~}
{~~}
{~~}
{FOOTER-}
{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; ~~}
{FOR: 'PAR_POZ'}
{INDEX: 'PAR_POZ'}
{PREFIX: PAR_NAG.ref}
{DO}
   { w[1]:=PAR_POZ.KONTO;
     w[2]:=PAR_POZ.OP+{? PAR_POZ.SYM_ZEW<>'' & PAR_POZ.SYM_ZEW<>PAR_POZ.OP
                      || ' ('+PAR_POZ.SYM_ZEW+')'
                      || ''
                      ?};
     w[3]:=PAR_POZ.TZ$1;
     OP.cntx_psh();
     OP.index('KON_O'); OP.prefix(SSTALE.WAL,PAR_NAG.ODD,PAR_POZ.KONTO,PAR_POZ.OP,PAR_POZ.OP,PAR_POZ.SYM_ROK);
     OP.first(); w[10]:=OP.DO$1;
     OP.cntx_pop();
     w[4]:={? -PAR_POZ.STR='wn' || sum_1+=PAR_POZ.SAL; form(PAR_POZ.SAL,,2,' ,') || form(0,,2,' ,') ?};
     w[5]:={? -PAR_POZ.STR='ma' || sum_2+=PAR_POZ.SAL; form(PAR_POZ.SAL,,2,' ,') || form(0,,2,' ,') ?};
     w[6]:={? -PAR_POZ.STR='ma' || sum_3+=PAR_POZ.KWOTA; form(PAR_POZ.KWOTA,,2,' ,') || form(0,,2,' ,') ?};
     w[7]:={? -PAR_POZ.STR='wn' || sum_4+=PAR_POZ.KWOTA; form(PAR_POZ.KWOTA,,2,' ,') || form(0,,2,' ,') ?};
     w[8]:={? -PAR_POZ.STR='wn'
           || sum_5+=(PAR_POZ.SAL-PAR_POZ.KWOTA); form((PAR_POZ.SAL-PAR_POZ.KWOTA),,2,' ,')
           || form(0,,2,' ,')
           ?};
     w[9]:={? -PAR_POZ.STR='ma'
           || sum_6+=(PAR_POZ.SAL-PAR_POZ.KWOTA); form((PAR_POZ.SAL-PAR_POZ.KWOTA),,2,' ,')
           || form(0,,2,' ,')
           ?}; ~~}
   {IF: (rsep & (lineleft()=2 | lineleft()=1)) | (~rsep & lineleft()=1)}{SUBSTITUTE: 'LINIAF'}{IF: lineleft()<>0}{PAGE}{FI}{FI}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{  w[1]:=w[3]:=w[10]:=''; w[2]:=texty[11]; w[4]:=form(sum_1,,2,' ,'); w[5]:=form(sum_2,,2,' ,');
   w[6]:=form(sum_3,,2,' ,'); w[7]:=form(sum_4,,2,' ,'); w[8]:=form(sum_5,,2,' ,'); w[9]:=form(sum_6,,2,' ,'); rsep:=1; ~~}
{IF: lineleft()=1 | lineleft()=2}{SUBSTITUTE: 'LINIAF'}{IF: lineleft()<>0}{PAGE}{FI}{FI}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'LINIAF'}
:!UTF-8
{TITLE:H. Raport niezrealizowanych zleceń}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  ZL.cntx_psh;
  ZLGD.cntx_psh;
  ZGH.cntx_psh();
  ZGP.cntx_psh();
  VAR_DEL.delete('PRN');
  PRN:= obj_new(@.CLASS.PRINT, 10);
  VAR_DEL.delete('__TAB','__TMP');
  __TAB:=tab_tmp(1,
     'DATA','DATE','DataRealizacji',
     'ZLREF','INTEGER','Ref do zlecenia',
     'SYM','STRING[20]','Symbol',
     'KOD','STRING[20]','Kod wyrobu',
     'NAZWA','STRING[40]','Nazwa wyrobu',
     'ODD','DATE','Data otwarcia',
     'IL','REAL','Ilosc wykonana',
     'ILWTOK','REAL','Ilosc w toku',
     'ILDOWYK','REAL','Ilosc do wykonania',
     'PRE','REAL','Procect realizacji',
     'REH','REAL','Prc. real',
     'JM','STRING[10]','Jednostka miary'
  );
  __TMP:=tab_tmp(2,
     'ZL','INTEGER','Ref do zl',
     'TIME','REAL','Czas'
  );
  PRN.sl_frame();
  tryb:=rep_export();
  VAR.A_DAT:=date(0,0,0);
  prn1:='PRN';
  waslbar:=0;
  sumax:=0;
  sumin:=0;
  dalej:=0;
  mode:=0;
  bylo:=0;
  ok:=0;
  dat:=date(0,0,0);
  color:='';
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  ZL.cntx_pop;
  ZLGD.cntx_pop;
  ZGH.cntx_pop();
  ZGP.cntx_pop();
  obj_del(PRN);
  obj_del(__TMP);
  obj_del(__TAB);
  &dalej;
  &bylo;
  &prn1;
  &color;
  &lm; &ll; &vp; &lmt; &rsep; &tryb
}
{COMMENT}
  Wydruk zlecen niezrealizowanych.
   [MK] - 2002/11/28 - [7.62]
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
   {
      mode:=FUN.choice('Zakres zleceń'@,,'Wszystkie otwarte zlecenia'@,'Kończące się dnia...'@);
      data:=date(0,0,0);
      {? mode=2
      || VAR.A_DAT:=date();
         VAR.win_edit('A_DAT');
         {? VAR.edit("exec('dobra_data','zl_common',VAR.A_DAT)")
         || data:=VAR.A_DAT
         || data:=date(0,0,0);
            mode:=0
         ?}
      ?};
      ~~
  }
{IF: mode=0}
{EXIT}
{FI}
{COMMENT}--Definicja wiersza wydruku zlecen niezrealizowanych.--------------------{COMMENT-}
{
{? tryb=1
|| PRN.add("form(__TAB.DATA)",18,'Nie zrealizowane w dniu'@)
?};
PRN.add("__TAB.KOD",18,'Kod wyrobu'@);
PRN.add("__TAB.NAZWA",20,'Nazwa wyrobu'@);
PRN.add("__TAB.SYM",20,'Zlecenie'@);
PRN.add("form(__TAB.ODD)",10,'Data otwarcia'@);
PRN.add("form(__TAB.IL,,4)",12,'Ilość wykonana'@,1);
PRN.add("form(__TAB.ILWTOK,,4)",12,'Ilość w toku'@,1);
PRN.add("form(__TAB.ILDOWYK,,4)",12,'Ilość do wykonania'@,1);
PRN.add("__TAB.JM",6,'Jedn. miary'@);
PRN.add("form(__TAB.PRE,,2)",6,'%% real. wg ilości'@,1);
PRN.add("form(__TAB.REH,,2)",6,'%% real. wg godzin'@,1);
PAR_WYDR.TITLE:='RAPORT ZLECEŃ NIEZREALIZOWANYCH'@;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'Rok: %1 Okres: %2'@[$ST.AR,form(ST.AM,-2)]}
{IF: tryb<>1 &data<>date(0,0,0)}
{<{lmt+1} 'Zlecenia niezrealizowane w dniu: %1'@[form(data)]}
{FI}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{MACRO: 'minmax'}
{_czas:=0;
ZL.cntx_psh();
ZL.clear;
ZL.index('STAN');
ZL.prefix('N','O');
{? ZL.first()
|| {!
   |?
      _czas:=0;
      _ilosc:=0;
      _ilwtok:=0;
      {? ZL.RODZAJ='P'
      || {? ((ZL.DTR=data & ZL.DTR<>date(0,0,0) & ZL.DTR<=date) |
            (data=date(0,0,0) & ZL.DTR<=date & ZL.DTR<>date(0,0,0)))
         ||
            ZGH.clear();
            ZGH.index('ZLNR');
            ZGH.prefix(ZL.ref);
            {? ZGH.first()
            || {!
               |?
                  {? ZGH.STAN='T'
                  || _ilwtok+=ZGH.ILGEN;
                     ZGP.index('PNRPP');
                     ZGP.prefix(ZGH.ref);
                     {? ZGP.first()
                     || {!
                        |?
                           _czas+=ZGP.NTIME;
                           ZGP.next
                        !}
                     ?}
                  ||
                     ZGP.index('PNRPP');
                     ZGP.prefix(ZGH.ref);
                     _ilmax:=0;
                     {? ZGP.first()
                     || _ilmax:=ZGP.IL;
                        _il1:=0;
                        {!
                        |?
                           _czas+=ZGP.NTIME;
                           _il1+=ZGP.IL;
                           {? _il1>_ilmax || _ilmax:=_il1 ?};
                           _il1:=0;
                           ZGP.next
                        !}
                     ?};
                     _ilwtok+=_ilmax
                  ?};
                  ZGH.next()
               !}
            ?};
            {? _czas<>0
            ||
               __TAB.DATA:=ZL.DTR;
               __TAB.ZLREF:=#ZL.ref;
               __TAB.SYM:=ZL.SYM;
               __TAB.KOD:=ZL.KTM().KTM;
               __TAB.NAZWA:=ZL.KTM().N;
               __TAB.JM:=ZL.KTM().J().KOD;
               __TAB.ODD:=ZL.OD;
               __TAB.IL:=ZL.ILWYK;
               __TAB.ILWTOK:=|(_ilwtok-ZL.ILWYK);
               __TAB.ILDOWYK:=ZL.IL-ZL.ILWYK;
               __TAB.PRE:={? ZL.IL<>0
                          ||(ZL.ILWYK/ZL.IL)*100
                          || 0
                          ?};
               __TAB.REH:=(ZL.HWYK/_czas)*100;
               __TAB.add
            ?}
         ?}
      ?};
      ZL.next()
   !}
?};
ZL.cntx_pop()
;~~}
{MACRO-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP;lk:=charleft();~~}
{SUBSTITUTE: 'minmax'}
{__TAB.prefix();~~}
{IF:__TAB.first}
{IF: data<>date(0,0,0)}
{FOR: __TAB}
  {DO}
    {FIRST}
     {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1;bylo:=1;~~}
    {FIRST-}
    {SUBSTITUTE: 'LINIA0'}
  {OD}
{IF: bylo}
{SUBSTITUTE: 'LINIAF'}
{FI}
{ELSE}
{__TAB.first;dat:=date(0,0,0);~~}
{FOR: __TAB}
{DO}
    {FIRST}
     {IF: ~tryb & lineleft<15}
     {PAGE}
     {FI}
     {IF: tryb<>1}{<{lmt+1} 'Zlecenia niezrealizowane w dniu: %1'@[form(__TAB.DATA)]}{FI}
     {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1;~~}
     {dat:=__TAB.DATA;~~}
   {FIRST-}
   {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIA0'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
     {FI}
{TOTAL: __TAB.DATA}
   {IF: tryb<>1}
   {IF: dat<>__TAB.DATA}
   {SUBSTITUTE: 'LINIAF'}
   {IF: ~tryb & lineleft<15}
   {PAGE}
   {FI}
   {<{lmt+1} 'Zlecenia niezrealizowane w dniu: %1'@[form(__TAB.DATA)]}
   {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
   {dat:=__TAB.DATA;~~}
   {FI}
   {FI}
{OD}
{IF: bylo}
{SUBSTITUTE: 'LINIAF'}
{FI}
{FI}
{ELSE}

{'Brak rekordów spełniających warunek.'@}
{EXIT}
{FI}

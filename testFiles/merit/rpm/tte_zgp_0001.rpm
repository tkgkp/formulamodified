:!UTF-8
{TITLE:1. Przewodnik do zlecenia}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','v'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   ZL.cntx_psh();
   ZGH.cntx_psh();
   ZGP.cntx_psh();
   ZTP.cntx_psh();
   ZLIM.cntx_psh();
   TMAT.cntx_psh();
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=1;
   exec('tktl_use','tech_common');
   exec('openmask','zl_common',ZGH.ZLEC);
   ktl:={? ZGH.ZLEC().TYP().TECH='T'
        || {? ZL.TKTL<>null() || ZL.TKTL || exec('FindAndGet','#table',TKTL,ZL.RTKTL,,,null()) ?}
        || {? ZL.KTL<>null() || ZL.KTL || exec('FindAndGet','#table',TKTL,ZL.RKTL,,,null()) ?}
        ?};
   sur_ktl:=1;
   sur_grp:=sur_ktl;
   _maska:=((8+$ktl)+3);
   {? _maska<>''
   || TMAT.use('txmat'+_maska)
   ?};
   xjm:=exec('FindAndGet','#table',TKTL,ktl,,"XJM",1);
   wsp:=ZGH.ILNPRZ/xjm;
   dalej:=0; dalej2:=0; dalej3:=0;
   zlec:=ZGH.ZLEC().NRNZL;
   zloz:=ZGH.ZLEC().RODZAJ<>'P';
   zgh:=0;
   temp:='';
   pop:='';
   nas:='';
   kreska:= '─';
   ramka:='│';
   spacja:={? exec('get','#params', 100236, 2)='N' || ^160 || ' ' ?};
   color_bl:="'0:0:1'";
   {? var_press('PRN')>100 || obj_del(PRN) ?};
   PRN:=obj_new(@.CLASS.PRINT,20,spacja,spacja,spacja,kreska,spacja ,kreska,kreska,spacja);
   {? var_press('PRN2')>100 || obj_del(PRN2) ?};
   PRN2:=obj_new(@.CLASS.PRINT,20,spacja,spacja ,spacja,kreska ,spacja ,kreska,kreska,spacja);
   prn1:='PRN';
   prn2:='';
   color:=color_h:='';
   lim:=nlim:=odp:=pf:=typ:=kody:=szczeg:=fazy:=0;
   pf:=(ZL.TREE_TYP='M' | ZL.TREE_TYP='F') & exec('czy_pf','tech_mater',ktl);
   czy_sur:=0;
   tmat:=0;
   {? var_press('size_prn')>100 || obj_del(size_prn) ?};
   size_prn:=obj_new(20);
   {? var_press('size_prn2')>100 || obj_del(size_prn2) ?};
   size_prn2:=obj_new(20);
   {! _i:=1..obj_len(size_prn) |! size_prn[_i]:=0 !};
   {! _i:=1..obj_len(size_prn2) |! size_prn2[_i]:=0 !};
   size_nast:=size_oper:=size_uwagi:=0;
   prn_width:=prn2_width:=max_width:=0;
   manual:=0;
   cdn:=0; head_sur:=1;
   lm:=ll:=vp:=lmt:=lmt_kreski:=rsep:=0
}
{END:
   ZL.cntx_pop();
   ZGH.cntx_pop();
   ZGP.cntx_pop();
   ZTP.cntx_pop();
   ZLIM.cntx_pop();
   TMAT.cntx_pop();
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   &ktl;
   &sur_ktl;
   &sur_grp;
   &dalej; &dalej2; &dalej3;
   &xjm; &wsp;
   &zlec;
   &zloz;
   &zgh;
   &temp;
   &pop;
   &nas;
   &kreska;
   &ramka;
   &spacja;
   &color_bl;
   obj_del(PRN);
   obj_del(PRN2);
   obj_del(size_prn);
   obj_del(size_prn2);
   &prn1;
   &prn2;
   &color; &color_h;
   &manual;
   &cdn; &head_sur;
   &lim; &nlim; &odp; &pf; &typ; &kody; &szczeg; &fazy;
   &czy_sur;
   &tmat;
   &size_nast; &size_oper; &size_uwagi;
   &prn_width; &prn2_width; &max_width;
   {? var_pres('ndx_tmp')>0
   || ZLIM.ndx_drop(ndx_tmp);
      &ndx_tmp
   ?};
   {? var_pres('ndx_tmp_tmat')>0
   || TMAT.ndx_drop(ndx_tmp_tmat);
      &ndx_tmp_tmat
   ?};
   exec('stop_tpar','tech_param');
   &lm; &ll; &vp; &lmt; &lmt_kreski; &rsep
}
{DEFINEFONT: 'kod=TexGyreCursor,160,x,8'}
{COMMENT}
  Przewodnik do zlecenia.
  [SS] - 2000/09/13 - [7.22]
  [TS] - 2001/10/03 - [7.53]
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Definicje -> Przewodniki -> Drukuj
{COMMENT-}
{MACRO:'stang'}
   {temp:={? ZGP.IDEAN<>'' || ZGP.IDEAN || form(#ZGP.ref(),-7,,'99') ?};~~}
   {<{(lmt_kreski+10)}}{BARCODE: temp;20;BARCODE_H;BARCODE_TYPE}
   {FONT: 'T8G'}{SPACE: 'B'}
{MACRO-}
{MACRO:'stang_zlim'}
   {temp:={? tmat || form(TMAT.PT().KODK) || form(ZLIM.KTM().KODK)?};~~}
   {FONT: 'kreski'}{SPACE: 'B'}
   {<{(lmt_kreski)}}{BARCODE: temp;20;BARCODE_H;BARCODE_TYPE}
   {FONT: 'T8G'}{SPACE: 'B'}
{MACRO-}
{MACRO: 'LINIA0Q'}
{  ll:=lineleft(); ~~}
{IF:  params_set(params_get());
      _f:='params_set(params_get()); '+prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; _v:=($(_f))();
      ll<(rsep+_v+{? kody || 2 || 0 ?}) & ll<>0}{PAGE}{FI}
{ENTIRE}
{IF: vp}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   {ELIF: rsep}
      {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   {FI}
{FI}
{WHILE: _f:=prn1+'.next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
{OD}
{IF: var_pres('fun')>=0 & fun<>''}
{  $fun(); ~~}
{FI}
{ENTIRE-}
{MACRO-}
{MACRO: 'TABHEADQ'}
{  ll:=lineleft(); ~~}
{IF:  params_set(params_get());
      _f:='params_set(params_get()); '+prn1+'.ini_head()'; _v:=($(_f))();
      _f:=prn1+'.hlength()'; _v:=($(_f))();
      ll<(rsep+_v) & ll<>0}{PAGE}{FI}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
{WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
{DO}
   {IF: prn1='PRN2' & sur_grp=0}
   {REP: _f:=prn1+'.h_fline(lmt)'; ($(_f))()}
   {ELSE}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {FI}
{OD}
{MACRO-}
{COMMENT}--------------------------PARAMETRY WYDRUKU----------------{COMMENT-}
{ _wydr_naz:='zgp_001';
  {? ~sur_ktl;1
  || WYDRUK.efld_opt('ZGP_001','enable=1',,'SUR_OPER')
  || WYDRUK.efld_opt('ZGP_001','enable=0',,'SUR_OPER')
  ?};
  {? pf
  || WYDRUK.efld_opt('ZGP_001','enable=1',,'PF')
  || WYDRUK.efld_opt('ZGP_001','enable=0',,'PF')
  ?};
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.CHKBOX01:=1;
     WYDRUKIL.CHKBOX02:=0;
     WYDRUKIL.CHKBOX03:=1;
     WYDRUKIL.CHKBOX04:=1;
     WYDRUKIL.CHKBOX05:=1;
     WYDRUKIL.CHKBOX06:=0;
     WYDRUKIL.CHKBOX06:=1;
     WYDRUKIL.CHKBOX07:=1;
     WYDRUKIL.CHKBOX08:=0;
     WYDRUKIL.CHKBOX09:=0;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.KODY:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?};
     WYDRUK.SZCZEG:={? WYDRUKIL.CHKBOX02 || 'T' || 'N' ?};
     WYDRUK.SUR:={? WYDRUKIL.CHKBOX03 || 'T' || 'N' ?};
     WYDRUK.ZAM:={? WYDRUKIL.CHKBOX04 || 'T' || 'N' ?};
     WYDRUK.OADR:={? WYDRUKIL.CHKBOX05 || 'T' || 'N' ?};
     WYDRUK.TOW:={? WYDRUKIL.CHKBOX06 || 'T' || 'N' ?};
     WYDRUK.FAZY:={? WYDRUKIL.CHKBOX07 || 'T' || 'N' ?};
     WYDRUK.SUR_OPER:={? WYDRUKIL.CHKBOX08 || 'T' || 'N' ?}
  ?};
  WYDRUK.win_edit('ZGP_001');~~}
{IF: WYDRUK.edit()}
{  WYDRUKIL.CHKBOX01:=(WYDRUK.KODY='T');
   WYDRUKIL.CHKBOX02:=(WYDRUK.SZCZEG='T');
   WYDRUKIL.CHKBOX03:=(WYDRUK.SUR='T');
   WYDRUKIL.CHKBOX04:=(WYDRUK.ZAM='T');
   WYDRUKIL.CHKBOX05:=(WYDRUK.OADR='T');
   WYDRUKIL.CHKBOX06:=(WYDRUK.TOW='T');
   WYDRUKIL.CHKBOX07:=(WYDRUK.FAZY='T');
   WYDRUKIL.CHKBOX08:=(WYDRUK.SUR_OPER='T');
   WYDRUKIL.CHKBOX09:=(WYDRUK.PF='T');
   WYDRUKIL.put(1);
   kody:=WYDRUKIL.CHKBOX01;
   szczeg:=WYDRUKIL.CHKBOX02;
   lim:=WYDRUKIL.CHKBOX03;
   nlim:=WYDRUKIL.CHKBOX04;
   odp:=WYDRUKIL.CHKBOX05;
   typ:=WYDRUKIL.CHKBOX06;
   fazy:=WYDRUKIL.CHKBOX07;
   pf:=pf & WYDRUKIL.CHKBOX09;
   czy_sur:=lim | nlim | odp | pf;
   sur_grp:=~WYDRUKIL.CHKBOX08;
~~}
{ELSE}
{EXIT}
{FI}
{ {? sur_grp
  || ndx_tmp_tmat:=TMAT.ndx_tmp(,1,'ACT',,,'LIMIT',,,'NRK',,,'MAG',,,'LP',,)
  || ndx_tmp_tmat:=TMAT.ndx_tmp(,1,'ACT',,,'LIMIT',,,'NRK',,,'MAG',,,'NROP',,,'LP',,)
  ?};
  TMAT.index(ndx_tmp_tmat);

  exec('stop_tpar','tech_param');
  tpar:=obj_new(@.CLASS.TPAr);
  tpar.TABLE:=TPAR;
  tpar.INDEX:='NN';
  tpar.PREFIX:=ktl;
  tpar.KTM:=ZL.KTM;
  tpar.loadp();

  _col_size1:=1;
  _col_size2:=1;
  _sum_prn2:=0;
  {? sur_grp=0
  || _sum_prn2+=6;
     {? var_press('PRN2')>100 || obj_del(PRN2) ?};
     PRN2:=obj_new(@.CLASS.PRINT,20,spacja,spacja,spacja,spacja,spacja,spacja,spacja,spacja)
  ?};
  'Kod:';size_prn2[1]:=43; 'Indeks:';size_prn2[2]:=20; 'Nazwa:'; size_prn2[3]:=25;  'Rodzaj:';size_prn2[4]:=11;
  'Operacja:';size_prn2[5]:=10; 'Faza:';size_prn2[6]:=10; 'Jm:';size_prn2[7]:=7; 'Ilość:';size_prn2[8]:=12;
  {? kody
  || PRN2.add("' '",size_prn2[1],'Kod kreskowy'@,,,color_bl,color_bl);
     _sum_prn2+=size_prn2[1]+_col_size1
  ?};
  PRN2.add("{? tmat || form(TMAT.PT().KTM) || form(ZLIM.KTM().KTM) ?}",size_prn2[2],'Indeks'@,,,color_bl,color_bl);
  _sum_prn2+=size_prn2[2]+_col_size1;
  PRN2.add("{? tmat || form(TMAT.PT().N) || form(ZLIM.KTM().N) ?}",size_prn2[3],'Nazwa'@,,,color_bl,color_bl);
  _sum_prn2+=size_prn2[3]+_col_size1;
  {? typ
  || PRN2.add("{? tmat || 'Półfabrykat'@ ||{? ZLIM.SO='O' || 'Odpad'@ |? ZLIM.LIMIT='T' || 'Limit'@ || 'Nielimit'@ ?}?}"
        ,size_prn2[4],'Rodzaj'@,,,color_bl,color_bl);
     _sum_prn2+=size_prn2[4]+_col_size1
  ?};
  {? sur_ktl=0 & sur_grp>0;0
  || PRN2.add("ZLIM.ZGP().NRP",size_prn2[5],'Operacja'@,1,,,color_bl,color_bl);
     _sum_prn2+=size_prn2[5]+_col_size1
  ?};
  {? fazy
  || PRN2.add("{? tmat || TMAT.PFAZ().KOD || ZLIM.PFAZ().KOD ?}",size_prn2[6],'Faza prod.'@,,,color_bl,color_bl);
     _sum_prn2+=size_prn2[6]+_col_size1
  ?};
  PRN2.add("{? tmat || form(TMAT.PT().J().KOD) || form(ZLIM.KTM().J().KOD) ?}",size_prn2[7],'jm'@,,,color_bl,color_bl);
  _sum_prn2+=size_prn2[7]+_col_size1;
  PRN2.add("{? tmat
            || _il:=0;
               _dokl:=exec('jaka_dok_m','jm',TMAT.PT);
               {? _dokl>0
               || {? TMAT.FORMB<>''
                  || _il:=tpar.calc(TMAT.FORMB)*wsp
                  || _il:=TMAT.WARB*wsp
                  ?}
               || {? TMAT.FORMB<>''
                  || _il:=exec('ceil','#math',tpar.calc(TMAT.FORMB)*wsp)
                  || _il:=exec('ceil','#math',TMAT.WARB*wsp)
                  ?}
               ?};
               form(_il,,4)
            || form({? ZLIM.SO='O' || -1 || 1 ?}*ZLIM.LIL,,4) ?}
           ",size_prn2[8],'Ilość'@,1,,,color_bl,color_bl);
  _sum_prn2+=size_prn2[8]+_col_size1;
  _sum_prn:=0;
  'Nr:';size_prn[1]:=3; 'Kod:';size_prn[2]:=27; 'Nast/pop:';size_prn[3]:=15; 'Operacja:';size_prn[4]:=25;
  'Faza:';size_prn[5]:=10; 'Nr fazy:';size_prn[6]:=5; 'Uwagi:';size_prn[7]:=19;
  _sum_prn+=size_prn[1]+{? kody || size_prn[2]+_col_size2 || 0 ?}+size_prn[3]+size_prn[4]
     +{? fazy || size_prn[5]+_col_size2+(4*(size_prn[6]+_col_size2)) || 0 ?}+size_prn[7]+(4*_col_size2);
  _left:=_sum_prn2-_sum_prn;
  _add_oper:=_add_uwagi:=0;
  {? _left>0
  || _add_oper:=int(_left/3);
     _add_uwagi:=_left-_add_oper
  ?};
  PRN.add("form(ZGP.NRP,3,0,'9')",size_prn[1],'Nr'@,,,color_bl,color_bl);
  {? kody
  || PRN.add("''",size_prn[2],'Kod kreskowy'@,,,color_bl,color_bl)
  ?};
  size_nast:=size_prn[3];
  size_oper:=size_prn[4]+_add_oper;
  size_uwagi:=size_prn[7]+_add_uwagi;
  PRN.add("form(pop-1,size_nast)+'#'+ size_nast*kreska+ '#'+ form(nas-1,size_nast)",size_nast,'Poprzedniki'@+'#'+size_nast*kreska+'#'+'Następniki'@,1,'#',,,color_bl);
  PRN.add("form(ZGP.OPIS)+' '*(size_oper-+(ZGP.OPIS)%*size_oper)+'#'+size_oper*kreska+'#'+VAR.STRING",size_oper,form('Operacja'@,size_oper)+'#'+size_oper*kreska+'#'+form('Złożone'@),,'#',,,color_bl);
  {? fazy
  || PRN.add("ZGP.PFAZ().KOD",size_prn[5],'Faza prod.'@,,,color_bl,color_bl);
     {! _i:=1..4 |! PRN.add("''",size_prn[6],$_i,,,color_bl,color_bl) !}
  ?};
  {? 0 || PRN.add("''",40,'Wykonano (Imię, nazwisko, podpis)'@) ?};
  PRN.add("{? ZGP.WEW='N' || 'Operacja zewnętrzna'@ || '' ?}",size_uwagi,'Uwagi'@,,,color_bl,color_bl);
  prn_width:=PRN.width(); prn2_width:=PRN2.width();
  max_width:={? czy_sur>0 || {? prn2_width>prn_width || prn2_width || prn_width ?} || prn_width ?};
  PAR_WYDR.TITLE:='PRZEWODNIK DO ZLECENIA'@;
~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'kreski'}{SPACE: 'B'}
{lmt_kreski:=int((charleft()-(max_width)/2)/2);~~}
{FONT: 'T8G'}{SPACE: 'B'}
{lmt:=int((charleft()-max_width)/2);~~}
{<{lmt} 'Zlecenie: %1'@[ZGH.ZLEC().SYM]}

{IF: zloz=0}
{<{lmt} 'Numer przewodnika: %1'@[ZGH.NRPRZ]}

{IF: ZGH.GKTL<>null()}
{<{lmt} 'Grupa technologii:  '+ ZGH.GKTL().GR+' - '+ZGH.GKTL().OPIS }

{FI}
{<{lmt} 'Ilość do wykonania: %1'@[form(ZGH.ILNPRZ,,4)]}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: zloz=0 & manual<>2}
   {FONT: 'T8GB'}{SPACE: 'B'}
   {IF: manual=3}{prn1:='PRN'; lmt-=6; color:=color_h; ~~}{FI}
   {SUBSTITUTE: 'TABHEADQ'}
   {IF: manual=1 | manual=3}
      { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
      {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
      { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
   {FI}
   {IF: manual=3}{prn1:='PRN2'; lmt+=3; color:='255:255:255'; ~~}{FONT: 'T8GB'}{IF: head_sur}{<{lmt}'Surowce'@}{FI}{SUBSTITUTE: 'TABHEADQ'}{FONT: 'T8G'}{ vp:=0; head_sur:=0; lmt+=3; ~~}{FI}
{FI}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: zloz=0 & manual<>2 & cdn=0}{SUBSTITUTE: 'LINIAF'}
{ELSE}{vp:=1;~~}
{FI}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}----------------------SUROWCE DO KARTY---------------------{COMMENT-}
{IF: czy_sur>0}
   {IF: sur_grp>0}
      {{? sur_ktl>0;1
       || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'ZGP','NRZLP',,'LIMIT',,1,'SO',,1,'KTM','KTM',0)
       || ndx_tmp:=ZLIM.ndx_tmp(,1,'ZLEC','',0,'ZGP','NRZLP',,'LIMIT',,1,'SO',,1,'ZGP','NRP',0,'KTM','KTM',0)
       ?};
       ZLIM.index(ndx_tmp); ZLIM.prefix(ZGH.ZLEC);
       rsep:=PAR_WYDR.RSEP;
       dalej:=
         {? lim || ZLIM.find_tab('first','ZGP','NRZLP','=',ZGH.ref(),'SO',,'=','S','LIMIT',,'=','T','KOR',,'=',0,
                                 'KOR2',,'=',0) || 0 ?}
         | {? nlim || ZLIM.find_tab('first','ZGP','NRZLP','=',ZGH.ref(),'SO',,'=','S','LIMIT',,'=','N') || 0 ?}
         | {? odp || ZLIM.find_tab('first','ZGP','NRZLP','=',ZGH.ref(),'SO',,'=','O','LIMIT',,'=','T') || 0 ?};
       dalej3:=0;
       {? pf>0
       || TMAT.prefix('T','T',ktl,'N');
          {? TMAT.find_tab('first','KTL',,'<>',null()) | TMAT.find_tab('first','DFLT_KTL',,'=','T')
          || dalej3:=1;
             TMAT.first()
          ?}
       ?};
       ~~}
      {IF: dalej | dalej3}
         {ZLIM.find_tab('first','ZGP','NRZLP','=',ZGH.ref());~~}
         {LINE}
         {FONT: 'T8GB'}{<{lmt}'Surowce'@}{FONT: 'T8GB'}
         {prn1:='PRN2';~~}{SUBSTITUTE: 'TABHEADQ'}{FONT: 'T8G'}{ vp:=1; ~~}
         {WHILE: dalej}
         {DO}
            {IF: ((lim>0 & ZLIM.SO='S' & ZLIM.LIMIT='T') | (nlim>0 & ZLIM.SO='S' & ZLIM.LIMIT='N') |
                 (odp>0 & ZLIM.SO='O' & ZLIM.LIMIT='T')) & ZLIM.KOR=0 & ZLIM.KOR2=0}
               {SUBSTITUTE: 'LINIA0Q'}
               {IF: kody}{SUBSTITUTE: 'stang_zlim'}{FI}
            {FI}
            {dalej:=ZLIM.find_tab('next','ZGP','NRZLP','=',ZGH.ref()); ~~}
         {OD}
         {WHILE: dalej3}
         {DO}
            {FIRST}
            {tmat:=1; ~~}
            {FIRST-}
            {IF: TMAT.KTL<>null() | TMAT.DFLT_KTL='T'}
               {SUBSTITUTE: 'LINIA0Q'}
               {IF: kody}{SUBSTITUTE: 'stang_zlim'}{FI}
            {FI}
            {dalej3:=TMAT.next(); ~~}
         {OD}
         {IF:lineleft()=1}{manual:=2;~~}{FI}
         {IF:lineleft()>0}{SUBSTITUTE: 'LINIAF'}{FI}
         {manual:=0; tmat:=0; ~~}
      {FI}
   {FI}
{FI}
{COMMENT}----------------------NAGŁÓWEK TABELI OPERACJI---------------------{COMMENT-}
{IF: lineleft()<={? kody || 10 || 8 ?}}
{prn1:='PRN';manual:=2;~~}
{IF: lineleft()<>0}{PAGE}{FI}
{FONT: 'T8GB'}{<{lmt}'Operacje'@}{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEADQ'}{FONT: 'T8G'}{ vp:=1; ~~}
{manual:=0;~~}
{ELSE}
{prn1:='PRN';dalej:=0;~~}
{FONT: 'T8GB'}{<{lmt}'Operacje'@}{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEADQ'}{FONT: 'T8G'}{ vp:=1; ~~}
{FI}
{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP;~~}
{IF: zloz}
{FOR: ZGP}
{INDEX: 'NRPP'}
{PREFIX: ZGH.ref}
{DO}
{FIRST}
{zgh:=#ZGP.NRZLP;~~}
{IF: lineleft<15}
{PAGE}
{FI}
{<{lmt} 'Zlecenie podrzędne: %1'@[ZGP.NRZLP().ZLEC().SYM]}


{<{lmt} 'Numer przewodnika zlec. podrzędnego: %1'@[ZGP.NRZLP().NRPRZ]}

{<{lmt} 'Ilość do wykonania: %1'@[form(ZGP.NRZLP().ILNPRZ,,4)]}
{<{lmt} 'Numer przewodnika zlec nadrzędnego: %1'@[ZGP.NRPRZ().NRPRZ]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FIRST-}
   {VAR.STRING:='';
    TOPER.cntx_psh();
    {? ZGP.TOPER().NRNOP<>0
    ||
       TOPER.index('UNROP');
       TOPER.prefix(ZGP.TOPER().NRNOP);
       {? TOPER.first()
       || _nrop:=ZGP.TOPER().NRNOP;
          {! |?
             TOPER.prefix(_nrop);
             {? TOPER.first()
             ||  VAR.STRING:=form(TOPER.NROP)+'.'+TOPER.NA+'/'+VAR.STRING;
                _nrop:=TOPER.NRNOP;
                TOPER.NRNOP<>0
             || 0
             ?}
          !}
     ?}
   ?};
   {? VAR.STRING<>''
   || VAR.STRING:=VAR.STRING-1;
     {? +VAR.STRING>31
     || {! |?
           _str:=VAR.STRING;
           _nr:=0;
          {! |?
             _nr+=_str*'/';
             _str:=_str*'/'-_str;
             _str*'/'>0
          !};
          VAR.STRING:=_nr+VAR.STRING;
          VAR.STRING:=VAR.STRING-1;
          _str:=VAR.STRING;
          +VAR.STRING>31
        !};
        VAR.STRING+='/...'
     ?}
   || VAR.STRING:='Brak'
   ?};
   TOPER.cntx_pop();~~}
   {pop:='';
   nas:='';
   ZGP.cntx_psh();
   NASZGP.cntx_psh();
   NASZGP.index('OPNAST');
   NASZGP.prefix(ZGP.ref);
   {? NASZGP.first
   || {!|?
        nas+=NASZGP.SCIEZKA+',';
        NASZGP.next
      !}
   ?};
   NASZGP.cntx_pop();
   NASZGP.index('NASTOP');
   NASZGP.prefix(ZGP.ref);
   {? NASZGP.first
   || {!|?
        pop+=$NASZGP.OPER().NRP+',';
        NASZGP.next
      !}
   ?};
   ZGP.cntx_pop()
   ;~~}
 {IF: lineleft > 10} {SUBSTITUTE: 'LINIA0'}
 {IF:kody}
     {SUBSTITUTE: 'stang'}
   {FI}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8GB'}{SPACE:'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {IF:kody}
     {SUBSTITUTE: 'stang'}
   {FI}
 {FI}
{TOTAL: #ZGP.NRZLP}
{SUBSTITUTE: 'LINIAF'}
{IF: zgh<>#ZGP.NRZLP}
   {IF: lineleft<15}
   {PAGE}
   {FI}


{<{lmt} 'Zlecenie podrzędne: %1'@[ZGP.NRZLP().ZLEC().SYM]}


{<{lmt} 'Numer przewodnika zlec. podrzędnego: %1'@[ZGP.NRZLP().NRPRZ]}


{<{lmt} 'Ilość do wykonania: %1'@[form(ZGP.NRZLP().ILNPRZ,,4)]}
{<{lmt} 'Numer przewodnika zlec nadrzędnego: %1'@[ZGP.NRPRZ().NRPRZ]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; zgh:=#ZGP.NRZLP;~~}
{FI}
{OD}
{ELSE}
   {IF: zlec=0}
   {ZGP.index('NRPP'); ZGP.prefix(ZGH.ref()); ~~}
   {ELSE}
   {ZGP.index('PNRPP'); ZGP.prefix(ZGH.ref()); ~~}
   {FI}
   {IF: ZGP.first()}{ dalej:=1; ~~}{FI}
   {WHILE: dalej}
   {DO}
   {VAR.STRING:='';
    TOPER.cntx_psh();
    {? ZGP.TOPER().NRNOP<>0
    || TOPER.index('UNROP');
       TOPER.prefix(ZGP.TOPER().NRNOP);
       {? TOPER.first()
       || _nrop:=ZGP.TOPER().NRNOP;
          {!
          |?
             TOPER.prefix(_nrop);
             {? TOPER.first()
             || VAR.STRING:=form(TOPER.NROP)+'.'+TOPER.NA+'/'+VAR.STRING;
                _nrop:=TOPER.NRNOP;
                TOPER.NRNOP<>0
             || 0
             ?}
          !}
       ?}
    ?};
   {? VAR.STRING<>''
   || VAR.STRING:=VAR.STRING-1;
     {? +VAR.STRING>31
     || {! |?
           _str:=VAR.STRING;
           _nr:=0;
          {! |?
             _nr+=_str*'/';
             _str:=_str*'/'-_str;
             _str*'/'>0
          !};
          VAR.STRING:=_nr+VAR.STRING;
          VAR.STRING:=VAR.STRING-1;
          _str:=VAR.STRING;
          +VAR.STRING>31
        !};
        VAR.STRING+='/...'
     ?}
   || VAR.STRING:='Brak'@
   ?};
   TOPER.cntx_pop();~~}
   {pop:='';
   nas:='';
   ZGP.cntx_psh();
   NASZGP.cntx_psh();
   NASZGP.index('OPNAST');
   NASZGP.prefix(ZGP.ref());
   {? NASZGP.first()
   || {!|?
        nas+=NASZGP.SCIEZKA+',';
        NASZGP.next()
      !}
   ?};
   NASZGP.cntx_pop();
   NASZGP.index('NASTOP');
   NASZGP.prefix(ZGP.ref());
   {? NASZGP.first()
   || {!|?
        pop+=$NASZGP.OPER().NRP+',';
        NASZGP.next()
      !}
   ?};
   ZGP.cntx_pop()
   ;~~}
   {SUBSTITUTE: 'LINIA0Q'}
   {IF:kody}
      {SUBSTITUTE: 'stang'}
   {FI}
   {FONT: 'T8G'}
{COMMENT}----------------------OPIS SZCZEGÓŁOWY---------------------{COMMENT-}
   {IF: szczeg & +ZGP.memo_txt(,1,'OPISMEMO')>0}
      {cdn:=1;VAR_DEL.delete('__memo','text','Text');~~}
      { __memo:=ZGP.memo_get('r','OPISMEMO'); text:='';~~}
      {IF: __memo.is_open}
         {WHILE: text:=fread(__memo); text<>'\n'}
         {DO}
            {
            max_T8:=prn_width-5;
            STR.split(text);
            Text:='';
            sep:=0;
            manual:=1
            ;~~}
            {IF: lineleft()=1}{cdn:=1;~~}{PAGE}{FI}
            {WHILE: STR.next()}
            {DO}
               {FIRST}{IF: lineleft()<>0}{LINE}{FI}{FIRST-}
               { word:=STR.get_word(); sep+=1;~~}
               {IF: +Text+(+word)>=(max_T8)}
                  {IF: lineleft()=1}{cdn:=1;~~}{PAGE}{FI}
                  {<{lmt+5}}{JUSTIFY}{<{lmt+5}>{max_T8+lmt+5}Text}
                  { Text:=word; sep:=1;~~}
               {ELSE}
                  { Text+={? sep>1 || ' ' || '' ?}+word;~~}
               {FI}
            {OD}
            {IF: +Text}
               {cdn:=0; ~~}
               {<{lmt+5}}{Text}
            {FI}
         {OD}
         {CENTER}
      {FI}
      {cdn:=0; VAR_DEL.delete('__memo','text','Text'); manual:=0; ~~}
   {FI}
{COMMENT}----------------------SUROWCE DO OPERACJI---------------------{COMMENT-}
   {IF: czy_sur>0}
      {IF: sur_grp=0}
         {ndx_tmp:=ZLIM.ndx_tmp(,1,'ZGP',,,'LIMIT',,1,'SO',,1,'KTM','KTM',0);
          ZLIM.index(ndx_tmp); ZLIM.prefix(ZGP.ref());
          rsep:=PAR_WYDR.RSEP;
          dalej2:=
            {? lim || ZLIM.find_tab('first','SO',,'=','S','LIMIT',,'=','T','KOR',,'=',0,'KOR2',,'=',0) || 0 ?}
            | {? nlim || ZLIM.find_tab('first','SO',,'=','S','LIMIT',,'=','N') || 0 ?}
            | {? odp || ZLIM.find_tab('first','SO',,'=','O','LIMIT',,'=','T') || 0 ?};
          dalej3:=0;
          {? pf>0
          || TMAT.prefix('T','T',ktl,'N',ZGP.TOPER);
             {? TMAT.find_tab('first','KTL',,'<>',null()) | TMAT.find_tab('first','DFLT_KTL',,'=','T')
             || dalej3:=1;
                TMAT.first()
             ?}
          ?};
          ~~}
          {IF: dalej2 | dalej3}
            {ZLIM.first();~~}
            {FONT: 'T8G'}
            {head_sur:=1; prn1:='PRN2'; manual:=3; lmt+=3; color_h:=color; color:='255:255:255'; ~~}
            {IF: lineleft()<>0}
               {IF: lineleft()<={? kody || 7 || 5 ?}}
               {cdn:=0;~~}
               {PAGE}
               {ELSE}
               {IF: kody | (szczeg & +ZGP.memo_txt(,1,'OPISMEMO')>0)}{LINE}{FI}
               {FONT: 'T8GB'}{<{lmt}'Surowce'@}{SUBSTITUTE: 'TABHEADQ'}{FONT: 'T8G'}{ head_sur:=0; vp:=1; ~~}
               {FI}
            {FI}
            {cdn:=1; lmt+=3; ~~}
            {WHILE: dalej2}
            {DO}
               {IF: ((lim>0 & ZLIM.SO='S' & ZLIM.LIMIT='T') | (nlim>0 & ZLIM.SO='S' & ZLIM.LIMIT='N') |
                    (odp>0 & ZLIM.SO='O' & ZLIM.LIMIT='T'))  & ZLIM.KOR=0 & ZLIM.KOR2=0}
                  {SUBSTITUTE: 'LINIA0Q'}
                  {IF: kody}
                     {SUBSTITUTE: 'stang_zlim'}
                  {FI}
               {FI}
               {dalej2:=ZLIM.next(); ~~}
               {IF: dalej2 & kody>0 & temp<>''}{LINE}{FI}
            {OD}
            {WHILE: dalej3}
            {DO}
               {FIRST}
               {tmat:=1; ~~}
               {FIRST-}
               {IF: TMAT.KTL<>null() | TMAT.DFLT_KTL='T'}
                  {SUBSTITUTE: 'LINIA0Q'}
                  {IF: kody}{SUBSTITUTE: 'stang_zlim'}{FI}
               {FI}
               {dalej3:=TMAT.next(); ~~}
            {OD}
            {cdn:=0;prn1:='PRN'; head_sur:=1; lmt-=6; color:=color_h; dalej2:=0; dalej3:=0; tmat:=0; manual:=0; ~~}
         {FI}
      {FI}
   {FI}
   {dalej:=ZGP.next();~~}
   {OD}
{FI}

:!UTF-8
{TITLE:A.  Plan kont - syntetycznie}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN: kl:=il:=lm:=0; mdc:='';
        kolu:=obj_new(20); w:=obj_new(20); p:=obj_new(20);
        r:=SSTALE.AR; SSTALE.AR:=ROK_F.ref; KS.cntx_psh()
        }
{END:   &kl; &il; &lm; &mdc;
        obj_del(kolu); obj_del(w); obj_del(p);
        SSTALE.AR:=r; &r; KS.cntx_pop()
        }
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: koplsynt.rpm
Utworzony:   ????-??-??
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Wydruk planu kont - syntetycznie
{COMMENT-}
  {
    kl:=7;
    kolu[1]:=8;
    kolu[2]:=62;
    kolu[3]:=15;
    kolu[4]:=17;
    kolu[5]:=29;
    kolu[6]:=7;
    kolu[7]:=7;
    il:=kolu[1]+kolu[2]+kolu[3]+kolu[4]+kolu[5]+kolu[6]+kolu[7]+kl+1;
    p[1]:=2+kolu[1];
    {!_a:=2 ..kl
    |! p[_a]:=1+p[_a-1]+kolu[_a]
    !};
    ~~
  }
  {IF: KS.index('SYM'); KS.prefix(ROK_F.ref); ~KS.first}
    { FUN.info('Brak kont syntetycznych.'@);  ~~}
    {EXIT}
  {FI}
  {
    KS.cntx_psh;
    KS.index('SYM');
    KS.prefix(ROK_F.ref);
    KS.first;
    WYDRUKI.KSOD:=KS.ref;
    KS.last;
    WYDRUKI.KSDO:=KS.ref;
    KS.cntx_pop;
    WYDRUKI.win_edit('KONTAKS');
    ~~
  }
  {EXIT: ~WYDRUKI.edit}
{'------------------------------------------------------------------';~~}
{'           DEFINICJE "MACRO", "HEADER" I "FOOTER"                 ';~~}
{'------------------------------------------------------------------';~~}
{MACRO: 'LINIA1'}
┌{kolu[1]*'─'}┬{kolu[2]*'─'}┬{kolu[3]*'─'}┬{kolu[4]*'─'}┬{kolu[5]*'─'}┬{kolu[6]*'─'}┬{kolu[7]*'─'}┐
{MACRO-}
{MACRO: 'LINIA2'}
├{kolu[1]*'─'}┼{kolu[2]*'─'}┼{kolu[3]*'─'}┼{kolu[4]*'─'}┼{kolu[5]*'─'}┼{kolu[6]*'─'}┼{kolu[7]*'─'}┤
{MACRO-}
{MACRO: 'LINIA3'}
└{kolu[1]*'─'}┴{kolu[2]*'─'}┴{kolu[3]*'─'}┴{kolu[4]*'─'}┴{kolu[5]*'─'}┴{kolu[6]*'─'}┴{kolu[7]*'─'}┘
{MACRO-}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='PLAN KONT - SYNTETYKA ZA ROK:  %1'@[BILANSP.AROK().NAZ];~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<6 'Od konta: %1 - %2'@[WYDRUKI.KSOD().SYM,WYDRUKI.KSOD().NAZ]}
{<6 'Do konta: %1 - %2'@[WYDRUKI.KSDO().SYM,WYDRUKI.KSDO().NAZ]}
{FI}
{FONT: 'R'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA1'}
│  Konto{<{p[1]}'│'}{<{p[1]}>{p[2]}'Nazwa konta'@}{<{p[2]}'│'}{<{p[2]}>{p[3]}'Typ konta'@}{<{p[3]}'│'}{<{p[3]}>{p[4]}'Charakter salda'@}{<{p[4]}'│'}{' Rodzaj - krąg kosztowy'@}{<{p[5]}'│'}{'Rozr.'@}{<{p[6]}'│'}{'Wielow.'@}{<{p[7]}'│'}
{SUBSTITUTE: 'LINIA2'}{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{'------------------------------------------------------------------';~~}
{'                   FOOTER                                         ';~~}
{'------------------------------------------------------------------';~~}
{FOOTER}{FONT: 'Q'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA3'}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{'------------------------------------------------------------------';~~}
{'                    CIAŁO  WYDRUKU                                ';~~}
{'------------------------------------------------------------------';~~}
{FONT: 'Q'}{SPACE: 'C'}
{FOR: 'KS'}
{INDEX: 'SYM'}
{FROM: ROK_F.ref,WYDRUKI.KSOD().SYM}
{TO: ROK_F.ref,WYDRUKI.KSDO().SYM}
{DO}
{'------------------------------------------------------------------';~~}
{'        Obliczenie kolejnego wyrażenia dla linii                  ';~~}
{'------------------------------------------------------------------';~~}
  { w[1]:=KS.SYM;
    w[2]:=KS.NAZ;
    ~~
  }
  {IF: KS.TYP='BL'}
    {w[3]:='Bilansowe'@; ~~}
  {ELIF: KS.TYP='BW'}
    {w[3]:='Wynikowe'@; ~~}
  {ELIF: KS.TYP='PB'@}
    {w[3]:='Pozabilansowe'@; ~~}
  {ELIF: KS.TYP='BN'}
    {w[3]:='Bezwynikowe'@; ~~}
  {ELIF: KS.TYP='P1'}
    {w[3]:='Pozabil.-jedn.'@; ~~}
  {ELIF: KS.TYP='PC'}
    {w[3]:='Pozabil.-cit'@; ~~}
  {FI}
  {IF: KS.SALDO='A'}
    {w[4]:='Aktywne'@; ~~}
  {ELIF: KS.SALDO='P'}
    {w[4]:='Pasywne'@; ~~}
  {ELIF: KS.SALDO='D'}
    {w[4]:='Aktywno-Pasywne'@; ~~}
  {FI}
  { exec('pw_krag','!zws_par_fask'); w[5]:=RKS.TKRAG().TRESC;~~}
  {IF: KS.ROZR='R'}
    {w[6]:='Tak'@; ~~}
  {ELIF: KS.ROZR='P'}
    {w[6]:='Dowol.'@; ~~}
  {ELIF: KS.ROZR='Z'}
    {w[6]:='Zabr.'@; ~~}
  {FI}
  {IF: KS.WIELO='T'}
    {w[7]:='Tak'@; ~~}
  {ELIF: KS.WIELO='N'}
    {w[7]:='Nie'@; ~~}
  {FI}
{'------------------------------------------------------------------';~~}
{'            Wydruk obliczonego wyrażenia                          ';~~}
{'------------------------------------------------------------------';~~}
│ {w[1]}{<{p[1]}'│'} {w[2]}{<{p[2]}'│'} {w[3]}{<{p[3]}'│'} {w[4]}{<{p[4]}'│'} {w[5]}{<{p[5]}'│'} {w[6]}{<{p[6]}'│'}{w[7]}{<{p[7]}'│'}
{OD} {' Do KS';~~}
{'------------------------------------------------------------------';~~}
{'            KONIEC  WYDRUKU                                       ';~~}
{'------------------------------------------------------------------';~~}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{ENTIRE}
{SUBSTITUTE: 'LINIA3'}
{<1>{il}'KONIEC WYDRUKU '@}
{ENTIRE-}
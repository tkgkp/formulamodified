:!UTF-8
{TITLE:1. Kalkulacja karty technologicznej}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   dalej:=0;
   VAR_DEL.delete('PRNKPK','PRNKPR','PRNKLM','PRNKLO','PRNKST','PRNKUS','PRNKTL','TABEDIT');
   PRNKPK:=obj_new(@.CLASS.PRINT, 4); PRNKPK.s_frame();
   PRNKPR:=obj_new(@.CLASS.PRINT, 3); PRNKPR.s_frame();
   PRNKLM:=obj_new(@.CLASS.PRINT, 5); PRNKLM.s_frame();
   PRNKLO:=obj_new(@.CLASS.PRINT, 3); PRNKLO.s_frame();
   PRNKST:=obj_new(@.CLASS.PRINT, 3); PRNKST.s_frame();
   PRNKUS:=obj_new(@.CLASS.PRINT, 3); PRNKUS.s_frame();
   PRNKTL:=obj_new(@.CLASS.PRINT, 4); PRNKTL.s_frame();
   prn1:='';
   color:='';
   lp:=0;
   lm:=ll:=vp:=lmt:=rsep:=0;
   pkpar:=pkalmat:=pkaloper:=pkalst:=pkalusl:=pkalnpu:=1
}
{END:
   &dalej;
   VAR_DEL.delete('PRNKPK','PRNKPR','PRNKLM','PRNKLO','PRNKST','PRNKUS','PRNKTL','TABEDIT');
   &prn1;
   &color;
   &lp;
   &lm; &ll; &vp; &lmt; &rsep;
   &pkpar; &pkalmat; &pkaloper; &pkalst; &pkalusl; &pkalnpu
}
{COMMENT}
  Wydruk kalkulacji karty technologicznej.
  [SS] - 2000/07/20 - [7.22]
  [TS] & [SS] - 2001/02/26 - [7.41]
  [TS] - 2001/10/04 - [7.53] OWwSK003_Aneks1
  [MKO] - 2001/11/07 - [7.53] OWwPR019/5.2
  [TS] - 2003/01/16 - [7.62] Data waznosci
  [TS] [8.70] Stawki uslug i koszty NPU
  Wywołanie: Karty technologiczne -> Kalkulacje TKW -> Drukuj
{COMMENT-}
{"---Definicja wiersza wydruku pozycji kalkulacji KPOZK.                  "; ~~}
{ PRNKPK.add("form(lp,,0,'9')",5,'Lp.'@,1);
  PRNKPK.add("form(KPOZK.RUBR().NR,,0,'9')",5,'Rubr.'@,1);
  PRNKPK.add("KPOZK.RUBR().OPIS",40,'Opis'@);
  PRNKPK.add("form(KPOZK.WART,,2)",20,'Wartość'@,1); ~~}
{"---Definicja wiersza wydruku parametrow kalkulacji KPAR.                "; ~~}
{ PRNKPR.add("form(KPAR.NRP,,0,'9')",5,'Nr'@,1);
  PRNKPR.add("KPAR.OPIS",40,'Opis'@);
  PRNKPR.add("form(KPAR.DEFAULT1,,10)",20,'Wartość'@,1); ~~}
{"---Definicja wiersza wydruku cennika surowcow pobranych do kalk. KALMAT."; ~~}
{ PRNKLM.add("KALMAT.PT().KTM",20,'Kod surowca'@);
  PRNKLM.add("(KALMAT.PT().N)",20,'Opis'@);
  PRNKLM.add("form(KALMAT.PRICE,,2)",15,'Cena'@,1);
  PRNKLM.add("{? KALMAT.POCH='H' || 'Wprowadzone ręcznie'@
            |? KALMAT.POCH='S' || 'Stany magazynowe'@
            |? KALMAT.POCH='K' || 'Kalkulacja półfabrykatu'@
            |? KALMAT.POCH='T' || 'Cennik tow. produkcyjnych'@ || '' ?}",13,'Pochodzenie'@);
  PRNKLM.add("{? KALMAT.TKTL<>null || KALMAT.TKTL().NRK+' '+KALMAT.TKTL().WER||''?}",20,'Technologia'@);~~}
{"---Definicja wiersza wydruku stawek operacji wykorzystanych w kalkulacji"; ~~}
{ PRNKLO.add("KALTTO.TTOPER().KOD",10,'Kod operacji'@);
  PRNKLO.add("KALTTO.TTOPER().NA",60,'Nazwa operacji'@);
  PRNKLO.add("form(KALTTO.STAWKA,,2)",15,'Stawka'@,1); ~~}
{"---Definicja wiersza kosztow stanowisk pobranych do kalkulacji";           ~~}
{ PRNKST.add("KALWRK.PLACE().KOD",10,'Kod operacji'@);
  PRNKST.add("KALWRK.PLACE().NA",60,'Nazwa operacji'@);
  PRNKST.add("form(KALWRK.KH,,2)",15,'Koszt godziny'@,1); ~~}
{"---Definicja wiersza stawek uslug";                                        ~~}
{ PRNKUS.add("KALTOU.TTOUT().KOD",10,'Kod operacji'@);
  PRNKUS.add("KALTOU.TTOUT().NA",50,'Nazwa operacji'@);
  PRNKUS.add("form(KALTOU.CENA,,2)",15,'Cena'@,1); ~~}
{"---Definicja wiersza kosztow NPU";                                         ~~}
{ PRNKTL.add("KALTLS.M().MGR().KOD",6,'Rodzaj'@);
  PRNKTL.add("KALTLS.M().KTM",10,'Symbol narzędzia'@);
  PRNKTL.add("KALTLS.M().N",40,'Nazwa narzędzia'@);
  PRNKTL.add("form(KALTLS.KOSZT,,2)",15,'Koszt'@,1);                          ~~}
  {INCLUDE: wsp_pw.rpi}
  {"---Wyswietl okienko decyzyjne, czy drukowac parametry kalkulacji      "; ~~}
  {"---oraz cennik surowcow pobranych do kalkulacji i stawek operacji.    "; ~~}
  {TABEDIT:=tab_tmp(
     ,'KPAR','STRING[1]','Parametry kalkulacji'@
     ,'KALMAT','STRING[1]','Cennik pobranych surowców'@
     ,'KALOPER','STRING[1]','Lista stawek operacji'@
     ,'KALST','STRING[1]','Lista kosztów stanowisk'@
     ,'KALUSL','STRING[1]','Stawki usług'@
     ,'KALNPU','STRING[1]','Ceny N-P-U'@
   );
   _wydr_naz:='ktte0001';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add();
      TABEDIT.KPAR:=TABEDIT.KALMAT:=TABEDIT.KALOPER:=TABEDIT.KALST:=TABEDIT.KALUSL:=TABEDIT.KALNPU:='T'
   || TABEDIT.KPAR:=WYDRUKIL.SORTUJ;
      TABEDIT.KALMAT:=WYDRUKIL.FAK;
      TABEDIT.KALOPER:=WYDRUKIL.RODZFAK;
      TABEDIT.KALST:=WYDRUKIL.FAKUM;
      TABEDIT.KALUSL:=WYDRUKIL.SR;
      TABEDIT.KALNPU:=WYDRUKIL.STANZAM
   ?};
   _red:=TABEDIT.mk_edit('Wydruk kartotek stowarzyszonych'@,0,'tabedit1');
   TABEDIT.win_efld(_red,,'KPAR',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALMAT',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALOPER',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALST',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALUSL',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALNPU',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_ebtn(_red,'text=%1,panel=bottom,align=end'['&Zapisz'@],"'key:F2'");
   TABEDIT.win_ebtn(_red,'text=%1,panel=bottom,align=end'['&Anuluj'@],"'key:Esc'");
   TABEDIT.win_edit(_red);
   ~~}
   {IF: TABEDIT.edit()}
    { WYDRUKIL.SORTUJ:=TABEDIT.KPAR;
      WYDRUKIL.FAK:=TABEDIT.KALMAT;
      WYDRUKIL.RODZFAK:=TABEDIT.KALOPER;
      WYDRUKIL.FAKUM:=TABEDIT.KALST;
      WYDRUKIL.SR:=TABEDIT.KALUSL;
      WYDRUKIL.STANZAM:=TABEDIT.KALNPU;
      WYDRUKIL.put(1);
      pkpar:=(-TABEDIT.KPAR='t');
      pkalmat:=(-TABEDIT.KALMAT='t');
      pkaloper:=(-TABEDIT.KALOPER='t');
      pkalst:=(-TABEDIT.KALST='t');
      pkalusl:=(-TABEDIT.KALUSL='t');
      pkalnpu:=(-TABEDIT.KALNPU='t');
    ~~}
   {ELSE}
      {EXIT}
   {FI}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{ prn1:='PRNKPK'; ~~}
{ PAR_WYDR.TITLE:='KALKULACJA KARTY TECHNOLOGICZNEJ'@; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRNKPK.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Nr kalkulacji:'@} {<{lmt+25} form(KKTL.KNR)}
{<{lmt+1} 'Data kalkulacji:'@} {<{lmt+25} form(KKTL.DT)}
{<{lmt+1} 'Data ważności:'@}{<{lmt+25} form(KKTL.DW)}
{<{lmt+1} 'Ceny surowców na dzień:'@} {<{lmt+25} {? KKTL.DT_D=date(0,0,0) || form(KKTL.DT) || form(KKTL.DT_D) ?}}
{<{lmt+1} 'Stan:'@} {<{lmt+25} exec('stan_opis','!tte_tec_ktkw',KKTL.STAN)}
{<{lmt+1} 'Wariant kalkulacji:'@} {<{lmt+25} KKTL.OPC().NAZ}
{<{lmt+1} 'Typ karty:'@}{<{lmt+25} KKTL.NRK().TYP().TYP+' - '+KKTL.NRK().TYP().OPIS}
{<{lmt+1} 'Karta:'@}{<{lmt+25} KKTL.NRK().NRK+' '+'wersja: %1'@[KKTL.NRK().WER]}
                    {<{lmt+25} KKTL.NRK().OPIS}
{<{lmt+1} 'Produkt:'@} {<{lmt+25} KKTL.TKTLW().KTM().KTM+' - '+M.N}
{<{lmt+1} 'Karta kalkulowana na:'@} {<{lmt+25} form(TKTL.XJM,,2)+' '+TKTL.JM().KOD}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{"---Drukuj pozycje kalkulacji karty technologicznej.                     "; ~~}
{ KPOZK.index('KR'); KPOZK.prefix(KKTL.ref()); ~~}
{IF: KPOZK.first()}{ dalej:=1; ~~}{FI}
{ lp:=0; ~~}
{WHILE: dalej}
{DO}
  {IF: KPOZK.RUBR().PRINTED='T'}
    { lp+=1; ~~}{SUBSTITUTE: 'LINIA0'}
  {FI}
  { dalej:=KPOZK.next(); ~~}
{OD}
{"---Drukuj parametry kalkulacji (jezeli tak zadecydowal operator).       "; ~~}
{IF: pkpar}
  { PAR_WYDR.TITLE:='PARAMETRY KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  { KPAR.index('KN'); KPAR.prefix(KKTL.ref()); ~~}
  {IF: KPAR.first()}{PAGE}{ dalej:=1; prn1:='PRNKPR'; ~~}
{ lmt:=int((charleft()-PRNKPR.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=KPAR.next(); ~~}
  {OD}
{FI}
{"---Drukuj ceny surowcow pobranych do kalkulacji (jezeli tak zadecydowal "; ~~}
{"---operator).                                                           "; ~~}
{IF: pkalmat}
  { PAR_WYDR.TITLE:='CENNIK SUROWCÓW POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  { KALMAT.index('KSN'); KALMAT.prefix(KKTL.ref()); ~~}
  {IF: KALMAT.first()}{PAGE}{ dalej:=1; prn1:='PRNKLM'; ~~}
{ lmt:=int((charleft()-PRNKLM.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=KALMAT.next(); ~~}
  {OD}
{FI}
{"---Drukuj listę stawek operacji do kalkulacji (jezeli tak zadecydowal operator)."; ~~}
{IF: pkaloper}
  { PAR_WYDR.TITLE:='LISTA STAWEK OPERACJI POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  { KALTTO.index('KO'); KALTTO.prefix(KKTL.ref()); ~~}
  {IF: KALTTO.first()}{PAGE}{ dalej:=1; prn1:='PRNKLO'; ~~}
{ lmt:=int((charleft()-PRNKLO.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=KALTTO.next(); ~~}
  {OD}
{FI}
{"---Drukuj listę kosztow stanowisk do kalkulacji (jezeli tak zadecydowal operator)."; ~~}
{IF: pkalst}
  { PAR_WYDR.TITLE:='LISTA KOSZTÓW STANOWISK POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  { KALWRK.index('KP'); KALWRK.prefix(KKTL.ref()); ~~}
  {IF: KALWRK.first()}{PAGE}{ dalej:=1; prn1:='PRNKST'; ~~}
{ lmt:=int((charleft()-PRNKST.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=KALWRK.next(); ~~}
  {OD}
{FI}
{"---Drukuj listę stawek uslug pobranych do kalkulacji (jezeli tak zadecydowal operator)."; ~~}
{IF: pkalusl}
  { PAR_WYDR.TITLE:='LISTA STAWEK USŁUG POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  { KALTOU.index('KT'); KALTOU.prefix(KKTL.ref()); ~~}
  {IF: KALTOU.first()}{PAGE}{ dalej:=1; prn1:='PRNKUS'; ~~}
{ lmt:=int((charleft()-PRNKUS.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=KALTOU.next(); ~~}
  {OD}
{FI}
{"---Drukuj listę kosztow N-P-U pobranych do kalkulacji (jezeli tak zadecydowal operator)."; ~~}
{IF: pkalnpu}
  { PAR_WYDR.TITLE:='LISTA CEN N-P-U POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  { KALTLS.index('KT'); KALTLS.prefix(KKTL.ref()); ~~}
  {IF: KALTLS.first()}{PAGE}{ dalej:=1; prn1:='PRNKTL'; ~~}
{ lmt:=int((charleft()-PRNKTL.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=KALTLS.next(); ~~}
  {OD}
{FI}

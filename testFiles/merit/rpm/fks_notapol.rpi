:!UTF-8
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: notapol.rpi
Utworzony:   2003-11-26 [AMK] Artur Makos
==================================================
Zawartosc: Nota odsetkowa - jezyk polski
{COMMENT-}
{ {? var_pres('PRN')>0 || obj_del(PRN) ?}; naz_str:='Strona ';
  PRN:=obj_new(@.CLASS.PRINT,11); PRN.sl_frame();
  {! _i:=1..11 |! w[_i]:='' !};
  PRN.add("w[11]",8,'Jed. ks.'@);
  PRN.add("w[1]",{? PAR_WYDR.PAR15 || 35 || 50 ?},'Należność'@);
  {? SER_NAG.WAL=null || PRN.add("w[8]",4,'Wal.') ?};
  PRN.add("form(w[2],,2,' ,')",14,'Kwota faktury'@,1);
  PRN.add("w[10]",10,'Data otwarcia'@);
  PRN.add("w[3]",10,'Termin płatności'@);
  PRN.add("w[4]",10,'Data wpłaty'@);
  PRN.add("form(w[5],,2,' ,')",14,'Kwota wpłaty'@,1);
  PRN.add("w[6]",6,'Zwłoka dni'@,1);
  PRN.add("form(w[7],,2,' ,')",12,'Kwota odsetek (po term. płat.)'@,1);
  {? PAR_WYDR.PAR15 || PRN.add("form(w[9],,2,' ,')",12,'Kwota odsetek (przed term. płat.)'@,1) ?};
  prn1:=prn2:='PRN';  PAR_WYDR.HEAD:=0;
  {! _i:=1..licz_wal  |! sumy[_i][2]:=sumy[_i][3]:=sumy[_i][4]:=sumy[_i][5]:=0 !};
  tab_adr1[1]:='ADRESAT:'@; tab_adr1[2]:='NADAWCA:'@; tab_adr1[3]:='ADRES:'; tab_adr1[4]:='KOD PŁATNIKA:'@;
~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}------------------------------WYDRUK----------------{COMMENT-}
{ sumafakt:=sumawpl:=sumaods:=sumaods1:=0; dokument:=''; tp:=date(0,0,0); kontosp:=oddzial:='';~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: 'SER_POZ'}
{INDEX: 'SER_POZU'}
{PREFIX: REF.FIRMA,SER_NAG.ref(),0,OPERATOR.DEPT().OD}
{DO}
{COMMENT}------------------------------MACRO-HEADX------------------{COMMENT-}
{IF: lineleft=0}
{IF: ~new_poz}{ new_poz:=1;~~}
{  PAR_WYDR.TITLE:='Nota odsetkowa'+{? SER_NAG.ID<>'' || ' nr '+SER_NAG.ID || '' ?}+' z dnia '+$SER_NAG.DATA; ~~}
{IF: ROZNE.UT_KOM}{SUBSTITUTE: 'KORHEADP'}{ELSE}{SUBSTITUTE: 'KORHEADL'}{FI}
{IF: SER_NAG.WAL<>null}{<3 'WALUTA:'@}{<20 SER_NAG.WAL().KOD+' '+SER_NAG.WAL().TR}{FI}{ kontbank:=exec('get_rb','rozrach_kor');~~}
{<3 'Sposób zapłaty - przelew do banku: '+SKID_RBK.BANK().NB+{? SKID_RBK.BANK().KODISO<>XINFO.KRAJ || ', SWIFT (BIC): '+SKID_RBK.BANK().SWIFT || '' ?}}
{<28 'na konto: '+kontbank}
{exec('kh_dod_ini','kontrahent',SER_NAG.KH);~~}
{IF: |KH_DOD.NASZ_KOD<>''}{<3 exec('symbol','rozrach',1)+'  '+|KH_DOD.NASZ_KOD}{FI}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{LINE: 1}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}{FONT: 'T8G'}{SPACE: 'C'}
{FI}
{COMMENT}------------------------------MACRO-HEADX-KONIEC-----------------{COMMENT-}
  { w[1]:=SER_POZ.DOK+{? SER_POZ.SYM_ZEW<>'' & SER_POZ.SYM_ZEW<>SER_POZ.DOK
                      || ' ('+SER_POZ.SYM_ZEW+')'
                      || ''
                      ?};
    w[11]:=SER_POZ.ODD().OD; w[2]:=w[3]:=w[8]:=w[10]:=''; w[4]:=$SER_POZ.DATW;
    w[5]:=SER_POZ.KWP; w[6]:=SER_POZ.ZWL;
    {? SER_POZ.PRZEDPO || w[7]:=0;  w[9]:=SER_POZ.ODS || w[7]:=SER_POZ.ODS;  w[9]:=0 ?};
    {? SER_POZ.SYM_ROK<>dokument | SER_POZ.TERM<>tp | SER_POZ.KON<>kontosp | SER_POZ.ODD().OD<>oddzial
    || {? SER_NAG.WAL<>null
       || sumafakt+=SER_POZ.KW
       || _znal:=0;
         {! _i:=1..licz_wal
         |? _znal=0
         |! {? sumy[_i][1]=SER_POZ.WAL().KOD || _znal:=1; sumy[_i][2]+=SER_POZ.KW ?}
         !}
       ?};
      w[2]:=SER_POZ.KW; w[8]:=SER_POZ.WAL().KOD
    ?};
    {? SER_POZ.SYM_ROK<>dokument | SER_POZ.TERM<>tp | SER_POZ.KON<>kontosp | SER_POZ.ODD().OD<>oddzial
    || w[3]:=$SER_POZ.TERM; w[10]:=$SER_POZ.DATA
    ?};
    dokument:=SER_POZ.SYM_ROK;  tp:=SER_POZ.TERM; kontosp:=SER_POZ.KON; oddzial:=SER_POZ.ODD().OD;
    {? SER_NAG.WAL<>null
    || sumawpl+=SER_POZ.KWP
    || _znal:=0;
       {! _i:=1..licz_wal
       |? _znal=0
       |! {? sumy[_i][1]=SER_POZ.WAL().KOD || _znal:=1; sumy[_i][3]+=SER_POZ.KWP ?}
       !}
    ?};
    {? SER_NAG.WAL<>null
    || {? SER_POZ.PRZEDPO || sumaods1+=SER_POZ.ODS || sumaods+=SER_POZ.ODS ?}
    || _znal:=0;
       {! _i:=1..licz_wal
       |? _znal=0
       |! {? sumy[_i][1]=SER_POZ.WAL().KOD
          || _znal:=1;
            {? SER_POZ.PRZEDPO || sumy[_i][4]+=SER_POZ.ODS || sumy[_i][4]+=SER_POZ.ODS ?}
          ?}
       !}
    ?};
 ~~}
   {SUBSTITUTE: 'LINIA0'}
{COMMENT}------------------------------LINIA KONCZACA + NOWY HEADER-----------------{COMMENT-}
   {IF: lineleft<3}{FONT: 'T8G'}
   {SPACE: 'C'}{SUBSTITUTE: 'LINIAF'}
   {IF: lineleft<>0}{PAGE}{FONT: 'T8GB'}{SPACE: 'C'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{FI}{ vp:=1;~~}
   {FI}
{COMMENT}------------------------------KONIEC NOWEGO HEADERA-----------------{COMMENT-}
{OD}
{ separat:=rsep; ~~}
{IF: SER_NAG.WAL<>null}
{IF: lineleft<3}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{PAGE}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{ w[1]:='RAZEM'; w[2]:=sumafakt; w[5]:=sumawpl; w[7]:=sumaods; w[9]:=sumaods1; w[3]:=w[4]:=w[6]:=w[8]:=w[10]:=w[11]:=''; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIAFS'}
{ELSE}
{ liczwal:=0; {! _i:=1..licz_wal  |! {? sumy[_i][2]<>0 || liczwal+=1 ?} !};~~}
{IF: lineleft<=(2+liczwal)}
{IF: lineleft<>0}{SUBSTITUTE: 'LINIAF'}{PAGE}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{FI}
{  w[1]:='RAZEM'; rsep:=1; w[3]:=w[4]:=w[6]:=w[8]:=w[10]:=w[11]:=''; licznik:=1; ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{WHILE: licznik<=licz_wal}
{DO}
   {IF: sumy[licznik][2]<>0}
    {  w[2]:=sumy[licznik][2]; w[5]:=sumy[licznik][3]; w[7]:=sumy[licznik][4]; w[8]:=sumy[licznik][1];~~}
    {SUBSTITUTE: 'LINIA0'} { w[1]:=''; rsep:=0;~~}
   {FI}
   { licznik+=1;~~}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FI} { rsep:=separat;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}------------------------------STOPKA-----------------{COMMENT-}
{ENTIRE}
{LINE: 1}
{IF: SER_NAG.WAL<>null}
{FONT: 'W8'}{SPACE: 'C'}
{<1 'Należna nam suma: '@+form(sumaods+{? PAR_WYDR.PAR15 || sumaods1 || 0 ?},,2,' ,')+' '+SER_NAG.WAL().KOD}
{IF: SER_NAG.WAL=FINFO.NAROD}
{<1 'Słownie: '@+STR.kwota_sł(sumaods+{? PAR_WYDR.PAR15 || sumaods1 || 0 ?})+'.'}
{FI}
{LINE: 1}
{FI}{ ref_defw:=null;
  SLO.cntx_psh();
  SER_DEFW.index('SERDEFW1'); SER_DEFW.prefix(SER_NAG.KOD,SER_KOR.JEZYK);
  {? SER_DEFW.first() || ref_defw:=SER_DEFW.ref() ?};
  SLO.cntx_pop();~~}
{ _tmp:=SER_DEFW.memo_txt(0,1,'TEKST'@);STR.spl_word();STR.split(_tmp,'\n'); ~~}
{WHILE: STR.next()}
{DO}
   {tekst:=STR.get_word();~~}
   {<1 tekst}
{OD}
{LINE: 1}
{<1 'Na przelewie prosimy podać swój kod: %1'@[SER_NAG.KH().KOD]}
{LINE: 1}
{ wdt:=charleft(); ~~}
{<1>{wdt/2} 'SPORZĄDZIŁ'@}{<{wdt/2}>{wdt} 'ZATWIERDZIŁ'@}
{LINE: 2}
{<1>{wdt/2} 50*'.'}{<{wdt/2}>{wdt} 50*'.'}
{ENTIRE-}
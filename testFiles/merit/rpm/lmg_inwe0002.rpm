:!UTF-8
{TITLE:B. Przybliżone rozliczenie inwentaryzacji}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   VAR_DEL.delete('w','k','s','PRN','__only');
   k:=obj_new(13); "--szerokosci kolumn--";
   w:=obj_new(13); "--wartosci kolumn--";
   s:=obj_new(13); "--razem--";
   dokl:=0; "--dokladnosc ilosc--";
   __dokcc:=0;
   doklw:=2; "--dokladnosc wartosc--";
   WYB1:=0;
   r:=0;
   PRN:=obj_new(@.CLASS.PRINT,13); PRN.s_frame();
   __only:=INN.MG().IL='T';

   WYDRUKIL.win_edit('INWE0002');
   {? WYDRUKIL.edit()
   || wyjscie:=0;
      WYB1:=WYDRUKIL.CHKBOX01;
      grup:=WYDRUKIL.SORTUJ;
      wydr:=WYDRUKIL.CHKBOX02;

      k[1] :=20;
      k[2] :=30;
      k[3] :=10;
      k[4] :=10;
      k[5] :=12;
      k[6] :=12;
      k[7] :=12;
      k[8] :=12;
      k[9] :=12;
      k[10]:=12;
      k[11]:= 4;
      k[12]:= 0;
      k[13]:= 5;
      {! _i..11 |! w[_i]:='' !};
      {! _i..11 |! s[_i]:=0 !}
   || wyjscie:=1
   ?};
   {? wyjscie=0
   ||
      INP.cntx_psh;
      INP.index('INP'); INP.prefix(INN.ref);
      {? INP.first
      ||
         {!
         |?
           {? INP.M().DOKL>dokl || dokl:=INP.M().DOKL ?};
           {? INP.M().DOKL_C>__dokcc || __dokcc:=INP.M().DOKL_C ?};

            INP.next
         !}
      ?};
      INP.cntx_pop
   ?}
}
{END:
   VAR_DEL.delete('color','prn1','prn2','__dokcc');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep');
   VAR_DEL.delete('w','k','s','dokl','doklw','PRN','lp','__only');
   VAR_DEL.delete('wyjscie','X_Xa');
   VAR_DEL.delete('WYB1','grup','wydr','r')
}
{IF: wyjscie=1}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{
PRN.add("w[11]",k[11],'Lp.'@,1);
PRN.add("w[1]",k[1],'Indeks'@);
PRN.add("w[2]",k[2],'Nazwa materiału'@);
PRN.add("w[13]",k[13],'jm'@);
{? grup='B' | grup='D' || PRN.add("w[3]",k[3],'Data'@) ?};
{? (grup='B' | grup='D' | grup='C') & ~__only || PRN.add("w[4]",k[4],'Cena'@,1) ?};
PRN.add("w[5]",k[5],'Wg dokumentów'@,1);
PRN.add("w[6]",k[6],'Ze spisu'@,1);
PRN.add("w[7]",k[7],'Niedobór ilość'@,1);
{? (grup='B' | grup='D' | grup='C') & ~__only || PRN.add("w[8]",k[8],'Niedobór wartość'@,1) ?};
PRN.add("w[9]",k[9],'Nadwyżka ilość'@,1);
{? (grup='B' | grup='D' | grup='C') & ~__only || PRN.add("w[10]",k[10],'Nadwyżka wartość'@,1) ?};

_ref:=BB.refsql(INN.ref);
_sql1:='select ';
{? grup='B' || _sql1+='M.KTM || case when INP.SCEAN<>\'\' then \' (KOD ID: \' || INP.SCEAN || \')\' else \'\' end KTM, M.N, INP.D, INP.C, SE as SE,SS as SS, JM.KOD as JM ' ?};
{? grup='D' || _sql1+='M.KTM, M.N, INP.D, INP.C, sum(SE) as SE,sum(SS) as SS, JM.KOD as JM ' ?};
{? grup='C' || _sql1+='M.KTM, M.N, INP.C, sum(SE) as SE, sum(SS) as SS, JM.KOD as JM ' ?};
{? grup='K' || _sql1+='M.KTM, M.N, sum(SE) as SE, sum(SS) as SS, JM.KOD as JM ' ?};
_sql1+='from INP join M using(INP.M,M.REFERENCE)'+
   ' join JM using(M.J,JM.REFERENCE) ';
_sql1+='where INP.IN=\':_a\'';
{? grup='B' || _sql1+='' ?};
{? grup='D' || _sql1+=' group by INP.D, INP.C, M.KTM, M.N, JM.KOD' ?};
{? grup='C' || _sql1+=' group by INP.C, M.KTM, M.N, JM.KOD' ?};
{? grup='K' || _sql1+=' group by M.KTM, M.N, JM.KOD' ?};
_sql1+=' order by KTM';
VAR_DEL.delete('X_Xa');
X_Xa:=sql(_sql1,_ref);

lmt:=int((charleft()-PRN.width())/2);

PAR_WYDR.TITLE:='PRZYBLIŻONE ROZLICZENIE INWENTARYZACJI\n'@+INN.SYM+' z dnia '@+form(INN.DI);
prn1:='PRN'
;~~}
{IF: X_Xa.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   RAPORT HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: '@+ST.MAG().SYM}
{IF: WYB1=0}{<{lmt+1}}{'Typ wydruku: Tylko różnica'@}{ELIF: WYB1=1}{<{lmt+1}}{'Typ wydruku: Wszystkie'@}{FI}
{IF: grup='B'}{<{lmt+1}}{'Grupowanie ilości: Brak'@}
{ELIF: grup='D'}{<{lmt+1}}{'Grupowanie ilości: Data, cena, KTM'@}
{ELIF: grup='C'}{<{lmt+1}}{'Grupowanie ilości: Cena, KTM'@}
{ELIF: grup='K'}{<{lmt+1}}{'Grupowanie ilości: KTM'@}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{ rsep:=PAR_WYDR.RSEP; vp:=1; lp:=0;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR:X_Xa}
{DO}
   {
   lp+=1;
   w[11]:=$lp;
   w[1] :=form(X_Xa.KTM);
   w[2] :=form(X_Xa.N);
   w[13]:=form(X_Xa.JM);
   M.cntx_psh;
   M.index('MATKTM');
   M.prefix(X_Xa.KTM,X_Xa.KTM);
      {? M.first
      || _mat:=M.ref
      || _mat:=null
      ?};
   M.cntx_pop;
   {? grup='D' | grup='B' || w[3] :=form(X_Xa.D) ?};
   {? grup<>'K'
   ||
      _cena:=
         {? 1+ST.MAG().TYP<>'Ś' | X_Xa.SS>=X_Xa.SE
         || X_Xa.C
         ||
            _cena:=0;
            {? INN.INR_R<>''
            || ND.cntx_psh; DK.cntx_psh;
               DK.index('MAM'); DK.prefix(BB.sqlint(INN.INR_R),_mat);
               {? DK.first & DK.N().Z='T' || _cena:=DK.C ?};
               ND.cntx_pop; DK.cntx_pop
            ?};
            {? _cena>0 || _cena || exec('cenas','magazyn_stan',_mat,'Ś') ?}
         ?}
   ?};
   {? (grup='B' | grup='D' | grup='C') & ~__only || w[4] :=form(_cena,, __dokcc,'.,') ?};
   r:=X_Xa.SE-X_Xa.SS;
   w[5]:=form(X_Xa.SE,,dokl,'.,'); {? WYB1 | r<>0 || s[5] +=X_Xa.SE ?};
   w[6]:=form(X_Xa.SS,,dokl,'.,'); {? WYB1 | r<>0 || s[6] +=X_Xa.SS ?};
   w[7]:=w[9]:=form(0,,dokl,'.,');
   w[8]:=w[10]:=form(0,,2,'.,');
   {? r>0
   ||
      w[7]:=form(r,,dokl,'.,'); s[7] +=r;
      {? (grup='B' | grup='D' | grup='C') & ~__only || _war:=(r*_cena)$2; w[8] :=form(_war,,2,'.,'); s[8] +=_war ?}
   |? r<0
   ||
      r:=-r;
      w[9]:=form(r,,dokl,'.,'); s[9] +=r;
      {? (grup='B' | grup='D' | grup='C') & ~__only || _war:=(r*_cena)$2; w[10] :=form(_war,,2,'.,'); s[10] +=_war ?}
   ?}
   ;~~}
   {IF: WYB1 | (r<>0)}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1] :=w[2] :=w[3] :=w[12] :=w[13] :='';
w[4] :='Razem:'@; PRN.FORM[5] :=0;
w[5] :=form(s[5],,dokl,'.,');
w[6] :=form(s[6],,dokl,'.,');
w[7] :=form(s[7],,dokl,'.,');
w[8] :=form(s[8],,2,'.,');
w[9] :=form(s[9],,dokl,'.,');
w[10]:=form(s[10],,2,'.,');
w[11]:='';
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
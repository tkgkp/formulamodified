:!UTF-8
{TITLE:D. Dokument źródłowy - obroty na wyróżniku}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,6); PRNS.s_frame();
   w:=obj_new(6);
   color:=prn1:=prn2:='PRN';
   lp:=lm:=ll:=vp:=lmt:=rsep:=NOMAXP:=0;
   _sql:='select distinct '+
      'SLU.NAZ '+
      'from POZ '+
      'left join POW using(POW.POZ,POZ.REFERENCE) '+
      'join DOK using(POZ.DOK,DOK.REFERENCE) '+
      'left join SLUAPPL using(SLUAPPL.REFERENCE,POW.SLU) '+
      'left join SLU using(SLU.REFERENCE,SLUAPPL.SLU) '+
      {? var_pres('TT_D_DOK')<100
      || 'where '+
         'DOK.REFERENCE=\':_a\' and SLU.NAZ<>\'\''
      || 'right join :_a using(:_a.REFSQL,DOK.REFERENCE) where SLU.NAZ<>\'\''
      ?}+
      'order by SLU.NAZ';
   VAR_DEL.delete('__POWSLU');
   __POWSLU:={? var_pres('TT_D_DOK')<100 || sql(_sql,$DOK.ref()) || sql(_sql,TT_D_DOK) ?};
   _o:=__POWSLU.mk_sel('Słowniku wyróżników'@);
   __POWSLU.win_fld(_o,,'NAZ',,,,,,,1);
   __POWSLU.win_act(_o,,'Formuła','TEN'@,,,,"sel_exit()",1);
   __POWSLU.win_sel(_o);
   WYDRUK.win_edit('RED22');
   UNPAR.P10:='';
   UNPAR.P10_BE:=UNPAR.P10_BV:='';
   UNPAR.P10_AE:='{?fld=\'\'||return(1)?};'+
                 '__POWSLU.cntx_psh();'+
                 '__POWSLU.prefix(fld);'+
                 '_ok:={?__POWSLU.first()||fld(__POWSLU.NAZ);1||FUN.info(\'Brak wskazanego słownika wśród wyróżników.\');0?};'+
                 '__POWSLU.cntx_pop();_ok';
   UNPAR.P10_F3:='{? __POWSLU.select(,1) || fld(__POWSLU.NAZ) || 0 ?}';
   1
}
{END:
   VAR_DEL.delete('PRN','PRNS','w','NOMAXP','__POZWYR','__POWSLU','sumawn','sumama');
   &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep; exec('end_msg','dok_fks_zest')
}
{IF: var_pres('TT_D_DOK')<100}
   {  POZ.index('DOK'); POZ.prefix(DOK.ref);
      {? ~POZ.first() || FUN.info('Brak pozycji w dokumencie.'@) ?}; ~~}
   {EXIT: ~POZ.first() | __POWSLU.first() & ~WYDRUK.edit()}
{ELSE}
   {EXIT: __POWSLU.first() & ~WYDRUK.edit()}
{FI}
{  PRN.add("w[1]",3,'Lp.'@,1);
   PRN.add("w[2]",35,'Konto'@);
   PRN.add("w[3]",8,'Wyróżnik'@);
   PRN.add("w[4]",40,'Opis'@);
   PRN.add("form(w[5],,2,' ,')",16,'Wn'@,1);
   PRN.add("form(w[6],,2,' ,')",16,'Ma'@,1);
   PRNS.add("w[1]",95,,,,,,,,,4);
   PRNS.add("form(w[5],,2,' ,')",16,,1);
   PRNS.add("form(w[6],,2,' ,')",16,,1);
   __POZWYR:=tab_tmp(3,
      'DOK','STRING[16]',,
      'KONTO','STRING[35]','Konto'@,
      'WYR','STRING[8]','Kod wyróżnika'@,
      'OPIS','STRING[60]','Opis'@,
      'WN','REAL',,
      'MA','REAL',
   );
   __POZWYR.index(__POZWYR.ndx_tmp('',1,'DOK',,,'KONTO',,,'WYR',,,'WYR',,));
   _fun:="
      POZ.index('DOK'); POZ.prefix(DOK.ref());
      {? POZ.first()
      || {!
         |? _jest:=0;
            POW.index('POZ'); POW.prefix(POZ.ref());
            {? POW.first()
            || {!
               |? {? POW.SLU().SLU().NAZ=UNPAR.P10
                  || _jest:=1;
                     {? __POZWYR.find_key($DOK.ref(),POZ.KON,POW.SLO().KOD,SLO.KOD)
                     || {? -POZ.STR='wn' || __POZWYR.WN+=POW.KW || __POZWYR.MA+=POW.KW ?};
                        __POZWYR.put()
                     || __POZWYR.DOK:=$DOK.ref();
                        __POZWYR.KONTO:=POZ.KON;
                        __POZWYR.WYR:=SLO.KOD;
                        __POZWYR.OPIS:=SLO.TR;
                        {? -POZ.STR='wn'
                        || __POZWYR.WN:=POW.KW;
                           __POZWYR.MA:=0
                        || __POZWYR.MA:=POW.KW;
                           __POZWYR.WN:=0
                        ?};
                        __POZWYR.add()
                     ?}
                  ?};
                  POW.next()
               !}
            ?};
            {? _jest=0
            || {? __POZWYR.find_key($DOK.ref(),POZ.KON,'','')
               || {? -POZ.STR='wn' || __POZWYR.WN+=POZ.SUM || __POZWYR.MA+=POZ.SUM ?};
                  __POZWYR.put()
               || __POZWYR.DOK:=$DOK.ref();
                  __POZWYR.KONTO:=POZ.KON;
                  __POZWYR.WYR:='';
                  __POZWYR.OPIS:='';
                  {? -POZ.STR='wn'
                  || __POZWYR.WN:=POZ.SUM;
                     __POZWYR.MA:=0
                  || __POZWYR.MA:=POZ.SUM;
                     __POZWYR.WN:=0
                  ?};
                  __POZWYR.add()
               ?}
            ?};
            POZ.next()
         !}
      ?}
   ";
   {? var_pres('TT_D_DOK')>100
   || {? TT_D_DOK.first()
      || {!
         |? {? DOK.seek(TT_D_DOK.REF,DOK.name())
            || _fun()
            ?};
            TT_D_DOK.next()
         !}
      ?}
   || _fun()
   ?};
   ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}------------------------------MACRO WYDRUK-----------------{COMMENT-}
{MACRO: 'WYDRUK'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='DOKUMENT ŹRÓDŁOWY %1 Z OBROTAMI NA WYRÓŻNIKU'@[DOK.NK]; ~~}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: rep_export()}
{'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}
{'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}{IF: DOK.NRKSEF<>''}
{'Nr KSeF: '+DOK.NRKSEF}{FI}
{'Data operacji: %1'@[$DOK.DOP]}
{'Data zapisu: %1'@[$DOK.DTW]}
{'Data wystawienia: %1'@[$DOK.DTO]}
{IF: DOK.KSEFCDAT<>date(0,0,0)}{'Data KSeF: '+$DOK.KSEFCDAT}{FI}
{IF: DOK.D4<>date(0,0,0)}{'Data otrzymania: %1'@[$DOK.D4]}{FI}
{IF: DOK.D3<>date(0,0,0)}{'Termin płatności: %1'@[$DOK.D3]}{FI}
{'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}
{'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}
{'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}
{'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}
{FI}
{ {? ~__POWSLU.first()
  || 'Brak wyróżników dla kont pozycji dokumentu'@
  |? UNPAR.P10='' || 'Nie wskazano słownika wyróżnika'@
  || 'Słownik wyróżnika: %1'@[UNPAR.P10]
  ?}
}
{LINE: 1}
{ELSE}
{<2 'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{<2 'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}{<40 'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{<2 'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}{IF: DOK.NRKSEF<>''}{<66 'Nr KSeF: '+DOK.NRKSEF}{FI}
{<2 'Data operacji: %1'@[$DOK.DOP]}{<34 'Data zapisu: %1'@[$DOK.DTW]}{<66 'Data wystawienia: %1'@[$DOK.DTO]}{IF: DOK.KSEFCDAT<>date(0,0,0)}{<106 'Data KSeF: '+$DOK.KSEFCDAT}{FI}{IF: DOK.D4<>date(0,0,0)}{<130 'Data otrzymania: %1'@[$DOK.D4]}{FI}{IF: DOK.D3<>date(0,0,0)}{<162 'Termin płatności: %1'@[$DOK.D3]}{FI}
{<2 'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{<2 'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}{<2 'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}{<66 'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}{<98 'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}{FI}
{<2 {? ~__POWSLU.first()
    || 'Brak wyróżników dla kont pozycji dokumentu'@
    |? UNPAR.P10='' || 'Nie wskazano słownika wyróżnika'@
    || 'Słownik wyróżnika: %1'@[UNPAR.P10]
    ?}
}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; prn1:='PRN'; prn2:='PRN'; sumawn:=sumama:=lp:=0;~~}
{FOR: __POZWYR}
{PREFIX: $DOK.ref()}
{DO}
   {  lp+=1;
      w[1]:=$lp;
      w[2]:=__POZWYR.KONTO;
      w[3]:=__POZWYR.WYR;
      w[4]:=__POZWYR.OPIS;
      w[5]:=__POZWYR.WN;
      w[6]:=__POZWYR.MA;
      sumawn+=__POZWYR.WN;
      sumama+=__POZWYR.MA;
      ~~
   }
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<3}{PAGE}{FI}
{IF: ~rep_export}
{FOOTER}{FOOTER-}
{  rsep:=1;
   w[1]:='RAZEM'; w[5]:=sumawn; w[6]:=sumama; prn1:='PRN'; prn2:='PRNS';~~}
{SUBSTITUTE: 'LINIA02'}{prn1:='PRNS';~~}
{SUBSTITUTE: 'LINIAF'}{prn1:='PRN'; prn2:='PRN';~~}
{FI}
{MACRO-}
{COMMENT}--------------------------END MACRO WYDRUK-----------------{COMMENT-}
{IF: var_pres('TT_D_DOK')>100}
   {NOMAXP:=1;~~}
   {FOR: 'TT_D_DOK'}
   {DO}
      {IF: DOK.seek(TT_D_DOK.REF,DOK.name()) & (POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first()) }
         {SUBSTITUTE: 'WYDRUK'}{l_druk+=1;~~}
         {IF: TT_D_DOK.next()}{HEADER}{HEADER-}{PAGE}{page(1);~~}{FI}
      {FI}
   {OD}
   {czy_druk:=1;~~}
{ELSE}
   {SUBSTITUTE: 'WYDRUK'}
{FI}


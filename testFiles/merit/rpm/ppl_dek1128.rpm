:!UTF-8
{TITLE:PIT-11(28) formularz obowiązujący od roku 2022}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,5,5,5,5,1,1,'A4'}
{BEGIN:
   VAR_DEL.delete('pit','czy_raport','x','y','dx','czy_pod','dl_z','__PES','czy_inf');
   VAR_DEL.delete('__P_107','__AK147');
   pit:=obj_new(114);
   {! _nx:=1..27 |! pit[_nx]:='' !};
   {! _nx:=28..obj_len(pit) |! pit[_nx]:=0 !};
   pit[90]:=pit[92]:=pit[94]:=pit[107]:=pit[108]:=pit[111]:=pit[112]:='';
   'sprawdzenie czy jest Aktualizacja 12.51_147 PIT-2022';
   __AK147:=exec('akt_147','edeklar');
   'nowe pole P_107 jako pkt 152, 153, 154 w PIT-11 od 2022 wersja 13';
   __P_107:=obj_new('PKT_152','PKT_153','PKT_154');
   __P_107.PKT_152:=__P_107.PKT_153:=__P_107.PKT_154:='N';
   exec('czytaj','#stalesys',date(),KST);

   pit[1]:=KST.NIP;
   pit[4]:=VAT_DEK.OKRES;
   'Nowe pole w wersji 25 poz. 5 Nr informacji VAT_DEK.NR_INFOR';
   {? var_pres('NR_INFOR',VAT_DEK)>0
   || pit[5]:=exec('nr_infor_xml','edeklar')
   || pit[5]:=''
   ?};
   pit[8]:='N';
   {? VAT_DEK.POD=1 || pit[8]:='T' ?};
   {? pit[8]='N'
   || pit[9]:=KST.NAZWA;    'Platnik niebedacy osoba fizyczna'
   || pit[10]:=KST.PODNAZ+', '+KST.PODIME+', '+STR.gsub(KST.PODURDAT$4,'.','-'); 'Platnik bedacy osoba fizyczna'
   ?};

   pit[108]:=VAT_DEK.PITR;

   {! _nx:=1..10 |! pit[_nx]:=~(-pit[_nx]) !};

   czy_raport:=0;
   {? VAT_DEK.KOR='T'
   || {? var_pres('__ord')>100 || obj_del(__ord) ?};
      __ord:=obj_new(13);
      {! _nx:=1..obj_len(__ord) |! __ord[_nx]:='' !};
      {? pit[8]='T'
      || __ord[1]:=KST.NIP;
         __ord[5]:=KST.PODNAZ;
         __ord[6]:=KST.PODIME;
         __ord[7]:=STR.gsub(KST.PODURDAT$4,'.','-')
      || __ord[1]:=KST.NIP;
         __ord[5]:=KST.NAZWA;
         __ord[6]:=KST.SKROT
      ?}
   ?};
   x:=y:=dx:=0;
   exec('rpm_as_pdf_ini','rpm_pdf');
   czy_pod:=0; __PES:=1; czy_inf:=0
}
{END:
   exec('stalesys','#stalesys');
   exec('rpm_as_pdf_end','rpm_pdf');
   VAR_DEL.delete('pit','czy_raport','x','y','dx','czy_pod','dl_z','__PES','czy_inf');
   VAR_DEL.delete('__P_107','__AK147');
   {? VAT_DEK.KOR='T' || obj_del(__ord); &__ord ?}
}
{COMMENT}OLD: dek1128.rpm{COMMENT-}
{COMMENT}OLD: dkw1128g.rpi{COMMENT-}
{COMMENT}OLD: ord_zu3g.rpi{COMMENT-}
{
   czy_raport:=0;
   pit[2]:=exec('upo_id','xml');
   pit[6]:=VAT_DEK.US().NU+{? +US.UU || ', '+US.UU || '' ?}+
           {? +US.UK & +US.MU || ', '+(6+US.UK)+' '+US.MU || '' ?};
   pit[6]:=~-pit[6];
   pit[7]:=VAT_DEK.KOR;
   pit[11]:='1';
   {? var_pres('REZYDENT',VAT_DEK)>0
   || pit[11]:={? VAT_DEK.REZYDENT='T' || '1' || '2' ?}
   ?};
   pit[12]:={? VAT_DEK.PESEL=''
            || __PES:=0;
               VAT_DEK.NIP
            || __PES:=1;
               VAT_DEK.PESEL
            ?};
   'jesli jest to nierezydent czyli 2 to wypelnij pola 12, 13, 14';
   {? pit[11]='2'
   || pit[13]:=VAT_DEK.ZNIP;
      pit[14]:=VAT_DEK.RZNIP().NAZWA;
      pit[15]:=VAT_DEK.KRAJZNIP().NAZ
   || pit[13]:=pit[14]:=pit[15]:=''
   ?};
   pit[16]:=VAT_DEK.NAZWISKO;
   pit[17]:=VAT_DEK.PIERWSZE;
   pit[18]:=VAT_DEK.DATA_UR$4;
   {? VAT_DEK.KRAJ<>null || pit[19]:=VAT_DEK.KRAJ().NAZ || pit[19]:='' ?};
   pit[20]:=VAT_DEK.WOJEWODZ;
   pit[21]:=VAT_DEK.POWIAT;
   pit[22]:=VAT_DEK.GMINA;
   pit[23]:=VAT_DEK.ULICA;
   pit[24]:=VAT_DEK.DOM;
   pit[25]:=VAT_DEK.LOKAL;
   pit[26]:=VAT_DEK.MIASTO;
   pit[27]:=VAT_DEK.KOD;

   {! _nx:=14..27 |! pit[_nx]:=~-pit[_nx] !};

   pit[28]:=#VAT_DEK.PKU;
   {? VAT_DEK.KOR='T'
   || __ord[2]:={? VAT_DEK.PESEL='' || VAT_DEK.NIP || VAT_DEK.PESEL ?};
      __ord[9]:=VAT_DEK.NAZWISKO;
      __ord[10]:=VAT_DEK.PIERWSZE;
      __ord[11]:=STR.gsub(VAT_DEK.DATA_UR$4,'.','-');
      __ord[12]:='';
      __ord[13]:=exec('opis_korekty','edeklar');
      {! _nx:=1..obj_len(__ord) |! __ord[_nx]:=~(-__ord[_nx]) !};
      __ord[3]:=pit[2]
   ?};

   VAT_POZ.index('NR_NRK');
   VAT_POZ.prefix(VAT_DEK.ref);
   {? VAT_POZ.first()
   || {!
      |? {! _ind:=1..4
         |! _kw:=($('VAT_POZ.KW'+$_ind))();
            _poz:=($('VAT_POZ.P'+$_ind))();
            {? _poz>=1 & _poz<=obj_len(pit)
            || pit[($('VAT_POZ.P'+$_ind))()]:=_kw;
               {? VAT_POZ.NR=17 || pit[90]:=VAT_POZ.RODZ ?};
               {? VAT_POZ.NR=18 || pit[92]:=VAT_POZ.RODZ ?};
               {? VAT_POZ.NR=19 || pit[94]:=VAT_POZ.RODZ ?};
               {? VAT_POZ.NR=31 & __AK147
               || __P_107.PKT_152:=VAT_POZ.ZP152;
                  __P_107.PKT_153:=VAT_POZ.ZP153;
                  __P_107.PKT_154:=VAT_POZ.ZP154
               ?};
               {? (VAT_POZ.NR>=1 & VAT_POZ.NR<=15) | (VAT_POZ.NR>=21 & VAT_POZ.NR<=24)
                  | VAT_POZ.NR=28 | VAT_POZ.NR=32 | VAT_POZ.NR=33
               || czy_pod+=_kw
               ?}
            ?};
            czy_raport+=_kw
         !};
         VAT_POZ.next()
      !}
   ?};
   {? czy_pod | VAT_DEK.KOR='T' & czy_raport=0
   || pit[111]:=VAT_DEK.IMIE+' '+VAT_DEK.NAZ+{? VAT_DEK.STN<>'' || ', '+VAT_DEK.STN || '' ?}
   ?};
   czy_inf:=pit[96];
   {? czy_inf || pit[112]:=VAT_DEK.IMIE+' '+VAT_DEK.NAZ+{? VAT_DEK.STN<>'' || ', '+VAT_DEK.STN || '' ?} ?};
   ~~
}
{IF: czy_raport>0 | VAT_DEK.KOR='T' & czy_raport=0}
{IF: VAT_DEK.KOR='T' & czy_raport=0}
{ _wyb:=FUN.choice('Należy określić miejsce drukowania podpisu dla korekty deklaracji PIT-11.'@,,
        'Podpis w bloku H oraz I.'@,'I. Podpis osoby upoważnionej'@,,,'H. Podpis płatnika'@,
        ' Podatnik: '+VAT_DEK.NAZWISKO+' '+VAT_DEK.PIERWSZE+' '+{? VAT_DEK.PESEL='' || VAT_DEK.NIP || VAT_DEK.PESEL ?});
  {? _wyb=0
  || czy_pod:=1
  |? _wyb=2
  || czy_inf:=1;
     pit[112]:=pit[111];
     pit[111]:=''
  || czy_pod:=1;
     czy_inf:=1;
     pit[112]:=pit[111]
  ?};
~~}
{FI}
{REP: '{INCLUDE: ppl_dkw1128g.rpi}'}
{IF: VAT_DEK.KOR='T' & __ord[13]<>''}
{REP: '{INCLUDE: ppl_ord_zu3g.rpi}'}
{FI}
{FI}
:!UTF-8
{TITLE:B. Szczegółowe zestawienie sprzedaży}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   fakz_002:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','dokl','doklw','PRN','kgr');
      VAR_DEL.delete('wyjscie','X_Xa','X_Xb');
      VAR_DEL.delete('filtr_m','rodzaj');
      VAR_DEL.delete('od_datyw','do_datyw');
      VAR_DEL.delete('od_daty','do_daty','lp','wydr_naz','wydr_edi')";
   fakz_002();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   VAR_DEL.delete('n','k','w','s','PRN');
   n     :=obj_new(8); "--nazwy kolumn--";
   k     :=obj_new(8); "--szerokosci kolumn--";
   w     :=obj_new(8); "--wartosci kolumn--";
   s     :=obj_new(3); "--razem--";
   dokl  :=3; "--dokladnosc ilosci--";
   doklw :=2; "--dokladnosc wartosc--";
   PRN   :=obj_new(@.CLASS.PRINT,9); PRN.s_frame();

   k[1] :=5;      n[1] :='Lp.'@;
   k[2] :=13;     n[2] :='Indeks'@;
   k[3] :=25;     n[3] :='Nazwa materiału/usługi'@;
   k[4] :=5;      n[4] :='jm'@;
   k[5] :=15;     n[5] :='Ilość'@;
   k[6] :=17;     n[6] :='Wartość netto'@;
   k[7] :=17;     n[7] :='Wartość VAT'@;
   k[8] :=17;     n[8] :='Wartość brutto'@;

   {! _ii..8 |! w[_ii] :='' !};
   {! _ii..3 |! s[_ii] :=0 !};
   tryb:=rep_export();
   kgr:='';
   od_datyw:=do_datyw:=date(0,0,0);
   od_daty:=do_daty:=date(0,0,0);
   lp:=0;

   filtr_m:='N';
   rodzaj:='';

   wydr_naz :='fakz_002';
   wydr_edi :='fakz_002';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   wyjscie:=1;
   WYDRUKIL.win_edit(~wydr_edi);
   {? WYDRUKIL.edit()
   || {? WYDRUKIL.put()
      || filtr_m:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         od_datyw:=WYDRUKIL.OD_DATY2;
         do_datyw:=WYDRUKIL.DO_DATY2;
         od_daty:=WYDRUKIL.OD_DATY;
         do_daty:=WYDRUKIL.DO_DATY;
         rodzaj:={? WYDRUKIL.RADIOB_1=2 || 'U' |? WYDRUKIL.RADIOB_1=1 || 'T' || '' ?};
         wyjscie:=0
      ?}
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_m','material',filtr_m,'N',rodzaj) ?}
}
{END:
   fakz_002(); VAR_DEL.delete('fakz_002')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
PRN.add("w[3]",k[3],n[3]);
PRN.add("w[4]",k[4],n[4]);
PRN.add("w[5]",k[5],n[5],1);
{? exec('upr_cs','ceny')
||
   PRN.add("w[6]",k[6],n[6],1);
   PRN.add("w[7]",k[7],n[7],1);
   PRN.add("w[8]",k[8],n[8],1)
?};

"--obliczenia--";
VAR_DEL.delete('X_Xb');
X_Xb:=tab_tmp(1,
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'N'      ,'STRING[100]' ,'Nazwa usługi',
   'JM'     ,'STRING[10]'  ,'jm',
   'IL'     ,'REAL'        ,'Ilość',
   'WN'     ,'REAL'        ,'Netto',
   'WV'     ,'REAL'        ,'VAT',
   'WB'     ,'REAL'        ,'Brutto',
   'MGR'    ,'STRING[8]'   ,'Grupa materiałowa');
_ndx:=X_Xb.ndx_tmp('',,'JM',,,'JM',,,'KTM',,,'KTM',,);
X_Xb.index(_ndx);

_stsupr:=exec('get','#params',2102,2,OPERATOR.USER)='T';
_stss:="
_wyn:=exec('usr_upr','b_perm','STS',_a,OPERATOR.USER);
_wyn
";
OKR.cntx_psh; FAKS.cntx_psh; FAP.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
_min:="{? _a & _a<_b || _a || _b ?}";
_min:=_min(od_datyw~1,od_daty~1);
_max:="{? _a & _a>_b || _a || _b ?}";
_max:=_max(do_datyw~1,do_daty~1);
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
{? OKR.first() & (_min=0 | OKR.find_ge(_min)) & X_Xa.first
||
   {!
   |?
      FAKS.use('faktu'+ST.ODDZ+(($OKR.ROK)+2));
      FAP.use('fakpo'+ST.ODDZ+(($OKR.ROK)+2));
      FAP.index('REPORT');
      {? X_Xa.first()
      ||
         {!
         |?
            FAP.prefix('S','T',X_Xa.REFI);
            {? FAP.first()
            ||
               {!
               |?
                  {? FAP.FAKS
                     & FAP.FAKS().STAT_REJ<>'A'
                     & FAP.FAKS().ZEST='T' & FAKS.ZEST_AKC='T'
                     & (_stsupr=0 | _stsupr=1 & FAKS.STS<>null & _stss(FAKS.STS))
                     & ((od_datyw<=FAKS.DW | od_datyw=date(0,0,0)) & (FAKS.DW<=do_datyw | do_datyw=date(0,0,0)))
                     & ((od_daty<=FAKS.D | od_daty=date(0,0,0)) & (FAKS.D<=do_daty | do_daty=date(0,0,0)))
                     & FAP.WB<>0
                  ||
                     X_Xb.prefix(FAP.JM().KOD,FAP.JM().KOD,FAP.M().KTM,FAP.M().KTM);
                     {? X_Xb.first
                     ||
                        X_Xb.IL+=FAP.IL;
                        X_Xb.WN+=FAP.WWAL_P;
                        X_Xb.WV+=FAP.VWAL_P;
                        X_Xb.WB+=FAP.BWAL_P;
                        X_Xb.put()
                     ||
                        X_Xb.KTM :=FAP.M().KTM;
                        X_Xb.N   :=FAP.M().N;
                        X_Xb.MGR :=FAP.M().MGR().KOD;
                        X_Xb.JM  :=FAP.JM().KOD;
                        X_Xb.IL  :=FAP.IL;
                        X_Xb.WN  :=FAP.WWAL_P;
                        X_Xb.WV  :=FAP.VWAL_P;
                        X_Xb.WB  :=FAP.BWAL_P;
                        X_Xb.add()
                     ?}
                  ?};
                  FAP.next()
               !}
            ?};
            X_Xa.next
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max)
   !}
?};
OKR.cntx_pop; FAKS.cntx_pop; FAP.cntx_pop;

PAR_WYDR.TITLE:='SZCZEGÓŁOWE ZESTAWIENIE SPRZEDAŻY';
prn1:='PRN'
;~~}
{IF: X_Xb.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Sprzedaż'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: filtr_m='T'}{<{lmt+1}}{'Z wyborem materiału i usługi'}{FI}
{<{lmt+1}'Rodzaj pozycji: '+{? rodzaj='T' || 'materiał' |? rodzaj='U' || 'usługa' || 'materiały i usługi' ?}}
{<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
{<{lmt+1}}{'Data wystawienia'}{<{lmt+1+20}od_datyw}{<{lmt+1+40}do_datyw}
{<{lmt+1}}{'Data sprzedaży'}{<{lmt+1+20}od_daty}{<{lmt+1+40}do_daty}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xb}
{INDEX: X_Xb.ndx_tmp('',,'KTM',,,'JM',,)}
{PREFIX:}
{DO}
   {FIRST}
   { dokl:=exec('fld_prec','#field',X_Xb,'IL');~~}
   {FIRST-}
   {
   w[1] :=form(lp+=1,,,'99');
   w[2] :=form(X_Xb.KTM);
   w[3] :=form(X_Xb.N);
   w[4] :=form(X_Xb.JM);
   w[5] :=form(X_Xb.IL,,dokl,'.,');
   w[6] :=form(X_Xb.WN,,doklw,'.,');
   w[7] :=form(X_Xb.WV,,doklw,'.,');
   w[8] :=form(X_Xb.WB,,doklw,'.,');
   kgr  :=form(X_Xb.MGR);
   s[1] +=X_Xb.WN;
   s[2] +=X_Xb.WV;
   s[3] +=X_Xb.WB
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: exec('upr_cs','ceny')}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {SUBSTITUTE: 'LINIAF'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {
   w[1] :=w[2] :=w[3] :=w[4] :='';
   w[5] :='Razem:'; PRN.FORM[5] :=0;
   w[6] :=form(s[1],,doklw,'.,');
   w[7] :=form(s[2],,doklw,'.,');
   w[8] :=form(s[3],,doklw,'.,');
   PRN.n_frame()
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{FI}
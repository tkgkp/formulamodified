:!UTF-8
{COMMENT}   MACRO - foot_tab                                         {COMMENT-}
{COMMENT}   Makro uzywane do drukowania sto3ki tabeli z pozycjami    {COMMENT-}
{DEFINEFONT: 'TM=Inter,,M,,0,0'}
{DEFINEFONT: 'TMB=Inter,,N,,0,0'}
{DEFINEFONT: 'TF=Inter,,F,,0,0'}
{MACRO: 'POZYCJA'}
{ llin:=lineleft(); lpoz:=0; lmax:=0;
  logr:={? __walh<>__walp & PARAM_W.WALH='T' || 4 || 3 ?};
  params_set(params_get());
  _f:='params_set(params_get()); '+prn1+'.ini_line()'; _v:=($(_f))();
  _f:=prn1+'.length()'; lmax:=($(_f))(); headfoot:={? lmax>(llin-logr-1) || 2 || 0 ?}; ~~}
{WHILE: lmax>0}
{DO}
   {IF: llin<=(logr+1)}
      {bpage:=page(); ~~}
      {IF: lpoz>0}{ {? headfoot=2 || {? ~walutowa || do_przep() || do_przeh() ?}; headfoot:=1 ?}; ~~}{FI}
      {SUBSTITUTE: 'foot_tab'}
      {IF: bpage=page}{PAGE}{FI}
      { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
      {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
      { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
      { llin:=lineleft(); ~~}
      {IF: lpoz=0}{ {? headfoot=2 || {? ~walutowa || do_przep() || do_przeh() ?}; headfoot:=1 ?}; ~~}{FI}
   {ELSE}
      { lpoz:=0; ~~}
      {ENTIRE}
      {IF: headfoot<>1}
         {IF: vp}
            { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
            {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
            { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
         {ELSE}
            {IF: PAR_WYDR.RSEP & rsep}
               {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
            {ELIF: rsep}
               {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
            {FI}
         {FI}
      {FI}
      {WHILE: _f:=prn1+'.next()'; ($(_f))() & (llin-lpoz>(logr+1) | lpoz=0)}
      {DO}
         { lpoz+=1; lmax-=1; ~~}
         {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
      {OD}
      {ENTIRE-}
      { llin:=lineleft(); ~~}
   {FI}
{OD}
{MACRO-}
{MACRO: 'foot_tab'}
{
vp:=0;
prn1_tmp:=prn1;
prn2_tmp:=prn2;
prn1:='PRN1';
prn2:='PRN3';
prz[1]:=exec('tr','tlumaczenia','1000130');
prz[2]:=do_p[1];
prz[3]:='';
prz[4]:=do_p[2];
prz[5]:=do_p[3];
prz[6]:=__walp;
rrsep:=rsep;
rsep:=1;
walutowa:=0
;~~}
{IF: PARAM_W.WALH<>'H'}{SUBSTITUTE: 'LINIA02'}{FI}
{IF: __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H')}
   {
   {? PARAM_W.WALH<>'H' || rsep:=0 ?};
   walutowa:=1;
   {? PARAM_W.WALH<>'H' || prz[1]:='' ?};
   prz[2]:=do_p[4];
   prz[4]:=do_p[5];
   prz[5]:=do_p[6];
   prz[6]:=__walh
   ;~~}
   {SUBSTITUTE: 'LINIA02'}
{FI}
{prn1:='PRN3'; vp:=0; rsep:=rrsep; VAR_DEL.delete('rrsep');~~}
{SUBSTITUTE: 'LINIAF'}
{prn1:=prn1_tmp; prn2:=prn2_tmp; VAR_DEL.delete('prn1_tmp','prn2_tmp'); vp:=1;~~}
{MACRO-}
{COMMENT}   MACRO - druk                                             {COMMENT-}
{MACRO: 'druk'}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}
{FOOTER}
{{? ilkop=1 || stron:=page-pages ?};~~}{FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}{<1>{charleft}
   exec('tr','tlumaczenia','1000211')+' '+$(page-pages-(ilkop-1)*stron)}
{FOOTER-}
{WHILE: ilkop<=PARAM_W.KOPIA}
{DO}
   {
   PAR_WYDR.TITLE:=exec('trans_typysp','tlumaczenia',TYPYSP.ref(),PARAM_W.JEZYK)+'\n'
      +exec('tr','tlumaczenia','1000102')+' '+FAKS.SYM;
   prn1:='PRN1';
   prn2:='PRN2';
   rsep:=0
   ;~~}
{COMMENT}   REPORT - HEADER                                          {COMMENT-}
   { head:=PAR_WYDR.HEAD;PAR_WYDR.HEAD:=0;~~}
   {SUBSTITUTE: 'HEADF'}
{COMMENT}   HEDER obowiazujacy przed wydrukiem pozycji               {COMMENT-}
   {HEADER}
   {SUBSTITUTE: 'HEADF'}
   {HEADER-}
{COMMENT}   oryginal / kopia, tryb probny                            {COMMENT-}
   {FONT: exec('font','param_wydr','ory_kop')}{SPACE: exec('space','param_wydr')}
   {IF: (';NZA'*FAKS.STAT_REJ)>1}{<1}{>{charleft}exec('tr','tlumaczenia','100021'+{? FAKS.STAT_REJ='A' || '5' || '0' ?})}{FI}
   {<1}{>{charleft}{? ~skladanka
      || {? ilkop=0 || exec('tr','tlumaczenia','1000103') || exec('tr','tlumaczenia','1000104') ?}
      || '' ?}+{? -PARAM_W.DR_DUPL='t' ||' - '+exec('tr','tlumaczenia','1000105')+' '+PARAM_W.DUPLIKAT$1||''?}}
{ ilkop+=1;~~}
{COMMENT}   DANE NAGLOWKOWE                                          {COMMENT-}
{COMMENT}   strony transakcji                                        {COMMENT-}
{ __STRTAB:='FAKS'; __STRODB:='FAKS.KH_ODB'; __STRLIN:=''; __STRDAT:=FAKS.DW;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
   {FONT: exec('font','param_wydr','strony_po')}
   {<1}{
   _sz1:=max_T8G/2$0;_sz2:=1;
   print4(_sz1,_sz2);
   _sz2+=charleft-PRN4.width();
   print4(_sz1,_sz2);
   ~~}
{COMMENT}   kurs dla waluty                                          {COMMENT-}
   {IF: walutowa=1}
      {
      op_1:=exec('tr','tlumaczenia','1000126')+' '+__walh+': '+form(FAKS.KRS,,4)+' '+
         exec('tr','tlumaczenia','1000127')+' '+$FAKS.DTK;
      B.cntx_psh();
      B.index('BANKOD');
      B.prefix(FAKS.BTK,);
      op_2:={? FAKS.SWAL=1 & B.first || exec('tr','tlumaczenia','1000128')+' '+B.NB || '' ?};
      B.cntx_pop()
      ;~~}
      { Text:=op_1; Line:=0; ~~}
      {SUBSTITUTE: 'Text'}
      { Text:=op_2; Line:=0; ~~}
      {SUBSTITUTE: 'Text'}
   {FI}
{COMMENT}   zamowienia                                               {COMMENT-}
   {IF: zam<>''}
      { Text:=zam; Line:=0; ~~}
      {SUBSTITUTE: 'Text'}
   {FI}
{COMMENT}   dokumenty magazynowe                                     {COMMENT-}
   {IF: mag<>''}
      { Text:=mag; Line:=0; ~~}
      {SUBSTITUTE: 'Text'}
   {FI}
{COMMENT}   wyliczenie szerokosci kolumn                             {COMMENT-}
   {FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
   {<1}{
   prn1:='PRN1';prn2:='PRN2';
   {? ilkop=1
   ||
      {! _i:=1..obj_len(fak) |! fak[_i]:=0 !};
      _czy_rab:=0;
      _war9:=_war11:=_war12:=0;
      {? FAKS.DSP_WPOZ='T' || fak[16]:=10 ?};
      FAP.index('FAP');
      FAP.prefix(FAKS.ref());
      {? FAP.first()
      || {!
         |? fak[1]:={? (+form(FAP.POZ,,0))>fak[1] || +form(FAP.POZ,,0) || fak[1] ?};
            _uwagi:={? FAP.memo_lin('UWAGI')<>'\n' || gsub(FAP.memo_lin('UWAGI'),'\n',' ') || '' ?};
            fak[2]:={? (+(FAP.M().KTM+' - '+M.N+' '+_uwagi))>fak[2] || +(M.KTM+' - '+M.N+' '+_uwagi) || fak[2] ?};
            _pkwiudl:=+exec('pkwiu','material',FAKS.DW,FAP.M,1,FAP.PKWIU_CN);
            fak[3]:={? _pkwiudl>fak[3] || _pkwiudl || fak[3] ?};
            fak[4]:={? (+form(FAP.IL,,ddokl))>fak[4] || +form(FAP.IL,,ddokl) || fak[4] ?};
            _tlumacz:=exec('trans_jm','tlumaczenia',FAP.JM,PARAM_W.JEZYK);
            _tlumacz:={? PARAM_W.JEZYK<>null || _tlumacz || FAP.JM().KOD ?};
            fak[5]:={? (+_tlumacz)>fak[5] || +_tlumacz || fak[5] ?};
            {? walutowa=0 | walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='N')
            || fak[6]:={? +form(FAP.CPR,,cdokl)>fak[6] || +form(FAP.CPR,,cdokl) || fak[6] ?}
            ?};
            {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
            || fak[6]:={? +form(FAP.CWAL,,cdokl)>fak[6] || +form(FAP.CWAL,,cdokl) || fak[6] ?}
            ?};
            fak[7]:={? (+form(FAKS.RAB+FAP.RAB,,rdokl))>fak[7] || +form(FAKS.RAB+FAP.RAB,,rdokl) || fak[7] ?};
            _czy_rab+=FAKS.RAB+FAP.RAB;
            {? walutowa=0 | walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='N')
            || fak[8]:={? +form(FAP.CN,,cdokl)>fak[8] || +form(FAP.CN,,cdokl) || fak[8] ?}
            ?};
            {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
            || _rabat:=1-exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)/100;
               fak[8]:={? +form(FAP.CWAL*_rabat,,2)>fak[8] || +form(FAP.CWAL*_rabat,,2) || fak[8] ?}
            ?};
            fak[9]+=FAP.WWAL_P;  _war9+=FAP.WWAL;
            fak[10]:=5;
            fak[11]+=FAP.VWAL_P; _war11+=FAP.VWAL;
            fak[12]+=FAP.BWAL_P; _war12+=FAP.BWAL;
            fak[13]:={? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || 6 || 0 ?};
            FAP.next()
         !}
      ?};
      {? walutowa=0 | PARAM_W.WALH='N' || _war9:=_war11:=_war12:=0 ?};
      fak[9]:={? +form(fak[9],,2)>+form(_war9,,2) || +form(fak[9],,2) || +form(_war9,,2) ?};
      fak[11]:={? +form(fak[11],,2)>+form(_war11,,2) || +form(fak[11],,2) || +form(_war11,,2) ?};
      fak[12]:={? +form(fak[12],,2)>+form(_war12,,2) || +form(fak[12],,2) || +form(_war12,,2) ?};
      _war9:=_war11:=_war12:=0;
      {? FAKS.WHERE='L'
      ||
         FAKSV.index('FF_SV');
         FAKSV.prefix('',faks);
         {? FAKSV.first
         ||
            {!
            |?
               _war9+=FAKSV.WN_Z;
               _war11+=FAKSV.WV_Z;
               _war12+=FAKSV.WB_Z;
               FAKSV.next
            !}
         ?};
         fak[9]:={? fak[9]>+form(_war9,,2) || fak[9] || +form(_war9,,2) ?};
         fak[11]:={? fak[11]>+form(_war11,,2) || fak[11] || +form(_war11,,2) ?};
         fak[12]:={? fak[12]>+form(_war12,,2) || fak[12] || +form(_war12,,2) ?}
      |? exec('czy_end','faktury_zalicz')
      ||
         FAKSV.index('FF_SV');
         FAKSV.prefix('',faks);
         {? FAKSV.first
         ||
            {!
            |? _war:=FAKSV.WN_R; _war9+=_war;
               fak[9]:={? fak[9]>+form(_war,,2) || fak[9] || +form(_war,,2) ?};
               _war:=FAKSV.WV_R; _war11+=_war;
               fak[11]:={? fak[11]>+form(_war,,2) || fak[11] || +form(_war,,2) ?};
               _war:=FAKSV.WB_R; _war12+=_war;
               fak[12]:={? fak[12]>+form(_war,,2) || fak[12] || +form(_war,,2) ?};
               FAKSV.next
            !};
            fak[9]:={? fak[9]>+form(_war9,,2) || fak[9] || +form(_war9,,2) ?};
            fak[11]:={? fak[11]>+form(_war11,,2) || fak[11] || +form(_war11,,2) ?};
            fak[12]:={? fak[12]>+form(_war12,,2) || fak[12] || +form(_war12,,2) ?}
         ?};
         {? walutowa
         || _war9:=_war11:=_war12:=0;
            FAKSV.index('FF_SV');
            FAKSV.prefix('',faks);
            {? FAKSV.first
            ||
               {!
               |?
                  _war:=FAKSV.WW-FAKSV.WW_Z; _war12+=_war;
                  fak[12]:={? fak[12]>+form(_war,,2) || fak[12] || +form(_war,,2) ?};
                  _sv:=(#((FAKSV.SV().KOD*'%')+FAKSV.SV().KOD-1))*0.01;
                  _wwn:=(FAKSV.WW/(1+_sv))$2;
                  _wwn_z:=(FAKSV.WW_Z/(1+_sv))$2;
                  _war:=_wwn-_wwn_z; _war9+=_war;
                  fak[9]:={? fak[9]>+form(_war,,2) || fak[9] || +form(_war,,2) ?};
                  _wwv:=FAKSV.WW-_wwn;
                  _wwv_z:=FAKSV.WW_Z-_wwn_z;
                  _war:=(_wwv-_wwv_z)$2; _war11:=_war;
                  fak[11]:={? fak[11]>+form(_war,,2) || fak[11] || +form(_war,,2) ?};
                  FAKSV.next
               !};
               fak[9]:={? fak[9]>+form(_war9,,2) || fak[9] || +form(_war9,,2) ?};
               fak[11]:={? fak[11]>+form(_war11,,2) || fak[11] || +form(_war11,,2) ?};
               fak[12]:={? fak[12]>+form(_war12,,2) || fak[12] || +form(_war12,,2) ?}
            ?}
         ?}
      ?};
      {? _czy_rab=0 || fak[6]:=fak[7]:=0 ?};
      fak[1]:=3;
      {? ('TO'*ndvat) || fak[10]:=fak[11]:=fak[12]:=0 ?};
      {! _i:=1..obj_len(k) |! {? fak[_i]=0 || k[_i]:=0 ?} !};
      {! _i:=3..obj_len(k)
      |!
         {? fak[_i]>k[_i] || k[_i]:=fak[_i] ?}
      !};
      k[2]:=1; print1();
      k[2]+=charleft-PRN1.width();
      {? k1[1]<fak[9]  || k1[1]:=fak[9] ?};
      {? k1[2]<fak[11] || k1[2]:=fak[11] ?};
      {? k1[3]<fak[12] || k1[3]:=fak[12] ?};
      print1(); print2(); print3()
   || print2()
   ?}
   ;~~}
{COMMENT}   informacje dodatkowe dla naglowka                        {COMMENT-}
   {
   "--pobranie danych--"; exec('pobgdnag','fakso','F',FAKS.ref);
   VAR_DEL.delete('oo','OPIK')
   ;~~}
   {FOR: S_GD}
   {PREFIX: 'G'}
   {DO}
      {FIRST}
         {LINE}
      {FIRST-}
      {SUBSTITUTE: 'inf_dod'}
   {OD}
{COMMENT}   platnosci                                                {COMMENT-}
   {FOR: FAKSPL}
   {INDEX: 'FAKS'}
   {PREFIX: FAKS.ref}
   {DO}
      {<{lmt+1}}{
      _tlumacz:={? PARAM_W.JEZYK=null || '' || exec('trans_slo','tlumaczenia',FAKSPL.PL,PARAM_W.JEZYK) ?};
      fp_tr:=exec('tr','tlumaczenia','1000121')+' ';
      fp_war:=
         {? PARAM_W.JEZYK<>null & _tlumacz<>''
         || _tlumacz
         || FAKSPL.PL().TR
         ?};
      tp_tr:=exec('tr','tlumaczenia','1000124');
      tp_war:=' '+form(FAKSPL.TERMPLAT);
      wp_tr:=', '+exec('tr','tlumaczenia','1000125');
      wp_war:=' '+form(FAKSPL.WAR,,2,'.,');
      wp_wal:=' '+__walh;
      odstep:=charleft()-(+fp_tr)-(+fp_war)-(+tp_tr)-(+tp_war)-(+wp_tr)-(+wp_war)-(+wp_wal);
      odstep:={? odstep>0 || ' '*odstep || ' ' ?};
      ~~}
      {<{lmt+1}
         }{FONT: exec('font','param_wydr','fp_tr')}{fp_tr
         }{FONT: exec('font','param_wydr','fp_war')}{fp_war
         }{FONT: exec('font','param_wydr','fp_ods')}{odstep
         }{FONT: exec('font','param_wydr','tp_tr')}{tp_tr
         }{FONT: exec('font','param_wydr','tp_war')}{tp_war
         }{FONT: exec('font','param_wydr','wp_tr')}{wp_tr
         }{FONT: exec('font','param_wydr','wp_war')}{wp_war
         }{FONT: exec('font','param_wydr','wp_wal')}{wp_wal}
   {OD}
{COMMENT}   odrysowanie po raz pierwszy naglowka tabeli pozycji      {COMMENT-}
   {FONT: exec('font','param_wydr','tabhead_poz')}
   {
   prn1:='PRN1'; prn2:='PRN2';
   {! _ii:=1..6 |! s[_ii]:=0 !};
   sp[1]:=sp[2]:=sp[3]:=0;
   il_lin:=0;
   FAP.index('FAP'); FAP.prefix(FAKS.ref()); {? FAP.first() || pozycja() ?};
   logr:=9+{? PARAM_W.WALH='T' || 1 || 0 ?}+{? exec('czy_rab','faktury_wspolne') || 2 || 0 ?};
   ~~}
   {IF: lineleft()>0 & lineleft()<logr}{PAGE}{FI}
   {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{COMMENT}   HEADER obowiazujacy przy drukowaniu pozycji faktury      {COMMENT-}
   {HEADER}
      {SUBSTITUTE: 'HEADF'}
      {FONT: exec('font','param_wydr','header')}{SPACE: exec('space','param_wydr')}
      {IF: fabs(do_p[1])+fabs(do_p[2])+fabs(do_p[3])+fabs(do_p[4])+fabs(do_p[5])+fabs(do_p[6])}
         {
         vp_tmp:=vp; vp:=0;
         rsep_tmp:=rsep; rsep:=0;
         prn1:='PRN3';
         prz[1]:=exec('tr','tlumaczenia','1000129');
         prz[2]:=do_p[1];
         prz[4]:=do_p[2];
         prz[5]:=do_p[3];
         prz[6]:=__walp
         ;~~}
         {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
         {
         ll:=lineleft();
         _f:=prn1+'.ini_line()'; _v:=($(_f))();
         _f:=prn1+'.length()'; _v:=($(_f))();
         ll<(rsep+_v) & ll<>0
         ;~~}
         {WHILE: _f:=prn1+'.next()'; ($(_f))()}
         {DO}
            {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
         {OD}
         {IF: __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H')}
            {
            prz[1]:='';
            prz[2]:=do_p[4];
            prz[4]:=do_p[5];
            prz[5]:=do_p[6];
            prz[6]:=__walh
            ;~~}
            {
            ll:=lineleft();
            _f:=prn1+'.ini_line()'; _v:=($(_f))();
            _f:=prn1+'.length()'; _v:=($(_f))();
            ll<(rsep+_v) & ll<>0
            ;~~}
            {WHILE: _f:=prn1+'.next()'; ($(_f))()}
            {DO}
               {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
            {OD}
         {FI}
         {SUBSTITUTE: 'LINIAF'}
         { prn1:='PRN1'; vp:=vp_tmp; rsep:=rsep_tmp; VAR_DEL.delete('vp_tmp','rsep_tmp');~~}
      {FI}
      {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
      {SUBSTITUTE: 'TABHEAD'}
   {HEADER-}
{COMMENT}   drukowanie pozycji faktury                               {COMMENT-}
   {FONT: exec('font','param_wydr','poz')}{SPACE: exec('space','param_wydr')}
   {
   rsep:=PAR_WYDR.RSEP;
   prn1:='PRN1'; prn2:='PRN2';
   {! _ii:=1..6 |! s[_ii]:=0 !};
   sp[1]:=sp[2]:=sp[3]:=0;
   {! _ii:=1..6 |! do_p[_ii]:=0 !}
   ;~~}
   {FOR: FAP}{INDEX: 'FAP'}{PREFIX: FAKS.ref}
   {DO}
      { headfoot:=0; doxprzeh:=0; il_lin:=0; walutowa:=PARAM_W.WALH='H'; pozycja();
        {? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || il_lin+=2 ?}; ~~}
      {SUBSTITUTE: 'POZYCJA'}
      {IF: __walh<>__walp & PARAM_W.WALH='T'}
         { {? PARAM_W.WALH='T' || rsep:=0 ?}; walutowa:=1; pozycja(); logr:=4; ~~}
         {IF: lineleft()<=(logr+1)}
            {bpage:=page(); do_przep(); doxprzeh:=1; ~~}
            {SUBSTITUTE: 'foot_tab'}
            {IF: bpage=page}{PAGE}{FI}
            { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
            {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
            { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
         {ELSE}
            { do_przeh(); ~~}
         {FI}
         {SUBSTITUTE: 'LINIA0'}
         { rsep:=PAR_WYDR.RSEP;~~}
      {FI}
      { {? ~headfoot || {? PARAM_W.WALH<>'H' || do_przep() || do_przeh() ?} ?}; headfoot:=0; ~~}
      { {? doxprzeh=1 || do_przeh() ?}; ~~}
{COMMENT}   informacje dodatkowe dla pozycji                         {COMMENT-}
      {FOR: FAKSO}{INDEX: 'FAP'}{PREFIX: FAP.ref}
      {DO}
         {FIRST}
            {
            VAR_DEL.delete('w_tmp');
            w_tmp:=obj_new(obj_len(k));
            {! _i..obj_len(k) |! w_tmp[_i]:=w[_i] !}
            ;~~}
         {FIRST-}
         { {! _i..obj_len(k) |! w[_i]:='' !};
           w[2]:=exec('printFAKSO','fakso',PARAM_W.JEZYK);
           PRN1.ini_line();~~}
         {WHILE: _f:=prn1+'.next()'; ($(_f))()}
         {DO}
            {IF: lineleft()>0 & lineleft()<5}
               { bpage:=page(); {! _i..obj_len(k) |! w[_i]:=w_tmp[_i] !};~~}
               {SUBSTITUTE: 'foot_tab'}
               {IF: bpage=page}{PAGE}{FI}
               { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
               {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
               { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); vp:=0; ~~}
            {FI}
            {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
         {OD}
         {TOTAL}
            { VAR_DEL.delete('w_tmp');~~}
      {OD}
   {OD}
{COMMENT}   podsumowanie pozycji                                     {COMMENT-}
   {
   w[1]:=exec('tr','tlumaczenia','1000115');
   w[9]:=s[1];
   w[10]:='';
   w[11]:=s[2];
   w[12]:=s[3];
   w[13]:=__walp;
   vp:=0;
   rsep:=1;
   ~~}
   {IF: __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H')}
      {IF: PARAM_W.WALH<>'H'}{SUBSTITUTE: 'LINIA02'}{FI}
      {
      w[1]:='';
      w[9]:=s[4];
      w[10]:='';
      w[11]:=s[5];
      w[12]:=s[6];
      w[13]:=__walh;
      {? PARAM_W.WALH<>'H' || vp:=0; rsep:=0; prn1:='PRN2' ?};
      ~~}
      {IF: PARAM_W.WALH<>'H'}{SUBSTITUTE: 'LINIA0'}{ELSE}{SUBSTITUTE: 'LINIA02'}{FI}
      { {? PARAM_W.WALH='H' || vp:=0; rsep:=0; prn1:='PRN2' ?};~~}
      {SUBSTITUTE: 'LINIAF'}
   {ELSE}
      {SUBSTITUTE: 'LINIAFS'}
   {FI}
   { rsep:=PAR_WYDR.RSEP;~~}
{COMMENT}   heder po wydrukowaniu pozyji                             {COMMENT-}
   {HEADER}
   {SUBSTITUTE: 'HEADF'}
   {HEADER-}
   {
   exec('jakislsv','ustawienia',2);
   prn1:='PRN2';tmp:=k[1];
   faksxx:=faks;
   FAKS.cntx_psh();
   {? exec('rozl_na_par','faktury_zalicz',FAKS.SYMF)=1 & FAKS.seek(BB.sqlint(FAKS.SYMF),) || faks:=BB.refsql(FAKS.ref()) ?};
   ~~}
   {IF: ~('TO'*ndvat) }
{COMMENT}   tabela podsumowujaca z pozycji faktury                   {COMMENT-}
      {IF: FAKS.WHERE<>'L'}
         { VAR_DEL.delete('il_stvat'); il_stvat:=exec('ilStVat','faktury_wydruk',FAKS.ref(),SLU.ref());~~}
         {FOR:'SLO'}{INDEX:'SL'}{PREFIX: SLU.ref}
         {DO}
            {FIRST}
               {FONT: exec('font','param_wydr','vat')}{SPACE: exec('space','param_wydr')}
               {
               licznik:=0;
               _ret:=__odsskr; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13) || k[_ii]+__odsram || 0 ?}) !}; _ret-=__odsram-__odsskr;
               tmp:=k[1];
               k[1]:=0;
               lmt:=_ret-{? vatzpoz=0 & odbrutto=1 || k1[1]+__odsram+k1[2]+__odsram |? vatzpoz=0 || k1[2]+__odsram+k1[3]+__odsram ?};
               il_lin:=il_stvat+{? walutowa=1 || 5 || 3 ?};
               print2(1,1)
               ;~~}
               {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
               {
               Text:=
                  {? walutowa=1
                  || exec('tr','tlumaczenia','1000131')+' '+(SLO.cntx_psh(); _wl:=FAP.WAL_P().KOD; SLO.cntx_pop(); _wl)
                  || exec('tr','tlumaczenia','1000132')
                  ?};
               Text_dl:=PRN2.width();
               ~~}
               {SUBSTITUTE: 'prn_txt'}
               {
               VAR_DEL.delete('Text','Text_dl');
               lmt-=1;
               ~~}
               {FONT: exec('font','param_wydr','vat')}{SPACE: exec('space','param_wydr')}
               {IF: vatzpoz=1}
                  {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
                  { rsep:=0; vp:=0;~~}
               {ELSE}
                  {FONT: exec('font','param_wydr','tabhead_vat')}{SPACE: exec('space','param_wydr')}
                  {SUBSTITUTE: 'TABHEAD'}
                  { rsep:=0; vp:=1;~~}
                  {FONT: exec('font','param_wydr','vat')}
               {FI}
            {FIRST-}
            { exec('w_tym','faktury_wydruk',FAKS.ref,SLO.ref,vat,2);~~}
            {IF:vat[1]<>0}
               {
               licznik+=1;
               w[9] :=vat[1];
               w[10]:=SLO.KOD;
               w[11]:=vat[2];
               w[12]:=vat[3];
               ~~}
               {SUBSTITUTE: 'LINIA0'}
               { rsep:=0; vp:=0;~~}
            {FI}
            {TOTAL}
               {IF: vatzpoz=0}
                  {
                  rsep:=1; vp:=0;
                  w[9] :=sp[1];
                  w[10]:='';
                  w[11]:=sp[2];
                  w[12]:=sp[3];
                  ~~}
                  {SUBSTITUTE: 'razem'}
                  { rsep:=0; vp:=0;~~}
               {FI}
               {IF: licznik>1 & walutowa=1 & vatzpoz=1}
                  { w[9]:=sp[1]; w[10]:=''; w[11]:=sp[2]; w[12]:=sp[3];~~}
                  {SUBSTITUTE: 'razem'}
               {FI}
               {IF: licznik>0}{SUBSTITUTE: 'LINIAF'}{FI}
               { k[1]:=tmp;~~}
         {OD}
         { VAR_DEL.delete('il_stvat');~~}
      {FI}
      {IF: FAKS.WHERE='L'}
{COMMENT}   tabela podusmowujaca dla faktury zaliczkowej             {COMMENT-}
         {FOR: FAKSV}
         {INDEX: 'FF_SV'}
         {PREFIX: '',faks}
         {DO}
            {FIRST}
               {FONT: exec('font','param_wydr','vat')}{SPACE: exec('space','param_wydr')}
               {
               licznik:=0;
               spz[1]:=spz[2]:=spz[3]:=0;
               _ret:=__odsskr; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13) || k[_ii]+__odsram || 0 ?}) !}; _ret-=__odsram-__odsskr;
               tmp:=k[1];
               k[1]:=0;
               lmt:=_ret;
               il_lin:=FAKSV.size()+5;
               print2(1)
               ;~~}
               {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
               {
               Text:=exec('tr','tlumaczenia','1000134')+' '+exec('tr','tlumaczenia','1000133')+' '+FAP.WAL_P().KOD;
               Text_dl:=PRN2.width();
               ~~}
               {SUBSTITUTE: 'prn_txt'}
               {
               VAR_DEL.delete('Text','Text_dl');
               lmt-=1;
               ~~}
               {FONT: exec('font','param_wydr','vat')}{SPACE: exec('space','param_wydr')}
               {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
               { rsep:=0; vp:=0;~~}
            {FIRST-}
            {
            vp:=0;
            licznik+=1;
            spz[1]+=w[9] :=FAKSV.WN_Z;
            spz[2]+=w[11]:=FAKSV.WV_Z;
            w[10]:=FAKSV.SV().KOD;
            spz[3]+=w[12]:=FAKSV.WB_Z
            ;~~}
            {SUBSTITUTE: 'LINIA0'}
            {TOTAL}
               {IF: licznik>1}
                  { w[9]:=spz[1]; w[10]:=''; w[11]:=spz[2]; w[12]:=spz[3];~~}
                  {SUBSTITUTE: 'razem'}
               {FI}
               {SUBSTITUTE: 'LINIAF'}
         {OD}
      {FI}
      {IF: exec('czy_end','faktury_zalicz')}
{COMMENT}   tabela podsumowujaca dla faktury rozliczajacej zaliczke  {COMMENT-}
         {FOR: FAKSV}
         {INDEX: 'FF_SV'}
         {PREFIX: '',faks}
         {DO}
            {FIRST}
               {FONT: exec('font','param_wydr','vat')}{SPACE: exec('space','param_wydr')}
               {
               sp[1]:=sp[9]:=sp[11]:=sp[12]:=0;
               il_lin:=FAKSV.size()+{? walutowa=1 || 5 || 3 ?}
               ;~~}
               {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
               {<{lmt+1}exec('tr','tlumaczenia','1000135')+' '+__walp}
               {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
            {FIRST-}
            {
            vp:=0;
            licznik+=1;
            w[12]:=FAKSV.WB_R;
            w[9] :=FAKSV.WN_R;
            w[10]:=FAKSV.SV().KOD;
            w[11]:=FAKSV.WV_R;
            sp[1]+=1;
            sp[9]+=w[9];
            sp[11]+=w[11];
            sp[12]+=w[12]
            ;~~}
            {SUBSTITUTE: 'LINIA0'}
         {OD}
         {IF: sp[1]>1}
            {
            w[9]:=sp[9];
            w[10]:='';
            w[11]:=sp[11];
            w[12]:=sp[12]
            ;~~}
            {SUBSTITUTE: 'razem'}
         {FI}
         {SUBSTITUTE: 'LINIAF'}
         {IF: walutowa}
            {FOR: FAKSV}
            {INDEX: 'FF_SV'}
            {PREFIX: '',faks}
            {DO}
               {FIRST}
                  {
                  sp[1]:=sp[9]:=sp[11]:=sp[12]:=0;
                  il_lin:=FAKSV.size()+{? walutowa=1 || 5 || 3 ?}
                  ;~~}
                  {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
                  {<{lmt+1}exec('tr','tlumaczenia','1000135')+' '+exec('tr','tlumaczenia','1000131')+' '+FAKSV.WAL().KOD}
                  {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
               {FIRST-}
               {
               vp:=0;
               licznik+=1;
               w[12]:=FAKSV.WW-FAKSV.WW_Z;
               _sv:=(#((FAKSV.SV().KOD*'%')+FAKSV.SV().KOD-1))*0.01;
               _wwn:=(FAKSV.WW/(1+_sv))$2;
               _wwn_z:=(FAKSV.WW_Z/(1+_sv))$2;
               w[9] :=_wwn-_wwn_z;
               w[10]:=FAKSV.SV().KOD;
               _wwv:=FAKSV.WW-_wwn;
               _wwv_z:=FAKSV.WW_Z-_wwn_z;
               w[11]:=(_wwv-_wwv_z)$2;
               sp[1]+=1;
               sp[9]+=w[9];
               sp[11]+=w[11];
               sp[12]+=w[12]
               ;~~}
               {SUBSTITUTE: 'LINIA0'}
            {OD}
            {IF: sp[1]>1}
               {
               w[9]:=sp[9];
               w[10]:='';
               w[11]:=sp[11];
               w[12]:=sp[12]
               ;~~}
               {SUBSTITUTE: 'razem'}
            {FI}
            {SUBSTITUTE: 'LINIAF'}
         {FI}
      {FI}
   {FI}
   {
   k[1]==tmp; VAR_DEL.delete('tmp')
   ;~~}
{COMMENT}   kwota do zaplaty wstepnie                                {COMMENT-}
   {
   {? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
   ||
      {? ('TO'*ndvat)
      || kw_fak:=s[4]
      || kw_fak:=s[6]
      ?}
   ||
      {? FAKS.WHERE='L'
      ||
         {? ~('TO'*ndvat)
         || kw_fak:=spz[3]
         || FAKSV.index('FF_SV');
            FAKSV.prefix('',faks);
            {? FAKSV.first
            || {!
               |? kw_fak+=FAKSV.WB_Z;
                  FAKSV.next
               !}
            ?}
         ?}
      |? exec('czy_end','faktury_zalicz')
      ||
         {? ~('TO'*ndvat)
         || kw_fak:=sp[12]
         || FAKSV.index('FF_SV');
            FAKSV.prefix('',faks);
            {? FAKSV.first
            || {!
               |? kw_fak+=FAKSV.WB_R;
                  FAKSV.next
               !}
            ?}
         ?}
      ||
         {? ('TO'*ndvat)
         || kw_fak:=sp[1]
         || kw_fak:=sp[3]
         ?}
      ?}
   ?};
   zal:=0;
   lmt:=lmt_org;
   ~~}
{COMMENT}   powiazane zaliczki                                       {COMMENT-}
   {IF: FAKS.WHERE='L'}
      {
      FAPOW.index('FAKS_K');
      FAPOW.prefix(faks,'N');
      {? FAPOW.first()
      ||
         "szuka powiazanych";
         {? var_pres('X_FAPOW')>=100 || obj_del(X_FAPOW) ?};
         _qq:="
            select distinct
               FAPOW.FAKS_SYM as FAKS_SYM,
               to_string(FAPOW.DW) as DW,
               FAPOW.KW as KW,
               FAPOW.WAL as WAL,
               FAPOW.FAKS as REF,
               'N' as CANCEL
            from
               FAPOW
            where
               FAPOW.F=':_a'
               and FAPOW.FAKS_SYM<>':_b'
               and FAPOW.KH=:_c
               and FAPOW.WAL=':_d'
               and FAPOW.KOREKTA='N'
               and FAPOW.AKC='T'
               and FAPOW.POZ<:_e
         ";
         X_FAPOW:=sql($_qq,FAPOW.F,FAPOW.FAKS_SYM,FAPOW.KH,FAPOW.WAL,FAPOW.POZ)
      ?}
      ;~~}
      {FOR: X_FAPOW}
      {DO}
         {FIRST}
            {<{lmt+1}}{FONT: exec('font','param_wydr','powzal_nag')}{exec('tr','tlumaczenia','1000136')+' '}
         {FIRST-}
         { {? exec('FindAndGet','#table',FAKS,X_FAPOW.REF,,"STAT_REJ",'N')='A'
           || X_FAPOW.CANCEL='T';
              X_FAPOW.put(1)
           ?};
         ~~}
         {<{lmt+5}
            }{FONT: exec('font','param_wydr','powzal_1')}{form(X_FAPOW.FAKS_SYM)
            }{FONT: exec('font','param_wydr','powzal_2')}{' '}{exec('tr','tlumaczenia','1000127')
            }{FONT: exec('font','param_wydr','powzal_3')}{' '}{form($X_FAPOW.DW)
            }{FONT: exec('font','param_wydr','powzal_4')}{', '}{exec('tr','tlumaczenia','1000137')
            }{FONT: exec('font','param_wydr','powzal_5')}{' '}{form(X_FAPOW.KW,,2,'.,')
            }{FONT: exec('font','param_wydr','powzal_6')}{' '}{X_FAPOW.WAL
             +{? X_FAPOW.CANCEL='T' || ' - '+exec('tr','tlumaczenia','1000215') || '' ?}}
      {OD}
      {<{lmt+1}
         }{FONT: exec('font','param_wydr','kwzal_tr')}{exec('tr','tlumaczenia','1000137')
         }{FONT: exec('font','param_wydr','kwzal_war')}{' '}{form(FAKS.KW_ZAL,,2,'.,')
         }{FONT: exec('font','param_wydr','kwzal_wal')}{' '}{__walh}
      { {? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || kw_fak:=FAKS.KW_ZAL ?};~~}
   {FI}
{COMMENT}   rozliczone zaliczki                                      {COMMENT-}
   { FAKSV.cntx_psh;~~}
   {FOR: FAPOW}
   {INDEX: 'END_K'}
   {PREFIX: faks,'N'}
   {DO}
      {FIRST}
         {<{lmt+1}}{FONT: exec('font','param_wydr','rozzal_nag')}{exec('tr','tlumaczenia','1000138')+' '}
      {FIRST-}
      {<{lmt+5}
         }{FONT: exec('font','param_wydr','rozzal_1')}{form(FAPOW.FAKS_SYM)
         }{FONT: exec('font','param_wydr','rozzal_2')}{' '}{exec('tr','tlumaczenia','1000127')
         }{FONT: exec('font','param_wydr','rozzal_3')}{' '}{form($FAPOW.DW)
         }{FONT: exec('font','param_wydr','rozzal_4')}{', '}{exec('tr','tlumaczenia','1000137')
         }{FONT: exec('font','param_wydr','rozzal_5')}{' '}{form(FAPOW.KW_ROZ,,2,'.,')
         }{FONT: exec('font','param_wydr','rozzal_6')}{' '}{FAPOW.WAL
          +{? exec('FindAndGet','#table',FAKS,FAPOW.FAKS,,"STAT_REJ",'N')='A' || ' - '+exec('tr','tlumaczenia','1000215') || '' ?}}
      {IF: __walh<>__walp & ~('TO'*ndvat)}
         { FAKSV.use('faksv'+(8+FAPOW.FAKS+3));~~}
         {FOR: FAKSV}
         {INDEX: 'FAPOW_SV'}
         {PREFIX: $FAPOW.ref}
         {DO}
            {FONT: exec('font','param_wydr','rozzal_narod')}
            {FIRST}
               { il_lin:=FAKSV.size()+3;~~}
               {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
               {<{lmt+7}exec('tr','tlumaczenia','1000132')+' '+INFO.NAROD().KOD+
                  ' '+exec('tr','tlumaczenia','1000158')+' '+form(FAKSV.KRS_ZAL,,4)}
               {lmt:=5; rsep:=vp:=0;~~}
               {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
            {FIRST-}
            {
            w[9]:=FAKSV.WN_Z;
            w[10]:=FAKSV.SV().KOD;
            w[11]:=FAKSV.WV_Z;
            w[12]:=FAKSV.WB_Z
            ;~~}
            {SUBSTITUTE: 'LINIA0'}
            {TOTAL}
               {SUBSTITUTE: 'LINIAF'}
         {OD}
      {FI}
      { {? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || kw_fak-=FAPOW.KW_ROZ ?};~~}
   {OD}
   { FAKSV.cntx_pop;~~}
{COMMENT}   do zaplaty                                               {COMMENT-}
   { lmt:=lmt_org;~~}
   {<{lmt+1}
      }{FONT: exec('font','param_wydr','dz_tr')}{exec('tr','tlumaczenia',{? FAKS.T().ZAL='T' || '1000214' || '1000139' ?})+' '
      }{FONT: exec('font','param_wydr','dz_war')}{form(kw_fak,,2,'.,')
      }{FONT: exec('font','param_wydr','dz_wal')}{' '+{? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || __walh || __walp ?}}
   {<1}{
   slownie:=exec('slownie','tlumaczenia',kw_fak,{? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || __walh || __walp ?});
   {? slownie=''
   || slownie:=exec('tr','tlumaczenia','1000141')+' '+
         {? {? PARAM_W.WALH='T' | PARAM_W.WALH='H' || __walh || __walp ?}='PLN'
         || STR.kwota_sł(kw_fak)
         || STR.słownie(kw_fak)+{? frac(kw_fak) ||' '+$(#(form(kw_fak,,2)+2))+'/100'||''?}+' '+__walh
         ?}
   ?};
   max_T8:=charleft;
   STR.split(slownie);
   char2cut:=+(exec('tr','tlumaczenia','1000141')+' ');
   ~~}
   {IF: PARAM_W.SLOWNIE='T'}
      {<{lmt+1}
         }{FONT: exec('font','param_wydr','dzsl_tr')}{exec('tr','tlumaczenia','1000141')+' '
         }{FONT: exec('font','param_wydr','dzsl_war')}{char2cut-STR.line(charleft())}
      {WHILE: +(Text:=STR.line(charleft()))}
      {DO}
         {<{lmt+1}}{Text}
      {OD}
   {FI}
   {
   FAKS.cntx_pop();
   faks:=faksxx;
   VAR_DEL.delete('faksxx');
   ~~}
{COMMENT}   informacje dodatkowe dla naglowka                        {COMMENT-}
   {FONT: exec('font','param_wydr','inf_dod')}
   {FOR: S_GD}
   {PREFIX: 'D'}
   {DO}
      {FIRST}
         {LINE}
      {FIRST-}
      {SUBSTITUTE: 'inf_dod'}
   {OD}
{COMMENT}   tekst dla faktury uproszczonej                           {COMMENT-}
   {FONT: exec('font','param_wydr','fakupr_txt')}
   {ENTIRE}
   {IF: (FAKS.T().UE='T' & FAKS.DO_UE='T') | ndvat='O' }
      {LINE}
      {<{lmt+1}}{
      {? FAKS.T().UE='T' & FAKS.DO_UE='T'
      || Text:=exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000142'))+' ';
         Text+=exec('tr','tlumaczenia','1000143')
      || Text:=exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000144'))
      ?};
      max_T8:=charleft;
      STR.split(Text)
      ;~~}
      {WHILE: +(Text:=STR.line(max_T8-4-2))}
      {DO}
         {IF: +Text>=(max_T8-4-8)}
            {<{lmt+1}}{JUSTIFY}{<{lmt+1}>{max_T8-5}Text}
         {ELSE}
            {<{lmt+1}Text}
         {FI}
      {OD}
   {FI}
{COMMENT}   uwagi                                                    {COMMENT-}
   {FONT: exec('font','param_wydr','uwagi')}
   { __memo:=FAKS.memo_get('r','UWAGI'); text:='';~~}
   {IF: __memo.is_open}
      {WHILE: text:=fread(__memo); text<>'\n'}
      {DO}
         {<{lmt+1}}{
         max_T8:=charleft-(lmt+8);
         STR.split(text);
         Text:='';
         sep:=0
         ;~~}
         {WHILE: STR.next()}
         {DO}
            {FIRST}{LINE}{FIRST-}
            { word:=STR.get_word(); sep+=1;~~}
            {IF: +Text+(+word)>=(max_T8-4-8)}
               {<{lmt+1}}{JUSTIFY}{<{lmt+1}>{max_T8-8}Text}
               { Text:=word; sep:=1;~~}
            {ELSE}
               { Text+={? sep>1 || ' ' || '' ?}+word;~~}
            {FI}
         {OD}
         {IF: +Text}
            {<{lmt+1}}{Text}
         {FI}
      {OD}
      {CENTER}
   {FI}
   { VAR_DEL.delete('__memo','text','Text');~~}
{COMMENT}   oswiadczenie RR                                          {COMMENT-}
{FONT: exec('font','param_wydr','oswiadczenie_rr')}
{IF: WYDRUKIL.WYDRUK='zak_rr'}
   {LINE}
   {<{lmt+1}'Oświadczam, że jestem rolnikiem ryczałtowym zwolnionym od podatku od towarów'}
   {<{lmt+1}'i usług na podstawie art. 43 ust. 1 pkt 3 ustawy o podatku od towarów i usług.'}
   {LINE}
   {<{lmt+1}'..................................................'}
{FI}
{COMMENT}   podpisujacy                                              {COMMENT-}
   {FONT: exec('font','param_wydr','podpisy')}
   {IF: FAKS.WHERE<>'Z' | FAKS.WHERE='Z' & (__wydrfa.samof | WYDRUKIL.WYDRUK='zak_rr' | WYDRUKIL.WYDRUK='kor_rr')}
      {
      _p100223:=exec('get','#params',100223,2);
      {? var_pres('STR1')>=100 || obj_del(STR1) ?};
      STR1:=obj_new(@.CLASS.STRING);
      Text:={? _p100223='O' | _p100223='N' || exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000145')) || '' ?};
      Text1:={? _p100223='O' | _p100223='S' || exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000146')) || '' ?};
      STR.split(Text);
      STR1.split(Text1)
      ;~~}
      {WHILE: _a:=+(Text:=STR.line(((max_T8-4)/2)$0-39)); _b:=+(Text1:=STR1.line(((max_T8-4)/2)$0-39)); _a | _b}
      {DO}
         {FIRST}
            {IF: lineleft()<8}{PAGE}{FI}{LINE: 8}
         {FIRST-}
         {IF: +Text | +Text1}
            {IF: +Text}{<{lmt+1}Text}{FI}{IF: +Text1}{<{((max_T8-4)/2)$0+20}Text1}{FI}
         {FI}
      {OD}
{COMMENT}   upowaznienie                                             {COMMENT-}
      {FONT: exec('font','param_wydr','upowaznienie')}
      {
      USERS.index('USR_KKOD');
      _p100223:=exec('get','#params',100223,2);
      _os:=form(exec('kh_osob_default','kontrahent',KH.ref));
      Text:={? _p100223='O' | _p100223='N' || {? +_os || _os || 20*'.' ?} || '' ?};
      Text1:={? _p100223='O' | _p100223='S' || {? USERS.find_key(FAKS.USER,FAKS.USER) & +USERS.DANE || USERS.DANE || 20*'.' ?} || '' ?};
      STR.split(Text);
      STR1.split(Text1)
      ;~~}
      {WHILE: _a:=+(Text:=STR.line(((max_T8-4)/2)$0-39)); _b:=+(Text1:=STR1.line(((max_T8-4)/2)$0-39)); _a | _b}
      {DO}
         {FIRST}
            {LINE}
         {FIRST-}
         {IF: +Text | +Text1}
            {IF: +Text}{<{lmt+1}Text}{FI}{IF: +Text1}{<{((max_T8-4)/2)$0+20}Text1}{FI}
         {FI}
      {OD}
   {FI}
   {ENTIRE-}
   {CENTER}
   { PAR_WYDR.HEAD:=head;~~}
   {HEADER}{HEADER-}
   {IF: lineleft()>0}{PAGE}{FI}
{OD}
{MACRO-}
:!UTF-8
{TITLE:Etykiety dla użytkowników terminala}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('Exit','BarAnyT','BarAny','lmt','ekiosk','__USERS');
   BarAny:=obj_new('USER','KOD');
   BarAny[1]:=''; BarAny[2]:='';
   {? var_pres('BarAnyT')<>2 || BarAnyT:='Wydruk kodów kreskowych dla tabeli'@ ?};
   USERS.cntx_psh(); OSOBA.cntx_psh();
   ekiosk:={? var_pres('__usmob')>0 & __usmob || 0 || 1 ?};
   __USERS:=sql('
      select
         USERS.DANE,
         USERS.KOD,
         USERS.IP,
         OSOBA.NAZWISKO,
         OSOBA.PIERWSZE,
         USERS.REFERENCE as REF,
         space(1) as CHECKED
      from USERS left join OSOBA using(OSOBA.REFERENCE,USERS.OSOBA)
      where USERS.AKT=\'T\' and :_a
      order by DANE',
      {? ekiosk || 'USERS.EKIOSK=\'T\'' || 'USERS.MOBIL=\'T\'' ?}
   );
   _wer:=__USERS.mk_sel('Wybór użytkowników do wydruku'@,,,'selectusersdruk',,,,,'U');
   __USERS.win_fld(_wer,,'KOD',,,,,,'Nazwa'@);
   __USERS.win_fld(_wer,,'DANE',,,,,,'Dane użytkownika'@);
   __USERS.win_act(_wer,,'Formuła','Wybierz'@@,,,
      "__USERS.CHECKED:='T'; __USERS.put(); {? __USERS.sel_size()=0 || sel_exit() || ~~ ?}",,1,1,
      "1","sel_exit()",'W'
   );
   __USERS.win_sel(_wer);
   __USERS.blank();
   __USERS.REF:=$USERS.ref();
   __USERS.find_rec();
   Exit:=~__USERS.select(,1,-1);
   ~~
}
{END:
   VAR_DEL.delete('Exit','BarAnyT','BarAny','lmt','ekiosk','__USERS');
   USERS.cntx_pop(); OSOBA.cntx_pop();
   ~~
}
{IF: Exit=0}
{
  PAR_WYDR.TITLE:=BarAnyT; ~~}
{INCLUDE: wsp_pw.rpi}
{FONT: 'T8G'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP;~~}
{ lmt:=10; ~~}
{FOR: __USERS}
{DO}
{IF: __USERS.CHECKED='T'}
{ENTIRE}
{FONT: 'T8G'}
{  BarAny[1]:='%1 %2 (%3)'[__USERS.NAZWISKO,__USERS.PIERWSZE,__USERS.KOD];
   BarAny[2]:=__USERS.IP;~~}
 {80*'─'}






{SPACE: 'A'}
{<{1}>{16}}{IF: BarAny[2]<>''}{BARCODE: BarAny[2];(+BarAny[2])*3;3;'Code 128'}{ELSE}Pusty identyfikator{FI}


{FONT: 'T8G'}{SPACE: 'B'}

{FONT: 'T8G'}{SPACE: 'B'}{<{1}>{16}}{BarAny[1]}

{ENTIRE-}
{FONT: 'T8G'}
{FI}
{OD}
{FI}
{FONT: 'T8G'}
{80*'─'}
:!UTF-8
{TITLE:A. Wykaz kart technologicznych}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('xTKTL','xTKTLW');
   exec('tktl_cntx_psh','tech_common');
   dalej:=0;
   PRN:=obj_new(@.CLASS.PRINT,9);
   PRN.s_frame();
   prn1:='PRN';
   wyr:='N';
   color:='';
   tryb:=rep_export();
   lm:=ll:=vp:=lmt:=rsep:=0;
   "0 - przerwanie wydruku, 1 - wszystkie karty, 2 - tylko zatwierdzone";
   "3 - tylko archiwalne";
   query:=0
}
{END:
   VAR_DEL.delete('xTKTL','xTKTLW');
   exec('tktl_cntx_pop','tech_common');
   &dalej;
   obj_del(PRN);
   &prn1;
   &wyr;
   &color;
   &lm; &ll; &vp; &lmt; &rsep; &tryb;
   &query
}
{
_wydr_naz:='tech0001';
WYDRUKIL.index('WYDRUKI');
WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
{? ~WYDRUKIL.first()
|| WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add();
   WYDRUK.blank()
|| WYDRUK.OPCJA:=WYDRUKIL.SORTUJ;
   WYDRUK.KTM:=WYDRUKIL.SR
?};
WYDRUK.win_edit('RED8');
~~}
{IF: WYDRUK.edit()}
{ {? WYDRUK.OPCJA='W' || query:=1
  |? WYDRUK.OPCJA='Z' || query:=2
  || query:=3
  ?};
  wyr:=WYDRUK.KTM;
  WYDRUKIL.SORTUJ:=WYDRUK.OPCJA;
  WYDRUKIL.SR:=WYDRUK.KTM;
  WYDRUKIL.put(1);
~~}
{ELSE}
{EXIT}
{FI}
{COMMENT}
  Wykaz kart technologicznych.
  [SS] - 2000/07/21 - [7.22]
  [SS] - 2001/04/03 - [7.41] - dodanie kolumny Wersja
  [TS] - 2001/10/09 - [7.53] OWwSK003_Aneks1
 Wywołanie: Karty technologiczne -> Raporty -> Zestawienia
{COMMENT-}
{MACRO: 'miscelaneous'}
  {COMMENT}----Query----------------------------------------{COMMENT-}
{MACRO-}
{SUBSTITUTE: 'miscelaneous'}
{COMMENT}--Definicja wiersza wydruku naglowka karty technologicznej TKTL.--{COMMENT-}
{IF: wyr='N'}
{ PRN.add("TKTL.NRK",20,'Numer'@);
  PRN.add("TKTL.WER",6,'Wersja'@);
  PRN.add("TKTL.TYP().TYP",3,'Typ'@);
  PRN.add("TKTL.OPIS",40,'Opis'@);
  PRN.add("TKTL.KTM().KTM",20,'Podstawowy produkt'@);
  PRN.add("{? TKTL.STAN='T' || 'Tak'@ |? TKTL.STAN='N' || 'Nie'@ || 'Częściowo'@ ?}",12,'Zatwierdzona'@);
  PRN.add("{? TKTL.ARCH='T' || 'Tak'@ || 'Nie'@ ?}",10,'Archiwalna'@);
  PRN.add("{? TKTL.DEFAULT='T' || 'Tak'@ |? TKTL.DEFAULT='N' || 'Nie'@ || 'Dla części produktów'@ ?}",10,'Domyślna'@);
  PRN.add("_year:=TKTL.name()+2; {? _year='__' || 'bieżące'@ || '20'+_year ?}",8,'Archiwum'@);
  PAR_WYDR.TITLE:='WYKAZ KART TECHNOLOGICZNYCH'@; ~~}
{ELSE}
{ PRN.add("{? pierwszy | lineleft()=0 || ktm || '' ?}",20,'Produkt'@);
  PRN.add("TKTLW.TKTL().NRK",20,'Numer'@);
  PRN.add("TKTL.WER",6,'Wersja'@);
  PRN.add("TKTL.TYP().TYP",3,'Typ'@);
  PRN.add("TKTL.OPIS",40,'Opis'@);
  PRN.add("{? TKTL.STAN='T' || 'Tak'@ |? TKTL.STAN='N' || 'Nie'@ || 'Częściowo'@ ?}",12,'Zatwierdzona'@);
  PRN.add("{? TKTL.ARCH='T' || 'Tak'@ || 'Nie'@ ?}",10,'Archiwalna'@);
  PRN.add("{? TKTLW.DEFAULT='T' || 'Tak'@ || 'Nie'@ ?}",8,'Domyślna'@);
  PRN.add("_year:=TKTL.name()+2; {? _year='__' || 'bieżące'@ || '20'+_year ?}",8,'Archiwum'@);
  PAR_WYDR.TITLE:='WYKAZ KART TECHNOLOGICZNYCH'@; ~~}
{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{IF: query=1}
  {<{lmt+1} form('Zakres:'@,10) + 'Wszystkie karty technologiczne'@}
{ELIF: query=2}
  {<{lmt+1} form('Zakres:'@,10) + 'Zatwierdzone karty technologiczne'@}
{ELIF: query=3}
  {<{lmt+1} form('Zakres:'@,10) + 'Archiwalne karty technologiczne'@}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~tryb}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{rsep:=PAR_WYDR.RSEP; TKTL.clear(); ~~}
{IF: wyr='N'}
{  {? query=1 || _where:='where TKTL.TORW=\'T\''
   |? query=2 || _where:='where TKTL.TORW=\'T\' and TKTL.STAN=\'T\''
   |? query=3 || _where:='where TKTL.TORW=\'T\' and TKTL.ARCH=\'T\''
              || _where:=''
   ?};
   xTKTL:=sql('
      select
         TKTL.REFERENCE as REF,
         TKTL.NRK,
         TKTL.WER
      from @TKTL
      :_a
      order by TKTL.NRK, TKTL.WER
   ',_where);
   ~~
}
{IF: xTKTL.first()}{dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
   {exec('tktl_use','tech_common',(8+xTKTL.REF)+3); TKTL.clear(); TKTL.seek(xTKTL.REF); ~~}
   {SUBSTITUTE: 'LINIA0'}
   { dalej:=xTKTL.next(); ~~}
{OD}
{ELSE}
{  {? query=1 || _where:='where TKTL.TORW=\'T\''
   |? query=2 || _where:='where TKTL.TORW=\'T\' and TKTL.STAN=\'T\''
   |? query=3 || _where:='where TKTL.TORW=\'T\' and TKTL.ARCH=\'T\''
              || _where:=''
   ?};
   xTKTLW:=sql('
      select
         TKTLW.REFERENCE as REF,
         TKTL.NRK,
         TKTL.WER,
         M.KTM
      from
         @TKTLW
         join @TKTL
         join M using(TKTLW.KTM,M.REFERENCE)
      :_a
      order by M.KTM, TKTL.NRK, TKTL.WER
   ',_where);
   ~~
}
{IF: xTKTLW.first()}
{dalej:=1; pierwszy:=1; ktm:=exec('FindAndGet','#table',TKTLW,xTKTLW.REF,,"TKTLW.KTM().KTM",''); ~~}
{FI}
{WHILE: dalej}
{DO}
   {exec('tktl_use','tech_common',(8+xTKTLW.REF)+3); TKTLW.clear(); TKTLW.seek(xTKTLW.REF); ~~}
   {IF: ktm<>TKTLW.KTM().KTM}
   {pierwszy:=1; ktm:=TKTLW.KTM().KTM; rsep:=1; ~~}
   {FI}
   {IF: pierwszy}
   {SUBSTITUTE: 'LINIA0'}
   {rsep:=0; pierwszy:=0; ~~}
   {ELSE}
   {SUBSTITUTE: 'LINIA0'}
   {FI}
   {dalej:=xTKTLW.next(); ~~}
{OD}
{FI}

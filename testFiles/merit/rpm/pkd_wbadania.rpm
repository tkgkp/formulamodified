:!UTF-8
{TITLE:Badania okresowe}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   tresc:='wbadania';
   BDO.cntx_psh();
   SLO_TYP.cntx_psh();
   SLO_KOD.cntx_psh();
   P.cntx_psh();
   P.clear();
   par:=obj_new(12);
   par[11]:=0;
   _upr:=exec('uprawnieniawrap','pkd','badaniach lekarskich'@,'PKD_EZK_OPBL','PKD_EZK_ORBL');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || BDO.index('PDTO');
      SLO_TYP.index('SYMBOL');
      SLO_TYP.prefix('BADLEKT',);
      {? SLO_TYP.first()
      || SLO_KOD.index('KOD');
         SLO_KOD.prefix(SLO_TYP.ref());
         {? SLO_KOD.first()
         || par[2]:=~~;
            par[3]:='';
            par[4]:=0;
            par[7]:=exec('pkd_wbadania','!pkd_zes_orka');
            PAR_WYDR.TITLE:={? par[7].RODS=2 || 'Badania okresowe'@ || 'Sprawdzenie badań okresowych'@ ?};
            {? par[7].size()
            || par[1]:=obj_new(@.CLASS.PRINT,11+(par[7].RODS=2));
               {? ~rep_export() || par[1].add("{? par[12] || '' || par[4]+=1 ?}",5,'Lp.'@,1,,,,,',,\'9.\'') ?};
               par[1].add("{? par[12] || '' || P.T ?}",9,'Nr teczki'@,1);
               par[1].add("{? par[12] || '' || P.OSOBA().NAZWISKO ?}",30,'Nazwisko'@);
               par[1].add("{? par[12] || '' || OSOBA.PIERWSZE ?}",20,'Imię'@);
               par[1].add("{? par[12] || '' || par[12]:=1; P.ST().ST ?}",30,'Stanowisko'@);
               {? par[7].RODS=2 || par[1].add("par[2].KT",7,'Rodzaj'@) ?};
               par[1].add("par[2].OD$1",10,'Data badań'@);
               par[1].add("par[2].DO$1",10,'Ważne do'@);
               par[1].add("par[2].OPIS",20,'Badanie'@);
               par[1].add("par[2].NRORZ",20,'Nr orzeczenia'@);
               par[1].add("par[2].WKOD",10,'Kod wyniku'@);
               {? rep_export() || par[1].add("par[2].WLINIA",42,'Opis wyniku'@)?};
               par[1].s_frame();

               par[2]:=tab_tmp(1,
                  'OPIS','STRING[20]',,
                  'NRORZ','STRING[20]',,
                  'WKOD','STRING[9]',,
                  'WLINIA','STRING[255]',,
                  'OD','DATE',,
                  'DO','DATE',,
                  'RODZ','STRING[5]',,
                  'KT','STRING[8]',
               );
               par[2].index(par[5]:=par[2].ndx_tmp(,,'RODZ',,,'RODZ',,,'OD',,));
               par[3]:=tab_tmp(1,
                  'OPIS','STRING[20]',,
                  'NRORZ','STRING[20]',,
                  'WKOD','STRING[9]',,
                  'WLINIA','STRING[255]',,
                  'RODZ','STRING[5]',
               );
               par[3].index(par[3].ndx_tmp(,,'RODZ',,,'RODZ',,));
               par[8]:=tab_tmp(1,
                  'DO','DATE',,
                  'OD','DATE',
               );

               prn1:='par[1]';
               lmt:=rsep:=0;
               vp:=1;
               par[11]:=1;
               params_set('P',exec('wybierz_parses','pracownik','PKD').P)
            ?}
         ?}
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   P.cntx_pop();
   SLO_KOD.cntx_pop();
   SLO_TYP.cntx_pop();
   BDO.cntx_pop()
}
{COMMENT}OLD: wbadania.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[11]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[11]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[11]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {IF: par[7].RODS=2}
      {<{lmt+1}'Zakres dat: od %1 do %2'@[par[7].OD$1,par[7].DO$1]}
   {ELSE}
      {<{lmt+1}'Data kontroli'@}: {par[7].DATA$1}
   {FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: {? par[7].RODS=1 || P.DZA<=par[7].DATA & (P.DZ=#0 | par[7].DATA<=P.DZ | par[7].ZA='T')
     |? par[7].RODS=2 || P.DZA<=par[7].DO & (P.DZ=#0 | (par[7].OD<=P.DZ) | par[7].ZA='T')
     ?}
}
{  par[12]:=0;
   par[2].clear();
   par[2].erase();
   par[2].OD:=par[2].DO:=date(0,0,0);
   par[2].NRORZ:=par[2].WKOD:=par[2].WLINIA:='BRAK';
   {? par[7].RODS=2
   || {? P.f_active() || P.f_clear() ?};
      P.f_set(,,'P.REFERENCE=:_a',P.ref());
      _tab:=exec('bdo_ending','personel',par[7].OD,par[7].DO,,par[7].ZA);
      {? _tab.first()
      || par[2].blank();
         par[2].OD:=_tab.DTO;
         par[2].DO:=_tab.DTNB;
         par[2].OPIS:=_tab.TYPO;
         par[2].NRORZ:=_tab.NRO;
         par[2].WKOD:=_tab.WYNK;
         par[2].WLINIA:=_tab.WYNL;
         par[2].KT:=_tab.TYPK;
         par[2].add()
      ?};
      P.f_clear()
   || BDO.prefix(P.ref());
      par[3].erase();
      par[3].blank();
      {? BDO.first()
      ||{!
         |? {? '|WSTĘPNE|OKRES|KONTROL|'*('|%1|'[BDO.TYP().KOD])
            || _opis:=BDO.TYP().NAZWA;
               par[2].OPIS:=_opis;
               par[2].NRORZ:=BDO.NRO;
               par[2].WKOD:=BDO.WYNIK().KOD;
               par[2].WLINIA:=BDO.WYNIK().LINIA;
               par[2].OD:=BDO.DTO;
               par[2].DO:={? BDO.DTUZ=#0 || BDO.DTNB || BDO.DTUZ ?};
               par[2].RODZ:='WOK';
               par[2].KT:=BDO.TYP().KOD;
               {? par[2].add() & ~par[3].find_key('WOK','WOK')
               || par[3].RODZ:='WOK';
                  par[3].OPIS:=_opis;
                  par[3].NRORZ:=BDO.NRO;
                  par[3].WKOD:=BDO.WYNIK().KOD;
                  par[3].WLINIA:=BDO.WYNIK().LINIA;
                  par[3].add()
               ?}
            ?};
            BDO.next()
         !};
         {? par[3].first()
         || {!
            |? par[8].erase();
               _opis:=par[3].OPIS;
               _nrorz:=par[3].NRORZ;
               _wkod:= par[3].WKOD;
               _wlinia:= par[3].WLINIA;
               par[2].prefix('WOK','WOK');
               {? par[2].last()
               || {? par[2].OD<=par[7].DATA & par[2].DO>=par[7].DATA
                  || par[2].erase()
                  |? par[2].DO<par[7].DATA
                  || _od:=par[2].OD;
                     _do:=par[2].DO;
                     _opis:=par[2].OPIS;
                     _nrorz:=par[2].NRORZ;
                     _wkod:= par[2].WKOD;
                     _wlinia:= par[2].WLINIA;
                     par[2].erase();
                     par[2].OD:=_od;
                     par[2].DO:=_do;
                     par[2].OPIS:=_opis;
                     par[2].NRORZ:=_nrorz;
                     par[2].WKOD:=_wkod;
                     par[2].WLINIA:=_wlinia;
                     par[2].add()
                  |? par[2].OD>par[7].DATA
                  || {!
                     |? {? par[2].OD>par[7].DATA
                        || par[2].del()
                        |? par[2].OD<=par[7].DATA & par[2].DO>=par[7].DATA
                        || par[2].erase();
                           0
                        || _od:=par[2].OD;
                           _do:=par[2].DO;
                           _opis:=par[2].OPIS;
                           _nrorz:=par[2].NRORZ;
                           _wkod:=par[2].WKOD;
                           _wlinia:=par[2].WLINIA;
                           par[2].erase();
                           par[2].OD:=_od;
                           par[2].DO:=_do;
                           par[2].OPIS:=_opis;
                           par[2].NRORZ:=_nrorz;
                           par[2].WKOD:=_wkod;
                           par[2].WLINIA:=_wlinia;
                           par[2].add();
                           0
                        ?}
                     !}
                  ?}
               ?};
               par[3].next()
            !}
         || par[2].cntx_psh();
            par[2].clear();
            par[2].OPIS:='BRAK BADAŃ';
            par[2].add();
            par[2].cntx_pop()
         ?}
      || par[2].cntx_psh();
         par[2].clear();
         par[2].OPIS:='BRAK BADAŃ';
         par[2].add();
         par[2].cntx_pop()
      ?}
   ?}; ~~
}
{ENTIRE}
{FOR: par[2]}
{INDEX: par[5]}
{DO}
   {par[1].ini_line()}
   {WHILE: par[1].next()}{DO}
      {REP: par[1].fline(lmt)}
   {OD}
{OD}
{ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
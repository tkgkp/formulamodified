:!UTF-8
{TITLE:Podział kosztów}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','__param','__edit');
   tresc:='ppodzkos';
   PAR_WYDR.TITLE:='Podział kosztów'@;
   par:=obj_new(11);
   par[4]:=1;
   par[6]:=0;
   _upr:=exec('uprawnieniawrap','pkd',
      'Podział kosztów'@,
      'PPL_PLL_PPKO',
      'PPL_PLL_RPKO',
      'PPL_PLL_RPPK',
      'PPL_GRP_APKO'
   );
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'OD','DATE','Data od'@,
         'DO','DATE','Data do'@
      );
      __param.OD:=date(,1,1);
      __param.DO:=date(,12,0);
      __param.add();
      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_ppodzkos');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'OD',,,11,,,,,'Data od'@);
      __param.win_efld(__edit,,'DO',,,11,,,,,'Data do'@);
      __param.efld_opt(__edit,'mark=1',,'OD');
      __param.efld_opt(__edit,'mark=1',,'DO');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[6]:=__param.edit(
            " {? (_chk:=__CHK.record(__param,,'OD','DO'))<>''
              || return(_chk)
              |? __param.DO<__param.OD
              || FUN.emsg(
                    '\"%1\" nie może być wcześniejsza niż \"%2\".'@
                    [MS.name(__param,'DO'),MS.name(__param,'OD')]
                 );
                 return('OD')
              || 1
              ?}
            "
         )
      || par[3]:=__param.OD; par[4]:=__param.DO;
         par[1]:=obj_new(@.CLASS.PRINT,8);
         {? rep_export()
         ||  par[1].add(
                " {? P.seek(par[2].PRAC,,1)
                  || P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+'(Nr teczki %1)'@[|P.T]
                  || ''
                  ?}
                "
                ,40,'Pracownik'@
             )
         ?};
         par[1].add("par[2].SKLADNIK",20,'Składnik'@);
         par[1].add("par[2].FORM",40,'Procent/Formuła'@);
         par[1].add("par[2].KONTO",20,'Konto kosztów'@);
         par[1].add("par[2].RODZAJ",10,'Rodzaj'@);
         par[1].add("par[2].ROK",5,'Rok'@,1,,,,,',,\'9.\'');
         par[1].add("par[2].MC",7,'Miesiąc'@,1,,,,,',,\'9.\'');
         par[1].s_frame();

         params_set('P',exec('wybierz_parses','pracownik','PPL').P);

         par[11]:=tab_tmp(,'REF','INTEGER','Numer');
         b_rat:=h_rat:=par[9]:=1;
         Lp:=0
      ?};
      prn1:='par[1]';
      lmt:=rsep:=0;
      vp:=1
   ?};
   P.cntx_psh()
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','__param','__edit')
}
{COMMENT}OLD: ppodzkos.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~par[6]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[9]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[9]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: '@}{par[3]} - {par[4]}
   {<{ceil(lmt*h_rat)+1}'Stały podział kosztów: '@
      }{{? PAR_SKID.get(395)='L' || 'wg typu listy płac'@ || 'domyślny'@ ?}}.
   {<{ceil(lmt*h_rat)+1}'Miesięczny podział kosztów: '@
      }{{? PAR_SKID.get(396)='L' || 'wg listy płac'@ || 'domyślny'@ ?}}.
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'opis'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: par[7]<>''}
   {IF: ~rep_export()}
      {<{ceil(lmt*b_rat)+1}}{Lp+=1; form(Lp,,,'9.')} {par[7]}
   {FI}
{FONT: 'T8'}
{SPACE: 'B'}
   {REP: par[1].fhbar(lmt)}
   {  par[7]:='';
      par[10]:=1; ~~
   }
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{MACRO-}
{MACRO: tresc}
{FONT: 'T8'}
{SPACE: 'B'}
{par[7]:=' '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' ('+form(P.T)+')'; ~~ }
{FOR: par[2]}{PREFIX: $P.ref()}{DO}
   {IF: lineleft() & lineleft()<=4}
      {IF: par[10]}
         {REP: par[1].flbar(lmt)}
         {  par[10]:=0;
            Lp-=1;
            par[7]:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' ('+form(P.T)+')'; ~~
         }
      {FI}
      {IF: lineleft()}
         {PAGE}
      {FI}
   {FI}
   {SUBSTITUTE: 'opis'}
   {  par[1].ini_line(); ~~ }
   {REP: par[1].fline(lmt)}
   {WHILE: par[1].next()}
   {DO}
      {REP: par[1].fline(lmt)}
   {OD}
{OD}
{IF: par[7]=''}
   {REP: par[1].flbar(lmt)}
   {  par[10]:=0; ~~ }
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{  lmt:=int((charleft()-par[1].width())/2);
   _czySL:=PAR_SKID.get(395)='L';
   _czyML:=PAR_SKID.get(396)='L';
   _maski:='';
   {! _i:=par[3]~1 .. par[4]~1
   |! _maski+='\'godz'+$_i+'\','
   !};

   _sql:='
      select /* MASK_FILTER(G, '+_maski+'\'godzstal\') */
         G.P PRAC,
         KK.SYM KONTO,
         R.RT SKLADNIK,
         CASE WHEN G.G<>0 THEN to_string(G.G) ELSE G.F END FORM,
         CASE WHEN G.LT=\'< ---- >\' or CHAR_LENGTH(G.LT)=3 THEN \'Stały\' ELSE \'Miesięczny\' END RODZAJ,
         CASE WHEN G.LT=\'< ---- >\' or CHAR_LENGTH(G.LT)=3 THEN \'\' ELSE substr(G.REFERENCE,5,4) END ROK,
         CASE WHEN G.LT=\'< ---- >\' or CHAR_LENGTH(G.LT)=3 THEN \'\' ELSE CAST(G.M AS STRING_TYPE) END MC
      from @G join KK join R
      where G.R=\'K\' and
         ('+{? _czySL || 'CHAR_LENGTH(G.LT)=3' || 'G.LT=\'< ---- >\'' ?}+'
         or
         '+{? _czyML || 'CHAR_LENGTH(G.LT)=8 and G.LT like (\'L%\')' || 'G.LT=\'\'' ?}+')';

   _sql+=' and ((G.D>=to_date(:_a) and G.D<=to_date(:_b)) or G.D IS NULL) and G.FIRMA=:_c';
   _sql+=' order by PRAC, RODZAJ, ROK, MC, SKLADNIK, KONTO';
   par[2]:=sql(_sql,par[3],par[4],exec('ref_firma','ustawienia'));
   ~~
}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {IF: ~par[11].find_key(#P.ref())}
          {SUBSTITUTE: tresc}
          { par[11].REF:=#P.ref();
            par[11].add(); ~~
          }
      {FI}
   {FI}
{OD}
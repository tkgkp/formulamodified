:!UTF-8
{TITLE:Zatrudnienie na dzień}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','lmt','tresc','maxlines','dod','pod','rsep','vp','fzatr');
   VAR_DEL.delete('lmm','dpod','przem','licznik','zchead');
   tresc:='wzatrdz';
   PAR_WYDR.TITLE:='Zatrudnienie na dzień'@;
   P.cntx_psh();
   H.cntx_psh();
   ZC.cntx_psh();
   RU.cntx_psh();
   SLO_TYP.cntx_psh();
   fzatr:=__F_ZATR.P;
   par:=obj_new(18);
   par[17]:=0;
   {? fzatr<>'P' & fzatr<>'Z'
   || FUN.emsg('Wydruk dostępny dla formy współpracy: \"Pracownicy\" lub \"Zleceniobiorcy\".'@)
   || par[18]:=obj_new('P','Z');
      _upr:=exec('uprawnieniawrap','pkd','przebiegu zatrudnienia'@,'PKD_EZK_PPZA','PKD_EZK_RPZA');
      {? +_upr
      || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
      || {? fzatr<>'P' & fzatr<>'Z'
         || _args:=__PARSES.args('F_ZATR');
            {? __F_ZATR.upr('P')
            || _args.KOD:='P'
            |? __F_ZATR.upr('Z')
            || _args.KOD:='Z'
            ?};
            __PARSES.setVal('F_ZATR',_args)
         ?};

         par[5]:=par[7]:=1;
         par[4]:=obj_new(3);
         par[4][1]:=par[4][2]:=par[4][3]:=0;
         par[10]:=par[11]:=par[15]:=0;
         par[14]:=obj_new(2);
         par[14][1]:=par[14][2]:='';
         par[2]:=obj_new('tab','wer');
         _add:=
            "  {? _>0
               || {! _ii:=1.._
                  |! par[2].tab.LP:=_ii;
                     par[2].tab.INF:=_[_ii];
                     par[2].tab.ZN:=0;
                     par[2].tab.add()
                  !}
               ?}
            ";
         par[2].tab:=tab_tmp(1,
            'LP','INTEGER','Kolejność',
            'INF','STRING[25]','Drukowane informacje',
            'ZN','INTEGER','Zaznaczenie'
         );
         _add('Data zatrudnienia'@,
              'Data zwolnienia'@,
              'Czas określony'@,
              'Rodzaj umowy'@,
              'Stanowisko'@,
              'Wymiar etatu'@,
              'Jednostka organizacyjna'@
         );
         _Opcje:=exec('get','#profile',,tresc,'Drukowane informacje');
         {? type_of(_Opcje)=type_of('') & par[2].tab.first()
         || {!
            |? par[2].tab.ZN:=#(par[2].tab.LP+_Opcje+1);
               par[2].tab.put();
               par[2].tab.next()
            !}
         ?};
         par[2].wer:=par[2].tab.mk_sel('Drukowane informacje'@,,0,'pkd_wzatrdzinf',,,10);
         {? ~rep_export() || par[2].tab.win_fld(par[2].wer,,'LP',,,3,,1,'Lp.'@) ?};
         par[2].tab.win_fld(par[2].wer,,'INF',,,25,,1,'Drukowane informacje'@);
         par[2].tab.win_fld(par[2].wer,,'ZN',,,,,0,'Wybór'@,,,2,,"1","0");
         par[2].tab.win_act(par[2].wer,0,'Rekord',,,,"par[2].tab.ZN");
         par[2].tab.win_act(par[2].wer,0,'Formuła','Wybierz'@,,,"sel_exit()",,1,0,,,,,'target=window');
         par[2].tab.win_act(par[2].wer,0,'Formuła','Zmień'@,,,"par[2].tab.ZN:=~par[2].tab.ZN; par[2].tab.put()",,,1);
         par[2].tab.win_btn(par[2].wer,'text=%1,panel=right,align=begin'['Zmień'@],'menu:Z');
         par[2].tab.win_btn(par[2].wer,'text=%1,panel=bottom,align=end'['OK'@],'menu:W');
         par[2].tab.win_btn(par[2].wer,'text=%1,panel=bottom,align=end'['Anuluj'@],'key:Esc');
         par[2].tab.win_sel(par[2].wer);

         par[3]:=obj_new('tab','red','chk');
         par[3].chk:="{? par[3].tab.DATA=#0 || FUN.emsg('Nieprawidłowa data'@); 0 || 1 ?}";
         par[3].tab:=tab_tmp(1,'DATA','DATE','Data kontroli','INNY','INTEGER','Inni współpracownicy');
         par[3].tab.DATA:=date();
         par[3].tab.INNY:=0;
         par[3].red:=par[3].tab.mk_edit(PAR_WYDR.TITLE,0,'pkd_wzatrnadz');
         par[3].tab.win_esep(par[3].red,'Parametry wydruku'@);
         par[3].tab.win_efld(par[3].red,,'DATA',,,11,,,'Data kontroli'@);
         par[3].tab.win_efld(
            par[3].red,,
            'INNY',,,,,,
            {? __F_ZATR.P='P' || 'Zleceniobiorcy'@ || 'Pracownicy'@ ?},,,
            'radio-buttons',,
            'Uwzględniać na wydruku'@,"1",
            'Nie uwzględniać na wydruku'@,"0"
         );
         par[3].tab.efld_opt(par[3].red,'enable='+$(__F_ZATR.upr('P') & __F_ZATR.upr('Z')),,'INNY');
         exec('ok_esc','#window',par[3].tab,par[3].red);
         par[3].tab.win_edit(par[3].red);
         {? par[17]:=(par[3].tab.edit("par[3].chk()") & par[2].tab.select())
         || _Opcje:='';
            _ncol:=3;
            {? par[2].tab.first() || {! |? _ncol+=(_t:=(par[2].tab.ZN<>0)); _Opcje+=$_t; par[2].tab.next() !} ?};
            exec('set','#profile',,tresc,'Drukowane informacje',_Opcje);

            {? __F_ZATR.P='P' | (__F_ZATR.P='Z' & par[3].tab.INNY)
            || przem:="_suma:=0;
                       {? par[2].tab.first() || {! |? _suma+=par[2].tab.ZN; par[2].tab.next()!} ?};
                       {? _suma=_a || _b || 0 ?}";
               _find:="_ok:=0; {? par[2].tab.find_key(_a) || _ok:=par[2].tab.ZN ?}; _ok";
               par[1]:=obj_new(@.CLASS.PRINT,_ncol);
               {? ~rep_export()
               || par[1].add("par[4][1]+=1",5,'Lp.'@,1,,,,,',,\'9.\'')
               ?};
               par[1].add("P.OSOBA().NAZWISKO",20+przem(0,12)+przem(1,12)+przem(2,12),'Nazwisko'@);
               par[1].add("OSOBA.PIERWSZE+' '+OSOBA.DRUGIE",20+przem(0,12)+przem(1,12)+przem(2,12),'Imię'@);
               {? _find(1) || par[1].add("{? P.DZA=#0 || '' || P.DZA$1 ?}",12,'Data zatrudnienia'@) ?};
               {? _find(2) || par[1].add("{? P.DZ=#0 || '' || P.DZ$1 ?}",10,'Data zwolnienia'@) ?};
               {? _find(3) || par[1].add("{? P.CO=#0 || '' || P.CO$1 ?}",10,'Czas określony'@) ?};
               {? _find(4) || par[1].add("H.RU().K",1,'R') ?};
               {? _find(5) || par[1].add("H.ST().ST",20,'Stanowisko'@) ?};
               {? _find(6) || par[1].add("H.RWY",6,'Wymiar'@,1,,,,,'6,3') ?};
               {? _find(7) || par[1].add("H.WYDZIAL().SYMBOL",16,'Jednostka organizacyjna'@) ?};
               par[1].s_frame()
            ?};

            RU.index('K');
            SLO_TYP.index('SYMBOL');
            SLO_TYP.prefix();
            pod:=
               "  {? _b=1 || obj_del(par[13]) ?};
                  {? SLO_TYP.find_key(_a,_a)
                  || RU.prefix(SLO_TYP.ref());
                     _ii:=0;
                     par[13]:=obj_new(RU.size()+1);
                     RU.first();
                     {!
                     |? _ii+=1;
                        par[13][_ii]:=obj_new(3);
                        par[13][_ii][1]:=RU.K;
                        par[13][_ii][2]:=RU.O;
                        par[13][_ii][3]:=0;
                        RU.next()
                     !};
                     par[13][obj_len(par[13])]:=obj_new(3);
                     par[13][obj_len(par[13])][1]:='';
                     par[13][obj_len(par[13])][2]:={? _a='UMPRAC' || 'PRACOWNICY ETATOWI'@
                                                   |? _a='UMZLEC' || 'UMOWY'@
                                                   ?};
                     par[13][obj_len(par[13])][3]:=0
                  ?}
               ";

            pod('UMPRAC',0);
            {? __F_ZATR.P='Z' | (__F_ZATR.P='P' & par[3].tab.INNY)
            || par[8]:=obj_new(@.CLASS.PRINT,7);
               {? ~rep_export() || par[8].add("par[4][1]",5,'Lp.'@) ?};
               par[8].add("par[14][1]",30,'Nazwisko'@);
               par[8].add("par[14][2]",20,'Imię'@);
               par[8].add("$ZC.DU",10,'Zlecenia od'@,1);
               par[8].add("$ZC.DW",10,'Zlecenia do'@,1);
               par[8].add("ZC.RU().K",1,'R',1);
               par[8].add("P.WYDZIAL().SYMBOL",16,'Jednostka organizacyjna'@);
               par[8].s_frame()
            ?};

            prn1:='par[1]';
            lmt:=rsep:=maxlines:=lmm:=dod:=0;
            vp:=1;
            dpod:=licznik:=zchead:=1;
            P.index('PRACOSOB');
            H.index('_HISTKOD');
            ZC.index('ZLECDAT');
            par[16]:=tab_tmp(1,'REF','INTEGER','OSOBA.ref()');
            params_set('P',exec('wybierz_parses','pracownik','PKD').P)
         ?}
      ?}
   ?}
}
{END:
   RU.cntx_pop();
   SLO_TYP.cntx_pop();
   ZC.cntx_pop();
   P.cntx_pop();
   H.cntx_pop();
   {? fzatr<>__F_ZATR.P || _args:=__PARSES.args('F_ZATR'); _args.KOD:=fzatr; __PARSES.setVal('F_ZATR',_args) ?};
   VAR_DEL.delete('par','prn1','lmt','tresc','maxlines','dod','pod','rsep','vp','fzatr');
   VAR_DEL.delete('lmm','dpod','przem','licznik','zchead')
}
{COMMENT}OLD: wzatrdz.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[17]}
{EXIT: {? params_get().P.first
       || {? __F_ZATR.P='P' || par[18].P:=params_get().P || par[18].Z:=params_get().P ?}; 0
       || 1
       ?}
}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Data kontroli'@}: {par[3].tab.DATA$1}
   {IF: ~rep_export()}
   {SUBSTITUTE: 'opis'}
   {<{lmt+1}'Opis'@}{': '}{ lmm:=charleft(); ~~ }{ STR.split(par[13][obj_len(par[13])][1]-2);STR.line(lmm)}
   {WHILE: STR.next()}{DO}{<{lmt+1}}{STR.line(lmm)}{OD}
   {FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'opis'}
{ par[11]:=0; ~~ }
{WHILE: (par[11]+=1)<=obj_len(par[13])-1}{DO}
{ par[13][obj_len(par[13])][1]+=par[13][par[11]][2]+' - '+par[13][par[11]][1]+'; '; ~~ }
{OD}
{MACRO-}
{MACRO: tresc}
{IF: ~par[7]}
   { par[6]:=0;
    P.cntx_psh();
    P.prefix(exec('ref_firma','ustawienia'),'P',P.OSOBA);
    {? P.first()
    || {!
       |? H.prefix(P.ref(),'Z');
          par[6]:=~{? H.find_le(par[3].tab.DATA) || H.DO=#0 | H.DO>=par[3].tab.DATA ?};
          par[6] & P.next()
       !}
    || par[6]:=1
    ?};
    P.cntx_pop(); ~~
   }
   {IF: par[6]}
      { par[4][3]:=0; par[15]+=1; ~~ }
      {FOR: ZC}
      {INDEX: 'ZLECDAT'}
      {PREFIX: exec('ref_firma','ustawienia'),P.OSOBA}
      {DO}
          {IF: ZC.DU<=par[3].tab.DATA & ZC.DW>=par[3].tab.DATA}
          {IF: dpod=0}
            { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~ }
            {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
            { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~ }
            { dpod:=1; ~~ }
          {FI}
          { {? par[4][3]=0
            || par[4][2]+=1;
               par[4][1]:=par[4][2];
               par[14][1]:=P.OSOBA().NAZWISKO;
               par[14][2]:=OSOBA.PIERWSZE;
               par[4][3]:=1
            || par[4][1]:=par[14][1]:=par[14][2]:=''
            ?}; ~~
          }
          {IF: rep_export() & zchead}
            {<{lmt+1}'Zatrudnienie na dzień (zleceniobiorcy)'}
            {SUBSTITUTE: 'TABHEAD'}
            {FONT: 'T8GB'}
            {SPACE: 'C'}
            {LINE}
            { zchead:=0; ~~ }
          {FI}
          {SUBSTITUTE: 'LINIA0'}
          {IF: ~rep_export() & lineleft()<=4}
            {SUBSTITUTE: 'LINIAF'}
            {PAGE}
          {FI}
          { _spr:=0;
            {! _n:=1..obj_len(par[13])
            |? _spr=0
            |! {? par[13][_n][1]=ZC.RU().K || par[13][_n][3]+=1; par[13][obj_len(par[13])][3]+=1; _spr:=1 ?}
            !};~~
          }
          {FI}
       {OD}
   {FI}
{ELSE}
   {IF:  H.prefix(P.ref(),'Z'); {? H.find_le(par[3].tab.DATA) || H.DO=#0 | H.DO>=par[3].tab.DATA ?}}
      {SUBSTITUTE: 'LINIA0'}
      {IF: ~rep_export() & lineleft()<=4}
           {SUBSTITUTE: 'LINIAF'}
           {PAGE}
      {FI}
      {  _spr:=0;
         {! _n:=1..obj_len(par[13])
         |? _spr=0
         |! {? par[13][_n][1]=H.RU().K
            || par[13][_n][3]+=H.RWY;
               par[13][obj_len(par[13])][3]+=H.RWY;
               _spr:=1
            ?}
         !};
         _ref:=#P.OSOBA;
         {? ~par[16].find_key(_ref) || par[16].REF:=_ref; par[16].add() ?}; ~~
      }
   {FI}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{COMMENT}pracownicy{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{IF: __F_ZATR.P='P' | (__F_ZATR.P='Z' & par[3].tab.INNY)}
{ maxlines:=lineleft();lmt:=int((charleft()-par[1].width())/2); dod:=int(charleft()/5*2); ~~ }
{IF: __F_ZATR.P='Z'}
{ _args:=__PARSES.args('F_ZATR');
  _args.KOD:='P';
  __PARSES.setVal('F_ZATR',_args);
  par[18].P:=exec('wybierz_parses','pracownik','PKD').P;
  _args.KOD:='Z';
  __PARSES.setVal('F_ZATR',_args); ~~ }
{FI}
{FOR: par[18].P}{DO}
   {IF: P.seek(par[18].P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}
{SPACE: 'C'}
{IF: ~rep_export()}
{ENTIRE}
{ par[12]:='%1: '['RAZEM PRACOWNIKÓW'@]; par[11]:=0; ~~ }
{<{lmt+1} '%1:'['PODSUMOWANIE'@]}
{WHILE: licznik<=obj_len(par[13])}
{DO}
{IF: par[13][licznik][3]} {<{lmt+5} par[13][licznik][2]} {<{dod} par[13][licznik][3]}{FI}
{ licznik+=1; ~~ }
{OD}
{<{lmt+5} par[12]}   {<{dod} par[4][1]}
{<{lmt+5} '%1:'['RAZEM OSÓB'@]}   {<{dod} par[16].size()}
{ENTIRE-}
{FI}
{FI}
{COMMENT}zleceniobiorcy{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{IF: par[3].tab.INNY}
   {IF: maxlines<>lineleft()}
      {PAGE}
   {FI}
{FI}
{IF: __F_ZATR.P='Z' | (__F_ZATR.P='P' & par[3].tab.INNY)}
   {  pod('UMZLEC',1);
      par[5]:=1;
      PAR_WYDR.TITLE+=' (%1)'['zleceniobiorcy'@];
      lmt:=int((charleft()-par[8].width())/2);
      prn1:='par[8]';
      par[7]:=0;
      ~~
   }
{IF: __F_ZATR.P='P'}
{ _args:=__PARSES.args('F_ZATR');
  _args.KOD:='Z';
  __PARSES.setVal('F_ZATR',_args);
  par[18].Z:=exec('wybierz_parses','pracownik','PKD').P;
  _args.KOD:='P';
  __PARSES.setVal('F_ZATR',_args); ~~ }
{FI}
{FOR: par[18].Z}{DO}
   {IF: P.seek(par[18].Z.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{FI}
{IF: par[15]}
{SUBSTITUTE: 'LINIAF'}
{IF: ~rep_export()}
{FONT: 'T8GB'}
{SPACE: 'C'}
{ENTIRE}
   { par[12]:='%1: '['RAZEM ZLECENIOBIORCÓW'@]; licznik:=1; ~~ }
   {<{lmt+1} '%1 :'['PODSUMOWANIE'@]}
   {WHILE: licznik<=obj_len(par[13])}
   {DO}
   {IF: par[13][licznik][3]} {<{lmt+5} par[13][licznik][2]} {<{dod} par[13][licznik][3]}{FI}
   { licznik+=1; ~~ }
   {OD}
   {<{lmt+5} par[12]}   {<{dod} par[4][2]}
   {ENTIRE-}
{FI}
{FI}
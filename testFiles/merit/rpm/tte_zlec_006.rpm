:!UTF-8
{TITLE:F. Zbiorcze zestawienie robocizny dla zleceń}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   ZL.cntx_psh();
   ZLGD.cntx_psh();
   ZLGB.cntx_psh();
   dalej:=0;
   {? var_pres('PRN')>0 || obj_del(PRN) ?};
   PRN:=obj_new(@.CLASS.PRINT,16);
   PRN.s_frame();
   {? var_pres('TABx')>0 || obj_del(TABx) ?};
   TABx:=tab_tmp(3,'WYD','STRING[8]','Ref.do wydz.',
                  'ZLREF','INTEGER','Ref.do zlec',
                  'NRR','INTEGER','Nr rubryki',
                  'WART','REAL','Wartosc',
                  'SYM','STRING[20]','Sym do zlec');
   prn1:='PRN';
   tryb:=rep_export();
   sym:='';
   color:='';
   wyd:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   zlec:='O'; okrkw:='O';
   ilo:=6;wyd:='N';odd:='T';rodz:='N';
   rob:=0;wartosc:=0
}
{END:
   ZL.cntx_pop();
   ZLGD.cntx_pop();
   ZLGB.cntx_pop();
   &dalej;
   {? var_pres('tabok')>=0 ||  &tabok ?};
   {? var_pres('druk')>=0 || &druk ?};
   obj_del(PRN);
   obj_del(TABx);
   &prn1;
   &color;
   &rodz;
   &sym;
   &ilo;&wyd;&odd;&zlec;&okrkw;
   &lm; &ll; &vp; &lmt; &rsep;
   &rob;&wartosc;&tryb
}
{COMMENT}
  Wydruk wartosci surowcow pobranych do zlecen
  [TS] - 2002/05/24 - [7.60]
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{ _wydr_naz:='zlec_006';
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.ZLECOZ:=WYDRUKIL.SORTUJ;
     WYDRUK.OKRKW:=WYDRUKIL.FAKUM;
     WYDRUK.ILO:=WYDRUKIL.TYP_ZEST;
     WYDRUK.WYD:=WYDRUKIL.FAK;
     WYDRUK.ODP:=WYDRUKIL.RODZFAK;
     WYDRUK.RODZAJ:=WYDRUKIL.SR
  ?};
  WYDRUK.win_edit(); WYDRUK.win_edit('ZLEC_006'); ~~}
{IF: WYDRUK.edit()}
{WYDRUKIL.SORTUJ:=WYDRUK.ZLECOZ;
 WYDRUKIL.FAKUM:=WYDRUK.OKRKW;
 WYDRUKIL.TYP_ZEST:=WYDRUK.ILO;
 WYDRUKIL.FAK:=WYDRUK.WYD;
 WYDRUKIL.RODZFAK:=WYDRUK.ODP;
 WYDRUKIL.SR:=WYDRUK.RODZAJ;
 WYDRUKIL.put(1);
 zlec:=WYDRUK.ZLECOZ;
 okrkw:=WYDRUK.OKRKW;
 ilo:=WYDRUK.ILO;
 wyd:=WYDRUK.WYD;
 odd:=WYDRUK.ODP;
 rodz:=WYDRUK.RODZAJ;
 {? ~tryb
 || {? wyd='T' & odd='T'
    || _o:=int((charleft()-39)/12)
    |? (wyd='T' & odd='N') | (wyd='N' & odd='T')
    || _o:=int((charleft()-27)/12)
    || _o:=int((charleft()-15)/12)
    ?}
 || _o:=12
 ?};
 {? ilo>_o
 || FUN.info('Podana liczba okresów nie mieści się na wydruku.\nIlość została zmniejszona do %1.'@[form(_o)]);
    ilo:=_o
 ?};
 tabok:=obj_new(ilo);
 druk:=obj_new(ilo+1);
 {! _i:=1..ilo+1
 |! druk[_i]:=0
 !};
 {! _i:=1..ilo
 |! tabok[_i]:=obj_new(2);
    tabok[_i][1]:='';
    tabok[_i][2]:=date(0,0,0)
 !};
 {? okrkw='O'
 || OKR.cntx_psh();
    OKR.clear();
    OKR.index('OKR');
    _can_continue:=OKR.find_key(REF.FIRMA,ST.AR,ST.AM);
    _i:=1;
    {!
    |? _can_continue>0
    |!
        tabok[_i][1]:=(OKR.NAZ-5)+'/'+(12-(+(OKR.NAZ-5)))*' '+$OKR.ROK;
        tabok[_i][2]:=date(OKR.ROK,OKR.MC,0);
        _i+=1;
        {? _i<=ilo & OKR.prev()
        || _can_continue:=1
        || _can_continue:=0
        ?}
    !};
    pocz:=date(OKR.ROK,OKR.MC,1);
    OKR.cntx_pop()
 || _zm:={? OKR.MC<4  || 1
         |? OKR.MC<7  || 2
         |? OKR.MC<10 || 3
         || 4
         ?};
   OKR.cntx_psh();
   OKR.clear();
   OKR.index('OKR');
   _can_continue:=OKR.find_key(REF.FIRMA,ST.AR);
   {! _i:=1..ilo
   |? _can_continue>0
   |! tabok[_i][1]:=$_zm+'/'+$OKR.ROK;
      tabok[_i][2]:={? _zm=1 || date(OKR.ROK,3,31)
         |? _zm=2 || date(OKR.ROK,6,30)
         |? _zm=3 || date(OKR.ROK,9,30)
         || date(OKR.ROK,12,31)
         ?};_zm-=1;
      {? _zm=0
      || _zm:=4;
         _can_continue:=OKR.prev()
      ?}
   !};
   _zm+=1;
   pocz:={? _zm=1 || date(OKR.ROK,1,1)
         |? _zm=2 || date(OKR.ROK,4,1)
         |? _zm=3 || date(OKR.ROK,7,1)
         || date(OKR.ROK,10,1)
         ?};
   OKR.cntx_pop()
 ?};
~~}
{ELSE}
{EXIT}
{FI}
{COMMENT}
Co ma robic wydruk:
===================
- mozliwosc wyboru wszystkich zlecen lub tylko otwartych
  lub tylko zamknietych.
- wydruk od poczatku zlecenia, dla aktywnego okresu, tygodnia(?), dnia
- jezeli od poczatku, to zobaczyc najdawniej otwarte zlecenie z przetwarzanych
  i iterowac po okresach wrzucajac do tabeli tymczasowej dane dla
  kazdego zlecenia - potem drukowac
- jezeli "od poczatku", to drukowac od n-tego miesiaca (okresu) wstecz
  w kolumnach co miesiac do biezacego miesiaca, a dla miesiecy
  dawniejszych niz n sume w pierwszej kolumnie.
- sumowana wartosciowo robocizna akordowa dla kazdego zlecenia ogolem
  (czy robic opcjonalnie jakies rozbicie wartosci robocizny np.
  wg zawodow?).
- Czy sumowac kazda kolumne? Chyba nie calkiem sens, chyba ze
  beda drukowane wszystkie zlecenia wystepujace w danym okresie,
  bo inaczej suma kolumny nie bedzie jednoznaczna z faktyczna suma
  akordu w danym okresie (albo trzeba o tym poprostu pamietac).
- obliczenia odbywaja sie z zawartosci tabeli ZLGD
- dac rozbicie na wydzialy?

- narazie wariant z biezacego okresu...
{COMMENT-}
{COMMENT}-Definicja wiersza wydruku -{COMMENT-}
{PRN.add("sym",20,'Zlecenie'@,0);
{? wyd='T' || PRN.add("wyd",7,'Wydział'@,0) ?};
{? odd='T' || PRN.add("form(druk[1],,2)",7,'Robocizna wcześn.'@,1) ?};
{! _i:=1..ilo
|!
  PRN.add($('form(druk['+$(ilo+2-_i)+'],,2)'),12,tabok[ilo+1-_i][1],1)
!};
PAR_WYDR.TITLE:='ZBIORCZE ZESTAWIENIE ROBOCIZNY DLA ZLECEŃ'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} {? zlec='O' || 'Zlecenia otwarte'@ |? zlec='Z' || 'Zlecenia zamknięte'@ || 'Zlecenia wszystkie'@ ?}}
{<{lmt+1} {? okrkw='O' || 'Podział na okresy'@ || 'Podział na kwartały'@ ?}}
{<{lmt+1} {? okrkw='O' || 'Szczegółowe zestawienie dla %1 okresów'@[form(ilo)] || 'Szczegółowe zestawienie dla %1 kwartałów'@[form(ilo)] ?}}
{<{lmt+1} {? wyd='T' || 'Rozbicie na wydziały'@ || 'Brak rozbicia na wydziały'@ ?} }
{<{lmt+1} {? odd='T' || 'Zestawienie od początku zlecenia'@ || '' ?} }
{FONT: 'T10B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------Macro pomocnicze--------------------{COMMENT-}
{MACRO: 'utwzl'}
{
  _data:=date(0,0,0);
  {? odd='T'
  || ZL.index('DATAO');
     {? ZL.first()
     || {!
        |? _data:=ZL.OD;
           ZL.next() & _data=date(0,0,0)
        !}
     ?}
  || _data:=pocz
  ?};
     OKR.clear();
     OKR.index('OKR');
     _ok:=OKR.find_ge(REF.FIRMA,_data~1,_data~2);
     {!
     |?
        'ustala nr okresu';
        ZLGD.use('zlgd_'+ST.ODDZ+(2-$OKR.ROK));
        {? date(OKR.ROK,OKR.MC,1)<pocz
        || _i:=0
        || _i:=1;_ok:=1;
           {? okrkw='O'
           || {! |?
                 {? tabok[_i][2]=date(OKR.ROK,OKR.MC,0)
                 || _ok:=0
                 || _i+=1
                 ?};
                 _i<=ilo & _ok
              !}
           || {! |?
                 {? tabok[ilo+1-_i][2]>=date(OKR.ROK,OKR.MC,0)
                 || _ok:=0
                 || _i+=1
                 ?};
                _i<=ilo & _ok
              !};
              _i:=ilo+1-_i
           ?}
        ?};
        ZL.index('STAN');
        {? zlec= 'O' || ZL.prefix('N','O')
        |? zlec= 'Z' || ZL.prefix('N','Z')
        || ZL.prefix()
        ?};
        {? ZL.first()
        || {? wyd='N'
           || {!
              |?
              wartosc:=0;
              ZLGD.index('ZL_OKRES');
              ZLGD.prefix(ZL.ref(),OKR.ref());
              {? ZLGD.first()
              || {!|?
                   wartosc+=ZLGD.KW;
                   ZLGD.next()
                 !}
              ?};
              {? wartosc>0
              ||
                 {? ZL.NRNZL=0 | rodz='T'
                 || TABx.prefix('',#ZL.ref(),_i);
                    {? TABx.first()
                    || TABx.WART+=wartosc;
                       TABx.put()
                    || TABx.WYD:='';
                       TABx.ZLREF:=#ZL.ref();
                       TABx.NRR:=_i;
                       TABx.WART:=wartosc;
                       TABx.SYM:=ZL.SYM;
                       TABx.add()
                    ?}
                 || ZL.cntx_psh();
                    ZL.index('UNRZL');
                    ZL.prefix(ZL.NRNZL);
                    ZL.first();
                    _zl:=#ZL.ref();
                    ZL.cntx_pop();
                    TABx.prefix('',_zl,_i);
                    {? TABx.first()
                    || TABx.WART+=wartosc;
                       TABx.put()
                    || TABx.WYD:='';
                       TABx.ZLREF:=_zl;
                       TABx.NRR:=_i;
                       TABx.WART:=wartosc;
                       TABx.SYM:=ZL.SYM-3;
                       TABx.add()
                    ?}
                 ?}
              ?};
              wartosc:=0;
              ZL.next()
              !}
           || {! |?
              wartosc:=0;
              ZLGD.index('ZL_OKRES');
              ZLGD.prefix(ZL.ref(),OKR.ref());
              {? ZLGD.first()
              || {!|?
                    wartosc:=ZLGD.KW;
                    {? ZL.NRNZL=0 | rodz='T'
                    || TABx.prefix(ZLGD.WYD().SYMBOL,#ZL.ref(),_i);
                       {? TABx.first()
                       || TABx.WART+=wartosc;
                          TABx.put()
                       || TABx.WYD:=ZLGD.WYD().SYMBOL;
                          TABx.ZLREF:=#ZL.ref();
                          TABx.NRR:=_i;
                          TABx.WART:=wartosc;
                          TABx.SYM:=ZL.SYM;
                          TABx.add()
                       ?}
                    || ZL.cntx_psh();
                       ZL.index('UNRZL');
                       ZL.prefix(ZL.NRNZL);
                       ZL.first();
                       _zl:=#ZL.ref();
                       ZL.cntx_pop();
                       TABx.prefix(ZLGD.WYD().SYMBOL,_zl,_i);
                       {? TABx.first()
                       || TABx.WART+=wartosc;
                          TABx.put()
                       || TABx.WYD:=ZLGD.WYD().SYMBOL;
                          TABx.ZLREF:=_zl;
                          TABx.NRR:=_i;
                          TABx.WART:=wartosc;
                          TABx.SYM:=ZL.SYM-3;
                          TABx.add()
                       ?}
                    ?};
                    ZLGD.next()
                 !}
              ?};
              wartosc:=0;
              ZL.next()
              !}
           ?}
        ?};
        _can_continue:=OKR.next();
        {? _can_continue>0
        || _can_continue:=date(OKR.ROK,OKR.MC,1)<=date(ST.AR,ST.AM,1)
        ?};
        _can_continue>0
     !}
;~~}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{SUBSTITUTE: 'utwzl'}
{TABx.clear();~~}
{IF: TABx.first()}{ dalej:=1;zl:=TABx.ZLREF;i:=1;sym:=TABx.SYM;wyd:=TABx.WYD;~~}{FI}
{WHILE: dalej}
{DO}
{WHILE: i<=ilo+1}
   {DO}
     {IF: TABx.NRR<>i-1 }
      {druk[i]:=0;i+=1;~~}
     {ELIF: sym=TABx.SYM & wyd=TABx.WYD}
      {druk[i]:=TABx.WART$3;i+=1;~~}
     {dalej:=TABx.next();~~}
     {ELSE}
      {druk[i]:=0;i+=1;~~}
     {FI}
   {OD}
    {SUBSTITUTE: 'LINIA0'}
   {i:=1;zl:=TABx.ZLREF;sym:=TABx.SYM;wyd:=TABx.WYD;~~}
{OD}

:!UTF-8
{TITLE:B. Wymagania osób - tabela zbiorcza}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  PRN := obj_new(@.CLASS.PRINT, 20);PRN.s_frame();
  kreska :='Ä';
  prn1:='PRN';
  lp:=0;
  select:=0;
  dalej:=0;
  width:=80; szerpodp:=15;
  __TABREF:=tab_tmp(1,'REF','INTEGER','REF');
  color:='';
  P.cntx_psh();
  ROZMP.cntx_psh();
  TAT.cntx_psh(); TAT.index('NA'); TAT.clear();
  wydref:=null();
  lm:=ll:=vp:=lmt:=0;
  rsep:=PAR_WYDR.RSEP
}
{END:
  obj_del(PRN);
  obj_del(__TABREF); &__TABREF;
  &kreska; &width; &szerpodp;
  &lp;
  &select;&dalej;&wydref;
  P.cntx_pop(); ROZMP.cntx_pop(); TAT.cntx_pop();
  &prn1;
  &color;
  &lm; &ll; &vp; &lmt; &rsep
}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
 (c) Macrologic S.A. Wszelkie prawa zastrzezone
 Wersja: 12.10
 Autor: Waldemar Werbiński
 Systemy: WYPOSAZ
{COMMENT-}
{COMMENT}pobranie atrybutow dla asortymentu{COMMENT-}
{FONT: 'T8B'}{SPACE: 'B'}
{ width:=charleft(); ~~}
{IF: exec('fltr_wymc','normatyw')}
  { {? var_pres('TMPWWWYM')>0 || obj_del(TMPWWWYM) ?};
    "definiowanie pomocniczych tabel tymczasowych";
    "bufor wymiarow";
    TMPWWWYM:=tab_tmp(1,'NAZWA','STRING[20]','Nazwa parametru'
                       ,'REF','INTEGER','Ref'
                       ,'WAR','STRING[10]','Bufor na wartości'
                     );
    _ind:=TMPWWWYM.index('?');
    TMPWWWYM.index(TMPWWWYM.ndx_tmp(,,'REF',,));
    "wstawianie danych o wymiarach";
    {? TAT.f_first() ||
      {! |?
         TAT.f_get();
         {? ~TMPWWWYM.find_key(#TAT.ref()) ||
             TMPWWWYM.blank();
             TMPWWWYM.NAZWA:=TAT.NA;
             TMPWWWYM.REF:=#TAT.ref();
             TMPWWWYM.add()
         ?};
         TAT.f_next()
      !}
    ?};
    TAT.f_clear();
    TMPWWWYM.index(_ind);
    PRN.add("lp+=1;form(lp,,0)",4,'Lp.',1);
    PRN.add("P.OSOBA().NAZWISKO+' '+P.OSOBA().PIERWSZE+' '+P.OSOBA().DRUGIE",30,'Osoba');
    _odb:=FUN.ask('Czy dodać kolumnę na podpis?'@);
    "obliczenie szerokosci kolumny na wymiary";
    {? TMPWWWYM.size()=0
    || _wym:=1
    || _wym:=((width-PRN.width()-{? _odb || szerpodp+3 || 0 ?})%TMPWWWYM.size())-3
    ?};
    {? _wym<=0 || _wym=1 ?};
    {? TMPWWWYM.first() ||
       {! |?
          PRN.add($('TMPWWWYM.seek('+($#TMPWWWYM.ref())+',);'+($_wym+'+TMPWWWYM.WAR')),_wym,TMPWWWYM.NAZWA);
          TMPWWWYM.next()
       !}
    ?};
    {? _odb || PRN.add("''",szerpodp,'Podpis odbiorcy') ?};
    ROZMP.index('OSOBA');ROZMP.clear();
  ~~}
  {MACRO: 'MDRUK'}
    {COMMENT}zabezpiecenie przed powtorzeniem osoby gdy wiele zatrudnieni pasujacych do klucza{COMMENT-}
    {IF: ~__TABREF.find_key(#P.OSOBA)}
      {PAR_WYDR.TITLE:='WYMAGANIA OSÓB';~~}
      { __TABREF.REF:=#P.OSOBA; __TABREF.add(); ~~}
      {FIRST}
        {SUBSTITUTE: 'HEAD'}
        {FONT: 'T8B'}{SPACE: 'B'}
        { lmt:=int((width-PRN.width())/2); ~~}
        {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
        {HEADER}
        {SUBSTITUTE: 'HEAD'}
        {FONT: 'T8B'}{SPACE: 'B'}
        {SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}
        {HEADER-}
        {FOOTER: 0}
        {SUBSTITUTE: 'LINIAF'}
        {LINE: 1}
        {SUBSTITUTE: 'FOOT'}
        {FOOTER-}
      {FIRST-}
      {COMMENT}ladowanie danych wymiarowych osoby{COMMENT-}
      { {? TMPWWWYM.first() ||
          {! |?
            TAT.seek(TMPWWWYM.REF,);
            {? ROZMP.find_key(P.OSOBA,TAT.ref())
            || TMPWWWYM.WAR:=form(ROZMP.WAR)
            || TMPWWWYM.WAR:=''
            ?};
            TMPWWWYM.put();
            TMPWWWYM.next()
          !}
        ?};
      ~~}
      {SUBSTITUTE: 'LINIA0'}
    {FI}
  {MACRO-}
  {_p6520:=exec('get','#params',6520,2,OPERATOR.USER);
   exec('filtruj_p','wyp',1+_p6520,_p6520+1);
   jest:=1;~~}
  {IF: P.f_first()}
    {WHILE: jest}
    {DO}
     {SUBSTITUTE: 'MDRUK'}
     {jest:=P.f_next; ~~}
    {OD}
  {FI}
{ELSE}
   {FUN.error('Nie udało się założyć filtra na tabelę atrybutów.'@);~~}
{FI}

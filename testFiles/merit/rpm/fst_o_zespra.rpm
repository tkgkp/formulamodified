:!UTF-8
{TITLE:N. Środki wg pracowników}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
     PRN:=obj_new(@.CLASS.PRINT,7); PRN.s_frame();
     c:=obj_new(7);
     {! _i:=1..7 |! c[_i]:= '' !};
     SRSR.cntx_psh(); SRST.cntx_psh(); OSOBA.cntx_psh();
     exec('init','fst');
     SRD.maski('r');
     exec('odd_filtr','fst');
     i:=0;
     color:=prn1:=prn2:=''; tytul:=''; ind2:=''; k:=0; TAB_SROD:=null; nextO:=null;
     lm:=ll:=vp:=lmt:=rsep:=0; wartow:=0; srst_dr:=0;__cur_mo:=null; __cur_r:=''; __cur_od:=null; __cur_rf:=null;
     par1:=0; par2:=0; par3:=0; par4:=0
}
{END:
     obj_del(PRN);
     obj_del(c);
     SRSR.cntx_pop(); SRST.cntx_pop(); OSOBA.cntx_pop()
     &i; &color; &prn1; &prn2;&tytul; &ind2; &k; &wartow; &srst_dr; &TAB_SROD; &nextO;
     &lm; &ll; &vp; &lmt; &rsep; &__cur_mo; &__cur_r; &__cur_od; &__cur_rf;
     &par1; &par2; &par3;&par4
}
{EXIT:
     exec('clear_par','fst');
     exec('set_parw','param_wydr');
     {? OPERATOR.DEPT<>null || PAR_WYDR.ODD:=OPERATOR.DEPT || PAR_WYDR.ODD:=0 ?};
     {? SSTALE.AO().KON<>date(0,0,0)
     || PAR_WYDR.STAN_NA:=SSTALE.AO().KON
     |? SSTALE.AO().OES=0
     || PAR_WYDR.STAN_NA:=SSTALE.AR().POCZ_ROK
     |? SSTALE.AO().OES>0
     || PAR_WYDR.STAN_NA:=SSTALE.AR().KON_ROK
     ?};
     PAR_WYDR.OSOBA:=null;
     PAR_WYDR.SKRESL:=1;
     PAR_WYDR.TO_DE:=SSTALE.AO().KON;
     PAR_WYDR.RODZ_POM:=1;
     SRD.PAR_WYDR:='T';
     PAR_WYDR.JORG:=null; EDIT_ES.JORG:='';
     PAR_WYDR.win_edit('ZEST_PRA');
     {? PAR_WYDR.edit()
     || exec('sql_pra','fst2');
        {? exec('if_ok','fst','TAB_SROD',1)
        || _exit:=0
        || _exit:=1
        ?}
     || _exit:=1
     ?};
     _exit
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_o_zespra.rpm
Utworzony  : 2018-07-16
Zawartość  : Wydruk środków w użytkowaniu pracownika.
==================================================
{COMMENT-}
{PAR_WYDR.cntx_psh();~~}
{INCLUDE: wsp_pw.rpi}
{PAR_WYDR.cntx_pop();~~}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
{MACRO: 'SRODEK'}
{
     c[1]:=k;
     c[2]:=TAB_SROD.NRI;
     c[3]:=TAB_SROD.NF;
     c[4]:=TAB_SROD.NST;
     c[5]:=TAB_SROD.Z;
     c[6]:=TAB_SROD.OD;
     c[7]:=TAB_SROD.SYMBOL
;~~}
{IF: lineleft() <=2 & lineleft()<>0}{PAGE}{FI}
{FONT: 'T8G'}{SPACE: 'C'}{SUBSTITUTE: 'LINIA02'}
{IF: TAB_SROD.GRP='T' & PAR_WYDR.RODZ_POM=2 }
{SUBSTITUTE: 'ELEMENTY'}
{FI}
{prn1:='PRN'; prn2:='PRN';~~}
{rsep:=PAR_WYDR.RSEP;~~}
{MACRO-}
{COMMENT} ---- Elementy ---- {COMMENT-}
{MACRO: 'ELEMENTY'}
{SRST.seek(TAB_SROD.SRST_R); __cur_mo:=SRST.OKRO_F; __cur_r:=SRST.R; __cur_od:=SRST.ODD; __cur_rf:=SRST.ref(); SRST.cntx_psh();~~}
{FOR: SRST }{INDEX: 'TREE' }{PREFIX: __cur_r, __cur_mo, __cur_od, __cur_rf}
{DO}
{IF: PAR_WYDR.SKRESL=0 | (PAR_WYDR.SKRESL=1 & SRST.SRSR().Z='N')}
{
     c[1]:='';
     c[2]:=SRST.NRI;
     c[3]:=SRST.NF;
     c[4]:=SRST.NST;
     c[5]:=SRST.SRSR().Z;
     c[6]:='';
     c[7]:='';
     rsep:=0;
     prn1:='PRN'; prn2:='PRN'
;~~}
{SUBSTITUTE: 'LINIA02'}
{FI}
{OD}
{SRST.cntx_pop();~~}
{MACRO-}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{    PRN.add("c[1]",3,'Lp.'@,,);
     PRN.add("c[2]",18,'Nr inwentarzowy'@,,);
     PRN.add("c[3]",30,'Nr fabryczny'@,,);
     PRN.add("c[4]",100,'Nazwa środka'@,,);
     PRN.add("c[5]",8,'Skreśl.?'@,,'#');
     PRN.add("c[6]",11,'J. Księgowa'@,,);
     PRN.add("c[7]",16,'J. organizacyjna'@,,);
     PAR_WYDR.TITLE:='Środki pracownika'@;
~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{MACRO: 'PARAMETRY'}
{FONT: 'TITLE'}{SPACE: 'A'}
{FONT: 'TITLE'}{<1>{charleft()} tytul}
{FONT: 'TITLE'}{<1>{charleft()}'Stan na dzień: '@+PAR_WYDR.STAN_NA$6}
{FONT: 'W8'}{SPACE: 'C'}
{FONT: 'W8B'}{<1 'PARAMETRY WYDRUKU:'@}
{FONT: 'W8B'}{<1 'Jednostka organizacyjna: '@}{FONT: 'W8'}{{? PAR_WYDR.JORG=null || 'dowolna'@ || PAR_WYDR.JORG().SYMBOL ?}}{FONT: 'W8B'}{<41 'Jednostka księgowa: '@}{FONT: 'W8'}{{? PAR_WYDR.ODD=null || 'dowolna'@ || PAR_WYDR.ODD().OD ?}}{FONT: 'W8B'}{<74 'Rodzaj wydruku: '@}{FONT: 'W8'}{{? PAR_WYDR.RODZ_POM=1 || 'główne'@ || 'z elementami'@ ?}}{FONT: 'W8B'}{<107 'Środki: '@}{FONT: 'W8'}{{? PAR_WYDR.ZAKRESDR=0 || 'wszystkie'@ |? PAR_WYDR.ZAKRESDR=1 || 'trwałe'@ |? PAR_WYDR.ZAKRESDR=2 || 'niskocenne'@ ?}}{FONT: 'W8B'}{<129 'Skreślone?: '@}{FONT: 'W8'}{{? PAR_WYDR.SKRESL=1 || 'nie'@ || 'tak'@ ?}}
{LINE: 1}
{MACRO-}
{IF:wartow=0}
{tytul:=TAB_SROD.PIERWSZE+' '+{? TAB_SROD.DRUGIE<>''|| TAB_SROD.DRUGIE+' '|| ''?}+TAB_SROD.NAZWISKO; wartow:=1 ;~~}
{FI}
{SUBSTITUTE: 'PARAMETRY'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{SUBSTITUTE: 'PARAMETRY'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{IF:PAR_WYDR.PODPIS=1}{LINE:1}
{FONT: 'SIGN'}{<162 'Podpis osoby odpowiedzialnej:'@}{LINE:2}
{FONT: 'SIGN'}{<162 '.....................................'}
{FI}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SPACE: 'B'}
{SIGNATURE}
{FONT: 'SIGN'}{ {? var_pres('PAGELANG')>0 || __P_STR:=exec('tran_sik','lang',17); __P_Z:=exec('tran_sik','lang',18) || __P_STR:='Strona'; __P_Z:='z' ?};{? PAR_WYDR.SIGN || s_sign:=exec('nazwa','#system')+' v.ST '+exec('wersja','#system') || s_sign:='' ?}; s_sign}{IF: PAR_WYDR.NUM}{>{charleft()++s_sign}__P_STR+' '+$page()}{FI}{&__P_STR;&__P_Z;~~}
{SIGNATURE-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{IF: PAR_WYDR.OSOBA<>null}
{FOR:TAB_SROD}
    {DO}
          {IF: $OSOBA.ref()=$TAB_SROD.OSOBA_R}
             {IF: TAB_SROD.KIND='T'}
             {tytul:=OSOBA.PIERWSZE+' '+{? OSOBA.DRUGIE<>''|| OSOBA.DRUGIE+' '|| ''?}+OSOBA.NAZWISKO; k+=1 ;~~}
             {SUBSTITUTE: 'SRODEK'}
             {FI}
          {FI}
    {OD}
{ELSE}
{ nextO:=TAB_SROD.OSOBA_R ;~~}
{FOR:TAB_SROD}
{DO}
    {IF: TAB_SROD.KIND='T'}
    {IF: nextO<>TAB_SROD.OSOBA_R}
    { nextO:=TAB_SROD.OSOBA_R; k:=0 ;~~}
    {PAGE}
    { page(1) ;~~}
    {FI}
    {tytul:=TAB_SROD.PIERWSZE+' '+{? TAB_SROD.DRUGIE<>''|| TAB_SROD.DRUGIE+' '|| ''?}+TAB_SROD.NAZWISKO; k+=1 ;~~}
    {SUBSTITUTE: 'SRODEK'}
    {FI}
{OD}
{FI}
{PAGE}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
{FOOTER}{FOOTER-}
{prn1:='PRN2'; prn2:='PRN2';~~}
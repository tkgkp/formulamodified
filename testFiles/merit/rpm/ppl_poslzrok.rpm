:!UTF-8
{TITLE:Pełne zestawienie składników płac i zleceń dla osoby}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__edit','__param','__czyZ');
   tresc:='poslzrok';
   PAR_WYDR.TITLE:='Pełne zestawienie składników płac i zleceń dla osoby'@;
   par:=obj_new(15);
   par[7]:=par[9]:=0;
   par[15]:=rep_export();
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || _upr:=exec('uprawnieniawrap','pkd','Rejestracja rachunków'@,'PPL_ZLC_RRAC','PPL_ZLC_PRAC');
      __czyZ:=(_upr='');
      {? +_upr
      || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
      ?};
      __param:=tab_tmp(1,
         'ROK','INTEGER','Raport za rok'@,
         'DRUP','INTEGER','Drukować parametry sprawozdania'@,
         'RMIN','INTEGER' ,'',
         'RMAX','INTEGER' ,''
      );
      __param.ROK:=O.R;

      O.cntx_psh();
      O.index('LISTYPLA');
      O.prefix(exec('ref_firma','ustawienia'),'P',);
      __param.RMIN:={? O.first() || O.R ?};
      __param.RMAX:={? O.last() || O.R ?};
      __param.DRUP:=0;
      O.cntx_pop();

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pkartubr');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'ROK',,,4,,,,,'Rok kosztowy'@);
      __param.win_efld(__edit,,'DRUP',,,,,,,,,'radio-buttons',,
         'na pierwszej stronie'@,"0",
         'na każdej stronie'@,"1"
      );
      __param.efld_opt(__edit,'mark=1',,'ROK');
      __param.efld_opt(__edit,'enable='+$(~par[15]),,'DRUP');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[7]:=__param.edit(
            "  {? __param.ROK<__param.RMIN | __param.RMAX<__param.ROK
               || __CHK.err_fld(__param,'ROK',1,'Dozwolone wartości z przedziału %1-%2.'@ [$__param.RMIN,$__param.RMAX])
               || ''
               ?}
            "
         )
      || par[9]:=__param.ROK;
         par[1]:=obj_new(@.CLASS.PRINT,14);
         {? par[15]
         || par[1].add(
               "  '%1 %2 %3 %4'
                     [  P.OSOBA().NAZWISKO,
                        OSOBA.PIERWSZE,
                        {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                        '(PESEL %1)'@[OSOBA.PESEL]
                     ]
               ",40,'Pracownik'@)
         ?};
         par[1].add("__RUB.RT[par[3].RN]",20,'Nazwa składnika'@);
         {! _str:=1..12
         |! STR.split(date(,_str,1)$8);
            par[1].add($('par[8]['+$_str+']'),12,STR.get_word(),1,,,,,'12,2')
         !};
         {? ~par[15] || par[1].add("par[3].KW",12,'RAZEM'@,1,,,,,'12,2') ?};
         par[1].s_frame();

         par[6]:=par[11]:=par[12]:=0;
         par[5]:={? __param.DRUP || 2 || 1 ?};
         par[8]:=obj_new(12);
         {! _ni:=1..obj_len(par[8]) |! par[8][_ni]:=0 !};
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)

      ?};
      prn1:='par[1]';
      lmt:=rsep:=par[13]:=0;
      vp:=b_rat:=1
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__edit','__param','__czyZ')
}
{COMMENT}OLD: poslzrok.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par[7]}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { {? par[5]=1 || par[5]:=0 ?}; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Rok kalendarzowy: '@+$par[9]}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~par[15]}
   {FOOTER:0}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {REP: par[1].flbar(lmt)}
   {FOOTER-}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W10'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ b_rat/={? par[15] || 1 || charleft() ?}; ~~ }
{SPACE: 'C'}
{  lmt:=int((charleft()-par[1].width())/2);
   echo('Trwa pobieranie danych z list płac...'@);
   _P:=params_get().P;
   _ndx:=_P.index('?');
   {? __czyZ
   || _P.index(_P.ndx_tmp(,,'OSQL',,));
      _tab:=tab_tmp(1,'PREF','STRING[16]','SQL_REF');
      _Z:=exec('dostepne_p','schemat','PPL','Z');
      'kolumn w _P jest sporo, ale tylko SQL jest istotna';
      _loop:=_Z.first();
      {!
      |? _loop
      |! {? _P.find_key(_Z.OSOBA)
         || _P.blank();
            _P.SQL:=_Z.REF;
            {? _P.add()
            || _tab.PREF:=$_P.ref();
               _tab.add()
            ?}
         ?};
         _loop:=_Z.next()
      !}
   ?};
   _P.index(_P.ndx_tmp(,,'SQL',,));
   par[14]:=sql(
      'select   OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL, R.LP, R.RN, O.M, '
      '         sum(LS.KW) as KW '
      'from     @LS join '
      '         O using(LS.O,O.REFERENCE) join '
      '         R using(LS.RB,R.REFERENCE) join '
      '         P using(LS.P,P.REFERENCE) join '
      '         OSOBA using(P.OSOBA,OSOBA.REFERENCE) '
      'where    LS.REFERENCE like \'l:_b%\' and P.FIRMA=:_c and '
      '         LS.P in (select :_a.SQL from :_a order by :_a.SQL) '
      'group by OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL, R.LP, R.RN, O.M '
      'order by 1,2,3,4,5,6',
      _P,
      $par[9]+2,
      exec('ref_firma','ustawienia')
   );
   {? __czyZ
   || {? _tab.first()
      || {!
         |? {? _P.seek(_tab.PREF,,1) || _P.del() ?};
            _tab.next()
         !}
      ?}
   ?};
   _P.index(_ndx); ~~
}
{EXIT: type_of(par[14])<>type_of(SYSLOG) | ~par[14].size()}
{  par[2]:=sql('
      select NAZWISKO, PIERWSZE, PESEL, LP, RN, M, sum(KW) KW
      from :_a
      group by NAZWISKO, PIERWSZE, PESEL, LP, RN, M
      order by 1,2,3,4,5,6',
      par[14]
   );
echo('Trwa przetwarzanie pobranych danych...'@); ~~
}
{EXIT: type_of(par[2])<>type_of(SYSLOG)}
{ par[3]:=sql('
      select NAZWISKO, PIERWSZE, PESEL, LP, RN, sum(KW) KW
      from :_a
      group by NAZWISKO, PIERWSZE, PESEL, LP, RN
      order by 1,2,3,4,5',
      par[2]
   );
   echo(); ~~
}
{EXIT: type_of(par[3])<>type_of(SYSLOG)}
{FOR: params_get().P}
{DO}
{IF: P.seek(params_get().P.SQL,,1)}
   { par[4]:=0; ~~ }
   {IF: par[13]<>#P.OSOBA}
      { par[13]:=#P.OSOBA; P.OSOBA();~~ }
      {FOR: par[3]}{PREFIX: OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL}
      {DO}
         {FIRST}
         {IF: ~par[15]}
            {FONT: 'W10'}
            {SPACE: 'B'}
            {<{ceil(lmt*b_rat)+1}}{form(par[6]+=(~par[12]),,,'9.')} {INCLUDE: p_osoba.rpi}
            {FONT: 'T8G'}
            {SPACE: 'C'}
         {FI}
         {IF: {? par[15] || ~par[4] || 1 ?}}
            {REP: par[1].fhbar(lmt)}
         {FI}
         { par[4]:=1; ~~ }
         {FIRST-}
         {IF: ~par[15] & par[12]}
         {FONT: 'W10'}
         {SPACE: 'B'}
         {<{ceil(lmt*b_rat)+1}}{form(par[6]+=(~par[12]),,,'9.')} {INCLUDE: p_osoba.rpi}
         {FONT: 'T8G'}
         {SPACE: 'C'}
         {REP: par[1].fhbar(lmt)}
         {FI}
         {IF: ~__RUB.sys_attr(par[3].RN,621,date())}
         {  {? lineleft()<2 || par[12]:=1 || par[12]:=0 ?};
            par[2].prefix(OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL, par[3].LP, par[3].RN);
            {! _ndx:=1..obj_len(par[8])
            |! {? par[2].find_key(_ndx)
               || par[8][_ndx]:=par[2].KW
               || par[8][_ndx]:=0
               ?}
            !};
            par[1].ini_line(); ~~
         }
         {REP: par[1].fline(lmt)}
         {FI}
      {OD}
      {IF: par[15]} {REP: par[1].flbar(lmt)} {FI}
      {IF: par[4] & ~par[12]}
         {PAGE}
      {FI}
      { par[12]:=0; ~~ }
   {FI}
{FI}
{OD}
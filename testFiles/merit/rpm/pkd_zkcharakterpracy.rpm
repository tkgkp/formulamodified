:!UTF-8
{TITLE:Zatrudnienie wg charakteru pracy}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PAR_WYDR.TITLE:='Zatrudnienie wg charakteru pracy'@;

   VAR_DEL.delete('par','prn1','vp','lmt','rsep','char_prac','tab');
   par:=obj_new(9);

   par[4]:=par[5]:=1;
   par[2]:=exec('charakterpracy','!pkd_zes_orze',PAR_WYDR.TITLE);
   par[7]:=0;
   'szerokości kolumn';
   par[8]:=obj_new(8);
   par[8][1]:=5; 'liczba porządkująca';
   par[8][2]:=70; 'oznaczenie jednostki org.';
   par[8][3]:=6; 'zatrudnieni w jednostce org.';
   par[8][4]:=10; 'wystąpienia charakteru w jednostce org.';
   par[8][5]:=1; 'bloki raportu';
   par[8][6]:=0; 'maksymalna szerokość';
   par[8][7]:=0; 'zliczanie pomocnicze';

   {? par[2].size()
   || {? par[2].SANI=1 || _chp:='CP.S'; char_prac:='Sposobu rozliczenia'@
      |? par[2].SANI=2 || _chp:='CP.R'; char_prac:='Rodzaju zatrudnienia'@
      || _chp:='CP.CP'; char_prac:='Charakteru pracy'@
      ?};

      _P:=exec('wybierz_parses','pracownik','PKD').P;

      tab:=sql('
         select distinct
         ROW_NUMBER() OVER (ORDER BY 2) as LICZBA, '+_chp+' CHARP
         from P join H using(P.REFERENCE,H.P) join UD_SKL using(H.WYDZIAL,UD_SKL.REFERENCE) join
              CP using(H.CP,CP.REFERENCE) join OSOBA
         where P.REFERENCE in (select :_a.SQL from :_a) and
               H.OD<=to_date(:_b) and (H.DO is null or to_date(:_b)<=H.DO)',
         _P,par[2].DATA);

      _val:={? par[2].RANI || '1' || 'H.RWY' ?};
      _lst:=sql('
         select UD_SKL.SYMBOL || \' \' || UD_SKL.OPIS WYDZIAL, '+_chp+' CHARP, '+_val+' LICZBA
         from P join H using(P.REFERENCE,H.P) join UD_SKL using(H.WYDZIAL,UD_SKL.REFERENCE) join
              CP using(H.CP,CP.REFERENCE) join OSOBA
         where P.REFERENCE in (select :_a.SQL from :_a) and
               H.OD<=to_date(:_b) and (H.DO is null or to_date(:_b)<=H.DO)',
         _P,par[2].DATA);
      {? type_of(_lst)=type_of(SYSLOG)
      || exec('object','#pivot');
         par[3]:=obj_new(@.CLASS.PIVOT,_lst,'CHARP','sum','LICZBA');
         par[3].grpfld('WYDZIAL').total(1).dosum(1).make()
      ?}
   ?};

   prn1:='par[1]';
   lmt:=rsep:=0;
   vp:=1
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','char_prac')
}
{FONT: 'T8G'}
{SPACE: 'C'}
{EXIT: ~par[2].size()}
{EXIT: ~par[3].valid()}
 { _cc:=par[3].fld_num();
   _cc-=(_cc%32);
   _bpc:=_lpc:=0;
   _wd:=par[8];
   'maksymalna szerokość';
   _wd[6]:=charleft();
   'wyliczenie liczby kolumn w pierwszym bloku wydruku';
   _fpc:=floor((_wd[6]-_wd[1]-_wd[2]-_wd[4]-10)/(_wd[4]+3));
   'czy wydruk powinien być podzielony na bloki?';
   '(liczba kolumn danych większa od wyliczonej)';
   {? _fpc<(_cc-3)
   || 'kolumny danych na kolejnych stronach';
      _bpc:=floor((_wd[6]-_wd[1]-3)/(_wd[4]+3));
      _lpc:=(_cc-_fpc-3)%*_bpc;
      _wd[5]+=(_cc-_fpc-_lpc-3)%_bpc;
      _wd[5]+=(_lpc<>0)
   || _fpc:=_cc-3
   ?};

   par[1]:={? ~rep_export() || obj_new(_wd[5]) || obj_new(@.CLASS.PRINT,1+tab.size()) ?};
   {? ~rep_export() || par[6]:=obj_new(_wd[5]) ?};

   _off:=0;
   {? ~rep_export()
   || {! _ii:=1.._wd[5]
      |! _bcc:={? _ii=_wd[5] & _ii>1 || {? _lpc || _lpc || _bpc ?}
               |? _ii=1 || _fpc
               || _bpc
               ?};
         par[1][_ii]:=obj_new(@.CLASS.PRINT,_bcc+{? _ii=1 || 2 ?}+1);
         par[1][_ii].add("$#par[3].OUT.ref",_wd[1],'Lp.'@,1,,,,,',,\'9.\'');
         {? _ii=1
         || par[1][_ii].add("par[3].OUT.WYDZIAL",_wd[2],'Jednostka organizacyjna'@);
            par[1][_ii].add("par[3].OUT.PQ_TOTAL",_wd[4],'Razem'@,1)
         ?};
         {! _ij:=_off+3.._off+_bcc+2 |! par[1][_ii].add($('par[3].fld_val('+$_ij+')'),_wd[4],par[3].fld_name(_ij),1) !};
         par[6][_ii]:=obj_new(@.CLASS.PRINT,_bcc+(_ii=1)+1);
         par[6][_ii].add("'Razem'@",_wd[1]+{? _ii=1 || _wd[2]+3 ?});
         {? _ii=1 || par[6][_ii].add("par[3].SUM.PQ_TOTAL",_wd[4],'',1) ?};
         {! _ij:=_off+2.._off+_bcc+1 |! par[6][_ii].add($('par[3].sfld_val('+$_ij+')'),_wd[4],'',1)!};
         par[6][_ii].s_frame();
         par[1][_ii].s_frame();
         _off+=_bcc
      !}
   || par[1].add("par[3].OUT.WYDZIAL",100,'Jednostka organizacyjna');
      {? tab.first()
      || {!
         |? _m:=tab.LICZBA+2;
            par[1].add($('par[3].fld_val('+$_m+')'),20,par[3].fld_name(_m),1,,,,,,',0,\'1,\'');
            tab.next()
         !}
      ?};
      par[1].s_frame()
   ?};
   par[7]:=1;

   {? par[3].OUT.first() || par[4]:=par[3].OUT.ref() ?}; ~~
}
{COMMENT}OLD: zk_pchar.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   { par[5]:=0; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Data kontroli:'@} {par[2].DATA}
   {<{lmt+1}'Rodzaj wydruku:'@} {{? par[4] || 'wykaz i suma'@ || 'tylko podsumowanie'@ ?}}
   {<{lmt+1}'Analiza według:'@} {char_prac}
{ELSE}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE: 5}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{IF: ~rep_export()}
{WHILE: (par[8][7]+=1)<=par[8][5]}{DO}{
      prn1:='par[1]['+$par[8][7]+']';
      lmt:=int({? par[8][5]=1 | (par[8][7]>1 & par[8][7]<par[8][5]) || int((charleft()-($prn1)().width())*0.5)
               |? par[8][7]=1 || charleft()-($prn1)().width()
               ?}); ~~
   }
   {IF: par[3].OUT.seek(par[4])}
{SUBSTITUTE: 'LINIA0'}
   {FI}
   {WHILE: (par[8][8]:=par[3].OUT.next()) & lineleft()<>1}{DO}
{SUBSTITUTE: 'LINIA0'}
   {OD}
   {IF: lineleft()=1}{
         {? par[8][8] & par[8][7]=par[8][5]
         || par[4]:=par[3].OUT.ref();
            par[8][7]:=0
         ?}; ~~
      }
      {PAGE}
   {FI}
   {IF: ~par[8][8]}
      { prn1:='par[6]['+$par[8][7]+']'; ~~ }
      {REP: '{<{lmt+1}}'+par[1][par[8][7]].jbar(par[6][par[8][7]])}
{SUBSTITUTE: 'LINIA0'}
      {PAGE}
   {FI}
{OD}
{ELSE}
{IF: par[3].OUT.seek(par[4])}
{SUBSTITUTE: 'LINIA0'}
   {FI}
   {WHILE: (par[8][8]:=par[3].OUT.next()) & lineleft()<>1}{DO}
{SUBSTITUTE: 'LINIA0'}
   {OD}
   {IF: lineleft()=1}{
         {? par[8][8] & par[8][7]=par[8][5]
         || par[4]:=par[3].OUT.ref();
            par[8][7]:=0
         ?}; ~~
      }
      {PAGE}
   {FI}
   {IF: ~rep_export() & ~par[8][8]}
      { prn1:='par[6]['+$par[8][7]+']'; ~~ }
      {REP: '{<{lmt+1}}'+par[1][par[8][7]].jbar(par[6][par[8][7]])}
{SUBSTITUTE: 'LINIA0'}
      {PAGE}
   {FI}
{FI}
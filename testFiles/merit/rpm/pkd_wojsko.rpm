:!UTF-8
{TITLE:Dane wojskowe}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat');
   tresc:='pkd_wojsko';
   PAR_WYDR.TITLE:='Dane wojskowe'@;
   par:=obj_new(5);
   par[5]:=0;
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','danych wojskowych'@,'PKD_DOS_PPWO','PKD_DOS_PRWO');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || par[5]:=1;
      par[1]:=obj_new(@.CLASS.PRINT,8);
      {? ~rep_export() || par[1].add("par[3]+=1",5,'Lp.'@,1,,,,,',,\'9.\'') ?};
      par[1].add("P.OSOBA().NAZWISKO",20,'Nazwisko'@);
      par[1].add("OSOBA.PIERWSZE",20,'Imię'@);
      par[1].add("WOJ.KW",13,'Nr książeczki'@);
      par[1].add("WOJ.SW().NAZWA",23,'Stopień wojskowy'@);
      par[1].add("WOJ.SZ",23,'Sztab'@);
      par[1].add("WOJ.SP",23,'Specjalizacja'@);
      par[1].add("WOJ.MOB",23,'Przydział'@);
      par[1].s_frame();

      par[2]:=tab_tmp(,'REF','INTEGER','Osoba');

      prn1:='par[1]';
      lmt:=par[3]:=0;
      vp:=rsep:=par[4]:=h_rat:=1;
      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat')
}
{COMMENT}OLD: wkredyt.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[5]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {FOR: 'WOJ'}{INDEX: '_WOJSKO'}{PREFIX: P.OSOBA}{DO}
   {FIRST}{ par[2].REF:=#P.OSOBA; par[2].add(); ~~ }{FIRST-}
      {SUBSTITUTE: 'LINIA0'}
   {OD}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
:!UTF-8
{TITLE:Przekroczenie okresów chorobowych i zasiłkowych}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','dni','rsep','lmt','vp','tresc','prn1','pyt','hrat','brat','OK','Lp','wydztmp','Jed');
   VAR_DEL.delete('__edit','__param');
   tresc:='pzschor';
   PAR_WYDR.TITLE:='Przekroczenie okresów chorobowych i zasiłkowych'@;
   par:=obj_new(11);
   dni:=obj_new(11);
   par[9]:=obj_new(3);
   par[11]:=par[9][1]:=par[9][2]:=par[9][3]:=0;
   dni[5]:=',';
   P.cntx_psh();
   N.cntx_psh();
   N.index('NIEOBECN');

   dni[10]:=
      "  {? N.DO<dni[3] || dni[3]:=N.DO ?};
         dni[1]:=dni[3]-N.OD+1;
         {? _a=1 || dni[7]+=dni[1]
         |? _a=2 || dni[6]+=dni[1]
         ?}
      ";
   dni[11]:=
      "  {? _a=1 || dni[7]+=N.NK
         |? _a=2 || dni[6]+=N.NK
         ?};
         {? par[6]=0 || par[6]:=1 ?};
         1
      ";
   _upr:=exec('uprawnieniawrap','pkd','Nieobecnościach'@,'PKD_EZK_ORNN','PKD_EZK_OPNN');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'OKR','INTEGER','Wybór typu zestawienia',
         'OD','DATE','Data kontroli'
      );
      __param.OKR:=0;
      __param.OD:=date();
      __param.add();
      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pzschor');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'OKR',,,,,,
         'Wybór typu zestawienia'@,,,
         'radio-buttons',,
         'wg Jednostek organizacyjnych'@,"1",
         'Całościowy'@,"0"
      );
      __param.win_efld(__edit,,'OD',,,11,,,'Data kontroli'@);
      __param.efld_opt(__edit,'mark=1',,'OD');
      exec('ok_esc','#window',__param,__edit);
      __param.fld_fml('OD','AFTER_EDIT',
         "  {? __param.OD=#0
            || FUN.emsg('Data kontroli musi być wypełniona.'@); 0
            || 1
            ?}
         "
      );
      __param.win_edit(__edit);
      {? par[11]:=__param.edit(
            "  {? __param.OD~1<=1900
               || FUN.emsg('Błędna wartość parametru daty kontroli.'@); 0
               || par[2]:=__param.OD; 1
               ?}
            "
         )
      || pyt:=__param.OKR;
         {? pyt:=__param.OKR=1
         || wydztmp:=tab_tmp(,'WYDZ','STRING[16]','WYDZ','P','INTEGER','P');
            Jed:=''
         ?};

         par[1]:=obj_new(@.CLASS.PRINT,4);
         {? ~rep_export() || par[1].add("{? par[3] || Lp+=1 || '' ?}",5,'Lp.'@,1,,,,,',,\'99\'') ?};
         par[1].add(
            "  {? par[3]
               || {? pyt=1 || par[3]:=0 ?};
                  P.OSOBA().NAZWISKO+' '+P.OSOBA().PIERWSZE
               || ''
               ?}
            ",50,'Nazwisko i Imię'@
         );
         {? pyt=0
         || par[1].add("{? par[3] || par[3]:=0; P.WYDZIAL().SYMBOL || '' ?}",25,'Jednostka organizacyjna'@,0)
         ?};
         par[1].add(
            "  {? par[9][1]=1 || 'Choroba - '@+$dni[6]+' dni'@ || '' ?}+
               {? par[9][2]=1 || {? par[9][1]=1 || ', ' || '' ?}+'Zasiłek - '@+$dni[7]+' dni '@ || '' ?}+
               {? par[9][3]=1 || {? par[9][1]=1 | par[9][2]=1 || ', ' || '' ?}+'Opieka - '@+$dni[8]+' dni'@ || '' ?}
            ",43,'Opis'@
         );
         par[1].s_frame();

         prn1:='par[1]';
         par[10]:=0; 'dla osob po 50 roku zycia 14 dni dla pozostalych 33';
         OK:=Lp:=par[4]:=lmt:=rsep:=0;
         par[3]:=par[5]:=par[6]:=b_rat:=h_rat:=vp:=1;
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','dni','rsep','lmt','vp','tresc','prn1','pyt','hrat','brat','OK','Lp','wydztmp','Jed');
   VAR_DEL.delete('__edit','__param');
   P.cntx_pop();
   N.cntx_pop()
}
{EXIT: KST_CHOR.clear();
      ~par[11]
      |
      {?  KST_CHOR.first()
      || {!
         |? dni[5]+=$KST_CHOR.WYN().RN+',';
            KST_CHOR.next()
         !}; 0
      || FUN.emsg('Brak informacji o sposobie rozpisania nieobecności. '@+
         '\n (uzupełnij dane w tabeli \'Choroby wynagradzane\')'@); 1
      ?}
}
{COMMENT}OLD: pzschor.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G L O W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Data kontroli: '@} {par[2]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: pyt=1 }
{<{lmt+1}}Jednostka org. - {P.WYDZIAL().SYMBOL}{IF: +(|P.WYDZIAL().OPIS)} ({P.WYDZIAL().OPIS}){FI}
{FI}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════
   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'dni'}
{  N.prefix('N',P.ref());
   par[10]:=
      {? P.OSOBA().UR_DATA>#0 & (par[2]~1)-(P.OSOBA().UR_DATA~1)>=51
      || 14
      || KST_PAR.CHOROBA
      ?};

   par[6]:=0;
   {? N.find_le(par[2]) & N.ROK=par[2]~1
   || dni[1]:=dni[6]:=dni[7]:=dni[8]:=0;
      dni[3]:=par[2];
      _dni_nieo:=obj_new(4);
      _data_cia:=exec('ciaglosc','nieobecnosc',N.OD,_dni_nieo,1,1);
      {? __RUB.sys_attr(N.NB,195,N.OD)
      || {? dni[5]*(','+$N.NB().RN+',') & N.OD~1=par[2]~1
         || dni[10](2)
         |? __RUB.sys_attr(N.NB,1225,N.OD) & N.OD~1=par[2]~1
         || {? N.DO<dni[3] || dni[3]:=N.DO ?};
            dni[1]:=dni[3]-N.OD+1;
            dni[8]+=dni[1]
         ?}
      ?};
      {!
      |? {? N.prev & (N.DO~1=par[2]~1 | N.OD~1=par[2]~1)
         || {? dni[5]*(','+$N.NB().RN+',') & N.OD~1=par[2]~1
            || dni[11](2)
            |? __RUB.sys_attr(N.NB,1225,N.OD) & N.OD~1=par[2]~1
            || dni[8]+=N.NK; 1
            || 1
            ?}
         ?}
      !};
      dni[7]:=_dni_nieo[1];
      obj_del(_dni_nieo);
      {? dni[6]>=par[10]
      || par[9][1]:=par[6]:=1
      || par[9][1]:=par[6]:=0
      ?};
      {? dni[7]>=KST_PAR.WALORYZ
      || par[9][2]:=1;
         {? par[6]=0 || par[6]:=1 ?}
      || par[9][2]:=0
      ?};
      {? dni[8]>=60
      || par[9][3]:=1;
         {? par[6]=0 || par[6]:=1 ?}
      || par[9][3]:=0
      ?}
   ?}; ~~
}
{MACRO-}
{MACRO: 'druk_dni'}
{ENTIRE}
   {REP: par[1].fhbar(lmt)}
   {  par[1].ini_line(); ~~ }
   {REP: par[1].fline(lmt)}
   {WHILE: par[1].next()}
   {DO}
     {REP: par[1].fline(lmt)}
   {OD}
   {REP: par[1].flbar(lmt)}
{ENTIRE-}
{MACRO-}
{MACRO: tresc}
{FONT: 'T8'}
{SPACE: 'B'}
   {  par[4]:=0;
      par[3]:=1; ~~
   }
   {IF: lineleft() & lineleft()<=4}
      {IF: lineleft()}
         {PAGE}
      {FI}
   {FI}
   {SUBSTITUTE: 'dni'}
   {IF: par[6]=1}
   {SUBSTITUTE: 'druk_dni'}
   {FI}
{MACRO-}
{MACRO: 'wydzial'}
{  _practmp:=sql(
      'select distinct N.P '
      'from P join N using(P.REFERENCE,N.P) join R '+
      'where P.REFERENCE in (select :_a.SQL from :_a) and N.KOR=\'N\' and N.ROK=:_b and R.RN in (:_c)',
       params_get().P,par[2]~1,__RUB.sys_sql(195,par[2])
   );
   {? _practmp.first()
     || {!
        |? {? P.seek(_practmp.P,,1)
           || wydztmp.WYDZ:=P.WYDZIAL().SYMBOL;
              wydztmp.P:=#P.ref();
              wydztmp.add()
           ?};
           _practmp.next()
        !}
   ?}; ~~
}
{MACRO-}
{MACRO: 'wydz'}
{FOR: wydztmp}
{DO}
   {IF: P.seek(wydztmp.P,P.name(),1)}
      {SUBSTITUTE: 'dni'}
      {IF: par[6]=1}
      {IF: ~OK}
         { Jed:=wydztmp.WYDZ;~~}
      {FI}
      {IF: wydztmp.WYDZ<>Jed}
         {PAGE}
         {  Jed:=wydztmp.WYDZ;
            Lp:=0; ~~
         }
      {FI}
      {  par[4]:=0;
         par[3]:=1; ~~
      }
      {IF: lineleft&lineleft<=4}
         {PAGE}
      {FI}
      {SUBSTITUTE: 'druk_dni'}
      { OK:=1;~~}
      {FI}
   {FI}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O S C   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2);~~}
{IF: pyt=0}
   {FOR: params_get().P}{DO}
      {IF: P.seek(params_get().P.SQL,,1)}
         {SUBSTITUTE: tresc}
      {FI}
   {OD}
{ELSE}
   {SUBSTITUTE: 'wydzial'}
   {SUBSTITUTE: 'wydz'}
{FI}
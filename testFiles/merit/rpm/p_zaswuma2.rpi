:!UTF-8
{COMMENT}OLD: zaswuma2.rpi{COMMENT-}
{  _wym:="{? _b=1 || 'pełen etat' || '%1/%2 etatu' [$_a,$_b] ?}";
   _pla:="
      _odp:=obj_new('S1','S2','S3','S4','PAT','PAW');
      _um:=1+-H.CP().S='u';
      _waluta:={? H.CZYWAL='T' || H.WAL().KOD+'' || 'zł' ?};
      _waluta2:={? H.CZYWAL2='T' || H.WAL().KOD+'' || 'zł' ?};
      _waluta3:={? H.CZYWAL3='T' || H.WAL().KOD+'' || 'zł' ?};
      _waluta4:={? H.CZYWAL4='T' || H.WAL().KOD+'' || 'zł' ?};
      _prec:={? H.S1=int(H.S1) & H.S2=int(H.S2) & H.S3=int(H.S3) & H.S4=int(H.S4) & H.PAW=int(H.PAW) || 0 || 2 ?};
      _odp.S1:=
         {? H.S1
         || {? _um || 'płaca zasadnicza' || 'stawka godzinowa' ?}+' '+form(H.S1,,_prec)+' '+_waluta
         || ''
         ?};
      _odp.S2:=
         {? H.S2
         || {? _um || 'dodatek funkcyjny' || 'dodatek brygadzistowski' ?}+' '+
            {? H.S2T='P'
            || 'wyrażony w %1, %2' [_waluta2,form(H.S2,,_prec)]+'% '+H.S2P().NAZWA
            || form(H.S2,,_prec)+' '+_waluta2
            ?}
         || ''
         ?};
      _odp.S3:=
         {? H.S3
         || {? H.S3T='P'
            || 'dodatek wyrażony w %1, %2' [_waluta3,form(H.S3,,_prec)]+'% '+H.S3P().NAZWA
            || 'dodatek %1 %2' [form(H.S3,,_prec),_waluta3]
            ?}
         || ''
         ?};
      _odp.S4:=
         {? H.S4
         || {? _um || 'płaca oddelegowanie' || 'stawka godzinowa płacy związanej z oddelegowaniem' ?}+
            ' '+form(H.S4,,_prec)+' '+_waluta4
         || ''
         ?};
      _odp.PAT:=H.PAT;
      _odp.PAW:={? H.PAW=0 || '' || form(H.PAW,,_prec)+{? H.PAT='K' || ' zł' |? H.PAT='P' || '%' || '' ?} ?};
      _odp";
   aneks.OZA:=H.P().OSOBA().PIERWSZE+{? +OSOBA.DRUGIE || ' '+OSOBA.DRUGIE || '' ?}+' '+OSOBA.NAZWISKO;
   aneks.OZA+=', zamieszkał'+{? OSOBA.PLEC='K' || 'a' || 'y' ?}+' ';
   aneks.OZA+={? H.OS_ADRES<>null()
              || exec('adres','osoba',H.OS_ADRES().RODZAJ,H.OS_ADRES().OD)
              || _adres:=exec('adres','osoba','C',H.OD);
                 {? _adres='' || _adres:=exec('adres','osoba','S',H.OD) ?};
                 _adres
              ?};
   aneks.PESEL:=OSOBA.PESEL;
   aneks.STN:=H.ST().ST;
   wydtmp:=H.MIEJSCE;
   aneks.WYM:=_wym(H.RWYL,H.RWYM);
   _hsxa:=_pla();
   aneks.SW1:=_hsxa.S1;
   aneks.SW2:=_hsxa.S2;
   aneks.SW3:=_hsxa.S3;
   aneks.SW4:='';
   aneks.SW5:=_hsxa.S4;
   aneks.PAT:=_hsxa.PAT;
   aneks.PAW:=_hsxa.PAW;
   STR.split(gsub(H.memo_txt(,1,'INNE'),'\n',' '));
   aneks.IW1:=STR.line(255);
   aneks.IW2:=STR.line(255);
   aneks.IW3:=STR.line(255);
   aneks.IW4:=STR.line(255);

   aneks.PPSF_OD:=aneks.PPSF_DO:=#0;
   aneks.PPSF_A:=aneks.PPSF_R:=aneks.PPSF_I:='';
   {? __ppsf.czyPPSF
   || params_set(_par:=params_get());
      __ppsf.obj.PPSF_ADR:=_par.par.H.PPSF_ADR;
      __ppsf.obj.PPSF_H:=_par.par.H.PPSF_H;
      _tt:=spli_str(_par.par.H.CFG);
      __ppsf.obj.jestH:=#_tt[1];
      __ppsf.obj.jestA:=#_tt[2];
      __ppsf.obj.brakH:=#_tt[3];
      __ppsf.obj.brakA:=#_tt[4];
      __ppsf.obj.drukH:=#_tt[5];
      __ppsf.obj.drukA:=#_tt[6];
      PPSF_ADR.cntx_psh();
      PPSF_H.cntx_psh();
      _adr:=spli_str(__ppsf.obj.PPSF_ADR,';');
      {? __ppsf.obj.jestA
      || {! _licz:=1..obj_len(_adr) |!
            {? PPSF_ADR.seek(_adr[_licz],,1)
            || aneks.PPSF_A+=exec('opis_warp','ppsf','adres')+'; '
            ?}
         !};
         aneks.PPSF_A-=2
      |? __ppsf.obj.brakA
      || aneks.PPSF_A:='brak'
      ?};
      {? __ppsf.obj.jestH & PPSF_H.seek(__ppsf.obj.PPSF_H,,1)
      || aneks.PPSF_OD:=PPSF_H.OD;
         aneks.PPSF_DO:=PPSF_H.DO;
         _co:=obj_new(3); _na:=obj_new(obj_len(_co));
         _co[1]:='\n'; _na[1]:=' ';
         _co[2]:='\t'; _na[2]:=' ';
         _co[3]:='"'; _na[3]:='\'';
         {? PPSF_H.PPSFROZD
         || PPSF_H.PPSFROZD();
            aneks.PPSF_R:=gsub(PPSFROZD.memo_txt(,1,'TEKST'),_co,_na)
         ?};
         aneks.PPSF_I:=gsub(PPSF_H.memo_txt(,1,'OPIS'),_co,_na)
      |? __ppsf.obj.brakH & PPSF_H.seek(__ppsf.obj.PPSF_H,,1)
      || aneks.PPSF_OD:=PPSF_H.OD;
         aneks.PPSF_DO:=PPSF_H.DO
      ?};
      {? ~+|aneks.PPSF_R || aneks.PPSF_R:='brak' ?};
      {? ~+|aneks.PPSF_I || aneks.PPSF_I:='brak' ?}
   ?};

   H.cntx_psh();
   {? ~H.prev() & {? __ppsf.czyPPSF || ~(__ppsf.obj.drukH+__ppsf.obj.drukA) || 1 ?}
   || H.cntx_pop();
      {? __ppsf.czyPPSF || PPSF_ADR.cntx_pop(); PPSF_H.cntx_pop() ?};
      return()
   ?};
   {? aneks.STN=H.ST().ST || aneks.STN:='' ?};
   {? wydtmp=H.MIEJSCE || aneks.WYD:='' || aneks.WYD:=wydtmp ?};
   {? aneks.WYM=_wym(H.RWYL,H.RWYM) || aneks.WYM:='' ?};
   _hsxb:=_pla();
   {? aneks.SW1=_hsxb.S1 & aneks.SW2=_hsxb.S2 & aneks.SW3=_hsxb.S3 & aneks.SW5=_hsxb.S4
   || aneks.SW1:=aneks.SW2:=aneks.SW3:=aneks.SW5:=aneks.SW4:=''
   ?};
   {? aneks.PAT=_hsxb.PAT & aneks.PAW=_hsxb.PAW
   || aneks.PAT:=aneks.PAW:=''
   ?};
   H.cntx_pop();
   {? __ppsf.czyPPSF || PPSF_ADR.cntx_pop(); PPSF_H.cntx_pop() ?};

   indent:=1;
   cnt+=1;
   ppkt:='';
   {? ~(exec('rep_set','rap_zew','PXX_ZASWUMAN')='T') || par.ZAL:='' ?};

   {? +aneks.STN || ppkt:=' pkt.1 ppkt. 1)' ?};
   {? +aneks.WYD || ppkt+={? ppkt<>'' || ', 2)' || ' pkt.1 ppkt. 2)' ?} ?};
   {? +aneks.WYM || ppkt+={? ppkt<>'' || ', 3)' || ' pkt.1 ppkt. 3)' ?} ?};
   {? +(|aneks.SW1+|aneks.SW2+|aneks.SW3+|aneks.SW4+|aneks.SW5)
   || ppkt+={? ppkt<>'' || ', 4)' || ' pkt.1 ppkt. 4)' ?}
   ?};
   {? __ppsf.czyPPSF & __ppsf.obj.drukH+__ppsf.obj.drukA || ppkt+={? ppkt<>'' || ', 5)' || ' pkt.1 ppkt. 5)' ?} ?};
   {? +ppkt || ppkt+=' zmieniają treść na następującą' ?};

   {? H.DOP_NADG | +(|aneks.IW1+|aneks.IW2+|aneks.IW3+|aneks.IW4)
   || ppkt+={? ppkt<>'' || ' oraz zmianie ulega pkt.3' || '' ?}
   ?};
   ppkt+=':';

   aneks.KW_SL1:={? H.S1<>0 & H.CZYWAL='N' || STR.kwota_sł(H.S1) || '' ?};
   aneks.KW_SL2:={? H.S2<>0 || {? H.S2T='P' | H.CZYWAL2='T' || '' || STR.kwota_sł(H.S2) ?} || '' ?};
   aneks.KW_SL3:={? H.S3<>0 || {? H.S3T='P' | H.CZYWAL3='T' || '' || STR.kwota_sł(H.S3) ?} || '' ?};
   aneks.KW_SL4:={? H.S4<>0 & H.CZYWAL4='N' || STR.kwota_sł(H.S4) || '' ?};
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'H_REF',$H.ref(),
      'F_NAZWA',KST.NAZWA,
      'MIEJSCE',PAR_WYDR.MIEJSC,
      'DATA',H.PRZYG$4,
      'F_ULICA',exec('ulica','stalesys'),
      'F_POCZTA',exec('poczta','stalesys'),
      'F_REGON',KST.REG,
      'F_NIP',KST.NIP,
      'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?},
      'DATA_ZAW',{? H.PODPIS<>#0 || H.PODPIS$6+' r.' || '' ?},
      'DATA_UM',H.PRZYG$4+' r.',
      'NR_UM',form(H.UMOWA().NUMER));
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'F_OSREP',H.PRZEDST,
      'OSOBA',aneks.OZA,
      'PESEL',aneks.PESEL,
      'PPKT',ppkt,
      'STANOW',aneks.STN,
      'WYDZIAL',aneks.WYD,
      'WYMIAR',aneks.WYM,
      'INNEWAR',|aneks.IW1+' '+|aneks.IW2+' '+|aneks.IW3+' '+|aneks.IW4,
      'GODZNADL',{? H.DOP_NADG || form(H.DOP_NADG,,2,' ,') || '' ?},
      'ROZPRAC',{? H.OD<>#0 || H.OD$4+' r.' || '' ?},
      'ZAKPRAC',{? H.DO<>#0 || H.DO$4+' r.' || '' ?},
      'CZY_ZAP',par.ZAL);
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'SW1',aneks.SW1,
      'SW2',aneks.SW2,
      'SW3',aneks.SW3,
      'SW4',aneks.SW4,
      'SW5',aneks.SW5,
      'PAT',aneks.PAT,
      'PAW',aneks.PAW,
      'KW_SL1',aneks.KW_SL1,
      'KW_SL2',aneks.KW_SL2,
      'KW_SL3',aneks.KW_SL3,
      'KW_SL4',aneks.KW_SL4);
   exec('ins_par','rap_zew',__xr_ins,cnt,'PRACA_ZD',$__xr_ins);
   exec('ins_par','rap_zew',__xr_ins,cnt,'PRACA_ZW',$cnt);
   _krecha:={? czy_szablon || '- ' || '' ?};
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'PRACA_OD', {? __ppsf.czyPPSF || {? aneks.PPSF_OD<>#0 || aneks.PPSF_OD$6 || '' ?} || '' ?},
      'PRACA_DO', {? __ppsf.czyPPSF || {? aneks.PPSF_DO<>#0 || aneks.PPSF_DO$6 || '' ?} || '' ?},
      'PR_ZD_2A', {? __ppsf.czyPPSF & __ppsf.obj.drukA
                  || '%1adres wykonywania pracy zdalnej: %2'[_krecha,form(aneks.PPSF_A)]
                  || ''
                  ?},
      'PR_ZD_3R', {? __ppsf.czyPPSF & __ppsf.obj.jestH
                  || '%1tryb pracy zdalnej: %2'[_krecha,aneks.PPSF_R]
                  || ''
                  ?},
      'PR_ZD_4I', {? __ppsf.czyPPSF & __ppsf.obj.jestH
                  || '%1inne postanowienia dotyczące pracy zdalnej: %2'[_krecha,aneks.PPSF_I]
                  || ''
                  ?}
   );
   ~~
}
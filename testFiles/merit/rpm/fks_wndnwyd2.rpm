:!UTF-8
{TITLE:A. Wydruk analizy należności i zobowiązań - pełny}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
          PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN: w:=obj_new(13); ww:=obj_new(13); i:=0; dru_sum:=0;
        PRN:=obj_new(@.CLASS.PRINT,3); PRN.sl_frame();
        PRN1:=obj_new(@.CLASS.PRINT,20); PRN1.sl_frame();
        PRN2:=obj_new(@.CLASS.PRINT,20); PRN2.sl_frame();
        {! _i:=1..13 |! w[_i]:=''; ww[_i]:=0 !};
        {? WIND_N.PRZED_TP<>0 || KA.O1:=form('< '+$WIND_N.PRZED_TP+' przed TP',-15); _i:=1 || _i:=0 ?};
        KA.O2:=KA.O3:=KA.O4:=''; prz:=prz1:='';
        max_prz:=exec('ile_prz','rozrach',1);
        {! _j:=1+_i..max_prz+1
        |! prz:=($('WIND_N.PRZ'+$({? _j>=max_prz+1-_i || max_prz-_i || _j-_i ?})))();
           {? _j<>max_prz+1
           || prz1:={? (WIND_N.PRZED_TP<>0 & _j>2) | (WIND_N.PRZED_TP=0 & _j>1)
                    || ($('WIND_N.PRZ'+$({? _j>=max_prz+1-_i & max_prz<>1 || max_prz-_i-1 || _j-_i-1 ?})))()
                    || WIND_N.PO_TP
                    ?}
           ?};
           ($('KA.O'+{?_j<5 || $(_j)+':=' || $(_j-4)+'+='?}+
           'form('+
           {? _j=max_prz+1
           || '''od ''+$prz'
           || ''''+'od '+$prz1+''''+'+'+''' do '''+'+$'+$prz
           ?}+
           '+'+''''+'#'+''''+
           ',-15)'))()
        !};
        {! _j:=1..4 |!
          {?($('KA.O'+$_j))()+1='#' || ($('KA.O'+$_j))():=($('KA.O'+$_j))()-1 ?}
        !};
        ile_kol:=0;
        color:=''; prn1:=prn2:='PRN1';
        lm:=ll:=lmt:=rsep:=0; vp:=1; rsep:=PAR_WYDR.RSEP; PAR_WYDR.RSEP:=1; fun:='';
        WIND_KH.cntx_psh()
      }
{END:  obj_del(w); obj_del(ww); obj_del(PRN); obj_del(PRN1); obj_del(PRN2); &i; &ile_kol;
       &color; &prn1; &prn2; &prz; &prz1; &dru_sum;
       &lm; &ll; &vp; &lmt; PAR_WYDR.RSEP:=rsep; &rsep; &fun; WIND_KH.cntx_pop
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: wndnwyd2.rpm
Utworzony:   2003-02-20 [AMK] Artur Makos
==================================================
Zawartosc:  Wydruk analizy naleznosci i zobowiazan - pelny
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{  PRN.add("",57,'Kontrahent'@); ~~}
{  PRN.add("",51,'Płatności'@); ~~}
{  PRN1.add("w[1]",55,'Nazwa'@); ~~}
{  PRN1.add("w[3]",1,'B'); ~~}
{  PRN1.add("w[4]",6,'Śr. po term.'@,1); ~~}
{  PRN1.add("w[5]",8,'Przez ost. dni'@,1); ~~}
{  PRN1.add("w[6]",15,'Zapłacono'@,1); ~~}
{  PRN1.add("w[7]",10,'Ostatnia platność'@); ~~}
{  PRN1.add("w[8]",8,'Sp. pł.'@); ~~}
{IF: KA.O1<>''}{ PRN1.add("w[10]",15,KA.O1,1,'#'); ile_kol+=1; ~~}{FI}
{IF: KA.O2<>''}{ PRN1.add("w[11]",15,KA.O2,1,'#'); ile_kol+=1; ~~}{FI}
{IF: KA.O3<>''}{ PRN1.add("w[12]",15,KA.O3,1,'#'); ile_kol+=1; ~~}{FI}
{IF: KA.O4<>''}{ PRN1.add("w[13]",15,KA.O4,1,'#'); ile_kol+=1; ~~}{FI}
{  PRN2.add("w[1]",109,'');~~}
{IF: KA.O1<>''}{ PRN2.add("w[10]",15,KA.O1,1,'#'); ~~}{FI}
{IF: KA.O2<>''}{ PRN2.add("w[11]",15,KA.O2,1,'#'); ~~}{FI}
{IF: KA.O3<>''}{ PRN2.add("w[12]",15,KA.O3,1,'#'); ~~}{FI}
{IF: KA.O4<>''}{ PRN2.add("w[13]",15,KA.O4,1,'#'); ~~}{FI}
{ PRN1.add("w[9]",15,'Przed terminem#Ogółem'@,1,'#'); ile_kol+=1; ~~}
{ PRN1.add("w[2]",15,'Odsetki#zarachowane#Do naliczenia'@,1,'#'); ile_kol+=1; ~~}
{ PRN2.add("w[9]",15,'',1,'#'); ~~}
{ PRN2.add("w[2]",15,'',1,'#'); ~~}
{  PRN.add("",ile_kol*15+ile_kol-1,'Kwoty');~~}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{ PRN.setcolor('255:255:255');~~}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}{ vp:=0; PRN.setcolor(color); ~~}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{ PRN1.setcolor('255:255:255');~~}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}{ vp:=0; PRN1.setcolor(color); ~~}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA2------------------{COMMENT-}
{MACRO: 'LINIA2'}
{ PRN2.setcolor('255:255:255');~~}
{PRN2.ini_line()}
{WHILE: PRN2.next()}
{DO}
{REP: PRN2.fline()}
{OD}{ vp:=0; PRN2.setcolor(color); ~~}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}{FONT: 'HEAD'}{SPACE: 'B'}
{LINE: 1}
{PAR_WYDR.TITLE:=
                 {? WIND_N.TYP='N' || 'WYDRUK ANALIZY NALEŻNOŚCI NA DZIEŃ %1'@[WIND_N.DATA$1] || 'WYDRUK ANALIZY ZOBOWIĄZAŃ NA DZIEŃ %1'@[WIND_N.DATA$1] ?};~~}
{SUBSTITUTE: 'HEAD'}{FONT: 'W8'}{SPACE: 'C'}
{IF: page=1}
{<3 'Paramet %1 - %2'@[WIND_N.WAL().TR,WIND_N.WAL().KOD]}{<60
 {? WIND_N.ZAK_KON='M' || 'Maska kont: %1'@[WIND_N.KON_MAS] || 'Prefix kont: %1'@[WIND_N.KON_PRE]?}}{<115
{? WIND_N.Z='T' ||  'Generować pozycje rozr. po terminie: tak'@ ||  'Generować pozycje rozr. po terminie: nie'@ ?}}
{<6 {? WIND_N.ODD=0 || 'Jednostka księgowa: wszystkie jednostki księgowe'@ || 'Jednostka księgowa: %1'@[WIND_N.ODD().OD] ?}}{<60
{?WIND_N.ZAK_ROZ='T' ||  'Rozrachunki przed terminem poniżej %1 dni'@[$WIND_N.PRZED_TP] ||   'Rozrachunki przed terminem powyżej %1 dni'@[$WIND_N.PO_TP] ?}}
{<6 'Operator: %1'@[WIND_N.USER]}{<60'Ostatnie płatności %1 dni wstecz'@[$WIND_N.OST_PL]}{<115
{? WIND_N.OZNACZ='T' ||  'Tylko oznaczone do monitowania: tak'@ ||  'Tylko oznaczone do monitowania: nie'@ ?}}
{<6 {? -WIND_N.ZAK_KH='b' || 'Kontrahenci: wybrani'@ || 'Kontrahenci: wszyscy'@ ?}}{<60
 'Kontrahenci powyżej wartości: %1'@[form(WIND_N.OD_KW,,2,' ,')]}
{<6 'Dla granicznych wartości przedziału zakres \'od\' nie należy do przedziału, zakres \'do\' należy'@}
{<6 'Opis: '+WIND_N.OPIS}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{ PRN1.setcolor(color); PRN.setcolor(color);~~}
{REP: PRN.fhbar(lmt)}
{PRN.ini_head()}
{WHILE: PRN.h_next()}{DO}
{REP: PRN.h_fmline(lmt)}{OD}
{REP: PRN.fjbar(PRN1)}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}{DO}
{REP: PRN1.h_fmline(lmt)}{OD}{ vp:=1;~~}
{HEADER-}
{FOOTER:0}
{IF: ~dru_sum}{IF: vp}{REP: PRN1.lbar()}{ELSE}{PRN1.lbar()}{FI}{ELSE}{PRN2.lbar()}{FI}{ vp:=1;~~}

{FOOTER-}
{COMMENT}-----------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ _m:=$(#WIND_N.ref()); _m:=('000'+_m)+4; WIND_KH.use('wndk'+_m);
  WIND_KH.index({? WIND_N.KHKODSKR=0 || 'SKROT'@ || 'KOD'@ ?}); i:=WIND_KH.first();~~}
{WHILE: i}
{DO}
{ {? lineleft()<3 || vp:=1 || vp:=0 ?}; ~~}
{ENTIRE}
{IF: vp}{ PRN1.setcolor(color);~~}{REP: PRN1.fbar()}{ELSE}{PRN1.bar()}{FI}
{ WIND_KH.KH();
  w[1]:={? WIND_N.KHKODSKR=0 || KH.SKR || KH.KOD ?}+{? +KH.NAZ || ' - '+KH.NAZ ?};
  w[3]:=__War_f('KH_DOD','B',WIND_KH.KH);
  w[4]:={? WIND_KH.SR_CZAS<>-1000000 || $WIND_KH.SR_CZAS || '' ?};
  w[5]:=$WIND_N.OST_PL; w[6]:=form(WIND_KH.OST_PL,,2,' ,'); w[7]:=WIND_KH.OST_DATA$1; w[8]:=WIND_KH.KH().PLATNOSC().KOD;
  w[10]:=form(WIND_KH.PRZ1,,2,' ,');  ww[2]+=WIND_KH.PRZ1$2;
  w[11]:=form(WIND_KH.PRZ2,,2,' ,');  ww[3]+=WIND_KH.PRZ2$2;
  w[12]:={? max_prz>1 || ww[4]+=WIND_KH.PRZ3$2; form(WIND_KH.PRZ3,,2,' ,') || '' ?};
  w[13]:={? max_prz>2 || ww[5]+=WIND_KH.PRZ4$2; form(WIND_KH.PRZ4,,2,' ,') || '' ?};
  w[9]:=form(WIND_KH.PRZEDTER,,2,' ,');  ww[1]+=WIND_KH.PRZEDTER$2;
  w[2]:=form(WIND_KH.ODSNALWC,,2,' ,');  ww[11]+=WIND_KH.ODSNALWC$2;
  ~~}
{SUBSTITUTE: 'LINIA1'}
{ {! _i:=1..13 |! w[_i]:='' !};
  w[10]:={? max_prz>3 || ww[6]+=WIND_KH.PRZ5$2; form(WIND_KH.PRZ5,,2,' ,') || '' ?};
  w[11]:={? max_prz>4 || ww[7]+=WIND_KH.PRZ6$2; form(WIND_KH.PRZ6,,2,' ,') || '' ?};
  w[12]:={? max_prz>5 || ww[8]+=WIND_KH.PRZ7$2; form(WIND_KH.PRZ7,,2,' ,') || '' ?};
  w[13]:={? max_prz>6 || ww[9]+=WIND_KH.PRZ8$2; form(WIND_KH.PRZ8,,2,' ,') || '' ?};
  w[9]:=form(WIND_KH.RAZEM,,2,' ,');     ww[10]+=WIND_KH.RAZEM$2;
  w[2]:=form(WIND_KH.ODSPOTER,,2,' ,');  ww[12]+=WIND_KH.ODSPOTER$2;
 ~~}
{SUBSTITUTE: 'LINIA1'}
{ENTIRE-}
{ i:=WIND_KH.next();~~}
{OD}
{ {? lineleft()<=3 | lineleft=0 || vp:=1 || vp:=0 ?};~~}
{IF: vp=1 & lineleft<>0}{PAGE}{FI}
{ {? lineleft=4 || dru_sum:=1 ?};~~}
{ENTIRE}
{IF: vp}{ PRN1.setcolor(color); PRN2.setcolor(color); ~~}{REP: PRN1.fjbar(PRN2)}{ELSE}{PRN1.jbar(PRN2)}{FI}
{ w[1]:='SUMA:';
  w[10]:=form(ww[2],,2,' ,');
  w[11]:=form(ww[3],,2,' ,');
  w[12]:={? max_prz>1 || form(ww[4],,2,' ,') || '' ?};
  w[13]:={? max_prz>2 || form(ww[5],,2,' ,') || '' ?};
  w[9]:=form(ww[1],,2,' ,');
  w[2]:=form(ww[11],,2,' ,');
~~}
{SUBSTITUTE: 'LINIA2'}
{ {! _i:=1..13 |! w[_i]:='' !};
  w[10]:={? max_prz>3 || form(ww[6],,2,' ,') || '' ?};
  w[11]:={? max_prz>4 || form(ww[7],,2,' ,') || '' ?};
  w[12]:={? max_prz>5 || form(ww[8],,2,' ,') || '' ?};
  w[13]:={? max_prz>6 || form(ww[9],,2,' ,') || '' ?};
  w[9]:=form(ww[10],,2,' ,');
  w[2]:=form(ww[12],,2,' ,');
 ~~}
{SUBSTITUTE: 'LINIA2'}
{ENTIRE-}
{COMMENT}------------------------------FOOTER-LAST----------------------{COMMENT-}
{FOOTER}{FOOTER-}
{ENTIRE}
{IF: vp}{REP: PRN1.flbar()}{ELSE}{PRN2.lbar()}{FI}
{<1>{PRN1.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
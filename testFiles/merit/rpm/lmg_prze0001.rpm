:!UTF-8
{TITLE:A. Przecena}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   prze001:="
      VAR_DEL.delete('color','prn1','prn2');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');

      VAR_DEL.delete('k','w','su','PRN');
      VAR_DEL.delete('dokl','doklw');
      VAR_DEL.delete('wyjscie','X_Xd')";
   prze001();

   VAR_DEL.delete('k','w','s','PRN');
   k:=obj_new(8); "--szerokosci kolumn--";
   w:=obj_new(8); "--wartosci kolumn--";
   su:=0; "--razem--";

   dokl:=3; "--dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosc--";

   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();

   k[1]:=20;
   k[2]:=20;
   k[3]:=10;
   k[4]:=12;
   k[5]:=15;
   k[6]:=12;
   k[7]:=15;
   k[8]:=15;

   w[1]:=w[2]:=w[3]:='';
   w[4]:=w[5]:=w[6]:=w[7]:=w[8]:=0;

   wyjscie:=0;

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=0;
   rsep:=PAR_WYDR.RSEP
}
{END:
   prze001(); VAR_DEL.delete('prze001')
}
{INCLUDE: wsp_pw.rpi}
{
PRN.add("w[1]",k[1],'Indeks'@);
PRN.add("w[2]",k[2],'Nazwa'@);
PRN.add("w[3]",k[3],'jm'@);
PRN.add("w[4]",k[4],'Ilość przed'@,1);
PRN.add("w[5]",k[5],'Stara cena'@,1);
PRN.add("w[6]",k[6],'Ilość po'@,1);
PRN.add("w[7]",k[7],'Nowa cena'@,1);
PRN.add("w[8]",k[8],'Wartość przeceny'@,1);

"--obliczenia--";
{? INN.D=date(0,0,0)
|| X_Xd:=tab_tmp(1,'KTM','STRING[50]',''
          ,'NAZ','STRING[150]',''
          ,'JM','STRING[10]',''
          ,'IL_PR','REAL',''
          ,'CENA_PR','REAL',''
          ,'IL_PO','REAL',''
          ,'CENA_PO','REAL',''
          ,'WAR','REAL','');
   INP.index('INP');
   INP.prefix(INN.ref());
   {? INP.first()
   || {!
      |? {? INP.C<>INP.SE
         || exec('obl_stan','magazyn_stan',INP.M,1,INN.MG,,,INN.DI);
            X_Xd.clear();
            X_Xd.blank();
            X_Xd.KTM:=INP.M().KTM;
            X_Xd.NAZ:=INP.M().N;
            X_Xd.JM:=INP.M().J().KOD;
            X_Xd.IL_PR:=BEER.S;
            X_Xd.CENA_PR:=INP.C;
            X_Xd.IL_PO:=BEER.S;
            X_Xd.CENA_PO:=INP.SE;
            X_Xd.WAR:=X_Xd.IL_PR*(X_Xd.CENA_PO-X_Xd.CENA_PR) $2;
            X_Xd.add(1)
         ?};
         INP.next()
      !}
   ?}
|| X_Xd:=sql('
     select
       M.KTM as KTM,
       M.N as NAZ,
       JM.KOD as JM,
       sum(case when DK.PLUS=''N'' then DK.IL else 0 end) as IL_PR,
       max(case when DK.PLUS=''N'' then DK.C else 0 end) as CENA_PR,
       sum(case when DK.PLUS=''T'' then DK.IL else 0 end) as IL_PO,
       max(case when DK.PLUS=''T'' then DK.C else 0 end) as CENA_PO,
       sum(case when DK.PLUS=''T'' then DK.WAR else -DK.WAR end) as WAR
    FROM @DK
       join @ND using (DK.N,ND.reference)
       join M using (DK.M,M.reference)
       join JM using (DK.JM,JM.reference)
    where
       ND.REFERENCE in ('':_a'','':_b'')
       and ND.REFERENCE like '':_c%''
    group by
       M.KTM, M.N, JM.KOD
    order by
       KTM
    ',INN.INP_R,INN.INR_R,{? INN.INP_R<>'' || 8+INN.INP_R || 8+INN.INR_R ?})
?};

{? X_Xd.first()=0 || wyjscie:=1; FUN.info('Brak danych spełniających kryteria.'@) ?};

PAR_WYDR.TITLE:={? INN.D=date(0,0,0) || 'SZACUNKOWA WARTOŚĆ PRZECENY' || 'PRZECENA' ?};
prn1:='PRN';
~~}
{EXIT: wyjscie=1}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{<{lmt+1}}{'Parametry wydruku:'}
{LINE}
{<{lmt+1}}{'Magazyn :'+INN.MG().SYM}
{IF: INN.D<>date(0,0,0)}
   {<{lmt+1}}{'Data rozpoczęcia przeceny: '+INN.DI$1}
   {<{lmt+1}}{'Data zakończenia przeceny: '+INN.D$1}
{ELSE}
   {<{lmt+1}}{'Data obliczeń przeceny: '+INN.DI$1}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   drukowanie pozycji wydruku                               {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xd}
{DO}
   {FIRST}{ rsep:=PAR_WYDR.RSEP;~~}{FIRST-}
   {
   w[1]:=X_Xd.KTM;
   w[2]:=X_Xd.NAZ;
   w[3]:=X_Xd.JM;
   w[4]:=form(X_Xd.IL_PR,,dokl,'.,');
   w[5]:=form(X_Xd.CENA_PR,,doklw,'.,');
   w[6]:=form(X_Xd.IL_PO,,doklw,'.,');
   w[7]:=form(X_Xd.CENA_PO,,doklw,'.,');
   w[8]:=form(X_Xd.WAR,,doklw,'.,');
   su+=X_Xd.WAR;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie jesli wydruk wartosciowy                    {COMMENT-}
{IF: lineleft()>0 & lineleft<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{SUBSTITUTE: 'LINIAF'}
{
PRN.n_frame();
w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:='';
w[7]:='Razem:'; PRN.FORM[7]:=0;
w[8]:=form(su,,doklw,'.,');
~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA0'}
:!UTF-8
{TITLE: Grafiki}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','col','lmt','tresc','a_okrm','raport','kal_ind','ind','ile_kol','week','opis','kol',',kolor');

   PAR_WYDR.TITLE:='';
   tresc:='sl_kalen';

   a_okrm:=0;
   P.cntx_psh();
   A_OKRM.cntx_psh;
   A_OKRM.index('A_OKRMR');
   A_OKRM.prefix();
   A_OKRM.find_key(date()~1,date()~2);
   A_OKRM.win_sel('SLO');
   {? A_OKRM.select(,1,5,'dup:d')
   || _TAB:=exec('tab_prac','!prc_czp_wydr');
      {? _TAB.first()
      || params_set('P',_TAB);
         a_okrm:=1;
         col:=obj_new(37);
         lmt:=ind:=ile_kol:=raport:=0;
         kol:=obj_new(2);
         kol[1]:=
            tab_tmp(1,
               'LP','INTEGER','Lp',
               'KOLOR','STRING[12]','Kolor'
            );
         _plik:=fopen('kolory.dfg','r',1);
         {? _plik
         || _ind:=0;
            {!
            |? (_wiersz:=fread(_plik))<>'\n'
            |! _ind+=1;
               kol[1].LP:=_ind;
               kol[1].KOLOR:=_wiersz;
               kol[1].add(1);
               {? _ind=13 || _ind:=1000 ?};
               {? _ind=1008 || _ind:=999 ?}
            !};
            fclose(_plik)
         ?};
         {? kol[1].first()
         || _ind:=0;
            {!
            |? _ind+=1;
               kol[1].LP:=_ind;
               kol[1].put();
               kol[1].next()
               !}
         ?};

         kol[2]:=
            "  {? kol[1].find_key(_a)
               || '0:0:0,'+kol[1].KOLOR
               || '0:0:0,255:255:255'
               ?}
            ";
         PAR_WYDR.TITLE:='%1 %2'['Grafik'@,date(A_OKRM.R,A_OKRM.M,1)$8]
       ?}
   ?};
   KAL_BUFF.cntx_psh();
   kal_ind:=KAL_BUFF.ndx_tmp(,1,'GPW',,,'P',,,'DATA',,);
   KAL_BUFF.index(kal_ind)
}
{END:
  KAL_BUFF.ndx_drop(kal_ind);
  KAL_BUFF.cntx_pop();
  A_OKRM.cntx_pop();
  P.cntx_pop();
  VAR_DEL.delete('par','col','lmt','tresc','a_okrm','raport','kal_ind','ind','ile_kol','week','opis','kol',',kolor')
}
{EXIT: ~a_okrm | ~params_get().P.first()}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SPACE: 'A'}
{IF: PAR_WYDR.LOGO}
{IF: exec('get_logo','img_blob')}{<1}{IMAGE: exec('get_logo','img_blob');0;1}{ELSE}
{<1}{BITMAP:'"logo.bmp",,1'}{FI}{FI}
{IF: PAR_WYDR.TITLE<>''}
{SPACE: 'A'}{FONT: 'TITLE'}{lm:=charleft();~~}
   {<1>{lm} _poz:=PAR_WYDR.TITLE*'\n';
            {? _poz
               || (_poz-1)+PAR_WYDR.TITLE
               || PAR_WYDR.TITLE
            ?}}
   {IF: PAR_WYDR.TITLE*'\n'}
      {<1>{lm} _poz:=PAR_WYDR.TITLE*'\n';
            PAR_WYDR.TITLE+(+PAR_WYDR.TITLE-_poz)}
   {FI}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{ par[6]:=0; ile_kol:=par[10].size(); ~~}
    Legenda:
{FONT: 'T8B'}
{SPACE: 'B'}
{IF: par[10].first()}
   {WHILE: ile_kol}
   {DO}
   {<6}{ind:=0;~~}{WHILE: ind<7}{DO}{IF: par[10].C='R'}{COLOR: kol[2](par[10][1])}{$par[10].A}{' - '}{$par[10].B}{COLOR: '0:0:0,255:255:255'}    { ind+=1;~~}{FI}{{? ~par[10].next() || ind:=7 ?}; ile_kol-=1;~~}{OD}
   {OD}
{FI}
{<6}{COLOR: '0:0:0,' +color}{COLOR: '0:0:0,250:250:250'}{'      Dni wolne     '@}{COLOR: '0:0:0,255:255:255'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
┌{58*'─'}{37*('┬'+(2*'─'))}┐
│ {<2>58'Nazwisko i imię, nr teczki'@} {>60'│'}{WHILE: (par[6]+=1)<=37}{DO}{
  _n:=par[6]%*7;
  {? _n=0 || 'Nd'@
  |? _n=1 || 'Pn'@
  |? _n=2 || 'Wt'@
  |? _n=3 || 'Śr'@
  |? _n=4 || 'Cz'@
  |? _n=5 || 'Pt'@
  || 'So'@
  ?}
}│{OD}
├{58*'─'}{37*('┼'+(2*'─'))}┤
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{FOOTER:0}
{FONT: 'T8B'}
{SPACE: 'B'}
{ par[6]:=0;~~}
└{58*'─'}{WHILE: (par[6]+=1)<=37}{DO}{
}┴{COLOR: col[par[6]]}{2*'─'}{OD}┘{COLOR: '0:0:0,255:255:255'}
{FOOTER-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}{
   par[2]:=A_OKRM.R;
   par[6]:=0; par[9]+=1;
   par[5]:=A_OKRM.M;
   par[3]:=1;
   KAL_BUFF.prefix('G',P.ref()); ~~
}
{FONT: 'T8B'}
{SPACE: 'B'}
{IF: raport}
├{58*'─'}{WHILE: (par[6]+=1)<=37}{DO}{
}┼{COLOR: col[par[6]]}{2*'─'}{OD}┤{COLOR: '0:0:0,255:255:255'}
{  par[6]:=0; ~~ }
{FI}
│ {<3P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+', '+form(P.T)}{>60'│'}{
   par[4]:=date(par[2],par[5],1)~4;
   par[7]:=date(par[2],par[5],0)~3;
   par[6]:=0;
   ~~
}{WHILE: (par[6]+=1)<=37}{DO}{
   par[8]:=par[6]-par[4]+1;
   col[par[6]]:='0:0:0,255:255:255';
   {? par[8]<=0
   || par[8]:=''
   |? par[7]<par[8]
   || par[8]:=''
   || _dm:=date(par[2],par[5],par[8]);
      {? par[3] & KAL_BUFF.find_key(_dm)
      || {? KAL_BUFF.TYP<>'R'
         || col[par[6]]:='0:0:0,'+color
         || par[10].find_key('R',KAL_BUFF.POCZATEK$3,KAL_BUFF.KONIEC$3);
            col[par[6]]:=kol[2](par[10][1])
         ?}
      ?}
   ?};
   raport:=1;
   ~~
}{COLOR: col[par[6]]}{>{62+((par[6]-1)*3)}par[8]
}{>{63+((par[6]-1)*3)}'│'}{OD}{COLOR: '0:0:0,255:255:255'}
{IF: lineleft()=2}
{  par[6]:=0;
   par[9]:=raport:=0; ~~
}
{PAGE}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{  exec('init','edit_kal');
   {? var_pres('par[10]')>=100 || obj_del(par[10]) ?};
   par[10]:=sql('
      select
         0 lp,
         to_string(max(KAL_BUFF.POCZATEK)) A,
         to_string(max(KAL_BUFF.KONIEC)) B,
         max(KAL_BUFF.TYP) C
      from
         KAL_BUFF
      where
         KAL_BUFF.DATA>=to_date(:_a) and KAL_BUFF.DATA<=to_date(:_b) and KAL_BUFF.GPW=\'G\'
      group by
         KAL_BUFF.POCZATEK, KAL_BUFF.KONIEC, KAL_BUFF.TYP',
      date(A_OKRM.R,A_OKRM.M,1),date(A_OKRM.R,A_OKRM.M,0));

      {? par[10].first()
      || _x:=1;
         {!
         |? {? par[10][4]='R'
            || par[10][1]:=_x;
               par[10].put();
               _x+=1
            ?};
            par[10].next()
         !}
      ?};
      par[10].index(par[10].ndx_tmp('',,'C',,,'A',,,'B',,));
      par[6]:=par[9]:=0;
   ~~
}
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
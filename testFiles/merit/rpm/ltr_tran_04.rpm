:!UTF-8
{TITLE:B. Zestawienie kosztów transportu w podziale na samochody}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','w','s','tryb','typs','zlec','samo','samm');
   PRN:=obj_new(@.CLASS.PRINT,23); PRN.s_frame();
   tryb:=rep_export();
   typs:=zlec:=samo:=samm:='';

   w:=obj_new(3); "--wartosci kolumn--";
   s:=obj_new(40);

   w[1] :=w[2] :=w[3] :='';
   s[2] :=s[12] :=s[22] :=s[32] :=0;
   s[3] :=s[13] :=s[23] :=s[33] :=0;
   aa :=bb := cc:='';
   oddz :=ST.ODDZ;

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   POJAZDY.win_sel('WER');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;
   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0
   || wyjscie:=1
   ?}
}
{END:
   VAR_DEL.delete('PRN','w','s','tryb','typs','zlec','samo','samm');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep');
   VAR_DEL.delete('aa','bb','cc','wyjscie','oddz','SXS')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1
|| PRN.add("w[1]",40,'Samochód / Typ samochodu / Zlecenie')
|| PRN.add("samo",40,'Samochód (marka)');
   PRN.add("samm",40,'Samochód (nr rejestracyjny)');
   PRN.add("typs",40,'Typ samochodu');
   PRN.add("zlec",40,'Zlecenie')
?};
PRN.add("w[2]",20,'Godziny pojazdu',1);
PRN.add("w[3]",20,'Kilometry pojazdu',1);

"--obliczenia--";
VAR_DEL.delete('SXS');
{? KARTDROG.WYBKART='Z'
|| _od:=KARTDROG.KARTY_OD;
   _do:=KARTDROG.KARTY_DO
|? KARTDROG.WYBKART='M'
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0)
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?}
?};
_sql:='select
      0 as LP,
      NWYR.KK||\' \'||NWYR.NAZ zlecenie,
      POJTYPY.NAZWA grupa,
      POJAZDY.NRREJ nr_rej,
      MAR.NAZ marka,
      CZAS godz,
      KM km

   from @SAMKOSZ
           join NWYR using (SAMKOSZ.NWYR, NWYR.REFERENCE)
           join SAMK using (SAMKOSZ.SAMK, SAMK.REFERENCE)
           join POJAZDY using (SAMK.POJAZD, POJAZDY.REFERENCE)
           left join POJTYPY using (POJAZDY.POJTYPY,POJTYPY.REFERENCE)
           left join MAR using (POJAZDY.MAR,MAR.REFERENCE)

   where (POJAZDY.FIRMA=:_a)
      and (POJAZDY.NRREJ =\':_d\' or \':_d\' =\'\' )';

   {? KARTDROG.WYBKART<>'W'
   || _sql+='and (SAMK.D between to_date(:_b) and to_date(:_c) or SAMK.D is null )'
   ?};

   {? KARTDROG.WYB_STAT<>'W'
   || _sql+=' and '+'SAMK.STATUS=\':_e\''
   ?};

   _sql+=' order by 2,3,4';
SXS :=sql(_sql,REF.FIRMA,_od,_do,'',KARTDROG.WYB_STAT);

{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Zestawienie kosztów transportu w podziale na samochody\n za okres od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Miesięczne zestawienie kosztów transportu \nza okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8
|| PAR_WYDR.TITLE:='Zestawienie kosztów transportu w podziale na samochody\n za okres od: '+$_od+' do: '+$_do
?};
prn1:='PRN'
;~~}
{IF: SXS.first()=0}{ FUN.info('Brak danych spełniających kryteria.');~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}{<70}{'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: 'SXS'}
{DO}
   {FIRST}{ aa:=zlec:=SXS.ZLECENIE; bb:=typs:=SXS.GRUPA; cc:=samm:=SXS.NR_REJ; samo:=SXS.MARKA;~~}{FIRST-}
   {IF: cc<>SXS.NR_REJ | bb<>SXS.GRUPA | aa<>SXS.ZLECENIE}
      {
      w[1] :='  '+cc1;
      w[2] :=form(s[32]);
      w[3] :=form(s[33]);
      s[32] :=0; s[33] :=0
      ;~~}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
   {IF: bb<>SXS.GRUPA | aa<>SXS.ZLECENIE}
      {
      w[1] :='  '+bb;
      w[2] :=form(s[22]);
      w[3] :=form(s[23]);
      s[22] :=s[23] :=0
      ;~~}
      {IF: tryb<>1}{SUBSTITUTE: 'LINIA0'}{FI}
   {FI}
   {IF: aa<>SXS.ZLECENIE}
      {
      w[1] :=aa;
      w[2] :=form(s[12]);
      w[3] :=form(s[13]);
      s[12] :=0; s[13] :=0
      ;~~}
      {IF: tryb<>1}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {FONT: 'T8G'}{SPACE: 'C'}
      {FI}
   {FI}
   {
   s[2] +=SXS.GODZ; s[3] +=SXS.KM;
   s[12]+=SXS.GODZ; s[13]+=SXS.KM;
   s[22]+=SXS.GODZ; s[23]+=SXS.KM;
   s[32]+=SXS.GODZ; s[33]+=SXS.KM;
   aa:=zlec:=SXS.ZLECENIE;
   bb:=typs:=SXS.GRUPA;
   cc:=samm:=SXS.NR_REJ;
   samo:=SXS.MARKA;
   cc1:='   -   '+SXS.NR_REJ+' '+SXS.MARKA
   ;~~}
{OD}
{
w[1] :='  '+cc1;
w[2] :=form(s[32]);
w[3] :=form(s[33])
;~~}
{SUBSTITUTE: 'LINIA0'}
{IF: tryb<>1}
{
w[1] :='  '+bb;
w[2] :=form(s[22]);
w[3] :=form(s[23])
;~~}
{SUBSTITUTE: 'LINIA0'}
{
w[1] :=aa;
w[2] :=form(s[12]);
w[3] :=form(s[13])
;~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA0'}
{FONT: 'T8G'}{SPACE: 'C'}
{
w[1] :='Razem:';
w[2] :=form(s[2]);
w[3] :=form(s[3])
;~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIA0'}
{FONT: 'T8G'}{SPACE: 'C'}
{FI}
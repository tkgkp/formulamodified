:!UTF-8
{TITLE:D. Zestawienie pracy pojazdów}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('k','w','s','PRN','tryb');
   k :=obj_new(15); "--szerokosci kolumn--";
   w :=obj_new(15); "--wartosci kolumn--";
   dokl :=2; "--dokladnosc--";
   PRN :=obj_new(@.CLASS.PRINT,15); PRN.s_frame();
   tryb:=rep_export();

   oddz :=KARTDROG.ODDZ;

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   k[1]  :=3;
   k[2]  :=30;
   k[3]  :=10;
   k[4]  :=10;
   k[5]  :=8;
   k[6]  :=5;
   k[7]  :=13;
   k[8]  :=13;
   k[9]  :=13;
   k[10] :=13;
   k[11] :=13;
   k[12] :=13;
   k[13] :=13;

   POJAZDY.win_sel('WER');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;
   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0
   || wyjscie:=1
   ?}
}
{END:
   VAR_DEL.delete('k','w','s','PRN','tryb');
   VAR_DEL.delete('dokl','wyjscie','oddz','SXS');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("form(w[1])",k[1],'Lp.') ?};
PRN.add("form(w[2])",k[2],'Samochód');
PRN.add("form(w[3],,0)",k[3],'Licz. p.',1);
PRN.add("form(w[4],,0)",k[4],'Licz. k.',1);
PRN.add("form(w[5],,0)",k[5],'Jazdy',1);
PRN.add("form(w[6],,0)",k[6],'Zał.',1);
PRN.add("form(w[7],,dokl)",k[7],'SP pocz.',1);
PRN.add("form(w[8],,dokl)",k[8],'SP kon.',1);
PRN.add("form(w[9],,dokl)",k[9],'Pobrano',1);
PRN.add("form(w[10],,dokl,' ,')",k[10],'Wg normy',1);
PRN.add("form(w[11],,dokl,' ,')",k[11],'Rzeczywiste',1);
PRN.add("form(w[12],,dokl,' ,')",k[12],'Przepał',1);
PRN.add("form(w[13],,dokl,' ,')",k[13],'Oszczędność',1);
{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Zestawienie pracy pojazdów za okres od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Zestawienie pracy pojazdów\nza okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?};
   PAR_WYDR.TITLE:='Zestawienie pracy pojazdów za okres od: '+$_od+' do: '+$_do
?};
prn1:='PRN'
;~~}
{IF: POJAZDY.index('NRREJ'); POJAZDY.prefix(REF.FIRMA); POJAZDY.first()=0}{ FUN.info('Brak danych spełniających kryteria.');~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}{<70}{'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE:'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{
rsep:=PAR_WYDR.RSEP;
POJAZDY.win_edit('RED');
w[1]:=0
;~~}
{FOR:'POJAZDY'}
{INDEX:'NRREJ'}
{PREFIX: REF.FIRMA}
{DO}
   {
   {? KARTDROG.WYBKART='Z'
   || _od:=KARTDROG.KARTY_OD;
      _do:=KARTDROG.KARTY_DO
   |? KARTDROG.WYBKART='M'
   || _od:=date(KARTDROG.AR,KARTDROG.AM,1);
      _do:=date(KARTDROG.AR,KARTDROG.AM,0)
   || _od:=date(KARTDROG.AR,KARTDROG.AM,1);
      _do:=date(KARTDROG.AR,KARTDROG.AM,0);
      SAMK.index('D2');
      SAMK.prefix();
      {? SAMK.first()
      || _od:=SAMK.D;
         SAMK.last();
         _do:=SAMK.D
      ?}
   ?};
   w[1]  +=1;
   w[2]  :=POJAZDY.NRREJ+' - '+POJAZDY.MAR().NAZ;
   w[3] :=w[4] :=w[7] :=w[8] :=0;
   SAMK.index('D');
   SAMK.prefix(POJAZDY.ref());
   _ref_f:=_ref_l:=null;
   _tmp_df:=_tmp_dl:=date(0,0,0);
   {? SAMK.first()
   || {! |?
         {? _ref_f=null
         || {? (SAMK.D>=_od & SAMK.D<=_do) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT ) & SAMK.STATUS<>'S'
            || _ref_f:=_ref_l:=SAMK.ref();
               _tmp_df:=_tmp_dl:=SAMK.D;
               w[3]:=SAMK.SP;
               w[7]:=SAMK.SPP;
               w[4]:=SAMK.SK;
               w[8]:=SAMK.SKP
            ?}
         |? (SAMK.D>=_od & SAMK.D<=_do) & (SAMK.D>=_tmp_dl) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT ) & SAMK.STATUS<>'S'
         || {? SAMK.D=_tmp_df & SAMK.SP<w[3]
            || _ref_f:=SAMK.ref();
               w[3]:=SAMK.SP;
               w[7]:=SAMK.SPP
            ?}; 
            {? (SAMK.D=_tmp_dl & SAMK.SK>w[4]) | SAMK.D>_tmp_dl
            || w[4]:=SAMK.SK; 
               w[8]:=SAMK.SKP
            ?};
            _ref_l:=SAMK.ref();
            _tmp_dl:=SAMK.D
         ?};
         SAMK.next()
      !};
      {? _ref_l=null & _ref_f<>null || _ref_l:=_ref_f ?};
      {? _ref_f=null & _ref_l<>null || _ref_l:=null ?}
   ?};
   w[5] :=exec('oblicz_prz','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT,'P');
   w[6]  :=exec('oblicz_zal','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT);
   w[9]  :=exec('oblicz_zpalm','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT);
   w[10] :=exec('oblicz_zpaln','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT);
   w[11] :=exec('oblicz_rzecz','karty_drog',POJAZDY.ref(),_od,_do,KARTDROG.WYB_STAT,'P');
   _zu :=w[11]-w[10];
   w[12] :=w[13] :=0;
   {? _zu>0 || w[12] :=|(_zu)
   |? _zu<0 || w[13] :=|(_zu)
   ?}
   ;~~}
   {SUBSTITUTE:'LINIA0'}
{OD}
:!UTF-8
{TITLE:E2. Zbiorcze podsumowanie pracy pojazdów wg kierowców +zbiornik dodatkowy}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,5,
   5,5,5,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('tryb');
   VAR_DEL.delete('w','s','k','PRN','PRN2','PRNS','s_tran','TMP_KIER','IDX_KIER','tmp_licz');
   k :=obj_new(14); "--szerokosci kolumn--";
   w :=obj_new(14); "--wartosci kolumn--";
   s_tran:=obj_new(14); "--podsumowania--";

   {! _i:=1..14 |! s_tran[_i]:=0 !};
   {! _i:=1..14 |! w[_i]:=0 !};
   {! _i:=1..14 |! k[_i]:=0 !};

   dokl :=2; "--dokladnosc--";
   PRN := obj_new(@.CLASS.PRINT,14); PRN.s_frame();
   PRN2 := obj_new(@.CLASS.PRINT,14); PRN2.s_frame();
   PRNS := obj_new(@.CLASS.PRINT,14); PRNS.s_frame();
   tryb:=rep_export();

   TMP_KIER:=tab_tmp(1,'REF_KIER','INTEGER','Ref kierowcy',
                      'NAZW','STRING[50]','Imię i nazwisko',
                      'JAZDY','REAL','Jazdy',
                      'POBRANO','REAL','Pobrano',
                      'WG_NORMY','REAL','Wg normy',
                      'RZECZYW','REAL','Rzeczywiste',
                      'PRZEPAL','REAL','Przepal',
                      'OSZCZ','REAL','Oszczędność',
                      'D_JAZD','REAL','Jazdy',
                      'D_POBRAN','REAL','Pobrano',
                      'D_WG_NOR','REAL','Wg normy',
                      'D_RZECZY','REAL','Rzeczywiste',
                      'D_PRZEPA','REAL','Przepal',
                      'D_OSZCZ','REAL','Oszczędność');
   IDX_KIER:=TMP_KIER.ndx_tmp(,,'NAZW',,);

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=0;rsep:=1;

   k[1]  := 3; 'Lp';
   k[2]  :=25; 'Kierowca';
   k[3]  := 7; 'Jazdy';
   k[4]  := 10; 'Pobrano';
   k[5]  :=12; 'Wg normy';
   k[6]  :=12; 'Rzeczywiste';
   k[7]  :=12; 'Przepał';
   k[8]  :=12; 'Oszczędność';
   k[9]  := 7; 'Jazdy';
   k[10] := 7; 'Pobrano';
   k[11] :=12; 'Wg normy';
   k[12] :=12; 'Rzeczywiste';
   k[13] :=12; 'Przepał';
   k[14] :=12; 'Oszczędność';

   POJAZDY.win_sel('WER');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;

   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0
   || wyjscie:=1
   ?}
}
{END:
   VAR_DEL.delete('tryb');
   VAR_DEL.delete('w','s','k','PRN','PRN2','PRNS','s_tran','TMP_KIER','IDX_KIER','tmp_licz');
   VAR_DEL.delete('wyjscie','dokl','oddz');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{MACRO: 'TABHEAD2'}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn1+'.ini_head()'; ($(_f))()}
{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
{WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
{ _f:=prn2+'.ini_head()'; ($(_f))()}
{ _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
{WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{MACRO-}
{
PRN.add("form(w[1])",k[1],'Lp.',1);
PRN.add("form(w[2])",k[2],'Kierowca');
PRN.add("form(w[3])",k[3],{? tryb=1 || 'ZP: ' || '' ?}+'Jazdy',1);
PRN.add("form(w[4],,dokl,'.,')",k[4],{? tryb=1 || 'ZP: ' || '' ?}+'Pobrano',1);
PRN.add("form(w[5],,dokl,'.,')",k[5],{? tryb=1 || 'ZP: ' || '' ?}+'Wg normy',1);
PRN.add("form(w[6],,dokl,'.,')",k[6],{? tryb=1 || 'ZP: ' || '' ?}+'Rzeczywiste',1);
PRN.add("form(w[7],,dokl,'.,')",k[7],{? tryb=1 || 'ZP: ' || '' ?}+'Przepał',1);
PRN.add("form(w[8],,dokl,'.,')",k[8],{? tryb=1 || 'ZP: ' || '' ?}+'Oszczędność',1);
PRN.add("form(w[9])",k[9],{? tryb=1 || 'ZD: ' || '' ?}+'Jazdy');
PRN.add("form(w[10],,dokl,'.,')",k[10],{? tryb=1 || 'ZD: ' || '' ?}+'Pobrano',1);
PRN.add("form(w[11],,dokl,'.,')",k[11],{? tryb=1 || 'ZD: ' || '' ?}+'Wg normy',1);
PRN.add("form(w[12],,dokl,'.,')",k[12],{? tryb=1 || 'ZD: ' || '' ?}+'Rzeczywiste',1);
PRN.add("form(w[13],,dokl,'.,')",k[13],{? tryb=1 || 'ZD: ' || '' ?}+'Przepał',1);
PRN.add("form(w[14],,dokl,'.,')",k[14],{? tryb=1 || 'ZD: ' || '' ?}+'Oszczędność',1);

PRN2.add("", k[1]+k[2]+3, '');
PRN2.add("", k[3]+k[4]+k[5]+k[6]+k[7]+k[8]+15, 'Zbiornik podstawowy');
PRN2.add("", k[9]+k[10]+k[11]+k[12]+k[13]+k[14]+15, 'Zbiornik dodatkowy');

PRNS.add("form(s_tran[1])",31,'Razem:');
PRNS.add("form(s_tran[3])",k[3],'Jazdy');
PRNS.add("form(s_tran[4],,dokl,'.,')",k[4],'Pobrano',1);
PRNS.add("form(s_tran[5],,dokl,'.,')",k[5],'Wg normy',1);
PRNS.add("form(s_tran[6],,dokl,'.,')",k[6],'Rzeczywiste',1);
PRNS.add("form(s_tran[7],,dokl,'.,')",k[7],'Przepał',1);
PRNS.add("form(s_tran[8],,dokl,'.,')",k[8],'Oszczędność',1);
PRNS.add("form(s_tran[9])",k[9],'Jazdy');
PRNS.add("form(s_tran[10],,dokl,'.,')",k[10],'Pobrano',1);
PRNS.add("form(s_tran[11],,dokl,'.,')",k[11],'Wg normy',1);
PRNS.add("form(s_tran[12],,dokl,'.,')",k[12],'Rzeczywiste',1);
PRNS.add("form(s_tran[13],,dokl,'.,')",k[13],'Przepał',1);
PRNS.add("form(s_tran[14],,dokl,'.,')",k[14],'Oszczędność',1);

{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Zbiorcze podsumowanie pracy pojazdów wg kierowców (+ z. dod.)\n'
                  +'za okres od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Zbiorcze podsumowanie pracy pojazdów wg kierowców (+ z. dod.)\n'
                  +'za okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?};
   PAR_WYDR.TITLE:='Zbiorcze podsumowanie pracy pojazdów wg kierowców (+ z. dod.)\n'
                  +'za okres od: '+$_od+' do: '+$_do
?};
prn1:='PRN';prn2:='PRN'
;~~}
{IF: POJAZDY.index('NRREJ'); POJAZDY.prefix(REF.FIRMA); POJAZDY.first()=0}{ FUN.info('Brak danych spełniających kryteria.');~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}{<70}{'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{IF: tryb=1}
{<{lmt+1}}{'ZP - Zbiornik podstawowy'}
{<{lmt+1}}{'ZD - Zbiornik dodatkowy'}
{FI}
{IF: tryb<>1}
{FONT: 'T8GB'}
{prn1:='PRN2';prn2:='PRN';~~}
{SUBSTITUTE: 'TABHEAD2'}
{prn1:='PRN';prn2:='PRN';~~}
{FONT: 'T8G'}{ vp:=1;~~}
{ELSE}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FI}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{IF: tryb<>1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRN2';prn2:='PRN';~~}
{SUBSTITUTE: 'TABHEAD2'}
{prn1:='PRN';prn2:='PRN';~~}
{ELSE}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{
rsep:=PAR_WYDR.RSEP;
POJAZDY.win_edit('RED');
w[1]:=0;
prn1:='PRN';prn2:='PRN'
;~~}
{
{? KARTDROG.WYBKART='M'
|| KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0)
|| KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || KARTDROG.KARTY_OD:=SAMK.D;
      SAMK.last();
      KARTDROG.KARTY_DO:=SAMK.D
   ?}
?};
POJAZDY.index('NRREJ');
POJAZDY.prefix(REF.FIRMA);
{? POJAZDY.first()
|| {! |?
      exec('tmp_kier','karty_drog',POJAZDY.ref(),KARTDROG.KARTY_OD,KARTDROG.KARTY_DO,'D');
      POJAZDY.next()
   !}
?};

{? TMP_KIER.first()
|| {! |?
      s_tran[3]+=TMP_KIER.JAZDY;
      s_tran[4]+=TMP_KIER.POBRANO;
      s_tran[5]+=TMP_KIER.WG_NORMY;
      s_tran[6]+=TMP_KIER.RZECZYW;
      s_tran[7]+=TMP_KIER.PRZEPAL;
      s_tran[8]+=TMP_KIER.OSZCZ;
      s_tran[9]+=TMP_KIER.D_JAZD;
      s_tran[10]+=TMP_KIER.D_POBRAN;
      s_tran[11]+=TMP_KIER.D_WG_NOR;
      s_tran[12]+=TMP_KIER.D_RZECZY;
      s_tran[13]+=TMP_KIER.D_PRZEPA;
      s_tran[14]+=TMP_KIER.D_OSZCZ;
      TMP_KIER.next()
   !}
?}
;~~}
{IF: TMP_KIER.size()>0}
   {tmp_licz:=0;~~}
   {FOR: 'TMP_KIER'}{INDEX: IDX_KIER}
   {DO}
      {
      tmp_licz+=1;
      w[1]:=tmp_licz;
      w[2]:=TMP_KIER.NAZW;
      w[3]:=TMP_KIER.JAZDY;
      w[4]:=TMP_KIER.POBRANO;
      w[5]:=TMP_KIER.WG_NORMY;
      w[6]:=TMP_KIER.RZECZYW;
      w[7]:=TMP_KIER.PRZEPAL;
      w[8]:=TMP_KIER.OSZCZ;
      w[9]:=TMP_KIER.D_JAZD;
      w[10]:=TMP_KIER.D_POBRAN;
      w[11]:=TMP_KIER.D_WG_NOR;
      w[12]:=TMP_KIER.D_RZECZY;
      w[13]:=TMP_KIER.D_PRZEPA;
      w[14]:=TMP_KIER.D_OSZCZ
      ;~~}
      {SUBSTITUTE:'LINIA0'}
   {OD}
{FI}
{COMMENT}------------------------------Podsumowanie------------------{COMMENT-}
{IF: tryb<>1}
{IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRN';prn2:='PRNS'; vp:=0;
 s_tran[1]:='Razem:'
; ~~}
{SUBSTITUTE: 'LINIA02'}
{prn1:='PRNS';prn2:='PRN'; vp:=0; ~~}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{FI}
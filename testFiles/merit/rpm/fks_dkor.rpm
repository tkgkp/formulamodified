:!UTF-8
{TITLE:Korekty danych JPK_V7}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,14); PRN.sl_frame();
   PRNF:=obj_new(@.CLASS.PRINT,4); PRNF.sl_frame();
   w:=obj_new(14);
   czy_w:=sumawn:=sumama:=sumawnb:=sumamab:=cnt:=1;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=NOMAXP:=0
}
{END:
   VAR_DEL.delete('PRN','PRNF','w','NOMAXP');
   &czy_w; &sumawn; &sumama; &sumawnb; &sumamab;
   &color; &cnt; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep; exec('end_msg','dok_fks_zest')
}
{MACRO: 'INIT'}
{  _o:=34;
   PRN.add("w[1]",3,'Lp.'@,1);
   PRN.add("form(w[2],,,' ,')",20,'Data korekty'@,1);
   PRN.add("form(w[3],,,' ,')",20,'Godzina korekty'@,1);
   PRN.add("form(w[4],,2,' ,')",28,'GTU'@,1);
   PRN.add("form(w[5],,2,' ,')",28,'Procedury szczególne'@,1);
   PRN.add("form(w[9],,2,' ,')",20,'Dokonujący korekty'@,1);
   PRNF.add("w[1]",41);
   PRNF.add("form(w[2],,2,' ,')",14,,1);
   PRNF.add("form(w[3],,2,' ,')",14,,1);
   ~~}
{MACRO-}
{MACRO: 'START'}
{  obj_del(PRN); obj_del(PRNF);
   PRN:=obj_new(@.CLASS.PRINT,14); PRN.sl_frame();
   PRNF:=obj_new(@.CLASS.PRINT,4); PRNF.sl_frame();
   sumawn:=sumama:=sumawnb:=sumamab:=0;
   ~~}
   {SUBSTITUTE: 'INIT'}
{MACRO-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}------------------------------MACRO WYDRUK-----------------{COMMENT-}
{MACRO: 'WYDRUK'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='Korekty danych JPK_V7 dokumentu źródłowego '@+DOK.NK;~~}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: rep_export()}
{'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}
{'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}
{'Data operacji: %1'@[$DOK.DOP]}
{'Data zapisu: %1'@[$DOK.DTW]}
{'Data wystawienia: %1'@[$DOK.DTO]}
{IF: DOK.D4<>date(0,0,0)}{'Data otrzymania: %1'@[$DOK.D4]}{FI}
{IF: DOK.D3<>date(0,0,0)}{'Termin płatności: %1'@[$DOK.D3]}{FI}
{'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}
{'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}
{'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}
{'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}
{FI}
{LINE: 1}
{ELSE}
{<2 'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{<2 'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}{<40 'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{<2 'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}
{<2 'Data operacji: %1'@[$DOK.DOP]}{<34 'Data zapisu: %1'@[$DOK.DTW]}{<66 'Data wystawienia: %1'@[$DOK.DTO]}{IF: DOK.D4<>date(0,0,0)}{<98 'Data otrzymania: %1'@[$DOK.D4]}{FI}{IF: DOK.D3<>date(0,0,0)}{<110 'Termin płatności: %1'@[$DOK.D3]}{FI}
{<2 'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{<2 'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}{<2 'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}{<66 'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}{<98 'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}{FI}
{FI}
{FONT: 'T8GB'}{ prn1:='PRN'; prn2:='PRNF'; ~~}
{SUBSTITUTE: 'TABHEAD'}
{FONT: 'T8G'}{vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; prn1:='PRN'; prn2:='PRNF'; ~~}
{FOR: 'DOK_JPK'}
{INDEX: 'TIME'}
{PREFIX: DOK.ref()}
{DO}
{ w[1]:=cnt; cnt+=1;
  w[2]:=DOK_JPK.DATE;
  w[3]:=DOK_JPK.TIME;
  w[4]:=DOK_JPK.JPK_GTU;
  w[5]:=DOK_JPK.JPK_PROC;
  w[9]:=DOK_JPK.OPERATOR;
 ~~}
  {SUBSTITUTE: 'LINIA0'}
{OD}
{MACRO-}
{COMMENT}--------------------------END MACRO WYDRUK-----------------{COMMENT-}
{IF: var_pres('TT_D_DOK')>100}
   {NOMAXP:=1;~~}
   {FOR: 'TT_D_DOK'}
   {DO}
      {IF: DOK.seek(TT_D_DOK.REF,DOK.name()) & (POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first()) }
         {SUBSTITUTE: 'START'}
         {SUBSTITUTE: 'WYDRUK'}{l_druk+=1;~~}
         {IF: TT_D_DOK.next()}{HEADER}{HEADER-}{PAGE}{page(1);~~}{FI}
      {FI}
   {OD}
   {czy_druk:=1;~~}
{ELSE}
   {SUBSTITUTE: 'INIT'}
   {SUBSTITUTE: 'WYDRUK'}
{FI}

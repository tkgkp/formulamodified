:!UTF-8
{TITLE:F1. Rozliczenie pracy pojazdu (kierowcy)}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('w','s','k','PRN','TMP_KIER','PRNS','IDX_KIER','kobrot','s_tran','tryb','samr','samt','samm');
   k :=obj_new(15); "--szerokosci kolumn--";
   w :=obj_new(15); "--wartosci kolumn--";
   s_tran:=obj_new(15); "--podsumowania--";
   dokl :=2; "--dokladnosc--";

   TMP_KIER:=tab_tmp(1,'REF_KIER','INTEGER','Ref kierowcy',
                      'NAZW','STRING[50]','Imię i nazwisko',
                      'JAZDY','REAL','Jazdy',
                      'POBRANO','REAL','Pobrano',
                      'WG_NORMY','REAL','Wg normy',
                      'RZECZYW','REAL','Rzeczywiste',
                      'PRZEPAL','REAL','Przepal',
                      'OSZCZ','REAL','Oszczędność');
   IDX_KIER:=TMP_KIER.ndx_tmp(,,'NAZW',,);

   PRN:=obj_new(@.CLASS.PRINT,17);PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,15);PRNS.s_frame();
   tryb:=rep_export();
   samr:=samt:=samm:='';

   {! _i:=1..15 |! s_tran[_i]:=0 !};

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=0;rsep:=1;

   k[1]  :=3;
   k[2]  :=10;
   k[3]  :=10;
   k[4]  :=20;
   k[5]  :=10;
   k[6]  :=10;
   k[7]  :=6;
   k[8]  :=4;
   k[9]  :=13;
   k[10] :=13;
   k[11] :=13;
   k[12] :=13;
   k[13] :=13;
   k[14] :=13;
   k[15] :=13;

   POJAZDY.win_sel('SLO_J');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;
   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0
   || wyjscie:=1
   ?}
}
{END:
   VAR_DEL.delete('tryb');
   VAR_DEL.delete('w','s','k','PRN','TMP_KIER','PRNS','IDX_KIER','kobrot','s_tran');
   VAR_DEL.delete('wyjscie','dokl','oddz');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1
|| PRN.add("form(w[1])",k[1],'Lp.',1)
|| PRN.add("form(samr)",15,'Samochód (Rejestracja)');
   PRN.add("form(samt)",15,'Samochód (Nazwa)');
   PRN.add("form(samm)",15,'Samochód (Marka)')
?};
PRN.add("form(w[2])",k[2],'Data');
PRN.add("form(w[3])",k[3],'Karta');
PRN.add("form(w[4])",k[4],'Nazwisko i imię kierowcy');
PRN.add("form(w[5],,dokl,' ,')",k[5],'Licz. pocz.',1);
PRN.add("form(w[6],,dokl,' ,')",k[6],'Licz. kon.',1);
PRN.add("form(w[7],,dokl,' ,')",k[7],'Jazdy',1);
PRN.add("form(w[8],,dokl,' ,')",k[8],'Zał.',1);
PRN.add("form(w[9],,dokl,' ,')",k[9],'Stan pal. pocz.',1);
PRN.add("form(w[10],,dokl,' ,')",k[10],'Stan pal. kon.',1);
PRN.add("form(w[11],,dokl,' ,')",k[11],'Pobrano',1);
PRN.add("form(w[12],,dokl,' ,')",k[12],'Zużycie wg normy',1);
PRN.add("form(w[13],,dokl,' ,')",k[13],'Zużycie rzeczywiste',1);
PRN.add("form(w[14],,dokl,' ,')",k[14],'Przepał',1);
PRN.add("form(w[15],,dokl,' ,')",k[15],'Oszczędność',1);

PRNS.add("s_tran[1]",78,'Razem');
PRNS.add("form(s_tran[7],,dokl,' ,')",k[7],'Jazdy',1);
PRNS.add("form(s_tran[8],,dokl,' ,')",k[8],'Zał.',1);
PRNS.add("form(s_tran[9],,dokl,' ,')",k[9],'Stan pal. początek',1);
PRNS.add("form(s_tran[10],,dokl,' ,')",k[10],'Stan pal. koniec',1);
PRNS.add("form(s_tran[11],,dokl,' ,')",k[11],'Pobrano',1);
PRNS.add("form(s_tran[12],,dokl,' ,')",k[12],'Zużycie wg normy',1);
PRNS.add("form(s_tran[13],,dokl,' ,')",k[13],'Zużycie rzeczywiste',1);
PRNS.add("form(s_tran[14],,dokl,' ,')",k[14],'Przepał',1);
PRNS.add("form(s_tran[15],,dokl,' ,')",k[15],'Oszczędność',1);
{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu za okres od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO+' (kierowcy)'
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu\nza okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8+' (kierowcy)'
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?};
   PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu za okres od: '+$_od+' do: '+$_do+' (kierowcy)'
?};
{? KARTDROG.WYBKART='M'
|| KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0)
|? KARTDROG.WYBKART='W'
|| KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || KARTDROG.KARTY_OD:=SAMK.D;
      SAMK.last();
      KARTDROG.KARTY_DO:=SAMK.D
   ?}
?};
prn1:='PRN'; vp:=1
;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}{<70}{'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{IF: tryb<>1}{<{lmt+1}POJAZDY.NRREJ+' '+POJAZDY.NAZ+' '+POJAZDY.MAR().NAZ}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR:'POJAZDY'}
{INDEX:'NRREJ'}
{PREFIX: REF.FIRMA}
{SELECT}
{DO}
   { w[1]:=0;{! _i:=1..15 |! s_tran[_i]:=0 !};
     samr:=POJAZDY.NRREJ;
     samt:=POJAZDY.NAZ;
     samm:=POJAZDY.MAR().NAZ;
   exec('tmp_kier','karty_drog',POJAZDY.ref(),KARTDROG.KARTY_OD,KARTDROG.KARTY_DO,'P')
   ;~~}
   {FOR:'SAMK'}
   {INDEX:'D'}
   {PREFIX:POJAZDY.ref()}
   {DO}
      {IF: (SAMK.D>=KARTDROG.KARTY_OD & SAMK.D<=KARTDROG.KARTY_DO) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT)}
         {
         w[1] +=1;
         w[2] :=SAMK.D;
         w[3] :=SAMK.SYM;
         P_KAP.index('K');
         P_KAP.prefix(SAMK.ref(),'T');
         {? P_KAP.first()
         || {? P_KAP.size()>1
            || P_KAP.cntx_psh();
               w[4]:='';
               {! |?
                  {? w[4]<>'' || w[4]+=',' ?};
                  w[4]+=P_KAP.OSOBA().NAZWISKO+' '+P_KAP.OSOBA().PIERWSZE;
                  P_KAP.next()
               !};
               P_KAP.cntx_pop()
            || w[4]:=P_KAP.OSOBA().NAZWISKO+' '+P_KAP.OSOBA().PIERWSZE
            ?}
         || w[4]:=''
         ?};

         w[5] :=SAMK.SP;
         w[6] :=SAMK.SK;
         w[7] :=w[6]-w[5];
         w[8] :=SAMK.ILZM+SAMK.ILZP;
         w[9] :=SAMK.SPP;
         w[10] :=SAMK.SKP;
         w[11] :=exec('i_palz','karty_drog',SAMK.ref(),'P');
         w[12] :=SAMK.Z_NORMA;
         w[13]:=(w[9]-w[10])+w[11];
         _zu:=w[13]-w[12];
         w[14]:=w[15]:=0;
         {? _zu>0 || w[14]:=|(_zu)
         |? _zu<0 || w[15]:=|(_zu)
         ?};

         s_tran[7]+=w[7];
         s_tran[11]+=w[11];
         s_tran[12]+=w[12];
         s_tran[13]+=w[13];
         s_tran[14]+=w[14];
         s_tran[15]+=w[15]
         ;~~}
         {prn1:='PRN';prn2:='PRN'; ~~}
         {SUBSTITUTE:'LINIA0'}
      {FI}
      {IF: lineleft()<>0 & lineleft()<=3}{PAGE}{FI}
   {OD}
   {IF: tryb<>1}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {prn1:='PRN';prn2:='PRNS';
   s_tran[1]:='Razem:';
   s_tran[8]:=s_tran[9]:=s_tran[10]:=''
   ; ~~}
   {SUBSTITUTE: 'LINIA02'}
   {IF: TMP_KIER.size()>0}
       {prn1:='PRNS';prn2:='PRNS';kobrot:=0;~~}
       {FOR: 'TMP_KIER'}{INDEX: IDX_KIER}
       {DO}
         { kobrot+=1;
          {? kobrot=1
          || s_tran[1]:='W tym: '+TMP_KIER.NAZW
          || s_tran[1]:='           '+TMP_KIER.NAZW
          ?};
          s_tran[7]:=TMP_KIER.JAZDY;
          s_tran[11]:=TMP_KIER.POBRANO;
          s_tran[12]:=TMP_KIER.WG_NORMY;
          s_tran[13]:=TMP_KIER.RZECZYW;
          s_tran[14]:=TMP_KIER.PRZEPAL;
          s_tran[15]:=TMP_KIER.OSZCZ

         ;~~}
         {{? vp=1 || prn1:='PRN';prn2:='PRNS' || prn1:='PRNS';prn2:='PRNS' ?};~~}
         {SUBSTITUTE:'LINIA02'}
       {OD}
       {TMP_KIER.erase();~~}
   {FI}
   {prn1:='PRNS';prn2:='PRN';~~}
   {FONT: 'T8G'}{SPACE: 'C'}
   {IF: lineleft()>0}{PAGE}{FI}
   {FI}
{OD}
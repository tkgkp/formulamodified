:!UTF-8
{TITLE:C. Szczegółowe zestawienie obrotów magazynowych}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_103:="
      VAR_DEL.delete('color','prn1','prn2','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');

      VAR_DEL.delete('k','w','s','PRN','doklsp','doklp','doklr','doklsk','kin','kgr','kjm','j2');
      VAR_DEL.delete('odd','dod','fm','iw');
      VAR_DEL.delete('wyjscie','X_Xa','X_Xb','X_Xc','il_lin');
      VAR_DEL.delete('wydr_naz','wydr_edi',' __maxdok')";
   mag_103();

   doklsp:=doklp:=doklr:=doklsk:=0; "--ilosc miejsc dziesietnych--";
   VAR_DEL.delete('k','w','s','PRN');
   k :=obj_new(11); "--szerokosci kolumn--";
   w :=obj_new(11); "--wartosci kolumn--";
   s :=obj_new(4); "--razem--";
   PRN :=obj_new(@.CLASS.PRINT,13); PRN.s_frame();
   k[1] :=20;
   k[2] :=40;
   k[3] :=10;
   k[4] :=15;
   k[5] :=15;
   k[6] :=15;
   k[7] :=15;
   k[8] :=10;
   k[9] :=15;
   k[10]:=15;
   k[11]:=15;
   w[1] :=w[2] :=w[8] :='';
   w[3] :=w[4] :=w[5] :=w[6] :=w[7] :=w[9] :=w[10]:=w[11]:=0;
   tryb :=rep_export();
   {! _ii..4 |! s[_ii] :=0 !};
   kin:=kgr:=kjm:='';

   wydr_naz:='mag_103';
   wydr_edi:='MAG_103';

   fm :='N';
   iw :='I';
   odd:=dod:=date(0,0,0);
   j2:='N';

"--parametry okreslane przez uzytkownika--";
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   {? WYDRUKIL.RADIOB_1=1 & exec('upr_cm','ceny')=0 || WYDRUKIL.RADIOB_1:=0 ?};
   {? WYDRUKIL.RADIOB_1=1 & ST.MAG().IL='T' || WYDRUKIL.RADIOB_1:=0 ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("
         _noupr:=exec('upr_cm','ceny')=0;
         {? ~_noupr & ST.MAG().IL='T' & WYDRUKIL.RADIOB_1=1
         || FUN.info('Magazyn tylko ilościowy zmieniono parametr Rodzaj na wydruk ilościowy.'@);
            WYDRUKIL.RADIOB_1:=0
         ?};
         {? WYDRUKIL.RADIOB_1=1 & _noupr
         || FUN.info('Brak uprawnień do wartości magazynowych.'@); 'RADIOB_1'
         || ''
         ?}
      ")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         fm:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         iw:={? WYDRUKIL.RADIOB_1=1 || 'W' || 'I' ?};
         j2:={? iw='I' & WYDRUKIL.CHKBOX02=1 || 'T' || 'N' ?};
         odd:=WYDRUKIL.OD_DATY;
         dod:=WYDRUKIL.DO_DATY
      ?}
   ?};

   dokl :={? iw='W' || 2 |? iw='I' || 3 ?};

   {? wyjscie=0 || wyjscie:=exec('filtr_m','material',fm,'N','T',1) ?};

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   il_lin:=0;
   M.cntx_psh(); M.prefix()
}
{END:
   M.cntx_pop();
   mag_103(); VAR_DEL.delete('mag_103')
}
{IF: wyjscie}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{
{? tryb=1
|| PRN.add("kgr",8,'Grupa materiałowa'@);
   PRN.add("kin",20,'Indeks'@);
   PRN.add("kjm",5,'jm'@)
?};
PRN.add("w[1]",k[1],{? tryb=1 || '' || 'Indeks'@+' / ' ?}+'Symbol dokumentu'@);
PRN.add("w[2]",k[2],{? tryb=1 || '' || 'Nazwa materiału'@+' / ' ?}+'Numer dokumentu'@);
PRN.add("w[3]",k[3],{? tryb=1 || '' || 'jm'@+' / ' ?}+'Data dokumentu'@);
PRN.add("w[4]",k[4],'Stan początkowy'@,1);
PRN.add("w[5]",k[5],'Przychody'@,1);
PRN.add("w[6]",k[6],'Rozchody'@,1);
{? j2='T'
|| PRN.add("w[8]",5,'jm'@);
   PRN.add("w[9]",k[9],'Stan początkowy 2'@,1);
   PRN.add("w[10]",k[10],'Przychody 2'@,1);
   PRN.add("w[11]",k[11],'Rozchody 2'@,1)
?};
{? tryb<>1 || PRN.add("w[7]",k[7],'Stan końcowy'@,1) ?};

"--obliczenia--";
{? X_Xa.first()
|| X_Xb:=tab_tmp(1,
      'KTM' ,'STRING[50]'  ,'Indeks',
      'N'   ,'STRING[100]' ,'Nazwa',
      'JM'  ,'STRING[10]'  ,'jm',
      'SP'  ,'REAL'        ,'Stan początkowy',
      'P'   ,'REAL'        ,'Przychody',
      'R'   ,'REAL'        ,'Rozchody',
      'SK'  ,'REAL'        ,'Stan końcowy',
      'MGR' ,'STRING[8]'   ,'Grupa materiałowa',
      'J2'  ,'STRING[10]'  ,'jm',
      'SP2' ,'REAL'        ,'Stan początkowy',
      'P2'  ,'REAL'        ,'Przychody',
      'R2'  ,'REAL'        ,'Rozchody',
      'SK2' ,'REAL'        ,'Stan końcowy');
   X_Xc:=tab_tmp(1,
      'KTM' ,'STRING[50]'  ,'Indeks',
      'SYM' ,'STRING[20]'  ,'Symbol dok.',
      'NR'  ,'INTEGER'     ,'Nr dok.',
      'D'   ,'DATE'        ,'Data dok.',
      'SP'  ,'REAL'        ,'Stan początkowy',
      'P'   ,'REAL'        ,'Przychody',
      'R'   ,'REAL'        ,'Rozchody',
      'SK'  ,'REAL'        ,'Stan końcowy',
      'MGR' ,'STRING[8]'   ,'Grupa materiałowa',
      'SP2'  ,'REAL'        ,'Stan początkowy',
      'P2'   ,'REAL'        ,'Przychody',
      'R2'   ,'REAL'        ,'Rozchody',
      'SK2'  ,'REAL'        ,'Stan końcowy');
   X_Xc.index(X_Xc.ndx_tmp(,,'KTM',,,'KTM',,));
   _il:=$X_Xa.size();
   _ii:=0;
   {!
   |? M.seek(BB.sqlint(X_Xa.REF),);
      _ii+=1; echo('Trwają obliczenia dla materiału: %1 %2'@[M.KTM,M.N],'%1 z %2'@[$_ii,_il]);
      _j2:=M.J2<>null();
      X_Xb.blank(1);
      X_Xb.KTM:=M.KTM;
      X_Xb.N:=M.N;
      X_Xb.JM:=M.J().KOD;
      X_Xb.MGR:=M.MGR().KOD;
      X_Xb.J2:={? _j2 || M.J2().KOD || '' ?};

      OKR.cntx_psh();
      OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      || ND.cntx_psh(); DK.cntx_psh();
         _sum:=0;
         _sum2:=0;
         _cont:=1;
         {!
         |? _maska:=$OKR.ROK+2;
            {? _maska<>DK.name()+2
            || DK.use((DK.name()-2)+_maska);
               ND.use((ND.name()-2)+_maska)
            ?};
            DK.index('DOKMAT');
            DK.prefix('T',M.ref(),ST.MAG);
            {? DK.first()
            || {!
               |? _nd_d:=DK.N().D;
                  {? dod<>date(0,0,0) & dod<_nd_d
                  || _cont:=0
                  || {? _nd_d<odd
                     || "--stan poczatkowy--";
                        {? DK.PLUS='T' || X_Xb.SP +={? iw='W' || DK.WAR |? iw='I' || DK.IL ?};
                                          X_Xb.SP2+={? iw='I' & _j2 || DK.IL2 || 0 ?}
                        |? DK.PLUS='N' || X_Xb.SP -={? iw='W' || DK.WAR |? iw='I' || DK.IL ?};
                                          X_Xb.SP2-={? iw='I' & _j2 || DK.IL2 || 0 ?}
                        ?};
                        _sum:=X_Xb.SP;
                        _sum2:=X_Xb.SP2
                     |? odd<=_nd_d & (_nd_d<=dod | dod=date(0,0,0))
                     || "--przychody i rozchody--";
                        X_Xc.KTM:=X_Xb.KTM;
                        X_Xc.MGR:=X_Xb.MGR;
                        X_Xc.SYM:=DK.N().SYM;
                        X_Xc.NR:=ND.NR;
                        X_Xc.D:=ND.D;
                        X_Xc.SP:=X_Xc.P:= X_Xc.R:=X_Xc.SK:=0;
                        X_Xc.SP2:=X_Xc.P2:=X_Xc.R2:=X_Xc.SK2:=0;
                        {? DK.PLUS='T'
                        || {? iw='W' || X_Xb.P +=DK.WAR; X_Xc.P :=DK.WAR; _sum +=DK.WAR
                           |? iw='I' || X_Xb.P +=DK.IL;  X_Xc.P :=DK.IL;  _sum +=DK.IL;
                                        {? _j2 || X_Xb.P2+=DK.IL2;  X_Xc.P2:=DK.IL2;  _sum2+=DK.IL2 ?}
                           ?};
                           X_Xc.SK:=_sum;
                           X_Xc.SK2:=_sum2;
                           X_Xc.add()
                        |? DK.PLUS='N'
                        || {? iw='W' || X_Xb.R +=DK.WAR; X_Xc.R :=DK.WAR; _sum -=DK.WAR
                           |? iw='I' || X_Xb.R +=DK.IL;  X_Xc.R :=DK.IL;  _sum -=DK.IL;
                                        {? _j2 || X_Xb.R2+=DK.IL2;  X_Xc.R2:=DK.IL2;  _sum2-=DK.IL2 ?}
                           ?};
                           X_Xc.SK:=_sum;
                           X_Xc.SK2:=_sum2;
                           X_Xc.add()
                        ?}
                     ?};
                     DK.next()
                  ?}
               !}
            ?};
            _cont=1 & OKR.next()
         !};
         ND.cntx_pop(); DK.cntx_pop()
      ?};
      OKR.cntx_pop();
      "--saldo--";
      X_Xb.SK:=X_Xb.SP+X_Xb.P-X_Xb.R;
      X_Xb.SK2:=X_Xb.SP2+X_Xb.P2-X_Xb.R2;

      {? X_Xb.SP<>0 | X_Xb.P<>0 | X_Xb.R<>0 | X_Xb.SP2<>0 || X_Xb.add() ?};

      X_Xa.next()
   !};
   echo()
?};

PAR_WYDR.TITLE:='SZCZEGÓŁOWE ZESTAWIENIE OBROTÓW MAGAZYNOWYCH'@;
prn1:='PRN'
;~~}
{IF: var_pres('X_Xb')<100}{FUN.info('Brak danych spełniających kryteria.'@);~~}{FI}
{EXIT: var_pres('X_Xb')<100}
{IF: X_Xb.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Za okres: %1 - %2'@[(odd$1),(dod$1)]}
{IF: fm='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
{IF: iw='I'}{<{lmt+1}}{'Ilościowy'@}{ELIF: iw='W'}{<{lmt+1}}{'Wartościowy'@}{FI}
{IF:j2='T'}{<{lmt+1}}{'Druga jednostka ewidencyjna'@}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}
   {IF: ~tryb}{LINE}{FI}
   {<{lmt+1}}{'Parametry wydruku:'@}
   {<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
   {<{lmt+1}}{'Za okres: %1 - %2'@[(odd$1),(dod$1)]}
   {IF: fm='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
   {IF: iw='I'}{<{lmt+1}}{'Ilościowy'@}{ELIF: iw='W'}{<{lmt+1}}{'Wartościowy'@}{FI}
   {IF:j2='T'}{<{lmt+1}}{'Druga jednostka ewidencyjna'@}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   Ustalenie max dokladnosci dla danych z wydruku           {COMMENT-}
{X_Xb.cntx_psh(); M.cntx_psh();
 X_Xb.prefix(); __maxdok:=0;
 M.index('MATKTM');
 {? X_Xb.first()
 || {!
    |? M.prefix(X_Xb.KTM,);
       {? M.first() & M.DOKL>__maxdok || __maxdok:=M.DOKL ?};
       {? j2='T' & M.J2<>null()
       || _dokl_s:=exec('jaka_dok_mjm','jm',M.ref(),M.J2,M.J);
          {? _dokl_s>__maxdok || __maxdok:=_dokl_s ?}
       ?};
       X_Xb.next()
    !}
 ?};
X_Xb.cntx_pop(); M.cntx_pop();
~~}
{COMMENT}   drukowanie pozycji wydruku                               {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xb}
{DO}
   {FIRST}
   {
   kolejny:=0;
   doklsp:=doklp:= doklr:=doklsk:=__maxdok;

   {? doklsk<doklsp || doklsk:=doklsp ?};

   {? iw='W' & doklsp<2 || doklsp:=2 ?};
   {? iw='W' & doklp<2 || doklp:=2 ?};
   {? iw='W' & doklr<2 || doklr:=2 ?};
   {? iw='W' & doklsk<2 || doklsk:=2 ?};
   ~~}
   {FIRST-}
   {
   "--szacowanie ilosci lini pozycji--";
   _f:=prn1+'.ini_line()'; _v:=($(_f))();
   _f:=prn1+'.length()'; il_lin:=($(_f))()+1
   ;~~}
   {IF: ~tryb & lineleft()>0 & lineleft()<il_lin+2}
      { kolejny:=0; vp:=1;~~}
      {PAGE}
   {FI}
   {IF: tryb<>1}
      {FOOTER: 0}
         {SUBSTITUTE: 'LINIAF'}
         {LINE: 1}
      {FOOTER-}
      {IF: kolejny=1}{ rsep:=0;~~}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {FI}
   {
   w[1] :={? tryb<>1 || X_Xb.KTM || '' ?};
   w[2] :={? tryb<>1 || X_Xb.N || '' ?};
   w[3] :={? tryb<>1 || X_Xb.JM || '' ?};
   w[4] :=form(X_Xb.SP,,doklsp);
   w[5] :='';
   w[6] :='';
   w[7] :=form(X_Xb.SP,,doklsk);
   kin  :=form(X_Xb.KTM);
   kgr  :=form(X_Xb.MGR);
   kjm  :=form(X_Xb.JM);
   w[8] :=form(X_Xb.J2);
   w[9] :={? X_Xb.J2<>'' || form(X_Xb.SP2,,doklsp) || '' ?};
   w[10]:='';
   w[11]:=''
   ;~~}
   {IF: tryb<>1}{FONT: 'T8GB'}{SPACE: 'C'}{FI}
   {SUBSTITUTE: 'LINIA0'}
   {IF: tryb<>1 & X_Xb.J2<>''}
      {
      rsep:=0;
      w[1] :='';
      w[2] :='';
      w[3] :=X_Xb.J2;
      w[4] :=form(X_Xb.SP2,,doklsp);
      w[5] :='';
      w[6] :='';
      w[7] :=form(X_Xb.SP2,,doklsk)
      ;~~}
      {SUBSTITUTE: 'LINIA0'}
      { rsep:=PAR_WYDR.RSEP;~~}
   {FI}
   { kolejny:=1; rsep:=PAR_WYDR.RSEP;~~}
   {FOR: X_Xc}
   {PREFIX: X_Xb.KTM, X_Xb.KTM}
   {DO}
      {
      w[1] :=X_Xc.SYM;
      w[2] :=form(X_Xc.NR);
      w[3] :=form(X_Xc.D);
      w[4] :='';
      w[5] :={? X_Xc.P<>0 || form(X_Xc.P,,doklp) || '' ?};
      w[6] :={? X_Xc.R<>0 || form(X_Xc.R,,doklr) || '' ?};
      w[7] :=form(X_Xc.SK,,doklsk);
      kin  :=form(X_Xb.KTM);
      kgr  :=form(X_Xb.MGR);
      kjm  :=form(X_Xb.JM);
      w[8] :=form(X_Xb.J2);
      w[9] :='';
      w[10]:={? X_Xc.P2<>0 || form(X_Xc.P2,,doklp) || '' ?};
      w[11]:={? X_Xc.R2<>0 || form(X_Xc.R2,,doklr) || '' ?}
      ;~~}
      {FONT: 'T8G'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {IF: tryb<>1 & X_Xb.J2<>''}
         {
         rsep:=0;
         w[1] :='';
         w[2] :='';
         w[3] :=X_Xb.J2;
         w[4] :='';
         w[5] :={? X_Xc.P2<>0 || form(X_Xc.P2,,doklp) || '' ?};
         w[6] :={? X_Xc.R2<>0 || form(X_Xc.R2,,doklr) || '' ?};
         w[7] :=form(X_Xc.SK2,,doklsk)
         ;~~}
         {SUBSTITUTE: 'LINIA0'}
         { rsep:=PAR_WYDR.RSEP;~~}
      {FI}
   {OD}
   {IF: tryb<>1}
      {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
      {FOOTER}{FOOTER-}
      {SUBSTITUTE: 'LINIAF'}
      {
      w[1] :='';
      w[2] :='Razem %1 [%2]:'@[X_Xb.KTM,X_Xb.JM];
      w[3] :='';
      w[4] :=form(X_Xb.SP,,doklsp); s[1]+=X_Xb.SP;
      w[5] :=form(X_Xb.P,,doklp);  s[2]+=X_Xb.P;
      w[6] :=form(X_Xb.R,,doklr);  s[3]+=X_Xb.R;
      w[7] :=form(X_Xb.SK,,doklsk); s[4]+=X_Xb.SK;
      PRN.n_frame(); PRN.FORM[2]:=0
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {IF: tryb<>1 & X_Xb.J2<>''}
         {
         rsep:=0;
         w[1] :='';
         w[2] :='[%1]:'@[X_Xb.J2];
         w[3] :='';
         w[4] :=form(X_Xb.SP2,,doklsp);
         w[5] :=form(X_Xb.P2,,doklp);
         w[6] :=form(X_Xb.R2,,doklr);
         w[7] :=form(X_Xb.SK2,,doklsk);
         PRN.n_frame(); PRN.FORM[2]:=0
         ;~~}
         {SUBSTITUTE: 'LINIA0'}
         { PRN.s_frame(); PRN.FORM[2]:=1;~~}
         { rsep:=PAR_WYDR.RSEP;~~}
      {ELSE}
         { PRN.s_frame(); PRN.FORM[2]:=1;~~}
         { rsep:=PAR_WYDR.RSEP;~~}
      {FI}
   {FI}
{OD}
{COMMENT}   podsumowanie jesli wydruk wartosciowy                    {COMMENT-}
{IF: ~tryb}
   {IF: iw='I'}
      {FOOTER}{FOOTER-}
   {ELIF: iw='W'}
      {IF: lineleft()=0}
         { vp:=1;~~}
         {SUBSTITUTE: 'LINIAF'}
      {FI}
      {FOOTER}{FOOTER-}
      {
      PRN.n_frame();
      w[1] :='';
      w[2] :='Razem:'@; PRN.FORM[2] :=0;
      w[3] :='';
      w[4] :=form(s[1],,doklsp,'.,');
      w[5] :=form(s[2],,doklp,'.,');
      w[6] :=form(s[3],,doklr,'.,');
      w[7] :=form(s[4],,doklsk,'.,')
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
{FI}
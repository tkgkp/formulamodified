:!UTF-8
{TITLE:Lista płac (paski) + RMUA}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','C',,,,,,1,5,5,5,5,0,0}
{BEGIN:
   'prolog';
   VAR_DEL.delete('par','Banknoty','max','nom','col','row','kwota','Text','LPrac','LWydz','__P','__param');
   FUNKCJE.TBEGIN();
   Banknoty:=" form(Nominal[_a],10,2)+':'+
               form(Tbank[_a],10)+'  '+(10*'─')+
               form(Nominal[_a]*Tbank[_a],13,2)
             ";

   par:=obj_new(13);
   par[1]:=obj_new(20);
   par[2]:=obj_new(6);
   par[6]:=obj_new(8);
   par[10]:=obj_new(8);
   par[13]:=obj_new(6);

   KP.cntx_psh();
   KP.index('_KARTAPO');
   LISTA.Update();
   max:=FUNKCJE.TCOLUMNS()*37;
   nom:=FUNKCJE.NOMINAL(200);
   FUNKCJE.TFREE();

   __param:=tab_tmp(1,
             'INFPOD','INTEGER','Informacje podatkowe',
             'RMUA','INTEGER','Informacje ubezpieczeniowe (ZUS RMUA)',
             'KARTURL','INTEGER','Karta urlopowa');
   __param.INFPOD:=__param.RMUA:=__param.KARTURL:=0;
   __param.add();
   _edit:=__param.mk_edit('Parametry wydruku'@,0,'ppl_listprmu');
   __param.win_esep(_edit,'Dodatkowe informacje na wydruku'@);
   __param.win_efld(_edit,,'INFPOD',,,,,,'Drukować'@,,,'check-box','check_label="%1"' ['Informacje podatkowe'@],"1","0");
   __param.win_efld(_edit,,'RMUA',,,,,,'Drukować'@,,,'check-box','check_label="%1"' ['Informacje ubezpieczeniowe (ZUS RMUA)'@],"1","0");
   __param.win_efld(_edit,,'KARTURL',,,,,,'Drukować'@,,,'check-box','check_label="%1"' ['Karta urlopowa'@],"1","0");
   exec('ok_esc','#window',__param,_edit);
   __param.win_edit(_edit);
   __param.edit("");
   col:=row:=0;
   kwota:=0;
   Text:='';
   LPrac:=LWydz:=0;
   _data:=date(O.R,O.M,0);

   P.cntx_psh();
   P.clear();
   H.cntx_psh();
   H.index('_HISTKOD');
   UD_SKL.cntx_psh();
   UD_SKL.clear();
   _upr:=exec('uprawnieniawrap','pkd',
               'Naliczonych składnikach płacowych'@,
               'PPL_PLL_RSKP',
               'PPL_PLL_PSKP'
             );
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   ||  __SKL:=exec('plaski_ud_def','schemat',,'PPL','PODZORG',exec('ref_firma','ustawienia'));

       _key:=__SKL.index('?');
      __SKL.index(__SKL.ndx_tmp(,,'INT_REF',,));

      __P:=sql(
         'select   0 WYDZIAL, '
         '         case '
         '         when P.DZA<=to_date(:_a) then '
         '            case '
         '            when P.DZ is null then \'T\' '
         '            when P.DZ<to_date(:_a) then \'N\' '
         '            else \'T\' '
         '            end '
         '         else \'N\' '
         '         end Z, '
         '         OSOBA.NAZWISKO, '
         '         OSOBA.PIERWSZE, '
         '         P.REFERENCE as P_REF, '
         '         0 MBREF '
         'from     P join OSOBA '
         'where    P.FIRMA=:_c and P.F_ZATR=:_b '
         'order by P_REF',
         _data,
         O.F_ZATR,
         exec('ref_firma','ustawienia')
      );

      {? var_pres('__P')=type_of(SYSLOG)
      || _test:=
         {? exec('is_active','gp_api')
         || "exec('has_access_p_h','gp_api',,_a,'PPL')"
         || "1"
         ?};
         {? __P.first()
         || {!
            |? {? P.seek(__P.P_REF) & _test(_data)
               || H.prefix(P.ref(),'Z');
                  __P.MBREF:=P.ref();
                  {? H.find_le(_data) &
                     __SKL.find_key(#H.WYDZIAL)
                  || __P.WYDZIAL:=H.WYDZIAL
                  || __P.WYDZIAL:=P.WYDZIAL
                  ?};
                  __P.put()
               ?};
               __P.next()
            !};
            __NDX:=__P.ndx_tmp(,,
               'WYDZIAL',,0,
               'Z',,0,
               'NAZWISKO',,0,
               'PIERWSZE',,0,
               'MBREF',,0
            );
            __P.index(__NDX)
         ?}
      ?};
      __SKL.index(_key)
   ?}
}
{END: 'epilog';
   {? var_pres('__P')=type_of(SYSLOG)
   || obj_del(__P);
      &__P
   ?};
   {? var_pres('__SKL')=type_of(SYSLOG)
   || obj_del(__SKL);
      &__SKL
   ?};
   P.cntx_pop();
   H.cntx_pop();
   KP.cntx_pop();
   UD_SKL.cntx_pop();
   FUNKCJE.TEND();
   FUNKCJE.NOMINAL();
   VAR_DEL.delete('par','Banknoty','max','nom','col','row','kwota','Text','LPrac','LWydz','__param')
}
{COMMENT}OLD: listprmu.rpm{COMMENT-}
{COMMENT}OLD: rmua.rpi{COMMENT-}
{COMMENT}OLD: kart_url.rpi{COMMENT-}
{EXIT: var_pres('__P')<>type_of(SYSLOG)}
{EXIT: LISTA.__Fatal}
{INCLUDE: p_rmua.rpi}
{INCLUDE: p_kart_url.rpi}
{HEADER}
  {STR.split(KST.NAZWA);STR.line(max-5-(+(KST.MIASTO+', dn. '+$date())))}  {>{max}KST.MIASTO+', dn. '+$date()}
  {IF: STR.next()}{STR.line(max-5)}{FI}
  {'NIP: '@}{KST.NIP}   {'REGON: '@}{KST.REG}
  {<3>{max}'W Y D R U K   L I S T Y   P Ł A C'@}
  {<3>{max}'Lista płac '@+VAR.NAZWALIS+' ('+date(O.R,O.M,1)$8+') nr '+$O.N}
{IF: +O.O}
  {<3>{max}O.O}
{FI}

{HEADER-}
{FOOTER:0 }
{max*'─'}
{>{max}'str. '@+$page}
{FOOTER-}
{COMMENT}

   Makrodefinicje do formatowania składników wypłaty pracownika i zbiorówek
   jednostek i zakładu. __nazwa(20 znaków)__kwota(13 znaków)

{COMMENT-}
{MACRO: 'prac'}{
   '  '+(_a:=form(FUNKCJE.TPRAC(col,row,2),20))+
   '  '+{? +(|_a) & FUNKCJE.TPRAC(col,row,1)
           || form(FUNKCJE.TPRAC(col,row,3),13,2)
           || 13*' '
        ?}
}{MACRO-}
{MACRO: 'wydz'}{
   '  '+(_a:=form(FUNKCJE.TWYDZ(col,row,2),20))+
   '  '+{? +(|_a) & FUNKCJE.TWYDZ(col,row,1)
           || form(FUNKCJE.TWYDZ(col,row,3),13,2)
           || 13*' '
        ?}
}{MACRO-}
{MACRO: 'zakl'}{
   '  '+(_a:=form(FUNKCJE.TZAKL(col,row,2),20))+
   '  '+{? +(|_a) & FUNKCJE.TZAKL(col,row,1)
           || form(FUNKCJE.TZAKL(col,row,3),13,2)
           || 13*' '
        ?}
}{MACRO-}
{MACRO: 'inf_pod'}
{  KP.prefix(P.ref(),O.RP);
   {! _ix:=1..4 |! par[13][_ix]:=0 !};
   par[13][6]:=date(O.RP,O.MP,1);
   {? KP.first()
   || par[13][5]:=date(KP.R,KP.M,1);
      par[13][5]:={? par[13][5]<P.DZA || P.DZA || par[13][5] ?};
      {!
      |? {? KP.M<O.MP
         || par[13][1]+=KP.S1;
            par[13][2]+=KP.S2;
            par[13][4]+=KP.S4;
            par[13][6]:=date(KP.R,KP.M,0)
         ?};
         KP.next()
      !};
      par[13][3]:=par[13][1]-par[13][2];
      {? par[13][3]<0 || par[13][3]:=0 ?}
   ?};
~~}
{IF: par[13][1]}
{max*'─'}

{'Informacje podatkowe za okres: '@}{par[13][5]} - {par[13][6]}

{<3}{'Przychód: '@}{form(par[13][1],,2)}{<39}{'Koszty uzyskania: '@}{form(par[13][2],,2)}{<75}{'Dochód: '@}{form(par[13][3],,2)}{<111}{'Zal. pod. doch.: '@}{form(par[13][4],,2)}
{FI}
{MACRO-}
{COMMENT}

   Przeglądaj wszystkie jednostki, i dla każdego z nich przeglądaj kartotekę
   osobową. Dla każdego z nich pobieraj składniki wypłaty. Jeżeli są jakieś,
   to drukuj pasek "wypłaty" pracownika.

{COMMENT-}
{FONT: 'm'}
{ maxline:=lineleft; ~~ }
{FOR: __SKL}{DO}
{  UD_SKL.seek(__SKL.SQL_REF);
   FUNKCJE.TWYDZ();
   LWydz:=0;
   ~~
}
{FOR: __P}{INDEX: __NDX}{PREFIX: __SKL.INT_REF}{DO}
{IF: P.seek(__P.MBREF,P.name())}
{  echo('Lista: '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE);
   FUNKCJE.TPRAC();
   col:=row:=1;
   ~~
}
{COMMENT}

   S K Ł A D N I K I   W Y P Ł A T Y   P R A C O W N I K A

{COMMENT-}
{IF: FUNKCJE.TROWS('Prac')}
{ENTIRE}
{ LPrac+=1; LWydz+=1; max*'─'}

{FONT: 'n'}{form(LPrac,-5,,'9.')} ({form(LWydz,-5,,'9.')})  {P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE}   {'nr akt '@}{|P.T}   {'Jednostka org.: '@}{P.WYDZIAL().SYMBOL}{FONT: 'm'}
               {'Lista płac za '@}{date(O.R,O.M,1)$8}
{IF: O.D<>date(0,0,0)}{'Wypłacona dnia '@}{O.D$1}{FI} {IF: {? O.DKURS<>date(0,0,0) || exec('h_find','pracownik'); H.CZYWAL='T' | H.CZYWAL2='T' | H.CZYWAL3='T'  ?}
}      {'Kurs z dnia '@}{O.DKURS}: {H.WAL(); exec('waluta_miara','lista_licz',SLO.KOD)} {SLO.KOD} = {exec('waluta_kurs','lista_licz',H.ZWAL,O.DKURS,H.WAL)} PLN  {FI}

{WHILE: row<=FUNKCJE.TROWS('Prac')}{DO}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'prac'}{ col+=1; ~~ }{OD}
{ col:=1; row+=1; ~~ }
{OD}
{ col:=1; row:=0; ~~ }
{FUNKCJE.TCOLUMNS()*('  '+(35*'═'))}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'prac'}{ col+=1; ~~ }{OD}
{  FUNKCJE.TBANK(kwota:=FUNKCJE.TPRAC(FUNKCJE.TCOLUMNS(),0,3)); ~~ }
{IF: __param.INFPOD}
     {SUBSTITUTE: 'inf_pod'}
{FI}
{IF: __param.RMUA}
     {SUBSTITUTE: 'inf_ubezp'}
     {ENTIRE-}
     {ENTIRE}
{FI}
{IF: __param.KARTURL}
     {SUBSTITUTE: 'kart_url'}
{FI}
{ENTIRE-}
{IF: __param.RMUA & maxline<>lineleft}
   {PAGE}
{FI}
{FI}
{FI}
{OD}
{COMMENT}

   S K Ł A D N I K I   Z B I O R Ó W K I   W Y D Z I A Ł U

{COMMENT-}
{ col:=row:=1; ~~ }
{IF: FUNKCJE.TROWS('Wydz')}
{IF: ~__param.RMUA & PAR_SKID.get(193)='T'}
   {PAGE}
{FI}
{ENTIRE}
{max*'─'}

  {'Podsumowanie cząstkowe dla jednostki: '@}{UD_SKL.SYMBOL}  {UD_SKL.OPIS}
  {'Lista płac '@}{VAR.NAZWALIS+' ('+date(O.R,O.M,1)$8+')'}
  {'Razem pracowników: '@}{LWydz}

{WHILE: row<=FUNKCJE.TROWS('Wydz')}{DO}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'wydz'}{ col+=1; ~~ }{OD}
{ col:=1; row+=1; ~~ }
{OD}
{ col:=1; row:=0; ~~ }
{FUNKCJE.TCOLUMNS()*('  '+(35*'═'))}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'wydz'}{ col+=1; ~~ }{OD}
{ENTIRE-}
{IF: PAR_SKID.get(193)='T'}{PAGE}{FI}
{FI}
{OD}
{COMMENT}

   {'S K Ł A D N I K I   Z B I O R Ó W K I   Z A K Ł A D U'@}

{COMMENT-}
{ENTIRE}
{ col:=row:=1; ~~ }
{IF: FUNKCJE.TROWS('Zakl')}
{max*'─'}

  {'Podsumowanie końcowe dla uwzględnionych jednostek'@}
  {'Razem pracowników: '@}{LPrac}

{WHILE: row<=FUNKCJE.TROWS('Zakl')}{DO}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'zakl'}{ col+=1; ~~ }{OD}
{ col:=1; row+=1; ~~ }
{OD}
{ col:=1; row:=0; ~~ }
{FUNKCJE.TCOLUMNS()*('  '+(35*'═'))}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'zakl'}{ col+=1; ~~ }{OD}
{ kwota:=FUNKCJE.TZAKL(FUNKCJE.TCOLUMNS(),0,3); ~~ }

{SPACE:'c'}
{>{max-50}'Nominały:'@}
{WHILE: nom>0}{DO}
{IF: Tbank[nom]}{>{max}Banknoty(nom)}{FI}
{ nom-=1; ~~ }
{OD}
{>{max}(13*'═')}
{>{max}form(kwota,13,2)}
{SPACE:'@'}


{SPACE:'c'}
{>{max}'Sprawdzono pod względem merytorycznym: '@+(20*'_')}
{SPACE:'@'}
Zatwierdzono do wypłaty: {,2kwota} zł
słownie: #{STR.kwota_sł(kwota)}#

{SPACE:'c'}
{<1>{max}'Główny księgowy: '@+(20*'_')}{>{max}'Osoba zatwierdzająca: '@+(20*'_')}
{SPACE:'@'}
{FI}
{ENTIRE-}
{COMMENT}

   Koniec definicji sprawozdania

{COMMENT-}
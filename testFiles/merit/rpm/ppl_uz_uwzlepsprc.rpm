:!UTF-8
{TITLE:Wydruk składnika wypłaty}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('rsep','lmt','vp','prn1','par','tresc','Edit_ok','__oblicz','__edit','__param');
   tresc:='zlepsprc';
   PAR_WYDR.TITLE:='Wykaz wybranych składników wypłaty z umów zleceń'@;

   par:=obj_new(9);
   par[9]:=rep_export();
   {? par[9]
   || R.cntx_psh();
      R.prefix();
      _maxcol:=R.size();
      R.cntx_pop()
   || _maxcol:=10
   ?};
   Edit_ok:=0;
   P.cntx_psh();
   LS.cntx_psh();
   O.cntx_psh();
   RH.cntx_psh;
   _upr:=exec('uprawnieniawrap','pkd','Rachunkach'@,'PPL_ZLC_RRAC','PPL_ZLC_PRAC');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'DATA','INTEGER','Wybrana data',
         'OD','DATE','Data od',
         'DO','DATE','Data do'
      );
      __param.OD:=date(,,1);
      __param.DO:=date(,,0);

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'uz_uwzlepsprc');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'DATA',,,,,,'Okres przygotowania wydruku wg'@,,
         'Data wg której filtrowane będą umowy'@,'radio-buttons',,'Daty wypłaty'@,"1",'Daty rachunku'@,"2");
      __param.win_efld(__edit,,'OD',,,11,,,'Data od'@,,'Data umowy od');
      __param.win_efld(__edit,,'DO',,,11,,,'Data do'@,,'Data umowy do');
      __param.efld_opt(__edit,'mark=1',,'OD');
      __param.efld_opt(__edit,'mark=1',,'DO');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? Edit_ok:=__param.edit(
            "  {? __param.DO=#0 | __param.OD=#0 | __param.OD>__param.DO
               || FUN.emsg('\nDaty muszą być wypełnione.\n'+
                           '\nData początku okresu nie może być większa od daty końcowej.\n'@);
                  {? __param.OD=#0 || 'OD' || 'DO' ?}
               |? __param.DO~1<1901 | __param.OD~1<1901
               || FUN.emsg('Daty muszą być późniejsze niż z 1900 roku.'@);
                  {? __param.OD~1<1901 || 'OD' || 'DO' ?}
               || 1
               ?}
            "
         )
      || __oblicz:=
            "  _rh:=sql(
                  'select '
                  '  O.LT as LT, '
                  '  RH.REFERENCE as RH, '
                  '  RH.FIRMA as FIRMA, '
                  '  ZC.OSOBA as OSOBA, '
                  '  ZC.P as P, '
                  '  RU.K as K, '
                  '  RH.DWY, '
                  '  RH.DRA '
                  'from '
                  '  RH join '
                  '  ZC using(RH.ZLE,ZC.REFERENCE) join '
                  '  RU using(ZC.RU,RU.REFERENCE) join '
                  '  O using(RH.O,O.REFERENCE) '
                  'where to_date(:_a)<='+{? __param.DATA=1 || 'RH.DWY' || 'RH.DRA' ?}+' and '
                  '     '+{? __param.DATA=1 || 'RH.DWY' || 'RH.DRA' ?}+'<=to_date(:_b) '
                  ' and RH.FIRMA = :_c '
                  ' and OSOBA = :_d '+
                  {? __param.DATA=1 || 'order by RH.DWY' || 'order by RH.DRA' ?},
                  __param.OD,
                  __param.DO,
                  exec('ref_firma','ustawienia'),
                  P.OSOBA
               );
               _wyn:=0;

               {? _rh.first()
               || {!
                  |? {? _rh.P=$P.ref()
                     || LS.use(_rh.LT);
                        LS.index('ZLEC');
                        _rh_ref:=exec('FindAndGet','#table',RH,_rh.RH,,\"ref()\",null());
                        LS.prefix(_rh_ref,_a);
                        {? LS.first()
                        || {!
                           |? _wyn+=LS.KW$2;
                              LS.next()
                           !}
                        ?}
                     ?};
                     _rh.next()
                  !}
               ?};
               _wyn
            ";
         {? Edit_ok & (_rub:=exec('code_grp','profile',,tresc,,_maxcol))<>''
         || RH.index('RACHDATA');
            par[1]:=obj_new(@.CLASS.PRINT,_maxcol+3);
            {? ~par[9]
            || par[1].add("par[4]",5,'Lp.'@,1,,,,,',,\'9.\'');
               par[6]:=obj_new(@.CLASS.PRINT,_maxcol+1);
               par[6].add("'Razem'@",61,'Razem'@)
            ?};
            par[1].add("par[3]:=0; P.OSOBA().NAZWISKO",30,'Nazwisko'@);
            par[1].add("OSOBA.PIERWSZE",20,'Imię'@);
            par[7]:=obj_new(_maxcol);
            {! _n:=1.._maxcol
            |! par[7][_n]:=0
            !};
            par[2]:='';
            STR.split(1-_rub-1,',');
            {! _cnt:=1.._maxcol
            |? _kod:=#STR.get_word()
            |! {? __RUB.ref(_kod)
               || _sx:='par[7]['+$_cnt+']';
                  _fx:='_ax:=__oblicz('+$_kod+'); par[3]+=(_ax<>0);'+_sx+'+=_ax;_ax';
                  par[1].add($_fx,12,__RUB.RT[_kod],1,,,,,'12,2,\'9,\'');
                  {? ~par[9] || par[6].add($_sx,12,__RUB.RT[_kod],1,,,,,'12,2,\'9,\'') ?};
                  par[2]+=', '+$_kod
               ?}
            !};

            par[1].s_frame();
            {? ~par[9] || par[6].s_frame() ?};
            prn1:='par[1]';
            rsep:=PAR_WYDR.RSEP;
            lmt:=par[3]:=par[8]:=0;
            vp:=par[4]:=par[5]:=1
         || Edit_ok:=0
         ?};
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?};
   ~~
}
{END:
   P.cntx_pop();
   LS.cntx_pop();
   O.cntx_pop();
   RH.cntx_pop();
   VAR_DEL.delete('rsep','lmt','vp','prn1','par','tresc','Edit_ok','__oblicz','__edit','__param')
}
{EXIT: ~Edit_ok}
{COMMENT}OLD: zlepsprc.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzialz.rpi{COMMENT-}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Filtrowanie wg: '@+{? __param.DATA=1 || 'Data wypłaty'@ || 'Data rachunku'@ ?}}
   {<{lmt+1}'Zakres dat: '@+($__param.OD+' - '+$__param.DO)}
   {<{lmt+1}'Uwzględniane składniki: '@+(1-par[2])}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{ par[8]:=0; ~~}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~par[9]}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: par[1].ini_line(); par[3]}
   { par[4]+=1; ~~ }
   {WHILE: par[1].next()}{DO}
      {IF: rsep & par[8]}
         {IF: lineleft()=1}
            {PAGE}
         {ELSE}
            {REP: _f:=prn1+'.fcbar(lmt)'; ($(_f))()}
         {FI}
      {FI}
      {par[8]:=1; ~~}
      {REP: par[1].fline(lmt)}
   {OD}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{IF: ~par[9] & par[4]>1}
   {IF: lineleft() & lineleft()<=2}
      {PAGE}
   {FI}
   {FOOTER: 0}
   {FOOTER-}
   {REP: '{<{lmt+1}}'+par[1].fjbar(par[6])}
   { par[6].ini_line(); ~~ }
   {WHILE: par[6].next()}{DO}
      {REP: par[6].fline(lmt)}
   {OD}
   {REP: par[6].flbar(lmt)}
{FI}
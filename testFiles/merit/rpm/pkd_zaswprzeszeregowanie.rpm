:!UTF-8
{TITLE:Przeszeregowanie}
{PRINTER: exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PXX_ZASWPLAC')='T',,,,'I','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='';
   VAR_DEL.delete('tresc','cnt','indent','Text','par','param','__CALL','__PRAC');
   tresc:='zaswplac';
   P.cntx_psh();
   H.cntx_psh();
   H.index('_HISTKOD');

   param:=obj_new(3);
   par:=params_get();
   {? type_of(par)=type_of(~~)
   || par:=obj_new('P','DALEJ')
   ?};
   {? var_pres('P',par)<0
   || _par:=exec('obj_ntab_set','#array',par,'P',~~);
      obj_del(par);par:=_par; obj_del(_par)
   ?};
   {? var_pres('DALEJ',par)<=0
   || _par:=exec('obj_ntab_set','#array',par,'DALEJ','');
      obj_del(par);par:=_par; obj_del(_par);
      par.DALEJ:=exec('uprawnieniawrap','pkd',
         'Przebiegu zatrudnienia'@,
         'PKD_EZK_PPZA',
         'PKD_EZK_RPZA'
      );
      {? par.DALEJ=''
      || par.P:=exec('przeszeregowanie','!pkd_zes_orza')
      || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@ [par.DALEJ])
      ?}
   ?};
   cnt:=indent:=0;
   Text:='';
    __xr_ins:=exec('new_ins','rap_zew','PXX_ZASWPLAC')
}
{END:
   H.cntx_pop();
   P.cntx_pop();
   exec('del_ins','rap_zew',&__xr_ins);
   VAR_DEL.delete('tresc','cnt','indent','Text','par','param','__CALL','__PRAC')
}
{COMMENT}OLD: zaswplac.rpm{COMMENT-}
{COMMENT}OLD: zaswplac.rpi{COMMENT-}
{EXIT: +par.DALEJ}
{EXIT: ~par.P.size()}
{EXIT: ~__xr_ins}
{MACRO: tresc}
{IF: {? +par.P.P & par.P.H=''
     || {? P.seek(par.P.P,,1)
        || H.prefix(P.ref(),'Z');
           {? par.P.H=''
           || H.find_le(param[3]) & H.OD>=param[2]
           ?}
        ?}
     |? par.P.P='' & +par.P.H
     || {? H.seek(par.P.H,,1)
        || param[1]:=H.PODPIS;
           param[2]:=H.OD;
           param[3]:=date();
           _prac:=H.P;
           _od:=H.OD;
           H.prefix(_prac,'Z');
           H.find_key(_od)
        ?}
     || 0
     ?}}
{  _s1:=H.S1;
   _s2:=H.S2;
   _s3:=H.S3;
   _s4:=H.S4;
   _st:=H.ST;
   _od:=H.OD;
   _waluta:={? H.CZYWAL='T'   || H.WAL().KOD || 'zł' ?};
   _waluta2:={? H.CZYWAL2='T' || H.WAL().KOD || 'zł' ?};
   _waluta3:={? H.CZYWAL3='T' || H.WAL().KOD || 'zł' ?};
   _waluta4:={? H.CZYWAL4='T' || H.WAL().KOD || 'zł' ?};
   _s2tn:=H.S2T; _s2pn:=H.S2P().NAZWA;
   _s3tn:=H.S3T; _s3pn:=H.S3P().NAZWA;

   _stannew:=H.ST().ST;
   _charprac:=H.CP().S;

   {? H.prev()
      &
      (_walprev:={? H.CZYWAL='T'   || H.WAL().KOD || 'zł' ?};
       _walpr2:={? H.CZYWAL2='T' || H.WAL().KOD || 'zł' ?};
       _walpr3:={? H.CZYWAL3='T' || H.WAL().KOD || 'zł' ?};
       _walpr4:={? H.CZYWAL4='T' || H.WAL().KOD || 'zł' ?};
       _s2tp:=H.S2T; _s2pp:=H.S2P().NAZWA;
       _s3tp:=H.S3T; _s3pp:=H.S3P().NAZWA;
       _waluta<>_walprev | _waluta2<>_walpr2 | _waluta3<>_walpr3 | _s1<>H.S1 | _s2<>H.S2 | _s3<>H.S3 | _s4<>H.S4 | _st<>H.ST
       | _s2tn<>_s2tp | _s2pn<>_s2pp | _s3tn<>_s3tp | _s3pn<>_s3pp)
   || _stanowis:=H.ST().ST;
      exec('ins_par','rap_zew',__xr_ins,cnt+=1,
         'F_NAZWA',KST.NAZWA,
         'MIEJSCE',PAR_WYDR.MIEJSC,
         'DATA', date()$4,
         'F_ULICA', exec('ulica','stalesys'),
         'F_POCZTA',exec('poczta','stalesys'),
         'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?});
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'NAZWISKO',P.OSOBA().NAZWISKO,
         'IMIE',OSOBA.PIERWSZE,
         'DRUGIEIM',OSOBA.DRUGIE,
         'PLEC',OSOBA.PLEC,
         'STANOWIS',_stanowis,
         'STANNEW',_stannew,
         'CHARPRAC',_charprac,
         'TECZKA',|P.T,
         'DATADEC',{? param[1]<>#0 || param[1]$6 || '' ?},
         'OD_DNIA',{? _od<>#0 || _od$6 || '' ?});
      _prec:={? _s1=int(_s1) & _s2=int(_s2) & _s3=int(_s3) || 0 || 2 ?};
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'WALUTA',_waluta,
         'WALUTA2',_waluta2,
         'WALUTA3',_waluta3,
         'WALUTA4',_waluta4,
         'S1',form(_s1,,_prec),
         'Z1',{? _s1<>H.S1 | _waluta<>_walprev || 'T' || 'N' ?},
         'S2',form(_s2,,_prec),
         'Z2',{? _s2<>H.S2 | _waluta2<>_walpr2 | _s2tn<>_s2tp | _s2pn<>_s2pp || 'T' || 'N' ?},
         'S3',form(_s3,,_prec),
         'Z3',{? _s3<>H.S3 | _waluta3<>_walpr3 | _s3tn<>_s3tp | _s3pn<>_s3pp || 'T' || 'N' ?},
         'S4',form(_s4,,_prec),
         'Z4',{? _s4<>H.S4 | _waluta4<>_walpr4 || 'T' || 'N' ?});
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'S2T',_s2tn,
         'S2P',_s2pn,
         'S3T',_s3tn,
         'S3P',_s3pn)
   || FUN.emsg('Bieżący zapis nie jest przeszeregowaniem.')
   ?}; ~~
}
{FI}
{MACRO-}
{FOR: par.P}
    {DO}
       {SUBSTITUTE: tresc}
    {OD}
{EXIT: ~exec('ready','rap_zew',__xr_ins)}
{IF: (cli_ver()='jterm' | exec('cli_ver','#system')='interm') & exec('rep_set','rap_zew','PXX_ZASWPLAC',1)='T'}
   {FOR: par.P}
   {DO}
      { _zaswplac:=obj_new('P','H','data_decyzji','data_od','data_do');
        _zaswplac.P:=par.P.P;
        _zaswplac.H:=par.P.H;
        _zaswplac.data_decyzji:=param[1];
        _zaswplac.data_od:=param[2];
        _zaswplac.data_do:=param[3];
        _exe:={? var_pres('SERIA',par)=type_of('') & par.SERIA='T' || -1 || 3 ?};
        _szab:={? _exe<0 || par.SZABLON || ~~ ?};
        params_set('zaswplac',_zaswplac);
        _ret:=exec('wypelnij','szablon','PXX_ZASWPLAC',_szab,,_exe);
        {? _exe<0 || par.SERIA_RET.STATUS:=_ret.STATUS; par.SERIA_RET.INFO:=_ret.INFO ?}; ~~
      }
   {OD}
   {EXIT}
{FI}
{IF: cli_ver()='jterm' & exec('rep_set','rap_zew','zaswplac')='T'}
   { exec('use_tmp','rap_zew','zaswplac'); ~~ }
   {EXIT}
{ELSE}
   { __CALL:=proc_exec('plac@zasw',#__xr_ins); ~~ }
   {INCLUDE: p_zaswplac.rpi}
{FI}
:!UTF-8
{TITLE:Informacja o warunkach pracy}
{VERSION: PEP}
{PRINTER: dziedzina:='PKD';
   exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PKD_ZASWWARP')='T',,,,'I','B',,,,,,1,
   PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='';
   VAR_DEL.delete('Text','cnt','__CALL','__KA','dalej','czy_szablon','czy_paperless','czy_pep','__ppsf');
   VAR_DEL.delete('__CALL2','__xr_ins');
   _par:=params_get();
   czy_szablon:=exec('rep_set','rap_zew','PKD_ZASWWARP',1)='T';
   czy_pep:=var_pres('_par')>100 & var_pres('PEP',_par)>0;
   KART_URL.cntx_psh();
   KART_URL.index('PRAC_ROK');
   H.cntx_psh();
   H.index('_HISTKOD');
   P.cntx_psh();
   P.clear();
   __ppsf:=exec('is_pzd01','ppsf');
   {? __ppsf
   || Cntx.psh(PPSF_H,PPSF_HD,PPSF_ADR);
      PPSF_H.index('PRAC_AKT');
      PPSF_HD.index('UNIQ_DT');
      PPSF_ADR.index('PRAC_AK')
   ?};
   dalej:=exec('uprawnieniawrap','pkd','Przebiegu zatrudnienia'@,'PKD_EZK_PPZA','PKD_EZK_RPZA');
   dalej+=exec('uprawnieniawrap','pkd','Karcie urlopowej'@,'PKD_EZK_OPKU','PKD_EZK_ORKU','PKD_ZES_KURL');

   {? +dalej
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@ [dalej])
   || _paperless:=czy_pep & var_pres('active',_par.PEP)>0 & _par.PEP.active;
      {? _paperless
      || czy_paperless:=0;
         {? ~czy_szablon & exec('rep_set','rap_zew','PKD_ZASWWARP')<>'T' & _par.PEP.fml_after=""
         || czy_paperless:=1
         ?}
      || czy_paperless:=1
      ?};
      __KA:=obj_new('year','month','day');
      Text:='';
      cnt:=0;
      __xr_ins:=exec('new_ins','rap_zew','PKD_ZASWWARP');
      {? _paperless
      || _args:=exec('wybierz_args','pracownik');
         _args.WIELU:=~_paperless;
         _args.SQL_FROM:=" JOIN OSOBA using(P.OSOBA, OSOBA.REFERENCE)"+
                         " JOIN USERS using(USERS.OSOBA, OSOBA.REFERENCE)"+
                         " JOIN USERSF using(USERSF.USERS, USERS.REFERENCE)";
         _args.SQL_WHERE:=''+"P.PORTAL='T' and USERSF.PORTAL2='T'";
         {? _par.PEP.P
         || params_set(
               'P',exec('wybierz_silent','pracownik',_par.PEP.P).P,
               'paperless',_paperless,
               'par',_par,
               'CALL',null(),
               'CALL2',null()
            )
         || params_set(
               'P',exec('wybierz_parses','pracownik',dziedzina,_args).P,
               'paperless',_paperless,
               'par',_par,
               'CALL',null(),
               'CALL2',null()
            )
         ?}
      || params_set('P',null(),'paperless',_paperless,'par',_par,'CALL',null(),'CALL2',null())
      ?};
      dalej:={? ~exec('info_o_warunkach','!pkd_zes_orza',_paperless,params_get(1).P,czy_paperless) || '0' || '' ?}
   ?}
}
{END:
   KART_URL.cntx_pop();
   H.cntx_pop();
   P.cntx_pop();
   {? __ppsf || Cntx.pop(PPSF_H,PPSF_HD,PPSF_ADR) ?};
   {? var_pres('__xr_ins')=type_of(null()) || exec('del_ins','rap_zew',&__xr_ins) ?};
   VAR_DEL.delete('Text','cnt','__CALL','__KA','dalej','czy_szablon','czy_paperless','dziedzina','czy_pep','__ppsf');
   VAR_DEL.delete('__CALL2','__xr_ins')
}
{COMMENT}OLD: zaswwarp.rpm{COMMENT-}
{COMMENT}OLD: zaswwarp.rpi{COMMENT-}
{EXIT: +dalej}
{EXIT: ~__xr_ins}
{EXIT: ~(exec('ready','rap_zew',__xr_ins) | (var_pres('czy_paperless')>0 & czy_paperless))}
{
exec('ins_par','rap_zew',__xr_ins,cnt,'CZY_ZAP',{? params_get().paperless || '1' || ' ' ?});
params_set(_par:=params_get());
{? _par.paperless & czy_paperless
|| __CALL:=_par.par.PEP.CALL;
   __CALL2:=_par.par.PEP.CALL2
|| __CALL:=proc_exec('warp@zasw',#__xr_ins);
   __CALL2:=proc_exec('warp2@zasw',#__xr_ins);
   {? czy_pep
   || _par.par.PEP.CALL:=__CALL;
      _par.par.PEP.CALL2:=__CALL2
   ?}
?};
~~ }
{IF: czy_szablon}
   {FOR: __CALL}{DO}
      {IF: P.seek(__CALL.P_REF,,1) & __CALL2.find_tab(,'P_REF',,'=',$P.ref())}{
         P.OSOBA();
         params_set('ZASWWARP',exec('scal_rekordy','szablon_rek',__CALL,__CALL2));
         _paperless:=params_get().paperless;
         _exe:={? _paperless || 2 || 3 ?};
         _file:=exec('wypelnij','szablon','PKD_ZASWWARP',,,_exe,,_paperless);
         {? _paperless & var_pres('_file')>0 & var_pres('INFO',_file)>0
         || exec('add_file_and_run','paperless_grp',
               P.ref(),_file.INFO,'Inne','Informacja o warunkach pracy')
         ?};
         ~~
      }{FI}
   {OD}
   {EXIT}
{FI}
{IF: exec('rep_set','rap_zew','PKD_ZASWWARP')='T'}
   {
   exec('use_tmp','rap_zew','PKD_ZASWWARP');
   {? params_get().paperless
   || exec('add_file_and_run','paperless_grp',P.ref(),'@!Tmp\\macrotmp.doc','Inne','Informacja o warunkach pracy')
   ?};
   ~~}
   {EXIT}
{ELSE}
   {IF: params_get().paperless & params_get().par.PEP.fml_after=""}
      {
      params_get().par.PEP.P:=P.ref();
      params_get().par.PEP.fml_after:="
         _pep:=obj_new('active','fml_after','P','CALL','CALL2');
         _pep.active:=1;
         _pep.fml_after:=\"1\";
         _pep.P:=_a;
         _pep.CALL:=_b;
         _pep.CALL2:=_c;
         {? var_pres('_params')>100
         || exec('obj_ntab_set','#array',_params,'PEP',_pep)
         || _params:=obj_new('PEP');
            _params.PEP:=_pep
         ?};
         params_set(_params);
         _name:='@'+tmp_dir()+exec('sep','#file')+'pkd_zaswwarunkipracy_%1'[$P.tm_stamp()]+'.pdf';
         rep_exec('pkd_zaswwarunkipracy',,0,_name,1,,1);
         exec('add_file_and_run','paperless_grp',
         _pep.P,_name,'Inne','Informacja o warunkach pracy')
      ";
      ~~}
      {EXIT}
   {ELSE}
      {INCLUDE: p_zaswwarp.rpi}
   {FI}
{FI}
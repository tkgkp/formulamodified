:!UTF-8
{TITLE:D. Skrócony raport magazynowy}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_104:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');

      VAR_DEL.delete('k','w','PRN','wyjscie');
      VAR_DEL.delete('odd','dod');
      VAR_DEL.delete('su','sp','X_Xb');
      VAR_DEL.delete('wydr_naz','wydr_edi')";
   mag_104();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   k :=obj_new(4); "--szerokosci kolumn--";
   w :=obj_new(4); "--wartosci kolumn--";
   su :=0; "--razem--";
   sp:=0; "--stan poczatkowy--";

   PRN :=obj_new(@.CLASS.PRINT,4); PRN.s_frame();
   k[1] :=10;
   k[2] :=35;
   k[3] :=11;
   k[4] :=30;
   w[1] :=w[2] :=w[3] := w[4] :='';
   tryb:=rep_export();

   odd:=dod:=date(0,0,0);

   wydr_naz:='mag_104';
   wydr_edi:='MAG_104';

"--parametry okreslane przez uzytkownika--";
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? ST.MAG().IL='T'
   || FUN.info('Zestawienie niedostępne w magazynie z ewidencją wyłącznie ilościową.'@)
   |? exec('upr_cm','ceny') & WYDRUKIL.edit()
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         odd:=WYDRUKIL.OD_DATY;
         dod:=WYDRUKIL.DO_DATY
      ?}
   ?}
}
{END:
   mag_104(); VAR_DEL.delete()
}
{IF: exec('upr_cm','ceny')=0}{ FUN.info('Brak uprawnień do wartości magazynowych.'@);~~}{EXIT}{FI}
{IF: wyjscie}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{
PRN.add("w[1]",k[1],'Typ dokumentu'@);
PRN.add("w[2]",k[2],'Nazwa typu dok.'@);
PRN.add("w[3]",k[3],'Przychodowy'@);
PRN.add("w[4]",k[4],'Wartość'@,1);

VAR_DEL.delete('X_Xb');
X_Xb:=tab_tmp(1,
   'TYP' ,'STRING[8]'   ,'Typ',
   'NAZ' ,'STRING[60]'  ,'Nazwa',
   'P'   ,'STRING[1]'   ,'Przychodowy',
   'W'   ,'REAL'        ,'Wartość');

su:=sp:=0;
TYPYDOK.index('TYP'); TYPYDOK.prefix();
{? TYPYDOK.first()
|| ND.cntx_psh(); DK.cntx_psh();
   OKR.cntx_psh();
   OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
   {!
   |? _war :=0;
      X_Xb.blank(1);
      X_Xb.TYP:=TYPYDOK.T;
      X_Xb.NAZ:=TYPYDOK.NAZ;
      X_Xb.P  :=TYPYDOK.P;

      {? OKR.first()
      || {!
         |? _maska:=$OKR.ROK+2;
            {? _maska<>DK.name()+2
            || DK.use((DK.name()-2)+_maska);
               ND.use((ND.name()-2)+_maska)
            ?};

            ND.index('NAGNUM');
            ND.prefix(ST.MAG,TYPYDOK.ref());
            {? ND.first()
            || {!
               |? {? ND.D<odd & ND.Z='T'
                  || "--oblicza stan poczatkowy--";
                     DK.index('DOKMAG'); DK.prefix(ND.ref());
                     {? DK.first()
                     || {!
                        |? {? DK.PLUS='T' || sp +=DK.WAR
                           |? DK.PLUS='N' || sp -=DK.WAR
                           ?};
                           DK.next()
                        !}
                     ?}
                  |? odd<=ND.D & (ND.D<=dod | WYDRUKIL.DO_DATY=date(0,0,0)) & ND.Z='T'
                  || "--oblicza wartosci dla typow dokumentow--";
                     DK.index('DOKMAG'); DK.prefix(ND.ref());
                     {? DK.first() || {! |? _war +=DK.WAR; DK.next() !} ?}
                  ?};
                  ND.next()
               !}
            ?};
            OKR.next()
         !};
         {? TYPYDOK.P='N' || _war:=-_war ?};
         su+=_war
      ?};
      {? _war<>0 || X_Xb.W:=_war; X_Xb.add() ?};
      TYPYDOK.next()
   !};
   OKR.cntx_pop();
   ND.cntx_pop(); DK.cntx_pop()
?};
su+=sp;

PAR_WYDR.TITLE:='SKRÓCONY RAPORT MAGAZYNOWY'@;
prn1:='PRN'
;~~}
{IF: sp=0 & X_Xb.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Za okres: %1 - %2'@[(odd$1),(dod$1)]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}
   {LINE}
   {<{lmt+1}}{'Parametry wydruku:'@}
   {<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
   {<{lmt+1}}{'Za okres: %1 - %2'@[(odd$1),(dod$1)]}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   drukowanie pozycji wydruku                               {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{COMMENT}   stan poczatkowy                                          {COMMENT-}
{
w[1] :='';
w[2] :='Stan początkowy'@;
w[3] :='';
w[4] :=form(sp,,2,'.,')
;~~}
{SUBSTITUTE: 'LINIA0'}
{COMMENT}   typy dokumentow                                          {COMMENT-}
{FOR: X_Xb}
{DO}
   {
   w[1] :=form(X_Xb.TYP);
   w[2] :=form(X_Xb.NAZ);
   w[3] :=form(X_Xb.P);
   w[4] :=form(X_Xb.W,,2,'.,')
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: tryb<>1}
{SUBSTITUTE: 'LINIAF'}
{COMMENT}   podsumowanie                                             {COMMENT-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1] :=w[2] :='';
w[3] :='Razem:'@; PRN.FORM[3]:=0;
w[4] :=form(su,,2,'.,');
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
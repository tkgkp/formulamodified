:!UTF-8
{TITLE:A.  Operacja rozliczenia rozrachunków}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN:
    w:=obj_new(11);
    PRN:=obj_new(@.CLASS.PRINT,11); PRN.sl_frame();
    PRN2:=obj_new(@.CLASS.PRINT,3);  PRN2.sl_frame();
    PRN.add("w[1]",50,' Symbol rozrachunku#    rozliczanego'@,,'#');
    PRN.add("w[11]",10,'Data otw.'@);
    PRN.add("w[2]",10,'T. płatn.'@);
    PRN.add("w[3]",20,'    Saldo Winien'@,1);
    PRN.add("w[4]",20,'      Saldo Ma'@,1);
    PRN.add("w[5]",20,' Kwota rozliczenia'@,1);
    PRN.add("w[6]",20,'Saldo po rozliczeniu'@,1);
    {? PAR_NAG.WAL2<>null
    || PRN.add("w[7]",20,'    Saldo Winien'@,1); PRN.add("w[8]",20,'      Saldo Ma'@,1);
       PRN.add("w[9]",20,' Kwota rozliczenia'@,1); PRN.add("w[10]",20,'Saldo po rozliczeniu'@,1);
       PRN2.add("",42,'');
       PRN2.add("",83,35*' '+'Kwoty w:  %1'@[PAR_NAG.WAL().KOD]);
       PRN2.add("",83,35*' '+'Kwoty w:  %1'@[PAR_NAG.WAL2().KOD])
    ?};
    {! _i:=1..11 |! w[_i]:='' !}; mdc:=''; lm:=0;
    sum_1:=sum_2:=sum_3:=sum_4:=sum_5:=sum_6:=sum_7:=sum_8:=0
 }
{END: obj_del(w); obj_del(PRN); obj_del(PRN2); &mdc; &lm;
      &sum_1; &sum_2; &sum_3; &sum_4; &sum_5; &sum_6; &sum_7; &sum_8;
      DOK.use('doku'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
      POZ.use('pozy'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
      POW.use('pow_'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
      AN.use('koan__'+SSTALE.AR().KOD)}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_rozl_opr.rpm
Utworzony:   2001-11-27 [AMK] Artur Makos
==================================================
Zawartosc:  Rozrachunki do rozliczenia
{COMMENT-}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}--------------------------MACRO-NAGLOWEK--------------{COMMENT-}
{MACRO: 'NAGLOWEK'}
{FONT: 'R'}{SPACE: 'C'}
{IF: PAR_NAG.WAL2=null}
{PRN.hbar()}
{ELSE}
{PRN2.hbar()}
{PRN2.ini_head()}
{WHILE: PRN2.h_next()}
{DO}
{PRN2.h_line()}
{OD}
{PRN2.jbar(PRN)}
{FI}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}{SPACE: 'C'}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='OPERACJA ROZLICZENIA ROZRACHUNKÓW'@;~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<5 {? -PAR_NAG.ZN='t'||'Status: zaakceptowany'@|?-PAR_NAG.ZN='n'||'Status: niezaakceptowany'@||'Status: wycofany'@?}
}{IF: -PAR_NAG.ZN='t'}{<29 'Data akceptacji: %1'@[PAR_NAG.DATA_AKC$3]}{<59 'Wykonał: %1'@[PAR_NAG.USER]}{FI}
{ OP.cntx_psh();
   OP.index('KON_O');
   OP.prefix(SSTALE.WAL,PAR_NAG.ODD,PAR_NAG.KONTO,PAR_NAG.OP,PAR_NAG.OP,PAR_NAG.SYM_ROK);~~}
{<5 'Konto: %1'@[PAR_NAG.KONTO]}
{<5 'Wystawca: %1  %2'@[PAR_NAG.POCH().KOD,PAR_NAG.POCH().TR]}
{<5 'Rozrachunek główny: %1'@[PAR_NAG.OP]}{IF: OP.first()}{<48 'Typ: %1   Data otwarcia: %2   Termin pł.: %3'@[OP.TYP,OP.DO$3,OP.TZ$3]}{FI}
{  OP.cntx_pop();~~}
{<5 'Kwota do rozliczenia:%1'@[form(PAR_NAG.KW_ROZ,20,2)]}
{IF: PAR_NAG.ZAP_OP<>null}
{ PAR_NAG.ZAP_OP(); exec('zap_mask','rozrach');~~}
{<5 'Zapis -  Rejestr: %1  numer: %2  pozycja: %3'@[PAR_NAG.ZAP_OP().REJ().KOD,$ZAP_OP.NR,$ZAP_OP.POZ]}
{FI}
{<5 'Rok obrotowy: %1'@[SSTALE.AR().NAZ]}{<44 'Waluta: %1  %2'@[PAR_NAG.WAL().KOD,PAR_NAG.WAL().TR]}
{<5 'Jednostka księgowa: %1'@[PAR_NAG.ODD().OD]}
{FI}
{SUBSTITUTE: 'NAGLOWEK'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}{FONT: 'Q'}{SPACE: 'C'}
{PRN.lbar()}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}-----------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{FOR: 'PAR_POZ'}
{INDEX: 'PAR_POZ'}
{PREFIX: PAR_NAG.ref()}
{DO}
{ w[1]:=PAR_POZ.OP; w[2]:=PAR_POZ.TZ$3; w[11]:='';
  OP.cntx_psh(); OP.index('KON_O');
  OP.prefix(PAR_NAG.WAL,PAR_NAG.ODD,PAR_POZ.KONTO,PAR_POZ.OP,PAR_POZ.OP,PAR_POZ.SYM_ROK);
  {? OP.first() || w[11]:=$OP.DO ?};
  OP.cntx_pop();
  w[3]:={? -PAR_NAG.STR='ma' || sum_1+=PAR_POZ.SAL; form(PAR_POZ.SAL,,2,' ,') || form(0,,2,' ,') ?};
  w[4]:={? -PAR_NAG.STR='wn' || sum_2+=PAR_POZ.SAL; form(PAR_POZ.SAL,,2,' ,') || form(0,,2,' ,') ?};
  w[5]:=form(PAR_POZ.KWOTA,,2,' ,'); sum_3+=PAR_POZ.KWOTA; {! _i:=7..10 |! w[_i]:='' !};
  w[6]:=form((PAR_POZ.SAL-PAR_POZ.KWOTA),,2,' ,'); sum_4+=(PAR_POZ.SAL-PAR_POZ.KWOTA);
  {? PAR_NAG.WAL2<>null
  || PAR_NAG.cntx_psh(); PAR_POZ.cntx_psh(); _rozr:=PAR_POZ.OP; _usym:=PAR_POZ.SYM_ROK;
     PAR_NAG.index('UNIK'); PAR_NAG.prefix(PAR_NAG.WAL2,'P',PAR_NAG.UNIK);
     {? PAR_NAG.first()
     || PAR_POZ.index('POZ_ROZ'); PAR_POZ.prefix(PAR_NAG.ref(),_rozr,_rozr,_usym);
        {? PAR_POZ.first()
        || PAR_POZ.PAR_NAG();
           w[7]:={? -PAR_NAG.STR='ma' || sum_5+=PAR_POZ.SAL; form(PAR_POZ.SAL,,2,' ,') || form(0,,2,' ,') ?};
           w[8]:={? -PAR_NAG.STR='wn' || sum_6+=PAR_POZ.SAL; form(PAR_POZ.SAL,,2,' ,') || form(0,,2,' ,') ?};
           w[9]:=form(PAR_POZ.KWOTA,,2,' ,'); sum_7+=PAR_POZ.KWOTA;
           w[10]:=form((PAR_POZ.SAL-PAR_POZ.KWOTA),,2,' ,'); sum_8+=(PAR_POZ.SAL-PAR_POZ.KWOTA)
        ?}
     ?};
     PAR_NAG.cntx_pop();  PAR_POZ.cntx_pop()
  ?};~~}
{ENTIRE}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{OD}
{ w[1]:='';   w[2]:='    Razem:'@;
  w[3]:=form(sum_1,,2,' ,'); w[4]:=form(sum_2,,2,' ,');
  w[5]:=form(sum_3,,2,' ,'); w[6]:=form(sum_4,,2,' ,');
  w[7]:=form(sum_5,,2,' ,'); w[8]:=form(sum_6,,2,' ,');
  w[9]:=form(sum_7,,2,' ,'); w[10]:=form(sum_8,,2,' ,');~~}
{ENTIRE}
{FONT: 'R'}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{FONT: 'Q'}
{ENTIRE-}
{COMMENT}----------------------OSTATNI FOOTER-----------------------{COMMENT-}
{FOOTER}{FOOTER-}
{ENTIRE}{FONT: 'Q'}{SPACE: 'C'}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}

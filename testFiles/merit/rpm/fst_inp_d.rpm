:!UTF-8
{TITLE:Nadmiary środków}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,12); PRN.s_frame();
   c:=obj_new(12);
   VAR_DEL.delete('TMP_KOM','TMP_KOMS');
   {! _i:=1..12 |! c[_i] := '' !};
   i:=0; __std:=null; slicznik:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0; par5:=''; par6:='';
   sum_wart:=0; licznik:=0; inw_index:=''; inw_prefix:=0
}
{END:
   obj_del(PRN);
   obj_del(c);
   VAR_DEL.delete('TMP_KOM','TMP_KOMS');
   &i; &color; &prn1; &prn2; &__std; &slicznik;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3; &par4; &par5; &par6;
   &sum_wart; &licznik; &inw_index; &inw_prefix
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_inp_d.rpm
Utworzony  : 2016-04-19
Zawartość  : Nadmiary środków
==================================================
{COMMENT-}
{PAR_WYDR.cntx_psh();~~}
{INCLUDE: wsp_pw.rpi}
{PAR_WYDR.cntx_pop();~~}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("c[1]",5,'Lp.',,'#');
      PRN.add("c[2]",20,'Nr inwentarz.',,'#');
      PRN.add("c[3]",30,'Nazwa środka',,'#');
      PRN.add("c[4]",25,'Nr fabryczny',,'#');
      PRN.add("c[5]",11,'Data zakupu',,'#');
      PRN.add("{? type_of(c[6]) = 1 || form(c[6],,2,' ,') || c[6] ?}",14,'Wartość',1,'#');
      PRN.add("c[7]",10,'Pomieszcz.',,'#');
      PRN.add("c[8]",20,'Użytkownik',,'#');
      PRN.add("c[9]",35,'Uwagi',,'#');

      PAR_WYDR.TITLE:='Nadmiary środków. Arkusz nr '+$SRXA.NR; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 1}
{<1 'PARAMETRY ARKUSZA:'}
{COMMENT} ---- Arkusz, Jedn. ksiegowa, Jedn. org., Pomieszczenie ---- {COMMENT-}
{
  par1:='Jedn. ks.: '+SRXA.ODD().OD;
  par2:='Jedn. org.: '+SRXA.JORG().SYMBOL;
  par3:='Pomieszczenie:' +SRXA.POM().S;
  par5:='Inwentaryzację wykonano: '+{? SRXI.DZ<>date(0,0,0) || SRXI.DZ$6 || $SRXI.DZ ?};
  par6:='Stan na dzień: '+{? SRXI.DS<>date(0,0,0) || SRXI.DS$6 || $SRXI.DS ?}
;~~}
{IF: SRXA.ODD().OD<>''}{<1 par1}{ELSE}{<1 'Wszystkie jedn. ks.'}{FI}{<40 par5}
{IF: SRXA.JORG().SYMBOL<>''}{<1 par2}{<40 par6}{ELSE}{<1 par6}{FI}
{IF: SRXA.POM().S<>''}{<1 par3}{FI}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{FOR: SRXD}{INDEX: 'STATUS'}{PREFIX: SRXA.ref(),'D'}
 {DO}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
 {
      licznik+=1;
      slicznik:=0;
      c[1]:=licznik;
      c[2]:=SRXD.NRI;
      c[3]:=SRXD.NST;
      c[4]:=SRXD.NF;
      c[5]:=form(SRXD.DZ,,1);
      c[6]:=SRXD.WARP;
      c[7]:=SRXD.POM_N().S;
      c[8]:=SRXD.OSOBA_N().NAZWISKO+' '+SRXD.OSOBA_N().PIERWSZE;
      c[9]:=SRXD.memo_txt(,1,'UWAGI');
      {? +c[9]>255 || c[9]:=255+c[9] ?};
      c[9]:=gsub(c[9],'\n',' ');
      {? SRXD.GRP<>'E' || sum_wart+=SRXD.WARP ?};
      __std:=SRXD.ref()
 ;~~}
  {IF: lineleft() <=4}{PAGE}{FI}
  {SUBSTITUTE: 'LINIA0'}
  {rsep:=0;~~}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{COMMENT}---nadmiarowe elementy w srodkach nie bedacych nadmiarami--{COMMENT-}
{FOR: SRXD}{INDEX: 'STATUS'}{PREFIX: SRXA.ref()}
 {DO}
   {IF: SRXD.STATUS<>'D' & SRXD.GRP='T'}
      {
         slicznik:=0;
         SRXD.cntx_psh();
         SRXD.index('SRXD');
         SRXD.prefix(SRXA.ref(),SRXD.ref());
         __std:=null;
         {? SRXD.first()
         || {! |?
               {? SRXD.STATUS='D' || __std:=SRXD.TREE ?};
               __std=null & SRXD.next()
            !}
         ?};
         SRXD.cntx_pop();
         {? __std<>null || licznik+=1 ?}
      ;~~}
{COMMENT} --- elementy --- {COMMENT-}
      {IF: __std<>null}
         {FOR: SRXD}{INDEX: 'SRXD'}{PREFIX: SRXA.ref(),__std}
         {DO}
         {IF: SRXD.STATUS='D'}
            {
               slicznik+=1;
               c[1]:=$licznik+'/'+$slicznik;
               c[2]:=SRXD.NRI;
               c[3]:=SRXD.NST;
               c[4]:=SRXD.NF;
               c[5]:=form(SRXD.DZ,,1);
               c[6]:=SRXD.WARP;
               c[7]:=' - ';
               c[8]:=' - ';
               c[9]:=' - ';

               sum_wart+=SRXD.WARP
            ;~~}
            {IF: lineleft() <=4}{PAGE}{FI}
            {SUBSTITUTE: 'LINIA0'}
            { rsep:=0; ~~}
         {FI}
         {OD}
      {FI}
      {rsep:=PAR_WYDR.RSEP;~~}
   {FI}
 {OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<6}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
   c[1]:='';
   c[2]:='';
   c[3]:='Ogółem ARKUSZ:';
   c[4]:='';
   c[5]:='';
   c[6]:=sum_wart;
   c[7]:='';
   c[8]:='';
   c[9]:=''
;~~}
{SUBSTITUTE: 'LINIA0'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}

{FONT: 'T8G'}
{ENTIRE}
{<12 'Skład komisji inwentaryzacyjnej:'}{<65 'Podpis'}{<120 'Inne osoby obecne przy spisie:'}

{
   TMP_KOM:=tab_tmp(1,'REF','INTEGER','Ref komisji','KOD','STRING[20]', 'Kod komisji');
   TMP_KOMS:=tab_tmp(1,'CZLON','STRING[50]', 'Członek komisji');
   SRXD.cntx_psh();
   SRXD.index('STATUS');
   SRXD.prefix(SRXA.ref(),'D');
   {? SRXD.first()
   || SRXN.cntx_psh();
      SRXN.index('AUTO');
      SRXN.prefix(SRXI.ref(),'T');
      {? SRXN.first()
      || _kod:=SRXN.SRXK().KOD;
         {? TMP_KOM.first()
         || _add:=1;
            {! |?
                {? TMP_KOM.KOD=_kod || _add:=0 ?};
                _add & TMP_KOM.next()
            !}
         || _add:=1
         ?};
         {? _add
         || TMP_KOM.REF:=#SRXN.SRXK;
            TMP_KOM.KOD:=SRXN.SRXK().KOD;
            TMP_KOM.add()
         ?}
      ?};
      SRXN.cntx_pop()
   ?};
   SRXD.cntx_pop();
   {? TMP_KOM.first()
   || {! |?
         SRXL.index('SRXL');
         SRXL.prefix(TMP_KOM.REF);
         {? SRXL.first()
         || {! |?
               {? SRXL.P='T' || _czlon:='(Przew.) ' || _czlon:='' ?};
               _czlon+=SRXL.OSOBA().PIERWSZE+' '+SRXL.OSOBA().NAZWISKO;
               {? TMP_KOMS.first()
               || _add:=1;
                  {! |?
                     {? TMP_KOMS.CZLON=_czlon || _add:=0 ?};
                     _add & TMP_KOMS.next()
                  !}
               || _add:=1
               ?};
               {? _add
               || TMP_KOMS.CZLON:=_czlon;
                  TMP_KOMS.add()
               ?};
               SRXL.next()
            !}
         ?};
         TMP_KOM.next()
      !}
   ?}
;~~}
{IF: TMP_KOMS.size=0}
{<12 '(Przew.)..........................................'}{<120 '..................................................'}

{<12 '..................................................'}{<120 '..................................................'}

{<12 '..................................................'}{<120 '..................................................'}
{ELSE}
{FOR: TMP_KOMS}
   {DO}
{<12 TMP_KOMS.CZLON}{<65 '.........................'}{<120 '..................................................'}
   {OD}
   {IF: TMP_KOMS.size()<3}
      {dod_podp:=3-TMP_KOMS.size();~~}
      {WHILE: dod_podp>0}
      {DO}
         {<12 '..................................................'}{<120 '..................................................'}
         {dod_podp:=dod_podp-1; ~~}
      {OD}
   {FI}
{FI}
{ENTIRE-}
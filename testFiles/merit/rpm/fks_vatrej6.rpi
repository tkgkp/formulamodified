:!UTF-8
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: vatrej6.rpi
Utworzony:   2003-12-10 [AMA]
Modyfikacje:
==================================================
Zawartosc: Plik wykorzystywany w wydruku 'Wewnątrzwspólnotowe nabycia',
                 'Wewnątrzwspólnotowe dostawy'
{COMMENT-}
{ {? var_press('WAL',VAT7)<=0 || exec('par_1','!fks_vat_zrej') || exec('par_6','!fks_vat_zrej') ?};colspan:=0;
  {? rejestr='_WWspNus'|rejestr='_SprzNOp'|rejestr='SprzNOp'
  || _vr:=rejestr; rsep:=1
  || hdr_vat:='Wewnątrzwspólnotowe '; rsep:=1;
     {? tryb=3 || _vr:='_WWspN%'; hdr_vat+='nabycia' || _vr:='_WWspD%'; hdr_vat+='dostawy' ?}
  ?};
  {? VAT7.TYPOKR='K'
  || _v:=#((7+$SSTALE.AO().KON)+2);
     _okres:={? _v<=3 || '1' |? _v<=6 || '4' |? _v<=9 || '7' || '10' ?}
  || _v:=((7+$SSTALE.AO().KON)+2);
     _okres:=SSTALE.AO().NR
  ?};
  PAR_WYDR.TITLE:=hdr_vat+exec('naz_okr_vat','!fks_vat_zrej',0);
  _rok:=SSTALE.AR().NAZ;
  {? VAT7.PAR2=1 || _sw:=' and DOK_REJ.KOR=\'T\'' |? VAT7.PAR2=2 || _sw:=' and DOK_REJ.KOR=\'N\'' || _sw:='' ?};
  {?grupa_vat='SprPKWSU'||_sw+=' and SLO_GR.KOD=\'SprPKWSU\' '?};
  {? rejestr='SprzNOp'
  ||  _sw1:='';
     {? VAT7.PAR10=1
     || _sw1+='SLO_GR.KOD=\'SprPDost\' '
     |? VAT7.PAR10=2
     || _sw1+='SLO_GR.KOD=\'SprPDosu\' '
     || _sw1+='SLO_GR.KOD like \'SprPDos%\' '
     ?};
     {? menu_txt='L. Czynności nieopodatkowane na terytorium kraju' & VAT7.PAR10=3
     || _sw1+=' or SLO_GR.KOD=\'SprPozKr\' ';
        _sw1:=' and ('+_sw1+')'
     || _sw1:=' and '+_sw1
     ?};
     _sw:=_sw+_sw1
  ?};
  _zrodlo:=exec('sql_zrodlo','!fks_vat_zrej');
  _sql:='select  RVAT.SYM as RSYM, DVAT.NR as NRRV, DVAT.SYM1 as DSYM, DVAT.DAT5 as DOPER, DVAT.DAT1 as DZAP, DVAT.DAT2 as DWYST, DVAT.DAT4 as DPOTW, REJ.KOD as REJD, DOK.NR as NRRK, DOK.D3 as TPL, DVAT.KH, '+
                 'DVAT.NIP, PVAT.SUM2 as NETTO, PVAT.VAT, PVAT.SUM1 as BRUTTO, SLOP.KOD, SLU.WZ, SLOW.KOD as WYS, KVAT.SYM as KLVAT'+
                 ',DVAT.MIASTO, DVAT.UL, ODD.OD,\'T\' as TR, DVAT.USUNIETY, PVAT.TYP_VAT, DOK.JPK_GTU, DOK.JPK_PROC, DOK.JPK_V_T'
    +', case when PVAT.OP is null then DOK.TR else PVAT.OP end as DTR '
    +'from DVAT '
    +'left join SLO as SLOW using (DVAT.WYS,SLOW.REFERENCE) '
    +'left join SLU using (SLOW.SLU,SLU.REFERENCE) '
    +'join RVAT using (DVAT.RVAT,RVAT.REFERENCE) '
    +'join @DOK using (DVAT.DOK,DOK.REFERENCE) '
    +'join DOK_REJ using (DOK.DOK_REJ,DOK_REJ.REFERENCE) '
    +'join REJ using (DOK.REJ,REJ.REFERENCE) '
    +'join ODD using (REJ.ODD,ODD.REFERENCE)'
    +'join PVAT using (DVAT.REFERENCE,PVAT.DVAT) '
    +'join SLO as SLOP using (PVAT.STVAT,SLOP.REFERENCE) '
    +'join OKRO_F as D_OKR using (DVAT.OBR,D_OKR.REFERENCE) '
    +'join ROK_F as D_ROK using (D_OKR.ROK,D_ROK.REFERENCE) '
    +'join OKRO_F as DP_OKR using (DVAT.DOKOKRO,DP_OKR.REFERENCE) '
    +'join ROK_F as DP_ROK using (DP_OKR.ROK,DP_ROK.REFERENCE) '
    +'join KVAT using (RVAT.KVAT,KVAT.REFERENCE) '
    +'join SLO as SLO_KR using (DVAT.KRAJ,SLO_KR.REFERENCE) '+
    {? var_press('WAL',VAT7)>0
    || 'join SLO as SLO_WAL using (DVAT.WAL, SLO_WAL.REFERENCE) '
    || ''
    ?} +
    {? grupa_vat='SprPKWSU' | rejestr='SprzNOp'
    || 'join SLO as SLO_GR using (PVAT.GRVAT, SLO_GR.REFERENCE) '
    || ''
    ?}
    +'where D_ROK.NAZ=\''+_rok+'\' '+
    {? VAT7.TYPOKR='K'
    || 'and D_OKR.NR between '+_okres+' and '+$(#_okres+2)
    || 'and D_OKR.NR='+$_okres
    ?} +
    {? var_press('WAL',VAT7)>0
    || ' and SLO_WAL.KOD=\''+VAT7.WAL().KOD+'\''
    || ''
    ?}
    +{? OPERATOR.DEPT || ' and ODD.OD=\''+OPERATOR.DEPT().OD+'\'' || '' ?}
    +' and SLO_KR.KOD=\''+VAT7.KRAJ().KOD+'\''
    +_zrodlo
    +' and KVAT.SYM like \''+_vr+'\' '
    +_sw;
  VAR_DEL.delete('TT_VAT');
  _typ_vat:=exec('typ_vat','!fks_vat_zrej');
  TT_VAT:=sql(_sql);
  {? TT_VAT.first()
  || {!
     |? {? TT_VAT.TYP_VAT<>'' & TT_VAT.TYP_VAT<>_typ_vat
        || TT_VAT.del()
        |? rejestr='_WWspN' & TT_VAT.KLVAT='_WWspNus'
        || TT_VAT.del()
        || {? TT_VAT.KLVAT='_WWspDot' | TT_VAT.KLVAT='_WWspNat' || TT_VAT.TR:='T' || TT_VAT.TR:='N' ?};
           TT_VAT.put(); TT_VAT.next()
        ?}
     !};
     _vind:=TT_VAT.ndx_tmp(,1,'NIP',,,'TR',,,'RSYM',,,'NRRV',,); TT_VAT.index(_vind)
  ?};~~}
{ PRN.add("w[1]",16,'NIP');colspan:=colspan+1;
  {? rep_expo()
  || PRN.add("w[12]",14,'Rejestr VAT',,'#');colspan:=colspan+1;
     PRN.add("w[13]",14,'Poz. w rej.',,'#');colspan:=colspan+1
  || PRN.add("w[2]",14,'Rejestr VAT#  poz. w rej.',,'#')
  ?};
  {? tryb=3 & VAT7.WY2
  || {? rep_expo()
     || PRN.add("w[14]",20,'Symbol dokumentu',,'#');colspan:=colspan+1;
        PRN.add("w[15]",20,'Zapłacono',,'#');colspan:=colspan+1
     || PRN.add("w[3]",51,'Symbol dokumentu/Zapłacono',,'#')
     ?}
  |? tryb=4
  || {? rep_expo()
     || PRN.add("w[14]",20,'Symbol dokumentu',,'#');colspan:=colspan+1;
        PRN.add("w[15]",20,'Termin płatności',,'#');colspan:=colspan+1
     || PRN.add("w[3]",51,'Symbol dokumentu/Termin płatności',,'#')
     ?}
  || PRN.add("w[3]",51,'Symbol dokumentu');colspan:=colspan+1
  ?};
  {? tryb=3
  || {? rep_expo()
     || PRN.add("w[16]",12,'Data operacji',,'#');colspan:=colspan+1;
        PRN.add("w[17]",12,'Data zapisu',,'#');colspan:=colspan+1;
        PRN.add("w[18]",12,'Data wystawienia',,'#');colspan:=colspan+1;
        PRN.add("w[19]",12,'Data otrzymania',,'#');colspan:=colspan+1
     || PRN.add("w[4]",13,'D. operacji/#zapisu /wystawienia/# otrzymania',,'#')
     ?}
  |? tryb=4 & VAT7.PAR2=2
  || {? rep_expo()
     || PRN.add("w[16]",12,'Data operacji',,'#');colspan:=colspan+1;
        PRN.add("w[17]",12,'Data zapisu',,'#');colspan:=colspan+1;
        PRN.add("w[18]",12,'Data wystawienia',,'#');colspan:=colspan+1
     || PRN.add("w[19]",13,'D. operacji/#zapisu /wystawienia',,'#')
     ?}
  || {? rep_expo()
     || PRN.add("w[16]",12,'Data operacji',,'#');colspan:=colspan+1;
        PRN.add("w[17]",12,'Data zapisu',,'#');colspan:=colspan+1;
        PRN.add("w[18]",12,'Data wystawienia',,'#');colspan:=colspan+1;
        PRN.add("w[19]",12,'Data pow. o kor.',,'#');colspan:=colspan+1
     || PRN.add("w[4]",13,'D. operacji/#zapisu /wystawienia/#pow. o kor.',,'#')
     ?}
  ?};
  {? rep_expo()
  || PRN.add("w[20]",13,' Rej. księg.',,'#');colspan:=colspan+1;
     PRN.add("w[21]",13,' Pozycja',,'#');colspan:=colspan+1;
     {? odd || PRN.add("w[22]",13,' Jedn. ks.',,'#');colspan:=colspan+1 ?}
  || PRN.add("w[5]",13,' Rej. księg./#   pozycja'+{? odd || '# Jedn. ks.' || '' ?},,'#')
  ?};
  PRN.add("w[6]",50,'Kontrahent - nazwa, siedziba'+{? grupa_vat='SprPKWSU'||', nazwa usługi'||''?},,'#');colspan:=colspan+1;
  {? param6<>''
  || PRN.add("w[50]",6,'GTU')
  ?};
  {? param7<>''
  || PRN.add("w[51]",10,'Procedury')
  ?};
  {? param8<>''
  || PRN.add("w[52]",7,'Typ JPK')
  ?};
  PRN.add("w[7]",5,'% VAT',1);colspan:=colspan+1;
  PRN.add("w[8]",20,'Netto',1);
  PRN.add("w[9]",16,'VAT',1);
  PRN.add("w[10]",20,'Brutto',1);
  {? rejestr<>'SprzNOp'
  || PRN.add("w[11]",8,'Trójstr.')
  ?};
  {? VAT7.WYD_TECH || PAR_WYDR.HEAD:=PAR_WYDR.LOGO:=rsep:=0 ?};
~~}
{ {? +param6++param7++param8>0
  || _szer:={? +param6>0 || 7 || 0 ?} + {? +param7>0 || 11 || 0 ?}+{? +param8>0 || 8 || 0 ?}
  || _szer:=0
  ?};
 PRN1.add("ww[1]",168+_szer,'Ogółem',,,,,,,,colspan); PRN1.add("ww[2]",20,'',1); PRN1.add("ww[3]",16,'',1); PRN1.add("ww[4]",20,'',1);~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}{IF: ~VAT7.WYD_TECH}{LINE: 1}{FI}
{SUBSTITUTE: 'HEAD'}{FONT: 'W8'}{SPACE: 'C'}{IF: page=1}
{<3 'Parametry wydruku:'}
{<6 'Jednostka księgowa: '+{? OPERATOR.DEPT || OPERATOR.DEPT().OD || 'wszystkie jednostki księgowe' ?}+{? VAT7.TYPOKR='M' || '   Rodzaj okresu: '+{? SSTALE.AO().ZAM='T'||'zamknięty'||'otwarty'?} || '' ?}}
{IF: ~VAT7.WYD_TECH}{IF: dokvpoch<>''}{<6 dokvpoch}{FI}
{<6 'Kraj opodatkowania: '+kraj+{? wal_op<>'' || '   Waluta opodatkowania: '+wal_op || '' ?}}
{IF: param1<>''}
{<6 param1}{FI}{IF: param2<>''}
{<6 param2}{FI}{IF: param3<>''}
{<6 param3}{FI}{IF: param4<>''}
{<6 param4}{FI}{IF: param6<>''}
{<6 param6}{FI}{IF: param7<>''}
{<6 param7}{FI}{IF: param8<>''}
{<6 param8}{FI}
{IF: tryb=3 & VAT7.DAT_ZAPL<>date(0,0,0)}{<6 'Data zapłaty: '+$VAT7.DAT_ZAPL+{? VAT7.TYLKOZAP || ' - tylko zapłacone'|| '' ?}}{FI}
{FI}{FI}
{LINE: 1}{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{MACRO: 'LINIA1'}
{ll:=lineleft();~~}
{PRN.ini_line()}
{IF: ll<(rsep+PRN.length()) & ll<>0}{PAGE}{FI}
{IF: ~font_gr}{IF: s2c}{FONT: 'T8U'}{ELSE}{FONT: 'T8G'}{FI}{ELSE}{FONT: 'T8GB'}{FI}
{IF: vp}
{ PRN.setcolor(color);~~}
{REP: PRN.fbar(lmt)}{ PRN.setcolor('255:255:255');vp:=0;~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: PRN.fbar(lmt)}
   {ELIF: rsep}
      {REP: PRN.fbar(lmt)}
   {FI}
{FI}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}{FONT: 'T8G'}
{MACRO-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{SPACE: 'C'}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{~~}
{FOOTER-}
{IF: rep_expo()}

{FI}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: 'TT_VAT'}
{DO}
{IF:   (VAT7.WY4<>0 & (VAT7.WY4=1 & {? VAT7.JPK_GTU='' || 1
                                  |? VAT7.JPK_GTU*'BRAK'>0 & TT_VAT.JPK_GTU='' || 1
                                  || exec('test_in','!fks_vat_zrej',VAT7.JPK_GTU,TT_VAT.JPK_GTU)
                                  ?})) |
  (VAT7.WY5<>0 & (VAT7.WY5=1 & {? VAT7.JPK_PROC='' || 1
                                  |? VAT7.JPK_PROC*'BRAK'>0 & TT_VAT.JPK_PROC='' || 1
                                  || exec('test_in','!fks_vat_zrej',VAT7.JPK_PROC,TT_VAT.JPK_PROC)
                                  ?})) |
   (VAT7.WY6<>0 & (VAT7.WY6=1 & {? VAT7.JPK_TD='' || 1
                                  |? VAT7.JPK_TD*'BRAK'>0 & TT_VAT.JPK_V_T='' || 1
                                  || exec('test_in','!fks_vat_zrej',VAT7.JPK_TD,TT_VAT.JPK_V_T)
                                  ?})) |
   (VAT7.WY4=0 & VAT7.WY5=0 & VAT7.WY6=0)
                                  }
   {IF: (TT_VAT.NIP<>pom_1 | (TT_VAT.NIP=pom_1 & TT_VAT.TR<>pom_2)) & numer>1}
{ w[2]:=w[3]:=w[4]:=w[5]:=w[7]:=w[12]:=w[13]:=w[14]:=w[15]:=w[16]:=w[17]:=w[18]:=w[19]:=w[20]:=w[21]:=w[22]:=w[50]:=w[51]:=w[52]:=''; w[6]:='Suma '+w[1]; w[1]:='';
       w[8]:=form(suma8,,2,' ,'); w[9]:=form(suma9,,2,' ,'); w[10]:=form(suma10,,2,' ,'); ~~}
      {FONT: 'T8GB'}
      {IF: ~rep_expo()}{SUBSTITUTE: 'LINIA0'}{FI}
      {FONT: 'T8G'}
      { suma8:=suma9:=suma10:=numer:=0;~~}
   {ELIF: TT_VAT.NIP<>pom_1 | (TT_VAT.NIP=pom_1 & TT_VAT.TR<>pom_2)}
      { suma8:=suma9:=suma10:=numer:=0; ~~}
   {FI}
   { numer+=1; s2c:=TT_VAT.USUNIETY; {? s2c || is_us:=1 ?};
     w[1]:=TT_VAT.NIP; pom_1:=TT_VAT.NIP;
     w[2]:=TT_VAT.RSYM+(8-(+TT_VAT.RSYM))*' '+form(TT_VAT.NRRV,6,,'99');
     w[3]:=TT_VAT.DSYM+(20-+TT_VAT.DSYM)*' '+{? tryb=4 || $TT_VAT.TPL || '' ?};
     w[4]:=TT_VAT.DOPER$1+'#'+TT_VAT.DZAP$1+'#'+TT_VAT.DWYST$1+'#'+TT_VAT.DPOTW$1;
     w[5]:=TT_VAT.REJD+(8-(+TT_VAT.REJD))*' '+form(TT_VAT.NRRK,5,,'99')+{?odd||'#'+TT_VAT.OD||''?};
     _kh:=TT_VAT.KH+{? TT_VAT.MIASTO<>'' || ', '+TT_VAT.MIASTO || '' ?}
          +{? grupa_vat='SprPKWSU'&TT_VAT.DTR<>''||', '+TT_VAT.DTR||''?};
     STR.split(_kh); _dr_kh:=STR.line(50);
     {! |? _dr_kh1:=STR.line(50); |_dr_kh1<>'' |! _dr_kh+=('#'+_dr_kh1) !};
     w[6]:=_dr_kh; w[7]:=TT_VAT.KOD; w[8]:=form(TT_VAT.NETTO,,2,' ,');
     w[9]:=form(TT_VAT.VAT,,2,' ,');
     w[10]:=form(TT_VAT.BRUTTO,,2,' ,');
     w[11]:={? TT_VAT.TR='T' || '     X' || '' ?}; pom_2:=TT_VAT.TR;
     w[12]:=TT_VAT.RSYM;
     w[13]:=form(TT_VAT.NRRV,6,,'99');
     w[14]:=TT_VAT.DSYM;
     w[15]:={? tryb=4 || $TT_VAT.TPL || '' ?};
     w[16]:=TT_VAT.DOPER$1;
     w[17]:=TT_VAT.DZAP$1;
     w[18]:=TT_VAT.DWYST$1;
     w[19]:=TT_VAT.DPOTW$1;
     w[20]:=TT_VAT.REJD;
     w[21]:=form(TT_VAT.NRRK,5,,'99');
     w[22]:={?odd||TT_VAT.OD||''?};
     {? param6<>'' || w[50]:=TT_VAT.JPK_GTU || w[50]:='' ?};
     {? ~rep_expo() || w[50]:=STR2.gsub(w[50],',') ?};
     {? param7<>'' || w[51]:=TT_VAT.JPK_PROC;  w[51]:=STR2.gsub(w[51],',',', ') || w[51]:='' ?};
     {? param8<>'' || w[52]:=TT_VAT.JPK_V_T || w[52]:='' ?};
     {? s2c
     || ssu[1]+=TT_VAT.NETTO; ssu[2]+=TT_VAT.VAT; ssu[3]+=TT_VAT.BRUTTO
     || suma8+=TT_VAT.NETTO; ww[2]+=TT_VAT.NETTO;
        suma9+=TT_VAT.VAT; ww[3]+=TT_VAT.VAT;
        suma10+=TT_VAT.BRUTTO;ww[4]+=TT_VAT.BRUTTO
     ?};~~}
   {SUBSTITUTE: 'LINIA1'}
    {FI}
{OD}
{IF: numer>1}
{FONT: 'T8GB'}
  { w[2]:=w[3]:=w[4]:=w[5]:=w[7]:=w[12]:=w[13]:=w[14]:=w[15]:=w[16]:=w[17]:=w[18]:=w[19]:=w[20]:=w[21]:=w[22]:=w[50]:=w[51]:=w[52]:=''; w[6]:='Suma '+w[1]; w[1]:=''; w[8]:=form(suma8,,2,' ,'); w[9]:=form(suma9,,2,' ,'); w[10]:=form(suma10,,2,' ,'); ~~}
  { w[50]:=w[51]:=w[52]:='';~~}
  {IF: ~rep_expo()}{SUBSTITUTE: 'LINIA0'}{FI}
  {FONT: 'T8G'}
{FI}
{IF: lineleft()<>0 & lineleft()<(3+is_us*2)}{PAGE}{FI}
{IF: lineleft()<>0}{FOOTER}{FOOTER-}{FI}
{ separat:=rsep; rsep:=1; ww[1]:='Razem'; prn1:='PRN'; prn2:='PRN1'; ww[2]:=form(ww[2],,2,' ,'); ww[3]:=form(ww[3],,2,' ,'); ww[4]:=form(ww[4],,2,' ,'); ~~}
{IF: ~rep_expo()}{SUBSTITUTE: 'LINIA02'}{FI}
{IF: is_us}
{ separat:=rsep; rsep:=1; ww[1]:='Razem operacje nie rozliczane w VAT'; prn1:='PRN1'; prn2:='PRN1'; ww[2]:=form(ssu[1],,2,' ,'); ww[3]:=form(ssu[2],,2,' ,'); ww[4]:=form(ssu[3],,2,' ,'); ~~}
{IF: ~rep_expo()}{SUBSTITUTE: 'LINIA02'}{FI}
{FI}
{FOOTER}{FOOTER-}{ rsep:=separat;~~}
{REP: PRN1.flbar()}
{IF: ~rep_expo()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'}
{FI}
{ obj_del(TT_VAT);~~}

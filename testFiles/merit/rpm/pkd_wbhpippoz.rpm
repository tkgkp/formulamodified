:!UTF-8
{TITLE:Szkolenia BHP i PPOŻ}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   tresc:='wsz_bhp';
   PAR_WYDR.TITLE:='Szkolenia BHP i PPOŻ'@;
   P.cntx_psh();
   KART_DEF.cntx_psh();

   par:=obj_new(9);
   par[2]:='KART_DOD';
   par[5]:=0;
   _upr:=exec('uprawnieniawrap','pkd','szkoleniach BHP i ppoż'@,'PKD_DOS_PPSZ','PKD_DOS_PRSZ');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[3]:='';
      par[4]:=0;
      par[7]:=obj_new('TAB','RED','CHK');
      exec('pkd_bhpippoz','!pkd_zes_orka',par[7],PAR_WYDR.TITLE);
      {? par[7].TAB.ASK
      || {? par[7].TAB.ASK<3
         || par[2]:=KART_DOD.ndx_tmp(,1,'FIRMA',,,'KT',,,'KART_DEF',,,'OSOBA',,,'OPIS',,,'DO',,1)
         ?};
         {? par[7].TAB.ASK=1
         || PAR_WYDR.TITLE:='Sprawdzenie szkoleń BHP i PPOŻ'@
         |? par[7].TAB.ASK=2
         || PAR_WYDR.TITLE:='Szkolenia BHP i PPOŻ tracące ważność'@
         ?};
         par[1]:=obj_new(@.CLASS.PRINT,{? par[7].TAB.ASK=3 || 8 || 7 ?});
         {? ~rep_export() || par[1].add("{? par[6] || '' || par[4]+=1 ?}",5,'Lp.'@,1,,,,,',,\'9.\'') ?};
         par[1].add("
            {? par[6]
            || {? ~rep_export() || '' || P.T ?}
            || P.T
            ?}
         ",10,'Numer teczki'@,1);
         par[1].add("
            {? par[6]
            || {? ~rep_export() || '' || P.OSOBA().NAZWISKO ?}
            || P.OSOBA().NAZWISKO
            ?}
         ",25,'Nazwisko'@);
         par[1].add("
            {? par[6]
            || {? ~rep_export() || '' || OSOBA.PIERWSZE ?}
            || OSOBA.PIERWSZE
            ?}
         ",20,'Imię'@);
         {? par[7].TAB.ASK=3 || par[1].add("KART_DOD.KT",6,'Rodzaj'@) ?};
         par[1].add("{? ~par[9] & ~par[8] || '' || KART_DOD.OD$1 ?}",10,'Data szkolenia'@);
         par[1].add("{? ~par[9] & ~par[8] || '' || KART_DOD.DO$1 ?}",10,'Ważne do'@);
         par[1].add("{? ~par[9] & ~par[8] || 'BRAK SZKOLENIA'@ || KART_DOD.OPIS ?}",40,'Szkolenie'@);
         par[1].s_frame();
         prn1:='par[1]';
         lmt:=rsep:=0;
         vp:=par[5]:=1;
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   {? par[2]<>'KART_DOD' || KART_DOD.ndx_drop(par[2]) ?};
   P.cntx_pop();
   KART_DEF.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc')
}
{EXIT: ~par[5]}
{EXIT: {? par[7].TAB.ASK || KART_DEF.index('SYMBOL'); KART_DEF.prefix(); ~KART_DEF.find_key('SZKOLBHP') || 1 ?}}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: {INCLUDE: std_info.rpi}{COMMENT-}
{COMMENT}OLD: {INCLUDE: skid_pw.rpi}{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {IF: par[7].TAB.ASK=1}
      {<{lmt+1}'Data kontroli: %1'@[par[7].TAB.DATA$1]}
   {ELSE}
      {<{lmt+1}'Zakres dat: od %1 do %2'@[par[7].TAB.OD$1,par[7].TAB.DO$1]}
   {FI}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'szk_dane'}
{par[1].ini_line()}
{WHILE: par[1].next()}{DO}
   {REP: par[1].fline(lmt)}
{OD}
{ par[8]:=par[6]:=1; ~~ }
{MACRO-}
{MACRO: tresc}
{IF: {? par[7].TAB.ASK=1
     || P.DZA<=par[7].TAB.DATA & (P.DZ=#0 | par[7].TAB.DATA<=P.DZ)
     || P.DZA<=par[7].TAB.DO & (P.DZ=#0 | par[7].TAB.OD<=P.DZ)
     ?}
}
{  par[8]:=par[9]:=par[6]:=0;
   par[3]:=''; ~~
}
{ENTIRE}
{REP: '{FOR: KART_DOD}{INDEX: par[2]}'+
      '{PREFIX: exec(\'ref_firma\',\'ustawienia\'),'+{? par[7].TAB.ASK=3 || '' || '\'O\',' ?}+'KART_DEF.ref,P.OSOBA}{DO}'+
      {? par[7].TAB.ASK=3
      || par[9]:=1;
            '{IF: par[7].TAB.OD<=KART_DOD.OD &
                  {? par[7].TAB.DO<>#0
                  || KART_DOD.OD<=par[7].TAB.DO
                  || 1
                  ?}}
                {SUBSTITUTE: \'szk_dane\'}
             {FI}'
      || '{IF: KART_DOD.OPIS<>par[3]}'+
            '{ par[3]:=KART_DOD.OPIS; ~~ }'+
            {? par[7].TAB.ASK=2
            || '{IF: KART_DOD.DO~1 & KART_DOD.DO>=par[7].TAB.OD & KART_DOD.DO<=par[7].TAB.DO}'
            || '{IF: KART_DOD.DO<par[7].TAB.OD}'
            ?}+
            '{ par[9]:=1; ~~ }'+
            '{SUBSTITUTE: \'szk_dane\'}'+
            '{ELSE}{ par[8]:=1; ~~ }'+
            '{FI}{FI}'
      ?}+'{OD}'
}
{IF: ~par[9] & ~par[8]}
   {SUBSTITUTE: 'szk_dane'}
{FI}
{ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
:!UTF-8
{TITLE:Wykaz pobranych załączników}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','b_rat','h_rat','pierwszy','zam','zal_ref','lp','tresc');
   tresc:='pkd_wpobranezal';
   PAR_WYDR.TITLE:='Wykaz pobranych załączników';
   OSOBA.cntx_psh();
   P.cntx_psh();
   par:=obj_new('prn1','SLO','p6','p7','p8','p_ref','TAB','ndx','firstRec','ok','tab');
   par.ok:=0;
   _upr:=exec('uprawnieniawrap','pkd','załącznikach pracownika'@,'PKD_EZK_OPZA','PKD_EZK_ORZA');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień pozwalających na przeglądanie informacji o: %1'@[_upr])
   || par.SLO:=obj_new('WYBOR','lista');
      par.SLO.WYBOR:='';
      par.SLO.lista:='';
      EDIT_VAR.SLO_TYP:='ZAL';
      _slo_naz:=null();
      _slo_typ:=exec('slo_typ','ext_slo');
      par.tab:=exec('pkd_zalaczniki','!pkd_zes_orka',PAR_WYDR.TITLE,0);
      {? par.ok:=par.tab.size()
      || {? par.tab.TYP
         || {? var_pres('_res')>100 || obj_del(_res) ?};
            _res:=exec('wybierz_nazwy','ext_slo',_slo_typ);
            {? _res.OK & _res.WYBOR.first()
            || par.SLO.WYBOR:=_res.WYBOR;
               {!
               |? par.SLO.lista+=_res.WYBOR.NAZWA+', ';
                  _res.WYBOR.next()
               !};
               par.SLO.lista:=(par.SLO.lista-2)+'.';
               0
            || ~FUN.ask('%1 %2'['Nie wybrano typu załącznika.'@,'Czy na pewno chcesz kontynuować?'@])
            ?}
         ?};

         par.TAB:=tab_tmp(1,
            'LP','INTEGER',,
            'P','STRING[150]',,
            'IMIONA','STRING[50]',,
            'NAZWISKO','STRING[50]',,
            'TECZKA','STRING[50]',,
            'ZAL_TYP','STRING[150]',,
            'ZAL_NAME','STRING[64]',,
            'ZAL_REF','STRING[16]',,
            'POB_D','DATE',,
            'POB_G','TIME',,
            'POTW','STRING[1]',,
            'LOGIN','STRING[100]',
         );
         par.ndx:=par.TAB.ndx_tmp(,1,'LP',,0,'POB_D',,0);
         par.TAB.index(par.ndx);

         par.prn1:=obj_new(@.CLASS.PRINT,{? rep_export() || 9 || 6 ?});
         {? rep_export()
         || par.prn1.add("par.TAB.NAZWISKO",30,'Nazwisko'@);
            par.prn1.add("par.TAB.IMIONA",41,'Imiona'@);
            par.prn1.add("par.TAB.TECZKA",9,'Nr Teczki'@)
         ?};
         par.prn1.add("
            {? par.firstRec
            || par.TAB.ZAL_TYP
            || {? ~rep_export()
               || ''
               || par.TAB.ZAL_TYP
               ?}
            ?}
         ",25,'Typ załącznika'@);
         par.prn1.add("
            {? par.firstRec
            || par.TAB.ZAL_NAME
            || {? ~rep_export()
               || ''
               || par.TAB.ZAL_NAME
               ?}
            ?}
         ",30,'Nazwa załącznika'@);
         par.prn1.add("par.TAB.POB_D$1",10,'Data pobrania'@);
         par.prn1.add("par.TAB.POB_G$3",10,'Godzina pobrania'@);
         par.prn1.add("par.TAB.POTW",13 ,'Potwierdzenie'@);
         par.prn1.add("par.TAB.LOGIN",20,'Login'@);
         par.prn1.s_frame();

         par.p7:=vp:=pierwszy:=zam:=b_rat:=h_rat:=1;
         par.p8:=par.p_ref:=lmt:=lp:=0;
         prn1:='par[1]';
         zal_ref:='';
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','b_rat','h_rat','pierwszy','zam','zal_ref','lp','tresc');
   P.cntx_pop();
   OSOBA.cntx_pop()
}
{COMMENT}OLD: wzalpobh.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par.ok}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par.p7}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par.p7:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Typy załączników'@}: {IF: par.SLO.lista=''}{<{ceil(lmt*h_rat)+19}'wszystkie'@
      }{ELIF: rep_export()}{par.SLO.lista
      }{ELSE}{STR.split(par.SLO.lista); ~~
      }{WHILE: STR.next()}{DO}{<{ceil(lmt*h_rat)+19} STR.line(par.prn1.width()-20) }
       {OD}
      {FI}
   {<{ceil(lmt*h_rat)+1}'%1: od %2 do %3'['Zakres dat'@,par.tab.OD$1,par.tab.DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W12'}
{SPACE: 'A'}
{IF: par.p_ref<>P.ref()}{ par.p8+=1;par.p_ref:=P.ref();~~ }{FI}
{IF: ~rep_export()}
   {<{ceil(lmt*b_rat)+1}}{form(par.p8,,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{ par.p6:=1; ~~ }
{MACRO-}
{MACRO: 'zal_dane'}
   {IF: ~rep_export() & ~par.p6}
      { vp:=~lineleft();~~ }
      {IF: lineleft() & lineleft()<=6}
         {IF: zam} {SUBSTITUTE: 'LINIAF'} {FI}
         {PAGE}
         {vp:=1;par.firstRec:=1;~~}
         {SUBSTITUTE: 'LINIAF'}
      {FI}
      {zam:=1;~~}
      {SUBSTITUTE: 'pracownik'}
      {REP: par.prn1.fhbar(lmt)}
      {  par.p6:=1; ~~ }
   {FI}
   { par.prn1.ini_line(); ~~ }
   {WHILE: par.prn1.next()}{DO}
      {REP: par.prn1.fline(lmt)}
   {OD}
   {IF: lineleft()<=6}
      {  par.p6:=0; ~~ }
   {FI}
{MACRO-}
{COMMENT}

   Przeglądaj kartotekę załączników w podanym lub pełnym zakresie dat.
   Pełny zakres - wszystkie pobrania załączników, ograniczony zakres - od daty
   do daty, od 'początku' kartoteki do daty lub od daty do 'końca'.

{COMMENT-}
{MACRO: tresc}
{  par.p6:=0;
   par.TAB.erase();
   _sql:='select '
            'ZAL_POBH.POB_D, ZAL_POBH.POB_G, ZAL_POBH.POTW, ZALACZ.P, ZALACZ.REFERENCE as REF, '
            'SLO_NAZ.NAZWA as ZAL_TYP, ZALACZ.ZAL_NAME, USERS.WEBLOGIN LOGIN '
            'from ZAL_POBH left join USERS join ZALACZ join SLO_NAZ '
            'where ZALACZ.FIRMA=:_f and (ZALACZ.P=:_a or ZALACZ.OSOBA=:_b)'+
            {? par.tab.DO=#0 || '' || ' and ZAL_POBH.POB_D<=to_date(:_d)'?}+
            {? par.tab.OD=#0 || '' || ' and ZAL_POBH.POB_D>=to_date(:_c)'?}+
            {? par.SLO.lista='' || '' || 'and SLO_NAZ.REFERENCE in (select REF from :_e) ' ?}+
            'order by ZAL_TYP, REF, ZAL_POBH.POB_D, ZAL_POBH.POB_G';

   _tab:=sql(_sql,P.ref(),P.OSOBA,par.tab.OD,par.tab.DO,par.SLO.WYBOR,exec('ref_firma','ustawienia'));

   {? _tab.first()
   || {!
      |? lp+=1;
         par.TAB.blank(1);
         par.TAB.P:=P.OSOBA().PIERWSZE+' '+OSOBA.NAZWISKO+' '+OSOBA.DRUGIE+' (Nr teczki '+P.T+')';
         par.TAB.NAZWISKO:=P.OSOBA().NAZWISKO;
         par.TAB.IMIONA:=P.OSOBA().PIERWSZE + ' ' + P.OSOBA().DRUGIE;
         par.TAB.TECZKA:=P.T;
         par.TAB.LP:=lp;
         par.TAB.ZAL_NAME:=_tab.ZAL_NAME;
         par.TAB.ZAL_TYP:=_tab.ZAL_TYP;
         par.TAB.ZAL_REF:=_tab.REF;
         par.TAB.POB_D:=_tab.POB_D;
         par.TAB.POB_G:=_tab.POB_G;
         par.TAB.POTW:=_tab.POTW;
         par.TAB.LOGIN:=_tab.LOGIN;
         par.TAB.add();
         _tab.next()
      !}
   ?};

   par.firstRec:=1;
   zal_ref:=''; ~~
}
{IF: ~rep_export() & pierwszy & par.TAB.first()} {pierwszy:=0;~~}{SUBSTITUTE: 'LINIAF'}{FI}
{FOR: par.TAB}{INDEX: par.ndx}{DO}
   {IF: par.firstRec}
   {zal_ref:=par.TAB.ZAL_REF;~~}
   {FI}
   {IF: ~par.firstRec & (zal_ref<>par.TAB.ZAL_REF)}
      {zal_ref:=par.TAB.ZAL_REF;
       par.firstRec:=1;~~}
      {IF: lineleft()<=6}
         { par.p6:=0; ~~ }
      {ELSE}
{par.prn1.bar(lmt)}
      {FI}
   {FI}
   {SUBSTITUTE: 'zal_dane'}
   {par.firstRec:=0;~~}
{OD}
{IF: ~rep_export() & par.TAB.first()}
   {REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}{zam:=0;~~}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}
{ {? rep_export()
  || b_rat:=charleft()/1; ~~
  || b_rat/=charleft(); ~~
  ?}
}
{SPACE: 'B'}
{  lmt:=int((charleft()-par.prn1.width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{IF: rep_export()}
{SUBSTITUTE: 'LINIAF'}
{FI}
{FOOTER: 0}
   { vp:=0; ~~ }
{FOOTER-}
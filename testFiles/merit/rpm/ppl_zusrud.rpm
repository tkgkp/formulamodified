:!UTF-8
{TITLE:ZUS RUD}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,5,5,5,5,1,1,'A4'}
{BEGIN:
   {? var_pres('zusr')>100 || obj_del(zusr) ?};
   zusr:=obj_new(50);
   {! _nx:=1..obj_len(zusr) |! zusr[_nx]:='' !};
   exec('czytaj','#stalesys',date(),KST);

   zusr[1]:='Z';
   zusr[2]:=KST.NIP;
   zusr[3]:=KST.REG;
   {? KST.PODFIZ<>0
   || zusr[7]:=KST.PODIME;
      zusr[8]:=KST.PODNAZ
   ?};
   zusr[9]:=KST.SKROT;
   zusr[10]:=KST.ULICA;
   zusr[11]:=KST.DOM;
   zusr[12]:=KST.LOKAL;
   zusr[13]:=KST.KOD;
   zusr[14]:=KST.MIASTO;
   zusr[15]:=KST.TEL;
   zusr[16]:=KST.EMAIL;
   {! _nx:=1..obj_len(zusr) |! zusr[_nx]:=~(-zusr[_nx]) !};

   {? KST.PODFIZ<>0 & zusr[4]=''
   || zusr[6]:=KST.PODURDAT
   || zusr[6]:=date(0,0,0)
   ?};

   P.cntx_psh(); P.prefix();
   ZC.cntx_psh(); ZC.prefix();
   exec('rpm_as_pdf_ini','rpm_pdf');
   VAR_DEL.delete('x','w','y','dl_z','ind','Licznik','__param','__exit','__ret','__zc',
                  '__ndx','__ndx2','__ndx3','__iter','__maxiter');

   ind:=0; Licznik:=0;
   y:=0;
   x:=obj_new(6);
   w:=obj_new(6);
   __maxiter:=2;

   __param:=tab_tmp(1,
             'DATAOD','DATE','Data zawarcia od',
             'DATADO','DATE','Data zawarcia do',
             'DATAWYP','DATE','Data wypełnienia zgłoszenia',
             'RODZAJ','INTEGER','Cel wysłania dokumentów',
             'WYGEN','INTEGER','Uwzględniać wygenerowane umowy',
             'GIODO','INTEGER','Przekazanie GIODO');
   __param.DATAOD:=__param.DATADO:=__param.DATAWYP:=date();
   __param.RODZAJ:=1;
   __param.WYGEN:=0;
   __param.add();
   _edit:=__param.mk_edit('Raport ZUS RUD'@,0,'ppl_listzrud');
   __param.win_esep(_edit,'Parametry raportu'@);
   __param.win_efld(_edit,,'DATAOD',,,12,0,,,,'Data, od której mają być uwzględniane umowy.'@);
   __param.win_efld(_edit,,'DATADO',,,12,0,,,,'Data, do której mają być uwzględniane umowy.'@);
   __param.win_efld(_edit,,'DATAWYP',,,12,0,,,,'Data wypełnienia zgłoszenia.'@);
   __param.win_efld(_edit,,'RODZAJ',,,,,,'Cel wysłania dokumentu'@,,'Cel wysłania dokumentu'@,
      'radio-buttons',,'Zgłoszenie'@,"1",'Korekta'@,"2");
   __param.win_efld(_edit,,'WYGEN',,,,,,'Raporty ZUS'@,,'Uwzględnienie wygenerowanych raportów ZUS'@,'check-box',
      'check_label="%1"' ['Uwzględnienie wygenerowanych raportów'@],"1","0");
   __param.win_efld(_edit,,'GIODO',,,,,0,'Dane osobowe'@,,'Czy rejestrować przekazanie danych osobowych?'@,
            'check-box','check_label=%1'['Przekazanie danych osobowych zostanie odnotowane'@],"1","0");
   __param.efld_opt(_edit,'mark=1',,'DATAOD');
   __param.efld_opt(_edit,'mark=1',,'DATADO');
   __param.efld_opt(_edit,'mark=1',,'DATAWYP');
   exec('ok_esc','#window',__param,_edit);
   __param.win_edit(_edit);

   __exit:=1;
   {? __exit:=__param.edit(
      "  _PAR:=cur_tab();
         {? (_chk:=__CHK.record(_PAR,,'DATAOD'))<>'' | (_chk:=__CHK.record(_PAR,,'DATADO'))<>'' |
            (_chk:=__CHK.record(_PAR,,'DATAWYP'))<>''
         || _chk
         |? _PAR.DATAOD<date(2021,1,1)
         || __CHK.err_fld(_PAR,'DATAOD',1,'Wprowadzono błędną datę.'@ )
         |? _PAR.DATAOD>_PAR.DATADO
         || __CHK.err_fld(_PAR,'DATADO',1,'Data nie może być wcześniejsza niż: \"Data zawarcia od\".'@)
         || 1
         ?}
      "
      )
   || _args:=exec('wybierz_args','pracownik');
      _args.DOMAIN:='PPL';
      _args.F_ZATR:='Z';
      _args.VIEW:='';
      _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
      _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
      __ret:=exec('wybierz','pracownik',_args);

      {? __ret.P.size()=0
      || __exit:=0
      ?}
   ?};
   zusr[1]:={? __param.RODZAJ=1 || 'Z' || 'K' ?};
   zusr[41]:=__param.DATAWYP;

   exec('oZus','rap_zus');
   exec('zus_win_sel','rap_zus','RUD','Z');

   _zusndx:=zus.tab.ndx_tmp('Osoba',1,'ZN',,0,'NAZWISKO',,0,'PIERWSZE',,0,'PESEL',,0,'DZA',,0);

   __ndx:=zus.tab.ndx_tmp('Osoba',1,'ZN',,0,'NAZWISKO',,0,'PIERWSZE',,0,'PESEL',,0,'DZA',,0);
   __ndx2:=zus.tab.ndx_tmp('Zlec',1,'ZN',,0,'PRZ',,0,'DZA',,0);
   __ndx3:=zus.tab.ndx_tmp('Wygen',1,'ZN',,0,'ZA',,0,'NAZWISKO',,0,'PIERWSZE',,0,'PESEL',,0,'DZA',,0);

   ZUS_RAP.index('ZC');
   ZUS_RAP.prefix();
   KOMM.init(,,'Umowy pominięte w wydruku'@)
}
{END:
   exec('rpm_as_pdf_end','rpm_pdf');
   VAR_DEL.delete('x','w','y','dl_z','ind','Licznik','zusr','__param','__exit','__ret','__zc',
                  '__ndx','__ndx2','__ndx3','__iter','__maxiter','zus');
   P.cntx_pop();
   ZC.cntx_pop();
   exec('stalesys','#stalesys')
}
{FONT: 'q'}
{EXIT: ~__exit}{
VAR_DEL.delete('__zc');
echo('Trwa przygotowywanie raportu, proszę czekać'@);
__zc:=sql('select ZC.REFERENCE REF, ZC.DZ '
          'from ZC '
          ' join RU using (ZC.RU, RU.REFERENCE) '
          ' join S_ZUS using (ZC.TTUB, S_ZUS.REFERENCE) '
          'where ZC.P in (select :_a.SQL from :_a) '
          ' and ZC.DZ>=to_date(:_b) '
          ' and ZC.DZ<=to_date(:_c) '
          ' and RU.K in (:_d) '
          ' and S_ZUS.KOD not like \'01%\' '
          'order by REF, ZC.DZ ',
          __ret.P,__param.DATAOD,__param.DATADO,'\'2\',\'6\''); ~~}
{IF:  var_pres('__zc')>100}
   {FOR: __zc}
   {DO}
      {IF: ZC.seek(__zc.REF,)}
         {IF: (~__param.WYGEN & ~ZUS_RAP.find_key(ZC.P().OSOBA,'RUD',ZC.ref())) | __param.WYGEN}
            {IF: ZC.memo_txt(0,1,'TRESC')<>'' & (ZC.P().OSOBA().PESEL<>'' | OSOBA.UR_DATA<>#0) &
                 ~(OSOBA.OBCY='N' & OSOBA.PESEL='') & ~(OSOBA.PESEL='' & (OSOBA.DOWOD='' & OSOBA.PASZPORT='')) }{
               exec('get_ZC_INFO','zlec_rh',__param.DATAWYP);
               zus.tab.blank();
               zus.tab.PREF:=#ZC.P;
               zus.tab.ZCREF:=#ZC.ref();
               zus.tab.PRZ:=#ZC.P;
               zus.tab.NAZWISKO:=ZC.P().OSOBA().NAZWISKO;
               zus.tab.PIERWSZE:=OSOBA.PIERWSZE;
               zus.tab.T:=P.T;
               zus.tab.PESEL:=OSOBA.PESEL;
               zus.tab.TTUB:=form((4+(|ZC_INFO.TTUB().KOD))+(1+(|ZC_INFO.PREM().KOD))+(1+(|ZC_INFO.STNP().KOD)),6);
               zus.tab.NU:=ZC.NU;
               zus.tab.DZA:=ZC.DZ;
               zus.tab.DOD:=ZC.DU;
               zus.tab.DDO:=ZC.DW;
               zus.tab.ZUS:=ZC_INFO.ZUS;
               zus.tab.FC:=ZC_INFO.FC;
               zus.tab.FW:=ZC_INFO.FW;
               zus.tab.KC:=ZC_INFO.KC;
               zus.tab.FP:=ZC_INFO.FP;
               zus.tab.ZA:=P.ZA;
               zus.tab.EMT:='N';
               zus.tab.ZN:='N';
               zus.tab.ZUA:=~ZUS_RAP.find_key(OSOBA.ref,'RUD',ZC.ref);
               zus.tab.add(); ~~}
            {ELSE}{
               _msg:=' '+ZC.P().OSOBA().NAZWISKO+' ' +OSOBA.PIERWSZE+' [nr teczki - '+form(P.T)+']:'@;
               {? KOMM.find_msg(_msg) || KOMM.set_root(_msg) || KOMM.sect_beg(_msg) ?};
               {? OSOBA.OBCY='N' & OSOBA.PESEL=''
               || _kom:='Umowa: %1, zawarta dnia: %2 - brak wprowadzonego numeru PESEL.'@[ZC.NU, $ZC.DZ];
                  KOMM.add(_kom,4)
               |? ~(OSOBA.PESEL<>'' | OSOBA.UR_DATA<>#0)
               || _kom:='Umowa: %1, zawarta dnia: %2 - brak wprowadzonego numeru PESEL lub daty urodzenia.'@
                        [ZC.NU, $ZC.DZ];
                  KOMM.add(_kom,4)
               |? OSOBA.PESEL='' & (OSOBA.DOWOD='' & OSOBA.PASZPORT='')
               || _kom:='Umowa: %1, zawarta dnia: %2 - brak wprowadzonego numeru PESEL '
                        'lub dokumentu potwierdzającego tożsamość.'@[ZC.NU, $ZC.DZ];
                  KOMM.add(_kom,4)
               ?};
               {? ~(ZC.memo_txt(0,1,'TRESC')<>'')
               || _kom:='Umowa: %1, zawarta dnia: %2 - brak wprowadzonego przedmiotu umowy.'@[ZC.NU, $ZC.DZ];
                  KOMM.add(_kom,4)
               ?};
               KOMM.sect_end(); ~~ }
            {FI}
         {FI}
      {FI}
   {OD}
{ELSE}{
   echo(''); ~~}
   {EXIT}
{FI}{
echo(''); ~~}
{IF: ~KOMM.empty()}{
   KOMM.select(); ~~}
{FI}
{IF: zus.tab.size()>0}{
   zus.tab.fld_attr(,2);
   {? zus.tab.select()
   || zus.tab.index(__ndx);
      zus.tab.prefix('T')
   || __exit:=0
   ?}; ~~}
{FI}
{EXIT:
   {? ~(zus.tab.size) | ~(__exit)
   || FUN.emsg('\nBrak umów do przetworzenia.\n'@);
      1
   || 0
   ?}}
{FOR: zus.tab}
   {INDEX: __ndx}
   {PREFIX: 'T'}
{DO}
   {IF: zus.tab.EMT='N'}
      {IF: ZC.seek(zus.tab.ZCREF,)}{
         zusr[17]:=ZC.P().OSOBA().PESEL;
         {? zusr[17]=''
         || {? OSOBA.DOWOD<>''
            || zusr[18]:='Dowód osobisty, ' + OSOBA.DOWOD
            || zusr[18]:='Paszport, ' + OSOBA.PASZPORT
            ?};
            zusr[19]:=OSOBA.UR_DATA
         || zusr[18]:='';
            zusr[19]:=date(0,0,0)
         ?};
         zusr[20]:=OSOBA.PIERWSZE;
         zusr[21]:=OSOBA.NAZWISKO;

         {! _nx:=22..40 |! zusr[_nx]:='' !}; ~~}
         {IF: {? ~exec('szukaj','osoba','C',ZC.DZ)
              || exec('szukaj','osoba','S',ZC.DZ)
              || 1
              ?}}{
            zusr[22]:=OS_ADRES.ULICA;
            zusr[23]:=OS_ADRES.DOM;
            zusr[24]:=OS_ADRES.LOKAL;
            zusr[25]:=OS_ADRES.KOD;
            zusr[26]:=OS_ADRES.MIASTO;
            zusr[27]:={? exec('os_adres_zagr','osoba') || OS_ADRES.KRAJ().NAZ || '' ?};
            zusr[34]:=OS_ADRES.TEL;
            zusr[35]:=OS_ADRES.EMAIL;~~}
         {FI}
         {IF: exec('szukaj','osoba','K',ZC.DZ) &
              (OS_ADRES.ULICA<>zusr[22] | OS_ADRES.DOM<>zusr[23] | OS_ADRES.LOKAL<>zusr[24] | OS_ADRES.KOD<>zusr[25] |
              OS_ADRES.MIASTO<>zusr[26] | OS_ADRES.TEL<>zusr[34] | OS_ADRES.EMAIL<>zusr[35])}{
            zusr[28]:=OS_ADRES.ULICA;
            zusr[29]:=OS_ADRES.DOM;
            zusr[30]:=OS_ADRES.LOKAL;
            zusr[31]:=OS_ADRES.KOD;
            zusr[32]:=OS_ADRES.MIASTO;
            zusr[33]:={? exec('os_adres_zagr','osoba') || OS_ADRES.KRAJ().NAZ || '' ?};
            zusr[34]:=OS_ADRES.TEL;
            zusr[35]:=OS_ADRES.EMAIL;~~}
         {FI}{
         __iter:=0;
         {! _nx:=42..44 |! zusr[_nx]:=date(0,0,0) !}; ~~}
         {FOR: zus.tab}
            {INDEX: __ndx2}
            {PREFIX: 'T',zus.tab.PRZ}
         {DO}
            {IF: zus.tab.EMT='N'}
               {IF: ZC.seek(zus.tab.ZCREF,)}{
                  __iter+=1;
                  zusr[(36+((__iter-1)*6))]:=ZC.DZ;
                  zusr[(37+((__iter-1)*6))]:=ZC.DU;
                  zusr[(38+((__iter-1)*6))]:=ZC.DW;
                  zusr[(39+((__iter-1)*6))]:=ZC.memo_txt(0,1,'TRESC');
                  zus.tab.EMT:='T';
                  zus.tab.put(); ~~}
               {FI}
            {FI}
            {EXIT: __iter>=__maxiter}
         {OD}{
         zusr[40]:=$__iter;

         zusr[18]:=~(-zusr[18]);
         zusr[39]:=~(-zusr[39]);
         zusr[45]:=~(-zusr[45]);
         {! _nx:=20..35 |! zusr[_nx]:=~(-zusr[_nx]) !};

         Licznik+=1;
         {? __param.GIODO=1 || exec('rap_zus','giodo',__param.DATAWYP,'RUD') ?}; ~~}
         {REP: '{INCLUDE: ppl_zusrud.rpi}'}
      {FI}
   {FI}
{OD}{

'Zapisanie w bazie informacji o wykonanych wydrukach'; ~~}
{FOR: zus.tab}
   {INDEX: __ndx3}
   {PREFIX: 'T','T'}
{DO}
   {IF: ZC.seek(zus.tab.ZCREF,)}
      {IF: ~ZUS_RAP.find_key(ZC.P().OSOBA,'RUD',ZC.ref)}{
         exec('get_ZC_INFO','zlec_rh',__param.DATAWYP);
         ZUS_RAP.prefix(); ZUS_RAP.blank();
         ZUS_RAP.RAPORT:='RUD';
         ZUS_RAP.ZC:=ZC.ref();
         ZUS_RAP.ZC_TTUB:=ZC_INFO.TTUB;
         ZUS_RAP.ZC_PREM:=ZC_INFO.PREM;
         ZUS_RAP.ZC_STNP:=ZC_INFO.STNP;
         ZUS_RAP.DATA:=date();
         ZUS_RAP.OSOBA:=P.OSOBA;
         ZUS_RAP.USER:=OPERATOR.USER;
         ZUS_RAP.add(); ~~}
      {FI}
   {FI}
{OD}
{EXIT:
   {? ~(Licznik>0)
   || FUN.emsg('\nBrak informacji do wykazania na deklaracji.\n'@);
      1
   || 0
   ?}
}
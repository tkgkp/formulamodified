:!UTF-8
{TITLE:S. Dokumenty VAT spoza bieżącego okresu VAT}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','PRN2','s','suma','lsuma','w','t','ndx_dok','wyjscie');
   wyjscie:=exec('dok_vat_okr','!fks_vat_zrej');
   {? wyjscie
   || PRN:=obj_new(@.CLASS.PRINT,11); PRN.sl_frame();
      PRN2:=obj_new(@.CLASS.PRINT,5); PRN2.sl_frame();
      w:=obj_new(16);
      s:=obj_new(16);
      suma:=obj_new(3); {! _ii:=1..3 |! suma[_ii]:=0 !}; lsuma:=0;
      t:=obj_new(7);
      t[2]:='PODSUMOWANIE';
      t[3]:='OGÓŁEM';
      t[5]:='napis nad tabelką';
      t[6]:=1;
      t[7]:='napis w podsumowaniu';
      PAR_WYDR.TITLE:='Pozycje dokumentów VAT spoza okresu VAT - '+exec('naz_okr_vat','!fks_vat_zrej',0,0);
      {? VAT7.TYPSUM='R'
      || ndx_dok:=__DOK.ndx_tmp(,,'REJ',,0,'RNR',,0);
         t[5]:=$'\'Rejestr księgowy: \'+__DOK.REJ'
      |? VAT7.TYPSUM='W'
      || ndx_dok:=__DOK.ndx_tmp(,,'REJVAT',,0,'REJ',,0,'RNR',,0);
         t[5]:=$'\'Rejestr VAT: \'+__DOK.REJVAT+\' - \'+__DOK.REJVATN'
      |? VAT7.TYPSUM='O'
      || ndx_dok:=__DOK.ndx_tmp(,,'ROK_F',,0,'OVAT',,0,'REJ',,0,'RNR',,0);
         t[5]:=$'\'Okres VAT: \'+{? __DOK.OVAT=\'\' || \'Zawieszony\' || __DOK.ROK_F+\'/\'+__DOK.OVAT ?}'
      || ndx_dok:=__DOK.ndx_tmp(,,'RTN',,0,'REJ',,0,'RNR',,0);
         t[5]:=$'\'Rodzaj transakcji: \'+__DOK.RTNB'
      ?}
   ?};~~
}
{END:
   VAR_DEL.delete('PRN','PRN2','s','suma','lsuma','w','t','ndx_dok','wyjscie','OkrVat','OkrVatNr','prn1','prn2','__DOK');~~
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
====================================================================================================
 Nazwa pliku: vat_okr.rpm
 Utworzony: 30.06.2014
 Autor: PMZ, MB
 Systemy: FIKS
====================================================================================================
Zawartosc: Zestawienie dokumentow VAT spoza okresu VAT
{COMMENT-}
{EXIT: wyjscie=0}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_pw2.rpi}
{INCLUDE: wsp_lic.rpi}
{FONT: 'T8G'}{SPACE: 'C'}
{COMMENT}------------------------------Budowanie tabeli-----------------------{COMMENT-}
{lmt:=charleft()-4;
   {! _ii:=1..2
   |!
      {? VAT7.TYPSUM='R'
      || PRN.add("form(8+w[12])",17,'R.Trans',0)
      || PRN.add("form(w[1])",17,'Rej.Ks',0)
      ?};
      PRN.add("form(w[2])",4,'Nr',0);
      {? VAT7.TYPSUM='W'
      || PRN.add("form(8+w[12])",8,'R.Trans',0)
      || PRN.add("form(w[3])",8,'Rej.VAT',0)
      ?};
      PRN.add("form(w[4])",20,'Symbol',0);
      PRN.add("form(w[5])",10,'D.wys.',0);
      {? VAT7.TYPSUM='O'
      || PRN.add("form(11+w[12])",11,'R.Trans',0)
      || PRN.add("form(w[7])",11,'Okres VAT',0)
      ?};
      PRN.add("form(w[8],,2,' ,')",18,'Netto',1);
      PRN.add("form(w[9])",5,'% VAT',1);
      PRN.add("form(w[10],,2,' ,')",16,'VAT',1);
      PRN.add("form(w[11],,2,' ,')",18,'Brutto',1);
      {? _ii=1
      ||
         t[6]+=charleft-PRN.width();
         VAR_DEL.delete('PRN');
         PRN:=obj_new(@.CLASS.PRINT,11); PRN.sl_frame()
      ?}
   !};

   PRN2.add("t[7]",PRN.width()-63,'',1);
   PRN2.add("form(s[1],,2,' ,')",18,'Netto',1);
   PRN2.add("form('')",5,'% VAT',1);
   PRN2.add("form(s[2],,2,' ,')",16,'VAT',1);
   PRN2.add("form(s[3],,2,' ,')",18,'Brutto',1);

   prn1:='PRN'; prn2:='PRN2';
~~}
{FONT: 'T8G'}{SPACE: 'C'}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{<3 'PARAMETRY WYDRUKU:'}
{<6   {? OPERATOR.DEPT
      || 'Jednostka księgowa: '+OPERATOR.DEPT().OD
      || 'Wszystkie jednostki księgowe' ?}}
{<6 {? VAT7.RTRAN='D' || 'Wszystkie rodzaje transakcji'
    || 'Rodzaj transakcji: '+
       {? VAT7.RTRAN='E' || 'Eksport towarów'
       |? VAT7.RTRAN='I' || 'Import towarów'
       |? VAT7.RTRAN='S' || 'Dostawa towarów na terytorium kraju'
       |? VAT7.RTRAN='W' || 'Operacje wewnątrzwspólnotowe'
       |? VAT7.RTRAN='Z' || 'Nabycie towarów i usług'
       || 'Inne'
       ?}
    ?}}
{<6 {? VAT7.REJ || 'Rejestr księgowy: '+VAT7.REJ().ODD().OD+'/'+VAT7.REJ().KOD || 'Wszystkie rejestry księgowe' ?}}
{<6 {? VAT7.RVAT_N || 'Rejestr VAT: '+VAT7.RVAT_N().SYM || 'Wszystkie rejestry VAT' ?}}
{<6 exec('typ_vat_str','!fks_vat_zrej',0)}

{FONT: 'T8GB'}{SPACE: 'C'}
{HEADER}
{FONT: 'T8G'}{SPACE: 'C'}
{SUBSTITUTE: 'HEAD'}

{HEADER-}
{FONT: 'T8G'}{SPACE: 'C'}
{IF: ~rep_expo()}{SUBSTITUTE: 'FOOT'}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{
   rsep:=PAR_WYDR.RSEP;
   {! _i:=1..15
   |! w[_i]:='';
      s[_i]:=0
   !};
~~}
{__DOK.index(ndx_dok);__DOK.prefix();__DOK.first();~~}
{FONT: 'T8B'}{SPACE: 'B'}{<{lmt+1}t[5]()}
{FONT: 'T8G'}{SPACE: 'C'}{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{rsep:=1;~~}
{FOR: __DOK}
{INDEX: ndx_dok}
{DO}
    { w[1]:=__DOK.REJ;
      w[2]:=__DOK.RNR;
      w[3]:=__DOK.REJVAT;
      w[4]:=__DOK.SYM;
      w[5]:=__DOK.DWYS;
      w[6]:=__DOK.KH;
      w[7]:={? __DOK.OVAT<>'' || __DOK.ROK_F+'/'+__DOK.OVAT || '' ?};
      w[8]:=__DOK.NETTO;
      w[9]:=__DOK.VATP;
      w[10]:=__DOK.VAT;
      w[11]:=__DOK.BRUTTO;
      w[12]:=__DOK.RTN;
      s[1]+=__DOK.NETTO;
      s[2]+=__DOK.VAT;
      s[3]+=__DOK.BRUTTO;
      s[10]+=1;
      t[7]:='Podsumowanie: '+t[5]();
    _f:=prn1+'.ini_line()'; _v:=($(_f))();
    _f:=prn1+'.length()'; il_lin:=($(_f))() + 2;
    ~~}
   {IF: lineleft<6}
      {SUBSTITUTE: 'LINIAF'}
      {IF: lineleft>0}
         {PAGE}
         {FONT: 'T8B'}{SPACE: 'B'}{<{lmt+1}t[5]()}
         {FONT: 'T8G'}{SPACE: 'C'}{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
      {FI}
      {IF: lineleft=0}
         {IF: ~rep_expo()}
            {FONT: 'T8B'}{SPACE: 'B'}{<{lmt+1}t[5]()}
            {FONT: 'T8G'}{SPACE: 'C'}{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
         {ELSE}
            {TABLE}
         {FI}
      {FI}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
   {TOTAL:t[5]()}
   {IF: lineleft<6}
      {SUBSTITUTE: 'LINIAF'}
      {IF: lineleft > 0}
         {PAGE}
         {FONT: 'T8B'}{SPACE: 'B'}{<{lmt+1}(14-t[7])}
         {FONT: 'T8G'}{SPACE: 'C'}{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
      {FI}
      {IF: lineleft=0}
         {IF: ~rep_expo()}
            {FONT: 'T8B'}{SPACE: 'B'}{<{lmt+1}(14-t[7])}
            {FONT: 'T8G'}{SPACE: 'C'}{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
         {ELSE}
            {TABLE}
         {FI}
      {FI}
   {FI}
   {IF: ~rep_expo()}
            {SUBSTITUTE: 'LINIAFS'}
         {ELSE}
            {TR}
         {FI}
   {TABLE-}
   { {! _ii:=1..3 |! suma[_ii]+=s[_ii] !}; s[1]:=s[2]:=s[3]:=0; lsuma+=1; ~~}
   {IF: s[10]<__DOK.size()}
      {IF: lineleft<11}
         {PAGE}
      {ELSE}
         {LINE:1}
      {FI}
      {FONT: 'T8B'}{SPACE: 'B'}{<{lmt+1}t[5]()}
      {FONT: 'T8G'}{SPACE: 'C'}{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
   {FI}
   {IF: lineleft<2}
      {SUBSTITUTE: 'LINIAF'}
      {IF: lineleft>0}{PAGE}{FI}
   {FI}
{OD}
{IF: ~rep_expo()}
   {IF: lsuma>1}
      {IF: lineleft<7}
         {PAGE}
      {ELSE}
         {LINE:1}
      {FI}
      {FONT: 'T8B'}{SPACE: 'B'}{<{lmt+1}'RAZEM'}
      {FONT: 'T8G'}{SPACE: 'C'}{SUBSTITUTE: 'TABHEAD'}{ vp:=1; t[7]:='RAZEM'; {! _ii:=1..3 |! s[_ii]:=suma[_ii] !};~~}
      {SUBSTITUTE: 'LINIAFS'}
   {FI}
{FI}
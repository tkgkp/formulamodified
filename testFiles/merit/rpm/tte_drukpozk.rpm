:!UTF-8
{TITLE:Porównanie kalkulacji karty technologicznej}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   params_set(params_get());
   KPOZK.cntx_psh();
   KALMAT.cntx_psh();
   KKTL.cntx_psh();
   delete:="VAR_DEL.delete('PRNKPK','PRNKLM','PRNNAG','PRNKPAR','PRNTTO','PRNWRK','__PRNTTO','__PRNWRK','__PRNKPAR','__PRNKLM','TABEDIT')";
   delete();
   dalej:=0;
   "---Obiekt wydruku naglowka porownania.";
   PRNNAG:=obj_new(@.CLASS.PRINT, 7); PRNNAG.s_frame();
   "---Obiekt wydruku pozycji kalkulacji karty technologicznej.";
   PRNKPK:=obj_new(@.CLASS.PRINT, 7); PRNKPK.s_frame();
   "---Obiekt wydruku cen.";
   PRNKLM:=obj_new(@.CLASS.PRINT, 8); PRNKLM.sl_frame();
   __PRNKLM:=tab_tmp(2,'KOD','STRING[15]','Kod surowca',
                       'OPIS','STRING[60]','Opis surowca',
                       'CENA','REAL','Cena surowca',
                       'CENA1','REAL','Cena surowca',
                       'POCH','STRING[11]','Pochodzenie',
                       'POCH1','STRING[11]','Poch1',
                       'TKTLNRK','STRING[20]','tech1',
                       'TKTL1NRK','STRING[20]','tech2',
                       'TKTLWER','STRING[6]','wer1',
                       'TKTL1WER','STRING[6]','wer2'
                       );
   PRNKPAR:=obj_new(@.CLASS.PRINT, 7); PRNKPAR.sl_frame();
   __PRNKPAR:=tab_tmp(1,'NR','INTEGER','Nr',
                       'OPIS','STRING[40]','Opis',
                       'WAR','REAL','Wartosc',
                       'WAR1','REAL','Wart1'
                       );
   PRNTTO:=obj_new(@.CLASS.PRINT, 7); PRNTTO.sl_frame();
   __PRNTTO:=tab_tmp(2,'KOD','STRING[10]','Kod operacji',
                       'NAZWA','STRING[50]','Nazwa operacji',
                       'STAWKA','REAL','Stawka operacji',
                       'STAWKA1','REAL','Stawka operacji');
   PRNWRK:=obj_new(@.CLASS.PRINT, 7); PRNWRK.sl_frame();
   __PRNWRK:=tab_tmp(2,'KOD','STRING[10]','Kod stanowiska',
                       'NAZWA','STRING[60]','Nazwa stanowiska',
                       'KH','REAL','Koszt godziny',
                       'KH1','REAL','Koszt godziny');
   prn1:='';
   color:='';
   lp:=0;
   lm:=ll:=vp:=lmt:=rsep:=0;
   pkpar:=pkalmat:=pkaloper:=pkalst:=1
}
{END:
   KKTL.cntx_pop();
   KALMAT.cntx_pop();
   KPOZK.cntx_pop();
   &dalej;
   delete(); &delete;
   &prn1;
   &color;
   &lp;
   &lm; &ll; &vp; &lmt; &rsep;
   &pkpar;&pkaloper;&pkalst;
   &pkalmat
}
  {TABEDIT:=tab_tmp(
     ,'KPAR','STRING[1]','Parametry kalkulacji'@
     ,'KALMAT','STRING[1]','Cennik pobranych surowców'@
     ,'KALOPER','STRING[1]','Lista stawek operacji'@
     ,'KALST','STRING[1]','Lista kosztów stanowisk'@
   );
   _wydr_naz:='drukpozk';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add();
      TABEDIT.KPAR:=TABEDIT.KALMAT:=TABEDIT.KALOPER:=TABEDIT.KALST:='T'
   || TABEDIT.KPAR:=WYDRUKIL.SORTUJ;
      TABEDIT.KALMAT:=WYDRUKIL.FAK;
      TABEDIT.KALOPER:=WYDRUKIL.RODZFAK;
      TABEDIT.KALST:=WYDRUKIL.FAKUM
   ?};
   _red:=TABEDIT.mk_edit('Wydruk kartotek stowarzyszonych'@,0,'tabedit1');
   TABEDIT.win_efld(_red,,'KPAR',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALMAT',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALOPER',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALST',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Zapisz'@],"'key:F2'");
   TABEDIT.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Anuluj'@],"'key:Esc'");
   TABEDIT.win_edit(_red);
   ~~}
   {IF: TABEDIT.edit()}
    { WYDRUKIL.SORTUJ:=TABEDIT.KPAR;
      WYDRUKIL.FAK:=TABEDIT.KALMAT;
      WYDRUKIL.RODZFAK:=TABEDIT.KALOPER;
      WYDRUKIL.FAKUM:=TABEDIT.KALST;
      WYDRUKIL.put(1);
      pkpar:=(-TABEDIT.KPAR='t');
      pkalmat:=(-TABEDIT.KALMAT='t');
      pkaloper:=(-TABEDIT.KALOPER='t');
      pkalst:=(-TABEDIT.KALST='t'); ~~}
   {ELSE}
      {EXIT}
   {FI}
{COMMENT}
  Wydruk porownujacy kalkulacje.
   [MK] - 2003/02/21 - [8.10]
   [TS] - [8.50] PR/WRT/PRODUKCJ/7.53/0002
  Wywolanie: Technologia -> Kalkulacja technologii -> Ta ->Ten->Porownanie->Ten -> drukuJ
{COMMENT-}
{COMMENT}--Definicja wiersza wydruku porownania kalkulacji.--------------------{COMMENT-}
{"---Definicja naglowka wydruku porownania.                  "; ~~}
{ PRNNAG.add("nr",5,'Nr kalk.',1);
  {? VAR.A_KKTL().TKTLW<>VAR.A_PKKTL().TKTLW
  || PRNNAG.add("ktm",30,'Produkt'@)
  ?};
  PRNNAG.add("dk",10,'Data kalkulacji'@,1);
  PRNNAG.add("dw",10,'Data ważności'@);
  PRNNAG.add("cd",10,'Ceny sur. z dnia'@,1);
  PRNNAG.add("exec('stan_opis','!tte_tec_ktkw',z)",17,'Stan kalkulacji'@);
  PRNNAG.add("war",13,'Wariant kalkulacji'@,1);~~}
{"---Definicja wiersza wydruku pozycji kalkulacji KPOZK.                  "; ~~}
{ PRNKPK.add("form(lp,,0,'9')",3,'Lp.'@,1);
  PRNKPK.add("form(KPOZK.RUBR().NR,,0,'9')",5,'Rubr.'@,1);
  PRNKPK.add("KPOZK.RUBR().OPIS",30,'Opis'@);
  PRNKPK.add("form(KPOZK.WART,,2)",12,'Wartość'@,1);
  PRNKPK.add("form(params_get().kpozk.WARTOSC,,2)",12,'Wartość por.'@,1);
  PRNKPK.add("form(KPOZK.WART-params_get().kpozk.WARTOSC,,2)",12,'Różnica'@,1);
  ~~}
{"---Definicja wiersza wydruku cennika surowcow pobranych do kalk. KALMAT."; ~~}
{ PRNKLM.add("__PRNKLM.KOD",15,'Kod surowca'@);
  PRNKLM.add("__PRNKLM.OPIS",25,'Opis'@);
  PRNKLM.add("form(__PRNKLM.CENA,,2)",8,'Cena'@,1);
  PRNKLM.add("form(__PRNKLM.CENA1,,2)",8,'Cena por.'@,1);
  PRNKLM.add("__PRNKLM.POCH",4,'Poch'@,1);
  PRNKLM.add("__PRNKLM.POCH1",4,'Poch por.'@,1);
  PRNKLM.add("__PRNKLM.TKTLNRK+' '+__PRNKLM.TKTLWER",20,'Technologia'@);
  PRNKLM.add("__PRNKLM.TKTL1NRK+' '+__PRNKLM.TKTL1WER",20,'Techn. por.'@);~~}
{"---Definicja wiersza wydruku cennika surowcow pobranych do kalk. KALMAT."; ~~}
{ PRNKPAR.add("__PRNKPAR.NR",5,'Nr'@,1);
  PRNKPAR.add("__PRNKPAR.OPIS",25,'Opis'@);
  PRNKPAR.add("form(__PRNKPAR.WAR,,10)",20,'Wartość'@,1);
  PRNKPAR.add("form(__PRNKPAR.WAR1,,10)",20,'Wartość por.'@,1);
  PRNKPAR.add("form(__PRNKPAR.WAR-__PRNKPAR.WAR1,,10)",20,'Różnica'@,1);
  ~~}
 {"---Definicja wiersza wydruku stawek operacji wykorzystanych w kalkulacji"; ~~}
{ PRNTTO.add("__PRNTTO.KOD",10,'Kod operacji'@);
  PRNTTO.add("__PRNTTO.NAZWA",60,'Nazwa operacji'@);
  PRNTTO.add("form(__PRNTTO.STAWKA,,2)",15,'Stawka'@,1);
  PRNTTO.add("form(__PRNTTO.STAWKA1,,2)",15,'Stawka por.'@,1);
  PRNTTO.add("form(__PRNTTO.STAWKA-__PRNTTO.STAWKA1,,2)",15,'Różnica'@,1);
  ~~}
{"---Definicja wiersza kosztow stanowisk pobranych do kalkulacji"; ~~}
{ PRNWRK.add("__PRNWRK.KOD",10,'Kod operacji'@);
  PRNWRK.add("__PRNWRK.NAZWA",60,'Nazwa operacji'@);
  PRNWRK.add("form(__PRNWRK.KH,,2)",15,'Koszt godziny'@,1);
  PRNWRK.add("form(__PRNWRK.KH1,,2)",15,'Koszt godziny por.'@,1);
  PRNWRK.add("form(__PRNWRK.KH-__PRNWRK.KH1,,2)",15,'Różnica'@,1);
  ~~}
  {INCLUDE: wsp_pw.rpi}
  {"---Wyswietl okienko decyzyjne, czy drukowac parametry kalkulacji      "; ~~}
  {"---oraz cennik surowcow pobranych do kalkulacji i stawek operacji.    "; ~~}
 {COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{ PAR_WYDR.TITLE:='PORÓWNANIE KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{prn1:='PRNNAG';~~}
{ lmt:=int((charleft()-PRNNAG.width())/2); ~~}

{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Typ karty:'@}{<{lmt+25} VAR.A_KKTL().NRK().TYP().TYP+' - '+VAR.A_KKTL().NRK().TYP().OPIS}
{<{lmt+1} 'Karta:'@}{<{lmt+25} VAR.A_KKTL().NRK().NRK+' '+'wersja:'@+' '+VAR.A_KKTL().NRK().WER}
                    {<{lmt+25} VAR.A_KKTL().NRK().OPIS}
{IF: VAR.A_KKTL().TKTLW=VAR.A_PKKTL().TKTLW}
{<{lmt+1} 'Produkt:'@} {<{lmt+25} VAR.A_KKTL().NRK().KTM().KTM+' - '+VAR.A_KKTL().NRK().KTM().N}
{FI}
{<{lmt+1} 'Karta kalkulowana na:'@} {<{lmt+25} form(VAR.A_KKTL().NRK().XJM,,2)+' '+VAR.A_KKTL().NRK().JM().KOD}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{MACRO: 'prnklm'}
{  KALMAT.index('KSN');
   KALMAT.prefix(VAR.A_KKTL);
   {? KALMAT.first()
   || {!
      |? __PRNKLM.KOD:=KALMAT.PT().KTM;
         __PRNKLM.OPIS:=KALMAT.PT().N;
         __PRNKLM.CENA:=KALMAT.PRICE;
         __PRNKLM.CENA1:=0;
         __PRNKLM.POCH:=KALMAT.POCH;
         __PRNKLM.POCH1:='';
         __PRNKLM.TKTLNRK:={? KALMAT.TKTL<>null() || KALMAT.TKTL().NRK || '' ?};
         __PRNKLM.TKTL1NRK:='';
         __PRNKLM.TKTLWER:={? KALMAT.TKTL<>null() || KALMAT.TKTL().WER || '' ?};
         __PRNKLM.TKTL1WER:='';
         __PRNKLM.add();
         KALMAT.next()
      !}
   ?};
   KALMAT.prefix(VAR.A_PKKTL);
   {? KALMAT.first()
   || {!
      |? __PRNKLM.prefix(KALMAT.PT().KTM);
         {? __PRNKLM.first()
         || __PRNKLM.CENA1:=KALMAT.PRICE;
            __PRNKLM.POCH1:=KALMAT.POCH;
            __PRNKLM.TKTL1NRK:={? KALMAT.TKTL<>null() || KALMAT.TKTL().NRK || '' ?};
            __PRNKLM.TKTL1WER:={? KALMAT.TKTL<>null() || KALMAT.TKTL().WER || '' ?};
            __PRNKLM.put()
         || __PRNKLM.KOD:=KALMAT.PT().KTM;
            __PRNKLM.OPIS:=KALMAT.PT().N;
            __PRNKLM.CENA1:=KALMAT.PRICE;
            __PRNKLM.CENA:=0;
            __PRNKLM.POCH1:=KALMAT.POCH;
            __PRNKLM.POCH:='';
            __PRNKLM.TKTL1NRK:={? KALMAT.TKTL<>null() || KALMAT.TKTL().NRK || '' ?};
            __PRNKLM.TKTLNRK:='';
            __PRNKLM.TKTL1WER:={? KALMAT.TKTL<>null() || KALMAT.TKTL().WER || '' ?};
            __PRNKLM.TKTLWER:='';
            __PRNKLM.add()
         ?};
         KALMAT.next()
      !}
   ?}
;~~}
{MACRO-}
{MACRO: 'prnpar'}
{  KPAR.index('KN');
   KPAR.prefix(VAR.A_KKTL);
   {? KPAR.first()
   || {!
      |? __PRNKPAR.NR:=KPAR.NRP;
         __PRNKPAR.OPIS:=KPAR.OPIS;
         __PRNKPAR.WAR:=KPAR.DEFAULT1;
         __PRNKPAR.add();
         KPAR.next()
      !}
   ?};
   KPAR.prefix(VAR.A_PKKTL);
   {? KPAR.first()
   || {!
      |? __PRNKPAR.prefix(KPAR.NRP);
         {? __PRNKPAR.first()
         || __PRNKPAR.WAR1:=KPAR.DEFAULT1;
            __PRNKPAR.put()
         || __PRNKPAR.NR:=KPAR.NRP;
            __PRNKPAR.OPIS:=KPAR.OPIS;
            __PRNKPAR.WAR1:=KPAR.DEFAULT1;
            __PRNKPAR.WAR:=0;
            __PRNKPAR.add()
         ?};
         KPAR.next()
      !}
   ?}
;~~}
{MACRO-}
{MACRO: 'prntto'}
{  KALTTO.index('KO');
   KALTTO.prefix(VAR.A_KKTL);
   {? KALTTO.first()
   || {!
      |? __PRNTTO.KOD:=KALTTO.TTOPER().KOD;
         __PRNTTO.NAZWA:=KALTTO.TTOPER().NA;
         __PRNTTO.STAWKA:=KALTTO.STAWKA;
         __PRNTTO.STAWKA1:=0;
         __PRNTTO.add();
         KALTTO.next()
      !}
   ?};
   KALTTO.prefix(VAR.A_PKKTL);
   {? KALTTO.first()
   || {!
      |? __PRNTTO.prefix(KALTTO.TTOPER().KOD,KALTTO.TTOPER().NA);
         {? __PRNTTO.first()
         || __PRNTTO.STAWKA1:=KALTTO.STAWKA;
            __PRNTTO.put()
         || __PRNTTO.KOD:=KALTTO.TTOPER().KOD;
            __PRNTTO.NAZWA:=KALTTO.TTOPER().NA;
            __PRNTTO.STAWKA1:=KALTTO.STAWKA;
            __PRNTTO.STAWKA:=0;
            __PRNTTO.add()
         ?};
         KALTTO.next()
      !}
   ?}
;~~}
{MACRO-}
{MACRO: 'prnwrk'}
{  KALWRK.index('KP');
   KALWRK.prefix(VAR.A_KKTL);
   {? KALWRK.first()
   || {!
      |? __PRNWRK.KOD:=KALWRK.PLACE().KOD;
         __PRNWRK.NAZWA:=KALWRK.PLACE().NA;
         __PRNWRK.KH:=KALWRK.KH;
         __PRNWRK.KH1:=0;
         __PRNWRK.add();
         KALWRK.next()
      !}
   ?};
   KALWRK.prefix(VAR.A_PKKTL);
   {? KALWRK.first()
   || {!
      |? __PRNWRK.prefix(KALWRK.PLACE().KOD,KALWRK.PLACE().NA);
         {? __PRNWRK.first()
         || __PRNWRK.KH1:=KALWRK.KH;
            __PRNWRK.put()
         || __PRNWRK.KOD:=KALWRK.PLACE().KOD;
            __PRNWRK.NAZWA:=KALWRK.PLACE().NA;
            __PRNWRK.KH1:=KALWRK.KH;
            __PRNWRK.KH:=0;
            __PRNWRK.add()
         ?};
         KALWRK.next()
      !}
   ?}
;~~}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{"---Drukuj naglowek porownania.                     "; ~~}
{nr:=form(VAR.A_KKTL().KNR);
 ktm:=KKTL.TKTLW().KTM().KTM+' - '+M.N;
 dk:=form(KKTL.DT);
 dw:=form(KKTL.DW);
 cd:={? KKTL.DT_D=date(0,0,0) || form(KKTL.DT) || form(KKTL.DT_D) ?};
 z:=KKTL.STAN;
 war:=KKTL.OPC().NAZ;~~}
{SUBSTITUTE: 'LINIA0'}
{nr:=form(VAR.A_PKKTL().KNR);
 ktm:=KKTL.TKTLW().KTM().KTM+' - '+M.N;
 dk:=form(KKTL.DT);
 dw:=form(KKTL.DW);
 cd:={? KKTL.DT_D=date(0,0,0) || form(KKTL.DT) || form(KKTL.DT_D) ?};
 z:=KKTL.STAN;
 war:=KKTL.OPC().NAZ;~~}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'LINIAF'}
{ prn1:='PRNKPK'; lmt:=int((charleft()-PRNKPK.width())/2); ~~}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{"---Drukuj pozycje kalkulacji karty technologicznej.                     "; ~~}
{ KPOZK.index('KR'); KPOZK.prefix(VAR.A_KKTL); ~~}
{IF: KPOZK.first()}{ dalej:=1; ~~}{FI}
{ lp:=0; ~~}
{WHILE: dalej}
{DO}
  {IF: KPOZK.RUBR().PRINTED='T'}
    {params_get().kpozk.prefix(KPOZK.RUBR().NR); params_get().kpozk.first();~~}
    { lp+=1; ~~}{SUBSTITUTE: 'LINIA0'}
  {FI}
  { dalej:=KPOZK.next(); ~~}
{OD}
{"---Drukuj ceny surowcow pobranych do kalkulacji (jezeli tak zadecydowal "; ~~}
{"---operator).                                                           "; ~~}
{IF: pkalmat}
  { PAR_WYDR.TITLE:='CENNIK SUROWCÓW POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  {SUBSTITUTE: 'prnklm'}
  {__PRNKLM.prefix(); ~~}
  {IF: __PRNKLM.first()}{PAGE}
{ dalej:=1; prn1:='PRNKLM'; lmt:=int((charleft()-PRNKLM.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=__PRNKLM.next(); ~~}
  {OD}
{FI}
{IF: pkpar}
  { PAR_WYDR.TITLE:='PARAMETRY KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  {SUBSTITUTE: 'prnpar'}
  {__PRNKPAR.prefix(); ~~}
  {IF: __PRNKPAR.first()}{PAGE}
{ dalej:=1; prn1:='PRNKPAR'; lmt:=int((charleft()-PRNKPAR.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=__PRNKPAR.next(); ~~}
  {OD}
{FI}
{IF: pkaloper}
  { PAR_WYDR.TITLE:='LISTA STAWEK OPERACJI POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  {SUBSTITUTE: 'prntto'}
  {__PRNTTO.prefix(); ~~}
  {IF: __PRNTTO.first()}{PAGE}
{ dalej:=1; prn1:='PRNTTO'; lmt:=int((charleft()-PRNTTO.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=__PRNTTO.next(); ~~}
  {OD}
{FI}
{IF: pkalst}
  { PAR_WYDR.TITLE:='LISTA KOSZTÓW STANOWISK POBRANYCH\nDO KALKULACJI KARTY TECHNOLOGICZNEJ'@; ~~}
  {SUBSTITUTE: 'prnwrk'}
  {__PRNWRK.prefix(); ~~}
  {IF: __PRNWRK.first()}{PAGE}
{ dalej:=1; prn1:='PRNWRK'; lmt:=int((charleft()-PRNWRK.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=__PRNWRK.next(); ~~}
  {OD}
{FI}

:!UTF-8
{TITLE:H. Skrócone zestawienie obrotów na kontach}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_108:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('tmp1','tmp2');
      VAR_DEL.delete('k','w','s','PRN','X_Xa','X_Xb','wyjscie','__th')";
   mag_108();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   tmp1:=tmp2:='';

   k:=obj_new(2); "--szerokosci kolumn--";
   w:=obj_new(2); "--wartosci kolumn--";
   s:=0; "--razem--";
   __th:=1;
   PRN:= obj_new(@.CLASS.PRINT,2); PRN.s_frame();
   k[1]:=20;
   k[2]:=20;
   w[1]:= w[2]:='';
   tryb:=rep_export();

   wyjscie:=1;

   _od:=date(ST.AR,ST.AM,1);
   _do:=date(ST.AR,ST.AM,0);

   _wydr_naz:='mag_108';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ||
      _od:=WYDRUKIL.OD_DATY;
      _do:=WYDRUKIL.DO_DATY
   ?};

   D_HELP.OD:=_od;
   D_HELP.DO:=_do;
   D_HELP.win_edit('ODDO');
   {? ST.MAG().IL='T'
   || FUN.info('Zestawienie niedostępne w magazynie z ewidencją wyłącznie ilościową.'@)
   |? exec('upr_cm','ceny')=0
   || FUN.info('Brak uprawnień do wartości magazynowych.'@)
   |? D_HELP.edit("
         {? D_HELP.DO>=D_HELP.OD
         || ''
         || FUN.info('Błędny zakres dat.'@); 'DO'
         ?}")
   ||
      WYDRUKIL.index('WYDRUKI');
      WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
      {? WYDRUKIL.first()
      ||
         WYDRUKIL.OD_DATY:=D_HELP.OD;
         WYDRUKIL.DO_DATY:=D_HELP.DO;
         WYDRUKIL.put();
         wyjscie:=0
      ?}
   ?}
}
{END:
   mag_108(); VAR_DEL.delete('mag_108')
}
{IF: exec('upr_cm','ceny')=0}{ FUN.info('Brak uprawnień do wartości magazynowych.'@);~~}{EXIT}{FI}
{IF: wyjscie=1}{EXIT}{FI}
{
PRN.add("w[1]",k[1],'Konto'@);
PRN.add("w[2]",k[2],'Wartość'@,1);

"--obliczenia--";
VAR_DEL.delete('X_Xa','X_Xb');
X_Xa:=sql('
   select
      case when KK2.SYM is null then KK1.SYM else KK2.SYM end KONTO,
      case when DK.PLUS=\'T\' then DK.WAR else -DK.WAR end W
   from @DK
      join @ND using(DK.N,ND.REFERENCE)
      left join KK KK1 using(ND.KK,KK1.REFERENCE)
      left join KK KK2 using(DK.KK,KK2.REFERENCE)
   where
      ND.Z=\'T\'
      and ND.MAG=:_e
      and ND.D>=to_date(:_a) and ND.D<=to_date(:_b)
      and ND.REFERENCE between \'nagdo:_c\' and \'nagdo:_dz\'
      and DK.REFERENCE between \'dokma:_c\' and \'dokma:_dz\''
    ,D_HELP.OD,D_HELP.DO,ST.ODDZ+($(D_HELP.OD~1)+2),ST.ODDZ+($(D_HELP.DO~1)+2),ST.MAG);
X_Xb:=sql('
   select KONTO KONTO, sum(W) W
   from :_a
   group by KONTO
   order by KONTO'
   ,X_Xa);

PAR_WYDR.TITLE:='SKRÓCONE ZESTAWIENIE OBROTÓW NA KONTACH MAGAZYNOWYCH'@;
prn1:='PRN'
;~~}
{IF: X_Xb.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2);~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{IF: ODDZ.cntx_psh();ODDZ.index('KOD2');ODDZ.prefix();_wyn:=ODDZ.find_key(ST.ODDZ,ST.ODDZ);ODDZ.cntx_pop();_wyn}
{<{lmt+1}}{'Oddział: %1 - %2'@[(ODDZ.KOD),(ODDZ.NAZ)]}
{FI}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Za okres: %1 - %2'@[(D_HELP.OD$1),(D_HELP.DO$1)]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}
   {<{lmt+1}}{'Parametry wydruku:'@}
   {IF: ODDZ.cntx_psh();ODDZ.index('KOD2');ODDZ.prefix();_wyn:=ODDZ.find_key(ST.ODDZ,ST.ODDZ);ODDZ.cntx_pop();_wyn}
   {<{lmt+1}}{'Oddział: %1 - %2'@[(ODDZ.KOD),(ODDZ.NAZ)]}
   {FI}
   {<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
   {<{lmt+1}}{'Za okres: %1 - %2'@[(D_HELP.OD$1),(D_HELP.DO$1)]}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {IF: __th}{SUBSTITUTE: 'TABHEAD'}{FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xb}
{DO}
   {
   w[1]:=form(X_Xb.KONTO);
   w[2]:=form(X_Xb.W,,2,'.,');
   s+=X_Xb.W;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{ __th:=0;~~}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}{SPACE: 'C'}
{
PRN.n_frame();
w[1]:='Razem:'@; PRN.FORM[1] :=0;
w[2]:=form(s,,2,'.,')
;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
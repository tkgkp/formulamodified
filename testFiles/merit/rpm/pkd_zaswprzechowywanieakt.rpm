:!UTF-8
{TITLE:Informacja o okresie przechowywania akt pracowniczych}
{VERSION: PEP}
{PRINTER: dziedzina:='PKD';
   exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PKD_ZASWPRZAKT')='T',,,,'I','B',,,,,,1,
   PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('Data','__CALL','tresc','cnt','lmt','licz','Text','indent','num','czy_szablon','czy_paperless');
   VAR_DEL.delete('tytul','czy_pep','__upr');
   _par:=params_get();
   czy_szablon:=exec('rep_set','rap_zew','PKD_ZASWPRZAKT',1)='T';
   tytul:='Informacja o okresie przechowywania akt pracowniczych';
   czy_pep:=var_pres('_par')>100 & var_pres('PEP',_par)>0;
   PAR_WYDR.TITLE:='';

   P.cntx_psh();
   KART_DEF.cntx_psh();
   KART_DEF.index('SYMBOL');
   KART_DEF.prefix('ZUS_RIA',);
   KART_DOD.cntx_psh();
   KART_DOD.index('KART_DOD');
   {? KART_DEF.first()
   || KART_DOD.prefix(exec('ref_firma','ustawienia'),KART_DEF.ref())
   ?};

   tresc:='pouczenie';
   cnt:=lmt:=licz:=0;
   _dt:=date(2019,1,1);
   __upr:=exec('uprawnieniawrap','pkd','Danych pracownika'@,'PKD_EZK_PPRA','PKD_EZK_ORPR');
   _d0:=date(0,0,0);

   {? +__upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@ [__upr])
   || P.clear();
      exec('czytaj','#stalesys',,KST,'ZUSOSW');

      _PRAC:=tab_tmp(3,
         'OSOBA','STRING[16]','OSOBA',
         'DZA','DATE','Data rozpoczęcia',
         'DZ','DATE','Data zakończenia',
         'REF','INTEGER','Wskazanie na pracownika'
      );

      _jestP:=var_pres('_par')>100 & var_pres('P',_par)=type_of(null());
      _paperless:=czy_pep & var_pres('active',_par.PEP)>0 & _par.PEP.active;
      {? _paperless
      || czy_paperless:=0;
         {? ~czy_szablon & exec('rep_set','rap_zew','PKD_ZASWPRZAKT')<>'T' & _par.PEP.fml_after=""
         || czy_paperless:=1
         ?}
      || czy_paperless:=1
      ?};
      _args:=exec('wybierz_args','pracownik');
      _args.DOMAIN:=dziedzina;
      _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
      _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
      _args.F_ZATR:=__PARSES.getVal('F_ZATR').KOD;
      _args.VIEW:='W';
      _args.SQL_WHERE:=
         '(P.DZ is not null and P.DZ<=to_date(\'%1\')) '
         'and '
         '((P.REFERENCE in (select H_UM.P from H_UM where H_UM.OD>=to_date(\'%2\'))) '
           'or '
           '((P.REFERENCE in (select H_UM.P from H_UM where H_UM.OD>=to_date(\'%3\') and H_UM.OD<to_date(\'%2\'))) '
             'and '
             '(P.OSOBA in ('
                'select KART_DOD.OSOBA from KART_DOD '
                'join KART_DEF using(KART_DOD.KART_DEF, KART_DEF.REFERENCE) '
                'where KART_DOD.OD is not null and KART_DEF.SYMBOL=\'%4\''
                ')'
             ')'
           ')'
         ')'
         [(date()+7)$1,_dt$1,date(1999,1,1)$1,'ZUS_RIA'];

      _TMP:={? _paperless
            || _args.WIELU:=~_paperless;
               _args.SQL_FROM:=" JOIN OSOBA using(P.OSOBA, OSOBA.REFERENCE)"+
                               " JOIN USERS using(USERS.OSOBA, OSOBA.REFERENCE)"+
                               " JOIN USERSF using(USERSF.USERS, USERS.REFERENCE)";
                  _args.SQL_WHERE+=' and '+"P.PORTAL='T' and USERSF.PORTAL2='T'";
               {? _par.PEP.P
               || exec('wybierz_silent','pracownik',_par.PEP.P).P
               || exec('wybierz_parses','pracownik',dziedzina,_args).P
               ?}
            || {? _jestP
               || exec('wybierz_silent','pracownik',_par.P).P
               || exec('wybierz','pracownik',_args).P
               ?}
            ?};
       params_set(
         'P',_TMP,
         'paperless',_paperless,
         'par',_par,
         'CALL',null(),
         'PRAC',_PRAC
      );
      {? _TMP.first()
      || P.cntx_psh();
         P.index('PRACOSOB');
         P.prefix();
         H_UM.cntx_psh();
         H_UM.index('OD');
         {!
         |? {? P.seek(_TMP.SQL,) & ({? _jestP || P.DZ>_d0 || 1 ?})
            || H_UM.prefix(P.ref());
               {? H_UM.last()
               || _PRAC.OSOBA:=$P.OSOBA;
                  _PRAC.REF:=#P.ref();
                  _PRAC.DZA:=H_UM.OD;
                  _PRAC.DZ:=H_UM.DO;
                  _PRAC.add()
               ?}
            ?};
            _TMP.next()
         !};
         H_UM.cntx_pop();
         P.cntx_pop()
      ?};
      __xr_ins:=exec('new_ins','rap_zew','PKD_ZASWPRZAKT')
   ?}
   }
{END:
   KART_DOD.cntx_pop();
   KART_DEF.cntx_pop();
   P.cntx_pop();
   {? var_pres('__xr_ins')=type_of(null()) || exec('del_ins','rap_zew',&__xr_ins) ?};
   VAR_DEL.delete('Data','__CALL','tresc','cnt','lmt','licz','Text','indent','num');
   VAR_DEL.delete('dziedzina','czy_szablon','czy_paperless','tytul','czy_pep','__upr')
}
{EXIT: +__upr}
{EXIT: ~params_get().PRAC.first()}
{EXIT: ~__xr_ins}
{MACRO: tresc}
{  cnt+=1;
   _ria:=0;
   _lata:=10;
   _rodz:=spli_str('S,C,K,I',',');
   _ulica:=_miasto:=_poczta:=_kraj:='';
   P.OSOBA();

   {! _ii:=1 .. obj_len(_rodz)
   |! {? _ulica='' & _miasto='' & _poczta='' & _kraj=''
         &
         exec('szukaj','osoba',_rodz[_ii],{? P.DZ=date(0,0,0) || date() || P.DZ ?})
      || _ulica:=exec('ulica','osoba');
         {? ~-OS_ADRES.MIASTO<>~-OS_ADRES.POCZTA || _miasto:=OS_ADRES.MIASTO ?};
         _poczta:=exec('poczta','osoba');
         _kraj:={? ~-OS_ADRES.KRAJ().SYM<>'PL' || ~-OS_ADRES.KRAJ().NAZ || '' ?}
      ?}
   !};

   _PRAC:=params_get().PRAC;

   {? _PRAC.DZA>date(2018,12,0)
   || _odbior:=date((_PRAC.DZ~1)+11,1,1);
      _zapomnij:=date((_PRAC.DZ~1)+11,1,0)
   || {? KART_DEF.first() & KART_DOD.find_key(OSOBA.ref()) & KART_DOD.OD<>date(0,0,0)
      || _ria:=1;
         _odbior:=date((KART_DOD.OD~1)+11,1,1);
         _zapomnij:=date((KART_DOD.OD~1)+11,1,0)
      || 'Poniższe nie występuje! Nie informujemy o 50 letnim okresie przechowywania akt.';
         'Formuła nie powinna wejść w poniższy warunek:';
         _lata:=50;
         _rok:=(P.DZ~1)+50;
         _mc:=P.DZ~2;
         _dz:=P.DZ~3;
         _dt:=date(_rok,_mc,0);
         {? _dz<_dt~3
         || _dt:=date(_rok,_mc,_dz)
         ?};
         _odbior:=_dt+1;

         {? _dt~3=1
         || _dt:=date(_dt~1,_dt~2,0)
         |? (_dt+1)~3=1
         || _dt+=1;
            _dt:=date(_dt~1,_dt~2,0)
         || _nm:=date(_dt~1,_dt~2,0)+1;
            _nm:=date(_nm~1,_nm~2,0);
            _dt:={? _dt~3<=_nm~3 || date(_nm~1,_nm~2,(_dt~3)-1) || _nm ?}
         ?};
         _zapomnij:=_dt
      ?}
   ?};

   exec('ins_par','rap_zew',__xr_ins,cnt,
      'F_NAZWA',KST.NAZWA,
      'MIEJSCE',PAR_WYDR.MIEJSC,
      'DATA',date()$4,
      'F_ULICA',exec('ulica','stalesys'),
      'F_POCZTA',exec('poczta','stalesys'),
      'F_REGON',KST.REG,
      'F_NKP',KST.NKP,
      'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?}
   );
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'NAZWISKO',P.OSOBA().NAZWISKO,
      'IMIE',form(OSOBA.PIERWSZE+' '+OSOBA.DRUGIE),
      'PLEC',OSOBA.PLEC,
      'DZ',_PRAC.DZ$4,
      'RIA',_ria,
      'ODBIOR',_odbior$4,
      'ZAPOMNIJ',_zapomnij$4,
      'LATA',_lata
   );
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'P_ULICA',_ulica,
      'P_MIASTO',_miasto,
      'P_POCZTA',_poczta,
      'P_KRAJ',_kraj
   );
   exec('ins_par','rap_zew',__xr_ins,cnt,'CZY_ZAP',{? params_get().paperless || '1' || ' ' ?});
   ~~
}
{MACRO-}
{FOR: params_get().PRAC}
{DO}
   {IF: _PRAC:=params_get().PRAC; P.seek(_PRAC.REF,,1) & _PRAC.DZ<=date()+7}
      {SUBSTITUTE: tresc}
      {  licz+=1; ~~}
   {FI}
{OD}
{  {? ~licz
   || FUN.info('Informacja dostępna po wygaśnięciu stosunku pracy.'@)
   ?}; ~~
}
{EXIT: ~exec('ready','rap_zew',__xr_ins) & ~(params_get().paperless & ~czy_paperless)}
{IF: czy_szablon}
   {FOR: params_get().PRAC}
   {DO}
      {IF: _PRAC:=params_get().PRAC; P.seek(_PRAC.REF,,1) & _PRAC.DZ<=date()+7}{
         P.OSOBA();
         _params:=obj_new('data_rozpoczecia','data_zakonczenia');
         _params.data_rozpoczecia:=params_get().PRAC.DZA;
         _params.data_zakonczenia:=params_get().PRAC.DZ;
         params_set('przechowywanie',_params);
         _paperless:=params_get().paperless;
         _exe:={? _paperless || 2 || 3 ?};
         _file:=exec('wypelnij','szablon','PKD_ZASWPRZAKT',,,_exe,,_paperless);
         {? _paperless & var_pres('_file')>0 & var_pres('INFO',_file)>0
         || exec('add_file_and_run','paperless_grp',
               P.ref(),_file.INFO,'Inne',tytul)
         ?};
         ~~
      }{FI}
   {OD}
   {EXIT}
{FI}
{IF: exec('rep_set','rap_zew','zasw_okr')='T'}
   {
   exec('use_tmp','rap_zew','zasw_okr');
   {? params_get().paperless
   || exec('add_file_and_run','paperless_grp',
         P.ref(),
         '@!Tmp\\macrotmp.doc',
         'Inne',
         tytul
      )
   ?};
   ~~ }
   {EXIT}
{ELSE}
{
   params_set(_par:=params_get());
   {? _par.paperless & ~czy_paperless
   || __CALL:=_par.par.PEP.CALL
   || __CALL:=proc_exec('pouc@zasw',#__xr_ins);
      {? czy_pep || _par.par.PEP.CALL:=__CALL ?}
   ?};
~~ }
   {IF: params_get().paperless & params_get().par.PEP.fml_after=""}
      {
      params_get().par.PEP.P:=P.ref();
      params_get().par.PEP.fml_after:="
         _pep:=obj_new('active','fml_after','P','CALL');
         _pep.active:=1;
         _pep.fml_after:=\"1\";
         _pep.P:=_a;
         _pep.CALL:=_b;
         {? var_pres('_params')>100
         || exec('obj_ntab_set','#array',_params,'PEP',_pep)
         || _params:=obj_new('PEP');
            _params.PEP:=_pep
         ?};
         params_set(_params);
         _name:='@'+tmp_dir()+exec('sep','#file')+'pkd_zaswprzechowywanieakt_%1'[$P.tm_stamp()]+'.pdf';
         rep_exec('pkd_zaswprzechowywanieakt',,0,_name,1,,1);
         exec('add_file_and_run','paperless_grp',
         _pep.P,_name,'Inne','Informacja o okresie przechowywania akt pracowniczych')
      ";
      ~~}
      {EXIT}
   {ELSE}
      {INCLUDE: p_zaswprzechowanieakt.rpi}
   {FI}
{FI}
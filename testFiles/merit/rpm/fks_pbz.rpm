:!UTF-8
{TITLE: Wydruk przelewów zbiorczych}
{PRINTER: FINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame(); w:=obj_new(6);
   PRNF:=obj_new(@.CLASS.PRINT,3); PRNF.s_frame();
   VAR_DEL.delete('suma');
   suma:=obj_new('K','V');
   suma.K:=suma.V:=0;
   color:=prn1:=prn2:=''; lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
   obj_del(PRN); obj_del(PRNF); obj_del(w);
   &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep;
   VAR_DEL.delete('suma');
   ~~
}
{
   PRN.add("w[1]",35,'Rozrachunek');
   PRN.add("w[2]",10,'Data otw.');
   PRN.add("w[3]",10,'Data wyst.');
   PRN.add("w[4]",10,'Termin pł.');
   PRN.add("w[5]",20,'Kwota',1,'#');
   PRN.add("w[6]",20,'Kwota VAT',1,'#');
   PRNF.add("'Razem'",74);
   PRNF.add("w[5]",20,,1,'#');
   PRNF.add("w[6]",20,,1,'#');
   PAR_WYDR.TITLE:='Przelew zbiorczy';
   prn1:='PRN'; prn2:=prn3:='PRNF'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'W8'}{SPACE: 'C'}

{<1 'Data przelewu: '+$PB.DP}
{<1 'Przelew z rachunku: '+RB.get_rbtx(3,PB.RD,PB.KRAJ_BAN().KODISO)}
{<1 'Przelew na rachunek: '+RB.get_rbtx(3,PB.RW,PB.KRAJ_BAN().KODISO)}
{<1 'Wierzyciel: '+PB.W}
{STR.split(PB.TYT);~~}
{<1 'Tytuł przelewu: '+STR.line(100)}
{WHILE: STR.next()}
{DO}
{<15 STR.line(100)}
{OD}
{IF: var_press('SP',PB)>0}
{<1 'Split payment: '+{? PB.SP='T' || 'Tak' || 'Nie' ?}}
{IF: var_press('SP_WYM',PB)>0 & PB.DATA_OD<>date(0,0,0)}
{<1 'Zakres dat: '+$PB.DATA_OD+' - '+$PB.DATA_DO}
{FI}
{FI}

{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{  rsep:=PAR_WYDR.RSEP; ~~}
{FOR: PB_OP}
{INDEX: 'PB'}
{PREFIX: PB.ref()}
{DO}
   {
      w[1]:=PB_OP.SYM_ZEW;
      w[3]:=$PB_OP.DSP;
      w[5]:=form(PB_OP.K,,2,' ,');
      w[6]:=form(PB_OP.SP_V,,2,' ,');
      suma.K+=PB_OP.K;
      suma.V+=PB_OP.SP_V;
      OP.cntx_psh();
      drukuj:=exec('find_op','rozrach_przel');
      {? drukuj
      || w[2]:=$OP.DO;
         w[4]:=$OP.TZ
      || w[2]:=w[4]:=$date(0,0,0)
      ?};
      OP.cntx_pop();
      ~~
   }
   {IF: drukuj}
   {FONT: 'T8G'}
   {ELSE}
   {FONT: 'T8U'}
   {FI}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}------------------------------REPORT-FOOTER----------------{COMMENT-}
{  w[1]:='';
   w[5]:=form(suma.K,,2,' ,');
   w[6]:=form(suma.V,,2,' ,');
   ~~
}
{IF: lineleft()<>0 & lineleft()<3}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8G'}
{SUBSTITUTE: 'LINIAFS'}
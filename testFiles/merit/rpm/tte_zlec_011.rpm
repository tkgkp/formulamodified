:!UTF-8
{TITLE:K. Wydruk porównawczy analizy z wynikami TKW}
{PRINTER:  exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,PAR_WYDR.MLEFT,0,1,,'enable'}
{BEGIN:
   dalej:=0;
   PRN:=obj_new(@.CLASS.PRINT,13); PRN.sl_frame();
   prn1:='PRN';
   tryb:=rep_export();
   color:='';
   typ:='1';
   lm:=ll:=vp:=lmt:=rsep:=kalval1:=ilwyk:=kalval3:=ilwyk1:=0;
   "---Zmienne, ktorych wartosci sa drukowane.";
   anval:='';
   "---Symbol zlecenia odpowiadajacego biezacej analizie.";
   zlec:='';
   anvalue:='';
   anval1:='';
  "---Wartosc odpowiadajaca biezacej anlizie.";
   "---Symbol karty technologicznej odpowiadajacej zleceniu z biezacej analizy.";
  ktl:='';
  ktm:=null;
   "---Wartosc kalkulacji karty technologicznej odpowiadajacej technologi      ";
   "---biezacego zlecenia.";
   kalvalue:=0;
   kalval2:=0;
   kalval4:=0;
  "---Numer kalkulacji.";
   kalnr:=0;
   "---Data kalkulacji.";
   kaldate:=date(0,0,0);
   "---REF ostatniej pozycji w tabeli analiz zlecen, spelniajacej warunek.     ";
   "---Zmienna uzywana tylko w ramkologii.";
   lref:=null();
   "---Okres - OKR.ref";
   okrref:=null();
   "---Wartosc kolumny Uwagi.";
   uwagi:=''
}
{END:
   &dalej;
   obj_del(PRN);
   &prn1;
   &color;
   &ktm;
   &lm; &ll; &vp; &lmt; &rsep;
   "---Sekcja zwalniania funkcji.";
   &anzhval;
   &kktprice;
   &getlref;
   &val;
   &typ;
   "---";
   &zlec;
   &anvalue;
   &anval;
   &anval1;
   &ktl;
   &kalval2;
   &kalval4;
   &kalvalue;
   &kalnr;
   &kaldate;
   &lref;
   &ilwyk;
   &okrref;
   &uwagi;
   &tryb
}
{COMMENT}
  Wydruk porownawczy TKW z wynikami analizy.
  [SS] - 2001.04.03 - [7.41]
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
  [TS] - 2001/10/08 - [7.53] OWwSK003_aneks1
  [MKO] - 2001/10/08 - [7.53] - dodanie na wydruku wartosci surowca
              i robocizny przypadajacej na jednostke wyrobu dla kalkulacji
              technologii i analizy zlecenia (OWwPR011/5.5)
  [MKO] - 2002/04/11 - [7.60] - uwzglednienie dodania jednego pola A01 oraz
                                    zmian indesków.
{COMMENT-}
{"---Zbior funkcji wykorzystywanych w biezacej definicji wydruku.         "; ~~}
{MACRO: 'functions'}
 { "--------------------------------------------------------------------------";
   "---Zwraca wartosc z pozycji biezacej analizy zlecenia, odpowiadajaca      ";
   "---rubryce o numerze okreslonym przez parametr _a. Jezeli w analizie      ";
   "---zlecenia brakuje rubryki o numerze _a, to funkcja zwraca 0.            ";
   "---Parametry:                                                             ";
   "---_a:NUMBER - numer rubryki, z ktorej bedzie odczytana wartosc           ";
   anzhval:="
     ANZP.cntx_psh();
     _result:=0;
     {? _<1
     || _result:=-1;
        FUN.emsg('Funkcja ANZHVAL wydruku ANZZ_002.RPM:\nniewłaściwa liczba parametrów!')
     ?};
     {? _result=0
     || ANZP.index('NRARUB');
        ANZP.prefix(ANZH.ref(),_a);
        {? ANZP.first()
        || _result:=ANZP.WAR
        || _result:=0
        ?}
     || _result:=0
     ?};
     ANZP.cntx_pop();
     _result
   ";
   "--------------------------------------------------------------------------";
   "--------------------------------------------------------------------------";
   "---Zwraca wartosc z pozycji biezacej karty technologicznej, odpowiadajaca ";
   "---rubryce o numerze okreslonym przez parametr _a. Jezeli w karcie        ";
   "---technologicznej brakuje rubryki o numerze _a, to funkcja zwraca 0.     ";
   "---Parametry:                                                             ";
   "---_a:NUMBER - numer rubryki, z ktorej bedzie odczytana wartosc           ";
   val:="
     TKTL.cntx_psh();
     TKTL.clear();
     _result:=0;
     {? _<1
     || _result:=-1;
        FUN.emsg('Funkcja VAL wydruku ANZZ_002.RPM:\nniewłaściwa liczba parametrów.')
     ?};
     {? _result=0
     || KPOZK.index('KR');
        KPOZK.prefix(KKTL.ref(),_a);
        {? KPOZK.first()
        || _result:=KPOZK.WART
        || _result:=0
        ?}
     || _result:=0
     ?};
     TKTL.cntx_pop();
     _result
   ";
   "--------------------------------------------------------------------------";
   "---Zwraca cene jednostkowa wg kosztu wytworzenia wyrobu dla biezacej     ";
   "---kalkulacji karty technologicznej. Jezeli w kalkulacji karty            ";
   "---technologicznej nie mozna znalezc rubryki zadeklarowanej na typie      ";
   "---karty, to zwraca -1.";
   kktprice:="
     _result:=0;
     TKTL.cntx_psh();
     TKTL.clear();
     'Numer rubryki z cena wyrobu.';
     _rubr:=KKTL.NRK().TYP().CRUB;
     KPOZK.index('KR');
     KPOZK.prefix(KKTL.ref(),_rubr);
     {? KPOZK.first()
     || _result:=KPOZK.WART
     || _result:=-1
     ?};
     TKTL.cntx_pop();
     _result
   ";
   "--------------------------------------------------------------------------";
   "---Zwraca REF ostatniej pozycji w tabeli ANZH, spelniajacej warunek dla   ";
   "---biezacego wydruku. Jezeli taka pozycja nie istnieje, zwraca null.      ";
   OKR.cntx_psh();
   OKR.index('NAZ');
   OKR.prefix();
   {? OKR.find_key(REF.FIRMA, ST.OKR) || okrref:=OKR.ref ?};
   OKR.cntx_pop();
   getlref:="
     ANZH.index('OKRES');
     ANZH.prefix(typ,okrref);
     _result:=null();
     {? ANZH.last()
     || {!|?
          {? ANZH.ZLEC().KTL<>null()
          || _result:=ANZH.ref()
          ?};
          ANZH.prev() & _result=null()
        !}
     ?};
     _result
   ";
   "----------------------------------------------------------------------"; ~~}
{MACRO-}
{SUBSTITUTE: 'functions'}
{"---Definicja wiersza wydruku porownawczego.                             "; ~~}
{ PRN.add("zlec",20,'Zlecenie'@);
  PRN.add("form(ANZH.NRA,7,0,,'9')",7,'Nr analizy'@,1);
  PRN.add("form(ANZH.DT)",10,'Data analizy'@);
  PRN.add("form(anval,10,2)",10,'Koszt surowców'@,1);
  PRN.add("form(anval1,10,2)",10,'Koszt robocizny'@,1);
  PRN.add("form(anvalue,10,2)",10,'Cena'@,1);
  PRN.add("ktl",15,'Karta technologiczna'@);
  PRN.add("form(kalnr,7,0,,'9')",10,'Nr kalkulacji'@,1);
  PRN.add("form(kaldate,10)",10,'Data kalkulacji'@);
  PRN.add("form(kalval2,10,2)",10,'Koszt surowców TKW'@,1);
  PRN.add("form(kalval4,10,2)",10,'Koszt robocizny TKW'@,1);
  PRN.add("form(kalvalue,10,2)",10,'TKW'@,1);
  PRN.add("uwagi",13,'Uwagi'@);
  PAR_WYDR.TITLE:='PORÓWNANIE TKW Z WYNIKAMI ANALIZY'@; ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Wylicza wartosci zmiennych drukowanych w kazdym wierszu: analiza,    "; ~~}
{"---zlec, ktl, kalvalue.                                                 "; ~~}
{MACRO: 'prepare'}
  {"---Przypisz symbol zlecenia.                                          "; ~~}
  {zlec:=ANZH.ZLEC().SYM; ~~}
  {"---Odczytaj cene jednostkowa z analizy zlecenia.                      "; ~~}
  {anvalue:=anzhval(ANZH.ZLEC().TYP().CRUBJEDN); ~~}
  {"---Odczytaj koszt materialow z analizy zlecenia.                      "; ~~}
  {anval:=anzhval(ANZH.ZLEC().TYP().CRUBMAT); ~~}
  {ilwyk:=anzhval(ANZH.ZLEC().TYP().CRUBIL);~~}
  {IF: ilwyk=0}
  {anval:=0;~~}
  {ELSE}
  {anval:=(anval/ilwyk);~~}
  {FI}
  {"---Odczytaj koszt robocizny z analizy zlecenia.                       "; ~~}
  {anval1:=anzhval(ANZH.ZLEC().TYP().CRUBROB); ~~}
  {ilwyk1:=anzhval(ANZH.ZLEC().TYP().CRUBIL);~~}
  {IF: ilwyk1=0}
  {anval1:=0;~~}
  {ELSE}
  {anval1:=(anval1/ilwyk1);~~}
  {FI}
  {"---Przypisz symbol karty technologicznej.                             "; ~~}
  {ktl:=ANZH.ZLEC().KTL().NRK;
   ktm:=ANZH.ZLEC().KTM; ~~}
  {"---Przejdz do odstatniej zatwierdzonej kalkulacji.                    "; ~~}
  {KKTL.index('KZ'); ~~}
  {KKTL.prefix(TKTL.ref(),'T',ktm); ~~}
  {IF: KKTL.last()}
    {"---Odczytaj cene jednostkowa,koszt surowcow,robocizny z ostatniej       "; ~~}
    {"---zaakceptowanej kalkulacji odpowiadajacej biezacej analizie zlecenia. "; ~~}
    {kalvalue:=kktprice(); ~~}
    {kalval1:=val(KKTL.NRK().TYP().CRUBIL);~~}
    {kalval2:=val(KKTL.NRK().TYP().CRUBMAT);~~}
    {IF: kalval1=0}
    {kalval2:=0;~~}
    {ELSE}
    {kalval2:=(kalval2/kalval1);~~}
    {FI}
    {kalval3:=val(KKTL.NRK().TYP().CRUBIL);~~}
    {kalval4:=val(KKTL.NRK().TYP().CRUBROB);~~}
    {IF: kalval3=0}
    {kalval4:=0;~~}
    {ELSE}
    {kalval4:=(kalval4/kalval3);~~}
    {FI}
    {"---Jaki numer kalkulacji.                                          "; ~~}
    {kalnr:=KKTL.KNR; ~~}
    {"---Jaka data kalkulacji.                                           "; ~~}
    {kaldate:=KKTL.DT; ~~}
    {uwagi:='OK'@; ~~}
  {ELSE}
    {kalnr:=0; ~~}
    {kaldate:=date(0,0,0); ~~}
    {kalvalue:=0; ~~}
    {uwagi:='Brak zatwierdzonej kalkulacji'@; ~~}
  {FI}
{MACRO-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Rok obrotowy: %1'@[$ST.AR]}
{<{lmt+1} 'Okres: %1'@[form(ST.AM,-2)]}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ ANZH.index('OKRES'); ANZH.prefix(typ,okrref);~~}
{IF: ANZH.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
  {IF: ANZH.ZLEC().KTL<>null()}
    {SUBSTITUTE: 'prepare'}
    {SUBSTITUTE: 'LINIA0'}
  {FI}
  { dalej:=ANZH.next(); ~~}
{OD}

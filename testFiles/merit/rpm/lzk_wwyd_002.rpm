:!UTF-8
{TITLE:B. Zestawienie zamówień dostaw}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   wwyd_002:="
      VAR_DEL.delete('ww','kk','doklw','lp','PRN','tryb');
      VAR_DEL.delete('kh','stan','zakres','sort1');
      VAR_DEL.delete('odd','dod');
      VAR_DEL.delete('wydr_naz','wydr_edi');
      VAR_DEL.delete('wyjscie','X_Xw1');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep','s')
   ";
   wwyd_002();

   _ilk:=8;
   PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();

   kk:=obj_new(_ilk); "--szerokosc kolumn--";
   ww:=obj_new(_ilk); "--wartosci kolumn--";
   doklw:=2; "--dokladnosc wartosci--";
   ss:=0; "--sumator--";

   kk[1]:=4;   "--Lp.--";
   kk[2]:=20;  "--Zamowienie--";
   kk[3]:=34;  "--Dostawca--";
   kk[4]:=30;  "--Stan--";
   kk[5]:=10;  "--Data--";
   kk[6]:=10;  "--Termin realizacji--";
   kk[7]:=14;  "--Wartosc--";
   kk[8]:=5;   "--Archiwalne--";
   tryb:=rep_export();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   kh:=stan:=zakres:=sort1:='';
   odd:=dod:=date(0,0,0);
   lp:=0;

   wydr_naz:='wwyd_002';
   wydr_edi:='WWYD_002';

   KH.cntx_psh;
   KH.win_dict('WER_SLO'); KH.actions('WER_SLO','W');

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("{? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || '' || FUN.info('Błędny zakres dat.'@);'DO_DATY' ?}")
   ||
      {? WYDRUKIL.put
      || wyjscie:=0;
         kh     :=WYDRUKIL.KH_OD().KOD;
         odd    :=WYDRUKIL.OD_DATY;
         dod    :=WYDRUKIL.DO_DATY;
         stan   :=WYDRUKIL.STANZAM;
         sort1  :=WYDRUKIL.SORTUJ;
         zakres :={? WYDRUKIL.RADIOB_1=2 || 'Z' |? WYDRUKIL.RADIOB_1=1 || 'A' || 'W' ?}
      ?}
   ?};
   KH.actions('WER_SLO','');
   KH.cntx_pop
}
{END:
   wwyd_002(); VAR_DEL.delete('wwyd_002')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("ww[1]",kk[1],'Lp.'@,1) ?};
PRN.add("ww[2]",kk[2],'Zamówienie'@);
PRN.add("ww[3]",kk[3],'Dostawca'@);
PRN.add("ww[4]",kk[4],'Stan'@);
PRN.add("ww[8]",kk[8],'Arch.'@);
PRN.add("ww[5]",kk[5],'Data'@);
PRN.add("ww[6]",kk[6],'Termin realizacji'@);
{? exec('upr_cm','ceny') || PRN.add("ww[7]",kk[7],'Wartość'@,1) ?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'SYM'    ,'STRING[20]'  ,'Symbol zamówienia',
   'KH'     ,'STRING[70]'  ,'Dostawca',
   'STAN'   ,'STRING[60]'  ,'Stan zamówienia',
   'DATA'   ,'DATE'        ,'Data przyjęcia',
   'DTPREAL','DATE'        ,'Termin realizacji',
   'NET'    ,'REAL'        ,'Wartość netto',
   'ARCH'   ,'STRING[1]'   ,'Archiwalne');
X_Xw1.index(X_Xw1.ndx_tmp(,,{? sort1='W' || 'NET' |? sort1='S' || 'STAN' || 'SYM' ?},,));

"--obliczenia w masce zbiorczej--";
ZD_NAG.index('SYM'); ZD_NAG.prefix();
{? (zakres='W' | zakres='A') & ZD_NAG.first
||
   {!
   |? {? ZD_NAG.STAT_REJ<>'A'
         & (ZD_NAG.DATA >= odd | odd=date(0,0,0)) & (ZD_NAG.DATA <= dod | dod=date(0,0,0))
         & (kh='' | kh=ZD_NAG.KH().KOD)
         & (stan='' | stan=ZD_NAG.STAN)
      ||
         X_Xw1.SYM      :=ZD_NAG.SYM;
         X_Xw1.KH       :=ZD_NAG.KH().KOD+' '+KH.NAZ;
         X_Xw1.STAN     :=(exec('zdnag_rek','zamdst_nag'); POM.STAN);
         X_Xw1.DATA     :=ZD_NAG.DATA;
         X_Xw1.DTPREAL  :=ZD_NAG.DTPREAL;
         X_Xw1.NET      :=ZD_NAG.WAR;
         X_Xw1.ARCH     :='';
         X_Xw1.add
      ?};
      ZD_NAG.next
   !}
?};
"--obliczenia w maskach archiwalnych--";
OKR.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? (zakres='W' | zakres='Z') & OKR.first
||
   ZD_NAG.cntx_psh; ZD_POZ.cntx_psh; ZD_RN.cntx_psh;
   {!
   |? _maska:=ST.ODDZ+form(form(OKR.ROK,,,'99'),-2);
      ZD_NAG.use('zdnag'+_maska);
      ZD_POZ.use('zdpoz'+_maska);
      ZD_RN.use('zdhin'+_maska);
      ZD_NAG.index('SYM'); ZD_NAG.prefix();
      {? ZD_NAG.first
      ||
         {!
         |? {? ZD_NAG.STAT_REJ<>'A'
               & (ZD_NAG.DATA >= odd | odd=date(0,0,0)) & (ZD_NAG.DATA <= dod | dod=date(0,0,0))
               & (kh=ZD_NAG.KH().KOD | kh='')
               & (stan=ZD_NAG.STAN | stan='')
            ||
               X_Xw1.SYM      :=ZD_NAG.SYM;
               X_Xw1.KH       :=ZD_NAG.KH().KOD+' '+KH.NAZ;
               X_Xw1.STAN     :=(exec('zdnag_rek','zamdst_nag'); POM.STAN);
               X_Xw1.DATA     :=ZD_NAG.DATA;
               X_Xw1.DTPREAL  :=ZD_NAG.DTPREAL;
               X_Xw1.NET      :=ZD_NAG.WAR;
               X_Xw1.ARCH     :='T';
               X_Xw1.add
            ?};
            ZD_NAG.next
         !}
      ?};
      OKR.next
   !};
   ZD_NAG.cntx_pop; ZD_POZ.cntx_pop; ZD_RN.cntx_pop
?};
OKR.cntx_pop();

PAR_WYDR.TITLE:='Zestawienie zamówień od klienta'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział: %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1}}{IF: (kh<>'')}{'Kontrahent: %1'@[kh]}{ELSE}{'Kontrahent: bez ograniczeń'@}{FI}
{<{lmt+1}}{'Zakres dat: od %1 do %2'@[$odd,$dod]}
{<{lmt+1}}{IF: stan<>''}{
   ZD_NAG.cntx_psh; ZD_NAG.STAN:=stan; exec('zd_pst','zamdst_nag'); ZD_NAG.cntx_pop; 'Stan zamówienia: %1'@[POM.STAN]}{ELSE}{'Stan zamówienia: bez ograniczeń'@}{FI}
{<{lmt+1}}{{? sort1='W' || 'Sortowanie wg: wartość'@ |? sort1='S' || 'Sortowanie wg: stan zamówienia'@ || 'Sortowanie wg: symbol zamówienia'@ ?}}
{<{lmt+1}}{{? zakres='Z' || 'Zakres zamówień: zarchiwizowane'@ |? zakres='A' || 'Zakres zamówień: aktywne'@ || 'Zakres zamówień: wszystkie'@ ?}}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xw1}
{DO}
   {
   lp+=1;
   ww[1]:=form(lp);
   ww[2]:=form(X_Xw1.SYM);
   ww[3]:=form(X_Xw1.KH);
   ww[4]:=form(X_Xw1.STAN);
   ww[5]:=form(X_Xw1.DATA);
   ww[6]:=form(X_Xw1.DTPREAL);
   ww[7]:=form(X_Xw1.NET,,doklw,'.,');
   ww[8] :=form(X_Xw1.ARCH);
   ss+=X_Xw1.NET;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: exec('upr_cm','ceny')}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {FOOTER}{FOOTER-}
   {SUBSTITUTE: 'LINIAF'}
   {
   PRN.n_frame();
   ww[1]:='';
   ww[2]:='';
   ww[3]:='';
   ww[4]:='';
   ww[5]:='';
   ww[6]:='Razem:'@; PRN.FORM[6]:=0;
   ww[7]:=form(ss,,2,'.,');
   ww[8]:='';
   ~~}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
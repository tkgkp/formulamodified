:!UTF-8
{TITLE:C.  Rozrachunki wyłącznie z zapisami technicznymi}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN:
      PRN:=obj_new(@.CLASS.PRINT,15); PRN.sl_frame();
      PRN1:=obj_new(@.CLASS.PRINT,15); PRN1.sl_frame();
      w:=obj_new(15); {! _i:=1..15 |! w[_i]:='' !};
      PRN.add("w[1]",50,'Symbol rozrachunku'@);
      PRN.add("w[2]",35,'Konto'@);
      PRN.add("w[3]",10,'Data otw.'@);
      PRN.add("w[4]",10,'T. płatn.'@);
      PRN.add("w[5]",16,'Kwota Wn'@,1);
      PRN.add("w[6]",16,'Kwota Ma'@,1);
      PRN.add("w[7]",50,'Rozrachunek główny'@);
      PRN.add("w[10]",5,'Rok rozl.'@);
      PRN.add("w[8]",11,'Data#rozliczenia'@,,'#');
      PRN.add("w[9]",20,'Wykonał'@);
      PRN1.add("",108,'Rozrachunek'@);
      PRN1.add("",123,'Zapisy'@);
      petla:=0; ile:=0; mdc:=''; lm:=0
}
{END:
      VAR_DEL.delete('w','PRN','PRN1','petla','ile','mdc','lm');
      exec('zapopuse','rozrach',0)
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: par_tech.rpm
Utworzony:  2000-12-12 [AMK] Artur Makos
==================================================
Zawartosc: Rozrachunki majace tylko zapisy techniczne wynikajace z rozliczen.
{COMMENT-}
{COMMENT}------------------------------HEADER----------------{COMMENT-}
{INCLUDE: wsp_pw3.rpi}
{HEADER}
{PAR_WYDR.TITLE:='ROZRACHUNKI WYŁĄCZNIE Z ZAPISAMI TECHNICZNYMI'@;~~}
{SUBSTITUTE: 'CHARHEAD'}
{IF: page=1}
{LINE: 1}
{FONT: 'Q'}{SPACE: 'B'}
{<1 'PARAMETRY WYDRUKU:'@}
{<5 'Rok obrotowy: %1'@[SSTALE.AR().NAZ]}
{<5 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<5 'Jednostka księgowa: %1'@[{? OPERATOR.DEPT || OPERATOR.DEPT().OD || 'wszystkie jednostki księgowe'@ ?}]}
{FI}
{FONT: 'R'}{SPACE: 'C'}
{PRN1.hbar()}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}
{DO}
{PRN1.h_line()}
{OD}
{PRN1.jbar(PRN)}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{PRN.lbar()}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ {? OPERATOR.DEPT
  || OP.index('KON_O'); OP.prefix(SSTALE.WAL,OPERATOR.DEPT)
  || OP.index('KON'); OP.prefix(SSTALE.WAL)
  ?}; petla:=OP.first(); ~~}
{WHILE: petla}
{DO}
{ ZAP_OP.index('PAR'); ZAP_OP.prefix(null,OP.ref);  ile:=ZAP_OP.size;~~}
{IF: ile=0}
{FOR: ZAP_OP}
{INDEX: 'OP'}
{PREFIX: OP.ref}
{DO}
{FIRST}
{ w[1]:=OP.SYM; w[2]:=OP.AN; w[3]:=OP.DO$3; w[4]:=OP.TZ$3;~~}
{FIRST-}
{ w[5]:=form(ZAP_OP.WN,,2,' ,');  w[6]:=form(ZAP_OP.MA,,2,' ,');
   exec('zapopuse','rozrach',1);
   w[7]:={? ZAP_OP.PAR_POZ().PAR_NAG().OP=OP.SYM
         || ZAP_OP.PAR_POZ().OP
         || ZAP_OP.PAR_POZ().PAR_NAG().OP
         ?};
  w[8]:={? ZAP_OP.PAR_POZ().PAR_NAG().DATA_AKC<>date(0,0,0)
        || ZAP_OP.PAR_POZ().PAR_NAG().DATA_AKC$3
        || ''
        ?};
 w[9]:=ZAP_OP.PAR_POZ().PAR_NAG().USER;
 w[10]:=ZAP_OP.ROK_ROZL().NAZ;
 exec('zapopuse','rozrach',0);
 ~~}
{ENTIRE}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{ w[1]:=w[2]:=w[3]:=w[4]:='';~~}
{OD}
{FI}
{ petla:=OP.next;~~}
{OD}
{FOOTER}{FOOTER-}
{ENTIRE}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}

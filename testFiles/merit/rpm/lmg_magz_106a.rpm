:!UTF-8
{TITLE:G. Towary zalegające w magazynie - wg okresu zalegania}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_106a:="
      VAR_DEL.delete('color','prn1','prn2','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('k','w','s','sg','PRN','dokl','kgr','w2','j2','dok2');
      VAR_DEL.delete('ilk','wyjscie','iter','cur_iter','XZAL','X_Xa','X_Xb','X_Xc')";
   mag_106a();

   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   wyjscie:=exec('definokr','magdok_wydruk'); ilk:=(4+XZAL.size);

   {? wyjscie=0
   ||
      k :=obj_new(ilk); {! _i..ilk |! k[_i] :=0  !}; "--szerokosci kolumn--";
      w :=obj_new(ilk); {! _i..ilk |! w[_i] :='' !}; "--wartosci kolumn--";
      s :=obj_new(ilk); {! _i..ilk |! s[_i] :=0  !}; "--razem--";
      sg:=obj_new(ilk); {! _i..ilk |! sg[_i]:=0  !}; "--razem grupa--";
      dokl:=obj_new(ilk); {! _i..ilk |! dokl[_i]:=2  !}; "--ilosc miejsc dziesietnych--";
      dok2:=obj_new(ilk); {! _i..ilk |! dok2[_i]:=2  !};
      w2:=obj_new(ilk); {! _i..ilk |! w[_i] :='' !};
      j2:='N';

      PRN :=obj_new(@.CLASS.PRINT,2*ilk+1); PRN.s_frame();
      k[1] :=20;
      k[2] :=30;
      k[3] :=6;
      {! _i:=4..ilk |! k[_i] :=12; w[_i] :='' !};
      tryb :=rep_export();
      kgr:='';

      wyjscie:=1;

      HELP.FM:='N';
      HELP.D:=date(ST.AR,ST.AM,0);
      HELP.IW:='N';
      HELP.GR:='T';

      _wydr_naz:='mag_106a';

      WYDRUKIL.index('WYDRUKI');
      WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
      {? ~WYDRUKIL.first()
      ||
         WYDRUKIL.blank();
         WYDRUKIL.USERS:=OPERATOR.USER().KOD;
         WYDRUKIL.WYDRUK:=_wydr_naz;
         WYDRUKIL.add()
      ||
         HELP.FM:=WYDRUKIL.SORTUJ;
         HELP.D:=WYDRUKIL.OD_DATY;
         HELP.IW:=WYDRUKIL.FAK;
         HELP.GR:=WYDRUKIL.RODZFAK;
         HELP.CHKBOX02:=WYDRUKIL.CHKBOX02
      ?};
      {? HELP.IW='T' & (exec('upr_cm','ceny')=0 | ST.MAG().IL='T') || HELP.IW:='N' ?};

      HELP.win_edit('MAG_106A');
      {? HELP.edit("
            _noupr:=exec('upr_cm','ceny')=0;
            {? ~_noupr & HELP.IW='T' & ST.MAG().IL='T'
            ||  FUN.info('Magazyn tylko ilościowy zmieniono parametr Rodzaj na wydruk ilościowy.'@);
                HELP.IW:='N'
            ?};
            {? __CHK.record(HELP,,'D')<>''
            || 'D'
            |? HELP.IW='T' & _noupr
            || FUN.info('Brak uprawnień do wartości magazynowych.'@); 'IW'
            || ''
            ?}
         ")
      || j2:={? HELP.IW='N' & HELP.CHKBOX02 || 'T' || 'N' ?};
         WYDRUKIL.index('WYDRUKI');
         WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
         {? WYDRUKIL.first()
         ||
            WYDRUKIL.SORTUJ:=HELP.FM;
            WYDRUKIL.OD_DATY:=HELP.D;
            WYDRUKIL.FAK:=HELP.IW;
            WYDRUKIL.RODZFAK:=HELP.GR;
            WYDRUKIL.CHKBOX02:=HELP.CHKBOX02;
            WYDRUKIL.put()
         ?};
         wyjscie:=0
      ?};

      {? wyjscie=0
      ||
         wyjscie:=exec('filtr_m','material',HELP.FM,HELP.GR,'T',1)
      ?}
   ?}
}
{END:
   mag_106a(); VAR_DEL.delete('mag_106a')
}
{IF: wyjscie=1}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
PRN.add("w[1]",k[1],'Indeks'@);
PRN.add("w[2]",k[2],'Nazwa materiału'@);
PRN.add("w[3]",k[3],'jm'@);
XZAL.first;
{! _ii:=4..(ilk-1)
|! _txt:=
      {? _ii=4 || 'Do %1 dni'@[$XZAL.OKR]
      || 'Od %1 do %2 dni'@[$XZAL.OD,$XZAL.OKR]
      ?};
   PRN.add($('w['+$_ii+']'),k[_ii],_txt,1);
   XZAL.next
!};
PRN.add("w[ilk]",k[ilk],'Pow. %1 dni'@[$XZAL.OKR],1);
{? j2='T' & tryb=1
|| PRN.add("w2[3]",k[3],'jm'@);
   XZAL.first;
   {! _ii:=4..(ilk-1)
   |! _txt:=
         {? _ii=4 || 'Druga jm Do %1 dni'@[$XZAL.OKR]
         || 'Druga jm Od %1 do %2 dni'@[$XZAL.OD,$XZAL.OKR]
         ?};
      PRN.add($('w2['+$_ii+']'),k[_ii],_txt,1);
      XZAL.next
   !};
   PRN.add("w2[ilk]",k[ilk],'Druga jm Pow. %1 dni'@[$XZAL.OKR],1)
?};

"--obliczenia--";
VAR_DEL.delete('X_Xb');
X_Xb:=tab_tmp(1,
   'MGRKOD' ,'STRING[8]'   ,'Grupa materiałowa',
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'N'      ,'STRING[100]' ,'Nazwa',
   'JM'     ,'STRING[10]'  ,'jm',
   'D'      ,'DATE'        ,'Data',
   'RDK'    ,'INTEGER'     ,'Ref dok. dostawy',
   'NDK'    ,'STRING[20]'  ,'Name dok. dostawy',
   'IL'     ,'REAL'        ,'Ilość',
   'WAR'    ,'REAL'        ,'Wartość',
   'J2'     ,'STRING[10]'  ,'jm',
   'IL2'    ,'REAL'        ,'Ilość');
_rndk:=X_Xb.ndx_tmp(,,'RDK',,,'NDK',,);
_ktm :=X_Xb.ndx_tmp(,,'KTM',,,'KTM',,);

_sr:='Ś'=(1+ST.MAG().TYP);
{? X_Xa.first()
||
   M.cntx_psh();
   M.prefix();
   {!
   |? M.seek(BB.sqlint(X_Xa.REF),);
      echo('Trwają obliczenia dla materiału: %1 %2'@[M.KTM,M.N]);
      _j2:=M.J2<>null();
      X_Xb.index(_rndk); X_Xb.prefix();
      _war:=_il:=_c_dk:=_il2:=0;
      OKR.cntx_psh();
      OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      ||
         ND.cntx_psh();
         DK.cntx_psh();
         {!
         |? ND.use('nagdo'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
            DK.use('dokma'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
            DK.index('DK_SM');
            DK.prefix(ST.MAG,M.ref(),'T',OKR.ROK);
            {? DK.first()
            ||
               {!
               |? {? DK.M().RODZ='T' & DK.N().D<=HELP.D
                  ||
                     {? X_Xb.find_key(DK.RDK,DK.NDK) & DK.DOST=X_Xb.D
                     ||
                        _put:=1
                     ||
                        _dost:=DK.DOST;
                        _put:=0;
                        X_Xb.MGRKOD:=M.MGR().KOD;
                        X_Xb.KTM:=M.KTM;
                        X_Xb.N  :=M.N;
                        X_Xb.JM :=M.J().KOD;
                        X_Xb.D:=_dost;
                        {? DK.PRDK=DK.SRDK
                        ||
                           {? #DK.ref()=DK.RDK & DK.name()=DK.NDK & DK.DOST=_dost || X_Xb.D:=DK.DOST ?}
                        ||
                           DK.cntx_psh();
                           DK.use(8+DK.PRDK);
                           DK.prefix();
                           {? DK.seek(BB.sqlint(DK.PRDK),) || X_Xb.D:=DK.DOST ?};
                           DK.cntx_pop()
                        ?};
                        X_Xb.RDK:=DK.RDK;
                        X_Xb.NDK:=DK.NDK;
                        X_Xb.J2:={? _j2 || M.J2().KOD || '' ?}
                     ?};
                     {? _put=1
                     ||
                        {? DK.PLUS='T'
                        || _il+=DK.IL; X_Xb.IL+=DK.IL;
                           _il2+={? _j2 || DK.IL2 || 0 ?}; X_Xb.IL2+={? _j2 || DK.IL2 || 0 ?}
                        || _il+=-DK.IL; X_Xb.IL+=-DK.IL;
                           _il2+={? _j2 || -DK.IL2 || 0 ?}; X_Xb.IL2+={? _j2 || -DK.IL2 || 0 ?}
                        ?};
                        {? DK.PLUS='T' || _war +=DK.WAR; X_Xb.WAR +=DK.WAR || _war +=-DK.WAR; X_Xb.WAR +=-DK.WAR ?};
                        X_Xb.put()
                     ||
                        _il  +=X_Xb.IL  :={? DK.PLUS='T' || DK.IL  || -DK.IL  ?};
                        _war +=X_Xb.WAR :={? DK.PLUS='T' || DK.WAR || -DK.WAR ?};
                        _il2 +=X_Xb.IL2 :={? _j2 || {? DK.PLUS='T' || DK.IL2  || -DK.IL2  ?} || 0 ?};
                        X_Xb.add()
                     ?}
                  ?};
                  DK.next()
               !}
            ?};
            OKR.next()
         !};
         DK.cntx_pop();
         ND.cntx_pop()
      ?};
      OKR.cntx_pop();
      "--dla srednich i ewidenycjnych uzupelnienie wartosci--";
      {? _sr=1
      ||
         _cena:={? _il>0 || (_war/_il)$2 || 0 ?};
         X_Xb.index(_ktm); X_Xb.prefix(M.KTM,M.KTM);
         {? X_Xb.first() || {! |? X_Xb.WAR:=(X_Xb.IL*_cena)$2; X_Xb.put(); X_Xb.next() !} ?}
      ?};
      X_Xa.next()
   !};
   M.cntx_pop();
   echo();
   "--usunięcie stanow zerowych i materialow zalegajacych krocej niz HELP.DNI--";
   X_Xb.prefix();
   {? X_Xb.first() || {! |? {? X_Xb.IL=0 & X_Xb.WAR=0 || X_Xb.del() || X_Xb.next() ?} !} ?};
   "--przypisanie do przedzialow--";
   VAR_DEL.delete('X_Xc');
   _iw:={? HELP.IW='N' || 'IL' || 'WAR' ?};
   _zn:=%'b'; _lp:=1;
   _sel:='select MGRKOD, KTM, N, JM, J2,';
      _sel+='sum(case when D<=to_date(:_'+%_zn+') and D>=to_date(:_'+%(_zn+1)+') then '+_iw+' else 0 end) P'+$_lp+',';
      _sel+={? j2='T'
            || 'sum(case when D<=to_date(:_'+%_zn+') and D>=to_date(:_'+%(_zn+1)+') then IL2 else 0 end) R'+$_lp+','
            || ''
            ?};
      _zn+=1; _lp+=1;
   {! _ii:=4..ilk-2
   |! _sel+='sum(case when D<to_date(:_'+%_zn+') and D>=to_date(:_'+%(_zn+1)+') then '+_iw+' else 0 end) P'+$_lp+',';
      _sel+={? j2='T'
            || 'sum(case when D<to_date(:_'+%_zn+') and D>=to_date(:_'+%(_zn+1)+') then IL2 else 0 end) R'+$_lp+','
            || ''
            ?};
      _zn+=1; _lp+=1
   !};
      _sel+='sum(case when D <to_date(:_'+%_zn+')                                then '+_iw+' else 0 end) P'+$_lp;
      _sel+={? j2='T'
            || ', sum(case when D <to_date(:_'+%_zn+')                                then IL2 else 0 end) R'+$_lp
            || ''
            ?};
   _from:=' from :_a ';
   _grby:=' group by MGRKOD, KTM, N, JM, J2';
   {? XZAL.first()
   ||
      _sql:='sql(\''+_sel+_from+_grby+'\',X_Xb,HELP.D';
      {!
      |? _sql+=',HELP.D-'+$XZAL.OKR;
         XZAL.next()
      !};
      _sql+=')';
      X_Xc:=($_sql)()
   ?}
?};

PAR_WYDR.TITLE:=
   {? HELP.IW='T'
   || 'WARTOŚCIOWE ZESTAWIENIE TOWARÓW ZALEGAJĄCYCH\nWG OKRESÓW ZALEGANIA'@
   || 'ILOŚCIOWE ZESTAWIENIE TOWARÓW ZALEGAJĄCYCH\nWG OKRESÓW ZALEGANIA'@
   ?};
prn1:='PRN'
;~~}
{IF: var_pres('X_Xc')<100}{FUN.info('Brak danych spełniających kryteria.'@);~~}{FI}
{EXIT: var_pres('X_Xc')<100}
{IF: X_Xc.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Na dzień: %1'@[(HELP.D$6)]}
{IF:HELP.FM='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
{IF: HELP.IW='N'}{<{lmt+1}}{'Ilościowy'@}{ELIF: HELP.IW='T'}{<{lmt+1}}{'Wartościowy'@}{FI}
{IF:j2='T'}{<{lmt+1}}{'Druga jednostka ewidencyjna'@}{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}
   {LINE}
   {<{lmt+1}}{'Parametry wydruku:'@}
   {<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
   {<{lmt+1}}{'Na dzień: %1'@[(HELP.D$6)]}
   {IF:HELP.FM='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
   {IF: HELP.IW='N'}{<{lmt+1}}{'Ilościowy'@}{ELIF: HELP.IW='T'}{<{lmt+1}}{'Wartościowy'@}{FI}
   {IF:j2='T'}{<{lmt+1}}{'Druga jednostka ewidencyjna'@}{FI}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   drukowanie pozycji wydruku                               {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;~~}
{IF: HELP.GR='N'}
{COMMENT}   wydruk bez grup towarowych                               {COMMENT-}
   {FOR: X_Xc}
   {DO}
      {FIRST}
      {
      {! _ii:=4..ilk
      |!
         {? HELP.IW='N' || dokl[_ii]:=exec('fld_prec','#field',X_Xc,'P'+$(_ii-3)) ?};
         {? j2='T' || dok2[_ii]:=exec('fld_prec','#field',X_Xc,'P'+$(_ii-3)) ?}
      !};
      ~~}
      {FIRST-}
      {
      w[1] :=form(X_Xc.KTM);
      w[2] :=form(X_Xc.N);
      w[3] :=form(X_Xc.JM);
      w2[3]:=form(X_Xc.J2);
      _lp:=0;
      {! _ii:=4..ilk
      |!
         _lp+=1;
         _war:=($('X_Xc.P'+$(_lp)))();
         w[_ii]:={? _war=0 || '' || form(_war,,dokl[_ii],'.,') ?};
         s[_ii]+=_war;
         _war   :={? j2='T' || ($('X_Xc.R'+$(_lp)))() || 0 ?};
         w2[_ii]:={? _war=0 || '' || form(_war,,dok2[_ii],'.,') ?}
      !}
      ;~~}
      {SUBSTITUTE: 'LINIA0'}
      {IF: j2='T' & X_Xc.J2<>'' & tryb<>1}
         {
         rsep:=0;
         w[1]:='';
         w[2]:='';
         w[3]:=X_Xc.J2;
         {! _ii:=4..ilk |! w[_ii]:=w2[_ii] !};
         ~~}
         {SUBSTITUTE: 'LINIA0'}
         { rsep:=PAR_WYDR.RSEP;~~}
      {FI}
   {OD}
{ELSE}
{COMMENT}   wydruk z grupami towarowymi                              {COMMENT-}
   {FOR: 'X_Xc'}
   {DO}
      {FIRST}
      {
      kolejny:=0;
      {! _ii:=4..ilk
      |!
         {? HELP.IW='N' || dokl[_ii]:=exec('fld_prec','#field',X_Xc,'P'+$(_ii-3)) ?};
         {? j2='T' || dok2[_ii]:=exec('fld_prec','#field',X_Xc,'R'+$(_ii-3)) ?}
      !};
      ~~}
      {FIRST-}
      {IF: tryb<>1 & kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
      {
      w[1] :=form(X_Xc.KTM);
      w[2] :=form(X_Xc.N);
      w[3] :=form(X_Xc.JM);
      w2[3]:=form(X_Xc.J2);
      kgr  :=form(X_Xc.MGRKOD);
      _lp:=0;
      {! _ii:=4..ilk
      |!
         _lp+=1;
         _war   :=($('X_Xc.P'+$(_lp)))();
         w[_ii] :={? _war=0 || '' || form(_war,,dokl[_ii],'.,') ?};
         s[_ii] +=_war;
         sg[_ii]+=_war;
         _war   :={? j2='T' || ($('X_Xc.R'+$(_lp)))() || 0 ?};
         w2[_ii]:={? _war=0 || '' || form(_war,,dok2[_ii],'.,') ?}
      !}
      ;~~}
      {SUBSTITUTE: 'LINIA0'}
      {IF: j2='T' & X_Xc.J2<>'' & tryb<>1}
         {
         rsep:=0;
         w[1]:='';
         w[2]:='';
         w[3]:=X_Xc.J2;
         {! _ii:=4..ilk |! w[_ii]:=w2[_ii] !};
         ~~}
         {SUBSTITUTE: 'LINIA0'}
         { rsep:=PAR_WYDR.RSEP;~~}
      {FI}
      { kolejny:=0; rsep:=PAR_WYDR.RSEP;~~}
      {TOTAL: X_Xc.MGRKOD}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
         {IF: tryb<>1 & HELP.IW='T'}
            {
            w[1] :=form(X_Xc.KTM);
            w[2] :=form(X_Xc.N);
            w[3] :=form(X_Xc.JM);
            _f:=prn1+'.ini_line()'; _v:=($(_f))();
            _f:=prn1+'.length()'; il_lin:=($(_f))()+3
            ;~~}
            {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
            {SUBSTITUTE: 'LINIAF'}
            {
            kolejny:=1; rsep:=0;
            PRN.n_frame();
            w[1] :='';
            w[2] :='Razem %1:'@[grp_fld()]; PRN.FORM[2]:=0;
            w[3] :='';
            {! _ii:=4..ilk
            |! w[_ii]:=form(sg[_ii],,dokl[_ii],'.,')
            !}
            ;~~}
            {FONT: 'T8GB'}{SPACE: 'C'}
            {SUBSTITUTE: 'LINIA0'}
            {FONT: 'T8G'}{SPACE: 'C'}
            {
            {! _ii:=4..ilk |! sg[_ii]:=0 !};
            PRN.s_frame();
            PRN.FORM[2]:=1
            ;~~}
         {FI}
      {TOTAL}
         { VAR_DEL.delete('kolejny');~~}
   {OD}
{FI}
{IF: tryb<>1 & HELP.IW='T'}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {IF: HELP.GR='N'}{SUBSTITUTE: 'LINIAF'}{FI}
   {FOOTER}{FOOTER-}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {
   PRN.n_frame();
   w[1] :='';
   w[2] :='Razem:'@; PRN.FORM[2]:=0;
   w[3] :='';
   {! _ii:=4..ilk
   |! w[_ii]:=form(s[_ii],,dokl[_ii],'.,')
   !}
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
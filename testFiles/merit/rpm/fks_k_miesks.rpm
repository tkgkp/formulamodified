:!UTF-8
{TITLE:A.  Miesięczna kartoteka księgowań}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,15,15,10,15,,,,'enable'}
{BEGIN:
        sstrwn:=sstrma:=sumawn:=sumama:=sstrwn1:=sstrma1:=sumawn1:=sumama1:=0;
        w:=obj_new(20); ww:=obj_new(10);  petla:=0; ROZNE.REJ:=null; nextPoz:=odd:=lm:=0;
        w9:=w10:=w91:=w92:=0; cechy:=null; ROZNE.PAR1:=1;mdc:='';font:='J'; szer:=obj_new(15);
        PRN:=obj_new(@.CLASS.PRINT,15); PRN.sl_frame();
        PRN1:=obj_new(@.CLASS.PRINT,15); PRN1.sl_frame();
        {? PAR_SKID.get(39)='T' || czy_w:=1
        |? PAR_SKID.get(39)='N' || czy_w:=0 || czy_w:=2 ?};
        wal:={? SSTALE.WAL=FINFO.NAROD || 0 || 1 ?};
        szer[2]:=20; szer[4]:=14; szer[5]:=szer[6]:=szer[9]:=szer[10]:=17; szer[8]:=5;
        szer[7]:=3; szer[11]:=22; szer[12]:=10; szer[1]:=5; szer[13]:=35; szer[14]:=10;szer[15]:=8
      }
{END: &sstrwn; &sstrma; &petla; &sumawn; &sumama;  &wal; &mdc; &lm; &font;
           obj_del(w); obj_del(PRN); obj_del(PRN1); &sstrma1; &sstrwn1; &sumama1; &sumawn1;
           obj_del(ww); &w9; &w10; &w91; &w92; obj_del(szer); &cechy; &czy_w; &odd; &nextPoz
     }
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: k_miesks.rpm
Utworzony:   ????-??-??
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Miesieczna kartoteka ksiegowan
{COMMENT-}
{  {? czy_w=2
   || WYDRUKIN.win_edit('WYBDATA6');
      PAR_WYDR.WSK_WAL:='T'
   || WYDRUKIN.win_edit('WYBDATA2')
   ?};
   REJ.f_clear();
   WYDRUKIN.DATAOD:=SSTALE.AO().POCZ;
   WYDRUKIN.DATADO:=SSTALE.AO().KON; ~~}
    {EXIT: ~(WYDRUKIN.edit
               ("
                 {? SSTALE.AO().POCZ=date(0,0,0)  &
                     (~exec('dat_not','dok_fks',WYDRUKIN.DATAOD) | ~exec('dat_not','dok_fks',WYDRUKIN.DATADO))
                 || FUN.emsg('Podane daty muszą być z \nbieżącego roku bilansowego.'@);0
                 |? ((WYDRUKIN.DATAOD<SSTALE.AO().POCZ) |
                       (WYDRUKIN.DATADO>SSTALE.AO().KON) |
                       (WYDRUKIN.DATAOD>WYDRUKIN.DATADO)) & SSTALE.AO().POCZ<>date(0,0,0)
                 || FUN.emsg('\nDaty muszą się mieścić\n w bieżącym okresie\n obrachunkowym\n'@);
                    {? (WYDRUKIN.DATAOD<SSTALE.AO().POCZ) | (WYDRUKIN.DATAOD>WYDRUKIN.DATADO)
                    || 'DATAOD'
                    || 'DATADO'
                    ?}
                 || 1
                 ?}
               "))}
{       {? czy_w=2
        || {? PAR_WYDR.WSK_WAL='T' || czy_w:=1 || czy_w:=0 ?}
        ?};
        {? ~czy_w & ~rep_export
        || szer[2]:=szer[11]:=50
        ?};
        {? rep_export=1
        || PRN.add("w[17]",10,'D. zapisu'@);
           PRN.add("w[2]",szer[2],'Symbol dokumentu'@)
        || PRN.add("w[2]",szer[2],'Symbol dokumentu/ D. zapisu'@)
        ?};
        PRN.add("w[16]",szer[8],'Nr w dz.'@,1);
        {? ROZNE.REJ=null
        || PRN.add("w[4]",szer[4],'Rejestr/    nr'@)
        || szer[4]-=7; szer[11]+=7;
           PRN.add("w[4]",szer[4],'Nr w rej.'@,1)
        ?};
        PRN.add("w[1]",szer[1],'Poz.',1); PRN.add("w[13]",szer[13],'Konto'@);
        PRN.add("w[5]",szer[5],'   Wn (%1)'[(3+FINFO.NAROD().KOD)],1);
        PRN.add("w[6]",szer[6],'   Ma (%1)'[(3+FINFO.NAROD().KOD)],1);
        {? czy_w
        || PRN.add("w[7]",szer[7],'Wal'@); PRN.add("w[15]",szer[15],'  Kurs'@,1);
           PRN.add("w[9]",szer[9],{? ~wal ||'  Wn (waluta)'@|| '  Wn (%1)'@[(3+SSTALE.WAL().KOD)]?},1);
           PRN.add("w[10]",szer[10],{? ~wal ||  'Ma (waluta)'@||  'Ma (%1)'@[(3+SSTALE.WAL().KOD)]?},1)
        || szer[11]+=10
        ?};
        PRN.add("w[11]",szer[11],'Typ/      symbol rozr.'@); PRN.add("w[12]",szer[12],'Data otw.'@);
        PRN.add("w[14]",szer[14],'T. płatn.'@);
        PRN1.add("ww[1]",szer[2]+szer[4]+szer[1]+szer[13]+szer[8]+4,'',,,,,,,,6); PRN1.add("ww[2]",szer[5],'',1);
        PRN1.add("ww[3]",szer[6],'',1);
        {? czy_w
        || PRN1.add("ww[4]",szer[7],''); PRN1.add("ww[10]",szer[15],'');
           PRN1.add("ww[5]",szer[9],'',1);  PRN1.add("ww[6]",szer[10],'',1);
           PRN1.add("ww[7]",szer[11]+szer[12]+szer[14]+2,'')
        ?};
        ww[1]:=ww[2]:=ww[3]:=ww[4]:=ww[5]:=ww[6]:=ww[7]:=ww[10]:='';~~
}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:=
   {? ROZNE.REJ=null
   || 'Zapisy księgowe w okresie od %1 do %2'@[$WYDRUKIN.DATAOD,$WYDRUKIN.DATADO]
   || 'Zapisy księgowe dla rejestru %1 - %2 od %3 do %4'@[ROZNE.REJ().KOD,ROZNE.REJ().ODD().OD,$WYDRUKIN.DATAOD,$WYDRUKIN.DATADO]
   ?};~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 {?ROZNE.PAR1||'Sumowane konta: wszystkie'@||'Sumowane konta: bilansowe'@?}}
{<5 {? STALE.AO().ZAM='T'
    ||'Okres obrachunkowy: %1 %2 - zamknięty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]
    ||'Okres obrachunkowy: %1 %2 - otwarty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]
    ?}
}
{<5 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<5 {? OPERATOR.DEPT
    || 'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
    || 'Jednostka księgowa: wszystkie jednostki księgowe'@
    ?}
}
{FI}{FONT: 'R'}{SPACE: 'C'}
{REP: PRN.fhbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{REP: PRN.h_fline()}
{OD}
{REP: PRN.fbar()}{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{REP: PRN.fjbar(PRN1)}{ ww[1]:='   SUMA STRONY'@;ww[2]:=form(sstrwn,,2,' ,');ww[3]:=form(sstrma,,2,' ,');
   {?wal||ww[5]:=form(sstrwn1,,2,' ,');ww[6]:=form(sstrma1,,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.fbar()}{IF: page>1}{ ww[1]:='   Z PRZENIESIENIA'@;ww[2]:=form((sumawn-sstrwn),,2,' ,');ww[3]:=form((sumama-sstrma),,2,' ,');
  {?wal||ww[5]:=form((sumawn1-sstrwn1),,2,' ,');ww[6]:=form((sumama1-sstrma1),,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.fbar()}{FI}
{ ww[1]:='   DO PRZENIESIENIA'@; ww[2]:=form(sumawn,,2,' ,'); ww[3]:=form(sumama,,2,' ,');
  {?wal||ww[5]:=form(sumawn1,,2,' ,');ww[6]:=form(sumama1,,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.flbar()}
{SUBSTITUTE: 'CHARFOOT'}
{FONT: 'Q'}{SPACE: 'C'}{ sstrwn:=sstrma:=sstrwn1:=sstrma1:=0; ~~}
{FOOTER-}
{COMMENT}------------------------------MACRO-SRODEK----------------------{COMMENT-}
{MACRO: 'SRODEK'}
{ {? cechy<>DOK.ref | rep_export=1
  || cechy:=DOK.ref();
     w[2]:=POZ.DOK().NK;
     {? ROZNE.REJ=null
     || w[4]:=POZ.DOK().REJ().KOD+(8-(+POZ.DOK().REJ().KOD))*' '+form(POZ.DOK().NR,szer[4]-8,,'99')
     || w[4]:=$POZ.DOK().NR
     ?};
     w[16]:=$POZ.DOK().NRDZ;
     {? rep_export=1 || w[17]:=$POZ.DOK().DTW ?}
  || w[2]:=w[3]:=w[4]:=w[16]:=w[17]:=''
  ?};
  odd+=1;
  {? ROZNE.REJ=null & OPERATOR.DEPT=null
  || {? odd=2 & rep_export=0 || w[4]:=POZ.DOK().REJ().ODD().OD ?}
  ?};
  {? odd=2 & rep_export=0 || w[2]:=$POZ.DOK().DTW ?};
  w[1]:=$POZ.POZ;
  {? (~wal & POZ.WAL<>null) | wal
  || {? ((ROZNE.PAR1=0 & -(1+POZ.KOM().KS().TYP)<>'p') | ROZNE.PAR1=1)
     || {? -POZ.STR='wn'
        ||  w[9]:=form(POZ.SUMW,,2,' ,'); w[10]:=form(0,,2,' ,')
        ||  w[10]:=form(POZ.SUMW,,2,' ,'); w[9]:=form(0,,2,' ,')
        ?}
     ||  w[9]:=form(0,,2,' ,'); w[10]:=form(0,,2,' ,')
     ?};
     w[7]:=(3+POZ.WAL().KOD);  w[15]:=form(POZ.KURS,8,4,' ,')
  || w[7]:=w[9]:=w[10]:=w[15]:=''
  ?};
  w[11]:=POZ.TID+' '+POZ.ID;
  {? |w[11]<>'' || w[12]:=$POZ.DO; w[14]:=$POZ.TP || w[12]:=''; w[14]:=''?};
  {? ((ROZNE.PAR1=0 & -(1+POZ.KOM().KS().TYP)<>'p') | ROZNE.PAR1=1)
  || {? -POZ.STR='wn'
     || w[5]:=form(POZ.SUM,,2,' ,'); w[6]:=form(0,,2,' ,')
     || w[6]:=form(POZ.SUM ,,2,' ,'); w[5]:=form(0,,2,' ,')
     ?}
  || w[5]:=form(0,,2,' ,'); w[6]:=form(0,,2,' ,')
  ?};
  w[13]:=POZ.KON;
  ~~}
{ENTIRE}
{SUBSTITUTE: 'LINIA'}
{ POZ.cntx_psh(); nextPoz:=POZ.next(); POZ.cntx_pop();~~}
{IF: ~nextPoz & odd=1}
{  {! _it:=1..20 |! w[_it]:='' !};
   {? ROZNE.REJ=null & OPERATOR.DEPT=null || w[4]:=POZ.DOK().REJ().ODD().OD ?};
   w[2]:=$POZ.DOK().DTW;
~~}
{SUBSTITUTE: 'LINIA'}
{FI}
{IF: ~nextPoz}{REP: PRN.fbar()}{odd:=0;~~}{FI}
{  {? ((ROZNE.PAR1=0 & -(1+POZ.KOM().KS().TYP)<>'p') | ROZNE.PAR1=1)
   ||  {? -POZ.STR='wn'
       ||   sstrwn+=POZ.SUM;  sumawn+=POZ.SUM;
           {? wal || sstrwn1+=POZ.SUMW; sumawn1+=POZ.SUMW ?}
       ||   sstrma+=POZ.SUM;  sumama+=POZ.SUM;
           {? wal || sstrma1+=POZ.SUMW; sumama1+=POZ.SUMW ?}
      ?}
   ?};
~~}
{ENTIRE-}
{MACRO-}
{COMMENT}----------------------WYDRUK-------------------------------------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ {? ROZNE.REJ<>null
   || {? OPERATOR.DEPT
      || DOK.index('PR'); DOK.prefix('T','T',OPERATOR.DEPT,ROZNE.REJ)
      || DOK.index('PR1'); DOK.prefix('T','T',ROZNE.REJ)
      ?}
   || {? OPERATOR.DEPT
      || DOK.index('PR'); DOK.prefix('T','T',OPERATOR.DEPT)
      || DOK.index('PR1'); DOK.prefix('T','T')
      ?}
  ?};
  petla:=DOK.first(); ~~}
{WHILE: petla}
{DO}
  {IF: (DOK.DTW>=WYDRUKIN.DATAOD)&(DOK.DTW<=WYDRUKIN.DATADO)}
    {IF: SSTALE.WAL<>FINFO.NAROD}
       {FOR: 'POZ'}
       {INDEX: 'CECHY'}
       {PREFIX: DOK.ref,SSTALE.WAL}
       {DO}
       {SUBSTITUTE: 'SRODEK'}
       {OD}
     {ELSE}
        {FOR: 'POZ'}
        {INDEX: 'DOK'}
        {PREFIX: DOK.ref}
        {DO}
        {SUBSTITUTE: 'SRODEK'}
        {OD}
     {FI}
 {FI}
{petla:=DOK.next(); ~~}
{OD}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{FONT: 'Q'}{SPACE: 'C'}
{FOOTER-}
{DIRECT}
{REP: PRN.fjbar(PRN1)}{IF: page>1}{ ww[1]:='   SUMA STRONY'@;ww[2]:=form(sstrwn,,2,' ,');ww[3]:=form(sstrma,,2,' ,');
   {?wal||ww[5]:=form(sstrwn1,,2,' ,');ww[6]:=form(sstrma1,,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.fbar()}{ ww[1]:='   Z PRZENIESIENIA'@;ww[2]:=form((sumawn-sstrwn),,2,' ,');ww[3]:=form((sumama-sstrma),,2,' ,');
  {?wal||ww[5]:=form((sumawn1-sstrwn1),,2,' ,');ww[6]:=form((sumama1-sstrma1),,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.fbar()}{FI}
{ ww[1]:='   RAZEM'@; ww[2]:=form(sumawn,,2,' ,'); ww[3]:=form(sumama,,2,' ,');
  {?wal||ww[5]:=form(sumawn1,,2,' ,');ww[6]:=form(sumama1,,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.fbar()}{ ww[1]:='   SALDO'@; ww[2]:=form((F.SWn(sumawn,sumama)),,2,' ,'); ww[3]:=form((F.SMa(sumawn,sumama)),,2,' ,');
  {?wal||ww[5]:=form((F.SWn(sumawn1,sumama1)),,2,' ,');ww[6]:=form((F.SMa(sumawn1,sumama1)),,2,' ,')?}; ~~}
{SUBSTITUTE: 'LINIA1'}
{REP: PRN1.flbar()}
{IF: rep_export=0}{<1>{PRN.width()}'KONIEC WYDRUKU'@}{FI}
{DIRECT-}

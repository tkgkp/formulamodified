:!UTF-8
{TITLE:D. Potwierdzone dostawy}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   wwyd_004:="
      VAR_DEL.delete('ww','kk','ss','dokl','doklw','PRN','tryb','kgr','kdt');
      VAR_DEL.delete('odd','dod','kh','fm','kolej','lline');
      VAR_DEL.delete('wydr_naz','wydr_edi');
      VAR_DEL.delete('wyjscie','X_Xw1','X_Xa','X_Xb','X_Xc','indX_Xw1','indX_Xc','indX_Xa');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep')";
   wwyd_004();

   PRN:=obj_new(@.CLASS.PRINT,12); PRN.s_frame();

   _ilk:=11;
   ww:=obj_new(_ilk); "--wartosci kolumn--";
   kk:=obj_new(_ilk); "--szerokosc kolumn--";
   dokl:=3; "--dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosci--";
   ss:=tab_tmp(1,'JM','STRING[10]',''
        ,'S1','REAL',''
        ,'S2','REAL',''
        ,'S3','REAL','');

   kk[1]:=20;        "--symbol zamowienia--";
   kk[2]:=45;        "--kontrahent--";
   kk[3]:=15;        "--stan zamowienia--";
   kk[4]:=11;        "--data--";
   kk[5]:=10;        "--termin realizacji--";
   kk[6]:=50;        "--material/usluga--";
   kk[7]:=10;        "--jm--";
   kk[8]:=12;        "--ilosc zamawiana--";
   kk[9]:=12;        "--ilosc potwierdzona--";
   kk[10]:=15;       "--wartosc zamowienia--";
   tryb:=rep_export();
   kgr:=kdt:='';

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   kolej:=0;
   lline:=0;

   odd:=dod:=date(0,0,0);
   fm :='N';
   kh :='';

   wydr_naz:='wwyd_004';
   wydr_edi:='WWYD_004';

   KH.cntx_psh(); HAN.cntx_psh();
   KH.win_dict('WER_SLO'); KH.actions('WER_SLO','W');

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   WYDRUKIL.OD_DATY:=date();
   {? WYDRUKIL.edit("{? WYDRUKIL.OD_DATY<date()
                     || FUN.info('Data OD nie może być mniejsza od daty bieżącej.'@);
                        'OD_DATY'
                     |? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY
                     || ''
                     || FUN.info('Błędny zakres dat.'@);
                        'DO_DATY'
                     ?}")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         kh     :=WYDRUKIL.KH_OD().KOD;
         odd    :=WYDRUKIL.OD_DATY;
         dod    :=WYDRUKIL.DO_DATY;
         fm     :={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?}
      ?}
   ?};

   KH.actions('WER_SLO','');
   KH.cntx_pop(); HAN.cntx_pop();
   {? wyjscie=0 & fm='T' || wyjscie:=exec('filtr_m','material',fm,'N','T',1) ?}
}
{END:
   wwyd_004(); VAR_DEL.delete('wwyd_004')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1
|| PRN.add("kdt",10,'Data dostawy'@);
   PRN.add("kgr",8,'Grupa materiałowa'@)
?};
PRN.add("ww[1]",kk[1],'Zamówienie'@);
PRN.add("ww[2]",kk[2],'Kontrahent'@);
PRN.add("ww[3]",kk[3],'Stan'@);
PRN.add("ww[4]",kk[4],'Data wystawienia'@);
PRN.add("ww[5]",kk[5],'Termin realizacji'@);
PRN.add("ww[6]",kk[6],'Materiał'@+'/'+'Usługa'@);
PRN.add("ww[7]",kk[7],'jm'@);
PRN.add("ww[8]",kk[8],'Ilość'@,1);
PRN.add("ww[9]",kk[9],'Ilość potwierdzona'@,1);
{? exec('upr_cm','ceny') || PRN.add("ww[10]",kk[10],'Wartość'@,1) ?};


"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(3
   ,'D_POTW'   ,'DATE'        ,'Data potwierdzenia'
   ,'M'        ,'STRING[151]' ,'Materiał/Usługa'
   ,'KTM'      ,'STRING[50]'  ,'Materiał/Usługa'
   ,'SYM'      ,'STRING[20]'  ,'Symbol zamówienia'
   ,'KH'       ,'STRING[70]'  ,'Kontrahent'
   ,'STAN'     ,'STRING[60]'  ,'Stan zamówienia'
   ,'DATA'     ,'DATE'        ,'Data przyjęcia'
   ,'DTPREAL'  ,'DATE'        ,'Termin realizacji'
   ,'JM'       ,'STRING[10]'  ,'jm'
   ,'IL'       ,'REAL'        ,'Ilość'
   ,'NET'      ,'REAL'        ,'Wartość netto'
   ,'IL_POZ'   ,'REAL'        ,'Ilość pozostał do realizacji'
   ,'MGR'      ,'STRING[8]'   ,'Grupa materiałowa');
indX_Xw1:=X_Xw1.ndx_tmp('',0,'D_POTW',,,'M',,);
{? fm='T' || indX_Xa:=X_Xa.ndx_tmp('',0,'REF',,) ?};

"--obliczenia w masce zbiorczej--";
exec('openzd_psh','open_tab');
exec('openzd','open_tab',ST.ODDZ+'__');
ZD_POZ.index('D_POTW');
ZD_POZ.prefix();
{? ZD_POZ.first()
||
   {!
   |? _wgpotw:=0;
      {? ZD_POZ.ZD_NAG().STAT_REJ<>'A'
      || _ilzre:=ZD_POZ.IL_ZRE;
         ZDP_POZ.index('ZD_POZ');
         ZDP_POZ.prefix($ZD_POZ.ref(),);
         {? ZDP_POZ.first()
         || {!
            |?
               {? ZDP_POZ.ZDP_NAG().STAT_REJ<>'A'
                  & ZDP_POZ.ZDP_NAG().D_WYS>=date() & (ZD_POZ.IL-_ilzre)>0
                  & (ZDP_POZ.ZDP_NAG().D_WYS>=odd | odd=date(0,0,0)) & (ZDP_POZ.ZDP_NAG().D_WYS<=dod | dod=date(0,0,0))
                  & (kh='' | kh=ZDP_POZ.ZDP_NAG().KH().KOD)
                  & {? fm='N' || 1 || X_Xa.index(indX_Xa); X_Xa.prefix($ZD_POZ.M,); X_Xa.first() ?}
               ||
                  X_Xw1.SYM      :=ZD_NAG.SYM;
                  X_Xw1.KH       :=ZD_NAG.KH().KOD+' '+KH.NAZ;
                  X_Xw1.STAN     :=(exec('zdnag_rek','zamdst_nag'); POM.STAN);
                  X_Xw1.DATA     :=ZD_NAG.DATA;
                  X_Xw1.DTPREAL  :=ZD_NAG.DTPREAL;
                  X_Xw1.M        :=ZD_POZ.M().KTM+' '+M.N;
                  X_Xw1.JM       :=ZD_POZ.JM().KOD;
                  X_Xw1.IL       :=ZD_POZ.IL;
                  X_Xw1.NET      :=ZD_POZ.WAR;
                  X_Xw1.IL_POZ   :=ZDP_POZ.IL;
                  X_Xw1.D_POTW   :=ZDP_POZ.ZDP_NAG().D_WYS;
                  X_Xw1.KTM      :=ZD_POZ.M().KTM;
                  X_Xw1.MGR      :=ZD_POZ.M().MGR().KOD;
                  X_Xw1.add();
                  _wgpotw:=1
               ?};
               ZDP_POZ.next()
            !}
         ?}
      ?};

      {? ~_wgpotw & ZD_POZ.ZD_NAG().STAT_REJ<>'A'
         & ZD_POZ.D_POTW>=date() & ZD_POZ.IL_POZ>0
         & (ZD_POZ.D_POTW>=odd | odd=date(0,0,0)) & (ZD_POZ.D_POTW<=dod | dod=date(0,0,0))
         & (kh='' | kh=ZD_NAG.KH().KOD)
         & {? fm='N' || 1 || X_Xa.index(indX_Xa); X_Xa.prefix($ZD_POZ.M,); X_Xa.first() ?}
      ||
         X_Xw1.SYM      :=ZD_NAG.SYM;
         X_Xw1.KH       :=ZD_NAG.KH().KOD+' '+KH.NAZ;
         X_Xw1.STAN     :=(exec('zdnag_rek','zamdst_nag'); POM.STAN);
         X_Xw1.DATA     :=ZD_NAG.DATA;
         X_Xw1.DTPREAL  :=ZD_NAG.DTPREAL;
         X_Xw1.M        :=ZD_POZ.M().KTM+' '+M.N;
         X_Xw1.JM       :=ZD_POZ.JM().KOD;
         X_Xw1.IL       :=ZD_POZ.IL;
         X_Xw1.NET      :=ZD_POZ.WAR;
         X_Xw1.IL_POZ   :=ZD_POZ.IL_POZ;
         X_Xw1.D_POTW   :=ZD_POZ.D_POTW;
         X_Xw1.KTM      :=ZD_POZ.M().KTM;
         X_Xw1.MGR      :=ZD_POZ.M().MGR().KOD;
         X_Xw1.add()
      ?};
      ZD_POZ.next
   !}
?};
exec('openzd_pop','open_tab');
X_Xb:=sql('select distinct :_a.D_POTW from :_a order by 1',X_Xw1);
X_Xc:=sql('select distinct :_a.D_POTW D_POTW, :_a.M M, :_a.KTM KTM from :_a order by 1,2',X_Xw1);
indX_Xc:=X_Xc.ndx_tmp('',0,'D_POTW',,,'M',,);

PAR_WYDR.TITLE:='Potwierdzone dostawy stan na: %1, %2'@[$date(),$time()];
prn1:='PRN';
~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział: %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1}}{IF: (kh<>'')}{'Kontrahent: %1'@[kh]}{ELSE}{'Kontrahent: bez ograniczeń'@}{FI}
{IF: odd<>date(0,0,0) | dod<>date(0,0,0)}{<{lmt+1}}{'Zakres dat: od %1 do %2'@[$odd,$dod]}{FI}
{IF: fm='T'}{<{lmt+1}}{'Z wyborem materiałów'@}{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}
{IF: tryb<>1}{SUBSTITUTE: 'LINIAF'}{FI}
{FONT: 'T8G'}{ vp:=0; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{ PRN.s_frame(); PRN.FORM[2]:=1; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{IF: tryb<>1}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8G'}{ vp:=0; rsep:=0; ~~}
{ _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
{FI}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{IF: tryb<>1 & kolej & lline<>1}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FI}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xb}
{DO}
   {IF: tryb<>1}
   {
   {! _i:=1..10 |! ww[_i]:='' !};
   ww[1] :='Na dzień: %1'@[form(X_Xb.D_POTW)];
   PRN.n_frame(); PRN.FORM[2]:=0
   ;~~}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'LINIAF'}
   {SUBSTITUTE: 'LINIA0'}
   { PRN.s_frame(); PRN.FORM[2]:=1; ~~}
   {FI}
   {FOR: X_Xc}
   {INDEX: indX_Xc}
   {PREFIX: X_Xb.D_POTW}
   {DO}
      {FOR: X_Xw1}
      {INDEX: indX_Xw1}
      {PREFIX: X_Xc.D_POTW,X_Xc.M,}
      {DO}
         {FIRST}
         {
           rsep:=0;
           dokl:=exec('fld_prec','#field',X_Xw1,'IL');
           ss.erase();~~}
           {IF: tryb<>1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
         {FIRST-}
         {
         ww[1] :=form(X_Xw1.SYM);
         ww[2] :=form(X_Xw1.KH);
         ww[3] :=form(X_Xw1.STAN);
         ww[4] :=form(X_Xw1.DATA);
         ww[5] :=form(X_Xw1.DTPREAL);
         ww[6] :=form(X_Xw1.M);
         ww[7] :=form(X_Xw1.JM);
         ww[8] :=form(X_Xw1.IL,,dokl,'.,');
         ww[9] :=form(X_Xw1.IL_POZ,,dokl,'.,');
         ww[10]:=form(X_Xw1.NET,,doklw,'.,');
         kgr   :=form(X_Xw1.MGR);
         kdt   :=form(X_Xw1.D_POTW);
         ss.clear();
         ss.prefix(X_Xw1.JM,);
         {? ss.first()
         || ss.S1+=X_Xw1.IL $dokl;
            ss.S2+=X_Xw1.IL_POZ $dokl;
            ss.S3+=X_Xw1.NET $dokl;
            ss.put(1)
         || ss.blank();
            ss.JM:=X_Xw1.JM;
            ss.S1:=X_Xw1.IL $dokl;
            ss.S2:=X_Xw1.IL_POZ $dokl;
            ss.S3:=X_Xw1.NET $dokl;
            ss.add(1)
         ?};
         kolej:=1;
         ~~}
         {SUBSTITUTE: 'LINIA0'}
         { rsep:=PAR_WYDR.RSEP;~~}
      {OD}
      {IF: tryb<>1}
         {lline:=lineleft();~~}
         {IF: lline=1}{SUBSTITUTE: 'LINIAF'}{LINE: 1}{FI}
         {IF: lline>1}{SUBSTITUTE: 'LINIAF'}{FI}
         {FOR: ss}
         {DO}
            {
            {! _i:=1..10 |! ww[_i]:='' !};
            ww[2] :='Dostawa: %1'@[form(X_Xc.D_POTW)];
            ww[6] :=X_Xc.KTM;
            ww[7] :=ss.JM;
            ww[8] :=form(ss.S1,,dokl,'.,');
            ww[9] :=form(ss.S2,,dokl,'.,');
            ww[10]:=form(ss.S3,,doklw,'.,');
            PRN.n_frame(); PRN.FORM[2]:=0; rsep:=0; kolej:=0;
            ~~}
            {FONT: 'T8GB'}{SPACE: 'C'}
            {SUBSTITUTE: 'LINIA0'}
         {OD}
         { PRN.s_frame(); PRN.FORM[2]:=1; ~~}
         {lline:=lineleft(); ~~}
         {IF: lline=1}{LINE: 1}{FI}
       {FI}
   {OD}
{OD}
{IF: tryb=1}
   {SUBSTITUTE: 'LINIAF'}
{FI}
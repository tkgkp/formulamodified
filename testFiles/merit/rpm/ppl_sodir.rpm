:!UTF-8
{TITLE: Raport wysyłki}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   tresc:='sodir';
   PAR_WYDR.TITLE:='Raport wysyłki do SODiR OffLine'@;
   SOD_P.cntx_psh();
   SOD_N.cntx_psh();
   SOD_S.cntx_psh();
   SOD.cntx_psh();
   SOD.prefix();
   SOD_N.index('SOD_N3');
   __DOC.first;
   __Kwota:=__Licz:=0;
   __KwotaSTR:=__Td:=__NI:=__Naz:=__Dok:='';

   {? var_pres('par')>100 || obj_del(par) ?};
   par:=obj_new(1);
   par[1]:=obj_new(@.CLASS.PRINT,5);
   par[1].add("{? SOD_N.IM='R  A  Z  E  M' || '' || __Licz ?}",4,'Lp.'@,1);
   par[1].add("SOD_N.NAZ+' '+SOD_N.IM",55,'Nazwisko i imię'@);
   par[1].add("SOD_N.PESEL",11,'PESEL');
   {? ~(__DOC.DOK='WND' & exec('d_read','!ppl_zes_exso',SOD_N.ref,0)=4)
   || par[1].add("__KwotaSTR",14,'Kwota (złotych)',1)
   ?};
   par[1].add("__DOC.DOK",12,'Załącznik');
   par[1].s_frame();

   prn1:='par[1]';
   lmt:=rsep:=0;
   vp:=1;
   b_rat:=h_rat:=1
}
{END:
   obj_del(par); &prn1; &vp; &lmt; &rsep; &b_rat; &h_rat;
   undefine; &tresc;

   SOD_P.cntx_pop();
   SOD_N.cntx_pop();
   SOD_S.cntx_pop();
   SOD.cntx_pop();
   &__Kwota;&__KwotaSTR;&__Licz; &__NI;&__Td; &__Naz; &__Dok
}
{COMMENT}OLD: sodir.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A
{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: page=1}
   {FONT: 'T8'}{ h_rat:=charleft; ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft/h_rat; ~~ }
   {SPACE: 'C'}
   {LINE}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Symbol paczki:'@}{<{ceil(lmt*h_rat)+20}SOD_P.ID}
   {<{ceil(lmt*h_rat)+1}'Okres:        '@}{<{ceil(lmt*h_rat)+20}__okSp}
   {<{ceil(lmt*h_rat)+1}'Rodzaj paczki:'@}{<{ceil(lmt*h_rat)+20}{? SOD_P.KOR='Z' || 'Zwykła'@ || 'Korygująca'@ ?}}
   {<{ceil(lmt*h_rat)+1}'Data eksportu:'@}{<{ceil(lmt*h_rat)+20}{? SOD_P.DW=date(0,0,0) || 'Nie wysłana'@ || SOD_P.DW$1 ?}}

{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'doctype'}
   {FONT: 'T8B'}

      { __Td:='Dokument: '+__DOC.DOK+' - '+SOD_N.NAZ;
      __Dok:=__DOC.DOK; __Naz:=SOD_N.NAZ;~~}
      {<{ceil(lmt*h_rat)}}{__Td}
   {FONT: 'T8'}
{MACRO-}
{MACRO: 'koniecstrony'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{PAGE}
{ __DOC.DOK:=__Dok;
  SOD_N.NAZ:=__Naz;
~~}
{SUBSTITUTE: 'doctype'}
{REP: par[1].fhbar(lmt)}
{ __DOC.get();
  SOD_N.get();
~~}
{MACRO-}
{MACRO: 'poczstrony'}
{REP: _f:=prn1+'.hbar(lmt)'; ($(_f))()}
{MACRO-}
{MACRO: tresc}
{vp:=0;~~}
{IF: __DOC.first()}
{FOR: __DOC}
   {PREFIX: SOD_P.ID}
{DO}
{__Licz:=__Kwota:=0;~~}
{FOR: SOD_N}
   {INDEX: 'SOD_N3'}
   {PREFIX: SOD_P.ref,__DOC.DOK,__DOC.DOK}
{DO}
{ _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
{IF: (__DOC.DOK='INF-U-P' | __DOC.DOK='INF-D-P')}
   {IF: SOD_N.size>0}
      { __NI:=SOD_N.NAZ+', '+SOD_N.IM;
       __Licz+=1;~~}
      {IF: __Licz=1}
         {SUBSTITUTE: 'poczstrony'}
      {FI}
      { {? __DOC.DOK='INF-U-P'
        || _ile:=exec('d_read','!ppl_zes_exso',SOD_N.ref,57)
        || _wer:=exec('d_read','!ppl_zes_exso',SOD_N.ref,0);
           _ile:={? _wer>=6
                 || 0
                 || exec('d_read','!ppl_zes_exso',SOD_N.ref,{? _wer<5 || 50 || 43 ?})
                 ?}
        ?};
       __Kwota+=_ile;
       __KwotaSTR:=exec('int2str','!ppl_zes_exso',_ile,SOD.PR);~~}
      {SUBSTITUTE: 'LINIA0'}
      {IF: __Licz=SOD_N.size}
      {SOD_N.IM:='R  A  Z  E  M'@;
       SOD_N.PESEL:=SOD_N.NAZ:=__DOC.DOK:='';
       __KwotaSTR:=exec('int2str','!ppl_zes_exso',__Kwota,2);
      ~~}
      {IF: __Kwota>0}
      {SUBSTITUTE: 'LINIA0'}
      {FI}
{REP: _f:=prn1+'.lbar(lmt)'; ($(_f))()}
         {__Kwota:=__Licz:=0;~~}
      {ELIF: lineleft & lineleft<=6}
         {SUBSTITUTE: 'koniecstrony'}
      {FI}
    {FI}
{ELIF: (__DOC.DOK='WND' | __DOC.DOK='WNU') }
   {IF: lineleft & lineleft<=6}
      {PAGE}
   {FI}
   {SUBSTITUTE: 'doctype'}
{ELIF: (__DOC.DOK='WND2II' | __DOC.DOK='WNU_II')}
   {<{ceil(lmt*h_rat)}'Dołączono załącznik %1 - %2'[__DOC.DOK,SOD_N.NAZ]}
{FI}
{OD}
{OD}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft; ~~ }
{FONT: 'T8'}{ b_rat/=charleft; ~~ }
{SPACE: 'B'}
{FONT: 'W12'}{ b_rat:=charleft; ~~ }
{FONT: 'T8'}{ b_rat/=charleft; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft-par[1].width())/2); ~~ }
      {SUBSTITUTE: tresc}
{FOOTER: 0}
   { vp:=0; ~~ }
{FOOTER-}
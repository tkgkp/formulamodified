:!UTF-8
{TITLE:Karta ubezpieczeniowa - zestawienie roczne}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','korndx','prn1','vp','lmt','rsep','tresc','b_rat','__edit','__param');
   tresc:='kartzus';
   PAR_WYDR.TITLE:='Karta ubezpieczeniowa - zestawienie roczne'@;
   P.cntx_psh();
   par:=obj_new(10);
   par[7]:=par[9]:=par[10]:=0;
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
        'ROK','INTEGER','Rok',
        'U1','INTEGER','Korekty ZUS',
        'U2','INTEGER','Umowy zlecenia',
        'U3','INTEGER','Umowy zlecenia - zatrudnienie',
        'U4','INTEGER','Bez umów zleceń'
      );
      __param.ROK:=O.RU;
      __param.add();

      _nx:='Zaznacz opcje';
      {? (_ox:=exec('get','#profile',,tresc,_nx))='' || _ox:=4*'0' ?};
      {! _ii:=1..+_ox |! ($('_a.U'+$_ii))(__param):=($(_ii+_ox+1))() !};

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pkartzus');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok ubezpieczeniowy'@);
      __param.win_efld(__edit,,'U1',,,,,,' ',,,'check-box'
         ,'check_label="%1"'['Korekty ZUS'@],"1","0"
      );
      __param.win_efld(__edit,,'U2',,,,,,' ',,,'check-box'
         ,'check_label="%1"'['Umowy zlecenia'@],"1","0"
      );
      __param.win_efld(__edit,,'U3',,,,,,' ',,,'check-box',
         'check_label="%1"'['Umowy zlecenia - zatrudnienie'@],"1","0"
      );
      __param.win_efld(__edit,,'U4',,,,,,' ',,,'check-box',
         'check_label="%1"'['Bez umów zleceń'@],"1","0"
      );
      __param.efld_opt(__edit,'mark=1',,'ROK');
      exec('ok_esc','#window',__param,__edit);

      __param.fld_fml('U2','AFTER_EDIT',"{? __param.U2|| __param.U3:=0; __param.U4:=0 ?}; __param.put()");
      __param.fld_fml('U3','AFTER_EDIT',"{? __param.U3|| __param.U2:=0; __param.U4:=0 ?}; __param.put()");
      __param.fld_fml('U4','AFTER_EDIT',"{? __param.U4|| __param.U2:=0; __param.U3:=0 ?}; __param.put()");

      __param.win_edit(__edit);

      {? par[10]:=__param.edit(
            "  {? __param.ROK<=1900
               || FUN.emsg('Błędna wartość parametru wydruku - Raport za rok.'@); 'ROK'
               || par[9]:=__param.ROK; 1
               ?}
            "
         )
      || par[1]:=obj_new(@.CLASS.PRINT,14);
         {? rep_export()
         || par[1].add("par[2][par[4]][17]",40,'Pracownik'@)
         ?};
         par[1].add("par[2][par[4]][14]",20,'Nazwa składnika'@);
         {! _sx:=1..12
         |! STR.split(date(date()~1,_sx,1)$8);
            par[1].add($('par[2][par[4]]['+$_sx+']'),12,STR.get_word(),1,,,,,'12,2')
         !};
         {? ~rep_export || par[1].add("par[2][par[4]][13]",12,'RAZEM'@,1,,,,,'12,2') ?};
         par[1].s_frame();
         par[2]:=obj_new(36);
         {! _ix:=1..obj_len(par[2])
         |! par[2][_ix]:=obj_new(17);
            par[2][_ix][14]:=($('KART_ZUS.S'+$_ix))();
            par[2][_ix][15]:=_ix;
            par[2][_ix][16]:=0;
            par[2][_ix][17]:=''
         !};

         par[3]:=tab_tmp(,'REF','INTEGER','Osoba');

         {? __param.U1=1
         || korndx:=KORN.ndx_tmp(,,'P','FIRMA',,'P','OSOBA',,'RU',,);
            KOR.index('KOR_PRN');
            KORN.index(korndx)
         ?};

         _ox:='';
         {! _ix:=1..4 |! _ox+=$(($('_a.U'+$_ix))(__param)) !};
         exec('set','#profile',,tresc,_nx,_ox);
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?};
   prn1:='par[1]';
   lmt:=rsep:=par[4]:=0;
   b_rat:=vp:=par[6]:=1;
   O.cntx_psh();
   LS.cntx_psh();
   KU.cntx_psh;
   KU.index('KARTAUB');
   RH.cntx_psh();
   RH.index('RACHPRZ');
   R.cntx_psh();
   R.index('RUBRSKU')
}
{END:
   {? par[10]
   || {? __param.U1=1 || KORN.ndx_drop(korndx) ?}
   ?};
   VAR_DEL.delete('par','korndx','prn1','vp','lmt','rsep','tresc','b_rat','__edit','__param');
   P.cntx_pop();
   R.cntx_pop();
   RH.cntx_pop();
   KU.cntx_pop();
   LS.cntx_pop();
   O.cntx_pop()
}
{EXIT: ~par[10]}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: pkartzus.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[6]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[6]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Karta ubezpieczeniowa: lista płac'@
   }{{? __param.U2=1 || ', umowy zlecenia'@ |? __param.U3=1 || ', umowy zlecenia - zgodne z zatrudnieniem'@ || '' ?}
   }{{? __param.U1=1 || ', korekty ZUS'@ || ''?}}
   {<{lmt+1}'Rok ubezpieczeniowy: '@+$par[9]}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'osoba'}
{ par[4]:=0; ~~ }
{IF: ~rep_export()}
   {FONT: 'W10'}
   {SPACE: 'B'}
   {<{ceil(lmt*b_rat)+1}}{form(par[7]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{MACRO-}
{MACRO: tresc}
{ENTIRE}
{ par[4]:=0; ~~ }
{IF: ~par[3].find_key(#P.OSOBA)}
   {  par[3].REF:=#P.OSOBA;
      par[3].add();

      echo('Trwa uzupełnianie informacji: '@+P.OSOBA().NAZWISKO);
      KU.prefix(P.FIRMA,P.OSOBA,par[9]);

      {! _nx:=1..obj_len(par[2])
      |! {! _mx:=1..obj_len(par[2][_nx])-4
         |! par[2][_nx][_mx]:=0
         !}
      !};

      {? KU.first()
      || par[4]:=1;
         {!
         |? {! _nx:=1..obj_len(par[2])
            |! _kw:=($('KU.S'+$_nx))();
               par[2][_nx][KU.M]+=_kw;
               par[2][_nx][13]+=_kw;
               {? _kw || par[2][_nx][16]:=1 ?};
               par[2][_nx][17]:='%1 %2 %3 %4'
                  [  KU.OSOBA().NAZWISKO,
                     OSOBA.PIERWSZE,
                     {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                     '(PESEL %1)'@[OSOBA.PESEL]
                  ]
            !};
            KU.next()
         !}
      ?};

      {? __param.U1=1
      || KORN.prefix(exec('ref_firma','ustawienia'),P.OSOBA,par[9]);
         {? KORN.first()
         || par[4]:=1;
            {!
            |? KOR.prefix(KORN.ref());
               {? KOR.first()
               || R.prefix();
                  _R_nr:=
                     "  R.seek(_a);
                        R.RSKU
                     ";
                  {!
                  |? par[2][_R_nr(KOR.RU)][KORN.MU]+=KOR.SKL;
                     par[2][R.RSKU][13]+=KOR.SKL;
                     {? KOR.SKL || par[2][R.RSKU][16]:=1 ?};
                     {? __RUB.sys_attr(R.RN,57112,date(KORN.RU,KORN.MU,1))
                     || par[2][1][KORN.MU]+=KOR.POD;
                        par[2][1][13]+=KOR.POD;
                        par[2][1][16]:=1;
                        par[2][1][17]:='%1 %2 %3 %4'
                           [  KORN.P().OSOBA().NAZWISKO,
                              OSOBA.PIERWSZE,
                              {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                              '(PESEL %1)'@[OSOBA.PESEL]
                           ]
                     |? __RUB.sys_attr(R.RN,5731,date(KORN.RU,KORN.MU,1))
                     || par[2][10][KORN.MU]+=KOR.POD;
                        par[2][10][13]+=KOR.POD;
                        par[2][10][16]:=1;
                        par[2][1][17]:='%1 %2 %3 %4'
                           [  KORN.P().OSOBA().NAZWISKO,
                              OSOBA.PIERWSZE,
                              {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                              '(PESEL %1)'@[OSOBA.PESEL]
                           ]
                     |? __RUB.sys_attr(R.RN,57411,date(KORN.RU,KORN.MU,1))
                     || par[2][13][KORN.MU]+=KOR.POD;
                        par[2][13][13]+=KOR.POD;
                        par[2][13][16]:=1;
                        par[2][1][17]:='%1 %2 %3 %4'
                           [  KORN.P().OSOBA().NAZWISKO,
                              OSOBA.PIERWSZE,
                              {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                              '(PESEL %1)'@[OSOBA.PESEL]
                           ]
                     ?};
                     KOR.next()
                  !}
               ?};
               KORN.next()
            !}
         ?}
      ?};

      {? __param.U3=1 | __param.U2=1
      || {? __param.U3=1
         || {? P.DZ=#0 | P.DZ>date(par[9],12,0)
            || _hdo:=date(par[9],12,0)
            || _hdo:=P.DZ
            ?};
            {? date(par[9],1,1)<P.DZA
            || _hod:=P.DZA
            || _hod:=date(par[9],1,1)
            ?}
         || _hod:=date(par[9],1,1);
            _hdo:=date(par[9],12,0)
         ?};
         P.cntx_psh();
         P.clear();
         exec('filtruj_p','schemat',
            'PPL',
            exec('szukaj_ud_def','schemat',
               exec('domyslny','schemat','PODZORG'),
               __PARSES.getVal('JednostkaOrganizacyjna').REF
            ).REF,
            'Z',,
            __PARSES.getVal('ZakresDanych'),,
            'P.OSOBA=\''+$P.OSOBA+'\''
         );
         {? P.f_first()
         || O.cntx_psh();
            {!
            |? RH.prefix(P.ref(),par[9]);
               {? RH.first()
               || par[4]:=1;
                  {!
                  |? {? RH.DWY>=_hod & RH.DWY<=_hdo
                     || RH.O();
                        LS.use(-O.LT);
                        LS.index('ZLEC');
                        {! _nx:=1..obj_len(par[2])
                        |! R.prefix(par[2][_nx][15]);
                           {? R.first()
                           || {!
                              |? LS.prefix(RH.ref(),R.RN);
                                 {? LS.first()
                                 || {!
                                    |? par[2][_nx][RH.DWY~2]+=LS.KW;
                                       par[2][_nx][13]+=LS.KW;
                                       par[2][_nx][16]:=1;
                                       par[2][_nx][17]:='%1 %2 %3 %4'
                                          [  LS.P().OSOBA().NAZWISKO,
                                             OSOBA.PIERWSZE,
                                             {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                                             '(PESEL %1)'@[OSOBA.PESEL]
                                          ];
                                       LS.next()
                                    !}
                                 ?};
                                 R.next()
                              !}
                           ?}
                        !}
                     ?};
                     RH.next()
                  !}
               ?};
               P.f_next()
            !};
            O.cntx_pop()
         ?};
         P.f_clear();
         P.cntx_pop()
      ?}; ~~
}
{FI}
{IF: par[4]}
   {SUBSTITUTE: 'osoba'}
   {REP: par[1].fhbar(lmt)}
   {WHILE: (par[4]+=1)<=obj_len(par[2])}{DO}
      {IF: par[2][par[4]][16]}
         { par[1].ini_line(); ~~ }
         {WHILE: par[1].next()}{DO}
            {REP: par[1].fline(lmt)}
         {OD}
      {FI}
   {OD}
   {REP: par[1].flbar(lmt)}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W10'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
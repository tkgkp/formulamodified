:!UTF-8
{TITLE:Zestawienie wniosków dotyczących podatków}
{PRINTER: dziedzina:='PKD';
          exec('prt','param_wydr','h',KST),,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PAR_WYDR.TITLE:='Zestawienie wniosków dotyczących podatków'@;
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','exit','Nr','__param','__WNPODAT','__slo_kod');
   _perm:=exec('uprawnieniawrap','pkd','urzędach skarbowych'@,'PKD_DOS_PPUS','PKD_DOS_PRUS');
   update:=(var_pres('OS_ZWZAL')>100);
   exit:=0;
   {? +_perm
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_perm]);
      exit:=1
   |? ~update
   || FUN.emsg('Brak wgranej aktualizacji dotyczącej Polskiego Ładu.'@);
      exit:=1
   || tresc:='os_zwzal';
      lmt:=lp:=0;
      color:=prn1:='';
      _slo_typ:=exec('slo_typ','ext_slo','WN_PODAT');
      __slo_kod:='';
      __param:=exec('pkd_wn_podat','personel',PAR_WYDR.TITLE);
      {? __param.size()
      || _where:='';
         _createWhere:="
            _f_zatr:={? __param.SP=1 & (_a+2)*'_P'
                     || ' and F_ZATR.KOD=\\'P\\''
                     |? __param.SP=1 & (_a+2)*'_Z'
                     || ' and F_ZATR.KOD=\\'Z\\''
                     || ''
                     ?};
            '(SLO_KOD.KOD=\\''+_a+'\\''+_f_zatr+') or '
         ";
         {? __param.TYP
         || {? var_pres('_res')>100 || obj_del(_res) ?};
            _res:=exec('wybierz_kody','ext_slo',_slo_typ);
            {? _res.OK &_res.WYBOR.first()
            || {!
               |? __slo_kod+=_res.WYBOR.KOD+', ';
                  _where+=_createWhere(_res.WYBOR.KOD);
                  _res.WYBOR.next()
               !}
            || exit:=1
            ?}
         || SLO_KOD.cntx_psh();
            SLO_KOD.index('KOD');
            SLO_KOD.prefix(_slo_typ);
            {? SLO_KOD.first()
            || {!
               |? __slo_kod+=SLO_KOD.KOD+', ';
                  _where+=_createWhere(SLO_KOD.KOD);
                  SLO_KOD.next()
               !}
            || exit:=1
            ?};
            SLO_KOD.cntx_pop()
         ?};
         {? ~exit
         || __slo_kod-=2;
            _where:={? +_where>0 || 'and ('+(_where-3)+') ' || '' ?};

            _query:=
               'select distinct '+
                     {? __param.SP=1
                     || 'P.T as T, '
                        'UD_SKL.SYMBOL as SYMBOL, '
                        'STN.ST as ST, '
                     || ''
                     ?}+
                     'OSOBA.PIERWSZE as PIERWSZE, '
                     'OSOBA.NAZWISKO as NAZWISKO, '
                     'OSOBA.PESEL as PESEL, '
                     'OS_ZWZAL.D_ZO as DT_ZOS, '
                     'OS_ZWZAL.D_OB as DT_OOS, '
                     'OS_ZWZAL.AKTYWNY as AKTYWNY, '
                     'SLO_KOD.NAZWA as RODZAJ '
               'from OS_ZWZAL '
                     'join SLO_KOD using(OS_ZWZAL.SLO_KOD, SLO_KOD.REFERENCE) '
                     'join OSOBA using(OS_ZWZAL.OSOBA, OSOBA.REFERENCE) '
                     'join P using(OSOBA.REFERENCE, P.OSOBA) '+
                     {? __param.SP=1
                     || 'join UD_SKL using(P.WYDZIAL, UD_SKL.REFERENCE) '
                        'join STN using(P.ST, STN.REFERENCE) '
                        'join F_ZATR using(P.F_ZATR, F_ZATR.REFERENCE) '
                     || ''
                     ?}+
               'where OS_ZWZAL.FIRMA=:_a '
                     'and P.REFERENCE in (select :_b.REF from :_b) '
                     'and OS_ZWZAL.ROK=:_c '+
                     {? __param.AKT=1
                     || 'and OS_ZWZAL.AKTYWNY=\'T\''
                     |? __param.AKT=2
                     || 'and OS_ZWZAL.AKTYWNY=\'N\''
                     || ''
                     ?}+
                     _where+
               'order by NAZWISKO,PIERWSZE,PESEL'+
                     {? __param.SP=1
                     || ',T'
                     || ''
                     ?};
            __WNPODAT:=sql(_query,exec('ref_firma','#firma'),exec('dostepne_p','schemat','PKD','*'),
               __param.ROK);

            'Sprawdzamy, czy są pracownicy spełniający kryteria:';
            {? __WNPODAT.size()
            || _base:=7;
               _ext:=0;
               {? __param.SP=1 || _ext+=2 ?};
               {? rep_export()
               || PRN:=obj_new(@.CLASS.PRINT,_base+_ext)
               || PRN:=obj_new(@.CLASS.PRINT,_base+_ext+1);
                  PRN.add("Nr+=1",3,'Lp.'@,1,,,,,',,\'9.\'')
               ?};
               PRN.add("__WNPODAT.NAZWISKO",MS.fld_len(OSOBA,'NAZWISKO'),'Nazwisko'@);
               PRN.add("__WNPODAT.PIERWSZE",MS.fld_len(OSOBA,'PIERWSZE'),'Imię'@);
               {? __param.SP=1
               || PRN.add("__WNPODAT.T",MS.fld_len(P,'T'),'Nr teczki'@);
                  PRN.add("__WNPODAT.SYMBOL",10,'Jednostka org.'@);
                  PRN.add("__WNPODAT.ST",19,'Stanowisko'@)
               || PRN.add("__WNPODAT.PESEL",MS.fld_len(OSOBA,'PESEL')+1,'PESEL'@)
               ?};
               PRN.add("__WNPODAT.RODZAJ",{? __param.SP=1 || 55 || 70 ?},'Rodzaj wniosku'@);
               PRN.add("$__WNPODAT.DT_ZOS",10,'Data złożenia wniosku'@);
               PRN.add("$__WNPODAT.DT_OOS",10,'Wniosek obowiązuje od'@);
               PRN.add("__WNPODAT.AKTYWNY",7,'Aktywny'@);
               PRN.s_frame();

               prn1:='PRN';
               lmt:=Nr:=0;
               rsep:=vp:=1
            || exit:=1;
               FUN.info('Brak pracowników spełniających kryteria.'@)
            ?}
         ?}
      || exit:=1
      ?}
   ?};
   ~~
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','exit','Nr','__param','__WNPODAT','__slo_kod')
}
{EXIT: exit}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{COMMENT} Parametry sprawozdania: {COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{<{ceil(lmt)+3}'Parametry sprawozdania'@}
{<{ceil(lmt)+3}'Rodzaje wniosków:'@} {__slo_kod}
{<{ceil(lmt)+3}'Rok wniosków: '@} {__param.ROK}
{<{ceil(lmt)+3}'Sposób prezentacji:'@} {{? __param.SP=1 || 'w etatach'@ || 'w osobach'@ ?}}
{<{ceil(lmt)+3}'Zakres wniosków:'@} {{? __param.AKT=1 || 'aktywne'@ |? __param.AKT=2 || 'nieaktywne'@ || 'wszystkie'@?}}
{FONT: 'T8B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: __WNPODAT}
{DO}
   {FONT: 'T8'}
   {SPACE: 'B'}
   {SUBSTITUTE: 'LINIA0'}
{OD}
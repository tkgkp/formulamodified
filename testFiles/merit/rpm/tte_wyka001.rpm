:!UTF-8
{TITLE:A. Zestawienie godzin wypracowanych przez pracowników}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  "---Obiekt wydruku godzin przepracownych na zleceniach.";
  PRNZGB:=obj_new(@.CLASS.PRINT, 9);PRNZGB.s_frame();
  "---Obiekt wydruku sumy dla pracownika.";
  PRNSUM:=obj_new(@.CLASS.PRINT, 9);PRNSUM.s_frame();

  kreska :='─';
  "---1 - pierwszy rekord w iteracji.";
  first:=1;
  "---Suma godzin dla pracownika.";
  sum_g:=0;sum_gp:=0;godz_p:=0;  sumgpall:=0;
  "---Suma z wartosci dla pracownika.";
  sum_kw:=0;
  "---'Z' - wydruk z podzialem na zlecenia (bardziej szczegolowy),";
  "---'D' - wydruk z podzialem na dni (bardziej ogolny).";
  podzial:='Z';
  "---3 ponizsze zmienne sa wykorzystywane do wydruku z podzialem na dni.";
  data:=date(0,0,0);
  zmiana:='';
  godziny:=0;
  kwota:=0;
  "---Ostatnio drukowana data.";
  lastdate:=date(0,0,0);
  lastzmiana:='';
  "---1 - selekcja po pracownikach, 0 - wszyscy pracownicy.";
  select:=0;
  "---Zmienna do pobrania informacji o wcisnietym przycisku.";
  asktmp:=0;
  "---Podsumowanie calosci.";
  sum_g_all:=0;
  sum_kw_all:=0;
  prn1:='PRNZGB';
  tryb:=rep_export();
  waslbar:=0;
  ndxzlgb1:=ZLGB.ndx_tmp(,1,
     'P',,0,
     'ZLGD','O',0,
     'ZLGD','ZL',0,
     'ZLGD','DT',0,
     'ZLGD','ZMIANA',0
  );
  dalej:=0;
  color:='';
  lm:=ll:=vp:=lmt:=rsep:=0;
  nazwisko:=pierwsze:=pesel:='';
  timedok:=exec('get','#params',500604,1);
  fnt:='8';
  razem:='RAZEM:'@;
  params_set('P',exec('wybierz_pracownika','tte',1).P)
}
{END:
  obj_del(PRNZGB);
  obj_del(PRNSUM);
  &kreska;
  &first;
  &sum_g;
  &sum_gp;
  &godz_p;
  &sum_kw;
  &sumgpall;
  &podzial;
  &data;
  &zmiana;
  &godziny;
  &kwota;
  &lastdate;
  &lastzmiana;
  &select;
  &asktmp;
  &sum_g_all;
  &sum_kw_all &dalej;
  &prn1;
  &color;
  &lm; &ll; &vp; &lmt; &rsep;
  ZLGB.ndx_drop();
  &ndxzlgb1;
  &timedok;
  &nazwisko;
  &pierwsze;
  &pesel;
  &razem;
  &fnt;
  &tryb
}
{COMMENT}
   Wydruk godzin wypracownych na zleceniach.
  [SS] - 2000/10/02 - [7.22]
  [MKO] - 2001/11/12 - [7.53] OWwSK003_Aneks1
  [MKO] - [7.60] godziny planowane i wykonane
  [TS] - 2002/11/22 - [7.62] rejestracja dla zmian roboczych
  Wywołanie: Wykonania produkcji -> Raporty -> Zestawienia
{COMMENT-}
{INCLUDE: tte_wykx001.rpi}
{"---Sekcja akcji inicjalizujacych.                                       "; ~~}
{MACRO: 'miscelaneous'}
  {"---QUERY (wg zlecen czy dni).                                           "; ~~}
  {asktmp:=FUN.choice('Wydruk z podziałem na:'@,0,'Zlecenia'@,'Dni'@);~~}
  {IF: asktmp=0}
    {EXIT}
  {ELIF: asktmp=1}
    {podzial:='Z'; ~~}
  {ELSE}
    {podzial:='D'; ~~}
  {FI}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Wykonanie akcji inicjalizujacych.                                    "; ~~}
{SUBSTITUTE: 'miscelaneous'}
{"------------------------------------------------------------------------"; ~~}
{"---Definicja wiersza jest zalezna od zmiennej podzial.                  "; ~~}
{IF: ~(-podzial)='Z'}
  {"---Definicja wiersza wydruku godzin przepracownych na zleceniach ZLGB "; ~~}
  {"---(z podzialem na zlecenia)."; ~~}
  { {? tryb=1
    || PRNZGB.add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
       PRNZGB.add("OSOBA.PIERWSZE",20,'Imię'@)
    ?};
    PRNZGB.add("form(ZLGB.ZLGD().DT)",10,'Data'@);
    PRNZGB.add("form(ZLGB.ZLGD().ZMIANA)",6,'Zmiana'@,1);
    PRNZGB.add("ZLGB.ZLGD().ZL().SYM",15,'Zlecenie'@);
    PRNZGB.add("form(ZLGB.TIME_P,,timedok)",10,'Godziny planowane'@,1);
    PRNZGB.add("form(ZLGB.TIME,,timedok)",11,'Godziny wypracowane'@,1);
    PRNZGB.add("form(ZLGB.ST,,2)",15,'Stawka'@,1);
    PRNZGB.add("form(ZLGB.KW,,2)",15,'Kwota'@,1); ~~}
{ELSE}
  {"---Definicja wiersza wydruku godzin przepracownych na zleceniach ZLGB "; ~~}
  {"---(z podzialem na dni i zmiany).                                     "; ~~}
  { {? tryb=1
    || PRNZGB.add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
       PRNZGB.add("OSOBA.PIERWSZE",20,'Imię'@)
    ?};
    PRNZGB.add("form(data)",10,'Data'@);
    PRNZGB.add("zmiana",6,'Zmiana'@,1);
    PRNZGB.add("form(godz_p,,timedok)",10,'Godziny planowane'@,1);
    PRNZGB.add("form(godziny,,timedok)",11,'Godziny wypracowane'@,1);
    PRNZGB.add("form(kwota,,2)",15,'Kwota'@,1); ~~}
{FI}
{"------------------------------------------------------------------------"; ~~}
{"---Definicja wiersza sumy dla pracownika.                               "; ~~}
{IF: ~(-podzial)='Z'}
  { PRNSUM.add("razem",10,'');
    PRNSUM.add("''",6,'');
    PRNSUM.add("''",15,'');
    PRNSUM.add("form(sum_gp,,timedok)",10,'Godziny planowane'@,1);
    PRNSUM.add("form(sum_g,,timedok)",11,'Godziny wypracowane'@,1);
    PRNSUM.add("''",15,'',1);
    PRNSUM.add("form(sum_kw,,2)",15,'Kwota'@,1); ~~}
{ELSE}
  { PRNSUM.add("razem",10,'');
    PRNSUM.add("''",6,'');
    PRNSUM.add("form(sum_gp,,timedok)",10,'Godziny planowane'@,1);
    PRNSUM.add("form(sum_g,,timedok)",11,'Godziny wypracowane'@,1);
    PRNSUM.add("form(sum_kw,,2)",15,'Kwota'@,1); ~~}
{FI}
{ PAR_WYDR.TITLE:='ZESTAWIENIE GODZIN WYPRACOWANYCH PRZEZ PRACOWNIKÓW'@;
  fnt:={? podzial='Z' || '8' || '10' ?}; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T'+fnt}{SPACE: 'B'}
{ lmt:=int((charleft()-PRNZGB.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1}}{'Oddział %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1} 'Rok obrotowy: %1 okres: %2'@[$ST.AR,form(ST.AM,-2)]}
{<{lmt+1} {? podzial='Z' || 'Podział według zleceń'@ || 'Podział według dat'@ ?}}

{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------MAKRA-----------------------{COMMENT-}
{"---Wnetrze glownej petli wydruku (wydruk z podzialem na zlecenia).      "; ~~}
{MACRO: 'core_z'}
   { echo(P.OSOBA().NAZWISKO);
    first:=1;prn1:='PRNZGB';
    sum_g:=sum_kw:=sum_gp:=0; ~~}
  {FOR: 'ZLGB'}
    {INDEX: ndxzlgb1}
    {PREFIX: P.ref(),ST.OKR_REF}
  {DO}
    {FIRST}
        {IF: tryb<>1}
        {IF: lineleft<7}{PAGE}{FI}
{<1      lk:=charleft(); ~~}
        {<1>{lk} 'Pracownik: %1 %2'@[P.OSOBA().NAZWISKO,OSOBA.PIERWSZE]}
        {FONT: 'T'+fnt+'B'}
        {SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}
        {SUBSTITUTE: 'LINIA0'}
        {waslbar:=0; ~~}
        {FI}
    {FIRST-}
    {IF: ~first}
     {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIA0'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T'+fnt+'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
    {FI}
    {first:=0; ~~}
    { sum_g+=ZLGB.TIME;
      sum_gp+=ZLGB.TIME_P;
      sum_kw+=ZLGB.KW;
      sum_g_all+=ZLGB.TIME;
      sumgpall+=ZLGB.TIME_P;
      sum_kw_all+=ZLGB.KW; ~~}
  {OD}
  {"---Sume drukuj tylko wowczas, gdy cokolwiek wystapilo.                "; ~~}
  {IF: first=0}
    {IF: tryb<>1}
     {waslbar:=1;prn1:='PRNSUM'; ~~}
     {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T'+fnt+'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
      {SUBSTITUTE: 'LINIAF'}
    {FI}
  {FI}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Wnatrze glownej petli wydruku (wydruk z podzialem na dni).           "; ~~}
{MACRO: 'core_p'}
  { echo(P.OSOBA().NAZWISKO);
    first:=1;prn1:='PRNZGB';
    sum_g:=sum_gp:=godz_p:=sum_kw:=godziny:=kwota:=0;
    data:=date(0,0,0); zmiana:=''; ~~}
  {FOR: 'ZLGB'}
    {INDEX: 'P_OKRES'}
    {PREFIX: P.ref(),ST.OKR_REF}
  {DO}
    {FIRST}
        {IF: tryb<>1}
        {IF: lineleft< 7 }{PAGE}{FI}
{<1      lk:=charleft(); ~~}
        {<10>{lk} 'Pracownik: %1 %2'@[P.OSOBA().NAZWISKO,OSOBA.PIERWSZE]}
        {FONT: 'T'+fnt+'B'}
        {SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}
        {waslbar:=0; ~~}
        {first:=0; ~~}
        {FI}
    {FIRST-}
    { ZLGB.cntx_psh();
      data:=ZLGB.ZLGD().DT;
      zmiana:=ZLGD.ZMIANA;
      godziny:=godz_p:=kwota:=0; ~~}
    {IF: lastdate<>data | lastzmian<>zmiana}
      {lastdate:=data; lastzmian:=zmiana; ~~}
      {FOR: 'ZLGB'}
        {INDEX: 'PRAC'}
        {PREFIX: P.ref(),data,zmiana}
      {DO}
        { godziny+=ZLGB.TIME;
          godz_p+=ZLGB.TIME_P;
          kwota+=ZLGB.KW;
          sum_g+=ZLGB.TIME;
          sum_gp+=ZLGB.TIME_P;
          sum_kw+=ZLGB.KW;
          sum_g_all+=ZLGB.TIME;
          sumgpall+=ZLGB.TIME_P;
          sum_kw_all+=ZLGB.KW; ~~}
      {OD}
     {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIA0'}
     {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T'+fnt+'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
     {FI}
   {FI}
   {ZLGB.cntx_pop(); ~~}
  {OD}
{"---Sume drukuj tylko wowczas, gdy cokolwiek wystapilo.                "; ~~}
  {IF: first=0}
    {IF: tryb<>1}
      {waslbar:=1;prn1:='PRNSUM'; ~~}
      {IF: lineleft > 5} {SUBSTITUTE: 'LINIA0'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T'+fnt+'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
      {SUBSTITUTE: 'LINIAF'}
    {FI}
  {FI}
{MACRO-}
{"---Wydruk calkowitej sumy (tylko wowczas, gdy drukujemy wszysko).       "; ~~}
{MACRO: 'prn_sum_all'}
  {IF: ~select}
    { prn1:='PRNSUM';
      sum_g:=sum_g_all;
      sum_gp:=sumgpall;
      sum_kw:=sum_kw_all; ~~}
     {IF: ~tryb & lineleft<7}{PAGE}{FI}
{<1   lk:=charleft(); ~~}
     {<1>{lk} 'Podsumowanie całości:'@}
     {FONT: 'T'+fnt+'B'}
     {SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}
     {SUBSTITUTE: 'LINIA0'}
     {SUBSTITUTE: 'LINIAF'}
  {FI}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T'+fnt}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{"---Czesc zasadnicza.                                                    "; ~~}
{sum_g_all:=sum_kw_all:=sumgpall:=0; ~~}
{SUBSTITUTE: 'filtrprac'}
{IF: ~(-podzial)='Z'}
{"---Z dokladnoscia do zlecenia.                                        "; ~~}
{IF: tryb = 1}
   {FONT: 'T'+fnt+'B'}
   {SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}
{FI}
{FOR: 'P'}
{DO}
   {nazwisko:=P.OSOBA().NAZWISKO;pierwsze:=OSOBA.PIERWSZE;pesel:=OSOBA.PESEL;~~}
   {IF: P.f_find(nazwisko,pierwsze,pesel)}
      {SUBSTITUTE: 'core_z'}
   {FI}
{OD}
{IF: tryb=1}
{SUBSTITUTE: 'LINIAF'}
{FI}
{ELSE}
{"---Z dokladnoscia do dni.                                             "; ~~}
{IF: tryb=1}
{FONT: 'T'+fnt+'B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}
{FI}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {nazwisko:=P.OSOBA().NAZWISKO;pierwsze:=OSOBA.PIERWSZE;pesel:=OSOBA.PESEL;lastdate:=date(0,0,0);lastzmiana:='';~~}
      {SUBSTITUTE: 'core_p'}
   {FI}
{OD}
{IF: tryb=1}
{SUBSTITUTE: 'LINIAF'}
{FI}
{FI}
{"---Drukuj calkowite podsumowanie, jesli trzeba.                         "; ~~}
{IF: tryb<>1}{SUBSTITUTE: 'prn_sum_all'}{FI}


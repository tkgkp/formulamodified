:!UTF-8
{TITLE:2. Historia analiz bieżącego zlecenia}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MBOT,
      PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   ANZH.cntx_psh();
   dalej:=0;
   PRN:=obj_new(@.CLASS.PRINT,10);
   PRN.s_frame();
   prn1:='PRN';
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   ndx_tmp:=ANZH.ndx_tmp(,1,'A01','',0,'ZLEC','',0,'OKR','NAZ',0,'OKR','MC',0,'NRA','',0)
}
{END:
   ANZH.cntx_pop();
   &dalej;
   obj_del(PRN);
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   ANZH.ndx_drop(ndx_tmp)
}
{COMMENT}
  Historia analiz biezacego zlecenia.
  [SS] - 2001/03/26 - [7.41]
  [TS] - 2001/10/08 - [7.53] OWwSK003_Aneks1
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Koszty -> Analiza -> Drukuj -> 2. Historia analiz bieżącego zlecenia
{COMMENT-}
{MACRO: 'functions'}
  {"--------------------------------------------------------------------------";
   "---Zwraca wartosc z pozycji biezacej analizy zlecenia, odpowiadajaca      ";
   "---rubryce o numerze okreslonym przez parametr _a. Jezeli w analizie      ";
   "---zlecenia brakuje rubryki o numerze _a, to funkcja zwraca 0.            ";
   "---Parametry:                                                             ";
   "---_a:NUMBER - numer rubryki, z ktorej bedzie odczytana wartosc           ";
   anzhval:="
     ANZP.cntx_psh();
     _result:=0;
     {? _<1
     || _result:=-1;
        FUN.emsg('Funkcja ANZHVAL wydruku ANZH_002.RPM:\nniewłaściwa liczba parametrów!')
     ?};
     {? _result=0
     || ANZP.index('NRARUB');
        ANZP.prefix(ANZH.ref(),_a);
        {? ANZP.first()
        || _result:=ANZP.WAR
        || _result:=0
        ?}
     || _result:=0
     ?};
     ANZP.cntx_pop();
     _result
   ";
   "--------------------------------------------------------------------------";
  ~~}
{MACRO-}
{SUBSTITUTE: 'functions'}
{COMMENT}---Definicja wiersza wydruku pozycji analizy zlecenia ANZP.{COMMENT-}
{  PRN.add("form(ANZH.OKR().ROK,0,0,'9')",4,'Rok'@);
   PRN.add("form(ANZH.OKR().MC,4)",4,'Okr.'@);
   PRN.add("form(ANZH.NRA,,0,,'9')",7,'Nr analizy'@,1);
   PRN.add("form(ANZH.DT)",10,'Data'@);
   "---Kolumna z wariantem analitycznym jest drukowany tylko wowczas, gdy   ";
   "---analizy nie sa filtrowane wariantem.                                 ";
   PRN.add("ANZH.OPC().NA",23,'Wariant'@);
   PRN.add("ANZH.MADE().KOD",10,'Wykonał'@);
   PRN.add("form(anzhval(ANZH.ZLEC().TYP().CRUBALL),,2)",14,'Całk. koszt zlecenia'@,1);
   PRN.add("form(anzhval(ANZH.ZLEC().TYP().CRUBMAT),,2)",14,'Koszt materiałów'@,1);
   PRN.add("form(anzhval(ANZH.ZLEC().TYP().CRUBROB),,2)",14,'Koszt robocizny'@,1);
   PRN.add("form(anzhval(ANZH.ZLEC().TYP().CRUBJEDN),,2)",12,'Cena jednostkowa'@,1);
   PAR_WYDR.TITLE:='HISTORIA ANALIZ ZLECENIA'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Zlecenie: %1'@[ANZH.ZLEC().SYM]}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ ANZH.index(ndx_tmp); ANZH.prefix(VAR.A01,ZL.ref()); ~~}
{IF: ANZH.first()}{ dalej:=1; ~~}{FI}
{WHILE: dalej}
{DO}
   {SUBSTITUTE: 'LINIA0'}
   { dalej:=ANZH.next(); ~~}
{OD}

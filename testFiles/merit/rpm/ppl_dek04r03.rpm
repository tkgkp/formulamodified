:!UTF-8
{TITLE:PIT-4R (3) (2012 r.)}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,5,5,5,5,1,0}
{BEGIN:
   {? var_pres('pit')>100 || obj_del(pit) ?};
   pit:=obj_new(18);
   {! _n:=1..18 |! pit[_n]:='' !};

   pit[1]:=KST.NIP;
   pit[4]:=4+VAT_DEK.OKRES;
   pit[5]:=KST.US().NU+
           {? +KST.US().UU || ', '+KST.US().UU || '' ?}+
           {? +KST.US().UK & +KST.US().MU ||
              ', '+(6+KST.US().UK)+' '+KST.US().MU || ''
           ?};
   pit[8]:=KST.NAZWA+', '+KST.REG;
   pit[9]:='POLSKA';
   pit[10]:=KST.WOJEWODZ;
   pit[11]:=KST.POWIAT;
   pit[12]:=KST.GMINA;
   pit[13]:=KST.ULICA;
   pit[14]:=KST.DOM;
   pit[15]:=KST.LOKAL;
   pit[16]:=KST.MIASTO;
   pit[17]:=KST.KOD;
   pit[18]:=KST.POCZTA;
   pit[6]:=VAT_DEK.KOR;
   pit[2]:=VAT_DEK.IMIE;
   pit[3]:=VAT_DEK.NAZ;

   {! _n:=1..18 |! pit[_n]:=~(-pit[_n]) !};

   {? var_pres('tab')>100 || obj_del(tab) ?};
   tab:=tab_tmp(2,
      'NR','INTEGER','Nr',
      'NRK','INTEGER','Nrk',
      'K1','INTEGER','Kwota',
      'K2','INTEGER','Kwota',
      'K3','INTEGER','Kwota',
      'K4','INTEGER','Kwota',
      'K5','INTEGER','Kwota',
      'K6','INTEGER','Kwota'
   );
   ind:=0; Podatek:=0; czy_raport:=0;
   DEKL_POD.use('pod_'+pit[4]);
   DEKL_POD.index('DEKL_PIT');
   DEKL_POD.prefix;
   CNT:="_c:=(_b-(+_a))%2; form(_c*' '+_a,_b)";

   {? VAT_DEK.KOR='T'
   || {? var_pres('__ord')>100 || obj_del(__ord) ?};
      __ord:=obj_new(13);
      {! _n:=1..obj_len(__ord) |! __ord[_n]:='' !};
      __ord[1]:=KST.NIP;
      __ord[5]:=KST.NAZWA;
      __ord[6]:=KST.SKROT;
      __ord[8]:=KST.REG
   ?};
   VAT_POZ.cntx_psh
}
{END:
   &CNT; &ind; &Podatek; &czy_raport;
   obj_del(pit); &pit; obj_del(tab); &tab;
   VAT_POZ.cntx_pop;
   {? VAT_DEK.KOR='T' || obj_del(__ord); &__ord ?}
}
{COMMENT}OLD: dek04r03.rpm{COMMENT-}
{COMMENT}OLD: dkw4r031.rpi{COMMENT-}
{COMMENT}OLD: dkw4r032.rpi{COMMENT-}
{COMMENT}OLD: dkw4r033.rpi{COMMENT-}
{COMMENT}OLD: dkw4r034.rpi{COMMENT-}
{COMMENT}OLD: ord_zu2.rpi{COMMENT-}
{IF: DEKL_POD.find_key('P','PIT-4R','PIT-4R')}
{EXIT:
   USERS.cntx_psh();
   USERS.prefix();
   _kod:=DEKL_POD.USER().KOD;
   USERS.cntx_pop();
   ~FUN.ask(
      'Za %1 rok wydrukowano deklarację podatkową %2\n'
      '(data: %3, operator: %4).\n'
      'Czy kontynuować wydruk?'@
      [pit[4],DEKL_POD.PIT,DEKL_POD.DATA$7,_kod]
   )
}{FI}
{  tab.prefix;
   VAT_POZ.index('NR_NRK');
   VAT_POZ.prefix(VAT_DEK.ref);
   {? VAT_POZ.first
   || {! |? tab.NR:=VAT_POZ.NR;
            tab.NRK:=VAT_POZ.NRK;
            {! _ind:=1..6
            |! _kw:=($('VAT_POZ.K'+$_ind))();
               ($('tab.K'+$_ind))():=_kw;
               {? tab.NR=11 || czy_raport+=_kw ?};
               {? tab.NR=13 || Podatek+=_kw ?}
            !};
            tab.add;
            VAT_POZ.next
      !}
   ?};
   {? VAT_DEK.KOR='T'
   || __ord[13]:=exec('opis_korekty','edeklar');
      {! _n:=1..obj_len(__ord) |! __ord[_n]:=~(-__ord[_n]) !}
   ?};

   tab.prefix;
   ~~
}
{EXIT:
   {? ~(czy_raport>0)
      || FUN.info('Brak informacji do wykazania na deklaracji.'@+'\n'+'(zerowe kwoty podatku)'@);
         1
      || 0
   ?}
}
{FONT: 'q'}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_dkw4r031.rpi}'}
{DIRECT-}
{PAGE}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_dkw4r032.rpi}'}
{DIRECT-}
{PAGE}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_dkw4r033.rpi}'}
{DIRECT-}
{PAGE}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_dkw4r034.rpi}'}
{DIRECT-}
{PAGE}
{IF: VAT_DEK.KOR='T'}
{DIRECT}
{SPACE: 'd'}{REP: '{INCLUDE: ppl_ord_zu2.rpi}'}
{DIRECT-}
{PAGE}
{FI}
{ DEKL_POD.clear; DEKL_POD.blank;
  DEKL_POD.RODZAJ:='P'; DEKL_POD.MSC:=0;
  DEKL_POD.DATA:=date; DEKL_POD.PIT:='PIT-4R';
  DEKL_POD.S1:=0; DEKL_POD.S2:=0; DEKL_POD.S3:=Podatek;
  DEKL_POD.USER:=OPERATOR.USER;
  DEKL_POD.add;
   ~~
}
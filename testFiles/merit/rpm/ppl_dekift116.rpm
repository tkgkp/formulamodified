:!UTF-8
{TITLE:IFT-1/IFT-1R(16) formularz obowiązujący od roku 2020}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,5,5,5,5,1,1,'A4'}
{BEGIN:
   VAR_DEL.delete('pit','__deklar','y','__ord','x','w','__tab','__iftdan');
   'tablica pit dane identyfikacyjne 1 strona';
   pit:=obj_new(33);
   {! _nx:=1..obj_len(pit) |! pit[_nx]:='' !};
   'tablica __iftdan dodatkowe dane identyfikacyjne 2 strona';
   __iftdan:=obj_new('D1','D2','IMIE','NAZ','DATA','POD_ODP','POD_UPR');
   {! _nx:=1..obj_len(__iftdan) |! __iftdan[_nx]:='' !};
   exec('czytaj','#stalesys',date(),KST);

   pit[1]:=KST.NIP;
   pit[2]:=exec('upo_id','xml');
   pit[8]:='N';
   {? VAT_DEK.POD=1 || pit[8]:='T' ?};
   {? pit[8]='N'
   || pit[9]:=KST.NAZWA;    'Platnik niebedacy osoba fizyczna'
   || pit[9]:=KST.PODNAZ+', '+KST.PODIME+', '+STR.gsub(KST.PODURDAT$4,'.','-'); 'Platnik bedacy osoba fizyczna'
   ?};
   pit[10]:=KST.KRAJ().NAZ;
   pit[11]:=KST.WOJEWODZ;
   pit[12]:=KST.POWIAT;
   pit[13]:=KST.GMINA;
   pit[14]:=KST.ULICA;
   pit[15]:=KST.DOM;
   pit[16]:=KST.LOKAL;
   pit[17]:=KST.MIASTO;
   pit[18]:=KST.KOD;
   {! _nx:=9..18 |! pit[_nx]:=~-pit[_nx] !};

   __ord:=obj_new(13);
   {! _nx:=1..obj_len(__ord) |! __ord[_nx]:='' !};
   {? pit[8]='T'
   || __ord[1]:=KST.NIP;
      __ord[5]:=KST.PODNAZ;
      __ord[6]:=KST.PODIME;
      __ord[7]:=STR.gsub(KST.PODURDAT$4,'.','-')
   || __ord[1]:=KST.NIP;
      __ord[5]:=KST.NAZWA;
      __ord[6]:=KST.SKROT
   ?};
   exec('rpm_as_pdf_ini','rpm_pdf');
   y:=0;
   x:=obj_new(4);
   w:=obj_new(4);
   __tab:=tab_tmp(2,
      'NR','INTEGER','Nr',
      'NRK','INTEGER','Nrk',
      'KW1','REAL','Kwota 1',
      'KW2','REAL','Kwota 2',
      'KW3','REAL','Kwota 3',
      'KW4','REAL','Kwota 4'
   );
   ~~
}
{END:
   exec('rpm_as_pdf_end','rpm_pdf');
   VAR_DEL.delete('pit','__deklar','y','__ord','x','w','__tab','__iftdan');
   exec('stalesys','#stalesys');
   ~~
}
{COMMENT}OLD: dekift116.rpm{COMMENT-}
{COMMENT}OLD: ift116g.rpi{COMMENT-}
{COMMENT}OLD: ord_zu3g.rpi{COMMENT-}
{  {! _ind:=19..33 |! pit[_ind]:='' !};
   {! _nx:=1..obj_len(__iftdan) |! __iftdan[_nx]:='' !};
   __tab.prefix();
   __tab.erase();

   VAT_POZ.index('NR_NRK');
   VAT_POZ.prefix(VAT_DEK.ref);
   {? VAT_POZ.first()
   || {!
      |? __tab.blank();
         __tab.NR:=VAT_POZ.NR;
         __tab.NRK:=VAT_POZ.NRK;
         {! _ind:=1..4
         |! _kw:=($('VAT_POZ.KW'+$_ind))();
            ($('__tab.KW'+$_ind))():=_kw
         !};
         __tab.add();
         VAT_POZ.next()
      !}
   ?};

   pit[4]:=VAT_DEK.OD;
   pit[5]:=VAT_DEK.DO;
   pit[6]:=VAT_DEK.US().NU+{? +US.UU || ', '+US.UU || '' ?}+
           {? +US.UK & +US.MU || ', '+(6+US.UK)+' '+US.MU || '' ?};
   pit[6]:=~-pit[6];
   pit[7]:={? VAT_DEK.KOR='T' || '2' || '1' ?};

   pit[19]:=VAT_DEK.NAZWISKO;
   pit[20]:=VAT_DEK.PIERWSZE;
   pit[21]:=VAT_DEK.OJCIEC;
   pit[22]:=VAT_DEK.MATKA;
   pit[23]:=VAT_DEK.DATA_UR;
   pit[24]:=VAT_DEK.UR_MIEJ;
   pit[25]:=VAT_DEK.ZNIP;
   pit[26]:=VAT_DEK.RZNIP().NAZWA;
   pit[27]:=VAT_DEK.KRAJZNIP().NAZ;
   pit[28]:=VAT_DEK.KRAJ().NAZ;
   pit[29]:=VAT_DEK.MIASTO;
   pit[30]:=VAT_DEK.KOD;
   pit[31]:=VAT_DEK.ULICA;
   pit[32]:=VAT_DEK.DOM;
   pit[33]:=VAT_DEK.LOKAL;
   {! _nx:=19..22 |! pit[_nx]:=~-pit[_nx] !};
   {! _nx:=24..33 |! pit[_nx]:=~-pit[_nx] !};
   'tablica __iftdan';
   __iftdan.D1:=VAT_DEK.D1;
   __iftdan.D2:=VAT_DEK.D2;
   __iftdan.IMIE:=~-VAT_DEK.IMIE;
   __iftdan.NAZ:=~-VAT_DEK.NAZ;
   __iftdan.DATA:=VAT_DEK.DATA;
   __iftdan.POD_ODP:=~-(VAT_DEK.IMIE+' '+VAT_DEK.NAZ+{? VAT_DEK.STN<>'' || ', '+VAT_DEK.STN || '' ?});
   {? VAT_DEK.KOR='T'
   || __ord[2]:=11+VAT_DEK.ZNIP;
      __ord[9]:=VAT_DEK.NAZWISKO;
      __ord[10]:=VAT_DEK.PIERWSZE;
      __ord[11]:=STR.gsub(VAT_DEK.DATA_UR$4,'.','-');
      __ord[12]:='';
      __ord[13]:=exec('opis_korekty','edeklar');
      {! _nx:=1..obj_len(__ord) |! __ord[_nx]:=~(-__ord[_nx]) !};
      __ord[3]:=pit[2]
   ?};

   __deklar:={? VAT_DEK.TYP='IFT1' || 1 || 0 ?};
~~}
{REP: '{INCLUDE: ppl_ift116g.rpi}'}
{IF: VAT_DEK.KOR='T' & __ord[13]<>''}
{REP: '{INCLUDE: ppl_ord_zu3g.rpi}'}
{FI}
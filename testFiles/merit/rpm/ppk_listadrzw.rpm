:!UTF-8
{TITLE: Lista aktywnych uczestników}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','__TABPAR','koniec','__TABDRZW');

   koniec:=0;
   PAR_WYDR.TITLE:='Lista aktywnych uczestników';
   _d0:=date(0,0,0);
   'Okienko edycji parametrów wydruku:';
   __TABPAR:=tab_tmp(,
      'DTRAP','DATE','Data raportu',
      'DRZW','INTEGER','Deklaracje rezygnacji',
      'WS60','INTEGER','Wnioski o wypłatę po 60 r. życia',
      'DEK','INTEGER','Deklaracje rezygnacji',
      'AKT','INTEGER','Aktywni uczestnicy',
      'WB25','INTEGER','Blokada ekonomiczna'
   );
   _ed:=__TABPAR.mk_edit(PAR_WYDR.TITLE);
   __TABPAR.win_esep(_ed,'Parametry wydruku'@);
   __TABPAR.DTRAP:=date();
   __TABPAR.AKT:=__TABPAR.DEK:=__TABPAR.DRZW:=__TABPAR.WS60:=__TABPAR.WB25:=1;
   __TABPAR.win_efld(_ed,,'DTRAP',,,11,,,'Data raportu'@);
   __TABPAR.win_efld(_ed,,'DRZW',,,,,,'Deklaracje rezygnacji'@,,,'check-box',
                     'check_label="Uwzględniać deklaracje rezygnacji"',"1","0");
   __TABPAR.win_efld(_ed,,'WS60',,,,,,'Wnioski o wypłatę po 60 r. życia'@,,,'check-box',
                     'check_label="Uwzględniać wnioski"',"1","0");
   __TABPAR.win_efld(_ed,,'DEK',,,,,,
            'Deklaracje rezygnacji/wnioski o wypłatę'@,,'Uwzględniane deklaracje/wnioski'@,'radio-buttons',,
            'Wszystkie'@,"0",
            'Złożone w miesiącu raportowania'@,"1",
            'Bez deklaracji/wniosków'@,"2");
   __TABPAR.win_efld(_ed,,'AKT',,,,,,'Aktywni uczestnicy'@,,,'check-box',
                     'check_label="Uczestnicy z aktywną umową"',"1","0");
   __TABPAR.win_efld(_ed,,'WB25',,,,,,'Blokada ekonomiczna'@,,,'check-box',
                     'check_label="Uczestnicy objęci blokadą ekonomiczną"',"1","0");
   __TABPAR.efld_opt(_ed,'mark=1',,'DTRAP');
   exec('ok_esc','#window',__TABPAR,_ed);
   __TABPAR.win_edit(_ed);
   {? __TABPAR.edit("   {? cur_tab().DTRAP=#0
                     || FUN.emsg('Niepoprawna data.'@);
                        'DTRAP'
                     || ''
                     ?}
                    ")
   || __TABPAR.add()
   ?};

   {? __TABPAR.first()
   || __TABDRZW:=exec('list_akt_ucz','ppk_rep',__TABPAR.DTRAP,__TABPAR.DRZW,__TABPAR.WS60,__TABPAR.DEK,__TABPAR.AKT,
                      __TABPAR.WB25);
      {? __TABDRZW.first()
      || PRN:=obj_new(@.CLASS.PRINT,11);
         PRN.add("__TABDRZW.NAZWISKO",MS.fld_len(OSOBA,'NAZWISKO'),'Nazwisko');
         PRN.add("__TABDRZW.PIERWSZE",MS.fld_len(OSOBA,'PIERWSZE'),'Imię');
         PRN.add("__TABDRZW.T",MS.fld_len(P,'T'),'Nr teczki',1);
         PRN.add("__TABDRZW.JEDN",MS.fld_len(UD_SKL,'SYMBOL'),'Jednostka organizacyjna');
         PRN.add("__TABDRZW.CP",MS.fld_len(CP,'CP'),'Charakter pracy');
         PRN.add("__TABDRZW.FZ",10,'Forma współpracy');
         PRN.add("{? __TABDRZW.DT<>date(0,0,0) & __TABDRZW.SYMBOL='DRZW' || __TABDRZW.DT$1 || '' ?}",
                 10,'Data złożenia deklaracji');
         PRN.add("{? __TABDRZW.DTOD<>date(0,0,0) & __TABDRZW.SYMBOL='DRZW' || __TABDRZW.DTOD$1 || '' ?}",
                 10,'Data obowiązywania deklaracji');
         PRN.add("{? __TABDRZW.DT<>date(0,0,0) & __TABDRZW.SYMBOL='WS60' || __TABDRZW.DT$1 || '' ?}",
                 10,'Data złożenia wniosku');
         PRN.add("{? __TABDRZW.DTOD<>date(0,0,0) & __TABDRZW.SYMBOL='WS60' || __TABDRZW.DTOD$1 || '' ?}",
                 10,'Data obowiązywania wniosku');
         PRN.add("__TABDRZW.UWAGI",30,'Uwagi');
         PRN.s_frame();

         prn1:='PRN';
         lmt:=0;
         rsep:=vp:=1
      || koniec:=1;
         FUN.info('Brak uczestników spełniających kryteria.'@);
         return(0)
      ?}
   || koniec:=1
   ?}
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','__TABPAR','koniec','__TABDRZW')
}
{EXIT: koniec}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{COMMENT} Parametry sprawozdania: {COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{<{ceil(lmt)+3}'Parametry sprawozdania'}
{<{ceil(lmt)+3}'Data raportu:'} { __TABPAR.DTRAP }
{<{ceil(lmt)+3}'Deklaracje rezygnacji:'} { {? __TABPAR.DRZW
                                           || 'uwzględniane'
                                           || 'nieuwzględniane'
                                           ?} }
{<{ceil(lmt)+3}'Wnioski o wypłatę po 60 r. życia:'} { {? __TABPAR.WS60
                                                      || 'uwzględniane'
                                                      || 'nieuwzględniane'
                                                      ?} }
{<{ceil(lmt)+3}'Deklaracje rezygnacji/wnioski o wypłatę:'} { {? __TABPAR.DEK=1
                                                             || 'złożone w miesiącu raportowania'
                                                             |? __TABPAR.DEK=2
                                                             || 'brak deklaracji/wniosków'
                                                             || 'wszystkie'
                                                             ?} }
{<{ceil(lmt)+3}'Aktywni uczestnicy:'} { {? __TABPAR.AKT || 'uczestnicy z aktywną umową' || 'wszyscy uczestnicy' ?} }
{<{ceil(lmt)+3}'Blokada ekonomiczna:'} { {? __TABPAR.WB25
                                         || 'uczestnicy objęci blokadą ekonomiczną'
                                         || 'uczestnicy nie objęci blokadą ekonomiczną'
                                         ?} }
{FONT: 'T8B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: __TABDRZW}
{DO}
   { __TABDRZW.cntx_psh();
     _osRefPr:={? __TABDRZW.prev() || __TABDRZW.OSOBAREF || '' ?};
     __TABDRZW.cntx_pop();
     {? __TABDRZW.OSOBAREF=_osRefPr
     || __TABDRZW.NAZWISKO:=__TABDRZW.PIERWSZE:=''
     ?};
     ~~ }
   {FONT: 'T8'}
   {SPACE: 'B'}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
========================================================================================================================
Nazwa pliku: ppk_listadrzw.rpm []
Utworzony: 09.03.2020
========================================================================================================================
Zawartość: Lista uczestników z uwzględnieniem złożonych deklaracji rezygnacji z dokonywania wpłat
{COMMENT-}
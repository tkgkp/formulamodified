:!UTF-8
{TITLE:Struktura organizacyjna}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','poziom','ud_sch','ndx','scfir','tabzakr','koniec');

   koniec:=0;
   tabzakr:=tab_tmp(1,
   'RODS','INTEGER','Rodzaj sprawozdania',
   'AKTY','INTEGER','Aktywne'
   );
   tabzakr.RODS:=tabzakr.AKTY:=1;

   _ed:=tabzakr.mk_edit('Struktura organizacyjna'@,0,'strorg');
   tabzakr.win_esep(_ed,'Parametry wydruku'@);
   {? REF.WFIRM
   || tabzakr.win_efld(
         _ed,,
         'RODS',,,,,,
         'Zakres struktury organizacji'@,,
         'Drukować całą grupę kapitałową, czy tylko obecną firmę?'@,
         'radio-buttons',,
         'Przedsiębiorstwo'@,"1",
         'Firma'@,"2"
      );
      tabzakr.win_efld(_ed,AH,'H',,,,,1,'')
   || tabzakr.RODS:=0
   ?};
   tabzakr.win_efld(
      _ed,,
      'AKTY',,,,,,
      'Zakres aktywności jednostek'@,,
      'Drukować wszystkie jednostki, czy tylko oznaczone jako aktywne?'@,
      'radio-buttons',,
      'Wszystkie'@,"1",
      'Aktywne'@,"0"
   );
   exec('ok_esc','#window',tabzakr,_ed);
   tabzakr.win_edit(_ed);

   {? tabzakr.edit()
   || ndx:=UD_DEF.ndx_tmp(,1,'UD_SCH',,,'SCIEZKA',,,'SYMBOL',,);
      ud_sch:=null();
      UD_TYP.cntx_psh();
      UD_TYP.index('SYMBOL');
      UD_TYP.prefix('PODZORG',);
      {? UD_TYP.first()
      || UD_SCH.cntx_psh();
         UD_SCH.index('SYMBOL');
         UD_SCH.prefix(UD_TYP.ref(),'STRORG',);
         {? UD_SCH.first() || ud_sch:=UD_SCH.ref() ?};
         UD_SCH.cntx_pop()
      ?};
      UD_TYP.cntx_pop();

      scfir:='';
      {? tabzakr.RODS=1
      || _nazgr:='przedsiębiorstwa'@;
         UD_DEF.cntx_psh();
         UD_DEF.index(ndx);
         UD_DEF.prefix(ud_sch);
         {? UD_DEF.first() || _nazgr:=UD_DEF.OPIS ?};
         UD_DEF.cntx_pop()
      || _nazgr:=REF.FIRMA().OPIS;
         _ndx2:=UD_DEF.ndx_tmp(,1,'SYMBOL',,);
         UD_DEF.cntx_psh();
         UD_DEF.index(_ndx2);
         UD_DEF.prefix(REF.FIRMA().SYMBOL);
         {? UD_DEF.first() || scfir:=UD_DEF.SCIEZKA ?};
         UD_DEF.cntx_pop()
      ?};
      PAR_WYDR.TITLE:='Struktura organizacyjna - '@ + _nazgr;
      PRN:=obj_new(@.CLASS.PRINT,3);
      PRN.add("UD_DEF.SYMBOL",16,'Symbol'@);
      PRN.add("UD_DEF.OPIS",60,'Nazwa'@);
      PRN.add("UD_DEF.UD_POZ().OPIS",40,'Opis'@);
      PRN.s_frame();

      prn1:='PRN';
      lmt:=rsep:=0;
      vp:=1;
      poziom:=null()
   || koniec:=1
   ?}
}
{END:
   {? ~koniec || UD_DEF.ndx_drop(ndx) ?};
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','poziom','ud_sch','ndx','scfir','tabzakr','koniec')
}
{EXIT: koniec}
{COMMENT}OLD: sl_komor.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{COMMENT} Parametry sprawozdania: {COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{<{ceil(lmt)+3}'Parametry sprawozdania'@}
{IF: REF.WFIRM}
{<{ceil(lmt)+3}'Zakres struktury organizacji:'@} {IF: tabzakr.RODS=1}{'Przedsiębiorstwo'@}{ELSE}{'Firma'@}{FI}
{FI}
{<{ceil(lmt)+3}'Zakres aktywności jednostek:'@} {IF: tabzakr.AKTY}{'Wszystkie'@}{ELSE}{'Aktywne'@}{FI}
{FONT: 'T8B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: UD_DEF}
{INDEX: ndx}
{PREFIX: ud_sch,scfir}
{DO}
   {FIRST}
      { poziom:=+UD_DEF.SCIEZKA; ~~ }
   {FIRST-}
   {IF: (tabzakr.AKTY | UD_DEF.UD_SKL().AKTYWNY='T') & +UD_DEF.SCIEZKA<>poziom}
      {IF: +UD_DEF.SCIEZKA=9+poziom}
         {FONT: 'T8B'}
      {ELSE}
         {FONT: 'T8'}
      {FI}
      {SUBSTITUTE: 'LINIA0'}
   {FI}
{OD}
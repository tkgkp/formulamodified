:!UTF-8
{TITLE: Historia środka}
{PRINTER: KONST.W_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   H_KOPIA := tab_tmp(5, 'WID','INTEGER','Widoczny',
                'ROK','INTEGER','Rok',
                'POZIOM','INTEGER','Poziom',
                'DATA','STRING[10]','Data',
                'Z','STRING[1]','Likwidacja',
                'RODZAJ','STRING[20]','Rodzaj',
                'DOK','STRING[20]','Symbol dokumentu',
                'OPIS', 'STRING[60]', 'Opis',
                'NR', 'INTEGER', 'Numer zmiany',
                'WIDOK','STRING[24]','Widok',
                'ZWIROZ','INTEGER','Zwinięty/Rozwinięty');
   H_SROD.cntx_psh();
   H_SROD.prefix();
   {? H_SROD.first
   || {! |?
         H_KOPIA.blank();
         H_KOPIA.WID := 1;
         H_KOPIA.ROK := H_SROD.ROK;
         H_KOPIA.POZIOM := H_SROD.POZIOM;
         H_KOPIA.DATA := H_SROD.DATA;
         H_KOPIA.Z := H_SROD.Z;
         H_KOPIA.RODZAJ := H_SROD.RODZAJ;
         H_KOPIA.DOK := H_SROD.DOK;
         H_KOPIA.OPIS := H_SROD.OPIS;
         H_KOPIA.NR := H_SROD.NR;
         H_KOPIA.WIDOK := H_SROD.WIDOK;
         H_KOPIA.ZWIROZ := H_SROD.ZWIROZ;
         H_KOPIA.add();
         H_SROD.next()
      !}
   ?};
   H_SROD.cntx_pop();
   PRN:=obj_new(@.CLASS.PRINT,12); PRN.s_frame();
   PRN2:=obj_new(@.CLASS.PRINT,12); PRN2.s_frame();
   PRN3:=obj_new(@.CLASS.PRINT,12); PRN3.s_frame();
   c:=obj_new(12);
   {! _i:=1..12 |! c[_i] := '' !};
   i:=0;
   color:=prn1:=prn2:=prn3:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   par1:=0; par2:=0; par3:=0; par4:=0;
   sum_wart:=0; licznik:=0; inw_index:=''; inw_prefix:=0
}
{END:
   obj_del(H_KOPIA);
   obj_del(PRN);
   obj_del(PRN2);
   obj_del(PRN3);
   obj_del(c);
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par1; &par2; &par3;&par4;
   &sum_wart; &licznik; &inw_index; &inw_prefix
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: historia.rpm
Utworzony  : 2005-06-13
Zawartość  : Lista środków pominiętych podczas wykonywania operacji
==================================================
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{

      PRN.add("c[1]",10,'Data',,'');
      PRN.add("c[2]",20,'Rodzaj zmiany',,' ');
      PRN.add("c[3]",20,'Symbol dokumentu',,'#');
      PRN.add("c[4]",50,'Opis',,' ');

      PRN2.add("c[2]",33,'',,' ');
      PRN2.add("c[3]",34,'',,' ');
      PRN2.add("c[4]",36,'',,' ');

      PRN3.add("c[8]",33,'',,' ');
      PRN3.add("c[5]",34,'',,' ');
      PRN3.add("c[6]",36,'',,' ');
      SRST.cntx_psh(); 
      SRST.index('SROD');
      SRST.prefix(SRSR.ref(),SSTALE.AR,SSTALE.AO);
      {? SRST.first || _nri:=SRST.NRI; _nst:=SRST.NST || _nri:=SRSR.NRI; _nst:=SRSR.NST ?};
      SRST.cntx_pop();
      PAR_WYDR.TITLE:='Historia środka: ' + _nri + '\n(' + _nst + ')'
;~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{prn1:='PRN';~~}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{FOR: H_KOPIA}
 {DO}
{COMMENT} ---- zwykłe wiersze ---- {COMMENT-}
{
      {? H_KOPIA.DATA = '' & H_KOPIA.RODZAJ = ''
      || _bop := ''; _bof := '';
          SZ.cntx_psh;
         SZ.prefix(H_KOPIA.NR);
         {? SZ.first
         || {! |? {? SZ.A = 'Netto pod. na BO' || _bop := 'Netto pod. na BO: ' + SZ.B ?};
                  {? SZ.A = 'Netto fin. na BO' || _bof := 'Netto fin. na BO: ' + SZ.B ?};
                  SZ.next
            !}
         ?};
         SZ.cntx_pop
      ?};

      c[1] := H_KOPIA.DATA;
      c[2] := {? H_KOPIA.DATA = '' & H_KOPIA.RODZAJ = '' || $H_KOPIA.ROK  || H_KOPIA.RODZAJ ?};
      c[3] := {? H_KOPIA.DATA = '' & H_KOPIA.RODZAJ = '' || _bop          || H_KOPIA.DOK    ?};
      c[4] := {? H_KOPIA.DATA = '' & H_KOPIA.RODZAJ = '' || _bof          || H_KOPIA.OPIS   ?}
;~~}
  {IF: H_KOPIA.DATA = '' & H_KOPIA.RODZAJ = ''}
  {FONT: 'T8GB'}{SPACE: 'C'}
  { {? prn1 = 'PRN3' || prn1 := 'PRN3' || prn1 := 'PRN' ?}; prn2:='PRN2'; ~~}
  {ELSE}
  {FONT: 'T8G'}{SPACE: 'C'}
  { {? prn2 = 'PRN2'
    || prn1 := 'PRN2'
    |? prn2 = 'PRN3'
    || prn1 := 'PRN3'
    || prn1 := 'PRN'
    ?};
    prn2:='PRN'; ~~}
  {FI}
  {IF: lineleft()<>0 & lineleft()<=4}{PAGE}{FI}{SUBSTITUTE: 'LINIA02'}
  {FONT: 'T8G'}{SPACE: 'C'}
    {IF: H_KOPIA.DATA <> '' & H_KOPIA.RODZAJ <> ''}
    {i := 0; ~~}
    {FOR: SZ}{PREFIX:H_KOPIA.NR}
    {DO}
    { {? i = 0 || prn1 := 'PRN' || prn1 := 'PRN3' ?}; prn2:='PRN3'; ~~}
    {new_page := 0;~~}
    { i+=1;
      {? i = 1 || c[8] := 'Informacje szczegółowe:' || c[8] := '' ?};
      c[5] := SZ.A;
      c[6] := SZ.B;
    ~~}
    {IF: lineleft()<>0 & lineleft()<=4}{PAGE}{prn1 := 'PRN';~~}{FI}{SUBSTITUTE: 'LINIA02'}
    { rsep:=0; ~~}
    {OD}
    { rsep:=1; ~~}
  {FI}
  {rsep:=PAR_WYDR.RSEP;~~}
 {OD}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
:!UTF-8
{TITLE:C.  Konta syntetyczne}
{PRINTER: exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:  {? var_pres('PRN')>0 || obj_del(PRN) ?};
         {? var_pres('PRNH')>0 || obj_del(PRNH) ?};
         {? var_pres('PRNS')>0 || obj_del(PRNS) ?};
         PRN:=obj_new(@.CLASS.PRINT,10);  PRN.sl_frame();
         PRNH:=obj_new(@.CLASS.PRINT,5);  PRNH.sl_frame();
         PRNS:=obj_new(@.CLASS.PRINT,10); PRNS.sl_frame();
         ROZNE.win_edit('KS_1'); SA:='A'; ROZNE.BZ:='N';
         KON_WYDR.actions('WER','I:I',':d');
         USERS.actions('SLO','T');USERS.win_dict('SLO');
         KON_WYDR.hdr_sel; KON_WYDR.hdr_sel(' - syntetyczne, użytkownika (%1)'@[@.OPERATOR.USER().KOD]);
         {? ~(var_pres('O_AN')>100) || ROZNE.blank(1); ROZNE.PAR4:=ROZNE.UT_KS:=0; ROZNE.PAR1:=ROZNE.UT_DOK:=ROZNE.UT_ROZR:=1; ROZNE.PAR3:=0; ROZNE.PAR2:=3 ?};
         ROZNE.DATA_OD:=SSTALE.AO().POCZ; ROZNE.DATA_DO:=SSTALE.AO().KON; ROZNE.UT_SCAL:=0; ROZNE.efld_opt(ROZNE.win_edit('?'),'enable=1, editable=1',,'UT_SCAL');
         exec('ini_wal','konto');
         kontotxt:=wal:=odd:=param:=k:=o:=color:=rokr:='';
         prn1:='PRN'; prn2:='PRNH'; lm:=ll:=lmt:=i:=rsep:=kon:=0;vp:=1;
         tuser:=tm_form(TMPVAT.tm_stamp);
         {? var_pres('w')>100 || obj_del(w) ?};
         {? var_pres('ww')>100 || obj_del(ww) ?};
         {? var_pres('wws')>100 || obj_del(wws) ?};
         {? var_pres('wwp')>100 || obj_del(wwp) ?};
         {? var_pres('wss')>100 || obj_del(wss) ?};
         w:=obj_new(10); ww:=obj_new(10); wws:=obj_new(10); wwp:=obj_new(10); wss:=obj_new(4);
         {!_i:=3..10|! ww[_i]:=w[_i]:=wws[_i]:=wwp[_i]:=0!}; wss[1]:=wss[2]:=wss[3]:=wss[4]:=0;
         VAR_DEL.delete('TABW','TABK','TABK_IA','TABK_IS','pref1','tmptabks','tmpsikks'); SSTALE.cntx_psh()
     }
{END: &SA; VAR_DEL.delete('PRN','PRNH','PRNS');
      &prn1; &prn2; &lm; &ll; &lmt; &rsep; &vp; &k; &i; &o; &rokr; &kon;
      &kontotxt; &wal; &odd; &param; &tuser; &color;
      obj_del(w); obj_del(ww); obj_del(wws); obj_del(wwp); obj_del(wss);
      VAR_DEL.delete('TABW','TABK','TABK_IA','TABK_IS','pref1','tmptabks','tmpsikks');
      USERS.actions('SLO',''); SSTALE.cntx_pop()}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: ks_synt.rpm
Utworzony:   ????-??-??
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Wydruk zestawienia obrotow i sald kont syntetycznych
{COMMENT-}
{ wal:='Waluta aktywna:  %1 - %2'@[SSTALE.WAL().TR,SSTALE.WAL().KOD];
{? WYDRUKI.ODD=0
|| odd:='Jednostka księgowa: wszystkie jednostki księgowe'@
|| odd:='Jednostka księgowa:  %1'@[WYDRUKI.ODD().OD]
?};~~}
{EXIT: ~ROZNE.edit(" _a:='';
                     {? ROZNE.PAR4=4
                     ||  _a:=__CHK.record(ROZNE,,'KS_OD','KS_DO');
                        {? _a=''
                        || {? ROZNE.KS_OD().SYM>ROZNE.KS_DO().SYM
                           || FUN.info('Błędny zakres kont.'@); _a:='KS_OD'
                           ?}
                        ?}
                     |? ROZNE.PAR4=5
                     || _a:=__CHK.record(ROZNE,,'KON_WYDR');
                        {? _a=''
                        || KON_SCH.prefix(KON_WYDR.ref);
                           _a:={? KON_SCH.first()
                               || ''
                               || FUN.info('Brak definicji kont w podanym zestawie.'@); 'KON_WYDR'
                               ?}
                        ?}
                     ?};
                     {? _a=''
                     || {? ROZNE.DATA_OD>ROZNE.DATA_DO
                        || FUN.info('Błędny zakres dat.'@); _a:='DATA_OD'
                        ?}
                     ?}; _a")}
{  {? ROZNE.PAR2=1 || kontotxt:='Konta: pozabilansowe'@
   |? ROZNE.PAR2=2 || kontotxt:='Konta: bilansowe'@
   |? ROZNE.PAR2=3 || kontotxt:='Konta: wszystkie'@
   |? ROZNE.PAR2=4 || kontotxt:='Konta: wynikowe'@
   || kontotxt:='Konta: wszystkie bez pozabilansowych'@
   ?};
~~}
{  {? ROZNE.PAR4=2
   || WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=ROZNE.MASKA;
      WYDRUKI.MASKA:=exec('usostsep','konto',WYDRUKI.MASKA);
      {? +WYDRUKI.MASKA < WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?}
   |? ROZNE.PAR4=0
   || WYDRUKI.MASKA:=ROZNE.PREFIX
   || WYDRUKI.MASKA:=''
   ?};~~}
{IF: ROZNE.PAR4=3}
 { _okn:=KS.win_sel('?'); exec('initmpks','dok_fks_zest'); KS.select; KS.win_sel(_okn);~~}
 { tmpsikks:=tab_tmp(,'T2','STRING[80]','Symbol','T3','STRING[80]','Typ');
   TMPSIK.cntx_psh();
   TMPSIK.prefix();
   {? TMPSIK.first()
   || {! |?
         tmpsikks.T2:=TMPSIK.T2;
         tmpsikks.T3:=TMPSIK.T3;
         tmpsikks.add();
         TMPSIK.next()
      !}
   ?};
   TMPSIK.cntx_pop()
  ;~~}
 {EXIT: ~TMPSIK.first()}
{ELIF: ROZNE.PAR4=6}
 { _okn:=AN.win_sel('?'); exec('initmpan','dok_fks_zest');
   _r:=ROZNE.SZUK; ROZNE.SZUK:=1;
   AN.select();
   ROZNE.SZUK:=_r; AN.win_sel(_okn);~~}
 {EXIT: ~TMPSIK.first()}
{FI}
{OKRO_F.index('ROK');
 OKRO_F.prefix(SSTALE.AR);
 _ilok:=0;
 _zn:='';
 _zmz:=0;
 {? OKRO_F.first()
 || {!
    |? {? OKRO_F.POCZ<>date(0,0,0)
          & ROZNE.DATA_OD<=OKRO_F.KON & ROZNE.DATA_DO>=OKRO_F.POCZ
       || _ilok+=1;
          {? _zn= ''
          || _zn:=OKRO_F.ZAM
          || {? _zn<>OKRO_F.ZAM || _zmz:=1 ?}
          ?}
       ?};
       ~_zmz & OKRO_F.next()
    !};
    {? ~_zmz || rokr:=_zn ?}
 ?};~~}
{ TABW:=proc_exec('ks_anal@ks_anal',SSTALE.AO().ROK().KOD,SSTALE.AO().ROK().NAZ,SSTALE.WAL().KOD,ROZNE.UT_SCAL,WYDRUKI.ODD().OD,
                                   {? ROZNE.UT_KS || 'T' || 'N' ?}, {? ROZNE.UT_ROZR||'S'||'O'?},ROZNE.PAR2,
                                   ROZNE.PAR4,ROZNE.PREFIX, ROZNE.MASKA,ROZNE.KS_OD().SYM,
                                   ROZNE.KS_DO().SYM,ROZNE.KON_WYDR().KOD,{?ROZNE.UT_DOK||'T'||'N'?},
                                   BILANSP.ASEP,BILANSP.ASYNT,rokr,#ROZNE.KON_WYDR,tuser,ROZNE.DATA_OD,ROZNE.DATA_DO,ROZNE.PAR1,ROZNE.PAR3,ROZNE.BZ,
                                   form(exec('kurs','konto',UNPAR.PSLO().KOD,UNPAR.P25),,,'9.'),0);
~~}
{EXIT: var_pres('TABW')<=0}
{ kl:=18;
  PRNH.add("",35+1+40,'',,,,,,,,2);
{? ROZNE.UT_ROZR
   ||  PRNH.add("",2*kl+1,'Bilans otwarcia - saldo'@,,,,,,,,2)
   ||  PRNH.add("",2*kl+1,'Bilans otwarcia - obroty'@,,,,,,,,2)
?};
  PRNH.add("",2*kl+1,'       Okres sprawozdawczy'@,,,,,,,,2);
  PRNH.add("",2*kl+1,'           Narastająco'@,,,,,,,,2);
  PRNH.add("",2*kl+1,'              Saldo'@,,,,,,,,2);
  PRN.add("w[1]",7,' '+'Konto'@);
  PRN.add("w[2]",68,12*' '+'Nazwa konta'@);
  PRN.add("form(w[3],kl,2,' ,')", kl,(kl-2)*' '+'Wn'@,1);
  PRN.add("form(w[4],kl,2,' ,')", kl,(kl-2)*' '+'Ma'@,1);
  PRN.add("form(w[5],kl,2,' ,')", kl,(kl-2)*' '+'Wn'@,1);
  PRN.add("form(w[6],kl,2,' ,')", kl,(kl-2)*' '+'Ma'@,1);
  PRN.add("form(w[7],kl,2,' ,')", kl,(kl-2)*' '+'Wn'@,1);
  PRN.add("form(w[8],kl,2,' ,')", kl,(kl-2)*' '+'Ma'@,1);
  PRN.add("form(w[9],kl,2,' ,')", kl,(kl-2)*' '+'Wn'@,1);
  PRN.add("form(w[10],kl,2,' ,')", kl,(kl-2)*' '+'Ma'@,1);
  PRNS.add("ww[1]",35+1+40, '',,,,,,,,2);
  PRNS.add("form(ww[3],kl,2,' ,')", kl,'',1);
  PRNS.add("form(ww[4],kl,2,' ,')", kl,'',1);
  PRNS.add("form(ww[5],kl,2,' ,')", kl,'',1);
  PRNS.add("form(ww[6],kl,2,' ,')", kl,'',1);
  PRNS.add("form(ww[7],kl,2,' ,')", kl,'',1);
  PRNS.add("form(ww[8],kl,2,' ,')", kl,'',1);
  PRNS.add("form(ww[9],kl,2,' ,')", kl,'',1);
  PRNS.add("form(ww[10],kl,2,' ,')", kl,'',1);
  PAR_WYDR.TITLE:='ZESTAWIENIE OBROTÓW I SALD KONT SYNTETYCZNYCH \nZA OKRES od %1 do %2'@[$ROZNE.DATA_OD,$ROZNE.DATA_DO];~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}  --------------------------------  T A B H E A D 0  ---------------------------------------  {COMMENT-}
{MACRO: 'TABHEAD0'}
{  PRNH.setcolor(color);
   PRNH.ini_head(); ~~}
{REP: PRNH.fhbar()}
{WHILE: PRNH.h_next()}
{DO}
   {REP: PRNH.h_fmline()}
{OD}
{REP: PRNH.fjbar(PRN)}
{ _f:='PRN'+'.setcolor(color);'+'PRN'+'.ini_head()'; ($(_f))();~~}
{WHILE: _f:='PRN'+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:='PRN'+'.h_fmline()'; ($(_f))()}
{OD}
{IF: kon}
{REP: _f:='PRN'+'.flbar(lmt)'; kon:=0; ($(_f))()}
{FI}
{MACRO-}
{COMMENT}  -----------------------------------   HEADER  -------------------------------------------   {COMMENT-}
{HEADER}
{LINE: 1}

{FONT: 'HEAD'}{SPACE: 'B'}{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}

{IF: page=1}
{ param:={? ROZNE.PAR4=2 & (_a:=STR.gsub(ROZNE.MASKA,'?','');+_a) || 'Dla kont o masce: '+ROZNE.MASKA
         |? ROZNE.PAR4=0 & +ROZNE.PREFIX || 'Wydruk dla kont o prefiksie: '+ROZNE.PREFIX
         |? ROZNE.PAR4=3  || 'Wydruk wybranych kont syntetycznych'@
         |? ROZNE.PAR4=4  || 'Wydruk dla zakresu kont od: '+ROZNE.KS_OD().SYM+' do: '+ROZNE.KS_DO().SYM
         || 'Wydruk dla wszystkich kont'
         ?};~~}
{IF: rep_export()}
{'Parametry wydruku:'}
{'Obroty: '+{? ROZNE.PAR1 || 'narastająco z bilansem otwarcia' || 'narastająco bez bilansu otwarcia' ?}}
{wal}
{'Bilans otwarcia - liczone saldo: '+{? ROZNE.UT_ROZR||'tak'||'nie'?}}
{'Drukowanie pozycji z zerowymi wartościami: '@+{? ROZNE.UT_DOK || 'tak'@ || 'nie'@ ?}}
{{? UNPAR.PSLO || 'Waluta wydruku:  %1 - %2'@[UNPAR.PSLO().TR,SLO.KOD] || 'Waluta wydruku:  %1 - %2'@[SSTALE.WAL().TR,SLO.KOD] ?}}
{{? UNPAR.P25<>1 | (UNPAR.PSLO<>null & UNPAR.PSLO<>SSTALE.WAL) || 'Kurs: '@+$UNPAR.P25 || '' ?}}
{param}
{odd}
{{? rokr<>''|| {? rokr='T' || 'Rodzaj okresu: zamknięty' || 'Rodzaj okresu: otwarty' ?} || '' ?}}
{{? ROZNE.PAR3 || 'Konta aktywno-pasywne drukować z podwójnymi saldami: tak' || 'Konta aktywno-pasywne drukować z podwójnymi saldami: nie' ?}}
{kontotxt}
{{? ROZNE.BZ='N' || 'Bilans zamknięcia nie uwzględniony'@ || 'Bilans zamknięcia uwzględniony'@ ?}}
{ELSE}
{<3 'Parametry wydruku:'}
{<6 'Obroty: '+{? ROZNE.PAR1 || 'narastająco z bilansem otwarcia' || 'narastająco bez bilansu otwarcia' ?}}{<76 wal}{<136 'Bilans otwarcia - liczone saldo: '+{? ROZNE.UT_ROZR||'tak'||'nie'?}}
{<6 param}{<76 odd}{<136 {? rokr<>''|| {? rokr='T' || 'Rodzaj okresu: zamknięty' || 'Rodzaj okresu: otwarty' ?} || '' ?}}
{<6 {? ROZNE.PAR3 || 'Konta aktywno-pasywne drukować z podwójnymi saldami: tak' || 'Konta aktywno-pasywne drukować z podwójnymi saldami: nie' ?}
}{<76 kontotxt}{<136 {? ROZNE.BZ='N' || 'Bilans zamknięcia nie uwzględniony'@ || 'Bilans zamknięcia uwzględniony'@ ?}}
{<6 'Drukowanie pozycji z zerowymi wartościami: '@+{? ROZNE.UT_DOK || 'tak'@ || 'nie'@ ?}
}{<76 {? UNPAR.PSLO || 'Waluta wydruku:  %1 - %2'@[UNPAR.PSLO().TR,SLO.KOD] || 'Waluta wydruku:  %1 - %2'@[SSTALE.WAL().TR,SLO.KOD] ?}
}{<136 {? UNPAR.P25<>1 | (UNPAR.PSLO<>null & UNPAR.PSLO<>SSTALE.WAL) || 'Kurs: '@+$UNPAR.P25 || '' ?}}
{FI}
{FI}
{LINE: 1}{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD0'}{vp:=1;~~}
{HEADER-}
{COMMENT}   -----------------------------------   FOOTER  -------------------------------------------  {COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{FOOTER:0}
{FONT:'T8G'}{SPACE:'C'}{PRN.jbar(PRNS)}
{ww[1]:=' SUMA STRONY'@;{! _i:=3..10|! ww[_i]:=wws[_i] !};prn1:='PRNS';~~}{SUBSTITUTE: 'LINIA0'}
{IF: page>1}{ ww[1]:=' Z PRZENIESIENIA'@;{! _i:=3..10|! ww[_i]:=wwp[_i]-wws[_i] !};~~}{SUBSTITUTE: 'LINIA0'}{FI}
{ww[1]:=' DO PRZENIESIENIA'@;{! _i:=3..10|! ww[_i]:=wwp[_i]; wws[_i]:=0 !};~~}{SUBSTITUTE: 'LINIA0'}
{vp:=1;prn1:='PRN';~~}{REP: PRNS.lbar()}
{FOOTER-}
{COMMENT}  -----------------------------------   WYDRUK  -------------------------------------------   {COMMENT-}
{IF: TABW.first()}
{FONT:'T8G'}
{IF: ROZNE.UT_DOK<>0}
{ {? ROZNE.PAR4=2
   || WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=ROZNE.MASKA;
       WYDRUKI.MASKA:=exec('usostsep','konto',WYDRUKI.MASKA);
     {? +WYDRUKI.MASKA<WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?}
   |? ROZNE.PAR4=0
   || WYDRUKI.MASKA:=ROZNE.PREFIX
   || WYDRUKI.MASKA:=''
   ?};~~}
{ _synt:=SSTALE.AR().SYNT; tmptabks:=tab_tmp(,'SYM','STRING[%1]'[$_synt],'Symbol'); ~~}
{IF: ROZNE.PAR4<>0}{ pref1:='';~~}{ELSE}{ pref1:=SSTALE.AR().SYNT+WYDRUKI.MASKA; ~~}{FI}
{IF: ROZNE.PAR4=0 | ROZNE.PAR4=2}
{FOR: KS}
{INDEX: 'SYM'}
{PREFIX: SSTALE.AO().ROK,pref1}
{DO}
{IF: (ROZNE.PAR2=1 & -(1+KS.TYP)='p') | (ROZNE.PAR2=2 & -(2+KS.TYP)='bl') | (ROZNE.PAR2=3) | (ROZNE.PAR2=4 & -KS.TYP='bw')
      | (ROZNE.PAR2=5 & -(1+KS.TYP)='b') }
{ {? (ROZNE.PAR4=2 & exec('mask','konto',KS.SYM)) | ROZNE.PAR4<>2
  || tmptabks.SYM:=KS.SYM; tmptabks.add()
  ?}; ~~}
{FI}
{OD}
{ELIF: ROZNE.PAR4=3 & var_pres('tmpsikks')>0}
{ tmpsikks.prefix();
  {? tmpsikks.first()
  || {! |?
        {? (ROZNE.PAR2=1 & -(1+tmpsikks.T3)='p') | (ROZNE.PAR2=2 & -(2+tmpsikks.T3)='bl') | (ROZNE.PAR2=3)
            | (ROZNE.PAR2=4 & -tmpsikks.T3='bw') | (ROZNE.PAR2=5 & -(1+tmpsikks.T3)='b')
        || tmptabks.SYM:=tmpsikks.T2; tmptabks.add()
        ?};
        tmpsikks.next()
     !}
  ?}; ~~ }
{ELIF: ROZNE.PAR4=4}
{FOR: 'KS'}
{INDEX: 'SYM'}
{FROM: SSTALE.AO().ROK,ROZNE.KS_OD().SYM}
{TO: SSTALE.AO().ROK,ROZNE.KS_DO().SYM}
{DO}
{IF: (ROZNE.PAR2=1 & -(1+KS.TYP)='p') | (ROZNE.PAR2=2 & -(2+KS.TYP)='bl') | (ROZNE.PAR2=3) | (ROZNE.PAR2=4 & -KS.TYP='bw')
      | (ROZNE.PAR2=5 & -(1+KS.TYP)='b') }
{tmptabks.SYM:=KS.SYM; tmptabks.add(); ~~}
{FI}
{OD}
{FI}
{  tmptabks.prefix();
   {? tmptabks.first()
   || _ndx:=TABW.ndx_tmp(,1,'KONTO',,); TABW.index(_ndx); TABW.prefix();
      {! |?
         {? ~TABW.find_key(tmptabks.SYM+'Ż') & ~TABW.find_key(tmptabks.SYM,)
         || TABW.prefix();
            TABW.blank();
            TABW.REK:='S';
            TABW.KS:=tmptabks.SYM;
            TABW.KSSALDO:='A';
            TABW.SCAL:=0;
            TABW.KONTO:=tmptabks.SYM+'Ż';
            TABW.POZ:=0;
            TABW.OPIS:='';
            TABW.BOWN:=TABW.BOMA:=TABW.OBMA:=TABW.OBWN:=TABW.ONMA:=TABW.ONWN:=TABW.SAMA:=TABW.SAWN:=0;
            TABW.add()
         ?};
         tmptabks.next()
      !}
   ?};~~ }
{FI}
{FOR: TABW}
{INDEX: TABW.ndx_tmp('',0,'KONTO',,0,'REK',,0)}
{DO}
{FIRST}{k:=TABW.KS;r:=TABW.REK;~~}{FIRST-}
{IF: TABW.REK='S' | (TABW.KS=TABW.KONTO & TABW.REK='P')}
{ w[1]:={? TABW.REK='S' || (TABW.KONTO-1) || TABW.KONTO ?};
  KS.cntx_psh();
  KS.index('SYM');
  KS.prefix(SSTALE.AR,w[1]);
  {? KS.first()
  || w[2]:=KS.NAZ
  || w[2]:=''
  ?};
  KS.cntx_pop();
  w[5]:=TABW.OBWN; w[6]:=TABW.OBMA; w[7]:=TABW.ONWN; w[8]:=TABW.ONMA;
  {? ROZNE.PAR3 & (TABW.REK='S' | TABW.REK='P') & ((TABW.BOWN<>0 & TABW.BOMA<>0) | (TABW.SAWN<>0 & TABW.SAMA<>0)) & TABW.KSSALDO='D'
  || w[3]:=TABW.BOWN; w[4]:=TABW.BOMA;
     w[9]:=TABW.SAWN; w[10]:=TABW.SAMA
  || {? ROZNE.UT_ROZR
     || w[3]:=F.SWn(TABW.BOWN,TABW.BOMA); w[4]:=F.SMa(TABW.BOWN,TABW.BOMA)
     || w[3]:=TABW.BOWN; w[4]:=TABW.BOMA
     ?};
     w[9]:=F.SWn(TABW.SAWN,TABW.SAMA); w[10]:=F.SMa(TABW.SAWN,TABW.SAMA)
  ?}; ~~}
{ PRN.ini_line();~~}
{IF: lineleft()<>0 & lineleft()<PRN.length()}{PAGE}{FI}
{i+=1;{!_i:=3..10|! wwp[_i]+=w[_i]; wws[_i]+=w[_i] !};k:=TABW.KS;~~}
{IF: (TABW.REK='S' & TABW.KS=(TABW.KONTO-1)) | (TABW.REK='P' & TABW.KS=TABW.KONTO)}
 {{? ROZNE.PAR3 & TABW.KSSALDO='D'
  || wss[1]+=TABW.BOWN; wss[2]+=TABW.BOMA;
     wss[3]+=TABW.SAWN; wss[4]+=TABW.SAMA
  || {? ROZNE.UT_ROZR
     || wss[1]+=F.SWn(TABW.BOWN,TABW.BOMA); wss[2]+=F.SMa(TABW.BOWN,TABW.BOMA)
     || wss[1]+=TABW.BOWN; wss[2]+=TABW.BOMA
     ?};
     wss[3]+=F.SWn(TABW.SAWN,TABW.SAMA); wss[4]+=F.SMa(TABW.SAWN,TABW.SAMA)
  ?};~~}
{FI}
{SPACE:'C'}{SUBSTITUTE: 'LINIA0'}
{IF: TABW.REK='S' & {? r='O' || TABW.REK<>'O' || 1 ?}}{FI}{FONT:'T8G'}{SPACE:'C'}
{r:=TABW.REK;~~}
{FI}
{OD}
{COMMENT}   -----------------------------------   FOOTER  -------------------------------------------  {COMMENT-}
{FOOTER:0}
{REP: PRNS.lbar()}
{FOOTER-}
{FONT:'T8G'}{SPACE:'C'}
{prn2:='PRNS'; rsep:=1;~~}
{IF: ~rep_export()}
{ ww[1]:=' SUMA STRONY';{! _i:=3..10|! ww[_i]:=wws[_i] !};~~}
{SUBSTITUTE: 'LINIA02'}
{FI}
{rsep:=0; prn1:='PRNS';~~}
{IF: page>1}
{ ww[1]:=' Z PRZENIESIENIA'@;{! _i:=3..10|! ww[_i]:=wwp[_i]-wws[_i] !};~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{IF: ~rep_export()}
{ ww[1]:=' RAZEM'@;{! _i:=5..8|! ww[_i]:=wwp[_i] !}; ww[3]:=wss[1]; ww[4]:=wss[2]; ww[9]:=wss[3]; ww[10]:=wss[4];~~}
{SUBSTITUTE: 'LINIA0'}
{ ww[1]:=' PER SALDO'@;
                      ww[3]:=F.SWn(wss[1],wss[2]); ww[4]:=F.SMa(wss[1],wss[2]);
                      ww[5]:=F.SWn(wwp[5],wwp[6]); ww[6]:=F.SMa(wwp[5],wwp[6]);
                      ww[7]:=F.SWn(wwp[7],wwp[8]); ww[8]:=F.SMa(wwp[7],wwp[8]);
                      ww[9]:=F.SWn(wss[3],wss[4]); ww[10]:=F.SMa(wss[3],wss[4]);~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{FOOTER:0}
{FOOTER-}
{REP: PRNS.flbar(lmt)}
{kon:=1;~~}
{ENTIRE}
{IF: ~rep_export()}

{<{lmt}>{PRN.width()+lmt} 'KONIEC WYDRUKU'@}{FI}
{ENTIRE-}
{ELSE}
{FOOTER:0}
{FOOTER-}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
{ _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{IF: ~rep_export()}
{<{lmt}>{PRN.width()+lmt} 'KONIEC WYDRUKU'@}
{FI}
{FI}
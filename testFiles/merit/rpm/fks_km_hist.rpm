:!UTF-8
{TITLE:B. Zestawienie zbiorcze kompensat}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0}
{BEGIN: w:=obj_new(20); {! _i:=1..15 |! w[_i]:='' !}; petla:=0;
        PRN:=obj_new(@.CLASS.PRINT,15); PRN.sl_frame();
        PRN2:=obj_new(@.CLASS.PRINT,4);  PRN2.sl_frame();
        ROZNE.STAT_KOM:=0;
        {? ROZRACH.KONTR=null
        || ROZNE.win_edit('KOMP_HIS');
           ROZNE.KOMP_ZAK:=0; ROZNE.KOMP_KH:=null
        || ROZNE.win_edit('KOMP_HI2');
           ROZNE.KOMP_ZAK:=1; ROZNE.KOMP_KH:=ROZRACH.KONTR
        ?};
        ROZNE.KOMP_DOD:=ROZNE.KOMP_DDO:=date(0,0,0); ROZNE.KOMP_OKR:=0;
        ROZNE.UMOWA:=null; KH.win_dict('WERHOME2'); pier_poz:=ilelinii:=0;
        sum_1:=sum_2:=sum_3:=sum_4:=sum_5:=sum_6:=lm:=0; mdc:=''
}
{END: VAR_DEL.delete('w','PRN','PRN2','petla','pier_poz','ilelinii');
      VAR_DEL.delete('sum_1','sum_2','sum_3','sum_4','sum_5','sum_6','mdc','lm')}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: km_hist.rpm
Utworzony:   2001-12-14 [AMK] Artur Makos
==================================================
Zawartosc:  Zestawienie zbiorcze kompensat
{COMMENT-}
{EXIT: ~ROZNE.edit("{? ROZRACH.KONTR=null & ROZNE.KOMP_ZAK=1 & ROZNE.KOMP_KH=null
                    || FUN.info('Proszę wypełnić pole kontrahent.'@); 'KOMP_KH'
                    || {? ROZNE.KOMP_OKR & ROZNE.KOMP_DDO<ROZNE.KOMP_DOD
                       || FUN.info('Wpisz poprawnie zakres dat.'@); 'KOMP_DOD'
                       || 1
                       ?}
                    ?}")}
{ PRN.add("w[1]",20,'    Symbol umowy'@);
  PRN.add("w[2]",10,'Data planu'@);
  {? ROZNE.KOMP_ZAK<>1 || PRN.add("w[3]",38,10*' '+'Kontrahent'@)   ?};
  {? FINFO.NAROD<>SSTALE.WAL || PRN.add("w[13]",10,'Kurs wal.'@) ?};
  PRN.add("w[5]",20,' Symbol rozrachunku#   kompensowanego'@,,'#');
  PRN.add("w[6]",10,'T. płatn.');
  PRN.add("w[7]",14,'%1Winien'@[4*' '],1);  PRN.add("w[8]",14,'%1Ma'@[6*' '],1);
  PRN.add("w[9]",14,'%1Winien'@[4*' '],1);  PRN.add("w[10]",14,'%1Ma'@[6*' '],1);
  PRN.add("w[11]",14,'%1Winien'@[4*' '],1);  PRN.add("w[12]",14,'%1Ma'@[6*' '],1);
  PRN2.add("",{? ROZNE.KOMP_ZAK<>1 || 102 || 63 ?}+{? FINFO.NAROD<>SSTALE.WAL || 11 || 0 ?},'');
  PRN2.add("",29,'%1Saldo przed kompensatą'@[3*' ']);
  PRN2.add("",29,'%1Kwota kompensaty'@[6*' ']);
  PRN2.add("",29,'%1Saldo po kompensacie'@[4*' ']);
    ~~}
{INCLUDE: wsp_pw.rpi}
{INCLUDE: wsp_pw3.rpi}
{INCLUDE: wsp_lic.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA1------------------{COMMENT-}
{MACRO: 'LINIA1'}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{<1 PRN1.line()}
{OD}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ZESTAWIENIE ZBIORCZE KOMPENSAT ROZRACHUNKÓW'@;~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Rok obrotowy: %1'@[SSTALE.AR().NAZ]
}
{<60{? ~ROZNE.KOMP_ZAK
    ||  'Zakres kompensat: dla wszystkich kontrahentów'@
    ||  'Zakres kompensat: dla kontrahenta - %1  %2'@[ROZNE.KOMP_KH().KOD,ROZNE.KOMP_KH().NAZ]
    ?}
}
{<5
{? OPERATOR.DEPT
   ||  'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
   ||  'Jednostka księgowa: wszystkie jednostki księgowe'@
?}
}
{IF: ROZNE.UMOWA<>null}{<60'Wybrana umowa: %1'@[ROZNE.UMOWA().SYM]}{FI}
{<5 'Waluta: %1  %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]
}{<60{? ROZNE.STAT_KOM=0
    ||  'Status kompensat: wszystkie'@
    |? ROZNE.STAT_KOM=1
    ||  'Status kompensat: niezaakceptowane'@
    |? ROZNE.STAT_KOM=2
    ||  'Status kompensat: zaakceptowane'@
    ||  'Status kompensat: zadekretowane'@
 ?}
 }
{IF: ROZNE.KOMP_OKR}{<60 'Przedział dat: od %1  do %2'@[$ROZNE.KOMP_DOD,$ROZNE.KOMP_DDO]}{FI}
{FI}{FONT: 'R'}{SPACE: 'C'}
{PRN2.hbar()}
{PRN2.ini_head()}
{WHILE: PRN2.h_next()}
{DO}
{PRN2.h_line()}
{OD}
{PRN2.jbar(PRN)}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{IF: _a:=PAR_SKID.get(34); _a='Z' | _a='A'}
{FOOTER}
{SUBSTITUTE: 'FOOT_LIC'}
{~~}
{~~}
{~~}
{FOOTER-}
{FI}
{COMMENT}-----------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ {? ROZNE.STAT_KOM=0
  || {? OPERATOR.DEPT
     || PAR_NAG.index('KONTO_O');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,'K',OPERATOR.DEPT,'')
        || PAR_NAG.prefix(SSTALE.WAL,'K',OPERATOR.DEPT,'',ROZNE.KOMP_KH().KOD)
        ?}
     || PAR_NAG.index('KONTO');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,'K','')
        || PAR_NAG.prefix(SSTALE.WAL,'K','',ROZNE.KOMP_KH().KOD)
        ?}
     ?}
  |? ROZNE.STAT_KOM=1 | ROZNE.STAT_KOM=2
  || _stat:={? ROZNE.STAT_KOM=1 || 'N' || 'T' ?};
     {? OPERATOR.DEPT<>null
     || PAR_NAG.index('ZNACZ_O3');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,'K',OPERATOR.DEPT,_stat,'')
        || PAR_NAG.prefix(SSTALE.WAL,'K',OPERATOR.DEPT,_stat,'',ROZNE.KOMP_KH().KOD)
        ?}
     || PAR_NAG.index('ZNACZ3');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,'K',_stat,'')
        || PAR_NAG.prefix(SSTALE.WAL,'K',_stat,'',ROZNE.KOMP_KH().KOD)
        ?}
     ?}
  || {? OPERATOR.DEPT<>null
     || PAR_NAG.index('ZNACZ_O1');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,'K',OPERATOR.DEPT,'T','T','T','')
        || PAR_NAG.prefix(SSTALE.WAL,'K',OPERATOR.DEPT,'T','T','T','',ROZNE.KOMP_KH().KOD)
        ?}
     || PAR_NAG.index('ZNACZ1');
        {? ROZNE.KOMP_KH=null
        || PAR_NAG.prefix(SSTALE.WAL,'K','T','T','T','')
        || PAR_NAG.prefix(SSTALE.WAL,'K','T','T','T','',ROZNE.KOMP_KH().KOD)
        ?}
     ?}
  ?};
  petla:=PAR_NAG.first();~~}
{WHILE: petla}
{DO}
{IF: (~ROZNE.KOMP_OKR |
       (ROZNE.KOMP_OKR & PAR_NAG.DATA>=ROZNE.KOMP_DOD & PAR_NAG.DATA<=ROZNE.KOMP_DDO)) &
     (ROZNE.UMOWA=null | PAR_NAG.PAR_UM=ROZNE.UMOWA)}
{FOR: 'PAR_POZ'}
{INDEX: 'PAR_POZ'}
{PREFIX: PAR_NAG.ref}
{DO}
{FIRST}
{ {! _i:=1..15 |! w[_i]:='' !}; pier_poz:=1;
  sum_1:=sum_2:=sum_3:=sum_4:=sum_5:=sum_6:=0;
  w[1]:=PAR_NAG.PAR_UM().SYM;  w[2]:=$PAR_NAG.DATA;
  w[3]:=PAR_NAG.KH1().SKR+'  '+KH.NAZ; ile_linii:=exec('ilelinii','rozrach',w[3],38);
  w[4]:=PAR_NAG.SER_DEF().KOD;  w[13]:=form(PAR_NAG.KURS,10,6,' ,');
~~}
{FIRST-}
{ w[5]:=PAR_POZ.OP;  w[6]:=$PAR_POZ.TZ;
  w[7]:={? -PAR_POZ.STR='wn'  || sum_1+=PAR_POZ.SAL$2;   form(PAR_POZ.SAL,,2,' ,')   || form(0,,2,' ,') ?};
  w[8]:={? -PAR_POZ.STR='ma'  || sum_2+=PAR_POZ.SAL$2;   form(PAR_POZ.SAL,,2,' ,')   || form(0,,2,' ,') ?};
  w[9]:={? -PAR_POZ.STR='ma'  || sum_3+=PAR_POZ.KWOTA$2; form(PAR_POZ.KWOTA,,2,' ,') || form(0,,2,' ,') ?};
  w[10]:={? -PAR_POZ.STR='wn' || sum_4+=PAR_POZ.KWOTA$2; form(PAR_POZ.KWOTA,,2,' ,') || form(0,,2,' ,') ?};
  w[11]:={? -PAR_POZ.STR='wn'
         || sum_5+=(PAR_POZ.SAL-PAR_POZ.KWOTA)$2; form((PAR_POZ.SAL-PAR_POZ.KWOTA),,2,' ,')
         || form(0,,2,' ,')
         ?};
  w[12]:={? -PAR_POZ.STR='ma'
         || sum_6+=(PAR_POZ.SAL-PAR_POZ.KWOTA)$2; form((PAR_POZ.SAL-PAR_POZ.KWOTA),,2,' ,')
         || form(0,,2,' ,')
         ?};
~~}
{IF: ((~pier_poz | ROZNE.KOMP_ZAK=1 | ilelinii=1) & (lineleft()=1 | lineleft()=2)) |
     (pier_poz=1 & lineleft<>0 & (lineleft()<(2+ile_linii)))}
{PRN.lbar()}{IF: lineleft()<>0}{PAGE}{FI}
{FI}
{ENTIRE}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{ {! _i:=1..15 |! w[_i]:='' !}; pier_poz:=0;~~}
{OD}
{ w[1]:='RAZEM UMOWA'@;
  w[7]:=form(sum_1,,2,' ,');  w[8]:=form(sum_2,,2,' ,');  w[9]:=form(sum_3,,2,' ,');
  w[10]:=form(sum_4,,2,' ,'); w[11]:=form(sum_5,,2,' ,'); w[12]:=form(sum_6,,2,' ,');~~}
{IF: (lineleft()=1 | lineleft()=2)}{PRN.lbar()}{IF: lineleft()<>0}{PAGE}{FI}{FI}
{ENTIRE}
{PRN.bar()}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}
{ {! _i:=1..15 |! w[_i]:='' !};~~}
{FI}
{ petla:=PAR_NAG.next();~~}
{OD}
{PRN.lbar()}
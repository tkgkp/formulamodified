:!UTF-8
{TITLE:I. Rozliczenie surowców}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  opcja:=2;
  wyd:=$ST.A_WYD;
  OKR.cntx_psh();
  OKR.index('OKR');
  OKR.prefix(REF.FIRMA);
  {? OKR.find_key(ST.AR,ST.AM)
  || okr:=$OKR.ref()
  || okr:='null'
  ?};
  OKR.cntx_pop();
  VAR_DEL.delete('PRN');
  PRN:= obj_new(@.CLASS.PRINT, 14);
  PRN.s_frame();
  tryb:=rep_export();
  lp:=0;
  tekst:='Do przeniesienia'@;
  pods:=0;
  pobwar:=zwwar:=0;
  prn1:='PRN';
  waslbar:=0;
  dalej:=0;
  color:='';
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  {? opcja<>2 || obj_del(TABx) ?};
  &opcja;
  &wyd;
  &okr;
  &pods;
  &pobwar;
  &zwwar;
  &tekst;
  VAR_DEL.delete('PRN');
  &dalej;
  &prn1;
  &lp;
  &color;
  &lm; &ll; &vp; &lmt; &rsep; &tryb
}
{COMMENT}
  Wydruk rozliczenia surowcow
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{COMMENT}--Definicja wiersza wydruku rozliczenia surowcow--------------------{COMMENT-}
{ opcja:=choice('Wybierz opcję wydruku - agregowanie wg:'@,FUN.TYT,'ASK',,0,,
                'Zleceń'@,'Surowców'@,'Rezygnacja'@); ~~}
{IF: opcja=2}
{EXIT}
{ELIF: opcja=0}
{ TABx:=proc_exec('zlec_009@zlecenia', wyd, okr, 'Z');
  PRN.add("{? pods=0 || form(lp) || '' ?}",4,'L.p.'@,1);
  PRN.add("{? pods=0 || TABx.ZL || '' ?}",20,'Zlecenie'@);
  PRN.add("{? pods=0 || TABx.O || tekst ?}",40,'Opis'@);
  PRN.add("{? pods=0 || form(TABx.WAR_P,,2) || form(pobwar,,2) ?}",12,'Pobrania - wartość'@,1);
  PRN.add("{? pods=0 || form(TABx.WAR_Z,,2) || form(zwwar,,2) ?}",12,'Zwroty - wartość'@,1);
  PRN.add("{? pods=0 || form(TABx.WAR_P-TABx.WAR_Z,,2) || form(pobwar-zwwar,,2) ?}",12,'Pobrania - Zwroty wartość'@,1);  ~~}
{ELSE}
{ TABx:=proc_exec('zlec_009@zlecenia', wyd, okr, 'S');
  PRN.add("{? pods=0 || form(lp)|| '' ?}",4,'L.p.'@,1);
  PRN.add("{? pods=0 || TABx.KTM || '' ?}",20,'Kod surowca'@);
  PRN.add("{? pods=0 || TABx.NAZ || tekst ?}",40,'Nazwa surowca'@);
  PRN.add("{? pods=0 || TABx.JM || '' ?}",4,'J.m.'@);
  PRN.add("{? pods=0 || form(TABx.IL_P,,4) || '' ?}",12,'Pobrania - ilość'@,1);
  PRN.add("{? pods=0 || form(TABx.IL_Z,,4) || '' ?}",12,'Zwroty - ilość'@,1);
  PRN.add("{? pods=0 || form(TABx.IL_P-TABx.IL_Z,,4) || '' ?}",12,'Pobrania - Zwroty ilość'@,1);
  PRN.add("{? pods=0 || form(TABx.WAR_P,,2) || form(pobwar,,2) ?}",12,'Pobrania - wartość'@,1);
  PRN.add("{? pods=0 || form(TABx.WAR_Z,,2) || form(zwwar,,2) ?}",12,'Zwroty - wartość'@,1);
  PRN.add("{? pods=0 || form(TABx.WAR_P-TABx.WAR_Z,,2) || form(pobwar-zwwar,,2) ?}",12,'Pobrania - Zwroty wartość'@,1);
   ~~}
{FI}
{IF: TABx.first()}
{ PAR_WYDR.TITLE:='ROZLICZENIE SUROWCÓW'@;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'Wydział: %1 - %2'@[ST.WYD().SYMBOL,ST.WYD().OPIS]}
{<{lmt+1} 'Okres: %1 - %2'@[$ST.AR,form(ST.AM,-2)]}
{<{lmt+1} {? opcja=1 || 'Rozliczenie w rozbiciu na poszczególne surowce'@ || 'Rozliczenie w rozbiciu na poszczególne zlecenia'@ ?}}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ _ndx:=TABx.ndx_tmp(,,'KTM',,,'ZL',,);
  TABx.index(_ndx);
  TABx.prefix();
  TABx.first();
~~}
{ dalej:=1;lp:=1;~~}
{WHILE: dalej}
{DO}
  {IF: tryb | lineleft>=10}
    {pobwar+=TABx.WAR_P;
     zwwar+=TABx.WAR_Z;~~}
    {SUBSTITUTE: 'LINIA0'}
  {ELSE}
    {pods:=1;~~}
    {SUBSTITUTE: 'LINIA0'}
    {SUBSTITUTE: 'LINIAF'}
    {PAGE}
    {FONT: 'T8B'}{SPACE:'B'}
    {SUBSTITUTE: 'TABHEAD'}
    {FONT: 'T8'}{ vp:=1; ~~}
    {pods:=0;
     pobwar+=TABx.WAR_P;
     zwwar+=TABx.WAR_Z;~~}
    {SUBSTITUTE: 'LINIA0'}
   {FI}
  { dalej:=TABx.next();lp+=1; ~~}
{OD}
{pods:=1;tekst:='Razem:'@;~~}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'LINIAF'}
{ELSE}
{ FUN.emsg('Brak pozycji do wydruku.'@);~~}
{EXIT}
{FI}

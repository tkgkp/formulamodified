:!UTF-8
{EXIT:
   _par:=params_get();
   {? czy_paperless
   || _TAB:=_par.TAB;
      _DRUK:=_par.DRUK;
      pars[1]:=exec('memo2print','#string',ZC,'TRESC');
      _TAB.REP:=KST.PRZEDST;
      _TAB.GODZ:={? ZC.TYP='G' || ZC.GODZ || 0 ?};
      _TAB.DNI:= {? ZC.TYP='N' || 14 |? ZC.TYP='M' || 10 |? ZC.TYP='D' || ZC.DNI || 0 ?};
      _TAB.ZLEC:=ZC.OSOBA().NAZWISKO+' '+ZC.OSOBA().PIERWSZE;
      _TAB.NR:=ZC.NU;
      _we:=_TAB.mk_edit({? _DRUK='uz' || 'Umowa - zlecenie'@ || 'Umowa o dzieło'@ ?},0,'umowa_uz_ud');
      _TAB.win_esep(_we,{? _DRUK='uz' || 'Dane od umowy zlecenia'@ || 'Dane od umowy o dzieło'@ ?});
      _TAB.win_efld(_we,,'NR',,,60,,1,'Numer umowy'@);
      _TAB.win_efld(_we,,'ZLEC',,,60,,1,'Zleceniobiorca'@);
      _TAB.win_efld(_we,,'REP',,,60,,,'Osoba reprezentująca zakład'@,,,,'F3_button=1');
      {? pars[1]='' || _TAB.win_efld(_we,,'TR',,,60,-4,,'Treść umowy'@) ?};
      {? ZC.TYP='G' || _TAB.win_efld(_we,,'GODZ',,,7,2,,'Przewidywany wymiar godzin'@)
      |? ZC.TYP='D' || _TAB.win_efld(_we,,'DNI',,,7,2,,'Przewidywany wymiar dni'@)
      |? ZC.TYP='N' || _TAB.win_efld(_we,,'DNI',,,5,,,'Ilość dni na realizację wypłaty'@)
      |? ZC.TYP='M' || _TAB.win_efld(_we,,'DNI',,,5,,,'Dzień miesiąca wypłaty'@)
      ?};
      exec('ok_esc','#window',_TAB,_we);
      _TAB.win_edit(_we);
      _exit:=
         {? _TAB.edit()
         || _TAB.add();
            _TAB.memo_put(,'TR');
            0
         || {? var_pres('PEP',_par)>100
            || _par.PEP.active:=0;
               _par.PEP.fml_after:=""
            ?};
            1
         ?};
      _exit:=_exit | var_pres('PEP',_par)>100
   || _exit:=0
   ?};
   params_set(_par);
   _exit
}
{
   PAR_WYDR.TITLE:='';
   {? pars[1]=''
   || _TAB:=params_get().TAB;
      _TAB.memo_get();
      pars[1]:=gsub(_TAB.memo_txt(),'\n',' ')
   ?}; ~~
}
{IF: PAR_WYDR.HEAD}
{FONT: 'HEAD'}
{SPACE: 'B'}
{ lmt:=charleft(); indent:=1; ~~ }
{>{lmt-13}PAR_WYDR.MIEJSC+', dn. '+date()$4}
{<13 STR.split(KST.NAZWA); STR.line(lmt-36)}
{WHILE: STR.next()}{DO}{<13 STR.line(lmt-36)}{OD}
{ PAR_WYDR.HEAD:=0; ~~ }
{MARGIN: 0.6}
{SUBSTITUTE: 'HEAD'}
{MARGIN: 0}
{ PAR_WYDR.HEAD:=1; ~~ }
{FONT: 'HEAD'}
{SPACE: 'B'}
{<13 STR.split(exec('ulica','stalesys')); STR.line(lmt-36)}
{WHILE: STR.next()}{DO}{<13 STR.line(lmt-36)}{OD}
{<13 exec('poczta','stalesys')}
{<13}REGON {KST.REG}
{<13}NIP {KST.NIP}
{ELSE}
{FONT: 'HEAD'}
{SPACE: 'B'}
{LINE: 6}
{FI}
{FONT: 'TITLE'}
{SPACE: 'A'}
{LINE: 2}
{IF: params_get.DRUK='uz'}
{<5>{charleft()-5} 'UMOWA ZLECENIE NR '+(|ZC.NU)}
{ELSE}
{<5>{charleft()-5} 'UMOWA O DZIEŁO NR '+(|ZC.NU)}
{FI}
{LINE: 2}
{FONT: 'W12'}
{SPACE: 'A'}
{ _kod:=obj_new(4); _kod[1]:='C'; _kod[2]:='S'; _kod[3]:='K'; _kod[4]:='I';
  _licz:=0;
  _exit:=0;
  {!
  |? _licz+=1;
     {? exec('szukaj','osoba',_kod[_licz],ZC.DZ) || _exit:=1 ?};
     ~_exit & _licz<obj_len(_kod)
  !};
  {? ~_exit || _licz:=1 ?};
  Tresc:=
      'Zawarta w dniu '+ZC.DZ$4+' pomiędzy firmą '+KST.NAZWA+
      ' '+exec('ulica','stalesys')+', '+exec('poczta','stalesys')+
      ', którą reprezentuje '+params_get().TAB.REP+', zwaną dalej Zamawiającym a Wykonawcą'+
      ', którym jest '+OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE+' '+
      OSOBA.DRUGIE+' mieszkając'+{? OSOBA.PLEC='K' || 'a' || 'y' ?}+
      ' pod adresem '+exec('adres','osoba',_kod[_licz],ZC.DZ)+
      ' na okres od '+ZC.DU$4+' do '+ZC.DW$4+
      ' o następującej treści:';
  STR.split(Tresc); ~~
}
{ENTIRE}
{SUBSTITUTE: 'FORMAT'}
{ENTIRE-}
{ENTIRE}

{<1>{charleft()} '(1)'}
{ Tresc:='Wykonawca zobowiązuje się wykonać dla Zamawiającego'+{? params_get.DRUK='uz' || '' || ' dzieło:' ?} ;
  Tresc+=' '+pars[1]+' z najlepszą wiedzą fachową i starannością jakiej wymaga ';
  Tresc+={? params_get.DRUK='uz' || 'praca' || 'dzieło' ?}+' tego rodzaju.';
  STR.split(Tresc); ~~
}
{IF: +Tresc}{SUBSTITUTE: 'FORMAT'}
{FI}

{ENTIRE-}
{ENTIRE}
{<1>{charleft()} '(2)'}
{IF: ZC.TYP='N'}
{ _kw:=form(ZC.KU,,2);
  Tresc:='Za wykonanie pracy Wykonawcy przysługiwać będzie wynagrodzenie brutto w wysokości: %1 '[_kw]+
       {? ZC.CZYWAL='T' || ZC.WAL().KOD+'.'||'zł.'?} ;
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{ELIF: ZC.TYP='M'}
{ _kw:=form(ZC.KU,,2);
  Tresc:='Za wykonanie pracy Wykonawcy przysługiwać będzie miesięczne wynagrodzenie brutto w wysokości: %1 '[_kw]+
       {? ZC.CZYWAL='T' || ZC.WAL().KOD+'.'||'zł.'?} ;
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{ELIF: ZC.TYP='D'}
{ _kw:=form(ZC.KU,,2);
  _dni:=params_get().TAB.DNI;
  Tresc:='Za wykonanie pracy Wykonawcy przysługiwać będzie wynagrodzenie brutto w wysokości: %1 '[_kw]+
       {? ZC.CZYWAL='T' || ZC.WAL().KOD ||'zł'?}+'/dzień'+
       {? _dni || ' w przewidywanym wymiarze %1 '[$(_dni$2)]+{? _dni<2 || 'dnia.' || 'dni.' ?} || '.' ?};
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{ELSE}
{ _kw:=form(ZC.KU,,2);
  Tresc:='Za wykonanie pracy Wykonawcy przysługiwać będzie wynagrodzenie brutto w wysokości: %1 '[_kw]+
       {? ZC.CZYWAL='T' || ZC.WAL().KOD ||'zł'?}+'/godz.'+
       {? params_get().TAB.GODZ$2 || ' w przewidywanym wymiarze %1 godz.'[$(params_get().TAB.GODZ$2)] || ' ' ?};
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{FI}
{ENTIRE-}

{ENTIRE}
{IF: ZC.TYP='N'}
{<1>{charleft()} '(3)'}
{ Dni:=params_get().TAB.DNI#0;
  Tresc:='Wynagrodzenie netto wypłacone będzie po dokonaniu stosownych potrąceń w terminie %1 '[$Dni]+
  {? Dni=1 || ' dnia' || ' dni' ?}+' od daty wykonania zlecenia.';
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{ELIF: ZC.TYP='M'}
{<1>{charleft()} '(3)'}
{ Dni:=params_get().TAB.DNI#0;
  Tresc:='Wynagrodzenie netto wypłacone będzie po dokonaniu stosownych potrąceń w terminie '+
  $Dni+'-go dnia każdego miesiąca.';
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{ELSE}
{<1>{charleft()} '(3)'}
{ Tresc:='Podstawę naliczania wynagrodzenia stanowi rachunek wystawiony przez Zamawiającego.';
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{FI}
{ENTIRE-}

{ENTIRE}
{<1>{charleft()} '(4)'}
{ Tresc:='Bez pisemnej zgody zamawiającego Wykonawca nie może powierzyć wykonania pracy innym osobom.';
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{ENTIRE-}

{ENTIRE}
{<1>{charleft()} '(5)'}
{ Tresc:='W sprawach nieuregulowanych niniejszą umową zastosowanie mają przepisy Kodeksu Cywilnego.';
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{ENTIRE-}

{ENTIRE}
{<1>{charleft()} '(6)'}
{ Tresc:='Umowę sporządzono w dwóch jednobrzmiących egzemplarzach, po jednym dla każdej ze stron.';
  STR.split(Tresc); ~~
}
{SUBSTITUTE: 'FORMAT'}
{LINE: 2}
{FONT: 'W8'}
{SPACE: 'C'}
{ lmt:=charleft()+8; ~~ }
{<10>{lmt/2}(62*'─')}{<{lmt/2}>{lmt}(32*'─')}
{<12>{lmt/2}'(podpis zamawiającego lub osoby reprezentującej zamawiającego'}{<{lmt/2}>{lmt}'(data i podpis wykonawcy)'}
{<12>{lmt/2}'albo osoby upoważnionej do składania oświadczeń'}
{<12>{lmt/2}'w imieniu zamawiającego)'}
{ENTIRE-}
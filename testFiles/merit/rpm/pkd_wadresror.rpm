:!UTF-8
{TITLE:Dane adresowe i RORy}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   tresc:='pkd_wadresror';
   PAR_WYDR.TITLE:='Dane adresowe i RORy'@;
   par:=obj_new(17);
   P.cntx_psh();
   PKO.cntx_psh();
   PKO.index('_PKO');
   par[5]:=0;
   _upr:=exec('uprawnieniawrap','pkd','danych adresowych'@,'ZWS_DOS_PPDA','ZWS_DOS_PRDA')+
         exec('uprawnieniawrap','pkd','rachunkach bankowych'@,'PKD_DOS_PPRB','PKD_DOS_PRRB');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o: %1'@[_upr])
   || par[17]:=exec('par_wydr_ddp','pkd',
         PAR_WYDR.TITLE,,
         date(),'Na dzień'@,,
         ,,,
         'Rodzaj wydruku'@,'Uproszczony'@,'Pełny'@,0
      );
      {? par[5]:=par[17].size()
      || par[1]:=obj_new(@.CLASS.PRINT,11);
         par[1].add("{? par[6] || OSOBA.NAZWISKO || '' ?}",17,'Nazwisko'@);
         par[1].add("{? par[6] || OSOBA.PIERWSZE || '' ?}",17,'Imię'@);
         par[1].add("{? par[6] || par[6]:=0; OSOBA.PESEL || '' ?}",11,'PESEL'@);
         par[1].add("{? (par[7]=1 | ~par[17].ASK) & par[14] || OS_ADRES.RODZAJ || '' ?}",6,'Rodzaj'@);
         par[1].add("{? (par[7]=1 | ~par[17].ASK) & par[14] || OS_ADRES.OD$1 || '' ?}",10,'Od dnia'@);
         par[1].add("{? (par[7]=1 | ~par[17].ASK) & par[14] || OS_ADRES.DO$1 || '' ?}",10,'Do dnia'@);
         {? par[17].ASK
         || par[1].add("{? par[7]=1 & par[14] || exec('ulica','osoba')
                        |? par[7]=2 & par[14] || exec('poczta','osoba')
                        |? par[7]=3 & par[14] || OS_ADRES.MIASTO
                        || ''
                        ?}",30,'Ulica/poczta/miejscowość'@);
            par[1].add("{? par[7]=1 & par[14] || OS_ADRES.TEL
                        |? par[7]=2 & par[14] || 18+OS_ADRES.EMAIL
                        |? par[7]=3 & par[14] || 18-OS_ADRES.EMAIL
                        || ''
                        ?}",18,'Telefon/e-mail'@);
            par[1].add("{? par[7]=1 & par[14] || OS_ADRES.WOJEWODZ
                        |? par[7]=2 & par[14] || OS_ADRES.POWIAT
                        |? par[7]=3 & par[14] || OS_ADRES.GMINA
                        || ''
                        ?}",25,'Województwo/powiat/gmina'@);
           par[1].add("{? par[7]=1 & par[14] || OS_ADRES.KRAJ().NAZ
                       || ''
                       ?}",15,'Kraj'@);

            par[1].add("{? (par[7]=1 & par[9] & par[13]) | (par[7]=1 & par[12] & par[13]) || PKO.R().RT
                        |? (par[7]=2 & par[9] & par[13]) | (par[7]=2 & par[12] & par[13]) || PKO.BA().NB
                        |? (par[7]=3 & par[9] & par[13]) | (par[7]=3 & par[12] & par[13]) || par[9]:=0; PKO.N
                        || ''
                        ?}",45,'Opis/bank/numer'@)
         || par[1].add("{? par[14] || exec('ulica','osoba') || '' ?}",35,'Ulica'@);
            par[1].add("{? par[14] || exec('poczta','osoba') || '' ?}",29,'Poczta'@);
            par[1].add("{? par[14] || OS_ADRES.MIASTO || '' ?}",20,'Miejscowość'@);
            par[1].add("{? par[14] || OS_ADRES.KRAJ().NAZ || '' ?}",15,'Kraj'@);
            par[1].add("{? (par[9] & par[13]) | (par[12] & par[13]) || par[9]:=0; PKO.N || '' ?}",35,'ROR - numer'@)
         ?};

         par[1].s_frame();

         par[2]:=tab_tmp(,'REF','INTEGER','Osoba');

         par[8]:=tab_tmp(1,
            'LP','INTEGER','Lp.',
            'A','REAL','Ref OS_ADRES',
            'R','REAL','Ref PKO'
         );

         prn1:='par[1]';
         lmt:=rsep:=par[7]:=0;
         vp:=par[9]:=par[4]:=par[6]:=1;
         params_set('P',exec('wybierz_parses','pracownik','PKD').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc');
   P.cntx_pop();
   PKO.cntx_pop()
}
{COMMENT}OLD: wadror.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[5]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[4]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[4]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}{? par[17].ASK || 'Wydruk pełny'@ || 'Wydruk uproszczony'@ ?}}
   {<{lmt+1}'%1: '['Data'@]} {par[17].OD$1}
   {<{lmt+1}'Rodzaj adresu: S - adres zameldowania, C - adres zamieszkania, K - adres korespondencyjny, I - inny.'@}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{REP: par[1].fbar(lmt)}
{par[1].setcolor('255:255:255')}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{IF: ~par[2].find_key(#P.OSOBA)}
   {  par[2].REF:=#P.OSOBA;
      par[6]:=1;
      par[9]:=1;
      par[2].add();
      par[10]:=1;
      par[12]:=1;
      par[13]:=0;
      par[14]:=0;
      par[15]:=1;
      par[16]:=0;

      par[8].erase();
      par[8].blank();

      OS_ADRES.index('OD');
      OS_ADRES.prefix(P.OSOBA().ref());
      {? OS_ADRES.first()
      || {!
         |? par[8].LP+=1;
            par[8].A:=OS_ADRES.ref();
            par[8].R:=0;
            par[8].add();
            OS_ADRES.next()
         !}
      ?};
      par[8].first();
      PKO.index('_PKO');
      PKO.prefix(P.OSOBA);
      {? PKO.first()
      || {!
         |? {? par[8].R | ~par[8].size()
            || par[8].LP+=1;
               par[8].A:=0;
               par[8].R:=PKO.ref();
               par[8].add()
            |? par[8].A & ~par[8].R
            || par[8].R:=PKO.ref();
               par[8].put();
               par[8].next()
            ?};
            PKO.next()
         !}
      ?};
      ~~
   }
   {ENTIRE}
      {FOR: par[8]}
      {DO}
         {
            par[14]:=
               {? OS_ADRES.seek(par[8].A,)
               || {? par[17].OD<>date(0,0,0)
                  || {? OS_ADRES.OD<=par[17].OD
                     || {? OS_ADRES.DO<>date(0,0,0)
                        || par[17].OD<=OS_ADRES.DO
                        || 1
                        ?}
                     ?}
                  || 1
                  ?}
               ?};
            ~~
         }
         {IF:  par[14] | PKO.seek(par[8].R,)}
            {
               par[16]:=1;
               {? par[15]
               || PKO.prefix();
                  {? PKO.find_key(P.OSOBA)
                  || par[13]:=1;
                     PKO.prefix(P.OSOBA);
                     {? par[10]:=PKO.size()
                     || PKO.first()
                     ?}
                  ?}
               ?};
               par[15]:=0;
               par[7]:={? ~par[17].ASK || 2 ?};
               {? par[10]<>0
               || par[12]:=1;
                  par[10]-=1
               || par[12]:=0
               ?};
               ~~
            }
            {WHILE: (par[7]+=1)<=3}{DO}
               { par[1].ini_line(); ~~ }
               {WHILE: par[1].next()}{DO}
                  {REP: par[1].fline(lmt)}
               {OD}
            {OD}
            { PKO.next(); ~~}
         {FI}
      {OD}
      {IF: par[10]<>0 & par[16]<>0}
         {WHILE: (par[10]-=1)>=0}
         {DO}
            {
               par[14]:=0;
               par[12]:=1;
               par[7]:={? ~par[17].ASK || 2 ?};
               ~~
            }
            {WHILE: (par[7]+=1)<=3}
            {DO}
               { par[1].ini_line(); ~~ }
               {WHILE: par[1].next()}
               {DO}
                  {REP: par[1].fline(lmt)}
               {OD}
            {OD}
            { PKO.next(); ~~}
         {OD}
      {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}

:!UTF-8
{TITLE:G. Zestawienie pozycji dokumentów zakupu}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   zakz_000:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','doklw','p','PRN');
      VAR_DEL.delete('od_daty','do_daty');
      VAR_DEL.delete('od_platnosc','do_platnosc','lp');
      VAR_DEL.delete('filtr_kh','fak_wew','mpp');
      VAR_DEL.delete('wydr_naz','wydr_edi','wyjscie','X_Xb','X_Xw1')
   ";
   zakz_000();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   tryb:=rep_export();

   filtr_kh :='N';
   mpp:=0;
   od_daty:=do_daty:=date(0,0,0);
   od_platnosc:=do_platnosc:=date(0,0,0);

   lp:=0;
   doklw :=2; "--dokladnosc wartosc--";

   wydr_naz:='zakz_001';
   wydr_edi:='ZAKZ_000';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? exec('upr_cz','ceny')
      & WYDRUKIL.edit("
         {? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || _wyn:='' || FUN.info('Błędny zakres dat.'@);_wyn:='DO_DATY' ?};
         {? _wyn=''
         || {? WYDRUKIL.DO_DATY1>=WYDRUKIL.OD_DATY1 || _wyn:='' || FUN.info('Błędny zakres dat.'@);_wyn:='DO_DATY1' ?}
         ?};_wyn")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         filtr_kh:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         fak_wew:=WYDRUKIL.CHKBOX02;
         mpp:={? exec('sp_active','faktury_wspolne') || WYDRUKIL.RADIOB_3 || 0 ?};
         od_daty:=WYDRUKIL.OD_DATY;
         do_daty:=WYDRUKIL.DO_DATY;
         od_platnosc:=WYDRUKIL.OD_DATY1;
         do_platnosc:=WYDRUKIL.DO_DATY1
      ?}
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_kh','kontrahent',filtr_kh,'N') ?};
   {? wyjscie=0
   ||
      _ilk:=10+mpp*2;
      PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();
      n:=obj_new(_ilk); "--nazwy kolumn--";
      k:=obj_new(_ilk); "--szerokosci kolumn--";
      w:=obj_new(_ilk); "--wartosci kolumn--";
      k[1]:=5;    n[1]:='Lp.'@;
      k[2]:=20;   n[2]:='Symbol dokumentu'@;
      k[3]:=10;   n[3]:='Kontrahent'@;
      k[4]:=12;   n[4]:='Data otrzymania'@;
      k[5]:=5;    n[5]:='Poz.'@;
      k[6]:=10;   n[6]:='Indeks'@;
      k[7]:=10;   n[7]:='PKWIU'@;
      k[8]:=10;   n[8]:='Wartość netto'@;
      k[9]:=10;   n[9]:='Wartość VAT'@;
      k[10]:=10;  n[10]:='Wartość brutto'@;

      {? mpp
      ||
         k[11]:=8;   n[11]:='Nagłówek split payment'@;
         k[12]:=8;   n[12]:='Pozycja split payment'@
      ?};
      {! _ii.._ilk |! w[_ii] :='' !};

      _ilks:=3+mpp;
      s:=obj_new(_ilks); "--razem--";
      {! _ii.._ilks |! s[_ii] :=0 !}
   ?}
}
{END:
   zakz_000(); VAR_DEL.delete('zakz_000')
}
{IF: exec('upr_cz','ceny')=0}{ FUN.info('Brak uprawnień do wartości zakupu.'@);~~}{EXIT}{FI}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
PRN.add("w[3]",k[3],n[3]);
PRN.add("w[4]",k[4],n[4]);
PRN.add("w[5]",k[5],n[5],1);
PRN.add("w[6]",k[6],n[6]);
PRN.add("w[7]",k[7],n[7]);
PRN.add("w[8]",k[8],n[8],1);
PRN.add("w[9]",k[9],n[9],1);
PRN.add("w[10]",k[10],n[10],1);
{? mpp
||
   PRN.add("w[11]",k[11],n[11]);
   PRN.add("w[12]",k[12],n[12])
?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
 'SYM'    ,'STRING[20]'  ,'Symbol faktury',
   'KH'     ,'STRING[10]'  ,'Skrót kontrahenta',
   'D'      ,'DATE'        ,'Data sprzedaży',
   'POZ'    ,'REAL'        ,'Pozycja',
   'IND'    ,'STRING[10]'  ,'Indeks',
   'PKWIU'  ,'STRING[10]'  ,'PKWIU',
   'NETTO'  ,'REAL'        ,'Netto',
   'VAT'    ,'REAL'        ,'VAT',
   'BRUTTO' ,'REAL'        ,'Brutto',
   'N_SP_WYM' ,'STRING[1]' ,'Nagłowek split payment',
   'P_SP_WYM' ,'STRING[1]' ,'Pozycja split payment');

OKR.cntx_psh; FAKS.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
_min:="{? _a & _a<_b || _a || _b ?}";
_min:=_min(od_daty~1,od_platnosc~1);
_max:="{? _a & _a>_b || _a || _b ?}";
_max:=_max(do_daty~1,do_platnosc~1);
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
{? OKR.first & (_min=0 | OKR.find_ge(_min)) & X_Xb.first()
||
   _stsupr:=exec('get','#params',6102,2,OPERATOR.USER)='T';
   _stss:="
      _wyn:=exec('usr_upr','b_perm','STZ',_a,OPERATOR.USER);
      _wyn
      ";
   {!
   |?
      FAKS.use('faktu'+ST.ODDZ+(($OKR.ROK)+2));
      FAKS.index('FAK_KH');
      {? X_Xb.first()
      ||
         {!
         |?
            FAKS.prefix('Z',X_Xb.KOD);
            {? FAKS.first()
            ||
               {!
               |? {? FAKS.STAT_REJ<>'A'
                     & (_stsupr=0 | _stsupr=1 & FAKS.STS<>null & _stss(FAKS.STS))
                     & (fak_wew=1 | FAKS.WHERE<>'E') & FAKS.KH().KOD=X_Xb.KOD
                     & ((od_daty<=FAKS.DO | od_daty=date(0,0,0)) & ( FAKS.DO<=do_daty | do_daty=date(0,0,0)))
                     & ((od_platnosc<=FAKS.TZ | od_platnosc=date(0,0,0) ) & (FAKS.TZ<=do_platnosc
                        | do_platnosc=date(0,0,0)))
                     & FAKS.ZEST='T' & FAKS.ZEST_AKC='T'
                     & (mpp=0 | mpp=1 & FAKS.SP_WYM='T' | mpp=2 & FAKS.SP_WAR<>0)
                  ||
                     FAP.cntx_psh();
                     FAP.use('fakpo'+ST.ODDZ+(($OKR.ROK)+2));
                     FAP.index('FAP');
                     FAP.prefix(FAKS.ref());

                     {? FAP.first()
                     ||
                        {!|?

                           X_Xw1.blank(1);
                           X_Xw1.SYM    :=FAKS.SYM;
                           X_Xw1.KH     :=KH.SKR;
                           X_Xw1.D      :=FAKS.D;
                           X_Xw1.POZ    :=FAP.POZ;
                           X_Xw1.IND    :=FAP.M().KTM;
                           X_Xw1.PKWIU  :=FAP.M().PKWIU().KOD;
                           X_Xw1.NETTO  :=FAP.WWAL_P;
                           X_Xw1.VAT    :=FAP.VWAL_P;
                           X_Xw1.BRUTTO :=FAP.BWAL_P;

                           {? mpp
                           ||
                              X_Xw1.N_SP_WYM:=FAKS.SP_WYM;
                              X_Xw1.P_SP_WYM:=FAP.SP_WYM
                           ?};

                           X_Xw1.add();
                           FAP.next()
                        !}
                     ?};
                     FAP.cntx_pop()
                  ?};
                  FAKS.next()
               !}
            ?};
            X_Xb.next()
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max)
   !}
?};
OKR.cntx_pop; FAKS.cntx_pop;

PAR_WYDR.TITLE:='ZESTAWIENIE POZYCJI DOKUMENTÓW ZAKUPU';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: filtr_kh='T'}{<{lmt+1}}{'Z wyborem kontrahentów'}{FI}
{IF: fak_wew=1}{<{lmt+1}}{'Uwzględniono faktury wewnętrzne'}{ELSE}{<{lmt+1}}{'Bez faktur wewnętrznych'}{FI}
{IF: mpp=1}{<{lmt+1}}{'Tylko dokumenty z wymaganym split payment'}{FI}
{IF: mpp=2}{<{lmt+1}}{'Tylko dokumenty z wartością split payment'}{FI}
{IF: od_daty$1+do_daty$1+od_platnosc$1+do_platnosc$1<>''}
   {<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
   {IF: od_daty$1+do_daty$1<>''}{<{lmt+1}}{'Data otrzymania'}{<{lmt+1+20}od_daty}{<{lmt+1+40}do_daty}{FI}
   {IF: od_platnosc$1+do_platnosc$1<>''}{<{lmt+1}}{'Termin płatności'}{<{lmt+1+20}od_platnosc}{<{lmt+1+40}do_platnosc}{FI}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; lp:=0;~~}
{FOR: X_Xw1}
{INDEX: X_Xw1.ndx_tmp(,,'D',,)}
{DO}
   {
w[1]:=form(lp+=1,,,'99');
   w[2]:=form(X_Xw1.SYM);
   w[3]:=form(X_Xw1.KH);
   w[4]:=form(X_Xw1.D);
   w[5]:=form(X_Xw1.POZ);
   w[6]:=form(X_Xw1.IND);
   w[7]:=form(X_Xw1.PKWIU);
   w[8]:=form(X_Xw1.NETTO,,doklw,'.,');
   w[9]:=form(X_Xw1.VAT,,doklw,'.,');
   w[10]:=form(X_Xw1.BRUTTO,,doklw,'.,');
   {? mpp
   ||
      w[11]:=form(X_Xw1.N_SP_WYM);
      w[12]:=form(X_Xw1.P_SP_WYM)
   ?};
   s[1] +=X_Xw1.NETTO;
   s[2] +=X_Xw1.VAT;
   s[3] +=X_Xw1.BRUTTO

   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{SUBSTITUTE: 'LINIAF'}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1] :=w[2] :=w[3] :=w[4] :=w[5] :=w[6] :='';
w[7] :='Razem:'; PRN.FORM[7] :=0;
w[8] :=form(s[1],,doklw,'.,');
w[9] :=form(s[2],,doklw,'.,');
w[10] :=form(s[3],,doklw,'.,');
{? mpp
||
   w[11]:='';
   w[12]:=''
?};
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
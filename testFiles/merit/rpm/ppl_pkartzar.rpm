:!UTF-8
{TITLE:Karta zarobkowa - zestawienie roczne}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','kolejkz','prn1','vp','lmt','rsep','tresc','b_rat','__param','__edit');
   tresc:='kartzar';
   PAR_WYDR.TITLE:='Karta zarobkowa - zestawienie roczne'@;
   par:=obj_new(9);
   par[9]:=0;
   P.cntx_psh();
   _upr:=exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1, 'ROK','INTEGER','Rok');
      __param.ROK:=O.R;
      __param.add();

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_pkartzar');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok karty zarobkowej'@);
      __param.efld_opt(__edit,'mark=1',,'ROK');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[9]:=__param.edit(
            "  {? __param.ROK<=1900
               || FUN.emsg('Błędna wartość parametru wydruku.'@); 0
               || 1
               ?}
            "
         )
      || par[1]:=obj_new(@.CLASS.PRINT,14);
         {? rep_export() || par[1].add("par[2][par[3]][16]",40,'Pracownik') ?};
         par[1].add("par[2][par[3]][14]",20,'Nazwa składnika');
         {! _is:=1..12
         |! STR.split(date(date~1,_is,1)$8);
            par[1].add($('par[2][par[3]]['+$_is+']'),12,STR.get_word(),1,,,,,'12,2')
         !};
         {? ~rep_export() || par[1].add("par[2][par[3]][13]",12,'RAZEM',1,,,,,'12,2') ?};
         par[1].s_frame();

         par[2]:=obj_new(76);
         {! _in:=1..obj_len(par[2])
         |! par[2][_in]:=obj_new(16)
         !};

         kolejkz:=sql('select min(R.RN) RN, R.RSKZ from R where R.RSKZ<>0 group by R.RSKZ order by 1');

         prn1:='par[1]';
         lmt:=rsep:=par[3]:=par[4]:=par[6]:=0;
         vp:=b_rat:=par[5]:=1;
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?};
   KZ.cntx_psh();
   KZ.index('_KARTAZA'); ~~
}
{END:
   VAR_DEL.delete('par','kolejkz','prn1','vp','lmt','rsep','tresc','b_rat','__param','__edit');
   P.cntx_pop();
   KZ.cntx_pop()
}
{COMMENT}OLD: pkartzar.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par[9]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Rok kalendarzowy: '@+$__param.ROK}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{IF: ~rep_export()}
   {FONT: 'W10'}
   {SPACE: 'B'}
   {<{ceil(lmt*b_rat)+1}}{form(par[6]+=1,,,'9.')} {INCLUDE: p_prac.rpi}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{MACRO-}
{MACRO: tresc}
{ENTIRE}
{  par[4]:=par[3]:=0;
   echo('Trwa uzupełnianie informacji: '@+P.OSOBA().NAZWISKO);
   KZ.prefix(P.ref(),__param.ROK);
   {? KZ.first()
   || par[4]:=1;
      {! _in:=1..obj_len(par[2])
      |! {! _im:=1..obj_len(par[2][_in])-2 |! par[2][_in][_im]:=0 !};
         par[2][_in][15]:=0
      !};
      {!
      |? {? kolejkz.first()
         || _in:=1;
            {!
            |? _kw:=($('KZ.S'+$kolejkz.RSKZ))();
               par[2][_in][KZ.M]+=_kw;
               par[2][_in][13]+=_kw;
               par[2][_in][14]:=($('KART_ZAR.S'+$kolejkz.RSKZ))();
               {? _kw || par[2][_in][15]:=1 ?};
               {? rep_export()
               || par[2][_in][16]:='%1 %2 %3 %4'
                  [  P.OSOBA().NAZWISKO,
                     OSOBA.PIERWSZE,
                     {? +OSOBA.DRUGIE || OSOBA.DRUGIE || '' ?},
                     '(Nr teczki %1)'@[|P.T]
                  ]
               ?};
               _in+=1;
               kolejkz.next()
            !}
         ?};
         KZ.next()
      !}
   ?}; ~~
}
{IF: par[4]}
   {SUBSTITUTE: 'pracownik'}
   {REP: par[1].fhbar(lmt)}
   {WHILE: (par[3]+=1)<=obj_len(par[2])}{DO}
      {IF: par[2][par[3]][15]}
         { par[1].ini_line(); ~~ }
         {WHILE: par[1].next()}{DO}
            {REP: par[1].fline(lmt)}
         {OD}
      {FI}
   {OD}
   {REP: par[1].flbar(lmt)}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W10'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
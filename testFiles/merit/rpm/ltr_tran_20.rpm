:!UTF-8
{TITLE:F. Rozliczenie pracy pojazdu}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('w','s','k','PRN','PRNS','s_tran','tryb','samr','samt','samm');
   k :=obj_new(16); "--szerokosci kolumn--";
   w :=obj_new(16); "--wartosci kolumn--";
   dokl :=2; "--dokladnosc--";
   s_tran:=obj_new(16); "--podsumowania--";

   {! _i:=1..16 |! s_tran[_i]:=0 !};

   PRN :=obj_new(@.CLASS.PRINT,17); PRN.s_frame();
   PRNS :=obj_new(@.CLASS.PRINT,15); PRNS.s_frame();
   tryb:=rep_export();
   samr:=samt:=samm:='';

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=0;rsep:=1;
   tmp_vp:=1;

   k[1]  :=3;
   k[2]  :=10;
   k[3]  :=10;
   k[4]  :=20;
   k[5]  :=10;
   k[6]  :=10;
   k[7]  :=8;
   k[8]  :=6;
   k[9]  :=13;
   k[10] :=13;
   k[11] :=13;
   k[12] :=13;
   k[13] :=13;
   k[14] :=13;
   k[15] :=13;
   k[16] :=13;

   POJAZDY.win_sel('SLO_J');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;
   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0
   || wyjscie:=1
   ?}
}
{END:
   VAR_DEL.delete('w','s','k','PRN','PRNS','s_tran','tryb','samr','samt','samm');
   VAR_DEL.delete('wyjscie','dokl','oddz','tmp_vp');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1
|| PRN.add("form(w[1])",k[1],'Lp.',1)
|| PRN.add("form(samr)",15,'Samochód (Rejestracja)');
   PRN.add("form(samt)",15,'Samochód (Nazwa)');
   PRN.add("form(samm)",15,'Samochód (Marka)')
?};
PRN.add("form(w[2])",k[2],'Data');
PRN.add("form(w[3])",k[3],'Karta');
PRN.add("form(w[4])",k[4],'Nazwisko i imię kierowcy');
PRN.add("form(w[5])",k[5],'Licz. pocz.',1);
PRN.add("form(w[6])",k[6],'Licz. kon.',1);
PRN.add("form(w[7],,dokl,' ,')",k[7],'Jazdy',1);
PRN.add("form(w[8],,dokl,' ,')",k[8],'Zał.',1);
PRN.add("form(w[9],,dokl,' ,')",k[9],'Stan pal. pocz.',1);
PRN.add("form(w[10],,dokl,' ,')",k[10],'Stan pal. kon.',1);
PRN.add("form(w[11],,dokl,' ,')",k[11],'Pobrano',1);
PRN.add("form(w[12],,dokl,' ,')",k[12],'Zużycie wg normy',1);
PRN.add("form(w[13],,dokl,' ,')",k[13],'Zużycie rzeczywiste',1);
PRN.add("form(w[14],,dokl,' ,')",k[14],'Przepał',1);
PRN.add("form(w[15],,dokl,' ,')",k[15],'Oszczędność',1);
PRN.add("form(w[16],,dokl,' ,')",k[16],'Motogodziny',1);

PRNS.add("form(s_tran[1])",78,'Razem');
PRNS.add("form(s_tran[7],,dokl,' ,')",k[7],'Jazdy',1);
PRNS.add("form(s_tran[8],,dokl,' ,')",k[8],'Zał.',1);
PRNS.add("form(s_tran[9],,dokl,' ,')",k[9],'Stan pal. pocz.',1);
PRNS.add("form(s_tran[10],,dokl,' ,')",k[10],'Stan pal. kon.',1);
PRNS.add("form(s_tran[11],,dokl,' ,')",k[11],'Pobrano',1);
PRNS.add("form(s_tran[12],,dokl,' ,')",k[12],'Zużycie wg normy',1);
PRNS.add("form(s_tran[13],,dokl,' ,')",k[13],'Zużycie rzeczywiste',1);
PRNS.add("form(s_tran[14],,dokl,' ,')",k[14],'Przepał',1);
PRNS.add("form(s_tran[15],,dokl,' ,')",k[15],'Oszczędność',1);
PRNS.add("form(s_tran[16],,dokl,' ,')",k[16],'Motogodziny',1);

{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu za okres od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu\nza okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?};
   PAR_WYDR.TITLE:='Rozliczenie pracy pojazdu za okres od: '+$_od+' do: '+$_do
?};
prn1:='PRN'; vp:=1
;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}{<70}{'Status kart drogowych: '}{{? KARTDROG.WYB_STAT='W' || 'wszystkie' |? KARTDROG.WYB_STAT='T' || 'zaakceptowane' |? KARTDROG.WYB_STAT='N' || 'niezaakceptowane' ?}}
{IF: tryb<>1}{<{lmt+1} POJAZDY.NRREJ+' '+POJAZDY.NAZ+' '+POJAZDY.MAR().NAZ}{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR:'POJAZDY'}
{INDEX:'NRREJ'}
{PREFIX: REF.FIRMA}
{SELECT}
{DO}
   { samr:=POJAZDY.NRREJ;
     samt:=POJAZDY.NAZ;
     samm:=POJAZDY.MAR().NAZ;
     w[1]:=0;
   {! _i:=1..15 |! s_tran[_i]:=0 !};
   {? KARTDROG.WYBKART='M'
   || KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
      KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0)
   |? KARTDROG.WYBKART='W'
   || KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
      KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
      SAMK.index('D2');
      SAMK.prefix();
      {? SAMK.first()
      || KARTDROG.KARTY_OD:=SAMK.D;
         SAMK.last();
         KARTDROG.KARTY_DO:=SAMK.D
      ?}
   ?}
   ;~~}
   {FOR:'SAMK'}
   {INDEX:'D'}
   {PREFIX:POJAZDY.ref()}
   {DO}
      {IF: (SAMK.D>=KARTDROG.KARTY_OD & SAMK.D<=KARTDROG.KARTY_DO) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT)}
         {
         w[1]+=1;
         w[2]:=SAMK.D;
         w[3]:=SAMK.SYM;
         P_KAP.index('K');
         P_KAP.prefix(SAMK.ref(),'T');
         {? P_KAP.first() & P_KAP.size()=1
         || w[4]:=P_KAP.OSOBA().NAZWISKO+' '+P_KAP.OSOBA().PIERWSZE
         |? P_KAP.first() & P_KAP.size()>1
         || w[4]:='';
            {! |?
               {? w[4]<>'' || w[4]+=',' ?};
               w[4]+=P_KAP.OSOBA().NAZWISKO+' '+P_KAP.OSOBA().PIERWSZE;
               P_KAP.next()
            !}
         || w[4]:=''
         ?};
         w[5]:=SAMK.SP;
         w[6]:=SAMK.SK;
         w[7]:=SAMK.SK-SAMK.SP;
         w[8]:=SAMK.ILZM+SAMK.ILZP;
         w[9]:=SAMK.SPP;
         w[10]:=SAMK.SKP;
         w[11]:=exec('i_palz','karty_drog',SAMK.ref(),'P');
         w[12]:=SAMK.Z_NORMA;
         w[13]:=(w[9]-w[10])+w[11];
         _zu:=w[13]-w[12];
         w[14]:=w[15]:=0;
         {? _zu>0 || w[14]:=|(_zu)
         |? _zu<0 || w[15]:=|(_zu)
         ?};
         w[16]:=SAMK.MOTGD;
         s_tran[7]+=w[7];
         s_tran[11]+=w[11];
         s_tran[12]+=w[12];
         s_tran[13]+=w[13];
         s_tran[14]+=w[14];
         s_tran[15]+=w[15];
         s_tran[16]+=w[16]
         ;~~}
         {prn1:='PRN';prn2:='PRN'; tmp_vp:=0;~~}
         {SUBSTITUTE:'LINIA0'}
         {IF: lineleft()<>0 & lineleft()<=3}{PAGE}{FI}
      {FI}
   {OD}
   {IF: tryb<>1}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {prn1:='PRN';prn2:='PRNS'; vp:=tmp_vp;
   s_tran[1]:='Razem:';
   {! _i:=2..6 |! s_tran[_i]:='' !};
   {! _i:=8..10 |! s_tran[_i]:='' !}
   ; ~~}
   {SUBSTITUTE: 'LINIA02'}
   {prn1:='PRNS';prn2:='PRN'; ~~}
   {FONT: 'T8G'}{SPACE: 'C'}
   {IF: lineleft()>0}{PAGE}{FI}
   {FI}
{OD}
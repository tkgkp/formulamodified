:!UTF-8
{TITLE:C. Dokumenty z niezgodnością dekretacji podatku VAT}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN: petla:=0;
        PRN := obj_new(@.CLASS.PRINT,20); PRN.sl_frame();PRN1 := obj_new(@.CLASS.PRINT,3); PRN1.sl_frame();
        ROZNE.REJ:=null;
        PAR_WYDR.S1PAR1:='W';
        KONTO.K1:=KONTO.K2:=KONTO.K3:=KONTO.K4:=KONTO.K5:='';
        KONTO.K1_BE:=KONTO.K2_BE:=KONTO.K3_BE:=KONTO.K4_BE:=KONTO.K5_BE:='';
        KONTO.K1_AE:=KONTO.K2_AE:=KONTO.K3_AE:=KONTO.K4_AE:=KONTO.K5_AE:='';
        KONTO.K1_SYNT:=KONTO.K2_SYNT:=KONTO.K3_SYNT:=KONTO.K4_SYNT:=KONTO.K5_SYNT:=0;
        KONTO.K1_WYM:=KONTO.K2_WYM:=KONTO.K3_WYM:=KONTO.K4_WYM:=KONTO.K5_WYM:=0;
        KONTO.K1_RODZ:=KONTO.K2_RODZ:=KONTO.K3_RODZ:=KONTO.K4_RODZ:=KONTO.K5_RODZ:=3;
        REJ.win_dict('SLO1'+{? OPERATOR.DEPT || '' || '_ODD' ?});
        WYDRUKIN.win_edit('WYBDATA8');
        WYDRUKIN.DATAOD:=SSTALE.AO().POCZ;
        WYDRUKIN.DATADO:=SSTALE.AO().KON;
        PAR_WYDR.PAR1:=PAR_WYDR.PAR2:=PAR_WYDR.PAR3:=PAR_WYDR.PAR4:=PAR_WYDR.PAR5:=PAR_WYDR.PAR6:=1;
        vat0:=vat1:=vat2:=vat3:=vat4:=vat5:=0; kvat1:=kvat2:=kvat3:=kvat4:=kvat5:=''; svat:=mdc:=''; lm:=0; font:='J';
        {? var_pres('KOMUNIKAT')>0 || &KOMUNIKAT ?};
        ~~
}
{END: &petla; obj_del(PRN); obj_del(PRN1);&vat0; &vat1; &vat2; &vat3; &vat4; &vat5; &svat; &kvat1; &kvat2; &kvat3; &kvat4; &mdc; &lm; &font; echo();
      &kvat5
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: dwdvat.rpm
Utworzony:   2003-12-01
==================================================
Zawartosc: Wydruk dokumentow z niezgodnoscia dekretacji podatku VAT
{COMMENT-}
{EXIT: ~(WYDRUKIN.edit("{? SSTALE.AO().POCZ=date(0,0,0) &
                          (~exec('dat_not','dok_fks',WYDRUKIN.DATAOD) | ~exec('dat_not','dok_fks',WYDRUKIN.DATADO))
                        ||  FUN.info('Podane daty muszą być z \nbieżącego roku bilansowego.'@); 'DATAOD'
                        |? ((WYDRUKIN.DATAOD<SSTALE.AO().POCZ) |(WYDRUKIN.DATADO>SSTALE.AO().KON) |
                           (WYDRUKIN.DATAOD>WYDRUKIN.DATADO)) & SSTALE.AO().POCZ<>date(0,0,0)
                        || FUN.info('\nDaty muszą się mieścić\n w bieżącym okresie obrachunkowym.'@);
                           {? (WYDRUKIN.DATAOD<SSTALE.AO().POCZ) | (WYDRUKIN.DATAOD>WYDRUKIN.DATADO) || 'DATAOD' || 'DATADO'?}
                        |? PAR_WYDR.PAR1+PAR_WYDR.PAR2+PAR_WYDR.PAR3+PAR_WYDR.PAR4+PAR_WYDR.PAR5+PAR_WYDR.PAR6=0
                        || FUN.info('Zaznacz rodzaj ewidencji VAT.'@); 'PAR1'
                        |? (PAR_WYDR.PAR1=1 | PAR_WYDR.PAR4=1 | PAR_WYDR.PAR5=1 | PAR_WYDR.PAR6=1) & KONTO.K1=''
                        || FUN.info('Wskaż konto dla podatku VAT należnego \ndo rozliczenia w bieżącym okresie.'@); 'K1'
                        |? (PAR_WYDR.PAR1=2 | PAR_WYDR.PAR4=3 | PAR_WYDR.PAR4=1 | PAR_WYDR.PAR6=1) & KONTO.K2=''
                        || FUN.info('Wskaż konto dla podatku VAT naliczonego \ndo rozliczenia w bieżącym okresie.'@); 'K2'
                        |? (PAR_WYDR.PAR1=1 | PAR_WYDR.PAR4=1 | PAR_WYDR.PAR5=1 | PAR_WYDR.PAR6=1) & KONTO.K3=''
                        || FUN.info('Wskaż konto dla podatku VAT należnego \ndo rozliczenia w innych okresach.'@); 'K3'
                        |? (PAR_WYDR.PAR1=2 | PAR_WYDR.PAR4=3 | PAR_WYDR.PAR4=1 | PAR_WYDR.PAR6=1) & KONTO.K4=''
                        || FUN.info('Wskaż konto dla podatku VAT naliczonego \ndo rozliczenia w innych okresach.'@); 'K4'
                        || 1 ?}"
         ))
}
{ PRN1.add(" ",84+30-(OPERATOR.DEPT | ROZNE.REJ)*9,'           D O K U M E N T'@);
  PRN.add("{? DOK.ZK='T'||'!'||''?}",2,'ZK'@);
  {? OPERATOR.DEPT | ROZNE.REJ
  || PRN.add("(9+(DOK.REJ().KOD+(9*' ')))+(((9*' ')+$DOK.NR)+9)",9,'Rejestr#Nr w rej'@,,'#')
  || PRN.add("(18+(DOK.REJ().KOD+(18*' ')))+((DOK.REJ().ODD().OD+( (18-(+DOK.REJ().ODD().OD++$DOK.NR))*' ')+$DOK.NR)+18)",18,'Rejestr#Jedn. ks. Nr w rej'@,,'#')
  ?};
  PRN.add("$DOK.DTW+' '+$DOK.DTO",10,'D. zapisu  D. dokum.'@);
  PRN.add("DOK.NK",50,'Symbol'@);
  PRN.add("DOK.TR",30,'Opis'@);
  PRN.add("form(vat0,15,2,' ,')",15,' VAT # bieżący okres'@,1,'#');
  PRN.add("form(vat2,15,2,' ,')",15,'Zadekretowano #- konto '@+KONTO.K1,1,'#');
  {? KONTO.K1<>KONTO.K3
  || PRN.add("form(vat10,15,2,' ,')",15,' VAT # inne okresy'@,1,'#');
     PRN.add("form(vat4,15,2,' ,')",15,'Zadekretowano #- konto '@+KONTO.K3,1,'#');
     PRN1.add("",63,'            V A T  N A L E Ż N Y'@)
  || PRN1.add("",31,'V A T  N A L E Ż N Y'@)
  ?};
  PRN.add("form(vat1,15,2,' ,')+form(vat1k,15,2,' ,')",15,' VAT bież. okr.#- do odliczenia#- w koszty'@,1,'#');
 PRN.add("form(vat3,15,2,' ,')+{? KONTO.K5<>'' || form(vatk,15,2,' ,') || '' ?}",15,'Zadekretowano #- konto %1'@[KONTO.K2]+
 {? KONTO.K5<>''
 || '#- k. koszt. %1'@[KONTO.K5]
 || '' ?}
 ,1,'#');
  {? KONTO.K2<>KONTO.K4
  || PRN.add("form(vat11,15,2,' ,')",15,' VAT # inne okresy'@,1,'#');
     PRN.add("form(vat5,15,2,' ,')",15,'Zadekretowano #- konto %1'@[KONTO.K4],1,'#');
     PRN1.add("",63,'            V A T  N A L I C Z O N Y'@)
  || PRN1.add("",31,'V A T  N A L I C Z O N Y'@)
  ?};~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{ENTIRE}
{PRN.bar()}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}
{ENTIRE-}
{MACRO-}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='DOKUMENTY Z NIEZGODNOŚCIĄ DEKRETACJI PODATKU VAT OD %1 DO %2'@[$WYDRUKIN.DATAOD,$WYDRUKIN.DATADO];~~}
{SUBSTITUTE: 'CHARHEAD'}
{FONT: 'Q'}{SPACE: 'B'}
{LINE: 1}{IF: page=1}
{<3 'Parametry wydruku:'@}
{<5 'Rejestr: '@}{IF: ROZNE.REJ<>null}{ROZNE.REJ().KOD}{ELSE}{'wszystkie rejestry'@}{FI}
{>{PRN.width()}
{? OPERATOR.DEPT
|| 'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
|| {? ROZNE.REJ<>null
   || 'Jednostka księgowa: %1'@[ROZNE.REJ().ODD().OD]
   || 'Jednostka księgowa: wszystkie jednostki księgowe'@
   ?}
 ?}
 }
{<5{? STALE.AO().ZAM='T'
   || 'Okres obrachunkowy: %1 %2 - zamknięty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]
   || 'Okres obrachunkowy: %1 %2 - otwarty'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]
   ?}
}
{>{PRN.width()} {?PAR_WYDR.S1PAR1='W'||'Dokumenty: wszystkie'@|?PAR_WYDR.S1PAR1='Z'||'Dokumenty: zaksięgowane'@||'Dokumenty: niezaakceptowane'@ ?}}
{<5 _opis:='Wybrane klasy ewidencji:'@+{? PAR_WYDR.PAR1=1 || ' Dostawy towarów/eksport,'@ || '' ?}
                                     +{? PAR_WYDR.PAR2=1 || ' Nabycia towarów/usług,'@ || '' ?}
                                     +{? PAR_WYDR.PAR3=1 || ' Import towarów,'@ || '' ?}
                                     +{? PAR_WYDR.PAR4=1 || ' Import usług,'@ || '' ?}
                                     +{? PAR_WYDR.PAR5=1 || ' WDT,'@ || '' ?}
                                     +{? PAR_WYDR.PAR6=1 || ' WNT,'@ || '' ?};
    {?_opis+1=',' || _opis:=_opis-1 ?};
    _opis+'.' }
{FI}{FONT: 'R'}{SPACE: 'C'}
{PRN1.hbar()}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}
{DO}
{PRN1.h_line()}
{OD}
{PRN1.jbar(PRN)}{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}{SPACE: 'C'}
{HEADER-}
{FONT: 'Q'}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{PRN.lbar()}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}----------------------WYDRUK-------------------------------------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{ {? OPERATOR.DEPT
  || {? PAR_WYDR.S1PAR1='W'
     || DOK.index('O_REJ');
        {? ROZNE.REJ<>null || DOK.prefix(OPERATOR.DEPT,ROZNE.REJ)
                           || DOK.prefix(OPERATOR.DEPT)
        ?}
     || DOK.index('PR');   {? PAR_WYDR.S1PAR1='Z' || _a:='T' || _a:='N' ?};
        {? ROZNE.REJ<>null || DOK.prefix(_a,_a,OPERATOR.DEPT,ROZNE.REJ)
                           || DOK.prefix(_a,_a,OPERATOR.DEPT)
        ?}
     ?}
  || {? PAR_WYDR.S1PAR1='W'
     || DOK.index('REJ');
        {? ROZNE.REJ<>null || DOK.prefix(ROZNE.REJ)
                           || DOK.prefix()
        ?}
     || DOK.index('PR1');  {? PAR_WYDR.S1PAR1='Z' || _a:='T' || _a:='N' ?};
        {? ROZNE.REJ<>null || DOK.prefix(_a,_a,ROZNE.REJ)
                           || DOK.prefix(_a,_a)
        ?}
     ?}
  ?};
  petla:=DOK.first(); ~~
}
{WHILE: petla}
{DO}
  {IF: _d:=DOK.DOK_REJ().SLO().KOD;(_d='VAT' | _d='SAD') & DOK.DTW>=WYDRUKIN.DATAOD & DOK.DTW<=WYDRUKIN.DATADO}
  {IF: _s:=DOK.RVAT().RVAT().KVAT().SYM;
       (PAR_WYDR.PAR1 & 1+_s='S') | (PAR_WYDR.PAR2 & 1+_s='Z') | (PAR_WYDR.PAR3 & 4+_s='ImpT') |
       (PAR_WYDR.PAR4 & 4+_s='ImpU') | (PAR_WYDR.PAR5 & 6+_s='_WWspD') | (PAR_WYDR.PAR6 & 6+_s='_WWspN')}
        { echo(DOK.TR);exec('vat_pocz','dok_fks',KONTO.K1,KONTO.K3,KONTO.K2,KONTO.K4,KONTO.K5);~~}
  {IF: exec('spr_one_rvat','dok_fks')}
        {FOR: 'POZ'}
        {INDEX: 'DOK'}
        {PREFIX: DOK.ref}
        {DO}
          { exec('vat_poz','dok_fks'); ~~}
        {OD}
        {IF: ~exec('vat_kon','dok_fks',0,1)}
{SUBSTITUTE: 'LINIA'}
        {FI}
  {FI}
  {FI}
  {FI}
  {petla:=DOK.next(); ~~}
{OD}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{ENTIRE}
{PRN.lbar()}
{<1>{PRN.width()}'KONIEC WYDRUKU'@}
{ENTIRE-}

:!UTF-8
{TITLE:F. Należności i zobowiązania w podziale na okresy}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,10,PAR_WYDR.MTOP,10,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:  i:=0;           "pomocnicza do pętli";
         max_per:=6;     "maksymalna liczba przedziałów + 1";
         il:=0;          "ostatnia kolumna w wierszu";
         tab_kol:=0;     "liczba kolumn tabeli";
         stan_na:=date;  "stan na wybrany dzień - dk obliczenia przedziału, do którego";
                         "trafia rozrachunek, zawsze brane są wszystkie zapisy i rozrachunki (stan aktualny)";
         pr:=0;
         pierwszy:=licznik:=0;
                         "zmienne pomocnicze - pierwszy rozrachunek, licznik rozrachunków ";
         suma:=0;        "saldo aktualnego rozrachunku";
         q:=obj_new(max_per+3); "wartości dla pojedynczego rozrachunku";
         s:=obj_new(max_per+3); "suma kolumn dla kontrahenta ";
         t:=obj_new(max_per+3); "totale kolumn dla całego wydruku";
         {! i:=1..max_per+3 |! q[i]:=s[i]:=t[i]:=0 !};
         per:=obj_new(max_per+1); "szerokości przedziałów";
         szer_kol:=obj_new(20); "szerokości kolumn";
         w:=obj_new(max_per+4); w[3]:=''; "wartosci dla macra linia i suma";
         petla:=vk:=kanc:=g1:=0;  PAR_WYDR.OMASKA:=1;
         own:=0; oma:=0; druk:=counter:=counterr:=0; p_odd:=indtym:=kan:='';
         {? var_pres('DRB',@.CLASS)<0 || exec('obj_drbt','raty') ?};
         VAR_DEL.delete('DRB'); DRB:=obj_new(@.CLASS.DRB);
         x:=obj_new(15);{! _a:=1..15 |! x[_a]:='' !};
         PRN:=obj_new(@.CLASS.PRINT,15); PRN.sl_frame();
         color:=prn1:=prn2:=''; KH.win_sel('WER_RPM');
         lm:=ll:=vp:=lmt:=rsep:=h_rsep:=0; pom_w:=''; PAR_WYDR.CZY_PODS:=1;
         PAR_WYDR.fld_fml('CZY_PODS','BEFORE_EDIT',"PAR_WYDR.S1PAR1=''");
         PAR_WYDR.fld_fml('S1PAR1','AFTER_EDIT',"PAR_WYDR.CZY_PODS:={? fld()='' || 0 || 1 ?};1")
      }
{END:    &il; &tab_kol; &stan_na; &pr; &i; &max_per; &suma; &petla;
         &pierwszy; &licznik; &vk; &kan; &kanc; &g1;
         obj_del(szer_kol); obj_del(w); obj_del(s); obj_del(q);
         obj_del(t); obj_del(per); undefine; &druk; &counter; &counterr;
         &own; &oma; &indtym; OP.ndx_drop; &p_odd; obj_del(DRB);
         obj_del(PRN); obj_del(x);
         &color; &prn1; &prn2;
         &lm; &ll; &vp; &lmt; &rsep; &h_rsep; pom_w
      }
{  UNPAR.P1:=1; UNPAR.P2:=2; UNPAR.P3:=0;
   KONTO.K1:=KONTO.K2:='';
   KONTO.K1_WYM:=KONTO.K2_WYM:=0;
   KONTO.K1_SYNT:=KONTO.K2_SYNT:=0;
   KONTO.K1_RODZ:=2; KONTO.K2_RODZ:=3;
   KONTO.K1_BE:='{? UNPAR.P1=1 || 1 || 0 ?}';
   KONTO.K2_BE:='{? UNPAR.P1=2 || 1 || 0 ?}';
   KONTO.K1_AE:=KONTO.K2_AE:='';
   UNPAR.P1_BE:=UNPAR.P2_BE:=UNPAR.P3_AE:='';
   UNPAR.P3_BE:='UNPAR.P2=2';
   UNPAR.P1_AE:='{? UNPAR.P1=1 || KONTO.K2:=\'\''
               +'|? UNPAR.P1=2 || KONTO.K1:=\'\''
               +'?}';
   UNPAR.P2_AE:='{? UNPAR.P2=1 || UNPAR.P3:=0 ?}; 1';
   PAR_WYDR.win_edit('RWOKRESY');
   PAR_WYDR.PAR1:=max_per;
   PAR_WYDR.PAR2:=7;
   PAR_WYDR.PAR3:=15;
   PAR_WYDR.PAR4:=30;
   PAR_WYDR.PAR5:=60;
   PAR_WYDR.PAR6:=90;
   PAR_WYDR.PAR7:=180;
   PAR_WYDR.S1PAR:='+';
   PAR_WYDR.DPAR1:=date();
   PAR_WYDR.S1PAR1:='N';
   PAR_WYDR.PAR8:=1;
   UNPAR.P9:=0; UNPAR.P9_BE:=''; UNPAR.P9_AE:='{? UNPAR.P9=0 || UNPAR.P7:=0;UNPAR.P8:=0;KONTO.K3:=\'\'?};1';
   _be:='UNPAR.P9';
   UNPAR.P7:=0; UNPAR.P7_BE:=_be; UNPAR.P7_AE:='';
   UNPAR.P8:=0; UNPAR.P8_BE:=_be; UNPAR.P8_AE:='';
   KONTO.K3:=''; KONTO.K3_WYM:=0; KONTO.K3_SYNT:=1; KONTO.K3_RODZ:=1;
   KONTO.K3_BE:=_be+' & PAR_WYDR.S1PAR1=\'N\''; KONTO.K3_AE:='';
   ~~}
  {EXIT: ~PAR_WYDR.edit("
       PAR_WYDR.S1PAR1:=~-PAR_WYDR.S1PAR1;
       {? PAR_WYDR.PAR1>max_per | PAR_WYDR.PAR1<1
       || FUN.info('Liczba przedziałów nie pochodzi z zakresu 1-%1.'@[$max_per]); 'PAR1'
       |? PAR_WYDR.PAR2>=PAR_WYDR.PAR3 & PAR_WYDR.PAR1>=2
       || FUN.info('Niewłaściwie zdefiniowane przedziały.'@); 'PAR2'
       |? PAR_WYDR.PAR3>=PAR_WYDR.PAR4 & PAR_WYDR.PAR1>=3
       || FUN.info('Niewłaściwie zdefiniowane przedziały.'@); 'PAR3'
       |? PAR_WYDR.PAR4>=PAR_WYDR.PAR5 & PAR_WYDR.PAR1>=4
       || FUN.info('Niewłaściwie zdefiniowane przedziały.'@); 'PAR4'
       |? PAR_WYDR.PAR5>=PAR_WYDR.PAR6 & PAR_WYDR.PAR1>=5
       || FUN.info('Niewłaściwie zdefiniowane przedziały.'@); 'PAR5'
       |? PAR_WYDR.PAR6>=PAR_WYDR.PAR7 & PAR_WYDR.PAR1>=6
       || FUN.info('Niewłaściwie zdefiniowane przedziały.'@); 'PAR6'
       |? PAR_WYDR.DPAR1=date(0,0,0)
       || FUN.info('Nie wypełniono daty na dzień.'@); 'DPAR1'
       |? UNPAR.P9 & KONTO.K3='' & PAR_WYDR.S1PAR1='N'
       || FUN.info('Nie podano konta rezerw na należności.'@); 'K3'
       || 1
       ?}")}
{  stan_na:=PAR_WYDR.DPAR1;
   PAR_WYDR.TITLE:=
   {? UNPAR.P9
   || {? UNPAR.P8
      || {? PAR_WYDR.S1PAR1='N' || 'Należności z tyt. dostaw, robót i usług - przeterminowane  na dzień: %1, nie spłacone w okresie.'@[PAR_WYDR.DPAR1$3]
         |? PAR_WYDR.S1PAR1='Z' || 'Zobowiązania z tyt. dostaw, robót i usług - przeterminowane  na dzień: %1, nie spłacone w okresie.'@[PAR_WYDR.DPAR1$3]
         || 'Należności i zobowiązania z tyt. dostaw, robót i usług - przeterminowane  na dzień: %1, nie spłacone w okresie.'@[PAR_WYDR.DPAR1$3]
         ?}
      || {? PAR_WYDR.S1PAR1='N' || 'Zestawienie należności z tyt. dostaw, robót i usług - o pozostającym od dnia okresie spłaty. %1'@[PAR_WYDR.DPAR1$3]
         |? PAR_WYDR.S1PAR1='Z'  || 'Zestawienie zobowiązań z tyt. dostaw, robót i usług - o pozostającym od dnia okresie spłaty. %1'@[PAR_WYDR.DPAR1$3]
         || 'Zestawienie należności i zobowiązań z tyt. dostaw, robót i usług - o pozostającym od dnia okresie spłaty. %1'@[PAR_WYDR.DPAR1$3]
         ?}
      ?}
   || {? PAR_WYDR.S1PAR1='N' || 'NALEŻNOŚCI W PODZIALE NA OKRESY  STAN NA DZIEŃ: %1'@[$stan_na]
      |? PAR_WYDR.S1PAR1='Z' || 'ZOBOWIĄZANIA W PODZIALE NA OKRESY  STAN NA DZIEŃ: %1'@[$stan_na]
      || 'NALEŻNOŚCI I ZOBOWIĄZANIA W PODZIALE NA OKRESY  STAN NA DZIEŃ: %1'@[$stan_na]
      ?}
   ?};
   KH.win_sel('WER_RPM');
   WYDRUKIN.DLKON:=35;
   WYDRUKI.MASKA:=KONTO.K1;
   {? WYDRUKI.MASKA=''
   || WYDRUKI.MASKA:=35*'?'
   || {? +WYDRUKI.MASKA<WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?}
   ?};
   {? OPERATOR.DEPT
   || indtym:=OP.ndx_tmp('',1,'WAL',,0,'KH',,0,'ODD',,0,'AN',,0,'TZ',,)
   || indtym:=OP.ndx_tmp('',1,'WAL',,0,'KH',,0,'AN',,0,'TZ',,)
   ?};
   w[4]:=date(0,0,0); w[5]:=''; w[6]:=0;
   PAR_WYDR.PAR1+=1;
   tab_kol:=PAR_WYDR.PAR1+7;
   szer_kol[1]:={? UNPAR.P2=2 || 20 || 26 ?};
   szer_kol[2]:={? UNPAR.P2=2 || 20 || 0 ?};
   szer_kol[3]:={? UNPAR.P2=2 || 10 || 0 ?};
   szer_kol[4]:={? UNPAR.P2=2 || 10 || 0 ?};
   szer_kol[5]:={? UNPAR.P2=2 || 5 || 0 ?};
   {! _a:=6..tab_kol
   |! szer_kol[_a]:=15
   !};
   {! _a:=1..tab_kol
   |! il+=szer_kol[_a]+1
   !};
   per[1]:=0;
   per[2]:={? PAR_WYDR.S1PAR='+' || 1 || -1 ?} * PAR_WYDR.PAR2;
   per[3]:={? PAR_WYDR.S1PAR='+' || 1 || -1 ?} * PAR_WYDR.PAR3;
   per[4]:={? PAR_WYDR.S1PAR='+' || 1 || -1 ?} * PAR_WYDR.PAR4;
   per[5]:={? PAR_WYDR.S1PAR='+' || 1 || -1 ?} * PAR_WYDR.PAR5;
   per[6]:={? PAR_WYDR.S1PAR='+' || 1 || -1 ?} * PAR_WYDR.PAR6;
   per[7]:={? PAR_WYDR.S1PAR='+' || 1 || -1 ?} * PAR_WYDR.PAR7;
   WYDRUKIN.REJSYM:='';
   {? OPERATOR.DEPT
   || p_odd:='Jednostka księgowa: %1'@[OPERATOR.DEPT().OD]
   || p_odd:='Wszystkie jednostki księgowe'@
   ?};
   {? rep_export()
   || {? UNPAR.P2=2
      || PRN.add({? PAR_WYDR.PAR15=0 || "w[3]" || "w[3]" ?},szer_kol[1],{? PAR_WYDR.PAR15=0 || 'Konto'@ || 'Kontrahent'@ ?})
      || PRN.add({? PAR_WYDR.PAR15=0 || "w[3]" || "KH.KOD+' - '+KH.NAZ" ?},szer_kol[1],{? PAR_WYDR.PAR15=0 || 'Konto'@ || 'Kontrahent'@ ?})
      ?}
   || PRN.add("x[1]",szer_kol[1],{? PAR_WYDR.PAR15=0 || 'Konto'@ || 'Kontrahent'@ ?});
      {? UNPAR.P2=2 || PRN.add("x[15]",szer_kol[2],'Skrót') ?}
   ?};
   {? UNPAR.P2=2 ||
      PRN.add("x[2]",szer_kol[2],'Dokument'@);
      PRN.add("x[3]",szer_kol[3],'Data otwarcia'@);
      PRN.add("x[4]",szer_kol[4],'Termin zapłaty'@);
      PRN.add("x[5]",szer_kol[5],'Typ'@)
   ?};
   {? UNPAR.P8=0 || PRN.add("x[6]",szer_kol[6],{?PAR_WYDR.S1PAR='+'|| 'Przed terminem płatności'@ || 'Po terminie płatności'@ ?},1) ?};
   {! _a:=1 .. PAR_WYDR.PAR1-1 |!
      PRN.add($('x['+$(_a+6)+']'),szer_kol[_a+6],'Od '+$({?PAR_WYDR.S1PAR='+'||1||-1?}*per[_a])+' do '+$({?PAR_WYDR.S1PAR='+'||1||-1?}*per[_a+1])+{? rep_export() || ' ' || ' #' ?}+{? PAR_WYDR.S1PAR='+' || 'po terminie'@ || 'przed terminem'@ ?},1,'#')
   !};
   PRN.add($('x['+$(_a+7)+']'),szer_kol[_a+7],'Powyżej '+$({? PAR_WYDR.S1PAR='+'||1||-1?}*per[_a+1])+{? rep_export() || ' ' || ' #' ?}+{?PAR_WYDR.S1PAR='+'||'po terminie'@||'przed terminem'@?},1,'#');
   PRN.add($('x['+$(_a+8)+']'),szer_kol[_a+8],'Razem'@,1);
   prn1:=prn2:='PRN';
   ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------LINIA_D------------------------------------{COMMENT-}
{MACRO: 'LINIA_D'}
{IF: pom_w<>''}{x[1]:=pom_w; {! _a:=2 .. PAR_WYDR.PAR1+7 |! x[_a]:='' !}; h_rsep:=rsep; rsep:=0; ~~}
{SUBSTITUTE: 'LINIA0'}{rsep:=h_rsep; pom_w:=''; ~~}
{FI}
{x[1]:=STR.line_pri(35); x[2]:=w[1];x[3]:={? type_of(w[2])=4 || $w[2] || w[2] ?};
 x[4]:=$w[4]; x[5]:=w[5]; x[6]:=form(q[1],,2,' ,');
 {! _a:=1 .. PAR_WYDR.PAR1+1 |! x[_a+6]:=form(q[_a+1],,2,' ,') !}; ~~}
{SUBSTITUTE: 'LINIA0'}{rsep:=0; ~~}
{MACRO-}
{COMMENT}---------------------------------SUMA------------------------------------{COMMENT-}
{MACRO: 'SUMA'}
{IF: pom_w<>'' & pom_w<>w[3] }{x[1]:=pom_w; {! _a:=2 .. PAR_WYDR.PAR1+7 |! x[_a]:='' !}; rsep:=0;~~}
{SUBSTITUTE: 'LINIA0'}{pom_w:=''; ~~}
{FI}
{x[1]:=w[3]; x[2]:=x[3]:=x[4]:=x[5]:=''; x[6]:=form(s[1],,2,' ,');
{! _a:=1 .. PAR_WYDR.PAR1+1 |! x[_a+6]:=form(s[_a+1],,2,' ,') !}; ~~}
{rsep:=1;~~}{SUBSTITUTE: 'LINIA0'}{rsep:=0; w[3]:=pom_w:='';~~}
{MACRO-}
{COMMENT}---------------------------------SUMA_-----------------------------------{COMMENT-}
{MACRO: 'SUMA_'}
{IF: pom_w<>'' & pom_w<>w[3] }{x[1]:=pom_w; {! _a:=2 .. PAR_WYDR.PAR1+7 |! x[_a]:='' !}; rsep:=0;~~}
{pom_w:=''; ~~}
{FI}
{x[1]:=w[3]; x[2]:=x[3]:=x[4]:=x[5]:=''; x[6]:=form(s[1],,2,' ,');
{! _a:=1 .. PAR_WYDR.PAR1+1 |! x[_a+6]:=form(s[_a+1],,2,' ,') !}; ~~}
{rsep:=1;~~}{rsep:=0; w[3]:=pom_w:='';~~}
{MACRO-}
{COMMENT}------------------------------CZOLO1-------------------------------------{COMMENT-}
{MACRO: 'CZOLO1'}
{SUBSTITUTE: 'HEAD'}{FONT: 'T8G'}{SPACE: 'C'}
{<1 'PARAMETRY WYDRUKU:'@}
{IF: PAR_WYDR.PAR8=1}{<1'Dla wszystkich kontrahentów'@}{ELSE}{<1'Dla wybranych kontrahentów'@}{FI}{IF: UNPAR.P7}{<70 'Termin płatności przedłużono do %1 dni.'@[$UNPAR.P7]}{FI}
{IF: UNPAR.P1=1 & KONTO.K1<>''}{<1'Maska konta: %1'@[KONTO.K1]}{ELIF: UNPAR.P1=2 & KONTO.K2<>''}{<1'Prefiks konta: %1'@[KONTO.K2]}{FI}
{<1'Waluta:  %1 %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<1 p_odd}
{<1 {? UNPAR.P2=1 || 'Zakres: bez rozrachunków'@ || {? UNPAR.P3 ||  'Zakres: z rozrachunkami z wyszczególnionymi płatnościami ratalnymi'@ ||  'Zakres: z rozrachunkami bez wyszczególnienia płatności ratalnych'@ ?} ?}
}{IF: UNPAR.P8}{<70 {? PAR_WYDR.S1PAR='+' || 'Tylko po terminie'@ || 'Tylko przed terminem'@ ?} }{FI}
{<1 {? PAR_WYDR.PAR15 || 'Sumowanie: według kontrahentów'@ || 'Sumowanie: według konta'@ ?} }{IF: ~rep_export()}{<70 'Dla granicznych wartości przedziału zakres \'od\' nie należy do przedziału, zakres \'do\' należy'@}{FI}
{{? rep_export() || 'Dla granicznych wartości przedziału zakres \'od\' nie należy do przedziału, zakres \'do\' należy'@ || ~~ ?} }
{FONT: 'T8GB'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{vp:=1;~~}
{MACRO-}
{COMMENT}------------------------------CZOLO--------------------------------------{COMMENT-}
{MACRO: 'CZOLO'}
{SUBSTITUTE: 'HEAD'}{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{vp:=1;~~}
{MACRO-}
{COMMENT}--------------------------------BODY_------------------------------------{COMMENT-}
{MACRO:'BODY_'}{ 'WYWOLYWANE W PETLI KH';~~}
{  {? |w[3]<>'' & STR.next() & licznik>0 || pom_w:=STR.line_pri(255) ?};
   rsep:=1;
   w[3]:=KH.KOD+' - '+KH.NAZ; STR.split(w[3]); licznik:=0;
   OP.index(indtym);
   {? OPERATOR.DEPT
   || OP.prefix(SSTALE.WAL,KH.ref,OPERATOR.DEPT,KONTO.K2)
   || OP.prefix(SSTALE.WAL,KH.ref,KONTO.K2)
   ?};
   petla:=OP.first(); ~~}
{WHILE: petla}
{DO}
   { {? UNPAR.P1=1 & KONTO.K1<>'' & KONTO.K1<>35*'?' || vk:=exec('mask','konto',OP.AN) || vk:=1 ?};
     {? UNPAR.P8
     || _tp:={? OP.TZ>OP.DO+UNPAR.P7 || OP.TZ || OP.DO+UNPAR.P7 ?};
        vk:=vk & {? PAR_WYDR.S1PAR='+' || _tp<=PAR_WYDR.DPAR1 || _tp>=PAR_WYDR.DPAR1 ?}
     ?};
   ~~}
   {IF: vk & (PAR_WYDR.S1PAR1='' | (1+OP.TYP)=PAR_WYDR.S1PAR1)}
      {  druk:=0;
         ZAP_OP.cntx_psh;
         ZAP_OP.index('OP');
         ZAP_OP.prefix(OP.ref);
         {? ZAP_OP.first
         || {! |?
               {? ZAP_OP.DATA<=PAR_WYDR.DPAR1 || druk:=1 ?};
               ZAP_OP.next & druk=0
            !}
         ?};
         ZAP_OP.cntx_pop; ~~}
      {IF: druk=1}
         {  'obroty na dzien podany w parametrach wejsciowych'@;
            own:=F.ROw(OP.ref, PAR_WYDR.DPAR1); oma:=F.ROm(OP.ref, PAR_WYDR.DPAR1); ~~}
         {IF: ((-(1+OP.TYP))='n' & own$2<>oma$2) | ((-(1+OP.TYP))='z' & own$2<>oma$2)}
            {  DRB.set_tab(OP.ref(),,PAR_WYDR.DPAR1);
               w[1]:=OP.SYM;
               w[2]:=OP.DO;
               w[6]:=OP.KH().SKR; pr:=1;
               licznik+=1; pierwszy:=1; counter:=counterr:=0; ~~}
            {WHILE: (DRB.EXIST & counter<D_RB.size()) | ~counter}
            {DO}
               {  counter+=1;
                  {? DRB.EXIST & UNPAR.P3
                  || suma:=t_drb[counter][1]-t_drb[counter][2];
                     _vtp:=t_drb[counter][3]

                  || suma:={? 1+OP.TYP='N' || own-oma || oma-own ?};
                     _vtp:=OP.TZ
                  ?};
                  {? suma=0 & counter=D_RB.size()
                  || suma:={? 1+OP.TYP='N' || own-oma || oma-own ?};
                     _vtp:=OP.TZ
                  ?};
                  _vtp_m:={? _vtp>OP.DO+UNPAR.P7 || _vtp || OP.DO+UNPAR.P7 ?};
                  _b:={? PAR_SKID.get(47)='N' & ( 1+OP.TYP='N' & oma>own | 1+OP.TYP='Z' & own>oma)
                      || {? PAR_WYDR.S1PAR='+' || -1 || 1 ?}
                      || {? PAR_WYDR.S1PAR='+' || PAR_WYDR.DPAR1-_vtp_m || _vtp_m-PAR_WYDR.DPAR1 ?}
                      ?};
                  {? {? PAR_WYDR.S1PAR='+' || -_b>=0 || -_b>0 ?}
                  || q[1]+=suma
                  |? _b>{? PAR_WYDR.S1PAR='+' || 1 || -1 ?}*per[PAR_WYDR.PAR1]
                  || q[PAR_WYDR.PAR1+1]+=suma
                  || {! i:=1..PAR_WYDR.PAR1-1
                     |! {? {? PAR_WYDR.S1PAR='-' & i=1
                           || _b>={? PAR_WYDR.S1PAR='+' || 1 || -1 ?}*per[i]
                           || _b>{? PAR_WYDR.S1PAR='+' || 1 || -1 ?}*per[i]
                           ?} & _b<={? PAR_WYDR.S1PAR='+' || 1 || -1 ?}*per[i+1]
                        || q[i+1]+=suma
                        ?}
                     !}
                  ?};
                  q[PAR_WYDR.PAR1+2]+=suma;
                  {? suma || counterr+=1 ?};
                  {? counter>1 & counterr>1 || w[1]:=w[2]:='' ?};
                  {? ~UNPAR.P3 & DRB.EXIST || counter:=D_RB.size() ?};
                  w[4]:=_vtp; w[5]:=OP.TYP; ~~}
               {IF: UNPAR.P2=2 & suma}
                  {SUBSTITUTE: 'LINIA_D'}
               {FI}
               { {! i:=1..(PAR_WYDR.PAR1+2) |! s[i]+=q[i]; q[i]:=0 !}; ~~}
           {OD}
         {FI}
      {FI}
   {FI}
   { petla:=OP.next(); ~~}
{OD}{' Do  OP';~~}
{IF: ~rep_export()}
{IF: UNPAR.P2<>2 | licznik>1}
{ {? w[3]<>'' & STR.next() || pom_w:=STR.line_pri(255) ?}; w[3]:={?UNPAR.P2=2||'Razem: '||''?}+{? UNPAR.P2=2 || KH.SKR||KH.KOD+' - '+KH.NAZ?};~~}
   {IF: UNPAR.P2=2}
      {rsep:=1;~~}
   {FI}
   {IF: s[PAR_WYDR.PAR1+2]}
      {SUBSTITUTE: 'SUMA'}
      {rsep:=1;~~}
   {FI}
{FI}
{ELSE}
{IF: UNPAR.P2=1}
{ w[3]:={?UNPAR.P2=2||'Razem: '||''?}+{? UNPAR.P2=2 || KH.SKR||KH.KOD+' - '+KH.NAZ?};~~}
       {IF: s[PAR_WYDR.PAR1+2]}
{x[1]:=w[3]; x[2]:=x[3]:=x[4]:=x[5]:='';x[6]:=form(s[1],,2,' ,');
{! _a:=1 .. PAR_WYDR.PAR1+1 |! x[_a+6]:=form(s[_a+1],,2,' ,') !}; ~~}
{rsep:=1;~~}{SUBSTITUTE: 'LINIA0'}{rsep:=0; w[3]:=pom_w:='';~~}
       {FI}
   {ELSE}
       {SUBSTITUTE: 'SUMA_'}
   {FI}
{FI}
{  {! i:=1..(PAR_WYDR.PAR1+2)
   |! t[i]+=s[i]; s[i]:=0
   !}; ~~}
{MACRO-}
{COMMENT}------------------------------MACRO-BODY-------------------{COMMENT-}
{MACRO:'BODY'}{ 'WYWOLYWANE W PETLI KH'@;~~}
{  licznik:=0;
   {? OPERATOR.DEPT
   || OP.index('PRZEG_O'); OP.prefix(SSTALE.WAL,OPERATOR.DEPT,KONTO.K2)
   || OP.index('PRZEG'); OP.prefix(SSTALE.WAL,KONTO.K2)
   ?};
   petla:=OP.first(); ~~}
{WHILE: petla}
{DO}
   {  {? UNPAR.P1=1 || vk:=exec('mask','konto',OP.AN) || vk:=1 ?};
      {? UNPAR.P8
      || _tp:={? OP.TZ>OP.DO+UNPAR.P7 || OP.TZ || OP.DO+UNPAR.P7 ?};
         vk:=vk & {? PAR_WYDR.S1PAR='+' || _tp<=PAR_WYDR.DPAR1 || _tp>=PAR_WYDR.DPAR1 ?}
      ?};
   ~~}
   {IF: vk & (PAR_WYDR.S1PAR1='' | (1+OP.TYP)=PAR_WYDR.S1PAR1)}
      {  druk:=0;
         ZAP_OP.cntx_psh;
         ZAP_OP.index('OP');
         ZAP_OP.prefix(OP.ref);
         {? ZAP_OP.first
         || {! |?
               {? ZAP_OP.DATA<=PAR_WYDR.DPAR1 ||
                  druk:=1
               ?};
               ZAP_OP.next & druk=0
            !}
         ?};
         ZAP_OP.cntx_pop;~~}
      {IF: druk=1}
         {  'obroty na dzien podany w parametrach wejsciowych'@;
            own:=F.ROw(OP.ref, PAR_WYDR.DPAR1); oma:=F.ROm(OP.ref, PAR_WYDR.DPAR1); ~~}
         {IF: ((-(1+OP.TYP))='n' & own$2<>oma$2) | ((-(1+OP.TYP))='z' & own$2<>oma$2)}
{'--------------------------------------------------------------------';~~}
           { DRB.set_tab(OP.ref(),,PAR_WYDR.DPAR1);
             w[1]:=OP.SYM;
             w[2]:=OP.DO;
             x[15]:=OP.KH().SKR;
             {? licznik=1 || {? PAR_WYDR.PAR8<>1 || w[3]:='' ?} ?};
             licznik+=1;  pierwszy:=1; counter:=counterr:=0; ~~}
            {WHILE: (DRB.EXIST & counter<D_RB.size()) | ~counter}
            {DO}
               {  counter+=1;
                  {? DRB.EXIST & UNPAR.P3
                  || suma:=t_drb[counter][1]-t_drb[counter][2];
                     _vtp:=t_drb[counter][3]
                  || suma:={? 1+OP.TYP='N' || own-oma || oma-own ?};
                     _vtp:=OP.TZ
                  ?};
                  {? suma=0 & counter=D_RB.size()
                  || suma:={? 1+OP.TYP='N' || own-oma || oma-own ?};
                     _vtp:=OP.TZ
                  ?};
                  _vtp_m:={? _vtp>OP.DO+UNPAR.P7 || _vtp || OP.DO+UNPAR.P7 ?};
                  _b:={? PAR_SKID.get(47)='N' & ( 1+OP.TYP='N' & oma>own | 1+OP.TYP='Z' & own>oma)
                      || {? PAR_WYDR.S1PAR='+' || -1 || 1 ?}
                      || {? PAR_WYDR.S1PAR='+' || PAR_WYDR.DPAR1-_vtp_m || _vtp_m-PAR_WYDR.DPAR1 ?}
                      ?};
                  {? {? PAR_WYDR.S1PAR='+' || -_b>=0 || -_b>0 ?}
                  || q[1]+=suma
                  |? _b>{? PAR_WYDR.S1PAR='+' || 1 || -1 ?}*per[PAR_WYDR.PAR1]
                  || q[PAR_WYDR.PAR1+1]+=suma
                  || {! i:=1..PAR_WYDR.PAR1-1
                     |! {? {? PAR_WYDR.S1PAR='-' & i=1
                           || _b>={? PAR_WYDR.S1PAR='+' || 1 || -1 ?}*per[i]
                           || _b>{? PAR_WYDR.S1PAR='+' || 1 || -1 ?}*per[i]
                           ?} & _b<={? PAR_WYDR.S1PAR='+' || 1 || -1 ?}*per[i+1]
                        || q[i+1]+=suma
                        ?}
                     !}
                  ?};
                  q[PAR_WYDR.PAR1+2]+=suma;
                  {? suma || counterr+=1 ?};
                  {? counter>1 & counterr>1 || w[1]:=w[2]:='' ?};
                  {? ~UNPAR.P3 & DRB.EXIST || counter:=D_RB.size() ?};
                  w[4]:=_vtp; w[5]:=OP.TYP; ~~}
               {IF:  UNPAR.P2=2 & suma}
                  {IF: OP.AN<>kan }
                     {IF: g1>1}
                        {w[3]:='Razem: '+kan; x[15]:=exec('set_skr','optermpl',kan); rsep:=1;~~}
                        {IF: ~rep_export()} {SUBSTITUTE: 'SUMA'} {FI}
                     {FI}
                     {  w[3]:=OP.AN; kan:=OP.AN; x[15]:=OP.KH().SKR; kanc:=1; g1:=0; STR.split(w[3]); rsep:=1;
                        {! i:=1..(PAR_WYDR.PAR1+2) |! t[i]+=s[i]; s[i]:=0 !};~~}
                  {ELSE}
                     { kanc:=0;~~}
                  {FI}
                  {SUBSTITUTE: 'LINIA_D'}
                  { licznik+=1; g1+=1; pr:=1; ~~}
               {ELIF: UNPAR.P2=1 & suma}
                  {IF: OP.AN<>kan & kan<>''}
                     {w[3]:=kan; g1:=1; rsep:=1; ~~}
                     {SUBSTITUTE: 'SUMA'}
                     { {! i:=1..(PAR_WYDR.PAR1+2) |! t[i]+=s[i]; s[i]:=0 !};~~}
                     { pr:=1; ~~}
                  {FI}
               {FI}
               {IF: UNPAR.P2=1 & kan<>OP.AN}
                  { kan:=OP.AN; ~~}
               {FI}
               { {! i:=1..(PAR_WYDR.PAR1+2)
                 |! s[i]+=q[i]; q[i]:=0
                 !};~~}
             {OD}
           {FI}
          {FI}
         {FI}
   {petla:=OP.next(); ~~}
{OD}{' Do  OP';~~}
   {IF: UNPAR.P2=1}
      {  w[3]:=kan; rsep:=1;~~}
      {IF: suma}
         {SUBSTITUTE: 'SUMA'}
      {FI}
      {  {! i:=1..(PAR_WYDR.PAR1+2)
         |! t[i]+=s[i]; s[i]:=0
      !};~~}
   {ELIF: UNPAR.P2=2}
      {IF: g1>1}
         { w[3]:='Razem: '+kan; ~~}
         {IF: ~rep_export()}{SUBSTITUTE: 'SUMA'}{FI}
      {FI}
      { w[3]:=OP.AN; kan:=OP.AN; kanc:=1; g1:=0; STR.split(w[3]);~~}
      {IF: pr>0}
      {FI}
      {  {! i:=1..(PAR_WYDR.PAR1+2)
         |! t[i]+=s[i]; s[i]:=0
      !};~~}
   {FI}
{MACRO-}
{FOOTER:0}

{FOOTER-}
{'--------------------------------------------------------------------';~~}
{SUBSTITUTE: 'CZOLO1'}
{HEADER}
{SUBSTITUTE: 'CZOLO'}
{HEADER-}
{'--------------------------------------------------------------------';~~}
{FOOTER:0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{'--------------------------------------------------------------------';~~}
{IF: PAR_WYDR.PAR8=1}
   {IF: PAR_WYDR.PAR15=1}
      {FOR: 'KH'}
      {INDEX: 'KOD'}
      {PREFIX: 2, WYDRUKIN.REJSYM}
      {DO}
         {SUBSTITUTE: 'BODY_'}
      {OD}{'  Do  KH';~~}
   {ELSE}
      {SUBSTITUTE: 'BODY'}
   {FI}
{ELIF: PAR_WYDR.PAR8=2}
   {KH.win_sel('WER_RPM');~~}
   {FOR: 'KH'}
   {INDEX: 'KOD'}
   {PREFIX: 2, WYDRUKIN.REJSYM}
   {SELECT:'#EXIT'}
   {DO}
      {SUBSTITUTE: 'BODY_'}
   {OD}{'  Do  KH';~~}
{FI}
{IF: t[PAR_WYDR.PAR1+2]$2<>0 & PAR_WYDR.CZY_PODS}
{  {? pom_w=w[3] || pom_w:='' ?};
   {? UNPAR.P9
   || w[3]:={? PAR_WYDR.S1PAR1='N' || 'Suma należności brutto'@ |? PAR_WYDR.S1PAR1='Z' || 'Suma zobowiązań'@ || 'Suma rozrachunków'@ ?}
   |? PAR_WYDR.PAR15
   || w[3]:='Ogółem wszyscy kontrahenci'@
   || w[3]:='Ogółem'@; x[15]:=''
   ?};
   {! i:=1..(PAR_WYDR.PAR1+2) |! s[i]:=t[i] !}; ~~}
{IF: ~rep_export()}{SUBSTITUTE: 'SUMA'}{FI}
{IF: PAR_WYDR.S1PAR1='N' & KONTO.K3<>''}
{  F.Obr(KONTO.K3,0,SSTALE.AO().NR);
   _vs:={? PAR_WYDR.S1PAR1='N' || F.ma-F.wn || F.wn-F.ma ?};
   w[3]:='Rezerwa na należności'@;
   {! i:=1..(PAR_WYDR.PAR1+2) |! s[i]:=0 !};
   s[1]:=s[PAR_WYDR.PAR1+2]:=-_vs; ~~}
{SUBSTITUTE: 'SUMA'}
{  w[3]:='Suma należności netto'@;
   {! i:=1..(PAR_WYDR.PAR1+2) |! s[i]:=t[i]+s[i] !};
~~}
{SUBSTITUTE: 'SUMA'}
{FI}
{FI}
{IF: lineleft()<2 & lineleft()<>0}{PAGE}{FI}
{FOOTER}
{FOOTER-}
{SUBSTITUTE: 'LINIAF'}
{IF: ~rep_export()}
{<1>{il}'KONIEC WYDRUKU'@}
{FI}
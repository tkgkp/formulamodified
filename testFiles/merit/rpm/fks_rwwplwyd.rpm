:!UTF-8
{TITLE:B. Spodziewane wpływy i wydatki}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN1:=obj_new(@.CLASS.PRINT,10); PRN1.s_frame();
   PRN2:=obj_new(@.CLASS.PRINT,5); PRN2.s_frame();
   {? PAR_SKID.get(39)='T' || czy_w:=1
   |? PAR_SKID.get(39)='N' || czy_w:=0 || czy_w:=2 ?};
   w:=obj_new(10);
   {? var_pres('DRB',@.CLASS)<0 || exec('obj_drbt','raty') ?};
   DRB:=obj_new(@.CLASS.DRB); raty:=l_rat:=0;
   tp:=date(0,0,0); ROZRACH.KONTR:=WYDRUKIN.KH:=null;
   suma5:=suma6:=suma7:=razem5:=razem6:=razem7:=petla:=gr:=wybor:=vp:=lm:=0; mdc:=''; dt:=date(0,0,0);
   swn:=sma:=sumdo1:=sumdo2:=sumdo3:=sumod1:=sumod2:=sumod3:=swal:=0;
   KH.win_dict('WER_SLO');
   KH.actions('WER_SLO','W')
   }
{END:
   VAR_DEL.delete('PRN1','PRN2','w','DRB','tp','suma5','suma6','suma7','razem5','razem6','razem7','czy_w','dt',
                  'gr','wybor','petla','vp','swn','sma','sumdo1','sumdo2','sumdo3','sumod1','sumod2','sumod3','swal');
   DOK.use('doku'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2)); &lm; &mdc; &raty; &l_rat;
   POZ.use('pozy'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
   POW.use('pow_'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
   KH.actions('WER_SLO')
}
{  UNPAR.P1:=0; UNPAR.P1_BE:=UNPAR.P1_AE:='';
   {? czy_w=2
   || WYDRUKIN.win_edit('WYBDATA7');
      PAR_WYDR.WSK_WAL:='T'
   || WYDRUKIN.win_edit('WYBDATA')
   ?};
   UNPAR.PSLO:=null;
   UNPAR.PSLO_BE:='{? XINFO.SLU_PROJ<>null || SLO.win_dict(\'ONE_SEL\'); SLO.win_sel(\'ONE_SEL\'); XINFO.SLU_PROJ(); 1 ?}';
   UNPAR.PSLO_BV:=UNPAR.PSLO_AE:='';
   OKRO_F.cntx_psh();
   OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
   {? SSTALE.AO().POCZ<>date(0,0,0) | OKRO_F.prev() | OKRO_F.next()
   || WYDRUKIN.DATA_OD2:=WYDRUKIN.DATA_NA:=OKRO_F.POCZ; WYDRUKIN.DATA_DO2:=OKRO_F.KON
   || WYDRUKIN.DATA_OD2:=WYDRUKIN.DATA_NA:=WYDRUKIN.DATA_DO2:=date(0,0,0)
   ?};
   OKRO_F.cntx_pop();~~}
{EXIT: ~WYDRUKIN.edit("_data:={? ~exec('dat_not','dok_fks',WYDRUKIN.DATA_NA)
                              || 'DATA_NA'
                              || ''
                              ?};
                       {? _data<>''
                       || FUN.info('Podaj datę z bieżącego\nroku bilansowego.'@); _data
                       || {? WYDRUKIN.DATA_DO2<WYDRUKIN.DATA_OD2
                          || FUN.info('Data końcowa musi być większa od daty początkowej.'@); 'DATA_DO2'
                          || 1
                          ?}
                       ?}")}
{ {? czy_w=2 || {? PAR_WYDR.WSK_WAL='T' || czy_w:=1 || czy_w:=0 ?} ?};
  dt:=WYDRUKIN.DATA_OD2;
  PRN1.add("w[1]",50,'      Dokument'@);
  PRN1.add("w[9]",10,'Data otw.'@);
  PRN1.add("w[2]",10,'   Data'@);
  PRN1.add("w[3]",40,15*' '+'Kontrahent'@);
  PRN1.add("w[10]",20,15*' '+'Skrót Kontrahenta'@);
  PRN1.add("w[4]",8,'  Kod'@);
  {? FINFO.NAROD<>SSTALE.WAL
  || PRN1.add("w[5]",16,'  Wpływ (%1)'@[FINFO.NAROD().KOD],1);
     PRN1.add("w[6]",16,' Wydatek (%1)'@[FINFO.NAROD().KOD],1)
  || PRN1.add("w[5]",16,'     Wpływ'@,1);
     PRN1.add("w[6]",16,'    Wydatek'@,1)
  ?};
  {? czy_w & ~UNPAR.P1
  || PRN1.add("w[7]",4,'Wal.'@,1);
     PRN1.add("w[8]",16,'Kwota w walucie'@,1)
  ?};
  PRN2.add("w[1]",153);
  {? FINFO.NAROD<>SSTALE.WAL
  || PRN2.add("w[2]",16,'  Wpływ (%1)'@[FINFO.NAROD().KOD],1);
     PRN2.add("w[3]",16,' Wydatek (%1)'@[FINFO.NAROD().KOD],1)
  || PRN2.add("w[2]",16,'     Wpływ'@,1);
     PRN2.add("w[3]",16,'    Wydatek'@,1)
  ?};
  {? czy_w & ~UNPAR.P1
  || PRN2.add("''",4,,1);
     PRN2.add("w[4]",16,,1)
  ?};
  {! _i:=1..9 |! w[_i]:='' !};~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------MACRO-LINIA1-----------------{COMMENT-}
{MACRO: 'LINIA1'}
{PRN1.ini_line()}
{WHILE: PRN1.next()}
{DO}
{REP: PRN1.fline()}
{OD}{ gr:=1; vp:=0; ~~}
{MACRO-}
{COMMENT}------------------------------MACRO-LINIA2-----------------{COMMENT-}
{MACRO: 'LINIA2'}
{PRN2.ini_line()}
{WHILE: PRN2.next()}
{DO}
{REP: PRN2.fline()}
{OD}{ gr:=2; vp:=0; ~~}
{MACRO-}
{COMMENT}------------------------------REPORT HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='SPODZIEWANE WPŁYWY I WYDATKI - STAN NA DZIEŃ %1'@[$WYDRUKIN.DATA_NA];~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}
{LINE: 1}{SPACE: 'C'}
{<1 'PARAMETRY WYDRUKU:'@}
{<1 'Waluta: %1 %2'@[SSTALE.WAL().KOD,SSTALE.WAL().TR]}
{<1 {? OPERATOR.DEPT || 'Jednostka księgowa: %1'@[OPERATOR.DEPT().OD] || 'Wszystkie jednostki księgowe'@ ?}}
{<1 'Terminy płatności - od dnia: %1 do dnia: %2'@[$WYDRUKIN.DATA_OD2,$WYDRUKIN.DATA_DO2]}
{IF: WYDRUKIN.KH<>null}{<1 'Dla kontrahenta: %1 - %2'@[WYDRUKIN.KH().NAZ, WYDRUKIN.KH().SKR]}{FI}
{IF: UNPAR.PSLO<>null}{<1 'Dla projektu: %1 - %2'@[UNPAR.PSLO().KOD,UNPAR.PSLO().TR]}{FI}
{IF: UNPAR.P1=1}
{<1 'Wersja syntetyczna'@}
{FI}
{FONT: 'R'}{SPACE: 'C'}
{IF: UNPAR.P1}
{REP: PRN2.fhbar()}
{PRN2.ini_head()}
{WHILE: PRN2.h_next()}
{DO}
{REP: PRN2.h_fline()}
{OD}
{ELSE}
{REP: PRN1.fhbar()}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}
{DO}
{REP: PRN1.h_fline()}
{OD}
{FI}
{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='SPODZIEWANE WPŁYWY I WYDATKI - STAN NA DZIEŃ %1'@[$WYDRUKIN.DATA_NA];~~}
{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}
{LINE: 1}{FONT: 'R'}{SPACE: 'C'}
{IF: UNPAR.P1}
{REP: PRN2.fhbar()}
{PRN2.ini_head()}
{WHILE: PRN2.h_next()}
{DO}
{REP: PRN2.h_fline()}
{OD}
{ELSE}
{REP: PRN1.fhbar()}
{PRN1.ini_head()}
{WHILE: PRN1.h_next()}
{DO}
{REP: PRN1.h_fline()}
{OD}
{FI}
{ vp:=1; ~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: gr=2}{PRN2.lbar()}{ELSE}{PRN1.lbar()}{FI}
{SUBSTITUTE: 'CHARFOOT'}
{ gr:=0; vp:=1; ~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{COMMENT}---------------przed terminem------------------------------{COMMENT-}
{  {? WYDRUKIN.KH<>null
   || OP.index('KH2'); OP.prefix(SSTALE.WAL,WYDRUKIN.KH)
   || {? OPERATOR.DEPT
      || OP.index('KON_O'); OP.prefix(SSTALE.WAL,OPERATOR.DEPT)
      || OP.index('KON'); OP.prefix(SSTALE.WAL)
      ?}
   ?};
   OP_PROJ.index('SLO'); OP_PROJ.prefix(UNPAR.PSLO);
   petla:=(UNPAR.PSLO=null | OP_PROJ.first()) & OP.first(); ~~}
{WHILE: petla}
{DO}
   {IF: WYDRUKIN.KH=null | ({? OPERATOR.DEPT=null || 1 || OP.ODD=OPERATOR.DEPT ?})}
   {  tp:={? DRB.set_tab(OP.ref(),,WYDRUKIN.DATA_NA) || OP.DO || OP.TZ ?}; ~~}
   {IF: tp<WYDRUKIN.DATA_OD2 & (-(1+OP.TYP)='n' | -(1+OP.TYP)='z')}
   {  {? SSTALE.WAL<>FINFO.NAROD
      || _op_sym:=OP.SYM; _op_an:=OP.AN; _op_odd:=OP.ODD; _usym:=OP.SYM_ROK;
         OP.cntx_psh();
         OP.index('KON_O'); OP.prefix(FINFO.NAROD,_op_odd,_op_an,_op_sym,_op_sym,_usym);
         OP_PROJ.index('OP'); OP_PROJ.prefix();
         {? OP.first() & (UNPAR.PSLO=null | OP_PROJ.find_key(OP.ref(),UNPAR.PSLO))
         || {? DRB.set_tab(OP.ref(),,WYDRUKIN.DATA_NA)
            || _k:=DRB.get_rzap(WYDRUKIN.DATA_OD2-1);
               {? -(1+OP.TYP)='n' || sumdo1+=_k || sumdo2+=_k ?}
            || F.RObr(OP.ref,WYDRUKIN.DATA_NA,UNPAR.PSLO);
               _wn:=F.SWn(F.rwn,F.rma); _ma:=F.SMa(F.rwn,F.rma);
               {? -(1+OP.TYP)='n'
               || sumdo1+={? _wn$2>0 || _wn || (-1)*_ma ?}
               || sumdo2+={? _ma$2>0 || _ma || (-1)*_wn ?}
               ?}
            ?}
         ?};
         OP.cntx_pop();
         F.RObr(OP.ref(),WYDRUKIN.DATA_NA,UNPAR.PSLO);
         _wn:=F.SWn(F.rwn,F.rma); _ma:=F.SMa(F.rwn,F.rma); _iwal:=0;
         {? _wn$2<>0
         || _iwal:={? _wn$2>0 || _wn || (-1)*_ma ?}
         |? _ma$2<>0
         || _iwal:={? _ma$2>0 || _ma || (-1)*_wn ?}
         ?};
         sumdo3+=_iwal
      || {? DRB.EXIST
         || _k:=DRB.get_rzap(WYDRUKIN.DATA_OD2-1);
            {? -(1+OP.TYP)='n' || sumdo1+=_k || sumdo2+=_k ?}
         || F.RObr(OP.ref,WYDRUKIN.DATA_NA,UNPAR.PSLO);
            _wn:=F.SWn(F.rwn,F.rma); _ma:=F.SMa(F.rwn,F.rma);
            {? -(1+OP.TYP)='n'
            || sumdo1+={? _wn$2>0 || _wn || (-1)*_ma ?}
            || sumdo2+={? _ma$2>0 || _ma || (-1)*_wn ?}
            ?}
         ?}
      ?}; ~~}
   {FI}
   {FI}
   { petla:=OP.next(); ~~}
{OD}
{IF:  (vp | gr=1) & ~UNPAR.P1}
{REP: PRN1.fjbar(PRN2)}
{ELSE}
{REP: PRN2.fbar()}
{FI}
{IF: (rep_export() & UNPAR.P1) | ~rep_export()}
{  w[1]:={? WYDRUKIN.KH=null || 'Razem wszyscy kontrahenci do dnia: '@ || 'Razem do dnia: '@ ?}+
         $(WYDRUKIN.DATA_OD2-1);
   w[2]:=form(sumdo1,,2,' ,'); w[3]:=form(sumdo2,,2,' ,');
   w[4]:={? czy_w & ~UNPAR.P1 & SSTALE.WAL<>FINFO.NAROD || form(sumdo3,,2,' ,') || '' ?}; ~~}
{FI}
{SUBSTITUTE: 'LINIA2'}
{COMMENT}---------------po terminie---------------------------------{COMMENT-}
{ razem5:=sumdo1; razem6:=sumdo2; razem7:=sumdo3;~~}
{WHILE: dt<=WYDRUKIN.DATA_DO2}
{DO}
   {  suma5:=suma6:=suma7:=0;
      echo('Wpływy i wydatki na dzień: '+$dt);
      {? WYDRUKIN.KH<>null
      || OP.index('KH2'); OP.prefix(SSTALE.WAL,WYDRUKIN.KH)
      || {? OPERATOR.DEPT
         || OP.index('KON_O'); OP.prefix(SSTALE.WAL,OPERATOR.DEPT)
         || OP.index('KON');   OP.prefix(SSTALE.WAL)
         ?}
      ?};
      OP_PROJ.index('SLO'); OP_PROJ.prefix(UNPAR.PSLO);
      petla:=OP.first() & (UNPAR.PSLO=null | OP_PROJ.first());~~}
   {WHILE: petla}
   {DO}
      {IF: WYDRUKIN.KH=null | ({? OPERATOR.DEPT=null || 1 || OP.ODD=OPERATOR.DEPT ?})}
         {  raty:={? DRB.set_tab(OP.ref(),,WYDRUKIN.DATA_NA) || l_rat:=obj_len(t_drb) ?};
            {? raty || tp:=DRB.get_drat(WYDRUKIN.DATA_NA) || tp:=OP.TZ ?}; swn:=sma:=0; ~~}
         {WHILE: ~raty | l_rat>0}
         {DO}
            {IF: (raty & dt=t_drb[l_rat][3] | ~raty & tp=dt ) & (-(1+OP.TYP)='n' | -(1+OP.TYP)='z')}
               {  {? SSTALE.WAL<>FINFO.NAROD
                  || _op_sym:=OP.SYM; _op_an:=OP.AN; _op_odd:=OP.ODD; _usym:=OP.SYM_ROK;
                     OP.cntx_psh(); OP.index('KON_O'); OP.prefix(FINFO.NAROD,_op_odd,_op_an,_op_sym,_op_sym,_usym);
                     OP_PROJ.index('OP'); OP_PROJ.prefix();
                     {? OP.first() & (UNPAR.PSLO=null | OP_PROJ.find_key(OP.ref(),UNPAR.PSLO))
                     || {? DRB.set_tab(OP.ref(),,WYDRUKIN.DATA_NA)
                        || _k:=t_drb[l_rat][1]-t_drb[l_rat][2];
                           {? -(1+OP.TYP)='n' || swn:=_k || sma:=_k ?}
                        || F.RObr(OP.ref,WYDRUKIN.DATA_NA,UNPAR.PSLO);
                           _wn:=F.SWn(F.rwn,F.rma); _ma:=F.SMa(F.rwn,F.rma);
                           {? -(1+OP.TYP)='n'
                           || swn:={? _wn$2>0 || _wn || (-1)*_ma ?}
                           || sma:={? _ma$2>0 || _ma || (-1)*_wn ?}
                           ?}
                        ?}
                     ?};
                     OP.cntx_pop()
                  || {? DRB.EXIST
                     || _k:=t_drb[l_rat][1]-t_drb[l_rat][2];
                         {? -(1+OP.TYP)='n' || swn:=_k || sma:=_k ?}
                     || F.RObr(OP.ref,WYDRUKIN.DATA_NA,UNPAR.PSLO);
                         _wn:=F.SWn(F.rwn,F.rma); _ma:=F.SMa(F.rwn,F.rma);
                        {? -(1+OP.TYP)='n'
                        || swn:={? _wn$2>0 || _wn || (-1)*_ma ?}
                        || sma:={? _ma$2>0 || _ma || (-1)*_wn ?}
                        ?}
                     ?}
                  ?}; ~~}
               {IF: swn | sma }
                  {  w[1]:=OP.SYM; w[2]:=$dt; w[3]:=OP.KH().NAZ; w[4]:=KH.KOD; w[9]:=$OP.DO; w[10]:=KH.SKR;
                     w[5]:=form(swn,,2,' ,'); suma5+=swn; razem5+=swn; sumod1+=swn;
                     w[6]:=form(sma,,2,' ,'); suma6+=sma; razem6+=sma; sumod2+=sma;
                     w[7]:=w[8]:=''; swal:=0;
                     ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
                     {? ZAP_OP.first()
                     || _op_wal:=null;
                        {? SSTALE.WAL<>FINFO.NAROD
                        || w[7]:=SSTALE.WAL().KOD; _op_wal:=SSTALE.WAL
                        || ZAP_OP.cntx_psh();
                           {! |?
                              {? ZAP_OP.WAL2<>null & exec('czy_nar','waluty',ZAP_OP.WAL2().KOD,0)=0
                              || w[7]:=ZAP_OP.WAL2().KOD; _op_wal:=ZAP_OP.WAL2
                              ?};
                              ZAP_OP.next()
                           !};
                           ZAP_OP.cntx_pop()
                        ?};
                        _op_sym:=ZAP_OP.OP().SYM; _op_an:=OP.AN; _op_odd:=OP.ODD; _usym:=OP.SYM_ROK;
                        {? _op_wal<>null
                        || OP.cntx_psh();
                           OP.index('KON_O'); OP.prefix(_op_wal,_op_odd,_op_an,_op_sym,_op_sym,_usym);
                           {? OP.first()
                           || F.RObr(OP.ref(),WYDRUKIN.DATA_NA,UNPAR.PSLO);
                              _wn:=F.SWn(F.rwn,F.rma); _ma:=F.SMa(F.rwn,F.rma); swal:=0;
                              {? swn$2<>0
                              || swal:={? _wn$2>0 || _wn || (-1)*_ma ?}
                              |? sma$2<>0
                              || swal:={? _ma$2>0 || _ma || (-1)*_wn ?}
                              ?};
                              w[8]:=form(swal,,2,' ,')
                           ?};
                           OP.cntx_pop()
                       ?}
                     ?};
                     suma7+=swal; razem7+=swal; sumod3+=swal;
                  ~~}
                  {IF: ~UNPAR.P1}
{ENTIRE}
{IF: gr=2 & lineleft()>1}
{REP: PRN2.fjbar(PRN1)}
{ELSE}
{REP: PRN1.fbar()}
{FI}
{SUBSTITUTE: 'LINIA1'}
{ENTIRE-}
                  {FI}
               {FI}
            {FI}
            { {? raty || l_rat-=1 || raty:=1 ?}; ~~}
         {OD}
      {FI}
      {  petla:=OP.next(); ~~}
   {OD}
{IF: (rep_export() & UNPAR.P1) | ~rep_export()}
   {IF: ((suma5$2<>0) | (suma6$2<>0)) }
      {  w[1]:='RAZEM DNIA '+$dt;
         w[2]:=form(suma5,,2,' ,');
         w[3]:=form(suma6,,2,' ,');
         w[4]:={? czy_w & ~UNPAR.P1 & SSTALE.WAL<>FINFO.NAROD || form(suma7,,2,' ,') || '' ?};
         w[9]:=''; ~~}
      {ENTIRE}
      {IF: lineleft<>0 & lineleft<2}{PAGE}{FI}
{IF: (vp | gr=1) & ~UNPAR.P1}{REP: PRN1.fjbar(PRN2)}{ELSE}{REP: PRN2.fbar()}{FI}
      {SUBSTITUTE: 'LINIA2'}
      {ENTIRE-}
   {FI}
{FI}
   {  dt+=1; ~~}
{OD}
{COMMENT}---------------podsumowanie--------------------------------{COMMENT-}
{COMMENT}------------------------------FOOTER LAST------------------{COMMENT-}
{IF: lineleft()<>0 & lineleft()<9}{PAGE}{FI}
{  w[1]:='Ogółem do dnia: '+$(WYDRUKIN.DATA_OD2-1);
   w[2]:=form(sumdo1,,2,' ,');
   w[3]:=form(sumdo2,,2,' ,');
   w[4]:={? czy_w & ~UNPAR.P1 & SSTALE.WAL<>FINFO.NAROD || form(sumdo3,,2,' ,') || '' ?}; ~~}
{IF: vp}{REP: PRN1.fjbar(PRN2)}{ELIF: gr=2}{REP: PRN2.fbar()}{ELSE}{REP: PRN1.fjbar(PRN2)}{FI}
{IF: ~rep_export()}{SUBSTITUTE: 'LINIA2'}{FI}
{  w[1]:='Ogółem od: '+$WYDRUKIN.DATA_OD2+' do: '+$WYDRUKIN.DATA_DO2;
   w[2]:=form(sumod1,,2,' ,');
   w[3]:=form(sumod2,,2,' ,');
   w[4]:={? czy_w & ~UNPAR.P1 & SSTALE.WAL<>FINFO.NAROD || form(sumod3,,2,' ,') || '' ?}; ~~}
{REP: PRN2.fbar()}
{IF: ~rep_export()}{SUBSTITUTE: 'LINIA2'}{FI}
{  w[1]:='Ogółem do dnia: '+$WYDRUKIN.DATA_DO2;
   w[2]:=form(razem5,,2,' ,');
   w[3]:=form(razem6,,2,' ,');
   w[4]:={? czy_w & ~UNPAR.P1 & SSTALE.WAL<>FINFO.NAROD || form(razem7,,2,' ,') || '' ?};
~~}
{REP: PRN2.fbar()}
{IF: ~rep_export()}{SUBSTITUTE: 'LINIA2'}{FI}
{  w[1]:='Ogółem saldo:';
   w[2]:=form(F.SWn(razem5,razem6),,2,' ,');
   w[3]:=form(F.SMa(razem5,razem6),,2,' ,');
   w[4]:={? czy_w & ~UNPAR.P1 & SSTALE.WAL<>FINFO.NAROD || form(razem7,,2,' ,') || '' ?};
~~}
{REP: PRN2.fbar()}
{IF: ~rep_export()}{SUBSTITUTE: 'LINIA2'}{FI}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{REP: PRN2.flbar()}

:!UTF-8
{TITLE:D. Zestawienie dniówek pracowników}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   ZLGD.cntx_psh();
   ZLGB.cntx_psh();
   PRN:=obj_new(@.CLASS.PRINT,6);
   PRN.s_frame();
   prn1:='PRN';
   tryb:=rep_export();
   waslbar:=0;
   wyd:='';
   dalej:=0;
   color:='';
   nazwisko:=pierwsze:=pesel:='';
   lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
   ZLGD.cntx_pop();
   ZLGB.cntx_pop();
   VAR_DEL.delete('PRN');
   VAR_DEL.delete('__TAB');
   &dalej;
   &prn1;
   &color;
   &nazwisko;
   &pierwsze;
   &pesel;
   &lm; &ll; &vp; &lmt; &rsep;
   &tryb
}
{COMMENT}
  Wydruk zestawienia dniówek wszystkich pracowników.
   [MK] - 2002/11/29 - [ 7.62]
  Wywołanie: Wykonania produkcji -> Raporty -> Zestawienia
{COMMENT-}
{COMMENT}
  RS [2010]
  Umozliwienie zadawania zakresu dat dla wydruku
{COMMENT-}
{ _wydr_naz:='wyka006';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add();
      WYDRUK.blank();
      WYDRUK.ODDNIA:=date(ST.AR,ST.AM,1);
      WYDRUK.DODNIA:=date(ST.AR,ST.AM,0)
   || WYDRUK.ODDNIA:=WYDRUKIL.OD_DATY;
      WYDRUK.DODNIA:=WYDRUKIL.DO_DATY
   ?};
  WYDRUK.win_edit('RED23');
~~}
{IF: ~WYDRUK.edit("{? WYDRUK.ODDNIA>WYDRUK.DODNIA
                   || FUN.emsg('Proszę podać prawidłowy zakres dat.'@)
                   || ''
                   ?}")}
{EXIT}
{ELSE}
{ WYDRUKIL.OD_DATY:=WYDRUK.ODDNIA;
  WYDRUKIL.DO_DATY:=WYDRUK.DODNIA;
  WYDRUKIL.put(1);
~~}
{FI}
 {INCLUDE: tte_wykx001.rpi}
{COMMENT}--Definicja wiersza wydruku dniówek wszystkich pracowników.-{COMMENT-}
{
   {? tryb=1
   || PRN.add("__TAB.OPIS",30,'Wydział'@)
   ?};
   PRN.add("form(__TAB.DT)",10,'Data'@);
   {? tryb=1
   || PRN.add("__TAB.NAZWISKO",30,'Nazwisko'@);
      PRN.add("__TAB.PIERWSZE",20,'Imię'@)
   || PRN.add("__TAB.NAZWISKO+' '+__TAB.PIERWSZE",30,'Pracownik'@)
   ?};
   PRN.add("form(__TAB.SUMA,,2)",8,'Kwota'@,1);
   PAR_WYDR.TITLE:='ZESTAWIENIE DNIÓWEK PRACOWNIKÓW'@;
~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1}}{'Oddział %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1} 'Zakres dat wydruku: %1 - %2'@[form(WYDRUK.ODDNIA,-2),form(WYDRUK.DODNIA,-2)]}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------MACRA-----------------------{COMMENT-}
{MACRO: 'utwtab'}
{
__TAB:=sql('select
ZLGD.WYD,
ZLGB.P,
sum( ZLGB.KW) as SUMA,
UD_SKL.OPIS,
ZLGD.DT,
OSOBA.NAZWISKO,
OSOBA.PIERWSZE ,
OSOBA.PESEL

from @ZLGB
         join @ZLGD using(ZLGB.ZLGD,ZLGD.REFERENCE)
         join UD_SKL using(ZLGD.WYD,UD_SKL.REFERENCE)
         join P using (ZLGB.P, P.REFERENCE)
         join OSOBA using(P.OSOBA, OSOBA.REFERENCE)
where ZLGD.DT>=to_date(:_a) and ZLGD.DT<=to_date(:_b)
group by ZLGD.WYD,UD_SKL.OPIS,ZLGB.P, OSOBA.NAZWISKO,OSOBA.PIERWSZE,
         OSOBA.PESEL,ZLGD.DT
order by ZLGD.WYD,OSOBA.NAZWISKO, OSOBA.PIERWSZE,ZLGD.DT,SUMA
  ',WYDRUK.ODDNIA,WYDRUK.DODNIA);
     ~~}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}

{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{SUBSTITUTE: 'filtrprac'}
{SUBSTITUTE: 'utwtab'}
{FOR: __TAB}
{DO}
 {nazwisko:=__TAB.NAZWISKO;pierwsze:=__TAB.PIERWSZE;pesel:=__TAB.PESEL;~~}
 {IF: P.f_find(nazwisko,pierwsze,pesel)}
 {FIRST}
     {IF: ~tryb & lineleft<7}
     {PAGE}
     {FI}
     {IF: tryb<>1}{<{lmt+1} 'WYDZIAŁ: %1'@[__TAB.OPIS]}{FI}
     {FONT: 'T10B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1;~~}
     {wyd:=__TAB.WYD;~~}
 {FIRST-}
 {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIA0'}
 {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T10B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
 {FI}
{FI}
{TOTAL: __TAB.WYD}
  {IF: tryb<>1}
     {SUBSTITUTE: 'LINIAF'}
     {IF: wyd<>__TAB.WYD}
        {IF: lineleft<7}
        {PAGE}
        {FI}
        {<{lmt+1} 'WYDZIAŁ: %1'@[__TAB.OPIS]}
        {FONT: 'T10B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1;~~}
        {wyd:=__TAB.WYD;~~}
     {FI}
   {FI}
{OD}
{IF: tryb=1 & __TAB.size()}{SUBSTITUTE: 'LINIAF'}{FI}
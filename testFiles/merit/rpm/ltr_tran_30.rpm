:!UTF-8
{TITLE:H. Ilość zarejestrowanego paliwa w podziale na pojazdy}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('tryb','samr','samt','samm');
   VAR_DEL.delete('k','w','s','PRN','PRNS','s_tran','liczkart');
   k :=obj_new(8); "--szerokosci kolumn--";
   w :=obj_new(8); "--wartosci kolumn--";
   s_tran:=obj_new(8); "--podsumowania--";
   dokl :=2; "--dokladnosc--";
   par1:='';
   PRN :=obj_new(@.CLASS.PRINT,10); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,8); PRNS.s_frame();
   liczkart:=0;
   tryb:=rep_export();
   samr:=samt:=samm:='';

   {! _i:=1..8 |! s_tran[_i]:=0 !};

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=0;rsep:=1;

   k[1] :=3;
   k[2] :=10;
   k[3] :=10;
   k[4] :=10;
   k[5] :=10;
   k[6] :=13;
   k[7] :=13;
   k[8] :=13;

   POJAZDY.win_sel('SLO_J');
   ODDZ.index('KOD2');
   ODDZ.prefix();
   oddz:=KARTDROG.ODDZ;
   KARTDROG.WYBKART:='Z';
   KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   KARTDROG.WYB_STAT:='W';
   KARTDROG.win_edit('DRUK');
   {? KARTDROG.edit("{? KARTDROG.WYBKART='Z' & KARTDROG.KARTY_DO<KARTDROG.KARTY_OD
                     || FUN.info('Błędny zakres dat.');
                        'KARTY_DO'
                     || ''
                     ?}")
   || wyjscie:=0
   || wyjscie:=1
   ?}
}
{END:
   VAR_DEL.delete('tryb','samr','samt','samm');
   VAR_DEL.delete('k','w','s','PRN','PRNS','s_tran','liczkart','par1');
   VAR_DEL.delete('oddz','dokl','wyjscie');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1
|| PRN.add("form(w[1])",k[1],'Lp.')
|| PRN.add("form(samr)",15,'Samochód (Rejestracja)');
   PRN.add("form(samt)",15,'Samochód (Nazwa)');
   PRN.add("form(samm)",15,'Samochód (Marka)')
?};
PRN.add("form(w[2])",k[2],'Data');
PRN.add("form(w[3])",k[3],'Karta');
PRN.add("form(w[4])",k[4],'Rodzaj paliwa');
PRN.add("form(w[5])",k[5],'Miejsce tankowania');
PRN.add("form(w[6],,dokl,' ,')",k[6],'Ilość',1);
PRN.add("form(w[7],,dokl,' ,')",k[7],'Cena',1);
PRN.add("form(w[8],,dokl,' ,')",k[8],'Wartość',1);

PRNS.add("form(s_tran[1])",55,'Razem:');
PRNS.add("form(s_tran[6],,dokl,' ,')",k[6],'Ilość',1);
PRNS.add("form(s_tran[7],,dokl,' ,')",k[7],'Cena',1);
PRNS.add("form(s_tran[8],,dokl,' ,')",k[8],'Wartość',1);
{? KARTDROG.WYBKART='Z'
|| PAR_WYDR.TITLE:='Ilość zarejestrowanego paliwa\nw okresie od: '+$KARTDROG.KARTY_OD+' do: '+$KARTDROG.KARTY_DO
|? KARTDROG.WYBKART='M'
|| PAR_WYDR.TITLE:='Ilość zarejestrowanego paliwa (+ zbiornik dodatkowy)\nza okres: '+date(KARTDROG.AR,KARTDROG.AM,1)$8
|| _od:=date(KARTDROG.AR,KARTDROG.AM,1);
   _do:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || _od:=SAMK.D;
      SAMK.last();
      _do:=SAMK.D
   ?};
   PAR_WYDR.TITLE:='Ilość zarejestrowanego paliwa\nw okresie od: '+$_od+' do: '+$_do
?};
{? KARTDROG.WYBKART='M'
|| KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0)
|? KARTDROG.WYBKART='W'
|| KARTDROG.KARTY_OD:=date(KARTDROG.AR,KARTDROG.AM,1);
   KARTDROG.KARTY_DO:=date(KARTDROG.AR,KARTDROG.AM,0);
   SAMK.index('D2');
   SAMK.prefix();
   {? SAMK.first()
   || KARTDROG.KARTY_OD:=SAMK.D;
      SAMK.last();
      KARTDROG.KARTY_DO:=SAMK.D
   ?}
?};
prn1:='PRN'
;~~}
{IF: POJAZDY.index('NRREJ'); POJAZDY.prefix(REF.FIRMA); POJAZDY.first()=0}{ FUN.info('Brak danych spełniających kryteria.');~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(oddz,oddz); {? ODDZ.first() || ODDZ.NAZ ?})}
{ {? KARTDROG.WYB_STAT='W'
  || par1:='wszystkie'
  |? KARTDROG.WYB_STAT='T'
  || par1:='zatwierdzone'
  |? KARTDROG.WYB_STAT='N'
  || par1:='niezatwierdzone'
  ?}
;~~}
{<{lmt+1}}{'Karty drogowe: '+par1}
{IF: tryb<>1}
{<{lmt+1} POJAZDY.NRREJ+' '+POJAZDY.NAZ+' '+POJAZDY.MAR().NAZ}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FOR:'POJAZDY'}{INDEX:'NRREJ'}{PREFIX: REF.FIRMA}
{SELECT}
{DO}
   {w[1]:=0;s_tran[6]:=s_tran[8]:=0;liczkart:=0;vp:=1;
    samr:=POJAZDY.NRREJ;
    samt:=POJAZDY.NAZ;
    samm:=POJAZDY.MAR().NAZ;~~}
   {FOR:'SAMK'}{INDEX:'D'}{PREFIX:POJAZDY.ref()}
   {DO}
      {IF: (SAMK.D>=KARTDROG.KARTY_OD & SAMK.D<=KARTDROG.KARTY_DO) & (KARTDROG.WYB_STAT='W' | SAMK.STATUS=KARTDROG.WYB_STAT)}
         {FOR: 'SAMP' }
         {INDEX: 'SAMK'}
         {PREFIX: SAMK.ref()}
         {DO}
            {
            w[1]+=1;
            w[2]:=SAMP.SAMK().D;
            w[3]:=SAMP.SAMK().SYM;
            w[4]:=SAMP.PAL().KOD;
            w[5]:=SAMP.TAN().KOD;
            w[6]:=SAMP.IL$2;
            w[7]:=SAMP.C;
            w[8]:=SAMP.W$2;

            s_tran[6]+=w[6];
            s_tran[8]+=w[8];

            liczkart+=1
            ;~~}
            {FONT: 'T8G'}{SPACE: 'C'}
            {prn1:='PRN';prn2:='PRN';~~}
            {SUBSTITUTE:'LINIA0'}
         {OD}
      {FI}
  {OD}
  {IF: tryb<>1}
  {IF: liczkart>0}
     {FONT: 'T8GB'}{SPACE: 'C'}
     {prn1:='PRN';prn2:='PRNS';
      s_tran[1]:='Razem:';
      s_tran[7]:=''
      ;~~}
     {SUBSTITUTE: 'LINIA02'}
     {FONT: 'T8G'}{prn1:='PRNS';prn2:='PRN';vp:=0;s_tran[6]:=s_tran[8]:=0;~~}
  {FI}
  {IF: lineleft()>0}{PAGE}{FI}
  {FONT: 'T8G'}{prn1:='PRN';prn2:='PRN';~~}
  {FI}
{OD}
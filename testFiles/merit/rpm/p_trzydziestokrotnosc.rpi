:!UTF-8
{BEGIN:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','__TABPAR','koniec','__TABTRZ');

   koniec:=0;
   {? dziedzina='PPL'
   || _upr:=exec('uprawnieniawrap','pkd','\n%1\n%2'['Naliczenie listy płac'@,'Wydruk kartotek kadrowych'@],
                 'PPL_PLL_NALS','PPL_ZES_WKAR')
   || _upr:=exec('uprawnieniawrap','pkd','\n%1\n%2'['Przeglądanie danych ubezp.'@,'Wydruk kartotek kadrowych'@],
                 'PKD_ZES_ORKA','PKD_DOS_PPDU');
      {? +_upr
      || _upr:=exec('uprawnieniawrap','pkd','\n%1\n%2'['Rejestracja danych ubezp.'@,'Wydruk kartotek kadrowych'@],
                    'PKD_ZES_ORKA','PKD_DOS_PRDU')
      ?}
   ?};
   {? +_upr
   || koniec:=1;
      FUN.emsg('Brak wymaganych uprawnień do czynności: %1'@[_upr])
   || PAR_WYDR.TITLE:='Przekroczenia 30-krotności podstawy składek ZUS'@;
      'Okienko edycji parametrów wydruku:';
      _dzis:=date();
      __TABPAR:=exec('par_wydr_ddp','pkd',
      PAR_WYDR.TITLE,
      "  {? cur_tab().OD<>#0 & cur_tab().DO<>#0
         || {? cur_tab().OD>cur_tab().DO || FUN.emsg('Niepoprawny zakres dat.'@); 'OD' || 1 ?}
         || FUN.emsg('Niepoprawna data.'@);
            {? cur_tab().OD=#0 || 'OD' || 'DO' ?}
         ?}
      ",
      date(_dzis~1,1,1),'Od daty'@,"1",
      _dzis,'Do daty'@,"1"
      );

      {? __TABPAR.first()
      || 'Wybór pracowników do sprawdzenia:';
         _tabWyb:=exec('wybierz_parses','pracownik',dziedzina).P;
         'Sprawdzenie blokad i przygotowanie tabeli z danymi:';
         _query:='select distinct '
                        'P.T as T, '
                        'P.REFERENCE as PREF, '
                        'OSOBA.REFERENCE as OSREF, '
                        'OSOBA.PIERWSZE as PIERWSZE, '
                        'OSOBA.NAZWISKO as NAZWISKO, '
                        'P_INFO.BLOKADA as BLOKADA, '
                        'S_ZUS.KOD as KOD, '
                        'S_ZUS.LINIA as LINIA '
                 'from P '
                 'join OSOBA using(P.OSOBA, OSOBA.REFERENCE) '
                 'join P_INFO using(OSOBA.REFERENCE, P_INFO.OSOBA) '
                 'join S_ZUS using(P_INFO.BLWN, S_ZUS.REFERENCE) '
                 'where '
                        'P.REFERENCE in (select :_c.SQL from :_c) '
                        'and P_INFO.FIRMA=:_d '
                        'and (P_INFO.BLOKADA between to_date(:_a) and to_date(:_b) ) '
                 'order by NAZWISKO, PIERWSZE';
         __TABTRZ:=sql(_query,__TABPAR.OD,__TABPAR.DO,_tabWyb,exec('ref_firma','#firma'));
         'Jeżeli nie znaleziono pracowników spełniających kryteria:';
         {? ~__TABTRZ.size()
         || koniec:=1;
            FUN.info('Brak pracowników spełniających kryteria.'@);
            return(0)
         ?};

         PRN:=obj_new(@.CLASS.PRINT,7);
         PRN.add("__TABTRZ.T",MS.fld_len(P,'T'),'Nr teczki'@,1);
         PRN.add("__TABTRZ.NAZWISKO",MS.fld_len(OSOBA,'NAZWISKO'),'Nazwisko'@);
         PRN.add("__TABTRZ.PIERWSZE",MS.fld_len(OSOBA,'PIERWSZE'),'Imię'@);
         PRN.add("__TABTRZ.BLOKADA$1",10,'Blokada od'@);
         PRN.add("__TABTRZ.KOD",7,'Na wniosek (kod)'@);
         PRN.add("gsub(__TABTRZ.LINIA,
               'w przypadku gdy informację o przekroczeniu rocznej podstawy wymiaru składek przekazał ','')",
               15,'Wniosek przekazał'@);
         PRN.add("_email:=exec('users_p_email','personel_alerty',__TABTRZ.PREF);
                  {? _email='' || _email:=exec('email','osoba',__TABTRZ.OSREF) ?};
                  _email
                 ",20,'Adres e-mail'@);
         PRN.s_frame();

         prn1:='PRN';
         lmt:=0;
         rsep:=vp:=1
      || koniec:=1
      ?}
   ?}
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','__TABPAR','koniec','__TABTRZ','dziedzina')
}
{EXIT: koniec}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{COMMENT} Parametry sprawozdania: {COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{<{ceil(lmt)+3}'Parametry sprawozdania'@}
{<{ceil(lmt)+3}'Data od:'@} { __TABPAR.OD }
{<{ceil(lmt)+3}'Data do:'@} { __TABPAR.DO }
{FONT: 'T8B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: __TABTRZ}
{DO}
   {FONT: 'T8'}
   {SPACE: 'B'}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
========================================================================================================================
Nazwa pliku: p_trzydziestokrotnosc.rpi
Utworzony: 14.01.2020
========================================================================================================================
Zawartość: Zestawienie pracowników, którzy przekroczyli 30-krotność podstawy składek ZUS.
{COMMENT-}
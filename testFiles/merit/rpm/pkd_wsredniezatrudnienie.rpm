:!UTF-8
{TITLE:Średnie zatrudnienie}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','linie');
   tresc:='wstat';
   PAR_WYDR.TITLE:='Średnie zatrudnienie'@;
   par:=obj_new(12);
   par[6]:='';
   par[10]:=0;
   _upr:=exec('uprawnieniawrap','pkd','danych osobowych'@,'ZWS_DOS_PPDO','ZWS_DOS_PRDO');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień pozwalających na przeglądanie informacji o: %1'@[_upr])
   || par[12]:=exec('pkd_sr_zat','!pkd_zes_orka',PAR_WYDR.TITLE);
      {? par[10]:=par[12].size()
      || par[6]:={? par[12].UB || 'B' || '' ?}+{? par[12].SW || 'O' || '' ?}+{? par[12].UW || 'W' || '' ?};
         {? par[12].RODZ=1
         || par[1]:=obj_new(@.CLASS.PRINT,2);
            par[1].add("par[8][2]",50,'Stanowisko'@);
            par[1].add("par[8][4]",15,'Średnie zatrudnienie'@,1,,,,,'12,2');
            par[1].s_frame()
         |? par[12].RODZ=2
         || par[1]:=obj_new(@.CLASS.PRINT,2);
            par[1].add("par[8][2]",16,'Jednostka organizacyjna'@);
            par[1].add("par[8][4]",15,'Średnie zatrudnienie'@,1,,,,,'12,2');
            par[1].s_frame()
         || par[1]:=obj_new(@.CLASS.PRINT,3);
            par[1].add("par[8][2]",16,'Jednostka organizacyjna'@);
            par[1].add("par[8][3]",50,'Stanowisko'@);
            par[1].add("par[8][4]",15,'Średnie zatrudnienie'@,1,,,,,'12,2');
            par[1].s_frame()
         ?};

         vp:=h_rat:=par[5]:=1;
         par[8]:=obj_new(6);
         par[8][5]:=par[8][6]:='';
         prn1:='par[1]';
         lmt:=rsep:=linie:=0
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','linie')
}
{EXIT: ~par[10]}
{COMMENT}OLD: wstat.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Forma współpracy: '@+__F_ZATR.OPIS}
   {<{ceil(lmt*h_rat)+1}'Okres sprawozdania: '@+$par[12].OD+' - '+$par[12].DO}
   {<{ceil(lmt*h_rat)+1}'Sposób prezentacji: '@+
   {? par[12].SN=1 || 'w Osobach'@ || 'w Etatach'@ ?}}
   {IF: par[6]<>''}
   {<{ceil(lmt*h_rat)+1}'Pomijane wartości:  '@+
      {? par[6]*'B' || 'urlop bezpłatny, '@  || '' ?}+
      {? par[6]*'O' || 'służba wojskowa, '@  || '' ?}+
      {? par[6]*'W' || 'urlop wychowawczy,'@ || '' ?}-1}
   {FI}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{REP: par[1].fhbar(lmt)}
{HEADER-}
{FOOTER:0}
{FONT: 'T8'}
{SPACE: 'B'}
{REP: par[1].flbar(lmt)}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'calosc'}
{ par[9]:=page(); ~~ }
{FOR: par[2]}{DO}
   {IF: par[9]<>page()}
      { par[8][5]:=par[8][6]:=''; par[9]:=page(); ~~ }
   {FI}
   {  par[8][1]:=par[8][2]:=par[2].WYDZIAL;
      par[8][3]:=par[2].ST;
      {? par[12].SN=1 || par[8][4]:=par[2].ILOSC$2 || par[8][4]:=par[2].ETAT$2 ?};
      {? par[8][1]=par[8][5]
      || par[8][1]:=''
      ?};
      {? ~rep_export()
      || {? par[8][2]<>par[8][6]
         || par[8][2]:=par[8][6]:=par[2].WYDZIAL
         || par[8][2]:=''
         ?}
      || par[8][2]:=par[8][6]:=par[2].WYDZIAL
      ?};

      par[1].ini_line(); ~~
   }
   {WHILE: par[1].next()}{DO}
      {REP: par[1].fline(lmt)}
   {OD}
{OD}
{MACRO-}
{MACRO: 'czesc'}
{ par[9]:=page(); ~~ }
{FOR: par[11]}{DO}
   {  {? par[9]<>page()
      || par[8][5]:=par[8][6]:='';
         par[9]:=page()
      ?};
      {? par[12].RODZ=2
      || par[8][2]:=par[11].WYDZIAL
      || par[8][2]:=par[11].ST
      ?};
      par[8][1]:=par[8][2];
      {? par[12].SN=1 || par[8][4]:=par[11].ILOSC$2 || par[8][4]:=par[11].ETAT$2 ?};
      {? par[8][1]=par[8][5]
      || par[8][1]:=''
      ?};
      par[1].ini_line(); ~~
   }
   {WHILE: par[1].next()}{DO}
      {REP: par[1].fline(lmt)}
   {OD}
{OD}
{MACRO-}
{MACRO: tresc}
{IF: par[12].RODZ=1 | par[12].RODZ=2}
   {  par[11]:=sql(
         {? par[12].RODZ=2
         || 'select   WYDZIAL, sum(ILOSC) as ILOSC, sum(ETAT) as ETAT from :_a group by WYDZIAL'
         || 'select ST, sum(ILOSC) as ILOSC, sum(ETAT) as ETAT from :_a group by ST'
         ?},par[2]); ~~
   }
   {SUBSTITUTE: 'czesc'}
{ELSE}
   {SUBSTITUTE: 'calosc'}
{FI}
{IF: lineleft() & lineleft()<>linie}
   {REP: par[1].fcbar(lmt)}
{FI}
{IF: ~rep_export()}
   {  par[8][1]:='Ogółem'@;
      par[8][2]:='';
      par[8][3]:='';
      {? par[12].SN=1
      || par[8][4]:=(sql('select sum(ILOSC) as SREDNIA from :_a',par[2]).SREDNIA)$2
      || par[8][4]:=(sql('select sum(ETAT) as SREDNIA from :_a',par[2]).SREDNIA)$2
      ?};
      par[1].ini_line(); ~~
   }
{REP: par[1].fline(lmt)}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{  linie:=lineleft();
   lmt:=int((charleft()-par[1].width())/2);
   par[2]:=exec('calosc','!pkd_zes_orka',par[12].OD,par[12].DO,par[6]);
   par[7]:={? var_pres('[2]',par)=type_of(0) || 0 || 1 ?}; ~~
}
{IF: par[7]}
   {SUBSTITUTE: tresc}
{FI}

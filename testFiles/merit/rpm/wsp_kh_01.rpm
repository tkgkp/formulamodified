:!UTF-8
{TITLE:B. Marża za dany okres}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   fir_01:="
      VAR_DEL.delete('k','w','s','sg','PRN');
      VAR_DEL.delete('doklw','doklp','X_Xw1');
      VAR_DEL.delete('kh','kh_kod');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('jakadata','wydr_naz','wyjscie')
   ";
   exec('upr_set','ceny');
   fir_01();
   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   k := obj_new(9); "--szerokosci kolumn--";
   w := obj_new(9); "--wartosci kolumn--";
   s :=obj_new(4); {! _ii..4 |! s[_ii]  :=0 !}; "--razem--";
   sg:=obj_new(4); {! _ii..4 |! sg[_ii] :=0 !}; "--razem grupa--";
   doklw :=2; "--dokladnosc wartosci--";
   doklp :=2; "--dokladnosc procentów--";
   PRN := obj_new(@.CLASS.PRINT,9); PRN.s_frame();

   k[1] :=10;
   k[2] :=20;
   k[3] :=8;
   k[4] :=14;
   k[5] :=14;
   k[6] :=14;
   k[7] :=14;
   k[8] :=14;
   k[9] :=14;
   {! _ii:=1..8 |! w[_ii] :='' !};

   kh:={? var_pres('wydr_edi')=2 & wydr_edi='FIRA02' || KH.ref() || null ?};
   kh_kod:={? var_pres('wydr_edi')=2 & wydr_edi='FIRA02' || KH.KOD || '' ?};

   jakadata:=WYDRUKIL.RADIOB_1:=0;

   wydr_naz:='FIR_01';
   wydr_edi:='FIR_01';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OZ.USER,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OZ.USER;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? exec('upr_cm','ceny') & WYDRUKIL.edit()
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         jakadata:=WYDRUKIL.RADIOB_1
      ?}
   ?}
}
{END:
   fir_01(); VAR_DEL.delete('wydr_edi','fir_01')
}
{IF: __PARSES.setEnv('LSP_CEN')=0}{EXIT}{FI}
{IF: exec('upr_cm','ceny')=0}{ FUN.info('Brak uprawnień do wartości magazynowych.'@);~~}{EXIT}{FI}
{EXIT: wyjscie}
{
PRN.add("w[1]",k[1],'Kontrahent'@);
PRN.add("w[2]",k[2],'Nazwa kontrahenta'@);
PRN.add("w[3]",k[3],'Odbiorca'@);
PRN.add("w[4]",k[4],'Wartość sprz. mies.'@,1);
PRN.add("w[5]",k[5],'Dochód mies.'@,1);
PRN.add("w[6]",k[6],'%% dochód mies.'@,1);
PRN.add("w[7]",k[7],'Wartość sprz. rok'@,1);
PRN.add("w[8]",k[8],'Dochód rok'@,1);
PRN.add("w[9]",k[9],'%% dochód rok'@,1);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'GRUPA'     ,'STRING[20]'  ,'GRUPA',
   'KOD_KH'    ,'STRING[10]'  ,'KOD_KH',
   'KOD_ODB'   ,'STRING[10]'  ,'KOD_ODB',
   'NAZ_KH'    ,'STRING[60]'  ,'NAZ_KH',
   'FAK_ROK'   ,'REAL'        ,'FAK_ROK',
   'MAG_ROK'   ,'REAL'        ,'MAG_ROK',
   'FAK_MC'    ,'REAL'        ,'FAK_MC',
   'MAG_MC'    ,'REAL'        ,'MAG_MC');
X_Xw1.index(X_Xw1.ndx_tmp(,,'GRUPA',,,'KOD_KH',,,'KOD_ODB',,,'KOD_KH',,));

KH.cntx_psh();
OKR.cntx_psh;
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
FAKS.cntx_psh();
{? OKR.first
||
   {!
   |?
      FAKS.use((5+FAKS.name)+ST.ODDZ+($OKR.ROK+2));
      FAKS.index('FAK_KH'); FAKS.prefix('S',kh_kod);
      {? FAKS.first()
      ||
         {!
         |? {? (kh=null | FAKS.KH=kh) & FAKS.AKC='T' & FAKS.T().ZEST='T'
               & {? jakadata || FAKS.D~1 || FAKS.DW~1 ?}=ST.AR
            ||
               X_Xw1.prefix(FAKS.KH().GRKH().KOD,KH.KOD,FAKS.KH_ODB().KOD,KH.KOD);
               {? ~X_Xw1.first()
               ||
                  X_Xw1.blank();
                  X_Xw1.GRUPA   :=KH.GRKH().KOD;
                  X_Xw1.KOD_KH  :=KH.KOD;
                  X_Xw1.KOD_ODB :=FAKS.KH_ODB().KOD;
                  X_Xw1.NAZ_KH  :={? X_Xw1.KOD_ODB<>'' || KH.SKR+' - '+KH_ODB.MIASTO || KH.NAZ ?};
                  X_Xw1.add()
               ?};
               X_Xw1.FAK_ROK +=FAKS.NETTO;
               {? {? jakadata || FAKS.D~2 || FAKS.DW~2 ?}=ST.AM || X_Xw1.FAK_MC+=FAKS.NETTO ?};
               X_Xw1.put()
            ?};
            FAKS.next()
         !}
      ?};
      OKR.next
   !}
?};
FAKS.cntx_pop();
OKR.cntx_pop;

ND.cntx_psh(); ND.index('KH');
{? kh || ND.prefix(kh) || ND.prefix() ?};
{? ND.first()
||
   {!
   |?
      {? ND.TYP().Z='T' & (( ND.TYP().P='T' & ND.TYP().DN='T') | (ND.TYP().P='N' )) & ND.Z='T'
      ||
         X_Xw1.prefix(ND.KH().GRKH().KOD,KH.KOD,ND.KH_ODB().KOD,KH.KOD);
         {? ~X_Xw1.first()
         ||
            X_Xw1.blank();
            X_Xw1.GRUPA   :=KH.GRKH().KOD;
            X_Xw1.KOD_KH  :=KH.KOD;
            X_Xw1.KOD_ODB :=ND.KH_ODB().KOD;
            X_Xw1.NAZ_KH  :={? X_Xw1.KOD_ODB<>'' || KH_ODB.NAZ || KH.NAZ ?};
            X_Xw1.add()
         ?};
         X_Xw1.MAG_ROK +={? ND.TYP().P='N' || ND.WAR || -ND.WAR ?};
         {? ND.AM=ST.AM || X_Xw1.MAG_MC +={? ND.TYP().P='N' || ND.WAR || -ND.WAR ?} ?};
         X_Xw1.put()
      ?};
      ND.next()
   !}
?};
ND.cntx_pop();
KH.cntx_pop();

PAR_WYDR.TITLE:='Marża za dany okres'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{(ODDZ.index('KOD2');ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || 'Oddział %1'@[ODDZ.NAZ] ?})}
{<{lmt+1}}{'Okres: %1'@[date(ST.AR,ST.AM,1)$8]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozcycje wydruku                                         {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;  ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xw1}
{DO}
   {FIRST}{ kolejny:=0;~~}{FIRST-}
   {IF: kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {
   w[1] :=form(X_Xw1.KOD_KH);
   w[2] :=form(X_Xw1.NAZ_KH);
   w[3] :=form(X_Xw1.KOD_ODB);
   w[4] :=form(X_Xw1.FAK_MC,,doklw,'.,');
   w[5] :=form(X_Xw1.FAK_MC-X_Xw1.MAG_MC,,doklw,'.,');
   w[6] :={? X_Xw1.FAK_MC=0 || 0 || (X_Xw1.FAK_MC-X_Xw1.MAG_MC)/X_Xw1.FAK_MC*100 ?}; w[6] :=form(w[6],,doklw,'.,');
   w[7] :=form(X_Xw1.FAK_ROK,,doklw,'.,');
   w[8] :=form(X_Xw1.FAK_ROK-X_Xw1.MAG_ROK,,doklw,'.,');
   w[9] :={? X_Xw1.FAK_ROK=0 || 0 || (X_Xw1.FAK_ROK-X_Xw1.MAG_ROK)/X_Xw1.FAK_ROK*100 ?}; w[9] :=form(w[9],,doklw,'.,');
   s[1] +=X_Xw1.FAK_MC;  sg[1] +=X_Xw1.FAK_MC;
   s[2] +=X_Xw1.MAG_MC;  sg[2] +=X_Xw1.MAG_MC;
   s[3] +=X_Xw1.FAK_ROK; sg[3] +=X_Xw1.FAK_ROK;
   s[4] +=X_Xw1.MAG_ROK; sg[4] +=X_Xw1.MAG_ROK
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
   { kolejny:=0; rsep:=PAR_WYDR.RSEP;~~}
   {TOTAL: X_Xw1.GRUPA}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
      {
      w[1] :=form(X_Xw1.KOD_KH);
      w[2] :=form(X_Xw1.NAZ_KH);
      w[3] :=form(X_Xw1.KOD_ODB);
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+3
      ;~~}
      {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {
      kolejny:=1; rsep:=0;
      PRN.n_frame();
      w[1] :='Razem:'@; PRN.FORM[1]:=0;
      w[2] :={? grp_fld()='' || 'bez grupy'@ || grp_fld() ?};
      w[3] :='';
      w[4] :=form(sg[1],,doklw,'.,');
      w[5] :=form(sg[1]-sg[2],,doklw,'.,');
      w[6] :={? sg[1]=0 || 0 || (sg[1]-sg[2])/sg[1]*100 ?}; w[6] :=form(w[6],,doklw,'.,');
      w[7] :=form(sg[3],,doklw,'.,');
      w[8] :=form(sg[3]-sg[4],,doklw,'.,');
      w[9] :={? sg[3]=0 || 0 || (sg[3]-sg[4])/sg[3]*100 ?}; w[9] :=form(w[9],,doklw,'.,')
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}
      {FONT: 'T8G'}{SPACE: 'C'}
      {
      {! _ii..4 |! sg[_ii]:=0 !};
      PRN.s_frame();
      PRN.FORM[1] :=1
      ;~~}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
w[1] :='Razem:'@;PRN.FORM[1] :=0;
w[2] :='';
w[3] :='';
w[4] :=form(s[1],,doklw,'.,');
w[5] :=form(s[1]-s[2],,doklw,'.,');
w[6] :={? s[1]=0 || 0 || (s[1]-s[2])/s[1]*100 ?}; w[6] :=form(w[6],,doklp,'.,');
w[7] :=form(s[3],,doklw,'.,');
w[8] :=form(s[3]-s[4],,doklw,'.,');
w[9] :={? s[3]=0 || 0 || (s[3]-s[4])/s[3]*100 ?}; w[9] :=form(w[9],,doklp,'.,');
PRN.n_frame()
;~~}
{SUBSTITUTE: 'LINIA0'}
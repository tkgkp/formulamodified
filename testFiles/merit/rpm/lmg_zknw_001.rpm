:!UTF-8
{TITLE:A. Koszty kontraktu w podziale na zamówienia wewnętrzne}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('w','s','sg','PRN','tryb','kon');
   w:=obj_new(6); "--wartosci kolumn--";
   s:=obj_new(3); {! _ii..3  |! s[_ii]:=0 !}; "--razem--";
   sg:=obj_new(3); {! _ii..3 |! sg[_ii]:=0 !}; "--razem grupa--";
   doklw:=2; "--dokladnosc wartosci--";
   lp:=0;
   PRN:=obj_new(@.CLASS.PRINT,6); PRN.s_frame();
   tryb:=rep_export();
   kon:='';

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   wyjscie:={? exec('upr_cm','ceny') || exec('filtr_zkon','kontrahent','T') ?}
}
{END:
   VAR_DEL.delete('w','s','sg','PRN','tryb','kon');
   VAR_DEL.delete('lp','wyjscie','doklw','X_Xf1','X_Xw1');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsep')
}
{IF: exec('upr_cm','ceny')=0}{ FUN.info('Brak uprawnień do wartości magazynowych.'@);~~}{EXIT}{FI}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1 || PRN.add("kon",5,'Kontrakt'@) ?};
{? tryb<>1 || PRN.add("w[1]",5,'Lp.'@,1) ?};
PRN.add("w[2]",20,'Zamówienie'@);
PRN.add("w[3]",40,'Zlecenie'@);
PRN.add("w[4]",15,'Koszty przych.'@,1);
PRN.add("w[5]",15,'Koszty rozch.'@,1);
PRN.add("w[6]",15,'Koszty suma'@,1);

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'ZKON'   ,'STRING[20]'  ,'Symbol kontraktu',
   'ZK'     ,'STRING[20]'  ,'Symbol zamówienia',
   'KH'     ,'STRING[70]'  ,'Kontrahent',
   'KP'     ,'REAL'        ,'Koszty przychodów',
   'KR'     ,'REAL'        ,'Koszty rozchodów',
   'KS'     ,'REAL'        ,'Koszty suma');
X_Xw1.index(X_Xw1.ndx_tmp(,,'ZKON',,,'ZK',,));

{? X_Xf1.first()
||
   OKR.cntx_psh();
   OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
   {!
   |? {? OKR.first()
      ||
         "--obliczenia w masce zbiorczej--";
         ZK_N.index('ZKON'); ZK_N.prefix(BB.sqlint(X_Xf1.REF));
         {? ZK_N.first()
         ||
            {!
            |? {? ZK_N.STAT_REJ<>'A' & ZK_N.T().R='W'
               || BEER.ZK_N:=ZK_N.ref();
                  exec('s_dokmag','zamsiw_rea');
                  X_Xw1.ZKON:=X_Xf1.SYM;
                  X_Xw1.ZK:=ZK_N.SYM;
                  X_Xw1.KH:={? ZK_N.T().R='W' || ZK_N.ZL().SYM || ZK_N.KH().KOD+' '+KH.NAZ ?};
                  X_Xw1.KP:=DISP.Z_P;
                  X_Xw1.KR:=DISP.Z_R;
                  X_Xw1.KS:=DISP.Z_S;
                  X_Xw1.add()
               ?};
               ZK_N.next()
            !}
         ?};
         "--obliczenia w maskach archiwalnych--";
         ZK_N.cntx_psh(); ZK_RN.cntx_psh();
         {!
         |? _maska:=ST.ODDZ+form(form(OKR.ROK,,,'99'),-2);
            ZK_N.use('zknag'+_maska);
            ZK_RN.use('zkhin'+_maska);
            ZK_N.index('ZKON'); ZK_N.prefix(BB.sqlint(X_Xf1.REF));
            {? ZK_N.first()
            ||
               {!
               |? {? ZK_N.STAT_REJ<>'A' & ZK_N.T().R='W'
                  || BEER.ZK_N:=ZK_N.ref();
                     exec('s_dokmag','zamsiw_rea');
                     X_Xw1.ZKON:=X_Xf1.SYM;
                     X_Xw1.ZK:=ZK_N.SYM;
                     X_Xw1.KH:={? ZK_N.T().R='W' || ZK_N.ZL().SYM || ZK_N.KH().KOD+' '+KH.NAZ ?};
                     X_Xw1.KP:=DISP.Z_P;
                     X_Xw1.KR:=DISP.Z_R;
                     X_Xw1.KS:=DISP.Z_S;
                     X_Xw1.add()
                  ?};
                  ZK_N.next()
               !}
            ?};
            OKR.next()
         !};
         ZK_N.cntx_pop(); ZK_RN.cntx_pop()
      ?};
      X_Xf1.next()
   !};
   OKR.cntx_pop()
?};

PAR_WYDR.TITLE:='Koszty kontraktu w podziale na zamówienia wewnętrzne'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział: %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: 'X_Xw1'}
{DO}
   {FIRST}{ kolejny:=lp:=0;~~}{FIRST-}
   {IF: kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {
   exec('s_dokmag','zamsiw_rea');
   lp+=1;
   w[1]:=form(lp);
   w[2]:=form(X_Xw1.ZK);
   w[3]:=form(X_Xw1.KH);
   w[4]:=form(X_Xw1.KP,,doklw,'.,');
   w[5]:=form(X_Xw1.KR,,doklw,'.,');
   w[6]:=form(X_Xw1.KS,,doklw,'.,');
   kon :=form(X_Xw1.ZKON);
   sg[1]+=X_Xw1.KP; s[1]+=X_Xw1.KP;
   sg[2]+=X_Xw1.KR; s[2]+=X_Xw1.KR;
   sg[3]+=X_Xw1.KS; s[3]+=X_Xw1.KS
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
   { kolejny:=0; rsep:=PAR_WYDR.RSEP;~~}
   {TOTAL: X_Xw1.ZKON}
      {IF: tryb<>1}
      {
      w[1]:=form(X_Xw1.ZK);
      w[2]:=form(X_Xw1.KH);
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+3
      ;~~}
      {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {
      kolejny:=1; rsep:=0;
      PRN.n_frame();
      w[1]:=w[2] :='';
      w[3]:='Razem %1:'@[grp_fld()]; PRN.FORM[3]:=0;
      w[4]:=form(sg[1],,doklw,'.,');
      w[5]:=form(sg[2],,doklw,'.,');
      w[6]:=form(sg[3],,doklw,'.,');
      rsep:=0
      ;~~}
      {FONT: 'T8GB'}{SPACE: 'C'}
      {SUBSTITUTE: 'LINIA0'}{FONT: 'T8G'}
      {
      sg[1]:=sg[2]:=sg[3]:=0;
      PRN.s_frame();
      PRN.FORM[3]:=1
      ;~~}
      {FI}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{
PRN.n_frame();
w[1]:=w[2]:='';
w[3]:='Razem:'@; PRN.FORM[3]:=0;
w[4]:=form(s[1],,doklw,'.,');
w[5]:=form(s[2],,doklw,'.,');
w[6]:=form(s[3],,doklw,'.,')
;~~}
{SUBSTITUTE: 'LINIA0'}
{ELSE}
{SUBSTITUTE: 'LINIAF'}
{FI}
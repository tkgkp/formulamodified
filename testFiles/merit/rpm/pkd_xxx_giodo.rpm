:!UTF-8
{TITLE:Informacja o zakresie przetwarzania danych osobowych}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('kol','yesno','lp','pom','text');
   lp:=pom:=0;
   kol:=obj_new(6);
   yesno:="{? _a='T' || 'tak' || 'nie' ?}";
   text:='';
   kol[1]:=4;
   kol[5]:=tab_tmp(3,
         'DATA','DATE',,
         'OPIS','STRING[40]',,
         'KOD','STRING[1]',,
         'OPIS1','STRING[255]',,
         'OPIS2','STRING[255]',,
         'OPIS3','STRING[255]',);
   kol[6]:=exec('kod','giodo');
   KRAJE.cntx_psh();
   KRAJE.prefix()
}
{END:
  KRAJE.cntx_pop();
  VAR_DEL.delete('kol','yesno','lp','pom','text')
}
{COMMENT}OLD: xxx_gio.rpm{COMMENT-}
{COMMENT}OLD: xxx_gio.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{SUBSTITUTE: 'FOOT'}
{FONT: 'T8'}{kol[2]:=kol[1]+19; kol[3]:=charleft()*0.5+kol[1]; kol[4]:=kol[3]+19; ~~}
{COMMENT}---------------------------------------------MACRO INFADRES-----------------------------------------{COMMENT-}
{MACRO: 'infadres'}
{FONT: 'T8'}{>{kol[1]}}Kraj{>{kol[2]}}{FONT: 'T8B'}{OS_ADRES.KRAJ().NAZ}{>{kol[3]}}{FONT: 'T8'}Województwo{>{kol[4]}
}{FONT: 'T8B'}{OS_ADRES.WOJEWODZ}
{FONT: 'T8'}{>{kol[1]}}Powiat{>{kol[2]}}{FONT: 'T8B'}{OS_ADRES.POWIAT}{>{kol[3]}}{FONT: 'T8'}Gmina{>{kol[4]}
}{FONT: 'T8B'}{OS_ADRES.GMINA}
{FONT: 'T8'}{>{kol[1]}}Miasto{>{kol[2]}}{FONT: 'T8B'}{OS_ADRES.MIASTO}{>{kol[3]}}{FONT: 'T8'}Kod pocztowy{>{kol[4]}
}{FONT: 'T8B'}{OS_ADRES.KOD}
{FONT: 'T8'}{>{kol[1]}}Ulica{>{kol[2]}}{FONT: 'T8B'}{STR.split(OS_ADRES.ULICA);STR.line(48)}{>{kol[3]}
}{FONT: 'T8'}Nr domu, nr lokalu{>{kol[4]}}{FONT: 'T8B'}{OS_ADRES.DOM}{IF: +OS_ADRES.LOKAL} m. {OS_ADRES.LOKAL}{FI}
{WHILE: STR.next()}
{DO}
{>{kol[2]}}{STR.line(48)}
{OD}
{FONT: 'T8'}{>{kol[1]}}Nr telefonu{>{kol[2]}}{FONT: 'T8B'}{OS_ADRES.TEL}{>{kol[3]}}{FONT: 'T8'}Adres e-mail{>{kol[4]}
}{FONT: 'T8B'}{OS_ADRES.EMAIL}
{MACRO-}
{COMMENT}---------------------------------------------MACRO SRODEK-----------------------------------------{COMMENT-}
{MACRO: 'srodek'}
{IF: _jest:=0;
     _data:=#0;
     GIODO.index('GIODOSOB');
     GIODO.prefix(OSOBA.ref());
     {? GIODO.find_ge(date()-180)
     || {!
        |? _data:=GIODO.DATA;
           _jest:=(GIODO.KOD='I');
           ~_jest & GIODO.next()
        !}
     ?};
     {? _jest
     || FUN.ask(OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE+' otrzymał'+{? OSOBA.PLEC='K' || 'a' || '' ?}+
           '\ninformację o zakresie przetwarzanych danych dnia '+($_data)+
           '\n(prawo do ponownego żądania udzielenia odpowiedzi na pytanie'+
           '\no zakres przetwarzanych danych nie przysługuje przed upływem'+
           '\nsześciu miesięcy od daty odpowiedzi na poprzednie zapytanie).'+
           '\nCzy mimo to przygotować wydruk?')
     || 1
     ?}
}
{_msg:='Udzielenie informacji osobie zainteresowanej o zakresie przetwarzania jej danych osobowych'@;
 exec('zapisz','giodo','osoba',OSOBA.ref(),date(),'I',_msg);~~}
{SPACE: 'A'}{IF: PAR_WYDR.LOGO}
{IF: exec('get_logo','img_blob')}{<1}{IMAGE: exec('get_logo','img_blob');0;1}{ELSE}
{<1}{BITMAP:'"logo.bmp",,1'}{FI}{FI}{SPACE: 'C'}
{FONT: 'T8'}{>{charleft()}'%1, dn. %2'@[PAR_WYDR.MIEJSC,$date()]}
{FONT: 'T8'}{'Administrator danych osobowych'@}
{FONT: 'T8'}{'Nazwa:'@}{FONT: 'T8B'}{STR.split(XINFO.NAZ);~~}{>8}{STR.line(charleft())}
{IF: STR.next()}{>8}{STR.line(charleft())}{FI}
{STR.split(XINFO.UL+' '+XINFO.NR_D+{? |XINFO.NR_L='' || '' || ' m. '+XINFO.NR_L ?});~~}
{FONT: 'T8'}{'Adres:'@}{FONT: 'T8B'}{>8}{ STR.line(charleft())}
{WHILE: STR.next()}{DO}{>8}{STR.line(charleft())}{OD}
{>8}{ XINFO.KOD_P+' '+{? |XINFO.POCZ='' || XINFO.MIA || XINFO.POCZ ?}}
{FONT: 'T8'}REGON:{FONT: 'T8B'}{>8}{XINFO.REGON}
{LINE: 2}
{FONT: 'TITLE'}
{SPACE: 'A'}
{<1>76'Informacja o zakresie przetwarzania danych'@}
{SPACE: '@'}
{LINE: 1}
{HEADER}
{HEADER-}
{FONT: 'T8'}{'Dane identyfikacyjne i ewidencyjne'@}
{FONT: 'f'}{charleft()*'─'}
{FONT: 'T8'}{>{kol[1]}'Nazwisko aktualne'@}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.NAZWISKO}{>{kol[3]}}{FONT: 'T8'}{
'Nazwisko rodowe'@}{>{kol[4]}}{FONT: 'T8B'}{OSOBA.RODOWE}
{FONT: 'T8'}{>{kol[1]}'Pierwsze imię'@}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.PIERWSZE}{>{kol[3]}}{FONT: 'T8'}{
'Drugie imię'@}{>{kol[4]}}{FONT: 'T8B'}{OSOBA.DRUGIE}
{FONT: 'T8'}{>{kol[1]}'Imię ojca'@}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.OJCIEC}{>{kol[3]}}{FONT: 'T8'}{
'Imię matki'@}{>{kol[4]}}{FONT: 'T8B'}{OSOBA.MATKA}

{FONT: 'T8'}{>{kol[1]}'Data urodzenia'@}{>{kol[2]}}{FONT: 'T8B'}{
{? OSOBA.UR_DATA<>#0 || $OSOBA.UR_DATA || '' ?}}{>{kol[3]}}{FONT: 'T8'}{
'Miejsce urodzenia'@}{>{kol[4]}}{FONT: 'T8B'}{OSOBA.UR_MIEJ}
{FONT: 'T8'}{>{kol[1]}'Płeć'@}{>{kol[2]}}{FONT: 'T8B'}{{? OSOBA.PLEC='K' || 'kobieta'@ || 'mężczyzna'@ ?}}

{FONT: 'T8'}{>{kol[1]}'Obcokrajowiec'@}{>{kol[2]}}{FONT: 'T8B'}{yesno(OSOBA.OBCY)}{>{kol[3]}}{FONT: 'T8'}{
'Posiada kartę'@}
{>{kol[3]}}{FONT: 'T8'}{('stałego pobytu'@)}{>{kol[4]}}{FONT: 'T8B'}{yesno(OSOBA.KARTA)}
{>{kol[3]}}{FONT: 'T8'}{'Obywatelstwo'@}{>{kol[4]}}{FONT: 'T8B'}{OSOBA.OBYWATEL}

{IF: exec('szukaj','osoba','S')}
{FONT: 'T8'}{>{kol[1]}'Adres pobytu stałego'@}
{SUBSTITUTE: 'infadres'}
{FI}
{IF: exec('szukaj','osoba','C')}
{FONT: 'T8'}{>{kol[1]}'Adres zamieszkania'@}
{SUBSTITUTE: 'infadres'}
{FI}
{IF: exec('szukaj','osoba','K')}
{FONT: 'T8'}{>{kol[1]}'Adres do korespondencji'@}
{SUBSTITUTE: 'infadres'}
{FI}

{FONT: 'T8'}{>{kol[1]}'Dowód osobisty'@}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.DOWOD}{IF: +OSOBA.ORG_DOW}{FONT: 'T8'}{
'('+'wydany przez'@}{FONT: 'T8B'} {OSOBA.ORG_DOW}{')'}{FI}
{FONT: 'T8'}{>{kol[1]}'Paszport'@}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.PASZPORT}{IF: +OSOBA.ORG_PASZ}{FONT: 'T8'}{
'('+'wydany przez'@}{FONT: 'T8B'} {OSOBA.ORG_PASZ}{')'}{FI}

{FONT: 'T8'}{>{kol[1]}'NIP'}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.NIP}{>{kol[3]}}{FONT: 'T8'}{
'Urząd skarbowy'}{>{kol[4]}}{FONT: 'T8B'}{OSOBA.US().NU}
{FONT: 'T8'}{>{kol[1]}'Numer PESEL'@}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.PESEL}

{ OS_SZKOL.prefix(P.OSOBA); ~~}
{IF:  OS_SZKOL.last()}
{FONT: 'T8'}{>{kol[1]}'Wykształcenie'@}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.WYK().WK}{>{kol[3]}}{FONT: 'T8'}{
'Zawód wyuczony'@}{>{kol[4]}}{FONT: 'T8B'}{OS_SZKOL.ZAW().ZD}
{FI}

{FONT: 'T8'}{>{kol[1]}'Dane wprowadził'@}{>{kol[2]}}{FONT: 'T8B'}{OSOBA.REJ_KTO}{>{kol[3]}}{FONT: 'T8'}{
'Źródło danych'@}{>{kol[4]}}{FONT: 'T8B'}{OSOBA.REJ_INFO}
{FONT: 'T8'}{>{kol[1]}'Data rejestracji'@}{>{kol[2]}}{FONT: 'T8B'}{{? OSOBA.REJ_DATA<>#0 || OSOBA.REJ_DATA || '' ?}}
{ENTIRE}
{FOR: PKO}{INDEX: '_PKO'}{PREFIX: OSOBA.ref()}{DO}
{FIRST}

{FONT: 'T8'}{'Informacje o rachunkach bankowych'@}
{FONT: 'f'}{charleft()*'─'}
{FONT: 'T8'}{>{kol[1]}'Nazwa banku'@}{>{kol[4]}'Numer konta'@}
{FIRST-}
{FONT: 'T8B'}{>{kol[1]}}{PKO.BA().NB}{>{kol[4]}}{PKO.N}
{OD}
{ENTIRE-}

{ENTIRE}
{FOR: RD}{INDEX: '_RODZINA'}{PREFIX: OSOBA.ref()}{DO}
{FIRST}
{FONT: 'T8'}{'Dane osobowe członków rodziny'@}
{FONT: 'f'}{charleft()*'─'}
{FIRST-}
{FONT: 'T8'}{>{kol[1]}'Nazwisko'@}{>{kol[2]}}{FONT: 'T8B'}{RD.NA}{>{kol[3]}}{FONT: 'T8'}{'Imię'@}{>{kol[4]}
}{FONT: 'T8B'}{RD.IM}
{FONT: 'T8'}{>{kol[1]}'Data urodzenia'@}{>{kol[2]}}{FONT: 'T8B'}{$RD.DA}{>{kol[3]}}{FONT: 'T8'}{
'Numer PESEL'@}{>{kol[4]}}{FONT: 'T8B'}{RD.PESEL}
{IF: RD.STPK}{FONT: 'T8'}{>{kol[1]}'Pokrewieństwo'@}{>{kol[2]}
}{FONT: 'T8B'}{form(exec('memo2print','#string',S_ZUS,'OPIS',RD.STPK))}
{LINE: 2}
{FI}
{OD}
{ENTIRE-}

{IF: lineleft()<>0}
   {PAGE}
{ELSE}
   {LINE: 2}
{FI}
{ kol[5].erase();
  GIODO.cntx_psh();
  GIODO.index('GIODOSOB');
  GIODO.prefix(OSOBA.ref());
  {? GIODO.first()
  || {!
     |? kol[5].DATA:=GIODO.DATA;
        kol[5].OPIS:=GIODO.OPER;
        kol[5].KOD:=GIODO.KOD;
        STR.split(gsub(GIODO.memo_txt(,1,'OPIS'),'\n',' '));
        kol[5].OPIS1:=STR.line(255);
        kol[5].OPIS2:=STR.line(255);
        kol[5].OPIS3:=STR.line(255);
        kol[5].add();
        GIODO.next()
     !}
  ?};
  GIODO.cntx_pop(); ~~}
{IF: kol[5].first()}
{FOR: kol[5]}{DO}
{FIRST}
{FONT: 'TITLE'}
{SPACE: 'A'}
{<1>{charleft()}'Wykaz operacji dokonanych na danych osobowych'@}
{SPACE: '@'}
{LINE: 1}
{FONT: 'T8'}
{<1'Data'}{<12'Opis'@}
{charleft()*'─'}
{ GIODO.cntx_psh();
  GIODO.index('GIODOSOB');
  GIODO.prefix();
  GIODO.blank(1);
  GIODO.OSOBA:=OSOBA.ref();
  GIODO.KOD:='W';~~}
{IF: |PAR_SKID.get(13)<>'' & ~GIODO.find_rec()}
{FONT: 'T8B'}
{<1}{|PAR_SKID.get(13)}{>12}{'Data rozpoczęcia rejestracji operacji na danych osobowych'@}
{FI}
{  GIODO.cntx_pop();~~}
{FIRST-}
{ENTIRE}{ dru_dat:=0;~~}
{FONT: 'T8B'}
{IF: (|kol[5].OPIS1='' & |kol[5].OPIS2='' & |kol[5].OPIS3='') | -kol[5].KOD='w'}
{<1}{kol[5].DATA}{>12}{FONT: 'T8'}{ {? -kol[5].KOD='w' || kol[5].OPIS+' - ' || '' ?}+
{? |kol[5].OPIS1='' & |kol[5].OPIS2='' & |kol[5].OPIS3=''
|| {? exec('kod_opis','giodo',kol[6],kol[5].KOD) || SKID.GIOOPIS || '' ?}
|| '' ?}}
{ dru_dat:=1; ~~}
{FI}
{FONT: 'T8'}
{IF: +(|kol[5].OPIS1)}
{ STR.split(|kol[5].OPIS1);~~}
{WHILE: STR.next()}
{DO}
{IF: dru_dat=0}{FONT: 'T8B'}{<1}{kol[5].DATA}{>12 {? -kol[5].KOD='w' || kol[5].OPIS+' ' || '' ?}
}{FONT: 'T8'}{STR.line(105)}{ dru_dat:=1;~~}{ELSE}{FONT: 'T8'}{>12}{STR.line(105)}{FI}
{OD}
{FI}
{IF: +(|kol[5].OPIS2)}
{ STR.split(|kol[5].OPIS2);~~}
{WHILE: STR.next()}
{DO}
{IF: dru_dat=0}{FONT: 'T8B'}{<1}{kol[5].DATA}{>12 {? -kol[5].KOD='w' || kol[5].OPIS+' ' || '' ?}
}{FONT: 'T8'}{STR.line(105)}{ dru_dat:=1;~~}{ELSE}{FONT: 'T8'}{>12}{STR.line(105)}{FI}
{OD}
{FI}
{IF: +(|kol[5].OPIS3)}
{ STR.split(|kol[5].OPIS3);~~}
{WHILE: STR.next()}
{DO}
{IF: dru_dat=0}{FONT: 'T8B'}{<1}{kol[5].DATA}{>12 {? -kol[5].KOD='w' || kol[5].OPIS+' ' || '' ?}
}{FONT: 'T8'}{STR.line(105)}{ dru_dat:=1;~~}{ELSE}{FONT: 'T8'}{>12}{STR.line(105)}{FI}
{OD}
{FI}
{ENTIRE-}
{OD}
{IF: kol[5].first() & lineleft()<>0}
{PAGE}
{FI}
{ obj_del(kol[5]) }
{FI}
{FI}
{MACRO-}
{COMMENT}---------------------------------------------CIALO WYDRUKU-----------------------------------------{COMMENT-}
{SUBSTITUTE: 'srodek'}
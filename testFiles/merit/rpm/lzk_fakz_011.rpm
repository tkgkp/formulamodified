:!UTF-8
{TITLE:E. Szczegółowe zestawienie zakupów w podziale na grupy kontrahentów}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   zakz_011:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','ss','sg','dokl','doklw','PRN','kgr','kgk');
      VAR_DEL.delete('od_daty','do_daty');
      VAR_DEL.delete('od_platnosc','do_platnosc');
      VAR_DEL.delete('filtr_kh','filtr_m','fak_wew');
      VAR_DEL.delete('wydr_naz','wydr_edi','wyjscie','X_Xa','X_Xb','X_Xw1')
   ";
   zakz_011();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   n :=obj_new(7); "--nazwy kolumn--";
   k :=obj_new(7); "--szerokosci kolumn--";
   w :=obj_new(7); "--wartosci kolumn--";
   dokl  :=0; "--dokladnosc ilosci--";
   doklw :=2; "--dokladnosc wartosc--";
   PRN := obj_new(@.CLASS.PRINT,8); PRN.s_frame();

   k[1] :=20;     n[1] :='Indeks'@;
   k[2] :=60;     n[2] :='Nazwa materiału lub usługi'@;
   k[3] :=5;      n[3] :='jm'@;
   k[4] :=13;     n[4] :='Ilość'@;
   k[5] :=13;     n[5] :='Wartość netto'@;
   k[6] :=13;     n[6] :='Wartość VAT'@;
   k[7] :=14;     n[7] :='Wartość brutto'@;

   {! _ii..7 |! w[_ii] :='' !};
   ss:=tab_tmp(1,'JM','STRING[10]',''
        ,'S1','REAL',''
        ,'S2','REAL',''
        ,'S3','REAL',''
        ,'S4','REAL','');
   sg:=tab_tmp(1,'JM','STRING[10]',''
        ,'S1','REAL',''
        ,'S2','REAL',''
        ,'S3','REAL',''
        ,'S4','REAL','');
   tryb:=rep_export();
   kgr:='';
   kgk:='';
   od_daty :=do_daty :=date(0,0,0);

   od_platnosc :=do_platnosc :=date(0,0,0);

   filtr_kh :=filtr_m :='N';

   wydr_naz:='zakz_011';
   wydr_edi:='ZAKZ_011';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("
         {? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || _wyn:='' || FUN.info('Błędny zakres dat.'@);_wyn:='DO_DATY' ?};
         {? _wyn=''
         || {? WYDRUKIL.DO_DATY1>=WYDRUKIL.OD_DATY1 || _wyn:='' || FUN.info('Błędny zakres dat.'@);_wyn:='DO_DATY1' ?}
         ?};
         _wyn")
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         filtr_kh:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         filtr_m :={? WYDRUKIL.CHKBOX02=1 || 'T' || 'N' ?};
         od_daty :=WYDRUKIL.OD_DATY;
         do_daty :=WYDRUKIL.DO_DATY;
         od_platnosc :=WYDRUKIL.OD_DATY1;
         do_platnosc :=WYDRUKIL.DO_DATY1;
         fak_wew:=WYDRUKIL.CHKBOX03
      ?}
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_kh','kontrahent',filtr_kh,'N','G'); X_Xb.index(X_Xb.ndx_tmp(,,'REFI',,)) ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_m','material',filtr_m,'N','') ?}
}
{END:
   zakz_011(); VAR_DEL.delete('zakz_011')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1
|| PRN.add("kgk",8,'Grupa kontrahenta'@);
   PRN.add("kgr",8,'Grupa materiałowa'@)
?};
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
PRN.add("w[3]",k[3],n[3]);
PRN.add("w[4]",k[4],n[4],1);
{? exec('upr_cz','ceny')
||
   PRN.add("w[5]",k[5],n[5],1);
   PRN.add("w[6]",k[6],n[6],1);
   PRN.add("w[7]",k[7],n[7],1)
?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(3,
   'KOD'    ,'STRING[20]'  ,'Kod',
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'JM'     ,'STRING[10]'  ,'jm',
   'NAZ'    ,'STRING[40]'  ,'Nazwa',
   'N'      ,'STRING[100]' ,'Nazwa materiału lub usługi',
   'IL'     ,'REAL'        ,'Ilość',
   'WN'     ,'REAL'        ,'Netto',
   'WV'     ,'REAL'        ,'VAT',
   'WB'     ,'REAL'        ,'Brutto',
   'MGR'    ,'STRING[8]'   ,'Grupa materiałowa');
X_Xw1.index(X_Xw1.ndx_tmp(,,'KOD',,,'KTM',,,'KTM',,));

_lp:=0;
OKR.cntx_psh; FAKS.cntx_psh; FAP.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
_min:="{? _a & _a<_b || _a || _b ?}";
_min:=_min(od_daty~1,od_platnosc~1);
_max:="{? _a & _a>_b || _a || _b ?}";
_max:=_max(do_daty~1,do_platnosc~1);
{? _min || _min-=1 ?};
{? _max || _max+=1 ?};
{? OKR.first & (_min=0 | OKR.find_ge(_min)) & X_Xb.first() & X_Xa.first
|| _stsupr:=exec('get','#params',6102,2,OPERATOR.USER)='T';
_stss:="
_wyn:=exec('usr_upr','b_perm','STZ',_a,OPERATOR.USER);
_wyn
";
   {!
   |?
      {? X_Xa.first()
      ||
         FAKS.use('faktu'+ST.ODDZ+(($OKR.ROK)+2));
         FAP.use('fakpo'+ST.ODDZ+(($OKR.ROK)+2));
         FAP.index('REPORT');
         {!
         |?
            FAP.prefix('Z','T',X_Xa.REFI);
            {? FAP.first()
            ||
               {!
               |?
                  {? FAP.FAKS
                     & FAP.FAKS().STAT_REJ<>'A'
                     & FAP.FAKS().ZEST='T' & FAKS.ZEST_AKC='T'
                     & (_stsupr=0 | _stsupr=1 & FAKS.STS<>null & _stss(FAKS.STS))
                     & (fak_wew=1 | FAKS.WHERE<>'E')
                     & X_Xb.find_key(#FAKS.KH)
                     & ((od_daty<=FAKS.DO | od_daty=date(0,0,0)) & ( FAKS.DO<=do_daty | do_daty=date(0,0,0)))
                     & ((od_platnosc<=FAKS.TZ | od_platnosc=date(0,0,0) ) & (FAKS.TZ<=do_platnosc
                        | do_platnosc=date(0,0,0)))
                  ||
                     _kod:=FAKS.KH().GRKH().KOD;
                     _ktm:=FAP.M().KTM;
                     _kjm:=FAP.JM().KOD;
                     {? X_Xw1.find_key(_kod,_ktm,_kjm,)
                     ||
                        X_Xw1.IL  +=FAP.IL;
                        X_Xw1.WN  +=FAP.WWAL_P;
                        X_Xw1.WV  +=FAP.VWAL_P;
                        X_Xw1.WB  +=FAP.BWAL_P;
                        X_Xw1.put()
                     ||
                        X_Xw1.KOD :=_kod;
                        X_Xw1.NAZ :=GRKH.NAZ;
                        X_Xw1.KTM :=M.KTM;
                        X_Xw1.N   :=M.N;
                        X_Xw1.MGR :=M.MGR().KOD;
                        X_Xw1.JM  :=_kjm;
                        X_Xw1.IL  :=FAP.IL;
                        X_Xw1.WN  :=FAP.WWAL_P;
                        X_Xw1.WV  :=FAP.VWAL_P;
                        X_Xw1.WB  :=FAP.BWAL_P;
                        {? M.DOKL>dokl || dokl:=M.DOKL ?};
                        X_Xw1.add()
                     ?}
                  ?};
                  FAP.next()
               !}
            ?};
            X_Xa.next()
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max)
   !}
?};
OKR.cntx_pop; FAKS.cntx_pop; FAP.cntx_pop;

PAR_WYDR.TITLE:='SZCZEGÓŁOWE ZESTAWIENIE ZAKUPÓW W PODZIALE NA GRUPY KONTRAHENTÓW';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: filtr_kh='T'}{<{lmt+1}}{'Z wyborem grup kontrahentów'}{FI}
{IF: filtr_m='T'}{<{lmt+1}}{'Z wyborem materiałów i usług'}{FI}
{IF: fak_wew=1}{<{lmt+1}}{'Uwzględniono faktury wewnętrzne'}{ELSE}{<{lmt+1}}{'Bez faktur wewnętrznych'}{FI}
{IF: od_daty$1+do_daty$1+od_platnosc$1+do_platnosc$1<>''}
   {<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
   {IF: od_daty$1+do_daty$1<>''}{<{lmt+1}}{'Data otrzymania'}{<{lmt+1+20}od_daty}{<{lmt+1+40}do_daty}{FI}
   {IF: od_platnosc$1+do_platnosc$1<>''}{<{lmt+1}}{'Termin płatności'}{<{lmt+1+20}od_platnosc}{<{lmt+1+40}do_platnosc}{FI}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1;~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozcycje wydruku                                         {COMMENT-}
{ rsep:=PAR_WYDR.RSEP;  ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xw1}
{DO}
   {FIRST}{ dokl:=exec('fld_prec','#field',X_Xw1,'IL'); kolejny:=0;~~}{FIRST-}
   {IF: tryb<>1 & kolejny=1}{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}{FI}
   {
   w[1] :=form(X_Xw1.KTM);
   w[2] :=form(X_Xw1.N);
   w[3] :=form(X_Xw1.JM);
   w[4] :=form(X_Xw1.IL,,dokl,'.,');
   w[5] :=form(X_Xw1.WN,,doklw,'.,');
   w[6] :=form(X_Xw1.WV,,doklw,'.,');
   w[7] :=form(X_Xw1.WB,,doklw,'.,');
   kgr  :=form(X_Xw1.MGR);
   kgk  :=form(X_Xw1.KOD);
   ss.clear();
   ss.prefix(X_Xw1.JM,);
   {? ss.first()
   || ss.S1+=X_Xw1.IL $dokl;
      ss.S2+=X_Xw1.WN $doklw;
      ss.S3+=X_Xw1.WV $doklw;
      ss.S4+=X_Xw1.WB $doklw;
      ss.put(1)
   || ss.blank();
      ss.JM:=X_Xw1.JM;
      ss.S1:=X_Xw1.IL $dokl;
      ss.S2:=X_Xw1.WN $doklw;
      ss.S3:=X_Xw1.WV $doklw;
      ss.S4:=X_Xw1.WB $doklw;
      ss.add(1)
   ?};
   sg.clear();
   sg.prefix(X_Xw1.JM,);
   {? sg.first()
   || sg.S1+=X_Xw1.IL $dokl;
      sg.S2+=X_Xw1.WN $doklw;
      sg.S3+=X_Xw1.WV $doklw;
      sg.S4+=X_Xw1.WB $doklw;
      sg.put(1)
   || sg.blank();
      sg.JM:=X_Xw1.JM;
      sg.S1:=X_Xw1.IL $dokl;
      sg.S2:=X_Xw1.WN $doklw;
      sg.S3:=X_Xw1.WV $doklw;
      sg.S4:=X_Xw1.WB $doklw;
      sg.add(1)
   ?};~~}
   {SUBSTITUTE: 'LINIA0'}
   { kolejny:=0; rsep:=PAR_WYDR.RSEP;~~}
   {TOTAL: '%1 (%2)'[X_Xw1.KOD,X_Xw1.JM]}
{COMMENT}   podsumowanie dla grupy                                   {COMMENT-}
      {IF: tryb<>1}
      {
      w[1] :=form(X_Xw1.KTM);
      w[2] :=form(X_Xw1.N);
      w[3] :=form(X_Xw1.JM);
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+3
      ;~~}
      {IF: lineleft()>0 & lineleft()<il_lin}{PAGE}{FI}
      {SUBSTITUTE: 'LINIAF'}
      {FOR: sg}
      {DO}
         {
         kolejny:=1; rsep:=0;
         PRN.n_frame();
         w[1]:='';
         w[2]:='Razem %1'@[X_Xw1.KOD]; PRN.FORM[2]:=0;
         w[3]:=form(sg.JM);
         w[4]:=form(sg.S1,,dokl,'.,');
         w[5]:=form(sg.S2,,doklw,'.,');
         w[6]:=form(sg.S3,,doklw,'.,');
         w[7]:=form(sg.S4,,doklw,'.,')
         ;~~}
         {FONT: 'T8GB'}{SPACE: 'C'}
         {SUBSTITUTE: 'LINIA0'}
         {FONT: 'T8G'}{SPACE: 'C'}
      {OD}
      {
      sg.erase();
      PRN.s_frame();
      PRN.FORM[2]:=1
      ;~~}
      {FI}
   {TOTAL}
      { VAR_DEL.delete('kolejny');~~}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{FOR: ss}
{DO}
   {
   w[1] :='';
   w[2] :='Razem:'; PRN.FORM[2] :=0;
   w[3] :=form(ss.JM);
   w[4] :=form(ss.S1,,dokl,'.,');
   w[5] :=form(ss.S2,,doklw,'.,');
   w[6] :=form(ss.S3,,doklw,'.,');
   w[7] :=form(ss.S4,,doklw,'.,');
   PRN.n_frame()
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{FI}
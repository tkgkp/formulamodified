:!UTF-8
{TITLE: Wartości sprawozdań wg wariantów}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN: PRN:=obj_new(@.CLASS.PRINT,7); PRN.sl_frame(); TT_TREE.cntx_psh(); w:=obj_new(6);
        color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=dl:=0; vp:=1; rsep:=0; licznik:=str:=1;
        ile:={? SSTALE.WARIANT=1
             || {? SSTALE.TYP='M' || licznik:=2; 6 || licznik:=1; liczba ?}
             |? SSTALE.WARIANT=2
             || {? liczba=1 || licznik:=1; 4 |? liczba=2 || licznik:=2; 5 || licznik:=2; 4 ?}
             |? SSTALE.WARIANT=3
             || licznik:=1; 4
             |? SSTALE.WARIANT=4
             || licznik:=1; stop-start+1
             |? SSTALE.WARIANT=5
             || licznik:=tt_max%6+1-(tt_max%*6=0); {? tt_max>6 || 6 || tt_max ?}
             || licznik:=1; 3
             ?}; poz:=0; pusta:=0;
        {? SSTALE.WARIANT=1
        || _ile:=stop-start+1;
           {? _ile<=ile || ile:=_ile; licznik:=1 ?}
        ?};
        {? SKID.LANG_SLO & SKID.LANG_SLO().KOD<>'POL' || PAGELANG:=1 ?};
        PAR_WYDR.TITLE:=
            {? __ONE_SPR
            || GR_SIK.cntx_psh(); GR_SIK.prefix();
               _txt:={? GR_SIK.seek(TT_TREE.REF,GR_SIK.name()) || GR_SIK.SKROT || '' ?};
               GR_SIK.cntx_pop();
               exec('tran_sik','lang',25)+' '+_txt
            || exec('tran_sik','lang',26)
            ?}+' '+exec('tran_sik','lang',27)
}
{END: VAR_DEL.delete('PRN','w'); TT_TREE.cntx_pop(); &ile; &poz; &licznik; &dl; &pusta;
      &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &str;
      VAR_DEL.delete('PAGELANG')
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: tree_spr.rpm
Utworzony:   21-04-2006 [MB] Mariusz Buraczewski
==================================================
Zawartosc: Wartości sprawozdań wg wariantów
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}--------------------------------DEFPRN---------------------{COMMENT-}
{MACRO: 'DEFPRN'}
{FONT: 'T8'}{SPACE: 'C'}
{  ll:=charleft();
   _dod_kol:=SSTALE.WARIANT=1 & (SSTALE.TYP='K' | SSTALE.TYP='P') & ~SSTALE.INC;
   VAR_DEL.delete('PRN'); PRN:=obj_new(@.CLASS.PRINT,ile+1+_dod_kol); PRN.sl_frame();
   dl:=ll-17*{? SSTALE.WARIANT=2 & liczba>1 || {? liczba=2 || 5 || 6 ?} || ile ?}-2-_dod_kol*17;
   PRN.add("{? STR.next() || pusta:=1; TT_TREE.POZIOM*' '+STR.line(dl-TT_TREE.POZIOM) || pusta:=0; exec('language','!fks_ksp_zspr'); STR.split(TT_TREE.POZIOM*' '+UNPAR.P11);  TT_TREE.POZIOM*' '+STR.line(dl-TT_TREE.POZIOM) ?}",dl,'');
   {? SSTALE.WARIANT=5
   || {? width>0
      || {! _i:=1..ile
         |! PRN.add($('{? pusta=0 || form(POMOC.P'+$(_i+poz)+',,SSTALE.PREC,\' ,\') || \'\' ?}'),16,tt_okr[tt_mov[1]+_i+poz][2],1)
         !}
      ?}
   || {? stop-start>=0
      || {! _i:=1 .. ile
         |! PRN.add($('{? pusta=0 & (SSTALE.WARIANT=4 & TT_TREE.POZIOM | SSTALE.WARIANT<>4 & TT_TREE.POZIOM>1)
                       || form(POMOC.P'+$(_i+poz)+',,SSTALE.PREC,\' ,\')
                       || \'\'
                       ?}'),16,
                    _ile:=(delta+_i+poz+{? SSTALE.WARIANT=4 || start-1 || 0 ?}); _ile1:='['+$_ile+']';
                    {? _ile>obj_len(naz_full) | var_pres(_ile1,naz_full)<=0
                    || ''
                    || naz_full[_ile]
                    ?},1)
         !};
         {? _dod_kol
         || PRN.add($('{? pusta=0 & TT_TREE.POZIOM>1 || form(POMOC.P'+$(ile+1+poz)+',,SSTALE.PREC,\' ,\') || \'\' ?}'),16,naz_full[rok_poz],1)
         ?}
      ?}
   ?};
~~}
{MACRO-}
{SUBSTITUTE: 'DEFPRN'}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}{FONT: 'W8'}{SPACE: 'C'}
{IF: page=str}
   {IF: page=1}
      {<1 exec('tran_sik','lang',1)+':'}
      {<3 exec('tran_sik','lang',28)+': '+
      {? SSTALE.WARIANT=1
      || exec('tran_sik','lang',29)+' '+SSTALE.ROK().NAZ
      |? SSTALE.WARIANT=2
      || exec('tran_sik','lang',30)+' '+SSTALE.OKRES().TR+' '+exec('tran_sik','lang',31)+' '+SSTALE.ROK2().NAZ+' - '+SSTALE.ROK3().NAZ
      |? SSTALE.WARIANT=3
      || exec('tran_sik','lang',32)
      |? SSTALE.WARIANT=4
      || exec('tran_sik','lang',33)+' '+exec('get_lang','lang',SSTALE.OSPR(); OSPR.ref())
      |? SSTALE.WARIANT=5
      || exec('tran_sik','lang',52)+' '+SIK.WIDOK().NAZWA
      || 'Wartości dla konsolidacji'@
      ?} }
      {<3 exec('tran_sik','lang',34)}{<12 '- '+exec('tran_sik','lang',35)+': '+{? (SSTALE.WARIANT=1 | SSTALE.WARIANT=2 | SSTALE.WARIANT=4) & SSTALE.INC || exec('tran_sik','lang',38) || exec('tran_sik','lang',39) ?} }
      {<12 '- '+exec('tran_sik','lang',36)+' '+$SSTALE.ZAOKR}
      {<12 '- '+exec('tran_sik','lang',37)+' '+$SSTALE.PREC}
      {IF: UNPAR.PSLO & UNPAR.PSLO<>FINFO.NAROD}
         {<12 '- w walucie '+UNPAR.PSLO().KOD+', kurs '+form(UNPAR.P25,,6)}
      {FI}
      {IF: SSTALE.WARIANT<>6}
         {<12 '- '+{? SIK.WYL='T' || 'jednostkowe'@ |? SIK.WYL='W' || {? REF.FIRMAWYL || 'wyłączeniowe: firma %1'@[REF.FIRMAWYL().SYMBOL] || 'wyłączeniowe'@ ?} || 'skonsolidowane'@ ?}}
      {FI}
      {<3 {? OPERATOR.DEPT || exec('tran_sik','lang',2)+': '+OPERATOR.DEPT().OD || exec('tran_sik','lang',3) ?}}
   {ELSE}
      {LINE: 6+(UNPAR.PSLO & UNPAR.PSLO<>FINFO.NAROD)}
   {FI}
{FI}
{FONT: 'T8B'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}{ vp:=1;~~}
{FOOTER-}
{COMMENT}-------------------------------TREE------------------------{COMMENT-}
{MACRO: 'TREE'}
{IF: TT_TREE.ROZ}
   {FOR: TT_TREE}
   {PREFIX: TT_TREE.ref()}
   {DO}
      {exec('bv_ppom','!fks_ksp_zspr');~~}
      {IF: TT_TREE.POZIOM=0}{FONT: 'T8B'}{ELSE}{FONT: 'T8'}{FI}
      {SUBSTITUTE: 'LINIA0'}
      {WHILE: STR.next()}{DO}{SUBSTITUTE: 'LINIA0'}{OD}
      {SUBSTITUTE: 'TREE'}
   {OD}
{FI}
{MACRO-}
{MACRO: 'ONE_SPR'}
   {exec('bv_ppom','!fks_ksp_zspr');~~}
   {IF: TT_TREE.POZIOM=0}{FONT: 'T8B'}{ELSE}{FONT: 'T8'}{FI}
   {SUBSTITUTE: 'LINIA0'}
   {WHILE: STR.next()}{DO}{SUBSTITUTE: 'LINIA0'}{OD}
   {SUBSTITUTE: 'TREE'}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{WHILE: licznik}
{DO}
{FONT: 'T8'}{SPACE: 'C'}
{IF: __ONE_SPR}
   {SUBSTITUTE: 'ONE_SPR'}
{ELSE}
   {FOR: TT_TREE}{PREFIX: 0}{DO}{SUBSTITUTE: 'ONE_SPR'}{OD}
{FI}
{licznik-=1;~~}
{IF: licznik}
{poz+=ile; ile:={? SSTALE.WARIANT=2
                || 1+3*liczba-ile
                |? SSTALE.WARIANT=5
                || tt_max-6
                || ile
                ?};
~~}
{IF: lineleft<>0}{PAGE}{FI}
{SUBSTITUTE: 'DEFPRN'}
{str:=page();~~}
{FI}
{OD}
{COMMENT}------------------------------FOOTER-last------------------{COMMENT-}
{IF: lineleft<3 & lineleft<>0}{PAGE}{FI}
{FOOTER}{FOOTER-}
{FONT: 'T8'}{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}{ vp:=1;~~}
{<{lmt+1}>{PRN.width()+lmt} exec('tran_sik','lang',43)}
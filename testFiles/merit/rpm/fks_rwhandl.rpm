:!UTF-8
{TITLE:J. Zestawienie rozrachunków wg przedstawicieli handlowych}
{PRINTER: FINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN1:=obj_new(@.CLASS.PRINT,9+(rep_export()>0)); PRN1.sl_frame(); w:=obj_new(9);
   PRN2:=obj_new(@.CLASS.PRINT,1); PRN2.sl_frame(); s:=obj_new(5);
   PRN3:=obj_new(@.CLASS.PRINT,6); PRN3.sl_frame(); r:=obj_new(5);
   {? var_pres('DRB',@.CLASS)<0 || exec('obj_drbt','raty') ?};
   DRB:=obj_new(@.CLASS.DRB); i:=0;
   s_sign:=color:=''; prn1:=prn2:='PRN1'; lm:=ll:=lmt:=rsep:=0; vp:=1;
   rwn:=rma:=drukuj:=all:=one:=0; han:=null;
   {! _i:=1..8 |! w[_i]:='' !}; {! _i:=1..5 |! r[_i]:=0 !}
}
{END:
   VAR_DEL.delete('PRN1','PRN2','PRN3','w','s','r','DRB');
   &s_sign; &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; &rsep; &han; &rwn; &rma;
   &drukuj; &all; &one; &i
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: rwhandl.rpm
Utworzony:   15.12.2005 [MB]
==================================================
Zawartosc: Zestawienie rozrachunków wg przedstawicieli handlowych
{COMMENT-}
{  PAR_WYDR.win_edit('RWHANDL');
   PAR_WYDR.HAN:=null;
   UNPAR.P1_AE:='UNPAR.P2:={? UNPAR.P1=0 || 1 || 2 ?}; 1'; UNPAR.P1_BV:=UNPAR.P1_BE:='';
   UNPAR.P2_BE:='UNPAR.P1=0'; UNPAR.P2_AE:='{? UNPAR.P2=2 || UNPAR.P2:=0 ?}; 1'; UNPAR.P2_BV:='';
   UNPAR.P3_BV:=UNPAR.P3_BE:=UNPAR.P3_AE:='';
   UNPAR.P4_AE:=UNPAR.P5_AE:='{? chk_fld() || {? ~exec(\'dat_not\',\'dok_fks\',fld) || FUN.info(\'Data spoza aktualnego roku.\'@); 0 || 1 ?} ?}';
   UNPAR.P4_BV:=UNPAR.P4_BE:=UNPAR.P5_BV:=UNPAR.P5_BE:='';
   UNPAR.P4:=SSTALE.AO().POCZ;
   UNPAR.P5:=SSTALE.AO().KON;
   UNPAR.P1:=UNPAR.P2:=2; UNPAR.P3:=0;
   ~~
}
{EXIT: ~PAR_WYDR.edit("{? UNPAR.P4>UNPAR.P5 || FUN.info('Nieprawidłowy zakres dat.'@); 'P4' || 1 ?}")}
{INCLUDE: wsp_pw.rpi}
{  _plus:=0;
   {? rep_export()
   || PRN1.add("HAN.KOD+' - '+HAN.NAZ",8,'Handlowiec'@)
   ?};
   {? OPERATOR.DEPT=null || PRN1.add("w[9]",8,'Jed. ks.'@); _plus+=9 ?};
   PRN1.add("w[1]",51,'Rozrachunek'@);                            PRN3.add("w[1]",42+31+_plus,'SUMA'@,,,,,,,,5);
   PRN1.add("w[2]",10,'Data otwarcia'@);
   PRN1.add("w[3]",10,'Termin płatności'@);
   PRN1.add("w[4]",16,'Stan Wn na koniec zakresu'@,1);            PRN3.add("form(s[1],,2,' ,')",16,'',1);
   PRN1.add("w[5]",16,'Stan Ma na koniec zakresu'@,1);            PRN3.add("form(s[2],,2,' ,')",16,'',1);
   PRN1.add("w[6]",16,'Zapłacono w zakresie'@,1); _plus+=17;      PRN3.add("form(s[3],,2,' ,')",16,'',1);
   {? UNPAR.P1<>1 ||
      PRN1.add("w[7]",16,'Pozostało do zapłaty'@,1); _plus+=17;   PRN3.add("form(s[4],,2,' ,')",16,'',1);
      {? UNPAR.P2
      || PRN1.add("w[8]",16,'W tym przeterminowane'@,1); _plus+=17;
                                                                 PRN3.add("form(s[5],,2,' ,')",16,'',1)
      ?}
   ?};
   PRN2.add("w[1]",76+31+_plus,'');
   PAR_WYDR.TITLE:=
                   {? PAR_WYDR.HAN ||'Zestawienie rozrachunków dla %1'@[PAR_WYDR.HAN().NAZ] || 'Zestawienie rozrachunków wg przedstawicieli handlowych'@ ?};
   ~~
}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{HEADER}
{ prn1:='PRN1'; rsep:=1;~~}
{SUBSTITUTE: 'HEAD'}{SPACE: 'C'}

{IF: page()=1}
{FONT: 'T8G'}{SPACE: 'C'}
{<1 'PARAMETRY WYDRUKU:'@}
{<3 'Zakres dat od %1 do %2'@[$UNPAR.P4,$UNPAR.P5]}
{<3 {? UNPAR.P1=0 || 'Rozrachunki niezapłacone'@ |? UNPAR.P1=1 || 'Rozrachunki zapłacone'@ || 'Rozrachunki wszystkie'@ ?}+
    {? UNPAR.P1<>2 || ' na dzień %1'@[$UNPAR.P5] || '' ?}+
    {? UNPAR.P1=0 & UNPAR.P2 || ' w tym przeterminowane'@ || '' ?}+
    {? UNPAR.P3 || ' z uwzględnieniem płatności ratalnych'@ || '' ?}
}
{<3 {? OPERATOR.DEPT || 'Jednostka księgowa %1'@[OPERATOR.DEPT().OD] || 'Wszystkie jednostki księgowe'@ ?} }
{<3 'Waluta %1'@[SSTALE.WAL().KOD] }
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
{HEADER-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8G'}{SPACE: 'C'}{prn1:=prn2;~~}
{SUBSTITUTE: 'LINIAF'}{prn1:='PRN1';~~}
{FOOTER-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{MACRO: 'STRONA'}
{IF: lineleft()<2 & lineleft()<>0}{PAGE}{prn1:='PRN1';~~}
{ELSE}
{IF: lineleft()=0}{prn1:='PRN1';~~}{ELSE}{prn1:=prn2;~~}{FI}
{FI}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{MACRO: 'OP'}
{  F.RObr(OP.ref(),UNPAR.P4-1); rwn:=F.rwn; rma:=F.rma; drukuj:=1;
   {? OP.DO>=UNPAR.P4 & OP.DO<=UNPAR.P5 | rwn<>rma | exec('zap_typ','fks_sp')
   || F.RObr(OP.ref(),UNPAR.P5);
      _przedplata:={? PAR_SKID.get(47)='N'
                   || (1+OP.TYP='N' & F.rwn<F.rma) | (1+OP.TYP='Z' & F.rma<F.rwn)
                   || 0
                   ?};
      {? UNPAR.P1=2 | (UNPAR.P1=1 & (F.rwn=F.rma | _przedplata)) | (UNPAR.P1=0 & F.rwn<>F.rma & ~_przedplata)
      || w[9]:=OP.ODD().OD;
         w[1]:=OP.SYM;
         w[2]:=$OP.DO;
         w[3]:=$OP.TZ;
         w[4]:=form(F.rwn,,2,' ,'); s[1]+=F.rwn;
         w[5]:=form(F.rma,,2,' ,'); s[2]+=F.rma;
         {? 1+OP.TYP='N'
         || _r:=F.rma-rma; w[6]:=form(_r,,2,' ,'); s[3]+=_r;
            {? PAR_SKID.get(47)='N' & F.rma>F.rwn
            || w[7]:=form(0,,2,' ,')
            || _r:=F.rwn-F.rma; w[7]:=form(_r,,2,' ,'); s[4]+=_r
            ?};
            {? UNPAR.P2
            || w[8]:=form({? UNPAR.P5>OP.TZ || w[7] || 0 ?},,2,' ,');
               s[5]+={? UNPAR.P5>OP.TZ & ~_przedplata || _r || 0 ?}
            ?}
         |? 1+OP.TYP='Z'
         || _r:=F.rwn-rwn; w[6]:=form(_r,,2,' ,'); s[3]+=_r;
            {? PAR_SKID.get(47)='N' & F.rwn>F.rma
               || w[7]:=form(0,,2,' ,')
               || _r:=F.rwn-F.rma; w[7]:=form(_r,,2,' ,'); s[4]+=_r
            ?};
            {? UNPAR.P2
            || w[8]:=form({? UNPAR.P5>OP.TZ || w[7] || 0 ?},,2,' ,');
               s[5]+={? UNPAR.P5>OP.TZ & ~_przedplata || _r || 0 ?}
            ?}
         || drukuj:=0
         ?}
      || drukuj:=0
      ?}
   || drukuj:=0
   ?};
   ~~
}
{IF: drukuj}
{prn2:='PRN1';~~}
{SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
{FI}
{MACRO-}
{MACRO: 'DRB_CPY'}
{  VAR_DEL.delete('t_drb2');
   _size:=obj_len(t_drb);
   _size2:=obj_len(t_drb[1]);
   t_drb2:=obj_new(_size);
   {! _i:=1.._size
   |! t_drb2[_i]:=obj_new(_size2);
     {! _j:=1.._size2
     |! t_drb2[_i][_j]:=t_drb[_i][_j]
     !}
   !};
~~}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{MACRO: 'OP_DRB'}
{IF: UNPAR.P3}
   {DRB.set_tab(OP.ref(),,UNPAR.P5); ~~}
   {IF: DRB.EXIST}
      {F.RObr(OP.ref(),UNPAR.P4-1); rwn:=F.rwn; rma:=F.rma;
       drukuj:=OP.DO>=UNPAR.P4 & OP.DO<=UNPAR.P5 | rwn<>rma;
      ~~}
      {IF: drukuj}
         {SUBSTITUTE: 'DRB_CPY'}
         {DRB.set_tab(OP.ref(),,UNPAR.P4-1);
          i:=1; drukuj:=0;
          w[9]:=OP.ODD().OD;
          w[1]:=OP.SYM;
          w[2]:=$OP.DO; rsep:=1;
          ~~}
         {WHILE: i<=D_RB.size()}
         {DO}
            {IF: (UNPAR.P1=2 | (UNPAR.P1=1 & t_drb2[i][3]<=UNPAR.P5 & t_drb2[i][1]=t_drb2[i][2]) | (UNPAR.P1=0 &  t_drb2[i][1]<>t_drb2[i][2] ))
            }
               {w[3]:=$t_drb2[i][3];
                w[4]:=form(t_drb2[i][1],,2,' ,'); s[1]+=t_drb2[i][1];
                w[5]:=form(t_drb2[i][2],,2,' ,'); s[2]+=t_drb2[i][2];
                _r:={? 1+OP.TYP='N' || t_drb2[i][2]-t_drb[i][2] || t_drb2[i][1]-t_drb[i][1] ?};
                w[6]:=form(_r,,2,' ,'); s[3]+=_r;
                _r:=(t_drb2[i][1]-t_drb2[i][2]);
                {? PAR_SKID.get(47)='N' & OP.TYP='NAL' & OP.MA>OP.WN | OP.TYP='ZOB' & OP.WN>OP.MA
                || w[7]:=form(0,,2,' ,')
                || w[7]:=form(_r,,2,' ,'); s[4]+=_r
                ?};
                w[8]:=form({? UNPAR.P5>t_drb2[i][3] || w[7] || 0 ?},,2,' ,');
                s[5]+={? UNPAR.P5>t_drb2[i][3] || _r || 0 ?};
                prn2:='PRN1';
                ~~
               }
               {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
               {w[1]:=w[2]:=''; rsep:=0; drukuj+=1; ~~}
            {FI}
            {i+=1;~~}
         {OD}
         {rsep:=1;~~}
         {IF: drukuj=0}
            {SUBSTITUTE: 'OP'}
         {FI}
      {FI}
   {ELSE}
      {SUBSTITUTE: 'OP'}
   {FI}
{ELSE}
   {SUBSTITUTE: 'OP'}
{FI}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{MACRO: 'HAN'}
{  OP.index('HAN'); one:=0;
   {? OPERATOR.DEPT
   || OP.prefix(SSTALE.WAL,HAN.ref(),OPERATOR.DEPT)
   || OP.prefix(SSTALE.WAL,HAN.ref())
   ?}; drukuj:=0;
   {? OP.first() ||
      {! |?
         F.RObr(OP.ref(),UNPAR.P4);
         {? PAR_SKID.get(47)='N'
         || drukuj:={? OP.DO>=UNPAR.P4 & OP.DO<=UNPAR.P5 | F.rwn<>F.rma | exec('zap_typ','fks_sp')
                    || F.RObr(OP.ref(),UNPAR.P5);
                       UNPAR.P1=2
                       | (UNPAR.P1=1 & ((1+OP.TYP='N' & F.rwn<=F.rma) | (1+OP.TYP='Z' & F.rma<=F.rwn)))
                       | (UNPAR.P1=0 & ((1+OP.TYP='N' & F.rwn>F.rma) | (1+OP.TYP='Z' & F.rma>F.rwn)))
                    || 0
                    ?}
         || drukuj:={? OP.DO>=UNPAR.P4 & OP.DO<=UNPAR.P5 | (1+OP.TYP='N' & F.rwn>F.rma) | (1+OP.TYP='Z' & F.rma>F.rwn) | exec('zap_typ','fks_sp')
                    || F.RObr(OP.ref(),UNPAR.P5);
                       UNPAR.P1=2 | (UNPAR.P1=1 & F.rwn=F.rma) | (UNPAR.P1=0 & F.rwn<>F.rma)
                    || 0
                    ?}
         ?};
         ~drukuj & OP.next()
      !}
   ?};~~
}
{IF: drukuj}
   { prn2:='PRN2'; w[1]:=HAN.KOD+' - '+HAN.NAZ; rsep:=1; {! _i:=1..5 |! s[_i]:=0 !}; ~~}
   {IF: ~rep_export()}
   {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
   {FI}
   {IF: OPERATOR.DEPT}
      {FOR: 'OP'}{INDEX: 'HAN'}{PREFIX: SSTALE.WAL,HAN.ref(),OPERATOR.DEPT}
      {DO}
         {SUBSTITUTE: 'OP_DRB'}{one+=drukuj;~~}
      {OD}
   {ELSE}
      {FOR: 'OP'}{INDEX: 'HAN'}{PREFIX: SSTALE.WAL,HAN.ref()}
      {DO}
         {SUBSTITUTE: 'OP_DRB'}{one+=drukuj;~~}
      {OD}
   {FI}
   { {! _i:=1..5 |! r[_i]+=s[_i] !}; ~~}
   {IF: one>1 & ~rep_export()}
      {w[1]:='Suma'; prn2:='PRN3';~~}{FONT: 'T8GB'}
      {SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}{FONT: 'T8G'}
   {FI}
{FI}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{IF: PAR_WYDR.HAN}
   {PAR_WYDR.HAN();~~}
   {SUBSTITUTE: 'HAN'}{all+={? one || 1 || 0 ?};~~}
{ELSE}
   {FOR: 'HAN'}
   {INDEX: 'NAZ'}
   {DO}
      {SUBSTITUTE: 'HAN'}{all+={? one || 1 || 0 ?};~~}
   {OD}
{FI}
{IF: all<>1}
{w[1]:='Razem'@; prn2:='PRN3'; {! _i:=1..5 |! s[_i]:=r[_i] !}; ~~}{FONT: 'T8GB'}
{SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}{FONT: 'T8G'}
{FI}
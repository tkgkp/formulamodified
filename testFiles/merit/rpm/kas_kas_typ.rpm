:!UTF-8
{TITLE:C. Zestawienie wartości dokumentów w podziale na typy dokumentów}
{PRINTER: KINFO.SZ_PRN,,,,,'E','C',,,,,,1,,,,,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','PRNF','RAPORT3','__Ttyp','tryb','kasa');
   PRN:=obj_new(@.CLASS.PRINT,10); PRN.sl_frame();
   PRNF:=obj_new(@.CLASS.PRINT,10); PRNF.sl_frame();
   tryb:=rep_export();
   kasa:='';
   color:=w1:=w2:=prn1:=prn2:='';temp1:=temp2:=temp3:=''; flag:=1; znacz:=''; bl:=0; s:=0;wart:=0; w:=0;
   lm:=ll:=vp:=lmt:=rsep:=0; lp:=0; suma_typ:=0; suma:=0;
   prnw:=prn1:='PRN';
   __typ_wyb:=2;
   prn2w:=prn2:='PRNF';
   WYDRUKI.TYP_DOK:='';
   znz:=0;
   rodz_mks:=''; ref_mks:=0
}
{END:
   VAR_DEL.delete('PRN','PRNF','RAPORT3','__Ttyp','tryb','kasa');
   &suma_typ; &lp; &suma; &__typ_wyb;
   &color; &w1; &w2; &prn1; &prn2; &temp1; &temp2; &temp3; &flag; &znacz; &bl; &s; &wart; &w;
   &rodz_mks; &ref_mks; &znz;
   &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
------------------------------------------------------------------------------
| Wydruk podsumowania wartoŁci typ≤w dokument≤w kasowych wraz z zaznaczeniem |
| jakie dokumnety wchodz╣ w sk│adowe sumy og≤lnej.                           |
| S│awomir Pu│ecki [SP] - 2005.12.02                                         |
------------------------------------------------------------------------------
{COMMENT-}
{EXIT: ~exec('an_ptyp','kasa_wydruki')}
{INCLUDE: wsp_pw.rpi}
{  STANKAS.cntx_psh;
   {? rodz_mks='S'
   || {? STANKAS.seek(BB.sqlint(ref_mks),STANKAS.name)
      || KUSERUPR.index('KU_ST');
         KUSERUPR.prefix(USERS.ref,STANKAS.ref);
         {? KUSERUPR.first()
         || temp1:=STANKAS.SYM;
            temp2:=STANKAS.NAZWA;
            temp3:=$STANKAS.KOD;
            temp3:='and DOKUMENT.REFERENCE like \'kdo'+(('00'+temp3)+3)+'%\''
         || temp1:='';
            temp2:='';
            temp3:=' and 0=1'
         ?}
      ?}
   || _ok:=0;
      {? rodz_mks='M' & MKASA.seek(BB.sqlint(ref_mks),MKASA.name)
      || temp1:=MKASA.NAZ;
         KUSERUPR.index('MKASAST');
         KUSERUPR.prefix(MKASA.ref);
         _ok:=1
      || temp1:='wszystkie uprawnione';
         KUSERUPR.index('KU');
         KUSERUPR.prefix(USERS.ref);
         _ok:=1
      ?};
      temp2:=temp3:='';
      {? KUSERUPR.first()
      || temp3+=' and (';
         {! |? {? rodz_mks='M'
               || KUSERUPR.cntx_psh;
                  KUSERUPR.index('KU_ST');
                  KUSERUPR.prefix(OPERATOR.USER,KUSERUPR.STANKAS);
                  {? KUSERUPR.first
                  || _upr:='T'
                  || _upr:='N'
                  ?};
                  KUSERUPR.cntx_pop;
                  {? _upr='T'
                  || temp3+=' DOKUMENT.REFERENCE like \'kdo'+(('00'+$KUSERUPR.STANKAS().KOD)+3)+'%\' or '
                  ?}
               || temp3+=' DOKUMENT.REFERENCE like \'kdo'+(('00'+$KUSERUPR.STANKAS().KOD)+3)+'%\' or '
               ?};
               KUSERUPR.next()
         !};
         {? temp3=' and ('
         || temp3:=' and 0=1'
         || temp3:=(temp3-3)+') '
         ?}
      || temp3:=' and 0=1'
      ?}
   ?};
   STANKAS.cntx_pop;
   ~~
}
{EXIT: {? temp3=' and 0=1'
       || FUN.info('Brak uprawnień do wybranych stanowisk lub multikas.'@);
          1
       || 0
       ?}
}
{     {? var_pres('__Ttyp')<0
      || znz:=1;
         __Ttyp:=sql('select distinct TYP_DOK, 1 as jest, \' \' zn from @DOKUMENT where '+
            ' DATA between to_date(:_a) and to_date(:_b) '+{? WYDRUKI.TYP_DOK<>'' || 'and TYP_DOK like (\''+WYDRUKI.TYP_DOK+'%\') ' || '' ?}
            ,WYDRUKI.DAT_OD, WYDRUKI.DAT_DO)
      ?};

   _sql:='select 0 as LP, SUBSTR(DOKUMENT.REFERENCE, 4, 3) as TYP0,  RAPORT.DATA_OD as DATA,RAPORT.NUM_RAP as NUM,DOKUMENT.DATA as DATA1,DOKUMENT.TYP_DOK as TYP, ';
   _sql+=' case when DOKUMENT.KW_PLN=0 and DOKUMENT.DOK_NUM<>0 then \'----dokument anulowany!----\' else DOKUMENT.OPIS end as OPIS, ';
   _sql+=' DOKUMENT.KW_PLN as KWOTA, DOKUMENT.PLUS_MIN as PLUS_MIN, DOKUMENT.DOK_NUM as NUM2 ';
   _sql+='from @DOKUMENT left join @RAPORT using (DOKUMENT.RAPORT,RAPORT.REFERENCE) ';
   _sql+='where TYP_DOK in (select TYP_DOK from :_c where JEST=1) and ';
   _sql+=' DOKUMENT.DATA>=to_date(:_a) and DOKUMENT.DATA<=to_date(:_b) and DOKUMENT.PLUS_MIN=\'+\' :_d ';
   _sql+='order by 2,5,4,3 ';
   _sql+='union all ';
   _sql+='select 0 as LP, SUBSTR(DOKUMENT.REFERENCE, 4, 3) as TYP0,  RAPORT.DATA_OD as DATA,RAPORT.NUM_RAP as NUM,DOKUMENT.DATA as DATA1,DOKUMENT.TYP_DOK as TYP, ';
   _sql+=' case when DOKUMENT.KW_PLN=0 and DOKUMENT.DOK_NUM<>0 then \'----dokument anulowany!----\' else DOKUMENT.OPIS end as OPIS, ';
   _sql+=' (-DOKUMENT.KW_PLN) as KWOTA, DOKUMENT.PLUS_MIN as PLUS_MIN, DOKUMENT.DOK_NUM as NUM2 ';
   _sql+='from @DOKUMENT left join @RAPORT using (DOKUMENT.RAPORT,RAPORT.REFERENCE) ';
   _sql+='where TYP_DOK in (select TYP_DOK from :_c where JEST=1) and ';
   _sql+=' DOKUMENT.DATA>=to_date(:_a) and DOKUMENT.DATA<=to_date(:_b) and DOKUMENT.PLUS_MIN=\'-\' :_d ';
   _sql+='order by 2,6,5,4,3 ';
   RAPORT3:=sql(_sql,WYDRUKI.DAT_OD,WYDRUKI.DAT_DO,__Ttyp,temp3 );
   RAPORT3.first;
   {? tryb=1 || PRN.add("form(kasa)",10,'Stanowisko kasowe'@) ?};
   PRN.add("$RAPORT3.NUM2",5,'Nr'@,1);
   PRN.add("$RAPORT3.DATA1",10,'Data'@);
   PRN.add("$RAPORT3.NUM",6,'Numer rap.'@,1);
   PRN.add("RAPORT3.PLUS_MIN",3,'+/-');
   PRN.add("RAPORT3.TYP",4,'Typ dok.'@);
   PRN.add("form(RAPORT3.KWOTA,,2)+{? RAPORT3.NUM2<>0 & RAPORT3.KWOTA=0 || '*'||' '?}",14,'Wartość'@,1);
   PRN.add("RAPORT3.OPIS",30,'Opis'@);
   PRNF.add("w1",32);
   PRNF.add("form(sum('suma_typ'),,2)+' '",14,,1);
   PAR_WYDR.TITLE:='Zestawienie wartości'@+{? WYDRUKI.TYP_DOK='' || ' wszystkich '@ || ' '?}+
      'dokumentów'@+{? WYDRUKI.TYP_DOK='' | WYDRUKI.TYP_DOK='*' || ' w podziale na ich typy'@ || ' '+WYDRUKI.TYP_DOK ?}+
      '\n za okres '@+$WYDRUKI.DAT_OD+' do '@+$WYDRUKI.DAT_DO;
   prn1:='PRN'; prn2:='PRNF'; ~~}
{EXIT: ~(var_pres('RAPORT3')>5)}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{FONT: 'HEAD'}{SPACE: 'B'}
{SUBSTITUTE: 'HEAD'}
{COMMENT}{ lmt:=int((charleft()-PRN.width())/2); ~~}{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{IF: page=1}
{<3 'Parametry wydruku: '+{? znz & WYDRUKI.TYP_DOK=''
                          || 'dla wszystkich typów'
                          |? znz
                          || 'dla '+WYDRUKI.TYP_DOK+'%'
                          || 'dla wybranych typów'
                          ?}}
{<5 'Rodzaj raportu: '+{? rodz_mks='M'
                       || ' dla multikasy '+temp1
                       |? rodz_mks='S'
                       || {? KPARAM.CZY_B='T' || ' dla raportów rodzajowych (stanowiska) "' || 'dla stanowiska "' ?}+temp2+'" '
                       || {? KPARAM.CZY_B='T' || 'wszyskie dostępne raporty (stanowiska)' || 'wszystkie dostępne stanowiska' ?}
                       ?}}
{COMMENT}{<5 'Waluta: '+{? STANKAS.WALUTA<>null || STANKAS.WALUTA().SYM || 'wszystkie waluty (raport w stanowisku wielowalutowym)' ?}}{COMMENT-}
{FI}
{FONT: 'T10B'}{SPACE: 'B'}
{IF: page=1}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{FI}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{ vp:=0;~~}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{przycz:=0;~~}
{FOR: RAPORT3}
{DO}
   {kasa:=RAPORT3.TYP0;~~}
   {ADD: suma_typ; #; RAPORT3.KWOTA}
   {suma+=RAPORT3.KWOTA; ~~}
   {IF: RAPORT3.PLUS_MIN='-'}
      { RAPORT3.KWOTA:=-RAPORT3.KWOTA;~~}
   {FI}
{IF:przycz=0}
     { rsep:=0; ~~}
   {ELIF:przycz=1}
     { rsep:=0; ~~}
   {ELSE}
   {IF: lineleft<2 & lineleft>0}
     {PAGE}
     { vp:=rsep:=0; ~~}
     { _f:=prn2+'.setcolor(''255:255:255'')'; ($(_f))()}
   {ELIF: lineleft}
      {REP: _f:=prn2+'.fjbar('+prn1+',lmt)'; ($(_f))()}
   {ELSE}
      { vp:=rsep:=0; ~~}
   {FI}
   { rsep:=0; ~~}
    { przycz:=0; ~~}
{FI}
   {SUBSTITUTE: 'LINIA0'}{prnw:=prn1; xx:=1; ~~}
   {TOTAL: RAPORT3.TYP+' (stan.: '+RAPORT3.TYP0+')'}
   {IF: tryb<>1}
   {FONT: 'T10B'}{SPACE: 'B'}
   {w1:='Ogółem typu : '+grp_fld; ~~}
   {IF:lineleft<4 & lineleft>0}{PAGE}{FI}
   { rsep:=1; ~~}
   {SUBSTITUTE: 'LINIA02'}{prnw:=prn2; ~~}
   {FONT: 'T10'}{SPACE: 'B'}
   { przycz:=2; ~~}
   { rsep:=1; ~~}
   {FI}
   {TOTAL}
   { rsep:=0; ~~}
   {IF: sum('suma_typ')<>0}
      { rsep:=0; ~~}
   {FI}
{OD}
{ vp:=0; ~~}
{ rsep:=0; ~~}
{IF:lineleft>2}{prn1:=prnw; ~~}{ELSE}{ vp:=1; ~~}{FI}

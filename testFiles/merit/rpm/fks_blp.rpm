:!UTF-8
{TITLE:A. Sprawdzenie API białej listy podatników}
{PRINTER: FINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('PRN1','NOMAXP','w','helper');
   PRN1:=obj_new(@.CLASS.PRINT,2); PRN1.s_frame();
   w:=obj_new(2);
   color:=''; prn1:=prn2:='PRN1';
   lm:=ll:=vp:=lmt:=rsep:=NOMAXP:=0;
   helper:=obj_new('set','value','rb');
   helper.set:="
      {! _ii:=1..obj_len(w) |! w[_ii]:='' !};
      {! _ii:=1.._
      |! {? type_of(_[_ii])=4
         || {? _[_ii]=date(0,0,0)
            || w[_ii]:=''
            || w[_ii]:=$_[_ii]
            ?}
         || w[_ii]:=_[_ii]
         ?}
      !};
      ~~
   ";
   helper.value:="
      _show:={? var_press('_b')>0 || _b || 1 ?};
      {? _show
      || form(_a,,2,' ,')
      || ''
      ?}
   ";
   helper.rb:="
      _ret:=2+_a+' '; _a:=2-_a;
      {!
      |? {? +_a>4
         || _ret+=(4+_a)+' ';
            _a:=4-_a;
            1
         ?}
      !};
      _ret+=_a;
      _ret
   ";
   ~~
}
{END:
   VAR_DEL.delete('PRN1','NOMAXP','w','helper','TabRB');
   &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep
}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------MACRO INIT ------------------{COMMENT-}
{  PRN1.add("w[1]",50,'Nazwa');
   PRN1.add("w[2]",84,'Wartość',,'#');
   ~~}
{PAR_WYDR.TITLE:='Sprawdzenie '+{? KHCHKNIP.TYPE='K' || 'podatnika VAT' || 'rachunku bankowego' ?};~~}
{dalej:=var_pres('PrnObj')<=0 | PrnObj.first();~~}
{WHILE: dalej}
{DO}
{IF: var_pres('PrnObj')<=0 | PrnObj.drukuj()}
{HEADER}{HEADER-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN1.width())/2); vp:=1;~~}

{>22 'Data zapytania: '}{<23 $KHCHKNIP.DATE}
{IF: KHCHKNIP.TIME<>time(0,0,0)}{>22 'Czas zapytania: '}{<23 $KHCHKNIP.TIME}{FI}

{IF: $KHCHKNIP.DATA_DO=$KHCHKNIP.D_ACTIVE}
{>22 'Stan na: '}{<23 $KHCHKNIP.D_ACTIVE}
{ELSE}
{>22 'Data od: '}{<23 $KHCHKNIP.D_ACTIVE}
{>22 'Data do: '}{<23 $KHCHKNIP.DATA_DO}
{FI}
{>22 'NIP: '}{<23 KHCHKNIP.NIP}
{IF: KHCHKNIP.TYPE='B'}
{>22 'Rachunek: '}{<23 helper.rb(KHCHKNIP.RBK)}
{FI}

{>22 'Identyfikator: '}{FONT: 'T8GB'}{<23 KHCHKNIP.ID_REQ}{FONT: 'T8G'}

{SUBSTITUTE: 'TABHEAD'}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}

{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{~~}
{FOOTER-}
{IF: KHCHKNIP.TYPE='K'}
{helper.set('Nazwa firmy',KHCHKNIP.NAZ); rsep:=1;~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('NIP',KHCHKNIP.NIP); ~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('PESEL',KHCHKNIP.PESEL); ~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Status podatnika (wg stanu na dzień sprawdzenia)',KHCHKNIP.STATUS); ~~}{FONT: 'T8GB'}{SUBSTITUTE: 'LINIA0'}{FONT: 'T8G'}
{helper.set('REGON',KHCHKNIP.REGON); ~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Numer KRS',KHCHKNIP.KRS); ~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Adres',KHCHKNIP.ADRES); ~~}{SUBSTITUTE: 'LINIA0'}
{STR.split(KHCHKNIP.memo_txt(,1,'OSOBY'),'\n');~~}
{helper.set('Osoby',STR.get_word()); ~~}{SUBSTITUTE: 'LINIA0'}
{WHILE: STR.next()}
{DO}
{helper.set('',STR.get_word()); rsep:=0;~~}{SUBSTITUTE: 'LINIA0'}
{OD}
{TabRB:=spli_str(KHCHKNIP.memo_txt(,1,'RB'),','); i:=1;~~}
{IF: obj_len(TabRB)=0}
{helper.set('Rachunki bankowe'); rsep:=1;~~}{SUBSTITUTE: 'LINIA0'}
{ELSE}
{WHILE: obj_len(TabRB)>=i}
{DO}
{helper.set({? i=1 || 'Rachunki bankowe' || '' ?},helper.rb(|TabRB[i])); rsep:=i=1;~~}{SUBSTITUTE: 'LINIA0'}
{i+=1;~~}
{OD}
{FI}
{helper.set('Data rejestracji jako podatnika VAT',KHCHKNIP.D_REG); rsep:=1;~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Data wykreślenia rejestracji jako podatnika VAT',KHCHKNIP.D_REMOVE); ~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Podstawa prawna wykreślenia',KHCHKNIP.P_REMOVE); rsep:=0;~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Data przywrócenia rejestracji jako podatnika VAT',KHCHKNIP.D_REST); rsep:=1;~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Podstawa prawna przywrócenia',KHCHKNIP.P_REST); rsep:=0;~~}{SUBSTITUTE: 'LINIA0'}
{ELSE}
{helper.set('NIP',KHCHKNIP.NIP); rsep:=1;~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Rachunek bankowy',helper.rb(KHCHKNIP.RBK)); ~~}{SUBSTITUTE: 'LINIA0'}
{helper.set('Aktywny',{? KHCHKNIP.C_ACTIVE='T' || 'TAK' || 'NIE' ?}); ~~}{FONT: 'T8GB'}{SUBSTITUTE: 'LINIA0'}{FONT: 'T8G'}
{FI}
{FI}
{dalej:=var_pres('PrnObj')>0 & PrnObj.next();~~}
{IF: dalej & lineleft()<>0}{PAGE}{FI}
{OD}
:!UTF-8
{TITLE: Błędy zarejestrowanych odczytów}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('vp','lmt','rsep','h_rat','tresc','pb','Temp','Data','Czas','Tekst','NrCzyt','Cnt','blad','druk');

   _TAB:=exec('tab_prac','!prc_czp_wydr');
   {? _TAB.first()
   || tresc:='w_bledy5';
      PAR_WYDR.TITLE:='Błędy odczytów'@;
      rsep:=PAR_WYDR.SEP;

      pb:=lmt:=blad:=Temp:=Cnt:=0;
      vp:=h_rat:=1;
      Data:=date();
      Czas:=time();
      Tekst:=NrCzyt:='';
      druk:=FUN.ask('Czy drukować szczegóły ?'@)
   ?};
   params_set('P',_TAB)
}
{END:
   VAR_DEL.delete('vp','lmt','rsep','h_rat','tresc','pb','Temp','Data','Czas','Tekst','NrCzyt','Cnt','blad','druk');
   exec('czytaj','#stalesys',date(),KST,'R_TZ')
}
{EXIT: ~params_get().P.first()}
{COMMENT}
   Zawartosc: Błędy zarejestrowanych odczytów
{COMMENT-}
{MARGIN: 0.5}
{COMMENT}OLD: w_bledy5.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{  h_rat:=charleft(); ~~}
   {FONT: 'T8G'}{ h_rat:=charleft()/h_rat; ~~}
   {SPACE: 'C'}
   {LINE}
   { pb:=1; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'%1 %2'['Od daty:'@,VAR_EDIT.D_OD$1]}
   {<{ceil(lmt*h_rat)+1}'%1 %2'['Do daty:'@,VAR_EDIT.D_DO$1]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{IF:  VAR_EDIT.win_edit('ZAKR_DAT');
      VAR_EDIT.D_OD:=date(,,1);
      VAR_EDIT.D_DO:=date(,,0);
      VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_OD');
      VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_DO');
      VAR_EDIT.edit(
         "  _result:=__CHK.record(VAR_EDIT,,'D_OD','D_DO');
            {? _result='' & VAR_EDIT.D_OD>=VAR_EDIT.D_DO
            || FUN.emsg('Wartość w polu \"Data od\" nie może być większa niż w polu \" Data do\".');
               _result:='D_OD'
            ?};
            _result
         "
      )}
   {exec('czytaj','#stalesys',VAR_EDIT.D_DO,KST,'R_TZ'); ~~}
   {FOR: params_get().P}
   {DO}
      {IF: P.seek(params_get().P.SQL,,1)}
         {  __IP:=0; Cnt:=VAR_EDIT.D_OD; ~~}
         {WHILE: Cnt<=VAR_EDIT.D_DO}
         {DO}
            {  _Mask:=MASK.MaskZbio(Cnt~1,Cnt~2);
               {? (R_REJ_WW.name()+4)<>_Mask || MASK.Use('R_REJ_WW',Cnt~1,Cnt~2) ?};
               blad:=0; ~~
            }
            {ENTIRE}
            {FOR: R_REJ_WW}
            {INDEX: 'R_REJ_WX'}
            {PREFIX: P.ref(),Cnt}
            {DO}
               {FIRST}
               {  blad:=0;
                  {? (R_REJ_WW.size() %* 2)<>0
                  || 'zła liczba odczytów';
                     blad+=1
                  ?};
                  typ:='';
                  pop:=R_REJ_WW.GD; ~~
               }
               {FIRST-}
               {  {? R_REJ_WW.prev() || pop:=R_REJ_WW.GD; R_REJ_WW.next() ?};
                  {? typ<>R_REJ_WW.TP
                  || typ:=R_REJ_WW.TP
                  || 'brak zmiany wewe wywy';
                     blad+=10
                  ?};
                  {? R_REJ_WW.DZK<>R_REJ_WW.DZ & R_REJ_WW.GD<>*0 & pop<>*0 & R_REJ_WW.ZM<>'D'
                  || 'być może błąd';
                     blad+=100
                  ?}; ~~
               }
            {OD}
            {IF: blad}
               {IF: __IP<>P.IP}
                  {FONT: 'T8B'}{SPACE: 'B'}{<{ceil(lmt*h_rat)+1}P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE
                  }{FONT: 'T8'}{SPACE: 'B'}{<{ceil(lmt*h_rat)+1+30}'Wydział:'@+' '+P.WYDZIAL().SYMBOL
                  }{IF: +(|P.WYDZIAL().OPIS)} ({P.WYDZIAL().OPIS}){FI}{<{ceil(lmt*h_rat)+1}'Nr teczki:'@+' '+(|P.T)}
               {FI}
               {FONT: 'T8B'}{SPACE: 'B'}{<{ceil(lmt*h_rat)+1+10}'BŁĄD:'@+' '+
                  {? blad>=100 || 'kwalifikacja'@
                  |? blad>=10 || 'podwójne WE lub WY'@
                  || 'brak odczytu'@
                  ?}+' '+'dnia:'@+' '+form(Cnt)}
               {IF: __IP<>P.IP}
                  {IF: druk}
                     {FONT: 'T8B'}{SPACE: 'B'}
                     {<{ceil(lmt*h_rat)+1+5}'Data kwalifikacji'@
                     }{<{ceil(lmt*h_rat)+1+25}'Data rejestracji'@
                     }{<{ceil(lmt*h_rat)+1+45}'Godzina'@
                     }{<{ceil(lmt*h_rat)+1+65}'Rodzaj'@}
                  {FI}
                  {__IP:=P.IP; ~~}
               {FI}
               {IF: druk}
                  {FOR: R_REJ_WW}
                  {INDEX: 'R_REJ_WX'}
                  {PREFIX: P.ref(),Cnt}
                  {DO}
                     {FONT: 'T8'}{SPACE: 'B'}
                     {<{ceil(lmt*h_rat)+1+5}R_REJ_WW.DZK
                     }{<{ceil(lmt*h_rat)+1+25}R_REJ_WW.DZ
                     }{<{ceil(lmt*h_rat)+1+45}R_REJ_WW.GD
                     }{<{ceil(lmt*h_rat)+1+65}R_REJ_WW.TP}
                  {OD}
                  {LINE}
               {FI}
            {FI}
            {ENTIRE-}
            {  Cnt+=1; ~~}
         {OD}
         {  Temp:=0; ~~}
      {FI}
   {OD}
{FI}
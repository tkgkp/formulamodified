:!UTF-8
{TITLE: Osoby w wieku od 55 do 70 lat, które mogą zadeklarować uczestnictwo}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','__TAB','ppklos55');
   tresc:='ppklos55';
   PAR_WYDR.TITLE:='Osoby w wieku od 55 do 70 lat, które mogą zadeklarować uczestnictwo'@;
   ppklos55:=obj_new('err');
   ppklos55.err:='';
   par:=obj_new(5);
   par[1]:=0;
   _upr:=exec('uprawnieniawrap','pkd','rejestracji komunikatów PPK'@,'PPK_EWI_RKOM');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do: %1'@[_upr])
   || par[5]:=rep_export();
      _dt:=date();
      par[4]:=exec('par2wydr','ppk_rep');
      {? par[4].size()
      || VAR_EDIT.D_OD:=par[4].OD;
         VAR_EDIT.D_DO:=par[4].DO;
         PPK_KRD.cntx_psh();
         PPK_KRD.index('SYMBOL');
         PPK_KRD.prefix('O','DUOS');
         {? PPK_KRD.first()
         || __TAB:=exec('ponad_55','ppk_rep',PPK_KRD.ref(),par[4]);
            {? __TAB.first()
            || par[3]:=obj_new(@.CLASS.PRINT,10);
               {? ~par[5] || par[3].add("__TAB.LP",7,'Lp.'@,1,,,,,',,\'99\'') ?};
               par[3].add("__TAB.NAZWISKO+' '+__TAB.PIERWSZE+' '+__TAB.DRUGIE",30,'Osoba'@);
               par[3].add("__TAB.PESEL",11,'PESEL'@);
               par[3].add("__TAB.UR_DATA$1",10,'Data urodzenia'@);
               par[3].add("__TAB.TYPDOK",6,'Rodzaj'@);
               par[3].add("__TAB.SYMDOK",15,'Symbol dokumentu'@);
               par[3].add("__TAB.ADRES",35,'Adres'@);
               par[3].add("__TAB.EMAIL",35,'Adres e-mail'@);
               par[3].add("__TAB.PRZ_POW",12,'Powiadomiono'@);
               par[3].add("{? __TAB.DATA=date(0,0,0) || '' || __TAB.DATA$1 ?}",11,'Data przekazania'@);
               par[3].s_frame();

               prn1:='par[3]';
               lmt:=rsep:=0;
               par[1]:=par[2]:=vp:=h_rat:=1
            ?}
         || FUN.emsg('Nieprawidłowa parametryzacja.\nBrak definicji komunikatu dla osób "DUOS".')
         ?};
         PPK_KRD.cntx_pop()
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','h_rat','__TAB','ppklos55')
}
{EXIT: ~par[1]}
{COMMENT}OLD: ppklos55.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[2]}
{FONT: 'T8'}{ h_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ h_rat:=charleft()/{? par[5] || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[2]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1} 'Parametry sprawozdania'@}
   {IF: par[4].BRAK='T'}
      {<{ceil(lmt*h_rat)+1} 'Data tworzenia komunikatu: %1' [$par[4].DT]}
   {FI}
   {<{ceil(lmt*h_rat)+1} 'Komunikaty nieprzekazane: %1' [par[4].BRAK]}
   {IF: par[4].EMAIL='T' | par[4].WYDRUK='T'}
      {<{ceil(lmt*h_rat)+1} 'Komunikaty przekazane e-mail''em: %1' [par[4].EMAIL]}
      {<{ceil(lmt*h_rat)+1} 'Komunikaty przekazane wydrukiem: %1' [par[4].WYDRUK]}
      {<{ceil(lmt*h_rat)+1} 'Okres przekazania komunikatu: %1 - %2' [$par[4].OD,$par[4].DO]}
   {FI}
   {IF: +ppklos55.err}
      {<{ceil(lmt*h_rat)+1} 'Ostrzeżenia: %1' [ppklos55.err]}
   {FI}
{FI}

{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
   {SUBSTITUTE: 'LINIA0'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[3].width())/2); ~~ }
{FOR: __TAB}
{DO}
   {SUBSTITUTE: tresc}
{OD}
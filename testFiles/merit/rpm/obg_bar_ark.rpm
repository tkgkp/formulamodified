:!UTF-8
{TITLE:A. Wydruk etykiet (52x29) do skanowania}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,2,2,2,2,0,1}
{BEGIN:
      temp1:=temp2:=temp3:=temp4:=''; nr_lbl:=nr_ark:=0;
      STR.split(REF.FIRMA().OPIS);
      firma1:=STR.line(25);
      firma2:=STR.line(25)
}
{END: &temp1; &temp2; &temp3; &temp4; &nr_lbl; &nr_ark; &firma1; &firma2}
{EXIT:
    _exit:=0;
    {? ~_exit
    || undefine();
       define('ARKUSZY',1,'Ile arkuszy etykiet:'@,,10,10,-1);
       {? ~def_edit("exec('ae_druk_ety','obiegi',2)")
       || _exit:=1
       || _exit:=0
       ?}
    ?};
    _exit
}
{DEFINEFONT: 'courier=Courier New,,m,8'}
{FONT: 'courier'}
{COMMENT}
(c) 2014 Macrologic
==============================================================================
    system: O B I E G I
      plik: BAR_ARK.RPM
            +----------------------------------------------------------------+
  Zawartość | Drukowanie etykiet do skanowania                               |
            +----------------------------------------------------------------+
==============================================================================
{COMMENT-}
{MACRO:'etykieta'}
{ENTIRE}
{
temp1:=exec('obiegi_etyk','urzadzenia');
temp2:=exec('obiegi_etyk','urzadzenia');
temp3:=exec('obiegi_etyk','urzadzenia');
temp4:=exec('obiegi_etyk','urzadzenia')
;~~}
{FONT: 'courier'}
{SPACE: 'C'}
{LINE:2}
{SPACE: 'A'}{<4}{BARCODE:temp1; 22; 1;PAR_WYDR.KODKRESK}{<38}{BARCODE:temp2; 22; 1;PAR_WYDR.KODKRESK}{<73}{BARCODE:temp3; 22; 1;PAR_WYDR.KODKRESK}{<108}{BARCODE:temp4; 22; 1;PAR_WYDR.KODKRESK}
{SPACE: 'C'}
{FONT: 'courier'}{<1>25 firma1}{<35>60 firma1}{<70>95 firma1}{<105>130 firma1}
{IF: firma2<>''}
{FONT: 'courier'}{<1>25 firma2}{<35>60 firma2}{<70>95 firma2}{<105>130 firma2}
{FI}
{LINE:{? nr_lbl=10 || 1 || {? firma2='' || 4 || 3 ?} ?}}
{ENTIRE-}
{MACRO-}
{WHILE: (nr_ark+=1)<=DEFINE.ARKUSZY}
{DO}
   {WHILE: (nr_lbl+=1)<11 }
   {DO}
      {SUBSTITUTE: 'etykieta'}
   {OD}
   {IF: nr_ark<DEFINE.ARKUSZY}{PAGE}{nr_lbl:=0; ~~}{FI}
{OD}
{COMMENT}
==============================================================================
      koniec pliku
==============================================================================
{COMMENT-}

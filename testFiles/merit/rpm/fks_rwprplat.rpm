:!UTF-8
{TITLE:L. Zestawienie rozrachunków - praktyki płatnicze}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   {? var_press('WIELKOSC',XINFO)<=0
   || FUN.info('Brak naniesionych zmian w definicji systemu\nzwiązanych z aktualizacją zatorów płatniczych.'@);
      return()
   ?};
   VAR_DEL.delete('repexp','tb_tmp'); repexp:=rep_export();
   PRN1:=obj_new(@.CLASS.PRINT,14); PRN1.sl_frame(); w:=obj_new(14);
   s_sign:=color:=''; prn1:=prn2:='PRN1'; lm:=ll:=lmt:=rsep:=0; vp:=1;
   ext_spr:=drukuj:=rekordy:=0;
   tuser:=tm_form(TMPVAT.tm_stamp);
   {! _i:=1..obj_len(w) |! w[_i]:='' !};
   ~~
}
{END:
   VAR_DEL.delete('PRN1','w','DRB','repexp');
   VAR_DEL.delete('s_sign','color','prn1','prn2','vp','lm','ll','lmt','rsep','tb_tmp','ext_spr','tuser');
   VAR_DEL.delete('drukuj','rekordy');
   ~~
}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_rwprplat.rpm
Utworzony:   18.05.2029 [JK]
==================================================
Zawartosc: Zestawienie rozrachunków - praktyki płatnicze
{COMMENT-}
{  {? var_press('WIELKOSC',XINFO)>0
   || KONTO.K1:=KONTO.K2:=KONTO.K3:=KONTO.K4:='';
      KONTO.K1_AE:=KONTO.K2_AE:=KONTO.K3_AE:=KONTO.K4_AE:='';
      KONTO.K1_RODZ:=2; KONTO.K2_RODZ:=KONTO.K3_RODZ:=KONTO.K4_RODZ:=3;
      KONTO.K1_SYNT:=KONTO.K2_SYNT:=KONTO.K3_SYNT:=KONTO.K4_SYNT:=KONTO.K1_WYM:=KONTO.K2_WYM:=KONTO.K3_WYM:=KONTO.K4_WYM:=0;
      KONTO.K1_BE:='UNPAR.P1=1';
      KONTO.K2_BE:='UNPAR.P1=2';
      KONTO.K3_BE:=KONTO.K4_BE:='UNPAR.P1=3';
      UNPAR.P1:=UNPAR.P9:=1;
      UNPAR.P3:=2;
      UNPAR.P1_BV:=UNPAR.P1_BE:=UNPAR.P3_BE:=UNPAR.P9_BE:='';
      UNPAR.P1_AE:='exec(\'p1_ae\',\'!fks_roz_zazr\')';
      UNPAR.P3_AE:='';
      PAR_WYDR.win_edit('RWPRPLAT');
      UNPAR.P4_AE:='{? chk_fld() || 1 || 0 ?}';
      UNPAR.P4_BV:=UNPAR.P4_BE:='';
      UNPAR.P4:=date(date~1,date~2,0);
      PAR_WYDR.SPAR1:='N';
      PAR_WYDR.PAR1:=0;
      PAR_WYDR.PAR2:=1;
      UNPAR.P9_AE:='{? UNPAR.P9=2 || UNPAR.P7:=1 ?}; 1';
      exec('p1_enable','!fks_roz_zazr')
   ?};
   ~~
}
{EXIT: ~PAR_WYDR.edit("{? UNPAR.P1=6 & ROZNE.KON_WYDR=null() || FUN.info('Nie wybrano zestawu kont.'@); 'KON_WYDR' || '' ?}")}
{INCLUDE: wsp_pw.rpi}
{
   _where:='';
   {? UNPAR.P1=1 & KONTO.K1<>'' & KONTO.K1<>35*'?'
   || _konto:=KONTO.K1;
      {? _konto+1='?'
      || {! |? _konto:=_konto-1; _konto+1='?' !};
         {? +_konto<35 || _konto+='%' ?}
      ?};
      _konto:=STR.gsub(_konto,'?','_');
      _konto:=STR.gsub(_konto,'*','%');
      _where:='and OP.AN like \''+_konto+'\''
   |? UNPAR.P1=2 & KONTO.K2<>''
   || _where:='and OP.AN like \''+KONTO.K2+'%\''
   |? UNPAR.P1=3
   || _k1:=KONTO.K3; _k1:=STR.gsub(_k1,'?','_'); _k1:=STR.gsub(_k1,'*','%');
      _k2:=KONTO.K4; _k2:=STR.gsub(_k2,'?','_'); _k2:=STR.gsub(_k2,'*','%');
      _where:='and (OP.AN between \''+_k1+'\' and \''+_k2+%255+'\') '
   |? UNPAR.P1=4
   || _okn:=KS.win_sel('?');
      TMPSIK.cntx_psh();
      exec('initmpks','dok_fks_zest');
      KS.select();
      KS.win_sel(_okn);
      {? TMPSIK.first()
      || _where+='and (OP.AN like \'' + TMPSIK.T2 + '%\' ';
         {!|? TMPSIK.next()
         |! _where+='or OP.AN like \'' + TMPSIK.T2 + '%\' '
         !};
         _where+=') '
      || _where+='and (OP.AN like \'%\')';ext_spr:=1
      ?};
      TMPSIK.cntx_pop()
   |? UNPAR.P1=5
   || _okn:=AN.win_sel('?');
      TMPSIK.cntx_psh();
      exec('initmpan','dok_fks_zest');
      AN.select();
      AN.win_sel(_okn);
      {? TMPSIK.first()
      || _where+='and (OP.AN like \'' + TMPSIK.T2 + '%\' ';
         {!|? TMPSIK.next()
         |! _where+='or OP.AN like \'' + TMPSIK.T2 + '%\' '
         !};
         _where+=') '
      || _where+='and (OP.AN like \'%\')';ext_spr:=1
      ?};
      TMPSIK.cntx_pop()
   |? UNPAR.P1=6
   || KON_SCH.cntx_psh();
      KON_SCH.index('IND_LP');
      KON_SCH.prefix(ROZNE.KON_WYDR);
      {? KON_SCH.first()
      || {? KON_SCH.MZ = 'M'
         || {! |?
               {? KON_SCH.MASKA+1='?' || KON_SCH.MASKA:=KON_SCH.MASKA-1 ?};
               KON_SCH.MASKA+1='?'
            !};
            KON_SCH.MASKA:=KON_SCH.MASKA+'%';
            KON_SCH.MASKA:=STR.gsub( KON_SCH.MASKA,'?','_');
            _where+='and (OP.AN like \''+KON_SCH.MASKA+'\''
         || _where+='and (OP.AN between \''+KON_SCH.ZOD+'\' and \''+KON_SCH.ZDO+%255+'\''
         ?};
         {!|? KON_SCH.next()
         |! {? KON_SCH.MZ = 'M'
            || {! |?
                  {? KON_SCH.MASKA+1='?' || KON_SCH.MASKA:=KON_SCH.MASKA-1 ?};
                  KON_SCH.MASKA+1='?'
               !};
               KON_SCH.MASKA:=KON_SCH.MASKA+'%';
               KON_SCH.MASKA:=STR.gsub( KON_SCH.MASKA,'?','_');
               _where+=' or OP.AN like \''+KON_SCH.MASKA+'\''
            || _where+=' or OP.AN between \''+KON_SCH.ZOD+'\' and \''+KON_SCH.ZDO+%255+'\''
            ?}
         !};
         _where+=') '
      || _where+='and (OP.AN like \'%\')'
      ?};
      KON_SCH.cntx_pop()
   ?};
   tb_tmp:=sql(
      'select '+{? UNPAR.P9=1 || 'OP.AN' || 'OP.KH' ?}+', OP.REFERENCE as REF '+
      'from OP '+
      'where OP.WAL=:_a '+
      {? OPERATOR.DEPT || 'and OP.ODD=:_b ' || '' ?}+_where+
      'order by 1'
      ,SSTALE.WAL,OPERATOR.DEPT
   );
   {? ~OPERATOR.DEPT || PRN1.add("w[13]",8,'Jed. ks.') ?};
   {? UNPAR.P9=1
   || PRN1.add("w[3]",20,'Konto'); PRN1.add("w[1]",10,'Kontrahent'); PRN1.add("w[2]",20,'Rodzaj kontrahenta')
   || PRN1.add("w[1]",10,'Kontrahent'); PRN1.add("w[2]",20,'Rodzaj kontrahenta'); PRN1.add("w[3]",20,'Konto')
   ?};
   PRN1.add("w[4]",51,'Symbol');
   PRN1.add("w[5]",10,'Termin płatności');
   PRN1.add("w[6]",10,'Data pierwszego zapisu');
   PRN1.add("w[7]",10,'Wymagana liczba dni',1);
   PRN1.add("w[8]",10,'Obecna liczba dni',1);
   PRN1.add("w[9]",10,'Liczba dni przekroczenia',1);
   PRN1.add("w[10]",16,'Obroty Wn stan na dzień',1);
   PRN1.add("w[11]",16,'Obroty Ma stan na dzień',1);
   PRN1.add("w[12]",16,'Wartość do zapłaty',1);
   PAR_WYDR.TITLE:='Zestawienie rozrachunków - praktyki płatnicze';
   ~~
}
{IF: ~repexp}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{HEADER}
{ prn1:='PRN1'; rsep:=1;~~}
{SUBSTITUTE: 'HEAD'}{SPACE: 'C'}

{IF: page()=1}
{FONT: 'T8G'}{SPACE: 'C'}
{<1 'PARAMETRY WYDRUKU:'}
{<3 'Stan na dzień '+$UNPAR.P4}
{<3 'Zakres kont '+{? UNPAR.P1=1 || ', maska: '+KONTO.K1
                   |? UNPAR.P1=2 || ', prefix: '+KONTO.K2
                   |? UNPAR.P1=3 || ', zakres od: '+KONTO.K3+' do: '+KONTO.K4
                   |? UNPAR.P1=4 || 'dla wybranych kont syntetycznych'
                   |? UNPAR.P1=5 || 'dla wybranych kont analitycznych'
                   |? UNPAR.P1=6 || 'dla zestawu kont: %1'@[ROZNE.KON_WYDR().KOD]
                   ?}
}
{<3 'Rozrachunki '+{? UNPAR.P3=1 || 'rozliczone'
                   |? UNPAR.P3=2 || 'nierozliczone'
                   || 'wszystkie' ?}
}
{<3 {? OPERATOR.DEPT || 'Jednostka księgowa '+OPERATOR.DEPT().OD || 'Wszystkie jednostki księgowe' ?} }
{<3 {? PAR_WYDR.SPAR1='N'
    || 'Należności'
    |? PAR_WYDR.SPAR1='Z'
    || 'Zobowiązania'
    || 'Należności i zobowiązania'
    ?}
}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1; ~~}
{HEADER-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{FOOTER: 0}{FONT: 'T8G'}{SPACE: 'C'}{prn1:=prn2;~~}
{SUBSTITUTE: 'LINIAF'}{prn1:='PRN1';~~}
{FOOTER-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{MACRO: 'STRONA'}
{IF: lineleft()<2 & lineleft()<>0}{PAGE}{prn1:='PRN1';~~}
{ELSE}
{IF: lineleft()=0}{prn1:='PRN1';~~}{ELSE}{prn1:=prn2;~~}{FI}
{FI}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{MACRO: 'OP'}
{ ZAP_OP.cntx_psh();
  ZAP_OP.index('OP');
  OP.prefix();
  {? OP.seek(BIT.sqlint(tb_tmp.REF),)
  || ZAP_OP.prefix(OP.ref);
     {? ZAP_OP.first() & UNPAR.P4>=ZAP_OP.DATA || drukuj:=1 || drukuj:=0 ?}
  || drukuj:=0
  ?};
  {? drukuj
  || F.RObr(OP.ref(),UNPAR.P4);
     drukuj:=UNPAR.P3>2 | (UNPAR.P3=1 & F.rwn=F.rma) | (UNPAR.P3=2 & F.rwn<>F.rma)
  ?};
  {? drukuj
  || drukuj:={? PAR_WYDR.SPAR1='N'
             || 1+OP.TYP='N'
             |? PAR_WYDR.SPAR1='Z'
             || 1+OP.TYP='Z'
             || 1
             ?}
  ?};
  {? drukuj
  || _wymagana:={? 1+OP.TYP='N'
                || {? -OP.KH().WIELKOSC='publiczny' || XINFO.L_D_PUB || XINFO.L_D_POZ ?}
                || {? -XINFO.WIELKOSC='publiczny' || XINFO.L_D_PUB || XINFO.L_D_POZ ?}
                ?};
     _obecna:=(date()-ZAP_OP.DATA);
     _przekr:={? _obecna>_wymagana || _obecna-_wymagana || 0 ?};
    {? PAR_WYDR.PAR1 & _przekr=0 || drukuj:=0 ?}
  ?};
  {? drukuj
  || rekordy+=1;
     {? ~OPERATOR.DEPT || w[13]:=OP.ODD().OD ?};
     w[1]:={? PAR_WYDR.PAR2=1 || OP.KH().SKR || OP.KH().KOD ?};
     w[2]:=OP.KH().WIELKOSC;
     w[3]:=OP.AN;
     w[4]:=OP.SYM;
     w[5]:=$OP.TZ;
     w[6]:=$ZAP_OP.DATA;
     w[7]:={? ~repexp || form(_wymagana,,0,' ,') || _wymagana ?};
     w[8]:={? ~repexp || form(_obecna,,0,' ,') || _obecna ?};
     w[9]:={? ~repexp || form(_przekr,,0,' ,') || _przekr ?};
     w[10]:=form(F.rwn,,2,' ,');
     w[11]:=form(F.rma,,2,' ,');
     w[12]:=form({? 1+OP.TYP='N' || F.SWn(F.rwn,F.rma) || F.SMa(F.rwn,F.rma) ?},,2,' ,')
   ?};
   ZAP_OP.cntx_pop();
   ~~
}
{IF: drukuj}
{prn2:='PRN1';~~}
{SUBSTITUTE: 'LINIA02'}{SUBSTITUTE: 'STRONA'}
{FI}
{MACRO-}
{COMMENT}--------------------------------------------------------------------------{COMMENT-}
{IF: ext_spr=0}
   {IF: UNPAR.P9=1 }
         {tb_tmp.prefix(); drukuj:=tb_tmp.first();~~}
            {IF: drukuj}
            {drukuj:=tb_tmp.first(); ~~}
            {WHILE: drukuj}
            {DO}
               {SUBSTITUTE: 'OP'}{drukuj:=tb_tmp.next();~~}
            {OD}
         {FI}
   {ELSE}
      {FOR: 'KH'}
      {INDEX:'SKR'}
      {PREFIX: 2}
      {DO}
         {tb_tmp.prefix($KH.ref,); drukuj:=tb_tmp.first();~~}
         {IF: drukuj}
            {drukuj:=tb_tmp.first(); ~~}
            {WHILE: drukuj}
            {DO}
               {SUBSTITUTE: 'OP'}{drukuj:=tb_tmp.next();~~}
            {OD}
         {FI}
      {OD}
   {FI}
{FI}
{IF: rekordy=0 | ext_spr}{FUN.info('Brak danych spełniających\nzałożone kryteria.'@);~~}{EXIT}{FI}

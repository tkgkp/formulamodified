:!UTF-8
{TITLE:A. Zestawienie dla tabeli tymczasowej}
{PRINTER: FINFO.W_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,20); PRN.sl_frame(); w:=obj_new(20);
   v_lkmax:=($(PAR_WYDR.TTMPAKR+'.fld_num()'))();
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   g1:={? var_pres('g_tt_w')<=0
       || g_tt_w:=obj_new(v_lkmax); {! _i:=1..v_lkmax |! g_tt_w[_i]:=0 !}; 1
       ?};
   g2:={? var_pres('g_tt_p')<=0
       || g_tt_p:=obj_new(v_lkmax); {! _i:=1..v_lkmax |! g_tt_p[_i]:=0 !}; 1
       ?};
   g3:={? var_pres('g_tt_e')<=0
       || g_tt_e:=obj_new(v_lkmax); {! _i:=1..v_lkmax |! g_tt_e[_i]:=1 !}; 1
       ?}; ~~
}
{END: VAR_DEL.delete('PRN','w');
   {? g1 || obj_del(g_tt_w) ?};
   {? g2 || obj_del(g_tt_p) ?};
   {? g3 || obj_del(g_tt_e) ?};
   &v_lkmax; &i; &g1; &g2; &g3;
   &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep
}
{COMMENT}
i - licznik
v_lkmax - maksymalna liczba kolumn
color, prn1, lm, ll, vp, lmt, rsep - zmienne pomocnicze do wydrukow graficzn.
g_tt_w - zmienna przechowujaca szerokosc kolumn
g_tt_p - zmienna przechowujaca precyzje kolumn
g_tt_e  - zmienna okreslajaca czy kolumna ma byc drukowana
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{  prn1:='PRN'; ~~}
{  {! i:=1..v_lkmax |!
      {? g_tt_e[i]
      || {? g_tt_w[i]
         || _v:=g_tt_w[i]
         || _v:=exec('fld_len','#sql',PAR_WYDR.TTMPAKR,i)
         ?};
         _v_fld:=($(PAR_WYDR.TTMPAKR+'.'+($(PAR_WYDR.TTMPAKR+'.fld_acr('+$i+')'))()))();
         {? type_of(_v_fld)=1
         || PRN.add($('form(w['+$i+'],,'+$g_tt_p[i]+',\' ,\')'),_v,($(PAR_WYDR.TTMPAKR+'.fld_acr('+$i+')'))(),1)
         |? type_of(_v_fld)=4
         || PRN.add($('$w['+$i+']'),_v,($(PAR_WYDR.TTMPAKR+'.fld_acr('+$i+')'))(),1)
         || PRN.add($('w['+$i+']'),_v,($(PAR_WYDR.TTMPAKR+'.fld_acr('+$i+')'))())
         ?}
      ?};
      i+=1
   !};
   {! i:=1..v_lkmax |! w[i]:='' !}; ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{FOR: ($(PAR_WYDR.TTMPAKR))()}
{DO}
{  {! i:=1..v_lkmax |!
      ($('w['+$i+']:='+PAR_WYDR.TTMPAKR+'.'+($(PAR_WYDR.TTMPAKR+'.fld_acr(i)'))()))()
   !}; ~~}
{SUBSTITUTE: 'LINIA0'}
{OD}

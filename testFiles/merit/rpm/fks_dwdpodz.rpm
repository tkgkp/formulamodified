:!UTF-8
{TITLE:C. Dokument źródłowy z podziałami controllingowymi}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
   PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,13); PRN.sl_frame(); w:=obj_new(14);
   color:=prn1:=prn2:='PRN'; lm:=ll:=vp:=lmt:=lporz:=0; rsep:=PAR_WYDR.RSEP;
   head:=PAR_WYDR.HEAD; vp:=1; NOMAXP:=0
}
{END:
   VAR_DEL.delete('PRN','w','NOMAXP');
   &color; &prn1; &prn2; &lporz; &head;
   &lm; &ll; &vp; &lmt; &rsep; exec('end_msg','dok_fks_zest')
}
{IF: var_pres('TT_D_DOK')<100}
   {SKIDXD.index('DOK'); SKIDXD.prefix(DOK.ref());
   {? ~SKIDXD.first()
    || FUN.info('Brak podziałów controllingowych w dokumencie.'@)
   ?};
 ~~}
   {EXIT: ~SKIDXD.first()}
{FI}
{MACRO: 'INIT'}
{  SKIDXD.cntx_psh(); _ost:=SKIDXD.SKID_MB;
   {!
   |? _ok:=_ost=SKIDXD.SKID_MB;
      _ok & SKIDXD.next()
   !};
   SKIDXD.cntx_pop();
   PRN.add("w[1]",5,'Lp.'@,1);
   PRN.add("SKIDXD.SKID_MB().KOD",16,'Model'@);
   {? _ok
   || SKID_MBP.index('LP'); SKID_MBP.prefix(SKIDXD.SKID_MB);
      {? SKID_MBP.first()
      || {!
         |? PRN.add(($('w['+$(SKID_MBP.LP+1)+']')),16,SKID_MBP.NAZ);
            SKID_MBP.next()
         !}
      ?}
   || PRN.add("w[2]",16,'Pozycja budżetowa'@);
      PRN.add("w[3]",16,'Wymiar 2'@);
      PRN.add("w[4]",16,'Wymiar 3'@);
      PRN.add("w[5]",16,'Wymiar 4'@);
      PRN.add("w[6]",16,'Wymiar 5'@)
   ?};
   PRN.add("w[7]",4,'Wal.'@);
   PRN.add("w[8]",16,'Wartość w walucie'@,1);
   PRN.add("w[9]",10,'Kurs'@,1);
   PRN.add("w[10]",16,'Wartość'@,1);
   PRN.add("w[11]",5,'Jedn.'@);
   PRN.add("w[12]",35,'Konto'@);
   ~~}
{MACRO-}
{MACRO: 'START'}
{  obj_del(PRN);
   PRN:=obj_new(@.CLASS.PRINT,13); PRN.sl_frame();~~}
   {SUBSTITUTE: 'INIT'}
{MACRO-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}------------------------------MACRO WYDRUK-----------------{COMMENT-}
{MACRO: 'WYDRUK'}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='DOKUMENT ŹRÓDŁOWY '+DOK.NK;~~}
{LINE: 1}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: rep_export()}
{'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}{<40 'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}{IF: DOK.NRKSEF<>''}{<66 'Nr KSeF: '+DOK.NRKSEF}{FI}
{'Data operacji: %1'@[$DOK.DOP]}
{'Data zapisu: %1'@[$DOK.DTW]}
{'Data wystawienia: %1'@[$DOK.DTO]}
{IF: DOK.KSEFCDAT<>date(0,0,0)}{'Data KSeF: '+$DOK.KSEFCDAT}{FI}
{IF: DOK.D4<>date(0,0,0)}{'Data otrzymania: %1'@[$DOK.D4]}{FI}
{IF: DOK.D3<>date(0,0,0)}{'Termin płatności: %1'@[$DOK.D3]}{FI}
{'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}
{'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}
{'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}
{'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}
{FI}
{LINE: 1}
{ELSE}
{<2 'Okres obrachunkowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]}
{<2 'Rejestr: %1 - %2 - %3'@[DOK.REJ().KOD,DOK.REJ().ODD().OD,$DOK.NR]}{<40 'Rodzaj dokumentu: %1'@[DOK.DOK_REJ().SLO().TR]}
{<2 'Symbol zewnętrzny: %1'@[DOK.SYM_ZEW]}{IF: DOK.NRKSEF<>''}{<66 'Nr KSeF: '+DOK.NRKSEF}{FI}
{<2 'Data operacji: %1'@[$DOK.DOP]}{<34 'Data zapisu: %1'@[$DOK.DTW]}{<66 'Data wystawienia: %1'@[$DOK.DTO]}{IF: DOK.KSEFCDAT<>date(0,0,0)}{<106 'Data KSeF: '+$DOK.KSEFCDAT}{FI}{IF: DOK.D4<>date(0,0,0)}{<130 'Data otrzymania: %1'@[$DOK.D4]}{FI}{IF: DOK.D3<>date(0,0,0)}{<162 'Termin płatności: %1'@[$DOK.D3]}{FI}
{<2 'Opis dokumentu: %1'@[DOK.TR]}
{IF: DOK.DOK_REJ().SLO().KOD<>'PROSTY' & ((+DOK.WYS().KOD>0) | (+DOK.WYS().TR>0) | (+DOK.NIP>0))}{<2 'Wystawca: %1 - %2   NIP: %3'@[DOK.WYS().KOD,DOK.WYS().TR,DOK.NIP]}{FI}
{IF: (+DOK.RVAT().RVAT().SYM>0)|(+DOK.OKRVAT().NAZ>0)}{<2 'Rejestr VAT: %1'@[exec('list_rvat','dok_fks')]}{<66 'Okres VAT: %1 %2'@[DOK.OKRVAT().NAZ,DOK.OKRVAT().ROK().NAZ]}{<98 'Kraj opodatkowania VAT: %1 (Waluta: %2)'@[DOK.KRAJ().TR,DOK.JORG().KOD]}{FI}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; prn1:=prn2:='PRN';~~}
{FOR: 'SKIDXD'}
{INDEX: 'DOKDSP'}
{PREFIX: DOK.ref()}
{DO}
  { lporz+=1;
    w[1]:=lporz;
    w[2]:=SKIDXD.PBUD().SYMBOL;
    w[3]:=SKIDXD.JORG().SYMBOL;
    w[4]:=SKIDXD.OKOSZ().SYMBOL;
    w[5]:=SKIDXD.WYM4().SYMBOL;
    w[6]:=SKIDXD.WYM5().SYMBOL;
    w[7]:={? SKIDXD.WAL<>null || SKIDXD.WAL().KOD || '' ?};
    w[8]:=form({? SKIDXD.WAL<>FINFO.NAROD || SKIDXD.WARTW || 0 ?},,2,' ,');
    w[9]:=form({? SKIDXD.WAL<>FINFO.NAROD || SKIDXD.KURS || 0 ?},,6,' ,');
    w[10]:=form(SKIDXD.WART,,{? SKIDXD.JM<>null || 6 || 2 ?},' ,');
    w[11]:=SKIDXD.JM().KOD;
    w[12]:=SKIDXD.AN; ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{MACRO-}
{COMMENT}--------------------------END MACRO WYDRUK-----------------{COMMENT-}
{IF: var_pres('TT_D_DOK')>100}
   {NOMAXP:=1;~~}
   {FOR: 'TT_D_DOK'}
   {DO}
      {IF: DOK.seek(TT_D_DOK.REF,DOK.name()) & (SKIDXD.index('DOK'); SKIDXD.prefix(DOK.ref()); SKIDXD.first())}
         {SUBSTITUTE: 'START'}
         {SUBSTITUTE: 'WYDRUK'}{l_druk+=1;~~}
         {IF: TT_D_DOK.next()}{HEADER}{HEADER-}{PAGE}{page(1);~~}{FI}
      {FI}
   {OD}
   {czy_druk:=1;~~}
{ELSE}
   {SUBSTITUTE: 'INIT'}
   {SUBSTITUTE: 'WYDRUK'}
{FI}

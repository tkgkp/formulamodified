:!UTF-8
{TITLE:Zestawienie wniosków urlopowych}
{PRINTER: dziedzina:='PKD';
          exec('prt','param_wydr','h',KST),,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PAR_WYDR.TITLE:='Zestawienie wniosków urlopowych'@;
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','exit','Nr','__param','__NWU','__range');
   _perm:=exec('uprawnieniawrap','pkd','wnioskach urlopowych'@,'PKD_EZK_ORWN');
   exit:=0;
   {? +_perm
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_perm]);
      exit:=1
   || tresc:='nwu';
      lmt:=lp:=0;
      color:=prn1:='';
      __param:=exec('pkd_nwu','personel',PAR_WYDR.TITLE);
      {? __param.size()
      || _all:=
            {? ~__param.ZKR | (__param.WTW & __param.DWR & __param.PRZ & __param.WPR & __param.ODR & __param.DWC &
               __param.WCF & __param.OPK)
            || 1 || 0
            ?};
         __range:='';

         {? ~exit
         || _where:='';
            {? ~_all
            || _where+='';
               {? __param.WTW || _where+='NWU.AZ=\'\' or ';
                                 __range+='%1, '[exec('status','wnioski_urlopowe','')] ?};
               {? __param.DWR || _where+='NWU.AZ=\'?\' or ';
                                 __range+='%1, '[exec('status','wnioski_urlopowe','?')] ?};
               {? __param.PRZ || _where+='(NWU.AZ=\'T\' and NWU.N is null and NWU.OS_N is null) or ';
                                 __range+='%1, '['Przyjęty'@] ?};
               {? __param.WPR || _where+='(NWU.AZ=\'T\' and (NWU.N is not null or NWU.OS_N is not null)) or ';
                                 __range+='%1, '['Wprowadzony'@] ?};
               {? __param.ODR || _where+='NWU.AZ=\'N\' or ';
                                 __range+='%1, '[exec('status','wnioski_urlopowe','N')] ?};
               {? __param.DWC || _where+='NWU.AZ=\'D\' or ';
                                 __range+='%1, '[exec('status','wnioski_urlopowe','D')] ?};
               {? __param.WCF || _where+='NWU.AZ=\'W\' or ';
                                 __range+='%1, '[exec('status','wnioski_urlopowe','W')] ?};
               {? __param.OPK || _where+='NWU.AZ=\'O\' or ';
                                 __range+='%1, '[exec('status','wnioski_urlopowe','O')] ?};
               {? +_where=4   || _where+='and NWU.AZ=\'NOTHING\' ' || _where:='and (%1) '[_where-3] ?};
               {? +__range    || __range:=__range-2 ?}
            || __range+='Wszystkie'@
            ?};
            _query:=
               'select distinct '+
                  'OSOBA.PIERWSZE as PIERWSZE, '
                  'OSOBA.NAZWISKO as NAZWISKO, '
                  'OSOBA.PESEL as PESEL, '
                  'NWU.REFERENCE as REF, '
                  'NWU.P as P, '
                  'NWU.OD as OD, '
                  'NWU.DO as DO, '
                  'NWU.AZ as STATUS, '
                  'NWU.KTO_WER as KTO_WER, '
                  'NWU.AO as AO, '
                  'R.RT as RODZAJ '
               'from NWU '
                  'join P using(NWU.P, P.REFERENCE) '
                  'join OSOBA using(P.OSOBA, OSOBA.REFERENCE) '
                  'join R using (NWU.R, R.REFERENCE) '
               'where P.REFERENCE in (select :_a.SQL from :_a) '
                  'and NWU.OD<=to_date(:_c) and NWU.DO>=to_date(:_b) '+
                  _where+
               'order by NAZWISKO,PIERWSZE,PESEL,OD';
            __NWU:=sql(_query,exec('wybierz_parses','pracownik','PKD').P,
               __param.DOD,__param.DDO);

            'Sprawdzamy, czy są pracownicy spełniający kryteria:';
            {? __NWU.size()
            || {? rep_export()
               || PRN:=obj_new(@.CLASS.PRINT,10)
               || PRN:=obj_new(@.CLASS.PRINT,11);
                  PRN.add("Nr+=1",3,'Lp.'@,1,,,,,',,\'9.\'')
               ?};
               PRN.add("__NWU.NAZWISKO",MS.fld_len(OSOBA,'NAZWISKO'),'Nazwisko'@);
               PRN.add("__NWU.PIERWSZE",MS.fld_len(OSOBA,'PIERWSZE'),'Imię'@);
               PRN.add("P.T",MS.fld_len(P,'T'),'Nr teczki'@);
               PRN.add("P.WYDZIAL().SYMBOL",10,'Jednostka org.'@);
               PRN.add("P.ST().ST",15,'Stanowisko'@);
               PRN.add("__NWU.RODZAJ",22,'Rodzaj urlopu'@);
               PRN.add("$__NWU.OD",11,'Data rozpoczęcia'@);
               PRN.add("$__NWU.DO",11,'Data zakończenia'@);
               PRN.add("
                  {? (__NWU.KTO_WER<>'' & OSOBA.seek(__NWU.KTO_WER)) |
                     (__NWU.AO<>'' & OSOBA.seek(__NWU.AO))
                  || OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE
                  || ''
                  ?}",26,'Osoba weryfikująca'@);
               PRN.add("exec('status','wnioski_urlopowe',__NWU.STATUS)",12,'Status'@);
               PRN.s_frame();

               prn1:='PRN';
               lmt:=Nr:=0;
               rsep:=vp:=1
            || exit:=1;
               FUN.info('Brak pracowników spełniających kryteria.'@)
            ?}
         ?}
      || exit:=1
      ?}
   ?};
   NWU.cntx_psh();
   NWU.prefix();
   OSOBA.cntx_psh();
   OSOBA.prefix();
   P.cntx_psh();
   P.prefix();
   UD_SKL.cntx_psh();
   STN.cntx_psh();
   ~~
}
{END:
   VAR_DEL.delete('PRN','prn1','vp','lmt','rsep','exit','Nr','__param','__NWU','__range');
   STN.cntx_pop();
   UD_SKL.cntx_pop();
   P.cntx_pop();
   OSOBA.cntx_pop();
   NWU.cntx_pop()
}
{EXIT: exit}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{COMMENT} Parametry sprawozdania: {COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{<{ceil(lmt)+3}'Parametry sprawozdania'@}
{<{ceil(lmt)+3}'Status wniosków: %1'@[__range]}
{<{ceil(lmt)+3}'Zakres dat: od %1 do %2'@[$__param.DOD,$__param.DDO]}
{FONT: 'T8B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
═══════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~ }
{FOR: __NWU}
{DO}
{IF: NWU.seek(__NWU.REF)}
   {NWU.P(); ~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{OD}
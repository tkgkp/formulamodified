:!UTF-8
{TITLE: Wydruk etykiet palet - kody kreskowe}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   lmg_palkodk:="VAR_DEL.delete('temp','font','wybierz','dalej','wyjscie','etykieta')";
   lmg_palkodk();
   temp:='';
   font:='';
   wybierz:='';
   dalej:=0;
   etykieta:=PAR_WYDR.URZ_LAB;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=exec('barcode_height','kody_kresk');
   PAR_WYDR.URZ_LAB:=exec('wyb_etykiety','kody_kresk');
   wyjscie:={? PAR_WYDR.TYPDR<>null || PAR_WYDR.URZ_LAB=null ?};
   {? wyjscie=0 & PAR_WYDR.TYPDR=null & PAR_WYDR.KODKRESK=''
   || FUN.info('Brak parametryzacji drukowania kodów kreskowych.'@);
      wyjscie:=1
   ?};
   exec('ini_kom','#message','Wydruk etykiet palet - kody kreskowe'@);
   PAL.cntx_psh
}
{END:
   PAL.cntx_pop;
   exec('end_kom','#message');
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   PAR_WYDR.URZ_LAB:=etykieta;
   lmg_palkodk(); VAR_DEL.delete('lmg_palkodk')
}
{EXIT: wyjscie=1}
{DEFINEFONT: 'courier=TexGyreCursor,,,8'}
{FONT: 'courier'}
{MACRO:'stan'}
   {ENTIRE}
   {'Typ palety: '+PAL.TPAL().TYP}

   {IF: PAL.KODK<>''}
      {BARCODE: PAL.KODK;50;BARCODE_H;BARCODE_TYPE}


      {FONT: 'courier'}{SPACE: 'B'}
      {PAL.KODK}
   {FI}
  ─────────────────────────────────────
   {ENTIRE-}
{MACRO-}
{MACRO:'etykiety'}
{IF: PAL.KODK=''}
   { exec('add_kom','#message','Paleta: '+PAL.KODK,7,'Brak kodu kreskowego'@);~~}
{ELSE}
   { wyjscie:=0;~~}
   {IF: PAR_WYDR.TYPDR<>null}
      { wyjscie:=exec('druk','kody_kresk')=0;~~}
   {ELSE}
      {SUBSTITUTE: 'stan'}
   {FI}
{FI}
{MACRO-}
{EXIT:
   wybierz:=FUN.choice('Wydruk etykiet dla lokalizacji'@,,' Bieżącego'@, 'Wszystkich'@, 'W&ybranych'@);
   {? wybierz <> 0
   || {? wybierz=1
      || wybierz:='B'
      |? wybierz=2
      || wybierz:='W'
      |? wybierz=3
      || wybierz:='Y'
      |? wybierz=4
      || wybierz:='Z'
      ?};0
   || "# koniec #"; 1
   ?}
}
{ wyjscie:=1;~~}
{IF:wybierz='B'}
   {SUBSTITUTE: 'etykiety'}
{ELIF:wybierz='Y'}
   {
   _wer:=PAL.mk_sel('Palety'@,,0,'palddppppswsadf');
   PAL.win_fld(_wer,,'TPAL','TYP',,10,,,'Typ'@);
   PAL.win_fld(_wer,,'KODK',,,30,,,'Kod kreskowy'@);
   _fb:="
      VAR_DEL.delete('__Sel');
      __Sel:=PAL.sel_aget; PAL.sel_adel;
      {? __Sel.first=0
      || __Sel.REF:=#PAL.ref;
         __Sel.add
      ?};
      sel_exit
   ";
   PAL.win_act(_wer,,'Formuła','Akceptuj'@@,,'Akceptacja wybranych pozycji'@,_fb,,1,1,_fb);

   PAL.index('AKT'); PAL.prefix(ST.ODDZ,'N','T');
   PAL.win_sel(_wer);

   {? PAL.select=0
   || VAR_DEL.delete('__Sel');
      __Sel:=PAL.sel_aget; PAL.sel_adel
   ?};
   PAL.cntx_psh;
   PAL.prefix();
   ~~}
   {FOR: __Sel}
   {DO}
      {IF: PAL.seek(__Sel.REF,)}
         {SUBSTITUTE: 'etykiety'}
      {FI}
   {OD}
   {
   PAL.cntx_pop;
   VAR_DEL.delete('__Sel');
   ~~}
{ELIF:wybierz='W'}
   {FOR: 'PAL'}
   {INDEX: 'AKT'}
   {PREFIX: ST.ODDZ,'N','T'}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{FI}
{EXIT: {? wybierz='W' | wybierz='Z' || {? wyjscie=1 || FUN.info('Brak danych spełniających kryteria.'@); 1 ?} ?}}
{EXIT: wyjscie}
{EXIT: PAR_WYDR.TYPDR<>null}

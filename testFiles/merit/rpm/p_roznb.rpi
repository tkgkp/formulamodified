:!UTF-8
{BEGIN:
   VAR_DEL.delete('par','prn','prn1','prn2','wyr','kol','tabn','lmt','all','resp','ll','color','__sk_m');
   VAR_DEL.delete('__param','__okredit','__edit','so','vp','wyr','ed','tresc','__Ediupr');
   PAR_WYDR.TITLE:='Rozliczenie nieobecności'@;
   tresc:='roznb';
   lmt:=ll:=rsep:=0;
   vp:=1;
   color:='';
   prn1:=prn2:='';
   wyr:=0;
   ed:=3;
   __Ediupr:=0;
   par:=obj_new('TYPN','CL','TYT','SKL','RUB','CN','WYSUW','MIN_WAL');
   kol:=obj_new(4);
   prn:=obj_new('C','CS2','CSL','CSP','U','US3','USW','NAG','CSKL','USKL');

   tabn:=tab_tmp(1,
      'M','DATE','Miesiąc',
      'NO','REAL','Norma',
      'PR','REAL','Praca',
      'NN','REAL','NN',
      'W','STRING[1]','Kwalifikacja',
      'KWE','REAL','Kwota (etat)',
      'KWZ','REAL','Kwota (zlecenia)'
   );
   __sk_m:=~~;

   OSOBA.cntx_psh();
   OSOBA.prefix();
   P.cntx_psh();
   P.index('PRACONAZ');
   P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P);
   UD_SKL.cntx_psh();
   UD_SKL.prefix();
   RU.cntx_psh();
   RU.prefix();
   H_UM.cntx_psh();
   H_UM.index('OD');
   R.cntx_psh();
   R.prefix();
   NW.cntx_psh();
   NW.index('CHW');
   NW.prefix();
   TZ.cntx_psh();
   SK_CHO.cntx_psh();
   LSW.cntx_psh();
   LSW.index('PN');
   par.MIN_WAL:=0;
   _upr:=exec('uprawnieniawrap','pkd','Rozliczenie nieobecności'@,
              'PKD_EZK_OPNN',
              'PKD_EZK_ORNN'
             )+
         exec('uprawnieniawrap','pkd','Naliczone składniki płacowe'@,
              'PPL_PLL_RSKP',
              'PPL_PLL_RSKP'
             );
   {? +_upr
   || __Ediupr:=1;
      FUN.emsg('Brak wymaganych uprawnień pozwalających na przeglądane informacji o: %1'@[_upr])
   || __param:=tab_tmp(1,
             'KOD','INTEGER','Wybór kodu',
             'SKL','INTEGER','Składniki podstaw',
             'L','REAL','Współczynnik rozjaśnienia'
      );
      __param.KOD:=__param.SKL:=0;
      __param.L:=75;
      __param.add();
      _opt:="__param.efld_opt(__edit,'enable=%1' [$__param.SKL],,'L')";
      __param.fld_fml('SKL','AFTER_EDIT',_opt);

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'pkd_wnieobecn');
      __param.win_esep(__edit,'Parametry wydruku '@
      +{? var_pres('typ')=type_of('') & typ='*'||'listy '+VAR.NAZWALIS||''?});
      __param.win_efld(__edit,,'SKL',,,,,,'Drukować?'@,,,'check-box','check_label="%1"' ['Składniki podstaw'@],"1","0");
      __param.win_efld(__edit,,'L',,,6,2,,,,'Kolor tła: 0 - bez zmian; 100 - bez kolorowania (rozjaśnione do białego)');

      {? var_pres('typ')=type_of('') & typ='*'
      || __param.win_efld(__edit,,'KOD',,,,,,'Nieobecności '@,,,'radio-buttons',,'Wszystkie'@,"0",
                                                                                 'Wybrane kody'@,"1")
      ?};
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      _opt();
      {? ed:=__param.edit(
            "  {? __param.SKL & (__param.L<0 | __param.L>100)
               || __CHK.err_fld(__param,'L',1,'Dozwolone wartości z przedziału %1-%2.'@ ['0','100'])
               || 1
               ?}
            "
         )
      || par.RUB:=
         {? __param.KOD
         || exec('code_grp','profile',,tresc,'N')
         || ''
         ?}
      ?};

      {? var_pres('typ')=type_of('') & typ='*'
      || params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn','prn1','prn2','wyr','kol','tabn','lmt','all','resp','ll','color');
   {? var_pres('typ')>=0
   || &typ
   ?};
   {? var_press('__sk_m')>100
   || obj_del(__sk_m)
   ?};
   VAR_DEL.delete('__param','__okredit','__edit','__sk_m','so','vp','wyr','ed','tresc','__Ediupr');

   OSOBA.cntx_pop();
   P.cntx_pop();
   UD_SKL.cntx_pop();
   RU.cntx_pop();
   H_UM.cntx_pop();
   R.cntx_pop();
   NW.cntx_pop();
   TZ.cntx_pop();
   SK_CHO.cntx_pop();
   LSW.cntx_pop();
   ~~
}
{COMMENT}OLD: roznb.rpi{COMMENT-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{EXIT: __Ediupr}
{EXIT: ~ed}
{EXIT:
      {? var_pres('typ')=type_of('') & typ='*'
      || ~params_get().P.first()
      ?}
}
{INCLUDE: wsp_pw.rpi}
{FONT: 'T8'}
{SPACE: 'B'}
{ par.CL:=charleft(); ~~}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{
   par.SKL:=exec('chk_role','#b__box',OPERATOR.USER,'PPL_PLL_RSKP') & __param.SKL;
   par.WYSUW:=0;

   'Nagłówek';
   prn.NAG:=obj_new(@.CLASS.PRINT,2);
   prn.NAG.add("kol[1]",25,'Rodzaj informacji');
   prn.NAG.add("kol[2]",80,'Informacja');
   prn.NAG.d_frame();

   'Choroba';
   prn.C:=obj_new(@.CLASS.PRINT,7);
   prn.C.add("7+tabn.M$2",7,'Miesiąc'@);
   prn.C.add("tabn.NO",12,'Powinien przepracować'@,1,,,,,',0,\' .\'');
   prn.C.add("tabn.PR",12,'Przepracował'@,1,,,,,',0,\' .\'');
   prn.C.add("tabn.NN",12,'Nieobecność nieuspraw.'@,1,,,,,',0,\' .\'');
   prn.C.add("tabn.KWE",14,'Wynagrodzenie'@,1,,,,,',2,\' .\'');
   prn.C.add("tabn.KWZ",14,'Zlecenia'@,1,,,,,',2,\' .\'');
   prn.C.add("
      {? tabn.W='T'
      || _dn:=tabn.NO-tabn.PR-tabn.NN;
         {? _dn || 'uzupełniony - '@+$_dn+' '+{? _dn=1 || 'dzień'@ ||  'dni'@ ?} || 'bez uzupełniania'@ ?}
      |? tabn.W='N' || 'pominięty'@
      |? tabn.W='P' || 'pobrany'@
      || ''
      ?}",30,'Opis'@);
   prn.C.s_frame();

   'Choroba, Suma, 2 kolumny (etat+zlecenia)';
   prn.CS2:=obj_new(@.CLASS.PRINT,3);
   _dl:=-3;
   {! _nrk:=1..4 |! _dl+=prn.C.LEN[_nrk]+3 !};
   prn.CS2.add("kol[1]",_dl);
   prn.CS2.add("kol[2]",prn.C.LEN[5],,1,,,,,',2,\' .\'');
   prn.CS2.add("kol[3]",prn.C.LEN[6],,1,,,,,',2,\' .\'');
   prn.CS2.s_frame();

   'Choroba, Suma, Lewa kolumna';
   prn.CSL:=obj_new(@.CLASS.PRINT,2);
   _dl:=-3;
   {! _nrk:=1..4 |! _dl+=prn.C.LEN[_nrk]+3 !};
   prn.CSL.add("kol[1]",_dl);
   prn.CSL.add("kol[2]",prn.C.LEN[5],,1,,,,,',2,\' .\'');
   prn.CSL.s_frame();

   'Choroba, Suma, Prawa kolumna';
   prn.CSP:=obj_new(@.CLASS.PRINT,2);
   _dl:=-3;
   {! _nrk:=1..4 |! _dl+=prn.C.LEN[_nrk]+3 !};
   prn.CSP.add("kol[1]",_dl);
   prn.CSP.add("kol[2]",prn.C.LEN[5]+3+prn.C.LEN[6],,1,,,,,',2,\' .\'');
   prn.CSP.s_frame();

   'Choroba, Skladniki';
   {? par.SKL
   || prn.CSKL:=obj_new(@.CLASS.PRINT,8);
      prn.CSKL.add("7+__sk_m.TAB.MC$2",7,'Miesiąc'@);
      prn.CSKL.add("
         {? __sk_m.TAB.SK_NR=0 | __sk_m.TAB.SK_NR=__sk_m.KOR || '' || __sk_m.TAB.SK_NR ?}",5,'Kod'@,1,,,,,',0,\'9.\'');
      prn.CSKL.add("__sk_m.TAB.SKLADNIK",20,'Nazwa'@);
      prn.CSKL.add("__sk_m.TAB.O",1,'R');
      prn.CSKL.add("{? __sk_m.TAB.KW=0 || '' || __sk_m.TAB.KW ?}",12,'Wypłata'@,1,,,,,',2,\' .\'');
      prn.CSKL.add("{? __sk_m.TAB.KW_N=0 || '' || __sk_m.TAB.KW_N ?}",12,'Do podstawy'@,1,,,,,',2,\' .\'');
      prn.CSKL.add("__sk_m.TAB.LT",8,'Lista'@);
      prn.CSKL.add("__sk_m.TAB.OPIS",46,'Opis'@);
      prn.CSKL.s_frame()
   ?};

   'Urlop';
   prn.U:=obj_new(@.CLASS.PRINT,5);
   prn.U.add("7+tabn.M$2",7,'Miesiąc'@);
   prn.U.add("tabn.NO",12,'Powinien przepracować'@,1,,,,,',2,\' .\'');
   prn.U.add("tabn.PR",12,'Przepracował'@,1,,,,,',2,\' .\'');
   prn.U.add("tabn.KWE",14,'Wynagrodzenie'@,1,,,,,',2,\' .\'');
   prn.U.add("''",30,'Opis'@);
   prn.U.s_frame();

   'Urlop, Suma, 3 kolumny';
   prn.US3:=obj_new(@.CLASS.PRINT,4);
   prn.US3.add("kol[1]",prn.U.LEN[1]);
   prn.US3.add("kol[2]",prn.U.LEN[2],,1,,,,,',2,\' .\'');
   prn.US3.add("kol[3]",prn.U.LEN[3],,1,,,,,',2,\' .\'');
   prn.US3.add("kol[4]",prn.U.LEN[4],,1,,,,,',2,\' .\'');
   prn.US3.s_frame();

   'Urlop, Suma, Wyliczenia';
   prn.USW:=obj_new(@.CLASS.PRINT,2);
   _dl:=-3;
   {! _nrk:=1..3 |! _dl+=prn.US3.LEN[_nrk]+3 !};
   prn.USW.add("kol[1]",_dl);
   prn.USW.add("kol[2]",prn.U.LEN[4],,1,,,,,',2,\' .\'');
   prn.USW.s_frame();

   'Urlop, Skladniki';
   {? par.SKL
   || prn.USKL:=obj_new(@.CLASS.PRINT,8);
      prn.USKL.add("7+__sk_m.TAB.MC$2",7,'Miesiąc'@);
      prn.USKL.add("
         {? __sk_m.TAB.SK_NR=0 | __sk_m.TAB.SK_NR=__sk_m.KOR || '' || __sk_m.TAB.SK_NR ?}",5,'Kod'@,1,,,,,',0,\'9.\'');
      prn.USKL.add("__sk_m.TAB.SKLADNIK",20,'Nazwa'@);
      prn.USKL.add("__sk_m.TAB.O",1,'R');
      prn.USKL.add("{? __sk_m.TAB.KW=0 || '' || __sk_m.TAB.KW ?}",12,'Do podstawy'@,1,,,,,',2,\' .\'');
      prn.USKL.add("__sk_m.TAB.LT",8,'Lista'@);
      prn.USKL.add("__sk_m.TAB.OPIS",46,'Opis'@);
      prn.USKL.s_frame()
   ?};

   ~~
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'nag1l'}
   {prn.NAG.ini_line();~~}
   {WHILE: prn.NAG.next()}
   {DO}
      {REP: prn.NAG.fline(lmt)}
   {OD}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'NAG'}
   {FONT: 'T8'}{SPACE: 'B'}
   {REP: prn.NAG.fhbar(lmt)}
   {SUBSTITUTE:
      kol[1]:='Nazwisko i imię:'@;
      kol[2]:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE;
      'nag1l'}
   {SUBSTITUTE: kol[1]:='PESEL / NIP:'@; kol[2]:=OSOBA.PESEL + ' / ' + OSOBA.NIP; 'nag1l'}
   {IF: par.CN}
      {SUBSTITUTE: kol[1]:='Data zatrudnienia:'@; kol[2]:=P.DZA$1; 'nag1l'}
      {SUBSTITUTE:
         kol[1]:='Jedn. org.:'@;
         kol[2]:=P.WYDZIAL().SYMBOL;
         {? kol[2]<>'' || kol[2]+=' - ' + UD_SKL.OPIS ?};
         'nag1l'}
      {SUBSTITUTE:
         kol[1]:='Zatrudnienie/umowa:'@;
         kol[2]:=P.ZA;
         H_UM.prefix(P.ref());
         {? H_UM.last()
         || _ru:=H_UM.RU().K;
            kol[2]+=' / ' +
               {? _ru='A'
               || 'Umowa na okres próbny do dnia '@+H_UM.DO$6
               |? _ru='B' & H_UM.OKR_UZAS().KOD='1'
               || 'Umowa na zastępstwo'@
               |? _ru='B'
               || 'Umowa na czas określony, do dnia '@+H_UM.DO$6
               |? _ru='C'
               || 'Umowa na czas nieokreślony'@+
                  {? H_UM.DO<>date(0,0,0) || ' zakończona w dniu '@+H_UM.DO$6 || '' ?}
               |? _ru='D'
               || 'Umowa na zastępstwo'@
               |? _ru='E'
               || 'Umowa na czas określony, do dnia porodu'@
               || ''
               ?};
            &_ru
         ?};
         'nag1l'}
      {SUBSTITUTE: kol[1]:='Wymiar zatrudnienia:'@; kol[2]:=$P.WY; 'nag1l'}
      { par.CN:=0; ~~}
   {FI}
   {REP: prn.NAG.fcbar(lmt)}
   {SUBSTITUTE: kol[1]:='Typ nieobecności:'@; kol[2]:=N.NB().RT; 'nag1l'}
   {SUBSTITUTE: kol[1]:='Okres nieobecności:'@; kol[2]:=N.OD$1+' - '+N.DO$1; 'nag1l'}
   {REP: prn.NAG.flbar(lmt)}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'linia0'}
   {IF:
      ($(prn1+'.ini_line()'))();
      _len:=($(prn1+'.length()'))();
      'Jezeli na stronie nie zmiesci sie kreska + linia + kreska + 3 puste linie + 2 linie na podpis';
      _ll:=lineleft();
      _ll<> 0 & _ll<1+_len+1+3+2
   }
      {PAGE}
      {REP: ($(prn1+'.fhbar(lmt)'))()}
   {ELSE}
      {REP: ($(prn1+'.fbar(lmt)'))()}
   {FI}
   {WHILE: ($(prn1+'.next()'))()}
   {DO}
      {<1}{REP: ($(prn1+'.fline(lmt)'))()}
   {OD}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'linia02'}
   {IF:
      ($(prn2+'.ini_line()'))();
      _len:=($(prn2+'.length()'))();
      'Jezeli na stronie nie zmiesci sie kreska + linia + kreska + 3 puste linie + 2 linie na podpis';
      _ll:=lineleft();
      _ll<>0 & _ll<1+_len+1+3+2
   }
      {PAGE}
      {REP: ($(prn2+'.fhbar(lmt)'))()}
   {ELSE}
      {REP: ($(prn1+'.fjbar('+prn2+',lmt)'))()}
   {FI}
   {WHILE: ($(prn2+'.next()'))()}
   {DO}
      {<1}{REP: ($(prn2+'.fline(lmt)'))()}
   {OD}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{MACRO: 'N'}
   { par.WYSUW:=0; par.MIN_WAL:=0; ~~}
   {IF: par.TYPN:=exec('n_rozliczalna','nieobecnosc',2); par.TYPN='C' | par.TYPN='U'}
      {COMMENT} Zbieranie danych  ----------------------------------------------------------------------------{COMMENT-}
      {
         tabn.erase();
         exec('sk_m_tworz','skladniki_okresowe',par.TYPN,0);

         N.cntx_psh();
         {? NW.find_key(N.ref())
         || TZ_EDIT.blank();
            TZ_EDIT.N:=N.ref();
            TZ_EDIT.R:=N.NB;
            N.cntx_psh();
            exec('n_aktualizuj_podstawy','nieobecnosc',1,1);
            N.cntx_pop();

            SK_CHO.use('s_ch'+$(4+$TZ_EDIT.N().OD));
            TZ.use('tabz'+$(4+$TZ_EDIT.N().OD));
            TZ.index('PNMR');
            TZ.prefix(TZ_EDIT.N);
            _dn:=TZ.first();
            {!
            |? _dn
            |! {? tabn.find_key(TZ.M)
               || _nowy:=0
               || _nowy:=1;
                  tabn.blank();
                  tabn.M:=TZ.M;
                  {? par.SKL || exec('sk_m_wypelnij','skladniki_okresowe',TZ.M,0) ?}
               ?};
               {? TZ.R='E'
               || tabn.NO:=TZ.NO;
                  tabn.PR:=TZ.PR;
                  tabn.NN:=TZ.NN;
                  tabn.W:=TZ.W;
                  tabn.KWE:=TZ.KW
               |? TZ.R='Z'
               || tabn.KWZ:=TZ.KW
               ?};
               {? _nowy || tabn.add() || tabn.put() ?};
               _dn:=TZ.next()
            !};
            &_dn
         ?};
         N.cntx_pop();

         par.CN:=1;

         prn1:='prn.'+par.TYPN;
         {? par.TYPN='C'
         || par.TYT:='Podstawa wymiaru'@
         |? par.TYPN='U'
         || par.TYT:='Schemat rozliczenia'@
         ?};

         _szer:=($(prn1+'.width()'))();
         prn.NAG.LEN[2]+=_szer-prn.NAG.width();
         lmt:=int((par.CL-_szer)/2);

         ~~
      }
      {COMMENT} Drukowanie  ----------------------------------------------------------------------------------{COMMENT-}
      {IF: tabn.first()}
         { par.WYSUW+=1; ~~}
         {HEADER}
            {SUBSTITUTE: 'HEAD'}
            {SUBSTITUTE: 'NAG'}
            {FONT: 'T8B'}{<1>{par.CL} par.TYT}{FONT: 'T8'}
            {SUBSTITUTE: 'TABHEAD'}
         {HEADER-}
         {FOOTER:0}
            {<1}{SUBSTITUTE: 'LINIAF'}
         {FOOTER-}
         {FOR: tabn}
         {DO}
            {SUBSTITUTE: rsep:=1; 'LINIA0'}
         {OD}
         {HEADER}
            {SUBSTITUTE: 'HEAD'}
            {SUBSTITUTE: 'NAG'}
            {FONT: 'T8B'}{<1>{par.CL} par.TYT}{FONT: 'T8'}{ vp:=0; ~~}
         {HEADER-}
         {IF: par.TYPN='C'}
            {IF: TZ_EDIT.LMCE>1}
               {SUBSTITUTE:
                  kol[1]:='Razem'@;
                  kol[2]:=TZ_EDIT.SET;
                  kol[3]:=TZ_EDIT.SUZ;
                  prn2:='prn.CS2';
                  'linia02'
               }
               { prn1:=prn2; ~~}
               {SUBSTITUTE:
                  kol[1]:='Przeciętne wynagrodzenie z '@+$TZ_EDIT.LMCE+' miesięcy'@;
                  kol[2]:=TZ_EDIT.PMWE;
                  kol[3]:=TZ_EDIT.PMWZ;
                  'linia0'
               }
            {FI}
            {IF: TZ_EDIT.SO<>0}
               {SUBSTITUTE:
                  kol[1]:='Składniki okresowe'@;
                  kol[2]:=TZ_EDIT.SO;
                  prn2:='prn.CSL';
                  'linia02'
               }
               { prn1:=prn2; ~~}
               {SUBSTITUTE:
                  kol[1]:='Razem miesięcznie'@;
                  kol[2]:=TZ_EDIT.RME;
                  'linia0'
               }
            {FI}
            {IF: TZ_EDIT.WAL='T'}
               {SUBSTITUTE:
                  kol[1]:='Waloryzacja ('+form(TZ_EDIT.WSP,,2,' .')+'%)';
                  kol[2]:=TZ_EDIT.WALKW;
                  kol[3]:=TZ_EDIT.WALKWZC;
                  par.MIN_WAL+=1;
                  prn2:='prn.CS2';
                  'linia02'
               }
               { prn1:=prn2; ~~}
            {FI}
            {IF: TZ_EDIT.KWMIN<>0}
               {SUBSTITUTE:
                  kol[1]:='Wyrównanie do minimalnej'@;
                  kol[2]:=TZ_EDIT.KWMIN;
                  prn2:='prn.CSL';
                  par.MIN_WAL+=1;
                  'linia02'
               }
               { prn1:=prn2; ~~}
            {FI}
            {IF: par.MIN_WAL>0}
               {SUBSTITUTE:
                  kol[1]:='Podstawa'@;
                  kol[2]:=TZ_EDIT.WALKW+TZ_EDIT.KWMIN;
                  kol[3]:={? TZ_EDIT.WAL='T' || TZ_EDIT.WALKWZC || TZ_EDIT.PMWZ ?};
                  prn2:='prn.CS2';
                  'linia02'
               }
               { prn1:=prn2; ~~}
            {FI}
            {SUBSTITUTE:
               kol[1]:='Razem podstawa (wynagrodzenie + zlecenia)'@;
               kol[2]:=TZ_EDIT.RDP;
               prn2:='prn.CSP';
               'linia02'
               }
            { prn1:=prn2; ~~}
            {SUBSTITUTE:
               wyr:=0;
               {? TZ_EDIT.N().RODZAJ='Z'
               || _proc:=TZ_EDIT.N().PROC
               || LSW.prefix(P.ref(),TZ_EDIT.N);
                  {? LSW.first()
                  || {!
                     |? wyr+=LSW.KW;
                        LSW.next()
                     !}
                  ?};
                  _proc:=30*TZ_EDIT.N().STD/TZ_EDIT.RDP$2*100
               ?};
               kol[1]:='Stawka dzienna ('@+form(_proc,,2,' .')+'%)';
               kol[2]:=TZ_EDIT.N().STD;
               'linia0'
            }
            {SUBSTITUTE:
               _nk:=TZ_EDIT.N().NK;
               kol[1]:='Kwota świadczenia (za '@+form(_nk,,0,' .')+' '+{? _nk=1 || 'dzień'@ || 'dni'@ ?}+')';
               kol[2]:=TZ_EDIT.N().WART;
               'linia0'
            }
            {IF: wyr<>0}
               {SUBSTITUTE:
                  kol[1]:='Kwota wyrównania świadczenia'@;
                  kol[2]:=wyr;
                  'linia0'
               }
            {FI}
         {ELIF: par.TYPN='U'}
            {IF: TZ_EDIT.LMCE>1}
               {SUBSTITUTE:
                  kol[1]:='Razem'@;
                  kol[2]:=TZ_EDIT.SNO;
                  kol[3]:=TZ_EDIT.SPR;
                  kol[4]:=TZ_EDIT.SET;
                  prn2:='prn.US3';
                  'LINIA02'
               }
               { prn1:=prn2; ~~}
            {FI}
            {SUBSTITUTE:
               kol[1]:='Stawka godzinowa'@;
               kol[2]:=TZ_EDIT.STG;
               prn2:='prn.USW';
               'linia02'
            }
            { prn1:=prn2; ~~}
            {SUBSTITUTE:
               _nk:=TZ_EDIT.N().NK;
               kol[1]:='Kwota wynagrodzenia (za '@+form(TZ_EDIT.N().NG,,2,' .')+' godz.)'@;
               kol[2]:=TZ_EDIT.N().WART;
               'linia0'
            }
         {FI}
         {SUBSTITUTE: 'LINIAF'}
         {FOOTER:0}{FOOTER-}
         {COMMENT} SKLADNIKI  --------------------------------------------------------------------------------{COMMENT-}
         {IF: par.SKL & sql('select * from :_a where SK_NR<>0',__sk_m.TAB).first() & __sk_m.TAB.first() }
            {
               prn1:='prn.'+par.TYPN+'SKL';
               _szer:=($(prn1+'.width()'))();
               prn.NAG.LEN[2]+=_szer-prn.NAG.width();
               lmt:=int((par.CL-_szer)/2);
               ~~
            }
            {IF:
               'Jezeli na stronie nie zmiesci sie caly miesiac';
               _dt:=__sk_m.TAB.MC;
               __sk_m.TAB.cntx_psh();
               __sk_m.TAB.prefix(_dt);
               _lr:=__sk_m.TAB.size()+5;
               __sk_m.TAB.cntx_pop();
               _ll:=lineleft();
               _ll<>0 & _ll<_lr*2
            }
               {PAGE}
            {ELSE}
               {FONT: 'T8B'}{<1>{par.CL} 'Składniki'}{FONT: 'T8'}
               {SUBSTITUTE: 'TABHEAD'}
            {FI}
            {HEADER}
               {SUBSTITUTE: 'HEAD'}
               {SUBSTITUTE: par.CN:=0; 'NAG'}
               {FONT: 'T8B'}{<1>{par.CL} 'Składniki'}{FONT: 'T8'}
               {SUBSTITUTE: 'TABHEAD'}
            {HEADER-}
            {FOOTER: 0}
               {SUBSTITUTE: 'LINIAF'}
            {FOOTER-}
            {FOR: __sk_m.TAB}
            {DO}
               {IF:
                  'Miesiac moze byc tak dlugi, ze nie moze zmiescic sie na stronie - sprawdzajmy kazdy wiersz';
                  ($(prn1+'.ini_line()'))();
                  _dl:=($(prn1+'.length()'))();
                  _ll:=lineleft();
                  _ll<>0 & _ll<_dl+1
               }
                  {PAGE}
               {FI}
               {REP:
                  _fc:=prn1+'.setcolor(_a)';
                  ($(prn1+'.ini_line()'))();
                  {? vp || ($(_fc))(color); vp:=0 ?};
                  ($(prn1+'.fbar(lmt)'))()
               }
               {
                  _fc:=prn1+'.setcolor(_a)';
                  _col:=Color.rekprzed('__sk_m#'+__sk_m.TYPN);
                  _poz:=_col*',';
                  _col:={? _poz>0 || (_poz-1)+_col || '255:255:255' ?};
                  _org:=spli_str(_col,':');
                  _hsl:=exec('rgb2hsl','#color',#_org[1],#_org[2],#_org[3]);
                  {? obj_ntab(_hsl)
                  || _hsl.l+=(100-_hsl.l)*__param.L/100;
                     _rgb:=exec('hsl2rgb','#color',_hsl.h,_hsl.s,_hsl.l);
                     {? obj_ntab(_rgb)
                     || _col:='%1:%2:%3' [$_rgb.r,$_rgb.g,$_rgb.b]
                     ?}
                  ?};
                  ($(_fc))(_col);
                  ~~
               }
               {WHILE: ($(prn1+'.next()'))()}
               {DO}
                  {REP: ($(prn1+'.fline(lmt)'))()}
               {OD}
               {TOTAL: __sk_m.TAB.MC}
               {IF:
                  _lpl:=0;
                  _dt:=grp_fld();
                  _dt:=date(_dt~1,_dt~2,0)+1;
                  __sk_m.TAB.cntx_psh();
                  {? __sk_m.TAB.find_ge(_dt)
                  || _dt:=__sk_m.TAB.MC;
                     __sk_m.TAB.prefix(_dt);
                     _lpl:=1+__sk_m.TAB.size()*2;
                     __sk_m.TAB.prefix();

                     _dt:=date(_dt~1,_dt~2,0)+1;
                     {? ~__sk_m.TAB.find_ge(_dt)
                     || _lpl+=5
                     ?}
                  ?};
                  __sk_m.TAB.cntx_pop();
                  _ll:=lineleft();
                  _ll<>0 & _ll<_lpl
               }
                  {PAGE}
               {FI}
            {OD}
            {SUBSTITUTE: 'LINIAF'}
            {HEADER}{HEADER-}
            {FOOTER}{FOOTER-}
         {FI}
         {LINE: 3}
         {<1>{par.CL/2}(18*'─')}{<{par.CL/2}>{par.CL}(18*'─')}
         {<1>{par.CL/2}'Obliczył'@}{<{par.CL/2}>{par.CL}'Sprawdził'@}
       {FI}
   {FI}
{MACRO-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
:!UTF-8
{TITLE:O.  Dokumenty elektroniczne w rejestrach VAT}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,
   PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,17); PRN.sl_frame(); w:=obj_new(21);
   PRN1:=obj_new(@.CLASS.PRINT,5); PRN1.sl_frame();
   lp:=ssize:=nl:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   DOK.cntx_psh()}
{END:
   DOK.cntx_pop();
   VAR_DEL.delete('PRN','w','PRN1');
   VAR_DEL.delete('lp','ssize','nl');
   VAR_DEL.delete('tvat','tn','tv','tb','gvat','gn','gv','gb','hdr_vat');
   VAR_DEL.delete('color','prn1','prn2','lm','ll','vp','lmt','rsep','s_sign');
   VAR_DEL.delete('okrVat','okrVatNr')}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
====================================================================================================
Nazwa pliku: vat_edok.rpm
====================================================================================================
Zawartosc: Zestawienie dokumentow elektronicznych w rejestrach VAT
{COMMENT-}
{  WYDRUKIN.win_edit('WYBDATA9');
   PAR_WYDR.PAR1:=PAR_WYDR.PAR2:=PAR_WYDR.PAR3:=PAR_WYDR.PAR4:=PAR_WYDR.PAR5:=PAR_WYDR.PAR6:=1;
   VAT7.RVAT_N:=null; ~~}
{EXIT: ~WYDRUKIN.edit()}
{  okrVat:=exec('okr_vat_wydr','!fks_vat_zrej');
   VAT7.KRAJ:=exec('kod_pl','slo_slu');
   SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
   {? SLU.find_key('~STAWKI VAT '+VAT7.KRAJ().KOD)
   || SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(SLU.ref());
      ssize:=SLO.size(); tvat:=obj_new(ssize); gvat:=obj_new(ssize);
      tn:=obj_new(ssize); tv:=obj_new(ssize); tb:=obj_new(ssize);
      gn:=obj_new(ssize); gv:=obj_new(ssize); gb:=obj_new(ssize);
      {? SLO.first()
      || {! _i:=1..ssize |!
            tvat[_i]:=gvat[_i]:=SLO.KOD; tn[_i]:=tv[_i]:=tb[_i]:=gn[_i]:=gv[_i]:=gb[_i]:=0;
            SLO.next()
         !}
      ?};
      SLO.cntx_pop()
   ?};
   SLU.cntx_pop();
   PRN.add("w[1]",4,'Lp.',1);
   {? rep_expo()
   || PRN.add("w[11]",9,'Rej. VAT',,'#');
      PRN.add("w[12]",9,'Pozycja',,'#')
   || PRN.add("w[2]",9,'Rej. VAT/pozycja'@,,'#')
   ?};
   {? rep_expo()
   || PRN.add("w[13]",20,'Symbol dokumentu',,'#');
      PRN.add("w[14]",20,'Termin płatności',,'#')
   || PRN.add("w[3]",50,'Symbol dokumentu/Termin płatności'@,,'#')
   ?};
   {? rep_expo()
   || PRN.add("w[15]",12,'Data operacji',,'#');
      PRN.add("w[16]",12,'Data zapisu',,'#');
      PRN.add("w[17]",12,'Data wystawienia',,'#');
      PRN.add("w[18]",12,'Data potwierdzenia odbioru',,'#')
   || PRN.add("w[4]",12,'D. operacji/zapisu/#wystawienia/potw. odb.'@,,'#')
   ?};
   {? rep_expo()
   || {? ~OPERATOR.DEPT || PRN.add("w[19]",21,'Jednostka księgowa#',,'#') ?};
      PRN.add("w[20]",{? OPERATOR.DEPT || 13 || 21 ?},'Rej. księg.',,'#');
      PRN.add("w[21]",{? OPERATOR.DEPT || 13 || 21 ?},'Pozycja',,'#')
   || PRN.add("w[5]",{? OPERATOR.DEPT || 13 || 21 ?},
      {? OPERATOR.DEPT || '' || 'Jednostka księgowa#' ?}+'Rej. księg.'+'/ pozycja',,'#')
   ?};
   PRN.add("w[6]",{? OPERATOR.DEPT || 80 || 72 ?},
      'Kontrahent - nazwa, siedziba, numer identyfikacyjny'@);
   PRN.add("w[7]",5,'%% VAT'@,1,'#');
   PRN.add("w[8]",20,'Netto',1,'#');
   PRN.add("w[9]",16,'VAT',1,'#');
   PRN.add("w[10]",20,'Brutto',1,'#');
   PRN1.add("w[1]",173);
   PRN1.add("w[2]",5,'% VAT',1,'#');
   PRN1.add("w[3]",20,'Netto',1,'#');
   PRN1.add("w[4]",16,'VAT',1,'#');
   PRN1.add("w[5]",20,'Brutto',1,'#');
   PAR_WYDR.TITLE:='DOKUMENTY ELEKTRONICZNE W REJESTRACH VAT';
   prn1:='PRN'; prn2:='PRN1'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}----------------------------------------MACRO-DATALINE---------------------------{COMMENT-}
{MACRO: 'DATALINE'}
{  lp+=1;
   w[1]:=$lp;
   w[2]:=form(RVAT.SYM,8)+'#'+$DVAT.NR;
   w[11]:=form(RVAT.SYM,8);
   w[12]:=$DVAT.NR;
   _vtp:=exec('spr_roz1','!fks_vat_zrej',DVAT.DOK,DVAT.SYM1,DVAT.DOKOKRO().ROK().KOD,DVAT.DOKOKRO().NR,date(0,0,0),DVAT.DAT2);
   w[3]:=form(DVAT.SYM1,15)+{? _vtp<>date(0,0,0) || '#'+_vtp$1 || '' ?};
   w[13]:=form(DVAT.SYM1,15);
   w[14]:={? _vtp<>date(0,0,0) || '#'+_vtp$1 || '' ?};
   w[15]:=DVAT.DAT5$1;
   w[16]:=DVAT.DAT1$1;
   w[17]:=DVAT.DAT2$1;
   w[18]:={? KVAT.SYM<>'_WWspN' & DVAT.DAT4<>date(0,0,0) || '#'+DVAT.DAT4$1 || '' ?};
   w[4]:=DVAT.DAT5$1+'#'+DVAT.DAT1$1+'#'+DVAT.DAT2$1
      +{? KVAT.SYM<>'_WWspN' & DVAT.DAT4<>date(0,0,0) || '#'+DVAT.DAT4$1 || '' ?};
   {? VAT7.TYPOKR='K'
   || DOK.use('doku'+DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2))
   ?};
   DVAT.DOK().REJ();
   {? OPERATOR.DEPT
   || w[5]:=REJ.KOD+(8-(+REJ.KOD))*' '
   || w[5]:=form(DVAT.ODD().OD,20)+'#'+REJ.KOD+(16-(+REJ.KOD))*' ';
      w[20]:=form(DVAT.ODD().OD,20);
      w[21]:=REJ.KOD;w[19]:=REJ.KOD
   ?};
   w[5]+=form(DOK.NR,5,,'99');
   w[6]:=exec('szukkh','!fks_vat_zrej');
   PVAT.prefix(DVAT.ref());
   {? PVAT.first()
   || {! |?
         {! _i:=1..ssize |!
            {? PVAT.STVAT().KOD=tvat[_i]
            || tv[_i]+=PVAT.VAT; tn[_i]+=PVAT.SUM2; tb[_i]+=PVAT.SUM1;
               gv[_i]+=PVAT.VAT; gn[_i]+=PVAT.SUM2; gb[_i]+=PVAT.SUM1
            ?}
         !};
         PVAT.next()
      !}
   ?};
   _cnt:=1;
   {! |? _cnt<=ssize|!
      {? (tn[_cnt]$2)<>0 | (tv[_cnt]$2)<>0 | (tb[_cnt]$2)<>0
      || {? +w[7] || w[7]+='#'+form(tvat[_cnt],4,2,' ,') || w[7]:=form(tvat[_cnt],4,2,' ,') ?};
         {? +w[8] || w[8]+='#'+form(tn[_cnt],19,2,' ,') || w[8]:=form(tn[_cnt],19,2,' ,') ?};
         {? +w[9] || w[9]+='#'+form(tv[_cnt],15,2,' ,') || w[9]:=form(tv[_cnt],15,2,' ,') ?};
         {? +w[10] || w[10]+='#'+form(tb[_cnt],19,2,' ,') || w[10]:=form(tb[_cnt],19,2,' ,') ?}
      ?};
      _cnt+=1
   !}; ~~}
{SUBSTITUTE: 'LINIA0'}
{  {! _i:=1..ssize |! tn[_i]:=tv[_i]:=tb[_i]:=0 !};
   w[7]:=w[8]:=w[9]:=w[10]:=''; ~~}
{MACRO-}
{COMMENT}----------------------------------------MACRO-RVATLINE---------------------------{COMMENT-}
{MACRO: 'RVATLINE'}
{IF: OPERATOR.DEPT}
   {okrVatNr:=1;~~}
   {WHILE: okrVatNr<=obj_len(okrVat)}
   {DO}
      {FOR: 'DVAT'}
      {INDEX: 'NR_O'}
      {PREFIX: OPERATOR.DEPT,VAT7.KRAJ,okrVat[okrVatNr],RVAT.ref()}
      {DO}
         {IF: DVAT.E_DOC}{SUBSTITUTE: 'DATALINE'}{FI}
      {OD}
      {okrVatNr+=1;~~}
   {OD}
{ELSE}
   {okrVatNr:=1;~~}
   {WHILE: okrVatNr<=obj_len(okrVat)}
   {DO}
      {FOR: 'DVAT'}
      {INDEX: 'NR'}
      {PREFIX: VAT7.KRAJ,okrVat[okrVatNr],RVAT.ref()}
      {DO}
         {IF: DVAT.E_DOC}{SUBSTITUTE: 'DATALINE'}{FI}
      {OD}
      {okrVatNr+=1;~~}
   {OD}
{FI}
{MACRO-}
{COMMENT}----------------------------------------SIGNATURE--------------------------------{COMMENT-}
{IF: ~rep_expo()}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}----------------------------------------REPORT-HEADER----------------------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<1 'PARAMETRY WYDRUKU:'}
{<1   {? OPERATOR.DEPT
      || 'Jednostka księgowa: '+OPERATOR.DEPT().OD
      || 'Wszystkie jednostki księgowe' ?}}
{<1 'Okres obrachunkowy: '+exec('naz_okr_vat','!fks_vat_zrej',0,0)}
{<1 {? VAT7.RVAT_N || 'Rejestr VAT: '+VAT7.RVAT_N().SYM || 'Wszystkie rejestry VAT' ?}}
{<1   {? PAR_WYDR.PAR1 & PAR_WYDR.PAR2 & PAR_WYDR.PAR3 & PAR_WYDR.PAR4 & PAR_WYDR.PAR5 & PAR_WYDR.PAR6
      || 'Wszystkie klasy ewidencji'
      || _t:='Klasy ewidencji: '
         +{? PAR_WYDR.PAR1 || 'Dostawy tow./eksport, ' || '' ?}
         +{? PAR_WYDR.PAR2 || 'Nabycia tow./usług, ' || '' ?}
         +{? PAR_WYDR.PAR3 || 'Import towarów, ' || '' ?}
         +{? PAR_WYDR.PAR4 || 'Import usług, ' || '' ?}
         +{? PAR_WYDR.PAR5 || 'WDT, ' || '' ?}
         +{? PAR_WYDR.PAR6 || 'WNT, ' || '' ?}; _t:=(_t-2)+'.'; _t
      ?}}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}----------------------------------------HEADER-----------------------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}----------------------------------------FOOTER-----------------------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{~~}
{FOOTER-}
{COMMENT}----------------------------------------WYDRUK-----------------------------------{COMMENT-}
{  w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:=w[8]:=w[9]:=w[10]:=w[11]:=w[12]:=w[13]:=w[14]:=w[15]:=w[16]:=w[17]:=w[18]:=w[19]:=w[20]:=w[21]:='';
   PVAT.index('DVAT_ST'); ~~}
{FONT: 'T8G'}
{SPACE: 'C'}
{FOR: 'KVAT'}
{INDEX: 'SYM'}
{DO}
   {IF:  _s:=KVAT.SYM;
         (PAR_WYDR.PAR1 & 1+_s='S') | (PAR_WYDR.PAR2 & 1+_s='Z') | (PAR_WYDR.PAR3 & 4+_s='ImpT')
         | (PAR_WYDR.PAR4 & 4+_s='ImpU') | (PAR_WYDR.PAR5 & 6+_s='_WWspD')
         | (PAR_WYDR.PAR6 & 6+_s='_WWspN')}
      {IF: VAT7.RVAT_N}
         {FOR: 'RVAT'}
         {INDEX: 'RVAT_EWI'}
         {PREFIX: REF.FIRMA,KVAT.EWID,VAT7.RVAT_N().SYM,VAT7.RVAT_N().SYM,KVAT.ref()}
         {DO}
            {SUBSTITUTE: 'RVATLINE'}
         {OD}
      {ELSE}
         {FOR: 'RVAT'}
         {INDEX: 'RVAT_ESY'}
         {PREFIX: REF.FIRMA,KVAT.SYM}
         {DO}
            {SUBSTITUTE: 'RVATLINE'}
         {OD}
      {FI}
   {FI}
{OD}
{COMMENT}----------------------------------------REPORT-FOOTER----------------------------{COMMENT-}
{  w[1]:='Razem'; w[2]:=w[3]:=w[4]:=w[5]:='';
   _cnt:=1;
   {! |? _cnt<=ssize|!
      {? (gn[_cnt]$2)<>0 | (gv[_cnt]$2)<>0 | (gb[_cnt]$2)<>0
      || {? +w[2] || w[2]+='#'+form(gvat[_cnt],4,2,' ,') || w[2]:=form(gvat[_cnt],4,2,' ,') ?};
         {? +w[3] || w[3]+='#'+form(gn[_cnt],19,2,' ,') || w[3]:=form(gn[_cnt],19,2,' ,') ?};
         {? +w[4] || w[4]+='#'+form(gv[_cnt],15,2,' ,') || w[4]:=form(gv[_cnt],15,2,' ,') ?};
         {? +w[5] || w[5]+='#'+form(gb[_cnt],19,2,' ,') || w[5]:=form(gb[_cnt],19,2,' ,') ?};
         nl+=1
      ?};
      _cnt+=1
   !}; ~~}
{IF: lineleft() & lineleft()<nl+2}{PAGE}{FI}
{IF: ~rep_expo()}{FOOTER}{FOOTER-}
{SUBSTITUTE: 'LINIAFS'}{FI}

:!UTF-8
{TITLE:A. Zamówienia dostaw wg dostawców}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   wwyd_001:="
      VAR_DEL.delete('ww','kk','doklw','lp','PRN','tryb');
      VAR_DEL.delete('kh','zakres','sort1','kh_col','mat_col');
      VAR_DEL.delete('odd','dod');
      VAR_DEL.delete('wydr_naz','wydr_edi');
      VAR_DEL.delete('wyjscie','X_Xw1','X_Xa','indX_Xa','X_Xb','indX_Xb');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep','s')
   ";
   wwyd_001();

   _ilk:=9;
   PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();

   kk:=obj_new(_ilk); "--szerokosc kolumn--";
   ww:=obj_new(_ilk); "--wartosci kolumn--";
   doklw:=2;   "--dokladnosc wartosci--";
   ss:=st:=0;  "--sumator--";
   kk[1]:=4;   "--Lp.--";
   kk[2]:=47;  "--Dostawca--";
   kk[3]:=8;   "--Liczba zamówień--";
   kk[4]:=14;  "--Wartosc netto--";
   kk[5]:=14;  "--Wartosc zrealizowana--";
   kk[6]:=5;   "--Grupa materiałowa--";
   kk[7]:=22;  "--Indeks--";
   kk[8]:=7;   "--Liczba pozycji--";
   kk[9]:=5;   "--Jednostka miary--";
   tryb:=rep_export();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   kh:=zakres:=sort1:='';
   odd:=dod:=date(0,0,0);
   lp:=0;

   wydr_naz:='wwyd_001';
   wydr_edi:='WWYD_001';

   KH.cntx_psh;
   KH.win_dict('WER_SLO'); KH.actions('WER_SLO','W');

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("{? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || '' || FUN.info('Błędny zakres dat.'@);'DO_DATY' ?}")
   ||
      {? WYDRUKIL.put
      || wyjscie:=0;
         odd    :=WYDRUKIL.OD_DATY;
         dod    :=WYDRUKIL.DO_DATY;
         sort1  :=WYDRUKIL.SORTUJ;
         zakres :={? WYDRUKIL.RADIOB_1=2 || 'Z' |? WYDRUKIL.RADIOB_1=1 || 'A' || 'W' ?};
         filter :={? WYDRUKIL.RADIOB_2=3 || 'G' |? WYDRUKIL.RADIOB_2=2 || 'M' |? WYDRUKIL.RADIOB_2=1 || 'K' || 'B' ?};
         kh_col :={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         mat_col:={? WYDRUKIL.CHKBOX02=1 || 'T' || 'N' ?}
      ?}
   ?};
   KH.actions('WER_SLO','');
   KH.cntx_pop();
   {? wyjscie=0 & filter='K' || wyjscie:=exec('filtr_kh','kontrahent','T','N','K')
   |? wyjscie=0 & filter='M' || wyjscie:=exec('filtr_m_ind','material')
   |? wyjscie=0 & filter='G' || wyjscie:=exec('filtr_mgr','material')
   ?}
}
{END:
   wwyd_001(); VAR_DEL.delete('wwyd_001')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("ww[1]",kk[1],'Lp.'@,1) ?};
{? mat_col='T' ||
   PRN.add("ww[2]",kk[6],'Grupa'@);
   PRN.add("ww[3]",kk[7],'Indeks'@);
   PRN.add("ww[4]",kk[8],'Ilość'@,1);
   PRN.add("ww[5]",kk[9],'jm'@)
?};
{? kh_col='T' ||
   PRN.add("ww[6]",kk[2],'Dostawca'@)
?};
PRN.add("ww[7]",kk[3],'Liczba zamówień'@,1);
{? exec('upr_cm','ceny') || PRN.add("ww[8]",kk[4],'Wartość'@,1) ?};
{? exec('upr_cm','ceny') || PRN.add("ww[9]",kk[5],'Wartość zrealizowana'@,1) ?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'KH'     ,'STRING[70]'  ,'Dostawca',
   'NET'    ,'REAL'        ,'Wartość netto',
   'ZRE'    ,'REAL'        ,'Wartość zrealizowana',
   'IL'     ,'REAL'        ,'Ilość zamówień',
   'GM'     ,'STRING[8]'   ,'Grupa materiałowa',
   'IND'    ,'STRING[50]'  ,'Indeks',
   'ILP'    ,'REAL'        ,'Ilość zamówionych',
   'JM'     ,'STRING[10]'  ,'Jednostka miary'
   );
X_Xw1.index(X_Xw1.ndx_tmp(,,{? sort1='W' || 'NET' |? sort1='L' || 'IL' || 'ZRE' ?},,));
{? filter='K' || indX_Xb:=X_Xb.ndx_tmp('',0,'REF',,)
|? filter='M' || indX_Xa:=X_Xa.ndx_tmp('',0,'REF',,)
|? filter='G' || indX_Xa:=X_Xa.ndx_tmp('',0,'REF',,)
?};

"--obliczenia w masce zbiorczej--";
ZD_POZ.index('DAM'); ZD_POZ.prefix();
{? (zakres='W' | zakres='A') & ZD_POZ.first
||
   {!
   |? {? ZD_POZ.ZD_NAG().STAT_REJ<>'A'
         & (ZD_POZ.ZD_NAG().DATA >= odd | odd=date(0,0,0)) & (ZD_POZ.ZD_NAG().DATA <= dod | dod=date(0,0,0))
         & (kh='' | kh=ZD_POZ.KH().KOD)
         & {? filter='B' || 1
           |? filter='K' || X_Xb.index(indX_Xb); X_Xb.prefix($ZD_POZ.KH,); X_Xb.first()
           |? filter='M' || X_Xa.index(indX_Xa); X_Xa.prefix($ZD_POZ.M,); X_Xa.first()
           |? filter='G' || X_Xa.index(indX_Xa); X_Xa.prefix($ZD_POZ.M().MGR,); X_Xa.first() ?}
      ||
         {? (mat_col='T' & kh_col='T'
              & ~X_Xw1.find_tab(1,'KH',,':-', ZD_POZ.KH().KOD+' '+KH.NAZ
                                 ,'IND',,':-', ZD_POZ.M().KTM
                                 ,'JM',,':-', ZD_POZ.JM().KOD)) |
            (mat_col='T' & kh_col='N'
              & ~X_Xw1.find_tab(1,'IND',,':-', ZD_POZ.M().KTM
                                 ,'JM',,':-', ZD_POZ.JM().KOD)) |
            (mat_col='N' & kh_col='T' & ~X_Xw1.find_tab(1,'KH',,':-', ZD_POZ.KH().KOD+' '+KH.NAZ)) |
            (mat_col='N' & kh_col='N' & ~X_Xw1.find_tab(1,'IL',,'>', 0))
         ||
            {? kh_col='T' ||
               X_Xw1.KH    :=ZD_POZ.KH().KOD+' '+KH.NAZ
            ?};
            {? mat_col='T' ||
               X_Xw1.GM    :=ZD_POZ.M().MGR().KOD;
               X_Xw1.IND   :=ZD_POZ.M().KTM;
               X_Xw1.ILP   :=ZD_POZ.IL;
               X_Xw1.JM    :=ZD_POZ.JM().KOD
            ?};
            X_Xw1.NET      :=ZD_POZ.WAR;
            X_Xw1.ZRE      :={? ZD_POZ.ZD_NAG().STAN='Z' || ZD_POZ.WAR || 0 ?};
            X_Xw1.IL       :=1;
            X_Xw1.add
         ||
            X_Xw1.IL       :=X_Xw1.IL+1;
            {?  mat_col='T' ||
               X_Xw1.ILP   :=X_Xw1.ILP+ZD_POZ.IL
            ?};
            X_Xw1.NET      :=X_Xw1.NET+ZD_POZ.WAR;
            X_Xw1.ZRE      :={? ZD_POZ.ZD_NAG().STAN='Z' || X_Xw1.ZRE+ZD_POZ.WAR || X_Xw1.ZRE ?};
            X_Xw1.put()
         ?}
      ?};
      ZD_POZ.next
   !}
?};
"--obliczenia w maskach archiwalnych--";
OKR.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? (zakres='W' | zakres='Z') & OKR.first
||
   ZD_NAG.cntx_psh; ZD_POZ.cntx_psh; ZD_RN.cntx_psh;
   {!
   |? _maska:=ST.ODDZ+form(form(OKR.ROK,,,'99'),-2);
      ZD_NAG.use('zdnag'+_maska);
      ZD_POZ.use('zdpoz'+_maska);
      ZD_RN.use('zdhin'+_maska);
      ZD_POZ.index('DAM'); ZD_POZ.prefix();
      {? ZD_POZ.first
      ||
         {!
         |? {? ZD_POZ.ZD_NAG().STAT_REJ<>'A'
               & (ZD_POZ.ZD_NAG().DATA >= odd | odd=date(0,0,0)) & (ZD_POZ.ZD_NAG().DATA <= dod | dod=date(0,0,0))
               & (kh=ZD_POZ.KH().KOD | kh='')
               & {? filter='B' || 1
                 |? filter='K' || X_Xb.index(indX_Xb); X_Xb.prefix($ZD_POZ.KH,); X_Xb.first()
                 |? filter='M' || X_Xa.index(indX_Xa); X_Xa.prefix($ZD_POZ.M,); X_Xa.first()
                 |? filter='G' || X_Xa.index(indX_Xa); X_Xa.prefix($ZD_POZ.M().MGR,); X_Xa.first() ?}
            ||
               {? (mat_col='T' & kh_col='T' & ~X_Xw1.find_tab(1,'KH',,':-', ZD_POZ.KH().KOD+' '+KH.NAZ,'IND',,':-', ZD_POZ.M().KTM)) |
                  (mat_col='T' & kh_col='N' & ~X_Xw1.find_tab(1,'IND',,':-', ZD_POZ.M().KTM)) |
                  (mat_col='N' & kh_col='T' & ~X_Xw1.find_tab(1,'KH',,':-', ZD_POZ.KH().KOD+' '+KH.NAZ)) |
                  (mat_col='N' & kh_col='N' & ~X_Xw1.find_tab(1,'IL',,'>', 0))
               ||
                  {? kh_col='T' ||
                     X_Xw1.KH    :=ZD_POZ.KH().KOD+' '+KH.NAZ
                  ?};
                  {? mat_col='T' ||
                     X_Xw1.GM    :=ZD_POZ.M().MGR().KOD;
                     X_Xw1.IND   :=ZD_POZ.M().KTM;
                     X_Xw1.ILP   :=ZD_POZ.IL;
                     X_Xw1.JM    :=ZD_POZ.JM().KOD
                  ?};
                  X_Xw1.NET      :=ZD_POZ.WAR;
                  X_Xw1.ZRE      :={? ZD_POZ.ZD_NAG().STAN='Z' || ZD_POZ.WAR || 0 ?};
                  X_Xw1.IL       :=1;
                  X_Xw1.add
               ||
                  X_Xw1.IL       :=X_Xw1.IL+1;
                  {?  mat_col='T' ||
                     X_Xw1.ILP   :=X_Xw1.ILP+ZD_POZ.IL
                  ?};
                  X_Xw1.NET      :=X_Xw1.NET+ZD_POZ.WAR;
                  X_Xw1.ZRE      :={? ZD_POZ.ZD_NAG().STAN='Z' || X_Xw1.ZRE+ZD_POZ.WAR || X_Xw1.ZRE ?};
                  X_Xw1.put()
               ?}
            ?};
            ZD_POZ.next
         !}
      ?};
      OKR.next
   !};
   ZD_NAG.cntx_pop; ZD_POZ.cntx_pop; ZD_RN.cntx_pop
?};
OKR.cntx_pop();

PAR_WYDR.TITLE:='Zamówienia dostaw wg dostawców'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział: %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{IF: filter='K'}{<{lmt+1}}{'Filtrowanie po: kontrahenci'@}
{ELIF: filter='M'}{<{lmt+1}}{'Filtrowanie po: materiały'@}
{ELIF: filter='G'}{<{lmt+1}}{'Filtrowanie po: grupy materiałowe'@}
{ELIF: filter='B'}{<{lmt+1}}{'Filtrowanie po: bez ograniczeń'@}
{FI}
{<{lmt+1}}{'Zakres dat: od %1 do %2'@[$odd,$dod]}
{<{lmt+1}}{{? sort1='W' || 'Sortowanie wg: wartość'@ |? sort1='L' || 'Sortowanie wg: liczba zamówień'@ || 'Sortowanie wg: wartość zrealizowana'@ ?}}
{<{lmt+1}}{{? zakres='Z' || 'Zakres zamówień: zarchiwizowane'@ |? zakres='A' || 'Zakres zamówień: aktywne'@ || 'Zakres zamówień: wszystkie'@ ?}}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xw1}
{DO}
   {
   lp+=1;
   ww[1]:=form(lp);
   {? mat_col='T' ||
      ww[2]:=form(X_Xw1.GM);
      ww[3]:=form(X_Xw1.IND);
      ww[4]:=form(X_Xw1.ILP);
      ww[5]:=form(X_Xw1.JM)
   ?};
   {? kh_col='T' ||
      ww[6]:=form(X_Xw1.KH)
   ?};
   ww[7]:=form(X_Xw1.IL);
   ww[8]:=form(X_Xw1.NET,,doklw,'.,');
   ww[9]:=form(X_Xw1.ZRE,,doklw,'.,');
   ss+=X_Xw1.NET;
   st+=X_Xw1.ZRE;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: exec('upr_cm','ceny')}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {FOOTER}{FOOTER-}
   {SUBSTITUTE: 'LINIAF'}
   {
   PRN.n_frame();
   ww[1]:='';
   ww[2]:='';
   ww[3]:='';
   ww[4]:='';
   ww[5]:='';
   ww[6]:='';
   {? mat_col='T' ||
      ww[7]:='Razem:'@; PRN.FORM[6]:=0
   |? kh_col='T' ||
      ww[7]:='Razem:'@; PRN.FORM[3]:=0
   ||
      ww[7]:='Razem:'@; PRN.FORM[2]:=0
   ?};
   ww[8]:=form(ss,,2,'.,');
   ww[9]:=form(st,,2,'.,');

   ~~}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
:!UTF-8
{COMMENT}OLD: zaswsw.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{FOR: __CALL}{DO}
   { gender.setGender(__CALL.PLEC);~~}
   {IF: PAR_WYDR.LOGO}
      {IF: exec('get_logo','img_blob')}
         {<1}{IMAGE: exec('get_logo','img_blob');0;1}
      {ELSE}
         {<1}{BITMAP:'"logo.bmp",,1'}
      {FI}
   {FI}
   {IF: PAR_WYDR.HEAD}
      {FONT: 'HEAD'}{SPACE: 'B'}
      { lmt:=charleft(); ~~ }
      {>{lmt}' %1, dn. %2'[__CALL.MIEJSCE,__CALL.DATA]}
      {<1 STR.split(__CALL.F_NAZWA); STR.line(lmt)}
      {WHILE: STR.next()}{DO}{<1 STR.line(lmt)}{OD}
      { VAR_DEL.delete('lmt'); ~~}
      {<1}{__CALL.F_ULICA}
      {<1}{__CALL.F_POCZTA}
      {IF: __CALL.F_NIP<>''}
         {<1}NIP {__CALL.F_NIP}
      {ELSE}
         {<1}REGON-PKD {__CALL.F_REGON}{{? +__CALL.F_PKD & +__CALL.F_REGON || '-' || '' ?}}{__CALL.F_PKD}
      {FI}
   {ELSE}
      {FONT: 'HEAD'}{SPACE: 'B'}
      {LINE: 5}
   {FI}
   {FONT: 'TITLE'}{SPACE: 'A'}
   {LINE: 3}
   {<1>{charleft()} 'ŚWIADECTWO PRACY'}
   {LINE: 2}
   {FONT: 'W12'}{SPACE: 'A'}
   {<1}1.{<5'Stwierdza się, że %1 %2,'[form(__CALL.IMIE+' '+__CALL.DRUGIEIM),__CALL.NAZWISKO]}
      {<5'%1 %2'[gender.text('urodzona','urodzony'),__CALL.UR_DATA+',']}
      {<5}{ STR.split(gender.text('była zatrudniona w ','był zatrudniony w ')+__CALL.F_NAZWA);
            _a:=STR.line(60);
            Temp:=STR.line(60);
            _a }
   {IF: |Temp<>''}{<5}{Temp}{FI}
   { Temp:=proc_exe('spwy@zasw',__CALL.PRACOWN,__CALL.DO_DATY,__CALL.OD_DATY); ~~ }
   {FOR: Temp}{DO}
      {<5}{Temp.OPIS}
   {OD}
   { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }

   {ENTIRE}
      {<1}2.{<5 'W okresie zatrudnienia %1 pracy tymczasowej'
               [gender.text('pracownica nie wykonywała','pracownik nie wykonywał')]}
   {ENTIRE-}

   {ENTIRE}
      {<1}3.{<5}{  {? __CALL.OD_DATY='0'
         || Temp:=proc_exe('spst@zasw',__CALL.PRACOWN,__CALL.DO_DATY)
         || Temp:=proc_exe('spstod@zasw',__CALL.PRACOWN,__CALL.DO_DATY,__CALL.OD_DATY)
         ?};
         _txt:={? Temp.size()>1 || 'stanowiskach' || 'stanowisku' ?};
         'W okresie zatrudnienia %1 pracę na %2:'[ gender.text('pracownica wykonywała','pracownik wykonywał'),_txt]}
      {FOR: Temp}{DO}
         {<5}{Temp.ST}
      {OD}
      { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
   {ENTIRE-}

   {ENTIRE}
      {<1}4.{<5'Stosunek pracy ustał w wyniku:'}
      {  _text:=
            {? __CALL.RUM='R' || ' a) rozwiązania '
            |? __CALL.RUM='W' || ' b) wygaśnięcia '
            || ''
            ?};
         _ml1:=5;
         _ml2:=_ml1+(+_text);
         _dl:=charleft()-_ml2;
         Temp:='{<'+$_ml1+'}'+_text;
         'ER/WRT/XP/23.25/2401/0032 Punkt czwarty świadectwa pracy nie mieści się na wydruku, decyzja ATA, 08.01.2024';
         _text:=__CALL.OPIS_ROZ+__CALL.OPIS_RO2;
         _text+=
            {? +(__CALL.PP_ROZ+__CALL.PP_RO2)
            || ' (podstawa prawna: '+gsub(__CALL.PP_ROZ+__CALL.PP_RO2,'§','par.')+')'
            || ''
            ?};
         STR.split(_text);
         _line:=STR.line(_dl);
         {!
         |? _line<>''
         |! Temp+='{<'+$_ml2+'}'+_line+'{LINE}';
            _line:=STR.line(_dl)
         !}; ~~
      }
      {REP: Temp}
   {ENTIRE-}

   {ENTIRE}
      {IF: __CALL.SKRWYPOD<>'' & __CALL.SKRWYPDO<>''}
         {<1}5.{<5}{'Został zastosowany skrócony okres wypowiedzenia umowy o pracę'}
               {<5}{'na podstawie art. 36(1) par. 1 Kodeksu pracy od %1 do %2' [__CALL.SKRWYPOD,__CALL.SKRWYPDO]}
      {ELSE}
         {<1}5.{<5}{'Nie został zastosowany skrócony okres wypowiedzenia umowy o pracę'}
               {<5}{'na podstawie art. 36(1) par. 1 Kodeksu pracy'}
      {FI}
   {ENTIRE-}

   {ENTIRE}
      {<1}6.{<5}{'W okresie zatrudnienia %1:'[gender.text('pracownica','pracownik')]}
      {IF: __CALL.USW<>''}
         {<5}1){<10}{'%1 zwolnienie od pracy przewidziane w art. 148(1) par. 1 Kodeksu pracy'
              [gender.text('wykorzystała','wykorzystał')]}
         {<10}{'w wymiarze %1'[__CALL.USW]}
      {ELSE}
         {<5}1){<10}{'%1 ze zwolnienia od pracy przewidzianego w art. 148(1) par. 1 Kodeksu pracy'
              [gender.text('nie korzystała','nie korzystał')]}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {<5}2){<10}{'%1 urlop wypoczynkowy w wymiarze '[gender.text('wykorzystała','wykorzystał')]
              }{IF: +__CALL.URLOPG}{'%1 godzin, co stanowi' [__CALL.URLOPG]+' '} {FI}{__CALL.URLOP},
         {  Temp:=gender.text('w tym wykorzystała ','w tym wykorzystał ')+
            __CALL.URLOPNZ + {? __CALL.URLOPNZG <> '' || ' ('+'%1 godzin' [__CALL.URLOPNZG]+')' || '' ?} +
            ' na podstawie art. 167(2) Kodeksu pracy';
            STR.split(Temp);~~
         }
         {WHILE: +(Temp:=STR.line(charleft() - 10))}{DO}
            {<10}{Temp}
         {OD}
   {ENTIRE-}
   {ENTIRE}
      {IF: __CALL.UOP<>''}
         {<5}3){<10}{'%1 urlop opiekuńczy w wymiarze %2'[gender.text('wykorzystała','wykorzystał'),__CALL.UOP]}
      {ELSE}
         {<5}3){<10}{'%1 z urlopu opiekuńczego'[gender.text('nie korzystała','nie korzystał')]}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {IF: +__CALL.URLOPB | +__CALL.WOJSKO}
         {<5}4){<10}{'%1 z urlopu bezpłatnego'[gender.text('korzystała','korzystał')]}
         {IF: +__CALL.URLOPB}
            { Temp:=proc_exe('spur@zasw',__CALL.PRACOWN,__CALL.DO_DATY,'B',0,__CALL.OD_DATY); ~~ }
            {FOR: Temp}{DO}
               {<10}{Temp.TEKST}
            {OD}
            { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
         {FI}
         {IF: +__CALL.WOJSKO}
            {IF: +__CALL.INFO_WO1}
               {
               Temp:=proc_exe('spur@zasw',__CALL.PRACOWN,__CALL.DO_DATY,'O',0,__CALL.OD_DATY,1);
               ~~ }
               {<10}{__CALL.INFO_WO1}
               {FOR: Temp}{DO}
                  {<10}{Temp.TEKST}
               {OD}
               { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
            {FI}
            {IF: +__CALL.INFO_WO2}
               {
               Temp:=proc_exe('spur@zasw',__CALL.PRACOWN,__CALL.DO_DATY,'O',0,__CALL.OD_DATY,2);
               ~~ }
               {<10}{__CALL.INFO_WO2}
               {FOR: Temp}{DO}
                  {<10}{Temp.TEKST}
               {OD}
               { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
            {FI}
         {FI}
      {ELSE}
         {<5}4){<10}{'%1 z urlopu bezpłatnego'[gender.text('nie korzystała','nie korzystał')]}
      {FI}
   {ENTIRE-}
   {IF: __CALL.ALL_UM}
      {ENTIRE}
         {IF: __CALL.OJCIEC_W}
            {<5}5){<10}{'%1 urlop ojcowski w wymiarze'[gender.text('wykorzystała','wykorzystał')]}
            { Temp:=proc_exe('url_rodz@zasw',__CALL.PRACOWN,__CALL.DO_DATY,1153); ~~ }
            {FOR: Temp}{DO}
               {<10}{STR.split(gsub(Temp.OPIS,'§','par.')); STR.line(80)}
               {WHILE: +(temp:=STR.line(80))}{DO}
                  {<10}{temp}
            {OD}{OD}
            { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
         {ELSE}
            {<5}5){<10}{'%1 z urlopu ojcowskiego'[gender.text('nie korzystała','nie korzystał')]}
         {FI}
      {ENTIRE-}
      {ENTIRE}
         {IF: __CALL.RODZIC_W}
            {<5}6){<10}{'%1 urlop rodzicielski '[gender.text('wykorzystała','wykorzystał')]}
            { Temp:=proc_exe('url_rodz@zasw',__CALL.PRACOWN,__CALL.DO_DATY,1154); ~~ }
            {FOR: Temp}{DO}
               {<10}{STR.split(gsub(Temp.OPIS,'§','par.')); STR.line(80)}
               {WHILE: +(temp:=STR.line(80))}{DO}
                  {<10}{temp}
            {OD}{OD}
            { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
         {ELSE}
            {<5}6){<10}{'%1 z urlopu rodzicielskiego'[gender.text('nie korzystała','nie korzystał')]}
         {FI}
      {ENTIRE-}
      {ENTIRE}
         {IF: __CALL.URLOPW='DATABASE'}
            {<5}7){<10}{'%1 urlop wychowawczy udzielony na podstawie'[gender.text('wykorzystała','wykorzystał')]}
            { Temp:=proc_exe('url_rodz@zasw',__CALL.PRACOWN,__CALL.DO_DATY,114); ~~ }
            {FOR: Temp}{DO}
               {<10}{STR.split(gsub(Temp.OPIS,'§','par.')); STR.line(80)}
               {WHILE: +(temp:=STR.line(80))}{DO}
                  {<10}{temp}
            {OD}{OD}
            { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
         {ELSE}
            {<5}7){<10}{'%1 z urlopu wychowawczego'[gender.text('nie korzystała','nie korzystał')]}
         {FI}
      {ENTIRE-}
   {ELSE}
      {ENTIRE}
         {IF: __CALL.OJCIEC_W=100}
            {<5}5){<10}{'%1 urlop ojcowski w wymiarze'[gender.text('wykorzystała','wykorzystał')]}
            {  _od:={? __CALL.OD_DATY='0' || '' || __CALL.OD_DATY ?};
               Temp:=proc_exe('url_rodz@zasw',__CALL.PRACOWN,__CALL.DO_DATY,1153,,_od);
            ~~ }
            {FOR: Temp}{DO}
               {<10}{STR.split(gsub(Temp.OPIS,'§','par.')); STR.line(80)}
               {WHILE: +(temp:=STR.line(80))}{DO}
                  {<10}{temp}
            {OD}{OD}
            { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
         {ELIF: __CALL.OJCIEC_W}
            {<5}5){<10}{'%1 urlop ojcowski w wymiarze '[gender.text('wykorzystała','wykorzystał')]
            }{__CALL.OJCIEC_W} {{? __CALL.OJCIEC_W=1 || 'tygodnia' || 'tygodni' ?}} w {__CALL.OJCIEC_C
            } {{? __CALL.OJCIEC_C>1 || 'częściach' || 'części' ?}}
         {ELSE}
            {<5}5){<10'%1 z urlopu ojcowskiego'[gender.text('nie korzystała','nie korzystał')]}
         {FI}
      {ENTIRE-}
      {ENTIRE}
         {IF: __CALL.RODZIC_W=100}
            {<5}6){<10}{'%1 urlop rodzicielski '[gender.text('wykorzystała','wykorzystał')]}
            {  _od:={? __CALL.OD_DATY='0' || '' || __CALL.OD_DATY ?};
               Temp:=proc_exe('url_rodz@zasw',__CALL.PRACOWN,__CALL.DO_DATY,1154,,_od);
            ~~ }
            {FOR: Temp}{DO}
               {<10}{STR.split(gsub(Temp.OPIS,'§','par.')); STR.line(80)}
               {WHILE: +(temp:=STR.line(80))}{DO}
                  {<10}{temp}
            {OD}{OD}
            { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
         {ELIF: __CALL.RODZIC_W}
            {<5}6){<10}{'%1 urlop rodzicielski w wymiarze '[gender.text('wykorzystała','wykorzystał')]
            }{__CALL.RODZIC_W} {{? __CALL.RODZIC_W=1 || 'tygodnia' || 'tygodni' ?}
            } w {__CALL.RODZIC_C} {{? __CALL.RODZIC_C>1 || 'częściach' || 'części' ?}}
         {ELSE}
            {<5}6){<10}{'%1 z urlopu rodzicielskiego'[gender.text('nie korzystała','nie korzystał')]}
         {FI}
      {ENTIRE-}
      {ENTIRE}
         {IF: __CALL.URLOPW='DATABASE'}
            {<5}7){<10}{'%1 urlop wychowawczy udzielony na podstawie'[gender.text('wykorzystała','wykorzystał')]}
            {  _od:={? __CALL.OD_DATY='0' || '' || __CALL.OD_DATY ?};
               Temp:=proc_exe('url_rodz@zasw',__CALL.PRACOWN,__CALL.DO_DATY,114,,_od);
            ~~ }
            {FOR: Temp}{DO}
               {<10}{STR.split(gsub(Temp.OPIS,'§','par.')); STR.line(80)}
               {WHILE: +(temp:=STR.line(80))}{DO}
                  {<10}{temp}
            {OD}{OD}
            { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
         {ELIF: +__CALL.URLOPW}
            {<5}7){<10}{'%1 urlop wychowawczy udzielony na podstawie '[gender.text('wykorzystała','wykorzystał')]}
            {<10}{STR.split(__CALL.WYCH_P); STR.line(60)}
            {WHILE: +(Temp:=STR.line(60))}{DO}
               {<10}{Temp}
            {OD}
            {<10}{'w wymiarze '+form(__CALL.URLOPW)+' dni'}
            { Temp:=proc_exe('spur@zasw',__CALL.PRACOWN,__CALL.DO_DATY,'W',0,__CALL.OD_DATY); ~~ }
            {FOR: Temp}{DO}
               {<10}{Temp.TEKST}
            {OD}
            { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
         {ELSE}
            {<5}7){<10}{'%1 z urlopu wychowawczego'[gender.text('nie korzystała','nie korzystał')]}
         {FI}
      {ENTIRE-}
   {FI}
   {ENTIRE}
      {IF: +__CALL.OCHRONA}
         {<5}8){<10}{'%1 z ochrony stosunku pracy,'[gender.text('korzystała','korzystał')]}
         {<10}o której mowa w art. 186(8) par. 1 pkt 2 Kodeksu pracy, w okresie (okresach)
         {<10}{STR.split(__CALL.OCHRONA); STR.line(60)}
         {WHILE: +(Temp:=STR.line(60))}{DO}
            {<10}{Temp}
         {OD}
      {ELSE}
         {<5}8){<10}{'%1 z ochrony stosunku pracy,'[gender.text('nie korzystała','nie korzystał')]}
         {<10}{'o której mowa w art. 186(8) par. 1 pkt 2 Kodeksu pracy'}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {IF: +__CALL.OPIEKA}
         {<5}9){<10}{'%1 zwolnienia od pracy przewidziane w art. 188'[gender.text('wykorzystała','wykorzystał')]}
         {<10}{'Kodeksu pracy w wymiarze %1' [__CALL.OPIEKA]}
      {ELSE}
         {<5}9){<10}{'%1 ze zwolnienia od pracy przewidzianego w art. 188 Kodeksu pracy'
            [gender.text('nie korzystała','nie korzystał')]}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {IF: +__CALL.ZDALNA}
         {<5}10){<10}{'%1 pracę zdalną przewidzianą w art. 67(33) par. 1 Kodeksu pracy '
            [gender.text('wykonywała','wykonywał')]}
         {<10}{'w wymiarze %1'[__CALL.ZDALNA]}
      {ELSE}
         {<5}10){<10}{'%1 pracy zdalnej przewidzianej w art. 67(33) par. 1 Kodeksu pracy'
            [gender.text('nie wykonywała','nie wykonywał')]}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {IF: +__CALL.CHOROBA}
         {<5}11){<10}{'%1 wynagrodzenie przewidziane w art. 92 Kodeksu pracy za %2'
            [gender.text('otrzymała','otrzymał'),__CALL.CHOROBA]}
         {<10}
      {ELSE}
         {<5}11){<10}{'%1 wynagrodzenia przewidzianego w art. 92 Kodeksu pracy'
            [gender.text('nie otrzymała','nie otrzymał')]}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {<5}12){<10}{'dni, za które %1 prawa do wynagrodzenia,'
         [gender.text('pracownica nie zachowała','pracownik nie zachował')]}
         {<10'przypadające w okresie od dnia 1 stycznia 2003 r. do dnia 31 grudnia 2003 r.'}
         {<10'zgodnie z art. 92 par. 1(1) Kodeksu pracy obowiązującym w tym okresie'}
         {<10{? +__CALL.ART92P1 || '' || ' brak' ?} }
      {IF: +__CALL.ART92P1}
         { Temp:=proc_exe('sp92p1@zasw',__CALL.PRACOWN,__CALL.DO_DATY,__CALL.OD_DATY); ~~ }
         {FOR: Temp}{DO}
            {<10}{Temp.TEKST}
         {OD}
         { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {IF: +__CALL.WOJSKO}
         {<5}13){<10}{'%1 służbę wojskową'[gender.text('odbywała','odbywał')]}
         { Temp:=proc_exe('spur@zasw',__CALL.PRACOWN,__CALL.DO_DATY,'O',0,__CALL.OD_DATY); ~~ }
         {FOR: Temp}{DO}
            {<10}{Temp.TEKST}
         {OD}
         { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
      {ELSE}
         {<5}13){<10}{'%1 służby wojskowej'[gender.text('nie odbywała','nie odbywał')]}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {IF: +__CALL.SZCZ_WAR}
         {<5}14){<10}{_txt:='%1 pracę w szczególnych warunkach lub w szczególnym charakterze %2'
         [gender.text('wykonywała','wykonywał'),__CALL.SZCZ_WAR];
         STR.split(_txt);
         STR.line(60)}
         {WHILE: +(Temp:=STR.line(60))}{DO}
            {<10}{Temp}
         {OD}
      {ELSE}
         {<5}14){<10}{'%1 pracy w szczególnych warunkach lub w szczególnym charakterze'
            [gender.text('nie wykonywała','nie wykonywał')]}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {IF: +__CALL.URLOPDOD | __CALL.URLNSPG<>'0' | __CALL.URLREHG<>'0'}
         {<5}15){<10}{'%1 dodatkowy urlop albo inne uprawnienia lub świadczenia'
            [gender.text('wykorzystała','wykorzystał')]}
            {<10'przewidziane przepisami prawa pracy:'}
         {IF: __CALL.URLNSPG<>'0'}{<10'urlop dodatkowy w wymiarze %1 godzin, co stanowi %2'
            [__CALL.URLNSPG,__CALL.URLNSP]}{FI}
         {IF: +__CALL.URLOPDOD}{<10}{__CALL.URLOPDOD}{FI}
         {IF: __CALL.URLREHG<>'0'}
            {<10'urlop rehabilitacyjny w wymiarze %1 godzin, co stanowi %2'[__CALL.URLREHG,__CALL.URLREH]}{FI}
      {ELSE}
         {<5}15){<10}{'%1 dodatkowego urlopu albo innych uprawnień'[gender.text('nie wykorzystała','nie wykorzystał')]}
         {<10'lub świadczeń przewidzianych przepisami prawa pracy'}
      {FI}
   {ENTIRE-}
   {ENTIRE}
      {<5}16){<10'okresy nieskładkowe, przypadające w okresie zatrudnienia'}
          {<10'wskazanym w ust. 1, uwzględniane przy ustalaniu'}
          {<10}{  {? +__CALL.OKR_NSKL
                  || 'prawa do emerytury lub renty (łącznie %1):'[__CALL.OKR_NSKL]
                  || 'prawa do emerytury lub renty: brak'
                  ?}}
      {IF: +__CALL.OKR_NSKL}
         { Temp:=proc_exe('spns@zasw',__CALL.PRACOWN,__CALL.DO_DATY,__CALL.OD_DATY); ~~ }
         {FOR: Temp}{DO}
            {<10}{Temp.TEKST}
         {OD}
         { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
      {FI}
   {ENTIRE-}

   {ENTIRE}
      {<1}7.{<5'Informacja o zajęciu wynagrodzenia:'}
      {IF: PAR_SKID.get(230)='T'}
         { Temp:=proc_exe('komornik@zasw',__CALL.OSOBA,__CALL.PRACOWN,__CALL.D_ZATR,__CALL.DO_DATY); ~~ }
         {IF: type_of(Temp)=type_of(SYSLOG) & Temp.first()}
            {FOR: Temp}{DO}
               {<10}{STR.split(Temp.TEKST); STR.line(80)}
               {WHILE: STR.next()}{DO}
                  {<10}{STR.line(80)}
               {OD}
            {OD}
         {ELSE}
            {<10'Oznaczenia komornika: brak'}
            {<10'Numery spraw egzekucyjnych: brak'}
         {FI}
         { {? type_of(Temp)=type_of(SYSLOG) || obj_del(Temp) ?}; ~~ }
      {ELSE}
         {<10}{'Oznaczenia komornika: %1'[{? +__CALL.KOMORNIK || __CALL.KOMORNIK || 'brak' ?}]}
         {<10}{{? +__CALL.NRSPRAWY
            || 'Numery spraw egzekucyjnych: %1'[__CALL.NRSPRAWY]
            || 'Numery spraw egzekucyjnych:  brak'
            ?}}
         {IF: __CALL.POTRKW}{<10'Wysokość potrąconych kwot: %1'[__CALL.POTRKW]}{FI}
      {FI}
   {ENTIRE-}

   {ENTIRE}
      {<1}8.{<5'Informacje uzupełniające'}
      {IF: __CALL.INFUZUP1='' & __CALL.INFUZUP2='' & __CALL.INFUZUP3='' & __CALL.INFUZUP4=''}
         {<5 'brak'}
      {ELSE}
         {<5}{__CALL.INFUZUP1}
         {<5}{__CALL.INFUZUP2}
         {<5}{__CALL.INFUZUP3}
         {<5}{__CALL.INFUZUP4}
      {FI}

   {ENTIRE-}
   {ENTIRE}
      {LINE: 3}
      {SPACE: 'C'}
      {<{charleft()/2}>{charleft()}(45*'─')}
      {FONT: 'W8'}
      {<{charleft/2}>{charleft}'(podpis pracodawcy lub osoby reprezentującej pracodawcę albo'}
      {<{charleft/2}>{charleft}'osoby upoważnionej do składania oświadczeń w imieniu pracodawcy)'}
      {FONT: 'TITLE'}{SPACE: 'A'}
      {LINE: 4}
      {<1>{charleft()} 'POUCZENIE'}
      {LINE: 1}
      {FONT: 'W8'}{SPACE: 'C'}
      {IF: __CALL.ODWOL='7'}
         {Temp:=
            'Pracownik może w ciągu 7 dni od otrzymania świadectwa pracy wystąpić do pracodawcy z wnioskiem o '
            'sprostowanie tego świadectwa. W razie nieuwzględnienia wniosku pracownikowi przysługuje, w ciągu 7 '
            'dni od otrzymania zawiadomienia o odmowie sprostowania świadectwa pracy, prawo wystąpienia z żądaniem '
            'sprostowania świadectwa pracy do Sądu Rejonowego - Sądu Pracy %1 (podstawa prawna - art. 97 par. 2(1) '
            'Kodeksu pracy)'[{? +__CALL.SADPRACY || ' '+__CALL.SADPRACY || '' ?}];
         ~~}
      {ELSE}
         {Temp:=
            'Pracownik może w ciągu %1 dni od otrzymania świadectwa pracy wystąpić z wnioskiem do pracodawcy o '
            'sprostowanie świadectwa pracy. W razie nieuwzględnienia wniosku pracownikowi przysługuje, w ciągu %1 '
            'dni od zawiadomienia o odmowie sprostowania świadectwa pracy, prawo wystąpienia z żądaniem jego '
            'sprostowania do sądu pracy. W przypadku niezawiadomienia przez pracodawcę o odmowie sprostowania '
            'świadectwa pracy, żądanie sprostowania świadectwa pracy wnosi się do sądu pracy. (podstawa prawna - '
            'art. 97 par. 2(1) Kodeksu pracy)'[__CALL.ODWOL];
         ~~}
      {FI}
      {  STR.split(Temp); ~~}
      {JUSTIFY}
      {WHILE: +(Temp:=STR.line(charleft()))}{DO}
         {REP: {? +STR.spl_str
               || '{<1>{charleft()}'
               || '{CENTER}{<1 '
            ?}+'Temp}'
      }{OD}
   {ENTIRE-}
   {PAGE}
{OD}
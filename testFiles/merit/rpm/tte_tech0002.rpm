:!UTF-8
{TITLE:B. Karty technologiczne, w których użyto technologię/półfabrykat}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('PRN','TABWYR','xTKTL','xTMAT');
   exec('tktl_cntx_psh','tech_common');
   NDX1:=TMAT.ndx_tmp('',1,'RKTL',,,'NRK','NRK',);
   PRN:=obj_new(@.CLASS.PRINT,5);
   PRN.s_frame();
   prn1:='PRN';
   VAR.A_KTL:=TKTL.ref();
   tktl:=VAR.A_KTL;
   waslbar:=0;
   TABWYR:=tab_tmp(2,'TECH','STRING[50]','Nrk','WER','STRING[6]','Wersja');
   ndx:=TABWYR.ndx_tmp(,1,'TECH',,,'WER',,);
   dalej:=dalej1:=0;
   color:=karta:=ktm:=wer:=element:='';
   lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
   TMAT.ndx_drop(NDX1);
   &NDX1;
   &tktl; &karta; &ktm; &wer; &element;
   &dalej;
   &dalej1;
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   exec('tktl_cntx_pop','tech_common');
   VAR_DEL.delete('PRN','TABWYR','xTKTL','xTMAT')
}
{
_wydr_naz:='tech0002';
WYDRUKIL.index('WYDRUKI');
WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
{? ~WYDRUKIL.first()
|| WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add();
   WYDRUK.blank()
|| WYDRUK.WYBOR:=WYDRUKIL.SORTUJ;
   WYDRUK.ARCH:=WYDRUKIL.SR
?};
WYDRUK.win_edit('RED5');
~~}
{IF: WYDRUK.edit()}
{ wybor:=WYDRUK.WYBOR;
  arch:=WYDRUK.ARCH;
  WYDRUKIL.SORTUJ:=WYDRUK.WYBOR;
  WYDRUKIL.SR:=WYDRUK.ARCH;
  WYDRUKIL.put(1);
~~}
{ELSE}
{EXIT}
{FI}
{COMMENT}
  Wydruk listy wyrobów, w których użyta jest dana część.
  [MKO] - 2002/01/30 - [7.60] OWwPRO027/5.1
 Wywołanie: Karty technologiczne -> Raporty -> Zestawienia
{COMMENT-}
{COMMENT}--Definicja wiersza wydruku.--------------------{COMMENT-}
{IF: wybor='T'}
{ PRN.add("xTKTL.NRK",40,'Karta technologiczna o symbolu'@);
  PRN.add("xTKTL.WER",8,'w wersji'@,,'#');
  PRN.add("karta",40,'jest użyta w technologii o symbolu'@,,'#');
  PRN.add("wer",8,'w wersji'@,,'#');
  PRN.add("ktm",40,'do produktu o kodzie'@,,'#');
  PAR_WYDR.TITLE:='LISTA KART TECHNOLOGICZNYCH, W KTÓRYCH ZOSTAŁA UŻYTA DANA TECHNOLOGIA'@; ~~}
{ELSE}
{ PRN.add("kod",40,'Półfabrykat o kodzie'@);
  PRN.add("karta",40,'jest użyty w technologii o symbolu'@,,'#');
  PRN.add("wer",8,'w wersji'@,,'#');
  PRN.add("ktm",40,'do produktu o kodzie'@,,'#');
  PRN.add("element",13,'element grupy'@,,'#');
  PAR_WYDR.TITLE:='LISTA KART TECHNOLOGICZNYCH, W KTÓRYCH ZOSTAŁ UŻYTY DANY PÓŁFABRYKAT'@; ~~}
{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} {? arch='N' || 'Tylko technologie aktywne'@ || 'Technologie aktywne i archiwalne'@ ?}}
{FONT: 'T8G'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; all:=1; ~~}
{COMMENT}
Gdy wydruk według technologii
{COMMENT-}
{IF: wybor='T'}
   {  {? arch='T' || _where:=' where TKTL.TORW=\'T\''
                  || _where:=' where TKTL.TORW=\'T\' and TKTL.ARCH=\'N\''
      ?};
      xTKTL:=sql('
         select
            TKTL.REFERENCE as REF,
            TKTL.NRK,
            TKTL.WER
         from
            @TKTL
         :_a
         order by NRK, WER
      ',_where);
      {? xTKTL.first() || dalej:=1 ?};
      ~~
   }
   {WHILE: dalej}
   {DO}
      {  VAR_DEL.delete('xTMAT');
         {? arch='T' || _where:='where TMAT.RKTL=\''+xTKTL.REF+'\' and TKTL.TORW=\'T\''
                     || _where:='where TMAT.RKTL=\''+xTKTL.REF+'\' and TKTL.TORW=\'T\' and TKTL.ARCH=\'N\''
         ?};
         xTMAT:=sql('select TMAT.REFERENCE as REF from @TMAT join @TKTL using(TMAT.NRK,TKTL.REFERENCE) :_a',_where);
         ~~
      }
      {IF: xTMAT.first()}
         { dalej1:=1; karta:=wer:=ktm:=''; ~~}
      {FI}
      {WHILE: dalej1}
      {DO}
         {  exec('tktl_use','tech_common',(8+xTMAT.REF)+3);
            {? TMAT.seek(xTMAT.REF)
            || TABWYR.index(ndx);
               TABWYR.prefix(TMAT.NRK().NRK,TMAT.NRK().WER,);
               {? ~TABWYR.first()
               || karta+=form(TMAT.NRK().NRK,40)+'#';
                  wer+=form(TMAT.NRK().WER,8)+'#';
                  ktm+=form(TMAT.NRK().KTM().KTM,40)+'#';
                  TABWYR.TECH:=TMAT.NRK().NRK;
                  TABWYR.WER:=TMAT.NRK().WER;
                  TABWYR.add()
               ?}
            ?};
            ~~
         }
         {dalej1:=xTMAT.next(); ~~}
      {OD}
      {IF: karta<>''}
      {SUBSTITUTE: 'LINIA0'}
      {FI}
      {dalej:=xTKTL.next(); karta:=ktm:=wer:=''; TABWYR.erase(); ~~}
   {OD}
{ELSE}
{COMMENT}
Gdy wydruk wg wyrobu
{COMMENT-}
   {FOR: M}
   {INDEX: 'R_PROD'}
   {PREFIX: 'T','T','T'}
   {DO}
      {kod:=M.KTM; ~~}
      {  VAR_DEL.delete('xTMAT');
         xTMAT:=sql("
            select
               TMAT.REFERENCE as REF,
               'N' as ELEMENT
            from
               @TMAT
               join @TKTL using(TMAT.NRK,TKTL.REFERENCE)
            where
               TMAT.PT=:_a
               and TKTL.TORW='T'
         "+{? arch='T' || "" || " and TKTL.ARCH='N' " ?}+"
            union all
            select
               TMAT.REFERENCE as REF,
               'T' as ELEMENT
            from
               @TMAT
               join @TKTL using(TMAT.NRK,TKTL.REFERENCE)
               join TTG using(TMAT.TGRP,TTG.REFERENCE)
               join TTGP using(TTGP.GR,TTG.REFERENCE)
            where
               TTGP.PT=:_a
               and TKTL.TORW='T'
         "+{? arch='T' || "" || " and TKTL.ARCH='N' " ?}
         ,M.ref());
         ~~
      }
      {IF: xTMAT.first()}
         {dalej1:=1; ~~}
      {FI}
      {WHILE: dalej1}
      {DO}
         {  exec('tktl_use','tech_common',(8+xTMAT.REF)+3);
            {? TMAT.seek(xTMAT.REF)
            || TABWYR.index(ndx);
               TABWYR.prefix(TMAT.NRK().NRK,TMAT.NRK().WER,);
               {? ~TABWYR.first()
               || karta+=form(TMAT.NRK().NRK,40)+'#';
                  wer+=form(TMAT.NRK().WER,8)+'#';
                  ktm+=form(TMAT.NRK().KTM().KTM,40)+'#';
                  element+=form({? xTMAT.ELEMENT='T' || 'Tak'@ || 'Nie'@ ?},13)+'#';
                  TABWYR.TECH:=TMAT.NRK().NRK;
                  TABWYR.WER:=TMAT.NRK().WER;
                  TABWYR.add()
               ?}
            ?};
            ~~
         }
         {dalej1:=xTMAT.next(); ~~}
      {OD}
      {IF: karta<>''}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
      {karta:=wer:=ktm:=element:=''; TABWYR.erase(); ~~}
   {OD}
{FI}

:!UTF-8
{TITLE:D.  Suma obrotów kont syntetycznych w rejestrze}
{PRINTER: exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN: kolu:=obj_new(20); p:=obj_new(20); kl:=0; il:=0; odd:=kontotxt:=wal:=mdc:='';
        rejkod:=''; sumawn:=0; sumama:=0; druk:=kurs:=0;
        saldown:=0; saldoma:=0; summw:=0; summm:=0; sumswn:=0; sumsma:=lm:=0;
        exec('ini_wal','konto');
        ROZNE.win_edit('KS_3'); pref1:=pref2:='';ROZNE.blank();
        ROK_F.clear(); SSTALE.AR();
        KS.win_sel('SLO_TEN');
        ROZNE.PAR1:=3; ROZNE.UT_DOK:=1; param:=''; dept:=OPERATOR.DEPT
      }
{END: VAR_DEL.delete('kolu','p','kl','il','rejkod','druk','pref1','pref2',
                     'param','odd','kontotxt','wal',
                     'sumawn','sumama','saldown','saldoma','summw','summm',
                     'sumswn','sumsma','mdc','lm','kurs');
      OPERATOR.DEPT:=dept; &dept
     }
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: ks_syntr.rpm
Utworzony:   ????-??-??
Modyfikacje: 2001-06-26 [AMK] Artur Makos
==================================================
Zawartosc: Wydruk zestawienia obrotow i sald kont syntetycznych w rejestrze
{COMMENT-}
{ kl:=6; kolu[1]:=6; kolu[2]:=60; kolu[3]:=20; kolu[4]:=20; kolu[5]:=20; kolu[6]:=20;
  il:=kolu[1]+kolu[2]+kolu[3]+kolu[4]+kolu[5]+kolu[6]+kl+1;
  p[1]:=2+kolu[1]; {!_a:=2 ..kl |! p[_a]:=1+p[_a-1]+kolu[_a] !};
  wal:='Waluta aktywna:  %1 - %2'@[SSTALE.WAL().TR,SSTALE.WAL().KOD];
  odd:=
       {? WYDRUKI.ODD=0
       || OPERATOR.DEPT:=null; 'Jednostka księgowa: wszystkie jednostki księgowe'@
       || 'Jednostka księgowa: %1'@[WYDRUKI.ODD().OD]
       ?};
~~}
{EXIT: ~ROZNE.edit("_a:=__CHK.record(ROZNE,,'KODREJ');
                    {? _a='' & ROZNE.PAR4=4
                    ||  _a:=__CHK.record(ROZNE,,'KS_OD','KS_DO');
                       {? _a=''
                        || {? ROZNE.KS_OD().SYM>ROZNE.KS_DO().SYM
                           || FUN.info('Podaj poprawnie zakres kont.'@); _a:='KS_OD'
                           ?}
                       ?}
                    ?}; _a")}
{   rejkod:=ROZNE.KODREJ;
    {? ROZNE.PAR1=1 || kontotxt:='Konta: pozabilansowe'@
    |? ROZNE.PAR1=2 || kontotxt:='Konta: bilansowe'@
    |? ROZNE.PAR1=3 || kontotxt:='Konta: wszystkie'@
    |? ROZNE.PAR1=4 || kontotxt:='Konta: wynikowe'@
    || kontotxt:='Konta: wszystkie bez pozabilansowych'@
    ?};
    kurs:=exec('kurs','konto',UNPAR.PSLO().KOD,UNPAR.P25);~~
}
{COMMENT}------DEFINICJE "MACRO", "HEADER" I "FOOTER"------{COMMENT-}
{MACRO: 'LINIA1'}
┌{kolu[1]*'─'}┬{kolu[2]*'─'}┬{(kolu[3]+kolu[4]+1)*'─'}┬{(kolu[5]+kolu[6]+1)*'─'}┐
{MACRO-}
{MACRO: 'LINIA2'}
├{kolu[1]*'─'}┼{kolu[2]*'─'}┼{kolu[3]*'─'}┼{kolu[4]*'─'}┼{kolu[5]*'─'}┼{kolu[6]*'─'}┤
{MACRO-}
{MACRO: 'LINIA3'}
└{kolu[1]*'─'}┴{kolu[2]*'─'}┴{kolu[3]*'─'}┴{kolu[4]*'─'}┴{kolu[5]*'─'}┴{kolu[6]*'─'}┘
{MACRO-}
{MACRO: 'LINIA4'}
├{kolu[1]*'─'}┴{kolu[2]*'─'}┼{kolu[3]*'─'}┼{kolu[4]*'─'}┼{kolu[5]*'─'}┼{kolu[6]*'─'}┤
{MACRO-}
{MACRO: 'LINIA5'}
└{kolu[1]*'─'}─{kolu[2]*'─'}┴{kolu[3]*'─'}┴{kolu[4]*'─'}┴{kolu[5]*'─'}┴{kolu[6]*'─'}┘
{MACRO-}
{ {? ROZNE.PAR4=2
   || WYDRUKIN.DLKON:=35;  WYDRUKI.MASKA:=ROZNE.MASKA;
      WYDRUKI.MASKA:=exec('usostsep','konto',WYDRUKI.MASKA);
     {? +WYDRUKI.MASKA < WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?}
   |? ROZNE.PAR4=0
   || WYDRUKI.MASKA:=ROZNE.PREFIX
   || WYDRUKI.MASKA:=''
   ?};~~
 }
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ZESTAWIENIE OBROTÓW I SALD KONT SYNTETYCZNYCH ZA OKRES %1 %2 REJESTR %3'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ,rejkod];~~}
{LINE: 1}

{FONT: 'HEAD'}{SPACE: 'B'}{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{IF: page=1}
{ KS.cntx_psh();
   param:={? ROZNE.PAR4=2
          || 'Dla kont o masce: %1'@[ROZNE.MASKA]
          |? ROZNE.PAR4=0 & form(ROZNE.PREFIX)=''
          || 'Wydruk dla wszystkich kont'@
          |? ROZNE.PAR4=0
          || 'Wydruk dla kont : %1'@[ROZNE.PREFIX+(35-+ROZNE.PREFIX)*'?']
          |? ROZNE.PAR4=3
          || 'Wydruk wybranych kont'@
          |? ROZNE.PAR4=4
          || 'Wydruk dla zakresu kont od: %1 do: %2'@[ROZNE.KS_OD().SYM,ROZNE.KS_DO().SYM]
          ?}; KS.cntx_pop();~~}
{IF: rep_export()}
{'Parametry wydruku:'@}
{{? ROZNE.UT_DOK
 || 'Drukowanie pozycji z zerowymi sumami: tak'@
 || 'Drukowanie pozycji z zerowymi sumami: nie'@
 ?}
}
{wal}
{{? SSTALE.AO().ZAM='T'|| 'Rodzaj okresu: zamknięty'@ ||'Rodzaj okresu: otwarty'@ ?}}
{param}
{{? UNPAR.PSLO || 'Waluta wydruku:  %1 - %2'@[UNPAR.PSLO().TR,SLO.KOD] || 'Waluta wydruku:  %1 - %2'@[SSTALE.WAL().TR,SLO.KOD] ?}}
{{? UNPAR.P25<>1 | (UNPAR.PSLO<>null & UNPAR.PSLO<>SSTALE.WAL) || 'Kurs: %1'@[$UNPAR.P25] || ~~ ?}}
{odd}
{kontotxt}
{ELSE}
{<3 'Parametry wydruku:'@}
{<6
   {? ROZNE.UT_DOK
      || 'Drukowanie pozycji z zerowymi sumami: tak'@
      || 'Drukowanie pozycji z zerowymi sumami: nie'@
   ?}
}
 {<66 wal}{<116 {? SSTALE.AO().ZAM='T'|| 'Rodzaj okresu: zamknięty'@ ||'Rodzaj okresu: otwarty'@ ?}}
{<6 param}{<66 {? UNPAR.PSLO || 'Waluta wydruku:  %1 - %2'@[UNPAR.PSLO().TR,SLO.KOD] || 'Waluta wydruku:  %1 - %2'@[SSTALE.WAL().TR,SLO.KOD] ?}}{<116 {? UNPAR.P25<>1 | (UNPAR.PSLO<>null & UNPAR.PSLO<>SSTALE.WAL) || 'Kurs: %1'@[$UNPAR.P25] || '' ?}}
{<6 odd}{<66 kontotxt}
{FI}
{FI}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: rep_export()}
{TABLE}
{TR}{TH}{TH}{TH:2}Obroty{TH:2}Saldo
{TR}{TH}Konto{TH}Nazwa{TH}Winien{TH}Ma{TH}Winien{TH}Ma
{ELSE}
{SUBSTITUTE: 'LINIA1'}
│{<{p[1]}'│'}{<{p[2]}'│'}{<{p[2]}>{p[4]}'Obroty'@}{<{p[4]}'│'}{<{p[4]}>{p[6]}'Saldo'}{<{p[6]}'│'}
│{<1>{p[1]}'Konto'@}{<{p[1]}'│'}{<{p[1]}>{p[2]}'Nazwa'@}{<{p[2]}'├'}{kolu[3]*'─'}┬{kolu[4]*'─'}{<{p[4]}'┼'}{kolu[5]*'─'}┬{kolu[6]*'─'}{<{p[6]}'┤'}
│{<{p[1]}'│'}{<{p[2]}'│'}{<{p[2]}>{p[3]}'Winien'@}{<{p[3]}'│'}{<{p[3]}>{p[4]}'Ma'}{<{p[4]}'│'}{<{p[4]}>{p[5]}'Winien'@}{<{p[5]}'│'}{<{p[5]}>{p[6]}'Ma'@}{<{p[6]}'│'}
{SUBSTITUTE: 'LINIA2'}{FONT: 'Q'}{SPACE: 'C'}
{FI}
{HEADER-}
{COMMENT}----------------------------FOOTER--------------------------{COMMENT-}
{FOOTER: 0}
{FONT:'T8G'}{SPACE:'C'}{SUBSTITUTE: 'LINIA3'}
{SUBSTITUTE: 'FOOT'}
{~~}
{~~}
{FOOTER-}
{MACRO: 'SRODEK'}
{FONT:'T8G'}{SPACE:'C'}
 {IF: (ROZNE.PAR1=1 & -(1+KS.TYP)='p') | (ROZNE.PAR1=2 & -(2+KS.TYP)='bl') | (ROZNE.PAR1=3) | (ROZNE.PAR1=4 & -KS.TYP='bw')
      | (ROZNE.PAR1=5 & -(1+KS.TYP)='b')}
  { sumawn:=0; sumama:=0; saldown:=0; saldoma:=0; druk:=0;~~}
  {FOR: 'AN'}
  {INDEX: 'WALSYM'}
  {PREFIX: SSTALE.WAL,pref2}
  {DO}
    { WYDRUKIN.DLKON:=35; ~~}
    {IF: (ROZNE.PAR4=2 & exec('mask','konto',AN.SYM)) | ROZNE.PAR4<>2}
    {IF: WYDRUKI.ODD}
        {IF: SSTALE.WAL<>FINFO.NAROD}
           {FOR: POZ}
           {INDEX: 'OKWAL_O'}
           {PREFIX: 'T','T',SSTALE.WAL,WYDRUKI.ODD,AN.SYM}
           {DO}
           {IF: POZ.DOK().REJ().KOD=rejkod & REJ.ODD=WYDRUKI.ODD}
             { druk:=1; {? -POZ.STR='wn' || sumawn+=POZ.SUMW ||  sumama+=POZ.SUMW ?};~~}
           {FI}
           {OD}
         {ELSE}
            {FOR: POZ}
            {INDEX: 'OKWAL_O1'}
            {PREFIX: 'T','T',WYDRUKI.ODD,AN.SYM}
            {DO}
            {IF: POZ.DOK().REJ().KOD=rejkod & REJ.ODD=WYDRUKI.ODD}
               { druk:=1; {? -POZ.STR='wn' || sumawn+=POZ.SUM ||  sumama+=POZ.SUM ?};~~}
            {FI}
            {OD}
          {FI}
    {ELSE}
           {IF: SSTALE.WAL<>FINFO.NAROD}
           {FOR: POZ}
           {INDEX: 'OKWAL'}
           {PREFIX: 'T','T',SSTALE.WAL,AN.SYM}
           {DO}
           {IF: POZ.DOK().REJ().KOD=rejkod}
             { druk:=1; {? -POZ.STR='wn' || sumawn+=POZ.SUMW ||  sumama+=POZ.SUMW ?};~~}
           {FI}
           {OD}
         {ELSE}
            {FOR: POZ}
            {INDEX: 'OKWAL1'}
            {PREFIX: 'T','T',AN.SYM}
            {DO}
            {IF: POZ.DOK().REJ().KOD=rejkod}
               { druk:=1; {? -POZ.STR='wn' || sumawn+=POZ.SUM ||  sumama+=POZ.SUM ?};~~}
            {FI}
            {OD}
          {FI}
    {FI}
   {FI}
  {OD}  {'Do AN';~~}{WYDRUKIN.DLKON:=ROK_F.SYNT; ~~}
  {IF: (ROZNE.UT_DOK | (~ROZNE.UT_DOK & druk=1)) &
         ((ROZNE.PAR4=2 & exec('mask','konto',KS.SYM)) | ROZNE.PAR4<>2)}
  { sumawn:=(sumawn/kurs)$2; sumama:=(sumama/kurs)$2;
    saldown:=F.SWn(sumawn,sumama); saldoma:=F.SMa(sumawn,sumama);
    summw+=sumawn; summm+=sumama; sumswn+=saldown; sumsma+=saldoma;~~}
   {IF: rep_export()}
      {TR}{TD}{KS.SYM}{TD}{KS.NAZ}{TD:;'right'}{form(sumawn,,2,' ,')}{TD:;'right'}{form(sumama,,2,' ,')}{TD:;'right'}{form(saldown,,2,' ,')}
      {TD:;'right'}{form(saldoma,,2,' ,')}
   {ELSE}
      {<1'│'}{KS.SYM}{<{p[1]}'│'}{KS.NAZ}{<{p[2]}'│'}{>{p[3]-2}(form(sumawn,,2,' ,'))}{<{p[3]}'│'}{>{p[4]-2}(form(sumama,,2,' ,'))}{<{p[4]}'│'}{>{p[5]-2}(form(saldown,,2,' ,'))
}{<{p[5]}'│'}{>{p[6]-2}(form(saldoma,,2,' ,'))}{<{p[6]}'│'}
   {FI}
  {FI} {WYDRUKIN.DLKON:=35;~~}
 {FI}  {'Do ROZNE.PAR1';~~}
{MACRO-}
{COMMENT}----------------------CIAŁO  WYDRUKU--------------------------{COMMENT-}
{IF: ROZNE.PAR4<>0}{ pref1:='';~~}{ELSE}{ pref1:=BILANSP.ASYNT+WYDRUKI.MASKA; ~~}{FI}
{IF: ROZNE.PAR4=0 | ROZNE.PAR4=2}
{FOR: 'KS'}
{INDEX: 'SYM'}
{PREFIX: SSTALE.AO().ROK,pref1}
{DO}
{IF: ROZNE.PAR4<>0}{ pref2:=KS.SYM;~~}{ELSE}{ pref2:=KS.SYM+(BILANSP.ASYNT-WYDRUKI.MASKA);~~}{FI}
{SUBSTITUTE: 'SRODEK'}
{OD}
{ELIF: ROZNE.PAR4=3}
{FOR: 'KS'}
{INDEX: 'SYM'}
{PREFIX: SSTALE.AO().ROK,pref1}
{SELECT}
{DO}
{ pref2:=KS.SYM;~~}
{SUBSTITUTE: 'SRODEK'}
{OD}
{ELIF: ROZNE.PAR4=4}
{FOR: 'KS'}
{INDEX: 'SYM'}
{FROM: SSTALE.AO().ROK,ROZNE.KS_OD().SYM}
{TO: SSTALE.AO().ROK,ROZNE.KS_DO().SYM}
{DO}
{ pref2:=KS.SYM;~~}
{SUBSTITUTE: 'SRODEK'}
{OD}
{FI}
{COMMENT}------------------------------FOOTER-LAST-----------------------{COMMENT-}
{FOOTER}
{SUBSTITUTE: 'FOOT'}
{~~}
{FOOTER-}
{DIRECT}
{IF: rep_export()}
{TR}{TD:2}Suma{TD:;'right'}{form(summw,,2,' ,')}{TD:;'right'}{form(summm,,2,' ,')}{TD:;'right'}{form(sumswn,,2,' ,')}{TD:;'right'}{form(sumsma,,2,' ,')}
{TABLE-}
{ELSE}
{SUBSTITUTE: 'LINIA4'}
   {<1'│'}       Suma{<{p[2]}'│'}{>{p[3]-2}(form(summw,,2,' ,'))}{<{p[3]}'│'}{>{p[4]-2}(form(summm,,2,' ,'))}{<{p[4]}'│'}{>{p[5]-2}(form(sumswn,,2,' ,'))}{<{p[5]}'│'}{>{p[6]-2
}(form(sumsma,,2,' ,'))}{<{p[6]}'│'}
{SUBSTITUTE: 'LINIA5'}
{<1>{il} 'KONIEC WYDRUKU'@}
{FI}
{DIRECT-}
:!UTF-8
{TITLE:A. Zestawienie kart przekazania odpadów}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','PRNS','w','s','tryb','kkh');
   PRN:=obj_new(@.CLASS.PRINT,20); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,20); PRNS.s_frame();
   tryb:=rep_export();

   w:=obj_new(20);
   s:=obj_new(20);

   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   ob:={? VAR.A_TABKPO='OKPO'
       || 'T'
       || 'N'
       ?};

   'parametry z tabeli WYDRUKI';
   _wydr_naz:='luo_kp_001';
   WYDRUKIL.win_edit('KPO_001');
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz,);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.CHKBOX01:=ST.AR;
      WYDRUKIL.CHKBOX02:=ST.AM;
      WYDRUKIL.add()
   ?};
~~}
{END:
   VAR_DEL.delete('PRN','PRNS','w','s','tryb','kkh');
   exec('kpo_open','open_tab',ST.ODDZ,$ST.AR+2);
   &i;
   &color; &prn1; &prn2;&ob;
   &lm; &ll; &vp; &lmt; &rsep
;~~}
{EXIT: ~WYDRUKIL.edit()}
{  WYDRUKIL.put(1);
   {? tryb<>1
   || PRN.add("w[1]",5,'Lp.',1)
   ?};
   PRN.add("w[2]",16,'Karta'@);
   PRN.add("w[3]",10,'Kod odpadu'@);
   PRN.add("w[4]",30,'Nazwa odpadu'@);
   PRN.add("w[5]",30,'Przekazujący'@);
   PRN.add("w[6]",30,'Przyjmujący'@);
   PRN.add("w[7]",30,'Transportujący'@);
   PRN.add("w[8]",30,'Składowisko'@);
   PRN.add("form(w[9],,3)",12,'Ilość'@,1);
   PRN.add("w[10]",6,'jm'@);

   PRNS.add("s[1]",202);
   PRNS.add("form(s[2],,3)",12,'Ilość'@,1);
   PRNS.add("s[3]",6,'jm'@)
;~~}
{COMMENT}
color, prn1, lm, ll, vp, lmt, rsep - zmienne pomocnicze do wydrukow graficzn.
{COMMENT-}
{  PAR_WYDR.TITLE:='Karty przekazania odpadów'@;
   prn1:='PRN'; prn2:='PRNS'; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Okres: %1/%2'@[$WYDRUKIL.CHKBOX02,$WYDRUKIL.CHKBOX01]}
{ kpo_r:=
  {? WYDRUKIL.KPO_R='W'
  || 'Wydanie'@
  |? WYDRUKIL.KPO_R='G'
  || 'Wydanie z gminnego punktu odbioru odpadów'@
  || 'Przyjęcie'@
  ?};
~~}
{<{lmt+1}}{'Rodzaj kart: %1'@[kpo_r]}{&kpo_r;~~}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'T8G'}{SPACE: 'C'}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: tryb<>1}
{LINE: 1}
{ELSE}
{SUBSTITUTE: 'LINIAF'}
{FI}
{FOOTER-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; w[1]:=0;
~~}
{s[1]:='Razem';s[2]:=0;s[3]:=exec('get','#params',100171,2);~~}
{exec('kpo_open','open_tab',ST.ODDZ,$WYDRUKIL.CHKBOX01+2);~~}
{FOR: 'KPO'}
{INDEX: 'R'}
{PREFIX: 'K',ob,ST.ODDZ,WYDRUKIL.KPO_R,WYDRUKIL.CHKBOX02}
{DO}
   {
    w[1]+=1;
    w[2]:=KPO.SYM;
    w[3]:=KPO.ODP().KOD;
    w[4]:=KPO.ODP().NAZ;
    w[5]:=KPO.KH().NAZ;
    w[6]:=KPO.KH_2().NAZ;
    KPOT.index('KPO');
    KPOT.prefix(KPO.ref());
    {? KPOT.first()
    || w[7]:=KPOT.KH().NAZ
    || w[7]:=''
    ?};
    w[8]:=KPO.WYS().NAZ;
    w[9]:=KPO.IL;
    w[10]:=exec('get','#params',100171,2);
    s[2]+=KPO.IL;
    ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: tryb<>1}
{SUBSTITUTE: 'LINIA02'}
{prn1:='PRNS';prn2:='PRN'; vp:=0; ~~}
{REP: _f:=prn1+'.flbar(lmt)'; ($(_f))()}
{FI}
{PAGE}
:!UTF-8
{TITLE:Lista osób mających otwarte umowy - zlecenia}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','__param','__edit');
   tresc:='zleosoby';
   PAR_WYDR.TITLE:='Lista osób mających otwarte umowy - zlecenia'@;
   P.cntx_psh();
   par:=obj_new(8);
   par[8]:=0;
   _upr:=exec('uprawnieniawrap','pkd','Umowach cywilnoprawnych'@,'PPL_ZLC_PUZC','PPL_ZLC_RUZC');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'TYP','INTEGER','Wybór sposobu kontroli'@,
         'DATA','DATE','Data kontroli'
      );
      __param.DATA:=date();
      __param.add();

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_uz_ulrhlist');
      __param.win_esep(__edit,'Sposób przygotowania wydruku '@);
      __param.win_efld(__edit,,'TYP',,,,,,'Wybór sposobu kontroli'@,,,
         'radio-buttons',,'wg Dat '@,"0",'wg Realizacji '@,"1"
      );
      __param.win_efld(__edit,,'DATA',,,11,,,'Data kontroli'@);
      __param.efld_opt(__edit,'mark=1',,'DATA');
      __param.win_edit(__edit);
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);

      {? par[8]:=__param.edit(
            "  {? __param.DATA=#0
               || FUN.emsg('Data musi być wypełniona.'@);
                  'DATA'
               || 1
               ?}
            "
         )
      || par[7]:=__param.TYP;
         par[3]:=__param.DATA;
         par[1]:=obj_new(@.CLASS.PRINT,7);
         {? ~rep_export()
         || par[1].add("{? par[6]<>P.ref() || par[6]:=P.ref() ; Lp+=1 || {? par[2] || Lp || '' ?} ?}",3,'Lp.'@,1)
         ?};
         par[1].add("{? par[2] || P.OSOBA().NAZWISKO || '' ?}",30,'Nazwisko'@);
         par[1].add("{? par[2] || {? ~rep_export() || par[2]:=0 ?}; OSOBA.PIERWSZE || '' ?}",20,'Imię'@);
         par[1].add(
            "  {? ZC.TYP='N'
               || 'Ustalona kwota'@
               |? ZC.TYP='M'
               || 'Miesięczne'@
               |? ZC.TYP='D'
               || 'Dzienne'@
               || 'Godzinowe'@
               ?}
            ",15,'Rozliczenie'@);
         par[1].add("ZC.RU().O",48,'Rodzaj umowy'@);
         par[1].add("$ZC.DU",10,'Data od'@,1);
         par[1].add("$ZC.DW",10,'Data do'@,1);
         par[1].s_frame();

         prn1:='par[1]';
         par[6]:=null();
         UZZ.JESTUM:=vp:=par[2]:=par[5]:=b_rat:=h_rat:=1;
         VAR.P:=lmt:=rsep:=Lp:=0;

         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','__param','__edit')
}
{COMMENT}OLD: zleposoby.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: oddzialz.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par[8]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 ||h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[5]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Data kontroli: '@}{par[3]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8'}
{SPACE: 'B'}
{SUBSTITUTE: 'LINIAF'}
{  par[2]:=1; ~~ }
{FOOTER-}
{IF: ~rep_export()}
{SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{FONT: 'W10'}
{SPACE: 'B'}
{par[2]:=1;~~}
{FOR: ZC}{INDEX: 'ZLECPRZ'}{PREFIX: P.ref()}{DO}
   {IF: ~par[7]}
      {IF: ZC.DU<=par[3] & ZC.DW>=par[3]}
         {SUBSTITUTE: 'LINIA0'}
      {FI}
   {ELSE}
      {IF: ZC.TYP='N'}
         {  {? ZC.CZYWAL='T'
            || exec('wal_real','zlec_rh')
            || exec('rh_sumuj','zlec_rh')
            ?}; ~~
         }
         {IF: UZZ.ZR<>ZC.KU}
            {SUBSTITUTE: 'LINIA0'}
         {FI}
      {ELIF: ZC.TYP='G'}
         {IF: ZC.GODZ=0 & ZC.DU<=par[3] & ZC.DW>=par[3]}
            {SUBSTITUTE: 'LINIA0'}
         {ELIF: ZC.GODZ<>0 & exec('godzreal','zlec_rh',54)<ZC.GODZ}
            {SUBSTITUTE: 'LINIA0'}
         {FI}
      {ELIF: ZC.TYP='D'}
         {IF: ZC.DNI=0 & ZC.DU<=par[3] & ZC.DW>=par[3]}
            {SUBSTITUTE: 'LINIA0'}
         {ELIF: ZC.DNI<>0 & exec('godzreal','zlec_rh',52)<ZC.DNI}
            {SUBSTITUTE: 'LINIA0'}
         {FI}
      {ELSE}
         {IF: ZC.DU<=par[3] & ZC.DW>=par[3]}
            {SUBSTITUTE: 'LINIA0'}
         {FI}
      {FI}
   {FI}
{OD}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O ? Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
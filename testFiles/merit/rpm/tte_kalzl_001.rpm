:!UTF-8
{TITLE:1. Bieżąca kalkulacja zlecenia}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   dalej:=0;
   VAR_DEL.delete('PRN','PRNMAT','PRNZTO','PRNZST','PRNZUS','PRNZTL','TABEDIT');
   PRN:=obj_new(@.CLASS.PRINT,3); PRN.s_frame();
   PRNMAT:=obj_new(@.CLASS.PRINT,5); PRNMAT.s_frame();
   PRNZTO:=obj_new(@.CLASS.PRINT,3); PRNZTO.s_frame();
   PRNZST:=obj_new(@.CLASS.PRINT,3); PRNZST.s_frame();
   PRNZUS:=obj_new(@.CLASS.PRINT,3); PRNZUS.s_frame();
   PRNZTL:=obj_new(@.CLASS.PRINT,4); PRNZTL.s_frame();
   prn1:='PRN';
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   pkalmat:=pkaloper:=pkalst:=pkalusl:=pkalnpu:=1;
   ndx_tmp:=ANZP.ndx_tmp(,1,'NRA',,0,'RUBR','PRINTED',0)
}
{END:
   &dalej;
   VAR_DEL.delete('PRN','PRNMAT','PRNZTO','PRNZST','PRNZUS','PRNZTL','TABEDIT');
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   &pkalmat; &pkaloper; &pkalst; &pkalusl; &pkalnpu;
   ANZP.ndx_drop(ndx_tmp)
}
  {TABEDIT:=tab_tmp(
     ,'KALMAT','STRING[1]','Cennik pobranych surowców'@
     ,'KALOPER','STRING[1]','Lista stawek operacji'@
     ,'KALST','STRING[1]','Lista kosztów stanowisk'@
     ,'KALUSL','STRING[1]','Stawki usług'@
     ,'KALNPU','STRING[1]','Ceny N-P-U'@
   );
   TABEDIT.KALMAT:=TABEDIT.KALOPER:=TABEDIT.KALST:=TABEDIT.KALUSL:=TABEDIT.KALNPU:='T';
   _red:=TABEDIT.mk_edit('Wydruk kartotek stowarzyszonych'@,0,'tabedit1');
   TABEDIT.win_efld(_red,,'KALMAT',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALOPER',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALST',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALUSL',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_efld(_red,,'KALNPU',,,,,,,,,'check-box',,"'T'","'N'");
   TABEDIT.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Zapisz'@],"'key:F2'");
   TABEDIT.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Anuluj'@],"'key:Esc'");
   TABEDIT.win_edit(_red);
   ~~}
   {IF: TABEDIT.edit()}
    { pkalmat:=(-TABEDIT.KALMAT='t');
      pkaloper:=(-TABEDIT.KALOPER='t');
      pkalst:=(-TABEDIT.KALST='t');
      pkalusl:=(-TABEDIT.KALUSL='t');
      pkalnpu:=(-TABEDIT.KALNPU='t'); ~~}
   {ELSE}
      {EXIT}
   {FI}
{COMMENT}
  Wydruk kalkulacji zlecenia.
  [MKO] - 2002/04/11 - [7.60] OWwPR032_5.1
  [TS] [8.70] Stawki uslug i kosztow NPU
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Koszty -> Kalkulacja wstępna ->
     -> Drukuj -> 1. Wydruk bieżącej kalkulacji
{COMMENT-}
{COMMENT}---Definicja wiersza wydruku pozycji analizy zlecenia ANZP.{COMMENT-}
{  PRN.add("form(ANZP.RUBR().NR,,0,'9')",8,'Nr rubr.'@,1);
   PRN.add("ANZP.RUBR().OPIS",40,'Opis rubryki'@);
   PRN.add("form(ANZP.WAR,,2)",15,'Wartość'@,1);
   PRNMAT.add("ANZMAT.T().KTM",20,'Kod surowca'@);
   PRNMAT.add("ANZMAT.T().N",20,'Nazwa surowca'@);
   PRNMAT.add("form(ANZMAT.PRICE,,2)",15,'Cena'@,1);
   PRNMAT.add("{? ANZMAT.POCH='H' || 'Wprowadzone ręcznie'@
            |? ANZMAT.POCH='S' || 'Stany magazynowe'@
            |? ANZMAT.POCH='K' || 'Kalkulacja półfabrykatu'@
            |? ANZMAT.POCH='T' || 'Cennik tow. produkcyjnych'@ || '' ?}",13,'Pochodzenie'@);
   PRNMAT.add("{? ANZMAT.TKTL<>null||ANZMAT.TKTL().NRK+' '+ANZMAT.TKTL().WER||''?}",20,'Technologia'@);
   PRNZTO.add("ANZTTO.TTOPER().KOD",10,'Kod operacji'@);
   PRNZTO.add("ANZTTO.TTOPER().NA",30,'Nazwa operacji'@);
   PRNZTO.add("form(ANZTTO.STAWKA,,2)",15,'Stawka'@,1);
   PRNZST.add("ANZWRK.PLACE().KOD",10,'Kod operacji'@);
   PRNZST.add("ANZWRK.PLACE().NA",60,'Nazwa operacji'@);
   PRNZST.add("form(ANZWRK.KH,,2)",15,'Koszt godziny'@,1);
   PRNZUS.add("ANZTOU.TTOUT().KOD",10,'Kod operacji'@);
   PRNZUS.add("ANZTOU.TTOUT().NA",50,'Nazwa operacji'@);
   PRNZUS.add("form(ANZTOU.CENA,,2)",15,'Cena'@,1);
   PRNZTL.add("ANZTLS.M().MGR().KOD",6,'Rodzaj'@);
   PRNZTL.add("ANZTLS.M().KTM",10,'Symbol narzędzia'@);
   PRNZTL.add("ANZTLS.M().N",40,'Nazwa narzędzia'@);
   PRNZTL.add("form(ANZTLS.KOSZT,,2)",15,'Koszt'@,1);
   PAR_WYDR.TITLE:='KALKULACJA ZLECENIA'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Nr kalkulacji:'@} {<{lmt+15} form(ANZH.NRA)}
{<{lmt+1} 'Zlecenie:'@}   {<{lmt+15} ANZH.ZLEC().SYM}
{<{lmt+1} 'Wariant:'@}    {<{lmt+15} ANZH.OPC().NA}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{"---Drukuj pozycje kalkulacji karty technologicznej.                     "; ~~}
{ ANZP.index('NRARUB'); ANZP.prefix(ANZH.ref()); ~~}
{IF: ANZP.first()}{ dalej:=1; ~~}{FI}
{ lp:=0; ~~}
{WHILE: dalej}
{DO}
  {IF: ANZP.RUBR().PRINTED='T'}
    { lp+=1; ~~}{SUBSTITUTE: 'LINIA0'}
  {FI}
  { dalej:=ANZP.next(); ~~}
{OD}
{"---Drukuj ceny surowcow pobranych do kalkulacji (jezeli tak zadecydowal "; ~~}
{"---operator).                                                           "; ~~}
{IF: pkalmat}
  { PAR_WYDR.TITLE:='CENNIK SUROWCÓW POBRANYCH\nDO KALKULACJI ZLECENIA'@; ~~}
  { ANZMAT.index('AS'); ANZMAT.prefix(ANZH.ref()); ~~}
  {IF: ANZMAT.first()}{PAGE}{ dalej:=1; prn1:='PRNMAT'; ~~}
{ lmt:=int((charleft()-PRNMAT.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=ANZMAT.next(); ~~}
  {OD}
{FI}
{"---Drukuj listę stawek operacji do kalkulacji (jezeli tak zadecydowal operator)."; ~~}
{IF: pkaloper}
  { PAR_WYDR.TITLE:='LISTA STAWEK OPERACJI POBRANYCH\nDO KALKULACJI ZLECENIA'@; ~~}
  { ANZTTO.index('AO'); ANZTTO.prefix(ANZH.ref()); ~~}
  {IF: ANZTTO.first()}{PAGE}{ dalej:=1; prn1:='PRNZTO'; ~~}
{ lmt:=int((charleft()-PRNZTO.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=ANZTTO.next(); ~~}
  {OD}
{FI}
{"---Drukuj listę kosztow stanowisk do kalkulacji (jezeli tak zadecydowal operator)."; ~~}
{IF: pkalst}
  { PAR_WYDR.TITLE:='LISTA KOSZTÓW STANOWISK POBRANYCH\nDO KALKULACJI ZLECENIA'@; ~~}
  { ANZWRK.index('AP'); ANZWRK.prefix(ANZH.ref()); ~~}
  {IF: ANZWRK.first()}{PAGE}{ dalej:=1; prn1:='PRNZST'; ~~}
{ lmt:=int((charleft()-PRNZST.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=ANZWRK.next(); ~~}
  {OD}
{FI}
{"---Drukuj listę stawek uslug pobranych do kalkulacji (jezeli tak zadecydowal operator)."; ~~}
{IF: pkalusl}
  { PAR_WYDR.TITLE:='LISTA STAWEK USŁUG POBRANYCH\nDO KALKULACJI ZLECENIA'@; ~~}
  { ANZTOU.index('AT'); ANZTOU.prefix(ANZH.ref()); ~~}
  {IF: ANZTOU.first()}{PAGE}{ dalej:=1; prn1:='PRNZUS'; ~~}
{ lmt:=int((charleft()-PRNZUS.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=ANZTOU.next(); ~~}
  {OD}
{FI}
{"---Drukuj listę kosztow N-P-U pobranych do kalkulacji (jezeli tak zadecydowal operator)."; ~~}
{IF: pkalnpu}
  { PAR_WYDR.TITLE:='LISTA KOSZTÓW N-P-U POBRANYCH\nDO KALKULACJI ZLECENIA'@; ~~}
  { ANZTLS.index('AT'); ANZTLS.prefix(ANZH.ref()); ~~}
  {IF: ANZTLS.first()}{PAGE}{ dalej:=1; prn1:='PRNZTL'; ~~}
{ lmt:=int((charleft()-PRNZTL.width())/2); ~~}{FI}
  {WHILE: dalej}
  {DO}
    {SUBSTITUTE: 'LINIA0'}
    { dalej:=ANZTLS.next(); ~~}
  {OD}
{FI}

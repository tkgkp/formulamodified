:!UTF-8
{COMMENT}   MACRO - LINIA0                                              {COMMENT-}
{COMMENT}
Makro używane do drukowania rekordu z linią poprzedzającą
dla wydruków z obiektem PRINTER o nazwie przekazywanej przez zmienną prn1.
Bez ENTIRE
{COMMENT-}
{DEFINEFONT: 'TM=Inter,,M,,0,0'}
{DEFINEFONT: 'TMB=Inter,,N,,0,0'}
{DEFINEFONT: 'TF=Inter,,F,,0,0'}
{MACRO: 'MY_LINIA0'}
{  ll:=lineleft(); ~~}
{IF:  _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; _v:=($(_f))();
      ll<(rsep+_v) & ll<>0}{PAGE}{FI}
{IF: vp}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   {ELIF: rsep}
      {REP: _f:=prn1+'.fbar(lmt)'; ($(_f))()}
   {FI}
{FI}
{WHILE: _f:=prn1+'.next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.fline(lmt)'; ($(_f))()}
{OD}
{IF: var_pres('fun')>=0 & fun<>''}
{  $fun(); ~~}
{FI}
{MACRO-}
{COMMENT}   MACRO - druk                                             {COMMENT-}
{MACRO: 'druk'}
{IF: wyjscie=1}{EXIT}{FI}
{WHILE: ilkop<=PARAM_W.KOPIA}
{DO}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}
   {FOOTER}
   {{? ilkop=1 || stron:=page ?};~~}{IF: duplex<>2}{FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}{<1>{charleft}exec('tr','tlumaczenia','1000211')+' '+$(page-(ilkop-1)*stron)}{FI}
   {FOOTER-}
   {
   PAR_WYDR.TITLE:=exec('trans_typysp','tlumaczenia',TYPYSP.ref(),PARAM_W.JEZYK)+'\n'
      +exec('tr','tlumaczenia','1000102')+' '+FAKS.SYM;
   rsep:=0
   ;~~}
{COMMENT}   REPORT - HEADER                                          {COMMENT-}
   { head:=PAR_WYDR.HEAD; PAR_WYDR.HEAD:=0;~~}
   {SUBSTITUTE: 'HEADF'}
{COMMENT}   oryginal / kopia, tryb probny                            {COMMENT-}
   {FONT: exec('font','param_wydr','ory_kop')}{SPACE: exec('space','param_wydr')}
   {IF: (';NZA'*FAKS.STAT_REJ)>1}{<1}{>{charleft}exec('tr','tlumaczenia','100021'+{? FAKS.STAT_REJ='A' || '5' || '0' ?})}{FI}
   {<1}{>{charleft}{? ~skladanka
      || {? ilkop=0 || exec('tr','tlumaczenia','1000103') || exec('tr','tlumaczenia','1000104') ?}
      || '' ?}+{? -PARAM_W.DR_DUPL='t' || {? ~skladanka || ' - ' || '' ?}+exec('tr','tlumaczenia','1000105')+' '+PARAM_W.DUPLIKAT$1 || '' ?}}
{COMMENT}   HEDER obowiazujacy przed wydrukiem pozycji               {COMMENT-}
   {HEADER}
   {FONT: exec('font','param_wydr','head')}{SPACE: exec('space','param_wydr')}
   {SUBSTITUTE: 'HEADF'}
   {HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
   {FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}
   {FOOTER}
   {{? ilkop=1 || stron:=page ?};~~}{IF: duplex<>2}{FONT: exec('font','param_wydr','footer')}{SPACE: exec('space','param_wydr')}{<1>{charleft}exec('tr','tlumaczenia','1000211')+' '+$(page-(ilkop-1)*stron)}{FI}
   {FOOTER-}
{ ilkop+=1;~~}
{COMMENT}   DANE NAGLOWKOWE                                          {COMMENT-}
{COMMENT}   strony transakcji                                        {COMMENT-}
{ __STRTAB:='FAKS'; __STRODB:=''; __STRLIN:='KOR'; __STRDAT:=FAKS.DW;~~}
{SUBSTITUTE: 'stronyf'}
{ VAR_DEL.delete('__STRTAB','__STRODB','__STRLIN','__STRDAT');~~}
{COMMENT}   TRESC FAKTURY                                            {COMMENT-}
   {FONT: exec('font','param_wydr','poz')}
   {LINE: 2}
   {<{lmt}
      }{FONT: exec('font','param_wydr','kz_oddo_tr')}{exec('tr','tlumaczenia','2000210')+' '+exec('tr','tlumaczenia','2000211')
      }{FONT: exec('font','param_wydr','kz_oddo')}{' '}{$FAKS.KZ_OD
      }{FONT: exec('font','param_wydr','kz_oddo_tr')}{' '}{exec('tr','tlumaczenia','2000212')
      }{FONT: exec('font','param_wydr','kz_oddo')}{' '}{$FAKS.KZ_DO
      }{FONT: exec('font','param_wydr','kz_oddo_tr')}{':'}
{COMMENT}   LISTA KORYGOWANYCH DOKUMENTOW                            {COMMENT-}
   {FONT: exec('font','param_wydr','poz')}
   {FOR: 'FAKS_KZF'}
   {INDEX: 'POZ'}
   {PREFIX: FAKS.ref}
   {DO}
      {IF: ref_name(FAKS_KZF.FAKS)=FAKS.name()}
         {<{lmt}exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000101'))+' '
            +exec('tr','tlumaczenia','1000102')+' '+FAKS_KZF.FAK_SYM+' '
            +exec('tr','tlumaczenia','1000127')+' '+$FAKS_KZF.FAK_DW}
      {FI}
   {OD}
{COMMENT}   LISTA INDEKSOW                                           {COMMENT-}
   {IF: FAKS.KZ_ADOST='N' & __Ind.first()}
      {FONT: exec('font','param_wydr','poz')}
      {<{lmt}}{ prn1:='PRN1'; print1();~~}
      {HEADER}
      {SUBSTITUTE: 'HEADF'}
      {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
      {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
      {HEADER-}
      {IF: lineleft() & lineleft()<7}
         {PAGE}
      {ELSE}
         {LINE:2}
         {FONT: exec('font','param_wydr','tabhead_poz')}{SPACE: exec('space','param_wydr')}
         {SUBSTITUTE: 'TABHEAD'}
      {FI}
      { vp:=1;~~}
      {FONT: exec('font','param_wydr','poz')}
      {FOR: __Ind}
      {DO}
         {
         il_lin:=0;
         ww1[1]:=__Ind.N;
         "szacowanie ilosci lini pozycji";
         _f:=prn1+'.ini_line()'; _v:=($(_f))();
         _f:=prn1+'.length()'; il_lin:=($(_f))()+1;
         ~~}
         {IF: lineleft() & lineleft()<il_lin}{SUBSTITUTE: 'LINIAF'}{IF: lineleft()}{PAGE}{FI}{FI}
         {SUBSTITUTE: 'LINIA0'}
      {TOTAL}
         { VAR_DEL.delete('il_lin');~~}
      {OD}
      {HEADER}
      {SUBSTITUTE: 'HEADF'}
      {HEADER-}
      {SUBSTITUTE: 'LINIAF'}
   {FI}
{COMMENT}   RABAT, KWOTA ZMNIEJSZENIA PODATKU                        {COMMENT-}
   {LINE:2}
   {<{lmt}
      }{FONT: exec('font','param_wydr','kz_rab_tr')}{exec('tr','tlumaczenia','1000187')+':'
      }{FONT: exec('font','param_wydr','kz_rab')}{' '}{form(FAKS.RAB,,2,',.')
      }{FONT: exec('font','param_wydr','kz_rab_proc')}{'%'}
   {IF: FAKS.TZP<>''}{<{lmt}exec('tr','tlumaczenia','3000111')+': '+FAKS.TZP}{FI}
   {LINE}
   {IF: walutowa & (PARAM_W.WALH='H' | PARAM_W.WALH='T')}
      {<{lmt}
         }{FONT: exec('font','param_wydr','kz_kwrab_tr')}{exec('tr','tlumaczenia','1000199')+':'
         }{FONT: exec('font','param_wydr','kz_kwrab')}{' '}{form(-FAKS.BRTW,,2,',.')
         }{FONT: exec('font','param_wydr','kz_kwrab_wal')}{' '}{FAKS.WAL().KOD}
   {FI}
   {IF: walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='T'}
   {<{lmt}
      }{FONT: exec('font','param_wydr','kz_kwrab_tr')}{exec('tr','tlumaczenia','1000199')+':'
      }{FONT: exec('font','param_wydr','kz_kwrab')}{' '}{form(-FAKS.BRUTTO,,2,',.')
      }{FONT: exec('font','param_wydr','kz_kwrab_wal')}{' '}{FAKS.NWAL().KOD}
   {<{lmt}
      }{FONT: exec('font','param_wydr','kz_kwzp_tr')}{exec('tr','tlumaczenia','2000213')+':'
      }{FONT: exec('font','param_wydr','kz_kwzp')}{' '}{form(-FAKS.VAT,,2,',.')
      }{FONT: exec('font','param_wydr','kz_kwzp_wal')}{' '}{FAKS.NWAL().KOD}
   {FI}
   {LINE}
   {<{lmt}}{FONT: exec('font','param_wydr','poz')}{exec('tr','tlumaczenia','1000132')}
   { prn1:='PRN'; ~~}
{COMMENT}   TABELA VAT                                               {COMMENT-}
   {ENTIRE}
   {FONT: exec('font','param_wydr','tabhead_vat')}{SPACE: exec('space','param_wydr')}
   {SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
   {FONT: exec('font','param_wydr','vat')}
   {FOR: FAKSV}
   {INDEX: 'FF_SV'}
   {PREFIX: '',$FAKS.ref}
   {DO}
      {FIRST}
      {FIRST-}
      {
      ww[1]:=form(-FAKSV.WN,,2,',.');
      ww[2]:=form(FAKSV.SV().KOD,,2,',.');
      ww[3]:=form(-FAKSV.WV,,2,',.');
      ww[4]:=form(-FAKSV.WB,,2,',.');
      {? FAKSV.SV().KOD=' -o' | FKOR.SV().KOD=' -o' || __oo:=1 ?};
      ~~
      }
      {SUBSTITUTE: 'MY_LINIA0'}
   {OD}
   {SUBSTITUTE: 'LINIAF'}
   {ENTIRE-}
{COMMENT}   opis stawki odwrotne obciążenie                          {COMMENT-}
   {  SLU.cntx_psh(); SLO.cntx_psh(); ~~}
   {IF:
      __oo
      &  (
            SLU.index('NAZ');
            SLO.index('SL');
            SLU.prefix();
            SLU.find_key('~STAWKI VAT '+KSTE.KRAJ().KOD);
            SLO.prefix(SLU.ref,' -o');
            SLO.first()
         )
   }
      {<1}{<{lmt}}{SLO.KOD+' - '+exec('tr','tlumaczenia','1000168')}
   {FI}
   {  SLU.cntx_pop(); SLO.cntx_pop(); ~~}
{COMMENT}   do zaplaty                                               {COMMENT-}
   {LINE}
   {
   zap:=
      {? __walh=__walp | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
      ||
         FAKS.BRUTTO
      ||
         FAKS.BRTW
      ?};
   slownie:=exec('slownie','tlumaczenia',fabs(zap),{? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || __walh || __walp ?});
   {? slownie=''
   || kw_fak:={? zap<0 || -zap || zap ?};
      slownie:=
         {? __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
            | __walp<>INFO.NAROD().KOD
         ||
            STR.słownie(kw_fak)+{? frac(kw_fak) ||' '+$(#(form(kw_fak,,2,',.')+2))+'/100'||''?}+' '+
               {? PARAM_W.WALH='T' | PARAM_W.WALH='H' || __walh || __walp ?}
         ||
            STR.kwota_sł(fabs(kw_fak))
         ?}
   ?}
   ;~~}
   {IF: zap<0}
      {<{lmt+1}
         }{FONT: exec('font','param_wydr','dz_tr')}{exec('tr','tlumaczenia','2000110')
         }{FONT: exec('font','param_wydr','dz_war')}{' '}{form(-zap,,2,'.,')
         }{FONT: exec('font','param_wydr','dz_wal')}{' '}{{? PARAM_W.WALH='T' | PARAM_W.WALH='H' || __walh || __walp ?}}
      {IF: PARAM_W.SLOWNIE='T'}
         {<{lmt+1}
            }{FONT: exec('font','param_wydr','dzsl_tr')}{exec('tr','tlumaczenia','1000141')
            }{FONT: exec('font','param_wydr','dzsl_war')}{' '}{slownie}
      {FI}
   {ELSE}
      {<{lmt+1}
         }{FONT: exec('font','param_wydr','dz_tr')}{exec('tr','tlumaczenia','1000139')
         }{FONT: exec('font','param_wydr','dz_war')}{' '}{form(zap,,2,'.,')
         }{FONT: exec('font','param_wydr','dz_wal')}{' '}{{? PARAM_W.WALH='T' | PARAM_W.WALH='H' || __walh || __walp ?}}
      {IF: PARAM_W.SLOWNIE='T'}
         {<{lmt+1}
            }{FONT: exec('font','param_wydr','dzsl_tr')}{exec('tr','tlumaczenia','1000141')+' '
            }{FONT: exec('font','param_wydr','dzsl_war')}{slownie}
      {FI}
   {FI}
{COMMENT}   podpisujacy                                              {COMMENT-}
   {FONT: exec('font','param_wydr','podpisy')}
   {<1}{
   max_T8:=charleft;
   _p100230:=exec('get','#params',100230,2);
   {? var_pres('STR1')>=100 || obj_del(STR1) ?};
   STR1:=obj_new(@.CLASS.STRING);
   Text:={? _p100230='O' | _p100230='N' || exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000145')) || '' ?};
   Text1:={? _p100230='O' | _p100230='S' || exec('tr','tlumaczenia',exec('kod_aktualny','tlumaczenia','1000146')) || '' ?};
   STR.split(Text);
   STR1.split(Text1);
   ~~}
   {WHILE: _a:=+(Text:=STR.line(((max_T8-4)/2)$0-30)); _b:=+(Text1:=STR1.line(((max_T8-4)/2)$0-30)); _a | _b}
   {DO}
      {FIRST}
         {IF: lineleft()<6}{PAGE}{FI}{LINE: 1}
      {FIRST-}
      {IF: +Text | +Text1}
         {IF: +Text}{<{lmt+1}Text}{FI}{IF: +Text1}{<{((max_T8-4)/2)$0+20}Text1}{FI}
      {FI}
   {OD}
{COMMENT}   upowaznienie                                             {COMMENT-}
   {FONT: exec('font','param_wydr','upowaznienie')}
   {_p100230:=exec('get','#params',100230,2);
   _os:=form(exec('kh_osob_default','kontrahent',KH.ref));
   USERS.cntx_psh;
   USERS.index('USR_KKOD');
   USERS.prefix;
   Text:={? _p100230='O' | _p100230='N' || {? +_os || _os || 20*'.' ?} || '' ?};
   Text1:={? _p100230='O' | _p100230='S' || {? USERS.find_key(FAKS.USER,FAKS.USER) & +USERS.DANE || USERS.DANE || 20*'.' ?} || '' ?};
   USERS.cntx_pop;
   STR.split(Text);
   STR1.split(Text1)
   ;~~}
   {WHILE: _a:=+(Text:=STR.line(((max_T8-4)/2)$0-30)); _b:=+(Text1:=STR1.line(((max_T8-4)/2)$0-30)); _a | _b}
   {DO}
      {FIRST}
         {LINE: 4}
      {FIRST-}
      {IF: +Text | +Text1}
         {IF: +Text}{<{lmt+1}Text}{FI}{IF: +Text1}{<{((max_T8-4)/2)$0+20}Text1}{FI}
      {FI}
   {OD}
   {ENTIRE-}
{COMMENT}   KONIEC FAKTURY                                           {COMMENT-}
   {HEADER}{HEADER-}
   { duplex:=page%*2=1;~~}
   {IF: lineleft()>0}{PAGE}{FI}
   {IF: BEER.SZ='S' & exec('get','#params',2124,2,OPERATOR.USER)='T' & duplex=1}
      {duplex:=2;~~}
      {PAGE}
      {duplex:=0;~~}
   {FI}
{OD}
{MACRO-}
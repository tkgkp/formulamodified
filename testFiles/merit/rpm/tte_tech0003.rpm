:!UTF-8
{TITLE:C. Wydruk zbiorczy kalkulacji}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  KKTL.cntx_psh();
  "---Obiekt wydruku kalkulacji kart technologicznych.";
  PRNKKT := obj_new(@.CLASS.PRINT,10);
  PRNKKT.s_frame();
  prn1:='PRNKKT';
  tryb:=rep_export();
  waslbar:=0;
  dalej:=0;
  color:='';
 "---Indeks tymczasowy do wyznaczenia kalkulacji z dnia.";
  ndx_tmp1:=KKTL.ndx_tmp(,1,'DT',,0);
  "---Zmienna wskazujaca na REF ostatniej pozycji w tabeli KKTL, spelniajacej ";
  "---odpowiedni warunek (szerszy opis dalej).";
  lastkkt:=null();
  "---Data poczatkowa oraz data koncowa, w obrebie ktorych beda drukowane     ";
  "---kalkulacje.";
  datap:=date();
  datak:=date();
  "---Zawiera REF typu karty technologicznej, do ktorego bedzie ograniczony   ";
  "---spis kalkulacji. Jezeli maja byc drukowane kalkulacje dla kart          ";
  "---wszystkich typow, to powinna zawierac null.";
  tpktl:=null();
  "---Symbol typu karty technologicznej, jesli 'tpktl'<>null.";
  tpktln1:='';
  "---Nazwa typu karty technologicznej, jesli 'tpktl'<>null.";
  tpktln2:='';
  TPKTL.win_sel('WERSLO');
  WYDRUK.fld_fml('TYPTKTL','AFTER_EDIT',"{? WYDRUK.TYPTKTL='T'
                                     || WYDRUK.efld_opt('RED9', 'mark=1',, 'TPKTL','TYP');
                                        WYDRUK.efld_opt('RED9', 'enable=1',, 'TPKTL','TYP');
                                        WYDRUK.efld_opt('RED9', 'enable=1',, 'TPKTL','OPIS')
                                     || WYDRUK.efld_opt('RED9', 'mark=0',, 'TPKTL','TYP');
                                        WYDRUK.efld_opt('RED9', 'enable=0',, 'TPKTL','TYP');
                                        WYDRUK.efld_opt('RED9', 'enable=0',, 'TPKTL','OPIS')
                                     ?}
                                    ");
   WYDRUK.TYPTKTL:='N';
   WYDRUK.TPKTL:=null();
   WYDRUK.ODDNIA:=date(0,0,0);
   WYDRUK.DODNIA:=date(0,0,0);
   exit:=0;
   lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  KKTL.cntx_pop();
  obj_del(PRNKKT);
  "---Sekcja zwalniania funkcji.";
  &kktprice;
  KKTL.ndx_drop(ndx_tmp1);
  &lastkkt;
  &datap;
  &datak;
  &tpktl;
  &tpktln1;
  &tpktln2;
  TPKTL.win_sel('WER');
  WYDRUK.fld_fml('TYPTKTL','AFTER_EDIT',"*");
  WYDRUK.TYPTKTL:='N';
  WYDRUK.TPKTL:=null();
  WYDRUK.ODDNIA:=date(0,0,0);
  WYDRUK.DODNIA:=date(0,0,0);
  &dalej;
  &prn1;
  &color;
  &exit;
  &lm; &ll; &vp; &lmt; &rsep; &tryb
}
{COMMENT}
  Wydruk zbiorczy kalkulacji kart technologicznych.
  [SS] - 2001/03/14 - [7.41]
  [MKO] - 2001/11/08 - [7.53] OWwSK003_Aneks1
 Wywołanie: Karty technologiczne -> Raporty -> Zestawienia
{COMMENT-}
{MACRO: 'functions'}
  {"--------------------------------------------------------------------------";
   "---Zwraca cene jednostkowa wg kosztu wytworzenia wyrobu dla biezacej     ";
   "---kalkulacji karty technologicznej. Jezeli w kalkulacji karty            ";
   "---technologicznej nie mozna znalezc rubryki zadeklarowanej na typie      ";
   "---karty, to zwraca -1.";
   kktprice:="
     _result:=0;
     TKTL.cntx_psh();
     TKTL.clear();
     'Numer rubryki z cena wyrobu.';
     _rubr:=KKTL.NRK().TYP().CRUB;
     KPOZK.index('KR');
     KPOZK.prefix(KKTL.ref(),_rubr);
     {? KPOZK.first()
     || _result:=KPOZK.WART
     || _result:=-1
     ?};
     TKTL.cntx_pop();
     _result
   ";
   "--------------------------------------------------------------------------";
  ~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Sekcja akcji inicjalizujacych.                                       "; ~~}
{MACRO: 'miscelaneous'}
{
_jest:=0;
_wydr_naz:='tech0003';
WYDRUKIL.index('WYDRUKI');
WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
{? ~WYDRUKIL.first()
|| WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add();
   WYDRUK.blank()
|| _jest:=1;
   WYDRUK.TYPTKTL:=WYDRUKIL.SR;
   WYDRUK.TPKTL:={? WYDRUK.TYPTKTL='N' || null() || WYDRUKIL.TPKTL ?}
?};
WYDRUK.win_edit('RED9');
WYDRUK.efld_opt('RED9',{? WYDRUK.TPKTL=null() || 'mark=0' || 'mark=1' ?},, 'TPKTL','TYP');
WYDRUK.efld_opt('RED9',{? WYDRUK.TPKTL=null() || 'enable=0' || 'enable=1' ?},, 'TPKTL','TYP');
WYDRUK.efld_opt('RED9',{? WYDRUK.TPKTL=null() || 'enable=0' || 'enable=1' ?},, 'TPKTL','OPIS');
KKTL.index(ndx_tmp1);
KKTL.clear();
{? KKTL.first()
|| WYDRUK.ODDNIA:={? _jest || WYDRUKIL.OD_DATY || KKTL.DT ?};
   {? KKTL.last()
   || WYDRUK.DODNIA:={? _jest || WYDRUKIL.DO_DATY || KKTL.DT ?}
   ?}
|| FUN.info('Brak kalkulacji.');
   exit:=1
?};
{? exit=0 & WYDRUK.edit("{? WYDRUK.TYPTKTL='T'
                || __CHK.record(WYDRUK,,'TPKTL')
                || ''
                ?}")
||
   {? WYDRUK.TYPTKTL='T'
   || tpktl:=WYDRUK.TPKTL;
      tpktln1:=WYDRUK.TPKTL().TYP;
      tpktln2:=WYDRUK.TPKTL().OPIS
   ?};
   datap:=WYDRUK.ODDNIA;
   datak:=WYDRUK.DODNIA;
   WYDRUKIL.SR:=WYDRUK.TYPTKTL;
   WYDRUKIL.TPKTL:={? WYDRUKIL.SR='N' || null() || WYDRUK.TPKTL ?};
   WYDRUKIL.OD_DATY:=WYDRUK.ODDNIA;
   WYDRUKIL.DO_DATY:=WYDRUK.DODNIA;
   WYDRUKIL.put(1)
|| exit:=1
?};~~}
{IF: exit=1}
   {EXIT}
{FI}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{"---Deklaracja funkcji wydruku.                                          "; ~~}
{SUBSTITUTE: 'functions'}
{"------------------------------------------------------------------------"; ~~}
{"---Wykonanie akcji inicjalizujacych.                                    "; ~~}
{SUBSTITUTE: 'miscelaneous'}
{"------------------------------------------------------------------------"; ~~}
{COMMENT}--Definicja wiersza wydruku kalkulacji.--------------------{COMMENT-}
{"---Jezeli wybrano kalkulacje z jednego dnia, to daty nie drukujemy.     "; ~~}
{IF: datap<>datak}
  {PRNKKT.add("form(KKTL.DT)",10,'Data'@); ~~}
{FI}
{ PRNKKT.add("KKTL.NRK().NRK+' / '+TKTL.WER",20,'Numer karty / wersja'@);
  PRNKKT.add("KKTL.OPC().NAZ",30,'Wariant kalkulacji'@);
  PRNKKT.add("form(KKTL.KNR)",10,'Nr kalkulacji'@,1); ~~}
{"---Jesli sa drukowane kalkulacje tylko dla kart jednego typu, to kolumna"; ~~}
{"---z typem nie musi byc drukowana.                                      "; ~~}
{IF: tpktl=null()}
  {PRNKKT.add("KKTL.NRK().TYP().TYP",5,'Typ karty'@); ~~}
{FI}
{ PRNKKT.add("KKTL.STAN",12,'Zatwierdzona'@);
  PRNKKT.add("KKTL.TKTLW().KTM().KTM+' - '+M.N",40,'Produkt'@);
  PRNKKT.add("(_price:=kktprice(KKTL.ref());
              {? _price<>-1
              || form(_price,,2)
              || form('<brak>'@,-15)
              ?})",15,'Cena jednostkowa'@,1); ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{PAR_WYDR.TITLE:='ZBIORCZE ZESTAWIENIE KALKULACJI KART TECHNOLOGICZNYCH'@; ~~}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRNKKT.width())/2); ~~}
{<{lmt+1} 'Zakres:'@}{<{lmt+10}{? tpktl=null()
                                    || 'Wszystkie typy kart technologicznych'@
                                    || 'Karty technologiczne typu: %1 - %2'@[tpktln1,tpktln2]
                                    ?}}
{<{lmt+1} {? datap<>datak || 'Okres od: %1 do: %2'@[form(datap),form(datak)]
                          || 'Z dnia: %1'@[form(datap)]
                                   ?}}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: ~tryb}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{"---Przypisz do zmiennej 'lastkkt' REF ostatniej pozycji, ktora jest     "; ~~}
{"---zwiazana z karta technologiczna typu okreslonego wczesniej.          "; ~~}
{KKTL.clear(); ~~}
{lastkkt:=null(); ~~}
{KKTL.clear(); ~~}
{"------------------------------------------------------------------------"; ~~}
{"---Wyznaczanie REF'a ostatniej kalkulacji spelniajacej warunek.         "; ~~}
{FOR: 'KKTL'}
  {INDEX: ndx_tmp1}
{DO}
  {IF: KKTL.DT>=datap & KKTL.DT<=datak & (KKTL.NRK().TYP=tpktl | tpktl=null())}
    {lastkkt:=KKTL.ref(); ~~}
  {FI}
{OD}
{"------------------------------------------------------------------------"; ~~}
{"---Czy jest jakas kalkulacja spelniajaca warunek.                       "; ~~}
{IF: lastkkt=null()}
  {FUN.info('Brak kalkulacji spełniających podany warunek.'@); ~~}
{FI}
{"------------------------------------------------------------------------"; ~~}
{"---Wydruk wszystkich kalkulacji wg daty.                                "; ~~}
{KKTL.clear(); ~~}
{FOR: 'KKTL'}
  {INDEX: ndx_tmp1}
{DO}
  {IF: KKTL.DT>=datap & KKTL.DT<=datak & (KKTL.NRK().TYP=tpktl | tpktl=null())}
    {SUBSTITUTE: 'LINIA0'}
  {FI}
{OD}
:!UTF-8
{TITLE:Podział kosztów (kwoty)}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','__param','__edit');
   tresc:='ppodzkw';
   PAR_WYDR.TITLE:='Podział kosztów (kwoty)'@;
   par:=obj_new(12); par[4]:=1; par[6]:=0;
   _upr:=exec('uprawnieniawrap','pkd',
      'Podział kosztów'@,
      'PPL_PLL_PPKO',
      'PPL_PLL_RPKO',
      'PPL_PLL_RPPK',
      'PPL_GRP_APKO'
   );
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'OD','DATE','Data od'@,
         'DO','DATE','Data do'@
      );
      __param.OD:=date(,1,1);
      __param.DO:=date(,12,0);
      __param.add();
      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'ppl_ppodzkw');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'OD',,,11,,,,,'Data od'@);
      __param.win_efld(__edit,,'DO',,,11,,,,,'Data do'@);
      __param.efld_opt(__edit,'mark=1',,'OD');
      __param.efld_opt(__edit,'mark=1',,'DO');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[6]:=__param.edit(
            "  {? (_chk:=__CHK.record(__param,,'OD','DO'))<>''
               || return(_chk)
               |? __param.DO<__param.OD
               || FUN.emsg('\"Data od\" nie może być wcześniejsza niż \"Data do\".'@);
                  return('OD')
               || 1
               ?}
            "
         )
      || par[3]:=__param.OD; par[4]:=__param.DO;
         par[1]:=obj_new(@.CLASS.PRINT,7);
         {? rep_export()
         || par[1].add(
               "  {? P.seek(par[2].PRAC,,1)
                  || P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+'(Nr teczki %1)'@[|P.T]
                  || ''
                  ?}
               ",
               40,'Pracownik'@
            )
         ?};
         par[1].add("par[2].SKLADNIK",20,'Składnik'@);
         par[1].add("par[2].CECHA",10,'Cecha'@);
         par[1].add("par[2].OPIS",22,'Opis'@);
         par[1].add("par[2].WARTC",22,'Wartość cechy'@);
         par[1].add("par[2].WARTD",20,'Wartość dekretu'@,1,,,,,'12,2');
         par[1].add("par[2].LISTA",10,'Lista'@,1);
         par[1].s_frame();
         par[5]:=params_set('P',exec('wybierz_parses','pracownik','PPL').P);

         b_rat:=h_rat:=par[9]:=1;
         par[12]:=tab_tmp(,'REF','INTEGER','Numer');
         Lp:=0
      ?};
      prn1:='par[1]';
      lmt:=rsep:=0;
      vp:=1
   ?};
   P.cntx_psh()
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','Lp','__param','__edit')
}
{COMMENT}OLD: ppodzkw.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{EXIT: ~par[6]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[9]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[9]:=0; ~~ }
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Zakres dat: '@}{par[3]} - {par[4]}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'opis'}
{IF: par[7]<>''}
{FONT: 'W12'}
{SPACE: 'A'}
   {IF: ~rep_export()}
      {<{ceil(lmt*b_rat)+1}}{Lp+=1; form(Lp,,,'9.')} {par[7]}
   {FI}
{FONT: 'T8'}
{SPACE: 'B'}
   {REP: par[1].fhbar(lmt)}
   {  par[7]:='';
      par[11]:=1; ~~
   }
{FI}
{MACRO-}
{MACRO: tresc}
{FONT: 'T8'}
{SPACE: 'B'}
{par[7]:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' ('+form(P.T)+')'; ~~ }
{FOR: par[2]}{PREFIX: $P.ref()}{DO}
   {IF: lineleft() & lineleft()<=4}
      {IF: par[11]}
         {REP: par[1].flbar(lmt)}
         {  par[11]:=0;
            Lp-=1;
            par[7]:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' ('+form(P.T)+')'; ~~
         }
      {FI}
      {IF: lineleft()}
         {PAGE}
      {FI}
   {FI}
   {SUBSTITUTE: 'opis'}
   {  par[1].ini_line(); ~~ }
   {REP: par[1].fline(lmt)}
   {WHILE: par[1].next()}
   {DO}
      {REP: par[1].fline(lmt)}
   {OD}
{OD}
{IF: par[7]=''}
   {REP: par[1].flbar(lmt)}
   {  par[11]:=0; ~~ }
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? rep_export() || 1 || charleft() ?}; ~~ }
{SPACE: 'B'}
{  lmt:=int((charleft()-par[1].width())/2);
   par[10]:=par[3];
   _sql:='select ATRUSE_L.P PRAC, ATRDEF.AKRONIM CECHA, ATRDEF.OPIS OPIS, '+
         ' ATRUSE_L.KON WARTC, ATRUSE_L.LT LISTA, ATRUSE_L.WARR WARTD, R.RT SKLADNIK';
   _sql+=' from @ATRUSE_L join ATRDEF join R join P using (ATRUSE_L.P,P.REFERENCE)';
   _sql+=' where (ATRUSE_L.REFERENCE like \'kak'+'l'+($(par[10]~1)+2)+form(par[10]~2,-2)+'%\'';
   par[10]:=date(par[10]~1,par[10]~2,0)+1;
   {!
   |? par[10]<=par[4]
   |! _sql+=' or ATRUSE_L.REFERENCE like \'kak'+'l'+($(par[10]~1)+2)+form(par[10]~2,-2)+'%\'';
      par[10]:=date(par[10]~1,par[10]~2,0)+1
   !};
   _sql+=') and P.FIRMA=:_c';
   _sql+=' order by PRAC, LISTA, CECHA';
   par[2]:=sql(_sql,par[3],par[4],exec('ref_firma','ustawienia')); ~~
}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {IF: ~par[12].find_key(#P.ref())}
         {SUBSTITUTE: tresc}
         {  par[12].REF:=#P.ref();
            par[12].add(); ~~
         }
      {FI}
   {FI}
{OD}

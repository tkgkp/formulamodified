:!UTF-8
{TITLE:A. Niezrealizowane polecenia wydania wyposażenia}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   POLWO.cntx_psh();
   PWOPOZ.cntx_psh();
   VAR_DEL.delete('PRN','PRNSUM','__TAB');
   PRN:=obj_new(@.CLASS.PRINT,10); PRN.s_frame();
   PRNSUM:=obj_new(@.CLASS.PRINT,10); PRNSUM.s_frame();
   prn1:='PRN';
   prn2:='PRNSUM';
   tryb:=rep_export();
   waslbar:=0;
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   _wydr_naz:='polwoz001';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add();
      _par:='P'
   || _par:=WYDRUKIL.SR
   ?};
   parametr:=exec('edit_radiobutton','#edit',_par,'Zestawienie według:'@,
      params_set('a','pracowników'@,'b','podgrup materiałowych'@); params_get(1),
      params_set('a','P','b','M'); params_get(1)
   );
   {? type_of(parametr)=type_of('')
   || WYDRUKIL.SR:=parametr;
      WYDRUKIL.put(1)
   ?};
   razem:='RAZEM'@;
   bylo:=0; total:=''
}
{END:
   POLWO.cntx_pop();
   PWOPOZ.cntx_pop();
   VAR_DEL.delete('PRN','PRNSUM','__TAB');
   &prn2;
   &prn1;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   &parametr;
   &razem;
   &bylo; &total; &tryb
}
{IF: parametr=~~}{EXIT}{FI}
{COMMENT}
  Wydruk zestawienia niezrealizowanych poleceń wydania wyposażenia
  TS [17.42]
{COMMENT-}
{COMMENT}--Definicja wiersza zestawienia wykonanych operacji w miesiacu wg pracownika.--------------------{COMMENT-}
{ {? parametr='P'
  || {? tryb=1
     || PRN.add("form(__TAB.NAZWISKO)",30,'Nazwisko'@);
        PRN.add("form(__TAB.PIERWSZE)",20,'Imię'@)
     ?};
     PRN.add("form(__TAB.DATA)",10,'Data'@);
     PRN.add("__TAB.SYM",20,'Symbol'@);
     PRN.add("__TAB.MGRP",20,'Podgrupa materiałowa'@);
     PRN.add("form(__TAB.IL,,0)",15,'Liczba'@,1);
     PRNSUM.add("razem",56,'Opis'@,1,,,,,,,3);
     PRNSUM.add("form(sum('ilosc'),,0)",15,'Liczba'@,1)
  ||
     {? tryb=1
     || PRN.add("__TAB.MGRP",20,'Podgrupa materiałowa'@)
     ?};
     PRN.add("form(__TAB.DATA)",10,'Data'@);
     PRN.add("form(__TAB.SYM)",20,'Symbol'@);
     {? tryb=1
     || PRN.add("form(__TAB.NAZWISKO)",30,'Nazwisko'@);
        PRN.add("form(__TAB.PIERWSZE)",20,'Pracownik'@)
     || PRN.add("__TAB.OSOBA",50,'Pracownik'@)
     ?};
     PRN.add("form(__TAB.IL,,0)",15,'Liczba'@,1);
     PRNSUM.add("razem",86,'Opis'@,1,,,,,,,3);
     PRNSUM.add("form(sum('ilosc'),,0)",15,'Liczba'@,1)
  ?};
  PAR_WYDR.TITLE:='NIEZREALIZOWANE POLECENIA WYDANIA WYPOSAŻENIA'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------MACRO-----------------------{COMMENT-}
{MACRO: 'TOP'}
{FONT: 'T8G'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{ width:=charleft(); ~~}
{<{1} XINFO.NAZ}{>{width} OPERATOR.USER().DANE+' '+PAR_WYDR.MIEJSC+' dn. '+$date()}

{FONT: 'TITLE'}{SPACE: 'B'}
{<{1}>{charleft()} PAR_WYDR.TITLE}
{FONT: 'T8G'}{SPACE: 'B'}
{MACRO-}
{MACRO: 'usttab'}
{  {? parametr='P'
   || __TAB:=sql('
         select
            POLWO.DATA,
            POLWO.SYM,
            MGRP.KOD as MGRP,
            OSOBA.NAZWISKO,
            OSOBA.PIERWSZE,
            OSOBA.NAZWISKO || \' \' || OSOBA.PIERWSZE || \' \' || OSOBA.DRUGIE as OSOBA,
            PWOPOZ.IL,
            OSOBA.NAZWISKO || OSOBA.PIERWSZE || OSOBA.DRUGIE || OSOBA.PESEL as TOTAL
         from PWOPOZ join POLWO join MGRP join P join OSOBA
         where POLWO.AKCEPT=\'T\' and POLWO.DATA_G is null
         order by TOTAL, DATA, SYM
      ')
   || __TAB:=sql('
         select
            POLWO.DATA,
            POLWO.SYM,
            MGRP.KOD as MGRP,
            OSOBA.NAZWISKO,
            OSOBA.PIERWSZE,
            OSOBA.NAZWISKO || \' \' || OSOBA.PIERWSZE || \' \' || OSOBA.DRUGIE as OSOBA,
            PWOPOZ.IL,
            MGRP.KOD as TOTAL
         from PWOPOZ join POLWO join MGRP join P join OSOBA
         where POLWO.AKCEPT=\'T\' and POLWO.DATA_G is null
         order by TOTAL, DATA, SYM
      ')
   ?};
   ~~
}
{MACRO-}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{IF: ~tryb}
   {SUBSTITUTE: 'HEAD'}
{ELSE}
   {SUBSTITUTE: 'TOP'}
{FI}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} {? parametr='P' || 'Zestawienie wg pracowników'@ || 'Zestawienie wg podgrup materiałowych'@ ?}}

{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{SUBSTITUTE: 'usttab'}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: __TAB}
{DO}
   {FIRST}
      {IF: tryb<>1}
         {IF: tryb=0 & lineleft<7}{PAGE}
         {FI}
         {IF: parametr='P'}{<{lmt+1} 'Pracownik: %1'@[__TAB.OSOBA]}
         {ELSE}{<{lmt+1} 'Podgrupa materiałowa: %1'@[__TAB.MGRP]}
         {FI}
     {FI}
     {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1; total:=__TAB.TOTAL; ~~}
   {FIRST-}
   {IF: tryb | lineleft > 5}{SUBSTITUTE: 'LINIA0'}{ADD: ilosc;#;__TAB.IL}
   {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
   {FI}
{TOTAL: __TAB.TOTAL}
   {IF: tryb<>1}
      {IF: tryb=2 | lineleft > 5} {SUBSTITUTE: 'LINIAFS'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIAFS'}
      {FI}
      {IF: total<>__TAB.TOTAL}
         {IF: tryb=0 & lineleft<7}{PAGE}
         {ELIF: tryb=2}{<{lmt+1} ''}
         {FI}
         {IF: parametr='P'}{<{lmt+1} 'Pracownik: %1'@[__TAB.OSOBA]}
         {ELSE}{<{lmt+1} 'Podgrupa materiałowa: %1'@[__TAB.MGRP]}
         {FI}
         {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1; total:=__TAB.TOTAL; ~~}
      {FI}
   {FI}
{OD}

:!UTF-8
{TITLE: Definicje schematu VAT}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT}
{BEGIN: PRN := obj_new(@.CLASS.PRINT,16);
        PRN.sl_frame();
        w:=obj_new(4);
        pierwszy:=1;
        ref_w:=null;
        dalej:=1;
        wyr:=PAR_SKID.get(35)='T';
        SKID_WYR.index('SKID_WYR'); mdc:='';lm:=0
}
{END:  VAR_DEL.delete('PRN','w','pierwszy','ref_w','dalej','wyr','mdc','lm')}
{PRN.add("{? pierwszy||$DEK.LP||''?}",3,'Lp'@,1);
 PRN.add("{? pierwszy||DEK.RODZK().TR||''?}",15,'  Rodzaj kwoty'@);
 PRN.add("{? pierwszy||DEK.KONTO||''?}",35,10*' '+'Konto'@);
 PRN.add("{? pierwszy||form(DEK.WSP,7,2)||''?}",7,'  Wsp.'@,1);
 PRN.add("{? pierwszy||DEK.STR||''?}",2,'St');
 PRN.add("{? pierwszy||DEK.OP||''?}",20,'        Opis'@);
 PRN.add("{? pierwszy||DEK.ID||''?}",20,' Identyfikator'@);
 PRN.add("{? pierwszy||DEK.TID||''?}",10,'    Typ'@);
 PRN.add("{? pierwszy||DEK.TP||''?}",15,' Termin płatn.'@);
 PRN.add("{? pierwszy||DEK.DO||''?}",15,' Data otwarcia'@);
 PRN.add("{? pierwszy||DEK.OPTYM||''?}",4,'Opt.'@);~~}
{IF: wyr}
{PRN.add("w[1]",3,'Lp'@);
 PRN.add("w[2]",12,'  Wyróżnik#  (słownik)'@,,'#');
 PRN.add("w[3]",15,'    Wyróżnik#     (kod)'@,,'#');
 PRN.add("w[4]",15,'    Wyróżnik# (wyznacz. kw.)'@,,'#');~~}
{FI}
{INCLUDE: wsp_pw3.rpi}
{PAR_WYDR.TITLE:='DEFINICJE SCHEMATU "%1" %2, rok %3'@[AV.KOD,AV.OP,AV.ROK_F().NAZ];~~}
{HEADER}
{SUBSTITUTE: 'CHARHEAD'}
{FONT: 'Q'}{SPACE: 'C'}
{LINE:2}
{<3{? AV.A='T' || 'Zaakceptowany: tak'@ || 'Zaakceptowany: nie'@ ?}}
{LINE:1}
{FONT: 'R'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}{DO}
{PRN.h_line()}
{OD}
{FONT: 'Q'}
{PRN.bar()}
{HEADER-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{MACRO: 'wyp_w'}
{ w[1]:=$SKID_WYR.LP;
  w[2]:=SKID_WYR.SLU().SLU().NAZ;
  w[3]:={? SKID_WYR.KOD<>null || 'stały: %1-%2'@[SKID_WYR.KOD().KOD,SKID_WYR.KOD().TR]
  |? SKID_WYR.FORMULA<>null || 'formuła: %1-%2'@[SKID_WYR.FORMULA().SKROT,SKID_WYR.FORMULA().NAZWA]
  || 'wszystkie ze   słownika'@
  ?};
  w[4]:={? SKID_WYR.PROC || '%1%% dekret.'@[$SKID_WYR.PROC]
  |? SKID_WYR.KWOTA<>null || 'formuła: %1-%2'@[SKID_WYR.KWOTA().SKROT,SKID_WYR.KWOTA().NAZWA]
  || 'bark'@
  ?};
  ~~
}
{MACRO-}
{MACRO: 'wyrozn'}
{ {! _i:=1..4 |! w[_i]:='' !};~~}
{ SKID_WYR.prefix(REF.FIRMA,'DEK',DEK.name(),#DEK.ref,'X');~~}
{IF: {? pierwszy || SKID_WYR.first() || SKID_WYR.seek(ref_w); SKID_WYR.next()?}}
{ ref_w:=SKID_WYR.ref();~~}
{SUBSTITUTE: 'wyp_w'}
{ELSE}
{ ref_w:=null;~~}
{FI}
{IF: ref_w<>null | pierwszy}
{SUBSTITUTE:'linia'}
{FI}
{MACRO-}
{MACRO: 'linia'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{IF:_l:=lineleft(); _l<2 & _l>0}
{PRN.lbar()}
{ELSE}
{<1 PRN.line()}
{FI}
{OD}
{MACRO-}
{FOR:'DEK'}
{INDEX:'DEK'}
{PREFIX:AV.ref()}
{DO}
{ pierwszy:=1; ref_w:=null; ~~}
{SUBSTITUTE:'wyrozn'}
{pierwszy:=0;~~}
{WHILE: ref_w<>null}
{DO}
{SUBSTITUTE:'wyrozn'}
{OD}
{ DEK.cntx_psh(); dalej:=DEK.next(); DEK.cntx_pop();~~}
{IF: (lineleft()>=3) | (lineleft=2 & ~dalej)}
{IF: dalej}
{PRN.bar()}
{ELSE}
{ENTIRE}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{FI}
{ELSE}
{IF: lineleft()=2}
{ENTIRE}
{PRN.lbar()}
{LINE: 1}
{ENTIRE-}
{ELSE}
{PRN.lbar()}
{IF: lineleft()=1}{PAGE}{FI}
{IF: ~dalej}
{ENTIRE}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{FI}
{FI}
{FI}
{OD}
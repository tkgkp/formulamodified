:!UTF-8
{TITLE:A. Zapisy księgowe z wybranymi wyróżnikami}
{PRINTER: FINFO.SZ_PRN,,,,,'Q','C',,,,,,1,15,15,10,15}
{BEGIN: SLO.cntx_psh(); SLO.win_dict('ONE'); SLO.win_sel('ONE');
        PRN:=obj_new(@.CLASS.PRINT,9); PRN.sl_frame(); w:=obj_new(9);s:=obj_new(2);
        wyrod:=wyrdo:=slowyr:=indtym:=''; is_zap:=0; dat_od:=dat_do:=date(0,0,0);
        kol1:=kol2:=''; sum1:=sum2:=sum11:=sum12:=sum21:=sum22:=lm:=0;
        licz1:=licz2:=1; vp:=1; dru_sum1:=dru_sum2:=dru_bar:=0; cosum1:=cosum2:=mdc:='';
        w1:=w2:=w3:=w4:=w5:=w6:=w7:=w8:=w9:=''}
{END: SLO.cntx_pop();
      obj_del(w); obj_del(s); obj_del(PRN); &wyrod; &wyrdo; &slowyr; &indtym; &is_zap;
      &dat_od; &dat_do; &kol1; &kol2; &sum1; &sum2; &sum11; &sum12; &sum21; &sum22;
      &licz1; &licz2; WYR.ndx_drop(); &vp; &cosum1; &cosum2;
      &dru_sum1; &dru_sum2; &dru_bar; &lm; &mdc;
      &w1; &w2; &w3; &w4; &w5; &w6; &w7; &w8; &w9}
{COMMENT}
(c) Macrologic S.A. Wszelkie prawa zastrzezone
==================================================
Nazwa pliku: fks_zap_wyr1.rpm
Utworzony:    2003-06-10 [AMK] Artur Makos
==================================================
Zawartosc: Zapisy ksiegowe z wybranym wyroznikiem.
{COMMENT-}
{ WYROZN.MASKA:='A';
  WYROZN.WYROD:=SLO.ref();
  SLO.cntx_psh(); SLO.last(); WYROZN.WYRDO:=SLO.ref(); SLO.cntx_pop();
  OKRO_F.cntx_psh();
  WYROZN.DATA_OD:=WYROZN.DATA_DO:=date(0,0,0);
  OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
  {? OKRO_F.first() & OKRO_F.next() || WYROZN.DATA_OD:=OKRO_F.POCZ ?};
  SSTALE.AO(); {? OKRO_F.KON=date(0,0,0) || OKRO_F.last(); OKRO_F.prev() ?};
  WYROZN.DATA_DO:=OKRO_F.KON;
  OKRO_F.cntx_pop();
  WYROZN.win_edit('ZAKR_WRM');
  dat_od:=WYROZN.DATA_OD; dat_do:=WYROZN.DATA_DO;~~}
{EXIT: ~WYROZN.edit("_spr:=exec('spr_zwyr','!fks_dks_zwro');
                    {? _spr='' || _spr:=exec('spr_zdat','!fks_dks_zwro') ?}; _spr")}
{ SLO.cntx_psh(); SLU.cntx_psh(); SLUAPPL.cntx_psh();
  SLU.index('NAZ'); SLU.prefix(TAB_S.NAZ);
  SLUAPPL.index('NAZ'); SLUAPPL.prefix('F');
  {? SLU.first() & SLU.NAZ=TAB_S.NAZ & SLUAPPL.find_key(SLU.NAZ)
  || SLO.index('SL'); SLO.prefix(SLU.ref);
     {? WYROZN.WYROD=null & SLO.first() || WYROZN.WYROD:=SLO.ref ?};
     {? WYROZN.WYRDO=null & SLO.last() || WYROZN.WYRDO:=SLO.ref ?};
     {? WYROZN.WYROD<>null || wyrod:=WYROZN.WYROD().KOD+' - '+WYROZN.WYROD().TR ?};
     {? WYROZN.WYRDO<>null || wyrdo:=WYROZN.WYRDO().KOD+' - '+WYROZN.WYRDO().TR ?};
     slowyr:=SLU.NAZ
  ?};
  SLO.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop();
  {? -WYROZN.MASKA='a'
  || PRN.add("kol1",48,'Wyróżnik'@);
     PRN.add("kol2",41,'Konto'@);
     indtym:=WYR.ndx_tmp('',1,'NAZ',,0,'NAZ',,0,'KOD',,0,'KON',,0,'DTW',,0,'REJ',,0,'NR',,0,'NK',,0)
  || PRN.add("kol1",41,'Konto'@);
     PRN.add("kol2",48,'Wyróżnik'@);
     indtym:=WYR.ndx_tmp('',1,'NAZ',,0,'NAZ',,0,'KON',,0,'KOD',,0,'DTW',,0,'REJ',,0,'NR',,0,'NK',,0)
  ?};
  PRN.add("w[3]",{? OPERATOR.DEPT=null || 19 || 8 ?},'Rejestr');
  PRN.add("w[8]",6,'Numer'@,1);
  PRN.add("w[9]",20,'Symbol dokumentu'@);
  PRN.add("w[4]",11,'Data zapisu'@,1);
  PRN.add("w[5]",16,'Kwota Wn'@,1);
  PRN.add("w[6]",16,'Kwota Ma'@,1);
  PRN.add("w[7]",{? OPERATOR.DEPT=null || 25 || 30 ?},'Treść');
  {! _i:=1..9 |! w[_i]:='' !};
  {! _i:=1..2 |! s[_i]:='' !}; ~~}
{INCLUDE: wsp_pw3.rpi}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{PAR_WYDR.TITLE:='ZAPISY KSIĘGOWE Z WYBRANYMI WYRÓŻNIKAMI'@;~~}{SUBSTITUTE: 'CHARHEAD'}{FONT: 'Q'}
{LINE: 1}
{SPACE: 'B'}{IF: page=1}
{<1 'PARAMETRY WYDRUKU:'@}
{<1 'Słownik wyróżnika: %1'@[slowyr]}{>{PRN.width()} 'Okres obrotowy: %1 %2'@[SSTALE.AO().NAZ,SSTALE.AO().ROK().NAZ]}
{<1 'Od wyróżnika: %1'@[wyrod]}{>{PRN.width()}
 {? OPERATOR.DEPT
 || 'Jednostka księgowa %1'@[OPERATOR.DEPT().OD]
 || 'Wszystkie jednostki księgowe'@
 ?}}
{<1 'Do wyróżnika: %1'@[wyrdo]}{>{PRN.width()} 'Od daty: %1 do daty: %2'@[$WYROZN.DATA_OD,$WYROZN.DATA_DO]}{FI}
{FONT: 'R'}
{PRN.hbar()}
{PRN.ini_head()}
{WHILE: PRN.h_next()}
{DO}
{PRN.h_line()}
{OD}
{PRN.bar()}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{PRN.lbar()}{ vp:=1;~~}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{<1 PRN.line()}
{OD}{ vp:=0;~~}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'Q'}{SPACE: 'C'}
{FOR: 'WYR'}
{INDEX: indtym}
{PREFIX: SLU.NAZ, SLU.NAZ}
{DO}
{IF: WYR.DTW>=WYROZN.DATA_OD & WYR.DTW<=WYROZN.DATA_DO & WYR.KOD>=WYROZN.WYROD().KOD &  WYR.KOD<=WYROZN.WYRDO().KOD}
{ is_zap:=1;
  w[3]:=WYR.REJ+{? OPERATOR.DEPT=null || ' - '+WYR.OD || '' ?};
  w[8]:=$WYR.NR;
  w[9]:=WYR.NK;
  w[4]:=$WYR.DTW;
  w[5]:=form(WYR.WN,,2,' ,'); sum1+=WYR.WN$2;
  w[6]:=form(WYR.MA,,2,' ,'); sum2+=WYR.MA$2;
  w[7]:=WYR.OP;~~}
{IF: w[1]<>{? -WYROZN.MASKA='a' || WYR.KOD+' - '+WYR.TR || WYR.KON ?}}
{ w[1]:={? -WYROZN.MASKA='a' || WYR.KOD+' - '+WYR.TR || WYR.KON ?};
  s[1]:={? -WYROZN.MASKA='a' || WYR.KOD || WYR.KON ?};
  kol1:=6*' '+w[1];
  s[2]:=kol2:=w[2]:=''; {? licz1>1 || dru_sum1:=1 ?}; {? vp=0 || dru_bar:=1 ?};
  ~~}
{ELSE}
{ kol1:=''; licz1+=1;~~}
{FI}
{IF: w[2]<>{? -WYROZN.MASKA<>'a' || WYR.KOD+' - '+WYR.TR || WYR.KON ?}}
{ w[2]:={? -WYROZN.MASKA<>'a' || WYR.KOD+' - '+WYR.TR || WYR.KON ?};
  s[2]:={? -WYROZN.MASKA<>'a' || WYR.KOD || WYR.KON ?};
  kol2:=6*' '+w[2];
  {? licz2>1 || dru_sum2:=1 ?};~~}
{ELSE}
{ kol2:=''; licz2+=1;~~}
{FI}
{IF: dru_sum2}
{  w1:=kol1; w2:=kol2; w3:=w[3]; w4:=w[4]; w5:=w[5]; w6:=w[6]; w7:=w[7]; w8:=w[8]; w9:=w[9];
  kol2:='Suma: '+cosum2; kol1:=''; w[3]:=w[4]:=w[7]:=w[8]:=w[9]:='';
  w[5]:=form(sum21, , 2, ' ,'); w[6]:=form(sum22, , 2, ' ,'); vp:=0;~~}
{SUBSTITUTE: 'LINIA'}
{  kol1:=w1; kol2:=w2; w[3]:=w3; w[4]:=w4; w[5]:=w5; w[6]:=w6; w[7]:=w7; w[8]:=w8; w[9]:=w9;~~}
{   dru_sum2:=0; licz2:=1;~~}
{FI}
{IF: dru_sum1}
{  w1:=kol1; w2:=kol2; w3:=w[3]; w4:=w[4]; w5:=w[5]; w6:=w[6]; w7:=w[7]; w8:=w[8]; w9:=w[9];
  kol1:='Suma: '+cosum1; kol2:=''; w[3]:=w[4]:=w[7]:=w[8]:=w[9]:='';
  w[5]:=form(sum11, , 2, ' ,'); w[6]:=form(sum12, , 2, ' ,'); vp:=0;~~}
{SUBSTITUTE: 'LINIA'}
{  kol1:=w1; kol2:=w2; w[3]:=w3; w[4]:=w4; w[5]:=w5; w[6]:=w6; w[7]:=w7; w[8]:=w8; w[9]:=w9;~~}
{   dru_sum1:=0; licz1:=1;~~}
{FI}
{ENTIRE}
{IF: vp=0 & dru_bar & lineleft>=2}{PRN.bar()}{ dru_bar:=0;~~}{ELIF: lineleft()=1}{PAGE}{FI}
{SUBSTITUTE: 'LINIA'}
{ENTIRE-}{ vp:=dru_bar:=0;~~}
{ {? cosum1<>s[1]
   || sum11:=sum21:=WYR.WN$2;
      sum12:=sum22:=WYR.MA$2;
      cosum1:=s[1];  cosum2:=s[2]
   |? cosum2<>s[2]
   || cosum2:=s[2];
      sum11+=WYR.WN$2; sum21:=WYR.WN$2;
      sum12+=WYR.MA$2; sum22:=WYR.MA$2
   || sum11+=WYR.WN$2; sum21+=WYR.WN$2;
      sum12+=WYR.MA$2; sum22+=WYR.MA$2
   ?};~~}
{FI}
{OD}
{IF: ~dru_sum2 & licz2>1}
{  w1:=kol1; w2:=kol2; w3:=w[3]; w4:=w[4]; w5:=w[5]; w6:=w[6]; w7:=w[7]; w8:=w[8]; w9:=w[9];
  kol2:='Suma: '+cosum2; kol1:=''; w[3]:=w[4]:=w[7]:=w[8]:=w[9]:='';
  w[5]:=form(sum21, , 2, ' ,'); w[6]:=form(sum22, , 2, ' ,');~~}
{SUBSTITUTE: 'LINIA'}
{  kol1:=w1; kol2:=w2; w[3]:=w3; w[4]:=w4; w[5]:=w5; w[6]:=w6; w[7]:=w7; w[8]:=w8; w[9]:=w9;~~}
{FI}
{IF: ~dru_sum1 & licz1>1}
{  w1:=kol1; w2:=kol2; w3:=w[3]; w4:=w[4]; w5:=w[5]; w6:=w[6]; w7:=w[7]; w8:=w[8]; w9:=w[9];
  kol1:='Suma: '+cosum1; kol2:=''; w[3]:=w[4]:=w[7]:=w[8]:=w[9]:='';
  w[5]:=form(sum11, , 2, ' ,'); w[6]:=form(sum12, , 2, ' ,');~~}
{SUBSTITUTE: 'LINIA'}
{  kol1:=w1; kol2:=w2; w[3]:=w3; w[4]:=w4; w[5]:=w5; w[6]:=w6; w[7]:=w7; w[8]:=w8; w[9]:=w9;~~}
{FI}
{IF: is_zap}
{ w[4]:='SUMA:'; w[5]:=form(sum1, , 2, ' ,'); w[6]:=form(sum2, , 2, ' ,'); s[1]:=s[2]:=w[1]:=w[2]:=w[3]:=w[7]:=w[8]:=w[9]:=kol1:=kol2:='';~~}
{IF: lineleft<>0 & lineleft<=4}{PAGE}{FI}
{ENTIRE}
{IF: vp=0}{PRN.bar()}{FI}
{SUBSTITUTE: 'LINIA'}
{PRN.lbar()}
{<1>{PRN.width()} 'KONIEC WYDRUKU'@}
{ENTIRE-}
{ELSE}
{PRN.lbar()}
{<1>{PRN.width()} 'BRAK REKORDÓW SPEŁNIAJĄCYCH KRYTERIA!'@}
{FI}
{COMMENT}------------------------------FOOTER-LAST------------------{COMMENT-}
{FOOTER}
{SUBSTITUTE: 'CHARFOOT'}
{~~}
{FOOTER-}

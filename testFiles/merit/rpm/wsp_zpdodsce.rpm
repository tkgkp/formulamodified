:!UTF-8
{TITLE:Wydruk etykiet towarowych - kody kreskowe}
{PRINTER: 'kody',,,,,'Q','B',,,,,,1,15,15,15,15,0,1}
{BEGIN:
   materean:="VAR_DEL.delete('temp','font','wybierz','dalej','wyjscie','etykieta')";
   materean();
   temp:='';
   font:='';
   wybierz:='';
   dalej:=0;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   BARCODE_TYPE:=exec('barcode_type','kody_kresk');
   BARCODE_H:=exec('barcode_height','kody_kresk');
   ZPARN.win_patt('REDM');
   etykieta:=PAR_WYDR.URZ_LAB;
   PAR_WYDR.URZ_LAB:=exec('wyb_etykiety','kody_kresk');
   wyjscie:={? PAR_WYDR.TYPDR<>null || PAR_WYDR.URZ_LAB=null ?};
   {? wyjscie=0 & PAR_WYDR.TYPDR=null & PAR_WYDR.KODKRESK=''
   || FUN.info('Brak parametryzacji drukowania kodów kreskowych.'@);
      wyjscie:=1
   ?};
   exec('ini_kom','#message','Wydruk etykiet towarowych - kody kreskowe'@);
   ZPARN.cntx_psh
}
{END:
   ZPARN.cntx_pop;
   VAR_DEL.delete('BARCODE_TYPE','BARCODE_H');
   exec('end_kom','#message');
   PAR_WYDR.URZ_LAB:=etykieta;
   materean(); VAR_DEL.delete('materean')
}
{EXIT: wyjscie=1}
{ wyjscie:=1;~~}
{DEFINEFONT: 'courier=TexGyreCursor,,,8'}
{FONT: 'courier'}
{MACRO:'stan'}
   {ENTIRE}
   {_op:=exec('formopis','kody_kresk','P',$ZPARN.ref(),0); _bop:=_op[1]; obj_del(_op); _bop}

    {IF: ZPARN.SCEAN<>''}
     {BARCODE: ZPARN.SCEAN;50;BARCODE_H;BARCODE_TYPE}


     {FONT: 'courier'}{SPACE: 'B'}
      {ZPARN.SCEAN}
   {FI}
  ─────────────────────────────────────
   {ENTIRE-}
{MACRO-}
{MACRO:'etykiety'}
{IF: ZPARN.SCEAN=''}
   { exec('add_kom','#message','Partia produkcyjna: %1'@[ZPARN.SYM],7,'Brak kodu kreskowego'@);~~}
{ELSE}
   { wyjscie:=0;~~}
   {IF: PAR_WYDR.TYPDR<>null}
      { wyjscie:=exec('druk','kody_kresk')=0;~~}
   {ELSE}
      {SUBSTITUTE: 'stan'}
   {FI}
{FI}
{MACRO-}
{EXIT: wybierz:=FUN.choice('Wydruk etykiet dla partii produkcyjnych'@,
                 ,' Bieżącego'@, 'Wszystkich'@, 'W&ybranych'@, 'Wg w&zorca'@);
 {? wybierz <> 0
 || {? wybierz=1
    || wybierz:='B'
    |? wybierz=2
    || wybierz:='W'
    |? wybierz=3
    || wybierz:='Y'
    |? wybierz=4
    || wybierz:='Z'
    ?};0
  || "# koniec #"; 1
 ?}
}
{IF:wybierz='B'}
   {SUBSTITUTE: 'etykiety'}
{ELIF:wybierz='Y'}
   {
   _wer:=ZPARN.mk_sel('Partie produkcyjne',,0,'parpfddppsdsadf');
   ZPARN.win_fld(_wer,,'SYM',,,20,,,'Symbol'@);
   ZPARN.win_fld(_wer,,'OPIS',,,20,,,'Opis'@);
   ZPARN.win_fld(_wer,,'SCEAN',,,30,,,'Kod kreskowy'@);
   _fbg:="
      VAR_DEL.delete('__Sel');
      __Sel:=ZPARN.sel_aget; ZPARN.sel_adel;
      {? __Sel.first=0
      || __Sel.REF:=#ZPARN.ref;
         __Sel.add
      ?};
      sel_exit
   ";
   ZPARN.win_act(_wer,,'Formuła','Akceptuj'@,,'Akceptacja wybranych pozycji'@,_fbg,,1,1,_fbg);

   ZPARN.win_sel(_wer);

   {? ZPARN.select=0
   || VAR_DEL.delete('__Sel');
      __Sel:=ZPARN.sel_aget; ZPARN.sel_adel
   ?};
   ZPARN.cntx_psh;
   ZPARN.prefix;
   ~~}
   {FOR: __Sel}
   {DO}
      {IF: ZPARN.seek(__Sel.REF,)}
         {SUBSTITUTE: 'etykiety'}
      {FI}
   {OD}
   {
   ZPARN.cntx_pop;
   VAR_DEL.delete('__Sel');
   ~~}
{ELIF:wybierz='W'}
   {FOR: 'ZPARN'}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{ELIF:wybierz='Z'}
   {FOR: 'ZPARN'}
   {FILTER}
   {DO}
      {SUBSTITUTE: 'etykiety'}
   {OD}
{FI}
{EXIT: {? wybierz='W' | wybierz='Z' || {? wyjscie=1 || FUN.info('Brak danych spełniających kryteria.'@); 1 ?} ?}}
{EXIT: wyjscie=1}
{EXIT: PAR_WYDR.TYPDR<>null}

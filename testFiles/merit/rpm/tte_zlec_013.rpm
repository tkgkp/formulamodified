:!UTF-8
{TITLE:M. Grupowe analizy zleceń}
{PRINTER:  exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR.A01:='1';
   VAR_DEL.delete('PRNPOJ','PRN');
   exec('tktl_cntx_psh','tech_common');
   ANWAR.cntx_psh();
   ANWAR.win_dict('SLO1');
   ANWAR.actions('SLO1','L');
   ANRUB.cntx_psh();
   MGR.cntx_psh();
   MGR.win_dict('SLO');
   KOMM.init(,,'Wykonane obliczenia'@);
   "---Obiekt wydruku godzin przepracownych na zleceniach.";
   PRN:=obj_new(@.CLASS.PRINT,6); PRN.sl_frame();
   ANRUB.clear();
   ANRUB.last();
   _nr:=ANRUB.size()+1;
   PRNPOJ:=obj_new(@.CLASS.PRINT,_nr); PRNPOJ.s_frame();
   exec('prep_tab','zl_kalk');
   ANWAR.clear();
   ANWAR.f_clear();
   ANWAR.f_set('NA',,'"ANWAR"."A01"=\':_a\'',VAR.A01);
   tryb:=rep_export();
   okrref:=null();
   kreska:='─';
   prn1:='PRN';
   o:=1;
   fnt:='8G';
   waslbar:=0;
   zero:=0;
   dalej:=0;
   color:='';
   zlecenie:='';
   lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
   exec('tktl_cntx_pop','tech_common');
   ANRUB.cntx_pop();
   ANWAR.cntx_pop();
   MGR.cntx_pop();
   obj_del(PRN);
   obj_del(PRNPOJ);
   obj_del(TABRUB);
   obj_del(TABPOJ);
   obj_del(numer);
   WYDRUK.GRUPA:=null();
   WYDRUK.WARIANT:=null();
   &okrref;
   &kreska;
   &zero;
   &o;
   &fnt;
   &waslbar;
   &prn1;
   &color;
   &zlecenie;
   &lm; &ll; &vp; &lmt; &rsep; &tryb
}
{COMMENT}
   Wydruk grupowej analizy zlecen.
  [MKO] - 2002/08/15 - [7.60]
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{COMMENT}
Jezeli nie zostanie wpisany wariant analizy to domyslny.
Jezeli nie zostanie wpisnany asortyment lub grupa to wszystkie.
{COMMENT-}
{  OKR.cntx_psh();
   OKR.index('NAZ');
   OKR.prefix();
   {? OKR.find_key(REF.FIRMA,ST.OKR) || okrref:=OKR.ref() ?};
   OKR.cntx_pop();
~~}
{  _wydr_naz:='zlec_013';
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add();
      WYDRUK.blank()
   || WYDRUK.GRUPA:=WYDRUKIL.MGR;
      WYDRUK.WARIANT:=WYDRUKIL.WARIANT;
      WYDRUK.POD:=WYDRUKIL.SR
   ?};
   WYDRUK.win_edit('RED14');
~~}
{IF: WYDRUK.edit("__CHK.record(WYDRUK,,'WARIANT')")}
{ WYDRUKIL.MGR:=WYDRUK.GRUPA;
  WYDRUKIL.WARIANT:=WYDRUK.WARIANT;
  WYDRUKIL.SR:=WYDRUK.POD;
  WYDRUKIL.put(1);
  {? WYDRUK.WARIANT=null
  || ZTP.index('RDTP');
     ZTP.prefix('P');
     ZTP.first();
     WYDRUK.WARIANT:=ZTP.DEF_OPCA
  ?};
  wariant:=WYDRUK.WARIANT;
  {? WYDRUK.POD<>'W' || fnt:='10'  || fnt:='8G' ?};
  _ok:=exec('spr_jm','zl_kalk');
  _zabr:='';
  AFORM.clear();
  AFORM.index('OR');
  AFORM.prefix(WYDRUK.WARIANT);
  {? AFORM.first()
  || {!
     |? TABRUB.NR:=AFORM.RUBR().NR;
        TABRUB.OPIS:=AFORM.RUBR().OPIS;
        TABRUB.FORM:=AFORM.FRM;
        TABRUB.SUMA:='N';
        TABRUB.AVG:='N';
        TABRUB.DRK:='T';
        {? _ok=0
        || {? TABRUB.FORM*'l('>0
           || _spr:=_zabr;
              STR.split(_zabr,',');
              {!
              |? _word:=STR.get_word();
                 {? TABRUB.FORM*('l('+_word+')')>0
                 || TABRUB.MOD:='N';
                    _zabr+=$TABRUB.NR+',';
                    0
                 || _spr:=(+_word+1)-_spr;
                    _spr<>''
                 ?}
              !};
              {? _spr=''
              || TABRUB.MOD:='T'
              ?}
           |? TABRUB.FORM*'made'>0 | TABRUB.FORM*'X'>0
           || TABRUB.MOD:='N';
              _zabr+=$TABRUB.NR+','
           || TABRUB.MOD:='T'
           ?}
        || TABRUB.MOD:='T'
        ?};
        TABRUB.add();
        AFORM.next()
     !}
  ?};
~~}
{IF: ~TABRUB.select()}
{EXIT}
{FI}
{ELSE}
{EXIT}
{FI}
{COMMENT}
Narazie tylko dla grupy.Asortyment brak
{COMMENT-}
{MACRO: 'tabp_zer'}
{TABPOJ.erase();~~}
{MACRO-}
{MACRO: 'tabr_zer'}
{  TABRUB.prefix();
   TABRUB.first();
   {!
   |? TABRUB.WAVG:=0;
      TABRUB.WSUMA:=0;
      TABRUB.put();
      TABRUB.next()
   !};
~~}
{MACRO-}
{MACRO:'dane'}
{
ZL.clear();
{? WYDRUK.GRUPA<>null()
|| _ndx:=ZL.ndx_tmp('',1,'HIDDEN',,,'ST_MJS',,,'MGR',,,'SYM',,);
   ZL.index(_ndx);
   ZL.prefix('N','Z',WYDRUK.GRUPA)
|| _ndx:=ZL.ndx_tmp('',1,'HIDDEN',,,'ST_MJS',,,'SYM',,);
   ZL.index(_ndx);
   ZL.prefix('N','Z')
?};
_can_continue:=1;
{? ZL.first()
|| FUN.prg_start(ZL.size(),'Grupowa analiza zleceń'@);
   {!
   |? exec('tktl_use','tech_common',{? ZL.RTKTL<>'' || ((8+ZL.RTKTL)+3) || ((8+ZL.RKTL)+3) ?});
      ZL.cntx_psh();
      ANZH.clear();
      ANZH.index('ZRO');
      ANZH.prefix('1',ZL.ref(),wariant,okrref,okrref);
      _ok:={? ~ANZH.first()
           || _ok:=exec('ANZH_new','zl_kalk',ZL.ref(),wariant);
              {? _ok || _ok:=exec('anal_one','zl_kalk',ANZH.ref(),1,,0) ?}
           || _ok:=1
           ?};
      {? _ok
      || ANZP.clear();
         ANZP.index('NRARUB');
         ANZP.prefix(ANZH.ref());
         {? ANZP.first()
         || 1
         || exec('anal_one','zl_kalk',ANZH.ref(),1,,0)
         ?};
         {!
         |? TABRUB.prefix(ANZP.RUBR().NR);
            {? TABRUB.first()
            || {? 'tT'*TABRUB.SUMA>0 | 'tT'*TABRUB.AVG>0
               || TABRUB.WSUMA+=ANZP.WAR$2;
                  TABRUB.LICZNIK+=1;
                  TABRUB.put()
               ?};
               {? WYDRUK.POD='W'
               || TABPOJ.ZLEC:=ZL.SYM;
                  TABPOJ.WSUMA:=ANZP.WAR;
                  TABPOJ.NRUB:=ANZP.RUBR().NR;
                  TABPOJ.add()
               ?}
            ?};
            ANZP.next()
         !}
      ?};
      ZL.cntx_pop();
      _can_continue:=FUN.prg_next(1);
      ZL.next() & _can_continue>0
   !};
   FUN.prg_stop()
?};
~~}
{MACRO-}
{"------------------------------------------------------------------------"; ~~}
{IF: WYDRUK.POD<>'W'}
{PRN.add("TABRUB.NR",5,'Numer'@,1); ~~}
{PRN.add("TABRUB.OPIS",35,'Opis'@); ~~}
{PRN.add("{? 'tT'*TABRUB.SUMA>0 || form(TABRUB.WSUMA,,4) || '--' ?}",25,'Suma'@,1); ~~}
{PRN.add("{? 'tT'*TABRUB.AVG>0 || form(TABRUB.WAVG,,4)|| '--' ?}",25,'Średnia'@,1); ~~}
{ELSE}
{FONT: 'T'+fnt}{SPACE: 'B'}
{<1 prn1:='PRNPOJ';
 TABRUB.cntx_psh();
 _i2:=TABRUB.ndx_tmp('',1,'DRK',,);
 TABRUB.index(_i2);
 TABRUB.prefix('T');
 o:=int((charleft()-35-TABRUB.size()*2)/TABRUB.size());
 TABRUB.cntx_pop();
 ~~}
 {IF: o<6}
 {FUN.emsg('Zbyt duża ilość rubryk do wydruku.\nMax. %1'@[form(int((charleft()-35-TABRUB.size()*2)/6))]);~~}
 {EXIT}
 {FI}
{PRNPOJ.add("zlecenie",15,'Zlecenie'@); ~~}
{FOR: TABRUB}
 {DO}
 {IF: 'Tt'*TABRUB.DRK>0}
   {PRNPOJ.add($( 'numer['+$(TABRUB.NR)+']'),o,form(TABRUB.NR,5,,'9.'),1); ~~}
 {FI}
 {OD}
{FI}
{"------------------------------------------------------------------------"; ~~}
{PAR_WYDR.TITLE:='GRUPOWE ANALIZY ZLECEŃ'@;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T'+fnt}{SPACE: 'B'}
{IF: WYDRUK.POD<>'W'}
{<1 lmt:=int((charleft()-PRN.width())/2); ~~}
{ELSE}
{ lmt:=int((charleft()-PRNPOJ.width())/2); ~~}
{FI}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Rok: %1 Okres: %2'@[$ST.AR,form(ST.AM,-2)]}
{<{lmt+1} {? WYDRUK.POD='W' || 'Wydruk wszystkich analiz'@ || 'Wydruk podsumowania'@ ?}}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{MACRO: 'drukuj'}
{TABRUB.prefix(); ~~}
{IF: WYDRUK.POD<>'W'}
  {IF: TABRUB.first()}{ dalej:=1; ~~}{FI}
   {<{lmt+1} 'Nazwa grupy towarowej: %1'@[WYDRUK.GRUPA().KOD]}
   {FONT: 'T'+fnt+'B'}{SPACE: 'B'}
   {SUBSTITUTE: 'TABHEAD'}
   {FONT: 'T'+fnt}{SPACE: 'B'}{ vp:=1; ~~}
  {WHILE: dalej}
  {DO}
    {{? TABRUB.LICZNIK<>0 & 'tT'*TABRUB.AVG>0 || TABRUB.WAVG:=(TABRUB.WSUMA/TABRUB.LICZNIK)$2;TABRUB.put() ?};~~}
    {IF: 'tT'*TABRUB.DRK>0}
      {IF: tryb | lineleft > 8} {SUBSTITUTE: 'LINIA0'}
      {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T'+fnt+'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
      {FI}
    {FI}
    {dalej:=TABRUB.next(); ~~}
  {OD}
  {SUBSTITUTE: 'LINIAF'}
{ELSE}
  {prn1:='PRNPOJ';TABPOJ.prefix();TABPOJ.first();zl:=TABPOJ.ZLEC;pierwszy:=1;~~}
  {FOR: TABPOJ}
  {DO}
    {IF: TABPOJ.ZLEC<>zl}
       {IF: pierwszy}
       {<{lmt+1} 'Nazwa grupy towarowej: %1'@[WYDRUK.GRUPA().KOD]}
       {FONT: 'T'+fnt+'B'}{SPACE: 'B'}
       {SUBSTITUTE: 'TABHEAD'}
       {FONT: 'T'+fnt}{SPACE: 'B'}{ vp:=1; ~~}
       {pierwszy:=0;~~}
       {FI}
       {IF: tryb | lineleft > 8} {SUBSTITUTE: 'LINIA0'}
       {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T'+fnt+'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
       {FI}
       {zl:=TABPOJ.ZLEC;zlecenie:=TABPOJ.ZLEC;numer[TABPOJ.NRUB]:=TABPOJ.WSUMA$2;~~}
    {ELSE}
      {zlecenie:=TABPOJ.ZLEC;numer[TABPOJ.NRUB]:=TABPOJ.WSUMA$2;~~}
    {FI}
  {OD}
  {IF: pierwszy=0}
  {zlecenie:='ŚREDNIA:'@;zero:=0;~~}
  {FOR: TABRUB}
  {DO}
    {{? TABRUB.LICZNIK<>0 & 'tT'*TABRUB.AVG>0 || TABRUB.WAVG:=(TABRUB.WSUMA/TABRUB.LICZNIK)$2;TABRUB.put();zero:=1?};~~}
    {{? TABRUB.WAVG<>0 || numer[TABRUB.NR]:=TABRUB.WAVG$2 ||numer[TABRUB.NR]:='--' ?};~~}
  {OD}
  {IF: zero=1}
    {IF: tryb | lineleft > 8} {SUBSTITUTE: 'LINIA0'}
    {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T'+fnt+'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
    {FI}
  {FI}
  {zlecenie:='SUMA'@;zero:=0;~~}
  {FOR: TABRUB}
  {DO}
  { {? 'tT'*TABRUB.SUMA>0 || numer[TABRUB.NR]:=TABRUB.WSUMA$2;zero:=1 || numer[TABRUB.NR]:='--' ?};~~}
  {OD}
  {IF: zero=1}
    {IF: tryb | lineleft > 8} {SUBSTITUTE: 'LINIA0'}
    {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T'+fnt+'B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T'+fnt}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
    {FI}
  {FI}
  {FI}
  {IF: pierwszy=0}
    {SUBSTITUTE: 'LINIAF'}
  {FI}
{FI}
{MACRO-}
{FONT: 'T'+fnt}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{IF: WYDRUK.GRUPA<>null()}
{SUBSTITUTE: 'dane'}
{SUBSTITUTE: 'drukuj'}
{ELSE}
{WYDRUK.GRUPA:=null()}
{SUBSTITUTE: 'dane'}
{SUBSTITUTE: 'drukuj'}
{SUBSTITUTE: 'tabp_zer'}
{FI}
{KOMM.select();~~}

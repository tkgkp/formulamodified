:!UTF-8
{TITLE:K. Wskaźnik rotacji}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','v'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_111:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('X_Xd','k','w','PRN','kgr');
      VAR_DEL.delete('wyjscie');
      VAR_DEL.delete('ilosc','wartosc','doklss','doklz','doklwr','wsokr');
      VAR_DEL.delete('j2','doklss2','doklz2','doklwr2')";
   mag_111();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   VAR_DEL.delete('k','w','PRN');
   k:=obj_new(10); "--szerokosci kolumn--";
   w:=obj_new(10); "--wartosci kolumn--";
   doklss:=doklz:=doklwr:=0; "--ilosc miejsc dziesietnych--";
   doklss2:=doklz2:=doklwr2:=prnj2:=0;
   PRN:=obj_new(@.CLASS.PRINT,11); PRN.s_frame();
   k[1]:=20;
   k[2]:=40;
   k[3]:=9;
   k[4]:=14;
   k[5]:=14;
   k[6]:=14;
   k[7]:=9;
   k[8]:=14;
   k[9]:=14;
   k[10]:=14;
   w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:=w[8]:=w[9]:=w[10]:='';
   tryb:=rep_export();
   kgr:='';
   j2:='N';

   M.cntx_psh();
   M.win_dict('NL_WER'); M.actions('NL_WER','W');

   wyjscie:=1;

   _wydr_naz:='mag_111';
   WYDRUKIL.KTM_OD:=WYDRUKIL.KTM_DO:=null;

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ?};
   {? WYDRUKIL.CHKBOX01=1 & (exec('upr_cm','ceny')=0 | ST.MAG().IL='T') || WYDRUKIL.CHKBOX01:=0 ?};

   WYDRUKIL.win_edit('MAG_111');
   {? WYDRUKIL.edit("
         _noupr:=exec('upr_cm','ceny')=0;
         {? ~_noupr & ST.MAG().IL='T' & WYDRUKIL.CHKBOX01=1
         || FUN.info('Magazyn tylko ilościowy zmieniono parametr Rodzaj na wydruk ilościowy.'@);
            WYDRUKIL.CHKBOX01:=0
         ?};
         {? WYDRUKIL.OD_DATY=date(0,0,0)
         || FUN.info('Należy podać Datę od'@); 'OD_DATY'
         |? WYDRUKIL.OD_DATY>WYDRUKIL.DO_DATY
         || FUN.info('Data początkowa musi być równa bądź wcześniejsza od daty końcowej.'@); 'OD_DATY'
         |? WYDRUKIL.CHKBOX01=1 & _noupr
         || FUN.info('Brak uprawnień do wartości magazynowych.'@); 'CHKBOX01'
         || 1
         ?}")
   || {? WYDRUKIL.KTM_OD=null | WYDRUKIL.KTM_DO=null
      || M.cntx_psh();
         M.index('RODZ');
         M.prefix('T');
         {? M.first()
         || WYDRUKIL.KTM_OD:=M.ref();
            M.last();
            WYDRUKIL.KTM_DO:=M.ref()
         || WYDRUKIL.KTM_OD:=null;
            WYDRUKIL.KTM_DO:=null
         ?};
         M.cntx_pop()
      ?};
      {? WYDRUKIL.KTM_OD().KTM>WYDRUKIL.KTM_DO().KTM || WYDRUKIL.KTM_OD==WYDRUKIL.KTM_DO ?};
      WYDRUKIL.put();
      wyjscie:=0;
      wsokr:=WYDRUKIL.DO_DATY-WYDRUKIL.OD_DATY+1;
      j2:={? WYDRUKIL.CHKBOX01=0 & WYDRUKIL.CHKBOX02=1 || 'T' || 'N' ?}
   ?};
   ilosc:=0; wartosc:=0;
   M.actions('SEL','');
   M.cntx_pop()
}
{END:
   mag_111(); VAR_DEL.delete('mag_111')
}
{EXIT: wyjscie=1}{ wyjscie:=1;~~}
{INCLUDE: wsp_pw.rpi}
{
X_Xd:=tab_tmp(3,
   'KTM','STRING[50]'  ,'Indeks materialu',
   'N'  ,'STRING[150]' ,'Nazwa materialu',
   'JM' ,'STRING[10]'  ,'jm',
   'SS' ,'REAL'        ,'Sredni stan',
   'Z'  ,'REAL'        ,'Zuzycie',
   'WR' ,'REAL'        ,'Wskaznik rotacji',
   'MGR','STRING[8]'   ,'Grupa materiałowa',
   'J2' ,'STRING[10]'  ,'jm',
   'SS2' ,'REAL'       ,'Sredni stan',
   'Z2'  ,'REAL'       ,'Zuzycie',
   'WR2' ,'REAL'       ,'Wskaznik rotacji');
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
PRN.add("w[1]",k[1],'Indeks'@);
PRN.add("w[2]",k[2],'Nazwa materiału'@);
PRN.add("w[3]",k[3],'jm'@);
PRN.add("form(w[4],,{? ~prnj2 || doklss || doklss2 ?})",k[4]
 ,{? WYDRUKIL.CHKBOX01 || 'Średni stan wartościowy'@ || 'Średni stan ilościowy'@ ?},1);
PRN.add("form(w[5],,{? ~prnj2 || doklz || doklz2 ?})",k[5]
 ,{? WYDRUKIL.CHKBOX01 || 'Zużycie wartościowe'@ || 'Zużycie ilościowe'@ ?},1);
PRN.add("form(w[6],,{? ~prnj2 || doklwr || doklwr2 ?})",k[6],'Wskaźnik rotacji'@,1);
{? j2='T' & tryb=1
|| PRN.add("w[7]",k[7],'jm'@);
   PRN.add("form(w[8],,doklss2)",k[8],'Średni stan ilościowy 2'@,1);
   PRN.add("form(w[9],,doklz2)",k[9],'Zużycie ilościowe 2'@,1);
   PRN.add("form(w[10],,doklwr2)",k[10],'Wskaźnik rotacji 2'@,1)
?};

PAR_WYDR.TITLE:={? WYDRUKIL.CHKBOX01 || 'WSKAŹNIK ROTACJI WARTOŚCIOWY'@ || 'WSKAŹNIK ROTACJI ILOŚCIOWY'@ ?};

prn1:='PRN';
~~}
{COMMENT}   drukowanie pozycji wydruku                               {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; ilosc:=0; wartosc:=0;~~}
{FOR: M}
{INDEX: 'RODZ'}
{FROM:'T',WYDRUKIL.KTM_OD().KTM,WYDRUKIL.KTM_OD().KTM}
{TO: 'T',WYDRUKIL.KTM_DO().KTM,WYDRUKIL.KTM_DO().KTM}
{DO}
   {
   "--obliczamy stan do danej daty--";
   _j2:=M.J2<>null();
   _rok:=WYDRUKIL.OD_DATY~1;
   _zapil:=_zapwa:=_stil:=_stwa:=0;
   _zapi2:=_sti2:=0;
   OKR.cntx_psh();
   OKR.index('MC'); OKR.prefix(REF.FIRMA,1);

   OKR.prefix(REF.FIRMA,1,_rok);
   {? OKR.first
   ||
      SM.cntx_psh();
      _rok_dk:=(_rok);
      _rok_dk:=form(form(_rok_dk,,,'99'),-4)+2;
      _rok_sm:=(_rok)-1;
      {? _rok_sm>0
      ||
         _rok_sm:=form(form(_rok_sm,,,'99'),-4)+2;
         SM.use('stm__'+ST.ODDZ+_rok_sm);
         SM.index('SM');
         SM.prefix(ST.MAG,M.ref);
         {? SM.first() || _stil:=SM.ST_IL; _stwa:=SM.ST_WAR; _sti2:={? _j2 || SM.ST_IL2 || 0 ?} ?}
      ?};
      SM.cntx_pop();

      _cont:=1;
      ND.cntx_psh();
      DK.cntx_psh();
      DK_L.cntx_psh();
      ND.use('nagdo'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
      DK.use('dokma'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
      DK_L.use('doklk'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
      DK.index('DOKMAT');
      DK.prefix('T',M.ref,ST.MAG,OKR.ROK);
      {? DK.first()
      || _data:=WYDRUKIL.OD_DATY;
         {!
         |? _data_nd:=DK.N().D;
            {? DK.M().RODZ='T' & _data_nd<=WYDRUKIL.DO_DATY
            || _ildn:=_data_nd-_data;
               {? _data_nd>WYDRUKIL.OD_DATY || _data:=_data_nd ?};
               {? _ildn & WYDRUKIL.OD_DATY<=_data_nd
               || _zapil+=_stil*_ildn;
                  _zapwa+=_stwa*_ildn;
                  _zapi2+=_sti2*_ildn
               ?};
               _stil+={? DK.PLUS='T' || DK.IL  || -1*DK.IL ?};
               _stwa+={? DK.PLUS='T' || DK.WAR || -1*DK.WAR ?};
               _sti2+={? _j2 || {? DK.PLUS='T' || DK.IL2 || -1*DK.IL2 ?} || 0 ?}
            |? _data_nd>WYDRUKIL.DO_DATY
            || _cont:=0
            ?};
            _cont=1 & DK.next()
         !};
         _ildn:=(WYDRUKIL.DO_DATY-_data)+1;
         {? _ildn
         || _zapil+=_stil*_ildn;
            _zapwa+=_stwa*_ildn;
            _zapi2+=_sti2*_ildn
         ?}
      ?};
      DK.cntx_pop();
      ND.cntx_pop();
      DK_L.cntx_pop()
   ?};
   OKR.cntx_pop();
   _dokl:=exec('jaka_dok_m','jm',M.ref());
   _dokl_s:={? _j2 || exec('jaka_dok_mjm','jm',M.ref(),M.J2,M.J) || 0 ?};
   X_Xd.SS:={? WYDRUKIL.CHKBOX01 || _zapwa/wsokr$2 || _zapil/wsokr$_dokl ?};
   X_Xd.SS2:={? _j2 || _zapi2/wsokr$_dokl || 0 ?};

   "--obliczamy rozchod w zakresie dat--";
   ilosc:=0; wartosc:=0;
   exec('stan_zest','magazyn_stan',ST.MAG,M.ref,WYDRUKIL.DO_DATY,1,"ilosc","wartosc",1,WYDRUKIL.OD_DATY);
   X_Xd.Z:={? WYDRUKIL.CHKBOX01 || -wartosc$2 || -ilosc$_dokl ?};
   X_Xd.Z2:={? _j2 || -HELP.SIL2$_dokl_s || 0 ?};

   X_Xd.KTM:=form(M.KTM);
   X_Xd.MGR:=form(M.MGR().KOD);
   X_Xd.N:=form(M.N);
   X_Xd.JM:=form(M.J().KOD);
   X_Xd.J2:={? _j2 || form(M.J2().KOD) || '' ?};
   X_Xd.WR:=
      {? WYDRUKIL.RADIOB_1=0
      || {? X_Xd.Z>0 || X_Xd.SS*wsokr/X_Xd.Z || 0 ?}$3
      || {? X_Xd.SS>0 || X_Xd.Z/X_Xd.SS || 0 ?}$3
      ?};
   X_Xd.WR2:=
    {? _j2
    || {? WYDRUKIL.RADIOB_1=0
       || {? X_Xd.Z2>0 || X_Xd.SS2*wsokr/X_Xd.Z2 || 0 ?}$3
       || {? X_Xd.SS2>0 || X_Xd.Z2/X_Xd.SS2 || 0 ?}$3
       ?}
    || 0
    ?};
   ~~}
   {IF: X_Xd.Z<>0 | X_Xd.SS<>0}
      { X_Xd.add;~~}
   {FI}
{OD}
{EXIT: {? X_Xd.first || 0 || FUN.info('Brak danych spełniających kryteria.'@); 1 ?}}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'W okresie: od %1 do %2'@[form(WYDRUKIL.OD_DATY),form(WYDRUKIL.DO_DATY)]}
{<{lmt+1}}{'Zakres materiałów: od %1 do %2'@[form(WYDRUKIL.KTM_OD().KTM),form(WYDRUKIL.KTM_DO().KTM)]}
{IF:j2='T'}{<{lmt+1}}{'Druga jednostka ewidencyjna'@}{FI}
{IF: WYDRUKIL.RADIOB_1=0}
   {<{lmt+1}}{'WSKAŹNIK ROTACJI W DNIACH = ( ŚREDNI STAN * %1 DNI) / ZUŻYCIE'@[$wsokr]}
{ELSE}
   {<{lmt+1}}{'WSKAŹNIK ROTACJI W RAZACH = ( ZUŻYCIE / ŚREDNI STAN )'@}
{FI}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   WYDRUK                                                   {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{FOR: X_Xd}
{DO}
   {FIRST}
   {
   doklss:={? WYDRUKIL.CHKBOX01 || 2 || exec('fld_prec','#field',X_Xd,'SS') ?};
   doklz:={? WYDRUKIL.CHKBOX01 || 2 || exec('fld_prec','#field',X_Xd,'Z') ?};
   doklwr:=exec('fld_prec','#field',X_Xd,'WR');
   doklss2:={? j2='T' || exec('fld_prec','#field',X_Xd,'SS2') || 0 ?};
   doklz2:={? j2='T' || exec('fld_prec','#field',X_Xd,'Z2') || 0 ?};
   doklwr2:=exec('fld_prec','#field',X_Xd,'WR2');
   ~~}
   {FIRST-}
   {
   prnj2:=0;
   w[1]:=X_Xd.KTM;
   w[2]:=X_Xd.N;
   w[3]:=X_Xd.JM;
   w[4]:=X_Xd.SS;
   w[5]:=X_Xd.Z;
   w[6]:=X_Xd.WR;
   kgr:=X_Xd.MGR;
   w[7]:=X_Xd.J2;
   w[8]:=X_Xd.SS2;
   w[9]:=X_Xd.Z2;
   w[10]:=X_Xd.WR2;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
   {IF: j2='T' & X_Xd.J2<>'' & tryb<>1}
      {
      prnj2:=1;
      rsep:=0;
      w[1]:='';
      w[2]:='';
      w[3]:=X_Xd.J2;
      w[4]:=X_Xd.SS2;
      w[5]:=X_Xd.Z2;
      w[6]:=X_Xd.WR2;
      ~~}
      {SUBSTITUTE: 'LINIA0'}
      { rsep:=PAR_WYDR.RSEP;~~}
   {FI}
{OD}
{IF: tryb=1}
   {SUBSTITUTE: 'LINIAF'}
{FI}
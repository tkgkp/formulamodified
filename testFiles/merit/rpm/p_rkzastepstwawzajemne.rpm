:!UTF-8
{TITLE:Zastępstwa wzajemne}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('PRN','PRH','PR','prn1','prn2','vp','lmt','rsep','h_rat','Nr');
   VAR_DEL.delete('__TRACK','__PZ','mainrec','skip','ftm','ok','counter','prog');

   show_selection:=(~(var_pres('show_selection')>0) & ~(var_pres('typ')>0));
   {? show_selection
   || SLO_KOD.cntx_psh();
      SLO_KOD.index('NAZWA');
      _slo_typ:=exec('FindInSet','#table','SLO_TYP','SYMBOL','TYPPOZ','TYPPOZ',,1,,null);
      SLO_KOD.prefix(_slo_typ);
      _sel:=obj_new('current','temp');
      _sel.current:=SLO_KOD.win_sel('?');
      _size:=SLO_KOD.size();
      _length:={? _size>9 || _size || 10 ?};
      _sel.temp:=SLO_KOD.mk_sel('Wybór typu pełnionych obowiązków'@,,0,'#wybtyppob',,,_length,,'U');
      SLO_KOD.win_fld(_sel.temp,,'KOD',,,10);
      SLO_KOD.win_fld(_sel.temp,,'NAZWA',,,30);
      SLO_KOD.win_fld(_sel.temp,,'SYSTEM',,,3);
      SLO_KOD.win_act(_sel.temp,,'Formuła','Wybierz'@,,,"sel_exit()",,1,,,,'W');
      _btn_cfg:=',btn_label_align=center,panel=right,align=begin';
      _btn_ok:=SLO_KOD.win_btn(_sel.temp,'text=%1%2'['Wybierz'@,_btn_cfg],'menu:W');
      SLO_KOD.win_btn(_sel.temp,'text=%1%2'['Anuluj'@,_btn_cfg],'key:Esc');
      SLO_KOD.btn_opt(_btn_ok,'default=1');
      SLO_KOD.win_sel(_sel.temp);
      typ:={? SLO_KOD.select() || SLO_KOD.ref() || null ?};
      {? _sel.current<>''
      || SLO_KOD.win_sel(_sel.current);
         SLO_KOD.win_del(_sel.temp)
      ?};
      SLO_KOD.cntx_pop()
   || {? ~(var_pres('typ')>0) || typ:=null ?}
   ?};

   P.cntx_psh(); OSOBA.cntx_psh(); UD_SKL.cntx_psh();
   P.prefix(); OSOBA.prefix(); UD_SKL.prefix();

   PAR_WYDR.TITLE:='Zastępstwa wzajemne'@;
   {? rep_export()
   || PRH:=obj_new(@.CLASS.PRINT,10)
   || PRH:=obj_new(@.CLASS.PRINT,11);
      PRH.add("Nr+=1",3,'Lp.'@,1,,,,,',,\'9.\'')
   ?};

   PRH.add("''",13,'Jedn. org. zastępowanego'@);
   {? rep_export()
   || PRH.add("''",20,'Nazwisko pracownika zastępowanego'@);
      PRH.add("''",10,'Imię pracownika zastępowanego'@)
   || PRH.add("''",30,'Nazwisko i imię pracownika zastępowanego'@)
   ?};
   PRH.add("''",15,'Nr teczki pracownika zastępowanego'@);

   PRH.add("''",13,'Jedn. org. zastępującego'@);
   {? rep_export()
   || PRH.add("''",20,'Nazwisko pracownika zastępującego'@);
      PRH.add("''",10,'Imię pracownika zastępującego'@)
   || PRH.add("''",30,'Nazwisko i imię pracownika zastępującego'@)
   ?};
   PRH.add("''",15,'Nr teczki pracownika zastępującego'@);

   PRH.add("''",11,'Okres od'@);
   PRH.add("''",11,'Okres do'@);

   PRH.s_frame();

   {? rep_export()
   || PRN:=obj_new(@.CLASS.PRINT,10)
   || PRN:=obj_new(@.CLASS.PRINT,11);
      PRN.add("Nr+=1",3,'',1,,,,,',,\'9.\'')
   ?};

   PRN.add("__TRACK.P_WYDZ",13,'');
   {? rep_export()
   || PRN.add("__TRACK.P_NAZW",20,'',,,"{? __TRACK.OSOBA=$mainrec || '255:0:0' || '0:0:0' ?}");
      PRN.add("__TRACK.P_IMIE",10,'',,,"{? __TRACK.OSOBA=$mainrec || '255:0:0' || '0:0:0' ?}")
   || PRN.add("'%1 %2'[__TRACK.P_NAZW,__TRACK.P_IMIE]",30,'',,,"{? __TRACK.OSOBA=$mainrec || '255:0:0' || '0:0:0' ?}")
   ?};
   PRN.add("__TRACK.P_T",15,'',1);

   PRN.add("__TRACK.PZ_WYDZ",13,'');
   {? rep_export()
   || PRN.add("__TRACK.PZ_NAZW",20,'',,,"{? __TRACK.OSOBAZ=$mainrec || '255:0:0' || '0:0:0' ?}");
      PRN.add("__TRACK.PZ_IMIE",10,'',,,"{? __TRACK.OSOBAZ=$mainrec || '255:0:0' || '0:0:0' ?}")
   || PRN.add("'%1 %2'[__TRACK.PZ_NAZW,__TRACK.PZ_IMIE]",30,'',,,
         "{? __TRACK.OSOBAZ=$mainrec || '255:0:0' || '0:0:0' ?}"
      )
   ?};
   PRN.add("__TRACK.PZ_T",15,'',1);

   PRN.add("$__TRACK.OD",11,'');
   PRN.add("$__TRACK.DO",11,'');

   PRN.s_frame();

   prn1:='PRH';
   prn2:='PRN';
   rsep:=PAR_WYDR.SEP;
   lmt:=Nr:=skip:=prog:=0;
   vp:=h_rat:=ok:=1;

   __PZ:=tab_tmp(1,'P','STRING[16]','P')
}
{END:
   UD_SKL.cntx_pop(); OSOBA.cntx_pop(); P.cntx_pop();

   VAR_DEL.delete('PRN','PRH','PR','prn1','prn2','vp','lmt','rsep','h_rat','Nr');
   VAR_DEL.delete('__TRACK','__PZ','mainrec','skip','ftm','ok','counter','prog','typ','show_selection')
}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'HEADER'}
   {FONT: 'T8B'}
   {SPACE: 'B'}
   {LINE}
   {  prn1:='PRH'; prn2:='PRN'; ~~}
   {SUBSTITUTE: 'TABHEAD2'}
   {  prn1:='PRN'; prn2:='PRH'; ~~}
{MACRO-}
{MACRO: 'TABHEAD2'}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn1+'.ini_head()'; ($(_f))()}
   {REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
   {WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
   {DO}
      {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
   {OD}
   { _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
   { _f:=prn2+'.ini_head()'; ($(_f))()}
   {WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
   {DO}
      {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
   {OD}
{MACRO-}
{MACRO: 'tresc'}
   {FONT: 'T8'}
   { prn1:='PRN'; prn2:='PRH'; ~~}
   {SPACE: 'C'}
   {SUBSTITUTE: 'LINIA0'}
   {SPACE: 'B'}
   {IF: ~rep_export() & lineleft()<=8}
      {SUBSTITUTE: 'LINIAF'}
      {SUBSTITUTE: 'FOOT'}
      {PAGE}
      {SUBSTITUTE: 'HEAD'}
   {FI}
{MACRO-}
{MACRO: 'PGHEAD'}
   {IF: ftm}
      {ftm:=0; ~~}
      {FONT: 'M'}
      {SPACE: 'B'}
      {SUBSTITUTE: 'HEADER'}
   {FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{EXIT: ~typ}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRH.width())/2); ~~}
{FOR: P_PZ}{INDEX: 'P_PZ2'}{PREFIX: 'Z',typ}
{DO}
   { skip:=0;
     P.cntx_psh();
     {? _loop:=__PZ.first()
     || {!
        |? _loop
        |!
           {? __PZ.P=$P_PZ.P().ref
           || skip:=1
           ?};
           _loop:=__PZ.next()
        !}
     ?};
     {? ~_loop | ~skip
     || __PZ.blank(1);
        __PZ.P:=$P_PZ.P().ref;
        __PZ.add(1)
     ?};
     P.cntx_pop();
     ~~
   }
   {IF: ~skip}
      { {? var_pres('__LIST')>100 || VAR_DEL.delete('__LIST') ?};
        __LIST:=exec('track_all_looped','portal_zaslu');
        ftm:=1;
        Nr:=0;
        mainrec:=P_PZ.P_OSO;
        ~~
      }
      {IF: __LIST.SIZE}
         {IF: ~rep_export()}
            {SUBSTITUTE: 'HEAD'}
         {FI}
         { P.cntx_psh();
           P.seek(P_PZ.P,,1); ~~
         }
         {<{lmt}}{INCLUDE: p_prac.rpi}
         { P.cntx_pop(); ~~}
         {SUBSTITUTE: 'PGHEAD'}
         { ok:=__LIST.first(); ~~}
         { counter:=0; prog+=1; ~~}
         {WHILE: ok}
         {DO}
            { {? var_pres('__TRACK')>100 || obj_del(__TRACK) ?};
              __TRACK:=__LIST.get();
              ~~
            }
            {IF: __TRACK.size()}
               {FOR: __TRACK}
               {DO}
                  {SUBSTITUTE: 'tresc'}
               {OD}
               { counter+=1; ~~}
               {SUBSTITUTE: 'LINIAF'}
               {IF: counter<__LIST.size()}
                  { Nr:=0; ~~}
                  {SUBSTITUTE: 'HEADER'}
               {FI}
            {FI}
            { ok:=__LIST.next(); ~~}
         {OD}
         {IF: rep_export()}
            {LINE}
         {FI}
         {SUBSTITUTE: 'FOOT'}
         {PAGE}
      {FI}
   {FI}
   {IF: rep_export()}
      {LINE}
   {FI}
{OD}
{IF: rep_export()}
   {TABLE-}
{FI}
{IF: ~prog}
   {FUN.info('Dla wybranego typu pełnionych obowiązków nie znaleziono zapętleń w strukturze zastępstw.'@); ~~}
{FI}
{EXIT: ~prog}
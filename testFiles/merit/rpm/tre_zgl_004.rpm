:!UTF-8
{TITLE:D. Raport awaryjności zasobu}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MBOT,PAR_WYDR.MLEFT,
      PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   dalej:=0;
   {? var_press('PRN') > 0 || obj_del(PRN) ?};
   {? var_press('PRNS') > 0 || obj_del(PRN) ?};
   PRN:=obj_new(@.CLASS.PRINT,7); PRN.s_frame();
   PRNS:=obj_new(@.CLASS.PRINT,4); PRNS.s_frame();
   prn1:='PRN'; prn2:='PRNS';
   tryb:=rep_export();
   color:='';
   vp:=lmt:=rsep:=0;
   d1:=date(0,0,0);
   d2:=date(0,0,0);
   filtr_kat:=0;
   godziny:=0;
   dni:=0;
   koszty:=0;
   q_data:='';
   q_filtr:='';
   q_zas:='';
   czy_where:=0;
   kategorie:='';
   wyjscie:=1;
   zas_naz:='';
   zas_sym:='';
   default:='';
   {? var_press('query3') > 0 || obj_del(query3) ?};
   {? var_press('query2') > 0 || obj_del(query2) ?};
   {? var_press('query') > 0 || obj_del(query) ?}
}
{END:
   undefine();
   &dalej;
   obj_del(PRN);
   obj_del(PRNS);
   &prn1;
   &color;
   &filtr_kat;
   &vp; &lmt; &rsep;
   &tryb;
   &d1;
   &d2;
   &godziny;
   &dni;
   &koszty;
   &q_data;
   &q_filtr;
   &wyjscie;
   &czy_where;
   &kategorie;
   &zas_naz;
   &zas_sym;
   &default;
   {? var_press('query2') > 0 || obj_del(query2);&query2 ?};
   {? var_press('query3') > 0 || obj_del(query3);&query3 ?};
   {? var_press('query') > 0 || obj_del(query);&query ?}
}
{COMMENT}
  Wydruk listy zgłoszeń remontowych dla danego zasobu
{COMMENT-}
{COMMENT}-----------------------Wykonanie akcji inicjalizujacych.---{COMMENT-}
{_wydr_naz:='zgl_004';
   REM_ZAS.prefix();
   _edit:=WYDRUKIL.mk_edit('Parametry zestawienia'@,,'zgl_004',,,,);
   WYDRUKIL.win_esep(_edit,'Parametry wydruku'@);
   WYDRUKIL.win_efld(_edit,REM_ZGL,'REM_ZAS','SYMBOL','*',11,,0,'Zasób'@,,,,'');
   WYDRUKIL.efld_opt(_edit,'mark=1',REM_ZGL,'REM_ZAS','SYMBOL');
   WYDRUKIL.win_efld(_edit,,'CHKBOX01',,,,,,'Filtrowanie'@,,,'check-box','check_label=kategorii zgłoszeń',"1","0");
   WYDRUKIL.win_esep(_edit,'Data wystąpienia awarii'@);
   WYDRUKIL.win_efld(_edit,,'OD_DATY',,,,,,'Od'@);
   WYDRUKIL.win_efld(_edit,,'DO_DATY',,,,,,'Do'@);
   exec('ok_esc','#window',WYDRUKIL,_edit);
   default:= REM_ZAS.actions('WER_SLO','DPUZBYŃ:D');
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ?};
   REM_ZGL.blank();
   REM_ZGL.ODDZ:=ST.ODDZ;
   WYDRUKIL.win_edit(_edit);
   REM_ZGL.fld_fml('REM_ZAS','BEFORE_EDIT',"");
   REM_ZGL.fld_fml('REM_ZAS','AFTER_EDIT',"");
   _sort:='SYMBOL';
   _from:='';
   _where:='REM_ZAS.AKT=\'T\' ';
   {? ST.ODDZ<>'' || _where+='and REM_ZAS.ODDZ=\'%1\''[REM_ZGL.ODDZ] ?};
   REM_ZAS.f_set(_sort,_from,_where);
~~}
{IF: WYDRUKIL.edit($("{? WYDRUKIL.OD_DATY=date(0,0,0) & WYDRUKIL.DO_DATY<>date(0,0,0)
                  || FUN.info('"+'Należy podać obie daty albo żadnej.'@+"'); 'OD_DATY'
                  |? WYDRUKIL.OD_DATY<>date(0,0,0) & WYDRUKIL.DO_DATY=date(0,0,0)
                  || FUN.info('"+'Należy podać obie daty albo żadnej.'@+"'); 'OD_DATY'
                  |? WYDRUKIL.DO_DATY<WYDRUKIL.OD_DATY
                  || FUN.info('"+'Data końcowa nie może być wcześniejsza niż początkowa.'@+"'); 'OD_DATY'
                  |? REM_ZGL.REM_ZAS().SYMBOL = ''
                  || FUN.info('"+'Należy wybrać zasób.'@+"'); 'REM_ZAS'
                  |? WYDRUKIL.CHKBOX01=1
                  || _data:='';
                     {? WYDRUKIL.OD_DATY<>date(0,0,0)
                     || _data := 'and ZGL.DT_WA > to_date('+''''+$WYDRUKIL.OD_DATY+''''+') and ZGL.DT_WA < to_date('+''''+$WYDRUKIL.DO_DATY+''''+') '
                     ?};
                     _sym:= REM_ZGL.REM_ZAS().SYMBOL;
                     {? var_press('query3') > 0 || obj_del(query3) ?};
                     query3:=sql('
                                 select
                                     DISTINCT KAT.SYMBOL
                                 from
                                    REM_ZGL ZGL
                                    join REM_KATG KAT using (ZGL.REM_KATG,KAT.REFERENCE)
                                    join REM_ZAS ZAS using (ZGL.REM_ZAS,ZAS.REFERENCE)
                                 where
                                   ZAS.SYMBOL='+''''+_sym+''''
                                   +_data
                                 );
                     {? query3.size>0
                     || 1
                     || FUN.info('"+'Brak zgłoszeń dla wybranych parametrów zestawienia.'@+"'); 'REM_ZAS'
                     ?}
                  || 1
                  ?}"))}
{
   {? WYDRUKIL.put()
   || wyjscie:=0;
      REM_ZGL.fld_fml('REM_ZAS','BEFORE_EDIT',"*");
      REM_ZGL.fld_fml('REM_ZAS','AFTER_EDIT',"*");
      REM_ZAS.actions('WER_SLO',,default);
      d1:=WYDRUKIL.OD_DATY;
      d2:=WYDRUKIL.DO_DATY;
      zas_naz:=REM_ZGL.REM_ZAS().NAZWA;
      zas_sym:=REM_ZGL.REM_ZAS().SYMBOL;
      filtr_kat:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?}
   ?};
~~}
{ELSE}
{REM_ZAS.actions('WER_SLO',,default);
REM_ZGL.fld_fml('REM_ZAS','BEFORE_EDIT',"*");
REM_ZGL.fld_fml('REM_ZAS','AFTER_EDIT',"*");~~}
{EXIT}
{FI}
{COMMENT}----------------OKREŚLANIE WARUNKÓW SQL----------------{COMMENT-}
{
      {? d1 <> date(0,0,0)
      || {? czy_where=1 || q_data:=' and ' || q_data:=' where '; czy_where:=1 ?};
         q_data+=' ZGL.DT_WA > to_date(\''+$d1+'\') and ZGL.DT_WA < to_date(\''+$d2+'\') '
      || q_data:=''
      ?};
      _symbol:=REM_ZGL.REM_ZAS().SYMBOL;
      {? _symbol=''
      || q_zas:=''
      || {? czy_where=1 || q_zas:=' and ' || q_zas:=' where ';     czy_where:=1 ?};
         q_zas+=' ZAS.SYMBOL = \''+_symbol+'\''; czy_where:=1
      ?};~~
}
{COMMENT} -------------------FUNKCJA FILTRUJĄCA-----------------------{COMMENT-}
{
ZAKR.ACCESS:='N';
VAR_DEL.delete('Tab_Kat');
{? filtr_kat='T'
||
      wyjscie:=1;
      query2:=sql('
                  select
                     DISTINCT KAT.SYMBOL
                  from
                     REM_ZGL ZGL
                     join REM_KATG KAT using (ZGL.REM_KATG,KAT.REFERENCE)
                     join REM_ZAS ZAS using (ZGL.REM_ZAS,ZAS.REFERENCE)'
                  +q_data+q_zas
      );
      _acr_sel:=query2.mk_sel('Kategorie zgłoszseń'@,'P',0,'filtr_kat',,,,,'U');
      query2.win_fld(_acr_sel,ZAKR,'ACCESS',,,,,,,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
      query2.win_fld(_acr_sel,,'SYMBOL',,,25,,0,'Symbol'@);
      query2.win_act(_acr_sel,0,'Formuła','Wybierz'@@,,'Wybór pozycji'@,
         "
         {? var_pres('Tab_Kat')=-1
         || Tab_Kat:=tab_tmp(2
               ,'SYMBOL'     ,'STRING[100]',
               ,'REF'      ,'STRING[16]',
               ,'REFI'     ,'INTEGER',)
         ?};
         Tab_Kat.prefix();
         Tab_Kat.blank();
         {? ~Tab_Kat.find_tab(,'REF',,'=',$query2.ref())
         ||
            Tab_Kat.SYMBOL:=query2.SYMBOL;
            Tab_Kat.REF:=$query2.ref();
            Tab_Kat.REFI:=#query2.ref;
            Tab_Kat.add(1)
         ?}
         ",,1,1,,,'W');
      query2.win_act(_acr_sel,0,'Formuła','Odznacz'@@,,'Rezygnacja z wybóru pozycji'@,
         "
         {? var_pres('Tab_Kat')=-1
         || Tab_Kat:=tab_tmp(2
               ,'SYMBOL'     ,'STRING[100]',
               ,'REF'      ,'STRING[16]',
               ,'REFI'     ,'INTEGER',)
         ?};
         Tab_Kat.clear();
         {? Tab_Kat.find_tab(,'REF',,'=',$query2.ref()) || Tab_Kat.del() ?}
         ",,,1,,,'O');
      query2.win_act(_acr_sel,0,'Formuła','Akceptuj'@@,,'Akceptacja wyboru pozycji'@,"sel_exit");
      query2.win_act(_acr_sel,0,'Rekord',,,,"
      {? ~(var_pres('Tab_Kat')=-1)
      ||
         ZAKR.ACCESS:='N';
         {? var_pres('Tab_Kat')>117
         || {| Tab_Kat
            |! cntx_psh();
               _ndx:=ndx_tmp(,,'REF',,0);
               index(_ndx);
               prefix($query2.ref(),);
               {? first() || ZAKR.ACCESS:='T' ?};
               ndx_drop(_ndx);
               cntx_pop()
            |}
         ?}
      ?};
      _tab:=cur_tab();
      {? ZAKR.ACCESS='N' | _tab.sel_size()
      || _tab.actions(_tab.win_sel('?'),,'W',1);
         {? _tab.sel_size()<1
         || _tab.actions_grayed(_tab.win_sel('?'),'O')
            || _tab.actions_grayed(_tab.win_sel('?'),'')
         ?}

      || _tab.actions(_tab.win_sel('?'),,'O',1);
         {? _tab.sel_size()<1
         || _tab.actions_grayed(_tab.win_sel('?'),'W')
         || _tab.actions_grayed(_tab.win_sel('?'),'')
         ?}
      ?};
      ~~");
      query2.win_sel(_acr_sel);
      query2.select()=0 |
            {? var_pres('Tab_Kat')=-1
            ||  FUN.info('Nic nie zostało wybrane.\nZestawienie niemożliwe.'@); wyjscie:=1
            || {? (Tab_Kat.clear(); ~Tab_Kat.size)
               || FUN.info('Nic nie zostało wybrane.\nZestawienie niemożliwe.'@); wyjscie:=1
               || wyjscie:=0
               ?}
            ?}
?};
~~
}
{IF:wyjscie=1}{EXIT}{FI}
{COMMENT}--------Definicja wiersza wydruku zgłoszeń rementowych.---{COMMENT-}
{
  PRN.add("query.SYM",15,'Symbol zgłoszenia'@);
  PRN.add("$query.DATA",15,'Data zgłoszenia'@);
  PRN.add("query.KAT",15,'Kategoria zgłoszenia'@);
  PRN.add("query.STATUS",15,'Status rejestracji'@);
  PRN.add("query.DNI",15,'Czas serwisu (dni)'@,1,,,,'DNI',',2');
  PRN.add("query.GODZINY",15,'Godziny realizacji'@,1,,,,'GODZ',',5');
  PRN.add("query.KOSZTY",20,'Koszty realizacji'@,1,,,,'KOSZT',',2');

  PRNS.add("'Razem'",69,''@);
  PRNS.add("form(PRN.sum('DNI'),,2)",15,''@,1);
  PRNS.add("form(PRN.sum('GODZ'),,5)",15,''@,1);
  PRNS.add("form(PRN.sum('KOSZT'),,2)",20,''@,1);

  PAR_WYDR.TITLE:='RAPORT AWARYJNOŚCI ZASOBU'@;
~~}
{COMMENT}----------------OKREŚLANIE WARUNKÓW FILTRA---------------{COMMENT-}
{IF: filtr_kat='T'}
{
  {? var_pres('Tab_Kat')<>-1
  ||
     {? Tab_Kat.first()
     || {!|?
            kategorie+='\''+Tab_Kat.SYMBOL+'\''+',';
            Tab_Kat.next()
        !};
        kategorie:=kategorie-1
     ?};
     {? czy_where=1 || q_filtr:=' and ' || q_filtr:='where '; czy_where:=1 ?};
     q_filtr+='KAT.SYMBOL in (' + kategorie + ') '
  ?};
~~}
{ELSE}
   { q_filtr:='';
   ~~}
{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1}}{'Oddział:'@}{<{lmt+1+30}ST.ODDZ}
{IF: zas_naz <> ''}
{<{lmt+1}}{'Wybrany zasób:'@}{<{lmt+1+30}zas_naz+' ('+zas_sym+')'}
{ELSE}
{<{lmt+1}}{'Wybrany zasób:'@}{<{lmt+1+30}'Wszystkie'@}
{FI}
{IF: filtr_kat = 'T'}
{<{lmt+1}}{'Wybrane kategorie:'@}{<{lmt+1+30}gsub(kategorie,'\'','')}
{ELSE}
{<{lmt+1}}{'Wybrane kategorie:'@}{<{lmt+1+30}'Wszystkie'@}
{FI}
{IF: d1 <> date(0,0,0)}
{<{lmt+1+30}}{'Od:'@}{<{lmt+1+50}'Do:'@}
{<{lmt+1}}{'Data wystąpienia awarii:'@}{<{lmt+1+30}d1}{<{lmt+1+50}d2}
{ELSE}
{<{lmt+1}}{'Data wystąpienia awarii:'@}{<{lmt+1+30}'Dowolna'@}
{FI}
{FONT: 'T8B'}
{ vp:=0; ~~}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{vp:=1;~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE:'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE:'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE:'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE:'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT:'T8'}{SPACE:'B'}
{rsep:=PAR_WYDR.RSEP; ~~}
{query:=sql('
             select
                ZAS.SYMBOL,
                ZGL.SYM as SYM,
                ZGL.STAT_REJ as STATUS,
                count(DISTINCT ZAS.SYMBOL) AS ILE,
                (cast(ZGL.DT_ZAM as REAL_TYPE) +cast(ZGL.TM_ZAM as REAL_TYPE)/(24*60)
                -cast(ZGL.DT_WA as REAL_TYPE) -cast(ZGL.TM_WA as REAL_TYPE)/(24*60)
                ) as DNI,
                sum(DISTINCT GRG.IL) as GODZINY,
                sum(DISTINCT PFAP.W) as KOSZTY,
                KAT.SYMBOL as KAT,
                ZGL.DT_WA as DATA
             from
                REM_ZGL ZGL
                left join @REM_GRG GRG  using (GRG.REM_ZGL,ZGL.REFERENCE)
                join REM_ZAS ZAS using (ZGL.REM_ZAS,ZAS.REFERENCE)
                left join @PROJ2FAP PFAP using (PFAP.REM_ZGL,ZGL.REFERENCE)
                join REM_KATG KAT using (ZGL.REM_KATG,KAT.REFERENCE)
            '
             +q_data+q_zas+q_filtr+'
             group by
                ZAS.SYMBOL, ZGL.SYM, ZGL.STAT_REJ,ZGL.DT_WA, ZGL.DT_ZAM,KAT.SYMBOL,ZGL.TM_ZAM,ZGL.TM_WA
             order by  2
            '
   );
~~}
{IF: query.first()}
{dalej:=1; ~~}{FI}
{WHILE:dalej}
{DO}
    {SUBSTITUTE:'LINIA0'}
    {dalej:=query.next(); ~~}
{OD}
{IF: tryb<>1}
   {prn1:='PRN';prn2:='PRNS';~~}
   {SUBSTITUTE:'LINIA02'}
   {prn1:='PRNS';prn2:='PRN';~~}
{FI}
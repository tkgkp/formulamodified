:!UTF-8
{TITLE:Nieobecności zleceniobiorców (rozliczenie)}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   tresc:='zleneroz';
   PAR_WYDR.TITLE:='Rozliczenie nieobecności zleceniobiorców'@;
   par:=obj_new(11);
   par[7]:='';
   par[11]:=exec('ref_firma','ustawienia');
   P.cntx_psh();
   TZ.cntx_psh();
   par[5]:=par[4]:=0;
   _upr:=exec('uprawnieniawrap','pkd','nieobecnościach'@,'PPL_ZLC_PNIE','PPL_ZLC_RNIE');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień pozwalających na przeglądanie informacji o: %1'@[_upr])
   || par[3]:=exec('pkd_wnieobecn','personel',,tresc,PAR_WYDR.TITLE,0);
      {? par[5]:=par[3].size()
      || {? par[3].KOD || par[7]:=par[3].memo_txt(,1,'LISTA') || par[7]:='' ?};
         par[8]:=null();

         par[1]:=obj_new(@.CLASS.PRINT,6);
         par[1].add("TZ.M~1",4,'Rok'@,1,,,,,'4,,\'99\'');
         par[1].add("TZ.M~2",4,'Mc'@,1,,,,,'2,,\'99\'');
         par[1].add("TZ.PR",12,'Przepracował'@,1);
         par[1].add("TZ.NO",12,'Powinien przepracować'@,1);
         par[1].add("TZ.KW",15,'Wynagrodzenie'@,1,,,,,'15,2');
         par[1].add(
            "  {? TZ.W='T' || 'm-c zakwalifikowany'@
               |? TZ.W='N' || 'm-c pominięty'@
               |? TZ.W='P' || 'm-c pobrany'@
               || ''
               ?}
            ",20,'Opis'@
         );
         par[1].s_frame();

         par[2]:=obj_new(@.CLASS.PRINT,1);
         par[2].add("{? par[9]=1
                     || P.OSOBA().NAZWISKO+' '+
                        OSOBA.PIERWSZE+' '+
                        OSOBA.DRUGIE+', PESEL: '+
                        OSOBA.PESEL
                     |? par[9]=2
                     || OS_N.R().RT+' od '@+OS_N.OD$1+' do '@+OS_N.DO$1
                     |? par[9]=3
                     || 'Podstawa wymiaru '@+form(OS_N.POD,,2,'9,')
                     || ''
                     ?}",par[1].width()-4,'Nieobecność'@
                   );
         par[2].s_frame();
         par[8]:=tab_tmp(,'REF','INTEGER','Osoba');
         par[9]:=0;
         par[10]:=1;
         h_rat:=1; lmt:=0;
         params_set('P',exec('wybierz_parses','pracownik','PPL').P)
      ?}
   ?}
}
{END:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','tresc','b_rat','h_rat');
   P.cntx_pop();
   TZ.cntx_pop()
}
{COMMENT}OLD: zleneroz.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par[5]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[10]}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft/h_rat; ~~ }
   {SPACE: 'C'}
   {LINE}
   { par[10]:=0; ~~ }
   {<{lmt+1}'Parametry sprawozdania'@}
   {<{lmt+1}'Uwzględniane kody'@}: {
      {? +par[7] || STR.gsub(1-par[7]-1,',',', ') || 'brak ograniczeń'@ ?}}
   {<{lmt+1}'Zakres dat: od %1 do %2'@[par[3].OD$1,par[3].DO$1]}
{FI}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'nieo_dane'}
{IF: {? +par[7] || par[7]*(','+$OS_N.R().RN+',') || 1 ?}}
   {  par[9]:=0;
      TZ.use('tabz'+(4+$OS_N.OD));
      ~~
   }
   {ENTIRE}
   {FOR: TZ}{INDEX: 'ZASTAO'}{PREFIX:'O',OS_N.ref}{DO}
   {FIRST}
       {LINE}
      {FONT: 'T8B'}
      {SPACE: 'B'}
      {REP: par[2].fhbar(lmt)}
      {WHILE: (par[9]+=1)<=3}{DO}
            {par[2].ini_line()}
            {WHILE: par[2].next()}{DO}
               {REP: par[2].fline(lmt)}
            {OD}
      {OD}
      {REP: '{<{lmt+1}}'+par[2].fjbar(par[1])}
      {FONT: 'T8'}
      {SPACE: 'B'}
      {par[1].ini_head()}
      {WHILE: par[1].h_next()}{DO}
         {REP: par[1].h_fmline(lmt)}
      {OD}
      {REP: par[1].fbar(lmt)}
   {FIRST-}
      {par[1].ini_line()}
      {WHILE: par[1].next()}{DO}
         {REP: par[1].fline(lmt)}
      {OD}
   {OD}
   {IF: par[9]}
      {REP: par[1].flbar(lmt)}
   {FI}
   {ENTIRE-}
{FI}
{MACRO-}
{COMMENT}

   Przeglądaj kartotekę nieobecności w podanym lub pełnym zakresie dat.
   Pełny zakres - wszystkie nieobecności, ograniczony zakres - od daty
   do daty, od 'początku' kartoteki do daty lub od daty do 'końca'.

{COMMENT-}
{MACRO: tresc}
{IF: ~par[8].find_key(#P.OSOBA) & exec('grant','os_nieob',1)}
{ par[8].REF:=#P.OSOBA; par[8].add; ~~ }
{REP: '{FOR: OS_N}{INDEX: \'OND\'}'+
      {? ~par[3].OKR
         || '{PREFIX: par[11],P.OSOBA}'
         || '{FROM: par[11],P.OSOBA'+{? par[3].OD=#0 || '' || ',par[3].OD' ?}+'}'+
            '{TO: par[11],P.OSOBA'+{? par[3].DO=#0 || '' || ',par[3].DO' ?}+'}'
      ?}+'{DO}{SUBSTITUTE: \'nieo_dane\'}{OD}'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{  lmt:=int((charleft()-par[1].width())/2);
   par[2].setcolor(color);
   ~~
}
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}

:!UTF-8
{TITLE:1. Kody kreskowe}
{PRINTER: INFO.W_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
  params_set(params_get());
  VAR_DEL.delete('PRN','wert','BARCODE_TYPE','BARCODE_H','BARCODE_W');
  BARCODE_TYPE:=exec('barcode_type','kody_kresk');
  BARCODE_H:=1;

  KAP.cntx_psh();
  wert:=KAP.mk_sel('Wybierz kartę do wydruku'@,,0,,,12,,,'U');
  KAP.win_fld(wert,,'B','KOD',,10,,,'Kod brygady'@);
  KAP.win_fld(wert,,'B','NAZ',,20,,,'Nazwa brygady'@);
  KAP.win_fld(wert,,'P','T',,,,,'Nr teczki'@);
  KAP.win_fld(wert,P,'OSOBA','NAZWISKO',,,,,'Nazwisko'@);
  KAP.win_fld(wert,P,'OSOBA','PIERWSZE',,,,,'Imię'@);
  KAP.win_fld(wert,,'DT');
  KAP.win_fld(wert,,'IL',,,15,4);
  KAP.win_sel(wert);
  ndxbz:=KAP.ndx_tmp(,1,'ZGP',,0,'DT',,0,'B','KOD',0);
  PRN:=obj_new(@.CLASS.PRINT, 5);
  PRN.s_frame();
  font:='';
  prn1:='PRN';
  waslbar:=0;
  dalej:=0;
  color:='';
  lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
  KAP.cntx_pop();
  VAR_DEL.delete('PRN','wert','BARCODE_TYPE','BARCODE_H','BARCODE_W');
  KAP.ndx_drop(ndxbz);
  &font;
  &dalej;
  &prn1;
  &color;
  &lm; &ll; &vp; &lmt; &rsep
}
{DEFINEFONT: 'courier=TexGyreCursor,120,m,8'}
{FONT: 'courier'}
{COMMENT}
(c) 2005 MacroSoft
==============================================================================
    system: P R O D U K C J A
      plik: KAP_0001.RPM
            +----------------------------------------------------------------+
  Zawartość | Drukowanie etykiet etykiet wykorzystywanych do rejestracji akordu                                 |
            +----------------------------------------------------------------+
==============================================================================
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Definicje -> Przewodniki -> Pozycje -> Karty pracy -> Drukuj
{COMMENT-}
{MACRO:'stang'}
   {ENTIRE}
   {temp:=form(#KAP.ZGP,-7,,'9.');~~}
   {temp2:=form(KAP.P().IP,-4,,'9.');~~}
   {BARCODE: temp;(+temp)*2;BARCODE_H;BARCODE_TYPE}{<40}{BARCODE: temp2;(+temp)*2;BARCODE_H;BARCODE_TYPE}{FONT: 'courier'}{SPACE: 'E'}

   {SPACE: 'B'}
   {ENTIRE-}
  ──────────────────────────────────────────────────────────────────────────
{MACRO-}
{MACRO:'etykiety'}
{IF: PAR_WYDR.TYPDR<>null}
   { dalej:=exec('druk','kody_kresk');~~}
{ELSE}
   {SUBSTITUTE: 'stang'}
{FI}
{MACRO-}
{ dalej:=1;~~}
{IF: dalej}
{FOR: 'KAP'}
{INDEX: {? VAR.TYMCZAS='P' || 'PDZ' |? VAR.TYMCZAS='B' || 'BDZ' |? VAR.TYMCZAS='BZ' || ndxbz || 'ZDP' ?}}
{PREFIX: {? VAR.TYMCZAS='P' || P.ref() |? VAR.TYMCZAS='B' || ZLBR.ref() |? VAR.TYMCZAS='BZ' || ZGP.ref() || ZGP.ref() ?}}
{SELECT}
{DO}
 {FIRST}
 {IF: PAR_WYDR.TYPDR=null() }
 {FONT: 'W12'}{SPACE: 'A'}
 {IF: VAR.TYMCZAS='P'}
 {form(KAP.P().OSOBA().PIERWSZE+' '+OSOBA.NAZWISKO)}
 {ELIF: VAR.TYMCZAS='B'}
 {form(KAP.B().KOD+' '+KAP.B().NAZ)}
 {ELSE}
  {'%1 poz. %2'@[KAP.ZGH().NRPRZ,form(KAP.ZGP().NRP)]}
 {FI}
  {FONT: 'courier'}{SPACE: 'B'}
 {FI}
 {FIRST-}
 {IF: PAR_WYDR.TYPDR=null()}
   {FONT: 'W12'}{SPACE: 'A'}

   {IF: VAR.TYMCZAS='P' | VAR.TYMCZAS='B'}
   {'%1 poz. %2'@[KAP.ZGH().NRPRZ,form(KAP.ZGP().NRP)]+' '+'data: %1'@[form(KAP.DT)]+' '+'ilość: %1'@[form(KAP.IL)]}
   {ELIF: VAR.TYMCZAS='BZ'}
    {'%1 %2'[KAP.B().KOD,ZLBR.NAZ]+' '+'data: %1'@[form(KAP.DT)]+' '+'ilość: %1'@[form(KAP.IL)]}
   {ELSE}
    {'%1 %2'[KAP.P().OSOBA().PIERWSZE,OSOBA.NAZWISKO]+' '+'data: %1'@[form(KAP.DT)]+' '+'ilość: %1'@[form(KAP.IL)]}
   {FI}

   {FONT: 'courier'}{SPACE: 'B'}
 {FI}
 {SUBSTITUTE: 'etykiety'}
 {IF: dalej=0}{EXIT}{FI}
{OD}
{FI}
{EXIT: {? dalej=0 || FUN.info('Podczas przygotowywania wydruku wystąpił błąd.\nDrukowanie zostało przerwane.'@);1 || 0 ?}}
{COMMENT}
==============================================================================
      koniec pliku
==============================================================================
{COMMENT-}




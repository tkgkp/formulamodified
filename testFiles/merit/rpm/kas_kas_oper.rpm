:!UTF-8
{TITLE:A. Obroty w zakresie dat}
{PRINTER: KINFO.W_PRN,,,,,'Q','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
          PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   kas_oper:="
      VAR_DEL.delete('w','PRN','ile_l','tryb');
      VAR_DEL.delete('ekw_1','ekw_2','ekw_3','ekw_4','wal_1','wal_2','wal_3','wal_4');
      VAR_DEL.delete('color','prn1','prn2','lm','ll','vp','lmt','rsep')
   ";
   kas_oper();

   w:=obj_new(25); {! _a:=1..25 |! w[_a]:='' !};
        ekw_1:=ekw_2:=ekw_3:=ekw_4:=wal_1:=wal_2:=wal_3:=wal_4:=0;
        WYDRUKI.win_edit('KASOPER');
        WYDRUKI.DAT_OD:=exec('data_okr','kasa_wspolne',USRCONST.ROK,0);
        WYDRUKI.DAT_DO:={? date~1=#USRCONST.ROK().NAZ || date || date(#USRCONST.ROK().NAZ,12,0) ?};
        WYDRUKI.PAR2:='T'; WYDRUKI.PAR1:='W';
        PRN:=obj_new(@.CLASS.PRINT,6); PRN.sl_frame();  ile_l:=0;
        tryb:=rep_export();
        color:=''; prn1:=prn2:='PRN'; lm:=ll:=lmt:=rsep:=0; vp:=1
}
{END:
   kas_oper(); VAR_DEL.delete('kas_oper')
}
{EXIT:
   ~WYDRUKI.edit("
      {? WYDRUKI.DAT_OD=date(0,0,0)
      || msg('Nie podano daty początkowej.'@,'Uwaga!'@); 'DAT_OD'
      |? #USRCONST.ROK().NAZ<>WYDRUKI.DAT_OD~1
      || FUN.info('Data początkowa nie zawiera się w wybranym roku obrachunkowym.'@); 'DAT_OD'
      |? WYDRUKI.DAT_DO=date(0,0,0)
      || msg('Nie podano daty końcowej.'@,'Uwaga!'@); 'DAT_DO'
      |? #USRCONST.ROK().NAZ<>WYDRUKI.DAT_DO~1
      || FUN.info('Data końcowa nie zawiera się w wybranym roku obrachunkowym.'@); 'DAT_DO'
      |? WYDRUKI.DAT_OD>WYDRUKI.DAT_DO
      || msg('Nie podano poprawnie zakresu dat.'@,'Uwaga!'@);'DAT_OD'
      || 1
      ?}")}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{FONT: 'HEAD'}{SPACE: 'B'}
{ _co:={? WYDRUKI.PAR1='T' || 'Obroty według transakcji' || 'Obroty według walut' ?};
  PAR_WYDR.TITLE:=~(-(_co+' od '+WYDRUKI.DAT_OD$1+' do '+WYDRUKI.DAT_DO$1)); ~~}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{ vp:=1;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER:0}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}{ vp:=1;~~}




{FOOTER-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ {? WYDRUKI.PAR1='W'
  || PRN.add("w[1]",26,'Waluta'@)
  || PRN.add("w[1]",48,'Transakcja'@)
  ?};
  PRN.add("w[2]",18,'BO'@,1);
  PRN.add("w[3]",18,'Przychody'@,1);
  PRN.add("w[4]",18,'Rozchody'@,1);
  PRN.add("w[5]",18,'Saldo końcowe'@,1);~~
}
{COMMENT}------------------------------MACRO-LINIA------------------{COMMENT-}
{MACRO: 'LINIA'}{ PRN.setcolor('255:255:255');~~}
{PRN.ini_line()}
{WHILE: PRN.next()}
{DO}
{REP: PRN.fline()}
{OD}{ vp:=0;~~}
{MACRO-}
{COMMENT}------------------------------MACRO WALUTY--------------------{COMMENT-}
{MACRO: 'WALUTY'}
{FOR: 'WAL'}{INDEX:'WAL_SYM'}{DO}
{ exec('suma_wal','kasa_wspolne','W',WAL.SYM,WYDRUKI.DAT_OD,WYDRUKI.DAT_DO);
  ile_l:=0;
  {? WYDRUKI.PAR2='N' | BO.WAL>0 | SUMA.WAL_PLUS>0 | SUMA.WAL_MIN>0 | SALDO.WAL>0 || ile_l+=1 ?};
  {? WAL.SYM<>KINFO.NAROD().KOD
  || {? WYDRUKI.PAR2='N' | BO.PLN>0 | SUMA.PLN_PLUS>0 | SUMA.PLN_MIN>0 | SALDO.PLN>0 || ile_l+=1 ?}
  ?};~~}
{IF: tryb<>1 & lineleft<>0 & lineleft<(ile_l+rsep)}{PAGE}{ vp:=1;~~}{FI}
{ENTIRE}
{IF: WYDRUKI.PAR2='N' | BO.WAL>0 | SUMA.WAL_PLUS>0 | SUMA.WAL_MIN>0 | SALDO.WAL>0}
{ w[1]:=WAL.SYM +' '+WAL.NAZ;
  w[2]:=form(BO.WAL,,2,' ,'); w[3]:=form(SUMA.WAL_PLUS,,2,' ,'); w[4]:=form(SUMA.WAL_MIN,,2,' ,'); w[5]:=form(SALDO.WAL,,2,' ,');~~}
{IF: tryb<>1 & vp}{PRN.setcolor(color);~~}{REP: PRN.fbar()}{ELIF: rsep}{PRN.bar()}{FI}
{IF: tryb<>1}{SUBSTITUTE: 'LINIA'}{ELSE}{SUBSTITUTE: 'LINIA0'}{FI}
{FI}
{IF: WAL.SYM<>KINFO.NAROD().KOD}
{IF: WYDRUKI.PAR2='N' | BO.PLN | SUMA.PLN_PLUS | SUMA.PLN_MIN | SALDO.PLN}
{ w[1]:=form('ekwiwalent w '@,-21)+KINFO.NAROD().KOD; ekw_1+=BO.PLN; ekw_2+=SUMA.PLN_PLUS; ekw_3+=SUMA.PLN_MIN; ekw_4+=SALDO.PLN;
  w[2]:=form(BO.PLN,,2,' ,'); w[3]:=form(SUMA.PLN_PLUS,,2,' ,'); w[4]:=form(SUMA.PLN_MIN,,2,' ,'); w[5]:=form(SALDO.PLN,,2,' ,');~~}
{IF: tryb<>1}{SUBSTITUTE: 'LINIA'}{ELSE}{SUBSTITUTE: 'LINIA0'}{FI}
{FI}
{FI}
{ENTIRE-}
{OD}
{MACRO-}
{COMMENT}------------------------------MACRO OPERACJE--------------------{COMMENT-}
{MACRO: 'OPERACJE'}
{
BO_KASY.index('STANKAS'); BO_KASY.prefix(USRCONST.STANKAS().STANKAS);
_bo:=0;
{? BO_KASY.first || {! |? _bo+=BO_KASY.BO_PLN; BO_KASY.next !} ?};
w[1]:='Stan początkowy'@; ekw_1+=_bo; ekw_4+=_bo;
w[2]:=form(_bo,,2,' ,');
w[3]:=form(0,,2,' ,');
w[4]:=form(0,,2,' ,');
w[5]:=form(_bo,,2,' ,');
~~}
{IF: BO_KASY.first}
   {IF: tryb<>1}{<1}{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.fbar()}{ELIF: rsep}{PRN.bar()}{FI}{FI}
   {IF: tryb<>1}{SUBSTITUTE: 'LINIA'}{ELSE}{SUBSTITUTE: 'LINIA0'}{FI}
   {IF: tryb<>1 & lineleft<>0 & lineleft<(ile_l+rsep)}{PAGE}{ vp:=1;~~}{FI}
   {IF: STANKAS.WALUTA & STANKAS.WALUTA().SYM<>KINFO.NAROD().KOD}
      {
      w[1]:='      w walucie - '@+STANKAS.WALUTA().SYM; wal_1+=BO_KASY.BO_WAL; wal_4+=BO_KASY.BO_WAL;
      w[2]:=form(BO_KASY.BO_WAL,,2,' ,');
      w[3]:=form(0,,2,' ,');
      w[4]:=form(0,,2,' ,');
      w[5]:=form(BO_KASY.BO_WAL,,2,' ,');
      ~~}
      {IF: tryb<>1}{SUBSTITUTE: 'LINIA'}{ELSE}{SUBSTITUTE: 'LINIA0'}{FI}
   {FI}
{FI}
{FOR: 'POZOPER'}{INDEX: 'KSIEG'}{DO}
{ exec('suma_wal','kasa_wspolne','O',POZOPER.ref(),WYDRUKI.DAT_OD,WYDRUKI.DAT_DO);
  POZOPER.OPER();
  ile_l:=0;
  {? WYDRUKI.PAR2='N' | BO.PLN | SUMA.PLN_PLUS | SUMA.PLN_MIN | SALDO.PLN || ile_l+=1 ?};
  {? STANKAS.WALUTA<>WAL.ref & STANKAS.WALUTA<>0
  || {? WYDRUKI.PAR2='N' | BO.WAL | SUMA.WAL_PLUS | SUMA.WAL_MIN | SALDO.WAL || ile_l+=1 ?}
  ?};~~}
{IF: tryb<>1 & lineleft<>0 & lineleft<(ile_l+rsep)}{PAGE}{ vp:=1;~~}{FI}
{ENTIRE}
{IF: WYDRUKI.PAR2='N' | BO.PLN | SUMA.PLN_PLUS | SUMA.PLN_MIN | SALDO.PLN}
{ w[1]:=form(OPER.KOD,8)+' '+POZOPER.KOD+' '+POZOPER.NAZWA; ekw_1+=BO.PLN; ekw_2+=SUMA.PLN_PLUS; ekw_3+=SUMA.PLN_MIN; ekw_4+=SALDO.PLN;
  w[2]:=form(BO.PLN,,2,' ,'); w[3]:=form(SUMA.PLN_PLUS,,2,' ,'); w[4]:=form(SUMA.PLN_MIN,,2,' ,'); w[5]:=form(SALDO.PLN,,2,' ,');~~}
{IF: tryb<>1}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.fbar()}{ELIF: rsep}{PRN.bar()}{FI}
{SUBSTITUTE: 'LINIA'}
{ELSE}
{SUBSTITUTE: 'LINIA0'}
{FI}
{FI}
{ USRCONST.STANKAS(); KUSERUPR.STANKAS(); ~~}
{ WAL.index('WAL_SYM'); WAL.prefix(); WAL.find_key(KINFO.NAROD().KOD); ~~}
{IF: STANKAS.WALUTA<>WAL.ref & STANKAS.WALUTA<>0}
{IF: WYDRUKI.PAR2='N' | BO.WAL | SUMA.WAL_PLUS | SUMA.WAL_MIN | SALDO.WAL}
{ w[1]:='      w walucie - '@+STANKAS.WALUTA().SYM; wal_1+=BO.WAL; wal_2+=SUMA.WAL_PLUS; wal_3+=SUMA.WAL_MIN; wal_4+=SALDO.WAL;
  w[2]:=form(BO.WAL,,2,' ,'); w[3]:=form(SUMA.WAL_PLUS,,2,' ,'); w[4]:=form(SUMA.WAL_MIN,,2,' ,'); w[5]:=form(SALDO.WAL,,2,' ,');~~}
{IF: tryb<>1}{SUBSTITUTE: 'LINIA'}{ELSE}{SUBSTITUTE: 'LINIA0'}{FI}
{FI}
{FI}
{ENTIRE-}
{OD}
{MACRO-}
{COMMENT}----------------------------------WYDRUK------------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{IF: WYDRUKI.PAR1='W'}{SUBSTITUTE: 'WALUTY'}{ELSE}{SUBSTITUTE: 'OPERACJE'}{FI}
{COMMENT}----------------------------------LAST FOOTER------------------------{COMMENT-}
{FOOTER}{FOOTER-}
{IF: tryb<>1}
{ENTIRE}
{IF: WYDRUKI.PAR1='W' & (ekw_1 | ekw_2 | ekw_3 | ekw_4)}
{ w[1]:='Razem ekwiwalent w '@+KINFO.NAROD().KOD; w[2]:=form(ekw_1,,2,' ,'); w[3]:=form(ekw_2,,2,' ,'); w[4]:=form(ekw_3,,2,' ,'); w[5]:=form(ekw_4,,2,' ,');~~}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.fbar()}{ELSE}{PRN.bar()}{FI}
{SUBSTITUTE: 'LINIA'}
{ELIF: WYDRUKI.PAR1<>'W'}
{IF: ekw_1 | ekw_2 | ekw_3 | ekw_4}
{
   ekw_1:=ekw_4:=0;
   wal_1:=wal_4:=0;
   WAL.cntx_psh;
   WAL.index('WAL_SYM'); WAL.prefix;
   {? WAL.first
   ||
      {!
      |?
         WAL.cntx_psh;
         exec('suma_wal','kasa_wspolne','W',WAL.SYM,WYDRUKI.DAT_OD,WYDRUKI.DAT_DO);
         WAL.cntx_pop;
         ekw_1+=BO.PLN;
         ekw_4+=SALDO.PLN;
         wal_1+=BO.WAL;
         wal_4+=SALDO.WAL;
         WAL.next
      !}
   ?};
   WAL.cntx_pop;
   w[1]:='Razem wszystkie transakcje w '+KINFO.NAROD().KOD; w[2]:=form(ekw_1,,2,' ,'); w[3]:=form(ekw_2,,2,' ,'); w[4]:=form(ekw_3,,2,' ,'); w[5]:=form(ekw_4,,2,' ,');~~}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.fbar()}{ELSE}{PRN.bar()}{FI}
{SUBSTITUTE: 'LINIA'}
{IF: STANKAS.WALUTA & STANKAS.WALUTA().SYM<>KINFO.NAROD().KOD & (wal_1 | wal_2 | wal_3 | wal_4)}
{ w[1]:='Razem wszystkie transakcje w '+STANKAS.WALUTA().SYM; w[2]:=form(wal_1,,2,' ,'); w[3]:=form(wal_2,,2,' ,'); w[4]:=form(wal_3,,2,' ,'); w[5]:=form(wal_4,,2,' ,');~~}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.fbar()}{ELSE}{PRN.bar()}{FI}
{SUBSTITUTE: 'LINIA'}
{FI}
{FI}
{FI}
{IF: vp}{PRN.setcolor(color);~~}{REP: PRN.flbar()}{ELSE}{PRN.lbar()}{FI}
{<1>{PRN.width()}'KONIEC WYDRUKU'@}
{ENTIRE-}
{ELSE}
{SUBSTITUTE: 'LINIAF'}
{FI}
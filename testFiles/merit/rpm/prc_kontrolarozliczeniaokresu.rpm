:!UTF-8
{TITLE: Rozliczenie okresu}
{PRINTER: exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','zaswumpr')='T',,,,'I','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('par','cnt','par','Lp','prn1','lmt','rsep','vp','h_rat','tresc','__CALL','ref_okr');
   tresc:='okrrozl';

   cnt:=lmt:=rsep:=Lp:=0;
   vp:=h_rat:=1;
   __xr_ins:=exec('new_ins','rap_zew','zaswumpr');

   par:=obj_new(@.CLASS.PRINT);
   par.add("Lp+=1",5,'Lp.'@,1,,,,,',,\'9.\'');
   par.add("Temp.DATA",14,'Data nadgodzin'@,1);
   par.add("Temp.GODZINY",16,'Liczba nadgodzin'@,1);
   par.add("Temp.RODZ",20,'Typ godzin'@);
   par.add("Temp.DATAO",15,'Data odbioru'@,1);
   par.add("Temp.W",30,'Odbiór godzin na wniosek'@);
   par.s_frame();
   prn1:='par';

   P.cntx_psh();
   R_PRACDN.cntx_psh();
   R_KWGODZ.cntx_psh();
   A_OKR.cntx_psh();
   A_OKR.index('A_OKRNN');
   A_OKR.win_sel('SLO');
   A_OKR.seek(__HARM.OKR_REF,,1);
   {? A_OKR.select(,1,5,'dpu:d')
   || ref_okr:=A_OKR.ref();
      exec('czytaj','#stalesys',A_OKR.DO,KST,'R_NTYG','R_NNTYG','R_NLIM');
      {? P.f_active() || P.f_clear() ?};
      _args:=exec('wybierz_args','pracownik');
      _args.DOMAIN:='PRC';
      _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
      _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
      {? +P_FILTER.STATUS
      || _args.F_ZATR:={? +P_FILTER.F_ZATR().KOD || P_FILTER.F_ZATR().KOD || '*T' ?};
         _args.VIEW:=P_FILTER.STATUS
      || _args.F_ZATR:=__PARSES.getVal('F_ZATR');
         _args.VIEW:=__PARSES.getVal('ZakresDanych')
      ?};
      _args.SQL_FROM:='';
      _args.SQL_WHERE:='P.REFERENCE in (select A_OKRP.P from A_OKRP where A_OKRP.OKR=\''+$ref_okr+'\' order by 1)';
      _TAB:=exec('wybierz','pracownik',_args).P;
      _NDX:=_TAB.ndx_tmp(,1,'SQL',,);
      params_set('P',_TAB,'NDX',_NDX)
   || ref_okr:=null()
   ?};
   A_OKR.cntx_pop()
}
{END:
   exec('del_ins','rap_zew',__xr_ins);
   VAR_DEL.delete('par','cnt','par','Lp','prn1','lmt','rsep','vp','h_rat','tresc','__CALL','ref_okr');
   P.cntx_pop();
   R_PRACDN.cntx_pop();
   R_KWGODZ.cntx_pop()
}
{EXIT: ref_okr=null() | ~params_get().P.first()}
{COMMENT}OLD: okrrozl.rpm{COMMENT-}
{COMMENT}OLD: okrrozl.rpi{COMMENT-}
{A_OKR.prefix(); A_OKR.seek(ref_okr);~~}
{FOR: A_OKRP}
{INDEX: 'A_OKRNI'}
{PREFIX: A_OKR.ref}
   {DO}
      {  A_OKRP.P(); ~~}
      {IF: _cfg:=params_get(); _cfg.P.index(_cfg.NDX); _cfg.P.find_key($A_OKRP.P)}
      {  cnt+=1; exec('oblicz','okres');
'firma';
         exec('ins_par','rap_zew',__xr_ins,cnt,
            'F_NAZWA',KST.NAZWA,
            'MIEJSCE',PAR_WYDR.MIEJSC,
            'DATA',date()$4,
            'F_ULICA',exec('ulica','stalesys'),
            'F_POCZTA',exec('poczta','stalesys')
         );
'pracownik';
         exec('ins_par','rap_zew',__xr_ins,cnt,
            'PRACOWN',$P.ref(),
            'NAZWISKO',P.OSOBA().NAZWISKO,
            'IMIE',OSOBA.PIERWSZE,
            'DRUGIEIM',OSOBA.DRUGIE,
            'PLEC',OSOBA.PLEC,
            'ZATRUDN',P.ZA,
            'D_ZATR',P.DZA$6,
            'TEC',P.T
         );
'okres';
         exec('ins_par','rap_zew',__xr_ins,cnt,
            'OKR_NAZ',A_OKR.NAZ().NAZ,
            'OKR_OD',A_OKRP.OD$6,
            'OKR_DO',A_OKRP.DO$6,
            'OKR_OD1',$(A_OKRP.OD~1)+'-'+$(A_OKRP.OD~2)+'-'+$(A_OKRP.OD~3),
            'OKR_DO1',$(A_OKRP.DO~1)+'-'+$(A_OKRP.DO~2)+'-'+$(A_OKRP.DO~3),
            'OKR_WYM',(*(60*exec('wymiar_prac','okres',A_OKRP.OD,A_OKRP.DO,1,1)))$1
         );
'rozliczenie';
         exec('ins_par','rap_zew',__xr_ins,cnt,
            'ROZ_NORM',A_OKRP.NORMA$1,
            'ROZ_PRZE',A_OKRP.PRZEP$1,
            'ROZ_NIED',A_OKRP.NIED$1,
            'ROZ_NADM',A_OKRP.NADM$1,
            'ROZ_NIEO',A_OKRP.NIEOB$1,
            'ROZ_NN',{? A_OKRP.NADM<>*0 || 'N' |? A_OKRP.NIED<>*0 || 'A' || 'X' ?}
         );
'szczegóły';
         _miesiace:=exec('roznica','prc_rozlicz',A_OKRP.OD,A_OKRP.DO);
         _praca:=_nadm:=time(0,0,0);
         _G50:=_G100:=time(0,0,0);
         _GOD:=time(0,0,0);
         {! _ind:=0.._miesiace
         |! MASK.Use('R_PRACDN',date(A_OKRP.DO~1,(A_OKRP.DO~2)-_ind,1)~1,date(A_OKRP.DO~1,(A_OKRP.DO~2)-_ind,1)~2);
            MASK.Use('R_KWGODZ',date(A_OKRP.DO~1,(A_OKRP.DO~2)-_ind,1)~1,date(A_OKRP.DO~1,(A_OKRP.DO~2)-_ind,1)~2);
            R_KWGODZ.clear();
            R_KWGODZ.index('RUB_KONT');
            R_PRACDN.clear();
            R_PRACDN.prefix(P.ref());
            {? R_PRACDN.first()
            || {!
               |? {? R_PRACDN.DT>=A_OKRP.OD & R_PRACDN.DT<=A_OKRP.DO
                  || _ok:=1;
                     R_KWGODZ.prefix(P.ref(),R_PRACDN.DT);
                     {? R_KWGODZ.first()
                     || {!
                        |? {? '54,55,56'*($R_KWGODZ.KW().RN)
                           || {? R.RN=54 || _ok:=0 ?};
                              {? '55,56'*($R_KWGODZ.KW().RN) || _nadm+=R_KWGODZ.GODZ ?};
                              {? R_KWGODZ.KW().RN=54 || _praca+=R_KWGODZ.GODZ ?};
                              {? R_KWGODZ.KW().RN=55 || _G50+=R_KWGODZ.GODZ ?};
                              {? R_KWGODZ.KW().RN=56 || _G100+=R_KWGODZ.GODZ ?}
                           ?};
                           R_KWGODZ.next()
                        !}
                     ?};
                     {? _ok || _praca+=R_PRACDN.CP ?}
                  ?};
                  R_PRACDN.next()
               !}
            ?}
         !};
         R_WYK.index('R_WYKDN');
         R_WYK.prefix(P.ref());
         {? R_WYK.first()
         || {!
            |? {? R_WYK.DN>=A_OKR.OD & R_WYK.DN<=A_OKR.DO
               || _GOD+=R_WYK.G
               ?};
               R_WYK.next()
            !}
         ?};
         exec('ins_par','rap_zew',__xr_ins,cnt,
            'SZ_PRACA',_praca$1,
            'SZ_NADM',_nadm$1,
            'SZ_NIEO',(A_OKRP.P_NIEOB)$1,
            'SZ_50',_G50$1,
            'SZ_100',_G100$1,
            'SZ_ODB',_GOD$1,
            'SZ_JOD',$(_GOD<>*0)
            );
      ~~}
   {FI}
{OD}
{EXIT: ~exec('ready','rap_zew',__xr_ins)}
{IF: exec('rep_set','rap_zew','okrrozl')='T'}
   { exec('use_tmp','rap_zew','okrrozl'); ~~ }
   {EXIT}
{ELSE}
   { __CALL:=proc_exec('rozl@okres',#__xr_ins); ~~ }
   {INCLUDE: prc_rozliczenieokresu.rpi}
{FI}
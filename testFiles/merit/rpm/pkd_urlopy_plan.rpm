:!UTF-8
{TITLE: Kalendarz do planu urlopów}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('dsw','par','prn1','vp','lmt','rsep','tresc','Lp');
   tresc:='url_plan';
   P.cntx_psh();
   par:=obj_new(9);

   par[9]:=exec('par_wydr_ddp','pkd',
      'Kalendarz do planu urlopów '@,,,,,,,,,,,,,
      date()~1,
      "{? cur_tab().ROK<1900 || FUN.emsg('Proszę wprowadzić rok do wydruku planów urlopowych.'@); 0 || 1 ?}"
   );

   {? par[9].size()
   || PAR_WYDR.TITLE:='Kalendarz do planu urlopów na rok %1'@[$par[9].ROK];
      dsw:=tab_tmp(1,
         'D','DATE','Data',
         'OPIS','STRING[35]','Opis'
      );
      par[3]:=obj_new(13);
      par[2]:=obj_new(12);
      par[5]:=obj_new(12);
      {! _ii:=1..12
      |! _mc:=date(,_ii,0)$8;
         par[2][_ii]:=(_mc*' ')+_mc;
         par[3][_ii]:='';
         par[5][_ii]:=obj_new(31)
      !};
      lmt:=Lp:=0;
      par[1]:=obj_new(@.CLASS.PRINT,31);
      par[1].add("Lp+=1",3,'Dz.',1);
      {! _ii:=1..12 |! par[1].add($('par[3]['+$_ii+']'),6,3+($('par[2][_a]'))(_ii)) !};
      par[1].s_frame();
      {! _ii:=1..12
      |! {! _ij:=1..31 |! par[5][_ii][_ij]:='-'*par[1].LEN[2] !}
      !};
      prn1:='par[1]';
      vp:=rsep:=1;
      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?};
   KAL_ROK.cntx_psh();
   KAL_ROK.index('KAL_ROK');
   KAL_ROK.prefix();
   KAL_WZOR.cntx_psh();
   KAL_WZOR.index('KAL_WZOR');
   KAL_WZOR.prefix(exec('ref_firma','ustawienia'));
   KAL_OPIS.cntx_psh();
   KAL_OPIS.index('KAL_OPIS');
   KAL_OPIS.prefix()
}
{END:
   KAL_OPIS.cntx_pop();
   KAL_WZOR.cntx_pop();
   KAL_ROK.cntx_pop();
   P.cntx_pop();
   VAR_DEL.delete('dsw','par','prn1','vp','lmt','rsep','tresc','Lp')
}
{COMMENT}OLD: url_plan.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_prac.rpi{COMMENT-}
{EXIT: ~par[9].size()}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{EXIT:
   'Do tabeli dsw przepiszmy ze wzorca "standard" prawdziwie swiateczne dni.';
   dsw.erase();
   {? KAL_WZOR.find_key('standard')
   || exec('sel_opis','kalendarz',KAL_WZOR.ref(),par[9].ROK,-1);
      KAL_OPIS.prefix(KAL_WZOR.ref(),par[9].ROK);
      {? KAL_OPIS.find_key(8)
      || {!
         |? dsw.blank();
            _dt:=KAL_OPIS.DATA;
            dsw.D:=date(#(4+_dt),#(2+(5-_dt)),#(2+(8-_dt)));
            dsw.OPIS:=KAL_OPIS.OPIS;
            dsw.add();
            KAL_OPIS.next()
         !}
      ?};
      _exit:=~dsw.first()
   || _exit:=1
   ?};
   {? _exit
   || FUN.emsg('Brak możliwości ustalenia dni świątecznych w %1 roku.'@[$par[9].ROK])
   ?};
   _exit
}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{HEADER-}
{FOOTER: 0}
{FONT: 'T8G'}
{SPACE: 'C'}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'pracownik'}
{FONT: 'W10'}
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{<{lmt}}{INCLUDE: p_prac.rpi}
{FOR: KART_URL}{INDEX: 'PRAC_ROK'}{PREFIX: P.ref(),par[9].ROK}{DO}

{<{lmt}'Data nabycia praw do urlopu wypoczynkowego %1'@[P.NAB_URL$1]}
{<{lmt}'Ilość dni urlopu wypoczynkowego zaległego %1'@[$KART_URL.LIM_ZAL]}
{<{lmt}'Ilość dni urlopu wypoczynkowego bieżącego %1'@[$KART_URL.LIM_AKT]}
{IF: KART_URL.URL_DOD>0}
{<{lmt}'Ilość dni urlopu uzupełniającego %1 od dnia %2'@[$KART_URL.URL_DOD,KART_URL.DATA_DOD$1]}
{FI}
{IF: KART_URL.URL_NSP>0}
{<{lmt}'Data nabycia praw do urlopu dodatkowego %1'@[KART_URL.DATA_NSP$1]}
{<{lmt}'Ilość dni urlopu dodatkowego zaległego %1'@[$KART_URL.NSP_ZAL]}
{<{lmt}'Ilość dni urlopu dodatkowego bieżącego %1'@[$KART_URL.URL_NSP]}
{FI}

{OD}
{FONT: 'T8G'}
{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{MACRO-}
{MACRO: 'url'}
{  par[6]:=date(par[9].ROK,1,1);
   __KAL.set_cal(P.KAL,par[9].ROK); ~~
}
{WHILE: par[6]<=date(par[9].ROK,12,0)}{DO}
   { {? __KAL.get_day(par[6])
      || par[5][par[6]~2][par[6]~3]:=
            {? KAL_DEF.TYP='W' || 'Wolny'
            |? KAL_DEF.TYP='S' || {? par[6]~4=7 & ~dsw.find_key(par[6]) || 'Nd' || 'Święto' ?}
            || ''
            ?}
      ?};
      par[6]+=1; ~~
   }
{OD}
{par[7]:=1; par[8]:=1; ~~}
{WHILE: par[7]<>32}{DO}
    {WHILE: par[8]<>13}{DO}
         {par[3][par[8]]:=par[5][par[8]][par[7]]; par[8]+=1; ~~}
    {OD}
{SUBSTITUTE: 'LINIA0'}
    {par[8]:=1; par[7]+=1; ~~}
{OD}
{MACRO-}
{MACRO: tresc}
{IF: P.DZA<=date(par[9].ROK,12,0) & (P.DZ=#0 | P.DZ>=date(par[9].ROK,1,1))}
{SUBSTITUTE: 'pracownik'}
{SUBSTITUTE: 'url'}
  {Lp:=0; ~~}
{PAGE}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8G'}
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}{DO}
{IF: P.seek(params_get().P.SQL,,1)}
{SUBSTITUTE: tresc}
{FI}
{OD}
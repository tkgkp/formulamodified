:!UTF-8
{TITLE:C. Zestawienie operacji wykonanych w miesiącu wg zleceń}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   ZLGB.cntx_psh();
   ZLGD.cntx_psh();
   ZGP.cntx_psh();
   ZL.cntx_psh();
   ndx:=ZLGD.ndx_tmp(,1,'O',,,'P',,,'B',,);
   ndxzlgb1:=ZLGB.ndx_tmp(,1,
      'ZLGD','O',0,
      'ZLGD',,0,
      'P',,0,
      'GAT',,0
   );
   PRN:=obj_new(@.CLASS.PRINT,9);
   PRN.s_frame();

   prn1:='PRN';
   tryb:=rep_export();
   __TAB:=tab_tmp(3,
      'REFZL','INTEGER','#Ref.zlecenia',
      'REFPRZ','STRING[16]','$Ref.przewodnika',
      'POZ','INTEGER','#Ref.do pozycji',
      'ZLEC','STRING[30]','Symb.zlecenia',
      'PRZEW','STRING[20]','Przewodnik',
      'PDZL','STRING[50]','Kod zesp.',
      'PDZL1','STRING[50]','Kod podzesp.',
      'OPERACJA','STRING[50]','Operacja',
      'ILOSC','INTEGER','Ilosc wyrobu',
      'CZASPL','REAL','Czas planowany',
      'CZASR','REAL','Czas rzeczywisty',
      'WYR','STRING[40]','Wyrob',
      'KOD','STRING[20]','Kod'
   );
   ndx1:=__TAB.ndx_tmp(,0,'REFPRZ',,,'POZ',,);
   waslbar:=0;
   bylo:=0;
   zlec:=0;
   sumakw:=0;
   color:='';
   nazwisko:=pierwsze:=pesel:='';
   timedok:=exec('get','#params',500604,1);
   lm:=ll:=vp:=lmt:=rsep:=0
}
{END:
   ZLGB.cntx_pop();
   ZLGD.cntx_pop();
   ZGP.cntx_pop();
   ZL.cntx_pop();
   obj_del(PRN);
   obj_del(__TAB);
   ZLGD.ndx_drop(ndx);
   ZLGB.ndx_drop(ndxzlgb1);
   &ndx;
   &bylo;
   &zlec;
   &sumakw;
   &prn1;
   &color;
   &nazwisko;
   &pierwsze;
   &pesel;
   &timedok;
   &lm; &ll; &vp; &lmt; &rsep;
   &tryb
}
{COMMENT}
  Wydruk zestawienia operacji wykonanych w miesiacu
   [MK] - 2002/11/29 - [7.62]
  Wywołanie: Wykonania produkcji -> Raporty -> Zestawienia
{COMMENT-}
{INCLUDE: tte_wykx001.rpi}
{COMMENT}--Definicja wiersza zestawienia wykonanych operacji w miesiacu wg zlecen.--------------------{COMMENT-}
{
{? tryb=1
|| PRN.add("form(__TAB.ZLEC)",30,'Zlecenie'@);
   PRN.add("form(__TAB.PRZEW)",20,'Przewodnik'@)
?};
PRN.add("__TAB.PDZL",20,'Wyrób'@);
PRN.add("__TAB.PDZL1",20,'Półfabrykat'@);
PRN.add("__TAB.OPERACJA",30,'Operacja'@);
PRN.add("form(__TAB.ILOSC,,ST.DOKL)",12,'Suma ilości'@,1);
PRN.add("form(__TAB.CZASPL,,timedok)",11,'Suma czasu planowana'@,1);
PRN.add("form(__TAB.CZASR,,timedok)",11,'Suma czasu rzeczywista'@,1);
PAR_WYDR.TITLE:='ZESTAWIENIE OPERACJI WYKONANYCH W MIESIĄCU WG ZLECEŃ'@;~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1}}{'Oddział %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{<{lmt+1} 'Rok: %1 Okres: %2'@[$ST.AR,form(ST.AM,-2)]}


{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------MACRO-----------------------{COMMENT-}
{MACRO: 'usttab'}
{  ZLGB.clear();
   ZLGB.index(ndxzlgb1);
   ZLGB.prefix(ST.OKR_REF);
   ZLGB.first();
   {!
   |? nazwisko:=ZLGB.P().OSOBA().NAZWISKO; pierwsze:=OSOBA.PIERWSZE; pesel:=OSOBA.PESEL;
      {? P.f_find(nazwisko,pierwsze,pesel)
      || __TAB.prefix(#ZLGB.ZLGD().ZL,$ZLGB.ZLGD().ZGH,#ZLGB.ZLGD().ZGP);
         {? __TAB.first()
         || __TAB.ILOSC+=ZLGB.IL;
            __TAB.CZASPL+=ZLGB.TIME_P;
            __TAB.CZASR+=ZLGB.TIME;
            __TAB.put()
         || __TAB.REFPRZ:=$ZLGB.ZLGD().ZGH;
            __TAB.POZ:=#ZLGB.ZLGD().ZGP;
            {? exec('top_rodzaj','zl_link',ZLGB.ZLGD().ZL)='Z'
            || ZL.cntx_psh();
               ZL.index('UNRZL');
               ZL.prefix(ZLGB.ZLGD().ZL().NRNZL);
               {? ZL.first()
               || __TAB.PDZL:=ZL.KTM().KTM
               || __TAB.PDZL:='-'
               ?};
               __TAB.WYR:=ZL.KTM().N;
               __TAB.KOD:=ZL.KTM().KTM;
               __TAB.ZLEC:=ZL.SYM;
               __TAB.REFZL:=#ZL.ref();
               ZL.cntx_pop();
               {? ZLGB.ZLGD().ZL().NR<>1
               || __TAB.PDZL1:=ZLGB.ZLGD().ZL().KTM().KTM
               || __TAB.PDZL1:=''
               ?};
               __TAB.PRZEW:=ZLGB.ZLGD().ZGH().NRPRZ
            || __TAB.PDZL:=ZLGB.ZLGD().ZL().KTM().KTM;
               __TAB.WYR:=ZLGB.ZLGD().ZL().KTM().N;
               __TAB.KOD:=ZLGB.ZLGD().ZL().KTM().KTM;
               __TAB.ZLEC:=ZLGB.ZLGD().ZL().SYM;
               __TAB.PRZEW:=ZLGB.ZLGD().ZGH().NRPRZ;
               __TAB.REFZL:=#ZLGB.ZLGD().ZL;
               __TAB.PDZL1:='-'
            ?};
            __TAB.OPERACJA:=ZLGB.ZLGD().ZGP().OPIS;
            __TAB.ILOSC:=ZLGB.IL;
            __TAB.CZASPL:=ZLGB.TIME_P;
            __TAB.CZASR:=ZLGB.TIME;
            __TAB.prefix();
            __TAB.add()
         ?}
      ?};
      ZLGB.next()
   !};
   ZLGD.clear();
   ZLGD.index(ndx);
   ZLGD.prefix(ST.OKR_REF,null(),null());
   {? ZLGD.first()
   || {!
      |? __TAB.prefix(#ZLGD.ZL,$ZLGD.ZGH,#ZLGD.ZGP);
         {? __TAB.first()
         || __TAB.ILOSC+=ZLGD.IL;
            __TAB.CZASPL+=ZLGD.TIME_P;
            __TAB.CZASR+=ZLGD.TIME;
            __TAB.put()
         || __TAB.REFPRZ:=$ZLGD.ZGH;
            __TAB.POZ:=#ZLGD.ZGP;
            {? exec('top_rodzaj','zl_link',ZLGD.ZL)='Z'
            || ZL.cntx_psh();
               ZL.index('UNRZL');
               ZL.prefix(ZLGD.ZL().NRNZL);
               {? ZL.first()
               || __TAB.PDZL:=ZL.KTM().KTM
               || __TAB.PDZL:='-'
               ?};
               __TAB.WYR:=ZL.KTM().N;
               __TAB.KOD:=ZL.KTM().KTM;
               __TAB.ZLEC:=ZL.SYM;
               __TAB.REFZL:=#ZL.ref();
               ZL.cntx_pop();
               {? ZLGD.ZL().NR<>1
               || __TAB.PDZL1:=ZLGD.ZL().KTM().KTM
               || __TAB.PDZL1:=''
               ?};
               __TAB.PRZEW:=ZLGD.ZGH().NRPRZ
            || __TAB.PDZL:=ZLGD.ZL().KTM().KTM;
               __TAB.WYR:=ZLGD.ZL().KTM().N;
               __TAB.KOD:=ZLGD.ZL().KTM().KTM;
               __TAB.ZLEC:=ZLGD.ZL().SYM;
               __TAB.PRZEW:=ZLGD.ZGH().NRPRZ;
               __TAB.REFZL:=#ZLGD.ZL;
               __TAB.PDZL1:='-'
            ?};
            __TAB.OPERACJA:=ZLGD.ZGP().OPIS;
            __TAB.ILOSC:=ZLGD.IL;
            __TAB.CZASPL:=ZLGD.TIME_P;
            __TAB.CZASR:=ZLGD.TIME;
            __TAB.prefix();
            __TAB.add()
         ?};
         ZLGD.next()
      !}
   ?};
~~}
{MACRO-}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{SUBSTITUTE: 'filtrprac'}
{SUBSTITUTE: 'usttab'}
{FOR: __TAB}
{INDEX: ndx1}
{DO}
 {FIRST}
     {IF: ~tryb & lineleft<13}
     {PAGE}
     {FI}
     {IF: ~tryb}
        {<{lmt+1} 'Zlecenie: %1 Przewodnik: %2'@[__TAB.ZLEC+20,__TAB.PRZEW]}
        {<{lmt+1} 'Kod wyrobu: %1'@[__TAB.KOD]}
        {<{lmt+1} 'Nazwa wyrobu: %1'@[__TAB.WYR]}
     {FI}
     {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1;~~}
     {zlec:=__TAB.REFZL; przew:=__TAB.REFPRZ; ~~}
 {FIRST-}
 {IF: tryb | lineleft > 5} {SUBSTITUTE: 'LINIA0'}
 {ELSE}{SUBSTITUTE: 'LINIAF'}{PAGE}{FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}{SUBSTITUTE: 'LINIA0'}
 {FI}
{TOTAL: __TAB.REFZL;__TAB.REFPRZ}
  {IF: tryb<>1}
     {SUBSTITUTE: 'LINIAF'}
     {IF: zlec<>__TAB.REFZL | przew<>__TAB.REFPRZ}
        {IF: lineleft<13}
           {PAGE}
        {FI}
        {<{lmt+1} 'Zlecenie: %1 Przewodnik: %2'@[__TAB.ZLEC+20,__TAB.PRZEW]}
        {<{lmt+1} 'Kod wyrobu: %1'@[__TAB.KOD]}
        {<{lmt+1} 'Nazwa wyrobu: %1'@[__TAB.WYR]}
        {FONT: 'T8B'}{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; bylo:=1;~~}
        {zlec:=__TAB.REFZL; przew:=__TAB.REFPRZ; ~~}
     {FI}
  {FI}
{OD}
{IF: tryb=1 & __TAB.size()}{SUBSTITUTE: 'LINIAF'}{FI}

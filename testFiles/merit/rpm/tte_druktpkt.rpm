:!UTF-8
{TITLE:Typy kart technologicznych}
{PRINTER: INFO.SZ_PRN,,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   TPKTL.cntx_psh();
   dalej:=0;
   PRN:=obj_new(@.CLASS.PRINT,13);
   PRN.s_frame();
   prn1:='PRN';
   kreska := '─';
   color:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   row:=''; canprint:=1
}
{END:
   TPKTL.cntx_pop();
   &dalej;
   obj_del(PRN);
   &prn1;&kreska;
   &color;
   &lm; &ll; &vp; &lmt; &rsep;
   &row; &canprint
}
{COMMENT}
  Wydruk tabeli typow kart technologicznych.
  [TS] & [SS] - 2000/07/13 - [7.22]
  [SS] - 2001/03/29 - [7.41] - dadanie listy wariantow kalkulacyjnych
  [TS] & [MKO] - 2001/10/09 - [7.53] OWwSK003_Aneks1
  [TS] - 2002/02/11 - [7.60] - dodanie pola TPKTL.PAR
  Wywołanie: Ustawienia i parametryzacja -> Produkcja -> Typy kart technologicznych
     -> Typy kart technologicznych -> Drukuj
{COMMENT-}
{"---Definicja wiersza wydruku typow kart technologicznych TPKTL.         "; ~~}
{  PRN.add("{? canprint || TPKTL.TYP || '' ?}",5,'Typ karty'@);
   "---W tej kolumnie pod opisem bedzie drukowany spis wariantow kalkulacji.";
   "---Drukowany wiersz nalezy umiescic w zmiennej 'row'.                   ";
   PRN.add("row",50,'Opis typu'@+' / '+'Warianty kalkulacji'@);
   PRN.add("{? canprint || form(TPKTL.XJM,,3) || '' ?}",12,'XJM'@,1);
   PRN.add("{? canprint || TPKTL.OPER || '' ?}",9,'Operacje'@);
   PRN.add("{? canprint || {? TPKTL.UTIL='N' || 'N' || 'T' ?} || '' ?}",9,'Narzędzia'@);
   PRN.add("{? canprint || form(TPKTL.CRUB,,0,'9') || '' ?}",10,'Nr rubr.ceny'@,1);
   PRN.add("{? canprint || form(TPKTL.CRUBMAT,,0,'9') || '' ?}",10,'Nr rubr.mat'@,1);
   PRN.add("{? canprint || form(TPKTL.CRUBROB,,0,'9') || '' ?}",10,'Nr rubr.rob'@,1);
   PRN.add("{? canprint || form(TPKTL.CRUBIL,,0,'9') || '' ?}",10,'Nr rubr.il'@,1);
   PRN.add("{? canprint || TPKTL.PAR || '' ?}",2,'P?');
   PAR_WYDR.TITLE:='WYKAZ TYPÓW KART TECHNOLOGICZNYCH'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'Legenda:'@}
{<{lmt+1} 'T   - '+'Operacje/narzędzia występują'@}
{<{lmt+1} 'N   - '+'Operacje/narzędzia nie występują'@}
{<{lmt+1} 'P?  - '+'Technologia parametryzowana'@}
{<{lmt+1} '*   - '+'Domyślny wariant kalkulacji'@}
{FONT: 'T8B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'C'}
{ lp:=1; TPKTL.index('TYP'); ~~}
{ rsep:=PAR_WYDR.RSEP; ~~}
{ TPKTL.prefix(); ~~}
{IF: TPKTL.first()}{ dalej:=1; ~~}
{WHILE: dalej}
{DO}
    {canprint:=1; ~~}
    {row:=TPKTL.OPIS; ~~}
    {SUBSTITUTE: 'LINIA0'}
    {"---Teraz juz nie drukujemy zawartosci kolumn - oprocz kolumny Opis. "; ~~}
    {canprint:=0;rsep:=0;~~}
    {"---Oddziel opis od wariantow pozioma kreska.                        "; ~~}
    {row:=kreska*50; ~~}
     {SUBSTITUTE: 'LINIA0'}
     {"---Dodaj opis, ze sa to warianty kalkulacji.                        "; ~~}
    {row:=' '+'Warianty kalkulacji:'@; ~~}
    {SUBSTITUTE: 'LINIA0'}
    {"---Teraz drukuj warianty analityczne.                               "; ~~}
    {TPKTLF.index('TW'); ~~}
    {TPKTLF.prefix(TPKTL.ref()); ~~}
    {opccount:=TPKTLF.size(); ~~}
    {iter:=1; ~~}
    {FOR: 'TPKTLF'}
      {INDEX: 'TW'}
      {PREFIX: TPKTL.ref()}
    {DO}
      {row:={? opccount > 1
            || form(iter,2)+'. '+TPKTLF.OPC().NAZ
            || ' '+TPKTLF.OPC().NAZ
            ?}; ~~}
      {"---Domyslny wariant kalkulacji zaznacz gwiazdka.                 "; ~~}
      {IF: TPKTL.DEF_OPCK=TPKTLF.OPC}
        {row:='*'+(1-row); ~~}
      {FI}
      {iter+=1; ~~}
     {SUBSTITUTE: 'LINIA0'}
  {OD}
  {dalej:=TPKTL.next(); rsep:=PAR_WYDR.RSEP;row:=''; ~~}
{OD}
{FI}

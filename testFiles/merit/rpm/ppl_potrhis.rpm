:!UTF-8
{TITLE:Historia spłaty potrąceń (wybranych)}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'M','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PAR_WYDR.TITLE:='Rozliczenie potrąceń'@;

   Cntx.psh(P,OSOBA,R,KOM_OS,KOM_SP);
   _ndx:=KOM_OS.ndx_tmp(,1,'FIRMA',,,'OSOBA',,,'KOM_RP','R',);

   VAR_DEL.delete('par');
   par:=obj_new(7);
   par[4]:=par[5]:=par[6]:=par[7]:=0;
   b_rat:=1;
   _upr:=
      exec('uprawnieniawrap','pkd','Potrąceniach'@,'PPL_PLL_PPOT','PPL_PLL_RPOT')+
      exec('uprawnieniawrap','pkd','Naliczonych składnikach płacowych'@,'PPL_PLL_RSKP','PPL_PLL_PSKP');

   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || par[1]:=obj_new(@.CLASS.PRINT,7);
      par[1].add("KOM_OS.KOM_RP().N",35,'Nazwa potrącenia'@);
      par[1].add("KOM_OS.A",7,'Aktywne'@);
      par[1].add("KOM_OS.OD$1",10,'Spłata od'@);
      par[1].add("KOM_OS.WART",15,'Wartość'@,1,,,,,'12,2');
      par[1].add("KOM_OS.RATA",11,'Rata'@,1,,,,,'10,2');
      par[1].add("par[7]",15,'Spłacono'@,1,,,,,'12,2');
      par[1].add("KOM_OS.SYG",20,'Sygnatura'@);
      par[1].s_frame();

      par[3]:=0;
      par[2]:=obj_new(@.CLASS.PRINT,5);
      par[2].add("$KOM_SP.R",5,'Rok'@,1);
      par[2].add("$KOM_SP.M",7,'Miesiąc'@,1);
      par[2].add("KOM_SP.KW",17,'Kwota'@,1,,,,,'15,2');
      par[2].add("KOM_SP.P().T",20,'Nr teczki'@);
      par[2].add("KOM_SP.OPIS",70,'Opis'@);
      par[2].s_frame();

      _SKL:=sql('
         select R.RN as RN, R.RT as RT, \'N\' as WB
         from R
         where R.REFERENCE in (select KOM_RP.R from KOM_RP)
         order by 1'
      );
      _where:=
         "  'P.OSOBA
               in (select KOM_OS.OSOBA from KOM_OS where KOM_RP
                     in (select KOM_RP.REFERENCE from KOM_RP join R where R.RN in ('+_a+'))
                  )'
         ";
      _P:=exec('historia_splaty','!ppl_zes_wkar',_SKL,'Potrącenia'@,_where);
      _P.index(_P.ndx_tmp(,1,'LP',,));
      params_set('P',_P,'SKL',_SKL,'ndx',_ndx);
      _P.ndx_drop()
   ?}
}
{END:
   VAR_DEL.delete('par','b_rat');
   KOM_OS.ndx_drop();
   Cntx.pop(P,OSOBA,R,KOM_OS,KOM_SP)
}
{EXIT: ~params_get().P.size()}
{COMMENT}OLD: potrhis.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzial.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{  par[1].setcolor(color); par[1].ini_head();
   par[2].setcolor(color); par[2].ini_head()
}
{REP: par[1].fhbar(lmt)}
{REP: par[1].h_fline(lmt)}
{REP: '{<{lmt+1}}'+par[1].fjbar(par[2])}
{REP: par[2].h_fline(lmt)}
{REP: par[2].flbar(lmt)}
{  par[1].setcolor('255:255:255');
   par[2].setcolor('255:255:255')
}
{HEADER-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/=charleft(); ~~ }
{SPACE: 'B'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{FOR: params_get().P}
{DO}
   {par[4]:=0; ~~ }
   {FOR: params_get().SKL}
   {DO}
      {IF: params_get().SKL.WB='T'}
         {  P.prefix();
            P.seek(params_get().P.REF,);
            R.index('RUBKOD');
            R.prefix(params_get().SKL.RN);
            R.first(); ~~
         }
      {FOR: KOM_OS}
      {INDEX: params_get().ndx}
      {PREFIX: exec('ref_firma','ustawienia'),P.OSOBA,R.ref()}
      {DO}
         {ENTIRE}
         {FIRST}
         {IF: ~par[4]}
{FONT: 'W12'}
{SPACE: 'A'}
{<{ceil(lmt*b_rat)+1}}{form(par[5]+=1,,,'9.')} {INCLUDE: p_osoba.rpi}
{FONT: 'T8'}
{SPACE: 'B'}
         { par[4]:=1; ~~ }
         {FI}
         {FIRST-}
         { par[3]:=0; ~~ }
         {REP: par[1].fhbar(lmt)}
         {  par[7]:=exec('licz_splate_komornik','lista_licz');
            par[1].ini_line(); ~~
         }
         {WHILE: par[1].next()}
         {DO}
            {REP: par[1].fline(lmt)}
         {OD}
         {FOR: KOM_SP}
         {PREFIX: KOM_OS.ref()}
         {DO}
            {FIRST}
            {REP: '{<{lmt+1}}'+par[1].fjbar(par[2])}
            {FIRST-}
            {  par[2].ini_line();
               par[3]:=1; ~~
            }
            {REP: par[2].fline(lmt)}
         {OD}
         {IF: par[3]}
            {REP: par[2].flbar(lmt)}
         {ELSE}
            {REP: par[1].flbar(lmt)}
            {<{ceil(lmt)+1} 'Potrącenie nierozliczone'@}
         {FI}
         {ENTIRE-}
      {OD}
      {FI}
   {OD}
{OD}
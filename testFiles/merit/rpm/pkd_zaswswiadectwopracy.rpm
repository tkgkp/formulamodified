:!UTF-8
{TITLE:Świadectwo pracy}
{VERSION: PEP}
{PRINTER: dziedzina:='PKD';
   exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PKD_ZASWSW')='T',,,,'I','B',,,,,,1,
   PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('__HUM_win','nieo','Temp','__TAB','__CALL','ekw','ekw_nsp','fnorma','cnt','__PRAC','par','sw',
                  'dn','gd','__TABSZ','__STOS');
   VAR_DEL.delete('czy_szablon','czy_paperless','tytul','czy_pep','wyjdz','Text','__ppsf');
   VAR_DEL.delete('__nru01','gender');
   Text:='';
   _par_pep:=params_get();
   tytul:='Świadectwo pracy';
   czy_szablon:=exec('rep_set','rap_zew','PKD_ZASWSW',1)='T';
   czy_pep:=var_pres('_par_pep')>100 & var_pres('PEP',_par_pep)>0;
   wyjdz:=0;
   P.cntx_psh();
   H_UM.cntx_psh();
   H.cntx_psh();
   DS.cntx_psh();
   S_ZUS.cntx_psh();
   S_ZUS.prefix();
   __ppsf:=exec('is_pzd01','ppsf');
   __nru01:=exec('upgrade2226_nru01','wnioski_urlopowe');
   gender:=exec('gender','peronel_wydruki');

   _max:={? (_tmax:={? P.f_active() || P.f_size() || P.size() ?})>32767 || 32767 || &_tmax ?};
   __STOS:=obj_new(@.CLASS.STACK,2*_max);
   __xr_ins:=exec('new_ins','rap_zew','PKD_ZASWSW');
   _upr:=exec('uprawnieniawrap','pkd','świadectwa pracy'@,'PKD_ZES_ORZA','PKD_EZK_WSPR');
   {? +_upr
   || Text:='Brak wymaganych uprawnień pozwalających na przygotowanie wydruku %1'@ [_upr];
      FUN.emsg(Text)
   || _paperless:=czy_pep & var_pres('active',_par_pep.PEP)>0 & _par_pep.PEP.active;
      {? _paperless
      || czy_paperless:=0;
         {? ~czy_szablon & exec('rep_set','rap_zew','PKD_ZASWSW')<>'T' & _par_pep.PEP.fml_after=""
         || czy_paperless:=1
         ?}
      || czy_paperless:=1
      ?};

      {? _paperless
      || {? _par_pep.PEP.P
         || params_set(
               'P',exec('wybierz_silent','pracownik',_par_pep.PEP.P).P,
               'paperless',_paperless,
               'par_pep',_par_pep,
               'CALL',null()
            )
         || _args:=exec('wybierz_args','pracownik');
            _args.DOMAIN:=dziedzina;
            _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
            _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
            _args.F_ZATR:=__PARSES.getVal('F_ZATR').KOD;
            _args.VIEW:=__PARSES.getVal('ZakresDanych');
            _args.SQL_FROM:=" JOIN OSOBA using(P.OSOBA, OSOBA.REFERENCE)"+
                            " JOIN USERS using(USERS.OSOBA, OSOBA.REFERENCE)"+
                            " JOIN USERSF using(USERSF.USERS, USERS.REFERENCE)";
            _args.SQL_WHERE:=''+"P.PORTAL='T' and USERSF.PORTAL2='T'";
            _args.WIELU:=~_paperless;
            params_set(
               'P',exec('wybierz_parses','pracownik',dziedzina,_args).P,
               'paperless',_paperless,
               'par_pep',_par_pep,
               'CALL',null()
            )
         ?};
         _upr:={? ~params_get(1).P.first() || 'Nie wybrano pracownika' || '' ?}
      || params_set(
            'P',null(),
            'paperless',_paperless,
            'par_pep',_par_pep,
            'CALL',null(),
            'args',~~
         )

      ?}
   ?};
   {? ~+_upr
   || sw:=obj_new('UDO','UOD','USPKODO','USPKODK','USPPPRAK','ROU','DS_LIM','OK','IL','ALL_UM','PROCES',
                  'DS_LIM_SW','OJCIEC_W','RODZIC_W');
      __HUM_win:=obj_new('ws','grp');
      H.index('_HISTKOD');
      H_UM.index('OD');
      DS.index('DYSCYPLI');
      nieo:=obj_new(30);
      'nieo[26] - 00, 01, 10, 11 - służba lub ćwiczenia przed/po 2022.04.23';
      'nieo[29] - urlop opiekuńczy (dni), nieo[30] - siła wyższa';
      'nieo[27/28] - urlop rehabilitacyjny (w Xpertis 25/26)';
      fnorma:="8";
      Temp:='';
      sw.OK:=sw.IL:=ekw:=ekw_nsp:=cnt:=0;
      __TAB:=tab_tmp(1,
         'OD','DATE','Data od',
         'DO','DATE','Data do');
      sw.UDO:=sw.UOD:=#0;
      sw.USPKODO:='';
      sw.USPKODK:='';
      sw.USPPPRAK:='';
      sw.ROU:='';
      'rodzaj limitu Art. 188 KP w Dyscyplinie pracy';
      sw.DS_LIM:='R';
      sw.ALL_UM:=0;
      sw.PROCES:=0;
      'rodzaj limitu siła wyższa w Dyscyplinie pracy';
      sw.DS_LIM_SW:='R';
      {? czy_szablon
      || __TABSZ:=tab_tmp(1,
            'CNT','INTEGER','Numer zestawu',
            'P_REF','STRING[16]','Referencja do pracownika',
            'SZCZ_WAR','STRING[100]','Szczególne warunki'
            );
         __TABSZ.index(__TABSZ.ndx_tmp(,,'CNT',,1))
      ?};
      par:=params_get();
      {? type_of(par)=type_of(~~)
      || par:=obj_new('H_UM','P','ZAL','args')
      ?};
      {? var_pres('H_UM',par)<=0
      || _par:=exec('obj_ntab_set','#array',par,'H_UM',null());
         obj_del(par);par:=_par; obj_del(_par)
      ?};
      {? var_pres('P',par)<=0
      || _par:=exec('obj_ntab_set','#array',par,'P',null());
         obj_del(par);par:=_par; obj_del(_par)
      ?};
      {? var_pres('ZAL',par)<=0
      || _par:=exec('obj_ntab_set','#array',par,'ZAL','1');
         obj_del(par);par:=_par; obj_del(_par)
      ?};
      {? var_pres('P',par)=type_of(null) & par.P<>null
      || sw.PROCES:=1
      ?};
      {? var_pres('args',par)<=0
      || _par:=exec('obj_ntab_set','#array',par,'args',~~);
         obj_del(par);par:=_par; obj_del(_par);
         par.args:=exec('wybierz_args','pracownik')
      ?}
   ?}
}
{END:
   S_ZUS.cntx_pop();
   DS.cntx_pop();
   H.cntx_pop();
   H_UM.cntx_pop();
   P.cntx_pop();
   exec('del_ins','rap_zew',&__xr_ins);
   {? var_pres('__HUM')>0 & __HUM_win.grp<>~~ || H_UM.win_del(__HUM_win.grp) ?};
   {? var_pres('__HUM')>0 & __HUM_win.ws<>~~ || H_UM.win_del(__HUM_win.ws) ?};
   VAR_DEL.delete('__HUM_win','nieo','Temp','__TAB','__CALL','ekw','ekw_nsp','fnorma','cnt','__PRAC','par','sw',
                  'dn','gd','__TABSZ','__STOS');
   VAR_DEL.delete('czy_szablon','czy_paperless','tytul','czy_pep','wyjdz','dziedzina','Text','__ppsf');
   VAR_DEL.delete('__nru01','gender');
   undefine()
}
{COMMENT}OLD: zaswsw.rpm{COMMENT-}
{COMMENT}OLD: zaswsw.rpi{COMMENT-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{EXIT: var_pres('par')<0}
{EXIT: ~__xr_ins}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{  __HUM_win.ws:=H_UM.mk_sel(,'N',,'#h_um_wyb',,,,,'P');
   H_UM.win_fld(__HUM_win.ws,,'OD');
   H_UM.win_fld(__HUM_win.ws,,'DO');
   H_UM.win_fld(__HUM_win.ws,,'RU','K','K',,,,'Typ'@);
   H_UM.win_fld(__HUM_win.ws,,'NUMER');
   H_UM.win_fld(__HUM_win.ws,,'SW',,,,,,'Świadectwo'@,,,2,,"'T'","'N'");
   H_UM.win_fml(__HUM_win.ws,,'NUMER',,'ICON_BEFORE',
      "  'xwin16.png:'+${? sw.UOD<=H_UM.OD & H_UM.DO<=sw.UDO || 38 || 81 ?}
      "
   );
   H_UM.win_fml(__HUM_win.ws,,'RU','K','ICON_BEFORE',
      "  _kod:=H_UM.RU().K;
         'xwin16.png:'+$
         {? _kod='A' || '120'
         |? _kod='B' || '118'
         |? _kod='C' || '111'
         |? _kod='D' || '186'
         |? _kod='E' || '33'
         || '110'
         ?}
      "
   );
   H_UM.win_act(__HUM_win.ws,,'Formuła','Wybierz'@@,,,
      "  _d0:=#0;
         {? sw.UOD=_d0 & sw.UDO=_d0 || sw.UOD:=H_UM.OD; sw.UDO:=H_UM.DO
         |? sw.UDO<H_UM.DO || sw.UDO:=H_UM.DO
         |? H_UM.OD<sw.UOD || sw.UOD:=H_UM.OD
         |? sw.UOD=H_UM.OD & sw.UDO=H_UM.DO || sw.UOD:=sw.UDO:=_d0
         |? sw.UOD=H_UM.OD || sw.UOD:=H_UM.DO+1
         || sw.UDO:=H_UM.OD-1
         ?}
      ",,1,1,,,'W'
   );
   H_UM.win_act(__HUM_win.ws,,'Formuła','Dalej'@@,,,"sel_exit()",,,,,,'D');

   __HUM_win.grp:=H_UM.grp_make('Umowy o pracę'@,,'#h_um_wybgrp',,,
      "  _d0:=#0;
         {? _a='sel_exit'
         || {? sw.UOD=_d0 & sw.UDO=_d0 || sw.UOD:=H_UM.OD; sw.UDO:=H_UM.DO ?}; 1
         |? FUN.ask('Czy na pewno rezygnujesz ze wskazania umów?'@)
         || sw.UOD:=sw.UDO:=_d0; 1
         ?}
      "
   );
   H_UM.grp_sel(__HUM_win.grp,,__HUM_win.ws,,,,,7,,,,,'maximized');
   H_UM.win_sel(__HUM_win.grp);
   ~~
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{WHILE: ~wyjdz & ((~params_get().paperless & exec('wybierz','!pkd_zes_orza',par.P,par.args))
   | (params_get().paperless & params_get().par_pep.PEP.fml_after=""))}
{DO}{
   {? params_get().paperless|| wyjdz:=1 ?};
   __KAL.set_cal({? P.KAL || P.KAL().NAZWA || 'standard' ?});
   fnorma:=exec('norma_fml','godziny');
   sw.UDO:=sw.UOD:=#0;
   sw.USPKODO:='';
   sw.USPKODK:='';
   sw.USPPPRAK:='';
   sw.ROU:='';
   sw.OJCIEC_W:=0;
   sw.RODZIC_W:=0;
   sw.IL+=1;
   ekw:=0;
   ekw_nsp:=0;
   nieo[26]:='00';
   exec('zaswsw_przebieg','peronel_wydruki');
   ~~}
   {IF: sw.UDO<>#0}{
      {! _n:=1..obj_len(nieo) |! nieo[_n]:=0 !};
      DS.prefix(P.ref(),sw.UDO~1,__RUB.ref(__RUB.sys_kod(1162)));
      {? DS.first() || sw.DS_LIM:=DS.NR || sw.DS_LIM:='R' ?};
      nieo[20]:=date(0,0,0);
      nieo[26]:='00';
      _swRref:=__RUB.ref(__RUB.sys_kod(1181));
      {? _swRref
      || DS.prefix(P.ref,sw.UDO~1,_swRref);
         {? DS.first
         || sw.DS_LIM_SW:=DS.NR
         || sw.DS_LIM_SW:='R'
         ?}
      || sw.DS_LIM_SW:='R'
      ?};
      ~~}
      {FOR: N}{INDEX: 'NIEOBECN'}{PREFIX: 'N',P.ref()}
      {DO}
         {exec('zaswsw_nieobecnosci','peronel_wydruki');~~}
      {OD}{
         exec('zaswsw_dane','peronel_wydruki');
         {? czy_szablon
         || __TABSZ.P_REF:=$P.ref();
            __TABSZ.SZCZ_WAR:=DEFINE.SCW;
            __TABSZ.CNT:=cnt;
            {? __TABSZ.add()
            || {? var_pres('_tmp')>100 || obj_del(&_tmp) ?};
               _tmp:=obj_new(obj_len(nieo));
               {! _licz:=1..obj_len(nieo) |!
                  _tmp[_licz]:=nieo[_licz]
               !};
               __STOS<=_tmp;
               {? var_pres('_tmp')>100 || obj_del(&_tmp) ?};
               _tmp:=obj_new(obj_len(sw));
               {! _licz:=1..obj_len(sw) |!
                  _tmp[_licz]:=sw[_licz]
               !};
               __STOS<=_tmp
            ?}
         ?};
         ~~}
   {FI}
{OD}
{EXIT: Text<>'' & cnt=0}
{EXIT: ~exec('ready','rap_zew',__xr_ins) & ~(params_get().paperless & ~czy_paperless)}
{IF: czy_szablon}
   {FOR: __TABSZ}
   {DO}
      {IF: P.seek(__TABSZ.P_REF,,1)}
         { _dane_dla_szablonu:=obj_new('szcz_war');
           _dane_dla_szablonu.szcz_war:=__TABSZ.SZCZ_WAR;
           params_set('dane',_dane_dla_szablonu);
           {? var_pres('_tmp')>100 || obj_del(&_tmp) ?};
           _tmp:=-__STOS;
           {? type_of(_tmp)>100
           || {! _licz:=1..obj_len(sw) |!
               sw[_licz]:=_tmp[_licz]
              !}
           ?};
           {? var_pres('_tmp')>100 || obj_del(&_tmp) ?};
           _tmp:=-__STOS;
           {? type_of(_tmp)>100
           || {! _licz:=1..obj_len(nieo) |!
               nieo[_licz]:=_tmp[_licz]
              !}
           ?};
            _arg:=exec('wypelnij_arg','szablon');
            {? sw.PROCES<>0
            || _arg.parametry.btn_alt:="
                  _a.win_ebtn(_b,'text=%1'[exec('text_red_zakoncz','#window','PKD_A')],\"status:=1;'key:F2'\");
                  _a.win_ebtn(_b,'text=%1'['Drukuj'@],\"status:=0;'key:F2'\");
                  _a.win_ebtn(_b,'text=%1'['Anuluj'@],\"status:=0;'key:Esc'\");
                  ~~
               "
            ?};
            _paperless:=params_get().paperless;
            {? _paperless
            || _file:=exec('wypelnij','szablon','PKD_ZASWSW',,,2,_arg,_paperless);
               {? _paperless & var_pres('_file')>0 & var_pres('INFO',_file)>0
               || exec('add_file_and_run','paperless_grp',
                     P.ref(),_file.INFO,'Inne',tytul)
                  ?}
               || _ret:=exec('wypelnij','szablon','PKD_ZASWSW',,,2,_arg);
                  {? type_of(_ret)>100 & _ret.STATUS=1 || exec('add_zal','zal','sw',,_ret.INFO) ?}
         ?}; ~~
         }
      {ELSE}
         {
            _tmp:=-__STOS;
            &_tmp;
            _tmp:=-__STOS;
            &_tmp;
         ~~}
      {FI}
   {OD}
   {EXIT}
{FI}
{IF: exec('rep_set','rap_zew','zaswsw')='T'}
   {
   exec('use_tmp','rap_zew','zaswsw');
   {? params_get().paperless
   || exec('add_file_and_run','paperless_grp',
         P.ref(),
         '@!Tmp\\macrotmp.doc',
         'Inne',
         tytul
      )
   || {? par.ZAL='1' || exec('add_zal','zal','sw') ?}
   ?};
   ~~}
   {EXIT}
{ELSE}
{
   params_set(_par:=params_get());
   {? _par.paperless & _par.par_pep.PEP.fml_after<>""
   || __CALL:=_par.par_pep.PEP.CALL
   || __CALL:=proc_exec('swpr@zasw',#__xr_ins);
      {? czy_pep || _par.par_pep.PEP.CALL:=__CALL ?}
   ?};
~~}
   {IF: params_get().paperless & params_get().par_pep.PEP.fml_after=""}
      {
      params_get().par_pep.PEP.P:=P.ref();
      params_get().par_pep.PEP.fml_after:="
         _pep:=obj_new('active','fml_after','P','CALL');
         _pep.active:=1;
         _pep.fml_after:=\"1\";
         _pep.P:=_a;
         _pep.CALL:=_b;
         {? var_pres('_params')>100
         || exec('obj_ntab_set','#array',_params,'PEP',_pep)
         || _params:=obj_new('PEP');
            _params.PEP:=_pep
         ?};
         params_set(_params);
         _name:='@'+tmp_dir()+exec('sep','#file')+'pkd_zaswswiadectwopracy_%1'[$P.tm_stamp()]+'.pdf';
         rep_exec('pkd_zaswswiadectwopracy',,0,_name,1,,1);
         exec('add_file_and_run','paperless_grp',
         _pep.P,_name,'Inne','Świadectwo pracy')
      ";
      ~~}
      {EXIT}
   {ELSE}
      {INCLUDE: p_zaswsw.rpi}
   {FI}
{FI}
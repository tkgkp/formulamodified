:!UTF-8
{TITLE:F. Towary zalegające w magazynie - powyżej ? dni}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   mag_106:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('k','w','PRN','kgr');
      VAR_DEL.delete('s');
      VAR_DEL.delete('X_Xb','X_Xc');
      VAR_DEL.delete('wyjscie');
      VAR_DEL.delete('dokl','dok2','j2')";
   mag_106();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   VAR_DEL.delete('k','w','PRN');
   k:=obj_new(9); "--szerokosci kolumn--";
   w:=obj_new(9); "--wartosci kolumn--";
   dokl:=3; "--ilosc miejsc dziesietnych--";
   dok2:=3;
   PRN:=obj_new(@.CLASS.PRINT,10); PRN.s_frame();
   k[1]:=20;
   k[2]:=40;
   k[3]:=9;
   k[4]:=10;
   k[5]:=12;
   k[6]:=15;
   k[7]:=10;
   k[8]:=9;
   k[9]:=15;
   w[1]:=w[2]:=w[3]:=w[4]:=w[5]:=w[6]:=w[7]:=w[8]:=w[9]:='';
   tryb:=rep_export();
   kgr:='';

   s:=0; "--razem--";

   wyjscie:=1;

   HELP.NDZ:=date();
   HELP.DNI:=90;
   HELP.SWG:=1;

   _wydr_naz:='mag_106';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
   {? ~WYDRUKIL.first()
   ||
      WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=_wydr_naz;
      WYDRUKIL.add()
   ||
      HELP.NDZ:=WYDRUKIL.OD_DATY;
      HELP.DNI:=WYDRUKIL.CHKBOX01;
      HELP.SWG:=WYDRUKIL.CHKBOX02
   ?};

   HELP.win_edit('MAG_106');
   {? HELP.edit("__CHK.record(HELP,,'NDZ')")
   ||
      WYDRUKIL.index('WYDRUKI');
      WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
      {? WYDRUKIL.first()
      ||
         WYDRUKIL.OD_DATY:=HELP.NDZ;
         WYDRUKIL.CHKBOX01:=HELP.DNI;
         WYDRUKIL.CHKBOX02:=HELP.SWG;
         WYDRUKIL.put()
      ?};
      wyjscie:=0
   ?}
}
{END:
   {? var_pres('X_Xc')>100 || X_Xc.ndx_drop() ?}; mag_106(); VAR_DEL.delete('mag_106')
}
{IF: wyjscie=1}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
PRN.add("w[1]",k[1],'Indeks'@);
PRN.add("w[2]",k[2],'Nazwa materiału'@);
PRN.add("w[4]",k[4],'Data dostawy'@);
PRN.add("w[5]",k[7],'Zalega dni'@,1);
PRN.add("w[3]",k[3],'jm'@);
PRN.add("w[6]",k[5],'Stan ilościowy'@,1);
PRN.add("w[8]",k[8],'jm'@);
PRN.add("w[9]",k[9],'Stan ilościowy 2'@,1);
{? exec('upr_cm','ceny') & ST.MAG().IL<>'T' || PRN.add("w[7]",k[6],'Stan wartościowy'@,1) ?};

"--obliczenia--";
VAR_DEL.delete('X_Xb');
X_Xb:=tab_tmp(1,
   'KTM' ,'STRING[50]'  ,'Indeks',
   'N'   ,'STRING[100]' ,'Nazwa',
   'JM'  ,'STRING[10]'  ,'jm',
   'D'   ,'DATE'        ,'Data',
   'RDK' ,'INTEGER'     ,'Ref dok. dostawy',
   'NDK' ,'STRING[20]'  ,'Name dok. dostawy',
   'IL'  ,'REAL'        ,'Ilość',
   'WAR' ,'REAL'        ,'Wartość',
   'MGR' ,'STRING[8]'   ,'Grupa materiałowa',
   'J2'  ,'STRING[10]'  ,'jm',
   'IL2' ,'REAL'        ,'Ilość 2');
_rndk:=X_Xb.ndx_tmp(,,'RDK',,,'NDK',,);
_ktm :=X_Xb.ndx_tmp(,,'KTM',,,'KTM',,);


_sr:='Ś'=(1+ST.MAG().TYP);
M.index('RODZ'); M.prefix('T');
{? M.first()
||
   {!
   |? echo('Trwają obliczenia dla materiału: %1 %2'@[M.KTM,M.N]);
      _j2:=M.J2<>null();
      X_Xb.index(_rndk); X_Xb.prefix();
      _war:=_il:=_c_dk:=_il2:=0;
      OKR.cntx_psh();
      OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      ||
         ND.cntx_psh();
         DK.cntx_psh();
         {!
         |? ND.use('nagdo'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
            DK.use('dokma'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
            DK.index('DK_SM');
            DK.prefix(ST.MAG,M.ref(),'T',OKR.ROK);
            {? DK.first()
            ||
               {!
               |? {? DK.M().RODZ='T' & DK.N().D<=HELP.NDZ
                  ||
                     {? X_Xb.find_key(DK.RDK,DK.NDK) & DK.DOST=X_Xb.D
                     ||
                        _put:=1
                     ||
                        _dost:=DK.DOST;
                        _put:=0;
                        X_Xb.KTM:=M.KTM;
                        X_Xb.N:=M.N;
                        X_Xb.JM:=M.J().KOD;
                        X_Xb.D:=_dost;
                        X_Xb.MGR:=M.MGR().KOD;
                        {? DK.PRDK=DK.SRDK
                        ||
                           {? #DK.ref()=DK.RDK & DK.name()=DK.NDK & DK.DOST=_dost || X_Xb.D:=DK.DOST ?}
                        ||
                           DK.cntx_psh();
                           DK.use(8+DK.PRDK);
                           DK.prefix();
                           {? DK.seek(BB.sqlint(DK.PRDK),) || X_Xb.D:=DK.DOST ?};
                           DK.cntx_pop()
                        ?};
                        X_Xb.RDK:=DK.RDK;
                        X_Xb.NDK:=DK.NDK;
                        X_Xb.J2:={? _j2 || M.J2().KOD || '' ?}
                     ?};
                     {? _put=1
                     ||
                        {? DK.PLUS='T'
                        || _il+=DK.IL; X_Xb.IL+=DK.IL; {? _j2 || _il2+=DK.IL2; X_Xb.IL2+=DK.IL2 ?}
                        || _il+=-DK.IL; X_Xb.IL+=-DK.IL; {? _j2 || _il2+=-DK.IL2; X_Xb.IL2+=-DK.IL2 ?}
                        ?};
                        {? DK.PLUS='T' || _war+=DK.WAR; X_Xb.WAR+=DK.WAR || _war+=-DK.WAR; X_Xb.WAR+=-DK.WAR ?};
                        X_Xb.put()
                     ||
                        _il+=X_Xb.IL:={? DK.PLUS='T' || DK.IL  || -DK.IL  ?};
                        _il2+=X_Xb.IL2:={? _j2 || {? DK.PLUS='T' || DK.IL2  || -DK.IL2  ?} || 0 ?};
                        _war+=X_Xb.WAR:={? DK.PLUS='T' || DK.WAR || -DK.WAR ?};
                        {? DK.M().DOKL>dokl || dokl:=DK.M().DOKL ?};
                        {? _j2
                        || _dokl_s:=exec('jaka_dok_mjm','jm',M.ref(),M.J2,M.J);
                           {? _dokl_s>dok2 || dok2:=_dokl_s ?}
                        ?};
                        X_Xb.add()
                     ?}
                  ?};
                  DK.next()
               !}
            ?};
            OKR.next()
         !};
         DK.cntx_pop();
         ND.cntx_pop()
      ?};
      OKR.cntx_pop();
      "--dla srednich i ewidenycjnych uzupelnienie wartosci--";
      {? _sr=1
      ||
         _cena:={? _il>0 || (_war/_il)$2 || 0 ?};
         X_Xb.index(_ktm); X_Xb.prefix(M.KTM,M.KTM);
         {? X_Xb.first() || {! |? X_Xb.WAR:=(X_Xb.IL*_cena)$2; X_Xb.put(); X_Xb.next() !} ?}
      ?};
      M.next()
   !};
   echo();
   "--usunięcie stanow zerowych i materialow zalegajacych krocej niz HELP.DNI--";
   X_Xb.prefix();
   {? X_Xb.first()
   || {! |? {? (X_Xb.IL=0 & X_Xb.WAR=0 & X_Xb.IL2=0) | (HELP.NDZ-X_Xb.D)<=HELP.DNI || X_Xb.del() || X_Xb.next() ?} !}
   ?};
   "--zgrupowanie wg dat--";
   X_Xc:=sql('
      select KTM, N, JM, D, sum(IL) IL, sum(WAR) WAR, sum(IL2) IL2, MGR, J2
      from :_a
      group by KTM, N, JM, D, MGR, J2
      having sum(IL)>0'
      ,X_Xb);
   X_Xb.ndx_drop()
?};

PAR_WYDR.TITLE:=
   {? HELP.DNI=1
   || 'ZESTAWIENIE TOWARÓW ZALEGAJĄCYCH\nW MAGAZYNIE POWYŻEJ %1 DNIA'@[$HELP.DNI]
   || 'ZESTAWIENIE TOWARÓW ZALEGAJĄCYCH\nW MAGAZYNIE POWYŻEJ %1 DNI'@[$HELP.DNI]
   ?};
prn1:='PRN'
;~~}
{IF: X_Xc.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{LINE}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
{<{lmt+1}}{'Na dzień: %1'@[(HELP.NDZ$6)]}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8G'}{SPACE: 'C'}
   {LINE}
   {<{lmt+1}}{'Parametry wydruku:'@}
   {<{lmt+1}}{'Dla magazynu: %1'@[ST.MAG().SYM]}
   {<{lmt+1}}{'Na dzień: %1'@[(HELP.NDZ$6)]}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE}
{FOOTER-}
{COMMENT}   drukowanie pozycji wydruku                               {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{rsep:=PAR_WYDR.RSEP
;~~}
{FOR: X_Xc}
{INDEX: {? HELP.SWG=1 || X_Xc.ndx_tmp(,,'KTM',,)
        |? HELP.SWG=2 || X_Xc.ndx_tmp(,,'N',,)
        |? HELP.SWG=3 || X_Xc.ndx_tmp(,,'D',,)?}}
{DO}
   {FIRST}
   { dokl:=exec('fld_prec','#field',X_Xc,'IL');
     dok2:=exec('fld_prec','#field',X_Xc,'IL2');~~}
   {FIRST-}
   {
    w[1]:=form(X_Xc.KTM);
    w[2]:=form(X_Xc.N);
    w[3]:=form(X_Xc.JM);
    w[4]:=form(X_Xc.D);
    w[5]:=form(HELP.NDZ-X_Xc.D);
    w[6]:=form(X_Xc.IL,,dokl,'.,');
    w[7]:=form(X_Xc.WAR,,2,'.,');
    w[8]:=form(X_Xc.J2);
    w[9]:={? X_Xc.J2<>'' || form(X_Xc.IL2,,dok2,'.,') || '' ?};
    kgr:=form(X_Xc.MGR);
    s+=X_Xc.WAR;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{IF: tryb<>1}
{IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
{SUBSTITUTE: 'LINIAF'}
{FOOTER}{FOOTER-}
{FONT: 'T8GB'}{SPACE: 'C'}
{IF: ST.MAG().IL<>'T' & exec('upr_cm','ceny')}
   {
   PRN.n_frame();
   w[1] :=w[2] :=w[3] := w[4] :=w[5] :='';
   w[6] :='Razem:'@; PRN.FORM[2]:=0;
   w[7] :=form(s,,2,'.,')
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{FI}
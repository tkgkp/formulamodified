:!UTF-8
{TITLE:C. Zestawienie zgłoszeń wg zasobów - koszty}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MBOT,PAR_WYDR.MLEFT,
      PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   dalej:=0;
   dalej2:=0;
   {? var_press('PRN') > 0 || obj_del(PRN) ?};
   PRN:=obj_new(@.CLASS.PRINT,8); PRN.s_frame();
   {? var_press('PRNS') > 0 || obj_del(PRNS) ?};
   PRNS:=obj_new(@.CLASS.PRINT,7); PRNS.s_frame();
   prn1:='PRNS';
   prn2:='PRN';
   tmp1:='';tmp2:='';
   tryb:=rep_export();
   color:='';
   vp:=lmt:=rsep:=0;
   d1:=date(0,0,0);
   d2:=date(0,0,0);
   chck1:=0;
   chck2:=0;
   chck3:=0;
   chck4:=0;
   chck5:=0;
   xx:=-1;
   yy:=-1;
   par1:='';
   prfx:='';
   godziny:=0;
   dni:=0;
   koszty:=0;
   czy_where:=0;
   q_filtr:='';
   q_data:='';
   {? var_press('query') > 0 || obj_del(query) ?};
   {? var_press('query2') > 0 || obj_del(query2) ?}
}
{END:
   undefine();
   &dalej;
   &dalej2;
   obj_del(PRN);
   obj_del(PRNS);
   &prn1;
   &prn2;
   &tmp1;
   &tmp2;
   &xx;
   &yy;
   &color;
   &vp; &lmt; &rsep;
   &tryb;
   &d1;
   &d2;
   &chck1;
   &chck2;
   &chck3;
   &chck4;
   &chck5;
   &par1;
   &prfx;
   &godziny;
   &dni;
   &koszty;
   &czy_where;
   &q_filtr;
   &q_data;
   {? var_press('query') > 0 || obj_del(query);&query ?};
   {? var_press('query2') > 0 || obj_del(query2);&query2 ?}
}
{MACRO: 'TABHEADX'}
{ _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn1+'.ini_head()'; ($(_f))()}
{REP: _f:=prn1+'.fhbar(lmt)'; ($(_f))()}
{WHILE: _f:=prn1+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn1+'.h_fmline(lmt)'; ($(_f))()}
{OD}
{REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
{ _f:=prn2+'.setcolor(color)'; ($(_f))(); ~~}
{ _f:=prn2+'.ini_head()'; ($(_f))()}
{WHILE: _f:=prn2+'.h_next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.h_fmline(lmt)'; ($(_f))()}
{OD}
   { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
   { _f:=prn2+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}
{MACRO-}
{MACRO: 'LINIA02X'}
{  ll:=lineleft(); ~~}
{IF:  params_set(params_get());
      _f:='params_set(params_get()); '+prn2+'.ini_line()'; _v:=($(_f))();
      _f:=prn2+'.length()'; _v:=($(_f))();
      ll<=(rsep+_v)&&ll<>0}
      {IF: xx=0}{yy:=0;~~}{ELIF: xx=1}{yy:=1;~~}{ELSE}{yy:=-1;~~}{FI}
      {PAGE}{FI}
{ENTIRE}
  {IF: vp}
   { _f:=prn1+'.setcolor(color)'; ($(_f))(); ~~}
   {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   { _f:=prn1+'.setcolor(''255:255:255'')'; ($(_f))(); ~~}{ vp:=0; ~~}
{ELSE}
   {IF: PAR_WYDR.RSEP & rsep}
      {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   {ELIF: rsep}
      {REP: _f:=prn1+'.fjbar('+prn2+',lmt)'; ($(_f))()}
   {FI}
{FI}
{WHILE: _f:=prn2+'.next()'; ($(_f))()}
{DO}
   {REP: _f:=prn2+'.fline(lmt)'; ($(_f))()}
{OD}
{IF: var_pres('fun')>=0 & fun<>''}
{  $fun(); ~~}
{FI}
{ENTIRE-}
{MACRO-}
{COMMENT}-----------------------Wykonanie akcji inicjalizujacych.---{COMMENT-}
{_wydr_naz:='zgl_003';
   _edit := WYDRUKIL.mk_edit('Parametry zestawienia'@,,'zgl_003',,,,);
   WYDRUKIL.win_esep(_edit,'Status rejestracji'@);
   WYDRUKIL.win_efld(_edit,,'CHKBOX01',,,,,,
   exec('status_new','remonty_zgloszenia',1),,,'check-box',,"1","0");
   WYDRUKIL.win_efld(_edit,,'CHKBOX02',,,,,,
   exec('status_ended','remonty_zgloszenia',1),,,'check-box',,"1","0");
   WYDRUKIL.win_efld(_edit,,'CHKBOX03',,,,,,
   exec('status_verified','remonty_zgloszenia',1),,,'check-box',,"1","0");
   WYDRUKIL.win_efld(_edit,,'CHKBOX04',,,,,,
   exec('status_completed','remonty_zgloszenia',1),,,'check-box',,"1","0");
   WYDRUKIL.win_efld(_edit,,'CHKBOX05',,,,,,
   exec('status_closed','remonty_zgloszenia',1),,,'check-box',,"1","0");
   WYDRUKIL.win_esep(_edit,'Data wystąpienia awarii'@);
   WYDRUKIL.win_efld(_edit,,'OD_DATY',,,,,,'Od'@);
   WYDRUKIL.win_efld(_edit,,'DO_DATY',,,,,,'Do'@);
   exec('ok_esc','#window',WYDRUKIL,_edit);
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
{? ~WYDRUKIL.first()
|| WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.CHKBOX01:=1;
   WYDRUKIL.CHKBOX02:=1;
   WYDRUKIL.CHKBOX03:=1;
   WYDRUKIL.CHKBOX04:=1;
   WYDRUKIL.CHKBOX05:=1;
   WYDRUKIL.add()
?};
   WYDRUKIL.win_edit(_edit);
~~}
{IF: WYDRUKIL.edit($("{? WYDRUKIL.OD_DATY=date(0,0,0) & WYDRUKIL.DO_DATY<>date(0,0,0)
                  || FUN.info('"+'Należy podać obie daty albo żadnej.'@+"'); 'OD_DATY'
                  |? WYDRUKIL.OD_DATY<>date(0,0,0) & WYDRUKIL.DO_DATY=date(0,0,0)
                  || FUN.info('"+'Należy podać obie daty albo żadnej.'@+"'); 'OD_DATY'
                  |? WYDRUKIL.DO_DATY<WYDRUKIL.OD_DATY
                  || FUN.info('"+'Data końcowa nie może być wcześniejsza niż początkowa.'@+"'); 'OD_DATY'
                  |? WYDRUKIL.CHKBOX01=0 & WYDRUKIL.CHKBOX02=0 & WYDRUKIL.CHKBOX03=0
                     & WYDRUKIL.CHKBOX04=0 & WYDRUKIL.CHKBOX05=0
                  || FUN.info('"+'Należy wybrać przynajmniej jeden status.'@+"'); 'CHKBOX01'
                  || 1
                  ?}"))}
{
   d1:=WYDRUKIL.OD_DATY;
   d2:=WYDRUKIL.DO_DATY;
   chck1:=WYDRUKIL.CHKBOX01;
   chck2:=WYDRUKIL.CHKBOX02;
   chck3:=WYDRUKIL.CHKBOX03;
   chck4:=WYDRUKIL.CHKBOX04;
   chck5:=WYDRUKIL.CHKBOX05;
   WYDRUKIL.put(1);
~~}
{ELSE}
{EXIT}
{FI}
{COMMENT}--------Warunki zapytania.---{COMMENT-}
{IF: chck1=1 & chck2=1 & chck3=1 & chck4=1 & chck5=1}
{q_filtr:='';par1:='Wszystkie'@;~~}
{ELSE}
{  _statusy :='';
   q_filtr:='';par1:='';
   {? chck1=1
   || _statusy+='\''+exec('status_new','remonty_zgloszenia')+'\', ' ;
      par1+=exec('status_new','remonty_zgloszenia',1)+', '
   ?};
   {? chck2=1
   || _statusy+='\''+exec('status_ended','remonty_zgloszenia')+'\', ';
      par1+=exec('status_ended','remonty_zgloszenia',1)+', '
   ?};
   {? chck3=1
   || _statusy+='\''+exec('status_verified','remonty_zgloszenia')+'\', ';
      par1+=exec('status_verified','remonty_zgloszenia',1)+', '
   ?};
   {? chck4=1
   || _statusy+='\''+exec('status_completed','remonty_zgloszenia')+'\', ';
      par1+=exec('status_completed','remonty_zgloszenia',1)+', '
   ?};
   {? chck5=1
   || _statusy+='\''+exec('status_closed','remonty_zgloszenia')+'\', ';
      par1+=exec('status_closed','remonty_zgloszenia',1)+', '
   ?};
   _statusy:=_statusy -2;
   par1:=par1-2;
   {? czy_where=1 || q_filtr:=' and ' || q_filtr:=' where '; czy_where:=1 ?};
   q_filtr+=' STAT_REJ in ('+_statusy+')';
~~
}
{FI}
{IF: d1 <> date(0,0,0)}
{  {? czy_where=1 || q_data:=' and ' || q_data:=' where '; czy_where:=1 ?};
   q_data+=' ZGL.DT_WA > to_date(\''+$d1+'\')
             and ZGL.DT_WA < to_date(\''+$d2+'\') ';
~~}
{ELSE}
{q_data:='';~~}
{FI}
{COMMENT}--------Definicja wiersza wydruku zgłoszeń rementowych.---{COMMENT-}
{
 {? tryb <> 1
 ||
  PRN.add("''",15,'Symbol zasobu'@);
  PRN.add("''",15,'Nazwa zasobu'@);
  PRN.add("''",8,'Liczba zgłoszeń'@,1)
 ||
  PRN.add("query.SYMBOL",15,'Symbol zasobu'@);
  PRN.add("query.NAZWA",15,'Nazwa zasobu'@)
 ?};
  PRN.add("query2.SYM",15,'Symbol zgłoszenia'@);
  PRN.add("query2.STATUS",15,'Status rejestracji'@);
  PRN.add("form(query2.DNI,,2)",10,'Czas serwisu (dni)'@,1);
  PRN.add("form(query2.GODZINY,,5)",10,'Godziny realizacji'@,1);
  PRN.add("form(query2.KOSZTY,,2)",10,'Koszty realizacji'@,1);

  PRNS.add("query.SYMBOL",15,'');
  PRNS.add("query.NAZWA",15,'');
  PRNS.add("query.ILE",8,'',1);
  PRNS.add("''",33,'Dane zgłoszenia'@);
  PRNS.add("form(dni,,2)",10,'',1);
  PRNS.add("form(godziny,,5)",10,'',1);
  PRNS.add("form(koszty,,2)",10,'',1);

  PAR_WYDR.TITLE:='ZESTAWIENIE ZGŁOSZEŃ REMONTOWYCH WG ZASOBÓW - KOSZTY'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8'}{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1}}{form('Wybrane statusy rejestracji:'@,30)}{<{lmt+1+30}}{'%1'[par1]}
{IF: d1 <> date(0,0,0)}
{<{lmt+1+30}}{'Od:'@}{<{lmt+1+50}'Do:'@}
{<{lmt+1}}{'Data wystąpienia awarii:'@}{<{lmt+1+30}d1}{<{lmt+1+50}d2}
{ELSE}
{<{lmt+1}}{'Data wystąpienia awarii:'@}{<{lmt+1+30}'Dowolna'@}
{FI}
{FONT: 'T8B'}
{IF: tryb<>1}
   {SUBSTITUTE: 'TABHEADX'}{FONT: 'T8'}{vp:=1;~~}
{ELSE}
   {prn1:='PRN';~~}
   {SUBSTITUTE: 'TABHEAD'}
{FI}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE:'HEAD'}
{FONT: 'T8B'}{SPACE: 'B'}
{  tmp1:=prn1;
   tmp2:=prn2;
   prn1:='PRNS';
   prn2:='PRN';~~}
{SUBSTITUTE:'TABHEADX'}
{ prn1:=tmp1;prn2:=tmp2;~~}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{IF: yy=0}
{prn1:='PRN';~~}
{SUBSTITUTE: 'LINIAF'}
{prn1:='PRN';prn2:='PRNS';~~}
{ELIF: yy=1}
{prn1:='PRNS';~~}
{SUBSTITUTE: 'LINIAF'}
{prn1:='PRN';prn2:='PRN';~~}
{ELSE}
{prn1:='PRN';~~}
{SUBSTITUTE: 'LINIAF'}
{FI}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP; ~~}
{
 query := sql('
   select
      nvl(ZAS.SYMBOL,\' Bez wskazanego zasobu\')  as SYMBOL,
      ZAS.NAZWA as NAZWA,
      count(DISTINCT ZGL.SYM) as ILE
   from
      REM_ZGL ZGL
      left join @REM_GRG GRG  using (GRG.REM_ZGL,ZGL.REFERENCE)
      left join REM_ZAS ZAS using (ZGL.REM_ZAS,ZAS.REFERENCE)
      left join @PROJ2FAP PFAP using (PFAP.REM_ZGL,ZGL.REFERENCE)
   '
   +q_filtr+q_data+'
   group by
      ZAS.SYMBOL, ZAS.NAZWA
   order by
      1,2
   '
   );
 query2 := sql('
   select
      ZAS.SYMBOL as SYMBOL,
      ZAS.NAZWA as NAZWA,
      ZGL.SYM as SYM,
      ZGL.STAT_REJ as STATUS,
      count(DISTINCT ZAS.SYMBOL) AS ILE,
      (cast(ZGL.DT_ZAM as REAL_TYPE) +cast(ZGL.TM_ZAM as REAL_TYPE)/(24*60)
      -cast(ZGL.DT_WA as REAL_TYPE) -cast(ZGL.TM_WA as REAL_TYPE)/(24*60)
      ) as DNI,
      sum(DISTINCT GRG.IL) as GODZINY,
      sum(DISTINCT PFAP.W) as KOSZTY
   from
      REM_ZGL ZGL
      left join @REM_GRG GRG  using (GRG.REM_ZGL,ZGL.REFERENCE)
      left join REM_ZAS ZAS using (ZGL.REM_ZAS,ZAS.REFERENCE)
      left join @PROJ2FAP PFAP using (PFAP.REM_ZGL,ZGL.REFERENCE)
   '
   +q_filtr+q_data+'
   group by
      ZAS.SYMBOL, ZGL.SYM, ZGL.STAT_REJ,ZGL.DT_WA, ZGL.DT_ZAM, ZAS.NAZWA,ZGL.TM_ZAM,ZGL.TM_WA
   order by
      1,2
   '
   );
~~}
{IF: query.first()}{ dalej:=1; ~~}{FI}
{ndx1:=query2.ndx_tmp(,,'SYMBOL',,);query2.index(ndx1);query2.prefix();
~~}
{WHILE: dalej}
{DO}
    {{? query.SYMBOL<>' Bez wskazanego zasobu'  || prfx:=query.SYMBOL || prfx:='' ?};
     query2.prefix(prfx,) ;
     {? query2.first()
     || {!|?
        dni+=query2.DNI;
        godziny+=query2.GODZINY;
        koszty+=query2.KOSZTY;
        query2.next
        !}
     ?};
     ~~}
    {IF: tryb <> 1}
       {prn1:='PRN';prn2:='PRNS';~~}
       {xx:=0;~~}
       {SUBSTITUTE: 'LINIA02X'}
       {dni:=0;koszty:=0;godziny:=0;~~}
    {FI}
    {IF: query2.first()}
       {dalej2:=1; ~~}
       {IF: tryb <> 1}
                {prn1:='PRNS';prn2:='PRN';~~}
                {xx:=1;~~}
                {SUBSTITUTE: 'LINIA02X'}
       {FI}
    {FI}
    {IF: tryb <> 1}{dalej2:=query2.next();~~}{FI}
    {prn1:='PRN';prn2:='';~~}
    {WHILE: dalej2}
    {DO}
       {xx:=2;yy:=2;~~}
       {SUBSTITUTE: 'LINIA0'}
       {dalej2:=query2.next(); ~~}
    {OD}
    {dalej:=query.next(); ~~}
{OD}
{query2.ndx_drop(ndx1);~~}
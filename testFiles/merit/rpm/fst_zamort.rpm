:!UTF-8
{TITLE: E. Wartości, amortyzacja i umorzenie}
{PRINTER: FINFO.SZ_PRN,,,,,'M','B',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   PRN:=obj_new(@.CLASS.PRINT,14); PRN.s_frame();
   PRN2:=obj_new(@.CLASS.PRINT,14); PRN2.s_frame();
   c:=obj_new(14);
   {! _i:=1..14 |! c[_i] := '' !};
   d:=obj_new(14);
   {! _i:=1..14 |! d[_i] := '' !};
   i:=0;
   color:=prn1:=prn2:='';
   lm:=ll:=vp:=lmt:=rsep:=0;
   __std:=null; slicznik:=0;
   par0:=0; par1:=0; par2:=0; par3:=0; par4:=0; par5:=0;
   exec('czytaj','#stalesys',,FINFO,'TOR_D')
}
{END:
   obj_del(PRN); obj_del(PRN2);
   obj_del(c); obj_del(d);
   &i; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; &rsep;
   &par0; &par1; &par2; &par3;&par4;&par5
}
{EXIT:
   _exit:=0;
   _exit
}
{COMMENT}
==================================================
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
Nazwa pliku: fst_zamort.rpm
Utworzony  : 2016-12-02
Zawartość  : Wartość amortyzacja i umorzenie wybranego środka.
==================================================
{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}--Definicja wiersza wydruku--------------------{COMMENT-}
{  prn1:='PRN'; prn2:='PRN'; ~~}
{
      PRN.add("c[1]",54,'Nazwa'@,0,'#');
      PRN.add("c[2]",15,'Grupa'@,0,'#');
      PRN.add("c[3]",16,'Jednostka organizacyjna'@,0,' ');
      PRN.add("c[4]",10,'Jednostka księgowa'@,0,' ');
      PRN.add("c[5]",12,'Data eksploatacji'@,0,' ');
      PRN.add("c[6]",20,'Rodzaj przychodu'@,0,' ');
      PRN.add("c[7]",12,'Data skreślenia'@,0,' ');
      PRN.add("c[8]",20,'Rodzaj rozchodu'@,0,' ');

      PRN2.add("d[1]",15,'Tor amortyzacji'@,0,' ');
      PRN2.add("d[2]",25,'Metoda'@,0,' ');
      PRN2.add("d[3]",15,'Zestaw kont',0,' ');
      PRN2.add("{? type_of(d[4])=1 || form(d[4],,2,' ,') || d[4] ?}",14,'Stawka',1,'#');
      PRN2.add("{? type_of(d[5])=1 || form(d[5],,2,' ,') || d[5] ?}",14,'Amortyzacja',1,'#');
      PRN2.add("{? type_of(d[6])=1 || form(d[6],,2,' ,') || d[6] ?}",14,'Narastająco',1,'#');
      PRN2.add("{? type_of(d[7])=1 || form(d[7],,2,' ,') || d[7] ?}",14,'Ulga',1,'#');
      PRN2.add("{? type_of(d[8])=1 || form(d[8],,2,' ,') || d[8] ?}",14,'Wartość',1,'#');
      PRN2.add("{? type_of(d[9])=1 || form(d[9],,2,' ,') || d[9] ?}",14,'Umorzenie',1,'#');
      PRN2.add("{? type_of(d[10])=1 || form(d[10],,2,' ,') || d[10] ?}",14,'Netto',1,'#');

      PAR_WYDR.TITLE:='E. Wartości, amortyzacja i umorzenie do dnia: %1\n dla środka '
                      'o numerze inwentarzowym: %2'@[$SSTALE.AO().KON,SRST.NRI];
      ~~}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{COMMENT}------------------------------Parametry wydruku------------{COMMENT-}
{FONT: 'W8'}{SPACE: 'C'}
{LINE: 2}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{LINE: 1}
{FONT: 'T8GB'}{SPACE: 'C'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}------------------------------WYDRUK-----------------------{COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=1; ~~}
{prn1:='PRN';~~}
{COMMENT} ---- Dane środka ---- {COMMENT-}
{
   c[1]:=SRST.NST;
   c[2]:=SRST.GR().GR;
   c[3]:=SRST.JORG().SYMBOL;
   c[4]:=SRST.ODD().OD;
   c[5]:=$SRST.DE;
   c[6]:=SRST.SRSR().PRZYCHOD().R;
   c[7]:=$SRST.SRSR().DES;
   c[8]:=SRST.SRSR().ROZ().R
;~~}
{SUBSTITUTE: 'LINIA0'}
{rsep:=PAR_WYDR.RSEP;~~}
{SUBSTITUTE: 'LINIAF'}
{COMMENT}------------------------------PODATKOWO-----------------------{COMMENT-}
{LINE: 1}
{  prn1:='PRN2'; prn2:='PRN2'; ~~}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{SPACE: 'C'}{ vp:=1; ~~}
{FONT: 'T8G'}{SPACE: 'C'}
{
   d[1]:='Podatkowy';
   d[2]:=SRST.MP().N;
   d[3]:=SRST.KONPOD().KOD;
   d[4]:=SRST.STAP * SRST.WKP;
   d[5]:=SRST.AMOP;
   d[6]:=SRD.cumulative('AMOP');
   d[7]:=SRD.getCurUlga('P');
   d[8]:=SRST.WARP;
   d[9]:=SRST.UMOP;
   d[10]:=SRST.NETP;
~~}
{SUBSTITUTE: 'LINIA0'}
{COMMENT}------------------------------BILANSOWO-----------------------{COMMENT-}
{
   d[1]:='Bilansowy';
   d[2]:=SRST.MF().N;
   d[3]:=SRST.KONFIN().KOD;
   d[4]:=SRST.STAF * SRST.WKF;
   d[5]:=SRST.AMOF;
   d[6]:=SRD.cumulative('AMOF');
   d[7]:=SRD.getCurUlga('F');
   d[8]:=SRST.WARF;
   d[9]:=SRST.UMOF;
   d[10]:=SRST.NETF;
~~}
{SUBSTITUTE: 'LINIA0'}
{COMMENT}------------------------------DODATKOWO-----------------------{COMMENT-}
{IF: FINFO.TOR_D='T'}
{
   d[1]:='Dodatkowy';
   d[2]:=SRST.MD().N;
   d[3]:=SRST.KONDOD().KOD;
   d[4]:=SRST.STAD * SRST.WKD;
   d[5]:=SRST.AMOD;
   d[6]:=SRD.cumulative('AMOD');
   d[7]:=SRD.getCurUlga('D');
   d[8]:=SRST.WARD;
   d[9]:=SRST.UMOD;
   d[10]:=SRST.NETD;
~~}
{SUBSTITUTE: 'LINIA0'}
{FI}
{rsep:=PAR_WYDR.RSEP;~~}
{COMMENT}------------------------------ZAKONCZENiE-----------------------{COMMENT-}
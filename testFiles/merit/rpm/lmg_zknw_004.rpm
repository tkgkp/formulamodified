:!UTF-8
{TITLE:D. Szczegółowe zestawienie zamówień wewnętrznych}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   zkl_006:="
      VAR_DEL.delete('ww','kk','ss','dokl','doklw','PRN','tryb','kgr');
      VAR_DEL.delete('han','kh','stan','zakres','sort1','rodzaj');
      VAR_DEL.delete('odd','dod');
      VAR_DEL.delete('wydr_naz','wydr_edi');
      VAR_DEL.delete('wyjscie','X_Xw1');
      VAR_DEL.delete('color','prn1');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep')";
   zkl_006();

   _ilk:=11;
   PRN:=obj_new(@.CLASS.PRINT,_ilk+1); PRN.s_frame();
   tryb:=rep_export();

   ww:=obj_new(_ilk); "--wartosci kolumn--";
   kk:=obj_new(_ilk); "--szerokosc kolumn--";
   dokl:=3; "--dokladnosc ilosci--";
   doklw:=2; "--dokladnosc wartosci--";
   ss:=0; "--sumator--";

   kk[1]:=20;        "--symbol zamowienia--";
   kk[2]:=25;        "--zlecenie--";
   kk[3]:=11;        "--stan zamowienia--";
   kk[4]:=10;        "--data przyjecia--";
   kk[5]:=10;        "--termin realizacji--";
   kk[6]:=32;        "--material/usluga--";
   kk[7]:=10;        "--jm--";
   kk[8]:=11;        "--ilosc zamawiana--";
   kk[9]:=11;        "--ilosc pozostala--";
   kk[10]:=15;       "--wartosc zamowienia--";
   kk[11]:=5;        "--Archiwalne--";
   kgr   :='';

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   han:=kh:=stan:=zakres:=sort1:='';
   odd:=dod:=date(0,0,0);

   wydr_naz:='zknw_004';
   wydr_edi:='ZKL_006';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit("{? WYDRUKIL.DO_DATY>=WYDRUKIL.OD_DATY || '' || FUN.info('Błędny zakres dat.'@);'DO_DATY' ?}")
   || exec('bufAndgetRec','#table','WYDRUKIL');
      {? WYDRUKIL.put()
      || wyjscie:=0;
         odd:=WYDRUKIL.OD_DATY;
         dod:=WYDRUKIL.DO_DATY;
         stan:=WYDRUKIL.STANZAM;
         sort1:=WYDRUKIL.SORTUJ;
         zakres:={? WYDRUKIL.RADIOB_1=2 || 'Z' |? WYDRUKIL.RADIOB_1=1 || 'A' || 'W' ?};
         rodzaj:='W'
      ?}
   ?}
}
{END:
   zkl_006(); VAR_DEL.delete('zkl_006')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
PRN.add("ww[1]",kk[1],'Zamówienie'@);
PRN.add("ww[2]",kk[2],'Zlecenie'@);
PRN.add("ww[3]",kk[3],'Stan'@);
PRN.add("ww[11]",kk[11],'Arch.'@);
PRN.add("ww[4]",kk[4],'Data przyjęcia'@);
PRN.add("ww[5]",kk[5],'Termin realizacji'@);
PRN.add("ww[6]",kk[6],'Materiał/Usługa'@);
PRN.add("ww[7]",kk[7],'jm'@);
PRN.add("ww[8]",kk[8],'Ilość'@,1);
PRN.add("ww[9]",kk[9],'Ilość poz.'@,1);
{? exec('upr_cs','ceny') || PRN.add("ww[10]",kk[10],'Wartość'@,1) ?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1
   ,'ZK'    ,'STRING[20]'  ,'Symbol zamówienia'
   ,'KH'    ,'STRING[70]'  ,'Kontrahent'
   ,'E'     ,'STRING[11]'  ,'Stan zamówienia'
   ,'DP'    ,'DATE'        ,'Data przyjęcia'
   ,'DT'    ,'DATE'        ,'Termin realizacji'
   ,'HAN'   ,'STRING[50]'  ,'Handlowiec'
   ,'M'     ,'STRING[151]' ,'Materiał/Usługa'
   ,'J'     ,'STRING[10]'  ,'jm'
   ,'IL'    ,'REAL'        ,'Ilość'
   ,'NET'   ,'REAL'        ,'Wartość netto'
   ,'ILP'   ,'REAL'        ,'Ilość pozostał do realizacji'
   ,'ZL'    ,'STRING[20]'  ,'Symbol zlecenia'
   ,'ARCH'  ,'STRING[1]'   ,'Archiwalne'
   ,'MGR'   ,'STRING[8]'   ,'Grupa materiałowa');
X_Xw1.index(X_Xw1.ndx_tmp(,,{? sort1='W' || 'NET' |? sort1='S' || 'E' || 'ZK' ?},,));

"--obliczenia w masce zbiorczej--";
ZK_N.index('ZKON'); ZK_N.prefix();
{? (zakres='W' | zakres='A') & ZK_N.first()
||
   {!
   |? {? ZK_N.STAT_REJ<>'A'
         & (ZK_N.DP>=odd | odd=date(0,0,0)) & (ZK_N.DP<=dod | dod=date(0,0,0))
         & (stan=exec('zwr_etap','zamsiw_nag',#ZK_N.E) | stan='')
         & (rodzaj='' | rodzaj=ZK_N.T().R)
      || ZK_P.cntx_psh();
         ZK_P.index('TYPN');
         ZK_P.prefix('A','Z',ZK_N.ref(),1);
         {? ZK_P.first()
         || {!
            |? X_Xw1.ZK    :=ZK_N.SYM;
               X_Xw1.KH    :=ZK_P.ZL().SYM;
               X_Xw1.E     :=exec('zwr_etap','zamsiw_nag',#ZK_N.E);
               X_Xw1.DP    :=ZK_N.DP;
               X_Xw1.DT    :=ZK_P.DT;
               X_Xw1.M     :=ZK_P.M().KTM+' '+M.N;
               X_Xw1.MGR   :=ZK_P.M().MGR().KOD;
               X_Xw1.J     :=ZK_P.J2().KOD;
               X_Xw1.IL    :=ZK_P.ILZ;
               X_Xw1.NET   :=ZK_P.NETTO;
               X_Xw1.ILP   :=ZK_P.ILP;
               X_Xw1.ARCH  :='';
               X_Xw1.add();
               ZK_P.next()
            !}
         ?};
         ZK_P.cntx_pop()
      ?};
      ZK_N.next()
   !}
?};
"--obliczenia w maskach archiwalnych--";
OKR.cntx_psh();
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? (zakres='W' | zakres='Z') & OKR.first()
||
   ZK_N.cntx_psh(); ZK_RN.cntx_psh();
   {!
   |? _maska:=ST.ODDZ+form(form(OKR.ROK,,,'99'),-2);
      ZK_N.use('zknag'+_maska);
      ZK_RN.use('zkhin'+_maska);
      ZK_N.index('ZKON'); ZK_N.prefix();
      {? ZK_N.first()
      ||
         {!
         |? {? ZK_N.STAT_REJ<>'A'
               & (ZK_N.DP>=odd | odd=date(0,0,0)) & (ZK_N.DP<=dod | dod=date(0,0,0))
               & (stan=exec('zwr_etap','zamsiw_nag',#ZK_N.E) | stan='')
               & (rodzaj='' | rodzaj=ZK_N.T().R)
            || ZK_P.cntx_psh();
               ZK_P.use('zkpoz'+_maska);
               ZK_P.index('TYPN');
               ZK_P.prefix('Z','Z',ZK_N.ref(),1);
               {? ZK_P.first()
               || {!
                  |? X_Xw1.ZK    :=ZK_N.SYM;
                     X_Xw1.KH    :=ZK_P.ZL().SYM;
                     X_Xw1.E     :=exec('zwr_etap','zamsiw_nag',#ZK_N.E);
                     X_Xw1.DP    :=ZK_N.DP;
                     X_Xw1.DT    :=ZK_N.DT;
                     X_Xw1.M     :=ZK_P.M().KTM+' '+M.N;
                     X_Xw1.MGR   :=ZK_P.M().MGR().KOD;
                     X_Xw1.J     :=ZK_P.J2().KOD;
                     X_Xw1.IL    :=ZK_P.ILZ;
                     X_Xw1.NET   :=ZK_P.NETTO;
                     X_Xw1.ILP   :=ZK_P.ILP;
                     X_Xw1.ARCH  :='T';
                     X_Xw1.add();
                     ZK_P.next()
                  !}
               ?};
               ZK_P.cntx_pop()
            ?};
            ZK_N.next()
         !}
      ?};
      OKR.next()
   !};
   ZK_N.cntx_pop(); ZK_RN.cntx_pop()
?};
OKR.cntx_pop();

PAR_WYDR.TITLE:='Szczegółowe zestawienie zamówień wewnętrznych'@;
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'@}
{<{lmt+1}}{'Oddział: %1'@[(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})]}
{IF: odd<>date(0,0,0) | dod<>date(0,0,0)}{<{lmt+1}}{'Zakres dat: od %1 do %2'@[$odd,$dod]}{FI}
{<{lmt+1}}{IF: stan<>''}{'Stan zamówienia: %1'@[stan]}{ELSE}{'Stan zamówienia: wszystkie'@}{FI}
{<{lmt+1}}{{? sort1='W' || 'Sortowanie wg: wartość'@ |? sort1='S' || 'Sortowanie wg: stan zamówienia'@ || 'Sortowanie wg: symbol zamówienia'@ ?}}
{<{lmt+1}}{{? zakres='Z' || 'Zakres zamówień: zarchiwizowane'@ |? zakres='A' || 'Zakres zamówień: aktywne'@ || 'Zakres zamówień: wszystkie'@ ?}}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8GB'}{SPACE: 'C'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP;~~}
{FOR: X_Xw1}
{DO}
   {FIRST}
   { dokl:=exec('fld_prec','#field',X_Xw1,'IL');~~}
   {FIRST-}
   {
   ww[1] :=form(X_Xw1.ZK);
   ww[2] :=form(X_Xw1.KH);
   ww[3] :=form(X_Xw1.E);
   ww[4] :=form(X_Xw1.DP);
   ww[5] :=form(X_Xw1.DT);
   ww[6] :=X_Xw1.M;
   ww[7] :=X_Xw1.J;
   ww[8] :=form(X_Xw1.IL,,dokl,'.,');
   ww[9] :=form(X_Xw1.ILP,,dokl,'.,');
   ww[10]:=form(X_Xw1.NET,,doklw,'.,');
   ww[11]:=form(X_Xw1.ARCH);
   kgr   :=form(X_Xw1.MGR);
   ss+=X_Xw1.NET;
   ~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}
{COMMENT}   podsumowanie                                             {COMMENT-}
{IF: tryb<>1}
{IF: exec('upr_cs','ceny')}
   {IF: lineleft()>0 & lineleft()<2}{PAGE}{FI}
   {FOOTER}{FOOTER-}
   {SUBSTITUTE: 'LINIAF'}
   {
   PRN.n_frame();
   ww[1]:='';
   ww[2]:='';
   ww[3]:='';
   ww[4]:='';
   ww[5]:='';
   ww[6]:='';
   ww[7]:='';
   ww[8]:='';
   ww[9]:='Razem:'@; PRN.FORM[9]:=0;
   ww[10]:=form(ss,,2,'.,');
   ww[11]:='';
   ~~}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'LINIA0'}
{FI}
{ELSE}
   {SUBSTITUTE: 'LINIAF'}
{FI}
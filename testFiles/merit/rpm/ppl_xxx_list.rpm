:!UTF-8
{TITLE:Lista płac (paski) + RMUA}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'m','c',,,,,,1,5,5,5,5,1,0}
{BEGIN: 'prolog';
   VAR_DEL.delete('par','Banknoty','max','nom','tmp1','tmp2','tmp3','col','row','kwota','Text','LPrac','LWydz','maxline');
   FUNKCJE.TBEGIN();

   Banknoty:=" form(Nominal[_a],10,2)+':'+
               form(Tbank[_a],10)+'  '+(10*'─')+
               form(Nominal[_a]*Tbank[_a],12,2)
             ";

   par:=obj_new(13);
   par[1]:=obj_new(20);
   par[2]:=obj_new(6);
   par[6]:=obj_new(8);
   par[10]:=obj_new(8);
   par[13]:=obj_new(6);

   KP.cntx_psh;
   KP.index('_KARTAPO');
   LISTA.Update();
   max:=FUNKCJE.TCOLUMNS()*39;
   nom:=FUNKCJE.NOMINAL(200);
   FUNKCJE.TFREE();
   tmp1:=0;
   tmp2:=1;
   tmp3:=0;
   col:=row:=0;
   kwota:=0;
   Text:='';
   LPrac:=LWydz:=0;
   maxline:=0
}
{END: 'epilog';
   KP.cntx_pop;
   FUNKCJE.TEND();
   FUNKCJE.NOMINAL();
   VAR_DEL.delete('par','Banknoty','max','nom','tmp1','tmp2','tmp3','col','row','kwota','Text','LPrac','LWydz','maxline')
}
{EXIT: LISTA.__Fatal}
{COMMENT}OLD: xxx_list.rpm{COMMENT-}
{COMMENT}OLD: xxx_list.rpi{COMMENT-}
{COMMENT}OLD: kart_url.rpi{COMMENT-}
{INCLUDE: p_xxx_list.rpi}
{INCLUDE: p_kart_url.rpi}
{HEADER}
  {STR.split(KST.NAZWA);STR.line(max-5-(+(KST.MIASTO+', dn. '+$date)))}  {>{max}KST.MIASTO+', dn. '+$date}
  {IF: STR.next()}{STR.line(max-5)}{FI}
  NIP: {KST.NIP}   REGON: {KST.REG}
  {<3>{max}'W Y D R U K   L I S T Y   P Ł A C'}
  {<3>{max}'Lista płac '+VAR.NAZWALIS+' ('+date(O.R,O.M,1)$8+') nr '+$O.N}
{IF: +O.O}
  {<3>{max}O.O}
{FI}

{HEADER-}
{FOOTER:0 }
{max*'─'}
{>{max}'str. '+$page}
{FOOTER-}
{COMMENT}

   Makrodefinicje do formatowania składników wypłaty pracownika i zbiorówek
   jednostek i zakładu. __nazwa(20 znaków)__kwota(12 znaków)

{COMMENT-}
{MACRO: 'prac'}{
   '  '+(_a:=form(FUNKCJE.TPRAC(col,row,2),20))+
   '  '+{? +(|_a) & FUNKCJE.TPRAC(col,row,1)
           || form(FUNKCJE.TPRAC(col,row,3),12,2)
           || 12*' '
        ?}
}{MACRO-}
{MACRO: 'inf_pod'}
    { KP.prefix(P.ref, O.RP);
      {! _i:=1..4 |!
         par[13][_i]:=0
      !};
      par[13][6]:=date(O.RP,O.MP,1);
      {? KP.first ||
          par[13][5] := date(KP.R, KP.M, 1);
          par[13][5] := {? par[13][5] < P.DZA || P.DZA || par[13][5] ?};
          {! |?
            {? KP.M < O.MP ||
                par[13][1] += KP.S1;
                par[13][2] += KP.S2;
                par[13][4] += KP.S4;
                par[13][6] := date(KP.R, KP.M, 0)
            ?};
            KP.next
          !};
          par[13][3] := par[13][1] - par[13][2];
          {? par[13][3] < 0 || par[13][3] := 0 ?}
      ?};~~
    }
{IF: par[13][1]}
{max*'─'}

Informacje podatkowe za okres: {par[13][5]} - {par[13][6]}

{<3}Przychód: {form(par[13][1],,2)}{<39}Koszty uzyskania: {form(par[13][2],,2)}{<75}Dochód: {form(par[13][3],,2)}{<111}Zal. pod. doch.: {form(par[13][4],,2)}
{FI}
{MACRO-}
{COMMENT}

   Przeglądaj wszystkie jednostki, i dla każdego z nich przeglądaj kartotekę
   osobową. Dla każdego z nich pobieraj składniki wypłaty. Jeżeli są jakieś,
   to drukuj pasek "wypłaty" pracownika.

{COMMENT-}
{FONT: 'm'}
{ maxline:=lineleft; ~~ }
{  FUNKCJE.TWYDZ();
   LWydz:=0;
   ~~
}
{  echo('Lista: '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE);
   FUNKCJE.TPRAC();
   col:=row:=1;
   ~~
}
{COMMENT}

   S K Ł A D N I K I   W Y P Ł A T Y   P R A C O W N I K A

{COMMENT-}
{IF: FUNKCJE.TROWS('Prac')}
{ENTIRE}
{ LPrac+=1; LWydz+=1; max*'─'}

{FONT: 'n'}{form(LPrac,-5,,'9.')} ({form(LWydz,-5,,'9.')})  {P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE}   nr akt {|P.T}   Jednostka org.: {P.WYDZIAL().SYMBOL}{FONT: 'm'}
                Lista płac za {date(O.R,O.M,1)$8}
{IF: O.D<>date(0,0,0)}Wypłacona dnia {O.D$1}{FI} {IF: {? O.DKURS<>date(0,0,0) || exec('h_find','pracownik'); H.CZYWAL='T' | H.CZYWAL2='T' | H.CZYWAL3='T'  ?}
}      Kurs z dnia {O.DKURS}: {H.WAL(); exec('waluta_miara','lista_licz',SLO.KOD)} {SLO.KOD} = {exec('waluta_kurs','lista_licz',H.ZWAL,O.DKURS,H.WAL)} PLN  {FI}

{WHILE: row<=FUNKCJE.TROWS('Prac')}{DO}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'prac'}{ col+=1; ~~ }{OD}
{ col:=1; row+=1; ~~ }
{OD}
{ col:=1; row:=0; ~~ }
{FUNKCJE.TCOLUMNS()*('  '+(34*'═'))}
{WHILE: col<=FUNKCJE.TCOLUMNS()}{DO}{SUBSTITUTE: 'prac'}{ col+=1; ~~ }{OD}
{  FUNKCJE.TBANK(kwota:=FUNKCJE.TPRAC(FUNKCJE.TCOLUMNS(),0,3)); ~~ }
{IF: tmp1}
     {SUBSTITUTE: 'inf_pod'}
{FI}
{IF: tmp2}
     {SUBSTITUTE: 'inf_ubezp'}
     {ENTIRE-}
     {ENTIRE}
{FI}
{IF: tmp3}
     {SUBSTITUTE: 'kart_url'}
{FI}
{ENTIRE-}
{IF: tmp2 & maxline<>lineleft}
   {PAGE}
{FI}
{FI}
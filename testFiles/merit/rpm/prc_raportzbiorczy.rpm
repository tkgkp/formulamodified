:!UTF-8
{TITLE: Raport zbiorczy}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   VAR_DEL.delete('lmt','rsep','h_rat','tresc','pb','Format','Columns','Dzien','Temp','Rok','Msc','Uwagi','TypDn');
   VAR_DEL.delete('Praca','Nadmiar','Niedobor','WyjPryw','WyjSloz','TypDnSep','Ppsf','Nieob','nieob','var','KodNieob');
   VAR_DEL.delete('ncz','line_r');

   _TAB:=exec('tab_prac','!prc_czp_wydr');
   {? _TAB.first()
   || tresc:='w_zbior';
      PAR_WYDR.TITLE:='Raport zbiorczy'@;

      lmt:=pb:=Columns:=Rok:=Msc:=0;
      rsep:=PAR_WYDR.SEP;
      h_rat:=Dzien:=1;
      Temp:=Uwagi:=TypDn:=Praca:=WyjPryw:=WyjSloz:=admiar:=Niedobor:=TypDnSep:=Ppsf:=Nieob:=nieob:=var:=KodNieob:='';
      ncz:=line_r:='';
      Format:=
         "  {? _a='00:00' || '     ' || _a ?}
         "
   ?};
   params_set('P',_TAB);
   nieob:=" _naz_n:='';
           N.cntx_psh;
           N.index('NIEOBECN');
           N.prefix('N',P.ref);
           {? N.first()
           || {!|?
                   {? _a>=N.OD & _a<=N.DO
                       || ncz:=N.PARTDAY; _naz_n+=$(N.NB().RN)
                   ?};
                   N.next
              !}
           ?};
          N.cntx_pop();_naz_n";
   var:=exec('uprawnieniawrap','pkd','nieobecnościach'@,'PKD_EZK_OPNN','PKD_EZK_ORNN');

   R_PRACDN.cntx_psh();
   R_WZCZ.cntx_psh();
   KAL_ROK.cntx_psh();
   KAL_DEF.cntx_psh()
}
{END:
   VAR_DEL.delete('lmt','rsep','h_rat','tresc','pb','Format','Columns','Dzien','Temp','Rok','Msc','Uwagi','TypDn');
   VAR_DEL.delete('Praca','Nadmiar','Niedobor','WyjPryw','WyjSloz','TypDnSep','Ppsf','Nieob','nieob','var','KodNieob');
   VAR_DEL.delete('ncz','line_r');

   KAL_DEF.cntx_pop();
   KAL_ROK.cntx_pop();
   R_WZCZ.cntx_pop();
   R_PRACDN.cntx_pop()
}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: w_zbior.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: ~pb}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~}
   {FONT: 'T8G'}{ h_rat:=charleft()/h_rat; ~~}
   {SPACE: 'C'}
   {LINE}
   {  pb:=1; ~~}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
{INCLUDE: p_obszarprc.rpi}
   {<{ceil(lmt*h_rat)+1}'%1 %2'['Za miesiąc:'@,date(VAR_EDIT.ROK,VAR_EDIT.MSC,1)$8]}
   {<{ceil(lmt*h_rat)+1}'Oznaczenie dni: '
      }{'R - roboczy'@}{' ,'
      }{'W - wolny'@}{' ,'
      }{'S - świąteczny'@}{' ,'
      }{'N - nieobecność'@}{' ,'
      }{'Nc - nieobecność na część dnia'@}
{FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{LINE}
{HEADER-}
{MARGIN: 0}
{IF:  VAR_EDIT.win_edit('PRDN_DAT');
      VAR_EDIT.ROK:=date()~1;
      VAR_EDIT.MSC:=date()~2;
      VAR_EDIT.efld_opt('PRDN_DAT','mark=1',,'ROK');
      VAR_EDIT.efld_opt('PRDN_DAT','mark=1',,'MSC');
      ~VAR_EDIT.edit(
         "  _result:=__CHK.record(VAR_EDIT,,'ROK','MSC');
            {? _result='' & (VAR_EDIT.MSC<1 | VAR_EDIT.MSC>12)
            || FUN.emsg('Proszę podać porawną wartość w polu \"Miesiąc\".'@);
               _result:='MSC'
            ?};
            _result
         "
      )
}
{EXIT}
{FI}
{  Rok:=VAR_EDIT.ROK;
   Msc:=VAR_EDIT.MSC;
   Columns:=date(Rok,Msc,0)-date(Rok,Msc,1)+1;
   MASK.Use('R_PRACDN',Rok,Msc);
   MASK.Use('R_REJ_WW',Rok,Msc); ~~
}
{FOR: params_get().P}
{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {FONT: 'T8G'}
      {SPACE: 'C'}
      {  lmt:=10;
         _setp:='{COLOR: \'0:0:0,255:255:255\'}';
         R_PRACDN.index('R_PRACDN');
         R_PRACDN.prefix(P.ref());
         TypDn:=Praca:=Niedobor:=TypDnSep:=Nadmiar:=WyjSloz:=WyjPryw:=Ppsf:=KodNieob:=ncz:='';
         {! _Dzien:=1..Columns
         |! _Data:=date(Rok,Msc,_Dzien);
            R_WZCZ.index('R_WZCZ');
            R_WZCZ.prefix(P.name(),P.ref());
            _Kalend:=
               {? R_WZCZ.find_le(_Data)
               || {? R_WZCZ.GRAFIK='T' || R_WZCZ.CZESC || R_WZCZ.KAL ?}
               || P.KAL
               ?};
            _x:={? _Data~4=6 || 'W' |? _Data~4=7 || 'S' || 'R' ?};
            Uwagi:='';
            {? _Kalend
            || KAL_ROK.index('KAL_ROK');
               KAL_ROK.prefix(_Kalend);
               {? KAL_ROK.find_key(Rok)
               || KAL_DEF.index('KAL_DEF');
                  KAL_DEF.prefix(KAL_ROK.ref());
                  {? ~KAL_DEF.find_key(_Data)
                  || Uwagi:='błędna definicja kalendarza'@
                  || _x:=KAL_DEF.TYP
                  ?}
               || Uwagi:='brak definicji kalendarza'@
               ?}
            || Uwagi:='nieokreślony kalendarz'@
            ?};
            Nieob:={? +var || '' || nieob(_Data) ?};
            {? Nieob<>''
              || {? ncz<>'T'
                 || _x:='N'
                || _x:='Nc'
                   ?}
            ?};
            _set:=
               {? _x='R' || '{COLOR: \'0:0:0,255:255:255\'}'
               |? _x='W' || '{COLOR: \'0:0:0,128:128:255\'}'
               |? 1+_x='N' || '{COLOR: \'0:0:0,128:255:128\'}'
               || '{COLOR: \'0:0:0,255:128:128\'}'
               ?};
            TypDn+=_setp+'│'+_set+'  '+{?_x='Nc' || _x+' ' || _x+'  '?};
            TypDnSep+=_setp+('┼'+_set+(5*'─'));
            _setp:=_set;
            {? R_PRACDN.find_key(_Data) & _x<>'N'
            || Praca+='│'+Format(form(R_PRACDN.CP,5));
               Nadmiar+='│'+Format(form(R_PRACDN.CN,5));
               Niedobor+='│'+Format(form({? _x='Nc' || '' || R_PRACDN.NB?},5));
               WyjSloz+='│'+Format(form(R_PRACDN.WS,5));
               WyjPryw+='│'+Format(form(R_PRACDN.WP,5));
               Ppsf+='│'+Format(form(exec('czas_rap','ppsf',_Data),5));
               KodNieob+='│'+Format(form(''+Nieob,5))
            || _x:='│'+form('     ',5);
               Praca+=_x; Nadmiar+=_x; Niedobor+=_x;
               WyjSloz+=_x; WyjPryw+=_x; Ppsf+=_x;
               KodNieob+='│'+form(' '+Nieob,5)
            ?}
         !}; ~~
      }
{ENTIRE}
{LINE}
{FONT: 'T8'}
{SPACE: 'B'}
{FONT: 'T8G'}
{SPACE: 'C'}
{LINE}
{ lmt:=10; ~~}
{>{lmt}}╔{27*'═'}{Columns*('╤'+(5*'═'))}╗
{>{lmt}}{ Temp:='║ '+'Dzień'@+(21*' '); {! _n:=1..Columns |! Temp+='│  '+form(_n,2)+' ' !}; Temp }║
{>{lmt}}╚{27*'═'}{Columns*('╧'+(5*'═'))}╝
{>12}{P.OSOBA().NAZWISKO} {OSOBA.PIERWSZE}, {'Jednostka organizacyjna:'@} {P.WYDZIAL().SYMBOL}{IF: +(|P.WYDZIAL().OPIS)
} ({P.WYDZIAL().OPIS}){FI}
{FONT: 'T8G'}
{SPACE: 'C'}
{IF: +Uwagi}{>12}{Uwagi}{FI}
{>{lmt}}╔{27*'═'}{Columns*('╤'+(5*'═'))}╗
{>{lmt}}║ {'Typ dnia'@+(18*' ')}{REP:TypDn+'║'+'{COLOR: \'0:0:0,255:255:255\'}'}
{IF: rsep}{>{lmt}}╟{27*'─'}{REP:TypDnSep+'╢'+'{COLOR: \'0:0:0,255:255:255\'}'}{FI}
{>{lmt}}║ {'Czas pracy'@+(16*' ')}{Praca}║
{IF: rsep}{>{lmt}}╟{27*'─'}{Columns*('┼'+(5*'─'))}╢{FI}
{>{lmt}}║ {'Niedobór'@+(18*' ')}{Niedobor}║
{IF: rsep}{>{lmt}}╟{27*'─'}{Columns*('┼'+(5*'─'))}╢{FI}
{>{lmt}}║ {'Nadmiar'@+(19*' ')}{Nadmiar}║
{IF: rsep}{>{lmt}}╟{27*'─'}{Columns*('┼'+(5*'─'))}╢{FI}
{>{lmt}}║ {'Wyjścia służbowe'@+(9*' ')} {WyjSloz}║
{IF: rsep}{>{lmt}}╟{27*'─'}{Columns*('┼'+(5*'─'))}╢{FI}
{>{lmt}}║ {'Wyjścia prywatne'@+(9*' ')} {WyjPryw}║
{IF: rsep}{>{lmt}}╟{27*'─'}{Columns*('┼'+(5*'─'))}╢{FI}
{>{lmt}}║ {'Praca poza siedzibą firmy'@} {Ppsf}║
{IF: rsep}{>{lmt}}╟{27*'─'}{Columns*('┼'+(5*'─'))}╢{FI}
{>{lmt}}║ {'Nieobecności'@+(13*' ')} {KodNieob}║
{>{lmt}}╚{27*'═'}{Columns*('╧'+(5*'═'))}╝
{ENTIRE-}
   {FI}
{OD}
{PAGE}
{LINE}
{<{ceil(lmt*h_rat)+1}'Legenda Nieobecności:'@}
{LINE}
{line_r:='';
R.cntx_psh; R.prefix;
R.index('RUBKLKOD'); R.prefix('N');
{? R.first ||
 {!|? line_r+=form(form(R.RN,,,'9999')+'-'+form(R.RT+',',20)+' ',30);
 R.next
 !}
?};
R.cntx_pop;
STR.split(line_r);~~}
{WHILE: +(line_r:=STR.line(210))}{DO}{line_r+'\n'}{OD}
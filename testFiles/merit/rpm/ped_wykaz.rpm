:!UTF-8
{TITLE:Wykaz dokumentów akt osobowych}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1
}
{BEGIN:
   VAR_DEL.delete('__ZALACZ','nazwa','tr','czesc','zal_len');
   VAR_DEL.delete('PRNZAL','PRNCZ','prn1','lmt','rsep','vp','h_rat');

   PAR_WYDR.TITLE:='Wykaz dokumentów akt osobowych'@+' '+{? ZAOH.ZAOH || '(fragment)'@ || '' ?};

   ZAOH.cntx_psh();
   ZAOH.index('LP');
   ZAOH.prefix();

   _tzalacz:=params_get().tzalacz;
   __ZALACZ:=_tzalacz.ZALACZ.TAB;

   __ZALACZ.cntx_psh();
   __ZALACZ.index(_tzalacz.ZALACZ.NDX.NR);
   __ZALACZ.prefix();

   nazwa:="
      _apath:=exec('zaoh_path','zalacz','OBJ');
      {? _apath.poziom=4
      || ZAOH.cntx_psh();
         ZAOH.prefix();
         _prfx:={? ZAOH.seek(ZAOH.ZAOH,) || ZAOH.NAZWA+' / ' || '' ?};
         ZAOH.cntx_pop();
         _prfx
      || ''
      ?}+ZAOH.NAZWA
   ";
   tr:='';
   czesc:='';
   zal_len:=0;

   PRNZAL:=obj_new(@.CLASS.PRINT,5);
   PRNZAL.s_frame();
   PRNZAL.add("__ZALACZ.NR",5,'Numer',1);
   PRNZAL.add("$__ZALACZ.DATA",10,'Data');
   PRNZAL.add("__ZALACZ.TYP_ZAL",30,'Typ załącznika');
   PRNZAL.add("
      {? __ZALACZ.DESCR<>'' || __ZALACZ.DESCR || __ZALACZ.NAME ?}+
      {? __ZALACZ.DEP<>date(0,0,0) || ' (%1)' [$__ZALACZ.DEP] || '' ?}",
      75,'Opis / Nazwa pliku'
   );

   _szer:=-3;
   {! _lp:=1 ..PRNZAL.nstr
   |! _szer+=PRNZAL.LEN[_lp]+3
   !};

   PRNCZ:=obj_new(@.CLASS.PRINT,2);
   PRNCZ.s_frame();
   PRNCZ.add("ZAOH.CZESC",7,'Część');
   PRNCZ.add("nazwa()",_szer-PRNCZ.LEN[1]-3,'Nazwa');

   prn1:='PRNZAL';
   lmt:=rsep:=0;
   vp:=1;
   h_rat:=1;

   ~~
}
{END:
   __ZALACZ.cntx_pop();

   ZAOH.cntx_pop();

   VAR_DEL.delete('PRNZAL','PRNCZ','prn1','lmt','rsep','vp','h_rat');
   VAR_DEL.delete('__ZALACZ','nazwa','tr','czesc','zal_len');
   ~~
}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRNCZ.width())/2); ~~}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {<1>{lm} OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE}
   {<1>{lm} 'PESEL: '+OSOBA.PESEL}
   {LINE}
   {FONT: 'T8'}
   {SPACE: 'B'}
   {SUBSTITUTE: prn1:='PRNCZ'; 'TABHEAD'}
   {REP: PRNCZ.fjbar(PRNZAL,lmt)}
   {
      PRNZAL.setcolor(color);
      PRNZAL.ini_head();
      ~~
   }
   {WHILE: PRNZAL.h_next()}
   {DO}
      {REP: PRNZAL.h_fmline(lmt)}
   {OD}
   {REP: PRNZAL.flbar(lmt)}
   {
      _bialy:='255:255:255';
      PRNCZ.setcolor(_bialy);
      PRNZAL.setcolor(_bialy);
      tr:='';
      ~~
   }
{HEADER-}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{SUBSTITUTE: 'FOOT'}
{COMMENT}-----------------------------------------------------------------------------------------------------{COMMENT-}
{FOR: __ZALACZ}
{DO}
   {
      PRNZAL.ini_line();
      zal_len:=PRNZAL.length();
      ~~
   }
   {IF: __ZALACZ.CZESC<>czesc & ZAOH.seek(__ZALACZ.ZAOH)}
      {COMMENT}
         Nastąpiła zmiana prezentowanej części - trzeba wydrukować nagłówek.
         Najpierw sprawdzimy, czy na bieżącej stronie zmieszczą się:
             - Odpowiednia kreska rozpoczynająca nagłówek;
             - Nagłówek;
             - Kreska łącząca nagłówek z załącznikami;
             - Bieżący załącznik;
             - Ewentualna kreska kończąca załączniki.
      {COMMENT-}
      {IF:
         _ll:=lineleft();
         czesc:=__ZALACZ.CZESC;
         PRNCZ.ini_line();
         _ll>0 & _ll<1+PRNCZ.length()+1+zal_len+1
      }
         {REP: tr:=''; PRNZAL.flbar(lmt)}
         {IF: lineleft()}{PAGE}{FI}
      {FI}
      {REP: {? tr='' || PRNCZ.fhbar(lmt) || PRNZAL.fjbar(PRNCZ,lmt) ?} }
      {WHILE: PRNCZ.next()}
      {DO}
         {REP: PRNCZ.fline(lmt)}
      {OD}
      {REP: PRNCZ.fjbar(PRNZAL,lmt)}
   {FI}
   {IF:
      _ll:=lineleft();
      _ll>0 & _ll<zal_len+1
   }
      {REP: PRNZAL.flbar(lmt)}
      {IF: lineleft()}{PAGE}{FI}
      {REP: PRNZAL.fhbar(lmt)}
   {FI}
   {WHILE: PRNZAL.next()}
   {DO}
      {REP: tr:='PRNZAL'; PRNZAL.fline(lmt)}
   {OD}
{OD}
{IF: lineleft()>0}
   {REP: PRNZAL.flbar(lmt)}
{FI}
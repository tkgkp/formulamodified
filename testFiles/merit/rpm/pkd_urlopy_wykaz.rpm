:!UTF-8
{TITLE: Wykaz urlopów}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('tresc','PRN','PR1','PR','prn1','prn2','vp','lmt','rsep','h_rat','par','pp','Wydzial');
   VAR_DEL.delete('SumaWD','SumaD','SumaWU','SumaU','SumaWC','SumaC','SumaWP','SumaP','SumaWW','SumaW','SumaWB');
   VAR_DEL.delete('SumaB','SumaWZ','SumaZ','SumaE','SumaWE','Lp','prac');
   tresc:='wyk_url';
   Lp:=0;
   PAR_WYDR.TITLE:='Karty urlopowe'@;
   P.cntx_psh();
   UD_SKL.cntx_psh();
   UD_SKL.prefix();
   par:=obj_new('tab','ok');
   par.tab:=exec('par_wydr_ddp','pkd',
      PAR_WYDR.TITLE,,,,,,,,
      'Typ zestawienia'@,'Całość'@,'Wg jednostek'@,,,
      date()~1,
      "  {? cur_tab().ROK<1900 || FUN.emsg('Proszę podać rok do przygotowania zestawienia.'@); 0 || 1 ?}
      "
   );
   {? par.tab.size()
   || PRN:=obj_new(@.CLASS.PRINT,{? rep_export() & par.tab.ASK=1 || 15 || 13 ?});
      {?~rep_export()
      || PRN.add("Lp+=1",5,'Lp.'@,1,,,,,',,\'99\'');
         PRN.add("P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE",48,'Nazwisko i imię'@)
      |? rep_export() & par.tab.ASK=1
      || PRN.add("P.WYDZIAL().SYMBOL",10,'Jednostka org. - symbol'@);
         PRN.add("P.WYDZIAL().OPIS",50,'Jednostka org. - opis'@);
         PRN.add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
         PRN.add("P.OSOBA().PIERWSZE",20,'Imię'@)
      |? rep_export()
      || PRN.add("P.OSOBA().NAZWISKO",30,'Nazwisko'@);
         PRN.add("P.OSOBA().PIERWSZE",20,'Imię'@)
      ?};
      PRN.add("KART_URL.LIM_ZAL",9,'Urlop zaległy'@,1,,,,,'12,2');
      PRN.add("KART_URL.LIM_AKT",7,'Urlop bieżący'@,1,,,,,'12,2');
      PRN.add("P.NAB_URL$1",10,'Data nabycia'@);
      PRN.add("{? _u:=KART_URL.URL_DOD || _u || '' ?}",13,'Urlop uzupełniający'@,1,,,,,'12,2');
      PRN.add("{? (_d:=KART_URL.DATA_DOD)<>#0 || _d$1 || ''?}",10,'Data nabycia'@);
      PRN.add("{? _u:=KART_URL.NSP_ZAL || _u || '' ?}",9,'Urlop dodatkowy zaległy'@,1,,,,,'12,2');
      PRN.add("{? _u:=KART_URL.URL_NSP || _u || '' ?}",9,'Urlop dodatkowy'@,1,,,,,'12,2');
      PRN.add("{? (_d:=KART_URL.DATA_NSP)<>#0 || _d$1 || ''?}",10,'Data nabycia'@);
      PRN.add(
         "  KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.NSP_ZAL+KART_URL.URL_NSP
         ",9,'Całkowity urlop'@,1,,,,,'12,2');
      PRN.add("KART_URL.URL_WYK+KART_URL.NSP_WYK",12,'Wykorzystano'@,1,,,,,'12,2');
      PRN.add("KART_URL.URL_POZ+KART_URL.NSP_POZ",9,'Pozostało'@,1,,,,,'12,2');
      PRN.s_frame();

      {? par.tab.ASK=1
      || PR:=obj_new(@.CLASS.PRINT,12);
         {? ~rep_export()
         || PR.add("'R A Z E M'@",56);
            PR.add("SumaZ",9,,1,,,,,',2,\'9.\'');
            PR.add("SumaB",7,,1,,,,,',2,\'9.\'');
            PR.add("''",10);
            PR.add("{? SumaU=0 || '' || SumaU ?}",13,,1,,,,,',2,\'9.\'');
            PR.add("''",10);
            PR.add("{? SumaE=0 || '' || SumaE ?}",9,,1,,,,,',2,\'9.\'');
            PR.add("{? SumaD=0 || '' || SumaD ?}",9,,1,,,,,',2,\'9.\'');
            PR.add("''",10);
            PR.add("SumaC",9,,1,,,,,',2,\'9.\'');
            PR.add("SumaW",12,,1,,,,,',2,\'9.\'');
            PR.add("SumaP",9,,1,,,,,',2,\'9.\'');
            PR.s_frame()
         ?}
      ?};
      PR1:=obj_new(@.CLASS.PRINT,12);
      {? ~rep_export()
      || PR1.add("{? par.tab.ASK=1 || 'S U M A  F I R M A'@ || 'R A Z E M'@?}",56);
         PR1.add("SumaWZ",9,,1,,,,,',2,\'9.\'');
         PR1.add("SumaWB",7,,1,,,,,',2,\'9.\'');
         PR1.add("''",10);
         PR1.add("{? SumaWU=0 ||''|| SumaWU ?}",13,,1,,,,,',2,\'9.\'');
         PR1.add("''",10);
         PR1.add("{? SumaWE=0 || '' || SumaWE ?}",9,,1,,,,,',2,\'9.\'');
         PR1.add("{? SumaWD=0 || '' || SumaWD ?}",9,,1,,,,,',2,\'9.\'');
         PR1.add("''",10);
         PR1.add("SumaWC",9,,1,,,,,',2,\'9.\'');
         PR1.add("SumaWW",12,,1,,,,,',2,\'9.\'');
         PR1.add("SumaWP",9,,1,,,,,',2,\'9.\'');
         PR1.s_frame()
      ?};
      prn1:='PRN';
      prn2:='PR';
      vp:=rsep:=h_rat:=1;
      lmt:=pp:=par.ok:=0;
      Wydzial:=null();
      SumaWD:=SumaD:=SumaWU:=SumaU:=SumaWC:=SumaC:=SumaWP:=SumaP:=SumaWW:=SumaW:=SumaWB:=SumaB:=SumaWZ:=SumaZ:=0;
      SumaE:=SumaWE:=0;
      params_set('P',exec('wybierz_parses','pracownik','PKD').P)
   ?}
}
{END:
   VAR_DEL.delete('tresc','PRN','PR1','PR','prn1','prn2','vp','lmt','rsep','h_rat','par','pp','Wydzial');
   VAR_DEL.delete('SumaWD','SumaD','SumaWU','SumaU','SumaWC','SumaC','SumaWP','SumaP','SumaWW','SumaW','SumaWB');
   VAR_DEL.delete('SumaB','SumaWZ','SumaZ','SumaE','SumaWE','Lp','prac');
   UD_SKL.cntx_pop();
   P.cntx_pop()
}
{COMMENT}OLD: url_kart.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{EXIT: ~par.tab.size()}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: vp}
{FONT: 'T8'}{ h_rat:=charleft(); ~~}
{FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat ?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {<{ceil(lmt*h_rat)+1}'Parametry sprawozdania'@}
   {<{ceil(lmt*h_rat)+1}'Rok kalendarzowy'@}: {$par.tab.ROK}
   {<{ceil(lmt*h_rat)+1}'Urlop w dniach'@}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{IF: ~rep_export() & par.tab.ASK=1 & UD_SKL.seek(Wydzial)}
{<{lmt+1}'Jednostka organizacyjna'@} - {UD_SKL.SYMBOL}{IF: +(|UD_SKL.OPIS)} ({|UD_SKL.OPIS}){FI}
{FI}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: 'fsum'}
{LINE}
{FONT: 'T8B'}
{SPACE: 'B'}
{IF: ~rep_export()}
   {REP: PR1.fhbar(lmt)}
{FI}
{ PR1.ini_line(); ~~}
{WHILE: PR1.next()}{DO}
   {REP: PR1.fline(lmt)}
{OD}
{REP: PR1.flbar(lmt)}
{MACRO-}
{MACRO: 'wsum'}
{{? par.tab.ASK=0 || prn2:='PR1'?}; rsep:=1; ~~}
{FONT: 'T8B'}
{SPACE: 'B'}
{IF: (rep_export() & ~par.tab.ASK) | ~rep_export()}
{SUBSTITUTE: 'LINIAFS'}
{FI}
{FONT: 'T8'}
{SPACE: 'B'}
{MACRO-}
{MACRO: tresc}
{FOR: KART_URL}{INDEX:'PRAC_ROK'}{PREFIX:P.ref(),par.tab.ROK}{DO}
{SUBSTITUTE: 'LINIA0'}
{ par.ok:=1; ~~}
{SUBSTITUTE: 'sumw'}
{IF: ~rep_export() & lineleft()<4}
{SUBSTITUTE: 'LINIAF'}
   {PAGE}
{FI}
{OD}
{MACRO-}
{MACRO: 'sumw'}
{ {? par.tab.ASK=1
  || SumaD+=KART_URL.URL_NSP;
     SumaE+=KART_URL.NSP_ZAL;
     SumaU+=KART_URL.URL_DOD;
     SumaC+=KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.NSP_ZAL+KART_URL.URL_NSP;
     SumaP+=KART_URL.URL_POZ+KART_URL.NSP_POZ;
     SumaW+=KART_URL.URL_WYK+KART_URL.NSP_WYK;
     SumaB+=KART_URL.LIM_AKT;
     SumaZ+=KART_URL.LIM_ZAL
  ?};
  SumaWD+=KART_URL.URL_NSP;
  SumaWE+=KART_URL.NSP_ZAL;
  SumaWU+=KART_URL.URL_DOD;
  SumaWC+=KART_URL.LIM_ZAL+KART_URL.LIM_AKT+KART_URL.URL_DOD+KART_URL.NSP_ZAL+KART_URL.URL_NSP;
  SumaWP+=KART_URL.URL_POZ+KART_URL.NSP_POZ;
  SumaWW+=KART_URL.URL_WYK+KART_URL.NSP_WYK;
  SumaWB+=KART_URL.LIM_AKT;
  SumaWZ+=KART_URL.LIM_ZAL; ~~
}
{MACRO-}
{MACRO: 'wydzialy'}
{ {? params_get().P.first()
  || prac:=tab_tmp(3,
         'WYDZIAL','STRING[16]','Wydzial',
         'NAZWISKO','STRING[30]','Nazwisko',
         'PIERWSZE','STRING[20]','Imię',
         'PREF','STRING[16]','Ref pracownika');
     {!
     |? {? P.seek(params_get().P.SQL,,1)
        || prac.NAZWISKO:=P.OSOBA().NAZWISKO;
           prac.PIERWSZE:=OSOBA.PIERWSZE;
           prac.PREF:=$P.ref();
           prac.WYDZIAL:=P.WYDZIAL().SYMBOL;
           prac.add()
        ?};
        params_get().P.next()
     !}
  ?}; ~~
}
{FOR: prac}{DO}
   {IF: P.seek(prac.PREF,,1)}
      {FOR: KART_URL}{INDEX:'PRAC_ROK'}{PREFIX:P.ref(),par.tab.ROK}{DO}
          {IF: ~par.ok}{Wydzial:=P.WYDZIAL; ~~}{FI}
            {IF: P.WYDZIAL<>Wydzial}
               {pp:=1; ~~}
               {IF: pp=1}
{SUBSTITUTE: 'wsum'}
               { SumaD:=SumaE:=SumaU:=SumaC:=SumaP:=SumaW:=SumaB:=SumaZ:=pp:=0; ~~}
             {FI}
             {PAGE}
             {Wydzial:=P.WYDZIAL;Lp:=0; ~~}
          {FI}
{SUBSTITUTE: 'LINIA0'}
{SUBSTITUTE: 'sumw'}
          {IF: ~rep_export() & lineleft()<4}
{SUBSTITUTE: 'LINIAF'}
             {PAGE}
          {FI}
          {par.ok:=1; ~~}
      {OD}
   {FI}
{OD}
{IF: par.ok=1}
{SUBSTITUTE:'wsum'}
{FI}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'T8'}
{SPACE: 'B'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{IF: par.tab.ASK=0 }
{FOR: params_get().P}{DO}
{IF: P.seek(params_get().P.SQL,,1)}
{SUBSTITUTE: tresc}
{FI}
{OD}
{IF: par.ok}
{SUBSTITUTE: 'wsum'}
{FI}
{ELSE}
{SUBSTITUTE: 'wydzialy'}
{IF: par.ok}
{SUBSTITUTE: 'fsum'}
{FI}
{FI}
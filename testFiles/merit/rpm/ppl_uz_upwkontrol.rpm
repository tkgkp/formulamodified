:!UTF-8
{TITLE:Kontrola poprawności NIP i PESEL}
{PRINTER: exec('prt','param_wydr','v',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('__prep','__TB','__skul','par','__par1','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__Lp');
   VAR_DEL.delete('dane','__ok','__param','__edit','__chk');
   tresc:='wkontrol';
   __prep:=__F_ZATR.P;
   __ok:=0;
   PAR_WYDR.TITLE:={? __prep='P' || 'Wykaz pracowników'@ || 'Wykaz zleceniobiorców'@ ?};
   OSOBA.cntx_psh();
   P.cntx_psh();
   OSOBA.prefix();
   RD.index('_RODZINA');
   {? __prep<>'Z'
   || FUN.emsg('Wydruk dostępny dla formy współpracy: \"Zleceniobiorcy\".'@)
   || _upr:=exec('uprawnieniawrap','pkd','danych osobowych'@,'ZWS_DOS_PPDO','ZWS_DOS_PRDO');
      {? +_upr
      || FUN.emsg('Brak wymaganych uprawnień pozwalających na przeglądanie informacji o: %1'@[_upr])
      || __TB:=tab_tmp(2,
            'PESEL','STRING[11]','Pesel pracownika/zleceniobiorcy',
            'OSOBA','INTEGER','ref osoby',
            'PRAC','STRING[51]','Pracownik',
            'CZRO','STRING[51]','Członek rodziny',
            'BNIP','STRING[10]','Błędny NIP',
            'BPESEL','STRING[11]','Błędny PESEL',
            'OPIS','STRING[100]','Opis błędu'
         );

         __par1:=obj_new(@.CLASS.PRINT,3);
         __par1.add("{? +__TB.CZRO || __TB.CZRO || __TB.PRAC ?}",51,'Zleceniobiorca'@);
         {? ~rep_export()
         || __par1.add(
               "  {? +__TB.CZRO
                  || 'członek rodziny'@
                  || {? __prep='P'
                     || 'pracownik'@
                     || 'zleceniobiorca'@
                     ?}
                  ?}
               ",15,'Prac/Rodz'@
            )
         ?};
         __par1.add(
            "  _txto:='Błąd weryfikacji sumy kontrolnej.'@;
               _nn:={? (__TB.OPIS*_txto) || (__TB.OPIS*_txto)-1 || +__TB.OPIS ?};
               _nn+__TB.OPIS
            ",60,'Informacja o błędzie'@
         );
         __par1.s_frame();

         __chk:=
            "  _ok:='';
               {? __param.NIP=0 & __param.PESEL=0
               || FUN.emsg('Obowiązkowe jest wybranie przynajmniej jednej opcji.'@);
                  _ok:='NIP'
               || PAR_WYDR.TITLE+={? __prep='P' & __param.R_PESEL=1
                                  || ' oraz członków ich rodzin'@
                                  || '' ?}+'\n%1 '['z niepoprawnymi numerami'@]+
                                  {? __param.NIP=1 & __param.PESEL=1 || 'NIP i PESEL'@
                                  |? __param.NIP=1 || 'NIP'
                                  || 'PESEL'
                                  ?}; 1
                  ?}
            ";

         __param:=tab_tmp(1,
            'NIP','INTEGER','NIP współpracownika'@,
            'PESEL','INTEGER','PESEL współpracownika'@,
            'R_PESEL','INTEGER','PESEL członka rodziny'@
         );
         __param.NIP:=__param.PESEL:=1;
         __param.R_PESEL:=0;
         __param.add();
         __param.fld_fml('PESEL','AFTER_EDIT',
            "  {? ~__param.PESEL || __param.R_PESEL:=0 ?};
               __param.efld_opt(__edit,'enable='+$(__param.PESEL & __prep='P'),,'R_PESEL');
               win_disp()
            "
         );
         __edit:=__param.mk_edit('Parametry wydruku'@,0,'pkd_kontrolanip');
         __param.win_esep(__edit,'Kontrola poprawności NIP i PESEL'@);
         __param.win_efld(__edit,,'NIP',,,,,,20*' '+'NIP'@,,,'check-box','check_label="NIP"',"1","0");
         __param.win_efld(__edit,,'PESEL',,,,,,'PESEL'@,,,'check-box','check_label="PESEL"',"1","0");
         __param.efld_opt(__edit,'enable='+$(__prep='P'),,'R_PESEL');
         exec('ok_esc','#window',__param,__edit);
         __param.win_edit(__edit);
         {? __param.edit("__chk()")
         || b_rat:=h_rat:=vp:=__ok:=1;
            __Lp:=lmt:=rsep:=dane:=0;
            prn1:='__par1';
            params_set('P',exec('wybierz_parses','pracownik','PPL').P)
         ?}
      ?}
   ?}
}
{END:
   VAR_DEL.delete('__prep','__TB','__skul','par','__par1','prn1','vp','lmt','rsep','tresc','b_rat','h_rat','__Lp');
   VAR_DEL.delete('dane','__ok','__param','__edit','__chk');
   OSOBA.cntx_pop();
   P.cntx_pop()
}
{EXIT: ~__ok}
{EXIT: ~params_get().P.first()}
{COMMENT}OLD: wkontrol.rpm{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: __TB.size()}
   {FONT: 'T8'}{ h_rat:=charleft(); ~~ }
   {FONT: 'T8G'}{ h_rat:=charleft()/{? rep_export() || 1 || h_rat?}; ~~ }
   {SPACE: 'C'}
   {LINE}
   {<{ceil(lmt*h_rat)+1}'%1:'['Parametry sprawozdania'@]}
   {<{ceil(lmt*h_rat)+1}'Zakres: '+{? __prep='P' || 'pracownicy'@ || 'zleceniobiorcy'@ ?}+' wskazani przez operatora'@}
   {IF: __param.NIP}{<{ceil(lmt*h_rat)+1}' - %1 NIP'['kontrola poprawności numerów'@]}{FI}
   {IF: __param.PESEL}{<{ceil(lmt*h_rat)+1}' - %1 PESEL'@['kontrola poprawności numerów'@]}{FI}
   {IF: __prep='P' & __param.R_PESEL}{<{ceil(lmt*h_rat)+1
                                      }' - kontrola poprawności numerów PESEL członków rodzin'@}
   {FI}
{FI}
{FONT: 'T8B'}
{SPACE: 'B'}
{LINE}
{HEADER-}
{IF: ~rep_export()}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{MACRO: tresc}
{FONT: 'T8'}
{SPACE: 'B'}
{  P.OSOBA();
   {? (exec('nieobcy','osoba') | +OSOBA.PESEL)
   || '%1 PESEL'['Kontrola poprawnosci'@];
      _kom:='%1 PESEL.'['Błędny numer'@];
      _errp:='';
      {? __param.PESEL
        & (+OSOBA.PESEL=0 | ( ~__TB.find_tab(0,'PESEL',,'=',OSOBA.PESEL,'BPESEL',,'=',OSOBA.PESEL)
          & +(_errp:=exec('pesel_ok','#id',OSOBA.PESEL,OSOBA.UR_DATA,OSOBA.PLEC))>1))
      || __TB.blank();
         {? +OSOBA.PESEL=0
         || _errp:='%1 PESEL.'['Brak numeru'@]
         ?};
         __TB.PESEL:=__TB.BPESEL:=OSOBA.PESEL;
         __TB.OSOBA:=#OSOBA.ref();
         __TB.PRAC:=OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE;
         __TB.OPIS:={? +OSOBA.PESEL || _kom+' ' || '' ?}+_errp;
         __TB.add()
      ?};
      'Kontrola poprawnosci NIP';
      {? __param.NIP
       & ~__TB.find_tab(0,'PESEL',,'=',OSOBA.PESEL,'BNIP',,'=',STR.gsub(OSOBA.NIP,'-',''))
       & ~exec('nip_ok','#id',OSOBA.NIP,1)
      || __TB.blank();
         __TB.PESEL:=OSOBA.PESEL;
         __TB.OSOBA:=#OSOBA.ref;
         __TB.BNIP:=STR.gsub(OSOBA.NIP,'-','');
         __TB.PRAC:=OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE;
         __TB.OPIS:='%1 NIP. %2.'['Błędny numer'@,'Błąd weryfikacji sumy kontrolnej'@];
         __TB.add()
      ?};
      'Kontrola poprawnosci PESEL czlonkow rodzin';
      {? __prep='P' & __param.R_PESEL
      || RD.prefix(OSOBA.ref());
         {? RD.first()
         || {!
            |? _errc:='';
               {? (exec('nieobcy','osoba') & +RD.PESEL=0)
                  | (+(_errc:=exec('pesel_ok','#id',RD.PESEL,RD.DA))>1
                    &
                    ~__TB.find_tab(0,'PESEL',,'=',OSOBA.PESEL,'BPESEL',,'=',RD.PESEL))
               || {? +RD.PESEL=0
                  || _errc:='%1 PESEL.'['Brak numeru'@]
                  ?};
                  __TB.blank();
                  __TB.PESEL:=OSOBA.PESEL;
                  __TB.OSOBA:=#OSOBA.ref();
                  __TB.PRAC:=OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE;
                  __TB.CZRO:=RD.NA+' '+RD.IM;
                  __TB.BPESEL:=RD.PESEL;
                  __TB.OPIS:={? +RD.PESEL || _kom+' ' || '' ?}+_errc;
                  __TB.add()
               ?};
               RD.next()
            !}
         ?}
      ?}
   ?};
~~}
{MACRO-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W12'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8'}{ b_rat/={? rep_export() || 1 || charleft()?}; ~~ }
{SPACE: 'B'}
{  lmt:=int((charleft()-__par1.width())/2); ~~ }
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{IF: __TB.first()}
{  __skul:=sql('select distinct PESEL, OSOBA, PRAC from :_a order by 3,1,2',__TB); ~~ }
{IF: rep_export()}
   {SUBSTITUTE: 'TABHEAD'}
   {SUBSTITUTE: 'LINIAF'}
{FI}
{FOR: __skul}
{DO}
   {ENTIRE}
   {IF: ~rep_export()}
      {  __Lp+=1; $__Lp+') '+{? __prep='P' || 'Pracownik: '@ || 'Zleceniobiorca: '@ ?}+__skul.PRAC}
   {FI}
   {REP: __par1.fhbar(lmt)}
   {FOR: __TB}{PREFIX: __skul.PESEL, __skul.OSOBA}
   {DO}
      {  __par1.ini_line(); ~~ }
      {REP: __par1.fline(lmt)}
   {OD}
   {REP: __par1.flbar(lmt)}
   {ENTIRE-}
{OD}
{FI}
:!UTF-8
{TITLE:E. Porównanie ilości zużytych surowców z normami technologicznymi}
{PRINTER: exec('set_parw','param_wydr');exec('prt','param_wydr','h'),,,,,'E','C',,,,,,1,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,
      PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
  ZL.cntx_psh();
  ZL.prefix();
  ZTP.cntx_psh();
  ZLIM.cntx_psh();
  VAR_DEL.delete('TAB1','PRN','ZLST','ndx');
  TAB1:=tab_tmp(2,
     'SYM','STRING[20]','Symbol',
     'KTM','STRING[50]','Ktm',
     'ILZ','REAL','Il.zużyta',
     'ILNOR','REAL','Il.wg normy',
     'ILTECH','REAL','Il.techn.',
     'LIMIT','STRING[1]','Limit',
     'JM','STRING[10]','Jednostka miary',
     'ZL_KTM','STRING[50]','Produkt zlecenia'
  );
  ZL_TAB:=~~;
  PRN:=obj_new(@.CLASS.PRINT, 10);
  PRN.s_frame();
  prn1:='PRN';
  tryb:=rep_export();
  waslbar:=0;
  dalej:=dalej1:=dalej2:=dalej3:=suma:=sumalim:=0;
  typ:=rodzaj:=zakr_wg:='';
  zlo_od:=zlo_do:=zlz_od:=zlz_do:=dk_od:=dk_do:=date(0,0,0);
  zakres_str:='';zakres_str2:='';
  color:='';
  lim:='T';
  lm:=ll:=vp:=lmt:=rsep:=0;
  _fb:="
     _win:=cur_win(1,1);
     {? WYDRUK.ZAKR_WG='D'
     || WYDRUK.ODDNIA:=WYDRUK.DODNIA:=WYDRUK.ODDNIA2:=WYDRUK.DODNIA2:=date(0,0,0);
        WYDRUK.efld_opt(_win,'editable=grayed',,'ODDNIA');
        WYDRUK.efld_opt(_win,'editable=grayed',,'DODNIA');
        WYDRUK.efld_opt(_win,'editable=grayed',,'ODDNIA2');
        WYDRUK.efld_opt(_win,'editable=grayed',,'DODNIA2');
        WYDRUK.efld_opt(_win,'editable=1',,'ODDNIA3');
        WYDRUK.efld_opt(_win,'editable=1',,'DODNIA3')
     || WYDRUK.ODDNIA3:=WYDRUK.DODNIA3:=date(0,0,0);
        WYDRUK.efld_opt(_win,'editable=1',,'ODDNIA');
        WYDRUK.efld_opt(_win,'editable=1',,'DODNIA');
        WYDRUK.efld_opt(_win,'editable=grayed',,'ODDNIA3');
        WYDRUK.efld_opt(_win,'editable=grayed',,'DODNIA3');
        {? WYDRUK.ZLECOZ='O'
        || WYDRUK.ODDNIA2:=WYDRUK.DODNIA2:=date(0,0,0);
           WYDRUK.efld_opt(_win,'editable=grayed',,'ODDNIA2');
           WYDRUK.efld_opt(_win,'editable=grayed',,'DODNIA2')
        || WYDRUK.efld_opt(_win,'editable=1',,'ODDNIA2');
           WYDRUK.efld_opt(_win,'editable=1',,'DODNIA2')
        ?}
     ?};
     ~~
  ";
  WYDRUK.fld_fml('ZLECOZ','AFTER_EDIT',_fb);
  WYDRUK.fld_fml('ZAKR_WG','AFTER_EDIT',_fb);
  WYDRUK.fld_fml('ZAKR_WG','BEFORE_DISPLAY',_fb)
}
{END:
  ZL.cntx_pop();
  ZL.prefix();
  {? ZL.f_active() || ZL.f_clear(1) ?};
  ZTP.cntx_pop();
  ZLIM.cntx_pop();
  VAR_DEL.delete('TAB1','PRN','ZLST','ndx','ZL_TAB');
  &dalej;
  &dalej1;
  &dalej2;
  &dalej3;
  &suma;
  &lim;
  &sumalim;
  &prn1;
  &color;
  &lm; &ll; &vp; &lmt; &rsep;
  &typ; &rodzaj; &zakr_wg;
  &zlo_od; &zlo_do; &zlz_od; &zlz_do; &dk_od; &dk_do;
  &zakres_str; &zakres_str2;
  &tryb;
  WYDRUK.fld_fml('ZLECOZ','AFTER_EDIT',"*");
  WYDRUK.fld_fml('ZAKR_WG','AFTER_EDIT',"*");
  WYDRUK.fld_fml('ZAKR_WG','BEFORE_DISPLAY',"*")
}
{COMMENT}
  Wydruk porównania ilości zużytych surowców z normami technologicznymi.
  [MKO] - 2002/01/30 - [7.60] OWwPRO27/5.2
  Wywołanie: Zlecenia (produkcyjne, warsztatowe) -> Raporty -> Zestawienia
{COMMENT-}
{ _wydr_naz:='zlec_005';
  WYDRUKIL.index('WYDRUKI');
  WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
  {? ~WYDRUKIL.first()
  || WYDRUKIL.blank();
     WYDRUKIL.USERS:=OPERATOR.USER().KOD;
     WYDRUKIL.WYDRUK:=_wydr_naz;
     WYDRUKIL.add();
     WYDRUK.blank()
  || WYDRUK.ZLECOZ:=WYDRUKIL.SORTUJ;
     WYDRUK.TYPZL:=WYDRUKIL.FAK;
     WYDRUK.ZAKR_WG:=WYDRUKIL.RODZFAK;
     WYDRUK.ODDNIA:=WYDRUK.DODNIA:=WYDRUK.ODDNIA2:=WYDRUK.DODNIA2:=WYDRUK.ODDNIA3:=WYDRUK.DODNIA3:=date(0,0,0)
  ?};
  WYDRUK.win_edit(); WYDRUK.win_edit('ZLEC_005');~~}
{IF: WYDRUK.edit() }
{ WYDRUKIL.SORTUJ:=WYDRUK.ZLECOZ;
  WYDRUKIL.FAK:=WYDRUK.TYPZL;
  WYDRUKIL.RODZFAK:=WYDRUK.ZAKR_WG;
  WYDRUKIL.put(1);
  typ:=WYDRUK.TYPZL;
  rodzaj:=WYDRUK.ZLECOZ;
  zakr_wg:=WYDRUK.ZAKR_WG;
  zlo_od:=WYDRUK.ODDNIA;
  zlo_do:=WYDRUK.DODNIA;
  zlz_od:=WYDRUK.ODDNIA2;
  zlz_do:=WYDRUK.DODNIA2;
  dk_od:=WYDRUK.ODDNIA3;
  dk_do:=WYDRUK.DODNIA3;
~~}
{ELSE}
{EXIT}
{FI}
{COMMENT}--Gdy zakres wg dok. to zebranie listy zleceń---{COMMENT-}
{ {? zakr_wg='D' & (dk_od<>date(0,0,0) | dk_do<>date(0,0,0))
  || ZL_TAB:=exec('zlst','zl_common',,dk_od,dk_do,1)
  ?};
~~}
{COMMENT}--Definicja wiersza wydruku --------------------{COMMENT-}
{ PRN.add("sym",20,'Symbol zlecenia'@);
  {? typ='P' || PRN.add("zl_ktm",20,'Produkt zlecenia'@) ?};
  {? tryb<>1
  || PRN.add("ktm",20,'Kod surowca'@,,'#');
     PRN.add("limit",5,'Limit'@,,'#');
     {? typ='P' ||
        PRN.add("t",12,'Ilość wg#technologii'@,1,'#')
     ?};
     PRN.add("n",12,'Ilość wg#normy'@,1,'#');
     PRN.add("z",12,'Ilość zużyta'@,1,'#');
     PRN.add("jm",6,'Jedn. miary'@,,'#');
     PRN.add("p",8,'%% wyk.'@,1,'#')
  || PRN.add("ktm",20,'Kod surowca'@);
     PRN.add("limit",5,'Limit'@);
     {? typ='P' ||
        PRN.add("t",12,'Ilość wg#technologii'@,1)
     ?};
     PRN.add("n",12,'Ilość wg#normy'@,1);
     PRN.add("z",12,'Ilość zużyta'@,1);
     PRN.add("jm",6,'Jedn. miary'@,,'#');
     PRN.add("p",8,'%% wyk.'@,1)
  ?};
  PAR_WYDR.TITLE:='PORÓWNANIE ILOŚCI ZUŻYTYCH SUROWCÓW Z NORMAMI'@; ~~}
{INCLUDE: wsp_pw.rpi}
{COMMENT}------------------------------REPORT-HEADER----------------{COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10'}{SPACE: 'B'}
{ {? zakr_wg='D' & (dk_od<>date(0,0,0) | dk_do<>date(0,0,0))
  || zakres_str:='Zakres wystawienia dokumentów:'+{? dk_od<>date(0,0,0) || ' od %1'@[form(dk_od)] || '' ?}+
      {? dk_do<>date(0,0,0) || ' do %1'@[form(dk_do)] || '' ?}
  |? zakr_wg='Z'
  || {? zlo_od<>date(0,0,0) | zlo_do<>date(0,0,0)
     || zakres_str:='Zakres powołania zleceń:'+{? zlo_od<>date(0,0,0) || ' od %1'@[form(zlo_od)] || '' ?}+
         {? zlo_do<>date(0,0,0) || ' do %1'@[form(zlo_do)] || '' ?}
     ?};
     {? zlz_od<>date(0,0,0) | zlz_do<>date(0,0,0)
     || zakres_str2:='Zakres zamknięcia zleceń:'+{? zlz_od<>date(0,0,0) || ' od %1'@[form(zlz_od)] || '' ?}+
         {? zlz_do<>date(0,0,0) || ' do %1'@[form(zlz_do)] || '' ?}
     ?}
  ?};
~~}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1} 'PARAMETRY WYDRUKU:'@}
{<{lmt+1} 'Typ: %1'@[{? typ='P' || 'Zlecenie produkcyjne'@ || 'Zlecenie warsztatowe'@ ?}]}
{<{lmt+1} {? rodzaj='O' || 'Zlecenia otwarte'@ |? rodzaj='Z' || 'Zlecenia zamknięte'@ || 'Zlecenia wszystkie'@ ?}}
{IF: zakres_str<>''}{<{lmt+1} zakres_str}{FI}
{IF: zakres_str2<>''}{<{lmt+1} zakres_str2}{FI}
{FONT: 'T10B'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T10'}{ vp:=1; ~~}
{COMMENT}------------------------------HEADER-----------------------{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T10B'}{SPACE: 'B'}
{SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}------------------------------FOOTER-----------------------{COMMENT-}
{FOOTER: 0}
{SUBSTITUTE: 'LINIAF'}
{LINE: 1}
{FOOTER-}
{COMMENT}------------------------------SIGNATURE--------------------{COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}----------------------------PRZYGOTOWANIE DANYCH-----------------------{COMMENT-}
{FONT: 'T10'}{SPACE: 'B'}
{ rsep:=PAR_WYDR.RSEP;
  exec('tktl_cntx_psh','tech_common');
  ~~
}
{IF: typ='P'}
  { ZTP.clear(); ZTP.index('RDTP'); ZTP.prefix('P'); ~~}
{ELSE}
  { ZTP.clear(); ZTP.index('RDTP'); ZTP.prefix('W'); ~~}
{FI}
{IF: ZTP.first}{dalej:=1;~~}{FI}
{WHILE: dalej}
{DO}
  {  _where:='ZL.TYP=:_a and HIDDEN=\'N\'';
     {? rodzaj='Z'
     || _where+=' and STAN=\'Z\' '
     |? rodzaj='O'
     || _where+=' and STAN=\'O\' '
     ?};
     {? zakr_wg='D'
     || {? dk_od<>date(0,0,0) | dk_do<>date(0,0,0)
        || _where+=' and ZL.REFERENCE in (select SQL from :_f) '
        ?}
     || {? zlo_od<>date(0,0,0)
        || _where+=' and ZL.OD>=TO_DATE(:_b) '
        ?};
        {? zlo_do<>date(0,0,0)
        || _where+=' and ZL.OD<=TO_DATE(:_c) '
        ?};
        {? zlz_od<>date(0,0,0)
        || _where+=' and ZL.DO>=TO_DATE(:_d) '
        ?};
        {? zlz_do<>date(0,0,0)
        || _where+=' and ZL.DO<=TO_DATE(:_e) '
        ?}
     ?};
     ZL.prefix();
     ZL.f_set(,,_where,ZTP.ref(),zlo_od,zlo_do,zlz_od,zlz_do,ZL_TAB);
  ~~}
  {IF: ZL.f_first()}{ dalej1:=1; ~~}{FI}
  {WHILE: dalej1}
  {DO}
{  VAR_DEL.delete('ZLST');
tktl_sqlref:='';
ZLST:=exec('zlst','zl_common',ZL.ref(),dk_od,dk_do);
ndx:=ZLST.ndx_tmp('',,'ZL',,,'KOD',,);
~~
}
 {IF: ZLIM.use('zlim'+ZL.MASKA)}
{ TKTL.clear();
  {? ZL.RTKTL<>''
  || tktl_sqlref:=ZL.RTKTL
  |? ZL.RKTL<>''
  || tktl_sqlref:=ZL.RKTL
  ?};
  exec('tktl_use','tech_common',(8+tktl_sqlref)+3);
  ~~
}
{IF: TKTL.seek(tktl_sqlref) | typ='W'}
  { TMAT.clear(); TMAT.index('NL'); TMAT.prefix(TKTL.ref()); ~~}
  {IF: TMAT.first()}{dalej2:=1;~~}{FI}
  {WHILE: dalej2}
  {DO}
{
ZLST.clear();
ZLST.index(ndx);
ZLST.prefix($ZL.ref(),$TMAT.PT);
{? ZLST.first() || ZLST.last(); suma:=ZLST.IL || suma:=0 ?};
 ~~}
  {IF: ZL.RODZAJ='P'}
{ ZLIM.clear(); ZLIM.index('ZKN'); ~~}
  {ELSE}
{ ZLIM.clear(); ZLIM.index('ZKP'); ~~}
  {FI}
{ ZLIM.prefix(ZL.ref(),'T',TMAT.PT); dalej3:=0; ~~}
  {IF: ZLIM.first()}
{ dalej3:=1;~~}
  {FI}
  {sumalim:=0; ~~}
  {IF: typ='P'}
{
  _xjm:=exec('FindAndGet','#table',TKTL,ZL.RKTL,,"XJM",1);
  wsp:=ZL.IL/_xjm;
  sumatech:=TMAT.WARB*wsp; ~~}
  {FI}
 {WHILE: dalej3}
 {DO}
{ sumalim+=ZLIM.LIL; ~~}
{ dalej3:=ZLIM.next(); ~~}
 {OD}
{
TAB1.prefix(ZL.SYM,TMAT.PT().KTM);
{? ~TAB1.first()
|| TAB1.SYM:=ZL.SYM;
TAB1.ZL_KTM:=ZL.KTM().KTM;
TAB1.KTM:=TMAT.PT().KTM;
TAB1.JM:=TMAT.PT().J().KOD;
TAB1.LIMIT:=TMAT.LIMIT;
TAB1.ILZ:=suma;
TAB1.ILNOR:=sumalim;
{? typ='P'
|| TAB1.ILTECH:=sumatech
?};
TAB1.add()
?}; ~~}
 { dalej2:=TMAT.next(); dalej3:=0; ~~}
  {OD}
  {IF: ZL.RODZAJ='P' & ZL.NRNZL=0}
{ ZLIM.clear(); ZLIM.index('ZKN'); ~~}
  {ELIF: (ZL.RODZAJ<>'P' & ZL.GENLIM='N' & ZL.NRNZL=0) |
 (ZL.RODZAJ='P' &  ZL.GENLIM='P' & ZL.NRNZL<>0) }
{ ZLIM.clear(); ZLIM.index('ZKN'); ~~}
  {ELSE}
{ ZLIM.clear(); ZLIM.index('ZKP'); ~~}
  {FI}
  { ZLIM.prefix(ZL.ref); dalej3:=0; ~~}
  {IF: ZLIM.first()}
  { dalej3:=1;~~}
  {FI}
{WHILE: dalej3}
 {DO}
{ TAB1.prefix(ZL.SYM,ZLIM.KTM().KTM);
{? ~TAB1.first()
|| TAB1.SYM:=ZL.SYM;
TAB1.ZL_KTM:=ZL.KTM().KTM;
TAB1.KTM:=ZLIM.KTM().KTM;
TAB1.JM:=ZLIM.KTM().J().KOD;
TAB1.LIMIT:='T';
ZLST.index(ndx);
{? (ZL.NRNZL=0) |
(ZL.GENLIM='P' & ZL.NRNZL<>0)
|| ZLST.prefix($ZL.ref(),$ZLIM.KTM)
|| ZL.cntx_psh();
ZL.index('UNRZL');
ZL.prefix(ZL.NRNZL);
ZL.first();
_zl:=ZL.ref();
ZL.cntx_pop();
ZLST.prefix($_zl,$ZLIM.KTM)
?};
{? ZLST.first() || ZLST.last(); TAB1.ILZ:=ZLST.IL || TAB1.ILZ:=0 ?};
TAB1.ILNOR:=ZLIM.LIL;
TAB1.ILTECH:=0;
TAB1.add()
|| {? TAB1.ILTECH=0 & typ='P'
|| TAB1.ILNOR+=ZLIM.LIL;
TAB1.put()
?}
?}; ~~}
{ dalej3:=ZLIM.next(); ~~}
 {OD}
 {FI}
  {FI}
 { dalej1:=ZL.f_next(); dalej2:=dalej3:=0; ~~}
  {OD}
  { dalej:=ZTP.next(); dalej1:=dalej2:=dalej3:=0; ~~}
{OD}
{ dalej:=0;
  exec('tktl_cntx_pop','tech_common');
  ~~
}
{COMMENT}--------------------WŁAŚCIWY WYDRUK-----------------------{COMMENT-}
{IF: TAB1.first()}
    { dalej:=z:=n:=t:=p:=ktm:=limit:=jm:='';sumaz:=suman:=sumat:=sumap:=0;~~}
{FI}
{FOR: TAB1}
{DO}
{IF: tryb<>1}
    {z+=form(TAB1.ILZ,12,4)+'#';
     n+=form(TAB1.ILNOR,12,4)+'#';
     {? typ='P' || t+=form(TAB1.ILTECH,12,4)+'#' ?};
    ktm+=form(TAB1.KTM,20)+'#';
    limit+=form(TAB1.LIMIT,5)+'#';
    jm+=form(TAB1.JM,6)+'#';
    p+={? TAB1.ILNOR
    || form(({? TAB1.ILNOR=0 || 0 || TAB1.ILZ/TAB1.ILNOR ?})*100,8,2)
    || form(({? TAB1.ILTECH=0 || 0 || TAB1.ILZ/TAB1.ILTECH ?})*100,8,2) ?}+'#';
    sumaz+=TAB1.ILZ;
    suman+=TAB1.ILNOR;
    sym:=TAB1.SYM;
    {? typ='P' || zl_ktm:=TAB1.ZL_KTM ?};
    {? typ='P' || sumat+=TAB1.ILTECH ?};
    {? TAB1.ILNOR >0 ||
       sumap+=({? TAB1.ILNOR=0 || 0 || TAB1.ILZ/TAB1.ILNOR*100 ?}) ||
    {? typ='P'|| sumap+=({? TAB1.ILTECH=0 || 0 || TAB1.ILZ/TAB1.ILTECH*100 ?}) || sumap:=0 ?}
    ?};
    ~~}
{ELSE}
    { z:=form(TAB1.ILZ,12,4);
      n:=form(TAB1.ILNOR,12,4);
      {? typ='P' || t:=form(TAB1.ILTECH,12,4) ?};
      ktm:=form(TAB1.KTM,20);
      limit:=form(TAB1.LIMIT,5);
      jm:=form(TAB1.JM,6);
      p:={? TAB1.ILNOR
         || form(({? TAB1.ILNOR=0 || 0 || TAB1.ILZ/TAB1.ILNOR ?})*100,8,2)
         || form(({? TAB1.ILTECH=0 || 0 || TAB1.ILZ/TAB1.ILTECH ?})*100,8,2)
         ?};
      sym:=TAB1.SYM;
      {? typ='P' || zl_ktm:=TAB1.ZL_KTM ?};
    ~~}
    {SUBSTITUTE: 'LINIA0'}
{FI}
{TOTAL: TAB1.SYM}
      {IF: tryb<>1}
         {SUBSTITUTE: 'LINIA0'}
         {z:=n:=t:=p:=ktm:=limit:=jm:='';sumaz:=suman:=sumat:=sumap:=0;~~}
      {FI}
{OD}

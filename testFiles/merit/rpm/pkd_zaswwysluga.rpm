:!UTF-8
{TITLE:Zmiana wysługi}
{VERSION: PEP}
{PRINTER: dziedzina:='PKD';
   exec('prt','param_wydr','v',KST),exec('rep_set','rap_zew','PKD_ZASWWYSL')='T',,,,'I','B',,,,,,1,
   PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1}
{BEGIN:
   P.cntx_psh();
   VAR_DEL.delete('Text','indent','Lata','Data','__CALL','tresc','cnt','Sm','Sl');
   VAR_DEL.delete('czy_szablon','czy_paperless','tytul','czy_pep');
   _par:=params_get();
   tytul:='Zmiana wysługi';
   czy_szablon:=exec('rep_set','rap_zew','PKD_ZASWWYSL',1)='T';
   czy_pep:=var_pres('_par')>100 & var_pres('PEP',_par)>0;
   tresc:='wysluga';
   PAR_WYDR.TITLE:='';
   indent:=1;
   Text:='';
   Lata:=cnt:=0;
   Data:=#0;
   Sm:=Sl:=0;

   _upr:=exec('uprawnieniawrap','pkd','Danych pracownika'@,'PKD_EZK_PPRA','PKD_EZK_ORPR');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@ [_upr])
   || _paperless:=czy_pep & var_pres('active',_par.PEP)>0 & _par.PEP.active;
      {? _paperless
      || czy_paperless:=0;
         {? ~czy_szablon & exec('rep_set','rap_zew','PKD_ZASWWYSL')<>'T' & _par.PEP.fml_after=""
         || czy_paperless:=1
         ?}
      || czy_paperless:=1
      ?};
      P.clear();
      {? (_paperless & _par.PEP.fml_after="") | ~_paperless
      || _tab:=exec('par_wydr_ddp','pkd',
            tytul,
            "  _tab:=params_get().tab;
               {? _tab.ROK>0 & _tab.ROK<((date()~1)+100)
               || {? _tab.MC<1 |_tab.MC>12
                  || FUN.emsg('Wartość w polu miesiąc musi mieć wartość z zakresu od 1 do 12.'); 'MC'
                  || 1
                  ?}
               || FUN.emsg('Nieprawidłowa wartość w polu \"Rok\".'); 'ROK'
               ?}
            ",,,,,,,,,,,,
            date()~1,,
            date()~2)
      ?};
      {? (_paperless & _par.PEP.fml_after<>"") | _tab.size()
      || Data:={? (_paperless & _par.PEP.fml_after="") | ~_paperless || date(_tab.ROK,_tab.MC,1) || date() ?};
         {? _paperless
         || {? _par.PEP.P
            || params_set(
                  'P',exec('wybierz_silent','pracownik',_par.PEP.P).P,
                  'paperless',_paperless,
                  'par',_par,
                  'CALL',null(),
                  'Data',Data
               )
            || _args:=exec('wybierz_args','pracownik');
               _args.DOMAIN:=dziedzina;
               _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
               _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
               _args.F_ZATR:=__PARSES.getVal('F_ZATR').KOD;
               _args.VIEW:=__PARSES.getVal('ZakresDanych');
               _args.SQL_FROM:=" JOIN OSOBA using(P.OSOBA, OSOBA.REFERENCE) "+
                               " JOIN USERS using(USERS.OSOBA, OSOBA.REFERENCE)"+
                               " JOIN USERSF using(USERSF.USERS, USERS.REFERENCE)";
               _args.SQL_WHERE:=''+"P.PORTAL='T' and USERSF.PORTAL2='T'";
               _args.WIELU:=~_paperless;
               params_set(
                  'P',exec('wybierz_parses','pracownik',dziedzina,_args).P,
                  'paperless',_paperless,
                  'par',_par,
                  'CALL',null(),
                  'Data',Data
               )
            ?}
         || params_set(
               'P',exec('wybierz_parses','pracownik','PKD').P,
               'paperless',_paperless,
               'par',_par,
               'CALL',null(),
               'Data',Data
            )
         ?}
      ?};

      __xr_ins:=exec('new_ins','rap_zew','PKD_ZASWWYSL');
      __ustal:="
        Sl:=0; Lata:=0;
        _buf:=exec('oblicz','staz',0,,1,,Data);
        {? _buf.lat[1]=0 & _buf.msc[1]=0 & _buf.dni[1]=0 || return() ?};
        _dS:=__KAL.sub_date(Data,_buf.lat[1],_buf.msc[1],_buf.dni[1]);
        {? _dS~3=1
        || Sm:=_dS~2
        || Sm:=(_dS~2)+1;
           {? Sm>12 || Sm:=1; Sl:=1 ?}
        ?};
        {? P.ZA='T' & Data~2=Sm
        || Lata:=(Data~1)-(_dS~1)-Sl
        ?};
         ~~
      "
   ?}
}
{END:
   P.cntx_pop();
   {? var_pres('__xr_ins')=type_of(null()) || exec('del_ins','rap_zew',&__xr_ins) ?};
   VAR_DEL.delete('Text','indent','Lata','Data','__CALL','tresc','cnt','__ustal','Sm','Sl');
   VAR_DEL.delete('dziedzina','czy_szablon','czy_paperless','tytul','czy_pep')
}
{COMMENT}OLD: zaswwysl.rpm{COMMENT-}
{COMMENT}OLD: zaswwysl.rpi{COMMENT-}
{EXIT: Data=#0}
{EXIT: ~params_get().P.first()}
{EXIT: ~__xr_ins}
{MACRO: tresc}
   {IF: (params_get().paperless & params_get().par.PEP.fml_after="") | ~params_get().paperless}
      { __ustal(); ~~ }
      {IF: Lata>=5 & Lata<21}{
         cnt+=1;
         exec('ins_par','rap_zew',__xr_ins,cnt,
            'F_NAZWA',KST.NAZWA,
            'MIEJSCE',PAR_WYDR.MIEJSC,
            'DATA',date()$4,
            'F_ULICA',exec('ulica','stalesys'),
            'F_POCZTA',exec('poczta','stalesys'),
            'F_REGON',KST.REG,
            'F_NKP',KST.NKP,
            'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?}
         );
         exec('ins_par','rap_zew',__xr_ins,cnt,
            'NAZWISKO',P.OSOBA().NAZWISKO,
            'IMIE',form(OSOBA.PIERWSZE+' '+OSOBA.DRUGIE),
            'PLEC',OSOBA.PLEC,
            'OD_DNIA',{? Data<>#0 || Data$6+'r.' || '' ?},
            'LATA',Lata
         );
         ~~}
      {FI}
   {FI}
   {exec('ins_par','rap_zew',__xr_ins,cnt,'CZY_ZAP',{? params_get().paperless || '1' || ' ' ?});~~}
{MACRO-}
{IF: czy_szablon}
   {FOR: params_get().P}{DO}
      {IF: P.seek(params_get().P.SQL,,1)}{
         P.OSOBA();
         __ustal();
         {? Lata>=5 & Lata<21
         || params_set('DATA',Data);
            _paperless:=params_get().paperless;
            _exe:={? _paperless || 2 || 3 ?};
            _file:=exec('wypelnij','szablon','PKD_ZASWWYSL',,,_exe,,_paperless);
            {? _paperless & var_pres('_file')>0 & var_pres('INFO',_file)>0
            || exec('add_file_and_run','paperless_grp',
                  P.ref(),_file.INFO,'Inne',tytul)
            ?}
         ?};
         ~~
      }{FI}
   {OD}
   {EXIT}
{FI}
{FOR: params_get().P}{DO}
   {IF: P.seek(params_get().P.SQL,,1)}
      {SUBSTITUTE: tresc}
   {FI}
{OD}
{EXIT: ~exec('ready','rap_zew',__xr_ins) & ~(params_get().paperless & ~czy_paperless)}
{IF: exec('rep_set','rap_zew','PKD_ZASWWYSL')='T'}
   {
   exec('use_tmp','rap_zew','PKD_ZASWWYSL');
   {? params_get().paperless
   || exec('add_file_and_run','paperless_grp',
         P.ref(),
         '@!Tmp\\macrotmp.doc',
         'Inne',
         tytul
      )
   ?};
   ~~ }
   {EXIT}
{ELSE}
{
   params_set(_par:=params_get());
   {? _par.paperless & ~czy_paperless
   || __CALL:=_par.par.PEP.CALL
   || __CALL:=proc_exec('wysl@zasw',#__xr_ins);
      {? czy_pep || _par.par.PEP.CALL:=__CALL ?}
   ?};
~~ }
   {IF: params_get().paperless & params_get().par.PEP.fml_after=""}
      {
      params_get().par.PEP.P:=P.ref();
      params_get().par.PEP.fml_after:="
         _pep:=obj_new('active','fml_after','P','CALL');
         _pep.active:=1;
         _pep.fml_after:=\"1\";
         _pep.P:=_a;
         _pep.CALL:=_b;
         {? var_pres('_params')>100
         || exec('obj_ntab_set','#array',_params,'PEP',_pep)
         || _params:=obj_new('PEP');
            _params.PEP:=_pep
         ?};
         params_set(_params);
         _name:='@'+tmp_dir()+exec('sep','#file')+'pkd_zaswwysluga_%1'[$P.tm_stamp()]+'.pdf';
         rep_exec('pkd_zaswwysluga',,0,_name,1,,1);
         exec('add_file_and_run','paperless_grp',
         _pep.P,_name,'Inne','Zmiana wysługi')
      ";
      ~~}
      {EXIT}
   {ELSE}
      {INCLUDE: p_zaswwysl.rpi}
   {FI}
{FI}
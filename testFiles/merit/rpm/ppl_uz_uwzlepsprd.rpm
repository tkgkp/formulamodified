:!UTF-8
{TITLE:Pełne zestawienie składników wypłat dla osoby}
{PRINTER: exec('prt','param_wydr','h',KST),,,,,'Q','C',,,,,,1,
          PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,PAR_WYDR.MBOT,0,1,,'enable'}
{BEGIN:
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','b_rat','h_rat','__edit','__param','__czyP');
   PAR_WYDR.TITLE:='Pełne zestawienie składników wypłat'@;
   P.cntx_psh();
   par:=obj_new(15);
   par[7]:=par[9]:=__czyP:=0;
   par[15]:=rep_export();
   _upr:=exec('uprawnieniawrap','pkd','Rachunkach'@,'PPL_ZLC_RRAC','PPL_ZLC_PRAC');
   {? +_upr
   || FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_upr])
   || __param:=tab_tmp(1,
         'PRAC','INTEGER','Uwzględniać informacje ze stosunku pracy?',
         'ROK','INTEGER','Rok',
         'DRUP','INTEGER','Parametry sprawozdania na każdej stronie'
      );
      __param.ROK:=date()~1;
      __param.DRUP:=0;
      __param.add();

      __edit:=__param.mk_edit(PAR_WYDR.TITLE,0,'uz_uwzlepsprd');
      __param.win_esep(__edit,'Parametry wydruku'@);
      __param.win_efld(__edit,,'PRAC',,,,,,'Stosunek pracy'@,,,'check-box',
                      'check_label="%1"'['Uwzględniać informacje ze stosunku pracy?'@],"1","0"
      );
      __param.win_efld(__edit,,'ROK',,,4,,,'Raport za rok'@,,'Rok podatkowy'@);
      __param.win_efld(__edit,,'DRUP',,,,,,'Drukować parametry sprawozdania'@,,,'radio-buttons'
                             ,,'na pierwszej stronie'@,"0",
                               'na każdej stronie'@,"1"
      );
      __param.efld_opt(__edit,'mark=1',,'ROK');
      __param.efld_opt(__edit,'enable='+$(~par[15]),,'DRUP');
      exec('ok_esc','#window',__param,__edit);
      __param.win_edit(__edit);
      {? par[9]:=__param.edit(
            "  {? __param.ROK<=1900
               || FUN.emsg('Błędna wartość parametru wydruku.'@);
                  return('ROK')
               || par[7]:=__param.ROK
               ?}
            "
         )
      || par[1]:=obj_new(@.CLASS.PRINT,14);
         {? par[15]
         || par[1].add("par[3].NAZWISKO+' '+par[3].PIERWSZE+' PESEL: '+par[3].PESEL",40,'Zleceniobiorca'@)
         ?};
         par[1].add("__RUB.RT[par[3].RN]",20,'Nazwa składnika'@);
         {! _sx:=1..12
         |! STR.split(date(,_sx,1)$8);
            par[1].add($('par[8]['+$_sx+']'),12,STR.get_word(),1,,,,,'12,2')
         !};
         {? ~par[15] || par[1].add("par[3].KW",12,'RAZEM'@,1,,,,,'12,2') ?};
         par[1].s_frame();
         par[11]:=par[12]:=0;
         {? __param.DRUP
         || par[5]:=2
         || par[5]:=1
         ?};
         {? __param.PRAC
         || PAR_WYDR.TITLE+=' dla osoby'@
         || PAR_WYDR.TITLE+=' ze zleceń dla osoby'@
         ?};
         par[6]:=0;
         par[8]:=obj_new(12);
         {! _nx:=1..obj_len(par[8])
         |! par[8][_nx]:=0
         !};
         params_set('P',exec('wybierz_parses','pracownik','PPL').P);
         {? __param.PRAC
         || _upr:=exec('uprawnieniawrap','pkd',
               'Naliczonych składnikach płacowych'@,
               'PPL_PLL_RSKP',
               'PPL_PLL_PSKP'
            );
            __czyP:=(_upr='')
         ?}
      ?};
      prn1:='par[1]';
      vp:=b_rat:=1;
      lmt:=rsep:=par[13]:=0
   ?}
}
{END:
   P.cntx_pop();
   VAR_DEL.delete('par','prn1','vp','lmt','rsep','b_rat','h_rat','__edit','__param','__czyP')
}
{COMMENT}OLD: zlepsprd.rpm{COMMENT-}
{COMMENT}OLD: std_info.rpi{COMMENT-}
{COMMENT}OLD: skid_pw.rpi{COMMENT-}
{COMMENT}OLD: oddzialz.rpi{COMMENT-}
{EXIT: ~par[9]}
{EXIT: ~params_get().P.first()}
{INCLUDE: wsp_pw.rpi}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   K O N F I G U R A C J A   -   N A G Ł Ó W E K,   S T O P K A

{COMMENT-}
{HEADER}
{SUBSTITUTE: 'HEAD'}
{IF: par[5]}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {LINE}
   { {? par[5]=1 || par[5]:=0 ?}; ~~ }
   {<{lmt+1}'Parametry sprawozdania'}
   {<{lmt+1}'Zakres informacji: ze zleceń'@+{? __param.PRAC || ' i etatu(ów).'@ || '.' ?}}
   {<{lmt+1}'Rok kalendarzowy: '+$par[7]}
{FI}
{FONT: 'T8GB'}
{SPACE: 'C'}
{LINE}
{SUBSTITUTE: 'TABHEAD'}
{SUBSTITUTE: 'LINIAF'}
{HEADER-}
{IF: ~par[15]}
   {FOOTER:0}
   {FONT: 'T8G'}
   {SPACE: 'C'}
   {REP: par[1].flbar(lmt)}
   {FOOTER-}
   {SUBSTITUTE: 'FOOT'}
{FI}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   M A K R O D E F I N I C J E

{COMMENT-}
{COMMENT}
══════════════════════════════════════════════════════════════════════════════

   Z A W A R T O Ś Ć   W Y D R U K U

{COMMENT-}
{FONT: 'W10'}{ b_rat:=charleft(); ~~ }
{FONT: 'T8G'}{ b_rat/={? par[15] || 1 || charleft() ?}; ~~ }
{SPACE: 'C'}
{ lmt:=int((charleft()-par[1].width())/2); ~~ }
{ echo('Trwa pobieranie informacji...'@);
  _ZLEC:=params_get().P;
  _ndx:=_ZLEC.index('?');
   {? __czyP
   || _ZLEC.index(_ZLEC.ndx_tmp(,,'OSQL',,));
      _tab:=tab_tmp(1,'PREF','STRING[16]','SQL_REF');
      _PRAC:=exec('dostepne_p','schemat','PPL','P');
      'kolumn w _ZLEC jest sporo, ale tylko SQL jest istotna';
      _loop:=_PRAC.first();
      {!
      |? _loop
      |! {? _ZLEC.find_key(_PRAC.OSOBA)
         || _ZLEC.blank();
            _ZLEC.SQL:=_PRAC.REF;
            {? _ZLEC.add()
            || _tab.PREF:=$_ZLEC.ref();
               _tab.add()
            ?}
         ?};
         _loop:=_PRAC.next()
      !}
   ?};
   _ZLEC.index(_ZLEC.ndx_tmp(,,'SQL',,));
   par[14]:=sql('
       select   OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL,
                R.LP, R.RN,
                CAST(substr(LS.REFERENCE,4,2) as INTEGER_TYPE) as M,
                sum(LS.KW) as KW
       from     P join @LS using(LS.P,P.REFERENCE) join OSOBA using(P.OSOBA,OSOBA.REFERENCE) join R
      where     P.REFERENCE in (select :_a.SQL from :_a order by :_a.SQL) and
                LS.REFERENCE like \'l:_b%\' and
                R.RN<>71
       group by OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL, R.LP, R.RN, CAST(substr(LS.REFERENCE,4,2) as INTEGER_TYPE)
       order by 1,2,3,4,5,6',
       _ZLEC,
       $par[7]+2
    );
    {? __czyP
    || {? _tab.first()
       || {!
          |? {? _ZLEC.seek(_tab.PREF,,1) || _ZLEC.del() ?};
             _tab.next()
          !}
       ?}
    ?};
   _ZLEC.index(_ndx);
~~}
{EXIT: type_of(par[14])<>type_of(SYSLOG) | ~par[14].size()}
{  par[2]:=sql('select   NAZWISKO, PIERWSZE, PESEL, LP, RN, M, sum(KW) KW
               from     :_a
               group by NAZWISKO, PIERWSZE, PESEL, LP, RN, M
               order by 1,2,3,4,5,6',
               par[14]
   ); ~~
}
{EXIT: type_of(par[2])<>type_of(SYSLOG) | ~par[2].size()}
{  echo('Trwa przetwarzanie pobranych danych...'@);
   par[3]:=sql('select NAZWISKO, PIERWSZE, PESEL, LP, RN, sum(KW) KW
               from :_a
               group by NAZWISKO, PIERWSZE, PESEL, LP, RN
               order by 1,2,3,4,5',
               par[2]
   );
   echo(); ~~
}
{EXIT: type_of(par[3])<>type_of(SYSLOG) | ~par[3].size()}
{ par[13]:=0; ~~ }
{FOR: params_get().P}
{DO}
{IF: P.seek(params_get().P.SQL,,1)}
   { par[4]:=0; ~~ }
   {IF: par[13]<>#P.OSOBA}
   { par[13]:=#P.OSOBA; P.OSOBA(); ~~ }
   {FOR: par[3]}{PREFIX: OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL}
   {DO}
   {FIRST}
      {IF: ~par[15]}
         {<{ceil(lmt*b_rat)+3}}{form(par[6]+=(~par[12]),,,'9.')}. {INCLUDE: p_osoba.rpi}
      {FI}
      {IF: {? par[15] || ~par[4] || 1 ?}}
         {REP: par[1].fhbar(lmt)}
      {FI}
      { par[4]:=1; ~~ }
   {FIRST-}
      {IF: ~par[15] & par[12]}
      {<{ceil(lmt*b_rat)+3}}{form(par[6]+=(~par[12]),,,'9.')}. {INCLUDE: p_osoba.rpi}
      {REP: par[1].fhbar(lmt)}
      {FI}
      {  {? lineleft()<2 || par[12]:=1 || par[12]:=0 ?};
         par[2].prefix(OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL, par[3].LP, par[3].RN);
         {! _nx:=1..obj_len(par[8])
         |! {? par[2].find_key(_nx)
            || par[8][_nx]:=par[2].KW
            || par[8][_nx]:=0
            ?}
         !};
         par[1].ini_line();
         ~~
      }
      {REP: par[1].fline(lmt)}
   {OD}
   {IF: par[15]} {REP: par[1].flbar(lmt)} {FI}
   {FI}
{FI}
   {IF: par[4] & ~par[12]}{PAGE}{FI}{ par[12]:=0; ~~ }
{OD}
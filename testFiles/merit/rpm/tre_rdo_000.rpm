:!UTF-8
{TITLE:A. Zestawienie reklamacji}
{PRINTER: exec('set_parw','param_wydr'); exec('prt','param_wydr','h'),,,,,'Q','C',,,,,,1,
   PAR_WYDR.MBOT,PAR_WYDR.MLEFT,PAR_WYDR.MTOP,PAR_WYDR.MRIGHT,0,1,,'enable'}
{BEGIN:
   rdo_000:="
      VAR_DEL.delete('color','prn1','tryb');
      VAR_DEL.delete('lm','ll','vp','lmt','rsep');
      VAR_DEL.delete('n','k','w','s','doklw','PRN','lp','kgr');
      VAR_DEL.delete('od_daty','do_daty','filtr_kh','koszty');
      VAR_DEL.delete('wydr_naz','wydr_edi','wyjscie','X_Xb','X_Xw1')";
   rdo_000();

   color:=prn1:='';
   lm:=ll:=vp:=lmt:=rsep:=0;

   tryb:=rep_export();

   filtr_kh:='N';
   od_daty:=do_daty:=date(0,0,0);
   koszty:=0;
   kgr:='';
   lp:=0;
   doklw:=2; "--dokladnosc wartosc--";

   wydr_naz:='rdo_000';
   wydr_edi:='RDO_000';

   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,wydr_naz);
   {? ~WYDRUKIL.first()
   || WYDRUKIL.blank();
      WYDRUKIL.USERS:=OPERATOR.USER().KOD;
      WYDRUKIL.WYDRUK:=wydr_naz;
      WYDRUKIL.add()
   ?};
   WYDRUKIL.win_edit(wydr_edi);
   wyjscie:=1;
   {? WYDRUKIL.edit()
   ||
      {? WYDRUKIL.put()
      || wyjscie:=0;
         filtr_kh:={? WYDRUKIL.CHKBOX01=1 || 'T' || 'N' ?};
         koszty:={? WYDRUKIL.CHKBOX02=1 || 1 || 0 ?};
         od_daty:=WYDRUKIL.OD_DATY;
         do_daty:=WYDRUKIL.DO_DATY
      ?}
   ?};
   {? wyjscie=0 || wyjscie:=exec('filtr_kh','kontrahent',filtr_kh,'N') ?};
   {? wyjscie=0
   ||
      _ilk:=14+(koszty*1);
      PRN:=obj_new(@.CLASS.PRINT,_ilk); PRN.s_frame();
      k:=obj_new(_ilk); "--szerokosci kolumn--";
      n:=obj_new(_ilk); "--nazwy kolumn--";
      w:=obj_new(_ilk); "--wartosci kolumn--";
      k[1] :=5;    n[1] :='Lp.'@;
      k[2] :=20;   n[2] :='Symbol reklamacji'@;
      k[3] :=10;   n[3] :='Kontrahent'@;
      k[4] :=10;   n[4] :='Ilość dni na reklamację'@;
      k[5] :=20;   n[5] :='Indeks'@;
      k[6] :=30;   n[6] :='Nazwa'@;
      k[7] :=12;   n[7] :='Ilość'@;
      k[8] :=8;    n[8] :='jm'@;
      k[9] :=11;   n[9] :='Data przyjęcia'@;
      k[10]:=11;  n[10] :='Data zakończenia'@;
      k[11]:=0;   n[11] :='Rodzaj reklamacji'@;
      k[12]:=12;  n[12] :='Czas rozpatrzenia (dni)'@;
      k[13]:=11;  n[13] :='Data zamknięcia'@;
      k[14]:=12;  n[14] :='Czas realizacji (dni)'@;
      {? koszty
      || k[15]:=15;    n[15]:='Koszty'
      ?};
      {! _ii.._ilk |! w[_ii] :='' !}
   ?}
}
{END:
   rdo_000(); VAR_DEL.delete('rdo_000')
}
{IF: wyjscie=1}{EXIT}{FI}
{
{? tryb<>1 || PRN.add("w[1]",k[1],n[1],1) ?};
PRN.add("w[2]",k[2],n[2]);
PRN.add("w[3]",k[3],n[3]);
PRN.add("w[4]",k[4],n[4],1);
{? tryb=1 || PRN.add("kgr",8,'Grupa materiałowa'@) ?};
PRN.add("w[5]",k[5],n[5]);
PRN.add("w[6]",k[6],n[6]);
PRN.add("w[7]",k[7],n[7],1);
PRN.add("w[8]",k[8],n[8]);
PRN.add("w[9]",k[9],n[9]);
PRN.add("w[10]",k[10],n[10]);
PRN.add("w[13]",k[13],n[13]);
PRN.add("w[12]",k[12],n[12],1);
PRN.add("w[14]",k[14],n[14],1);
{? koszty
||
   PRN.add("w[15]",k[15],n[15],1)
?};

"--obliczenia--";
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'SYM'    ,'STRING[20]'  ,'Symbol reklamacji',
   'KH'     ,'STRING[10]'  ,'Skrót kontrahenta',
   'DP'     ,'DATE'        ,'Data przyjęcia',
   'DZ'     ,'DATE'        ,'Data zakończenia',
   'DX'     ,'DATE'        ,'Data zamknięcia',
   'KTM'    ,'STRING[50]'  ,'Indeks',
   'N'      ,'STRING[100]' ,'Nazwa',
   'JM'     ,'STRING[10]'  ,'jm',
   'IL'     ,'REAL'        ,'Ilość',
   'ILDNI'  ,'INTEGER'     ,'Ilość dni na reklamację',
   'KOSZT'  ,'REAL'        ,'Koszty',
   'MGR'    ,'STRING[8]'   ,'Grupa materiałowa',
   'R'      ,'STRING[255]' ,'Rodzaj reklamacji',
   'TR'     ,'INTEGER'     ,'Czas realizacji',
   'TREA'   ,'INTEGER'     ,'Czas rozpatrzenia'
   );

OKR.cntx_psh(); REK_N.cntx_psh();
_min:=0;
_max:=0;
_arch:={? REK_N.name()+2='__' || 1 || 0 ?};
{? _arch=0
|| _min:=od_daty~1;
   _max:=do_daty~1;
   {? _min || _min-=1 ?};
   {? _max || _max+=1 ?}
?};
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? OKR.first & (_min=0 | OKR.find_ge(_min)) & X_Xb.first()
||
   KH.cntx_psh();
   M.cntx_psh();
   REK_R.cntx_psh();
   {!
   |?
      {? _arch>0
      || REK_N.use('rekn'+ST.ODDZ+'__')
      || REK_N.use('rekn'+ST.ODDZ+(($OKR.ROK)+2))
      ?};
      REK_N.index('KH');
      {? X_Xb.first()
      ||
         {!
         |?
            REK_N.prefix('Z',X_Xb.KOD);
            {? REK_N.first()
            ||
               {!
               |?
                  {? ((od_daty<=REK_N.DP | od_daty=date(0,0,0)) & (REK_N.DP<=do_daty | do_daty=date(0,0,0)))
                  ||
                     REK_N.M();
                     REK_N.KH();
                     REK_N.REK_R();
                     X_Xw1.blank(1);
                     X_Xw1.SYM:=REK_N.SYM;
                     X_Xw1.KH:=KH.SKR;
                     X_Xw1.DP:=REK_N.DP;
                     X_Xw1.DZ:=REK_N.DZ;
                     X_Xw1.DX:=REK_N.DX;
                     X_Xw1.IL:=REK_N.IL;
                     X_Xw1.ILDNI:=REK_N.ILDNI;
                     X_Xw1.R:=REK_R.KOD+': '+REK_R.OPIS;
                     X_Xw1.KTM:=M.KTM;
                     X_Xw1.N:=M.N;
                     X_Xw1.JM:=M.J().KOD;
                     X_Xw1.MGR:=M.MGR().KOD;
                     X_Xw1.TR:={? X_Xw1.DZ<>date(0,0,0) || X_Xw1.DZ || date() ?}-X_Xw1.DP+1;
                     X_Xw1.TREA:={? X_Xw1.DZ<>date(0,0,0)
                                 || {? X_Xw1.DX<>date(0,0,0) || X_Xw1.DX || date() ?}-X_Xw1.DZ+1
                                 || 0
                                 ?};
                     {? koszty
                     || X_Xw1.KOSZT:=REK_N.KOSZT
                     ?};
                     X_Xw1.add()
                  ?};
                  REK_N.next()
               !}
            ?};
            X_Xb.next()
         !}
      ?};
      OKR.next() & (_max=0 | OKR.ROK<=_max) & _arch=0
   !};
   M.cntx_pop();
   KH.cntx_pop();
   REK_R.cntx_pop()
?};
OKR.cntx_pop(); REK_N.cntx_pop();

PAR_WYDR.TITLE:='ZESTAWIENIE REKLAMACJI DO DOSTAWCÓW';
prn1:='PRN'
;~~}
{IF: X_Xw1.first()=0}{ FUN.info('Brak danych spełniających kryteria.'@);~~}{EXIT}{FI}
{INCLUDE: wsp_pw.rpi}
{COMMENT}   SIGNATURE                                                {COMMENT-}
{IF: tryb<>1}{SUBSTITUTE: 'FOOT'}{FI}
{COMMENT}   REPORT-HEADER                                            {COMMENT-}
{SUBSTITUTE: 'HEAD'}
{FONT: 'T8G'}{SPACE: 'C'}
{ lmt:=int((charleft()-PRN.width())/2); ~~}
{<{lmt+1}}{'Parametry wydruku:'}
{<{lmt+1}}{'Oddział '+(ODDZ.index('KOD2'); ODDZ.prefix(ST.ODDZ,ST.ODDZ); {? ODDZ.first() || ODDZ.NAZ ?})}
{IF: filtr_kh='T'}{<{lmt+1}}{'Z wyborem kontrahentów'}{FI}
{<{lmt+1+20}}{'Od:'}{<{lmt+1+40}'Do:'}
{<{lmt+1}}{'Data przyjęcia'}{<{lmt+1+20}od_daty}{<{lmt+1+40}do_daty}
{FONT: 'T8GB'}
{SUBSTITUTE: 'TABHEAD'}{FONT: 'T8G'}{ vp:=1; ~~}
{COMMENT}   HEADER                                                   {COMMENT-}
{HEADER}
   {SUBSTITUTE: 'HEAD'}
   {FONT: 'T8GB'}{SPACE: 'C'}
   {SUBSTITUTE: 'TABHEAD'}
{HEADER-}
{COMMENT}   FOOTER                                                   {COMMENT-}
{FOOTER: 0}
   {SUBSTITUTE: 'LINIAF'}
   {LINE: 1}
{FOOTER-}
{COMMENT}   pozycje wydruku                                          {COMMENT-}
{FONT: 'T8G'}{SPACE: 'C'}
{ rsep:=PAR_WYDR.RSEP; lp:=0;~~}
{FOR: X_Xw1}
{INDEX: X_Xw1.ndx_tmp(,,'DP',,)}
{DO}
   {
   w[1] :=form(lp+=1,,,'99');
   w[2] :=form(X_Xw1.SYM);
   w[3] :=form(X_Xw1.KH);
   w[4] :=form(X_Xw1.ILDNI);
   w[5] :=form(X_Xw1.KTM);
   w[6] :=form(X_Xw1.N);
   w[7] :=form(X_Xw1.IL,,doklw,'.,');
   w[8] :=form(X_Xw1.JM);
   w[9] :=form(X_Xw1.DP);
   w[10]:={? X_Xw1.DZ<>date(0,0,0) || form(X_Xw1.DZ) || '' ?};
   w[11]:=form(X_Xw1.R);
   w[12]:=form(X_Xw1.TR);
   w[13]:={? X_Xw1.DX <>date(0,0,0) || form(X_Xw1.DX) || '' ?};
   w[14]:={? X_Xw1.TREA<>0 || form(X_Xw1.TREA) || '' ?};
   kgr  :=form(X_Xw1.MGR);
   {? koszty
   ||
      w[15]:=form(X_Xw1.KOSZT,,doklw,'.,')
   ?}
   ;~~}
   {SUBSTITUTE: 'LINIA0'}
{OD}

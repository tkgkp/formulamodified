:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zes_zimi.fml
:: Utworzony: 22.06.2016
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PPL_ZES_ZIMI - Załącznik IMiR
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [EJ] [12.30]
:: OPIS: Formuła generująca dokumenty IMIR z formatu xlm do pdf oraz wczytująca pliki do bazy
::   WE:
::   WY:
:: ~OST: INFILECHOOSER
::  OLD: \imir_ini/gen_zal.fml
::  OLD: \imir_gen/gen_zal.fml
::  OLD: \imir_zal/gen_zal.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
{? exec('interm','#system')
|| FUN.emsg(exec('interm_nacc_msg','#system'));
   return(~~)
?};
{? FUN.ask('Funkcja dokonuje konwersji plików utworzonych w programie "Płatnik" na dokumenty w formacie pdf'@+'\n'+
      'Następnie zapisuje je w kartotece załączników.'@+'\n'+'Czy kontynuować?'@)
|| OSOBA.cntx_psh();
   OSOBA.clear();
   _tmp:=tab_tmp(1,
       'FILE','STRING[255]','Nazwa pliku',
       'FPATH','STRING[255]','Ścieżka do pliku',
       'DESC','STRING[80]','Przyczyna',
       'TYPE','STRING[2]', 'Rodzaj raportu',
       'LNAME','STRING[30]','Nazwisko aktualne',
       'FNAME','STRING[20]','Pierwsze imię',
       'IDT','STRING[1]','Symbol identyfikatora',
       'IDN','STRING[8]','Nazwa identyfikatora',
       'ID','STRING[20]','Identyfikator',
       'DATE','STRING[10]','Data/rok',
       'D','DATE','Data');

   _set:=obj_new(6);
   _set[1]:="_a.TYPE:=_b";
   _set[2]:="_a.LNAME:=_b";
   _set[3]:="_a.FNAME:=_b";
   _set[4]:="_a.IDT:=_b;
   _a.IDN:={? -_b='p' || 'pesel' |? -_b='n' || 'nip' |? -_b='2' || 'paszport' || 'nieznany' ?}";
   _set[5]:="_a.ID:=_b";
   _set[6]:="_a.DATE:=_b";

   _name:='lokalny katalog deklaracji IMiR';
   _par:=obj_new('tab','red');
   _par.tab:=tab_tmp(,'FOLDER','STRING[100]','Folder plików',
                      'PODPIS','INTEGER','Podpis w plikach',
                      'FIT','INTEGER','Czy dopasować podpis do ramki'
                    );
   _par.tab.FOLDER:=exec('get','#profile',,OPERATOR.USER().KOD,_name);
   _par.tab.PODPIS:=0;
   _par.tab.FIT:=0;
   _par.red:=_par.tab.mk_edit('Załączniki IMiR',0);
   _par.tab.win_esep(_par.red,'Parametry wydruku');
   _par.tab.win_efld(_par.red,,'FOLDER',,,50,,0,'Folder plików');
   _par.tab.fld_ebtn(_par.red,'icon=xwin16.png:90',
           "cur_tab(,1).FOLDER:=exec('filechooser','#file','Lokalizacja plików IMiR',,,,,'DIRECTORIES_ONLY',1)");
   _par.tab.efld_opt(_par.red,'mark=1',,'FOLDER');
   _par.tab.win_efld(_par.red,,'PODPIS',,,,,0,'Podpis w plikach IMiR',,,'check-box',,"1","0");
   _par.tab.fld_fml('PODPIS','AFTER_EDIT',"{? fld()=0 || cur_tab.FIT:=0 ?};
                                           cur_tab().efld_opt(cur_win(),'enable=%1'[$fld()],,'FIT')");
   _par.tab.win_efld(_par.red,,'FIT',,,,,0,'Dopasować podpis do ramki?',,,'check-box',,"1","0");
   _par.tab.efld_opt(_par.red,'enable=0',,'FIT');
   exec('ok_esc','#window',_par.tab,_par.red,,,,,,,exec('text_red_ok','#window'));
   _par.tab.win_edit(_par.red);
   {? _par.tab.edit("__CHK.record(cur_tab(,1),,'FOLDER')")
   || _dir:=STR.w952maz(_par.tab.FOLDER);
      {? _dir='' || OSOBA.cntx_pop(); return(0) || exec('set','#profile',,OPERATOR.USER().KOD,_name,_dir) ?};
::    zmiana związana z wykorzystaniem MBJAR.JavaExec
::    plik jar kończy się poleceniem System.exit(0), a to powoduje zwrot komunikatu równy 0, przez co nie jest podpinany
::    załącznik
      {? exec('xmltopdf','!ppl_zes_zimi',_dir,_par.tab.PODPIS,_par.tab.FIT)=0
      || _var:='@'+_dir+'\\wynik.txt';
         _var2:='@'+_dir+'\\pdf\\';
         _lfile:=fopen(_var,'Ur',0);
         {? _lfile
         || {!
            |? _rec:=fread(_lfile);
               {? (+_rec)>=1 | _rec+1=';' || _rec:=_rec-1 ?};
               {? _rec<>''
               || _name:=_var2+_rec;
                  _tmp.blank();
                  _tmp.FILE:=_rec;
                  _tmp.FPATH:=_name;
                  STR.split(_rec,'_');
                  {! _ii:=1..obj_len(_set) |! _set[_ii](_tmp,STR.get_word()) !};
                  _tmp.DESC:=
                     {? ~fexists(_name,0) || 'Brak pliku załącznika.'@
                     |? ~(-_tmp.TYPE='im' | -_tmp.TYPE='ir') || 'Błędna nazwa pliku.'@
                     |? ~('pn2'*-_tmp.IDT) || 'Błędny typ identyfikatora.'@
                     || ''
                     ?};
                  {? _tmp.DESC=''
                  || _tmp.D:=
                         {? -_tmp.TYPE='ir'
                         || date(#_tmp.DATE,12,0)
                         || STR.split(_tmp.DATE,'-');
                            date(#STR.get_word(),#STR.get_word(),0)
                         ?}
                  ?};
                  _tmp.add()
               ?}
            !};
            fclose(_lfile)
         ?};
         _size:=_tmp.size();
         {? _tmp.first()
         || _key:=obj_new(3);
            _key[1]:=OSOBA.ndx_tmp(,1,'PESEL',,,'PESEL',,);
            _key[2]:=OSOBA.ndx_tmp(,1,'NIP',,,'NIP',,);
            _key[3]:=OSOBA.ndx_tmp(,1,'PASZPORT',,,'PASZPORT',,);
            PROGRESS.set(_size,'Trwa wczytywanie danych.'@);
            {!
            |? PROGRESS.next();
               {? _tmp.DESC=''
               || {? exec('imir_prac','!ppl_zes_zimi',_tmp,_key['pn2'*-_tmp.IDT]) || _tmp.next() || _tmp.del ?}
               || _tmp.next()
               ?}
            !};
            PROGRESS.close();
            {! _ii:=1..obj_len(_key) |! OSOBA.ndx_drop(_key[_ii]) !};
            FUN.info('Zakończono wczytywanie informacji do kartoteki załączników.'@+'\n'+
               'Pobrano %1 z %2 plików.'@[$(_size-_tmp.size()),$_size] );
            {? _tmp.first()
            || _tmp.index(_tmp.ndx_tmp('Osoba',,'LNAME',,,'FNAME',,,'ID',,));
               _win:=_tmp.mk_sel('Pliki pominięte podczas importu'@,,0,'raport_imir');
               _tmp.win_fld(_win,,'LNAME',,,20,,,'Nazwisko aktualne'@);
               _tmp.win_fld(_win,,'FNAME',,,15,,,'Pierwsze imię'@);
               _tmp.win_fld(_win,,'ID',,,15,,,'Identyfikator'@);
               _tmp.win_fld(_win,,'FILE',,,40,,,'Nazwa pliku'@);
               _tmp.win_fld(_win,,'DESC',,,60,,,'Przyczyna'@);
               _tmp.win_act(_win,,'Szukaj');
               _tmp.win_act(_win,,'Kolejność');
               _tmp.win_sel(_win);
               _tmp.select();
               _loop:=_tmp.first();
               {! |? _loop |! ferase(_tmp.FPATH); _loop:=_tmp.del() !}
            ?}
         || FUN.info('Nie znaleziono plików do wczytania do kartoteki załączników.'@)
         ?};
         {? fcopy(_var,'wynik_'+$(date()$0)+'_'+time()$0+'.txt',0,1,1) || ferase(_var) ?}
      ?}
   ?};
   OSOBA.cntx_pop()
?};
~~


\xmltopdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RG [12.30]
:: OPIS: Funkcja uruchamia pliki *.jar konwertujące dokumenty IMIR z formatu xml do pdf
::   WE: _a - scieżka do katalogu, w którym znajdują się pliki IMIR
::       _b - czy umieszczać podpis na plikach PDF
::       _c - czy skalować plik z podpisem tak aby się zmieścił w ramce?
::   WY: 0 - poprawnie wykonana funkcja
:: ~OST: INFMKDIR
::  OLD: \xmltopdf/gen_zal.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<0 || return(1) ?};
{? var_press('_b')<0 || _b:=0 ?};
{? _b
|| _file:=exec('ask4File','podpis','png',,,'imir');
:: jeżeli nie wybrano pliku to anulujemy umieszczanie pliku z podpisem
   {? _file='' || _b:=0 ?}
?};
_fit:={? var_pres('_c')=type_of(0) & _c>0 || 1 || 0 ?};
exec('MBJAR','#object');
_katalog:=fmkdir('@!Tmp','imir');
{? _katalog='' || return(1) ?};
_katalog:=_katalog+'\\';
_handle:=fopen(_katalog+'todo.txt','Uw',0);
_fcopy:="fcopy(_a,_b+_a,1,0,1)";

{? _handle
|| fwrite(_handle,'path:'+_a);
   fwrite(_handle,'Podpis:'+$_b);
   fwrite(_handle,'Fit2Frame:'+$_fit);
   fclose(_handle)
?};
{? ~_fcopy('commons-io-2.4.jar',_katalog) |
   ~_fcopy('core-renderer.jar',_katalog) |
   ~_fcopy('itext-paulo-155_pl.jar',_katalog) |
   ~_fcopy('jsoup-1.7.3.jar',_katalog) |
   ~_fcopy('pdfbox-app-1.8.6.jar',_katalog) |
   ~_fcopy('xml-apis-xerces-2.9.1.jar',_katalog) |
   ~_fcopy('imir2pdf.jar',_katalog) |
   ~{? _b
    || {? _file<>''
       || fcopy(_file,_katalog+'podpis.png',0,0,1)
       ?}
    || 1
    ?}
|| FUN.emsg('Wystąpił błąd podczas przygotowania środowiska.'@);
   return(1)
?};

_wynik:=MBJAR.JavaExec(1-_katalog,1,'-jar','imir2pdf.jar');
ferase(_katalog+'commons-io-2.4.jar',0);
ferase(_katalog+'core-renderer.jar',0);
ferase(_katalog+'itext-paulo-155_pl.jar',0);
ferase(_katalog+'jsoup-1.7.3.jar',0);
ferase(_katalog+'pdfbox-app-1.8.6.jar',0);
ferase(_katalog+'xml-apis-xerces-2.9.1.jar',0);
ferase(_katalog+'imir2pdf.jar',0);
{? _b || ferase(_katalog+'podpis.png',0) ?};
_wynik


\imir_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [EJ] [12.30]
:: OPIS: Formuła identyfikująca osoby wg nazwy dokumentu IMIR przekonwertowanego z xml do pdf
::   WE: _a - alias do tabeli z nazwami plików pdf
::       _b - akronim indeksu wg którego bądzie wyszukiwana osoba
::   WY: 1/0 - wystapił błąd/dane zostały poprawnie przetworzone
::  OLD: \imir_prac/gen_zal.fml
::----------------------------------------------------------------------------------------------------------------------
OSOBA.index(_b);
_forma_z:=obj_new(5);
_forma_z[1]:='P';
_forma_z[2]:='Z';
_forma_z[3]:='K';
_forma_z[4]:='T';
_forma_z[5]:='R';
{? OSOBA.find_key(~_a.ID,~_a.ID)
|| {? (-(OSOBA.NAZWISKO)=-_a.LNAME) & (-(OSOBA.PIERWSZE)=-_a.FNAME)
   || _exists:=0;
      P.cntx_psh();
      P.index('PRACOSOB');
      {! _licz:=1..obj_len(_forma_z)
      |? ~_exists
      |! P.prefix(exec('ref_firma','ustawienia'),_forma_z[_licz],OSOBA.ref);
         {? P.find_le(_a.D)
         || {!
            |? {? P.DZ=date(0,0,0) | {? -_a.TYPE='im' || P.DZ>=date(_a.D~1,_a.D~2,1) || P.DZ~1>=_a.D~1 ?}
               || exec('add_zal','zal',-_a.TYPE,1,_a.FPATH,_a.D);
                  _exists:=1
               ?};
               ~_exists & P.prev()
            !}
         ?}
      !};
      P.cntx_pop();
      {? ~_exists || _a.DESC:='Brak osoby w kartotece współpracowników.'@ ?}
   || _a.DESC:='Osoba o danym nazwisku i imieniu nie występuje w kartotece osobowej.'@
   ?}
|| _a.DESC:='Osoba o danym identyfikatorze nie występuje w kartotece osobowej.'@
?};
{? _a.DESC<>'' || _a.put() ?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 1f8a2e537589a93072000f142814138026bec36818850f230a41affd825ae02529c91cf8498e72601f90a322ef2a46c6cae3df6191955917ca385bcc5c0ac9e5d9a6a802d5e7658a893d4bd18c61358b0c4abf98ada82e4025c81e7663bc609195cbeec708b9e41527b023883ff7406cf6eb04539dc3f19eb43f20ac5674a059

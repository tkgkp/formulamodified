:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fst_ewz_zpla.fml
:: Utworzony: 18.02.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWZ_ZPLA - Przegląd - plany amortyzacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przegląd - plany amortyzacji - główna formuła czynności FST_EWZ_ZPLA.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS

SRD.selPlan()


\srsp_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla definicję wariantu planu amortyzacji
::----------------------------------------------------------------------------------------------------------------------
SRSP.win_edit('RED');
SRSP.display()


\ae_srsp_okro_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła po redakcji pola SRSP.OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? SRSP.OKRO_F=null || SRSP.ROK_F:=null
|? SRSP.OKRO_F<>null || SRSP.ROK_F:=SRSP.OKRO_F().ROK
?};
1


\srsp_plan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla plan amortyzacji
::----------------------------------------------------------------------------------------------------------------------
SRD.maski(SRSP.MASKA);
exec('create_win','!fst_ewz_zpla');
EDIT_ES.SRSR:=null;
SRSR.index('KIND');
SRSR.prefix('T');
SRSR.select()


\create_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła tworzy okno przeglądania planów amortyzacji
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__WPLAN','__GPLAN','__SPLAN','__WSRSP');
_mob:='mobile_visible=1';
_head:='mobile_visible=1, mobile_header=1';
_title:='Środki'@;
__WPLAN:=SRSR.mk_sel(_title,,,'_wplan',5,5,20,,'U');
SRSR.win_fld(__WPLAN,,'GR','GR',,5,,,'Grupa'@,,,,,,,,_mob);
SRSR.win_fld(__WPLAN,,'NRI',,,15,,,'Nr inwentarzowy'@,,,,,,,,_head);
SRSR.win_fld(__WPLAN,,'NST',,,12,,,'Nazwa'@,,,,,,,,_mob);
SRSR.win_fld(__WPLAN,,'DE',,,10,,,'Od'@,,'W eksploatacji od'@);
SRSR.win_fld(__WPLAN,,'ODD','OD',,10,,,'J. księgowa'@);
SRSR.win_fld(__WPLAN,,'JORG','SYMBOL',,9,,,'J. org.'@,,,,,,,,_mob);
SRSR.win_fld(__WPLAN,,'PLAN',,,5,,,'Plan?'@,,,2,,"'T'","'N'");

::SRSR.win_act(__WPLAN,1,'Menu','Planowane środki'@,,,,"",,,,,'P');
SRSR.win_act(__WPLAN,,'Menu','Planowane środki'@,,,,"",,,,,'P');
task_attach('FST_EWZ_DPOB');
::SRSR.win_act(__WPLAN,1,'Formuła','Dołącz środek'@@,'Planowane środki'@,,,"SRD.addAssetPlan()",,,,,'D');
SRSR.win_act(__WPLAN,,'Formuła','Dołącz środek'@@,'Planowane środki'@,,,"SRD.addAssetPlan()",,,,,'D');
task_attach('FST_EWZ_DPOB');
SRSR.win_act(__WPLAN,,'Formuła','Dołącz zes&taw'@@,'Planowane środki'@,,,"SRD.addAssetPlan('G')",,,,,'T');
task_attach('FST_EWZ_DPOB');
::SRSR.win_act(__WPLAN,1,'Formuła','Dołącz zes&taw'@,'Planowane środki'@,,,"SRD.addAssetPlan('G')",,,,,'T');
SRSR.win_act(__WPLAN,,'Formuła','Dołącz &element'@@,'Planowane środki'@,,,"SRD.addAssetPlan('E')",,,,,'E');
task_attach('FST_EWZ_DPOB');
::SRSR.win_act(__WPLAN,1,'Formuła','Dołącz &element'@,'Planowane środki'@,,,"SRD.addAssetPlan('E')",,,,,'E');
_belka:='--X';
SRSR.win_act(__WPLAN,,'Formuła',_belka,'Planowane środki'@,,,"");
task_attach('FST_EWZ_DPOB');
SRSR.win_act(__WPLAN,,'Formuła','Popraw środek'@@,'Planowane środki'@,,,"SRD.editAssetPlan()",,,,,'P');
task_attach('FST_EWZ_DPOB');
SRSR.win_act(__WPLAN,,'Formuła','Usuń środek'@@,'Planowane środki'@,,,"SRD.delAssetPlan()",,,,,'U');
task_attach('FST_EWZ_DPOB');

SRSR.win_act(__WPLAN,,'Formuła','Dokumen&ty'@@,,,,"exec('dok_sel','!fst_ewz_zpla')",,,,,'T');
task_attach('FST_EWZ_ZPLA');
task_attach('FST_EWZ_DPOB');
SRSR.win_act(__WPLAN,1,'Formuła','Generuj'@@,,,,"SRD.generuj()",1,,,,'G');
task_attach('FST_EWZ_DPOB');
:: dla niezaaceptowanych planów można generować dane planu od nowa
{? SRSP.AKCEPT='N'
|| SRSR.win_act(__WPLAN,,'Formuła','Generuj'@@,,,,"SRD.generuj()",,,,,'G');
   task_attach('FST_EWZ_DPOB');
   SRSR.win_act(__WPLAN,,'Formuła','&Obliczanie planu'@@,,,,"SRD.liczPlan()",1,,,,'O');
   task_attach('FST_EWZ_DPOB')
?};

SRSR.win_act(__WPLAN,,'Formuła','Elementy składowe'@@,,,,"exec('elementy','!fst_ewz_zpla')",,,,,'E');
task_attach('FST_EWZ_ZPLA');
task_attach('FST_EWZ_DPOB');
SRSR.win_act(__WPLAN,,'Wyświetl',,,,,"SRD.showEarlyAsset()");
_form:="{? SRSR.GRP='T' || 'xwin16.png:74' |? SRSR.GRP='E' || 'xwin16.png:76' || '' ?}";
SRSR.win_fml(__WPLAN,,'NST',,'ICON_BEFORE',_form);
SRSR.win_act(__WPLAN,,'Formuła','Podsu&mowanie dla środka'@@,,,,"exec('podsumowanie','!fst_ewz_zpla')",1,,,,'M');
task_attach('FST_EWZ_ZPLA');
task_attach('FST_EWZ_DPOB');
task_attach('FST_EWZ_ZPWD');
SRSR.win_act(__WPLAN,,'Menu','Raport&y'@,,,,"",,,,,'Y');
task_attach('FST_EWZ_ZPWD');
SRSR.win_act(__WPLAN,,'Formuła','Zestawienia z planów amortyzacji'@@,'Raport&y'@,,,"exec('np_run','#b__box','FST_EWZ_ZPWD')",,,,,'Z');
task_attach('FST_EWZ_ZPWD');
SRSR.win_act(__WPLAN,,'Formuła','Zapytania S&QL'@@,'Raport&y'@,,,"exec('main','!fst_ewz_zsql',,1)",,,,,'Q');
task_attach('FST_EWZ_ZSQL');
SRSR.win_act(__WPLAN,,'Formuła','Legenda'@@,,,,"exec('legenda','color','SRSR#02#')",,,,,'L');
SRSR.win_act(__WPLAN,,'Rekord',,,,"exec('rekprzed','color','SRSR#02')");

_title:='Planowana amortyzacja'@;
__SPLAN:=SRST.mk_sel(_title,,,'_splan',5,5,20,,'U');
SRST.win_fld(__SPLAN,,'ROK',,,4,,,'Rok'@);
SRST.win_fld(__SPLAN,,'OKRES',,,5,,,'Okres'@);
SRST.win_fld(__SPLAN,,'WARP',,,9,2,,'Wartość pod.'@);
SRST.win_fld(__SPLAN,,'UMOP',,,9,2,,'Umorzenie pod.'@);
SRST.win_fld(__SPLAN,,'AMOP',,,9,2,,'Amortyzacja pod.'@);
SRST.win_fld(__SPLAN,,'WARF',,,9,2,,'Wartość bil.'@);
SRST.win_fld(__SPLAN,,'UMOF',,,9,2,,'Umorzenie bil.'@);
SRST.win_fld(__SPLAN,,'AMOF',,,9,2,,'Amortyzacja bil.'@);
SRST.win_act(__SPLAN,,'Wyświetl',,,,,"SRD.showAsset()");
SRST.win_act(__SPLAN,,'Formuła','Elementy składowe'@@,,,,"exec('elementy','!fst_ewz_zpla')",,,,,'E');
task_attach('FST_EWZ_ZPLA');
task_attach('FST_EWZ_DPOB');
SRST.win_act(__SPLAN,,'Formuła','Tabela z&użycia'@@,,,,"exec('srsr_tab_zuz','!fst_ewi_dotz','P')",,,,,'U');
task_attach('FST_EWZ_DPOB');
SRST.win_act(__SPLAN,,'Formuła','Podsumowanie dla środka'@@,,,,"exec('podsumowanie','!fst_ewz_zpla')",1,,,,'P');
task_attach('FST_EWZ_ZPLA');
task_attach('FST_EWZ_DPOB');
task_attach('FST_EWZ_ZPWD');
SRST.win_act(__SPLAN,,'Menu','Raport&y'@,,,,"",,,,,'Y');
task_attach('FST_EWZ_ZPWD');
SRST.win_act(__SPLAN,,'Formuła','Zestawienia z planów amortyzacji'@@,'Raport&y'@,,,"exec('np_run','#b__box','FST_EWZ_ZPWD')",,,,,'Z');
task_attach('FST_EWZ_ZPWD');
SRST.win_act(__SPLAN,,'Formuła','Zapytania S&QL'@@,'Raport&y'@,,,"exec('main','!fst_ewz_zsql',,1)",,,,,'Q');
task_attach('FST_EWZ_ZSQL');

__WSRSP:=SRSP.mk_edit('Plan amortyzacji'@,0,'_srsp');
SRSP.win_efld(__WSRSP,SRSP,'NAZWA',,,30,,1,'Nazwa planu'@);
SRSP.win_ecol(__WSRSP);
SRSP.win_efld(__WSRSP,SRSP,'DATA','',,10,,1,'Wprowadzono'@);
SRSP.win_ecol(__WSRSP);
SRSP.win_efld(__WSRSP,EDIT_ES,'OKRNAZ','',,15,,1,'Dane z okresu'@);
SRSP.win_ecol(__WSRSP);
SRSP.win_efld(__WSRSP,SRSP,'OD','',,10,,1,'Od roku'@);
SRSP.win_ecol(__WSRSP);
SRSP.win_efld(__WSRSP,SRSP,'DO','',,10,,1,'Do'@);
SRSP.win_ecol(__WSRSP);
SRSP.win_efld(__WSRSP,EDIT_ES,'R_PLAN','',,10,,1,'Środki'@);

_title:='Plan amortyzacji'@;
_form:="EDIT_ES.OKRNAZ:=SRSP.OKRO_F().NAZ+' '+SRSP.OKRO_F().ROK().NAZ;
        {? SRSP.R='W' || EDIT_ES.R_PLAN:='Wszystkie'
        |? SRSP.R='T' || EDIT_ES.R_PLAN:='Trwałe'
        |? SRSP.R='N' || EDIT_ES.R_PLAN:='Niskocenne'
        ?};
        grp_edisp(SRSP,__WSRSP)
       ";
__GPLAN:=SRSR.grp_make(_title,"",'_grpplan',5,5,,"");
SRSR.grp_edit(__GPLAN,SRSP,__WSRSP,,"","","",,'maximized');
SRSR.grp_splt(__GPLAN,,'horizontal','srodki');
SRSR.grp_sel(__GPLAN,,__WPLAN,,"exec('refresh','!fst_ewz_zpla')",,,,_form,"",,,'maximized_with_title','__wplan',1);
SRSR.grp_splt(__GPLAN,'srodki','vertical','amort');
SRSR.grp_sel(__GPLAN,SRST,__SPLAN,,"",,,,"","",,,'maximized_with_title');
SRSR.win_sel(__GPLAN)


\refresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na odśwież w grupowym oknie wertowania środków i planów
::----------------------------------------------------------------------------------------------------------------------
SRST.index('PODAT');
SRST.prefix(SRSR.ref());
SRST.first();
grp_disp(SRST,__SPLAN)


\elementy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla informacje o elementach składowych środka w okresie generowania danych planu
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.GRP<>'T'
|| FUN.emsg('Bieżący środek nie jest zestawem. Funkcja niedostępna.'@);
   return()
?};

EDIT_ES.SRSR:=SRSR.ref();

VAR_DEL.delete('__EPLAN');
__EPLAN:=SRSR.mk_sel('Elementy składowe'@,,,'_eplan',5,5,20,,'U');
SRSR.win_fld(__EPLAN,,'NRI',,,15,,,'Nr inwentarzowy'@);
SRSR.win_fld(__EPLAN,,'NST',,,20,,,'Nazwa'@);
SRSR.win_fld(__EPLAN,,'DE',,,10,,,'W eksploatacji'@);
SRSR.win_fld(__EPLAN,,'ROK',,,6,,,'Od roku'@);
SRSR.win_fld(__EPLAN,,'OKRES',,,10,,,'Od okresu'@);
SRSR.win_fld(__EPLAN,,'WARP',,,10,2,,'Wartość podatkowa'@);
SRSR.win_fld(__EPLAN,,'WARF',,,10,2,,'Wartość bilansowa'@);
SRSR.win_fld(__EPLAN,,'PLAN',,,5,,,'Plan?'@,,'Element zaplanowany do zakupienia'@,2,,"'T'","'N'");
SRSR.win_act(__EPLAN,,'Formuła','Dołącz'@@,,,,"SRD.addAssetPlan('E')",1,,,,'D');
SRSR.win_act(__EPLAN,1,'Formuła','Dołącz'@@,,,,"SRD.addAssetPlan('E')",1,,,,'D');
SRSR.win_act(__EPLAN,,'Formuła','Popraw'@@,,,,"SRD.editAssetPlan()",,,,,'P');
SRSR.win_act(__EPLAN,,'Formuła','Usuń'@@,,,,"SRD.delAssetPlan()",,,,,'U');
SRSR.win_act(__EPLAN,,'Wyświetl',,,,,"SRD.showEarlyAsset()");

SRSR.win_act(__EPLAN,,'Formuła','Tabela &zużycia'@@,,,,"exec('srsr_tab_zuz','!fst_ewi_dotz','P')",,,,,'Z');
SRSR.win_act(__EPLAN,,'Formuła','Dokumen&ty'@@,,,,"exec('dok_sel','!fst_ewz_zpla')",,,,,'T');
SRSR.win_act(__EPLAN,,'Formuła','&Historia zmian wartości'@@,,,,"exec('srsr_tab_hist','fst','P')",,,,,'H');

SRSR.win_btn(__EPLAN,'text=%1,btn_label_align=center,panel=right,align=begin'['Dołącz'@],'menu:D',,,,,,'');
SRSR.win_btn(__EPLAN,'text=%1,btn_label_align=center,panel=right,align=begin'['Popraw'@],'menu:P',,,,,,'noempty');
SRSR.win_btn(__EPLAN,'text=%1,btn_label_align=center,panel=right,align=begin'['Usuń'@],'menu:U',,,,,,'noempty');

SRSR.win_sel(__EPLAN);
SRSR.cntx_psh();
SRSR.index('TREE');
SRSR.prefix(SRSR.ref());
SRSR.select();
SRSR.cntx_pop();
VAR_DEL.delete('__EPLAN')


\podsumowanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla podsumowanie amortyzacji dla środka
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__RAZEMS','__idx_ra');
{? FINFO.TOR_D='T'
|| _sql:='select TO_STRING(SRST.ROK) as ROKP, sum(AMOP) as SAMOP, sum(AMOF) as SAMOF, sum(AMOD) as SAMOD'
        +' from SRST where SRST.SRSR=:_a group by SRST.ROK'
|| _sql:='select TO_STRING(SRST.ROK) as ROKP, sum(AMOP) as SAMOP, sum(AMOF) as SAMOF'
        +' from SRST where SRST.SRSR=:_a group by SRST.ROK'
?};
__RAZEMS:=sql(_sql,SRSR.ref());
{? __RAZEMS.first()
|| _samop:=_samof:=0;
   {? FINFO.TOR_D='T' || _samod:=0 ?};
   __RAZEMS.cntx_psh();
   {! |?
      _samop+=__RAZEMS.SAMOP;
      _samof+=__RAZEMS.SAMOF;
      {? FINFO.TOR_D='T' || _samod+=__RAZEMS.SAMOD ?};
      __RAZEMS.next()
   !};
   __RAZEMS.cntx_pop();
   __RAZEMS.ROKP:='Razem';
   __RAZEMS.SAMOP:=_samop;
   __RAZEMS.SAMOF:=_samof;
   {? FINFO.TOR_D='T' || __RAZEMS.SAMOD:=_samod ?};
   __RAZEMS.add();
   __idx_ra:=__RAZEMS.ndx_tmp(,,'ROKP',,0);
   __RAZEMS.index(__idx_ra);
   __RAZEMS.find_key($SSTALE.AO().RES)
?};
_winr:=__RAZEMS.mk_sel('Podsumowanie dla środka: %1'@[SRSR.NRI],,,'__razems_win_p',,,10);
__RAZEMS.win_sel(_winr);
__RAZEMS.win_fld(_winr,__RAZEMS,'ROKP',,,66,,,'Rok podatkowy'@,,'Rok podatkowy'@);
__RAZEMS.win_fld(_winr,__RAZEMS,'SAMOP',,,16,2,,'Amortyzacja pod.'@,,'Amortyzacja podatkowa'@);
__RAZEMS.win_fld(_winr,__RAZEMS,'SAMOF',,,16,2,,'Amortyzacja bil.'@,,'Amortyzacja bilansowa'@);
{? FINFO.TOR_D='T'
|| __RAZEMS.win_fld(_winr,__RAZEMS,'SAMOD',,,16,2,,'Amortyzacja dod.'@,,'Amortyzacja dodatkowa'@)
?};
__RAZEMS.win_act(_winr,,'Rekord',,,,"{? __RAZEMS.ROKP*'Razem' || 1 || '' ?}");
__RAZEMS.select();
VAR_DEL.delete('__RAZEMS','__idx_ra')


\b_compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed porównywaniem wersji planów amortyzacji
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__PCOMP');
{? SRSP.sel_size()<>2
|| FUN.emsg('Należy zaznaczyć 2 wersje planu amortyzacji do porównania.'@);
   0
|| __PCOMP:=SRSP.sel_aget();
   SRSP.cntx_psh();
   SRSP.prefix();
   __PCOMP.prefix();
   __PCOMP.first();
   _brak:=0;
   {? SRSP.seek(__PCOMP.REF,SRSP.name())
   || _gen1:=SRSP.GEN;
      SRSR.cntx_psh();
      SRSR.use('srsr'+SRSP.MASKA+REF.FIRMA().SYMBOL);
      SRSR.index('PLAN');
      SRSR.prefix('T','N');
      {? SRSR.first() || _obl1:='N' || _obl1:='T' ?};
      SRSR.cntx_pop();
      _rodz1:=SRSP.R
   || _brak:=1
   ?};
   __PCOMP.last();
   {? SRSP.seek(__PCOMP.REF,SRSP.name())
   || _gen2:=SRSP.GEN;
      SRSR.cntx_psh();
      SRSR.use('srsr'+SRSP.MASKA+REF.FIRMA().SYMBOL);
      SRSR.index('PLAN');
      SRSR.prefix('T','N');
      {? SRSR.first() || _obl2:='N' || _obl2:='T' ?};
      SRSR.cntx_pop();
      _rodz2:=SRSP.R
   || _brak:=1
   ?};
   SRSP.cntx_pop();
   {? _brak
   || FUN.emsg('Jeden z zaznaczonych planów jest niedostępny, prawdopodobnie został usunięty '
               'przez innego użytkownika.'@);
      return(0)
   ?};
   {? _gen1<>'T' | _gen2<>'T' | _obl1<>'T' | _obl2<>'T'
   || FUN.emsg('Porównywanie dostępne jest tylko dla planów, które zostały wygenerowane i obliczone\n'
               '(Posiadają dane i naliczoną amortyzację).'@);
      return(0)
   ?};
   {? _rodz1<>'W' & _rodz2<>'W' & _rodz1<>_rodz2
   || FUN.emsg('Oba plany muszą się pokrywać (choćby częściowo) w zakresie rodzaju uwzględnianych środków.\n'
               'Porównywanie planu dla środków niskocennych z planem dla środków trwałych nie jest możliwe.'@);
      return(0)
   ?};
   1
?}


\compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła dotycząca porównań wersji planów amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRSP.sel_size()=0
|| FUN.emsg('Aby porównać wersje planu amortyzacji należy najpierw zaznaczyć dwa (2) warianty planu.'@);
   return(0)
|| 1
?}


\a_compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Porównanie wersji planów amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? __PCOMP.size()<>2
|| FUN.emsg('Niepoprawne parametry porówania planów.'@);
   return()
?};
_maska1:=_maska2:='';
SRSP.cntx_psh();
SRSP.prefix();
__PCOMP.prefix();
__PCOMP.first();
{? SRSP.seek(__PCOMP.REF,SRSP.name()) || _maska1:=SRSP.MASKA ?};
__PCOMP.last();
{? SRSP.seek(__PCOMP.REF,SRSP.name()) || _maska2:=SRSP.MASKA ?};
SRSP.cntx_pop();
{? _maska1='' | _maska2=''
|| FUN.emsg('Niepoprawne parametry porówania planów.'@);
   return()
?};

VAR_DEL.delete('__TSRSR');
__TSRSR:=tab_tmp(2,'NRI','STRING[20]','Nr inwentarzowy',
                   'ODD','STRING[10]','J. księgowa',
                   'NST','STRING[50]','Nazwa',
                   'KIND','STRING[1]','Środek',
                   'GRUPA','STRING[15]','Grupa',
                   'DE','DATE','Data eksploatacji',
                   'DZ','DATE','Data zakupu',
                   'EWI','STRING[25]','W ewidencji',
                   'JORG','STRING[16]','J. organizacyjna',
                   'EXIST1','STRING[1]','EXIST1',
                   'EXIST2','STRING[1]','EXIST2',
                   'REF1','INTEGER','REF1',
                   'MASKA1','STRING[1]','MASKA1',
                   'REF2','INTEGER','REF2',
                   'MASKA2','STRING[1]','MASKA2');

SRD.maski(_maska1);
SRSR.cntx_psh();
SRSR.index('KIND');
SRSR.prefix('T');
{? SRSR.first()
|| {! |?
      {? SRSR.GRP<>'E'
      || __TSRSR.blank();
         __TSRSR.NRI:=SRSR.NRI;
         __TSRSR.NST:=SRSR.NST;
         __TSRSR.ODD:=SRSR.ODD().OD;
         __TSRSR.JORG:=SRSR.JORG().SYMBOL;
         __TSRSR.KIND:=SRSR.KIND;
         __TSRSR.GRUPA:=SRSR.GR().GR;
         __TSRSR.DE:=SRSR.DE;
         __TSRSR.DZ:=SRSR.DZ;
         __TSRSR.EWI:=SRSR.OKRO_F().NAZ+' '+SRSR.OKRO_F().ROK().NAZ;
         __TSRSR.EXIST1:='T';
         __TSRSR.EXIST2:='N';
         __TSRSR.REF1:=#SRSR.ref();
         __TSRSR.MASKA1:=_maska1;
         __TSRSR.REF2:=0;
         __TSRSR.MASKA2:=_maska2;
         __TSRSR.add()
      ?};
      SRSR.next()
   !}
?};
SRSR.cntx_pop();

SRD.maski(_maska2);
SRSR.cntx_psh();
SRSR.index('KIND');
SRSR.prefix('T');
{? SRSR.first()
|| {! |?
      {? SRSR.GRP<>'E'
      || {? __TSRSR.find_key(SRSR.NRI,SRSR.ODD().OD)
         || __TSRSR.EXIST2:='T';
            __TSRSR.REF2:=#SRSR.ref();
            __TSRSR.put()
         || __TSRSR.blank();
            __TSRSR.NRI:=SRSR.NRI;
            __TSRSR.NST:=SRSR.NST;
            __TSRSR.ODD:=SRSR.ODD().OD;
            __TSRSR.JORG:=SRSR.JORG().SYMBOL;
            __TSRSR.KIND:=SRSR.KIND;
            __TSRSR.GRUPA:=SRSR.GR().GR;
            __TSRSR.DE:=SRSR.DE;
            __TSRSR.DZ:=SRSR.DZ;
            __TSRSR.EWI:=SRSR.OKRO_F().NAZ+' '+SRSR.OKRO_F().ROK().NAZ;
            __TSRSR.EXIST1:='N';
            __TSRSR.EXIST2:='T';
            __TSRSR.REF1:=0;
            __TSRSR.MASKA1:=_maska1;
            __TSRSR.REF2:=#SRSR.ref();
            __TSRSR.MASKA2:=_maska2;
            __TSRSR.add()
         ?}
      ?};
      SRSR.next()
   !}
?};
SRSR.cntx_pop();

exec('create_win_comp','!fst_ewz_zpla');
_idx:=__TSRSR.ndx_tmp(,,'KIND',,,'ODD',,0,'NRI',,);
__TSRSR.index(_idx);
__TSRSR.prefix('T');
__TSRSR.select();

VAR_DEL.delete('__TSRSR')


\create_win_comp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła tworzy okno do porównywania wersji planó amortyzacji
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__PPLAN','__PLAN1','__PLAN2','__GPLAN');
_title:='Środki'@;
__PPLAN:=__TSRSR.mk_sel(_title,,,'_pplan',5,5,20,,'U');
__TSRSR.win_fld(__PPLAN,,'EXIST1',,,5,,,'Lewy'@,,'Czy występuje w planie z lewej strony porówania'@,2,,"'T'","'N'");
__TSRSR.win_fld(__PPLAN,,'NRI',,,20,,,'Nr inwentarzowy'@,,'Numer inwentarzowy środka'@);
__TSRSR.win_fld(__PPLAN,,'NST',,,42,,,'Nazwa'@,,'Nazwa środka'@);
__TSRSR.win_fld(__PPLAN,,'ODD',,,15,,,'J. księgowa'@,,'Jednostka księgowa'@);
__TSRSR.win_fld(__PPLAN,,'JORG',,,16,,,'J. organizacyjna'@,,'Jednostka organizacyjna'@);
__TSRSR.win_fld(__PPLAN,,'GRUPA',,,15,,,'Grupa'@,,'Grupa'@);
__TSRSR.win_fld(__PPLAN,,'DE',,,10,,,'W eksploatacji'@,,'W eksploatacji od'@);
__TSRSR.win_fld(__PPLAN,,'DZ',,,10,,,'Data zakupu'@,,'Data zakupu środka'@);
__TSRSR.win_fld(__PPLAN,,'EWI',,,25,,,'W ewidencji'@,,'W ewidencji od'@);
__TSRSR.win_fld(__PPLAN,,'EXIST2',,,5,,,'Prawy'@,,'Czy występuje w planie z prawej strony porówania'@,2,,"'T'","'N'");
__TSRSR.win_act(__PPLAN,,'Menu','Podsu&mowania planów'@,,'Podsumowania planów dla firmy'@,
                "","",,,,,'M');
__TSRSR.win_act(__PPLAN,,'Formuła','Plan w &lewym panelu'@@,'Podsu&mowania planów'@,'Podsumowanie planu z lewego panelu'@,
                "","exec('l_sum','!fst_ewz_zpla')",,,,,'L');
__TSRSR.win_act(__PPLAN,,'Formuła','Plan w p&rawym panelu'@@,'Podsu&mowania planów'@,'Podsumowanie planu z prawego panelu'@,
                "","exec('p_sum','!fst_ewz_zpla')",,,,,'R');

SRSP.cntx_psh();
SRSP.prefix();
__PCOMP.prefix();
__PCOMP.first();
{? SRSP.seek(__PCOMP.REF,SRSP.name()) || _plan1:=SRSP.NAZWA ?};
__PCOMP.last();
{? SRSP.seek(__PCOMP.REF,SRSP.name()) || _plan2:=SRSP.NAZWA ?};
SRSP.cntx_pop();

_title:='Plan %1'@[_plan1];
__PLAN1:=SRST.mk_sel(_title,,,'_plan1',5,5,20,,'U');
SRST.win_fld(__PLAN1,,'ROK',,,5,,,'Rok'@,,'Rok podatkowy'@);
SRST.win_fld(__PLAN1,,'OKRES',,,5,,,'Okres'@,,'Okres podatkowy'@);
SRST.win_fld(__PLAN1,,'WARP',,,9,2,,'Wartość (P)'@,,'Wartość podatkowa'@);
SRST.win_fld(__PLAN1,,'UMOP',,,9,2,,'Umorzenie (P)'@,,'Umorzenie podatkowe'@);
SRST.win_fld(__PLAN1,,'AMOP',,,9,2,,'Amortyz. (P)'@,,'Amortyzacja podatkowa'@);
SRST.win_fld(__PLAN1,,'WARF',,,9,2,,'Wartość (B)'@,,'Wartość bilansowa'@);
SRST.win_fld(__PLAN1,,'UMOF',,,9,2,,'Umorzenie (B)'@,,'Umorzenie bilansowe'@);
SRST.win_fld(__PLAN1,,'AMOF',,,9,2,,'Amortyz.(B)'@,,'Amortyzacja bilansowa'@);
SRST.win_act(__PLAN1,,'Formuła','Podsu&mowanie'@@,,'Podsumowanie bieżącego planu dla środka'@,
             "","exec('podsumowanie','!fst_ewz_zpla')",1,,,,'M');

_title:='Plan %1'@[_plan2];
__PLAN2:=SRST.mk_sel(_title,,,'_plan2',5,5,20,,'U');
SRST.win_fld(__PLAN2,,'ROK',,,5,,,'Rok'@,,'Rok podatkowy'@);
SRST.win_fld(__PLAN2,,'OKRES',,,5,,,'Okres'@,,'Okres podatkowy'@);
SRST.win_fld(__PLAN2,,'WARP',,,9,2,,'Wartość (P)'@,,'Wartość podatkowa'@);
SRST.win_fld(__PLAN2,,'UMOP',,,9,2,,'Umorzenie (P)'@,,'Umorzenie podatkowe'@);
SRST.win_fld(__PLAN2,,'AMOP',,,9,2,,'Amortyz. (P)'@,,'Amortyzacja podatkowa'@);
SRST.win_fld(__PLAN2,,'WARF',,,9,2,,'Wartość (B)'@,,'Wartość bilansowa'@);
SRST.win_fld(__PLAN2,,'UMOF',,,9,2,,'Umorzenie (B)'@,,'Umorzenie bilansowe'@);
SRST.win_fld(__PLAN2,,'AMOF',,,9,2,,'Amortyz.(B)'@,,'Amortyzacja bilansowa'@);
SRST.win_act(__PLAN2,,'Formuła','Podsu&mowanie'@@,,'Podsumowanie bieżącego planu dla środka'@,
             "","exec('podsumowanie','!fst_ewz_zpla')",1,,,,'M');

_title:='Porównanie planów amortyzacji'@;
__GPLAN:=__TSRSR.grp_make(_title,"",'_tsrsrplan',5,5,,"");
__TSRSR.grp_sel(__GPLAN,,__PPLAN,,"exec('cmp_refresh','!fst_ewz_zpla')",,,15,"","",,,'maximized_with_title');
__TSRSR.grp_splt(__GPLAN,,'horizontal','porownanie');
__TSRSR.grp_sel(__GPLAN,SRST,__PLAN1,,"",,,,"exec('before_plan1','!fst_ewz_zpla')","",,,'maximized_with_title');
__TSRSR.grp_splt(__GPLAN,'porownanie','vertical','prawy');
__TSRSR.grp_sel(__GPLAN,SRST,__PLAN2,,"",,,,"exec('before_plan2','!fst_ewz_zpla')","",,,'maximized_with_title');
__TSRSR.win_sel(__GPLAN)


\before_plan1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed odświeżeniem okienka __PLAN1
::----------------------------------------------------------------------------------------------------------------------
exec('refresh1','!fst_ewz_zpla')


\before_plan2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed odświeżeniem okienka __PLAN2
::----------------------------------------------------------------------------------------------------------------------
exec('refresh2','!fst_ewz_zpla')


\cmp_refresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła odświeża dane amortyzacji obu wariantów planu po odświeżeniu okna środków
::----------------------------------------------------------------------------------------------------------------------
exec('refresh1','!fst_ewz_zpla');
exec('refresh2','!fst_ewz_zpla');
grp_disp(SRST,__PLAN1);
grp_disp(SRST,__PLAN2)


\refresh1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła odświeża lewy panel porównania
::----------------------------------------------------------------------------------------------------------------------
SRST.use('srst'+__TSRSR.MASKA1+REF.FIRMA().SYMBOL);
SRSR.use('srsr'+__TSRSR.MASKA1+REF.FIRMA().SYMBOL);
SRSR.prefix();
SRST.index('PODAT');
{? SRSR.seek(__TSRSR.REF1,SRSR.name())
|| SRST.prefix(SRSR.ref())
|| SRST.prefix(null)
?}


\refresh2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła odświeża prawy panel porównania
::----------------------------------------------------------------------------------------------------------------------
SRST.use('srst'+__TSRSR.MASKA2+REF.FIRMA().SYMBOL);
SRSR.use('srsr'+__TSRSR.MASKA2+REF.FIRMA().SYMBOL);
SRSR.prefix();
SRST.index('PODAT');
{? SRSR.seek(__TSRSR.REF2,SRSR.name())
|| SRST.prefix(SRSR.ref())
|| SRST.prefix(null)
?}


\plan_dla_srodka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla plany dla bieżącego środka w bazie rzeczywitych danych
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh();
SRST.cntx_psh();
SRSP.cntx_psh();
_odd:=SRSR.ODD;
_nri:=SRSR.NRI;
_kind:=SRSR.KIND;
SRSP.prefix();
{? SRSP.first()
|| _tmp:=tab_tmp(1,'NAZWA','STRING[30]','Nazwa',
                   'OD','INTEGER','Od roku',
                   'DO','INTEGER','Do roku',
                   'DATA','DATE','Data wprowadzenia',
                   'REFP','INTEGER','Ref planu',
                   'REFS','INTEGER','Ref środka');
   _win:=_tmp.mk_sel('Lista planów amortyzacji'@,'P',0,'_listaplan',20,5,15);
   _tmp.win_fld(_win,,'NAZWA',,,30,,,'Nazwa'@,,'Nazwa planu'@);
   _tmp.win_fld(_win,,'OD',,,6,,,'Od roku'@,,'Od roku'@);
   _tmp.win_fld(_win,,'DO',,,6,,,'Do roku'@,,'Do roku'@);
   _tmp.win_fld(_win,,'DATA',,,10,,,'Data wprowadzenia'@,,'Data wprowadzenia'@);
   _tmp.win_act(_win,,'Formuła','Wybierz'@@,,,"","sel_exit()",1,,,,'W');
   _tmp.win_sel(_win);

   {! |?
      SRD.maski(SRSP.MASKA);
      SRSR.index('NRI_O');
      SRSR.prefix(_odd,_kind,_nri);
      {? SRSR.first()
      || _tmp.blank();
         _tmp.NAZWA:=SRSP.NAZWA;
         _tmp.OD:=SRSP.OD;
         _tmp.DO:=SRSP.DO;
         _tmp.DATA:=SRSP.DATA;
         _tmp.REFP:=#SRSP.ref();
         _tmp.REFS:=#SRSR.ref();
         _tmp.add()
      ?};
      SRSP.next()
   !};
   {? _tmp.first()
   || {? _tmp.select()
      || SRSP.seek(_tmp.REFP,SRSP.name());
         SRD.maski(SRSP.MASKA);
         _title:='Planowana amortyzacja, wariant: %1 (%2)'@[SRSP.NAZWA,$SRSP.DATA];
         _splan:=SRST.mk_sel(_title,'P',,'_splans',5,5,20,,'U');
         SRST.win_fld(_splan,,'ROK',,,6,,,'Rok'@,,'Rok podatkowy'@);
         SRST.win_fld(_splan,,'OKRES',,,6,,,'Okres'@,,'Okres podatkowy'@);
         SRST.win_fld(_splan,,'WARP',,,12,2,,'Wartość pod.'@,,'Wartość podatkowa'@);
         SRST.win_fld(_splan,,'UMOP',,,12,2,,'Umorzenie pod.'@,,'Umorzenie podatkowe'@);
         SRST.win_fld(_splan,,'AMOP',,,12,2,,'Amortyzacja pod.'@,,'Amortyzacja podatkowa'@);
         SRST.win_fld(_splan,,'WARF',,,12,2,,'Wartość bil.'@,,'Wartość bilansowa'@);
         SRST.win_fld(_splan,,'UMOF',,,12,2,,'Umorzenie bil.'@,,'Umorzenie bilansowe'@);
         SRST.win_fld(_splan,,'AMOF',,,12,2,,'Amortyzacja bil.'@,,'Amortyzacja bilansowa'@);
         SRST.win_act(_splan,,'Wyświetl',,,,,"SRD.showAsset()");
         SRST.win_act(_splan,,'Formuła','Podsumowanie dla środka'@@,,,,"exec('podsumowanie','!fst_ewz_zpla')",1,,,,'P');
         SRST.win_sel(_splan);
         SRSR.prefix();
         {? SRSR.seek(_tmp.REFS,SRSR.name())
         || SRST.index('PODAT');
            SRST.prefix(SRSR.ref());
            SRST.select()
         || FUN.emsg('Błąd, nie znaleziono wskazanego środka w bazie planu amortyzacji.'@)
         ?}
      ?}
   || FUN.emsg('Środek nie występuje w żadnym z planów amortyzacji.'@)
   ?}
|| FUN.emsg('W systemie brak planów amortyzacji dla bieżącej firmy'@)
?};
SRD.maski('r');
SRSP.cntx_pop();
SRST.cntx_pop();
SRSR.cntx_pop();
1


\l_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Podsumowanie dla firmy wg wariantu planu z lewego panelu porównania planów
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh(); SRST.cntx_psh();
SRST.use('srst'+__TSRSR.MASKA1+REF.FIRMA().SYMBOL);
SRSR.use('srsr'+__TSRSR.MASKA1+REF.FIRMA().SYMBOL);
SRSP.cntx_psh();
SRSP.index('SRSP');
SRSP.prefix(__TSRSR.MASKA1);
{? SRSP.first()
|| _plan:=', wariant planu: %1'@[SRSP.NAZWA]
?};
SRSP.cntx_pop();
exec('f_podsumowanie','fst',_plan);
SRSR.cntx_pop(); SRST.cntx_pop()


\p_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Podsumowanie dla firmy wg wariantu planu z prawego panelu porównania planów
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh(); SRST.cntx_psh();
SRST.use('srst'+__TSRSR.MASKA2+REF.FIRMA().SYMBOL);
SRSR.use('srsr'+__TSRSR.MASKA2+REF.FIRMA().SYMBOL);
SRSP.cntx_psh();
SRSP.index('SRSP');
SRSP.prefix(__TSRSR.MASKA2);
{? SRSP.first()
|| _plan:=', wariant planu: %1'@[SRSP.NAZWA]
?};
SRSP.cntx_pop();
exec('f_podsumowanie','fst',_plan);
SRSR.cntx_pop(); SRST.cntx_pop()


\dok_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetlanie dokumentów bieżącego środka z planu amortyzacji
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__DPLAN');
__DPLAN:=SRDO.mk_sel('Dokumenty wartościowe'@,,,'_dplan',5,5,20,,'U',,,,,'html_maximized');
SRDO.win_fld(__DPLAN,,'SYMBOL',,,20,,,'Symbol dokumentu'@,,'Symbol dokumentu'@);
SRDO.win_fld(__DPLAN,,'DW',,,10,,,'Data wystawienia'@,,'Data wystawienia dokumentu'@);
SRDO.win_fld(__DPLAN,,'ROK',,,6,,,'Od roku'@,,'Od roku'@);
SRDO.win_fld(__DPLAN,,'OKRES',,,6,,,'Od okresu'@,,'Od okresu'@);

SRDO.win_fld(__DPLAN,,'P',,,10,2,,'Zmiana wartości'@,,'Zmiana wartości'@);
SRDO.win_fld(__DPLAN,,'WARP',,,10,2,,'Wartość podatkowa'@,,'Wartość podatkowa'@);
SRDO.win_fld(__DPLAN,,'UMOP',,,10,2,,'Umorzenie podatkowa'@,,'Umorzenie podatkowe'@);
SRDO.win_fld(__DPLAN,,'WARF',,,10,2,,'Wartość bilansowa'@,,'Wartość bilansowa'@);
SRDO.win_fld(__DPLAN,,'UMOF',,,10,2,,'Umorzenie bilansowe'@,,'Umorzenie bilansowe'@);
SRDO.win_fld(__DPLAN,,'Z',,,5,,,'Skreślenie'@,,'Dokument skreślający'@,2,,"'T'","'N'");
SRDO.win_fld(__DPLAN,,'PLAN',,,5,,,'Plan?'@,,'Dokument zaplanowany'@,2,,"'T'","'N'");

SRDO.win_act(__DPLAN,,'Formuła','Dołącz'@@,,,,"exec('dok_add','!fst_ewz_zpla')",1,,,,'D');
SRDO.win_act(__DPLAN,1,'Formuła','Dołącz'@@,,,,"exec('dok_add','!fst_ewz_zpla')",1,,,,'D');
SRDO.win_act(__DPLAN,,'Formuła','Popraw'@@,,,,"exec('dok_edit','!fst_ewz_zpla')",,,,,'P');
SRDO.win_act(__DPLAN,,'Formuła','Usuń'@@,,,,"exec('dok_del','!fst_ewz_zpla')",,,,,'U');
SRDO.win_act(__DPLAN,,'Wyświetl',,,,,"exec('dok_show','!fst_ewz_zpla')");

_btn_a:=SRDO.win_btn(__DPLAN,'text=%1,btn_label_align=center,panel=right,align=begin'['Dołącz'@],'menu:D',,,,,,'');
SRDO.btn_opt(_btn_a,'tooltip=%1'['Dołącz dokument wartościowy'@]);
_btn_b:=SRDO.win_btn(__DPLAN,'text=%1,btn_label_align=center,panel=right,align=begin'['Popraw'@],'menu:P',,,,,,'noempty');
SRDO.btn_opt(_btn_b,'tooltip=%1'['Popraw dokument wartościowy'@]);
_btn_c:=SRDO.win_btn(__DPLAN,'text=%1,btn_label_align=center,panel=right,align=begin'['Usuń'@],'menu:U',,,,,,'noempty');
SRDO.btn_opt(_btn_c,'tooltip=%1'['Usuń dokument wartościowy'@]);

SRDO.win_sel(__DPLAN);

{? SRSR.GRP='E'
|| SRDO.index('DATA_E');
   SRDO.prefix(SRSR.TREE,SRSR.ref())
|| SRDO.index('PODAT');
   SRDO.prefix(SRSR.ref())
?};
SRDO.select()


\dok_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie dokumentu wartościowego do środka z planu amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRSP.AKCEPT='T'
|| FUN.emsg('Zaakceptowanych planów amortyzacji nie można modyfikować.'@);
   return(0)
?};
SRSR.get();
{? SRSR.Z='T'
|| FUN.emsg('Środek skreślony, modyfikacje nie są możliwe.'@);
   return(0)
|? SRSR.R='S'
|| FUN.emsg('Wskazany środek został zbyty na rzecz innej firmy z grupy kapitałowej.'@);
   return(0)
?};
{? SRSR.R='S'
|| FUN.emsg('Wskazany środek został zbyty na rzecz innej firmy z grupy kapitałowej.\n'
            'Modyfikacje nie są możliwe.'@);
   return(0)
?};
{? SRSR.GRP='T' & SRST.SRSR().Z='N' & SRD.isSetElementsActive()
|| FUN.emsg('Nie można dodawać i modyfikować dokumentów wartościowych dla zestawu środków.\n'
            'Dane tego typu są oddzielne dla elementów zestawu dlatego taki dokument należy\n'
            'dodawać na poziomie elementu składowego.\nAby zlikwidować zestaw należy najpierw '
            'zlikwidować wszystkie elementy składowe zestawu.'@);
   return(0)
?};
{? SRSR.PLAN_NAL<>'T'
|| FUN.emsg('Przed wprowadzaniem dokumentów korekty wartościowej należy obliczyć plan amortyzacji dla środka.'@);
   return()
?};

_ref:=null;
_tmp:=EDIT_ES.RODZ;
EDIT_ES.RODZ:='W';
SRDT.cntx_psh(); SRSR.cntx_psh();

SRDT.use('srdt'+'r'+REF.FIRMA().SYMBOL);
SRDT.index('SRDT');
SRDT.prefix(EDIT_ES.RODZ);
{? SRDT.first()
|| {! |?
:: dokumenty związane ze zbyciem środków w grupie kapitałowej są pomijane dla planów amortyzacji
      {? SRDT.P<>'S'
      || _typ:=SRDT.TYP;
         _warp:=SRDT.WARP;
         _warf:=SRDT.WARF;
         _ward:=SRDT.WARD;
         _p:=SRDT.P;
         _przych:=SRDT.PRZYCHOD;
         _roz:=SRDT.ROZ;
         _z:=SRDT.Z;
         _umop:=SRDT.UMOP;
         _umof:=SRDT.UMOF;
         _umod:=SRDT.UMOD;
         _ostatek:=SRDT.OSTATEK;
         _max:=SRDT.MAX;
         _oke:=SRDT.OKE;
         SRDT.cntx_psh();
         SRDT.use('srdt'+SRSP.MASKA+REF.FIRMA().SYMBOL);
         SRDT.index('TYP');
         SRDT.prefix(_typ,);
         {? ~SRDT.first()
         || SRDT.add()
         || SRDT.WARP:=_warp;
            SRDT.WARF:=_warf;
            SRDT.WARD:=_ward;
            SRDT.P:=_p;
            SRDT.PRZYCHOD:=_przych;
            SRDT.ROZ:=_roz;
            SRDT.Z:=_z;
            SRDT.UMOP:=_umop;
            SRDT.UMOF:=_umof;
            SRDT.UMOD:=_umod;
            SRDT.OSTATEK:=_ostatek;
            SRDT.MAX:=_max;
            SRDT.OKE:=_oke;
            SRDT.put()
         ?};
         SRDT.cntx_pop()
      ?};
      SRDT.next()
   !}
?};

SRDT.use('srdt'+SRSP.MASKA+REF.FIRMA().SYMBOL);
SRDT.index('SRDT');
SRDT.prefix(EDIT_ES.RODZ);

{? SRDT.first()
|| SRDT.win_sel('SELECT');
   {? SRDT.select()
   || {? 1+SRSR.MF().T<>'N' & exec('field_test_nat','fst')
      || FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                  'Wskazany typ dokumentu dotyczy tylko pól wartościowych związanych\n'
                  'z metodą naturalną bilansową. Środek nie jest amortyzowany tą metodą.'@[SRDT.TYP,SRDT.NAZWA]);
         _ref:=null
      |? SRDT.P='S'
      || FUN.emsg('W module planów amortyzacji nie można dokonywać zbycia środków\n'
                  'na rzecz innych firm z grupy kapitałowej.'@);
        _ref:=null
      || _ref:=SRDT.ref()
      ?}
   ?}
|| FUN.emsg('W systemie nie zdefiniowano żadnego typu dokumentu wartościowego, który może być użyty\n'
            'w planach amortyacji. Należy uzupełnić brakujące dane w obszarze Ustawienia i parametryzacja.'@)
?};

{? _ref<>null
|| {? SRSP.r_lock(1,1)
   ||
      SRDO.blank();
      SRDO.OKRO_F:=null;
      SRDO.ROK_F:=null;
      SRDO.TYP:=_ref;
      SRDO.PLAN:='T';
:: jeżeli element to dodatkowe złączenie na element składowy
      {? SRSR.GRP='E'
      || SRDO.SRSR_E:=SRSR.ref();
         SRDO.SRSR:=exec('get_srsr_root','fst',SRSR.ref())
      || SRDO.SRSR:=SRSR.ref()
      ?};
      {? SRSR.GRP='T'
      || SRDO.win_edit('PLAN_WG')
      || SRDO.win_edit('PLAN_W')
      ?};
      exec('set_field','fst');
      {? SRSP.OKRO_F().RES:=SRSP.OD & SRSP.OKRO_F().OES=1
      || SRDO.DO:=SRDO.DW:=date(SRSP.OD,1,1)
      || SRDO.DO:=SRDO.DW:=SRSP.OKRO_F().POCZ
      ?};
      {? SRDO.edit("exec('chk_dok','!fst_ewz_zpla',0)")
      || {? SRDO.add()
         || {? SRDO.Z='T'
            || SRSR.Z:='T';
               SRSR.put()
            ?};
:: jeżeli zestaw to odwieżanie danych elementów
            {? SRSR.GRP='T'
            || SRD.updateElementsPlan()
:: jeżeli element to odświeżenie danych wartościowych zestawu
            |? SRSR.GRP='E'
            || _t_ref:=SRSR.ref();
               SRD.updatePlan(SRSR.ref());
               SRSR.cntx_psh();
               SRSR.index('TREE');
               SRSR.prefix(SRSR.TREE);
               {? SRSR.first()
               || {! |?
                     {? SRSR.ref()<>_t_ref
                     || SRD.updatePlan(SRSR.ref())
                     ?};
                     SRSR.next()
                  !}
               ?};
               SRSR.cntx_pop();
               SRD.rootUpdatePlan(SRSR.TREE)
            || SRD.updatePlan(SRSR.ref())
            ?}
         ?}
      ?};
      SRSP.r_unlock()
   || FUN.emsg('Plan amortyzacji został zablokowany przez innego użytkownika.'@)
   ?}
?};
SRDT.cntx_pop(); SRSR.cntx_pop();
EDIT_ES.RODZ:=_tmp


\dok_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Poprawianie dokumentu wartościowego środka z planu amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRSP.AKCEPT='T'
|| FUN.emsg('Zaakceptowanych planów amortyzacji nie można modyfikować.'@);
   return()
?};

{? SRDO.PLAN<>'T'
|| FUN.emsg('Poprawiać można tylko te dokumenty, które zostały dodane w planie amortyzacji.'@);
   return()
?};

{? SRSR.Z='T'
|| FUN.emsg('Środek skreślony, modyfikacje nie są możliwe.'@);
   return(0)
?};

{? SRSR.R='S'
|| FUN.emsg('Wskazany środek został zbyty na rzecz innej firmy z grupy kapitałowej.'
            'Modyfikacje nie są możliwe.'@)
?};

{? SRSP.r_lock(1,1)
||
   _tmp:=EDIT_ES.RODZ;
   EDIT_ES.RODZ:='W';
   {? SRSR.GRP='T'
   || SRDO.win_edit('PLAN_WG')
   || SRDO.win_edit('PLAN_W')
   ?};
   exec('set_field','fst');
   {? SRDO.edit("exec('chk_dok','!fst_ewz_zpla',1)")
   || {? SRDO.put()
      ||
::    jeżeli zestaw to odwieżanie danych elementów
         {? SRSR.GRP='T'
         || SRD.updateElementsPlan()
::    jeżeli element to odświeżenie danych wartościowych zestawu
         |? SRSR.GRP='E'
         || _t_ref:=SRSR.ref();
            SRD.updatePlan(SRSR.ref());
            SRSR.cntx_psh();
            SRSR.index('TREE');
            SRSR.prefix(SRSR.TREE);
            {? SRSR.first()
            || {! |?
                  {? SRSR.ref()<>_t_ref
                  || SRD.updatePlan(SRSR.ref())
                  ?};
                  SRSR.next()
               !}
            ?};
            SRSR.cntx_pop();
            SRD.rootUpdatePlan(SRSR.TREE)
         || SRD.updatePlan(SRSR.ref())
         ?}
      ?}
   ?};
   EDIT_ES.RODZ:=_tmp;
   SRSP.r_unlock()
|| FUN.emsg('Plan amortyzacji został zablokowany przez innego użytkownika.'@)
?}


\dok_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie dokumentu wartościowego środka z planu amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRSP.AKCEPT='T'
|| FUN.emsg('Zaakceptowanych planów amortyzacji nie można modyfikować.'@);
   return()
?};

{? SRDO.PLAN<>'T'
|| FUN.emsg('Usuwać można tylko te dokumenty, które zostały dodane w planie amortyzacji.'@);
   return()
?};

{? SRSR.Z='T' & SRDO.Z<>'T'
|| FUN.emsg('Środek skreślony, modyfikacje nie są możliwe.'@);
   return(0)
|? SRSR.R='S'
|| FUN.emsg('Wskazany środek został zbyty na rzecz innej firmy z grupy kapitałowej.'@)
?};

{? ~FUN.ask('Usunąć bieżący dokument wartościowy?'@)
|| return()
?};

{? SRSP.r_lock(1,1)
||
   {? SRDO.Z='T'
   || _z:=1;
      {? SRSR.DOKSKR<>null
      || SRSR.DOKSKR:=null;
         SRSR.put()
      ?}
   || _z:=0
   ?};

   {? SRDO.del(1,1)
   ||
      {? _z || SRSR.Z:='N'; SRSR.put() ?};
:: jeżeli zestaw to odwieżanie danych elementów
      {? SRSR.GRP='T'
      || SRD.updateElementsPlan()
:: jeżeli element to odświeżenie danych wartościowych zestawu
      |? SRSR.GRP='E'
      || _t_ref:=SRSR.ref();
         SRD.updatePlan(SRSR.ref());
         SRSR.cntx_psh();
         SRSR.index('TREE');
         SRSR.prefix(SRSR.TREE);
         {? SRSR.first()
         || {! |?
               {? SRSR.ref()<>_t_ref
               || SRD.updatePlan(SRSR.ref())
               ?};
               SRSR.next()
            !}
         ?};
         SRSR.cntx_pop();
         SRD.rootUpdatePlan(SRSR.TREE)
      || SRD.updatePlan(SRSR.ref())
      ?}
   ?};
   SRSP.r_unlock()
|| FUN.emsg('Plan amortyzacji zablokowany przez innego użytkownika.'@)
?};
1


\chk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja poprawności dokumentu
::   WE: _a = 1 - popraw, 0 - dołącz
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRDO,,'SYMBOL','DW','DO');
{? _wy=''
|| {? SRDO.OKRES=0
   || FUN.emsg('Nie uzupełnione pole: Obowiązuje od okresu.'@);
      _wy:='OKRES'
   ?}
?};

{? _wy=''
|| {? SRDO.OKRES>12
   || FUN.emsg('Nieprawidłowa wartość w polu: Obowiązuje od okresu.'@);
      _wy:='OKRES'
   ?}
?};

{? _wy=''
|| {? SRDO.ROK=0
   || FUN.emsg('Nie uzupełnione pole: Obowiązuje od roku.'@);
      _wy:='ROK'
   ?}
?};
{? _wy=''
|| {? __CHK.index(SRDO,_a)<>''
   || {? FUN.ask('Wygenerować nowy numer dokumentu?'@)
      || SRDO.NR:=exec('bl_srdo_nr','fst',SRDO.NR);
         SRDO.SYMBOL:=exec('symdok','fst')
      ?};
      _wy:='SYMBOL'
   ?}
?};
:: kontrola miesiąca i roku obowiązywania
{? _wy='' & (SRDO.ROK<SRSR.ROK | (SRDO.ROK=SRSR.ROK & SRDO.OKRES<SRSR.OKRES))
|| FUN.emsg('Dokument nie może obowiązywać od okresu wcześniejszego niż okres wprowadzenia środka do eksploatacji.'@);
   _wy:='OKRES'
?};
{? _wy='' & (SRDO.ROK<SRSP.OD | SRDO.ROK>SRSP.DO)
|| FUN.emsg('Okres obowiązywania dokumentu musi zawierać się w zakresie planu amortyzacji.'@);
   _wy:='OKRES'
?};

{? _wy='' & (SRDO.ROK<SRSP.OKRO_F().RES | (SRDO.ROK=SRSP.OKRO_F().RES & SRDO.OKRES<SRSP.OKRO_F().OES))
|| _par1:=$SRDO.OKRES+'.'+$SRDO.ROK;
   _par2:=$SRSP.OKRO_F().OES+'.'+$SRSP.OKRO_F().RES;
   FUN.emsg('Okres obowiązywania dokumentu (%1) nie może być wcześniejszy niż okres '
            'danych źródłowych inwentaryzacji (%2).'@[_par1,_par2]);
   _wy:='OKRES'
?};

:: kontrola daty wystawienia
{? _wy='' & (SRDO.DW<date(SRDO.ROK,1,1) | SRDO.DW>date(SRDO.ROK,12,0))
|| FUN.emsg('Data wystawienia niezgodna z rokiem obowiązywania dokumentu.'@);
   _wy:='DW'
?};
{? _wy='' & (SRDO.DW>date(SRDO.ROK,SRDO.OKRES,0))
|| FUN.emsg('Data wystawienia późniejsza od okresu obowiązywania dokumentu.'@);
   _wy:='DW'
?};

:: kontrola czy wypełniono jakiekolwiek wartości
{? _wy=''
|| _pola:=_wpola:=0;
   _gdzie:='';
   {? SRDO.TYP().WARP='T' || _pola+=1; {? _gdzie='' || _gdzie:='WARP' ?} ?};
   {? SRDO.WARP<>0 || _wpola+=1 ?};
   {? SRDO.TYP().UMOP='T' || _pola+=1; {? _gdzie='' || _gdzie:='UMOP' ?} ?};
   {? SRDO.UMOP<>0 || _wpola+=1 ?};
   {? SRDO.TYP().WARF='T' || _pola+=1; {? _gdzie='' || _gdzie:='WARF' ?} ?};
   {? SRDO.WARF<>0 || _wpola+=1 ?};
   {? SRDO.TYP().UMOF='T' || _pola+=1; {? _gdzie='' || _gdzie:='UMOF' ?} ?};
   {? SRDO.UMOF<>0 || _wpola+=1 ?};
   {? FINFO.TOR_D='T' & SRDO.TYP().WARD='T' || _pola+=1; {? _gdzie='' || _gdzie:='WARD' ?} ?};
   {? FINFO.TOR_D='T' & SRDO.WARD<>0 || _wpola+=1 ?};
   {? FINFO.TOR_D='T' & SRDO.TYP().UMOD='T' || _pola+=1; {? _gdzie='' || _gdzie:='UMOD' ?} ?};
   {? FINFO.TOR_D='T' & SRDO.UMOD<>0 || _wpola+=1 ?};
   {? SRDO.TYP().OSTATEK='T' ||  _pola+=1; {? _gdzie='' || _gdzie:='OSTATEK' ?} ?};
   {? SRDO.OSTATEK<>0 || _wpola+=1 ?};
   {? SRDO.TYP().OKE='T' ||  _pola+=1; {? _gdzie='' || _gdzie:='OKE' ?} ?};
   {? SRDO.OKE<>0 || _wpola+=1 ?};
   {? SRDO.TYP().MAX='T' ||  _pola+=1; {? _gdzie='' || _gdzie:='MAX' ?} ?};
   {? SRDO.MAX<>0 || _wpola+=1 ?}
?};
{? _wy='' & _pola>0 & _wpola=0 & SRDO.SRSR().GRP<>'T'
|| FUN.emsg('Nie wypełniono żadnego z pól wartościowych. Dokument nie zmieni żadnej z cech wartościowych środka.'@);
   _wy:={? _gdzie<>'' || _gdzie || 'UWAGI' ?}
?};
:: kontrola wartości niezgodnych z charakterem dokumentu (przychodowy/rozchodowy)
{? _wy=''
|| {? SRDO.TYP().P='+'
   || {? SRDO.TYP().WARP='T' & SRDO.WARP<0 || _wy:='WARP' ?};
      {? _wy='' & SRDO.TYP().UMOP='T' & SRDO.UMOP<0 || _wy:='UMOP' ?};
      {? _wy='' & SRDO.TYP().WARF='T' & SRDO.WARF<0 || _wy:='WARF' ?};
      {? _wy='' & SRDO.TYP().UMOF='T' & SRDO.UMOF<0 || _wy:='UMOF' ?};
      {? FINFO.TOR_D='T'
      || {? _wy='' & SRDO.TYP().WARD='T' & SRDO.WARD<0 || _wy:='WARD' ?};
         {? _wy='' & SRDO.TYP().UMOD='T' & SRDO.UMOD<0 || _wy:='UMOD' ?}
      ?}
   |? SRDO.TYP().P='-'
   || {? SRDO.TYP().WARP='T' & SRDO.WARP>0 || _wy:='WARP' ?};
      {? _wy='' & SRDO.TYP().UMOP='T' & SRDO.UMOP>0 || _wy:='UMOP' ?};
      {? _wy='' & SRDO.TYP().WARF='T' & SRDO.WARF>0 || _wy:='WARF' ?};
      {? _wy='' & SRDO.TYP().UMOF='T' & SRDO.UMOF>0 || _wy:='UMOF' ?};
      {? FINFO.TOR_D='T'
      || {? _wy='' & SRDO.TYP().WARD='T' & SRDO.WARD>0 || _wy:='WARD' ?};
         {? _wy='' & SRDO.TYP().UMOD='T' & SRDO.UMOD>0 || _wy:='UMOD' ?}
      ?}
   ?};
   {? _wy<>''
   || {? SRDO.TYP().P='+' || _typ:='przychodowy'@ || _typ:='rozchodowy'@ ?};
      FUN.emsg('Wprowadzone wartości w dokumencie nie są zgodne z charakterem\n'
               'dokumentu ( dokument %1).'@[_typ])
   ?}
?};
:: obsługa samochodów osobowych
{? _wy='' & SRDO.WARP>0 & SRST.SRSR().SAMOCHOD='T'
|| exec('czytaj','#stalesys',,FINFO,'LIMIT_OS');
   exec('czytaj','#stalesys',,FINFO,'LIMIT_OE');
   _prz_lim:=0;
   {? SRST.SRSR().DE<date(2019,1,1)
   || {? SRST.SRSR().WARP+SRDO.WARP>(FINFO.LIMIT_EU*SRSR.KURS_EUR)
      || _prz_lim:=1
      ?}
   || _limit:={? SRST.SRSR().SAM_EL='N' || FINFO.LIMIT_OS
              |? SRST.SRSR().SAM_EL='T' || FINFO.LIMIT_OE
              ?};
      {? _limit>0 & (SRST.SRSR().WARP+SRDO.WARP)>_limit
      || _prz_lim:=1
      ?}
   ?};
   {? _prz_lim
   || FUN.emsg('Wprowadzona wartość podatkowa może spowodować przekroczenie limitu '
               'dla samochodów osobowych.'@);
      _wy:='WARP'
   ?}
?};
:: kontrola wpływu zmiany wartości w kolejnych okresach
{? _wy=''
|| _ujemna:='';
   _ref:=SRST.ref();

:: analiza dokumentów
   SRDO.cntx_psh();
   _warp:=_umop:=0;
   _warf:=_umof:=0;
   _ward:=_umod:=0;
   _ostatek:=_oke:=_max:=0;
   _rok:=SRDO.ROK;
   _okres:=SRDO.OKRES;
   {? _a=1 || _srdo:=SRDO.ref() || _srdo:=null ?};
   SRDO.index('SRODZAJ');
   SRDO.prefix(SRSR.ref(),'W');
   {? SRDO.first()
   || {! |?
          {? (SRDO.ROK>_rok | (SRDO.ROK=_rok & SRDO.OKRES>=_okres)) & SRDO.ref()<>_srdo
          || _warp+=SRDO.WARP;_umop+=SRDO.UMOP;
             _warf+=SRDO.WARF;_umof+=SRDO.UMOF;
             _ostatek+=SRDO.OSTATEK;_oke+=SRDO.OKE; _max+=SRDO.MAX;
             {? FINFO.TOR_D='T' || _ward+=SRDO.WARD;_umod+=SRDO.UMOD ?}
          ?};
          SRDO.next()
      !}
   ?};
   SRDO.cntx_pop();

   SRST.cntx_psh();
   SRST.index('PODAT');
   SRST.prefix(SRSR.ref(),SRDO.ROK,SRDO.OKRES);
   {? SRST.first()
   || _umop+=SRST.UMOP;
      _umof+=SRST.UMOF;
      {? FINFO.TOR_D='T' || _umod+=SRST.UMOD ?}
   ?};
   SRST.cntx_pop();

   {? SRDO.TYP().WARP='T' & (SRSR.WARP+_warp+SRDO.WARP<0) || _ujemna:='WARP' ?};
   {? _ujemna='' & SRDO.TYP().WARF='T' & (SRSR.WARF+_warf+SRDO.WARF<0) || _ujemna:='WARF' ?};
   {? _ujemna='' & FINFO.TOR_D='T' & SRDO.TYP().WARD='T' & (SRSR.WARD+_ward+SRDO.WARD<0) || _ujemna:='WARD' ?};

   {? _ujemna='' & SRDO.TYP().UMOP='T' & (SRSR.UMOP+_umop+SRDO.UMOP<0) || _ujemna:='UMOP' ?};
   {? _ujemna='' & SRDO.TYP().UMOF='T' & (SRSR.UMOF+_umof+SRDO.UMOF<0) || _ujemna:='UMOF' ?};
   {? _ujemna='' & SRDO.TYP().UMOP='T' & ((SRSR.WARP+_warp+SRDO.WARP-(SRSR.OSTATEK+_ostatek+SRDO.OSTATEK))-(SRDO.UMOP)<0)
   || _ujemna:='UMOP'
   ?};
   {? _ujemna='' & SRDO.TYP().UMOF='T' & ((SRSR.WARF+_warf+SRDO.WARF-(SRSR.OSTATEK+_ostatek+SRDO.OSTATEK))-(SRDO.UMOF)<0)
   || _ujemna:='UMOF'
   ?};
   {? _ujemna='' & FINFO.TOR_D='T' & SRDO.TYP().UMOD='T' & (SRSR.UMOD+_umod+SRDO.UMOD<0) || _ujemna:='UMOD' ?};
   {? _ujemna='' & FINFO.TOR_D='T' & SRDO.TYP().UMOD='T' & ((SRSR.WARD+_ward+SRDO.WARD)-(SRDO.UMOD)<0)
   || _ujemna:='UMOD'
   ?};
   {? _ujemna='' & SRDO.TYP().OSTATEK='T' & (SRSR.OSTATEK+_ostatek+SRDO.OSTATEK<0) || _ujemna:='OSTATEK' ?};
   {? _ujemna='' & SRDO.TYP().OKE='T' & (SRSR.OKE+_oke+SRDO.OKE<0) || _ujemna:='OKE' ?};
   {? _ujemna='' & SRDO.TYP().MAX='T' & (SRSR.MAX+_max+SRDO.MAX<0) || _ujemna:='MAX' ?};

   {? _ujemna<>''
   || FUN.emsg('Wprowadzenie przygotowanej korekty zmniejszającej spowoduje powstanie ujemnych wartości środka,\n'
               '(lub zmniejszenie poniżej wartości rezydualnej). Należy skorygować wysokość zmniejszenia.'@);
      _wy:=_ujemna
   ?}
?};
_wy


\dok_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na wyświetl dla dokumentów w planach amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.SRSR().GRP='T'
|| SRDO.win_edit('PLAN_WG')
|| SRDO.win_edit('PLAN_W')
?};
SRDO.display()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:49 24815d861be0ec24f512df8150043c81a721e798608cebe98886eabde98cd2c2831821b29ce52d806727ac3352a1f581aa13e72aa7c6f8941065c081f141e1cb061d5bccbb89df555bc2db6bea46ec6ff0b4edc77ecfe6d965d81a65c54b0b0e3c7e57fb31130827bff23024d04249078d64bb3e3134638b61e087a498da7228

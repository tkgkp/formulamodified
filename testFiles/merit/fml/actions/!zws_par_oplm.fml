:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_oplm.fml
:: Utworzony: 15.12.2021
:: Autor: [rr]
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_OPLM
::            Definiowanie opłat dodatkowych dla indeksu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Czynność ZWS_PAR_OPLM - definiowanie opłat dodatkowych dla indeksu
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejsciowe czynnosci
::       _int - [obj_new] - parametry wewnetrzne czynnosci
::       _out - [obj_new] - parametry wyjsciowe czynnosci
::       _mp  - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
''


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Czynność ZWS_PAR_OPLM - definiowanie opłat dodatkowych - opis na TODO
::----------------------------------------------------------------------------------------------------------------------
'Definiowanie opłaty dodatkowej dla indeksu'


\def_mopl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: definiowanie opłat dodatkowych dla indeksu
::----------------------------------------------------------------------------------------------------------------------
BEER.MJM:=M.ref();
TAXS.prefix();
{? TAXS.first()
|| _allact:=exec('allActions','#string','wKkSsL');
   _act:=TAXS.actions('WER',_allact);
   _formikon:="{? TAXS.MOD*'W'
            || TAXS.MOD:=''; 'xwin16.png:9'
            |? TAXS.MOD*'A'
            || TAXS.MOD:=''; 'xwin16.png:3'
            |? TAXS.MOD*'V'
            || TAXS.MOD:=''; 'xwin16.png:10'
            || exec('pusta','#icon')
            ?}";
   TAXS.win_fml('WER',TAXS,'MOD',,'ICON_BEFORE',_formikon);
   TAXX.dnd_sel('WER',,'records.TAXX',"exec('renumtaxx','!zws_par_oplm')");
   TAXX.hdr_sel(' dla %1'@[M.KTM+' - '+M.N]);
   _formikon:="{? TAXX.MOD*'K'
               || TAXX.MOD:=''; 'xwin16.png:9'
               |? TAXX.MOD*'W'
               || TAXX.MOD:=''; 'xwin16.png:3'
               || exec('pusta','#icon')
               ?}";
   TAXX.win_fml('WER',TAXX,'MOD',,'ICON_BEFORE',_formikon);
   TAXX.win_sel('GRP');
   TAXX.index('M');
   TAXX.prefix(M.ref());
   TAXX.first();
   TAXX.select();
   TAXX.dnd_sel('WER',,'records.TAXX',"");
   TAXS.actions('WER',_act)
|| FUN.info('Brak definicji jakichkolwiek Opłat dodatkowych.\nFunkcja niedostępna.'@)
?};
~~


\pogrptax
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: po odswiezeniu - okienka grupowego
::----------------------------------------------------------------------------------------------------------------------
TAXI.index('M');
TAXI.prefix(BEER.MJM,TAXX.TAXS);
grp_disp(TAXI,'WER');
1


\bl_lp_taxx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: blankuje liczbę porządkową
::   WY: numer
::----------------------------------------------------------------------------------------------------------------------
TAXX.cntx_psh();
TAXX.index('M');
TAXX.prefix(BEER.MJM);
_res:={? TAXX.last() || TAXX.LP || 0 ?}+1;
TAXX.cntx_pop();
_res


\add_taxx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dodanie opłaty dodatkowej dla indeksu
::   WE: [_a] - tryb dodania 0-domyślnie z okienkiem 1-auto
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
_tryb:={? var_pres('_a')=type_of(0) || _a || 0 ?};

TAXX.blank();
{? _tryb || TAXX.TAXS:=TAXXYZ.TAXS ?};
{? _tryb | TAXX.edit("exec('chk_taxx','!zws_par_oplm')")
|| _res:=1;
   do();
   {? TAXX.add(1)
   || _taxs:=TAXX.TAXS;
      TAXT.index('MNAG');
      TAXT.prefix(_taxs,'M');
      {? TAXT.first()
      || {!
         |? _rodz:=TAXT.RV;
            {? (';RSDTL'*_rodz)>1
            || TAXI.index('M');
               TAXI.prefix(BEER.MJM,_taxs,TAXT.KOD,);
               {? ~TAXI.first()
               || TAXI.blank();
                  TAXI.TAXS:=_taxs;
                  TAXI.M:=BEER.MJM;
                  TAXI.TAXT:=TAXT.KOD;
                  TAXI.DESC:=TAXT.DESC;
                  ($('TAXI.VAL%1:=TAXT.VAL%1'[_rodz]))();
                  exec('value','oplaty_dod',TAXI,_rodz);
                  {? ~TAXI.add(1) || _res:=0; undo() ?}
               ?}
            ?};
            _res & TAXT.next()
         !}
      ?}
   || _res:=0; undo()
   ?};
   {? _res
   || {? ~exec('ctrlatrm','oplaty_dod',BEER.MJM,_taxs)
      || TAXX.MOD:='K';
         TAXX.put(1)
      || TAXX.MOD:='';
         TAXX.put(1)
      ?}
   ?};
   end()
?};
_res


\del_taxx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usunięcie opłaty dodatkowej dla indeksu
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć opłatę dodatkową dla indeksu?'@)
|| _oki:=1;
   do();
   TAXX.cntx_psh();
   _taxs:=TAXX.TAXS;
   _mat:=TAXX.M;
   TAXI.index('M');
   TAXI.prefix(_mat,_taxs);
   {? TAXI.first()
   || {!
      |? _oki:=TAXI.del(,1);
         _oki>1
      !}
   ?};
   TAXX.cntx_pop();
   {? ~_oki || undo() || TAXX.del(); exec('calctaxx','oplaty_dod',_taxs,_mat) ?};
   end()
?}


\chk_taxx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Poprawność danych opłaty dodatkowej dla indeksu
::----------------------------------------------------------------------------------------------------------------------
_res:='';
{? TAXX.TAXS=null()
|| FUN.info('Należy podać opłatę dodatkową dla indeksu.');
   _res:='TAXS'
|? exec('FindInSet','#table','TAXX','UNIK',TAXX.TAXS,BEER.MJM)<>null()
|| FUN.info('Przypisano już opłatę dodatkową %1 do indeksu.'@[TAXX.TAXS().TAX]);
   _res:='TAXS'
?};
_res


\renumtaxx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: renumeracja TAXX
::   WE: [_a] - ''(domyślnie) - drag&drop, 'U','D','N'-akcja do przenumerowania
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (';UDN'*_a)>1 || _a || '' ?};

TAXX.cntx_psh();
{? _tryb=''
|| _ref:=dnd_info('dest_record');
   {? TAXX.seek(_ref) || exec('zmien_lp','#dragdrop','LP','M') ?}
|| exec('zmien_lpa','#dragdrop','LP','M',,,_tryb)
?};
TAXX.cntx_pop()


\mod_taxi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Modyfikacja atrybutu dla indeksu
::----------------------------------------------------------------------------------------------------------------------
_rodz:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.RV",1,,'');
_prec:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.RP",,,0);
_form:=exec('FindInSet','#table','TAXT','R_RV','F',TAXI.TAXS,"@.TAXT.FORM",1,'M','');

_valid:='\"%1\"'[_form];
_edit:='RED'+_rodz;
_fldval:={? (';RSDTL'*_rodz)>1 || 'TAXI.VAL%1'[_rodz] || '' ?};
_prevval:=($_fldval)();
TAXI.cntx_psh();
TAXX.cntx_psh();
TAXI.win_edit(_edit);
{? TAXI.edit(($_valid)())
|| exec('value','oplaty_dod',TAXI,_rodz,_prec);
   {? TAXI.put(1) || exec('actTAXX','oplaty_dod',TAXI.ref(),_prevval,($_fldval)()) ?}
?};
TAXI.win_edit('REDL');
TAXI.cntx_pop();
TAXX.cntx_pop();
{? cur_win()='GRP' || grp_disp(TAXX,'WER')
|? cur_win()='TAX' || grp_disp(TAXX,'MAT')
?};
~~


\add_taxx_taxs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dodanie indeksów do opłaty
::----------------------------------------------------------------------------------------------------------------------
_mwin:=M.mk_sel('Wybór indeksów materiałowych'@,'P',0,'#taxs2mate',,,,,'U',,,,,'normal');

M.win_fld(_mwin,,'KTM',,,30,,1,'Indeks'@);
M.win_fld(_mwin,,'N',,,50,,1,'Nazwa'@);
M.win_fld(_mwin,,'RODZ',,,6,,1,'Rodzaj'@);
M.win_act(_mwin,0,'Formuła','&Akceptuj'@@,,,"exec('mat2taxx','!zws_par_oplm')",,1,1,"exec('mat2taxx','!zws_par_oplm')",,'A');
M.win_act(_mwin,0,'Szukaj');
M.win_act(_mwin,0,'Kolejność');
M.win_sel(_mwin);

TAXXYZ.TAXS:=TAXS.ref();
TAXS.cntx_psh();
M.cntx_psh();
M.index('ANAZ');
M.prefix('T');
{? M.first() || M.select() ?};
TAXS.cntx_pop();
M.cntx_pop();
~~


\mat2taxx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: akceptacja przypisania do materialu
::----------------------------------------------------------------------------------------------------------------------
_tab:=M.sel_aget();
M.sel_adel();
sel_exit();
{? _tab.size()
|| {? _tab.first() & FUN.ask('Czy przypisać zaznaczone indeksy do opłaty dodatkowej %1?'@[TAXXYZ.TAXS().TAX])
   || {!
      |? {? (M.clear(); M.seek(_tab.REF,)) & exec('FindInSet','#table','TAXX','UNIK',TAXXYZ.TAXS,M.ref())=null()
         || BEER.MJM:=M.ref();
            exec('add_taxx','!zws_par_oplm',1)
         ?};
         _tab.next()
      !}
   ?}
|| {? FUN.ask('Czy przypisać indeks do opłaty dodatkowej %1?'@[TAXXYZ.TAXS().TAX])
   || {? exec('FindInSet','#table','TAXX','UNIK',TAXXYZ.TAXS,M.ref())=null()
      || BEER.MJM:=M.ref();
         exec('add_taxx','!zws_par_oplm',1)
      || FUN.info('Opłata została już przypisana do indeksu.'@)
      ?}
   ?}
?};
~~


\grp_taxi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: grupowe uzupełnienie atrybutów indeksu
::----------------------------------------------------------------------------------------------------------------------
_sel:=TAXX.sel_aget();
TAXX.sel_adel();
_beermjm:=BEER.MJM;
TAXX.cntx_psh();
TAXI.cntx_psh();
{? _sel.first()
|| {? FUN.ask('Czy zmienić wskazane atrybuty opłaty wybranym indeksom?'@) & (TAXX.prefix(); TAXX.seek(_sel.REF,))
   || _taxs:=TAXX.TAXS;
      BEER.MJM:=TAXX.M;
      VAR_DEL.delete('__taxi');
      __taxi:=tab_tmp(1,'TAXT','STRING[8]',
               ,'DESC','STRING[100]',''
               ,'SEL','INTEGER',''
               ,'VALUE','STRING[150]',''
               ,'VALR','REAL',''
               ,'VALS','STRING[150]',''
               ,'VALD','DATE',''
               ,'VALT','TIME',''
               ,'VALL','STRING[1]','');
      TAXI.index('M');
      TAXI.prefix(BEER.MJM,_taxs);
      {? TAXI.first()
      || {!
         |? __taxi.blank();
            __taxi.TAXT:=TAXI.TAXT;
            __taxi.DESC:=TAXI.DESC;
            __taxi.SEL:=0;
            __taxi.VALUE:='';
            __taxi.VALL:='N';
            __taxi.add(1);
            TAXI.next()
         !}
      ?};
      _wer:=__taxi.mk_sel('Zmiana atrybutów dla indeksów','P',0,'#zmgrptaxi',,,,,'U');
      __taxi.win_fld(_wer,,'TAXT',,,8,,,'Kod'@);
      __taxi.win_fld(_wer,,'DESC',,,30,,,'Opis'@);
      __taxi.win_fld(_wer,,'SEL',,,3,,,,,'Czy wybrano [T/N]?'@,2,,"1","0");
      __taxi.win_fld(_wer,,'VALUE',,,20,,,'Wartość'@);
      __taxi.win_act(_wer,,'Formuła','&Popraw'@@,,,"exec('gmod_taxi','!zws_par_oplm')",,1,,,,'P');
      __taxi.win_act(_wer,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');
      __taxi.win_act(_wer,,'Formuła','&Usuń z modyfikacji'@@,,,"exec('gdel_taxi','!zws_par_oplm')",,,,,,'U');
      __taxi.win_sel(_wer);
      {? __taxi.select()
      || {? __taxi.first() || {! |? {? ~__taxi.SEL || __taxi.del() || __taxi.next() ?} !} ?};
         {? __taxi.first() & _sel.first()
         || {!
            |? {? (TAXX.prefix(); TAXX.seek(_sel.REF,))
               || _taxs:=TAXX.TAXS;
                  _mat:=TAXX.M;
                  {? __taxi.first()
                  || {!
                     |? TAXI.index('M');
                        TAXI.prefix(_mat,_taxs,__taxi.TAXT,);
                        {? TAXI.first()
                        || TAXI.VALR:=__taxi.VALR;
                           TAXI.VALS:=__taxi.VALS;
                           TAXI.VALD:=__taxi.VALD;
                           TAXI.VALT:=__taxi.VALT;
                           TAXI.VALL:=__taxi.VALL;
                           TAXI.VALUE:=__taxi.VALUE;
                           _rodz:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.RV",1,,'');
                           _fldval:={? (';RSDTL'*_rodz)>1 || 'TAXI.VAL%1'[_rodz] || '' ?};
                           _prevval:=($_fldval)();
                           {? TAXI.put(1) || exec('actTAXX','oplaty_dod',TAXI.ref(),_prevval,($_fldval)()) ?}
                        ?};
                        __taxi.next()
                     !}
                  ?}
               ?};
               _sel.next()
            !}
         || FUN.info('Nie wskazano żadnych atrybutówdo modyfikacji.\nNie nie zostanie zmienione.'@)
         ?}
      ?};
      VAR_DEL.delete('__taxi')
   ?}
|| FUN.info('Niczego nie zaznaczono grupowa zmiana atrybutów niedostępna.'@)
?};
TAXX.cntx_pop();
TAXI.cntx_pop();
obj_del(_sel);
BEER.MJM:=_beermjm;
~~


\gmod_taxi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: grupowe uzupełnienie atrybutów indeksu
::----------------------------------------------------------------------------------------------------------------------
_rodz:=exec('FindInSet','#table','TAXT','TAXS',__taxi.TAXT,TAXXYZ.TAXS,"@.TAXT.RV",1,,'');
_prec:=exec('FindInSet','#table','TAXT','TAXS',__taxi.TAXT,TAXXYZ.TAXS,"@.TAXT.RP",,,0);
_form:=exec('FindInSet','#table','TAXT','R_RV','F',TAXXYZ.TAXS,"@.TAXT.FORM",1,'M','');


_valid:='\"%1\"'[_form];
_edit:='RED'+_rodz;
_fldval:={? (';RSDTL'*_rodz)>1 || 'TAXI.VAL%1'[_rodz] || '' ?};
_prevval:=($_fldval)();

TAXI.TAXS:=TAXXYZ.TAXS;
TAXI.M:=BEER.MJM;
TAXI.TAXT:=__taxi.TAXT;
TAXI.DESC:=__taxi.DESC;
TAXI.VALR:=__taxi.VALR;
TAXI.VALS:=__taxi.VALS;
TAXI.VALD:=__taxi.VALD;
TAXI.VALT:=__taxi.VALT;
TAXI.VALL:=__taxi.VALL;
TAXI.win_edit(_edit);
{? TAXI.edit(($_valid)())
|| __taxi.VALR:=TAXI.VALR;
   __taxi.VALS:=TAXI.VALS;
   __taxi.VALD:=TAXI.VALD;
   __taxi.VALT:=TAXI.VALT;
   __taxi.VALL:=TAXI.VALL;
   __taxi.SEL:=1;
   exec('value','oplaty_dod',__taxi,_rodz,_prec);
   __taxi.put(1)
?};
TAXI.win_edit('REDL');
~~


\gdel_taxi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: usunięcie znacznika modyfikacji
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć znacznik modyfikacji atrybutu?'@)
|| __taxi.VALUE:='';
   __taxi.VALR:=0;
   __taxi.VALS:='';
   __taxi.VALD:=date(0,0,0);
   __taxi.VALT:=time(0,0,0);
   __taxi.VALL:='N';
   __taxi.SEL:=0;
   __taxi.put(1)
?};
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 b0eb0424695b964d9bca85ff15fd5d1ff88dd182446efa182d98113af515b181ac36ac6f118660c278afd7b9974600364e1d4c313171c1b60909a1c2e41d37bf95b5c678d8e3546028e0c0e83a430605f0f3fa9c9e06aaa2c402a2b8e434fafe3d8e11bd3b4488d9afdd05dced2fc54abd1e4c3b0bda756ecec646f6130f2586

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_mcr_dzmk.fml
:: Utworzony: 10.10.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_MCR_DZMK - Zam mc z przeniesieniem danych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Zamknięcie rozliczenia miesiąca i przeniesienie danych dla wszystkich współpracowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKRM, type=_A_OKRM, name=Wskazanie na miesiąc rozliczeniowy, required=T, keyref=T
::# kind=WEW, symbol=DATA, type=DATE, name=Data, do której zostało zamknięte rozliczenie miesiąca, required=N
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_int:=_par.int;
_akcja:=_mp.akcja();
_result:='';

_uid_OKRM:=exec('ref2uid','#table',_in.A_OKRM);
{? _uid_OKRM=''
|| _result:=exec('error','!prc_mcr_dzmk','Nie znaleziono miesiąca rozliczeniowego.'@)
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uid_OKRM);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uid_OKRM);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('zamknij_mc','!prc_mcr_dzmk',_in,_int);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _par.mp.save(_int);
         _mp.keep()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Zamknięcie rozliczenia miesiąca i przeniesienie danych dla wszystkich współpracowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_tab:=params_exec('opis_mc','okres',_mp.load(exec('kind_in','#b_port')).A_OKRM);

{? _tab.ZAW_DANE='T'
|| 'Zamknij rozliczenie miesiąca %1'@@[date(_tab.R,_tab.M,)$8]
|| 'Zamknij rozliczenie miesiąca'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Informacja o rodzaju błędu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Zamknięcie miesiąca rozliczeniowego nie jest możliwe.'@,_a]


\opis_mc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Opis wybranego miesiąca rozliczeniowego.
::   WE: _a - [REFERENCE] Wskazanie na wybrany miesiąc rozliczeniowy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mc:='';
A_OKRM.cntx_psh();
{? A_OKRM.seek(_a,,1)
|| _mc:='%1'@[date(A_OKRM.R,A_OKRM.M,)$8]
?};
A_OKRM.cntx_pop();
_mc


\zamknij_mc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a - Tablica z parametrami wejściowymi.
::       _b - Tablica z parametrami wewnętrznymi.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
A_OKRM.cntx_psh();
{? A_OKRM.seek(_a.A_OKRM,,1)
|| {? A_OKRM.S_PLAN='O'
   || _result:=exec('error','!prc_mcr_dzmk','Miesiąc rozliczeniowy jest odblokowany do planowania.'@)
   |? A_OKRM.S='Z'
   || _result:=exec('error','!prc_mcr_dzmk','Miesiąc rozliczeniowy jest już zamknięty.'@)
   || _tab:=tab_tmp(,
         'RESULT','INTEGER','',
         'DATA','DATE','Do daty'
      );
      _ed:=_tab.mk_edit('Zamknięcie miesiąca rozliczeniowego %1'@[date(A_OKRM.R,A_OKRM.M,)$8],0);
      _tab.win_efld(_ed,AH,'H',,,,,,'Miesiąc rozliczeniowy %1 zostanie zamknięty.'@[date(A_OKRM.R,A_OKRM.M,)$8]);
      _tab.win_efld(_ed,,'DATA',,,10,,,'Przeniesienie rozliczenia do daty'@);
      _tab.DATA:={? var_pres('DATA',_b)=type_of(#0) || _b.DATA || date(A_OKRM.R,A_OKRM.M,A_OKRM.DZ~3) ?};
      exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:F2'",,
         exec('help_red_zakoncz','#window','PKD_H'),exec('text_red_zakoncz','#window','PKD_H'));
      exec('ok_esc','#window',_tab,_ed,1,"cur_tab().RESULT:=2; 'key:F2'","cur_tab().RESULT:=0; 'key:Esc'",,,
         exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
      _tab.win_edit(_ed);
      _tab.edit(
         "  {? cur_tab().DATA~1=A_OKRM.R & cur_tab().DATA~2=A_OKRM.M
            || 1
            || FUN.emsg('%1\n%2'['Data spoza zamykanego okresu rozliczeniowego.'@,'Proszę poprawić datę.'@]);
               cur_tab().DATA:=date(A_OKRM.R,A_OKRM.M,A_OKRM.DZ~3);
               'DATA'
            ?}
         "
      );
      {? _tab.RESULT=1
      || exec('a_okrm_wer_bfr','prc_mscrozlicz',0,_tab.DATA);
         A_OKRM.seek(_a.A_OKRM,,1);
         _result:=A_OKRM.S='Z'
      |? _tab.RESULT=2
      || _b.DATA:=_tab.DATA;
         _result:=0
      || _result:='Zrezygnowano z zamknięcia miesiąca rozliczeniowego %1.'@[date(A_OKRM.R,A_OKRM.M,)$8]
      ?};
      obj_del(_tab)
   ?}
|| _result:=exec('error','!prc_mcr_dzmk','Nie znaleziono miesiąca rozliczeniowego.'@)
?};
A_OKRM.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 a918be2bd1c049e3ba715a3c0828fd3e0949c20a3b41b6e0ec2e80e96fa697b5eaec3d42690fdcd4df3cb2931d02a0f623f9f411dde3f1e05d920e24ce4eb02d6c3ec81f3938e70309caa0c60c8301dbafd9bc2bc6b3eae7c2a15b9cbaadcbd21971502d822cc98c0a39f54232b8c1e3031465341fc186fee86e57cbf80fc750

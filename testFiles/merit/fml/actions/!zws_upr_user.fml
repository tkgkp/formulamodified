:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zws_upr_users.fml
:: Utworzony: 02.12.2014
:: Autor: [rr]
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_UPR_USERS
::            Dodawanie użytkowników
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Czynność ZWS_UPR_USER - dołączenie nowego użytkownika
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejsciowe czynnosci
::       _int - [obj_new] - parametry wewnetrzne czynnosci
::       _out - [obj_new] - parametry wyjsciowe czynnosci
::       _mp  - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::# kind=WE,   symbol=DANE,   type=STRING,   name=Dane nowego użytkownika,   required=N
::# kind=WE,   symbol=LOGIN,  type=STRING,   name=Login nowego użytkownika,  required=N
::# kind=WE,   symbol=EMAIL,  type=STRING,   name=Email nowego użytkownika,  required=N
::# kind=WE,   symbol=TEL,  type=STRING,   name=Telefon nowego użytkownika,  required=N
::# kind=WE,   symbol=DIALOG,  type=STRING,   name=Czy ma być pokazywane okno redakcji (T/N),  required=N
::# kind=WY,   symbol=USER,   type=_USERS,   name=Wskazanie na użytkownika,  required=N

_args:=params_get();
_IsEnv:=(var_pres('_args')>100 & var_pres('mp',_args)>100);
{? _IsEnv
|| _we:=_args.in;
   _wy:=_args.out;
   _mp:=_args.mp;
   {? var_pres('DANE',_we)=type_of(~~)  || _we.DANE:='' ?};
   {? var_pres('LOGIN',_we)=type_of(~~) || _we.LOGIN:='' ?};
   {? var_pres('EMAIL',_we)=type_of(~~) || _we.EMAIL:='' ?};
   {? var_pres('TEL',_we)=type_of(~~) || _we.TEL:='' ?};
   {? var_pres('DIALOG',_we)=type_of(~~) || _we.DIALOG:='T' ?}
|| _we:=obj_new('DANE','LOGIN','DIALOG','EMAIL','TEL');
   _we.DANE:='';
   _we.LOGIN:='';
   _we.EMAIL:='';
   _we.TEL:='';
   _we.DIALOG:='T'
?};

_user:=exec('add_action','users',_we.DANE,_we.LOGIN,_we.DIALOG);

USERS.cntx_psh();
USERS.prefix();
{? USERS.seek(_user)
|| USERS.EMAIL:=_we.EMAIL;
   USERS.TEL:=_we.TEL;
   exec('users_put','#users')
?};
USERS.cntx_pop();

{? _IsEnv
|| _mp.save('OUT','USER',_user);
   _mp.done()
?};
''


\desc
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: [rr] [17.00]
:: OPIS: Opis ZWS_UPR_USER - dołączenie nowego użytkownika
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejsciowe czynnosci
::       _int - [obj_new] - parametry wewnetrzne czynnosci
::       _out - [obj_new] - parametry wyjsciowe czynnosci
::       _mp  - obiekt odpowiedzialny za obsluge procesu
::   WY: zwraca opis Zadania
::---------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_IsEnv:=obj_len(_args)>=1;

{? _IsEnv
|| _aa:=_args.mp;
   _we:=_aa.load(exec('kind_in','#b_port'));
   {? var_pres('DANE',_we)=type_of(~~)  || _we.DANE:='' ?};
   {? var_pres('LOGIN',_we)=type_of(~~) || _we.LOGIN:='' ?};
   _dane:=+(form(_we.LOGIN)+form(_we.DANE));
   _dane1:={? _dane
           || ' ('+{? form(_we.LOGIN)<>'' || ' login: %1'@[_we.LOGIN] || '' ?}
                  +{? form(_we.DANE)<>'' || ' dane: %1'@[_we.DANE] || '' ?}+' ).'
           || '.'
           ?};
   _desc:='Dodać do systemu nowego użytkownika%1'@@[_dane1]
|| _desc:=''
?};
_desc


\users_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli USERS.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? USERS.OSOBA<>null() & USERS.AKT='T' & exec('_osoba_unique','users',USERS.OSOBA,null())=0
|| undo('Wskazana osoba [%1] jest już przypisana do innego, aktywnego użytkownika.'@
      [exec('record','#to_string',USERS.OSOBA)]
   );
   return(0)
?};
1


\users_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz po dołączeniu rekordu do tabeli USERS
::   WE: _a - wynik akcji dołączania: 1 - udało się dodać rekord, 0 - rekord nie został dodany
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie dodano rekordu
{? ~_a | do_state()<>1 || return() ?};

_type:='add';
_key:=$USERS.ref();
:: Ustaw semafor operacji hurtowej
exec('add','#bulk',USERS,'add',_key);
:: Zakładaj domyślne uprawnienia z wyłączoną aktualizacją
exec('upr_usr_add','schemat',USERS.ref());
:: Usuń semafor hurtowego przetwarzania
exec('del','#bulk',USERS,'add',_key);

:: Ustaw znacznik uprawnień na "brak"
exec('Perm','#object');
FIRMA.cntx_psh();
FIRMA.prefix();
_loop:=FIRMA.first();
{!
|? _loop
|! Perm.setPermission('UD_SKL',FIRMA.ref(),USERS.ref(),0);
   _loop:=FIRMA.next()
!};
FIRMA.cntx_pop();
~~


\users_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Wyzwalacz po poprawieniu rekordu do tabeli USERS
::   WE: _a - wynik akcji dołączania: 1 - udało się dodać rekord, 0 - rekord nie został dodany
::----------------------------------------------------------------------------------------------------------------------
:: zakończ, jeśli nie poprawiono rekordu
{? ~_a | do_state()<>1 || return() ?};

{? bfld('OSOBA')<>USERS.OSOBA & exec('prefix','#table',BIPPYTO,,'first',,'UNIQ',USERS.ref())
|| undo('Użytkownik jest osobą obsługującą pytania z portalu. Zmiana przypisania osoby nie jest możliwa.'@);
   return()
?};

{? USERS.AKT<>bfld('AKT') | USERS.JTERM<>bfld('JTERM')
|| exec('upr_usr_add','schemat',USERS.ref())
?};

_por:=exec('lic','#b_domain','POR');

:: synchronizacja tabeli etypy_p
{? _por & bfld('OSOBA')<>USERS.OSOBA
|| exec('clear_etypyp_users','obiegi2',bfld('OSOBA'),USERS.OSOBA);
   exec('add_etypyp_users','obiegi2',bfld('OSOBA'))
?};

:: wymuszenie modyfikacji pracowników, jeżeli zmiana loginu webowego albo logowania webowego albo przypisania osoby
:: albo zmiana sposobu autoryzacji w Portal HR albo zmiana adresu e-mail albo zmiana logowania CAS
{? _por &
   (  bfld('WEBLOGIN')<>USERS.WEBLOGIN |
      bfld('PORTAL')<>USERS.PORTAL |
      bfld('OSOBA')<>USERS.OSOBA |
      bfld('OF365')<>USERS.OF365 |
      bfld('LOG2')<>USERS.LOG2 |
      bfld('EMAIL')<>USERS.EMAIL |
      bfld('CAS')<>USERS.CAS |
      bfld('CASUID')<>USERS.CASUID
   ) & USERS.OSOBA<>null()
|| P.trig_off('*','*');
   P.cntx_psh();
   P.index('OSOBA');
   P.prefix(USERS.OSOBA);
   {? P.first()
   || {!
      |? P.put(,1);
         P.next()
      !}
   ?};
   P.cntx_pop();
   P.trig_on('*','*')
?};

:: wymuszenie aktualizacji tabeli USERSF w przypadku zmiany sposobu autoryzacji w Portal HR
{? _por &
   (  bfld('OSOBA')<>USERS.OSOBA |
      bfld('KOD')<>USERS.KOD |
      bfld('DANE')<>USERS.DANE |
      bfld('OF365')<>USERS.OF365 |
      bfld('LOG2')<>USERS.LOG2 |
      bfld('EMAIL')<>USERS.EMAIL |
      bfld('CAS')<>USERS.CAS |
      bfld('CASUID')<>USERS.CASUID
   )
|| USERSF.trig_off('*','*');
   USERSF.index('USER');
   USERSF.prefix(USERS.ref());
   {? USERSF.first()
   || {!
      |? USERSF.put(,1);
         USERSF.next()
      !}
   ?};
   USERSF.trig_on('*','*')
?};

:: wymuszenie modyfikacji uprawnień na portalu, jeżeli zmiana loginu webowego albo znacznika portalowego
{? _por & USERS.OSOBA<>null() & (bfld('WEBLOGIN')<>USERS.WEBLOGIN | bfld('PORTAL2')<>USERS.PORTAL2)
|| ETYPY_P.trig_off('*','*');
   P.cntx_psh(); ETYPY_P.cntx_psh();
   P.index('OSOBA');
   P.prefix(USERS.OSOBA);
   {? P.first()
   || {!
      |? ETYPY_P.index('PRAC');
         ETYPY_P.prefix(P.ref());
         {? ETYPY_P.first()
         || {!
            |? ETYPY_P.put(,1);
               ETYPY_P.next()
            !}
         ?};
         P.next()
      !}
   ?};
   P.cntx_pop(); ETYPY_P.cntx_pop();
   ETYPY_P.trig_on('*','*')
?};

:: aktualizacja uprawnień do funkcjonalności portalu, jeżeli zmiana loginu webowego
{? _por & bfld('WEBLOGIN')<>USERS.WEBLOGIN
|| _par:=obj_new('WEBLOGIN_b','WEBLOGIN_a');
   _par.WEBLOGIN_b:=bfld('WEBLOGIN');
   _par.WEBLOGIN_a:=USERS.WEBLOGIN;
   exec('add','portaloa','users_puta2portalu',USERS.uidref(),_par)
?};
~~


\users_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz przed usunięciem rekordu z tabeli USERS
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',USERS_FZ,'FK_USR',USERS.ref()) &
exec('upr_usr_del','schemat',USERS.ref())

:Sign Version 2.0 jowisz:1045 2024/01/23 11:35:10 9ce65ca4f6bda5d88429ca835809068a816b8c017976e3f811e5fa97a0c1881759bed0c31143f30a29f179b673ec559314212414580d9ec44ea323c2d9137bcd71d4defb3fa5f60e695a3e3d75fbd033f8aa37e832b20515b2ceb1e1d925307be5f16b4a5473baecca2b8f38729f116df2ee9ddbc63f1956216371b7dcb21ca8

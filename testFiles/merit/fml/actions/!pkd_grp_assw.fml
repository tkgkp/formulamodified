:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_grp_assw.fml
:: Utworzony: 28.06.2018
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_GRP_ASSW - Gr. wprowdz. stał. składników.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Gr. wprowdz. stał. składników - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
_choice:=FUN.choice(
   'Wybierz sposób grupowego wprowadzania stałych składników'@,,
   'Rejestracja ręczna'@,'Przetwarzanie automatyczne'@,'Wycofanie ostatniej operacji'@
);
{? _choice=1
|| exec('rmain','!pkd_grp_assw')
|? _choice=2
|| exec('amain','!pkd_grp_assw')
|? _choice=3
|| exec('_undo_run','grupskl','S')
?}


\rmain
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Rejestracja ręczna - formuła główna.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rmain','grupskl','S',"params_exec('aktualizuj','!pkd_grp_assw',_a,0,_b,_c)")


\amain
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Przetwarzanie automatyczne - formuła główna.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('amain','grupskl','S',"params_exec('aktualizuj','!pkd_grp_assw',_a,1,_b,_c)",'PKD')


\aktualizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła odpowiedzialna za aktualizację stałych składników, wykonywana dla bieżącego pracownika.
::   WE:  _a  [TABLE]  - Tabela z definicją zestawu do zastosowania (patrz \_zestaw/grupskl.fml). Zakłada się,
::                       że dzedzina tabeli jest ustawiona prawidłowo.
::       [_b] [NUMBER] - Czy prezentować rekord nagłówkowy definicji? (0*/1)
::       [_c] [STRING] - Parametr nieużywany (dla zgodności z przetwarzaniem składników list płac).
::       [_d] [NUMBER] - Parametr nieużywany (dla zgodności z przetwarzaniem składników list płac).
::   WY: Status operacji (0/1).
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(SYSLOG)
|| _DEF:=_a
|| return()
?};
_def:=var_pres('_b')=type_of(0) & _b;

_par:=params_get();
_KOMM:=_par.KOMM;
_ikony:=_par.ikony;
_root:=_par.root;

_UNDO:=_par.UNDO;
_undo:='';

_sukces:=1;
do();
_loop:=_DEF.first();
{!
|? _loop
|! {? _def
   || _KOMM.sect_beg(
         'DEFINICJA'@+' | '+
         'Składnik: %1 - %2'@ [$_DEF.RN,_DEF.RT]+' | '+
         'Akcja: %1'@ [_DEF.AC]+' | '+
         {? _DEF.AC<>'--'
         || 'Wartość: %1'@ [form(_DEF.KW,15,2)]+' | '
         || ''
         ?}+
         'Daty: %1 - %2'@ [$_DEF.OD,$_DEF.DO]+' | '+
         'Konto: %1'@ [_DEF.KT]+ ' | '
      )
   ?};

   _zmiana:=0;

   _d0:=date(0,0,0);
   _dt:=
      {? _DEF.DO<>_d0
      || _DEF.DO
      |? _DEF.OD<>_d0
      || _DEF.OD
      || date()
      ?};
   _kk:=exec('_kref','grupskl',_DEF.KT,_dt);
   {? _kk=null()
   || _KOMM.add('Ustalenie konta kosztów nie powiodło się'@,_ikony.pM)
   ?};
   LSS.prefix(P.ref(),_DEF.RN,_kk);
   LSS.S();
   LSS.KK();

   {? _DEF.AC='=='
::    Przypisz wartość
   || {? LSS.find_key(_DEF.OD)
::       Istnieje składnik z taką samą datą. Zmieniamy tylko kwotę i datę końcową.
      || exec('_log_lss','grupskl',_KOMM,_ikony.przed,'Składnik przed modyfikacją'@);
         _kwn:={? _DEF.KW=0 || ($_DEF.memo_txt(,1,'FM'))(1) || _DEF.KW ?};
         {? LSS.KW<>_kwn | LSS.DO<>_DEF.DO
         || _undo+=exec('undo','!pkd_grp_assw','put');
            LSS.KW:=_kwn;
            LSS.DO:=_DEF.DO;
            {? LSS.put(1)
            || _zmiana:=LSS.DO<>_DEF.DO;
               exec('_log_lss','grupskl',_KOMM,_ikony.po,'Składnik po modyfikacji'@);
               _KOMM.add('Wartość składnika została zmieniona'@,_ikony.sC)
            || _KOMM.add('Zmiana wartości składnika nie powiodła się'@,_ikony.pC);
               _sukces:=0
            ?}
         || exec('_log_lss','grupskl',_KOMM,_ikony.po,'Składnik po modyfikacji'@);
            _KOMM.add('Zmiana wartości składnika nie była wymagana'@,_ikony.sM)
         ?}
      || LSS.blank();
         LSS.P:=P.ref();
         LSS.S:=__RUB.ref(_DEF.RN);
         LSS.OD:=_DEF.OD;
         LSS.DO:=_DEF.DO;
         LSS.KW:={? _DEF.KW=0 || ($_DEF.memo_txt(,1,'FM'))(0) || _DEF.KW ?};
         LSS.KK:=_kk;
         {? LSS.add(1)
         || _undo+=exec('undo','!pkd_grp_assw','add');
            _zmiana:=1;
            exec('_log_lss','grupskl',_KOMM,_ikony.po,'');
            _KOMM.add('Składnik został utworzony'@,_ikony.sC)
         || _KOMM.add('Utworzenie składnika nie powiodło się'@,_ikony.pC);
            _sukces:=0
         ?}
      ?}

   |? _DEF.AC='--'
::    Usuń
   || {? LSS.find_key(_DEF.OD)
      || exec('_log_lss','grupskl',_KOMM,_ikony.przed,'Składnik usuwany'@);
         _undo+=exec('undo','!pkd_grp_assw','del');
         {? LSS.del(1,1)
         || _KOMM.add('Składnik został usunięty'@,_ikony.sC)
         || _KOMM.add('Usunięcie składnika nie powiodło się'@,_ikony.pC);
            _sukces:=0
         ?}
      || _KOMM.add('Odnalezienie składnika nie powiodło się'@,_ikony.pC);
         _sukces:=0
      ?}

   |? _DEF.AC='>@'
::    Zamknij
   || _found:=0;
      {? LSS.first()
      || {!
         |? {? LSS.DO=_d0
            || _found+=1;
               _undo+=exec('undo','!pkd_grp_assw','put');
               exec('_log_lss','grupskl',_KOMM,_ikony.przed,'Składnik przed zmianą daty'@);
               LSS.DO:=_DEF.DO;
               {? LSS.put(1)
               || exec('_log_lss','grupskl',_KOMM,_ikony.po,'Składnik po zmianie daty'@);
                  _KOMM.add('Daty obowiązywania zostały zmienione',_ikony.sC)
               || _KOMM.add('Zmiana dat obowiązywania składnika nie powiodła się',_ikony.pC);
                  _sukces:=0
               ?}
            ?};
            LSS.next()
         !}
      ?};
      {? ~_found
      || _KOMM.add('Brak składników do zamknięcia'@,_ikony.pC);
         _sukces:=0
      ?}

::    Dodaj / Odejmij wartość / procent
   || {? LSS.find_key(_DEF.OD)
      || exec('_log_lss','grupskl',_KOMM,_ikony.przed,'Składnik przed modyfikacją'@);
         _kw:={? _DEF.KW=0 || ($_DEF.memo_txt(,1,'FM'))(1) || _DEF.KW ?};
         _kwo:=LSS.KW;
         _kwn:=($('(_a%1_b' [1+_DEF.AC]+{? _DEF.AC+1='%' || '*_a*0.01' || '' ?}+')$2'))(_kwo,_kw);
         {? _kwn<=0
         || _KOMM.add('Wartość składnika po zmianie (%1) nie jest dodatnia'@ [form(_kwn,,2,'9.')],_ikony.pC);
            _sukces:=0
         |? LSS.KW<>_kwn | LSS.DO<>_DEF.DO
         || _undo+=exec('undo','!pkd_grp_assw','put');
            LSS.KW:=_kwn;
            LSS.DO:=_DEF.DO;
            {? LSS.put(1)
            || _zmiana:=LSS.DO<>_DEF.DO;
               exec('_log_lss','grupskl',_KOMM,_ikony.po,'Składnik po modyfikacji'@);
               _KOMM.add('Wartość składnika została zmieniona'@,_ikony.sC)
            || _KOMM.add('Zmiana wartości składnika nie powiodła się'@,_ikony.pC);
               _sukces:=0
            ?}
         || exec('_log_lss','grupskl',_KOMM,_ikony.po,'Składnik po modyfikacji'@);
            _KOMM.add('Zmiana wartości składnika nie była wymagana'@,_ikony.sM)
         ?}
      |? LSS.find_le(_DEF.OD)
      || exec('_log_lss','grupskl',_KOMM,_ikony.przed,'Składnik bazowy'@);
         _kw:={? _DEF.KW=0 || ($_DEF.memo_txt(,1,'FM'))(0) || _DEF.KW ?};
         _kwo:=LSS.KW;
         _kwn:=($('(_a%1_b' [1+_DEF.AC]+{? _DEF.AC+1='%' || '*_a*0.01' || '' ?}+')$2'))(_kwo,_kw);
         {? _kwn<=0
         || _KOMM.add('Wartość składnika po zmianie (%1) nie jest dodatnia'@ [form(_kwn,,2,'9.')],_ikony.pC);
            _sukces:=0
         || LSS.KW:=_kwn;
            LSS.OD:=_DEF.OD;
            LSS.DO:=_DEF.DO;
            {? LSS.add(1)
            || _undo+=exec('undo','!pkd_grp_assw','add');
               _zmiana:=1;
               exec('_log_lss','grupskl',_KOMM,_ikony.po,'Składnik utworzony'@);
               _KOMM.add('Składnik został utworzony'@,_ikony.sC)
            || _KOMM.add('Utworzenie składnika nie powiodło się'@,_ikony.pC);
               _sukces:=0
            ?}
         ?}
      || _KOMM.add('Odnalezienie składnika nie powiodło się'@,_ikony.pC);
         _sukces:=0
      ?}
   ?};

   {? _zmiana & LSS.first()
::    Usuwamy ewentualne kolizje.
   || _d0:=date(0,0,0);
      {!
      |? _zmiana:=0;
         {? LSS.OD<>_DEF.OD | LSS.DO<>_DEF.DO
         || {? LSS.OD<_DEF.OD
            || {? LSS.DO=_d0 | _DEF.OD<=LSS.DO
               || _undo+=exec('undo','!pkd_grp_assw','put');
                  exec('_log_lss','grupskl',_KOMM,_ikony.przed,'Składnik przed zmianą daty'@);
                  LSS.DO:=_DEF.OD-1;
                  {? LSS.put(1)
                  || exec('_log_lss','grupskl',_KOMM,_ikony.po,'Składnik po zmianie daty'@);
                     _KOMM.add('Daty obowiązywania zostały zmienione',_ikony.sC)
                  || _KOMM.add('Zmiana dat obowiązywania składnika nie powiodła się',_ikony.pC);
                     _sukces:=0
                  ?}
               ?}
            || {? _DEF.DO=_d0 | LSS.DO<=_DEF.DO
               || _undo+=exec('undo','!pkd_grp_assw','del');
                  exec('_log_lss','grupskl',_KOMM,_ikony.przed,'Składnik usuwany (konflikt)'@);
                  _ret:=LSS.del(1,1);
                  _zmiana:=_ret=2;
                  {? _ret>0
                  || _KOMM.add('Składnik został usunięty'@,_ikony.sC)
                  || _KOMM.add('Usunięcie składnika nie powiodło się'@,_ikony.pC);
                     _sukces:=0
                  ?}
               |? LSS.OD<=_DEF.DO
               || _undo+=exec('undo','!pkd_grp_assw','put');
                  exec('_log_lss','grupskl',_KOMM,_ikony.przed,'Składnik przed zmianą daty'@);
                  LSS.OD:=_DEF.DO+1;
                  {? LSS.put(1)
                  || exec('_log_lss','grupskl',_KOMM,_ikony.po,'Składnik po zmianie daty'@);
                     _KOMM.add('Daty obowiązywania zostały zmienione',_ikony.sC)
                  || _KOMM.add('Zmiana dat obowiązywania składnika nie powiodła się',_ikony.pC);
                     _sukces:=0
                  ?}
               ?}
            ?}
         ?};
         _zmiana | LSS.next()
      !}
   ?};

   LSS.prefix();

   {? _def
   || _KOMM.sect_end()
   ?};

   _loop:=_DEF.next()
!};
{? end()
|| {? _undo<>''
   || _txt:=
         '_KOMM:=_a; '
         '_ikony:=_b; '
         '_root:=_c; '
         '_sukces:=1; '
         '{? P.seek(''%1'') ' [P.uidref()]+
         '|| _sp:=_KOMM.sect_beg(exec(''P'',''#to_string''),_ikony.P); '+
             _undo+
            '_KOMM.sect_end() '
         '|| _sp:=_KOMM.add(''Odnalezienie pracownika nie było możliwe'',_ikony.pC); '
            '_sukces:=0 '
         '?}; '
         '{? _sukces '
         '|| _lp:=_root.slp; '
            '_root.sile+=1 '
         '|| _lp:=_root.plp; '
            '_root.pile+=1 '
         '?}; '
         '_KOMM.chngroot(_sp,_lp) ';
      exec('_undo_add','grupskl',_UNDO,_txt)
   ?}
|| _KOMM.add('W trakcie przetwarzania wystąpił błąd'@,_ikony.pC);
   _sukces:=0
?};
_sukces


\undo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Formuła tworząca ślad zmian do późniejszego wycofania.
::   WE: _a [STRING] - Rodzaj wykonywanej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_akcja:={? var_pres('_a')=type_of('') || _a || '' ?};

_tresc:='';
{? _akcja='put'
|| _tresc+=
      '{? LSS.seek(''%1'') ' [LSS.uidref()]+
      '|| exec(''_log_lss'',''grupskl'',_KOMM,_ikony.przed,''Składnik przed modyfikacją''@); '+
         'LSS.OD:=date(%1,%2,%3); ' [$(LSS.OD~1),$(LSS.OD~2),$(LSS.OD~3)]+
         'LSS.DO:=date(%1,%2,%3); ' [$(LSS.DO~1),$(LSS.DO~2),$(LSS.DO~3)]+
         'LSS.KW:=%1; ' [form(LSS.KW,,2,'9.')]+
         '{? LSS.put(1) '
         '|| exec(''_log_lss'',''grupskl'',_KOMM,_ikony.po,''Składnik po modyfikacji''@); '
            '_KOMM.add(''Wartość składnika została przywrócona''@,_ikony.sC) '
         '|| _KOMM.add(''Zmiana wartości składnika nie powiodła się''@,_ikony.pC); '
            '_sukces:=0 '
         '?} '
      '|| _KOMM.add(''Odnalezienie zmienionego składnika nie powiodło się''@,_ikony.pC); '
         '_sukces:=0 '
      '?}; '

|? _akcja='add'
|| _tresc+=
      '{? LSS.seek(''%1'') ' [LSS.uidref()]+
      '|| exec(''_log_lss'',''grupskl'',_KOMM,_ikony.przed,''Składnik usuwany''@); '
          '{? LSS.del(1,1)>0 '
          '|| _KOMM.add(''Składnik został usunięty''@,_ikony.sC) '
          '|| _KOMM.add(''Usunięcie składnika nie powiodło się''@,_ikony.sC) '
          '?} '
      '|| _KOMM.add(''Odnalezienie dodanego składnika nie powiodło się''@,_ikony.pC); '
         '_sukces:=0 '
      '?}; '

|? _akcja='del'
|| _tresc+=
      '{? R.seek(''%1'') ' [R.uidref()]+
      {? LSS.KK || '& KK.seek(''%1'') ' [KK.uidref()] || '' ?}+
      '|| LSS.blank(1); '
         'LSS.P:=P.ref(); '
         'LSS.S:=R.ref(); '
         'LSS.OD:=date(%1,%2,%3); ' [$(LSS.OD~1),$(LSS.OD~2),$(LSS.OD~3)]+
         'LSS.DO:=date(%1,%2,%3); ' [$(LSS.DO~1),$(LSS.DO~2),$(LSS.DO~3)]+
         'LSS.KW:=%1; ' [form(LSS.KW,,2,'9.')]+
         {? LSS.KK
         || 'LSS.KK:=KK.ref(); '
         || ''
         ?}+
         '{? LSS.add(1) '
         '|| exec(''_log_lss'',''grupskl'',_KOMM,_ikony.przed,''Składnik odtworzony''@); '
            '_KOMM.add(''Wartość składnika została odtworzona''@,_ikony.sC) '
         '|| _KOMM.add(''Odtworzenie składnika nie powiodło się''@,_ikony.pC); '
            '_sukces:=0 '
         '?} '
      '|| _KOMM.add(''Odnalezienie rubryki lub konta kosztów nie powiodło się''@,_ikony.pC); '
         '_sukces:=0 '
      '?}; '
?};

_tresc

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:53 a5763cc1d3cad2ac39b194e36c8044a94057a5673bbe84342c11cd27fad8189099ee75739d2c1861d6cb80f12f60a26f3c6390312d8ab5abbf538968ec9654c2629faa37359cb0aa6d321a73a9baf7ab7941ed6da219820c8173c271fc9421c9ca20d826dba40857af39ea96077377d31759a94cf5ce9706ae97cdf7adc7d529

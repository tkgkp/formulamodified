:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdw_rzap.fml
:: Utworzony: 21.11.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności PSZ_SDW_RZAP - Zapotrzebowania własne.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Zapotrzebowania własne - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('env_wt','psz')
|| return()
?};

web_params_set(
   exec('obj_ntab_set','#array',web_params_get(),
::    Po odświeżeniu okna WEBWER tabeli SZK_ZAP - wyszarzanie przycisków.
      'SZK_ZAP_WEBWER_AR',"exec('szk_zap_webwer_ar','!psz_sdw_rzap')",
::    Pracownik, którego dotyczy zapotrzebowanie (tutaj: właściciel widoku) - formułą zwracająca SQL-ref'a.
      'SZK_ZAP_P',$('\''+$P.ref()+'\''),
::    Typ zgłoszenia: P - zgłoszenie pracownika.
      'SZK_ZAP_ZGL_TYP','P',
::    Podstawa realizacji: R - bieżąca potrzeba.
      'SZK_ZAP_POD_REAL','R',
::    Dziedzina konta kosztowego (formuła).
      'BPMN_KK_DOM',$('\''+BPMN.KK_DOM+'\'')
   )
);

SZK_ZAP.cntx_psh();
SZK_ZAP.index('PT');
SZK_ZAP.prefix(P.ref());
_ws:='WEBGW';
SZK_ZAP.web_win_init(_ws,,'title=" (%1)",grayed=AO' ['własne'@]);
SZK_ZAP.web_select(_ws);
SZK_ZAP.cntx_pop();
~~


\szk_zap_webwer_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: "Po odświeżeniu" dla okna WEBWER tabeli SZK_ZAP. Formuła wywoływana dynamicznie (poprzez zmienną
::       web_params_get().SZK_ZAP_WEBWER_AR). Odpowiada za ukrywanie przycisków.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Wstępnie blokujemy wszystko.
_ag1:='DPUAO';
_ag0:='D';

{? SZK_ZAP.get()
|| {? SZK_ZAP.SZK_PRAC=null() & SZK_ZAP.ZO_TEST=null()
::    Jest bieżący rekord jeszcze nie "skonsumowane" (nie odbyły się związane z nimi szkolenia) i
::    zapotrzebowanie nie jest związane z oceną,
::    to niektóre akcje mogą być dostępne.

   || _ag1:='AO';
::    Dla zapotrzebowań własnych nie ma akcji Akceptuj/Odrzuć. Są one zarezerwowane dla przełożonego. A jeżeli pracownik
::    nie ma przełożonego, to zapotrzebowanie automatycznie się akceptuje.
      _ag0:='';

      {? SZK_ZAP.P=SZK_ZAP.ZGL_KTO
      || SLO_KOD.cntx_psh();
         SLO_KOD.prefix();
         {? SZK_ZAP.WER_WYN().KOD<>'X'
         || _ag1+='P'
         ?};
         SLO_KOD.cntx_pop()
      || _ag1+='PU'
      ?}

::    Na podstawie zapotrzebowania odbyło się szkolenie lub zapotrzebowanie związane z oceną
::    - blokujemy Popraw/Usuń/Akceptuj/Odrzuć.
   || _ag1:='PUAO';
      _ag0:=''
   ?}

:: Jeżeli dziedzina jest pusta (a tak zinterpretujmy brak możliwości wczytania bieżącego rekordu), to wycofajmy się
:: z ustawień wstępnych (blokady wszystkich akcji) i pozwólmy na "Dołącz".
|| _ag0:=''
?};

web_top_tab(0,0).web_win_opt(web_top_win(0,0),web_top_ident(0),'grayed=%1:%2' [_ag1,_ag0]);
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:59 a25573d87e29bdad90cca2b49b1a2b1ca31cc581c499e7b10bd8e2affd03dbec1df579e49c7f67dec15fc0015322502644272e59cb1be795dcb71ea28b33caae09c611847f5b58ab7d68cb23d4073d6d1638a330cb46cf9acce8493e070e9bcf0ebd9779e1cddc9c8fa2fe08ba6ea3cfd50264ea957e295eb9a66367ea26b627

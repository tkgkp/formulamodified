:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_faw_eakp.fml
:: Utworzony: 07.06.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_EAKP - Akceptacja wniosku w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja wniosku w obiegu - główna formuła czynności OBE_FAW_EAKP
::----------------------------------------------------------------------------------------------------------------------
::# portal=STD_PC_E_AuthorisationPath_SLO;STD_PC_E_Acceptance_Accept_PRC;STD_PC_E_Acceptance_Hist_SEC; ^
::# STD_PC_E_Acceptance_Own_SEC;STD_PC_E_Acceptance_Reject_PRC;STD_PC_E_Acceptance_SLO;STD_PC_E_AcceptanceKPI_SLO; ^
::# STD_PC_E_AcceptanceSummary_SLO;STD_PC_E_REQUEST_ACCEPT_PRC;STD_PC_E_REQUEST_ATTACHMENTADD_PRC; ^
::# STD_PC_E_REQUEST_REJECT_PRC; ^
::# STD_PC_E_PERSONATTACHMENT_GETSIG_SLO_PRC;STD_PC_E_PERSONATTACHMENT_OVR_SLO_PRC; ^
::# STD_PC_E_PERSONATTACHMENT_SIG_SLO_PRC;STD_PC_E_PERSONATTACHMENT_SIGHND_SLO_PRC; ^
::# STD_PC_E_PERSONATTACHMENT_SIGSMP_SLO_PRC; ^
::# STD_PC_E_PERSONATTACHMENT_MYCERT_SLO_PRC;STD_PC_E_PERSONMYCERT_ADDPDF_SLO_PRC; ^
::# STD_PC_E_PERSONMYCERT_ADDSIMPLY_SLO_PRC;STD_PC_E_PERSONMYCERT_DEL_SLO_PRC;STD_PC_E_PERSONMYCERT_SLO; ^
::# STD_PC_E_PersonRequest_GenerateReport_PRC; ^
::# STD_PC_S_PersonRequestCostShare_PRC_Accept;STD_PC_S_PersonRequestCostShare_PRC_Close; ^
::# STD_PC_S_PersonRequestCostShare_PRC_PushDocStatus;STD_PC_S_PersonRequestCostShare_PRC_Reject; ^
::# STD_PC_S_PersonRequestCostShare_PRC_Revoke;STD_PC_S_PersonRequestCostShare_PRC_UndoDocStatus

::# parses=exec('parses_edokum','obiegi')
::# access=exec('upr_oper','obiegi_akc')
::# properties=TODOTRIG,~RELEASE

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=T, keyref=T
::# kind=WE, symbol=USERS, type=_USERS, name=Użytkownik, required=N
::# kind=WE, symbol=B_PREL, type=STRING, name=Poprzedni element w procesie, required=N
::# kind=WE, symbol=B_PRELS, type=STRING, name=Symbol poprzedniego elementu w procesie, required=N
::# kind=WE, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WE, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WE, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WE, symbol=STATUS, type=STRING, name=Status, required=N

::# kind=WE, symbol=FORM_PRZ, type=STRING, name=Formuła wykonywana przy kończeniu akceptacji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_WAL, type=STRING, name=Formuła walidacyjna przy kończeniu akceptacji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_ODR, type=STRING, name=Formuła wykonywana przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORMWODR, type=STRING, name=Formuła walidacyjna przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_USR, type=STRING, name=Formuła walidacyjna użytkownika akceptującego, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"

::# kind=WE, symbol=ED_PODZ, type=STRING, name=Edycja podziałów controllingowych, required=N, fml_val="exec('czy_ed_podz','obiegi')"
::# kind=WE, symbol=ED_INND, type=STRING, name=Edycja informacji dodatkowych, required=N, fml_val="exec('czy_inn_dan','obiegi')"
::# kind=WE, symbol=ED_PROJ, type=STRING, name=Edycja projektów, required=N, fml_val="exec('czy_ed_proj','obiegi')"
::# kind=WE, symbol=UWODRWYM, type=STRING, name=Wymagane wypełnienie uwag przy odrzuceniu, required=N, fml_val="exec('czy_wymoduw','obiegi_akc')"
::# kind=WE, symbol=KONT_BUD, type=STRING, name=Kontrola budżetu, required=N, fml_val="exec('kont_bud','obiegi',_a)"

::# kind=WE, symbol=TYPY_ZAL, type=MEMO, name=Typy załączników i ich podpisy, required=N, fml_val="exec('typy_zal_akc','portal_paperless',_a)"

::# kind=WE, symbol=B_ROLEF, type=STRING, name=Rola użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=B_ROLE, type=STRING, name=Domyślna rola użytkowników, required=N, fml_val="exec('rola','obiegi',_a)", fml_exp="exec('b_role_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=BLZMROL, type=STRING, name=Blokada zmiany roli, required=N, fml_val="exec('blzmrol','obiegi')"
::# kind=WE, symbol=BLZMUSR, type=STRING, name=Blokada zmiany uzytkowników, required=N, fml_val="exec('blzmusers','obiegi')"
::# kind=WE, symbol=USR_FORM, type=STRING, name=Wybór użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=JEDEN_OP, type=STRING, name=Obsługuje jeden operator - formuła, required=N, fml_val="exec('jeden_op','obiegi2')"


:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N
::# kind=WY, symbol=USERS, type=_USERS, name=Użytkownik, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N
::# kind=WY, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WY, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WY, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WY, symbol=EM_NAD, type=STRING, name=Adres e-mail użytkownika akceptujacego (nadawca), required=N
::# kind=WY, symbol=EM_ODB, type=MEMO, name=Adres e-mail użytkownika akceptującego (odbiorca), required=N
::# kind=WY, symbol=EM_NAG, type=STRING, name=Nagłówek wiadomości e-mail, required=N
::# kind=WY, symbol=EM_TR, type=MEMO, name=Treść wiadomości e-mail, required=N
::# kind=WY, symbol=BUDZ_COND, type=NUMBER, name=Budżet przekroczony warunkowo?, required=N

:: PARAMETRY WEW:
::# kind=WEW, symbol=USERSN, type=_USERS, name=Użytkownik, required=N

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

{? ~exec('is_act','#b__box','OBE_FAW_EAKP')
|| FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków.'@);
   _mp.cancel(); return(~~)
|? _mp.isMicro()
|| FUN.info('Wniosek nie jest w trakcie akceptacji.'@); return(~~)
?};

{? _akcja='Portal'
|| {? var_pres('EDOKUM',_we)>0 & _we.EDOKUM
   || EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_we.EDOKUM));
      {? EDOKUM.seek(_we.EDOKUM)
      || params_exec('ustaw_bprel','obiegi');
         _wy.EDOKUM:=EDOKUM.ref();
         _wy.WAL:=EDOKUM.WAL;
         _wy.NETTO:=EDOKUM.NETTO;
         _wy.BRUTTO:=EDOKUM.WART;
         _wy.DEKR:=EDOKUM.TYP().DEKR;
         _wy.STATUS:={? EDOKUM.STATUS='D' || 'O' || 'A' ?};
         _wy.EM_NAD:=_wy.EM_ODB:=OPERATOR.USER().EMAIL;
         _wy.EM_NAG:={? EDOKUM.STATUS='Y' || 'Zaakceptowano' || 'Odrzucono' ?}+
                     ' wniosek: '+EDOKUM.ID+', data operacji '+$EDOKUM.DOP;
         _wy.B_PREL:=OBIEGI.B_PREL;
         _wy.B_PRELS:=OBIEGI.B_PRELS;

         {? EDOKUM.STATUS='D'
         || {? var_pres('FORM_ODR',_we)>0 & _we.FORM_ODR<>''
            || _fml:=_we.FORM_ODR;
               {? _fml<>''
               || ($_fml)()
               ?}
            ?}
         || {? var_pres('FORM_PRZ',_we)>0 & _we.FORM_PRZ<>''
            || _fml:=_we.FORM_PRZ;
               {? _fml<>''
               || ($_fml)()
               ?}
            ?}
         ?};
         {? var_pres('_args')>0 & var_pres('context',_args)>0 & var_pres('DONE',_args.context)>0
         || _args.context.B_PREL:=OBIEGI.B_PREL;
            _args.context.DONE:=1
         ?};
         _mp.save(,_wy);
         _mp.keep();
         _mp.done()
      ?};
      EDOKUM.cntx_pop()
   ?};
   return(~~)
?};

editWni:=0;
zak_akc_ed:=0;
budz_cond:=0;
OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
{? _we.EDOKUM & EDOKUM.seek(_we.EDOKUM,ref_name(_we.EDOKUM),1)
|| params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
   {? app_info('web_sesid')=''
   || {? _akcja=''
      || _akcja:=exec('akc_win','obe_faw','A')
      ?}
   ?};
:: sprawdzenie z poziomu webterma
   {? _akcja='Akceptuj2'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('akceptuj2','obiegi_akc','A');
      EDOKUM.cntx_psh(); EDOKUM.index('DISP'); EDOKUM.prefix();
      {? zak_akc_ed=1 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'A');
         VAR_DEL.delete('no_bl');
         no_bl:=_wy.B_PREL;
         no_bprel:=_nobp:=exec('no_bprel','obiegi_akc',_wy.B_PREL);
         _mp.save(,_wy);
         _mp.done();
         no_bprel:=_nobp
      ?};
      EDOKUM.cntx_pop()
   |? _akcja='Odrzuc2'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('odrzuc2','obiegi_akc');
      EDOKUM.cntx_psh(); EDOKUM.index('DISP'); EDOKUM.prefix();
      {? zak_akc_ed=2 & EDOKUM.get()
      || VAR_DEL.delete('edokosOset');
         exec('ustaw_out','obiegi_akc',_wy,'O');
         _mp.save(,_wy);
         _mp.done()
      ?};
      EDOKUM.cntx_pop()
   |? _akcja='chkWT'
   || {? app_info('web_sesid')<>''
      || web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                               'popetap',_we.B_PRELS
                               ))
      ?};
      params_get().context.SPR_WT:=1
:: akceptacja z pulpitu
   |? _akcja='Akceptuj'
   || {? app_info('web_sesid')<>'' || params_get().context.EDOKUM ?};
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('akceptuj','obiegi_akc','A');
      EDOKUM.cntx_psh(); EDOKUM.index('DISP'); EDOKUM.prefix();
      {? zak_akc_ed=1 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'A');
         VAR_DEL.delete('no_bl');
         no_bl:=_wy.B_PREL;
         no_bprel:=_nobp:=exec('no_bprel','obiegi_akc',_wy.B_PREL);
         _mp.save(,_wy);
         _mp.done();
         no_bprel:=_nobp
      ?};
      EDOKUM.cntx_pop()
   |? _akcja='Odrzuć'
   || {? app_info('web_sesid')<>'' || params_get().context.EDOKUM ?};
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('odrzuc','obiegi_akc');
      EDOKUM.cntx_psh(); EDOKUM.index('DISP'); EDOKUM.prefix();
      {? zak_akc_ed=2 & EDOKUM.get()
      || VAR_DEL.delete('edokosOset');
         exec('ustaw_out','obiegi_akc',_wy,'O');
         _mp.save(,_wy);
         _mp.done()
      ?};
      EDOKUM.cntx_pop()
   |? _akcja='Popraw'
   || {? app_info('web_sesid')<>'' || params_get().context.EDOKUM ?};
      _mp.descTodo();
      _mp.keep()
   ?}
?};
VAR_DEL.delete('zak_akc_ed','zawin','budz_cond','editWni');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zaakceptuj'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));
params_exec('ustaw_bprel','obiegi');
{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
         _desc:={? EDOKUM.TYP().W_PORTAL<>'N'
                || exec('obe_faw_eakp_desc','portal_wnioski',EDOKUM.ref())
                || 'Zaakceptuj %1: %2, data operacji %3'@@[(-EDOKUM.TYP().NAZWA),EDOKUM.ID,$EDOKUM.DOP]
                ?};
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)


      ?};
      {? var_pres('co_dalej')>0 &
         ((4+web_top_win(0,0))='WPRG' | (4+web_top_win(1,0))='WPRG' | (4+web_top_win(2,0))='WPRG')
      || co_dalej:='A'
      ?};
      {? var_pres('STATUS',_we)>0 & _we.STATUS='O' & _mp.akcja()<>'Akceptuj' & _set
      || {? OBIEGI.B_PREL<>''
         || EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
            {? EDOKOS.first()
            || {! |?
                  EDOKOS.STATUS:='O'; EDOKOS.DATA:=date(0,0,0); EDOKOS.TIME:=time(0,0,0); EDOKOS.put();
                  EDOKOS.next()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?}
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::       _b - B_PREL.ref() lub BI_PREL.ref() lub exec('mp_run_a','#b__box')
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
{? _path='Todo'
|| _path_param:=_b;
   _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   _prel:=_path_param;
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || _name:=((_keyrefs[1]-8)+8);
      EDOKUM.use(_name); EDOKUM.prefix();
      {? EDOKUM.seek(_keyrefs[1],,1)
      || exec('maski_w','obiegi',1);
         EDOKUM.memo_get(,'UW_OPDL');
         exec('bobs_edokum','obiegi','W');
         SKID.SL_KH:=SKID.NIP:='';
         web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'ed_kh_ed',0,'editWni',0));
:: Okno wyświetla się tylko dynamicznie.
         EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
         {? exec('szuk_edokpar','obiegi') & EDOKPAR.ED_INND='R'
         || params_exec('pop_dob_wt','obiegi',2,1,'A')
         || _red:=exec('win_red_wnioski','obiegi2','A');
               EDOKUM.web_cntx_save();
               web_def_disp(_red,"
                  EDOKUM.web_cntx_load();
                  exec('inne_akcje','obiegi2',_a)
               ",'Wniosek w obiegu'@)
         ?}
      ?}
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
?}


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności OBE_FAW_EAKP
::  WE: _a - 'add po', 'put przed', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='add_po' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='add_po'
         || 3
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'OBE_FAW_EAKP')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [19.22]
:: OPIS: Podglad dokumentu obiegu z listy zadań
::----------------------------------------------------------------------------------------------------------------------
params_exec('disp_wni','obiegi2',2)

:Sign Version 2.0 jowisz:1045 2023/12/08 07:41:16 451ddf041a4fac8add5a1f13facaa66356b10006efba4c0d2ac8bfbccbdd558952c26d1bd71785c9770e7ea3765511285ae42c5921529554ff5ee2951cb80e274b34d7122477ba55d5b118d2f548451ce1ab06b4911cfaa4fe4f6c8a52ea34d5bfda01e175338c2f245af21965f23910dc10b66b0715625e1ed3301c55456988

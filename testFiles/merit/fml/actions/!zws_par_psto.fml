:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_psto.fml
:: Utworzony: 02.11.2015
:: Autor: PT
::======================================================================================================================
:: Zawartość:
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Konfiguracja pozycji w organizacji, stanowisk i jednostek organizacyjnych i powiązań
::       między nimi - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('select','!zws_par_psto')


\zws_par_psto_dsk_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła przygotowuje tłumaczenia dla kontrolki !zws_par_psto.dsk.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_TAB:=exec('elements_table','#desktop');
_add:="_a.blank(); _a.ID_SYS:=_b; _a.NAME:=_c; _a.add()";

_add(_TAB,'schTile@panel','Schemat'@);

_TAB


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Okno grupowe do konfiguracji pozycji w organizacji, stanowisk i jednostek organizacyjnych
::       i powiązań między nimi
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ud_skl:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_mode:='maximized_with_title';
_cfg:=obj_new('nav','psto_mod');
_cfg.nav:=obj_new('main','side');
_cfg.nav.main:=exec('nawigator','schemat','PODZORG','MID',,1);
_cfg.nav.side:='STO_WER2';
_cfg.psto_mod:=0;
params_set('cfg',_cfg,'ud_skl',_ud_skl);
P_FILTER.UD_SCH:=exec('domyslny','schemat','PODZORG');

_wierszy:=24;

STO.cntx_psh();
PSTO.cntx_psh();
STO.index('ST');
POZWORG.index('KOD');
POZWORG.prefix(exec('ref_firma','ustawienia'));

_wnd:=PSTO.grp_make('Stanowiska i pozycje w organizacji'@,
:  po wyświetleniu
   "  _par:=params_get();
      params_set(_par);
      UD_DEF.cntx_psh();
      exec('nawigator_ustaw','schemat',_par.cfg.nav.main,P_FILTER.UD_SCH,_par.ud_skl);
      grp_disp(UD_DEF,_par.cfg.nav.main,1,1);
      1
   ",
:  identyfikator
   '#zws_par_psto',
:  położenie
   ,,
:  zamknięcie
   "  UD_DEF.cntx_pop();
      1
   "
);

: drzewko struktury organizacyjnej
PSTO.grp_sel(_wnd,UD_DEF,_cfg.nav.main,'Stanowiska i pozycje wg jednostek'@,
:  po odświeżeniu
   "  UD_DEF.UD_SKL();
      grp_disp(STO,'WER2',1);
      grp_disp(PSTO,'WER3',1)
   ",
:  położenie i wysokość
   ,,_wierszy,
:  przed obsługą
   ,
:  po obsłudze
   ,
:  utrwalenie - aktywacja - wypełnienie
   0,1,'maximized',
:  identyfikator
   _cfg.nav.main,
   1
);

PSTO.tab_splt(_wnd,,'vertical','panel3');
PSTO.grp_sel(_wnd,STO,'WER2',,
:  po odświeżeniu
   "grp_disp(PSTO,'WER3',1)",
:  położenie i wysokość
   ,,_wierszy,
:  przed obsługą
   "  STO.fld_fml('UD_SKL','BLANK',""UD_DEF.UD_SKL"");
      STO.prefix({? UD_DEF.size() || UD_DEF.UD_SKL || null() ?});
      STO.win_edit('RED')
   ",
:  po obsłudze
   "STO.fld_fml('UD_SKL','BLANK',""*"")",
:  utrwalenie - aktywacja - wypełnienie
   0,0,_mode,
:  identyfikator
   'STO_WER2'
);

PSTO.tab_splt(_wnd,,'vertical','panel4');
PSTO.grp_sel(_wnd,,'WER3',,
:  po odświeżeniu
   "PSTO.actions('WER3',{? STO.size() || '' || 'D:D' ?},,1)",
:  położenie i wysokość
   ,,_wierszy,
:  przed obsługą
   "  PSTO.win_edit('RED');
      PSTO.index('POZKOD');
      PSTO.prefix({? STO.size() || STO.ref() || null() ?});
      PSTO.fld_fml('UD_SKL','BLANK',""STO.UD_SKL"")
   ",
:  po obsłudze
   "PSTO.fld_fml('UD_SKL','BLANK',""*"")",
:  utrwalenie - aktywacja - wypełnienie
   0,0,_mode,
:  identyfikator
);

PSTO.grp_sel(_wnd,POZWORG,'WER','Stanowiska i jednostki dla pozycji'@,"grp_disp(PSTO,'WER2',1)",,,_wierszy,,,,,_mode);
PSTO.tab_splt(_wnd,,'vertical','panel1');
PSTO.grp_sel(_wnd,,'WER2',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,_wierszy,
:  przed obsługą
   "  STO.fld_fml('UD_SKL','BLANK',"""");
      PSTO.fld_fml('STO','BLANK',"""");
      PSTO.fld_fml('POZWORG','BLANK',""POZWORG.ref"");
      PSTO.win_edit('RED2');
      STO.win_sel('WER3');
      STO.win_edit('RED2');
      PSTO.index('POZWORG');
      PSTO.prefix({? POZWORG.size() || POZWORG.ref() || null() ?})
   ",
:  po obsłudze
   "  STO.fld_fml('UD_SKL','BLANK',""*"");
      PSTO.fld_fml('STO','BLANK',""*"");
      PSTO.fld_fml('POZWORG','BLANK',""*"")
   ",
:  utrwalenie - aktywacja - wypełnienie
   0,0,_mode,
:  identyfikator
);

STO.dnd_sel('WER2',,'records.PSTO',"exec('dnd_sto','!zws_par_psto')");
UD_DEF.dnd_sel(_cfg.nav.main,,'records.STO',"exec('dnd_ud_def','!zws_par_psto')");
POZWORG.dnd_sel('WER',,'records.PSTO',"exec('dnd_pozworg','!zws_par_psto')");

PSTO.win_sel(_wnd);
PSTO.select();

STO.cntx_pop();
PSTO.cntx_pop()


\dsk_callback_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Reakcja na wybór schematu w selektorze.
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
UD_SCH.prefix();
{? UD_SCH.seek(exec('get_value','#desktop','','selektor','schCB@panel'))
|| P_FILTER.UD_SCH:=UD_SCH.ref()
?};
grp_disp(UD_DEF,_par.cfg.nav.main,1,1);
win_activate(params_get().cfg.nav.side)


\dnd_pozworg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Obsługa drag'n'drop dla okna tabeli POZWORG
::----------------------------------------------------------------------------------------------------------------------
_dest:=dnd_info('dest_record');
{? _dest=null()
|| return()
?};

_tab:=dnd_info('dropped_records');
{? _tab.size()>1
|| FUN.emsg('Nie można zmienić powiązania dla więcej niż jednego stanowiska.'@);
   return()
?};
_mask:=dnd_info('table_name');
_loop:=_tab.first();
_odsw:=0;
POZWORG.cntx_psh();
PSTO.cntx_psh();
PSTO.index('POZWORG');
PSTO.prefix();
{? POZWORG.seek(_dest)
|| _dest_opis:='"'+POZWORG.KOD+' '+POZWORG.OPIS+'"';
   {? PSTO.seek(_tab.REF,_mask)
   || {? PSTO.POZWORG<>_dest
      || PSTO.cntx_psh();
         PSTO.prefix(_dest);
         PSTO.cntx_pop();
         {? FUN.ask('Czy na pewno chcesz powiązać wybrane stanowisko z pozycją %1?'@ [_dest_opis])
         || _odsw:=1;
            {!
            |? _loop
            |! {? PSTO.seek(_tab.REF,_mask)
               || PSTO.POZWORG:=POZWORG.ref();
                  PSTO.put()
               ?};
               _loop:=_tab.next()
            !}
         ?}
      ?}
   ?}
?};
POZWORG.cntx_pop();
PSTO.cntx_pop();
{? _odsw
|| POZWORG.seek(_dest);
   grp_disp(PSTO,'WER2',1);
   {? _tab.last()
   || PSTO.seek(_tab.REF,_mask)
   ?}
?}


\dnd_sto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Obsługa drag'n'drop dla okna tabeli STO
::----------------------------------------------------------------------------------------------------------------------
_dest:=dnd_info('dest_record');
{? _dest=null()
|| return()
?};

_tab:=dnd_info('dropped_records');
{? type_of(_tab)=type_of(~~)
|| return()
|? _tab.size()>1
|| FUN.emsg('Nie można zmienić powiązania dla więcej niż jednej pozycji.'@);
   return()
|? ~_tab.first()
|| return()
?};

_mask:=dnd_info('table_name');
_odsw:=0;
STO.cntx_psh();
STO.prefix();
PSTO.cntx_psh();
PSTO.index('PSTO');
PSTO.prefix();
POZWORG.cntx_psh();
{? STO.seek(_dest) & PSTO.seek(_tab.REF,_mask) & PSTO.STO<>_dest
|| _use:=0;
    H.cntx_psh();
   {! _lp:=1 .. {? -PAR_SKID.get(180)='t' || 2 || 1 ?}
   |? ~_use
   |! {? _lp=1
      || H.use('_hist')
      || H.use('_his_')
      ?};
      H.index('HISTODKZ');
      H.prefix(exec('ref_firma','ustawienia'));
      H.blank(1);
      H.POZWORG:=PSTO.POZWORG;
      _use:=H.find_rec()
   !};
   H.cntx_pop();

   {? _use
   || FUN.emsg(
         'Pozycja %1 - %2\njest już wykorzystywana w przebiegu zatrudnienia.\n'
         'Przywiązanie do innego stanowiska nie jest możliwe.'@ [PSTO.POZWORG().KOD,PSTO.POZWORG().OPIS]
      )
   |? FUN.ask('Czy na pewno chcesz powiązać wybraną pozycję ze stanowiskiem "%1"?'@ [STO.ST])
   || _odsw:=1;
      PSTO.STO:=_dest;
      PSTO.put()
   ?}
?};
POZWORG.cntx_pop();
PSTO.cntx_pop();
STO.cntx_pop();
{? _odsw
|| STO.seek(_dest);
   grp_disp(PSTO,'WER3',1);
   {? _tab.last()
   || PSTO.seek(_tab.REF,_mask)
   ?}
?}


\dnd_ud_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Obsługa drag'n'drop dla okna tabeli UD_DEF
::----------------------------------------------------------------------------------------------------------------------
_dest:=dnd_info('dest_record');
{? _dest=null()
|| return()
?};

_tab:=dnd_info('dropped_records');
_mask:=dnd_info('table_name');
_loop:=_tab.first();
_odsw:=0;
UD_DEF.cntx_psh();
STO.cntx_psh();
STO.index('ST');
STO.prefix();
{? UD_DEF.seek(_dest)
|| _dest_opis:='"'+UD_DEF.SYMBOL+'"';
   {? STO.seek(_tab.REF,_mask)
   || {? STO.UD_SKL<>UD_DEF.UD_SKL
      || _use:=0;
         {? _tab.first()
         || {!
            |? {? STO.seek(_tab.REF,_mask)
               || _use+=exec('stn_in_ud_skl','!zws_par_psto',STO.UD_SKL,STO.STN)
               ?};
               ~_use & _tab.next()
            !}
         ?};
         {? {? _tab.size()>1
            || _txt:=
                  {? _use
                  || 'Co najmniej jedno z zaznaczonych stanowisk\n'
                     'jest wykorzystywane w dotychczasowej jednostce organizacyjnej.\n'@
                  || ''
                  ?};
               FUN.ask(_txt+'Czy na pewno chcesz powiązać wybrane stanowiska z jednostką %1?'@ [_dest_opis])
            || _txt:=
                  {? _use
                  || 'Wybrane stanowisko jest wykorzystywane w dotychczasowej jednostce organizacyjnej.\n'
                     'Czy na pewno chcesz je powiązać z jednostką %1?'@ [_dest_opis]
                  || 'Czy na pewno chcesz powiązać wybrane stanowisko z jednostką %1?'@ [_dest_opis]
                  ?};
               FUN.ask(_txt)
            ?}
         || _odsw:=1;
            {!
            |? _loop
            |! {? STO.seek(_tab.REF,_mask)
               || STO.UD_SKL:=UD_DEF.UD_SKL;
                  STO.put()
               ?};
               _loop:=_tab.next()
            !}
         ?}
      ?}
   ?}
?};
UD_DEF.cntx_pop();
STO.cntx_pop();
{? _odsw
|| UD_DEF.seek(_dest);
   grp_disp(STO,'WER2',1);
   {? _tab.last()
   || STO.seek(_tab.REF,_mask)
   ?}
?}


\sto_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń" okna wertowania WER2 tabeli STO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? {? exec('stn_in_ud_skl','!zws_par_psto',STO.UD_SKL,STO.STN)
   || FUN.ask(
         'Stanowisko "%1" jest wykorzystywane w jednostce organizacyjnej "%2".\n'
         'Czy na pewno chesz usunąć stanowisko ze struktury?'
         [STO.STN().ST,STO.UD_SKL().SYMBOL]
      )
   || exec('del_ask','#table')
   ?}
|| STO.del()
?}


\psto_dolacz_sto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Akcja "Dołącz" okna wertowania WER2 tabeli PSTO
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_par.cfg.psto_mod:=0;
params_set(_par);
STO.cntx_psh();
STO.index('UD_SKL');
STO.win_sel('WER3');
STO.win_dict('WER3');
STO.prefix();
STO.f_set('UD_SKL',,'REFERENCE not in (select STO from PSTO)');
STO.actions('WER3',,'');
STO.select();
STO.f_clear(1);
STO.cntx_pop()


\psto_popraw_sto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Akcja "Popraw" okna wertowania WER2 tabeli PSTO
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_par.cfg.psto_mod:=1;
params_set(_par);
STO.cntx_psh();
STO.index('UD_SKL');
STO.win_sel('WER3');
STO.win_dict('WER3');
STO.prefix();
STO.f_set('UD_SKL',,'REFERENCE not in (select STO from PSTO) or REFERENCE=:_a',PSTO.STO);
STO.f_seek(PSTO.STO);
STO.select(,1,-1);
STO.f_clear(1);
STO.cntx_pop()


\psto_dolacz_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Akcja "Dołącz" okna wertowania WER3 tabeli PSTO
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_par.cfg.psto_mod:=0;
params_set(_par);
POZWORG.cntx_psh();
POZWORG.win_sel('WER2');
POZWORG.win_dict('WER2');
POZWORG.index('KOD');
POZWORG.prefix(exec('ref_firma','ustawienia'));
POZWORG.select();
POZWORG.cntx_pop()


\psto_popraw_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Akcja "Popraw" okna wertowania WER3 tabeli PSTO
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_par.cfg.psto_mod:=1;
params_set(_par);
POZWORG.cntx_psh();
POZWORG.win_sel('WER2');
POZWORG.win_dict('WER2');
POZWORG.index('KOD');
POZWORG.prefix(exec('ref_firma','ustawienia'));
POZWORG.select(,1,-1);
POZWORG.cntx_pop()


\psto_wybierz_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Formuła "Przed" akcji "Wybierz (Formuła)" w oknie WER3 tabeli STO i w oknie WER2 tabeli POZWORG
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
{? _par.cfg.psto_mod
|| {? PSTO.POZWORG<>POZWORG.ref() | PSTO.STO<>STO.ref()
   || PSTO.STO:=STO.ref();
      PSTO.POZWORG:=POZWORG.ref();
      {? __CHK.index(PSTO,1,1)=''
      || PSTO.put()
      ?}
   ?}
|| PSTO.blank();
   PSTO.POZWORG:=POZWORG.ref();
   PSTO.STO:=STO.ref();
   {? __CHK.index(PSTO,0,1)=''
   || PSTO.add()
   ?}
?};
1


\stn_in_ud_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła sprawdza, czy w podanej jednostce organizacyjnej jest wykorzystywane podane stanowisko. Innymi słowy:
::       czy istnieje choć jeden zapis w przebiegu zatrudnienia ze wskazanymi: jednostką organizacyjną i stanowiskiem.
::   WE: _a [REFERENCE] - Jednostka organizacyjna.
::       _b [REFERENCE] - Stanowisko.
::   WY: 1 - Stanowisko "występuje" (jest użyte) w jednostce organizacyjnej.
::       0 - Stanowisko nie jest wykorzystywane w jednostce organizacyjnej.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=UD_SKL
|| _wydzial:=_a
|| return(0)
?};
{? var_pres('_b')=type_of(null()) & _b<>null() & ref_tab(_b)=STN
|| _st:=_b
|| return(0)
?};

_ret:=0;
H.cntx_psh();
{! _lp:=1 .. {? -PAR_SKID.get(180)='t' || 2 || 1 ?}
|? ~_ret
|! {? _lp=1
   || H.use('_hist')
   || H.use('_his_')
   ?};
   H.index('HISTODKZ');
   H.prefix(exec('ref_firma','ustawienia'));
   H.blank(1);
   H.WYDZIAL:=_wydzial;
   H.ST:=_st;
   _ret:=H.find_rec()
!};
H.cntx_pop();
_ret

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:02 9c675e5d394d476ea31262331e92296b82f23ac87aabed81e5afc01a10667bee42548fce90f4c8c8fb3f6d446cec677269cb1355474fe4949eb333cefcfa3c96885c3c2063477d0d6c59f019d54a011e32c473b7aa4184cb78674135dc34523052a205bcca868a2cb83487c014fdc58e798ed36a7fd765845e7cfcddc487faec

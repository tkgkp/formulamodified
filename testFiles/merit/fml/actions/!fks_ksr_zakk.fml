:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_zakk.fml
:: Utworzony: 27.05.2015
:: Autor: MB
::======================================================================================================================
:: Zawartosc: Formuły czynności FKS_KSR_ZAKK - Wydruki kartoteki księgowań
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wydruki kartoteki księgowań - główna formuła czynności.
::   WE: [_a] - [1]- z raportów
::               0 - z obszaru
::   WY: 0 - Dla wywołania z konkretnym raportem: pliku nie znaleziono.
::       1 - W przeciwnym wypadku.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
{? _=0 || _a:=1 ?};
params_set(params_get());
{? _a
|| REJ.prefix();
   REJ.f_clear();
   REJ.f_set('ODD,KOD',
      'left join USERSDEP using(USERSDEP.DEPT,REJ.ODD)',
      'ROK=:_c and (:_a=1 or USERSDEP.USERS=:_b) and ODD like :_d',
      Perm.hasFull('FJKS'),OPERATOR.USER,SSTALE.AR,{? OPERATOR.DEPT || OPERATOR.DEPT || '''%''' ?}
   );
   exec('F','object');
   exec('RB','object')
?};
REJ.f_clear();
REJ.win_dict('SLO1');
ROZNE.cntx_psh();
ROZNE.ROKKON:=SSTALE.AR;
exec('rep_exec','#b_report','FKS_KSR_ZAKK','fks_k_*',,1);
ROZNE.cntx_pop();
{? _a
|| REJ.prefix();
   REJ.f_clear()
?}


\ar_k_kazapk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.60]
:: OPIS: Sprawdza poprawnosc wypelnienia parametrow w oknie wydruku k_kazapk.rpm
::  OLD: \k_kazapk/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_a:='';
{? ROZNE.PAR4=4
|| _a:=__CHK.record2(ROZNE,'KS_OD','Zakres kont od','KS_DO','Zakres kont do');
   {? _a='' || {? ROZNE.KS_OD().SYM> ROZNE.KS_DO().SYM
               || FUN.info('Błędny zakres kont.'@); _a:='KS_OD'
               ?}
   ?}
|? ROZNE.PAR4=5
|| _a:=__CHK.record2(ROZNE,'KON_WYDR','Zestaw kont');
   {? _a=''
   || KON_SCH.prefix(KON_WYDR.ref);
      _a:={? KON_SCH.first()
          || ''
          || FUN.info('Wybrany zestaw kont nie ma zdefiniowanej definicji kont.'@); 'KON_WYDR'
          ?}
   ?}
|| {? ~{? WYDRUKI.P1=2
       || ROZNE.UT_OKROD<>null & ROZNE.UT_OKRDO<>null & (ROZNE.UT_OKRDO().NR<=SSTALE.AR().LOBR+1) & (ROZNE.UT_OKROD().NR<=ROZNE.UT_OKRDO().NR) & (ROZNE.UT_OKROD().NR>=0)
       || ROZNE.DATA_OD<>date(0,0,0) & ROZNE.DATA_DO<>date(0,0,0) & ROZNE.DATA_DO>=ROZNE.DATA_OD
       ?}
   || {? WYDRUKI.P1=2
      || {? ROZNE.UT_OKROD=null
         || FUN.info('Niewypełniony okres początkowy.'@);
            OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AO().ROK); OKRO_F.first();
            ROZNE.UT_OKROD:=OKRO_F.ref(); _a:='UT_OKROD'
         |? ROZNE.UT_OKRDO=null
         || FUN.info('Niewypełniony okres końcowy.'@);
            ROZNE.UT_OKRDO:=SSTALE.AO; _a:='UT_OKRDO'
         || FUN.info('Wybrano zakres okresów obrachunkowych w odwrotnej kolejności.'@); _a:='UT_OKROD'
         ?}
      || FUN.info('\nDaty muszą być z bieżącego roku bilansowego\n i \"Data od\" nie może być większa od \"Data do\".\n'@);
         {? ROZNE.DATA_OD=date(0,0,0) || ROZNE.DATA_OD:=dp ?};
         {? ROZNE.DATA_DO=date(0,0,0) || ROZNE.DATA_DO:=dk ?}; _a:='DATA_OD'
      ?}
   ?}
?};
{?_a='' || exec('wyd_selk','dok_fks_zest','K_KAZAPK') ?};
_a


\zakr_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: ROZNE.PAR4=4
::  OLD: \zakr_kon/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.PAR4=4


\bv_konwy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.60]
:: OPIS: Przed wyswietl pola ROZNE.KON_WYDR
::  OLD: \bv_konwy/dok_zest.fml
::----------------------------------------------------------------------------------------------------------------------
UNPAR.P1=6 | ROZNE.PAR4=5


\be_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.60]
:: OPIS: Przed redakcja pola PAR_WYDR.BO
::  OLD: \be_bo/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_WYDR.NAR


\be_nzsal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Przed redakcja i przed wyswietleniem pol PAR_WYDR.NSAL, PAR_WYDR.ZSAL
::  OLD: \be_nzsal/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_WYDR.SAL


\ae_wydruki_p1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.60]
:: OPIS: Formula po redakcji pola WYDRUKI.P1
::  OLD: \aep1/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab()=WYDRUKI & WYDRUKI.win_edit('?')='OKRESY1' | _=1
|| {? WYDRUKI.P1=2
   || ROZNE.DATA_OD:=ROZNE.DATA_DO:=date(0,0,0);
      {? ROZNE.UT_OKRDO=null || ROZNE.UT_OKRDO:=SSTALE.AO ?};
      {? ROZNE.UT_OKROD=null || OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
                                ROZNE.UT_OKROD:={? OKRO_F.first() || OKRO_F.ref() || null ?}
                             ?}
   || ROZNE.UT_OKROD:=ROZNE.UT_OKRDO:=null;
      {? ROZNE.DATA_OD=date(0,0,0) || ROZNE.DATA_OD:=dp ?};
      {? ROZNE.DATA_DO=date(0,0,0) || ROZNE.DATA_DO:=dk ?}
   ?};
   _enable:=WYDRUKI.P1<>2;
   WYDRUKI.efld_opt('OKRESY1','enable='+$_enable,ROZNE,'DATA_OD');
   WYDRUKI.efld_opt('OKRESY1','enable='+$_enable,ROZNE,'DATA_DO');
   WYDRUKI.efld_opt('OKRESY1','enable='+$(~_enable),ROZNE,'UT_OKROD');
   WYDRUKI.efld_opt('OKRESY1','enable='+$(~_enable),ROZNE,'UT_OKRDO')
?}; 1


\be_konwy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.60]
:: OPIS: Przed redakcja pola ROZNE.KON_WYDR
::  OLD: \be_konwy/dok_zest.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZNE.PAR4=5 | UNPAR.P1=6
||
   _a:=menu_pth()+1;
   {? 1 | _a='KOA' | _a='NTA'
   || SA:={? ROZNE.PAR1=1 & _a<>'B' || 'S' || 'A' ?};
      KON_WYDR.hdr_sel();
      {? cur_win()='ZOIS' || SA:='A' ?};
      {? SA='A'
      || KON_WYDR.hdr_sel(' - analityczne'@)
      || KON_WYDR.hdr_sel(' - syntetyczne'@)
      ?}
   ?};
   1
|| 0
?}


\ae_nar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.60]
:: OPIS: Po redakcji pola PAR_WYDR.NAR
::  OLD: \ae_nar/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_WYDR.NAR=0 & PAR_WYDR.BO || PAR_WYDR.BO:=0 ?};
win_disp();
1


\ae_sal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Po redakcji pola PAR_WYDR.SAL
::  OLD: \ae_sal/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_WYDR.SAL=0 || PAR_WYDR.NSAL:=0 ?};
win_disp();
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 3de2f75a74a47a17a0e80bebf9b4fd1335a3f6459939e0fb6ec9a020ebb62f92e0b90ac3ae230e25a04b72be737fb7101549125d12c6b96a73f9b493030dad196020589f40578044687a0e65e81c5184769b98f5080c4385be07649d89baa8faf6185039a905cb25c540cdcc641f6ccca6648dcdd9a22211a900c0ab0494e6ee

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !poc_ocp_rpza.fml
:: Utworzony: 25.08.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności POC_OCP_RPZA - Zamknięcie sesji ocen.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Zamknięcie sesji ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=ZO_PROC, type=_ZO_PROC, name=Wskazanie sesji ocen, required=T, keyref=T
::
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,NIEOTWARTA,ZAMKNIĘTA,FORMULARZE,REZYGNACJA)", required=N
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;

:: Jedyny parametr wejściowy ma ustawioną flagę keyref=T, co oznacza, że przekazana wartość automatycznie stanie się
:: rekordem kluczowym.
_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error('Nieprawidłowy parametr wejściowy.'@);
   return()
?};

_ret:=exec('zamknij','!poc_ocp_rpza',_keyRefs[1],~_mp.isMicro());

{? _ret.errmsg<>''
|| FUN.emsg(_ret.errmsg);
   _mp.error(_ret.errmsg)

|? _ret.status='X'
|| _mp.keep()

|| _out.RESULT:=_ret.status;
   _mp.save(,_out);
   _mp.done()
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Zamknięcie sesji ocen - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_keyRefs:=_mp.getRefs();
_ref:={? var_pres('[1]',_keyRefs) || _keyRefs[1] || _mp.load(exec('kind_in','#b_port')).ZO_PROC ?};
_tab:=exec('desc_tab','phr_dane');
ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROC.cntx_psh();
ZO_PROC.prefix();
{? type_of(_ref)=type_of(null) & ZO_PROC.seek(_ref)
|| _tab.ZAW_DANE:='T';
   _tab.NAZWA:=ZO_PROC.ZO_PROG().NAZWA;
   _tab.RODZAJ:=ZO_PROC.ZO_PROG().RODZAJ;
   _tab.OKRES_OD:=$ZO_PROC.OKRES_OD;
   _tab.OKRES_DO:=$ZO_PROC.OKRES_DO
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();
{? _tab.ZAW_DANE='T'
|| 'W ramach programu "%1" (%2) zamknij sesję ocen od %3 do %4.'@@[_tab.NAZWA,_tab.RODZAJ,_tab.OKRES_OD,_tab.OKRES_DO]
|| 'Zamknij sesję ocen'@@
?}


\zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Główna formuła zajmująca się zakończeniem procesu.
::   WE: _a [REFERENCE] - Wskazanie sesji ocen.
::       _b [NUMBER]    - Czy obsługa w procesie? [1/0]
::   WY: Tablica nazwana m.in. ze statusem operacji.
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_proces:=_b;

_ret:=obj_new('errmsg','status');
_ret.errmsg:='';
_ret.status:='';

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROC.cntx_psh();
ZO_PROC.f_clear();
ZO_PROC.prefix();
{? ZO_PROC.seek(_ref)
|| {? ZO_PROC.A<>'T'
   || _ret.status:='NIEOTWARTA'

   |? ZO_PROC.Z='T'
   || _ret.status:='ZAMKNIĘTA'

   |? _TEST:=sql(
         'select count(*) as ERR '
         'from   ZO_TEST join ZO_OSOBA '
         'where  ZO_OSOBA.ZO_PROC=:_a and ZO_TEST.L_FORM<>1',
         ZO_PROC.ref()
      );
      _TEST.first() & _TEST.ERR>0
::    Błędy w formularzach uniemożliwiają zamknięcie. Nie zgłaszamy błędu (errmsg), ale pozostawiamy result.
   || FUN.emsg(
         'Wykryto błędy na liście formularzy ocen pracowników.\n'
         'Zamknięcie sesji ocen nie jest możliwe.'@
      );
      _ret.status:='FORMULARZE'

   || _akcja:=exec('dialog','!poc_ocp_rpza',_proces);
      {? _akcja='T'
      || ZO_PROC.A:='N';
         ZO_PROC.Z:='T';
         {? ZO_PROC.put()
         || _ret.status:='OK'
         || _ret.errmsg:='Zamknięcie sesji ocen nie powiodła się.'
         ?}
      |? _akcja='N'
      || _ret.status:='REZYGNACJA'
      || _ret.status:='X'
      ?}
   ?}
:: Brak rekordu przekazanego parametrem.
|| _ret.errmsg:='Zamknięcie sesji ocen niemożliwe.'
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();

_ret


\dialog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła przygotowuje okno redagowania.
::   WE: _a [NUMBER] - Czy obsługa w procesie? [1/0]
::   WY: T / N / X
::----------------------------------------------------------------------------------------------------------------------
_proces:=_a;

_TAB:=sql(
   'select sum(ZO_OSOBA.FORM_SUM) as U, sum(ZO_OSOBA.FORM_AKC) as Z, cast( 0 as real_type) as PROC '
   'from ZO_OSOBA '
   'where ZO_OSOBA.RODZAJ=\'E\' and ZO_OSOBA.ZO_PROC=:_a',
   ZO_PROC.ref()
);
{? _TAB.first() & _TAB.U
|| _TAB.PROC:=(100*_TAB.Z/_TAB.U)$2;
   _TAB.put()
?};

{? _proces
|| _ret:=obj_new(1);
   _ret[1]:='X';
   params_set('ret',_ret);

   _info:='Zamknięcie sesji ocen'@;
   _we:=ZO_PROC.mk_edit(_info);
   ZO_PROC.win_esep(_we,'Program oceny'@);
   ZO_PROC.win_efld(_we,ZO_PROG,'NAZWA',,,40,,,,,MS.comment(ZO_PROG,'NAZWA'));
   ZO_PROC.win_efld(_we,ZO_PROG,'RODZAJ',,,,,,,,'Rodzaj oceny'@,'radio-buttons',,
      'Roczna'@,"'R'",
      'Półroczna'@,"'P'",
      'Kwartalna'@,"'K'",
      'Miesięczna'@,"'M'",
      'Tygodniowa'@,"'T'",
      'Ad hoc'@,"'A'"
   );
   ZO_PROC.win_ecol(_we);
   ZO_PROC.win_esep(_we,'Sesja ocen'@);
   ZO_PROC.win_efld(_we,,'R',,,5,,,,,MS.comment(ZO_PROC,'R'));
   ZO_PROC.win_efld(_we,,'M',,,5,,,,,MS.comment(ZO_PROC,'M'));
   ZO_PROC.win_efld(_we,,'OKRES_OD',,,,,,,,MS.comment(ZO_PROC,'OKRES_OD'));
   ZO_PROC.win_efld(_we,,'OKRES_DO',,,,,,,,MS.comment(ZO_PROC,'OKRES_DO'));
   ZO_PROC.win_efld(_we,,'PLAN_OD',,,,,,,,MS.comment(ZO_PROC,'PLAN_OD'));
   ZO_PROC.win_efld(_we,,'PLAN_DO',,,,,,,,MS.comment(ZO_PROC,'PLAN_DO'));
   ZO_PROC.win_ecol(_we);
   ZO_PROC.win_esep(_we,'Formularze'@);
   ZO_PROC.win_efld(_we,_TAB,'U',,,10,0,,'Utworzone'@,,'Liczba utworzonych formularzy'@);
   ZO_PROC.win_efld(_we,_TAB,'Z',,,10,0,,'Zaakceptowane'@,,'Liczba zaakceptowanych formularzy'@);
   ZO_PROC.win_efld(_we,_TAB,'PROC',,,10,2,,'Postęp [%%]'@,,'Procent postępu'@);

   _p1:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['Zamknij'@],
      "params_get().ret[1]:='T'; 'key:F2'");
   ZO_PROC.btn_eopt(_we,_p1,'tooltip="%1"' ['Zamknięcie wskazanej sesji ocen i zakończenie bieżącej czynności'@]);
   _p2:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['Nie zamykaj'@],
      "params_get().ret[1]:='N'; 'key:F2'");
   ZO_PROC.btn_eopt(_we,_p2,'tooltip="%1"' ['Zakończenie czynności bez zamykania wskazanej sesji ocen'@]);
   _p3:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['Anuluj'@],'key:Esc');
   ZO_PROC.btn_eopt(_we,_p3,'tooltip="%1",default=1' ['Odłożenie czynności na listę zadań'@]);

   ZO_PROC.win_edit(_we);

   ZO_PROC.ZO_PROG();
   ZO_PROC.display(,,_info);

   _ret[1]

:: Mikroproces
|| {? FUN.ask(
         'Liczba formularzy:\n'
         ' - utworzonych: %1;\n'
         ' - zaakceptowanych: %2;\n'
         'Postęp: %3 %%.\n'@ [$_TAB.U,$_TAB.Z,form(_TAB.PROC,,2)]+'\n'+
         'Czy na pewno zamknąć sesję ocen?\n(wszystkie formularze zostaną zamknięte)'@
      )
   || 'T'
   || 'N'
   ?}

?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 3e2f01c5cad95a0c32e24ca7d5dce2fe6789546717556bfecc09d3086cc55176250ca2b687a6e7ddbe8b618e7fcc6b20d5a5bf5a0fce0006d5ece3610cdf990228c35345287f6f25716c810cd8a79d0ac9c741e6c4c9ea96ffc26a34a4e0d22bf47b6498b18e58a596ec35edf3231b87add334b0ea0b152077540f1a6f9b9734

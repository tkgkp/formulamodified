:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !hbn_prz_zbpr.fml
:: Utworzony: 15.02.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności HBN_PRZ_ZBPR - Przegląd - archiwum przelewów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie archiwum przelewów - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=HRB,HRP,FJKS
_ref:=PB.ref();
tmp_typ:=1;
PFZ.cntx_psh(); PFP.cntx_psh();
exec('save_pfp','hbn_filtr','H');
exec('marchprz','!hbn_prz_zbpr');
PFZ.cntx_pop(); PFP.cntx_pop();
VAR_DEL.delete('tmp_typ');
PB.win_sel('PB_GRP'+{? PAR_SKID.get(68)='T' || 'W' || '' ?});
exec('setf','hbn_filtr',0,'E','H');
PB.f_seek(_ref)


\marchprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Archiwum przelewow
::       archiwum z roku - ARCHROK
::       wprowadzone w HB - ARCHHB
::       wg uprawnien/wszystkie - ARCHUPRW
::       wg uprawnien/z rachunku - ARCHUPRR
::  OLD: \marchprz/proc_02.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('setf','hbn_filtr',0,'EA','D')
|| PB.win_edit('PB_EDI1');
   PB.win_sel('PB_WER2');
   PB.win_fml('PB_WER2',PB,'S_VAT',,'ICON_BEFORE',"exec('icona_svat','blp',PB.S_VAT)");
   RACHBANK.KB_4R_BD:='RACHBANK.KB_4R:=RB.get_rbtx(3,PB.RW,XINFO.KRAJ().KODISO)';
   RACHBANK.KB_5R_BD:='RACHBANK.KB_5R:=RB.get_rbtx(2,PB.RD,PB.NBD().KODISO().KODISO)';
   PB.hdr_sel();
   PB.hdr_sel(' - archiwum z roku %1'@[TT_ROK.ROK]);
   PB.select();
   PB.f_clear();
   PB.use('pbxxxx'); PB_OP.use('popxxxx'); PBHIST.use('phxxxx');
   VAR_DEL.delete('TAB_SPEC','TT_TYPP','TT_JKS','TT_SORT','TT_ROK','ws_tp','ws_jk','STAB_SPE',
                  'STT_TYPP','STT_JKS','STT_SORT','STT_ROK')
?};
1


\przywr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DPU
:: OPIS: Przywrócenie przelewu do ponownego wyslania
::  OLD: \przywr/homebank.fml
::----------------------------------------------------------------------------------------------------------------------
PB.cntx_psh();
_dalej:=0;
{? PB.PR='A'
|| {? PB.sel_size()=0
   || {? ~FUN.ask('Ten przelew był już przywracany i jest oznaczony jako nieudany!\n'
                  'Przywrócić ponownie? '
                  '(zostanie stworzona kolejna kopia\nprzelewu do ponownego wysłania)'@)
      || _dalej:=-1
      || _dalej:=1
      ?}
   ?}
?};
{? _dalej<>-1
|| {? PB.sel_size()>0 | _dalej=1 | FUN.ask('Stworzyć kopie przelewu do ponownego wysłania?\n'
                                           'Bieżący przelew zostanie oznaczony jako nieudany.'@)
   || SKID_RBK.cntx_psh();
      SKID_RBK.prefix();
      _v:=PB.RD;
      {? +form(3+_v)=2 & +form(12+_v)=11 || _v:=3-_v ?};
      {? (9+_v)+1=' ' | (9+_v)+1='-' || _v:=9-_v ?};
      SKID_RBK.index('TAB');
      SKID_RBK.prefix(null,REF.INFO,REF.INFO,0,0,_v);
      {? SKID_RBK.size()=0
      || FUN.emsg('Utworzenie kopii przelewu na nieistniejący rachunek jest niemożliwe!'@)
      || PB.PR:='N';
         {? XINFO.REST_ACC<>'' || PB.ZT:=XINFO.REST_ACC ?};
         PB.EMAIL:='';
         _old_tyt:=PB.TYT;
         _poz:=PB.TYT*' TMS ';
         {? _poz || PB.TYT:=form((_poz-1)+PB.TYT) ?};
         _vref:=PB.ref();
         PB.use('pbxxxx');
         PB.prefix();
         {? PB.add()
         || {? var_pres('g_tot')>0 || g_cnt+=1 ?};
            PB_OP.index('PB');
            PB_OP.prefix(_vref);
            {? PB_OP.first()
            || {! |?
                  PB_OP.PB:=PB.ref();
                  PB_OP.cntx_psh();
                  PB_OP.use('popxxxx');
                  PB_OP.prefix();
                  PB_OP.add();
                  exec('upd_pb_zal','zaliczki');
                  PB_OP.cntx_pop();
                  PB_OP.next()
               !}
            ?}
         ?};
         PB.use('pb'+TT_ROK.ROK);
         PB.PR:='A';
         PB.TYT:=_old_tyt;
         {? PB.put()
         || PBHIST.index('PBHIST');
            PBHIST.prefix();
            PBHIST.blank();
            PBHIST.KTO:='PROCES: '+app_info('app_user');
            PBHIST.PB:=PB.ref();
            PBHIST.add()
         ?}
      ?};
      SKID_RBK.cntx_pop()
   ?}
?};
PB.cntx_pop()


\gb_przar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.40]
:: OPIS: Przed przywracaniem grupowym z archiwum.
::  OLD: \gb_przar/homebank.fml
::----------------------------------------------------------------------------------------------------------------------
g_cnt:=0; g_tot:=PB.sel_size();
{? g_tot & FUN.ask('Czy na pewno przywrócić zaznaczone przelewy\ndo ponownego wysłania?'@) || 1 || &g_cnt; &g_tot; 0 ?}


\ga_przar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.60]
:: OPIS: Formula po akcji grupowego zatwierdzania/anulowania zatwierdzenia przelewow.
::  OLD: \ga_przar/homebank.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba przywróconych przelewów: %1.'@[$g_cnt]);
&g_tot; &g_cnt; 1


\pb_hist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Wywoluje okno historii przywrocen przelewow (w archiwum przelewow)
::  OLD: \pb_hist/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
{? PB.PR<>'A'
|| FUN.emsg('Ten przelew nie był przywracany.'@);
   return(0)
?};
PBHIST.index('PBHIST');
PBHIST.prefix(PB.ref());
{? PBHIST.first()
|| PBHIST.select()
|| FUN.emsg('Nie znaleziono historii przywróceń dla tego przelewu.'@)
?};
PBHIST.prefix()


\raporty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wydruk przelewów
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','HBN_PRZ_ZBPR','hbn_pb1',,,,,,,,'W')


\rk_pb_arch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Rekord przed dla tabeli PB w maskach archiwalnych
::----------------------------------------------------------------------------------------------------------------------
_grayed:='';
{? PB.PR<>'A' || _grayed+='H' ?};
{? (PB.USTYPPL<>'' & PB.RODZ='KU') | PB.RODZ='KZ'
|| _grayed+='R'
|| PB_OP.index('PB');
   PB_OP.prefix(PB.ref());
   {? ~PB_OP.first() || _grayed+='R' ?}

?};
PB.actions_grayed('PB_WER2',_grayed);
''

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 d7c9018bfa8992878cd9679ccecd4707a224cf9075d5c2897d3c85e016ed3b69ac0e82033145607bec43ab386ed3c5f92beec4e9d48ddbd0622f15260f572b29034518fcb322b91a84887c40efa539a17bf7b6aeffab436f83a32b9039ac240329a7fb7eb45a2ecb49221a12a9fb887496b58197efe2d285561aca6cbffeee75

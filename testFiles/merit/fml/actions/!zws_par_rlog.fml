:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_rlog.fml
:: Utworzony: 14.02.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_RLOG - Ustawienia rejestru zmian.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Ustawienia rejestru zmian - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
SYSSLGMN.cntx_psh();
SYSSLGMN.use('0l0000');

SKID_LOG.cntx_psh();
SKID_LOG.clear();
SKID_LOG.index('REKSYM');
SKID_LOG.win_sel('GRP');
SKID_LOG.select();
SKID_LOG.cntx_pop();

SYSSLGMN.cntx_pop();
~~


\skid_log_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Przed dołączeniem lub poprawieniem wiersza tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? SKID_LOG.TYP='T'
|| {? SKID_LOG.GRUPA=''
   || SKID_LOG.GRUPA:='N'
   ?}
|| SKID_LOG.GRUPA:=''
?};
~~


\skid_log_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli SKID_LOG.
::   WE:
::   WY: zgodne ze specyfikacją MacroBASE
::----------------------------------------------------------------------------------------------------------------------
exec('skid_log_modb','!zws_par_rlog');
1


\skid_log_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli SKID_LOG.
::   WE:
::   WY: zgodne ze specyfikacją MacroBASE
::----------------------------------------------------------------------------------------------------------------------
exec('skid_log_modb','!zws_par_rlog');
1


\skid_log_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Wyzwalacz po poprawieniu wiersza tabeli SKID_LOG.
::   WE: _a - zgodny ze specyfikacją MacroBASE
::   WY: zgodne ze specyfikacją MacroBASE
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

{? SKID_LOG.TYP='T'
|| {? bfld('MASKA')<>SKID_LOG.MASKA
   || _maska:=SKID_LOG.MASKA;
      SKID_LOG.cntx_psh();
      SKID_LOG.index('REKSYM');
      SKID_LOG.prefix(#SKID_LOG.ref());
      _loop:=SKID_LOG.first();
      {!
      |? _loop
      |! SKID_LOG.MASKA:=_maska;
         SKID_LOG.put();
         _loop:=SKID_LOG.next()
      !};
      SKID_LOG.cntx_pop();
      SYSSLGMN.cntx_psh();
      SYSSLGMN.clear();
      SYSSLGMN.f_set(,,'MASK=\':_a\'',_maska);
      _loop:=SYSSLGMN.f_first();
      {!
      |? _loop
      |! SYSSLGMN.MASK:=SKID_LOG.MASKA;
         SYSSLGMN.put();
         _loop:=SYSSLGMN.f_next()
      !};
      SYSSLGMN.cntx_pop()
   ?}

|? SKID_LOG.TYP='F'
|| {? bfld('STATUS')<>SKID_LOG.STATUS
   || _status:=SKID_LOG.STATUS;
      _rekord:=#SKID_LOG.ref();
      SKID_LOG.cntx_psh();
      SKID_LOG.index('REKSYM');
      SKID_LOG.prefix(_rekord);
      _num:=1;
      {? SKID_LOG.last()
      || {? (_num:=#SKID_LOG.SYMBOL)=0
         || _num:=SKID_LOG.size()
         ?}
      ?};
      SKID_LOG.cntx_pop();
      SKID_LOG.blank(1);
      SKID_LOG.TYP:='L';
      SKID_LOG.REKORD:=_rekord;
      SKID_LOG.SYMBOL:=form(_num+1,-8,0,'99');
      SKID_LOG.STATUS:=_status;
      exec('zmien_opis','!zws_par_rlog');
      exec('zmien_info','!zws_par_rlog');
      SKID_LOG.add();
      SKID_LOG.seek(_rekord,);

      SYSSLGMN.clear();
      {? SYSSLGMN.find_key(SKID_LOG.MASKA,SKID_LOG.SYMBOL,)
      || {? SKID_LOG.STATUS<>'T'
         || SYSSLGMN.del()
         ?}
      |? SKID_LOG.STATUS='T'
      || SYSSLGMN.blank(1);
         SYSSLGMN.MASK:=SKID_LOG.MASKA;
         SYSSLGMN.ACR:=SKID_LOG.SYMBOL;
         SYSSLGMN.add()
      ?}
   ?}
?};
~~


\skid_log_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Wyzwalacz przed usunięciem wiersza tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',SKID_LOG,'REKSYM',#SKID_LOG.ref())


\exc_tabs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [GS] [2008]
:: OPIS: Zwraca listę tabel, dla których nie będą rejestrowane zmiany - nie będą one widoczne w oknie konfiguracji.
::       Lista musi mieć postać: ,ACR1,ACR2,...,ACRN, - ważne są przecinki na początku i końcu
::  OLD: \exc_tabs/skid_log.fml
::----------------------------------------------------------------------------------------------------------------------
',SKID_LOG,'
:: tabele techniczne
'BLK,DATY,DATYW,'
:: tytuły obszarów roboczych
'AREATIT,AREATPAR,'
:: elementy instancji procesów
'BI_ARCH,BI_CONN,BI_COUNT,BI_MSG,BI_PORT,BI_PREL,BI_PROC,BI_QUEUE,BI_STAT,BI_TODO,'
:: elementy definicji procesów
'B_ACTION,B_ARHIS,B_GATELE,B_KEYREF,B_PROUTE,B_TAB_WT,B_URHIS,B_USEHIS,B_USRDOM,'
:: analizy zleceń
'ANH,ANZMAT,ANZTLS,ANZTOU,ANZWRK,'
:: kalkulacje technologii
'KALMAT,KALTLS,KALTOU,KALTTO,KALWRK,KKALKH,'
:: stany
'OPAK_SM,OPAK_ZWR,SC,SL,SLD,SM,'
:: synchro
'SYNC,'
:: okresy
'OZ,'
:: parametry
'PARSES,WYDRUKIL,USRCONST,USR_SESS,'
:: plan dostaw
'PD_BUF,'
:: polskie znaki
'PL,'
:: rejestracja wykonań
'PROD_REJ,PROD_NXT,'
:: plan strategiczny
'PX_CON_G,PX_CON_O,PX_KOMM,PX_KOMU,PX_POZ,PX_SUR,PX_TASK,'
:: obsługa upgrade
'UPG_FIRM,UPG_HEAD,UPG_LOG,UPG_MSG,UPG_REAL,UPG_TASK,UPG_VER'
:: kolorowanie
'UST_KOL,UST_MWYK,UST_S,'
:: dane raportów zewnętrznych
'XR_INS,XR_PAR,'
:: zasoby dla projektów
'PROJDOST,PROJREZD,PROJREZE,PROJREZZ,'
:: składniki list płac i zbiorówek
'LS,ZB_DEF,ZB_WART,'
:: zarządzanie kapitałem ludzkim
'ZZ_WIKON,'
:: Tabele "tymczasowe" webTerm-a
'P_WEB_CX,PRC_WS,'
'WT_HTML,WT_NWU,WT_R,WT_TREE,WT_GROUP,WT_GPDWL,WT_ZOS,'


\update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Aktualizuje ustawienia na podstawie definicji.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('_a')<>type_of(0) | _a=0) &
   exec('check_crc_def','#table')=0
|| return()
?};

_ttab:=type_of(SYSLOG);
_exct:=exec('exc_tabs','!zws_par_rlog');
_prgs:=(var_pres('_a')<>type_of(0) | _a=0);
_info:='Trwa aktualizacja ustawień...'@;
_size:=tab_num();
_cnt:=0;

SKID_LOG.cntx_psh();
SKID_LOG.clear();
SKID_LOG.index('REKSYM');

SKID_LOG.f_set(,,'TYP=\'T\' or TYP=\'F\'');
_size+=SKID_LOG.f_size();

: pierwszy przebieg aktualizacji
: wyłączenie elementów usuniętych
_loop:=SKID_LOG.f_first();
{!
|? _loop
|! {? _prgs
   || progress((100*_cnt/_size)$0,_info)
   ?};
   {? SKID_LOG.TYP='T'
   || {? var_pres(SKID_LOG.SYMBOL)<>_ttab
      || {? SKID_LOG.STATUS<>'-'
         || SKID_LOG.STATUS:='-';
            SKID_LOG.put()
         ?}
      |? SKID_LOG.STATUS='-'
      || SKID_LOG.STATUS:='N';
         SKID_LOG.put()
      ?}
   |? SKID_LOG.TYP='F'
   || _exists:=0;
      SKID_LOG.cntx_psh();
      _fld:=SKID_LOG.SYMBOL;
      {? SKID_LOG.seek(SKID_LOG.REKORD,) & var_pres(SKID_LOG.SYMBOL)=_ttab
      || _exists:=var_pres(_fld,($SKID_LOG.SYMBOL)())
      ?};
      SKID_LOG.cntx_pop();
      {? ~_exists
      || {? SKID_LOG.STATUS<>''
         || SKID_LOG.STATUS:='-';
            SKID_LOG.put()
         ?}
      |? SKID_LOG.STATUS='-'
      || SKID_LOG.STATUS:='N';
         SKID_LOG.put()
      ?}
   ?};
   _cnt+=1;
   _loop:=SKID_LOG.f_next()
!};
SKID_LOG.f_clear();

: drugi przebieg aktualizacji
: uzupełnienie o nowe elementy
{! _ti:=1..tab_num()
|! _acr:=tab_acr(_ti);
   {? _prgs
   || progress((100*_cnt/_size)$0,_info)
   ?};
   {? tab_real(_ti) & _exct*(','+_acr+',')=0 & exec('tab_sys','#table',_acr)=0
   || _ref:=0;
      _TAB:=($_acr)();
      _mdb:=_TAB.name(1);
      _com:=_TAB.comment();
      {? SKID_LOG.find_key(0,_acr,)
      || _ref:=#SKID_LOG.ref();
         {? SKID_LOG.OPIS<>_com |
            SKID_LOG.MASKA<>_mdb
         || SKID_LOG.OPIS:=_com;
            SKID_LOG.MASKA:=_mdb;
            SKID_LOG.put()
         ?}
      || SKID_LOG.blank(1);
         SKID_LOG.TYP:='T';
         SKID_LOG.SYMBOL:=_acr;
         SKID_LOG.STATUS:='N';
         SKID_LOG.OPIS:=_com;
         SKID_LOG.MASKA:=_mdb;
         {? SKID_LOG.add()
         || _ref:=#SKID_LOG.ref()
         ?}
      ?};
      {? _ref<>0
      || {! _fi:=1.._TAB.fld_num()
         |! _acr:=_TAB.fld_acr(_fi);
            _com:=_TAB.fld_comm(_fi);
            {? SKID_LOG.find_key(_ref,_acr,)
            || {? SKID_LOG.OPIS<>_com
               || SKID_LOG.OPIS:=_com;
                  SKID_LOG.put()
               ?}
            || SKID_LOG.blank(1);
               SKID_LOG.TYP:='F';
               SKID_LOG.REKORD:=_ref;
               SKID_LOG.SYMBOL:=_acr;
               SKID_LOG.STATUS:='N';
               SKID_LOG.OPIS:=_com;
               SKID_LOG.MASKA:=_mdb;
               SKID_LOG.add()
            ?}
         !}
      ?};
      &_TAB
   |? SKID_LOG.find_key(0,_acr,)
   || SKID_LOG.del()
   ?};
   _cnt+=1
!};

SKID_LOG.cntx_pop();
prgs_clr();
~~


\setup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Przygotowuje konsolę ustawień do działania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SKID_LOG.win_fml('WER',SKID_LOG,'SYMBOL',,'ICON_BEFORE',"exec('cfg_icon','!zws_par_rlog')");
SYSSLGMN.index(SYSSLGMN.ndx_tmp(,1,'MASK',,,'ACR',,));
~~


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Uzupełnia konfigurację na podstawie ustawień.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_info:='Trwa weryfikacja konfiguracji...'@;
_cnt:=0;
SYSSLGMN.cntx_psh();
SYSSLGMN.clear();
SKID_LOG.cntx_psh();
SKID_LOG.index('STATUS');
SKID_LOG.prefix('F');
_size:=SKID_LOG.size();
_loop:=SKID_LOG.first();
{!
|? _loop
|! progress((100*_cnt/_size)$0,_info);
   {? SYSSLGMN.find_key(SKID_LOG.MASKA,SKID_LOG.SYMBOL,)
   || {? SKID_LOG.STATUS<>'T'
      || SYSSLGMN.del()
      ?}
   |? SKID_LOG.STATUS='T'
   || SYSSLGMN.blank(1);
      SYSSLGMN.MASK:=SKID_LOG.MASKA;
      SYSSLGMN.ACR:=SKID_LOG.SYMBOL;
      SYSSLGMN.add()
   ?};
   _cnt+=1;
   _loop:=SKID_LOG.next()
!};
SKID_LOG.cntx_pop();
SYSSLGMN.cntx_pop();
prgs_clr();
~~


\cfg_icon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Ustala ikonę właściwą dla gałązki drzewa.
::   WE:
::   WY: nazwa ikony
::----------------------------------------------------------------------------------------------------------------------
'xwin16.png:'+$
   {? SKID_LOG.STATUS='T'
   || 38
   |? SKID_LOG.STATUS='-'
   || 8
   || {? SKID_LOG.TYP='T'
      || 125
      |? SKID_LOG.TYP='F'
      || 190
      || 81
      ?}
   ?}


\skid_log_grp_of
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Wypełnienie okienka GRP tabeli SKID_LOG.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('update','!zws_par_rlog');
exec('setup','!zws_par_rlog');
exec('init','!zws_par_rlog');
1


\skid_log_wer_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Po odświeżeniu okienka WER w oknie grupowym GRP tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_hide:='';
{? SKID_LOG.sel_size()=0
|| {? exec('skid_log_zmien_b','!zws_par_rlog')
   || {? SKID_LOG.STATUS='T'
      || _hide:='W'
      || _hide:='Y'
      ?}
   || _hide:='WY'
   ?};
   {? SKID_LOG.TYP<>'T' | SKID_LOG.STATUS='-'
   || _hide+='I(F)I(G)'
   |? SKID_LOG.GRUPA<>'T'
   || _hide+='I(F)'
   |? SKID_LOG.GRUPA='T'
   || _hide+='I(G)'
   ?}
?};
SKID_LOG.actions_grayed('WER',_hide);
~~


\skid_log_zmien_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Przed modyfikacją zapisu w konfiguracji.
::   WE:
::   WY: 1 - zapis może być zmieniony
::       0 - zapisu nie można modyfikować
::----------------------------------------------------------------------------------------------------------------------
{? SKID_LOG.STATUS='-'
|| return(0)
?};

SKID_LOG.TYP='T' | SKID_LOG.TYP='F'


\skid_log_zmien_gb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Przed modyfikacją grupy zapisów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
sel_nchk()


\skid_log_zmien_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Modyfikacja zapisu w konfiguracji.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_status:=SKID_LOG.STATUS;
{? _status='T'
|| _status:='N'
|? _status='N'
|| _status:='T'
|| return()
?};

SKID_LOG.STATUS:=_status;
exec('zmien_info','!zws_par_rlog');
SKID_LOG.clear();
SKID_LOG.put();

{? SKID_LOG.TYP='T'
|| SKID_LOG.cntx_psh();
   SKID_LOG.index('REKSYM');
   SKID_LOG.prefix(#SKID_LOG.ref());
   _loop:=SKID_LOG.first();
   {!
   |? _loop
   |! SKID_LOG.STATUS:=_status;
      exec('zmien_info','!zws_par_rlog');
      SKID_LOG.cntx_psh();
      SKID_LOG.prefix();
      SKID_LOG.put();
      SKID_LOG.cntx_pop();
      _loop:=SKID_LOG.next()
   !};
   SKID_LOG.cntx_pop()

|? SKID_LOG.TYP='F'
|| SKID_LOG.cntx_psh();
   SKID_LOG.clear();
   {? SKID_LOG.seek(SKID_LOG.REKORD,) & SKID_LOG.STATUS<>_status
   || _mod:=0;
      {? _status='T'
      || _mod:=1
      |? _status='N'
      || SKID_LOG.f_set(,,'REKORD=:_a and STATUS<>\':_b\'',#SKID_LOG.ref(),_status);
         _mod:=(SKID_LOG.f_size()=0);
         SKID_LOG.f_clear()
      ?};
      {? _mod
      || SKID_LOG.STATUS:=_status;
         exec('zmien_info','!zws_par_rlog');
         SKID_LOG.put()
      ?}
   ?};
   SKID_LOG.cntx_pop()
?}


\skid_log_wlacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Przed akcją "Włącz" okienka WER tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('skid_log_zmien_b','!zws_par_rlog') & SKID_LOG.STATUS='N'


\skid_log_wlacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Po akcji "Włącz" okienka WER tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('skid_log_zmien_a','!zws_par_rlog')


\skid_log_wylacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Przed akcją "Wyłącz" okienka WER tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('skid_log_zmien_b','!zws_par_rlog') & SKID_LOG.STATUS='T'


\skid_log_wylacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Po akcji "Wyłącz" okienka WER tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('skid_log_zmien_a','!zws_par_rlog')


\skid_log_firma_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Przed akcją "Tylko dla firmy" okienka WER tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SKID_LOG.TYP='T' & SKID_LOG.GRUPA='T'


\skid_log_firma_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Po akcji "Tylko dla firmy" okienka WER tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SKID_LOG.GRUPA:='N';
SKID_LOG.put()


\skid_log_grupa_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Przed akcją "Dla całej grupy" okienka WER tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SKID_LOG.TYP='T' & SKID_LOG.GRUPA<>'T'


\skid_log_grupa_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Po akcji "Dla całej grupy" okienka WER tabeli SKID_LOG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SKID_LOG.GRUPA:='T';
SKID_LOG.put()


\skid_log_konfiguracja_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Formuła akcji "Konfiguracja".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__WND','#object');

_acr:='WER';
_wnd:=__WND.SEL.get(SYSSLGMN,_acr);
{? _wnd=''; 1
|| _wnd:=SYSSLGMN.mk_sel('Konfiguracja'@,'T',1,-'SYSSLGMN_WER',,,20,,'U');
   SYSSLGMN.win_act(_wnd,,'Formuła','Wyłącz'@,,,
      "exec('szukaj_skid_log','!zws_par_rlog','F','T',SYSSLGMN.MASK,SYSSLGMN.ACR)",
      "exec('skid_log_wylacz_a','!zws_par_rlog')",,
      1,
      "sel_nchk()"
   );
   SYSSLGMN.win_act(_wnd,,'Wyświetl',,,,
      "exec('szukaj_skid_log','!zws_par_rlog','F','T',SYSSLGMN.MASK,SYSSLGMN.ACR)",
      "exec('wyswietl_skid_log','!zws_par_rlog')"
   );
   SYSSLGMN.win_btn(_wnd,'text=%1'['Wyłącz'@],'menu:W');
   __WND.SEL.put(SYSSLGMN,_wnd,_acr)
?};
SKID_LOG.cntx_psh();
SYSSLGMN.win_sel(_wnd);
SYSSLGMN.select();
SKID_LOG.cntx_pop();
~~


\skid_log_legenda_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Formuła akcji "Legenda".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','#SKID_LOG#01')


\szukaj_skid_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Wyszukuje według klucza 'STATUS' wiersz w tabeli SKID_LOG
::   WE: _a - typ zapisu
::       _b - status
::       _c - maska
::       _d - symbol
::   WY: wskazanie znalezionego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
{? _c='' | _d=''
|| return(null)
?};
SKID_LOG.index('STATUS');
SKID_LOG.prefix(_a,_b);
{? SKID_LOG.find_key(_c,_d)
|| SKID_LOG.ref()
|| null
?}


\wyswietl_skid_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Wyświetla informacje z tabeli SKID_LOG właściwe dla bieżącego rekordu tabeli SYSSLGMN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_acr:='';
_com:='';
SKID_LOG.cntx_psh();
SKID_LOG.clear();
{? SKID_LOG.seek(SKID_LOG.REKORD,)
|| _acr:=SKID_LOG.SYMBOL;
   _com:=SKID_LOG.OPIS
?};
SKID_LOG.cntx_pop();

FUN.info(
   'Dnia %1 o godzinie %2 użytkownik %3 włączył śledzenie modyfikacji danych.\n\n'
   'Tabela: %4 %5\n'
   'Pole: %6 %7'@
   [$SKID_LOG.MODDATA,$SKID_LOG.MODCZAS,SKID_LOG.MODKTO,_acr,_com,SKID_LOG.SYMBOL,SKID_LOG.OPIS]
)


\zmien_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Ustala opis na podstawie statusu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_status:=SKID_LOG.STATUS;
_message:=' rejestrowanie modyfikacji zawartości';
SKID_LOG.OPIS:=
   {? _status='T' || 'włączono'+_message
   |? _status='N' || 'wyłączono'+_message
   |? _status='-' || 'usunięto z definicji systemu'
   || 'wprowadzono nieznane ustawienie konfiguracji'
   ?}


\zmien_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.14]
:: OPIS: Aktualizuje pola informacyjne MODKTO, MODDATA, MODCZAS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SKID_LOG.MODKTO:=exec('username','#users');
SKID_LOG.MODDATA:=date();
SKID_LOG.MODCZAS:=time();
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:07 06de040be42bbb0fc15ee902c01ef46efeea4b94f2b0a96111df624ef9d678e49557d72f30cd5cc78a7c4c4175ad9ff865a83ee681c0bf5dad672031eb2dd88f1195edb094b04e6e3fbdfa30978b803db3f2d5dcff70db2f29133fe3f05754388044c3c5112848f728e87fbe52b1ac4a73aa96214d50e50e3701e087fd67b370

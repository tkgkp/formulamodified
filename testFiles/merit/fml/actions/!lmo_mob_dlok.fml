:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmo_mob_dlok.fml
:: Utworzony: 15.03.2016
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Czynność LMO_MOB_DLOK - Operacja zmiany lokalizacji i terminów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=LOOP
::# permissions=ODDZ
::# parses=exec('parses_dk_ln','lmg')
::# kind=WE,  symbol=DK_LN,   type=_DK_LN,   name=Dokument reorganizacji,        required=T,   keyref=T
::# kind=WE,  symbol=NRC,     type=NUMBER,   name=Nr czytnika,                   required=N,   fml_val="EANX.NRC_Z:=0;exec('wyb_czyt','magazyn_mob',1,'EANX.NRC_Z');EANX.NRC_Z"
::# kind=WE,  symbol=GEN_OPE, type=STRING,   name=Generacja operacji,            required=N
::# kind=WEW, symbol=EANN,    type=_EANN,    name=Operacja mobilna,              required=N
::# kind=WY,  symbol=EANN,    type=_EANN,    name=Operacja mobilna,              required=N
::# kind=WY,  symbol=GEN_OPE, type=STRING,   name=Generacja operacji,            required=N
::# condition=Rejestracja dokumentu reorganizacji, act_uid=LMG_MAG_DREO,   auto=T,  formula=_a.EANN<>~~ & _a.EANN<>null()
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

exec('init','lmo');

_akcja:=_mp.akcja();
_auto:=_akcja<>'Do wysyłki' & _mp.isAutoRun();

_mp.trigRef('EANN',,1,,exec('kind_internal','#b_port'),'EANN');

_nrc:={? var_pres('NRC',_in)=type_of(0) || _in.NRC || -1 ?};

{? ~(var_pres('DK_LN',_in)=type_of(null()) & _in.DK_LN)
|| _mp.error('Brak wymaganego parametru DK_LN.')
||
   _continue:=0;
   _gen_ope:='';
   _ctrl:=1;

   {? _mp.loop()=0
:: pierwsze wywołanie
   || DK_LN.cntx_psh();
      _jest:=DK_LN.seek(_in.DK_LN);
      {? ~_jest || DK_LN.prefix(); _jest:=DK_LN.seek(_in.DK_LN) ?};
      {? ~_jest
      || _mp.error('Nie znaleziono dokumentu reorganizacji.')
      |? ~exec('ctrlDK_LN','!lmo_mob_dlok')
      || _ctrl:=0;
         _mp.cancel()
      || _answer:=
         {? _akcja='Do wysyłki'
            | _auto
            | _mp.pathTodo()
         || _grp_key:=exec('wysdk_ln','!lmo_mob_dlok',_auto,_nrc);

            _internal:=_mp.load(exec('kind_internal','#b_port'));
            _continue:={? var_pres('EANN',_internal)=type_of(null()) & _internal.EANN || 1 || -1 ?};
            {? _continue=1
            || _gen_ope:=exec('FindAndGet','#table',EANN,_internal.EANN,,"GRP_KEY",'')
            ?}
         || _mp.error('Nieobsługiwana ścieżka.')
         ?}
      ?};
      DK_LN.cntx_pop()
:: wywołanie w pętli
   || _continue:=var_pres('GEN_OPE',_in)=type_of('') & _in.GEN_OPE<>'';
      {? _continue || _gen_ope:=_in.GEN_OPE ?}
   ?};

   {? _continue=-1
:: Wycofano się z wystawiania operacji
   || 1
   |? _continue=1 & _gen_ope<>''
:: Zapisanie parametru wyjściowego EANN, wykluczenie kolejnej realizacji z pętli, zakończenie czynności
   || EANN.cntx_psh();
      EANN.prefix();
      _grp_key:=_gen_ope-1;
      _grp_key_on:=_grp_key+'1';
      _grp_key_off:=_grp_key+'0';
      {? _grp_key<>''
      || EANN.index('GRP_KEY');
         EANN.prefix(_grp_key_on);
         {? EANN.first()
         || EANN.cntx_psh();
            EANN.prefix();
            EANN.GRP_KEY:=_grp_key_off;
            do();
            EANN.put();
            _mp.save(exec('kind_out','#b_port'),'EANN',EANN.ref());
            _mp.save(exec('kind_out','#b_port'),'GEN_OPE',_gen_ope);
            end();
            EANN.cntx_pop();
            {? EANN.first()
::          kontynuacja pętli
            || _mp.loop_continue()
            ?}
         ?}
      ?};
      EANN.cntx_pop();
      _mp.done()
   |? _ctrl
   || _mp.error('Brak oczekiwanego parametru GEN_OPE.')
   ?}
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_in','#b_port'));

_wgr:={? var_pres('DK_LN',_in)<>type_of(~~) & _in.DK_LN
      || _in.DK_LN
      || null()
      ?};

{? _wgr<>null()
|| 'Zarejestruj operację mobilną dla reorganizacji %1'@@[exec('record','#to_string',_wgr)]
|| 'Zarejestruj operację mobilną'@@
?}


\dk_ln2eann
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła Dołaczenia Reorganizacji
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LMO_MOB_DLOK';
_params.AKCJA:='Do wysyłki';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DK_LN',DK_LN.ref());
exec('mp_run','#b__box',_params)


\wysdk_ln
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: wysylka reorganizacji na urzadzenie mobilne
::   WE: _a - 0-pytamy lub 1-nie
::       _b - numer czytnika
::   WY: GRP_KEY- klucz grupujący lub pusty string
::  OLD: \wysdk_ln/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_lock:=1;
{? exec('get','#params',4022,2,OPERATOR.USER)='T' & _b<=0 || _b:=0 ?};
{? ~DK_LN.r_lock(1,1,1)
|| _lock:=0;
   {? ~_a & FUN.ask('Dokument reorganizacji obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@) & DK_LN.r_lock(1,,1)
   || DK_LN.r_unlock()
   ?};
   0
|? _a | FUN.ask('Czy wygenerować zadanie zmiany lokalizacji na urządzenie mobilne?'@)
|| VAR_DEL.delete('__eanc');

   _nrc:=-1;
   {? (_b>0 & exec('FindInSet','#table','EANC','AKT',_b,'T') | _b=0)
   || _nrc:=EANX.NRC_Z:=_b;
      _ideanc:=EANX.IDEANC_Z:=exec('FindInSet','#table','EANC','AKT',_b,'T',"EANC.IDADD",,,'')
   || {? exec('wyb_czyt','magazyn_mob',1,'EANX.NRC_Z','EANX.IDEANC_Z',1,DK_LN.MG)
      || _nrc:=EANX.NRC_Z;
         _ideanc:=EANX.IDEANC_Z
      ?}
   ?};
   _grp_key:=tm_stamp()+'1';

   {? _nrc>=0
   || {? DK_LN.RODZ<>'I'
      || DK_L.index('DK_LNMOB');
         DK_L.prefix('Z',DK_LN.ref());
         {? DK_L.first()
         || _czypal:=DK_LN.MG().PAL='T';
            _rozpa:=DK_L.ZPALET=2;
            do();
            EANN.clear();
            EANN.blank();
            EANN.DATA:=date();
            EANN.TYP:='R';
            EANN.REFSQL:=$DK_LN.ref();
            EANN.UNIK:=$DK_LN.ref();
            EANN.IDZAM:=DK_LN.IDADD;
            EANN.SYM:=form(form(DK_LN.DT,,3)+' '+form(DK_LN.TM,,3)+' '+DK_LN.US().KOD);
            EANN.KH:=null;
            EANN.ODB:=null;
            EANN.NRC:=_nrc;
            EANN.IDEANC:=_ideanc;
            EANN.TIME:=time();
            EANN.STAN:='!';
            EANN.MG:=DK_LN.MG;
            EANN.GRP_KEY:=_grp_key;
            EANN.RODZ:={? _rozpa || 'W' || 'W'+{? DK_LN.MG().PAL='T' || 'P' || '' ?} ?};
            {? _rozpa || EANN.PALEAN:=DK_L.PALDO().KODK ?};
            {? EANN.add(1)
            || EANN.SYM:='RE/'+gsub(19+EANN.IDADD,'/','');
               EANN.UNIK:=$EANN.ref();
               _lp:=0;
               {? ~EANN.put() || undo() || _wyn:=EANN.GRP_KEY ?};
               {!
               |? {? _czypal | form(DK_L.M().KODK)<>''
                  || _zpalet:=DK_L.ZPALET;
                     _lp+=1;
                     EANP.clear();
                     EANP.blank;
                     EANP.EANN:=EANN.ref();
                     EANP.NRC:=EANN.NRC;
                     EANP.LOKZ:=DK_L.LOK;
                     EANP.LOKDO:=DK_L.LOKDO;
                     EANP.M:={? _czypal & ~_zpalet || null || DK_L.M ?};
                     EANP.EAN:={? _czypal & ~_zpalet || DK_L.PAL().KODK || DK_L.M().KODK ?};
                     EANP.IL:={? _czypal & ~_zpalet || 1 || DK_L.IL ?};
                     EANP.ILS:=0;
                     EANP.LP:=_lp;
                     EANP.RSQL:=DK_L.IDADD;
                     EANP.PAL:=DK_L.PAL;
                     EANP.PALEAN:=DK_L.PAL().KODK;
                     EANP.PALDO:=DK_L.PALDO;
                     EANP.PALDOEAN:=DK_L.PALDO().KODK;
                     EANP.ZPALET:=DK_L.ZPALET;
                     EANP.BEZOZN:={? EANP.PALDO<>null || EANP.PALDO().BEZOZN || 'N' ?};
                     EANP.TYMCZAS:={? (1+EANP.BEZOZN)='T' || EANP.PALDOEAN || '' ?};
                     EANP.SCEAN:=DK_L.SCEAN;
                     EANP.TW:=DK_L.TW;
                     EANP.IL2:={? DK_L.M().J2<>null() || DK_L.IL2 || 0 ?};
                     EANP.ILS2:=0;
                     EANP.IS_RM:='R';
                     exec('uzupIDkod','magdok_palety',EANP);
                     {? EANP.add(1)
                     || EANP.UNIK:=(($EANP.EANN)+8)+(form(8+$EANP.ref())+3)+($EANP.ref()+8);
                        {? ~EANP.put() || undo() ?}
                     || undo()
                     ?}
                  ?};
                  {? DK_L.PAL<>null & DK_L.ZPALET=2
                  || PAL_POZ.cntx_psh();
                     {? DK_L.PAL().AKT='N'
                     || PAL_POZ.use('palet'+form(DK_L.PAL().AR-2000,-2,0,'99'))
                     || PAL_POZ.use('paletyp')
                     ?};
                     PAL_POZ.index('PAL');
                     PAL_POZ.prefix(DK_L.PAL);
                     {? PAL_POZ.first()
                     || {!
                        |? {? PAL_POZ.M=DK_L.M
                           || PAL_POZ.AKT:='N'; PAL_POZ.put(1);
                              PAL_POZ.AKT:='T'; PAL_POZ.put(1)
                           ?};
                           PAL_POZ.next()
                        !}
                     ?};
                     PAL_POZ.cntx_pop()
                  ?};
                  DK_L.next()
               !};
               EANN.STAN:='O';
               EANN.ILP:=exec('addilp','magazyn_mob',EANN.ref());
               {? ~EANN.put()
               || undo()
               || exec('akt_R_M','magazyn_mobi',EANN.ref())
               ?}
            || undo()
            ?};
            end()
         ?}
      || DK_LI.index('DK_LN');
         DK_LI.prefix(DK_LN.ref());
         {? DK_LI.first()
         || do();
            {!
            |? EANN.clear();
               EANN.blank();
               EANN.DATA:=date();
               EANN.TYP:='R';
               EANN.REFSQL:=$DK_LN.ref();
               EANN.IDZAM:=DK_LN.IDADD;
               EANN.UNIK:=$DK_LN.ref();
               EANN.SYM:=form(form(DK_LN.DT,,3)+' '+form(DK_LN.TM,,3)+' '+DK_LN.US().KOD);
               EANN.KH:=null;
               EANN.ODB:=null;
               EANN.NRC:=_nrc;
               EANN.IDEANC:=_ideanc;
               EANN.TIME:=time();
               EANN.STAN:='O';
               EANN.ILP:=0;
               EANN.EANL:=DK_LI.EANL;
               EANN.MG:=DK_LN.MG;
               EANN.GRP_KEY:=_grp_key;
               EANN.RODZ:='W'+{? DK_LN.MG().PAL='T' || 'P' || '' ?};
               {? EANN.add(1)
               || EANN.SYM:='RE/'+gsub(19+EANN.IDADD,'/','');
                  EANN.UNIK:=$EANN.ref();
                  {? ~EANN.put() || undo() || _wyn:=EANN.GRP_KEY ?}
               ?};
               DK_LI.next()
            !};
            end()
         ?}
      ?}
   || FUN.info('Nie wybrano czytnika.\nOperacja niemożliwa.'@)
   ?};
   VAR_DEL.delete('__eanc')
?};
{? _lock || DK_LN.r_unlock() ?};
_wyn


\spr_rlok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009+]
:: OPIS: sprawdza czy wszystkie pozycje dokumentu maja kod kreskowy
::   WE: _a - ref ND
::   WY: 1-maja 0-nie maja
::  OLD: \spr_rlok/ean.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
DK_L.cntx_psh();
DK_L.index('DK_LN');
DK_L.prefix('Z',_a);
{? DK_L.first()
|| {!
   |? _wyn:=form(DK_L.M().KODK)<>'';
      _wyn & DK_L.next()
   !}
?};
DK_L.cntx_pop();
_wyn


\ctrlDK_LN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: sprawdza czy daną reorganizację można przesłać na urządzenie
::   WY: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? DK_LN.RODZ='D'
|| FUN.info('Operacja niedostępna dla reorganizacji powiązanej z depaletyzacją.'@)
|? DK_LN.AKC='T'
|| FUN.info('Operacja dostępna dla reorganizacji niezaakceptowanej.'@)
|? DK_LN.RODZ='I' & exec('czy_zinw','magdok_wspolne',$DK_LN.ref())
|| FUN.info('Dokument wygenerowany automatycznie z arkusza inwentaryzacyjnego.\n'
            'Wysłanie na urządzenie mobilne niemożliwe.'@)
|? (DK_LN.RODZ<>'I' & exec('FindInSet','#table','DK_L','DK_LN',DK_LN.ref(),'Z')=null) |
   (DK_LN.RODZ='I' & exec('FindInSet','#table','DK_LI','DK_LN',DK_LN.ref())=null)
|| FUN.info('Brak pozycji na reorganizacji.'@)
|? exec('FindInSet','#table','EANN','AKT',$DK_LN.ref(),'T')
|| FUN.info('Reorganizacja już została wysłana na urządzenie mobilne.'@)
|? DK_LN.RODZ='C'
|| FUN.info('Reorganizacja powstała na podstawie danych z urządzenia mobilnego.'@)
|? exec('FindInSet','#table','EANN','AKT',$DK_LN.ref(),'N',"EANN.STAN",,,'')='+'
|| FUN.info('Reorganizacja już została zatwierdzona na urządzeniu mobilnym.'@)
|? ST.MAG().PAL='N' & ~exec('spr_rlok','!lmo_mob_dlok',DK_LN.ref())
|| FUN.info('Istnieją pozycje reorganizacji bez przypisanych kodów kreskowych.\n'
            'Wysłanie na urządzenie mobilne niemożliwe.'@)
|? ~exec('komropal','magdok_wymiary',DK_LN.ref(),0)
|| 0
|| _wyn:=1
?};
_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 dd011c362a08987cb4383eb3364a911aba2f7de9f96b8fda48164b60eb8d5251746babb3dfbefc777ed623a694a11e43a2f0131ad67fcce055448848116fbd0bb1ef499fad6af58eba1e40878dd24a74dd08ca0199f2f3096721e88f2b805fde26a87c8e884c2c9b361e670b024d1039ee35b9800c77c3ff78fc773b76f99124

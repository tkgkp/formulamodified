:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdw_pucz.fml
:: Utworzony: 12.12.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności PSZ_SDW_PUCZ - Potwierdzenia uczestnictwa.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Potwierdzenia uczestnictwa - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('env_wt','psz')
|| return()
?};

SZK_PRAC.cntx_psh();
SZK_PRAC.f_clear();
SZK_PRAC.index('PRAC_DAT');
SZK_PRAC.prefix(P.ref());
SZK_PRAC.web_select('WEBPOTW');
SZK_PRAC.cntx_pop();
~~


\szk_prac_webpotw_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Po odświeżeniu okna WEBPOTW tabeli SZK_PRAC.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Wstępnie blokujemy wszystkie akcje.
_ag:='PZ';

SZK_OPIS.cntx_psh();
SZK_OPIS.prefix();
{? SZK_PRAC.get() & (SZK_PRAC.SZKOL().POTW_DO=date(0,0,0) | date()<SZK_PRAC.SZKOL().POTW_DO)
|| _ag:='';
   {? SZK_PRAC.EMAIL_PO='T'
   || _ag+='P'
   |? SZK_PRAC.EMAIL_PO='N'
   || _ag+='Z'
   ?}

?};
SZK_OPIS.cntx_pop();

web_top_tab(0,0).web_win_opt(web_top_win(0,0),web_top_ident(0),'grayed=%1' [_ag]);
~~


\szk_prac_webpotw_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WEBPOTW tabeli SZK_PRAC.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SZK_PRAC.SZKOL();
~~


\szk_prac_webpotw_statuspotw_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Przed wyświetleniem pola POLA_GRP.TXT_1 wyświetlającego status potwierdzenia
::       (na podstawie pola SZK_PRAC.EMAIL_PO).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_stat:=SZK_PRAC.EMAIL_PO;
POLA_GRP.TXT_1:=
   {? _stat='T'
   || 'Potwierdzone'@
   |? _stat='N'
   || 'Rezygnacja'@
   |? _stat='X'
   || 'Brak informacji'@
   || ''
   ?};
~~


\szk_prac_webpotw_potwierdz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Obsługa akcji "Potwierdź" w oknie WEBPOTW tabeli SZK_PRAC.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~(exec('env_wt','psz') & exec('exist','#record'))
|| FUN.emsg('Ustalenie środowiska pracy nie było możliwe.'@);
   return()
?};
{? SZK_PRAC.EMAIL_PO<>'T'
|| SZK_PRAC.EMAIL_PO:='T';
   SZK_PRAC.POTW_KO:='';
   SZK_PRAC.put();
   SZK_PRAC.web_refresh()
?};
~~


\szk_prac_webpotw_zrezygnuj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Obsługa akcji "Zrezygnuj" w oknie WEBPOTW tabeli SZK_PRAC.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~(exec('env_wt','psz') & exec('exist','#record'))
|| FUN.emsg('Ustalenie środowiska pracy nie było możliwe.'@);
   return()
?};
{? SZK_PRAC.EMAIL_PO<>'N'
|| SZK_PRAC.web_edit('WEBPOTW',,,,
      "  web_top_close(0);
         {? _a='OK'
         || {? exec('env_wt','psz')
            || SZK_PRAC.EMAIL_PO:='N';
               SZK_PRAC.put();
               SZK_PRAC.web_refresh(web_top_win(1))
            ?}
         ?}
      "
   )
?};
~~

:Sign Version 2.0 jowisz:1045 2020/04/03 17:04:51 b6f690156e6f4b70caace3ec409a6d5a765222aa0a9013b6b117286560952b8104404dcc7b031a7b09d3b9f6c38d5cc989c55f4359cf783c7528498d45477f87658462190f41bac538d3c50dde482765a38300d78c5ee98f1ad586945bbf014e556479425b587ebaa4547c867dfccae757855d0e84d19c1b86a5d8fedb1529d7

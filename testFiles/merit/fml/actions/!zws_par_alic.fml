:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zws_par_alic.fml [17.00]
:: Utworzony: 2013/09/18
:: Autor: AMK
::======================================================================================================================
:: Zawartosc: Formuly czynnosci wprowadzania danych licencjobiorcy
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wprowadzanie danych licencjobiorcy - glowna formula czynnosci
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
VAR.REF_POW:=null;
exec('RB','object');
KST.win_edit('PROCES');
KST.hdr_edit('Dane licencjobiorcy'@);
US.cntx_psh(); US.win_dict('WER'); US.win_edit('RED');
KRAJE.cntx_psh(); KRAJE.win_dict('WER'); KRAJE.win_edit('RED');
ADRES.cntx_psh(); ADRES.win_dict('WER'); ADRES.win_edit('RED');
exec('czytaj','#stalesys',,KST,XINFO);
_nip:=KST.NIP;
_skrot:=KST.SKROT;
_nazwa:=KST.NAZWA;
_kraj:=KST.KRAJ;

{? PAR_SKID.get(34)='P'
|| KST.efld_opt('PROCES','editable=grayed',XINFO,'KAP_ZAK')
|| KST.efld_opt('PROCES','editable=1',XINFO,'KAP_ZAK')
?};
{? PAR_SKID.get(34)<>'A'
|| KST.efld_opt('PROCES','editable=grayed',XINFO,'KAP_WPL')
|| KST.efld_opt('PROCES','editable=1',XINFO,'KAP_WPL')
?};

_dataod:=exec('edytuj','#stalesys',KST,'PROCES',"exec('spr_dlic','!zws_par_alic')");
{? var_pres('_dataod')=type_of(date()) & _dataod<>date(0,0,0) || _od:=_dataod || _od:=1 ?};
{? exec('jest','#stalesys',KST)
|| SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix('WALUTY');
   {? SLU.first() & XINFO.SLWAL=null()
   || XINFO.SLWAL:=SLU.ref();
      exec('zapisz','#stalesys',_od,XINFO,'SLWAL')
   ?};
   SLU.cntx_pop();
   {? var_pres('WIELKOSC',XINFO)>0
   || exec('zapisz','#stalesys',_od,XINFO,'WOJ','WIELKOSC')
   || exec('zapisz','#stalesys',_od,XINFO,'WOJ')
   ?};
   exec('zapisz','#stalesys',_od,XINFO,'WOJ');
   exec('zapisz','#stalesys',_od,XINFO,'KAP_ZAK','KAP_WPL');
   exec('zapisz','#stalesys',_od,KST,'JST');
   ~~
?};
KST.hdr_edit();
VAR.REF_POW:=null();
US.cntx_pop(); KRAJE.cntx_pop(); ADRES.cntx_pop();
params_get().mp.done();
~~


\spr_dlic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Weryfikacja poprawnosci danych licencjobiorcy
::   WY: '' jesli dane sa poprawne, akronim pola zmiennej jesli dane sa bledne
::----------------------------------------------------------------------------------------------------------------------
_spr:=__CHK.record(KST,,'NAZWA','SKROT','KRAJ','WOJEWODZ','POWIAT','GMINA','MIASTO','DOM','KOD');
{? _spr<>'' || return(_spr) ?};

 _spr:=__CHK.record(XINFO,,'WIELKOSC');
{? _spr<>'' || return(_spr) ?};

_spr:=__CHK.record(KST,,'KRAJ','WOJEWODZ','POWIAT','GMINA','MIASTO','DOM','KOD');
{? _spr='' || return(_spr) ?};

{? -KST.KRAJ().SYM='pl'
|| {? KST.KOD='  -'
   || __CHK.err_fld(KST,'KOD');
      return('KOD')
   ?};
   {? +KST.KOD<>6
   || FUN.emsg('Pole "Kod" nie jest prawidłowo wypełnione.'@);
      return('KOD')
   ?}
?};
_spr:=__CHK.record(KST,,'POCZTA','NIP','REG');
{? _spr<>'' || return(_spr) ?};

{? KST.PODFIZ
|| _spr:=__CHK.record(KST,,'PODNAZ','PODIME','PODURDAT');
   {? _spr<>'' || return(_spr) ?}
?};


{? -KST.KRAJ().SYM='pl'
|| {? +KST.KKOD
   || {? KST.KKOD='  -'
      || __CHK.err_fld(KST,'KKOD');
         return('KKOD')
      ?};
      {? +KST.KKOD<>6
      || FUN.emsg('Pole "Kod" (adres korespondencyjny) nie jest prawidłowo wypełnione.'@);
         return('KKOD')
      ?}
   ?}
?};

: weryfikacja zgodnosci z formatem
{? exec('nip_ok','#id',KST.NIP)<=0  || return('NIP')
|? exec('regon_ok','#id',KST.REG)<=0 || return('REG')
?};
''


\slo_pop_skid_rbk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akcja 'Popraw' z poziomu okienek wertowania tabeli SLO
::  OLD: \slo_pop/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? exec('test_slo','slo_slu')=0
|| FUN.info('Poprawienie pozycji słownika niemożliwe w bieżącej firmie.'@);
   return(0)
?};
SLO.SLU();
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ,) & RS.TAB='SKID_RBK'
|| _sp:={? var_pres('slo_pop')>0 || _slo_pop:=slo_pop; 1 || 0 ?};
   _rs:={? var_pres('ref_slo')>0 || _ref_slo:=ref_slo; 1 || 0 ?};
   slo_pop:=1; ref_slo:=null; exec('slu_okn','slo_slu');
   _i:=RS.ref();
   {? (_r_szuk:=exec('wz_szuk','slo_slu'))
   || SKID_RBK.index('TAB'); SKID_RBK.prefix(null,REF.INFO,REF.INFO);
      SKID_RBK.win_edit('RED_RACH');
      _g:=$(RS.TAB+'.put()');
      exec('cn_psh','slo_slu');
:      RACHBANK.KB_4R:=SKID_RBK.N;
      RACHBANK.KB_4R_BD:='RACHBANK.KB_4R:=RB.get_rbtx(2,SKID_RBK.N,SKID_RBK.KRAJ().KODISO)';
      RACHBANK.KB_4R_AE:='SKID_RBK.N:=RB.get_rbel(2,RACHBANK.KB_4R,SKID_RBK.KRAJ().KODISO,1); '+RACHBANK.KB_4R_BD
      +'; exec(\'ae_rb\',\'rachunki\')';
      RACHBANK.KB_5R_BD:='RACHBANK.KB_5R:=RB.get_rbtx(1,SKID_RBK.ref(),SKID_RBK.KRAJ().KODISO)';

      {? SKID_RBK.edit("RB.chk_rban(SKID_RBK.N,1,'KB_4R')")
      || exec('cn_pop','slo_slu');
         {? var_pres('ref_slo')>0 & ref_slo<>null || SLO.seek(ref_slo) ?}; _h:=1;
         do();
         _k:=SLO.KOD; _t:=SLO.TR; _msg:='';
         {? _g()
         || {? _r_szuk<>-1 & ~RS.seek(_i)
            || undo();
               _msg:='Nie znaleziono rekordu tabeli RS.'
            || _msg:=exec('akt_slo','slo_slu',_k,_t)
            ?}
         ?};
         end();
         {? _msg<>'' || FUN.info(_msg) ?}
      || exec('cn_pop','slo_slu'); {? ref_slo<>null || SLO.seek(ref_slo) ?}
      ?}
   ?};
   {? _sp || slo_pop:=_slo_pop || VAR_DEL.delete('slo_pop') ?};
   {? _rs || ref_slo:=_ref_slo || VAR_DEL.delete('ref_slo') ?}
|| FUN.info('Akcja dostępna wyłącznie dla słowników o wzorcu Rachunek bankowy.'@)
?};
exec('tab_f_rfresh','slo_filtr',SLO);
RS.cntx_pop();
1


\ae_podfiz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji dla pola KST.PODFIZ
::----------------------------------------------------------------------------------------------------------------------
exec('podfiz','!zws_par_alic');
{? ~KST.PODFIZ
|| KST.PODNAZ:=KST.PODIME:=''; KST.PODURDAT:=date(0,0,0)
?};
1


\bds_podfiz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Na wyswietl dla pola KST.PODFIZ
::----------------------------------------------------------------------------------------------------------------------
exec('podfiz','!zws_par_alic');
1


\podfiz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawienie widocznosci pól związanych z osoba fizyczną
::----------------------------------------------------------------------------------------------------------------------
{? KST.PODFIZ
|| KST.efld_opt('PROCES','enable=1,mark=1',KST,'PODNAZ');
   KST.efld_opt('PROCES','enable=1,mark=1',KST,'PODIME');
   KST.efld_opt('PROCES','enable=1,mark=1',KST,'PODURDAT')
|| KST.efld_opt('PROCES','enable=0,mark=0',KST,'PODNAZ');
   KST.efld_opt('PROCES','enable=0,mark=0',KST,'PODIME');
   KST.efld_opt('PROCES','enable=0,mark=0',KST,'PODURDAT')
?}


\ae_kst_woj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redakcji KST.WOJEWODZ
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
EDIT_VAR.SLO_TYP:='WOJEW';
_typ:=EDIT_VAR.SLO_TYP;

{? ((cur_tab(1,1)=OS_ADRES & -OS_ADRES.KRAJ().SYM='pl')
   | (cur_tab()=KST & -KST.KRAJ().SYM='pl')
   | var_pres('WOJEWODZ',cur_tab()) & var_pres('KRAJ',cur_tab()) & -KST.KRAJ().SYM='pl')
|| WOJEWODZ.cntx_psh();
   WOJEWODZ.index('WOJNAZWA'); WOJEWODZ.prefix();
   {? ~WOJEWODZ.find_key(fld(),)
   || FUN.info('Brak województwa \'%1\' w słowniku.'@[fld()])
   || fld(WOJEWODZ.NAZWA);
      XINFO.WOJ:=WOJEWODZ.ref()
   ?};
   WOJEWODZ.cntx_pop()
|| SLO_TYP.cntx_psh();
   {? _slo:=exec('slo_typ','ext_slo',_typ)
   || SLO_NAZ.index('NAZWA');
      SLO_NAZ.prefix(_slo);
      {? ~SLO_NAZ.find_key(fld)
      || FUN.info('Brak województwa \'%1\' w słowniku.'@[fld()])
      || fld(SLO_NAZ.NAZWA)
      ?}
   ?};
   SLO_TYP.cntx_pop();
:: Przywrócenie wartości zmienionej przez \slo_typ
   EDIT_VAR.SLO_TYP:=SLO_TYP.SYMBOL
?};
_ok


\f3_size
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Formuła na f3 w polu na wielkość przedsiębiorstwalicencjobiorcy
::   WY: XINFO.WIELKOSC
::----------------------------------------------------------------------------------------------------------------------
popup(0,'Wybierz wielkość przedsiębiorstwa',
       'mikro', '', "XINFO.WIELKOSC:='mikro'",
       'mały','',"XINFO.WIELKOSC:='mały'",
       'średni','',"XINFO.WIELKOSC:='średni'",
       'duży','',"XINFO.WIELKOSC:='duży'",
       'publiczny','',"XINFO.WIELKOSC:='publiczny'",
       'leczniczy','',"XINFO.WIELKOSC:='leczniczy'",
       'nieustalony','',"XINFO.WIELKOSC:='nieustalony'")

:Sign Version 2.0 jowisz:1045 2023/09/21 07:48:07 c2626e2f87e352f8a866cd275327a8a453751b9188431eedcc176872da5e7188a8ff33395a58a1a3312db85db08cab814e952eeb74ca0ee7cd5ca2547c22a5397348bfd0de87c00100bd6ae3ec5b04b5b3d73004bc3ed4a6f33729bca1b9dd812b9c1ee511963bf841bc19c91015b6ea3022c4cf29b828b6347168c390a5f470

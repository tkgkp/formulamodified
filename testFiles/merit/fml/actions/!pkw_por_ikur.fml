:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkw_por_ikur.fml
:: Utworzony: 08.06.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności webTerm-owej PKW_POR_IKUR - Przeglądanie kart urlopowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przeglądanie kart urlopowych (webTerm) - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pkw_por__kur','pkw_por')


\kart_url_web_wer_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Obsługa akcji "Rekord przed" w oknie WEB_WER tabeli KART_URL.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('kart_url_sumuj','kart_url');

0


\kart_url_web_wer_szczegoly_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.02]
:: OPIS: Obsługa akcji "Szczegóły" w oknie WEB_WER tabeli KART_URL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

exec('set_env','pkw_por');
KART_URL.P();

_dzis:=date();
_br:=_dzis~1;
EDIT_VAR.DATA:=
   {? KART_URL.ROK<_br
   || date(KART_URL.ROK,12,31)
   |? KART_URL.ROK=_br
   || _dzis
   || date(KART_URL.ROK,1,1)
   ?};
exec('_kart_url_na_dzien','!pkw_por_ikur');

KART_URL.web_edit('WEB_RED','#web_red',,,"
   {? _a='ANULUJ'
   || KART_URL.web_eclose()
   |? _a='OK'
   || _ret:=exec('_kart_url_na_dzien','!pkw_por_ikur');
      {? _ret='close'
      || KART_URL.web_eclose();
         KART_URL.web_refresh(web_top_win(1))
      || KART_URL.web_efld_opt('WEB_RED',,'enable=%1' [$(_ret<>'DATA')],EDIT_VAR);
         KART_URL.web_efld_opt('WEB_RED',,'enable=1',EDIT_VAR,'DATA');
         KART_URL.web_update(,,EDIT_VAR,'DATA')
      ?}
   ?}
")


\_kart_url_na_dzien
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła przygotowuje dane dotyczące limitów urlopowych na podany dzień.
::   WE:
::   WY: 'DATA' - Data spoza roku karty urlopowej.
::           0  - Rekord został usunięty.
::           1  - Obliczenia wykonane.
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.NAB_URL:=EDIT_VAR.DATA_DOD:=EDIT_VAR.DATA_NSP:=date(0,0,0);

EDIT_VAR.LIM_ZAL:=EDIT_VAR.LIM_ZALG:=0;
EDIT_VAR.LIM_AKT:=EDIT_VAR.LIM_AKTG:=0;
EDIT_VAR.URL_DOD:=EDIT_VAR.URL_DODG:=0;
EDIT_VAR.NSP_ZAL:=EDIT_VAR.NSP_ZALG:=0;
EDIT_VAR.URL_NSP:=EDIT_VAR.URL_NSPG:=0;

EDIT_VAR.RAZEM:=EDIT_VAR.RAZEMG:=0;
EDIT_VAR.RAZ_NSP:=EDIT_VAR.RAZ_NSPG:=0;

EDIT_VAR.UWYPD:=EDIT_VAR.UWYPG:=0;
EDIT_VAR.UNZD:=0;
EDIT_VAR.UNSPD:=EDIT_VAR.UNSPG:=0;

_ret:='';

{? ~exec('exist','#record')
|| _ret:='close'

|? _rok:=EDIT_VAR.DATA~1;
   _rok<>KART_URL.ROK
|| FUN.emsg('Data musi dotyczyć roku %1.'@ [$KART_URL.ROK]);
   _ret:='DATA'

|| exec('init','pkw');

   P.prefix();
   KART_URL.P();

:: Prawo do urlopu.  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
   EDIT_VAR.NAB_URL:=P.NAB_URL;
   EDIT_VAR.DATA_DOD:=KART_URL.DATA_DOD;
   EDIT_VAR.DATA_NSP:=KART_URL.DATA_NSP;

:: Limity nominalne. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
   EDIT_VAR.LIM_ZAL:=KART_URL.LIM_ZAL;
   EDIT_VAR.LIM_ZALG:=KART_URL.LIM_ZALG;
   EDIT_VAR.NSP_ZAL:=KART_URL.NSP_ZAL;
   EDIT_VAR.NSP_ZALG:=KART_URL.NSP_ZALG;

   {? KART_URL.PIERWSZY='T'
   || _TAB:=exec('pierwszy','kart_url',1);
      {? _TAB.first()
      || _dzien:=EDIT_VAR.NAB_URL~3;
         {? _dzien=date(_rok,EDIT_VAR.NAB_URL~2,0)~3
         || _dzien:=0
         ?};
         {!
         |? _ostatni:=date(_rok,_TAB.N,0);
            {? {? _dzien<=_ostatni~3 || date(_rok,_TAB.N,_dzien) || _ostatni ?}<EDIT_VAR.DATA
            || EDIT_VAR.LIM_AKT+=_TAB.W;
               _TAB.next()
            ?}
         !};
         EDIT_VAR.LIM_AKTG:=EDIT_VAR.LIM_AKT*8$2
      ?}

   || EDIT_VAR.LIM_AKT:=KART_URL.LIM_AKT;
      EDIT_VAR.LIM_AKTG:=KART_URL.LIM_AKTG
   ?};

   {? EDIT_VAR.DATA>=KART_URL.DATA_DOD
   || EDIT_VAR.URL_DOD:=KART_URL.URL_DOD;
      EDIT_VAR.URL_DODG:=KART_URL.URL_DODG
   ?};

   {? EDIT_VAR.DATA>=KART_URL.DATA_NSP
   || EDIT_VAR.URL_NSP:=KART_URL.URL_NSP;
      EDIT_VAR.URL_NSPG:=KART_URL.URL_NSPG
   ?};

   EDIT_VAR.RAZEM:=EDIT_VAR.LIM_ZAL+EDIT_VAR.LIM_AKT+EDIT_VAR.URL_DOD;
   EDIT_VAR.RAZEMG:=EDIT_VAR.LIM_ZALG+EDIT_VAR.LIM_AKTG+EDIT_VAR.URL_DODG;

   EDIT_VAR.RAZ_NSP:=EDIT_VAR.NSP_ZAL+EDIT_VAR.URL_NSP;
   EDIT_VAR.RAZ_NSPG:=EDIT_VAR.NSP_ZALG+EDIT_VAR.URL_NSPG;

:: Limity dostępne.  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
   _fogr:="{? _a<0 || 0 || _a ?}";

   EDIT_VAR.UWYPD:=_fogr(EDIT_VAR.RAZEM-KART_URL.URL_WYK);
   EDIT_VAR.UWYPG:=_fogr(EDIT_VAR.RAZEMG-KART_URL.URL_WYKG);
   EDIT_VAR.UNSPD:=_fogr(EDIT_VAR.RAZ_NSP-KART_URL.NSP_WYK);
   EDIT_VAR.UNSPG:=_fogr(EDIT_VAR.RAZ_NSPG-KART_URL.NSP_WYKG);
:: Sprawdzenie czy dyscyplina pracy urlopu rehabilitacyjnego zmniejsza dostępny urlop dodatkowy
   _fnorma:=exec('norma_fml','godziny');
   _rn:=__RUB.sys_kod(1171,,1);
   {? _rn<>0
   || _ref:=__RUB.ref(_rn);
      DS.cntx_psh();
      DS.index('DYSCYPLI');
      DS.prefix(P.ref(),_rok,_ref);
      {? DS.first()
      || _poz_rg:=0;
         _poz_rd:=0;
         _z_dsg:=0;
         _z_dsd:=0;
         {? DS.NR='G'
         || _poz_rg:=DS.NN-DS.NW;
            _poz_rd:=_poz_rg/_fnorma(date(DS.R,1,1))$2;
            _z_dsg:=1
         |? DS.NR='R' | DS.NR='K'
         || _poz_rd:=DS.NN-DS.NW;
            _poz_rg:=_poz_rd*_fnorma(date(DS.R,1,1))$2;
            _z_dsd:=1
         ?};
         NWU.cntx_psh();
         NWU.index('ROD');
         NWU.prefix(P.ref(),_ref);
         {? NWU.last()
         || {!
            |? {? NWU.OD~1=_rok
               || {? (NWU.AZ='?' | NWU.AZ='T') & NWU.N=null() & NWU.OS_N=null()
                  || {? DS.NR='G'
                     || _poz_rg-=NWU.NG
                     |? DS.NR='R'
                     || _poz_rd-=NWU.NR
                     |? DS.NR='K'
                     || _poz_rd-=1
                     ?}
                  ?};
                  NWU.prev()
               ?}
            !}
         ?};
         NWU.cntx_pop();
         {? _z_dsd & _poz_rd<EDIT_VAR.UNSPD
         || EDIT_VAR.UNSPD:=_poz_rd;
            EDIT_VAR.UNSPG:=_poz_rd*_fnorma(date(DS.R,1,1))
         |? _z_dsg & _poz_rg<EDIT_VAR.UNSPG
         || EDIT_VAR.UNSPG:=_poz_rg;
            EDIT_VAR.UNSPD:=_poz_rg/_fnorma(date(DS.R,1,1))$2
         ?}
      ?};
      DS.cntx_pop()
   ?};

   _urlop:=obj_new('wyp','nz','nsp');
   KST_PAR.cntx_psh();
   exec('czytaj','#stalesys',EDIT_VAR.DATA,KST_PAR,'URLOP','URL_CHOR','URL_LIM');
   _urlop.wyp:=KST_PAR.URLOP;
   _urlop.nz:=KST_PAR.URL_CHOR;
   _urlop.nsp:=0;
   EDIT_VAR.UNZD:=KST_PAR.URL_LIM;
   KST_PAR.cntx_pop();
   _rn:=__RUB.sys_kod(1113,,1);
   {? _rn<>0
   || _urlop.nsp:=__RUB.ref(_rn)
   ?};

:: Odejmujemy urlopy na żądanie.
   N.cntx_psh();
   N.index('NIEORM');
   N.prefix('N',P.ref(),EDIT_VAR.DATA~1);
   {? N.first()
   || {!
      |? {? N.NB=_urlop.nz
         || EDIT_VAR.UNZD-=N.NR
         ?};
         N.next()
      !}
   ?};
   N.cntx_pop();

:: Odejmujemy złożone wnioski: wprowadzone, zaakceptowane, z których nie powstały nieobecności.
   NWU.cntx_psh();
   NWU.index('ROD');
   {! _lp:=1 .. 3
   |! NWU.prefix(P.ref(),_urlop[_lp]);
      {? NWU.last()
      || {!
         |? {? NWU.OD~1=_rok
            || {? (NWU.AZ='?' | NWU.AZ='T') & NWU.N=null() & NWU.OS_N=null()
               || {? _lp=1 | _lp=2
                  || EDIT_VAR.UWYPD-=NWU.NR;
                     EDIT_VAR.UWYPG-=NWU.NG;
                     {? _lp=2
::                      Na żądanie
                     || EDIT_VAR.UNZD-=NWU.NR
                     ?}
                  || EDIT_VAR.UNSPD-=NWU.NR;
                     EDIT_VAR.UNSPG-=NWU.NG
                  ?}
               ?};
               NWU.prev()
            ?}
         !}
      ?}
   !};
   NWU.cntx_pop();

   EDIT_VAR.UWYPD:=_fogr(EDIT_VAR.UWYPD);
   EDIT_VAR.UWYPG:=_fogr(EDIT_VAR.UWYPG);
   EDIT_VAR.UNZD:=_fogr(EDIT_VAR.UNZD);
   EDIT_VAR.UNSPD:=_fogr(EDIT_VAR.UNSPD);
   EDIT_VAR.UNSPG:=_fogr(EDIT_VAR.UNSPG);

   {? EDIT_VAR.UNZD>EDIT_VAR.UWYPD
   || EDIT_VAR.UNZD:=EDIT_VAR.UWYPD
   ?}

?};

_ret

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:59 77caeb98d69d9db4c267bea5dd72f7b7ca307dbb3d6a69ae5cf68ea7a92a0961dff5844ed395eb750b4311c7343d0de6ca10b75bc2847d4713c3614f76d94cb9e37df0d028e71e8feeed82d2054518463d2bb9ce9458b054e84fd38170dafc65aa8b90b7425eb19ec4485cac17a76945179a5904908d523bcba47a1f6dafd60c

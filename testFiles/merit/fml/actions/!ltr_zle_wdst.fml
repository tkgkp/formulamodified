:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ltr_zle_wdst.fml
:: Utworzony: 09.03.2020
:: Autor: DG
::======================================================================================================================
:: Zawartość: Utworzenie dyspozycji transportu w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.14]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,   symbol=TR_NAG,  type=_TR_NAG,  name=Transport,             required=N,    keyref=T
::# kind=WE,   symbol=CZY_PDF, type=STRING,   name=Utwórz PDF,            required=N,    keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE,   symbol=PRN_MSK, type=STRING,   name=Maska wydruku,         required=N,    keyref=N, fml_val="exec('rep_exec','#b_report','LTR_ZLE_WDST','ltr_zle_dys*','Wydruki dyspozycji transportu',-1)"
::# kind=WE,   symbol=TR_NZL,  type=_TR_NZL,  name=Dyspozycja transportu, required=T,    keyref=T
::# kind=WY,   symbol=DOKUM,   type=_DOKUM,   name=Załącznik,             required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_akcja:=_mp.akcja();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' ?};
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

:_msk:={? var_pres('PRN_MSK',_in)=type_of('') || _in.PRN_MSK || '' ?};
:_msk:={? menu_txt='Drukuj' | menu_txt='Utwórz PDF' || 'ltr_zle_dysp_*' || 'ltr_zle_dysw_*' ?};
_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || {? exec('ctrlMask','#b_report',_in.PRN_MSK,'ltr_zle_dys') || _in.PRN_MSK || '' ?}
      || ''
      ?};
_trnzl:={? var_pres('TR_NZL',_in)=type_of(null()) & _in.TR_NZL
        || _in.TR_NZL
        |? ~(_msk*'ltr_zle_dysw')
        || TR_NZL.ref()
        || null()
        ?};
_trnag:={? var_pres('TR_NAG',_in)=type_of(null()) & _in.TR_NAG
        || _in.TR_NAG
        |? _trnzl
        || exec('FindAndGet','#table',TR_NZL,_trnzl,,"TR_NAG",null())
        || null()
        ?};
_wgdysp:=_trnzl<>null();

{? (_msk*'dysp_cmr') & _trnag=null()
||
   msg('Dla pozycji nie można dokonać wydruku dokumentu CMR, ponieważ nie została skierowana do transportu.');
   return(0)
?};

{? _trnzl<>null() | _trnag<>null()
||
:: Ustawienie kontekstu wg instancji elementu w procesie
   TR_NZL.cntx_psh();
   TR_NAG.cntx_psh();

   _mtr:='';
   _use:={? _wgdysp
         || _mtr:=ref_name(_trnzl);
            _mtr<>TR_NZL.name()
         || _mtr:=ref_name(_in.TR_NAG);
            _mtr<>TR_NAG.name()
         ?};
   {? _use || exec('opentr','open_tab',_mtr+4) ?};

   {? {? _wgdysp || TR_NZL.prefix(); TR_NZL.seek(_trnzl) || TR_NAG.prefix(); TR_NAG.seek(_trnag) ?}
   || _dokum:=exec('trnag_druk','!ltr_zle_wdst',_czy_pdf,_msk,_trnzl);
      {? _czy_pdf & _dokum<>null()
      ||
         _out.DOKUM:=_dokum;
         _mp.save(,_out);
         {? ~_keep || _mp.done() ?}

      |? ~_czy_pdf & _dokum
      ||
         {? ~_keep || _mp.done() ?}
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};

   TR_NAG.cntx_pop();
   TR_NZL.cntx_pop()

|? _trnzl=null()
|| _mp.error('Brak parametru wejściowego TR_NZL.')
|| _mp.error('Brak parametru wejściowego TR_NAG.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.14]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('TR_NZL',_in)<>type_of(~~) & _in.TR_NZL
|| 'Utwórz PDF dyspozycji transportu: %1'@@[exec('record','#to_string',_in.TR_NZL)]
|? var_pres('TR_NAG',_in)<>type_of(~~) & _in.TR_NAG
|| 'Utwórz PDF dla transportu: %1'@@[exec('record','#to_string',_in.TR_NAG)]
|| ''
?}


\trnag_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.14]
:: OPIS: wydruki dla dyspozycji transportu
::   WE: [_a] - 1-PDF 0-normalny wydruk
::       [_b] - maska wydruku
::       [_c] - TR_NZL.ref()
::   WY: dla _a=1: ref/null _a=0: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
_msk:={? var_pres('_b')=type_of('') || _b || '' ?};
_trnzl:={? var_pres('_c')=type_of(null()) || _c || null() ?};
_wyn:={? _a || null || 0 ?};

{? _msk<>''
|| _wydr:=_msk
|| _wydr:='ltr_zle_dysp*'
?};

:POLA.NAZ_WYDR:='';
_sym:={? _trnzl=null() || TR_NAG.SYM || exec('FindAndGet','#table',TR_NZL,_trnzl,,"SYM",TR_NAG.SYM) ?};
_print:=1;
_grp_dr:=var_pres('__GRP_DR')>100 & __GRP_DR.GR & __GRP_DR.FIRST=0 & __GRP_DR.WZ<>'';
_grsave:={? var_pres('__GRP_DR')>100 || __GRP_DR.NO_SAVE || -2 ?};
{? _a & _trnzl<>null() & (~_grsave | exec('czy_autoadd','dokum',_trnzl))
|| _print:=0;
   {? _grsave=-1
   || _print:=-1
   |? ~_grsave
   || {? FUN.ask('Czy dla tworzonych PDF zaznaczonych dyspozycji\n'
              'tworzyć kolejne (o ile są już utworzone)?'@)
      || __GRP_DR.NO_SAVE:=-1;
         _print:=-1
      || __GRP_DR.NO_SAVE:=1
      ?}
   |? _grp_dr
   ||
      exec('add_kom','#message','Utworzono już PDF dla dokumentu.'@,7,exec('record','#to_string',_trnzl))
   |? FUN.ask('Utworzono już PDF dla dokumentu %1.\nCzy utworzyć kolejny?'@[exec('record','#to_string',_trnzl)])
   ||
      _print:=-1
   ?}
?};
{? _a=1 & _print<>0
||
   {? _grp_dr
   ||
      _symb:=exec('str_to_pth','#string',_sym);
      _file:='Dyspozycja transportu '+_symb+'.pdf';
      _wydr:=rep_exec(__GRP_DR.WZ,,,_file,1)
   ||
      _symb:=exec('str_to_pth','#string',_sym);
      _file:='Dyspozycja transportu '+_symb+'.pdf';
      _wydr:=rep_exec(_wydr,,,_file,1)
   ?}
|? ~_a
||
   _wydr:=exec('rep_exec','#b_report','LTR_ZLE_WDST',_wydr,'Dyspozycja transportu',2,,,,,,'W')
|| _wydr:=0
?};

{? var_pres('__GRP_DR')>100 & _wydr=0 & __GRP_DR.NO_SAVE<>1
||
   __GRP_DR.ESC:=1
?};

{? _a=1 & _wydr=1
|| _wyn:=exec('save_dok','dokum',{? _trnzl=null() || 'TR_NAG' || 'TR_NZL' ?},_file,POLA.NAZ_WYDR,_sym
      ,{? _trnzl=null()
       || ''
       || exec('FindAndGet','#table',TR_NZL,_trnzl,
           ,"{? KH_ODB<>null() & KH_ODB().KH_LNK<>null()
             || {? KH_ODB().EM<>'' || KH_ODB().EM || '' ?}
             || KH().EM
             ?}",'')
       ?}
      ,'Dyspozycja transportu: '+_sym,PARAM_W.JEZYK,'N')
|? _a=0
|| _wyn:=_wydr
?};

_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 a63581aea93bf96378520a1594e6bc9898aa9caa4448d1a7ea7c68ad411433727ec0c8dff609470defa1379ba4bded611a13e23a62bffabf1a56bbd5b92e0fb8ca8cf6ff4024cb296f3e1df173246812874c551e7f0014e42617ef9d90e11d800c5078f9a6d0a543ad375378c74c15a1a09755b913dd635afd870bab5d26afe0

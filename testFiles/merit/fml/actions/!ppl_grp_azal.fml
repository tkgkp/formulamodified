:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_grp_azal.fml
:: Utworzony: 12.12.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PPL_GRP_AZAL - Potr. na podst. zaliczek.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Potr. na podst. zaliczek - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# permissions=F_ZATR,UD_SKL
::
:: Wynik działania czynności. Parametr szczególnie istotny w przypadku uruchomienia czynności usługowej.
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,BŁĄD)", required=N
::
_par:=params_get();
_mp:=_par.mp;
_out:=_par.out;

{? ~exec('lic','#b_domain','OBE')
|| _out.RESULT:='BŁĄD'
|| exec('run','!ppl_grp_azal',~_mp.isService());
   _out.RESULT:='OK'
?};
_mp.save(,_out);
_mp.done();
~~


\run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiedzialna za przetwarzanie danych.
::   WE: [_a] [INTEGER] - Tryb użytkownika (z komunikatami).
::                         0 - Nie [domyślnie].
::                         1 - Tak.
::   WY:
::  OLD: \zalicz_auto/komornik.fml
::----------------------------------------------------------------------------------------------------------------------
_msg:=var_pres('_a')=type_of(0) & _a;

_count:=0;

_firma:=exec('ref_firma','ustawienia');
RAPLS.ustaw(1);

EZAL.cntx_psh();
EZAL.index('INDEX5TZ');
EZAL.prefix(_firma,'T','T','N',);
{? EZAL.first()
|| _par230:=PAR_SKID.get(230)='T';

   EZALPOZ.cntx_psh();
   EZALPOZ.index('ZALICZKA');
   OSOBA.cntx_psh();
   OSOBA.prefix();
   P.cntx_psh();
   P.prefix();
   _typ:='PODZORG';
   _ud_def:=exec('ud_def_firma','schemat',_typ);
   P.f_set('OSOBA',,exec('filtr_p','schemat',__PARSES.getVal('F_ZATR').KOD,,'W'),
      'Parametr nie jest używany',
      exec('szukaj_udb_sys','schemat','PPL',_typ),
      exec('operatorUser','#users'),
      _ud_def.UD_SCH,
      '\''+_ud_def.SCIEZKA+'%\''
   );
   KOM_OS.cntx_psh();
   KOM_OS.index('KOM_OZ');
   KOM_OS.prefix(_firma);
   ZALICZ.cntx_psh();
   ZALICZ.index('ZALICZKA');
   ZALICZ.prefix(_firma);
   {!
   |? _ezalref:=EZAL.ref();
      _next:=0;
      {? P.f_find(EZAL.ZAL_DLA) & ~KOM_OS.find_key('T',_ezalref) & ~ZALICZ.find_key('T',_ezalref)
      || do();
         _bilans:=0;
         EZALPOZ.prefix(_ezalref);
         {? EZALPOZ.first()
         || {!
            |? {? 3+EZALPOZ.POCH<>'PPL'
               || _bilans+=EZALPOZ.ROZCHOD-EZALPOZ.PRZYCHOD
               ?};
               EZALPOZ.next()
            !}
         ?};
         {? _bilans>0 & _par230
         || {? exec('zalicz_potr','komornik',_firma,EZAL.ZAL_DLA,EZAL.POTRODDN,_bilans,_ezalref)
            || _count+=1;
               _next:=EZAL.next();
               EZAL.cntx_psh();
               {? EZAL.seek(_ezalref)
               || EZAL.prefix();
                  EZAL.SPLATA:=_bilans;
                  EZAL.PRZ_P:='T';
                  EZAL.put()
               ?};
               EZAL.cntx_pop()
            ?}
         |? _bilans<0
         || _next:=EZAL.next();
            EZAL.cntx_psh();
            {? EZAL.seek(_ezalref)
            || _count+=1;
               EZAL.prefix();
               EZAL.WPLATA:=-_bilans;
               EZAL.PRZ_P:='T';
               EZAL.put();

               ZALICZ.blank(1);
               ZALICZ.FIRMA:=_firma;
               ZALICZ.OSOBA:=EZAL.ZAL_DLA;
               ZALICZ.R:=EZAL.POTRODDN~1;
               ZALICZ.M:=EZAL.POTRODDN~2;
               ZALICZ.A:='T';
               ZALICZ.EZAL:=_ezalref;
               ZALICZ.KW:=EZAL.WPLATA;
               ZALICZ.DWP:=EZAL.POTRODDN;
               ZALICZ.AUTO:='T';
               {? ZALICZ.add(1)
               || RAPLS.msg('',ZALICZ.OSOBA().NAZWISKO,OSOBA.PIERWSZE,
                     'W kartotece zaliczek został wygenerowany automatyczny zapis'@,
                     'na podstawie danych z obszaru "Zaliczki pracownicze".'@)
               ?}
            ?};
            EZAL.cntx_pop()
         ?};
         end()
      ?};
      _next | EZAL.next()
   !};
   ZALICZ.cntx_pop();
   KOM_OS.cntx_pop();
   P.cntx_pop();
   OSOBA.cntx_pop();
   EZALPOZ.cntx_pop()
?};
EZAL.cntx_pop();

{? _msg
|| {? _count=0
   || RAPLS.msg('','','','Brak danych do przeniesienia.'@)
   ?};
   RAPLS.raport(2)
?};
RAPLS.ustaw(0);
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:55 44acbd7b9f31b740d8622575dcc3484321749bdf6a13b4126088b05da4e3beed391496d6ce132cf115eb7f1a897089c2bc2bf826cbb9fb5c5a5f079005e1c16cec3ad9829c08b7985b605e385b1ab5dcc841db2daf9b17dc4ab5b0c6f6823ae1fb0108712573debb568dc0ce5886c5385197d2e050a662c3ccc6c1c5afa2cd8c

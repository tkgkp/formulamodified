:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_pruz.fml
:: Utworzony: 04.09.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_DOS_PRUZ - Rejestracja uprawnień zaw.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja uprawnień zaw. - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE,   symbol=OSOBA,     type=_OSOBA,   name=Wskazanie osoby,   required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.OSOBA);
{? _uidref=''
|| _result:=exec('error','!pkd_dos_pruz')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_pruz',_in.OSOBA);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja uprawnień zaw. - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj uprawnienia zawodowe: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj uprawnienia zawodowe: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj uprawnienia zawodowe: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj uprawnienia zawodowe'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie uprawnień zawodowych niemożliwe.\nNie znaleziono osoby.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie osoby.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & OSOBA.seek(_a)
|| UPR_ZAWK.cntx_psh();
   UPR_ZAWK.win_dict('WER');
   UPR_ZAWK.clear();
   UPR_ZAWO.cntx_psh();
   UPR_ZAWO.index('OUN');
   UPR_ZAWO.prefix(OSOBA.ref());
   UPR_ZAWO.index('OUS');
   UPR_ZAWO.prefix(OSOBA.ref());
   UPR_ZAWO.win_sel(exec('wnd','!pkd_dos_pruz'));
   UPR_ZAWO.win_edit('RED');
   _ret:=UPR_ZAWO.select();
   UPR_ZAWO.cntx_pop();
   UPR_ZAWK.cntx_pop()
|| _ret:=exec('error','!pkd_dos_pruz')
?};
OSOBA.cntx_pop();
_ret


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przygotowanie okna wertowania uprawnień zawodowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized';

_wnd:=UPR_ZAWO.grp_make('Uprawnienia zawodowe',"mb_exec('GRP_EDISP_OSOBA_INFO')",'upr_zaw_weri',,,,,_mode);
UPR_ZAWO.grp_edit(_wnd,OSOBA,'INFO',,,,,,_mode);
UPR_ZAWO.grp_splt(_wnd,,'horizontal','upr_zaw');
UPR_ZAWO.grp_sel(_wnd,,'WER',,,,,,,,,,_mode,,1);

UPR_ZAWO.win_fml('WER',,'UPR_ZAWK','SYMBOL','ICON_BEFORE',"exec('x_zalacz_icon','zalacz')");

_wnd


\cechy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła zwraca tablicę z akronimami pól zawierających cechy uprawnienia zawodowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
spli_str('NR,OD,DO,UTRATA,DOK_NR,DOK_DT,DOK_WYD',',')


\upr_zawo_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed modyfikacją rekordu tabeli UPR_ZAWO.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? UPR_ZAWO.OSOBA=null() | UPR_ZAWO.UPR_ZAWK=null()
|| return(0)
?};

: Porządki - zerowanie pól, które są "niedostępne".
UPR_ZAWK.cntx_psh();
_cechy:=exec('cechy','!pkd_dos_pruz');
{! _lp:=1 .. obj_len(_cechy)
|! _flaga:=($('UPR_ZAWO.UPR_ZAWK().'+_cechy[_lp]))();
   {? _flaga='N'
   || exec('blank','#field',UPR_ZAWO,,_cechy[_lp])
   ?}
!};

UPR_ZAWK.cntx_pop();

1


\upr_zawo_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawo_modb','!pkd_dos_pruz')


\upr_zawo_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawo_modb','!pkd_dos_pruz')


\upr_zawo_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',UPR_ZAWZ,'OD',UPR_ZAWO.ref()) &
exec('del_ndx','#table',ZALACZ,'NAG',UPR_ZAWO.uidref())


\upr_zawo_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli UPR_ZAWO.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE: [_a] [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(UPR_ZAWK)
|| _tab:=_b
|? _tryb='1'
|| _tab:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? _fld='' | _fld='UPR_ZAWK'
|| _empty:=UPR_ZAWO.UPR_ZAWK=null();
   _cechy:=exec('cechy','!pkd_dos_pruz');
   {! _lp:=1 .. obj_len(_cechy)
   |! {? _empty
      || _tab.efld_opt(_we,'mark=0',UPR_ZAWO,_cechy[_lp]);
         _tab.efld_opt(_we,'enable=0',UPR_ZAWO,_cechy[_lp])
      || _flaga:=($('UPR_ZAWO.UPR_ZAWK().'+_cechy[_lp]))();
         _tab.efld_opt(_we,'mark='+$(_flaga='W'),UPR_ZAWO,_cechy[_lp]);
         _tab.efld_opt(_we,'enable='+$(_flaga='W' | _flaga='O'),UPR_ZAWO,_cechy[_lp])
      ?}
   !}
?};

1


\upr_zawo_dok_wyd_pt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wzorzec dla pola UPR_ZAWO.DOK_WYD - ustawia typ słownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.SLO_TYP:=UPR_ZAWO.UPR_ZAWK().SLO_TYP().SYMBOL;
''


\upr_zawo_upr_zawk_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed redagowaniem pola UPR_ZAWO.UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
-menu_txt()<>'popraw'


\upr_zawo_upr_zawk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola UPR_ZAWO.UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawo_efld_opt','!pkd_dos_pruz')


\upr_zawo_x_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pól zawierających cechy uprawnienia zawodowego.
::       Zakłada się, że akronimy tych pól są zgodne z akronimami pól-flag w tabeli konfiguracyjnej UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? UPR_ZAWO.UPR_ZAWK=null()
|| return(0)
?};

_flaga:=($('UPR_ZAWO.UPR_ZAWK().'+cur_afld()))();
_flaga='W' | _flaga='O'


\upr_zawo_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UPR_ZAWO.blank();
exec('upr_zawo_efld_opt','!pkd_dos_pruz','*',UPR_ZAWO,'RED')


\upr_zawo_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawo_efld_opt','!pkd_dos_pruz','*',UPR_ZAWO,'RED')


\upr_zawo_ab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zawieszenia" w oknie WER tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UPR_ZAWZ.cntx_psh();
UPR_ZAWZ.index('OD');
UPR_ZAWZ.prefix(UPR_ZAWO.ref());
UPR_ZAWZ.win_sel('WER');
UPR_ZAWZ.win_edit('RED');
UPR_ZAWZ.win_patt('RED');

_d0:=date(0,0,0);
UPR_ZAWZ.efld_opt('RED','mark='+$(UPR_ZAWO.DO<>_d0 | UPR_ZAWO.UTRATA<>_d0),,'DO');
UPR_ZAWZ.win_fml('WER',,'OPIS',,'ICON_BEFORE',"exec('x_zalacz_icon','zalacz')");
_ha:={? UPR_ZAWO.UPR_ZAWK().ZAWIES='R' || '' || 'dpu:d' ?};

UPR_ZAWZ.select(,,,_ha);
UPR_ZAWZ.cntx_pop()


\upr_zawo_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli UPR_ZAWO z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli UPR_ZAWO jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=UPR_ZAWO
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\upr_zawo_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie WER tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawo_efld_opt','!pkd_dos_pruz','*',UPR_ZAWO,'RED');
UPR_ZAWO.display()


\upr_zawo_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
{? var_pres('_par')>100 &
   var_pres('cfg',_par)>100 &
   var_pres('done',_par.cfg)>100 &
   var_pres('noempty',_par.cfg.done)=type_of('') & var_pres('empty',_par.cfg.done)=type_of('')
|| _hid:=_par.cfg.done.noempty+':'+_par.cfg.done.empty
|| _hid:=''
?};
_hid:={? UPR_ZAWO.UPR_ZAWK().ZAWIES='N' || 'A' || '' ?}+_hid;
UPR_ZAWO.actions_grayed('WER',_hid);
0


\upr_zawo_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=__CHK.record(UPR_ZAWO,,'OSOBA','UPR_ZAWK'))<>''
|| return(_chk)
?};

_cechy:=exec('cechy','!pkd_dos_pruz');
{! _lp:=1 .. obj_len(_cechy)
|! _flaga:=($('UPR_ZAWO.UPR_ZAWK().'+_cechy[_lp]))();
   {? _flaga='W' & (_chk:=__CHK.record(UPR_ZAWO,,_cechy[_lp]))<>''
   || return(_chk)
   ?}
!};

_d0:=date(0,0,0);
_od:=UPR_ZAWO.UPR_ZAWK().OD<>'N' & UPR_ZAWO.OD<>_d0;
_do:=UPR_ZAWO.UPR_ZAWK().DO<>'N' & UPR_ZAWO.DO<>_d0;
_utrata:=UPR_ZAWO.UPR_ZAWK().UTRATA<>'N' & UPR_ZAWO.UTRATA<>_d0;

{? _od & _do & UPR_ZAWO.DO<UPR_ZAWO.OD
|| FUN.info('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(UPR_ZAWO,'DO'),MS.name(UPR_ZAWO,'OD')]);
   return('DO')
?};

{? _utrata & _od & UPR_ZAWO.UTRATA<UPR_ZAWO.OD
|| FUN.info('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(UPR_ZAWO,'UTRATA'),MS.name(UPR_ZAWO,'OD')]);
   return('UTRATA')
?};

{? _do & _utrata & UPR_ZAWO.DO<UPR_ZAWO.UTRATA
|| FUN.info('"%1" nie może być późniejsza niż "%2".'@ [MS.name(UPR_ZAWO,'UTRATA'),MS.name(UPR_ZAWO,'DO')]);
   return('UTRATA')
?};

{? -menu_txt()='popraw'
|| UPR_ZAWZ.cntx_psh();
   UPR_ZAWZ.index('OD');
   UPR_ZAWZ.prefix(UPR_ZAWO.ref());
   _size:=UPR_ZAWZ.size();
   {? UPR_ZAWZ.first()
   || _zod:=UPR_ZAWZ.OD
   ?};
   {? UPR_ZAWZ.last()
   || _zdo:=UPR_ZAWZ.DO
   ?};
   UPR_ZAWZ.cntx_pop();
   {? _size
   || {? _od & _zod<UPR_ZAWO.OD
      || FUN.info('"%1" nie może być późniejsza niż "%2" pierwszego okresu zawieszenia (%3).'@
            [MS.name(UPR_ZAWO,'OD'),MS.name(UPR_ZAWZ,'OD'),$_zod]
         );
         return('OD')
      ?};
      {? _do & _zdo<>_d0 & UPR_ZAWO.DO<_zdo
      || FUN.info('"%1" nie może być wcześniejsza niż "%2" ostatniego okresu zawieszenia (%3).'@
            [MS.name(UPR_ZAWO,'DO'),MS.name(UPR_ZAWZ,'DO'),$_zdo]
         );
         return('DO')
      ?};
      {? _utrata & _zdo<>_d0 & UPR_ZAWO.UTRATA<_zdo
      || FUN.info('"%1" nie może być wcześniejsza niż "%2" ostatniego okresu zawieszenia (%3).'@
            [MS.name(UPR_ZAWO,'UTRATA'),MS.name(UPR_ZAWZ,'DO'),$_zdo]
         );
         return('UTRATA')
      ?}
   ?}
?};

''


\upr_zawz_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli UPR_ZAWZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',UPR_ZAWZ.uidref())


\upr_zawz_upr_zawo_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wartość początkowa pola UPR_ZAWZ.UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UPR_ZAWO.ref()


\upr_zawz_ab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Załączniki" w oknie WER tabeli UPR_ZAWZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','UPR_ZAWZ',exec('chk_role','#b__box',OPERATOR.USER,'PKD_DOS_PRUZ'))


\upr_zawz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli UPR_ZAWZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_popraw:=-menu_txt()='popraw';

_chk:=__CHK.table(UPR_ZAWZ,_popraw,,'OD');
{? (type_of(_chk)=type_of('') & _chk<>'') | (type_of(_chk)=type_of(0) & ~_chk)
|| return(_chk)
?};

_d0:=date(0,0,0);

{? UPR_ZAWZ.DO<>_d0 & UPR_ZAWZ.DO<UPR_ZAWZ.OD
|| FUN.info('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(UPR_ZAWZ,'DO'),MS.name(UPR_ZAWZ,'OD')]);
   return('DO')
?};

{? UPR_ZAWZ.OD<UPR_ZAWO.OD
|| FUN.info('"%1" okresu zawieszenia nie może być wcześniejsza niż "%2" uprawnienia.'@
      [MS.name(UPR_ZAWZ,'OD'),MS.name(UPR_ZAWO,'OD')]
   );
   return('OD')
?};
{? UPR_ZAWO.UTRATA<>_d0 & UPR_ZAWO.UTRATA<UPR_ZAWZ.OD
|| FUN.info('"%1" okresu zawieszenia nie może być późniejsz niż "%2" uprawnienia.'@
      [MS.name(UPR_ZAWZ,'OD'),MS.name(UPR_ZAWO,'UTRATA')]
   );
   return('OD')
?};
{? UPR_ZAWO.DO<>_d0 & UPR_ZAWO.DO<UPR_ZAWZ.OD
|| FUN.info('"%1" okresu zawieszenia nie może być późniejsza niż "%2" uprawnienia.'@
      [MS.name(UPR_ZAWZ,'OD'),MS.name(UPR_ZAWO,'DO')]
   );
   return('OD')
?};

{? (UPR_ZAWO.UTRATA<>_d0 | UPR_ZAWO.DO<>_d0) & (_chk:=__CHK.record(UPR_ZAWZ,,'DO'))<>''
|| return(_chk)
?};

{? UPR_ZAWZ.DO<>_d0
:  Ze sprawdzonych powyżej warunków:
:     - daty: OD i DO są wypełnione;
:     - OD<=DO;
:     - OD nie jest mniejsze niż "Data uzyskania" uprawnienia;
:  wynika, że DO nie jest mniejsza niż "Data uzyskania" uprawnienia - nie musimy tego sprawdzać.
|| {? UPR_ZAWO.UTRATA<>_d0 & UPR_ZAWO.UTRATA<UPR_ZAWZ.DO
   || FUN.info('"%1" okresu zawieszenia nie może być późniejsza niż "%2" uprawnienia.'@
         [MS.name(UPR_ZAWZ,'DO'),MS.name(UPR_ZAWO,'UTRATA')]
      );
      return('DO')
   ?};
   {? UPR_ZAWO.DO<>_d0 & UPR_ZAWO.DO<UPR_ZAWZ.DO
   || FUN.info('"%1" okresu zawieszenia nie może być późniejsza niż "%2" uprawnienia.'@
         [MS.name(UPR_ZAWZ,'DO'),MS.name(UPR_ZAWO,'DO')]
      );
      return('DO')
   ?}
?};

exec('check','overlap',{? _popraw || UPR_ZAWZ.ref() || null() ?},UPR_ZAWZ,,,,,,'OD',UPR_ZAWO.ref())


\upr_zawo_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.42]
:: OPIS: Obsługa akcji "Załączniki - przed" w oknach tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','UPR_ZAWO',exec('chk_role','#b__box',OPERATOR.USER,'PKD_DOS_PRUZ'))


:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 0566bf28449b9cde5ea908018b0d1ac9d469a2740878feac3a00abae42eee0a367a57262b8af6775e58385185a6d6b9a595afac118fd3079ccfdb05e2805984ec5fd7b50fbc99c6d09b5ef2f34bde628459127e5dde1e6826e77e3b5c71a8a660d449f0bc14d630089494df05d66546cfbd9880e72f86c69b999e5fad8196e5e

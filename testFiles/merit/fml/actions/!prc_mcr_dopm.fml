:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_mcr_dopm.fml
:: Utworzony: 05.10.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_MCR_DOPM - Odblokowanie planowania w mc.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Odblokowanie planowania dla miesiąca rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKRM, type=_A_OKRM, name=Wskazanie na miesiąc rozliczeniowy, required=T
::# kind=WY, symbol=S_PLAN, type=STRING, name=Status blokady planowania, required=N
::
_par:=params_get();
params_set(_par);
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;
_uidref:=exec('ref2uid','#table',_in.A_OKRM);
_result:='';

{? _uidref=''
|| _mp.error(exec('error','!prc_mcr_dopm','Nie znaleziono miesiąca rozliczeniowego.'@))
|? _mp.pathProc() | _mp.pathTodo()
|| _result:=exec('odblokowanie','!prc_mcr_dopm',_in,_out);
   {? _out.S_PLAN='O'
   || _mp.save(,_out);
      _mp.done()
   |? var_pres('_result')=type_of('')
   || _mp.error(_result)
   |? _result:=2
   || _mp.keep()
   || _mp.save(,_out);
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Zablokowanie planowania dla miesiąca rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a_okrm:=params_get().mp.load(exec('kind_in','#b_port')).A_OKRM;
_tab:=params_exec('opis_mc','okres',_a_okrm);

{? _tab.ZAW_DANE='T'
|| 'Odblokuj planowanie w miesiącu: %1'@@[date(_tab.R,_tab.M,)$8]
|| 'Odblokuj planowanie w miesiącu'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Szczegóły komunikatu o błędzie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Odblokowanie planowania nie jest możliwe.'@,_a]


\odblokowanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Odblokowanie planowania dla miesiąca rozliczeniowego.
::   WE: _a - parametry wejściowe czynności
::       _b - parametry wyjściowe czynności
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:=_result:='';
A_OKRM.cntx_psh();
{? A_OKRM.seek(_a.A_OKRM,,1)
|| {? A_OKRM.S='Z'
   || _result:=_msg:=exec('error','!prc_mcr_dopm','Miesiąc rozliczeniowy jest zamknięty.'@)
   |? A_OKRM.S_PLAN='O'
   || _result:=_msg:=exec('error','!prc_mcr_dopm','Miesiąc rozliczeniowy jest odblokowany.'@)
   || _tab:=tab_tmp(,'RESULT','INTEGER','');
      _ed:=_tab.mk_edit('Odblokowanie miesiąca rozliczeniowego %1'@[date(A_OKRM.R,A_OKRM.M,)$8],0);
      _tab.win_efld(_ed,AH,'H',,,,,,'Miesiąc rozliczeniowy %1 zostanie odblokowany.'@[date(A_OKRM.R,A_OKRM.M,)$8]);
      exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:Esc'",,
         exec('help_red_zakoncz','#window','PKD_C'),exec('text_red_zakoncz','#window','PKD_C'));
      exec('ok_esc','#window',_tab,_ed,1,,"cur_tab().RESULT:=0; 'key:Esc'",0,,
         exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
      _tab.win_edit(_ed);
      _tab.edit();
      {? _tab.RESULT=1
      || exec('a_okrm_wer_bfo','prc_mscrozlicz',0);
         A_OKRM.seek(_a.A_OKRM,,1,1);
         {? A_OKRM.S_PLAN='O'
         || _b.S_PLAN:='O';
            _msg:='Miesiąc rozliczeniowy %1 został odblokowany.'@[date(A_OKRM.R,A_OKRM.M,)$8];
            _result:=1
         || _result:=_msg:='Miesiąc rozliczeniowy %1 nie został odblokowany.'@[date(A_OKRM.R,A_OKRM.M,)$8]
         ?}
      || _result:=2
      ?};
      obj_del(_tab)
   ?}
|| _result:=_msg:=exec('error','!prc_mcr_dopm','Nie udało się odnaleźć miesiąca rozliczeniowego do odblokowania.'@)
?};
{? +_msg || FUN.emsg(_msg) ?};
A_OKRM.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 845060691e96613629a96c7c68664706778805e9c8fecac5533a2e1965df78c86acc3c4420a3a94eec45a197b4221d6969f9541f6947a30f9f24591a04503ba8d4f7a464da5d9972c17205bc5a606411f9ccea21402c76748c762553197c7d1819299006c31088782a6abf595b3e56e7d6735956085f380523ed7b4989adca9d

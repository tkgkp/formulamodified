:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_roz_khpt.fml
:: Utworzony: 21.03.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_ROZ_KHPT - Wysłanie powiadomień o zapłacie
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wysłanie powiadomień o zapłacie - główna formuła czynności FKS_ROZ_KHPT
::----------------------------------------------------------------------------------------------------------------------
::# properties=SEND
:: WE
::# kind=WE, symbol=KONTO, type=STRING, name=Prefiks kont analitycznych, required=N
::# kind=WE, symbol=DNI_PRZED, type=NUMBER, name=Liczba dnie przed terminem płatności, required=N
::# kind=WE, symbol=WAL, type=STRING, name=Symbol waluty, required=N
::# kind=WE, symbol=JEDNKS, type=STRING, name=Symbol jednostk księgowej, required=N
::# kind=WE, symbol=TYP_ROZ, type=STRING, name=Typ rozrachunku, required=N, fml_val="exec('typ_roz_val','!fks_roz_khpt',_a)"
::# kind=WE, symbol=EMAIL_ERR, type=STRING, name=E-mail z inf. o błędach w wysyłce, required=N

_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_komunik:=(_mp.pathTodo() | _mp.pathProc()) & ~_mp.isAutoRun() & ~_mp.isService();
_ok:=~_komunik |
   FUN.ask('Czy uruchomić wysyłanie powiadomień e-mail\ndo kontrahentów o płatnościach za faktury?'@);
{? _ok
|| _konto:={? var_press('KONTO',_in)>0 || _in.KONTO || '' ?};
   _dni:={? var_press('DNI_PRZED',_in)>0 || _in.DNI_PRZED || 0 ?};
   _wal:={? var_press('WAL',_in)>0 || _in.WAL || '' ?};
   _jedn:={? var_press('JEDNKS',_in)>0 || _in.JEDNKS || '' ?};
   _typ_str:={? var_press('TYP_ROZ',_in)>0 || _in.TYP_ROZ || 'NAL I NOD' ?};
   _typ:={? _typ_str='NAL' || 1 |? _typ_str='NOD' || 2 || 3 ?};
   _email:={? var_press('EMAIL_ERR',_in)>0 || _in.EMAIL_ERR || '' ?};
   exec('pow_przyp','!fks_roz_khpt',
      _konto,_dni,_wal,_jedn,_typ,_in.SENDER,_in.FROM,_in.REPLYTO,_email,
      _komunik
   );
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Wyślij powiadomienia o płatnościach do kontrahentów'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('DNI_PRZED',_we)>0
|| _ile:=_we.DNI_PRZED;
   {? _ile=0
   || _desc:='Wyślij powiadomienia o płatnościach do kontrahentów w dniu płatności'@@
   || {? _ile=1
      || {? _ile<0
         || _desc:='Wyślij powiadomienia o płatnościach do kontrahentów %1 dzień po terminem płatności'@@[$(|_ile)]
         || _desc:='Wyślij powiadomienia o płatnościach do kontrahentów %1 dzień przed terminem płatności'@@[$(|_ile)]
         ?}
      || {? _ile<0
         || _desc:='Wyślij powiadomienia o płatnościach do kontrahentów %1 dni po terminem płatności'@@[$(|_ile)]
         || _desc:='Wyślij powiadomienia o płatnościach do kontrahentów %1 dni przed terminem płatności'@@[$(|_ile)]
         ?}
      ?}
   ?}
?};
_desc


\pow_przyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.10]
:: OPIS: Start systemu - wprowadzanie danych
::   WE: _a - prefiks kont analitycznych (pusty string - wszystkie konta)
::       _b - liczba dni przed terminem platnosci (ujemna - przypomnienie po terminie platnosci)
::       _c - symbol waluty (pusty to narodowa)
::       _d - symbol jednostki ksiegowej (pusta to wszystkie)
::       _e - typy rozrachunkow - 1 - 'NAL', 2 - 'NOD', 3 - 'NAL' i 'NOD'
::       _f - nadawca
::       _g - od
::       _h - odpowiedź do
::       _i - email administratora - wysylanie maila z informacją o błędach
::       _j - wyświetlać podsumowanie? 1-tak 0-nie
::  OLD: \pow_przyp/kh_zaleg.fml
::----------------------------------------------------------------------------------------------------------------------
_konto:=_a;
_ldn:=_b;
_wals:=_c;
_odds:=_d;
_typ:=_e;
_wal:=FINFO.NAROD;
_err:=_i<>'';
{? _err
|| VAR_DEL.delete('Body');
   Body:=obj_new('BODY','LP','ind','add');
   Body.BODY:=tab_tmp(1,
      'LP','INTEGER','Lp',
      'TEKST','STRING[255]','Tekst',
      'KH','INTEGER',
   );
   Body.ind:=Body.BODY.ndx_tmp('',1,'KH',,0);
   Body.LP:=0;
   Body.add:="
      {? var_press('_b')>0 & _b
      || Body.BODY.cntx_psh();
         Body.BODY.index(Body.ind); Body.BODY.prefix(#_b);
         _ok:=~Body.BODY.first();
         Body.BODY.cntx_pop()
      || _ok:=1
      ?};
      {? _ok
      || Body.LP+=1;
         Body.BODY.LP:=Body.LP;
         Body.BODY.TEKST:=_a;
         Body.BODY.KH:={? var_press('_b')>0 || #_b || 0 ?};
         Body.BODY.add()
      ?}
   "
?};
VAR_DEL.delete('Info');
Info:=obj_new('F','OK','show');
Info.F:=Info.OK:=0;
Info.show:="
   FUN.info(
      'Liczba powiadomień: %1\n'
      'Liczba wysłanych powiadomień: %2\n'
      'Liczba nie wysłanych powiadomień: %3.'@[$Info.F,$Info.OK,$(Info.F-Info.OK)]
   )
";
{? _wals<>''
|| SLU.cntx_psh(); SLUAPPL.cntx_psh();
   SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(FINFO.SLWAL().SLU,_wals);
   {? SLO.first() || _wal:=SLO.ref() ?};
   SLO.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop()
?};
_odd:=null;
{? _odds<>''
|| ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA,_odds);
   {? ODD.first() || _odd:=ODD.ref() ?};
   ODD.cntx_pop()
?};
ROK_F.cntx_psh();
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
_znalokr:={? OKRO_F.find_ge(date()) || 1
          |? OKRO_F.find_ge(date(date()~1-1,1,1)) || 1
          || 0
          ?};
{? _znalokr
|| OKRO_F.ROK();
   OP.cntx_psh(); OP.use('operac'+ROK_F.KOD);
   ZAP_OP.cntx_psh(); ZAP_OP.use('rozzap'+ROK_F.KOD);
   {? _odd=null
   || OP.index('STAN'); OP.prefix(_wal,'N',_konto)
   || OP.index('STAN_O'); OP.prefix(_wal,_odd,'N',_konto)
   ?};
   {? OP.first()
   || {!
      |? {? OP.KH<>null & OP.TZ=date()+_ldn & OP.WN$2>0 &
            ((_typ=1 & OP.TYP='NAL') | (_typ=2 & OP.TYP='NOD') | (_typ=3 & (OP.TYP='NOD' | OP.TYP='NAL')))
         || Info.F+=1;
            _email:=exec('email_os_kon','kontrahent',OP.KH,'Windykacja');
            {? _email<>''
            || _att:=null;
               _plik:=exec('op_zal','!fks_roz_khpt');
               {? _plik.first()
               || _att:=_plik.ATT
               ?};
               {? exec('add','!fks_roz_khpt',_f,_g,_h,_att,_email)
               || _opis:='Wysłano przypomnienie o terminie zapłaty faktury '+OP.SYM+'\n';
                  _opis+='Data wystawienia: '+$OP.DO+'\n';
                  _opis+='Termin płatności: '+$OP.TZ+'\n';
                  _opis+='Na adres:\n'+_email;
                  exec('dod_zdarz','!fks_roz_khpt','Faktura '+OP.SYM+' - wysłano przypomnienie.',_opis,KH_OSOB.IMIE+' '+KH_OSOB.NAZWISKO)
               ?};
               obj_del(_plik);
               Info.OK+=1
            |? _err
            || {? Body.LP=0
               || Body.add('Brak osoby kontaktowej dla widnykacji lub jej adresu email dla następujących kontrahentów:')
               ?};
               Body.add('- '+OP.KH().KOD+' - '+KH.SKR,KH.ref())
            ?};
            1
         ?};
         OP.next()
      !}
   ?};
   OP.cntx_pop();
   ZAP_OP.cntx_pop()
?};
OKRO_F.cntx_pop();
ROK_F.cntx_pop();
{? _err
|| {? Body.BODY.first()
   || exec('add_err','!fks_roz_khpt',_f,_g,_h,_i,Body.BODY)
   ?};
   VAR_DEL.delete('Body')
?};
{? _j
|| Info.show()
?};
VAR_DEL.delete('Info');
1


\op_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.10]
:: OPIS: Sprawdzenie, czy dla rozrachunku jest zalacznik z faktura
::   WY: nazwa pliku
::  OLD: \op_zal/kh_zaleg.fml
::----------------------------------------------------------------------------------------------------------------------
_plik:=tab_tmp(1,'PATH','STRING[255]','Ścieżka','ATT','BLOBRAW','Załącznik');
DOK.cntx_psh; POZ.cntx_psh;
ZAP_OP.cntx_psh(); ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
{? ZAP_OP.first()
|| {! |?
      {? ZAP_OP.WN$2>0
      || _maska:=(ref_name(ZAP_OP.POZDOK)+4);
         {? _maska<>(DOK.name()+4) | _maska<>(POZ.name()+4)
         || POZ.use('pozy'+_maska); DOK.use('doku'+_maska)
         ?};
         {? ZAP_OP.POZDOK().DOK().E_DOC
         || _plik.PATH:='';
            _plik.ATT:=DOK.E_DOC;
            _plik.add()
         ?}
      ?};
      ZAP_OP.next()
   !}
?};
DOK.cntx_pop; POZ.cntx_pop; ZAP_OP.cntx_pop();
_plik


\dod_zdarz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.10]
:: OPIS: Krotki opis zdarzenia
::   WE: _a - krótki opis
::       _b - peły opis
::       _c - osoba kontaktowa
::  OLD: \dod_zdarz/kh_zaleg.fml
::----------------------------------------------------------------------------------------------------------------------
ZDARZT.cntx_psh(); ZDARZT.index('ZDARZT'); ZDARZT.prefix();
{? ~ZDARZT.find_key('N','FAKTURA_POWIAD')
|| ZDARZT.blank();
   ZDARZT.SYS:='N';
   ZDARZT.ZDARZ:='FAKTURA_POWIAD';
   ZDARZT.ICON:='xwin16.png:77';
   ZDARZT.add()
?};
{? ZDARZT.find_key('N','FAKTURA_POWIAD')
|| ZDARZ.cntx_psh();
   ZDARZ.prefix(); ZDARZ.blank();
   ZDARZ.DOT:=ZDARZT.ref();
   ZDARZ.T:='H';
   ZDARZ.KH:=OP.KH;
   ZDARZ.KR_OP:=_a;
   {? ZDARZ.add()
   || {? ZDARZ.memo_set(_b,'OP') || ZDARZ.memo_put(,'OP') ?}
   ?};
   ZDARZ.cntx_pop()
?};
POM.TAB:='DOKUM';
POM.TYPDOK:='SYS';
DOKUM.prefix();
exec('add_grnr','numery','SYS');
DOKUM.blank(1);
DOKUM.FIRMA:=REF.FIRMA;
DOKUM.TYP:='P';
::DOKUM.REFSQL:=_a;
DOKUM.KR_OP:=_a;
::DOKUM.EM:=_d;
DOKUM.KH:=OP.KH;
DOKUM.DOT:=ZDARZT.ref();
DOKUM.DATAKONT:=date();
DOKUM.OPER:=exec('name','users');
DOKUM.OSOBA:=_c;
DOKUM.DATA:=date();
DOKUM.CZAS:=time();
_ok:=DOKUM.add();
{? _ok
|| DOKUM.NR:=exec('numer_new','numery','PACZKA');
   exec('znak','numery','DOKUM');
   DOKUM.memo_set(_b,'OPIS');
   DOKUM.memo_put(,'OPIS');
   DOKUM.put()
?};
ZDARZT.cntx_pop()


\header
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.10]
:: OPIS: Naglowek wiadomosci e-mail wysylanej ze zdarzenia POW_WYS_PRZYP
::  OLD: \header/kh_zaleg.fml
::----------------------------------------------------------------------------------------------------------------------
'Przypomnienie o terminie zapłaty faktury '+OP.SYM


\body
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.10]
:: OPIS: Tresc wiadomosci e-mail wysylanej ze zdarzenia POW_WYS_PRZYP
::   WE: _a - dostępny załacznik?
::  OLD: \body/kh_zaleg.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('ML',@)<=100 || ML:=obj_new(@.CLASS.MEMO) ?};
{? var_pres('BODY')>0
|| BODY.erase()
|| BODY:=tab_tmp(1,'LP','INTEGER','Lp',
                   'TEKST','STRING[255]','Tekst')
?};
__LP:=0;
_add:="__LP+=1; BODY.LP:=__LP; BODY.TEKST:=_a; BODY.add()";
_ad:=obj_new(3);
_tekst:='<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Final//EN">'; _add(_tekst);
_tekst:='<html>'; _add(_tekst);
_tekst:='<head>'; _add(_tekst);
_tekst:='<meta http-equiv="content-type" content="text/html; charset=iso-8859-2">'; _add(_tekst);
_tekst:='</head>'; _add(_tekst);
_tekst:='<body>'; _add(_tekst);
_tekst:='<BR><BR>'; _add(_tekst);
_tekst:='<CENTER><H3>Przypomnienie o terminie zapłaty faktury '+OP.SYM+'</H3></CENTER><BR><BR>'; _add(_tekst);
_tekst:=('<table align=\"center\" valign=\"top\" border=\"0\" cellspacing=\"0\" cellpadding=\"0\"  width=\"100%\">');
_add(_tekst);
_tekst1:="('<tr >'+
           '<td width=\"800\">'+_a+'</td></tr>')";
_tekst:=_tekst1(OP.KH().NAZ_P); _add(_tekst);
_tekst:=_tekst1(OP.KH().UL); _add(_tekst);
_tekst:=_tekst1(OP.KH().POCZ+' '+OP.KH().MIASTO); _add(_tekst);
_tekst:='</table><BR><BR>'; _add(_tekst);
{? OP.TZ>date()
|| _tekst:='<H4>Uprzejmie przypominamy o zbliżającym się terminie zapłaty faktury: </H4></CENTER><BR>'
|? OP.TZ<date()
|| _tekst:='<H4>Uprzejmie przypominamy że minął termin zapłaty faktury: </H4></CENTER><BR>'
|? OP.TZ=date()
|| _tekst:='<H4>Uprzejmie przypominamy że dziś mija termin zapłaty faktury: </H4></CENTER><BR>'
?};
_add(_tekst);
_tekst:=('<table align=\"center\" valign=\"top\" border=\"0\" cellspacing=\"0\" cellpadding=\"0\"  width=\"100%\">');
_add(_tekst);
_tekst1:="('<tr >'+
           '<td width=\"250\">'+_a+'</td>'+
           '<td width=\"800\">'+_b+'</td></tr>')";
_tekst:=_tekst1('Symbol Faktury:',OP.SYM); _add(_tekst);
_tekst:=_tekst1('Data wystawienia:',$OP.DO); _add(_tekst);
_tekst:=_tekst1('Termin płatności:',$OP.TZ   ); _add(_tekst);
_tekst:=_tekst1('Wartość brutto:',form(OP.WN,,2)+' '+OP.WAL().KOD); _add(_tekst);
_tekst:='</table><BR><BR></H4>'; _add(_tekst);
{? _a
|| _tekst:='<H4>Obraz faktury w załączeniu. </H4><BR>'; _add(_tekst)
?};
_tekst:='</body>'; _add(_tekst);
_tekst:='</html>'; _add(_tekst);
obj_del(_ad); VAR_DEL.delete('__LP','ML');
BODY


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje e-mail do kolejki
::   WE: _a - od
::       _b - wysyłający
::       _c - odpowiedź do
::       _d - załacznik
::       _e - odbiorca
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('add_email_a','#mailbox');
_args.FROM:=_a;
_args.SENDER:=_b;
_args.REPLYTO:=_c;
_args.RCV:=_e;
_args.SUB:=exec('header','!fks_roz_khpt');
_args.BODYH:=exec('body','!fks_roz_khpt',_d<>null);
{? _d || _args.ATTACH:=_d ?};
exec('add_email','#mailbox',_args)


\add_err
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje e-mail do kolejki
::   WE: _a - od
::       _b - wysyłający
::       _c - odpowiedź do
::       _d - odbiorca
::       _e - tabela z treścią maila
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('add_email_a','#mailbox');
_args.FROM:=_a;
_args.SENDER:=_b;
_args.REPLYTO:=_c;
_args.RCV:=_d;
_args.SUB:='Wysłanie powiadomień o płatnościach - uwagi';
_args.BODYT:=_e;
exec('add_email','#mailbox',_args)


\typ_roz_val
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zwraca wartość paraemtru TYP_ROZ czynności
::----------------------------------------------------------------------------------------------------------------------
_typ:={? _a='NAL' || 1 |? _a='NOD' || 2 |? _a='NAL I NOD' || 3 ?};
_typ:=FUN.choice('Wybierz typ rozrachunków.'@,_typ,'&NAL','N&OD','NAL &I NOD');
{? _typ=0
|| ~~
|? _typ=1
|| 'NAL'
|? _typ=2
|| 'NOD'
|| 'NAL I NOD'
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:49 0c69d1f0378cb01ddf7c546fcbd1fd77ab23a46f9fa011a06529fb13fe18a7b482b01dafb44dc37f46e798f5934b1716e75e2480c988b50ca3c00a439d367c1753d18e439541679b201b6ad9d86fe047019fdf035535aca80371601c54a57a4d9ea7698885caa3f1412ea8698dee1a7cba6951df5471131f71f16c96077821ef

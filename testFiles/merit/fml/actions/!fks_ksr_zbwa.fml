:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_zbwa.fml
:: Utworzony: 15.06.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_KSR_ZBWA - Sprawozdania według analityk
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Sprawozdania według analityk - główna formuła czynności FKS_KSR_ZBWA.
::   WE: jeżeli _a i _a = 1  - wywołanie z raportów (pulpit)
::----------------------------------------------------------------------------------------------------------------------
exec('F','object');
exec('RB','object');
exec('nag_sel','!fks_ksr_zbwa')


\nag_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Definicje zestawien
::  OLD: \nag_sel/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
SPR_NAG.index('NAZWA');
SPR_NAG.prefix();
SPR_NAG.win_sel('WER');
SPR_NAG.select(,,,{? PAR_SKID.get(33)='N' || 'W' || '' ?})


\spr_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula na blank pola SPR_K.SPR_NAG
::  OLD: \spr_nag/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
SPR_NAG.ref()


\bl_kolum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula na blank pola SPR_K.KOLUMNA
::  OLD: \bl_kolum/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
BLKOL


\sprclp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula na blank dla pola SPR_K.LP
::  OLD: \sprclp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.NR


\sprk_wae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula po redakcji pola SPR_K.WYZNACZN - rodzaj kwoty lub poziom sumowania
::  OLD: \sprk_wae/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? BLKOL='T'
|| _zwrot:={? |SPR_K.WYZNACZN<>'' || exec('kw_rodae','koszty','J') || 1 ?}
|| _sl:=SPR_K.CZY_SLU;
   _co:=|KOSZTY.WYZNACZN;
   {? _co<>'' &
         (TAB_SLU.index(ind_slu2);
          TAB_SLU.first();
          _zwrot:=TAB_SLU.find_key(_sl,_co);
          {? ~_zwrot
          || TAB_SLU.index(ind_slu3);
             TAB_SLU.first();
             _zwrot:=TAB_SLU.find_key(_co)
          ?};
          _zwrot)
   || {? -menu_txt='popraw' || _ref:=SPR_K.ref() ?};
      _co:=TAB_SLU.NAZ;
      SPR_K.cntx_psh();
      SPR_K.index('KOLUMNA1');
      SPR_K.prefix(SPR_NAG.ref(),_sl,'N',_co,_co);
      _zwrot:=~(SPR_K.first() & {? -menu_txt='popraw' || _ref<>SPR_K.ref() || 1 ?});
      SPR_K.cntx_pop();
      {? ~_zwrot
     || {? _sl=1
         ||  FUN.info('Istnieje już słownik o nazwie: "%1".'@[_co])
         ||  FUN.info('Istnieje już pozycja o nazwie: "%1".'@[_co])
         ?};
         SPR_K.WYZNACZN:=KOSZTY.WYZNACZN:=''
      || SPR_K.WYZNACZN:=KOSZTY.WYZNACZN:=TAB_SLU.NAZ;
         SPR_K.CZY_SLU:=TAB_SLU.CZY_SL;
         SLU.cntx_psh();
         SLU.index('NAZ');
         SLU.prefix();
         _znal_sl:=0;
         {? ((SPR_K.CZY_SLU=0 & -KOSZTY.WYZNACZN='jednostka księgowa') & +(|SPR_K.MASKA)<>8)
         || SPR_K.MASKA:=8*'?'; SPR_K.OD:=SPR_K.DO:=''
         |? ((SPR_K.CZY_SLU=0 & -KOSZTY.WYZNACZN='konto analityczne') & +(|SPR_K.MASKA)<>35)
         || SPR_K.MASKA:=35*'?'; SPR_K.OD:=SPR_K.DO:=''
         |? ((SPR_K.CZY_SLU=0 & -KOSZTY.WYZNACZN='konto syntetyczne') & +(|SPR_K.MASKA)<>6)
         || SPR_K.MASKA:=6*'?'; SPR_K.OD:=SPR_K.DO:=''
         |? (SPR_K.CZY_SLU=1 & ({? SLU.find_key(KOSZTY.WYZNACZN)
                                || _znal_sl:=(SLU.NAZ=KOSZTY.WYZNACZN)
                                || _znal_sl:=0
                                ?};
                                {? _znal_sl
                                || SLUAPPL.cntx_psh();
                                   SLUAPPL.index('NAZ'); SLUAPPL.prefix('F',KOSZTY.WYZNACZN);
                                   {? SLUAPPL.first() & SLUAPPL.SLU().NAZ=KOSZTY.WYZNACZN
                                   || SPR_K.SLU:=SLUAPPL.ref()
                                   ?};
                                   SLUAPPL.cntx_pop();
                                   +(|SPR_K.MASKA)<>SLU.DL
                                || 0
                                ?})
            )
         || SPR_K.MASKA:=SLU.DL*'?';
            SPR_K.OD:=SPR_K.DO:=''
         ?};
         SLU.cntx_pop()
      ?}
   || SPR_K.WYZNACZN:=KOSZTY.WYZNACZN:='';
      FUN.info('Wybierz poprawną wartość ze słownika.'@);
      _zwrot:=0
   ?}
?};
_zwrot


\sprk_wf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula na klawisz F3 w polu SPR_K.WYZNACZN - rodzaj kwoty lub poziom sumowania
::  OLD: \sprk_wf3/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? BLKOL='T'
|| exec('kw_rodf3','koszty','J')
|| TAB_SLU.index(ind_slu1);
   {? |KOSZTY.WYZNACZN<>''
   || TAB_SLU.index(ind_slu2);
      TAB_SLU.prefix();
      {? ~TAB_SLU.find_key(SPR_K.CZY_SLU,|KOSZTY.WYZNACZN)
      || TAB_SLU.index(ind_slu3);
         TAB_SLU.prefix();
         {? ~TAB_SLU.find_key(|KOSZTY.WYZNACZN) || TAB_SLU.first() ?}
      ?};
      TAB_SLU.index(ind_slu1)
   || TAB_SLU.first()
   ?};
   {? TAB_SLU.select(,1)
   || SPR_K.CZY_SLU:=TAB_SLU.CZY_SL;
      SPR_K.WYZNACZN:=KOSZTY.WYZNACZN:=TAB_SLU.NAZ
   ?}
?}


\f3konco
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Na klawisz F3 dla pola SPR_K.OD i SPR_K.DO
::  OLD: \f3konco/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? -(6+menu_txt())='szukaj'
|| 1
|| {? (SPR_K.CZY_SLU=0 & (-SPR_K.WYZNACZN='konto analityczne' | -SPR_K.WYZNACZN='konto syntetyczne'))
   || KS.cntx_psh();
      KS.index('SYM');
      KS.prefix(SSTALE.AR);
      {? ~(fld()<>'' & KS.find_key(fld())) || KS.first() ?};
      KS.win_sel('SLO_TEN');
      {? KS.select(,1) || fld(KS.SYM) ?};
      KS.cntx_pop()
   |? (SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='jednostka księgowa')
   || ODD.cntx_psh();
      ODD.index('ODDZIALY');
      ODD.prefix(REF.FIRMA);
      ODD.win_sel('WYB');
      {? ~(fld()<>'' & ODD.find_key(fld())) || ODD.first() ?};
      {? ODD.select(,1) || fld(ODD.OD) ?};
      ODD.cntx_pop()
   || SLO.cntx_psh();
      SLU.cntx_psh();
      _znal_sl:=0;
      SLU.index('NAZ');
      SLU.prefix(SPR_K.WYZNACZN);
      _znal_sl:={? SLU.first() || SLU.NAZ=SPR_K.WYZNACZN || 0 ?};
      {? _znal_sl
      || SLOSLU.SLSLU:=SPR_K.WYZNACZN;
         SLO.win_sel('ONE_SEL');
         SLO.index('SL');
         SLO.prefix(SLU.ref());
         {? ~(fld()<>'' & SLO.find_key(fld())) || SLO.first() ?};
         {? SLO.select(,1) || fld(SLO.KOD) ?}
      ?};
      SLO.cntx_pop();
      SLU.cntx_pop()
   ?};
   fld()
?}


\wz_maska
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula na wzorzec dla pola SPR_K.MASKA
::  OLD: \wz_maska/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='konto analityczne'
|| exec('wz_konto','edkonto','SPR_K','MASKA')
|| ''
?}


\beconmas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula przed redakcja pola SPR_K.MASKA
::  OLD: \beconmas/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='konto analityczne'
|| exec('be_konto','edkonto','SPR_K','MASKA')
|| 1
?}


\f3conmas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula na klawisz F3 dla pola SPR_K.MASKA
::  OLD: \f3conmas/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='konto analityczne'
|| exec('f3_konto','edkonto','SPR_K','MASKA')
|| 1
?}


\querymar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Po redakcji pola SPR_K.MASKA
::   WE: _a - 0 - po znaku wzorca
::            1 - po redakcji pola
::  OLD: \querymar/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? -SPR_K.WYZNACZN='konto analityczne'
|| exec('ae_konto','edkonto',_a,'SPR_K','MASKA',0,2,0,"exec('querymar1','!fks_ksr_zbwa')")
|| exec('querymar1','!fks_ksr_zbwa')
?}


\querymar1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Po redakcji pola SPR_K.MASKA
::   WE: _a - 0 - po znaku wzorca
::            1 - po redakcji pola
::  OLD: \querymar1/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_K.CZY_SLU=1
|| SLU.cntx_psh();
   SLU.index('NAZ');
   SLU.prefix();
   _znal_sl:=0;
   {? SLU.find_key(SPR_K.WYZNACZN)
   || _znal_sl:=(SLU.NAZ=SPR_K.WYZNACZN)
   || _znal_sl:=0
   ?};
   {? _znal_sl
   || fld(form(fld+'????????',SLU.DL)); 1
   || FUN.info('Nie znaleziono w systemie FIKS słownika o nazwie "%1'' .'@[SPR_K.WYZNACZN]); 0
   ?};
   SLU.cntx_pop()
|? SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='jednostka księgowa'
|| fld(form(fld+8*'?',8)); 1
|? -SPR_K.WYZNACZN='konto analityczne'
|| fld(form(fld+35*'?',35)); 1
|? -SPR_K.WYZNACZN='konto syntetyczne'
|| fld(form(fld+6*'?',6)); 1
|| 1
?}


\il_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula sprawdza ilosc rekordow w tabeli SPR_K przy dolaczaniu kolumn
::  OLD: \il_grp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
exec('bepopspr','!fks_ksr_zbwa');
ROZNE.NR:={? SPR_K.size()
          || {? -menu_txt='dołącz'
             || SPR_K.LP
             || SPR_K.LP+1
             ?}
          || 1
          ?};
{? -BLKOL='t' & SPR_K.size()=8
|| FUN.info('Ilość kolumn nie może przekroczyć 8.'@); _zwrot:=0
?};
_zwrot


\aedolcon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Dolacz po dla tabeli SPR_K
::  OLD: \aedolcon/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt='dołącz'
|| _ref:=SPR_K.ref();
   _nr:=SPR_K.LP;
   SPR_K.cntx_psh();
   {? SPR_K.last()
   || {! |?
         {? SPR_K.LP>=_nr & SPR_K.ref()<>_ref
         || SPR_K.LP+=1; SPR_K.put()
         ?};
         SPR_K.prev()
      !}
   ?};
   SPR_K.cntx_pop()
?}; 0


\bepopspr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Przed akcja 'Popraw' w tabeli SPR_K
::  OLD: \bepopspr/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
SPR_K.win_edit({? BLKOL='T' || 'RED_KOL' || 'RED_KLU' ?}); 1


\us_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Usuwanie rekordow w tabeli SPR_K
::  OLD: \us_kol/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć bieżący wiersz?'@)
|| {? SPR_K.size()>1
   || SPR_K.cntx_psh();
      _refnew:={? SPR_K.next() | SPR_K.prev() || SPR_K.ref() || null ?};
      SPR_K.cntx_pop();
      SPR_K.cntx_psh();
      _lp:=SPR_K.LP;
      {? SPR_K.next()
      || _ref:=SPR_K.ref();
         do();
         SPR_K.cntx_pop();
         SPR_K.del();
         {? SPR_K.seek(_ref)
         || SPR_K.index('KOLUMNA');
            {! |?
               SPR_K.LP:=_lp; _lp+=1; SPR_K.put();
               SPR_K.next()
            !}
         ?};
         end()
      || SPR_K.cntx_pop();
         SPR_K.del()
      ?};
      {? _refnew<>null || SPR_K.seek(_refnew) ?}
   || SPR_K.del()
   ?}
?}


\rekpocon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Rekord po dla filtra w sprawozdaniach definiowalnych
::  OLD: \rekpocon/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? SPR_K.OD<>'' & SPR_K.DO<>'' & SPR_K.DO<SPR_K.OD
|| FUN.info('Nie wprowadzono poprawnie zakresu\ndla poziomu grupowania.'@);
   _zwrot:=0
|| {? -SPR_K.SUMA='t'
   || SPR_K.cntx_psh();
      _refbiez:=SPR_K.ref();
      {? SPR_K.first()
      || _licznik:=0;
         _czybie:=0;
         {! |?
           {? -SPR_K.SUMA='t'
           || _licznik+=1;
              {? SPR_K.ref()=_refbiez || _czybie:=1 ?}
           ?};
           SPR_K.next()
         !};
         {? -menu_txt='popraw' & _czybie=0 || _licznik+=1 ?};
         {? (_licznik>5 & -menu_txt<>'popraw') | (_licznik>6 & -menu_txt='popraw')
         || FUN.info('Ilość poziomów grupowania nie może przekroczyć 6.'@);
            _zwrot:=0
         ?}
      ?};
      SPR_K.cntx_pop()
   ?}
?};
_zwrot


\dsppozsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Na wyswietl w okienku WER_KLU tabeli SPR_K
::  OLD: \dsppozsp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
SPR_K.win_edit('RED_KLU');
SPR_K.display();
1


\szukpozs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: okienko wyszukiwania
::  OLD: \szukpozs/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
SPR_K.win_patt('SZUK_POZ');
1


\sprkolco
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Rekord po dla tabeli SPR_K - kolumny sprawozdania
::  OLD: \sprkolco/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? UNPAR.P10='W'
|| _zwrot:=__CHK.record(SPR_K,,'WYZNACZN')
|| _zwrot:=__CHK.record(SPR_K,,'FORMULA')
?};
_zwrot


\szukkols
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: okienko wyszukiwania
::  OLD: \szukkols/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
SPR_K.win_patt();
1


\popsprco
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Poprawianie naglowka sprawozdania definiowalnego
::  OLD: \popsprco/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_NAG.r_lock(1,1)
|| SPR_NAG.win_edit('RED');
   {? SPR_NAG.edit() || SPR_NAG.put() ?};
   SPR_NAG.r_unlock()
|| FUN.info('Sprawozdanie jest obsługiwane przez innego użytkownika.'@)
?}


\spr_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Usuwa cale sprawozdanie
::  OLD: \spr_del/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_NAG.r_lock(1,1)
|| {? FUN.ask('Usunąć definicję sprawozdania?'@)
   || do();
      SPR_K.index('KOLUMNA');
      SPR_K.prefix(SPR_NAG.ref());
      {? SPR_K.first() || {! |? SPR_K.del() !} ?};
      SPR_NAG.del();
      end()
   || SPR_NAG.r_unlock()
   ?}
|| FUN.info('Sprawozdanie jest obsługiwane przez innego użytkownika.'@)
?}


\spr_def_dnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Obsługa drag&drop podczas definiowania sprawozdań
::----------------------------------------------------------------------------------------------------------------------
SPR_K.dnd_sel('WER_KLU',,'records.SPR_K',"SPR_K.cntx_psh();
                                          _ref:=dnd_info('dest_record');
                                          {? SPR_K.seek(_ref)
                                          || exec('zmien_lp','#dragdrop','LP','KOLUMNA')
                                          ?};
                                          SPR_K.cntx_pop()");
SPR_K.dnd_sel('WER_KOL',,'records.SPR_K',"SPR_K.cntx_psh();
                                          _ref:=dnd_info('dest_record');
                                          {? SPR_K.seek(_ref)
                                          || exec('zmien_lp','#dragdrop','LP','KOLUMNA')
                                          ?};
                                          SPR_K.cntx_pop()")


\sprcdef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Definicje zestawien - kolumny i grupowanie
::   WE: _a - 'T' - kolumna, 'N' - klucz (grupowanie)
::  OLD: \sprcdef/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_NAG.r_lock(1,1)
|| UNPAR.P10_AE:=UNPAR.P10_BV:=$"exec('def_en','!fks_ksr_zbwa');~~";
   UNPAR.P10_BE:='';
   exec('slu_tmp','!fks_ksr_zbwa');
   BLKOL:='';
   exec('spr_def_dnd','!fks_ksr_zbwa');
   SPR_K.win_sel('GRUP');
   SPR_K.hdr_sel(': "%1" '@[SPR_NAG.NAZWA]);
   SPR_K.select();
   VAR_DEL.delete('TAB_SLU');
   &ind_slu1; &ind_slu2; &ind_slu3; &BLKOL;
   SPR_NAG.r_unlock()
|| FUN.info('Sprawozdanie jest obsługiwane przez innego użytkownika.'@)
?}


\slu_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Stworzenie tabelki tymczasowej dla poziomow sumowania
::  OLD: \slu_tmp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
TAB_SLU:=tab_tmp(3,'LP','INTEGER','Lp.'@,
                   'NAZ','STRING[20]','Nazwa'@,
                   'OP','STRING[60]','Opis'@,
                   'CZY_SL','INTEGER','');
ind_slu1:=TAB_SLU.index('?');
ind_slu2:=TAB_SLU.ndx_tmp('',1,'CZY_SL',,0,'NAZ',,0,'NAZ',,0);
ind_slu3:=TAB_SLU.ndx_tmp('',1,'NAZ',,0,'NAZ',,0);
_sel:=TAB_SLU.mk_sel('Poziomy grupowania'@,,0,'tab_slu_wer');
TAB_SLU.win_fld(_sel,,'NAZ');
TAB_SLU.win_fld(_sel,,'OP');
TAB_SLU.win_act(_sel,0,'Formuła','Wybierz'@@,,'Wybór bieżącej pozycji'@,"sel_exit()",,1,,,,'W');
TAB_SLU.win_act(_sel,0,'Rekord',,,,"{? ~TAB_SLU.CZY_SL || exec('findtmp','#color') || 0 ?}");
TAB_SLU.win_act(_sel,0,'Szukaj');
TAB_SLU.win_sel(_sel); _lp:=1;
TAB_SLU.LP:=_lp;
TAB_SLU.NAZ:='Konto analityczne';
TAB_SLU.OP:='Symbol konta analitycznego';
TAB_SLU.CZY_SL:=0;
TAB_SLU.add();
_lp+=1;
TAB_SLU.LP:=_lp;
TAB_SLU.NAZ:='Konto syntetyczne';
TAB_SLU.OP:='Symbol konta syntetycznego';
TAB_SLU.CZY_SL:=0;
TAB_SLU.add();
_lp+=1;
TAB_SLU.LP:=_lp;
TAB_SLU.NAZ:='Jednostka księgowa';
TAB_SLU.OP:='Skrótowe oznaczenie jednostki księgowej';
TAB_SLU.CZY_SL:=0;
TAB_SLU.add();
_lp+=1;
SLU.cntx_psh();
SLU.prefix();
SLUAPPL.cntx_psh();
SLUAPPL.index('NAZ');
SLUAPPL.prefix('F');
{? SLUAPPL.first()
|| {! |?
      SLUAPPL.SLU();
      TAB_SLU.LP:=_lp;
      TAB_SLU.NAZ:=SLU.NAZ;
      TAB_SLU.OP:=SLU.OP;
      TAB_SLU.CZY_SL:=1;
      TAB_SLU.add();
      _lp+=1;
      SLUAPPL.next()
   !}
?};
SLU.cntx_pop();
SLUAPPL.cntx_pop()


\tab_ogr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Tworzy tabelke tymczasowa z ograniczeniami nie bedacymi
::       poziomami grupowania
::  OLD: \tab_ogr/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
TABCOGR:=tab_tmp(1,'RODZAJ','STRING[10]','Rodzaj'@,
                   'REFSL','INTEGER','Ref slownika'@,
                   'OD','STRING[8]','Od'@,
                   'DO','STRING[8]','Do'@,
                   'MASKA','STRING[35]','Maska'@,
                   'SUMA','STRING[1]','Suma'@
                 );
indcogr1:=TABCOGR.index('?')


\poziomy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Funkcja ustawia w zmiennych parametry zestawienia
::  OLD: \poziomy/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh();
SLU.index('NAZ'); SLU.prefix();
TABCOGR.prefix();
SPR_K.index('KOLUMNA'); SPR_K.prefix(SPR_NAG.ref(),'N');
poziomy:=obj_new(6);
{! _i:=1..6  |!
   poziomy[_i]:=obj_new(5);
   poziomy[_i][1]:=poziomy[_i][3]:=poziomy[_i][4]:=poziomy[_i][5]:='';
   poziomy[_i][2]:=null
!};
il_poz:=0;
{? SPR_K.first()
|| {! |?
      _ok:=1; _maska:=SPR_K.MASKA;
      {! _i:=1..(+SPR_K.MASKA)
      |? _ok
      |! _znak:=(1+_maska); _maska:=1-_maska; _ok:=(_znak='?')
      !};
      {? _ok || SPR_K.MASKA:='' ?};
      {? SPR_K.CZY_SLU=1
      || _znal_sl:=0;
         {? SLU.find_key(SPR_K.WYZNACZN)
         || _znal_sl:=(SLU.NAZ=SPR_K.WYZNACZN)
         || _znal_sl:=0
         ?};
         {? _znal_sl
         || {? -SPR_K.SUMA='t'
            || il_poz+=1;
               poziomy[il_poz][1]:='slownik'; poziomy[il_poz][2]:=SLU.ref();
               poziomy[il_poz][3]:=SPR_K.OD;  poziomy[il_poz][4]:=SPR_K.DO;
               poziomy[il_poz][5]:=SPR_K.MASKA
            |? |SPR_K.OD<>'' | |SPR_K.DO<>'' | |SPR_K.MASKA<>'' | SPR_K.SUMA='F'
            || TABCOGR.RODZAJ:='slownik';  TABCOGR.REFSL:=#SLU.ref();
               TABCOGR.OD:=SPR_K.OD;       TABCOGR.DO:=SPR_K.DO;
               TABCOGR.MASKA:=SPR_K.MASKA; TABCOGR.SUMA:=SPR_K.SUMA;
               TABCOGR.add()
            ?}
         ?}
      |? SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='konto analityczne'
      || {? -SPR_K.SUMA='t'
         || il_poz+=1; poziomy[il_poz][1]:='konto analityczne'
         |? |SPR_K.OD<>'' | |SPR_K.DO<>'' | |SPR_K.MASKA<>''
         || TABCOGR.RODZAJ:='konto analityczne';    TABCOGR.REFSL:=0;
            TABCOGR.OD:=SPR_K.OD;       TABCOGR.DO:=SPR_K.DO;
            TABCOGR.MASKA:=SPR_K.MASKA; TABCOGR.add()
         ?}
      |? SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='konto syntetyczne'
      || {? -SPR_K.SUMA='t'
         || il_poz+=1; poziomy[il_poz][1]:='konto syntetyczne'
         |? |SPR_K.OD<>'' | |SPR_K.DO<>'' | |SPR_K.MASKA<>''
         || TABCOGR.RODZAJ:='konto syntetyczne';    TABCOGR.REFSL:=0;
            TABCOGR.OD:=SPR_K.OD;       TABCOGR.DO:=SPR_K.DO;
            TABCOGR.MASKA:=SPR_K.MASKA; TABCOGR.add()
         ?}
      |? SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='jednostka księgowa'
      || {? -SPR_K.SUMA='t'
         || il_poz+=1; poziomy[il_poz][1]:='jednostka księgowa'; czy_odd:=1
         |? |SPR_K.OD<>'' | |SPR_K.DO<>'' | |SPR_K.MASKA<>''
         || czy_odd:=2
         ?};
         mask_odd:=SPR_K.MASKA; od_odd:=SPR_K.OD; do_odd:=SPR_K.DO
      ?};
      SPR_K.next()
   !}
?};
SLU.cntx_pop()


\def_okn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Definiowanie okienka tymczasowego
::  OLD: \def_okn/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
undefine();
SPR_K.cntx_psh(); SPR_K.index('KOLUMNA');
{! _i:=1..l_kol
|! SPR_K.prefix(SPR_NAG.ref(),'T',_i);
   _opiskol:=form({? SPR_K.first & |SPR_K.WYZNACZN<>''
                  || |SPR_K.WYZNACZN+' '+SPR_K.STR
                  || 'Kolumna '+$_i
                  ?},18);
   define(('P'+$_i),0,_opiskol,'',,,2)
!};
SPR_K.cntx_pop()


\add_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Obroty - naliczanie wartosci
::  OLD: \add_war/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{! _i:=1..l_kol |! ($('DEFINE.P'+$_i+'+=TABCONTR.KOL'+$_i))() !}


\obl_obr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Obliczenie obrotow dla pozycji
::  OLD: \obl_obr/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? TABCONTR.sel_size()=0
|| exec('def_okn','!fks_ksr_zbwa');
   exec('add_war','!fks_ksr_zbwa');
   def_disp('Obroty zaznaczonych pozycji'@);
   undefine()
|| exec('add_war','!fks_ksr_zbwa')
?}


\be_gobr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Grupa przed dla liczenia obrotow
::  OLD: \be_gobr/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
exec('def_okn','!fks_ksr_zbwa')


\zakr_zap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Zakres zapisow dla konta
::  OLD: \zakr_zap/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_fun_sel:=FUN_SEL.ref();
{? FUN_SEL.select(,1,FUN_SEL.size()) & FUN_SEL.ref()<>_fun_sel
|| zmzapsel:=1;
   sel_exit()
?}


\zapisy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Zapisy na koncie
::  OLD: \zapisy/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('fun_sel','!fks_ksr_zbwa') || exec('zap_sel','!fks_ksr_zbwa') ?};
1


\dok_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Wyswietlanie danych dokumentu
::  OLD: \dok_poz/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
POZ.cntx_psh(); POZ.prefix(); _error:=0;
_ref:=BB.sqlint(ZAP_SEL.POZ_REF); _nam:=form(ZAP_SEL.POZ_REF-8);
{? _ref<>0 & _nam<>'' & POZ.seek(_ref,_nam)
|| DOK.cntx_psh();
   DOK.use('doku'+(POZ.name()+4)); DOK.prefix();
   POZ.DOK();
   exec('dok_spac','dok_fks');
   DOK.cntx_pop()
|| _error:=1
?};
{? _error || FUN.info('Nie znaleziono dokumentu.'@) ?};
POZ.cntx_pop()


\wysw_zap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Wyswietlanie danych zapisu
::  OLD: \wysw_zap/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
POZ.cntx_psh(); POZ.prefix(); _error:=0;
_ref:=BB.sqlint(ZAP_SEL.POZ_REF); _nam:=form(ZAP_SEL.POZ_REF-8);
{? _ref<>0 & _nam<>'' & POZ.seek(_ref,_nam)
|| DOK.cntx_psh();
   DOK.use('doku'+(POZ.name()+4)); DOK.prefix();
   spr_grp:=1;
   exec('zap_spac','fks_ksr',ZAP_SEL.BO);
   &spr_grp;
   DOK.cntx_pop()
|| _error:=1
?};
{? _error || FUN.info('Nie znaleziono pozycji dokumentu.'@) ?};
POZ.cntx_pop()


\zap_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Tabelka z zapisami - tworzenie
::  OLD: \zap_tab/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('ZAP_SEL')<=0
|| ZAP_SEL:=tab_tmp(7,'ODD','STRING[8]','Jedn. ks.'@,
                      'REJ','STRING[8]','Rejestr'@,
                      'NR','INTEGER','Nr'@,
                      'POZ','INTEGER','Poz.'@,
                      'DTW','DATE','Data zap.'@,
                      'DOK','STRING[20]','Symbol dokumentu'@,
                      'KON','STRING[35]','Konto'@,
                      'DOP','DATE','Data oper.'@,
                      'OP','STRING[100]','Treść'@,
                      'WN','REAL','Wn'@,
                      'MA','REAL','Ma'@,
                      'POZ_REF','STRING[20]','Ref sql poz-a'@,
                      'BO','INTEGER',''
                    );
   zapsind1:=ZAP_SEL.index('?');
   _sel:=ZAP_SEL.mk_sel('Zapisy'@,'P',0,'tab_zap_sel');
   ZAP_SEL.win_fld(_sel,,'ODD');
   ZAP_SEL.win_fld(_sel,,'REJ');
   ZAP_SEL.win_fld(_sel,,'NR',,,5);
   ZAP_SEL.win_fld(_sel,,'POZ',,,4);
   ZAP_SEL.win_fld(_sel,,'DOP');
   ZAP_SEL.win_fld(_sel,,'DTW');
   ZAP_SEL.win_fld(_sel,,'DOK');
   ZAP_SEL.win_fld(_sel,,'KON',,,15);
   ZAP_SEL.win_fld(_sel,,'OP',,,10);
   ZAP_SEL.win_fld(_sel,,'WN',,,10,2);
   ZAP_SEL.win_fld(_sel,,'MA',,,10,2);
   ZAP_SEL.win_act(_sel,,'Formuła','Oblicz obroty'@@,,'Obroty i salda zaznaczonych pozycji'@,,
   "exec('obl_obr','dok_fks',ZAP_SEL,ZAP_SEL.WN,ZAP_SEL.MA,F.SWn(ZAP_SEL.WN,ZAP_SEL.MA),F.SMa(ZAP_SEL.WN,ZAP_SEL.MA))",1, 1,"exec('be_gobr','dok_fks')","exec('ae_gobr','dok_fks')",'O');
   ZAP_SEL.win_act(_sel,,'Formuła','Zak&res zapisów'@@,,'Zakres prezentowanych zapisów'@,,
   "exec('zakr_zap','!fks_ksr_zbwa')",,,,,'R');
   ZAP_SEL.win_act(_sel,1,'Formuła','Zak&res zapisów'@@,,'Zakres prezentowanych zapisów'@,,
   "exec('zakr_zap','!fks_ksr_zbwa')",,,,,'R');
   ZAP_SEL.win_act(_sel,,'Formuła','Zapisy dokumentu'@@,,,
   "exec('zapisy','dok_fks',4-(8+ZAP_SEL.POZ_REF),BB.sqlint(ZAP_SEL.POZ_REF),SSTALE.WAL<>FINFO.NAROD)",,,,,,'Z');
   ZAP_SEL.win_act(_sel,,'Formuła','Nagłówek dokumentu'@@,,'Nagłówek dokumentu'@,,"exec('dok_poz','!fks_ksr_zbwa')",,,,,'N');
   ZAP_SEL.win_act(_sel,,'Formuła','Wyróżniki'@@,,'Wyróżniki'@,,
   "exec('wyrozniki','!fks_ksr_zbwa')",,,,,'W');
   ZAP_SEL.win_act(_sel,,'Formuła','Op&is konta'@@,,'Opis konta'@,,
   "exec('opis_konta','!fks_ksr_zbwa')",,,,,'I');
        _menu:='Dane źródłowe'@;
   ZAP_SEL.win_act(_sel,,'Menu',_menu,,'');
   ZAP_SEL.win_act(_sel,0,'Formuła','Dokumenty z innej dziedziny'@@,_menu,'Przeglądanie dokumentu źródłowego'@,"
                                   DOK.cntx_psh();POZ.cntx_psh();
                                   DOK.use('doku'+(4-(8+ZAP_SEL.POZ_REF)));
                                   POZ.use(8+ZAP_SEL.POZ_REF);
                                   DOK.prefix();POZ.prefix();
                                   {? POZ.seek(BB.sqlint(ZAP_SEL.POZ_REF),(8+ZAP_SEL.POZ_REF))
                                   ||  {? POZ.DOK().get() || exec('dok_zrd','dok_fks') ?}
                                   ?};
                                   DOK.cntx_pop();POZ.cntx_pop()",,,,,,'D');
   ZAP_SEL.win_act(_sel,0,'Formuła','E-dokument'@@,_menu,,"exec('wys_e_dok','!fks_ksr_zbwa')",,,,,,'E');
   ZAP_SEL.win_act(_sel,0,'Formuła','Załączniki'@@,_menu,," exec('dok_zal','dokum_zal',4-(8+ZAP_SEL.POZ_REF),BB.sqlint(ZAP_SEL.POZ_REF))",,,,,,'Z');
   ZAP_SEL.win_act(_sel,,'Wyświetl',,,,"exec('wysw_zap','!fks_ksr_zbwa')");
   ZAP_SEL.win_act(_sel,0,'Kolejność');
   ZAP_SEL.win_act(_sel,0,'Szukaj');
   ZAP_SEL.win_act(_sel,,'Rekord',,,,"KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+ZAP_SEL.KON);
                                      {? KS.first()
                                      || exec('opis','konto',ZAP_SEL.KON); exec('ka_opis','konto')
                                      ?}; KS.cntx_pop();
                                      DOK.cntx_psh();POZ.cntx_psh();
                    DOK.use('doku'+(4-(8+ZAP_SEL.POZ_REF)));
                    POZ.use(8+ZAP_SEL.POZ_REF);
                    DOK.prefix();POZ.prefix();
                     _hid_d:=''; _hid:='';
                    {? POZ.seek(BB.sqlint(ZAP_SEL.POZ_REF),(8+ZAP_SEL.POZ_REF))
                    ||  {? POZ.DOK().get()
                        ||  {? PAR_SKID.get(82)='N'
                             ||     DOKUM.index('DOKUM');
                                     DOKUM.prefix(REF.FIRMA,$DOK.ref);
                                     {? DOKUM.first()
                                     ||  ''
                                     || _hid_d+='Z'
                                     ?}
                               ?};'załącznik';
                              {? DOK.DOKZRODL='' | DOK.REJ().KOD='~BO' || _hid_d+='D' ?};'dokument źródłowy';
                              {? ~DOK.E_DOC || _hid_d+='E' ?};'skan ';
                              {? _hid_d<>'' || _hid:='D('+_hid_d+')' || _hid:='' ?}
                         ?}
                     ?};
                     DOK.cntx_pop();POZ.cntx_pop();
                    ZAP_SEL.actions_grayed(ZAP_SEL.win_sel('?'),_hid);0");
   ZAP_SEL.win_sel(_sel)
?}


\wyrozniki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetla wyróżniki z poziomu zapisów w sprawadzaniach według analityk
::----------------------------------------------------------------------------------------------------------------------
POZ.cntx_psh();
DOK.cntx_psh();
{? ZAP_SEL.POZ_REF<>''
|| _ref:=BB.sqlint(ZAP_SEL.POZ_REF);
   _name:=form((ZAP_SEL.POZ_REF)-8);
   {? _ref<>0 & _name<>''
   || {? POZ.name()<>_name || POZ.use(_name) ?};
      POZ.prefix();
      {? POZ.seek(_ref,_name)
      || exec('roz_poz','!fks_dks_dawr')
      || FUN.emsg('Nie znaleziono pozycji dokumentu.'@)
      ?}
   || FUN.emsg('Błędne wskazanie na pozycję dokumentu.'@)
   ?}
|| FUN.emsg('Brak wskazania na pozycję dokumentu.'@)
?};
POZ.cntx_pop();
DOK.cntx_pop()


\opis_konta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetla opis konta z poziomu zapisów w sprawadzaniach według analityk
::----------------------------------------------------------------------------------------------------------------------
exec('op_konta','konto',ZAP_SEL.KON)


\wys_e_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetla e-dokument z poziomu zapisów w sprawadzaniach według analityk
::----------------------------------------------------------------------------------------------------------------------
POZ.cntx_psh();
DOK.cntx_psh();
{? ZAP_SEL.POZ_REF<>''
|| _ref:=BB.sqlint(ZAP_SEL.POZ_REF);
   _name:=form((ZAP_SEL.POZ_REF)-8);
   {? _ref<>0 & _name<>''
   || {? POZ.name()<>_name || POZ.use(_name) ?};
      POZ.prefix();
      {? POZ.seek(_ref,_name)
      || {? DOK.name()<>'doku'+(_name+4)
         || DOK.use('doku'+(_name+4));
            DOK.prefix()
         ?};
         exec('efakview2','dok_fks',POZ.DOK)
      || FUN.emsg('Nie znaleziono pozycji dokumentu.'@)
      ?}
   || FUN.emsg('Błędne wskazanie na pozycję dokumentu.'@)
   ?}
|| FUN.emsg('Brak wskazania na pozycję dokumentu.'@)
?};
POZ.cntx_pop();
DOK.cntx_pop()


\odd_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Szukanie podrzednych jednostek ksiegowych - rekurencyjne
::  OLD: \odd_pod/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
TABCONTR.cntx_psh();
TABCONTR.index(indcont3); TABCONTR.prefix(TABCONTR.ref());
{? TABCONTR.first()
|| {! |?
      {? poziomy[TABCONTR.POZIOM][1]='jednostka księgowa'
      || {? ~ODD_OGR.find_key(TABCONTR.WARTTREE,TABCONTR.WARTTREE)
         || ODD_OGR.ODD:=TABCONTR.WARTTREE; ODD_OGR.add()
         ?}
      || exec('odd_pod','!fks_ksr_zbwa')
      ?};
      TABCONTR.next()
   !}
?};
TABCONTR.cntx_pop()


\szuk_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Szukanie nadrzednych lub podrzednych jednostek ksiegowych
::  OLD: \szuk_odd/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
ODD_OGR:=tab_tmp(1,'ODD','STRING[8]','');
_index:=ODD_OGR.ndx_tmp('',1,'ODD',,0,'ODD',,0);
ODD_OGR.index(_index); ODD_OGR.prefix();
TABCONTR.cntx_psh();
TABCONTR.index(indcont3); TABCONTR.prefix();
{? odd_poz<odd_kan
|| {! |?
      {? TABCONTR.TREE<>0 & TABCONTR.seek(TABCONTR.TREE,TABCONTR.name())
      || {? poziomy[TABCONTR.POZIOM][1]='jednostka księgowa'
         || ODD_OGR.ODD:=TABCONTR.WARTTREE; ODD_OGR.add(); 0
         || 1
         ?}
      || 0
      ?}
   !}
|| exec('odd_pod','!fks_ksr_zbwa')
?};
TABCONTR.cntx_pop()


\spr_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Sprawdzanie podrzednych pozycji - czy konto wystepuje w nich
::  OLD: \spr_pod/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
TABCONTR.cntx_psh();
TABCONTR.index(indcont3); TABCONTR.prefix(TABCONTR.ref(),POZ.KON,POZ.KON);
_zwrot:=TABCONTR.first();
TABCONTR.cntx_pop();
_zwrot


\spr_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Sprawdza jednostke ksiegowa
::   WE: _a - skrocona nazwa sprawdzanej jednostki ksiegowej
::   WY: 1/0 - pasuje/nie paduje jednostka ksiegowa
::  OLD: \spr_odd/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
ODD_OGR.cntx_psh(); ODD_OGR.prefix();
_zwrot:=ODD_OGR.find_key(_a,_a);
ODD_OGR.cntx_pop();
_zwrot


\zap_nap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Tabelka z zapisami - napelnianie
::  OLD: \zap_nap/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_odd:=OPERATOR.DEPT();
{? odd_poz<>0 || exec('szuk_odd','!fks_ksr_zbwa') ?};
POZ.cntx_psh(); DOK.cntx_psh();
OKRO_F.cntx_psh();
OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
_nr:=SSTALE.AO().NR;
{? ((FUN_SEL.LP=1 | FUN_SEL.LP=3) & OKRO_F.first()) |
   (FUN_SEL.LP=2 & OKRO_F.first() & OKRO_F.next()) |
   FUN_SEL.LP=4
|| {! |?
      POZ.use('pozy'+SSTALE.AR().KOD+form(OKRO_F.NR,-2));
      DOK.use('doku'+SSTALE.AR().KOD+form(OKRO_F.NR,-2)); DOK.prefix();
      {? OPERATOR.DEPT
      || {? SSTALE.WAL<>FINFO.NAROD
         || POZ.index('OKWAL_O'); POZ.prefix('T','T',SSTALE.WAL,OPERATOR.DEPT,TABCONTR.WARTTREE)
         || POZ.index('OKWAL_O1'); POZ.prefix('T','T',OPERATOR.DEPT,TABCONTR.WARTTREE)
         ?}
      || {? SSTALE.WAL<>FINFO.NAROD
         || POZ.index('OKWAL'); POZ.prefix('T','T',SSTALE.WAL,TABCONTR.WARTTREE)
         || POZ.index('OKWAL1'); POZ.prefix('T','T',TABCONTR.WARTTREE)
         ?}
      ?};
      _bo:=0;
      {? OKRO_F.NR=0
      || ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); OKRO_F.ROK();
         {? ~exec('first_rok_obsz','zam_okr_rok','FKS',ROK_F.ref()) & ROK_F.prev() || _bo:=1 ?};
         ROK_F.cntx_pop()
      ?};
      {? POZ.first()
      || {! |?
            POZ.DOK().ODD();
            {? (poziomy[TABCONTR.POZIOM][1]='konto analityczne' |
                poziomy[TABCONTR.POZIOM][1]='konto syntetyczne' & exec('spr_pod','!fks_ksr_zbwa')) &
               (odd_poz=0 | (odd_poz<>0 & exec('spr_odd','!fks_ksr_zbwa',ODD.OD)))
            || ZAP_SEL.ODD:=ODD.OD;
               ZAP_SEL.REJ:=DOK.REJ().KOD;
               ZAP_SEL.NR:=DOK.NR;
               ZAP_SEL.POZ:=POZ.POZ;
               ZAP_SEL.DOP:=DOK.DOP;
               ZAP_SEL.DTW:=DOK.DTW;
               ZAP_SEL.KON:=POZ.KON;
               ZAP_SEL.OP:=POZ.OP;
               ZAP_SEL.DOK:=DOK.NK;
               ZAP_SEL.BO:=_bo;
               ZAP_SEL.WN:=ZAP_SEL.MA:=0;
               {? SSTALE.WAL=FINFO.NAROD
               || {? -POZ.STR='wn' || ZAP_SEL.WN:=POZ.SUM || ZAP_SEL.MA:=POZ.SUM ?}
               || {? -POZ.STR='wn' || ZAP_SEL.WN:=POZ.SUMW || ZAP_SEL.MA:=POZ.SUMW ?}
               ?};
               ZAP_SEL.POZ_REF:=$POZ.ref();
               ZAP_SEL.add()
            ?};
            POZ.next()
         !}
      ?};
      FUN_SEL.LP<>1 & FUN_SEL.LP<>4 & OKRO_F.next() & OKRO_F.NR<=_nr
   !}
?};
OKRO_F.cntx_pop(); POZ.cntx_pop(); DOK.cntx_pop();
VAR_DEL.delete('ODD_OGR')


\zap_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Tabelka z zapisami - tworzenie i napelnianie
::  OLD: \zap_sel/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
exec('zap_tab','!fks_ksr_zbwa');
ZAP_SEL.index(zapsind1); ZAP_SEL.prefix();
dwie_wal:=0; zmzapsel:=0;
{! |?
   zmzapsel:=0;
   ZAP_SEL.erase();
   exec('zap_nap','!fks_ksr_zbwa');
   ZAP_SEL.hdr_sel(' na koncie: %1 , Zakres zapisów: %2'@[TABCONTR.WARTTREE,FUN_SEL.NAZ]);
   ZAP_SEL.select(,,,'R:R');
   zmzapsel
!};
VAR_DEL.delete('dwie_wal','zmzapsel');
1


\fun_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Tabelka z funkcjami do przegladania zapisow
::   WE: [_a] - ustawiac zakres
::  OLD: \fun_sel/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('FUN_SEL')<=0
|| FUN_SEL:=tab_tmp(1,
      'LP','INTEGER','Lp.',
      'NAZ','STRING[40]',''
   );
   _index:=FUN_SEL.ndx_tmp('',1,'LP',,0,'NAZ',,0,'NAZ',,0);
   FUN_SEL.index(_index); FUN_SEL.prefix();
   _lp:=1;
   FUN_SEL.LP:=1; FUN_SEL.NAZ:='Bilans otwarcia'; FUN_SEL.add();
   FUN_SEL.LP:=2; FUN_SEL.NAZ:='Do bieżącego okresu bez bilansu otwarcia'@; FUN_SEL.add();
   FUN_SEL.LP:=4; FUN_SEL.NAZ:='Bieżący okres'@; FUN_SEL.add();
   FUN_SEL.LP:=3; FUN_SEL.NAZ:='Do bieżącego okresu z bilansem otwarcia'@; FUN_SEL.add();
   _sel:=FUN_SEL.mk_sel('Zakres zapisów'@,,0,'tab_fun_sel',,,FUN_SEL.size());
   FUN_SEL.win_fld(_sel,,'NAZ');
   FUN_SEL.win_act(_sel,0,'Formuła','Wybierz'@@,,'Wybór bieżącej pozycji'@,"sel_exit()",,1,,,,'W');
   FUN_SEL.win_act(_sel,0,'Szukaj');
   FUN_SEL.win_sel(_sel)
?};
{? var_press('FunSel')<=0 | var_press('_a')>0
|| {? SSTALE.AO().NR=0
   || FUN_SEL.first(); 1
   || _ref:=null;
      FUN_SEL.cntx_psh();
      {? FUN_SEL.select(,1,FUN_SEL.size())
      || FunSel:=1;
         _ref:=FUN_SEL.ref()
      ?};
      FUN_SEL.cntx_pop();
      {? _ref
      || FUN_SEL.seek(_ref)
      ?}
   ?}
|| 1
?}


\add_wid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Dodaje rekordy do tabeli z widocznymi pozycjami
::  OLD: \add_wid/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
TAB_WID.WART:=TABCONTR.WART;
TAB_WID.POZIOM:=TABCONTR.POZIOM;
TAB_WID.KLUCZ:=TABCONTR.KLUCZ;
{! _i:=1..8
|! ($('TAB_WID.KOL'+$_i+':=TABCONTR.KOL'+$_i))()
!};
TAB_WID.add()


\rekuren
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Rekurencyjnie wrzuca do tabelki wynikowej widoczne pozycje
::   WE: _a - ref rekordu nadrzednego
::  OLD: \rekuren/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
TABCONTR.cntx_psh();
TABCONTR.index(indcont2); TABCONTR.prefix(_a);
{? TABCONTR.first()
|| {! |?
      {? TABCONTR.tr_state()=1 || exec('rekuren','!fks_ksr_zbwa',TABCONTR.ref()) ?};
      exec('add_wid','!fks_ksr_zbwa');
      TABCONTR.next()
   !}
|| _zwrot:=0
?};
TABCONTR.cntx_pop();
_zwrot


\widoczne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Wrzuca do tabelki tymczasowej rozwiniete elementy i drukuje je
::  OLD: \widoczne/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
TAB_WID:=tab_tmp(1,'KLUCZ','STRING[60]','Klucz'@,
                   'WART','STRING[50]','',
                   'KOL1','REAL','Kolumna 1'@,
                   'KOL2','REAL','Kolumna 2'@,
                   'KOL3','REAL','Kolumna 3'@,
                   'KOL4','REAL','Kolumna 4'@,
                   'KOL5','REAL','Kolumna 5'@,
                   'KOL6','REAL','Kolumna 6'@,
                   'KOL7','REAL','Kolumna 7'@,
                   'KOL8','REAL','Kolumna 8'@,
                   'POZIOM','INTEGER','Poziom'@
                );
TABCONTR.cntx_psh();
TABCONTR.index(indcont2); TABCONTR.prefix(null);
{? TABCONTR.first()
|| {! |?
      {? TABCONTR.tr_state()=1 || exec('rekuren','!fks_ksr_zbwa',TABCONTR.ref()) ?};
      exec('add_wid','!fks_ksr_zbwa');
      TABCONTR.next()
   !}
?};
TABCONTR.cntx_pop();
exec('rep_exec','#b_report','FKS_KSR_ZBWA','fks_spr_grp',,,,,,,,'W');
VAR_DEL.delete('TAB_WID');
1


\wyzn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Ustawia naglowki kolumn z wartosciami w okienku i na wydruku
::  OLD: \wyzn/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_K.WYZNACZN='Różnica stron' || 'Różnica stron'
|? SPR_K.WYZNACZN='Bilans otwarcia' || 'BO'
|? SPR_K.WYZNACZN='Miesiąc' || 'Miesiąc'
|? SPR_K.WYZNACZN='Obroty' || 'Obroty'
|| SPR_K.WYZNACZN
?}


\ini_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Funkcja tworzy tabelke tymczasowa z wartosciami
::  OLD: \ini_tmp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
TABCONTR:=tab_tmp(1,'TREE','TREE_REF','',
                    'KLUCZ','STRING[60]','Klucz'@,
                    'WID','INTEGER','Widoczna pozycja'@,
                    'POZIOM','INTEGER','Poziom'@,
                    'WART','STRING[50]','',
                    'WARTTREE','STRING[35]','',
                    'KOL1','REAL','Kolumna 1'@,
                    'KOL2','REAL','Kolumna 2'@,
                    'KOL3','REAL','Kolumna 3'@,
                    'KOL4','REAL','Kolumna 4'@,
                    'KOL5','REAL','Kolumna 5'@,
                    'KOL6','REAL','Kolumna 6'@,
                    'KOL7','REAL','Kolumna 7'@,
                    'KOL8','REAL','Kolumna 8@',
                    'POZIOM1','STRING[35]','Poziom 1'@,
                    'POZIOM2','STRING[35]','Poziom 2'@,
                    'POZIOM3','STRING[35]','Poziom 3'@,
                    'POZIOM4','STRING[35]','Poziom 4'@,
                    'POZIOM5','STRING[35]','Poziom 5'@,
                    'POZIOM6','STRING[35]','Poziom 6'@,
                    'OPIS','STRING[255]','Opis'@
                 );
indcont1:=TABCONTR.ndx_tmp('',1,'KLUCZ',,0,'KLUCZ',,0);
indcont2:=TABCONTR.index('?');
indcont3:=TABCONTR.ndx_tmp('',1,'TREE',,0,'WARTTREE',,0,'WARTTREE',,0);
contrsel:=TABCONTR.mk_sel('Sprawozdanie'@,'N',0,'tabcontr_wer1',,,,1);
TABCONTR.win_fld(contrsel,,'WARTTREE',,,15);
TABCONTR.win_fld(contrsel,,'OPIS',,,21);
TABCONTR.win_act(contrsel,,'Rekord',,,,"exec(\'hid_act\',\'!fks_ksr_zbwa\'); 0");
TABCONTR.win_act(contrsel,,'Formuła','Zwiń/&rozwiń całość'@@,,
                 'Zwijanie/rozwijanie całości'@,"exec('zwrw_all','#tree','TABCONTR','TREE')",,1,,,,'R');
TABCONTR.win_act(contrsel,,'Formuła','O&blicz obroty'@@,,'Obroty zaznaczonych pozycji'@,,
                 "exec('obl_obr','!fks_ksr_zbwa')",,1,"exec('be_gobr','!fks_ksr_zbwa')",
                 "exec('ae_gobr','rozrach_kor')" ,'B');
{? var_pres('__KURS')<=0
|| TABCONTR.win_act(contrsel,,'Formuła','Zapisy'@@,,'Zapisy na koncie'@,"exec('zapisy','!fks_ksr_zbwa')",,,,,,'Z');
   TABCONTR.win_act(contrsel,,'Formuła','Z&mień zakres zapisów'@@,,'Zmienia zakres dla zapisów'@,"exec('fun_sel','!fks_ksr_zbwa',1)",,,,,,'M');
   {? SSTALE.AO().NR=0
   || TABCONTR.actions_grayed(contrsel,'M')
   ?}
?};
TABCONTR.win_act(contrsel,,'Formuła','Druku&j'@@,,'Wydruk bieżącej zawartości ekranu'@,
                 "exec('widoczne','!fks_ksr_zbwa')",,,,,,'J');
TABCONTR.win_act(contrsel,,'Szukaj');
TABCONTR.win_fml(contrsel,,'WARTTREE',,'ICON_BEFORE',
                 "TABCONTR.cntx_psh(); TABCONTR.index(indcont2); TABCONTR.prefix(TABCONTR.ref());
                  _zwrot:={? TABCONTR.first()
                          || TABCONTR.cntx_pop();
                             {? TABCONTR.tr_state()=1
                             || 'xwin16.png:75'
                             || 'xwin16.png:74'
                             ?}
                          || TABCONTR.cntx_pop(); 'xwin16.png:76'
                          ?}; _zwrot");
SPR_K.cntx_psh(); SPR_K.index('KOLUMNA');
szer_kol:={? l_kol>=1 & l_kol<=4 || 16
          |? l_kol=5 || 14
          |? l_kol=6 || 13
          |? l_kol=7 || 11
          || 9
          ?};
{! _i:=1..l_kol
|! SPR_K.prefix(SPR_NAG.ref(),'T',_i);
   {? SPR_K.first()
   || SPR_K.WYZNACZN:=|SPR_K.WYZNACZN;
      _wyzn:=exec('wyzn','!fks_ksr_zbwa');
      _opiskol:={? _wyzn<>'' || '\''+_wyzn+' '+SPR_K.STR || '\'Kolumna %1'@[$_i] ?};
      ($('TABCONTR.win_fld(contrsel,,\'KOL'+$_i+'\',,,szer_kol,2,,'+_opiskol+'\')'))()
   ?}
!};
SPR_K.cntx_pop();
VAR_DEL.delete('szer_kol');
TABCONTR.win_sel(contrsel)


\spr_slu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Funkcja sprawdza, czy w konto analityczne spelnia parametry klucza
::       zdefiniowanego przez uzytkownika (sa wszystkie slowniki i kazda pozycja
::       w slowniku jest zgodna z parametrami uzytkownika)
::  OLD: \spr_slu/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? il_poz
|| SLO.cntx_psh(); SLO.prefix();
   AN_SLU.cntx_psh();
   AN_SLU.index('AN_SLU');
   {! _i:=1..il_poz
   |? _ok=1
   |! {? poziomy[_i][1]='slownik'
      || AN_SLU.prefix(AN.ref(),poziomy[_i][2]);
         WYDRUKIN.DLKON:=(+poziomy[_i][5]); WYDRUKI.MASKA:=poziomy[_i][5];
         {? AN_SLU.first()
         || {! |?
               _kod:=AN_SLU.SLO().KOD;
               _ok:=((poziomy[_i][3]='' | (poziomy[_i][3]<>'' & _kod>=poziomy[_i][3])) &
                     (poziomy[_i][4]='' | (poziomy[_i][4]<>'' & _kod<=poziomy[_i][4])) &
                     (|poziomy[_i][5]='' | (|poziomy[_i][5]<>'' & exec('mask','konto',_kod))));
               _ok=1 & AN_SLU.next()
            !}
         || _ok:=0
         ?}
      ?}
   !};
   SLU.cntx_psh(); SLU.prefix();
   {? _ok
   || TABCOGR.cntx_psh(); TABCOGR.index(indcogr1); TABCOGR.prefix('slownik');
      {? TABCOGR.first()
      || {! |?
           {? SLU.seek(TABCOGR.REFSL,SLU.name())
           || AN_SLU.prefix(AN.ref(),SLU.ref());
              WYDRUKIN.DLKON:=(+TABCOGR.MASKA); WYDRUKI.MASKA:=TABCOGR.MASKA;
              {? AN_SLU.first()
              || {! |?
                    _kod:=AN_SLU.SLO().KOD;
                    _ok:=((TABCOGR.OD='' | (TABCOGR.OD<>'' & _kod>=TABCOGR.OD)) &
                          (TABCOGR.DO='' | (TABCOGR.DO<>'' & _kod<=TABCOGR.DO)) &
                          (|TABCOGR.MASKA='' | (|TABCOGR.MASKA<>'' & exec('mask','konto',_kod))));
                    _ok=1 & AN_SLU.next()
                 !}
              |? TABCOGR.SUMA='F'
              || _ok:=0
              ?}
           ?};
            _ok=1 & TABCOGR.next()
         !}
      ?};
      TABCOGR.cntx_pop()
   ?};
   SLU.cntx_pop(); SLO.cntx_pop(); AN_SLU.cntx_pop()
?};
_ok


\wartosc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Obliczanie wartosci dla kolumny
::       _a - typ obliczanej wartosci, _b - strona
::  OLD: \wartosc/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh(); OKRO_F.prefix();
_okres:=SSTALE.AO().NR;
OKRO_F.cntx_pop();
{? _a=''
|| 0
|? _a='Bilans otwarcia'
|| {? -_b='wn' || F.Wn(0,0) || F.Ma(0,0) ?}
|? _a='Saldo'
|| {? -_b='wn' || F.Pw(_okres) || F.Pm(_okres) ?}
|? _a='Miesiąc'
|| {? -_b='wn' || F.Wn(_okres,_okres) || F.Ma(_okres,_okres) ?}
|? _a='Obroty'
|| {? -_b='wn' || F.Wn(0,_okres) || F.Ma(0,_okres) ?}
|? _a='Różnica stron'
|| {? -_b='wn' || F.Wn(0,_okres)-F.Ma(0,_okres) || F.Ma(0,_okres)-F.Wn(0,_okres) ?}
|| 0
?}


\opis_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Funkcja ustawia opisy poziomow.
::  OLD: \opis_poz/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_wart:={? var_pres('TAB_WID')>0
       || (2-(|TAB_WID.WART))
       || |TABCONTR.WARTTREE
       ?};
_poziom:={? var_pres('TAB_WID')>0 || TAB_WID.POZIOM || TABCONTR.POZIOM ?};
_zwrot:='';
{? poziomy[_poziom][1]='jednostka księgowa'
|| ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.find_key(_wart,_wart) || _zwrot:=ODD.N ?};
   ODD.cntx_pop()
|? poziomy[_poziom][1]='slownik'
|| SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
   {? SLU.seek(poziomy[_poziom][2])
   || SLO.cntx_psh(); SLO.index('SL'); SLO.prefix();
      {? SLO.find_key(SLU.ref(),_wart) || _zwrot:=SLO.TR ?};
      SLO.cntx_pop()
   ?};
   SLU.cntx_pop()
|? poziomy[_poziom][1]='konto analityczne'
|| KS.cntx_psh(); KS.index('SYM'); KS.prefix();
   {? KS.find_key(SSTALE.AR,SSTALE.AR().SYNT+_wart)
   || exec('opis','konto',_wart);
      opis_poz:='';
      exec('ka_opis','konto');
      _zwrot:=opis_poz; &opis_poz
   ?};
   KS.cntx_pop()
|? poziomy[_poziom][1]='konto syntetyczne'
|| KS.cntx_psh(); KS.index('SYM'); KS.prefix();
   {? KS.find_key(SSTALE.AR,SSTALE.AR().SYNT+_wart) || _zwrot:=KS.NAZ ?};
   KS.cntx_pop()
?};
_zwrot


\add_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Dodawanie rekordow do tabeli tymczasowej - wartosci z kluczami
::   WE: _a - klucz
::       _b - czy pozycja widoczna
::       _c - poziom
::       _d - opis wiersza
::       _e - ref nadrzednego zapisu
::   WY: ref dodanego lub zmodyfikowanego rekordu
::  OLD: \add_tmp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
TABCONTR.cntx_psh();
TABCONTR.index(indcont1); TABCONTR.prefix(_a,_a);
{? TABCONTR.first()
|| {! _i:=1..l_kol
   |! ($('TABCONTR.KOL'+$_i+'+=wartosc['+$_i+']'))()
   !};
   {? TABCONTR.put() || _ref:=TABCONTR.ref() ?}
|| TABCONTR.KLUCZ:=_a;
   {! _i:=1..l_kol
   |! ($('TABCONTR.KOL'+$_i+':=wartosc['+$_i+']'))()
   !};
   {! _i:=1..il_poz
   |! ($('TABCONTR.POZIOM'+$_i+':=war_poz['+$_c+']['+$_i+']'))()
   !};
   TABCONTR.WID:=_b;
   TABCONTR.POZIOM:=_c;
   TABCONTR.TREE:=_e;
   TABCONTR.WARTTREE:=(2-(|_d));
   TABCONTR.WART:=_d;
   TABCONTR.OPIS:=exec('opis_poz','!fks_ksr_zbwa');
   {? TABCONTR.add() || _ref:=TABCONTR.ref() ?}
?};
TABCONTR.cntx_pop();
_ref


\bud_key
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Tworzenie rekordow w tabelce tymczasowej wg klucza
::   WE: _a - kurs waluty (mnoznik wartosci)
::  OLD: \bud_key/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
{? OPERATOR.DEPT
|| _first:=1; OPERATOR.DEPT()
|| _first:=ODD.first()
?}; _jestodd:=0;
{? _first
|| klucz:=obj_new(il_poz); war_poz:=obj_new(il_poz); opis:=obj_new(il_poz);
   wartosc:=obj_new(l_kol); {! _i:=1..l_kol |! wartosc[_i]:=0 !};
   {! _i:=1..il_poz
   |! war_poz[_i]:=obj_new(il_poz);
      klucz[_i]:=opis[_i]:='';
      {! _j:=1..il_poz |! war_poz[_i][_j]:='' !}
   !}; _czy_war:=0;
   ROK_F.cntx_psh(); _dsynt:=SSTALE.AR().SYNT; ROK_F.cntx_pop();
   AN_SLU.cntx_psh();
   AN_SLU.index('AN_SLU'); AN_SLU.prefix(AN.ref());
   SLO.cntx_psh(); SLO.prefix(); _first:=0;
   {! |?
     WYDRUKI.MASKA:=mask_odd; WYDRUKIN.DLKON:=(+mask_odd);
     _okodd:=(od_odd='' | (od_odd<>'' & ODD.OD>=od_odd)) &
             (do_odd='' | (do_odd<>'' & ODD.OD<=do_odd)) &
             (|mask_odd='' | (|mask_odd<>'' & exec('mask','konto',ODD.OD)));
     F.set_odd(ODD.ref());
     {? _okodd
     || {! _i:=1..il_poz
        |! {? czy_odd=1 || {! _k:=1..l_kol |! wartosc[_k]:=0 !} ?};
           {? czy_odd=1 | (czy_odd<>1 & _first=0) || klucz[_i]:='' ?}; opis[_i]:='';
           {! _j:=1..il_poz |! war_poz[_i][_j]:='' !}
        !};
        {! _i:=1..il_poz
        |! _co:={? poziomy[_i][1]='slownik' & AN_SLU.find_key(poziomy[_i][2])
                || AN_SLU.SLO().KOD
                |? poziomy[_i][1]='konto analityczne'
                || AN.SYM
                |? poziomy[_i][1]='konto syntetyczne'
                || _dsynt+AN.SYM
                || ODD.OD
                ?};
           {! _j:=1..il_poz
           |! {? _j>=_i
              || war_poz[_j][_i]:=_co;
                 {? czy_odd=1 | (czy_odd<>1 & _first=0) || klucz[_j]+=_co ?};
                 {? _j=_i
                 || opis[_i]:=(_i-1)*'  '+{? _i=il_poz || '- ' || '+ ' ?}+_co
                 ?}
              ?}
           !}
        !};
        SPR_K.cntx_psh();
        SPR_K.index('KOLUMNA'); SPR_K.prefix(SPR_NAG.ref(),'T');
        {? SPR_K.first()
        || _i:=1;
           {? czy_odd=1 || _czy_war:=0 ?};
           {! |?
              _suma:={? SPR_K.WYZNACZN<>''
                     || exec('wartosc','!fks_ksr_zbwa',SPR_K.WYZNACZN,SPR_K.STR)
                     |? SPR_K.FORMULA<>''
                     || ($SPR_K.FORMULA)()
                     || 0
                     ?};
              _suma:=(_suma/_a);
              {? czy_odd=1 || wartosc[_i]:=_suma || wartosc[_i]+=_suma ?};
              {? _suma$2<>0 || _czy_war:=1 ?}; _i+=1;
              SPR_K.next()
           !};
           {? czy_odd=1 & _czy_war
           || _ref:=null;
              {! _i:=1..il_poz
              |! _wid:=(_i=1);
                 _ref:=exec('add_tmp','!fks_ksr_zbwa',klucz[_i],_wid,_i,opis[_i],_ref)
              !}
           ?}
        ?};
        SPR_K.cntx_pop()
     ?};
     OPERATOR.DEPT=null & ODD.next()
   !};
   {? czy_odd<>1 & _czy_war
   || _ref:=null;
      {! _i:=1..il_poz
      |! _wid:=(_i=1);
         _ref:=exec('add_tmp','!fks_ksr_zbwa',klucz[_i],_wid,_i,opis[_i],_ref)
      !}
   ?};
   obj_del(wartosc); obj_del(war_poz); obj_del(klucz); obj_del(opis);
   SLO.cntx_pop(); AN_SLU.cntx_pop()
?};
ODD.cntx_pop()


\petl_an
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Petla po tabeli AN - zbieranie danych
::   WE: _a - kurs waluty (mnoznik wartosci)
::  OLD: \petl_an/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
AN.cntx_psh();
AN.index('WALSYM'); AN.prefix(SSTALE.WAL);
{? AN.first()
|| {? czy_odd<>0
   || ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
      {? OPERATOR.DEPT
      || _first:=1; OPERATOR.DEPT()
      || _first:=ODD.first()
      ?}; _jestodd:=0;
      {? _first
      || {! |?
            WYDRUKI.MASKA:=mask_odd; WYDRUKIN.DLKON:=(+mask_odd);
            _jestodd:=(od_odd='' | (od_odd<>'' & ODD.OD>=od_odd)) &
                      (do_odd='' | (do_odd<>'' & ODD.OD<=do_odd)) &
                      (|mask_odd='' | (|mask_odd<>'' & exec('mask','konto',ODD.OD)));
            OPERATOR.DEPT=null & _jestodd=0 & ODD.next()
         !}
      ?};
      {? _jestodd=0 || brak_dan:=1 ?};
      ODD.cntx_pop()
   ?};
   {? brak_dan=0
   || _maska:=_maska1:=_od:=_od1:=_do:=_do1:='';
      SPR_K.cntx_psh(); SPR_K.prefix(); SPR_K.blank(1);
      SPR_K.SPR_NAG:=SPR_NAG.ref(); SPR_K.KOLUMNA:='N'; SPR_K.WYZNACZN:='Konto analityczne'; SPR_K.CZY_SLU:=0;
      {? SPR_K.find_rec()
      || _maska:={? SPR_K.MASKA=(35*'?') || '' || SPR_K.MASKA ?}; _od:=SPR_K.OD; _do:=SPR_K.DO
      ?};
      SPR_K.blank(1);
      SPR_K.SPR_NAG:=SPR_NAG.ref(); SPR_K.KOLUMNA:='N'; SPR_K.WYZNACZN:='Konto syntetyczne'; SPR_K.CZY_SLU:=0;
      {? SPR_K.find_rec()
      || _maska1:={? SPR_K.MASKA=(6*'?') || '' || SPR_K.MASKA ?}; _od1:=SPR_K.OD; _do1:=SPR_K.DO
      ?};
      SPR_K.cntx_pop();
      echo();
      {! |?
         echo('Trwa wyliczanie wartości z kont....'@,AN.SYM);
         WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=_maska;
         _ok:=(_maska='' | (_maska<>'' & exec('mask','konto',AN.SYM))) &
              (_od='' | (_od<>'' & (SSTALE.AR().SYNT+AN.SYM)>=_od)) & (_do='' | (_do<>'' & (SSTALE.AR().SYNT+AN.SYM)<=_do));
         {? _ok
         || WYDRUKIN.DLKON:=SSTALE.AR().SYNT; WYDRUKI.MASKA:=_maska1;
            _ok:=(_maska1='' | (_maska1<>'' & exec('mask','konto',AN.SYM))) &
                 (_od1='' | (_od1<>'' & (SSTALE.AR().SYNT+AN.SYM)>=_od1)) & (_do1='' | (_do1<>'' & (SSTALE.AR().SYNT+AN.SYM)<=_do1));
            {? _ok
            || TABCOGR.cntx_psh(); TABCOGR.index(indcogr1); TABCOGR.prefix('konto analityczne');
               {? TABCOGR.first()
               || WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=TABCOGR.MASKA;
                  _ok:=(TABCOGR.MASKA='' | (TABCOGR.MASKA<>'' & exec('mask','konto',AN.SYM))) &
                       (TABCOGR.OD='' | (TABCOGR.OD<>'' & (SSTALE.AR().SYNT+AN.SYM)>=TABCOGR.OD)) &
                       (TABCOGR.DO='' | (TABCOGR.DO<>'' & (SSTALE.AR().SYNT+AN.SYM)<=TABCOGR.DO))
               ?};
               TABCOGR.cntx_pop()
            ?};
            {? _ok
            || TABCOGR.cntx_psh(); TABCOGR.index(indcogr1); TABCOGR.prefix('konto syntetyczne');
               {? TABCOGR.first()
               || WYDRUKIN.DLKON:=SSTALE.AR().SYNT; WYDRUKI.MASKA:=TABCOGR.MASKA;
                  _ok:=(TABCOGR.MASKA='' | (TABCOGR.MASKA<>'' & exec('mask','konto',AN.SYM))) &
                       (TABCOGR.OD='' | (TABCOGR.OD<>'' & (SSTALE.AR().SYNT+AN.SYM)>=TABCOGR.OD)) &
                       (TABCOGR.DO='' | (TABCOGR.DO<>'' & (SSTALE.AR().SYNT+AN.SYM)<=TABCOGR.DO))
               ?};
               TABCOGR.cntx_pop()
            ?};
            {? _ok & exec('spr_slu','!fks_ksr_zbwa')
            || exec('bud_key','!fks_ksr_zbwa',_a)
            ?}
         ?};
         AN.next()
      !};
      echo();
      {? TABCONTR.prefix(); ~TABCONTR.first() || brak_dan:=1 ?}
   ?};
   F.set_odd(OPERATOR.DEPT)
|| brak_dan:=1
?};
AN.cntx_pop()


\del_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Funkcja kasuje tabelke tymczasowa z wartosciami
::  OLD: \del_tmp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TABCONTR','indcont1','indcont2','indcont3','contrsel')


\del_ogr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Kasuje tabelke tymczasowa z ograniczeniami nie bedacymi
::       poziomami grupowania
::  OLD: \del_ogr/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TABCOGR','indcogr1')


\wyk_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Wykonanie sprawozdania
::  OLD: \wyk_sel/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? SPR_NAG.r_lock(0,1)
|| SPR_K.index('KOLUMNA'); SPR_K.prefix(SPR_NAG.ref(),'N');
   _spr_k_n:=SPR_K.first(); _ok:=1;
   {? _spr_k_n
   || _czy_gr:=0;
      {! |?
         _czy_gr:=(-SPR_K.SUMA='t');
         _czy_gr=0 & SPR_K.next()
      !};
      {? _czy_gr=0
      || FUN.info('Nie wprowadzono żadnego poziomu grupowania.'@); _ok:=0
      ?}
   || FUN.info('Nie zdefiniowano filtrów i poziomów grupowania.'@); _ok:=0
   ?};
   {? _ok
   || zap_syn:=odd_poz:=odd_kan:=odd_ksn:=0;
      SPR_K.index('KOLUMNA1'); SPR_K.prefix(SPR_NAG.ref(),0,'N','Konto syntetyczne','Konto syntetyczne');
      {? SPR_K.first()
      || odd_ksn:=SPR_K.LP;
         SPR_K.index('WYDRUK'); SPR_K.prefix(SPR_NAG.ref(),'N',0,SPR_K.LP+1);
         {? SPR_K.first() & SPR_K.WYZNACZN='Konto analityczne' || zap_syn:=1 ?}
      ?};
      SPR_K.index('KOLUMNA1'); SPR_K.prefix(SPR_NAG.ref(),0,'N','Konto analityczne','Konto analityczne');
      {? SPR_K.first()
      || odd_kan:=SPR_K.LP;
         SPR_K.index('KOLUMNA'); SPR_K.prefix(SPR_NAG.ref(),'N');
         {? SPR_K.first()
         || {! |?
               {? SPR_K.CZY_SLU=0 & -SPR_K.WYZNACZN='jednostka księgowa'
               || odd_poz:=SPR_K.LP
               ?};
               odd_poz=0 & SPR_K.next()
            !}
         ?}
      ?};
      SPR_K.index('KOLUMNA'); SPR_K.prefix(SPR_NAG.ref(),'T'); l_kol:=SPR_K.size();
      czy_odd:=brak_dan:=0; mask_odd:=od_odd:=do_odd:='';
      {? l_kol
      || exec('tab_ogr','!fks_ksr_zbwa');
         exec('poziomy','!fks_ksr_zbwa');
         exec('ini_tmp','!fks_ksr_zbwa');
         _is_kurs:=var_pres('__KURS')>0;
         _kurs:={? _is_kurs || exec('kurs','konto',UNPAR.PSLO().KOD,UNPAR.P25) || 1 ?};
         exec('petl_an','!fks_ksr_zbwa',_kurs);
         {? brak_dan
         || FUN.info('Brak danych spełniających zadane kryteria.'@)
         || TABCONTR.index(indcont2); TABCONTR.prefix();
             {? _is_kurs
             ||  TABCONTR.hdr_sel(': %1 , Waluta: %2 , Kurs: %3'@[SPR_NAG.NAZWA,UNPAR.PSLO().KOD,form(UNPAR.P25,,6,'9,')])
             ||  TABCONTR.hdr_sel(': %1 '@[SPR_NAG.NAZWA])
            ?};
            TABCONTR.select();
            VAR_DEL.delete('FUN_SEL','ZAP_SEL','zapsind1','FunSel')
         ?};
         exec('del_tmp','!fks_ksr_zbwa');
         exec('del_ogr','!fks_ksr_zbwa');
         VAR_DEL.delete('poziomy','il_poz','klucz','war_poz','wartosc','opis')
      || FUN.info('Nie zdefiniowano kolumn w sprawozdaniu.'@)
      ?};
      VAR_DEL.delete('l_kol','czy_odd','brak_dan','mask_odd','od_odd','do_odd','zap_syn','odd_poz','odd_kan','odd_ksn')
   ?};
   SPR_NAG.r_unlock()
|| FUN.info('Sprawozdanie jest obsługiwane przez innego użytkownika.'@)
?}


\wyk_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2008]
:: OPIS: Wykonanie sprawozdania w walucie
::  OLD: \wyk_wal/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.win_edit('SPR_GRP');
exec('ini_wal','konto');
{? ROZNE.edit("__CHK.record3(UNPAR,'PSLO','Waluta',UNPAR,'P25','Kurs')")
|| __KURS:=1;
   exec('wyk_sel','!fks_ksr_zbwa');
   &__KURS
?}


\contgdsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Formula na wypelnienie - tabele SPR_K, okienko GRUP
::  OLD: \contgdsp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
grp_disp(SPR_K,'WER_KOL')


\spr_ksel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Przed obsluga - kolumny i grupowanie
::   WE: _a - 'T' - kolumna, 'N' - klucz (grupowanie)
::  OLD: \spr_ksel/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
BLKOL:=_a;
SPR_K.index('KOLUMNA');
SPR_K.prefix(SPR_NAG.ref(),_a);
1


\spr_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Definicje zestawien - 'przed obsluga' - grupowanie
::  OLD: \spr_grp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
exec('spr_ksel','!fks_ksr_zbwa','N')


\spr_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Definicje zestawien - 'przed obsluga' - kolumny
::  OLD: \spr_kol/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
exec('spr_ksel','!fks_ksr_zbwa','T')


\dsp_wyzn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Na wyswietl dla pola KOSZTY.WYZNACZN
::  OLD: \dsp_wyzn/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
SLUAPPL.cntx_psh();
SLUAPPL.prefix();
SLU.cntx_psh();
SLU.prefix();
_co:={? SPR_K.CZY_SLU=0 || SPR_K.WYZNACZN || SPR_K.SLU().SLU().NAZ ?};
SLUAPPL.cntx_pop();
SLU.cntx_pop();
KOSZTY.WYZNACZN:=_co


\dsptypsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Na wyswietl dla pola KOSZTY.POZIOM
::  OLD: \dsptypsp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.POZIOM:={? SPR_K.SUMA='T' || 'grupa'
               |? SPR_K.SUMA='N' || 'warunek'
               || 'filtr'
               ?}; 1


\slu_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Rekord przed tabela SPR_K
::  OLD: \slu_dsp/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? -SPR_K.SUMA='t' || 'SPR_K#01#01' || '' ?}


\hid_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Ukrywa akcje
::  OLD: \hid_act/spr_grp.fml
::----------------------------------------------------------------------------------------------------------------------
{? (poziomy[TABCONTR.POZIOM][1]='konto analityczne' |
    (poziomy[TABCONTR.POZIOM][1]='konto syntetyczne' & zap_syn))
|| TABCONTR.actions(TABCONTR.win_sel('?'),'',,1)
|| TABCONTR.actions(TABCONTR.win_sel('?'),'Z',,1)
?}


\def_en
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawienie dostępności pól w definicji kolumn sprawozdania
::----------------------------------------------------------------------------------------------------------------------
SPR_K.efld_opt(SPR_K.win_edit('?'),'enable='+$(UNPAR.P10='W'),SPR_K,'WYZNACZN');
SPR_K.efld_opt(SPR_K.win_edit('?'),'mark='+$(UNPAR.P10='W'),SPR_K,'WYZNACZN');
SPR_K.efld_opt(SPR_K.win_edit('?'),'enable='+$(UNPAR.P10='F'),SPR_K,'FORMULA');
SPR_K.efld_opt(SPR_K.win_edit('?'),'mark='+$(UNPAR.P10='F'),SPR_K,'FORMULA');
~~


\sprk_move
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: Akcja Przesuń
::   WE: _a - STRING - kierunek przesuwania: 'U','D','N'
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dir:='N';
{? var_pres('_a')=type_of('')
|| _dir:=_a
?};
exec('zmien_lpa','#dragdrop','LP','KOLUMNA',,,_dir);
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 b24beecf5d67410a95de8ac488dc65a1a34f054e7f73d9a672060634daf82e33032de21e1cd37204fb2552e4e85a54774b9d252c05c1c2412f2f72583660835e6b2f02c66cd652dde3b697666413e319e86398d62f19fea418a4dfaf69b722dfed79f55adfbe9cc5337808a934d40c6580609bdbce8eeaf614dccfc6c72258b9

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obg_fzo_cprz.fml
:: Utworzony: 18.03.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBG_FZO_CPRZ - Przekazanie faktury w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie faktury w obiegu - główna formuła czynności OBG_FZO_CPRZ
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_edokum','obiegi')
::# access=exec('uprawnienia','obiegi')
::# web_todo=exec('web_todo','!obg_fzo_cprz')

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=T, keyref=T
::# kind=WE, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WE, symbol=B_PREL, type=STRING, name=Poprzedni element w procesie, required=N
::# kind=WE, symbol=B_PRELS, type=STRING, name=Symbol poprzedniego elementu w procesie, required=N
::# kind=WE, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WE, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WE, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WE, symbol=STATUS, type=STRING, name=Status, required=N

::# kind=WE, symbol=B_ROLEF, type=STRING, name=Rola użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=B_ROLE, type=STRING, name=Domyślna rola użytkowników, required=N, fml_val="exec('rola','obiegi',_a)", fml_exp="exec('b_role_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=BLZMROL, type=STRING, name=Blokada zmiany roli, required=N, fml_val="exec('blzmrol','obiegi')"
::# kind=WE, symbol=FORM_ODR, type=STRING, name=Formuła wykonywana przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORMWODR, type=STRING, name=Formuła walidacyjna przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"

::# kind=WE, symbol=BLZMUSR, type=STRING, name=Blokada zmiany uzytkowników, required=N, fml_val="exec('blzmusers','obiegi')"
::# kind=WE, symbol=USR_FORM, type=STRING, name=Wybór użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=JEDEN_OP, type=STRING, name=Obsługuje jeden operator - formuła, required=N, fml_val="exec('jeden_op','obiegi2')"
::# kind=WE, symbol=UWODRWYM, type=STRING, name=Wymagane wypełnienie uwag przy odrzuceniu, required=N, fml_val="exec('czy_wymoduw','obiegi_akc')"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WY, symbol=USERS, type=_USERS, name=Użytkownik, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WY, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WY, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N

:: WŁAŚCIWOŚCI CZYNNOŚCI:
::# properties=LOOP,TODOTRIG,~RUNMICRO,~RELEASE

:: WARUNKI BRAMY:
::# condition=Akceptacja faktury, act_uid=OBG_FZO_EAKC, auto=T, formula=_a.EDOKUM<>~~ & _a.EDOKUM<>null
exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_context:=_args.context;
_akcja:={? type_of(_context)>100 & var_pres('AKCJA',_context)>0 || _context.AKCJA || _mp.akcja() ?};

OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
zak_akc_ed:=0;

{? _akcja='Przekaż2'
|| params_get().context.EDOKUM;
   params_exec('przekaz2','obiegi_akc')
|| {? var_pres('UNIK_ID',_we)>0 & _we.UNIK_ID<>''
   || non_eds:=1;
      params_exec('par_out','obiegi_akc',_we.UNIK_ID);
      VAR_DEL.delete('non_eds')
   |? _we.EDOKUM & EDOKUM.seek(_we.EDOKUM,ref_name(_we.EDOKUM),1)
   || {? app_info('web_sesid')<>''
      || params_exec('ustaw_bprel','obiegi');
         exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
         {? exec('ust_b_role','obiegi_akc',_we.EDOKUM,OBIEGI.B_PREL)=0 || return(~~) ?}
      ?};
:: sprawdzenie z poziomu webterma
      {? _akcja='chkWT'
      || params_get().context.SPR_WT:=1
:: akceptacja z pulpitu
      |? _akcja='Przekaż'
      || params_get().context.EDOKUM;
         params_exec('przekaz','obiegi_akc')
      |? _akcja='Odrzuć'
      || params_get().context.EDOKUM;
         EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',5); OBIEGI.EDOKOS();
         params_exec('odrzuc','obiegi_akc');
         {? zak_akc_ed=2 & EDOKUM.get()
         || exec('ustaw_out','obiegi_akc',_wy,'O');
            _mp.save(,_wy);
            _mp.done()
         ?}
      |? _akcja='Odrzuc2'
      || params_get().context.EDOKUM;
         EDOKOS.use('skid_y'+(EDOKUM.name()+2)); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref());
         exec('rkprz1','obiegi',5); OBIEGI.EDOKOS();
         params_exec('odrzuc2','obiegi_akc');
         {? zak_akc_ed=2 & EDOKUM.get()
         || exec('ustaw_out','obiegi_akc',_wy,'O');
            _mp.save(,_wy);
            _mp.done()
         ?}
      |? _mp.pathTodo() | _mp.isAutoRun()
      || menu_txt(,'Przekaż');
         {? var_pres('EDOKUM',_we)
         || _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
            {? _ref<>0 & _nam<>''
            || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
               {? EDOKUM.seek(_ref,_nam)
               || _zakr:=0; {? var_pres('zakres')>0 || _zakr:=zakres ?};
                  exec('init','obg'); zakres:=5;
                  exec('rkprz','obiegi');
                  {? _mp.isAutoRun()
                  || params_exec('przekaz','obiegi_akc')
                  || params_exec('przekaz_todo','obiegi_akc','P');
                     {? EDOKUM.get()
                     || {? zak_akc_ed=1
                        || exec('ustaw_out','obiegi_akc',_wy,'A');
                           no_bprel:=_nobp:=exec('no_bprel','obiegi_akc',_wy.B_PREL);
                           _mp.save(,_wy);
                           _mp.done();
                           no_bprel:=_nobp
                        |? zak_akc_ed=2
                        || exec('ustaw_out','obiegi_akc',_wy,'O');
                           _mp.save(,_wy);
                           _mp.done()
                        ?}
                     ?}
                  ?};
                  {? _zakr || zakres:=_zakr ?}
               ?};
               EDOKUM.cntx_pop()
            ?}
         ?}
      ?}
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Przekaż do akceptacji fakturę w obiegu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));

params_exec('ustaw_bprel','obiegi');
{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || KH.cntx_psh();
         {? EDOKUM.KHKH
         || _desc:='Przekaż do akceptacji fakturę w obiegu: %1, data sprzedaży %2, otrzymano %3, brutto: %4, kontrahent: %5'@@[EDOKUM.SYM,$EDOKUM.DOP,$EDOKUM.DO,form(EDOKUM.WART,,2,' ,'),EDOKUM.KHKH().SKR]
         || _desc:='Przekaż do akceptacji fakturę w obiegu: %1, data sprzedaży %2, otrzymano %3, brutto: %4'@@[EDOKUM.SYM,$EDOKUM.DOP,$EDOKUM.DO,form(EDOKUM.WART,,2,' ,')]
         ?};
         KH.cntx_pop();
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)
      ?};
      {? var_pres('co_dalej')>0 &
         ((4+web_top_win(0,0))='WPRG' | (4+web_top_win(1,0))='WPRG' | (4+web_top_win(2,0))='WPRG')
      || co_dalej:='P'
      ?};
      {? var_pres('STATUS',_we)>0 & _we.STATUS='O' & _mp.akcja()<>'Przekaż' & -menu_txt()<>'przekaż' & _set
      || params_exec('ustaw_bprel','obiegi');
         {? OBIEGI.B_PREL<>''
         || EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
            {? EDOKOS.first()
            || {! |?
                  EDOKOS.STATUS:='O'; EDOKOS.put();
                  EDOKOS.next()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?}
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc


\fak_ob_prz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie faktur w obiegu
::   WE: _a - zadeklarowane i równe 1 - odpalanie z TODO na WEBTERM
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? app_info('web_sesid')<>''
|| {? ~_ || _a:=0 ?};
   {? _a
   || exec('todo_run','obiegi',1); todo_run:=1
   || exec('todo_run','obiegi',0)
   ?};
   EDOKUM.web_cntx_save(0);
   {? ~_a || EDOKOS.web_cntx_save(0) ?};
   {? ~_a || EDOKUMW.EDOKUM() ?};
   _dalej:=exec('env_edokum','obiegi','PF',0);
   VAR_DEL.delete('todo_run');
   {? _dalej
   || _dalej:=exec('is_act_user','#b__box','OBG_FZO_CPRZ',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
      {? ~_dalej
      || FUN.info('Przekazanie faktury nie jest na liście zadań użytkownika.'@)
      ?}
   ?};
   {? _dalej
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='OBG_FZO_CPRZ';
      _params.AKCJA:='chkWT';
      _params.UIDREF:=EDOKUM.uidref();
      _params.WT_SKIP:=1;
      _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
      _params.CONTEXT.EDOKUM:=EDOKUM.ref();
      _params.CONTEXT.SPR_WT:=0;
      exec('maski_w','obiegi',1);
      exec('mp_run','#b__box',_params);
      {? _params.CONTEXT.SPR_WT
      || params_exec('przekaz','obiegi_akc')
      ?}
   ?}
|| _dalej:=exec('is_act_user','#b__box','OBG_FZO_CPRZ',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
   {? _dalej
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='OBG_FZO_CPRZ';
      _params.AKCJA:='Przekaż';
      _params.UIDREF:=EDOKUM.uidref();
      _params.CONTEXT:=obj_new('EDOKUM');
      _params.CONTEXT.EDOKUM:=EDOKUM.ref();
      exec('mp_run','#b__box',_params)
   || FUN.info('Przekazanie faktury nie jest na liście zadań użytkownika.'@)
   ?}
?}


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::       _b - B_PREL.ref() lub BI_PREL.ref() lub exec('mp_run_a','#b__box')
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
_auto:=0;
:: Inicjuje obiekt Menadżera Procesu, aby sprawdzić czy web_main uruchomiony w pętli:
BI_PREL.cntx_psh();
{? ref_tab(_b)=BI_PREL & BI_PREL.seek(_b,ref_name(_b),1)
|| _class:={? __develop || @.Class.cProMan || @.CLASS.cProMan ?};
   _proMan:=obj_new(_class);
   _proMan.setCntx(_b);
   _auto:=_proMan.loop()
?};
BI_PREL.cntx_pop();
:: lub za pomocą pobrania portów wejściowcyh, gdzie w przypadku loop jest wskazanie na unik_id
::BI_PREL.cntx_psh();
::{? ref_tab(_b)=BI_PREL & BI_PREL.seek(_b,ref_name(_b),1)
::|| _ports:=exec('getPorts','#b_port',BI_PREL.B_PREL().B_ELE,'IN',BI_PREL.B_PREL);
::   _we:=exec('fillPorts','#bi_port',BI_PREL.ref(),'IN',_ports)
::?};
::_auto:=(var_pres('UNIK_ID',_we)>0 & _we.UNIK_ID<>'');
::BI_PREL.cntx_pop();
{? _auto
|| exec('web_run','#b__box',_b,'Todo',,'web_Todo',,,,,,'T')
|? _path='Todo'
|| _path_param:=_b;
   _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   _prel:=_path_param;
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || _name:=((_keyrefs[1]-8)+8);
      EDOKUM.use(_name); EDOKUM.prefix();
      {? EDOKUM.seek(_keyrefs[1],,1)
      || exec('maski_w','obiegi',1);
         EDOKUM.memo_get(,'UW_OPDL');
         exec('bobs_edokum','obiegi','F');
         SKID.SL_KH:=SKID.NIP:='';
         web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'ed_kh_ed',0));
         EDOKUM.web_display('WF01',,"EDOKUM.web_eclose()")
      ?}
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
?}


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności OBG_FZO_CPRZ
::  WE: _a - 'add po', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='add_po' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='add_po'
         || 3
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'OBG_FZO_CPRZ')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Podglad dokumentu obiegu z listy zadań
::----------------------------------------------------------------------------------------------------------------------
_webterm:=+app_info('web_sesid');

_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
_rfd:=null();
{? _wy.EDOKUM <> ~~
|| _rfd:=_wy.EDOKUM
|| _in:=_mp.load(exec('kind_in','#b_port'));
   _rfd:=_in.EDOKUM
?};
{? _rfd <> ~~
|| {? _webterm
   || EDOKUM.use(8+$_rfd);
      EDOKUM.seek(_rfd);
      EDOKUM.web_cntx_save();
      EDOKUM.web_display('WF03',,"EDOKUM.web_cntx_load(); EDOKUM.web_eclose(); web_top_refresh(1)")
   || exec('dok_spac','obg',_rfd)
   ?}
|| FUN.info('Nie znaleziono dokumentu obiegu.')
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 df21e8902f1677ff6ff6a9bae54fc3e9b881b6f40d650605051d9563960cd0bc344f89f75c2603e71f70b952ebff232cff25726f533105f001f6f8a239ce9e23508d5a15b0f452275d91a6f2f4a93fd8ba88e2f7757e37b625cc2e8c19ef7cfc136299d61a597a6211f51196dde992f4ea92ddaef61f983af1e4808180dc7362

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pktl.fml
:: Utworzony: 12.04.2023
:: Autor: MicKoc
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PKTL - Ustawowe limity obecności alkoholu lub innych substancji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Ustawowe limity obecności alkoholu lub innych substancji - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
KOTLIMIT.index('WYNIK');
KOTLIMIT.prefix();
KOTLIMIT.win_sel('WER');
KOTLIMIT.win_edit('RED');
KOTLIMIT.fld_fml('WYNIK','BEFORE_DISPLAY',"
   {? KOTLIMIT.KOT_JM
   || KOTLIMIT.KOT_JM().TYP='T'
   || 1
   ?}
");
KOTLIMIT.fld_fml('TEST','BEFORE_DISPLAY',"
   {? KOTLIMIT.KOT_JM
   || KOTLIMIT.KOT_JM().TYP='N'
   || 1
   ?}
");
exec('ustaw_zmienne','kontrola_trzezwosci',KOTLIMIT);
KOTLIMIT.select();
KOTLIMIT.fld_fml('WYNIK','DISPLAY_FORMAT',"*");
KOTLIMIT.fld_fml('TEST','DISPLAY_FORMAT',"*");
~~


\kotlimit_kot_jm_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Po edycji pola KOT_JM tabeli KOTLIMIT
::  OLD: \kotlimit_kot_jm_ae/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOTLIMIT.JM:=KOTLIMIT.KOT_JM().JM;
exec('ustaw_zmienne','kontrola_trzezwosci',KOTLIMIT)


\kotlimit_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Dołącz przed tabeli KOTLIMIT
::  OLD: \kotlimit_dolacz_b/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOTLIMIT.blank(1);
exec('ustaw_zmienne','kontrola_trzezwosci',KOTLIMIT);
1


\kotlimit_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Popraw przed tabeli KOTLIMIT
::  OLD: \kotlimit_popraw_b/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ustaw_zmienne','kontrola_trzezwosci',KOTLIMIT);
1


\kotlimit_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Rekord przed tabeli KOTLIMIT
::  OLD: \kotlimit_rb/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ustaw_zmienne','kontrola_trzezwosci',KOTLIMIT);
''


\kotlimit_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Rekord po tabeli KOTLIMIT
::  OLD: \kotlimit_ra/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
_jm_typ:=KOTLIMIT.KOT_JM().TYP;
{? _jm_typ='N' & KOTLIMIT.TEST=''
|| __CHK.empty_msg('TEST');
   return('TEST')
?};
_chk:=__CHK.table(KOTLIMIT,-menu_txt()='popraw',,'STAN','KOT_JM');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & _chk=0)
|| return(_chk)
?};
_put:={? -(menu_pth()+1)='d' || null() || KOTLIMIT.ref() ?};
KOTLIMIT.cntx_psh();
{? _jm_typ='N'
|| KOTLIMIT.index('TEST');
   KOTLIMIT.prefix(KOTLIMIT.KOT_JM().JM().KOD,KOTLIMIT.TEST)
|| KOTLIMIT.index('WYNIK');
   KOTLIMIT.prefix(KOTLIMIT.KOT_JM().JM().KOD,KOTLIMIT.WYNIK)
?};
_jest:=0;
{? KOTLIMIT.first()
|| {!
   |? {? KOTLIMIT.ref()<>_put || _jest:=1 ?};
      ~_jest & KOTLIMIT.next()
   !}
?};
KOTLIMIT.cntx_pop();
{? _jest
|| _txt:=
      {? _jm_typ='N'
      || KOTLIMIT.TEST
      || $KOTLIMIT.WYNIK
      ?};
   FUN.error('Limit "%1 - %2" o wartości %3 już istnieje.\nPopraw wartość'@
      [KOTLIMIT.JM().KOD,JM.NAZ,_txt]);
   return({? _jm_typ='N' || 'TEST' || 'WYNIK' ?})
?};
{? KOTLIMIT.WYNIK<0
|| FUN.error('Wartość od limitu nie może być mniejsza od 0.'@);
   return('WYNIK')
?};
''


\kotlimit_okno_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Okienko po, weryfikacja nachodzenia na siebie limitów
::  OLD: \kotlimit_okno_a/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_sql:=''+"select distinct KOD
          from KOTLIMIT
          join KOT_JM using(KOTLIMIT.KOT_JM, KOT_JM.REFERENCE)
          join JM using(KOT_JM.JM, JM.REFERENCE)
          where KOT_JM.TYP='T'";
_JM:=sql(_sql);

KOTLIMIT.cntx_psh();
KOTLIMIT.index('WYNIK');
{? _JM.first()
|| {!
   |? KOTLIMIT.prefix(_JM.KOD,);
      {? KOTLIMIT.first()
      || _od:=KOTLIMIT.WYNIK;
         {!
         |? _ret & KOTLIMIT.next()
         |! {? KOTLIMIT.WYNIK=_od
            || _ret:=0
            ?};
            _od:=KOTLIMIT.WYNIK
         !}
      ?};
      _JM.next()
   !}
?};
KOTLIMIT.cntx_pop();
_txt1:='Nieprawidłowa definicja wartości limitów.'@;
_txt2:='Zaleca się poprawienie danych.'@;
_txt3:='Chcesz powrócić do edycji?'@;
_ret | (~_ret & ~FUN.ask('%1 %2\n%3'[_txt1,_txt2,_txt3]))


\_kotlimit_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Wyzwalacz dołącz przed tabeli KOTLIMIT
::  OLD: \_kotlimit_addb/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOTLIMIT.JM:={? KOTLIMIT.KOT_JM || KOTLIMIT.KOT_JM().JM || null() ?};
1


\_kotlimit_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Wyzwalacz popraw przed tabeli KOTLIMIT
::  OLD: \_kotlimit_putb/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOTLIMIT.JM:={? KOTLIMIT.KOT_JM || KOTLIMIT.KOT_JM().JM || null() ?};
1

:Sign Version 2.0 jowisz:1045 2023/07/21 13:25:24 410a4a5f19b38788940f901fd9131cb95e776b711c4d29ccaacd0e4597a2cf38cfe9259bd16eed0f6b24179302041ac98538cca8d4e4fcace185c942327c6e52cd64a5db0fc5c1466846d46106199dd168eb034b6c161a68262942f1beada1b2e07d112b8c2b76caa809eabf528d335541e4a21c9047ff89b53b2c8b93b95487

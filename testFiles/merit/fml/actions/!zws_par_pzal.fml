:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pzal.fml
:: Utworzony: 14.03.2018
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PZAL - Przekazanie załączników osob.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Przekazanie załączników osobowych - formuła główna czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Czynność wymaga podania parametru P lub OSOBA. Jeżeli podane zostaną obydwa, to parametr OSOBA jest ignorowany.
::
:: Parametr wejściowy P wskazuje współpracownika, którego dotyczą załączniki.
::# kind=WE, symbol=P, type=_P, name=Wskazanie współpracownika, required=N, keyref=T
::
:: Parametr wejściowy OSOBA wskazuje osobę, której dotyczą załączniki.
::# kind=WE, symbol=OSOBA, type=_OSOBA, name=Wskazanie osoby, required=N, keyref=T
::
:: Parametr wejściowy BLOB wskazuje wprowadzone załączniki. Najczęściej będzie wykorzystany w trybie przeglądania
:: wprowadzonych załączników, czyli dla MODIFY=0.
::# kind=WE, symbol=BLOB, type=BLOB, name=Wprowadzone załączniki, required=N, keyref=N
::
:: Parametr wejściowy TITLE definiuje tytuł okna z załącznikami [domyślnie: Załączniki]. Parametr może posłużyć do
:: uszczegółowienia czynności (np. "Aneks do umowy").
::# kind=WE, symbol=TITLE, type=STRING, name=Tytuł okna, required=N, keyref=N
::
:: Parametr wejściowy MODIFY określa tryb pracy:
::    - dołączanie załączników (MODIFY='T');
::    - przeglądanie załączników (MODIFY='N') [domyślnie].
::# kind=WE, symbol=MODIFY, type=STRING, name=Modyfikacja załączników: [T/N*], required=N, keyref=N, fml_val="exec('edit_boolean','#edit',_a,'Modyfikacja załączników?')"
::
:: Parametr wejściowy MULTI określa czy możliwe będzie dołączanie wielu załączników (MULTI='T') czy tylko jednego
:: (MULTI='N' - domyślnie). Parametr ma znaczenia dla MODIFY='T'.
::# kind=WE, symbol=MULTI, type=STRING, name=Wiele załączników: [T/N*], required=N, keyref=N, fml_val="exec('edit_boolean','#edit',_a,'Wiele załączników?')"
::
:: Parametr wejściowy REQUIRED określa, czy wymagane jest dołączenie choć jednego załącznika (REQUIRED='T') czy też
:: czynność można zakończyć bez dołączania załącznika (REQUIRED='N' - domyślnie). Parametr ma znaczenie wyłącznie dla
:: MODIFY='T'.
::# kind=WE, symbol=REQUIRED, type=STRING, name=Wymagany załącznik: [T/N*], required=N, keyref=N, fml_val="exec('edit_boolean','#edit',_a,'Załącznik wymagany?')"
::
:: Parametr wewnętrzny BLOB wskazuje na wprowadzane załączniki.
::# kind=WEW, symbol=BLOB, type=BLOB, name=Wprowadzane załączniki, required=N, keyref=N
::
::
:: Parametr wyjściowy BLOB wskazuje na wprowadzone załączniki.
::# kind=WY, symbol=BLOB, type=BLOB, name=Wprowadzone załączniki, required=N, keyref=N
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_int:=_par.int;
_out:=_par.out;

_default:="{? ($('_a.'+_b))(_a)=~~ || ($('_a.'+_b+':=_b'))(_a,_c) ?}";
:: Określenie wartości domyślnych
_default(_in,'P',null());
_default(_in,'OSOBA',null());
_default(_in,'BLOB',null());
_default(_in,'TITLE','Załączniki'@);
_default(_in,'MODIFY','N');
_default(_in,'MULTI','N');
_default(_in,'REQUIRED','N');
_default(_int,'BLOB',null());
_default(_out,'BLOB',null());

{? _in.P=null() & _in.OSOBA=null()
|| _mp.error('Jeden z parametrów wejściowych (P lub OSOBA) musi być podany.');
   return()
?};

_arg:=exec('arg','!zws_par_pzal');
_arg.P:=_in.P;
_arg.OSOBA:=_in.OSOBA;

_blob:={? _int.BLOB<>null() || _int.BLOB |? _in.BLOB<>null() || _in.BLOB || null() ?};
_BLOB:=_mp.bl_get(_blob);
{? _BLOB.first()
|| {!
   |? _arg.BUF.blank();
      _arg.BUF.BLOB:=_BLOB.BLOB;
      _arg.BUF.add();
      _BLOB.next()
   !}
?};

_arg.TITLE:=_in.TITLE;
_arg.MODIFY:=_in.MODIFY='T';
_arg.MULTI:=_in.MULTI='T';
_arg.REQUIRED:=_arg.MODIFY & _in.REQUIRED='T';

_ret:=exec('select','!zws_par_pzal',_arg);
:: _ret: 0-Esc / 1-OK / 2-Zakończ

_BUF:={? _ret=0 || _BLOB || _arg.BUF ?};
_port:={? _ret<2 || exec('kind_internal','#b_port') || exec('kind_out','#b_port') ?};
{? _BUF.first()
|| {!
   |? _mp.bl_add('BLOB',_port,_BUF.BLOB);
      _BUF.next()
   !}
?};

{? _ret<2
|| _mp.save(_int);
   _mp.keep()

|| _mp.save(,_out);
   _mp.done()
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Przekazanie załączników osobowych - formuła opisu czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

_default:="{? ($('_a.'+_b))(_a)=~~ || ($('_a.'+_b+':=_b'))(_a,_c) ?}";
:: Określenie wartości domyślnych
_default(_in,'P',null());
_default(_in,'OSOBA',null());
_default(_in,'MODIFY','N');
_default(_in,'MULTI','N');

_modif:=(_in.MODIFY='T');
_multi:=(_in.MULTI='T');

{? var_pres('P',_in)=type_of(null()) & _in.P<>null()
|| _tab:=exec('desc','pracownik',_mp);
   {? _tab.ZAW_DANE='T'
   || {? _modif & _multi
      || {? _tab.OBCY='T'
         || 'Przekaż załączniki osobowe: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
         |? +_tab.PESEL
         || 'Przekaż załączniki osobowe: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
         || 'Przekaż załączniki osobowe: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
         ?}
      |? _modif & ~_multi
      || {? _tab.OBCY='T'
         || 'Przekaż załącznik osobowy: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
         |? +_tab.PESEL
         || 'Przekaż załącznik osobowy: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
         || 'Przekaż załącznik osobowy: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
         ?}
      |? ~_modif & _multi
      || {? _tab.OBCY='T'
         || 'Przejrzyj załączniki osobowe: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
         |? +_tab.PESEL
         || 'Przejrzyj załączniki osobowe: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
         || 'Przejrzyj załączniki osobowe: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
         ?}
      || {? _tab.OBCY='T'
         || 'Przejrzyj załącznik osobowy: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
         |? +_tab.PESEL
         || 'Przejrzyj załącznik osobowy: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
         || 'Przejrzyj załącznik osobowy: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
               [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
         ?}
      ?}
   || {? _modif & _multi
      || 'Przekaż załączniki osobowe'@@
      |? _modif & ~_multi
      || 'Przekaż załącznik osobowy'@@
      |? ~_modif & _multi
      || 'Przejrzyj załączniki osobowe'@@
      || 'Przejrzyj załącznik osobowy'@@
      ?}
   ?}
|? var_pres('OSOBA',_in)=type_of(null()) & _in.OSOBA<>null()
|| _tab:=exec('desc','osoba',params_get().mp);
   {? _tab.ZAW_DANE='T'
   || {? _modif & _multi
      || {? _tab.OBCY='T'
         || 'Przekaż załączniki osobowe: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
         |? +_tab.PESEL
         || 'Przekaż załączniki osobowe: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
         || 'Przekaż załączniki osobowe: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
         ?}
      |? _modif & ~_multi
      || {? _tab.OBCY='T'
         || 'Przekaż załącznik osobowy: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
         |? +_tab.PESEL
         || 'Przekaż załącznik osobowy: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
         || 'Przekaż załącznik osobowy: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
         ?}
      |? ~_modif & _multi
      || {? _tab.OBCY='T'
         || 'Przejrzyj załączniki osobowe: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
         |? +_tab.PESEL
         || 'Przejrzyj załączniki osobowe: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
         || 'Przejrzyj załączniki osobowe: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
         ?}
      || {? _tab.OBCY='T'
         || 'Przejrzyj załącznik osobowy: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
         |? +_tab.PESEL
         || 'Przejrzyj załącznik osobowy: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
         || 'Przejrzyj załącznik osobowy: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
         ?}
      ?}
   || {? _modif & _multi
      || 'Przekaż załączniki osobowe'@@
      |? _modif & ~_multi
      || 'Przekaż załącznik osobowy'@@
      |? ~_modif & _multi
      || 'Przejrzyj załączniki osobowe'@@
      || 'Przejrzyj załącznik osobowy'@@
      ?}
   ?}
|| {? _modif & _multi
   || 'Przekaż załączniki osobowe'@@
   |? _modif & ~_multi
   || 'Przekaż załącznik osobowy'@@
   |? ~_modif & _multi
   || 'Przejrzyj załączniki osobowe'@@
   || 'Przejrzyj załącznik osobowy'@@
   ?}
?}


\arg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła przygotowuje tablicę z elementami nazwanymi, będącą parametrem formuły \select.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_BUF:=tab_tmp(1,
   'IDADD','IDADD','Systemowy identfikator rekordu'@,
   'BLOB','BLOBRAW','Załącznik'@
);

exec('obj_ntab_set','#array',,
   'P',null(),
   'OSOBA',null(),
   'TITLE','',
   'MODIFY',0,
   'MULTI',0,
:: Wymagany jest co najmniej jeden załącznik.
   'REQUIRED',1,
   'BUF',_BUF
)


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Główna formuła obsługi.
::       Uwaga: poprawność argumentów nie jest weryfikowana.
::   WE: _a [ARRAY] - Tablica z elementami nazwanymi, będącymi parametrami działania (patrz \arg).
::   WY: Sposób wyjścia z okna:
::          0 - Esc;
::          1 - OK;
::          2 - Zakończ.
::----------------------------------------------------------------------------------------------------------------------
_arg:=_a;
_ret:=0;

OSOBA.cntx_psh();
OSOBA.prefix();
{? _arg.P<>null()
|| P.cntx_psh();
   P.prefix();
   F_ZATR.cntx_psh();
   F_ZATR.prefix();
   CP.cntx_psh();
   CP.prefix();
   UD_SKL.cntx_psh();
   UD_SKL.prefix();
   STN.cntx_psh();
   STN.prefix();
   KK.cntx_psh();
   KK.prefix();
   _ok:=P.seek(_arg.P) & P.OSOBA().get()
|| _ok:=OSOBA.seek(_arg.OSOBA)
?};

{? _ok
|| _cfg:=exec('wnd','!zws_par_pzal',_arg);
   _arg.BUF.win_sel(_cfg.grp);
   params_set('arg',_arg,'cfg',_cfg);
   {? _arg.BUF.select()
   || _ret:=_cfg.tryb
   ?}
?};

{? _arg.P<>null()
|| KK.cntx_pop();
   STN.cntx_pop();
   UD_SKL.cntx_pop();
   CP.cntx_pop();
   F_ZATR.cntx_pop();
   P.cntx_pop()
?};
OSOBA.cntx_pop();

_ret


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła buduje interfejs czynności.
::       Uwaga: poprawność argumentów nie jest weryfikowana.
::   WE: _a [ARRAY] - Tablica z elementami nazwanymi, będącymi parametrami działania (patrz \arg).
::   WY: Akronim utworzonego okna grupowego.
::----------------------------------------------------------------------------------------------------------------------
_arg:=_a;


:: Kontrolka
_ctr_id:='zal';
_ctr_win:=_arg.BUF.mk_ctr(,'#zal_wc');
_arg.BUF.win_cctr(_ctr_win,_ctr_id,'@filelistpanel',{? _arg.MODIFY || 'dnd' || '' ?});

_arg.BUF.win_cfml(_ctr_win,_ctr_id,'CONTROL_INIT',"params_exec('control_init','!zws_par_pzal')");
_arg.BUF.win_cfml(_ctr_win,_ctr_id,'CONTROL_DISPLAY',"params_exec('control_display','!zws_par_pzal')");
_arg.BUF.win_cfml(_ctr_win,_ctr_id,'CONTROL_CALLBACK',"params_exec('control_callback','!zws_par_pzal')");


:: Okno przycisków
_wb:=_arg.BUF.mk_edit(,,'#zal_wb');
exec('zakoncz','#window',_arg.BUF,_wb,1,"params_get().cfg.tryb:=2; sel_exit(); ''",1);
exec('ok_esc','#window',_arg.BUF,_wb,1,
   "params_get().cfg.tryb:=1; sel_exit(); ''",
   "params_get().cfg.tryb:=0; 'key:Esc'"
   ,_arg.MODIFY,1,,
   exec('text_red_ok','#window')
);

:: Budowa okna grupowego
_mode:='maximized';
_grp:=_arg.BUF.grp_make(_arg.TITLE,,'#zal_grp',,,
   {? _arg.REQUIRED || "params_exec('ae','!zws_par_pzal',_a)" || "" ?},,'normal'
);
{? _arg.P<>null()
|| _arg.BUF.grp_edit(_grp,P,'INFO_'+{? P.F_ZATR().KOD='Z' || 'Z' || 'P' ?},,,,,,_mode)
|| _arg.BUF.grp_edit(_grp,OSOBA,'INFO',,,,,,_mode)
?};
_arg.BUF.grp_splt(_grp,,'horizontal','ctr');
_arg.BUF.grp_ctr(_grp,,_ctr_win,,,,,,,,,,,'maximized');
_arg.BUF.grp_splt(_grp,,'horizontal','wb',30);
_arg.BUF.grp_edit(_grp,,_wb,,,,,,'maximized');


exec('obj_ntab_set','#array',,
   'ctr_id',_ctr_id,
   'grp',_grp,
   'tryb',-1
)


\ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła wykonywana przy próbie opuszczenia okna.
::   WE: _a [STRING] - Sposób zamknięcia okna grupowego.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a<>'sel_exit'
:: Szczególnej obsługi wymagają przyciski: Zakończ i OK. Oba są związane z sel_exit().
|| return(1)
?};

_par:=params_get();
_arg:=_par.arg;
_cfg:=_par.cfg;

{? _cfg.tryb=1
:: Przycisk OK - możemy jeszcze nie weryfikować.
|| 1

|? ~_arg.BUF.first()
|| FUN.emsg('Co najmniej jeden załącznik powinien być dodany.'@);
   0
|| 1
?}


\tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła tworzy tabelę tymczasową do komunikacji z kontrolką. Jeżeli przekazany jest parametr z tabelą
::   WE: [_a] [TABLE] - Uchwyt tabeli z polem o akronimie BLOB typu BLOBRAW, na podstawie której wypełniona zostanie
::                      zwracana tabela.
::   WY: Tabela do komunikacji z kontrolką.
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(1,
   'NAME','STRING[64]','Plik',
   'SIZE','INTEGER','Rozmiar',
   'CRT_DATE','DATE','Data utworzenia',
   'CRT_TIME','TIME','Czas utworzenia',
   'MOD_DATE','DATE','Data modyfikacji',
   'MOD_TIME','TIME','Czas modyfikacji',
   'INT_DATA','INTEGER','Identyfikator rekordu',
   'STR_DATA','STRING[48]','Identyfikator rekordu'
);

{? var_pres('_a')=type_of(_TAB)
|| _BUF:=_a;
   {? _BUF.first()
   || {!
      |? _TAB.blank();
         _TAB.NAME:=_BUF.bl_info('BLOB','NAME');
         _TAB.SIZE:=_BUF.bl_info('BLOB','SIZE');
         _TAB.CRT_DATE:=_BUF.bl_info('BLOB','CREAT_DATE');
         _TAB.CRT_TIME:=_BUF.bl_info('BLOB','CREAT_TIME');
         _TAB.MOD_DATE:=_BUF.bl_info('BLOB','MODIFY_DATE');
         _TAB.MOD_TIME:=_BUF.bl_info('BLOB','MODIFY_TIME');
         _TAB.INT_DATA:=#_BUF.ref();
         _TAB.STR_DATA:=_BUF.uidref();
         _TAB.add();
         _BUF.next()
      !}
   ?}
?};

_TAB


\control_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła wykonywana po pojawieniu się kontrolki.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_arg:=_par.arg;
_cfg:=_par.cfg;

ctr_set(,_cfg.ctr_id,'setColumns','NAME,SIZE,#TYPE,CRT_DATE,CRT_TIME,MOD_DATE,MOD_TIME');
ctr_set(,_cfg.ctr_id,'setColumnWidth','NAME',40);
ctr_set(,_cfg.ctr_id,'setColumnWidth','SIZE',10);
ctr_set(,_cfg.ctr_id,'setColumnWidth','#TYPE',20);
ctr_set(,_cfg.ctr_id,'setColumnWidth','CRT_DATE',12);
ctr_set(,_cfg.ctr_id,'setColumnWidth','CRT_TIME',12);
ctr_set(,_cfg.ctr_id,'setColumnWidth','MOD_DATE',12);
ctr_set(,_cfg.ctr_id,'setColumnWidth','MOD_TIME',12);

params_exec('control_display','!zws_par_pzal');

ctr_set(,_cfg.ctr_id,'removeAllActions');
ctr_set(,_cfg.ctr_id,'addToolbarAction','setView','','xwin16.png:47','Zmiana widoku'@,0,0);
ctr_set(,_cfg.ctr_id,'addToolbarAction','refresh','','xwin16.png:48','Odświeżenie widoku'@,0,0);
ctr_set(,_cfg.ctr_id,'addToolbarSeparator');
{? _arg.MODIFY
|| {? _arg.MULTI | ~_arg.BUF.first()
   || ctr_set(,_cfg.ctr_id,'addAction','add','&Dołącz'@,'','Dołączenie załącznika'@,0,0)
   ?};
   ctr_set(,_cfg.ctr_id,'addAction','del','&Usuń'@,'','Usunięcie załącznika'@,2,0)
?};
ctr_set(,_cfg.ctr_id,'addAction','open','&Otwórz'@,'','Otworzenie załącznika'@,1,1);
{? _arg.MODIFY & (_arg.MULTI | ~_arg.BUF.first())
|| ctr_set(,_cfg.ctr_id,'addAction','edit','&Edytuj'@,'','Edycja załącznika'@,1,0)
?};
ctr_set(,_cfg.ctr_id,'addAction','save','&Zapisz'@,'','Zapisanie załącznika'@,2,0);
ctr_call(,_cfg.ctr_id,'initToolbar');

~~


\control_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła wykonywana w momencie odświeżania okna z kontrolką.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_arg:=_par.arg;
_cfg:=_par.cfg;

ctr_tab('files_data',,_cfg.ctr_id,,exec('tab','!zws_par_pzal',_arg.BUF));
ctr_call(,_cfg.ctr_id,'updateItems','files_data');

~~


\control_callback
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła obsługuje akcje wywołane z kontrolki.
::   WE:
::   WY:
:: ~OST: INFILECHOOSER,INFEXISTS, INBLGET, INBLEDIT
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
_arg:=_par.arg;
_cfg:=_par.cfg;

_action:=ctr_info('action_id');

:: Obsługa akcji, które nie są związane z plikami. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
{? _action='setView'
|| _view:=ctr_call(,_cfg.ctr_id,'getViewName');
   ctr_call(,_cfg.ctr_id,'setView',{? _view='icons' || 'table' || 'icons' ?});
   win_disp();
   return()

|? _action='refresh'
|| exec('control_init','!zws_par_pzal');
   win_disp();
   return()
?};

:: Dodawanie plików do kontrolki.  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

{? _action='add'
|| _tmp_dir:=fmk_tmp_dir(0);
   _refresh:=0;
   {? type_of(_tmp_dir)<>type_of(~~)
   || _pth:=_tmp_dir.get_path();
      {? _pth<>''
      || _uploaded:=dlg_upload(_pth,0,,1);
         {? _uploaded<>''
         || _split:=spli_str(_uploaded,'\n');
            _len:=obj_len(_split);
            _sep:=exec('sep','#file',1);
            {! _it:=1.._len
            |! _filename:=_split[_it];
               {? _filename<>''
               || _fp:=_pth+_sep+_filename;
                  _arg.BUF.blank();
                  _arg.BUF.add();
                  _arg.BUF.bl_put('BLOB',_fp);
                  _refresh:=1
               ?}
            !}
         ?}
      ?}
   ?};
   {? _refresh
   || win_disp()
   ?};
   return()

|? _action='dnd' & (_arg.MULTI | ~_arg.BUF.first())
|| _TIN:=ctr_call(,_cfg.ctr_id,'getDroppedFiles');
   {? _TIN.first()
   || _sep:=exec('sep','#file');
      {!
      |? _fp:=_TIN.DIR+_sep+_TIN.FILENAME;
         echo(_fp+' ...');
         _arg.BUF.blank();
         _arg.BUF.add();
         _arg.BUF.bl_put('BLOB',_fp);
         _arg.MULTI & _TIN.next()
      !};
      echo();
      {? ~_arg.MULTI &_arg.BUF.size()=1
::       Trzeba usunąć akcję "Dołącz".
      || exec('control_init','!zws_par_pzal')
      ?};
      win_disp()
   ?};
   return()
?};

:: Obsługa akcji, które moga być wykonywane na pliku/plikach kontrolki.  - - - - - - - - - - - - - - - - - - - - - - - -
_TIN:=ctr_call(,_cfg.ctr_id,'getCurrActionItems');
{? ~_TIN.first()
|| return()
?};

:: Akcje dla wiele plików
{? _action='del'
|| {? FUN.ask({? _TIN.size()>1 || 'Czy usunąć zaznaczone załączniki?'@ || 'Czy usunąć zaznaczony załącznik?'@ ?})
   || _refresh:=0;
      {!
      |? {? _arg.BUF.seek(_TIN.STR_DATA)
         || _refresh+=_arg.BUF.del(,1)
         ?};
         _TIN.next()
      !};
      {? _refresh
      || {? ~_arg.MULTI & ~_arg.BUF.first()
::          Być może trzeba dodać akcję "Dołącz".
         || exec('control_init','!zws_par_pzal')
         ?};
         win_disp()
      ?}
   ?};
   return()

|? _action='save'
|| {? exec('interm','#system')
   || {!
      |? {? _arg.BUF.seek(_TIN.STR_DATA)
         || _fn:=_arg.BUF.bl_info('BLOB','NAME')
         ?};
         _txt:=exec('bl_save','#blob',_arg.BUF,'BLOB');
         {? _txt='' | errno()
         || FUN.info('Zapisanie pliku %1 na dysku użytkownika nie powiodło się.'@ [_fn])
         ?};
         _TIN.next()
      !};
      return()
   ?};
   _dir:=exec('filechooser','#file','Wybór katalogu'@,0,,,,'DIRECTORIES_ONLY',1);
   {? _dir<>''
   || _sep:=exec('sep','#file');

      _ovr:=0;
      _esc:=0;
      {!
      |? {? _arg.BUF.seek(_TIN.STR_DATA)
         || errno();
            _fn:=_arg.BUF.bl_info('BLOB','NAME');
            _again:=1;
            {!
            |? _again
            |! _again:=0;
               _fp:=_dir+_sep+_fn;
               {? _ovr<>2
               || {? fexists('@'+_fp,0)
                  || _txt:='Plik %1 już istnieje. Nadpisać?'@ [_fp];
                     {? _TIN.size()>1
                     || _ret:=FUN.choice(_txt,,'Nadpisz'@,'Nadpisz &wszystkie'@,'Zmień nazwę'@,'Pomiń'@,'Anuluj'@)
                     || _ret:=FUN.choice(_txt,,'Nadpisz'@,'Zmień nazwę'@,,,'Anuluj'@);
                        {? _ret=2
                        || _ret:=3
                        ?}
                     ?};
                     {? _ret=0
::                      Anuluj
                     || _esc:=1

                     |? _ret=1 | _ret=2
::                      Nadpisz lub Nadpisz wszystkie
                     || _ovr:=_ret

                     |? _ret=3
::                      Zmień nazwę
                     || _fnn:=exec('edit_string','#edit',_fn,'Nazwa pliku'@,
                           "__CHK.record2(cur_tab(1,1),'VAL','Nazwa pliku'@)"
                        );
                        {? type_of(_fnn)=type_of('')
                        || _fn:=_fnn
                        ?};
                        _again:=1

                     |? _ret=4
::                      Pomiń
                     || _ovr:=0
                     ?}
                  || _ovr:=1
                  ?}
               ?}
            !};
            {? _ovr
            || _nm:=no_msg(1);
               {? ~_arg.BUF.bl_get('BLOB','@'+_fp,0) | errno()
               || FUN.info('Zapisanie pliku %1 na dysku użytkownika nie powiodło się.'@ [_fp])
               ?};
               no_msg(_nm);
               {? _ovr<>2
               || _ovr:=0
               ?}
            ?}

         ?};
         ~_esc & _TIN.next()
      !}
   ?};
   return()
?};

:: Akcje dla jednego pliku
{? ~_arg.BUF.seek(_TIN.STR_DATA)
|| FUN.info('Zaznaczony załącznik został usunięty przez innego użytkownika.'@);
   win_disp()

|? _action='open'
|| exec('bl_view','#blob',_arg.BUF,'BLOB')

|? _action='edit'
|| {? exec('cli_functions','#system')=0
   || FUN.emsg(exec('indevice_nacc_msg','#system'));
      return()
   || {? _arg.BUF.bl_edit('BLOB')
:     Jeżeli plik został zmieniony to trzeba odświeżyć widok - zmieniła się data i czas modyfikacji a może i rozmiar.
      || win_disp();
         ctr_set(,_cfg.ctr_id,'selectItem',_TIN.INT_DATA,_TIN.STR_DATA,0)
      ?}
   ?}
?};

~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 d6c9acfee39bb91786f5c4f56b4c3e2a7059ff23177d5216e2cbd3587e7ff38869d1958a939f9401a3ff6f1f3ed8f0e96d7ef7adce9eafe71784176550df26b196814912ae596dd8780a8797330e7dbcc92fc48ca06d101cb7f68ad2e75d6b4aed3b4142aa62bc18da1ee688bef6316197654f6b62c27a659609d8058660b3a6

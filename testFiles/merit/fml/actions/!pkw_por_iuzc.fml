:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkw_por_iuzc.fml
:: Utworzony: 16.06.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności webTerm-owej PKW_POR_IUZC - Przegl. umów cywilnoprawnych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przegl. umów cywilnoprawnych (webTerm) - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pkw_por__uzc','pkw_por')


\zc_bd_web
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed" dla tabeli ZC.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('uz_pku_set','zlec_rh');
exec('uz_dwr_set','zlec_rh');
~~


\zc_szczegoly_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Szczegóły" w oknie WEB_WER tabeli ZC.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

:: !!! Poniższe podczytania chyba nie powinno mieć miejsca.
::ZC.ZWAL();
ZC.WAL();

exec('uz_dwr_set','zlec_rh');
exec('uz_pku_set','zlec_rh');
_we:='WEB_RED';
exec('zc_efld_opt','zlec_rh','*',ZC,_we);
ZC.memo_get(,'TRESC');
ZC.web_display(_we,,"web_top_tab().web_eclose(web_top_win())")


\zc_web_grp_rh_web_wer_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed obsługą okna WEB_WER tabeli RH w ramach okna grupowego WEB_GRP tabeli ZC.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('env_wt','b_proces');

_zc:=web_win_ref('ZC_WEB_WER');
{? exec('get_par','#parametr',315,2)='T'
|| RH.index('RACHUNKI');
   RH.prefix(_zc)
|| RH.index('RACHO');
   RH.prefix(_zc,'T',)
?};
RH.first()


\rh_bd_web
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed" dla tabeli RH.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
LS.cntx_psh();
_typ:=RH.ZLE().TYP;
UZ.STAWKA:=UZ.GODZ:=UZ.DNI:=0;
{? _typ='G'
|| UZ.STAWKA:=exec('licz_rhs','lista_licz',91);
   UZ.GODZ:=exec('licz_rhs','lista_licz',54)
|? _typ='D'
|| UZ.DNI:=exec('licz_rhs','lista_licz',52);
   UZ.STAWKA:=exec('licz_rhs','lista_licz',7070)
?};
UZ.RH:=exec('licz_rhs','lista_licz',200);
LS.cntx_pop();
~~


\rh_szczegoly_a_web
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Szczegóły" w oknie tabeli RH.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

{? RH.O=null()
|| FUN.info('Składniki listy płac nie zostały naliczone.'@);
   return()
?};

RH.web_cntx_save(1);
RH.web_select('WEB_SZCZ');
~~


\rh_web_szcz_ls_web_rh_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: "Przed obsługą" okna WEB_RH tabeli LS w ramach okna grupowego WEB_SZCZ tabeli RH.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RH.web_cntx_load(1);

LS.use(RH.O().LT);
LS.index('ZLEC');
LS.prefix(RH.ref());
~~


\rh_web_szcz_rhp_web_wer_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: "Przed obsługą" okna WEB_WER tabeli RHP w ramach okna grupowego WEB_SZCZ tabeli RH.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RH.web_cntx_load(1);

RHP.index('RHP');
RHP.prefix(RH.ref());

~~

:Sign Version 2.0 jowisz:1028 2019/10/14 09:20:05 22e0d075bb57d94e638559d94163e57d3fa155912b9fca23a56a439fc0988c59254e75b2edbdcf42b79bc4084f4e6c38f9dd77ea954bc631a04b04743ff3ba355ebe73a910dd1b5ed9235794fcbd4e68a2b8aac1045485dbef4b3b4b01d206c23a373812e23bdc163128a06780b830edc9270677d90033bd18e825e61de27209

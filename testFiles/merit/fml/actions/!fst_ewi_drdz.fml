:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_drdz.fml
:: Utworzony: 30.10.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DRDZ - Redakcja dokumentu zmiany właściwości środka
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Redakcja dokumentu zmiany właściwości środka - główna formuła czynności FST_EWI_DRDZ.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# access=exec('uprawnienia','!fst_ewi_drdz')
:: PARAMETRY WE:
::# kind=WE, symbol=SRSR, type=_SRSR, name=Wskazanie na środek, required=N
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WE, symbol=TYP, type=STRING, name=Symbol typu dokumentu, required=N
:: PARAMETRY WY:
::# kind=WY, symbol=SRDO, type=_SRDO, name=Wskazanie na dokument, required=T
::

_mp:=params_get().mp;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_akcja:=-_mp.akcja();

{? _mp.pathProc() | _mp.pathTodo()
|| exec('init','fst');
   OKRO_F.win_dict('SLO_SR');
   _akcja:='dołącz'
?};

_typ:=null;
_ok:=1;
{? ~exec('okr_mod','fst') || _ok:=0 ?};

{? _ok
|| {? _mp.pathProc()
   || SRST.index('OKR_O_W');
      {? OPERATOR.DEPT<>null
      || SRST.prefix(SSTALE.AO,OPERATOR.DEPT)
      || SRST.prefix(SSTALE.AO)
      ?};
      _win:=exec('create_win_srst','fst','L');
      SRST.win_sel(_win);
      {? SRST.select()
      || SRSR.prefix();
         SRST.SRSR()
      || _ok:=0
      ?}
   |? _mp.pathTodo()
   || {? var_pres('SRSR',_we) & type_of(_we.SRSR)=type_of(null) & _we.SRSR<>null
      || SRSR.prefix();
         {? SRSR.seek(_we.SRSR)
         || SRST.index('SROD');
            SRST.prefix(SRSR.ref(),SSTALE.AR,SSTALE.AO);
            {? ~SRST.first()
            || FUN.emsg('Nie znaleziono wskazanego środka, rejestracja dokumentu nie jest możliwa.'@);
               _ok:=-1
            ?}
         || FUN.emsg('Nie znaleziono wskazanego środka, rejestracja dokumentu nie jest możliwa.'@);
            _ok:=-1
         ?}
      || FUN.emsg('Niepoprawne dane wejściowe, nie znaleziono wskazanego środka, rejestracja dokumentu nie jest możliwa.'@);
         _ok:=-1
      ?}
   ?}
?};

{? _ok=1
|| {? var_pres('TYP',_we) & type_of(_we.TYP)=type_of('string') & _we.TYP<>''
   || SRDT.cntx_psh();
      SRDT.index('SRDT');
      SRDT.prefix('L',_we.TYP);
      {? SRDT.first()
      || _typ:=SRDT.ref()
      ?};
      SRDT.cntx_pop()
   ?}
?};

{? _ok=1 & SRD.isElement() & SRDO.win_sel('?')<>'WER_D'
|| FUN.emsg('Nie można dodawać i modyfikować dokumentów zmiany właściwości dla elementów składowych zestawu.\n'
            'Dane tego typu są wspólne dla zestawu dlatego taki dokument należy dodawać na poziomie zestawu.'@);
   _ok:=0
?};

{? _ok=1 & ~SRD.isDataCompleted()
|| FUN.emsg('Środek wymaga uzupełnienia danych. Dołączanie lub modyfikacja dokumentów nie jest możliwa.'@);
   _ok:=0
?};

_srdo_rf:=null;
{? _ok=1
|| {? _akcja='dołącz' || _srdo_rf:=exec('dok_add','!fst_ewi_drdz',_typ)
   |? _akcja='popraw' || _srdo_rf:=exec('dok_edit','!fst_ewi_drdz')
   |? _akcja='usuń' || _srdo_rf:=exec('dok_del','!fst_ewi_drdz')
   ?}
?};

{? _srdo_rf
|| {? ~_mp.isMicro() & (type_of(_srdo_rf)=type_of(null))
   || SRDO.cntx_psh();
      SRDO.prefix();
      {? SRDO.seek(_srdo_rf)
      || _wy.SRDO:=SRDO.ref();
         _mp.keyRef(SRDO.uidref());
         _mp.save(,_wy)
      || _mp.error();
         SRDO.cntx_pop();
         return()
      ?};
      SRDO.cntx_pop()
   ?};
   _mp.done()
|? _srdo_rf=null & _ok=-1
|| _wy.SRDO:=null;
   _mp.save(,_wy);
   _mp.done()
|| _mp.cancel()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('SRSR',_in)=type_of(null()) & _in.SRSR & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
|| 'Zarejestruj dokument zmiany właściwości dla środka: %1'@@ [exec('record','#to_string',_in.SRSR)]
|| 'Zarejestruj dokument zmiany właściwości'@@
?}


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynnoścl
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh(); USERS.prefix();
{? USERS.seek(_user)
||
:: czy user ma uprawnienia do jednostki księgowej środka
   SRSR.cntx_psh();
   SRSR.prefix();
   {? var_pres('SRSR',_in)=type_of(null()) & _in.SRSR<>null() & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
   || {? exec('usr_fjks','b_perm',SRSR.ODD,USERS.ref())
      || _result:=1
      ?}
   || _result:=1
   ?};
   SRSR.cntx_pop()
?};
USERS.cntx_pop();
_result


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła ustala PARSES dla czynności na podstawie rekordu środka
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_in:=params_get().in;
_mp:=params_get().mp;
{? _mp.pathTodo()
|| {? var_pres('SRSR',_in) & var_pres('SRSR',_in)=type_of(null()) & _in.SRSR<>null
      & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
   || __PARSES.setVal('OkresRok',SRSR.OKRO_F);
      __PARSES.setVal('JednostkaKsięgowa',SRSR.ODD);
      _result:=1
   ?}
|| _result:=1
?};
_result


\dok_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowego dokumentu zmiany właściwości
::   WE: [_a] - wskazane na typ dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
_ref:=_srdo_rf:=null;
{? SRSR.r_lock(1,1,1)
|| __DEPT:=0;
   _tmp:=EDIT_ES.RODZ;
   {? SRDO.win_sel('?')='WER_D'
   || EDIT_ES.RODZ:='D'
   || EDIT_ES.RODZ:='L'
   ?};
   OSOBA.win_sel('SLO');
   SRDT.cntx_psh();
   {? _=0 | type_of(_a)<>type_of(null) | _a=null
   || SRDT.index('SRDT');
      SRDT.prefix(EDIT_ES.RODZ);
      {? SRDT.first()
      || SRDT.win_sel('SELECT');
         {? SRDT.select()
         || _ref:=SRDT.ref()
         ?}
      || FUN.emsg('W systemie nie zdefiniowano żadnego typu dokumentu zmiany właściwości.\n'
                  'Należy uzupełnić brakujące dane w obszarze Ustawienia i parametryzacja.'@)
      ?}
   || _ref:=_a
   ?};

   {? SRSR.U_SR='T' & SRSR.GRP='E' & (SRSR.KONPOD=null | SRSR.KONFIN=null)
   || FUN.emsg('Należy najpierw uzupełnić dane zestawu utworzonego w kartotece uproszczonej,\n'
               'dopiero później można wprowadzać dokumenty zmieniające właściwości zestawu.'@);
      _ref:=null
   ?};
   {? _ref<>null
   || SRDO.blank();
      SRDO_POM.blank();
      SRDO.TYP:=_ref;
      SRDO.ODD_DOK:=SRST.ODD;
      SRDO.NR:=exec('bl_srdo_nr','fst');
      SRDO.SYMBOL:=exec('symdok','fst');
::    jeżeli dokument zmiany właściwości dofinansowania to wskazanie na dofinansowanie
      {? SRDO.RODZ='D' || SRDO.SRZF:=SRZF.ref() ?};
      {? SRDO.TYP().NRI='T' || SRDO_POM.NRI:=SRDO.NRI:=SRST.NRI ?};
      {? SRDO.TYP().NST='T' || SRDO_POM.NST:=SRDO.NST:=SRST.NST ?};
      {? SRDO.TYP().JORG='T'
      || SRDO_POM.JORG:=SRDO.JORG:=SRST.JORG;
         EDIT_ES.JORG:=SRDO.JORG().SYMBOL;
         SRDO_POM.ODD:=SRDO.ODD:=SRST.ODD
      ?};
      {? SRDO.TYP().OSOBA='T'
      || SRDO_POM.OSOBA:=SRDO.OSOBA:=SRST.OSOBA;
         SRDO_POM.PRCD:=SRST.P().OSOBA().NAZWISKO;
         SRDO_POM.PRC:=SRST.P;
         SRDO_POM.PSTN:=SRST.P().ST().ST;
         SRDO_POM.PWYD:=SRST.P().WYDZIAL().SYMBOL;
         EDIT_ES.PRCNAZ:=SRST.P().OSOBA().NAZWISKO;
         EDIT_ES.STNAZ:=SRST.P().ST().ST;
         EDIT_ES.WYDZNAZ:=SRST.P().WYDZIAL().SYMBOL;
         {? SRDO.PRC=null()
         || SRDO.PRC:=SRST.P
         ?}
      ?};
      {? SRDO.TYP().POM='T' || SRDO_POM.POM:=SRDO.POM:=SRST.POM ?};
      {? SRDO.TYP().GR='T'
      || _klas:=SRST.GR().KST().KST;
         _obow:=exec('kst_obow','fst_kst',SSTALE.AO().RES);
         {? _klas=_obow || SRDO_POM.GR:=SRDO.GR:=SRST.GR || SRDO_POM.GR:=SRDO.GR:=null ?}
      ?};
      {? SRDO.TYP().R='T' || SRDO_POM.R:=SRDO.R:=SRST.R ?};
      {? SRDO.TYP().KONPOD='T' || SRDO_POM.KONPOD:=SRDO.KONPOD:=SRST.KONPOD ?};
      {? SRDO.TYP().KONFIN='T' || SRDO_POM.KONFIN:=SRDO.KONFIN:=SRST.KONFIN ?};
      {? FINFO.TOR_D='T'
      || {? SRDO.TYP().KONDOD='T' || SRDO_POM.KONDOD:=SRDO.KONDOD:=SRST.KONDOD ?}
      ?};

      _rodzaj:=SRST.R;
      _grupa:=SRST.GR;
      SRDO.R:=_rodzaj;
      _poj_ref:=exec('poj_srsr','samochody',SRST.SRSR);
      {? SRST.SRSR().POJAZD='T' & _poj_ref
      || _pojazd:=1;
:: środek jest zasobem w kartotece pojazdów - specjalna obsługa z polami dotyczącymi limitów
         SRDO.win_edit('RED_L_ZS');
         exec('zsb_blank','zasoby_wspolne',SRST.SRSR)
      || _pojazd:=0;
         SRDO.win_edit('RED_L')
      ?};
      POJAZDY.prefix();
      {? _pojazd=0 | (POJAZDY.seek(_poj_ref) & POJAZDY.r_lock(1,1,1))
      || exec('set_field_drdz','fst');
::       w widoku drzewa z lista j. ksiegowych zapamietuje bieżącą j. ksiegowa i po Esc odtwarza
         {? var_press('__DTREE')>0 & __DTREE>0 & OPERATOR.DEPT=null() || _odd:=ODD.ref() ?};
         tmpodd:=SRDO.ODD;

         {? SRDO.edit("exec('chk_dok','!fst_ewi_drdz',0)")
         || exec('mod_rec','fst');
            SRSR.r_unlock();
            {? SRDO.add()
            || _srdo_rf:=SRDO.ref();
               SRD.updateRecState(SRDO.OKRO_F,4);
               {? SRD.isSet() || SRD.updateElements(SRDO.OKRO_F,4) ?};
:: jeżeli pojazd to zapis zmian dla zasobu
               {? _pojazd
               || ZASOB_ZM.DATAOD:=SRDO.DO;
                  ZASOB_ZM.OSOBA:=SRDO.OSOBA;
                  ZASOB_ZM.JORG:=SRDO.JORG;
                  ZASOB_ZM.SRDO_Z:=SRDO.ref();
                  ZM_ZASOB.ZASOBY:=ZASOB_ZM.ZASOBY;
                  {? ZASOB_ZM.add()
                  || exec('zam_poprz','zasoby_wspolne')
                  ?}
               ?};

               {? SRDO.R<>_rodzaj
               || FUN.info('W przypadku zmiany rodzaju środka z trwałego na niskocenny (lub odwrotnie)\n'
                           'należy także wprowadzić dokument zmieniający metodę na odpowiednią.'@)
               ?};
               {? SRDO.GR<>null & SRDO.GR<>_grupa
               || FUN.info('W przypadku zmiany grupy środka należy także wprowadzić\n'
                           'dokument zmiany metody amortyzacji ze zmianą stawki na odpowiednią.'@)
               ?}
            ?}
         || SRSR.r_unlock();
            {? __DEPT=1 || OPERATOR.DEPT:=null; __DEPT:=0 ?};
            {? var_press('__DTREE')>0 & __DTREE>0 & OPERATOR.DEPT=null()  || ODD.seek(_odd) ?}
         ?};
         &tmpodd;
         {? _pojazd || POJAZDY.r_unlock() ?}
      || FUN.emsg('Bieżący środek jest pojazdem występującym w kartotece zasobów i jest obecnie\n'
                  'wykorzystywany przez innego użytkownika. Wprowadzanie zmian nie jest możliwe.'@)
      ?}
   ?};
   EDIT_ES.RODZ:=_tmp;
   EDIT_ES.PRCNAZ:=SRDO.SRSR().P().OSOBA().NAZWISKO;
   EDIT_ES.STNAZ:=SRDO.SRSR().P().ST().ST;
   EDIT_ES.WYDZNAZ:=SRDO.SRSR().P().WYDZIAL().SYMBOL;
   SRDT.cntx_pop();
   SRSR.r_unlock()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?};
_srdo_rf


\dok_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja dokumentu zmiany właściwości
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
{? SRDO.AUTO='T'
|| FUN.emsg('Dokument utworzony automatycznie podczas generowania dokumentów poinwentaryzacyjnych.\n'
            'Nie może być modyfikowany, może być usunięty tylko poprzez anulowanie dokumentów poinwentaryzacyjnych.'@);
   return(0)
?};
_srdo_rf:=null;
SRDO_POM.blank();
{? SRSR.r_lock(1,1,1)
|| {? SRDO.r_lock(1,1,1)
   || ZASOB_ZM.cntx_psh();
      _tmp:=EDIT_ES.RODZ;
      {? SRDO.win_sel('?')='WER_D'
      || EDIT_ES.RODZ:='D'
      || EDIT_ES.RODZ:='L'
      ?};
      __DEPT:=0;
      OSOBA.win_sel('SLO');
      _new_zm:=0;
      {? SRST.SRSR().POJAZD='T' & exec('poj_srsr','samochody',SRST.SRSR)
      || _pojazd:=1;
         SRDO.win_edit('RED_L_ZS');
         ZASOB_ZM.prefix();
         ZASOB_ZM.index('SRDO_Z');
         {? ~ZASOB_ZM.find_key(SRDO.ref())
         || _new_zm:=1;
            exec('zsb_blank','zasoby_wspolne',SRST.SRSR);
            ZM_ZASOB.ZASOBY:=ZASOB_ZM.ZASOBY;
            ZASOB_ZM.SRDO_Z:=SRDO.ref();
            ZASOB_ZM.OSOBA:=SRDO.OSOBA;
            ZASOB_ZM.JORG:=SRDO.JORG
         ?}
      || SRDO.win_edit('RED_L');
         _pojazd:=0
      ?};
      {? SRDO.JORG<>null
      || EDIT_ES.JORG:=SRDO.JORG().SYMBOL
      || EDIT_ES.JORG:=''
      ?};
      SRDO_POM.PRCD:=SRDO.SRSR().P().OSOBA().NAZWISKO;
      SRDO_POM.PRC:=SRDO.SRSR().P;
      SRDO_POM.PSTN:=SRDO.SRSR().P().ST().ST;
      SRDO_POM.PWYD:=SRDO.SRSR().P().WYDZIAL().SYMBOL;
      _grupa:=SRST.GR;
      SRDO_POM.EDIT:='T';
      exec('set_srdo_pom','fst2');

      exec('set_field_drdz','fst');
:: w widoku drzewa z lista j. ksiegowych zapamietuje bieżącą j. ksiegowa i po Esc odtwarza
      {? var_press('__DTREE')>0 & __DTREE>0 & OPERATOR.DEPT=null() || _odd:=ODD.ref() ?};
      tmpodd:=SRDO.ODD;
      {? SRDO.edit("exec('chk_dok','!fst_ewi_drdz',1)")
      || exec('mod_rec','fst');
         SRSR.r_unlock();
         {? SRDO.put()
         || _srdo_rf:=SRDO.ref();
            SRD.updateRecState(SRDO.OKRO_F,4);
            {? SRD.isSet() || SRD.updateElements(SRDO.OKRO_F,4) ?};
            {? _pojazd
            || ZASOB_ZM.DATAOD:=SRDO.DO;
               ZASOB_ZM.OSOBA:=SRDO.OSOBA;
               ZASOB_ZM.JORG:=SRDO.JORG;
               ZASOB_ZM.SRDO_Z:=SRDO.ref();
               ZM_ZASOB.ZASOBY:=ZASOB_ZM.ZASOBY;
               {? _new_zm || _dalej:=ZASOB_ZM.add() || _dalej:=ZASOB_ZM.put() ?};
               {? _dalej || exec('zam_poprz','zasoby_wspolne') ?}
            ?};
:: jeżeli dokument zmienia grupę środka
            {? SRDO.GR<>null & SRDO.GR<>_grupa
            || FUN.info('W przypadku zmiany grupy środka należy także wprowadzić\n'
                        'dokument zmiany metody amortyzacji ze zmianą stawki na odpowiednią.'@)
            ?}
         ?}
      || SRSR.r_unlock();
         {? __DEPT=1 || OPERATOR.DEPT:=null; __DEPT:=0 ?};
         {? var_press('__DTREE')>0 & __DTREE>0 & OPERATOR.DEPT=null()  || ODD.seek(_odd) ?}
      ?};
      &tmpodd;
      EDIT_ES.RODZ:=_tmp;
      ZASOB_ZM.cntx_pop();
      SRDO.r_unlock()
   || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
   ?}
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?};
_srdo_rf


\dok_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie dokumentu zmiany właściwości
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
{? SRDO.AUTO='T' & SRDO.SRXI<>null
|| FUN.emsg('Dokument utworzony automatycznie podczas generowania dokumentów poinwentaryzacyjnych.\n'
            'Nie może być modyfikowany, może być usunięty tylko poprzez anulowanie dokumentów poinwentaryzacyjnych.'@);
   return(0)
|? SRDO.AUTO='T' & SRDO.TYP().GR='T'
|| FUN.emsg('Dokument utworzony automatycznie podczas zmiany klasyfikacji środków trwałych.\n'
            'Nie może być modyfikowany, może być usunięty tylko poprzez wycofanie zmiany klasyfikacji.'@);
   return(0)
?};
_wy:=0;
_lock:=1;
_pojazd:=null();
POJAZDY.cntx_psh();
{? SRDO.SRSR().POJAZD='T'
|| ZASOB_ZM.index('SRDO_Z');
   ZASOB_ZM.prefix(SRDO.ref());
   {? ZASOB_ZM.first()
   || _pojazd:=ZASOB_ZM.ref();
      POJAZDY.index('FIRSRSR');
      POJAZDY.prefix(REF.FIRMA,SRSR.ref());
      {? POJAZDY.first()
      || {? ~POJAZDY.r_lock(1,1,1)
         || _lock:=0
         ?}
      ?};
      {? _lock
      || ZASOB_ZM.cntx_psh();
         ZASOB_ZM.prefix();
         ZASOB_ZM.SRDO_Z:=null;
         ZASOB_ZM.put();
         ZASOB_ZM.cntx_pop()
      ?}
   ?}
?};
{? _lock=0
|| FUN.emsg('Dokument dotyczy środka będącego pojazdem ewidencjonowanym w kartotece zasobów.\n'
            'Zapis w kartotece zasobów został zablokowany przez innego użytkownika.\n'
            'Usunięcie dokumentu nie jest możliwe.'@);
   POJAZDY.cntx_pop();
   return(0)
?};
{? SRDO.count()=0
|| {? SRDO.r_lock(1,1,1)
   ||
:: Kontrola czy istnieją dokumenty wystawione w poźniejszych okresach
      {? SRDO.ODD_DOK<>SRDO.ODD
      || _tmppocz:=SRDO.OKRO_F().POCZ;
         SRDO.cntx_psh();
         SRDO.index('AUTO');
         SRDO.prefix(SRDO.SRSR);
         {? SRDO.first()
         || {!
            |? {? SRDO.OKRO_F().POCZ>_tmppocz & SRDO.TYP().TYP='MT'
               || FUN.emsg('Usunięcie dokumentu zmieniającego jednostkę księgową nie jest możliwe, gdy istnieją dokumenty w poźniejszych okresach.'@);
                  SRDO.cntx_pop(); POJAZDY.cntx_pop();
                  return(0)
               |? SRDO.OKRO_F().POCZ>_tmppocz & SRDO.TYP().TYP='PU'
               || FUN.emsg('Usunięcie dokumentu zmiany właściwości nie jest możliwe, gdy istnieją dokumenty w poźniejszych okresach.'@);
                  SRDO.cntx_pop(); POJAZDY.cntx_pop();
                  return(0)
               |? SRDO.OKRO_F().POCZ>_tmppocz & SRDO.TYP().TYP='MG'
               || FUN.emsg('Usunięcie dokumentu zmiany grupy nie jest możliwe, gdy istnieją dokumenty w poźniejszych okresach.'@);
                  SRDO.cntx_pop(); POJAZDY.cntx_pop();
                  return(0)
               |? SRDO.OKRO_F().POCZ>_tmppocz
               || FUN.emsg('Usunięcie tego dokumentu nie jest możliwe, gdy istnieją dokumenty w poźniejszych okresach.'@);
                  SRDO.cntx_pop(); POJAZDY.cntx_pop();
                  return(0)
               ?};
               SRDO.next()
            !}
         ?};
         SRDO.cntx_pop()
      ?};
:: Właściwe usuwanie
      {? FUN.ask('Usunąć dokument zmiany właściwości o symbolu: %1?'@[SRDO.SYMBOL])
      || SRSR.r_unlock(); SRDO.r_unlock(); POJAZDY.r_unlock(); POJAZDY.cntx_pop();
         _okr:=SRDO.OKRO_F;
         {? SRDO.del(1,1)
         || SRD.updateRecState(_okr,4);
            {? SRD.isSet() || SRD.updateElements(_okr,4) ?};
            {? ~(cur_tab()=POJAZDY) || exec('srst_rfresh','!fst_ewi_drdz') ?};
:: jeżeli pojazd to usunięcie powiązanych z dokumentem zapisów zmian zasobów
            {? _pojazd<>null
            || ZASOB_ZM.prefix();
               {? ZASOB_ZM.seek(_pojazd)
               || ZM_ZASOB.ZASOBY:=ZASOB_ZM.ZASOBY;
                  {? ZASOB_ZM.del(,1)
                  || exec('zasob_zm_trig','zasoby_wspolne',3)
                  ?}
               ?}
            ?};
            _wy:=1
         ||
:: jeżeli pojazd to przywrócenie powiązania z dokumentem zapisu w zmianach zasobów
            {? _pojazd<>null
            || ZASOB_ZM.prefix();
               {? ZASOB_ZM.seek(_pojazd)
               || ZASOB_ZM.SRDO_Z:=SRDO.ref();
                  ZASOB_ZM.put()
               ?}
            ?}
         ?}
:: w przypadku rezygnacji
      || SRSR.r_unlock(); SRDO.r_unlock(); POJAZDY.r_unlock(); POJAZDY.cntx_pop();
         {? _pojazd<>null
         || ZASOB_ZM.prefix();
            {? ZASOB_ZM.seek(_pojazd)
            || ZASOB_ZM.SRDO_Z:=SRDO.ref();
               ZASOB_ZM.put()
            ?}
         ?}
      ?}
   || POJAZDY.cntx_pop();
      FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
   ?}
|| POJAZDY.cntx_pop();
   FUN.emsg('Bieżący dokument jest wykorzystywany w systemie i nie można go usunąć.'@)
?};
_wy


\srst_rfresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Odświeżanie okna rejestru stanów środków po zmianach w dokumentach
::----------------------------------------------------------------------------------------------------------------------
{? cur_win()='WER_D' || return() ?};
_odd:=exec('odd_count','fst');
{? EDIT_ES.R='T'
|| {? var_press('__DTREE')>0 & __DTREE>0
   || {? OPERATOR.DEPT | _odd=1 || grp_disp(SRST,'DWER_O') || grp_disp(SRST,'DWER') ?}
   || grp_disp(SRST,'WER')
   ?}
|? EDIT_ES.R='N'
|| {? var_press('__DTREE')>0 & __DTREE>0
   || {? OPERATOR.DEPT | _odd=1 || grp_disp(SRST,'DWER_N_O') || grp_disp(SRST,'DWER_N') ?}
   || grp_disp(SRST,'WER_N')
   ?}
|? EDIT_ES.R=''
|| grp_disp(SRST,'WER_U')
?}


\chk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja poprawności dokumentu
::   WE: _a = 1 - popraw, 0 - dołącz
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.TYP().NRI='T'
|| _wy:=__CHK.record(SRDO,,'SYMBOL','DW','DO','NRI')
|| _wy:=__CHK.record(SRDO,,'SYMBOL','DW','DO')
?};
{? _wy='NRI'
|| {? FUN.ask('Wygenerować nowy numer inwentarzowy?'@)
   || exec('podp_srdo_nri','fst')
   ?}
?};
{? _wy=''
|| {? SRDO.OKRO_F=null
   || FUN.emsg('Nie uzupełnione pole Okres obowiązywania\n'
               '(pole uzupełni się automatycznie po redakcji pola Data operacji).'@);
      _wy:='DO'
   ?}
?};
{? _wy='' & SRDO.OKRO_F<>null
|| {? exec('test_new_year','fst',SRDO.OKRO_F().ROK)
   || FUN.emsg('Wskazana data operacji pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='DO'
   ?}
?};
:: kontrola unikalności numeru i symbolu dokumentu
{? _wy=''
|| {? __CHK.index(SRDO,_a)<>''
   || {? FUN.ask('Wygenerować nowy numer dokumentu?'@)
      || SRDO.NR:=exec('bl_srdo_nr','fst',SRDO.NR);
         SRDO.SYMBOL:=exec('symdok','fst')
      ?};
      _wy:='SYMBOL'
   ?}
?};
:: kontrola miesiąca obowiązywania
{? _wy='' & ~exec('chk_dok_okres','fst') || _wy:='DO' ?};
{? _wy='' & SRDO.TYP().NRI='T'
|| {? ~exec('chk_nri','fst',_a) || _wy:='NRI' ?}
?};
:: kontrola czy okres jest zamknięty
{? _wy='' & SRDO.DW<>date(0,0,0)
|| _okres:=exec('find_okro_f','fst', SRDO.DW);
   {? ~exec('okr_mod','fst',_okres)
   || _wy:='DW'
   ?}
?};
:: kontrola daty wystawienia i daty eksploatacji środka
{? _wy='' & exec('ae_srdo_dw','fst')=0 || _wy:='DW' ?};
:: kontrola daty wystawienia i daty dofinansowania
{? _wy='' & SRDO.SRZF<>null & (SRDO.DW<SRDO.SRZF().DATA | SRDO.DO<SRDO.SRZF().DATA)
|| FUN.emsg('Data wystawienia dokumentu i data operacji nie mogą być wcześniejsze niż data dofinansowania.'@);
   {? SRDO.DW<SRDO.SRZF().DATA || _wy:='DW' || _wy:='DO' ?}
?};
:: pomieszczenie - weryfikacja czy pochodzi z jednostki zgodnej z dokumentem i/lub środkiem
{? _wy='' & SRDO.TYP().POM='T' & SRDO.POM<>null
   & ((SRDO.ODD<>null & SRDO.POM().ODD<>SRDO.ODD) | (SRDO.ODD=null & SRST.ODD<>SRDO.POM().ODD))
|| FUN.emsg('Wybrane pomieszczenie jest przypisane do innej jednostki księgowej niż\n'
            'wskazana w dokumencie lub aktualnie przypisana do środka.'@);
   _wy:='POM'
?};
:: kontrola pól z zestawami kont kosztowych
{? _wy=''
|| {? SRDO.TYP().KONPOD='T' & SRDO.KONPOD=null
   || FUN.emsg('Należy wypełnić pole Konta amortyzacji podatkowej.'@);
      _wy:='KONPOD'
   ?}
?};
{? _wy='' & SRDO.TYP().KONPOD='T' & SRDO.KONPOD<>null
|| {? ~exec('zestaw_kont_ok','fst',SRDO.KONPOD)
   || FUN.emsg('Wybrany zestaw kont dla toru podatkowego jest niepoprawnie zdefiniowany.'@);
      _wy:='KONPOD'
   ?}
?};
{? _wy=''
|| {? SRDO.TYP().KONFIN='T' & SRDO.KONFIN=null
   || FUN.emsg('Należy wypełnić pole Konta amortyzacji bilansowej.'@);
      _wy:='KONFIN'
   ?}
?};
{? _wy='' & SRDO.TYP().KONFIN='T' & SRDO.KONFIN<>null
|| {? ~exec('zestaw_kont_ok','fst',SRDO.KONFIN)
   || FUN.emsg('Wybrany zestaw kont dla toru bilansowego jest niepoprawnie zdefiniowany.'@);
      _wy:='KONFIN'
   ?}
?};
{? _wy='' & FINFO.TOR_D='T'
|| {? SRDO.TYP().KONDOD='T' & SRDO.KONDOD=null
   || FUN.emsg('Należy wypełnić pole Konta amortyzacji dodatkowej.'@);
      _wy:='KONDOD'
   ?};
   {? _wy='' & SRDO.TYP().KONDOD='T' & SRDO.KONDOD<>null
   || {? ~exec('zestaw_kont_ok','fst',SRDO.KONDOD)
      || FUN.emsg('Wybrany zestaw kont dla toru dodatkowego jest niepoprawnie zdefiniowany.'@);
         _wy:='KONDOD'
      ?}
   ?}
?};
:: kontrola czy zmiana nie dotyczy zadekretowanej amortyzacji w przypadku dokumentów wartościowych
:: lub dokumentów zmiany właściwości dotyczących zestawów kont kosztowych
{? _wy='' & (SRDO.RODZ='W'
             | (SRDO.RODZ='L' & (SRDO.TYP().KONPOD='T'
                                 | SRDO.TYP().KONFIN='T'
                                 | (FINFO.TOR_D='T' & SRDO.TYP().KONDOD='T'))))
|| _dekret:=0;
   _konto:=0;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRSR.ref());
   {? SRST.find_key(SRDO.ROK_F,SRDO.OKRO_F)
   || {? SRDO.RODZ='L'
      || {? SRST.KONPOD<>SRDO.KONPOD | SRST.KONFIN<>SRDO.KONFIN | (FINFO.TOR_D='T' & SRST.KONDOD<>SRDO.KONDOD)
         || _konto:=1
         ?}
      ?};
      {! |?
         {? SRST.DEKRET='T' || _dekret:=1 ?};
         _dekret=0 & SRST.next()
      !}
   ?};
   SRST.cntx_pop();
   {? _dekret=1 & (SRDO.RODZ='W' | (SRDO.RODZ='L' & _konto=1))
   ||  FUN.emsg('Amortyzacja w okresie obowiązywania dokumentu lub w późniejszym została już\n'
                'zadekretowana dla co najmniej jednego toru amortyzacji. Usuwanie amortyzacji\n'
                'lub wprowadzanie zmian mogących wpłynąć na amortyzację nie jest możliwe.'@);
      _wy:='DO'
   ?}
?};
:: kontrola czy wprowadzenie dokumentu nie spowoduje zdublowania dat zapisów zmian dla zasobu
:: co spowoduje błąd unikalności indeksu w systemie
{? _wy='' & SRDO.SRSR().POJAZD='T'
|| _zsb_zm:=0;
   _pojazd:=exec('poj_srsr','samochody',SRDO.SRSR);
   {? _pojazd
   || POJAZDY.cntx_psh();
      POJAZDY.prefix();
      {? POJAZDY.seek(_pojazd)
      || ZASOBY.cntx_psh();
         ZASOBY.index('TYP');
         ZASOBY.prefix('P',POJAZDY.ID);
         {? ZASOBY.first()
         || ZASOB_ZM.cntx_psh();
            ZASOB_ZM.index('DISP');
            ZASOB_ZM.prefix(ZASOBY.ref(),SRDO.DO);
            {? ZASOB_ZM.first() & (_a=0 | (_a=1 & ZASOB_ZM.SRDO_Z<>SRDO.ref()))
            || _zsb_zm:=1
            ?};
            ZASOB_ZM.cntx_pop()
         ?};
         ZASOBY.cntx_pop()
      ?};
      POJAZDY.cntx_pop()
   ?};
   {? _zsb_zm
   || FUN.emsg('Środek trwały jest pojazdem ewidencjonowanym w kartotece zasobów\n'
               'a dla podanej daty operacji (%1) w systemie istnieje już zapis\n'
               'zmiany dla zasobu. Należy wprowadzić inną datę.'@[$SRDO.DO]);
      _wy:='DO'
   ?}
?};
::kontrola czy istnieją inne dokumenty, wystawione później niż wpisany okres obowiązywania
{? _wy='' & tmpodd<>SRDO.ODD
|| _tmppocz:=SRDO.OKRO_F().POCZ;
   SRDO.cntx_psh();
   SRDO.index('AUTO');
   SRDO.prefix(SRDO.SRSR);
   {? SRDO.first()
   || {!
      |? {? SRDO.OKRO_F().POCZ>_tmppocz
         || _wy:='OKRO_F';
             FUN.emsg('Nie można zmienić jednostki księgowej, gdy istnieją dokumenty w poźniejszych okresach.'@)
         ?};
         _wy='' & SRDO.next()
      !}
   ?};
   SRDO.cntx_pop()
?};
_wy


\be_srdo_pomi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRDO.POMI
::----------------------------------------------------------------------------------------------------------------------
{? OPERATOR.DEPT=null & SRDO.ODD<>null
|| OPERATOR.DEPT:=SRDO.ODD;
   __DEPT:=1
?};
1


\ae_srdo_pomi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji pola SRDO.POMI
::----------------------------------------------------------------------------------------------------------------------
{? __DEPT=1
|| OPERATOR.DEPT:=null;
   __DEPT:=0
?};
1


\ae_srdo_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji pola SRDO.R
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(207)='T' & SRDO.R=SRST.R & SRDO.NRI<>SRST.NRI
|| SRDO.NRI:=SRST.NRI
|? PAR_SKID.get(207)='T' & (SRDO.R<>SRST.R | 1+SRDO.NRI<>SRDO.R)
|| SRSR.cntx_psh();
   SRSR.R:=SRDO.R;
   _formula:=exec('nri_form','fst');
   {? _formula<>'' || SRDO.NRI:=($(_formula))() ?};
   SRSR.cntx_pop()
?}
;
1


\be_srdo_konpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRDO.KONPOD
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='P';
ZSKON.win_dict('WYB_P');
1


\be_srdo_konfin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRDO.KONFIN
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='F';
ZSKON.win_dict('WYB_F');
1


\be_srdo_kondod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRDO.KONDOD
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='D';
ZSKON.win_dict('WYB_D');
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 0337522041a8d3921c93694959fd6a165c51d4b022156d79d7812677cfaad0bfb7d1de829b5bdd5d5ed83a9845829befd37d35031369e5fed50e9d58bcf36ce4fc49d5fc3b9f4ebe2ade82294fc02cef7ceecdfc19c7f939e441a86727dbd3536daaf372a4fe7c69e7f562211084dde1fe0d9675d1c46eb85d7c0c740b1e20d2

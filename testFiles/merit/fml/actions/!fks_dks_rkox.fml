:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_rkox.fml
:: Utworzony: 05.08.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_RKOX - Dekretacja rozliczenia kosztów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja rozliczenia kosztów - główna formuła czynności FKS_DKS_RKOX.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
::# parses=exec('parses','!fks_dks_rkox')
::# access=exec('uprawnienia','!fks_dks_rkox')
::
:: PARAMETRY WE:
::# kind=WE, symbol=ROW_NAG, type=_ROW_NAG, name=Wskazanie na wykonania rozliczenia kosztów, required=T, keyref=T

:: PARAMETRY WY:
::# kind=WY, symbol=DOK,  type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=T

_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
:: czynność startowa
{? var_pres('ROW_NAG',_we)
|| {? _we.ROW_NAG=null
   || {? _mp.isAutoRun()
      || _mp.done()
      || FUN.info('Nie podano parametru wejściowego ROW_NAG.'@); _mp.error()
      ?}
   || DOK.cntx_psh();
      _ref:=BB.sqlint($_we.ROW_NAG); _nam:=form(($_we.ROW_NAG)-8);
      {? _ref<>0 & _nam<>''
      || ROW_NAG.cntx_psh(); ROW_NAG.use(_nam); ROW_NAG.prefix();
         {? ROW_NAG.seek(_ref,_nam)
         || exec('roz_dek1','!fks_dks_rkox',_args)
         || {? ~_mp.isAutoRun()
            || FUN.info('Nie znaleziono rozliczenia kosztów.'@)
            ?};
            _wy.DOK:=null;
            _mp.save(,_wy);
            _mp.done()
         ?};
         ROW_NAG.cntx_pop()
      ?};
      DOK.cntx_pop()
   ?}
|| {? ~_mp.isAutoRun()
   || FUN.info('Nie znaleziono rozliczenia kosztów.'@)
   ?};
   _wy.DOK:=null;
   _mp.save(,_wy);
   _mp.done()
?};
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ dekretację rozliczenia kosztów pośrednich'@@;
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('ROW_NAG',_in)<>type_of(~~)
|| ROW_NAG.cntx_psh(); ROW_NAG.use(ref_name(_in.ROW_NAG)); ROW_NAG.prefix();
   {? ROW_NAG.seek(_in.ROW_NAG)
   || _desc:='Zakończ dekretację rozliczenia kosztów pośrednich %1 w jedn. ks. %2, okres %3 %4'@@[ROW_NAG.ROZ_NAZ().NAZWA,ROW_NAG.ODD().OD,ROW_NAG.OKRES().NAZ,ROW_NAG.OKRES().ROK().NAZ]
   ?};
   ROW_NAG.cntx_pop()
?};
_desc


\roz_dek1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Dekretacja rozliczeń kosztów
::  OLD: \roz_dek/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROW_NAG.ZAKS='T'
|| FUN.info('Rozliczenie zostało już zadekretowane.'@); _a.mp.done()
|| _ok:=1;
   {? ROW_NAG.ROZ_NAZ().TYP='R'
   || ROW_DOC.index('KONTO'); ROW_DOC.prefix(ROW_NAG.ref());
      {? ROW_DOC.first()
      || _n:=0;
         {! |? {? ROW_DOC.KWOTA$6<>0  || _n:=1; 0 || ROW_DOC.next ?} !};
         {? _n=0
         || FUN.info('Nie naliczono narzutów dla rozdzielnika.'@); _ok:=0
         ?}
      || FUN.info('Brak pozycji rozdzielnika.'@); _ok:=0
      ?}
   ?};
   {? _ok
   || ROW_KON.index('KONTO'); ROW_KON.prefix(ROW_NAG.ref());
      _ok:={? ROW_KON.first()
           || 1
           || FUN.info('Brak wartości kont źródłowych.'@); 0
           ?}
   ?};
   {? ~_ok
   || _a.mp.cancel()
   || DOK.win_edit('KSWYKRK');
      _okno:=REJ.win_dict('?'); REJ.win_dict('SLO1');
      DOK.prefix();
      DOK.blank(0);
      DOK.RVAT:=DOK.REJ:=DOK.DOK_REJ:=null;
      ROZRACH.OPIS:=DOK.TR:=ROW_NAG.ROZ_NAZ().NAZWA;
      DOK.NK:='';
      DOK.DTO:=SSTALE.AO().KON;
      _odd:=OPERATOR.DEPT;
      OPERATOR.DEPT:=ROW_NAG.ODD;
      KOSZTY.WYBOR1:='N'; czy_podp:=0;
      {? DOK.edit("exec('chk_dok','dok_fks')")
      || DOK.cntx_psh;
         DOK.index('REJ');
         DOK.prefix(DOK.REJ);
         _numer:={? DOK.last || DOK.NR+1 || 1 ?};
         DOK.cntx_pop();
         DOK.NR:=_numer;
         DOK.prefix();
         DOK.JPK_V_T:=exec('typ_dok','obiegi2');
         DOK.DR:=date();
         DOK.ZAR:=exec('usr_zar','dok_fks');
         DOK.A:=DOK.ZK:=DOK.ZP:=DOK.WSK_XD:='N';
         DOK.ODD:=OPERATOR.DEPT;
         DOK.DOKZRODL:='row_nag'+$(#ROW_NAG.ref);
         do();
         {? DOK.add()
         || exec('pop_poz','dok_fks','DOK',0); exec('roz_poz','!fks_dks_rkox');
            {? ROW_NAG.ZAKS='T'
            || _a.out.DOK:=DOK.ref();
               _a.mp.save(,_a.out);
               _a.mp.done()
            ?}
         ?};
         end()
      ?};
      VAR_DEL.delete('pamietaj','czy_podp');
      OPERATOR.DEPT:=_odd; REJ.win_dict(_okno)
   ?}
?}


\roz_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'dekretacja -> Dekretuj' w okienku WER tabeli ROW_NAG
::  OLD: \roz_dek/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_DKS_RKOX';
_params.UIDREF:=ROW_NAG.uidref;
_params.AKCJA:='Dekretuj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ROW_NAG',ROW_NAG.ref());
exec('mp_run','#b__box',_params)


\main_roz_kosz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja rozliczeń kosztów z menu dokumentów
::----------------------------------------------------------------------------------------------------------------------
{? exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER)
|| return(0)
?};
SSTALE.AR();
ROW_NAG.cntx_psh();
ROW_NAG.win_sel('WER'); ROW_NAG.hdr_sel(); ROW_NAG.hdr_sel(' - %1 %2'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ]);
{? (SSTALE.AO().ZAM='T' | exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER,0)) || ROW_NAG.actions('WER','DUO:D') || ROW_NAG.actions('WER','DU') ?};
ROW_NAG.win_edit('RED');
{? ~OPERATOR.DEPT
|| ROW_NAG.index('OKRES'); ROW_NAG.prefix(SSTALE.AO)
|| ROW_NAG.index('OKR_ODD'); ROW_NAG.prefix(SSTALE.AO,OPERATOR.DEPT)
?};
ROW_NAG.select();
ROW_NAG.cntx_pop()


\roz_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Formula tworzaca pozycje dekretow dla wykonania rozliczenia
::  OLD: \roz_poz/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_wyr:=obj_new(6);   _roz:=obj_new(6);
POZ.index('DOK');
POZ.prefix(DOK.ref());
ROW_KON.index('KONTO'); ROW_KON.prefix(ROW_NAG.ref);
{? ROW_KON.first()
|| _konto:=''; {! _i:=1..6 |! _wyr[_i]:=0 !}; _kw:=0; _strona:='';
   {! _i:=1..6 |! _roz[_i]:=0 !}; _ref:=ROW_KON.ref();
   {!|? _ok:=1;
        {! _i:=1..6 |!
           {? _ok & _roz[_i]=0 & _wyr[_i]<>($('#ROW_KON.WYR'+$_i))() || _ok:=0  ?}
        !};
        {? _ok=0 | _konto<>ROW_KON.KONTOZ
        || {? _kw$2<>0
           || {? exec('poz_add','dekret_aut',_kw,_strona,_konto,'','',date(0,0,0),date(0,0,0),ROZRACH.OPIS)
                 & POZ.last()
              || exec('uzu_wyr','!fks_dks_rkox',_ref)
              ?};
              _ref:=ROW_KON.ref()
           ?};
           _konto:=ROW_KON.KONTOZ; _strona:={? ROW_KON.STR='Wn' || 'Ma' || 'Wn' ?}; _kw:=0;
           KS_W.index('LP');
           KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+_konto));
           {? KS_W.first()
           || {! _i:=1..6 |!
                 {? KS_W.find_key(_i)
                 || _wyr[_i]:={? KS_W.ROZDZ='T' || _roz[_i]:=1; 0 || _roz[_i]:=0; ($('#ROW_KON.WYR'+$_i))() ?}
                 || _roz[_i]:=0; _wyr[_i]:=0
                 ?}
              !}
           ?}
        ?};
        _kw+=ROW_KON.KWOTA;
        ROW_KON.next()
   !};
   {? _kw$2<>0
   || {? exec('poz_add','dekret_aut',_kw,_strona,_konto,'','',date(0,0,0),date(0,0,0),ROZRACH.OPIS)
         & POZ.last()
      || exec('uzu_wyr','!fks_dks_rkox',_ref,1)
      ?}
   ?}
?};
ROW_DOC.index('KONTO');  ROW_DOC.prefix(ROW_NAG.ref);
_typ:=ROW_NAG.ROZ_NAZ().TYP;
{? _typ='R'
|| {? ROW_DOC.first()
   || _konto:='';  _ref:=ROW_DOC.ref(); {! _i:=1..6 |! _wyr[_i]:=0 !}; _kw:=0; _strona:='';
      {! _i:=1..6 |! _roz[_i]:=0 !};
      {!|? _ok:=1;
           {! _i:=1..6 |!
              {? _ok & _roz[_i]=0 & _wyr[_i]<>($('#ROW_DOC.WYR'+$_i))() || _ok:=0  ?}
           !};
           {? _ok=0 | _konto<>ROW_DOC.KONTODC
           || {? _kw$2<>0
              || {? exec('poz_add','dekret_aut',_kw,_strona,_konto,'','',date(0,0,0),date(0,0,0),ROZRACH.OPIS)
                    & POZ.last()
                 || exec('uzu_doc','!fks_dks_rkox',_ref)
                 ?};
                 _ref:=ROW_DOC.ref()
              ?};
              _konto:=ROW_DOC.KONTODC; _strona:='Wn'; _kw:=0;
              KS_W.index('LP');
              KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+_konto));
              {? KS_W.first()
              || {! _i:=1..6 |!
                    {? KS_W.find_key(_i)
                    || _wyr[_i]:={? KS_W.ROZDZ='T' || _roz[_i]:=1; 0 || _roz[_i]:=0; ($('#ROW_DOC.WYR'+$_i))() ?}
                    || _roz[_i]:=0; _wyr[_i]:=0
                    ?}
                 !}
              ?}
           ?};
           _kw+=ROW_DOC.KWOTA;
           ROW_DOC.next()
      !};
      {? _kw$2<>0
      || {? exec('poz_add','dekret_aut',_kw,_strona,_konto,'','',date(0,0,0),date(0,0,0),ROZRACH.OPIS)
            & POZ.last()
         || exec('uzu_doc','!fks_dks_rkox',_ref,1)
         ?}
      ?}
   ?}
|| {? ROW_DOC.first()
   || {!|? {? exec('poz_add','dekret_aut',ROW_DOC.KWOTA,ROW_DOC.STR,ROW_DOC.KONTODC,'','',
              date(0,0,0),date(0,0,0),ROZRACH.OPIS) & POZ.last()
           || ROW_WYR.index('ROW_WYR');
              ROW_WYR.prefix(ROW_DOC.ref());
              {? ROW_WYR.first()
              || POW.index('POZ');
                 POW.prefix(POZ.ref);
                 {? POW.first
                 || {!|? {? ROW_WYR.find_key(POW.LP) || POW.del() || POW.next() ?} !};
                    ROW_WYR.first()
                 ?};
                 {!|? POW.blank();
                      POW.LP:=ROW_WYR.LP;
                      POW.SLU:=ROW_WYR.SLU;
                      POW.SLO:=ROW_WYR.SLO;
                      POW.KW:=ROW_WYR.KW;
                      POW.add();
                      ROW_WYR.next()
                 !}
              ?}
           ?};
           ROW_DOC.next()
       !}
   ?}
?};
_user:=username();
ROW_NAG.ZAKS:='T';
ROW_NAG.KTOZ:={? _user*'~' || (_user*'~'-1)+_user || 10+_user ?};
ROW_NAG.SLAD:=DOK.REJ().KOD+((8-+DOK.REJ().KOD)*'~')+'/'+$DOK.NR;
{? var_pres('ind_row')>0 ||  DOK.DOKZRODL:='row_nag'+$(#ROW_NAG.ref); DOK.put() ?};
{? ~ROW_NAG.put() || undo() ?}


\uzu_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Uzupelnienie wyroznikow dla pozycji dekretu na podstawie rekordow
::       tabeli ROW_DOC
::   WE: _a - ref ROW_DOC, na ktorym konczy sie uzupelnianie wyroznikow
::      [_b - jesli jest nalezy rozpoczac uzupelnianie od ostatniego w
::            dziedzinie rekordu ROW_DOC, w.p.p. nalezy rozpoczac od
::            poprzedniego rekordu]
::  OLD: \uzu_doc/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_DOC.cntx_psh();
{? _=2 || ROW_DOC.last() || ROW_DOC.prev() ?};
SLO.prefix();
POW.index('POZ'); POW.prefix();
_czy:=obj_new(6); {! _i:=1..6 |! _czy[_i]:=0 !};
KS_W.index('LP');
KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROW_DOC.KONTODC));
{! |?
   {! _i:=1..6
   |! _wyr:=($('ROW_DOC.WYR'+$_i))();
      {? _wyr<>null & SLO.seek(_wyr) & KS_W.find_key(_i)
      || {? _czy[_i]=0
         || POW.prefix(POZ.ref,_i);
            {? POW.first() || {!|? POW.del !} ?};
            POW.prefix(); _czy[_i]:=1
         ?};
         {? POW.find_key(POZ.ref,_i,KS_W.SLU,SLO.ref)
         || POW.KW+=ROW_DOC.KWOTA; POW.put()
         || POW.blank();
            POW.SLO:=SLO.ref;
            POW.KW:=ROW_DOC.KWOTA;
            POW.add(1)
         ?}
      ?}
   !};
   ROW_DOC.ref<>_a & ROW_DOC.prev()
!};
ROW_DOC.cntx_pop()


\uzu_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Uzupelnienie wyroznikow dla pozycji dekretu na podstawie rekordow
::       tabeli ROW_KON
::   WE: _a - ref ROW_KON, na ktorym konczy sie uzupelnianie wyroznikow
::      [_b - jesli jest nalezy rozpoczac uzupelnianie od ostatniego w
::            dziedzinie rekordu ROW_KON, w.p.p. nalezy rozpoczac od
::            poprzedniego rekordu]
::  OLD: \uzu_wyr/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_KON.cntx_psh();
{? _=2 || ROW_KON.last() || ROW_KON.prev() ?};
SLO.prefix();
POW.index('POZ');
POW.prefix();
KS_W.index('LP');
_czy:=obj_new(6); {! _i:=1..6 |! _czy[_i]:=0 !};
KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROW_KON.KONTOZ));
{! |?
   {! _i:=1..6
   |! _wyr:=($('ROW_KON.WYR'+$_i))();
      {? _wyr<>null & SLO.seek(_wyr) & KS_W.find_key(_i)
      || {? _czy[_i]=0
         || POW.prefix(POZ.ref,_i);
            {? POW.first() || {!|? POW.del !} ?};
            POW.prefix(); _czy[_i]:=1
         ?};
         {? POW.find_key(POZ.ref,_i,KS_W.SLU,SLO.ref)
         || POW.KW+=ROW_KON.KWOTA; POW.put()
         || POW.blank();
            POW.SLO:=SLO.ref;
            POW.KW:=ROW_KON.KWOTA;
            POW.add(1)
         ?}
      ?}
   !};
   ROW_KON.ref<>_a & ROW_KON.prev()
!};
ROW_KON.cntx_pop()


\czy_rdek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Formula sprawdzajaca, czy biezace wykonanie rozliczenia jest
::       zadekretowane
::  OLD: \czy_rdek/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROW_NAG.ZAKS<>'T'
|| FUN.info('Akcja dostępna tylko dla zadekretowanych rozliczeń.'@); 0
|| 1
?}


\dek_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'dekreTacja -> Pozycje dokumentu' w okienku WER tabeli ROW_NAG
::  OLD: \dek_poz/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
REJ.cntx_psh(); DOK.cntx_psh();
{? exec('fnd_dok','!fks_dks_rkox') || exec('poz_dok','dok_fks') ?};
REJ.cntx_pop(); DOK.cntx_pop()


\fnd_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Funkcja znajdujaca naglowek dokumentu dla zadekretowanego wykonania
::  OLD: \fnd_dok/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROW_NAG.SLAD*'~'
|| _rej:=((ROW_NAG.SLAD*'~')-1)+ROW_NAG.SLAD
|| _rej:=8+ROW_NAG.SLAD
?};
REJ.index('KOD'); REJ.prefix(SSTALE.AR,ROW_NAG.ODD);
{? REJ.find_key(_rej,_rej)
|| DOK.index('REJ');
   DOK.prefix(REJ.ref,#(((ROW_NAG.SLAD*'/'))-ROW_NAG.SLAD));
   DOK.first()
?}


\dek_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'dekreTacja -> Naglowek dokumentu' w okienku WER tabeli ROW_NAG
::  OLD: \dek_nag/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
REJ.cntx_psh(); DOK.cntx_psh();
{? exec('fnd_dok','!fks_dks_rkox')
|| DOK.win_edit('DOK_PRO');
   exec('gr_bufor','dok_fks','DOK');
   DOK.display()
?};
REJ.cntx_pop(); DOK.cntx_pop()


\dek_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'dekreTacja -> Usun dekretacje' w okienku WER tabeli ROW_NAG
::  OLD: \dek_usu/ks_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
REJ.cntx_psh(); DOK.cntx_psh();
{? exec('fnd_dok','!fks_dks_rkox')
|| {? DOK.A='T'
   || FUN.info('Dokument zaakceptowany.\nUsunięcie niemożliwe.'@)
   || exec('dok_usun','dok_fks1')
   ?}
|| ROW_NAG.ZAKS:='N'; ROW_NAG.KTOZ:=ROW_NAG.SLAD:='';  ROW_NAG.put()
?};
REJ.cntx_pop(); DOK.cntx_pop()


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła ustala PARSES dla czynności na podstawie rekordu tabeli ROW_NAG,
::       wskazanie na rekord tabeli ROW_NAG jest przekazane poprzez parametr ROW_NAG czynności.
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_in:=params_get().in;
_out:=params_get().out;
{? var_pres('ROW_NAG',_in) & var_pres('ROW_NAG',_in)=type_of(null()) & _in.ROW_NAG<>null()
|| _maska:=form(($_in.ROW_NAG)-8);
   ROW_NAG.cntx_psh(); ROW_NAG.use(_maska); ROW_NAG.prefix();
   {? ROW_NAG.seek(_in.ROW_NAG,_maska)
   || __PARSES.setVal('OkresRok',ROW_NAG.OKRES);
      _result:=1
   || FUN.emsg('Nie znaleziono rozliczenia kosztów.'@); params_get().mp.error()
   ?};
   ROW_NAG.cntx_pop()
|| FUN.emsg('Nie znaleziono rozliczenia kosztów.'@); params_get().mp.error()
?};
_result


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
{? var_pres('ROW_NAG',_in) & var_pres('ROW_NAG',_in)=type_of(null()) & _in.ROW_NAG<>null()
|| USERS.cntx_psh(); USERS.clear();
   {? USERS.seek(_user)
   || ROW_NAG.cntx_psh();
      ROW_NAG.prefix();
      {? ROW_NAG.seek(_in.ROW_NAG,form(($_in.ROW_NAG)-8))
      || {? exec('usr_fjks','b_perm',ROW_NAG.ODD().OD,USERS.ref())
         || _result:=1
         ?}
      ?};
      ROW_NAG.cntx_pop()
   ?};
   USERS.cntx_pop()
:: czy w ogóle user ma uprawnienia do jakiejkolwiek jednostki księgowej
|| {? exec('usr_fjks_any','b_perm',USERS.ref())
   || _result:=1
   ?}
?};
_result


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'row_nag'>0
         || _record:=exec('FindAndGet','#table',ROW_NAG,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 659c7096a259434d9d5a07e363d19cae8cd701106c8bfa41687c8a6b1a1f9cc9d1e61eef10e107ae58ff85e2eee1ca4d96e32af57b511552ddea0f44b7d81a4b1b21db0ceb0a9c079be864d2866952abd042125b1c36f5e15f04be44fde6ebbd2328636c1b7a4c0d10c5d00779f51275b4d3f16a46fb14954b80b9399541fe74

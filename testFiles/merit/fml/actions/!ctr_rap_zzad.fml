:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_rap_zzad.fml
:: Utworzony: 25.02.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_RAP_ZZAD - Definiowanie arkuszy raportów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Definiowanie arkuszy raportów - główna formuła czynności CTR_RAP_ZZAD
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego - Definiowanie arkuszy raportów
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::  OLD: \k_raporty_s/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_RAP_U.dnd_sel('SLO',,'records.USERS',"
   {? K_RAPORT.AKC='N'
   || USERS.cntx_psh();
      USERS.prefix();
      _tab:=dnd_info('dropped_records');
      {? _tab.first()
      || {!
         |? {? USERS.seek(_tab.REF,)
            || exec('add_usr','!ctr_rap_zzad')
            ?};
            _tab.next()
         !}
      ?};
      USERS.cntx_pop()
   || FUN.info('Raport zaakceptowany.'@)
   ?}
");
K_RAPORT.dnd_sel('WER',,'records.USERS',"
   K_RAPORT.cntx_psh();
   USERS.cntx_psh(); USERS.prefix();
   _ref:=dnd_info('dest_record');
   {? K_RAPORT.seek(_ref)
   || {? K_RAPORT.AKC='N'
      || _tab:=dnd_info('dropped_records');
         {? _tab.first()
         || {!
            |? {? USERS.seek(_tab.REF,)
               || K_RAP_U.K_RAPORT:=K_RAPORT.ref();
                  K_RAP_U.USERS:=USERS.ref();
                  K_RAP_U.cntx_psh(); K_RAP_U.prefix();
                  {? ~K_RAP_U.find_rec()
                  || K_RAP_U.add()
                  ?};
                  K_RAP_U.cntx_pop()
               ?};
               _tab.next()
            !}
         ?}
      || FUN.info('Raport zaakceptowany.'@)
      ?}
   ?};
   K_RAPORT.cntx_pop(); USERS.cntx_pop()
");
USERS.dnd_sel('SLO2',,'records.K_RAP_U',"
   _tab:=dnd_info('dropped_records');
   {? _tab.first()
   || _one:=_tab.size()=1;
      {!
      |? {? K_RAP_U.seek(_tab.REF,)
         || exec('del_usr','!ctr_rap_zzad',_one)
         ?};
         _tab.next()
      !}
   ?};
   grp_disp(K_RAP_U,'SLO')
");
SKID_MBN.win_sel('SLO');
K_RAPORT.win_edit('RED');
UNPAR.P10:='W'; UNPAR.P10_BV:=UNPAR.P10_BE:=UNPAR.P10_AE:='';
exec('set_zak_rap','!ctr_rap_zzad');
_b.grp_sel(_c,K_RAPORT,'WER',_a,"exec('arf_k_raporty','!ctr_rap_zzad')",,,,"exec('set_k_raporty','!ctr_rap_zzad','E')",,,,'maximized');
task_attach('CTR_RAP_ZZAD');
::_b.grp_sel(_c,,'WER','Z usług raportowania',"exec('arf_k_raporty','!ctr_rap_zzad')",,,,"exec('set_k_raporty','!ctr_rap_zzad','R')",,,,'maximized');
_b.tab_splt(_c,,'horizontal','panel1',12);
_b.grp_sel(_c,K_RAP_U,'SLO',,,,,,,,,,'maximized_with_title');
_b.tab_splt(_c,'panel1','vertical','panel2');
_b.grp_sel(_c,USERS,'SLO2',,"
   {? USERS.sel_size()=0
   || VAR_DEL.delete('UserRap');
      exec('bf_k_raporty','ctr',0)
   ?}
",,,,,"
   VAR_DEL.delete('SelUsr');
   {? USERS.sel_size()
   || SelUsr:=USERS.sel_aget()
   ?};
   1
",,,'maximized_with_title');
~~


\set_zak_rap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Ustawia dziedzine raportow
::  OLD: \set_zak_rap/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_RAPORT.win_sel('WER');
K_RAPORT.hdr_sel();
{? UNPAR.P10='W'
|| K_RAPORT.index('NAZWA2'); K_RAPORT.prefix(REF.S_FIRMA,__KRTyp);
   K_RAPORT.hdr_sel(' - wszystkie'@)
|| K_RAPORT.index('AKC'); K_RAPORT.prefix(REF.S_FIRMA,__KRTyp,UNPAR.P10);
   {? UNPAR.P10='T'
   || K_RAPORT.hdr_sel(' - zaakceptowane'@)
   || K_RAPORT.hdr_sel(' - niezaakceptowane'@)
   ?}
?}


\arf_k_raporty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Po odswiezeniu okna WER w oknie grupowym tabeli K_RAPORTY
::  OLD: \arf_k_raporty/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_RAP_U.index('K_RAPORT');
{? K_RAPORT.get()
|| K_RAP_U.prefix(K_RAPORT.ref())
|| K_RAP_U.prefix(null)
?};
grp_disp(K_RAP_U,'SLO')


\set_k_raporty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Ustawia dziedzine raportow
::   WE: _a - typ raportu
::  OLD: \set_k_raporty/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_RAPORT.index('NAZWA2');
K_RAPORT.prefix(REF.S_FIRMA,_a);
__KRTyp:=_a;
exec('set_zak_rap','!ctr_rap_zzad')


\bl_krap_utw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Wartosc poczatkowa pola K_RAPORT.KTO_UTW
::  OLD: \bl_krap_utw/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
OPERATOR.USER


\bl_k_raport_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Wartosc poczatkowa pola K_RAPORT.TYP
::  OLD: \bl_k_raport_typ/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__KRTyp')>0
|| __KRTyp
|| 'E'
?}


\ba_k_raport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Przed dodaniem raportu dla controllingu
::  OLD: \ba_k_raport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh();
{? __KRTyp='E'
|| K_RAPORT.win_edit('RED')
|| K_RAPORT.win_edit('RED2')
?};
K_RAPORT.blank();
K_RAPORT.bl_file('EXCEL',1);
{? K_RAPORT.edit("exec('ar_k_raport','!ctr_rap_zzad')")
|| K_RAPORT.cntx_psh(); K_RAPORT.prefix();
   {? K_RAPORT.add()
   || {? (_file:=K_RAPORT.bl_file('EXCEL'))<>''
      || K_RAPORT.bl_put('EXCEL',_file)
      ?}
   ?};
   K_RAPORT.cntx_pop()
?};
K_RAPORT.bl_file('EXCEL',1);
USERS.cntx_pop()


\ar_k_raport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord po tabeli K_RAPORT
::  OLD: \ar_k_raport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_r:={? __KRTyp='E' || __CHK.record(K_RAPORT,,'NAZWA','EXCEL') || __CHK.record(K_RAPORT,,'NAZWA','LINK_RS') ?};
{? _r=''
|| _ref:=K_RAPORT.ref();
   K_RAPORT.cntx_psh();
   K_RAPORT.index('NAZWA'); K_RAPORT.prefix(REF.FIRMA,K_RAPORT.NAZWA,);
   {? K_RAPORT.first() & (-menu_txt()<>'popraw' | K_RAPORT.ref()<>_ref)
   || FUN.info('Istnieje raport o nazwie: %1.'@[K_RAPORT.NAZWA]);
      _r:='NAZWA'
   ?};
   K_RAPORT.cntx_pop()
?};
{? _r='' & __KRTyp='E' & -menu_txt()<>'kopiuj'
|| _file:=K_RAPORT.bl_file('EXCEL');
   {? _file<>'' & _file+4<>'xlsx'
   || FUN.info('Plik raportu nie jest plikiem MS Excel (*.xlsx).'@);
      _r:='EXCEL'
   ?}
?};
_r


\bm_kraport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Przed modyfikacja raportu
::  OLD: \bm_kraport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_RAPORT.AKC='T'
|| FUN.info('Raport jest zaakceptowany.'@); 0
|| {? __KRTyp='E'
   || K_RAPORT.win_edit('RED')
   || K_RAPORT.win_edit('RED2')
   ?};
   1
?}


\bd_kraport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Usuniecie raportu dla controllingu
::  OLD: \bd_kraport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('bm_kraport','!ctr_rap_zzad') & FUN.ask('Czy usunąć raport dla Controllingu?'@)
|| K_RAP_U.cntx_psh(); K_RAP_U.index('K_RAPORT'); K_RAP_U.prefix(K_RAPORT.ref());
   {? K_RAP_U.first() || {! |? K_RAP_U.del() !} ?};
   K_RAP_U.cntx_pop();
   K_RAPORT.del()
?}


\edit_raport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Otwiera plik raportu dla Controllingu do edycji
::  OLD: \edit_raport/skid_con.fml
:: ~OST: INBLEDIT
::----------------------------------------------------------------------------------------------------------------------
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
{? exec('bm_kraport','!ctr_rap_zzad')
|| {? __KRTyp='E'
   || K_RAPORT.bl_edit('EXCEL');
      K_RAPORT.DATA_MOD:=K_RAPORT.bl_info('EXCEL','MODIFY_DATE');
      K_RAPORT.put()
   || exec('open_http','control',K_RAPORT.LINK_RS)
   ?}
?}


\kopia_raportu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Tworzy kopie raportu
::  OLD: \kopia_raportu/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
UNPAR.P11:='T'; UNPAR.P11_BV:=UNPAR.P11_BE:=UNPAR.P11_AE:='';
K_RAPORT.cntx_psh(); K_RAPORT.prefix();
K_RAPORT.win_edit('KOPIA');
{? K_RAPORT.edit("exec('ar_k_raport','!ctr_rap_zzad')")
|| _ref:=K_RAPORT.ref();
   K_RAPORT.AKC:='N';
   K_RAPORT.KTO_UTW:=OPERATOR.USER;
   K_RAPORT.DATA_UTW:=date();
   K_RAPORT.DATA_MOD:=date(0,0,0);
   {? K_RAPORT.add() & UNPAR.P11='T'
   || K_RAP_U.cntx_psh();
      K_RAP_U.index('K_RAPORT'); K_RAP_U.prefix(_ref);
      {? K_RAP_U.first()
      || {!
         |? K_RAP_U.cntx_psh(); K_RAP_U.prefix();
            K_RAP_U.K_RAPORT:=K_RAPORT.ref();
            K_RAP_U.add();
            K_RAP_U.cntx_pop();
            K_RAP_U.next()
         !}
      ?};
      K_RAP_U.cntx_pop()
   ?}
?};
K_RAPORT.cntx_pop()


\akc_raport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Akceptacja/anulowanie akceptacji raportu
::   WE: _a - typ operacji: 1-akceptacja 0-anulowanie
::  OLD: \akc_raport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
{? _a
|| {? exec('interm','#system')
   || FUN.emsg(exec('interm_nacc_msg','#system'));
      return(0)
   ?};
   {? K_RAPORT.AKC<>'T' &
      ( __KRTyp='E' & FUN.ask('Zaakceptować raport i utworzyć pliki dla użytkowników?'@) |
        __KRTyp='R' & FUN.ask('Zaakceptować raport?'@)
      )
   || {? __KRTyp='E' || exec('gen_rap','!ctr_rap_zzad') ?};
      K_RAPORT.AKC:='T';
      _ok:=1
   ?}
|| {? K_RAPORT.AKC='T' & FUN.ask('Anulować akceptację raportu?'@)
   || K_RAPORT.AKC:='N';
      _ok:=1
   ?}
?};
{? _ok
|| K_RAPORT.cntx_psh(); K_RAPORT.prefix();
   K_RAPORT.put();
   K_RAPORT.cntx_pop()
?}


\gen_rap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Tworzy pliki raportow dla uzytkownikow
::  OLD: \gen_rap/skid_con.fml
:: ~OST: INBLGET,INFERASE,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
_plik:='r'+$#K_RAPORT.ref()+'.xlsx';
K_RAPORT.bl_get('EXCEL','@'+tmp_dir()+'\\'+_plik,0);
VAR_DEL.delete('TTRAP');
TTRAP:=tab_tmp(1,'REF','INTEGER',,'PLIK','STRING[100]',,'NAZWA','STRING[80]',);
TTRAP.REF:=#K_RAPORT.ref();
TTRAP.PLIK:=_plik;
TTRAP.NAZWA:=K_RAPORT.NAZWA;
TTRAP.add();
exec('run_con','control',5);
{? TTRAP.first()
|| K_RAPORT.cntx_psh(); K_RAPORT.prefix();
   K_RAP_U.cntx_psh(); K_RAP_U.index('K_RAPORT');
   {!
   |? {? K_RAPORT.seek(TTRAP.REF,)
      || K_RAP_U.prefix(K_RAPORT.ref());
         {? K_RAP_U.first()
         || {!
            |? _p:='@'+tmp_dir()+'\\raport'+$#K_RAP_U.ref()+'.xlsx';
               K_RAP_U.bl_put('EXCEL',_p,0);
               ferase(_p);
               K_RAP_U.next()
            !}
         ?}
      ?};
      ferase('@'+tmp_dir()+'\\'+TTRAP.PLIK);
      TTRAP.next()
   !};
   K_RAP_U.cntx_pop();
   K_RAPORT.cntx_pop()
?};
VAR_DEL.delete('TTRAP');
1


\zakres_raport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Zmiana zakresu prezentowanych raportow
::  OLD: \zakres_raport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_RAPORT.cntx_psh(); K_RAPORT.win_edit('ZAKRES');
_ok:=K_RAPORT.edit();
K_RAPORT.cntx_pop();
{? _ok
|| exec('set_zak_rap','!ctr_rap_zzad')
?}


\br_k_raport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord po tabeli K_RAPORT
::  OLD: \br_k_raport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_grey:='';
{? K_RAPORT.AKC='T'
|| _grey+='PUAR'
|| _grey+='N'
?};
K_RAPORT.actions_grayed('WER',_grey);
K_RAP_U.btn_sopt('SLO','DEL','state='+{? K_RAPORT.AKC='T' || 'grayed' || 'normal' ?});
{? K_RAPORT.AKC='T'
|| Color.rekprzed('K_RAPORT#01#01')
|| ''
?}


\bv_kraport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Przed wyswietleniem K_RAPORT
::  OLD: \bv_kraport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_RAPORT.TYP='E'
|| K_RAPORT.win_edit('RED')
|| K_RAPORT.win_edit('RED2')
?};
K_RAPORT.display()


\user_raport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Wyswietla raport uzytkownika
::  OLD: \user_raport/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:=K_RAP_U.K_RAPORT().TYP;
{? _typ='E' & K_RAP_U.EXCEL
|| exec('bl_view','#blob',K_RAP_U,'EXCEL')
|? _typ='R' & K_RAPORT.LINK_RS<>''
|| exec('open_http','control',K_RAPORT.LINK_RS)
|| FUN.info('Nie przygotowano jeszcze raportu.'@)
?}


\br_k_rap_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed tabeli K_RAP_U
::----------------------------------------------------------------------------------------------------------------------
_typ:=K_RAP_U.K_RAPORT().TYP;
{? _typ='E' & K_RAP_U.EXCEL | _typ='R' & K_RAPORT.LINK_RS<>''
|| K_RAP_U.actions_grayed('SLO')
|| K_RAP_U.actions_grayed('SLO','R')
?}


\add_usr_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Dodanie użytkownika do użytkowników raportu z przycisku
::----------------------------------------------------------------------------------------------------------------------
{? USERS.sel_aget().size()>0 || _info:=0 || _info:=1 ?};
{? exec('user_has_pass','control',_info)
|| exec('add_usr','!ctr_rap_zzad');
    grp_disp(K_RAP_U,'SLO')
?}


\add_usr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodanie użytkownika do użytkowników raportu
::----------------------------------------------------------------------------------------------------------------------
K_RAP_U.K_RAPORT:=K_RAPORT.ref();
K_RAP_U.USERS:=USERS.ref();
K_RAP_U.cntx_psh(); K_RAP_U.prefix();
{? ~K_RAP_U.find_rec()
|| K_RAP_U.add()
?};
K_RAP_U.cntx_pop();
1


\del_usr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Usunięcie użytkownika z użytkowników raportu
::   WE: _a - czy pojedynczy rekord
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=~K_RAP_U.sel_size() ?};
{? K_RAP_U.K_RAPORT().AKC<>'T'
|| K_RAP_U.del()
|? _a
|| FUN.info('Raport zaakceptowany.'@)
?};
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:48 6079f0d8b5b6ba83b266d4da78dc84903d432ec78fae1a0496fe56674cdb2bd9a258c6f31657d27b2256254008659f51b18b3be8cec2aacbbdd38a1f61729f572065706369e3cc6084562a62bde3cfc062f266350675beff51cdbb298f6de764102471d06090151a1a8bf1dbae6feb577cc18c5c0afe7c358c3660268bc8ba01

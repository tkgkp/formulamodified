:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_wte_drte.fml
:: Utworzony: 02.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_WTE_DRTE - Redagowanie wzorca technologii
::            UWAGA: większość kodu jest wspólna z czynnością TTE_TEC_DRTE - w przypadku zmian modyfikować oba pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności rejestrowania wzorca technologii (TTE_WTE_DRTE)
::       Zadaniem formuły jest zredagowanie nagłówka wzorca technologii
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_a:=params_get().in;
_b:=params_get().int;
_c:=params_get().out;
_d:=params_get().mp;
_e:=params_get().context;

::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TPKTL, type=_TPKTL, name=Typ karty technologicznej, required=N, fml_val="exec('tpktl_select','tech_head',_a)", fml_exp="exec('tpktl_export','tech_head',_a)"
{? var_pres('TPKTL',_a)<>type_of(~~) & var_pres('TPKTL',_a)<>type_of(null()) || return() ?};
{? var_pres('TPKTL',_a)=type_of(~~) || _a.TPKTL:=null() ?};
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=N, keyref=T
{? var_pres('TKTL',_a)<>type_of(~~) & var_pres('TKTL',_a)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_a)=type_of(~~) || _a.TKTL:=null() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=N
::# kind=WY, symbol=OPER, type=STRING, name=Redagowanie listy operacji, required=N
{? var_pres('TKTL',_c)<>type_of(~~) & var_pres('TKTL',_c)<>type_of(null()) || return() ?};
{? var_pres('OPER',_c)<>type_of('') || _c.OPER:='N' ?};

exec('tech_red','!tte_wte_drte',_a,_b,_c,_d,_e)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności rejestrowania wzorca technologii (TTE_WTE_DRTE)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"{? NRK*'dodawana'>0 || ' ' || NRK+' ||| '+WER ?}",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' ||
            {? _tmp<>' ' || 'Zredaguj wzorzec technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]]
            || 'Zredaguj nowy wzorzec technologii'@@
            ?}
          || ''
          ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,
::      "{? NRK*'dodawana'>0
::       || 'Zredaguj nowy wzorzec technologii'@@
::       || 'Zredaguj wzorzec technologii %1 wer. %2'@@[NRK,WER]
::       ?}
::   ",'')

|| _desc:='Zredaguj nowy wzorzec technologii'@@

?};
_desc


\tech_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła rejestrowania wzorca technologii
::       Używana przez exec('main','!tte_wte_drte')
::   WE: _a - [obj_new] - parametry wejściowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::       _e - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_out:=_c;
_mp:=_d;
_context:=_e;

_var_torw:=VAR.A_TORW;
_var_arch:=VAR.ARCH;

_torw:=VAR.A_TORW:='W';
_arch:=VAR.ARCH:='N';

_one:=_mp.isMicro();
_czykey:=0;
_tktl:=null();
_tktl_uid:='';
_act:=_mp.akcja();
_keyRefs:=_mp.getRefs();
_path:=_mp.path();

:: Sprawdzenie typu karty technologicznej
{? _in.TPKTL
|| {? exec('FindAndGet','#table',TPKTL,_in.TPKTL,,"1+TYP",'')='~'
   || _msg:='Niezgodność wywołania czynności.\nPrzekazany typ nie może być zastrzeżony.';
      FUN.emsg(_msg);
      _mp.error(_msg);
      return()
   ?}
?};

{? var_pres('[1]',_keyRefs)
:: jest rekord kluczowy to ustawiamy odpowiedniego TKTL
|| _czykey:=1;
   TKTL.cntx_psh();
   {? TKTL.seek(_keyRefs[1],ref_name(_keyRefs[1]),1)
   || _tktl:=TKTL.ref();
      _tktl_uid:=TKTL.uidref()
   ?};
   TKTL.cntx_pop()
|| {? (';KOPIUJ;NOWY_WZORZEC_DO_KARTY;NOWY_WZORZEC_DO_OPERACJI'*_act)>1 & _in.TKTL=null()
   || _tktl:=_context.TKTL;
      _toper:=_context.TOPER;
      _tktl_uid:=exec('FindAndGet','#table',TKTL,_context.TKTL,,"uidref()",'')
   |? (';POPRAW;USUŃ;ZAKOŃCZ;ZAKOŃCZ_BEZ_PYTANIA'*_act)>1 & _in.TKTL=null()
   || _tktl:=TKTL.ref();
      _tktl_uid:=TKTL.uidref()
   || _tktl:=_in.TKTL;
      _tktl_uid:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"uidref()",'')
   ?}
?};

{? _tktl & _in.TPKTL & exec('FindAndGet','#table',TKTL,_tktl,,"TYP")<>exec('FindAndGet','#table',TPKTL,_in.TPKTL)
|| _msg:='Niezgodność wywołania czynności.\nTyp wzorca musi być zgodny z typem przekazanym w procesie.';
   FUN.emsg(_msg);
   _mp.error(_msg);
   return()
?};

{? _act='DOŁĄCZ' & exec('FindAndGet','#table',TKTL,_tktl,,"ARCH='T'",0)
|| _msg:='Niezgodność wywołania czynności.\nNie można redagować archiwalnego wzorca.';
   FUN.emsg(_msg);
   _mp.error(_msg);
   return()
?};

_mp.trigRef('TKTL',1,1);

::FUN.info(_mp.Path);
:: z listy ToDo - znany keyref i stoję na liście zadań - poprawianie nagłówka gdy możliwe
{? _path='Todo' & _tktl<>null()
|| TKTL.cntx_psh();
   TKTL.clear();
   {? TKTL.seek(_tktl)
   || {? TKTL.STAT_N='T'
      || FUN.info('Zakończono rejestrację nagłówka wzorca.'@);
         _out.TKTL:=_tktl;
         _out.OPER:=TKTL.TYP().OPER;
         _mp.save(,_out);
         _mp.done()
      || {? exec('tktl_pop','tech_head',TKTL.ref(),,,_in.TPKTL)
         || TKTL.get();
            _out.TKTL:=_tktl;
            _out.OPER:=TKTL.TYP().OPER;
            _mp.save(,_out);
            {? TKTL.STAT_N='T'
            || _mp.done()
            ?}
         ?}
      ?}
   || _mp.error('Brak wzorca o refie '+$_tktl+'.')
   ?};
   TKTL.cntx_pop()

:: nie ma keyref, wywołanie z panelu czynności - powołanie nowego wzorca
|? _path='Proc'
|| exec('tktl_use','tech_common');
   _tktl:=exec('tktl_add','tech_head',,_in.TPKTL);
   {? _tktl<>null()
   || _out.TKTL:=_tktl;
      _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
      _mp.save(,_out);
      {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
      || _mp.done()
      ?}
   ||
      _mp.cancel()
   ?}

:: nie ma keyref, wywołanie z listy ToDo
|? _path='Todo'
|| exec('tktl_use','tech_common');
   _tktl:=exec('tktl_add','tech_head',,_in.TPKTL);
   {? _tktl<>null()
   || _out.TKTL:=_tktl;
      _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
      _mp.save(,_out);
      {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
      || _mp.done()
      ?}
   || _mp.cancel()
   ?}

:: Dołączanie nowego wzorca
|? _act='DOŁĄCZ'
|| _tktl:=exec('tktl_add','tech_head',,_in.TPKTL);
   {? _tktl<>null()
   || _out.TKTL:=_tktl;
      _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
      _mp.save(,_out);
      {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
      || _mp.done()
      ?}
   || _mp.cancel()
   ?}

:: Kopiowanie na nowy wzorzec
|? _act='KOPIUJ'
|| exec('tktl_cntx_psh','tech_common');
   exec('tktl_use','tech_common',ref_name(_tktl)+3);
   _args_copy:=exec('args_copy','tech_arch');
   {? exec('tktl_copy_act','tech_arch',_tktl,_args_copy)>0
   || _tktl:=_args_copy.TKTL_DST;
      {? _tktl<>null()
      || _out.TKTL:=_tktl;
         _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
         _mp.save(,_out);
         {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
         || _mp.done()
         ?}
      || _mp.cancel()
      ?}
   ?};
   exec('tktl_cntx_pop','tech_common')

:: Tworzenie nowego wzorca z poziomu karty/wzorca
|? _act='NOWY_WZORZEC_DO_KARTY'
|| exec('tktl_cntx_psh','tech_common');
   exec('tktl_use','tech_common',ref_name(_tktl)+3);
   _args_copy:=exec('args_copy','tech_arch');
   {? exec('tktl2wzor','tech_arch',_tktl,_args_copy)>0
   || _tktl:=_args_copy.TKTL_DST;
      {? _tktl<>null()
      || _out.TKTL:=_tktl;
         _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
         _mp.save(,_out)
      || _mp.cancel()
      ?}
   ?};
   exec('tktl_cntx_pop','tech_common');
   {? TKTL.f_active() || TKTL.f_rfresh() ?}

:: Tworzenie nowego wzorca z poziomu operacji karty/wzorca
|? _act='NOWY_WZORZEC_DO_OPERACJI'
|| exec('tktl_cntx_psh','tech_common');
   exec('tktl_use','tech_common',ref_name(_tktl)+3);
   _args_copy:=exec('args_copy','tech_arch');
   {? exec('toper2wzor','tech_arch',_toper,_args_copy)>0
   || _tktl:=_args_copy.TKTL_DST;
      {? _tktl<>null()
      || _out.TKTL:=_tktl;
         _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
         _mp.save(,_out)
      || _mp.cancel()
      ?}
   ?};
   exec('tktl_cntx_pop','tech_common')

:: Poprawianie wzorca
|? _act='POPRAW'
|| {? exec('tktl_pop','tech_head',_tktl,,,_in.TPKTL)
   || _out.TKTL:=_tktl;
      _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
      _mp.save(,_out);
      {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
      || _mp.done()
      ?}
   ?}

:: Usuwanie wzorca
|? _act='USUŃ'
|| {? exec('TKTL_usun','tech_head',_tktl)
   || _out.TKTL:=null();
      {? _one
      || _mp.save(,_out);
         _mp.done()
      || _mp.error('Nie można kontynuować procesu — wzorzec został usunięty.')
      ?}
   ?}

:: Zakończenie redagowania nagłówka
|? _act='ZAKOŃCZ'
|| TKTL.cntx_psh();
   TKTL.clear();
   {? TKTL.seek(_tktl)
   || _chk:=exec('tktl_chk','tech_head',1);
      {? _chk
      || {? FUN.ask('Czy zakończyć rejestrację nagłówka wzorca technologii?'@)
         || TKTL.STAT_N:='T';
            exec('stat_act','tech_head');
            {? TKTL.put()
            || _mp.save(,_out);
               _mp.done()
            ?}
         ?}
      ?}
   || _mp.cancel()
   ?};
   TKTL.cntx_pop()

:: Zakończenie redagowania nagłówka (bez pytania)
|? _act='ZAKOŃCZ_BEZ_PYTANIA'
|| TKTL.cntx_psh();
   TKTL.clear();
   {? TKTL.seek(_tktl)
   || TKTL.STAT_N:='T';
      exec('stat_act','tech_head');
      {? TKTL.put()
      || _mp.save(,_out);
         _mp.done()
      ?}
   || _mp.cancel()
   ?};
   TKTL.cntx_pop()

:: Tutaj nie powinno dojść, więc błąd
|| _mp.error('Nieobsłużony kontekst wywołania czynności.')
?};

VAR.A_TORW:=_var_torw;
VAR.ARCH:=_var_arch;
~~


\action_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja wzorca technologii - akcja Dołącz
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_WTE_DRTE';
_args.AKCJA:='DOŁĄCZ';
_args.PROC_START:='T';
exec('mp_run','#b__box',_args)


\action_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja wzorca technologii - akcja kOpiuj
::----------------------------------------------------------------------------------------------------------------------
exec('tktl_cntx_psh','tech_common');

_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_WTE_DRTE';
_args.AKCJA:='KOPIUJ';
_args.PROC_START:='T';
_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
_args.CONTEXT:=obj_new('TKTL','TOPER'); _args.CONTEXT.TKTL:=TKTL.ref(); _args.CONTEXT.TOPER:=null();

exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TPKTL',TKTL.TYP);

exec('mp_run','#b__box',_args);

exec('tktl_cntx_pop','tech_common')


\action_pattern_4card
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja wzorca technologii - akcja tworzenia wzorca z poziomu karty technologicznej/wzorca
::----------------------------------------------------------------------------------------------------------------------
exec('tktl_cntx_psh','tech_common');
_var_torw:=VAR.A_TORW;

_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_WTE_DRTE';
_args.AKCJA:='NOWY_WZORZEC_DO_KARTY';
_args.PROC_START:='T';
_args.CONTEXT:=obj_new('TKTL','TOPER'); _args.CONTEXT.TKTL:=TKTL.ref(); _args.CONTEXT.TOPER:=null();
exec('mp_run','#b__box',_args);

VAR.A_TORW:=_var_torw;
exec('tktl_cntx_pop','tech_common')


\action_pattern_4card_zl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Rejestracja wzorca technologii - akcja tworzenia wzorca z poziomu zlecenia (jego karty technologicznej)
::----------------------------------------------------------------------------------------------------------------------
_tktl:=exec('FindAndGet','#table',TKTL,ZL.RTKTL,,"ref()",null());

{? _tktl<>null()
|| exec('tktl_cntx_psh','tech_common');
   _var_torw:=VAR.A_TORW;

   _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRTE';
   _args.AKCJA:='NOWY_WZORZEC_DO_KARTY';
   _args.PROC_START:='T';
   _args.CONTEXT:=obj_new('TKTL','TOPER'); _args.CONTEXT.TKTL:=_tktl; _args.CONTEXT.TOPER:=null();
   exec('mp_run','#b__box',_args);

   VAR.A_TORW:=_var_torw;
   exec('tktl_cntx_pop','tech_common')

|| FUN.info('Nie można utworzyć wzorca — zlecenie nie ma własnej technologii.'@)
?};
~~


\action_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja wzorca technologii - akcja Popraw
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRP_MOD='' ||  VAR.GRP_MOD:='N' ?};
{? TKTL.STAT_N<>'T' & VAR.GRP_MOD='N'
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRTE';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='POPRAW';
   _args.PROC_START:='N';
   exec('mp_run','#b__box',_args)
|| exec('tktl_pop','tech_head',TKTL.ref())
?}


\action_modify_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Grupa przed dla akcji Popraw w oknach wertowania dla wzorców technologicznych
::----------------------------------------------------------------------------------------------------------------------
_continue:=1;
TKTL.cntx_psh();
{? TKTL.sel_size()>1
|| _continue:=FUN.ask('Ilość zaznaczonych wzorców technologii: %1. Czy poprawić wybrane wzorce kart technologicznych?'@
      [form(TKTL.sel_size())]);
   {? _continue>0
   ||
      TKTL.seek(null(),,,1);
      TKTL.blank(1);
      VAR.GRP_MOD:='P';
      {? _continue>0
      || _buffer:=exec('obj_new','#buf','TKTL');
         _buffer.get();
         params_set('buffer',_buffer);
         TKTL.win_edit('GRP_POP_');
         _valid:="
            _res:=1;
            params_set(params_get());
            _buffer:=exec('obj_new','#buf','TKTL');
            _buffer.get();
            {? exec('compare','#table',_buffer,params_get().buffer,0,'GKTL')
            || FUN.info('Przynajmniej jedno z pól musi zostać określone.'@);
               _res:=0
            ?};
            _res
         ";
         _continue:=TKTL.edit(_valid);
         {? _continue>0
         || _buffer_grp:=exec('obj_new','#buf','TKTL');
            _buffer_grp.get();
            {? var_pres('__BUFF_GRP')>100
            || VAR_DEL.delete('__BUFF_GRP')
            ?};
            __BUFF_GRP:=_buffer_grp;
            sel_nchk();
            KOMM.init(255,,'Grupowa modyfikacja wzorców kart technologicznych'@);
            VAR.GRP_MOD:='T';
            VAR.GRUPA:='T'
         || VAR.GRP_MOD:='N';
            VAR.GRUPA:='N';
            {? var_pres('__BUFF_GRP')>100
            || VAR_DEL.delete('__BUFF_GRP')
            ?}
         ?}
      ?}
   ?}
?};
TKTL.cntx_pop();
_continue


\action_modify_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Grupa po dla akcji Popraw w oknach wertowania dla wzorców technologicznych
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRP_MOD='T'
|| VAR.GRUPA:='N';
   VAR.GRP_MOD:='N';
   {? var_pres('__BUFF_GRP')>100
   || VAR_DEL.delete('__BUFF_GRP')
   ?};
   KOMM.select()
?};
~~


\action_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja wzorca technologii - akcja Usuń
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N<>'T'
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRTE';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='USUŃ';
   exec('mp_run','#b__box',_args)
|| {? TKTL.sel_size()>0
   || KOMM.add('Zakończono rejestrację nagłówka wzorca %1 wer. %2 — modyfikacje niemożliwe.'@[TKTL.NRK,TKTL.WER])
   || FUN.info('Zakończono rejestrację nagłówka wzorca.\nModyfikacje niemożliwe.'@)
   ?}
?}


\action_delete_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja wzorca technologii - akcja Usuń przed grupą rekordów
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Ilość zaznaczonych wzorców technologii: %1. Czy usuwać?'@[form(TKTL.sel_size())])
|| VAR.GRUPA:='T';
   KOMM.init(,,'Usuwanie wzorców technologii'@,'');
   1
|| 0
?}


\action_delete_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja wzorca technologii - akcja Usuń po grupie rekordów
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
KOMM.select();
1


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja wzorca technologii - akcja Zakończ
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRTE';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='ZAKOŃCZ';
   exec('mp_run','#b__box',_args)
|| FUN.info('Zakończono rejestrację nagłówka wzorca.\nModyfikacje niemożliwe.'@)
?};
'key:Esc'


\tktl_dalej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obsługa przycisku 'Zakończ' w oknie redagowania nagłówka wzorca technologii
::       Zatwierdza nagłówek i popycha proces dalej
::----------------------------------------------------------------------------------------------------------------------
TKTL.STAT_N:='T';
'key:F2'

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 13b0e59f70a006e40cc9f662b497894f14589f895b56643f3b9cc96f9c7b1f6545749bc7b2aae1fc2817c7af913092de97ca5b912629039508a23b0319e8779194caa1a6d5fe843938193cf778d4d1baad4b299f9b43f11d63c7e8ae14fda67698e622c9251a6b442b46fbf855de212233a304900853d01f5db714af9331f59a

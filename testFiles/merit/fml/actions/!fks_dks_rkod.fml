:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_rkod.fml
:: Utworzony: 09.07.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_RKOD - Definiowanie rozliczeń kosztów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Definiowanie rozliczeń kosztów - główna formuła czynności FKS_DKS_RKOD.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
1


\pr_rtyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed redakcja pola ROZ_NAZ.TYP
::  OLD: \pr_rtyp/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt='popraw'
|| ROZ_KON.index('ROZ_KON');
   ROZ_KON.prefix(ROZ_NAZ.ref,KOSZTY.TYP);
   {? ROZ_KON.first()
   || 0
   || ROW_NAG.index('ROZL');
      ROW_NAG.prefix(ROZ_NAZ.ref());
      {? ROW_NAG.first() || 0 || 1 ?}
   ?}
|| 1
?}


\sik_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula
::  OLD: \sik_akc/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
SIK.AKC


\sel_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Wyswietlenie definicji kont dla definicji rozliczenia
::  OLD: \sel_kon/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_def:=var_pres('wyr')>0 & var_pres('ref_wd')>0 & var_pres('ref_wk')>0;
wyr:={? PAR_SKID.get(35)='T' || '' || '0' ?};
ref_wd:=ref_wk:=ref_zk:=ref_zk2:=null;
pierwsze:=1;
_ok:=ROZ_KON.grp_make('Konta definicji'@,"grp_disp(ROZ_KON,'WER_Z'+wyr);
                                         {? ROZ_NAZ.TYP<>'R'
                                         || grp_disp(ROZ_KON,'WER_'+ROZ_NAZ.TYP+wyr)
                                         ?}",'grp_roz_kon');
ROZ_KON.grp_sel(_ok,ROZ_KON,'WER_Z'+wyr,,"",,,10,
                "{? ~pierwsze || ref_zk2:=ROZ_KON.ref() ?};
                 pierwsze:=0;
                 ROZ_KON.index('ROZ_KON');
                 ROZ_KON.prefix(ROZ_NAZ.ref,'Z');
                 KOSZTY.TYP:='Z';
                 {? ref_zk<>null || ROZ_KON.seek(ref_zk) ?};
                 KOSZTY.win_edit('RED_'+KOSZTY.TYP+wyr)",,,,'maximized_with_title');
{? ROZ_NAZ.TYP<>'R'
|| ROZ_KON.grp_splt(_ok,,'horizontal','left');
   ROZ_KON.grp_sel(_ok,ROZ_KON,'WER_'+ROZ_NAZ.TYP+wyr,,"",,15,10,
                   "{? ~pierwsze || ref_zk:=ROZ_KON.ref() ?};
                    pierwsze:=0;
                    ROZ_KON.index('ROZ_KON');
                    ROZ_KON.prefix(ROZ_NAZ.ref,ROZ_NAZ.TYP);
                    {? ref_zk2<>null || ROZ_KON.seek(ref_zk2) ?};
                    KOSZTY.TYP:=ROZ_NAZ.TYP;
                    KOSZTY.win_edit('RED_'+KOSZTY.TYP+wyr)",,,,'maximized_with_title')
?};
ROZ_KON.win_sel(_ok);
{? ROZ_NAZ.AKC='T'
|| ROZ_KON.actions('WER_Z'+wyr,'DPU:D');
   {? ROZ_NAZ.TYP<>'R' || ROZ_KON.actions('WER_'+ROZ_NAZ.TYP+wyr,'DPU:D') ?};
   ROZ_KON.select();
   ROZ_KON.actions('WER_Z'+wyr,'');
   {? ROZ_NAZ.TYP<>'R' || ROZ_KON.actions('WER_'+ROZ_NAZ.TYP+wyr,'') ?}
|| {? ROZ_NAZ.r_lock(1,1)
   || ROZ_KON.select();
      ROZ_NAZ.r_unlock()
   || FUN.info('Definicję rozliczenia redaguje inny operator.'@)
   ?}
?};
{? ~_def || &wyr; &ref_wd; &ref_wk ?};
&ref_zk; &ref_zk2; &pierwsze


\roz_nazp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Przed akcja Popraw w okienku WER tabeli ROZ_NAZ
::  OLD: \roz_nazp/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZ_NAZ.AKC='T'
|| FUN.info('Definicja rozliczenia zaakceptowana.\nPoprawianie niemożliwe.'@); 0
|| KOSZTY.TYP:=ROZ_NAZ.TYP; 1
?}


\roz_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Usuniecie definicji rozliczenia
::  OLD: \roz_usun/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZ_NAZ.AKC='T'
|| {? ~ROZ_NAZ.sel_size()
   || FUN.info('Definicja rozliczenia zaakceptowana.\nUsunięcie niemożliwe.'@)
   ?}; 0
|| ROW_NAG.index('ROZL');
   ROW_NAG.prefix(ROZ_NAZ.ref);
   {? ROW_NAG.first()
   || {? ~ROZ_NAZ.sel_size()
      || FUN.info('Istnieją rozliczenia wykonane \nna podstawie tej definicji. '
                   '\nUsunięcie niemożliwe.'@)
      ?};
      0
   || {? ROZ_NAZ.sel_size() | FUN.ask('Usunąć definicję rozliczenia kosztów?'@)
      || do();
         exec('usu_kon','!fks_dks_rkod','Z');
         exec('usu_kon','!fks_dks_rkod',ROZ_NAZ.TYP);
         exec('usu_kon','!fks_dks_rkod','D');
         ROZ_NAZ.del();
         {? end() || {? ROZ_NAZ.sel_size() || ile_usu+=1 ?} ?}
      ?}
   ?}
?};
1


\usu_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Usuniecie definicji konta
::  OLD: \usu_kon/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
ROZ_KON.index('ROZ_KON'); ROZ_KON.prefix(ROZ_NAZ.ref,_a);
{? ROZ_KON.first()
|| ROZ_WYR.index('ROZ_WYR');
   {! |?
      ROZ_WYR.prefix(ROZ_KON.ref());
      {? ROZ_WYR.first() || {!|? ROZ_WYR.del() !} ?};
      ROZ_KON.del()
   !}
?}


\bgrozusu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2009]
:: OPIS: Grupa przed dla usuwania definicji rozliczen kosztow
::  OLD: \bgrozusu/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? FUN.ask('Usunąć zaznaczone definicje rozliczeń kosztów?'@)
|| ile_zazn:=ROZ_NAZ.sel_size();
   ile_usu:=0;
   _zwrot:=1
?};
_zwrot


\agrozusu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2009]
:: OPIS: Grupa po dla usuwania definicji rozliczen kosztow
::  OLD: \agrozusu/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_dl:=(+$ile_zazn);
FUN.info(form(form('Liczba definicji zaznaczonych: ',31)+$ile_zazn,31+_dl)+'\n'+
         form(form('  Liczba definicji usuniętych: ',31)+$ile_usu,31+_dl));
&ile_zazn; &ile_usu


\roz_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja akceptuj w okienku WER tabeli ROZ_NAZ
::  OLD: \roz_akc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_kom:='';
{? ROZ_NAZ.AKC<>'T'
|| _kom:=exec('spr_kon','!fks_dks_rkod','Z');
   {? _kom='' & (ROZ_NAZ.TYP='P' | ROZ_NAZ.TYP='K')
   || _kom:=exec('spr_kon','!fks_dks_rkod',ROZ_NAZ.TYP)
   ?}
|| _kom:='Definicja rozliczenia jest już zaakceptowana.'
?};
{? _kom=''
|| ROZ_NAZ.AKC:='T'; ROZ_NAZ.DATA_A:=date(); ROZ_NAZ.put();
   {? var_pres('ile')>0 || ile+=1 ?}
|| {? ROZ_NAZ.sel_size()=0 || FUN.info(_kom) ?}
?};
1


\spr_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Sprawdzenie poprawnosci definicji rozliczenia
::  OLD: \spr_kon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_kom:='';
ROZ_KON.index('ROZ_KON'); ROZ_KON.prefix(ROZ_NAZ.ref,_a);
{? ROZ_KON.first()
|| {! |?
      {? ROZ_KON.MASKA=''
      || _kom:='Brak maski konta w definicji kont '+{? _a='Z' || 'źródłowych.' || 'docelowych.' ?}
      ?};
      _kom='' & ROZ_KON.next()
   !}
|| _kom:='Brak definicji kont '+{? _a='Z' || 'źródłowych.' || 'docelowych.' ?}
?};
_kom


\pr_rakc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed akcja grupowa - akceptuj w okienku WER tabeli ROZ_NAZ
::  OLD: \pr_rakc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
max:=ROZ_NAZ.sel_size(); ile:=0; 1


\po_rakc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po akcji grupowej - akceptuj w okienku WER tabeli ROZ_NAZ
::  OLD: \po_rakc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ile=max
|| FUN.info('Zaakceptowano wszystkie zaznaczone definicje rozliczeń.'@)
|| FUN.info('Liczba zaakceptowanych definicji rozliczeń:%1'
            '\nRozliczenia, dla których operacja się nie powiodła: %2'@[$ile,$(max-ile)])
?};
&max; &ile


\roz_anu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja anuluj akceptacje w okienku WER tabeli ROZ_NAZ
::  OLD: \roz_anu/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_kom:='';
_sel:=ROZ_NAZ.sel_size();
{? ROZ_NAZ.AKC='T'
|| {? (_sel | (_sel=0 & FUN.ask('Anulować akceptację rozliczenia?'@)))
   || ROZ_NAZ.AKC:='N'; ROZ_NAZ.DATA_A:=date(0,0,0); ROZ_NAZ.put();
      {? var_pres('ile')>0 || ile+=1 ?}
   ?}
|| _kom:='Definicja rozliczenia nie jest zaakceptowana.'
?};
{? _kom<>'' & _sel=0 || FUN.info(_kom,) ?};
1


\pr_ganu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed akcja grupowa - anuluj akceptacje w okienku WER tabeli ROZ_NAZ
::  OLD: \pr_ganu/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Anulować akceptację zaznaczonych rozliczeń?'@)
|| max:=ROZ_NAZ.sel_size(); ile:=0; 1
?}


\po_ganu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po akcji grupowej - anuluj akceptacje w okienku WER tabeli ROZ_NAZ
::  OLD: \po_ganu/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ile=max
|| FUN.info('Anulowano akceptację wszystkich zaznaczonych definicji.'@)
|| FUN.info('Liczba anulowanych akceptacji definicji rozliczeń:  %1'
            '\nRozliczenia, dla których operacja się nie powiodła: %2'@[$ile,$(max-ile)])
?};
&max; &ile


\kop_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja kopia w okienku wert. tabeli ROZ_NAZ
::  OLD: \kop_roz/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KS.index('SYM'); KS.prefix(SSTALE.AR);
{? KS.first()
|| TROZ_NAZ:=tab_tmp(4,'ROK','STRING[20]','Rok',
                       'TYP','STRING[3]','Typ',
                       'KOD','STRING[10]','Kod',
                       'NAZWA','STRING[30]','Nazwa',
                       'REF','INTEGER','Ref');
   ROZ_NAZ.cntx_psh();
   ROZ_NAZ.index('TYP');
   ROZ_NAZ.prefix();
   {? ROZ_NAZ.first()
   || {!
      |? _kod:=ROZ_NAZ.KOD;
         TROZ_NAZ.ROK:=ROZ_NAZ.ROK().NAZ;
         TROZ_NAZ.KOD:=ROZ_NAZ.KOD;
         TROZ_NAZ.NAZWA:=ROZ_NAZ.NAZWA;
         TROZ_NAZ.TYP:=ROZ_NAZ.TYP;
         TROZ_NAZ.REF:=ROZ_NAZ.ref;
         TROZ_NAZ.add();
         ROZ_NAZ.next()
      !}
   ?};
   {? TROZ_NAZ.first()
   || _w:=TROZ_NAZ.mk_sel('Definicje rozliczeń'@,'P',,'troz_naz_wer');
      TROZ_NAZ.win_fld(_w,,'ROK',,,20,,,'Rok'@);
      TROZ_NAZ.win_fld(_w,,'TYP',,,3,,,'Typ'@);
      TROZ_NAZ.win_fld(_w,,'KOD',,,10,,,'Kod'@);
      TROZ_NAZ.win_fld(_w,,'NAZWA',,,30,,,'Nazwa'@);
      TROZ_NAZ.win_act(_w,0,'Formuła','Kopiuj'@@,,'Kopiowanie definicji rozliczenia do bieżącego roku'@,
         ,"exec('kop_roz1','!fks_dks_rkod',TROZ_NAZ.REF)",1,1
         ,"ile:=0; s_sel:=TROZ_NAZ.sel_size(); 1"
         ,"FUN.info('Liczba zaznaczonych definicji: %1\n'
          'Liczba skopiowanych definicji: %2'@[form($s_sel,+$s_sel),form($ile,-(+$s_sel))]); &s_sel; &ile",'K');
      TROZ_NAZ.win_act(_w,0,'Kolejność','Ko&lejność'@@,,,,,,,,,'L');
      TROZ_NAZ.win_sel(_w);
      TROZ_NAZ.select()
   || FUN.info('Brak definicji rozliczeń do kopiowania.'@)
   ?};
   VAR_DEL.delete('TROZ_NAZ');
   SSTALE.AR();
   ROZ_NAZ.cntx_pop()
|| FUN.info('Nie zdefiniowano planu kont w bieżącym roku bilansowym.'@)
?}


\kop_roz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Kopiowanie definicji biezacego rozliczenia
::   WE: _a - ref rozliczenia
::  OLD: \kop_roz1/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZ_NAZ.seek(_a,)
|| _ref:=ROZ_NAZ.ref();
   ROZ_NAZ.ROK:=SSTALE.AR;
   ROZ_NAZ.AKC:='N';
   ROZ_NAZ.DATA_A:=date(0,0,0);
   ROZ_NAZ.cntx_psh();
   ROZ_NAZ.index('KOD'); ROZ_NAZ.prefix();
   _zb:=1; _za:=ROZ_NAZ.KOD;
   {! |? ROZ_NAZ.find_key(STALE.AR,_za,_za)
   |! {? ROZ_NAZ.KOD=_za
      || _zb+=1
      || _zb:=0
      ?}
   !};
   ROZ_NAZ.cntx_pop();
   ROZ_NAZ.KOD:=_za;
   {? ROZ_NAZ.add(1)
   || SSTALE.AR();
      {? var_pres('ile')>0 || ile+=1 ?};
      exec('kop_kon','!fks_dks_rkod',_ref,'Z');
      exec('kop_kon','!fks_dks_rkod',_ref,ROZ_NAZ.TYP)
   ?}
?};
sel_exit();
1


\kop_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Kopiowanie definicji kont rozliczenia
::  WE: _a - ref rozliczenia do kopiowania
::      _b - typ kont (Z/P/K)
::  OLD: \kop_kon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROZ_KON.index('ROZ_KON');
ROZ_KON.prefix(_a,_b);
_lp:=1;
{? ROZ_KON.first()
|| {!
   |? _kon:=ROZ_KON.ref();
      ROZ_KON.cntx_psh();
      ROZ_KON.prefix();
      ROZ_KON.ROZ_NAZ:=ROZ_NAZ.ref();
      ROZ_KON.LP:=_lp;
      {? exec('czy_kkon','!fks_dks_rkod',ROZ_KON.MASKA) & ROZ_KON.add()
      || _kon_n:=ROZ_KON.ref();
         _lp+=1;
         ROZ_WYR.index('ROZ_WYR');
         ROZ_WYR.prefix(_kon);
         {? ROZ_WYR.first()
         || {!
            |? {? exec('czy_kwyr','!fks_dks_rkod',ROZ_KON.MASKA,ROZ_WYR.LP,ROZ_WYR.SLUAPPL,ROZ_WYR.PROC)
               || ROZ_WYR.cntx_psh();
                  ROZ_WYR.prefix();
                  ROZ_WYR.ROZ_KON:=ROZ_KON.ref();
                  ROZ_WYR.add();
                  ROZ_WYR.cntx_pop()
               ?};
               ROZ_WYR.next()
            !}
         ?};
         {? _b='K'
         || ROZ_KON.index('ROZ_KLU');
            ROZ_KON.prefix();
            {? ROZ_KON.find_key(_a,'D',#_kon)
            || _r:=ROZ_KON.ref();
               ROZ_KON.ROZ_NAZ:=ROZ_NAZ.ref();
               ROZ_KON.KLUCZ:=#_kon_n;
               {? exec('czy_kkon','!fks_dks_rkod',ROZ_KON.MASKA) & ROZ_KON.add()
               || ROZ_WYR.index('ROZ_WYR');
                  ROZ_WYR.prefix(_r);
                  {? ROZ_WYR.first()
                  || {!
                     |? {? exec('czy_kwyr','!fks_dks_rkod',ROZ_KON.MASKA,ROZ_WYR.LP,ROZ_WYR.SLUAPPL,ROZ_WYR.PROC)
                        || ROZ_WYR.cntx_psh();
                           ROZ_WYR.prefix();
                           ROZ_WYR.ROZ_KON:=ROZ_KON.ref();
                           ROZ_WYR.add();
                           ROZ_WYR.cntx_pop()
                        ?};
                        ROZ_WYR.next()
                     !}
                  ?}
               ?}
            ?}
         ?}
      ?};
      ROZ_KON.cntx_pop();
      ROZ_KON.next()
   !}
?}


\czy_kwyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Czy wyroznik jest zgodny z planem kont
::   WE: _a - symbol konta
::       _b - numer wyroznika
::  OLD: \czy_kwyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KS_W.index('LP');
KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+_a),_b);
KS_W.first() & KS_W.SLU=_c & (_d=100 | KS_W.ROZDZ='T')


\czy_kkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Czy symbol konta jest zgodny z planem kont
::   WE: _a - symbol konta
::  OLD: \czy_kkon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_k:=(ROK_F.SYNT+_a)*'?';
{? _k>1
|| KS.index('SYM');
   KS.prefix(ROK_F.ref(),(_k-1)+_a);
   KS.first()
|? _k=1
|| 1
|| KS.index('SYM');
   KS.prefix(ROK_F.ref(),ROK_F.SYNT+_a);
   {? KS.first()
   || BUD.index('KS');
      BUD.prefix(KS.ref);
      _ok:=1; _sep:={? ROK_F.SEP=',' || 0 || 1 ?}; _s:=ROK_F.SEP;
      _dl:=ROK_F.SYNT;
      {? BUD.first()
      || {! |?
            {? _sep>0 || _ok:=1+(_dl-_a)=_s ?};
            _dl+=BUD.SLU().SLU().DL+_sep;
            _ok & BUD.next()
         !};
         {? _ok || _ok:=+_a=_dl ?}
      ?};
      _ok
   ?}
?}


\sel_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: sprawdzenie poprawnosci danych w okienku edycyjnym tabeli ROZ_NAZ
::  OLD: \sel_rek/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=__CHK.record(ROZ_NAZ,,'KOD','NAZWA');
{? _zwrot=''
|| {? __CHK.index(ROZ_NAZ,-menu_txt(,)='popraw')<>'' || _zwrot:='KOD' ?}
?};
_zwrot


\rec_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Rekord przed w okienku WER tabeli ROZ_NAZ
::  OLD: \rec_roz/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=_gray:='';
{? ROZ_NAZ.sel_size()
|| _gray:=''
|| {? ROZ_NAZ.AKC='T'
   || _zwrot:='ROZ_NAZ#01#01'; _gray:='PUA'
   || _zwrot:=''; _gray:='L'
   ?}
?};
ROZ_NAZ.actions_grayed('WER',_gray);
_zwrot


\dol_zrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'Dolacz' w okienkach wert. tabeli ROZ_KON
::   WE: [_a=0 - pusta dziedzina, _a<>0 - dolacz jako ostatni]
::  OLD: \dol_zrd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZ_NAZ.AKC<>'T'
|| _lp:={? _=0
        || ROZ_KON.LP
        |? _a=0
        || 1
        || ROZ_KON.cntx_psh();
           _lp:={? ROZ_KON.last() || ROZ_KON.LP+1 || 1 ?};
           ROZ_KON.cntx_pop();
           _lp
        ?};
   _typ:=KOSZTY.TYP;
   KOSZTY.blank();
   ROZ_KON.blank();
   KOSZTY.LP:=_lp;
   KOSZTY.TYP:=_typ;
   {? KOSZTY.TYP='Z'
   || KOSZTY.WSP:=1
   |? KOSZTY.TYP='P'
   || exec('ini_tpow','dok_fks');
      KOSZTY.WYBOR1:='P';
      ROZ_KON.cntx_psh();
      _p:=100;
      {? ROZ_KON.first() || {!|? _p-=ROZ_KON.WSP; ROZ_KON.next() !} ?};
      ROZ_KON.cntx_pop();
      KOSZTY.WSP:=_p
   |? KOSZTY.TYP='K'
   || exec('ini_tpow','dok_fks')
   ?};
   WSK.index('TR');
   WSK.prefix();
   {? WSK.find_key('A','Mies') || KOSZTY.FUN:=WSK.ref ?};
   {? KOSZTY.edit("exec('chk_kos','!fks_dks_rkod')")
   || {? _=0
      || {? ROZ_KON.last()
         || {!|? {? ROZ_KON.LP>=KOSZTY.LP
                 || ROZ_KON.LP+=1; ROZ_KON.put(); ROZ_KON.prev()
                 ?}
             !}
         ?}
      ?};
      ROZ_KON.LP:=KOSZTY.LP;
      ROZ_KON.ROZ_NAZ:=ROZ_NAZ.ref();
      ROZ_KON.TYP:=KOSZTY.TYP;
      ROZ_KON.MASKA:=KOSZTY.MASKA;
      ROZ_KON.FUN:=KOSZTY.FUN;
      ROZ_KON.FORM:=KOSZTY.FORM;
      ROZ_KON.STR:=KOSZTY.STRONA;
      ROZ_KON.WSP:=KOSZTY.WSP;
      {? ROZ_KON.add()
      || exec('zrd_wyr','!fks_dks_rkod');
         {? KOSZTY.TYP='K' || exec('wyp_doc','!fks_dks_rkod') ?}
      ?}
   ?};
   {? var_pres('TPOW')>0 || obj_del(TPOW) ?}
?}


\chk_kos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Sprawdzenie poprawnosci wypelnienia okienek red. zmiennej KOSZTY
::  OLD: \chk_kos/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.MASKA=35*'?'
|| FUN.info('Podaj maskę konta.'@); 'MASKA'
|| {? KOSZTY.TYP='Z'
   || KOSZTY.WSP:=KOSZTY.WSP$2;
      _p:=__CHK.record(KOSZTY,,'FUN','WSP');
      {? _p='' || exec('spr_roz','!fks_dks_rkod') || _p ?}
   |? KOSZTY.TYP='P' & KOSZTY.WYBOR1='P' & KOSZTY.WSP<0
   || FUN.info('Podaj dodatni procent.'@); 'WSP'
   |? KOSZTY.TYP='P' & KOSZTY.WYBOR1<>'P'
   || __CHK.record(KOSZTY,,'FORM')
   |? KOSZTY.TYP='K'
   || _p:=__CHK.record(KOSZTY,,'MASKA','FUN','MASKA2');
      {? _p=''
      || {? KOSZTY.MASKA2=35*'?'
         || FUN.info('Podaj maskę konta.'@); 'MASKA2'
         || exec('spr_roz','!fks_dks_rkod')
         ?}
      || _p
      ?}
   || ''
   ?}
?}


\spr_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Sprawdzenie, czy podano maske tylko dla maksymalnie jednego slownik
::       wyroznikow z rozdzielnikiem
::  OLD: \spr_roz/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_p:=''; _jest:=0;
{! _i:=1..6 |!
   {? _p=''
   || _roz:=($('KOSZTY.ROZ'+$_i))()='T';
      {? _roz
      || _k:=($('KOSZTY.KOD'+$_i))();
         {? _k*'?'>0 || {? _jest=0 || _jest:=1 || _p:='KOD'+$_i ?} ?}
      ?}
   ?}
!};
{? _p<>'' || FUN.info('Tylko dla jednego wyróżnika z rozdzielnikiem\nmożna podać maskę.'@) ?};
_p


\wyp_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Wypelnienie definicji konta
::  OLD: \wyp_doc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=#ROZ_KON.ref();
ROZ_KON.cntx_psh();
ROZ_KON.prefix();
ROZ_KON.blank();
ROZ_KON.ROZ_NAZ:=ROZ_NAZ.ref();
ROZ_KON.TYP:='D';
ROZ_KON.MASKA:=KOSZTY.MASKA2;
ROZ_KON.STR:=KOSZTY.STR2;
ROZ_KON.KLUCZ:=_ref;
ROZ_KON.add();
exec('zrd_wyr','!fks_dks_rkod',1);
ROZ_KON.cntx_pop()


\zrd_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Uzupelnienie wyroznikow konta po edycji definicji
::  WE: [_a - jesli jest - uzupelnienie dla konta docelowego rozliczenia 'K']
::  OLD: \zrd_wyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
exec('usu_wyr','!fks_dks_rkod');
_p:={? _=0 || 0 || 6 ?};
{! _i:=1..6 |!
   _zn:={? _=0 || KOSZTY.TYP='P' | ($('KOSZTY.ZN'+$(_i+_p)))()='T' || 1 ?};
   _slu:=($('KOSZTY.SLUA'+$(_i+_p)))();
   {? _slu & _zn
   || _kod:=($('KOSZTY.KOD'+$(_i+_p)))();
      {? (KOSZTY.TYP='P' | _>0) & _kod='<- F3 ->'
      || {? _=0 | (_>0 & ($('KOSZTY.WYR'+$_i))()='W')
         || TPOW.prefix(_i+_p);
            {? TPOW.first()
            || ($('KOSZTY.SLUA'+$(_i+_p)+'().SLU()'))();
               SLO.index('SL');
               SLO.prefix(SLU.ref);
               {!|? {? SLO.find_key(TPOW.SLO)
                    || exec('add_wyr','!fks_dks_rkod',_i,_slu,SLO.KOD,SLO.ref,TPOW.KW,'W')
                    ?};
                    TPOW.next()
               !}
            ?}
         |? _>0
         || exec('add_wyr','!fks_dks_rkod',_i,_slu,'',null,100,'K')
         ?}
      || exec('add_wyr','!fks_dks_rkod',_i,_slu,_kod,($('KOSZTY.SLO'+$(_i+_p)))(),100,
         {? _=0 || '' || ($('KOSZTY.WYR'+$_i))() ?})
      ?}
   ?}
!}


\add_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Dodanie rekordu do ROZ_WYR
::   WE: _a - lp
::       _b - ref SLUAPPL
::       _c - maska kodu
::       _d - SLO.ref
::       _e - procent
::       _f - znacznik (czy z klucza, wtedy 'K', wpp 'W')
::  OLD: \add_wyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _c<>'' | (_c='' & _f='K')
|| {? _f='K' || _c:=''; _d:=null ?};
   ROZ_WYR.prefix();
   ROZ_WYR.blank();
   ROZ_WYR.ROZ_KON:=ROZ_KON.ref();
   ROZ_WYR.LP:=_a;
   ROZ_WYR.SLUAPPL:=_b;
   ROZ_WYR.MASKA:=_c;
   ROZ_WYR.SLO:=_d;
   ROZ_WYR.PROC:=_e;
   ROZ_WYR.ZN:=_f;
   ROZ_WYR.add()
?}


\usu_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Usuniecie definicji wyroznikow konta
::  OLD: \usu_wyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROZ_WYR.index('ROZ_WYR'); ROZ_WYR.prefix(ROZ_KON.ref);
{? ROZ_WYR.first() || {! |? ROZ_WYR.del() !} ?}


\pop_zrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'Popraw' w okienkach wert. tabeli ROZ_KON
::  OLD: \pop_zrd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.TYP='K' | KOSZTY.TYP='P' || exec('ini_tpow','dok_fks') ?};
exec('ust_kon','!fks_dks_rkod',1);
{? KOSZTY.edit("exec('chk_kos','!fks_dks_rkod')")
|| ROZ_KON.MASKA:=KOSZTY.MASKA;
   ROZ_KON.FUN:=KOSZTY.FUN;
   ROZ_KON.STR:=KOSZTY.STRONA;
   ROZ_KON.WSP:=KOSZTY.WSP;
   ROZ_KON.FORM:=KOSZTY.FORM;
   {? ROZ_KON.put()
   || exec('zrd_wyr','!fks_dks_rkod');
      {? KOSZTY.TYP='K' || exec('akt_doc','!fks_dks_rkod') ?}
   ?}
?};
{? var_pres('TPOW')>0 || obj_del(TPOW) ?}


\ust_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Ustawienie pol zmiennej KOSZTY na podstawie definicji konta
::   WE: [_a - jest jesli przed akcja Popraw]
::  OLD: \ust_kon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:=KOSZTY.TYP;
KOSZTY.blank();
KOSZTY.TYP:=_typ;
KOSZTY.LP:=ROZ_KON.LP;
KOSZTY.MASKA:=ROZ_KON.MASKA;
KOSZTY.STRONA:=ROZ_KON.STR;
KOSZTY.FUN:=ROZ_KON.FUN;
KOSZTY.FORM:=ROZ_KON.FORM;
KOSZTY.WSP:=ROZ_KON.WSP;
KOSZTY.KONTO:=ROZ_KON.MASKA;
{? KOSZTY.TYP='P'
|| KOSZTY.WYBOR1:={? KOSZTY.FORM<>'' || 'W' || 'P' ?}
?};
exec('ust_wyr','!fks_dks_rkod',_=1);
1


\akt_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Aktualizacja danych konta docelowego dla rozliczenia typu K
::  OLD: \akt_doc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=#ROZ_KON.ref();
ROZ_KON.cntx_psh(); ROZ_KON.index('ROZ_KLU'); ROZ_KON.prefix(ROZ_NAZ.ref,'D',_ref);
{? ROZ_KON.first()
|| ROZ_KON.MASKA:=KOSZTY.MASKA2;
   ROZ_KON.STR:=KOSZTY.STR2;
   ROZ_KON.put()
?};
exec('zrd_wyr','!fks_dks_rkod',1);
ROZ_KON.cntx_pop()


\ust_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Ustawienie danych o definicjach wyroznikow na podstawie tabeli ROZ_WYR
::   WE: [ _a - czy przy akcji Popraw
::         _b - czy dla konta docelowego w typie rozliczenia K]
::  OLD: \ust_wyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0
|| _typ:=KOSZTY.TYP;
   KOSZTY.blank();
   KOSZTY.TYP:=_typ;
   _a:=0;
   {? var_pres('zref')>0 & KOSZTY.TYP='Z' || zref:=ROZ_KON.ref ?};
   {? var_pres('tref')>0 & KOSZTY.TYP<>'Z' || tref:=ROZ_KON.ref ?}
?};
ROZ_KON.cntx_psh();
{? _=2
|| _p:=6;
   ROZ_KON.index('ROZ_KLU');
   ROZ_KON.prefix(ROZ_NAZ.ref(),'D',#ROZ_KON.ref);
   {? ROZ_KON.first()
   || pop_kon:=ROZ_KON.MASKA;
      KOSZTY.MASKA2:=ROZ_KON.MASKA;
      KOSZTY.STR2:=ROZ_KON.STR
   || KOSZTY.MASKA2:='';
      KOSZTY.STR2:='Wn'
   ?}
|| _p:=0
?};
{? _<2 | (_a>-1 & _=2)
|| KS_W.index('LP');
   KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROZ_KON.MASKA));
   _lp:=1;
   ROZ_WYR.index('ROZ_WYR');
   ROZ_WYR.prefix(ROZ_KON.ref);
   {! _i:=1..6 |!
      {? KS_W.find_key(_i)
      || ($('KOSZTY.SLU'+$(KS_W.LP+_p)+':=KS_W.SLU().SLU().NAZ'))();
         ($('KOSZTY.SLUA'+$(KS_W.LP+_p)+':=KS_W.SLU'))();
         {? KOSZTY.TYP='Z' | (KOSZTY.TYP='K' & _<2)
         || {? KS_W.ROZDZ='T' || ($('KOSZTY.ROZ'+$(_i+_p)+':=''T'''))() ?};
            {? ROZ_WYR.find_key(_i)
            || ($('KOSZTY.KOD'+$(_i+_p)+':=ROZ_WYR.MASKA'))();
               ($('KOSZTY.SLO'+$(_i+_p)+':=ROZ_WYR.SLO'))();
               ($('KOSZTY.ZN'+$(_i+_p)+':=''T'''))()
            || ($('KOSZTY.KOD'+$(_i+_p)+':='''''))()
            ?}
         || {? KS_W.ROZDZ='N'
            || {? ROZ_WYR.find_key(_i)
               || {? _=2 || ($('KOSZTY.WYR'+$_i+':=ROZ_WYR.ZN'))() ?};
                  ($('KOSZTY.KOD'+$(_i+_p)+':=ROZ_WYR.SLO().KOD'))();
                  ($('KOSZTY.SLO'+$(_i+_p)+':=ROZ_WYR.SLO'))()
               ?}
            || ($('KOSZTY.KOD'+$(_i+_p)+':=''<- F3 ->'''))();
               {? ROZ_WYR.find_key(_i)
               || {? _=2 || ($('KOSZTY.WYR'+$_i+':=ROZ_WYR.ZN'))() ?};
                  {? _>0 & _a &  ROZ_WYR.SLO<>null
                  || {!|? TPOW.LP:=_i+_p;
                          TPOW.SLU:=KS_W.SLU().SLU().NAZ;
                          TPOW.SLO:=ROZ_WYR.SLO().KOD;
                          TPOW.KW:=ROZ_WYR.PROC;
                          TPOW.add();
                          ROZ_WYR.next() & ROZ_WYR.LP=_i
                     !}
                  ?}
               ?}
            ?}
         ?}
      ?}
   !}
?};
ROZ_KON.cntx_pop();
{?  KOSZTY.TYP='K'
|| {? _=0 || exec('ust_wyr','!fks_dks_rkod',-1,1)
   |? _=1 || exec('ust_wyr','!fks_dks_rkod',_a,1)
   ?}
?};
''


\usu_zrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'Usun' w okienkach wert. tabeli ROZ_KON
::  OLD: \usu_zrd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{?  ROZ_KON.sel_size() | FUN.ask('Usunąć konto rozliczenia?'@)
|| do();
   {? ROZ_KON.next()
   || ROZ_KON.prev();
      exec('del_kon','!fks_dks_rkod');
      {? ROZ_KON.sel_size()=0
      || ROZ_KON.cntx_psh();
         {!|? ROZ_KON.LP-=1; ROZ_KON.put(); ROZ_KON.next !};
         ROZ_KON.cntx_pop()
      ?}
   || exec('del_kon','!fks_dks_rkod')
   ?};
   end()
?}


\del_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Usuniecie definicji konta docelowego dla rozliczenia typu K
::  OLD: \del_kon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.TYP='K'
|| _ref:=#ROZ_KON.ref();
   ROZ_KON.cntx_psh();
   ROZ_KON.index('ROZ_KLU');
   ROZ_KON.prefix(ROZ_NAZ.ref,'D',_ref);
   {? ROZ_KON.first()
   || exec('usu_wyr','!fks_dks_rkod');
      ROZ_KON.del()
   ?};
   ROZ_KON.cntx_pop()
?};
exec('usu_wyr','!fks_dks_rkod'); ROZ_KON.del()


\czy_kusu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed akcja grupowa 'Usun' w okienkach wert. tabeli ROZ_KON
::  OLD: \czy_kusu/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.ask('Usunąć zaznaczone konta rozliczenia?'@)


\po_kusu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po akcji grupowej 'Usun' w okienkach wert. tabeli ROZ_KON
::  OLD: \po_kusu/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZ_KON.first()
|| {! |? ROZ_KON.LP-=1; ROZ_KON.put(); ROZ_KON.next !}
?}


\wys_zrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'Wyswietl' w okienkach wert. tabeli ROZ_KON
::  OLD: \wys_zrd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ust_kon','!fks_dks_rkod');
KOSZTY.display()


\wyr_ksel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja wyrozniki - w okienkach wertowania tabeli ROZ_KON
::  OLD: \wyr_ksel/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROZ_KON.cntx_psh();
SSTALE.AR();
{? KOSZTY.TYP='K'
|| ROZ_KON.index('ROZ_KLU');
   ROZ_KON.prefix(ROZ_NAZ.ref,'D',#ROZ_KON.ref);
   ROZ_KON.first()
?};
KS_W.index('LP'); KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROZ_KON.MASKA));
{? KS_W.first()
|| {? KS_W.size=1
   || exec('roz_rkd','!fks_dks_rkod')
   || {? var_pres('SEL_KKSW')<0;1
      || SEL_KKSW:=KS_W.mk_sel(,,,'ks_w_wer2');
         KS_W.win_fld(SEL_KKSW,,'LP');
         KS_W.win_fld(SEL_KKSW,SLU,'NAZ');
         KS_W.win_act(SEL_KKSW,,'Rekord',,,,"KS_W.SLU().SLU();
                                             ROZ_WYR.index('ROZ_WYRK'); ROZ_WYR.prefix(ROZ_KON.ref);
                                             {? ROZ_WYR.find_key(KS_W.LP) || exec('findtmp','#color') || ''?} ");
         KS_W.win_act(SEL_KKSW,,'Formuła','Wybierz'@@,,,"exec('roz_rkd','!fks_dks_rkod')",,1,,,,'W')
      ?};
      KS_W.win_sel(SEL_KKSW);
      {! |?
         {? KS_W.select(,1)
         || KS_W.SLU().SLU(); exec('roz_rkd','!fks_dks_rkod'); 1
         || 0
         ?}
      !}
   ?}
|| FUN.info('Nie zdefiniowano słowników wyróżników dla docelowego konta.'@); 0
?};
ROZ_KON.cntx_pop()


\roz_rkd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Wyswietlenie wyroznikow dla wybranego slownika - z poziomu tabeli ROZ_KON
::  OLD: \roz_rkd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KS_W.SLU().SLU();
ROZ_WYR.index('ROZ_WYRK');
ROZ_WYR.prefix(ROZ_KON.ref,KS_W.LP);
{? ROZ_WYR.first()
|| {? KOSZTY.TYP='K' & ROZ_WYR.ZN='K'
   || FUN.info('Wyróżnik określany na podstawie wyróżnika klucza.'@)
   || ROZ_WYR.win_sel('WER');
      ROZ_WYR.select()
   ?}
|| FUN.info('Nie zdefiniowano warunków dla wyróżników.'@); 0
?}


\po_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po redakcji pola KOSZTY.WYBOR1
::  OLD: \po_wyb/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.WYBOR1='P' || KOSZTY.FORM:='' || KOSZTY.WSP:=0 ?};
1


\ae_kkon2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS:
::  OLD: \ae_kkon2/skid_edk.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=exec('ae_konto','edkonto',_a,'KOSZTY','MASKA2',0,2,0,"exec('po_kon','!fks_dks_rkod',1)");_a


\po_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po red. pol KOSZTY.KONTO, KOSZTY.KONTO2
::   WE: [_a - jesli jest to wywolanie dla KOSZTY.KONTO2, wpp dla KOSZTY.KONTO]
::  OLD: \po_kon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
SSTALE.AR();
_s:=ROK_F.SYNT;
{? PAR_SKID.get(35)='T'
|| {? _=0
   || _konto:=KOSZTY.KONTO; _maska:=KOSZTY.MASKA; _p:=0
   || _konto:=KOSZTY.KONTO2; _maska:=KOSZTY.MASKA2; _p:=6
   ?};
   _lp:=1;
   {? _s+_maska<>_s+_konto
   || KS_W.cntx_psh();
      {? (_s+fld)*'?'=0
      || KS_W.index('LP');
         KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+_maska));
         _dalej:=KS_W.first()
      || _dalej:=0
      ?};
      {? _dalej
      || {!|? ($('KOSZTY.SLU'+$(_p+KS_W.LP)+':=KS_W.SLU().SLU().NAZ'))();
              {? _p=0 || ($('KOSZTY.ZN'+$KS_W.LP+':='''''))() ?};
              {? (KOSZTY.TYP='P' | _>0) & KS_W.ROZDZ='T'
              || ($('KOSZTY.KOD'+$(_p+KS_W.LP)+':=''<- F3 ->'''))()
              || ($('KOSZTY.KOD'+$(_p+KS_W.LP)+':='''''))();
                 {? _=0
                 || {? KS_W.ROZDZ='T'
                    || ($('KOSZTY.ROZ'+$(KS_W.LP+_p)+':=''T'''))()
                    || ($('KOSZTY.ROZ'+$(KS_W.LP+_p)+':='''''))()
                    ?}
                 ?}
              ?};
              ($('KOSZTY.SLO'+$(_p+KS_W.LP)+':=null'))();
              ($('KOSZTY.SLUA'+$(_p+KS_W.LP)+':=KS_W.SLU'))();
              _lp+=1;
              KS_W.next()
         !}
      ?};
      {? _=0
      || {? _lp<6
         || {! _i:=_lp..6 |!
            ($('KOSZTY.SLU'+$_i+':='''''))();
            ($('KOSZTY.KOD'+$_i+':='''''))();
            ($('KOSZTY.ROZ'+$_i+':=''N'''))();
            ($('KOSZTY.SLO'+$_i+':=null'))();
            ($('KOSZTY.ZN'+$_i+':='''''))()
            !}
         ?};
         KOSZTY.KONTO:=KOSZTY.MASKA
      || {? _lp<6
         || {! _i:=_lp+_p..12 |!
            ($('KOSZTY.SLU'+$_i+':='''''))();
            ($('KOSZTY.KOD'+$_i+':='''''))();
            ($('KOSZTY.SLO'+$_i+':=null'))()
            !}
         ?};
         KOSZTY.KONTO2:=KOSZTY.MASKA2
      ?};
      {? (KOSZTY.TYP='P' | _>0) || TPOW.erase() ?};
      {? KOSZTY.TYP='P' || win_disp() ?};
      KS_W.cntx_pop()
   ?}
?};
1


\ae_kkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS:
::  OLD: \ae_kkon/skid_edk.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=exec('ae_konto','edkonto',_a,'KOSZTY','MASKA',0,2,0,"exec('po_kon','!fks_dks_rkod')");_a


\pw_fun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed red. dla pola KOSZTY.FUN
::  OLD: \pw_fun/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
RKS.RWSK:='A';
1


\pr_kwsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed redakcja pola KOSZTY.WSP
::  OLD: \pr_kwsp/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.TYP='P' & KOSZTY.WYBOR1='W' || 0 || 1 ?}


\po_kwsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po redakcji pola KOSZTY.WSP
::  OLD: \po_kwsp/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.WSP:=KOSZTY.WSP$2;
{? KOSZTY.TYP='P' & KOSZTY.WSP>100
|| FUN.info('Procent kwoty do rozliczenia\n nie może być większy niż 100.'@); 0
|| 1
?}


\pr_kform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed redakcja pola KOSZTY.FORM
::  OLD: \pr_kform/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.TYP='P' & KOSZTY.WYBOR1='P' || 0 || 1 ?}


\pw_slu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed wyswietl dla pol: KOSZTY.SLU*
::   WE: _a - numer pola
::  OLD: \pw_slu/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
($('KOSZTY.SLU'+$_a+'<>'''''))()


\wz_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Formula na wzorzec dla pol KOSZTY.KOD*
::   WE: _a - numer pola
::  OLD: \wz_kod/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ($('KOSZTY.KOD'+$_a))()<>'<- F3 ->'
|| _dl:=($('KOSZTY.SLUA'+$_a+'().SLU().DL'))();
   _dl*'u'+'#'
|| ''
?}


\pw_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed wyswietl dla pol: KOSZTY.KOD*
::   WE: _a - numer pola
::  OLD: \pw_kod/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ($('KOSZTY.SLU'+$_a+'<>'''''))()
|| {? KOSZTY.TYP='Z' & ($('KOSZTY.ZN'+$_a))()<>'T'
   || 0
   |? KOSZTY.TYP='K' & _a<7 & ($('KOSZTY.ZN'+$_a))()<>'T'
   || 0
   |? KOSZTY.TYP='K' & _a>6 & ($('KOSZTY.WYR'+$(_a-6)))()='K'
   || 0
   |? (KOSZTY.TYP='P' | _a>6) & ($('KOSZTY.KOD'+$_a))()='<- F3 ->'
   || {? var_pres('TPOW')>0
      || TPOW.prefix(_a);
         {? TPOW.first() || exec('findtmp','#color') || '' ?}
      || _ref:=#ROZ_KON.ref();
         ROZ_KON.cntx_psh();
         {? _a>6
         || ROZ_KON.index('ROZ_KLU');
            ROZ_KON.prefix(ROZ_NAZ.ref,'D',_ref);
            ROZ_KON.first()
         ?};
         ROZ_WYR.index('ROZ_WYR');
         ROZ_WYR.prefix(ROZ_KON.ref,{? _a>6 || _a-6 || _a ?});
         _ok:=ROZ_WYR.first();
         ROZ_KON.cntx_pop();
         {? _ok || exec('findtmp','#color') || '' ?}
      ?}
   || ''
   ?}
?}


\pr_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed red. dla pol: KOSZTY.KOD*
::   WE: _a - numer pola
::  OLD: \pr_kod/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ($('KOSZTY.SLU'+$_a+'<>'''''))()
|| {? KOSZTY.TYP='K' & _a>6 & ($('KOSZTY.WYR'+$(_a-6)))()='K'
   || 0
   |? (KOSZTY.TYP='Z' | (KOSZTY.TYP='K' & _a<7))  & ($('KOSZTY.ZN'+$_a))()<>'T'
   || 0
   || 1
   ?}
?}


\f3_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: F3 dla pol: KOSZTY.KOD*
::   WE: _a - numer pola
::  OLD: \f3_kod/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.fld_fml('SUMA','BEFORE_DISPLAY',"
   _kw:=0;
   TPOW.cntx_psh();
   {? SLO.f_active()
   || {? SLO.f_first()
      || {!
         |? TPOW.prefix(KS_W.LP,SLO.SLU().NAZ,SLO.KOD,);
            {? TPOW.first()
            || _kw+=TPOW.KW
            ?};
            SLO.f_next()
         !}
      ?}
   || TPOW.prefix(KS_W.LP);
      {? TPOW.first()
      || {!
         |? _kw+=TPOW.KW;
            TPOW.next()
         !}
      ?}
   ?};
   TPOW.cntx_pop();
   KOSZTY.SUMA:=_kw;
   KOSZTY.POZ:=100-_kw;
   ~~
");
LP_WYR:=_a;
{? KOSZTY.TYP='P' | _a>6
|| {? ($('KOSZTY.KOD'+$_a))()='<- F3 ->'
   || SLO.win_sel('K_WYR2');
      SLO.hdr_sel();
      SLO.hdr_sel(' rozdzielnik procentowy'@);
      TPOW.prefix(_a,);
      KOSZTY.SUMA:=0;
      {? TPOW.first() || {! |? KOSZTY.SUMA+=TPOW.KW; TPOW.next !} ?};
      KOSZTY.POZ:=100-KOSZTY.SUMA
   || SLO.win_sel('ONE_SEL')
   ?}
|| SLO.win_sel('K_WYR1');
   SLO.hdr_sel();
   MASKA:=($('KOSZTY.KOD'+$_a))();
   SLO.hdr_sel(' (maska: %1)'@[MASKA])
?};
SLO.win_edit();
SLO.index('SL');
SLO.prefix(($('KOSZTY.SLUA'+$_a+'().SLU'))());
SLUAPPL.SLU();
{? ($('KOSZTY.SLO'+$_a))()<>null || ($('KOSZTY.SLO'+$_a+'()'))() || SLO.first() ?};
{? SLO.select(,1)
|| ($('KOSZTY.SLO'+$_a+':=SLO.ref; KOSZTY.KOD'+$_a+':=SLO.KOD'))()
?};
KOSZTY.fld_fml('SUMA','BEFORE_DISPLAY',"*");
VAR_DEL.delete('MASKA','LP_WYR');
1


\po_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po red. dla pol: KOSZTY.KOD*
::   WE: _a - numer pola
::  OLD: \po_kod/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? (KOSZTY.TYP='P' | _a>6) & ($('KOSZTY.KOD'+$_a))()='<- F3 ->'
|| 1
|| _dl_s:=($('KOSZTY.SLUA'+$_a+'().SLU().DL'))();
   _kod:=($('KOSZTY.KOD'+$_a))();
   {? +_kod<_dl_s & KOSZTY.TYP<>'P' & _a<7 || _kod:=_kod+((_dl_s-(+_kod))*'?') || _kod:=_dl_s+_kod ?};
   fld(_kod);
   {? (KOSZTY.TYP<>'P' & _a<7 & _kod*'?') | _kod=''
   || ($('KOSZTY.SLO'+$_a+':=null'))(); 1
   || SLO.index('SL'); SLO.prefix();
      {? ~SLO.find_key(SLU.ref(),_kod)
      || FUN.info('Podany kod nie występuje w słowniku.'@);0
      || ($('KOSZTY.SLO'+$_a+':=SLO.ref'))(); 1
      ?}
   ?}
?}


\pw_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed wysw. dla pol: KOSZTY.SLO*
::   WE: _a - numer pola
::  OLD: \pw_slo/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ($('KOSZTY.SLU'+$_a+'<>'''''))()
|| {? KOSZTY.TYP='K' & _a>6 & ($('KOSZTY.WYR'+$(_a-6)))()='K'
   || 0
   || ($('KOSZTY.SLO'+$_a))()<>null
   ?}
?}


\po_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po red. pol KOSZTY.WYR7..12
::  OLD: \po_wyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
win_disp();
1


\pr_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed red. pol KOSZTY.WYR7..12
::   WE: _a - numer 1..6
::  OLD: \pr_wyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
($('KOSZTY.SLU'+$(_a+6)+'<>'''''))()


\po_zn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed redakcja pola KOSZTY.ZN1..6
::   WE: _a - numer do akronimu KOSZTY.ZN?
::  OLD: \po_zn/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld='T'
|| ($('KOSZTY.SLUA'+$_a+'().SLU()'))();
    {? ($('KOSZTY.KOD'+$_a))()=''
    || ($('KOSZTY.KOD'+$_a+':=SLU.DL*''?'''))()
    ?}
|| ($('KOSZTY.KOD'+$_a+':='''''))()
?};
1


\wyr_mas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Rekord przed w okienku WYR1 tabeli SLO
::  OLD: \wyr_mas/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? MASKA<>''
|| {? F.spr_mask(MASKA,SLO.KOD) || exec('findtmp','#color') ?}
|| 0
?}


\pp_kproc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed redakcja pola KOSZTY.PROC
::  OLD: \pp_kproc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.PROC$2=0 & KOSZTY.POZ$2=0
|| FUN.info('Rozdzielono już 100%%.'@); 0
|| {? KOSZTY.PROC$2=0
   || KOSZTY.PR_PROC:=0;
      KOSZTY.PROC:=KOSZTY.POZ
   || KOSZTY.PR_PROC:=KOSZTY.PROC
   ?}; 1
?}


\po_kproc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Rekord po w okienku K_WYR2 tabeli SLO
::  OLD: \po_kproc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? TPOW.find_key(SLU.NAZ,SLO.KOD)
|| {? KOSZTY.PROC$2=0
   || TPOW.del()
   || TPOW.KW:=KOSZTY.PROC; TPOW.put()
   ?}
|| TPOW.LP:=LP_WYR;
   TPOW.SLU:=SLU.NAZ;
   TPOW.SLO:=SLO.KOD;
   TPOW.KW:=KOSZTY.PROC;
   TPOW.add()
?};
1


\wyr_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Record przed dla okienka wert. K_WYR2 tabeli SLO
::  OLD: \wyr_proc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? TPOW.find_key(SLU.NAZ,SLO.KOD)
|| KOSZTY.PROC:=TPOW.KW;
   exec('findtmp','#color')
|| KOSZTY.PROC:=0;
   ''
?}


\wyr_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po redakcji pola KOSZTY.PROC
::  OLD: \wyr_rec/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.PROC:=KOSZTY.PROC$2;
{? KOSZTY.PROC$2<0
|| FUN.info('Należy podać liczbę dodatnią.'@); 0
|? KOSZTY.PR_PROC$2=0 & KOSZTY.PROC$2=0
|| FUN.info('Należy podać liczbę dodatnią.'@); 0
|? KOSZTY.SUMA-KOSZTY.PR_PROC+KOSZTY.PROC>100
|| FUN.info('Przekroczono 100%%.'@);
   KOSZTY.PROC:=KOSZTY.PR_PROC+KOSZTY.POZ; 0
|| 1
?}


\be_km2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.70]
:: OPIS: Formula przed redakcja konta w polu KOSZTY.MASKA2
::  OLD: \be_km2/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.KONTO2:=KOSZTY.MASKA2; 1


\wydr_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Wydruk definicji rozliczenia kosztow
::  OLD: \wydr_roz/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FKS_DKS_RKOD','fks_roz_kosz',,,,,,,,'W')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 6f03baacc14862132b8f60b7d5fe5f57cf8b3c31d64209c57445d80dbfbfaa9c2de9c5f21213a14866f7bfff16194e73e353a91477c31f742279f04a829ed7fe55aaf05959d47f5c2684088e980c13fc727809105c2a7e02f6954625c9eeaabd6d00a0d37cfd7ea77482e0f2781ac08bbea5a3eb38d64924ef17d15589056d75

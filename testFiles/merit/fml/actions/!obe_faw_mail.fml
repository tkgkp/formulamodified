:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_faw_mail.fml
:: Utworzony: 26.10.2022
:: Autor: PBS
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_MAIL - Wysłanie powiadomień e-mail o niezamkniętych wnioskach
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła główna czynności OBE_FAW_MAIL - Wysłanie powiadomień e-mail o niezamkniętych wnioskach
::----------------------------------------------------------------------------------------------------------------------
::# properties=SEND
::# kind=WE, symbol=EMAIL_ERR, type=STRING, name=E-mail z inf. o błędach w wysyłce, required=N

_mp:=params_get().mp;
_in:=params_get().in;
_komunik:=(_mp.pathTodo() | _mp.pathProc()) & ~_mp.isAutoRun() & ~_mp.isService();
_errmail:={? var_press('EMAIL_ERR',_in)>0 || _in.EMAIL_ERR || '' ?};
_ok:=1;
_title:='Powiadomienie o niezamkniętych wnioskach w obiegu'@;
_tab:=exec('tab','!obe_faw_mail');
{? ~_tab.first()
|| {? _komunik
   || FUN.info('Brak niezamkniętych wniosków w obiegu.'@)
   ?};
   _ok:=1
|| _ok:=~_komunik |
        FUN.ask('Czy uruchomić wysyłanie wiadomości e-mail z informacją o \nniezamkniętych wnioskach w obiegu?'@);
   {? _ok
   || exec('send_remind','!obe_faw_mail',_in.FROM,_in.SENDER,_in.REPLYTO,_errmail,_title)
   ?}
?};
{? _ok || _mp.done() ?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła na opis czynności w ToDo
::----------------------------------------------------------------------------------------------------------------------
'Wyślij powiadomienie e-mail o niezamkniętych wnioskach w obiegu'@@


\tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Tworzy tabelę tymczasową z niezamkniętymi wnioskami w obiegu
::----------------------------------------------------------------------------------------------------------------------
TYPOBIEG.cntx_psh();
TYPOBIEG.index('UNIK'); TYPOBIEG.prefix('Obieg wniosków',);
_typobieg:={? TYPOBIEG.first() || TYPOBIEG.ref() || null ?};
TYPOBIEG.cntx_pop();
EDOKUM.cntx_psh();
_tab:=sql(
   'select '+
      'distinct EDOKUM.ID, EDOKUM.DATAW, ETYPY.NAZWA as TYP, OSOBA.PIERWSZE, OSOBA.NAZWISKO, USERS.EMAIL, '+
      'USERS.REFERENCE as U_REF, EDOKUM.TR as TEMAT, EDOKUM.USERS as REJOSOB, EDOKUM.REFERENCE as EREF '+
   'from '+
      '@EDOKUM '+
   'join '+
      '@EDOKOS using (EDOKUM.REFERENCE, EDOKOS.EDOKUM) '+
   'join '+
      '@USERS using (USERS.REFERENCE, EDOKOS.USERS) '+
   'left join '+
      '@OSOBA using (OSOBA.REFERENCE, EDOKUM.DOSTAWCA) '+
   'join '+
      '@ETYPY using (ETYPY.REFERENCE, EDOKUM.TYP) '+
   'where '+
      'EDOKUM.ZAM<>\'T\' and '+
      'EDOKUM.TYPOBIEG=:_a and '+
      'EDOKOS.B_PREL=EDOKUM.B_PREL and '+
      'EDOKOS.WID=\'T\' and '+
      'EDOKUM.FIRMA=\':_b\' and ETYPY.W_PORTAL<>\'T\' '+
      'or '+
      'EDOKUM.TYPOBIEG=:_a and '+
      '(EDOKUM.STATUS=\'s\' or EDOKUM.STATUS=\'S\' or EDOKUM.STATUS=\'R\') and '+
      'EDOKUM.FIRMA=\':_b\' and ETYPY.W_PORTAL=\'T\' '
   ,_typobieg,$REF.FIRMA
);
EDOKUM.cntx_pop();
_tab


\tabMail
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Tablica przechowująca treść maila
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('BODY','LP','add');
_tab.LP:=0;
_tab.BODY:=tab_tmp(1,
   'LP','INTEGER','Lp',
   'TEKST','STRING[255]','Tekst'
);
_tab.add:="
   {!
   |? .LP+=1;
      .BODY.LP:=.LP;
      .BODY.TEKST:=255+_a; _a:=255-_a;
      .BODY.add();
      _a<>''
   !}
";
_tab


\send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Utworzenie i wysyłka e-mail
::   WE: _a - Nadawca
::       _b - Od
::       _c - Odpowiedź do
::       _d - Tabela tymcz. z informacją o niezamkniętych wnioskach
::       _e - Tytuł
::       _f - Odbiorca
::----------------------------------------------------------------------------------------------------------------------
_tab:=_d;
exec('czytaj','#stalesys',,XINFO,'LINK_SRV','LINK_PRT','LINK_INT');
{? XINFO.LINK_INT<>''
|| _address:=XINFO.LINK_INT
|| _address:=0
?};
{? _tab.first()
|| _mail:=exec('tabMail','!obe_faw_mail');
   _mail.add('<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Final//EN">');
   _mail.add('<html>');
   _mail.add('<head>');
   _mail.add('<meta http-equiv="content-type" content="text/html; charset=iso-8859-2">');
   _mail.add('</head>');
   _mail.add('<body>');
   _mail.add('<BR><BR>');
   _date:=' na dzień %1'@[$date()];
   _mail.add('<h3>'+_e+_date+'</h3><br><br>');
   _mail.add('<table valign=\"top\" border=\"1\" cellspacing=\"0\" cellpadding=\"0\"  >');
   _mail.add(
      '<tr class="head3">'
      '<td width="200"><b>'+'Zarejestrowano'@+'</b></td>'
      '<td width="300"><b>'+'Rejestrujący'@+'</b></td>'
      '<td width="300"><b>'+'Identyfikator wniosku'@+'</b></td>'
      '<td width="300"><b>'+'Osoba'@+'</b></td>'
      '<td width="300"><b>'+'Temat'@+'</b></td>'
      '<td width="300"><b>'+'Typ'@+'</b></td>'
      '<td width="300"><b>'+'Link'@+'</b></td>'
      '</tr>'
   );
   {!
   |? _osrej:='';
      USERS.cntx_psh();
      {? USERS.seek(_tab.REJOSOB)
      || _osrej:=USERS.DANE
      ?};
      USERS.cntx_pop();

      EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_tab.EREF));
      EDOKUM.index('TYP'); EDOKUM.prefix();
      _uri_html:='';
      {? EDOKUM.seek(_tab.EREF)
      || _uri_html:=exec('link_uri','#system',exec('obj_ntab_set','#array',,'LINK',EDOKUM.uidref()),_address)
      ?};
      EDOKUM.cntx_pop();

      _mail.add('<tr class=\"wiersz\">');
      _mail.add('<td class=\"wiersz\" width=\"200\">'+$_tab.DATAW+'</td>');
      _mail.add('<td class=\"wiersz\" width=\"300\">'+_osrej+'</td>');
      _mail.add('<td class=\"wiersz\" width=\"300\">'+_tab.ID+'</td>');
      _mail.add('<td class=\"wiersz\" width=\"300\">'+_tab.PIERWSZE+' '+_tab.NAZWISKO+'</td>');
      _mail.add('<td class=\"wiersz\" width=\"300\">'+_tab.TEMAT+'</td>');
      _mail.add('<td class=\"wiersz\" width=\"300\">'+_tab.TYP+'</td>');
      {? _uri_html<>''
      || _mail.add('<td class=\"wiersz\" width=\"300\"><a href=\"'+_uri_html+'\">'+'Link do wniosku'@+'</a></td>')
      ?};
      _mail.add('</tr>');
      _tab.next()
   !};
   _mail.add('</table>');
:: Dodanie e-maila
   _args:=exec('add_email_a','#mailbox');
   _args.FROM:=_a;
   _args.SENDER:=_b;
   _args.REPLYTO:=_c;
   _args.RCV:=_f;
   _args.SUB:=_e;
   _args.BODYH:=_mail.BODY;
   exec('add_email','#mailbox',_args)
?};
1


\send_remind
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Utworzenie przypomnień e-mail
::   WE: _a - Nadawca
::       _b - Od
::       _c - Odpowiedź do
::       _d - Odbiorca (błąd)
::       _e - Tytuł
::----------------------------------------------------------------------------------------------------------------------
_err:=_d<>'';
USERS.cntx_psh();
{? _err
|| VAR_DEL.delete('EBody');
   EBody:=obj_new('BODY','LP','ind','add','first');
   EBody.first:=1;
   EBody.BODY:=tab_tmp(1,
      'LP','INTEGER','Lp',
      'TEKST','STRING[255]','Tekst',
      'USR','INTEGER',
   );
   EBody.ind:=EBody.BODY.ndx_tmp('',1,'USR',,0);
   EBody.LP:=0;
   EBody.add:="
      {? EBody.first
      || EBody.first:=0;
         EBody.add('<b>Błędy w adresach e-mail użytkowników dokumentów w obiegu:</b>')
      ?};
      {? var_press('_b')>0 & _b
      || EBody.BODY.cntx_psh();
         EBody.BODY.index(EBody.ind); EBody.BODY.prefix(#_b);
         _ok:=~EBody.BODY.first();
         EBody.BODY.cntx_pop()
      || _ok:=1
      ?};
      {? _ok
      || EBody.LP+=1;
         EBody.BODY.LP:=EBody.LP;
         EBody.BODY.TEKST:=_a;
         EBody.BODY.USR:={? var_press('_b')>0 || #_b || 0 ?};
         EBody.BODY.add()
      ?}
   "
?};

_tab_edokum:=exec('tab','!obe_faw_mail');
_tab_usr:=sql('select distinct U_REF from :_a',_tab_edokum);
_ndx:=_tab_edokum.ndx_tmp(,1,'U_REF',,,'DATAW',,,'ID',,);
_tab_edokum.index(_ndx);
{? _tab_usr.first()
|| {! |?
      _useek:=USERS.seek(_tab_usr.U_REF,,1);
      _usr:=_tab_usr.U_REF;
      _tab_edokum.prefix(_usr);
      {? _tab_edokum.first
      || {? _useek & exec('mail_ok','#email',USERS.EMAIL)=0
         || {? _err
            || EBody.add(USERS.KOD+' - nieprawidłowy adres e-mail: '+USERS.EMAIL,USERS.ref())
            ?}
         || exec('send','!obe_faw_mail',_a,_b,_c,_tab_edokum,_e,_tab_edokum.EMAIL)
         ?}
      ?};
      _tab_usr.next()
   !}
?};
_tab_edokum.ndx_drop();
{? _err
|| {? EBody.BODY.first()
   || exec('send_error','!obe_faw_mail',_b,_a,_c,_d,EBody.BODY)
   ?};
   VAR_DEL.delete('EBody')
?};
USERS.cntx_pop();
1


\send_error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wysyła powiadomienie o błędach w adresach e-mail użytkowników
::   WE: _a - od
::       _b - wysyłający
::       _c - odpowiedź do
::       _d - odbiorca
::       _e - tabela z treścią maila
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('add_email_a','#mailbox');
_args.FROM:=_a;
_args.SENDER:=_b;
_args.REPLYTO:=_c;
_args.RCV:=_d;
_args.SUB:='Błędy w adresach e-mail użytkowników obiegu wniosków';
_args.BODYT:=_e;
exec('add_email','#mailbox',_args)

:Sign Version 2.0 jowisz:1045 2024/02/05 13:40:07 8e96e918b1eb76528f53f9866826bb2cfcf9de1a59ebf17b60c1a84b4804d6346f02a40804ef0b8683e938cf0b3a2a3534a6525c4e33e36535652213faf7f3b9e615e37e5996f6b01bd0781fdfd8512d2387bcd70ab3a227c5b4749e4a95074e3488c177ee2f6158d505cb0ae0a4e5e97801bc3027089c40e49d70a87c1e7282

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zlc_wrac.fml
:: Utworzony: 02.06.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PPL_ZLC_WRAC - Wydruk rachunku do umowy
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wydruk rachunku do umowy - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd','RH','RH.ZLE().P().get()','ZC')
::
::# kind=WE, symbol=RH, type=_RH, name=Wskazanie rachunku do umowy, required=T, keyref=T
::
_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

SEEK.RH:=_in.RH;
SEEK.RH().ZLE().OSOBA();

_id:=exec('ref2uid','#table',_in.RH);
_result:='';

{? _id=''
|| _result:=exec('error','!ppl_zlc_wrac')

|? _mp.isMicro()
|| _mp.keyRef(_id);
   exec('rh_drukuj','zlec_rh');
   _mp.delRef(_id);
   _mp.cancel()

|| _mp.save(_ib,_rv);
   _msg:='Wydruk rachunku do umowy.\n%1\nZawarta z %2'@
      [exec('ZC','#to_string'),exec('OSOBA','#to_string')];
   {? (_todo:=FUN.choice(_msg,,'Zakończ'@,'Drukuj'@))<>0
   || exec('rh_drukuj','zlec_rh')
   ?};
   {? _todo=1
   || _mp.done()
   || _mp.keep()
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wydruk rachunku do umowy - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().mp.load(exec('kind_in','#b_port'));

{? type_of(_in.RH)<>type_of(null) | _in.RH=null
|| return(exec('error','!ppl_zlc_wrac'))
?};

SEEK.RH:=_in.RH;
SEEK.RH().ZLE().OSOBA();

{? OSOBA.OBCY='T'
|| 'Wydrukuj rachunek dla %1 %2: Paszport - %3. Umowa nr %4 z dnia %5 r.'@@[OSOBA.NAZWISKO,OSOBA.PIERWSZE,OSOBA.PASZPORT,ZC.NU,ZC.DU$4]
|? +OSOBA.PESEL
|| 'Wydrukuj rachunek dla %1 %2: PESEL - %3. Umowa nr %4 z dnia %5 r.'@@[OSOBA.NAZWISKO,OSOBA.PIERWSZE,OSOBA.PESEL,ZC.NU,ZC.DU$4]
|| 'Wydrukuj rachunek dla %1 %2: Data urodzenia - %3. Umowa nr %4 z dnia %5 r.'@@[OSOBA.NAZWISKO,OSOBA.PIERWSZE,$OSOBA.UR_DATA,ZC.NU,ZC.DU$4]
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca treść komunikatu błędu.
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Wydrukowanie rachunku do umowy cywilnoprawnej niemożliwe.'@

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 bf656b8be5b6da3e4873d6e2b50fa2bcb63912d16991d0831b3b6a979c4bf763f9aa645feb9978fe8abffe9419cde8dcd731198e0b5d4bcffc9e3c676cdb186828db6ef3cd5eee9398fc2e01c9822a738926df3b7fd3533735f29f3070685664cbeec592e6b7ce42984e3c92df4cc21c0e55501f714f3fc09e2bf33ea0a60381

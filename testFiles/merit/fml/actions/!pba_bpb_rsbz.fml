:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_rsbz.fml
:: Utworzony: 30.11.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Formuły czynności PBA_BPB_RSBZ - Zamknięcie sesji badań.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Zamknięcie sesji badań - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=ZO_PROC, type=_ZO_PROC, name=Wskazanie sesji ocen, required=T, keyref=T
::
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,NIEOTWARTA,ZAMKNIĘTA,FORMULARZE,REZYGNACJA)", required=N
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;

:: Jedyny parametr wejściowy ma ustawioną flagę keyref=T, co oznacza, że przekazana wartość automatycznie stanie się
:: rekordem kluczowym.
_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error('Nieprawidłowy parametr wejściowy.'@);
   return()
?};

_ret:=exec('zamknij','!pba_bpb_rsbz',_keyRefs[1],~_mp.isMicro());

{? _ret.errmsg<>''
|| FUN.emsg(_ret.errmsg);
   _mp.error(_ret.errmsg)

|? _ret.status='X'
|| _mp.keep()

|| _out.RESULT:=_ret.status;
   _mp.save(,_out);
   _mp.done()
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Zamknięcie sesji badań - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_keyRefs:=_mp.getRefs();
_ref:={? var_pres('[1]',_keyRefs) || _keyRefs[1] || _mp.load(exec('kind_in','#b_port')).ZO_PROC ?};
_tab:=exec('desc_tab','phr_dane');

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROC.cntx_psh();
ZO_PROC.prefix();
{? ZO_PROC.seek(_ref)
|| _tab.ZAW_DANE:='T';
   _tab.NAZWA:=ZO_PROC.ZO_PROG().NAZWA;
   _tab.RODZAJ:=ZO_PROC.ZO_PROG().RODZAJ;
   _tab.OKRES_OD:=$ZO_PROC.OKRES_OD;
   _tab.OKRES_DO:=$ZO_PROC.OKRES_DO
|| _tab.ZAW_DANE:='N'
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();

{? _tab.ZAW_DANE='T'
|| 'W ramach programu "%1" (%2) zamknij sesję badań od %3 do %4.'@@
      [_tab.NAZWA,_tab.RODZAJ,_tab.OKRES_OD,_tab.OKRES_DO]
|| 'Zamknij sesję badań'@@
?}


\zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Główna formuła zajmująca się zamknięciem sesji.
::   WE: _a [REFERENCE] - Wskazanie sesji badań.
::       _b [NUMBER]    - Czy obsługa w procesie? [1/0]
::   WY: Tablica nazwana m.in. ze statusem operacji.
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_proces:=_b;

_ret:=obj_new('errmsg','status');
_ret.errmsg:='';
_ret.status:='';

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROC.cntx_psh();
ZO_PROC.f_clear();
ZO_PROC.prefix();
{? ZO_PROC.seek(_ref)
|| {? ZO_PROC.A<>'T'
   || _ret.status:='NIEOTWARTA'

   |? ZO_PROC.Z='T'
   || _ret.status:='ZAMKNIĘTA'

   |? _TEST:=sql(
         'select count(*) as ERR '
         'from   ZA_ZEST join '
         '       SLO_KOD using (ZA_ZEST.SLO_KOD,SLO_KOD.REFERENCE) '
         'where  ZA_ZEST.ZZ_LINK=:_a and SLO_KOD.KOD<>\':_b\'',
         ZO_PROC.ZZ_DOK,
         'Z'
      );
      _TEST.first() & _TEST.ERR>0
::    Wykryto ankiety ktore nie maja statusu zamknietego. Nie zgłaszamy błędu (errmsg), ale pozostawiamy result.
   || FUN.emsg(
         'Wykryto niezamknięte ankiety.\n'
         'Zamknięcie sesji badań nie jest możliwe.'@
      );
      _ret.status:='FORMULARZE'

   || _akcja:=exec('dialog','!pba_bpb_rsbz',_proces);
      {? _akcja='T'
      || ZO_PROC.A:='N';
         ZO_PROC.Z:='T';
         {? ZO_PROC.put()
         || _ret.status:='OK'
         || _ret.errmsg:='Zamknięcie sesji badań nie powiodła się.'
         ?}
      |? _akcja='N'
      || _ret.status:='REZYGNACJA'
      || _ret.status:='X'
      ?}
   ?}
:: Brak rekordu przekazanego parametrem.
|| _ret.errmsg:='Zamknięcie sesji badań niemożliwe.'
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();

_ret


\dialog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Formuła przygotowuje okno redagowania.
::   WE: _a [NUMBER] - Czy obsługa w procesie? [1/0]
::   WY: T / N / X
::----------------------------------------------------------------------------------------------------------------------
_proces:=_a;

_TAB:=sql(
   'select sum(ZO_OSOBA.FORM_SUM) as U, sum(ZO_OSOBA.FORM_AKC) as Z, cast( 0 as real_type) as PROC '
   'from ZO_OSOBA '
   'where ZO_OSOBA.RODZAJ=\'E\' and ZO_OSOBA.ZO_PROC=:_a',
   ZO_PROC.ref()
);
{? _TAB.first() & _TAB.U
|| _TAB.PROC:=(100*_TAB.Z/_TAB.U)$2;
   _TAB.put()
?};

{? _proces
|| _ret:=obj_new(1);
   _ret[1]:='X';
   params_set('ret',_ret);

   _info:='Zamknięcie sesji ocen'@;
   _we:=ZO_PROC.mk_edit(_info);
   ZO_PROC.win_esep(_we,'Program badań'@);
   ZO_PROC.win_efld(_we,ZO_PROG,'NAZWA',,,40,,,,,MS.comment(ZO_PROG,'NAZWA'));
   ZO_PROC.win_efld(_we,ZO_PROG,'RODZAJ',,,,,,,,'Rodzaj oceny'@,'radio-buttons',,
      'Roczny'@,"'R'",
      'Półroczny'@,"'P'",
      'Kwartalny'@,"'K'",
      'Miesięczny'@,"'M'",
      'Tygodniowy'@,"'T'",
      'Ad hoc'@,"'A'"
   );
   ZO_PROC.win_ecol(_we);
   ZO_PROC.win_esep(_we,'Sesja badań'@);
   ZO_PROC.win_efld(_we,,'R',,,5,,,,,MS.comment(ZO_PROC,'R'));
   ZO_PROC.win_efld(_we,,'M',,,5,,,,,MS.comment(ZO_PROC,'M'));
   ZO_PROC.win_efld(_we,,'OKRES_OD',,,,,,,,MS.comment(ZO_PROC,'OKRES_OD'));
   ZO_PROC.win_efld(_we,,'OKRES_DO',,,,,,,,MS.comment(ZO_PROC,'OKRES_DO'));
   ZO_PROC.win_efld(_we,,'PLAN_OD',,,,,,,,MS.comment(ZO_PROC,'PLAN_OD'));
   ZO_PROC.win_efld(_we,,'PLAN_DO',,,,,,,,MS.comment(ZO_PROC,'PLAN_DO'));

   _p1:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['Zamknij'@],
      "params_get().ret[1]:='T'; 'key:F2'");
   ZO_PROC.btn_eopt(_we,_p1,'tooltip="%1"' ['Zamknięcie wskazanej sesji ocen i zakończenie bieżącej czynności'@]);
   _p2:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['Nie zamykaj'@],
      "params_get().ret[1]:='N'; 'key:F2'");
   ZO_PROC.btn_eopt(_we,_p2,'tooltip="%1"' ['Zakończenie czynności bez zamykania wskazanej sesji badań'@]);
   _p3:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['Anuluj'@],'key:Esc');
   ZO_PROC.btn_eopt(_we,_p3,'tooltip="%1",default=1' ['Odłożenie czynności na listę zadań'@]);

   ZO_PROC.win_edit(_we);

   ZO_PROC.ZO_PROG();
   ZO_PROC.display(,,_info);

   _ret[1]

:: Mikroproces
|| {? FUN.ask(
         'Czy na pewno zamknąć sesję badań?\n(możliwość redakcji ankiet i generowania formularzy będzie niemożliwa)'@
      )
   || 'T'
   || 'N'
   ?}

?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 b5d9d7ea92a68dc5aad4af3ff52cfbecc8f48ac6a0f96b6683c0390455ba05259e954589b0ada91bed1dcebf996efbbfd606497d134e1836f3b686f3baf4f8a071f76cf1103999c2521eb206153aeb0af5e13f01a380c9140684d8ac88d4e61126766784217554048e063fdb5364596086b382fb306a1b7a9e960796d0d7e095

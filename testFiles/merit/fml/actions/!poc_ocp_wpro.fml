:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !poc_ocp_wpro.fml
:: Utworzony: 08.08.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności POC_OCP_WPRO - Wybór sesji ocen
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Wybór sesji ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::
:: Parametr wejściowy ON_ESC określa sposób działania czynności przy braku wyboru.
:: Parametr może przyjmować wartości:
::    DONE     - Czynność zostanie zakończona, a parametry wyjściowe przyjmą wartości puste [DOMYŚLNIE].
::    KEEP     - Czynność zostanie odłożona na listę zadań aby użytkownik mógł do niej wrócić. Oznacza to, że czynności
::               nie da się zakończyć bez wyboru sesji ocen.
::    CANCEL   - Jeśli czynność jest pierwszą w procesie to po zakończeniu formuły głównej instancja czynności zostanie
::               usunięta. Jeśli czynność jest kolejną w procesie to otrzyma status oczekująca i pozostanie na liście
::               zadań - czyli tak jak dla KEEP.
::    ERROR    - Czynność jest kończona a proces zatrzymywany z ustawioną flagą błędu.
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy rezygnacji, required=N, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'DONE' || _a ?})"
::
::# kind=WE, symbol=ZO_PROG, type=_ZO_PROG, name=Wskazanie programu ocen, required=T, keyref=T
:: Parametry wejściowe: OTWARTA i ZAMKNIETA umożliwiają filtrowanie prezentowanego zakresu sesji ocen.
::# kind=WE, symbol=OTWARTA, type=STRING, name=Tylko otwarte sesje (T/N), required=N, fml_val="exec('edit_boolean','#edit',_a,'Tylko otwarte sesje')"
::# kind=WE, symbol=ZAMKNIETA, type=STRING, name=Tylko zamknięte sesje (T/N), required=N, fml_val="exec('edit_boolean','#edit',_a,'Tylko zamknięte sesje')"
::
::# kind=WY, symbol=ZO_PROC, type=_ZO_PROC, name=Wskazanie sesji ocen, required=N

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;

_id:=exec('ref2uid','#table',_in.ZO_PROG);

_result:='';

{? _id=''
|| _result:=exec('error','!poc_ocp_wpro')

|? _mp.pathProc() | _mp.pathTodo()
|| _out:=_par.out;

   _ref:=exec('select','!poc_ocp_wpro',_in.ZO_PROG,_in.OTWARTA,_in.ZAMKNIETA);

   _out.ZO_PROC:=_ref;

   {? _ref<>null
   || _mp.save(,_out);
      _mp.done()

   |? _in.ON_ESC='KEEP'
   || _mp.keep()

   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()

   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')

   || _mp.save(,_out);
      _mp.done()
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg('%1'@[_result])
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Wybór sesji ocen - formuła opisu zadania.
::   WE:
::   WY: tekst do wyświetlenia na liście zadań
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_nazwa:='';
ZO_PROG.cntx_psh();
ZO_PROG.prefix();
{? ZO_PROG.seek(_in.ZO_PROG,,1) || _nazwa+=ZO_PROG.NAZWA ?};
ZO_PROG.cntx_pop();
{? +_nazwa
|| 'Wybierz sesję ocen programu %1'@@[_nazwa]
|| 'Wybierz sesję ocen programu'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Wybór sesji ocen niemożliwy.\nNie znaleziono programu ocen.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Wybór sesji ocen właściwej dla programu.
::   WE: _a [_ZO_PROG] - wskazanie programu oceny
::       _b [ANY] - wartość filtra dla kolumny ZO_PROC.A
::       _c [ANY] - wartość filtra dla kolumny ZO_PROC.Z
::   WY: wskazanie sesji ocen lub null w przypadku rezygnacji
::----------------------------------------------------------------------------------------------------------------------
_ret:=null;

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
{? ZO_PROG.seek(_a)
|| ZO_PROC.cntx_psh();
   ZO_PROC.prefix();
   ZO_PROC.f_set(
      'OKRES_OD'+{? PAR_SKID.get(236)='T' || '^' || '' ?},,
      'ZO_PROC.ZO_PROG=:_a'+
      {? type_of(_b)=type_of('') & _b<>''
      || ' and ZO_PROC.A=\'%1\''[_b]
      || ''
      ?}+
      {? type_of(_c)=type_of('') & _c<>''
      || ' and ZO_PROC.Z=\'%1\''[_c]
      || ''
      ?},
      _a
   );
   ZO_PROC.win_sel('WYB');
   ZO_PROC.hdr_sel('Sesje programu "%1"'@[ZO_PROG.NAZWA]);
   {? ZO_PROC.select()
   || _ret:=ZO_PROC.ref()
   ?};
   ZO_PROC.cntx_pop()
?};
ZO_PROG.cntx_pop();
_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:18 083b052df988be5bca6da8b419825d8a648c37f1d15b0f65ce5ee445012fda8c87d0d107aa8a630f8188c3abdb7ad25ca4cf201ace9345a4a87a25bf4b93a0383bd63eb2b66990404848a6d85e47ab74e0255313884b11143c3d1f366d36854c80da32e1371f42f83ce9ad5e07bd01d75e01882ffc2220b2af1060aebe1b6b38

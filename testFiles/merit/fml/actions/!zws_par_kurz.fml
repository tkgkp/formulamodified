:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zws_par_kurz.fml [17.00]
:: Utworzony: 2013/09/18
:: Autor: AMK
::======================================================================================================================
:: Zawartosc: Formuly czynnosci rejestracja danych urzedow skarbowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rejestracja danych urzedow skarbowych - glowna formula czynnosci
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
B.cntx_psh(); B.win_dict('SLO3');
US.cntx_psh(); US.index('URZSKNAZ'); US.prefix();
US.win_sel('WER');
US.select();
US.cntx_pop(); B.cntx_pop();
~~


\rachunki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła uzupełnia rachunki bankowe urzędu
::   WE: _a - wczytany nowy slownik rachunków urzędów skarbowych i celnych
::       [_b] - KRAJE.ref()
::----------------------------------------------------------------------------------------------------------------------
_dalej:={? US.find_tab(1,'EDEK_SYM',,'=',_a.EDEK_SYM) || 1 || 0 ?};

{? _dalej
||
   {? US.B3<>_a.CIT
   || US.B3:=_a.CIT;
      _id:=2-(10+_a.CIT);
      US.BANK:=exec('bank','!zws_par_kurz',_id,_a.B,_b);
      {? US.put()
      || __tmp_us.KOD:=US.EDEK_SYM;
         __tmp_us.NAZWA:=US.NU;
         __tmp_us.ZMIANA:='Zmiana numeru konta: '+US.B3;
         __tmp_us.add()
      ?}
   ?};

   SLU.cntx_psh(); SLO.cntx_psh(); SKID_RBK.cntx_psh(); KRAJE.cntx_psh();
   SLO.index('SL'); SLO.prefix(XINFO.SLWAL,'PLN');
   {? SLO.first() || _wal:=SLO.ref() || _wal:=null ?};
   _us:=#US.ref();
   SLO.index('SL_TR');
   SKID_RBK.index('TAB');
:: CIT (dla Izb Celnych SADN)
   {? +_a.CIT
   || _kod_pod:={? ~(US.NU*'Celna') || 'CIT' || 'SADN' ?};
      SLO.prefix(XINFO.SLTYPPL,_kod_pod,);
      {? SLO.first() || _slo:=SLO.ref() || _slo:=null ?};
      {? _slo & _us
      || SKID_RBK.prefix(null,'US','US',_us,_slo);
         {? ~SKID_RBK.first()
         || _add:=1;
            SKID_RBK.blank();
            SKID_RBK.WAL:=_wal;
            SKID_RBK.KRAJ:=_b;
            SKID_RBK.SLU:=XINFO.SLTYPPL;
            SKID_RBK.SLO:=_slo;
            SKID_RBK.TAB:='US';
            SKID_RBK.REF:=_us;
            SKID_RBK.N:=_a.CIT;
            _id:=2-(10+_a.CIT);
            SKID_RBK.BANK:=exec('bank','!zws_par_kurz',_id,_a.B,_b)
         || _add:=0;
            {? SKID_RBK.N<>_a.CIT
            || _zmiana:=1;
               SKID_RBK.N:=_a.CIT;
               _id:=2-(10+_a.CIT);
               SKID_RBK.BANK:=exec('bank','!zws_par_kurz',_id,_a.B,_b)
            || _zmiana:=0
            ?}
         ?};
         {? _add
         || {? SKID_RBK.add(1)
            || __tmp_us.KOD:=US.EDEK_SYM;
               __tmp_us.NAZWA:=US.NU;
               __tmp_us.ZMIANA:='Nowy rachunek: '+SKID_RBK.N+', dla płatności: '+SKID_RBK.SLO().TR;
               __tmp_us.add()
            ?}
         || {? _zmiana
            || {? SKID_RBK.put(1)
               || __tmp_us.KOD:=US.EDEK_SYM;
                  __tmp_us.NAZWA:=US.NU;
                  __tmp_us.ZMIANA:='Zmiana rachunku: '+SKID_RBK.N+', dla płatności: '+SKID_RBK.SLO().TR;
                  __tmp_us.add()
               ?}
            ?}
         ?}
      ?}
   ?};
:: VAT (dla Izb Celnych SADAKC)
   {? +_a.VAT
   || _kod_pod:={? ~(US.NU*'Celna') || 'VAT' || 'SADAKC' ?};
      SLO.prefix(XINFO.SLTYPPL,_kod_pod,);
      {? SLO.first() || _slo:=SLO.ref() || _slo:=null ?};
      {? _slo & _us
      || SKID_RBK.prefix(null,'US','US',_us,_slo);
         {? ~SKID_RBK.first()
         || _add:=1;
            SKID_RBK.blank();
            SKID_RBK.WAL:=_wal;
            SKID_RBK.KRAJ:=_b;
            SKID_RBK.SLU:=XINFO.SLTYPPL;
            SKID_RBK.SLO:=_slo;
            SKID_RBK.TAB:='US';
            SKID_RBK.REF:=_us;
            SKID_RBK.N:=_a.VAT;
            _id:=2-(10+_a.VAT);
            SKID_RBK.BANK:=exec('bank','!zws_par_kurz',_id,_a.B,_b)
         || _add:=0;
            {? SKID_RBK.N<>_a.VAT
            || _zmiana:=1;
               SKID_RBK.N:=_a.VAT;
               _id:=2-(10+_a.VAT);
               SKID_RBK.BANK:=exec('bank','!zws_par_kurz',_id,_a.B,_b)
            || _zmiana:=0
            ?}
         ?};
         {? _add
         || {? SKID_RBK.add(1)
            || __tmp_us.KOD:=US.EDEK_SYM;
               __tmp_us.NAZWA:=US.NU;
               __tmp_us.ZMIANA:='Nowy rachunek: '+SKID_RBK.N+', dla płatności: '+SKID_RBK.SLO().TR;
               __tmp_us.add()
            ?}
         || {? _zmiana
            || {? SKID_RBK.put(1)
               || __tmp_us.KOD:=US.EDEK_SYM;
                  __tmp_us.NAZWA:=US.NU;
                  __tmp_us.ZMIANA:='Zmiana rachunku: '+SKID_RBK.N+', dla płatności: '+SKID_RBK.SLO().TR;
                  __tmp_us.add()
               ?}
            ?}
         ?}
      ?}
   ?};
:: PIT (dla Izb Celnych TYTULIC)
   {? +_a.PIT
   || _kod_pod:={? ~(US.NU*'Celna') || 'PIT' || 'TYTULIC' ?};
      SLO.prefix(XINFO.SLTYPPL,_kod_pod,);
      {? SLO.first() || _slo:=SLO.ref() || _slo:=null ?};
      {? _slo & _us
      || SKID_RBK.prefix(null,'US','US',_us,_slo);
         {? ~SKID_RBK.first()
         || _add:=1;
            SKID_RBK.blank();
            SKID_RBK.WAL:=_wal;
            SKID_RBK.KRAJ:=_b;
            SKID_RBK.SLU:=XINFO.SLTYPPL;
            SKID_RBK.SLO:=_slo;
            SKID_RBK.TAB:='US';
            SKID_RBK.REF:=_us;
            SKID_RBK.N:=_a.PIT;
            _id:=2-(10+_a.PIT);
            SKID_RBK.BANK:=exec('bank','!zws_par_kurz',_id,_a.B,_b)
         || _add:=0;
            {? SKID_RBK.N<>_a.PIT
            || _zmiana:=1;
               SKID_RBK.N:=_a.PIT;
               _id:=2-(10+_a.PIT);
               SKID_RBK.BANK:=exec('bank','!zws_par_kurz',_id,_a.B,_b)
            || _zmiana:=0
            ?}
         ?};
         {? _add
         || {? SKID_RBK.add(1)
            || __tmp_us.KOD:=US.EDEK_SYM;
               __tmp_us.NAZWA:=US.NU;
               __tmp_us.ZMIANA:='Nowy rachunek: '+SKID_RBK.N+', dla płatności: '+SKID_RBK.SLO().TR;
               __tmp_us.add()
            ?}
         || {? _zmiana
            || {? SKID_RBK.put(1)
               || __tmp_us.KOD:=US.EDEK_SYM;
                  __tmp_us.NAZWA:=US.NU;
                  __tmp_us.ZMIANA:='Zmiana rachunku: '+SKID_RBK.N+', dla płatności: '+SKID_RBK.SLO().TR;
                  __tmp_us.add()
               ?}
            ?}
         ?}
      ?}
   ?};
   KRAJE.cntx_pop(); SKID_RBK.cntx_pop(); SLO.cntx_pop(); SLU.cntx_pop()
?}


\eksport
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [2010]
:: OPIS: Export urzedow skarbowych
::  OLD: \urzskarb/demo_ka.fml
::----------------------------------------------------------------------------------------------------------------------
_fname:='urzskarb.txt';
US.cntx_psh();
US.prefix();
{? FUN.ask('Rozpocząć eksport słownika urzędów skarbowych i celnych do pliku urzskarb.txt?'@)
|| US.export(_fname,1,';','UTF-8',1024,'EDEK_SYM',,1,,'UK',,2,,'MU',,3,,'UU',,4,,'NU',,5,,'BANK','NB',6,,'B3',,7,)
?};
US.cntx_pop()


\bank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła zwraca ref oddziału banku o wskazanym numerze kierunkowym, jeżeli oddziału nie było
::       w bazie zostanie dodany. formuła zakłada wcześniejsze ustawienie indeksu i pustego prefiksu
::   WE: _a - numer kierunkowy oddziału banku, _b - nazwa oddziału bankum, _c - kraj (KRAJE.ref())
::   WY: ref banku (B)
::----------------------------------------------------------------------------------------------------------------------
{? ~B.find_key(_a)
|| B.blank();
   B.NUMER:=_a; B.NB:=_b; B.KODISO:=_c;
   B.add()
|| {? ~B.KODISO || B.KODISO:=_b ?};
   B.put()
?};
B.ref()


\czy_plik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [2010]
:: OPIS: Sprawdza, czy plik o podanej przez argument nazwie istnieje
::  OLD: \czy_plik/demo_ka.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_fname:={? var_pres('_a')=type_of('') & +|_a || _a || '' ?})<>''
|| {? _hfile:=fopen(_fname,'r',1)
   || fclose(&_hfile); 1
   || FUN.emsg('\nOdnalezienie pliku \"%1\" nie jest możliwe.'
               '\nDziałanie procesu importu danych zostanie zakończone.\n'@[_fname]); 0
   ?}
?}


\us_edek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2008]
:: OPIS: Uzupełnia symbole US
::  OLD: \grupa15/demo_fk.fml
::----------------------------------------------------------------------------------------------------------------------
US.cntx_psh();
US.prefix();
lp:=1;
US.for_each("{? US.SYM=''
             || US.SYM:=form(lp,-3);
                US.put();
                lp+=1
             ?}");
US.cntx_pop();
VAR_DEL.delete('lp')


\us_rbk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [7.51]
:: OPIS: Wyswietla okienko selekcyjne rach. bank. urzedow skarbowych
::  OLD: \us_rbk/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh(); SLO.cntx_psh(); RS.cntx_psh();
PAR_WYDR.TABAKR:='US'; PAR_WYDR.REF:=#US.ref;
RACHBANK.KB_4R_BE:=RACHBANK.KB_4R_F3:=RACHBANK.KB_5R_BE:=RACHBANK.KB_5R_F3:=RACHBANK.KB_5R_AE:='';
RACHBANK.KB_5R_BD:='RACHBANK.KB_5R:=RB.get_rbtx(1,SKID_RBK.ref(),SKID_RBK.KRAJ().KODISO)';
RACHBANK.KB_4R_BD:='RACHBANK.KB_4R:=RB.get_rbtx(2,SKID_RBK.N,SKID_RBK.KRAJ().KODISO)';
RACHBANK.KB_4R_AE:='SKID_RBK.N:=RB.get_rbel(2,RACHBANK.KB_4R,SKID_RBK.KRAJ().KODISO,1); '+RACHBANK.KB_4R_BD
   +'; exec(\'ae_rb\',\'rachunki\')';
SKID_RBK.win_sel('WER_US');
SKID_RBK.win_dict('WER_US');
SKID_RBK.win_edit('RED');
SKID_RBK.win_patt('SZUK_US');
SKID_RBK.index('TAB');
SKID_RBK.prefix(null,PAR_WYDR.TABAKR,PAR_WYDR.TABAKR,PAR_WYDR.REF);
SKID_RBK.select();
SLU.cntx_pop(); SLO.cntx_pop(); RS.cntx_pop()


\ba_rbk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [7.51]
:: OPIS: Dolaczanie rekordow w tabeli SKID_RBK dla US
::  OLD: \ba_rbk/skid_ror.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_RBK.blank();
SKID_RBK.SLU:=XINFO.SLTYPPL; XINFO.SLTYPPL();
{? PAR_WYDR.TABAKR='US'
|| {? SKID_RBK.SLU
   ||
      {? SKID_RBK.edit("
         _ret:=RB.chk_rban(SKID_RBK.N,0,'KB_4R');
         {? ~+_ret & ~SKID_RBK.SLO || FUN.info('Nie podano typu przelewu.'@); _ret:='SLO' ?};
         {? ~+_ret
         || _vtyp:=SKID_RBK.SLO; SKID_RBK.cntx_psh();
            {? SKID_RBK.first()
            || {! |?
                  {? SKID_RBK.SLO=_vtyp
                  || FUN.info('Rachunek bankowy dla podanego typu przelewu został już zdefiniowany.'@); _ret:='SLO'
                  ?}; ~+_ret & SKID_RBK.next()
               !}
            ?}; SKID_RBK.cntx_pop()
         ?}; _ret")
      || SKID_RBK.TAB:=PAR_WYDR.TABAKR; SKID_RBK.REF:=PAR_WYDR.REF;
         {? (2+SKID_RBK.N)<>'00' & #(2+SKID_RBK.N)=0 || SKID_RBK.N:=2-SKID_RBK.N ?};
         SKID_RBK.add()
      ?}
   || FUN.info('Nie zdefiniowano słownika płatności do US.'@); 0
   ?}
|? PAR_WYDR.TABAKR='ZUS'
|| 1
?}


\fbe_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [7.53]
:: OPIS: Przed popraw dla okienka tabeli SKID_RBK dla US
::   WE: \fbe_rb/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLTYPPL
|| SKID_RBK.SLU:=XINFO.SLTYPPL; XINFO.SLTYPPL();
   {? SKID_RBK.edit("
      _ret:=RB.chk_rban(SKID_RBK.N,0,'KB_4R');
      {? ~+_ret & ~SKID_RBK.SLO || FUN.emsg('Nie podano typu przelewu.'@); _ret:='SLO' ?};
      {? ~+_ret
      || _vtyp:=SKID_RBK.SLO; _vref:=SKID_RBK.ref(); SKID_RBK.cntx_psh();
         {? SKID_RBK.first()
         || {! |?
               {? SKID_RBK.SLO=_vtyp & SKID_RBK.ref()<>_vref
               || FUN.emsg('Rachunek bankowy dla podanego typu przelewu został już zdefiniowany.'@);
                  _ret:='SLO'
               ?}; ~+_ret & SKID_RBK.next()
            !}
         ?}; SKID_RBK.cntx_pop()
      ?}; _ret")
   || SKID_RBK.TAB:=PAR_WYDR.TABAKR; SKID_RBK.REF:=PAR_WYDR.REF;
      {? (2+SKID_RBK.N)<>'00' & #(2+SKID_RBK.N)=0 || SKID_RBK.N:=2-SKID_RBK.N ?};
      SKID_RBK.put()
   ?}
?}


\slo_pop_us
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akcja 'Popraw' z poziomu okienek wertowania tabeli SLO
::  OLD: \slo_pop/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? exec('test_slo','slo_slu')=0
|| FUN.info('Poprawienie pozycji słownika niemożliwe w bieżącej firmie.'@);
   return(0)
?};
SLO.SLU();
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ,) & RS.TAB='US'
|| _sp:={? var_pres('slo_pop')>0 || _slo_pop:=slo_pop; 1 || 0 ?};
   _rs:={? var_pres('ref_slo')>0 || _ref_slo:=ref_slo; 1 || 0 ?};
   slo_pop:=1; ref_slo:=null; exec('slu_okn','slo_slu');
   _i:=RS.ref();
   {? (_r_szuk:=exec('wz_szuk','slo_slu'))
   || US.win_edit('RED');
      _g:=$(RS.TAB+'.put()');
      exec('cn_psh','slo_slu');
      {? US.edit("exec('spr_us','rachunki')")
      || exec('cn_pop','slo_slu');
         {? var_pres('ref_slo')>0 & ref_slo<>null || SLO.seek(ref_slo) ?}; _h:=1;
         do();
         _k:=SLO.KOD; _t:=SLO.TR; _msg:='';
         {? _g()
         || {? _r_szuk<>-1 & ~RS.seek(_i)
            || undo();
               _msg:='Nie znaleziono rekordu tabeli RS.'@
            || _msg:=exec('akt_slo','slo_slu',_k,_t)
            ?}
         ?};
         end();
         {? _msg<>'' || FUN.info(_msg) ?}
      || exec('cn_pop','slo_slu'); {? ref_slo<>null || SLO.seek(ref_slo) ?}
      ?}
   ?};
   {? _sp || slo_pop:=_slo_pop || VAR_DEL.delete('slo_pop') ?};
   {? _rs || ref_slo:=_ref_slo || VAR_DEL.delete('ref_slo') ?}
|| FUN.info('Akcja dostępna wyłącznie dla słowników o wzorcu Urząd skarbowy.'@)
?};
exec('tab_f_rfresh','slo_filtr',SLO);
RS.cntx_pop();
1

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:05 3bf69057c93a61a061d9ec7f7eb743c8df650664028c2d32474f90c76a43df02639716d797aa7eb84645de51f19e66d67ddf238733fd58304bda51378e698a1b0919b74a41a0f7a53168791f162e39483ef4418c5295bc98da29f01ae938465923d8f6035f2529a038782a2101ef3246e68104c6bfe300565e5344576a38d913

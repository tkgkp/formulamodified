:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zso_scn_tdko.fml
:: Utworzony: 21.04.2020
:: Autor: MP
::======================================================================================================================
:: Zawartość: Formuły czynności ZSO_SCN_TDKO - Tworzenie dokumentu OCR z kontaktu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła główna czynności ZSO_SCN_TDKO
::----------------------------------------------------------------------------------------------------------------------
::# properties=LOOP,SERVICE
:: PARAMETRY WE:
::# kind=WE, symbol=DOKUM,   type=_DOKUM, name=Wskazanie na dokument, required=N, keyref=N
::# kind=WE, symbol=CZY_ALL,   type=STRING, name=Czy proces dla wszystkich kontaktów? [T/N], required=T, keyref=N
::# kind=WE, symbol=TYP_DOK,   type=STRING, name=Typ dokumentu, required=N, keyref=N
:: PARAMETRY WY:
::# kind=WY, symbol=DOKUM,   type=_DOKUM, name=Wskazanie na dokument, required=N, keyref=N
::# kind=WY, symbol=SKAN_DOK,   type=_SKAN_DOK, name=Wskazanie na utworzony dokument, required=N, keyref=N
::# kind=WY, symbol=CZY_LOOP,   type=STRING, name=czy pętla dalej, required=N, keyref=N
_args:=params_get();
_in:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
DOKUM.cntx_psh();
DOKUMZ.cntx_psh();
{? _in.CZY_ALL='T'
|| DOKUM.index('KH2F');
   DOKUM.prefix(REF.FIRMA,1);
   DOKUM.first();
   {! |?
   DOKUMZ.index('KOCR');
   DOKUMZ.prefix(DOKUM.ref(),'T','N');
   {? DOKUMZ.first() || 0 || DOKUM.next() ?}
   !};
   _in.DOKUM:=DOKUM.ref
?};
DOKUM.seek(_in.DOKUM);
DOKUMZ.index('KOCR');
DOKUMZ.prefix(DOKUM.ref(),'T','N');
{? DOKUMZ.first()
|| _file:=fopen(DOKUMZ.DOKUM,'br',0);
   _file2:=fopen(DOKUMZ.DOKUM,'br',0,,1);
   _exist:=0;
   {? _file<>null()
   || _hash:=hash(_file2);
      SKAN_DOK.cntx_psh();
      SKAN_DOK.index('HASH');
      SKAN_DOK.prefix(REF.FIRMA);
      {? ~SKAN_DOK.find_key(_hash,)
      || SKAN_DOK.blank();
         SKAN_DOK.NAZ:=DOKUMZ.NAZWA;
         SKAN_DOK.R_DATA:=date();
         SKAN_DOK.R_CZAS:=time();
         SKAN_DOK.FIRMA:=DOKUM.FIRMA;
         SKAN_DOK.R_USER:=OPERATOR.USER().KOD;
         SKAN_DOK.STATUS:='R';
         SKAN_DOK.HASH:=_hash;
         {? var_pres('TYP_DOK',_in)>0 || SKAN_DOK.TYP:=_in.TYP_DOK ?};
         SKAN_DOK.POW_DOK:=DOKUMZ.uidref();
         SKAN_DOK.add();
         SKAN_DOK.bl_put('PLIK',_file,,,DOKUMZ.NAZWA)
      || _exist:=1
      ?};
      fclose(_file);
      fclose(_file2);
      _uidref:=DOKUMZ.uidref();
      _skan_ref:=SKAN_DOK.ref();
      _wy.SKAN_DOK:=SKAN_DOK.ref();
      SKAN_DOK.cntx_pop();
      DOKUMZ.prefix();
      {? DOKUMZ.seek(_uidref)
      || {? _exist=0 || DOKUMZ.W_OCR:='T' || DOKUMZ.W_OCR:='P' ?};
         DOKUMZ.put()
      ?};
      &_file2; &_file
   ?}
?};
DOKUMZ.index('KOCR');
DOKUMZ.prefix(DOKUM.ref(),'T','N');
{? _in.CZY_ALL='T'
|| {! |?
   DOKUMZ.prefix(DOKUM.ref(),'T','N');
   {? DOKUMZ.first() || 0 || DOKUM.next() ?}
   !};
   {? DOKUMZ.first() || _wy.CZY_LOOP:='T'; _wy.DOKUM:=DOKUM.ref() || _wy.CZY_LOOP:='N' ?}
|| {? DOKUMZ.first() || _wy.CZY_LOOP:='T'; _wy.DOKUM:=DOKUM.ref() || _wy.CZY_LOOP:='N' ?}
?};
DOKUMZ.cntx_pop();
DOKUM.cntx_pop();
_mp.save(,_wy);
_mp.done();
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła na opis TO DO
::----------------------------------------------------------------------------------------------------------------------
_desc:='Dodawanie dokumentów z kontaktów do OCR dla '@@;
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
DOKUM.cntx_psh();
{? var_pres('DOKUM',_in)>0 & _in.DOKUM
|| _ref:=_in.DOKUM
|| DOKUM.index('KH2F');  DOKUM.prefix(REF.FIRMA,1); _in.DOKUM:=DOKUM.first(); _ref:=DOKUM.ref()
?};
DOKUM.prefix();
KH.cntx_psh();
KH.prefix();
{? DOKUM.seek(_ref)
|| {? KH.seek(DOKUM.KH)
   || _desc+='kontrahenta: '@@ + KH.NAZ_P
   ?};
   _desc+=' dla kontaktu o temacie: '@@ + DOKUM.KR_OP
?};
DOKUM.cntx_pop();
KH.cntx_pop();
_desc


\wys_ocr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: akcja wyślij do OCR w kontaktach
::----------------------------------------------------------------------------------------------------------------------
DOKUMZ.cntx_psh();
DOKUMZ.index('KOCR');
DOKUMZ.prefix(DOKUM.ref(),'T','N');
{? ~DOKUMZ.first()
|| FUN.info('Nie znaleziono żadnego dokumentu do OCR.'@);
   DOKUMZ.cntx_pop();
   return()
?};
DOKUMZ.cntx_pop();

_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZSO_SCN_TDKO';
_params.AKCJA:='Wyślij do OCR';
_params.UIDREF:=DOKUM.uidref();
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOKUM',DOKUM.ref());
exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:02 7807b73dba72e349d032cbdfe07104775920a6173a4152e0107cf03d6cf7d73196ae985a601f167feb82810f2a1b42c2de412b50139bc519f3dd5bf5eaed4ef83ec095c8c461268b1cfff9945171186ac8964ee0893cba844d4b4d92d912878db0b3b02a56fd4d252a7720821a38864d3adc550fa794866c88cc764babe11612

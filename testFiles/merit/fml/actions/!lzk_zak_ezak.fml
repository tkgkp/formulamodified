:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zak_ezak.fml
:: Utworzony: 03.06.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Akceptacja dokumentu zakupu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LZK
::# properties=SERVICE
::# parses=exec('parses','!lzk_zak_ezak')
::# kind=WE,   symbol=FAKS,      type=_FAKS,    name=Dokument zakupu,   required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

exec('init_zak','lzk');

_akcja:=_mp.akcja();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & (_mp.isAutoRun() | _mp.isService()));

_group:=_mp.isGroup();

{? ~(var_pres('FAKS',_in)=type_of(null()) & _in.FAKS)
|| _mp.error('Brak wymaganego parametru FAKS.')
|| FAKS.cntx_psh();
   FAKS.prefix();
   {? ~FAKS.seek(_in.FAKS)
   || _mp.error('Nie znaleziono dokumentu.')
   || {? _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref())
      || exec('polafaks_get','jpk_log',FAKS.ref(),FAKS);
         params_set('mp',_mp);
         {? _akcja='Akceptuj' | _auto | _group
         || exec('akceptuj','!lzk_zak_ezak',_auto,_group)
         |? _mp.pathTodo()
            | _mp.pathArea() & _akcja=''
         || _win_red:=exec('faks_win_edit','faktury_nag');
            _ff:="params_exec('faks_pozycje_todo','!lzk_zak_ezak')";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
            _ff:="params_exec('faks_akceptuj_todo','!lzk_zak_ezak'); 'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
            _ff:="'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
            FAKS.win_edit(_win_red);
            exec('set_efld_opt','faktury_nag',_win_red);
::          Dalsza obsługa akceptacji w exec('akceptuj','!lzk_zak_ezak')
            FAKS.display();
::          Usunięcie definicji tymczasowych okien
            FAKS.win_edit(''); FAKS.win_edel(_win_red)
         || _mp.error('Nieobsługiwana ścieżka.')
         ?};
         exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
      || exec('who_rlock_faks','faktury_nag')

      ?}
   ?};
   FAKS.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| {? exec('FindAndGet','#table',FAKS,_in.FAKS,,"FAKS.T().KOR='T'",0)
   || 'Zaakceptuj korektę zakupu: %1'@@[exec('record','#to_string',_in.FAKS)]
   || 'Zaakceptuj dokument zakupu: %1'@@[exec('record','#to_string',_in.FAKS)]
   ?}
|| ''
?}


\faks_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zakupu - Akceptuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('faks_akceptuj','faktury_nag')


\faks_akceptuj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zakupu - Akceptuj z todo
::   WE: params_get()   - ustawiane w exec('main','!lzk_zak_ezak')
::----------------------------------------------------------------------------------------------------------------------
params_exec('akceptuj','!lzk_zak_ezak')


\faks_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zakupu - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('pozycje_fak','faktury_poz',0);
''


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument zakupu - Akceptacja dokumentu
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       [_b] - 0/1 - operacja grupowa
::   WE: params_get()   - ustawiane w exec('main','!lzk_zak_ezak')
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_group:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_mp:=params_get().mp;

_result:=0;

{? ~exec('czy_z_ok','okresy',-3)
||
   1

|? FAKS.STAT_REJ='A'
||
   _txt:='Dokument jest anulowany.\nAkceptacja niemożliwa.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?} ?};
   _mp.done()

|? FAKS.STAT_REJ='T'
||
   _txt:='Dokument jest już zaakceptowany.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?} ?};
   _mp.done()

|? FAKS.STAT_REJ='N'
||
   _txt:='Nie zakończono rejestracji dokumentu.\nAkceptacja niemożliwa.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',FAKS.SYM,5,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?} ?};
   {? ~_group || _mp.error(gsub(_txt,'\n',' ')) ?}

|? (_chk_rozl_zal:=exec('chk_rozl_zal','faktury_zalicz'); _chk_rozl_zal.RESULT=0)
||
   _txt:=_chk_rozl_zal.INFO;
   {? ~_auto || {? _group || exec('add_kom','#message',FAKS.SYM,5,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?} ?}

|? FAKS.OBI_POW<>'' & exec('edokum_status','obiegi_log',FAKS.OBI_POW)=''
||
   _txt:='Nie zakończono obiegu dokumentu.\nAkceptacja niemożliwa.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',FAKS.SYM,5,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?} ?}

|? FAKS.OBI_POW<>'' & exec('edokum_status','obiegi_log',FAKS.OBI_POW)='N'
||
   _txt:='Obieg dokumentu zakończył się odrzuceniem.\nAkceptacja niemożliwa.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',FAKS.SYM,6,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?} ?};
   _mp.error(gsub(_txt,'\n',' '))

|? FAKS.T().VALACC<>null() & ~exec('validate','wzorce',FAKS.T().VALACC,FAKS,FAKS.ref())
||
   ~~

|? _auto | _group | FUN.ask('Zaakceptować dokument %1?'@[FAKS.SYM])
||
   FAKS.STAT_REJ:='T';
   {? FAKS.put()
   ||
      _txt:='Zaakceptowano dokument.'@;
      {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) ?};
      _mp.done()
   ?}
?};
~~


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;


{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| FAKS.cntx_psh();
   FAKS.use(ref_name(_in.FAKS));
   FAKS.prefix();
   {? FAKS.seek(_in.FAKS)
   || __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LZK';
      _args.AR:=FAKS.AR;
      _args.AM:=FAKS.AM;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FAKS.cntx_pop()
?};

1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 379c24efe6a8869a8b5c0d94016f76978ed1cb64e2b2faa4ac4ca8bf85db3c4c7e654df1a27ebf65f94479a8f17cd0a481c5072b61640ace02904f353900b5d7a60efb9211aba828fcc3c423643060a06d0db15c666daeacf398908dee2256ff64f6d52373c4bc5d04ab65aca626e96d56bb7cca808bc522a88b4aedd6648805

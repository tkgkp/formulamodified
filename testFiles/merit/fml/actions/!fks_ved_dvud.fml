:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ved_dv7d.fml
:: Utworzony: 07.08.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VED_DVUD - Tworzenie deklaracji VAT-UE
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie deklaracji VAT-UE - główna formuła czynności FKS_VED_DVUD.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# kind=WE, symbol=OKRES, type=_OKRO_F, name=Okres deklaracji, required=N, keyref=N
:: PARAMETRY WY:
::# kind=WY, symbol=DEKLARACJA, type=_VAT_DEK, name=Deklaracja VAT-UE, required=T
::# kind=WY, symbol=OKRES, type=_OKRO_F, name=Okres deklaracji, required=T

_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
vat_typ:='POD';
BPMN.SYM_DOM:='FKS';
{? var_press('OKRES',_wy)>0
|| OKRO_F.prefix();
   OKRO_F.seek(_wy.OKRES)
|? var_press('OKRES',_we)>0
|| OKRO_F.prefix();
   OKRO_F.seek(_we.OKRES)
|| SSTALE.AO()
?};
_ar:=SSTALE.AR;
_ao:=SSTALE.AO;
SIK.OKRROK2:=OKRO_F.ref();
SIK.ROK2:=OKRO_F.ROK;
_jest:=0;
Add:=0;
ref:=null;
VAT_DEK.cntx_psh();
_refy:=_mp.getRefs();
{? var_press('[1]',_refy)>0
|| VAT_DEK.index('UNIK');
   VAT_DEK.prefix();
   {? VAT_DEK.seek(_refy[1])
   || _jest:=1;
      Add:=1;
      ref:=VAT_DEK.ref();
      exec('set_var','fks_ved')
   || _jest:=-1
   ?}
|| exec('init','fks_ved',OKRO_F.ROK,OKRO_F.ref());
   rodzdekl:='P'
?};
_spr:="exec('vat_rec','!fks_ved_dvud')";
{? _jest=-1 | _jest=0 & _mp.pathTodo()
|| FUN.info('Nie znaleziono deklarcji VAT-UE.'@);
   _wy.DEKLARACJA:=null;
   _wy.OKRES:=null;
   _mp.save(,_wy);
   _mp.done()
|? _jest=0 & (_mp.pathProc() | _akcja='Dołącz')
|| {? exec('okres','!fks_ved_dvud') & exec('set_vat_dek','!fks_ved_dvud')
   || _add:="exec('add','!fks_ved_dvud')";
      exec('set_win','fks_ved',_spr,_add);
      _mp.trigRef('VAT_DEK');
      {? VAT_DEK.edit(_spr)
      || {? Add=0
         || ref:=_add();
            {? ref
            || Add:=1;
               VAT_POZ.index('VAT_POZ');
               VAT_POZ.prefix(ref);
               exec('vat_dpoz','fks_ved',vat_typ);
               VAT_DEK.edit(_spr)
            ?}
         ?}
      ?}
   ?}
|? VAT_DEK.STATUS<>'N' & VAT_DEK.STATUS<>''
|| FUN.info('Zakończono już rejestrację deklaracji.'@)
|? exec('bevatdek','fks_ved')
|| {? _akcja='Zakończ'
   || VAT_DEK.STATUS:='R';
      VAT_DEK.put()
   || exec('ust_okn','fks_ved');
      _add:="";
      exec('set_win','fks_ved',_spr,_add);
      {? VAT_DEK.edit(_spr)
      || VAT_DEK.put();
         exec('vat_uzas','fks_ved')
      ?}
   ?};
   VAT_DEK.r_unlock()

?};
VAT_DEK.cntx_pop();
{? ref<>null
|| VAT_DEK.cntx_psh();
   VAT_DEK.prefix();
   {? VAT_DEK.seek(ref)
   || _wy.DEKLARACJA:=VAT_DEK.ref();
      _wy.OKRES:=SSTALE.AO;
      _mp.save(,_wy);
      {? VAT_DEK.STATUS<>'N'
      || _mp.done()
      ?}
   ?};
   VAT_DEK.cntx_pop();
   VAT_DEK.seek(ref)
?};
{? _ar<>SIK.ROK2 | _ao<>SIK.OKRROK2
|| SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   exec('open','fks_ved',SSTALE.AR().KOD,SSTALE.AO().NR)
?};
VAR_DEL.delete('vat_ver','zaok','niedruk','pv','ref')


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis czynności na ToDo
::----------------------------------------------------------------------------------------------------------------------
''@@;
params_exec('desc_add','fks_ved','VAT-UE')


\okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Umożliwia wybór okresu deklaracji
::----------------------------------------------------------------------------------------------------------------------
OkroFInd:=1;
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
ROK_F.win_dict('WER');
OKRO_F.win_dict('SLO');
SIK.win_edit('ROKOKRES');
_ok:=SIK.edit("
   _r:=__CHK.record2(SIK,'ROK2','Rok bilansowy','OKRROK2','Okres');
   {? _r=''
   || {? OKRO_F.POCZ=date(0,0,0)
      || FUN.info('W okresie %1 nie można tworzyć deklaracji.'@[OKRO_F.NAZ]); 0
      |? ~exec('czy_ue','fks_vat',1)
      || FUN.info('Wybierz okres od maja 2004.'@); 0
      |? exec('zwr_rozl_okr','fks_ved')='K' & OKRO_F.POCZ<date(2009,1,1)
      || FUN.info('Deklaracja VAT-7D\nobowiązuje od 1 stycznia 2009.'@); 0
      || 1
      ?}
   || _r
   ?}
");
VAR_DEL.delete('OkroFInd');
{? _ok
|| {? SSTALE.AR<>SIK.ROK2 | SSTALE.AO<>SIK.OKRROK2
   || SSTALE.AR:=SIK.ROK2;
      SSTALE.AO:=SIK.OKRROK2;
      exec('open','fks_ved',SSTALE.AR().KOD,SSTALE.AO().NR)
   ?}
?};
_ok


\set_vat_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed dodaniem deklaracji - ustawienie danych
::----------------------------------------------------------------------------------------------------------------------
vat_ver:=exec('vat_ver','!fks_ved_dvud',SIK.OKRROK2().KON);
VAT_DEK.blank(1);
zaok:=1;
niedruk:=1;
_dekl:=OKRO_F.ROK;
_dekk:=OKRO_F.ROK().KOD;
_dekp:=ROK_F.POCZ_ROK;
_rodz:=rodzdekl;
_wer:=1;
_dalej:=1;
_ref:=null;
{? exec('czy_ks','fks_vat')
|| {? vat_ver>1 & PAR_SKID.get(41)='N' | vat_ver>3
   || SSTALE.AO();
      _okres:=$(OKRO_F.POCZ~1)+'/'+form(OKRO_F.POCZ~2,-2)
   || _v:=#((7+$SSTALE.AO().KON)+2);
      _okres:=(4+$SSTALE.AO().KON)+'/'
              +{? _v<=3 || '1' |? _v<=6 || '2' |? _v<=9 || '3' || '4' ?};
      _rok:=#(4+_okres);
      _mies:=#(5-_okres)*3;
      OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA,date(_rok,_mies,0));
      _dekl:={? OKRO_F.first
                || OKRO_F.ROK
                || FUN.info('Należy dołączyć nowy rok bilansowy\n z powodu braku końca kwartału w danych.'@);
                   _dalej:=0
                ?};
      _dekk:=OKRO_F.ROK().KOD;
      _dekp:=ROK_F.POCZ_ROK
   ?};
   {? _dalej
   || _is_vat:=0;
      VAT_DEK.index('VAT_DEK');
      {? vat_ver>1 & PAR_SKID.get(41)='N' | vat_ver>3
      || VAT_DEK.prefix(_dekp,vat_typ,_okres);
         _okres1:=$(SSTALE.AO().POCZ~1)+'/'+form(OKRO_F.POCZ~2,-2);
         _is_vat:=VAT_DEK.last()
      || _okres1:=$(SSTALE.AO().POCZ~1)+'/'+form(OKRO_F.POCZ~2,-2);
         VAT_DEK.prefix(_dekp,vat_typ,_okres1,);
         {? VAT_DEK.last()
         || _is_vat:=1
         || _okres1:=''
         ?};
         _v:=#((7+$OKRO_F.KON)+2);
         _okres2:=(4+$OKRO_F.KON)+'/'+{? _v<=3 || '1' |? _v<=6 || '2' |? _v<=9 || '3' || '4' ?};
         VAT_DEK.prefix(_dekp,vat_typ,_okres2,);
         {? VAT_DEK.last()
         || _is_vat+=1
         || _okres2:=''
         ?}
      ?};

      {? _is_vat
      || {? VAT_DEK.TYP='PODK'
         || _ok:=FUN.choice( 'Istnieje już deklaracja na okres %1.'@[_okres],1,'utworzyć Korektę'@);
            {? _ok || _ok+=2 ?}
         || _ok:=FUN.choice( 'Istnieje już deklaracja na okres %1'@[_okres],2,'utworzyć Korektę'@);
            {? _ok || _ok+=2 ?}
         ?};
         _wer:=VAT_DEK.WER;
         {? _ok=3 | _ok=2
         || {? _ok=3
            || {? _is_vat>1
               || _pyt:=FUN.choice('Jaką korektę utworzyć?'@,,'Miesięczną'@,'Kwartalną'@);
                  {? _pyt=0
                  || _ok:=0
                  |? _pyt=1
                  || _okres:=_okres1
                  || _okres:=_okres2
                  ?}
               |? vat_ver>1
               || _okres:={? _okres1<>'' || _okres1 || _okres2 ?}
               ?};
               _typ:='PODK'
            |? _ok=2
            || _typ:=vat_typ
            ?};
            VAT_DEK.cntx_psh();
            VAT_DEK.prefix(_dekp,_typ,_okres);
            _wer:={? VAT_DEK.last() || VAT_DEK.WER || 0 ?};
            VAT_DEK.cntx_pop()
         ?};
         {? _ok=1
         || {? vat_typ<>VAT_DEK.TYP
            || _wer+=1
            ?};
            _ok:=exec('vat_usu','fks_ved',1)
         || _wer+=1
         ?}
      || _ok:=2
      ?};
      {? _ok>0
      || VAT_DEK.prefix(_dekp);
         VAT_DEK.ROK:=_dekl;
         VAT_DEK.RODZAJ:=_rodz;
         {? _ok=3
         || VAT_DEK.TYP:=SKID.DEKL_NAZ:='PODK'
         || VAT_DEK.TYP:=SKID.DEKL_NAZ:=vat_typ
         ?};
         VAT_DEK.OKRES:=_okres;
         {? 1+(_okres+2)='/'
         || _nr_okr:=#(_okres+1);
            _mc:=(#(_okres+1)-1)*3+1;
            _d:=date(#(4+_okres),_mc,0)
         || _nr_okr:=#(_okres+2);
            _d:=date(#(4+_okres),#(_okres+2),0)
         ?};
         VAT_VER.index('VER_OD'); VAT_VER.prefix(BPMN.SYM_DOM,SKID.DEKL_NAZ,SKID.DEKL_NAZ);
         VAT_DEK.NR:={? SKID.DEKL_NAZ<>'' & VAT_VER.find_le(_d) || VAT_VER.ref() || null ?};
         VAT_DEK.WER:=_wer;
         VAT_DEK.win_edit('POD');
         VAT_DEK.DATA:=date();
         {? _ok=3
         || VAT_DEK.OPIS:='Korekta informacji podsumowującej VAT-UE'
         || VAT_DEK.OPIS:='Informacja podsumowująca VAT-UE'
         ?};
         1
      ?}
   ?}
?}


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje deklarację i jej pozycje
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
VAT_DEK.prefix();
VAT_DEK.STATUS:='N';
{? VAT_DEK.add()
|| _ref:=VAT_DEK.ref();
   exec('vat_uzas','fks_ved');
   _ok:=exec('utw_pod','!fks_ved_dvud');
   VAT_POZ.index('VAT_POZ');
   VAT_POZ.prefix(_ref);
   {? VAT_DEK.TYP='PODK' & ~VAT_POZ.first() | ~_ok
   || {? niedruk=1
      || FUN.info('Nie stwierdzono potrzeby korekty.'@)
      || FUN.info('Liczba pozycji korekty przekroczyła założoną wartość.\n Korekta nie będzie generowana.'@)
      ?};
      {? exec('vat_usu','fks_ved',1)
      || _ref:=null
      ?}
   ?}
?};
_ref


\vat_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL  [8.40]
:: OPIS: Formula Dolacz dla okienka wert. tabeli VAT_DEK
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_VED_DVUD';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRES',SSTALE.AO);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\vat_ver
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Ustala numer wersji deklaracji przy tworzeniu
::----------------------------------------------------------------------------------------------------------------------
{? _a<date(2010,1,1)
|| 1
|? _a<date(2013,7,1)
|| 2
|? _a<date(2017,1,11)
|| 3
|| 4
?}


\vat_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formula po redakcji okienka VAT_DEK
::  OLD: \vat_rec/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~VAT_DEK.NR
|| FUN.info('Nie wypełnione pole z numerem wersji deklaracji.'@); 'NR'
|| ''
?}


\bm_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMA [8.40]
:: OPIS: Formula przed redakcja naglowka deklaracji
::----------------------------------------------------------------------------------------------------------------------
{? exec('is_edek','xml',VAT_DEK.TYP)
|| FUN.info('Istnieją e-Deklaracje.\nPoprawianie zabronione.'@);
   return(0)
|? exec('bevatdek','fks_ved')
|| exec('ust_okn','fks_ved');
   {? VAT_DEK.edit("exec('vat_rec','!fks_ved_dvud')")
   || VAT_DEK.put();
      exec('vat_uzas','fks_ved')
   ?};
   VAT_DEK.r_unlock()
?}


\pr_vat_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formula Popraw przed dla okienek wert. tabeli VAT_POZ
::  OLD: \pr_vat_p/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('is_edek','xml',{? VAT_DEK.TYP+4='_KOR' || VAT_DEK.TYP-4 || VAT_DEK.TYP ?})
|| FUN.info('Istnieją e-Deklaracje.\nModyfikowanie zabronione.'@); 0
|? VAT_POZ.TYP='S'
|| FUN.info('Wiersz będący podsumowaniem.\nModyfikacje zabronione.'@); 0
|? VAT_POZ.TYP='A'
|| FUN.ask('Wiersz wyliczany automatycznie na podstawie\nrejestrów VAT.\n'
           'Zmiany wartości spowodują niezgodności\n z wydrukami rejestrów VAT\n'
           '\nKontynuować?'@)
|| 1
?}


\be_vatkw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2008]
:: OPIS: Przed redakcja pol VAT,NETTO i TEXT tabeli VAT_POZ
::  OLD: \be_vatkw/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='VAT'   || 'TD'*VAT_POZ.TYP=0 & VAT_POZ.POZ2<>0
|? cur_afld()='NETTO' || 'TD'*VAT_POZ.TYP=0 & VAT_POZ.POZ1<>0
|? cur_afld()='P4'    || VAT_POZ.TYP='D' & VAT_POZ.POZ1<>0
|| VAT_POZ.TYP='T'
?}


\utw_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [?????]
:: OPIS: Tworzenie pozycji deklaracji VAT_UE
::  OLD: \utw_pod/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_wer:=VAT_DEK.NR().NR;
{? _wer=1 | _wer>1 & _wer<4 & PAR_SKID.get(41)='T'
|| _v:=#((7+$SSTALE.AO().KON)+2);
   _okres:={? _v<=3 || '1' |? _v<=6 || '4' |? _v<=9 || '7' || '10' ?};
   _rok:=SSTALE.AR().NAZ; _vr:=#(4+$SSTALE.AO().POCZ);
   _okres1:=date(_vr,#_okres,1);
   _vo:=#_okres+2; _okres2:=date(_vr,_vo,0)
|| _okres1:=SSTALE.AO().POCZ;
   _okres2:=SSTALE.AO().KON
?};
_ssqlj:='join @DVAT using (PVAT.DVAT, DVAT.REFERENCE) '
   +'join OKRO_F as D_OKR using (DVAT.OBR, D_OKR.REFERENCE) '
   +'join ROK_F as D_ROK using (D_OKR.ROK, D_ROK.REFERENCE) '
   +'join RVAT using (DVAT.RVAT, RVAT.REFERENCE) '
   +'join KVAT using (RVAT.KVAT, KVAT.REFERENCE) '
   +'join SLO as SLO_KR using (DVAT.KRAJ, SLO_KR.REFERENCE) '
   +'join SLO as GRVAT using (PVAT.GRVAT, GRVAT.REFERENCE) '
   +'join SLO as STVAT using (PVAT.STVAT, STVAT.REFERENCE) ';
_ssqlw:=' AND D_OKR.POCZ>=to_date(\''+$_okres1+'\') AND D_OKR.KON<=to_date(\''
   +$_okres2+'\')'+' AND SLO_KR.KOD=''PL'' AND DVAT.USUNIETY=0 ';
_mdvat:=_mpvat:='';
ROK_F.cntx_psh();
ROK_F.index('KOD'); ROK_F.prefix(REF.S_FIRMA);
{? ROK_F.first()
|| {!
   |? _mdvat:=_mdvat+',\'dvat__'+ROK_F.KOD+'\'';
      _mpvat:=_mpvat+',\'pvat__'+ROK_F.KOD+'\'';
      ROK_F.next()
   !}
?};
ROK_F.cntx_pop();
_sqlf:='/*+MASK_FILTER(PVAT'+_mpvat+') MASK_FILTER(DVAT'+_mdvat+')*/ ';
_ssqlp:='select '+_sqlf
   +'\'U\' as KLVAT, \'D\' as KLVAT2, DVAT.NIP, SUM2 as NETTO '
   +'from @PVAT :_a'
   +'where KVAT.SYM=\'_SprzNOp\' and GRVAT.KOD=\'SprPKWSU\' and STVAT.KOD!=\' -zw0\' :_b'
   +' and (PVAT.TYP_VAT=\'\' or PVAT.TYP_VAT=\'Z\')'
   +'union all '
   +'select '+_sqlf
   +'substr(KVAT.SYM,6,1) as KLVAT, '
   +'\'D\' as KLVAT2, DVAT.NIP, SUM2 as NETTO '
   +'from @PVAT :_a'
   +'where KVAT.SYM like \'_WWsp%\''
   +' AND KVAT.SYM not in (\'_WWspDot\',\'_WWspNat\',\'_WWspNus\') :_b'
   +' and (PVAT.TYP_VAT=\'\' or PVAT.TYP_VAT=\'Z\')'
   +'union all '
   +'select '+_sqlf
   +'substr(KVAT.SYM,6,1) as KLVAT, '
   +'\'T\' as KLVAT2, DVAT.NIP, SUM2 as NETTO '
   +'from @PVAT :_a'
   +'where  KVAT.SYM in (\'_WWspDot\',\'_WWspNat\') :_b'
   +' and (PVAT.TYP_VAT=\'\' or PVAT.TYP_VAT=\'Z\')';
_ssql:='select '
   +'KLVAT, KLVAT2, NIP, sum(NETTO) as NETTO, cast(0 as REAL_TYPE) as NETTOK '
   +'from :_a '
   +'group by KLVAT, KLVAT2, NIP order by KLVAT, KLVAT2, NIP, NIP';
{? VAT_DEK.TYP='POD'
|| _tab:=sql(_ssqlp,_ssqlj,_ssqlw);
   exec('upd_tab_pod','!fks_ved_dvud',_tab);
   TT_VAT:=sql(_ssql,_tab);
   VAT_POZ.prefix();
   _i:=_j:=_k:=1;
   {? _wer<4
   || _na_str:={? _wer=3 || 59 || 53 ?};
      _na_str_c:={? _wer=3 || 54 || 53 ?};
      {? _wer=1
      || _max_c:=12; _max_d:=9
      |? _wer=2
      || _max_c:=6; _max_d:=6; _max_e:=5
      || _max_c:=12; _max_d:=12; _max_e:=12
      ?}
   ?};
   {? TT_VAT.first()
   || {! |?
         {? TT_VAT.NETTO$0<>0
         || VAT_POZ.blank();
            VAT_POZ.VAT_DEK:=VAT_DEK.ref();
            {? _wer>=4
            || {? TT_VAT.KLVAT='U'
               || VAT_POZ.LP:='VAT-UE E. '+(2-+($_k))*' '+$_k; _k+=1
               |? TT_VAT.KLVAT='D'
               || VAT_POZ.LP:='VAT-UE C. '+(2-+($_i))*' '+$_i; _i+=1
               || VAT_POZ.LP:='VAT-UE D. '+(2-+($_j))*' '+$_j; _j+=1
               ?}
            |? _wer>1 & TT_VAT.KLVAT='U'
            || {? _k<_max_e+1
               || _vi:='VAT-UE E.'; _in:=_k
               || _vi:='VAT-UE/C '+$ceil((_k-_max_e)/_na_str_c);
                  _in:=_k-_max_e-((#(_vi+1)-1)*_na_str_c)
               ?};
               VAT_POZ.LP:=_vi+' '+(2-+($_in))*' '+$_in; _k+=1
            |? TT_VAT.KLVAT='D'
            || {? _i<_max_c+1
               || _vi:='VAT-UE C.'; _in:=_i
               || _vi:='VAT-UE/A '+$ceil((_i-_max_c)/_na_str);
                  _in:=_i-_max_c-((#(_vi+1)-1)*_na_str)
               ?};
               VAT_POZ.LP:=_vi+' '+(2-+($_in))*' '+$_in; _i+=1
            || {? _j<_max_d+1
               || _vj:='VAT-UE D.'; _jn:=_j
               || _vj:='VAT-UE/B '+$ceil((_j-_max_d)/_na_str);
                  _jn:=_j-_max_d-((#(_vj+1)-1)*_na_str)
               ?};
               VAT_POZ.LP:=_vj+' '+(2-+($_jn))*' '+$_jn; _j+=1
            ?};
            VAT_POZ.OP:=TT_VAT.NIP;
            VAT_POZ.Z:={? TT_VAT.KLVAT2='T' || 'X' || '' ?};
            VAT_POZ.TYP:='A';
            VAT_POZ.NETTO:=TT_VAT.NETTO;
            VAT_POZ.POZ1:=1;
            VAT_POZ.add()
         ?};
         TT_VAT.next()
      !}
   ?};
   VAR_DEL.delete('TT_VAT');
   exec('gen_pod_f','fks_ved')
|| _tab:=sql(_ssqlp,_ssqlj,_ssqlw);
   exec('upd_tab_pod','!fks_ved_dvud',_tab);
   TT_VATK:=sql(_ssql,_tab);
   VAT_POZ.index('VAT_POZ');
   VAT_DEK.cntx_psh();
   VAT_DEK.prefix(VAT_DEK.ROK().POCZ_ROK,'POD',VAT_DEK.OKRES);
   {? VAT_DEK.first()
   || VAT_POZ.prefix(VAT_DEK.ref());
      {? VAT_POZ.first()
      || {! |?
            _typ:=
            {? VAT_POZ.LP*'C.' | VAT_POZ.LP*'/A' || 'D'
            |? VAT_POZ.LP*'D.' | VAT_POZ.LP*'/B' || 'N'
            || 'U'
            ?};
            _zn:={? VAT_POZ.Z='X' || 'T' || 'D' ?};
            _nip:=STR.gsub(VAT_POZ.OP,'-','');
            TT_VATK.prefix(_typ,_zn,_nip,_nip);
            {? TT_VATK.first()
            || TT_VATK.NETTOK:=VAT_POZ.NETTO; TT_VATK.put()
            || TT_VATK.prefix(); TT_VATK.blank();
               TT_VATK.KLVAT:=_typ;
               TT_VATK.KLVAT2:=_zn;
               TT_VATK.NIP:=_nip; TT_VATK.NETTOK:=VAT_POZ.NETTO;
               TT_VATK.add()
            ?};
            VAT_POZ.next()
         !}
      ?}
   ?};
   VAT_DEK.prefix(VAT_DEK.ROK().POCZ_ROK,'PODK',VAT_DEK.OKRES);
   {? VAT_DEK.first()
   || {!
      |? VAT_POZ.prefix(VAT_DEK.ref());
         {? VAT_POZ.first()
         || {!
            |? {? VAT_POZ.LP+1='J'
               || {? VAT_POZ.NETTO$0=0
                  || VAT_POZ.cntx_psh();
                     VAT_POZ.prev();
                     _typ:=
                     {? VAT_POZ.LP*'C.' | VAT_POZ.LP*'/A' || 'D'
                     |? VAT_POZ.LP*'D.' | VAT_POZ.LP*'/B' || 'N'
                     || 'U'
                     ?};
                     _zn:={? VAT_POZ.Z='X' || 'T' || 'D' ?};
                     _nip:=STR.gsub(VAT_POZ.OP,'-','');
                     TT_VATK.prefix(_typ,_zn,_nip,_nip);
                     {? TT_VATK.first()
                     || TT_VATK.NETTOK:=0; TT_VATK.put()
                     ?};
                     VAT_POZ.cntx_pop()
                  || _typ:=
                     {? VAT_POZ.LP*'C.' | VAT_POZ.LP*'/A' || 'D'
                     |? VAT_POZ.LP*'D.' | VAT_POZ.LP*'/B' || 'N'
                     || 'U'
                     ?};
                     _zn:={? VAT_POZ.Z='X' || 'T' || 'D' ?};
                     _nip:=STR.gsub(VAT_POZ.OP,'-','');
                     TT_VATK.prefix(_typ,_zn,_nip,_nip);
                     {? TT_VATK.first()
                     || TT_VATK.NETTOK:=VAT_POZ.NETTO; TT_VATK.put()
                     || TT_VATK.prefix(); TT_VATK.blank();
                        TT_VATK.KLVAT:=_typ;
                        TT_VATK.KLVAT2:=_zn;
                        TT_VATK.NIP:=_nip; TT_VATK.NETTOK:=VAT_POZ.NETTO;
                        TT_VATK.add()
                     ?}
                  ?}
               ?};
               VAT_POZ.next()
            !}
         ?};
         VAT_DEK.next()
      !}
   ?};
   VAT_DEK.cntx_pop();
   _tt_pom:=sql('select max(:_a.KLVAT2), count(:_a.KLVAT2) as ILE from :_a group by :_a.KLVAT2 ',TT_VATK);
   {? _tt_pom.first || {! |? {? _tt_pom.ILE<=594 || _tt_pom.next || niedruk:=0 ?} !} ?};
   {? niedruk
   || {? TT_VATK.prefix(); TT_VATK.first()
      || VAT_POZ.prefix();
         {? _wer<4
         || _i:=_j:=_k:=_in:=_jn:=_kn:=1;
            {? _wer=1
            || _max_c:=6; _max_d:=6
            |? _wer=2
            || _max_c:=4; _max_d:=4; _max_e:=4
            || _max_c:=6; _max_d:=6; _max_e:=6
            ?}
         || _i:=_j:=_k:=0
         ?};
         {! |?
            {? (TT_VATK.NETTO$0<>0 | TT_VATK.NETTOK$0<>0) & TT_VATK.NETTO$0<>TT_VATK.NETTOK$0
            || VAT_POZ.blank();
               VAT_POZ.VAT_DEK:=VAT_DEK.ref();
               {? _wer>=4
               || {? TT_VATK.KLVAT='D'
                  || _lp:='VAT-UEK C. '+form((_i+=1),-3,0,'9.')+' B'
                  |? TT_VATK.KLVAT='N'
                  || _lp:='VAT-UEK D. '+form((_j+=1),-3,0,'9.')+' B'
                  || _lp:='VAT-UEK E. '+form((_k+=1),-3,0,'9.')+' B'
                  ?};
                  VAT_POZ.LP:=_lp
               |? TT_VATK.KLVAT='D'
               || _vii:={? _i<_max_c+1 || 1 || ceil(_i/_max_c) ?};
                  _vi:='VAT-UEK C. '+form((_vii),-2,0,'9.');
                  _in:={? _in>_max_d || _in:=1 || _in ?};
                  VAT_POZ.LP:=_vi+' '+$_in+' B'
               |? TT_VATK.KLVAT='N'
               || _vjj:={? _j<_max_d+1 || 1 || ceil(_j/_max_d) ?};
                  _vj:='VAT-UEK D. '+form((_vjj),-2,0,'9.');
                  _jn:={? _jn>_max_d || _jn:=1 || _jn ?};
                  VAT_POZ.LP:=_vj+' '+$_jn+' B'
               || _vkk:={? _k<_max_e+1 || 1 || ceil(_k/_max_e) ?};
                  _vk:='VAT-UEK E. '+form((_vkk),-2,0,'9.');
                  _kn:={? _kn>_max_d || _kn:=1 || _kn ?};
                  VAT_POZ.LP:=_vk+' '+$_kn+' B'
               ?};
               VAT_POZ.OP:={? TT_VATK.NETTOK$0 || TT_VATK.NIP || '' ?};
               VAT_POZ.Z:={? TT_VATK.KLVAT2='T' || 'X' || '' ?};
               VAT_POZ.TYP:='A';
               VAT_POZ.NETTO:=TT_VATK.NETTOK;
               VAT_POZ.POZ1:=1;
               VAT_POZ.add();
               VAT_POZ.blank();
               VAT_POZ.VAT_DEK:=VAT_DEK.ref();
               {? _wer>=4
               || VAT_POZ.LP:=(_lp-1)+'J'
               |? TT_VATK.KLVAT='D'
               || VAT_POZ.LP:=_vi+' '+$_in+' J'; _i+=1; _in+=1
               |? TT_VATK.KLVAT='N'
               || VAT_POZ.LP:=_vj+' '+$_jn+' J'; _j+=1; _jn+=1
               || VAT_POZ.LP:=_vk+' '+$_kn+' J'; _k+=1; _kn+=1
               ?};
               VAT_POZ.OP:={? TT_VATK.NETTO$0 || TT_VATK.NIP || '' ?};
               VAT_POZ.Z:={? TT_VATK.KLVAT2='T' || 'X' || '' ?};
               VAT_POZ.TYP:='A';
               VAT_POZ.NETTO:=TT_VATK.NETTO;
               VAT_POZ.POZ1:=1;
               VAT_POZ.add()
            ?};
            TT_VATK.next()
         !}
      ?}
   ?};
   VAR_DEL.delete('TT_VATK');
   exec('gen_pod_f','fks_ved')
?};
1


\bd_vat_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Przed usunieciem VAT_POZ
::  OLD: \bd_vat_poz/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('can_edit2','fks_ved')
|| 0
|? VAT_DEK.TYP='POD' & VAT_POZ.TYP='C'
|| exec('vat_pod_del','fks_ved')
|? VAT_DEK.TYP='POD'
|| FUN.info('Nie można usunąć tej pozycji.')
|? FUN.ask('Usunąć pozycje "było" i "jest" deklaracji?'@)
|| _ref:=null;
   _kod:=9+VAT_POZ.LP;
   VAT_POZ.cntx_psh();
   VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),_kod);
   {? -(VAT_POZ.LP+1)='j' || VAT_POZ.prev() ?};
   do();
   {? VAT_POZ.del() & VAT_POZ.del()
   || _ref:=VAT_POZ.ref();
      _typ:=_kod+1;
      _l0:=0;
      _l1:=0;
      {? VAT_DEK.NR().NR<4
      || _l2:=1;
         {? VAT_DEK.NR().NR=1 | VAT_DEK.NR().NR=3
         || _max:=6
         || _max:=4
         ?};
         {? VAT_POZ.first()
         || {!
            |? _l0+=1;
               {? _l0=3 || _l0:=1 ?};
               {? _l0=1 || _l1+=1 ?};
               {? _l1>_max || _l1:=1; _l2+=1 ?};
               VAT_POZ.LP:=_kod+'. '+form((_l2),-2,0,'9.')+' '+$_l1+' '+{? _l0=1 || 'B' || 'J' ?};
               VAT_POZ.put();
               VAT_POZ.next()
            !}
         ?}
      || {? VAT_POZ.first()
         || {!
            |? _l0+=1;
               {? _l0=3 || _l0:=1 ?};
               {? _l0=1 || _l1+=1 ?};
               VAT_POZ.LP:=_kod+'. '+form((_l1),-3,0,'9.')+' '+{? _l0=1 || 'B' || 'J' ?};
               VAT_POZ.put();
               VAT_POZ.next()
            !}
         ?}
      ?}
   ?};
   end();
   VAT_POZ.cntx_pop();
   {? _ref || VAT_POZ.seek(_ref) ?}
?}


\upd_tab_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dodatkowe operacja na tabeli do obsługi deklaracji VAT-UE.
::   WE: _a - tabela do przetworzenia
::  OLD: \upd_tab_pod/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
{? _tab.first()
|| {!
   |? _tab.NIP:=STR.gsub(_tab.NIP,'-','');
      {? +_tab.NIP>2
      || _tab.put();
         _tab.next()
      || _tab.del()
      ?}
   !}
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 547ad7a8f16692acdb50461ac56952dc10f6b39af3541d24516b8a60bd2a791b3fcbcd65f53a37b4240452dfc4f5c096feb66acf2dcc9da9e0a0f5efadd50f638e85b7666ee8f189895c35cbbe0ba8e8b021b5130a6cbb25eb68452c3ff1d520652f1dfabe8d59b1e74efde151ca1026cbfe3f625e50aa9cc230ba9274dd5505

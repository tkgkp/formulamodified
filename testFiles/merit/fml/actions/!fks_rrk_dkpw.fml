:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rrk_dkpw.fml
:: Utworzony: 20.11.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RRK_DKPW - Naniesienie potwierdzeń sald
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Naniesienie potwierdzeń sald - główna formuła czynności FKS_RRK_DKPW
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,HRB
::# parses=exec('parses_ser_nags','rozrach_kor')
::# access=exec('upr_ser_nags','rozrach_kor')
::# kind=WE, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na potwierdzenie salda, required=T, keyref=T
::# kind=WY, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na potwierdzenie salda, required=T, keyref=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='P';
params_set(params_get());
{? var_pres('SER_NAG',_in) & ~var_pres('SER_NAG',_out)
|| _out.SER_NAG:=_in.SER_NAG; _mp.save(,_out)
?};
{? _mp.pathTodo() | _mp.isAutoRun()
|| {? var_press('SER_NAG',_out)>0
   || _ref:=BB.sqlint($_in.SER_NAG); _nam:=form(($_in.SER_NAG)-8);
      {? _ref<>0 & _nam<>''
      || SER_NAG.cntx_psh(); SER_NAG.prefix();
         {? SER_NAG.seek(_ref,_nam)
         || {? SER_NAG.T<>SER_KOR.TYP
            || FUN.info('Wybrany dokument nie jest potwierdzeniem salda.'@); _mp.error()
            |? SER_NAG.ST_AKC<>'T'
            || FUN.info('Potwierdzenie salda nie jest zaakceptowane.'@)
            || SER_NAGS.cntx_psh(); SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref());
               {? SER_NAGS.first()
               || {? _mp.isAutoRun()
                  || params_exec('potwkor_zak_nan1','!fks_rrk_dkpw',0)
                  || {? SER_NAGS.r_lock(1,1)
                     || params_exec('okno_potwier','rozrach_kor','POTW','Potwierdzenie salda');
                        SER_NAGS.r_unlock()
                     || FUN.info('Dokument obsługuje inny operator.'@)

                     ?}
                  ?}
               || FUN.info('Nie znaleziono potwierdzenia salda.'@); _mp.error()
               ?};
               SER_NAGS.cntx_pop()

            ?}
         || {? ~_mp.isAutoRun()
            || FUN.info('Nie znaleziono potwierdzenia salda.'@)
            ?};
            _out.SER_NAG:=null;
            _mp.save(,_out);
            _mp.done()
         ?};
         SER_NAG.cntx_pop()
      || {? ~_mp.isAutoRun()
         || FUN.info('Nie znaleziono potwierdzenia salda.'@)
         ?};
         _out.SER_NAG:=null;
         _mp.save(,_out);
         _mp.done()
      ?}
   || {? ~_mp.isAutoRun()
      || FUN.info('Nie znaleziono potwierdzenia salda.'@)
      ?};
      _out.SER_NAG:=null;
      _mp.save(,_out);
      _mp.done()
   ?}
|? _akcja='NANIES'
|| _mp.trigRef('SER_NAG');
   params_exec('potwkor_zak_nan1','!fks_rrk_dkpw',0)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ nanoszenie potwierdzeń sald'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('SER_NAG',_we)
|| _ref:=BB.sqlint($_we.SER_NAG); _nam:=form(($_we.SER_NAG)-8);
   {? _ref<>0 & _nam<>''
   || SER_NAG.cntx_psh(); SER_NAG.use(_nam); SER_NAG.prefix();
      KH.cntx_psh(); KH.prefix();
      {? SER_NAG.seek(_ref,_nam)
      || _desc:='Zakończ nanoszenie potwierdzeń sald dla kontrahenta: %1 z dnia: %2'@@[SER_NAG.KH().SKR,$SER_NAG.DATA]
      ?};
      SER_NAG.cntx_pop(); KH.cntx_pop()
   ?}
?};
_desc


\potwkor_zak_nan1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Naniesienie potwierdzenia kontrahenta - wewnętrzna
::   WE: _a - 0 - jedna, 1 - kilka
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
{? SER_NAG.r_lock(1,1)
|| {? SER_NAG.ST_AKC='N'
   || {? ~_a & SER_NAG.sel_size()=0
      || FUN.info('Potwierdzenie salda nie jest zaakceptowane.'@)
      ?}
   |? SER_NAG.ST_POT='T'
   || {? ~_a & SER_NAG.sel_size()=0
      || FUN.info('Dla potwierdzenia salda zakończone jest nanoszenie potwierdzeń.'@)
      ?}
   || SER_NAG.cntx_psh(); SER_NAG.prefix();
      SER_NAG.ST_POT:='T';
      SER_NAG.put();
      SER_NAG.cntx_pop();
      {? SER_NAG.get() & SER_NAG.ST_POT='T' || _mp.done() ?};
      {? var_pres('licz_gr')>0 || licz_gr+=1 ?}
   ?};
   SER_NAG.r_unlock()
|| {? ~_a
   || {? SER_NAG.sel_size()=0 || exec('blok_err','rozrach_kor') ?}
   || ile_zabl+=1
   ?}
?}


\potw_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Potwierdzenie salda przez kontrahenta
::  OLD: \potw_dok/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_size:=SER_POZ.sel_size();
{? _size>0
|| {? SER_POZ.POTWIER<>2
   || ile_zmie+=1; SER_POZ.POTWIER:=2;
      SER_POZ.KOMEN:=DEFINE.UWAGI; SER_POZ.put()
   ?}
|| {? SER_POZ.POTWIER<>2
   || SER_POZ.POTWIER:=2; undefine();
      define('UWAGI','','','Uwagi kontrahenta'@,60,60);
      _ok:=def_btn('text=%1'['Zapisz'@],'key:F2');
      _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
      def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
      def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
      {? def_edit(,'Uwagi kontrahenta'@)
      || SER_POZ.KOMEN:=DEFINE.UWAGI; SER_POZ.put();
         {? SER_NAGS.SER_NAG().WAL<>null || exec('sum_potw','rozrach_kor') ?}
      ?};
      undefine()
   || FUN.info('Pozycja wcześniej potwierdzona.'@)
   ?}
?}


\bepotdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed akcja grupowa potwierdzenia salda przez kontrahenta
::  OLD: \bepotdok/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Potwierdzić zaznaczone?'@)
|| undefine();
   define('UWAGI','','','Uwagi kontrahenta'@,60,60);
   _ok:=def_btn('text=%1'['OK'@],'key:F2');
   _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
   def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
   def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   {? def_edit(,'Uwagi kontrahenta'@)
   || ile_zmie:=0; ile_zazn:=SER_POZ.sel_size(); 1
   || 0
   ?}
|| 0
?}


\aepotdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po akcji grupowej potwierdzenia salda przez kontrahenta
::  OLD: \aepotdok/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_ile:=(+$ile_zazn);
{? (+$ile_zmie)>_ile || _ile:=(+$ile_zmie) ?};
FUN.info(form(form('Pozycji zaznaczonych: ',24)+$ile_zazn,_ile+24)+'\n'+
         form(form('Pozycji potwierdzonych: ',24)+$ile_zmie,_ile+24));
{? SER_NAGS.SER_NAG().WAL<>null || exec('sum_potw','rozrach_kor') ?};
&ile_zazn; &ile_zmie; undefine()


\odrzucps
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Anulowanie potwierdzenia salda przez kontrahenta
::  OLD: \odrzucps/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_size:=SER_POZ.sel_size();
{? _size>0
|| {? SER_POZ.POTWIER<>1
   || ile_zmie+=1; SER_POZ.POTWIER:=1;
      SER_POZ.KOMEN:=DEFINE.UWAGI; SER_POZ.put()
   ?}
|| {? SER_POZ.POTWIER<>1
   || SER_POZ.POTWIER:=1; undefine();
      define('UWAGI','','','Uwagi kontrahenta'@,60,60);
      _ok:=def_btn('text=%1'['Zapisz'@],'key:F2');
      _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
      def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
      def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
      {? def_edit(,'Uwagi kontrahenta'@)
      || SER_POZ.KOMEN:=DEFINE.UWAGI; SER_POZ.put();
         {? SER_NAGS.SER_NAG().WAL<>null || exec('sum_potw','rozrach_kor') ?}
      ?};
      undefine()
   || FUN.info('Pozycja wcześniej odrzucona.'@)
   ?}
?}


\beodrzps
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed akcja grupowa odrzucenia salda dokumentu przez kontrahenta
::  OLD: \beodrzps/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Odrzucić zaznaczone?'@)
|| undefine();
   define('UWAGI','','','Uwagi kontrahenta'@,60,60);
   _ok:=def_btn('text=%1'['OK'@],'key:F2');
   _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
   def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
   def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   {? def_edit(,'Uwagi kontrahenta'@)
   || ile_zmie:=0; ile_zazn:=SER_POZ.sel_size(); 1
   || 0
   ?}
|| 0
?}


\aeodrzps
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po akcji grupowej odrzucenia salda dokumentu przez kontrahenta
::  OLD: \aeodrzps/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info(form('Pozycji zaznaczonych: ',24)+$ile_zazn+'\n'+
         form('Pozycji odrzuconych: ',24)+$ile_zmie);
{? SER_NAGS.SER_NAG().WAL<>null || exec('sum_potw','rozrach_kor') ?};
&ile_zazn; &ile_zmie; undefine()


\zak_nan_potw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zakończenie nanoszenia potwierdzeń
::----------------------------------------------------------------------------------------------------------------------
_selsize:=SER_NAGS.sel_size();
_pyt:={? _selsize=0
      || FUN.choice('Zakończenie nanoszenia potwierdzeń sald?'@,,'Bieżącego'@,'Wszystkich z dnia %1'@[form(SER_NAG.DATA)])
      || 1
      ?};
{? _pyt
|| SER_NAG.cntx_psh; SER_NAG.prefix(REF.FIRMA);
   ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? _pyt=1
   || SER_NAGS.SER_NAG();
      {? SER_NAG.ST_AKC='N'
      || {? _selsize=0 || FUN.info('Potwierdzenie salda nie jest zaakceptowane.'@) ?}
      |? SER_NAG.ST_POT='T'
      || {? _selsize=0 || FUN.info('Dla potwierdzenia salda zakończone jest nanoszenie potwierdzeń.'@) ?}
      || exec('potwkor_zak_nan','!fks_rrk_dkpw')
      ?}
   |? _pyt=2
   || SER_KOR.DATA:=SER_NAG.DATA;
      SER_NAGS.cntx_psh;
      {? ROZRACH.KONTR=null
      || SER_NAGS.index('SER_NAG'); SER_NAGS.prefix(REF.FIRMA,OPERATOR.DEPT().OD,SER_NAG.T,SER_KOR.DATA)
      || SER_NAGS.index('TYPKH');
         SER_NAGS.prefix(REF.FIRMA,OPERATOR.DEPT().OD,SER_NAG.T,ROZRACH.KONTR().KOD,ROZRACH.KONTR().KOD,SER_KOR.DATA)
      ?};
      {? SER_NAGS.first()
      || _czy:=1;
         ile_zabl:=0;
         {! |?
            SER_NAGS.SER_NAG();
            {? SER_NAG.ST_AKC='T' & SER_NAG.ST_POT='N'
            || exec('potwkor_zak_nan','!fks_rrk_dkpw')
            ?};
            SER_NAGS.next()
         !};
         {? _selsize=0
         || {? ile_zabl>0
            || FUN.info('Zakończono nanoszenie potwierdzeń.'
                        '\nLiczba potwierdzeń sald używanych ich przez innych użytkowników systemu: %1'@[$ile_zabl])
            ?}
         ?};
         VAR_DEL.delete('ile_zabl')
      ?};
      SER_NAGS.cntx_pop
   || 0
   ?};
   ODD.cntx_pop(); SER_NAG.cntx_pop()
?}


\potwkor_zak_nan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Naniesienie potwierdzenia kontrahenta
::----------------------------------------------------------------------------------------------------------------------
SER_NAGS.SER_NAG();
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RRK_DKPW';
_params.AKCJA:='NANIES';
_params.UIDREF:=SER_NAG.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.PROC_START:='N';
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SER_NAG',SER_NAG.ref());
exec('mp_run','#b__box',_params)


\bg_zak_nan_potw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formula początkowa dla zakończenia naniesienia potwierdzenia kontrahenta
::----------------------------------------------------------------------------------------------------------------------
licz_gr:=0; licz_zaz:=SER_NAGS.sel_size();
_zwrot:=FUN.ask('Zakończyć nanoszenie potwierdzeń kontrahentów?'@);
{? ~_zwrot || VAR_DEL.delete('licz_gr','licz_zaz') ?};
_zwrot


\ag_zak_nan_potw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formula koncowa dla zakończenia naniesienia potwierdzenia kontrahenta
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba zaznaczonych pozycji: %1'
         '\nLiczba zakończonych naniesień potwierdzeń kontrahentów: %2'@[$licz_zaz,$licz_gr]);
VAR_DEL.delete('licz_gr','licz_zaz'); 1


\anu_zak_nan_potw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Anulowanie zakończenia nanoszenia potwierdzeń kontrahentów
::----------------------------------------------------------------------------------------------------------------------
_selsize:=SER_NAGS.sel_size();
SER_NAG.cntx_psh; SER_NAG.prefix(REF.FIRMA);
SER_NAGS.cntx_psh();
ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
SER_KOR.DATA:=SER_NAG.DATA;
_pyt:={? _selsize=0
      || FUN.choice('Anulowanie zakończenia nanoszenia potwierdzeń kontrahentów dla potwierdzeń.'@,,
                    'Bieżącego'@,'Wszystkich z dnia %1'@[form(SER_NAG.DATA)])
      || 1
      ?};
{? _pyt<>0
|| {? _pyt=1
   || SER_NAGS.SER_NAG();
      {? SER_NAG.ST_POT='N'
      || {? _selsize=0
         || FUN.info('W potwierdzeniu salda nie jest zakończone nanoszenie potwierdzeń kontrahenta.'@)
         ?}
      || exec('anu_zak_nan_potw1','!fks_rrk_dkpw')
      ?}
   |? _pyt=2
   || {? ROZRACH.KONTR=null
      || SER_NAGS.index('SER_NAG'); SER_NAGS.prefix(REF.FIRMA,OPERATOR.DEPT().OD,SER_NAG.T,SER_KOR.DATA)
      || SER_NAGS.index('TYPKH');
         SER_NAGS.prefix(REF.FIRMA,OPERATOR.DEPT().OD,SER_NAG.T,ROZRACH.KONTR().KOD,ROZRACH.KONTR().KOD,SER_KOR.DATA)
      ?};
      ile_zabl:=0;
      {? SER_NAGS.first()
      || {! |?
           SER_NAGS.SER_NAG();
           {? SER_NAG.ST_POT='T'
           || exec('anu_zak_nan_potw1','!fks_rrk_dkpw')
           ?};
           SER_NAGS.next()
         !}
      ?};
      {? _selsize=0
      || {? ile_zabl>0
         || FUN.info('Zakończono anulowanie nanoszenia potwierdzeń.'
                     '\nLiczba potwierdzeń sald używanych ich przez innych użytkowników systemu: %1'@[$ile_zabl])
         ?}
      ?};
      VAR_DEL.delete('ile_zabl')
   ?}
?};
SER_NAGS.cntx_pop(); SER_NAG.cntx_pop(); ODD.cntx_pop()


\anu_zak_nan_potw1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Anulowanie zakończenia nanoszenia potwierdzeń kontrahentów
::----------------------------------------------------------------------------------------------------------------------
{? SER_NAG.r_lock(1,1)
|| SER_NAG.cntx_psh(); SER_NAG.prefix();
   SER_NAG.ST_POT:='N';
   SER_NAG.put();
   SER_NAG.cntx_pop();
   {? var_pres('licz_gr')>0 || licz_gr+=1 ?};
   SER_NAG.r_unlock()
|| {? ~_a
   || {? SER_NAG.sel_size()=0 || exec('blok_err','rozrach_kor') ?}
   || ile_zabl+=1
   ?}
?}


\bg_anu_zak_nan_potw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formula początkowa dla anulowania zakończenia naniesienia potwierdzenia kontrahenta
::----------------------------------------------------------------------------------------------------------------------
licz_gr:=0; licz_zaz:=SER_NAGS.sel_size();
_zwrot:=FUN.ask('Anulowanć zakończenie nanoszenia potwierdzeń kontrahentów dla potwierdzeń?'@);
{? ~_zwrot || VAR_DEL.delete('licz_gr','licz_zaz') ?};
_zwrot


\ag_anu_zak_nan_potw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formula koncowa dla anulowania zakończenia naniesienia potwierdzenia kontrahenta
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba zaznaczonych pozycji: %2'
         '\nLiczba anulowanych naniesień potwierdzeń kontrahentów: %1'@[$licz_zaz,$licz_gr]);
VAR_DEL.delete('licz_gr','licz_zaz'); 1


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'ser_nag'>0
         || _record:=exec('FindAndGet','#table',SER_NAG,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 70ea577a4652fb86b0d5fd214591ae860c6fd4d405fe987c67aefa1413d38252c4187d1f9be55b51ca97cc79754b8e07abbddea8cb34a7567ec92a5e2d29827daaec8bdaf606de7b11a5739cbff7305802ea83a896e3febb22370a31e89785f64ee333c77aa1bf64c1e89e2fb62dec08fd59d753c007e688d46d718bda401606

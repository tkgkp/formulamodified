:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pktu.fml
:: Utworzony: 12.04.2023
:: Autor: MicKoc
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PKTU - Ewidencja urządzeń do badań trzeźwości
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Ewidencja urządzeń do badań trzeźwości - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
KOT_URZA.index('NAZWA');
KOT_URZA.prefix();
KOT_URZA.win_sel('WER');
KOT_URZA.win_edit('RED');
exec('kot_urza_zalaczniki','kontrola_trzezwosci');
~~


\kot_urza_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Formuła na "Dołącz - przed" dla rekordu KOT_URZA
::   WE:
::   WY:
::  OLD: \kot_urza_dolacz_b/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOT_URZA.blank();
{? KOT_URZA.edit("exec('kot_urza_ra','!zws_par_pktu')")
|| KOT_URZA.cntx_psh();
   KOT_URZA.prefix();
   KOT_URZA.add();
   KOT_URZA.cntx_pop()
?}


\kot_urza_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Formuła na "Popraw - przed" dla rekordu KOT_URZA
::   WE:
::   WY:
::  OLD: \kot_urza_popraw_b/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOT_URZA.edit("exec('kot_urza_ra','!zws_par_pktu')")
|| KOT_URZA.cntx_psh();
   KOT_URZA.prefix();
   KOT_URZA.put();
   KOT_URZA.cntx_pop()
?}


\kot_urza_okienko_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Okienko przed tabeli KOT_URZA
::   WE:
::   WY:
::  OLD: \kot_urza_okienko_b/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOT_URZA.cntx_psh();
KOT_URZA.index('UNIQ');
KOT_URZA.prefix();
{? KOT_URZA.first()
|| {!
   |? {? KOT_URZA.DATA<date() & KOT_URZA.A<>'N'
      || KOT_URZA.A:='N';
         KOT_URZA.put()
      ?};
      KOT_URZA.next()
   !}
?};
KOT_URZA.cntx_pop();
1


\kot_urza_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Rekord po tabeli KOT_URZA
::  OLD: \kot_urza_ra/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(KOT_URZA,-menu_txt()='popraw',,'DATA','KOT_URZA','NAZ','TYP','KOT_JM');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & _chk=0)
|| return(_chk)
?};
{? KOT_URZA.A='T' & KOT_URZA.DATA<=date()
|| {? ~FUN.ask('Data ważności urządzenia upłynęła. Czy chcesz wprowadzić takie urządzenie?'@)
   || return('DATA')
   ?}
?};
''


\kot_urza_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Wartość początkowa
::   WE:
::   WY:
::  OLD: \kot_urza_bl/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOT_URZA.ref()

:Sign Version 2.0 jowisz:1045 2023/04/28 15:59:40 84c231334da611ed8eddcb2995aa6141b74e41b1b57deee1dcd235aa883060d3bec30bc3214e1d98e6f65d32b32d656402d0c5c3a9c3876d940424e476245ebb64daca86075ba2c416aab148c6161608719cfe543c446e02c49fea8bcea5e08b6837b6deeebfe1b397f906e4a5583f3980faed1f791ddaf1af05f26d851f90db

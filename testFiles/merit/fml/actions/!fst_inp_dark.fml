:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fst_inp_dark.fml
:: Utworzony: 03.03.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_INP_DARK - Utw. arkuszy inwentar. środków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Utw. arkuszy inwentar. środków - główna formuła czynności FST_INP_DARK.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
_akcja:=-menu_txt();
{? _akcja='dołącz' || exec('srxa_add','!fst_inp_dark')
|? _akcja='popraw' || exec('srxa_edit','!fst_inp_dark')
|? _akcja='usuń' || exec('srxa_del','!fst_inp_dark')
?}


\srxa_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowego arkusza inwentaryzacyjnego
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, dołączanie arkuszy nie jest możliwe.'@);
   return()
?};
{? SRXI.r_lock(1,1,1)
||
   SRXA.win_edit('RED');
   SRXA.blank();
   EDIT_ES.JORG:='';
   SRXA.SRXI:=SRXI.ref();
   {? SRXA.SRXI().ODD<>null || SRXA.ODD:=SRXA.SRXI().ODD ?};
   SRXA.NR:=exec('get_nr','fst');
   exec('odd_filtr','fst');
   SRXA.memo_set('');
   {? SRXA.edit("exec('chk_srxa','!fst_inp_dark','ADD')")
   || SRXA.add();
      SRXA.memo_put()
   ?};
   SRXI.r_unlock()
|| FUN.emsg('Inwentaryzacja zablokowana przez innego użytkownika. Dołączanie arkuszy niemożliwe.'@)
?}


\be_srxa_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redagowaniem pola SRXA.ODD
::----------------------------------------------------------------------------------------------------------------------
{? SRXA.SRXI().ODD<>null || 0 || 1 ?}


\srxa_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja arkusza inwentaryzacyjnego
::----------------------------------------------------------------------------------------------------------------------
{? SRXA.SRXI().STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikowanie arkuszy nie jest możliwe.'@);
   return()
?};
{? SRXA.STATUS='T'
|| FUN.emsg('Bieżący arkusz jest zamknięty i nie można go poprawić.'@);
   return()
?};
_size:=0;
SRXD.cntx_psh();
SRXD.index('SRXD');
SRXD.prefix(SRXA.ref());
{? SRXD.size()>0 || _size:=1 ?};
SRXD.cntx_pop();

{? _size
|| {? ~FUN.ask('Arkusz posiada pozycje, poprawianie danych arkusza nie jest możliwe,\n'
              'z wyjątkiem opisu arkusza. Kontynuować?'@)
   || return()
   || SRXA.win_edit('OPIS')
   ?}
|| SRXA.win_edit('RED')
?};
{? SRXA.r_lock(1,1,1)
|| exec('odd_filtr','fst');
   SRXA.memo_get();
   {? SRXA.edit("exec('chk_srxa','!fst_inp_dark','EDIT')")
   || SRXA.put();
      SRXA.memo_put()
   ?};
   SRXA.r_unlock()
|| FUN.emsg('Arkusz spisowy zablokowany przez innego użytkownika.'@)
?}


\srxa_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie arkusza inwentaryzacyjnego
::----------------------------------------------------------------------------------------------------------------------
{? SRXA.SRXI().STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, usuwanie arkuszy nie jest możliwe.'@);
   return()
?};
{? SRXA.STATUS='T'
|| FUN.emsg('Bieżący arkusz jest zamknięty i nie można go usunąć.'@);
   return()
?};
{? SRXA.count()>0
|| FUN.emsg('Bieżący arkusz jest wykorzystywany w systemie i nie można go usunąć.'@)
|| {? SRXA.r_lock(1,1,1)
   || {? FUN.ask('Usunąć arkusz?'@)
      || SRXA.r_unlock();
         SRXA.del()
      || SRXA.r_unlock()
      ?}
   || FUN.emsg('Arkusz spisowy zablokowany przez innego użytkownika.'@)
   ?}
?}


\chk_srxa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła weryfikuje dane w oknie redagowania arkusza inwentaryzacyjnego środków trwałych
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRXA,,'NR');
{? _wy=''
|| {? _a='EDIT' || _ref:=SRXA.ref() || _ref:=null ?};
   _nr:=SRXA.NR;
   SRXA.cntx_psh();
   SRXA.index('SRXI');
   SRXA.prefix(SRXI.ref(),_nr);
   {? SRXA.first() & (_a='ADD' | (_a='EDIT' & SRXA.ref()<>_ref))
   || FUN.emsg('Arkusz inwentaryzacyjny o podanym numerze już istnieje.'@);
      _wy:='NR'
   ?};
   SRXA.cntx_pop()
?};
{? _wy=''
|| {? SRXA.NR<=0
   || FUN.emsg('Numer arkusza powinien być większy od zera.'@);
      _wy:='NR'
   ?}
?};
{? _wy=''
|| {? SRXA.SRXI().ODD<>null & SRXA.ODD<>null & SRXA.SRXI().ODD<>SRXA.ODD
   || FUN.emsg('Jednostka księgowa niezgodna z definicją inwentaryzacji.'@);
      _wy:='ODD'
   ?}
?};
{? _wy=''
|| {? SRXA.POM<>null & SRXA.ODD<>null & SRXA.POM().ODD<>SRXA.ODD
   || FUN.emsg('Nieprawidłowa definicja arkusza: pomieszczenie należy do innej j. księgowej.'@);
      _wy:='POM'
   ?}
?};
{? _wy=''
|| {? SRXA.JORG<>null & SRXI.ODD<>null
   || SKIDXODD.cntx_psh();
      SKIDXODD.index('UD_SKL');
      SKIDXODD.prefix(SRXA.JORG);
      {? SKIDXODD.first()
      || _odd:=SKIDXODD.ODD
      || _odd:=null
      ?};
      SKIDXODD.cntx_pop();
      {? _odd<>null & _odd<>SRXI.ODD
      || FUN.emsg('Nieprawidłowa definicja arkusza: jednostka organizacyjna związana jest\n'
                  'z inną jednostką księgową niż jednostka ustalona w definicji inwentaryzacji.'@);
         _wy:='JORG'
      ?}
   ?}
?};
_wy


\srxa_zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła zamyka bieżący arkusz inwentaryzacyjny
::----------------------------------------------------------------------------------------------------------------------
{? SRXA.SRXI().STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikacje arkuszy nie są możliwe.'@);
   return()
?};
{? SRXA.STATUS='T'
|| FUN.emsg('Arkusz inwentaryzacyjny jest już zamknięty.'@);
   return()
?};
_find:=0;
SRXD.cntx_psh();
SRXD.index('STATUS');
SRXD.prefix(SRXA.ref(),'N');
{? SRXD.first() || _find:=1 ?};
SRXD.cntx_pop();
{? _find
|| FUN.emsg('Arkusz zawiera niezatwierdzone pozycje. Zamknięcie arkusza niemożliwe.'@);
   return()
?};
{? FUN.ask('Zamknąć arkusz inwentaryzacyjny nr %1?'@[$SRXA.NR])
|| SRXA.STATUS:='T';
   SRXA.put()
?}


\srxa_otworz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła otwiera bieżący arkusz inwentaryzacyjny
::----------------------------------------------------------------------------------------------------------------------
{? SRXA.SRXI().STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikacje arkuszy nie są możliwe.'@);
   return()
?};
{? SRXA.STATUS='N'
|| FUN.emsg('Arkusz inwentaryzacyjny nie jest zamknięty.'@);
   return()
?};

SRXD.cntx_psh();
SRXD.index('DOK_L');
SRXD.prefix(SRXA.ref(),'T');
_dok_l:=SRXD.size();
SRXD.index('DOK_W');
SRXD.prefix(SRXA.ref(),'T');
_dok_w:=SRXD.size();
SRXD.cntx_pop();
{? _dok_l>0 | _dok_w>0
|| FUN.emsg('Dla pozycji arkusza istnieją już wygenerowane dokumenty poinwentaryzacyjne.\n'
            'Arkusza nie nmozna otworzyć.'@);
   return()
?};

{? FUN.ask('Otworzyć arkusz inwentaryzacyjny nr %1?'@[$SRXA.NR])
|| SRXA.STATUS:='N';
   SRXA.put()
?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:44 d4a8cdd831d3d6de298a3425642f7d35d657027ac2d6c13339d1a85797b5fe74b14e4a6c30fdd13c17ae77a0b0e5d12e73043980b9b2aa252f559d6a16d44705832f740f0cf0d3e5ec5c9817bc031e02dd4807415100edadf8e2e7d315f69cd06146f5f5e6a99a4c64abe9f40a339cce313b5bb8cd44fba7fa7ebded228fdb8f

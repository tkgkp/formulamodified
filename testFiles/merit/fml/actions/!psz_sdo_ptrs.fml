:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdo_ptrs.fml
:: Utworzony: 28.09.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PSZ_SDO_PTRS - Trenerzy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Trenerzy - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

_wnd:=exec('create_wnd','!psz_sdo_ptrs');

ZZ_POM.cntx_psh();
SZK_TREN.cntx_psh();
SZK_TREN.win_sel(_wnd);
SZK_TREN.select();
SZK_TREN.cntx_pop();
SZK_TREN.win_del(_wnd);
ZZ_POM.cntx_pop();
~~


\create_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Tworzy okienko czynności.
::   WE:
::   WY: akronim okienka tymczasowego
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';

_wnd:=SZK_TREN.grp_make('Trenerzy'@,
:  po wyświetleniu
   "  _par:=params_get();
      params_set(_par);
      ZZ_OSOBA.cntx_psh();
      ZZ_OSOBA.win_edit('RED');
      ZZ_OSOBA.win_patt('WZO');
      SZK_OPIS.cntx_psh();
      SZK_OPIS.clear();
      1
   ",
:  identyfikator
   'psz_sdo_ptrs',
:  położenie
   0,0,
:  zamknięcie
   "  ZZ_OSOBA.cntx_pop();
      SZK_TREN.f_clear();
      SZK_OPIS.f_clear();
      SZK_OPIS.cntx_pop();
      1
   "
);

SZK_TREN.grp_sel(_wnd,SZK_TREN,'WER','Wszyscy'@,
:  po odświeżeniu
   "grp_disp(SZK_OPIS,'PSZ_OC')",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "  SZK_TREN.f_clear();
      {? _a
      || ZZ_POM.TREN_DOD:=''
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

SZK_TREN.grp_sel(_wnd,SZK_TREN,'WER','Pracownicy'@,
:  po odświeżeniu
   "grp_disp(SZK_OPIS,'PSZ_OC')",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "  exec('szk_tren_filt','!psz_sdo_ptrs','P','P');
      {? _a
      || ZZ_POM.TREN_DOD:='exec(\\'szk_tren_dod_p\\',\\'!psz_sdo_ptrs\\',\\'P\\')'
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

SZK_TREN.grp_sel(_wnd,SZK_TREN,'WER','Zleceniobiorcy'@,
:  po odświeżeniu
   "grp_disp(SZK_OPIS,'PSZ_OC')",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "  exec('szk_tren_filt','!psz_sdo_ptrs','P','Z');
      {? _a
      || ZZ_POM.TREN_DOD:='exec(\\'szk_tren_dod_p\\',\\'!psz_sdo_ptrs\\',\\'Z\\')'
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

SZK_TREN.grp_sel(_wnd,SZK_TREN,'WER','Osoby z zewnątrz'@,
:  po odświeżeniu
   "grp_disp(SZK_OPIS,'PSZ_OC')",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "  exec('szk_tren_filt','!psz_sdo_ptrs','ZZ_OSOBA','');
      {? _a
      || ZZ_POM.TREN_DOD:='exec(\\'szk_tren_dod_o\\',\\'!psz_sdo_ptrs\\')'
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

SZK_TREN.grp_splt(_wnd,,'horizontal','bottom');
SZK_TREN.grp_sel(_wnd,SZK_OPIS,'PSZ_OC',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "  {? {? SZK_TREN.f_active()
         || ~SZK_TREN.f_size()
         || ~SZK_TREN.size()
         ?}
      || return('#disable')
      ?};
      SZK_OPIS.f_set('OD',,'SZK_TREN=:_a',SZK_TREN.ref())
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

_wnd


\szk_tren_filt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.10]
:: OPIS: Zakłada filtr na tabelę SZK_TREN
::   WE: _a - typ zapisu (wartosc SZK_TREN.ZZ_OSOBA.ZZ_DOK.KLASA)
::       _a - kod formy współpracy
::  OLD: \szk_tren_filt/zz_view.fml
::----------------------------------------------------------------------------------------------------------------------
SZK_TREN.f_set(
   'ZZ_OSOBA(NAZWISKO),ZZ_OSOBA(PIERWSZE),ZZ_OSOBA(PESEL)',
   'join ZZ_OSOBA using (SZK_TREN.ZZ_OSOBA,ZZ_OSOBA.REFERENCE) '+
   'join ZZ_DOK using (ZZ_OSOBA.ZZ_LINK,ZZ_DOK.REFERENCE) '+
   {? _a='P' || ' join P using (ZZ_OSOBA.ZZ_LINK,P.ZZ_DOK) join F_ZATR' || '' ?},
   'ZZ_DOK.KLASA=\':_a\''+{? _a='P' || ' and F_ZATR.KOD=\':_b\'' || '' ?},
   _a,_b
)


\szk_tren_xxx_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła akcji "Okienko przed" okienek tabeli SZK_TREN.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_psh','phr_widok','PSZ_SDO_PTRS');

ZZ_OSOBA.win_edit('RED');
SZK_TREN.win_patt('WZO');

1


\szk_tren_xxx_oa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła akcji "Okienko po" okienek tabeli SZK_TREN.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_pop','phr_widok');
1


\szk_tren_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła akcji "dołącz przed" okien tabeli SZK_TREN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? SZK_TREN.f_active()=0 | SZK_TREN.f_active()=2
|| exec('szk_tren_dod_x','!psz_sdo_ptrs')
|| ($ZZ_POM.TREN_DOD)()
?}


\szk_tren_dolacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła akcji "dołącz po" okien tabeli SZK_TREN
::   WE:
::   WY: 0
::----------------------------------------------------------------------------------------------------------------------
{? SZK_TREN.f_active()
|| SZK_TREN.f_rfresh();
   SZK_TREN.f_seek(SZK_TREN.ref())
?};
0


\szk_tren_dod_x
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła akcji "dołącz przed" okien tabeli SZK_TREN jeśli przeglądana jest cała dziedzina.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_done:=1;
{!
|? _typ:=FUN.choice(
      'Proszę określić pozycję trenera względem organizacji?'@,'ASK',
      'Pracownik'@,'Zleceniobiorca'@,'Osoba z zewnątrz'@
   );
   {? _typ=1 || ~exec('szk_tren_dod_p','!psz_sdo_ptrs','P')
   |? _typ=2 || ~exec('szk_tren_dod_p','!psz_sdo_ptrs','Z')
   |? _typ=3 || ~exec('szk_tren_dod_o','!psz_sdo_ptrs')
   || _done:=0
   ?}
!};
_done


\szk_tren_dod_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: "Tworzy" trenera na podstawie wybranego współpracownika.
::   WE: _a [STRING] - kod formy współpracy
::   WY: 1 - liczba dodanych trenerów, 0 - wpp
::  OLD: \trener_p/zz_view.fml
::  OLD: \trener_prz/zz_view.fml
::----------------------------------------------------------------------------------------------------------------------
_arg:=exec('wybierz_args','pracownik');
_arg.DOMAIN:='PSZ';
_arg.F_ZATR:=_a;
_arg.WIELU:=1;
_arg.SQL_FROM:=
   'left join ZZ_OSOBA using(ZZ_OSOBA.ZZ_LINK,P.ZZ_DOK) '
   'left join SZK_TREN using(SZK_TREN.ZZ_OSOBA,ZZ_OSOBA.REFERENCE)';
_arg.SQL_WHERE:=
   'SZK_TREN.REFERENCE is null';

_val:=0;
_ref:=null;
_ret:=exec('wybierz','pracownik',_arg);
_loop:=_ret.P.first();

P.cntx_psh();
P.prefix();
{!
|? _loop
|! {? P.seek(_ret.P.SQL)
   || _osoba:=exec('czy_osoba','phr_dane',P,1);
      {? _osoba
      || SZK_TREN.blank(1);
         SZK_TREN.ZZ_OSOBA:=_osoba;
         _val+=SZK_TREN.add(1)
      ?}
   ?};
   _loop:=_ret.P.next()
!};
P.cntx_pop();

_val


\szk_tren_dod_o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: "Tworzy" trenera na podstawie wybranej osoby.
::  OLD: \trener_osoba/zz_view.fml
::----------------------------------------------------------------------------------------------------------------------
_arg:=exec('zz_osoba_wyb_args','phr_widok');
_arg.SQL_FROM:='left join SZK_TREN using(SZK_TREN.ZZ_OSOBA,ZZ_OSOBA.REFERENCE)';
_arg.SQL_WHERE:='SZK_TREN.REFERENCE is null';

_val:=0;
_ref:=null;
_ret:=exec('zz_osoba_wybierz','phr_widok',_arg);
_loop:=_ret.ZZ_OSOBA.first();

ZZ_OSOBA.cntx_psh();
ZZ_OSOBA.clear();
{!
|? _loop
|! {? ZZ_OSOBA.seek(_ret.ZZ_OSOBA.SQL)
   || SZK_TREN.blank(1);
      SZK_TREN.ZZ_OSOBA:=ZZ_OSOBA.ref();
      _val+=SZK_TREN.add(1)
   ?};
   _loop:=_ret.ZZ_OSOBA.next()
!};
ZZ_OSOBA.cntx_pop();

_val


\szk_tren_wyswietl_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła akcji "wyświetl przed" okien tabeli SZK_TREN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_osoba_disp','poc',SZK_TREN.ZZ_OSOBA)

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:59 08b2353c69fb8c170bade2739d19ac00b281c86f8d25fc8d476be9631f9d683c49c3faf570cc59b5d0a4118853176f02e206e2110644a6cbcb2997f54cf4df290fa73d4816069c27c7aed9574c5b0eb3ca5cdd17c129a2821fa67c359d123cc5874beab5632f7a876621d8198c2abbc87afc5d69712adca3f8714c8932711f2c

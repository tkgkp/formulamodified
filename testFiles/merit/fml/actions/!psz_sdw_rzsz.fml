:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdw_rzsz.fml
:: Utworzony: 06.12.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Formuły czynności PSZ_SDW_RZSZ - Zapisy własne.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Główna formuła czynności PSZ_SDW_RZSZ - Zapisy własne.
::----------------------------------------------------------------------------------------------------------------------
{? ~(exec('env_wt','psz') & exec('load_cur_prac','p_web'))
|| return()
?};

_szk:=sql('
   select
      SZK_OPIS.OD as OD,
      SZK_OPIS.REFERENCE as REF,
      SZK_ORG.NAZWA as ORG,
      SZK_TEM.TEMAT as TEMAT,
      SZK_OPIS.LU as LU,
      case
         when SZK_OPIS.LIMIT>0 then SZK_OPIS.LIMIT
         else -1
      end as L_MIEJSC
   from
      SZK_OPIS join
      SZK_ORG using(SZK_OPIS.ORG, SZK_ORG.REFERENCE) join
      SZK_TEM using(SZK_OPIS.TEMAT, SZK_TEM.REFERENCE) join
      SLO_KOD using(SZK_OPIS.STATUS, SLO_KOD.REFERENCE)
   where
      SLO_KOD.KOD=\'P\' and
      SZK_OPIS.PORTAL=\'T\' and
      (SZK_OPIS.DO>=current_date OR SZK_OPIS.DO IS NULL)
   order by
      1
');

ZZ_POM.MODUL:='A';
_typ_op:=exec('typ_op','phr_dane');

:: szkolenia dostępne dla pracownika

exec('wt_szkz_clean','psz',3);

Cntx.psh(P,SZK_PRAC,SZK_OPIS);
Cntx.clr(P,SZK_PRAC);
SZK_PRAC.index('SZK_PRAC');

WT_SZKZ.cntx_psh();
WT_SZKZ.index('KEY');
WT_SZKZ.prefix(app_info('web_tcid'),app_info('web_sesid'),app_info('web_tabid'),);
SZK_OPIS.prefix();
_loop:=_szk.first();
{!
|? _loop
|! {? SZK_OPIS.seek(_szk.REF)
   || SZK_PRAC.index('SZK_PRAC');
      SZK_PRAC.prefix(SZK_OPIS.ref());
      _jest:=(SZK_PRAC.first() & SZK_PRAC.find_key(P.ref()));
      _wolne:=SZK_OPIS.LIMIT-SZK_OPIS.LU;
      {? P.seek(P.ref()) & (_wolne>0 | SZK_OPIS.LIMIT=0)
         & exec('test_kryt','phr_dane',SZK_OPIS.ZZ_DOK,_typ_op,0,date(),P)>0
      || WT_SZKZ.blank();
         WT_SZKZ.SZK_OPIS:=SZK_OPIS.ref();
         WT_SZKZ.TEMAT:=_szk.TEMAT;
         WT_SZKZ.ORG:=_szk.ORG;
         {? _jest
         || WT_SZKZ.ZAPISANY:='T'
         || WT_SZKZ.ZAPISANY:='N'
         ?};
         WT_SZKZ.add()
      ?}
   ?};
   _loop:=_szk.next()
!};

Cntx.pop(P,SZK_PRAC,SZK_OPIS);

WT_SZKZ.web_select('WT_SZKZ');
WT_SZKZ.cntx_pop();
~~


\zapisz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Obsługa akcji "Zapisz się".
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('env_wt','psz')
|| return()
?};

Cntx.psh(SZK_PRAC,SZK_OPIS);
SZK_OPIS.prefix();
_szk:={? SZK_OPIS.seek(WT_SZKZ.SZK_OPIS) || SZK_OPIS.ref() || null() ?};
{? SZK_OPIS.seek(_szk)
|| SZK_PRAC.index('SZK_PRAC');
   SZK_PRAC.prefix(_szk);
   _jestLP:=(SZK_PRAC.first() & SZK_PRAC.find_key(P.ref()));
   {? _jestLP
   || FUN.info('Jesteś już zapisany na szkolenie.'@)
   || {? SZK_OPIS.LIMIT=0 | (SZK_OPIS.LIMIT-SZK_OPIS.LU)>0
      || SZK_PRAC.blank();
         SZK_PRAC.P:=P.ref();
         SZK_PRAC.SZKOL:=_szk;
         SZK_PRAC.WYDZIAL:=P.WYDZIAL;
         SZK_PRAC.STN:=P.ST;
         {? SZK_PRAC.add()
         || SZK_OPIS.LU:=SZK_OPIS.LU+1;
            SZK_OPIS.put();
            WT_SZKZ.ZAPISANY:='T';
            WT_SZKZ.put();
            FUN.info('Zostałeś zapisany na szkolenie.'@);
            WT_SZKZ.web_refresh('WT_SZKZ')
         ?}
      ?}
   ?}
?};
Cntx.pop(SZK_PRAC,SZK_OPIS);
~~


\wypisz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Obsługa akcji "Wypisz się"
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('env_wt','psz')
|| return()
?};

Cntx.psh(SZK_PRAC,SZK_OPIS);
SZK_OPIS.prefix();
_szk:={? SZK_OPIS.seek(WT_SZKZ.SZK_OPIS) || SZK_OPIS.ref() || null() ?};
{? SZK_OPIS.seek(_szk)
|| SZK_PRAC.index('SZK_PRAC');
   SZK_PRAC.prefix(_szk);
   _jestLP:=(SZK_PRAC.first() & SZK_PRAC.find_key(P.ref()));
   {? ~_jestLP
   || FUN.info('Nie zostałeś zapisany na szkolenie. Wypisanie niemożliwe.'@)
   || SZK_PRAC.del();
      SZK_OPIS.LU:=SZK_OPIS.LU-1;
      SZK_OPIS.put();
      WT_SZKZ.ZAPISANY:='N';
      WT_SZKZ.put();
      FUN.info('Zostałeś wypisany ze szkolenia.'@);
      WT_SZKZ.web_refresh('WT_SZKZ')
   ?}
?};
Cntx.pop(SZK_PRAC,SZK_OPIS);
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:59 6399ae24986c4ec37286b089697211c695a27ecfd408acf598871776578fbef094736a14fe7e5a340390a0df0dcd0ef119dd1049dc421cf1a75885d5ea827d933318f5fbe0007dfcda1d2124814f4e864412167b64313b4613187c99d9cc45e22828bdab56fa4b95c63f7542be6528a202f194b1ba8c442b92d543d0ee9b6a17

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_basl.fml
:: Utworzony: 25.07.2022
:: Autor: PD
:: Systemy:
::======================================================================================================================
:: Zawartość: Kanały sprzedaży - parametryzacja
::======================================================================================================================
\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Formuła główna czynności kanałów sprzedaży
::----------------------------------------------------------------------------------------------------------------------
BASLIN_K.cntx_psh();
BASLIN_K.index('ID');
_grp:=BASLIN_K.grp_make('BaseLinker',,'baslin_param',,,,,'maximized');
_before_k:="
BASLIN_K.cntx_psh();
BASLIN_K.win_edit('RED');
BASLIN_K.prefix('K',)
";
_after_k:="
BASLIN_K.cntx_pop()
";
BASLIN_K.grp_sel(_grp,,'WER','Kanały sprzedaży'@,,,,,_before_k,_after_k,,,'maximized','baslin_ksp');
_before_z:="
BASLIN_K.cntx_psh();
BASLIN_K.win_edit('RED_Z');
BASLIN_K.prefix('Z',)
";
_after_z:="
BASLIN_K.cntx_pop()
";
BASLIN_K.grp_sel(_grp,,'WER_Z','Źródła zamówień'@,,,,,_before_z,_after_z,,,'maximized','baslin_zz');
_before_s:="
ABS_STA.cntx_psh();
ABS_STA.index('NR');
ABS_STA.prefix('B',)
";
_after_s:="
ABS_STA.cntx_pop()
";
BASLIN_K.grp_sel(_grp,ABS_STA,'WER_P','Statusy BaseLinker'@,,,,,_before_s,_after_s,,,'maximized');

BASLIN_K.win_sel(_grp);
BASLIN_K.select();
BASLIN_K.cntx_pop()


\baslin_k_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Dołączanie kanału sprzedaży
::----------------------------------------------------------------------------------------------------------------------
BASLIN_K.cntx_psh();
BASLIN_K.prefix();
BASLIN_K.blank();
BASLIN_K.TYPE:='K';
BASLIN_K.efld_opt('RED','editable=1',,'NAME');
BASLIN_K.efld_opt('RED','editable=1',,'ODDZ');
BASLIN_K.efld_opt('RED','editable=1',,'ID');
BASLIN_K.efld_opt('RED','editable=1',,'AKT');
BASLIN_K.efld_opt('RED','mark=1',,'NAME');
BASLIN_K.efld_opt('RED','mark=1',,'DESC');
BASLIN_K.efld_opt('RED','mark=1',,'ODDZ');
_edit:=$'{? BASLIN_K.NAME=\'\' || \'NAME\'
         |? BASLIN_K.DESC=\'\' || \'DESC\'
         |? BASLIN_K.ODDZ=null || \'ODDZ\'
         || 1
         ?}';
{? BASLIN_K.edit(_edit) || {? BASLIN_K.add() || exec('add_def','!zws_par_basl') ?} ?};
BASLIN_K.cntx_pop()


\baslin_k_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Poprawa kanału sprzedaży
::----------------------------------------------------------------------------------------------------------------------
BASLIN_K.cntx_psh();
BASLIN_K.prefix();
BASLIN_K.efld_opt('RED','editable=grayed',,'NAME');
BASLIN_K.efld_opt('RED','editable=grayed',,'ODDZ');
BASLIN_K.efld_opt('RED','editable=grayed',,'ID');
BASLIN_K.efld_opt('RED','editable=grayed',,'AKT');
BASLIN_K.efld_opt('RED','mark=1',,'DESC');
_edit:=$'{? BASLIN_K.DESC=\'\' || \'DESC\' || 1 ?}';
{? BASLIN_K.edit(_edit) || BASLIN_K.put() ?};
BASLIN_K.cntx_pop()



\baslin_k_check
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Sprawdzenie wymaganych parametrów
::----------------------------------------------------------------------------------------------------------------------
{? BASLIN_K.TAR_ID='' || FUN.info('Brak domyślnego cennika.'@);return(0) ?};
{? BASLIN_K.MG_ID='' || FUN.info('Brak domyślnego magazynu.'@);return(0) ?};
1

\baslin_k_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktywuje/dezaktywuje/aktualizuje kanały sprzedaży
::   WE: [_a] - 0 dezaktywacja / 1 aktywacja
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',700808,2)='' || FUN.info('Niewypełniony parametr 700808(Token BaseLinker)'@); return('') ?};
_akt:={? var_pres('_a')=type_of(0) || _a || 1 ?};
{? _akt & ~exec('baslin_k_check','!zws_par_basl') || return('') ?};
BASLIN_K.cntx_psh();
BASLIN_K.prefix();
{? _akt
|| _inventory_id:=exec('addInventory','base_linker');
   {? _inventory_id=''
   || {? BASLIN_K.ID=''
      || FUN.info('Kanał sprzedaży nie został dodany.'@)
      || FUN.info('Kanał sprzedaży nie został zaktualizowany.'@)
      ?}
   ||
      {? BASLIN_K.ID=''
      || BASLIN_K.ID:=_inventory_id;
         BASLIN_K.AKT:='T';
         BASLIN_K.put();
         FUN.info('Kanał sprzedaży został dodany.'@)
      || FUN.info('Kanał sprzedaży został zaktualizowany.'@)
      ?}
   ?}
|| {? exec('deleteInventory','base_linker')
   || FUN.info('Nie udało się dezaktywować kanału sprzedaży.'@)
   || BASLIN_K.ID:='';
      BASLIN_K.AKT:='N';
      BASLIN_K.put();
      FUN.info('Kanał sprzedaży został dezaktywowany.'@)
   ?}
?};
BASLIN_K.cntx_pop()


\baslin_k_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktualizuje źródła zamówień
::----------------------------------------------------------------------------------------------------------------------
'UUUUUUUUUUUUUUUUUUUU#'


\baslin_k_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktualizuje źródła zamówień
::----------------------------------------------------------------------------------------------------------------------
BASLIN_K.del();
~~


\baslin_kz_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktualizuje źródła zamówień
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',700808,2)='' || FUN.info('Niewypełniony parametr 700808(Token BaseLinker)'@); return('') ?};
BASLIN_K.cntx_psh();
BASLIN_K.prefix();
exec('getOrderSources','base_linker');
BASLIN_K.cntx_pop()


\baslin_kz_typyzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktualizuje źródła zamówień
::----------------------------------------------------------------------------------------------------------------------
TYPYZAM.cntx_psh();
TYPYZAM.index('TYP');
TYPYZAM.prefix('Z',);
TYPYZAM.win_sel('SEL');
{? TYPYZAM.select()
||
   BASLIN_K.cntx_psh();
   BASLIN_K.prefix();
   BASLIN_K.TYPYZAM:=TYPYZAM.ref();
   BASLIN_K.put();
   BASLIN_K.cntx_pop()
?};
TYPYZAM.cntx_pop();
~~


\baslin_p_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Parametr domyślny
::----------------------------------------------------------------------------------------------------------------------
BASLIN_K.get();
_default:=($('BASLIN_K.%1_ID'[_a]))();
{? _default=__TAB.ID
|| ($('BASLIN_K.%1_ID'[_a]))():='';
   BASLIN_K.put()
|| ($('BASLIN_K.%1_ID'[_a]))():=__TAB.ID;
   BASLIN_K.put()
?}


\baslin_p_uni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Wyświetlanie parametrów - uniwersalne
::   WE: _a - akronim tabeli
::       _b - tytuł okienka
::       _c - pierwszy akronim
::       _d - pierwsza nazwa
::       _e - drugi akronim
::       _f - druga nazwa
::       _g - czy kolumna DEFAULT
::       _h - dodatkowa część zapytania sql
::----------------------------------------------------------------------------------------------------------------------
__maska:='';
_tabela:=($_a)();
{? var_pres('__TAB')>100 || obj_del(__TAB) ?};

{!|?
   __loop:=0;
   __maska:=exec('get_mask','!zws_par_basl',BASLIN_K.NAME+'_'+_a);
   __sql:='select SYNC_ID.ID, %1.%2, %1.%3, SYNC_ID.OPIS, SYNC_ID.REK, SYNC_ID.KOD as SYNC_KOD, SYNC_ID.REFERENCE as REF,
   SYNC_ID.ID as DEF from SYNC_ID join %1 using(%1.REFERENCE,SYNC_ID.REK)'[_a,_c,_e];
   {? var_pres('_h')=type_of('') || __sql+=_h ?};

   {? var_pres('__TAB')>100 || obj_del(__TAB) ?};
   {? __maska<>''
   ||
      SYNC_ID.cntx_psh();
      SYNC_ID.use(__maska);
      __TAB:=sql(__sql);
      SYNC_ID.cntx_pop()
   ||
      SYNC_ID.cntx_psh();
      {? var_pres('_h')=type_of('') || __TAB:=sql(__sql+' and 1=0') || __TAB:=sql(__sql+' where 1=0') ?};
      SYNC_ID.cntx_pop()
   ?};

   _sel:=__TAB.mk_sel(_b,,,'baslin_p_sel');
   __TAB.win_fld(_sel,,'ID',,,15,,,'Identyfikator');
   __TAB.win_fld(_sel,,_c,,,10,,,_d);
   __TAB.win_fld(_sel,,_e,,,30,,,_f);
   {? _g || __TAB.win_fld(_sel,,'DEF',,,8,,,'Domyślny',,,2,,$('BASLIN_K.%1_ID'[_a]),"\'brak\'") ?};
   _erase:='
   {? __maska=\'\'
   ||
      __maska:=exec(\'get_mask\',\'!zws_par_basl\',BASLIN_K.NAME+\'_\'+\'%1\')
   ?};
   __TAB.erase();
   SYNC_ID.cntx_psh();
   SYNC_ID.use(__maska);
   __tab_p:=sql(__sql);
   SYNC_ID.cntx_pop();
   __tab_p.for_each(\"
   __TAB.ID:=__tab_p.ID;
   __TAB.%2:=__tab_p.%2;
   __TAB.%3:=__tab_p.%3;
   __TAB.OPIS:=__tab_p.OPIS;
   __TAB.REK:=__tab_p.REK;
   __TAB.SYNC_KOD:=__tab_p.SYNC_KOD;
   __TAB.REF:=__tab_p.REF;
   __TAB.DEF:=__tab_p.DEF;
   __TAB.add()\");
   {? var_pres(\'__tab_p\')>100 || obj_del(__tab_p) ?}'[_a,_c,_e];

   _sel_p:=_tabela.mk_sel(_b,,,'baslin_p_tmp',,,,,'U');
   _tabela.win_fld(_sel_p,,_c,,,10,,,_d);
   _tabela.win_fld(_sel_p,,_e,,,30,,,_f);
::Wybierz
   _add_b:=$('
   {? (_tab_id:=exec(\'get_id\',\'#sync_id\',BASLIN_K.NAME+\'_%1\',%2); type_of(_tab_id)=type_of(~~)) |
   (type_of(_tab_id)=118 & _tab_id.size()=0)
   || exec(\'add_id\',\'#sync_id\',BASLIN_K.NAME+\'_%1\',%2,\'brak\')
   ?}'[_a,'$'+_a+'.ref()']);
   _add_b_grp:=$('
   _sel_tab:=%1.sel_aget();
   %1.sel_adel();
   %1.cntx_psh();
   %1.prefix();
   {? _sel_tab.first() & %1.seek(_sel_tab.REF)
   || {!|?
         {? (_tab_id:=exec(\'get_id\',\'#sync_id\',BASLIN_K.NAME+\'_%1\',%2); type_of(_tab_id)=type_of(~~)) |
         (type_of(_tab_id)=118 & _tab_id.size()=0)
         || exec(\'add_id\',\'#sync_id\',BASLIN_K.NAME+\'_%1\',%2,\'brak\')
         ?};
         {? var_pres(\'_tab_id\')>100 || obj_del(_tab_id) ?};
         _sel_tab.next() & %1.seek(_sel_tab.REF)
      !}
   ?};
   %1.cntx_pop();1
   '[_a,'$'+_a+'.ref()']);
   _add_a:="sel_exit()";
   _tabela.win_act(_sel_p,0,'Formuła','&Wybierz',,,_add_b,_add_a,1,1,_add_b_grp,_add_a);
   _tabela.win_sel(_sel_p);
::Dołącz
   _formula:=$('%1.select()'[_a]);
   __TAB.win_act(_sel,1,'Formuła','&Dołącz',,,_formula,$_erase,1);
   __TAB.win_act(_sel,0,'Formuła','&Dołącz',,,_formula,$_erase,1);
::Usuń
   _del_b:=$('exec(\'del_rek\',\'#sync_id\',BASLIN_K.NAME+\'_%1\',__TAB.REK)'[_a]);
   _del_a:=$_erase;
   _del_b_grp:=$('
   _sel_tab:=__TAB.sel_aget();
   __TAB.sel_adel;
   {? _sel_tab.first() & __TAB.seek(_sel_tab.REF)
   || {!|?
         {? __TAB.ID=\'brak\' | __TAB.ID=\'\'
         ||
            exec(\'del_rek\',\'#sync_id\',BASLIN_K.NAME+\'_%1\',__TAB.REK)
         ?};
         _sel_tab.next() & __TAB.seek(_sel_tab.REF)
      !}
   ?};1
   '[_a]);
   __TAB.win_act(_sel,0,'Formuła','&Usuń',,,_del_b,_del_a,,1,_del_b_grp,_del_a);
::Aktywuj
   _act_b:=$('exec(\'baslin_p_%1_akt\',\'!zws_par_basl\',1,0,\'\')'[~_a]);
   _act_a:=$_erase;
   _act_b_grp:=$('
   _sel_tab:=__TAB.sel_aget();
   __TAB.sel_adel;
   {? _sel_tab.first() & __TAB.seek(_sel_tab.REF)
   ||  exec(\'ini_kom\',\'#message\',\'Komunikaty aktywacyjne\');
      {!|?
         _wyn:=exec(\'baslin_p_%1_akt\',\'!zws_par_basl\',1,1,__TAB.%2);
         _wyn & _sel_tab.next() & __TAB.seek(_sel_tab.REF)
      !};
      exec(\'end_kom\',\'#message\')
   ?};1
   '[~_a,_c]);
   __TAB.win_act(_sel,0,'Formuła','&Aktywuj',,,_act_b,_act_a,,1,_act_b_grp,_act_a);
::Dezaktywuj
   _dact_b:=$('exec(\'baslin_p_%1_akt\',\'!zws_par_basl\',0,0,\'\')'[~_a]);
   _dact_a:=$_erase;

   _dact_b_grp:=$('
   _sel_tab:=__TAB.sel_aget();
   __TAB.sel_adel;
   {? _sel_tab.first() & __TAB.seek(_sel_tab.REF)
   ||  exec(\'ini_kom\',\'#message\',\'Komunikaty aktywacyjne\');
      {!|?
         _wyn:=exec(\'baslin_p_%1_akt\',\'!zws_par_basl\',0,1,__TAB.%2);
         _wyn & _sel_tab.next() & __TAB.seek(_sel_tab.REF)
      !};
      exec(\'end_kom\',\'#message\')
   ?};1
   '[~_a,_c]);

   __TAB.win_act(_sel,0,'Formuła','D&ezaktywuj',,,_dact_b,_dact_a,,1,_dact_b_grp,_dact_a);
::Aktualizuj
   __TAB.win_act(_sel,0,'Formuła','Ak&tualizuj',,,_act_b,_act_a,,1,_act_b_grp,_act_a);
::Domyślny
   _def_b:=$('exec(\'baslin_p_default\',\'!zws_par_basl\',\'%1\')'[_a]);
   _def_a:="__loop:=1;sel_exit()";
   {? _g || __TAB.win_act(_sel,0,'Formuła','Do&myślny',,,_def_b,_def_a) ?};
::Podrupy
   {? _a='MGR'
   ||
      _mgrp_b:="exec('baslin_p_mgrp','!zws_par_basl',0)";
      _mgrp_a:="__loop:=1;sel_exit()";
      __TAB.win_act(_sel,0,'Formuła','&Podgrupu materiałowe',,,_mgrp_b,_mgrp_a)
   ?};
   {? _a='MGR' | _a='MGRP'
   ||
      _m_b:="exec('baslin_p_m','!zws_par_basl',__TAB.REK)";
      _m_a:="__loop:=1;sel_exit()";
      __TAB.win_act(_sel,0,'Formuła','&Materiały',,,_m_b,_m_a)
   ?};
::Wyszarzanie akcji
   _rec_b:=$('{? _a & var_pres(\'__TAB\')>100
              || _actions:={? __TAB.ID<>\'\' & __TAB.ID<>\'brak\' || \'AU\' || \'ETM\' ?};
                 _actions+={? (__TAB.ID<>\'\' & __TAB.ID<>\'brak\')
                           & (__TAB.ID=BASLIN_K.MG_ID | __TAB.ID=BASLIN_K.TAR_ID)
                           || \'E\'
                           || \'\'
                           ?};
                 _actions+={? (\'%1\'=\'MGR\' | \'%1\'=\'MGRP\' | \'%1\'=\'M\') & BASLIN_K.ID=\'\'
                           || \'A\'
                           || \'\'
                           ?};
                 __TAB.actions_grayed(cur_win(1),_actions)
              ?}'[_a]);
   __TAB.win_act(_sel,0,'Rekord',,,,_rec_b);

   __TAB.win_sel(_sel);
   __TAB.first();
   __TAB.select(,1);
   __loop
!};
{? var_pres('__TAB')>100 || obj_del(__TAB) ?};
VAR_DEL.delete('__loop','__sql','__maska');
~~


\baslin_p_uni_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktywuje/dezaktywuje/aktualizuje parametr
::   WE: [_a] - 0 dezaktywacja / 1 aktywacja
::       _b - nazwa funckji aktywującej
::       _c - info 1 ('Grupa cenowa nie została dodana'@)
::       _d - info 2 ('Grupa cenowa nie została zaktualizowana'@)
::       _e - info 3 ('Grupa cenowa została dodana'@)
::       _f - info 4 ('Grupa cenowa została zaktualizowana'@)
::       _g - info 5 ('Nie udało się dezaktywować grupy cenowej.'@)
::       _h - info 6 ('Grupa cenowa została dezaktywowana'@)
::       _i - nazwa funckji dezaktywyjącej
::       _j - parametr
::       _k - funkcja grupowa
::       _l - nazwa aktywowanego rekordu
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',700808,2)='' || FUN.info('Niewypełniony parametr 700808(Token BaseLinker)'@); return('') ?};
_akt:={? var_pres('_a')=type_of(0) || _a || 1 ?};
SYNC_ID.cntx_psh();
SYNC_ID.use(ref_name(__TAB.REF));
SYNC_ID.prefix();
{? ~SYNC_ID.seek(__TAB.REF)
|| SYNC_ID.cntx_pop(); FUN.info('Nie udało się odnleźć rekordu'@); return(0)
?};
{? _akt
|| _id:={? var_pres('_j')=type_of(1) || exec(_b,'base_linker',_j) || exec(_b,'base_linker') ?};
   {? _id=''
   || {? SYNC_ID.ID='brak'
      || {? _k || exec('add_kom','#message',_l+' - '+_c,2) || FUN.info(_c) ?}
      || {? _k || exec('add_kom','#message',_l+' - '+_d,2) || FUN.info(_d) ?}
      ?}
   ||
      {? SYNC_ID.ID='brak'
      || exec('add_id','#sync_id',SYNC_ID.KOD,SYNC_ID.REK,SYNC_ID.ID,SYNC_ID.OPIS,_id);
         {? _k || exec('add_kom','#message',_l+' - '+_e,1) || FUN.info(_e) ?}
      || {? _k || exec('add_kom','#message',_l+' - '+_f,1) || FUN.info(_f) ?}
      ?}
   ?}
|| {?  exec(_i,'base_linker')
   || {? _k || exec('add_kom','#message',_l+' - '+_g,2) || FUN.info(_g) ?}
   || exec('add_id','#sync_id',SYNC_ID.KOD,SYNC_ID.REK,SYNC_ID.ID,SYNC_ID.OPIS,'brak');
      {? _k || exec('add_kom','#message',_l+' - '+_h,1) || FUN.info(_h) ?}
   ?}
?};
SYNC_ID.cntx_pop();
1


\baslin_p_tar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Cenniki kanału sprzedaży
::----------------------------------------------------------------------------------------------------------------------
ODDZ.cntx_psh();
TAR.f_set('KOD',,'TAR.ODDZ=\'%1\''[BASLIN_K.ODDZ().KOD]);
ODDZ.cntx_pop();
exec('baslin_p_uni','!zws_par_basl','TAR','Cenniki','KOD','Kod','NAZ','Nazwa',1);
TAR.f_clear()


\baslin_p_tar_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktywuje/dezaktywuje/aktualizuje grupy cenowe
::   WE: [_a] - 0 dezaktywacja / 1 aktywacja
::----------------------------------------------------------------------------------------------------------------------
exec('baslin_p_uni_akt','!zws_par_basl',
{? var_pres('_a')=type_of(0) || _a || 1 ?},
'addInventoryPriceGroup',
'Grupa cenowa nie została dodana'@,
'Grupa cenowa nie została zaktualizowana'@,
'Grupa cenowa została dodana'@,
'Grupa cenowa została zaktualizowana'@,
'Nie udało się dezaktywować grupy cenowej.'@,
'Grupa cenowa została dezaktywowana'@,
'deleteInventoryPriceGroup',
,_b, _c)


\baslin_p_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Magazyny kanału sprzedaży
::----------------------------------------------------------------------------------------------------------------------
ODDZ.cntx_psh();
MG.f_set('SYM',,'MG.ODDZ=\'%1\''[BASLIN_K.ODDZ().KOD]);
ODDZ.cntx_pop();
exec('baslin_p_uni','!zws_par_basl','MG','Magazyny','SYM','Symbol','NAZ','Nazwa',1);
MG.f_clear()


\baslin_p_mg_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktywuje/dezaktywuje/aktualizuje magazyny
::   WE: [_a] - 0 dezaktywacja / 1 aktywacja
::----------------------------------------------------------------------------------------------------------------------
exec('baslin_p_uni_akt','!zws_par_basl',
{? var_pres('_a')=type_of(0) || _a || 1 ?},
'addInventoryWarehouse',
'Magazyn nie został dodany.'@,
'Magazyn nie został zaktualizowany.'@,
'Magazyn został dodany.'@,
'Magazyn został zaktualizowany.'@,
'Nie udało się dezaktywować magazynu.'@,
'Magazyn został dezaktywowany.'@,
'deleteInventoryWarehouse',
,_b,_c)


\baslin_p_mgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Grupy materiałowe kanału sprzedaży
::----------------------------------------------------------------------------------------------------------------------
MGR.f_set('KOD',,'MGR.RODZ=\'T\'');
exec('baslin_p_uni','!zws_par_basl','MGR','Grupy materiałowe','KOD','Kod','NAZ','Nazwa',0);
MGR.f_clear()


\baslin_p_mgr_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktywuje/dezaktywuje/aktualizuje grupy materiałowe
::   WE: [_a] - 0 dezaktywacja / 1 aktywacja
::----------------------------------------------------------------------------------------------------------------------
exec('baslin_p_uni_akt','!zws_par_basl',
{? var_pres('_a')=type_of(0) || _a || 1 ?},
'addInventoryCategory',
'Grupa materiałowa nie została dodana.'@,
'Grupa materiałowa nie została zaktualizowana.'@,
'Grupa materiałowa została dodana.'@,
'Grupa materiałowa została zaktualizowana.'@,
'Nie udało się dezaktywować grupy materiałowej.'@,
'Grupa materiałowa została dezaktywowana.'@,
'deleteInventoryCategory',
,_b,_c)


\baslin_p_mgrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Podgrupy materiałowe kanału sprzedaży
::----------------------------------------------------------------------------------------------------------------------
MGRP.f_set('KOD',,'MGRP.MGR=\'%1\''[$MGR.ref()]);
exec('baslin_p_uni','!zws_par_basl','MGRP','Podrupy materiałowe','KOD','Kod','NAZ','Nazwa',0);
MGRP.f_clear()


\baslin_p_mgrp_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktywuje/dezaktywuje/aktualizuje podgrupy materiałowe
::   WE: [_a] - 0 dezaktywacja / 1 aktywacja
::----------------------------------------------------------------------------------------------------------------------
exec('baslin_p_uni_akt','!zws_par_basl',
{? var_pres('_a')=type_of(0) || _a || 1 ?},
'addInventoryCategory',
'Podgrupa materiałowa nie została dodana.'@,
'Podgrupa materiałowa nie została zaktualizowana.'@,
'Podgrupa materiałowa została dodana.'@,
'Podgrupa materiałowa została zaktualizowana.'@,
'Nie udało się dezaktywować Podgrupa materiałowej.'@,
'Podgrupa materiałowa została dezaktywowana.'@,
'deleteInventoryCategory',
1,_b, _c)


\baslin_p_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Produkty kanału sprzedaży
::----------------------------------------------------------------------------------------------------------------------
{? ref_name(_a)='matgrupy'
|| M.f_set('KTM',,'MGR=\'%1\''[_a]);
   _sql:=' where M.MGR=\'%1\''[_a]
|| M.f_set('KTM',,'MGRP=\'%1\''[_a]);
   _sql:=' where M.MGRP=\'%1\''[_a]
?};

exec('baslin_p_uni','!zws_par_basl','M','Materiały','KTM','Indeks','N','Nazwa',0,_sql);
M.f_clear()


\baslin_p_m_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktywuje/dezaktywuje/aktualizuje produkty
::   WE: [_a] - 0 dezaktywacja / 1 aktywacja
::----------------------------------------------------------------------------------------------------------------------
exec('baslin_p_uni_akt','!zws_par_basl',
{? var_pres('_a')=type_of(0) || _a || 1 ?},
'addInventoryProduct',
'Produkt nie został dodany.'@,
'Produkt nie został zaktualizowany.'@,
'Produkt został dodany.'@,
'Produkt został zaktualizowany.'@,
'Nie udało się dezaktywować produktu.'@,
'Produkt został dezaktywowany.'@,
'deleteInventoryProduct',
,
_b,
_c)


\add_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Dołączanie kanału sprzedaży
::----------------------------------------------------------------------------------------------------------------------
::Produkty
_atr:=exec('def_a','#sync_id');
_atr.SYSTEM:='BASELINKER';
_atr.OPIS:='Kartoteka materiałowa';
_atr.TABELA:='M';
_atr.KOD:=BASLIN_K.NAME+'_'+_atr.TABELA;
_atr.UZUFIRMA:='T';
exec('add_def','#sync_id',_atr);
{? var_press('_atr')>100 || obj_del(_atr) ?};

::Cenniki
_atr:=exec('def_a','#sync_id');
_atr.SYSTEM:='BASELINKER';
_atr.OPIS:='Cenniki';
_atr.TABELA:='TAR';
_atr.KOD:=BASLIN_K.NAME+'_'+_atr.TABELA;
_atr.UZUFIRMA:='T';
exec('add_def','#sync_id',_atr);
{? var_press('_atr')>100 || obj_del(_atr) ?};

::Cenniki
_atr:=exec('def_a','#sync_id');
_atr.SYSTEM:='BASELINKER';
_atr.OPIS:='Cenniki';
_atr.TABELA:='TAR';
_atr.KOD:=BASLIN_K.NAME+'_'+_atr.TABELA;
_atr.UZUFIRMA:='T';
exec('add_def','#sync_id',_atr);
{? var_press('_atr')>100 || obj_del(_atr) ?};

::Magazyny
_atr:=exec('def_a','#sync_id');
_atr.SYSTEM:='BASELINKER';
_atr.OPIS:='Magazyny';
_atr.TABELA:='MG';
_atr.KOD:=BASLIN_K.NAME+'_'+_atr.TABELA;
_atr.UZUFIRMA:='T';
exec('add_def','#sync_id',_atr);
{? var_press('_atr')>100 || obj_del(_atr) ?};

::Grupy materiałowe
_atr:=exec('def_a','#sync_id');
_atr.SYSTEM:='BASELINKER';
_atr.OPIS:='Grupy materiałowe';
_atr.TABELA:='MGR';
_atr.KOD:=BASLIN_K.NAME+'_'+_atr.TABELA;
_atr.UZUFIRMA:='T';
exec('add_def','#sync_id',_atr);
{? var_press('_atr')>100 || obj_del(_atr) ?};

::Podgrupy materiałowe
_atr:=exec('def_a','#sync_id');
_atr.SYSTEM:='BASELINKER';
_atr.OPIS:='Podgrupy materiałowe';
_atr.TABELA:='MGRP';
_atr.KOD:=BASLIN_K.NAME+'_'+_atr.TABELA;
_atr.UZUFIRMA:='T';
exec('add_def','#sync_id',_atr);
{? var_press('_atr')>100 || obj_del(_atr) ?};
~~

\get_mask
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Zwraca maskę identyfikatorów
::   WE: _a [STRING] - kod definicji
::----------------------------------------------------------------------------------------------------------------------
:: Pobieranie maski identyfikatorów:
_maska:='';
SYNC_IDM.cntx_psh();
SYNC_IDM.index('KOD');
SYNC_IDM.prefix(_a,);
{? SYNC_IDM.first()
|| _maska:=SYNC_IDM.MASKA_ID
?};
SYNC_IDM.cntx_pop();
_maska


\efak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Zwraca maskę identyfikatorów
::   WE: _a [STRING] - kod definicji
::----------------------------------------------------------------------------------------------------------------------
__choice_ef:=BASLIN_K.EFAK;

_Choice:=tab_tmp(1,'KOD','STRING[10]','Kod','O','STRING[80]','Opis');
_Choice.KOD:='N';_Choice.O:='Nie wysyłać'@; _Choice.add();
_Choice.KOD:='W'; _Choice.O:='Zawsze wysyłać'@; _Choice.add();
_Choice.KOD:='E'; _Choice.O:='Przy zgodzie na otrzymywanie dokumentów e-mailem'@; _Choice.add();
_Choice.KOD:='B'; _Choice.O:='Przy zgodzie na otrzymywanie dokumentów w usłudze Businesslink'@; _Choice.add();
_Choice.KOD:='EB'; _Choice.O:='Przy zgodzie na otrzymywanie dokumentów e-mailem lub poprzez Businesslink'@; _Choice.add();

_wer:=_Choice.mk_sel('Przesyłanie e-faktury'@);
_Choice.win_fld(_wer,,'KOD',,,5,,,'Akronim'@);
_Choice.win_fld(_wer,,'O',,,80,,,'Opis'@);

_fb:="
   _Tab:=cur_tab();
   _Sel:=_Tab.sel_aget();_Sel.REF:=#_Tab.ref(); _Sel.add();
   {? _Sel.first() & _Tab.seek(_Sel.REF,) & _Tab.KOD<>'' || __choice_ef:=_Tab.KOD ?};
   sel_exit();
   0
";
_Choice.win_act(_wer,,'Formuła','&Akceptuj'@@,,,_fb,,1,1,_fb,,'A');
_Choice.win_sel(_wer);
_Choice.select();
BASLIN_K.EFAK:=__choice_ef;BASLIN_K.put();
VAR_DEL.delete('__choice_ef');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 09f6d431ffecbbaf1d148e0f3431896509ba688c8e117ec0c2845a72f51e2e3827b91d011ea8d8800888a8e8ef9651e53a08fcd1d4c8afae1b173569be71a5d1f5b237ed4d143fa188903cb2b9ee206a60aa5804870263eb97e47e1acc3ddaa4a2448965c6f0b02c2de95d79c04921ec5475dc467a175a62d232d5bf45802077

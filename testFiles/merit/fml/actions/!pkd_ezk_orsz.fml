:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orsz.fml
:: Utworzony: 06.06.2016
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_EZK_ORSZ - Rejestracja szkoleń dla pracowników
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Rejestracja szkoleń odbytych przez pracowników - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_ezk_orsz')
|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@[_mp.buf_act.UID,_do]
   ?}
|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orsz',_in.P);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Rejestracja szkoleń dla pracowników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Rejestracja szkoleń dla pracowników: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Rejestracja szkoleń dla pracowników: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Rejestracja szkoleń dla pracowników: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Rejestracja szkoleń dla pracowników'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Komunikat o błędzie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie szkolenia dla pracownika niemożliwe.'@+'\n'+'Nie znaleziono pracownika.'@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie pracownika.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::  OLD: \sel_szko/szk_prac.fml
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a)
|| SZK_OPIS.cntx_psh();
   SZK_OPIS.win_edit('WER');
   SZK_PRAC.cntx_psh();
   SZK_PRAC.win_sel(exec('wnd','!pkd_ezk_orsz'));
   SZK_PRAC.index('PRAC_SZK');
   SZK_PRAC.prefix(P.ref());
   SZK_PRAC.win_edit('RED');
   SZK_PRAC.fld_fml('P','BLANK',"P.ref()");
   SZK_PRAC.fld_fml('SZKOL','BLANK',"null()");
   SZK_PRAC.efld_opt('RED','enable=1',,'OBECNY');
   SZK_PRAC.efld_opt('RED','enable=0',,'PRC');
   SZK_PRAC.efld_opt('RED','enable=0',,'KOSZT');
   SZK_PRAC.efld_opt('RED','enable=0',,'LISTA');
   SZK_PRAC.fld_fml(
      'SZKOL',
      'AFTER_EDIT',
      "  SZK_PRAC.efld_opt(\'RED\',\'enable=\'+$(SZK_PRAC.SZKOL<>null()),,\'OBECNY\');
         SZK_PRAC.efld_opt(\'RED\',\'enable=\'+$(SZK_PRAC.SZKOL<>null()),,\'PRC\');
         SZK_PRAC.efld_opt(\'RED\',\'enable=\'+$(SZK_PRAC.SZKOL<>null()),,\'KOSZT\');
         SZK_PRAC.efld_opt(\'RED\',\'enable=\'+$(SZK_PRAC.SZKOL<>null() & SZK_PRAC.KOSZT),,\'LISTA\')
      "
   );
   SZK_PRAC.hdr_sel(': '+@.P.OSOBA().NAZWISKO+' '+@.OSOBA.PIERWSZE);
   _ret:=SZK_PRAC.select(,1);
   SZK_PRAC.hdr_sel();
   SZK_PRAC.fld_fml('P','BLANK',"*");
   SZK_PRAC.fld_fml('SZKOL','BLANK',"*");
   SZK_PRAC.fld_fml('SZKOL','AFTER_EDIT',"*");
   SZK_PRAC.cntx_pop();
   SZK_OPIS.cntx_pop()
|| _ret:=exec('error','!pkd_ezk_orsz')
?};
P.cntx_pop();
_ret


\szk_prac_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana po wywołaniu z listy zadań i w ramach obszaru roboczego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=SZK_PRAC
|| sel_exit()
|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przygotowanie okna wertowania ze szkoleń pracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized';
_czyZ:=exec('dest','f_zatr',P.F_ZATR,'KOD')='Z';
_before:={? _czyZ|| "mb_exec('GRP_EDISP_P_INFO_Z')"|| "mb_exec('GRP_EDISP_P_INFO_P')"?};
_wnd:=SZK_PRAC.grp_make('Szkolenia pracownika',_before,'szk_prac_gwer',,,,,_mode);
SZK_PRAC.grp_edit(_wnd,P,
   {? _czyZ || 'INFO_Z' || 'INFO_P' ?},,
   "  win_activate('szkolenia');
      grp_disp(SZK_PRAC,\'WER\')
   ",,,,_mode
);
SZK_PRAC.grp_splt(_wnd,,'horizontal','szk_prac');
SZK_PRAC.grp_sel(_wnd,,'WER',,,,,,,,,,_mode,'szkolenia');
_wnd

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 7fe8a63c59d644f2893de997c0a4206f23fe86170ed427e544fc8c1af9cdd1f1f7425e150d17c80df7f8572c4822b9a575fdef6874787021207f9c4508e97bef32ac48267d16f015c7806d2183b38491272587f17a131e8a42c9ff32ce53c93494867cba2e38c293e7d362fe04cb0b4b6f926fb8b87bd4dfd0b9d815f9e29013

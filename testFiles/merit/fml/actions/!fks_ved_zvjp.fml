:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ved_zvjp.fml
:: Utworzony: 03.08.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VED_ZVJP - Przeglądanie deklaracji JPK_V7
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Przeglądanie deklaracji JPK_V7 - główna formuła czynności FKS_VED_ZVJP.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Dodanie zakładki do okna grupowego obszaru deklaracji podatkowych (FKS_VED)
::   WE: _a - nazwa zakładki
::       _b - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
VAT_DEK.grp_sel(_b,,'VAT_V7',_a,"",0,0,,"exec('bw','!fks_ved_zvjp')","",,,'maximized_with_title');
exec('task_attach','fks_ved','J')


\bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed obsługą okien okna grupowego obszaru FKS_VED
::----------------------------------------------------------------------------------------------------------------------
rodzdekl:={? SIK.ROZL_VAT='K' || 'J' || 'j' ?};
VAT_DEK.index('VAT_DEKR');
VAT_DEK.prefix(rodzdekl,SIK.ROK1().POCZ_ROK);
vat_typ:={? SIK.ROZL_VAT='K' || 'V7K' || 'V7M' ?};
VAT_DEK.win_sel('VAT_V7');
VAT_DEK.hdr_sel();
{? rodzdekl='j'
|| VAT_DEK.hdr_sel(' JPK_V7M'@);
   SKID.TYP_ZAP:=SKID.DEKL_NAZ:='V7M'
|? rodzdekl='J'
|| VAT_DEK.hdr_sel(' JPK_V7K'@);
   SKID.TYP_ZAP:=SKID.DEKL_NAZ:='V7K'
?};
VAT_DEK.hdr_sel(' z roku %1'@[ROK_F.NAZ])


\vat_dwy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formula Wydruk dla okienka wert. tabeli VAT_DEK
::  OLD: \vat_dwy/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? vat_typ='VAT7'
|| _wer:=exec('wer_pop','fks_ved');
   {? _wer=1
   || exec('rep_exec','#b_report','FKS_VED_ZVJP','fks_dek_'+(-vat_typ),,,,,,,,'W')
   |? _wer<6
   || exec('rep_exec','#b_report','FKS_VED_ZVJP','fks_dk'+form(_wer,-2)+(-vat_typ),,,,,,,,'W')
   || _v1:=exec('vat_zd_size','fks_ved');
      _v2:=VAT_DEK.ZAL3;
      {? _v1+_v2>0
      || wer:=_wer;
         _popup:=
            'popup(0,\'Wydruk deklaracji\','+
            '\'A. Deklaracja VAT-7\',\'Wydruk deklaracji VAT-7\',\"'+
            '   {? exec(\'rpm_as_pdf\',\'rpm_pdf\') & wer>6'+
            '   || exec(\'drukuj\',\'fks_ved\',\'fks_dk\'+form(wer,-2)+\'pv7\')'+
            '   || exec(\'drukuj\',\'fks_ved\',\'fks_dk\'+form(wer,-2)+(-vat_typ))'+
            '   ?}'+
            '   \"';
         {? _v1
         || _popup+=','+
            '\'B. Załącznik VAT-ZD (strona 1)\',\'Wydruk załącznika VAT-ZD (strona 1)\',"exec(\'vat_zd\',\'!fks_ved_zv7p\',1)",'+
            '\'C. Załącznik VAT-ZD (strona 2)\',\'Wydruk załącznika VAT-ZD (strona 2)\',"exec(\'vat_zd\',\'!fks_ved_zv7p\',2)"'
         ?};
         {? _v2
         || _popup+=','+
            '\'D. Załącznik ORD-ZU\',\'Wydruk uzasadnienia korekty ORD-ZU\',"{? exec(\'rpm_as_pdf\',\'rpm_pdf\') || exec(\'ord_zu\',\'!fks_ved_zv7p\') || exec(\'rep_exec\',\'#b_report\',\'FKS_VED_ZV7P\',\'fks_ord_zuv7\',,,,,,,,\'W\') ?}"'
         ?};
         _popup+=')';
         ($_popup)();
         VAR_DEL.delete('wer')
      || {? exec('rpm_as_pdf','rpm_pdf') & _wer>6
         || exec('drukuj','fks_ved','fks_dk'+form(_wer,-2)+'pv7')
         || exec('drukuj','fks_ved','fks_dk'+form(_wer,-2)+(-vat_typ))
         ?}
      ?}
   ?}
|? vat_typ='VAT7D'
|| _wer:=exec('wer_pop','fks_ved');
   {? _wer=1
   || exec('rep_exec','#b_report','FKS_VED_ZVJP','fks_dek_v7d',,,,,,,,'W')
   |? _wer<4
   || exec('rep_exec','#b_report','FKS_VED_ZVJP','fks_dd'+form(_wer,-2)+'v7d',,,,,,,,'W')
   || _v1:=exec('vat_zd_size','fks_ved');
      _v2:=VAT_DEK.ZAL3;
      wer:=_wer;
      {? _v1+_v2>0
      || wer:=_wer;
         _popup:=
            'popup(0,\'Wydruk deklaracji\','+
            '\'A. Deklaracja VAT-7D\',\'Wydruk deklaracji VAT-7D\',\"'+
            '   {? exec(\'rpm_as_pdf\',\'rpm_pdf\') & wer>4 '+
            '   || exec(\'drukuj\',\'fks_ved\',\'fks_dd\'+form(wer,-2)+\'pv7d\') '+
            '   || exec(\'drukuj\',\'fks_ved\',\'fks_dd\'+form(wer,-2)+\'v7d\')  '+
            '   ?}'+
            '   "';
         {? _v1
         || _popup+=','+
            '\'B. Załącznik VAT-ZD (strona 1)\',\'Wydruk załącznika VAT-ZD (strona 1)\',\"exec(\'vat_zd\',\'!fks_ved_zv7p\',1)\",'+
            '\'C. Załącznik VAT-ZD (strona 2)\',\'Wydruk załącznika VAT-ZD (strona 2)\',\"exec(\'vat_zd\',\'!fks_ved_zv7p\',2)\"'
         ?};
         {? _v2
         || _popup+=','+
            '\'D. Załącznik ORD-ZU\',\'Wydruk uzasadnienia korekty ORD-ZU\',"{? exec(\'rpm_as_pdf\',\'rpm_pdf\') || exec(\'ord_zu\',\'!fks_ved_zv7p\') || exec(\'rep_exec\',\'#b_report\',\'FKS_VED_ZV7P\',\'fks_ord_zuv7\',,,,,,,,\'W\') ?}"'
         ?};
         _popup+=')';
         ($_popup)();
         VAR_DEL.delete('wer')
      || {? exec('rpm_as_pdf','rpm_pdf') & _wer>4
         || exec('drukuj','fks_ved','fks_dd'+form(_wer,-2)+'pv7d')
         || exec('drukuj','fks_ved','fks_dd'+form(_wer,-2)+'v7d')
         ?}
      ?}
   ?}
?}


\vat_zd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Wydruk deklaracji VAT-ZD
::  OLD: \vat_zd/vat.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_PS.cntx_psh();
VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
VAT_PS.index('TYP'); VAT_PS.prefix(VAT_DEK.ref(),'N');
next:=VAT_PS.first();
lvatzd:=VAT_PS.size();
lvatzd:=lvatzd%8+(lvatzd%*8>0);
nr_zal:=1;
dmies:=1+(VAT_DEK.OKRES+3)='/';
{? exec('rpm_as_pdf','rpm_pdf')
|| exec('rep_exec','#b_report','FKS_VED_ZVJP','fks_dvzdp'+$_a,,,,,,,,'W')
|| exec('rep_exec','#b_report','FKS_VED_ZVJP','fks_dvzd'+$_a,,,,,,,,'W')
?};
VAT_PS.cntx_pop();
VAR_DEL.delete('lvatzd','next','lvatzd','nr_zal','dmies')


\vat_ps_wydruk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB  [2011]
:: OPIS: Wydruk wykazu powiazan pozycji deklaracji z dokumentami VAT
::  OLD: \vat_ps_wydruk/vat.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FKS_VED_ZVJP','fks_ps_vat',,,,,,,,'W')


\chk_sik_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ  [12.41]
:: OPIS: Sprawdzenie czy dla wybranego roku bylo ustawone choc raz wybrane rozliczenie VAT
::  OLD: \chk_sik_rozl/skid_sik.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('chk_sik_rozl','fks_ved')
|| rodzdekl:={? SIK.ROZL_VAT='K' || 'J' || 'j' ?};
   ''
|| 'ROZL_VAT'
?}


\vat7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Przed wydrukiem deklaracji przepisanie wartosci z tabeli VAT_POZ do tablicy pv
::  OLD: \vat7/nvat7.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_POZ.index('VAT_POZ');
VAT_POZ.prefix(VAT_DEK.ref());
{? VAT_POZ.first()
|| {!
   |? {? VAT_POZ.POZ1<>0 || pv[VAT_POZ.POZ1]:=VAT_POZ.NETTO ?};
      {? VAT_POZ.POZ2<>0 || pv[VAT_POZ.POZ2]:=VAT_POZ.VAT ?};
      VAT_POZ.next()
   !}
?}


\be_zwl_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Przed wyswietleniem pola Liczba dni zwłoki w deklaracji VAT
::  OLD: \be_zwl_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
(VAT_PS.REJ_VAT='' ) &
~(VAT_PS.REJ_VAT='' &  VAT_PS.ZWL=-1)


\vat_dek_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja pliki JPK dla okien deklaracji VAT
::  OLD: \vat_dek_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.win_sel('W_JPK');
ISTDEF.win_edit('R_JPK');
ISTDEF.actions('W_JPK','dpUTHOEIA:dAI');
_typ:=3+VAT_DEK.TYP;
JPK.index('DODAT'); JPK.prefix(_typ,$VAT_DEK.ref(),);
JPK.win_sel('WER_V7');
_actions:='CPZ:CZ';
{? 2+VAT_DEK.TYP<>'V7'
|| _actions:='DUATW'+_actions+'D'
|| _actions:='E'+_actions
?};
VAT_DEK.cntx_psh();
JPK.select(,,,_actions);
VAT_DEK.cntx_pop();
ISTDEF.actions('W_JPK')


\bv_vat_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietleniem HELPJPK.VAT_JPK
::  OLD: \bv_vat_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.VAT_JPK:={? exec('jpk4vat','jpk') || 'T' || 'N' ?}


\vat_ps_rp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB  [2011]
:: OPIS: Formula rekord przed dla tabeli VAT_PS
::  OLD: \vat_ps_rp/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_POZ.TYP='A' & exec('vat_kor_on','fks_ved') & (-VAT_PS.TYP='z' | -VAT_PS.TYP='n' | -VAT_PS.TYP='a' | -VAT_PS.TYP='r')
|| _grayed:=''
|? -VAT_PS.TYP='a' | -VAT_PS.TYP='r'
|| _grayed:='A:A'
|? exec('vat_kor_on','fks_ved')
|| _grayed:='PU'
|| _grayed:='ADPU:DA'
?};
VAT_PS.actions_grayed('WER',_grayed);
{? VAT_PS.PVAT_REF<>''
|| {? 4+VAT_PS.PVAT_REF='pvat'
   || PVAT.cntx_psh(); DVAT.cntx_psh();
      PVAT.use(VAT_PS.PVAT_REF-8);
      DVAT.use('dvat'+(PVAT.name()+4));
      PVAT.prefix();
      {? PVAT.seek(BB.sqlint(VAT_PS.PVAT_REF),PVAT.name()) & VAT_PS.SHA=exec('sha1','#to_sha1','PVAT',2+VAT_PS.SHA) &
         PVAT.DVAT().NR=VAT_PS.NR_VAT
      || _wynik:=''
      || _wynik:='VAT_PS#01#01'
      ?};
      PVAT.cntx_pop(); DVAT.cntx_pop()
   || VPOZ.cntx_psh();
      VPOZ.use(VAT_PS.PVAT_REF-8);
      VPOZ.prefix();
      {? VPOZ.seek(BB.sqlint(VAT_PS.PVAT_REF),VPOZ.name()) & VAT_PS.SHA=exec('sha1','#to_sha1','VPOZ',2+VAT_PS.SHA)
      || _wynik:='VAT_PS#01#02'
      || _wynik:='VAT_PS#01#01'
      ?};
      VPOZ.cntx_pop()
   ?};
   _wynik
|? VAT_PS.TYP='J'
|| 'VAT_PS#01#04'
|? VAT_PS.REJ_KS=null
|| 'VAT_PS#01#03'
|| 'VAT_PS#01#01'
?}


\br_vat_dek7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Rekord przed okna VAT_V7 tabeli VAT_DEK
::----------------------------------------------------------------------------------------------------------------------
exec('br_vat','fks_ved','VAT_V7');
''

\vat_dekz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [8.50]
:: OPIS: Formula dla zmiany zakresu przy przegladaniu deklaracji podatkowych
::  OLD: \vat_dekz/vat.fml
::  OLD: \vat_dekz/!fks_ved_zv7p.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('OknROKE2');
exec('okno_rok2','fks_ved');
SIK.win_edit(OknROKE2);
{? SIK.edit("{? SIK.ROK1=null
             || FUN.info('Należy wybrać rok bilansowy.'@);
                0
             || exec('chk_sik_rozl','!fks_ved_zvjp')
             ?}")
|| VAT_DEK.hdr_sel();
   {? rodzdekl='j'
   || vat_typ:='V7M'; VAT_DEK.hdr_sel(' JPK_V7M'); SKID.TYP_ZAP:=SKID.DEKL_NAZ:='V7M'
   |? rodzdekl='J'
   || vat_typ:='V7K'; VAT_DEK.hdr_sel(' JPK_V7K'); SKID.TYP_ZAP:=SKID.DEKL_NAZ:='V7K'
   ?};
   VAT_DEK.index('VAT_DEKR');
   {? SIK.ROK1=null
   || VAT_DEK.hdr_sel(' ze wszystkich lat'@);
      VAT_DEK.prefix(rodzdekl)
   || VAT_DEK.hdr_sel(' z roku %1'@[SIK.ROK1().NAZ]);
      VAT_DEK.prefix(rodzdekl,ROK_F.POCZ_ROK)
   ?};
   AreaTitle.setTitle()
?};
SIK.win_edit();
VAR_DEL.delete('OknROKE2');
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 03e356de4cafce9ff7db8de5f60001aeec288902baea66f747cd9824c23fe68d535fb362f30db63269669274c73c5c4db371b0e0021b24718fcda09d47126916ed5205cb5cd6f3b2413f32bf12d5f583c811f9e5b7614deba3334788dd9d822aaeaaa830c01c143450386abad22f06ffce6c24d8e593df65b61f464dde8f2fee

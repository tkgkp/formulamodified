:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_wgrp.fml
:: Utworzony: 17.05.2021
:: Autor: AFI
::======================================================================================================================
:: Zawartość: Grupy wniosków i delegacji w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Formuła główna czynności ZWS_PAR_WGRP
::----------------------------------------------------------------------------------------------------------------------
ETYP_GRP.cntx_psh();
ETYP_GRP.index('N'); ETYP_GRP.prefix();
ETYP_GRP.win_sel('WERW');
ETYP_GRP.select();
ETYP_GRP.cntx_pop()


\add_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Dołącz grupę dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
ETYP_GRP.win_edit('REDW');
ETYP_GRP.blank();
{? ETYP_GRP.edit("exec('valid_grp','!zws_par_wgrp')") || ETYP_GRP.add() ?}


\valid_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Walidacja po redakcji grupy
::----------------------------------------------------------------------------------------------------------------------
_dalej:='';
{? ETYP_GRP.N=''
|| FUN.info('Nie wprowadzono skrótu.'@); _dalej:='N'
?};
{? ETYP_GRP.NAZ='' & _dalej=''
|| FUN.info('Nie wprowadzono nazwy grupy.'@); _dalej:='NAZ'
?};
{? _dalej=''
|| _popraw:=(-menu_txt()='popraw');
   {? _popraw || _ref:=ETYP_GRP.ref() ?};
   ETYP_GRP.cntx_psh();
   ETYP_GRP.index('N'); ETYP_GRP.prefix(REF.FIRMA,ETYP_GRP.N,ETYP_GRP.N);
   {? ETYP_GRP.first() & {? _popraw || ETYP_GRP.ref()<>_ref || 1 ?}
   || FUN.info('Istnieje już skrót o podanej nazwie.'@); _dalej:='N'
   ?};
   ETYP_GRP.cntx_pop()
?};
_dalej


\edt_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Edycja grupy
::----------------------------------------------------------------------------------------------------------------------
ETYP_GRP.win_edit('REDW');
{? ETYP_GRP.edit("exec('valid_grp','!zws_par_wgrp')") || ETYP_GRP.put() ?}


\del_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Usuwanie grupy
::----------------------------------------------------------------------------------------------------------------------
{? ETYP_GRP.count()<>0
|| FUN.info('Grupa wykorzystywana w systemie. Usunięcie niemożliwe.'@)
|? FUN.ask('Usunąć grupę?'@)
|| ETYP_GRP.del()
?}

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:03 2bdb2939b530ebdff2fa7d2d42b83deeaacec766956d3de17415ef6b8eefd0b6ff0260fbbf104eb6930561d730e47d222f4a0b4e277186ae31d3d6a74a5e0223bdfe03138f6b2407f4d714502017b959c7eb3ceb586f3d6585a04e6e78266d2b185ad863a64922e839a6c96727a18dab2f00805672272d716288415413cfe44e

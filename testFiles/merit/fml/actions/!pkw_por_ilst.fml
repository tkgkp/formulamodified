:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkw_por_ilst.fml
:: Utworzony: 13.06.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności webTerm-owej PKW_POR_ILST - Przeglądanie list płac.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przeglądanie list płac (webTerm) - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pkw_por__lst','pkw_por')


\o_p_ctrl_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Inicjalizacja kontrolki.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
web_ctrl_init('',,'MCL.controls.HTML');
web_ctrl_call('O_P_CTRL',,'hideBtn','Drukuj')


\o_p_web_ctrl_o_p_ctrl_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: "Przed obsługą" okna CTRL tabeli O_P w ramach okna grupowego WEB_CTRL tabeli O_P.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
web_ctrl_call('O_P_CTRL',,'hideBtn','Drukuj');
web_ctrl_call('O_P_CTRL',,'setBody','')


\o_p_wyswietl_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl" (składniki wybranej listy płac) w oknie WEB_WER tabeli O_P.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

{? (_lt:=O_P.O().LT)<>''
|| exec('set_env','pkw_por');
   O_P.P().OSOBA();
   VAR.JESTLIST:=1;
   exec('czytaj','#stalesys',,KST_PAR,'NAROD');
   exec('wybierz','lista_plac',O.ref());
   web_ctrl_call('O_P_CTRL',,'showBtn','Drukuj');
   web_ctrl_call('O_P_CTRL',,'setBody',exec('html','!pkw_por_ilst'))
?}


\html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Lista płac (pasek) w postaci HTML.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FUNKCJE.TBEGIN();
LISTA.Update();

FUNKCJE.TPRAC();

_maxr:=FUNKCJE.TROWS('Prac');
_maxc:=FUNKCJE.TCOLUMNS();

_fel:=
   "  _col:=_a;
      _row:=_b;
      _style:={? var_pres('_c')=type_of('') || _c || '' ?};
      {? +(_opis:=FUNKCJE.TPRAC(_col,_row,2)) & FUNKCJE.TPRAC(_col,_row,1)
      || '<td class=\"'+_style+'\">'+_opis+'</td>'+
         '<td class=\"lskwota '+_style+'\">'+form(FUNKCJE.TPRAC(_col,_row,3),,2)+'</td>'
      || '<td></td><td></td>'
      ?}
   ";

_ret:=
   '<style type="text/css">\n'
   ' .lskwota { text-align: right; background-color: rgb(222,222,222); }\n'
   ' .lsbold { font-weight: bold; }\n'
   '</style>\n'
;

::_ret+='<img align="right" src="https://cas.macrologic.pl/cas/css/../images/macrologic.png">\n';

_ret+='Lista płac: '@+(~O.LT)+' - '+O.T().T+' '+date(O.R,O.M,1)$8+' r.\n<br>'@;
{? O.D<>date(0,0,0)
|| _ret+='Wypłacona dnia '@+O.D$1
?};
{? {? O.DKURS<>date(0,0,0)
   || exec('h_find','pracownik');
      H.WAL();
      H.CZYWAL='T' | H.CZYWAL2='T' | H.CZYWAL3='T'
   ?}
|| _ret+=
      '<br> Kurs z dnia '@+$O.DKURS+': '+$exec('waluta_miara','lista_licz',SLO.KOD)+' '+
      SLO.KOD+' = '+$exec('waluta_kurs','lista_licz',H.ZWAL,O.DKURS,H.WAL)+' PLN'
?};
_ret+='<br>';

_ret+='<table class="ls">\n';
:: Składniki
{! _row:=1 .. _maxr
|! _ret+='<tr>';
   {! _col:=1 .. _maxc
   |! _ret+=_fel(_col,_row)
   !};
   _ret+='</tr>\n'
!};
:: Podsumowanie
_ret+='<tr>';
{! _col:=1 .. _maxc
|! _ret+=_fel(_col,0,'lsbold')
!};
_ret+='</tr>\n';
_ret+='</table>\n';
_ret+='<br><hr>';


FUNKCJE.TEND();

_ret

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:40 a879faf53e70c34b44087a3aba56da3f5850ac80a242f40eacbe15c5bee4ab359d102c669a3b931c8d4718bf2585dd20ddc11211f3d348d8a6b316ee790a0119fa66abf0e1509349c3c2fbe703e18f066eb4f1a922ab085f5f1c36ce912d6ce4b9f5d4eff14ecbacc76e55c6ca08afe7d97335ff6a19b65a1fd388eec3f959c8

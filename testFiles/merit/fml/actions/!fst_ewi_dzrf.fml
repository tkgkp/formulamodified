:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_dzrf.fml
:: Utworzony: 27.01.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DZRF - Rejest. dofinansowań  środków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Rejest. dofinansowań  środków - główna formuła czynności FST_EWI_DZRF.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\srzd_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Źródła dofinansowań do środków
::----------------------------------------------------------------------------------------------------------------------
SRZD.win_sel('WER');
SRZD.win_edit('RED');
SRZD.select()


\srzd_rec_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Tabela SRZD, okno wertowania WER, formuła na rekord po
::----------------------------------------------------------------------------------------------------------------------
SRZD.PKD:=SRZD.PKD$FINFO.DOKL;
_wy:=__CHK.record(SRZD,,'NAZWA');
{? _wy='' & SRZD.PKD<0
|| FUN.emsg('Planowana kwota dofinansowania powinna być większa od zera.'@);
   _wy:='PKD'
?};
{? _wy='' & SRZD.DATA=date(0,0,0)
|| FUN.emsg('Należy podać datę udostępnienia środków.'@);
   _wy:='DATA'
?};
{? _wy='' & SRZD.PKD<SRZD.WKD
|| FUN.emsg('Planowana kwota dofinansowania nie może być mniejsza od kwoty już wykorzytanej.'@);
   _wy:='PKD'
?};
{? _wy=''
|| {? -menu_txt()='popraw' || _ref:=SRZD.ref() || _ref:=null ?};
   _nazwa:=SRZD.NAZWA;
   SRZD.cntx_psh();
   SRZD.index('SRZD');
   SRZD.prefix(_nazwa,);
   {? SRZD.first() & (_ref=null | (_ref<>null & _ref<>SRZD.ref()))
   || FUN.emsg('Źródło finansowania o podanej nazwie już istnieje.'@);
      _wy:='NAZWA'
   ?};
   SRZD.cntx_pop()
?};
_wy


\srzd_del_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed usunięciem źródła dofinansowania (RDZ
::----------------------------------------------------------------------------------------------------------------------
{? SRZD.count()>0
|| FUN.emsg('Źródło finansowania jest wykorzystywane w systemie i nie można go usunąć.'@);
   0
|| 1
?}


\srzd_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed Dołącz, Popraw dla SRZD
::----------------------------------------------------------------------------------------------------------------------
SRZD.win_edit('RED');
{? FINFO.TOR_D='T'
|| SRZD.efld_opt('RED', 'enable=1',, 'KONDOD');
   SRZD.efld_opt('RED', 'enable=1',, 'ZK_DOD')
|| SRZD.efld_opt('RED', 'enable=0',, 'KONDOD');
   SRZD.efld_opt('RED', 'enable=0',, 'ZK_DOD')
?};
1


\srzd_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła odświeża wartość wykorzystanej kwoty dofinansowania w źródle finansowania
::   WE: _a - wskazanie na źródło finansowania do odświeżenia
::----------------------------------------------------------------------------------------------------------------------
{? _=0 | type_of(_a)<>type_of(null) || return ?};
SRZD.cntx_psh(); SRZF.cntx_psh();
SRZD.prefix();
{? SRZD.seek(_a,SRZD.name())
|| _kwota:=0;
   SRZF.index('SRZD');
   SRZF.prefix(SRZD.ref());
   {? SRZF.first()
   || {! |?
         _kwota+=SRZF.WARP_P;
         SRZF.next()
      !}
   ?};
   SRZD.WKD:=_kwota;
   SRZD.put()
?};
SRZD.cntx_pop(); SRZF.cntx_pop()


\srzf_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla dofinansowania do bieżącego środka
::----------------------------------------------------------------------------------------------------------------------
{? SRST.GRP='T'
|| FUN.emsg('W przypadku zestawów środków dofinansowania muszą być przypisane do elementów składowych zestawu'@);
   return()
?};
SRZD.win_sel('WER');
SRZD.win_dict('WER');
SRZF.win_sel('WER');
SRZF.win_edit('RED');
SRZF.index('SRODEKU');
SRZF.prefix(SRSR.ref(),'D');
EDIT_ES.R_DOFIN:='S';
SRZF.select()


\srzf_select_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla dofinansowania do bieżącego dokumentu środka
::----------------------------------------------------------------------------------------------------------------------
{? ~(SRDO.RODZ='W' & SRDO.TYP().P='+')
|| FUN.emsg('Funkcja dotyczy tylko dokumentów przychodowych.'@);
   return()
?};
SRZD.win_sel('WER');
SRZD.win_dict('WER');
SRZF.win_sel('WER');
SRZF.win_edit('RED');
SRZF.index('DOKUMENT');
SRZF.prefix(SRDO.ref());
EDIT_ES.R_DOFIN:='D';
SRZF.select()


\srzf_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła dodaje nowe dofinansowanie dla środka
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('okr_mod','fst') || return() ?};
{? SRST.SRSR().Z='T'
|| FUN.emsg('Środek zlikwidowany, dołączanie dofinansowań nie jest możliwe.'@);
   return(0)
?};
{? SRSR.r_lock(1,1,1)
|| SRZF.win_edit('RED');
   SRZD.win_edit('RED');
   {? FINFO.TOR_D='T'
   || SRZF.efld_opt('RED', 'enable=1',,'KONDOD');
      SRZF.efld_opt('RED', 'enable=1',,'WARD')
   || SRZF.efld_opt('RED', 'enable=0',,'KONDOD');
      SRZF.efld_opt('RED', 'enable=0',,'WARD')
   ?};
   SRZF.blank();
   SRZF.DATA:={? SSTALE.AO().POCZ<SRST.SRSR().DE
              || SRST.SRSR().DE
              || SSTALE.AO().POCZ
              ?};
   SRZF.SRSR:=SRSR.ref();
   {? EDIT_ES.R_DOFIN='D' || SRZF.SRDO:=SRDO.ref() ?};
   SSTALE.AR();
   {? SRZF.edit("exec('chk_srzf','!fst_ewi_dzrf','ADD')")
   || {? SRZF.add()
      || SRD.updateRecState(SRZF.OKRO_F,1);
         exec('srzd_update','!fst_ewi_dzrf',SRZF.SRZD)
      ?}
   ?};
   SRSR.r_unlock()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?}


\srzf_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja bieżącego dofinansowania do środka
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('okr_mod','fst') || return() ?};
{? exec('test_nal_amo','fst',SRZF.SRSR,SRZF.OKRO_F().RES, SRZF.OKRO_F().OES)
|| FUN.emsg('Dla środka naliczono już amortyzację w okresach, w których obowiązuje dofinansowanie. '
            'Aby zmodyfikować dofinansowanie należy usunąć naliczoną amortyzację.'@);
   return()
?};
{? SRSR.r_lock(1,1,1)
|| {? SRZF.r_lock(1,1,1)
   || SRZF.win_edit('RED');
      {? FINFO.TOR_D='T'
      || SRZF.efld_opt('RED', 'enable=1',,'KONDOD');
         SRZF.efld_opt('RED', 'enable=1',,'WARD')
      || SRZF.efld_opt('RED', 'enable=0',,'KONDOD');
         SRZF.efld_opt('RED', 'enable=0',,'WARD')
      ?};
      SRZF.ROK_F();
      _p_okr:=SRZF.OKRO_F;
      _p_okr_od:=SRZF.OKRO_F().POCZ;
      {? SRZF.edit("exec('chk_srzf','!fst_ewi_dzrf','EDIT')")
      || {? SRZF.put()
         || {? SRZF.OKRO_F<>_p_okr & _p_okr_od<SRZF.OKRO_F().POCZ
            || SRD.updateRecState(_p_okr,1)
            || SRD.updateRecState(SRZF.OKRO_F,1)
            ?};
            exec('srzd_update','!fst_ewi_dzrf',SRZF.SRZD)
         ?}
      ?};
      SRZF.r_unlock()
   || FUN.emsg('Dofinansowanie zablokowane przez innego użytkownika.'@)
   ?};
   SRSR.r_unlock()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?}


\srzf_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła usuwa bieżące dofinansowanie do środka
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('okr_mod','fst') || return() ?};
{? exec('test_nal_amo','fst',SRZF.SRSR,SRZF.OKRO_F().RES, SRZF.OKRO_F().OES)
|| FUN.emsg('Dla środka naliczono już amortyzację w okresach, w których obowiązuje dofinansowanie. '
            'Aby usunąć dofinansowanie należy usunąć naliczoną amortyzację.'@);
   return()
?};
:: weryfikacja czy sa dokumenty zmieniajace dofinansowania
SRDO.cntx_psh();
SRDO.index('SRODZAJD');
SRDO.prefix(SRZF.SRSR,'D',SRZF.ref());
{? SRDO.first() || _doc:=1 || _doc:=0 ?};
SRDO.cntx_pop();
{? _doc
|| FUN.emsg('Wskazane dofinansowanie posiada co najmniej jeden dokument zmiany właściwości.\n'
            'Aby usunąć dofinansowanie należy najpierw usunąć powiązane dokumenty.'@);
   return()
?};

{? SRSR.r_lock(1,1,1)
|| {? SRZF.r_lock(1,1,1)
   || {? FUN.ask('Usunąć dofinansowanie?'@)
      || SRZF.r_unlock();
         _wy:=0;
         do();
            {? SRD.delValueCompAll(SRST.SRSR,SRZF.ref())
            || _okro_f:=SRZF.OKRO_F;
               _srzd:=SRZF.SRZD;
               {? SRZF.del(1,1)
               || SRD.updateRecState(_okro_f,1);
                  exec('srzd_update','!fst_ewi_dzrf',_srzd);
                  _wy:=1
               ?}
            ?};
            {? ~_wy || undo() ?};
         end();
         {? ~_wy || FUN.emsg('Podczas usuwania dofinansowania wystąpił błąd, usuwanie zostało anulowane.'@) ?}
      || SRZF.r_unlock()
      ?}
   || FUN.emsg('Dofinansowanie zablokowane przez innego użytkownika.'@)
   ?};
   SRSR.r_unlock()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?}


\test_nal_amo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Test czy naliczono amortyzcję choć w jednym z okresów w czasie obowiązywania dofinansowania,
::       jeżeli tak to modyfikacje kwot lub usunięcie dofinansowania nie jest możliwe
::   WE: _a - wskazanie na środek (SRSR)
::       _b - rok podatkowy (INT) od którego obowiązuje dofinansowanie
::       _c - okres podatkowy (INT) od którego obowiązuje dofinansowanie
::   WY: 1 - naliczono, 0 - nie naliczono
::----------------------------------------------------------------------------------------------------------------------
_find:=0;
SRSR.cntx_psh(); SRST.cntx_psh(); SRZF.cntx_psh();
SRST.index('PODAT');
SRST.prefix(_a);
{? SRST.find_key(_b,_c)
|| {! |?
      {? SRST.NAL='T' || _find:=1 ?};
      _find=0 & SRST.next()
   !}
?};
SRSR.cntx_pop(); SRST.cntx_pop(); SRZF.cntx_pop();
_find


\srzd_srzf_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Lista środków dofinansowanych z bieżącego źródła
::----------------------------------------------------------------------------------------------------------------------
SRZF.cntx_psh();
SRZF.index('SRZD');
SRZF.prefix(SRZD.ref());
SRZF.win_sel('SEL');
SRZF.select();
SRZF.cntx_pop()


\srzf_historia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Historia dofinansowania
::----------------------------------------------------------------------------------------------------------------------
SRSW.cntx_psh(); SRST.cntx_psh();
SRSW.index('HISTORIA');
SRSW.prefix(SRZF.ref());
SRSW.first();
SRSW.win_sel('HISTORIA');
_hdr:=': %1, dla środka: %2, kwota do rozliczenia pod.: %3'
      ' bil.:%4'@[SRZF.NAZWA,SRZF.SRSR().NRI,form(SRZF.WARP,,2),form(SRZF.WARF,,2)];
SRSW.hdr_sel(_hdr);
SRSW.select();
SRSW.hdr_sel();
SRST.cntx_pop(); SRSW.cntx_pop()


\srzf_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na wyświetl w tabeli SRZF, okno wertowania SEL
::----------------------------------------------------------------------------------------------------------------------
SRZF.win_edit('SHOW');
{? FINFO.TOR_D='T'
|| SRZF.efld_opt('SHOW', 'enable=1',,'KONDOD');
   SRZF.efld_opt('SHOW', 'enable=1',,'ROZD');
   SRZF.efld_opt('SHOW', 'enable=1',,'DATA_ZD')
|| SRZF.efld_opt('SHOW', 'enable=0',,'KONDOD');
   SRZF.efld_opt('SHOW', 'enable=0',,'ROZD');
   SRZF.efld_opt('SHOW', 'enable=0',,'DATA_ZD')
?};
SRZF.display();
SRZF.efld_opt('SHOW', 'enable=1',,'KONDOD');
SRZF.efld_opt('SHOW', 'enable=1',,'ROZD');
SRZF.efld_opt('SHOW', 'enable=1',,'DATA_ZD');
1


\rp_srzf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła dla akcji Rekord przed w oknie wertowania tabeli SRZF (dofinansowania do środka)
::----------------------------------------------------------------------------------------------------------------------
_wylacz:=0;
{? SSTALE.AO().AMOR='T'
|| _wylacz:=1
|| {? SSTALE.AO().POCZ=date(0,0,0) & SSTALE.AO().KON=date(0,0,0)
   || _first:=exec('first_okr','fst',SSTALE.AO);
      {? ~_first || _wylacz:=1 ?}
   ?}
?};
{? _wylacz || SRZF.actions('WER','DPU:D',,1) || SRZF.actions('WER','',,1) ?};
~~


\chk_srzf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formułą kontroluje poprawność wypełnienia danych w oknie redagowania dofinansowań
::   WE: _a = 'ADD' - podczas dodawania, 'EDIT' podczas poprawiania
::   WY: Kod błędnie wypełnionego pola lub ''
::----------------------------------------------------------------------------------------------------------------------
SRZF.WARP_P:=SRZF.WARP_P$FINFO.DOKL;
SRZF.WARP:=SRZF.WARP$FINFO.DOKL;
SRZF.WARF:=SRZF.WARF$FINFO.DOKL;
{? FINFO.TOR_D='T' || SRZF.WARD:=SRZF.WARD ?};

{? FINFO.TOR_D='T'
|| _wy:=__CHK.record(SRZF,,'SRZD','DATA','KONPOD','KONFIN','KONDOD')
|| _wy:=__CHK.record(SRZF,,'SRZD','DATA','KONPOD','KONFIN')
?};
{? _wy=''
|| {? SRZF.OKRO_F=null
   || FUN.emsg('Należy wskazać okres od którego będzie uwzględnianie dofinansowanie.'@);
      _wy:='OKRO_F'
   ?}
?};
:: kontrola okresu
{? _wy='' & SRZF.OKRO_F<>null
|| {? exec('test_new_year','fst',SRZF.OKRO_F().ROK)
   || FUN.emsg('Wskazany okres rozliczania pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='OKRO_F'
   ?}
?};
{? _wy='' & SRZF.OKRO_F().AMOR='T'
|| FUN.emsg('Wskazany pierwszy okres rozliczania jest już zamknięty w obszarze Środków trwałych.'@);
   _wy:='OKRO_F'
?};
{? _wy=''
|| {? SRZF.OKRO_F().KON<SRZF.SRZD().DATA
   || FUN.emsg('Okres obowiązywania dofinansowania nie może być wcześniejszy niż\n'
               'data udostępnienia środków źródła dofinansowania.'@);
      _wy:='OKRO_F'
   ?}
?};
{? _wy=''
|| {? SRZF.DATA<SRZF.SRZD().DATA
   || FUN.emsg('Data wprowadzenia dofinansowania nie może być wcześniejsza niż\n'
               'data udostępnienia środków źródła dofinansowania.'@);
      _wy:='DATA'
   ?}
?};
{? _wy=''
|| {? SRZF.DATA>SRZF.OKRO_F().KON
   || FUN.emsg('Data wprowadzenia dofinansowania nie może być późniejsza niż\n'
               'okres obowiązywania dofinansowania.'@);
      _wy:='DATA'
   ?}
?};
{? _wy=''
|| {? SRZF.OKRO_F().POCZ<SRZF.SRSR().OKRO_F().POCZ
   || FUN.emsg('Okres obowiązywania dofinansowania nie może być wcześniejszy niż okres\n'
               'od którego środek jest ewidencjonowany w systemie.'@);
      _wy:='OKRO_F'
   ?}
?};
{? _wy=''
|| {? SRZF.WARP_P<=0
   || FUN.emsg('Kwota pełna dofinansowania powinna być większa od zera.'@);
      _wy:='WARP_P'
   ?}
?};
{? _wy=''
|| {? SRZF.WARP<=0 & (SRZF.SRDO=null | (SRZF.SRDO<>null & SRZF.SRDO().WARP>0))
   || FUN.emsg('Kwota dofinansowania do rozliczenia (podatkowa) powinna być większa od zera.'@);
      _wy:='WARP'
   ?}
?};
{? _wy=''
|| {? SRZF.WARF<=0 & (SRZF.SRDO=null | (SRZF.SRDO<>null & SRZF.SRDO().WARF>0))
   || FUN.emsg('Kwota dofinansowania do rozliczenia (bilansowa) powinna być większa od zera.'@);
      _wy:='WARF'
   ?}
?};
{? _wy='' & FINFO.TOR_D='T'
|| {? SRZF.WARD<=0 & (SRZF.SRDO=null | (SRZF.SRDO<>null & SRZF.SRDO().WARD>0))
   || FUN.emsg('Kwota dofinansowania do rozliczenia (dodatkowa) powinna być większa od zera.'@);
      _wy:='WARD'
   ?}
?};
{? _wy=''
|| {? SRZF.WARP>SRZF.WARP_P
   || FUN.emsg('Kwota dofinansowania do rozliczenia (podatkowa) nie może być większa od pełnej kwoty dofinansowania.'@);
      _wy:='WARP'
   ?}
?};
{? _wy=''
|| {? SRZF.WARF>SRZF.WARP_P
   || FUN.emsg('Kwota dofinansowania do rozliczenia (bilansowa) nie może być większa od pełnej kwoty dofinansowania.'@);
      _wy:='WARF'
   ?}
?};
{? _wy=''& FINFO.TOR_D='T'
|| {? SRZF.WARD>SRZF.WARP_P
   || FUN.emsg('Kwota dofinansowania do rozliczenia (dodatkowa) nie może być większa od pełnej kwoty dofinansowania.'@);
      _wy:='WARD'
   ?}
?};
{? _wy=''
|| {? _a='ADD' & SRZF.WARP_P>(SRZF.SRZD().PKD-SRZF.SRZD().WKD)
   || FUN.emsg('Kwota pełna dofinansowania dla środka nie może przekroczyć kwoty dostępnych jeszcze środków\n'
               'z przewidzanej planowanej kwoty zdefiniowanej w źródle '
               'finansowania (%1).'@[form(SRZF.SRZD().PKD-SRZF.SRZD().WKD,,2)]);
      _wy:='WARP_P'
   |? _a='EDIT'
   || _ref:=SRZF.ref();
      _suma:=0;
      SRZF.cntx_psh();
      SRZF.index('SRZD');
      SRZF.prefix(SRZF.SRZD);
      {? SRZF.first()
      || {! |?
            {? SRZF.ref()<>_ref
            || _suma+=SRZF.WARP_P
            ?};
            SRZF.next()
         !}
      ?};
      SRZF.cntx_pop();
      {? SRZF.WARP_P>(SRZF.SRZD().PKD-_suma)
      || FUN.emsg('Kwota pełna dofinansowania dla środka nie może przekroczyć kwoty dostępnych jeszcze środków\n'
                  'z przewidzanej planowanej kwoty zdefiniowanej w źródle '
                  'finansowania (%1).'@[form(SRZF.SRZD().PKD-_suma,,2)]);
         _wy:='WARP_P'
      ?}
   ?}
?};
{? _wy='' & EDIT_ES.R_DOFIN='D'
|| {? SRZF.WARP>SRDO.WARP | SRZF.WARF>SRDO.WARF | (FINFO.TOR_D='T' & SRZF.WARD>SRDO.WARD)
   || FUN.emsg('Kwota dofinansowania nie może przekroczyć wartości dokumentu.'@);
      _wy:='WARP_P'
   ?}
?};
{? _wy='' & EDIT_ES.R_DOFIN='D'
|| {? SRZF.OKRO_F().KON<SRDO.OKRO_F().POCZ
   || FUN.emsg('Okres obowiązywania dla dofinansowania nie może być wcześniejszy niż\n'
               'okres obowiązywania dla dokumentu.'@);
      _wy:='OKRO_F'
   ?}
?};
{? _wy='' & EDIT_ES.R_DOFIN='D'
|| {? SRZF.DATA<SRDO.DW
   || FUN.emsg('Data wprowadzenia dofinansowania nie może być wcześniejsza niż\n'
               'data wprowadzenia dokumentu.'@);
      _wy:='DATA'
   ?}
?};
{? _wy=''
|| _okro_f:=SRZF.OKRO_F;
   _rok_f:=SRZF.ROK_F;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRZF.SRSR,_rok_f,_okro_f);
   {? SRST.first()
   || {? SRST.SRSR().SAMOCHOD='T' || _warp:=SRST.SRSR().WARPELNA || _warp:=SRST.WARP ?};
      {? SRZF.WARP_P>_warp & SRZF.WARP_P>SRST.WARF & (FINFO.TOR_D='T' & SRZF.WARP_P>SRST.WARD)
      || FUN.emsg('Pełna kwota dofinansowania nie może przekroczyć wartości środka.'@);
         _wy:='WARP_P'
      ?}
   ?};
   SRST.cntx_pop()
?};

{? _wy=''
|| _okro_f:=SRZF.OKRO_F;
   _rok_f:=SRZF.ROK_F;
   _kwotap:=SRZF.WARP_P;
   _kwotadp:=SRZF.WARP;
   _kwotadf:=SRZF.WARF;
   _kwotadd:=SRZF.WARD;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRZF.SRSR,_rok_f,_okro_f);
   {? SRST.first()
   || _warp:=SRST.WARP;
      _warf:=SRST.WARF;
      _ward:=SRST.WARD
   ?};
   {? SRST.SRSR().SAMOCHOD='T' || _warp:=SRST.SRSR().WARPELNA ?};
   SRST.cntx_pop();

   {? _a='EDIT' || _ref:=SRZF.ref() || _ref:=null ?};
   SRZF.cntx_psh();
   SRZF.index('SRODEK');
   SRZF.prefix(SRSR.ref());
   _suma:=_sump:=_sumf:=_sumd:=0;
   {? SRZF.first()
   || {! |?
         {? SRZF.ref()<>_ref
         || _suma+=SRZF.WARP_P;
            _sump+=SRZF.WARP;
            _sumf+=SRZF.WARF;
            _sumd+=SRZF.WARD
         ?};
         SRZF.next()
      !}
   ?};
   SRZF.cntx_pop();
   _suma+=_kwotap;
   _sump+=_kwotadp;
   _sumf+=_kwotadf;
   _sumd+=_kwotadd;
   {? _sump>_warp
   || FUN.emsg('Suma kwot dofinansowań i ulg podatkowo (%1) nie może przekroczyć podatkowej wartości środka (%2).'@[$_sump,$_warp]);
      _wy:='WARP_P'
   ?};
   {? _wy='' & _sumf>_warf
   || FUN.emsg('Suma kwot dofinansowań i ulg bilansowo (%1) nie może przekroczyć bilansowej wartości środka (%2).'@[$_sumf,$_warf]);
      _wy:='WARP_P'
   ?};
   {? _wy='' & (FINFO.TOR_D='T' & _sumd>_ward)
   || FUN.emsg('Suma kwot dofinansowań i ulg dla toru dodatkowego (%1) nie może przekroczyć dodatkowej wartości środka (%2).'@[$_sumd,$_ward]);
      _wy:='WARP_P'
   ?}
?};

{? _wy=''
|| _okro_f:=SRZF.OKRO_F;
   _rok_f:=SRZF.ROK_F;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRZF.SRSR,_rok_f,_okro_f);
   {? SRST.first()
   || {? SRZF.WARP>(SRST.NETP+SRST.AMOP-(_sump-_kwotadp))
      || FUN.emsg('Wprowadzona kwota dofinansowania do rozliczenia podatkowo (%1) nie może przekroczyć\n'
                  'bieżącej nieumorzonej podatkowej wartości środka po uwzględnieniu wszystkich\n'
                  'zdefiniowanych dofiansowań środka (%2). Kwotę skorygowano.'@[$SRZF.WARP,$(SRST.NETP+SRST.AMOP-(_sump-_kwotadp))]);
         SRZF.WARP:=SRST.NETP+SRST.AMOP-(_sump-_kwotadp);
         _wy:='WARP'
      ?};
      {? _wy='' & SRZF.WARF>(SRST.NETF+SRST.AMOF-(_sumf-_kwotadf))
      || FUN.emsg('Wprowadzona kwota dofinansowania do rozliczenia bilansowo (%1) nie może przekroczyć\n'
                  'bieżącej nieumorzonej bilansowej wartości środka po uwzględnieniu wszystkich\n'
                  'zdefiniowanych dofiansowań środka (%2). Kwotę skorygowano.'@[$SRZF.WARF,$(SRST.NETF+SRST.AMOF-(_sumf-_kwotadf))]);
         SRZF.WARF:=SRST.NETF+SRST.AMOF-(_sumf-_kwotadf);
         _wy:='WARF'
      ?};
      {? _wy='' & FINFO.TOR_D='T' & SRZF.WARD>(SRST.NETD+SRST.AMOD-(_sumd-_kwotadd))
      || FUN.emsg('Wprowadzona kwota dofinansowania do rozliczenia dodatkowo (%1) nie może przekroczyć\n'
                  'bieżącej nieumorzonej dodatkowej wartości środka po uwzględnieniu wszystkich\n'
                  'zdefiniowanych dofiansowań środka (%2). Kwotę skorygowano.'@[$SRZF.WARD,$(SRST.NETD+SRST.AMOD-(_sumd-_kwotadd))]);
         SRZF.WARD:=SRST.NETD+SRST.AMOD-(_sumd-_kwotadd);
         _wy:='WARD'
      ?}
   ?};
   SRST.cntx_pop()
?};
_wy


\be_srzf_kondod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRZF.KONDOD
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.TOR_D='T'
|| exec('be_kondod','!fst_ewi_dzrf');
   1
|| 0
?}


\ae_srzf_srzd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła po redakcji pola SRZF.SRZD
::----------------------------------------------------------------------------------------------------------------------
{? SRZF.KONPOD=null || SRZF.KONPOD:=SRZF.SRZD().KONPOD ?};
{? SRZF.KONFIN=null || SRZF.KONFIN:=SRZF.SRZD().KONFIN ?};
{? FINFO.TOR_D='T' & SRZF.KONDOD=null || SRZF.KONDOD:=SRZF.SRZD().KONDOD ?};
{? (SRZF.KONPOD<>null & SRZF.KONPOD<>SRZF.SRZD().KONPOD)
   | (SRZF.KONFIN<>null & SRZF.KONFIN<>SRZF.SRZD().KONFIN)
   | (FINFO.TOR_D='T' & SRZF.KONDOD<>null & SRZF.KONDOD<>SRZF.SRZD().KONDOD)
|| {? FUN.ask('Zestawy kont dofinansowania różnią się od zestawów zdefiniowanych w źródle\n'
           'dofinansowania. Przywrócić konta ze źródła dofinansowania?'@)
   || SRZF.KONPOD:=SRZF.SRZD().KONPOD;
      SRZF.KONFIN:=SRZF.SRZD().KONFIN;
      {? FINFO.TOR_D='T' || SRZF.KONDOD:=SRZF.SRZD().KONDOD ?}
   ?}
?};
{? SRZF.DATA<SRZF.SRZD().DATA || SRZF.DATA:=SRZF.SRZD().DATA ?};
{? SRZF.NAZWA='' || SRZF.NAZWA:=SRZF.SRZD().NAZWA ?};
1


\ae_srzf_warpp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła po redakcji pola SRZF.WARP_P
::----------------------------------------------------------------------------------------------------------------------
SRZF.WARP_P:=SRZF.WARP_P$FINFO.DOKL;
{? (SRZF.WARP=0 | SRZF.TYP='U') & (SRZF.SRDO=null | (SRZF.SRDO<>null & SRDO.WARP>0))|| SRZF.WARP:=SRZF.WARP_P ?};
{? (SRZF.WARF=0 | SRZF.TYP='U') & (SRZF.SRDO=null | (SRZF.SRDO<>null & SRDO.WARF>0)) || SRZF.WARF:=SRZF.WARP_P ?};
{? FINFO.TOR_D='T'
|| {? (SRZF.WARD=0 | SRZF.TYP='U') & (SRZF.SRDO=null | (SRZF.SRDO<>null & SRDO.WARD>0)) || SRZF.WARD:=SRZF.WARP_P ?}
?};
1


\ae_srzf_okro_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji pola SRZF.OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? SRZF.OKRO_F<>null
|| SRZF.ROK_F:=SRZF.OKRO_F().ROK
|| SRZF.ROK_F:=null
?}


\be_konpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRZD.KONPOD lub SRZF.KONPOD lub SRZD.ZK_POD
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='P';
ZSKON.win_dict('WYB_P');
1


\be_konfin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRZD.KONFIN lub SRZF.KONFIN lub SRZD.ZK_FIN
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='F';
ZSKON.win_dict('WYB_F');
1


\be_kondod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRZD.KONDOD lub SRZF.KONDOD lub SRZD.ZK_DOD
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='D';
ZSKON.win_dict('WYB_D');
1


\srzf_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetla dokumenty zmieniające właściwości dofinansowania lub ulgi
::----------------------------------------------------------------------------------------------------------------------
_win:=SRDO.win_sel('?');
SRDO.index('SRODZAJD');
SRDO.prefix(SRSR.ref(),'D',SRZF.ref());
SRDO.win_sel('WER_D');
SRDO.select();
SRDO.win_sel(_win)


\ae_srzf_konpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji zestawu kont kosztowych w dofinansowaniu lub uldze
::   WY: 1/0
::  OLD: \ae_srzf_konpod/fst.fml
::----------------------------------------------------------------------------------------------------------------------
exec('spr_zsdo','fst')


\ae_srzf_konfin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji zestawu kont kosztowych w dofinansowaniu lub uldze
::   WY: 1/0
::  OLD: \ae_srzf_konfin/fst.fml
::----------------------------------------------------------------------------------------------------------------------
exec('spr_zsdo','fst')


\ae_srzf_kondod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji zestawu kont kosztowych w dofinansowaniu lub uldze
::   WY: 1/0
::  OLD: \ae_srzf_kondod/fst.fml
::----------------------------------------------------------------------------------------------------------------------
exec('spr_zsdo','fst')

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:44 23c2573f5b6abdb9b051bcbb9241b46c0cc5964b01725b27a63b746ce2c609fb4f8a99c520d98f846198e02ecfde48dababdb506347c84d7e04314ba21957e1478ccca8d6150360cba335a98496df4e86a65e5161bcdc3aab35e9c8184e552f9e9d7eb4ec426672c9725b6f4ffce8abec946bf2942cdc57c4bf346a1af1d14be

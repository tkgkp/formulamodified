:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_deku.fml
:: Utworzony: 08.09.2020
:: Autor: MicKoc [20.42]
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_DEKU - Dezaktywacja konta użytkownika.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [20.42]
:: OPIS: Dezaktywacja konta użytkownika - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::# kind=WE, symbol=D, type=DATE, name=Data badania zwolnienia, required=N, keyref=N
::# kind=WE, symbol=CZY_INFO, type=STRING, name=Komunikaty informacyjne [T/N*], required=N, keyref=N, fml_val="exec('edit_boolean','#edit',{? _a=~~ || 'N' || _a ?}, 'Komunikaty informacyjne')"
::
_par:=params_get();

_in:=_par.in;
_out:=_par.out;
_mp:=_par.mp;
_akcja:=_mp.akcja();
_czy_info:=(var_press('CZY_INFO',_in)=type_of('') & _in.CZY_INFO='T');
_id:=exec('ref2uid','#table',_in.P);
_result:='';

{? var_pres('D',_in)=type_of(date) & _in.D<>#0
|| _data:=_in.D
|| _data:=date()
?};

{? _id=''
|| _result:=exec('dezaktywacja_error','pracownik')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_id);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_id);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _ret:=exec('dezaktywuj','pracownik',_in.P,_data);
      {? var_press('STATUS',_ret)=type_of('') & var_press('OK',_ret)=type_of(0) & ~_ret.OK
      || _result:=_ret.STATUS
      |? var_press('STATUS',_ret)=type_of('') & var_press('OK',_ret)=type_of(0) & _ret.OK
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   {? _czy_info | (var_press('OK',_ret)=type_of(0) & ~_ret.OK) || FUN.emsg(_result) ?}
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [20.42]
:: OPIS: Dezaktywacja konta użytkownika - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Dezaktywacja konta pracownika: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Dezaktywacja konta pracownika: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Dezaktywacja konta pracownika: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Dezaktywacja konta pracownika'@@
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 5d33da7b9ef1c6e6651af3ab65ea9b1dd3a6efa4764e4e1fb26d1917c4bbb7ff42a40235db8326e993e304931f945e9069f73959197feeb1cf155d2c4ccf6842fb5163aecb03c99909dd23458ee4b559ee4a3f8b01c14ed72650d5e8cbc4db7cde69a4681edcca01a1d1c1aaa4dbdefa321cce8d1fe5bebc5d5e85bf5a124594

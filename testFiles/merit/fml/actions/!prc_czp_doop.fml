:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_doop.fml
:: Utworzony: 28.11.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: Czynność PRC_CZP_DOOP - Otworzenie okresu  prac.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Otworzenie okresu rozliczeniowego dla współpracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=T
::# kind=WE, symbol=P, type=_P, name=Wskazanie na współpracownika, required=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();
_result:='';

_uid_OKR:=exec('ref2uid','#table',_in.A_OKR);
_uid_P:=exec('ref2uid','#table',_in.P);
{? _uid_OKR=''
|| _result:=exec('error','!prc_czp_doop','Nie znaleziono okresu rozliczeniowego.'@)
|? _uid_OKR=''
|| _result:=exec('error','!prc_czp_doop','Nie znaleziono współpracownika.'@)
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uid_OKR);
      _mp.keyRef(_uid_P);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uid_OKR);
      _mp.delRef(_uid_P);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('odblokuj_dane','!prc_czp_doop',_in);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Otworzenie okresu rozliczeniowego dla współpracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab_a_okr:=obj_new('ZAW_DANE','A_OKRN_NAZWA','OD','DO');
{! _ii..obj_len(_tab_a_okr) |! _tab_a_okr[_ii]:='' !};
_tab_p:=exec('init_desc_tab','pracownik');
_mp:=params_get().mp;
A_OKR.cntx_psh();
{? A_OKR.seek(_mp.load(exec('kind_in','#b_port')).A_OKR,,1)
|| _tab_a_okr.ZAW_DANE:='T';
   _tab_a_okr.A_OKRN_NAZWA:=A_OKR.NAZ().NAZ;
   _tab_a_okr.OD:=A_OKR.OD$1;
   _tab_a_okr.DO:=A_OKR.DO$1
?};
A_OKR.cntx_pop();
OSOBA.cntx_psh();
P.cntx_psh();
{? P.seek(_mp.load(exec('kind_in','#b_port')).P,,1)
|| _tab_p.ZAW_DANE:='T';
   _tab_p.NAZWISKO:=P.OSOBA().NAZWISKO;
   _tab_p.PIERWSZE:=OSOBA.PIERWSZE;
   _tab_p.T:=P.T
?};
P.cntx_pop();
OSOBA.cntx_pop();

{? _tab_a_okr.ZAW_DANE='T' & _tab_p.ZAW_DANE='T'
|| 'Otworzenie okresu rozliczeniowego: %1 %2 %3 dla współpracownika %4 %5 %6'@@
      [_tab_a_okr.A_OKRN_NAZWA,_tab_a_okr.OD,_tab_a_okr.DO,_tab_p.T,_tab_p.NAZWISKO,_tab_p.PIERWSZE]
|| 'Otworzenie okresu rozliczeniowego'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Szczegóły komunikatu o błędzie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Otworzenie okresu rozliczeniowego współpracownika nie jest możliwe.'@,_a]


\odblokuj_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Otworzenie wybranego okresu rozliczeniowego współpracownika.
::   WE: _a - parametry wejściowe czynności
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
P.cntx_psh();
A_OKR.cntx_psh();
A_OKRP.cntx_psh();
{? P.seek(_a.P,,1)
|| {? A_OKR.seek(_a.A_OKR,,1)
   || A_OKRP.index('A_OKRPR');
      A_OKRP.prefix(A_OKR.ref(),P.ref());
      {? A_OKRP.first()
      || {? A_OKRP.S_PLAN='X'
         || _tab:=tab_tmp(,'RESULT','INTEGER','');
            _ed:=_tab.mk_edit('Okres: %1 dla: %2'@
               [  A_OKRP.OKR().NAZ().NAZ+' '+A_OKRP.OD$1+' '+A_OKRP.DO$1,
                  A_OKRP.P().OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE
               ],0
            );
            _tab.win_efld(_ed,AH,'H',,,,,,'Okres rozliczeniowy dla współpracownika zostanie otworzony'@);
            exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:F2'",,
               exec('help_red_zakoncz','#window','PKD_E'),exec('text_red_zakoncz','#window','PKD_E'));
            exec('ok_esc','#window',_tab,_ed,1,,"cur_tab().RESULT:=0; 'key:Esc'",0,,
               exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
            _tab.win_edit(_ed);
            _tab.edit();
            {? _tab.RESULT
            || A_OKRP.S_PLAN:='Z';
               A_OKRP.put();
               _result:=1
            ?}
         |? A_OKRP.S_PLAN='O'
         || _result:=exec('error','!prc_czp_doop','Okres rozliczeniowy jest odblokowany do planowania.'@)
         |? A_OKRP.S_PLAN='Z'
         || _result:=exec('error','!prc_czp_doop','Nie można otworzyć okresu rozliczeniowego dla współpracownika.'@)
         ?}
      || _result:=exec('error','!prc_czp_doop','Nie znaleziono okresu rozliczeniowego współpracownika.'@)
      ?}
   || _result:=exec('error','!prc_czp_doop','Nie znaleziono okresu rozliczeniowego.'@)
   ?}
|| _result:=exec('error','!prc_czp_doop','Nie znaleziono współpracownika.'@)
?};
A_OKRP.cntx_pop();
A_OKR.cntx_pop();
P.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 d42bd7a8ad824b834513c4b3718b218ae767f5eb125d101455d510f1ffd9fa3e9fb7f9348bf0ae9b3acdc4a81daf5af861d79cc8a0a36da0625035f5e37f1141dea6ce4c8d040c1cbdd09ae98c0ffbadf84baf8dbca52d2ae5f9ab4e9e031de5e294ca53b329067444551a363fb45aa23a0d2c6863f05f636a35129ba3ff805d

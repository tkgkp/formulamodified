:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_fkst.fml
:: Utworzony: 08.02.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Stanowiska kasowe
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Czynność ZWS_PAR_FKST - stanowiska kasowe
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejsciowe czynnosci
::       _int - [obj_new] - parametry wewnetrzne czynnosci
::       _out - [obj_new] - parametry wyjsciowe czynnosci
::       _mp  - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('mf1_sta','!zws_par_fkst')


\mf1_sta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: definicja stanowisk kasowych
::  OLD: \mf1_sta/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
stan_naz:='';
::Poczatek modyfikacji MacLex 01-29-2010 WS [1110]
STANKAS.win_sel('WER'+{? KPARAM.CZY_B='T' || 'B' || '' ?});
STANKAS.win_edit('STANKAS'+{? KPARAM.CZY_B='T' || 'B' || '' ?});
::Koniec modyfikacji MacLex
STANKAS.select();
&stan_naz;
0


\sta_kodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed redakcję pola kod stanowiska
::       - redakcja dozwolona tylko jesli
::   WE:
::   WY:
::  OLD: \sta_kodb/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=0;
{? ROK_F.first()
        ||      {!      |?      RAPORT.use('krp'+form(fld,-3)+(form(ROK_F.KOD)+2));
            RAPORT.clear();
            _a+=RAPORT.first();
            ROK_F.next()
      !}
?};
{? +APARAM.MASKA<>5 || exec('get_puzy','kasa_wspolne') ?};
exec('open_tabele','open_tab');
_a=0 | Popraw=0


\sta_koda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: A.Kucharczyk [--.--]
:: OPIS: po redakcji pola kod stanowiska kasowego
::   WE:
::   WY:
::  OLD: \sta_koda/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? STANKAS.KOD>999 | STANKAS.KOD<1
|| FUN.info('Kod stanowiska kasowego powinien się zawierać w przedziale od 1 do 999.'@);0
|| 1
?}


\aewalkas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Po redakcji pola WALUTA na stanowisu kasowym (ustawia opis waluty stanu min.)
::  OLD: \aewalkas/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
exec('tyt_min','kasa_wspolne');
1


\czy_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Pola parametryzujace podpowiedz kursu - dostepne jesli jest ustawiona podpowiedz na T
::   WE:
::   WY:
::  OLD: \czy_wal/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
::STANKAS.CZY_KTIP='T'
1


\aeoddkas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Po redakcji pola STANKAS.ODD
::   WE:
::   WY:
::  OLD: \aeoddkas/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
1


\num_wr0
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011] 01-29-2010
:: OPIS: Wartosc >=0
::  OLD: \num_wr0/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld>=0 || 1 || 0 ?}


\pgpp_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Przed redakcją pola PGPP w STANKAS
::  OLD: \pgpp_be/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
ML_KSBUD.index('STKAS');
ML_KSBUD.prefix(STANKAS.KOD);
{? ML_KSBUD.size>0
|| 0
|| 1
?}


\pgpp_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: F3 STANKAS.PGPP
::  OLD: \pgpp_f3/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
1


\pgpp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Po STANKAS.PGPP
::  OLD: \pgpp_ae/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
1


\stmin_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Przed edycja stanu minimalnego na stanowisku - dostepne dla edycji jesli nie wlaczona obsluga budzetu
::  OLD: \stmin_be/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
1


\symst_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: wywoluje slownik rodzajow raportow rodzajowych i uzywa go jako wzorca na pola stanowiska kasowego
::  OLD: \symst_f3/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------

{? STANKAS.SYM<>''
|| MLTYPYRR.find_key(STANKAS.SYM)
?};
{? MLTYPYRR.select(,1)
|| STANKAS.SYM:=MLTYPYRR.SYM
?};
1


\symst_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Po redakcji symbolu kasy - symbol moze pochodzic tylko ze slownika
::   jesli pole nazwa stanowiska jest puste to uzupelnia pola ze wzorca
::  OLD: \symst_ae/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? STANKAS.SYM='' | ~exec('sprwsk','!zws_par_fkst',STANKAS.SYM)
|| FUN.emsg('Symbol stanowiska musi być wypełniony kodem ze słownika typy raportów.'@);
   return(0)
?};
{? STANKAS.NAZWA=''
|| STANKAS.NAZWA:=MLTYPYRR.NAZ;
   1
?};
_wyn


\sprwsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: sprawdza czy podany symbol jest w MLKTYPYRR
::  OLD: \sprwsk/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
MLTYPYRR.find_key(_a)


\pg_tzn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: przed redakcja znaku
::  OLD: \pg_tzn/ml_znaki.fml
::----------------------------------------------------------------------------------------------------------------------
KPARAM.TZN='T'


\sta_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Stanowisko kasowe - dołącz przed
::   WE:
::   WY:
::  OLD: \sta_dol/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
Popraw:=0;
1


\sta_dol_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [22.26]
:: OPIS: Stanowisko kasowe - dołącz po
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_uzupełnij_pelne','users_upraw','KAS');
~~


\sta_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Przed popraw dla STANKAS
::   WE:
::   WY:
::  OLD: \sta_pop/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
stan_naz:=STANKAS.NAZWA;
Popraw:=1


\bv_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
::  UTW: MB [2006]
:: OPIS: Przed wyswietl okien tabeli STANKAS
::  OLD: \bv_stan/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
STANKAS.display()


\sta_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Rekord po okien tabeli STANKAS
::  OLD: \sta_chk/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? STANKAS.CZY_KTIP<>'T' || STANKAS.CZY_ZMKU:='N' ?};
_ok:=
   {? KPARAM.CZY_B='T'
   || __CHK.record(STANKAS,
         ,'KOD','SYM','NAZWA'
         ,'CZY_KASA','CZY_KILK','CZY_KDNI','CZY_OTW','CZY_SMIN'
         ,'CZY_KTIP'
         ,'F_NUMER','F_KP','F_KW','F_INNY','F_KWT'
         ,'ODD')
   || __CHK.record(STANKAS,
         ,'KOD','NAZWA'
         ,'CZY_KASA','CZY_KILK','CZY_KDNI','CZY_OTW','CZY_SMIN'
         ,'CZY_KTIP'
         ,'F_NUMER','F_KP','F_KW','F_INNY'
         ,'ODD')
   ?};
{? _ok='' & -menu_txt()='popraw' & stan_naz<>STANKAS.NAZWA
|| exec('akt_stan','!zws_par_fkst',stan_naz,STANKAS.NAZWA);
   KUSERUPR.cntx_psh(); STANKAS.cntx_psh();
   {? USRCONST.STANKAS().STANKAS=STANKAS.ref() || exec('titlebar','kas',STANKAS.NAZWA) ?};
   KUSERUPR.cntx_pop(); STANKAS.cntx_pop()
?}; _ok


\be_bokas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Przed akcja Bilans otwarcia okna WER tabeli STANKAS
::  OLD: \be_bokas/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
BO_KASY.index('STANKAS'); BO_KASY.prefix(STANKAS.ref());
BO_KASY.hdr_sel();
BO_KASY.hdr_sel(': '+STANKAS.NAZWA);
BO_KASY.win_edit('RED'+{? STANKAS.WALUTA<>exec('wal_nar','kasa_wspolne') || '' || '1' ?});
ROK_F.cntx_psh(); _r:=ROK_F.first(); ROK_F.cntx_pop();
_is_rap:=exec('is_rap','kasa_wspolne',STANKAS.KOD);
exec('tyt_nar','!zws_par_fkst');
{? _r ||
   {? _is_rap & (menu_pth='FST' | menu_pth='FUPT')
   || {? BO_KASY.first()
      || BO_KASY.select(,,,'dpUA:dpU')
      || FUN.info('Brak pozycji bilansu otwarcia.\nIstnienie raportów uniemożliwia ich dodanie.'@)
      ?}
   || _off:={? STANKAS.WALUTA<>null || 'dk' || '' ?};
      {? _is_rap
      || BO_KASY.select(,,,_off)
      || BO_KASY.select(,,,'A'+_off)
      ?}
   ?}
?}


\bl_skas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Wartosc poczatkowa dla pola BO_KASY.STANKAS
::  OLD: \bl_skas/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
STANKAS.ref()


\bd_bowal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Przed Wyswietl dla pola BO_KASY.WAL
::  OLD: \bd_bowal/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? BO_KASY.BO_WAL=null & STANKAS.WALUTA<>null || BO_KASY.WAL:=STANKAS.WALUTA || 1 ?}


\be_bowal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Przed redakcja pola BO_KASY.WAL
::  OLD: \be_bowal/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? STANKAS.WALUTA<>0
|| fld(STANKAS.WALUTA);
   {? STANKAS.WALUTA=exec('wal_nar','kasa_wspolne') ||
      BO_KASY.BO_PLN:=BO_KASY.BO_WAL
   ?};
   0
|| 1
?}


\ae_bowal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Po redakcji pola BO_KASY.WAL
::  OLD: \ae_bowal/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=BO_KASY.ref();
BO_KASY.cntx_psh();
BO_KASY.index('STANKAS'); BO_KASY.prefix(STANKAS.ref());
_ok:=BO_KASY.find_key(BO_KASY.WAL().SYM,WAL.SYM) & (-menu_txt<>'popraw' | BO_KASY.ref()<>_ref);
BO_KASY.cntx_pop();
{? _ok ||
   FUN.info('Wprowadzono już kwotę dla waluty %1.'@[BO_KASY.WAL().SYM]);
   fld(null)
?};
~_ok


\ae_bowar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Po redakcji pola BO_WAL i BO_PLN tabeli BO_KASY
::  OLD: \ae_bowar/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
fld(fld()$2);
{? fld()<=0 & STANKAS.CZY_SMIN<>'T'
|| FUN.info('Kwota powinna być większa od zera.'@); 0
|| 1
?}


\ba_bokas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Przed akcja dolacz dla okna WER tabeli BO_KASY
::  OLD: \ba_bokas/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? STANKAS.WALUTA<>0
|| BO_KASY.cntx_psh();
   BO_KASY.index('STANKAS'); BO_KASY.prefix(STANKAS.ref());
   _ok:=~BO_KASY.find_key(STANKAS.WALUTA().SYM,WAL.SYM);
   BO_KASY.cntx_pop();
   _ok
|| 1
?}


\ad_bokas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Po akcji Usun dla okna WER tabeli BO_KASY
::  OLD: \ad_bokas/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć pozycję bilansu otwarcia stanowiska kasowego?'@)
||
   {? exec('is_rap','kasa_wspolne',STANKAS.KOD)
   || _wal:=BO_KASY.BO_WAL;
      _pln:=BO_KASY.BO_PLN;
      BO_KASY.BO_WAL:=0;
      BO_KASY.BO_PLN:=0;
      BO_KASY.put();
      _ok:=exec('akt_bo','kasa_wspolne',0);
      {? ~_ok ||
         BO_KASY.BO_WAL:=_wal;
         BO_KASY.BO_PLN:=_pln;
         BO_KASY.put()
      ?}
   || _ok:=1
   ?};
   {? _ok
   || BO_KASY.del()
   || FUN.info('Usunięcie niemożliwe.\nPowoduje powstanie ujemnego salda kasy.'@)
   ?}
?}


\bg_aktbo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Przed akcja grupowa Aktualizacji bilansu otwarcia raportu
::  OLD: \bg_aktbo/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy aktualizować bilans otwarcia raportów dla zaznaczonych walut?'@)
|| {? exec('blokada','kasa_wspolne',1)
   || g_sel:=BO_KASY.sel_size();
      g_akt:=0; g_noakt:=0;
      g_wal:='';
      1
   || exec('blokada','kasa_wspolne',0);
      FUN.info('Raporty są obsługiwane przez innych użytkowników.\nWstrzymano aktualizację.'@
              +'\n\nStanowisko %1: waluta %2.'@[STANKAS.NAZWA,WAL.SYM]);
      0
   ?}
|| 0
?}


\ag_aktbo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Po akcja grupowej Aktualizacji bilansu otwarcia raportu
::  OLD: \ag_aktbo/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
exec('blokada','kasa_wspolne',0);
FUN.info('Liczba zaznaczonych walut: %1'@[$g_sel]+
    {? g_akt || '\nLiczba zaktualizowanych BO raportów: %1'@[$g_akt] || '' ?}+
    {? g_noakt || '\nLiczba zbędnych aktualizacji: %1'@[$g_noakt] || '' ?}+
    {? g_wal<>''
    || {? ((g_wal-2)*',')=0
       || '\nWystąpiło ujemne saldo dla waluty'@
       || '\nWystąpiło ujemne saldo dla walut'@
       ?}+': \n'+(g_wal-2)
    || ''
    ?});
&g_sel; &g_akt


\tyt_nar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: ustawia tytul waluty narodowej
::  OLD: \tyt_nar/wzorce.fml
::----------------------------------------------------------------------------------------------------------------------
{? KINFO.NAROD
|| SLOSLU.TYT_NAR:='Kwota w '+KINFO.NAROD().KOD
|| SLOSLU.TYT_NAR:='Waluta narodowa'
?};
BO_KASY.fld_opt('WER','col_name="%1"'[SLOSLU.TYT_NAR],BO_KASY,'BO_PLN',)


\akt_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Aktualizacja nazwy stanowiska kasowego w KUSERUPR
::   WE: _a - nazwa poprzednia stanowiska kasowego
::       _b - nazwa aktualna stanowiska kasowego
::  OLD: \akt_stan/sta_pola.fml
::----------------------------------------------------------------------------------------------------------------------
KUSERUPR.cntx_psh(); KUSERUPR.index('KU'); KUSERUPR.prefix();
{? KUSERUPR.first()
|| {! |?
      {? KUSERUPR.STANKASN=_a
      || KUSERUPR.STANKASN:=_b;
         KUSERUPR.put()
      ?};
      KUSERUPR.next()
   !}
?};
KUSERUPR.cntx_pop()


\sta_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: usuwa stanowisko kasowe
::----------------------------------------------------------------------------------------------------------------------
{? STANKAS.count()-exec('sta_ile_upr','!zws_par_fkst',STANKAS.ref())>0
|| FUN.info('Stanowisko kasowe wykorzystywane.\nUsunięcie niemożliwe.'@)
|? FUN.ask('Usunąć stanowisko kasowe?'@)
|| KUSERUPR.cntx_psh();
   KUSERUPR.index('S1');
   KUSERUPR.prefix(STANKAS.ref());
   {? KUSERUPR.first()
   || {!
      |? KUSERUPR.del();
         KUSERUPR.first()
      !}
   ?};
   KUSERUPR.cntx_pop();
   STANKAS.del()
?};
~~


\sta_ile_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [22.26]
:: OPIS: sprawdza liczbę użytkowników uprawnionych do danego stanowiska kasowego
::   WE: _a - STANKAS.ref
::   WY: ilosc użytkowników
::----------------------------------------------------------------------------------------------------------------------
KUSERUPR.cntx_psh();
KUSERUPR.index('S1');
KUSERUPR.prefix(_a);
_wyn:=KUSERUPR.size();
KUSERUPR.cntx_pop();
_wyn

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 abefe0e760934586599098f2ab0fc6fdf5764c2d5da8e18ac41e0b2d7e9170282248cb39b8462505508b0c2647162edb249dc1633026002a7755b1b093ae61806628573635c5aa986608540f84216c215dd89eb3568c952f1ffa229130c9117876e5397a42cfda13412b9dc5a080626214d1ac06d5904ac280a0f4509a49f593

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ppsr.fml
:: Utworzony: 16.03.2023
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PPSR - Rodzaje pracy poza siedzibą firmy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.31]
:: OPIS: Rodzaje pracy poza siedzibą firmy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PPSFR.cntx_psh();
PPSFR.index('KOD');
PPSFR.prefix(exec('firma','ustawienia'));
PPSFR.win_sel('WER');
PPSFR.win_edit('RED');
PPSFR.select();
PPSFR.cntx_pop();
~~


\ppsfr_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER tabeli PPSFR.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _ag:=':';
   {? PPSFR.SYSTEM='T'
   || _ag:='U%1'[_ag]
   ?};
   PPSFR.actions_grayed('WER',_ag)
?};
~~


\ppsfr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli PPSFR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfr_chk','!zws_par_ppsr',-menu_txt()='popraw')


\ppsfr_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Wyświelt - przed" w oknie WER tabeli PPSFR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfr_efld_opt','!zws_par_ppsr','*',PPSFR,'RED');
PPSFR.display()


\ppsfr_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed obsługą akcji "Dołącz" w oknie WER tabeli PPSFR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PPSFR.blank();
exec('ppsfr_efld_opt','!zws_par_ppsr','*',PPSFR,'RED');
1


\ppsfr_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed obsługą akcji "Popraw" w oknie WER tabeli PPSFR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfr_efld_opt','!zws_par_ppsr','*',PPSFR,'RED');
1


\ppsfr_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli PPSFR.
::   WE:
::   WY:
::  OLD: \ppsfr_addb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfr_modb','!zws_par_ppsr') & exec('ppsfr_chk','!zws_par_ppsr',0)


\ppsfr_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli PPSFR.
::   WE:
::   WY:
::  OLD: \ppsfr_putb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfr_modb','!zws_par_ppsr') & exec('ppsfr_chk','!zws_par_ppsr',1)


\ppsfr_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
::  MOD: IGPTASZE [22.26]
:: OPIS: Wyzwalacz "Popraw - po" dla tabeli PPSFR.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::  OLD: \ppsfr_puta/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

{? bfld('FIRMA')<>PPSFR.FIRMA
|| undo('PPSFR - wycofanie zmiany przypisania do firmy.');
   return()
?};

{? bfld('SYSTEM')='T' & PPSFR.SYSTEM='N'
|| undo('PPSFR - zapis systemowy. Zmiana niemożliwa.');
   return()
|? PPSFR.SYSTEM='T' & bfld('KOD')<>PPSFR.KOD
|| undo('PPSFR - zapis systemowy. Modyfikacja pola "KOD" niemożliwa.');
   return()
?};

{? bfld('LIMITY')<>PPSFR.LIMITY & PPSFR.LIMITY='T'
|| PPSFROZD.cntx_psh();
   PPSFROZD.index('AUTOKAL');
   PPSFROZD.prefix(PPSFR.ref(),'T',);
   _autokal:=PPSFROZD.first();
   PPSFROZD.cntx_pop();
   {? _autokal
   || undo('Istnieją rozkłady z włączoną autoprezentacją.\nZmiany dotyczące limitów nie są możliwe.');
      return()
   ?}
?};

{? exec('lic','#b_domain','POR')
|| exec('idput_ndx','#table',PPSFT,'PPSFR',PPSFR.ref())
?};
~~


\ppsfr_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli PPSFR.
::   WE:
::   WY:
::  OLD: \ppsfr_delb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',PPSFT,'PPSFR',PPSFR.ref()) &
exec('del_ndx','#table',PPSFROZD,'AUTOKAL',PPSFR.ref()) &
exec('del_ndx','#table',PPSFL,'UNIQUE',PPSFR.FIRMA,PPSFR.ref())


\ppsfr_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed modyfikacją rekordu.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli PPSFR.
::   WE:
::   WY:
::  OLD: \ppsfr_modb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? PPSFR.LIMITY<>'T'
|| PPSFR.MODLIM:='N'
?};
1


\ppsfr_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
::  MOD: IGPTASZE [22.26]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli PPSFR.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::  OLD: \ppsfr_chk/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;

__CHK.validate(PPSFR,
   $("_a.table(_b,"+$_put+",,
      'FIRMA','KOD','NAZWA','SYSTEM','LIMITY','MODLIM','TD_PRAC','WSK_ADR','OCHR_ADR','KAL_ZESP')"),
   "_a.in_set(_b,'SYSTEM',,'T','N')",
   "_a.in_set(_b,'LIMITY',,'T','N')",
   "_a.in_set(_b,'MODLIM',,'T','N')",
   "_a.in_set(_b,'TD_PRAC',,'T','N')",
   "_a.in_set(_b,'WSK_ADR',,'T','N')",
   "_a.in_set(_b,'OCHR_ADR',,'T','N')",
   "_a.in_set(_b,'KAL_ZESP',,'T','N')"
)


\ppsfr_kod_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed redagowaniem pola PPSFR.KOD
::   WE:
::   WY:
::  OLD: \ppsfr_kod_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSFR.SYSTEM<>'T'


\ppsfr_limity_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed redagowaniem pola PPSFR.LIMITY.
::   WE:
::   WY:
::  OLD: \ppsfr_limity_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? PPSFR.KOD='ZDALN_ST'
|| FUN.info('Definiowanie limitów dla pracy zdalnej stałej nie jest możliwe.'@);
   0
|? -menu_txt()='popraw' & PPSFR.LIMITY<>'T'
:: Jeżeli zdarzenie jest nielimitowane to może ma włączoną flagę "autoprezentacja" (IsVirtual)?
|| PPSFROZD.cntx_psh();
   PPSFROZD.index('AUTOKAL');
   PPSFROZD.prefix(PPSFR.ref(),'T',);
   _autokal:=PPSFROZD.first();
   PPSFROZD.cntx_pop();
   {? _autokal
   || FUN.info('Istnieją rozkłady z włączoną autoprezentacją.\nDefiniowania limitów nie jest możliwe.'@);
      0
   || 1
   ?}
|| 1
?}


\ppsfr_limity_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pola PPSFR.LIMITY.
::   WE:
::   WY:
::  OLD: \ppsfr_limity_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfr_efld_opt','!zws_par_ppsr')


\ppsfr_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli PPSFR.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE: [_a] [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują się pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::  OLD: \ppsfr_efld_opt/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(PPSFR)
|| _TAB:=_b
|? _tryb='1'
|| _TAB:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? exec('lic','#b_domain','POR')
:: Dla Portalu HR:
::    * Ze względu na brak obsługi widoku "Ewidencja pracy" WYŁĄCZONO redakcję pola Skrót (SKROT).
::    * Ze względu na brak obsługi typu TSLSloPanel WYŁĄCZONO redakcję pól:
::       * Wskazanie adresu (PPSFR.WSK_ADR);
::       * Ochrona adresu (PPSFR.OCHR_ADR).
::    * "Rejestracja wniosku tylko przez pracownika" - obecnie TAK - nie ma potrzeby parametryzacji.
|| _TAB.efld_opt(_we,'enable=0',,'SKROT');
   _TAB.efld_opt(_we,'enable=0',,'WSK_ADR');
   _TAB.efld_opt(_we,'enable=0',,'OCHR_ADR');
   _TAB.efld_opt(_we,'enable=0',,'TD_PRAC')
?};

{? _fld='' | _fld='LIMITY'
|| _TAB.efld_opt(_we,'enable=%1'[$(PPSFR.LIMITY='T')],,'MODLIM')
?};

1


\ppsfrozd_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli PPSFROZD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_addb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_chk','!zws_par_ppsr',0)


\ppsfrozd_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli PPSFROZD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_putb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_chk','!zws_par_ppsr',1)


\ppsfrozd_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed modyfikacją rekordu.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli PPSFROZD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_modb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? PPSFROZD.PPSFR
|| _ok:=1;
   PPSFR.cntx_psh();
   PPSFR.prefix();
   PPSFROZD.PPSFR();
   {? PPSFROZD.PPSFR().LIMITY='T'
   || PPSFROZD.AUTOKAL:='N'
   ?};
   {? PPSFROZD.FIRMA=null()
   || PPSFROZD.FIRMA:=PPSFR.FIRMA
   |? PPSFROZD.FIRMA<>PPSFR.FIRMA
   || undo('%1: %2' ['PPSFROZD','Brak jednoznaczności powiązania z firmą.']);
      _ok:=0
   ?};
   PPSFR.cntx_pop();
   {? ~_ok
   || return(0)
   ?}
?};
{? PPSFROZD.ZAKRES='S'
|| PPSFROZD.OL:='T';
   PPSFROZD.JML:='D';
   PPSFROZD.WARTD:=7;
   PPSFROZD.WYBDNI:='N'
?};
{? PPSFROZD.JML='D'
|| PPSFROZD.WARTG:=time(0,0,0)
|? PPSFROZD.JML='G'
|| PPSFROZD.WARTD:=0
?};
{? PPSFROZD.OL+PPSFROZD.JML<>'TD'
|| PPSFROZD.WYBDNI:='N'
?};
{? PPSFROZD.WYBDNI<>'T'
|| PPSFROZD.D1:=PPSFROZD.D2:=PPSFROZD.D3:=PPSFROZD.D4:=PPSFROZD.D5:=PPSFROZD.D6:=PPSFROZD.D7:='N'
?};
1


\ppsfrozd_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
::  MOD: IGPTASZE [22.26]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli PPSFROZD.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::  OLD: \ppsfrozd_chk/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
exec('ppsfrozd_modb','!zws_par_ppsr');
__CHK.validate(PPSFROZD,
   $("_a.table(_b,"+$_put+",,
      'FIRMA','PPSFR','KOD','NAZWA','OSW','AUTOKAL','ZAKRES','OL','JML','WYBDNI','D1','D2','D3','D4','D5','D6','D7')"),
   "_a.in_set(_b,'OSW',,'T','N')",
   "_a.in_set(_b,'AUTOKAL',,'T','N')",
   "_a.in_set(_b,'ZAKRES',,'S','H')",
   "_a.in_set(_b,'OL',,'D','T','M','R')",
   "_a.in_set(_b,'JML',,'G','D')",
   " _ok:=1;
      {! _lp:=1 .. 7
      |? _ok
      |! _ok:=_a.in_set(_b,'D%1'[$_lp],,'T','N','')
      !};
      _ok
   ",
   $("_a.table(_b,"+$_put+",,{? PPSFROZD.JML='G' || 'WARTG' || 'WARTD' ?} )"),
   "{? PPSFROZD.JML='G'
     || _wart:=*PPSFROZD.WARTG/60;
        {? _wart<0
        || _a.err_incorrect('Liczba godzin'@,'Wartość nie może być ujemna.'@);
           'WARTG'
        |? (PPSFROZD.OL='D' & _wart>24) | (PPSFROZD.OL='T' & _wart>7*24) | (PPSFROZD.OL='M' & _wart>31*24)
           | (PPSFROZD.OL='R' & _wart>365*24)
        || _a.err_msg('Liczba godzin większa niż wybrany okres.'@);
           'WARTG'
        || ''
        ?}
     |? PPSFROZD.JML='D'
     || _wart:=PPSFROZD.WARTD;
        {? _wart<0
        || _a.err_incorrect('Liczba dni'@,'Wartość nie może być ujemna.'@);
           'WARTD'
        |? PPSFROZD.OL='D'
        || _a.err_msg('Nieprawidłowa jednostka dla wybranego okresu.'@)
        |? (PPSFROZD.OL='T' & _wart>7) | (PPSFROZD.OL='M' & _wart>31)| (PPSFROZD.OL='R' & _wart>365)
        || _a.err_msg('Liczba dni większa niż wybrany okres.'@);
           'WARTD'
        || ''
        ?}
     ?}",
     "{? PPSFROZD.WYBDNI='T'
     || _ld:=0;
        {! _lp:=1 .. 7
        |! _ld+=($('PPSFROZD.D'+$_lp))()='T'
        !};
        {? _ld<>PPSFROZD.WARTD
        || _a.err_msg('Nieprawidłowa liczba zaznaczonych dni tygodnia.'@)
        || ''
        ?}
     || ''
     ?}
   "
)


\ppsfrozd_ppsfr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pola PPSFROZD.PPSFR.
::   WE:
::   WY:
::  OLD: \ppsfrozd_ppsfr_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~(PPSFROZD.PPSFR & PPSFROZD.PPSFR().LIMITY='T')
|| PPSFROZD.AUTOKAL:='N'
?};
exec('ppsfrozd_efld_opt','!zws_par_ppsr')


\ppsfrozd_jml_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pola PPSFROZD.JML.
::   WE:
::   WY:
::  OLD: \ppsfrozd_jml_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_efld_opt','!zws_par_ppsr')


\ppsfrozd_ol_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pola PPSFROZD.OL.
::   WE:
::   WY:
::  OLD: \ppsfrozd_ol_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_efld_opt','!zws_par_ppsr')


\ppsfrozd_wybdni_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pola PPSFROZD.WYBDNI.
::   WE:
::   WY:
::  OLD: \ppsfrozd_wybdni_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_efld_opt','!zws_par_ppsr')


\ppsfrozd_wartd_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pola PPSFROZD.WARTD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_wartd_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
fld(int(fld()));
1


\ppsfrozd_wartd_df
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Format wyświetlania pola PPSFROZD.WARTD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_wartd_df/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
'out_prec=0'


\ppsfrozd_wartd_ef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Format redagowania pola PPSFROZD.WARTD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_wartd_ef/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
'in_prec=0'


\ppsfrozd_wartg_df
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Format wyświetlania pola PPSFROZD.WARTG.
::   WE:
::   WY:
::  OLD: \ppsfrozd_wartg_df/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
'hour_chars=4'


\ppsfrozd_wartg_ef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Format redagowania pola PPSFROZD.WARTG.
::   WE:
::   WY:
::  OLD: \ppsfrozd_wartg_ef/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
'hour_chars=4'


\ppsfrozd_zakres_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pola PPSFROZD.ZAKRES.
::   WE:
::   WY:
::  OLD: \ppsfrozd_zakres_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_efld_opt','!zws_par_ppsr')


\ppsfrozd_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli PPSFROZD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_dolacz_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSFROZD.blank();
exec('ppsfrozd_efld_opt','!zws_par_ppsr','*',PPSFROZD,'RED');
1


\ppsfrozd_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli PPSFROZD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_popraw_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_efld_opt','!zws_par_ppsr','*',PPSFROZD,'RED');
1


\ppsfrozd_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli PPSFROZD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_chk','!zws_par_ppsr',-menu_txt()='popraw')


\ppsfrozd_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie WER tabeli PPSFROZD.
::   WE:
::   WY:
::  OLD: \ppsfrozd_bw/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsfrozd_efld_opt','!zws_par_ppsr','*',PPSFROZD,'RED');
PPSFROZD.display()


\ppsfrozd_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli PPSFROZD.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE: [_a] [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują się pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::  OLD: \ppsfrozd_efld_opt/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(PPSFROZD)
|| _TAB:=_b
|? _tryb='1'
|| _TAB:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? _fld='' | _fld='PPSFR'
|| _val:=0;
:: Na razie nie będziemy obsługiwali prezentacji wirtualnej.
::   {? PPSFROZD.PPSFR
::   || PPSFR.cntx_psh();
::      PPSFR.prefix();
::      _val:=PPSFROZD.PPSFR().LIMITY<>'T';
::      PPSFR.cntx_pop()
::   ?};
   _TAB.efld_opt(_we,'enable=%1'[$_val],,'AUTOKAL')
?};

{? _fld='' | _fld='ZAKRES'
|| _sval:=$(PPSFROZD.ZAKRES='H');
   _TAB.efld_opt(_we,'enable=%1'[_sval],,'OL');
   _TAB.efld_opt(_we,'enable=%1'[_sval],,'JML');
   exec('ppsfrozd_efld_opt','!zws_par_ppsr','1',_TAB,_we,'JML');
   exec('ppsfrozd_efld_opt','!zws_par_ppsr','1',_TAB,_we,'WYBDNI')
?};

{? _fld='' | _fld='OL' | _fld='JML'
|| _sval:=$(PPSFROZD.ZAKRES='H' & PPSFROZD.OL='T' & PPSFROZD.JML='D');
   _TAB.efld_opt(_we,'enable=%1'[_sval],,'WYBDNI');
   _color:={? PPSFROZD.OL='D' & PPSFROZD.JML='D' || '255:0:0' || '0:0:0' ?};
   _TAB.efld_opt(_we,'label_color=%1'[_color],,'OL');
   _TAB.efld_opt(_we,'label_color=%1'[_color],,'JML')
?};

{? _fld='' | _fld='JML'
|| _TAB.efld_opt(_we,'enable=%1,mark=%1'[$(PPSFROZD.ZAKRES='H' & PPSFROZD.JML='G')],,'WARTG');
   _TAB.efld_opt(_we,'enable=%1,mark=%1'[$(PPSFROZD.ZAKRES='H' & PPSFROZD.JML='D')],,'WARTD')
?};

{? _fld='' | _fld='WYBDNI'
|| _sval:=$(PPSFROZD.ZAKRES='H' & PPSFROZD.OL='T' & PPSFROZD.JML='D' & PPSFROZD.WYBDNI='T');
   {! _lp:=1 .. 7
   |! _TAB.efld_opt(_we,'enable=%1'[_sval],,'D%1'[$_lp])
   !}
?};

1


\ppsfr_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Formuła "przed" dla akcji "Usuń" w okienkach wertowania tabeli PPSFR
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_ret:=exec('del_conf','#table');
{? _ret
|| do();
   {? ~PPSFR.del(,1)
   || _ret:=0;
      undo()
   ?};
   end()
?};
_ret

:Sign Version 2.0 jowisz:1045 2023/08/25 11:56:11 88c183e7caed1d52b92cdeeae80abe76aa8fe1ff35b69858a843ddfe17ac7973dbe37cb1bbe2b486d56f545e52ed67d9c2340ac77ea888d948d10a50f5b59e252ae6ad09ca535724a82abf86e040cba49e424a31b08f01c8114fbc61685ba6220448106575c73bef7e9d919932c1b09227aefdb80cf3e293fc741aa8130daf53

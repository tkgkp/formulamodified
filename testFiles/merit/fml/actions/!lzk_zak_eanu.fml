:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zak_eanu.fml
:: Utworzony: 24.07.2017
:: Autor: Mario
::======================================================================================================================
:: Zawartość: Anulowanie dokumentu zakupu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LZK
::# properties=SERVICE
::# parses=exec('parses','!lzk_zak_eanu')
::# kind=WE,   symbol=FAKS,      type=_FAKS,    name=Dokument zakupu,   required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

exec('init_zak','lzk');

_akcja:=_mp.akcja();
_auto:=_akcja<>'Anuluj' & (_mp.isAutoRun() | _mp.isService());

{? ~(var_pres('FAKS',_in)=type_of(null()) & _in.FAKS)
|| _mp.error('Brak wymaganego parametru FAKS.')
|| FAKS.cntx_psh();
   FAKS.prefix();
   {? ~FAKS.seek(_in.FAKS)
   || _mp.error('Nie znaleziono dokumentu.')
   || {? _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref())
      || params_set('mp',_mp);
         {? _akcja='Anuluj' | _auto
         || exec('anuluj','!lzk_zak_eanu',_auto)
         |? _mp.pathTodo()
            | _mp.pathArea() & _akcja=''
         || _win_red:=exec('faks_win_edit','faktury_nag');
            _ff:="params_exec('faks_pozycje_todo','!lzk_zak_eanu')";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
            _ff:="params_exec('faks_anuluj_todo','!lzk_zak_eanu'); 'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Anuluj &dokument'@],_ff);
            _ff:="'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
            FAKS.win_edit(_win_red);
            exec('set_efld_opt','faktury_nag',_win_red);
            FAKS.display();
::          Usunięcie definicji tymczasowych okien
            FAKS.win_edit(''); FAKS.win_edel(_win_red)
         || _mp.error('Nieobsługiwana ścieżka.')
         ?};
         exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
      ||
         exec('who_rlock_faks','faktury_nag')
      ?}
   ?};
   FAKS.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| {? exec('FindAndGet','#table',FAKS,_in.FAKS,,"FAKS.T().KOR='T'",0)
   || 'Anuluj korektę zakupu: %1'@@[exec('record','#to_string',_in.FAKS)]
   || 'Anuluj dokument zakupu: %1'@@[exec('record','#to_string',_in.FAKS)]
   ?}
|| ''
?}


\faks_anuluj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument zakupu - Anuluj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LZK_ZAK_EANU';
_params.UIDREF:=FAKS.uidref();
_params.AKCJA:='Anuluj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'FAKS',FAKS.ref());

exec('mp_run','#b__box',_params)


\faks_anuluj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument zakupu - Anuluj z todo
::   WE: params_get()   - ustawiane w exec('main','!lzk_zak_eanu')
::----------------------------------------------------------------------------------------------------------------------
params_exec('anuluj','!lzk_zak_eanu')


\faks_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument zakupu - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('pozycje_fak','faktury_poz',0);
''


\anuluj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument zakupu - Anulowanie dokumentu
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::   WE: params_get()   - ustawiane w exec('main','!lzk_zak_eanu')
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_mp:=params_get().mp;

_result:=0;

{? ~exec('czy_z_ok','okresy',-3)
|| 1
|? FAKS.STAT_REJ='A'
|| {? ~_auto || FUN.info('Dokument został anulowany.'@) ?};
   _result:=1;
   _mp.done()
|? FAKS.ZAK='T'
|| {? ~_auto || FUN.info('Dokument został zaksięgowany.'@) ?};
   _result:=1;
   _mp.done()
|? FAKS.STAT_REJ='N'
|| FUN.info('Nie zakończono jeszcze rejestracji dokumentu.\nAnulowanie niemożliwe.'@);
   _mp.error('Nie zakończono jeszcze rejestracji dokumentu. Anulowanie niemożliwe.');
   _result:=1
|? FAKS.KORYG='T'
|| {? ~_auto || FUN.info('Dokument został skorygowany.\nAnulowanie niemożliwe.'@) ?};
   _result:=1;
   _mp.done()
|? exec('przyFaks','faktury_wspolne',FAKS.ref())
|| {? ~_auto || FUN.info('Pozycje dokumentu powiązane z dokumentem magazynowym.\nAnulowanie niemożliwe.'@) ?};
   _result:=1;
   _mp.done()
|? FAKS.INTRA='T'
|| FUN.info('Dokument użyty w deklaracji Intrastat.\n'
      'Należy usunąć dokument z deklaracji przed anulowaniem dokumentu.'@);
   _result:=1;
   _mp.done()
|? FAKS.T().VALACC<>null() & ~exec('validate','wzorce',FAKS.T().VALACC,FAKS,FAKS.ref())
|| _result:=0
|? _auto | FUN.ask('Anulować dokument %1?\nOperacja nieodwracalna.'@[FAKS.SYM])
||
  {? exec('faks_anuluj','faktury_nag')
  ||
     _result:=1;
     _mp.done()
  ||
     _result:=0
  ?}
?};
_result


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| FAKS.cntx_psh();
   FAKS.use(ref_name(_in.FAKS));
   FAKS.prefix();
   {? FAKS.seek(_in.FAKS)
   || __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LZK';
      _args.AR:=FAKS.AR;
      _args.AM:=FAKS.AM;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FAKS.cntx_pop()
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 319c89b7d801e285cc37d6cdffccfd651c73c5574f91fb149e381be1a8f1a78ac8c9d7dccd9f5bf9c9ec19922b544e67747f17d3640fcea8099cfbfdc5e27b8391e82266c4e7615fd497ccaef10c32ba1139b39d2b51bafad24250e56743e5c77e4c8c175b112cdaa4dd4be80f0e52e54a9b1b9843bc58adafd195beff6adc3f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pzrp.fml
:: Utworzony: 29.07.2021
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PZRP - Zestawienia roczne.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Zestawienia roczne - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zrpn','dane_startowe');
exec('__WND','#object');

ZRPP.cntx_psh();
ZRPP.f_clear();
ZRPN.cntx_psh();
ZRPN.f_clear();
ZRPN.index('VIEW');
ZRPN.prefix(exec('ref_firma','ustawienia'));
ZRPN.win_sel(exec('zrpn_wnd','!zws_par_pzrp'));
ZRPN.select();
ZRPN.cntx_pop();
ZRPP.cntx_pop();
~~


\zrpn_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Formuła buduje okno grupowe do obsługi zestawień rocznych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_grp:=ZRPN.grp_make('Definicja zestawień rocznych'@,,'#zrpngrp',,,,,'html_maximized');

:: Nagłówek (okno górne).
ZRPN.grp_sel(_grp,,'WER',,"
   {? ZRPN.WIDOK=1
   || tab_show(1,'pozycje');
      tab_hide(2,,'pozycje');
      grp_disp(ZRPP,'WER1')
   |? ZRPN.WIDOK=2
   || tab_hide(1,,'pozycje');
      tab_show(2,'pozycje');
      grp_disp(ZRPP,'WER2')
   || tab_hide(,1,'pozycje')
   ?}
",,,7,,,,,'maximized_with_title');
ZRPN.grp_splt(_grp,,'horizontal','pozycje');

: Pozycje - zakładka I (widok 1).
ZRPN.grp_sel(_grp,ZRPP,'WER1','Składniki',,,,,"
   ZRPP.win_edit('REDS');
   ZRPP.index('ZRPNRN');
   ZRPP.prefix(ZRPN.ref())
",,,,'maximized');

: Pozycje - zakładka II (widok 2).
ZRPN.grp_sel(_grp,ZRPP,'WER2','Składniki',,,,,"
   ZRPP.index('TREE');
   ZRPP.prefix(ZRPN.ref())
",,,,'maximized');

_grp


\zrpp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Obsługa akcji "Rekord - po" tabeli ZRPP
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zrpp_chk','portal_zestrocz',var_pres('_a')=type_of(0) & _a)


\zrpp_dolaczg_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Obsługa akcji "Dołącz grupę" w oknie WER2 tabeli ZRPP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZRPP.blank();
ZRPP.win_edit('REDG');
{? ZRPP.edit("exec('zrpp_ae','!zws_par_pzrp',0)")
|| ZRPP.add()
?}


\zrpp_dolaczs_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Obsługa akcji "Dołącz (składniki)" w oknach WER1 / WER2 tabeli ZRPP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ZRPN.WIDOK=2
|| ZRPP.win_edit('REDS');
   _zrpp:={? ZRPP.ZRPP || ZRPP.ZRPP || #ZRPP.ref() ?}
|| _zrpp:=0
?};

R.cntx_psh();
R.index('RUBKOD');
R.prefix();
R.f_set('RN',,'"R".REFERENCE not in (select ZRPP.R from ZRPP where ZRPP.R is not null and ZRPP.ZRPN=:_a)',ZRPN.ref());
_acr:='ZRPP_R_WYBX';
{? (_ws:=__WND.SEL.get(R,_acr))=''
|| _ws:=R.mk_sel('Rubryki'@,'N',,-_acr,,,,,'U');
   R.win_fld(_ws,,'RN',,,,,,MS.name(R,'RN'),,MS.comment(R,'RN'));
   R.win_fld(_ws,,'RT',,,,,,MS.name(R,'RT'),,MS.comment(R,'RT'));
   R.win_act(_ws,,'Formuła','Wybierz'@@,,,
      "  ZRPP.blank();
         ZRPP.ZRPP:=params_get().zrpp;
         ZRPP.R:=R.ref();
         exec('add','#table',ZRPP)
      ",
      "  {? ~R.sel_size()
         || sel_exit()
         ?}
      ",
      1,1,
      "FUN.ask('Czy dodać zaznaczone rubryki?'@)",
      "sel_exit()",
      'W'
   );
   __WND.SEL.put(R,_acr,_ws)
?};
R.win_sel(_ws);
params_set('zrpp',_zrpp);
R.select();
R.f_clear();
R.cntx_pop();
~~


\zrpp_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Obsługa akcji "Popraw" w oknach WER1 / WER2 tabeli ZRPP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_widok:=ZRPN.WIDOK;
_edit:="{? ZRPP.edit(""exec('zrpp_ae','!zws_par_pzrp',1)"") || ZRPP.put() ?}";
{? _widok=1 | (_widok=2 & ZRPP.ZRPP)
|| ZRPP.win_edit('REDS');
   R.cntx_psh();
   R.index('RUBKOD');
   R.prefix();
   R.f_set('RN',,'"R".REFERENCE not in (select ZRPP.R from ZRPP where ZRPP.ZRPN=:_a and ZRPP.R<>:_b)',
      ZRPN.ref(),ZRPP.R
   );
   _edit();
   R.f_clear();
   R.cntx_pop()
|? _widok=2 & ZRPP.ZRPP=0
|| ZRPP.win_edit('REDG');
   _edit()
?};
~~


\zrpp_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Obsługa akcji "Wyświetl" w oknie WER2 tabeli ZRPP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZRPP.win_edit({? ZRPP.ZRPP || 'REDS' || 'REDG' ?});
ZRPP.display()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 7858b7f6da116da4f9672de48abdd89df462b12993ca4a20ddebdcc968d31a6b24bbd0cf0180877639d0796c3f90a829a363c280491dae78ecc1defded821eeadc6dceab5d11c8cf81e3c5dc86f1a3230c49de156db39ac2085177a6dc24d04c14cc143ce927e1bbe82e86cf85c8cfe88dc4b653b1587b103d0acba7fc83d72d

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orob.fml
:: Utworzony: 18.05.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_OROB - Rejestracja obowiązków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja obowiązków - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \init/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_ezk_orob')

|? _mp.isMicro()
|| {? _do='VIEW'
   || _mp.keyRef(_id);
      exec('select','!pkd_ezk_orob',_in.P,_mp);
      _mp.delRef(_id);
      _mp.cancel()
   |? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@[_mp.buf_act.UID,_do]
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orob',_in.P,_mp);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja obowiązków - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj pełnione obowiązki: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj pełnione obowiązki: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj pełnione obowiązki: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj pełnione obowiązki'@@
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie pracownika
::       _b - wskazanie menadżera procesów
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!pkd_ezk_orob');

{? var_pres('_a')<>type_of(null) | _a=null
|| return(_err_msg)
?};

OSOBA.cntx_psh();
P.cntx_psh();
P.prefix();
{? ~P.seek(_a)
|| P.cntx_pop();
   OSOBA.cntx_pop();
   return(_err_msg)
?};
P.OSOBA();

{? _b.isMicro()
|| P_PO.actions_grayed('WER','Z:Z')
?};

: pokaż obowiązki
P_PO.cntx_psh();
P_PO.index('WIDOK');
P_PO.prefix(_a);
P_PO.win_sel('GRP');
_result:=P_PO.select();
P_PO.cntx_pop();

P_PO.actions_grayed('WER');

: porządki
P.cntx_pop();
OSOBA.cntx_pop();

_result


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań
::       - w ramach obszaru roboczego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=P_PO
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestracja pełnionych obowiązków niemożliwa.'@


\p_po_co_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wyświetleniem pola CO tabeli P_PO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wnd:=P_PO.win_edit('?');
_opt:='enable=%1,mark=%1'[$(P_PO.CO='T')];
P_PO.efld_opt(_wnd,_opt,,'OD');
P_PO.efld_opt(_wnd,_opt,,'DO');
_opt:='enable=%1,mark=%1'[$(PAR_SKID.get(313)='T')];
P_PO.efld_opt(_wnd,_opt,EDIT_VAR,'PO_PSTO');
1


\p_po_okno_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Akcja "przed" okna WER tabeli P_PO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P_PO.win_edit('RED');
1


\p_po_wyd_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [2011]
:: OPIS: Przed wyświetleniem pola PO_WYD zmiennej EDIT_VAR.
::  OLD: \po_wyd_db/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.PO_WYD:=P_PO.WYDZIAL().SYMBOL;
1


\p_po_wyd_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [2011]
:: OPIS: Przed redakcją pola PO_WYD zmiennej EDIT_VAR. Blokuje edycję jeśli określone zostało stanowisko.
::   WY: 0/1 - zależnie od tego, czy stanowisko jest okreslone (1)
::  OLD: \po_wyd_be/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.PO_ST=null & EDIT_VAR.PO_PSTO=null


\p_po_wyd_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [2011]
:: OPIS: Po redakcji pola PO_WYD zmiennej EDIT_VAR. Ustala wartość pola WYDZIAL w buforze tabeli P_PO.
::   WY: ref lub null - zależnie od tego, czy podany symbol poprawnie identyfikuje jednostkę organizacyjną.
::  OLD: \po_wyd_ae/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
P_PO.WYDZIAL:=exec('szukaj_ud_skl','schemat','PODZORG',EDIT_VAR.PO_WYD)


\p_po_st_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [2011]
:: OPIS: Przed wyświetleniem pola PO_ST zmiennej EDIT_VAR.
::  OLD: \po_st_db/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.PO_ST:=exec('szukaj_sto','stanprac',P_PO.WYDZIAL,P_PO.ST)


\p_po_st_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [2011]
:: OPIS: Przed redakcją pola PO_ST zmiennej EDIT_VAR. Blokuje edycję jeśli nie jest określona jednostka organizcyjna.
::   WY: 0/1 - zależnie od tego, czy jednostka organizacyjna jest określona (1)
::  OLD: \po_st_be/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.PO_WYD<>''


\p_po_st_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [2011]
:: OPIS: Po redakcji pola PO_ST zmiennej EDIT_VAR. Ustala wartość pola ST w buforze tabeli P_PO.
::  OLD: \po_st_ae/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
P_PO.ST:=EDIT_VAR.PO_ST().STN;
1


\p_po_psto_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KO [12.30]
:: OPIS: Akcja przed wyświetleniem zmiennej EDIT_VAR.PO_PSTO
::  OLD: \pw_popsto/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
_sto:=exec('szukaj_sto','stanprac',P_PO.WYDZIAL,P_PO.ST);
EDIT_VAR.PO_PSTO:=exec('szukaj_psto','stanprac',_sto,P_PO.POZWORG)


\p_po_psto_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KO [12.30]
:: OPIS: Akcja przed redakcją zmiennej EDIT_VAR.PO_PSTO
::  OLD: \pr_popsto/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.PO_ST<>null


\p_po_psto_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KO [12.30]
:: OPIS: Akcja po redakcji zmiennej EDIT_VAR.PO_PSTO
::  OLD: \po_popsto/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
P_PO.POZWORG:=EDIT_VAR.PO_PSTO().POZWORG;
1


\p_po_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [2011]
:: OPIS: Sprawdza poprawnosc edytowanych danych.
::   WE:
::  OLD: \sprawdz/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
{? __CHK.record(P_PO,,'TYP')<>''
|| return('TYP')
?};

{? P_PO.CO='T'
|| {? (_chk:=__CHK.record(P_PO,,'OD','DO'))<>''
   || return(_chk)
   |? P_PO.DO<P_PO.OD
   || exec('alert','overlap');
      return('OD')
   ?}
?};

{? (_chk:=__CHK.record(P_PO,,'WYDZIAL','ST'))<>''
|| return(
      {? _chk='WYDZIAL' || 'PO_WYD'
      |? _chk='ST' || 'PO_ST'
      || _chk
      ?}
   )
?};

{? PAR_SKID.get(313)='T' & __CHK.record(P_PO,,'POZWORG')<>''
|| return('PO_PSTO')
?};

__CHK.index(P_PO,-menu_txt()='popraw')

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 4f6be1ad65586071fc404622cbc43992eec0528d77a93e0ffccfbfdd161ec78d73d057169173c74e32ac7013902fdf1a983b6d7c7c87064e5def6a2367183c2dd94636eaa6f4c80c1e9e40be41c34d69da50997ee7187de61008b5c29a746a927dc62aaff6b3da4b3450c8661e3816827909f793f22a35142b4111cce3573f01

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdw_pppp.fml
:: Utworzony: 30.11.2017
:: Autor: AFI
::======================================================================================================================
:: Zawartość: Formuły czynności PSZ_SDW_PPPP - Przegl. planów podwładnych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [18.02]
:: OPIS: Formuła główna czynności - Przegl. planów podwładnych
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('env_wt','psz')
|| return()
?};

OSOBA.cntx_psh();
_POD:=exec('prac_pod','stanprac',P.OSOBA,'TYPPOZ','DOSTINF');
_POD.index(_POD.ndx_tmp(,,'P_SQL',,));
_POD.prefix();
OSOBA.cntx_pop();

SZK_PLAN.cntx_psh();
SZK_PLAN.index('SZK_PNAG');
SZK_PLAN.prefix(exec('ref_firma','ustawienia'),'T','T',);

{? SZK_PLAN.first()
|| SZK_PSZK.cntx_psh();
   SZK_PSZK.index('PLAN');
   exec('wt_pszk_clean','psz',3);
   WT_PSZK.index('KEY');
   WT_PSZK.prefix(app_info('web_tcid'),app_info('web_sesid'),app_info('web_tabid'),);
   {!
   |? SZK_PSZK.prefix(SZK_PLAN.ref());
      {? SZK_PSZK.first()
      || {!
         |?
            {? _POD.find_key($SZK_PSZK.SZK_ZAP().P)
            || WT_PSZK.blank();
               WT_PSZK.SZK_PLAN:=SZK_PLAN.ref();
               WT_PSZK.PRAC:=SZK_PSZK.SZK_ZAP().P().OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE;
               WT_PSZK.WYDZ:=P.WYDZIAL().SYMBOL;
               WT_PSZK.TEMAT:=SZK_PSZK.SZK_ZAP().TEMAT;
               WT_PSZK.ROK:=SZK_PSZK.SZK_ZAP().ROK;
               WT_PSZK.KWARTAL:=SZK_PSZK.SZK_ZAP().KWARTAL;
               WT_PSZK.MC:=SZK_PSZK.SZK_ZAP().MC;
               WT_PSZK.STATZAP:=SZK_PSZK.SZK_ZAP().WER_WYN().NAZWA;
               {? SZK_PSZK.SZK_ZAP().SZK_PRAC
               || WT_PSZK.STATSZK:=SZK_PSZK.SZK_ZAP().SZK_PRAC().SZKOL().STATUS().NAZWA
               || WT_PSZK.STATSZK:='niezaplanowane'@
               ?};
               WT_PSZK.add()
            ?};
            SZK_PSZK.next()
         !}
      ?};
      SZK_PLAN.next()
   !};
   SZK_PSZK.cntx_pop()
?};
SZK_PLAN.web_select('WEBGR');
SZK_PLAN.cntx_pop();
~~


\szk_plan_webgr_wt_pszk_wer_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [18.02]
:: OPIS: "Przed obsługą" okna WER tabeli WT_PSZK w ramach okna grupowego WEBGR tabeli SZK_PLAN - ustala środowisko
::       pracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:=web_win_ref('pp_nag');
{?_ref=~~ || _ref:=null() ?};
WT_PSZK.index('KEY');
WT_PSZK.prefix(app_info('web_tcid'), app_info('web_sesid'), app_info('web_tabid'), _ref);
WT_PSZK.first();
~~

:Sign Version 2.0 jowisz:1045 2020/04/03 17:04:51 29b9cbd5c045185373f3b178ef6f22a6a8af4512df54e7a7c6bdc4aee80af16cf293ada304a4de4cf588815cdaf02206efd00054708e0c5b7e8673b2c4621cf54844aa1cca0c65d12f656a072b3f088ff4bd507e21e744d462613eacd3b10e2c0a80381bfa49d130e635021a5c1fda11c64ac704ca324065288306d6a72a3643

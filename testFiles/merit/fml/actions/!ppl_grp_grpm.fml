:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_grp_grpm.fml
:: Utworzony: 08.05.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PPL_GRP_GRPM - Gr. rej. premii uznaniowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Gr. rej. premii uznaniowych - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_atr:=exec('pm_atr','pm_wspolne','Grupowa rejestracja premii uznaniowych nie jest możliwa.'@);
{? ~_atr.OK
|| return()
?};

_cfg:=obj_new(
   'ROK','MSC',
   'pm_nag_last_mod',
   'pm_prem_last_mod',
   'pm_nag_ref',
   'grF',
   'grN',
   'grS',
   'SUMA_fml'
);
_cfg.ROK:=O.R;
_cfg.MSC:=O.M;
_cfg.pm_nag_last_mod:=0;
_cfg.pm_prem_last_mod:=0;
_cfg.pm_nag_ref:=null();
:: Pola wykorzystywane przy obsłudze akcji grupowych.
_cfg.grF:=0;
_cfg.grN:=0;
_cfg.grS:='';

params_set(
   'cfg',_cfg,
   'PUPR',exec('dostepne_p','schemat','PPL','*T',,)
);

exec('pm_nag_hdr_sel','!ppl_grp_grpm');

CP.cntx_psh();
F_ZATR.cntx_psh();
KK.cntx_psh();
OSOBA.cntx_psh();
P.cntx_psh();
STN.cntx_psh();
UD_SKL.cntx_psh();
R.cntx_psh();
R.prefix();
PM_PREM.cntx_psh();
PM_PREM.win_edit('RED');
PM_RODZ.cntx_psh();
PM_RODZ.prefix();
PM_BUDZ.cntx_psh();
PM_BUDZ.prefix();
PM_NAG.cntx_psh();
PM_NAG.prefix();
PM_NAG.win_sel(exec('wnd','!ppl_grp_grpm'));
PM_NAG.win_edit('RED');
PM_NAG.select();
PM_NAG.cntx_pop();
PM_BUDZ.cntx_pop();
PM_RODZ.cntx_pop();
PM_PREM.cntx_pop();
R.cntx_pop();
UD_SKL.cntx_pop();
STN.cntx_pop();
P.cntx_pop();
OSOBA.cntx_pop();
KK.cntx_pop();
F_ZATR.cntx_pop();
CP.cntx_pop();

~~


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła buduje okno grupowe do grupowego wprowadzania premii uznaniowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';

_grp:=PM_NAG.grp_make('Grupowa rejestracja premii uznaniowych'@,,'pm_nag_grp',,,
   "  PM_NAG.sel_adel();
      PM_NAG.f_clear();
      PM_PREM.sel_adel();
      PM_PREM.f_clear();
      1
   "
);

PM_NAG.grp_sel(_grp,PM_NAG,'WER',,
   "  params_set(_par:=params_get());
      {? ~PM_NAG.sel_size()
      || _cfg:=_par.cfg;
         _lm:=exec('last_mod_get','#table',PM_NAG);
         {? _cfg.pm_nag_last_mod<>_lm
         || {? _cfg.pm_nag_last_mod
            || PM_NAG.f_rfresh();
               win_disp()
            ?};
            _cfg.pm_nag_last_mod:=_lm
         ?}
      ?};
      PM_PREM.actions_grayed('WER',{? PM_NAG.AKC='T' || 'DPU:D' || '' ?});
      grp_disp(PM_PREM,'WER')
   ",,,,
   "  {? ~PM_NAG.sel_size()
      || _par:=params_get();
         _cfg:=_par.cfg;
         PM_NAG.prefix();
         PM_NAG.f_set('OSOBA(NAZWISKO),OSOBA(PIERWSZE),OSOBA(PESEL),R(RN)',,
            '\"PM_NAG\".\"ROK\"=:_a and \"PM_NAG\".\"MSC\"=:_b and '
            '\"PM_NAG\".\"P\" in (select REF from :_c)',
            _cfg.ROK,_cfg.MSC,_par.PUPR
         );
         {? ~_cfg.pm_nag_ref | ~PM_NAG.f_seek(_cfg.pm_nag_ref)
         || PM_NAG.f_first()
         ?}
      ?}
   ",
   "  params_get().cfg.pm_nag_ref:=PM_NAG.ref()
   ",,,_mode,'pm_nag_grp_nag'
);
PM_NAG.grp_splt(_grp,,'horizontal','prem',15);
PM_NAG.grp_sel(_grp,PM_PREM,'WER',,
   "  {? ~PM_PREM.sel_size()
      || _cfg:=params_get().cfg;
         _lm:=exec('last_mod_get','#table',PM_PREM);
         {? _cfg.pm_prem_last_mod<>_lm
         || {? _cfg.pm_prem_last_mod
            || PM_PREM.f_rfresh();
               win_disp()
            ?};
            _cfg.pm_prem_last_mod:=_lm
         ?}
      ?}
   ",,,,
   "  {? grp_empty(PM_NAG,'WER')
      || return('#disable')
      ?};
      {?  ~_a & PM_PREM.sel_size()
      || PM_PREM.sel_adel()
      ?};
      {? ~PM_PREM.sel_size()
      || PM_PREM.prefix();
         PM_PREM.f_set('OSOBA(NAZWISKO),OSOBA(PIERWSZE),OSOBA(PESEL)',,
            '\"PM_PREM\".\"PM_NAG\"=:_a and '
            '\"PM_PREM\".\"P\" in (select REF from :_b)',
            PM_NAG.ref(),params_get().PUPR
         )
      ?}
   ",,,,_mode,'pm_nag_grp_prem'
);

_grp


::======================================================================================================================
:: Obsługa pól i akcji tabeli PM_NAG.
::======================================================================================================================


\pm_nag_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
_txt:=exec('pm_nag_modify_b','pm_wspolne',_par.cfg.ROK,_par.cfg.MSC);
{? _txt<>''
|| FUN.info(_txt+'\n'+'Dołączenie nagłówka nie jest możliwe.'@);
   return()
?};

_sel:=0;
PM_BUDZ.cntx_psh();
PM_BUDZ.prefix();
PM_BUDZ.f_set(
   'OSOBA(NAZWISKO),OSOBA(PIERWSZE),OSOBA(PESEL),R(RN),OD',,
   '("PM_BUDZ"."LIMIT"=\'N\' or "PM_BUDZ"."POZOST">0) and '
   '"PM_BUDZ"."OD"<=to_date(:_e) and ("PM_BUDZ"."DO" is null or "PM_BUDZ"."DO">=to_date(:_d)) and '
   '"PM_BUDZ"."P" in (select REF from :_a) and '
   '"PM_BUDZ"."REFERENCE" not in (select PM_NAG.PM_BUDZ from PM_NAG where PM_NAG.ROK=:_b and PM_NAG.MSC=:_c)',
   _par.PUPR,_par.cfg.ROK,_par.cfg.MSC,date(_par.cfg.ROK,_par.cfg.MSC,1),date(_par.cfg.ROK,_par.cfg.MSC,0)
);
PM_BUDZ.win_sel('WYB');
_sel:=PM_BUDZ.select();
PM_BUDZ.f_clear();
PM_BUDZ.cntx_pop();

{? _sel
|| grp_disp(PM_NAG,'WER')
?};
~~


\pm_nag_popraw_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Popraw - grupa przed" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set('text','Czy na pewno chcesz w zaznaczonych wierszach zmienić treść \"Uwagi\" na pusty napis?'@);

PM_NAG.cntx_psh();
PM_NAG.UWAGI:='';
PM_NAG.win_edit('UWAGI');
{? _ret:=PM_NAG.edit("PM_NAG.UWAGI<>'' | FUN.ask(params_get().text)")
|| {? PM_NAG.UWAGI='' |
      FUN.ask('Czy na pewno chcesz w zaznaczonych wierszach zmienić treść "Uwagi" na:\n%1\n?'@ [PM_NAG.UWAGI])
   || _cfg:=params_get().cfg;
      _cfg.grS:=PM_NAG.UWAGI;
      _cfg.grF:=0
   || _ret:=0
   ?}
?};
PM_NAG.cntx_pop();
_ret


\pm_nag_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_gr:=PM_NAG.sel_size();
{? _gr
|| _cfg:=params_get().cfg;
   {? PM_NAG.AKC='T'
   || _cfg.grF:=1
   || _uwagi:=_cfg.grS;
      {? PM_NAG.UWAGI<>_uwagi
      || PM_NAG.UWAGI:=_uwagi;
         PM_NAG.put()
      ?}
   ?}
|? PM_NAG.AKC='T'
|| FUN.info('Nagłówek zaakceptowany. Zmiany niemożliwe.'@)
|| R.cntx_psh();
   R.prefix();
   R.f_set('RN',,'"R"."REFERENCE" in (select PM_RODZ.R from PM_RODZ where PM_RODZ.PM_DYSP=:_a)',PM_NAG.PM_DYSP);
   {? PM_NAG.edit("exec('pm_nag_ae','!ppl_grp_grpm')")
   || PM_NAG.put()
   ?};
   R.f_clear();
   R.cntx_pop()
?};
~~


\pm_nag_popraw_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Popraw - grupa po" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? params_get().cfg.grF
|| FUN.info('Poprawienie wszystkich zaznaczonych wierszy nie było możliwe.'@)
?}


\pm_nag_usun_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - grupa przed" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('del_ask','#table')
|| params_get().cfg.grF:=0;
   1
?}


\pm_nag_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - przed" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_gr:=PM_NAG.sel_size();
{? PM_NAG.AKC='T'
|| {? _gr
   || params_get().cfg.grF:=1
   || FUN.info('Nagłówek premii uznaniowych jest już zaakceptowany.'@+'\n'+'Usunięcie nie jest możliwe.'@)
   ?};
   0
|? _gr | exec('del_ask','#table')
|| PM_NAG.del(,1)
?}


\pm_nag_usun_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - grupa po" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? params_get().cfg.grF
|| FUN.info('Usunięcie wszystkich zaznaczonych wierszy nie było możliwe.'@)
?}


\pm_nag_mc_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Miesiąc - przed" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
_cfg:=_par.cfg;

_ret:=exec('wybierz_rok_miesiac','daty',_cfg.ROK,_cfg.MSC);
{? _ret.R<>0 & _ret.M<>0 & (_cfg.ROK<>_ret.R | _cfg.MSC<>_ret.M)
|| _cfg.ROK:=_ret.R;
   _cfg.MSC:=_ret.M;
   exec('pm_nag_hdr_sel','!ppl_grp_grpm');
   grp_disp(PM_NAG,'WER',1)
?};
~~


\pm_nag_akc_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Akceptuj/Wycofaj - grupa przed" w oknie WER tabeli PM_NAG.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Akceptuj.
::                         0 - Wycofaj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=var_pres('_a')=type_of(0) & _a;
{? {? _tryb
   || FUN.ask('Czy na pewno chcesz zaakceptować zaznaczone nagłówki premii uznaniowych?'@)
   || FUN.ask('Czy na pewno chcesz wycofać akceptację zaznaczonych nagłówków premii uznaniowych?'@)
   ?}
|| KOMM.init();
   1
?}


\pm_nag_akc_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Akceptuj/Wycofaj - przed" w oknie WER tabeli PM_NAG.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Akceptuj.
::                         0 - Wycofaj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=var_pres('_a')=type_of(0) & _a;
_gr:=PM_NAG.sel_size();
{? _gr |
   {? _tryb
   || FUN.ask('Czy na pewno chcesz zaakceptować bieżący nagłówek premii uznaniowych?'@)
   || FUN.ask('Czy na pewno chcesz wycofać akceptację bieżącego nagłówka premii uznaniowych?'@)
   ?}
|| exec('pm_nag_akc','pm_wspolne',_tryb,_gr)
?}


\pm_nag_akc_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Akceptuj/Wycofaj - grupa po" w oknie WER tabeli PM_NAG.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Akceptuj.
::                         0 - Wycofaj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KOMM.select('Raport z przetwarzania grupowego'@,'');
~~


\pm_nag_blok_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Zablokuj/Odblokuj - grupa przed" w oknie WER tabeli PM_NAG.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Blokuj.
::                         0 - Odblokuj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=var_pres('_a')=type_of(0) & _a;
{? {? _tryb
   || FUN.ask(
         'Czy na pewno chcesz zablokować możliwość zmian w zaznaczonych nagłówkach (i pozycjach) premii uznaniowych?'@
      )
   || FUN.ask(
         'Czy na pewno chcesz odblokować możliwość zmian w zaznaczonych nagłówkach (i pozycjach) premii uznaniowych?'@
      )
   ?}
|| KOMM.init();
   1
?}


\pm_nag_blok_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Zablokuj/Odblokuj - przed" w oknie WER tabeli PM_NAG.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Blokuj.
::                         0 - Odblokuj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=var_pres('_a')=type_of(0) & _a;
_blok:={? _tryb || 'T' || 'N' ?};

{? PM_NAG.BLOK=_blok
|| return(1)
?};

_gr:=PM_NAG.sel_size();

{? PM_NAG.AKC<>'T'
|| _txt:='Nagłówek nie jest zaakceptowany. Zablokowanie niemożliwe.'@;
   {? _gr
   || KOMM.set_root(exec('record','#to_string',PM_NAG.P));
      KOMM.sect_beg(exec('record','#to_string',PM_NAG.R));
      KOMM.add(_txt);
      KOMM.sect_end();
      KOMM.sect_end()
   || FUN.info(_txt)
   ?}

|? _gr |
   {? _tryb
   || FUN.ask(
         'Czy na pewno chcesz zablokować możliwość zmian w bieżącym nagłówku (i jego pozycjach) premii uznaniowych?'@
      )
   || FUN.ask(
         'Czy na pewno chcesz odblokować możliwość zmian w bieżącym nagłówku (i jego pozycjach) premii uznaniowych?'@
      )
   ?}
|| PM_NAG.BLOK:=_blok;
   PM_NAG.put()
?}


\pm_nag_blok_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Zablokuj/Odblokuj - grupa po" w oknie WER tabeli PM_NAG.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Blokuj.
::                         0 - Odblokuj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KOMM.select('Raport z przetwarzania grupowego'@,'');
~~


\pm_nag_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER tabeli PM_NAG.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _ag:='';
   PM_NAG.actions_grayed('WER',
      {? PM_NAG.sel_size()
      || ''
      || _akc:=PM_NAG.AKC='T';
         _blok:=PM_NAG.BLOK='T';
         {? exec('pm_nag_modify_b','pm_wspolne',PM_NAG.ROK,PM_NAG.MSC)<>'' || 'D' || '' ?}+
         {? _akc | _blok || 'PU' || '' ?}+
         {? _blok
         || 'AB(Z)'
         |? ~_akc
         || 'A(W)B'
         || 'A(A)B(O)'
         ?}
      ?}
   )
?};
~~


\pm_nag_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(PM_NAG,,'R')


\pm_nag_hdr_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=params_get().cfg;
_ws:=PM_NAG.win_sel('?');
PM_NAG.win_sel('WER');
PM_NAG.hdr_sel();
PM_NAG.hdr_sel(': %1' [date(_cfg.ROK,_cfg.MSC,1)$8]);
PM_NAG.win_sel(_ws);
~~


::======================================================================================================================
:: Obsługa akcji tabeli PM_BUDZ (podczas dodawania rekordów do tabeli PM_NAG).
::======================================================================================================================


\pm_budz_wybierz_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - grupa przed" w oknie WYB tabeli PM_BUDZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FUN.ask('Czy na pewno chcesz dołączyć nagłówki premii dla wybranych budżetów (dysponentów i rodzajów)?'@)


\pm_budz_wybierz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - przed" w oknie WYB tabeli PM_BUDZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
_cfg:=_par.cfg;
PM_NAG.blank();
PM_NAG.PM_BUDZ:=PM_BUDZ.ref();
PM_NAG.ROK:=_cfg.ROK;
PM_NAG.MSC:=_cfg.MSC;
{? exec('pm_nag_add','pm_wspolne',1)
|| _par.cfg.pm_nag_ref:=PM_NAG.ref();
   1
?}


\pm_budz_wybierz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - po" w oknie WYB tabeli PM_BUDZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~PM_BUDZ.sel_size()
|| sel_exit()
?}


\pm_budz_wybierz_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - grupa po" w oknie WYB tabeli PM_BUDZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
sel_exit()


::======================================================================================================================
:: Obsługa pól i akcji tabeli PM_PREM.
::======================================================================================================================


\pm_prem_pm_nag_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Wartość początkowa pola PM_PREM.PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PM_NAG.ref()


\pm_prem_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli PM_PPREM.
::   WE: _a [STRING] - Zakres rekordów źródłowych:
::                      'D' - Pracownicy domyślni.
::                      'W' - Wszyscy pracownicy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='D' | _a='W')
|| _tryb:=_a
|| return()
?};

_where:='';
{? _tryb='D'
|| PM_DOMP.cntx_psh();
   PM_DOMP.index('UNIQUE');
   PM_DOMP.prefix(PM_NAG.PM_DYSP);
   {? PM_DOMP.first()
   || {!
      |? _where+='\'%1\',' [$PM_DOMP.P];
         PM_DOMP.next()
      !};
      _where:='and "P"."REFERENCE" in (%1)' [_where-1]
   ?};
   PM_DOMP.cntx_pop();
   {? _where=''
   || FUN.info('Brak pracowników domyślnych.'@);
      return()
   ?}
?};

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PPL';
_args.SQL_WHERE:=
   '"P"."REFERENCE" not in (select PM_PREM.P from PM_PREM where PM_PREM.PM_NAG=\'%1\') ' [$PM_NAG.ref()]+
   _where;
_args.WIELU:=1;
_args.GRP_IDENT:='p_wybierz';

_ret:=exec('wybierz','pracownik',_args);
{? _ret.STATUS<>''
|| FUN.info(_ret.STATUS);
   return()
|? ~_ret.P.first()
|| return()
?};

P.cntx_psh();
P.prefix();
{!
|? {? P.seek(_ret.P.UID)
   || PM_PREM.blank();
      {? exec('add','#table',PM_PREM)
      || PM_PREM.f_add()
      ?}
   ?};
   _ret.P.next()
!};
P.cntx_pop();

~~


\pm_prem_popraw_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Popraw - grupa przed" w oknie WER tabeli PM_PREM.
::       Uwaga: Dla premii z limitem budżetu przetwarzanie również odbywa się w bieżącej formule.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PM_PREM.cntx_psh();
params_set(_par:=params_get());
_cfg:=_par.cfg;
_cfg.grF:=0;
POLA_GRP.TXT_1:='N';
PM_PREM.KW:=0;
POLA_GRP.TXT_2:='N';
PM_PREM.UWAGI:='';

_fae:="exec('pm_prem_ae','!ppl_grp_grpm',1)";
_limit:=PM_PREM.PM_NAG().PM_BUDZ().LIMIT='T';
{? _limit
|| _we:='GRPL';

   _TAB:=tab_tmp(2,
      'KW','REAL','KWOTA',
      'UIDREF','STRING[48]','uidref'
   );

   POLA_GRP.REAL_1:=PM_PREM.PM_NAG().PM_BUDZ().POZOST;
   _SEL:=PM_PREM.sel_aget();
   {? _SEL.first()
   || PM_PREM.cntx_psh();
      {!
      |? {? PM_PREM.seek(_SEL.REF)
         || POLA_GRP.REAL_1+=PM_PREM.KW;
            _TAB.blank();
            _TAB.UIDREF:=PM_PREM.uidref();
            _TAB.KW:=PM_PREM.KW;
            _TAB.add()
         ?};
         _SEL.next()
      !};
      PM_PREM.cntx_pop()
   ?};

   POLA_GRP.INT_1:=_TAB.size();
   {? POLA_GRP.INT_1
   || _zaokr:="roundmeth(1); _a$2";
      POLA_GRP.REAL_2:=_zaokr(POLA_GRP.REAL_1/POLA_GRP.INT_1)
   || FUN.error('Próba obsługi akcji grupowej nie powiodła się.'@);
      PM_PREM.cntx_pop();
      return(0)
   ?};

   _fae:=$("_ret:="+_fae+";
      {? (type_of(_ret)=type_of('') & _ret<>'') | (type_of(_ret)=type_of(0) & ~_ret)
      || _ret
      |? POLA_GRP.REAL_2<PM_PREM.KW
      || __CHK.err_fld(PM_PREM,'KW',1,'"+'Kwota nie powinna być większa niż %1.'@ [form(POLA_GRP.REAL_2,,2)]+"')
      || ''
      ?}
   ")

|| _we:='GRP'
?};

PM_PREM.win_edit(_we);
_ae1:=$("PM_PREM.efld_opt('"+_we+"','enable=%1' [$(POLA_GRP.TXT_1='T')],,'KW')");
_ae2:=$("PM_PREM.efld_opt('"+_we+"','enable=%1' [$(POLA_GRP.TXT_2='T')],,'UWAGI')");

_ae1();
_ae2();

_ae1:=POLA_GRP.fld_fml('TXT_1','AFTER_EDIT',_ae1);
_ae2:=POLA_GRP.fld_fml('TXT_2','AFTER_EDIT',_ae2);

{? _ret:=(PM_PREM.edit(_fae) & (POLA_GRP.TXT_1='T' | POLA_GRP.TXT_2='T'))
|| _cfg.grN:={? POLA_GRP.TXT_1='T' || PM_PREM.KW || ~~ ?};
   _cfg.grS:={? POLA_GRP.TXT_2='T' || PM_PREM.UWAGI || ~~ ?};
   {? _limit & _TAB.last()
   || do();
      {!
      |? {? PM_PREM.seek(_TAB.UIDREF)
         || _mod:=exec('pm_prem_modify_b','pm_wspolne');
            {? _mod=''
            || {? _cfg.grS<>~~
               || PM_PREM.UWAGI:=_cfg.grS
               ?};
               {? _cfg.grN<>~~ & PM_PREM.KW<>_cfg.grN
               || {? PM_PREM.PM_NAG().PM_BUDZ().WYKON+_cfg.grN-PM_PREM.KW<=PM_BUDZ.PLAN
                  || PM_PREM.KW:=_cfg.grN
                  || undo()
                  ?}
               ?};
               PM_PREM.put();
               PM_PREM.sel_del()
            || undo(_mod)
            ?}
         ?};
         _TAB.prev()
      !};
      {? end()
      || grp_disp(PM_NAG,'WER')
      || FUN.info(
            'Poprawienie wszystkich zaznaczonych wierszy nie było możliwe - żadne zmiany nie zostały wprowadzone.'@
         )
      ?}
   ?}
?};

POLA_GRP.fld_fml('TXT_1','AFTER_EDIT',_ae1);
POLA_GRP.fld_fml('TXT_2','AFTER_EDIT',_ae2);

PM_PREM.cntx_pop();

~_limit & _ret


\pm_prem_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli PM_PREM.
::       Uwaga: obsługa akcji grupowej w całosci odbywa się w formule \pm_prem_popraw_bg/!ppl_grp_grpm.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
_mod:=exec('pm_prem_modify_b','pm_wspolne');

{? _mod=''
|| _kw:=PM_PREM.KW;
   {? PM_PREM.edit(
         $("_ret:=exec('pm_prem_ae','!ppl_grp_grpm',1);
            {? (type_of(_ret)=type_of(0) & ~_ret) | (type_of(_ret)=type_of('') & _ret<>'')
            || _ret
            |? PM_PREM.PM_NAG().PM_BUDZ().LIMIT='N' | PM_BUDZ.WYKON+PM_PREM.KW-"+$_kw+"<=PM_BUDZ.PLAN
            || 1
            || FUN.info('"+'Budżet premii uznaniowych nie może być przekroczony.'@+"');
               'KW'
            ?}
         ")
      ) & PM_PREM.put()
   || grp_disp(PM_NAG,'WER')
   ?}
|| FUN.emsg(_mod)
?};
0


\pm_prem_usun_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - grupa przed" w oknie WER tabeli PM_PREM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('del_ask','#table')
|| params_get().cfg.grF:=0;
   1
?}


\pm_prem_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - przed" w oknie WER tabeli PM_PREM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
_gr:=PM_PREM.sel_size();
_mod:=exec('pm_prem_modify_b','pm_wspolne');
{? _mod<>''
|| {? _gr
   || _par.cfg.grF:=1
   || FUN.emsg(_mod)
   ?};
   0
|? _gr | exec('del_ask','#table')
|| PM_PREM.del(,1)
?};
{? ~_gr
|| grp_disp(PM_NAG,'WER')
?}


\pm_prem_usun_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - grupa po" w oknie WER tabeli PM_PREM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());
grp_disp(PM_NAG,'WER');
{? _par.cfg.grF
|| FUN.info('Usunięcie wszystkich zaznaczonych wierszy nie było możliwe.'@)
?}


\pm_prem_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli PM_PREM.
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
exec('pm_prem_chk','pm_wspolne',_put)

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:53 c07ce7b99729aa1d43231843a4a2111b7ce989c3a298f9f7ecb727e5d3ebfe0729bd8bcb9083e751601fbdb9fcbaed75c8e630ece31ba0ddc7101c03129eef5571e444395b04cb84e7a9c7d782fcdc807447435d2e60ad0a4b85e12cb27775059134a6b2652a8f7d7b537b485814c487eab4857c3b845818bf168a417bbf8c2d

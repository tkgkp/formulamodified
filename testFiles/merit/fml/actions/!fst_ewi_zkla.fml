:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_zkla.fml
:: Utworzony: 15.12.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_ZKLA - Przegląd środków wg klasyfikacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przeglądanie środków wg klasyfikacji - główna formuła czynności FST_EWI_ZKLA.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\tsk_k_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowej klasyfikacji środków trwałych
::----------------------------------------------------------------------------------------------------------------------
TSK.TREE:=0;
TSK.NAZ:=TSK.OPIS:='';
TSK.win_edit('K');
{? TSK.edit("exec('chk_k','!fst_ewi_zkla','ADD')")
|| TSK.add()
?}


\tsk_t_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowego typu do klasyfikacji środków trwałych
::----------------------------------------------------------------------------------------------------------------------
{? TSK.size()=0
|| FUN.emsg('Brak klasyfikacji. Przed dodaniem typu należy zdefiniować przynajmniej jedną klasyfikację.'@);
   return(0)
?};
{? TSK.TREE=0
|| TSK.TREE:=TSK.ref();
   _tree:=TSK.ref()
|| _tree:=TSK.TREE
?};
:: zablokowanie klasyfikacji
TSK.cntx_psh();
TSK.prefix();
{? TSK.seek(_tree,TSK.name())
|| {? TSK.r_lock(1,1,1) || _lock:=1 || _lock:=0 ?}
?};
TSK.cntx_pop();

{? _lock
|| TSK.NAZ:=TSK.OPIS:='';
   TSK.win_edit('T');
   {? TSK.edit("exec('chk_t','!fst_ewi_zkla','ADD')")
   || TSK.add()
   ?};
:: odblokowanie klasyfikacji
   TSK.cntx_psh();
   TSK.prefix();
   {? TSK.seek(_tree,TSK.name()) || TSK.r_unlock() ?};
   TSK.cntx_pop()

|| FUN.emsg('Klasyfikacja zablokowana przez innego użytkownika.'@)
?}


\tsk_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja rekordu TSK w zależności czy bieżący rekord jest typem czy klasyfikacją
::----------------------------------------------------------------------------------------------------------------------
{? TSK.TREE=0
|| exec('tsk_k_edit','!fst_ewi_zkla')
|| exec('tsk_t_edit','!fst_ewi_zkla')
?}


\tsk_k_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja klasyfikacji środków trwałych
::----------------------------------------------------------------------------------------------------------------------
TSK.win_edit('K');
{? TSK.r_lock(1,1)
|| {? TSK.edit("exec('chk_k','!fst_ewi_zkla','PUT')")
   || TSK.put()
   ?};
   TSK.r_unlock()
|| FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
?}


\tsk_t_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja typu klasyfikacji środków trwałych
::----------------------------------------------------------------------------------------------------------------------
TSK.win_edit('T');
{? TSK.r_lock(1,1)
|| {? TSK.edit("exec('chk_t','!fst_ewi_zkla','PUT')")
   || TSK.put()
   ?};
   TSK.r_unlock()
|| FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
?}


\chk_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła weryfikuje wypełnienie okna edycji klasyfikacji
::   WE: _a = 'ADD' lub 'PUT' - tryb sprawdzania
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(TSK,,'NAZ');
{? _wy=''
|| {? __CHK.index(TSK,_a='PUT')<>'' || _wy:='NAZ' ?}
?};
_wy


\chk_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła weryfikuje wypełnienie okna edycji typu klasyfikacji
::   WE: _a = 'ADD' lub 'PUT' - tryb sprawdzania
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(TSK,,'NAZ','OPIS');
{? _wy=''
|| {? __CHK.index(TSK,_a='PUT')<>'' || _wy:='NAZ' ?}
?};
_wy


\tsk_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie klasyfikacji lub typu środków
::----------------------------------------------------------------------------------------------------------------------
{? TSK.TREE=0
|| _tree:=TSK.ref();
   TSK.cntx_psh();
   TSK.index('MAIN');
   TSK.prefix(_tree);
   {? TSK.first() || _find:=1 || _find:=0 ?};
   TSK.cntx_pop();
   {? _find
   || FUN.emsg('Wskazana klasyfikacja (%1) posiada zdefiniowane typy środków i nie można jej usunąć.'@[TSK.NAZ])
   || {? TSK.r_lock(1,1,1)
      || {? FUN.ask('Usunąć klasyfikację: %1?'@[TSK.NAZ])
         || TSK.r_unlock();
            TSK.del()
         || TSK.r_unlock()
         ?}
      || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
      ?}
   ?}
|| {? TSK.count()>0
   || {? FUN.ask('Wskazany typ środka (%1) jest wykorzystywany i nie można go usunąć.'
                 'Wyświetlić listę środków związanych ze wskazanym typem?'@[TSK.NAZ])
      || _sql:='select SRSR.NRI as NRI, SRSR.NST as NST, SRSR.DE as DE, SRSR.DZ as DZ, '
              +'OKRO_F.NAZ||'' ''|| ROK_F.NAZ as OKRES from TSR join SRSR using(TSR.SRSR,SRSR.REFERENCE) '
              +'join OKRO_F using (SRSR.OKRO_F,OKRO_F.REFERENCE) join ROK_F using (SRSR.ROK_F,ROK_F.REFERENCE) '
              'where TSR.TYP=:_a order by NRI';
         _tmp:=sql(_sql,TSK.ref());
         _win:=_tmp.mk_sel('Środki związane z bieżącym typem (%1)'@[TSK.NAZ],,0,'_tmp_tsr',10,8,15);
         _tmp.win_fld(_win,,'NRI',,,15,,,'Nr inwentarzowy'@);
         _tmp.win_fld(_win,,'NST',,,25,,,'Nazwa'@);
         _tmp.win_fld(_win,,'DE',,,10,,,'Data eksploatacji'@);
         _tmp.win_fld(_win,,'DZ',,,10,,,'Data zakupu'@);
         _tmp.win_fld(_win,,'OKRES',,,25,,,'W ewidencji od'@);
         _tmp.win_sel(_win);
         _tmp.select()
      ?}
   || {? TSK.r_lock(1,1)
      || {? FUN.ask('Usunąć typ: %1?'@[TSK.NAZ])
         || TSK.r_unlock();
            TSK.del()
         || TSK.r_unlock()
         ?}
      || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
      ?}
   ?}
?}


\br_tst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na rekord przed tabeli TST, okno wertowania WER
::----------------------------------------------------------------------------------------------------------------------
SRST.prefix(); SRSR.prefix();
TST.SRST();
SRST.SRSR();
SRD.REF:=SRSR.ref();
''


\tsk_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na wyświetl tabeli TST, okno wertowania WER
::----------------------------------------------------------------------------------------------------------------------
{? TSK.TREE=0
|| TSK.win_edit('K');
   TSK.display()
|| TSK.win_edit('T');
   TSK.display()
?}


\tst_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na Wyświetl w oknie z listą  środków przypisanych do danego typu klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
SRD.showAsset()


\find_class
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła znajduje klasyfikację po TREE
::----------------------------------------------------------------------------------------------------------------------
_find:='';
TSK.cntx_psh();
TSK.prefix();
{? TSK.seek(_a,TSK.name()) || _find:=TSK.NAZ ?};
TSK.cntx_pop();
_find


\tst_show_class
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla listę klasyfkacji do których należy środek
::----------------------------------------------------------------------------------------------------------------------
{? EDIT_ES.TSK='T' || _ref:=TST.SRST().SRSR || _ref:=SRST.SRSR ?};
TSR.cntx_psh();
TST.cntx_psh();
TSK.cntx_psh();
TSR.index('TYPK');
TSR.prefix(_ref);
{? TSR.first()
|| _tmp:=tab_tmp(1,'CLASS','STRING[30]','Klasyfikacja',
                   'TYP'  ,'STRING[30]','Typ');
   _win:=_tmp.mk_sel('Klasyfikacje środka'@,'P',0,'_tsr_klas',20,8,15,,'U');
   _tmp.win_fld(_win,,'CLASS',,,30,,,'Klasyfikacja'@,,'Nazwa klasyfikacji'@);
   _tmp.win_fld(_win,,'TYP',,,30,,,'Typ'@,,'Nazwa typu'@);
   _tmp.win_sel(_win);
   {! |?
      _class:=exec('find_class','!fst_ewi_zkla',TSR.TYP().TREE);
      _tmp.CLASS:=_class;
      _tmp.TYP:=TSR.TYP().NAZ;
      _tmp.add();
      TSR.next()
   !};
   _tmp.select()
|| FUN.info('Bieżący środek nie jest przypisany do żadnej klasyfikacji.'@)
?};
TST.cntx_pop();
TSK.cntx_pop();
TSR.cntx_pop()

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:44 7ec719a64c7b919bacee8d61c3fcc413afc41a17ec4e3483dea660253127cf8d5648cfa5d504e4c3feefab01d55c8a7f424485deca15d3e8c41ac75ae3554ba4a82916f81ea08cba17704001bebf08a018395f027b9fb6d9ae7c8770748213f44c0f354da074bff296d4f60697367aba9c5c1094a6518f05c8376f05634e9bc7

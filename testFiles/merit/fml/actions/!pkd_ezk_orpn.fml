:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orpn.fml
:: Utworzony: 10.06.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORPN - Rej. punktów za publikacje.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rej. punktów za publikacje - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.P);
{? _uidref=''
|| _result:=exec('error','!pkd_ezk_orpn')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orpn',_in.P);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rej. punktów za publikacje - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj punkty za publikacje: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj punkty za publikacje: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj punkty za publikacje: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj punkty za publikacje'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie punktów za publikacje niemożliwe.\nNie znaleziono pracownika.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie pracownika.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::  OLD: \pwer/dnauk.fml
::  OLD: \pgwer/dnauk.fml - Zmiana obsługi.
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a)
|| OSOBA.cntx_psh();
   P.OSOBA();
   OS_DNAUK.cntx_psh();
   OS_DNAUK.prefix();
   P_DNAUK.cntx_psh();
   P_DNAUK.index('POS');
   P_DNAUK.prefix(P.ref());
   P_DNAUK.win_sel({? exec('dest','f_zatr',P.F_ZATR,'KOD')='Z' || 'WER_Z' || 'WER_P' ?});
   _ret:=P_DNAUK.select();
   P_DNAUK.cntx_pop();
   OS_DNAUK.cntx_pop();
   OSOBA.cntx_pop()
|| _ret:=exec('error','!pkd_ezk_orpn')
?};
P.cntx_pop();
_ret


\suma_punkty_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pola SUMA.PUNKTY.
::       Formuła służy do obliczenia sumy zaznaczonych rekordów. Dokładnie sumowane jest pole P_DNAUK.OS_DNAUK().PUNKTY.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_sel_size:=P_DNAUK.sel_size();
_fml:='\'empty='+$(_sel_size=0)+'\'';
SUMA.fld_fml('PUNKTY','DISPLAY_FORMAT',$_fml);
{? _sel_size
|| _sql:=
      'select sum(:_a.PUNKTY) PUNKTY '+
      'from :_a '+
      'where reference_num(:_a.REFERENCE) in (select SEL.REF from :_b SEL)';
   _SUMA:=sql(_sql,cur_tab(1,1),P_DNAUK.sel_aget());
   SUMA.PUNKTY:=_SUMA.PUNKTY
?};
1


\p_dnauk_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord przed" dla tabeli P_DNAUK.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P_DNAUK.OS_DNAUK().memo_get(,'UWAGI');
0


\p_dnauk_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.30]
:: OPIS: Obsługa akcji Dołącz dla tabeli P_DNAUK. Formuła pokazuje cały dorobek naukowy osoby (tabela OS_DNAUK) i
::       pozwala na wybór (akcja grupowa) pozycji.
::   WE:
::   WY: ~~
::  OLD: \oswer/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
SLO_TYP.cntx_psh(); SLO_TYP.prefix();
SLO_KOD.cntx_psh(); SLO_KOD.prefix();
OS_DNAUK.cntx_psh();
OS_DNAUK.win_sel('SLO');
OS_DNAUK.win_edit('RED');
OS_DNAUK.prefix();
OS_DNAUK.f_set(
   'OSOBA,DATA,TYP(KOD)',,
   '"OS_DNAUK".OSOBA=:_a and "3SLO_KOD".KOD=\'PUBLIK\' and '
   '"OS_DNAUK".REFERENCE not in (select P_DNAUK.OS_DNAUK from P_DNAUK where P_DNAUK.P=:_b)',
   P.OSOBA,P.ref()
);
OS_DNAUK.select();
OS_DNAUK.f_clear();
OS_DNAUK.cntx_pop();
SLO_KOD.cntx_pop();
SLO_TYP.cntx_pop();
~~


\p_dnauk_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.30]
:: OPIS: Obsługa akcji "Wyświetl - przed" dla tabeli P_DNAUK.
::   WE:
::   WY:
::  OLD: \p_dnaukwb/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
OS_DNAUK.display()


\p_dnauk_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli P_DNAUK z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli P_DNAUK jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=P_DNAUK
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\os_dnauk_wbg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Wybierz - przed" dla tabeli OS_DNAUK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FUN.ask('Czy dołączyć zaznaczony dorobek naukowy do pracownika?'@)


\os_dnauk_wb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wybierz - przed" dla tabeli OS_DNAUK.
::   WE:
::   WY:
::  OLD: \os_dnauk_tb/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
P_DNAUK.cntx_psh();
P_DNAUK.index('POS');
P_DNAUK.prefix(P.ref(),OS_DNAUK.ref());
{? P_DNAUK.first()
:  Rekord dodany z innej końcówki, już po zastosowaniu filtra.
|| _ret:=1
|| P_DNAUK.blank();
   P_DNAUK.P:=P.ref();
   P_DNAUK.OS_DNAUK:=OS_DNAUK.ref();
   P_DNAUK.PUNKTY:=OS_DNAUK.PUNKTY;
   _ret:=P_DNAUK.add()
?};
P_DNAUK.cntx_pop();
_ret


\os_dnauk_wa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wybierz - po" dla tabeli OS_DNAUK.
::   WE:
::   WY:
::  OLD: \os_dnauk_ta/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
{? OS_DNAUK.sel_size()=0
|| sel_exit()
?}


\os_dnauk_wag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Wybierz - po" dla tabeli OS_DNAUK.
::   WE:
::   WY:
::  OLD: \os_dnauk_ta/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
sel_exit()

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 bb894954532f3b3c04f9a8d1ae36dfbb25113e7b7ab24094193520cb5eeed089867cc86704a6f8eaf75e4d21a05f0bd04be4b4fd4c8456f929d31ebc6066ef3485c034e1841466b78c829343aea74d6131a93caeadb43763e3969a34cd32ba28f90888cf89a35586eef36c15fa8add039b33931a220ee79152892ea68f4ed400

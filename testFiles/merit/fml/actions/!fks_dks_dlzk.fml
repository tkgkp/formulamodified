:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dlzk.fml
:: Utworzony: 10.08.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DLZK - Dekretacja dokumentów zakupu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja dokumentów zakupu - główna formuła czynności FKS_DKS_DLZK.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
::# parses=exec('parses_faks','dok_fks','LZK','LZ')
::# access=exec('uprawnienia','!fks_dks_dlzk')
::
:: PARAMETRY WE:
::# kind=WE, symbol=FAKS,   type=_FAKS, name=Wskazanie na nagłówek dokumentu zakupu, required=T, keyref=T

:: PARAMETRY WY:
::# kind=WY, symbol=DOK,   type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=T
_args:=params_get();
_we:=_args.in;
_mp:=_args.mp;
_akcja:=_mp.akcja();
{? var_pres('FAKS',_we)
|| _wew:=_args.int;
   _wy:=_args.out;
   exec('fl_decl','dekret_aut');
   exec('dek_decl','dekret_aut');
   exec('sd_decl','dekret_aut');
   exec('upr_set','ceny');
   _mp.descTodo();
   _auto:=_mp.isAutoRun();
   _ref:=BB.sqlint($_we.FAKS); _nam:=form(($_we.FAKS)-8);
   {? _ref<>0 & _nam<>''
   || FAKS.cntx_psh(); FAKS.use(_nam); FAKS.prefix();
      {? FAKS.seek(_ref,_nam)
      || {? FAKS.STAT_REJ='T'
         || {? _akcja='Dekretuj'
            || _ref:=FAKS.ref();
               FAKS.cntx_psh(); exec('faks_dekret2','!fks_dks_dlzk'); FAKS.cntx_pop();
               {? FAKS.seek(_ref) & FAKS.ZAK='T'
               || _wy.DOK:=DOK.ref();
                  _mp.save(,_wy);
                  _mp.done()
               ?}
            |? (_mp.pathTodo() | _mp.isAutoRun())
            || menu_txt(,'Popraw');
               POMOC.OKR:=SSTALE.AO;
               EKSG.ODD:=POMOC.OKR().POCZ;
               EKSG.DOD:=POMOC.OKR().KON;
               EKSG.SYS:='LZK';
               EKSG.WS:=EKSG.WT:='N';
               exec('faks_win_edit_set','dok_fks_aut_dok');
               {? _mp.isAutoRun()
               || BtnDek:=1
               || BtnDek:=0;
                  FAKS.display()
               ?};
               {? BtnDek & exec('faks_dekret','!fks_dks_dlzk',~_mp.pathTodo() & ~_mp.isAutoRun(),~_mp.isAutoRun())
               || _wy.DOK:=DOK.ref();
                  _mp.save(,_wy);
                  _mp.done()
               ?};
               VAR_DEL.delete('BtnDek')
            ?}
         || {? _akcja<>'Dekretuj'
            || {? FAKS.STAT_REJ='A'
               || FUN.info('Dokument zakupu został anulowany.'@);
                  _wy.DOK:=null;
                  _mp.save(,_wy);
                  _mp.done()
               || FUN.info('Dokument zakupu nie ma statusu "zaakceptowany".'@)
               ?}
            ?}
         ?}
      || {? _akcja<>'Dekretuj'
         || FUN.info('Nie znaleziono dokumentu zakupu.'@)
         ?};
         _wy.DOK:=null;
         _mp.save(,_wy);
         _mp.done()
      ?};
      FAKS.cntx_pop()
   || {? _akcja<>'Dekretuj'
      || FUN.info('Nie znaleziono dokumentu zakupu.'@)
      ?};
      _wy.DOK:=null;
      _mp.save(,_wy);
      _mp.done()
   ?}
|| {? _akcja<>'Dekretuj'
   || FUN.info('Nie znaleziono dokumentu zakupu.'@)
   ?};
   _wy:=_args.out;
   _wy.DOK:=null;
   _mp.save(,_wy);
   _mp.done()
?};
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_desc:='Zadekretuj dokument zakupu'@@;
{? var_pres('FAKS',_in)<>type_of(~~) & _in.FAKS<>null
|| FAKS.cntx_psh(); FAKS.use(ref_name(_in.FAKS)); FAKS.prefix();
   {? FAKS.seek(_in.FAKS)
   || _desc:='Zadekretuj dokument zakupu: %1'@@[FAKS.SYM]
   ?};
   FAKS.cntx_pop()
?};
_desc


\faks_dekret
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja dokumentu zakupu
::   WE: _a - Pytanie o dekretowanie
::       _b - Wyświetlać podsumowanie
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
{? FAKS.STAT_REJ<>'T'
|| {? _b
   || FUN.info('Nie zaakceptowano dokumentu \'%1\'.'@[FAKS.SYM])
   ?}
|? FAKS.SZ<>'Z'
|| {? _b
   || FUN.info('Wybrany dokument nie jest dokumentem zakupu \'%1\'.'@[FAKS.SYM])
   ?}
|? ~(FAKS.STS=null | FAKS.STS().KSG='T')
|| {? _b
   || FUN.info('Wybrany dokument nie jest dekretowany \'%1\'.'@[FAKS.SYM])
   ?}
|? FAKS.HIDDEN='T'
|| {? _b
   || FUN.info('Wybrany dokument \'%1\' jest dokumentem niewidocznym.'@[FAKS.SYM])
   ?}
|? FAKS.ZAK='T'
|| {? _b
   || FUN.info('Dokument \'%1\' jest już zadekretowany.'@[FAKS.SYM])
   ?};
   _ok:=1
|? ~_a | FUN.ask('Zadekretować dokument'@,'',1,'Nie'@,'Tak'@)
|| {? FAKS.r_lock(1,1)
   || {? FAKS.get()
      || {? exec('initWARL','dok_fks_aut_dok',SSTALE.AR,'LZK','LZ')
         || exec('init','dok_fks_aut_dok','Informacje o błędach dekretacji dokumentów zakupu');
            FAKS.cntx_psh(); _ref:=FAKS.ref();
            exec('faks_dekret1','!fks_dks_dlzk');
            FAKS.cntx_pop();
            {? FAKS.seek(_ref) & FAKS.ZAK='T'
            || {? _b || FUN.info('Dokument zadekretowany.'@) ?};
               _ok:=1
            || {? _b || FUN.info('Dekretowanie dokumentu \'%1\' nie powiodło się.'@[FAKS.SYM]) ?}
            ?};
            exec('done','dok_fks_aut_dok')
         ?}
      || {? _b || FUN.info('Dekretowanie dokumentu \''+FAKS.SYM+'\' nie powiodło się.') ?}
      ?};
      FAKS.r_unlock()
   || {? _b || FUN.info('Dokument obsługuje inny operator.'@) ?}
   ?}
?};
_ok


\faks_dekret1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja dokumentu zakupu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
exec('set_fld','dok_fks_aut_dok','O','LZ');
exec('faks_dekret2','!fks_dks_dlzk')


\faks_dekret2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja dokumentu zakupu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
{? exec('spr_war','dok_fks_aut_dok',SSTALE.AR,'LZK','LZ',$FAKS.T)
|| exec('set_fld','dok_fks_aut_dok','N','LZ');
   exec('pozycje','dok_fks_aut_dok',SSTALE.AR,'LZK','LZ',$FAKS.T)
?}


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma niezbędnych uprawnień do danych czynności
::       1 - użytkownik ma uprawnienia do dancyh czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh();
USERS.clear();
{? USERS.seek(_user)
|| {? exec('usr_fjks_any','b_perm',USERS.ref())
   || _result:=1
   ?}
?};
USERS.cntx_pop();
_result


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [19.02]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};

{? var_pres('_b')>100
|| _we:=_b
|| _we:=params_get().in
?};

_can_continue:=1;
_obj:=obj_new('RESULT','FAKS');
_obj.RESULT:=0;
_obj.FAKS:=null();

_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? 5+ref_name(_kref)='faktu'
         || _obj.FAKS:=exec('FindAndGet','#table',FAKS,_kref,,,null());
            {? _obj.FAKS=null()
            || _can_continue:=0;
               _wy.DOK:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      ?}
   !}
?};

{? _can_continue>0
||
:: jest parametr wejściowy FAKS
   {? _obj.FAKS=null() & var_pres('FAKS',_we)=type_of(null())
   || _obj.FAKS:=_we.FAKS
   ?}
?};

{? _can_continue>0
||
   {? _obj.FAKS=null()
   ||
::    Nie było key refa ani parametru wejściowego, robię error
      _can_continue:=0;
      _msg:='Dokument nie został przekazany do czynności.'@;
      {? _mp.isService()=0 & _mp.CLEANER=0
      || {? _mp.isGroup()
         || KOMM.add(_msg,2,,1)
         || FUN.emsg(_msg)
         ?}
      ?};
      _mp.error(_msg)
   ?}
?};

{? _can_continue>0
|| _obj.RESULT:=1
?};
_obj

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 b2fb24098f6d3ed04fcd5cced64cf17f8a4a219abf55d1cb24a64772b1a7f3b4ef0dc8ae148853ace868461c336af5373feadb697cb0cd72367a18c49b2b3831e0875c8e5ffa34d6a7b713211e369d3dbe8472420f1247ac0af43d886fea092d963bbb193c81a62159c6ee9644915a6d90341dee7ad4a88de36b5fafa72e5b57

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_pld_dpla.fml
:: Utworzony: 12.05.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa planu dostaw - rejestracja
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LMG,TARD
~~


\pd_k_kh_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed wyświetleniem PD_K.KH
::  OLD: \pd_k_kh_bd/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_S:=$fld();
{? PD_K.TYPYZAM().R='D'
|| ''
|| exec('findfnv','#color')
?}


\pd_k_kh_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed redakcja PD_K.KH
::  OLD: \pd_k_kh_be/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
win_disp();
PD_K.TYPYZAM().R='D'


\pd_k_kh_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: po redakcji PD_K.KH
::  OLD: \pd_k_kh_ae/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
{? DPOZ.WPR_S<>'' & $fld()<>DPOZ.WPR_S & FUN.ask('Zmienił się dostawca czy wyznaczyć cenę zakupu?'@)
|| PD_K.CWAL:=PD_K.CENA:=0;
   {? exec('pd_k_cwal_pr','!lzk_pld_dpla')=0
   || exec('pd_k_cena_be','!lzk_pld_dpla')
   ?};
   exec('pd_k_obl','plan_dostaw')
?};
1


\pd_k_cwal_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed redakcją PD_K.CWAL
::  OLD: \pd_k_cwal_pr/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('spr_npoz','eurusd');
DPOZ.WPR_R:=fld;
{? _wyn=1 & PD_K.WAL=null || _wyn:=0 ?};
{? _wyn
|| exec('taz_sd_set','ceny');
   {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & PD_K.CWAL=0
   || exec('st_licz_wz','ceny','PD_K');
      _jmz:=PD_K.JMZ & PD_K.JM<>PD_K.J2;
      {? _jmz
      || KALK.JM:=KALK.J2:=PD_K.J2; KALK.WS2:=1
      || KALK.JM:=KALK.J2:=PD_K.M().J; KALK.WS2:=1
      ?};
      exec('stplicz','ceny',{? _jmz || PD_K.IL2 || PD_K.IL ?});
      exec('ceny_mat','ceny_mat',PD_K.M,PD_K.KH,'PD_K.CWAL',0,,'','N',PD_K.WALK,'',,0,,,0,0,0);
      {? PD_K.CWAL || DPOZ.WPR_R:=-1; exec('po_cenaw','ceny',0); DPOZ.WPR_R:=fld ?}
   ?}
?};
_wyn


\pd_k_cwal_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: F3 PD_K.CWAL
::  OLD: \pd_k_cwal_f3/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
:: cena walutowa magazynowa na dokumentach przychodowych
TAZ.RABOFF:=1;
exec('st_licz_wz','ceny','PD_K');
_jmz:=PD_K.JMZ & PD_K.JM<>PD_K.J2;
{? _jmz
|| KALK.JM:=KALK.J2:=PD_K.J2; KALK.WS2:=1
|| KALK.JM:=KALK.J2:=PD_K.M().J; KALK.WS2:=1
?};
exec('stplicz','ceny',{? _jmz || PD_K.IL2 || PD_K.IL ?});
exec('f3_zdcen','ceny_mat',PD_K.M,PD_K.KH,PD_K.CWAL,,'PD_K',PD_K.WALK);
TAZ.RABOFF:=0;
''


\pd_k_mg_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redakcja PD_K.MG
::  OLD: \pd_k_mg_ae/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
exec('pd_k_set_efld_opt','plan_dostaw');
{? exec('samgdost','mat_atr')=0
|| PD_K.DK_C:=null(); PD_K.RDKC:='';
   {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !};
   ATR.FLAG:=0;
   ATR_FLAG_ED:=0;
   win_disp()
|| ATR.M_ATR:=PD_K.M().M_ATR;
   ATR.FLAG_ED:=(1+PD_K.MG().TYP)='D' & ATR.CZY_ATR & ATR.M_ATR().EDIT;
   ATR.FLAG:={? ATR.FLAG_ED & PD_K.M().M_ATR<>null() || 2 || 0 ?};
   {? ATR.FLAG_ED || {? PD_K.M().M_ATR().EDIT || ATR.FLAG_ED:=2 ?} ?};
   {? PD_K.DK_C<>null() & PD_K.DK_C().M_ATR<>null()
   || {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():=($('PD_K.DK_C().WAR'+form(_i,-2,,'99')))() !}
   |? PD_K.M().M_ATR=null()
   || {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !}
   ?};
   win_disp()
?};
MG.f_clear();
1


\pd_k_zmg_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed wyświetleniem PD_K.Z_MG
::  OLD: \pd_k_zmg_bd/plan_dostaw.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('pd_k_zmg_be','plan_dostaw') || '' || exec('findfnv','#color') ?}


\pd_k_zmg_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redakcji PD_K.Z_MG
::  OLD: \pd_k_zmg_ae/plan_dostaw.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_K.TYPYZAM().R='W' || MG.f_clear() ?};
1


\pd_k_il_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redakcji PD_K.IL
::  OLD: \pd_k_il_ae/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

_dokl:=exec('jaka_dok_m','jm',PD_K.M);
PD_K.IL:=PD_K.IL$_dokl;
{? PD_K.T2='N' | PD_K.WS2=0 | PD_K.J2=null()

|| PD_K.IL2:=PD_K.IL;
   PD_K.WS2:=1;
   PD_K.J2:=PD_K.M().J
?};

{? PD_K.IL<0
|| FUN.info('Ilość materiału na zamówieniu nie może być ujemna.'@)
|| {? PD_K.CZY_ZAM='N' & PD_K.CZY_ZL='N'
   || PD_K.ILP:=PD_K.IL;
      exec('pd_k_obl','plan_dostaw'); _wyn:=1
   || _il_old:=0;
      PD_K.cntx_psh();
      {? PD_K.get
      || _il_old:=PD_K.IL
      ?};
      PD_K.cntx_pop();
      _ilzl:=exec('pd_k_ilzl','plan_dostaw',$PD_K.ref);
      _ilza:=_il_old-PD_K.ILP-_ilzl;
      _ilzrea:=_ilzl+_ilza;
      _diff:=_il_old-PD_K.IL;
      _ilp:=PD_K.ILP-_diff;
      {? _ilp<0 | _il_old=0
      || FUN.info('Ilość materiału nie może być mniejsza od ilości już zrealizowanej (%1).'@[$_ilzrea])
      || PD_K.ILP:=PD_K.ILP-_diff;
         PD_K.CZY_ZL:= {? _ilzl=0 || 'N'
                       |? _ilzl=PD_K.IL || 'T'
                       || 'C'
                       ?};
         exec('pd_k_obl','plan_dostaw'); _wyn:=1
      ?}
   ?}
?};

{? _wyn || exec('aktpdkil2','material') ?};
_wyn


\pd_k_cena_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed redagowaniem PD_K.CENA
::  OLD: \pd_k_cena_be/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;

{? PD_K.TYPYZAM().R='D'
|| _wyn:=exec('czy_rwal','eurusd','P');
   {? _wyn
   || exec('taz_sd_set','ceny');
      {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?}
      || exec('st_licz_wz','ceny','PD_K');
         _jmz:=PD_K.JMZ & PD_K.JM<>PD_K.J2;
         {? _jmz
         || KALK.JM:=KALK.J2:=PD_K.J2; KALK.WS2:=1
         || KALK.JM:=KALK.J2:=PD_K.M().J; KALK.WS2:=1
         ?};
         exec('stplicz','ceny',{? _jmz || PD_K.IL2 || PD_K.IL ?});
         exec('ceny_mat','ceny_mat',PD_K.M,PD_K.KH,'PD_K.CENA',0,,'','N',INFO.NAROD,'',,0
          ,{? _jmz || PD_K.J2().KOD || '' ?}
          ,{? PD_K.J2<>null & PD_K.WS2<>0 || {? _jmz || 1/PD_K.WS2 || PD_K.WS2 ?} || 0 ?}
          ,0,0,0)
      ?}
   ?}
?};
_wyn


\pd_k_cena_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: F3 PD_K.CENA
::----------------------------------------------------------------------------------------------------------------------
{? PD_K.TYPYZAM().R='D'
||
:: wyłączenie pól rabatowych w cenniku
   TAZ.RABOFF:=1;
   exec('st_licz_wz','ceny','PD_K');
   _jmz:=PD_K.JMZ & PD_K.JM<>PD_K.J2;
   {? _jmz
   || KALK.JM:=KALK.J2:=PD_K.J2; KALK.WS2:=1
   || KALK.JM:=KALK.J2:=PD_K.M().J; KALK.WS2:=1
   ?};
   exec('stplicz','ceny',PD_K.IL);
   exec('f3_zdcen','ceny_mat',PD_K.M,PD_K.KH,PD_K.CENA,,'PD_K');
   TAZ.RABOFF:=0
?};
''


\pd_k_cena_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redagowaniu PD_K.CENA
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? PD_K.CENA<0
|| _wyn:=0;
   FUN.info('Cena musi być dodatnia.'@)
|| exec('pd_k_obl','plan_dostaw')
?};
_wyn


\blpdkj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: blankuje jednostkę dodatkową - koszyk planu dostaw
::  OLD: \blpdkj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
null


\pwpdkj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed wyświetleniem pól dla jednostek dodatkowych - koszyk planu dostaw
::  OLD: \pwpdkj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_K.TYPYZAM().R='D'
|| exec('pwdj2','zamdst_poz')
|| exec('pwzj2','zamsiw_poz')
?}


\prpdkj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed redakcją pól dla jednostek dodatkowych - koszyk planu dostaw
::  OLD: \prpdkj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_S:=$fld();
{? PD_K.TYPYZAM().R='D'
|| exec('prdj2','zamdst_poz')
|| exec('przj2','zamsiw_poz')
?}


\popdkj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redakcji pól dla jednostek dodatkowych - koszyk planu dostaw
::  OLD: \popdkj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_K.TYPYZAM().R='D'
|| exec('podj2','zamdst_poz')
|| exec('pozj2','zamsiw_poz')
?}


\blpdkt2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: blankuje typ przeliczenia jednostki dodatkowej (koszyk planu dostaw)
::  OLD: \blpdkt2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? BEER.TYPYZAM().R='D'
|| exec('bldt2','zamdst_poz')
|| exec('blzt2','zamsiw_poz')
?}


\pd_n_bdod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: dolącz przed tabeli PD_N
::  OLD: \pd_n_bdod/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();

POM.TAB:='PD_N';
POM.TYPDOK:='PDN';

PD_N.cntx_psh();
PD_N.blank();
PD_N.add();
{? ~exec('pd_n_lock','plan_dostaw') || PD_N.cntx_pop(); return(0) ?};

PD_Z.USER:=PD_N.USER;
_valid:="__CHK.record(PD_N,,'SYM','DU','FORMULY')";

PD_MG_ZN.win_dict('SLO');
PD_N.win_edit('RED');
_fb:="exec('pd_n_ico','!lzk_pld_dpla')"; PD_N.win_efml('RED',,'STATUS',,'ICON_BEFORE',_fb);
{? PD_N.edit(_valid)
||
   _ref:=PD_N.ref();
   {? exec('pd_n_upd','plan_dostaw')
   ||
      exec('open_pdn','plan_dostaw',$_ref,1)
   ?};
   exec('pd_n_unlock','plan_dostaw')
||
   do();
   numer:=PD_N.NR;
   oldnumer:=1;
   exec('nr_old','numery');
   PD_N.del();
   end()
?};

PD_N.cntx_pop();

{? _ref || PD_N.seek(_ref) ?};

POM.TAB:=POM.TYPDOK:='';
0


\pd_n_ico
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: ikony PD_P
::  OLD: \pd_n_ico/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_N.STATUS='N' || 'xwin16.png:156'
|? PD_N.STATUS='Z' || 'xwin16.png:157'
|| 'xwin16.png:110'
?}


\pd_n_bpop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: popraw przed tabeli PD_N
::  OLD: \pd_n_bpop/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('pd_n_lock','plan_dostaw') || return(0) ?};

PD_Z.USER:=PD_N.USER;
_valid:="__CHK.record(PD_N,,'SYM','DU')";

PD_MG_ZN.win_dict('SLO');
PD_N.win_edit('RED');
_fb:="exec('pd_n_ico','!lzk_pld_dpla')"; PD_N.win_efml('RED',,'STATUS',,'ICON_BEFORE',_fb);
{? PD_N.edit(_valid)
|| exec('pd_n_upd','plan_dostaw')
?};

exec('pd_n_unlock','plan_dostaw');
0


\pd_n_busu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: usun przed tabeli PD_N
::  OLD: \pd_n_busu/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
exec('pd_usun','plan_dostaw'); 0


\pd_n_bzamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: zamknij przed tabeli PD_N
::  OLD: \pd_n_bzamknij/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_N.DZ<>date(0,0,0)
||
   FUN.info('Plan dostaw jest zamknięty.'@);
   return(0)
?};

{? ~exec('pd_n_lock','plan_dostaw') || return() ?};

{? FUN.ask('Zamknąć plan dostaw %1?'@[PD_N.SYM])
||
   PD_N.DZ:=date();
   PD_N.TZ:=time();
   PD_N.STATUS:='Z';
   do();
   PD_N.put();
   exec('open_pdn','plan_dostaw',$PD_N.ref(),2);
   exec('pd_k_zamknij','!lzk_pld_dpla',PD_N.ref(),null());
   end()
?};

exec('pd_n_unlock','plan_dostaw')


\pd_n_bzamkwyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: wycofaj zamknięcie przed tabeli PD_N
::  OLD: \pd_n_bzamkwyc/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------

{? PD_N.DZ=date(0,0,0)
||
   FUN.info('Plan dostaw jest niezamykanięty.'@);
   return(0)
?};

{? ~exec('pd_n_lock','plan_dostaw') || return() ?};

{? FUN.ask('Wycofać zamknięcie planu dostaw %1?'@[PD_N.SYM])
||
   PD_N.DZ:=date(0,0,0);
   PD_N.TZ:=time(0,0,0);
   PD_N.STATUS:='N';
   do();
   PD_N.put();
   exec('open_pdn','plan_dostaw',$PD_N.ref(),1);
   exec('pd_k_zamkwyc','!lzk_pld_dpla',PD_N.ref(),null());
   end()
?};

exec('pd_n_unlock','plan_dostaw')


\pd_k_zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Zamkniecie pozycji koszyka
::   WE: _a - PD_N.ref()
::       _b - PD_P.ref()
::  OLD: \pd_k_zamknij/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
PD_K.cntx_psh();
PD_K.index('CZY_ZAM');
{? _b=null()
|| PD_K.prefix('N',_a)
|| PD_K.prefix('N',_a,_b)
?};
_loop:=PD_K.first();
{!
|? _loop
|!
   PD_K.cntx_psh();
   PD_K.prefix();
   PD_K.CZY_ZAM:='Z';
   PD_K.ZAM_REF:='';
   PD_K.ZAM_SYM:='';
   PD_K.put();
   PD_K.cntx_pop();
   _loop:=PD_K.first()
!};
PD_K.cntx_pop()


\pd_k_zamkwyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Wycofanie zamkniecia pozycji koszyka
::   WE: _a - PD_N.ref()
::       _b - PD_P.ref()
::  OLD: \pd_k_zamkwyc/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
PD_K.cntx_psh();
PD_K.index('CZY_ZAM');
{? _b=null()
|| PD_K.prefix('Z',_a)
|| PD_K.prefix('Z',_a,_b)
?};
_loop:=PD_K.first();
{!
|? _loop
|!
   PD_K.cntx_psh();
   PD_K.prefix();
   PD_K.CZY_ZAM:='N';
   PD_K.put();
   PD_K.cntx_pop();
   _loop:=PD_K.first()
!};
PD_K.cntx_pop()


\pd_n_bwys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: wyświetl przed tabeli PD_N
::  OLD: \pd_n_bwys/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
PD_N.win_edit('RED');
_fb:="exec('pd_n_ico','!lzk_pld_dpla')"; PD_N.win_efml('RED',,'STATUS',,'ICON_BEFORE',_fb);
PD_N.display()


\pd_n_bformuly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed formuły planu PD_N
::  OLD: \pd_n_bformuly/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('pd_n_lock','plan_dostaw')
|| PD_Z.NAGPOZ:='N';
   PD_D_USE.cntx_psh();
   _wer:='GRP';
   PD_D_USE.index('PD_D_USE');
   PD_D_USE.prefix(PD_N.ref(),null());
   PD_D_USE.first();
   PD_D_USE.win_sel(_wer);
   _act:=
      {? exec('get','#params',100700,2)='T' || 'A' || '' ?}
      +
      {? exec('mozna_wybrac_zestaw','!lzk_pld_dpla',PD_N) || '' || 'Z:Z' ?};
   {? PD_N.PLANPROD='T' || _act:='DZU:DZ' ?};
   PD_D_USE.actions('WER',_act);
   exec('wer_pd_d_use','!lzk_pld_dpla',PD_Z.NAGPOZ);
   PD_D_USE.select();
   PD_D_USE.cntx_pop();
   exec('pd_n_unlock','plan_dostaw')
?}


\pd_n_bzamawiaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: tworzenie zamowien
::  OLD: \pd_n_bzamawiaj/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
exec('pd2zam','!lzk_pld_dpla','N')


\wer_pd_d_use
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: ustawia okienko wertowania
::   WE: _a - wolane z [N]aglowka planu, z [P]ozycji
::   WY: akronim okna
::  OLD: \wer/pd_d_use.fml
::----------------------------------------------------------------------------------------------------------------------

_gdzie:=_a;
PD_Z.NAGPOZ:=_a;

_wyn:='WER';

_txt:=
   {? _gdzie='N'
   || ' - nagłówek'@+{? PD_N.PD_D_ZN  || ' - zestaw - %1'@[PD_N.PD_D_ZN().NAZ] || '' ?}
   |? _gdzie='P'
   || ' - pozycja'@+{? PD_P.PD_D_ZN || ' - zestaw - %1'@[PD_P.PD_D_ZN().NAZ] || '' ?}
   || ''
   ?};

PD_D_USE.hdr_sel('');
PD_D_USE.hdr_sel(' '+_txt);

_wyn


\pdduse_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: dolacz tabeli PD_D_USE
::  OLD: \pdduse_dol/pd_d_use.fml
::----------------------------------------------------------------------------------------------------------------------
_gdzie:=PD_Z.NAGPOZ;

PD_D.cntx_psh();

VAR_DEL.delete('__zatw');
__zatw:=0;

_tab:=tab_tmp(1,'SYM','STRING[20]','Kod'@
      ,'NAZ','STRING[60]','Nazwa'@
      ,'TYP','STRING[1]','Typ'@
      ,'ACCESS','STRING[1]','Wybrano'@
      ,'REF','STRING[16]','Ref SQL'@);

PD_D_USE.cntx_psh();
PD_D.index('A');
PD_D.prefix('T');

{? PD_D.first()
|| {!
   |? _tab.blank();
      _tab.SYM:=PD_D.KOD;
      _tab.NAZ:=PD_D.FORMULA().NAZWA;
      _tab.TYP:=PD_D.TYP;
      _tab.REF:=$PD_D.ref();
      PD_D_USE.index('PD_D_USE');
      PD_D_USE.prefix(PD_N.ref(),{? PD_Z.NAGPOZ='N' || null() || PD_P.ref() ?},PD_D.ref());
      _tab.ACCESS:={? PD_D_USE.first() || 'T' || 'N' ?};
      _tab.add(1);
      PD_D.next()
   !}
?};

_sel:=_tab.mk_sel('Formuły planu dostaw'@,'P',,'pd_d_wyb',,,,,'U');
_tab.win_fld(_sel,,'ACCESS',,,,,,,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
_tab.win_fld(_sel,,'TYP');
_tab.win_fld(_sel,,'SYM');
_tab.win_fld(_sel,,'NAZ');

_formula:="_tab:=cur_tab(); _tab.ACCESS:='T'; _tab.put()";
_tab.win_act(_sel,,'Formuła','Wybierz'@@,,,_formula,,1,1,,,'W');
_tab.win_btn(_sel,'text=%1,panel=right,align=begin'['Wybierz'@],'menu:W',,,,,,'noempty');
_formula:="_tab:=cur_tab(); _tab.ACCESS:='N'; _tab.put()";
_tab.win_act(_sel,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
_tab.win_btn(_sel,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O',,,,,,'noempty');
_formula:="__zatw:=1; sel_exit()";
_tab.win_act(_sel,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
_tab.win_btn(_sel,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A',,,,,,'noempty');
_tab.win_btn(_sel,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
_tab.win_act(_sel,,'Kolejność',);
_tab.win_act(_sel,,'Okienko',,,,,"
   {? ~__zatw
   || FUN.ask('Czy zakończyć wybór formuł planu dostaw?\nUwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@)
   || 1
   ?}");
_tab.win_act(_sel,,'Rekord',,,,"
      _tab:=cur_tab();
      {? _tab.ACCESS='T' | _tab.sel_size()
      || _tab.actions(_tab.win_sel('?'),,'O',1)
      || _tab.actions(_tab.win_sel('?'),,'W',1)
      ?}
   ");
_tab.win_sel(_sel);
{? _tab.select()
||
   _tab.clear();
   {? _tab.first()
   ||
      _add:=0;
      {!
      |? {? (PD_D.clear(); PD_D.seek(_tab.REF))
         ||
            PD_D_USE.index('PD_D_USE');
            PD_D_USE.prefix(PD_N.ref(),{? PD_Z.NAGPOZ='N' || null() || PD_P.ref() ?},PD_D.ref());
            {? ~PD_D_USE.first() & _tab.ACCESS='T'
            ||
               PD_D_USE.blank();
               PD_D_USE.PD_N:=PD_N.ref();
               PD_D_USE.PD_P:={? PD_Z.NAGPOZ='N' || null() || PD_P.ref() ?};
               PD_D_USE.PD_D:=PD_D.ref();
               _add+=PD_D_USE.add()

            |? PD_D_USE.first() & _tab.ACCESS='N'
            ||
               PD_D_USE.del()
            ?}
         ?};
         _tab.next()
      !};
      {? _add
      ||
         {? PD_Z.NAGPOZ='N'
         ||
            PD_N.PD_D_ZN:=null();
            PD_N.put()
         ||
            PD_P.PD_D_ZN:=null();
            PD_P.put()
         ?};
         exec('wer_pd_d_use','!lzk_pld_dpla',PD_Z.NAGPOZ)
      ?}
   ?}
?};
VAR_DEL.delete('__zatw');
PD_D_USE.cntx_pop();
PD_D.win_sel('GRP');
PD_Z.NAGPOZ:=_gdzie;
exec('wer_pd_d_use','!lzk_pld_dpla',PD_Z.NAGPOZ);
PD_D.cntx_pop()


\pdduse_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: usuń tabeli PD_D_USE
::  OLD: \pdduse_usu/pd_d_use.fml
::----------------------------------------------------------------------------------------------------------------------
_Sel:=PD_D_USE.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#PD_D_USE.ref(); _Sel.CRC:=PD_D_USE.crc(); _Sel.add() ?};

{? {? _Sel.first() & _Sel.next()
   || ~FUN.ask('Usunąć zaznaczone pozycje?'@)
   || ~FUN.ask('Usunąć pozycję?'@)
   ?}
||
   return(0)
?};

_pd_n:=null();
_pd_p:=null();
_loop:=_Sel.first();
{!
|? _loop
|!
   {? PD_D_USE.seek(_Sel.REF,)
   ||
      _pd_n:=PD_D_USE.PD_N;
      _pd_p:=PD_D_USE.PD_P;
      PD_D_USE.del()
   ?};
   _loop:=_Sel.next()
!};

{? _pd_p
||
   PD_P.PD_D_ZN:=null();
   PD_P.put();
   exec('wer_pd_d_use','!lzk_pld_dpla',PD_Z.NAGPOZ)

|? _pd_n
||
   PD_N.PD_D_ZN:=null();
   PD_N.put();
   exec('wer_pd_d_use','!lzk_pld_dpla',PD_Z.NAGPOZ)
?};

PD_D_USE.sel_adel();
0


\pdduse_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: rekord przed tabeli PD_D_USE
::  OLD: \pdduse_rek/pd_d_use.fml
::----------------------------------------------------------------------------------------------------------------------
PD_Z.F_NAZWA:=PD_D_USE.PD_D().FORMULA().NAZWA


\pd2zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: utworzenie zamowien z planow dostaw
::   WE: _a - wolane z 'N'-naglowkow planow, 'P'-poozycji planu, 'K'-koszyka
::   WY: 0-brak zmian w zamowieniach, 1-nastapily zmiany w zamowieniach
::  OLD: \pd2zam/pd_zam.fml
::----------------------------------------------------------------------------------------------------------------------
_wolanez:=_a;

:: wynik
_wyn:=1;

_czysc:="
   VAR_DEL.delete('__Plz','__plzw','__plzwgr','__pdkwe3','__pdkwe4')
";
_czysc();

_Tab:={? _wolanez='N' || PD_N |? _wolanez='P' || PD_P |? _wolanez='K' || PD_K || return(0) ?};

_Sel:=_Tab.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#_Tab.ref(); _Sel.CRC:=_Tab.crc(); _Sel.add() ?};

PD_N.cntx_psh(); PD_P.cntx_psh(); PD_K.cntx_psh(); ZD_NAG.cntx_psh();

:: Uprawnienia do czynności LZK_ZDS_DZAD Rejestracja zamówienia dostaw
_lzk_zds:=obj_new('TAB','WER','NDX');
exec('usractTab','#b_desktop',_lzk_zds,'LZK_ZDS_DZAD');
_lzk_zds_upr:=_lzk_zds.TAB.first();
:: Uprawnienia do czynności LMG_ZAM_DRZK  Rejestracja zamów.wewnętrznego
_lmg_zam:=obj_new('TAB','WER','NDX');
exec('usractTab','#b_desktop',_lmg_zam,'LMG_ZAM_DRZK');
_lmg_zam_upr:=_lmg_zam.TAB.first();

exec('ini_kom','#message','Zamawiaj');

:: I - lock koszyka
_loop:=_Sel.first();
{!
|? _loop
|!
   _loop1:=
      {? _wolanez='N'
      || {? PD_N.seek(_Sel.REF,)
         || PD_K.prefix();
            PD_K.f_clear();
            PD_K.f_set(
               ,'join PD_N using("PD_K".PD_N,PD_N.REFERENCE) join PD_P using("PD_K".PD_P,PD_P.REFERENCE)'
               ,'PD_N.STATUS=\'N\' and PD_P.STAT<>\'Z\' and "PD_K".PD_N=:_a and "PD_K".CZY_ZAM=\'N\' and "PD_K".CZY_ZL<>\'T\''
               ,PD_N.ref());
            PD_K.f_first()
         ?}
      |? _wolanez='P'
      || {? PD_P.seek(_Sel.REF,)
         || PD_K.prefix();
            PD_K.f_clear();
            PD_K.f_set(
               ,'join PD_N using("PD_K".PD_N,PD_N.REFERENCE) join PD_P using("PD_K".PD_P,PD_P.REFERENCE)'
               ,'PD_N.STATUS=\'N\' and PD_P.STAT<>\'Z\' and "PD_K".PD_N=:_a and "PD_K".PD_P=:_b and "PD_K".CZY_ZAM=\'N\' and "PD_K".CZY_ZL<>\'T\''
               ,PD_N.ref(),PD_P.ref());
            PD_K.f_first()
         ?}
      |? _wolanez='K'
      || PD_K.seek(_Sel.REF,) & PD_K.CZY_ZAM='N' & PD_K.CZY_ZL<>'T'
      ?};

   {!
   |? _loop1
   |!
      _err:=
         {? PD_K.TYPYZAM().R='D' & ~_lzk_zds_upr || 3
         |? PD_K.TYPYZAM().R='w' & ~_lmg_zam_upr || 4
         |? PD_K.TYPYZAM().R='W' & PD_K.Z_MG=null() | TYPYZAM.R='D' & PD_K.MG=null() || 1
         |? PD_K.M().A='N' || 2
         |? exec('pd_k_lock','plan_dostaw',0)=0 || 99
         || 0
         ?};
      {? _err
      || __kom_on:=1;
         _sym:=PD_K.PD_N().SYM;
         {? __kom.find_msg(_sym)
         || __kom.set_root(_sym)
         || __kom.sect_beg(_sym)
         ?};
         _ktm:=PD_K.M().KTM;
         {? __kom.find_msg(_ktm)
         || __kom.set_root(_ktm)
         || __kom.sect_beg(_ktm)
         ?};
         __kom.sect_beg($PD_K.ID);
         {? _err=1 || __kom.add('Pozycja koszyka ma niewypełnione pola "Magazyn dostawy". Została pominięta.'@,7)
         |? _err=2 || __kom.add('Pozycja koszyka dotyczy nieaktywnego indeksu. Została pominięta.'@,7)
         |? _err=3 || __kom.add('Brak upranień do czynności rejestracji zamówień dostaw.'@,7)
         |? _err=4 || __kom.add('Brak upranień do czynności rejestracji zamówień wewnętrznych.'@,7)
         |? _err=99 || __kom.add('Pozycja koszyka zablokowana przez innego użytkownika. Została pominięta.'@,7)
         ?};
         __kom.sect_end()
      ?};
      _loop1:={? _wolanez='K' || 0 || PD_K.f_next() ?}
   !};
   _loop:=_Sel.next()
!};
{? _wolanez='N' | _wolanez='P' || PD_K.f_clear() ?};

:: wynik
PD_K.index('SIDZAM'); PD_K.prefix(PD_K.ses_id(),'',);
_size:=PD_K.size();

:: II - zgrupowanie pozycji koszyka wg wyrozniajacych atrybotow
VAR_DEL.delete('__ZD_PROC','__ZW_PROC');
__ZD_PROC:=null();
__ZW_PROC:=null();
:: __Plz - lista planowanych zamowien
__Plz:=exec('plz','zamowienia');
PD_K.index('SID'); PD_K.prefix(PD_K.ses_id());
_loop:=PD_K.first();
{!
|? _loop
|!
   exec('plz_fill','zamowienia');
   _loop:=PD_K.next()
!};
VAR_DEL.delete('__ZD_PROC','__ZW_PROC');

:: IIa lock zamowien
_loop:=__Plz.first();
{!
|? _loop
|!
   _del:=0;
   _Nag:={? __Plz.R='D' || ZD_NAG || ZK_N ?};
   _Nag.cntx_psh();
   _Nag.use(8+__Plz.RSQL);
   _Nag.prefix();
   {? _Nag.seek(__Plz.RSQL)
   || {? ~_Nag.r_lock(1,1,1)
      ||
         __kom_on:=1;
         __kom.set_root(_Nag.SYM);
         __kom.add('Zamówienie obsługiwane przez innego użytkownika. Zostało pomnięte.'@,7);
         _del:=1
      ?}
   || _del:=1
   ?};
   _Nag.cntx_pop();
   &_Nag;
   _loop:={? _del || __Plz.del() || __Plz.next() ?}
!};

:: III - edycja planowanych zamowien
{? ~__Plz.first()
||
   __kom_on:=1;
   __kom.set_root('Podsumowanie');
   __kom.add('Wybrane pozycje nie wymagają planowania.'@,7);
   exec('end_kom','#message')
||
   {? __kom_on
   || __kom.set_root('Podsumowanie');
      __kom.add('Zamknij okno aby kontynuować.'@,7)
   ?};
   exec('end_kom','#message');
   exec('plz_okna','zamowienia');
   {? __Plz.select()
   || _Tab.sel_adel()
   || _wyn:=0
   ?}
?};

:: wynik
{? _wyn
||
   PD_K.index('SIDZAM'); PD_K.prefix(PD_K.ses_id(),'',);
   _wyn:=PD_K.size()<>_size
?};

:: IV - unlock koszyka
PD_K.index('SID'); PD_K.prefix(PD_K.ses_id());
_loop:=PD_K.first();
{!
|? _loop
|!
   exec('pd_k_unlock','plan_dostaw');
   _loop:=PD_K.first()
!};

:: IVA - unlock zamowien
_loop:=__Plz.first();
{!
|? _loop
|!
   _next:=0;
   _Nag:={? __Plz.R='D' || ZD_NAG || ZK_N ?};
   _Nag.cntx_psh();
   _Nag.use(8+__Plz.RSQL);
   _Nag.prefix();
   {? _Nag.seek(__Plz.RSQL)
   ||
      PD_K.cntx_psh();
      PD_K.index('SIDZAMP'); PD_K.prefix(PD_K.ses_id(),__Plz.RSQL,'',);
      {? ~PD_K.first()
      ||
         _next:=exec('plz_del','zamowienia',__Plz.RSQL)=1
      ?};
      PD_K.cntx_pop();
      _Nag.r_unlock()
   ?};
   _Nag.cntx_pop();
   &_Nag;
   _loop:=_next | __Plz.next()
!};

PD_N.cntx_pop(); PD_P.cntx_pop(); PD_K.cntx_pop(); ZD_NAG.cntx_pop();
_czysc();
_wyn


\pd_p_bdod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed dołącz tabeli PD_P
::  OLD: \pd_p_bdod/pd_p.fml
::       \m2pd_p/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__zaz','__zatw');

__zaz:=tab_tmp(1,'REF','STRING[16]','');
__zatw:=0;

PD_P.cntx_psh();
{? PD_P.first()
|| {!
   |? __zaz.blank();
      __zaz.REF:=$PD_P.M;
      __zaz.add();
      PD_P.next()
   !}
?};
PD_P.cntx_pop();
M.cntx_psh();
_m_sel:=M.mk_sel('Materiały'@,'P',0,'#mplan_dostaw',1,,,,'U');
M.win_fld(_m_sel,BPMN,'ACCESS',,,,,,,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
M.win_fld(_m_sel,,'KTM',,,20);
M.win_fld(_m_sel,,'N',,,50);
M.win_fld(_m_sel,,'J','KOD',,10,,,'jm');
_formula:="__zaz.clear(); {? ~__zaz.find_key($M.ref()) || __zaz.blank(); __zaz.REF:=$M.ref(); __zaz.add(1) ?}";
M.win_act(_m_sel,,'Formuła','Wybierz'@@,,,_formula,,1,1,,,'W');
M.win_btn(_m_sel,'text=Wybierz,panel=right,align=begin','menu:W',,,,,,'noempty');
_formula:="__zaz.clear(); {? __zaz.find_key($M.ref()) || __zaz.del() ?}";
M.win_act(_m_sel,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
M.win_btn(_m_sel,'text=Odbierz,panel=right,align=begin','menu:O',,,,,,'noempty');
_formula:="__zatw:=1; sel_exit()";
M.win_act(_m_sel,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A',,'target=window');
M.win_btn(_m_sel,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A',,,,,,'noempty');
M.win_act(_m_sel,,'Szukaj');
M.win_act(_m_sel,,'Kolejność');
_formula:="__zaz.clear();
          {? __zaz.find_key($M.ref())
          || BPMN.ACCESS:='T';
             M.actions(M.win_sel('?'),,'O',1)
          || BPMN.ACCESS:='N';
             {? M.sel_size
             || M.actions(M.win_sel('?'),,'O',1)
             || M.actions(M.win_sel('?'),,'W',1)
             ?}
          ?}";
M.win_act(_m_sel,,'Rekord',,,,_formula);
M.win_sel(_m_sel);
M.win_btn(_m_sel,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
M.win_act(_m_sel,,'Okienko',,,,,"
   {? ~__zatw
   || FUN.ask('Czy zakończyć wybór indeksów?\nUwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@)
   || 1
   ?}");
M.index('ARODZ');
M.prefix('T','T');
{? M.first() & M.select()
|| __zaz.clear();
   {? __zaz.first()
   || _dkc:=0;
      M.prefix();
      {!
      |? {? M.seek(__zaz.REF)
         ||
            _dost:=exec('dost','ceny_mat',M.ref(),date(0,0,0),INFO.NAROD,0).KH;
            exec('pd_p_add','plan_dostaw',PD_N.ref(),M.MGR,M.MGRP,M.MGAT,M.MODM,M.ref(),M.J,_dost)
         ?};
         __zaz.next()
      !};
      PD_P.f_rfresh()
   ?}
?};
M.cntx_pop();
VAR_DEL.delete('__zaz','__zatw');
''


\pd_p_bdodl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed dołącz z listy tabeli PD_P
::  OLD: \pd_p_bdodl/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
_czysc:="
   VAR_DEL.delete('__wer_lm','__Sel')
";
_czysc();

__wer_lm:='WER';

PD_LMN.index('USER'); PD_LMN.prefix();

PD_LMN.win_sel(__wer_lm);
{? PD_LMN.select()
||
   _pd_p:={? PD_P.get() || PD_P.ref() || null() ?};
   PD_LMN.cntx_psh(); PD_LMP.cntx_psh();
:: tabela __Sel powolywana w trakcie akceptacji wyboru z tabeli PD_LMN
   _loop:=__Sel.first();
   {!
   |? _loop
   |!
      PD_LMN.prefix();
      {? PD_LMN.seek(__Sel.REF,)
      ||
::       lista bazowa lub jesli nie okreslona to biezaca lista
         _pd_lmn:=$PD_LMN.ref();
         {? PD_LMN.LISTA=''
         || PD_LMN.ref()
         || PD_LMN.index('KOD'); PD_LMN.prefix(PD_LMN.LISTA,);
            {? PD_LMN.first()
            || _pd_lmn+=$PD_LMN.ref()
            ?}
         ?};
         {!
         |? _pd_lmn<>''
         |!
            M.cntx_psh();
            PD_LMP.index('PD_LMN'); PD_LMP.prefix(BB.sqlint(_pd_lmn+16),); _pd_lmn-=16;
            {? PD_LMP.first()
::          lista wg definicji
            ||
               {!
               |?
                  _kh:=PD_LMP.KH;
                  {? _kh
                  ||
::                   lista materiałów wg dostawców
                     PD_P.cntx_psh();
                     PD_P.index('POZ');
                     PD_P.prefix(PD_N.ref());
                     MDOST.cntx_psh();
                     MDOST.index('KH');
                     {? exec('get','#params',100701,2)='T'
                     || MDOST.prefix('D',_kh,'T')
                     || MDOST.prefix('D',_kh)
                     ?};
                     _loop:=MDOST.first();
                     {!
                     |? _loop
                     |!
                        {? MDOST.SD='D' & MDOST.M & ~PD_P.find_key(MDOST.M) & MDOST.M().A='T'
                        ||
                           _dost:=exec('dost','ceny_mat',MDOST.M,date(0,0,0),INFO.NAROD,0).KH;
                           exec('pd_p_add','plan_dostaw',PD_N.ref(),MDOST.M().MGR,M.MGRP,M.MGAT,M.MODM,M.ref(),M.J
                              ,_dost)
                        ?};
                        _loop:=MDOST.next()
                     !};
                     MDOST.cntx_pop();
                     PD_P.cntx_pop()
                  ||
                     M.prefix(); M.f_clear();
                     {? PD_LMP.M    || M.f_set(,,'M.REFERENCE=:_a and M.A=\'T\'',PD_LMP.M)
                     |? PD_LMP.MODM || M.f_set(,,'M.MODM=:_a and M.A=\'T\'',PD_LMP.MODM)
                     |? PD_LMP.MGAT || M.f_set(,,'M.MGAT=:_a and M.A=\'T\'',PD_LMP.MGAT)
                     |? PD_LMP.MGRP || M.f_set(,,'M.MGRP=:_a and M.A=\'T\'',PD_LMP.MGRP)
                     |? PD_LMP.MGR  || M.f_set(,,'M.MGR=:_a and M.A=\'T\'',PD_LMP.MGR)
                     ?};
                     {? M.f_active()
                     ||
                        _loop1:=M.f_first();
                        {!
                        |? _loop1
                        |!
                           _dost:=exec('dost','ceny_mat',M.ref(),date(0,0,0),INFO.NAROD,0).KH;
                           exec('pd_p_add','plan_dostaw',PD_N.ref(),M.MGR,M.MGRP,M.MGAT,M.MODM,M.ref(),M.J,_dost);
                           _loop1:=M.f_next()
                        !}
                     ?}
                  ?};
                  PD_LMP.next()
               !};
               M.f_clear()
::          jesli lista pusta to cala kartoteka materialow
            |? 0
            ||
               M.index('ARODZ'); M.prefix('T','T');
               _loop1:=M.first();
               {!
               |? _loop1
               |!
                  _dost:=exec('dost','ceny_mat',M.ref(),date(0,0,0),INFO.NAROD,0).KH;
                  exec('pd_p_add','plan_dostaw',PD_N.ref(),M.MGR,M.MGRP,M.MGAT,M.MODM,M.ref(),M.J,_dost);
                  _loop1:=M.next()
               !}
            ||
               FUN.info('Lista nie zawiera pozycji.\nPominięto dodawanie pozycji planu dostaw.'@)
            ?};
            M.cntx_pop()
         !}
      ?};
      _loop:=__Sel.next()
   !};
   PD_LMN.cntx_pop(); PD_LMP.cntx_pop();
:: przywrocenie aktywnej pozycji sprzed dodawania
   PD_P.f_rfresh();
   {? _pd_p=null()
   || {? PD_P.f_active() || PD_P.f_first() || PD_P.first() ?}
   |? _pd_p<>PD_P.ref()
   || {? PD_P.f_active() || PD_P.f_rfresh() ?};
      PD_P.seek(_pd_p)
   ?}
?};

_czysc();
0


\pd_lmn_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: akceptuj tabeli PD_LMN
::  OLD: \pd_lmn_akc/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
__Sel:=PD_LMN.sel_aget();
{? ~__Sel.first() || __Sel.REF:=#PD_LMN.ref(); __Sel.add() ?};
sel_exit();
0


\pd_lmn_bdod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed dolacz tabeli PD_LMN
::  OLD: \pd_lmn_bdod/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
exec('pd_lmn_fld_fml','!lzk_pld_dpla');
1


\pd_lmn_bpop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed dolacz tabeli PD_LMN
::  OLD: \pd_lmn_bpop/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('pd_lmn_lock','!lzk_pld_dpla')
|| exec('pd_lmn_fld_fml','!lzk_pld_dpla');
   1
?}


\pd_lmn_busu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed dolacz tabeli PD_LMN
::  OLD: \pd_lmn_busu/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask('Usunąć listę?'@) || return() ?};

do();
PD_LMN.get();
{? PD_LMN.LISTA=''
||
   PD_LMN.cntx_psh();
   PD_LMN.index('LISTA'); PD_LMN.prefix(PD_LMN.KOD,);
   {? PD_LMN.first()
   ||
      {!
      |?
         PD_LMN.cntx_psh();
         PD_LMN.prefix();
         PD_LMN.LISTA:='';
         PD_LMN.put();
         PD_LMN.cntx_pop();
         PD_LMN.first()
      !}
   ?};
   PD_LMN.cntx_pop()
?};
PD_LMP.index('PD_LMN'); PD_LMP.prefix(PD_LMN.ref());
{? PD_LMP.first() || {! |? PD_LMP.del() !} ?};
PD_LMN.del();
end();

1


\pd_lmn_fld_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: formuly pol tabeli PD_LMN
::  OLD: \pd_lmn_fld_fml/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
PD_LMN.fld_fml('LISTA','F3',"
   _Tab:=sql('select KOD,NAZ from PD_LMN where LISTA=''''');
   {? _Tab.first()
   ||
      _wer:=_Tab.mk_sel('Bazowe listy materiałów'@,,,'fankeknrfdjfvna');
      _Tab.win_fld(_wer,,'KOD',,,10,,,'Kod'@);
      _Tab.win_fld(_wer,,'NAZ',,,20,,,'Nazwa'@);
      _fb:=$'sel_exit()'; _Tab.win_act(_wer,0,'Formuła','Akceptuj'@@,,,_fb,,1);

      _Tab.win_sel(_wer);
      {? _Tab.select() || fld(_Tab.KOD) ?}
   ?}
");
PD_LMN.fld_fml('LISTA','AFTER_EDIT',"
   {? PD_LMN.KOD=PD_LMN.LISTA
   || FUN.info('Niedozwolone wskaznie listy bazowej.'@); 0
   || 1
   ?}
")


\pd_lmn_bpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: pozycje listy materialow
::  OLD: \pd_lmn_bpoz/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
PD_LMP.cntx_psh();
PD_LMP.index('PD_LMN'); PD_LMP.prefix(PD_LMN.ref());
_wer:='WER';
PD_LMP.win_sel(_wer);
PD_LMP.select();
PD_LMP.cntx_pop()


\pd_lmp_bdod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed dolacz tabeli PD_LMP
::  OLD: \pd_lmp_bdod/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
_ch:=FUN.choice('Wybierz opcję'@,1,'Dostawca'@,'Grupa'@,'Materiał'@,'Inne'@);
_ch:={? _ch=1 || 'D' |? _ch=2 || 'G' |? _ch=3 || 'M' |? _ch=4 || 'I' |? _ch=0 || '' ?};
{? _ch=''
||
   return()
|? _ch='I'
||
   _ch:=FUN.choice('Wybierz opcję'@,1,'Podgrupa'@,'G&atunek'@,'Odmiana'@);
   _ch:={? _ch=1 || 'P' |? _ch=2 || 'A' |? _ch=3 || 'O' |? _ch=0 || '' ?}
?};
{? _ch=''
||
   return()
?};

PD_LMP.cntx_psh();
VAR_DEL.delete('__zatw','__wybr');
__zatw:=0;
__wybr:=tab_tmp(1,'SQL','STRING[16]','');

VAR_DEL.delete('__KH');
{? _ch='G' || _tab:=MGR;  _opis:='Grupy materiałowe'@;    _etyk:='pd_lmp_mgr'
|? _ch='P' || _tab:=MGRP; _opis:='Podgrupy materiałowe'@; _etyk:='pd_lmp_mgrp'
|? _ch='A' || _tab:=MGAT; _opis:='Gatunki materiałowe'@;  _etyk:='pd_lmp_mgat'
|? _ch='O' || _tab:=MODM; _opis:='Odmiany materiałowe'@;  _etyk:='pd_lmp_modm'
|? _ch='M' || _tab:=M;    _opis:='Indeksy materiałowe'@;  _etyk:='pd_lmp_m'
|? _ch='D' || __KH:=_tab:=exec('kh','!lzk_pld_dpla'); _opis:='Dostawcy'@;  _etyk:='pd_lmp_kh'
?};

_tab.cntx_psh();
{? _ch='G' || _tab.index('KOD'); _tab.prefix('T'); _formakc:="__zatw:=1; exec('pd_lmp_mgr_dod','!lzk_pld_dpla')"
|? _ch='P' || _tab.index('MR'); _tab.prefix('T');  _formakc:="__zatw:=1; exec('pd_lmp_mgrp_dod','!lzk_pld_dpla')"
|? _ch='A' || _tab.index('KOD'); _tab.prefix();    _formakc:="__zatw:=1; exec('pd_lmp_mgat_dod','!lzk_pld_dpla')"
|? _ch='O' || _tab.index('KOD'); _tab.prefix();    _formakc:="__zatw:=1; exec('pd_lmp_modm_dod','!lzk_pld_dpla')"
|? _ch='M' || _tab.index('ARODZ'); _tab.prefix('T','T'); _formakc:="__zatw:=1; exec('pd_lmp_m_dod','!lzk_pld_dpla')"
|? _ch='D' || _tab.prefix(); _formakc:="__zatw:=1; exec('pd_lmp_kh_dod','!lzk_pld_dpla')"
?};

{? _tab.first()
|| ($('exec(\''+_etyk+'_dod\',\'!lzk_pld_dpla\',1)'))();
   _wer:=_tab.mk_sel(_opis,,,_etyk,1,,,,'U');
   _tab.win_fld(_wer,BPMN,'ACCESS',,,,,,,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
   {? _ch<>'M'
   || {? 'PAO'*_ch || _tab.win_fld(_wer,,'MGR','KOD',,8,,,'Grupa'@) ?};
      {? 'AO'*_ch || _tab.win_fld(_wer,,'MGRP','KOD',,8,,,'Podgrupa'@) ?};
      {? 'O'*_ch || _tab.win_fld(_wer,,'MGAT','KOD',,8,,,'Gatunek'@) ?};
      _tab.win_fld(_wer,,'KOD',,,8,,,'Kod'@);
      _tab.win_fld(_wer,,'NAZ',,,30,,,'Nazwa'@)
   || _tab.win_fld(_wer,,'KTM',,,20,,,'Indeks'@);
      _tab.win_fld(_wer,,'N',,,30,,,'Nazwa'@)
   ?};

   _formula:="_tab:=cur_tab(); __wybr.clear();
              {? ~__wybr.find_key($_tab.ref()) || __wybr.SQL:=$_tab.ref(); __wybr.add(1) ?}";
   _tab.win_act(_wer,,'Formuła','Wybierz'@@,,,_formula,,1,1,,,'W');
   _tab.win_btn(_wer,'text=%1,panel=right,align=begin'['Wybierz'@],'menu:W',,,,,,'noempty');
   _formula:="_tab:=cur_tab(); __wybr.clear();
              {? __wybr.find_key($_tab.ref()) || __wybr.del() ?}";
   _tab.win_act(_wer,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
   _tab.win_btn(_wer,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O',,,,,,'noempty');
   _tab.win_act(_wer,,'Formuła','Akceptuj'@@,,,_formakc,,,,,,'A',,'target=window');
   _tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A',,,,,,'noempty');
   _tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   _tab.win_act(_wer,,'Kolejność',);
   _tab.win_act(_wer,,'Okienko',,,,,"
   {? ~__zatw
   || FUN.ask('Czy zakończyć wybór?\nUwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@)
   || 1
   ?}");
   _tab.win_act(_wer,,'Rekord',,,,"
      _tab:=cur_tab();
      __wybr.clear();
      BPMN.ACCESS:={? __wybr.find_key($_tab.ref()) || 'T' || 'N' ?};
      {? BPMN.ACCESS='T' | _tab.sel_size()
      || _tab.actions(_tab.win_sel('?'),,'O',1)
      || _tab.actions(_tab.win_sel('?'),,'W',1)
      ?}
   ");
   _tab.win_sel(_wer);
   _tab.select()
?};
_tab.cntx_pop();
VAR_DEL.delete('__zatw','__wybr');
PD_LMP.cntx_pop();
~~


\pd_lmp_mgr_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: dodanie pozycji listy materialowej na podstawie MGR
::   WE: [_a] - pobranie informacji z PD_LMN 0-nie(domyslnie) 1-tak
::  OLD: \pd_lmp_mgr_dod/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? MGR.first()
|| {!
   |? {? ~_a
      || __wybr.clear();
         {? __wybr.find_key($MGR.ref())
         || PD_LMP.prefix();
            PD_LMP.blank();
            PD_LMP.PD_LMN:=PD_LMN.ref();
            PD_LMP.MGR:=MGR.ref();
            PD_LMP.add(1)
         || PD_LMP.index('PD_LMN');
            PD_LMP.prefix(PD_LMN.ref(),MGR.ref());
            {? PD_LMP.first() || PD_LMP.del() ?}
         ?}
      || PD_LMP.index('PD_LMN');
         PD_LMP.prefix(PD_LMN.ref(),MGR.ref());
         {? PD_LMP.first()
         || __wybr.blank();
            __wybr.SQL:=$MGR.ref();
            __wybr.add(1)
         ?}
      ?};
      MGR.next()
   !}
?};
{? ~_a || sel_exit() ?};
0


\pd_lmp_mgrp_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: dodanie pozycji listy materialowej na podstawie MGRP
::   WE: [_a] - pobranie informacji z PD_LMN 0-nie(domyslnie) 1-tak
::  OLD: \pd_lmp_mgrp_dod/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? MGRP.first()
|| {!
   |? {? ~_a
      || __wybr.clear();
         {? __wybr.find_key($MGRP.ref())
         || PD_LMP.prefix();
            PD_LMP.blank();
            PD_LMP.PD_LMN:=PD_LMN.ref();
            PD_LMP.MGR:=MGRP.MGR;
            PD_LMP.MGRP:=MGRP.ref();
            PD_LMP.add(1)
         || PD_LMP.index('PD_LMN');
            PD_LMP.prefix(PD_LMN.ref(),MGRP.MGR,MGRP.ref());
            {? PD_LMP.first() || PD_LMP.del() ?}
         ?}
      || PD_LMP.index('PD_LMN');
         PD_LMP.prefix(PD_LMN.ref(),MGRP.MGR,MGRP.ref());
         {? PD_LMP.first()
         || __wybr.blank();
            __wybr.SQL:=$MGRP.ref();
            __wybr.add(1)
         ?}
      ?};
      MGRP.next()
   !}
?};
{? ~_a || sel_exit() ?};
0


\pd_lmp_mgat_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: dodanie pozycji listy materialowej na podstawie MGAT
::   WE: [_a] - pobranie informacji z PD_LMN 0-nie(domyslnie) 1-tak
::  OLD: \pd_lmp_mgat_dod/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? MGAT.first()
|| {!
   |? {? ~_a
      || __wybr.clear();
         {? __wybr.find_key($MGAT.ref())
         || PD_LMP.prefix();
            PD_LMP.blank();
            PD_LMP.PD_LMN:=PD_LMN.ref();
            PD_LMP.MGR:=MGAT.MGR;
            PD_LMP.MGRP:=MGAT.MGRP;
            PD_LMP.MGAT:=MGAT.ref();
            PD_LMP.add(1)
         || PD_LMP.index('PD_LMN');
            PD_LMP.prefix(PD_LMN.ref(),MGAT.MGR,MGAT.MGRP,MGRP.ref());
            {? PD_LMP.first() || PD_LMP.del() ?}
         ?}
      || PD_LMP.index('PD_LMN');
         PD_LMP.prefix(PD_LMN.ref(),MGAT.MGR,MGAT.MGRP,MGAT.ref());
         {? PD_LMP.first()
         || __wybr.blank();
            __wybr.SQL:=$MGAT.ref();
            __wybr.add(1)
         ?}
      ?};
      MGAT.next()
   !}
?};
{? ~_a || sel_exit() ?};
0


\pd_lmp_modm_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: dodanie pozycji listy materialowej na podstawie MODM
::   WE: [_a] - pobranie informacji z PD_LMN 0-nie(domyslnie) 1-tak
::  OLD: \pd_lmp_modm_dod/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? MODM.first()
|| {!
   |? {? ~_a
      || __wybr.clear();
         {? __wybr.find_key($MODM.ref())
         || PD_LMP.prefix();
            PD_LMP.blank();
            PD_LMP.PD_LMN:=PD_LMN.ref();
            PD_LMP.MGR:=MODM.MGR;
            PD_LMP.MGRP:=MODM.MGRP;
            PD_LMP.MGAT:=MODM.MGAT;
            PD_LMP.MODM:=MODM.ref();
            PD_LMP.add(1)
         || PD_LMP.index('PD_LMN');
            PD_LMP.prefix(PD_LMN.ref(),MODM.MGR,MODM.MGRP,MODM.MGAT,MODM.ref());
            {? PD_LMP.first() || PD_LMP.del() ?}
         ?}
      || PD_LMP.index('PD_LMN');
         PD_LMP.prefix(PD_LMN.ref(),MODM.MGR,MODM.MGRP,MODM.MGAT,MODM.ref());
         {? PD_LMP.first()
         || __wybr.blank();
            __wybr.SQL:=$MODM.ref();
            __wybr.add(1)
         ?}
      ?};
      MODM.next()
   !}
?};
{? ~_a || sel_exit() ?};
0


\pd_lmp_m_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: dodanie pozycji listy materialowej na podstawie M
::   WE: [_a] - pobranie informacji z PD_LMN 0-nie(domyslnie) 1-tak
::  OLD: \pd_lmp_m_dod/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? M.first()
|| {!
   |? {? ~_a
      || __wybr.clear();
         {? __wybr.find_key($M.ref())
         || PD_LMP.prefix();
            PD_LMP.blank();
            PD_LMP.PD_LMN:=PD_LMN.ref();
            PD_LMP.MGR:=M.MGR;
            PD_LMP.MGRP:=M.MGRP;
            PD_LMP.MGAT:=M.MGAT;
            PD_LMP.MODM:=M.MODM;
            PD_LMP.M:=M.ref();
            PD_LMP.add(1)
         || PD_LMP.index('PD_LMN');
            PD_LMP.prefix(PD_LMN.ref(),M.MGR,M.MGRP,M.MGAT,M.MODM,M.ref());
            {? PD_LMP.first() || PD_LMP.del() ?}
         ?}
      || PD_LMP.index('PD_LMN');
         PD_LMP.prefix(PD_LMN.ref(),M.MGR,M.MGRP,M.MGAT,M.MODM,M.ref());
         {? PD_LMP.first()
         || __wybr.blank();
            __wybr.SQL:=$M.ref();
            __wybr.add(1)
         ?}
      ?};
      M.next()
   !}
?};
{? ~_a || sel_exit() ?};
0


\pd_lmp_busu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed usu tabeli PD_LMP
::  OLD: \pd_lmp_busu/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
_Sel:=PD_LMP.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#PD_LMP.ref(); _Sel.add() ?};

{? FUN.ask('Usunąć pozycje?'@)
||
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      {? PD_LMP.seek(_Sel.REF,)
      ||
         PD_LMP.del()
      ?};
      _loop:=_Sel.next()
   !};
   PD_LMP.sel_del()
?};

0


\pd_lmn_brek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed rekord tabeli PD_LMP
::  OLD: \pd_lmp_brek/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_LMN.SES_ID<>''
|| exec('pd_lmn_unlock','!lzk_pld_dpla')
?};
~~


\pd_lmn_arek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po rekord tabeli PD_LMP
::  OLD: \pd_lmp_arek/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(PD_LMP,,'KOD')


\pd_lmn_lock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: r_lock PD_LMN
::   WE: [_a] - 0-bez komunikatow, 1-z komunikatami
::  OLD: \pd_lmn_lock/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};

{? exec('lock','#record','PD_LMN','SES_ID',PD_LMN.ref,_a)
|| PD_LMN.get()
?}


\pd_lmn_unlock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: r_unlock na PD_LMN
::  OLD: \pd_lmp_unlock/pd_lm.fml
::----------------------------------------------------------------------------------------------------------------------
exec('unlock','#record','PD_LMN','SES_ID',PD_LMN.ref)


\pd_p_busu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed usun tabeli PD_P
::  OLD: \pd_p_busu/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
_Sel:=PD_P.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#PD_P.ref(); _Sel.CRC:=PD_P.crc(); _Sel.add() ?};

{? {? _Sel.first()+_Sel.next()
   || FUN.ask('Usunąć zaznaczone pozycje?'@)
   || FUN.ask('Usunąć pozycję?'@)
   ?}
||
   exec('ini_kom','#message','Usuwanie pozycji planu dostaw'@);
   PD_P.cntx_psh(); PD_K.cntx_psh();
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      _undo:=0;
      do();
      {? PD_P.seek(_Sel.REF,)
      ||
         _sect:=PD_P.M().KTM;
         {? PD_P.crc()<>_Sel.CRC
         ||
            __kom_on:=1;
            __kom.set_root(_sect);
            __kom.add('Zmieniła się zawartość bufora. Usunięcie niedostępne.'@,7,,1)
         ||
::          koszyk pozycji
            PD_K.index('PD_K'); PD_K.prefix(PD_N.ref(),PD_P.ref());
            {? PD_K.first()
            ||
               __kom.sect_beg(_sect,,,1);
               {!
               |?
                  _wyn:=exec('pd_k_del','plan_dostaw',1);
                  {? _wyn=0
                  || undo(); _undo:=1; 0
                  || PD_K.first()
                  ?}
               !};
               {? _undo=0 || __kom.sect_clr() ?};
               __kom.sect_end()
            ?};
::          pozycja
            {? _undo=0
            ||
               _del:=exec('pd_p_del','plan_dostaw',2);
               {? _del=0
               || undo(); 0
               ?}
            ?}
         ?}
      ?};
      end();
      _loop:=_Sel.next()
   !};
   PD_P.sel_adel();
   PD_P.cntx_pop(); PD_K.cntx_pop();
   exec('end_kom','#message')
?};

0


\pd_p_bbuf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: podglad bufora planu dostaw z pozycji planu
::  OLD: \pd_p_bbuf/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('pd_p_lock','plan_dostaw')
|| DK_C.cntx_psh();
   PD_BUF.fld_fml('DK_C','BEFORE_DISPLAY',"{? PD_BUF.DK_C || DK_C.use(ref_name(PD_BUF.DK_C)) ?}");
   PD_BUF.cntx_psh();
   PD_BUF.index('M'); PD_BUF.prefix(PD_P.M);
   PD_BUF.win_sel('WER');
   PD_BUF.select();
   PD_BUF.cntx_pop();
   exec('pd_p_unlock','plan_dostaw');
   DK_C.cntx_pop()
?}


\pd_p_bzamawiaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: tworzenie zamowien
::  OLD: \pd_p_bzamawiaj/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
exec('pd2zam','!lzk_pld_dpla','P')


\pd_p_banal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: analiza pozycji planu
::  OLD: \pd_p_banal/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
_Sel:=PD_P.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#PD_P.ref(); _Sel.CRC:=PD_P.crc(); _Sel.add() ?};

{? {? _Sel.first() & _Sel.next()
   || ~FUN.ask('Wykonać analizę wybranych pozycji?'@)
   || ~FUN.ask('Wykonać analizę wybranej pozycji?'@)
   ?}
|| return(0)
?};

_params:=exec('pda_a','plan_dostaw');

PD_P.cntx_psh();

_loop:=_Sel.first();
{!
|? _loop
|!
   {? PD_P.seek(_Sel.REF,) & PD_P.STAT<>'Z'
   ||
      {? exec('pd_p_lock','plan_dostaw',0)
      ||
         PD_P.REFRESH:='T';
         {? PD_P.put()
         ||
            exec('pd_pow_del','plan_dostaw',PD_P.PD_N,PD_P.ref(),'','',~~);
            _params.PAR:='';
            _params.M:=PD_P.M;
            _params.TM_STAMP:=PD_P.tm_stamp();
            _params.DK:=PD_P.PD_N().DK;
            _params.TK:=PD_N.TK;
            _params.DISP:=0;
            _params.PD_P:=PD_P.ref();
            _params.PD_P_ODS:=1;
            exec('pda','plan_dostaw',_params);
            exec('pd_p_unlock','plan_dostaw')
         ?}
      ?}
   ?};
   _loop:=_Sel.next()
!};

PD_P.sel_adel();
PD_P.cntx_pop();
0


\pd_p_buzustan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed uzupelnienie stanu PD_P
::  OLD: \pd_p_buzustan/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
_Sel:=PD_P.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#PD_P.ref(); _Sel.CRC:=PD_P.crc(); _Sel.add() ?};

{? {? _Sel.first() & _Sel.next()
   || ~FUN.ask('Uzupełnić stan dla wybranych pozycji?'@)
   || ~FUN.ask('Uzupełnić stan dla wybranej pozycji?'@)
   ?}
|| return(0)
?};

PD_N.cntx_psh(); PD_P.cntx_psh(); PD_D.cntx_psh(); PD_D_USE.cntx_psh();

:: wyznaczenie formul uzupelnienia
_F_uzu:=tab_tmp(2
   ,'SORT'  ,'STRING[10]'  ,'Kolejność'
   ,'KOD'   ,'STRING[10]'  ,'Kod formuły'
   ,'NAZWA' ,'STRING[60]'  ,'Nazwa formuły');

:: formuly uzupelnienia - plan
PD_D_USE.index('PD_D_USE');
PD_D_USE.prefix(PD_P.PD_N,null());
_loop:=PD_D_USE.first();
{!
|? _loop
|!
   {? PD_D_USE.PD_D().TYP='U'
   || _F_uzu.SORT:=PD_D.SORT;
      _F_uzu.KOD:=PD_D.KOD;
      _F_uzu.NAZWA:=PD_D.FORMULA().NAZWA;
      _F_uzu.add()
   ?};
   _loop:=PD_D_USE.next()
!};

:: formuly uzupelnienia - pozycja
PD_D_USE.index('PD_D_USE');
_loop:=_Sel.first();
{!
|? _loop
|!
   {? PD_P.seek(_Sel.REF,)
   ||
      PD_D_USE.index('PD_D_USE');
      PD_D_USE.prefix(PD_P.PD_N,PD_P.ref());
      _loop:=PD_D_USE.first();
      {!
      |? _loop
      |!
         {? PD_D_USE.PD_D().TYP='U' & ~_F_uzu.find_key(PD_D.SORT,PD_D.KOD)
         || _F_uzu.SORT:=PD_D.SORT;
            _F_uzu.KOD:=PD_D.KOD;
            _F_uzu.add()
         ?};
         _loop:=PD_D_USE.next()
      !}
   ?};
   _loop:=_Sel.next()
!};

_params:=exec('pda_a','plan_dostaw');
_params.SEL:=_Sel;
exec('upr_set','ceny');
exec('ini_kom','#message','Uzupełnij stan'@);
{? _F_uzu.size()>1 || exec('wyb_formul_uzup','!lzk_pld_dpla',_F_uzu) || PD_Z.TYPYZAM:=null() ?};
PD_D.index('KOD');
_loop:=_F_uzu.first();
{!
|? _loop
|!
   _params.PD_D:=_F_uzu.KOD;
   {? _params.PD_D<>''
   ||
      __kom_on:=1;
      __kom.set_root(_F_uzu.NAZWA);
      __kom.add('Początek (%1)'@[(16+tm_form(PD_D.tm_stamp))],7);
::    formula uzupelniajaca - init
      {? exec('uzup_init','plan_dostaw',_params,null())
      ||
::       formula uzupelniajaca
         _loop1:=_Sel.first();
         {!
         |? _loop1
         |!
            {? PD_P.seek(_Sel.REF,) & PD_P.STAT<>'Z'
            || {? PD_P.M().A='N'
               || __kom.add('Pozycja planu pominięta. Nieaktywny indeks: %1.'@[M.KTM],7)
               || {? exec('pd_p_lock','plan_dostaw')
               ||
                  _params.DK:=PD_P.PD_N().DK;
                  _params.TK:=PD_N.TK;
                  _params.PD_P_ODS:=1;
                  _params.UZUP_ST:=1;
                  _params.M:=PD_P.M;
                  _params.TM_STAMP:=PD_P.tm_stamp();
                  _params.PD_P:=PD_P.ref();
                  exec('pda','plan_dostaw',_params);
                  PD_P.get();
                  PD_P.ILKOSZYK:=exec('ilwkoszyku','plan_dostaw',PD_N.ref(),PD_P.ref());
                  PD_P.put();
                  exec('pd_p_unlock','plan_dostaw')
               ?}
               ?}
            ?};
            _loop1:=_Sel.next()
         !}
      || __kom.add('Formuła pominięta.'@,7)
      ?};
      __kom.add('Koniec (%1)'@[(16+tm_form(PD_D.tm_stamp))],7)
   ?};
   _loop:=_F_uzu.next()
!};
exec('end_kom','#message');

PD_N.cntx_pop(); PD_P.cntx_pop(); PD_D.cntx_pop(); PD_D_USE.cntx_pop();
0


\pd_p_bformuly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed formuly planu PD_P
::  OLD: \pd_p_bformuly/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('pd_p_lock','plan_dostaw')
|| PD_Z.NAGPOZ:='P';
   PD_D_USE.cntx_psh();
   _wer:='GRP';
   PD_D_USE.index('PD_D_USE'); PD_D_USE.prefix(PD_N.ref(),PD_P.ref());
   PD_D_USE.first();
   PD_D_USE.win_sel(_wer);
   _act:=
      'A'
      +
      {? exec('mozna_wybrac_zestaw','!lzk_pld_dpla',PD_P) || '' || 'Z:Z' ?};
   PD_D_USE.actions('WER',_act);
   exec('wer_pd_d_use','!lzk_pld_dpla',PD_Z.NAGPOZ);
   PD_D_USE.select();
   PD_D_USE.cntx_pop();
   exec('pd_p_unlock','plan_dostaw')
?}


\pd_p_bzamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed zamkniji PD_P
::  OLD: \pd_p_bzamknij/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('pd_p_lock','plan_dostaw') || return() ?};
PD_K.cntx_psh();
PD_K.index('PD_K');
PD_K.prefix(PD_P.PD_N,PD_P.ref());
_ret:=0;
_loop:=PD_K.first();
{!
|? _loop
|!
   {? exec('pd_k_lock','plan_dostaw')=0 || _ret:=1 ?};
   _loop:=_ret=0 & PD_K.next()
!};

{? _ret=0
||
   {? FUN.ask('Zamknąć pozycję %1?'@[PD_P.M().KTM])
   ||
      do();
      PD_P.get();
      PD_P.REFRESH:=PD_P.STAT:='Z';
      PD_P.put();
      exec('pd_k_zamknij','!lzk_pld_dpla',PD_P.PD_N,PD_P.ref());
      end()
   ?}
?};

PD_K.index('SID');
PD_K.prefix(PD_K.ses_id());
_loop:=PD_K.first();
{!
|? _loop
|!
   exec('pd_k_unlock','plan_dostaw');
   _loop:=PD_K.first()
!};

PD_K.cntx_pop();
exec('pd_p_unlock','plan_dostaw')


\pd_p_bzamkwyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed wycofaj zamkniecie PD_P
::  OLD: \pd_p_bzamkwyc/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Wycofać zamkniecie pozycji %1?'@[PD_P.M().KTM])
||
   PD_P.get();
   PD_P.STAT:={? PD_P.STAT_DA=date(0,0,0) || 'N' || 'P' ?};
   PD_P.REFRESH:=
      {? PD_P.STAT='N'
      || 'T'
      || {? PD_P.TMSTAMPA<PD_P.TMSTAMPB || 'T' || 'N' ?}
      ?};
   do();
   PD_P.put();
   exec('pd_k_zamkwyc','!lzk_pld_dpla',PD_P.PD_N,PD_P.ref());
   end()
?}


\pd_p_bszcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: szczegoly pozycji planu dostawy
::  OLD: \pd_p_bszcz/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('pd_p_lock','plan_dostaw') || return() ?};

PD_N.cntx_psh();

_params:=exec('pda_a','plan_dostaw');
_params.PAR:='';
_params.M:=PD_P.M;
_params.TM_STAMP:=PD_P.tm_stamp();
_params.DK:=PD_P.PD_N().DK;
_params.TK:=PD_N.TK;
_params.DISP:=1;
_params.PD_P:=PD_P.ref();
_params.PD_P_ODS:=1;
_params.ODSW:=0;

PD_N.cntx_pop();

exec('pda','plan_dostaw',_params);

exec('pd_p_unlock','plan_dostaw')


\pd_k_bpop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed popraw tabeli PD_K
::  OLD: \pd_k_bpop/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
ATR.MJS:='PD_K';
ATR.M_ATR:=PD_K.M().M_ATR;
ATR.UZUP:=exec('wz_uzup','mat_atr',ATR.M_ATR);
ATR.FLAG_ED:=(1+PD_K.MG().TYP)='D' & ATR.CZY_ATR & ATR.M_ATR().EDIT;
ATR.FLAG:={? ATR.FLAG_ED & PD_K.M().M_ATR<>null() || 2 || 0 ?};
{? ATR.FLAG_ED || {? PD_K.M().M_ATR().EDIT || ATR.FLAG_ED:=2 ?} ?};
{? PD_K.DK_C<>null() & PD_K.DK_C().M_ATR<>null()
|| {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():=($('PD_K.DK_C().WAR'+form(_i,-2,,'99')))() !}
|? PD_K.M().M_ATR=null() | PD_K.DK_C=null()
|| {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !}
?};

{? exec('pd_k_lock','plan_dostaw')
||
   exec('var_set','plan_dostaw',PD_K.TYPYZAM().R);

   PD_K.cntx_psh();
   {? PD_K.ILP<>0
   ||
      {? PD_K.T2='N' | PD_K.WS2=0 | PD_K.J2=null()
      ||
         PD_K.IL2:=PD_K.IL;
         PD_K.WS2:=1;
         PD_K.J2:=PD_K.M().J
      ?};
      {? PD_K.edit("exec('pd_k_chk','plan_dostaw')")
      ||
         exec('ustapdkprz','jm',PD_K.M,PD_K.M().J);
         {? PD_K.POZ_REF=''
         || PD_K.ZAM_REF:='';
            PD_K.ZAM_SYM:=''
         ?};
         {? PD_K.put()
         ||
            exec('FindAndGet','#table',PD_P,PD_K.PD_P,,"
               PD_P.ILKOSZYK:=exec('ilwkoszyku','plan_dostaw',PD_P.PD_N,PD_P.ref());
               PD_P.put()
            ")
         ?}
      ||
         MG.f_clear()
      ?}
   ||
::      PD_K.hdr_edit(' - powiązana z pozycją zamówienia'@);
      PD_K.display()
   ?};
   PD_K.cntx_pop();
   exec('pd_k_unlock','plan_dostaw')
?}


\pd_k_busu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed usun tabeli PD_K
::  OLD: \pd_k_busu/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
_Sel:=PD_K.sel_aget();
{? ~_Sel.first() || PD_K.get(); _Sel.REF:=#PD_K.ref(); _Sel.CRC:=PD_K.crc(); _Sel.add() ?};

{? {? _Sel.first() & _Sel.next()
   || FUN.ask('Usunąć zaznaczone pozycje?'@)
   || FUN.ask('Usunąć pozycję?'@)
   ?}
||
   _disp:=0;
   exec('ini_kom','#message','Usuwanie pozycji'@);
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      {? PD_K.seek(_Sel.REF,)
      ||
         {? PD_K.crc()<>_Sel.CRC
         ||
            __kom_on:=1;
            __kom.sect_beg($PD_K.ID);
            __kom.add('Zmieniła się zawartość bufora. Usunięcie niedostępne.'@,7,,1)
         ||
            _zam_ref:=PD_K.ZAM_REF;
            _disp+=PD_K.ZAM_REF<>'';
            _pd_n:=PD_K.PD_N;
            _pd_p:=PD_K.PD_P;
            {? exec('pd_k_del','plan_dostaw',1)
            || exec('del','zamowienia',_zam_ref)
            ?};
            PD_P.cntx_psh();
            {? PD_P.seek(_pd_p)
            || PD_P.get();
               PD_P.ILKOSZYK:=exec('ilwkoszyku','plan_dostaw',_pd_n,_pd_p);
               PD_P.put()
            ?};
            PD_P.cntx_pop()
         ?}
      ?};
      _loop:=_Sel.next()
   !};
   PD_K.sel_adel();
   exec('end_kom','#message')
?};
0


\pd_k_bdd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: pozycja koszyka dedykowana dla zamowien - dostawa dedykowana
::  OLD: \pd_k_bdd/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('pd_k_lock','plan_dostaw') || return() ?};

{? PD_K.TYPYZAM().R='W'
|| FUN.info('Funkcja niedostępna dla pozycji dotyczącej zamówienia wewnętrznego.'@);
   exec('pd_k_unlock','plan_dostaw');
   return()
?};

_czysc:="VAR_DEL.delete(exec('pddd_ns','plan_dostaw'));
         VAR_DEL.delete('__zaz')";
_czysc();
__zaz:=tab_tmp(1,'REF','STRING[16]','');
:: _ns - przestrzen nazw
:: TAB1 - pozycje zamowien przypisane do pozycji koszyka w ramach dostawy dedykowanej
_ns:="($(_a+':='+_b))()"; _ns:=_ns(exec('pddd_ns','plan_dostaw'),"obj_new('TAB1')");
_Tab1:=_ns.TAB1:=obj_new('TAB','NDX1','NDX2','WER1');
_Tab1t:=_Tab1.TAB:=exec('pddd_tab1','!lzk_pld_dpla');
_Tab1.NDX1:=_Tab1t.index('?'); 'kolejnosc: D';
_Tab1.NDX2:=_Tab1t.ndx_tmp(,,'PD_K_DD',,,'ZK_P',,);
_Tab1.WER1:=exec('pddd_tab1_wer1','!lzk_pld_dpla');
_wer1:=exec('pddd_tab1_wsk','plan_dostaw','WER1');

:: wypelnienie tabeli
_params:=exec('pda_a','plan_dostaw');
_params.M:=PD_K.M;
_params.DK:=PD_K.PD_N().DK;
_params.TK:=PD_N.TK;
_params.PD_K:=PD_K.ref();
exec('dd','%plan_dostaw',_params);

_Tab1t.prefix();
{? _Tab1t.first()
|| {!
   |? {? _Tab1t.DD='T'
      || __zaz.clear();
         __zaz.blank();
         __zaz.REF:=$_Tab1t.ref();
         __zaz.add(1)
      ?};
      _Tab1t.next()
   !}
?};
_Tab1t.win_sel(_wer1);
_Tab1t.select();

exec('pd_k_unlock','plan_dostaw');
_Tab1t.win_sel();
_Tab1t.win_del(_wer1);
_czysc()


\pd_k_bzamawiaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: tworzenie zamowien
::----------------------------------------------------------------------------------------------------------------------
exec('pd2zam','!lzk_pld_dpla','K')


\pddd_tab1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: pozycje zamowien w ramach dostawy dedykowanej
::  OLD: \pddd_tab1/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=tab_tmp(1
   ,'D'        ,'DATE'        ,'Data'
   ,'ZAM'      ,'STRING[50]'  ,'Zamówienie'
   ,'IL'       ,'REAL'        ,'Ilość'
   ,'ZK_P'      ,'STRING[16]'  ,'$ZK_P'
   ,'DD'       ,'STRING[1]'   ,'Dostawa dedykowana [T/N]'
   ,'PD_K_DD'  ,'STRING[16]'  ,'$PD_K_DD');
_wyn.fld_attr('PD_K_DD',2);
_wyn.fld_attr('ZK_P',2);
_wyn


\pddd_tab1_wer1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: pozycje zamowien w ramach dostawy dedykowanej - okno wertowania
::  OLD: \pddd_tab1_wer1/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=exec('pddd_tab1_wsk','plan_dostaw','TAB1');

_wyn:=obj_new('WER');

_wer:=_Tab.mk_sel('Pozycje zamówień w ramach dostawy dedykowanej'@,,,'#kdfjjfiapdadnv',1,,,,'U');
_wyn.WER:=_wer;

_Tab.win_fld(_wer,,'D',,,10,,,'Data'@);
_Tab.win_fld(_wer,,'ZAM',,,50,,,'Zamówienie'@);
_Tab.win_fld(_wer,,'IL',,,10,ST.DOKL,,'Ilość'@);
_Tab.win_fld(_wer,POM,'PAR5',,,10,,,'Dostawa dedykowana'@,,,2,,"'T'","'N'");
_fb:="_Tab:=cur_tab(1,1);
      __zaz.clear();
      __zaz.prefix($_Tab.ref());
      {? __zaz.first()
      || __zaz.del()
      || __zaz.blank(); __zaz.REF:=$_Tab.ref(); __zaz.add(1)
      ?};
      ~~
     ";
_Tab.win_act(_wer,,'Formuła','Dostawa dedykowana'@@,,,_fb,,1);

_formula:="{? FUN.ask('Czy zaakceptować zmiany dla dostaw dedykowanych?'@)
           || sel_exit();
              _Tab:=cur_tab(1,1);
              _Tab.prefix();
              {? _Tab.first()
              || {!
                 |? __zaz.clear();
                    __zaz.prefix($_Tab.ref());
                    {? (_Tab.DD='T' & ~__zaz.first()) | (_Tab.DD='N' & __zaz.first())
                    || exec('pddd_act_dd','!lzk_pld_dpla')
                    ?};
                    _Tab.next()
                 !}
              ?}
           ?}";
_Tab.win_act(_wer,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
_Tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A',,,,,,'noempty');
_Tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
_Tab.win_act(_wer,,'Kolejność',);
_formula:="_Tab:=cur_tab(1,1);
           __zaz.clear();
           __zaz.prefix($_Tab.ref());
           POM.PAR5:={? __zaz.first() || 'T' || 'N' ?};
           ~~";
_Tab.win_act(_wer,,'Rekord' ,,,,_formula);

_wyn


\pddd_act_dd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przypisanie pozycji zamowienia do koszyka, przyszlej dostawy dedykowanej
::  OLD: \pddd_act_dd/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=exec('pddd_tab1_wsk','plan_dostaw','TAB1');

PD_K_DD.cntx_psh();
do();
{? _Tab.DD='T'
||
   PD_K_DD.prefix();
   {? PD_K_DD.seek(_Tab.PD_K_DD)
   || ZK_P.cntx_psh();
      ZK_P.use(8+PD_K_DD.ZK_P);
      ZK_P.prefix();
      {? ZK_P.seek(PD_K_DD.ZK_P)
      ||
         ZK_P.ZD_POZ:='';
         ZK_P.put()
      ?};
      ZK_P.cntx_pop();
      PD_K_DD.del();
      _Tab.DD:='N';
      _Tab.PD_K_DD:='';
      _Tab.put()
   ?}
||
   PD_K_DD.prefix();
   PD_K_DD.PD_K:=PD_K.ref();
   PD_K_DD.ZK_P:=_Tab.ZK_P;
   {? PD_K_DD.add()
   || _Tab.DD:='T';
      _Tab.PD_K_DD:=$PD_K_DD.ref();
      _Tab.put();
      ZK_P.cntx_psh();
      ZK_P.use(8+PD_K_DD.ZK_P);
      ZK_P.prefix();
      {? ZK_P.seek(PD_K_DD.ZK_P)
      ||
         ZK_P.ZD_POZ:=
            {? PD_K_DD.PD_K().POZ_REF=''
            || $PD_K_DD.PD_K
            || PD_K.POZ_REF
            ?};
         ZK_P.put()
      ?};
      ZK_P.cntx_pop()
   ?}
?};
end();
PD_K_DD.cntx_pop()


\pd_p_addf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: analiza pełnej kartoteki materiałowej dla pozycji planu dostaw
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy dodać do planu pozycję wg kartoteki materiałowej?\n\n'
           'Uwaga. Analizowane są wszystkie pozycje.\n'
           'Do planu dołączone zostaną indeksy materiałowe wykazujące problem.'@)
|| _params:=exec('pda_a','plan_dostaw');
:: zachowanie informacji o indeksach na aktualnym stanie
   _buf:=tab_tmp(1,'MAT','STRING[16]','');
   PD_P.cntx_psh();
   {? PD_P.first()
   || {!
      |? _buf.clear();
         _buf.blank();
         _buf.MAT:=$PD_P.M;
         _buf.add(1);
         PD_P.next()
      !}
   ?};
   M.cntx_psh();
   M.index('ARODZ');
   M.prefix('T','T');
   {? M.first()
   || {!
      |? M.cntx_psh();
         do();
         echo('Analizuję ... %1'@[M.KTM]);
         _dost:=exec('dost','ceny_mat',M.ref(),date(0,0,0),INFO.NAROD,0).KH;
         exec('pd_p_add','plan_dostaw',PD_N.ref(),M.MGR,M.MGRP,M.MGAT,M.MODM,M.ref(),M.J,_dost);
         {? exec('pd_p_lock','plan_dostaw',0)
         || _params.PAR:='';
            _params.M:=PD_P.M;
            _params.TM_STAMP:=PD_P.tm_stamp();
            _params.DK:=PD_P.PD_N().DK;
            _params.TK:=PD_N.TK;
            _params.DISP:=0;
            _params.PD_P:=PD_P.ref();
            _params.PD_P_ODS:=1;
            exec('pda','plan_dostaw',_params);
            exec('pd_p_unlock','plan_dostaw');
            {? 'AN'*PD_P.STAT & (_buf.clear(); ~_buf.find_key($PD_P.M)) || exec('pd_p_del','plan_dostaw',0) ?}
         ?};
         end();
         M.cntx_pop();
         M.next()
      !};
      echo()
   ?};
   M.cntx_pop();
   PD_P.cntx_pop();
   PD_P.f_rfresh();
   obj_del(_buf)
?};
0


\filtr_typzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Ustawienie filtru dla TYPZAM
::----------------------------------------------------------------------------------------------------------------------
TYPYZAM.prefix();
TYPYZAM.f_clear();
TYPYZAM.f_set('T',,'R=\''+PD_Z.RODZ+'\'');
1


\pd_k_typzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Po wyborze typu zamówienia
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? PD_K.TYPYZAM=null()
|| FUN.info('Należy wskazać typ zamówienia.'@);
   0
|| BEER.TYPYZAM:=TYPYZAM.ref();
   exec('ustapdkprz','jm');
   exec('var_set','plan_dostaw',TYPYZAM.R);
   {? PD_K.TYPYZAM().R='D' & PD_K.KH=null() || PD_K.KH:=exec('dost','ceny_mat',PD_P.M,date,PD_K.WAL,0).KH ?};
   {? TYPYZAM.R='D'
   || exec('ustapdkprz','jm',PD_K.M,PD_K.JM)
   || exec('ustazprz','zamsiw_poz',PD_K.M,PD_K.JM)
   ?};

   ATR.MJS:='PD_K';
   ATR.M_ATR:=PD_K.M().M_ATR;
   ATR.UZUP:=exec('wz_uzup','mat_atr',ATR.M_ATR);
   ATR.FLAG:=0;
   ATR.FLAG_ED:=0;
   {? PD_K.DK_C<>null() & PD_K.DK_C().M_ATR<>null()
   || {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():=($('PD_K.DK_C().WAR'+form(_i,-2,,'99')))() !}
   || {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !}
   ?};
   1
?}


\wyb_formul_uzup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: umożliwia wybór formuł do uzupełnienia stanu
::   WE: _a - uchwyt tabeli formuł
::----------------------------------------------------------------------------------------------------------------------
_buf:=_a;
VAR_DEL.delete('__zatw');
__zatw:=0;
_tab:=tab_tmp(1,'SYM','STRING[20]','Kod'@
      ,'NAZ','STRING[60]','Nazwa'@
      ,'ACCESS','STRING[1]','Wybrano'@
      ,'REF','STRING[16]','Ref SQL');

_buf.clear();
{? _buf.first()
|| {!
   |? _tab.blank();
      _tab.SYM:=_buf.KOD;
      _tab.NAZ:=_buf.NAZWA;
      _tab.REF:=$_buf.ref();
      _tab.ACCESS:='N';
      _tab.add(1);
      _buf.next()
   !}
?};

_sel:=_tab.mk_sel('Formuły uzupełnienia koszyka'@,'P',,'pd_p2pd_k',,,,,'U');
_tab.win_fld(_sel,,'ACCESS',,,,,,,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
_tab.win_fld(_sel,,'SYM');
_tab.win_fld(_sel,,'NAZ');

_formula:="_tab:=cur_tab(); _tab.ACCESS:='T'; _tab.put()";
_tab.win_act(_sel,,'Formuła','Wybierz'@@,,,_formula,,1,1,,,'W');
_tab.win_btn(_sel,'text=%1,panel=right,align=begin'['Wybierz'@],'menu:W',,,,,,'noempty');
_formula:="_tab:=cur_tab(); _tab.ACCESS:='N'; _tab.put()";
_tab.win_act(_sel,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
_tab.win_btn(_sel,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O',,,,,,'noempty');
_formula:="__zatw:=1; sel_exit()";
_tab.win_act(_sel,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
_tab.win_btn(_sel,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A',,,,,,'noempty');
_tab.win_btn(_sel,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
_tab.win_act(_sel,,'Kolejność',);
_tab.win_act(_sel,,'Okienko',,,,,"
   {? ~__zatw
   || FUN.ask('Czy zakończyć wybór formuł uzupełnienia koszyka?\nUwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@)
   || 1
   ?}");
_tab.win_act(_sel,,'Rekord',,,,"
      _tab:=cur_tab();
      {? _tab.ACCESS='T' | _tab.sel_size()
      || _tab.actions(_tab.win_sel('?'),,'O',1)
      || _tab.actions(_tab.win_sel('?'),,'W',1)
      ?}
   ");
_tab.win_sel(_sel);
{? _tab.select()
|| _tab.clear();
   {? _tab.first()
   || {!
      |? {? _tab.ACCESS<>'T' & (_buf.clear(); _buf.seek(_tab.REF))
         || _buf.del()
         ?};
         _tab.next()
      !}
   ?}
|| _buf.erase()
?};
VAR_DEL.delete('__zatw');

{? _buf.size>1 & FUN.ask('Czy wybrać wspólne parametry generowania dla wszystkich przetwarzanych pozycji?'@)
|| exec('wyb_zam','zamowienia','D');
   PD_Z.TYPYZAM:=BEER.TYPYZAM
?};
~~


\pd_buf_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Aktualizacja bufora planu dostaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask('Zaaktualizować bufor planu dostaw?') || return() ?};

PD_BUF_D.index('KOD');
PD_BUF_D.prefix();
_loop:=PD_BUF_D.first();
_ii:=0;
_size:=PD_BUF_D.size();
{!
|? _loop
|!
   progress((_ii/_size)*100,,'Aktualizacja bufora planu dostaw'@);
   PD_Z.PAR:={? PD_BUF_D.A='T' || 'S' || 'E' ?};
   ($PD_BUF_D.FORMULA().FORMULA)();
   _ii+=1;
   _loop:=PD_BUF_D.next()
!};
{? _size || progress((_ii/_size)*100) ?};
prgs_clr()


\kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Dostawcy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=tab_tmp(1
   ,'KOD',  'STRING[8]',   'Kod'
   ,'NAZ',  'STRING[60]',  'Nazwa'
   ,'REF',  'STRING[16]',  '$KH.ref()');

KH.cntx_psh();
M.cntx_psh();
MDOST.cntx_psh();
MDOST.index('MREF');
_loop:=MDOST.first();
{!
|? _loop
|!
   {? MDOST.M().A='T'
   ||
      _kod:=MDOST.KH().KOD;
      {? ~_Tab.find_key(_kod)
      ||
         _Tab.KOD:=_kod;
         _Tab.NAZ:=MDOST.KH().NAZ;
         _Tab.REF:=$MDOST.KH;
         _Tab.add()
      ?}
   ?};
   _loop:=MDOST.next()
!};
MDOST.cntx_pop();
M.cntx_pop();
KH.cntx_pop();
_Tab


\pd_lmp_kh_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: dodanie pozycji listy materialowej dla wybranych dostawców
::   WE: [_a] - pobranie informacji z PD_LMN 0-nie(domyslnie) 1-tak
::----------------------------------------------------------------------------------------------------------------------
_Tab:=__KH;
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _Tab.first()
||
   {!
   |?
      _kh:=exec('FindAndGet','#table',KH,_Tab.REF,,,null());
      {? ~_a
      ||
         __wybr.clear();
         {? __wybr.find_key($_Tab.ref())
         ||
            PD_LMP.prefix();
            PD_LMP.blank();
            PD_LMP.PD_LMN:=PD_LMN.ref();
            PD_LMP.KH:=_kh;
            PD_LMP.add(1)
         ||
            PD_LMP.index('PD_LMN');
            PD_LMP.prefix(PD_LMN.ref(),_kh);
            {? PD_LMP.first() || PD_LMP.del() ?}
         ?}
      ||
         PD_LMP.index('PD_LMN');
         PD_LMP.prefix(PD_LMN.ref(),null(),null(),null(),null(),null(),_kh);
         {? PD_LMP.first()
         ||
            __wybr.blank();
            __wybr.SQL:=$_Tab.ref();
            __wybr.add(1)
         ?}
      ?};
      _Tab.next()
   !};
   {? ~_a || sel_exit() ?}
?};
0


\pd_d_zu_default_zu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: PD_D_ZU - Domyślny zestaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_default:='';
{? PD_D_ZU.DEFAULT='T'
||
   {? FUN.ask('Czy zestaw formuł ma przestać być domyślny?'@)
   ||
      _default:='N'
   ?}
||
   {? FUN.ask('Czy zestaw formuł ma być domyślny?'@)
   ||
      _default:='T'
   ?}
?};
{? _default<>''
||
   {? _default='N'
   ||
      PD_D_ZU.DEFAULT:='N';
      PD_D_ZU.put()

   |? _default='T'
   ||
      PD_D_ZU.cntx_psh();
      PD_D_ZU.index('USER');
      PD_D_ZU.prefix(OPERATOR.USER,'T');
      {? PD_D_ZU.first()
      ||
         PD_D_ZU.prefix();
         PD_D_ZU.DEFAULT:='N';
         PD_D_ZU.put()
      ?};
      PD_D_ZU.cntx_pop();
      PD_D_ZU.DEFAULT:='T';
      PD_D_ZU.put()
   ?}
?}


\pd_n_pd_d_zn_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: PD_N.PD_D_ZN - wartość domyślna
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null();
PD_D_ZU.cntx_psh();
PD_D_ZU.index('USER');
PD_D_ZU.prefix(OPERATOR.USER);
{? PD_D_ZU.find_key('T')
      |
   PD_D_ZU.first() & ~PD_D_ZU.next()
||
   _wyn:=PD_D_ZU.PD_D_ZN;
   PD_Z.PD_D_ZN:=PD_D_ZU.PD_D_ZN().NAZ
?};
PD_D_ZU.cntx_pop();
_wyn


\pd_z_pd_d_zn_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: PD_Z.PD_D_ZN - przed wyświetleniem
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PD_Z.PD_D_ZN:=PD_N.PD_D_ZN().NAZ


\pd_z_pd_d_zn_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: PD_Z.PD_D_ZN - przed redakcją
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('mozna_wybrac_zestaw','!lzk_pld_dpla',cur_tab(1,1))


\pd_z_pd_d_zn_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: PD_Z.PD_D_ZN - F3
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_pd_d_zn:=exec('wybierz_zestaw','!lzk_pld_dpla',PD_Z.PD_D_ZN);
PD_N.PD_D_ZN:=_pd_d_zn;
PD_Z.PD_D_ZN:=PD_N.PD_D_ZN().NAZ


\pd_z_pd_d_zn_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: PD_Z.PD_D_ZN - po redakcji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();
_wyn:=1;
{? _fld=''
||
   PD_N.PD_D_ZN:=null()
||
   PD_D_ZN.cntx_psh();
   PD_D_ZN.index('NAZ');
   PD_D_ZN.prefix(_fld,);
   {? ~PD_D_ZN.first()
   ||
      FUN.info('Brak wartości w słowniku.\nNależy poprawić lub usunąć wartość pola.'@);
      _wyn:=0
   ||
      PD_N.PD_D_ZN:=PD_D_ZN.ref()
   ?};
   PD_D_ZN.cntx_pop()
?};
_wyn


\mozna_wybrac_zestaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Sprawdza czy można wybrać zestaw formuł
::   WE: _a - uchwyt do tabeli
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_wyn:=1;
PD_K.cntx_psh();
{? _tab=PD_N
||
   PD_K.index('PD_K');
   PD_K.prefix(PD_N.ref());
   {? PD_K.first() || _wyn:=0 ?}

|? _tab=PD_P
||
   PD_K.index('PD_K');
   PD_K.prefix(PD_N.ref(),PD_P.ref());
   {? PD_K.first() || _wyn:=0 ?}
?};
PD_K.cntx_pop();
{? _wyn
||
   PD_D_ZU.win_dict('SLO');
   PD_D_ZU.index('USER');
   PD_D_ZU.prefix(OPERATOR.USER);
   {? ~PD_D_ZU.first() || _wyn:=0 ?}
?};
_wyn


\wybierz_zestaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Wybór zestawu formuł
::   WE: _a - nazwa zestawu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_naz:=_a;
_wyn:=null();
PD_D_ZU.index('NAZ');
PD_D_ZU.prefix(OPERATOR.USER);
{? PD_D_ZU.find_key(_naz,)
||
   _wyn:=PD_D_ZU.PD_D_ZN
?};
PD_D_ZU.win_sel('SLO');
{? PD_D_ZU.select(,1,1)
||
   _wyn:=PD_D_ZU.PD_D_ZN
?};
_wyn


\pd_d_use_dolacz_zestaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Dołączenie zestawu formuł do nagłówka lub pozycji planu dostaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PD_Z.NAGPOZ<>'N' & PD_Z.NAGPOZ<>'P'
||
   FUN.info('Nieobsługiwane miejsce wywołania.'@)

||
   _tab:={? PD_Z.NAGPOZ='N' || PD_N || PD_P ?};
   {? ~exec('mozna_wybrac_zestaw','!lzk_pld_dpla',_tab)
   ||
      FUN.info('Wybór zestawu formuł niedostępny.'@)
   ||
      {? PD_Z.NAGPOZ='N'
      ||
         _pd_n:=PD_N.ref();
         _pd_p:=null();
         _pd_d_zn_b:=PD_N.PD_D_ZN;
         _naz:=PD_N.PD_D_ZN().NAZ
      ||
         _pd_n:=PD_N.ref();
         _pd_p:=PD_P.ref();
         _pd_d_zn_b:=PD_P.PD_D_ZN;
         _naz:=PD_P.PD_D_ZN().NAZ
      ?};
      _pd_d_zn_a:=exec('wybierz_zestaw','!lzk_pld_dpla',_naz);
      {? _pd_d_zn_b<>_pd_d_zn_a
      ||
         do();
         PD_D_USE.cntx_psh();
         PD_D_USE.index('PD_D_USE');
         {? PD_Z.NAGPOZ='N'
         ||
            PD_N.PD_D_ZN:=_pd_d_zn_a;
            PD_N.put();
            PD_D_USE.prefix(PD_N.ref())
         ||
            PD_P.PD_D_ZN:=_pd_d_zn_a;
            PD_P.put();
            PD_D_USE.prefix(PD_N.ref(),PD_P.ref())
         ?};
         exec('wer_pd_d_use','!lzk_pld_dpla',PD_Z.NAGPOZ);
         _loop:=PD_D_USE.first();
         {!
         |? _loop
         |!
            _loop:=PD_D_USE.del()
         !};
         PD_D_ZP.cntx_psh();
         PD_D_ZP.index('PD_D_ZN');
         PD_D_ZP.prefix(_pd_d_zn_a);
         _loop:=PD_D_ZP.first();
         {!
         |? _loop
         |!
            PD_D_USE.PD_N:=_pd_n;
            PD_D_USE.PD_P:=_pd_p;
            PD_D_USE.PD_D:=PD_D_ZP.PD_D;
            _loop:=PD_D_USE.add() & PD_D_ZP.next()
         !};
         PD_D_ZP.cntx_pop();
         PD_D_USE.cntx_pop();
         end()
      ?}
   ?}
?}


\pd_n_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Plan dostawy - Magazyny
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('pd_n_lock','plan_dostaw') || return(0) ?};

PD_Z.NAGPOZ:='N';
_wer:=PD_MG.mk_sel('Magazyny do analizy'@,,,'pd_mg_wer',,,,,'U');
PD_MG.win_fld(_wer,,'MG','ODDZ',,3,,1,'Oddział'@);
PD_MG.win_fld(_wer,,'MG','SYM',,8,,1,'Magazyn'@);
PD_MG.win_fld(_wer,,'MG','NAZ',,60,,1,'Nazwa'@);
_wer_mm:=PD_MG.mk_sel('Magazyny do przesunięć'@,,,'pd_mg_wer_mm',,,,,'U');
PD_MG.win_fld(_wer_mm,,'SORT',,,10,,,'Kolejność do przesunięć'@);
PD_MG.win_fld(_wer_mm,,'MG','ODDZ',,3,,1,'Oddział'@);
PD_MG.win_fld(_wer_mm,,'MG','SYM',,8,,1,'Z magazynu'@);
PD_MG.win_fld(_wer_mm,,'MG','NAZ',,60,,1,'Nazwa'@);
_wer_gr:=PD_MG.grp_make('Magazyny planu dostaw'@,,'pd_mg_wer_gr');
_fb:="PD_MG.index('WER'); PD_MG.prefix('N',PD_N.ref(),null()); PD_MG.first()";
PD_MG.grp_sel(_wer_gr,,_wer,'Magazyny do analizy'@,,,,,_fb,,,,'maximized');
_fb:="PD_MG.index('WER'); PD_MG.prefix('T',PD_N.ref(),null()); PD_MG.first()";
PD_MG.grp_sel(_wer_gr,,_wer_mm,'Magazyny do przesunięć'@,,,,,_fb,,,,'maximized');
PD_K.cntx_psh();
PD_K.index('PD_K');
PD_K.prefix(PD_N.ref());
{? ~PD_K.first() & PD_N.PLANPROD<>'T'
||
   _fb:="exec('pd_mg_dol','!lzk_pld_dpla','N')";
   PD_MG.win_act(_wer,1,'Formuła','Dołącz'@@,,,_fb,,1);
   PD_MG.win_act(_wer,,'Formuła','Dołącz'@@,,,_fb,,1);
   _fb:="exec('pd_mg_dolacz_zestaw','!lzk_pld_dpla')";
   PD_MG.win_act(_wer,1,'Formuła','Dołącz &zestaw magazynów'@@,,,_fb);
   PD_MG.win_act(_wer,,'Formuła','Dołącz &zestaw magazynów'@@,,,_fb);
   PD_MG.win_act(_wer,,'Usuń',,,,,"PD_N.PD_MG_ZN:=null(); PD_N.put(); exec('pd_p_refresh2','plan_dostaw')",,1);
   _fb:="exec('pd_mg_dol','!lzk_pld_dpla','T')";
   PD_MG.win_act(_wer_mm,1,'Formuła','Dołącz'@@,,,_fb,,1);
   PD_MG.win_act(_wer_mm,,'Formuła','Dołącz'@@,,,_fb,,1);
   _fb:="exec('pd_mg_dolacz_zestaw','!lzk_pld_dpla')";
   PD_MG.win_act(_wer_mm,1,'Formuła','Dołącz &zestaw magazynów'@@,,,_fb);
   PD_MG.win_act(_wer_mm,,'Formuła','Dołącz &zestaw magazynów'@@,,,_fb);
   PD_MG.win_act(_wer_mm,,'Popraw');
   PD_MG.win_act(_wer_mm,,'Usuń',,,,,"PD_N.PD_MG_ZN:=null(); PD_N.put(); exec('pd_p_refresh2','plan_dostaw')",,1)

?};
PD_K.cntx_pop();
PD_MG.cntx_psh();
PD_MG.win_sel(_wer_gr);
PD_MG.select();
exec('pd_mg_verify','!lzk_pld_dpla');
PD_MG.cntx_pop();
exec('pd_n_unlock','plan_dostaw');
0


\pd_mg_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Magazyny planu dostaw - Dołącz
::   WE: _a - T/N - magazyny do przesunięć
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mm:=_a;
_czy_zm:='N';

PD_MG.cntx_psh();
_pd_mg:=exec('pd_mg_sel','plan_dostaw',_mm,PD_MG);
{? ~_pd_mg.EXIT
||
   PD_K.cntx_psh();
   PD_K.index('PD_K');
   PD_K.prefix(PD_N.ref());
   {? PD_K.first()
   ||
      FUN.info('Plan ma pozycje w koszyku.\nZmiana definicji magazynów planu dostaw niedostępna.'@)
   ||
      _mod:=0;
      PD_MG.index('UNIK');
      PD_MG.prefix(_mm,PD_N.ref(),null());
      _Tab:=_pd_mg.TAB;
      _ndx:=_pd_mg.NDX2;
      _Tab.index(_ndx);
:: usunięcie niewybranych magazynów
      _Tab.cntx_psh();
      _ndx:=_Tab.ndx_tmp(,,'REF',,);
      _Tab.index(_ndx);
      _loop:=PD_MG.first();
      {!
      |? _loop
      |!
         _loop:=
            {? _Tab.find_key($PD_MG.MG) & _Tab.MARK='T'
            || PD_MG.next()
            || _mod:=1;
               _czy_zm:='T';
               PD_MG.del()
            ?}
      !};
      _Tab.cntx_pop();
:: dodanie wybranych magazynów
      _loop:=_Tab.first();
      {!
      |? _loop
      |!
         {? _Tab.MARK='T'
         ||
            _mg:=exec('FindAndGet','#table',MG,_Tab.REF,,,null());
            {? _mg & ~PD_MG.find_key(_mg)
            ||
               _mod:=1;
               PD_MG.PD_N:=PD_N.ref();
               PD_MG.PD_P:=null();
               PD_MG.MG:=_mg;
               PD_MG.MM:=_mm;
               PD_MG.SORT:='';
               _czy_zm:='T';
               PD_MG.add()
            ?}
         ?};
         _loop:=_Tab.next()
      !};
      {? _mod
      ||
         PD_N.PD_MG_ZN:=null();
         PD_N.put()
      ?}
   ?};
   PD_K.cntx_pop()
?};
{? _czy_zm='T' || exec('pd_p_refresh2','plan_dostaw') ?};

PD_MG.cntx_pop()


\pd_mg_verify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Magazyny planu dostaw - sprawdza poprawność definicji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_chk_oddz:=exec('get','#params',600400,2)='N';
exec('ini_kom','#message','Wynik weryfikacji magazynów planu dostaw'@);
_pd_n:=PD_N.ref();
MG.cntx_psh();
PD_MG.cntx_psh();
PD_MG.index('UNIK');
PD_MG.prefix('N',_pd_n,null());
{? PD_MG.first()
||
   PD_MG.prefix('T',_pd_n,null());
   _loop:=PD_MG.first();
   {!
   |? _loop
   |!
      _mg:=PD_MG.MG().SYM;
      _mg_ref:=PD_MG.MG;
      _oddz:=PD_MG.MG().ODDZ;
      PD_MG.cntx_psh();
      {? _chk_oddz
      ||
         PD_MG.index('ODDZ_MM');
         PD_MG.prefix(_oddz,_pd_n,null(),'N');
         {? ~PD_MG.first()
         ||
            exec('add_kom','#message','Brak magazynu przyjmującego w oddziale %1 dla magazynu do przesunięć %2.'@[
               _oddz,_mg],7)
         ?}
      ?};
      PD_MG.index('UNIK');
      PD_MG.prefix('N',_pd_n,null(),_mg_ref);
      {? ~PD_MG.first()
      ||
         exec('add_kom','#message','Należy dodać magazyn do przesunięć %1 do magazynów do analiz.'@[_mg],7)
      ?};
      PD_MG.cntx_pop();
      _loop:=PD_MG.next()
   !}
?};
PD_MG.cntx_pop();
MG.cntx_pop();
exec('end_kom','#message')


\pd_d_zn_reka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Rekord po PD_D_ZN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(PD_D_ZN,,'NAZ');
{? _wyn=''
|| {? __CHK.index(PD_D_ZN,-menu_txt()='popraw')<>'' || _wyn:='NAZ' ?}
?};
_wyn


\pd_d_zu_reka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Rekord po PD_D_ZU
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(PD_D_ZU,,'USER');
{? _wyn=''
|| {? __CHK.index(PD_D_ZU,-menu_txt()='popraw')<>'' || _wyn:='USER' ?}
?};
_wyn


\pd_mg_zn_reka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Rekord po PD_MG_ZN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(PD_MG_ZN,,'USER','NAZ');
{? _wyn=''
|| {? __CHK.index(PD_MG_ZN,-menu_txt()='popraw')<>'' || _wyn:='NAZ' ?}
?};
_wyn


\pd_mg_zn_pozycje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Plan dostawy - Magazyny
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wer:=PD_MG_ZP.mk_sel('Magazyny do analizy'@,,,'pd_mgzp_wer',,,,,'U');
PD_MG_ZP.win_fld(_wer,,'MG','ODDZ',,3,,1,'Oddział'@);
PD_MG_ZP.win_fld(_wer,,'MG','SYM',,8,,1,'Magazyn'@);
PD_MG_ZP.win_fld(_wer,,'MG','NAZ',,60,,1,'Nazwa'@);
_wer_mm:=PD_MG_ZP.mk_sel('Magazyny do przesunięć'@,,,'pd_mgzp_wer_mm',,,,,'U');
PD_MG_ZP.win_fld(_wer_mm,,'SORT',,,10,,,'Kolejność do przesunięć'@);
PD_MG_ZP.win_fld(_wer_mm,,'MG','ODDZ',,3,,1,'Oddział'@);
PD_MG_ZP.win_fld(_wer_mm,,'MG','SYM',,8,,1,'Z magazynu'@);
PD_MG_ZP.win_fld(_wer_mm,,'MG','NAZ',,60,,1,'Nazwa'@);
_wer_gr:=PD_MG_ZP.grp_make('Magazyny planu dostaw'@,,'pd_mgzp_wer_gr');
_fb:="PD_MG_ZP.index('WER'); PD_MG_ZP.prefix('N',PD_MG_ZN.ref()); PD_MG_ZP.first()";
PD_MG_ZP.grp_sel(_wer_gr,,_wer,'Magazyny do analizy'@,,,,,_fb,,,,'maximized');
_fb:="PD_MG_ZP.index('WER'); PD_MG_ZP.prefix('T',PD_MG_ZN.ref()); PD_MG_ZP.first()";
PD_MG_ZP.grp_sel(_wer_gr,,_wer_mm,'Magazyny do przesunięć'@,,,,,_fb,,,,'maximized');
_fb:="exec('pd_mg_zp_dol','!lzk_pld_dpla','N')";
PD_MG_ZP.win_act(_wer,1,'Formuła','Dołącz'@@,,,_fb,,1);
PD_MG_ZP.win_act(_wer,,'Formuła','Dołącz'@@,,,_fb,,1);
PD_MG_ZP.win_act(_wer,,'Usuń',,,,,,,1);
_fb:="exec('pd_mg_zp_dol','!lzk_pld_dpla','T')";
PD_MG_ZP.win_act(_wer_mm,1,'Formuła','Dołącz'@@,,,_fb,,1);
PD_MG_ZP.win_act(_wer_mm,,'Formuła','Dołącz'@@,,,_fb,,1);
PD_MG_ZP.win_act(_wer_mm,,'Popraw');
PD_MG_ZP.win_act(_wer_mm,,'Usuń',,,,,,,1);
PD_MG_ZP.cntx_psh();
PD_MG_ZP.win_sel(_wer_gr);
PD_MG_ZP.select();
exec('pd_mg_zp_verify','!lzk_pld_dpla');
PD_MG_ZP.cntx_pop();
0


\pd_mg_zp_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Magazyny planu dostaw - Dołącz
::   WE: _a - T/N - magazyny do przesunięć
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mm:=_a;
_czy_zm:='N';

PD_MG_ZP.cntx_psh();
_pd_mg:=exec('pd_mg_sel','plan_dostaw',_mm,PD_MG_ZP);
{? ~_pd_mg.EXIT
||
   PD_MG_ZP.index('UNIK');
   PD_MG_ZP.prefix(_mm,PD_MG_ZN.ref());
   _Tab:=_pd_mg.TAB;
   _ndx:=_pd_mg.NDX2;
   _Tab.index(_ndx);
:: usunięcie niewybranych magazynów
   _Tab.cntx_psh();
   _ndx:=_Tab.ndx_tmp(,,'REF',,);
   _Tab.index(_ndx);
   _loop:=PD_MG_ZP.first();
   {!
   |? _loop
   |!
      _loop:=
         {? _Tab.find_key($PD_MG_ZP.MG) & _Tab.MARK='T'
         || PD_MG_ZP.next()
         || _czy_zm:='T';
            PD_MG_ZP.del()
         ?}
   !};
   _Tab.cntx_pop();
:: dodanie wybranych magazynów
   _loop:=_Tab.first();
   {!
   |? _loop
   |!
      {? _Tab.MARK='T'
      ||
         _mg:=exec('FindAndGet','#table',MG,_Tab.REF,,,null());
         {? _mg & ~PD_MG_ZP.find_key(_mg)
         ||
            PD_MG_ZP.PD_MG_ZN:=PD_MG_ZN.ref();
            PD_MG_ZP.MG:=_mg;
            PD_MG_ZP.MM:=_mm;
            PD_MG_ZP.SORT:='';
            _czy_zm:='T';
            PD_MG_ZP.add()
         ?}
      ?};
      _loop:=_Tab.next()
   !}
?};
PD_MG_ZP.cntx_pop()


\pd_mg_zp_verify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Magazyny planu dostaw - sprawdza poprawność definicji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_chk_oddz:=exec('get','#params',600400,2)='N';
exec('ini_kom','#message','Wynik weryfikacji magazynów planu dostaw'@);
_pd_mg_zn:=PD_MG_ZN.ref();
MG.cntx_psh();
PD_MG_ZP.cntx_psh();
PD_MG_ZP.index('UNIK');
PD_MG_ZP.prefix('N',_pd_mg_zn);
{? PD_MG_ZP.first()
||
   PD_MG_ZP.prefix('T',_pd_mg_zn);
   _loop:=PD_MG_ZP.first();
   {!
   |? _loop
   |!
      _mg:=PD_MG_ZP.MG().SYM;
      _mg_ref:=PD_MG_ZP.MG;
      _oddz:=PD_MG_ZP.MG().ODDZ;
      PD_MG_ZP.cntx_psh();
      {? _chk_oddz
      ||
         PD_MG_ZP.index('ODDZ_MM');
         PD_MG_ZP.prefix(_oddz,_pd_mg_zn,'N');
         {? ~PD_MG_ZP.first()
         ||
            exec('add_kom','#message','Brak magazynu przyjmującego w oddziale %1 dla magazynu do przesunięć %2.'@[
               _oddz,_mg],7)
         ?}
      ?};
      PD_MG_ZP.index('UNIK');
      PD_MG_ZP.prefix('N',_pd_mg_zn,_mg_ref);
      {? ~PD_MG_ZP.first()
      ||
         exec('add_kom','#message','Należy dodać magazyn do przesunięć %1 do magazynów do analiz.'@[_mg],7)
      ?};
      PD_MG_ZP.cntx_pop();
      _loop:=PD_MG_ZP.next()
   !}
?};
PD_MG_ZP.cntx_pop();
MG.cntx_pop();
exec('end_kom','#message')


\pd_n_pd_mg_zn_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: PD_N.PD_MG_ZN - wartość domyślna
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null();
PD_MG_ZN.cntx_psh();
PD_MG_ZN.index('USER');
PD_MG_ZN.prefix(OPERATOR.USER);
{? PD_MG_ZN.find_key('T')
      |
   PD_MG_ZN.first() & ~PD_MG_ZN.next()
||
   _wyn:=PD_MG_ZN.ref()
?};
PD_MG_ZN.cntx_pop();
_wyn


\wybierz_zestaw_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Wybór zestawu magazynów
::   WE: _a - nazwa zestawu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_naz:=_a;
_wyn:=null();
PD_MG_ZN.index('VIEW_PD');
PD_MG_ZN.prefix(OPERATOR.USER);
{? PD_MG_ZN.find_key(_naz,)
||
   _wyn:=PD_MG_ZN.ref()
?};
PD_MG_ZN.win_sel('SLO_WYB');
{? PD_MG_ZN.select(,1,1)
||
   _wyn:=PD_MG_ZN.ref()
?};
_wyn


\pd_mg_dolacz_zestaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Dołączenie zestawu magazynów do nagłówka
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PD_Z.NAGPOZ<>'N' & PD_Z.NAGPOZ<>'P'
||
   FUN.info('Nieobsługiwane miejsce wywołania.'@)

||
   _pd_n:=PD_N.ref();
   _pd_p:=null();
   _pd_mg_zn_b:=PD_N.PD_MG_ZN;
   _naz:=PD_N.PD_MG_ZN().NAZ;
   _pd_mg_zn_a:=exec('wybierz_zestaw_mg','!lzk_pld_dpla',_naz);
   {? _pd_mg_zn_b<>_pd_mg_zn_a
   ||
      do();
      PD_MG.cntx_psh();
      PD_MG.index('MM');
      PD_N.PD_MG_ZN:=_pd_mg_zn_a;
      PD_N.put();
      PD_MG.prefix(PD_N.ref());
      exec('wer_pd_d_use','!lzk_pld_dpla',PD_Z.NAGPOZ);
      _loop:=PD_MG.first();
      {!
      |? _loop
      |!
         _loop:=PD_MG.del()
      !};
      PD_MG_ZP.cntx_psh();
      PD_MG_ZP.index('MM');
      PD_MG_ZP.prefix(_pd_mg_zn_a);
      _loop:=PD_MG_ZP.first();
      {!
      |? _loop
      |!
         PD_MG.PD_N:=_pd_n;
         PD_MG.PD_P:=_pd_p;
         PD_MG.MG:=PD_MG_ZP.MG;
         PD_MG.MM:=PD_MG_ZP.MM;
         PD_MG.SORT:=PD_MG_ZP.SORT;
         _loop:=PD_MG.add() & PD_MG_ZP.next()
      !};
      PD_MG_ZP.cntx_pop();
      PD_MG.cntx_pop();
      end()
   ?}
?}


\pd_mg_zn_default_zu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: PD_MG_ZN - Domyślny zestaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_default:='';
{? PD_MG_ZN.DEFAULT='T'
||
   {? FUN.ask('Czy zestaw magazynów ma przestać być domyślny?'@)
   ||
      _default:='N'
   ?}
||
   {? FUN.ask('Czy zestaw magazynów ma być domyślny?'@)
   ||
      _default:='T'
   ?}
?};
{? _default<>''
||
   {? _default='N'
   ||
      PD_MG_ZN.DEFAULT:='N';
      PD_MG_ZN.put()

   |? _default='T'
   ||
      PD_MG_ZN.cntx_psh();
      PD_MG_ZN.index('USER');
      PD_MG_ZN.prefix(OPERATOR.USER,'T');
      {? PD_MG_ZN.first()
      ||
         PD_MG_ZN.prefix();
         PD_MG_ZN.DEFAULT:='N';
         PD_MG_ZN.put()
      ?};
      PD_MG_ZN.cntx_pop();
      PD_MG_ZN.DEFAULT:='T';
      PD_MG_ZN.put()
   ?}
?}

:Sign Version 2.0 jowisz:1045 2024/02/09 09:16:59 994eadb6365279aee3d0a8f67d4eb2199fddd86c45b8cd8456f8170bdf19e40a274f7ded7e25d56734263a298be85b1e5c7ad8d1eae213d39d4e9f1680c73ba112a13abc85639f9002379540bbf0d43095ffffe4ac6ff3aaca542415019b0f72fca0f99d7c899bc187cc092e37bb73f923df81c3e7f032b8732c54fdb8471105

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zpr_ins_clea.fml
:: Utworzony: 25.08.2021
:: Autor: WH
::======================================================================================================================
:: Zawartosc: Formuły czynnosci ZPR_INS_CLEA
::            Czyszczenie instancji procesów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Formuła główna czynności czyszczenia instancji procesów (ZPR_INS_CLEA)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

::# properties=SERVICE,GRP_FIRM

:: PARAMETRY WE:
::# kind=WE, symbol=PROCES, type=_B_PROC, name=Proces do czyszczenia, required=N, fml_val="exec('select_proces','!zpr_ins_clea',_a,_b)"
::# kind=WE, symbol=DNI, type=NUMBER, name=Ilość dni wtecz do czyszczenia, required=N
::# kind=WE, symbol=CLEANER, type=STRING, name=Usuwać tylko te errorowane przez ZPR_CLE_ANER, required=N, fml_val="exec('edit_cleaner','!zpr_ins_clea',_a,_b)"
{? var_pres('PROCES',_in)<>type_of(~~) & var_pres('PROCES',_in)<>type_of(null()) || return() ?};
{? _in.PROCES=~~ || _in.PROCES:=null() ?};
{? var_pres('DNI',_in)<>type_of(~~) & var_pres('DNI',_in)<>type_of(0) || return() ?};
{? _in.DNI=~~ || _in.DNI:=0 ?};
{? var_pres('CLEANER',_in)<>type_of(~~) & var_pres('CLEANER',_in)<>type_of('') || return() ?};
{? _in.CLEANER=~~ || _in.CLEANER:='' ?};

:: PARAMETRY WY:
::# kind=WY, symbol=RESULT, type=STRING, name=Wynik czynności (OK gdy wszystkie procesy zostały poprawnie usunięte), required=N
::# kind=WY, symbol=ILOSC, type=NUMBER, name=Ilość usuniętych instancji, required=N
{? var_pres('RESULT',_out)<>type_of(~~) & var_pres('RESULT',_out)<>type_of('') || return() ?};
{? var_pres('ILOSC',_out)<>type_of(~~) & var_pres('ILOSC',_out)<>type_of(0) || return() ?};


BI_PROC.cntx_psh();
{? _in.PROCES<>null()
|| BI_PROC.index('PROC_ERR');
   {? _in.CLEANER='T'
   || BI_PROC.prefix(_in.PROCES,'T',_in.CLEANER)
   || BI_PROC.prefix(_in.PROCES,'T')
   ?};
   ~~
|| BI_PROC.index('CLEANERR');
   {? _in.CLEANER='T'
   || BI_PROC.prefix(REF.FIRMA,'T',_in.CLEANER)
   || BI_PROC.prefix(REF.FIRMA,'T')
   ?}
?};

_il_del:=0;
_dialog:=1;
{? _mp.isService()
|| _dialog:=0
?};

_can_continue:=1;
_result:=1;

{? _dialog>0
|| _proces:='brak'@;
   {? _in.PROCES<>null
   || _proces:=exec('record','#to_string',_in.PROCES)
   ?};
   _clean:='Nie'@;
   {? _in.CLEANER='T'
   || _clean:='Tak'@
   ?};
   _msg:='Uruchomić czyszczenie instancji procesów z problemami?\n\n'
         'Proces: %1\n'
         'Liczba dni wstecz: %2\n'
         'Tylko errorowane przez ZPR_CLE_ANER?: %3'@[_proces,$_in.DNI,_clean];
   _can_continue:=FUN.ask(_msg)
?};
_prg_step:=exec('progress_step','#b__box');

{? _can_continue>0 & BI_PROC.first()
||
   _tm_grn:=0;
   {? _in.DNI>0
   || _tm_grn:=exec('create','#tm_stamp',date(),time())-_in.DNI*exec('day','#tm_stamp')
   ?};

   {? BI_PROC.first()
   || {? _dialog>0
      || FUN.prg_start(BI_PROC.size()/_prg_step,'Czyszczenie instancji procesów...'@,1,,1)
      ?};
      _lp:=1;
      {!
      |?
         {? _dialog>0
         || {? _lp%*_prg_step=0
            || FUN.prg_next()
            ?}
         ?};
         _next:=0;
         _ref_nxt:=null();
         BI_PROC.cntx_psh();
         {? BI_PROC.next()
         || _ref_nxt:=BI_PROC.ref()
         ?};
         BI_PROC.cntx_pop();

         _can_del:=1;
         {? _tm_grn>0
         || {? BI_PROC.TM_BORN<_tm_grn
            || _can_del:=1
            || _can_del:=0
            ?}
         ?};

         {? _can_del>0
         || {? exec('delete','#bi_proc',BI_PROC.ref())
            || _il_del+=1
            || _result:=0
            ?}
         ?};

         {? _ref_nxt<>null()
         || _next:=BI_PROC.seek(_ref_nxt)
         ?};

         _lp+=1;
         _next>0 & _can_continue>0
      !};
      {? _dialog>0
      || FUN.prg_stop()
      ?}
   ?}
?};

{? _can_continue>0 & _dialog>0
|| _msg:='Zakończono czyszczenie.\n\n'
         'Usuniętych instancji: %1'@[$_il_del];
   FUN.info(_msg)
?};

_out.ILOSC:=_il_del;
{? _result>0
|| _out.RESULT:='OK'
|| _out.RESULT:='BŁĄD'
?};
_mp.save(,_out);
_mp.done();

BI_PROC.cntx_pop();
_can_continue


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Opis dla czynności archiwizacji instancji procesów (ZPR_INS_CLEA)
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_desc:='Wyczyść instancje procesów z problemami'@@;

_desc


\select_proces
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Formuła służąca do redagowania parametru wejściowego PROCES
::   WE: _a - B_PROC.ref() przed redakcją
::   WY: B_PROC.ref() po redakcji
::----------------------------------------------------------------------------------------------------------------------
_in_proc:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_out_proc:=null();

B_PROC.cntx_psh();
B_PROC.index('AKT');
B_PROC.prefix('N','T',REF.FIRMA);
B_PROC.seek(_in_proc);
_wer:=B_PROC.mk_sel('Wybierz proces'@,,,'editprcssd3x',,,,,'U');
B_PROC.win_fld(_wer,,'SYMBOL',,,20);
B_PROC.win_fld(_wer,,'NAME',,,40);
B_PROC.win_fld(_wer,,'VER',,,10);
B_PROC.win_fld(_wer,,'ACCEPTED',,,,,,,,,2,,"\'T\'","\'N\'");
B_PROC.win_act(_wer,,'Formuła','&Wybierz'@@,,,"sel_exit()",,1,,,,'W');
B_PROC.win_sel(_wer);
{? B_PROC.select(,1,5)
|| _out_proc:=B_PROC.ref()
?};
B_PROC.cntx_pop();
_out_proc


\edit_cleaner
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Redagowanie parametru CLEANER
::   WE: _a - STRING - poprzednia wartość
::   WY: STRING lub ~~
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_old:=_a;

_res:=exec('edit_boolean','#edit',_old,'Usuwać tylko te errorowane przez ZPR_CLE_ANER?'@);
_res

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:02 32009fa3279f7d22ac2b3c26accf4fdc07cf406d8b3adf6a038c75e8b0c62d8ec24f79573189602be48f239878d9571411355aa9070053e9c2a30862b9e580d283d6f193ee8769060881c7676bd95aca7dfca2935166cbe8e8587689be69532e3bdd5beb64625f8c04f260fd8ede9557c8e84705b5225ec8cc14af2f5346e3b3

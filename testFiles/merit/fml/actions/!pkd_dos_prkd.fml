:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prkd.fml
:: Utworzony: 21.06.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_DOS_PRKD - Rej. danych w kart. dodatk.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rej. danych w kart. dodatk. - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE, symbol=OSOBA, type=_OSOBA, name=Wskazanie osoby, required=T, keyref=T
::# kind=WE, symbol=KART_DEF, type=_KART_DEF, name=Wskazanie kartoteki dodatkowej,  required=T, keyref=T, fml_val="exec('kart_def','!pkd_dos_prkd',_a)", fml_exp="exec('kart_def_exp','kart_dod',_a)"
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_int:=_par.int;
_out:=_par.out;
_akcja:=_mp.akcja();


_result:='';

_uidO:=exec('ref2uid','#table',_in.OSOBA);
_uidK:=exec('ref2uid','#table',_in.KART_DEF);
{? _uidO=''
|| _result:=exec('error','!pkd_dos_prkd')

|? _uidK=''
|| _result:=exec('error','!pkd_dos_prkd','#K')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidO);
      _mp.keyRef(_uidK);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidK);
      _mp.delRef(_uidO);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_prkd',_in.OSOBA,_in.KART_DEF);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};

~~


\setPorts
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Formuła ustawia wymagane parametry wejściowe dla czynności uruchamianej z obszaru roboczego.
::   WE: _a [OBJECT] - Obiekt z parametrami zgodny z exec('mp_run_a','#b__box')
::   WY: 1 - Parametr został ustawiony poprawnie.
::       0 - Parametru nie udało się ustawić.
::      -1 - Błędny argument wywołania.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(obj_new('obj'))
|| exec('portsInSet','#b__box',_a.PORTS_IN,_a.ACT_UID,'KART_DEF',KART_DEF.ref())
|| -1
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rej. danych w kart. dodatk. - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_tab:=exec('desc','osoba',_mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj dane w kartotekach dodatkowych: %1 %2: Paszport - %3'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj dane w kartotekach dodatkowych: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj dane w kartotekach dodatkowych: %1 %2: Data urodzenia - %3'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj dane w kartotekach dodatkowych'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE: [_a] [STRING] - Dodatkowy opis błędu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie danych w kartotekach dodatkowych niemożliwe.\n'+
{? var_pres('_a')=type_of('')
|| {? _a='#K'
   || 'Nie znaleziono definicji kartoteki dodatkowej.'
   || _a
   ?}
|| 'Nie znaleziono osoby.'
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a [REFERENCE] - Wskazanie osoby.
::       _b [REFERENCE] - Wskazanie kartoteki dodatkowej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('select','kart_dod',_a,_b,'PKD_DOS_PRKD','PKD_DOS_PPKD')


\kart_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła pozwala wybrać akronim kartoteki dodatkowej.
::       Formuła wykorzystywana jako argument atrybutu 'fml_val' parametru czynności.
::   WE: [_a] [STRING] - Akronim aktualnie wybranej kartoteki dodatkowej.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_in:={? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=KART_DEF || _a || null() ?};

KART_DEF.cntx_psh();
KART_DEF.prefix();
exec('f_set_nosys','kart_dod');
_dh:=KART_DEF.f_size();
{? _dh<5
|| _dh:=5
|? _dh>30
|| _dh:=30
?};
_ws:=KART_DEF.mk_sel('Kartoteki dodatkowe'@,'N',,'sel_kart_dod',,,_dh,,'U');
KART_DEF.win_fld(_ws,,'SYMBOL',,,,,,,,MS.comment(KART_DEF,'SYMBOL'));
KART_DEF.win_fld(_ws,,'NAZWA',,,,,,,,MS.comment(KART_DEF,'NAZWA'));
KART_DEF.win_act(_ws,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
KART_DEF.win_sel(_ws);
KART_DEF.f_seek(_in);
_ret:=
   {? KART_DEF.select(,1,-1)
   || KART_DEF.ref()
   || _in
   ?};
KART_DEF.f_clear();
KART_DEF.cntx_pop();

_ret

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:39 0542d94d8c7c42ca06bd10cb0efcdc62f31732f6c4f13b30c9fdbd2916f73acc923af2164e0130a175536d6cb93d958d0f2bf026bc6bf0a2901bcfddb7b74de47d601df6f836162dac581113ee9fd0da38b3d8d70319684029770d87e8ebe7ef0b8b33182f982cb1e8ea79070cc4fe629c75b94d4f041997b083889c22435ecb

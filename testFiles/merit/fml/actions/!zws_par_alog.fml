:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_alog.fml
:: Utworzony: 20.02.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_ALOG - Przeglądanie rejestru zmian.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Przeglądanie rejestru zmian - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
exec('BIT','#object');

_wnd:=exec('setup','!zws_par_alog');

SKID_LOG.cntx_psh();
SKID_LOG.clear();
: uwzględnij tylko tabele, które nie zostały wykluczone
SKID_LOG.f_set('SYMBOL',,'TYP=\'T\' and STATUS<>\'-\'');
SKID_LOG.win_sel(_wnd.grp);
SKID_LOG.select();
SKID_LOG.cntx_pop();

: porządki
SKID_LOG.win_del(_wnd.grp);
SKID_LOG.win_del(_wnd.log);
USERS.win_del(_wnd.usr);
~~


\setup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Tworzy okienka panelu przeglądania rejestru zmian.
::   WE:
::   WY: wskazanie tablicy nazwanej zawierającej akronimy utworzonych okienek: grp - panel, log - wertowanie tabeli
::       SKID_LOG, usr - wertowanie tabeli użytkowników.
::----------------------------------------------------------------------------------------------------------------------
_wnd:=obj_new('grp','log','usr');

: przeglądanie tabeli ustawień
_log:=SKID_LOG.mk_sel(,'P',0,'skid_log_conf',,,,,'U');
SKID_LOG.win_fld(_log,,'SYMBOL',,,-12);
SKID_LOG.win_fld(_log,,'OPIS',,,30);
SKID_LOG.win_fld(_log,,'STATUS',,,-3,,,,,'%1 %2'[exec('fldComment','#field',SKID_LOG,'STATUS'),'[T/N]'],2,,"'T'","'N'");
SKID_LOG.win_act(_log,,'Formuła','Wybierz'@,,,"exec('log_tabela','!zws_par_alog',SKID_LOG.SYMBOL,SKID_LOG.GRUPA)","",1);

: przeglądanie listy użytkowników
_usr:=USERS.mk_sel(,'P',0,'skid_log_usr',,,,,'U');
USERS.win_fld(_usr,,'KOD',,,-10);
USERS.win_fld(_usr,,'DANE',,,35);
USERS.win_act(_usr,,'Formuła','Wybierz'@,,,"exec('log_uzytkownik','!zws_par_alog',USERS.KOD)","",1);

: okienko panelu
_grp:=SKID_LOG.grp_make('Rejestr modyfikacji'@,
   "  USERS.cntx_psh();
      USERS.index('USR_KKOD');
      USERS.prefix();
      1
   ",
   'skid_log_con',,,
   "  USERS.cntx_pop();
      1
   ",,
   'normal'
);
SKID_LOG.grp_sel(_grp,SKID_LOG,_log,'Tabele'@);
SKID_LOG.grp_sel(_grp,USERS,_usr,'Użytkownicy'@);

_wnd.grp:=_grp;
_wnd.log:=_log;
_wnd.usr:=_usr;

_wnd


\log_tabela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Przeglądanie zmian wprowadzonych w wybranej tabeli.
::   WE: _a [STRING] - akronim tabeli
::       _b [STRING] - T/N prezentacja danych w grupie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | var_pres(_a)<>type_of(SYSLOG)
|| FUN.emsg('Pominięto lub podano błędny akronim tabeli.'@);
   return()
?};

_grupa:={? var_pres('_b')=type_of('') & _b='T' || 'T' || 'N' ?};

_TAB:=($_a)();
_dbf:=_TAB.name();

: czy tabela jest maskowalna?
{? _TAB.name()<>_TAB.name(1)
|| _DBF:=_TAB.names();
   _wnd:=_DBF.mk_sel('Zbiory tabeli %1'@[_a],,0,'#log_tab_dane');
   _DBF.win_fld(_wnd,,'NAME',,,,,,'Plik'@,,'Nazwa zbioru'@);
   _DBF.win_act(_wnd,,'Formuła','Wybierz'@,,'Wybór bieżącego zapisu'@,"sel_exit()",,1);
   _DBF.win_sel(_wnd);
:  wybór zbioru tabeli
   {? _DBF.select()
   || _dbf:=_DBF.NAME
   || return()
   ?}
?};

_LOG:=sql(
   'select '
   '  cast(L.IDADD as DATE_TYPE) as DATA, '
   '  cast(L.IDADD as TIME_TYPE) as CZAS, '
   '  case '
   '     when L.COMMAND=\'A\' then \'3\' '
   '     when L.COMMAND=\'P\' then \'2\' '
   '     when L.COMMAND=\'D\' then \'1\' '
   '     else \'0\' '
   '  end as NUMER, '
   '  L.MUTATCOD as KTO, '
   '  L.COMMAND as KOD, '
   '  case '
   '     when L.COMMAND=\'A\' then \'%1\' '
   '     when L.COMMAND=\'P\' then \'%2\' '
   '     when L.COMMAND=\'D\' then \'%3\' '
   '     else \'?\' '
   '  end as OPIS, '
   '  L.RECNO as REKORD, '
   '  L.IDSRC as IDSRC, '
   '  L.REFERENCE as SQLREF '
   'from @SYSSLG as L '
   'where L.DBF=\':_a\' %4 '
   'order by DATA desc, CZAS desc, REKORD, NUMER'
   [  'Utworzenie'@,'Modyfikacja'@,'Usunięcie'@,
      {? _grupa<>'T' || 'and L.COMMENT=\'%1\''[REF.FIRMA().SYMBOL] || '' ?}
   ],
   _dbf
);

_wnd:=_LOG.mk_sel('Modyfikacje zbioru %1 tabeli %2'@[_dbf,_a],'T',0,'sysslg_tab',,,,,'U');
_LOG.win_fld(_wnd,,'DATA',,,-10,,,'Data'@,,'Data zdarzenia'@);
_LOG.win_fld(_wnd,,'CZAS',,,-10,,,'Godzina'@,,'Godzina zdarzenia'@);
_LOG.win_fld(_wnd,,'KTO',,,-10,,,'Użytkownik'@,,'Identyfikator użytkownika'@);
_LOG.win_fld(_wnd,,'OPIS',,,-20,,,'Operacja'@,,'Opis operacji'@);
_LOG.win_fld(_wnd,,'REKORD',,,-12,,,'Wiersz'@,,'Numer wiersza tabeli'@);
_LOG.win_fld(_wnd,,'IDSRC',,,-36,,,'Identyfikator'@,,'Unikalny identyfikator wiersza'@);
_LOG.win_act(_wnd,,'Formuła','Szczegół&y'@,,,$'exec(\'szczegoly\',\'#syslog\',cur_tab(1,1).SQLREF,\'%1\')'[_a],,1);
_LOG.win_act(_wnd,,'Szukaj');
_LOG.win_act(_wnd,,'Kolejność');
_LOG.win_sel(_wnd);

_wnd:=_LOG.mk_edit('Zmiana'@,0,'sysslg_red');
_LOG.win_esep(_wnd,'Dane podstawowe'@);
_LOG.win_efld(_wnd,,'DATA',,,10,,,'Data'@,,'Data zdarzenia'@);
_LOG.win_efld(_wnd,,'CZAS',,,25,,,'Godzina'@,,'Godzina zdarzenia'@);
_LOG.win_efld(_wnd,,'KTO',,,25,,,'Użytkownik'@,,'Identyfikator użytkownika'@);
_LOG.win_efld(_wnd,,'KOD',,,25,,,'Kod'@,,'Kod operacji'@);
_LOG.win_efld(_wnd,,'OPIS',,,25,,,'Opis'@,,'Opis operacji'@);
_LOG.win_efld(_wnd,,'REKORD',,,25,,,'Wiersz'@,,'Numer wiersza tabeli'@);
_LOG.win_efld(_wnd,,'IDSRC',,,25,,,'Identyfikator'@,,'Unikalny identyfikator wiersza'@);
exec('ok_esc','#window',_LOG,_wnd);
_LOG.win_edit(_wnd);

_LOG.select();
obj_del(_LOG);
~~


\log_uzytkownik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Przeglądanie zmian wprowadzonych przez wybranego użytkownika.
::   WE: _a [STRING] - identyfikator użytkownika.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_LOG:=sql(
   'select '
   '  cast(L.IDADD as DATE_TYPE) as DATA, '
   '  cast(L.IDADD as TIME_TYPE) as CZAS, '
   '  case '
   '     when L.COMMAND=\'A\' then \'3\' '
   '     when L.COMMAND=\'P\' then \'2\' '
   '     when L.COMMAND=\'D\' then \'1\' '
   '     else \'0\' '
   '  end as NUMER, '
   '  L.COMMAND as KOD, '
   '  case '
   '     when L.COMMAND=\'A\' then \'%1\' '
   '     when L.COMMAND=\'P\' then \'%2\' '
   '     when L.COMMAND=\'D\' then \'%3\' '
   '     else \'?\' '
   '  end as OPIS, '
   '  L.DBF as PLIK, '
   '  L.RECNO as REKORD, '
   '  L.IDSRC as IDSRC, '
   '  L.REFERENCE as SQLREF '
   'from @SYSSLG as L '
   'where L.MUTATCOD=\':_a\' and L.COMMAND in (\'A\',\'P\',\'D\') '
   'order by DATA desc, CZAS desc, REKORD, NUMER'
   ['Utworzenie'@,'Modyfikacja'@,'Usunięcie'@],
   _a
);

_wnd:=_LOG.mk_sel('Modyfikacje użytkownika %1'@[_a],'T',0,'sysslg_tab',,,,,'U');
_LOG.win_fld(_wnd,,'DATA',,,-10,,,'Data'@,,'Data zdarzenia'@);
_LOG.win_fld(_wnd,,'CZAS',,,-10,,,'Godzina'@,,'Godzina zdarzenia'@);
_LOG.win_fld(_wnd,,'OPIS',,,-20,,,'Operacja'@,,'Opis operacji'@);
_LOG.win_fld(_wnd,,'PLIK',,,-10,,,'Zbiór'@,,'Nazwa zbioru tabeli'@);
_LOG.win_fld(_wnd,,'REKORD',,,-12,,,'Wiersz'@,,'Numer wiersza tabeli'@);
_LOG.win_fld(_wnd,,'IDSRC',,,-36,,,'Identyfikator'@,,'Unikalny identyfikator wiersza'@);
_LOG.win_act(_wnd,,'Formuła','Szczegół&y'@,,,
   "  _LOG:=cur_tab(1,1);
      _hex:=(8*'0'+BIT.hex(_LOG.REKORD))+8;
      _ref:=form(_LOG.PLIK,8)+-_hex;
      _TAB:=ref_tab(_ref);
      {? type_of(_TAB)=type_of(SYSLOG)
      || exec('szczegoly','#syslog',_LOG.SQLREF,!_TAB)
      || FUN.info('Tabela właściwa dla zapisu %1 nie występuje w systemie.'@[_ref])
      ?}
   ",,1
);
_LOG.win_act(_wnd,,'Szukaj');
_LOG.win_act(_wnd,,'Kolejność');
_LOG.win_sel(_wnd);

_wnd:=_LOG.mk_edit('Zmiana'@,0,'sysslg_red');
_LOG.win_esep(_wnd,'Dane podstawowe'@);
_LOG.win_efld(_wnd,,'DATA',,,10,,,'Data'@,,'Data zdarzenia'@);
_LOG.win_efld(_wnd,,'CZAS',,,25,,,'Godzina'@,,'Godzina zdarzenia'@);
_LOG.win_efld(_wnd,,'KOD',,,25,,,'Kod'@,,'Kod operacji'@);
_LOG.win_efld(_wnd,,'OPIS',,,25,,,'Opis'@,,'Opis operacji'@);
_LOG.win_efld(_wnd,,'PLIK',,,25,,,'Zbiór'@,,'Nazwa zbioru tabeli'@);
_LOG.win_efld(_wnd,,'REKORD',,,25,,,'Wiersz'@,,'Numer wiersza tabeli'@);
_LOG.win_efld(_wnd,,'IDSRC',,,25,,,'Identyfikator'@,,'Unikalny identyfikator wiersza'@);
exec('ok_esc','#window',_LOG,_wnd);
_LOG.win_edit(_wnd);

_LOG.select();
obj_del(_LOG);
~~

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:42 7880fbb3c909e53db8854d79f5dd70de729f88e768baade6a48836cb98470e8ee027944c48f4a5406418ba9feb9c6493e6f824f73983a970e8c85498d86dfdcb6158b8522eed53f08a506f819e101affbd34ceb08d116c17e3c443a4bfa73e283a5456c5cd76e2171bba009e9bfa2ccad770414294eb08ca1044b80be56a64d4

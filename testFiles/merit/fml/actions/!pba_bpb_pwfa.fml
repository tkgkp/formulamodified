:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_pwfa.fml
:: Utworzony: 18.04.2019
:: Autor: RO
::======================================================================================================================
:: Zawartość: Obsługa czynności PBA_BPB_PWFA - Przetwarzanie formularzy ankiet.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Przetwarzanie formularzy ankiet - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=LOOP,SERVICE
::
::# kind=WE, symbol=ZA_ZEST, type=_ZA_ZEST, name=Wskazanie ankiety, required=T, keyref=T

::# kind=WY, symbol=ZA_FORM, type=_ZA_FORM, name=Wskazanie formularza ankiety, required=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;

{? _mp.pathProc() | _mp.pathTodo()
|| _keyRefs:=_mp.getRefs();
   {? var_pres('[1]',_keyRefs)<=0
   || _mp.error('Nieprawidłowy parametr wejściowy.');
      return()
   ?};

   _result:='';

   _id:=exec('ref2uid','#table',_in.ZA_ZEST);

   {? _id=''
   || _result:=exec('error','!pba_bpb_pwfa')
   ||
:      _dialog:=~(_mp.isService() | _mp.isAutoRun());

:: Ustawienie domyślnej wartości parametru LOOP (żeby brama się nie zawiesiła).
      _mp.save(exec('kind_out','#b_port'),'LOOP','T');

:: Ustalenie / odzyskanie klucza grupującego.
      _out.GRPKEY:=_mp.grpkey(_out.GRPKEY,_in.GRPKEY);
      {? ~_mp.loop()
::    Pierwszy obrót pętli - przygotujmy dane.
::    Usuwamy wszystkie dotychczasowe klucze, które mogły zostać zapamiętane przy poprzednim uruchomieniu czynności
::    (zakończonej _mp.keep()).
      || _mp.grpkeyDelAll();
         _err:='';
         ZA_ZEST.cntx_psh();
         ZA_ZEST.prefix();
         {? ZA_ZEST.seek(_in.ZA_ZEST)
         || _typ:=ZA_ZEST.TYP().KOD;
            ZA_FORM.cntx_psh();
            ZA_FORM.index('ZA_ZEST');
            ZA_FORM.prefix(ZA_ZEST.ref());
            _loop:=ZA_FORM.first();
            {!
            |? _loop
            |! {? ZA_FORM.SLO_KOD().KOD='P'
               || _out.ZA_FORM:=ZA_FORM.ref();
                  _mp.grpkeyAdd(ZA_FORM.uidref())
               ?};
               _loop:=ZA_FORM.next()
            !};
            ZA_FORM.cntx_pop()
         || _err:='Nieprawidłowa wartość parametru wejściowego.'
         ?};
         ZA_ZEST.cntx_pop();
         {? _err<>''
         || _mp.error(_err);
            return()
         ?}
      ?};

      _uidref:=_mp.grpkeyGet();
      {? _uidref=~~
::    Brak elementów do "wypchnięcia".
      || _mp.done()
::    Są pozycje do przetworzenia. Operacje usunięcia elementu z kolejki i ustawienia parametrów wyjściowych wykonajmy
::    w transakcji.
      || do();
         {? _mp.grpkeyDel()
         || _ref:=exec('FindAndGet','#table',ZA_FORM,_uidref,,,null());
::       Parametr wyjściowy musi być typu _ZA_FORM - stąd (powyżej) "konwersja" uidref -> ref.
::       Zapamiętanie parametru wyjściowego ZA_FORM.
            _mp.save(exec('kind_out','#b_port'),ZA_FORM,_ref);
            {? _mp.grpkeyGet()<>~~
::          Jeżeli jest jeszcze choć jeden element do przetworzenia, to pętla powinna być kontynuowana.
            || _mp.loop_continue()
            ?}
         ?};
         _mp.done();
         end()
      ?}
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Przetwarzanie formularzy ankiet - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_nazwa:='';
ZA_ZEST.cntx_psh();
ZA_ZEST.prefix();
{? ZA_ZEST.seek(_in.ZA_ZEST,,1) || _nazwa+=ZA_ZEST.NAZWA ?};
ZA_ZEST.cntx_pop();
{? +_nazwa
|| 'Przetwarzanie formularzy ankiety %1'@@[_nazwa]
|| 'Przetwarzanie formularzy ankiety'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Przetwarzanie formularzy ankiety niemożliwe.\nNie znaleziono udostępnionej ankiety.'

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 96ae110a0edf94f2a5f8da9b69622a286df530a3568c6f708a1e2f585f8b2e3eaf06b47046ad25d1b35f93888531bc6e3a915b9193841a5071334e6b609f15d2a3605a2b74458a4fb3d06a2d434095819d409387dfb930b3761054e522fb7cfbf132aa126d9a1ec7c1fb962a09f44e182ead176f6d3541a75b0657bf41b0f365

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_poj_mail.fml
:: Utworzony: 07.06.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_ZAS_MAIL - Wysyłanie e-maili z informacją o wyniku analizy zasobów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Wysyłanie e-maili z informacją o wyniku analizy zasobów - główna formuła czynności ZWS_ZAS_MAIL
::----------------------------------------------------------------------------------------------------------------------
::# properties=SEND
::# kind=WE, symbol=ANZASPE, type=_ANZASPE, name=Analiza dla zasobów, required=T, keyref=T
::# kind=WE, symbol=EMAIL_ERR, type=STRING, name=E-mail z inf. o błędach w wysyłce, required=N
::# kind=WY, symbol=ANZASPE, type=_ANZASPE, name=Analiza dla zasobów, required=T

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;
_akcja:=_mp.akcja();
_out.ANZASPE:=null;
_komunik:=(_mp.pathTodo() | _akcja<>'') & ~_mp.isAutoRun() & ~_mp.isService();
_errmail:={? var_press('EMAIL_ERR',_in)>0 || _in.EMAIL_ERR || '' ?};
_analiza:={? var_press('ANZASPE',_in)>0 || _in.ANZASPE || null ?};
_ok:=1;
{? _analiza<>null
|| ANZASPE.cntx_psh();
   ANZASPE.prefix();
   {? ANZASPE.seek(_analiza)
   || {? ANZASPE.AKC_ANAL<>'T'
      || {? _komunik
         || FUN.info('Analiza dla zasobów nie jest zaakceptowana.'@)
         ?};
         _ok:=0
      |? ANZASPE.WYS_POW='T'
      || {? _komunik
         || FUN.info('Analiza dla zasobów jest już wysłana.'@)
         ?}
      || _ok:=0;
         _err:=_errmail<>'' | _komunik;
         {? _err
         || VAR_DEL.delete('TabErr');
            TabErr:=exec('tab_error','!zws_zas_mail')
         ?};
         {? _komunik
         || exec('edit','!zws_zas_mail',TabErr.TAB.first());
            akc_send:=0;
            ANZASPE.display();
            _pyt:=akc_send;
            VAR_DEL.delete('akc_send')
         || _pyt:=1
         ?};
         {? _pyt=1
         || exec('send','!zws_zas_mail',_in.SENDER,_in.FROM,_in.REPLYTO);
            {? _errmail<>''
            || exec('send_err','!zws_zas_mail',_in.SENDER,_in.FROM,_in.REPLYTO,_errmail,TabErr)
            ?};
            ANZASPE.cntx_psh();
            ANZASPE.prefix();
            ANZASPE.WYS_POW:='T';
            ANZASPE.WYS_DATA:=date();
            ANZASPE.WYS_TIME:=time();
            ANZASPE.WYS_USER:=OPERATOR.USER;
            ANZASPE.put();
            ANZASPE.cntx_pop();
            _out.ANZASPE:=ANZASPE.ref();
            _ok:=1
         ?};
         VAR_DEL.delete('TabErr')
      ?}
   |? _komunik
   || FUN.info('Nie znaleziono analizy zasobów.'@)
   ?};
   ANZASPE.cntx_pop()
|? _komunik
|| FUN.info('Nie znaleziono analizy zasobów.'@)
?};
{? _ok || _mp.done() ?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='';
_mp:=params_get().mp;
_refs:=_mp.getRefs();
{? var_press('_refs')>0 & obj_len(_refs)>0
|| _ref:=_refs[1];
   {? var_press('_ref')>0
   || AN_ZASP.cntx_psh();
      ANZASPE.cntx_psh(); ANZASPE.prefix();
      {? ANZASPE.seek(_ref)
      || _desc:='Wyślij powiadomienia e-mail dla analizy zasobów: %1'@@[ANZASPE.OPIS]
      || _desc:='Wyślij powiadomienia e-mail dla analizy zasobów'@@
      ?};
      ANZASPE.cntx_pop();
      AN_ZASP.cntx_pop()
   ?}
|| _desc:='Wyślij powiadomienia e-mail dla analizy zasobów'@@
?};
_desc


\send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Wysłanie powiadomień o wynikach analizy zasobów
::   WE: _a - nadawca
::       _b - od
::       _c - odpowiedź do
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh(); OSOBA.prefix();
AN_ZAS.cntx_psh();
_typ:=ANZASPE.DEFAN().RODZZAS;
VAR_DEL.delete('TabPoz');
TabPoz:=exec('pozf','!zws_zas_mail',_typ);
exec('use_pozan','zasoby_wspolne');
POZAN.index('LP'); POZAN.prefix(ANZASPE.ref());
{? POZAN.first()
|| {!
   |? POZAN.prefix(ANZASPE.ref(),POZAN.ZAS);
      _zas:=POZAN.ZAS().KARTOTEK;
      _osoba:=null;
      _zas_fiz:=null;
      _zas_naz:='';
      {? _typ='P'
      || POJAZDY.cntx_psh();
         POJAZDY.index('ID'); POJAZDY.prefix();
         {? POJAZDY.find_key(_zas)
         || POJAZDY.OSOBA();
            _osoba:=POJAZDY.OSOBA;
            _zas_naz:='Pojazd: '+POJAZDY.NRREJ+{? POJAZDY.NAZ<>'' || ' - '+POJAZDY.NAZ || '' ?};
            _zas_fiz:=POJAZDY.ref()
         ?};
         POJAZDY.cntx_pop()
      || NRKRTSIM.cntx_psh();
         NRKRTSIM.index('ID'); NRKRTSIM.prefix();
         {? NRKRTSIM.find_key(_zas)
         || NRKRTSIM.OSOBA();
            _osoba:=NRKRTSIM.OSOBA;
            _zas_naz:='Karta SIM: '+NRKRTSIM.NR_KARTY+{? NRKRTSIM.OPIS<>'' || ' - '+NRKRTSIM.OPIS || '' ?};
            _zas_fiz:=NRKRTSIM.ref()
         ?};
         NRKRTSIM.cntx_pop()
      ?};
      {? _osoba
      || _email:=exec('email','osoba',_osoba);
         {? _email<>''
         || exec('send_zasob','!zws_zas_mail',_a,_b,_c,_email,_typ,_zas_fiz,_zas_naz)
         ?}
      ?};
      POZAN.last();
      POZAN.prefix(ANZASPE.ref());
      POZAN.next()
   !}
?};
OSOBA.cntx_pop();
AN_ZAS.cntx_pop();
VAR_DEL.delete('Mail','ErrMail','TabPoz');
1


\tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Tablica do przechowywania treści e-maila
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('BODY','LP','ind','add','first');
_tab.LP:=obj_new(2);
{! _ii:=1..obj_len(_tab.LP) |! _tab.LP[_ii]:=(_ii-1)*1000000 !};
_tab.first:=obj_new(2);
{! _ii:=1..obj_len(_tab.first) |! _tab.first[_ii]:=1 !};
_tab.BODY:=tab_tmp(1,
   'LP','INTEGER','Lp',
   'TEKST','STRING[255]','Tekst',
   'USR','INTEGER',,
   'TYP','INTEGER',
);
_tab.ind:=_tab.BODY.ndx_tmp('',1,'USR',,0,'TYP',,0);
_tab.add:="
   {? var_press('_b')>0
   || {? .first[_c]
      || .first[_c]:=0;
         _txt:={? _c=1
               || '<b>Błędy w adresach e-mail użytkowników zasobów:</b><br>'
               || '<b>Nie znaleziono użytkowników następujących zasobów:</b><br>'
               ?};
         .add(_txt,0,_c)
      ?};
      .BODY.cntx_psh();
      .BODY.index(.ind); .BODY.prefix(_b,_c);
      _ok:=~.BODY.first();
      .BODY.cntx_pop()
   || _ok:=1;
      _c:=1
   ?};
   {? _ok
   || .LP[_c]+=1;
      .BODY.LP:=.LP[_c];
      .BODY.TEKST:=_a;
      .BODY.USR:={? var_press('_b')>0 || {? type_of(_b)=type_of(null) || #_b || _b ?} || 0 ?};
      .BODY.TYP:={? var_press('_c')>0 || _c || 0 ?};
      .BODY.add()
   ?}
";
_tab


\send_zasob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Wysłanie powiadomień o wynikach analizy dla zasobu
::   WE: _a - nadawca
::       _b - od
::       _c - odpowiedź do
::       _d - email użytkownika zasobu
::       _e - typ zasobu
::       _f - ref zasobu (pojazd lub karta SIM)
::       _g - nazwa zasobu
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Mail');
Mail:=exec('tab','!zws_zas_mail');
Mail.add('<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Final//EN">');
Mail.add('<html>');
Mail.add('<head>');
Mail.add('<meta http-equiv="content-type" content="text/html; charset=iso-8859-2">');
Mail.add('</head>');
Mail.add('<body>');
Mail.add('<BR><BR>');
Mail.add('Okres od '+$ANZASPE.OKAN().POCZ+' do '+$OKRO_F.KON+'<br>');
Mail.add('Użytkownik: <b>'+OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO+'</b><br>');
Mail.add('Zasób: <b>'+_g+'</b>');
{? _e='P'
|| Mail.add('<br><br>Zarejestrowano następujące tankowania paliwa:<br><br>');
   Mail.add('<table valign=\"top\" border=\"1\" cellspacing=\"0\" cellpadding=\"0\"  >');
   Mail.add(
      '<tr class="head3">'
      '<td width="50">Data</td>'
      '<td width="300">Nazwa</td>'
      '<td width="100">Ilość</td>'
      '</tr>'
   );
   _record:="
      '<tr class=\"wiersz\">'+
      '<td class=\"wiersz\" width=\"50\">'+$TabPoz.TAB.DT+'</td>'+
      '<td class=\"wiersz\" width=\"300\">'+TabPoz.TAB.NAZ+'</td>'+
      '<td class=\"wiersz2\" width=\"100\">'+$TabPoz.TAB.IL+'</td></tr>'
   ";
   _tab:=TabPoz.TAB;
   _ind:=TabPoz.IND1;
   _tab.index(_ind);
   _tab.prefix($ZASOBY.ref());
   {? _tab.first()
   || {!
      |? Mail.add(_record());
         _tab.next()
      !}
   ?};
   Mail.add('</table>')
?};
Mail.add('<br><br><table valign=\"top\" border=\"1\" cellspacing=\"0\" cellpadding=\"0\"  >');
_record2:="
   '<tr class=\"wiersz\">'+
   '<td class=\"wiersz\" width=\"360\">'+POZAN.DEF().OPISKOL+'</td>'+
   '<td class=\"wiersz2\" width=\"100\">'+\
   {? AN_ZASP.RODZ_ZWR=1 || POZAN.WART_01 |? AN_ZASP.RODZ_ZWR=2 || $POZAN.WART_03 || $POZAN.WART_02 ?}+'</td>'+
   '</tr>'
";
{? POZAN.first()
|| {!
   |? Mail.add(_record2());
      POZAN.next()
   !}
?};
Mail.add('</table>');

_file:='';
{? _e='S'
|| BILINGI.cntx_psh();
   BILINGI.index('OKRO_F'); BILINGI.prefix(ANZASPE.OKAN,_f);
   {? BILINGI.first()
   || _ext:=BILINGI.bl_info('PLIK','EXTENSION');
      _tmpdir:= fmk_tmp_dir(0);
      {? type_of(_tmpdir) <> type_of(~~)
      || _fdir:=_tmpdir.get_path;
         _file:=_fdir+'/'+BILINGI.NR_SIM+'.'+_ext;
         {? ~BILINGI.bl_get('PLIK',_file,0)
         || _file:=''
         ?}
      ?}
   ?};
   BILINGI.cntx_pop()
?};

: Dodanie e-maila
_args:=exec('add_email_a','#mailbox');
_args.FROM:=_a;
_args.SENDER:=_b;
_args.REPLYTO:=_c;
_args.RCV:=_d;
_args.SUB:={? _e='P' || 'Informacja o potrąceniach za paliwo'@ || 'Informacja o potrąceniach za telefon'@ ?};
_args.BODYH:=Mail.BODY;
{? _file<>''
|| _args.ATTACH:=_file
?};
exec('add_email','#mailbox',_args);
{? _file<>''
|| ferase(_file,0)
?};
VAR_DEL.delete('Mail');
1


\pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Zwraca tablicę z tabelę z pozycjami faktury
::   WE: _a - typ analizy
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('TAB','IND1');
_maska:=ANZASPE.OKAN().ROK().KOD+form(OKRO_F.NR,-2);
DOK.cntx_psh();
POZF.cntx_psh();
POZF.use('pozf'+_maska);
DOK.use('doku'+_maska);
_tab.TAB:=sql(
   'select ZASOBY.REFERENCE as ZAS, POZF.DT, POZF.NAZ, POZF.IL '
   'from POZF join ZASOBY left join DOK, KART_ZAS join KH '
   'where POZF.EDOKUM is null and ZASOBY.TYP=\':_a\' and '
   'KH.NAZ_P=DOK.KH_FULL and POZF.KODP=KART_ZAS.POZF and '
   'KART_ZAS.OBC=\'T\''
   'order by ZAS,DT'
   ,_a
);
POZF.cntx_pop();
DOK.cntx_pop();
_tab.IND1:=_tab.TAB.index('?');
_tab


\akc_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Akcja wysyłana powiadomień
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_ZAS_MAIL';
_params.AKCJA:='Wyślij';
_params.UIDREF:=ANZASPE.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ANZASPE',ANZASPE.ref());
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\akc_wyc_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Akcja wycofania wysyłana powiadomień
::----------------------------------------------------------------------------------------------------------------------
ANZASPE.WYS_POW:='N';
ANZASPE.WYS_DATA:=date(0,0,0);
ANZASPE.WYS_TIME:=time(0,0,0);
ANZASPE.WYS_USER:=null;
ANZASPE.put()


\tab_error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Zwraca tabelę z informacjami o błędach przy tworzeniu powiadomień
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('TAB','IND1','IND2','add','show');
_tab.TAB:=tab_tmp(2,
   'TYP','INTEGER','Typ błedu',
   'REF','INTEGER',,
   'DESC','STRING[255]','Treść',
   'ZAS','STRING[255]',
);
_tab.IND1:=_tab.TAB.index('?');
_tab.IND2:=_tab.TAB.ndx_tmp('',1,'TYP',,0, 'DESC',,0);
_tab.add:="
   .TAB.index(.IND1);
   .TAB.prefix();
   {? ~.TAB.find_key(_a,_b)
   || .TAB.TYP:=_a;
      .TAB.REF:=#_b;
      .TAB.DESC:=_c;
      .TAB.ZAS:=_d;
      .TAB.add()
   ?}
";
_tab.show:="
   _o:=.TAB.mk_sel('Uwagi'@,'P',,'#zwszasmailsel',,,,,'U');
   .TAB.win_fld(_o,,'DESC',,,100,,,'Uwaga'@);
   .TAB.win_act(_o,,'Kolejność');
   .TAB.win_sel(_o);
   .TAB.cntx_psh();
   .TAB.index(.IND2); .TAB.prefix();
   _ret:=.TAB.select();
   .TAB.cntx_pop();
   _ret
";
OSOBA.cntx_psh(); OSOBA.prefix();
AN_ZAS.cntx_psh();
_typ:=ANZASPE.DEFAN().RODZZAS;
exec('use_pozan','zasoby_wspolne');
POZAN.index('LP'); POZAN.prefix(ANZASPE.ref());
{? POZAN.first()
|| {!
   |? POZAN.prefix(ANZASPE.ref(),POZAN.ZAS);
      _zas:=POZAN.ZAS().KARTOTEK;
      _osoba:=null;
      _zas_naz:='';
      {? _typ='P'
      || POJAZDY.cntx_psh();
         POJAZDY.index('ID'); POJAZDY.prefix();
         {? POJAZDY.find_key(_zas)
         || POJAZDY.OSOBA();
            _osoba:=POJAZDY.OSOBA;
            _zas_naz:='Pojazd '+POJAZDY.NRREJ
         ?};
         POJAZDY.cntx_pop()
      || NRKRTSIM.cntx_psh();
         NRKRTSIM.index('ID'); NRKRTSIM.prefix();
         {? NRKRTSIM.find_key(_zas)
         || NRKRTSIM.OSOBA();
            _osoba:=NRKRTSIM.OSOBA;
            _zas_naz:='Karta SIM '+NRKRTSIM.NR_KARTY
         ?};
         NRKRTSIM.cntx_pop()
      ?};
      {? ~_osoba
      || _tab.add(2,ZASOBY.ref(),'Brak użytkownika zasobu: '+_zas_naz,_zas_naz)
      |? exec('email','osoba',_osoba)=''
      || _tab.add(1,OSOBA.ref(),'Brak adresu e-mail dla: '+OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO,OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO)
      ?};
      POZAN.last();
      POZAN.prefix(ANZASPE.ref());
      POZAN.next()
   !}
?};
OSOBA.cntx_pop();
AN_ZAS.cntx_pop();
_tab


\send_err
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Wysłanie e-maila z informacjami o błędach powstałych przy tworzeniu powiadomień
::   WE: _a - nadawca
::       _b - od
::       _c - odpowiedź do
::       _d - adres email
::       _e - tablica z błędami
::----------------------------------------------------------------------------------------------------------------------
_err:=_e.TAB;
_tab:=exec('tab','!zws_zas_mail');
{? _err.first()
|| {!
   |? _tab.add(_err.ZAS,_err.REF,_err.TYP);
      _err.next()
   !}
?};
_args:=exec('add_email_a','#mailbox');
_args.FROM:=_a;
_args.SENDER:=_b;
_args.REPLYTO:=_c;
_args.RCV:=_d;
_args.SUB:={? ANZASPE.DEFAN().RODZZAS='P'
           || 'Błędy powstałe przy wysyłce informacji o potrąceniach za paliwo'
           || 'Błędy powstałe przy wysyłce informacji o potrąceniach za użytkowanie kart SIM'
           ?};
_args.BODYH:=_tab.BODY;
exec('add_email','#mailbox',_args)


\edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Tworzy okno redagowania analizy zasobów
::   WE: _a - pokazywa błędy?
::----------------------------------------------------------------------------------------------------------------------
_okn:=ANZASPE.mk_edit('Wysyłanie powiadomień'@);
ANZASPE.win_ewin(_okn,,'RED_POP');
_btn0:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['P&ozycje'@],"
   exec('poz_anzaspe','zasoby_wspolne');
   ''
");
ANZASPE.btn_eopt(_okn,_btn0,'tooltip='+'Przeglądanie pozycji analizy zasobów'@);
{? _a
|| _btn1:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Uwagi'@],"
      TabErr.show();''
   ");
   ANZASPE.btn_eopt(_okn,_btn1,'tooltip='+'Przeglądanie listy uwag'@)
?};
ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Wyślij'@],"
   {? TabErr.TAB.first()
   || _pyt:=FUN.choice(
         'Wykryto niekompletność danych podczas generowania powiadomień.\n\n'
         'Czy uruchomić mimo to wysyłanie wiadomości e-mail\nz wynikami analizy dla zasobów?'@,1,'Tak'@,'Uwagi'@
      )
   || _pyt:=1
   ?};
   {? _pyt=2
   || TabErr.show();
      _pyt:=0
   ?};
   akc_send:=_pyt;
   {? _pyt=1
   || 'key:Esc'
   || ''
   ?}
");
_btnan:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],'key:Esc');
ANZASPE.btn_eopt(_okn,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
ANZASPE.win_edit(_okn)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 216584eec6b28592293aca0fbf3eb2cbafe73bad69732dd49af84983151bf0c9f030aab98cd5a96e302a21cee24225752c302013c9927b02c4d5ce79e1034c506678779fea3d250198d15c243bf923b4d49d085c7d4cef9105dd8befd4deb7e6dbc7c07857a944ca18a5a731139a4fd06a497040e54770c0996bc8b0fbf76926

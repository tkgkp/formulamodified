:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tre_rdo_drdo.fml
:: Utworzony: 19.02.2021
:: Autor: TMW
::======================================================================================================================
:: Zawartość: Formuły czynności TRE_RDO_DRDO - Rejestracja reklamacji do dostawców
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Formuła główna czynności TRE_RDO_DRDO - Rejestracja reklamacji do dastawców
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_action:=_mp.akcja();
_context:=_mp.context;
_auto:=_mp.isAutoRun();
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo();

_autoEND:=_auto & (_proc | _todo);
_res:=null();
::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=REK_N, type=_REK_N, name=Reklamacja do dostawcy, required=N, keyref=T
{? var_pres('REK_N',_in)<>type_of(~~) & var_pres('REK_N',_in)<>type_of(null()) || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=REK_N, type=_REK_N, name=Reklamacja do dostawcy, required=N
{? var_pres('REK_N',_out)<>type_of(~~) & var_pres('REK_N',_out)<>type_of(null()) || return() ?};

:: Wyzwalacz, który po dodaniu nagłówka reklamacji:
:: - add/put: dodaje/aktualizuje rekord kluczowy utworzonej reklamacji
::   del: usuwa rekord kluczowy utworzonej reklamacji
:: - add/put: zapisuje parametr wyjściowy REK_N - wskazanie na utworzoną reklamację
::   del: zapisuje parametr wyjściowy REK_N - null()
:: - aktualizuje opis na ToDo
_mp.trigRef('REK_N',1,1,1,exec('kind_out','#b_port'),'REK_N');

BEER.SZ:='Z';
_can_continue:=0;
_rek_n:=null();
{? _action='DOŁĄCZ' | _action='KOPIUJ' | _action='POPRAW' | _action='ZAKOŃCZ' | _action='USUŃ' | _mp.pathProc()
|| _can_continue:=1
|| _clean_result:=params_exec('clean','!tre_rdo_drdo');
   _can_continue:=_clean_result.RESULT;
   _rek_n:=_clean_result.REK_N
?};

{? _can_continue>0 & _rek_n=null() & _mp.akcja()<>'' & _context<>~~ & var_pres('REK_N',_context)>0
|| _rek_n:=_context.REK_N
?};

{? _can_continue>0
|| REK_N.cntx_psh();
   REK_N.index('DP');
   REK_N.prefix('Z');

:: Odtwarzamy kontekst rekordu REK_N, jeżeli podany (rekord kluczowy albo kontekst)
   {? _rek_n<>null()
   || _find:=REK_N.seek(_rek_n)
   || _find:=0
   ?};

:: Numeracja zgłoszeń
   POM.TAB:='REK_N';
   POM.TYPDOK:='ZEK';

:: Akcja 'Dołącz' z obszaru roboczego
:: Uruchomienie ze startu procesów
:: Uruchomienie z listy zadań (bez przekazanego parametru wejściowego)
   {? (_action='DOŁĄCZ' | _mp.pathProc() | _mp.pathTodo() & _rek_n=null())
   || _czy_num:=exec('nr_pola','numery');
      REK_N.blank();
      REK_N.SZ:='Z';
      _opis:='';
      Plugin.run('REK_N_EDIT_001','add','blank');
      exec('rek_n_set_zmienne','reklamacje',0);
      exec('rek_n_win_edit_set','reklamacje');
      params_set('context',_context,'mp',_mp);
      {? _czy_num & REK_N.add()
      || _ref:=#REK_N.ref();
         {? exec('blk_lock','#table','REK_N',REK_N.name(),_ref)
         || REK_N.memo_set(_opis,'OPIS');
            Plugin.run('REK_N_EDIT_001','add','edit');
            {? REK_N.edit("exec('chk_rek_n','reklamacje')")
            || {? REK_N.NR=0
               || exec('fak_numer','numery','',0);
                  exec('znak','numery','REK_N')
               ?};
               {? REK_N.put()
               || {? REK_N.memo_put(,'OPIS')
                  || _res:=_out.REK_N:=REK_N.ref();
                     _mp.save(,_out);
                     {? REK_N.STAT_REJ='Z' || _mp.done() ?}
                  ?}
               ?}
            || oldnumer:=1;
               numer:=REK_N.NR;
               exec('nr_old','numery');
               REK_N.del();
               _mp.cancel()
            ?}
         ?};
         exec('blk_unlock','#table','REK_N',REK_N.name(),_ref)
      ?}

:: Akcja 'Popraw' z obszaru roboczego
:: Uruchomienie z listy zadań (z przekazanym parametrem wejściowym)
   |? (_action='POPRAW' | _mp.pathTodo())
   || {? exec('blk_lock','#table','REK_N',REK_N.ref(),,,'Reklamacja jest redagowana'@)
      || exec('rek_n_set_zmienne','reklamacje',1);
         exec('rek_n_win_edit_set','reklamacje');
         REK_N.memo_get(,'OPIS',);
         params_set('context',_context,'mp',_mp);
         Plugin.run('REK_N_EDIT_001','put','edit');
         {? REK_N.edit("exec('chk_rek_n','reklamacje')")
         || {? REK_N.put()
            || {? REK_N.memo_put(,'OPIS')
               || exec('desc_update','#b__box',REK_N.uidref());
                  _out.REK_N:=REK_N.ref();
                  _mp.save(,_out);
                  {? REK_N.STAT_REJ='Z' || _mp.done() ?}
               ?}
            ?}
         ?}
      ?};
      exec('blk_unlock','#table','REK_N',REK_N.ref())

:: Akcja 'Usuń' z obszaru roboczego
   |? _action='USUŃ'
   || _ref:=REK_N.ref();
      _rep:=REK_N.ref();
      _fap:=REK_N.FAK_POZ;
      _zrp:=REK_N.ZAM_ZRP;
      REKVAR.ILR:=REK_N.IL;
      REK_N.cntx_psh(); REK_N.prev(); _rep:=REK_N.ref(); REK_N.cntx_pop();
      {? exec('blk_lock','#table','REK_N',REK_N.name(),_ref,,'Reklamacja jest redagowana'@)
      || {? FUN.ask('Czy usunąć reklamację?'@)
         ||
::          Dodatkowe zabezpieczenie, jaby podczas czekania na odpowiedź, ktoś usunął rekord
            {? REK_N.seek(_ref)
            || do();
               {? exec('deleRekD','reklamacje',REK_N.ref())
               || oldnumer:=1;
                  numer:=REK_N.NR;
                  exec('nr_old','numery');
                  {? REK_N.del(,1)>0
                  || _res:=_rep;
                     {? _fap<>'' || exec('FindAndGet','#table',FAP,_fap,,"IL_RK-=REKVAR.ILR;put(1)",0) ?};
                     {? _zrp<>'' || exec('FindAndGet','#table',ref_tab(_zrp),_zrp,,"IL_RK-=REKVAR.ILR;put(1)",0) ?};
                     _mp.cancel()
                  ?}
               || undo()
               ?};
               end()
            || FUN.info('Reklamacja została już usunięta.'@);
               _mp.cancel()
            ?}
         ?}
      ?};
      exec('blk_unlock','#table','REK_N',REK_N.name(),_ref)

:: Akcja 'Zakończ' z obszaru roboczego
   |? _action='ZAKOŃCZ'
   || {? exec('blk_lock','#table','REK_N',REK_N.ref(),,,'Reklamacja jest redagowana'@)
      || {? FUN.ask('Czy zakończyć rejestrację reklamacji %1?'@[REK_N.SYM])
         || REK_N.STAT_REJ:='Z';
            REK_N.ST:=exec('status_zarejestrowano','reklamacje');
            {? REK_N.put()
            || exec('desc_update','#b__box',REK_N.uidref());
               _out.REK_N:=REK_N.ref();
               _mp.save(,_out);
               _mp.done()
            ?}
         ?}
      ?};
      exec('blk_unlock','#table','REK_N',REK_N.ref())

:: Tutaj nie powinno dojść, więc błąd
   || _msg:='Nieobsłużony kontekst wywołania czynności %1.'@@['TRE_RDO_DRDO'];
      FUN.emsg(_msg);
      _mp.error(_msg)
   ?};

   REK_N.cntx_pop();
   {? _res<>null() || {? REK_N.f_active() || REK_N.f_seek(_res) || REK_N.seek(_res) ?} ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Opis dla czynności rejestrowania reklamacji do dostawcy (TRE_RDO_DRDO)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();

_desc:='Zarejestruj reklamację do dostawcy'@@;

:: jest rekord kluczowy to ustawiam odpowiedniego REK_N
{? var_pres('[1]',_keyRefs)=type_of('')
|| {? ref_tab(_keyRefs[1])=REK_N
   || _tmp:=exec('FindAndGet','#table',REK_N,_keyRefs[1],,"SYM",'');
      _desc:={? _tmp<>'' || 'Zakończ rejestrację reklamacji do dostawcy: %1'@@[_tmp] || '' ?}
   ?}
?};
_desc


\action_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Akcja 'Dołącz' reklamacji do dostawcy
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TRE_RDO_DRDO';
_args.AKCJA:='DOŁĄCZ';
_args.PROC_START:='T';

_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);

exec('mp_run','#b__box',_args);
~~


\action_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Akcja 'Popraw' reklamacji do dostawcy
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TRE_RDO_DRDO';
_args.UIDREF:=REK_N.uidref();
_args.AKCJA:='POPRAW';
_args.PROC_START:='N';

_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'REK_N',REK_N.ref());

exec('mp_run','#b__box',_args);
~~


\action_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Akcja 'Usuń' reklamacji do dostawcy
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TRE_RDO_DRDO';
_args.UIDREF:=REK_N.uidref();
_args.AKCJA:='USUŃ';
_args.PROC_START:='N';

_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'REK_N',REK_N.ref());

exec('mp_run','#b__box',_args);
~~


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Akcja 'Zakończ' reklamacji do dostawcy
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TRE_RDO_DRDO';
_args.UIDREF:=REK_N.uidref();
_args.AKCJA:='ZAKOŃCZ';
_args.PROC_START:='N';

_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'REK_N',REK_N.ref());

exec('mp_run','#b__box',_args);
~~


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_mp:=params_get().mp;
_result:=1;
_result


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby w zależności od stanu rekordu zrobi done
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_can_continue:=1;
_obj:=obj_new('RESULT','REK_N');
_obj.RESULT:=0;
_obj.REK_N:=null();

_keyRefs:=_mp.getRefs();

{? obj_len(_keyRefs)>0
||
   {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];

      {? type_of(_kref)>0
      ||
         {? (5+ref_name(_kref))=(5+REK_N.name())
         || _obj.REK_N:=exec('FindAndGet','#table',REK_N,_kref,,,null());

            {? _obj.REK_N=null()
            ||
::             Nie znaleziono rekordu kluczowego powiązanego z reklamacją, więc robię error
               _can_continue:=0;
               exec('rek_n_deleted_in_proc','reklamacje',_mp)
            ||
::             Sprawdzenie statusu rejestracji reklamacji, jeżeli różny od 'new', to popychanie
               _stat_new:='N';
               _stat_rej:=exec('FindAndGet','#table',REK_N,_obj.REK_N,,"STAT_REJ",_stat_new);
               {? _stat_rej<>_stat_new
               || _can_continue:=0;
                  {? _mp.isService()=0 & _mp.CLEANER=0
                  || FUN.info('Reklamacja została już zakończona.'@)
                  ?};
                  _out.REK_N:=_obj.REK_N;
                  _mp.save(,_out);
                  _mp.done()
               ?}
            ?}
         ?}
      ||
::       Nie znaleziono rekordu kluczowego powiązanego z reklamacją, więc nic nie robię
::         _can_continue:=0;
::         exec('rek_n_deleted_in_proc','reklamacje',_mp);
         ~~
      ?}
   !}
?};

{? _can_continue>0
||
:: jest parametr wejściowy REM_ZGL
   {? _obj.REK_N=null() & var_pres('REK_N',_in)=type_of(null())
   || _obj.REK_N:=_in.REK_N
   ?}
?};

{? _can_continue>0
|| _obj.RESULT:=1
?};

_obj

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:54 a1285cbefa91ce63b0d09bca6f4f7f82d78e448da64cf34777875154d64ab3134eb10aadb3b3ab4a1823a1ddaeff9bff7a693424fc3948eb869d27bac8f29b4b6387a0bda0ae598b876fcbf9e99a914d2f0873b6b5e1c093bba92786eb2ddc255745218fd9b08c627151d5a65ef8e6440df39bb417d0a1a4b2c66246f20f0eff

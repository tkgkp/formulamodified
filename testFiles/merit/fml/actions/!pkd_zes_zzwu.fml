:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_zes_zzwu.fml
:: Utworzony: 28.06.2016
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_ZES_ZZWU - Przygotowanie raportu zgłoszeniowego ZUS ZWUA
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Przygotowanie raportu zgłoszeniowego ZUS ZWUA - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::# kind=WE, symbol=ZC, type=_ZC, name=Wskazanie umowy cywilnoprawnej, required=N, keyref=N
::
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, ERROR, UMOWA)", required=N
::
::# kind=WEW, symbol=FOLDER, type=STRING, name=Folder lokalny, required=N, keyref=N
::# kind=WEW, symbol=PLIK, type=STRING, name=Nazwa pliku, required=N, keyref=N
::# kind=WEW, symbol=DATA, type=DATE, name=Data wypełnienia, required=N, keyref=N
::# kind=WEW, symbol=ZUA, type=NUMBER, name=Uwzględniać zarejestrowanych, required=N, keyref=N
::# kind=WEW, symbol=GIODO, type=STRING, name=Przekazanie danych osobowych, required=N, keyref=N
::# kind=WEW, symbol=TTUB, type=STRING, name=Tytuł ubezpieczeia, required=N, keyref=N
::# kind=WEW, symbol=EMD, type=DATE, name=Data wyrejestrowania, required=N, keyref=N
::# kind=WEW, symbol=EMK, type=STRING, name=Kod wyrejestrowania, required=N, keyref=N
::# kind=WEW, symbol=F_ZATR, type=STRING, name=Forma zatrudnienia, required=N, keyref=N
::
_par:=params_get();
params_set(_par);
_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_zes_zzwu')
|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@[_mp.buf_act.UID,_do]
   ?}
|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('generuj','!pkd_zes_zzwu');
      {? type_of(_value)=type_of(0) & _value=8
      || _rv.RESULT:='UMOWA';
         _mp.save(,_rv);
         _mp.done()
      |? type_of(_value)=type_of(0)
      || _rv.RESULT:='OK';
         _mp.save(,_rv);
         {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value;
         _rv.RESULT:='ERROR';
         _mp.save(,_rv);
         _mp.done()
      ?}
   ?}
?};

{? _result<>''
::  obsługa błędów
|| FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Opis do zadania na liście zadań do zrobienia
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Przygotuj raport wyrejestrowania z ubezpieczenia ZUS ZWUA: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Przygotuj raport wyrejestrowania z ubezpieczenia ZUS ZWUA: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Przygotuj raport wyrejestrowania z ubezpieczenia ZUS ZWUA: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Przygotuj raport wyrejestrowania z ubezpieczenia ZUS ZWUA'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Komunikat o błędzie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wyrejestrowanie z ubezpieczenia ZUS ZWUA nie jest możliwe.'@+'\n'+'Nie znaleziono pracownika.'@


\generuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Generowanie raportu ZUS ZWUA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
VAR_DEL.delete('ZWUA');
_inTerm:=exec('interm','#system');
:: przygotowanie katalogu tymczasowego po stronie serwera dla pracy w inTerm
_file:='';
{? _inTerm
|| _tmp_dir:=fmk_tmp_dir(0);
   {? type_of(_tmp_dir)<>type_of(~~)
   || _pth:=_tmp_dir.get_path();
      _file:='';
      _sep:=exec('sep','#file',2);
      _file:=_pth+_sep
   || _msg:='Nie udało się utworzyć katalogu tymczasowego po stronie serwera.'@;
      VAR_DEL.delete('ZWUA');
      return(_msg)
   ?}
?};
_param:=exec('init','!pkd_zes_zzwu',_file);
_result:=_param.VALUE;

{? _result=2
|| {? ZWUA.first()
   || ZUS_RAP.cntx_psh();
      ZUS_RAP.clear();
      {? _out:=exec('otworz','rap_zus','ZUS ZWUA')
      || exec('xml_zwua_zapisz','rap_zus',ZWUA,_out,_param.F_ZATR);
         {? _inTerm || dlg_save(exec('nazwa_pliku','rap_zus'),0,) ?};
         exec('info','rap_zus',3,'ZUS ZWUA');
         fclose(_out)
      ?};
      ZUS_RAP.cntx_pop()
   || exec('info','rap_zus',2,'ZUS ZWUA')
   ?}
|? _result=3
|| _result:='Współpracownik jest już wyrejestrowany z ubezpieczenia.'@+'\n'+'Plik nie zostanie utworzony.'@
|? _result=4
|| _result:='Współpracownik nie ma wprowadzonych danych ubezpieczeniowych.'@+'\n'+'Plik nie zostanie utworzony.'@
|? _result=5
|| _result:='Nie znaleziono wskazanej umowy cywilnoprawnej.'@+'\n'+'Plik nie zostanie utworzony.'@
|? _result=6
|| _result:='Nie wskazano umowy cywilnoprawnej.'@+'\n'+'Plik nie zostanie utworzony.'@
|? _result=7
||
:: Umowa cywilno-prawna bez ubezpieczeń ZUS
   FUN.emsg('%1\n%2'
      [  'Wybrana umowa cywilnoprawna nie podlega ubezpieczeniu w ZUS.'@,
         'Raport ZWUA nie zostanie wygenerowany.'@
      ]
   )
|? _result=8
||
:: Rozwiązanie umowy o pracę (w trybie pracy z parametrem 180='T') nie zostało zaakceptowane.
   FUN.emsg('%1\n%2'
      [  'Rozwiązanie umowy o pracę nie zostało zaakceptowane.'@,
         'Plik nie zostanie utworzony.'@
      ]
   );
   1
|| _result:=0
?};
{? ~_inTerm
|| exec('set','#profile',,OPERATOR.USER().KOD,'lokalny katalog raportów ZUS',ZUS.FOLDER)
?};
VAR_DEL.delete('ZWUA');
_result


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: ustawienie wartości początkowych dla raportu ZUS ZWUA
::   WE: [_a] - ścieżka do katalogu tymczasowego na serwerze (inTerm)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_in:=_par.in;
_ib:=_par.int;
_mp:=_par.mp;
_result:=obj_new('F_ZATR','VALUE');
P.cntx_psh();
P.clear();
P.seek(_in.P,,1);
OSOBA.cntx_psh();
ZUS_RAP.cntx_psh();
ZC.cntx_psh();

ZWUA:=tab_tmp(1,
   'PREF','INTEGER','Pracownik/umowa',
   'NAZWISKO','STRING[31]','Nazwisko',
   'PIERWSZE','STRING[22]','Imię',
   'TTUB','STRING[6]','Tytuł ubezpieczenia',
   'EMT','STRING[1]','E',
   'EMD','DATE','Data wyrejestrowania',
   'EMK','STRING[4]' ,'Kod wyrejestrowania',
   'PRZ','INTEGER','Zleceniobiorca',
   'ZCREF','INTEGER','Umowa',
   'DW','DATE','Data zakończenia',
   'DO','DATE','Data rozwiązania stosunku pracy',
   'USPKOD','STRING[3]','Kod rozwiązania/wygaśnięcia stosunku pracy',
   'USPPPRAK','STRING[3]','Kod podstawy prawnej ustania stosunku pracy/służbowego',
   'USPPPRAT','STRING[70]','Podstawa prawna ustania stosunku pracy/służbowego',
   'USPSTR','STRING[1]','Strona inicjująca rozwiązanie stosunku pracy'
);

ZUS.blank();
_fileInTerm:={? var_press('_a')=type_of('') & _a<>'' || _a || '' ?};
{? _fileInTerm=''
|| {? type_of(_ib.FOLDER)=type_of(ZUS.FOLDER)
   || ZUS.FOLDER:=_ib.FOLDER
   || ZUS.FOLDER:=exec('get','#profile',,OPERATOR.USER().KOD,'lokalny katalog raportów ZUS')
   ?}
|| ZUS.FOLDER:=''
?};
{? type_of(_ib.PLIK)=type_of(ZUS.PLIK)
|| ZUS.PLIK:=_ib.PLIK
|| ZUS.PLIK:='zwua_'+P.OSOBA().NAZWISKO+'_'+OSOBA.PIERWSZE+'_'+$OSOBA.PESEL
?};
{? type_of(_ib.DATA)=type_of(ZUS.DATA) || ZUS.DATA:=_ib.DATA || ZUS.DATA:=date() ?};
{? type_of(_ib.ZUA)=type_of(ZUS.ZUA) || ZUS.ZUA:=_ib.ZUA ?};
{? type_of(_ib.GIODO)=type_of(ZUS.GIODO) || ZUS.GIODO:=_ib.GIODO ?};
{? type_of(_ib.F_ZATR)=type_of('') || _result.F_ZATR:=_ib.F_ZATR || _result.F_ZATR:=P.F_ZATR().KOD ?};
ZUS.RAPORT:='ZUS ZWUA';
ZUS.DATAG:=date(0,0,0);

_ok:={? _result.F_ZATR='P'
     || 1
     |? _result.F_ZATR='Z'
     || {? var_pres('ZC',_in)=type_of(null()) & _in.ZC<>null()
        || ZC.prefix();
           {? ZC.seek(_in.ZC)
           ||  exec('get_ZC_INFO','zlec_rh',ZUS.DATA)
           || _result.VALUE:=5;
              0
           ?}
        || _result.VALUE:=6;
           0
        ?}
     || 0
     ?};

{? _result.F_ZATR='P' & exec('zn_ttub','pracownik',P.OSOBA,ZUS.DATA)=null()
|| _result.VALUE:=4;
   _ok:=0
|? _ok
|| {? exec('nowy_zwua','rap_zus',ZWUA,_result.F_ZATR)
   || {? type_of(_ib.TTUB)=type_of(ZWUA.TTUB) || ZWUA.TTUB:=_ib.TTUB ?};
      {? type_of(_ib.EMD)=type_of(ZWUA.EMD)
      || ZWUA.EMD:=_ib.EMD
      |? _result.F_ZATR='P' & P.DZ<>date(0,0,0)
      || ZWUA.EMD:=P.DZ+1
      ?};
      {? type_of(_ib.EMK)=type_of(ZWUA.EMK) || ZWUA.EMK:=_ib.EMK ?};
      {? ZWUA.EMK='100' | ZWUA.EMK='500'
      || H_UM.cntx_psh();
         H_UM.index('OD');
         H_UM.prefix(P.ref());
         {? H_UM.last() & H_UM.P().F_ZATR().KOD='P' & (2+ZWUA.TTUB='01')
         || ZWUA.DO:=H_UM.DO;
            ZWUA.USPKOD:=H_UM.USPKOD().KOD;
            ZWUA.USPPPRAK:=H_UM.USPPPRAK().KOD;
            ZWUA.USPPPRAT:=H_UM.USPPPRAT;
            ZWUA.USPSTR:=H_UM.USPSTR().KOD
         || ZWUA.DO:=date(0,0,0);
            ZWUA.USPKOD:=ZWUA.USPPPRAK:=ZWUA.USPPPRAT:=ZWUA.USPSTR:=''
         ?};
         H_UM.cntx_pop()
      ?}
   || _result.VALUE:=7;
      _ok:=0
   ?}
|| {? type_of(_result.VALUE)=0
   || _result.VALUE:=3
   ?}
?};

{? _ok
|| _zuswer:=S_ZUS.mk_sel('Kody w dokumentach ubezpieczeniowych'@,,0,'kodywdokzus',,15,,,'U');
   S_ZUS.win_fld(_zuswer,,'KOD',,,8,,1,'Kod'@);
   S_ZUS.win_fld(_zuswer,,'OPIS',,,60,,1,'Opis'@);
   S_ZUS.win_act(_zuswer,0,'Formuła','Wybierz'@@,,,"sel_exit()",,1,0);
   S_ZUS.win_sel(_zuswer);
   ZWUA.fld_fml('EMK','F3',
      "  S_ZUS.cntx_psh();
         S_ZUS.index('S_ZUS');
         S_ZUS.prefix('Z');
         {? S_ZUS.select()
         || ZWUA.EMK:=S_ZUS.KOD
         ?};
         S_ZUS.win_sel('WER');
         S_ZUS.cntx_pop()
      "
   );
   ZWUA.fld_fml('EMK','AFTER_EDIT',
      "  S_ZUS.cntx_psh();
         S_ZUS.index('S_ZUS');
         S_ZUS.prefix('Z',fld());
         {? ~S_ZUS.first()
         || FUN.emsg('Brak kodu w słowniku'@);
            ZWUA.EMK:=''
         || {? ZWUA.EMK<>'100' & ZWUA.EMK<>'500'
            || ZWUA.DO:=date(0,0,0);
               ZWUA.USPKOD:=ZWUA.USPPPRAK:=ZWUA.USPPPRAT:=ZWUA.USPSTR:=''
            || H_UM.cntx_psh();
               H_UM.index('OD');
               H_UM.prefix(P.ref());
               {? H_UM.last() & H_UM.P().F_ZATR().KOD='P' & (2+ZWUA.TTUB='01')
               || ZWUA.DO:=H_UM.DO;
                  ZWUA.USPKOD:=H_UM.USPKOD().KOD;
                  ZWUA.USPPPRAK:=H_UM.USPPPRAK().KOD;
                  ZWUA.USPPPRAT:=H_UM.USPPPRAT;
                  ZWUA.USPSTR:=H_UM.USPSTR().KOD
               || ZWUA.DO:=date(0,0,0);
                  ZWUA.USPKOD:=ZWUA.USPPPRAK:=ZWUA.USPPPRAT:=ZWUA.USPSTR:=''
               ?};
               H_UM.cntx_pop()
            ?}
         ?};
         S_ZUS.cntx_pop()
      "
   );
   _result.VALUE:=exec('zus_dialog','rap_zus','ZWUA',_result.F_ZATR,1,_fileInTerm);
   {? ZWUA.size() || ZWUA.put() ?};

   ZWUA.cntx_psh();
   _pop:=0;
   {? exec('repeat_zwua','rap_zus',~ZUS.ZUA,_result.F_ZATR,0)
   || _pop:=ZWUA.cntx_pop();
      {? _result.VALUE=1
      || _ib.FOLDER:=ZUS.FOLDER;
         _ib.PLIK:=ZUS.PLIK;
         _ib.DATA:=ZUS.DATA;
         _ib.ZUA:=ZUS.ZUA;
         _ib.GIODO:=ZUS.GIODO;
         _ib.TTUB:=ZWUA.TTUB;
         _ib.EMD:=ZWUA.EMD;
         _ib.EMK:=ZWUA.EMK;
         _ib.F_ZATR:=_result.F_ZATR;
         _mp.save(_ib)
      ?}
   || _result.VALUE:=3
   ?};
   {? ~_pop || ZWUA.cntx_pop() ?}
?};
ZC.cntx_pop();
ZUS_RAP.cntx_pop();
OSOBA.cntx_pop();
P.cntx_pop();
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:52 6197887d228ef7ade5a2b7171b7372f1a8e88488350ef0c665a8ce6e7fc3dc13a9c734ab9c1cf4a6d41eb7301d6d1369b46354ae9bf8db3cfdb604c1921c41ab4fe8f41697f19657533fd7c1e138b9dfa665ad1313b6584fc91ed93a5992ae28cf624010d6d6fc20673ff066fa15ae81430826a73defe5fb9c6863412660aada

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdo_pdos.fml
:: Utworzony: 19.09.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PSZ_SDO_PDOS - Organizatorzy szkoleń.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Organizatorzy szkoleń - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
_wnd:=exec('create_wnd','!psz_sdo_pdos');

SZK_ORG.cntx_psh();
SZK_ORG.win_sel(_wnd);
SZK_ORG.select();
SZK_ORG.cntx_pop();
SZK_ORG.win_del(_wnd);
~~


\create_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Tworzy okienko czynności.
::   WE:
::   WY: akronim okienka tymczasowego
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';

_wnd:=SZK_ORG.grp_make('Organizatorzy szkoleń'@,
:  po wyświetleniu
   "  SZK_ORG.cntx_psh();
      SZK_ORG.f_clear();
      SZK_ORG.index('SZK_ORG');
      SZK_ORG.prefix();
      SZK_OPIS.cntx_psh();
      SZK_OPIS.f_clear();
      SZK_OPIS.index('ORG_DATA');
      grp_disp(SZK_ORG,'PSZ_OC',1);
      1
   ",
:  identyfikator
   'psz_sdo_pdos',
:  położenie
   0,0,
:  zamknięcie
   "  SZK_OPIS.cntx_pop();
      SZK_ORG.cntx_pop();
      1
   "
);

: lista organizatorow szkoleń
SZK_ORG.grp_sel(_wnd,SZK_ORG,'PSZ_OC',,
:  po odświeżeniu
   "grp_disp(SZK_OPIS,'PSZ_OC',1)",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);
SZK_ORG.grp_splt(_wnd,,'horizontal','bottom');

: szkolenia wskazywanego organizatora
SZK_ORG.grp_sel(_wnd,SZK_OPIS,'PSZ_OC',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "  {? grp_empty(SZK_ORG,'PSZ_OC')=1
      || return('#disable')
      ?};
      SZK_OPIS.prefix(SZK_ORG.ref())
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

_wnd

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:59 be205e0ada0f1a74679cc5320877400d9334d22a34e0c3054721d4551e1aef16900e34d7e1d787219353724129d9948adf4df235945c79db963262046ceb081565ce28e770c682fe92c18991574b26de35f89fbbd603184c590fa6d72d756ab10931e34ab1061020bf5620baa845e2957534c41946c85be877044a2c135eb31c

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_ofe_aaof.fml
:: Utworzony: 09.06.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły czynności LSP_OFE_AAOF - Akceptacja oferty
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=OFE,      type=_OFE,    name=Oferta,   required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

_akcja:=_mp.akcja();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & _mp.isAutoRun());

exec('init_ofe','lsp');
{? ~(var_pres('OFE',_in)=type_of(null()) & _in.OFE)
|| _mp.error('Brak wymaganego parametru OFE.')
|| OFE.cntx_psh();
   OFE.prefix();
   {? ~OFE.seek(_in.OFE)
   || _mp.error('Nie znaleziono oferty.')
   |? OFE.r_lock(1,1,1)
   || params_set('mp',_mp);
      {? _akcja='Akceptuj' | _auto
      || exec('akceptuj','!lsp_ofe_aaof',_auto)
      |? _mp.pathTodo() | _mp.pathArea() & _akcja=''
      || _win_red:=exec('ofe_win_edit','!lsp_ofe_aaof');
         _ff:="params_exec('ofe_pozycje_todo','!lsp_ofe_aaof')";
         OFE.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
         _ff:="params_exec('ofe_akceptuj_todo','!lsp_ofe_aaof'); 'key:Esc'";
         OFE.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
         _ff:="'key:Esc'";
         OFE.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
         OFE.win_edit(_win_red);
         OFE.display()
      || _mp.error('Nieobsługiwana ścieżka.')
      ?};
      OFE.r_unlock()
   || {? FUN.ask('Dokument obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
      || OFE.r_unlock()
      ?}
   ?};
   OFE.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('OFE',_in)<>type_of(~~) & _in.OFE
|| 'Zaakceptuj ofertę: %1'@@[exec('record','#to_string',_in.OFE)]
|| ''
?}


\oferta_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - Akceptuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_OFE_AAOF';
_params.UIDREF:=OFE.uidref();
_params.AKCJA:='Akceptuj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OFE',OFE.ref());

exec('mp_run','#b__box',_params)


\ofe_akceptuj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument sprzedaży - Akceptuj z todo
::   WE: params_get()   - ustawiane w exec('main','!lsp_ofe_aaof')
::----------------------------------------------------------------------------------------------------------------------
params_exec('akceptuj','!lsp_ofe_aaof');
''


\ofe_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('ofe_poz','lsp',0,1);
''


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - Akceptacja dokumentu
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       params_get()   - ustawiane w exec('main','!lsp_ofe_aaof')
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_mp:=params_get().mp;

_result:=0;

{? OFE.STAT_REJ='T'
|| {? ~_auto || FUN.info('Oferta została już zaakceptowana.'@) ?};
   _result:=1;
   _mp.done()
|? OFE.STAT_REJ='N'
|| FUN.info('Nie zakończono jeszcze rejestracji oferty.\nAkceptacja niemożliwa.'@);
   _mp.error('Nie zakończono jeszcze rejestracji oferty. Akceptacja niemożliwa.');
   _result:=1
|? OFE.T().VALACC<>null() & ~exec('validate','wzorce',OFE.T().VALACC,OFE,OFE.ref())
|| _result:=0
|? _auto | FUN.ask('Zaakceptować ofertę %1?'@[OFE.SYM])
|| OFE.STAT_REJ:='T';
   {? OFE.put()
   || _result:=1;
      _mp.done()
   ?}
?};

_result


\ofe_win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - tymczasowe okienko redakcji
::----------------------------------------------------------------------------------------------------------------------
_win_akr:='RED';
:: Tymczasowe okno redakcji
_title:='Dane oferty';
_win_red:=OFE.mk_edit(_title);
OFE.win_etab(_win_red,'Dane podstawowe');
OFE.win_ewin(_win_red,,_win_akr);
_win_red

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 a20c91e0e83c2bf85df08c859d80405076ad41e0da6b8353c264e5ff2c127cfd88ab3f682e0e621eb670093ab91b556f84f895229beca573ea2e835345c4c3acdbe10684dc23f7348eb02755b351ec0e54483c18d29ccf20d96888357a5865cf97c3fa62fdf0592e0ef815d1cd9caf7a5baef7941b7036230411748ff60ad860

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !hbn_prz_eakw.fml
:: Utworzony: 23.12.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności HBN_PRZ_EAKW - Zatwierdzanie przelewów dla wynagrodzeń
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zatwierdzanie przelewów dla wynagrodzeń - główna formuła czynności HBN_PRZ_EAKW.
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=O, type=_O, name=Wskazanie listy płac, required=T, keyref=T
::# kind=WY, symbol=O, type=_O, name=Wskazanie listy płac, required=T
::# permissions=FJKS,HRB,HRP
::# access=exec('uprawnienia','!hbn_prz_eakw')
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_ok:=0;
{? var_press('O',_in)>0 & _in.O
|| O.cntx_psh();
   O.prefix();
   {? O.seek(_in.O)
   || _sid:=exec('kbFromO','hbn');
      PB.cntx_psh();
      {? PB.name()<>'pbxxxx' || PB.use('pbxxxx') ?};
      PB.index('PBKO'); PB.prefix(_sid);
      _ok:=PB.first();
      {? _ok
      ||
::    weryfikacja uprawnień - typ przelewu
         {? ~exec('usr_hrp','b_perm',PB.KD)
         || FUN.info(
               'Brak uprawnień do typów przelewów '+
               {? PB.KD='HBN: Prosty' || 'krajowych i zagranicznych'
               |? PB.KD='HBN: US' || 'do US'
               |? PB.KD='HBN: ZUS' || 'do ZUS'
               || ''
               ?}+'.'
            );
            _ok:=-1
         ?};
::    weryfikacja uprawnień - rachnunek bankowy licencjobiorcy
         {? _ok>0 & ~exec('usr_hrb','b_perm',PB.RD)
         || FUN.info('Brak uprawnień do rachunku bankowego licencjobiorcy: %1.'@[PB.RD]);
            _ok:=-1
         ?};
         {? _ok>0 & ~Perm.hasFull('FJKS',OPERATOR.USER)
         || _tab:=tab_tmp(1,
               'ODD','STRING[10]',
            );
            {? PB.first()
            || {!
               |? {? ~_tab.find_key(PB.ODD().OD,)
                  || _tab.ODD:=ODD.OD;
                     _tab.add()
                  ?};
                  PB.next()
               !};
               {? _tab.first()
               || {!
                  |?
::                weryfikacja uprawnień - jednostka księgowa
                     {? ~exec('usr_fjks','b_perm',_tab.ODD)
                     || FUN.info('Brak uprawnień do jednostki księgowej %1.'@[_tab.ODD]);
                        _ok:=-1
                     ?};
                     _ok>0 & _tab.next()
                  !}
               ?}
            ?}
         ?}
      ?};
      PB.cntx_pop();
      {? _ok=0
      || FUN.info('Nie znaleziono przelewów dla wynagrodzeń z listy płac: %1.'@[~O.LT]);
         _ok:=-1
      |? _ok=-1
      || _ok:=0
      || {? _mp.akcja()='Wycofaj'
         || _ok:=FUN.ask('Wycofać zatwierdzenie przelewów dla listy płac: %1?'@[~O.LT])
         || _ok:=1
         ?};
         {? _ok
         || _ok:=params_exec('confirm','!hbn_prz_eakw',_sid,_mp,~O.LT,_mp.isMicro())
         ?}
      ?}
   || FUN.info('Nie znaleziono listy płac.'@);
      _ok:=-1
   ?};
   O.cntx_pop()
|| FUN.info('Nie podano listy płac.'@);
   _ok:=-1
?};
{? _ok
|| _out.O:=_in.O;
   _mp.save(,_out);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zatwierdzenie przelewów dla listy płac'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('O',_we) & _we.O
|| _desc:='Zatwierdzenie przelewów dla listy płac: %1'@@[exec('record','#to_string',_we.O)]
?};
_desc


\create_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła definiuje okno dla listy przelewów do zatwierdzenia
::   WY: akronim okna tymczasowego
::----------------------------------------------------------------------------------------------------------------------
_win:=PB.mk_sel('Zatwierdzanie przelewów z listy płac'@,'P',0,'_list_przel_zat',10,5,20,,'U',,,,,'normal');
PB.win_fld(_win,PB,'ODD','OD','ODDZIALY',10,,,'Jednostka księgowa'@,,'Jednostka księgowa'@);
PB.win_fld(_win,PB,'DP',,,10,,,'Data przelewu'@,,'Data przelewu'@);
PB.win_fld(_win,PB,'DZ',,,10,,,'Data utworzenia'@,,'Data utworzenia'@);
PB.win_fld(_win,PB,'W',,,23,,,'Odbiorca'@,,'Odbiorca'@);
PB.win_fld(_win,PB,'TYT',,,21,,,'Tytułem'@,,'Tytułem'@);
PB.win_fld(_win,PB,'WAL','KOD','SL',7,,,'Waluta'@,,'Waluta'@);
PB.win_fld(_win,PB,'KW',,,24,2,,'Kwota'@,,'Kwota'@);
PB.win_act(_win,,'Kolejność');
PB.win_act(_win,,'Formuła','Zatwierdź'@@,,,"sel_exit()",,,,,,'Z',0);
PB.win_btn(_win,'text=%1,btn_label_align=center,panel=bottom,align=end'['Zatwierdź'@],'menu:Z');
PB.win_btn(_win,'text=%1,btn_label_align=center,panel=bottom,align=end'['Anuluj'@],'key:Esc');
_form:="RACHBANK.KB_1R:=PB.RD;
        RACHBANK.KB_1R_BD:='RACHBANK.KB_1R:=RB.get_rbtx(2,RACHBANK.KB_1R,\\'\\')';
        RACHBANK.RB_KH_PB:=PB.RW";
PB.win_act(_win,,'Rekord',,,,_form);
_win


\confirm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła zatwierdza lub anuluje przelew
::   WE: _a - SID listy przelewów, _b-obiekt odpowiedzialny za obługę procesu, _c-symbol listy płac
::       _d - 0/1 czy mikroproces
::   WY: 0 - nie udało się, 1 - udało się
::----------------------------------------------------------------------------------------------------------------------
_mp:=_b;
_tryb:={? _mp.akcja()='Wycofaj' || 2 || 1 ?};
g_cnt:=0;
_wy:=1;
{? _<4 || _d:=0 ?};
PB.cntx_psh();
{? PB.name()<>'pbxxxx' || PB.use('pbxxxx') ?};
PB.index('PBKO');
PB.prefix(_a);
_ile:=PB.size();
{? PB.first()
|| _win:=exec('create_win','!hbn_prz_eakw');
   PB.win_sel(_win);
   {? _d=1 | PB.select()
   || PB.first();
      {!
      |? {? exec('pb_akc','hbn',_tryb)
         || PB.put()
         ?};
         PB.next()
      !}
   || _wy:=0
   ?}
?};
PB.cntx_pop();
{? ~_mp.pathTodo()
|| {? PAR_SKID.get(68)='N' || exec('podsuma','hbn') ?};
   PAR_WYDR.RPAR3:=PAR_WYDR.RPAR1+PAR_WYDR.RPAR2
?};
{? _wy || FUN.info('Zatwierdzono przelewy z listy płac: %3.\nLiczba przelewów, dla których zmieniono status zatwierdzenia: %1 z %2.'@[$g_cnt,$_ile,~O.LT]) ?};

VAR_DEL.delete('g_cnt');
_wy


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
{? var_press('O',_in)>0 & _in.O
|| O.cntx_psh();
   O.prefix();
   {? O.seek(_in.O)
   || _sid:=exec('kbFromO','hbn');
      PB.cntx_psh();
      {? PB.name()<>'pbxxxx' || PB.use('pbxxxx') ?};
      PB.index('PBKO');
      PB.prefix(_sid);
      _ok:=PB.first();
      {? _ok
      || USERS.cntx_psh();
         USERS.clear();
         {? USERS.seek(_user)
         || {? exec('usr_fjks','b_perm',PB.ODD().OD,USERS.ref())
               & exec('usr_hrb','b_perm',PB.RD,USERS.ref())
               & exec('usr_hrp','b_perm',PB.KD,USERS.ref())
            || _result:=1
            ?}
         ?};
         USERS.cntx_pop()
      ||
:: nie ma przelewów dla wskazanej listy płac to znaczy, że należy zwrócić pełne uprawnienia
:: inaczej zadanie ich akceptacji nigdy nie zniknie z todo.
         _result:=1
      ?};
      PB.cntx_pop()
   ?};
   O.cntx_pop()
||
:: nie ma wejściowej listy płac to znaczy, że należy zwrócić pełne uprawnienia
:: inaczej zadanie do usuniętej listy nigdy nie zniknie z todo.
   _result:=1
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 1b9a189a60636fd174dd2dce19b213d8309153eb1177ec0f56dac6c2cb916f96088f08b5017590958d06deedd5bec962541943fa5f90e2146b8a2298e2cf942cd69787a1b38309d985bf0a77ff648a37817af722d774738f5c58aa8cee8f2be14adfa4fa0e6fbb84342d6da67775955af076a20c022cec821051281cbfa0c4f5

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ptna.fml
:: Utworzony: 16.02.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PTNA - Tytuły naukowe.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Redagowanie tytułów naukowych i innych - główna formuła czynności.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
TYTULN.cntx_psh();
TYTULN.index('POZIOM');
TYTULN.prefix();
TYTULN.win_sel('WER');
TYTULN.win_edit('RED');
TYTULN.select();
TYTULN.cntx_pop();
~~


\przenumeruj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła porządkująca poziom stopnia naukowego.
::   WE:
::   WY: ~~
::  OLD: \tytuln_poziom/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
TYTULN.cntx_psh();
TYTULN.index('POZIOM');
TYTULN.prefix('T');
{? TYTULN.last()
|| _poziom:=1;
   {!
   |? TYTULN.POZIOM:=_poziom;
      _poziom+=TYTULN.put(1);
      TYTULN.prev()
   !}
?};
TYTULN.cntx_pop();
~~


\tytuln_poziom_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła, która (po redagowaniu rekordu) ustala jaka powinna być wartość pola TYTULN.POZIOM. W tym sensie,
::       merytorycznie odpowiada formule na wartość początkową, mimo, że jest wykonywana znacznie później.
::   WE:
::   WY: Wartość, która powinna zostać nadana polu TYTULN.POZIOM.
::----------------------------------------------------------------------------------------------------------------------
{? TYTULN.NAUKOWY='T'
|| TYTULN.cntx_psh();
   TYTULN.index('POZIOM');
   TYTULN.prefix('T');
   _poziom:={? TYTULN.first() || TYTULN.POZIOM ?}+1;
   TYTULN.cntx_pop()
|| _poziom:=0
?};
TYTULN.POZIOM:=_poziom


\tytuln_naukowy_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed redagowaniem pola TYTULN.NAUKOWY. Formuła blokuje dostęp do zmiany wartości pola, jeżeli tytuł jest już
::       wykorzystywany w danych (istnieją rekordy powiązane).
::   WE:
::   WY:
::  OLD: \tytuln_be_nau/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()<>'popraw' || return(1) ?};

: Metoda count() podczytuje bieżący rekord - musimy zachować kontekst.
TYTULN.cntx_psh();
_count:=TYTULN.count();
TYTULN.cntx_pop();
_count=0


\tytuln_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Dołącz w tabeli TYTULN.
::   WE:
::   WY:
::  OLD: \tytuln_add/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
TYTULN.blank();
{? TYTULN.edit("exec('tytuln_ae','!zws_par_ptna',0)")
|| do();
   exec('tytuln_poziom_bl','!zws_par_ptna');
   TYTULN.add();
   {? ~end()
   || FUN.info('Dołączenie nowego zapisu nie powiodło się.'@)
   ?}
?}


\tytuln_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Popraw w tabeli TYTULN.
::   WE:
::   WY:
::  OLD: \tytuln_edit/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_naukowy:=TYTULN.NAUKOWY;
{? TYTULN.edit("exec('tytuln_ae','!zws_par_ptna',1)")
|| do();
   {? _naukowy='T' & TYTULN.NAUKOWY='N'
:     Był naukowy, ale już nie jest.
   || TYTULN.POZIOM:=0;
      TYTULN.put();
      exec('przenumeruj','!zws_par_ptna')

   |? _naukowy='N' & TYTULN.NAUKOWY='T'
:     Nie był naukowy, ale teraz jest.
   || exec('tytuln_poziom_bl','!zws_par_ptna');
      TYTULN.put()

:     Był i jest lub nie był i nie jest naukowy.
   || TYTULN.put()
   ?};
   {? ~end()
   || FUN.info('Poprawienie bieżącego zapisu nie powiodło się.'@)
   ?}
?}


\tytuln_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Usuń w tabeli TYTULN.
::   WE:
::   WY:
::  OLD: \del_rec/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? TYTULN.count()
|| exec('del_warn','#table')
|? exec('del_ask','#table')
|| do();
   _naukowy:=TYTULN.NAUKOWY='T';
   {? TYTULN.del() & _naukowy || exec('przenumeruj','!zws_par_ptna') ?};
   {? ~end()
   || FUN.info('Usunięcie bieżącego zapisu nie powiodło się.'@)
   ?}
?}


\tytuln_przesun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji przesuń "Wyżej" lub "Niżej".
::   WE: _a - Kierunek przesunięcia:
::          +1 - wyżej;
::          -1 - niżej.
::   WY:
::  OLD: \tytuln_move/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_kierunek:={? var_pres('_a')=type_of(0) & (_a=1 | _a=-1) || _a ?};
{? _kierunek=0 | TYTULN.NAUKOWY<>'T' || return(0) ?};

_ret:=1;
TYTULN.cntx_psh();
TYTULN.index('POZIOM');
TYTULN.prefix('T');
do();
_tyt1:=obj_new('REF','POZIOM');
_tyt1.REF:=TYTULN.ref();
_tyt1.POZIOM:=TYTULN.POZIOM;
_dalej:={? _kierunek=1 || TYTULN.prev() || TYTULN.next() ?};
_err:='Przesunięcie bieżącego zapisu nie powiodło się.'@;
{? ~_dalej
|| undo();
   _err:=
      {? _kierunek=1 || 'Tytuł jest najwyżej w hierarchii.'@ || 'Tytuł jest najniżej w hierarchii.'@ ?}+'\n'+
      'Zmiana poziomu nie jest możliwa.'@;
   _ret:=0
|| _tyt2:=obj_new('REF','POZIOM');
   _tyt2.REF:=TYTULN.ref();
   _tyt2.POZIOM:=TYTULN.POZIOM;

   TYTULN.POZIOM:=0;
   TYTULN.put();
   {? TYTULN.seek(_tyt1.REF)
   || TYTULN.POZIOM:=_tyt2.POZIOM;
      TYTULN.put()
   || undo()
   ?};
   {? TYTULN.seek(_tyt2.REF)
   || TYTULN.POZIOM:=_tyt1.POZIOM;
      TYTULN.put()
   || undo()
   ?}
?};
{? ~end()
|| FUN.info(_err)
?};
TYTULN.cntx_pop();
_ret


\tytuln_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem rekordu tabeli TYTULN. Formuła ukrywa akcje typu "przesuń" dla tytułów, które nie są
::       naukowe.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| TYTULN.actions_grayed('WER',{? TYTULN.NAUKOWY='T' || '' || 'WN' ?})
?};
0


\tytuln_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu rekordu tabeli TYTULN.
::   WE: _a - Specyfikacja testu
::            0 - Dołącz;
::            1 - Popraw.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(TYTULN,_a,,'KOD','NAZ')

:Sign Version 2.0 jowisz:1028 2019/10/14 09:20:09 93942ab36190a7ffce6cbb4c2207d567bc12e330b81ce676bef504216fec4bc10517a6a31c74222c4c4d3679a7247c97bae4e0779177e82a58d28efcfbeb69b98423b4b5b104acfcabc41ad45b0eb031c6dc4de80cac4d980366d713b293369ae489c822477e07d1c3795a22c4391181e15ab72e9d68394c269c6872de30b27c

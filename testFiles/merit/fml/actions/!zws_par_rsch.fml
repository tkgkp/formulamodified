:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_rsch.fml
:: Utworzony: 08.06.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_RSCH - Schematy danych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Schematy danych - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
: uzupełnij dane "systemowe"
exec('dane_sys','schemat');

: ustaw domyślne okienka
exec('ud_wnd','schemat');

_cntx:=obj_new('CURRENT','M4');
params_set('cntx',_cntx);
: bieżące wersje schematów
_cntx.CURRENT:=tab_tmp(,
   'SCH','INTEGER','Schemat'@,
   'WER','INTEGER','Wersja'@
);
: podobno inaczej się nie da...
_cntx.M4:=(var_pres('m4_def')>0);

UD_SCH.clear();

UD_DEF.cntx_psh();
UD_TYP.cntx_psh();
{? _cntx.M4
|| UD_DEF.win_sel('M4_WER');
   UD_DEF.win_dict('M4_WER');
   UD_DEF.dnd_sel('M4_WER',,'records.UD_DEF',"exec('ud_def_dnd','schemat')");
   UD_TYP.win_sel('M4_ADM');
   UD_TYP.select();
   UD_DEF.dnd_sel('M4_WER',,'records.UD_DEF',"")

|| UD_DEF.win_sel('WER');
   UD_DEF.dnd_sel('WER',,'records.UD_DEF',"exec('ud_def_dnd','schemat')");
   UD_TYP.win_sel('ADM');
   UD_TYP.select();
   UD_DEF.dnd_sel('WER',,'records.UD_DEF',"")
?};
UD_TYP.cntx_pop();
UD_DEF.cntx_pop();
~~


\ud_typ_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa specjalnych zapisów w tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
{? exec('chron_typ','schemat',UD_TYP.SYMBOL)=1
|| UD_TYP.OCHRONA:='T'
?};
1


\ud_typ_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dołącz przed" tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
exec('ud_typ_modb','!zws_par_rsch');
1


\ud_typ_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw przed" tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
exec('ud_typ_modb','!zws_par_rsch');
1


\ud_typ_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw po" tabeli UD_TYP
::   WE: _a - wynik akcji poprawiania: 1 - udało się zmienić rekord, 0 - rekord nie został zmieniony
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie zmieniono rekordu
{? ~_a | do_state()<>1 || return() ?};

{? UD_TYP.OCHRONA<>bfld('OCHRONA') & UD_TYP.OCHRONA<>'T'
|| exec('del_ndx','#table',UDB_SYS,'UD_TYP',UD_TYP.ref())
?};
~~


\ud_typ_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',UD_SCH,'SYMBOL',UD_TYP.ref()) &
exec('del_ndx','#table',UD_SKL,'SYMBOL',UD_TYP.ref())


\ud_sch_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dołącz przed" tabeli UD_SCH.
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.AKTYWNY:='T';

{? UD_SCH.UD_SCH<>0
|| _symbol:=UD_SCH.SYMBOL;
   _opis:='';
   _rekord:=0;
   UD_SCH.cntx_psh();
   UD_SCH.clear();
   {? UD_SCH.seek(UD_SCH.UD_SCH,)
   || _rekord:=UD_SCH.REKORD;
      _opis:=UD_SCH.OPIS
   ?};
   {? _symbol=''
   || _uid:='';
      UD_SCH.index('SYMBOL');
      UD_SCH.prefix(UD_SCH.UD_TYP);
      {!
      |? _uid:=exec('gen_id','#id',8);
         UD_SCH.find_key(_uid,)
      !};
      _symbol:=_uid
   ?};
   UD_SCH.cntx_pop();
   UD_SCH.SYMBOL:=_symbol;
   UD_SCH.OPIS:=_opis;
   UD_SCH.AKTYWNY:='N';
   UD_SCH.REKORD:=_rekord
|| UD_SCH.UWAGI:=exec('ud_sch_uwagi_wp','schemat')
?};

1


\ud_sch_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dołącz po" tabeli UD_SCH.
::   WE: _a - wynik akcji dołączania: 1 - udało się dodać rekord, 0 - rekord nie został dodany
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie dodano rekordu
{? ~_a | do_state()<>1 || return() ?};

{? UD_SCH.DOMYSLNY='T'
|| exec('ud_sch_domyslny_wyl','!zws_par_rsch',UD_SCH.UD_TYP,UD_SCH.ref())
?};

UD_SCH.put();
~~


\ud_sch_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw przed" tabeli UD_SCH.
::----------------------------------------------------------------------------------------------------------------------
{? UD_SCH.AKTYWNY='N'
|| UD_SCH.DOMYSLNY:='N'

|? UD_SCH.AKTYWNY='T'
|| _rekord:=#UD_SCH.ref();
   _domyslny:=UD_SCH.DOMYSLNY;
   _symbol:=UD_SCH.SYMBOL;
   _opis:=UD_SCH.OPIS;
   {? UD_SCH.UD_SCH<>0
   || UD_SCH.cntx_psh();
      UD_SCH.clear();
      {? UD_SCH.seek(UD_SCH.UD_SCH,)
      || _rekord:=#UD_SCH.ref();
         _domyslny:=UD_SCH.DOMYSLNY;
         _symbol:=UD_SCH.SYMBOL;
         _opis:=UD_SCH.OPIS;
         UD_SCH.cntx_psh();
         UD_SCH.index('SYMBOL');
         UD_SCH.prefix(UD_SCH.UD_TYP);
         _uid:='';
         {!
         |? _uid:=exec('gen_id','#id',8);
            UD_SCH.find_key(_uid,)
         !};
         UD_SCH.cntx_pop();
         UD_SCH.SYMBOL:=_uid;
         UD_SCH.AKTYWNY:='N';
         UD_SCH.put()
      ?};
      UD_SCH.cntx_pop()
   ?};
   {? UD_SCH.REKORD=0
   || UD_SCH.REKORD:=_rekord
   ?};
   UD_SCH.DOMYSLNY:=_domyslny;
   UD_SCH.SYMBOL:=_symbol;
   UD_SCH.OPIS:=_opis;
   UD_SCH.UD_SCH:=0
?};

1


\ud_sch_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw po" tabeli UD_SCH.
::   WE: _a - wynik akcji poprawiania: 1 - udało się zmienić rekord, 0 - rekord nie został zmieniony
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie zmieniono rekordu
{? ~_a | do_state()<>1 || return() ?};

{? UD_SCH.AKTYWNY<>bfld('AKTYWNY')
|| {? UD_SCH.AKTYWNY='T'
   || _sch:=#UD_SCH.ref();
      UD_SCH.cntx_psh();
      UD_SCH.index('DATA');
      UD_SCH.prefix(UD_SCH.REKORD);
      _loop:=UD_SCH.first();
      {!
      |? _loop
      |! {? UD_SCH.ref()<>_sch
         || UD_SCH.UD_SCH:=_sch;
            UD_SCH.put()
         ?};
         _loop:=UD_SCH.next()
      !};
      UD_SCH.cntx_pop()
   ?}
?};

{? UD_SCH.UD_SCH<>bfld('UD_SCH') & UD_SCH.UD_SCH<>0 & bfld('UD_SCH')=0
:: schemat się "zdeaktualizował", ewentualna miana wykorzystań
|| _new:=UD_SCH.ref();
   _old:=null();
   UD_SCH.cntx_psh();
   UD_SCH.prefix();
   {? UD_SCH.seek(UD_SCH.UD_SCH,)
   || _new:=UD_SCH.ref()
   ?};
   UD_SCH.cntx_pop();
   exec('zmien_wersje','schemat',_old,_new)
?};

{? UD_SCH.DOMYSLNY<>bfld('DOMYSLNY')
|| {? UD_SCH.DOMYSLNY='T'
   || exec('ud_sch_domyslny_wyl','!zws_par_rsch',UD_SCH.UD_TYP,UD_SCH.ref());
      exec('ud_sch_domyslny_akt','!zws_par_rsch',UD_SCH.ref(),"UD_DEF.SCIEZKA")
   || exec('ud_sch_domyslny_akt','!zws_par_rsch',UD_SCH.ref(),"''")
   ?}
?};
~~


\ud_sch_domyslny_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Aktualizuje wyrażenie ścieżkowe w tabeli UD_SKL zgodnie ze wskazanym schematem.
::   WE: _a - wskazanie schematu danych
::       _b - formuła na wartość pola ścieżka w tabeli UD_SKL
::----------------------------------------------------------------------------------------------------------------------
UD_SKL.cntx_psh();
UD_SKL.clear();
UD_DEF.cntx_psh();
UD_DEF.index('SYMBOL');
UD_DEF.prefix(_a);
_loop:=UD_DEF.first();
{!
|? _loop
|! UD_DEF.UD_SKL();
   UD_SKL.SCIEZKA:=_b();
   UD_SKL.put();
   _loop:=UD_DEF.next()
!};
UD_DEF.cntx_pop();
UD_SKL.cntx_pop();
~~


\ud_sch_domyslny_wyl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyłącza znacznik domyślności ustawiony dla innego zapisu niż wskazany.
::   WE: _a - wskazanie typu danych
::       _b - wskazanie schematu
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.cntx_psh();
UD_SCH.index('SYMBOL');
UD_SCH.prefix(_a);
_loop:=UD_SCH.first();
{!
|? _loop
|! {? UD_SCH.ref()<>_b & UD_SCH.DOMYSLNY='T'
   || UD_SCH.DOMYSLNY:='N';
      UD_SCH.put();
      _loop:=0
   ?};
   _loop:=(_loop & UD_SCH.next())
!};
UD_SCH.cntx_pop();
~~


\ud_sch_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli UD_SCH
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',UD_SCH,'UD_SCH',#UD_SCH.ref()) &
exec('del_ndx','#table',UD_DEF,'SYMBOL',UD_SCH.ref()) &
exec('del_ndx','#table',UD_POZ,'NUMER',UD_SCH.ref())


\ud_skl_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz po dołączeniu rekordu tabeli UD_SKL
::   WE: _a - wynik akcji dołączania: 1 - udało się dodać rekord, 0 - rekord nie został dodany
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie zmieniono rekordu
{? ~_a | do_state()<>1 || return() ?};

: uzupełnij uprawnienia dla składnika
exec('upr_skl_add','schemat',UD_SKL.ref());
~~


\ud_skl_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz przed poprawieniem rekordu tabeli UD_SKL
::----------------------------------------------------------------------------------------------------------------------
UD_SKL.DOMYSLNY:={? UD_SKL.SCIEZKA<>'' || 'T' || 'N' ?};
1


\ud_skl_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz po poprawieniu rekordu tabeli UD_SKL
::   WE: _a - wynik akcji poprawiania: 1 - udało się zmienić rekord, 0 - rekord nie został zmieniony
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie zmieniono rekordu
{? ~_a | do_state()<>1 || return() ?};

: aktualizuj anomalie w definicji drzewka - tabela UD_DEF
{? UD_SKL.SYMBOL<>bfld('SYMBOL') | UD_SKL.OPIS<>bfld('OPIS')
|| UD_DEF.cntx_psh();
   UD_DEF.index('SKLREF');
   UD_DEF.prefix(UD_SKL.ref());
   _loop:=UD_DEF.first;
   {!
   |? _loop
   |! UD_DEF.put();
      _loop:=UD_DEF.next()
   !};
   UD_DEF.cntx_pop();

   {? exec('lic','#b_domain','POR')
   || _wm:='_hist';
      H.cntx_psh();
      {? H.name()<>_wm
      || H.use(_wm)
      ?};
      H.index('UD_SKL');
      H.prefix(UD_SKL.ref());
      {? H.first()
      || H.trig_off('*','*');
         {!
         |? H.put(,1);
            H.next()
         !};
         H.trig_on('*','*')
      ?};
      H.cntx_pop()
   ?}
?};

: aktualizuj anomalie w uprawnieniach - tabela UDB_UPR
{? UD_SKL.DOMYSLNY<>bfld('DOMYSLNY')
|| UDB_UPR.cntx_psh();
   UDB_UPR.index('FK_SKL');
   UDB_UPR.prefix(UD_SKL.ref());
   _loop:=UDB_UPR.first();
   {!
   |? _loop
   |! UDB_UPR.put();
      _loop:=UDB_UPR.next()
   !};
   UDB_UPR.cntx_pop()
?};

{? UD_SKL.SYMBOL<>bfld('SYMBOL') | UD_SKL.OPIS<>bfld('OPIS')
|| _f:="SLO.TR:=UD_SKL.OPIS; SLO.put()";
   exec('ud2slo','slo_slu',1,_f);
   exec('ud2slo','slo_slu',0,_f)
?};
~~


\ud_skl_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz przed usunięciem rekordu tabeli UD_SKL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',SKIDXODD,'UD_SKL',UD_SKL.ref())
& exec('upr_skl_del','schemat',UD_SKL.ref())


\ud_def_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz przed dołączeniem rekordu tabeli UD_DEF
::  OLD: \ait_def/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.SCIEZKA:=exec('ud_def_sciezka','!zws_par_rsch');
exec('ud_def_akt','!zws_par_rsch')


\ud_def_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz po dołączeniu rekordu tabeli UD_DEF
::   WE: _a - wynik akcji dołączania: 1 - udało się dodać rekord, 0 - rekord nie został dodany
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie dodano rekordu
{? ~_a | do_state()<>1 || return() ?};

: zaktualizuj ścieżkę w UD_SKL
exec('ud_def_sciezka_skl','!zws_par_rsch');

: uzupełnij wszystkie widoki
exec('upr_def_add','schemat',UD_DEF.ref());
~~


\ud_def_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz przed poprawieniem rekordu tabeli UD_DEF
::  OLD: \but_def/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.SCIEZKA:=exec('ud_def_sciezka','!zws_par_rsch');
exec('ud_def_akt','!zws_par_rsch')


\ud_def_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz po poprawieniu rekordu tabeli UD_DEF
::   WE: _a - wynik akcji poprawiania: 1 - udało się zmienić rekord, 0 - rekord nie został zmieniony
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie zmieniono rekordu
{? ~_a | do_state()<>1 || return() ?};

: zmiana przypiętego składnika
{? UD_DEF.UD_SKL<>bfld('UD_SKL')
|| {? UD_DEF.UD_SCH().DOMYSLNY='T'
   || UD_SKL.cntx_psh();
      UD_SKL.clear();
      {? UD_SKL.seek(bfld('UD_SKL'))
      || UD_SKL.SCIEZKA:='';
         UD_SKL.put()
      ?};
      UD_SKL.cntx_pop()
   ?};

:  aktualizuj widoki wynikające z uprawnień
   exec('upr_def_put','schemat',UD_DEF.ref())
?};

: aktualizuj ścieżki
{? UD_DEF.SCIEZKA<>bfld('SCIEZKA')
|| UD_DEF.cntx_psh();
   UD_DEF.index('SYMBOL');
   UD_DEF.prefix(UD_DEF.UD_SCH,#UD_DEF.ref());
   _loop:=UD_DEF.first();
   {!
   |? _loop
   |! UD_DEF.put();
      _loop:=UD_DEF.next()
   !};
   UD_DEF.cntx_pop();
:  zaktualizuj ścieżki w UD_SKL i UDB_WID
   exec('ud_def_sciezka_skl','!zws_par_rsch');
   exec('ud_def_sciezka_wid','!zws_par_rsch')
?};

:: Sprawdzenie czy jest dziedzina portal:
{? exec('lic','#b_domain','POR')
|| UD_TYP.cntx_psh();
   UD_SKL.cntx_psh();
   UD_SKL.prefix();
:: Czy element jest ze schematu wysyłanego na portal:
   {? UD_DEF.UD_SKL().UD_TYP<>exec('szukaj_ud_typ','schemat','PODZORG') | UD_SKL.DOMYSLNY<>'T'
   || UD_DEF.cntx_psh();
::    Ograniczenie dziedziny do podrzędnych pierwszego stopnia:
      UD_DEF.index('SQLSYM');
      UD_DEF.prefix($UD_DEF.ref(),);
      {? UD_DEF.first()
      || {!
         |? UD_DEF.put(,1);

            UD_DEF.next()
         !}
      ?};
      UD_DEF.cntx_pop()
   ?};
   UD_SKL.cntx_pop(); UD_TYP.cntx_pop()
?};

~~


\ud_def_sciezka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Określa ścieżkę bieżącego elementu.
::   WY: _a - ścieżka do
::----------------------------------------------------------------------------------------------------------------------
_path:='';
{? UD_DEF.UD_DEF
|| UD_DEF.cntx_psh();
   UD_DEF.prefix();
   {? UD_DEF.seek(UD_DEF.UD_DEF,)
   || _path:=UD_DEF.SCIEZKA
   ?};
   UD_DEF.cntx_pop()
?};
: pełna ścieżka
_path+($UD_DEF.UD_SKL+8)+'|'


\ud_def_sciezka_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Aktualizuje ścieżkę w tabeli UD_SKL na podstawie ścieżki w bieżącym zapisie UD_DEF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.cntx_psh();
{? UD_DEF.UD_SCH().DOMYSLNY='T'
|| UD_SKL.cntx_psh();
   UD_SKL.clear();
   UD_DEF.UD_SKL();
   UD_SKL.SCIEZKA:=UD_DEF.SCIEZKA;
   UD_SKL.put();
   UD_SKL.cntx_pop()
?};
UD_SCH.cntx_pop();
~~


\ud_def_sciezka_wid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Aktualizuje ścieżkę w tabeli UDB_WID na podstawie ścieżki w bieżącym zapisie UD_DEF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UDB_WID.cntx_psh();
UDB_WID.index('FK_DEF');
UDB_WID.prefix(UD_DEF.ref());
_loop:=UDB_WID.first();
{!
|? _loop
|! UDB_WID.put();
   _loop:=UDB_WID.next()
!};
UDB_WID.cntx_pop();
~~


\ud_def_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Aktualizuje anomalie w tabeli UD_DEF
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.SYMBOL:=UD_DEF.UD_SKL().SYMBOL;
UD_DEF.OPIS:=UD_DEF.UD_SKL().OPIS;

UD_DEF.UD_SQL:=
   {? UD_DEF.UD_DEF
   || _ref:=((8*'0')+$BIT.hex(UD_DEF.UD_DEF))+8;
      form(UD_DEF.name,8)+-_ref
   || ''
   ?};

{? UD_DEF.ZN_AGR=''
|| UD_DEF.ZN_AGR:='+'
?};

1


\ud_def_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli UD_DEF
::  OLD: \bdt_ud_def/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',UD_DEF,'SYMBOL',UD_DEF.UD_SCH,#UD_DEF.ref()) &
exec('del_ndx','#table',K_FORM,'K_W_OBL',UD_DEF.ref()) &
exec('upr_def_del','schemat',UD_DEF.ref())


\ud_def_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń po" tabeli UD_DEF
::   WE: _a - wynik akcji dołączania: 1 - udało się dodać rekord, 0 - rekord nie został dodany
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie usunięto rekordu
{? ~_a | do_state()<>1 || return() ?};

UD_SCH.cntx_psh();
UD_SCH.prefix();
{? UD_SCH.seek(bfld('UD_SCH')) & UD_SCH.DOMYSLNY='T'
|| UD_SKL.cntx_psh();
   UD_SKL.prefix();
   {? UD_SKL.seek(bfld('UD_SKL'))
   || UD_SKL.SCIEZKA:='';
      UD_SKL.put()
   ?};
   UD_SKL.cntx_pop()
?};
UD_SCH.cntx_pop();
~~


\udb_grp_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli UDB_GRP
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',UDB_DOM,'SYMBOL',UDB_GRP.ref()) &
exec('del_ndx','#table',UDB_SYS,'UNIQUE',UDB_GRP.ref())


\udb_sys_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dołącz po" tabeli UDB_SYS
::   WE: _a - wynik akcji dołączania: 1 - udało się dodać rekord, 0 - rekord nie został dodany
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie dodano rekordu
{? ~_a | do_state()<>1 || return() ?};

exec('upr_sys_add','schemat',UDB_SYS.ref());
~~


\udb_sys_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw po" tabeli UDB_SYS
::   WE: _a - wynik akcji poprawiania: 1 - udało się zmienić rekord, 0 - rekord nie został zmieniony
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie zmieniono rekordu
{? ~_a | do_state()<>1 || return() ?};

{? UDB_SYS.UD_TYP<>bfld('UD_TYP')
|| exec('upr_sys_del','schemat',UDB_SYS.ref());
   exec('upr_sys_add','schemat',UDB_SYS.ref())
?};
~~


\udb_sys_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli UDB_SYS
::----------------------------------------------------------------------------------------------------------------------
exec('upr_sys_del','schemat',UDB_SYS.ref())


\ud_skl_lista_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed redakcją pola LISTA tabeli UD_SKL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UD_SKL.AKTYWNY='T'


\ud_skl_aktywny_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po redakcji pola AKTYWNY tabeli UD_SKL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? UD_SKL.AKTYWNY='N'
|| UD_SKL.LISTA:='N'
?};
1


\ud_typ_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Dołącz przed" okienka WER tabeli UD_TYP
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::  OLD: \be_udtdl/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
_fml:=exec('save_fml_all','#field',UD_TYP);

UD_TYP.fld_fml('SYMBOL','AFTER_EDIT',"
   {? exec('chron_typ','schemat',UD_TYP.SYMBOL)=1
   || UD_TYP.OCHRONA:='T'
   ?};
   1
");
UD_TYP.fld_fml('OCHRONA','BEFORE_EDIT',"
   exec('chron_typ','schemat',UD_TYP.SYMBOL)<>1
");
UD_TYP.fld_fml('SYMBOL','BEFORE_EDIT',"1");
UD_TYP.fld_fml('DLUGOSC','BEFORE_EDIT',"1");

UD_TYP.blank();
{? UD_TYP.edit("exec('ud_typ_ae','!zws_par_rsch',0)")
|| UD_TYP.add()
?};

exec('restore_fml_all','#field',UD_TYP,_fml);
~~


\ud_typ_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka WER tabeli UD_TYP
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::  OLD: \be_udtdl/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
UD_SKL.cntx_psh();
UD_SKL.index('SYMBOL');
UD_SKL.prefix(UD_TYP.ref());
_mod:=$$~UD_SKL.first();
UD_SKL.cntx_pop();

_fml:=exec('save_fml_all','#field',UD_TYP);

UD_TYP.fld_fml('SYMBOL','BEFORE_EDIT',"
   UD_TYP.SYSTEM<>'T'
");
UD_TYP.fld_fml('SYMBOL','AFTER_EDIT',"
   {? exec('chron_typ','schemat',UD_TYP.SYMBOL)=1
   || UD_TYP.OCHRONA:='T'
   ?};
   1
");
UD_TYP.fld_fml('OCHRONA','BEFORE_EDIT',"
   exec('chron_typ','schemat',UD_TYP.SYMBOL)<>1
");
UD_TYP.fld_fml('DLUGOSC','BEFORE_EDIT',_mod);

UD_TYP.get();
_ochrona:=UD_TYP.OCHRONA;
{? UD_TYP.edit("exec('ud_typ_ae','!zws_par_rsch',1)")
|| {? UD_TYP.put()
   || UD_DEF.cntx_psh();
      UD_DEF.index('TYPPOZ');
      UD_DEF.prefix(UD_TYP.ref());
      {? UD_TYP.POZIOM='N' & UD_DEF.last() & UD_DEF.UD_POZ
      || {? FUN.ask('Znaleziono struktury z określonym poziomem elementów.\n'
               'Czy usunąć informacje o poziomie elementów?'@)
         || {!
            |? UD_DEF.last() & UD_DEF.UD_POZ
            |! UD_DEF.UD_POZ:=null;
               UD_DEF.put()
            !}
         || UD_TYP.POZIOM:='T';
            UD_TYP.put()
         ?}
      |? UD_TYP.POZIOM='T' & UD_DEF.first() & UD_DEF.UD_POZ=null
      || FUN.emsg('Znaleziono struktury bez informacji o poziomie elementów.'@)
      ?};
      UD_DEF.cntx_pop();

      {? UD_TYP.OCHRONA<>_ochrona
      || UDB_SYS.cntx_psh();
         UDB_SYS.index('UD_TYP');
         UDB_SYS.prefix(UD_TYP.ref());
         {? UDB_SYS.first()
         || {? ~FUN.ask(
                  'Znaleziono wykorzystanie typu w podsystemie ochron.\n'
                  'Czy usunąć informacje o wykorzystaniu typu?'@)
            || UD_TYP.OCHRONA:='T';
               UD_TYP.put()
            ?}
         ?};
         UDB_SYS.cntx_pop()
      ?}
   ?}
?};

exec('restore_fml_all','#field',UD_TYP,_fml);
~~


\ud_typ_bu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Usuń przed" okienka WER tabeli UD_TYP
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
: sprawdź, czy typ jest "systemowy"
{? UD_TYP.SYSTEM='T'
|| FUN.emsg('Typ jest wykorzystywany w funkcjach programu.\n'
      'Usunięcie zapisu nie jest możliwe.'@);
   return()
?};
: sprawdź, czy elementy danego typu są wykorzystywane w słownikach
UD_SKL.cntx_psh();
UD_SKL.index('SYMBOL');
UD_SKL.prefix(UD_TYP.ref());
_loop:=UD_SKL.first();
{!
|? _loop
|! {? exec('symbol_in_use','schemat',UD_TYP.ref(),UD_SKL.SYMBOL)
   || FUN.emsg('Elementy są wykorzystwane w słownikach użytkownika.\n'
         'Usunięcie typu nie jest możliwe.'@);
      UD_SKL.cntx_pop();
      return()
   ?};
   _loop:=UD_SKL.next()
!};
UD_SKL.cntx_pop();

exec('ud_xxx_bu','schemat',UD_TYP)


\ud_typ_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Po edycji" tabeli UD_TYP
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::  OLD: \spr_rek/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
: sprawdź wypełnienie i unikalność
_chk:=__CHK.table(UD_TYP,_a,,'SYMBOL','OPIS');
{? type_of(_chk)<>type_of(0) | ~_chk
:  porażka podstawowego testu
|| return(_chk)
?};

: dodatkowe testy poprawności
_max:=MS.fld_len('UD_SKL','SYMBOL');
{? UD_TYP.DLUGOSC<0 | UD_TYP.DLUGOSC>_max
|| FUN.emsg('"Długość symbolu" nie może być mniejsza od 0 i większa od %1.'@[$_max]);
   return('DLUGOSC')
?};

1


\ud_sch_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Dołącz przed" okienka WER tabeli UD_SCH
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
_fml_s:=UD_SCH.fld_fml('SYMBOL','BEFORE_EDIT',"1");
_sch:=null;

UD_SCH.blank();
UD_SCH.win_edit('RED');
{? UD_SCH.edit("exec('ud_sch_ae','!zws_par_rsch',0)") & UD_SCH.add()
|| _sch:=UD_SCH.ref()
?};

_fml_s:=UD_SCH.fld_fml('SYMBOL','BEFORE_EDIT',_fml_s);

_sch


\ud_sch_sch_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Dołącz przed" okienka SCH tabeli UD_SCH
::----------------------------------------------------------------------------------------------------------------------
_sch:={? UD_SCH.UD_SCH || UD_SCH.UD_SCH || #UD_SCH.ref() ?};
_ret:=null;

UD_SCH.fld_fml('AKTYWNY','BEFORE_EDIT',"0");

UD_SCH.blank();
UD_SCH.AKTYWNY:='N';
UD_SCH.UD_SCH:=_sch;
UD_SCH.REKORD:=_sch;
UD_SCH.win_edit('SCH');
{? UD_SCH.edit("exec('ud_sch_sch_ae','!zws_par_rsch',0)")
|| UD_SCH.add();
   _ret:=UD_SCH.ref()
?};

UD_SCH.fld_fml('AKTYWNY','BEFORE_EDIT',"*");

_ret


\ud_sch_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka WER tabeli UD_SCH
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
_fml_s:=UD_SCH.fld_fml('SYMBOL','BEFORE_EDIT',"UD_SCH.SYSTEM<>'T'");

UD_SCH.get();
UD_SCH.win_edit('RED');
{? UD_SCH.edit("exec('ud_sch_ae','!zws_par_rsch',1)")
|| UD_SCH.put()
?};

_fml_s:=UD_SCH.fld_fml('SYMBOL','BEFORE_EDIT',_fml_s);
~~


\ud_sch_sch_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka SCH tabeli UD_SCH
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.cntx_psh();
UD_SCH.index('DATA');
UD_SCH.prefix(UD_SCH.REKORD);
{? UD_SCH.size()=1 | UD_SCH.AKTYWNY='T'
|| UD_SCH.fld_fml('AKTYWNY','BEFORE_EDIT',"0")
?};
UD_SCH.cntx_pop();

UD_SCH.get();
UD_SCH.win_edit('SCH');
{? UD_SCH.edit("exec('ud_sch_sch_ae','!zws_par_rsch',1)")
|| UD_SCH.put()
?};

UD_SCH.fld_fml('AKTYWNY','BEFORE_EDIT',"*");
~~


\ud_sch_bu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Usuń przed" okienka WER tabeli UD_SCH
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
: sprawdź, czy schemat jest "systemowy"
{? UD_SCH.SYSTEM='T'
|| FUN.emsg('Schemat jest wykorzystywany w funkcjach programu.\n'
      'Usunięcie zapisu nie jest możliwe.'@);
   return()
?};
exec('ud_xxx_bu','schemat',UD_SCH)


\ud_sch_sch_bu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Usuń przed" okienka SCH tabeli UD_SCH
::----------------------------------------------------------------------------------------------------------------------
exec('ud_xxx_bu','schemat',UD_SCH)


\ud_sch_bk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Kopiuj przed" okienka WER tabeli UD_SCH
::----------------------------------------------------------------------------------------------------------------------
_org:=UD_SCH.ref();
_new:=exec('ud_sch_bd','!zws_par_rsch');

{? _org & _new
|| exec('ud_sch_kopiuj','!zws_par_rsch',_org,_new)
?}


\ud_sch_sch_bk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Kopiuj przed" okienka SCH tabeli UD_SCH
::----------------------------------------------------------------------------------------------------------------------
_org:=UD_SCH.ref();
_new:=exec('ud_sch_sch_bd','!zws_par_rsch');

{? _org & _new
|| exec('ud_sch_kopiuj','!zws_par_rsch',_org,_new)
?}


\ud_sch_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Kopiuje definicję schematu z jednego schematu do drugiego (nowego, pustego)
::   WE: _a - schemat zawierający źródłową strukturę
::       _b - docelowy, pusty schemat
::----------------------------------------------------------------------------------------------------------------------
: skopiuj poziomy źródłowego schematu
: utwórz "mapę" oryginał->kopia
_POZ:=tab_tmp(1,
   'ORG','INTEGER','Źródłowy',
   'NEW','INTEGER','Docelowy'
);
UD_POZ.cntx_psh();
UD_POZ.clear();
UD_POZ.f_set('NUMER',,'UD_POZ.UD_SCH=:_a',_a);
_loop:=UD_POZ.f_first();
{!
|? _loop
|! _POZ.blank(1);
   _POZ.ORG:=UD_POZ.ref();
   UD_POZ.UD_SCH:=_b;
   {? UD_POZ.add()
   || _POZ.NEW:=UD_POZ.ref();
      _POZ.add()
   ?};
   _loop:=UD_POZ.f_next()
!};
UD_POZ.f_clear();
UD_POZ.cntx_pop();

: formułka kopiowania definicji schematu
: _a - wskazanie oryginalnego schematu
: _b - węzeł w definicji oryginalnej
: _c - wskazanie schematu pochodnego
: _d - węzeł w definicji popchodnej
: _e - formuła kopiująca
: _f - mapa poziomów
_copy:="
   UD_DEF.prefix(_a,_b);
   _loop:=UD_DEF.first();
   {!
   |? _loop
   |! {? UD_DEF.UD_DEF=_b
      || _ref:=UD_DEF.ref();
         _poz:=UD_DEF.UD_POZ;
         UD_DEF.cntx_psh();
         UD_DEF.UD_SCH:=_c;
         UD_DEF.UD_DEF:=_d;
         {? _f.find_key(#_poz) & UD_POZ.seek(_f.NEW,)
         || UD_DEF.UD_POZ:=UD_POZ.ref()
         ?};
         UD_DEF.prefix();
         {? UD_DEF.add()
         || _e(_a,_ref,_c,UD_DEF.ref(),_e,_f)
         ?};
         UD_DEF.cntx_pop()
      ?};
      _loop:=UD_DEF.next()
   !}
";

: rozpocznij kopiowanie
UD_POZ.cntx_psh();
UD_POZ.clear();
UD_DEF.cntx_psh();
UD_DEF.index('SYMBOL');
_copy(_a,0,_b,0,_copy,_POZ);
UD_DEF.cntx_pop();
UD_POZ.cntx_pop();
~~


\ud_sch_wersje_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Obsługa akcji "Wersje" w oknie WER tabeli UD_SCH.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);

_typ:=UD_SCH.UD_TYP;
_sym:=UD_SCH.SYMBOL;
_sch:=#UD_SCH.ref();
_rec:=UD_SCH.REKORD;

_CUR:=_par.cntx.CURRENT;
_CUR.prefix();
_test:=_CUR.find_key(_sch);

UD_SCH.cntx_psh();
UD_SCH.index('DATA');
UD_SCH.prefix(UD_SCH.REKORD);
UD_SCH.win_sel('SCH');

{? _test<>0 & UD_SCH.seek(_CUR.WER,,1)
|| {? UD_SCH.select(,1)
   || _CUR.WER:=#UD_SCH.ref();
      _CUR.put()
   ?}
|| UD_SCH.select()
?};
{? _test<>0 &
   UD_SCH.find_tab(,
      'UD_TYP',,'=',_typ,
      'REKORD',,'=',_rec,
      'AKTYWNY',,'=','T'
   ) &
   _CUR.SCH<>#UD_SCH.ref()
|| _CUR.SCH:=#UD_SCH.ref();
   _CUR.put()
?};

UD_SCH.cntx_pop();
UD_SCH.find_tab(,
   'UD_TYP',,'=',_typ,
   'SYMBOL',,'=',_sym
);
~~


\ud_sch_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Po edycji" tabeli UD_SCH
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::  OLD: \spr_rek/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(UD_SCH,_a,,'SYMBOL','OPIS')


\ud_sch_sch_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Po edycji" tabeli UD_SCH w oknie SCH
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::  OLD: \spr_rek/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(UD_SCH,,'DATA','UWAGI')


\ud_poz_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Dołącz przed" okienka WER tabeli UD_POZ
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POZ.blank();
{? UD_POZ.edit("exec('ud_poz_ae','!zws_par_rsch',0)")
|| UD_POZ.add()
?};
~~


\ud_poz_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka WER tabeli UD_POZ
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POZ.get();
{? UD_POZ.edit("exec('ud_poz_ae','!zws_par_rsch',1)")
|| UD_POZ.put()
?};
~~


\ud_poz_bu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Usuń przed" okienka WER tabeli UD_POZ
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ud_xxx_bu','schemat',UD_POZ)


\ud_poz_bu_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuń okienka tabeli UD_POZ
::----------------------------------------------------------------------------------------------------------------------
{? UD_POZ.count()=0
|| UD_POZ.web_cntx_save();
   _us:="{? _a
         || UD_POZ.web_cntx_load();
            UD_POZ.del();
            UD_POZ.web_refresh(web_top_win(0))
         ?}";
   web_ask(_us,'Usunąć wybrany poziom?'@,FUN.TYT)
|| FUN.info('Wybrany poziom posiada powiązania.'@)
?}


\ud_poz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Po edycji" tabeli UD_POZ
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::  OLD: \spr_rek/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(UD_POZ,_a,,'NUMER','OPIS')


\ud_def_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Okienko przed" okienek wertowania tabeli UD_DEF.
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.hdr_sel();
UD_DEF.hdr_sel(UD_SCH.OPIS);
1


\ud_def_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Przed edycją" (dołącz/popraw/...) okienek tabeli UD_DEF.
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.hdr_edit();
UD_DEF.win_edit({? UD_TYP.POZIOM='T' || 'RED_POZ' || 'RED_SMP' ?});
UD_DEF.hdr_edit();
UD_DEF.hdr_edit(UD_SCH.OPIS);
UD_DEF.win_patt(UD_DEF.win_edit('?'));
UD_SKL.win_dict('WER');
1


\ud_def_ad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Dołącz po" okienka WER tabeli UD_DEF
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
{? UD_SCH.SYMBOL='STR_PRAC' & UD_SCH.UD_TYP().SYMBOL='STANPRAC'
|| exec('dod_prac','control')
|| _ref:=UD_DEF.ref();
   UD_DEF.blank();
   _par:=params_get();
   {? type_of(_par)>100 &
      var_pres('cntx',_par)>100 &
      var_pres('CURRENT',_par.cntx)=type_of(SYSLOG)
   || UD_SCH.cntx_psh();
      _CUR:=_par.cntx.CURRENT;
      _CUR.prefix(#UD_DEF.UD_SCH);
      {? _CUR.first() & UD_SCH.seek(_CUR.WER,,1)
      || UD_DEF.UD_SCH:=UD_SCH.ref()
      ?};
      UD_SCH.cntx_pop()
   ?};
   {? UD_DEF.edit("exec('ud_def_ae','!zws_par_rsch',0)")
   || UD_DEF.add()
   ?};
   UD_DEF.seek(_ref)
?};
~~


\ud_def_ad_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie rekordów w tabeli UD_DEF - WEBTERM
::----------------------------------------------------------------------------------------------------------------------
web_params_set(web_params_get());
exec('env_wt','b_proces','ud_def_add');
UD_SCH.web_cntx_load(0); UD_SCH.UD_TYP();
_okn_typ:={? UD_TYP.POZIOM='T' || 'RED_POZW' || 'RED_SMPW' ?};
UD_DEF.web_efld_init(_okn_typ,,'win_dict=WER_WT',,'UD_SKL');
UD_DEF.web_efld_init(_okn_typ,,'win_dict=WER_WT',,'UD_POZ');
UD_DEF.web_edit(_okn_typ,,,,
                "{? _a='OK'
                 || {? exec('ud_def_ae_wt','!zws_par_rsch',0)=''
                    || {? UD_DEF.add()
                       || UD_DEF.web_eclose();
                          UD_DEF.web_refresh(web_top_win(1))
                       ?}
                    ?}
                 || UD_DEF.web_eclose()
                 ?}");
~~


\ud_def_ae_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: "Po edycji" tabeli UD_DEF
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::----------------------------------------------------------------------------------------------------------------------
{? ~_a & UD_POM.GLOWNY<>'T' & UD_DEF.size()
|| UD_DEF.UD_DEF:=#UD_DEF.ref()
?};
{? ~UD_DEF.UD_SKL
|| FUN.info('Nie wybrano elementu.'@); 'UD_SKL'
|? UD_DEF.UD_SCH().UD_TYP().POZIOM='T' & ~UD_DEF.UD_POZ
|| FUN.info('Nie wybrano poziomu.'@); 'UD_POZ'
|| _chk:=__CHK.index(UD_DEF,_a,0);
   {? _chk<>'' || FUN.info('Znaleziono rekord o podanym kluczu.'@) ?};
   _chk
?}


\ud_def_ap_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie rekordów w tabeli UD_DEF - WEBTERM
::----------------------------------------------------------------------------------------------------------------------
web_params_set(web_params_get());
exec('env_wt','b_proces','ud_def_pop');
UD_SCH.web_cntx_load(0); UD_SCH.UD_TYP();
_okn_typ:={? UD_TYP.POZIOM='T' || 'RED_POZW' || 'RED_SMPW' ?};
UD_DEF.web_efld_init(_okn_typ,,'win_dict=WER_WT',,'UD_SKL');
UD_DEF.web_efld_init(_okn_typ,,'win_dict=WER_WT',,'UD_POZ');
UD_DEF.web_edit(_okn_typ,,,,
                "{? _a='OK'
                 || {? exec('ud_def_ae_wt','!zws_par_rsch',1)=''
                    || {? UD_DEF.put()
                       || UD_DEF.web_eclose();
                          UD_DEF.web_refresh(web_top_win(1))
                       ?}
                    ?}
                 || UD_DEF.web_eclose()
                 ?}");
~~


\ud_def_ap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw po" okienka WER tabeli UD_DEF
::  OLD: \menu_ba/skid_ud.fml
::  OLD: \menu_aa/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.get();
{? UD_DEF.edit("exec('ud_def_ae','!zws_par_rsch',1)")
|| UD_DEF.put()
?};
~~


\ud_def_au_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Usuń dla tabeli UD_DEF
::----------------------------------------------------------------------------------------------------------------------
web_params_set(web_params_get());
exec('env_wt','b_proces','ud_def_del');


_pod:=0;
UD_DEF.cntx_psh(); UD_DEF.index('SYMBOL');
UD_DEF.prefix(UD_DEF.UD_SCH,#UD_DEF.ref());
_pod:=UD_DEF.first();
UD_DEF.cntx_pop();

{? _pod
|| UD_DEF.web_cntx_save();
   _us:="UD_DEF.web_cntx_load();
      {? _a=0
      || _ud_def:=UD_DEF.UD_DEF;
         UD_DEF.cntx_psh();
         UD_DEF.clear();
         UD_DEF.f_set(,,'UD_DEF.UD_DEF=:_a',#UD_DEF.ref());
         _loop:=UD_DEF.f_first();
         do();
         {!
         |? _loop & _del
         |! UD_DEF.UD_DEF:=_ud_def;
            _del:=UD_DEF.put();
            _loop:=UD_DEF.f_next()
         !};
         UD_DEF.f_clear();
         UD_DEF.cntx_pop();
         end();
         _del
      |? _a=1
      || {? ~_pow
         || {? exec('ud_def_skl_pow','schemat',1)<0
            || return(-1)
            ?}
         ?};
         FUN.ask('\nOperacja usunięcia informacji jest nieodwracalna.'
            '\nCzy na pewno usunąć wiersz i powiązane informacje?\n'@)
      ?}
   ";
   web_choice(_us,'Usunąć bieżący wiersz?',,'ASK',2,3,
             'Pozostaw podrzędne'@,'Usuń podrzędne'@,'Rezygnacja')
|| UD_DEF.web_cntx_save();
   _us:="{? _a
         || UD_DEF.web_cntx_load();
            UD_DEF.del();
            UD_DEF.web_refresh(web_top_win(0))
         ?}";
   web_ask(_us,'Usunąć bieżący wiersz?'@,FUN.TYT)
?}


\ud_poz_bd_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołącz okienka tabeli UD_POZ
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.web_cntx_load(0); UD_SCH.UD_TYP();
UD_POZ.blank();
UD_POZ.web_edit('RED_WT',,,,
                "{? _a='OK'
                 || {? exec('ud_poz_ae_wt','!zws_par_rsch',0)=''
                    || UD_POZ.add();
                       UD_POZ.web_eclose();
                       UD_POZ.web_refresh(web_top_win(1))
                    ?}
                 || UD_POZ.web_eclose()
                 ?}");
~~


\ud_poz_ae_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: "Po edycji" tabeli UD_POZ
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::----------------------------------------------------------------------------------------------------------------------
{? ~UD_POZ.NUMER
|| FUN.info('Nie wprowadzono numeru poziomu.'@); 'NUMER'
|? UD_POZ.OPIS=''
|| FUN.info('Nie wprowadzono opisu poziomu.'@); 'UD_POZ'
|| _chk:=__CHK.index(UD_POZ,_a,0);
   {? _chk<>'' || FUN.info('Znaleziono rekord o podanym kluczu.'@) ?};
   _chk
?}


\ud_poz_bp_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka tabeli UD_POZ
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.web_cntx_load(0); UD_SCH.UD_TYP();
UD_POZ.get();
UD_POZ.web_edit('RED_WT',,,,
                "{? _a='OK'
                 || {? exec('ud_poz_ae_wt','!zws_par_rsch',1)=''
                    || UD_POZ.put();
                       UD_POZ.web_eclose();
                       UD_POZ.web_refresh(web_top_win(1))
                    ?}
                 || UD_POZ.web_eclose()
                 ?}");
~~


\bobs_ud_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą okienek tabeli UD_POZ
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.web_cntx_load(0); UD_SCH.UD_TYP();
1


\ud_def_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Po edycji" tabeli UD_DEF
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::  OLD: \spr_rek/skid_ud.fml
::----------------------------------------------------------------------------------------------------------------------
UD_TYP.cntx_psh();
UD_SCH.cntx_psh();

{? ~_a & UD_POM.GLOWNY<>'T' & UD_DEF.size()
|| UD_DEF.UD_DEF:=#UD_DEF.ref()
?};
{? UD_TYP.SYMBOL='PODZORG' & (UD_SCH.SYMBOL='STRORG' | UD_SCH.DOMYSLNY='T')
|| {? ~exec('test_jk_dla_jo','schemat',UD_DEF.UD_SKL,UD_DEF.name(),#UD_DEF.ref(),0)
   || UD_SCH.cntx_pop();
      UD_TYP.cntx_pop();
      return(0)
   ?}
?};

_ret:=
   {? UD_DEF.UD_SCH().UD_TYP().POZIOM='T'
   || __CHK.table(UD_DEF,_a,,'UD_SKL','UD_POZ')
   || __CHK.table(UD_DEF,_a,,'UD_SKL')
   ?};

UD_SCH.cntx_pop();
UD_TYP.cntx_pop();

_ret


\aktywny_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wyświetleniem pola AKTYWNY zmiennej UD_POM. Prezentacja znacznika aktywności elementu.
::----------------------------------------------------------------------------------------------------------------------
UD_POM.AKTYWNY:=UD_DEF.UD_SKL().AKTYWNY;
1


\udb_grp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Dołącz przed" okienka WER tabeli UDB_GRP
::----------------------------------------------------------------------------------------------------------------------
UDB_GRP.blank();
{? UDB_GRP.edit("exec('udb_grp_ae','!zws_par_rsch',0)")
|| UDB_GRP.add()
?};
~~


\udb_grp_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka WER tabeli UDB_GRP
::----------------------------------------------------------------------------------------------------------------------
UDB_GRP.get();
{? UDB_GRP.edit("exec('udb_grp_ae','!zws_par_rsch',1)")
|| UDB_GRP.put()
?};
~~


\udb_grp_bu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Usuń przed" okienka WER tabeli UDB_GRP
::----------------------------------------------------------------------------------------------------------------------
exec('ud_xxx_bu','schemat',UDB_GRP)


\udb_grp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Po edycji" tabeli UDB_GRP
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(UDB_GRP,_a)


\udb_dom_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Dołącz przed" okienka WER tabeli UDB_DOM
::----------------------------------------------------------------------------------------------------------------------
exec('udb_dom_filtr_set','!zws_par_rsch');
_BUF:=exec('wybierz_wiele','#b_domain');
exec('udb_dom_filtr_clr','!zws_par_rsch');

B_DOMAIN.cntx_psh();
_loop:=_BUF.first();
{!
|? _loop
|! UDB_DOM.blank();
   {? B_DOMAIN.seek(_BUF.REF,,1)
   || UDB_DOM.B_DOMAIN:=B_DOMAIN.ref();
      UDB_DOM.add()
   ?};
   _loop:=_BUF.next()
!};
B_DOMAIN.cntx_pop();
~~


\udb_dom_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka WER tabeli UDB_DOM
::----------------------------------------------------------------------------------------------------------------------
UDB_DOM.get();
exec('udb_dom_filtr_set','!zws_par_rsch',,UDB_DOM.B_DOMAIN);
_buf:=exec('wybierz','#b_domain',UDB_DOM.B_DOMAIN().SYMBOL);
{? _buf.Ref<>null
|| UDB_DOM.B_DOMAIN:=_buf.Ref;
   UDB_DOM.put()
?};
exec('udb_dom_filtr_clr','!zws_par_rsch');
~~


\udb_dom_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Szukaj przed" okienka WER tabeli UDB_DOM
::----------------------------------------------------------------------------------------------------------------------
exec('udb_dom_filtr_set','!zws_par_rsch',1);
1


\udb_dom_filtr_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustawienie filtra na obszary przed rozpoczęciem edycji danych w tabeli UDB_DOM.
::   WE: [_a] [INTEGER] - uwzględniaj/pomijaj* dziedziny dodane do UDB_DOM
::       [_b] [_B_DOMAIN] - rekord dodatkowo uwzględniany w filtrze
::----------------------------------------------------------------------------------------------------------------------
_in:={? var_pres('_a')<>type_of(0) | ~_a || 'not' || '' ?};
B_DOMAIN.prefix();
B_DOMAIN.f_set(
   'SYMBOL',,
   'B_DOMAIN.REFERENCE %1 in ('
   '  select UDB_DOM.B_DOMAIN from UDB_DOM'
   ')'[_in]
);
{? var_pres('_b')=type_of(null) &
   B_DOMAIN.seek(_b,,1)
|| B_DOMAIN.f_add()
?};
~~


\udb_dom_filtr_clr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przywrócenie stanu tabeli B_DOMAIN po zakończeniu edycji danych w tabeli UDB_DOM.
::----------------------------------------------------------------------------------------------------------------------
B_DOMAIN.f_clear();
~~


\udb_dom_bu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Usuń przed" okienka WER tabeli UDB_DOM
::----------------------------------------------------------------------------------------------------------------------
exec('ud_xxx_bu','schemat',UDB_DOM)


\udb_dom_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Po edycji" tabeli UDB_DOM
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(UDB_DOM,_a)


\udb_sys_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka WER tabeli UDB_SYS
::----------------------------------------------------------------------------------------------------------------------
UDB_SYS.blank();
{? UDB_SYS.edit("exec('udb_sys_ae','!zws_par_rsch',0)")
|| UDB_SYS.add()
?};
~~


\udb_sys_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienka WER tabeli UDB_SYS
::----------------------------------------------------------------------------------------------------------------------
UDB_SYS.get();
{? UDB_SYS.edit("exec('udb_sys_ae','!zws_par_rsch',1)")
|| UDB_SYS.put()
?};
~~


\udb_sys_bu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Usuń przed" okienka WER tabeli UDB_SYS
::----------------------------------------------------------------------------------------------------------------------
exec('ud_xxx_bu','schemat',UDB_SYS)


\udb_sys_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Po edycji" tabeli UDB_SYS
::   WE: _a - tryb modyfikacji: 0 - dodawanie, 1 - poprawianie
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(UDB_SYS,_a)


\udb_sys_odswiez_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Aktualizuje uprawnienia dla bieżącego wykorzystania.
::----------------------------------------------------------------------------------------------------------------------
exec('upr_sys_add','schemat',UDB_SYS.ref());
FUN.info('Zakończono aktualizację uprawnień.'@);
~~


\adm_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wypełnieniem okienka ADM tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
UD_TYP.cntx_psh();
UD_TYP.win_edit('RED');
UD_TYP.index('SYMBOL');
UD_TYP.prefix();

UD_SCH.cntx_psh();
UD_SCH.win_edit('RED');
UD_SCH.index('SYMBOL');

UD_DEF.cntx_psh();
UD_DEF.index('SYMBOL');

UD_POZ.cntx_psh();
UD_POZ.win_edit('RED');

UD_SKL.cntx_psh();
UD_SKL.win_edit('RED');

UDB_GRP.cntx_psh();
UDB_GRP.index('SYMBOL');
UDB_GRP.win_edit('RED');

UDB_DOM.cntx_psh();
UDB_DOM.index('SYMBOL');
UDB_DOM.win_edit('RED');

UDB_SYS.cntx_psh();
UDB_SYS.index('TYP_SYM');
UDB_SYS.win_edit('RED');

B_DOMAIN.cntx_psh();
B_DOMAIN.win_dict('SLO');

1


\adm_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przy zamykaniu okienka ADM tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
exec('udb_dom_filtr_clr','!zws_par_rsch');

UD_TYP.fld_fml('OCHRONA','BLANK',"*");

UD_TYP.cntx_pop();
UD_SCH.cntx_pop();
UD_DEF.cntx_pop();
UD_POZ.cntx_pop();
UD_SKL.cntx_pop();

UDB_GRP.cntx_pop();
UDB_DOM.cntx_pop();
UDB_SYS.cntx_pop();

B_DOMAIN.cntx_pop();

UD_DEF.hdr_sel();

1


\adm_typ_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli UD_TYP w okienku ADM tabeli UD_TYP
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| UD_TYP.fld_fml('OCHRONA','BLANK',"'N'")
?}


\adm_typ_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka WER tabeli UD_TYP w okienku ADM tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

UD_TYP.actions_grayed('WER',{? UD_TYP.SYSTEM='T' || 'U' || '' ?});

grp_disp(UD_SCH,'WER',1,1);
~~


\adm_sch_1_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli UD_SCH w okienku ADM tabeli UD_TYP
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
_jest:={? UD_TYP.f_active() || UD_TYP.f_size() || UD_TYP.size() ?};
UD_SCH.actions('WER',{? _jest || '' || 'D:D' ?},,1);

UD_SCH.index('AKTYWNY');
UD_SCH.prefix({? _jest || UD_TYP.ref() || null ?},'T')


\adm_sch_1_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okienka WER tabeli UD_SCH w okienku ADM tabeli UD_TYP
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
~~


\adm_sch_1_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka WER tabeli UD_SCH w okienku ADM tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(params_get());

_typ:=UD_SCH.UD_TYP;
_sch:=#UD_SCH.ref();
_rec:=UD_SCH.REKORD;
_CUR:=_par.cntx.CURRENT;
_CUR.prefix(_sch);
{? ~_CUR.first()
|| UD_SCH.cntx_psh();
   UD_SCH.clear();
   {? UD_SCH.find_tab(,
         'UD_TYP',,'=',_typ,
         'REKORD',,'=',_rec,
         'AKTYWNY',,'=','T'
      )
   || _CUR.SCH:=_sch;
      _CUR.WER:=#UD_SCH.ref();
      _CUR.add()
   ?};
   UD_SCH.cntx_pop()
?};

{? _par.cntx.M4
|| grp_disp(UD_DEF,'M4_WER',1,1)
|| grp_disp(UD_DEF,'WER',1,1)
?};

:: UD_SCH.cntx_psh();
:: grp_disp(UD_SCH,'SCH',1,1);
:: UD_SCH.cntx_pop();
~~


\adm_sch_2_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka SCH tabeli UD_SCH w okienku ADM tabeli UD_TYP
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
_jest:={? UD_TYP.f_active() || UD_TYP.f_size() || UD_TYP.size() ?};
UD_SCH.actions('SCH',{? _jest || '' || 'D:D' ?},,1);

UD_SCH.index('DATA');
UD_SCH.prefix(params_get().cntx.REKORD)


\adm_sch_2_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okienka SCH tabeli UD_SCH w okienku ADM tabeli UD_TYP
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
~~


\adm_sch_2_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka SCH tabeli UD_SCH w okienku ADM tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.actions_grayed('SCH',{? UD_SCH.AKTYWNY='T' || 'U' || '' ?});
{? params_get().cntx.M4
|| grp_disp(UD_DEF,'M4_WER',1,1)
|| grp_disp(UD_DEF,'WER',1,1)
?}


\adm_def_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli UD_DEF w okienku ADM tabeli UD_TYP
::   WE:  _a  - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::       [_b] - przed obsługą [1]-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_grp:={? var_pres('_b')>0 || _b || 1 ?};
{? _grp & grp_empty(UD_SCH,'WER',1)
|| return('#disable')
?};

_par:=params_get();
params_set(_par);

UD_SCH.cntx_psh();
{? _grp & var_pres('_par')>0
|| _CUR:=_par.cntx.CURRENT;
   _CUR.prefix(#UD_SCH.ref());
   {? ~_CUR.first() | ~UD_SCH.seek(_CUR.WER,UD_SCH.name(),1)
   || UD_SCH.cntx_pop();
      return('#disable')
   ?}
?};

{? var_pres('_par')>0 & _par.cntx.M4
|| _act:=':';
   {? UD_SCH.ref()=XINFO.MSCHKLPR
   || _act:='DJP'+_act+'DJ'
   |? UD_SCH.ref()=XINFO.MSCHWIEL
   || _act:='TG'+_act+'GT'
   |? UD_SCH.ref()=XINFO.MSCHMKLP
   || _act:='DJPUTM'+_act+'DJT'
   |?  UD_SCH.ref()=XINFO.MSCHKLI | UD_SCH.ref()=XINFO.MSCHPROD
   || _act:='TGM'+_act+'TG'
   || _act:='TGM'+_act+'GT'
   ?};
   {? UD_SCH.ref()<>XINFO.MSCHWIEL || _act:='Y'+_act ?};
   UD_DEF.actions('M4_WER',_act,,1)

|| _act:=':';
   {? UD_SCH.ref()=XINFO.MSCHKLPR | UD_SCH.ref()=XINFO.MSCHMKLP |
      UD_SCH.ref()=XINFO.MSCHKLI | UD_SCH.ref()=XINFO.MSCHPROD
   || _act:='DJPU'+_act+'DJ'
   ?};
   UD_DEF.actions('WER',_act,,1)
?};

UD_DEF.hdr_sel();
UD_DEF.hdr_sel(' - %1 (%2)'[UD_SCH.UWAGI,$UD_SCH.DATA]);

{? _a
|| _fml:="";
   {? UD_SCH.BLOKADA<>'T'
   || _fml:="exec('ud_def_dnd','schemat')"
   ?};
   {? var_pres('_par')>0 & _par.cntx.M4
   || UD_DEF.dnd_sel('M4_WER',,'records.UD_DEF',_fml)
   || UD_DEF.dnd_sel('WER',,'records.UD_DEF',_fml)
   ?}
|| UD_DEF.prefix(UD_SCH.ref())
?};

UD_SCH.cntx_pop();

_act


\adm_def_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka WER tabeli UD_DEF w okienku ADM tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
_act:=
   {? ~exec('dom_empty','#table',UD_SCH)
   || {? UD_SCH.BLOKADA='T'
      || 'DPUE:D'
      || ''
      ?}
   || 'D:D'
   ?};
UD_DEF.actions_grayed('WER',_act)


\adm_grp_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka WER tabeli UDB_GRP w okienku ADM tabeli UD_TYP
::----------------------------------------------------------------------------------------------------------------------
grp_disp(UDB_DOM,'WER',1,1);
grp_disp(UDB_SYS,'WER',1,1)


\adm_dom_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli UDB_DOM w okienku ADM tabeli UD_TYP
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
_jest:=~exec('dom_empty','#table',UDB_GRP);
UDB_DOM.actions('WER',{? _jest || '' || 'D:D' ?},,1);
{? _a
|| exec('udb_dom_filtr_set','!zws_par_rsch')
|| UDB_DOM.prefix({? _jest || UDB_GRP.ref() || null ?})
?}


\adm_dom_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okienka WER tabeli UDB_DOM w okienku ADM tabeli UD_TYP
::   WE: _a - wartość przekazywana do formuły "po obsłudze" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| exec('udb_dom_filtr_clr','!zws_par_rsch')
?};
~~


\adm_sys_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli UDB_SYS w okienku ADM tabeli UD_TYP
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
_jest:=~exec('dom_empty','#table',UDB_GRP);
UDB_SYS.actions('WER',{? _jest || '' || 'D:D' ?},,1);
{? _a
|| UD_TYP.fld_fml('OCHRONA','BLANK',"'T'")
|| UDB_SYS.prefix({? _jest || UDB_GRP.ref() || null ?})
?}


\slo_pop_ud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akcja 'Popraw' z poziomu okienek wertowania tabeli SLO
::  OLD: \slo_pop/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? exec('test_slo','slo_slu')=0
|| FUN.info('Poprawienie pozycji słownika niemożliwe w bieżącej firmie.'@);
   return(0)
?};
SLO.SLU();
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ,) & (RS.TAB='UD_DEF' | RS.TAB='UD_SKL')
|| _slo_pop:=0; _ref_slo:=null;
   _sp:={? var_pres('slo_pop')>0 || _slo_pop:=slo_pop; 1 || 0 ?};
   _rs:={? var_pres('ref_slo')>0 || _ref_slo:=ref_slo; 1 || 0 ?};
   slo_pop:=1; ref_slo:=null; exec('slu_okn','slo_slu');
   _rs:=RS.ref();
   {? (_r_szuk:=exec('wz_szuk','slo_slu'))
   || {? RS.TAB='UD_DEF' & _r_szuk=1 || UD_DEF.UD_SKL() ?};
      _fun:={? RS.TAB='UD_DEF' || $('UD_SKL.put()') || $(RS.TAB+'.put()') ?};
      exec('cn_psh','slo_slu');
      {? exec('ud_skl_ap','schemat')
      || exec('cn_pop','slo_slu');
         {? var_pres('ref_slo')>0 & ref_slo<>null || SLO.seek(ref_slo) ?};
         do();
         _kod:=SLO.KOD; _tresc:=SLO.TR; _msg:='';
         {? _fun()
         || {? _r_szuk<>-1 & ~RS.seek(_rs)
            || undo();
               _msg:='Nie znaleziono rekordu tabeli RS.'
            || {? RS.TAB<>'UD_SKL' & RS.TAB<>'UD_DEF'
               || _msg:=exec('akt_slo','slo_slu',_kod,_tresc)
               ?}
            ?}
         ?};
         end();
         {? _msg<>'' || FUN.info(_msg) ?}
      || exec('cn_pop','slo_slu');
         {? ref_slo<>null || SLO.seek(ref_slo) ?}
      ?}
   ?};
   {? _sp || slo_pop:=_slo_pop || VAR_DEL.delete('slo_pop') ?};
   {? _rs || ref_slo:=_ref_slo || VAR_DEL.delete('ref_slo') ?}
|| FUN.info('Akcja dostępna wyłącznie dla słowników o wzorcach dotyczących struktury hierarchicznej.'@)
?};
exec('tab_f_rfresh','slo_filtr',SLO);
RS.cntx_pop();
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 6982af6e6c9aacdf10f124aeeeaf1410068939dbf9f01daa7370ee3cfe1ab1bd3c3a4f0c39cc8260b9fd8c04ea45f5e287b36f2c1eb1ffb8ad9d564eaf06ad59a737e4c4dc68e32108b8c0bfa9f168d31a56dbbfc8b6837d7a64f2f5c68772d2d04c61ceb9603c65484154201b7ce369119164833f044825540e70c7ee8d78fd

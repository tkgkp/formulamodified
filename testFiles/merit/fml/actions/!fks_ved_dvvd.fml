:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ved_dvvd.fml
:: Utworzony: 21.09.2021
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VED_DVVD - Tworzenie deklaracji VIU-DO
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Przeglądanie deklaracji VIU-DO - główna formuła czynności FKS_VED_DVVD
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# kind=WE, symbol=OKRES, type=_OKRO_F, name=Okres deklaracji, required=N, keyref=N
:: PARAMETRY WY:
::# kind=WY, symbol=DEKLARACJA, type=_VAT_DEK, name=Deklaracja VIU-DO, required=T
::# kind=WY, symbol=OKRES, type=_OKRO_F, name=Okres deklaracji, required=T

_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
vat_typ:='VIUDO';
BPMN.SYM_DOM:='FKS';
{? var_press('OKRES',_wy)>0
|| OKRO_F.prefix();
   OKRO_F.seek(_wy.OKRES)
|? var_press('OKRES',_we)>0
|| OKRO_F.prefix();
   OKRO_F.seek(_we.OKRES)
|| SSTALE.AO()
?};
_ar:=SSTALE.AR;
_ao:=SSTALE.AO;
SIK.OKRROK2:=OKRO_F.ref();
SIK.ROK2:=OKRO_F.ROK;
_r1:=SIK.ROK1;
_jest:=0;
Add:=0;
ref:=null;
VAT_DEK.cntx_psh();
_refy:=_mp.getRefs();
{? var_press('[1]',_refy)>0
|| VAT_DEK.index('UNIK');
   VAT_DEK.prefix();
   {? VAT_DEK.seek(_refy[1])
   || _jest:=1;
      Add:=1;
      ref:=VAT_DEK.ref();
      exec('set_var','fks_ved')
   || _jest:=-1
   ?}
|| exec('init','fks_ved',OKRO_F.ROK,OKRO_F.ref());
   rodzdekl:='Y'
?};
exec('set_viudo_fml','fks_viudo');
_spr:="exec('after_record','!fks_ved_dvvd')";
{? _jest=-1 | _jest=0 & _mp.pathTodo()
|| FUN.info('Nie znaleziono deklaracji VIU-DO.'@);
   _wy.DEKLARACJA:=null;
   _wy.OKRES:=null;
   _mp.save(,_wy);
   _mp.done()
|? _jest=0 & (_mp.pathProc() | _akcja='Dołącz')
|| {? exec('okres','!fks_ved_dvvd') & exec('set_vat_dek','!fks_ved_dvvd')
   || _add:="exec('add','!fks_ved_dvvd')";
      exec('set_win','fks_ved',_spr,_add);
      _mp.trigRef('VAT_DEK');
      exec('tkrs_eb','fks_viudo',1);
      {? VAT_DEK.edit(_spr)
      || {? Add=0
         || ref:=_add();
            {? ref
            || Add:=1;
               VAT_POZ.index('VAT_POZ');
               VAT_POZ.prefix(ref);
               exec('vat_dpoz','fks_ved',vat_typ);
               VAT_DEK.edit(_spr)
            ?}
         ?}
      ?};
      exec('tkrs_eb','fks_viudo',2)
   ?}
|? VAT_DEK.STATUS<>'N' & VAT_DEK.STATUS<>''
|| FUN.info('Zakończono już rejestrację deklaracji.'@)
|? exec('bevatdek','fks_ved')
|| {? _akcja='Zakończ'
   || VAT_DEK.STATUS:='R';
      VAT_DEK.put()
   || exec('ust_okn','fks_ved');
      _add:="";
      exec('set_win','fks_ved',_spr,_add);
      exec('tkrs_eb','fks_viudo',1);
      {? VAT_DEK.edit(_spr)
      || VAT_DEK.put();
         exec('vat_uzas','fks_ved')
      ?};
      exec('tkrs_eb','fks_viudo',2)
   ?};
   VAT_DEK.r_unlock()

?};
VAT_DEK.cntx_pop();
{? ref<>null
|| VAT_DEK.cntx_psh();
   VAT_DEK.prefix();
   {? VAT_DEK.seek(ref)
   || _wy.DEKLARACJA:=VAT_DEK.ref();
      _wy.OKRES:=SSTALE.AO;
      _mp.save(,_wy);
      {? VAT_DEK.STATUS<>'N'
      || _mp.done()
      ?}
   ?};
   VAT_DEK.cntx_pop();
   VAT_DEK.seek(ref)
?};
{? _ar<>SIK.ROK2 | _ao<>SIK.OKRROK2
|| SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   exec('open','fks_ved',SSTALE.AR().KOD,SSTALE.AO().NR)
?};
SIK.ROK1:=_r1;
VAR_DEL.delete('vat_ver','zaok','niedruk','pv','ref')


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Opis czynności na ToDo
::----------------------------------------------------------------------------------------------------------------------
''@@;params_exec('desc_add','fks_ved','VAT27')


\okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Umożliwia wybór okresu deklaracji
::----------------------------------------------------------------------------------------------------------------------
OkroFInd:=1;
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
ROK_F.win_dict('WER');
OKRO_F.win_dict('SLO');
SIK.win_edit('ROKOKRES');
_ok:=SIK.edit("
   _r:=__CHK.record2(SIK,'ROK2','Rok bilansowy','OKRROK2','Okres');
   {? _r=''
   || {? OKRO_F.POCZ=date(0,0,0)
      || FUN.info('W okresie %1 nie można tworzyć deklaracji.'@[OKRO_F.NAZ]); 0
      |? OKRO_F.POCZ<date(2021,7,1)
      || FUN.info('Deklarację VIU-DO można wystawiać od trzeciego kwartału 2021 roku.'@); 0
      || 1
      ?}
   || _r
   ?}
");
VAR_DEL.delete('OkroFInd');
{? _ok
|| {? SSTALE.AR<>SIK.ROK2 | SSTALE.AO<>SIK.OKRROK2
   || SSTALE.AR:=SIK.ROK2;
      SSTALE.AO:=SIK.OKRROK2;
      exec('open','fks_ved',SSTALE.AR().KOD,SSTALE.AO().NR)
   ?}
?};
_ok


\set_vat_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Przed dodaniem deklaracji - ustawienie danych
::----------------------------------------------------------------------------------------------------------------------
vat_ver:=exec('vat_ver','!fks_ved_dvvd',-vat_typ,SIK.OKRROK2().KON);
zaok:=0;
niedruk:=1;
_dekl:=OKRO_F.ROK;
_dekk:=OKRO_F.ROK().KOD;
_dekp:=ROK_F.POCZ_ROK;
_rodz:='Y';
_wer:=1;
_ref:=null;
_dalej:=1;
{? exec('czy_ks','fks_vat')
|| _v:=#((7+$SSTALE.AO().KON)+2);
   _kw:={? _v<=3 || 1 |? _v<=6 || 2 |? _v<=9 || 3 || 4 ?};
   _okres:=(4+$SSTALE.AO().KON)+'/'+$_kw;
   _rok:=#(4+_okres);
   _mies:=#(5-_okres)*3;
   OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA,date(_rok,_mies,0));
   _dekl:={? OKRO_F.first
          || OKRO_F.ROK
          || FUN.info('Należy dołączyć nowy rok bilansowy'
                 '\n z powodu braku końca kwartału w danych.'@);
             _dalej:=0
          ?};
   _dekk:=OKRO_F.ROK().KOD;
   _dekp:=ROK_F.POCZ_ROK;

   {? _dalej
   || _is_vat:=0;
      VAT_DEK.index('VAT_DEK');
      VAT_DEK.prefix(_dekp,vat_typ,_okres);
      _is_vat:=VAT_DEK.last();
      {? _is_vat
      || _ok:=FUN.choice(
            'Istnieje już deklaracja na okres %1'@[_okres],2,
            'Usunąć ostatnią'@,'utworzyć Nową'@
         );
         _wer:=VAT_DEK.WER;
         {? _ok=2
         || _typ:=vat_typ;
            VAT_DEK.cntx_psh();
            VAT_DEK.index('VAT_DEKR');
            VAT_DEK.prefix(_rodz,_dekp,_okres,_typ,);
            _wer:={? VAT_DEK.last() || VAT_DEK.WER || 0 ?};
            VAT_DEK.cntx_pop()
         ?};
         {? _ok=1
         || {? vat_typ<>VAT_DEK.TYP
            || _wer+=1
            ?};
            _ok:=exec('vat_usu','fks_ved',1)
         || _wer+=1
         ?}
      || _ok:=2
      ?};
      {? _ok>0
      || VAT_DEK.prefix(_dekp);
         VAT_DEK.ROK:=_dekl;
         VAT_DEK.RODZAJ:=_rodz;
         VAT_DEK.TYP:=SKID.DEKL_NAZ:=vat_typ;
         VAT_DEK.OKRES:=_okres;
         _nr_okr:=#(_okres+1);
         _mc:=(#(_okres+1)-1)*3+1;
         _d:=date(#(4+_okres),_mc,0);
         VAT_VER.index('VER_OD'); VAT_VER.prefix(BPMN.SYM_DOM,SKID.DEKL_NAZ,SKID.DEKL_NAZ);
         VAT_DEK.NR:={? SKID.DEKL_NAZ<>'' & VAT_VER.find_le(_d) || VAT_VER.ref() || null ?};
         VAT_DEK.WER:=_wer;
         VAT_DEK.win_edit('VIUDO');
         VAT_DEK.DATA:=date();
         {? VAT_DEK.DATA<date(2021,10,1)
         || VAT_DEK.DATA:=date(2021,10,1)
         ?};
         VAT_DEK.D1:=date(_rok,_kw*3,0);
         POMOC.WAL_TKRS:=exec('FindInSet','#table','SLO','SL','EUR',FINFO.SLWAL().SLU,,1);
         VAT_DEK.BANK:=FINFO.B_VIUDO;
         VAT_DEK.TKRS:=null;
         {? VAT_DEK.BANK
         || exec('after_edit','fks_viudo','BANK')
         ?};
         VAT_DEK.KOR:='2';
         VAT_DEK.OPIS:='Deklaracja VIU-DO';
         1
      ?}
   ?}
?}


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Dodaje deklarację i jej pozycje
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
VAT_DEK.prefix();
VAT_DEK.STATUS:='N';
{? VAT_DEK.add()
|| _ref:=VAT_DEK.ref();
   exec('vat_uzas','fks_ved');
   _ok:=exec('utw_viudo','!fks_ved_dvvd')
?};
_ref


\akc_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Formula Dolacz dla okienka wert. tabeli VAT_DEK
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_VED_DVVD';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRES',SSTALE.AO);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\akc_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Akcja poprawiania deklaracji VIU-DO
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('can_edit2','fks_ved')
|| 0
|? exec('bevatdek','fks_ved')
|| exec('ust_okn','fks_ved');
   exec('tkrs_eb','fks_viudo',1);
   {? VAT_DEK.edit("exec('after_record','!fks_ved_dvvd')")
   || VAT_DEK.put()
   ?};
   exec('tkrs_eb','fks_viudo',2);
   VAT_DEK.r_unlock()
?}


\akc_cel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Akcja zmiana celu złożenia deklaracji VIU-DO
::----------------------------------------------------------------------------------------------------------------------
_put:=0;
{? VAT_DEK.KOR='1'
|| {? FUN.ask('Zmienić cel złożenia deklaracji na \'Wersja robocza\'?'@)
   || VAT_DEK.KOR:='2';
      _put:=1
   ?}
|| {? FUN.ask('Zmienić cel złożenia deklaracji na \'Złożenie\'?'@)
   || VAT_DEK.KOR:='1';
      _put:=1
   ?}
?};
{? _put
|| VAT_DEK.put()
?}


\vat_ver
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: ustala numer wersji deklaracji przy tworzeniu
::   WE: _a - typ deklaracji VAT7
::----------------------------------------------------------------------------------------------------------------------
1


\after_record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Formula po redakcji okienka VAT_DEK
::----------------------------------------------------------------------------------------------------------------------
_ret:=__CHK.record(VAT_DEK,,'NR','D1','BANK','TKRS');
{? _ret='' & exec('empty_krs','!fks_ved_dvvd')
|| FUN.info('Tabela kursów zawiera zerowe kursy średnie walut.'@);
   _ret:='TKRS'
?};
{? _ret='' & VAT_DEK.DATA<date(2021,10,1)
|| FUN.info('Data wypełnienia powinna być późniejsza niż 30 wrzesień 2021.'@);
   VAT_DEK.DATA:=date(2021,10,1);
   _ret:='DATA'
?};
_ret


\utw_viudo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Formula tworzenia pozycji deklaracji typu VIU-DO
::----------------------------------------------------------------------------------------------------------------------
_czy_kor:=0;
_kor:=VAT_DEK.TYP+3='KOR';
_wer:=VAT_DEK.NR().NR;
VAR_DEL.delete('BrakKursu');
BrakKursu:=obj_new(
   'BRAK',
   'add'
);
BrakKursu.BRAK:='';
BrakKursu.add:="
   {? .BRAK*_a=0
   || .BRAK+=_a+', '
   ?}
";
exec('utw_decl','!fks_ved_dvvd');
exec('utw_kor','!fks_ved_dvvd');
exec('utw_sum','!fks_ved_dvvd');
{? BrakKursu.BRAK<>''
|| FUN.emsg(
      'Nie znalezionu kursu EUR dla następujących walut:\n%1.\n\n'
      'Do wyliczeń przyjęto kurs równy 1.'@[BrakKursu.BRAK-2]
   )
?};
VAR_DEL.delete('BrakKursu');
VAR_DEL.delete('TVAT','TVATK','TVATKH','TVATI1');
~_kor | _czy_kor


\utw_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Utworzenie tabeli z danymi do deklaracjie VIU-DO
::   WE: _a - 1-deklaracja 2-korekta
::----------------------------------------------------------------------------------------------------------------------
_deklaracja:=_a=1;
_all:=''; _maska:='';
DVAT.cntx_psh();
PVAT.cntx_psh();
_kw:=#(VAT_DEK.OKRES+1);
_od:=(_kw-1)*3+1;
_vr:=#(4+$SSTALE.AO().POCZ);
_okres1:=date(_vr,_od,1);
_od+=2;
_okres2:=date(_vr,_od,0);
OKRO_F.cntx_psh(); OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
_k1:={? OKRO_F.find_ge(_okres1) || OKRO_F.ROK().KOD || '' ?};
_k2:={? OKRO_F.find_le(_okres2) || OKRO_F.ROK().KOD || '' ?};
OKRO_F.cntx_pop();
{? _k1=_k2
|| DVAT.use('dvat__'+_k1);
   PVAT.use('pvat__'+_k1);
   _where:='D_ROK.KOD='''+_k1+''' '
|| _all:='@';
   _where:='D_ROK.KOD>='''+_k1+''' AND D_ROK.KOD<='''+_k2+'''';
   _mdvat:='';
   _mpvat:='';
   {! _i:=#_k1 .. #_k2
   |! _mdvat+=',\'dvat__'+(('0'+$_i)+2)+'\'';
      _mpvat+=',\'pvat__'+(('0'+$_i)+2)+'\''
   !};
   _maska:='/*+MASK_FILTER(DVAT'+_mdvat+') MASK_FILTER(PVAT'+_mpvat+')*/ '
?};
VAR_DEL.delete('TVAT');
TVAT:=sql('select '+_maska+
   'SLO_KR.KOD as KRAJ, '+
   {? _deklaracja
   || ' \'T\' as T, '+
      ' \'S\' as TSV, '
   || '2021 as ROKV, '+
      '1 as KWV, '
   ?}+
   'SLO_ST.KOD as SVAT, '
   'DVAT.REFERENCE as RDVAT, PVAT.REFERENCE as RPVAT, '+
   'SLO_WAL.KOD as WAL,'+
   'CAST(1 as REAL_TYPE) as KURS,'+
   'SLO_GR.KOD as GRPOD '+
   ',0 as CRC '+
   ', SUM2 as NETTO, VAT as PODAT, SUM1 as BRUTTO '+
   ', SUM2 as W_NETTO, VAT as W_PODAT, SUM1 as W_BRUTTO '+
   ', RVAT.SYM as REJ_VAT, DVAT.NR as NR_VAT, DVAT.REJ as REJ_KS, DVAT.NR_W_REJ as NR_KS '+
   ', DVAT.NIP as NIP, DVAT.KH as KH, DVAT.DAT2 as DATAW '+
   ', PVAT.REFERENCE as PVAT_REF '+
   ', 0 as ODD '+
   ', \'                                   \' as KON '+
   ', \'                                                       \' as SYM_ROK '+
   ', \' \' as TYP '+
   ', \'                                                  \' as DSYM '+
   ', DVAT.DAT2 as TP '+
   'from '+_all+'PVAT left join SLO as SLO_GR using (PVAT.GRVAT, SLO_GR.REFERENCE) '+
   'left join SLO as SLO_ST using (PVAT.STVAT, SLO_ST.REFERENCE) '+
   'join '+_all+'DVAT using (PVAT.DVAT, DVAT.REFERENCE) '+
   {? _deklaracja
   || 'join OKRO_F as D_OKR using (DVAT.OBR, D_OKR.REFERENCE) '
   || 'join OKRO_F as D_OKR using (DVAT.DOKOKRO, D_OKR.REFERENCE) '
   ?}+
   'join ROK_F as D_ROK using (D_OKR.ROK, D_ROK.REFERENCE) '+
   'join RVAT using (DVAT.RVAT, RVAT.REFERENCE) '+
   'join KVAT using (RVAT.KVAT, KVAT.REFERENCE) '+
   'join SLO as SLO_KR using (DVAT.KRAJ, SLO_KR.REFERENCE) '+
   'join SLO as SLO_WAL using (DVAT.WAL, SLO_WAL.REFERENCE) '+
   'where '+_where+
   ' and (KVAT.SYM like \'_WWsp%\') '+
   ' AND SLO_KR.KOD!=''PL'' '+
   ' AND D_OKR.POCZ>=to_date(\''+$_okres1+'\') AND D_OKR.KON<=to_date(\''+$_okres2+'\')'+
   'AND DVAT.USUNIETY=0 '+
   {? ~_deklaracja
   || ' and SYM2!=\'\' '
   || ''
   ?}+
   'order by '+{? _deklaracja || 'KRAJ,T,TSV,SVAT' || 'KRAJ,ROKV,KWV' ?}
);
:: Usunięcie pozycji bez szczególnych procedur: EE i WSTO_EE oraz ze stawkami zwolnionymi
VAR_DEL.delete('TKurs');
TVAT.for_each("
   _del:=TVAT.PODAT=0;
   {? ~_del
   || _deklaracja:=var_pres('T',TVAT)>0;
      DVAT.cntx_psh();
      DVAT.use(8+TVAT.RDVAT);
      DVAT.prefix();
      {? DVAT.seek(BIT.sqlint(TVAT.RDVAT),)
      || DOK.cntx_psh();
         DOK.use(8+ref_name(DVAT.DOK));
         DOK.prefix();
         {? DOK.seek(DVAT.DOK)
         || _del:=(','+DOK.JPK_PROC+',')*',EE,'=0 & (','+DOK.JPK_PROC+',')*',WSTO_EE,'=0;
            {? ~_del
            || PVAT.cntx_psh();
               PVAT.use(8+TVAT.RPVAT);
               PVAT.prefix();
               {? PVAT.seek(BIT.sqlint(TVAT.RPVAT),)
               || {? _deklaracja
                  || TVAT.TSV:=exec('pvat_typ_stawki','fks_ved');
                     {? TVAT.TSV=''
                     || _del:=1
                     || TVAT.T:=exec('pvat_towar_usluga','fks_ved')
                     ?}
                  || TVAT.ROKV:=DVAT.OBR().ROK().POCZ_ROK~1;
                     _mc:=OKRO_F.POCZ~2;
                     TVAT.KWV:={? _mc<4 || 1 |? _mc<7 || 2 |? _mc<10 || 3 || 4 ?};
                     _del:=VAT_DEK.OKRES<=$TVAT.ROKV+'/'+$TVAT.KWV
                  ?};
                  {? ~_del
                  || TVAT.CRC:=PVAT.crc();
                     {? TVAT.WAL<>'EUR'
                     || _kurs:=exec('get_kurs','!fks_ved_dvvd',TVAT.WAL);
                        {? _kurs=-1
                        || _kurs:=1;
                           BrakKursu.add(TVAT.WAL)
                        ?};
                        TVAT.KURS:=_kurs;
                        TVAT.NETTO:=(TVAT.NETTO/_kurs)$2;
                        TVAT.PODAT:=(TVAT.PODAT/_kurs)$2;
                        TVAT.BRUTTO:=TVAT.NETTO+TVAT.PODAT;
                        _del:=TVAT.PODAT=0
                     ?};
                     TVAT.put()
                  ?}
               ?};
               PVAT.cntx_pop()
            ?}
         ?};
         DOK.cntx_pop()
      ?};
      DVAT.cntx_pop()
   ?};
   {? _del
   || TVAT.del()
   ?}
");
VAR_DEL.delete('TKurs');
_tab:=TVAT;
VAR_DEL.delete('TVAT');
::_tab.win_sel(_tab.mk_sel(,,1));
::_tab.select;
_tab


\utw_decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Utworzenie częsci deklaracyjnej w oparciu o tabelę z danymi dokumentów
::   WE: _a - tabela z pozycjami dokumentó
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('utw_tab','!fks_ved_dvvd',1);
{? _tab.first()
|| _lk:=_lp:=0; _kraj:='';
   VAT_POZ.cntx_psh();
   VAT_POZ.prefix();
   VAT_PS.cntx_psh();
   VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
   VAT_PS.prefix();
   {!
   |? _tab.prefix(_tab.KRAJ,_tab.T,_tab.TSV,_tab.SVAT,);
      {? _tab.first()
      || {? _kraj<>_tab.KRAJ
         || _kraj:=_tab.KRAJ;
            _lk+=1;
            _lp:=0
         ?};
         _lp+=1;
         VAT_POZ.blank(1);
         VAT_POZ.VAT_DEK:=VAT_DEK.ref();
         VAT_POZ.LP:='C.2.'+$_lk+'.'+$_lp;
         VAT_POZ.POZ1:=VAT_POZ.POZ2:=1;
         VAT_POZ.TYP:='A';
         VAT_POZ.OP:=
            _tab.KRAJ+', '+{? _tab.T='T' || 'Dostawa towarów' || 'Świadczenie usług' ?}+', '+
            {? _tab.TSV='S' || 'Podstawowa' || 'Zredukowana' ?}+', '+_tab.SVAT;
         {? VAT_POZ.add()
         || {!
            |? VAT_POZ.NETTO+=_tab.NETTO;
               VAT_POZ.VAT+=_tab.PODAT;
               exec('add_vat_ps','!fks_ved_dvvd',_tab);
               _tab.next()
            !};
            VAT_POZ.put()
         ?}
      ?};
      _tab.prefix();
      _tab.next()
   !};
   VAT_PS.cntx_pop();
   VAT_POZ.cntx_pop()
?};
DVAT.cntx_pop();
PVAT.cntx_pop();
1


\utw_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Utworzenie częsci związanej z korektami w oparciu o tabelę z danymi dokumentów
::----------------------------------------------------------------------------------------------------------------------
_tab:= exec('utw_tab','!fks_ved_dvvd',2);
{? _tab.first()
|| _lk:=_lp:=0; _kraj:='';
   VAT_POZ.cntx_psh();
   VAT_POZ.prefix();
   VAT_PS.cntx_psh();
   VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
   VAT_PS.prefix();
   {!
   |? _tab.prefix(_tab.KRAJ,_tab.ROKV,_tab.KWV);
      {? _tab.first()
      || {? _kraj<>_tab.KRAJ
         || _kraj:=_tab.KRAJ;
            _lk+=1;
            _lp:=0
         ?};
         _lp+=1;
         VAT_POZ.blank(1);
         VAT_POZ.VAT_DEK:=VAT_DEK.ref();
         VAT_POZ.LP:='C.5.'+$_lk+'.'+$_lp;
         VAT_POZ.POZ1:=VAT_POZ.POZ2:=1;
         VAT_POZ.TYP:='A';
         VAT_POZ.OP:=_tab.KRAJ+', '+$_tab.ROKV+'/'+$_tab.KWV;
         {? VAT_POZ.add()
         || {!
            |? VAT_POZ.NETTO+=_tab.NETTO;
               VAT_POZ.VAT+=_tab.PODAT;
               exec('add_vat_ps','!fks_ved_dvvd',_tab);
               _tab.next()
            !};
            VAT_POZ.put()
         ?}
      ?};
      _tab.prefix();
      _tab.next()
   !};
   VAT_PS.cntx_pop();
   VAT_POZ.cntx_pop()
?};
DVAT.cntx_pop();
PVAT.cntx_pop();
1


\utw_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Tworzy pozycje podumowujące deklaracji VIU-DO
::----------------------------------------------------------------------------------------------------------------------
VAT_POZ.cntx_psh();
VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),'C.6.');
{? VAT_POZ.first() || {! |? VAT_POZ.del !} ?};
VAT_POZ.prefix(VAT_DEK.ref(),'C.7.');
{? VAT_POZ.first() || {! |? VAT_POZ.del !} ?};
_netto:=0;
_vat:=0;
VAT_POZ.prefix(VAT_DEK.ref());
{? VAT_POZ.first()
|| _tab:=tab_tmp(1,
      'K','STRING[2]','Kraj',
      'N','REAL','Netto',
      'V','REAL','VAT'
   );
   {!
   |? _str:=spli_str(VAT_POZ.OP,',');
      _kraj:=_str[1];
      {? _tab.find_key(_kraj,)
      || _tab.N+=VAT_POZ.NETTO;
         _tab.V+=VAT_POZ.VAT;
         _tab.put()
      || _tab.blank(1);
         _tab.K:=_kraj;
         _tab.N:=VAT_POZ.NETTO;
         _tab.V:=VAT_POZ.VAT;
         _tab.add()
      ?};
      &_str;
      VAT_POZ.next()
   !};
   {? _tab.first()
   || _lp:=0;
      {!
      |? _lp+=1;
         VAT_POZ.blank(1);
         VAT_POZ.VAT_DEK:=VAT_DEK.ref();
         VAT_POZ.LP:='C.6.'+$_lp;
         VAT_POZ.POZ1:=VAT_POZ.POZ2:=1;
         VAT_POZ.TYP:='S';
         VAT_POZ.OP:=_tab.K;
         VAT_POZ.NETTO:=_tab.N;
         VAT_POZ.VAT:=_tab.V;
         VAT_POZ.add();
         _netto+=VAT_POZ.NETTO;
         _vat+=VAT_POZ.VAT;
         _tab.next()
      !}
   ?}
?};
VAT_POZ.blank(1);
VAT_POZ.VAT_DEK:=VAT_DEK.ref();
VAT_POZ.LP:='C.7.';
VAT_POZ.POZ1:=VAT_POZ.POZ2:=1;
VAT_POZ.TYP:='S';
VAT_POZ.OP:='CAŁKOWITA KWOTA NALEŻNEGO PODATKU VAT DO WPŁATY';
VAT_POZ.NETTO:=_netto;
VAT_POZ.VAT:=_vat;
VAT_POZ.add();
VAT_POZ.cntx_pop()


\get_kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Zwraca tablicę z kursem i jednostką waluty na potrzeby deklaracji VIU-DO
::   WE: _a - symbol waluty
::----------------------------------------------------------------------------------------------------------------------
_wal_str:=_a;
{? var_pres('TKurs')<=0
|| TKurs:=tab_tmp(1,
      'WAL','STRING[3]','Waluta',
      'K','REAL','Kurs'
   )
?};
{? TKurs.find_key(_wal_str,)
|| TKurs.K
|| _wal:=exec('FindInSet','#table','SLO','SL',_wal_str,FINFO.SLWAL().SLU,,1);
   _kurs:=-1;
   KRS.cntx_psh();
   KRS.index('KRS_WAL'); KRS.prefix(VAT_DEK.TKRS,_wal);
   {? KRS.first() & KRS.SR<>0
   || _kurs:=KRS.SR
   ?};
   KRS.cntx_pop();
   TKurs.blank(1);
   TKurs.WAL:=_wal_str;
   TKurs.K:=_kurs;
   TKurs.add();
   _kurs
?}


\add_vat_ps
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Dodaje szczegóły deklaracji VIU-DO
::   WE: _a - tabela z danymio
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
VAT_PS.blank(1);
VAT_PS.VAT_DEK:=VAT_DEK.ref();
VAT_PS.VAT_POZ:=VAT_POZ.ref();
VAT_PS.NETTO:=_tab.NETTO;
VAT_PS.VAT:=_tab.PODAT;
VAT_PS.BRUTTO:=_tab.BRUTTO;
VAT_PS.PVAT_REF:=_tab.RPVAT;
VAT_PS.REJ_VAT:=_tab.REJ_VAT;
VAT_PS.NR_VAT:=_tab.NR_VAT;
VAT_PS.SVAT:=_tab.SVAT;
VAT_PS.REJ_KS:=exec('FindAndGet','#table','REJ',_tab.REJ_KS,,,null);
VAT_PS.NR_KS:=_tab.NR_KS;
VAT_PS.NIP:=_tab.NIP;
VAT_PS.KH:=_tab.KH;
VAT_PS.CRC:=_tab.CRC;
VAT_PS.DATA:=_tab.DATAW;
VAT_PS.TP:=_tab.TP;
VAT_PS.WAL:=_tab.WAL;
VAT_PS.KURS:=_tab.KURS;
VAT_PS.W_NETTO:=_tab.W_NETTO;
VAT_PS.W_VAT:=_tab.W_PODAT;
VAT_PS.W_BRUTTO:=_tab.W_BRUTTO;
VAT_PS.add()


\akc_poz_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Po poprawieniu pozycji VIU-DO
::----------------------------------------------------------------------------------------------------------------------
exec('utw_sum','!fks_ved_dvvd');
~~



\empty_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Sprawdza, czy istnieją zerowe kursy walut
::----------------------------------------------------------------------------------------------------------------------
_empty:=0;
KRS.cntx_psh();
KRS.index('KRS_KRAJ'); KRS.prefix(VAT_DEK.TKRS);
{? KRS.first()
|| {!
   |? _empty:=KRS.SR=0;
      _empty=0 & KRS.next()
   !}
?};
KRS.cntx_pop();
_empty

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 46e75cc53cb33a4546f816d248bdaa2837c33431437357082533c410293811b343ecf3e0a615f2c8dccf28e1be1868b6c3aff90bbc0d695b0e75f6a366feea5114f9c54f22ab44755b3f1731ee3e7da491ae5db8a1c7dd66177a1f2f99a47b4cce9cdd0fdd9846ac01431c242dceb5301069d4d7a5d62814f69a4c1acbde11ae

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_dwyf.fml
:: Utworzony: 12.02.2016
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa generacji dokumentów wydania na podstawie faktury sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LMG
::# properties=LOOP
::# kind=WE,   symbol=TYPYDOK,      type=_TYPYDOK,    name=Typ dokumentu magazynowego, required=N, fml_val="exec('typydok','!lmg_mag_dwyf')", fml_exp="exec('typydok_export','magdok_nag',_a)"
::# kind=WE,   symbol=FAKS,         type=_FAKS,       name=Faktura sprzedaży,          required=T,    keyref=T
::# kind=WE,   symbol=GEN_DOK,      type=STRING,      name=Generacja dokumentów,       required=N
::# kind=WEW,  symbol=ND,           type=_ND,         name=Dokument rozchodowy,        required=N
::# kind=WY,   symbol=GEN_DOK,      type=STRING,      name=Generacja dokumentów,       required=N
::# kind=WY,   symbol=ND,           type=_ND,         name=Dokument rozchodowy,        required=N
::# condition=Rejestracja dokumentu wydania, act_uid=LMG_MAG_DWYD,   auto=T,  formula=_a.ND<>~~ & _a.ND<>null()
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

exec('init_zkn','lsp');
exec('init','lmg');

_akcja:=_mp.akcja();
_auto:=_akcja<>'Wydania' & _mp.isAutoRun();
_cancel:=0;

:: Wyzwalacz
_mp.trigRef('ND',,1,,exec('kind_internal','#b_port'),'ND');

{? ~(var_pres('FAKS',_in)=type_of(null()) & _in.FAKS)
|| _mp.error('Brak wymaganego parametru FAKS.')
|? exec('FindAndGet','#table',FAKS,_in.FAKS,,"FAKS.STAT_REJ",'')='A'
|| _mp.error('Dokument sprzedaży został anulowany.')
||
   _continue:=0;
   _gen_dok:='';
   _typydok:={? var_pres('TYPYDOK',_in)=type_of(null()) || _in.TYPYDOK || null() ?};

   {? _mp.loop()=0
:: pierwsze wywołanie
   || FAKS.cntx_psh();
      FAP.cntx_psh();
      ND.cntx_psh();
      DK.cntx_psh();
      _jest:=FAKS.seek(_in.FAKS,ref_name(_in.FAKS));
      {? ~_jest || FAKS.prefix(); _jest:=FAKS.seek(_in.FAKS,ref_name(_in.FAKS)) ?};
      {? _jest
      ||
         FAP.use((FAP.name()-3)+(ref_name(FAKS.ref())+3));
         _aktmsk:=FAKS.name()+3;
         _newmsk:=(1+_aktmsk)+form((exec('FindAndGet','#table',FAKS,$_in.FAKS,,"D",'date()')~1)-2000,,0,'99');
         ND.use('nagdo'+_newmsk);
         DK.use((DK.name()-3)+(ND.name()+3))
      ?};
      {? ~_jest
      || _cancel:=1;
         _mp.error('Nie znaleziono dokumentu sprzedaży.')
      |? FAKS.STAT_REJ='A'
      || _cancel:=1;
         _mp.error('Błędny parametr FAKS. Wskazany dokument został anulowany.')
      |? ~(FAKS.WHERE='G' & FAKS.T().KOR<>'T' & ~(FAKS.T().PAR='N' & FAKS.SYMF<>''))
      || _cancel:=1;
         _mp.error('Błędny parametr FAKS. Do wskazanego dokumentu nie można wystawić dokumentu wydania.')
      |? _jest & FAKS.STAT_REJ<>'T'
      || FUN.info('Dokument sprzedaży nie został zaakceptowany'@);
         _cancel:=1
      |? _jest & exec('wydaFaks','faktury_wspolne')
      || FUN.info('Do dokumentu sprzedaży zostały już wystawione dokumenty wydania'@);
         _mp.done()
      ||
         {? _akcja='Wydania'
            | _auto
            | _mp.pathTodo()
         || {? FAKS.WHERE='G' & FAKS.T().KOR<>'T' & ~(FAKS.T().PAR='N' & FAKS.SYMF<>'')
            || _gendel:={? var_pres('__gen')>0 || 0 || __gen:=1; 1  ?};
               _ok:=exec('fak_magg','magdok_wspolne',_typydok);
               {? _gendel || VAR_DEL.delete('__gen') ?}
            ?};

            _internal:=_mp.load(exec('kind_internal','#b_port'));
            _continue:={? var_pres('ND',_internal)=type_of(null()) & _internal.ND || 1 || -1 ?};
            {? _continue=1 || _gen_dok:=exec('FindAndGet','#table',ND,_internal.ND,,"GRP_KEY",'') ?}
         || _mp.error('Nieobsługiwana ścieżka.')
         ?}
      ?};
      FAP.cntx_pop();
      FAKS.cntx_pop();
      ND.cntx_pop();
      DK.cntx_pop()
:: wywołanie w pętli
   || _continue:=var_pres('GEN_DOK',_in)=type_of('') & _in.GEN_DOK<>'';
      {? _continue || _gen_dok:=_in.GEN_DOK ?}
   ?};

   {? _continue=-1
:: Wycofano się z wystawiania dokumentów
   || 1
   |? _continue=1 & _gen_dok<>''
:: Zapisanie parametru wyjściowego ND, wykluczenie kolejnej realizacji z pętli, zakończenie czynności
   || _aktmsk:=FAKS.name()+3;
      _newmsk:=(1+_aktmsk)+form((exec('FindAndGet','#table',FAKS,$_in.FAKS,,"D",'date()')~1)-2000,,0,'99');
      ND.cntx_psh();
      ND.use('nagdo'+_newmsk);
      ND.prefix();
      _grp_key:=_gen_dok-1;
      _grp_key_on:=_grp_key+'1';
      _grp_key_off:=_grp_key+'0';
      {? _grp_key<>''
      || ND.index('GRP_KEY');
         ND.prefix(_grp_key_on);
         {? ND.first()
         || ND.cntx_psh();
            ND.prefix();
            ND.GRP_KEY:=_grp_key_off;
            do();
            ND.put();
            _mp.save(exec('kind_out','#b_port'),'ND',ND.ref());
            _mp.save(exec('kind_out','#b_port'),'GEN_DOK',_gen_dok);
            end();
            ND.cntx_pop();
            {? ND.first()
::          kontynuacja pętli
            || _mp.loop_continue()
            ?}
         ?}
      ?};
      ND.cntx_pop();
      _mp.done()
   |? ~_cancel
   || _mp.error('Brak oczekiwanego parametru GEN_DOK.')
   ?}
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FAKS',_in)<>type_of(~~)
|| 'Wystaw dokumenty wydania w magazynie do dokumentu sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
|| ''
?}


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_wyn:=0;

_opis:='';

_wyn:=exec('usr_upr','b_perm','MG',ST.MAG,_user);

{? _wyn=0 || _opis+='\n- magazynu' ?};

:: Sprawdzenie uprawnień do typu dokumentu
{? _wyn & var_pres('TYPYDOK',_in)=type_of(null()) & _in.TYPYDOK
|| _typydok:=exec('FindAndGet','#table',TYPYDOK,$_in.TYPYDOK,,,null());
   T2MG.cntx_psh();
   T2MG.index('T');
   T2MG.prefix(ST.MAG);
   {? T2MG.first() || _wyn:=T2MG.find_key(_typydok) ?};
   T2MG.cntx_pop();
   {? _wyn
   || _typy:=exec('get','#params',1001,2,_user);
      _typydok_t:=exec('FindAndGet','#table',TYPYDOK,$_in.TYPYDOK,,"TYPYDOK.T",'');
      {? form(_typy)<>'' || _wyn:=(_typy*(_typydok_t+' '))>0 ?}
   ?};

   {? _wyn=0 || _opis+='\n- typu dokumentu' ?}
?};

:: Sprawdzenie czy jest ustawiony rok
{? _wyn
|| _wyn:=ST.AR>0;
   {? _wyn=0 || _opis+='\n- okresu' ?}
?};

:: Sprawdzenie czy jest ustawiony oddział
{? _wyn
|| {? ST.ODDZ=''
   || _wyn:=0
   || _wyn:=exec('usr_upr','b_perm','ODDZ',ST.ODDZ_REF,_user)
   ?};
   {? _wyn=0 || _opis+='\n- oddziału' ?}
?};

{? _wyn=0 & var_pres('opis',params_get())
|| params_get().opis.TXT:='Brak uprawnień do:'+_opis
?};

_wyn


\typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na wartość parametru TYPYDOK
::   WY: TYPYDOK.ref()
::----------------------------------------------------------------------------------------------------------------------
_prod:=exec('tte_lic','tte');
_prod:=exec('tte_lic','tte');
exec('typ_dok','lmg','"TYPYDOK".P=\'N\' and "TYPYDOK".DS=\'T\''
    +{? _prod='N' || ' and "TYPYDOK".WYR=\'N\'' || '' ?},,,0,0,,,,,-1)


\faks2nd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokumenty magazynowe na podstawie faktury
::----------------------------------------------------------------------------------------------------------------------
exec('faks2nd','faktury_nag')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 762037038e4ee3ee531e9c02bae0aeaed3dd233db291089ca448cc7a0d9cc8134813c3f1e07d72efa20365628b420e31fd1a99d4cfca26a2a9645de016201bee64fd9497e5d5591bb7df2f483a63431ea637960ffe4bf127e204574ec853de8282ff5e201b1064a63614d8ac929a7fe1b3a795cdcae26e90cdc70f75050c6ced

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_zkn_wwzs.fml
:: Utworzony: 29.10.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Utworzenie zamówienia sprzedaży w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=ZK_N,    type=_ZK_N,  name=Zamówienie sprzedaży, required=T,    keyref=T
::# kind=WE,   symbol=CZY_PDF, type=STRING, name=Utwórz PDF,           required=N,    keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE,   symbol=PRN_MSK, type=STRING, name=Maska wydruku,        required=N,    keyref=N, fml_val="exec('rep_exec','#b_report','LSP_ZKN_WWZS','lsp_zkzam*','Wydruki zamówienia sprzedaży',-1)"
::# kind=WY,   symbol=DOKUM,   type=_DOKUM, name=Załącznik,            required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_zkn','lsp');

:: WSTĘPNE WALIDACJE
_clean_result:=params_exec('clean','!lsp_zkn_wwzs',_mp,_in);
_can_continue:=_clean_result.RESULT;
{? _can_continue=0
|| return()
?};

_akcja:=_mp.akcja();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' ?};
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || {? exec('ctrlMask','#b_report',_in.PRN_MSK,'lsp_zkzam') || _in.PRN_MSK || '' ?}
      || ''
      ?};

{? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N
||
:: Ustawienie kontekstu wg instancji elementu w procesie
   exec('openz_psh','open_tab');
   _use:=ref_name(_in.ZK_N)<>ZK_N.name();
   {? _use || ZK_N.use(ref_name(_in.ZK_N)) ?};
   ZK_N.prefix();
   {? ZK_N.seek(_in.ZK_N)
   ||
      {? exec('perm_lsp','!lsp_zkn_wwzs')
      ||
::    Nie ustawiamy wcześniej parses ponieważ i tak domyślnie otwierane są maski '__'
::    a zamówienie może być w archiwum
         {? _use || exec('openz','open_tab',ZK_N.name()+3) ?};
         _dokum:=exec('zam_druk','!lsp_zkn_wwzs',_czy_pdf,_msk);
         {? _czy_pdf & _dokum<>null()
         ||
            _out.DOKUM:=_dokum;
            _mp.save(,_out);
            {? ~_keep || _mp.done() ?}
         |? ~_czy_pdf & _dokum
         ||
            {? ~_keep || _mp.done() ?}
         ?}
      || _msg:='Brak uprawnień do stanowisk sprzedaży.';
         _show_msg:=1;
         {? var_pres('__GRP_DR')>0 & var_pres('FIRST',__GRP_DR)>0 & ~__GRP_DR.FIRST
         || _show_msg:=0
         ?};
         {? _show_msg
         || FUN.info(_msg)
         ?};
         _mp.error(_msg)
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};
   exec('openz_pop','open_tab')
||
   _mp.error('Brak parametru wejściowego ZK_N.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZK_N',_in)<>type_of(~~) & _in.ZK_N
|| 'Utwórz PDF zamówienia sprzedaży: %1'@@[exec('record','#to_string',_in.ZK_N)]
|| ''
?}


\perm_lsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Sprawdzenie uprawnienia do stanowisk sprzedaży dla zamówienia sprzedaży
::   WY: 1 - OK, 0 -nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? ZK_N.T().R='Z'
|| _wyn:=0;
   B_PERM.cntx_psh();
   B_PERM_U.cntx_psh();
   B_PERM.index('NAME');
   B_PERM.prefix('LSP');
   {? B_PERM.first()
   || B_PERM_U.index('USER');
      B_PERM_U.prefix(REF.FIRMA,OPERATOR.USER,B_PERM.ref());
      {? B_PERM_U.first() & B_PERM_U.ACCESS='T'
      || _wyn:=1
      ?}
   ?};
   B_PERM.cntx_pop();
   B_PERM_U.cntx_pop()
?};
_wyn


\zkn_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zamówienie sprzedaży - Utwórz PDF
::   WE: [_a] - akcja
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:='Area Utwórz PDF' ?};

exec('zkn_pdf','zamsiw_wydruk',_a)


\zam_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2004]
:: OPIS: wydruki dla zamowienia
::   WE: [_a] - 1-PDF 0-normalny wydruk
::       [_b] - maska wydruku
::   WY: dla _a=1: ref/null _a=0: 0/1
::  OLD: \zam_druk/zk2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
_msk:={? var_pres('_b')=type_of('') || _b || '' ?};
_wyn:={? _a || null || 0 ?};

{? _msk<>''
|| _wydr:=_msk
|| {? ZK_N.T().R='W' || _wydr:='lmg_zkwew*' || _wydr:='lsp_zkzam*' ?}
?};

{? _a=1
|| NRDOK.index('NRDOK');
   NRDOK.prefix('SYS','');
   {? ~NRDOK.first()
   || _ok:=0;
      FUN.info('Nie zdefiniowano numeracji SYS dla dokumentów powiązanych.'@)
   ?}
?};

POLA.NAZ_WYDR:='';
_txt:={? ZK_N.T().R='W' || 'Zamówienie wewnętrzne' || 'Zamówienie klienta' ?};

{? _a=1
||
   {? var_pres('__GRP_DR')>100&__GRP_DR.GR & __GRP_DR.FIRST=0 & __GRP_DR.WZ<>''
   ||
      _symb:=exec('str_to_pth','#string',ZK_N.SYM);
      _file:='%1 %2.pdf'[_txt,_symb];
      _wydr:=rep_exec(__GRP_DR.WZ,,,_file,1)
   ||
      _symb:=exec('str_to_pth','#string',ZK_N.SYM);
      _file:='%1 %2.pdf'[_txt,_symb];
      _wydr:=rep_exec(_wydr,,,_file,1)
   ?}
||
   _wydr:=exec('rep_exec','#b_report','LSP_ZKN_WWZS',_wydr,_txt,2,,,,,,'W')
?};

{? var_pres('__GRP_DR')>100 & _wydr=0
||
   __GRP_DR.ESC:=1
?};

{? POLA.NAZ_WYDR<>'' & _a=1 & _wydr=1
|| _wyn:=exec('save_dok','dokum','ZK_N',_file,POLA.NAZ_WYDR,ZK_N.SYM,ZK_N.KH().EM
      ,'Zamówienie o symbolu: '+ZK_N.SYM,PARAM_W.JEZYK,ZK_N.STAT_REJ)
|? _a=0
|| _wyn:=_wydr
?};

_wyn


\zkn_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Drukuj
::   WE: [_a] - akcja
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:='Area Drukuj' ?};

exec('zkn_pdf','zamsiw_wydruk',_a)


\zkn_pdf_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [19.42]
:: OPIS: Zamówienie - Utwórz PDF
::----------------------------------------------------------------------------------------------------------------------
exec('zkn_druk_gr','!lsp_zkn_wwzs',1,'LSP_ZKN_WWZS','Area Utwórz PDF')


\zkn_prn_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [19.42]
:: OPIS: Zamówienie - Drukuj
::----------------------------------------------------------------------------------------------------------------------
exec('zkn_druk_gr','!lsp_zkn_wwzs',0,'LSP_ZKN_WWZS','Area Drukuj')


\zkn_druk_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [19.42]
:: OPIS: Wydruk zamówień
::   WE: _a - utworz pdf 0-nie 1-tak
::       _b - UID czynności
::       _c - nazwa akcji
::       [_d] - maska wydruku
::---------------------------------------------------------------------------------------------------------------------
_czy_pdf:=_a;
_act_uid:=_b;
_akcja:=_c;
_msk:={? var_pres('_d')=type_of('') || _d || '' ?};

_czysc:="VAR_DEL.delete('__GRP_DR')";
_czysc();
:: __GRP_DR - srodowisko grupowego drukowania
__GRP_DR:=obj_new('GR','SEL','SIZE','FIRST','ESC','WZ','PDF','SHOW_PDF','DR_N_AKC','JEZYK','JEZYK_KH','DR_N_CAN');
:: __GRP_DR.SEL, _Sel - wybrane dokumenty
__GRP_DR.SEL:=_Sel:=ZK_N.sel_aget();

{? _Sel.first()
|| __GRP_DR.GR:=1
|| __GRP_DR.GR:=0;
   _Sel.REF:=#ZK_N.ref(); _Sel.CRC:=ZK_N.crc(); _Sel.add()
?};
__GRP_DR.SIZE:=_Sel.size();
:: __GRP_DR.FIRST - drukowanie pierwszego dokumentu sposrod zaznaczonych 0-nie 1-tak
__GRP_DR.FIRST:=1;
:: __GRP_DR.SHOW_PDF - wyswietlac utworzone pdf 0-nie 1-tak
__GRP_DR.SHOW_PDF:=0;
:: __GRP_DR.PDF - utworz pdf 0-nie 1-tak
__GRP_DR.PDF:=_czy_pdf;
:: __GRP_DR.WZ - wybrany wzorzec wydruku
__GRP_DR.WZ:=_msk;
:: __GRP_DR.ESC - zrezygnowano z wypelnienia parametrow wydruku 0-nie 1-tak
__GRP_DR.ESC:=0;
:: _ok - stan dzialania funkcji 0-nie ok 1-ok
_ok:=1;

ZK_N.cntx_psh();

{? _ok=1
|| {? __GRP_DR.GR
   || __GRP_DR.SHOW_PDF:={? __GRP_DR.PDF || FUN.ask('Wyświetlić wygenerowane dokumenty?'@) ?}
   ?};
   {? __GRP_DR.DR_N_AKC=-1 || __GRP_DR.DR_N_AKC:=0 ?}
?};
:: Drukowanie
{? _ok
||
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      _res:=1;
      {? ZK_N.seek(_Sel.REF,)
      ||
         _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:=_act_uid;
         _params.UIDREF:=ZK_N.uidref();
         _params.AKCJA:=_akcja;
         _params.GRUPA:={? __GRP_DR.GR || 'T' || 'N' ?};
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZK_N',ZK_N.ref());
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'CZY_PDF',{? _czy_pdf || 'TAK' || 'NIE' ?});

         exec('mp_run','#b__box',_params);

         obj_del(_params);
         __GRP_DR.FIRST:=0
      ?};
      _loop:=__GRP_DR.ESC=0 & {? _czy_pdf || _res & _Sel.next() || 0 ?}
   !}
?};
ZK_N.sel_adel();
ZK_N.cntx_pop();
_czysc();
0


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zam_clean','zamsiw_nag',_mp,_in)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 507427d92fb122a007c107dc8d94ef3ee4c154c13687b125c9b8b4b8b6d2d13218c862dae25637af9eb8e01c2c1e7d2a88f0be1684ed51eb444154e0dd73d41a771181d7498db4c7542606b19ede9629c041f3270f52ba2aa1d521e80cd99313224bc19929d8ee9a67b0fbcc3959b14b8c36d4fd9082dfea9fbfa804c6fc432a

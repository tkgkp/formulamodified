:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dpzw.fml
:: Utworzony: 09.06.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DPZW - Dekretacja umów zleceń
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dekretacja umów zleceń - główna formuła czynności FKS_DKS_DPZW.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
:: PARAMETRY WE:
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Okres umowy zlecenia, required=N, fml_val="exec('okro','!fks_dks_dpzw',_a)"
::# kind=WE, symbol=ODD, type=_ODD, name=Jednostka księgowa, required=N, fml_val="exec('odd','!fks_dks_dpzw',_a)"
:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na dokument księgowy, required=T
_mp:=params_get().mp;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_akcja:=-_mp.akcja();
OKRO_F.cntx_psh(); ODD.cntx_psh();
OKRO_F.index('FIRMA_NR'); OKRO_F.prefix();
ODD.index('ODDZIALY'); ODD.prefix();
_okres:=_odd:=null;
{? var_pres('OKRO_F',_we)>0
|| {? OKRO_F.seek(_we.OKRO_F)
   || _okres:=OKRO_F.ref()
   ?}
?};
{? _okres=null
|| _refs:=_mp.getRefs();
   {! _i:=1..obj_len(_refs)
   |! {? var_press('['+$_i+']',_refs)>0 & ref_name(_refs[_i])=OKRO_F.name()
      || {? OKRO_F.seek(_refs[_i])
         || _okres:=OKRO_F.ref()
         ?}
      ?}
   !};
   &_refs
?};
{? var_pres('ODD',_we)>0
|| {? ODD.seek(_we.ODD)
   || _odd:=ODD.ref()
   ?}
?};
{? _odd=null
|| _refs:=_mp.getRefs();
   {! _i:=1..obj_len(_refs)
   |! {? var_press('['+$_i+']',_refs)>0 & ref_name(_refs[_i])=ODD.name()
      || {? ODD.seek(_refs[_i])
         || _odd:=ODD.ref()
         ?}
      ?}
   !}
?};
{? _okres=null || _okres:=SSTALE.AO ?};
ROZNE.OKRO_F:=_okres;
ROZNE.ROK_F:=ROZNE.OKRO_F().ROK;
ROZNE.ODD2:=_odd;
_ok:=1;
{? _mp.pathTodo() | _mp.pathProc()
|| _ok:=0;
   ROZNE.win_edit('UMZLEC');
   {? ROZNE.edit("
         __CHK.record2(ROZNE,'ROK_F','Rok','OKRO_F','Okres')
      ")
   || _ok:=1;
      _okres:=ROZNE.OKRO_F;
      _mp.keyRef(ROZNE.OKRO_F().uidref());
      _mp.keyRef(ROZNE.ODD2().uidref())
   ?}
?};
{? _ok & ROZNE.OKRO_F
|| OKRO_F.ROK();
   exec('fl_decl','dekret_aut');
   exec('dek_decl','dekret_aut');
   exec('sd_decl','dekret_aut');
   _mp.descTodo();
   _ao:=SSTALE.AO; _ar:=SSTALE.AR;
   {? SSTALE.AO<>ROZNE.OKRO_F | SSTALE.AR<>ROZNE.ROK_F
   || SSTALE.AO:=ROZNE.OKRO_F;
      SSTALE.AR:=ROZNE.ROK_F;
      exec('open_tabele','open_tab','F')
   ?};
   isRH:=0;
   _ok:=exec('dekretuj','!fks_dks_dpzw',~_mp.isAutoRun());
   {? _ok | isRH=0
   || _wy.DOK:={? _ok || DOK.ref() || null ?};
      _mp.save(,_wy);
      _mp.done()
   ?};
   {? SSTALE.AO<>_ao | SSTALE.AR<>_ar
   || SSTALE.AO:=_ao;
      SSTALE.AR:=_ar;
      exec('open_tabele','open_tab','F')
   ?};
   VAR_DEL.delete('isRH')
?};
OKRO_F.cntx_pop(); ODD.cntx_pop()


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
OKRO_F.cntx_psh(); OKRO_F.prefix();
{? var_pres('OKRO_F',_in)=type_of(null()) & _in.OKRO_F & OKRO_F.seek(_in.OKRO_F,)
|| _desc:='Zadekretuj umowy zlecenia za okres %1'@@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]
|| _desc:='Zadekretuj umowy zlecenia'@@
?};
OKRO_F.cntx_pop();
_desc


\dekretuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS, KGS [2011]
:: OPIS: Formula ksiegujaca rachunki z umow-zlecen
::   WE: _a - czy komunikaty?
::  OLD: \ksieguj_zleceni/ksieguj.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
EKSG.SYS:='PPL';
exec('start_zlecenia','dok_fks_aut_dok');
optym:=(PAR_SKID.get(312)='T');
rodz_dat:={? PAR_SKID.get(314)='R' || 'DRA' || 'DWY' ?};
{? exec('initWARL','dok_fks_aut_dok',ROZNE.ROK_F,'PPL','KU')
|| exec('init','dok_fks_aut_dok','Informacje o błędach dekretacji umów zleceń');
   POMOC.OKR:=OKRO_F.ref();
   ODD.cntx_psh();
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   RH.cntx_psh();
   {? __optym
   || _RHndx:=RH.ndx_tmp('Rodzaj umowy',0,'ZLE','ODDZIAL',0,rodz_dat,,0)
   || _RHndx:=RH.ndx_tmp('Rodzaj umowy',0,'ZLE','ODDZIAL',0,'ZLE','RU',0,rodz_dat,,0)
   ?};
   RH.index(_RHndx);
   VAR_DEL.delete('TmpRH');
   TmpRH:=obj_new('TAB','add','erase');
   TmpRH.TAB:=tab_tmp(1,
      'REF','INTEGER','REF'
   );
   TmpRH.add:="
      {? ~.TAB.find_key(_a)
      || .TAB.REF:=_a;
         .TAB.add()
      ?}
   ";
   TmpRH.erase:="
      .TAB.erase()
   ";
   {? (ROZNE.ODD2<>null & ODD.seek(ROZNE.ODD2)) | ODD.first()
   || {!
      |? RH.prefix(ODD.ref);
         exec('set_fld','dok_fks_aut_dok','O','KU');
         {? exec('spr_war','dok_fks_aut_dok',ROZNE.ROK_F,'PPL','KU')
         || exec('set_fld','dok_fks_aut_dok','N','KU');
            {? exec('slo_typ','ext_slo','UMZLEC')
            || RU.index('K'); RU.prefix(SLO_TYP.ref);
               {? RU.first()
               || _dok:='';
                  {!
                  |? {? __optym
                     || RH.prefix(ODD.ref)
                     || RH.prefix(ODD.ref,RU.ref)
                     ?};
                     {? RH.find_ge(ROZNE.OKRO_F().POCZ)
                     || _rh_jest:=0;
                        {!
                        |? {? ($('RH.'+rodz_dat))()<=OKRO_F.KON
                           || _rh_jest:=1;
                              {? ~+RH.DOK
                              || isRH+=1;
                                 _ok:={? +DEK_NAG.TYP
                                      || exec('pozycje','dok_fks_aut_dok',ROK_F.ref,'PPL','KU',$RU.ref)
                                      || exec('pozycje','dok_fks_aut_dok',ROK_F.ref,'PPL','KU')
                                      ?};
                                 {? _ok
                                 || {? TmpRH.TAB.first()
                                    || RH.cntx_psh();
                                       {!
                                       |? {? RH.seek(TmpRH.TAB.REF,)
                                          || RH.DOK:=$DOK.ref();
                                             _dok:=RH.DOK;
                                             RH.put(1)
                                          ?};
                                          TmpRH.TAB.next()
                                       !};
                                       RH.cntx_pop();
                                       TmpRH.erase()
                                    ?}
                                 ?}
                              |? ~__optym & RH.DOK<>_dok
                              || exec('add_err','dok_fks_aut_dok','','Rachunek ze zlecenia '+form(RH.ZLE().NU)+' za okres: '
                                      +$(OKRO_F.POCZ~1)+'-'+$(OKRO_F.POCZ~2)
                                      +' zadekretowano wcześniej - pominięto.','KU');
                                 RH.next()
                              || RH.next()
                              ?}
                           || 0
                           ?}
                        !};
                        {? ~__optym & ~_rh_jest
                        || exec('add_err','dok_fks_aut_dok','','Rachunki z umów ('+form(RU.O)+') za okres: '+$(OKRO_F.POCZ~1)+'-'+$(OKRO_F.POCZ~2)+' - Brak pozycji.','KU')
                        ?}
                     || {? ~__optym
                        || exec('add_err','dok_fks_aut_dok','','Rachunki z umów ('+form(RU.O)+') za okres: '+$(OKRO_F.POCZ~1)+'-'+$(OKRO_F.POCZ~2)+' - Brak pozycji.','KU')
                        ?}
                     ?};
                     {? __optym || 0 || RU.next ?}
                  !}
               ?}
            ?}
         ?};
         ROZNE.ODD2=null & ODD.next()
      !}
   ?};
   VAR_DEL.delete('TmpRH','IsDek');
   RH.cntx_pop();
   RH.ndx_drop(&_RHndx);
   ODD.cntx_pop()
?};
VAR_DEL.delete('optym','rodz_dat');
{? ~_a
|| VAR_DEL.delete('__kom')
?};
exec('done','dok_fks_aut_dok');
exec('stop_zlecenia','dok_fks_aut_dok');
_ok


\okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia okres parametru
::   WE: _a - okres
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.index('FIRMA_NR');
OKRO_F.prefix(REF.FIRMA);
{? var_press('_a')>0 || OKRO_F.seek(_a) || SSTALE.AO ?};
OKRO_F.win_sel('WER');
{? OKRO_F.select(,1,5)
|| OKRO_F.ref()
|| ~~
?}


\odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia jednostkę księgową parametru
::   WE: _a - jednostka ksiegowa
::----------------------------------------------------------------------------------------------------------------------
ODD.index('ODDZIALY');
ODD.clear();
ODD.win_sel('WYB');
ODD.f_clear();
ODD.f_set('OD,N',
      'left join USERSDEP using(USERSDEP.DEPT,ODD.REFERENCE)',
      '(:_a=1 or USERSDEP.USERS=:_b) and ODD.FIRMA=:_c',Perm.hasFull('FJKS'),OPERATOR.USER,REF.FIRMA);
_odd:=~~;
{? var_press('_a')>0 || ODD.f_seek(_a) || ODD.f_first() ?};
{? ODD.select(,1,5)
|| _odd:=ODD.ref()
?};
ODD.f_clear();
_odd

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 b5ae9752035944187da1778eede0e402d92a5bded95c1b39ac8cd26dfbf891254539aa8f5335aa09de2633b2c511734e2949ae4d82346410f065a2ef25e172642b18d544352fad4d448591d31a3f3f717da68894173c21b501fc07a2c8d7ba5a967c7a81f9f1bbe341cc08da59992cb53467951f381bdc2785d50f9a6f296e13

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fst_ewz_zpwd.fml
:: Utworzony: 19.02.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWZ_ZPWD - Zestaw. planowanej amortyzacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zestaw. planowanej amortyzacji - główna formuła czynności FST_EWZ_ZPWD.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
exec('lista','!fst_ewz_zpwd')


\lista
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Lista dostępnych raportów
::----------------------------------------------------------------------------------------------------------------------
_RPLAN:=tab_tmp(2,'LP','STRING[3]','Lp',
                  'NAZWA','STRING[60]','Nazwa',
                  'RUN','STRING[255]','Run');
_win:=_RPLAN.mk_sel('Raporty'@,'P',0,'_winrplan',35,5,20,,'U');
_RPLAN.win_fld(_win,,'LP',,,3,,,'Lp.'@);
_RPLAN.win_fld(_win,,'NAZWA',,,50,,,'Nazwa'@);
_RPLAN.win_act(_win,,'Formuła','Wybierz'@@,,,,"sel_exit()",1,,,,'W');
_RPLAN.win_sel(_win);


_RPLAN.blank();
_RPLAN.LP:='01.';
_RPLAN.NAZWA:='Podsumowania dla firmy';
_RPLAN.RUN:='exec(\'f_podsumowanie\',\'fst\')';
_RPLAN.add();

_RPLAN.blank();
_RPLAN.LP:='02.';
_RPLAN.NAZWA:='Tabela planowanej amortyzacji';
_RPLAN.RUN:='exec(\'tabela\',\'!fst_ewz_zpwd\')';
_RPLAN.add();

{? _RPLAN.select()
|| {? _RPLAN.RUN=''
   || FUN.emsg('Niepoprawnie skonfigurowany raport. Uruchomienie niemożliwe.'@)
   || _dalej:=1;
:: jeżeli z pulpitu to należy wskazać wersję planu
      {? form(cur_win())=''
      || SRSP.use('srspr'+REF.FIRMA().SYMBOL);
         SRSP.index('NAZWA');
         SRSP.prefix();
         _win:=exec('create_srsp_win','!fst_ewz_zpwd');
         SRSP.win_sel(_win);
         {? ~SRSP.select()
         || _dalej:=0
         || exec('SRD','object');
            SRD.maski(SRSP.MASKA);
            {? SRSR.size()=0
            || FUN.emsg('Brak danych we wskazanej wersji planu amortyzacji.'@);
               _dalej:=0
            ?}
         ?}
      ?};
      {? _dalej || ($(_RPLAN.RUN))() ?}
   ?}
?}





\tabela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Raport - tabela planowanej amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.TOR_D='' || exec('czytaj','#stalesys',,FINFO,'TOR_D') ?};
{? FINFO.TOR_D='T'
|| EDIT_ES.win_edit('TOR_D')
|| EDIT_ES.win_edit('TOR')
?};

{? ~EDIT_ES.edit() || return() ?};

VAR_DEL.delete('__tb','__idx_tb');
__tb:=tab_tmp(3,'ODD','STRING[8]','J. księgowa',
                'NRI','STRING[18]','Nr inwentarzowy',
                'ROK','STRING[4]','Rok',
                'NST','STRING[100]','Nazwa',
                'ODD2','STRING[8]','J. księgowa',
                'NRI2','STRING[18]','Nr inwentarzowy',
                'NST2','STRING[100]','Nazwa',
                'AM_01','REAL','01',
                'AM_02','REAL','02',
                'AM_03','REAL','03',
                'AM_04','REAL','04',
                'AM_05','REAL','05',
                'AM_06','REAL','06',
                'AM_07','REAL','07',
                'AM_08','REAL','08',
                'AM_09','REAL','09',
                'AM_10','REAL','10',
                'AM_11','REAL','11',
                'AM_12','REAL','12',
                'RAZEM','REAL','Razem',
                'METODA','STRING[3]','Metoda',
                'STAWKA','REAL','Stawka',
                'REF','INTEGER','Ref');
__idx_tb:=__tb.index('?');
_idx_tmp:=__tb.ndx_tmp(,,'REF',,,'ROK',,);

_win:=__tb.mk_sel('Tabela planowanej amortyzacji'@,'N',0,'_tabplanamo',1,1,20,,'U',,,,,'maximized');
__tb.win_fld(_win,,'NRI2',,,15,,,'Nr inwentarzowy'@);
__tb.win_fld(_win,,'NST2',,,19,,,'Nazwa'@);
__tb.win_fld(_win,,'ODD2',,,10,,,'J. księgowa'@);
__tb.win_fld(_win,,'ROK',,,4,,,'Rok'@);
__tb.win_fld(_win,,'AM_01',,,9,2);
__tb.win_fld(_win,,'AM_02',,,9,2);
__tb.win_fld(_win,,'AM_03',,,9,2);
__tb.win_fld(_win,,'AM_04',,,9,2);
__tb.win_fld(_win,,'AM_05',,,9,2);
__tb.win_fld(_win,,'AM_06',,,9,2);
__tb.win_fld(_win,,'AM_07',,,9,2);
__tb.win_fld(_win,,'AM_08',,,9,2);
__tb.win_fld(_win,,'AM_09',,,9,2);
__tb.win_fld(_win,,'AM_10',,,9,2);
__tb.win_fld(_win,,'AM_11',,,9,2);
__tb.win_fld(_win,,'AM_12',,,9,2);
__tb.win_fld(_win,,'RAZEM',,,11,2);
__tb.win_act(_win,,'Wyświetl',,,,,"exec('tab_show','!fst_ewz_zpwd')");
__tb.win_act(_win,,'Formuła','Druku&j'@@,,,,"exec('rep_exec','#b_report','FST_EWZ_ZPWD','fst_ewz_tab',,,,,,,,'W')",
             1,,,,'J',,'icon=print');
__tb.win_sel(_win);

SRSR.cntx_psh(); SRST.cntx_psh();
SRSR.prefix();
__tb.index(_idx_tmp);
{? SRSR.first()
|| _licznik:=0;
   _size:=SRSR.size();
   {! |?
      _licznik+=1;
      _procent:=ceil((_licznik/_size)*100);
      _txt:='Trwa generowanie danych... %1%%'@[form(_procent)];
      progress(_procent,_txt,'Tabela planowanej amortyzacji'@,0);
      SRST.index('PODAT');
      SRST.prefix(SRSR.ref());
      _ref:=null;
      {? SRST.first()
      || {! |?
            {? _ref<>SRSR.ref() || _full:=1 || _full:=0 ?};
            _ref:=SRSR.ref();
            {? __tb.find_key(#SRSR.ref(),$SRST.ROK)
            || ($('__tb.AM_'+form(SRST.OKRES,-2)+':=SRST.AMO'+EDIT_ES.TOR))();
               {? SRST.OKRES=12
               || __tb.RAZEM:=__tb.AM_01+__tb.AM_02+__tb.AM_03+__tb.AM_04+__tb.AM_05+__tb.AM_06;
                  __tb.RAZEM+=__tb.AM_07+__tb.AM_08+__tb.AM_09+__tb.AM_10+__tb.AM_11+__tb.AM_12
               ?};
               __tb.put()
            || __tb.blank();
               __tb.ROK:=$SRST.ROK;
               {? _full
               || __tb.ODD:=__tb.ODD2:=SRSR.ODD().OD;
                  __tb.NRI:=__tb.NRI2:=SRSR.NRI;
                  __tb.NST:=__tb.NST2:=SRSR.NST
               || __tb.ODD:=SRSR.ODD().OD;
                  __tb.NRI:=SRSR.NRI;
                  __tb.NST:=SRSR.NST
               ?};
               __tb.REF:=#SRSR.ref();
               ($('__tb.AM_'+form(SRST.OKRES,-2)+':=SRST.AMO'+EDIT_ES.TOR))();
               {? EDIT_ES.TOR='P'
               || __tb.METODA:=SRST.MP().K;
                  __tb.STAWKA:=SRST.STAP
               |? EDIT_ES.TOR='F'
               || __tb.METODA:=SRST.MF().K;
                  __tb.STAWKA:=SRST.STAF
               |? EDIT_ES.TOR='D'
               || __tb.METODA:=SRST.MD().K;
                  __tb.STAWKA:=SRST.STAD
               ?};
               {? SRST.OKRES=12
               || __tb.RAZEM:=__tb.AM_01+__tb.AM_02+__tb.AM_03+__tb.AM_04+__tb.AM_05+__tb.AM_06;
                  __tb.RAZEM+=__tb.AM_07+__tb.AM_08+__tb.AM_09+__tb.AM_10+__tb.AM_11+__tb.AM_12
               ?};
               __tb.add()
            ?};
            SRST.next()
         !}
      ?};
      SRSR.next()
   !};
   prgs_clr()
?};
__tb.index(__idx_tb);
SRSR.cntx_pop(); SRST.cntx_pop();

{? __tb.first()
|| __tb.select()
|| FUN.info('Brak danych do wyświetlenia.'@)
?};
VAR_DEL.delete('__tb','__idx_tb')


\tab_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na wyświetl w onie raportu 02.
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh(); SRST.cntx_psh();
SRSR.prefix();
{? SRSR.seek(__tb.REF,SRSR.name())
|| SRST.index('PODAT');
   SRST.prefix(SRSR.ref(),#__tb.ROK,12);
   {? SRST.first()
   || SRD.showAsset()
   || FUN.emsg('Błąd - nie odnaleziono danych środka.'@)
   ?}
?};
SRSR.cntx_pop(); SRST.cntx_pop()


\create_srsp_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Tworzy okno z listą wersji planow amortyzacji
::----------------------------------------------------------------------------------------------------------------------
_win:=SRSP.mk_sel('Wybierz wersję planu amortyzacji'@,'P',,'srspwin',,,,,'U');
SRSP.win_fld(_win,,'NAZWA',,,20);
SRSP.win_fld(_win,,'OD',,,5);
SRSP.win_fld(_win,,'DO',,,5);
SRSP.win_fld(_win,,'R_OPIS',,,30,,,'Rodzaj'@);
SRSP.win_act(_win,,'Formuła','Wybierz'@@,,'Wybór wersji planu'@,"","sel_exit()",1,,,,'W');
_win

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 5c48ae7beb3fb2e69677cbe33915152c631c645a7459544057f3a244b95e1a61e7606725d79204d41d71b1d60ac4b541871ce50c0aaa217de6a003a7c6c4f6ecd2529beac962c7669c35cfd6323ccb82869fdaa2f68947e290b719ccd4cc53e2b618e07f2841a9d43509a0d9553eb449cb1d1b1abe493539cfeba46ff366a781

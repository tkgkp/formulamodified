:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dawr.fml
:: Utworzony: 02.08.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DAWR - Uzupełnianie wyróżników
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL  [7.20]
:: OPIS: Funkcja wywolywana z systemu FIKS z opcji: Ewidencje pomocnicze ->
::       zapisy ksiegowe z wyroznikami->przegladanie zapisow.
::       Funkcja umozliwia przeglad i uzupelnienie wyroznikow dla pozycji
::       dokumentow zrodlowych w wybranym okresie obrachunkowym
::  OLD: \sel_dok/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ

{? PAR_SKID.get(35)='T'
|| exec('ini_tpow','dok_fks');
   ROZRACH.TABELA:='POZ';
   POZ.cntx_psh();
   KA.PREFIX:='';
   {? OPERATOR.DEPT
   || POZ.index('CZY_WYRO');
      POZ.prefix(1,'T','T',OPERATOR.DEPT)
   || POZ.index('CZY_WYR');
      POZ.prefix(1,'T','T')
   ?};
   POZ.win_sel('WYR');
   POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?}+'3');
   exec('hdr_poz','wyrozniki');
   {? MLEX.FIKSB & var_pres('T_BZ')<0
   || exec('ob_bz','ml_zad')
   ?};
   __Zn_dz:=1;
   DOK.cntx_psh();
   {? MLEX.FIKSB
   || exec('edit_wyr','ml_fiks',(SSTALE.AO().ZAM='N' & exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER,0)=0))
   || POZ.select(,,,{? (SSTALE.AO().ZAM='N' & exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER,0)=0) || 'R' || 'P' ?})
   ?};
   DOK.cntx_pop();
   &__Zn_dz;
   obj_del(TPOW);
   POZ.cntx_pop()
?};
1


\poz_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Wydruk listy brakujacych wyroznikow
::  OLD: \poz_wyr/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FKS_DKS_DAWR','fks_poz_wyr',,,,,,,,'W')


\ae_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Walidacja parametrow w poz_wyr.rpm
::  OLD: \ae_par/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
{? WYROZN.WYBOR_SL
|| {? |WYROZN.SLOWYR2=''
   || FUN.info('Proszę wybrać słownik wyróżników.'@); 'SLOWYR2'
   || _ok:=0;
      TAB_S.cntx_psh();
      {? TAB_S.first()
      || {! |?
            {? TAB_S.NAZ=WYROZN.SLOWYR2 || _ok:=1 ?};
            ~_ok & TAB_S.next()
         !}
      ?};
      TAB_S.cntx_pop();
      {? ~_ok
      || FUN.info('Nie znaleziono słownika wyróżników o podanej nazwie.'@); 'SLOWYR2'
      || ''
      ?}
   ?}
|| ''
?}


\bvwybslu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [17.00]
:: OPIS: Przed wyswietleniem pola WYROZN.WYBOR_SL
::----------------------------------------------------------------------------------------------------------------------
WYROZN.efld_opt('ZAK_NWP','enable='+$WYROZN.WYBOR_SL,,'SLOWYR2');
''


\aewybslu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Po redakcji pola WYROZN.WYBOR_SL
::  OLD: \aewybslu/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~WYROZN.WYBOR_SL || WYROZN.SLOWYR2:='' ?}; 1


\f3slowyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Na klawisz F3 dla pola WYROZN.SLOWYR2
::  OLD: \f3slowyr/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
WYROZN.SLOWYR2:={? TAB_S.select(,1,,'Z')
                || TAB_S.NAZ
                || ''
                ?}


\beslowyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Po redakcji pola WYROZN.SLOWYR2
::  OLD: \beslowyr/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
WYROZN.WYBOR_SL


\ae_maska
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Po redakcji pola WYROZN.MASKA1
::  OLD: \ae_maska/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
{? +WYROZN.MASKA1<35
|| WYROZN.MASKA1+=(35-+WYROZN.MASKA1)*'?'
?}; 1


\aeslowyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Po redakcji pola WYROZN.SLOWYR2
::  OLD: \aeslowyr/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
{? |WYROZN.SLOWYR2<>'' & TAB_S.find_key(WYROZN.SLOWYR2)
|| WYROZN.SLOWYR2:=TAB_S.NAZ
?};
1


\roz_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL  [8.10]
:: OPIS: Funkcja akcji 'wyrozniki' z  poziomu okienek wertowania tabeli POZ
::  OLD: \roz_poz/skid_wyr.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(35)='T' & PAR_SKID.get(36)='T'
|| {? _=0 & POZ.KOM<>null
   || POZ.KOM().KS(); _k:=KS.ref()
   || KS.index('SYM');
      KS.prefix();
      {? _=0 || _a:=POZ.KON ?};
      _k:={? _a='' || -1 |? KS.find_key(SSTALE.AR,SSTALE.AR().SYNT+_a) || KS.ref || null ?}
   ?};
   {? _k=-1
   || FUN.info('Brak konta w pozycji dokumentu.'@); 0
   |? _k<>null
   || KS_W.index('LP');
      KS_W.prefix(_k);
      {? KS_W.first()
      || KS_W.win_sel('SLO_UZU');
         KS_W.select(,1)
      || FUN.info('Nie zdefiniowano słowników wyróżników\n dla konta pozycji dokumentu.'@); 0
      ?}
   || FUN.info('Niewłaściwe konto w pozycji dokumentu.'@); 0
   ?}
|| FUN.info('Niedostępna akcja redagowania wyróżników.'@); 0
?}


\pow_show_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uzupełnienie wyróżnika
::   WE: _a - czy edycja? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_bo:=exec('roz_bo','dok_fks');
KS_W.SLU().SLU();
SLO.win_dict('ONE'); SLO.win_sel('ONE');
{? KS_W.ROZDZ='T' | _bo
|| exec('roz_pow','!fks_dks_dawr',_a)
|| exec('red_pow','!fks_dks_dawr',_a)
?}


\roz_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL  [8.10]
:: OPIS: Funkcja redakcji wyroznikow z rozdzielnikiem z akcji 'wyrozniki' z
::       poziomu okienek wertowania tabeli POZ
::   WE: _a - czy edycja? 1-tak 0-nie
::  OLD: \roz_pow/skid_wyr.fml
::----------------------------------------------------------------------------------------------------------------------
KS_W.SLU().SLU();
_wer:={? SSTALE.AO().NR<>0 || 'WER' || 'WER2' ?};
POW.win_edit();
POW.win_patt('SZUK');
exec('ust_sum','wyrozniki');
_btnwin:=POW.mk_edit(,,,,,,'wrapped');
_btnzam:=POW.win_ebtn(_btnwin,'text=%1,btn_label_align=center,panel=top,align=end,display=1'['&Zamknij okno'@],"'key:Esc'");
POW.btn_eopt(_btnwin,_btnzam,'tooltip='+exec('help_red_esc','#window','P'));
_grp:=POW.grp_make('Rozdzielnik wartości dekretu'@,,'#grpowsel');
POW.grp_sel(_grp,,_wer,,,,,,,,,,'maximized_with_title');
POW.grp_splt(_grp,,'horizontal','bottom',35);
POW.grp_edit(_grp,,_btnwin,,,,,,'maximized');
POW.win_sel(_grp);
POW.actions(_wer,{? _a=0 || 'DPU:D' || '' ?});
POW.select()


\red_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL  [8.10]
:: OPIS: Funkcja redakcji wyroznika bez rozdzielnika z akcji 'wyrozniki' z
::       poziomu okienek wertowania tabeli POZ
::   WE: _a - czy edycja? 1-tak 0-nie
::  OLD: \red_pow/skid_wyr.fml
::----------------------------------------------------------------------------------------------------------------------
KS_W.SLU().SLU();
POW.index('POZ');
POW.prefix(POZ.ref(),KS_W.LP);
POW.win_edit('RED');
{? (SSTALE.AO().ZAM='N' & exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER,0)=0) & _a
|| n:={? ~POW.first() || POW.blank(); POW.SLO:=KS_W.ST; POW.KW:=POZ.SUM$2; 1 || 0 ?};
    {? POW.edit("{? REF.WFIRM & (var_pres('__KS_W')<0 | __KS_W=0)
                || {? exec('test_slo','slo_slu')=0
                   || FUN.emsg('Nie można wybrać pozycji związanej z inną firmą.'@); 0
                   || 1
                   ?}
                || 1
                ?}")
   || {? POW.SLO<>null || {? n || POW.add() || POW.put ?}
                       || {? n=0 || POW.del() ?}
      ?}
   ?};
   &n
|| {? POW.first() || POW.display() || FUN.info('Nie podano wyróżnika.'@) ?}
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 5551dbcde53ec0da68bc91537add3e8f9d399b2bb1260afe7c8629bac5408ea9b42d26cdac700c9bf10f70cfca97ae098f665509b2c2c44920932302e1ec913d4e1e70eebdb0804256ec811b4ed8d8141804aeb818f7c673afe8c5b5f79cacd59e531a1f4ebe3b0126d20beff7fbeeab2bf1a7ef9dca32008a15a65e39fbbd90

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_dopo.fml
:: Utworzony: 02.11.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_DOPO - Odblokowanie planowania w okr.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Odblokowanie planowania dla okresu rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=T
::# kind=WY, symbol=S_PLAN, type=STRING, name=Status blokady planowania, required=N
::
_par:=params_get();
params_set(_par);
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;
_uidref:=exec('ref2uid','#table',_in.A_OKR);
_result:='';

{? _uidref=''
|| _mp.error(exec('error','!prc_czp_dopo','Nie znaleziono okresu rozliczeniowego.'@))
|? _mp.pathProc() | _mp.pathTodo()
|| _result:=exec('odblokowanie','!prc_czp_dopo',_in,_out);
   {? _out.S_PLAN='O'
   || _mp.save(,_out);
      _mp.done()
   |? var_pres('_result')=type_of('')
   || _mp.error(_result)
   |? _result=2
   || _mp.keep()
   || _mp.save(,_out);
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Zablokowanie planowania dla okresu rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a_okr:=params_get().mp.load(exec('kind_in','#b_port')).A_OKR;
_tab:=obj_new('ZAW_DANE','A_OKRN_NAZWA','OD','DO');
{! _ii..obj_len(_tab) |! _tab[_ii]:='' !};
A_OKR.cntx_psh();
{? A_OKR.seek(_a_okr,,1)
|| _tab.ZAW_DANE:='T';
   _tab.A_OKRN_NAZWA:=A_OKR.NAZ().NAZ;
   _tab.OD:=A_OKR.OD$1;
   _tab.DO:=A_OKR.DO$1
?};
A_OKR.cntx_pop();

{? _tab.ZAW_DANE='T'
|| 'Odblokuj planowanie w okresie: %1 %2 %3'@@[_tab.A_OKRN_NAZWA,_tab.OD,_tab.DO]
|| 'Odblokuj planowanie w okresie'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Szczegóły komunikatu o błędzie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Odblokowanie okresu do planowania nie jest możliwe.'@,_a]


\odblokowanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Odblokowanie planowania dla okresu rozliczeniowego.
::   WE: _a - parametry wejściowe czynności
::       _b - parametry wyjściowe czynności
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:=_result:='';
A_OKR.cntx_psh();
{? A_OKR.seek(_a.A_OKR,,1)
|| {? A_OKR.S='Z'
   || _result:=_msg:=exec('error','!prc_czp_dopo','Okres rozliczeniowy jest zamknięty.'@)
   |? A_OKR.S_PLAN='O'
   || _result:=_msg:=exec('error','!prc_czp_dopo','Okres rozliczeniowy jest odblokowany.'@)
   || _tab:=tab_tmp(,'RESULT','INTEGER','');
      _ed:=_tab.mk_edit('Okres: %1'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1],0);
      _tab.win_efld(_ed,AH,'H',,,,,,'Okres rozliczeniowy zostanie odblokowany.'@);
      exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:Esc'",,
         exec('help_red_zakoncz','#window','PKD_I'),exec('text_red_zakoncz','#window','PKD_I'));
      exec('ok_esc','#window',_tab,_ed,1,,"cur_tab().RESULT:=0; 'key:Esc'",0,,
         exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
      _tab.win_edit(_ed);
      _tab.edit();
      {? _tab.RESULT=1
      || params_exec('a_okr_wer_bflo','okres',0);
         A_OKR.seek(_a.A_OKR,,1,1);
         {? A_OKR.S_PLAN='O'
         || _b.S_PLAN:='O';
            _msg:='Okres rozliczeniowy %1 został odblokowany.'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1];
            _result:=1
         || _msg:='Okres rozliczeniowy %1 nie został odblokowany.'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1];
            _result:=_msg
         ?}
      || _result:=2
      ?};
      obj_del(_tab)
   ?}
|| _result:=_msg:=exec('error','!prc_czp_dopo','Nie udało się odnaleźć okresu rozliczeniowego do odblokowania.'@)
?};
{? +_msg || FUN.emsg(_msg) ?};
A_OKR.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 7de8b5f85b72003004a5b79fb934a2a447fc608b2341ee16e80461ee6fdaa5595cc45e56121c23559912af556ff0819f84ce4902a52dcf271642022ad899d9f590f587b82f7e454767bde0c9b74470896cb0eb6023f48e14b8764d5775b9398622db73010dac3cc2afbd3a9538192dfe86f2d3724381886da24af061ebdec1df

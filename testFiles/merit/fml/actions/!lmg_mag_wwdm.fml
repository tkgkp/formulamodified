:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_mag_wwdm.fml
:: Utworzony: 07.08.2017
:: Autor: Mario
::======================================================================================================================
:: Zawartość: Utworzenie dokumentu magazynowego w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LMG
::# kind=WE,   symbol=ND,      type=_ND,    name=Dokument,      required=T, keyref=T
::# kind=WE,   symbol=CZY_PDF, type=STRING, name=Utwórz PDF,    required=N, keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE,   symbol=PRN_MSK, type=STRING, name=Maska wydruku, required=N, keyref=N, fml_val="exec('rep_exec','#b_report','LSP_MAG_WWDM','lmg_mag_*','Wydruki dokumentów',-1)"
::# kind=WY,   symbol=DOKUM,   type=_DOKUM, name=Załącznik,     required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init','lmg');

_akcja:=_mp.akcja();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' ?};
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || _in.PRN_MSK
      || ''
      ?};

{? var_pres('ND',_in)=type_of(null()) & _in.ND
||
:: Ustawienie kontekstu wg instancji elementu w procesie
   ND.cntx_psh();
   ND.prefix();
   {? ND.seek(_in.ND)
   ||
      _dokum:=exec('druk_dok','magdok_wydruk',_czy_pdf,_msk);
      {? _czy_pdf & _dokum<>null()
      ||
         _out.DOKUM:=_dokum;
         _mp.save(,_out);
         {? ~_keep || _mp.done() ?}

      |? ~_czy_pdf & _dokum
      ||
         {? ~_keep || _mp.done() ?}
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};
   ND.cntx_pop()
||
   _mp.error('Brak parametru wejściowego ND.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ND',_in)<>type_of(~~) & _in.ND
|| 'Utwórz PDF dokumentu: %1'@@[exec('record','#to_string',_in.ND)]
|| ''
?}


\mag_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument magazynowy - Drukuj
::   WE: [_a] - akcja
::----------------------------------------------------------------------------------------------------------------------
exec('mag_druk_gr','!lmg_mag_wwdm',0,'LMG_MAG_WWDM','Area Drukuj');
~~


\mag_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument magazynowy - Utwórz PDF
::   WE: [_a] - akcja
::----------------------------------------------------------------------------------------------------------------------
exec('mag_druk_gr','!lmg_mag_wwdm',1,'LMG_MAG_WWDM','Area Utwórz PDF')


\mag_druk_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Wydruk dokumentów magazynowych
::   WE: _a - utworz pdf 0-nie 1-tak
::       _b - UID czynności
::       _c - nazwa akcji
::       [_d] - maska wydruku
::       [_e] - 0- wydruk z okna tabeli ND (domyślnie), 1- wydruk z okna tabeli DK
::---------------------------------------------------------------------------------------------------------------------
_czy_pdf:=_a;
_act_uid:=_b;
_akcja:=_c;
_msk:={? var_pres('_d')=type_of('') || _d || '' ?};
_dk_czy:={? var_pres('_e')=type_of(0) || _e || 0 ?};

_czysc:="VAR_DEL.delete('__GRP_DR')";
_czysc();
:: __GRP_DR - srodowisko grupowego drukowania
__GRP_DR:=obj_new('GR','SEL','SIZE','FIRST','ESC','WZ','PDF','SHOW_PDF','DR_N_AKC','JEZYK','JEZYK_KH','DR_N_CAN');

:: __GRP_DR.SEL, _Sel - wybrane dokumenty
{? _dk_czy
|| {? DK.sel_size()>0
   || DK.sel_adel()
   ?};
   __GRP_DR.SEL:=_Sel:=DK.sel_aget();
   __GRP_DR.GR:=0;
   _Sel.REF:=#DK.ref(); _Sel.CRC:=DK.crc(); _Sel.add()
|| __GRP_DR.SEL:=_Sel:=ND.sel_aget();
   {? _Sel.first()
   || __GRP_DR.GR:=1
   || __GRP_DR.GR:=0;
      _Sel.REF:=#ND.ref(); _Sel.CRC:=ND.crc(); _Sel.add()
   ?}
?};

__GRP_DR.SIZE:=_Sel.size();
:: __GRP_DR.FIRST - drukowanie pierwszego dokumentu sposrod zaznaczonych 0-nie 1-tak
__GRP_DR.FIRST:=1;
:: __GRP_DR.SHOW_PDF - wyswietlac utworzone pdf 0-nie 1-tak
__GRP_DR.SHOW_PDF:=0;
:: __GRP_DR.PDF - utworz pdf 0-nie 1-tak
__GRP_DR.PDF:=_czy_pdf;
:: __GRP_DR.WZ - wybrany wzorzec wydruku
__GRP_DR.WZ:=_msk;
:: __GRP_DR.ESC - zrezygnowano z wypelnienia parametrow wydruku 0-nie 1-tak
__GRP_DR.ESC:=0;
:: _ok - stan dzialania funkcji 0-nie ok 1-ok
_ok:=1;

ND.cntx_psh();
{? _dk_czy
|| DK.cntx_psh()
?};

{? _ok=1
|| {? __GRP_DR.GR
   || __GRP_DR.SHOW_PDF:={? __GRP_DR.PDF || FUN.ask('Wyświetlić wygenerowane dokumenty?'@) ?}
   ?};
   {? __GRP_DR.DR_N_AKC=-1 || __GRP_DR.DR_N_AKC:=0 ?}
?};
:: Drukowanie
{? _ok
||
   _loop:=_Sel.first();
   {!
   |? _loop
   |! {? _dk_czy
      || _ok:=DK.seek(_Sel.REF,) & ND.seek(DK.N)
      || _ok:=ND.seek(_Sel.REF,)
      ?};
      {? _ok
      ||
         _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:=_act_uid;
         _params.UIDREF:=ND.uidref();
         _params.AKCJA:=_akcja;
         _params.GRUPA:={? __GRP_DR.GR || 'T' || 'N' ?};
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ND',ND.ref());
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'CZY_PDF',{? _czy_pdf || 'TAK' || 'NIE' ?});

         exec('mp_run','#b__box',_params);

         obj_del(_params);
         __GRP_DR.FIRST:=0
      ?};
      _loop:=__GRP_DR.ESC=0 & {? _czy_pdf || _Sel.next() || 0 ?}
   !}
?};
{? ~_dk_czy
|| ND.sel_adel()
?};
ND.cntx_pop();
{? _dk_czy
|| DK.cntx_pop()
?};
_czysc();
0

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 8f097241aa8c432ed38353bf26b8a51fcc31f9a3964876c51a607648a0c1811074012c7c902d6a79fbfc22341ed653f259505cb782bfb3fe7c8b9d3b3e3f913bac35c7c66f436416ece15e116f93897e5452d83c59b4b11f48de23d90f2f5482bff50f3c8d2645b940a9125ce6c27c50aa81dbc924aacf6f33239bf5799a81d7

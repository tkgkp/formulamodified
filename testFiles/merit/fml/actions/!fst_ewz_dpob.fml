:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fst_ewz_dpob.fml
:: Utworzony: 19.02.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWZ_DPOB - Obliczanie planów amortyzacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Generowanie danych dla planu amortyzacji - główna formuła czynności FST_EWZ_DPOB.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS

SRD.generuj();
{? FUN.ask('Obliczyć plan amortyzacji?'@)
|| SRD.liczPlan()
?}


\liczplan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła oblicza plan amortyzacji
::----------------------------------------------------------------------------------------------------------------------
SRD.liczPlan()


\get_free
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: zwraca wolny znak maski dla nowego planu (STRING[1])
::   WY: wolny znak maski dla nowego planu (STRING[1])
::----------------------------------------------------------------------------------------------------------------------
SRSP.cntx_psh();
SRSP.index('SRSP');
SRSP.prefix();
:: w dostępnych znakach brak 'r' - to znak zarezerwowany dla rzeczywistych danych
_txt:='abcdefghijklmnopqstuwxyz0123456789';
_i:=1;
{! |?
   _znak:=exec('mid','#string',_txt,_i,1);
   SRSP.prefix(_znak);
   {? SRSP.first() || _i+=1; _znak:='' || _i:=(+_txt)+1 ?};
   _i<=(+_txt)
!};
SRSP.cntx_pop();
_znak


\srsp_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła dodaje nowy plan amortyzacji
::----------------------------------------------------------------------------------------------------------------------
_znak:=exec('get_free','!fst_ewz_dpob');
{? _znak<>''
|| SRSP.blank();
   SRSP.MASKA:=_znak;
   {? SSTALE.AO<>null
   || SRSP.OKRO_F:=SSTALE.AO;
      SRSP.ROK_F:=SSTALE.AR;
      SRSP.OD:=SRSP.DO:=SRSP.OKRO_F().RES
   ?};
   {? SRSP.edit("exec('chk_srsp','!fst_ewz_dpob','ADD')")
   || {? SRSP.R='W' || SRSP.R_OPIS:='Wszystkie'
      |? SRSP.R='T' || SRSP.R_OPIS:='Trwałe'
      |? SRSP.R='N' || SRSP.R_OPIS:='Niskocenne'
      ?};
      SRSP.add()
   ?}
|| FUN.emsg('Maksymalna liczba wariantów planów amortyzacji została już osiągnięta.'@)
?}


\chk_srsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła weryfikuje poprawność wypełnienia okna edycyjnego planu amortyzacji
::   WE: 'ADD' tryb dodawania, 'EDIT' tryb edycji
::   WY: '' lub akronim pola
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRSP,,'NAZWA','OD','DO','DATA','OKRO_F');
{? _wy=''
|| {? SRSP.OD>SRSP.DO
   || FUN.emsg('Nieprawidłowy zakres planu: od roku - do roku.'@);
      _wy:='OD'
   ?}
?};
{? _wy=''
|| _first:=exec('first_rok_pocz','fst');
   {? SRSP.DATA<_first
   || FUN.emsg('Data wprowadzenia nie może być wcześniejsza\nniż pierwszy rok prowadzenia ewidencji środków.'@);
      _wy:='DATA'
   ?}
?};
{? _wy=''
|| _first:=exec('first_okr_podat','fst');
   {? SRSP.OD<_first
   || FUN.emsg('Rok początkowy planu nie może być wcześniejszy\nniż pierwszy rok prowadzenia ewidencji środków.'@);
      _wy:='OD'
   ?}
?};
{? _wy=''
|| _last:=exec('last_okr_podat','fst');
   {? SRSP.OD>_last
   || FUN.emsg('Rok początkowy planu nie może być późniejszy\nniż ostatni rok prowadzenia ewidencji środków.'@);
      _wy:='OD'
   ?}
?};
{? _wy=''
|| {? SRSP.OD>2100 | SRSP.DO>2100 | SRSP.OD>SRSP.DO
   || FUN.emsg('Nieprawidłowa lub nieprawdopodobna wartość zakresu lat.'@);
      _wy:='OD'
   ?}
?};
{? _wy=''
|| {? SRSP.OKRO_F().RES<>SRSP.OD
   || FUN.emsg('Dane źródłowe powinny pochodzić z roku będącego początkiem zakresu lat planu.'@);
      _wy:='OKRO_F'
   ?}
?};
_wy


\srsp_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła poprawia plan amortyzacji
::----------------------------------------------------------------------------------------------------------------------
SRSP.get();
{? SRSP.AKCEPT='T'
|| FUN.emsg('Planów zaakceptowanych nie można poprawiać.'@);
   return()
?};
SRD.maski(SRSP.MASKA);
SRSR.prefix();
{? SRSR.first()
|| {? ~FUN.ask('Plan amortyzacji został wygenerowany, modyfikacja parametrów planu\n'
               'może wymagać ponownego wygenerowania danych. Kontynuować?'@)
   || return()
   ?}
?};
{? SRSP.r_lock(1,1)
|| BUFSP.save();
   {? SRSP.edit("exec('chk_srsp','!fst_ewz_dpob','EDIT')")
   || {? SRSP.R='W' || SRSP.R_OPIS:='Wszystkie'
      |? SRSP.R='T' || SRSP.R_OPIS:='Trwałe'
      |? SRSP.R='N' || SRSP.R_OPIS:='Niskocenne'
      ?};
      SRSP.put();
:: weryfikacja czy zmieniły się parametry związane z generowaniem planów
      _change:=0;
      {? SRSP.OD<>BUFSP.get('OD') || _change:=1
      |? SRSP.DO<>BUFSP.get('DO') || _change:=1
      |? SRSP.OKRO_F<>BUFSP.get('OKRO_F') || _change:=1
      ?};
      {? _change
      || SRSP.GEN:='N';
         {? exec('delete_dpob','fst')
         || SRSP.put()
         || FUN.emsg('Proces usuwania danych planu amortyzacji nie powiódł się.'@)
         ?}
      ?}
   ?};
   SRSP.r_unlock()
|| FUN.emsg('Plan amortyzacji zablokowany przez innego użytkownika.'@)
?}


\srsp_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła usuwa plan amortyzacji
::----------------------------------------------------------------------------------------------------------------------
SRSP.get();
{? SRSP.AKCEPT='T'
|| FUN.emsg('Planów zaakceptowanych nie można usunąć.'@);
   return()
?};
{? SRSP.r_lock(1,1)
|| {? FUN.ask('Usunąć plan amortyzacji: %1?'@[SRSP.NAZWA])
   || {? exec('delete_dpob','fst')
      || SRSP.r_unlock();
         {? SRSP.count()=0
         || SRSP.del()
         || FUN.emsg('Nie udało się usunąć danych planu amortyzacji: %1.'@[SRSP.NAZWA])
         ?}
      || SRSP.r_unlock();
         FUN.emsg('Proces usuwania danych planu amortyzacji nie powiódł się.'@)
      ?}
   || SRSP.r_unlock()
   ?}
|| FUN.emsg('Plan amortyzacji zablokowany przez innego użytkownika.'@)
?}


\f3_srsp_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na F3 w polu SRSP.OD
::   WY: rok (INT) lub ~~
::----------------------------------------------------------------------------------------------------------------------
_sql:='select distinct RES from OKRO_F where RES >0';
_tmp:=sql(_sql);
{? _tmp.first()
|| _win:=_tmp.mk_sel('Lata'@,,,'_tmp_lata',10,5,15,,'U');
   _tmp.win_fld(_win,,'RES',,,10,,,'Rok'@);
   _tmp.win_act(_win,,'Formuła','Wybierz'@@,,,,"sel_exit()",1,,,,'W');
   _tmp.win_sel(_win);
   {? _tmp.select() || _tmp.RES || ~~ ?}
|| ~~
?}


\f3_srsp_do
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na F3 w polu SRSP.OD
::   WY: rok (INT) lub ~~
::----------------------------------------------------------------------------------------------------------------------
_sql:='select distinct RES from OKRO_F where RES >0';
_tmp:=sql(_sql);
{? _tmp.first()
|| _win:=_tmp.mk_sel('Lata'@,,,'_tmp_lata',10,5,15,,'U');
   _tmp.win_fld(_win,,'RES',,,10,,,'Rok'@);
   _tmp.win_act(_win,,'Formuła','Wybierz'@@,,,,"sel_exit()",1,,,,'W');
   _tmp.win_sel(_win);
   {? _tmp.select() || _tmp.RES || ~~ ?}
|| ~~
?}


\srsp_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła akceptuje wariant planu amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRSP.AKCEPT='T'
|| FUN.emsg('Bieżący plan amortyzacji jest już zaakceptowany.'@);
   return()
?};
{? SRSP.GEN='N'
|| FUN.emsg('Dla bieżącego planu amortyzacji nie wygenerowano danych, dlatego nie można go zaakceptować.'@);
   return()
?};
SRSR.cntx_psh();
SRSR.use('srsr'+SRSP.MASKA+REF.FIRMA().SYMBOL);
SRSR.index('PLAN');
SRSR.prefix();
{? ~SRSR.first()
|| FUN.emsg('Bieżący plan amortyzacji nie zawiera środków. Zaakceptowanie planu nie jest możliwe.'@);
   SRSR.cntx_pop();
   return()
?};
SRSR.prefix('T','N');
{? SRSR.first()
|| FUN.emsg('Bieżący plan amortyzacji zawiera środki, dla których wymagane jest przeliczenie\n planowanej amortyzacji.'
            'Zaakceptowanie planu nie jest możliwe.'@);
   SRSR.cntx_pop();
   return()
?};
SRSR.cntx_pop();

{? SRSP.r_lock(1,1)
|| {? FUN.ask('Zaakceptować plan amortyzacji?'@)
   || SRSP.AKCEPT:='T';
      SRSP.put()
   ?}
|| FUN.emsg('Plan amortyzacji zablokowany przez innego użytkownika.'@)
?}


\srsp_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wycofuje akceptację wariant planu amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRSP.AKCEPT='N'
|| FUN.emsg('Bieżący plan amortyzacji nie jest zaakceptowany.'@)
|| {? SRSP.r_lock(1,1)
   || {? FUN.ask('Wycofać akceptację?'@)
      || SRSP.AKCEPT:='N';
         SRSP.put()
      ?}
   || FUN.emsg('Plan amortyzacji zablokowany przez innego użytkownika.'@)
   ?}
?}


\bl_srsp_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wartość początkowa pola SRSP.AKCEPT
::----------------------------------------------------------------------------------------------------------------------
{? var_press('SrspAkc')>0 || SrspAkc || 'N' ?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:44 014277936a877faeb160433f21ccf4c87e97248dd9b39ce640d71fac9f4558f201a770f7165f7d1a093c05fe87568aaa8f5eb21e60b9773c33a22cdbb6ad0010ce81c7a4a5c75ace10a28512ec93d518f02a765b1bff7fd296640558ccc7fd23763b253caad07eba8433bb313e2f6602665b1170f4a47566b2e3e81486627629

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmo_mob_dwwm.fml
:: Utworzony: 14.02.2023
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Czynność LMO_MOB_DWWM - Operacja wydania magazynowego - wysłanego na urządzenie
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# parses=exec('parses_nd','lmg')
::# kind=WE,  symbol=ND,      type=_ND,      name=Dokument magazynowy,           required=N,   keyref=T
::# kind=WE,  symbol=NRC,     type=NUMBER,   name=Nr czytnika,                   required=N,   fml_val="EANX.NRC_Z:=0;exec('wyb_czyt','magazyn_mob',1,'EANX.NRC_Z');EANX.NRC_Z"
::# kind=WE,  symbol=GEN_OPE, type=STRING,   name=Generacja operacji,            required=N
::# kind=WEW, symbol=EANN,    type=_EANN,    name=Operacja mobilna,              required=N
::# kind=WY,  symbol=EANN,    type=_EANN,    name=Operacja mobilna,              required=N
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

exec('init','lmo');

_akcja:=_mp.akcja();
_auto:=_akcja<>'Do wysyłki' & _mp.isAutoRun();
_proc:=~_mp.isMicro();

_mp.trigRef('EANN',,1,,exec('kind_internal','#b_port'),'EANN');

_nrc:={? var_pres('NRC',_in)=type_of(0) || _in.NRC || -1 ?};

{? ~(var_pres('ND',_in)=type_of(null()) & _in.ND)
|| _mp.error('Brak wymaganego parametru ND.')
||
   _continue:=0;
   _eann:=null();
   _ctrl:=1;

   ND.cntx_psh();
   _jest:=ND.seek(_in.ND);
   {? ~_jest || ND.prefix(); _jest:=ND.seek(_in.ND) ?};
   {? ~_jest
   || _mp.error('Nie znaleziono dokumentu magazynowego.')
   |? ~exec('ctrlND','magazyn_mobi',2)
   || _mp.cancel()
   || {? _akcja='Do wysyłki'
         | _auto
         | _mp.pathTodo()
      || _eann:=exec('wysND','magazyn_mobi',_auto,_nrc,2,_proc)
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   ND.cntx_pop();

   {? _eann<>null()
:: Zapisanie parametru wyjściowego EANN, wykluczenie kolejnej realizacji z pętli, zakończenie czynności
   || _mp.save(exec('kind_out','#b_port'),'EANN',EANN.ref());
      _mp.done()
   ?}
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_in','#b_port'));

_wgr:={? var_pres('ND',_in)<>type_of(~~) & _in.ND
      || _in.ND
      || null()
      ?};

{? _wgr<>null()
|| 'Zarejestruj operację wydania w magazynie dla dokumentu %1'@@[exec('record','#to_string',_wgr)]
|| 'Zarejestruj operację wydania w magazynie'@@
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 766dea867398875c55c00a3b0fbc24c1d52bd96f2dbbd2a8b12268172bcefa7a722483191ee3ffa76a2a9c8b2912a68e3df21f90bfe2046212b1b4959ed8e76a93f15dfe4305772e45b29c9b4ebd43689b4c3881658355c49c51626d3e1066943108dfa4fa7d3103a6ce7bc98bbe52f55f8b2003720394100d5e68a4ca0b655a

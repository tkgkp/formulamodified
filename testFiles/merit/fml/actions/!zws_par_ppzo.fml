:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ppzo.fml
:: Utworzony: 12.04.2023
:: Autor: MicKoc
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PPZO - Typy oświadczeń pracy zdalnej
::            Czynność jest potrzebna wyłącznie ze względu na uprawnienia.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
::  MOD: IGPTASZE [22.26]
:: OPIS: Typy oświadczeń pracy zdalnej - główna formuła czynności.
::  OLD: \menu_ppsf_no/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_NO.cntx_psh();
PPSF_NO.index('KOD');
PPSF_NO.prefix(exec('firma','ustawienia'));
PPSF_NO.win_edit('RED');
PPSF_NO.win_sel('WER');
PPSF_NO.select();
PPSF_NO.cntx_pop();
~~


\ppsf_no_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_no_addb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_no_modb','!zws_par_ppzo') & exec('ppsf_no_chk','!zws_par_ppzo',0)


\ppsf_no_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_no_putb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_no_modb','!zws_par_ppzo') & exec('ppsf_no_chk','!zws_par_ppzo',1)


\ppsf_no_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Popraw - po" dla tabeli PPSF_NO.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::  OLD: \ppsf_no_puta/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

:: Pielęgnacja typów.
_wx_size:=exec('ppsf_no_wx_size','ppsf');
_wb:=_wa:=0;
{! _lp:=1 .. _wx_size
|? _wb=0 & _wa=0
|! {? bfld('W%1'[$_lp])='T'
   || _wb:=1
   ?};
   {? ($('PPSF_NO.W%1=\'T\''[$_lp]))()
   || _wa:=1
   ?}
!};
{? _wb<>_wa
|| PPSFT.cntx_psh();
   PPSFT.index('PPSF_NO');
   PPSFT.prefix(PPSF_NO.ref());
   {? PPSFT.first()
   || {!
      |? {? PPSFT.SKL_WN='T'
::          Niech zadziała magia (wyzwalaczy).
         || PPSFT.put()
         ?};
         PPSFT.next()
      !}
   ?};
   PPSFT.cntx_pop()
?};
~~


\ppsf_no_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed modyfikacją rekordu.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_no_modb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_wx_size:=exec('ppsf_no_wx_size','ppsf');
{! _lp:=1 .. _wx_size
|! {? ($('PPSF_NO.W%1<>\'T\' & PPSF_NO.W%1<>\'N\''[$_lp]))()
   || ($('PPSF_NO.TR%1:=\'\''[$_lp]))()
   ?}
!};
1


\ppsf_no_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
::  MOD: IGPTASZE [22.26]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli PPSF_NO.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::  OLD: \ppsf_no_chk/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;

__CHK.validate(PPSF_NO,
   $("_a.table(_b,"+$_put+",,'FIRMA','KOD','OPIS','SYSTEM','W1','TR1')"),
   "_a.in_set(_b,'W1',,'T','N')",
   " _ok:=1;
     _wx_size:=exec('ppsf_no_wx_size','ppsf');
     {! _lp:=2 .. _wx_size
     |? _ok
     |! _ok:=_a.in_set(_b,'W%1'[$_lp],,'T','N','')
     !};
     _ok
   ",
   " _ret:='';
     _wx_size:=exec('ppsf_no_wx_size','ppsf');
     {! _lp:=2 .. _wx_size
     |? {? ($('PPSF_NO.W%1=\\'T\\' | PPSF_NO.W%1=\\'N\\''[$_lp]))()
        || _ret:=_a.record(_b,,'TR%1'[$_lp]);
           _ret=''
        || 1
        ?}
     !};
     _ret
   "
)


\ppsf_no_kod_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed redagowaniem pola PPSF_NO.KOD
::   WE:
::   WY:
::  OLD: \ppsf_no_kod_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_NO.SYSTEM<>'T'


\ppsf_no_wx_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed redagowaniu pól PPSF_NO.Wx (dla x>1).
::       Oświadczenie N jest dostępne, jeżeli oświadczzenie N-1 jest użyte.
::   WE:
::   WY:
::  OLD: \ppsf_no_wx_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
($('PPSF_NO.W%1=\'T\' | PPSF_NO.W%1=\'N\''[$(#(cur_afld()+1)-1)]))()


\ppsf_no_wx_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pól PPSF_NO.Wx.
::   WE:
::   WY:
::  OLD: \ppsf_no_wx_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()=''
|| _wx_size:=exec('ppsf_no_wx_size','ppsf');
   {! _lp:=#(cur_afld()+1) .. _wx_size
   |! ($('PPSF_NO.W%1:=\'\''[$_lp]))()
   !}
?};
exec('ppsf_no_efld_opt','!zws_par_ppzo')


\ppsf_no_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_no_dolacz_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_NO.blank();
exec('ppsf_no_efld_opt','!zws_par_ppzo','*',PPSF_NO,'RED');
1


\ppsf_no_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_no_popraw_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_PO.cntx_psh();
PPSF_PO.index('UNIQUE');
PPSF_PO.prefix(PPSF_NO.ref());
_ile:=PPSF_PO.size();
PPSF_PO.cntx_pop();
{? _ile
|| FUN.info('W systemie wprowadzone już zostały oświadczenia (%1) pracowników danego typu.\nZmiany niemożliwe.'@
            [$_ile]);
   return(0)
?};
exec('ppsf_no_efld_opt','!zws_par_ppzo','*',PPSF_NO,'RED');
1


\ppsf_no_aktywuj_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Obsługa akcji "Aktywuj - przed" w oknie WER tabeli PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_no_aktywuj_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_NO.AKT:='T';
PPSF_NO.put()


\ppsf_no_dezakt_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Obsługa akcji "deZaktywuj - przed" w oknie WER tabeli PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_no_dezakt_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_NO.AKT:='N';
PPSF_NO.put()


\ppsf_no_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER tabeli PPSF_NO
::   WE:
::   WY:
::  OLD: \ppsf_no_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? PPSF_NO.AKT='T'
|| PPSF_NO.actions_grayed('WER','A')
|| PPSF_NO.actions_grayed('WER','E')
?};
''


\ppsf_no_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Rekord po tabeli PPSF_NO
::   WE:
::   WY:
::  OLD: \ppsf_no_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_no_chk','!zws_par_ppzo',-menu_txt()='popraw')


\ppsf_no_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_no_bw/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_no_efld_opt','!zws_par_ppzo','*',PPSF_NO,'RED');
PPSF_NO.display()


\ppsf_no_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli PPSF_NO.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE: [_a] [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują się pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::  OLD: \ppsf_no_efld_opt/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(PPSF_NO)
|| _TAB:=_b
|? _tryb='1'
|| _TAB:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? _fld='' | (+_fld=2 & 1+_fld='W')
|| _ok:=1;
   _wx_size:=exec('ppsf_no_wx_size','ppsf');
   {! _lp:=1 .. _wx_size
   |! {? _lp>1
      || _TAB.efld_opt(_we,'enable=%1'[$_ok],,'W%1'[$_lp])
      ?};
      _val:=($('_a.W%1=\'T\' | _a.W%1=\'N\''[$_lp]))(_TAB);
      _ok:=_ok & _val;
      _TAB.efld_opt(_we,'enable=%1'[$_val],,'TR%1'[$_lp])
   !}
?};

1

:Sign Version 2.0 jowisz:1045 2023/06/27 11:10:45 08ea1bb7631c5668668cb29d079f7fdce5de9064bc37fa7883362f5af91253fbabcb941e454b90194ba916badbe321230a705f1b26fec1d9e38046dd23d42f7cc0732425e265c5e7da3754b46a5f662ba3dab0e4d321e188aa6fc3ec4652536bd857a788d61e6f2465a0bdc99ef3112a03cb7737f5addc5758fd1143dbf1bc42

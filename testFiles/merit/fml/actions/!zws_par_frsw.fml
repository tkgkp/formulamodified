:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_frsw.fml
:: Utworzony: 29.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_FRSW - Schematy windykacyjne
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Schematy windykacyjne - główna formuła czynności ZWS_PAR_FRSW.
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys');
SLO.win_dict('ONE'); SLO.win_sel('ONE');
SER_DEF.cntx_psh(); SER_DEF.win_dict('SERWZW');

_ok:=SER_SCH.grp_make('Schematy windykacyjne'@,,'ser_def_umor');
SER_SCH.grp_sel(_ok,,'WER','Wszystkie'@,"exec('ar_ser_tm','!zws_par_frsw',SER_SCH.get() & SER_SCH.SER_TM)"
                ,,,15,"exec('winsch','!zws_par_frsw')",,,,'maximized');
SER_SCH.grp_sel(_ok,SER_WOW,'WER','Domyślne'@,
                "exec('ar_ser_tm','!zws_par_frsw',SER_WOW.get() & SER_WOW.SER_SCH & SER_WOW.SER_SCH().SER_TM)"
                ,,,15,"exec('windoms','!zws_par_frsw')",,,,'maximized');
SER_SCH.grp_splt(_ok,,'horizontal','left');
SER_SCH.grp_sel(_ok,SER_TPOZ,'WERS','Poziomy trybu monitowania'@,,,,,
               "exec('ser_tpoz_before','!zws_par_frsw')","",0,,'maximized');
SER_SCH.grp_sel(_ok,SER_TPOZ,'WERS1','Poziomy trybu monitowania'@,,,,,
               "exec('ser_tpoz_before','!zws_par_frsw')","",0,,'maximized');
SER_SCH.win_sel(_ok);
SER_SCH.select();
SER_DEF.cntx_pop();
VAR_DEL.delete('zakltpoz');
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
''


\ar_ser_tm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu okienka ze schematem
::   WE: _a - czy w schemacie jest ustawiony SER_TM
::----------------------------------------------------------------------------------------------------------------------
zakltpoz:='';
{? ~_a | SER_SCH.SER_TM().TYP=0
|| tab_show(1,'left');
   tab_hide(2,,'left');
   zakltpoz:='WERS';
   SER_TPOZ.win_edit('RED')
|| tab_show(2,'left');
   tab_hide(1,,'left');
   zakltpoz:='WERS1';
   SER_TPOZ.win_edit('RED1')
?};
ile_dni1:=ile_dni2:=0;
SER_TPOZ.index('SER_TPOZ');
{? _a & SER_SCH.SER_TM || SER_TPOZ.prefix(SER_SCH.SER_TM) || SER_TPOZ.prefix(null) ?};
{? zakltpoz<>'' || grp_disp(SER_TPOZ,zakltpoz) ?};
1


\winsch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wszystkie schematy windykacyjne
::  OLD: \winsch/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.index('KOD'); SER_SCH.prefix();
{? ~SER_SCH.get() || SER_SCH.first() ?};
1


\windoms
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Domyślne schematy windykacyjne
::  OLD: \windoms/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.KONTRAH:=null;
SER_KOR.GRKH:=null;
SER_WOW.index('KH'); SER_WOW.prefix(null,null);
{? ~SER_WOW.get() || SER_WOW.first() ?};
1


\ser_tpoz_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą okienka wertowania tabeli SER_TPOZ
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.SER_TM
|| SER_TPOZ.actions(zakltpoz,'',,1)
|| SER_TPOZ.actions(zakltpoz,'DpuM:D',,1)
?};
1


\po_s_mon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Po redakcji pola SER_SCH.CZY_MON
::  OLD: \po_s_mon/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.CZY_MON='N'
|| SER_SCH.SER_TM:=null
?};
1


\sch_tm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcją pola SER_SCH.SER_TM
::  OLD: \sch_tm/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.CZY_MON='T'


\sch_tmae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Po redakcji pola SER_SCH.SER_TM
::  OLD: \sch_tmae/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.SER_TM=null
|| SER_SCH.CZY_ODS1:='N';
   {? SER_SCH.CZY_ODS='N' || SER_SCH.ODS:=null ?}
?};
1


\po_s_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Po redakcji pola SER_SCH.CZY_BL
::  OLD: \po_s_bl/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.CZY_BL='N'
|| SER_SCH.BL_WAR:='R'; SER_SCH.FOR_WAR:=null;
   SER_SCH.BL_BWR:='R'; SER_SCH.FOR_BWR:=null
?};
1


\sch_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcja pól SER_SCH.BL_BWR, SER_SCH.BL_WAR
::  OLD: \sch_bl/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.CZY_BL='T'


\po_s_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Po redakcji pola SER_SCH.BL_WAR
::  OLD: \po_s_bw/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.BL_WAR<>'F' || SER_SCH.FOR_WAR:=null ?}; 1


\sch_b_fb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcją pola SER_SCH.FOR_BWR
::  OLD: \sch_b_fb/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
ZMIENNE.RODZAJ:='B';
SER_SCH.BL_BWR='F'


\sch_b_fw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcją pola SER_SCH.FOR_WAR
::  OLD: \sch_b_fw/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
ZMIENNE.RODZAJ:='B';
SER_SCH.BL_WAR='F'


\po_s_bb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Po redakcji pola SER_SCH.BL_BWR
::  OLD: \po_s_bb/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.BL_BWR<>'F' || SER_SCH.FOR_BWR:=null ?}; 1


\po_s_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Po redakcji pól SER_SCH.CZY_ODS  i SER_SCH.CZY_ODS1
::  OLD: \po_s_od/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.CZY_ODS='N' || SER_SCH.DNI_ODS:=SER_SCH.ODSPNOTA:=0 ?};
{? SER_SCH.CZY_ODS1='N' || SER_SCH.ODSPWEZW:=0  ?};
{? SER_SCH.CZY_ODS='N' & SER_SCH.CZY_ODS1='N' || SER_SCH.ODS:=null ?};
1


\ods_wezw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcją pola SER_SCH.CZY_ODS1
::  OLD: \ods_wezw/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.SER_TM<>null


\sodsszuk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Wzorzec - slownik odsetek
::  OLD: \sodsszuk/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? -(6+menu_txt)='szukaj'
|| SLO.win_dict('ONE'); SLO.win_sel('ONE');
   FINFO.SL_ODS().SLU()
?};
''


\sch_ods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcją pola SER_SCH.ODS
::  OLD: \sch_ods/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.SL_ODS<>null & (SER_SCH.CZY_ODS='T' | SER_SCH.CZY_ODS1='T')
|| FINFO.SL_ODS().SLU();
   SLO.win_dict('ODS'); SLO.win_sel('ODS');
   SLO.win_edit('RED');
   1
|| SER_SCH.ODS:=null; 0
?}


\be_czyzw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMA [8.40]
:: OPIS: Przed redakcja pola CZY_ZWPL tabeli SER_SCH
::  OLD: \be_czyzw/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
1


\ae_czyzw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMA [8.40]
:: OPIS: Po redakcji pola CZY_ZWPL tabeli SER_SCH
::  OLD: \ae_czyzw/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.CZY_ZWPL<>'T' || SER_SCH.ZW_PL:=0 ?};
1


\be_zw_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA][8.50]
:: OPIS: Przed redakcją pola ZW_PL tabeli SER_SCH.
::  OLD: \be_zw_pl/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.CZY_ZWPL='T'


\beopnota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcją pola SER_SCH.ODSPNOTA
::  OLD: \beopnota/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.CZY_ODS='T'


\beopwezw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcją pola SER_SCH.ODSPWEZW
::  OLD: \beopwezw/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.CZY_ODS1='T'


\pop_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Popraw Przed w okienku tabeli SER_SCH
::  OLD: \pop_sch/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('be_wart','rozrach')
|| {? SER_SCH.count()=0
   || 1
   || FUN.info('Definicja schematu windykacyjnego jest powiązana z kontrahentami lub stałymi systemu.'@); 1
   ?}
?}


\chk_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja rekord po w okienku wertowania tabeli SER_SCH
::  OLD: \chk_sch/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
_v:='';
{? SER_SCH.KOD=''
|| FUN.info('Nie podano kodu schematu windykacyjnego.'@); _v:='KOD'
?};
{? _v='' & SER_SCH.OPIS=''
|| FUN.info('Nie podano opisu schematu windykacyjnego.'@); _v:='OPIS'
?};
{? _v='' & SER_SCH.CZY_MON='T' & ~SER_SCH.SER_TM
|| FUN.info('Nie wybrano trybu monitowania.'@); _v:='SER_TM'
?};
{? _v='' & ~SER_SCH.ODS
|| FUN.info('Nie wybrano tabeli odsetek.'@); _v:='ODS'
?};
{? _v='' & SER_SCH.BL_WAR='F' & ~SER_SCH.FOR_WAR
|| FUN.info('Nie wybrano blokowania warunkowego.'@); _v:='FOR_WAR'
?};
{? _v='' & SER_SCH.BL_BWR='F' & ~SER_SCH.FOR_BWR
|| FUN.info('Nie wybrano blokowania bezwarunkowego.'@); _v:='FOR_BWR'
?};
{? _v=''
|| {? SER_SCH.CZY_ZWPL='T' & ~SER_SCH.ZW_PL
   || FUN.info('Nie podano liczby dni do uwzględniania zwłoki w płatności.'@); _v:='ZW_PL'
   ?}
?};
_v


\kop_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Kopiowanie schematu windykacyjnego
::  OLD: \kop_sch/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.KOD:='';
SER_SCH.win_edit('RED');
{! |?
   {? SER_SCH.edit("exec('chk_sch','!zws_par_frsw')")
   || {? SER_SCH.add(1)
      || 0
      || FUN.info('Istnieje schemat o podanym kodzie.'@); 1
      ?}
   ?}
!}


\be_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.40]
:: OPIS: Przed akcją Szukaj w tabeli SER_SCH
::  OLD: \be_sch/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SLO.win_dict('ONE'); SLO.win_sel('ONE'); 1


\pow_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja powiązania w okienku wertowania tabeli SER_SCH
::  OLD: \pow_sch/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_WOW.cntx_psh(); SER_WOW.index('SER_SCH'); SER_WOW.prefix(SER_SCH.ref());
{? SER_WOW.first()
|| SER_WOW.win_sel('WER2');
   SER_WOW.select()
|| FUN.info('Brak powiązań do schematu.'@)
?};
SER_WOW.cntx_pop()


\pow_dr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Wydruk schematu windykacyjnego
::  OLD: \pow_dr/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','ZWS_PAR_FRSW','fks_ser_sch',,,,,,,,'W')


\rp_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Rekord przed w okienku wertowania tabeli SER_WOW
::  OLD: \rp_pow/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_WOW.KH=null & SER_WOW.GRKH=null
|| SER_KOR.KTO:='Schemat domyślny'; 'SER_WOW#01#01'
|? SER_WOW.KH<>null & SER_WOW.GRKH=null
|| SER_WOW.KH();
   SER_KOR.KTO:='Kh: '+KH.KOD+' '+KH.NAZ; ''
|? SER_WOW.KH=null & SER_WOW.GRKH<>null
|| SER_WOW.GRKH();
   SER_KOR.KTO:='Gr: '+GRKH.KOD+' '+GRKH.NAZ; ''
?}


\bl_k_wow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Wartość początkowa pola SER_WOW.KH
::  OLD: \bl_k_wow/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.KONTRAH


\bl_g_wow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Wartość początkowa pola SER_WOW.GRKH
::  OLD: \bl_g_wow/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.GRKH


\chk_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Rekord po w okienkach wertowania tabeli SER_WOW
::  OLD: \chk_pow/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
_a:='';
{? SER_WOW.DATA_OD=date(0,0,0)
|| FUN.info('Nie wprowadzono daty początkowej obowiązywania schematu.'@); _a:='DATA_OD'
?};
{? _a='' & ~SER_WOW.SER_SCH
|| FUN.info('Nie wybrano schametu windykacyjnego.'@); _a:='SER_SCH'
?};
{? _a='' & SER_KOR.KONTRAH=null & SER_KOR.GRKH=null
|| SER_WOW.SER_SCH();
   {? SER_SCH.CZY_MON='T' & SER_SCH.SER_TM=null
   || FUN.info('W definicji wybranego schematu brak trybu monitowania.'@); _a:='SER_SCH'
   ?};
   {? _a='' & SER_SCH.CZY_ODS='T' & SER_SCH.ODS=null
   || FUN.info('W definicji wybranego schematu brak typu tabeli odsetek'@); _a:='SER_SCH'
   ?}
?};
{? _a='' & SER_WOW.DATA_OD>SER_WOW.DATA_DO & SER_WOW.DATA_DO<>date(0,0,0)
|| FUN.info('Nie wprowadzono poprawnego zakresu dat.'@); _a:='DATA_OD'
?};
{? _a=''
|| _dat_od:=SER_WOW.DATA_OD;
   _dat_do:=SER_WOW.DATA_DO;
   {? -menu_txt<>'popraw'
   || SER_WOW.cntx_psh();
      {? _dat_do<>date(0,0,0)
      || {? SER_WOW.first()
         || {! |?
              {?  (SER_WOW.DATA_DO<>date(0,0,0) &
                   ((_dat_od>=SER_WOW.DATA_OD & _dat_od<=SER_WOW.DATA_DO) |
                   (_dat_do>=SER_WOW.DATA_OD & _dat_do<=SER_WOW.DATA_DO))) |
                  (SER_WOW.DATA_DO=date(0,0,0) &
                   SER_WOW.DATA_OD>=_dat_od & SER_WOW.DATA_OD<=_dat_do)
              || FUN.info('Daty schematów nie mogą się zawierać.'@); _a:='DATA_OD'
              ?};
              {? _a='' & SER_WOW.DATA_DO=date(0,0,0) & _dat_od>SER_WOW.DATA_OD
              || SER_WOW.DATA_DO:=_dat_od-1; SER_WOW.put()
              ?};
              _a='' & SER_WOW.next()
            !}
         ?}
      || {? SER_WOW.last()
         || {? ~(_dat_od>SER_WOW.DATA_OD & _dat_od>SER_WOW.DATA_DO)
            ||  FUN.info('Daty schematów nie mogą się zawierać.'@); _a:='DATA_OD'
            ?};
            {? _a='' & SER_WOW.DATA_DO=date(0,0,0)
            || SER_WOW.DATA_DO:=_dat_od-1; SER_WOW.put()
            ?}
         ?}
      ?};
      SER_WOW.cntx_pop()
   || _ref:=SER_WOW.ref();
      SER_WOW.cntx_psh();
      {? SER_WOW.prev() & SER_WOW.DATA_DO>=_dat_od
      || FUN.info('Data końcowa poprzedniego schematu jest większa od daty początkowej bieżącego schematu.'@);
         _a:='DATA_OD'
      ?};
      SER_WOW.cntx_pop();
      {? _a=''
      || SER_WOW.cntx_psh();
        {? SER_WOW.next()
        ||  {? _dat_do=date(0,0,0)
            || FUN.info('Data końcowa schematu nie może być zerowa. Istnieje kolejny schemat.'@);
               _a:='DATA_DO'
            ?};
            {? _a='' & _dat_do>=SER_WOW.DATA_OD
            || FUN.info('Data końcowa schematu musi być mniejsza\nod daty początkowej kolejnego schematu.'@);
                _a:='DATA_DO'
            ?}
        ?};
        SER_WOW.cntx_pop()
      ?}
   ?}
?};
_a


\sch_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Wyświetl w okienkach wertowania tabeli SER_WOW
::  OLD: \sch_pow/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_WOW.SER_SCH();
SER_SCH.win_edit('RED');
SER_SCH.display();
1


\tr_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Tryby monitowania w okienkach wertowania tabeli SER_WOW
::  OLD: \tr_pow/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_WOW.SER_SCH();
exec('tr_sch','rozrach')


\sch_grkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RL] [7.62]
:: OPIS: Akcja scHematy windykacyjne w okienku WER tabeli GRKH
::  OLD: \sch_grkh/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.KONTRAH:=null;
SER_KOR.GRKH:=GRKH.ref();
SER_WOW.index('GRKH');
SER_WOW.prefix(GRKH.ref);
SER_WOW.win_sel('GRKH');
SER_WOW.select()


\sch_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Schematy windykacyjne w okienkach wertowania kontrahentów
::  OLD: \sch_kh/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.KONTRAH:=KH.ref();
SER_KOR.GRKH:=null;
SER_WOW.index('KH');
SER_WOW.prefix(KH.ref);
SER_WOW.win_sel('KH');
SER_WOW.select()


\sch_grk2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja schematy windykacyjne dla grupy z poziomu okienka wertowania tabeli KH
::  OLD: \sch_grk2/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KH.GRKH
|| KH.GRKH();
   exec('sch_grkh','!zws_par_frsw')
|| FUN.info('Dla kontrahenta nie wskazano grupy kontrahentów.'@)
?}


\sch_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przeglądanie domyślnych schematów windykacyjnych
::  OLD: \sch_dom/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.KONTRAH:=null;
SER_KOR.GRKH:=null;
SER_WOW.index('KH'); SER_WOW.prefix(null,null);
SER_WOW.win_sel('WER'); SER_WOW.select()


\be_sch_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed usuwaniem schematu windykacyjnego
::----------------------------------------------------------------------------------------------------------------------
{? SER_SCH.count()
|| FUN.info('Schemat wykorzystywany w systemie. Usunięcie niemożliwe.'@); 0
|| 1
?}


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 19c0462c388b704a56eb5303d7198930ccca96383b8995a73a86867f8c2437a385f09fb2e69066d53d702773d7a610cf3125765c2bd95ac5c90edab650c1c574bdbb4f346d35617550dd718308edcf62efe08bbf202d67432180115eada8774a578248716adc4ada7378fc5c65f52a42b34fe44331e25a70097279a0a9976e87

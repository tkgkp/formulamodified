:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obg_fzo_zako.fml
:: Utworzony: 03.08.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBG_FZO_ZAKO - Ustalenie statusu faktury w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustalenie statusu faktury w obiegu - główna formuła czynności OBG_FZO_ZAKO
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=T, keyref=T
::# kind=WE, symbol=TYP, type=STRING, name=Ustalić status/Wycofać status, required=N, fml_val="exec('edokum_zak','obiegi_log',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status faktury w obiegu, required=N

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_service:=_mp.isService();
_auto:=_mp.isAutoRun();
{? _we.EDOKUM<>~~ & _we.EDOKUM
|| _name:=ref_name(_we.EDOKUM);
   EDOKUM.cntx_psh(); EDOKUM.use(_name); EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM)
   || {? _we.TYP=~~ | _we.TYP='' | _we.TYP='Ustalić status'
      || {? _service | _auto | FUN.ask('Ustalić status faktury w obiegu?'@)
         || EDOKUM.STATUS:=exec('ustal_status','obiegi_log',EDOKUM.uidref());
            {? EDOKUM.STATUS<>''
            || EDOKUM.B_PREL:=''; EDOKUM.B_PRELS:='';
               exec('add_elog','obiegi_akc','Zakończono obieg dokumentu',1)
            ?};
            EDOKUM.put();
            _wy.EDOKUM:=EDOKUM.ref(); _wy.STATUS:=EDOKUM.STATUS;
            _mp.save(,_wy);
            _mp.done()
         ?}
      || {? _service | _auto | FUN.ask('Wycofać status faktury w obiegu?'@)
         || EDOKUM.STATUS:=''; EDOKUM.put();
            _wy.EDOKUM:=null; _wy.STATUS:=EDOKUM.STATUS;
            _mp.save(,_wy);
            _mp.done()
         ?}
      ?}
   |? _service
   || _wy.EDOKUM:=null; _wy.STATUS:='';
      _mp.save(,_wy);
      _mp.done()
   ?};
   EDOKUM.cntx_pop()
|| _wy.EDOKUM:=null; _wy.STATUS:='';
   _mp.save(,_wy);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? _we.EDOKUM<>~~ & _we.EDOKUM
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || _desc:='Ustal status faktury w obiegu: %1, data sprzedaży %2, otrzymano %3'@@[EDOKUM.SYM,$EDOKUM.DOP,$EDOKUM.DO]
      || _desc:='Ustal status faktury w obiegu'@@
      ?};
      EDOKUM.cntx_pop()
   || _desc:='Ustal status faktury w obiegu'@@
   ?}
|| _desc:='Ustal status faktury w obiegu'@@
?};
_desc

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:39 7ed3984093eaa140b7a8595663dd659f9d12560eb625e10450818434a2c64f4d82c2a432a7c02a8509284343afb5c2dbe7b2e019b7e5ee390f101518454449c155fab208d1bebee48d9fa185bad4fbf48a354edc8668cdedd04b27e8a9e9f19d6e4b0f3802629d7cde21dccc4fc21b000b6a154d3cd13bfe960cafe96999072e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zso_scn_tdok.fml
:: Utworzony: 10.02.2020
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZSO_SCN_TDOK - DOkument księgowy z OCR
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła główna czynności ZSO_SCN_TDOK
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
:: PARAMETRY WE:
::# kind=WE, symbol=SKAN_DOK, type=_SKAN_DOK, name=Wskazanie na zeskanowany dokument, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na dokument księgowy, required=T

_args:=params_get();
_in:=_args.in;
_out:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_brak:=1;
{? var_pres('SKAN_DOK',_in)>0 & _in.SKAN_DOK
|| _ref:=BB.sqlint($_in.SKAN_DOK);
   {? _ref<>0
   || SKAN_DOK.cntx_psh();
      SKAN_DOK.prefix();
      {? SKAN_DOK.seek(_ref,)
      || _brak:=0;
         {? SKAN_DOK.STATUS='D' | _akcja='Grupowa'
         || _dok:=null;
            {? _mp.isAutoRun() | _akcja='Dokument księgowy'
            || _dok:=exec('add_dok','skanuj',1)
            |? _akcja=''
            || VAR_DEL.delete('DokRef');
               DokRef:=null;
               exec('set_edit','!zso_scn_tdok');
               SKAN_DOK.display();
               _dok:=DokRef;
               VAR_DEL.delete('DokRef')
            ?};
            {? SKAN_DOK.seek(_ref) & SKAN_DOK.STATUS='Z' & _dok<>null
            || SKAN_DOK.OKRES:=8+$_dok+4;
               SKAN_DOK.put();
               _out.DOK:=_dok;
               _mp.save(,_out);
               _mp.done()
            ?}
         |? SKAN_DOK.STATUS='Z'
         || FUN.info('Zeskanowany dokument jest już zadekretowany.');
            _brak:=2
         ?}
      ?};
      SKAN_DOK.cntx_pop()
   ?}
?};
{? _brak
|| {? _brak=1
   || FUN.info('Nie znaleziono zeskanowanego dokumentu.'@)
   ?};
   _in.SKAN_DOK:=null;
   _mp.save(,_in);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła na opis TO DO
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('SKAN_DOK',_we)>0 & _we.SKAN_DOK
|| SKAN_DOK.cntx_psh();
   SKAN_DOK.prefix();
   {? SKAN_DOK.seek(_we.SKAN_DOK)
   || _desc:='Utworzenie dokumentu księgowego z OCR: %1'@@[SKAN_DOK.SYM_ZEW]
   || _desc:='Utworzenie dokumentu księgowego z OCR'@@
   ?};
   SKAN_DOK.cntx_pop()
|| _desc:='Utworzenie dokumentu księgowego z OCR'@@
?};
_desc


\set_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Ustawia okno edycyjne
::----------------------------------------------------------------------------------------------------------------------
_okn:=SKAN_DOK.mk_edit('Zeskanowany dokument'@,,'#skandokfdok');
SKAN_DOK.win_ewin(_okn,,'INFO');
SKAN_DOK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Podgląd'@],"
   exec('show_dok','zso_scn');
   ''
");
SKAN_DOK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Zadekretuj'@],"
   _ref:=SKAN_DOK.ref();
   DokRef:=exec('add_dok','skanuj',1);
   _end:=0;
   {? DokRef<>null
   || SKAN_DOK.cntx_psh();
      _end:=SKAN_DOK.seek(_ref) & SKAN_DOK.STATUS='Z';
      SKAN_DOK.cntx_pop()
   ?};
   {? _end
   || 'key:Esc'
   || ''
   ?}
");
_an:=SKAN_DOK.win_ebtn(_okn,
                        'text=%1, btn_label_align=center, panel=bottom, align=end, display=1'['&Anuluj'@],'key:Esc');
SKAN_DOK.btn_eopt(_okn,_an,'tooltip='+exec('help_red_esc','#window','A'));
SKAN_DOK.win_edit(_okn)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? 8+ref_name(_kref)='skan_dok'
         || _ref:=exec('FindAndGet','#table',SKAN_DOK,_kref,,,null());
            {? _ref=null
            || _wy.DOK:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 6ce77e2dde8d1a1a61ee05cadc48740ad48f3391f1ca069cc0222bd88ab8277f534bf0d903954dadea54b7a1c297f36e94c13b1e7cb73822025da031ea78715c26bd734775ee5821009c4b7d4ae9bbc29055b2c0d4047ed7501f2c4d4ddfe6a8ab9352f85477295596e0abb52dd9a7fd8e781c49cbc94e8bf5710cc23fa36d79

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_ioto.fml
:: Utworzony: 27.07.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Otworzenie okresu - Magazyn
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=ROK,       type=NUMBER,   name=Rok,               required=N, fml_val="exec('rok','!lmg_mag_ioto')"
::# kind=WE,   symbol=MIESIAC,   type=NUMBER,   name=Miesiąc,           required=N, fml_val="exec('miesiac','!lmg_mag_ioto')"
::# kind=WY,   symbol=WYNIK,     type=MEMO,     name=Wynik zamknięcia,  required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: Uruchomiona akcja
_akcja:=_mp.akcja();

:: Uruchomiona automatycznie
_auto:=_akcja<>'Otwórz' & _mp.isAutoRun();

_rok:={? var_press('ROK',_in)=type_of(0) & _in.ROK || _in.ROK || ST.AR ?};
_miesiac:={? var_press('MIESIAC',_in)=type_of(0) & _in.MIESIAC || _in.MIESIAC || ST.AM ?};

_out.WYNIK:='';

OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_rok,_miesiac);
{? OKR.first()
|| _okr_naz:=OKR.NAZ;
   _zam_mag:=1+OKR.ZAM;
   _zam_spr:=1+(1-OKR.ZAM);
   _zam_zak:=1+(2-OKR.ZAM);
   _zam_umo:=OKR.ZAM+1;
   {? _zam_mag='N'
   || {? exec('FindInSet','#table','OKRD','ODDZ',OKR.MC,OKR.ROK)<>null()
      || exec('zam_dok','lmg')
      || FUN.info('Okres nie jest zamknięty.'@);
         _mp.done()
      ?}
   |? FUN.ask('Otworzyć okres %1?'@[_okr_naz])
   || OKR.ZAM:='N'+_zam_spr+_zam_zak+_zam_umo;
      {? OKR.put()
      || _out.WYNIK+='\nOkres %1 został otworzony.'@[_okr_naz];
         _mp.done()
      ?}
   ?}
|| _out.WYNIK+='Brak okresu %1/%2'+@[form(_miesiac,-2,,'9 '),form(_miesiac,-4,,'9 ')];
   _mp.done()
?};
OKR.cntx_pop();
_mp.save(,_out)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

_rok:={? var_press('ROK',_in)=type_of(0) & _in.ROK || _in.ROK || 0 ?};
_miesiac:={? var_press('MIESIAC',_in)=type_of(0) & _in.MIESIAC || _in.MIESIAC || 0 ?};

OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_rok,_miesiac);
_wyn:=
   {? OKR.first()
   || 'Otwórz okres %1 w magazynie'@@[OKR.NAZ]
   || 'Otwórz okres w magazynie'@@
   ?};
OKR.cntx_pop();
_wyn


\otworz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Otworzenie okresu - Zakup
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LMG_MAG_IOTO';
_params.AKCJA:='Otwórz';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);

OKR.cntx_psh();
OKR.prefix();
{? OKR.seek(exec('zwr_obs_okres','parses','LMG',OKRO.ref()))
||
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ROK',OKR.ROK);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'MIESIAC',OKR.MC);

   exec('mp_run','#b__box',_params)
|| FUN.info('Okres nie jest otworzony dla magazynów.'@)
?};
OKR.cntx_pop()


\rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła parametru - ROK
::   WE:
::   WY: rok lub ~~ jeśl nie wybrano roku
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
OKR.last();
_wer:=OKR.mk_sel('Wybór roku'@,,0,,,,OKR.size());
OKR.win_fld(_wer,,'ROK',,,20,,,'Rok'@);
OKR.win_act(_wer,,'Formuła','&Wybierz'@@,,,"sel_exit()",,1,,,,'W');
OKR.win_sel(_wer);
{? OKR.select(,1,OKR.size()) || _wyn:=OKR.ROK ?};
OKR.cntx_pop();
_wyn


\miesiac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła parametru - OKRES
::   WE:
::   WY: miesiąc lub ~~ jeśl nie wybrano roku
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
_Tab:=tab_tmp(1
   ,'MC'    ,'INTEGER'     ,'Nr miesiąca'
   ,'NAZ'   ,'STRING[20]'  ,'Nazwa miesiąca');
{! _ii:=1..12 |! _Tab.MC:=_ii; _Tab.NAZ:=(date(,_ii)$8)-5; _Tab.add() !};
_Tab.find_key(date()~2);
_wer:=_Tab.mk_sel('Wybór miesiąca'@,,0,,,,12);
_Tab.win_fld(_wer,,'NAZ',,,,,,'Miesiąc'@);
_Tab.win_act(_wer,,'Formuła','&Wybierz'@@,,,"sel_exit()",,1,,,,'W');
_Tab.win_sel(_wer);
{? _Tab.select(,1,12) || _wyn:=_Tab.MC ?};
_wyn

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:57 ac9cd08276e9743b6ac2d4beeb985b931dd8874f1734ec620caa5a993e3d32ab60083c9d7ce0c713e58fafb476cadb6e09a56709ef1f72e3b9bfc3aa5bd9faac527540c27a3a498341eae34c9a6b368b087b906d0bfd01bc17017743c5e135b5eca4b37487b4ea520eeede3f155b9fc64956321b713d25daf54edeff6925e594

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_fdl_dbrp.fml
:: Utworzony: 23.06.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FDL_DBRP - Rejestracja delegacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rejestracja delegacji - główna formuła czynności OBE_FDL_DBRP
::----------------------------------------------------------------------------------------------------------------------
::# portal=STD_PC_S_BusinessTripSett_PRC_PushDocStatus;STD_PC_S_BusinessTripSett_PRC_Revoke; ^
::# STD_PC_S_BusinessTripSett_SLO_D;STD_PC_S_BTWay_SLO;STD_PC_S_BTWay_SLO_D;STD_PC_S_BTWay_SLO_I;STD_PC_S_BTWay_SLO_U; ^
::# STD_PC_S_BusinessTripPlannedExpensesInfo_SLO;STD_PC_S_BusinessTripReasonInfo_SLO; ^
::# STD_PC_S_BusinessTripTimePlanInfo_SLO;STD_PC_S_PurchaseDocLinked_PRC_PurchaseDocAttachUpload; ^
::# STD_PC_S_PurchaseDocLinked_PRC_PurchaseDocRegisterBill; ^
::# STD_PC_S_PurchaseDocLinked_SLO;STD_PC_S_PurchaseDocLinked_SLO_D;STD_PC_S_BusinessContactInfo_SLO; ^
::# STD_PC_S_CostShare_SLO;STD_PC_S_CostShare_SLO_D;STD_PC_S_CostShare_SLO_I;STD_PC_S_CostShare_SLO_U; ^
::# STD_PC_S_ShowAttach_SLO;STD_PC_S_AdvancePaymentType_SLO;STD_PC_S_BusinessContact_SLO; ^
::# STD_PC_S_BusinessTripKind_SLO;STD_PC_S_BusinessTripReason_SLO;STD_PC_S_BusinessTripVehicle_SLO; ^
::# STD_PC_S_CostCategory_SLO;STD_PC_S_CostCenter_SLO;STD_PC_S_CostCenterType_SLO;STD_PC_S_CurrencyCode_SLO; ^
::# STD_PC_S_PaymentType_SLO; ^
::# STD_PC_S_BusinessTripPlan_PRC_BusinessTripRegister;STD_PC_S_BusinessTripPlan_PRC_PushDocStatus; ^
::# STD_PC_S_BusinessTripPlan_SLO;STD_PC_S_BusinessTripPlan_SLO_U;STD_PC_S_BTTimeTable_SLO;^
::# STD_PC_S_BTTimeTable_SLO_D;STD_PC_S_BTTimeTable_SLO_I;STD_PC_S_BTTimeTable_SLO_U; ^
::# STD_PC_S_BTSettlement_SLO;STD_PC_S_PurchaseDocLinked_PRC_PushDocStatus;STD_PC_S_AcceptanceHistory_SLO; ^
::# STD_PC_S_Request_SLO; ^
::# STD_PC_S_Request_PRC_PushDocStatus;STD_PC_S_Request_PRC_UndoDocStatus;STD_PC_S_Request_SLO_D; ^
::# STD_PC_S_RequestType_SLO; ^
::# STD_PC_S_PurchaseDocSimple_SLO; STD_PC_S_PurchaseDocSimple_SLO_U; ^
::# STD_PC_S_BusinessTripSett_SLO_U

::# parses=exec('parses_edokum','obiegi')
::# access=exec('uprawnienia_rej','obiegi')
::# properties=TODOTRIG,~RELEASE

:: PARAMETRY WE:
::# kind=WE, symbol=FORM_PRZ, type=STRING, name=Formuła wykonywana przy kończeniu rejestracji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_WAL, type=STRING, name=Formuła walidacyjna przy kończeniu rejestracji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_ADD, type=STRING, name=Formuła wykonywana po wprowadzeniu nagłówka dokumentu, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=ED_PODZ, type=STRING, name=Edycja podziałów controllingowych, required=N, fml_val="exec('czy_ed_podz','obiegi')"
::# kind=WE, symbol=ED_INND, type=STRING, name=Edycja informacji dodatkowych, required=N, fml_val="exec('czy_inn_dan','obiegi')"
::# kind=WE, symbol=ED_PROJ, type=STRING, name=Edycja projektów, required=N, fml_val="exec('czy_ed_proj','obiegi')"
::# kind=WE, symbol=TYP_DOK, type=STRING, name=Typ dokumentu, required=N, fml_val="exec('wybor_typu','obiegi',3,_a)", fml_exp="exec('etypy_fml_exp','obiegi2',3,_a)"
::# kind=WE, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na delegację, required=N, keyref=T
::# kind=WE, symbol=KONT_BUD, type=STRING, name=Kontrola budżetu, required=N, fml_val="exec('kont_bud','obiegi',_a)"

::# kind=WE, symbol=ED_PRZEJ, type=STRING, name=Edycja przejazdów, required=N, fml_val="exec('czy_ed_przej','obiegi')"
::# kind=WE, symbol=ED_DIET, type=STRING, name=Edycja diet, required=N, fml_val="exec('czy_ed_diet','obiegi')"
::# kind=WE, symbol=ED_WYD, type=STRING, name=Edycja wydatków, required=N, fml_val="exec('czy_ed_wyd','obiegi')"

::# kind=WE, symbol=B_ROLEF, type=STRING, name=Rola użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=B_ROLE, type=STRING, name=Domyślna rola użytkowników, required=N, fml_val="exec('rola','obiegi',_a)", fml_exp="exec('b_role_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=BLZMROL, type=STRING, name=Blokada zmiany roli, required=N, fml_val="exec('blzmrol','obiegi')"
::# kind=WE, symbol=BLZMUSR, type=STRING, name=Blokada zmiany uzytkowników, required=N, fml_val="exec('blzmusers','obiegi')"
::# kind=WE, symbol=USR_FORM, type=STRING, name=Wybór użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=JEDEN_OP, type=STRING, name=Obsługuje jeden operator - formuła, required=N, fml_val="exec('jeden_op','obiegi2')"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na delegację, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WY, symbol=EM_NAD, type=STRING, name=Adres e-mail użytkownika rejestrującego (nadawca), required=N
::# kind=WY, symbol=EM_ODB, type=MEMO, name=Adres e-mail użytkownika rejestrującego (odbiorca), required=N
::# kind=WY, symbol=EM_NAG, type=STRING, name=Nagłówek wiadomości e-mail, required=N
::# kind=WY, symbol=BUDZ_COND, type=NUMBER, name=Budżet przekroczony warunkowo?, required=N

:: PARAMETRY WEW:
::# kind=WEW, symbol=CZY_ZAK, type=STRING, name=Czy zakończenie czynności, required=N

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_wew.CZY_ZAK:='N'; _mp.save(_wew);
{? _akcja='webTermProcCancel' || return(~~) ?};

{? _akcja='Portal'
|| {? ~_mp.isMicro()
   || params_exec('ustaw_bprel','obiegi');
      EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_we.EDOKUM));
      EDOKUM.prefix();
      {? EDOKUM.seek(_we.EDOKUM)
      || _done:=1;
         {? EDOKUM.PAPERLES
         || exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL);
            _done:=exec('chk_zal','portal_paperless',_we.TYPY_ZAL,0);
            {? _done
            || exec('EDOKOS_add','portal_wnioski',EDOKUM.ref(),EDOKUM.USERS,'Y',,'S',OBIEGI.B_PRELS,'T');
               EDOKUM.STATUS:='s';
               EDOKUM.put()
            ?}
         ?};
         _mp.keyRef(EDOKUM.uidref(),1,0);
         _wy.EDOKUM:=EDOKUM.ref();
         _wy.DEKR:=EDOKUM.TYP().DEKR;
         _wy.STATUS:='A';
         _wy.EM_NAD:=_wy.EM_ODB:=OPERATOR.USER().EMAIL;
         _wy.EM_NAG:='Zarejestrowano delegację: '+EDOKUM.ID+', data operacji '+$EDOKUM.DOP;
         _wy.B_PREL:=OBIEGI.B_PREL;
         _wy.B_PRELS:=OBIEGI.B_PRELS;
         _args.context.B_PREL:=OBIEGI.B_PREL;
         {? var_pres('B_PRELS',_args.context)>0
         || _args.context.B_PRELS:=OBIEGI.B_PRELS
         ?};
         EDOKUSER.cntx_psh();
         EDOKUSER.use('skidk_'+(EDOKUM.name()+2));
         EDOKUSER.index('UNIK');
         EDOKUSER.prefix(EDOKUM.ref(),'OBE_FDL_DBRP',);
         {? EDOKUSER.first()
         || {!
            |? EDOKUSER.cntx_psh();
               EDOKUSER.prefix();
               EDOKUSER.B_PREL:=OBIEGI.B_PREL;
               EDOKUSER.B_PRELS:=OBIEGI.B_PRELS;
               EDOKUSER.put();
               EDOKUSER.cntx_pop();
               EDOKUSER.first()
            !}
         ?};
         EDOKUSER.cntx_pop();
         _mp.save(,_wy);
         _mp.keep();
         {? _done || _mp.done() ?};
         _args.context.STATUS:=1;
         {? var_pres('FORM_PRZ',_we)>0 & _we.FORM_PRZ<>''
         || _fml:=_we.FORM_PRZ;
            ($_fml)()
         ?}
      ?};
      EDOKUM.cntx_pop()
   || _mp.cancel()
   ?};
   return(~~)
?};

zak_akc_ed:=0;
budz_cond:=0;
OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
params_exec('ustaw_bprel','obiegi');
{? _akcja='Popraw'
|| params_get().context.EDOKUM;
   _mp.descTodo();
   _mp.keep()
|? _akcja='Usuń'
|| params_get().context.EDOKUM;
   exec('us_dob1','obiegi');
   {? var_pres('STATUS',_we) & _we.STATUS='O'
   || _wy.EDOKUM:=null;
      _wy.STATUS:='Z';
      _mp.save(,_wy);
      _mp.done()
   || _mp.cancel()
   ?}
|| {? ~exec('is_act','#b__box','OBE_FDL_DBRP')
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu delegacji dla bieżącej czynności.'@);
      _mp.cancel(); return(~~)
   |? _mp.isMicro()
   || {? _akcja='Dołącz' | _mp.pathProc()
      || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu delegacji dla bieżącej czynności.'@)
      || FUN.info('Delegacja nie jest w trakcie rejestracji lub nie jest obecnie obsługiwana przez zalogowanego użytkownika.'@)
      ?};
      return(~~)
   ?};
   SSTALE.AO:=null;
   exec('env_wt','b_proces');
   {? _mp.pathProc()
   || {? SSTALE.AO=null
      || FUN.info('Nie ustawiono parametrów pracy systemu.'@); _mp.cancel(); return(~~)
      ?}
   ?};
   params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
:: sprawdzenie z poziomu webterma
   {? _akcja='chkWT'
   || params_get().context.SPR_WT:=1
:: czynność startowa
   |? _akcja='Dołącz' | _mp.pathProc()
   || exec('dol_edokos','obiegi',_we);
      _mp.keyRef(EDOKUM.uidref());
      _wy.EDOKUM:=EDOKUM.ref(); _mp.save(,_wy);
      _mp.descTodo();
      web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                            'EDOKUM',#EDOKUM.ref()
                            ));
      _mp.keep()
   |? _akcja='Zakończ'
   || _wew.CZY_ZAK:='T'; _mp.save(_wew);
      params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
      params_exec('akceptuj2','obiegi_akc','W');
      {? zak_akc_ed=1 & EDOKUM.get()
      || exec('ustaw_outd','obiegi_akc',_wy,'A');
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _akcja='Zamknij2'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
      params_exec('zamknij2','obiegi_akc');
      {? zak_akc_ed & EDOKUM.get()
      || exec('ustaw_outd','obiegi_akc',_wy,'Z');
         _mp.save(,_wy);
         _mp.done()
      ?}
   ?}
?};
VAR_DEL.delete('zak_akc_ed','budz_cond');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('inDESC')=type_of(1) || return ?};
_desc:='Zakończ rejestrację delegacji'@@;
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
_we:=_mp.load(exec('kind_in','#b_port'));
_wew:=_mp.load(exec('kind_internal','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));
params_exec('ustaw_bprel','obiegi');
_edok:=null;
{? var_pres('EDOKUM',_wy)
|| _edok:=_wy.EDOKUM
|? var_pres('EDOKUM',_we)
|| _edok:=_we.EDOKUM
?};
{? _edok
|| _ref:=BB.sqlint($_edok); _nam:=form(($_edok)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || _desc:='Zakończ rejestrację delegacji: %1, data polecenia %2, delegowany %3 %4'@@[EDOKUM.ID,$EDOKUM.DW,EDOKUM.DOSTAWCA().NAZWISKO,OSOBA.PIERWSZE];
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)
      ?};
      {? var_pres('CZY_ZAK',_wew)=0 & _we.STATUS='O'
      || _wew.CZY_ZAK:='N';
         VAR_DEL.delete('inDESC'); inDESC:=1;
         _mp.save(_wew);
         VAR_DEL.delete('inDESC')
      ?};
      {? var_pres('STATUS',_we)>0 & _we.STATUS='O' &
         (var_pres('CZY_ZAK',_wew)<= 0 | (var_pres('CZY_ZAK',_wew)>0 & _wew.CZY_ZAK<>'T')) & _set
      || {? OBIEGI.B_PREL<>''
         || EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
            {? EDOKOS.first()
            || {! |?
                  EDOKOS.STATUS:='O'; EDOKOS.put();
                  EDOKOS.next()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?}
      ?};
      _wp:=EDOKUM.TYP().W_PORTAL;
      {? _wp='D' & _set
      || _stat:={? var_pres('StartProc')>0 || 'T' || 'N' ?};
         EDOKOS.cntx_psh();
         EDOKOS.index('PORTAL'); EDOKOS.prefix(EDOKUM.ref(),'S',OBIEGI.B_PREL,EDOKUM.USERS,_stat);
         {? ~EDOKOS.first()
         || EDOKOS.cntx_psh();
            EDOKOS.index('DISP'); EDOKOS.prefix('S',EDOKUM.ref(),);
            {? EDOKOS.last() & EDOKOS.OPERACJA='Y' & EDOKOS.USERS=EDOKUM.USERS & 1+EDOKOS.B_PREL='R'
            || EDOKOS.del()
            ?};
            EDOKOS.cntx_pop();
            EDOKOS.cntx_psh();
            EDOKOS.index('DISP'); EDOKOS.prefix('S',EDOKUM.ref());
            _oper_nr:={? EDOKOS.last() || EDOKOS.OPER_NUM+1 || 1 ?};
            EDOKOS.cntx_pop();
            EDOKOS.blank(1);
            EDOKOS.EDOKUM:=EDOKUM.ref();
            EDOKOS.USERS:=EDOKUM.USERS;
            EDOKOS.OPERACJA:='Y';
            EDOKOS.DATA:=date();
            EDOKOS.TIME:=time();
            EDOKOS.B_PREL:={? _stat='T' || 'R'+$_oper_nr || OBIEGI.B_PREL ?};
            EDOKOS.B_PRELS:=OBIEGI.B_PRELS;
            EDOKOS.STATUS:=_stat;
            EDOKOS.WID:='S';
            EDOKOS.OPER_NUM:=_oper_nr;
            EDOKOS.prefix();
            EDOKOS.add()
         ?};
         {? EDOKUM.STATUS='D'
         || _reason:=~~;
            EDOKOS.cntx_psh();
            EDOKOS.index('SZUK2');
            EDOKOS.prefix(EDOKUM.ref(),'D');
            {? EDOKOS.last()
            || _reason:=EDOKOS.memo_txt(,1,'UW_DL')
            ?};
            EDOKOS.cntx_pop();
            EDOKOS.prefix();
            EDOKOS.blank(1);
            EDOKOS.EDOKUM:=EDOKUM.ref();
            EDOKOS.USERS:=EDOKUM.USERS;
            EDOKOS.OPERACJA:='P';
            EDOKOS.DATA:=date();
            EDOKOS.TIME:=time();
            EDOKOS.WID:='P';
            EDOKOS.add();
            {? var_pres('_reason')>0
            || EDOKOS.memo_set(_reason,'UW_DL');
               EDOKOS.memo_put();
               EDOKOS.put()
            ?};

            EDOKUM.STATUS:='P';
            EDOKUM.put()
         ?};
         EDOKOS.cntx_pop()
      ?};

      EDOKUM.cntx_pop()
   ?}
?};
_desc


\dol_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie delegacji
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='OBE_FDL_DBRP';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
exec('env_wt','b_proces');
exec('set_wt_par','obiegi2','path_run',_path);
_par:=web_global_params_get();
{? type_of(_par)>100
|| {? var_pres('path_run',_par)<=0
   || _getx:=obj_ntab_add(_par,'path_run');
      _getx.path_run:=_path;
      web_global_params_set(_getx)
   || _par.path_run:=_path;
      web_global_params_set(_par)
   ?}
|| web_global_params_set('path_run',_path)
?};
:: uruchomienie z pulpitu
{? _path='Proc'
|| web_params_set('prel',_b);
   _result:=exec('szuk_okr','okresy',date())<>null;
   _rok_f:=exec('FindAndGet','#table',OKRO_F,__PARSES.getVal('OkresRok').OKRO_O,,"@.OKRO_F.ROK",null);
   {? _result & _rok_f<>ROZNE.UT_OKROD().ROK
   || _form:="exec('form_proc','!obe_fdl_dbrp',1,_a)";
      _ask:='W parametrach pracy wybrałeś rok: %1\nBieżący rok kalendarzowy: %2\n\nCzy chcesz zmienić rok w parametrach pracy?'@
            [SSTALE.AR().NAZ,ROZNE.UT_OKROD().ROK().NAZ];
      web_ask(_form,_ask,FUN.TYT)
   || {? ~_result
      || FUN.info('Wybrany rok w parametrach pracy jest nieaktualny.\nRok %1 nie został jeszcze założony w danych systemu.'@[$(date()~1)])
      ?};
      exec('form_proc','!obe_fdl_dbrp',0,0)
   ?}
|? _path='Todo'
|| _path_param:=_b;
   _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || _name:=((_keyrefs[1]-8)+8);
      EDOKUM.use(_name); EDOKUM.prefix();
      {? EDOKUM.seek(_keyrefs[1],,1)
      || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
         params_exec('pop_dob_wt','obiegi',3,1)
      ?}
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
:: uruchomienie z obszaru roboczego
|? _path='Area'
|| _get:=web_params_get(1);
   {? ~exec('is_act','#b__box','OBE_FDL_DBRP') |
      exec('FindAndGet','#table',B_PREL,_get.B_PREL,,"B_PREL.CLASS='B_ACTION'")
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu delegacji dla bieżącej czynności.'@)
   || _result:=exec('szuk_okr','okresy',date())<>null;
      _rok_f:=exec('FindAndGet','#table',OKRO_F,__PARSES.getVal('OkresRok').OKRO_O,,"@.OKRO_F.ROK",null);
      {? _result & _rok_f<>ROZNE.UT_OKROD().ROK
      || _form:="exec('form_obsz','!obe_fdl_dbrp',1,_a)";
         _ask:='W parametrach pracy wybrałeś rok: %1\nBieżący rok kalendarzowy: %2\n\nCzy chcesz zmienić rok w parametrach pracy?'@
               [SSTALE.AR().NAZ,ROZNE.UT_OKROD().ROK().NAZ];
         web_ask(_form,_ask,FUN.TYT)
      || {? ~_result
         || FUN.info('Wybrany rok w parametrach pracy jest nieaktualny.\nRok %1 nie został jeszcze założony w danych systemu.'@[$(date()~1)])
         ?};
         exec('form_obsz','!obe_fdl_dbrp',0,0)
      ?}
   ?}
?}


\form_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła wykonywana podczas uruchomienia procesu z pulpitu
::   WE: _a - 0/1 - scieżka ze zmianą okresu
::       _b - 0/1 - odpowiedź na pytanie
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<0 || _a:=0 ?};
{? var_press('_b')<0 || _b:=0 ?};
{? _b || exec('chk_okres','obiegi2',0,1) ?};
_get:={? _a || web_params_get(0) || web_params_get(1) ?}; web_params_set(_get);
exec('todo_run','obiegi',0);
web_params_set(exec('obj_ntab_set','#array',_get,'B_PREL',$_get.prel));
zakres:=1;
exec('init','obe');
_par1:=exec('get_act_proc_params','#b__box','OBE_FDL_DBRP',_get.prel,"");
params_exec('dol_dob_wt','obiegi',_par1,3,2)


\form_obsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła wykonywana podczas uruchomienia z obszaru roboczego
::   WE: _a - 0/1 - scieżka ze zmianą okresu
::       _b - 0/1 - odpowiedź na pytanie
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<0 || _a:=0 ?};
{? var_press('_b')<0 || _b:=0 ?};
{? _b || exec('chk_okres','obiegi2',1,1) ?};
_get:={? _a || web_params_get(0) || web_params_get(1) ?};
web_params_set(_get);
_wt_bprel:=exec('FindAndGet','#table',B_PREL,_get.B_PREL);
_parr:=exec('get_act_proc_params','#b__box',_get.ACT_UID,_wt_bprel,_get.PORTS_IN);
params_exec('dol_dob_wt','obiegi',_parr,3,0)


\pop_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie delegacji
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| {? EDOKUMW.get()
   || EDOKUM.use('skid_v'+(EDOKUMW.name()+2)); EDOKUM.prefix(); EDOKUMW.EDOKUM();
      exec('todo_run','obiegi',0);
      _dalej:=exec('env_edokum','obiegi','WD',0);
      VAR_DEL.delete('todo_run');
      {? _dalej
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='OBE_FDL_DBRP';
         _params.AKCJA:='chkWT';
         _params.UIDREF:=EDOKUM.uidref();
         _params.WT_SKIP:=1;
         _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
         _params.CONTEXT.EDOKUM:=EDOKUM.ref();
         _params.CONTEXT.SPR_WT:=0;
         exec('mp_run','#b__box',_params);
         {? _params.CONTEXT.SPR_WT
         || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
            exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
            params_exec('pop_dob_wt','obiegi',3,0)
         ?}
      ?}
   || FUN.info('Nie znaleziono delegacji.'@);
      exec('dob_refresh','obiegi','W','D')
   ?}
|| _we:=~~;
   _wy:=obj_new('EDOKUM');
   _wy.EDOKUM:=EDOKUM.ref();
   _ref:=exec('put_del','obe_faw',_we,_wy,0,2)
?}


\usu_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie delegacji
::----------------------------------------------------------------------------------------------------------------------
exec('us_dob_wt','obiegi',3)


\del_przek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zakończenie delegacji w obiegu
::   WE: _a - 1 - odpalanie z TODO na WEBTERM
::            2 - obszar roboczy tabela EDOKUM
::            0 - obszar roboczy akcja dla EDOKUMW
::            3 - odpalanie z pulpitu
::----------------------------------------------------------------------------------------------------------------------
{? ~_ || _a:=0 ?}; _dalej:=1;
{? _a=1
|| exec('todo_run','obiegi',1); todo_run:=1;
   _dalej:=exec('env_edokum','obiegi','WD',0)
|? _a=3
|| exec('todo_run','obiegi',-1); todo_run:=0;
   _dalej:=exec('env_edokum','obiegi','WD',0)
|? _a=0
|| {? EDOKUMW.get()
   || exec('todo_run','obiegi',0);
      _dalej:=exec('env_edokum','obiegi','WD',1);
      EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
      EDOKUM.prefix(); EDOKUMW.EDOKUM()
   || FUN.info('Nie znaleziono delegacji.'@);
      exec('dob_refresh','obiegi','W','D');
      _dalej:=0
   ?}
|| EDOKUMW.prefix();
   exec('todo_run','obiegi',0);
   _dalej:=exec('env_edokum','obiegi','WD',0)
?};
VAR_DEL.delete('todo_run');
{? _dalej
|| _get:=web_params_get();
   {? type_of(_get)>100 & var_pres('akcja',_get)>0
   || {? _get.akcja='popraw'
      || _dalej:=(params_exec('pop_dob_wt2','obiegi',0,_a<>1 & _a<>3)='')
      |? _get.akcja='dołącz'
      || _par:=web_global_params_get();
         _dalej:={? type_of(_par)>100 & var_pres('act_ok',_par)>0 & ~_par.act_ok
                 || params_exec('dol_dob_wt2','obiegi',0,_a<>1)=''
                 || params_exec('pop_dob_wt2','obiegi',0,_a<>1 & _a<>3)=''
                 ?}
      ?}
   ?}
?};
{? _dalej
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FDL_DBRP';
   _params.AKCJA:='chkWT';
   _params.UIDREF:=EDOKUM.uidref();
   _params.WT_SKIP:=1;
   _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
   _params.CONTEXT.EDOKUM:=EDOKUM.ref();
   _params.CONTEXT.SPR_WT:=0;
   exec('mp_run','#b__box',_params);
   {? _params.CONTEXT.SPR_WT
   || BPMN.END:=1;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
      params_exec('akceptuj','obiegi_akc','W')
   ?}
?}


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności OBE_FDL_DBRP
::  WE: _a - 'add po', 'put przed', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'OBE_FDL_DBRP')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [19.22]
:: OPIS: Podglad dokumentu obiegu z listy zadań
::----------------------------------------------------------------------------------------------------------------------
params_exec('disp_wni','obiegi2',3)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zamknięty
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? 6+ref_name(_kref)='skid_v'
         || _edok:=exec('FindAndGet','#table',EDOKUM,_kref,,,null());
            {? _edok=null()
            || _mp.done()
            |? exec('FindAndGet','#table',EDOKUM,_kref,,"@.EDOKUM.ZAM='T'",1)
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1045 2023/09/14 11:12:02 618c47c3f63b585a70c5e3a6d648f087a6fb6782ae0cb1ac72c9dbba22fae8ca6ab1fe2354888303064942ad7dba554c940e226dc505d1a26d2b79f0c25bd49155fb675f7c34b01867d05b417f7131305a48ca24fa59b3f660a7b037c9a5f3157e2e5caeee03e40511bf11e62584e088ead60505be38175fdec5491e7433cc70

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_apow.fml
:: Utworzony: 30.10.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Formuły czynności PBA_BPB_APOW - Wysłanie powiadomień.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Główna formuła czynności - Wysłanie powiadomień.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::
::# kind=WE, symbol=ZA_FORM, type=_ZA_FORM, name=Wskazanie na  formularz ankiety, required=T, keyref=T
::
:: Wynik działania czynności. Parametr szczególnie istotny w przypadku uruchomienia czynności usługowej.
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,BŁĄD,POMIŃ|xxx)", required=N
::
::# kind=WY, symbol=ZA_FORM, type=_ZA_FORM, name=Wskazanie na  formularz ankiety, required=N
::# kind=WY, symbol=SUB, type=STRING, name=Temat, required=N
::# kind=WY, symbol=TO, type=MEMO, name=Odbiorca, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N

_args:=exec('za_form_main_a','phr_poczta');
_args.warunek:="exec('warunek','!pba_bpb_apow')";
_args.tytul:="exec('tytul','!pba_bpb_apow')";
_args.tresc:="exec('tresc','!pba_bpb_apow')";
params_exec('za_form_main','phr_poczta',_args)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Powiadomienie o szkoleniu - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=params_exec('za_form_desc','phr_poczta',,1);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Wyślij powiadomienie o szkoleniu: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Wyślij powiadomienie o szkoleniu: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Wyślij powiadomienie o szkoleniu: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Wyślij powiadomienie o szkoleniu'@@
?}


\warunek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Formuła weryfikuje, czy bieżący rekord tabeli ZA_FORM może być podstawą wysłania powiadomienia.
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;

ZA_FORM.cntx_psh();
ZA_FORM.prefix();
{? ZA_FORM.get()
|| _ret:=ZA_FORM.SLO_KOD().KOD='P'
?};
ZA_FORM.cntx_pop();
_ret


\tytul
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Formuła, dla bieżącego rekordu tabeli ZA_FORM, zwraca napis będący tytułem powiadomienia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Ankieta - pośba o wypełnienie'


\tresc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Formuła, dla bieżącego rekordu tabeli ZA_FORM, zwraca napis będący treścią powiadomienia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZA_FORM.cntx_psh();
_klasa:=($ZA_FORM.ZA_ZEST().ZZ_LINK().KLASA)();
_link:=exec('pba_por_anki_link','!pba_bpb_apow',ZA_FORM.ID);
_ret:='<center><h4>Ankieta</h4></center>'+
   'Dzień dobry,'+
   '<br>'+
   'Zgodnie z procedurami obowiązującymi w '+KST.NAZWA+' w zakresie '+
   {? _klasa=SZK_OPIS || 'szkoleń pracowniczych'
   |? _klasa=RP_PROC || 'rekrutacji'
   || 'pozyskiwania opinii'
   ?}+', '+
   'prosimy o wypełnienie'+{? ZA_FORM.ZA_ZEST().ANONIM='T' || ' anonimowej' || '' ?}+' ankiety.<br>'+
   'Aby wypełnić ankietę należy na stronach serwisu pracowniczego, po zalogowaniu, w obszarze Ankiety, '+
   'wyszukać ankietę o nazwie: '+
   '<p><b><code>'+ZA_FORM.ZA_ZEST().NAZWA+'</code></b></p>'+
   'Po wybraniu wskazanej ankiety należy w oknie poniżej wskazać formularz ankiety i użyć przycisku "Wypełnij".'+
   '<br>'+
   {? ZA_FORM.ZA_ZEST().ANONIM='T'
   || 'Pragniemy zapewnić, że ankieta jest anonimowa, identyfikatory formularzy ankiety są zapisane w bazie danych, '+
      'ale nie są w żaden sposób powiązane z danymi osobowymi pracowników (nie istnieje możliwość powiązania '+
      'odpowiedzi na pytania ankiety z danymi osoby, która ich udzieliła).'+
      '<br>'+
      'Istnieje również możliwość wypełnienia formularza, za pomocą specjalnego, anonimowego użytkownika ankieta.'+
      '<br>'+
      'Link do ankiet: '+
      '<br>'+
      '<a href="'+_link+'">'+_link+'</a>'+
      '<br>'+
      'Wystarczy w przeglądarkę internetową wkleić powyższy link i zalogować się używając następujących poświadczeń:' +
      '<br>'+
      'Login: <b>ankieta</b>'+
      '<br>'+
      'Hasło: <b>ankieta</b>'+
      '<br>'
   || '<p>'+
      '<b>Uczestnik: '+ZA_FORM.ZZ_KTO().PIERWSZE +' '+ZA_FORM.ZZ_KTO().NAZWISKO+'</b><br>'+
      '</p>'
   ?}+
   '<br>'+
   'Ta wiadomość została wygenerowana automatycznie - prosimy na nią nie odpowiadać.';
ZA_FORM.cntx_pop();
_ret


\pba_por_anki_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.42]
:: OPIS: Generuje link dla pracownika, dzięki któremu będzie mozliwe wypełnienie ankiety jako anonimowy uzytkownik.
::   WE: _a - identyfikator formularza ankiety
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_formID:={? var_pres('_a')=type_of('') || _a || '' ?};
_formula:=''+"exec('ank_por','ank_por','"+_formID+"')";
_UID_EXE:=exec('bdexec_check','#b_desktop',_formula,'PBA');
_tabid:=app_info('web_tabid');
{? +KST.PORT_URL
|| _host:=KST.PORT_URL
|| _host:='http://localhost:8080/merit'
?};
_inet:=inet_get ('http://');
::powołanie obiektu inet w celu wykorzystania metody encodującej napis
_encode_uid:=_inet.url_encode(_UID_EXE);
_url:='/#action=AREA&p0='+_encode_uid+'&ds=a954a5&keepParams=1';
_link:=_host+_url;
_link

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 4b29b09b1d6f806cbf47a90704dd36403fc7612300801fdbd9350b8bf0a27d83f0433cefb223fc57defc5b7ccaff70f2ab0bb86f1b8b855c515126525f70290d4bc4db07d0cf0ad237c39fd1b1abe7154052a266b21f468e1028c2c36b6af7e75bc2c95b7b2ac02724516477f0fd13964a941bd9cb8500854ce3489f58eaa3e5

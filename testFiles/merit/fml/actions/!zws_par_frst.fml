:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_frst.fml
:: Utworzony: 28.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_FRST - Tryby monitowania
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tryby monitowania - główna formuła czynności ZWS_PAR_FRST.
::  OLD: \wintry/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys');
SER_DEF.cntx_psh(); SER_DEF.win_dict('SERWZW');
SER_TM.cntx_psh(); SER_TM.index('KOD'); SER_TM.prefix();
_txt:='';
_grp:=SER_TM.grp_make(_txt,,'grp_ser_tm');
SER_TM.grp_sel(_grp,,'WER',,"exec('ar_ser_tm','!zws_par_frst')",,,,,,,,'maximized');
SER_TM.grp_splt(_grp,,'horizontal','left');
SER_TM.grp_sel(_grp,SER_TPOZ,'WER','Poziomy trybu monitowania'@,,,,,
               "exec('ser_tpoz_before','!zws_par_frst')","",0,,'maximized');
SER_TM.grp_sel(_grp,SER_TPOZ,'WER1','Poziomy trybu monitowania'@,,,,,
               "exec('ser_tpoz_before','!zws_par_frst')","",0,,'maximized');
SER_TM.win_sel(_grp);
SER_TM.select();
SER_DEF.win_sel('SERWZW'); SER_DEF.win_dict('SERWZW');
SER_DEF.hdr_sel();
SER_TM.cntx_pop();
VAR_DEL.delete('ile_dni1','ile_dni2','zakltpoz');
SER_KOR.TYP:='';
SER_DEF.cntx_pop();
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
''


\ar_ser_tm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu okienka tabeli SER_TM
::----------------------------------------------------------------------------------------------------------------------
zakltpoz:='WER';
{? ~SER_TM.get() | SER_TM.TYP=0
|| tab_show(1,'left');
   tab_hide(2,,'left');
   SER_TPOZ.win_edit('RED')
|| tab_show(2,'left');
   tab_hide(1,,'left');
   zakltpoz:='WER1';
   SER_TPOZ.win_edit('RED1')
?};
ile_dni1:=ile_dni2:=0;
SER_TPOZ.index('SER_TPOZ');
{? SER_TM.get() || SER_TPOZ.prefix(SER_TM.ref()) || SER_TPOZ.prefix(null) ?};
grp_disp(SER_TPOZ,zakltpoz);
1


\ser_tpoz_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą okienka wertowania tabeli SER_TPOZ
::----------------------------------------------------------------------------------------------------------------------
{? SER_TM.get()
|| SER_TPOZ.actions(zakltpoz,'',,1)
|| SER_TPOZ.actions(zakltpoz,'DpuM:D',,1)
?};
1


\pr_i_tm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcją pola SER_TM.ILE
::  OLD: \pr_i_tm/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()='popraw'
|| SER_TPOZ.index('SER_TPOZ');
   SER_TPOZ.prefix(SER_TM.ref());
   {? SER_TPOZ.first() & SER_TPOZ.count()<>0 || 0 || 1 ?}
|| 1
?}


\betypm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Formula przed redakcja pola SER_TM.TYP
::  OLD: \betypm/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt='popraw'
|| SER_TPOZ.cntx_psh();
   SER_TPOZ.index('SER_TPOZ'); SER_TPOZ.prefix(SER_TM.ref());
   _zwrot:=~SER_TPOZ.first();
   SER_TPOZ.cntx_pop()
|| _zwrot:=1
?};
_zwrot


\aetyptm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Po redakcji pola SER_TM.TYP
::  OLD: \aetyptm/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_TM.TYP=0 || SER_TM.ODSTEP:=0 ?}; 1


\beodstep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcja pola SER_TM.ODSTEP
::  OLD: \beodstep/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_TM.TYP=1


\pow_tm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Powiązania trybów monitowania ze schematami windykacyjnymi
::  OLD: \pow_tm/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.index('SER_TM'); SER_SCH.prefix(SER_TM.ref());
{? SER_SCH.first()
|| SER_SCH.win_sel('WER2');
   SER_SCH.select()
|| FUN.info('Brak powiązań trybu monitowania \nze schematami windykacyjnymi.'@)
?}


\kop_tm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Kopiowanie trybu monitowania
::  OLD: \kop_tm/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_TM.win_edit('RED');
SER_TM.KOD:='';
_ref:=SER_TM.ref();
{! |?
   {? SER_TM.edit("exec('chk_tm','rozrach_kor')")
   || {? ~SER_TM.add(1)
      || FUN.info('Istnieje tryb monitowania o podanym kodzie.'@); 1
      || do();
         SER_TPOZ.index('SER_TPOZ'); SER_TPOZ.prefix(_ref);
         {? SER_TPOZ.first()
         || {! |?
               SER_TPOZ.cntx_psh();
               SER_TPOZ.prefix();
               SER_TPOZ.SER_TM:=SER_TM.ref();
               SER_TPOZ.add();
               SER_TPOZ.cntx_pop();
               SER_TPOZ.next()
            !}
         ?};
         end(); 0
      ?}
   ?}
!}


\bl_s_tm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2010]
:: OPIS: Zwraca SER_TM.ref
::  OLD: \bl_s_tm/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_TM.ref()


\tpoz_ile
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcją pola SER_TPOZ.ILE
::  OLD: \tpoz_ile/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_TM.TYP=0 || SER_TPOZ.LP<>1 || 1 ?}


\be_tsdef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcją pola SER_TPOZ.SER_DEF
::  OLD: \be_tsdef/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.TYP:='W';
1


\tpoz_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Dołącz w okienku wertowania tabeli SER_TPOZ
::  OLD: \tpoz_dol/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? SER_TM.TYP=1
|| {? _a=0
   || _ile_dni:=1; ile_dni1:=ile_dni2:=0
   |? _a=1
   || {? SER_TPOZ.ILE=0
      || FUN.info('Nie można dołączyć wcześniejszego poziomu.\nBrak ciągłości.'@); _ok:=0
      || ile_dni2:=_ile_dni:=(SER_TPOZ.ILE-1);
         SER_TPOZ.cntx_psh();
         ile_dni1:={? SER_TPOZ.prev() || SER_TPOZ.ILE+1 || 0 ?};
         SER_TPOZ.cntx_pop();
         {? ile_dni1>ile_dni2
         || FUN.info('Nie można dołączyć poziomu w tym miejscu.\nBrak ciągłości.'@); _ok:=0
         ?}
      ?}
   || ile_dni1:=_ile_dni:=(SER_TPOZ.ILE+1); ile_dni2:=0
   ?}
?};
{? _a=1 & _ok
|| _l:=SER_TPOZ.count()<>0;
   {? SER_TPOZ.prev() || _l+=SER_TPOZ.count()<>0; SER_TPOZ.next() ?};
   {? _l=2
   || FUN.info('Dołączanie poziomu %1 niemożliwe.\nPoziomy %2'
               ' i %1 były wykorzystane\ndo utworzenia monitów.'@[$SER_TPOZ.LP,$(SER_TPOZ.LP-1)]);
               _ok:=0
   ?}
?};
{? _ok
|| WYRAZ.LP:={? _a=0 || 1
             |? _a=1 || SER_TPOZ.LP
             |? _a=2
             || SER_TPOZ.cntx_psh();
                _lp:={? SER_TPOZ.last() || SER_TPOZ.LP+1 || 1 ?};
                SER_TPOZ.cntx_pop();
                _lp
             ?};
   SER_TPOZ.blank();
   {? SER_TM.TYP=1 || SER_TPOZ.ILE:=_ile_dni ?};
   {? SER_TPOZ.edit("exec('chk_tpoz','!zws_par_frst')")
   || do();
      {? _a=1
      || SER_TPOZ.cntx_psh();
         _lp:=SER_TPOZ.LP;
         {? SER_TPOZ.last()
         || {! |? {? SER_TPOZ.LP>=_lp || SER_TPOZ.LP+=1; SER_TPOZ.put(); SER_TPOZ.prev() ?}!}
         ?};
         SER_TPOZ.cntx_pop()
      ?};
      SER_TPOZ.add();
      end()
   ?}
?};
1


\chk_tpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Rekord po w okienku wertowania tabeli SER_TPOZ
::  OLD: \chk_tpoz/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? SER_TPOZ.SER_DEF=null
|| FUN.info('Nie wybrano typu monitu.'@); _zwrot:='SER_DEF'
?};
{? SER_TM.TYP=1 & _zwrot=''
|| {? (ile_dni1<>0 & SER_TPOZ.ILE<ile_dni1) | (ile_dni2<>0 & SER_TPOZ.ILE>ile_dni2)
   || FUN.info('Należy wprowadzić poprawnie parametr \'Ile dni po terminie\''
               '\ntak aby zachować ciągłość.'@);
      _zwrot:='ILE'
   ?}
?};
_zwrot


\be_tpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed Popraw i Przed Usuń dla SER_TPOZ
::  OLD: \be_tpoz/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_TM.TYP=1 & -menu_txt='popraw'
|| SER_TPOZ.cntx_psh();
   ile_dni1:={? SER_TPOZ.prev() || SER_TPOZ.ILE+1 || 0 ?};
   SER_TPOZ.cntx_pop(); SER_TPOZ.cntx_psh();
   ile_dni2:={? SER_TPOZ.next() || SER_TPOZ.ILE-1 || 0 ?};
   SER_TPOZ.cntx_pop()
?};
{? -menu_txt='usuń' & SER_TM.TYP=0 & SER_TPOZ.size()>1 &
   (SER_TPOZ.cntx_psh(); _prev:=SER_TPOZ.prev(); SER_TPOZ.cntx_pop(); ~_prev)
|| FUN.info('Przed usunięciem pierwszego poziomu\n'
            'należy najpierw usunąć wyższe poziomy.'@); 0
|| {? SER_TPOZ.count()=0
   || 1
   || FUN.info('Definicja poziomu monitowania jest wykorzystana w innych strukturach.'@); 0
   ?}
?}


\tpoz_pdo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Akcja Usuń Po w okienku wertowania tabeli SER_TPOZ
::  OLD: \tpoz_pdo/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
_lp:=1;
SER_TPOZ.cntx_psh();
{? SER_TPOZ.first()
|| {! |? SER_TPOZ.LP:=_lp; _lp+=1; SER_TPOZ.put(); SER_TPOZ.next() !}
?};
SER_TPOZ.cntx_pop();
1


\rprz_tpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rekord przed tabeli SER_TPOZ
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.BLOK_KH:={? |SER_TPOZ.BL='' || 'brak' |? -SER_TPOZ.BL='w' || 'warunkowa' || 'bezwarunkowa' ?};
1


\be_tm_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed usuwaniem trybu monitowania
::----------------------------------------------------------------------------------------------------------------------
{? SER_TM.count()
|| FUN.info('Tryb monitowania wykorzystywany w systemie. Usunięcie niemożliwe.'@); 0
|| 1
?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:04 9b355793f47cc67bf5d6c6235325686b5289dda6f47987bce48d75dfbf917b8767fcda14ed75d3be0705325d90683ef35456d789412d7de5b3e892fd1f0174eebce5fb326dd063b2a6a61bfdd4fd1e649b6e93a019f1a7a3fcf3f6514a5e48dbc8aac0dcdb897c619105a40dbb8991d4c2f0876515533b09926a752a8e352456

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_dupr.fml
:: Utworzony: 09.10.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Obsługa czynności PBA_BPB_DUPR - Otwarcie sesji badan cyklicznych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Uruchomienie sesji - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=ZO_PROC, type=_ZO_PROC, name=Wskazanie sesji badań, required=T, keyref=T
::
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, OTWARTA, REZYGNACJA)", required=N
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;

:: Jedyny parametr wejściowy ma ustawioną flagę keyref=T, co oznacza, że przekazana wartość automatycznie stanie się
:: rekordem kluczowym.
_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error('Nieprawidłowy parametr wejściowy.'@);
   return()
?};

_ret:=exec('otworz','!pba_bpb_dupr',_keyRefs[1],~_mp.isMicro());

{? _ret.errmsg<>''
|| FUN.emsg(_ret.errmsg);
   _mp.error(_ret.errmsg)

|? _ret.status='X'
|| _mp.keep()

|| _out.RESULT:=_ret.status;
   _mp.save(,_out);
   _mp.done()
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Otwarcie sesji badań - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_keyRefs:=_mp.getRefs();
_ref:={? var_pres('[1]',_keyRefs) || _keyRefs[1] || _mp.load(exec('kind_in','#b_port')).ZO_PROC ?};
_tab:=exec('desc_tab','phr_dane');

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROC.cntx_psh();
ZO_PROC.prefix();
{? ZO_PROC.seek(_ref)
|| _tab.ZAW_DANE:='T';
   _tab.NAZWA:=ZO_PROC.ZO_PROG().NAZWA;
   _tab.RODZAJ:=ZO_PROC.ZO_PROG().RODZAJ;
   _tab.OKRES_OD:=$ZO_PROC.OKRES_OD;
   _tab.OKRES_DO:=$ZO_PROC.OKRES_DO
|| _tab.ZAW_DANE:='N'
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();

{? _tab.ZAW_DANE='T'
|| 'W ramach programu "%1" (%2) otwórz sesję badań od %3 do %4.'@@
      [_tab.NAZWA,_tab.RODZAJ,_tab.OKRES_OD,_tab.OKRES_DO]
|| 'Otwórz sesję badań'@@
?}


\otworz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Główna formuła zajmująca się otwarciem sesji badań.
::   WE: _a [REFERENCE] - Wskazanie sesji badań.
::       _b [NUMBER]    - Czy obsługa w procesie? [1/0]
::   WY: Tablica nazwana m.in. ze statusem operacji.
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_proces:=_b;

_ret:=obj_new('errmsg','status');
_ret.errmsg:='';
_ret.status:='';

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROC.cntx_psh();
ZO_PROC.f_clear();
ZO_PROC.prefix();
{? ZO_PROC.seek(_ref)
|| {? ZO_PROC.A='T'
   || _ret.status:='OTWARTA'
      || _akcja:=exec('dialog','!pba_bpb_dupr',_proces);
         {? _akcja='T'
         || ZO_PROC.A:='T';
            ZO_PROC.Z:='N';
            {? ZO_PROC.put()
            || _ret.status:='OK'
         || _ret.errmsg:='Otwarcie sesji badań nie powiodło się.'@
            ?}
         |? _akcja='N'
         || _ret.status:='REZYGNACJA'
         || _ret.status:='X'
         ?}
      ?}
:: Brak rekordu przekazanego parametrem.
|| _ret.errmsg:='Otwarcie sesji badań niemożliwe.'@
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();

_ret


\dialog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Formuła przygotowuje okno redagowania.
::   WE: _a [NUMBER] - Czy obsługa w procesie? [1/0]
::   WY: T / N / X
::----------------------------------------------------------------------------------------------------------------------
_proces:=_a;

{? _proces
|| _ret:=obj_new(1);
   _ret[1]:='X';
   params_set('ret',_ret);

   _info:='Otwarcie sesji badań'@;
   _we:=ZO_PROC.mk_edit(_info);
   ZO_PROC.win_esep(_we,'Program badania'@);
   ZO_PROC.win_efld(_we,ZO_PROG,'NAZWA',,,40,,,,,MS.comment(ZO_PROG,'NAZWA'));
   ZO_PROC.win_efld(_we,ZO_PROG,'RODZAJ',,,,,,,,'Rodzaj badania'@,'radio-buttons',,
      'Roczny'@,"'R'",
      'Półroczny'@,"'P'",
      'Kwartalny'@,"'K'",
      'Miesięczny'@,"'M'",
      'Tygodniowy'@,"'T'",
      'Ad hoc'@,"'A'"
   );
   ZO_PROC.win_ecol(_we);
   ZO_PROC.win_esep(_we,'Sesja badań'@);
   ZO_PROC.win_efld(_we,,'R',,,5,,,,,MS.comment(ZO_PROC,'R'));
   ZO_PROC.win_efld(_we,,'M',,,5,,,,,MS.comment(ZO_PROC,'M'));
   ZO_PROC.win_efld(_we,,'OKRES_OD',,,,,,,,MS.comment(ZO_PROC,'OKRES_OD'));
   ZO_PROC.win_efld(_we,,'OKRES_DO',,,,,,,,MS.comment(ZO_PROC,'OKRES_DO'));
   ZO_PROC.win_efld(_we,,'PLAN_OD',,,,,,,,MS.comment(ZO_PROC,'PLAN_OD'));
   ZO_PROC.win_efld(_we,,'PLAN_DO',,,,,,,,MS.comment(ZO_PROC,'PLAN_DO'));

   _p1:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['Otwórz'@],
      "params_get().ret[1]:='T'; 'key:F2'");
   ZO_PROC.btn_eopt(_we,_p1,'tooltip="%1"' ['Otwarcie wskazanej sesji badań i zakończenie bieżącej czynności'@]);
   _p2:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['&Nie otwieraj'@],
      "params_get().ret[1]:='N'; 'key:F2'");
   ZO_PROC.btn_eopt(_we,_p2,'tooltip="%1"' ['Zakończenie czynności bez otwarcia wskazanej sesji badań'@]);
   _p3:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['&Anuluj'@],'key:Esc');
   ZO_PROC.btn_eopt(_we,_p3,'tooltip="%1",default=1' ['Odłożenie czynności na listę zadań'@]);

   ZO_PROC.win_edit(_we);

   ZO_PROC.ZO_PROG();
   ZO_PROC.display(,,_info);

   _ret[1]

:: Mikroproces
|| {? {? ZO_PROC.Z='N'
      || FUN.ask('Czy na pewno otworzyć sesję badań?'@)
      || FUN.ask('Czy na pewno anulować zamknięcie sesji badań?'@)
      ?}
   || 'T'
   || 'N'
   ?}
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 73160c920470ef47e5ffb64e055ad79aae80e97c23fc694be5d8e1421fda83a77b8c90303b7fca0f899681e523d673b7cff7c1f83de40c7756f0c2a201a7cd2fa645e949ae94595ce1dae5ff0be711938ce7b3168e36ddf7b930afd8ffcd6c3d1731714c83cee3f9b07c911704a09ebdf6f27b1600488cbf40b38aab67cc8eaa

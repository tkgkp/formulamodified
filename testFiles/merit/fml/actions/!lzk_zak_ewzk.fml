:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zak_ewzk.fml
:: Utworzony: 11.09.2019
:: Autor: MW
::======================================================================================================================
:: Zawartość: Wycofanie akceptacji lub zakończenia dokumentu zakupu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LZK
::# properties=SERVICE
::# parses=exec('parses','!lzk_zak_ewzk')
::# kind=WE,   symbol=FAKS,      type=_FAKS,    name=Dokument sprzedaży,   required=N,    keyref=T
::# kind=WY,   symbol=FAKS,      type=_FAKS,    name=Dokument sprzedaży,   required=N
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

exec('init_zak','lzk');

_akcja:=_mp.akcja();
_group:=_mp.isGroup();
_service:=_mp.isService();
_auto:=_akcja<>'Wycofaj' & (_mp.isAutoRun() | _service);

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| _faks_ref:=_in.FAKS
|| _faks_ref:=null()
?};

{? _faks_ref=null()

|| _mp.error('Brak wymaganego parametru FAKS.')

|| _out.FAKS:=_faks_ref;
   _mp.save('OUT','FAKS',_faks_ref);
:: dla serwisu ustalamy jego środowisko
   _msk:=ref_name(_faks_ref)+3;
   exec('fak_psh','open_tab');
   {? _service | (_auto & _msk<>(FAKS.name()+3))
   || exec('fak_open','open_tab',(1+_msk),(_msk+2))
   ?};

   FAKS.prefix();
   {? ~FAKS.seek(_faks_ref)
   || _mp.error('Nie znaleziono dokumentu.')
   || {? _service
      || ST.MAG:=FAKS.MAG;
         ST.AM:=FAKS.AM;
         ST.AR:=FAKS.AR
      ?};
      {? _akcja='Wycofaj' | _auto | _group
         || params_set('mp',_mp);
            _oki:=exec('wycofaj','!lzk_zak_ewzk');
            {? _oki<=0
            || {? _service
               || _mp.done()
               || _mp.error('Nie można wycofać dokumentu.')
               ?}
            ?}
         |? _mp.pathTodo() |( _mp.pathArea() & _akcja='')
         || params_set('mp',_mp);
            _win_red:=exec('faks_win_edit','faktury_nag');
            _ff:="params_exec('faks_pozycje_todo','!lzk_zak_ewzk')";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
            _ff:="params_exec('faks_wycofaj_todo','!lzk_zak_ewzk'); 'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Wycofaj'@],_ff);
            _ff:="'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
            FAKS.win_edit(_win_red);
            exec('set_efld_opt','faktury_nag',_win_red);
::          Dalsza obsługa wycofania w exec('wycofaj','!lzk_zak_ewzk')
            FAKS.display();
::          Usunięcie definicji tymczasowych okien
            FAKS.win_edit(''); FAKS.win_edel(_win_red)
         || _mp.error('Nieobsługiwana ścieżka.')
         ?}
   ?};
   exec('fak_pop','open_tab')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| _kor:=exec('FindAndGet','#table',FAKS,_in.FAKS,,"FAKS.T().KOR",'');
   {? _kor='T'
   || 'Wycofaj korektę sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   |? _kor='Z'
   || 'Wycofaj korektę zbiorczą: %1'@@[exec('record','#to_string',_in.FAKS)]
   || 'Wycofaj dokument sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   ?}
|| ''
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| FAKS.cntx_psh();
   FAKS.use(ref_name(_in.FAKS));
   FAKS.prefix();
   {? FAKS.seek(_in.FAKS)
   || __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LZK';
      _args.AR:=FAKS.AR;
      _args.AM:=FAKS.AM;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FAKS.cntx_pop()
?};

1


\faks_wycofaj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Dokument sprzedaży - Wycofaj z todo
::   WE: params_get()   - ustawiane w exec('main','!lzk_zak_ewzk')
::----------------------------------------------------------------------------------------------------------------------
params_exec('wycofaj','!lzk_zak_ewzk');
''


\faks_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Dokument sprzedaży - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('pozycje_fak','faktury_poz',0);
''


\wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MM] [19.42]
:: OPIS: Dokument sprzedaży - Wycofanie dokumentu
::   WY: 1-wycofano 0-nie
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_result:=exec('faks_wycofaj','faktury_wycof');
{? _result
|| _mp.done()
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 864598d48a4dbffd71cf3077de8a0659a217d7b2fe419aef67f539d3533b458c6ea5b1805cc92c260348227e8a4f5c52ea73f952902db171b04d6a8f7798f90623d8fe9f4a97ca5bb61926b17a227d688a14d3d1875ee763b7e650c92a7ef06484b732b2c9ef4f3aee5304a780ac987c6babd437702684386de2c8bd8b87c30a

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_upr_rcfz.fml
:: Utworzony: 07.27.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_UPR_RCFZ - Czynności dla formy współpracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Czynności dla formy współpracy - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::
exec('f_zatra_init','f_zatr');

_cntx:=obj_new('ndx','pos','tab');
_cntx.ndx:=obj_new('F_ZATR','B_ACTION');
_cntx.pos:=obj_new('F_ZATR','B_ACTION');
params_set('cntx',_cntx);

F_ZATR.cntx_psh();
F_ZATR.index('KOD');
F_ZATR.prefix();
F_ZATR.win_sel('ACT');
F_ZATR.select();
F_ZATR.cntx_pop();
~~


\f_zatra_odtworz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.14]
:: OPIS: Obsługa akcji "Odtwórz - przed" w oknie NAW tabeli F_ZATR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask(
      'Utracone zostaną wszystkie modyfikacje wprowadzone w widoku.\n'
      'Czy na pewno odtworzyć domyślne przypisanie czynności dla form współpracy?'@
   )
|| F_ZATRA.cntx_psh();
   F_ZATRA.prefix();
   F_ZATRA.erase();
   F_ZATRA.cntx_pop();
   exec('f_zatra_init','f_zatr',1)
?}


\f_zatra_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Dołącz przed" okienek tabeli F_ZATRA.
::----------------------------------------------------------------------------------------------------------------------
exec('f_zatra_filter_set','!zws_upr_rcfz',($params_get().cntx.tab)(),'not in',0);
1


\f_zatra_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Popraw przed" okienek tabeli F_ZATRA.
::----------------------------------------------------------------------------------------------------------------------
exec('f_zatra_filter_set','!zws_upr_rcfz',($params_get().cntx.tab)(),'not in',1);
1


\f_zatra_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: "Szukaj przed" okienek tabeli F_ZATRA.
::----------------------------------------------------------------------------------------------------------------------
exec('f_zatra_filter_set','!zws_upr_rcfz',($params_get().cntx.tab)(),'in',1);
1


\f_zatra_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po edycji rekordu w tabeli F_ZATRA
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(F_ZATRA,-menu_txt()='popraw')


\f_zatra_filter_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustawienie filtra na obszary przed rozpoczęciem edycji danych w tabeli UDB_DOM.
::   WE: _a - alias filtrowanej tabeli F_ZATR/B_ACTION
::       _b - dyrektywa filtra 'in'/'not in'
::       _c - 0 - dołączanie, 1 - poprawianie
::----------------------------------------------------------------------------------------------------------------------
{? _a=B_ACTION
|| B_ACTION.prefix();
   B_ACTION.f_set(
      'UID',,
      'B_ACTION.REFERENCE '+_b+' (select F.B_ACTION from F_ZATRA as F where F.F_ZATR=:_a)'+
      {? _c
      || ' or B_ACTION.REFERENCE=:_b'
      || ''
      ?},
      F_ZATR.ref(),
      F_ZATRA.B_ACTION
   )
|? _a=F_ZATR
|| F_ZATR.prefix();
   F_ZATR.f_set(
      'KOD',,
      'F_ZATR.REFERENCE '+_b+' (select F.F_ZATR from F_ZATRA as F where F.B_ACTION=:_a)'+
      {? _c
      || ' or F_ZATR.REFERENCE=:_b'
      || ''
      ?},
      B_ACTION.ref(),
      F_ZATRA.F_ZATR
   )
?};
~~


\adm_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wypełnienie okna ACT tabeli F_ZATR.
::----------------------------------------------------------------------------------------------------------------------
exec('f_clear','!zws_upr_rcfz');

F_ZATR.cntx_psh();
F_ZATR.index('KOD');
F_ZATR.prefix();
F_ZATR.first();
F_ZATR.win_dict('SLO');

B_ACTION.cntx_psh();
B_ACTION.index('UNIK');
B_ACTION.prefix();
B_ACTION.win_dict('SLO');

params_set(params_get());
_cntx:=params_get().cntx;
_cntx.ndx.F_ZATR:='ACT_SYM';
_cntx.ndx.B_ACTION:='ZAT_KOD';
_cntx.pos.F_ZATR:=null;
_cntx.pos.B_ACTION:=null;

F_ZATRA.cntx_psh();
F_ZATRA.index(_cntx.ndx.F_ZATR);
F_ZATRA.prefix(F_ZATR.ref());
F_ZATRA.first();

grp_disp(F_ZATR,'NAW',1,1);

1


\adm_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zamykanie okna ACT tabeli F_ZATR
::----------------------------------------------------------------------------------------------------------------------
exec('f_clear','!zws_upr_rcfz');

F_ZATRA.cntx_pop();
F_ZATR.cntx_pop();
B_ACTION.cntx_pop();

1


\adm_f_zatr_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okna NAW tabeli F_ZATR w oknie ACT tabeli F_ZATR.
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(F_ZATRA,'B_ACTION',1,1)


\adm_link_1_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna B_ACTION tabeli F_ZATRA w oknie ACT tabeli F_ZATR.
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
_cntx:=params_get().cntx;

F_ZATRA.index(_cntx.ndx.F_ZATR);
F_ZATRA.prefix(F_ZATR.ref());
F_ZATRA.seek(_cntx.pos.F_ZATR);

{? _a
|| _cntx.tab:=!B_ACTION;
   F_ZATRA.win_edit('B_ACTION');
   F_ZATRA.fld_fml('B_ACTION','BLANK',"");
   F_ZATRA.fld_fml('F_ZATR','BLANK',"F_ZATR.ref()")
?}


\adm_link_1_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okna B_ACTION tabeli F_ZATRA w oknie ACT tabeli F_ZATR.
::   WE: _a - wartość przekazywana do formuły "po obsłudze" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| B_ACTION.f_clear();
   _cntx:=params_get().cntx;
   _cntx.pos.F_ZATR:=F_ZATRA.ref();
   _cntx.ndx.F_ZATR:=F_ZATRA.index('?')
?}


\adm_b_action_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna SLO tabeli B_ACTION w oknie ACT tabeli F_ZATR.
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------


\adm_b_action_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okna SLO tabeli B_ACTION w oknie ACT tabeli F_ZATR.
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(F_ZATRA,'F_ZATR',1,1)


\adm_link_2_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna F_ZATR tabeli F_ZATRA w oknie ACT tabeli F_ZATR.
::   WE: _a - wartość przekazywana do formuły "przed obsługą" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
_cntx:=params_get().cntx;

F_ZATRA.index(_cntx.ndx.B_ACTION);
F_ZATRA.prefix(B_ACTION.ref());
F_ZATRA.seek(_cntx.pos.B_ACTION);

{? _a
|| _cntx.tab:=!F_ZATR;
   F_ZATRA.win_edit('F_ZATR');
   F_ZATRA.fld_fml('F_ZATR','BLANK',"");
   F_ZATRA.fld_fml('B_ACTION','BLANK',"B_ACTION.ref()");
   ~~
?}


\adm_link_2_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okna F_ZATR tabeli F_ZATRA w oknie ACT tabeli F_ZATR.
::   WE: _a - wartość przekazywana do formuły "po obsłudze" zależnie od tryby odświeżania (grp_disp/aktywowanie)
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| F_ZATR.f_clear();
   _cntx:=params_get().cntx;
   _cntx.pos.B_ACTION:=F_ZATRA.ref();
   _cntx.ndx.B_ACTION:=F_ZATRA.index('?')
?}


\f_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zdejmuje filtry z tabel.
::----------------------------------------------------------------------------------------------------------------------
B_ACTION.f_clear();
F_ZATR.f_clear();
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:07 cb31847df1adcd6190f6000b83a7cebfc7cc78b5e40bd147fc18f17298cd6b9748ea5ec43c21162e59627d4524851dc46524951ad96f6e28c51edf561bef0ad3f8d75a3ec05965953211f2def5d935673572244509cf5236064b4417b53f895404279c5c36b09196e6249e92c2319c4a5c712163d6ce3541f42ed7df66fcb8c9

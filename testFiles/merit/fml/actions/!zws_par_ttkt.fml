:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ttkt.fml
:: Utworzony: 27.01.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_TTKT - Definicje kart technologicznych
::            Obsługa tabel: TPKTL, TPKTLF (TAG: <TPKTL>), KRUB, KFORM, KWAR, KKS (TAG: <KWAR>),
::                           TTXCAUSE, GKTL
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła główna czynności parametryzacyjnej (nieprocesowej) - redagowanie typów kart technologicznych
::  TAG: <TPKTL><KWAR><CZYNNOŚĆ><LISTA>
::----------------------------------------------------------------------------------------------------------------------
Cntx.psh(TPKTL,KRUB,KWAR,TTXCAUSE);

TPKTL.win_sel('GRP');
TPKTL.index('TYP');
TPKTL.prefix();
TPKTL.win_edit('RED');

KRUB.index('NR');
KRUB.prefix();

KWAR.f_clear();
KWAR.index('NA');
KWAR.prefix();
KWAR.hdr_sel(' '+'TKW'@);

TTXCAUSE.index('NAZ');
TTXCAUSE.prefix();
TTXCAUSE.actions('WER','W');

:: tablica zmiennych służących do kontroli redakcji pól
_edit:=obj_new('par','util','xjm','sur','dj','oper');
_edit.par:=_edit.util:=_edit.xjm:=_edit.sur:=_edit.dj:=_edit.oper:=1;
params_set('edit',_edit);

TPKTL.select();
Cntx.pop(TPKTL,KRUB,KWAR,TTXCAUSE);
~~


\grp_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Formuła "before" - wypełnianie okna grupowego
::----------------------------------------------------------------------------------------------------------------------
grp_disp(TTXCAUSE,'WER');
~~


::======================================================================================================================
:: Formuły obsługi tabeli TPKTL (typy kart technologicznych)
::======================================================================================================================


\przedokno
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Przed oknem przy redagowaniu
::  OLD: \przedokno/tex_kalk.fml
::  TAG: <TPKTL><AKCJA><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
params_get().edit.par:=1;
params_get().edit.sur:=1;
params_get().edit.util:=1;
params_get().edit.xjm:=1;
params_get().edit.dj:=1;
params_get().edit.oper:=1;
1


\przeddol
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Przed dołączeniem rekordu
::  OLD: \przeddol/tex_kalk.fml
::  TAG: <TPKTL><AKCJA><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
params_get().edit.par:=1;
params_get().edit.sur:=1;
params_get().edit.util:=1;
params_get().edit.xjm:=1;
params_get().edit.dj:=1;
params_get().edit.oper:=1;
VAR1.NRUB:=exec('opis_krub','!zws_par_ttkt',exec('tpktlnrr','!zws_par_ttkt'));
VAR1.NRUBMAT:='';
VAR1.NRUBROB:='';
VAR1.NRUBIL:='';
1


\prsur
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Formuła przed poprawianiem rekordu sprawdza czy istnieją karty technologiczne danego typu
::       i ustawina zmienne par,sur,util,xjm,oper - umożliwiające redakcję pól
::  OLD: \prsur/tex_kalk.fml
::  TAG: <TPKTL><AKCJA><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? exec('type_reserved','!zws_par_ttkt')
|| params_get().edit.par:=0;
   params_get().edit.sur:=0;
   params_get().edit.util:=0;
   params_get().edit.xjm:=0;
   params_get().edit.dj:=0;
   params_get().edit.oper:=0;
   FUN.emsg('Zastrzeżony typ karty — możliwości redagowania będą ograniczone.'@);
   return(1)
?};

_typ:=TPKTL.ref();
params_get().edit.par:=1;
TKTL.cntx_psh();
TKTL.index('TYP');
TKTL.prefix(_typ);
:: + pętla po archiwach... niestety...
{? TKTL.first()
|| params_get().edit.sur:=0;
   params_get().edit.util:=0;
   params_get().edit.xjm:=0;
   params_get().edit.dj:=0;
   params_get().edit.oper:=0;
   {? TPKTL.PAR='T'
   || {!
      |?
         TPAR.clear();
         TPAR.index('NN');
         TPAR.prefix(TKTL.ref());
         {? TPAR.first() || params_get().edit.par:=0 ?};
         TKTL.next() & params_get().edit.par
      !}
   ?}
|| params_get().edit.par:=1;
   params_get().edit.sur:=1;
   params_get().edit.util:=1;
   params_get().edit.xjm:=1;
   params_get().edit.dj:=1;
   params_get().edit.oper:=1
?};
TKTL.cntx_pop();
1


\pusu_tpktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: TPKTL - Usuwanie
::   WY: 1 / 0
::  OLD: \pusu_tpktl/tech1.fml
::  TAG: <TPKTL><AKCJA><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? exec('type_reserved','!zws_par_ttkt')
|| FUN.info('Zastrzeżony typ karty — usunięcie nie jest możliwe.'@);
   return(0)
?};
_link:=TPKTL.testlink(2);
{? _link.first()
|| FUN.info('Usunięcie nie jest możliwe — wskazany typ karty jest już użyty w systemie.'@);
   0
|| {? FUN.ask('Czy usunąć bieżący wiersz?'@) || TPKTL.del() || 0 ?}
?}


\tpktlarc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [2010]
:: OPIS: TPKTL - Formuła na Rekord-po dla okienka WER
::  OLD: \tpktlarc/tech1.fml
::  TAG: <TPKTL><CHK><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_tpktl','tech_common',-menu_txt()='popraw')


\pred_tp_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: TPKTL - Przed redakcją pola TYP
::   WY: 1 / 0
::  OLD: \pred_tp_typ/tech1.fml
::  TAG: <TPKTL><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
{? exec('type_reserved','!zws_par_ttkt') || 0 || 1 ?}


\ae_tp_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: TPKTL - Po redakcji pola TYP
::  OLD: \ae_tp_typ/tech1.fml
::  TAG: <TPKTL><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? exec('type_reserved','!zws_par_ttkt')
|| FUN.info('Typ karty nie może się zaczynać od znaku ''%1''.'@['~']); 0
|| 1
?}


\pred_tp_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: TPKTL - Przed redakcją pola OPIS (to samo co przed TPKTL.TYP)
::   WY: 1 / 0
::  OLD: \pred_tp_opis/tech1.fml
::  TAG: <TPKTL><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
exec('pred_tp_typ','!zws_par_ttkt')


\przedxjm
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Przed redakcją pola XJM
::  OLD: \przedxjm/tex_kalk.fml
::  TAG: <TPKTL><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
params_get().edit.xjm=1


\przedoper
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Przed redakcją pola OPER
::  OLD: \przedoper/tex_kalk.fml
::  TAG: <TPKTL><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
params_get().edit.oper=1


\pooper
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Po redakcji pola OPER
::  OLD: \pooper/tex_kalk.fml
::  TAG: <TPKTL><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? TPKTL.OPER='N'
||
::   {? TPKTL.UTIL='O' || TPKTL.UTIL:='K' ?};
::   TPKTL.SUR:='K';
   params_get().edit.sur:=0
|| params_get().edit.sur:=1
?};
1


\przedsur
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Przed redakcją pola SUR
::  OLD: \przedsur/tex_kalk.fml
::  TAG: <TPKTL><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
params_get().edit.sur=1


\przedutil
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Przed redakcją pola UTIL
::  OLD: \przedutil/tex_kalk.fml
::  TAG: <TPKTL><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
{? params_get().edit.util=1
||
::   {? TPKTL.OPER='N'
::   || {? TPKTL.UTIL='O' || TPKTL.UTIL:='K' ?}
::   ?};
   1
|| 0
?}


\przedpar
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Przed redakcją pola PAR
::  OLD: \przedpar/tex_kalk.fml
::  TAG: <TPKTL><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
params_get().edit.par=1


\przeddj
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Przed redakcją pola DJ
::  OLD: \przeddj/tex_kalk.fml
::  TAG: <TPKTL><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
params_get().edit.dj=1


\prn_tpkt
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTL - Druk tabeli
::  OLD: \prn_tpkt/drukujp.fml
::  TAG: <TPKTL><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_druktpkt',,1);
1


\type_reserved
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TPKTL - Sprawdza, czy typ karty technologicznej jest zastrzeżony
::       Operuje na bieżącym buforze tabeli TPKTL
::   WY: 1 - typ zastrzeżony, 0 - typ niezastrzeżony
::  TAG: <TPKTL>
::----------------------------------------------------------------------------------------------------------------------
1+TPKTL.TYP='~'


\tpktlnrr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [2010]
:: OPIS: Numer rubryki kalkulacji zawierającej TKW produktu
::  OLD: \tpktlnrr/tex_kalk.fml
::  TAG: <TPKTL><KWAR>
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',500302,1)


\f3_krub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MKO [7.53]
:: OPIS: TPTKL - obsługa na F3 dla pól CRUB*
::  OLD: \f3_krub/tex_kalk.fml
::  TAG: <TPKTL><KWAR><MBUILDER><F3>
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();
KRUB.cntx_psh();
KRUB.index('NR');
KRUB.prefix();
KRUB.find_key(_fld);
KRUB.win_sel('WYB');
{? KRUB.select(0,1,5)<>0
|| _fld:=KRUB.NR
?};
KRUB.cntx_pop();
fld(_fld)


\tpktl_rub_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TPKTL - Po redakcji dla pól CRUB*
::  TAG: <TPKTL><KWAR><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();
_res:=0;
{? _fld=0
|| ($('VAR1.N'+(1-cur_afld())))():='';
   _res:=1
|| KRUB.cntx_psh();
   KRUB.index('NR');
   KRUB.prefix(_fld);
   {? KRUB.first()
   || ($('VAR1.N'+(1-cur_afld())))():=KRUB.OPIS;
      _res:=1
   || FUN.info('Brak rubryki o tym numerze.'@)
   ?};
   KRUB.cntx_pop()
?};
_res


\tpktl_br
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TPKTL - Przed rekord w oknach
::  TAG: <TPKTL><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
VAR1.NRUB:=exec('opis_krub','!zws_par_ttkt',TPKTL.CRUB);
VAR1.NRUBMAT:=exec('opis_krub','!zws_par_ttkt',TPKTL.CRUBMAT);
VAR1.NRUBROB:=exec('opis_krub','!zws_par_ttkt',TPKTL.CRUBROB);
VAR1.NRUBIL:=exec('opis_krub','!zws_par_ttkt',TPKTL.CRUBIL);
~~


::======================================================================================================================
:: Formuły obsługi tabeli KRUB (rubryki kalkulacji technologii)
::======================================================================================================================


\ar_krub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: KRUB - Po rekordzie w oknie WER
::  OLD: \ar_krub/tex_kalk.fml
::  TAG: <KWAR><CHK><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_krub','tech_kalk',-menu_txt()='popraw')


\usun_krub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: KRUB - Usunięcie
::  OLD: \usun_krub/tex_kalk.fml
::  TAG: <KWAR><AKCJA><DEL><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_link:=KRUB.testlink(2);
{? _link.first()
|| FUN.info('Usunięcie nie jest możliwe.\nWskazana rubryka jest już użyta w systemie.'@);
   0
|| {? FUN.ask('Czy usunąć wskazaną rubrykę?'@)
   || KRUB.del()
   ?}
?};
~~


\prn_krub
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: KRUB - Druk tabeli
::  OLD: \prn_krub/drukujp.fml
::  TAG: <KWAR><AKCJA><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_drukkrub',,1);
1


\opis_krub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: KRUB - Opis rubryki kalkulacyjnej
::   WE: _a - numer rubryki
::  TAG: <KWAR>
::----------------------------------------------------------------------------------------------------------------------
KRUB.cntx_psh();
KRUB.index('NR');
KRUB.prefix(_a);
{? KRUB.first()
|| _res:=KRUB.OPIS
|| _res:=''
?};
KRUB.cntx_pop();
_res


::======================================================================================================================
:: Formuły obsługi tabeli KFORM (formuły kalkulacji technologii)
::======================================================================================================================


\fk_prred
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2011]
:: OPIS: KFORM - przed redakcją formuły kalkulacyjnej [fka]
::  OLD: \fk_prred/fkalk.fml
::  TAG: <KWAR><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
FRMK_TMP:=KFORM.FRM;
1


\fk_pored
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2011]
:: OPIS: KFORM - po redakcji formuły kalkulacyjnej [fka]
::  OLD: \fk_pored/fkalk.fml
::  TAG: <KWAR><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('FRMK_TMP')>0) & (FRMK_TMP=KFORM.FRM)
||
:: jeśli nie wprowadzono zmian to ok
   &FRMK_TMP;
   1
||
:: wprowadzono zmiany
   _ret:=exec('fparse','kalkulator',KFORM.FRM,'fka');
   {? _ret=1
   ||
      &FRMK_TMP
   ?};
   _ret
?}


\fksprred
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2011]
:: OPIS: KFORM - przed wyświetl formułę do wyświetlenia szczegółów pozycji kalkulacji
::  OLD: \fksprred/fkalk.fml
::  TAG: <KWAR><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
FRMK_TMP:=KFORM.FRS;
1


\fkspored
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2011]
:: OPIS: KFORM - po wyświetl formułę do wyświetlenia szczegółów pozycji kalkulacji
::  OLD: \fkspored/fkalk.fml
::  TAG: <KWAR><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('FRMK_TMP')>0) & (FRMK_TMP=KFORM.FRS)
||
:: jesli nie wprowadzono zmian to ok
   &FRMK_TMP;
   1
||
:: wprowadzono zmiany
:: sprawdzenie czy zrobił to administrator, dla testów można zrobić tutaj *0
   _ret:=sec_superuser()>0;
   {? _ret=1
   ||
      &FRMK_TMP
   ||
      {? FUN.ask('Treść tego pola może zmieniać tylko osoba z uprawnieniami administratora.\n'
                 'Czy przywrócić poprzednią wartość tego pola?'@)
      ||
         KFORM.FRS:=FRMK_TMP
      ?}
   ?};
   _ret
?}


\prn_kfrm
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: KFORM - Druk tabeli
::  OLD: \prn_kfrm/drukujp.fml
::  TAG: <KWAR><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_drukkfrm',,1);
1


\ar_kform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: KFORM - Po rekordzie w oknie WER
::  OLD: \ar_kform/tex_kalk.fml
::  TAG: <KWAR><CHK><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_kform','tech_kalk',-menu_txt()='popraw')


\kwarform
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: KFORM - formuły kalkulacyjne wariantu
::  OLD: \kwarform/slownik1.fml
::  TAG: <KWAR><LISTA><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
KWAR.cntx_psh();
VAR.A_WAR:=KWAR.ref();
KFORM.index('WR');
KFORM.prefix(VAR.A_WAR);
KFORM.first();
KRUB.win_dict('WYB');
KRUB.actions('WYB','W');
KFORM.win_sel('WER');
KFORM.win_edit('RED');
KFORM.select();
KRUB.actions('WYB');
KWAR.cntx_pop();
~~


::======================================================================================================================
:: Formuły obsługi tabeli TPKTLF (warianty formuł dostępne dla typu karty technologicznej)
::======================================================================================================================


\tpktlfrm
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTLF - Warianty kalkulacji dla typu karty technologicznej
::  OLD: \tpktlfrm/slownik1.fml
::  TAG: <TPKTL><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
KWAR.f_clear();
VAR.A_TPKTL:=TPKTL.ref();
TPKTL.cntx_psh();
TPKTLF.clear();
TPKTLF.index('TW');
TPKTLF.prefix(VAR.A_TPKTL);
TPKTLF.win_sel('WER');
{!
|? TPKTLF.select();
   {? TPKTLF.size()=1
   || TPKTLF.first();
      TPKTL.DEF_OPCK:=TPKTLF.OPC;
      TPKTL.put();
      0
   |? TPKTLF.first()
   || _loop:=1;
      {!
      |?
         {? TPKTLF.OPC=TPKTLF.TYP().DEF_OPCK
         || _loop:=0
         ?};
         TPKTLF.next()
      !};
      {? _loop
      || FUN.emsg('Wskaż domyślny wariant kalkulacyjny.'@)
      ?};
      _loop
   || 0
   ?}
!};
TPKTL.cntx_pop();
~~


\usuntpktlf
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTLF - Akcja na usuń wariant typu karty technologicznej
::  OLD: \usuntpktlf/tech.fml
::   WY: 0 / 1 - czy usunięto wariant
::  TAG: <TPKTL><MBUILDER><AKCJA><DEL>
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? TPKTLF.OPC=TPKTLF.TYP().DEF_OPCK
|| {? FUN.ask('Wybrany wariant jest wariantem domyślnym.\n\nCzy na pewno usunąć?'@)
   || TPKTLF.TYP().DEF_OPCK:=null();
      TPKTL.put();
      TPKTLF.del()
   || _ok:=0
   ?}
|| {? FUN.ask('Czy na pewno usunąć?'@)
   || TPKTLF.del()
   ?}
?};
0


\tpktlfde
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTLF - ustala domyślny wariant dla danego typu karty technologicznej
::  OLD: \tpktlfde/slownik1.fml
::  TAG: <TPKTL><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
_new:=TPKTLF.OPC;
_ok:={? TPKTLF.TYP().DEF_OPCK=null()
     || 2
     || TPKTLF.OPC<>TPKTLF.TYP().DEF_OPCK
     ?};
{? _ok=1
|| {? FUN.ask('Czy na pewno zmienić domyślny wariant?'@)
   || TPKTL.seek(TPKTLF.TYP);
      TPKTL.DEF_OPCK:=_new;
      TPKTL.put()
   ?}
|? _ok=2
|| TPKTL.seek(TPKTLF.TYP);
   TPKTL.DEF_OPCK:=_new;
   TPKTL.put()
|| FUN.info('Ten wariant jest wariantem domyślnym.'@)
?}


\tpktlfrb
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: TPKTLF - przed rekord w wariantach typu
::  OLD: \tpktlfrb/tech.fml
::  TAG: <TPKTL><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? TPKTLF.OPC=TPKTLF.TYP().DEF_OPCK
|| echo('Wariant domyślny typu'@);
   'TPKTLF#01#01'
|| echo('','');
   ''
?}


\chk_tpktlf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TPKTLF - po rekordzie
::  TAG: <TPKTL><MBUILDER><CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_tpktlf','tech_common',-menu_txt()='popraw')


\leg_tpktlf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2009]
:: OPIS: TPKTLF - Legenda w oknie tabeli
::  OLD: \leg_tpktlf/slownik1.fml
::  TAG: <TPKTL><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','TPKTLF#01#01')


::======================================================================================================================
:: Formuły obsługi tabeli KWAR (warianty kalkulacji technologii)
::======================================================================================================================


\chk_kwar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: KWAR - Kontrola wypełnienia rekordu w słowniku wariantów kalkulacji technologii
::  OLD: \chk_kwar/tex_kalk.fml
::  TAG: <KWAR><CHK><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_kwar','tech_kalk',-menu_txt()='popraw')


\usun_kwar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: KWAR - Usunięcie
::  OLD: \usun_kwar/tex_kalk.fml
::  TAG: <KWAR><DEL><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
_ask:='';
_link:=KWAR.testlink(2);
{? _link.first()
|| {!
   |? {? _link.TABELA='KFORM'
      || _ask:='formuły'@;
         _link.del()
      |? _link.TABELA='KKS'
      || _ask:={? _ask<>'' || 'formuły i stałe'@ || 'stałe' ?};
         _link.del()
      || _link.next()
      ?}
   !}
?};
{? _link.first()
|| FUN.info('Usunięcie nie jest możliwe.\nWskazany wariant jest już użyty w systemie.'@);
   0
|| {? FUN.ask(
         'Czy usunąć wskazany wariant?\n\n'@+
         {? _ask<>'' || 'Uwaga: usunięte także zostaną %1\nprzypisane do wariantu.'@[_ask] || '' ?},
      )
   || KFORM.index('WR');
      KFORM.prefix(KWAR.ref());
      {? KFORM.first() || {! |? KFORM.del() !} ?};
      KKS.index('WR');
      KKS.prefix(KWAR.ref());
      {? KKS.first() || {! |? KKS.del() !} ?};
      KWAR.del()
   ?}
?};
~~


\kwarcopy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: KWAR - Kopiowanie wariantu kalkulacji
::  OLD: \kwarcopy/tex_kalk.fml
::  TAG: <KWAR><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
_kwar:=KWAR.ref();
undefine();
define('WAR',KWAR.NAZ,'Nowy wariant'@,'Nazwa nowego wariantu'@,40,40);
def_btn('text=%1'['Zapisz'@],'key:F2');
def_btn('text=%1'['Anuluj'@],'key:Esc');
{? def_edit("chk_rec()",FUN.TYT)
|| KWAR.NAZ:=DEFINE.WAR;
   {? KWAR.add(1)
   || KFORM.index('WR');
      KFORM.prefix(_kwar);
      {? KFORM.first()
      || {!
         |? KFORM.cntx_psh();
            KFORM.OPC:=KWAR.ref();
            KFORM.prefix();
            KFORM.add();
            KFORM.cntx_pop();
            KFORM.next()
         !}
      ?};
      KKS.index('WR');
      KKS.prefix(_kwar);
      {? KKS.first()
      || {!
         |? KKS.cntx_psh();
            KKS.OPC:=KWAR.ref();
            KKS.prefix();
            KKS.add();
            KKS.cntx_pop();
            KKS.next()
         !}
      ?}
   || FUN.info('"'+KWAR.NAZ+'"\n\n'+'Wariant o tej nazwie już istnieje.'@)
   ?}
?};
undefine();
~~


\kwar_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: KWAR - Rekord przed
::   WY: Schemat kolorowania
::  OLD: \kwar_rec/tech1.fml
::  TAG: <KWAR><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? 2+menu_pth()='TR'
|| {? TKTL.TYP().DEF_OPCK=KWAR.ref() || 'KWAR#01#01' || '' ?}
|? 1+menu_pth()='T'
|| {? VAR.A_KTL().DEF_OPCK=KWAR.ref() || 'KWAR#01#01' || '' ?}
|| ''
?}


::======================================================================================================================
:: Formuły obsługi tabeli KKS (stałe do kalkulacji technologii)
::======================================================================================================================


\tpktlcon
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: KKS - Stałe kalkulacji wywoływane z okienka wariantów kalkulacji
::  OLD: \tpktlcon/slownik1.fml
::  TAG: <KWAR><LISTA>
::----------------------------------------------------------------------------------------------------------------------
VAR.A_WAR:=KWAR.ref();
KWAR.cntx_psh();
KKS.index('WR');
KKS.prefix(VAR.A_WAR);
KRUB.win_dict('WYB');
KRUB.actions('WYB','W');
KKS.win_sel('WER');
KKS.select();
KRUB.actions('WYB');
KWAR.cntx_pop();
~~


\chk_kks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: KKS - po rekord w oknie WER
::  TAG: <KWAR><MBUILDER><CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_kks','tech_kalk',-menu_txt()='popraw')


::======================================================================================================================
:: Formuły obsługi tabeli TTXCAUSE (przyczyny zmian w technologii)
::======================================================================================================================


\chk_ttxcause
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: TTXCAUSE - po rekord w oknie WER
::----------------------------------------------------------------------------------------------------------------------
exec('chk_ttxcause','tech_common',-menu_txt()='popraw')


::======================================================================================================================
:: Formuły obsługi tabeli GKTL (grupy kart technologicznych)
::======================================================================================================================


\chk_gktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: GKTL - po rekord w oknie WER
::----------------------------------------------------------------------------------------------------------------------
exec('chk_gktl','tech_common',-menu_txt()='popraw')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 90e30b00f1a5df5d01230002655a3a1726eba7aa8c47b5973bb1736e854a430d5401f50a8b8901cc422eacd18d67461f9a650390a1ddebaf8d15adb801316dfc8b418e177dfd03979ac959d6a9f0e364d2cdb2b14afc9e24b1b389cfdcd49c4972b0d985a670cb78d2fb8b1325a2bc1d3e593aef0498c7d589a1d6c693d9b203

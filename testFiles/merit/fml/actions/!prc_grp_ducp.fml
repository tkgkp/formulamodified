:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_grp_ducp.fml
:: Utworzony: 06.12.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_GRP_DUCP - Usunięcie rozliczenia.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Usunięcie danych z tabel z rozliczeniem czasu pracy.
::   WE:
::   WY:
::  OLD: \PUSDANpo/sprdane.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

{? FUN.ask('%1\n%2\n%3'
      [  'Dla wskazanego zakresu dat oraz wybranego współpracownika zostaną usunięte wszystkie dane z kartotek:',
         '   - danych z czytników,\n   - wejść/wyjść,\n   - przepracowanych dni,\n'+
         '   - opisów błędnych zapisów,\n   - kwalifikacji godzin.\n\n',
         'Czy kontynuować?'
      ]
   )
|| {? A_OKR.OD>date() & A_OKR.S='O'
   || VAR_EDIT.D_OD:=VAR_EDIT.D_DO:=date(A_OKR.OD~1,A_OKR.OD~2,1)
   |? A_OKR.DO<date() &A_OKR.S='O'
   || VAR_EDIT.D_OD:=VAR_EDIT.D_DO:=date(A_OKR.DO~1,A_OKR.DO~2,0)
   || VAR_EDIT.D_OD:=VAR_EDIT.D_DO:=date()
   ?};
   VAR_EDIT.win_edit('ZAKR_DAT');
   VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_OD');
   VAR_EDIT.efld_opt('ZAKR_DAT','mark=1',,'D_DO');

   {? VAR_EDIT.edit("exec('sprawdz_dane','!prc_grp_ducp')")
   || _rok:=VAR_EDIT.D_DO~1;
      _msc:=VAR_EDIT.D_DO~2;

      _args:=exec('wybierz_args','pracownik');
      _args.DOMAIN:='PRC';
      _args.UD_SCH:=exec('domyslny','schemat','PODZORG');
      _args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
      _args.F_ZATR:={? +P_FILTER.F_ZATR().KOD || P_FILTER.F_ZATR().KOD || '*T' ?};
      _args.VIEW:=P_FILTER.STATUS;
      _TAB:=exec('wybierz','pracownik',_args).P;
      _okr:=__HARM.OKR_REF;

      {? _TAB.first()
      || A_OKRP.cntx_psh();
         A_OKRP.index('A_OKRPR');
         P.cntx_psh();
         R_ERRKAL.cntx_psh();
         MASK.Use('R_ERRKAL',_rok,_msc);
         R_ERRKAL.index(R_ERRKAL.ndx_tmp(,1,'P',,,'D',,));
         R_ERRMAX.cntx_psh();
         MASK.Use('R_ERRMAX',_rok,_msc);
         R_ERRMAX.index('R_ER_DZ');

         PROGRESS.set(_TAB.size(),'Trwa usuwanie danych.'@);
         KOMM.init(
            MS.fld_len('OSOBA','NAZWISKO')+MS.fld_len('OSOBA','PIERWSZE')+MS.fld_len('P','T')+15,,
            ' Uwagi po wykonaniu operacji grupowej: Usunięcie danych.'@,''
         );

         {!
         |? {? P.seek(_TAB.REF,,1)
            || exec('delDaneRCP4Prac','okres',P.ref(),VAR_EDIT.D_OD,VAR_EDIT.D_DO,_okr,1,0,0)
            ?};
            PROGRESS.next();
            _TAB.next()
         !};
         PROGRESS.close();
         KOMM.select(,,,1,,1);

         R_ERRMAX.cntx_pop();
         R_ERRKAL.cntx_pop();
         P.cntx_pop();
         A_OKRP.cntx_pop()
      ?}
   ?}
?};
~~


\sprawdz_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Sprawdzenie poprawności wprowadzonych wartości w oknie do edycji dla czynności usunięcia rozliczenia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=__CHK.record(VAR_EDIT,,'D_OD','D_DO');
{? _result=''
|| _result:='D_OD';
   {? VAR_EDIT.D_OD>VAR_EDIT.D_DO
   || FUN.emsg('Wartość w polu \"Od daty\" nie może być większa od wartości w polu \"Do daty\".'@)
   |? VAR_EDIT.D_OD~1<>VAR_EDIT.D_DO~1 | VAR_EDIT.D_OD~2<>VAR_EDIT.D_DO~2
   || FUN.emsg('Podane daty muszą być z tego samego roku i miesiąca rozliczeniowego.'@)
   |? exec('msc_otwarty','prc_mscrozlicz',VAR_EDIT.D_OD~1,VAR_EDIT.D_OD~2,'R')='Z'
   || FUN.emsg('Podane daty nie mogą dotyczyć zamkniętego miesiąca rozliczeniowego.'@)
   || _result:=''
   ?}
?};
_result

:Sign Version 2.0 jowisz:1045 2023/07/21 13:25:24 d65d7ac2c451ccd11890dd5824913a71470ad411b24e9c3f165991c95f2e061ab4a22245bc204e94fd5f491195f363c25a7aa7d8128ca3989440fe28bd29b30fd40cfeb11251b6292d09512f14680c9033af6c5fce8cabbd1969b8762e814a09ae43a80c726a1fd6036bad0f705b841e682f3b0be48fc2ea9671af049452e3a0

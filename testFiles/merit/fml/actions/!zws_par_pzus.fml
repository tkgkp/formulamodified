:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pzus.fml
:: Utworzony: 08.03.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PZUS — Słowniki kodów ZUS.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słowniki kodów ZUS - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Parametry importu / eksportu.
_cfg:=exec('s_zus_cfg','personel');

:: Tabela tymczasowa kategorii słowników ZUS.
_KAT:=exec('kat','!zws_par_pzus');

:: Okno prezentacji.
_wnd:=exec('wnd','!zws_par_pzus',_KAT);

params_set('cfg',_cfg);

_kod:=EDIT_VAR.KOD_DOK;

S_ZUS.cntx_psh();
S_ZUS.index('S_ZUS');
S_ZUS.prefix();
S_ZUS.win_edit('RED');
S_ZUS.win_patt('RED');
_KAT.win_sel(_wnd.grp);
_KAT.win_edit(_wnd.we);
_KAT.select();
S_ZUS.cntx_pop();

EDIT_VAR.KOD_DOK:=_kod;

~~


\kat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przygotowanie tabeli tymczasowej z kategoriami słowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Zapytanie, którego wynikiem są wszystkie istniejące w danych kategorie słowników ZUS i wolne miejsce na ich nazwę.
_KAT:=sql('select distinct S_ZUS.RODZAJ Kod, space(60) Nazwa from S_ZUS  order by Kod');
_add:=
   "  _KAT:=_a;
      {? _KAT.find_key(_b,)
      || _KAT.NAZWA:=_c;
         _KAT.put()
      || _KAT.KOD:=_b;
         _KAT.NAZWA:=_c;
         _KAT.add()
      ?}
   ";

:: Uzupełnienie nazwy każdego słownika.
_add(_KAT,'A','Praca w szczególnych warunkach');
_add(_KAT,'B','Praca o szczególnym charakterze');
_add(_KAT,'BADLEKO','Wyniku orzeczenia badania lekarskiego');
_add(_KAT,'C','Choroba (niezdolności do pracy)');
_add(_KAT,'E','Prawo do emerytury');
_add(_KAT,'F','Niezdolność do pracy');
_add(_KAT,'I','Typ identyfikatora');
_add(_KAT,'N','Niepełnosprawność');
_add(_KAT,'P','Tytuł ubezpieczenia');
_add(_KAT,'R','Stopień pokrewieństwa');
_add(_KAT,'S','Świadczenie/przerwa');
_add(_KAT,'USPKOD','Kod ustania stosunku pracy / stosunku służbowego');
_add(_KAT,'USPPPRAK','Podstawa prawna ustania stosunku pracy / stosunku służbowego');
_add(_KAT,'USPSTR','Strona inicjująca rozwiązanie stosunku pracy');
_add(_KAT,'W','Wnioskodawca blokady obliczeń składek emerytalno-rentowych');
_add(_KAT,'Y','Zgłoszenie/wyrejestrowanie członka rodziny');
_add(_KAT,'Z','Przyczyna wyrejestrowania');

_KAT


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Budowa okna prezentacji.
::   WE: _a [TABLE] - Alias tabeli tymczasowej z kategoriami słowników ZUS (patrz formuła \kat).
::   WY: Akronim utworzonego okna grupowego.
::----------------------------------------------------------------------------------------------------------------------
_KAT:=_a;

_ws:=_KAT.mk_sel('Kategorie'@,,,'#s_zuskat',,,,,'U');
_KAT.win_fld(_ws,,'KOD',,,,,,'Kod'@,,'Kod kategorii słownika'@);
_KAT.win_fld(_ws,,'NAZWA',,,,,,'Nazwa'@,,'Opis zawartości słownika'@);
_KAT.win_act(_ws,,'Formuła','Import'@@,,'Import zawartości słowników'@,
   "params_exec('import_b','!zws_par_pzus')",
   "params_exec('import_a','!zws_par_pzus')",,,,,'I'
);
_KAT.win_act(_ws,,'Formuła','Eksport'@@,,'Eksport zawartości słowników'@,
   "params_exec('eksport_b','!zws_par_pzus')",
   "params_exec('eksport_a','!zws_par_pzus')",,,,,'E'
);
_KAT.win_act(_ws,,'Szukaj');
_KAT.win_act(_ws,,'Kolejność');
_KAT.win_btn(_ws,'text='+'Import'@+',panel=bottom,align=begin','menu:I');
_KAT.win_btn(_ws,'text='+'Eksport'@+',panel=bottom,align=begin','menu:E');

_mode:='maximized_with_title';
_mode:='maximized';
_grp:=_KAT.grp_make('Słowniki kodów ZUS'@,,,,,,,'html_maximized');
_KAT.grp_sel(_grp,,_ws,,"grp_disp(S_ZUS,'WER')",,,,,,,,_mode);
_KAT.grp_splt(_grp,,'vertical','s_zus');
_KAT.grp_sel(_grp,S_ZUS,'WER',,,,,,"S_ZUS.prefix(EDIT_VAR.KOD_DOK:=cur_tab(0,0).KOD,)",,,,_mode);

_we:=_KAT.mk_edit('Kategoria'@);
_KAT.win_esep(_we,'Dane podstawowe'@);
_KAT.win_efld(_we,,'KOD',,,40);
_KAT.win_efld(_we,,'NAZWA',,,40);

_wnd:=obj_new('ws','we','grp');
_wnd.ws:=_ws;
_wnd.grp:=_grp;
_wnd.we:=_we;

_wnd


\import_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Import - przed".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=params_get().cfg;
{? ~fexists(_cfg.fn,_cfg.pth)
|| FUN.info('Brak dostępu do pliku %1.\n'@ [_cfg.ffn]);
   0
|| FUN.ask('Czy aktualizować zawartość słowników na podstawie pliku %1?'@ [_cfg.ffn])
?}


\import_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Import - po" - właściwy import słowników.
::       Treść formuły powinna być zsynchronizowana z treścią formuły \eksport_a.
::   WE:
::   WY: Wynik importu [0/1].
::----------------------------------------------------------------------------------------------------------------------
_ret:=exec('s_zus_import','personel',0);
FUN.info(
   {? _ret
   || 'Import zakończony powodzeniem.'@
   || 'Import nie zakończył się powodzeniem.'@
   ?}
);
_ret


\eksport_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Eksport - przed".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=params_get().cfg;
{? fexists(_cfg.fn,_cfg.pth)
|| FUN.ask('Plik %1 juz istnieje. Nadpisać?'@ [_cfg.ffn])
|| FUN.ask('Czy eksportować zawartość słowników do pliku %1?'@ [_cfg.ffn])
?}


\eksport_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Eksport - po" - właściwy eksport słowników.
::       Treść formuły powinna być zsynchronizowana z treścią formuły \import_a.
::   WE:
::   WY: Wynik eksportu [0/1].
::----------------------------------------------------------------------------------------------------------------------
_cfg:=params_get().cfg;
S_ZUS.cntx_psh();
S_ZUS.index('S_ZUS');
S_ZUS.prefix();
_ret:=S_ZUS.export(_cfg.fn,1,_cfg.sep,_cfg.opt,,'RODZAJ',,1,,'KOD',,2,,'OPIS',,3,);
S_ZUS.cntx_pop();
FUN.info(
   {? _ret
   || 'Eksport zakończony powodzeniem.'@
   || 'Eksport nie zakończył się powodzeniem.'@
   ?}
);
_ret


\s_zus_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" dla tabeli S_ZUS.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(S_ZUS,-menu_txt()='popraw',,'KOD')

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 04892c61e89efeccd18cc1d236355083a4c60c99a9e0323baff80a74cab23d987c79e650c4b2aa57732bf9d643597f2b8a28272d0e63002af82c5cbc2d0c85e092382e156ec7de6ad1eaf9acdb55389bf65764c3ad70169b8e43a78a6bfd6822df6d37cce63f6b687123f06e747b182f5a7ede77e6d4b236c90de75e8d58f458

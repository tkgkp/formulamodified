:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ltds.fml
:: Utworzony: 02.12.2014
:: Autor: [rr]
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_LTDS
::            Definiowanie typów dokumentów sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Czynność ZWS_PAR_LTDS - definiowanie typów dokumentów sprzedaży
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejsciowe czynnosci
::       _int - [obj_new] - parametry wewnetrzne czynnosci
::       _out - [obj_new] - parametry wyjsciowe czynnosci
::       _mp  - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('select','!zws_par_ltds');
''


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: definiowanie typów dokumentów sprzedaży
::  OLD: \typy_sp/defin.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.NRDOK_AB:='FAKS';
BEER.ZAK:='N';
BPMN.KK_DOM:='LMG';

:: Ustawienie okienka KH w typach dokumentów sprzedaży
KH.cntx_psh();
KH.win_sel('WER_SLO');
KH.actions('WER_SLO','W:W');

TYPYSP.win_sel('WER');
TYPYSP.hdr_sel();
TYPYSP.hdr_sel(' sprzedaży');
TYPYSP.win_edit('REDS');
TYPYSP.win_patt('FINDS');
TYPYSP.index('TYPYSP');
TYPYSP.prefix('N');
TYPYSP.select();

KH.cntx_pop();
~~


\prtsppar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcją pola TYPYSP.PAR i TYPYSP.DRPAR
::   WY: 1-do redakcji 0-zablokowana redakcja
::  OLD: \prtsppar/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? cur_afld()='PAR'
||
   {? TYPYSP.BL='T'
   || _wyn:=0
   |? exec('sp_count','typysp') & exec('FindInSet','#table','TYPYSP','DOKSP',TYPYSP.T,TYPYSP.T)=null & TYPYSP.KOR<>'Z'
      & TYPYSP.KOR='N'
   || _wyn:=1
   || _wyn:=0
   ?}
|? cur_afld()='DRPAR' & ((TYPYSP.ZAL='N' & TYPYSP.FIS='N') | TYPYSP.KOR='T' | TYPYSP.KOR='Z')
|| _wyn:=0
?};
_wyn


\potyppar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcją pola TYPYSP.DOKSP, TYPYSP.KH
::   WY: 0-jeśli TYPYSP.PAR<>'T' 1-w każdym innym przypadku
::  OLD: \potyppar/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('t_or_n','#field')
|| {? TYPYSP.PAR='N' || TYPYSP.DOKSP:=''; TYPYSP.KH:=null || TYPYSP.FIS:='T'; TYPYSP.NDVAT:='N' ?};
   exec('set_efld_opt','typysp');
   1
|| 0
?}


\prtyppar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcją pola TYPYSP.DOKSP, TYPYSP.KH
::   WY: 0-jeśli TYPYSP.PAR<>'T' 1-w każdym innym przypadku
::  OLd: \prtyppar/defin.fml
::----------------------------------------------------------------------------------------------------------------------
TYPYSP.PAR='T'


\f3tsppar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: klawisz F3 na typy dokumentów do paragonu
::  OLD: \f3tsppar/defin.fml
::----------------------------------------------------------------------------------------------------------------------
TYPYSP.cntx_psh();
BEER.SZ:='S';
_where:='KOR=\'N\' and ZAL=\'%1\' and PAR=\'N\' and DRPAR=\'N\' and ZAK=\'N\' and FIS=\'T\''[TYPYSP.ZAL];
_where+={? (1+menu_txt)='P' || ' and T<>\''+TYPYSP.T+'\'' || '' ?};
_typ:=exec('tsp_upr','typysp',_where,1,,,,0);
_doksp:={? _typ<>null() & (TYPYSP.clear(); TYPYSP.seek(_typ)) || TYPYSP.T || '' ?};
TYPYSP.cntx_pop();
TYPYSP.DOKSP:=_doksp;
1


\potsppar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po typie dokumentu do paragonu
::   WY: 1-jest OK 0-źle
::  OLD: \potsppar/defin.fml
::----------------------------------------------------------------------------------------------------------------------
TYPYSP.cntx_psh();
BEER.SZ:='S';
_where:='KOR=\'N\' and ZAL=\'%1\' and PAR=\'N\' and DRPAR=\'N\' and ZAK=\'N\' and FIS=\'T\''[TYPYSP.ZAL];
_where+={? (1+menu_txt)='P' || ' and T<>\''+TYPYSP.T+'\'' || '' ?};
_typ:=' '+exec('tsp_upr','typysp',_where,2,0,,,0)+' ';
TYPYSP.cntx_pop();
{? TYPYSP.DOKSP<>'' & ~(_typ*(' '+TYPYSP.DOKSP+' '))
|| FUN.info('Nieprawidłowy typ dokumentu sprzedaży do paragonu.'@); 0
|| 1
?}


\po_khpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji pola kontrahent dla paragonu
::   WY: 1-jest OK 0-źle
::  OLD: \po_khpar/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld=null
|| FUN.info('Należy podać domyślnego kontrahenta dla paragonu.'@);
   0
|| 1
?}

:Sign Version 2.0 jowisz:1045 2023/11/21 14:46:44 b7cfea08d8749e64b9012edfa065bff178c2280d2222bc34514c97bcd44972f9ebccd35b4657f9fdd497e2328e90e198e205620eddb638d7961881033ed6af0b509a7c7c1c24154de88e0013af7340cdfd97a7bbcb8c36094e5104a11c204af61c4c4bcd9e0a37eefc3fb8f18f55d1d3f0901cc57d6503ae66e2dce71bcfe9f3

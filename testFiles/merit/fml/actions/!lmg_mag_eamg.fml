:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_eamg.fml
:: Utworzony: 06.08.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Akceptacja dokumentu magazynowego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# permissions=ODDZ,LMG
::# parses=exec('parses_nd','lmg')
::# access=exec('sprUprMag','%todo')
::# kind=WE,   symbol=ND,      type=_ND,    name=Dokument magazynowy,   required=T,    keyref=T
::# kind=WY,   symbol=RESULT,  type=STRING, name=Rezultat działania,    required=N

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_context:=params_get().context;

_akcja:=_mp.akcja();
_service:=_mp.isService();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & (_mp.isAutoRun() | _service));
_group:=_mp.isGroup();

{? var_pres('RESULT',_out)<>type_of('') || _out.RESULT:='OK'; _mp.save('OUT','RESULT','OK') ?};

_can_continue:=1;
_clean_result:=params_exec('clean','!lmg_mag_eamg',_mp,_in);
_can_continue:=_clean_result.RESULT;
_nd:=_clean_result.ND;

{? _can_continue>0
||
:: dla serwisu ustalamy jego środowisko
   _msk:=ref_name(_nd)+3;
   exec('mag_psh','open_tab');
   {? _service | (_auto & _msk<>(ND.name()+3)) || exec('mag_open','open_tab',(1+_msk),(_msk+2)) ?};

   ND.prefix();
   {? ~ND.seek(_nd)
   || _mp.error('Nie znaleziono dokumentu.')
   || {? _service
      || ST.MAG:=ND.MAG;
         ST.AM:=ND.AM;
         ST.AR:=ND.AR
      ?};
      {? _akcja='Akceptuj' | _auto
      || _arg:=
            {? var_pres('_context')<>type_of(~~) & var_pres('AKCEPTUJ',_context)=type_of(obj_new('a'))
            || _context.AKCEPTUJ
            || exec('akceptuj_a','!lmg_mag_eamg')
            ?};
         _arg.MP:=_mp;
         _arg.AUTO:=_auto;
         _arg.GROUP:=_group;
         {? (_oki:=exec('akceptuj','!lmg_mag_eamg',_arg))<=0
         || {? _service
            || _out.RESULT:='BŁĄD';
               _mp.save(,_out);
               _mp.done()
            |? _oki<0
            || _mp.error('Nie zakończono jeszcze rejestracji dokumentu. Akceptacja niemożliwa.')
            ?}
         ?}
      |? _mp.pathTodo()
         | _mp.pathArea() & _akcja=''
      || params_set(params_get());
         _win_red:=exec('nd_win_edit','magdok_nag');
         _ff:="params_exec('nd_pozycje_todo','!lmg_mag_eamg')";
         ND.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
         _ff:="params_exec('nd_akceptuj_todo','!lmg_mag_eamg'); 'key:Esc'";
         ND.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
         _ff:="'key:Esc'";
         ND.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
         ND.win_edit(_win_red);
         exec('set_efld_opt','magdok_nag',_win_red);
::       Dalsza obsługa akceptacji w exec('akceptuj','!lmg_mag_eamg')
         ND.display();
::       Usunięcie definicji tymczasowych okien
         ND.win_edit(''); ND.win_edel(_win_red)
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   exec('mag_pop','open_tab')
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ND',_in)<>type_of(~~) & _in.ND
|| 'Zaakceptuj dokument magazynowy: %1'@@[exec('record','#to_string',_in.ND)]
|| ''
?}


\nd_akceptuj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument magazynowy - Akceptuj z todo
::   WE: params_get()   - ustawiane w exec('main','!lmg_mag_eamg')
::----------------------------------------------------------------------------------------------------------------------
_arg:=exec('akceptuj_a','!lmg_mag_eamg');
_arg.MP:=params_get().mp;
exec('akceptuj','!lmg_mag_eamg',_arg);
''


\nd_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument magazynowy - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('poz_dok','magdok_poz',0);
''


\akceptuj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Parametry funkcji exec('akceptuj','!lmg_mag_eamg')
::   WE:
::   WY: tablica argumentów
::----------------------------------------------------------------------------------------------------------------------
_arg:=obj_new('MP','AUTO','ND_UNLOCK','GROUP');
:: =1-automatycnza akceptacja, 0-wpp
_arg.AUTO:=0;
:: =1-odblokować ND, =0-wpp
_arg.ND_UNLOCK:=1;
:: operacja grupowa
_arg.GROUP:=0;
:: argumenty
_arg


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument magazynowy - Akceptacja dokumentu
::   WE: _a - exec('akceptuj_a','!lmg_mag_eamg')
::   WY: 1-zaakceptowano 0-nie -1-dokument niezakończony
::----------------------------------------------------------------------------------------------------------------------
_mp:=_a.MP;
_auto:=_a.AUTO;
_nd_unlock:=_a.ND_UNLOCK;
_group:=0;
{? var_pres('GROUP',_a)
|| _group:=_a.GROUP
?};

_result:=0;

{? exec('czy_z_ok','okresy',1,1,ND.AR,ND.AM,ND.MAG) & exec('blok_nd','magdok_nag','T')
||
   {? ND.STAT_REJ='T'
   || {? ~_auto
      || _txt:='Dokument został już zaakceptowany.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
         || FUN.info(_txt)
         ?}
      ?};
      _result:=1;
      _mp.done()
   |? ND.STAT_REJ='N'
   || {? ~_auto
      || _txt:='Nie zakończono jeszcze rejestracji dokumentu.\nAkceptacja niemożliwa.'@;
         {? _group
         || exec('add_kom','#message',ND.SYM,5,gsub(_txt,'\n',' '))
         || FUN.info(_txt)
         ?}
      ?};
      _result:=-1
   |? ND.TYP().VALACC<>null() & ~exec('validate','wzorce',ND.TYP().VALACC,ND,ND.ref())
   || _result:=0
   |? _auto | | _group | FUN.ask('Zaakceptować dokument %1?'@[ND.SYM])
   || ND.Z:='T';
      ND.STAT_REJ:='T';
      ND.DA:=date();
      ND.TA:=time();
      {? ND.put()
      || {? _group
         || _txt:='Zaakceptowano dokument.'@;
            exec('add_kom','#message',ND.SYM,1,gsub(_txt,'\n',' '))
         ?};
         exec('FindAndGet','#table',FAKS,ND.FAKS,,"
            _mp:=_b;
            {? FAKS.WHERE='G' & FAKS.CZY_DRUK<>'T' & FAKS.EDOKUMEN='T'
            ||
               _dr_fak:=0;
               exec('dok_maga','powdok',0);
               _loop:=__tt.first();
               {!
               |? _loop
               |!
                  _dr_fak:=exec('FindAndGet','#table',ND,__tt.REF,,\"ND.STAT_REJ='T'\",0);
                  _loop:=_dr_fak & __tt.next()
               !};
               VAR_DEL.delete('__tt');
               {? _dr_fak
               ||
                  {? FAKS.EDOKUMEN='T'
                  ||
                     {? _mp.isGroup()
                     ||
                        exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Akceptacja E-dokument',,1)
                     ||
                        exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Akceptacja E-dokument')
                     ?}
                  ||
                     {? _mp.isGroup()
                     ||
                        exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Akceptacja Utwórz PDF',,1)
                     ||
                        exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Akceptacja Utwórz PDF')
                     ?}
                  ?}
               ?}
            ?}
         ",,_mp);
         _result:=1;
         _mp.done()
      ?}
   ?};
   {? _nd_unlock || ND.r_unlock() ?}
?};

_result


\nd_akceptuj_wer_dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument magazynowy - Akceptuj - okno pozycji
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LMG_MAG_EAMG';
_params.UIDREF:=ND.uidref();
_params.AKCJA:='Akceptuj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ND',ND.ref());
_params.CONTEXT:=obj_new('AKCEPTUJ');
_params.CONTEXT.AKCEPTUJ:=exec('akceptuj_a','!lmg_mag_eamg');
_params.CONTEXT.AKCEPTUJ.ND_UNLOCK:=0;

exec('mp_run','#b__box',_params)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};

_can_continue:=1;
_obj:=obj_new('RESULT','ND');
_obj.RESULT:=0;
_obj.ND:=null();

_keyRefs:=_mp.getRefs();

{? obj_len(_keyRefs)>0
||
   {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];

      {? type_of(_kref)>0
      ||
         {? ref_tab(_kref)=ND
         || _obj.ND:=exec('FindAndGet','#table',ND,_kref,,,null());

            {? _obj.ND=null()
            ||
::             Nie znaleziono rekordu kluczowego powiązanego, więc robię error
               _can_continue:=0;
               _msg:='Dokument magazynowy nie został odnaleziony, prawdopodobnie został usunięty.'@;
               {? _mp.isService()=0 & _mp.CLEANER=0
               || {? _mp.isGroup()
                  || KOMM.add(_msg,2,,1)
                  || FUN.emsg(_msg)
                  ?}
               ?};
               _mp.error(_msg)
            |? ~_mp.isMicro() & exec('FindAndGet','#table',ND,_obj.ND,,"ND.STAT_REJ",'')='T'
            ||
::             Dokument już zaakceptowany, robimy done()
               _can_continue:=0;

               _msg:='Dokument magazynowy jest już zaakceptowany, proces będzie kontynuowany.'@;
               {? _mp.isService()=0 & _mp.CLEANER=0
               || {? _mp.isGroup()
                  || KOMM.add(_msg,2,,1)
                  || FUN.emsg(_msg)
                  ?}
               ?};
               _out:=_mp.load(exec('kind_out','#b_port'));
               _out.RESULT:='OK';
               _mp.save(,_out);
               _mp.done()
            ?}
         ?}
      ?}
   !}
?};

{? _can_continue>0
||
:: jest parametr wejściowy ND
   {? _obj.ND=null() & var_pres('ND',_in)=type_of(null())
   || _obj.ND:=_in.ND
   ?}
?};

{? _can_continue>0
||
   {? _obj.ND=null()
   ||
::    Nie było key refa ani parametru wejściowego, robię error
      _can_continue:=0;
      _msg:='Dokument magazynowy nie został przekazany do czynności.'@;
      {? _mp.isService()=0 & _mp.CLEANER=0
      || {? _mp.isGroup()
         || KOMM.add(_msg,2,,1)
         || FUN.emsg(_msg)
         ?}
      ?};
      _mp.error(_msg)
   ?}
?};

{? _can_continue>0
|| _obj.RESULT:=1
?};
_obj


:Sign Version 2.0 jowisz:1045 2023/11/08 13:34:32 4c5c3457bf28f9dd4d8921b62164db959caa1d2751a6619b3f384a2030fa10d0106ec56c6f989fbf8c1e6b0c452df8ceb76a331b1b9ff8a2b900981242f4574b01b3f730547fe84be14ff2bdfc386c7008ec4bc5661c2e01ecb349993dcecb050cf986ecc032a6a4d2a1e7cf238aaa9c411bf707e3a57f5bc81010fbdffcc7dc

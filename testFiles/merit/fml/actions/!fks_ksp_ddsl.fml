:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksp_ddsl.fml
:: Utworzony: 06.09.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_KSP_DDSL - Wybór sprawozdania
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wybór sprawozdania - główna formuła czynności FKS_KSP_DDSL
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::# permissions=FJKS
:: PARAMETRY WY:
::# kind=WY, symbol=GR_SIK, type=_GR_SIK, name=Wskazanie na sprawozdanie, required=T
::# kind=WY, symbol=RECZNE, type=STRING, name=Sprawozdanie z wartościami ręcznymi [T/N], required=T
::# kind=WY, symbol=OKRES_OD, type=_OKRO_F, name=Okres bilansowy od, required=T
::# kind=WY, symbol=OKRES_DO, type=_OKRO_F, name=Okres bilansowy do, required=T
:: WARUNKI BRAMY:
::# condition=Wiersze ręczne, act_uid=FKS_KSP_DRED, auto=T, formula=_a.RECZNE='T'
::# condition=Wiersze automatyczne, act_uid=FKS_KSP_DEAT, auto=T, formula=_a.RECZNE='N'
_args:=params_get();
_wy:=_args.out;
_mp:=_args.mp;
{? exec('sel_gr_sik','!fks_ksp_ddsl')
|| _wy.GR_SIK:=GR_SIK.ref();
   _wy.RECZNE:=exec('reczne','!fks_ksp_ddsl');
   OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
   {? OKRO_F.first()
   || _wy.OKRES_OD:=OKRO_F.ref()
   ?};
   _wy.OKRES_DO:=SSTALE.AO;
   _mp.save(,_wy);
   _mp.done()
?}


\sel_gr_sik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia sprawozdanie wybrane przez użytkownika
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
_o:=GR_SIK.mk_sel('Sprawozdania'@,'P',,'#grsiksel',,,,,'U');
GR_SIK.win_fld(_o,,'SKROT',,,,,,'Kod'@);
GR_SIK.win_fld(_o,,'NAZWA',,,,,,'Nazwa'@);
GR_SIK.win_fld(_o,,'ZAOKR',,,,,,'Wskaźnik zaokrąglenia'@);
GR_SIK.win_fld(_o,,'DOKL',,,,,,'Dokładność'@);
GR_SIK.win_fld(_o,,'AKC',,,,,,'Akceptacja'@,,,2,,"'T'","'N'");
GR_SIK.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
GR_SIK.win_sel(_o);
_firma:={? REF.S_FIRMA<>REF.GRUPA || '(GR_SIK.FIRMA=\''+$REF.FIRMA+'\' or GR_SIK.FIRMA=\''+$REF.GRUPA+'\') and ' || 'GR_SIK.FIRMA=\''+$REF.GRUPA+'\' and ' ?};
GR_SIK.f_set('SKROT','join DEFW',_firma+'GR_SIK.AKC=\'T\'');
{? GR_SIK.select()
|| _ok:=1
?};
GR_SIK.f_clear();
_ok


\reczne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy sprawozdanie zawiera wiersze ręczne
::   WY: 'T'ak lub 'N'ie
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
DEFW.cntx_psh();
DEFW.index('ALGORYTM'); DEFW.prefix(GR_SIK.ref(),'R');
_wy:={? DEFW.first() || 'T' || 'N' ?};
DEFW.cntx_pop();
_wy

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:41 d65ed25d6c77fbd2b0cdda88b2c4d97f64cfb19ff7b64eb784d68852197d43081df90d730d0ff1d3f6c50ed95d5875f3c32fde1aaad441bcd038764e2c1fd9443e537ef6f030321dc7a174960e14da674bdee3b2e308ff574427dc1f535fc1723a1d849a07cb9676600ba0df04a052fc964b073a65dcef4d4ef1141d848563ef

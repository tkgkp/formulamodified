:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_ofe_wwof.fml
:: Utworzony: 30.10.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Utworzenie oferty w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=OFE,     type=_OFE,   name=Oferta,        required=T, keyref=T
::# kind=WE,   symbol=CZY_PDF, type=STRING, name=Utwórz PDF,    required=N, keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE,   symbol=PRN_MSK, type=STRING, name=Maska wydruku, required=N, keyref=N, fml_val="exec('rep_exec','#b_report','LSP_OFE_WWOF','lsp_ofer*','Wydruki oferty',-1)"
::# kind=WY,   symbol=DOKUM,   type=_DOKUM, name=Załącznik,     required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_zkn','lsp');

_akcja:=_mp.akcja();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' ?};
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || {? exec('ctrlMask','#b_report',_in.PRN_MSK,'lsp_ofer') || _in.PRN_MSK || '' ?}
      || ''
      ?};

{? var_pres('OFE',_in)=type_of(null()) & _in.OFE
||
:: Ustawienie kontekstu wg instancji elementu w procesie
   OFE.cntx_psh();
   OFE.prefix();
   {? OFE.seek(_in.OFE)
   ||
      _dokum:=exec('ofe_dru','!lsp_ofe_wwof',_czy_pdf,_msk);
      {? _czy_pdf & _dokum<>null()
      ||
         _out.DOKUM:=_dokum;
         _mp.save(,_out);
         {? ~_keep || _mp.done() ?}

      |? ~_czy_pdf & _dokum
      ||
         {? ~_keep || _mp.done() ?}
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};
   OFE.cntx_pop()
||
   _mp.error('Brak parametru wejściowego OFE.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('OFE',_in)<>type_of(~~) & _in.OFE
|| 'Utwórz PDF oferty: %1'@@[exec('record','#to_string',_in.OFE)]
|| ''
?}


\ofe_dru
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: Akcja drukuJ dla okna OFE.WER
::   WE: [_a] - czy zapisywać dokument 0 - nie 1 - tak
::       [_b] - maska wydruku
::   WY: dla _a=1: ref/null _a=0: 0/1
::  OLD: \ofe_dru/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
_msk:={? var_pres('_b')=type_of('') || _b || '' ?};
_wyn:={? _a || null || 0 ?};

{? exec('upr_cs','ceny')=0 || FUN.info('Brak uprawnień do wartości sprzedaży.\nOpcja niedostępna.'@); return() ?};

{? _msk<>''
|| _wydr:=_msk
|| _wydr:='lsp_ofer*'
?};

{? _a=1
|| NRDOK.index('NRDOK');
   NRDOK.prefix('SYS','');
   {? ~NRDOK.first()
   || _ok:=0;
      FUN.info('Nie zdefiniowano numeracji SYS dla dokumentów powiązanych.'@)
   ?}
?};

POLA.NAZ_WYDR:='';

{? _a=1
||
   _symb:=exec('str_to_pth','#string',OFE.SYM);
   _file:='Oferta '+_symb+'.pdf';
   _wydr:=rep_exec(_wydr,,,_file,1)
||
   _wydr:=exec('rep_exec','#b_report','LSP_OFE_WWOF',_wydr,'Oferta',2,,,,,,'W')
?};

{? POLA.NAZ_WYDR<>'' & _a=1 & _wydr=1
|| _wyn:=exec('save_dok','dokum','OFE',_file,POLA.NAZ_WYDR,OFE.SYM,OFE.KH().EM
      ,'Oferta o symbolu: '+OFE.SYM+' wystawca: '+XINFO.NAZ,
      PARAM_W.JEZYK,OFE.STAT_REJ)
|? _a=0
|| _wyn:=_wydr
?};
_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 d6a7a3a36bc01865bd2f9287b0a704056cd2cbe367519a42d6da402bfa2e2450a1f2174695510555e6fb12aa5881fe7bed0eeec315701428293dd8b71a0c6afdb306a303c489dec3fecdbc2116f60daba8898c68024661bc246b1b7a99431c60f02fd49da2cfd9a1113ab89c74b8b28834d0a57b377e425ace2d3c88d68631d7

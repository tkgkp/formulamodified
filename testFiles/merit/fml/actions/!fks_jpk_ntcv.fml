:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_jpk_ntcv.fml
:: Utworzony: 05.04.2023
:: Autor: PBS
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_JPK_NTCV - Wysyłanie powiadomień o plikach JPK_V7
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wysyłanie powiadomień JPK_V7 - główna formuła czynności ZWS_NTC_SEND
::----------------------------------------------------------------------------------------------------------------------
::# properties=SEND,GRP_FIRM
params_set(params_get());
_mp:=params_get().mp;
_in:=params_get().in;

{? _mp.isAutoRun() | _mp.isService() | 1
|| params_exec('jpk_v7_ntc_send','!fks_jpk_ntcv')
?};
_mp.done()


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wysyłanie powiadomień JPK_V7 - opis na liście zadań dla czynności FKS_JPK_NTCV
::----------------------------------------------------------------------------------------------------------------------
'Wyślij powiadomienie w aplikacji'@@


\jpk_v7_ntc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Powiadomienia dla użytkowników o konieczności wysłania JPK_V7 - pomocnicza
::----------------------------------------------------------------------------------------------------------------------
_okrof:=exec('cur_okro_f','tagger');
_obj:=obj_new('NTC','OKRES','VAT_DEK','JPK');
OKRO_F.cntx_psh(); OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
{? OKRO_F.seek(_okrof) & OKRO_F.prev()
|| {! |?
      {? OKRO_F.POCZ=date(0,0,0) || OKRO_F.prev() || 0 ?}
   !};
   {? OKRO_F.POCZ=date(0,0,0)
   || _obj.OKRES:=_okres:=''; _rokf:=null
   || _obj.OKRES:=_okres:=$(OKRO_F.POCZ~1)+'/'+form(OKRO_F.POCZ~2,-2); _rokf:=OKRO_F.ROK
   ?}
|| _obj.OKRES:=_okres:=''; _rokf:=null
?};
_ntc:=0;

OKRO_F.cntx_pop();
VAT_DEK.cntx_psh(); JPK.cntx_psh();
VAT_DEK.index('VAT_ROT'); VAT_DEK.prefix(_rokf,_okres,'V7');
{? _okres<>'' & _rokf<>null & VAT_DEK.first()
|| {! |?
      _obj.VAT_DEK:=VAT_DEK.uidref();
      {? VAT_DEK.STATUS='A'
      || JPK.index('DODAT'); JPK.prefix(3+VAT_DEK.TYP,$VAT_DEK.ref());
         {? JPK.first()
         || {! |?
               _obj.JPK:=JPK.uidref();
               {? JPK.STAT='T'
               || _ntc:=0
               |? JPK.STAT='W'
               || _ntc:=5
               || _ntc:=4
               ?};
               JPK.next & _ntc<>0
            !}
         || _ntc:=3; _obj.JPK:=''
         ?}
      || _ntc:=2; _obj.JPK:=''
      ?};
      VAT_DEK.next() & _ntc<>0
   !}
|| _ntc:=1; _obj.VAT_DEK:=''; _obj.JPK:=''
?};
{? _obj.VAT_DEK=''
|| VAT_DEK.index('VAT_TYP'); VAT_DEK.prefix('V7');
   {? VAT_DEK.last()
   || _obj.VAT_DEK:=VAT_DEK.uidref()
   ?}
?};
VAT_DEK.cntx_pop(); JPK.cntx_pop();
_obj.NTC:=_ntc;
_obj


\jpk_v7_ntc_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła wysyłająca powiadomienie o konieczności wykonania akcji wysłania JPK_V7
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'LINK_SRV','LINK_PRT','LINK_INT');
_wyn:=exec('jpk_v7_ntc','!fks_jpk_ntcv');
_ntc:=_wyn.NTC;
_okres:=_wyn.OKRES;
_vat_dek:=_wyn.VAT_DEK;
_jpk:=_wyn.JPK;
::_tosend:=obj_new('TXT','TITLE','INFO','BTN','ACT');
{? _ntc
|| _txt:=exec('txt','%jpk',_ntc,_okres);
   _title:=exec('title','%jpk',_ntc);
   _obj:=obj_new('variant','icon','info');
   _obj.variant:=exec('variant','%jpk');
   _obj.icon:=exec('icon','%jpk');
   _obj.info:=exec('info','%jpk');
   _priority:=json_obj(_obj);
   _btn:=exec('jpk_v7_ntc_btn','%jpk',_ntc);
   _app_ident:=exec('app_ident','#ntc');
   _cluster_group:=app_info('cluster_group');
   {? _ntc<>4 & _ntc<>5
   || _link:=exec('dek_link','%jpk',_vat_dek,_app_ident,_cluster_group)
   || _link:=exec('dek_link','%jpk',_jpk,_app_ident,_cluster_group)
   ?};
   _users:=params_exec('r_users_to_send','%jpk');
   {? _users<>''
   || STR.split(_users,',');
      {! |?
         _user:=STR.get_word();
         USERS.cntx_psh();
         USERS.index('USR_KKOD'); USERS.prefix(_user);
         {? USERS.first() || _uref:=USERS.ref() || _uref:=null ?};
         USERS.cntx_pop();
         _act:=exec('jpk_v7_ntc_act','%jpk',_link,_uref);
         _dni:=date()+exec('get','#params',7013,,_uref);
         ntc_send(_user,_app_ident,_cluster_group,_title,_txt,'jpk_v7_'+_okres,_priority,,_dni,_btn,_act);
         STR.next()
      !}
   ?}
|| ''
?}

:Sign Version 2.0 jowisz:1045 2023/09/13 08:33:30 c90078148f4fc1f93c7ef9a35a0497bc1155894d1f7cad741b5e95b85a815c56f40007a9166bbfde896bea52d597abf883f5ba98fa3e790e4209da8c74cffeebef3a0b2e9f616bbd687ff91d3535b9584eba8f10081ac4bbd87988451b013cc20b144a9e79dbf9ce4af23b3d1933a3c04de4c4f400021d20ee271d521a856c2e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_man_uall.fml
:: Utworzony: 13.01.2015 [17.00]
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności ręcznej ZWS_MAN_UALL
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Czynność ręczna (ZWS_MAN_UALL) - formuła główna
::----------------------------------------------------------------------------------------------------------------------
::# properties=MANUAL,GRP_FIRM

params_set(params_get());
_mp:=params_get().mp;
_out:=params_get().out;
_web:=app_info('web_sesid')<>'';
_web_done:=0;
_web_keep:=0;
_web_cancel:=0;
{? _web
   & var_pres('context',params_get())=type_of(obj_new('obj'))
||
   _context:=params_get().context;
   _web_done:=_context.METH='DONE';
   _web_keep:=_context.METH='KEEP';
   _web_cancel:=_context.METH='CANCEL';
   _context.BI_PREL:=_mp.bi_prel
?};

{? _mp.bi_proc<>null() & _mp.bi_prel<>null() & _mp.buf_act.UID<>''
||
   _biPrel:=exec('buffer','#bi_prel'); _biPrel.cntx_get(_mp.bi_prel);
   _bPrel:=exec('buffer','#b_prel'); _bPrel.cntx_get(_biPrel.B_PREL);
   {? _web=0
      | _web_keep
   ||
      {? _bPrel.FMANOUT<>''
      || _val:=exec('run','!zws_man_uall',_bPrel.FMANOUT);
         {? type_of(_val)=type_of(null()) & _val<>null() & ref_tab(_val)=BI_BLOB
         || _blobs:=_mp.bl_get(_val);
            {? _blobs.first()
            || {!
               |? _mp.bl_add(_blobs.BLOB);
                  _blobs.next()
               !}
            ?};
            exec('delete','#bi_blob',_val)
         || _out.OUT:=_val
         ?};
         _mp.save(,_out)
      ?}
   ?};
   {? _web
   || {? _web_done || _mp.done()
      |? _web_keep || {? var_pres('_out')=type_of(obj_new('obj')) & var_pres('OUT',_out)>0 || _context.OUT:=_out.OUT ?};
         _mp.keep()
      |? _web_cancel || _mp.cancel()
      ?}
   |? _bPrel.FMANMAIN=''
   || _ask:='Czy zakończyć czynność ręczną: \''+_bPrel.SYMBOL+'\'?';
      {? FUN.ask(_ask) || _mp.done() ?}
   || {? _bPrel.FMANOUT<>''
      || _done:=exec('run','!zws_man_uall',_bPrel.FMANMAIN,_out.OUT)
      || _done:=exec('run','!zws_man_uall',_bPrel.FMANMAIN)
      ?};
      {? type_of(_done)=type_of(0) & _done>0
      || _mp.done()
      |? type_of(_done)=type_of(~~)
      || _mp.error('Błędny typ wyniku formuły dla czynności ręcznej.')
      ?}
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis czynności ręcznej (ZWS_MAN_UALL)
::   WY: Opis na To-Do
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_mp:=params_get().mp;
:: sztuczny zapis aby czynność nie była wykazywana jako niedostosowana do tłumaczeń podczas aktualizacji czynność
''@@;
_desc:='';
{? _mp.bi_proc<>null() & _mp.bi_prel<>null() & _mp.buf_act.UID<>''
|| _biPrel:=exec('buffer','#bi_prel'); _biPrel.cntx_get(_mp.bi_prel);
   _bPrel:=exec('buffer','#b_prel'); _bPrel.cntx_get(_biPrel.B_PREL);
   {? _bPrel.FMANDESC<>''
   || _desc:=exec('run','!zws_man_uall',_bPrel.FMANDESC);
      {? type_of(_desc)<>type_of('')
      || _mp.error('Błędny typ wyniku formuły dla opisu czynności ręcznej.')
      ?}
   ?}
?};
_desc


\run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Uruchomienie formuły dla czynności ręcznej
::   WE: _a - treść formuły
::       [_b] - parametr formuły
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? var_pres('_b')>=0 || ($_a)(_b) || ($_a)() ?}


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.02]
:: OPIS: Czynność ręczna (ZWS_MAN_UALL) - formuła główna - webTerm
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=params_get();
_path:=_params.PATH;

params_set(_params);

:: logowanie kolejki BI_QUEUE
_log_on:=0;
_jTerm:=app_info('web_sesid')='';
_webTerm:=~_jTerm;
_log_name:='%1_%2_%3.log'['bi_queue',{? _webTerm || 'webterm' || 'jterm' ?},hash(FIRMA.ses_id(),'md5')];
:: zainicjowanie instancji czynności ZWS_MAN_UALL
_ffaa:="";
{? _path='Proc'
||
   _b_prel:=_params.PREL;
   _b_prel_act:=_params.PREL_ACT;
   _context:=obj_new('METH','BI_PREL','OUT','LOG_ON');
   _context.METH:='KEEP';
   _context.BI_PREL:=null();
   _context.OUT:=~~;
   _context.LOG_ON:=_log_on;
   {? _log_on
   ||
      _log:=fopen(_log_name,'Ua',1,,1);
      {? _log.is_open() || fwrite(_log,'%1\t%2'['>>>','web_main_proc']) ?};
      obj_del(_log)
   ?};
:: po wykonaniu web_run instancja będzie miała status __Status.OCZEKUJACA
   exec('web_run','#b__box',_b_prel,'Proc',,'web_Proc',,_context);
   _bi_prel:=_context.BI_PREL;
   _out:=_context.OUT;
   _ffaa:="
      _context:=obj_new('METH','BI_PREL','OUT');
      _context.METH:={? _a || 'DONE' || 'CANCEL' ?};
      _context.BI_PREL:=web_params_get().BI_PREL;
      _context.OUT:=web_params_get().OUT;
      {? web_params_get().LOG_ON
      ||
         _log:=fopen(web_params_get().LOG_NAME,'Ua',,,1);
         {? _log.is_open() || fwrite(_log,'%1\t%2'['>>>','web_main_proc_po']) ?};
         obj_del(_log)
      ?};
      exec('web_run','#b__box',_context.BI_PREL,'Todo',,'web_Todo',,_context,,,,'T')
   "
|?
   _path='Todo'
||
   _bi_prel:=_params.PREL;
   _b_prel_act:=exec('FindAndGet','#table',BI_PREL,_bi_prel,,"BI_PREL.B_PREL");
   _context:=obj_new('METH','BI_PREL','OUT','LOG_ON');
   _context.METH:='KEEP';
   _context.BI_PREL:=_bi_prel;
   _context.OUT:=~~;
   _context.LOG_ON:=_log_on;
:: po wykonaniu web_run instancja będzie miała status __Status.OCZEKUJACA
   {? _log_on
   ||
      _log:=fopen(_log_name,'Ua',1,,1);
      {? _log.is_open() || fwrite(_log,'%1\t%2'['>>>','web_main_todo']) ?};
      obj_del(_log)
   ?};
   exec('web_run','#b__box',_bi_prel,'keep',,'web_Todo',,_context);
   _out:=_context.OUT;
   _ffaa:="
      _context:=obj_new('METH','BI_PREL','OUT');
      _context.METH:={? _a || 'DONE' || 'CANCEL' ?};
      _context.BI_PREL:=web_params_get().BI_PREL;
      _context.OUT:=web_params_get().OUT;
      {? web_params_get().LOG_ON
      ||
         _log:=fopen(web_params_get().LOG_NAME,'Ua',,,1);
         {? _log.is_open() || fwrite(_log,'%1\t%2'['>>>','web_main_todo_po']) ?};
         obj_del(_log)
      ?};
      exec('web_run','#b__box',_context.BI_PREL,'Todo',,'web_Todo',,_context,,,,'T')
   "
|?
   _path='Area'
||
:: brak obsługi bo nie ma automatycznego uruchamiania więc nie uruchomi się w obszarze
   ~~
?};

:: obsługa instancji czynności ZWS_MAN_UALL
{? _bi_prel<>null()
||
   _ff:=exec('FindAndGet','#table',B_PREL,_b_prel_act,,"B_PREL.FMANMAIN");
   {? _ff<>''
   ||
::    uruchomienie formuły głównej podanej w definicji zadania ręcznego
      params_set('BI_PREL',_bi_prel,'PATH',_path,'LOG_ON',_log_on,'LOG_NAME',_log_name);
      _ff:="params_set(params_get()); "+_ff;
      {? _context.OUT=~~ || ($_ff)() || ($_ff)(_context.OUT) ?}
   ||
::    standardowa obsługa zadania ręcznego (nie podano formuły głównej)
      web_params_set('BI_PREL',_bi_prel,'OUT',_out,'LOG_ON',_log_on,'LOG_NAME',_log_name);
      _symbol:=exec('FindAndGet','#table',B_PREL,_b_prel_act,,"B_PREL.SYMBOL");
      web_ask(_ffaa,'Czy zakończyć czynność ręczną: \''+_symbol+'\'?',exec('nazwa','#system'))
   ?}
?}


\web_main_example
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.02]
:: OPIS: przykład formuły web_main
::   WE: [_a] - wartość parametru wyjściowego (w przykładzie przyjęto, że jest typu STRING)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_out:=''; {? var_pres('_a')=type_of('') || _out:=_a ?};

{? app_info('web_sesid')=''
||
:: obsługa jTerm
   FUN.info('web_main_example_cntx_jTerm');
   1

||
:: obsługa webTerm
   web_msg('web_main_example_cntx_webTerm');
   _params:=params_get();
   _bi_prel:=_params.BI_PREL;
   _path:=_params.PATH;

   _b_prel:=exec('FindAndGet','#table',BI_PREL,_bi_prel,,"BI_PREL.B_PREL",null());
   _symbol:=exec('FindAndGet','#table',B_PREL,_b_prel,,"B_PREL.SYMBOL");
   _system:=exec('nazwa','#system');
:: Inicjuje obiekt Menadżera Procesu
   _class:={? __develop || @.Class.cProMan || @.CLASS.cProMan ?};
   _mp:=obj_new(_class);
   _mp.setCntx(_bi_prel);
:: wczytuję parametry wejściowe
   _in:=_mp.load(exec('kind_in','#b_port'));
:: ustawiam parametry dla formuły _ffaa
   web_params_set('BI_PREL',_bi_prel,'OUT',_out);

   {? _path='Proc'
   ||
      _ffaa:="
         _context:=obj_new('METH','BI_PREL','OUT');
         _context.METH:={? _a || 'KEEP' || 'CANCEL' ?};
         _context.BI_PREL:=web_params_get().BI_PREL;
         _context.OUT:=web_params_get().OUT;
         exec('web_run','#b__box',_context.BI_PREL,'Todo',,'web_Todo',,_context,,,,'T')
      ";
      web_ask(_ffaa,'Czy zachować czynność ręczną na liście zadań: %1?'
         '\n\nParametr wyjściowy ma wartość: %2.'@[_symbol,_out],_system)

   |? _path='Todo'
   ||
      _ffaa:="
         _context:=obj_new('METH','BI_PREL','OUT');
         _context.METH:={? _a || 'DONE' || 'CANCEL' ?};
         _context.BI_PREL:=web_params_get().BI_PREL;
         _context.OUT:=web_params_get().OUT;
         exec('web_run','#b__box',_context.BI_PREL,'Todo',,'web_Todo',,_context,,,,'T')
      ";
      web_ask(_ffaa,'Czy zakończyć czynność ręczną: %1?'
         '\n\nParametr wyjściowy ma wartość: %2.'@[_symbol,_out],_system)

   |? _path='Area'
   ||
::    brak obsługi bo nie ma automatycznego uruchamiania więc nie uruchomi się w obszarze
      web_msg('web_main_example_cntx_webTerm_area');
      ~~
   ?}
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 3116dbf03e407a6926313e595706a0967461540f0edc8118106da5def4ee0fe15034b4def172b51b0ce83530269332c2d3daa0aa4a86e02ea79388fd9125f6a6f722b1cead5236f7b01c5f733b34842bbd69871bf74ec508af3d241e6c8dad232e5f2b102d5b965f9ac42142daed6de5f3d267556ed7595347c1048fdeea70c1

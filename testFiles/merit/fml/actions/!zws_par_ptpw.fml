:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ptpw.fml
:: Utworzony: 12.02.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PTPW - Rodzaje potrąceń wynagrodzenia.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Redagowanie rodzajów potrąceń wynagrodzenia - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \potr_slownik/komornik.fml
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

{? __RUB.sys_sql(2111)=''
|| FUN.info(
      'Atrybuty składników płacowych nie pozwalają na parametryzację typów potrąceń.\n'
      'Brak wartości atrybutu 2111.\n'
      'Uzupełnij brakującą konfigurację i spróbuj ponownie.'@
   );
   return(0)
?};

R.cntx_psh();
R.prefix();

KOM_RP.cntx_psh();
KOM_RP.index('KOM_RP');
KOM_RP.prefix();
KOM_RP.win_sel('WER');
KOM_RP.win_edit('RED');
KOM_RP.first();
KOM_RP.select();
{? KOM_RP.first() & PAR_SKID.get(230)<>'T'
|| FUN.info('Parametr %1 (%2) nie jest ustawiony.\nPotrącenia pracownicze nie będę realizowane.'@
      ['230',PAR_SKID.opis(230)]
   )
?};
KOM_RP.cntx_pop();

R.cntx_pop();
~~


\kom_rp_r_pt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wzorzec dla pola KOM_RP.R.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_rs:=__RUB.sys_sql(2111);
{? _rs='' || _rs:='0' ?};
R.f_set('RN',,'RN in (:_a)',_rs);
''


\kom_rp_ao
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Okienko - po".
::       Jeżeli operator wszedł do pola KOM_RP.R (w formule przed redagowaniem założony został filtr) i zrezygnował
::       z redagowania (nacisnął ESC) to formuła po redagowaniu pola (zdejmująca filtr) nie wykonała się.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.f_clear();
1


\kom_rp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu rekordu tabeli KOM_RP.
::   WE:
::   WY:
::  TAG:
::  OLD: \spr_rodz/komornik.fml
::----------------------------------------------------------------------------------------------------------------------
_put:=-menu_txt()='popraw';
_chk:=__CHK.table(KOM_RP,_put,,'N','R','P');
{? type_of(_chk)<>type_of(0) | ~_chk
|| return(_chk)
?};

: Może trochę nadmiarowo, ale sprawdzamy, czy rubryka jest odpowiednia.
{? ~__RUB.sys_attr(KOM_RP.R,2111)
|| FUN.emsg(
      'Składnik płacowy: "%1" nie jest uwzględniony w atrybucie rubryk nr 2111.\n'
      'Należy dodać ten składnik płacowy do odpowiedniego atrybutu.'@ [KOM_RP.R().RT]);
      return('R')
?};

: Sprawdzenie priorytetu.
{? KOM_RP.P<0
|| return(__CHK.err_fld(KOM_RP,'P',1,'Wartość nie może być ujemna.'@))
|? ~exec('sprawdz_priorytet','komornik',
      KOM_RP,{? _put || KOM_RP.ref() || null() ?},KOM_RP.P,KOM_RP.PROC,KOM_RP.MIN,KOM_RP.ZUS,KOM_RP.EMER)
|| FUN.emsg(
      'Niedozwolone jest, aby potrącenia z tym samym priorytetem\n'
      'miały inny procent wynagrodzenia zasadniczego lub minimalnego.'@
   );
   return('P')
?};

{? KOM_RP.ALIMENTY='T' & ~__RUB.sys_attr(KOM_RP.R,21111)
|| FUN.emsg(
      'Składnik płacowy: "%1" nie jest uwzględniony w atrybucie rubryk nr 21111.\n'
      'Należy dodać ten składnik płacowy do atrybutu: Potrącenie alimentacyjne.'@ [KOM_RP.R().RT]
   );
   return('R')
?};
{? KOM_RP.INNE='T' & ~__RUB.sys_attr(KOM_RP.R,21114)
|| FUN.emsg('Składnik płacowy: "%1" nie jest uwzględniony w atrybucie rubryk nr 21114.\n'
      'Należy dodać ten składnik płacowy do atrybutu: Potrącenia inne - nie występujące w KP.'@ [KOM_RP.R().RT]
   );
   return('R')
?};

: Na wszelki wypadek sprawdzamy poprawność wartości procentowych.
{? ~exec('czy_procent','#field',KOM_RP,'PROC')
|| return('PROC')
|? ~exec('czy_procent','#field',KOM_RP,'MIN')
|| return('MIN')
|? ~exec('czy_procent','#field',KOM_RP,'ZUS')
|| return('ZUS')
|? ~exec('czy_procent','#field',KOM_RP,'EMER')
|| return('EMER')
?};

{? KOM_RP.F<>''
|| _tab:=form_chk($KOM_RP.F);
   {? _tab.first()
   || FUN.emsg(
         'Formuła ma nieprawidłową składnię.\n\nZnak: %1\n%2\n\nNależy poprawić treść formuły.'@
         [$_tab.ERR_COL,_tab.ERR_DESC]
      );
      return('F')
   ?}
?};


''

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:06 b3ef79ae340bcef02eb777a8aee2fee42c53ee58bbea3e50d35129436ba39e196412392c2cd3dd40f2ea208fad0b1bb17a068d7c624d0d2b2a1e60d04848a3e327a80d032b180443823e4042aef605821392d6b55e192ba30861f2ba6a66421563651f2c9f53af671646aecff78a57eb6468b8fa633c57f765df0bc297310d83

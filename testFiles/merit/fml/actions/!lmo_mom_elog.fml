:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmo_mom_elog.fml
:: Utworzony: 23.03.2018
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły czynności LMO_MOM_ELOG - Archiwizacja informacji o log-ach urządzeń mobilnych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=IL_DNI,  type=NUMBER, name=Ilość dni wstecz do archiwizacji,    required=N,    keyref=N
::# kind=WY, symbol=IL_ARCH, type=NUMBER, name=Ilość zarchiwizowanych informacji,   required=N,    keyref=N
::# properties=SERVICE

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_service:=_mp.isService();

{? ~_service
|| _mp.error('Czynność wyłącznie serwisowa.');
   _mp.done()
|| _il_dni:={? var_pres('IL_DNI',_in)=type_of(0) & _in.IL_DNI>0 || _in.IL_DNI || 1 ?};
   _date:=date()-_il_dni;
   _il_arch:=0;
   EANLOG.cntx_psh();
   EANLOG.use('elog____');
   EANLOG.index('DTTM');
   {? EANLOG.find_le(_date)
   || {!
      |? {? {? EANLOG.DATA<>date(0,0,0)
            || EANLOG.DATA
            || exec('str2date','#convert',(10+EANLOG.IDADD))
            ?}<=_date
         || _msk:=form((EANLOG.DATA~1)-2000,-2,0,'99')+form((EANLOG.DATA~2),-2,0,'99');
            EANLOG.get();
            EANLOG.cntx_psh();
            EANLOG.use('elog'+_msk);
            EANLOG.prefix();
            EANLOG.IDOLD:=EANLOG.IDADD;
            {? EANLOG.add() || _il_arch+=1 ?};
            EANLOG.cntx_pop();
            EANLOG.del()
         || EANLOG.next()
         ?}
      !}
   ?};
   EANLOG.cntx_pop();
   _mp.save('OUT','IL_ARCH',_il_arch);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
''

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:46 002d0d1778f78eab209583c08675f23f8a627ae7f5fc69e8534d68ac5d374cef4f22b34240704068cab01b1deaea26fe972525345da3f901dff8ad129266eb87a115fca9715889bc624e30fa45407f3b0eec903fa8f64813937e69a47ab270891a6d5b53f483da94e417a35d2432f3c6873c26189c4a343e9227ad3a3706acef

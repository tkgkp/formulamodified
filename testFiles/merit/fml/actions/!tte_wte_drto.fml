:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_wte_drto.fml
:: Utworzony: 01.06.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_WTE_DRTO - Rejestrowanie operacji wzorca technologii
::            Uwaga: większość kodu jest wspólna z czynnością TTE_TEC_DRTO - w przypadku zmian modyfikować oba pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności rejestrowania listy operacji dla wzorca technologii (TTE_WTE_DRTO)
::       Zadaniem formuły jest zredagowanie listy operacji z zamiennkami stanowisk, atrybutami i dokumentami
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;

:: UPRAWNIENIA
::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=T, keyref=T
{? var_pres('TKTL',_in)<>type_of(~~) & var_pres('TKTL',_in)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_in)=type_of(~~) || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=N
{? var_pres('TKTL',_out)<>type_of(~~) & var_pres('TKTL',_out)<>type_of(null()) || return() ?};

{? _mp.isMicro()
||
:: Obsługa poza procesem
   _akcja:=_mp.akcja();

:: Zakończenie rejestrowania
   {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
   ||
      VAR.A_KTL:=_in.TKTL;

::    Generuje następniki, jeżeli brak
      NASTOPER.cntx_psh();
      NASTOPER.index('OPER');
      NASTOPER.prefix('T',_in.TKTL);
      {? ~NASTOPER.first()
      || exec('er_tab','tech_oper',_in.TKTL);
         exec('gen_pow','tech_oper',_in.TKTL);
         exec('graph_oper','tech_oper',0)
      ?};
      NASTOPER.cntx_pop();

      {? exec('valid_op1','tech_oper',_in.TKTL)
            &
         exec('valid_op','tech_oper',_in.TKTL,0,1)
            &
         exec('valid_str','tech_oper',_in.TKTL)
            &
         FUN.ask('Czy zakończyć rejestrację operacji dla całego wzorca technologii?'@)

      || exec('FindAndGet','#table',TKTL,_in.TKTL,,"STAT_O:='T'; STAN:='P'; put()")
      ?}

   |? _akcja='SEL'
   ||
::    Obsługa funkcji 'Operacje' wywołanej z menu okna wertowania
      exec('toper_main','tech_oper',_in.TKTL,1)
   ?}
||
:: Obsługa w procesie
   exec('toper_in_process','!tte_wte_drto',_in,_int,_out,_mp)
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności rejestrowania listy operacji dla wzorca technologii (TTE_WTE_DRTO)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj operacje dla wzorca technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"'Zredaguj operacje dla wzorca technologii %1 wer. %2'@@[NRK,WER]",'')

:: jest parametr wejściowy to ustawiam odpowiedniego TKTL
|? var_pres('TKTL',_in)
|| _tmp:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj operacje dla wzorca technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"'Zredaguj operacje dla wzorca technologii %1 wer. %2'@@[NRK,WER]",'')

?};
_desc


\toper_in_process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła rejestrowania listy operacji dla wzorca technologii
::       Używana przez exec('main','!tte_wte_drto')
::   WE: _a - [obj_new] - parametry wejściowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_int:=_b;
_out:=_c;
_mp:=_d;

_torw:=VAR.A_TORW:='W';
_arch:=VAR.ARCH:='N';

_akcja:=_mp.akcja();

_ok:=0;
_res:=~~;

{? _in.TKTL
||
   {? exec('FindAndGet','#table',TKTL,_in.TKTL,,"TORW",'')<>_torw
   || _msg:='Niezgodność wywołania czynności.\nNie można redagować operacji dla karty technologicznej.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   |? exec('FindAndGet','#table',TKTL,_in.TKTL,,"ARCH",'')<>_arch
   || _msg:='Niezgodność wywołania czynności.\nNie można redagować operacji dla wzorca archiwalnego.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   |? exec('FindAndGet','#table',TKTL,_in.TKTL,,"TYP().OPER",'')<>'T'
   || _msg:='Niezgodność wywołania czynności.\nTyp wzorca wyklucza redagowanie operacji.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   ||
      exec('tktl_cntx_psh','tech_common');
      exec('tktl_use','tech_common',ref_name(_in.TKTL)+3);
      TKTL.clear();

      {? TKTL.seek(_in.TKTL)
      ||
         _mp.keyRef(TKTL.uidref());
         _mp.descTodo();

         VAR.A_KTL:=TKTL.ref();
         VAR.GRUPA:='N';
         VAR.A_OP:=null();
         VAR.A_ZLEC:=null();

::       Właściwa obsługa
::       Zakończenie rejestrowania
         {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
         ||
::          Generuje następniki, jeżeli brak
            NASTOPER.cntx_psh();
            NASTOPER.index('OPER');
            NASTOPER.prefix('T',TKTL.ref());
            {? ~NASTOPER.first()
            || exec('er_tab','tech_oper',TKTL.ref());
               exec('gen_pow','tech_oper',TKTL.ref());
               exec('graph_oper','tech_oper',0)
            ?};
            NASTOPER.cntx_pop();

            {? exec('valid_op1','tech_oper',TKTL.ref())
                  &
               exec('valid_op','tech_oper',TKTL.ref(),0,1)
                  &
               exec('valid_str','tech_oper',TKTL.ref())
                  &
               FUN.ask('Czy zakończyć rejestrację operacji dla całego wzorca technologii?'@)

            || TKTL.STAT_O:='T';
               TKTL.STAN:='P';
               TKTL.put()
            ?}

::       Wywołanie z menu okna wertowania
         |? _akcja='SEL'
         || exec('toper_main','tech_oper',TKTL.ref(),1)

::       Lista TODO
         ||
::          Już wcześniej zakończono redagowanie operacji
            {? TKTL.STAT_O='T'
            || FUN.info('Zakończono rejestrację operacji dla całego wzorca technologii.'@)

::          Nie zakończono jeszcze redagowania nagłówka (być może usunięto zatwierdzenie)
            |? TKTL.STAT_N='N'
            || {? exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTE')
               || _choice:=FUN.choice('Nie zakończono rejestracji nagłówka wzorca.'@,,'Zakończ nagłówek i redaguj'@,'Podgląd'@);
                  {? _choice=1
                  || _args:=exec('mp_run_a','#b__box');
                     _args.ACT_UID:='TTE_WTE_DRTE';
                     _args.UIDREF:=TKTL.uidref();
                     _args.AKCJA:='ZAKOŃCZ_BEZ_PYTANIA';
                     exec('mp_run','#b__box',_args);
                     exec('toper_main','tech_oper',TKTL.ref(),1)
                  |? _choice=2
                  || exec('toper_main','tech_oper',TKTL.ref(),0)
                  ?}
               || _choice:=FUN.info('Nie zakończono rejestracji nagłówka wzorca.\nModyfikacje niemożliwe.'@,,'Podgląd'@,'Anuluj'@);
                  {? _choice=1
                  || exec('toper_main','tech_oper',TKTL.ref(),0)
                  ?}
               ?}
            || exec('toper_main','tech_oper',TKTL.ref(),1)
            ?}
         ?};

         TKTL.clear();
         {? TKTL.seek(_in.TKTL)
         ||
::          Ustawiony status - czynność zostaje zakończona
            {? TKTL.STAT_O='T'
            || _out.TKTL:=TKTL.ref();
               _mp.save(,_out);
               _mp.done();
               _res:='#CLOSE#'

            ?}
         ?}
      ?};
      exec('tktl_cntx_pop','tech_common')

   ?}

|| _msg:='Nie przekazono albo usunięto wzorzec technologii.';
   FUN.emsg(_msg);
   _mp.error(_msg)
?};

_res


\action_menu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie operacji dla wzorca technologii - właściwa akcja z menu okna wertowania TKTL
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| set_help(exec('set_help','#help','TTE_WTE_DRTO'));
   exec('toper_main','tech_oper',TKTL.ref(),0)

|? TKTL.ARCH='N' & TKTL.STAT_O='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTO')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRTO';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='SEL';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_WTE_DRTO'));
   exec('toper_main','tech_oper',TKTL.ref(),1)
?}


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zakończenie rejestrowania operacji dla wzorca technologii - obsługa przycisku w oknie wzorców technologii
::       (obsługa z pytaniem)
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_O='N'
|| {? exec('tktl_lock','tech_common',,'O')
   || _args:=exec('mp_run_a','#b__box');
      _args.ACT_UID:='TTE_WTE_DRTO';
      _args.UIDREF:=TKTL.uidref();
      _args.AKCJA:='ZAKOŃCZ_Z_PYTANIEM';
      _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
      exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

      exec('mp_run','#b__box',_args);

      exec('tktl_unlock','tech_common',,'O')
   ?}

|| FUN.info('Zakończono rejestrację operacji dla wzorca technologii.\nModyfikacje niemożliwe.'@)
?};
'key:Esc'

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:21 d76efe3b6439737c539dbeb3d359ae2975d8684354a2f279126e006d133b0ae67e773beb697124781e2ebacf93ffcdfa09b575003052c8b001dc3d7e795cb098c4c38e3db7b40bbabb8523089a3f2a06dae1b616e12fc02e666fa79f5087ff216231566df81c69582626d0ce1fed846fd1b1777578c536e421e35a66a513bcf8

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_zddz.fml
:: Utworzony: 23.04.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_KSR_ZDDZ - Przeglądanie dziennika
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przeglądanie dziennika - główna formuła czynności FKS_KSR_ZDDZ
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Dodaje okno do okna grupowego z rozdzieleniem na inTerm i jTerm
::   WE: _a - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')=0
||_red:=DZIENNIK.mk_edit(,,,,,,'auto');
   DZIENNIK.win_ebtn(_red,'text=%1,text_len=20, display=1,panel=left,align=begin'['Wydruki'@],"exec('raporty','!fks_ksr_zddz');''");
   DZIENNIK.win_ebtn(_red,'text=%1,text_len=20, display=1,panel=left,align=begin'['Prezentacja'@],"exec('dziennik','!fks_ksr_zddz');''");
   _b.grp_edit(_c,DZIENNIK,_red,_a)
|| VAR_DEL.delete('__TAB');
   __TAB:=tab_tmp(1
         ,'LP','INTEGER','Lp.'
         ,'NAME','STRING[60]','Nazwa'
         ,'HELP','STRING[60]','Podpowiedź'
         ,'FORMULA','STRING[255]','Formuła do wykonania'
      );
   _add:="
      _a.blank();
      _a.LP:=_a.size()+1;
      {? var_pres('_d')=3
      || _a.FORMULA:=$_d
      ?};
      _a.NAME:=
         {? _a.FORMULA=''
         || '[ %1 ]' [_b]
         || _b
         ?};
      _a.HELP:={? var_pres('_c')=type_of('') || _c || _a.NAME ?};
      _a.add()
   ";
   _add(__TAB,'Wydruki'@,'Wydruki'@,"exec('raporty','!fks_ksr_zddz');''");
   _add(__TAB,'Prezentacja'@,'Prezentacja'@,"exec('dziennik','!fks_ksr_zddz');''");
   __TAB.first();
   _ws:=__TAB.mk_sel(,'N',,'_fst_fun_tech',,,,,'U');
   __TAB.win_fld(_ws,,'NAME');
   __TAB.win_sopt(_ws,'select_record_checkbox=0');
   __TAB.win_act(_ws,,'Formuła','Uruchom'@,,
      ," _tab:=cur_tab(1,1);
         _ts:=time();
         ($_tab.FORMULA)();
         _tab.put()
      "
      ,,1,,,,'U'
   );
   _b.grp_sel(_c,__TAB,_ws,_a)
?};
~~


\dziennik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [19.22]
:: OPIS: Prezentacja dziennika w nowym oknie
::----------------------------------------------------------------------------------------------------------------------
{? var_press('WinRej')<=0
|| WinRej:=obj_new('DOK','AKC','WINREJ','REJ')
|| obj_del(WinRej.REJ)
?};
zakladka:=5;
WinRej.DOK:='C_REN_DZ';
WinRej.AKC:='FKS_KSR_ZDDZ';
WinRej.REJ:=exec('tab_rej','!fks_ksr_zddz');
WinRej.WINREJ:=WinRej.REJ.win_sel('?');
exec('context','dok_fks_ks',1);
WinRej.REJ.win_sopt(WinRej.WINREJ,'select_record_checkbox = 0');
DZIENNIK.fld_fml('OBWN','BEFORE_DISPLAY',"exec('nal_obr1','!fks_ksr_zddz')");
_grp:=DOK.grp_make('Dziennik',"");
DOK.grp_sel(_grp,WinRej.REJ,WinRej.WINREJ,,"
{? var_press('zakladka') || _zak:=zakladka ?};
   _odd:=_rej:=null;
   {? WinRej.REJ.TYP='W'
   || DOK.index('NRDZ');
      DOK.prefix('T','T')
   |? WinRej.REJ.TYP='O'
   || _odd:=WinRej.REJ.REF;
      DOK.index('NRDZ');
      DOK.prefix('T','T',WinRej.REJ.REF)
   || REJ.prefix();
      DOK.index('NRDZ2');
      {? REJ.seek(WinRej.REJ.REF,)
      || _rej:=REJ.ref();
         _odd:=REJ.ODD;
         DOK.prefix('T','T',REJ.ref())
      || DOK.prefix('T','T',null)
      ?}
   ?};
   DOK.first();
   grp_disp(DOK,WinRej.DOK)
",,,,"exec('nal_obr2','!fks_ksr_zddz',null,null)");
DOK.grp_splt(_grp,,'vertical','left',',30%');
DOK.grp_sel(_grp,DOK,WinRej.DOK);
DOK.win_sel(_grp);
DOK.select


\raporty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wyświetla raporty dziennika
::----------------------------------------------------------------------------------------------------------------------
DZIENNIK.cntx_psh();
REJ.f_clear();
exec('rep_exec','#b_report','FKS_KSR_ZDDZ','fks_dzien*','Wydruki dzienników',,,,,);
DZIENNIK.cntx_pop()


\obr_mc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Funkcja liczaca obroty dziennika w m-cu. Funkcja ustawia 4 pola zmiennej DZIENNIK:
::       sumy obrotow WN i MA dla kont bilansowych i pozabilansowych.
::       Brane sa pod uwage tylko operacje zaksiegowane koncowo.
::   WE: _a - nr roku bilansowego
::       _b - nr okresu
::       _c - ref rejestru
::       _d - ref jednostki ksiegowej
::  OLD: \obr_mc/dziennik.fml
::----------------------------------------------------------------------------------------------------------------------

DOK.cntx_psh(); POZ.cntx_psh(); VPOZ.cntx_psh(); POW.cntx_psh();


{? AN.use('koan__'+_a) & AN_SLU.use('koansl'+_a)
|| {? DOK.use('doku'+_a+_b) & POZ.use('pozy'+_a+_b) & VPOZ.use('pozv'+_a+_b) & POW.use('pow_'+_a+_b)
   || {? _b='00' & UNPAR.P7
      || VAR_DEL.delete('TAB_DZ');
         TAB_DZ:=tab_tmp(1,'KON','STRING[35]','Konto',
                           'TYP','STRING[1]','Typ',
                           'WN','REAL','WN',
                           'MA','REAL','MA')
      ?};


      {? _c
      || {? _d
         || DOK.index('NRDZ'); DOK.prefix('T','T',_d,_c)
         || DOK.index('NRDZ2'); DOK.prefix('T','T',_c)
         ?}
      || {? _d
         || DOK.index('NRDZ'); DOK.prefix('T','T',_d)
         || DOK.index('NRDZ2'); DOK.prefix('T','T')
         ?}
      ?};
      {? DOK.first()
      || AN.cntx_psh(); AN.index('WALSYM'); AN.prefix(FINFO.NAROD);
         {!
         |? POZ.index('DOK');
            POZ.prefix(DOK.ref());
            {? POZ.first()
            || {!
               |? {? AN.find_key(POZ.KON)
                  || {? _b='00' & UNPAR.P7
                     || TAB_DZ.prefix(POZ.KON);
                        {? TAB_DZ.first()
                        || {? -POZ.STR='wn'
                           || TAB_DZ.WN+=POZ.SUM
                           || TAB_DZ.MA+=POZ.SUM
                           ?};
                           TAB_DZ.put()
                        || TAB_DZ.prefix();
                           TAB_DZ.blank();
                           TAB_DZ.KON:=POZ.KON;
                           TAB_DZ.TYP:=-(1+AN.KS().TYP);
                           {? -POZ.STR='wn'
                           || TAB_DZ.WN:=POZ.SUM
                           || TAB_DZ.MA:=POZ.SUM
                           ?};
                           TAB_DZ.add()
                        ?}
                     || {? -(1+AN.KS().TYP)='b'
                        || {? -POZ.STR='wn' || DZIENNIK.OBWN+=POZ.SUM || DZIENNIK.OBMA+=POZ.SUM ?}
                        || {? -POZ.STR='wn' || DZIENNIK.OBWNPB+=POZ.SUM || DZIENNIK.OBMAPB+=POZ.SUM ?}
                        ?}
                     ?}
                  ?};
                  POZ.next()
               !}
            ?};
            DOK.next()
         !};
         AN.cntx_pop()
      ?};
      {? _b='00' & UNPAR.P7
      || TAB_DZ.prefix();
         {? TAB_DZ.first()
         || {!
            |? {? TAB_DZ.TYP='b'
               || DZIENNIK.OBWN+=F.SWn(TAB_DZ.WN,TAB_DZ.MA);
                  DZIENNIK.OBMA+=F.SMa(TAB_DZ.WN,TAB_DZ.MA)
               || DZIENNIK.OBWNPB+=F.SWn(TAB_DZ.WN,TAB_DZ.MA);
                  DZIENNIK.OBMAPB+=F.SMa(TAB_DZ.WN,TAB_DZ.MA)
               ?};
               TAB_DZ.next()
            !}
        ?};
        VAR_DEL.delete('TAB_DZ')
      ?}
   ?}
?};
DOK.cntx_pop();
POZ.cntx_pop();
VPOZ.cntx_pop();
POW.cntx_pop();
''


\obr_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Funkcja oblicza obroty dziennika w roku. Funkcja ustawia 4 pola zmiennej DZIENNIK:
::       sumy obrotów WN i MA dla kont bilansowych i pozabilansowych.
::       Brane sa pod uwage tylko operacje zaksiegowane koncowo.
::   WE: _a - ref rejestru
::       _b - ref jednostki ksiegowej
::       _c - nr końcowego okresu
::  OLD: \obr_rok/dziennik.fml
::----------------------------------------------------------------------------------------------------------------------
_okres:={? _=2 || SSTALE.AO().NR || _c ?};
OKRO_F.cntx_psh();
OKRO_F.index('ROK');
OKRO_F.prefix(SSTALE.AR);
{? OKRO_F.first()
|| {! |?
      {? OKRO_F.NR<=_okres
      || _ms1:=OKRO_F.ROK().KOD;
         _ms2:=form(OKRO_F.NR,-2);
         exec('obr_mc','!fks_ksr_zddz',_ms1,_ms2,_a,_b)
      ?};
      OKRO_F.next()
   !}
?};
OKRO_F.cntx_pop()


\tab_rej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Tworzy tabelę i okno z rejestrami
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TREJ');
TREJ:=tab_tmp(2,
   'TREE','TREE_REF',,
   'NAZ','STRING[71]','Nazwa',
   'TYP','STRING[1]',,
   'REF','INTEGER',
);
_o:=TREJ.mk_sel('Dzienniki częściowe'@,'P',,'#rejseldz1',,,,1);
TREJ.win_fld(_o,,'NAZ',,,39);
TREJ.win_sel(_o);
TREJ.win_fml(_o,,'NAZ',,'ICON_BEFORE',"
   TREJ.cntx_psh();
   TREJ.prefix(TREJ.ref());
   _child:=TREJ.first();
   TREJ.cntx_pop();
   _zwrot:={? _child
           || {? TREJ.tr_state()=1
              || 'xwin16.png:75'
              || 'xwin16.png:74'
              ?}
           || 'xwin16.png:76'
           ?};
   _zwrot
");
TREJ.tr_fml(_o,,"
   {? _a=-1 || 1 || _a ?}
");
TREJ


::\nal_obr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Funkcja naliczjaca oboty
::  OLD: \nal_obr/dziennik.fml
::   WE: _a - jednostka księgowa
::       _b - rejestr
::----------------------------------------------------------------------------------------------------------------------
::DZIENNIK.OBWN:=DZIENNIK.OBMA:=DZIENNIK.OBWNPB:=DZIENNIK.OBMAPB:=0;
::_omask_a:=SSTALE.AR().KOD;
::_omask_b:=form(SSTALE.AO().NR,-2);
::exec('obr_rok','!fks_ksr_zddz',_b,_a);
::DZIENNIK.OBWNN:=DZIENNIK.OBWN;
::DZIENNIK.OBMAN:=DZIENNIK.OBMA;
::DZIENNIK.OBWNPBN:=DZIENNIK.OBWNPB;
::DZIENNIK.OBMAPBN:=DZIENNIK.OBMAPB;
::AN.use('koan__'+_omask_a); AN_SLU.use('koansl'+_omask_a);
::DOK.use('doku'+_omask_a+_omask_b);
::_ms1:=SSTALE.AR().KOD;
::_ms2:=form(SSTALE.AO().NR,-2);
::DZIENNIK.OBWN:=DZIENNIK.OBMA:=DZIENNIK.OBWNPB:=DZIENNIK.OBMAPB:=0;
::exec('obr_mc','!fks_ksr_zddz',_ms1,_ms2,_b,_a);
::0


\nal_obr1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [14.51]
:: OPIS: Naliczanie obrotow dziennika - dla pola
::  OLD: \nal_obr1/dziennik.fml
::----------------------------------------------------------------------------------------------------------------------
_ms1:=SSTALE.AR().KOD; _ms2:=form(SSTALE.AO().NR,-2);
ROK_F.cntx_psh(); OKRO_F.cntx_psh(); AN.cntx_psh();

 AN.use('koan__'+_ms1);
   exec('obr_mc_sum','!fks_ksr_zddz',form(SSTALE.AO().NR,-2));
   DZIENNIK.OBWNN:=DZIENNIK.OBWN;
   DZIENNIK.OBMAN:=DZIENNIK.OBMA;
   DZIENNIK.OBWNPBN:=DZIENNIK.OBWNPB;
   DZIENNIK.OBMAPBN:=DZIENNIK.OBMAPB;
   {? var_pres('NAL_OBR')>0
   || _prefix:='@@@';
      {? WinRej.REJ.TYP='W'
      || _prefix:='0'
      |? WinRej.REJ.TYP='O'
      || ODD.cntx_psh();
         ODD.prefix();
         {? ODD.seek(WinRej.REJ.REF) || _prefix:=$ODD.ref() ?};
         ODD.cntx_pop()
      || REJ.cntx_psh();
         REJ.prefix();
         {? REJ.seek(WinRej.REJ.REF) || _prefix:=$REJ.ref() ?};
         REJ.cntx_pop()
      ?};
      NAL_OBR.prefix(_prefix);
      {? NAL_OBR.first()
      || DZIENNIK.OBWNN+=NAL_OBR.OBWN;
         DZIENNIK.OBMAN+=NAL_OBR.OBMA;
         DZIENNIK.OBWNPBN+=NAL_OBR.OBWNPB;
         DZIENNIK.OBMAPBN+=NAL_OBR.OBMAPB
      ?}
   ?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop(); AN.cntx_pop();
1


\nal_obr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [14.51]
:: OPIS: Naliczanie obrotow dziennika - dla pola - do okresu poprzedzajacego
::   WE: _a - ref rejestru lub null - wszystkie
::       _b - ref j księgowej
::  OLD: \nal_obr2/dziennik.fml
::----------------------------------------------------------------------------------------------------------------------
_ms1:=SSTALE.AR().KOD; _ms2:=form(SSTALE.AO().NR,-2);
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
AN.cntx_psh(); AN.use('koan__'+_ms1);
DOK.cntx_psh(); DOK.use('doku'+_ms1+_ms2);
VAR_DEL.delete('NAL_OBR');
NAL_OBR:=tab_tmp(1,'REJ','STRING[16]','Rejestr',
                   'OBWN','REAL','',
                   'OBMA','REAL','',
                   'OBWNPB','REAL','',
                   'OBMAPB','REAL',''
                );
REJ.cntx_psh(); REJ.index('KOD'); REJ.prefix(SSTALE.AR);
{? _a
|| {? REJ.seek(_a)
   || DZIENNIK.OBWN:=DZIENNIK.OBMA:=DZIENNIK.OBWNPB:=DZIENNIK.OBMAPB:=0;
      exec('obr_rok','!fks_ksr_zddz',REJ.ref(),REJ.ODD,SSTALE.AO().NR-1);
      NAL_OBR.OBWN:=DZIENNIK.OBWN;
      NAL_OBR.OBMA:=DZIENNIK.OBMA;
      NAL_OBR.OBWNPB:=DZIENNIK.OBWNPB;
      NAL_OBR.OBMAPB:=DZIENNIK.OBMAPB;
      NAL_OBR.REJ:=$REJ.ref();
      NAL_OBR.add()
   ?}
|| {? REJ.first()
   || _obwn:=_obma:=_obwnpb:=_obmapb:=0;
      {! |?
         DZIENNIK.OBWN:=DZIENNIK.OBMA:=DZIENNIK.OBWNPB:=DZIENNIK.OBMAPB:=0;
         exec('obr_rok','!fks_ksr_zddz',REJ.ref(),REJ.ODD,SSTALE.AO().NR-1);
         NAL_OBR.OBWN:=DZIENNIK.OBWN;
         NAL_OBR.OBMA:=DZIENNIK.OBMA;
         NAL_OBR.OBWNPB:=DZIENNIK.OBWNPB;
         NAL_OBR.OBMAPB:=DZIENNIK.OBMAPB;
         NAL_OBR.REJ:=$REJ.ref();
         NAL_OBR.add();

::       obroty dla wszystkich
         _obwn+=DZIENNIK.OBWN;
         _obma+=DZIENNIK.OBMA;
         _obwnpb+=DZIENNIK.OBWNPB;
         _obmapb+=DZIENNIK.OBMAPB;

::       obroty dla j. księgowej
         {? NAL_OBR.find_key($REJ.ODD)
         || NAL_OBR.OBWN+=DZIENNIK.OBWN;
            NAL_OBR.OBMA+=DZIENNIK.OBMA;
            NAL_OBR.OBWNPB+=DZIENNIK.OBWNPB;
            NAL_OBR.OBMAPB+=DZIENNIK.OBMAPB;
            NAL_OBR.put()
         || NAL_OBR.OBWN:=DZIENNIK.OBWN;
            NAL_OBR.OBMA:=DZIENNIK.OBMA;
            NAL_OBR.OBWNPB:=DZIENNIK.OBWNPB;
            NAL_OBR.OBMAPB:=DZIENNIK.OBMAPB;
            NAL_OBR.REJ:=$REJ.ODD;
            NAL_OBR.add()
         ?};

         REJ.next()
      !};

      NAL_OBR.OBWN:=_obwn;
      NAL_OBR.OBMA:=_obma;
      NAL_OBR.OBWNPB:=_obwnpb;
      NAL_OBR.OBMAPB:=_obmapb;
      NAL_OBR.REJ:='0';
      NAL_OBR.add()
   ?}
?};
REJ.cntx_pop(); DOK.cntx_pop(); ROK_F.cntx_pop(); OKRO_F.cntx_pop(); AN.cntx_pop()


\obr_mc_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [14.51]
:: OPIS: Sumowanie obrotow miesiaca
::       _a - nr okresu
::  OLD: \obr_mc_sum/dziennik.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='00' & UNPAR.P7
|| VAR_DEL.delete('TAB_DZ');
   TAB_DZ:=tab_tmp(1,'KON','STRING[35]','Konto',
                     'TYP','STRING[1]','Typ',
                     'WN','REAL','WN',
                     'MA','REAL','MA'
                  )
?};
_ref:={? ~DOK.sel_size() & DOK.f_active() & DOK.f_get() || DOK.ref() || null ?};

{? f_usr_changed() | ~DOK.f_active()
|| DZIENNIK.OBWN:=DZIENNIK.OBMA:=DZIENNIK.OBWNPB:=DZIENNIK.OBMAPB:=0;
   DOK.cntx_psh(); POZ.cntx_psh();
   {? {? DOK.f_active() || DOK.f_first() || DOK.first() ?}
   || AN.cntx_psh(); AN.index('WALSYM'); AN.prefix(FINFO.NAROD);
      {!
      |? POZ.index('DOK');
         POZ.prefix(DOK.ref());
         {? POZ.first()
         || {!
            |? {? AN.find_key(POZ.KON)
               || {? _a='00' & UNPAR.P7
                  || TAB_DZ.prefix(POZ.KON);
                     {? TAB_DZ.first()
                     || {? -POZ.STR='wn'
                        || TAB_DZ.WN+=POZ.SUM
                        || TAB_DZ.MA+=POZ.SUM
                        ?};
                        TAB_DZ.put()
                     || TAB_DZ.prefix();
                        TAB_DZ.blank();
                        TAB_DZ.KON:=POZ.KON;
                        TAB_DZ.TYP:=-(1+AN.KS().TYP);
                        {? -POZ.STR='wn'
                        || TAB_DZ.WN:=POZ.SUM
                        || TAB_DZ.MA:=POZ.SUM
                        ?};
                        TAB_DZ.add()
                     ?}
                  || {? -(1+AN.KS().TYP)='b'
                     || {? -POZ.STR='wn' || DZIENNIK.OBWN+=POZ.SUM || DZIENNIK.OBMA+=POZ.SUM ?}
                     || {? -POZ.STR='wn' || DZIENNIK.OBWNPB+=POZ.SUM || DZIENNIK.OBMAPB+=POZ.SUM ?}
                     ?}
                  ?}
               ?};
               POZ.next()
            !}
         ?};
         {? DOK.f_active() || DOK.f_next() || DOK.next() ?}
      !};
      AN.cntx_pop()
   ?};
   {? _a='00' & UNPAR.P7
   || TAB_DZ.prefix();
     {? TAB_DZ.first()
     || {!
        |? {? TAB_DZ.TYP='b'
           || DZIENNIK.OBWN+=F.SWn(TAB_DZ.WN,TAB_DZ.MA);
              DZIENNIK.OBMA+=F.SMa(TAB_DZ.WN,TAB_DZ.MA)
           || DZIENNIK.OBWNPB+=F.SWn(TAB_DZ.WN,TAB_DZ.MA);
              DZIENNIK.OBMAPB+=F.SMa(TAB_DZ.WN,TAB_DZ.MA)
           ?};
           TAB_DZ.next()
        !}
     ?}
   ?};
   DOK.cntx_pop(); POZ.cntx_pop();
   {? _ref || DOK.f_seek(_ref) ?}
?};
VAR_DEL.delete('TAB_DZ');
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:49 b560f8b871dc2dd20e793844e4fee1e04668aad12d16208030673181c5f9c45adaf84ffd5f26fc8c4fdd2efa66aa04f7c64de27577e50a0c58a4ce539a0bbdcfaf93c5e3b3c0581e5ba0c5173cf74321604ecabfb91febc38db0da2a450da27c9a0c513f05ed59f6a11f52cf09af06c16a10dc5352899d69083099d1abc27477

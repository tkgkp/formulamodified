:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksp_zspr.fml
:: Utworzony: 09.10.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_KSP_ZSPR - Przeglądanie wartości sprawozdań
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przeglądanie wartości sprawozdań - główna formuła czynności FKS_KSP_ZSPR
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::# permissions=FJKS


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
{? var_press('Konsolid')>0
|| USRGRSIK.index('FIRMA');
   USRGRSIK.prefix(OPERATOR.USER,REF.GRUPA)
|| USRGRSIK.index('NAZWA');
   USRGRSIK.prefix(OPERATOR.USER)
?};
_b.grp_sel(_c,USRGRSIK,'WER',_a,"
",,,,"
   {? var_press('Konsolid')>0
   || AreaTitle.setArea('KON_SPR')
   || AreaTitle.setArea('FKS2KSP','FKS_KSP')
   ?};
   AreaTitle.setTitle();
   SKID.fld_fml('LANG_WER','BEFORE_DISPLAY',\"SKID.LANG_WER:=exec('get_lang','lang',USRGRSIK.GR_SIK);~~\");
   ~~
","
   SKID.fld_fml('LANG_WER','BEFORE_DISPLAY',\"*\");
   ~~
",,,'maximized_with_title');
task_attach('FKS_KSP_ZSPR');
USRGRSIK.win_fml('WER',SKID,'LANG_WER',,'ICON_BEFORE',"exec('icon','lang',USRGRSIK.GR_SIK)");
USRGRSIK.win_fml('WER',,'GR_SIK','SKROT','ICON_BEFORE',"exec('ikona_grupa','sprfin')")


\baugrsik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Akcja dolacz dla tabeli USRGRSIK
::  OLD: \baugrsik/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
FIRMA.index('SYMBOL2'); FIRMA.prefix();
GR_SIK.index('SKROT'); GR_SIK.clear();
_where:=
   'FIRMA=\''+$REF.FIRMA+'\''+
   {? FIRMA.find_key('C') & REF.FIRMA<>FIRMA.ref()
   || ' or FIRMA=\''+$FIRMA.ref()+'\''
   || ''
   ?};
GR_SIK.f_set('SKROT',,_where);
_o:=GR_SIK.mk_sel('Sprawozdania'@,'P',,'#grsiksel',,,,,'U');
GR_SIK.win_fld(_o,,'SKROT',,,,,,'Kod'@);
GR_SIK.win_fld(_o,,'NAZWA',,,,,,'Nazwa'@);
GR_SIK.win_fld(_o,,'ZAOKR',,,,,,'Wskaźnik zaokrąglenia'@);
GR_SIK.win_fld(_o,,'DOKL',,,,,,'Dokładność'@);
GR_SIK.win_fld(_o,,'AKC',,,,,,'Akceptacja'@,,,2,,"'T'","'N'");
GR_SIK.win_act(_o,,'Formuła','Wybierz'@@,,,"OPERATOR.USER(); exec('tengrsik','!fks_ksp_zspr')",,1,1);
GR_SIK.win_sel(_o);
GR_SIK.select()


\tengrsik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Dodaje wybrane sprawozdanie\a do tabeli USRGRSIK
::  OLD: \tengrsik/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
USRGRSIK.cntx_psh();
USRGRSIK.index('USER'); USRGRSIK.prefix(USERS.ref());
{? ~USRGRSIK.find_key(GR_SIK.ref())
|| USRGRSIK.USER:=USERS.ref();
   USRGRSIK.GR_SIK:=GR_SIK.ref();
   USRGRSIK.add()
?};
_ref:=USRGRSIK.ref();
USRGRSIK.cntx_pop();
{? _ref || USRGRSIK.seek(_ref) ?};
{? GR_SIK.sel_size()<=1 || sel_exit() ?}


\tree_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Wyswietla sprawozdania w strukturze hierarchicznej
::  OLD: \tree_spr/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
OPERATOR.USER();
_firma:=REF.FIRMA;
REF.FIRMA:=SSTALE.AF;
REF.W_SCH:=SSTALE.W_SCH;
SIK.WYL:='T';
SSTALE.ROK:={? REF.S_FIRMA=REF.GRUPA
            || {? REF.FIRMA=REF.GRUPA
               || SSTALE.AR
               || exec('rok_f','konsolidacja',SSTALE.AR)
               ?}
            || SSTALE.AR
            ?};
{? REF.FIRMA<>SSTALE.ROK().FIRMA | SIK.WYL<>REF.WYKON().WYKON | REF.FIRMA<>WERSJE.FIRMA | REF.W_SCH<>REF.WYKON().W_SCH
|| REF.FIRMA:=SSTALE.ROK().FIRMA;
   REF.WYKON:=exec('wykon_firmy','konsolidacja',REF.FIRMA,REF.W_SCH)
?};
REF.ROK_G:=SSTALE.ROK;
OPERATOR.USER();
{? (
      USRGRSIK.cntx_psh();
      USRGRSIK.index('USER'); USRGRSIK.prefix(USERS.ref()); _brak:=~USRGRSIK.first();
      USRGRSIK.cntx_pop();
      _brak
   )
|| FUN.info('Brak sprawozdań przypisanych użytkownikowi: %1.'@[USERS.DANE]);
   REF.FIRMA:=_firma;
   return(0)
|? OSPR.index('ROK_NAZ'); OSPR.prefix(REF.FIRMA,SSTALE.ROK); ~OSPR.first()
|| FUN.info('Brak okresów raportowania dla firmy %1 w roku %2.'@[REF.FIRMA().SYMBOL,SSTALE.ROK().NAZ]);
   REF.FIRMA:=_firma;
   return(0)
?};
{? var_press('__Kodcz')<=0 || __Kodcz:='' ?};
ROK_F.win_sel('WER');
SLO.win_sel('ONE_SEL');
SLO.win_dict('ONE_SEL');
{? SSTALE.WARIANT>99
|| SSTALE.WARIANT:=1
|? SSTALE.WARIANT=2
|| {? SSTALE.OKRES=null | SSTALE.ROK2=null | SSTALE.ROK3=null | exec('spr_rec','!fks_ksp_zspr',0)<>''
   || {? ~exec('params','!fks_ksp_zspr')
      || return(0)
      ?}
   ?}
?};
VAR_DEL.delete('TT_TREE','okr','l_okr','naz','naz_full');
SSTALE.cntx_psh();
SSTALE.GR_SIK:=GR_SIK.ref();
exec('ini_wal','konto',0);
UNPAR.P10_BV:='exec(\'bv_l_skr\',\'okresy\')';
_msg_err:="
   FUN.info(
      'Rok %1 nie zawiera okresu %2.\n'@[_a,_b]+
      'Należy wybrać wariant prezentacji z nowymi ustawieniami.'@
   )
";
_rok:=_widok:=_lang:=null;
WARTOSCI.cntx_psh(); pierwszy:=1; _cur_lab:=''; _cur_ref:=_win_poz:=0;
{!
|? l_okr:=0; dalej:=0; liczba:=0; delta:=0;
   {? SSTALE.WARIANT=1 | SSTALE.WARIANT=4 | (SSTALE.WARIANT=3 & SSTALE.ROK=SSTALE.ROK2) | SSTALE.WARIANT=6
   || WARTOSCI.use('wsik_'+SSTALE.ROK().KOD)
   ?};
   {? SSTALE.WARIANT=1
   || _f_dl:="{? SSTALE.TYP='M' | SSTALE.WARIANT=3 || 6 |? SSTALE.TYP='K' || 7 || 16 ?}";
      OSPR.index('ROK_NAZ'); OSPR.prefix(SSTALE.ROK().FIRMA,SSTALE.ROK); l_okr:=OSPR.size();
      okr:=obj_new(l_okr); naz:=obj_new(l_okr); naz_full:=obj_new(l_okr);
      {? OSPR.first()
      ||  _i:=1; rok_poz:=0;
         {!
         |? {? OSPR.TYP<>'M' | (OSPR.OKRES_OD().POCZ<>date(0,0,0) & OSPR.OKRES_DO().POCZ<>date(0,0,0))
            || okr[_i]:=#OSPR.ref();
               naz[_i]:=exec('naz_kol','!fks_ksp_zspr',0);
               naz_full[_i]:=exec('naz_kol','!fks_ksp_zspr',1);
               {? OSPR.TYP='R' || rok_poz:=_i ?};
               _i+=1
            || l_okr-=1
            ?};
            OSPR.next()
         !}
      ?};
      exec('set_war','!fks_ksp_zspr')
   |? SSTALE.WARIANT=2
   || _f_dl:="{? liczba=1 || 16 || 7 ?}";
      okr:=obj_new(3); naz:=obj_new(10); naz_full:=obj_new(10);
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
      OSPR.index('OKR_ROK'); OSPR.prefix(REF.FIRMA,SSTALE.OKRES);
      {? ROK_F.first()
      || SSTALE.ROK2();
         {! |?
            liczba+=ROK_F.ref<>SSTALE.ROK2;
            ROK_F.ref()<>SSTALE.ROK3 & ROK_F.next()
         !};
         _ok:=1;
         {? OSPR.find_key(SSTALE.ROK2)
         || naz[1]:=exec('naz_kol','!fks_ksp_zspr',liczba=1,_f_dl());
            naz_full[1]:=exec('naz_kol','!fks_ksp_zspr',1)
         || _ok:=0; _msg_err(SSTALE.ROK2().NAZ,': '+SSTALE.OKRES().TR)
         ?};
         {? _ok
         || SSTALE.ROK2(); _i:=0;
            _t_zm:=exec('tran_sik','lang',41);
            _t_dn:=exec('tran_sik','lang',42);
            {! |?
               {? ROK_F.ref<>SSTALE.ROK2
               || _i+=1;
                  _ok:=OSPR.find_key(ROK_F.ref()); {? ~_ok || _msg_err(ROK_F.NAZ,': '+SSTALE.OKRES().TR) ?};
                  okr[_i]:=#OSPR.ref();
                  ROK_F.cntx_psh();
                  naz[1+_i]:=exec('naz_kol','!fks_ksp_zspr',liczba=1,_f_dl());
                  naz_full[1+_i]:=exec('naz_kol','!fks_ksp_zspr',1);
                  ROK_F.cntx_pop();
                  naz[1+liczba+_i]:=(4+_t_dn)+ROK_F.NAZ; naz_full[1+liczba+_i]:=_t_dn+' '+ROK_F.NAZ;
                  naz[1+2*liczba+_i]:=(2+_t_zm)+ROK_F.NAZ; naz_full[1+2*liczba+_i]:=_t_zm+' '+ROK_F.NAZ
               ?};
               _ok & ROK_F.ref()<>SSTALE.ROK3 & ROK_F.next()
            !}
         ?};
         {? _ok || start:=1; stop:=3*liczba+1 || start:=stop:=0; delta:=1 ?}
      ?}
   |? SSTALE.WARIANT=3
   || _f_dl:="16";
      start:=1; stop:=4;
      naz:=obj_new(4); naz_full:=obj_new(4);
      {? SSTALE.OSPR
      || SSTALE.OSPR()
      || _msg_err(SSTALE.ROK().NAZ,' podanego w parametrach wariantu');
         start:=stop:=0; delta:=1
      ?};
      naz[1]:=naz_full[1]:=exec('naz_kol','!fks_ksp_zspr',1);
      {? start
      || {? SSTALE.OSPR2
         || SSTALE.OSPR2()
         || _msg_err(SSTALE.ROK2().NAZ,' podanego w parametrach wariantu'); start:=stop:=0; delta:=1
         ?};
         naz[2]:=naz_full[2]:=exec('naz_kol','!fks_ksp_zspr',1);
         naz[3]:=naz_full[3]:=exec('tran_sik','lang',40);
         naz[4]:=naz_full[4]:=exec('tran_sik','lang',41)
      ?}
   |? SSTALE.WARIANT=4
   || _f_dl:="16";
      GR_KOL.index('LP'); GR_KOL.prefix(SSTALE.GR_SIK); liczba:=GR_KOL.size();
      {? pierwszy
      || start:=1;
         stop:={? liczba>5 || 5 || liczba ?}
      ?};
      _lp:=1;
      {? liczba || naz:=obj_new(liczba); naz_full:=obj_new(liczba); okr:=obj_new(liczba) ?};
      {? GR_KOL.first()
      || {! |?
            _nazwa:=exec('get_lang','lang',GR_KOL.ref());
            naz[_lp]:={? +_nazwa>16 || $GR_KOL.LP || _nazwa ?};
            naz_full[_lp]:=_nazwa;
            okr[_lp]:=#GR_KOL.ref();
            _lp+=1;
            GR_KOL.next()
         !}
      ?}
   |? SSTALE.WARIANT=5
   || SIK.ROK1:=SSTALE.ROK;
      {? _rok<>SIK.ROK1 | _widok<>SIK.WIDOK | width=0 | _lang<>SKID.LANG_SLO
      || _rok:=SIK.ROK1;
         _widok:=SIK.WIDOK;
         WERPVIEW.cntx_psh(); WERPVIEW.index('WER_VIEW'); WERPVIEW.prefix(SIK.WIDOK); _wp:=WERPVIEW.first(); WERPVIEW.cntx_pop();
         {? _wp
         || _lang:=SKID.LANG_SLO;
            UNPAR.P35:=12;
            UNPAR.P34:=1;
            width:=exec('gen_okr','sprfin');
            {? width=-1
            || FUN.info('Nie zdefiniowano lat obrachunkowych\nwymaganych dla okresów widoku.'@)
            |? width=0
            || FUN.info('Nie zdefiniowano okresu \'%1\' \nwystępującego w widoku planu.'@[WERPVIEW.SLO().TR+' '+SIK.ROK1().NAZ])
            ?}
         || FUN.info('Widok %1 nie zawiera pozycji.'@[SIK.WIDOK().NAZWA]);
            width:=-2;
            tt_max:=0;
            tt_size:=0
         ?};
         VAR_DEL.delete('kolor')
      ?};
      kolor:='';
      1
   || _f_dl:="16"; liczba:=3;
      start:=1;
      stop:=liczba;
      naz:=obj_new(liczba); naz_full:=obj_new(liczba); okr:=obj_new(liczba);
      _wyl:=SIK.WYL;
      {! _lp:=1..liczba
      |? naz[_lp]:={? _lp=1 || 'Jednostkowe' |? _lp=2 || 'Wyłączenia' || 'Skonsolidowane' ?};
         naz_full[_lp]:=naz[_lp];
         SIK.WYL:={? _lp=1 || 'T' |? _lp=2 || 'W' || 'S' ?};
         okr[_lp]:=exec('wykon_firmy','konsolidacja',REF.FIRMA,REF.W_SCH)
      !};
      SIK.WYL:=_wyl
   ?};
   {? pierwszy
   || {? SSTALE.WARIANT=4
      || _f:='TT_TREE:=sql(\'select '+
             'CAST (0 AS TREE_REF_TYPE) as TREE, '+
             '1 as LP, '+
             '\'\''+ 120*' '+'\'\' as LABEL, '+
             '1 as POZIOM, '+
             '1 as REF, '+
             '1 as RKOL, '+
             '1 as DANE, '+
             '1 as ROZ, '+
             '1 as CZY_WAL, '+
             '\'\''+ 20*' '+'\'\' as KOD';
         {! _i:=1 .. liczba |!
            _f+=', 1 as KOL'+$_i
         !};
         _f+=' from SYSLOG where 0=1  order by 1,2\')';
         ($_f)()
      || TT_TREE:=tab_tmp(2,'TREE','TREE_REF',,'LP','INTEGER',,'LABEL','STRING[120]',,'POZIOM','INTEGER',,'REF','INTEGER',,'RKOL','INTEGER',,'DANE','INTEGER',,'ROZ','INTEGER',,'KOL','INTEGER',,'KOD','STRING[20]',,'CZY_WAL','INTEGER',)
      ?};
      _ndx_lab:=TT_TREE.ndx_tmp(,,'LABEL',,,'REF',,);
      __ndx_tl:=TT_TREE.ndx_tmp(,,'TREE',,,'LABEL',,);
      __ndx_lr:=TT_TREE.ndx_tmp(,,'LABEL',,,'REF',,);
      exec('tree_gr','!fks_ksp_zspr');
      pierwszy:=0
   ?};
   {! |?
      {? SSTALE.WARIANT=5
      || _o:=TT_TREE.mk_sel('Wartości sprawozdań'@,'P',0,'#tt_tree_spr'+$SSTALE.WARIANT+$tt_max,,,,1);
         UNPAR.P11_BV:='exec(\'language\',\'!fks_ksp_zspr\')';
         TT_TREE.win_fld(_o,UNPAR,'P11',,,37,,,,1);
         {? width>0
         || {! _i:=1..tt_max
            |! TT_TREE.win_fld(_o,POMOC,'P'+$_i,,,width,,,tt_okr[_i+tt_mov[1]][2],0,tt_okr[_i+tt_mov[1]][3])
            !}
         ?}
      || _szer:=121-(stop-start+1); _sz:=0;
         _dl:=_f_dl();
         {! _i:=start .. stop |! _sz+={? +naz[delta+_i]>_dl || +naz[delta+_i] || _dl ?} !};
         {? SSTALE.WARIANT=1 & SSTALE.TYP<>'M' & SSTALE.TYP<>'R' & ~SSTALE.INC || _sz+=16 ?};
         _szer-=_sz;
         {? _szer>50 || _szer:=50 ?};
         _o:=TT_TREE.mk_sel('Wartości sprawozdań'@,'P',0,'#tt_tree_spr'+$SSTALE.WARIANT+$({? stop-start+1<0 || 0 || stop-start+1 ?}),,,,1);
         UNPAR.P11_BV:='exec(\'language\',\'!fks_ksp_zspr\')';
         TT_TREE.win_fld(_o,UNPAR,'P11',,,_szer,,,,1);
         {? start
         || {! _i:=start .. stop |!
               TT_TREE.win_fld(_o,POMOC,'P'+$(_i-start+1),,, _f_dl() ,SSTALE.PREC,,naz[delta+_i],,naz_full[delta+_i] )
            !};
            {? SSTALE.WARIANT=1 & SSTALE.TYP<>'M' & SSTALE.TYP<>'R' & ~SSTALE.INC & rok_poz
            || TT_TREE.win_fld(_o,POMOC,'P'+$(stop-start+2),,, 15,SSTALE.PREC,,naz[rok_poz],,naz_full[rok_poz] )
            ?}
         ?}
      ?};
      TT_TREE.win_act(_o,0,'Formuła','Zwiń/rozwiń całość'@@,,'Zwija lub rozwija wszystko z bieżącej gałęzi'@,"
         exec('t_g_roz','!fks_ksp_zspr',~TT_TREE.ROZ)
      ",,,,,,'Z');
      {! _i:=0..1 |!
         TT_TREE.win_act(_o,_i,'Menu','Wariant &prezentacji'@,,'Wybór wariantu do prezentacji wartości'@);
         TT_TREE.win_act(_o,_i,'Formuła','Wartości wg okresów'@@,'Wariant &prezentacji'@,'Prezentacja wartości wg okresów'@,"
            _h1:=SSTALE.WARIANT; _h2:=SSTALE.ROK; _h3:=SSTALE.TYP;
            SSTALE.WARIANT:=1;
            {? REF.FIRMA().TYP<>'S'
            || __F_ref:='S';
               REF.GRUPA()
            ?};
            WARIANTY.win_edit('WARIANT'+$SSTALE.WARIANT);
            {? WARIANTY.edit(\"exec('spr_rec','!fks_ksp_zspr')\") & (SSTALE.WARIANT<>_h1 | SSTALE.ROK<>_h2 | SSTALE.TYP<>_h3 )
            || dalej:=1;
               pierwszy:=_h1=4;
               exec('set_war','!fks_ksp_zspr');
               exec('save_war','!fks_ksp_zspr');
               sel_exit()
            || SSTALE.WARIANT:=_h1; SSTALE.ROK:=_h2; SSTALE.TYP:=_h3
            ?};
            VAR_DEL.delete('__F_ref')
         ",,,,,,'W');
         TT_TREE.win_act(_o,_i,'Formuła','Wartości wg w&idoku'@@,'Wariant &prezentacji'@,'Prezentacja wartości wg widoku okresów'@,"
            _h1:=SSTALE.WARIANT; _h2:=SSTALE.ROK; _h3:=SIK.WIDOK; __CHG_VIEW:=0;
            SSTALE.WARIANT:=5;
            WARIANTY.win_edit('WARIANT'+$SSTALE.WARIANT);
            {? WARIANTY.edit(\"exec('spr_rec','!fks_ksp_zspr')\") & (SSTALE.WARIANT<>_h1 | SSTALE.ROK<>_h2 | SIK.WIDOK<>_h3 | __CHG_VIEW)
            || {? __CHG_VIEW
               || width:=0
               ?};
               dalej:=1;
               pierwszy:=_h1=4;
               exec('set_war','!fks_ksp_zspr');
               exec('save_war','!fks_ksp_zspr');
               sel_exit()
            || {? __CHG_VIEW
               || width:=0;
                  dalej:=1;
                  sel_exit()
               ?};
               SSTALE.WARIANT:=_h1; SSTALE.ROK:=_h2; SIK.WIDOK:=_h3
            ?};
            VAR_DEL.delete('__CHG_VIEW')
         ",,,,,,'I');
         TT_TREE.win_act(_o,_i,'Formuła','Dynamika wg &lat'@@,'Wariant &prezentacji'@,'Prezentacja kroczącej dynamiki wartości wg lat'@,"
            _h1:=SSTALE.WARIANT; _h2:=SSTALE.OKRES; _h4:=SSTALE.ROK2; _h5:=SSTALE.ROK3;
            SSTALE.WARIANT:=2;
            {? exec('params','!fks_ksp_zspr') & (SSTALE.WARIANT<>_h1 | SSTALE.OKRES<>_h2 | SSTALE.ROK2<>_h4 | SSTALE.ROK3<>_h5)
            || dalej:=1;
               pierwszy:=_h1=4;
               exec('save_war','!fks_ksp_zspr');
               sel_exit()
            || SSTALE.WARIANT:=_h1; SSTALE.OKRES:=_h2; SSTALE.ROK2:=_h4; SSTALE.ROK3:=_h5
            ?}
         ",,,,,,'L');
         TT_TREE.win_act(_o,_i,'Formuła','Dynamika wg &okresów'@@,'Wariant &prezentacji'@,'Prezentacja dynamiki wartości dwóch okresów'@,"
            _h1:=SSTALE.WARIANT; _h2:=SSTALE.ROK; _h3:=SSTALE.OSPR; _h4:=SSTALE.ROK2; _h5:=SSTALE.OSPR2;
            SSTALE.WARIANT:=3;
            OSPR.win_sel('WER');
            WARIANTY.win_edit('WARIANT'+$SSTALE.WARIANT);
            {? WARIANTY.edit(\"exec('spr_rec','!fks_ksp_zspr')\") & (SSTALE.WARIANT<>_h1 | SSTALE.ROK<>_h2 | SSTALE.OSPR<>_h3 | SSTALE.ROK2<>_h4 | SSTALE.OSPR2<>_h5)
            || dalej:=1;
               pierwszy:=_h1=4;
               exec('save_war','!fks_ksp_zspr');
               sel_exit()
            || SSTALE.WARIANT:=_h1; SSTALE.ROK:=_h2; SSTALE.OSPR:=_h3; SSTALE.ROK2:=_h4; SSTALE.OSPR2:=_h5
            ?};
            OSPR.actions('WER','')
         ",,,,,,'O')
      !};

      TT_TREE.win_act(_o,0,'Formuła','--Q','Wariant &prezentacji'@);
      TT_TREE.win_act(_o,0,'Formuła','Wartości wg &kolumn'@@,'Wariant &prezentacji'@,'Prezentacja wartości wg kolumn w okresie'@,"
         TT_TREE.cntx_psh();
         {! |?
            TT_TREE.POZIOM & TT_TREE.seek(TT_TREE.TREE,TT_TREE.name())
         !};
         GR_SIK.prefix();
         {? GR_SIK.seek(TT_TREE.REF,GR_SIK.name())
         || TT_TREE.cntx_pop();
            _h1:=SSTALE.WARIANT; _h2:=SSTALE.ROK; _h3:=SSTALE.OSPR; _h4:=SSTALE.GR_SIK;
            SSTALE.WARIANT:=4; SSTALE.GR_SIK:=GR_SIK.ref();
            OSPR.win_sel('WER');
            WARIANTY.win_edit('WARIANT'+$SSTALE.WARIANT);
            {? WARIANTY.edit(\"chk_rec()\") & (SSTALE.WARIANT<>_h1 | SSTALE.ROK<>_h2 | SSTALE.OSPR<>_h3 | SSTALE.GR_SIK<>_h4)
            || pierwszy:=SSTALE.WARIANT<>_h1;
               dalej:=1;
               exec('save_war','!fks_ksp_zspr');
               sel_exit()
            || SSTALE.WARIANT:=_h1; SSTALE.ROK:=_h2; SSTALE.OSPR:=_h3; SSTALE.GR_SIK:=_h4
            ?}
         || FUN.info('Nie znaleziono sprawozdania: %1.'@[TT_TREE.LABEL]);
            TT_TREE.cntx_pop()
         ?}
      ",,,,,,'K');
      {? var_press('Konsolid')>0
      || TT_TREE.win_act(_o,0,'Formuła','--X','Wariant &prezentacji'@);
         TT_TREE.win_act(_o,0,'Formuła','Wartości dla kon&solidacji'@@,'Wariant &prezentacji'@,'Prezentacja wartości dla konsolidacji w okresie'@,"
            _h1:=SSTALE.WARIANT;
            SSTALE.WARIANT:=6;
            WARIANTY.win_edit('WARIANT6');
            {? WARIANTY.edit(\"
                  _ret:=__CHK.record2(SSTALE,'ROK','Rok','OSPR','Okres');
                  {? _ret=''
                  || {? REF.FIRMA=REF.FIRMAWYL
                     || FUN.info('Firma dla wyłączeń musi być inna niż bieżąca.'@);
                        _ret:='FIRMAWYL'
                     ?}
                  ?};
                  {? _ret=''
                  || SIK.WYL:='W';
                     {? var_press('okr')>0
                     || okr[2]:=exec('wykon_firmy','konsolidacja',REF.FIRMA,REF.W_SCH)
                     ?}
                  ?};
                  _ret
               \") & _h1<>6
            || pierwszy:=_h1=4;
               dalej:=1;
               exec('save_war','!fks_ksp_zspr');
               sel_exit()
            ?}
         ",,,,,,'S')
      ?};
      {? SSTALE.WARIANT=4 & liczba>5 | SSTALE.WARIANT=5 & tt_max<tt_size
      || TT_TREE.win_act(_o,0,'Menu','Kolumna'@,,'Nawigowanie prezentowanymi kolumnami sprawozdań'@);
         TT_TREE.win_act(_o,0,'Formuła','Następna'@@,'Kolumna'@,'Pokaż następną kolumnę'@,"
            {? SSTALE.WARIANT=4
            || {? dalej:=exec('kolumna','!fks_ksp_zspr',1) || sel_exit() ?}
            || exec('mov_okr','sprfin',1)
            ?}
         ",,,,,,'N');
         TT_TREE.win_act(_o,0,'Formuła','Poprzednia'@@,'Kolumna'@,'Pokaż poprzednią kolumnę'@,"
            {? SSTALE.WARIANT=4
            || {? dalej:=exec('kolumna','!fks_ksp_zspr',-1) || sel_exit() ?}
            || exec('mov_okr','sprfin',-1)
            ?}
         ",,,,,,'P')
      ?};
      TT_TREE.win_act(_o,0,'Formuła','Pa&rametry prezentacji'@@,,'Parametry prezentowanych wartości'@,"
         _be:=\"__F_ref:='';REF.S_FIRMA=REF.GRUPA\";
         REF.fld_fml('FIRMA','BEFORE_EDIT',_be);
         REF.fld_fml('W_SCH','BEFORE_EDIT',_be);
         _bv:=\"ROZNE.efld_opt('SPR_GRP','enable='+$(REF.S_FIRMA=REF.GRUPA),REF,'W_SCH')\";
         REF.fld_fml('W_SCH','BEFORE_DISPLAY',_bv);
         _be:=\"{? PAR_SKID.get(39)<>'N' || ($UNPAR.P25_BE)() ?}\";
         UNPAR.fld_fml('P25','BEFORE_EDIT',_be);
         _bv:=\"ROZNE.efld_opt('SPR_GRP','enable='+$(PAR_SKID.get(39)<>'N' & UNPAR.PSLO),UNPAR,'P25')\";
         UNPAR.fld_fml('P25','BEFORE_DISPLAY',_bv);
         _be:=\"{? PAR_SKID.get(39)<>'N' || ($UNPAR.PSLO_BE)() ?}\";
         UNPAR.fld_fml('PSLO','BEFORE_EDIT',_be);
         _bv:=\"
            ROZNE.efld_opt('SPR_GRP','enable='+$(PAR_SKID.get(39)<>'N'),UNPAR,'PSLO');
            ROZNE.efld_opt('SPR_GRP','enable='+$(PAR_SKID.get(39)<>'N' & UNPAR.PSLO),UNPAR,'P25')
         \";
         UNPAR.fld_fml('PSLO','BEFORE_DISPLAY',_bv);
         _be:=\"SSTALE.WARIANT<>3 & SSTALE.WARIANT<>5\";
         SSTALE.fld_fml('INC','BEFORE_EDIT',_be);
         _bv:=\"
            ROZNE.efld_opt('SPR_GRP','enable='+
            $((SSTALE.WARIANT=1 & SSTALE.TYP<>'R') | SSTALE.WARIANT=2 | SSTALE.WARIANT=4),
            SSTALE,'INC')
         \";
         SSTALE.fld_fml('INC','BEFORE_DISPLAY',_bv);
         REF.fld_fml('FIRMAWYL','BEFORE_EDIT',\"__F_ref:='S';1\");
         ROZNE.win_edit(exec('win_tree_par','!fks_ksp_zspr'));
         _firma:=REF.FIRMA;
         _w_sch:=REF.W_SCH;
         _h1:=UNPAR.PSLO; _h2:=UNPAR.P25;
         _prec:=SSTALE.PREC;
         _zaokr:=SSTALE.ZAOKR;
         _inc:=SSTALE.INC;
         _kol:=ROZNE.KOL_WIER;
         _wyl:=SIK.WYL;
         _firmawyl:=REF.FIRMAWYL;
         __F_ref:='';
         W_SCH.win_sel('SLO');
         {? ROZNE.edit(\"
               _r:='';
               {? REF.S_FIRMA=REF.GRUPA
               || _r:=__CHK.record(REF,,'FIRMA');
                  {? _r=''
                  || {? REF.FIRMA().TYP<>'S' & FIRMA.TYP<>'W'
                     || _r:=__CHK.record2(REF,'W_SCH','Struktura');
                        {? _r=''
                        || W_STR.index('FIRMA'); W_STR.prefix(REF.W_SCH,REF.FIRMA);
                           {? ~W_STR.first()
                           || FUN.info('Firma nie występuje w strukturze grupy kapitałowej.'@);
                              _r:='W_SCH'
                           ?}
                        ?}
                     || REF.W_SCH:=null
                     ?}
                  ?}
               ?};
               {? _r=''
               || {? SSTALE.ZAOKR<1
                  || FUN.info('Mnożnik powinien być dodatni.'@);
                     _r:='ZAOKR'
                  |? SSTALE.ZAOKR%*10 & SSTALE.ZAOKR<>1
                  || FUN.info('Mnożnik powinien być wielokrotnością 10.'@);
                     _r:='ZAOKR'
                  |? SSTALE.ZAOKR>100000000
                  || FUN.info('Mnożnik przekroczył 1.000.000.000.'@);
                     _r:='ZAOKR'
                  ?}
               ?};
               {? _r='' & SSTALE.PREC<0 | SSTALE.PREC>10
               || FUN.info('Dozwolone wartości 0, 1, ... ,10.'@);
                  _r:='PREC'
               ?};
               _r
            \")
         || {? REF.FIRMA<>_firma | REF.W_SCH<>_w_sch | _wyl<>SIK.WYL | _firmawyl<>REF.FIRMAWYL
            || {? ~exec('update_params','!fks_ksp_zspr')
               || REF.FIRMA:=_firma;
                  REF.W_SCH:=_w_sch;
                  SIK.WYL:=_wyl
               || exec('save_war','!fks_ksp_zspr')
               ?}
            ?};
            {? UNPAR.P25<>_h2
            || sel_exit()
            |? UNPAR.PSLO<>_h1
            || exec('set_tyt','!fks_ksp_zspr')
            ?};
            {? _inc<>SSTALE.INC
            || {? SSTALE.WARIANT=1 & ( SSTALE.TYP='K' | SSTALE.TYP='P' )
               || sel_exit
               || exec('set_tyt','!fks_ksp_zspr')
               ?}
            ?};
            {? SSTALE.ZAOKR<1 || SSTALE.ZAOKR:=1 ?};
            {? SSTALE.ZAOKR<>_zaokr
            || exec('set_tyt','!fks_ksp_zspr')
            ?};
            {? SSTALE.PREC<>_prec
            || exec('save_war','!fks_ksp_zspr');
               sel_exit()
            ?};
            {? ROZNE.KOL_WIER<>_kol
            || dalej:=pierwszy:=1;
               sel_exit()
            ?}
         || REF.FIRMA:=_firma;
            UNPAR.PSLO:=_h1;
            UNPAR.P25:=_h2;
            SSTALE.INC:=_inc;
            SSTALE.PREC:=_prec;
            SSTALE.ZAOKR:=_zaokr;
            ROZNE.KOL_WIER:=_kol;
            SIK.WYL:=_wyl;
            REF.FIRMAWYL:=_firmawyl
         ?};
         VAR_DEL.delete('__F_ref');
         _be:=\"*\";
         REF.fld_fml('FIRMA','BEFORE_EDIT',_be);
         REF.fld_fml('W_SCH','BEFORE_EDIT',_be);
         REF.fld_fml('W_SCH','BEFORE_DISPLAY',_be);
         UNPAR.fld_fml('P25','BEFORE_EDIT',_be);
         UNPAR.fld_fml('P25','BEFORE_DISPLAY',_be);
         UNPAR.fld_fml('PSLO','BEFORE_EDIT',_be);
         UNPAR.fld_fml('PSLO','BEFORE_DISPLAY',_be);
         SSTALE.fld_fml('INC','BEFORE_EDIT',_be);
         SSTALE.fld_fml('INC','BEFORE_DISPLAY',_be);
         REF.fld_fml('FIRMAWYL','BEFORE_EDIT',_be)
      ",,,,,,'R');
::      {! _i:=0..1 |!
::         TT_TREE.win_act(_o,_i,'Menu','Jednostka księgowa',,'Wybór obszaru roboczego: wybrana lub wszystkie jednostki ks.');
::         TT_TREE.win_act(_o,_i,'Formuła','wybór Jedn. ks.','Jednostka księgowa','Wybór jednostki księgowej',"
::            _ref:=OPERATOR.DEPT;
::            exec('wyb_oddz','param',1);
::            {? _ref<>OPERATOR.DEPT
::            || dalej:=1; sel_exit()
::            ?}
::         ");
::         TT_TREE.win_act(_o,_i,'Formuła','Wszystkie jedn. ks.','Jednostka księgowa','Wszystkie jednostki księgowe',"
::            _ref:=OPERATOR.DEPT;
::            exec('wyb_oddz','param',0);
::            {? _ref<>OPERATOR.DEPT
::            || dalej:=1; sel_exit()
::            ?}
::         ")
::      !};

      TT_TREE.win_act(_o,0,'Formuła','Drukuj'@@,,'Wydruk prezentowanych wartości sprawozdań'@,"
         __ONE_SPR:=0;
         popup(1,'WZORCE SPRAWOZDAŃ',
                 'A. Wskazane sprawozdanie'@,'',\"
                     TT_TREE.cntx_psh(); TT_TREE.prefix();
                     {! |? TT_TREE.seek(TT_TREE.TREE,TT_TREE.name()) !};
                     __ONE_SPR:=1; exec('rep_exec','#b_report','FKS_KSP_ZSPR','fks_tree_spr',,,,,,,,'W');
                     TT_TREE.cntx_pop()
                 \",
                 'B. Wszystkie sprawozdania'@,'',\"__ONE_SPR:=0; exec('rep_exec','#b_report','FKS_KSP_ZSPR','fks_tree_spr',,,,,,,,'W')\"
         );
         &__ONE_SPR
      ",,,,,,'D');
      TT_TREE.win_act(_o,0,'Formuła','Ustaw jęz&yk tłumaczeń'@@,,'Wybór języka tłumaczeń'@,"
         _h1:=SKID.LANG_SLO;
         exec('set_lang','lang');
         {? _h1<>SKID.LANG_SLO
         || exec('set_tyt','!fks_ksp_zspr');
            dalej:=1; sel_exit()
         ?}
      ",,,,,,'Y');
      TT_TREE.win_act(_o,0,'Formuła','Wartości skład&ników'@@,,'Wyświetla wartości algorytmów wiesza dla wykonania'@,"exec('show_wykon','!fks_ksp_zspr')",,,,,,'N');
      TT_TREE.win_act(_o,0,'Menu','Opisy wiersza'@);
      TT_TREE.win_act(_o,0,'Formuła','Opis algorytmu'@@,'Opisy wiersza'@,'Wyświetla dokument z opisem algorytmu'@,"exec('v_plik','!fks_ksp_zspr')",,,,,,'O');
      TT_TREE.win_act(_o,0,'Formuła','Notatki dla wiersza'@@,'Opisy wiersza'@,'Wyświetla dokument z opisem algorytmu'@,"
         DEFW.prefix();
         {? DEFW.seek(TT_TREE.REF,)
         || exec('def_opis','sprfin')
         ?}
      ",,,,,,'N');
      TT_TREE.win_act(_o,0,'Szukaj');
      TT_TREE.win_act(_o,0,'Rekord',,,,"
         DEFW.prefix();
         _gray:='';
         {? TT_TREE.POZIOM<2 | DEFW.seek(TT_TREE.REF,) & ~DEFW.PLIK
         || _gray+='O(O)'
         ?};
         {? ~(SSTALE.WARIANT<99 & TT_TREE.POZIOM>1 |
            SSTALE.WARIANT>99 & (UNPAR.P16='O' | TT_TREE.POZIOM & (UNPAR.P16='K' | TT_TREE.MOD))
            )
         || _gray+='N'
         || DEFW.cntx_psh();
            {? DEFW.seek(TT_TREE.REF,) & DEFW.ALGORYTM<>'F'
            || _gray+='N'
            ?};
            DEFW.cntx_pop()
         ?};
         {? TT_TREE.POZIOM<2 | DEFW.seek(TT_TREE.REF,)
         || WOPIS.index('OPIS');
            WOPIS.prefix(DEFW.ref());
            {? ~WOPIS.first()
            || _gray+='O(N)'
            ?}
         ?};
         TT_TREE.actions_grayed(TT_TREE.win_sel('?'),_gray);
         ~~
      ");
      TT_TREE.copy_chk(_o);
      TT_TREE.win_sel(_o);
::      TT_TREE.fld_attr(,2);
      TT_TREE.tr_fml(_o,"exec('tree_roz','!fks_ksp_zspr',_a)","TT_TREE.ROZ");
      TT_TREE.win_fml(_o,UNPAR,'P11',,'ICON_BEFORE',"
         {? TT_TREE.POZIOM>1
         || DEFW.prefix();
            {? DEFW.seek(TT_TREE.REF,DEFW.name())
            || {? DEFW.PLIK
               || 'xwin16.png:7'
               || ''
               ?}
            ?}
         |? TT_TREE.POZIOM=0
         || 'xwin16.png:32'
         || 'xwin16.png:33'
         ?}
      ");
      TT_TREE.win_act(_o,0,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,,
                      "exec('legenda','color','#SPRAWOZD#')",,,,,'L');
      _o_szuk:=TT_TREE.mk_edit('Szukaj'@,0,'#tt_tree_szukaj');
      TT_TREE.win_efld(_o_szuk,,'LABEL');
      TT_TREE.win_patt(_o_szuk);
      exec('set_tyt','!fks_ksp_zspr');
      {? _cur_lab<>''
      || TT_TREE.cntx_psh(); TT_TREE.index(_ndx_lab); TT_TREE.prefix(_cur_lab,_cur_ref);
         _ref:={? TT_TREE.first() || TT_TREE.ref() || null ?};
         TT_TREE.cntx_pop();
         {? _ref=null || TT_TREE.find_key(0) || TT_TREE.seek(_ref) ?}
      ?};
      (_win_poz:=TT_TREE.select(,_cur_lab<>'',_win_poz);_win_poz) & (_cur_lab:=TT_TREE.LABEL;_cur_ref:=TT_TREE.REF;1) & ~dalej
   !};
   VAR_DEL.delete('okr','naz','naz_full');
   {? pierwszy || VAR_DEL.delete('TT_TREE') ?};
   dalej
!};
SSTALE.cntx_pop();
VAR_DEL.delete('TT_TREE','kolor','rok_poz');
WARTOSCI.cntx_pop();
VAR_DEL.delete('tt_okr','tt_size','tt_max','tt_mov','__ndx_tl');
VAR_DEL.delete('l_okr','liczba','delta','dalej','pierwszy','start','stop','width');
REF.FIRMA:=_firma


\win_tree_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Tworzy okno z parametrami do przegladania sprawozdań
::----------------------------------------------------------------------------------------------------------------------
_red:=ROZNE.mk_edit('Parametry prezentacji',,'rozparspr');
ROZNE.win_ewin(_red,,'SPR_TRE1');
{? var_press('Konsolid')>0
|| {? SSTALE.WARIANT=6
   || ROZNE.win_ewin(_red,,'SPR_TRE4')
   || ROZNE.win_ewin(_red,,'SPR_TRE2')
   ?}
?};
ROZNE.win_ewin(_red,,'SPR_TRE3');
_btn:=ROZNE.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['OK'],"'key:F2'");
ROZNE.btn_eopt(_red,_btn,'tooltip='+exec('help_red_ok','#window','P'));
_an:=ROZNE.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'],'key:Esc');
ROZNE.btn_eopt(_red,_an,'tooltip='+exec('help_red_esc','#window','A'));
_red


\spr_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Kontrola poprawnosci wprowadzonych danych dla wariantow
::   WE: [_a] - sprawdzac wypelnienie pol [1]-tak 0-nie
::  OLD: \spr_rec/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=1 ?};
_pole:={? _a
       || {? SSTALE.WARIANT=1
          || __CHK.record2(SSTALE,'ROK','Rok')
          |? SSTALE.WARIANT=2
          || __CHK.record2(SSTALE,'OKRES','Okres','ROK2','Od','ROK3','Do')
          |? SSTALE.WARIANT=3
          || __CHK.record2(SSTALE,'ROK','Rok 1','OSPR','Okres 1','ROK2','Rok 2','OSPR2','Okres 2')
          |? SSTALE.WARIANT=4
          || __CHK.record2(SSTALE,'ROK','Rok','OSPR','Okres')
          |? SSTALE.WARIANT=5
          || __CHK.record3(SSTALE,'ROK','Rok',SIK,'WIDOK','Widok')
          || chk_rec()
          ?}
       || ''
       ?};
{? _pole=''
|| {? SSTALE.WARIANT=1 &
     (OSPR.index('OKRES1'); OSPR.prefix(SSTALE.ROK().FIRMA,SSTALE.TYP,SSTALE.ROK); ~OSPR.first())
   || _to:={? SSTALE.TYP='M' || 'miesięcznego'@ |? SSTALE.TYP='K' || 'kwartalnego'@ |? SSTALE.TYP='P' || 'półrocznego'@ || 'rocznego'@ ?};
      FUN.info('Rok %1 nie ma zdefiniowanego %2 okresu raportowania.'@[_to,SSTALE.ROK().NAZ]);
      _pole:='ROK'
   |? SSTALE.WARIANT=2
   || {? SSTALE.ROK2().POCZ_ROK>SSTALE.ROK3().POCZ_ROK
      || FUN.info('Nieprawidłowy zakres lat.'@); _pole:='ROK2'
      || ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
         {? ROK_F.first()
         || OSPR.index('OKR_ROK'); OSPR.prefix(REF.FIRMA,SSTALE.OKRES);
            SSTALE.ROK2(); _l:=0; _ok:=1;
            {! |?
               {? ROK_F.ref()<>SSTALE.ROK2 || _l+=1 ?};
               _ok:=OSPR.find_key(ROK_F.ref());
               _ok & ROK_F.ref<>SSTALE.ROK3 & ROK_F.next()
            !};
            {? _ok=0 || FUN.info('Rok %1 nie zawiera wskazanego okresu.'@[ROK_F.NAZ]); _pole:='ROK2'
            |? _l>3  || FUN.info('Dopuszczalny trzyletni zakres lat.'@); _pole:='ROK2'
            |? _l=0  || FUN.info('Zakres lat powinien zawierać co najmniej dwa lata.'@); _pole:='ROK2'
            ?}
         ?}
      ?}
   |? SSTALE.WARIANT=3 & SSTALE.OSPR=SSTALE.OSPR2
   || FUN.info('Wybrano te same okresy sprawozdawcze.'@); _pole:='OSPR2'
   ?}
?};
_pole


\params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Wypelnienie parametrow wariantu
::  OLD: \params/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.WARIANT=2
|| SLU.index('WZORZEC'); SLU.prefix('prosty','~TYPY OKRESÓW');
   {? SLU.first() || SLO.win_sel('ONE'); SLO.win_dict('ONE') ?};
   WARIANTY.win_edit('WARIANT'+$SSTALE.WARIANT);
   WARIANTY.edit("exec('spr_rec','!fks_ksp_zspr')")
?}


\naz_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Zwraca nazwe kolumny tabeli w opraciu o dane w buforze
::   WE:  _a  - 1-pelna nazwa 0-nazwa skrocona
::       [_b] - maksymalna dlugosc pelnej nazwy okresu - domyslnie [16]
::  OLD: \naz_kol/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=1 || _b:=16 ?};
_r:={? OSPR.TYP='M'
    || {? _a
       || exec('get_lang','lang',OSPR.OKRES_OD();OKRO_F.ref())+' '+OKRO_F.ROK().NAZ
       || exec('get_lang','lang',OSPR.ref(),,0)+{? SSTALE.WARIANT=2 || OSPR.OKRES_OD().ROK().NAZ || '' ?}
       ?}
    || {? _a
       || exec('get_lang','lang',OSPR.ref())
       || {? +OSPR.NAZWA>_b
          || exec('get_lang','lang',OSPR.ref(),,0)+{? SSTALE.WARIANT=2 || ' '+OSPR.OKRES_OD().ROK().NAZ || '' ?}
          || exec('get_lang','lang',OSPR.ref())
          ?}
       ?}
    ?};
_b+_r


\set_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Ustawia zmienne w oparciu o wartosci bufora tabeli SSTALE
::  OLD: \set_war/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
OSPR.index('ROK_NAZ'); OSPR.prefix(SSTALE.ROK().FIRMA,SSTALE.ROK);
_l:=0;
{? OSPR.first()
|| {! |?
      {? OSPR.TYP<>'M' | SSTALE.TYP='M' | (OSPR.OKRES_OD().POCZ<>date(0,0,0) & OSPR.OKRES_DO().POCZ<>date(0,0,0))
      || _l+=1
      ?};
      OSPR.TYP<>SSTALE.TYP & OSPR.next()
   !}
?};
delta:=_l-1;
OSPR.prefix(SSTALE.ROK().FIRMA,SSTALE.ROK,SSTALE.TYP);
liczba:=OSPR.size()-
        {? SSTALE.TYP='M'
        || (OSPR.first() & OSPR.OKRES_OD().POCZ=date(0,0,0))+
           (OSPR.last() & OSPR.OKRES_OD().POCZ=date(0,0,0))
        || 0
        ?};
exec('start','!fks_ksp_zspr')


\start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Ustawia zmienne odpowiedzialne za przesuniecie
::  OLD: \start/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
start:=1; stop:=liczba


\tree_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Dodanie sprawozdan do struktury hierarchicznej - pierwszy poziom drzewa
::  OLD: \tree_gr/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
_lp:=0;
{? SSTALE.WARIANT=4
|| USRGRSIK.index('GR_SIK'); USRGRSIK.prefix(SSTALE.GR_SIK);
   {? USRGRSIK.first()
   || GR_SIK.prefix();
      {? GR_SIK.seek(SSTALE.GR_SIK) & GR_SIK.AKC='T'
      || TT_TREE.blank();
         TT_TREE.TREE:=0;
         TT_TREE.LP:=_lp; _lp+=1;
         TT_TREE.LABEL:='['+GR_SIK.SKROT+'] '+GR_SIK.NAZWA;
         TT_TREE.POZIOM:=0;
         TT_TREE.REF:=#GR_SIK.ref();
         TT_TREE.RKOL:=TT_TREE.DANE:=TT_TREE.ROZ:=0;
         TT_TREE.add();
         exec('tree_wie','!fks_ksp_zspr')
      ?}
   || SSTALE.GR_SIK:=null; exec('save_war','!fks_ksp_zspr')
   ?}
|| USRGRSIK.cntx_psh();
   {? var_press('Konsolid')<=0
   || USRGRSIK.index('NAZWA'); USRGRSIK.prefix(OPERATOR.USER)
   || USRGRSIK.index('FIRMA'); USRGRSIK.prefix(OPERATOR.USER,REF.GRUPA)
   ?};
   {? USRGRSIK.first()
   || {! |?
         {? USRGRSIK.GR_SIK().AKC='T'
         || TT_TREE.blank();
            TT_TREE.TREE:=0;
            TT_TREE.LP:=_lp; _lp+=1;
            TT_TREE.LABEL:='['+GR_SIK.SKROT+'] '+GR_SIK.NAZWA;
            TT_TREE.POZIOM:=0;
            TT_TREE.REF:=#GR_SIK.ref();
            TT_TREE.KOL:=TT_TREE.RKOL:=TT_TREE.DANE:=TT_TREE.ROZ:=0;
            TT_TREE.add();
            exec('tree_kol','!fks_ksp_zspr')
         ?};
         USRGRSIK.next()
      !}
   ?};
   USRGRSIK.cntx_pop()
?}


\t_g_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Rozwija lub zwija wszystkie podgalezie aktualnej galezi
::   WE: _a - 1-rozwiniecie 0-zwiniecie
::  OLD: \t_g_roz/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
TT_TREE.cntx_psh();
exec('tree_roz','!fks_ksp_zspr',_a);
TT_TREE.prefix(TT_TREE.ref());
{? TT_TREE.first()
|| {! |?
      exec('tree_roz','!fks_ksp_zspr',_a);
      exec('t_g_roz','!fks_ksp_zspr',_a);
      TT_TREE.next()
   !}
?};
TT_TREE.cntx_pop()


\tree_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Na akcje Rozwiniecie dla struktury hierarchicznej sprawozdan - tabele TT_TREE
::   WE: _a - 1-rozwiniecie 0-zwiniecie
::  OLD: \tree_roz/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? TT_TREE.ROZ<>_a || TT_TREE.ROZ:=_a; TT_TREE.put() ?};
{? _a & ~TT_TREE.DANE
|| TT_TREE.DANE:=1; TT_TREE.put();
   {? TT_TREE.POZIOM | SSTALE.WARIANT=4
   || TT_TREE.cntx_psh();
      TT_TREE.prefix(TT_TREE.ref());
      {? TT_TREE.first()
      || _poziom:=TT_TREE.POZIOM+1;
         {! |?
            DEFW.prefix(); DEFA.index('WIERSZ'); GR_KOL.prefix();
            {? DEFW.seek(TT_TREE.REF,DEFW.name()) & ( SSTALE.WARIANT=4 | GR_KOL.seek(TT_TREE.KOL,GR_KOL.name()) )
            || {? {? SSTALE.WARIANT=4
                  || GR_KOL.index('LP'); GR_KOL.prefix(SSTALE.GR_SIK); GR_KOL.first()
                  || 1
                  ?}
               || _lp:=_nr:=0; _defw:=DEFW.ref();
                  {? DEFW.TAB<>'' | DEFW.SLU
                  || {? DEFW.SLU
                     || SLO.index('SL'); SLO.prefix(DEFW.SLU().SLU);
                        {? SLO.first()
                        || _lp:=0; _nr:=1;
                           {! |?
                              {? DEFW.MASKA='' | exec('spr_mask','dok_fks',DEFW.MASKA,SLO.KOD)
                              || TT_TREE.TREE:=TT_TREE.ref();
                                 TT_TREE.LP:=_lp; _lp+=1;
                                 TT_TREE.LABEL:='['+SLO.KOD+'] '+SLO.TR;
                                 TT_TREE.POZIOM:=_poziom;
                                 TT_TREE.REF:=#DEFW.ref();
                                 {? SSTALE.WARIANT=4
                                 || ($('TT_TREE.KOL'+$_nr+':=#GR_KOL.ref()'))()
                                 || TT_TREE.KOL:=#GR_KOL.ref()
                                 ?};
                                 TT_TREE.RKOL:=TT_TREE.DANE:=TT_TREE.ROZ:=0;
                                 TT_TREE.KOD:=SLO.KOD;
                                 TT_TREE.CZY_WAL:=DEFW.VAL_TYP<>'J';
                                 TT_TREE.cntx_psh(); TT_TREE.prefix();
                                 TT_TREE.add(); _tree1:=TT_TREE.ref();
                                 TT_TREE.cntx_pop();
                                 exec('rozwin1','!fks_ksp_zspr',DEFW.ref(),_tree1,_lp,3)
                              ?};
                              SLO.next()
                           !}
                        ?}
                     |? DEFW.TAB<>''
                     || ($(DEFW.TAB+'.index(\''+DEFW.INDEX+'\')'))();
                        ($(DEFW.TAB+'.prefix('+DEFW.PREFIX+')'))();
                        {? ($(DEFW.TAB+'.first'))()
                        || _lp:=0; _nr:=1;
                           {! |?
                              _kod:=($(DEFW.KODF))();
                              {? DEFW.MASKA='' | exec('spr_mask','dok_fks',DEFW.MASKA,_kod)
                              || TT_TREE.TREE:=TT_TREE.ref();
                                 TT_TREE.LP:=_lp; _lp+=1;
                                 TT_TREE.LABEL:='['+_kod+'] '+($(DEFW.TR))();
                                 TT_TREE.POZIOM:=_poziom;
                                 TT_TREE.REF:=#DEFW.ref();
                                 {? SSTALE.WARIANT=4
                                 || ($('TT_TREE.KOL'+$_nr+':=#GR_KOL.ref()'))()
                                 || TT_TREE.KOL:=#GR_KOL.ref()
                                 ?};
                                 TT_TREE.RKOL:=TT_TREE.DANE:=TT_TREE.ROZ:=0;
                                 TT_TREE.KOD:=_kod;
                                 TT_TREE.CZY_WAL:=DEFW.VAL_TYP<>'J';
                                 TT_TREE.cntx_psh(); TT_TREE.prefix();
                                 TT_TREE.add(); _tree1:=TT_TREE.ref();
                                 TT_TREE.cntx_pop();
                                 exec('rozwin1','!fks_ksp_zspr',DEFW.ref(),_tree1,_lp,3)
                              ?};
                              ($(DEFW.TAB+'.next()'))()
                           !}
                        ?}
                     ?}
                  || {! |?
                        _kol:=GR_KOL.ref(); _kol_alg:=GR_KOL.ALGORYTM;
                        _nr+=1;
                        DEFA.prefix(_defw,GR_KOL.LP);
                        {? SSTALE.WARIANT=4 || GR_KOL.cntx_psh() ?};
                        {? ROZNE.KOL_WIER=0 & DEFA.first()
                        || {! |?
                              DEFA.ARGUMENT();
                              DEFA.ARG_KOL();
                              TT_TREE.TREE:=TT_TREE.ref();
                              TT_TREE.LP:=_lp; _lp+=1;
                              TT_TREE.LABEL:='['+DEFW.KOD+'] '+DEFW.NAZWA;
                              TT_TREE.KOD:='';
                              TT_TREE.POZIOM:=_poziom;
                              TT_TREE.REF:=#DEFW.ref();
                              {? SSTALE.WARIANT=4
                              || ($('TT_TREE.KOL'+$_nr+':=#GR_KOL.ref()'))()
                              || TT_TREE.KOL:=#GR_KOL.ref()
                              ?};
                              TT_TREE.RKOL:=_nr;
                              TT_TREE.DANE:=DEFW.SLU | DEFW.TAB<>'';
                              TT_TREE.ROZ:=0;
                              TT_TREE.CZY_WAL:=DEFW.VAL_TYP<>'J';
                              TT_TREE.cntx_psh(); TT_TREE.index(__ndx_lr); TT_TREE.prefix();
                              {? SSTALE.WARIANT=4 & TT_TREE.find_key(TT_TREE.LABEL,TT_TREE.REF) & (TT_TREE.RKOL<>_nr | TT_TREE.RKOL=0 & _kol_alg)
                              || TT_TREE.RKOL:=0;
                                 ($('TT_TREE.KOL'+$_nr+':=#GR_KOL.ref()'))();
                                 TT_TREE.put()
                              || TT_TREE.add()
                              ?};
                              _tree1:=TT_TREE.ref();
                              TT_TREE.cntx_pop();
                              exec('rozwin1','!fks_ksp_zspr',DEFW.ref(),_tree1,_lp,3);
                              DEFA.next()
                           !}
                        ?};
                        {? SSTALE.WARIANT=4 || GR_KOL.cntx_pop() ?};
                        SSTALE.WARIANT=4 & GR_KOL.next()
                     !}
                  ?}
               ?}
            ?};
            TT_TREE.next()
         !}
      ?};
      TT_TREE.cntx_pop()
   || TT_TREE.cntx_psh();
      TT_TREE.prefix(TT_TREE.ref()); GR_SIK.prefix();
      {? GR_SIK.seek(TT_TREE.REF,GR_SIK.name()) & TT_TREE.first()
      || {! |? exec('tree_wie','!fks_ksp_zspr'); TT_TREE.next() !}
      ?};
      TT_TREE.cntx_pop()
   ?}
?}


\tree_wie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Dodanie wierszy sprawozdania do struktury hierarchicznej - trzeci poziom drzewa
::  OLD: \tree_wie/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
_tree:=TT_TREE.ref();
{? TT_TREE.POZIOM
|| GR_KOL.prefix();
   _gr_alg:=GR_KOL.seek(TT_TREE.KOL,) & GR_KOL.ALGORYTM<>'F'
|| _gr_alg:=0
?};
{? _gr_alg | SSTALE.WARIANT=4 & exec('kol_ma_alg','sprfin')
|| {? TT_TREE.POZIOM=1 | SSTALE.WARIANT=4
   || DEFW.index('GRUPA'); DEFW.prefix(GR_SIK.ref()); _lp:=0;
      {? DEFW.first()
      || {!
         |? TT_TREE.TREE:=_tree;
            TT_TREE.LP:=_lp; _lp+=1;
            TT_TREE.LABEL:='['+DEFW.KOD+'] '+DEFW.NAZWA;
            TT_TREE.KOD:='';
            TT_TREE.POZIOM:=2;
            TT_TREE.REF:=#DEFW.ref();
            {? SSTALE.WARIANT=4
            || TT_TREE.RKOL:=0;
               {! _i:=1 .. liczba |! ($('TT_TREE.KOL'+$_i+':=_a'))(okr[_i]) !}
            || TT_TREE.KOL:=TT_TREE.RKOL:=TT_TREE.KOL
            ?};
            TT_TREE.DANE:=DEFW.SLU | DEFW.TAB<>''; TT_TREE.ROZ:=0;
            TT_TREE.CZY_WAL:=DEFW.VAL_TYP<>'J';
            TT_TREE.cntx_psh(); TT_TREE.prefix();
            TT_TREE.add(); _tree1:=TT_TREE.ref();
            TT_TREE.cntx_pop();
            exec('rozwin1','!fks_ksp_zspr',DEFW.ref(),_tree1,_lp,3);
            DEFW.next()
         !}
      ?}
   ?}
|| DEFW.index('GRUPA'); DEFW.prefix(GR_SIK.ref()); _lp:=0;
   {? DEFW.first()
   || {!
      |? {? ROZNE.KOL_WIER=0
         || DEFA.index('SKLADNIK'); DEFA.prefix(DEFW.ref());
            {? DEFA.first()
            || _ok:=1;
               DEFW.cntx_psh();
               {! |?
                  {? DEFA.WYR().GRUPA=GR_SIK.ref() || _ok:=0 ?};
                  _ok & DEFA.next()
               !};
               DEFW.cntx_pop()
            || _ok:=DEFW.DEFW_NAD=0
            ?}
         || _ok:=1
         ?};
         {? _ok
         || TT_TREE.TREE:=_tree;
            TT_TREE.LP:=_lp; _lp+=1;
            TT_TREE.LABEL:='['+DEFW.KOD+'] '+DEFW.NAZWA;
            TT_TREE.KOD:='';
            TT_TREE.POZIOM:=2;
            TT_TREE.REF:=#DEFW.ref();
            {? SSTALE.WARIANT=4
            || TT_TREE.RKOL:=0;
               {! _i:=1 .. liczba |! ($('TT_TREE.KOL'+$_i+':=_a'))(okr[_i]) !}
            || TT_TREE.KOL:=TT_TREE.RKOL:=TT_TREE.KOL
            ?};
            TT_TREE.DANE:=DEFW.SLU | DEFW.TAB<>''; TT_TREE.ROZ:=0;
            TT_TREE.CZY_WAL:=DEFW.VAL_TYP<>'J';
            TT_TREE.cntx_psh(); TT_TREE.prefix();
            TT_TREE.add(); _tree1:=TT_TREE.ref();
            TT_TREE.cntx_pop();
            exec('rozwin1','!fks_ksp_zspr',DEFW.ref(),_tree1,_lp,3)
         ?};
         DEFW.next()
      !}
   ?}
?}


\rozwin1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Formula pomocnicza przy rozwijaniu w strukturze hierarchicznej
::   WE: _a - Wiersz
::       _b - nadrzedna galaz drzewa
::       _c - lp pozycji
::       _d - poziom drzewa
::  OLD: \rozwin1/plan_spr.fml
::----------------------------------------------------------------------------------------------------------------------
DEFW.cntx_psh(); TT_TREE.cntx_psh();
DEFW.index('DEFW_NAD'); DEFW.prefix(_a);
{? DEFW.first()
|| _lp:=_c;
   {!
   |? _i:=0;
      {!
      |? _i+=1;
         GR_KOL.cntx_psh(); GR_KOL.prefix();
         {? SSTALE.WARIANT=100 & UNPAR.P21='K' | SSTALE.WARIANT=101 & UNPAR.P16='K'
         || GR_KOL.seek(tt_okr[_i][1])
         |? SSTALE.WARIANT<>4
         || GR_KOL.seek(TT_TREE.KOL,)
         ?};
         {? SSTALE.WARIANT=100 & UNPAR.P21='K' | SSTALE.WARIANT=101 & UNPAR.P16='K'
         || TT_TREE.index(TT_IND4); TT_TREE.prefix();
            _is:=TT_TREE.find_key(_b,#DEFW.ref())
         || _is:=0
         ?};
         {? ~_is
         || TT_TREE.TREE:=_b;
            TT_TREE.LP:=_lp; _lp+=1;
            TT_TREE.LABEL:='['+DEFW.KOD+'] '+DEFW.NAZWA;
            TT_TREE.KOD:='';
            TT_TREE.POZIOM:=_d;
            TT_TREE.REF:=#DEFW.ref();
            TT_TREE.DANE:=DEFW.SLU | DEFW.TAB<>'';
            {? SSTALE.WARIANT>99
            || TT_TREE.TYP:={? DEFW.ANA_TYP='' || 1 || -1 ?};
               TT_TREE.VIEW:=#DEFW.ANA_VIEW;
               TT_TREE.VAL_TYP:=DEFW.VAL_TYP='P';
               TT_TREE.NOT_KAL:=1
            ?}
         ?};
         DEFA.cntx_psh(); DEFA.prefix(DEFW.ref(),GR_KOL.LP);
         _w:=SSTALE.WARIANT=101 & UNPAR.P16='A' | ~DEFA.first() | DEFW.SLU | DEFW.TAB<>'';
         {? SSTALE.WARIANT=100 & UNPAR.P21='K' | SSTALE.WARIANT=101 & UNPAR.P16='K'
         || ($('TT_TREE.KOL'+$_i+'='+$#GR_KOL.ref()))();
            ($('TT_TREE.MOD'+$_i+':=_a'))(_w)
         |? SSTALE.WARIANT<>4
         || TT_TREE.KOL:=#GR_KOL.ref();
            {? SSTALE.WARIANT>99 || TT_TREE.MOD:=_w ?}
         ?};
         DEFA.cntx_pop();
         GR_KOL.cntx_pop();
         TT_TREE.cntx_psh(); TT_TREE.prefix();
         {? _is
         || TT_TREE.put()
         || TT_TREE.add()
         ?};
         TT_TREE.cntx_pop();
         (SSTALE.WARIANT=100 & UNPAR.P21='K' | SSTALE.WARIANT=101 & UNPAR.P16='K') & _i<tt_max
      !};
      DEFW.next()
   !}
?};
DEFW.cntx_pop(); TT_TREE.cntx_pop()


\save_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Zapamietuje parametry zwiazane z wariantami prezentacji wartosci sprawozdan
::  OLD: \save_war/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
WARIANTY.prefix();
{? WARIANTY.get()
|| WARIANTY.WARIANT:=SSTALE.WARIANT;
   WARIANTY.ROK:=SSTALE.ROK;
   WARIANTY.ROK2:=SSTALE.ROK2;
   WARIANTY.TYP:=SSTALE.TYP;
   WARIANTY.OSPR:=SSTALE.OSPR;
   WARIANTY.OSPR2:=SSTALE.OSPR2;
   WARIANTY.ZAOKR:=SSTALE.ZAOKR;
   WARIANTY.PREC:=SSTALE.PREC;
   WARIANTY.INC:=SSTALE.INC;
   WARIANTY.GR_SIK:=SSTALE.GR_SIK;
   WARIANTY.OKRES:=SSTALE.OKRES;
   WARIANTY.WER_VIEW:=SIK.WIDOK;
   WARIANTY.AF:=SSTALE.AF;
   WARIANTY.W_SCH:=SSTALE.W_SCH;
   WARIANTY.put()
?}


\kolumna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przesuwa kolumny dla wariantu nr 4 i 5 - ustawia zmienne
::   WE: _a - (1)-nastepna (-1)-poprzednia
::   WY: Czy przesuniecie jest dozwolone? (1)-tak (0)-nie
::  OLD: \kolumna/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? start+_a>0 & stop+_a<=liczba
|| start+=_a; stop+=_a; 1
?}


\update_params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Ustawienie parametrow
::  OLD: \update_params/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
_rr:=SSTALE.ROK;
_rr2:=SSTALE.ROK2;
_rr3:=SSTALE.ROK3;
_rw:=REF.WYKON;
_rok:=SSTALE.ROK().POCZ_ROK~1;
_rok2:=SSTALE.ROK2().POCZ_ROK~1;
_rok3:=SSTALE.ROK3().POCZ_ROK~1;
{? REF.FIRMA().TYP<>'S' & FIRMA.TYP<>'W'
|| SSTALE.ROK:=exec('rok_f_g','konsolidacja',SSTALE.ROK);
   SSTALE.ROK2:=exec('rok_f_g','konsolidacja',SSTALE.ROK2);
   SSTALE.ROK3:=exec('rok_f_g','konsolidacja',SSTALE.ROK3);
   SSTALE.OSPR:=exec('ospr_fg','konsolidacja',SSTALE.OSPR,'G');
   SSTALE.OSPR2:=exec('ospr_fg','konsolidacja',SSTALE.OSPR2,'G')
|| SSTALE.ROK:=exec('rok_f_f','konsolidacja',SSTALE.ROK);
   SSTALE.ROK2:=exec('rok_f_f','konsolidacja',SSTALE.ROK2);
   SSTALE.ROK3:=exec('rok_f_f','konsolidacja',SSTALE.ROK3);
   SSTALE.OSPR:=exec('ospr_fg','konsolidacja',SSTALE.OSPR,'F');
   SSTALE.OSPR2:=exec('ospr_fg','konsolidacja',SSTALE.OSPR2,'F')
?};
REF.WYKON:=exec('wykon_firmy','konsolidacja',REF.FIRMA,REF.W_SCH);
_ok:=1;
{? _rr<>null & SSTALE.ROK=null
|| FUN.info('Brak roku %1 dla firmy o symbolu %2.'@[$_rok,FIRMA.SYMBOL]); _ok:=0
|? _rr2<>null & SSTALE.ROK2=null
|| FUN.info('Brak roku %1 dla firmy o symbolu %2.'@[$_rok2,FIRMA.SYMBOL]); _ok:=0
|? _rr3<>null & SSTALE.ROK3=null
|| FUN.info('Brak roku %1 dla firmy o symbolu %2.'@[$_rok3,FIRMA.SYMBOL]); _ok:=0
?};
{? _ok
|| sel_exit();
   SSTALE.AF:=REF.FIRMA;
   SSTALE.W_SCH:=REF.W_SCH;
   dalej:=1
|| SSTALE.ROK:=_rr;
   SSTALE.ROK2:=_rr2;
   SSTALE.ROK3:=_rr3;
   REF.WYKON:=_rw
?};
_ok


\set_tyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Ustawia tytul okna wertowania
::  OLD: \set_tyt/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
_tyt:=' ['+'Firma'@+' '+REF.FIRMA().SYMBOL+'] - '+
      {? SSTALE.WARIANT=1
      || 'Wartości %1w okresach %2'@[{? SSTALE.INC || 'narastająco'@+' ' || '' ?},SSTALE.ROK().NAZ]
      |? SSTALE.WARIANT=2
      || 'Dynamika zmian wartości %1w latach'@[{? SSTALE.INC || 'narastająco'@+' ' || '' ?}]
      |? SSTALE.WARIANT=3
      || 'Dynamika zmian wg podanych okresów'@
      |? SSTALE.WARIANT=4
      || 'Wartości %1wg kolumn w okresie %2'@[{? SSTALE.INC || 'narastająco'@+' ' || '' ?},exec('get_lang','lang',SSTALE.OSPR(); OSPR.ref())]
      |? SSTALE.WARIANT=5
      || 'Wartości wg widoku - %1'@[SIK.WIDOK().NAZWA]
      || 'Wartości dla konsolidacji'@
      ?}+' '+
      ' [x'+form(SSTALE.ZAOKR,,,'.')+']'+
      {? UNPAR.PSLO & UNPAR.PSLO<>FINFO.NAROD
      || ' - '+'waluta %1'@[UNPAR.PSLO().KOD]+' , '+'kurs: %1'@[form(UNPAR.P25,,6)]
      || ''
      ?}+
      {? SKID.LANG_SLO
      || ' - '+'język %1'@[-SKID.LANG_SLO().TR]
      || ''
      ?};
TT_TREE.hdr_sel();
TT_TREE.hdr_sel(_tyt)


\zaokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Zmienia precyzje zaokraglenia prezentowanych danych
::   WE: _a - wspolczynnik zmiany (np. 10 lub 0.1)
::  OLD: \zaokr/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a>1 & SSTALE.ZAOKR>=1000000000 || return(0) ?};
SSTALE.ZAOKR*=_a;
{? SSTALE.ZAOKR<1 || SSTALE.ZAOKR:=1 ?}


\v_plik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przegladanie pliku zwiazanego z wierszem sprawozdania
::  OLD: \v_plik/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.WARIANT=4 & TT_TREE.POZIOM | SSTALE.WARIANT<>4 & TT_TREE.POZIOM>1
|| DEFW.prefix();
   {? DEFW.seek(TT_TREE.REF,DEFW.name())
   || exec('view_defw','sprfin')
   ?}
|| FUN.info('Plik prezentujący algorytm może być związany jedynie z wierszem sprawozdania.'@)
?}


\show_wykon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Prezentuje dla wierszy typu F algorytm ich powstania (ALG_PAR)
::  OLD: \show_wykon/plan_spr.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TTPOM');
_odd:={? var_pres('ODD_PLAN')>=0 || ODD_PLAN || ODD_PLAN:=OPERATOR.DEPT; -1 ?};
{? SSTALE.WARIANT<99 & TT_TREE.POZIOM>1 |
   SSTALE.WARIANT>99 & (UNPAR.P16='O' | TT_TREE.POZIOM & (UNPAR.P16='K' | TT_TREE.MOD))
|| _ok:=0;
   DEFW.prefix(); GR_KOL.prefix();
   {? SSTALE.WARIANT=5 || UNPAR.P16:='A' ?};
   {? SSTALE.WARIANT<99 & SSTALE.WARIANT<5
   || TTPOM:=tab_tmp(1,
         'LP','INTEGER',,
         'NAZWA','STRING[20]','Nazwa'@,
         'REF','REAL',
      );
      {? SSTALE.WARIANT=1
      || _x1:=start+delta; _x2:=stop+delta
      |? SSTALE.WARIANT=3
      || _x1:=1; _x2:=2
      |? SSTALE.WARIANT=4
      || _x1:=1; _x2:=obj_len(okr)
      || _x1:=1; _x2:=liczba+1
      ?};
      {! _i:=_x1.._x2
      |! TTPOM.LP:=_i;
         TTPOM.NAZWA:=naz_full[_i];
         {? SSTALE.WARIANT=1 | SSTALE.WARIANT=4
         || TTPOM.REF:=okr[_i]
         |? SSTALE.WARIANT=3
         || TTPOM.REF:={? _i=1 || SSTALE.OSPR || SSTALE.OSPR2 ?}
         || TTPOM.REF:={? SSTALE.WARIANT=2 & _i=1 || SSTALE.OKRES || okr[_i-1] ?}
         ?};
         TTPOM.add()
      !};
      _o:=TTPOM.mk_sel({? SSTALE.WARIANT=4 || 'Kolumny' || 'Okresy' ?});
      TTPOM.win_fld(_o,,'NAZWA');
      TTPOM.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
      TTPOM.win_sel(_o);
      {? TTPOM.select()
      || OSPR.prefix(REF.FIRMA);
         {? SSTALE.WARIANT=4
         || OKRESY.OKR_RAP:=SSTALE.OSPR;
            _ok:=DEFW.seek(TT_TREE.REF,) & GR_KOL.seek(TTPOM.REF,) & OKRESY.OKR_RAP
         || OKRESY.OKR_RAP:={? OSPR.seek(TTPOM.REF,) || OSPR.ref() || null ?};
            _ok:=DEFW.seek(TT_TREE.REF,) & GR_KOL.seek(TT_TREE.KOL,) & OKRESY.OKR_RAP
         ?}
      || _ok:=-1
      ?}
   |? SSTALE.WARIANT=6
   || TTPOM:=tab_tmp(1,
         'LP','INTEGER',,
         'NAZWA','STRING[20]','Nazwa'@,
         'REF','REAL',
      );
      {! _i:=1..liczba
      |! TTPOM.LP:=_i;
         TTPOM.NAZWA:=naz_full[_i];
         TTPOM.REF:=okr[_i];
         TTPOM.add()
      !};
      _o:=TTPOM.mk_sel('Typ wartości');
      TTPOM.win_fld(_o,,'NAZWA');
      TTPOM.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
      TTPOM.win_sel(_o);
      {? TTPOM.select()
      || WERSJE.prefix();
         {? WERSJE.seek(TTPOM.REF,)
         || REF.WYKON:=WERSJE.ref();
            OKRESY.OKR_RAP:=SSTALE.OSPR;
            _ok:=DEFW.seek(TT_TREE.REF,) & GR_KOL.seek(TT_TREE.KOL,) & OKRESY.OKR_RAP
         ?}
      || _ok:=-1
      ?}
   |? UNPAR.P16='S'
   || OKRESY.OKR_RAP:=OKRESY.OKRES4;
      _ok:=DEFW.seek(TT_TREE.REF,) & GR_KOL.seek(TT_TREE.KOL,)
   |? UNPAR.P16='O'
   || OSPR.prefix(REF.FIRMA);
      OKRESY.OKR_RAP:={? OSPR.seek(TT_TREE.REF,) || OSPR.ref() || null ?};
      SIK.GR_KOL();
      SIK.DEFW();
      _ok:=SIK.GR_KOL & SIK.DEFW & OKRESY.OKR_RAP
   |? UNPAR.P16='A'
   || VAR_DEL.delete('TTPOM');
      TTPOM:=tab_tmp(1,
         'LP','INTEGER',,
         'NAZWA','STRING[20]','Nazwa'@,
         'REF','REAL',
      );
      {! _i:=1..tt_size
      |! TTPOM.LP:=_i;
         TTPOM.NAZWA:=tt_okr[_i][3];
         TTPOM.REF:=tt_okr[_i][1];
         TTPOM.add()
      !};
      _o:=TTPOM.mk_sel('Okresy'@);
      TTPOM.win_fld(_o,,'NAZWA');
      TTPOM.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
      TTPOM.win_sel(_o);
      {? TTPOM.select()
      || OSPR.prefix(REF.FIRMA);
         OKRESY.OKR_RAP:={? OSPR.seek(TTPOM.REF,) || OSPR.ref() || null ?};
         _ok:=DEFW.seek(TT_TREE.REF,) & GR_KOL.seek(TT_TREE.KOL,) & OKRESY.OKR_RAP
      || _ok:=-1
      ?}
   |? UNPAR.P16='D'
   || VAR_DEL.delete('TTPOM');
      TTPOM:=tab_tmp(1,
         'LP','INTEGER',,
         'NAZWA','STRING[20]','Nazwa'@,
         'REF','REAL',
      );
      {! _i:=1..tt_size-1
      |! TTPOM.LP:=_i;
         TTPOM.NAZWA:=tt_okr[_i][2];
         TTPOM.REF:=tt_okr[_i][1];
         TTPOM.add()
      !};
      _o:=TTPOM.mk_sel('Jednostki księgowe'@);
      TTPOM.win_fld(_o,,'NAZWA');
      TTPOM.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
      TTPOM.win_sel(_o);
      {? TTPOM.select()
      || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
         OKRESY.OKR_RAP:=OKRESY.OKRES4;
         ODD_PLAN:={? ODD.seek(TTPOM.REF,) || ODD.ref() || null ?};
         _ok:=DEFW.seek(TT_TREE.REF,) & GR_KOL.seek(TT_TREE.KOL,) & OKRESY.OKR_RAP
      || _ok:=-1
      ?}
   || VAR_DEL.delete('TTPOM');
      TTPOM:=tab_tmp(1,
         'LP','INTEGER',,
         'NAZWA','STRING[60]','Nazwa'@,
         'REF','REAL',
      );
      {! _i:=1..tt_size
      |! TTPOM.LP:=_i;
         TTPOM.NAZWA:=tt_okr[_i][3];
         TTPOM.REF:=tt_okr[_i][1];
         TTPOM.add()
      !};
      _o:=TTPOM.mk_sel('Kolumny'@);
      TTPOM.win_fld(_o,,'NAZWA');
      TTPOM.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
      TTPOM.win_sel(_o);
      {? TTPOM.select()
      || OKRESY.OKR_RAP:=OKRESY.OKRES4;
         _ok:=DEFW.seek(TT_TREE.REF,) & GR_KOL.seek(TTPOM.REF,) & OKRESY.OKR_RAP
      || _ok:=-1
      ?}
   ?};
   {? _ok>0
   || {? DEFW.ALGORYTM='F'
      || WARTOSCI.cntx_psh(); W_ALGPAR.cntx_psh();
         WYRAZ.WIERSZ:=DEFW.ref();
         _rok:=OKRESY.OKR_RAP().ROK().KOD;
         WARTOSCI.use('wsik_'+_rok);
         W_ALGPAR.use('walg_'+_rok);
         WARTOSCI.index('OKRES');
         WARTOSCI.prefix(OKRESY.OKR_RAP,REF.WYKON,DEFW.ref(),GR_KOL.ref(),{? SSTALE.WARIANT<99 || OPERATOR.DEPT || ODD_PLAN ?});
         PARAM.KW1:={? WARTOSCI.first || WARTOSCI.WARTOSC || 0 ?};
         PARAM.AKTUAL:='1';
         UNPAR.P1_AE:=exec('get_lang','lang',DEFW.ref());
         UNPAR.P2_AE:=exec('get_lang','lang',GR_KOL.ref());
         UNPAR.P3_AE:={? ALG_PAR.get() & ALG_PAR.GR_KOL || exec('get_lang','lang',ALG_PAR.GR_KOL) || '' ?};
         WYRAZ.GRUPA:=DEFW.GRUPA;
         ALG_PAR.win_sel('PAR_PLAN');
         exec('title','lang',ALG_PAR);
         ALG_PAR.hdr_sel(' - %1'@[OKRESY.OKR_RAP().NAZWA]);
         _wyl:={? REF.WYKON().WYKON='W' || 'T' |? WERSJE.WYKON='T' || 'N' || '' ?};
         VAR_DEL.delete('AlgMno');
         {? _wyl<>'' || AlgMno:=1 ?};
         {? REF.FIRMA=REF.GRUPA
         || _firmy:=sql('select FIRMA from W_STR where W_SCH=:_a and FIRMA!=:_b',REF.W_SCH,REF.GRUPA);
            ALG_PAR.prefix();
            ALG_PAR.f_set(
               'FIRMA(SYMBOL),ROK(NAZ),LP',
               'join ROK_F using(ALG_PAR.ROK,ROK_F.REFERENCE)',
               'DEFW=:_a and GR_KOL=:_b '+
               {? _wyl<>'' || 'and WYL=\':_c\' ' || '' ?}+
               {? SIK.ROK1<>null || 'and ROK_F.POCZ_ROK=TO_DATE(\':_d\') ' || '' ?}+
               'and ALG_PAR.FIRMA in ( select FIRMA from :_e)',
               DEFW.ref(),GR_KOL.ref(),_wyl,$OKRESY.OKR_RAP().ROK().POCZ_ROK,_firmy
            )
         || {? ALG_PAR.f_active() || ALG_PAR.f_clear() ?};
            {? _wyl=''
            || ALG_PAR.index('ALG_PAR1'); ALG_PAR.prefix(REF.FIRMA,DEFW.ref(),GR_KOL.ref(),OKRESY.OKR_RAP().ROK().NAZ)
            || ALG_PAR.index('ALG_PAR7'); ALG_PAR.prefix(REF.FIRMA,DEFW.ref(),GR_KOL.ref(),_wyl,OKRESY.OKR_RAP().ROK().NAZ)
            ?}
         ?};
         _dept:=OPERATOR.DEPT; OPERATOR.DEPT:=ODD_PLAN;
         ALG_PAR.select(,,,{? REF.FIRMA=REF.GRUPA || 'Z' || '' ?});
         OPERATOR.DEPT:=_dept;
         WARTOSCI.cntx_pop(); W_ALGPAR.cntx_pop()
      || FUN.info('Akcja dostępna dla wierszy na najniższym poziomie.'@)
      ?}
   |? _ok=0
   || FUN.info('Nie znaleziono wiersza lub kolumny.'@)
   ?}
|| FUN.info('Akcja nie jest dostępna z tego poziomu.'@)
?};
{? _odd<>-1 || ODD_PLAN:=_odd || VAR_DEL.delete('ODD_PLAN') ?};
VAR_DEL.delete('TTPOM','AlgMno')


\tree_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Dodanie kolumn sprawozdania do struktury hierarchicznej - drugi poziom drzewa
::  OLD: \tree_kol/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
_tree:=TT_TREE.ref();
GR_KOL.index('LP'); GR_KOL.prefix(GR_SIK.ref()); _lp:=0;
{? GR_KOL.first()
|| {! |?
      TT_TREE.blank();
      TT_TREE.TREE:=_tree;
      TT_TREE.LP:=_lp; _lp+=1;
      TT_TREE.LABEL:='['+$GR_KOL.LP+'] '+GR_KOL.NAZWA;
      TT_TREE.POZIOM:=1;
      TT_TREE.REF:=#GR_KOL.ref();
      TT_TREE.KOL:=#GR_KOL.ref();
      TT_TREE.DANE:=TT_TREE.ROZ:=0;
      TT_TREE.add();
      GR_KOL.next()
   !}
?}


\kolor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Kolorowanie rekordow prezentowanych hierarchicznie wartosci sprawozdan
::  OLD: \kolor/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? TT_TREE.POZIOM=0 || 'TT_TREE#01#01' |? TT_TREE.POZIOM=1 || 'TT_TREE#01#02' || '' ?}


\bv_ppom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Przed wyswietl zmiennej POMOC.P1
::  OLD: \bv_ppom/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.WARIANT>99
|| return(exec('kolor2','!fks_ksp_zspr',1))
?};
_krs:=UNPAR.P25;
{? dalej || return(0) ?};
{? SSTALE.WARIANT<>4 & TT_TREE.POZIOM>1
|| {? SSTALE.WARIANT=1 & (WARTOSCI.index('OKR_OST'); WARTOSCI.prefix(REF.WYKON,TT_TREE.REF,TT_TREE.KOL,OPERATOR.DEPT); WARTOSCI.first() )
   || _ost:=0;
      _fun:={? TT_TREE.CZY_WAL || '(_a/SSTALE.ZAOKR)$SSTALE.PREC' || '_a$2' ?};
      {! _i:=start .. stop |!
         _wart:={? WARTOSCI.find_key(okr[_i+delta],TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || WARTOSCI.WARTOSC/_krs || 0 ?};
         ($( 'POMOC.P'+$(_i-start+1)+':='+_fun ))(_wart+_ost);
         {? SSTALE.INC || _ost+=_wart ?}
      !};
      {? SSTALE.WARIANT=1 & SSTALE.TYP<>'M' & SSTALE.TYP<>'R' & ~SSTALE.INC
      || ($( 'POMOC.P'+$(stop-start+2)+':='+_fun ))({? WARTOSCI.find_key(okr[rok_poz],TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || WARTOSCI.WARTOSC/_krs || 0 ?})
      ?}
   |? SSTALE.WARIANT=2
   || WARTOSCI.use('wsik_'+SSTALE.ROK2().KOD);
      WARTOSCI.index('OKR_OST'); WARTOSCI.prefix(REF.WYKON,TT_TREE.REF,TT_TREE.KOL,OPERATOR.DEPT);
      OSPR.index('OKR_ROK'); OSPR.prefix(REF.FIRMA,SSTALE.OKRES,SSTALE.ROK2);
      _ospr:={? OSPR.first() || OSPR.ref || null ?};
      {? WARTOSCI.first()
      || {? SSTALE.INC
         || _wart:=0;
            OSPR.index('OKRES2'); OSPR.prefix(REF.FIRMA,1+SSTALE.OKRES().KOD,SSTALE.ROK2);
            {? OSPR.first()
            || {! |?
                  {? OSPR.TYP<>'M' | (OSPR.OKRES_OD().POCZ<>date(0,0,0) & OSPR.OKRES_DO().POCZ<>date(0,0,0))
                  || _wart+={? WARTOSCI.find_key(OSPR.ref(),TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || WARTOSCI.WARTOSC/_krs || 0 ?}
                  ?};
                  OSPR.ref<>_ospr & OSPR.next()
               !}
            ?}
         || _wart:={? WARTOSCI.find_key(_ospr,TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || WARTOSCI.WARTOSC/_krs || 0 ?}
         ?}
      || _wart:=0
      ?};
      POMOC.P1:={? TT_TREE.CZY_WAL || (_wart/SSTALE.ZAOKR)$SSTALE.PREC || _wart$2 ?};
      _fun:={? TT_TREE.CZY_WAL || '(_a/SSTALE.ZAOKR)$SSTALE.PREC' || '_a$2' ?};
      OSPR.prefix(REF.FIRMA);
      {! _i:=1..liczba |!
         {? OSPR.seek(okr[_i],OSPR.name())
         || WARTOSCI.use('wsik_'+OSPR.OKRES_OD().ROK().KOD);
            WARTOSCI.index('OKR_OST'); WARTOSCI.prefix(REF.WYKON,TT_TREE.REF,TT_TREE.KOL,OPERATOR.DEPT);
            {? WARTOSCI.first()
            || {? SSTALE.INC
               || _wart:=0;
                  _ospr:=OSPR.ref();
                  OSPR.cntx_psh();
                  OSPR.index('OKRES2'); OSPR.prefix(REF.FIRMA,1+SSTALE.OKRES().KOD,ROK_F.ref());
                  {? OSPR.first()
                  || {! |?
                        {? OSPR.TYP<>'M' | (OSPR.OKRES_OD().POCZ<>date(0,0,0) & OSPR.OKRES_DO().POCZ<>date(0,0,0))
                        || _wart+={? WARTOSCI.find_key(OSPR.ref(),TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || WARTOSCI.WARTOSC/_krs || 0 ?}
                        ?};
                        OSPR.ref<>_ospr & OSPR.next()
                     !}
                  ?};
                  OSPR.cntx_pop()
               || _wart:={? WARTOSCI.find_key(OSPR.ref(),TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || WARTOSCI.WARTOSC/_krs || 0 ?}
               ?}
            || _wart:=0
            ?};
            ($( 'POMOC.P'+$(_i+1)+':='+_fun ))(_wart);
            _r:=($('POMOC.P'+$(_i+1)+'-POMOC.P'+$_i))();
            ($( 'POMOC.P'+$(_i+1+2*liczba)+':=_a$SSTALE.PREC' ))(_r);
            ($( 'POMOC.P'+$(_i+1+liczba)+':=_a$SSTALE.PREC' ))({? ($('POMOC.P'+$_i))()=0 || {? ($( 'POMOC.P'+$(_i+1)))()=0 || 0 || {? _r<0 || -9.999999999999999e16383 || 9.999999999999999e16383 ?} ?} || _r/($('|POMOC.P'+$_i))()*100 ?})
         || ($('POMOC.P'+$(_i+1)+':=0'))()
         ?}
      !}
   |? SSTALE.WARIANT=3
   || _chg_m:=SSTALE.ROK<>SSTALE.ROK2;
      {? _chg_m || WARTOSCI.use('wsik_'+SSTALE.ROK().KOD) ?};
      WARTOSCI.index('OKR_OST'); WARTOSCI.prefix(REF.WYKON,TT_TREE.REF,TT_TREE.KOL,OPERATOR.DEPT);
      {? WARTOSCI.first()
      || _wart:={? WARTOSCI.find_key(SSTALE.OSPR,TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || WARTOSCI.WARTOSC/_krs || 0 ?}
      || _wart:=0
      ?};
      POMOC.P1:={? TT_TREE.CZY_WAL || (_wart/SSTALE.ZAOKR)$SSTALE.PREC || _wart$2 ?};
      {? _chg_m || WARTOSCI.use('wsik_'+SSTALE.ROK2().KOD) ?};
      WARTOSCI.index('OKR_OST'); WARTOSCI.prefix(REF.WYKON,TT_TREE.REF,TT_TREE.KOL,OPERATOR.DEPT);
      {? WARTOSCI.first()
      || _wart:={? WARTOSCI.find_key(SSTALE.OSPR2,TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || WARTOSCI.WARTOSC/_krs || 0 ?}
      || _wart:=0
      ?};
      POMOC.P2:={? TT_TREE.CZY_WAL || (_wart/SSTALE.ZAOKR)$SSTALE.PREC || _wart$2 ?};
      POMOC.P4:=POMOC.P2-POMOC.P1;
      POMOC.P3:={? POMOC.P1=0 || {? POMOC.P2=0 || 0 || {? POMOC.P4<0 || -9.999999999999999e16383 || 9.999999999999999e16383 ?} ?} || POMOC.P4/|POMOC.P1*100 ?}$SSTALE.PREC
   |? SSTALE.WARIANT=5
   || {! _i:=1..tt_max
      |! WARTOSCI.use('wsik_'+tt_okr[_i+tt_mov[1]][6]);
         WARTOSCI.index('OKR_OST');
         WARTOSCI.prefix(REF.WYKON,TT_TREE.REF,TT_TREE.KOL,OPERATOR.DEPT);
         {? WARTOSCI.find_key(tt_okr[_i+tt_mov[1]][1],TT_TREE.KOD,{?TT_TREE.KOD='' || 'N' || 'T' ?})
         || ($('POMOC.P'+$_i+':=_a'))( (WARTOSCI.WARTOSC/_krs)/{? TT_TREE.CZY_WAL || SSTALE.ZAOKR || 1 ?} )
         || ($('POMOC.P'+$_i+':=_a'))(0)
         ?}
      !}
   |? SSTALE.WARIANT=6
   || WARTOSCI.index('OKR_OST');
      {! _i:=1..liczba-1
      |! WARTOSCI.prefix(okr[_i],TT_TREE.REF,TT_TREE.KOL,OPERATOR.DEPT,SSTALE.OSPR);
         {? WARTOSCI.find_key(TT_TREE.KOD,{? TT_TREE.KOD='' || 'N' || 'T' ?})
         || ($('POMOC.P'+$_i+':=_a'))( (WARTOSCI.WARTOSC/_krs)/{? TT_TREE.CZY_WAL || SSTALE.ZAOKR || 1 ?} )
         || ($('POMOC.P'+$_i+':=_a'))(0)
         ?}
      !};
      POMOC.P3:=POMOC.P1-POMOC.P2
   || {! _i:=start .. stop+(SSTALE.WARIANT=1 & SSTALE.TYP<>'M' & SSTALE.TYP<>'R' & ~SSTALE.INC) |!
         ($( 'POMOC.P'+$(_i-start+1)+':=_a' ))(0)
      !}
   ?}
|? SSTALE.WARIANT=4 & TT_TREE.POZIOM
|| WARTOSCI.index('OKRES'); WARTOSCI.prefix(SSTALE.OSPR,REF.WYKON,TT_TREE.REF);
   {? TT_TREE.RKOL
   || {! _i:=start .. stop |!
         ($( 'POMOC.P'+$(_i-start+1)+':=_a' ))(0)
      !};
      _fun:={? TT_TREE.CZY_WAL || '(_a/SSTALE.ZAOKR)$SSTALE.PREC' || '_a$2' ?};
      {? TT_TREE.RKOL>=start & TT_TREE.RKOL<=stop
      || {? SSTALE.INC
         || OSPR.index('OKRES3'); OSPR.prefix(REF.FIRMA,SSTALE.ROK);
            _typ:=SSTALE.OSPR().TYP;
            {? OSPR.first()
            || WARTOSCI.index('OKR_OST'); WARTOSCI.prefix(REF.WYKON,TT_TREE.REF,($('TT_TREE.KOL'+$TT_TREE.RKOL))(),OPERATOR.DEPT);
               _wart:=0;
               {! |?
                  {? OSPR.TYP=_typ & WARTOSCI.find_key(OSPR.ref(),TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || _wart+=WARTOSCI.WARTOSC/_krs ?};
                  OSPR.ref()<>SSTALE.OSPR & OSPR.next()
               !}
            ?}
         || _wart:={? TT_TREE.KOD=''
                   || {? WARTOSCI.find_key( ($('TT_TREE.KOL'+$TT_TREE.RKOL))(),OPERATOR.DEPT,'N') || WARTOSCI.WARTOSC/_krs || 0 ?}
                   || {? WARTOSCI.find_key( ($('TT_TREE.KOL'+$TT_TREE.RKOL))(),OPERATOR.DEPT,'T',TT_TREE.KOD) || WARTOSCI.WARTOSC/_krs || 0 ?}
                   ?}
         ?};
         ($( 'POMOC.P'+$(TT_TREE.RKOL-start+1)+':='+_fun ))(_wart)
      ?}
   || _typ:=SSTALE.OSPR().TYP;
      _fun:={? TT_TREE.CZY_WAL || '(_a/SSTALE.ZAOKR)$SSTALE.PREC' || '_a$2' ?};
      {! _i:=start .. stop |!
         {? SSTALE.INC
         || OSPR.index('OKRES3'); OSPR.prefix(REF.FIRMA,SSTALE.ROK);
            {? OSPR.first()
            || WARTOSCI.index('OKR_OST'); WARTOSCI.prefix(REF.WYKON,TT_TREE.REF,($('TT_TREE.KOL'+$_i))(),OPERATOR.DEPT);
               _wart:=0;
               {! |?
                  {? OSPR.TYP=_typ & WARTOSCI.find_key(OSPR.ref(),TT_TREE.KOD,{? TT_TREE.KOD<>'' || 'T' || '' ?}) || _wart+=WARTOSCI.WARTOSC/_krs ?};
                  OSPR.ref()<>SSTALE.OSPR & OSPR.next()
               !}
            ?}
         || _wart:={? TT_TREE.KOD=''
                   || {? WARTOSCI.find_key( ($('TT_TREE.KOL'+$_i))(),OPERATOR.DEPT,'N') || WARTOSCI.WARTOSC/_krs || 0 ?}
                   || {? WARTOSCI.find_key( ($('TT_TREE.KOL'+$_i))(),OPERATOR.DEPT,'T',TT_TREE.KOD) || WARTOSCI.WARTOSC/_krs || 0 ?}
                   ?}
         ?};
         ($( 'POMOC.P'+$(_i-start+1)+':='+_fun ))(_wart)
      !}
   ?}
|? SSTALE.WARIANT=5
|| {! _i:=1 .. tt_max
   |! ($( 'POMOC.P'+$_i+':=_a' ))(0)
   !}
|| {! _i:=start .. stop+(SSTALE.WARIANT=1 & SSTALE.TYP<>'M' & SSTALE.TYP<>'R' & ~SSTALE.INC)
   |! ($( 'POMOC.P'+$(_i-start+1)+':=_a' ))(0)
   !}
?};
kolor:=exec('rekprzed','color','TT_TREE#01')


\kolor2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Kolorowanie pol POMOC.P1 .. POMOC.P12
::   WE: _a - nr pola
::  OLD: \kolor/plan_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.WARIANT=100
|| {? dalej || return() |? kolor[_a]='' || '' || exec('rekprzed','color',kolor[_a]) ?}
|? SSTALE.WARIANT=101
|| {? dalej || return || kolor[_a] ?}
|| kolor
?}


\language
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Tlumaczy nazwy wystepujace w pierwszym polu okna
::  OLD: \language/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
UNPAR.P11:={? SKID.LANG_SLO=null | SKID.LANG_SLO().KOD='POL' | TT_TREE.POZIOM<0
           || TT_TREE.LABEL
           |? TT_TREE.POZIOM=0
           || GR_SIK.prefix(); GR_SIK.seek(TT_TREE.REF,);
              '['+GR_SIK.SKROT+'] '+exec('get_lang','lang',GR_SIK.ref())
           |? TT_TREE.POZIOM=1
           || GR_KOL.prefix(); GR_KOL.seek(TT_TREE.KOL,);
              '['+$GR_KOL.LP+'] '+exec('get_lang','lang',GR_KOL.ref())
           |? TT_TREE.KOD<>''
           || TT_TREE.LABEL
           || {? SSTALE.WARIANT=101 & UNPAR.P16='O'
              || OSPR.prefix(REF.FIRMA); OSPR.seek(TT_TREE.REF,);
                 exec('get_lang','lang',OSPR.ref())
              || DEFW.prefix(); DEFW.seek(TT_TREE.REF,);
                 '['+DEFW.KOD+'] '+exec('get_lang','lang',DEFW.ref())
              ?}
           ?}


\bl_wvtyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Wartosc poczatkowa pola WER_VIEW.TYP
::  OLD: \bl_wvtyp/plan_spr.fml
::----------------------------------------------------------------------------------------------------------------------
__WV_TYP


\pw_pwarp
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Kolorowanie pozycji o kodzie 'PARAM#WAR_PAR#01'
::  OLD: \pw_pwarp/def_wsk.fml
::----------------------------------------------------------------------------------------------------------------------

{? exec('war_alg','!fks_ksp_zspr',DEFW.DOKRES)
|| ''
|| 'PARAM#WAR_PAR#01'
?}


\war_alg
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Formula do kolorowania o kodzie 'PARAM#WAR_PAR#01'
::   WE: _a - jakiego okresu dotycza aktualizowane dane
::  OLD: \war_alg/def_wsk.fml
::----------------------------------------------------------------------------------------------------------------------

{? var_pres('ks2alg_par')>0 || return(0) ?};
PARAM.WAR_PAR:=0;
_ospr:=OKRESY.OKR_RAP;
OKRESY.OKR_RAP:=exec('ospr_fg','konsolidacja',OKRESY.OKR_RAP,'F',ALG_PAR.FIRMA);
W_ALGPAR.cntx_psh();
W_ALGPAR.use('walg_'+OKRESY.OKR_RAP().ROK().KOD);
W_ALGPAR.index('W_ALGPAR');
_typ:=OKRESY.OKR_RAP().TYP;
_mno:={? var_press('AlgMno')>0 || AlgMno |? ALG_PAR.WYL='T' || -1 || 1 ?};
{? _typ='M'
|| {? REF.FIRMAWYL
   || W_ALGPAR.prefix(ALG_PAR.ref,OKRESY.OKR_RAP,OPERATOR.DEPT,REF.FIRMAWYL)
   || W_ALGPAR.prefix(ALG_PAR.ref,OKRESY.OKR_RAP,OPERATOR.DEPT)
   ?};
   {? W_ALGPAR.first() || PARAM.WAR_PAR:=_mno*W_ALGPAR.WAR ?}
|| OSPR.index('OKRES2'); OSPR.prefix(ALG_PAR.FIRMA,'M',OKRESY.OKR_RAP().ROK,OKRESY.OKR_RAP().OKRES_OD().NR,OKRO_F.NR);
   {? OSPR.first || _b:=OSPR.ref() ?};
   OSPR.prefix(ALG_PAR.FIRMA,'M',OKRESY.OKR_RAP().ROK,OKRESY.OKR_RAP().OKRES_DO().NR,OKRO_F.NR);
   {? OSPR.first || _c:=OSPR.ref() ?};
   {? _b & _c
   || {? _a='K'
      || W_ALGPAR.prefix(ALG_PAR.ref,_c,OPERATOR.DEPT);
         {? W_ALGPAR.first() || PARAM.WAR_PAR:=_mno*W_ALGPAR.WAR ?}
      |? _a='P'
      || W_ALGPAR.prefix(ALG_PAR.ref,_b,OPERATOR.DEPT);
         {? W_ALGPAR.first() || PARAM.WAR_PAR:=_mno*W_ALGPAR.WAR ?}
      |? _a='Z'
      || W_ALGPAR.prefix(ALG_PAR.ref,_b,OPERATOR.DEPT);
         {? W_ALGPAR.first() || PARAM.WAR_PAR:=_mno*W_ALGPAR.WAR ?};
         W_ALGPAR.prefix(ALG_PAR.ref,_c,OPERATOR.DEPT);
         {? W_ALGPAR.first() || PARAM.WAR_PAR-=_mno*W_ALGPAR.WAR ?}
      |? _a='S'
      || OSPR.index('OKRES2'); OSPR.prefix(ALG_PAR.FIRMA,'M',OKRESY.OKR_RAP().ROK);
         _do:=OKRESY.OKR_RAP().OKRES_DO().NR;
         {? OSPR.find_key(OKRESY.OKR_RAP().OKRES_OD().NR)
         || {!
            |? W_ALGPAR.prefix(ALG_PAR.ref,OSPR.ref,OPERATOR.DEPT);
               {? W_ALGPAR.first() || PARAM.WAR_PAR+=_mno*W_ALGPAR.WAR ?};
               OSPR.OKRES_DO().NR<_do & OSPR.next()
            !}
         ?}
      ?}
   ?}
?};
W_ALGPAR.cntx_pop();
OKRESY.OKR_RAP:=_ospr;
{? cur_win='PAR_PLAN'
|| PARAM.WAR_PAR
|| PARAM.WAR_PAR<>0
?}


\alg_zap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Zapisy dokumentow dla algorytmow wierszy typu F
::  OLD: \alg_zap/plan_spr.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1; _bz:=1;
_typ:=ALG_PAR.FORMULA().SKROT;
_str:={? 5+_typ='OBROT' || ALG_PAR.P2 || '' ?};
OKRESY.OKR_RAP().ROK();
OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
{? _typ='PERSALDO' | _typ='OBROTY' | _typ='OBROTY1' | _typ='OBROTY_B' | _typ='ROZNICA' | _typ='ROZNICA1' | _typ='SALDO1'
|| {? OKRO_F.first()
   || _fpoz:=_fdok:='';
      {!
      |? {? _typ<>'OBROTY_B' | OKRO_F.NAZ<>'Bilans otwarcia'
         || _fpoz+='\'pozy'+ ROK_F.KOD+form(OKRO_F.NR,-2)+'\',';
            _fdok+='\'doku'+ ROK_F.KOD+form(OKRO_F.NR,-2)+'\','
         ?};
         OKRESY.OKR_RAP().OKRES_DO<>OKRO_F.ref() & OKRO_F.next()
      !}
   ?};
   {? _typ='OBROTY1' | _typ='ROZNICA1' | _typ='SALDO1' || _bz:=0 ?}
|? _typ='BO' | _typ='BO2'
|| {? OKRO_F.first()
   || _fpoz:='\'pozy'+ ROK_F.KOD+form(OKRO_F.NR,-2)+'\'';
      _fdok:='\'doku'+ ROK_F.KOD+form(OKRO_F.NR,-2)+'\''
   ?}
|? _typ='OBROTY_O' | _typ='OBROT_O1' | _typ='ROZNICAO' | _typ='ROZNICO1' | _typ='SALDO2'
|| {? OKRO_F.first()
   || OKRESY.OKR_RAP().OKRES_OD();
      _fpoz:=_fdok:='';
      {!
      |? _fpoz+='\'pozy'+ ROK_F.KOD+form(OKRO_F.NR,-2)+'\',';
         _fdok+='\'doku'+ ROK_F.KOD+form(OKRO_F.NR,-2)+'\',';
         OKRO_F.ref<>OSPR.OKRES_DO & OKRO_F.next()
      !}
   ?};
   {? _typ='OBROT_O1' | _typ='ROZNICO1' || _bz:=0 ?}
|| _ok:=0;
   FUN.info('Akcja nie obsługuje funkcji %1.'@[_typ])
?};
{? _ok
|| {? _fpoz+1=',' || _fpoz:=_fpoz-1 ?};
   {? _fdok+1=',' || _fdok:=_fdok-1 ?};
   _maska:=35+exec('str_sql','#sql',ALG_PAR.P1);
   _odd:={? ALG_PAR.P3=''
         || {? var_press('ODD_PLAN')>0 & ODD_PLAN
            || ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA); ODD.seek(ODD_PLAN);
               _w:=ODD.OD;
               ODD.cntx_pop();
               _w
            || '%'
            ?}
         || ALG_PAR.P3
         ?};
   VAR_DEL.delete('TABTMP','ind');
   _wal:={? ALG_PAR.P4=FINFO.NAROD().KOD || '' || ALG_PAR.P4 ?};
   exec('dekrety','dok_fks',_maska,_odd,date(0,0,0),date(0,0,0),'T',_fpoz,_fdok,_wal,_str);
   ind:=TABTMP.index('?');
   TABTMP.index(ind);
   {? _bz=0
   || TABTMP.cntx_psh();
      _ind:=TABTMP.ndx_tmp(,,'REJESTR',,);
      TABTMP.index(_ind); TABTMP.prefix('~BZ');
      {? TABTMP.first() || {! |? TABTMP.del() !} ?};
      TABTMP.cntx_pop()
   ?};
   _tytul:='Zapisy dla funkcji \'%1\' %2 %3 %4 %5 '@[_typ,ALG_PAR.P1,ALG_PAR.P2,ALG_PAR.P3,ALG_PAR.P4];
   _wer:=TABTMP.mk_sel(_tytul,,,'tabtmp_wer',,,,,'U');
   TABTMP.win_fld(_wer,,'KONTO',,,20,,,'Konto'@);
   TABTMP.win_fld(_wer,,'TR',,,10,,,'Opis'@);
   TABTMP.win_fld(_wer,,'D_ZAPKS',,,10,,,'Data zapisu'@);
   TABTMP.win_fld(_wer,,'ODDZIAL',,,8,,,'Jednostka księgowa'@);
   TABTMP.win_fld(_wer,,'REJESTR',,,8,,,'Rejestr'@);
   TABTMP.win_fld(_wer,,'NR_W_REJ',,,5,,,'Numer'@);
   TABTMP.win_fld(_wer,,'POZYCJA',,,5,,,'Pozycja'@);
   TABTMP.win_fld(_wer,,'WN',,,15,,,'             Wn'@);
   TABTMP.win_fld(_wer,,'MA',,,15,,,'             Ma'@);
   TABTMP.win_act(_wer,,'Formuła','Podsumowanie'@@,,'Podsumowanie wyświetlanych pozycji'@,,
                  "exec('suma','dok_fks')",1,,,,'P');
   TABTMP.win_act(_wer,,'Formuła','Oblicz obroty'@@,,'Obroty dla maski kont lub dla wybranych pozycji'
                  ,,"exec('suma_tmp','dok_fks')",,1
                  ,"exec('pr_obl','dok_fks')","exec('wys_tobr','dok_fks');&JAK",'O');
   TABTMP.win_act(_wer,,'Formuła','Nagłówek dokumentu'@@,,'Nagłówek dokumentu źródłowego'@,
                  "exec('naglowek','dok_fks',4-(8+TABTMP.POZ_REF),BB.sqlint(TABTMP.POZ_REF))",,,,,,'N');
   TABTMP.win_act(_wer,,'Formuła','Zapisy dokumentu'@@,,'Wyświetlenie zapisów dokumentu źródłowego'@,
                  "exec('zapisy','dok_fks',4-(8+TABTMP.POZ_REF),BB.sqlint(TABTMP.POZ_REF), SSTALE.WAL<>FINFO.NAROD)",,,,,,'Z');
     _menu:='Dane źródłowe'@;
   TABTMP.win_act(_wer,,'Menu',_menu,,'');
   TABTMP.win_act(_wer,0,'Formuła','Dokumenty z innej dziedziny'@@,_menu,'Przeglądanie dokumentu źródłowego'@,"
                                   DOK.cntx_psh();POZ.cntx_psh();
                                   DOK.use('doku'+(4-(8+TABTMP.POZ_REF)));
                                   POZ.use(8+TABTMP.POZ_REF);
                                   DOK.prefix();POZ.prefix();
                                   {? POZ.seek(BB.sqlint(TABTMP.POZ_REF),(8+TABTMP.POZ_REF))
                                   ||  {? POZ.DOK().get() || exec('dok_zrd','dok_fks') ?}
                                   ?};
                                   DOK.cntx_pop();POZ.cntx_pop()",,,,,,'D');
   TABTMP.win_act(_wer,0,'Formuła','E-dokument'@@,_menu,'Wyświetlanie E-dokumentu',"exec('wyswietl_edok','dok_fks',4-(8+TABTMP.POZ_REF),BB.sqlint(TABTMP.POZ_REF))",,,,,,'E');
   TABTMP.win_act(_wer,0,'Formuła','Załączniki'@@,_menu,'Wyświetlanie załączników'," exec('dok_zal','dokum_zal',4-(8+TABTMP.POZ_REF),BB.sqlint(TABTMP.POZ_REF))",,,,,,'Z');
   TABTMP.win_act(_wer,,'Rekord',,,,
                  " DOK.cntx_psh();POZ.cntx_psh();
                    DOK.use('doku'+(4-(8+TABTMP.POZ_REF)));
                    POZ.use(8+TABTMP.POZ_REF);
                    DOK.prefix();POZ.prefix();
                     _hid_d:=''; _hid:='';
                    {? POZ.seek(BB.sqlint(TABTMP.POZ_REF),(8+TABTMP.POZ_REF))
                    ||  {? POZ.DOK().get()
                        ||  {? PAR_SKID.get(82)='N'
                             ||     DOKUM.index('DOKUM');
                                     DOKUM.prefix(REF.FIRMA,$DOK.ref);
                                     {? DOKUM.first()
                                     ||  ''
                                     || _hid_d+='Z'
                                     ?}
                               ?};'załącznik';
                               '!+Ç`i+!!)$0ûŻKW2';
                              {? DOK.DOKZRODL='' | DOK.REJ().KOD='~BO' || _hid_d+='D' ?};'dokument źródłowy';
                              {? ~DOK.E_DOC || _hid_d+='E' ?};'skan ';
                              {? _hid_d<>'' || _hid:='D('+_hid_d+')' || _hid:='' ?}
                         ?}
                     ?};
                     DOK.cntx_pop();POZ.cntx_pop();
                    TABTMP.actions_grayed(TABTMP.win_sel('?'),_hid);
                    {? ~(_hid*'E')
                    || exec('findtmp','#color')
                    || ''
                    ?}
                  ");
   TABTMP.win_act(_wer,0,'Formuła','Legenda'@@,,'Opis znaczenia ikon i kolorowania'@,
                  "exec('legenda','color','$Pozycje dokumentów z podpiętym E-dokumentem')",,,,,,'L');
   TABTMP.win_sel(_wer);
   TABTMP.select();
   VAR_DEL.delete('TABTMP','ind')
?}


\bv_sik_wyl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Przed wyświetleniem pola SIK.WYL
::----------------------------------------------------------------------------------------------------------------------
_enabled:=SIK.WYL='W';
cur_tab.efld_opt(cur_tab.win_edit('?'),'enable='+$_enabled,REF,'FIRMAWYL');
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 99127d9d47d91f638621faa0ca97943891b6885b765d29bd8ecf6b72a3d8474a03d125c3fcd15b9e1b15d4bc97f3d5b9b7943f45851139a0c178caad4e22c2d1ed27a5d07db40568972afe205371907704e8d31a9d1fd4f2e7d6f4e755b4032e7a476d0cd4fff188f0dc92fb716d0bbfb7b64c3472954f47b79c4b0457b0f31e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_upr_rufz.fml
:: Utworzony: 05.08.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_UPR_RUFZ - Upraw. do form współpracy
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Upraw. do form współpracy - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::
USERS.cntx_psh();

USERS.win_sel(exec('mk_wnd_grp','!zws_upr_rufz'));
USERS.select();

USERS.cntx_pop();
~~


\users_fz_zmien_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zmienia uprawnienia na odwrotne.
::----------------------------------------------------------------------------------------------------------------------
USERS_FZ.DOSTEP:={? USERS_FZ.DOSTEP='T' || 'N' || 'T' ?};
~~


\users_fz_nadaj_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Nadaje uprawnienie.
::----------------------------------------------------------------------------------------------------------------------
USERS_FZ.DOSTEP:='T';
1


\users_fz_odbierz_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wycofuje uprawnienie.
::----------------------------------------------------------------------------------------------------------------------
USERS_FZ.DOSTEP:='N';
1


\users_fz_zmien_aa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zapisuje zmianę uprawnień.
::----------------------------------------------------------------------------------------------------------------------
{? USERS_FZ.put()
|| {? USERS_FZ.sel_size()>1
   || return()
   ?};

   _query:=
      'select U.DOSTEP, count(*) as TEST '
      'from USERS_FZ as U '
      'where U.FIRMA=:_a and U.USERS=:_b '
      'group by U.DOSTEP';
   _rs:=sql(_query,USERS_FZ.FIRMA,USERS_FZ.USERS);
   _rs.index(_rs.ndx_tmp(,,'DOSTEP',,));

   _access:=0;
   {? _rs.find_key('T')
   || {? _rs.size()=1
      || _access:=2
      || _access:=1
      ?}
   ?};

   Perm.setPermission('F_ZATR',USERS_FZ.FIRMA,USERS_FZ.USERS,_access)
?}


\mk_wnd_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy panel dla przypadku, gdy firmy grupy są obsługiwane centralnie.
::   WY: akronim okna
::----------------------------------------------------------------------------------------------------------------------
_wnd:=USERS.grp_make(
:  tytuł
   'Uprawnienia',
:  przed wyświetleniem
   "exec('adm_bf','!zws_upr_rufz','zws_upr_rufz')",
:  identyfikator
   'zws_upr_rufz',
:  położenie
   ,,
:  przy zamknięciu
   "exec('adm_oc','!zws_upr_rufz')"
);

: obszar przeglądania użytkowników
USERS.grp_sel(_wnd,,'NAW','Wg użytkowników',
:  po odświeżeniu
   "exec('adm_users_ar','!zws_upr_rufz')",
:  położenie wysokość
   ,,10,
:  przed obsługą
   "exec('adm_users_bs','!zws_upr_rufz',_a)",
:  po obsłudze
   "",
:  ustalenie, aktywność, tryb
   ,,'maximized_with_title',
:  identyfikator
   'USERS_NAW',
:  główne
   1
);

: obszar przeglądania uprawnień
USERS.tab_splt(_wnd,,'horizontal','perms1');
USERS.grp_sel(_wnd,USERS_FZ,'F_ZATR',,
:  po odświeżeniu
   "",
:  położenie wysokość
   ,,,
:  przed obsługą
   "exec('adm_users_upr_bs','!zws_upr_rufz',_a)",
:  po obsłudze
   "exec('adm_users_upr_as','!zws_upr_rufz',_a)",
:  ustalenie, aktywność, tryb
   ,,'maximized_with_title',
);

: obszar przeglądania form współpracy
USERS.grp_sel(_wnd,F_ZATR,'NAW','Wg form współpracy',
:  po odświeżeniu
   "exec('adm_forms_ar','!zws_upr_rufz')",
:  położenie wysokość
   ,,10,
:  przed obsługą
   "exec('adm_forms_bs','!zws_upr_rufz',_a)",
:  po obsłudze
   "",
:  ustalenie, aktywność, tryb
   ,,'maximized_with_title',
:  identyfikator
   'FORMS_NAW'
);

: obszar przeglądania uprawnień
USERS.tab_splt(_wnd,,'horizontal','perms2');
USERS.grp_sel(_wnd,USERS_FZ,'USERS',,
:  po odświeżeniu
   "",
:  położenie wysokość
   ,,,
:  przed obsługą
   "exec('adm_forms_upr_bs','!zws_upr_rufz',_a)",
:  po obsłudze
   "exec('adm_forms_upr_as','!zws_upr_rufz',_a)",
:  ustalenie, aktywność, tryb
   ,,'maximized_with_title',
);

_wnd


\adm_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wypełnieniem panelu zarządzania.
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh();
USERSF.cntx_psh();
USERS_FZ.cntx_psh();
F_ZATR.cntx_psh();

__adm_cntx:=obj_new('win_id','users_ndx','forms_ndx');
__adm_cntx.win_id:='USERS_NAW';
__adm_cntx.users_ndx:='W_ZATR';
__adm_cntx.forms_ndx:='W_USR';

P_FILTER.FIRMA:=exec('ref_firma','ustawienia');

F_ZATR.index('KOD');

1


\adm_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przy zamknięciu panelu zarządzania.
::----------------------------------------------------------------------------------------------------------------------
obj_del(__adm_cntx);

F_ZATR.cntx_pop();
USERS_FZ.cntx_pop();
USERSF.cntx_pop();
USERS.cntx_pop();

1


\adm_firma_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna FIRMA zmiennej P_FILTER.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| edit_start();
   win_activate(__adm_cntx.win_id)
?}


\adm_users_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okna NAW tabeli USERS.
::----------------------------------------------------------------------------------------------------------------------
grp_disp(USERS_FZ,'F_ZATR',1,1)


\adm_users_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna NAW tabeli USERS.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
__adm_cntx.win_id:='USERS_NAW';

USERS.clear();
{? P_FILTER.FIRMA<>null
|| USERS.f_set(
      'KOD',,
      'USERS.REFERENCE in ('+
      '  select distinct U.USERS'+
      '  from B_USRROL as U '+
      '  where U.FIRMA=:_a'+
      ')',
      P_FILTER.FIRMA
   )
|| USERS.f_clear()
?}


\adm_users_upr_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna F_ZATR tabeli USERS_FZ.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
_size:=~exec('dom_empty','#table',USERS);

{? _size
|| exec('users_upr','f_zatr',P_FILTER.FIRMA,USERS.ref())
?};

USERS_FZ.index(__adm_cntx.users_ndx);
USERS_FZ.prefix(P_FILTER.FIRMA,{? _size || USERS.ref() || null ?})


\adm_users_upr_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okna F_ZATR tabeli USERS_FZ.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| __adm_cntx.users_ndx:=USERS_FZ.index('?')
?}


\adm_forms_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okna NAW tabeli F_ZATR.
::----------------------------------------------------------------------------------------------------------------------
grp_disp(USERS_FZ,'USERS',1,1)


\adm_forms_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna NAW tabeli F_ZATR.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
__adm_cntx.win_id:='FORMS_NAW'


\adm_forms_upr_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna USERS tabeli USERS_FZ.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
USERS_FZ.index(__adm_cntx.forms_ndx);
USERS_FZ.prefix(P_FILTER.FIRMA,F_ZATR.ref())


\adm_forms_upr_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okna USERS tabeli USERS_FZ.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| __adm_cntx.forms_ndx:=USERS_FZ.index('?')
?}


\dsk_set_callback
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiada za ustawienie wartości odpowiednich
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('set_value','#desktop','','selektor','firmaCB@panel',$P_FILTER.FIRMA)


\dsk_get_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca dane dla selektora firmy.
::   WY: alias do tabeli tymczasowej zawierającej listę firm.
::----------------------------------------------------------------------------------------------------------------------
sql('
   select
      F.REFERENCE as REF,
      F.SYMBOL as SYMBOL,
      F.OPIS as OPIS
   from
      FIRMA as F
   order by
      SYMBOL
')


\dsk_callback
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Reakcja na wybór firmy w selektorze.
::----------------------------------------------------------------------------------------------------------------------
FIRMA.prefix();
{? FIRMA.seek(exec('get_value','#desktop','','selektor','firmaCB@panel'))
|| P_FILTER.FIRMA:=FIRMA.ref()
?};
win_activate(__adm_cntx.win_id)

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:03 7ad892a9ae6715206395955888de08f4191966e2734f9bc53b02f6ec1fff0e70330fc2b55328ce8ab6ebf8a67b002bd63507a08c05cbe922f5d21791d3a713c016296544e51fbdb6ee83a5c6424000faf2b561aaf11298fae671f71ee0b13aff0283bc71946e837c671e43d5e60b18ce5c26a671e1ecd696adbfe7b093fcb296

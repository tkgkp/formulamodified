:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_apzp.fml
:: Utworzony: 05.12.2019
:: Autor: IS
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_APZP - Pracownicy kończący określony wiek.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Pracownicy kończący określony wiek - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# permissions=F_ZATR,UD_SKL
::
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
::
:: Czynność zbiera informacje o pracownikach, którzy skończą określony wiek w okresie
:: określonym parametrami wejściowymi OD i DO. Dla czynności serwisowej wymagana jest data OD i data DO.
::# kind=WE, symbol=OD, type=DATE, name=Data początkowa badanego okresu, required=N
::# kind=WE, symbol=DO, type=DATE, name=Data końcowa badanego okresu, required=N
::
:: Parametr wejściowy UD_SCH pozwala wskazać symbol schematu (typu PODZORG), który posłuży do ustalenia jednostki
:: organizacyjnej, przekazanej parametrem UD_SKL. Jeżeli parametr nie zostanie przekazany (ustawiony), to wybrany
:: zostanie schemat domyślny lub (jeżeli domyślny nie został określony) pierwszy (w kolejności alfabetycznej).
::# kind=WE, symbol=UD_SCH, type=STRING, name=Schemat organizacyjny, required=N, ^
::#    fml_val="exec('ud_sch_wybierz','schemat',,_a).SYMBOL"
::
:: Parametr wejściowy UD_SKL pozwala wskazać jednostkę organizacyjną, do której będzie ograniczony zakres przetwarzania.
:: Domyślnie przetwarzane będą wszystkie jednostki organizacyjne.
::# kind=WE, symbol=UD_SKL, type=STRING, name=Jednostka organizacyjna, required=N, ^
::#    fml_val="exec('ud_skl_wybierz','schemat',,,1,_a)"
::
:: Parametr wejściowy SQL_FROM umożliwia zbudowanie dodatkowych relacji na potrzeby dodatkowego warunku
:: (patrz SQL_WHERE).
:: Domyślnie przyjmuje wartość pustą.
::# kind=WE, symbol=SQL_FROM, type=STRING, name=Dodatkowe relacje filtra, required=N
::
:: Parametr wejściowy SQL_WHERE umożliwia dodatkowe ograniczenie listy współpracowników. Warunek może odwoływać się
:: do tabeli pracowników (poprzez alias "P" np. "P".ID>1000) oraz
:: do tabeli osób (poprzez alias "1OSOBA" np. "1OSOBA".PLEC='K').
:: Odwołania do innych tabel wymagają odpowiedniego użycia parametru SQL_FROM.
:: Domyślnie SQL_WHERE przyjmuje wartość pustą.
::# kind=WE, symbol=SQL_WHERE, type=STRING, name=Dodatkowy warunek filtra, required=N
::
:: Parametr wejściowy FML_TEST umożliwia dodatkową weryfikację wyboru współpracownika do przetwarzania. Jest treścią
:: formuły w formacie napisu. Jeżeli formuła zwróci wartość 0 to dany współpracownik nie będzie przetwarzany.
:: Formuła jest wykonywana przy ustawionych buforach tabel: P, OSOBA.
:: Domyślnie przyjmuje wartość '1'.
::# kind=WE, symbol=FML_TEST, type=STRING, name=Dodatkowy warunek wyboru, required=N
::
:: Parametr wejściowy WIEK określa wiek, którego ukończenie kwalifikuje do zestawienia
:: Domyślnie przyjmuje wartość 26.
::# kind=WE, symbol=WIEK, type=NUMBER, name=Wiek, required=N
::
:: Parametr wejściowy FZ określa badane formy zatrudnienia. Należy podawać formy zatrudnienia po przecinku np. "P,Z"
:: Domyślnie przyjmuje wartość P,Z.
::# kind=WE, symbol=FZ, type=STRING, name=Formy współpracy, required=N, fml_val="exec('wybierz','f_zatr',_a,1,'T')"
::
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
::
:: Wynik działania czynności. Parametr szczególnie istotny w przypadku uruchomienia czynności usługowej.
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,BŁĄD)", required=N
::
::# kind=WY, symbol=OD, type=DATE, name=Data początkowa badanego okresu, required=N
::# kind=WY, symbol=DO, type=DATE, name=Data końcowa badanego okresu, required=N
::
::# kind=WY, symbol=SUB, type=STRING, name=Temat, required=N
::# kind=WY, symbol=RCV, type=MEMO, name=Lista odbiorców, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
params_set(_par:=params_get());
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;
_service:=_mp.isService();

:: Przypisanie wieku 26 jeśli nie podano ,lub podano błędny:
{? var_press('WIEK',_in)<>type_of(0) | _in.WIEK<1
|| _in.WIEK:=26
?};
:: Przypisanie domyślnych form zatrudnienia jeśli nie podano:
{? var_press('FZ',_in)<>type_of('') | _in.FZ=''
|| _in.FZ:='P,Z'
?};
:: Utworzenie obiektu do przekazania do \input_chk_get/personel_alerty.fml:
_objWiek:=obj_new('AKR','TYPE','NAZ','DESC','WD','PREC');
_objWiek.AKR:='WIEK';
_objWiek.TYPE:='INTEGER';
_objWiek.NAZ:='Wiek';
_objWiek.DESC:='Wiek, którego ukończenie kwalifikuje do zestawienia';
_objWiek.WD:=13;
_objWiek.PREC:=0;
_objW:=obj_new(1);
_objW[1]:=_objWiek;

_out.SUB:='Pracownicy, którzy kończą określony wiek'@;
{? (_result:=exec('input_chk_get','personel_alerty',_service,_in,_out.SUB,_objW,1))=''
|| _out.SUB:='Pracownicy, którzy kończą %1 lat'@ [$_in.WIEK];
   _ret:=exec('run','!pkd_ezk_apzp',_service,_mp.buf_act.UID,_in,_out.SUB);
   _out.RESULT:='OK';
   _out.OD:=_in.OD;
   _out.DO:=_in.DO;
   _out.RCV:=_ret.RCV;
   _out.BODYH:=_ret.BODYH
|| {? ~_service
   || FUN.emsg(_result)
   ?};
   _out.RESULT:='BŁĄD'
?};
_mp.save(,_out);
_mp.done();

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Pracownicy kończący określony wiek - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().mp.load(exec('kind_in','#b_port'));
_d0:=date(0,0,0);
:: warunek na dopisanie jednostki organizacyjnej
_addUdSklInfo:=_in.UD_SKL<>~~ & _in.UD_SKL<>'';
:: warunek na dopisanie [*]
_addInfo:=(_in.SQL_FROM<>~~ & _in.SQL_FROM<>'') | (_in.SQL_WHERE<>~~ & _in.SQL_WHERE<>'')
   | (_in.FML_TEST<>~~ & _in.FML_TEST<>'');

{? _in.OD<>~~ & _in.OD<>_d0
|| {? _in.DO=_d0 | _in.DO=~~
   || {? _addUdSklInfo
      || {? _addInfo
         || 'Pracownicy, którzy kończą określony wiek %1 i później %2 [*]'@@[_in.OD$1,_in.UD_SKL]
         || 'Pracownicy, którzy kończą określony wiek %1 i później %2'@@[_in.OD$1,_in.UD_SKL]
         ?}
      || {? _addInfo
         || 'Pracownicy, którzy kończą określony wiek %1 i później [*]'@@[_in.OD$1]
         || 'Pracownicy, którzy kończą określony wiek %1 i później'@@[_in.OD$1]
         ?}
      ?}
   |? _in.OD=_in.DO
   || {? _addUdSklInfo
      || {? _addInfo
         || 'Pracownicy, którzy kończą określony wiek %1 %2 [*]'@@[_in.OD$1,_in.UD_SKL]
         || 'Pracownicy, którzy kończą określony wiek %1 %2'@@[_in.OD$1,_in.UD_SKL]
         ?}
      || {? _addInfo
         || 'Pracownicy, którzy kończą określony wiek %1 [*]'@@[_in.OD$1]
         || 'Pracownicy, którzy kończą określony wiek %1'@@[_in.OD$1]
         ?}
      ?}
   || {? _addUdSklInfo
      || {? _addInfo
         || 'Pracownicy, którzy kończą określony wiek w okresie od %1 do %2 %3 [*]'@@
               [_in.OD$1,_in.DO$1,_in.UD_SKL]
         || 'Pracownicy, którzy kończą określony wiek w okresie od %1 do %2 %3'@@
               [_in.OD$1,_in.DO$1,_in.UD_SKL]
         ?}
      || {? _addInfo
         || 'Pracownicy, którzy kończą określony wiek w okresie od %1 do %2 [*]'@@[_in.OD$1,_in.DO$1]
         || 'Pracownicy, którzy kończą określony wiek w okresie od %1 do %2'@@[_in.OD$1,_in.DO$1]
         ?}
      ?}
   ?}
|| 'Pracownicy, którzy kończą określony wiek'@@
?}


\run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Formuła odpowiedzialna za przetwarzanie danych. Formuła zakłada odpowiednie (względem parametrów wejściowych)
::       filtry na tabeli P. Tak przygotowaną listę pracowników zwraca w postaci tabeli HTML.
::   WE: _a [NUMBER] - czynność bezdialogowa (serwisowa) czy dialogowa  - 1/0
::       _b [STRING] - uid czynności
::       _c [OBJ]    - tablica nazwana z parametrami wejściowymi. Parametry powinny przejść weryfikację za pomocą
::                     funkcji \input_chk_get/personel_alerty.fml. Struktura tablicy:
::                     OD        [DATA]   - data początkowa
::                     DO        [DATA]   - data końcowa
::                     UD_SCH    [STRING] - schemat organizacyjny
::                     UD_SKL    [STRING] - jednostka organizacyjna
::                     SQL_FROM  [STRING] - dodatkowe relacje filtra
::                     SQL_WHERE [STRING] - dodatkowy warunek filtra
::                     FML_TEST  [STRING] - dodatkowy warunek wyboru
::                     FZ        [STRING] - badane formy zatrudnienia (np. 'P,Z')
::       _d [STRING] - tytuł wiadomości e-mail
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_service:=_a;
_actUid:=_b;
_in:=_c;
_sub:=_d;

P.cntx_psh();
:: Założenie odpowiednich filtrów na tabelę P wg. tego co podano w _in:
_fZatr:=spli_str(_in.FZ,',');
exec('fltr_p_ud_sql','personel_alerty',_service,_in,_fZatr);
obj_del(_fZatr);
:: Wygenerowanie tabeli z danymi pracowników, którzy kończą określony wiek:
_TAB:=exec('prac_kon_wiek','personel_alerty',_in.OD,_in.DO,_in.FML_TEST,'T',_in.WIEK);
P.f_clear();
P.cntx_pop();

_ret:=obj_new('RCV','BODYH');
_ret.RCV:=_ret.BODYH:='';

:: Pobranie listy adresatów:
_ret.RCV:=exec('emls_w_perm','personel_alerty',_actUid);

{? _TAB.first()
|| _th:="'<th [[STYLE_TABLE_TH]]>'+_a+'</th>'";
   _td:="'<td [[STYLE_TABLE_TD]]>'+_a+'</td>'";

   _ret.BODYH:=
      '<h3>'+
      {? _in.OD=_in.DO
      || 'Pracownicy, którzy kończą %2 lat %1 r.'@ [_in.OD$6, $_in.WIEK]
      || 'Pracownicy, którzy kończą %3 lat w okresie od %1 r. do %2 r.'@ [_in.OD$6,_in.DO$6, $_in.WIEK]
      ?}+
      '</h3>\n'

      '<table [[STYLE_TABLE]]>\n'
      '<tr [[STYLE_TABLE_TR]]>'+
         _th('Lp.'@)+
         _th('Nazwisko'@)+
         _th('Imię'@)+
         _th('PESEL'@)+
         _th('Nr teczki'@)+
         _th('Data zatrudnienia'@)+
         _th('Data zwolnienia'@)+
         _th('Jednostka organizacyjna'@)+
         _th('Data urodzenia'@)+
         _th('Data ukończenia %1 lat'@ [$_in.WIEK])+
      '</tr>\n';

   _lp:=0;
   {!
   |? _lp+=1;
      _ret.BODYH+=
         '<tr [[STYLE_TABLE_TR]]>'+
            _td($_lp)+
            _td(_TAB.NAZWISKO)+
            _td(_TAB.PIERWSZE)+
            _td(_TAB.PESEL)+
            _td(_TAB.T,'wiersz2')+
            _td(_TAB.DZA$1)+
            _td(_TAB.DZ$1)+
            _td(_TAB.WYDZIAL)+
            _td(_TAB.UR_DATA$1)+
            _td(_TAB.UK_DATA$1)+
         '</tr>\n';
      _TAB.next()
   !};

   _ret.BODYH+=
      '</table>\n'
      '<p>'+'Ta wiadomość została wygenerowana automatycznie - prosimy na nią nie odpowiadać.'@+'</p>'
?};

{? ~_service
|| FUN.info(
      _sub+'\n\n'+
      'Analiza danych zakończona.'@+'\n'+
      'Liczba zapisów spełniających kryteria: %1.' [$_TAB.size()]
   )
?};

_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 58f386abc0c7df65abae8f4aeb7cd067cbe0d9e77eafe088c28d5ebe198a01c5b332a848858b17f5cc6953be627ffc223074b2dffc0d3c10ee775329ff5156ca458fbb7d68bdede9dbcac681b2edf93a84b42ca4dfbf17d8528c34efc21e478faf668be7f1843036d2ec15246b0570ff8aa5a6db2f115c39768f43b0a2ea3375

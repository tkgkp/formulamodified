:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_zes_orze.fml
:: Utworzony: 05.05.2016
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_ZES_ORZE - Wydruk zestawień kadrowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Wydruk zestawień kadrowych - główna formuła czynności.
::   WY:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
{? ~exec('dostepny_u','f_zatr',,,'P',1)
|| return()
|? __PARSES.getVal('F_ZATR').KOD<>'P'
|| _args:=__PARSES.args('F_ZATR');
   _args.KOD:='P';
   __PARSES.setVal('F_ZATR',_args)
?};
exec('rep_exec','#b_report','PKD_ZES_ORZE','pkd_zk*','Zestawienia kadrowe',1)


\lista_obecnosci
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Wydruk zestawień kadrowych - lista obecności
::   WY: _a - zmienna do przypisania danych
::   WY: tabela z wybranymi do wydruku jednostkami organizacyjnymi
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(1,'JORG','STRING[16]','Jednostka organizacyjna');

_ws:=UD_DEF.mk_sel('Jednostki organizacyjne'@,'P',,'jedn_organ',,,,1,,,,,,'maximized');
UD_DEF.win_fld(_ws,,'UD_SKL','SYMBOL',,,,1,'Symbol'@,,'Symbol jednostki organizacyjnej'@);
UD_DEF.win_fld(_ws,,'OPIS',,,,,1,'Opis'@,,'Nazwa jednostki organizacyjnej'@);
UD_DEF.win_act(_ws,,'Formuła','Wybierz'@@,,,
   "  _par:=params_get();
      {? exec('test_usr_upr','schemat',exec('szukaj_udb_sys','schemat','PKD',UD_TYP.SYMBOL),OPERATOR.USER,UD_DEF.UD_SKL)
      || _par.TAB.JORG:=$UD_DEF.ref();
         _par.TAB.add()
      ?}
   ",,1,1,,,'W'
);
UD_DEF.win_act(_ws,,'Formuła','Pomiń'@@,,,
   "  _par:=params_get();
      _par.TAB.del()
   ",,,,,,'P'
);
UD_DEF.win_act(_ws,,'Formuła','Drukuj'@@,,,
   "  sel_exit()
   ",,,,,,'D',,'icon=print'
);
UD_DEF.win_act(_ws,,'Formuła','Zwiń/rozwiń'@@,,,
   "  exec('zwin_rozwin','#tree')
   ",,,,,,'Z',,'target=window'
);
UD_DEF.win_act(_ws,,'Rekord',,,,
   "  params_get().TAB.find_key($UD_DEF.ref())
   "
);
_btn:=obj_new('W','P','D','A');
_btn.W:=UD_DEF.win_btn(_ws,'text=%1,panel=right,align=begin'['Wybierz'@],'menu:W');
_btn.P:=UD_DEF.win_btn(_ws,'text=%1,panel=right,align=begin'['Pomiń'@],'menu:P');
_btn.D:=UD_DEF.win_btn(_ws,'text=%1,panel=bottom,align=end'['Drukuj'@],'menu:D');
_btn.A:=UD_DEF.win_btn(_ws,'text=%1,panel=bottom,align=end'['Anuluj'@],'key:Esc');
UD_DEF.win_sel(_ws);

_typ:=exec('szukaj_ud_typ','schemat','PODZORG');
_sch:=exec('domyslny','schemat',_typ);
UD_DEF.index('SYMBOL');
UD_DEF.prefix(_sch);
_skl:=exec('ud_skl_firma','schemat',_typ);
exec('ud_def_root','schemat',_ws,_sch,_skl);

_PAR:=tab_tmp(1,
   'ROK','INTEGER','Rok',
   'MC','INTEGER','Miesiąc',
   'ZHN','INTEGER','Przebieg zatrudnienia i Nieobecności',
   'ZK','INTEGER','Kalendarz',
   'POMIN','INTEGER','Pomijać pracowników nieobecnych cały miesiąc?',
   'ZAPAS','INTEGER','Liczba "zapasowych" kolumn',
   'TZHN','INTEGER','Pole techniczne',
   'TPOMIN','INTEGER','Pole techniczne'
);
_PAR.blank();
_PAR.ROK:=date()~1;
_PAR.MC:=date()~2;
_PAR.ZHN:=_PAR.ZK:=_PAR.POMIN:=1;
_PAR.ZAPAS:=0;

_PAR.fld_fml('ROK','AFTER_EDIT',
   "  _par:=params_get();
      {? _par.PAR.ROK<date()~1
      || FUN.emsg('Proszę podać rok nie mniejszy od bieżącego.'@); 0
      || 1
      ?}
   "
);
_PAR.fld_fml('MC','AFTER_EDIT',
   "  _par:=params_get();
      {? _par.PAR.MC<1 | _par.PAR.MC>12
      || FUN.emsg('Proszę podać miesiąc z zakresu od 1 do 12.'@); 0
      || 1
      ?}
   "
);
_PAR.fld_fml('ZAPAS','AFTER_EDIT',
   "  _par:=params_get();
      {? _par.PAR.ZAPAS<0 | _par.PAR.ZAPAS>20
      || FUN.emsg('Dopuszczalna wartość dodatkowych kolumn wynosi od 0 do 20.'@); 0
      || 1
      ?}
   "
);
_PAR.fld_fml('ZHN','BEFORE_EDIT',"_par:=params_get(); _par.PAR.TZHN:=_par.PAR.ZHN; 1");
_PAR.fld_fml('ZHN','AFTER_EDIT',
   "  _par:=params_get();
      {? _par.PAR.TZHN<>_par.PAR.ZHN
      || {? _par.PAR.ZHN || _par.PAR.POMIN:=_par.PAR.TPOMIN || _par.PAR.TPOMIN:=_par.PAR.POMIN; _par.PAR.POMIN:=-1 ?}
      ?};
      1
   "
);
_PAR.fld_fml('POMIN','BEFORE_DISPLAY',"params_get().PAR.ZHN");
_PAR.fld_fml('POMIN','BEFORE_EDIT',"params_get().PAR.ZHN");

_we:=_PAR.mk_edit('Lista obecności'@,,'zk_lobec2par');
_PAR.win_esep(_we,'Parametry wydruku'@);
_PAR.win_efld(_we,,'ROK',,,4,,,'Rok'@,,'Rok dla drukowanej listy obecności, nie wcześniejszy niż %1.'@[$(date()~1)]);
_PAR.efld_opt(_we,'mark=1',,'ROK');
_PAR.win_efld(_we,,'MC',,,2,,,'Miesiąc'@,,'Miesiąc dla drukowanej listy obecności, zakres od 1 do 12.'@);
_PAR.efld_opt(_we,'mark=1',,'MC');
_PAR.win_esep(_we,'Zakres analizy danych'@);
_PAR.win_efld(
   _we,,
   'ZHN',,,,,,
   'Uwzględniać'@,,'Uwzględnianie przebiegu zatrudnienia i nieobecności współpracownika.'@,
   'check-box',
   'check_label="%1"'['Przebieg zatrudnienia i nieobecności'@],
   "1",
   "0"
);
_PAR.win_efld(
   _we,,
   'ZK',,,,,,
   'Uwzględniać'@,,'Uwzględnianie kalendarza współpracowinka.',
   'check-box',
   'check_label="%1"'['Kalendarz'@],
   "1",
   "0"
);
_PAR.win_efld(
   _we,,
   'POMIN',,,,,,
   'Pomijać'@,,'Pomijanie wsółpracowników nieobecnych przez cały miesiąc.'@,
   'check-box',
   'check_label="%1"'['Pracowników nieobecnych cały miesiąc'@],
   "1",
   "0"
);
_PAR.win_efld(_we,,'ZAPAS',,,3,-1,,'Liczba "zapasowych" kolumn'@,,'Liczba dodatkowych kolumn, zakres od 0 do 20.'@);
exec('ok_esc','#window',_PAR,_we);

_gr:=UD_DEF.grp_make('Lista obecności'@,
   "  params_set(_par:=params_get());
      UD_DEF.first();
      UD_DEF.actions_grayed(_par.ws,'WPD');
      grp_disp(UD_DEF,_par.ws)
   ",'grplistaocec',,,,,'maximized'
);
UD_DEF.win_sel(_gr);
UD_DEF.grp_edit(_gr,_PAR,_we,,,
   "  params_set(_par:=params_get());
      {! _ii:=1..4
      |! UD_DEF.btn_sopt(_par.ws,_par.btn[_ii],'state=grayed')
      !};
      {? edit_start()
      || win_activate('wybjednorg');
         {! _ii:=1..4
         |! UD_DEF.btn_sopt(_par.ws,_par.btn[_ii],'state=normal')
         !};
         grp_disp(UD_DEF,_par.ws)
      || sel_exit()
      ?}
   ",,,'maximized'
);
UD_DEF.grp_splt(_gr,,'vertical','jednst_org',75);
UD_DEF.grp_sel(_gr,UD_DEF,_ws,,
   "  _par:=params_get();
      _gray:='';
      _gray+={? ~_par.TAB.first() || 'D' ?};
      _gray+={? ~exec('test_usr_upr','schemat',
                     exec('szukaj_udb_sys','schemat','PKD',UD_TYP.SYMBOL),
                     OPERATOR.USER,
                     UD_DEF.UD_SKL
                 )
             || {? UD_DEF.sel_size()>1 || 'P' || 'WP' ?}
             || ''
             ?};
      _gray+={? _par.TAB.find_key($UD_DEF.ref())
             || {? _gray*'W' || '' || 'W' ?}
             || {? _gray*'P' || '' || 'P'?}
             ?};
      UD_DEF.actions_grayed(_par.ws,_gray)
   ",,,30,,,,,'maximized','wybjednorg'
);
params_set('PAR',_PAR,'TAB',_TAB,'ws',_ws,'btn',_btn);

_a.JORG:=_TAB;
_a.PAR:=_PAR;
_a.SCH:=_sch;
{? ~UD_DEF.select()
   & _a.JORG.size()
   & FUN.ask('Niektóre jednostki organizacjyne zostały wybrane do wydruku.'@+'\n'+'Czy na pewno zrezygnować?'@)
|| _a.JORG.erase()
?};
~~


\charakterpracy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Wydruk zestawień kadrowych - zatrudnienie wg charakteru pracy
::   WY: _a - tytuł okna redakcji
::   WY: tabela z parametrami
::----------------------------------------------------------------------------------------------------------------------
_PAR:=tab_tmp(1,
   'DATA','DATE','Data kontroli',
   'RANI','INTEGER','Rodzaj analizowanej informacji',
   'SANI','INTEGER','Sposób analizy infrmacji'

);
_PAR.DATA:=date();
_PAR.RANI:=_PAR.SANI:=0;
_PAR.add();

_we:=_PAR.mk_edit(_a,,'pkd_zkchp');
_PAR.win_esep(_we,'Parametry wydruku'@);
_PAR.win_efld(_we,,'DATA',,,11,,,'Data kontroli'@,,'Data, dla której wykonywane jest zestawienie'@);
_PAR.win_esep(_we);
_PAR.win_efld(
   _we,,
   'RANI',,,,,,
   'Analizowana informacja'@,,
   'Wybór rodzaju analizowanej informacji'@,
   'radio-buttons',,
   'Wymiar zatrudnienia'@,
   "0",
   'Liczba angaży'@,
   "1"
);
_PAR.win_efld(
   _we,,
   'SANI',,,,,,
   'Sposób analizy'@,,
   'Wybór sposobu analizy danych'@,
   'radio-buttons',,
   'Charakter pracy'@,
   "0",
   'Sposób rozliczania'@,
   "1",
   'Rodzaj zatrudnienia'@,
   "2"
);
_PAR.efld_opt(_we,'mark=1',,'DATA');
exec('ok_esc','#window',_PAR,_we);

_PAR.win_edit(_we);
{? ~_PAR.edit(
      "  {? cur_tab().DATA=#0
         || FUN.emsg('Proszę podać datę kontroli.'@);
            0
         || 1
         ?}
      "
    )
|| _PAR.del()
?};

_PAR


\wg_typów_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Wydruk zestawień kadrowych - Średnie zatrudnienie wg typów stanowisk
::   WY: _a - tytuł okna redakcji
::       _b - zmienna do przypisania danych
::   WY: tabela z parametrami
::----------------------------------------------------------------------------------------------------------------------
:: okno edycyjne
_PAR:=tab_tmp(1,
   'ASK','INTEGER','Sposób prezentacji',
   'ROK','INTEGER','Rok',
   'MOD','INTEGER','Miesiąc początkowy',
   'MDO','INTEGER','Miesiąc końcowy'
);
_PAR.ASK:=0;
_PAR.ROK:=date()~1;
_PAR.MOD:=1;
_PAR.MDO:=12;
_PAR.add();

_we:=_PAR.mk_edit(_a,,'pkd_zksrzattyp',,,'maximized');
_PAR.win_esep(_we,'Parametry wydruku'@);
_PAR.win_efld(
   _we,,
   'ASK',,,,,,
   'Sposób prezentacji'@,,
   'Wybór sposobu prezentacji'@,
   'radio-buttons',,
   'w Etatach'@,
   "1",
   'w Osobach'@,
   "2"
);
_PAR.win_esep(_we,'Zestawienie za okres'@);
_PAR.win_efld(_we,,'ROK',,,5,,,'Rok'@,,'Rok, za który wykonywane jest zestawienie'@);
_PAR.win_efld(_we,,'MOD',,,5,,,'Od miesiąca'@,,'Pierwszy miesiąc roku wybrany do zestawienia'@);
_PAR.win_efld(_we,,'MDO',,,5,,,'Do miesiąca'@,,'Ostatni miesiąc roku wybrany do zestawienia'@);
_PAR.efld_opt(_we,'mark=1',,'ROK');
_PAR.efld_opt(_we,'mark=1',,'MOD');
_PAR.efld_opt(_we,'mark=1',,'MDO');
exec('ok_esc','#window',_PAR,_we);
_PAR.win_edit(_we);

:: okno wertowania
_TYP:=sql(
   'select distinct upper(SLO_KOD.NAZWA) as NAZ, 0 as WYB, 0 as LP '
   'from SLO_KOD join SLO_TYP using(SLO_KOD.SLO_TYP,SLO_TYP.REFERENCE) '
   'where SLO_TYP.SYMBOL=\'STN_TYP\' '
   'order by 3,1'
);

{? var_pres('_TYP')=type_of(SYSLOG)
|| _TYP.LP:=_TYP.size()+1;
   _TYP.NAZ:='POZOSTAŁE';
   _TYP.add(1);
   _TYP.first();
   {!
   |? _TYP.LP:=#_TYP.ref();
      _TYP.put();
      _TYP.first() & _TYP.LP=0
   !};

   _UpDown:=
      "  _lp:=cur_tab(1,1).LP;
         _ref:=cur_tab(1,1).ref();
         {? (_a='+' & cur_tab(1,1).next()) | (_a='-' & cur_tab(1,1).prev())
         || cur_tab(1,1).LP:=_lp;
            cur_tab(1,1).put(1);
            cur_tab(1,1).seek(_ref);
            cur_tab(1,1).LP:=($($_lp+_a+'1'))();
            cur_tab(1,1).put(1)
         ?}
      ";

   _l_lab:='Lp.'@;
   _l_tip:='Liczba porządkująca'@;
   _t_lab:='Typ stanowiska'@;
   _t_tip:=_t_lab;

   _win:=_TYP.mk_edit('Typ stanowiska'@,,'20220331101624');
   _TYP.win_esep(_win,'Dane podstawowe'@);
   _TYP.win_efld(_win,,'LP',,,,,,_l_lab,,_l_tip);
   _TYP.win_efld(_win,,'NAZ',,,60,,,_t_lab,,_t_tip);
   exec('ok_esc','#window',_TYP,_win);
   _TYP.win_edit(_win);

   _win:=_TYP.mk_sel(,,,'20110324100013',,,15,,'U',,,,,'maximized');
   _TYP.win_fld(_win,,'LP',,,3,,,_l_lab,,_l_tip);
   _TYP.win_fld(_win,,'NAZ',,,60,,1,_t_tip,,_t_tip);
   _TYP.win_act(_win,,'Formuła','Wybierz'@@,,'Wybór typów stanowisk'@,
      "  cur_tab().WYB:=1;
         cur_tab().put(1)
      ",,1,1,
      "  {? cur_tab().sel_size()>30
         || FUN.emsg('Maksymalnie można wskazać 30 kolumn jednocześnie.'@);
            0
         || 1
         ?}
      ",,'W');
   _TYP.win_act(_win,,'Formuła','W&yżej'@@,,,"params_get().UpDown('-')",,,,,,'Y');
   _TYP.win_act(_win,,'Formuła','Niżej'@@,,,"params_get().UpDown('+')",,,,,,'N');
   _TYP.win_act(_win,,'Formuła','Drukuj'@@,,,"sel_exit()",,,,,,'D',,'icon=print');
   _TYP.win_act(_win,,'Szukaj');
   _TYP.win_act(_win,,'Rekord',,,,"cur_tab().WYB");
   _btn:=obj_new('Y','N','D','A');
   _btn.Y:=_TYP.win_btn(_win,'text=%1,panel=right,align=begin'['W&yżej'@],'menu:Y');
   _btn.N:=_TYP.win_btn(_win,'text=%1,panel=right,align=begin'['Niżej'@],'menu:N');
   _btn.D:=_TYP.win_btn(_win,'text=%1,panel=bottom,align=end'['Drukuj'@],'menu:D');
   _btn.A:=_TYP.win_btn(_win,'text=%1,panel=bottom,align=end'['Anuluj'@],'key:Esc');
   _TYP.hdr_sel('Wskaż uwzględniane typy stanowisk'@);
   _TYP.win_sel(_win)
|| FUN.emsg('Błąd wykonania sprawozdania.'@);
   _PAR.del();
   return(0)
?};
:: definicja okna grupowego
_gr:=_TYP.grp_make('Typy stanowisk'@,
   "  params_set(_par:=params_get());
      _par.TYP.first();
      _par.TYP.actions_grayed(_par.ws,'WYND');
      grp_disp(_par.TYP,_par.ws)
   ",'srzatwgtypstn',,,,,'maximized'
);
_TYP.win_sel(_gr);

_TYP.grp_edit(_gr,_PAR,_we,,,
   "  params_set(_par:=params_get());
      {! _ii:=1..4
      |! _par.TYP.btn_sopt(_par.ws,_par.btn[_ii],'state=grayed')
      !};
      {? edit_start(
            \" _par:=params_get();
               {? _par.PAR.ROK<1980 | _par.PAR.ROK>9999
               || FUN.info(\'Rok musi zawierać się w zakresie od %1 do %2.\'@[\'1980\',\'9999\']); 'ROK\'
               |? _par.PAR.MOD<1 | _par.PAR.MOD>_par.PAR.MDO
               || FUN.info(\'Podano nieprawidłowy bądź niespójny zakres miesięcy.\'@); \'MOD\'
               |? _par.PAR.MDO>12
               || FUN.info(\'Podano nieprawidłowy bądź niespójny zakres miesięcy.\'@); \'MDO\'
               || 1
               ?}
            \"
         )
      || win_activate('win_typ_stan');
         {! _ii:=1..4
         |! _par.TYP.btn_sopt(_par.ws,_par.btn[_ii],'state=normal')
         !};
         grp_disp(_par.TYP,_par.ws)
      || _par.PAR.del();
         sel_exit()
      ?}
   ",,,'maximized'
);
_TYP.grp_splt(_gr,,'vertical','typy_stanowisk',75);
_TYP.grp_sel(_gr,_TYP,_win,,
   "  params_set(_par:=params_get());
      _par.TYP.cntx_psh();
      _gray:={? ~_par.TYP.find_tab(0,'WYB',,'=',1) || 'D' || '' ?};
      _par.TYP.cntx_pop();
      _par.TYP.actions_grayed(_par.ws,_gray)
   ",,,30,,,,,'maximized','win_typ_stan'
);

params_set('UpDown',_UpDown,'TYP',_TYP,'PAR',_PAR,'ws',_win,'btn',_btn);

{? ~_TYP.select()
|| _PAR.del()
?};

_TYP.index(_TYP.ndx_tmp(,1,'WYB',,));
_TYP.prefix(0);
{? _TYP.first() || {! |? _TYP.del() !} ?};
_TYP.prefix();
_TYP.ndx_drop();
_b.PAR:=_PAR;
_b.TYP:=_TYP;
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 36f60eaf42f79126a7917c7e6006e62555576b7bb41dc4ecead87ca0836d9dcadf7859e8e66003df01832c4f87c3a9e2f846c2bf4ff22d85d581656649a1088aa45018b1746af223e571c6ab87a4986dd60eb634c687cf008cdd033048837c37e1edcfbbe0dc17475db523ac0f5706f1424e69c4cdf8ce9ce1ec181f2529d035

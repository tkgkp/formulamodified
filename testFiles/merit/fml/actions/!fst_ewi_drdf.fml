:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_drdf.fml
:: Utworzony: 30.10.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DRDF - Redakcja dokumentu wartościowego środka
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Redakcja dokumentu wartościowego środka - główna formuła czynności FST_EWI_DRDF.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
:: PARAMETRY WE:
::# kind=WE, symbol=SRST, type=_SRST, name=Wskazanie na środek, required=N
:: PARAMETRY WY:
::# kind=WY, symbol=SRDO, type=_SRDO, name=Wskazanie na dokument wartościowy, required=T

_mp:=params_get().mp;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_akcja:=-_mp.akcja();
exec('init','fst');
exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,FINFO);
OKRO_F.win_dict('SLO_SR');
_ok:=1;

{? ~exec('okr_mod','fst') || _ok:=0 ?};

{? _ok
|| {? _mp.pathProc()
   || _akcja:='dołącz';
      SRST.index('OKR_O_W');
      {? OPERATOR.DEPT<>null
      || SRST.prefix(SSTALE.AO,OPERATOR.DEPT)
      || SRST.prefix(SSTALE.AO)
      ?};
      _win:=exec('create_win_srst','fst');
      SRST.win_sel(_win);
      {? SRST.select() || SRST.SRSR() || _ok:=0 ?}
   |? _mp.pathArea()
   || {? ~SRST.seek(_we.SRST) || _ok:=0 ?}
   ?}
?};

{? _ok & ~SRD.isDataCompleted()
|| FUN.emsg('Środek wymaga uzupełnienia danych. Dołączanie dokumentów nie jest możliwe.'@);
   _ok:=0
?};

_del_set:=0;
{? _ok & SRD.isSet() & SRST.SRSR().Z='N' & SRD.isSetElementsActive()
|| FUN.emsg('Nie można dodawać i modyfikować dokumentów wartościowych dla zestawu środków.\n'
            'Dane tego typu są oddzielne dla elementów zestawu dlatego taki dokument należy\n'
            'dodawać na poziomie elementu składowego.\nAby zlikwidować zestaw należy najpierw '
            'zlikwidować wszystkie elementy składowe zestawu.'@);
   _ok:=0
?};

{? _ok
|| {? _akcja='dołącz' || _ok:=exec('dok_add','!fst_ewi_drdf',{? _mp.pathProc() || 1 || 0 ?})
   |? _akcja='popraw' || _ok:=exec('dok_edit','!fst_ewi_drdf')
   |? _akcja='usuń' || _ok:=exec('dok_del','!fst_ewi_drdf')
   ?}
?};

{? _ok
|| {? _akcja<>'usuń'
   || _wy.SRDO:=SRDO.ref();
      _mp.keyRef(SRDO.uidref());
      _mp.save(,_wy)
   ?};
   _mp.done()
|| _mp.cancel()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:=''@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
SRST.cntx_psh();
{? var_pres('SRST',_we)
|| SRST.cntx_psh();
   {? _we.SRST<>~~
   || SRST.use(8+$_we.SRST)
   ?};
   SRST.prefix();
   {? SRST.seek(_we.SRST)
   || _desc:='Środek: %1. Redakcja dokumentu wartościowego'@@[SRST.NRI]
   ?};
   SRST.cntx_pop()
?};
SRST.cntx_pop();
_desc



\dok_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowego dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
_ok:=0;
_ref:=_element:=null;
_tmp:=EDIT_ES.RODZ;

{? SRSR.r_lock(1,1,1)
|| EDIT_ES.RODZ:='W';
   SRDT.cntx_psh();
   {? SRD.isSet()
   || SRDT.index('SRDTL');
      SRDT.prefix(EDIT_ES.RODZ,'T')
   || SRDT.index('SRDT');
      SRDT.prefix(EDIT_ES.RODZ)
   ?};
   {? SRDT.first()
   || SRDT.win_sel('SELECT');
      {? SRDT.select()
      || {? 1+SRST.MF().T<>'N' & exec('field_test_nat','fst')
         || FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                     'Wskazany typ dokumentu dotyczy tylko pól wartościowych związanych\n'
                     'z metodą naturalną bilansową. Środek nie jest amortyzowany tą metodą.'@[SRDT.TYP,SRDT.NAZWA]);
            _ref:=null
         |? FINFO.TOR_D='T' & ~(SRDT.WARD='T' | SRDT.UMOD='T')
         || FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                     'Wskazany typ dokumentu nie uwzględnia pól wartościowych związanych\n'
                     'z torem dodatkowym, który jest obsługiwany w systemie.'@[SRDT.TYP,SRDT.NAZWA]);
            _ref:=null
         |? FINFO.SPR_GRP<>'T' & SRDT.P='S'
         || FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                     'Wskazany typ dokumentu dotyczy zbycia w obrębie grupy\n'
                     'kapitałowej, obsługa zbycia nie jest włączona w parametrach systemu.'@[SRDT.TYP,SRDT.NAZWA]);
            _ref:=null
         |? FINFO.SPR_GRP='T' & SRDT.P='S' & SRSR.GRP='E'
         || FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                     'Wskazany typ dokumentu dotyczy zbycia w obrębie grupy\n'
                     'kapitałowej, zbywanie elementów zestawów nie jest obsługiwane w systemie.'@[SRDT.TYP,SRDT.NAZWA]);
            _ref:=null()
         || _ref:=SRDT.ref()
         ?};
         {? FINFO.SPR_GRP='T' & SRSR.ORG_REF<>'' & SRDT.P<>'S'
         || {? ~exec('srdt_diff','fst2',SRDT.ref(),(ref_name(SRSR.ORG_REF))+3)
            || FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                        'Definicja wskazanego typu dokumentu jest niezgodna z odpowiednim typem\n'
                        'dokumentu w firmie pierwotnej środka (w grupie kapitałowej).'@[SRDT.TYP,SRDT.NAZWA]);
               _ref:=null
            ?}
         ?}
      ?}
   || FUN.emsg('W systemie nie zdefiniowano żadnego typu dokumentu wartościowego.\n'
               'Należy uzupełnić brakujące dane w obszarze Ustawienia i parametryzacja.'@);
      SRSR.r_unlock();
      SRDT.cntx_pop();
      EDIT_ES.RODZ:=_tmp;
      return(0)
   ?};

   {? _ref<>null
   || SRDO.blank();
      SRDO.TYP:=_ref;
      SRDO.ODD_DOK:=SRST.ODD;
      SRDO.NR:=exec('bl_srdo_nr','fst');
      SRDO.SYMBOL:=exec('symdok','fst');
:: jeżeli element to dodatkowe złączenie na element składowy
      {? SRSR.GRP='E'
      || _element:=SRDO.SRSR_E:=SRSR.ref();
         SRDO.SRSR:=exec('get_srsr_root','fst',SRSR.ref())
      || SRDO.SRSR:=SRSR.ref()
      ?};
      {? SRD.isSet()
      || SRDO.win_edit('RED_WG')
      || exec('czytaj','#stalesys',,FINFO);
         {? SRDO.TYP().P='S'
         || SRDO.win_edit('RED_WS')
         || SRDO.win_edit('RED_W')
         ?}
      ?};
      exec('set_field','fst');
:: jeżeli zbycie do kolejnej firmy w grupie kapitałowej (nie z pierwotnej!) to dokument zachowuje się jak likwidujący
      {? SRDO.TYP().P='S' & SRSR.ORG_REF<>'' || SRDO.Z:='T' ?};
      {? SRDO.edit("exec('chk_dok','!fst_ewi_drdf',0)")
      || exec('mod_rec','fst');
         SRSR.r_unlock();
:: jeżeli okres obowiązywania nie jest z kolejnego roku
         {? ~(SRDO.DW<SRDO.OKRO_F().ROK().POCZ_ROK)
         || SRDO.ROK:=SRDO.OKRO_F().RES;
            SRDO.OKRES:=SRDO.OKRO_F().OES
         ?};
         {? SRDO.add()
         || _ok:=1;
            _pdm_f:=null;
            {? SRDO.Z='T' & (SRSR.MP().PDM='T' | SRSR.MF().PDM='T' | (FINFO.TOR_D='T' & SRSR.MD().PDM='T'))
            || _pdm_f:=exec('find_prev_okro','fst',SRDO.OKRO_F().ROK,SRDO.OKRO_F().NR)
            ?};
            {? _element=null
            || {? _pdm_f
               || SRD.updateRecState(_pdm_f,1)
               || SRD.updateRecState(SRDO.OKRO_F)
               ?}
            ?};
            SRSR.cntx_psh();
            {? _element<>null & SRSR.seek(_element)
            || {? _pdm_f
               || SRD.updateRecState(_pdm_f,1)
               || SRD.updateRecState(SRDO.OKRO_F)
               ?};
               SRD.rootUpdate(SRSR.TREE, SRDO.OKRO_F,1)
            ?};
            SRSR.cntx_pop();
            {? SRDO.TYP().RODZ='W' & (1+SRDO.TYP().TYP='K' | SRDO.TYP().TYP='LT') & SRDO.DW>=SRST.OKRO_F().POCZ & SRDO.DW<=SRST.OKRO_F().KON
               & SRDO.OKRO_F().POCZ~2<>SRDO.DW~2
            || {? SRDO.TYP().TYP='LT' | (SRDO.TYP().TYP='K-' & fabs(SRDO.WARF)>SRST.NETF)
               || SRST.NETFK+=SRDO.WARF-SRDO.UMOF;
                  SRST.NETPK+=SRDO.WARP-SRDO.UMOP;
                  {? FINFO.TOR_D='T' || SRST.NETDK+=SRDO.WARD-SRDO.UMOD ?}
               || SRST.NETFK+=SRDO.WARF;
                  SRST.NETPK+=SRDO.WARP;
                  {? FINFO.TOR_D='T' || SRST.NETDK+=SRDO.WARD ?}
               ?};
               SRST.put()
            |? SRDO.TYP().RODZ='W' & (1+SRDO.TYP().TYP='K' | SRDO.TYP().TYP='LT') & SRDO.DW>=SRST.OKRO_F().POCZ & SRDO.DW<=SRST.OKRO_F().KON
               & SRDO.OKRO_F().POCZ~2=SRDO.DW~2
            || SRST.cntx_psh(); OKRO_F.cntx_psh();
               _okro:=exec('find_prev_okro','fst', SRST.OKRO_F().ROK, SRST.OKRO_F().NR);
               _pocz:=exec('FindAndGet','#table',OKRO_F,_okro,,"@.OKRO_F.POCZ",date(0,0,0));
               _srsr:=SRST.SRSR;
               SRST.index('POCZ');
               SRST.prefix(_srsr, _pocz);
               {? SRST.first()
               || {? SRDO.TYP().TYP='LT' | (SRDO.TYP().TYP='K-' & fabs(SRDO.WARF)>SRST.NETF)
                  || SRST.NETFK+=SRDO.WARF-SRDO.UMOF;
                     SRST.NETPK+=SRDO.WARP-SRDO.UMOP;
                     {? FINFO.TOR_D='T' || SRST.NETDK+=SRDO.WARD-SRDO.UMOD ?}
                  || SRST.NETFK+=SRDO.WARF;
                     SRST.NETPK+=SRDO.WARP;
                     {? FINFO.TOR_D='T' || SRST.NETDK+=SRDO.WARD ?}
                  ?};
                  SRST.put()
               ?};
               SRST.cntx_pop(); OKRO_F.cntx_pop()
            ?};
:: jeżeli grupa kapitałowa i dotyczy środka nabytego to kopia dokumentu dla środka pierwotnego
:: o ile nie jest to sprzedaż do kolejnej firmy z grupy
            {? FINFO.SPR_GRP='T' & SRDO.SRSR().ORG_REF<>'' & SRDO.TYP().P<>'S'
            || {? ~exec('copy_org','fst',SRDO.ref(),'W')
               || FUN.emsg('Nie udało się odświeżyć danych w firmie pierwotnej bieżącego środka.'@)
               ?}
            ?};
:: jeżeli element to odświeżenie danych wartościowych zestawu
            {? SRD.isElement() || SRD.rootUpdate(SRSR.TREE, SRDO.OKRO_F,1) ?};
            {? _=0 | _a=0 || exec('srst_rfresh','!fst_ewi_drdf') ?};
:: jeżeli element ostatni jest likwidowany to wystawia automatycznie dokument likwidacji dla zestawu
            {? SRD.isElement() & SRDO.TYP().TYP='LT'
            || _srsr:=SRSR.TREE;
               SRSR.cntx_psh();
               {? SRSR.seek(_srsr,)
               || SRST.cntx_psh();
                  SRST.index('SROD');
                  SRST.prefix();
                  {? SRST.find_key(SRSR.ref,SRDO.ROK_F,SRDO.OKRO_F)
                  || {? SRD.isSetElementsActive()=0
                     || FUN.info('Likwidowany środek jest ostatnim elementem zestawu.\n'
                                 'Zostanie wystawiony dodatkowo dokument likwidacji dla zestawu.'@);
                        SRDO.ODD_DOK:=SRST.ODD;
                        SRDO.NR:=exec('bl_srdo_nr','fst');
                        SRDO.SYMBOL:=exec('symdok','fst');
                        SRDO.SRSR:=SRST.SRSR; SRDO.SRSR_E:=null();
                        SRDO.WARF:=SRDO.UMOF:=SRDO.WARP:=SRDO.UMOP:=SRDO.WARD:=SRDO.UMOD;
:: jeżeli zbycie do kolejnej firmy w grupie kapitałowej (nie z pierwotnej!) to dokument zachowuje się jak likwidujący
                       {? SRDO.TYP().P='S' & SRSR.ORG_REF<>'' || SRDO.Z:='T' ?};
                        exec('mod_rec','fst');
                        SRDO.cntx_psh();
                        SRDO.prefix();
                        {? SRDO.add() || SRD.updateRecState(SRDO.OKRO_F,1,1) ?};
                        SRDO.cntx_pop()
                     ?}
                  ?};
                  SRST.cntx_pop()
               ?};
               SRSR.cntx_pop();
               {? _=0 | _a=0 || exec('srst_rfresh','!fst_ewi_drdf') ?}
            ?};
            FUN.info('Po wprowadzeniu nowego dokumentu wartościowego należy ponownie naliczyć\n'
                     'amortyzację dla środka w okresie obowiązywania dokumentu i następnych.'@)
         ?}
      || SRSR.r_unlock()
      ?}
   ?};
   SRDT.cntx_pop()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?};

EDIT_ES.RODZ:=_tmp;
_ok


\dok_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
{? SRDO.AUTO='T'
|| FUN.emsg('Dokument utworzony automatycznie podczas generowania dokumentów poinwentaryzacyjnych.\n'
            'Nie może być modyfikowany, może być usunięty tylko poprzez anulowanie dokumentów poinwentaryzacyjnych.'@);
   return(0)
?};
{? SRDO.K='T'
|| FUN.emsg('Dokument został już zadekretowany, nie może być modyfikowany lub usunięty,\n'
            'bez uprzedniego usunięcia dokumentu księgowego utworzonego podczas dekretowania.'@);
   return(0)
?};

{? SRSR.R='S' & SRDO.TYP().P<>'S'
|| FUN.emsg('Dokument dotyczy środka, który został zbyty na rzecz innej firmy z grupy kapitałowej\n'
            'Modyfikacja dokumentu nie jest możliwa.'@);
   return(0)
?};

{? SRSR.R='S' & SRDO.TYP().P='S'
|| SRSFIRMA.cntx_psh();
   SRSFIRMA.index('SRDO');
   SRSFIRMA.prefix(SRDO.uidref(),);
   {? SRSFIRMA.first() & SRSFIRMA.SRSR_D<>''
   || FUN.emsg('Środek został zbyty w ramach grupy kapitałowej i jest już wprowadzony do ewidencji\n'
               'w firmie docelowej. Modyfikacja dokumentu nie jest możliwa.'@);
      SRSFIRMA.cntx_pop();
      return(0)
   ?};
   SRSFIRMA.cntx_pop()
?};

_ok:=0;

{? SRSR.r_lock(1,1,1)
|| {? SRDO.r_lock(1,1,1)
   || _p_okr:=SRDO.OKRO_F;
      _p_data:=SRDO.OKRO_F().POCZ;
      _tmp:=EDIT_ES.RODZ;
      EDIT_ES.RODZ:='W';
      {? SRDO.TYP().P='S' || SRDO.win_edit('RED_WS') || SRDO.win_edit('RED_W') ?};
      exec('set_field','fst',1);
      {? SRDO.edit("exec('chk_dok','!fst_ewi_drdf',1)")
      || exec('mod_rec','fst');
         SRSR.r_unlock();
         SRDO.ROK:=SRDO.OKRO_F().RES;
         SRDO.OKRES:=SRDO.OKRO_F().OES;
         {? SRDO.put()
         || _ok:=1;
            {? SRDO.OKRO_F().POCZ>_p_data
            || _okro_f:=_p_okr
            || _okro_f:=SRDO.OKRO_F
            ?};
            SRD.updateRecState(_okro_f);
:: jeżeli grupa kapitałowa i dotyczy środka nabytego to aktualizacja kopia dokumentu dla środka pierwotnego
            {? FINFO.SPR_GRP='T' & SRDO.TYP().P<>'S' & SRDO.SRSR().ORG_REF<>''
            || {? ~exec('update_org','fst',SRDO.ref(),'W')
               || FUN.emsg('Nie udało się odświeżyć danych w firmie pierwotnej bieżącego środka.'@)
               ?}
            ?};

:: jeżeli element to odświeżenie danych wartościowych zestawu
            {? SRD.isElement() || SRD.rootUpdate(SRSR.TREE,_okro_f,1) ?};

            exec('srst_rfresh','!fst_ewi_drdf');
            FUN.info('Po poprawieniu dokumentu wartościowego należy ponownie naliczyć\n'
                     'amortyzację dla środka w okresie obowiązywania dokumentu i następnych.'@)
         ?}
      || SRSR.r_unlock()
      ?};
      EDIT_ES.RODZ:=_tmp;
      SRDO.r_unlock()
   || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
   ?}
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?};
_ok


\dok_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie dokumentu
::----------------------------------------------------------------------------------------------------------------------
SRDO.get();
_element:=SRDO.SRSR_E<>null;
{? SRDO.AUTO='T'
|| FUN.emsg('Dokument utworzony automatycznie podczas generowania dokumentów poinwentaryzacyjnych.\n'
            'Nie może być modyfikowany, może być usunięty tylko poprzez anulowanie dokumentów poinwentaryzacyjnych.'@);
   return(0)
?};
:: kontrola czy zmiana nie dotyczy zadekretowanej amortyzacji
_dekret:=0;
SRST.cntx_psh(); SRSR.cntx_psh();
SRST.index('SROD');
SRST.prefix(SRSR.ref());
{? SRST.find_key(SRDO.ROK_F,SRDO.OKRO_F)
|| {! |?
      {? SRST.DEKRET='T' || _dekret:=1 ?};
      _dekret=0 & SRST.next()
   !}
?};
SRST.cntx_pop(); SRSR.cntx_pop();
{? _dekret=1
||  FUN.emsg('Amortyzacja w okresie obowiązywania dokumentu lub w późniejszym została już\n'
             'zadekretowana dla co najmniej jednego toru amortyzacji. Usuwanie amortyzacji\n'
             'lub wprowadzanie zmian mogących wpłynąć na amortyzację nie jest możliwe.'@);
   return(0)
?};
{? SRDO.K='T'
|| FUN.emsg('Dokument został już zadekretowany, nie może być modyfikowany lub usunięty,\n'
            'bez uprzedniego usunięcia dokumentu księgowego utworzonego podczas dekretowania.'@);
   return(0)
?};
:: jeżeli grupa kapitałowa i dokument jest kopią dokumentu wartościowego z bieżącej firmy w któej jest obecnie środek
{? FINFO.SPR_GRP='T' & SRDO.LINK_REF<>''
|| FUN.emsg('Dokument jest kopią oryginalnego dokumentu utworzonego w bieżącej firmie środka,\n'
            'nie może być modyfikowany lub usunięty, należy zmodyfikować lub usunąć dokument oryginalny.'@);
   return(0)
?};
{? SRDO.SRSR_E<>null()
|| SRDO.cntx_psh();
   SRDO.index('SRODZAJ');
   SRDO.prefix(SRDO.SRSR, SRDO.RODZ);
   {? SRDO.first()
   || {! |?
            {? SRDO.TYP().TYP='LT' & SRDO.SRSR_E=null
            ||  FUN.emsg('Zestaw został zlikwidowany, usunięcie dokumentu LT wystawionego dla elementu nie jest możliwe.\n'
               ' Najpierw należy usunąć dokument dla zestawu.'@);
               SRDO.cntx_pop();
               return(0)
            ?};
            SRDO.next()
      !}
   ?};
   SRDO.cntx_pop()
?};
:: jeżeli grupa kapitałowa i dokument zbycia w grupie
_lock:=0;
{? FINFO.SPR_GRP='T' & SRDO.TYP().P='S'
|| _con_zb:=1;
   SRSFIRMA.cntx_psh();
   SRSFIRMA.index('SRDO');
   SRSFIRMA.prefix(SRDO.uidref());
   {? SRSFIRMA.first() & SRSFIRMA.PRZYJETO='T'
   || FUN.emsg('Środek został już przyjęty na stan innej firmy z grupy kapitałowej. Dokumentu nie można usunać.'@);
      SRSFIRMA.cntx_pop();
      return(0)
   || {? SRSFIRMA.r_lock(1,1,1)
      || _lock:=1
      || _lock:=0;
         FUN.emsg('Dokument został zablokowany przez innego użytkownika i nie można go usunąć.'@);
         SRSFIRMA.cntx_pop();
         return(0)
      ?}
   ?};
   SRSFIRMA.cntx_pop()
|| _con_zb:=0
?};
_uidref:=SRDO.uidref();
_dalej:=0;
{? SRDO.TYP().RODZ='W' & (1+SRDO.TYP().TYP='K' | SRDO.TYP().TYP='LT') & SRDO.DW>=SRST.OKRO_F().POCZ & SRDO.DW<=SRST.OKRO_F().KON
   & SRDO.OKRO_F().POCZ~2<>SRDO.DW~2
|| {? SRDO.TYP().TYP='LT'
   || SRST.NETFK-=SRDO.WARF+SRDO.UMOF;
      SRST.NETPK-=SRDO.WARP+SRDO.UMOP;
      {? FINFO.TOR_D='T' || SRST.NETDK-=SRDO.WARD+SRDO.UMOD ?}
   || SRST.NETFK-=SRDO.WARF;
      SRST.NETPK-=SRDO.WARP;
      {? FINFO.TOR_D='T' || SRST.NETDK-=SRDO.WARD ?}
   ?};
   SRST.trig_off('*','*');
   SRST.put();
   SRST.trig_on('*','*');
   SRD.updateRecState(exec('find_okro_f','fst',SRDO.DW))
|? SRDO.TYP().RODZ='W' & (1+SRDO.TYP().TYP='K' | SRDO.TYP().TYP='LT') & SRDO.DW>=SRST.OKRO_F().POCZ & SRDO.DW<=SRST.OKRO_F().KON
   & SRDO.OKRO_F().POCZ~2=SRDO.DW~2
|| SRST.cntx_psh(); OKRO_F.cntx_psh();
   _okro:=exec('find_prev_okro','fst', SRST.OKRO_F().ROK, SRST.OKRO_F().NR);
   _pocz:=exec('FindAndGet','#table',OKRO_F,_okro,,"@.OKRO_F.POCZ",date(0,0,0));
   _srsr:=SRST.SRSR;
   SRST.index('POCZ');
   SRST.prefix(_srsr, _pocz);
   {? SRST.first()
   || {? SRDO.TYP().TYP='LT'
      || SRST.NETFK-=SRDO.WARF+SRDO.UMOF;
         SRST.NETPK-=SRDO.WARP+SRDO.UMOP;
         {? FINFO.TOR_D='T' || SRST.NETDK-=SRDO.WARD+SRDO.UMOD ?}
      || SRST.NETFK-=SRDO.WARF;
         SRST.NETPK-=SRDO.WARP;
         {? FINFO.TOR_D='T' || SRST.NETDK-=SRDO.WARD ?}
      ?};
      SRST.put()
   ?};
   SRST.cntx_pop(); OKRO_F.cntx_pop()
?};
{? SRDO.Z<>'T'
|| {? ~SRD.isDefunct(_con_zb)
   || {? SRDO.count()=0
      || {? SRDO.r_lock(1,1)
         || {? FUN.ask('Usunąć dokument wartościowy o symbolu: %1?'@[SRDO.SYMBOL])
            || SRDO.r_unlock();
               _okr:=SRDO.OKRO_F;
               _sr_ref:=SRDO.SRSR;
               {? SRDO.SRSR_E<>null
               || _zb:=SRDO.SRSR_E().ZB='T'
               || _zb:=SRDO.SRSR().ZB='T'
               ?};
:: jeżeli usuwany dokument był dokumentem zbycia to czyszczenie zapisu zbycia
               {? ~(FINFO.SPR_GRP='T' & _zb) | _lock
               || {? SRDO.del(1,1)
                  || _dalej:=1;
                     {? FINFO.SPR_GRP='T' & _zb
                     || SRSFIRMA.cntx_psh();
                        SRSFIRMA.index('SRDO');
                        SRSFIRMA.prefix(_uidref);
                        {? SRSFIRMA.first()
                        || {? _lock || SRSFIRMA.r_unlock(); _lock:=0 ?};
                           SRSFIRMA.del()
                        ?};
                        SRSFIRMA.cntx_pop();
:: i czyszczenie znacznika zbycia dla środka
                        SRSR.cntx_psh();
                        SRSR.prefix();
                        {? SRSR.seek(_sr_ref,ref_name(_sr_ref))
                        || SRSR.ZB:='N';
                           SRSR.put()
                        ?};
                        SRSR.cntx_pop()
                     ?}
                  ?}
               ?};
:: jeżeli grupa kapitałowa i dokument dotyczy środka nabytego to usunięcie kopii dokumentu dla środka pierwotnego
               _org_ref:={? SRDO.SRSR_E<>null
                         || SRDO.SRSR_E().ORG_REF
                         || SRDO.SRSR().ORG_REF
                         ?};
               {? FINFO.SPR_GRP='T' & SRDO.TYP().P<>'S' & _org_ref<>''
               || {? ~exec('delete_org','fst',_uidref,'W',_org_ref)
                  || FUN.emsg('Nie udało się odświeżyć danych w firmie pierwotnej bieżącego środka.'@)
                  ?}
               ?}
            || SRDO.r_unlock()
            ?}
         || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
         ?}
      || FUN.emsg('Bieżący dokument jest wykorzystywany w systemie i nie można go usunąć.'@)
      ?}
   ?};
   {? _lock
   || SRSFIRMA.cntx_psh();
      SRSFIRMA.index('SRDO');
      SRSFIRMA.prefix(_uidref);
      {? SRSFIRMA.first() || SRSFIRMA.r_unlock(); _lock:=0 ?};
      SRSFIRMA.cntx_pop()
   ?}
|| {? SRDO.r_lock(1,1,1)
   || {? FUN.ask('Usunąć dokument wartościowy o symbolu: %1?'@[SRDO.SYMBOL])
      || SRDO.r_unlock();
         _okr:=SRDO.OKRO_F;
         do();
            SRSR.DOKSKR:=null;
            SRSR.DES:=date(0,0,0);
            SRSR.ROZ:=null;
            _srdo_z:=SRSR.Z;
            SRSR.Z:='N';
            SRSR.put();
            _okr:=SRDO.OKRO_F;
            {? _srdo_z='T' & (SRSR.MP().PDM='T' | SRSR.MF().PDM='T' | (FINFO.TOR_D='T' & SRSR.MD().PDM='T'))
               & SRDO.OKRO_F<>SRSR.OKRO_F
            || _o_pdm:=exec('find_prev_okro','fst',SRDO.OKRO_F().ROK,SRDO.OKRO_F().NR);
               {? _o_pdm || _okr:=_o_pdm ?}
            ?};
            _uidref:=SRDO.uidref();
            _sr_ref:=SRDO.SRSR;
            {? SRDO.SRSR_E<>null
            || _zb:=SRDO.SRSR_E().ZB='T'
            || _zb:=SRDO.SRSR().ZB='T'
            ?};
            {? SRDO.del(1,1) || _dalej:=1 ?};
         end();
         {? _dalej
         ||
:: jeżeli grupa kapitałowa i dokument dotyczy zbycia środka do kolejnej firmy to czyszczenie
:: znacznika zbycia i zapisu zbycia
            {? FINFO.SPR_GRP='T' & _zb
            || SRSFIRMA.cntx_psh();
               SRSFIRMA.index('SRDO');
               SRSFIRMA.prefix(_uidref);
               {? SRSFIRMA.first() || SRSFIRMA.del() ?};
               SRSFIRMA.cntx_pop();
:: znacznik zbycia dla środka
               SRSR.cntx_psh();
               SRSR.prefix();
               {? SRSR.seek(_sr_ref,ref_name(_sr_ref))
               || SRSR.ZB:='N';
                  SRSR.put()
               ?};
               SRSR.cntx_pop()
            ?};
:: jeżeli grupa kapitałowa i dokument dotyczy środka nabytego to usunięcie kopii dokumentu dla środka pierwotnego
            {? SRDO.SRSR_E<>null
            || _org_ref:=SRDO.SRSR_E().ORG_REF
            || _org_ref:=SRDO.SRSR().ORG_REF
            ?};
            {? FINFO.SPR_GRP='T' & SRDO.TYP().P<>'S' & _org_ref<>''
            || {? ~exec('delete_org','fst',_uidref,'W',_org_ref)
               || FUN.emsg('Nie udało się odświeżyć danych w firmie pierwotnej bieżącego środka.'@)
               ?}
            ?}
         ?}
      || SRDO.r_unlock()
      ?}
   || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
   ?}
?};
{? _dalej
|| SRD.updateRecState(_okr);
:: jeżeli element to odświeżenie danych wartościowych zestawu
   {? SRD.isElement() | _element || SRD.rootUpdate(SRSR.TREE,_okr,1) ?};
   exec('srst_rfresh','!fst_ewi_drdf')
?};
_dalej


\srst_rfresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Odświeżanie okna rejestru stanów środków po zmianach w dokumentach wartościowych
::----------------------------------------------------------------------------------------------------------------------
_odd:=exec('odd_count','fst');
{? EDIT_ES.R='T'
|| {? var_press('__DTREE')>0 & __DTREE>0
   || {? OPERATOR.DEPT | _odd=1 || grp_disp(SRST,'DWER_O') || grp_disp(SRST,'DWER') ?}
   || {? var_pres('__SRST_E')>0 & __SRST_E='WER_E' ||  grp_disp(SRST,'WER_E') || grp_disp(SRST,'WER') ?}
   ?}
|? EDIT_ES.R='N'
|| {? var_press('__DTREE')>0 & __DTREE>0
   || {? OPERATOR.DEPT | _odd=1 || grp_disp(SRST,'DWER_N_O') || grp_disp(SRST,'DWER_N') ?}
   || {? var_pres('__SRST_E')>0 & __SRST_E='WER_E' ||  grp_disp(SRST,'WER_N_E') || grp_disp(SRST,'WER_N') ?}
   ?}
|? EDIT_ES.R=''
|| grp_disp(SRST,'WER_U')
?}


\chk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja poprawności dokumentu
::   WE: _a = 1 - popraw, 0 - dołącz
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRDO,,'SYMBOL','DW','DO');
SRSR.cntx_psh();
{? _wy=''
|| {? SRDO.OKRO_F=null
   || FUN.emsg('Nie uzupełnione pole Okres obowiązywania\n'
               '(pole uzupełni się automatycznie po redakcji pola Data obowiązywania).'@);
      _wy:='DO'
   ?}
?};
:: kontrola unikalności numeru i symbolu dokumentu
{? _wy=''
|| {? __CHK.index(SRDO,_a)<>''
   || {? FUN.ask('Wygenerować nowy numer dokumentu?'@)
      || SRDO.NR:=exec('bl_srdo_nr','fst',SRDO.NR);
         SRDO.SYMBOL:=exec('symdok','fst')
      ?};
      _wy:='SYMBOL'
   ?}
?};
:: kontrola daty operacji
{? _wy='' & SRDO.OKRO_F<>null
|| {? exec('test_new_year','fst',SRDO.OKRO_F().ROK)
   || FUN.emsg('Wskazana data operacji pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='DO'
   ?}
?};
:: kontrola czy okres jest zamknięty
{? _wy='' & SRDO.DW<>date(0,0,0)
|| _okres:=exec('find_okro_f','fst', SRDO.DW);
   {? ~exec('okr_mod','fst',_okres)
   || _wy:='DW'
   ?}
?};
::kontrola wartości rezydualnej
{? _wy='' & SRDO.OSTATEK>SRST.WARF
|| FUN.emsg('Wartość rezydualna nie może być większa niż wartość bilansowa środka.'@);
   _wy:='OSTATEK'
?};
:: kontrola miesiąca obowiązywania
{? _wy='' & ~exec('chk_dok_okres','fst') || _wy:='DO' ?};
:: kontrola daty wystawienia
{? _wy='' & exec('ae_srdo_dw','fst')=0 || _wy:='DW' ?};
:: kontrola czy wypełniono jakiekolwiek wartości
{? _wy=''
|| _pola:=_wpola:=0;
   _gdzie:='';
   {? SRDO.TYP().WARP='T' || _pola+=1; {? _gdzie='' || _gdzie:='WARP' ?} ?};
   {? SRDO.WARP<>0 || _wpola+=1 ?};
   {? SRDO.TYP().UMOP='T' || _pola+=1; {? _gdzie='' || _gdzie:='UMOP' ?} ?};
   {? SRDO.UMOP<>0 || _wpola+=1 ?};
   {? SRDO.TYP().WARF='T' || _pola+=1; {? _gdzie='' || _gdzie:='WARF' ?} ?};
   {? SRDO.WARF<>0 || _wpola+=1 ?};
   {? SRDO.TYP().UMOF='T' || _pola+=1; {? _gdzie='' || _gdzie:='UMOF' ?} ?};
   {? SRDO.UMOF<>0 || _wpola+=1 ?};
   {? FINFO.TOR_D='T' & SRDO.TYP().WARD='T' || _pola+=1; {? _gdzie='' || _gdzie:='WARD' ?} ?};
   {? FINFO.TOR_D='T' & SRDO.WARD<>0 || _wpola+=1 ?};
   {? FINFO.TOR_D='T' & SRDO.TYP().UMOD='T' || _pola+=1; {? _gdzie='' || _gdzie:='UMOD' ?} ?};
   {? FINFO.TOR_D='T' & SRDO.UMOD<>0 || _wpola+=1 ?};
   {? SRDO.TYP().OSTATEK='T' ||  _pola+=1; {? _gdzie='' || _gdzie:='OSTATEK' ?} ?};
   {? SRDO.OSTATEK<>0 || _wpola+=1 ?};
   {? SRDO.TYP().OKE='T' ||  _pola+=1; {? _gdzie='' || _gdzie:='OKE' ?} ?};
   {? SRDO.OKE<>0 || _wpola+=1 ?};
   {? SRDO.TYP().MAX='T' ||  _pola+=1; {? _gdzie='' || _gdzie:='MAX' ?} ?};
   {? SRDO.MAX<>0 || _wpola+=1 ?}
?};
{? _wy='' & _pola>0 & _wpola=0 & SRDO.SRSR().GRP<>'T'
|| FUN.emsg('Nie wypełniono żadnego z pól wartościowych. Dokument nie zmieni żadnej z cech wartościowych środka.'@);
   _wy:={? _gdzie<>'' || _gdzie || 'UWAGI' ?}
?};
:: kontrola czy w przypadku niskocennego nie przekroczono wartości 3500 zł
{? _wy='' & SRST.R='N' & SRDO.TYP().P='+'
|| {? SRST.WARP+SRDO.WARP>FINFO.W
   || _wy:='WARP'
   |? SRST.WARF+SRDO.WARF>FINFO.W
   || _wy:='WARF'
   |? FINFO.TOR_D='T' & SRST.WARF+SRDO.WARF>FINFO.W
   || _wy:='WARD'
   ?};
   {? _wy<>''
   || FUN.emsg('Wprowadzenie dokumentu spowodowałoby przekroczenie limitu wartości dla środków\n'
               'niskocennych (jednorazowo umarzanych) - %1'@[$FINFO.W])
   ?}
?};
:: kontrola czy wartość umorzenia nie jest większa od wartości brutto
{? _wy='' & (SRDO.TYP().P<>'-' & SRDO.TYP().P<>'S') & PAR_SKID.get(202)='N'
|| {? (SRDO.UMOP>SRDO.WARP)
   || _wy:='UMOP'
   |? (SRDO.UMOF>SRDO.WARF)
   || _wy:='UMOF'
   |? (FINFO.TOR_D='T' & SRDO.UMOD>SRDO.WARD)
   || _wy:='UMOD'
   ?};
   {? _wy<>'' || FUN.emsg('Umorzenie nie może być większe od wartości.'@) ?}
|? _wy='' & (SRDO.TYP().P<>'-' & SRDO.TYP().P<>'S') & PAR_SKID.get(202)<>'N'
|| _prevokro:=exec('find_prev_okro','fst',SRDO.OKRO_F().ROK,SRDO.OKRO_F().NR,1);
   _naliczona:=0;
   _np:=_nf:=_nd:=0;
   {? _prevokro<>null
   || OKRO_F.cntx_psh(); SRST.cntx_psh(); SRST.index('SROD');
      {? OKRO_F.seek(_prevokro)
      || SRST.prefix(SRSR.ref(),OKRO_F.ROK,OKRO_F.ref());
         {? SRST.first() & SRST.NAL='T'
         || _naliczona:=1;
            _np:=SRST.NETP; _nf:=SRST.NETF;
            {? FINFO.TOR_D='T' || _nd:=SRST.NETD ?}
         |? SRST.first() & SRST.NAL<>'T'
         || _naliczona:=0
         |? ~SRST.first()
         || _naliczona:=2
         ?}
      ?};
      OKRO_F.cntx_pop(); SRST.cntx_pop()
   || _naliczona:=2
   ?};
   {? SRDO.UMOP<=SRDO.WARP & SRDO.UMOF<=SRDO.WARF & ((FINFO.TOR_D='T' & SRDO.UMOD<=SRDO.WARD) | FINFO.TOR_D<>'T')
   || _wy:=''
   || {? _naliczona=0
      || FUN.info('Aby wprowadzić umorzenie większe od wartości, amortyzacja dla środka musi być naliczona\n'@+
                  'w okresie poprzedzającym okres obowiązywania dokumentu.'@); _wy:='UMOP'
      |? _naliczona=1
      || {? (SRDO.UMOP>_np)
         || _wy:='UMOP'
         |? (SRDO.UMOF>_nf)
         || _wy:='UMOF'
         |? (FINFO.TOR_D='T' & SRDO.UMOD>_nd)
         || _wy:='UMOD'
         ?}
      |? _naliczona=2
      || {? (SRDO.UMOP>SRST.NETP)
         || _wy:='UMOP'
         |? (SRDO.UMOF>SRST.NETF)
         || _wy:='UMOF'
         |? (FINFO.TOR_D='T' & SRDO.UMOD>SRST.NETD)
         || _wy:='UMOD'
         ?}
      ?};
      {? _wy<>'' & _naliczona
      || FUN.info('Wartość umorzenia nie może przekraczać wartości netto środka w okresie obowiązywania dokumentu.'@)
      ?}
   ?}
?};

:: amortyzacja jednorazowa do 100 tys.
{? SRDO.TYP().P='+'
|| {? _wy='' & SRDO.WARP100>0 & SRDO.SRSR().AMOR100<>'T'
   || FUN.emsg('Wartość amortyzacji jednorazowej w dokumentcie wartościowym można wproadzić tylko dla środków '
               'wprowadzonych z amortyzacją jednorazową.');
      _wy:='WARP100'
   ?};
   {? _wy='' & SRDO.WARP100<0
   || FUN.emsg('Wartość amortyzacji jednorazowej w dokumentcie wartościowym nie może być mniejsza od zera.');
      _wy:='WARP100'
   ?};
   {? _wy='' & SRDO.WARP100>SRDO.WARP
   || FUN.emsg('Wartość amortyzacji jednorazowej w dokumentcie wartościowym nie może być większa od wartości podatkowej.');
      _wy:='WARP100'
   ?};
   {? _wy='' & (SRDO.WARP100+SRDO.SRSR().WARP100+SRDO.SRSR().ZALP100)>100000
   || FUN.emsg('Wartość amortyzacji jednorazowej (razem dla środka i w bieżącym dokumencie) nie może być większa '
               'od 100 tys. zł.');
      _wy:='WARP100'
   ?};
   {? _wy='' & SRDO.WARP100>0 & SRDO.SRSR().DE~1<>SSTALE.AR().POCZ_ROK~1
   || FUN.emsg('Wartość amortyzacji jednorazowej można wprowadzić tylko dla nowych środków trwałych '
               'w pierwszym roku ich eksploatacji.');
      _wy:='WARP100'
   ?}
?};

:: kontrola czy nie próba zmniejszenia wartości środka jednorazowo umarzanego już po jego umorzeniu
::{? _wy='' & SRDO.WARP<0 & SRST.MP().K='UJP' & SRDO.Z<>'T' &
::  ((SRDO.OKRO_F().RES=SRST.SRSR().ROK & SRDO.OKRO_F().OES>(SRST.SRSR().OKRES+SRDO.MP().MP)) | SRDO.OKRO_F().RES>SRST.SRSR().ROK)
::|| FUN.emsg('Zmniejszenie wartości środka jednorazowo umarzanego już po jego umorzeniu nie jest możliwe.'@);
::   _wy:='DO'
::?};
::{? _wy='' & SRDO.WARF<0 & SRST.MF().K='UJF' & SRDO.Z<>'T' &
::  ((SRDO.ROK=SRST.SRSR().ROK & SRDO.OKRES>(SRST.SRSR().OKRES+SRDO.MF().MP)) | SRDO.ROK>SRST.SRSR().ROK)
::|| FUN.emsg('Zmniejszenie wartości środka jednorazowo umarzanego już po jego umorzeniu nie jest możliwe.'@);
::   _wy:='DO'
::?};
::{? FINFO.TOR_D='T'
::|| {? _wy='' & SRDO.WARD<0 & SRST.MD().K='UJD' & SRDO.Z<>'T' &
::      ((SRDO.ROK=SRST.SRSR().ROK & SRDO.OKRES>(SRST.SRSR().OKRES+SRDO.MD().MP)) | SRDO.ROK>SRST.SRSR().ROK)
::   || FUN.emsg('Zmniejszenie wartości środka jednorazowo umarzanego już po jego umorzeniu nie jest możliwe.'@);
::      _wy:='DO'
::   ?}
::?};
:: kontrola wartości niezgodnych z charakterem dokumentu (przychodowy/rozchodowy)
{? _wy=''
|| {? SRDO.TYP().P='+'
   || {? SRDO.TYP().WARP='T' & SRDO.WARP<0 || _wy:='WARP' ?};
      {? _wy='' & SRDO.TYP().UMOP='T' & SRDO.UMOP<0 || _wy:='UMOP' ?};
      {? _wy='' & SRDO.TYP().WARF='T' & SRDO.WARF<0 || _wy:='WARF' ?};
      {? _wy='' & SRDO.TYP().UMOF='T' & SRDO.UMOF<0 || _wy:='UMOF' ?};
      {? FINFO.TOR_D='T'
      || {? _wy='' & SRDO.TYP().WARD='T' & SRDO.WARD<0 || _wy:='WARD' ?};
         {? _wy='' & SRDO.TYP().UMOD='T' & SRDO.UMOD<0 || _wy:='UMOD' ?}
      ?}
   |? SRDO.TYP().P='-'
   || {? SRDO.TYP().WARP='T' & SRDO.WARP>0 || _wy:='WARP' ?};
      {? _wy='' & SRDO.TYP().UMOP='T' & SRDO.UMOP>0 || _wy:='UMOP' ?};
      {? _wy='' & SRDO.TYP().WARF='T' & SRDO.WARF>0 || _wy:='WARF' ?};
      {? _wy='' & SRDO.TYP().UMOF='T' & SRDO.UMOF>0 || _wy:='UMOF' ?};
      {? FINFO.TOR_D='T'
      || {? _wy='' & SRDO.TYP().WARD='T' & SRDO.WARD>0 || _wy:='WARD' ?};
         {? _wy='' & SRDO.TYP().UMOD='T' & SRDO.UMOD>0 || _wy:='UMOD' ?}
      ?}
   ?};
   {? _wy<>''
   || {? SRDO.TYP().P='+' || _typ:='przychodowy'@ || _typ:='rozchodowy'@ ?};
      FUN.emsg('Wprowadzone wartości w dokumencie nie są zgodne z charakterem\n'
               'dokumentu ( dokument %1).'@[_typ])
   ?}
?};
{? _wy='' & SRDO.WARP>0 & SRST.SRSR().SAMOCHOD='T'
|| exec('czytaj','#stalesys',,FINFO,'LIMIT_OS');
   exec('czytaj','#stalesys',,FINFO,'LIMIT_OE');
   _prz_lim:=0;
   {? SRST.SRSR().DE<date(2019,1,1) & SRST.NETP=0
   || {? SRST.SRSR().WARP+SRDO.WARP>(FINFO.LIMIT_EU*SRSR.KURS_EUR)
      || _prz_lim:=1
      ?}
   || _limit:={? SRST.SRSR().SAM_EL='N' || FINFO.LIMIT_OS
              |? SRST.SRSR().SAM_EL='T' || FINFO.LIMIT_OE
              ?};
      {? _limit>0 & (SRST.SRSR().WARP+SRDO.WARP)>_limit
      || _prz_lim:=1
      ?}
   ?};
   {? _prz_lim
   || FUN.emsg('Wprowadzona wartość podatkowa może spowodować przekroczenie limitu '
               'dla samochodów osobowych.'@);
      _wy:='WARP'
   ?}
?};
:: kontrola naliczenia amortyzacji przy skreśleniu lub korekcie ujemnej lub zbyciu w grupie
{? _wy='' & (SRDO.Z='T' | SRDO.TYP().P='-' | SRDO.TYP().P='S')
|| _ref:=SRST.SRSR;
   _rok:=SRDO.OKRO_F().RES;
   _okr:=SRDO.OKRO_F().OES;
   SRST.cntx_psh();
   SRST.index('PODAT');
   SRST.prefix(_ref);
   {? SRST.find_key(_rok,_okr)
   || {! |?
         {? SRST.prev()
         || {? SRST.OKRO_F().POCZ<>date(0,0,0) || _dalej:=0 || _dalej:=1 ?}
         || _dalej:=0
         ?};
         _dalej
      !};
      {? SRST.OKRO_F().POCZ<>date(0,0,0) & SRST.NAL<>'T'
      || FUN.emsg('Nie naliczono amortyzacji dla środka do ostatniego okresu eksploatacji. Likwidacja środka (lub korekta ujemna)\n'
                  'będzie możliwa po naliczeniu amortyzacji do okresu poprzedzającego okres obowiązywania dokumentu.'@);
         _wy:='DO'
      ?}
   ?};
   SRST.cntx_pop()
?};
:: kontrola czy istnieją dokumenty wartościowe o okresie obowiązywania równym lub późniejszym
:: niż właśnie wprowadzany dokument likwidacji środka
{? _wy='' & SRDO.Z='T'
|| _ref:=SRST.SRSR;
   _rok:=SRDO.OKRO_F().RES;
   _okr:=SRDO.OKRO_F().OES;
   SRDO.cntx_psh();
   SRDO.index('SRDO');
   SRDO.prefix(_ref);
   _loop:=1;
   {? SRDO.first()
   || {! |?
         {? SRDO.TYP().RODZ='W' & (SRDO.ROK>_rok | (SRDO.ROK=_rok & SRDO.OKRES>_okr))
         || _loop:=0
         ?};
         _loop & SRDO.next()
      !}
   ?};
   SRDO.cntx_pop();
   {? _loop=0
   || FUN.emsg('Dla bieżącego środka istnieją już dokumenty wartościowe obowiązujące\n'
               'po okresie likwidacji. Nie można zlikwidować środka we wskazanym okresie.'@);
      _wy:='DO'
   ?}
?};
:: kontrola czy dokument likwidacji likwiduje pełną wartość środka
{? SRST.GRP='T' || {? SRDO.SRSR_E<>null() || _grp:=1 || _grp:=0 ?} || _grp:=1 ?};
{? _wy='' & SRDO.Z='T' & _grp
|| _ref:=SRST.SRSR;
   _rok:=SRDO.OKRO_F().RES;
   _okr:=SRDO.OKRO_F().OES;
   SRST.cntx_psh();
   SRST.index('PODAT');
   SRST.prefix(_ref);
   {? SRST.find_key(_rok,_okr)
   || {? SRST.OKRO_F<>SRST.SRSR().OKRO_F || SRST.prev() ?};
      {? (SRDO.WARP+SRST.WARP<>0) || _wy:='WARP' ?};
      {? _wy='' & (SRDO.WARF+SRST.WARF<>0) || _wy:='WARF' ?};
      {? _wy='' & FINFO.TOR_D='T' & (SRDO.WARD+SRST.WARD<>0) || _wy:='WARD' ?}
   ?};
   SRST.cntx_pop();
   {? _wy<>''
   || FUN.emsg('Dokument likwidacji powinien zlikwidować całą wartość środka\n'
               'lub elementu składowego w poszczególnych torach amortyzacji.'@)
   ?}
?};
:: kontrola wpływu zmiany wartości w kolejnych okresach
{? _wy=''
|| _ujemna:='';
:: jeżeli poprawianie dokumentu to podczytanie wartości sprzed poprawiania
   {? _>0 & _a=1
   || SRDO.cntx_psh();
      SRDO.get();
      _s_warp:=SRDO.WARP;
      _s_umop:=SRDO.UMOP;
      _s_warf:=SRDO.WARF;
      _s_umof:=SRDO.UMOF;
      _s_ward:=SRDO.WARD;
      _s_umod:=SRDO.UMOD;
      SRDO.cntx_pop()
   || _s_warp:=_s_umop:=_s_warf:=_s_umof:=_s_ward:=_s_umod:=0
   ?};
   _ref:=SRST.ref();
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRSR.ref());
   _m_amop:=_m_amof:=_m_amod:=0;
   {? SRST.find_key(SRDO.ROK_F,SRDO.OKRO_F) & SRST.OKRO_F<>SRST.SRSR().OKRO_F
   || {! |?
         {? SRST.ROK=SRDO.ROK & SRST.OKRES=SRDO.OKRES
         || {? SRST.AMOP>0 || SRST.UMOP-=SRST.AMOP; SRST.NETP+=SRST.AMOP; _m_amop:=SRST.AMOP ?};
            {? SRST.AMOF>0 || SRST.UMOF-=SRST.AMOF; SRST.NETF+=SRST.AMOF; _m_amof:=SRST.AMOF ?};
            {? FINFO.TOR_D='T' & SRST.AMOD>0 || SRST.UMOD-=SRST.AMOD; SRST.NETD+=SRST.AMOD; _m_amod:=SRST.AMOD ?}
         || {? _m_amop>0 || SRST.UMOP-=_m_amop; SRST.NETP+=_m_amop ?};
            {? _m_amof>0 || SRST.UMOF-=_m_amof; SRST.NETF+=_m_amof ?};
            {? FINFO.TOR_D='T' & _m_amod>0 || SRST.UMOD-=_m_amod; SRST.NETD+=_m_amod ?}
         ?};

         {? SRDO.TYP().WARP='T' & ((SRST.WARP-_s_warp)+SRDO.WARP<0) || _ujemna:='WARP' ?};
         {? _ujemna='' & SRDO.TYP().WARF='T' & ((SRST.WARF-_s_warf)+SRDO.WARF<0) || _ujemna:='WARF' ?};
         {? _ujemna='' & FINFO.TOR_D='T' & SRDO.TYP().WARD='T' & ((SRST.WARD-_s_ward)+SRDO.WARD<0) || _ujemna:='WARD' ?};
         {? _ujemna='' & SRDO.TYP().UMOP='T' & ((SRST.UMOP-_s_umop)+SRDO.UMOP<0) || _ujemna:='UMOP' ?};
         {? _ujemna='' & SRDO.TYP().UMOF='T' & ((SRST.UMOF-_s_umof)+SRDO.UMOF<0) || _ujemna:='UMOF' ?};
::         {? _ujemna='' & SRDO.TYP().UMOP='T' & (((SRST.WARP-_s_warp)+SRDO.WARP-(SRST.OSTATEK+SRDO.OSTATEK))-((SRST.UMOP-_s_umop)+SRDO.UMOP)<0)
::         || _ujemna:='UMOP'
::         ?};
::         {? _ujemna='' & SRDO.TYP().UMOF='T' & (((SRST.WARF-_s_warf)+SRDO.WARF-(SRST.OSTATEK+SRDO.OSTATEK))-((SRST.UMOF-_s_umof)+SRDO.UMOF)<0)
::         || _ujemna:='UMOF'
::         ?};
         {? _ujemna='' & FINFO.TOR_D='T' & SRDO.TYP().UMOD='T' & ((SRST.UMOD-_s_umod)+SRDO.UMOD<0) || _ujemna:='UMOD' ?};
::         {? _ujemna='' & & FINFO.TOR_D='T' & SRDO.TYP().UMOD='T' & (((SRST.WARD-_s_ward)+SRDO.WARD)-((SRST.UMOD-_s_umod)+SRDO.UMOD)<0)
::         || _ujemna:='UMOD'
::         ?};
         {? _ujemna='' & SRDO.TYP().OSTATEK='T' & (SRST.OSTATEK+SRDO.OSTATEK<0) || _ujemna:='OSTATEK' ?};
         {? _ujemna='' & SRDO.TYP().OKE='T' & (SRST.OKE+SRDO.OKE<0) || _ujemna:='OKE' ?};
         {? _ujemna='' & SRDO.TYP().MAX='T' & (SRST.MAX+SRDO.MAX<0) || _ujemna:='MAX' ?};
         _ujemna='' & SRDO.Z<>'T' & SRST.next()
      !}
   ?};
   SRST.cntx_pop();
   {? _ujemna<>'' & _ujemna<>'DEKRET'
   || FUN.emsg('Wprowadzenie dokumentu spowoduje powstanie ujemnej wartości środka w jednym z kolejnych okresów\n'
              +'(lub zmniejszenie poniżej wartości rezydualnej).'@);
      _wy:=_ujemna
   ?}
?};
:: kontrola czy zmiana nie dotyczy zadekretowanej amortyzacji
{? _wy=''
|| _dekret:=0;
   SRST.cntx_psh();SRSR.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRSR.ref());
   VAR.OKRO_F:=null;
   {? SRDO.Z='T' & (SRDO.SRSR().MF().PDM='T' | SRDO.SRSR().MP().PDM='T' | (FINFO.TOR_D='T' & SRDO.SRSR().MD().PDM='T'))
      & (SRDO.OKRO_F().POCZ>SRDO.DO)
   || VAR.OKRO_F:=exec('find_prev_okro','fst',SRDO.OKRO_F().ROK,SRDO.OKRO_F().NR)
   ?};
   _t_okr:={? VAR.OKRO_F || VAR.OKRO_F || SRDO.OKRO_F ?};
   _t_rok:={? VAR.OKRO_F || VAR.OKRO_F().ROK || SRDO.ROK_F ?};
   {? SRST.find_key(_t_rok,_t_okr)
   || {! |?
         {? SRST.DEKRET='T' || _dekret:=1 ?};
         _dekret=0 & SRST.next()
      !}
   ?};
   SRST.cntx_pop();SRSR.cntx_pop();
   {? _dekret=1
   ||  FUN.emsg('Amortyzacja w okresie obowiązywania dokumentu lub w późniejszym została już\n'
                'zadekretowana dla co najmniej jednego toru amortyzacji. Usuwanie amortyzacji\n'
                'lub wprowadzanie zmian mogących wpłynąć na amortyzację nie jest możliwe.\n'
                '(W przypadku likwidacji środków amortyzowanych metodą uwzględniającą dni amortyzacja\n'
                'nie może być także zadekretowana w miesiącu zgodnym z datą obowiązywania dokumentu.)'@);
      _wy:='DO'
   ?}
?};
:: kontrola czy zmniejszenie wartości nie zmniejszy wartości poniżej dofinansowania jeżeli takie jest
::{? _wy='' & SRDO.TYP().P='-' & SRDO.WARP<0 & SRDO.Z<>'T'
::|| SRZF.cntx_psh();
::   SRZF.prefix(SRDO.SRSR);
::   _srzf:=0;
::   {? SRZF.first()
::   || {! |?
::         _srzf+=SRZF.WARP;
::         SRZF.next()
::      !}
::   ?};
::   SRZF.cntx_pop();
::   {? _srzf>(SRST.WARP+SRDO.WARP)| _srzf>(SRST.WARF+SRDO.WARF)
::      | (FINFO.TOR_D='T' & _srzf>(SRST.WARP+SRDO.WARP))
::   || FUN.emsg('Wprowadzenie dokumentu spowodowałoby zmniejszenie wartości środka poniżej wartości\n'
::               'dofinansowań i ulg przypisanych do środka. Dodanie dokumentu nie jest możliwe.'@);
::      _wy:='WARP'
::   ?}
::?};

:: kontrola czy dokument zbycia w obrębie grupy kapitałowej i czy wskazano firmę docelową - tylko ostrzeżenie
{? _wy='' & FINFO.SPR_GRP='T' & SRDO.TYP().P='S' & SRDO.FIRMA_D=null
|| {? ~FUN.ask('W dokumencie zbycia środka w obrębie grupy kapitałowej nie wskazano\n'
               'firmy docelowej - firmy do której środek jest sprzedawany. Kontynuować?'@)
   || _wy:='FIRMA_D'
   ?}
?};
{? _wy='' & FINFO.SPR_GRP='T' & SRDO.TYP().P='S' & SRDO.FIRMA_D=REF.FIRMA
|| FUN.emsg('W dokumencie zbycia środka w obrębie grupy kapitałowej wskazano '
            'bieżącą firmę jako firmę docelową.\n Należy wskazać dowolną firmę grupy '
            'poza bieżącą lub pozostawić puste pole.'@);
   _wy:='FIRMA_D'
?};
{? _wy='' & FINFO.SPR_GRP='T' & SRDO.TYP().P='S' & SRDO.FIRMA_D().SYMBOL='000'
|| FUN.emsg('W dokumencie zbycia środka w obrębie grupy kapitałowej wskazano '
            'firmę 000 (grupę kapitałową) jako firmę docelową.\n Należy wskazać dowolną zwykłą firmę grupy '
            'lub pozostawić puste pole.'@);
   _wy:='FIRMA_D'
?};
SRSR.cntx_pop();
_wy


\set_field
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Ustawia wartość i dostępność pól w oknie dokumentu zgodnie z definicją typu
::   WE: jeżeli _a i _a=1 to tylko dostępność pól
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=0 ?};
{? FINFO.TOR_D='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'WARD');
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'UMOD');
{? SRDO.win_edit('?')='RED_WS'
|| SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'WARSP_D')
?};
{? _a=0
|| SRDO.P:=SRDO.TYP().P;
   SRDO.PRZYCHOD:=SRDO.TYP().PRZYCHOD;
   SRDO.ROZ:=SRDO.TYP().ROZ;
   SRDO.Z:=SRDO.TYP().Z
?};
{? SRDO.TYP().WARP='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'WARP');
{? SRDO.TYP().WARF='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'WARF');
{? SRDO.TYP().UMOP='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'UMOP');
{? SRDO.TYP().UMOF='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'UMOF');
{? SRDO.TYP().OSTATEK='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'OSTATEK');
{? SRDO.TYP().WP='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'WP');
{? SRDO.TYP().OKE='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'OKE');
{? SRDO.TYP().MAX='N' || _enable:=0 || _enable:=1 ?};
SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'MAX');
1


\be_srdo_przychod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRDO.PRZYCHOD
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.TYP().P='+' || 1 || 0 ?}


\be_srdo_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRDO.ROZ
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.TYP().P='-' || 1 || 0 ?}


\be_srdo_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRDO.P
::----------------------------------------------------------------------------------------------------------------------
0


\be_srdo_z
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRDO.Z
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.TYP().P='-' || 1 || 0 ?}


\be_srdo_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRDO.K
::----------------------------------------------------------------------------------------------------------------------
0


\ae_srdo_warp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła po redakcji pola SRDO.WARP
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.TYP().WARF='T' & SRDO.WARF=0 || SRDO.WARF:=SRDO.WARP ?};
{? FINFO.TOR_D='T' & SRDO.TYP().WARD='T' & SRDO.WARD=0 || SRDO.WARD:=SRDO.WARP ?};
1

:Sign Version 2.0 jowisz:1045 2024/02/28 13:15:18 80032713eb9e5bd9cda31ca403a7c5b290ba90dfd1b048e1dc15182316e032ed59572a53bd8ecf51102f8aa982bdae9d3dc3a7c049b72ad7f13afb06d31696b58e361deff1bbfe76aeb0582cf502735d938ac7fe07925eb52b6b0b02829dbec934cab15df5863c56962192809d5ce1287f269b14461e080c39ccd60e85fe7828

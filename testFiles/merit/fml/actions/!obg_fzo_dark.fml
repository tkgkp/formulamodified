:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obg_fzo_dark.fml
:: Utworzony: 13.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBG_FZO_DARK - Rejestracja faktury w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rejestracja faktury w obiegu - główna formuła czynności OBG_FZO_DARK
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_edokum','obiegi')
::# access=exec('uprawnienia_rej','obiegi')

:: PARAMETRY WE:
::# kind=WE, symbol=FORM_PRZ, type=STRING, name=Formuła wykonywana przy kończeniu rejestracji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_WAL, type=STRING, name=Formuła walidacyjna przy kończeniu rejestracji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_ADD, type=STRING, name=Formuła wykonywana po wprowadzeniu nagłówka dokumentu, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=ED_PODZ, type=STRING, name=Edycja podziałów controllingowych, required=N, fml_val="exec('czy_ed_podz','obiegi')"
::# kind=WE, symbol=ED_INND, type=STRING, name=Edycja informacji dodatkowych, required=N, fml_val="exec('czy_inn_dan','obiegi')"
::# kind=WE, symbol=ED_PROJ, type=STRING, name=Edycja projektów, required=N, fml_val="exec('czy_ed_proj','obiegi')"
::# kind=WE, symbol=TYP_DOK, type=STRING, name=Typ dokumentu, required=N, fml_val="exec('wybor_typu','obiegi',1,_a)", fml_exp="exec('etypy_fml_exp','obiegi2',1,_a)"
::# kind=WE, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N, keyref=T
::# kind=WE, symbol=KONT_BUD, type=STRING, name=Kontrola budżetu, required=N, fml_val="exec('kont_bud','obiegi',_a)"
::# kind=WE, symbol=POZF_WYM, type=STRING, name=Wymagane wypełnienie pozycji faktury przy rejestracji, required=N, fml_val="exec('czy_wympozf','obiegi_akc')"
::# kind=WE, symbol=POZV_WYM, type=STRING, name=Wymagane wypełnienie pozycji VAT przy rejestracji, required=N, fml_val="exec('czy_wympozv','obiegi_akc')"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WY, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WY, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WY, symbol=EM_NAD, type=STRING, name=Adres e-mail użytkownika rejestrującego (nadawca), required=N
::# kind=WY, symbol=EM_ODB, type=MEMO, name=Adres e-mail użytkownika rejestrującego (odbiorca), required=N
::# kind=WY, symbol=EM_NAG, type=STRING, name=Nagłówek wiadomości e-mail, required=N
::# kind=WY, symbol=BUDZ_COND, type=NUMBER, name=Budżet przekroczony warunkowo?, required=N

:: WARUNKI DLA BRAM
::# condition=Sprzedaż - faktura,   act_uid=,   auto=N,  formula=exec('rodz_doklog','obiegi_log',_a.EDOKUM,'LSP_FAK')
::# condition=Sprzedaż - zaliczka,  act_uid=,   auto=N,  formula=exec('rodz_doklog','obiegi_log',_a.EDOKUM,'LSP_ZAL')
::# condition=Sprzedaż - korekta,   act_uid=,   auto=N,  formula=exec('rodz_doklog','obiegi_log',_a.EDOKUM,'LSP_KOR')
::# condition=Zakup - faktura,      act_uid=,   auto=N,  formula=exec('rodz_doklog','obiegi_log',_a.EDOKUM,'LZK_FAK')
::# condition=Zakup - zaliczka,     act_uid=,   auto=N,  formula=exec('rodz_doklog','obiegi_log',_a.EDOKUM,'LZK_ZAL')
::# condition=Zakup - korekta,      act_uid=,   auto=N,  formula=exec('rodz_doklog','obiegi_log',_a.EDOKUM,'LZK_KOR')

:: WŁAŚCIWOŚCI CZYNNOŚCI:
::# properties=~RUNMICRO,TODOTRIG,~RELEASE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
params_exec('ustaw_bprel','obiegi');
zak_rej_ed:=0;
budz_cond:=0;
:: czynność startowa
{? _mp.pathProc()
|| menu_txt(,'Dołącz');
   _mp.trigRef('EDOKUM');
   exec('init','obg');
   params_exec('dol_dob','obiegi',2);
   {? zak_rej_ed & EDOKUM.get()
   || params_exec('ustaw_out','!obg_fzo_dark',_wy,'A');
      _mp.save(,_wy);
      _mp.done()
   ?};
      exec('del_zm','obg')
:: dołącz z pulpitu
|? _akcja='Dołącz'
|| menu_txt(,'Dołącz');
   _mp.trigRef('EDOKUM');
   params_exec('dol_dob','obiegi',1);
   {? zak_rej_ed & EDOKUM.get()
   || params_exec('ustaw_out','!obg_fzo_dark',_wy,'A');
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _akcja='Dol_dokum'
|| menu_txt(,'Dołącz');
   _mp.trigRef('EDOKUM');
   exec('init','obg');
   params_exec('dol_edok','obiegi2');
   {? zak_rej_ed & EDOKUM.get()
   || params_exec('ustaw_out','!obg_fzo_dark',_wy,'A');
      _mp.save(,_wy);
      _mp.done()
   ?}
:: popraw z pulpitu
|? _akcja='Popraw'
|| menu_txt(,'Popraw');
   params_get().context.EDOKUM; _ref:=EDOKUM.ref();
   params_exec('pop_dob','obiegi',1);
   {? zak_rej_ed & EDOKUM.seek(_ref)
   || params_exec('ustaw_out','!obg_fzo_dark',_wy,'A');
      statnoz2:=1;
      _mp.save(,_wy);
      _mp.done()
   ?}
:: usuń z pulpitu
|? _akcja='Usuń'
|| menu_txt(,'Usuń');
   params_get().context.EDOKUM;
   {? params_exec('us_dob','obiegi',1)
   || {? var_pres('STATUS',_we) & _we.STATUS='O'
      || _mp.error()
      || _mp.cancel()
      ?}
   ?}
:: zakończ z pulpitu
|? _akcja='Zakończ'
|| params_get().context.EDOKUM;
   EDOKUM.cntx_psh();
   exec('zak_rej_dok','obiegi');
   EDOKUM.cntx_pop();
   {? zak_rej_ed & EDOKUM.get()
   || params_exec('ustaw_out','!obg_fzo_dark',_wy,'A');
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _akcja='Zamknij'
|| params_get().context.EDOKUM;
   EDOKOS.use('skid_y'+(EDOKUM.name()+2));
   exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
   params_exec('zamknij','obiegi_akc');
   {? zak_rej_ed & EDOKUM.get()
   || params_exec('ustaw_out','!obg_fzo_dark',_wy,'Z');
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _mp.pathTodo() | _akcja='Pop_dokum'
|| menu_txt(,'Popraw');
   _w1:=0;
   {? (_w1:=var_pres('EDOKUM',_wy)>0) | var_pres('EDOKUM',_we)>0
   || {? _w1=0
      || _wy.EDOKUM:=_we.EDOKUM
      ?};
      _edokum:={? _w1 || _wy.EDOKUM || _we.EDOKUM ?};
      _ref:=BB.sqlint($_edokum); _nam:=form(($_edokum)-8);
      {? _ref<>0 & _nam<>''
      || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
         {? EDOKUM.seek(_ref,_nam)
         || _ar:=SSTALE.AR;
            SSTALE.AR:=EDOKUM.ROK_F;
            exec('maski_w','obiegi',1);
            exec('init','obg');
            exec('rkprz','obiegi');
            params_exec('pop_dob','obiegi',2);
            SSTALE.AR:=_ar;
            {? zak_rej_ed & EDOKUM.get()
            || params_exec('ustaw_out','!obg_fzo_dark',_wy,'A');
               statnoz2:=1;
               _mp.save(,_wy);
               _mp.done()
            |? _akcja='Pop_dokum'
            || _mp.save(,_wy);
               _mp.keep()
            ?}
         ?};
         EDOKUM.cntx_pop()
      ?}
   || FUN.info('Nie znaleziono dokumentu.'@);
      _wy.EDOKUM:=null;
      _wy.STATUS:='Z';
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _akcja='Businesslink'
|| {? var_pres('EDOKUM',_we)=type_of(null()) & _we.EDOKUM
      &
      (var_pres('EDOKUM',_wy)<>type_of(null()) | _wy.EDOKUM=null())
   || _wy.EDOKUM:=_we.EDOKUM;
      _mp.save(,_wy)
   ?};
   _mp.keep()
|? _akcja='DołączMWA'
|| {? PAR_SKID.get(450)<>'N'
   || _webdok:=_args.context.WEB_DOK;
      WEB_DOK.cntx_psh();
      WEB_DOK.index('IDADD');
      WEB_DOK.prefix(_webdok);
      {? WEB_DOK.first()
      || _edok:=exec('add_rek','mwa_dok');
         {? _edok<>null() & EDOKUM.seek(_edok)
         ||
            params_exec('ustaw_out','!obg_fzo_dark',_wy,'A');
            _mp.save(,_wy);
            _mp.done()
         ?}
      ?};
      WEB_DOK.cntx_pop()
   ?};
   1
?};
{? _akcja='Dol_dokum' | _akcja='Pop_dokum'
|| VAR_DEL.delete('budz_cond')
|| VAR_DEL.delete('zak_rej_ed','budz_cond')
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ rejestrację faktury w obiegu'@@;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
_wy:=_mp.load(exec('kind_out','#b_port'));
_we:=_mp.load(exec('kind_in','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));
_edok:=null;
{? var_pres('EDOKUM',_wy)
|| _edok:=_wy.EDOKUM
|? var_pres('EDOKUM',_we)
|| _edok:=_we.EDOKUM
?};
{? var_pres('zakres')<=0 || zakres:=1 ?};
{? _edok
|| _ref:=BB.sqlint($_edok); _nam:=form(($_edok)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      EDOKOS.cntx_psh(); EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      EDOKPAR.cntx_psh(); EDOKPAR.use('skidh_'+(EDOKUM.name()+2));
      {? EDOKUM.seek(_ref,_nam)
      || KH.cntx_psh();
         {? EDOKUM.KHKH
         || _desc:='Zakończ rejestrację faktury w obiegu: %1, data sprzedaży %2, otrzymano %3, brutto: %4, kontrahent: %5'@@[EDOKUM.SYM,$EDOKUM.DOP,$EDOKUM.DO,form(EDOKUM.WART,,2,' ,'),EDOKUM.KHKH().SKR]
         || _desc:='Zakończ rejestrację faktury w obiegu: %1, data sprzedaży %2, otrzymano %3, brutto: %4'@@[EDOKUM.SYM,$EDOKUM.DOP,$EDOKUM.DO,form(EDOKUM.WART,,2,' ,')]
         ?};
         KH.cntx_pop();
         {? _set
         || params_exec('ustaw_bprel','obiegi');
            {? var_press('prz_act')<=0 || exec('dol_edokos','obiegi',_we) ?};
            EDOKOS.cntx_psh();
            EDOKOS.index('SZUK'); EDOKOS.prefix(EDOKUM.ref(),EDOKUM.USERS,'W');
            {? EDOKOS.first()
            || _bprel:=EDOKOS.B_PREL; _bprels:=EDOKOS.B_PRELS;
               EDOKOS.index('SZUK2'); EDOKOS.prefix(EDOKUM.ref(),'W');
               {? EDOKOS.size()>1 & EDOKOS.first()
               || {! |?
                     EDOKOS.B_PREL:=_bprel; EDOKOS.B_PRELS:=_bprels; EDOKOS.put();
                     EDOKOS.next()
                  !}
               ?}
            ?};
            EDOKOS.cntx_pop()
         ?};
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set); EDOKUM.get()
      ?};
      {? var_pres('STATUS',_we)>0 & _we.STATUS='O' & _akcja<>'Zakończ' & _akcja<>'Zamknij' &
         var_pres('statnoz1')<=0 & var_pres('statnoz2')<=0 & _set
      || params_exec('ustaw_bprel','obiegi');
         {? OBIEGI.B_PREL<>''
         || EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
            {? EDOKOS.first()
            || {! |?
                  {? EDOKOS.WID<>'S'
                  || EDOKOS.STATUS:='O'; EDOKOS.put()
                  ?};
                  EDOKOS.next()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?}
      ?};
      EDOKOS.cntx_pop(); EDOKUM.cntx_pop(); EDOKPAR.cntx_pop()
   ?}
?};
VAR_DEL.delete('statnoz2');
_desc


\ustaw_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych
::   WE: _a - obiekt wyjsciowy
::       _b - 'A' - akceptacja
::            'Z' - zamknięcie
::----------------------------------------------------------------------------------------------------------------------
_a.EDOKUM:=EDOKUM.ref();
_a.WAL:=EDOKUM.WAL;
_a.NETTO:=EDOKUM.NETTO;
_a.BRUTTO:=EDOKUM.WART;
_a.DEKR:=EDOKUM.TYP().DEKR;
_a.STATUS:=_b;
USERS.cntx_psh(); USERS.prefix();
_a.EM_NAD:=_a.EM_ODB:=OPERATOR.USER().EMAIL;
_a.EM_NAG:='Zarejestrowano fakturę: '+EDOKUM.SYM+', data sprzedaży '+$EDOKUM.DOP+', otrzymano '+$EDOKUM.DO;
_a.BUDZ_COND:=budz_cond;
USERS.cntx_pop();
EDOKOS.use('skid_y'+(EDOKUM.name()+2));
exec('rkprz1','obiegi',1);
{? OBIEGI.EDOKOS
|| OBIEGI.EDOKOS();
   {? exec('szuk_edokpar','obiegi')
   || _a.B_PREL:=EDOKPAR.B_PREL;
      _a.B_PRELS:=EDOKPAR.B_PRELS
   ?}
?}


\add_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? exec('chk_okres','obiegi2',1,0)>=0
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBG_FZO_DARK';
   _params.AKCJA:='Dołącz';
   _params.PROC_START:='T';
   exec('mp_run','#b__box',_params)
?}


\pop_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('zakres')>0 & zakres=3
|| popdokks:=1;
   params_exec('pop_dob','obiegi',1);
   zakres:=3; VAR_DEL.delete('popdokks')
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBG_FZO_DARK';
   _params.AKCJA:='Popraw';
   _params.UIDREF:=EDOKUM.uidref();
   _params.CONTEXT:=obj_new('EDOKUM');
   _params.CONTEXT.EDOKUM:=EDOKUM.ref();
   exec('mp_run','#b__box',_params)
?}


\del_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUM.ZAM='T'
|| {? exec('chk_role','#b__box',OPERATOR.USER,'OBG_FZO_ZAMK')
   || exec('us_dob','obiegi',1)
   || FUN.emsg('Brak uprawnień do usuwania zamkniętych dokumentów.'@)
   ?}
|| _params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='OBG_FZO_DARK';
_params.AKCJA:='Usuń';
_params.UIDREF:=EDOKUM.uidref();
_params.CONTEXT:=obj_new('EDOKUM');
_params.CONTEXT.EDOKUM:=EDOKUM.ref();
exec('mp_run','#b__box',_params)
?}


\fak_przek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zakończenie faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
_zakr:={? var_pres('zakres')>0 || zakres || 0 ?};
statnoz1:=1;
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='OBG_FZO_DARK';
_params.AKCJA:='Zakończ';
_params.UIDREF:=EDOKUM.uidref();
_params.CONTEXT:=obj_new('EDOKUM');
_params.CONTEXT.EDOKUM:=EDOKUM.ref();
exec('mp_run','#b__box',_params);
{? _zakr || zakres:=_zakr ?}; VAR_DEL.delete('statnoz1')


\dolvatdo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Dolaczanie pozycji VAT
::  OLD: \dolvatdo/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EVAT.blank();
{? EVAT.edit("exec('chk_vat','!obg_fzo_dark')") || EVAT.add() ?};
exec('sum_vat','obiegi',1);
1


\popvatdo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Poprawianie pozycji VAT
::  OLD: \popvatdo/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EVAT.edit("exec('chk_vat','!obg_fzo_dark')") || EVAT.put() ?};
exec('sum_vat','obiegi',1);
1


\usvatdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Usuwanie pozycji VAT
::  OLD: \usvatdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
exec('sum_vat','obiegi',1);
1


\chk_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji rekordu w tabeli EVAT
::  OLD: \chk_vat/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? EVAT.PR=null
|| FUN.info('Należy wybrać procent VAT.'@); _zwrot:='PR'
|? EVAT.NETTO=0 & EVAT.VAT=0 & EVAT.BRUTTO=0
|| FUN.info('Przynajmniej jedna z pozycji: Netto, VAT lub Brutto\nmusi być różna od zera'@); _zwrot:='NETTO'
|? (EVAT.NETTO+EVAT.VAT)$2<>EVAT.BRUTTO$2
|| _roznica:=EVAT.NETTO$2+EVAT.VAT$2-EVAT.BRUTTO;
   {? _roznica<0 || _roznica:=-_roznica ?};
   {? app_info('web_sesid') <> ''
   || _zwrot:='NETTO+VAT<>BRUTTO'
   || _ask:='Wartości pozycji netto+VAT nie zgadzają się z'
            '\nwartością pozycji brutto na kwotę: %1'
            '\nnetto        = %2'
            '\nvat            = %3'
            '\nnetto+vat = %4'
            '\nbrutto       = %5'
            '\nAkceptować?\n'@[form((_roznica$2)$2,,2,' ,'),form(EVAT.NETTO$2,16,2,' ,'),
            form(EVAT.VAT$2,16,2,' ,'),form(EVAT.NETTO$2+EVAT.VAT$2,16,2,' ,'),form(EVAT.BRUTTO$2,16,2,' ,')];
      _zwrot:=FUN.ask(_ask)
   ?}
|| {? EVAT.VAT$2<>(0.01*EVAT.NETTO*exec('vat','dok_fks',EVAT.PR().KOD))$2
   ||
      {? app_info('web_sesid') <> ''
      || _zwrot:='KWOTA_VAT<>NETTO*VAT'
      ||
         _ask:='Wartość pozycji VAT nie zgadza się z procentem VAT. Akceptować?'@;
         _v:=FUN.choice(_ask,,'Tak'@,'Popraw'@);
         {? _v=0
         || _zwrot:='VAT'
         |? _v=2
         || _zwrot:='VAT'; exec('aeprdob','obiegi')
         ?}
      ?}
   ?}
?};
_zwrot


\dol_evat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Dołączanie pozycji VAT
::----------------------------------------------------------------------------------------------------------------------
exec('dolvatdo','!obg_fzo_dark');
exec('desc_update','#b__box',EDOKUM.uidref())


\pop_evat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Poprawianie pozycji VAT
::----------------------------------------------------------------------------------------------------------------------
exec('popvatdo','!obg_fzo_dark');
exec('desc_update','#b__box',EDOKUM.uidref())


\del_evat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Usuwanie pozycji VAT
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć bieżącą pozycję?'@) & EVAT.del(,1)
|| exec('usvatdob','!obg_fzo_dark');
   exec('desc_update','#b__box',EDOKUM.uidref())
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Podglad dokumentu w obiegu z listy zadań
::----------------------------------------------------------------------------------------------------------------------
_webterm:=+app_info('web_sesid');

_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
_rfd:=null();
{? _wy.EDOKUM <> ~~
|| _rfd:=_wy.EDOKUM
|| _in:=_mp.load(exec('kind_in','#b_port'));
   _rfd:=_in.EDOKUM
?};
{? _rfd <> ~~
|| {? _webterm
   || EDOKUM.use(8+$_rfd);
      EDOKUM.seek(_rfd);
      EDOKUM.web_cntx_save();
      EDOKUM.web_display('WF03',,"EDOKUM.web_cntx_load(); EDOKUM.web_eclose(); web_top_refresh(1)")
   || exec('dok_spac','obg',_rfd)
   ?}
|| FUN.info('Nie znaleziono dokumentu obiegu.')
?}


\edokum_miasto_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.14]
:: OPIS: przycisk pola EDOKUM.MIASTO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_msc_wybierz','teryt','EDOKUM','MIASTO')
?}


\edokum_ul_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.14]
:: OPIS: przycisk pola EDOKUM.UL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_uli_wybierz','teryt','EDOKUM','UL','MIASTO')
?}


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Trigger dla czynności OBG_FZO_DARK
::  WE: _a - 'add po', 'put przed', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'OBG_FZO_DARK')
?}


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zamknięty
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? 6+ref_name(_kref)='skid_v'
         || _edok:=exec('FindAndGet','#table',EDOKUM,_kref,,,null());
            {? _edok=null()
            || _mp.done()
            |? exec('FindAndGet','#table',EDOKUM,_kref,,"@.EDOKUM.ZAM='T'",1)
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1045 2023/12/21 13:22:33 ded7f41a0ce7b3d020ce37f50ac9ac03dda7a41173157f6c6b8704829a48a87f88edf213529820ac476345313036f45b7e465f6b9d2cfe46e9abc176bf4ede0c7cec6780a2314e5d1196232f80162e37d7ff91802e006aeaf10b1145a0d9f3687585888a4a4d8f34ae3ae886feead8cf37899b4d5b991f2ead635c8c750fd250

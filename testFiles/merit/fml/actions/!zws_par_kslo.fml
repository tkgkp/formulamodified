:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zws_par_kslo.fml [17.00]
:: Utworzony: 2015/01/26
:: Autor: DRO
::======================================================================================================================
:: Zawartość: Formuły czynności obsługi słowników użytkownika
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Formula glowna czynnosci obslugi slownikow uzytkownika
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
exec('select','!zws_par_kslo');
~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wywołanie okna z listą słowników
::----------------------------------------------------------------------------------------------------------------------
exec('ini_tsys','slo_slu');
SLU.index('NAZ');
SLU.prefix();
B.win_sel('WER');
KH.win_sel('WER_SLO');
OS.win_sel('WER');
US.win_sel('SEL');
WAL.win_sel('WALUTY');
WAL.win_dict('WALUTYF3');
WAL.win_edit('WALUTA');
WAL.win_patt('SZUK');
WOJEWODZ.win_sel('WOJEWODZ');
SLU.hdr_sel();
OSOBA.win_sel('SLO');
RS.index('RS');
RS.prefix();
SLU.win_sel('SLU_WER');
SLU.select();
VAR_DEL.delete('TT_TSYS','TT_PSYS');
''


\slu_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed Dołącz dla tabeli SLU w oknie SLU_WER
::  OLD: \slu_dol/wspol.fml
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
POMOC.SLUPOP:=0;
1


\slu_syst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [?????]
:: OPIS: Czy można poprawiać lub usuwać słownik systemowy SLU
::   WE: [_a - 0/1 czy wyświetlać komunikat]
::  OLD: \slu_syst/wspol.fml
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
POMOC.SLUPOP:=1;
{? SLU.SYSTEM='T'
|| {? _=0 | _a
   || FUN.info('Słownik systemowy, modyfikacje niedozwolone.'@)
   ?};
   0
|| 1
?}


\zawart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.40]
:: OPIS: Wyświetlanie zawartości słownika użytkownika
::   WE: _a=0 - ze SLU, _a=1 - ze SLUAPPL
::  OLD: \zawart/wspol.fml
::----------------------------------------------------------------------------------------------------------------------
KH.win_sel('WER_SLO');
OS.win_sel('WER');
WAL.win_sel('WALUTY'); WAL.win_dict('WALUTYF3');
WAL.win_edit('WALUTA'); WAL.win_patt('SZUK');
SKID_RBK.win_patt('SZUK');
ZL.win_sel('SLO');
SKID_RBK.win_sel('SLO_SLR');
B.win_sel('WER');
KRAJE.win_sel('SLO');
SLO.cntx_psh(); SLU.cntx_psh();
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ)
|| {? RS.TAB='SLO_OSOB' | RS.TAB='OS' | RS.TAB='OSOBA'
   || OSOBA.win_sel('WER'); OSOBA.win_dict('WER')
   |? RS.TAB='KH'
   || __KH_MOD:=1
   |? RS.TAB='ZL'
   || USERS.win_dict('SLO1')
   ?};
   {? RS.TAB='UD_SKL'
   || UD_SKL.index('SYMBOL'); UD_SKL.prefix(RS.UD_TYP);
      UD_SKL.win_sel('WYB')
   |? RS.TAB='UD_DEF'
   || UD_SKL.index('SYMBOL'); UD_SKL.prefix(RS.UD_TYP);
      UD_POZ.index('NUMER'); UD_POZ.prefix(RS.UD_SCH);
      UD_SKL.win_sel('WER')
   ?}
?};
SLO.win_sel('ONE'); SLO.win_dict('ONE');
__PAR188:=PAR_SKID.get(188)='T';

RS.cntx_pop();
KH.win_dict('UDTSLO');
KH.actions('UDTSLO','');
exec('slu_okn','slo_slu');
SLO.hdr_sel();
SLO.index('SL');
_act:='';
{? _a=0
|| SLO.prefix(SLU.ref()); SLO.hdr_sel(' %1'@[SLU.NAZ])
|| SLO.prefix(SLUAPPL.SLU); SLO.hdr_sel(' %1'@[SLUAPPL.SLU().NAZ]);
   ROK_F.seek(PARAM.ROK);
   {? 'CzęśćDziałRozdziałParagrafPozycja'*SLUAPPL.SLU().WZ
   || _wr1:=exec('tab_sl','ml_fiks','',SLUAPPL.SLU().WZ);
      _wr:=exec('tab_sl','ml_fiks',ROK_F.KOD,SLUAPPL.SLU().WZ);
      {? _wr1='-' | _wr*SLUAPPL.SLU().NAZ
      || 1
      || _act:='DPU:D'
      ?}
   ?}
?};
VAR_DEL.delete('tab','ref','find');
_wz:={? _a=0 || SLU.WZ || SLUAPPL.SLU().WZ ?};
{? _wz<>'~Płatności' | _wz<>'~Rozliczenie VAT' || _act:='A'+_act ?};
SLO.select();
VAR_DEL.delete('tab','ref','find');
SLO.hdr_sel();
SLO.cntx_pop(); SLU.cntx_pop();
VAR_DEL.delete('__KH_MOD','__PAR188')


\ten_slu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po Dołącz dla tabeli SLU, okno SLU_WER
::  OLD: \ten_slu/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
exec('sluappl_add','!zws_par_kslo');
wyb:=1;
SLO.index('SL');
SLO.prefix(SLU.ref);
SLO.hdr_sel;
SLO.hdr_sel(48+SLU.OP);
1


\sluappl_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła uzupełnia tabele SLUAPPL dla podanego słownika
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
SLUAPPL.cntx_psh();
SLUAPPL.index('NAZ');
SLUAPPL.prefix('F',SLU.NAZ,);
{? ~SLUAPPL.first()
|| SLUAPPL.blank();
   SLUAPPL.GDZIE:='F';
   SLUAPPL.SLU:=SLU.ref();
   SLUAPPL.add()
?};
SLUAPPL.cntx_pop()


\slu_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formuła po na akcje Usuń okienka SLU.WER
::  TAG: <SLOSLU>
::  OLD: \slu_del/wspol
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('ssize_s')>0;
SLUAPPL.cntx_psh();
{? ~SLU.r_lock(1,1)
|| {? ~_grp
   || FUN.info('Słownik redagowany przez innego użytkownika.\nUsunięcie niemożliwe.'@)
   || bledy_s+=1
   ?}
|| SLO.index('SL');SLO.prefix(SLU.ref());
   _is_slo:=SLO.size();
   {? _grp | {? _is_slo || FUN.ask('Czy usunąć bieżący słownik wraz z zawartością?'@) || FUN.ask('Czy usunąć bieżący słownik?'@) ?}
   || {? exec('slu_syst','!zws_par_kslo',~_grp)
      || _dl:=1;
         _cn_slu:=SLU.count();
         _is_sta:=exec('czy_stale','slo_slu', SLU.ref());
         {? _cn_slu>0 | _is_sta
         || SLUAPPL.index('REF'); SLUAPPL.prefix(SLU.ref());
            _is_sla:=SLUAPPL.size(); SLUAPPL.first();
            {? (_cn_slu>(_is_sla+_is_slo)) | _is_sta
            || _dl:=0;
               {? ~_grp
               || FUN.info('Słownik używany w innych tabelach lub stałych systemu.\nUsunięcie niemożliwe.'@)
               || bledy_s+=1
               ?}
            || _del_slo:=1;
               {? SLO.first()
               || {! |? _del_slo:=(SLO.count()=0); _del_slo & SLO.next() !}
               ?};
               {? _del_slo & (_is_slo | _is_sla)
               || do();
                  {? _is_sla || {! |? SLUAPPL.del() !} ?};
                  {? SLO.first() || {! |? SLO.del() !} ?};
                  end()
               ?};
               {? SLO.first()
               || _dl:=0;
                  {? ~_grp
                  || FUN.info('Nie udało się usunąć zawartości słownika.'
                              '\nUsunięcie słownika niemożliwe.'@)
                  || bledy_s+=1
                  ?}
               ?}

            ?}
         ?};
         {? _dl
         || {? ~SLU.del()
            || {? _grp || bledy_s+=1 ?}
            ?}
         ?}
      || {? _grp || bledy_s+=1 ?}
      ?}
   ?}
?};
SLUAPPL.cntx_pop()


\gpr_uslu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula przed na akcje grupowa Usun okienka SLU.WER
::  OLD: \gpr_uslu/wspol
::----------------------------------------------------------------------------------------------------------------------
exec('usun_bg','sprfin','SLU','Usunąć zaznaczone słowniki użytkownika?'@)


\gpo_uslu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula po na akcje grupowa Usun okienka SLU.WER
::  OLD: \gpo_uslu/wspol
::----------------------------------------------------------------------------------------------------------------------
exec('usun_ag','sprfin')


\powiazania
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PB] [8.50]
:: OPIS: Prezentacja powiazań słowników użytkownika
::       wywoływana w okienkach wertowania tabeli SLU oraz SLUAPPL
::   WE: _a - akronim tabeli
::  OLD: \powiazania/m
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
SLUAPPL.cntx_psh();
SLU.cntx_psh();
_find:=1;
{? _a='SLU'
|| _pom:=SLU.NAZ;
   SLUAPPL.index('NAZ');
   SLUAPPL.prefix('F');
   _find:=SLUAPPL.find_key(SLU.NAZ) & SLUAPPL.SLU().NAZ=_pom
?};
{? _find
|| _prefix:=SLUAPPL.SLU().NAZ;
    TMPPOW:=tab_tmp(5,'MIEJSCE','STRING[60]','Miejsce'@,
                      'FIRMA','STRING[4]','Firma'@,
                      'ROK','STRING[20]','Rok'@,
                      'KONTO','STRING[70]','Konto'@,
                      'POZIOM','STRING[6]','Poziom'@);
   _wer:=TMPPOW.mk_sel('Miejsca wykorzystania słownika: '@,,,'tmppow_wer');
   TMPPOW.win_fld(_wer,,'MIEJSCE',,,50,,,,,'Miejsce wykorzystania'@);
   TMPPOW.win_fld(_wer,,'FIRMA',,,,,,,,'Symbol firmy'@);
   TMPPOW.win_fld(_wer,,'ROK',,,,,,,,'Rok'@);
   TMPPOW.win_fld(_wer,,'KONTO',,,30,,,,,'Symbol i nazwa konta'@);
   TMPPOW.win_fld(_wer,,'POZIOM',,,,,,,,'Poziom'@);
   {? var_pres('cr')=-1 & var_pres('PRN')<=0
   || TMPPOW.win_act(_wer,0,'Formuła','Drukuj'@@,,'Drukowanie miejsc wykorzystania słownika'@,
                  "exec('ust_wyd','!zws_par_kslo'); exec('rep_exec','#b_report','ZWS_PAR_KSLO','wsp_tt',,,,,,,,'W'); exec('del_wyd','!zws_par_kslo'); 0",,,,,,'D')
   ?};
   TMPPOW.win_act(_wer,0,'Szukaj');
   TMPPOW.win_act(_wer,0,'Kolejność');
   TMPPOW.win_act(_wer,0,'Wyświetl',,,,"TMPPOW.display");
   _edit:=TMPPOW.mk_edit(,,'tmppow_red');
   TMPPOW.win_efld(_edit,,'MIEJSCE');
   TMPPOW.win_efld(_edit,,'ROK');
   TMPPOW.win_efld(_edit,,'KONTO',,,);
   TMPPOW.win_efld(_edit,,'POZIOM');
: sprawdzenie powiazań w budowie planu kont
   KS.cntx_psh();
   ROK_F.cntx_psh();
   BUD.cntx_psh();
   BUD.index('SLU');
   BUD.prefix(SLUAPPL.ref);
   {? BUD.first
   || {! |? BUD.KS();
            TMPPOW.blank;
            TMPPOW.MIEJSCE:='Budowa konta syntetycznego';
            TMPPOW.ROK:=KS.ROK().NAZ;
            TMPPOW.FIRMA:=ROK_F.FIRMA().SYMBOL;
            TMPPOW.KONTO:=KS.SYM+ ' - '+ KS.NAZ;
            TMPPOW.POZIOM:=form(BUD.POZ,6);
            TMPPOW.add;
            BUD.next
      !}
   ?};
   BUD.cntx_pop();
: sprawdzenie powiazań w wyróżnikach planu kont
   KS_W.cntx_psh();
   KS_W.index('SLUKS');
   KS_W.prefix(SLUAPPL.ref());
   {? KS_W.first
   || {! |? KS_W.S();
            TMPPOW.blank;
            TMPPOW.MIEJSCE:='Wyróżniki konta';
            TMPPOW.ROK:=KS.ROK().NAZ;
            TMPPOW.FIRMA:=ROK_F.FIRMA().SYMBOL;
            TMPPOW.KONTO:=KS.SYM+ ' - '+ KS.NAZ;
            TMPPOW.POZIOM:=form(KS_W.LP,6);
            TMPPOW.add;
            KS_W.next
      !}
   ?};
   KS.cntx_pop();
   ROK_F.cntx_pop();
   KS_W.cntx_pop();
: sprawdzanie powiazań w sprawozdaniach controllingowych
   SPR_K.cntx_psh();
   _ind_spr:=SPR_K.ndx_tmp('',1,'KOLUMNA',,0,'CZY_SLU',,0,'WYZNACZN',,0,'WYZNACZN',,0);
   SPR_K.index(_ind_spr); SPR_K.prefix('N',1,SLU.NAZ,SLU.NAZ);
   {? SPR_K.first()
   || {! |?
         TMPPOW.blank;
         TMPPOW.MIEJSCE:='Sprawozdanie wg analityk '+SPR_K.SPR_NAG().NAZWA;
         TMPPOW.KONTO:=TMPPOW.ROK:=TMPPOW.POZIOM:='';
         TMPPOW.add;
         SPR_K.next()
      !}
   ?};
   SPR_K.cntx_pop(); SPR_K.ndx_drop();
   TMPPOW.win_edit(_edit);
   TMPPOW.win_sel(_wer);
   TMPPOW.hdr_sel();
   TMPPOW.hdr_sel(SLU.NAZ);
   TMPPOW.select;
   obj_del(TMPPOW)
|| FUN.emsg('Słownik nie jest podpięty w słownikach użytkownika dla aplikacji.'@)
?};
SLUAPPL.cntx_pop();
SLU.cntx_pop()


\drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Wydruk słownika użytkownika
::  TAG: <SLOSLU>
::  OLD: \drukuj/wspol
::----------------------------------------------------------------------------------------------------------------------
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ)
|| {? RS.TAB='KH'
   || exec('rep_exec','#b_report','ZWS_PAR_KSLO','wsp_slkh',,,,,,,,'W')
   || exec('rep_exec','#b_report','ZWS_PAR_KSLO','wsp_slo',,,,,,,,'W')
   ?}
?};
RS.cntx_pop()


\pre_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [?????]
:: OPIS: przed redagowaniem wzorca, tabela SLU
::  OLD: \pre_wz/slo_slu
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
wz:=SLU.WZ;
1


\f3_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [?????]
:: OPIS: Formuła na klawisz F3 pola SLU.WZ
::  OLD: \f3_wz/slo_slu
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
RS.win_sel('SLO');
{? RS.select || RS.WZ || fld ?}


\por_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [?????]
:: OPIS: po red. SLU.WZ
::   WY: 1-pole dobrze wypełnione; 0-wpp
::  TAG: <SLOSLU>
::  OLD: \por_wz/slo_slu
::----------------------------------------------------------------------------------------------------------------------
{? SLU.WZ='' || fld('prosty') ?};
RS.index('RS'); RS.prefix();
{? fld<>wz & POMOC.SLUPOP
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(SLU.ref);
   {? SLO.first()
   || FUN.info('Słownik nie jest pusty.\n Zmiana wzorca nie jest dopuszczalna.'@);
      fld(wz);
      _wyn:=0
   || {? ~RS.find_key(SLU.WZ)
      || FUN.info('Niezdefiniowany wzorzec.'@); fld('prosty');
         _wyn:=0
      || fld(RS.WZ);
         {? (RS.TAB='UD_SKL' | RS.TAB='UD_DEF') & RS.UD_TYP().DLUGOSC
         || SLU.DL:=RS.UD_TYP().DLUGOSC
         ?};
         _wyn:=1
      ?}
   ?};
   SLO.cntx_pop
|| {? ~RS.find_key(SLU.WZ)
   || FUN.info('Niezdefiniowany wzorzec.'@);
      fld('prosty');
      _wyn:=0
   || {? RS.WZ='Kraj'
      || FUN.info('Wzorzec "Kraj" zastrzeżony dla słownika systemowego.'@);
         _wyn:=0
      || fld(RS.WZ);
         {? (RS.TAB='UD_SKL' | RS.TAB='UD_DEF') & RS.UD_TYP().DLUGOSC
         || SLU.DL:=RS.UD_TYP().DLUGOSC
         ?};
         _wyn:=1
      ?}
   ?}
?};
:: odświeżenie okienko na potrzeby pola SLU.KDK (wyszarzone dla wzorców innych niż prosty)
win_disp();
_wyn


\bd_slu_dl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [$1210]
:: OPIS: Formuła przed wyświetleniem pola SLU.DL wyszaża pole jeżeli ustawione brak kontroli długości kodu
::  OLD: \bd_slu_dl/slo_slu
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? SLU.KDK=0 || 1 || exec('findfnv','#color') ?}


\pre_dl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [??]
:: OPIS: przed redagowaniem długosci tabela SLU
::  OLD: \pre_dl/slo_slu
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
wz:=SLU.DL;
{? SLU.WZ<>''
|| RS.index('RS'); RS.prefix();
   {? RS.find_key(SLU.WZ) & (RS.TAB='UD_SKL' | RS.TAB='UD_DEF') & RS.UD_TYP().DLUGOSC<>0 || 0 || 1 ?}
|| 1
?}


\pore_dl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [??]
:: OPIS: po redkacji SLU.DL
::  OLD: \pore_dl/slo_slu
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
:: dodatkowy test długości w przypadku wersji wielofirmowej i słowników opartych na wzorcu z tabela lokalną
_par300233:=exec('get','#params',300233,2);
{? REF.WFIRM & _par300233='N'
|| _dalej:=0;
   RS.cntx_psh();
   RS.index('RS');
   RS.prefix();
   {? RS.find_key(SLU.WZ)
   || {? RS.TAB<>''
      || {? exec('czy_tab_glob','#table',RS.TAB,1)=0
         || _dalej:=1
         ?}
      ?}
   ?};
   RS.cntx_pop();
   {? _dalej & fld<4
   || FUN.emsg('Długość kodu w przypadku systemu wielofirmowego nie może być\n'
              'mniejsza niż 4 (trzy znaki zarezerowane są na symbol firmy).'@);
      return(0)
   ?}
?};
{? fld<>wz & POMOC.SLUPOP=1
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(SLU.ref());
   {? SLO.first()
   || FUN.info('Słownik nie jest pusty.\n Zmiana długości kodu nie jest dopuszczalna.'@);
      fld(wz); _wyn:=0
   || _wyn:=1
   ?};
   SLO.cntx_pop()
|| _wyn:=1
?};
_wyn


\bd_slu_kdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [$12.10]
:: OPIS: Przed wyświetleniem pola SLU.KDK
::  OLD: \bd_slu_kdk/skid_slu
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? ~SLU.KDK
|| SLU.efld_opt(SLU.win_edit('?'),'mark=1,enable=1',SLU,'DL')
|| SLU.efld_opt(SLU.win_edit('?'),'mark=0,enable=0',SLU,'DL')
?};
(SLU.WZ='prosty') & ~(SLU.index('?')='KDK_NAZ' & SLU.cur_prfx()='0')


\be_slu_kdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [$12.10]
:: OPIS: Przed redakcją pola SLU.KDK
::  OLD: \be_slu_kdk/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? (SLU.WZ='prosty') & ~(SLU.index('?')='KDK_NAZ' & SLU.cur_prfx()='0')
|| _ok:=1;
   {? -menu_txt()='popraw'
   || _pom:=SLU.NAZ;
      SLU.cntx_psh();
      SLUAPPL.cntx_psh();
      SLUAPPL.index('NAZ');
      SLUAPPL.prefix('F');
      {? SLUAPPL.find_key(SLU.NAZ) & SLUAPPL.SLU().NAZ=_pom
      || BUD.cntx_psh();
         BUD.index('SLU');
         BUD.prefix(SLUAPPL.ref);
         {? BUD.first
         || FUN.info('Słownika użyto do budowy planu kont.'
                     '\nZmiana kontroli długości kodu zabroniona.'@);
            _ok:=0
         ?};
         BUD.cntx_pop()
      ?};
      SLUAPPL.cntx_pop();
      SLU.cntx_pop()
   ?}
|| _ok:=0
?};
_ok


\ae_slu_kdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [$12.10]
:: OPIS: Po redakcji pola SLU.KDK. Formuła sprawdza czy są zgodne długosci kodów z ustaloną długoscia w nagłówku słownika.
::  OLD: \ae_slu_kdk/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? -menu_txt()='popraw' & SLU.KDK=0
|| SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(SLU.ref());
   {? SLO.first()
   || _dl:=(+SLO.KOD);
      {!|? {? +SLO.KOD<>_dl || _ok:=0 ?}; _ok & SLO.next() !};
      {? _ok=0
      || FUN.info('W słowniku występują kody o długości innej niż zadeklarowana.'
                  '\nWłączenie kontroli długości kodu zabronione.'@);
         SLU.KDK:=1
      || SLU.DL:=_dl
      ?}
   ?};
   SLO.cntx_pop()
|? SLU.KDK=1
|| SLU.DL:=0
?};
_ok


\pocz_slu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wartosc poczatkowa pola SLO.SLU
::  OLD: \pocz_slu/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| exec('czytaj','#stalesys');
   _acr:=web_top_fld_acr(0);
   {? _acr='DEL_CEL'
   || XINFO.SLU_CEL()
   |? _acr='WAL'
   || FINFO.SLWAL().SLU()
   |? _acr='KH'
   || _dynamic:=exec('get_wt_par','obiegi2','dynamic');
      {? _dynamic=1
      || _flds:=web_def_get();
         SKID.SL_KH:=_flds.SL_KH
      ?};
      {? +SKID.SL_KH || SLU.index('NAZ'); SLU.prefix(SKID.SL_KH,); SLU.first() ?}
   |? _acr='RWYD'
   || XINFO.SLU_WYD()
   |? _acr='INNEDANE'
   || XINFO.SLU_INNY()
   |? _acr='SV'
   || SLU.index('NAZ'); SLU.prefix();SLU.find_key('~STAWKI VAT PL')
   ?}
?};

SLU.ref()


\wz_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Ustawia wzorzec dla SLO.KOD (duże litery) gdy dotyczy słownika walut
::  OLD: \wz_slo/wzorce
::----------------------------------------------------------------------------------------------------------------------
_wzorzec:='';
RS.cntx_psh(); RS.index('RS'); RS.prefix();
RS.blank(); RS.WZ:=SLU.WZ;
{? RS.find_rec() & (RS.TAB='WAL' | RS.TAB='KH') || _wzorzec:='u!' ?};
RS.cntx_pop();
_wzorzec


\por_dlkd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: Formuła po redakcji pola SLO.KOD
::  OLD: \por_dlkd/skid_kh
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
SLO.SLU();
{? -menu_txt()<>'szukaj'
|| {? SLU.KDK=0 & +SLO.KOD>SLU.DL
   || FUN.info('Za długi kod.\nWymagana ilość znaków w kodzie: %1.'@[$SLU.DL]); 0
   |? SLU.KDK=0 & +SLO.KOD<SLU.DL
   || FUN.info('Za krótki kod.\nWymagana ilość znaków w kodzie: %1.'@[$SLU.DL]); 0
   || 1
   ?}
|| 1
?}


\slo_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dodawanie rekordu do słownika użytkownika
::  OLD: \slo_dol/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('czy_sys','!zws_par_kslo') & exec('czy_wcon','ml_zad')
|| exec('pw_kodsl','slo_slu',{? SLO.f_active>0 || SLO.f_size || SLO.size ?}>0);
   {? exec('spr_wz','slo_slu',SLU.WZ)
   || {? RS.find_key(SLU.WZ)
      || {? RS.TAB='KH'
         || ($(RS.TAB+'.prefix(2)'))()
         |? RS.TAB='SKID_RBK'
         || RACHBANK.AKTYWNY:='T';
            exec('rb_aktywne','rachunki',REF.INFO,0);
            ROZNE.KKBAN:=1;
            PAR_WYDR.TABAKR:=REF.INFO; PAR_WYDR.REF:=null;
            SKID_RBK.win_sel('SLO_SLR')
         |? RS.TAB='OSOBA'
         || OSOBA.win_sel('WER1')
         |? RS.TAB='SLO_OSOB'
         || SLO_OSOB.index('ID'); SLO_OSOB.prefix(RS.F_ZATR);
            exec('sel_slo_osob','!zws_par_kslo')
         |? RS.TAB='US'
         || US.win_sel('SEL')
         |? RS.TAB='ZL'
         || ZL.win_sel('SLO')
         |? RS.TAB='UDT'
         || UDT.win_sel('WER_SLO')
         |? RS.TAB='PROJEKTY'
         || PROJEKTY.win_sel('WER_SLO')
         |? RS.TAB='FIRMA'
         || FIRMA.win_sel('WER_SLO')
         ?};
         {? RS.TAB='UD_DEF'
         || _sym:=exec('ud_def_wybierz','schemat',RS.UD_TYP().SYMBOL,RS.UD_SCH().SYMBOL,,,,1);
            UD_DEF.cntx_psh(); UD_DEF.prefix();
            {? UD_DEF.seek(_sym.REF)
            || UD_DEF.UD_SKL();
               exec('ten_slo_s','slo_slu',0)
            ?};
            UD_DEF.cntx_pop()
         |? RS.TAB='UD_SKL'
         || _sym:=exec('ud_skl_wybierz','schemat',RS.UD_TYP().SYMBOL,,,,1);
            UD_SKL.cntx_psh(); UD_SKL.prefix();
            {? UD_SKL.seek(_sym.REF)
            || exec('ten_slo_s','slo_slu',0)
            ?};
            UD_SKL.cntx_pop()
         || sl_ref:=SLU.ref();
            _a:=RS.TAB+'.select(,1';
            g_filtr:=3;
            _bez:={? exec('ps_65','rachunki')='T' || 'N' || 'C' ?};
            _a:=_a+{? RS.TAB='KH'||',,\''+_bez+'\')' || ')' ?};
            VAR_DEL.delete('sl_ref');
            {? RS.TAB='KH'
            || VAR_DEL.delete('__KH_MOD');
               KH.win_sel('WER_SLO');
::Początek modyfikacji dla Maclex Fiks 14-10-2009 AK [1120]
              {? RS.TAB<>''
              || {? SLO.KOD=''
                 || ($(RS.TAB+'.first'))()
                 |? (type_of(($(RS.TAB+'.'+RS.KOD))())=2)|(type_of(($(RS.TAB+'.'+RS.KOD))())=3)
                 || ($(RS.TAB+'.find_tab(,\''+RS.KOD+'\',,\'=\',\''+SLO.KOD+'\')'))()
                 || ($(RS.TAB+'.find_tab(,\''+RS.KOD+'\',,\'=\','+SLO.KOD+')'))()
                 ?}
              ?}
::Koniec modyfikacji dla Maclex
            ?};
         _wyn:='';
         _acrs:={? Plugin.runnable('RS_TSYS_001') || Plugin.run('RS_TSYS_001') || '' ?};
         _sel:=RS.TAB+'.win_sel(';
         {? _acrs<>''
         || STR.split(_acrs,',');
            {! |?
               _acr:=STR.get_word();
               {? RS.TAB=_acr
               || VAR_DEL.delete('wyn');
                  wyn:=_wyn:=($(_acr+'.mk_sel(,,1)'))();
                  ($(_acr+'.win_act(wyn,0,\'Formuła\',\'Wybierz\',,,,"exec(\'ten_slo\',\'slo_slu\')",1,,,,\'W\',)'))()
               ?};
               {? _wyn=''
               || _tmp:=STR.get_word();
                  STR.next()
               || _sel:=_sel+'\''+_wyn+'\')';
                  ($(_sel))();
                  0
               ?}
            !}
         ?};
            _okn_ed:=SLO.win_edit('?');
            ($(_a))();
            {? _okn_ed<>'' || SLO.win_edit(_okn_ed) ?};
            {? RS.TAB='KH' || __KH_MOD:=1 ?};
            {? MLEX.FIKSB & ~(9+menu_pth()='SFST:7:0:') & ~('!F!'*(1+menu_pth())) & exec('conn','ml_zad','GLOBAL','FILTR',SSTALE.AR().KOD)='F' &
               'CzęśćDziałRozdziałParagrafPozycjaDziałanieFunkcjaZadaniePodzadanie'*SLU.WZ>0
            || exec('filtr_slo_bud','ml_xpert', fld())
            ?};
            {? var_pres('slo_ref')>0 || SLO.seek(slo_ref) ?}
         ?};
         VAR_DEL.delete('g_filtr','slo_ref')
      || FUN.info('Błędnie zdefiniowany słownik - nie istnieje wzorzec %1.'@[SLU.WZ])
      ?}
   || SLO.blank();
      {? SLO.edit("exec('spr_slo','slo_slu')" ) || SLO.add() ?}
   ?};
   exec('tab_f_rfresh','slo_filtr',SLO);
   VAR_DEL.delete('wyn')
?};
1


\pop_tab_o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL [7.60]
:: OPIS: Formuła podmieniająca treść we wszystkich słownikach użytkownika, w których został użyty poprawiany rekord
::       tabeli OSOBY
::   WE: [_a - akronim tabeli, z poziomu której poprawiamy zapis]
::  OLD: \pop_tab/skid_oso
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
US.win_dict('SEL_DIC');
_rek_pop:=_mod_pr:=_edycja:=0;
OSOBA.cntx_psh();
OSOBA.win_edit('RED');
exec('win_edit_osoba','slo_slu');
{? ~{? _ & _a='OS_POP'
   || {? OS.OSOBA(); OSOBA.KALI='T'  || _mod_pr:=1; 0 ?}
   |? _
   || ($(RS.TAB+".index('"+RS.KOD+"')"))();
      {? RS.TAB='SLO_OSOB' || SLO_OSOB.prefix(RS.F_ZATR) || ($(RS.TAB+".prefix()"))() ?};
      _ok:=~{? ($(RS.TAB+".find_key(exec('kod2id','slo_slu'))"))()
           || {? OSOBA.seek(($(RS.TAB+".OSOBA"))())
              || ~{? OSOBA.KALI='T' || _mod_pr:=1; 0 ?}
              ?}
           ?};
      _ok
   || 0
   ?} &
   {? ~_mod_pr
   || FUN.ask('Zmiana będzie dotyczyła wszystkich słowników użytkownika.\nKontynuować?'@)
   || 1
   ?}
|| OSOBA.bl_file('ZDJECIE',1);
   {? _mod_pr | OSOBA.edit("__CHK.record(OSOBA,,'NAZWISKO','PIERWSZE')")
   || {? (_file:=OSOBA.bl_file('ZDJECIE'))<>''
      || OSOBA.bl_put('ZDJECIE', _file)
      ?};
      RS.cntx_psh();
      _rs_ndx:=RS.ndx_tmp(,0,'TAB',,);
      RS.index(_rs_ndx);
      do();
      _ok:=1;
      {? OSOBA.put()
      || _edycja:=1
      ?};
      {? ~_ok || undo() ?};
      end();
      RS.ndx_drop(_rs_ndx);
      RS.cntx_pop()
   ?};
   OSOBA.bl_file('ZDJECIE',1)
?};
{? _mod_pr
|| FUN.info('Dana pozycja jest używana w systemie kadrowo-płacowym.'
            +'\nNa podstawie tego systemu uaktualniono dane\nwe wszystkich słownikach użytkownika.'
            +'\nModyfikacje zabronione.'@
           )

?};
OSOBA.cntx_pop();
exec('tab_f_rfresh','slo_filtr',SLO);
1


\slo_up
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Usuwanie rekordu do słownika użytkownika
::  OLD: \slo_up/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć bieżący wiersz?'@)
|| exec('slo_del','slo_slu')
?}


\slo_pop1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [7.60]
:: OPIS: Formuła do poprawiania rekordu w tabeli SLO przy redakcji poziomów analityki
::  OLD: \slo_pop/synt
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
_okn:=SLO.win_edit('?');
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLO.SLU().WZ)  & (RS.TAB='SLO_OSOB' | RS.TAB='OS' | RS.TAB='OSOBA')
|| exec('pop_tab_o','!zws_par_kslo',RS.TAB)
|| exec('slo_pop','!zws_par_kslo')
?};
RS.cntx_pop();
SLO.win_edit(_okn)


\slo_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [7.60]
:: OPIS: Formuła wyświetla liste zdefiniowanych parametrów dla wybranego rekordu ze słownika
::       jeżeli zdefiniowano dla wzorca parametry.
::       Wołana w definicji na akcje parametry podczas wertowania rekordów słownika.
::       Po akcji z formuły SLO_PAR.
::   WY: 1 - funkcja paramterów dostępna, 0 - wpp
::  OLD: \slo_par/skid_slu
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
SLUAPPL.cntx_psh();
{? SLO.SLU=XINFO.SLP || _a:=0 ?};
ZR_DSLO.cntx_psh();
{? _=1 & _a || ZR_DSLO.win_sel('WER'+$_a) || ZR_DSLO.win_sel('WER') ?};
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLO.SLU().WZ)
|| ZR_DSLO.index('WZ_NR');
   ZR_DSLO.prefix(RS.ref);
   {? ZR_DSLO.first()
   || ZR_DSLO.select();
      _ok:=1
   || FUN.info('Dla rekordu ze słownika opartego na wzorcu: %1'
               '\nfunkcja nie jest dostępna.'@[RS.WZ])
   ?}
?};
RS.cntx_pop();
ZR_DSLO.cntx_pop(); SLUAPPL.cntx_pop();
_ok


\slo_actions_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: NN [12.10]
:: OPIS: Zwraca blokowane akcje w SLO w zależności od wzorca
::----------------------------------------------------------------------------------------------------------------------
_actions:='';
{? SLU.NAZ='' || SLU.seek(SLU.ref()) ?};
{? 1+SLU.NAZ='~' | (var_pres('slo_noact')>0 & slo_noact=1)
|| _actions:='ERCBAODPUFMJ';
   ZR_DSLO.cntx_psh(); ZR_DSLO.index('WZ_NR'); ZR_DSLO.prefix(RS.ref);
   _actions+={? ZR_DSLO.first() || 'W(UDNSAOKBRZ)' || 'W' ?};
   ZR_DSLO.cntx_pop();
   _actions+=':D'
|? -SLU.WZ='prosty' | -SLU.WZ='~rozliczenie vat'
|| _actions:='ERCBAOFMJ';
   ZR_DSLO.cntx_psh(); ZR_DSLO.index('WZ_NR'); ZR_DSLO.prefix(RS.ref);
   _actions+={? ZR_DSLO.first() || 'W(UDNSAOKBRZH)' || 'W' ?};
   ZR_DSLO.cntx_pop()
|? -SLU.WZ='~płatności' | -SLU.WZ='~powody korekt'
|| _actions:='ERCBAOFMJ';
   ZR_DSLO.cntx_psh(); ZR_DSLO.index('WZ_NR'); ZR_DSLO.prefix(RS.ref);
   _actions+={? ZR_DSLO.first() || 'W(UDNSAOKBRZH)' || 'W' ?};
   ZR_DSLO.cntx_pop()
|| RS.cntx_psh(); RS.index('RS'); RS.prefix(SLU.WZ,);
   {? RS.first()
   || {? RS.TAB='KH'
      || _actions:={? exec('ps_65','rachunki')='T' || 'ERCBPAMJW(PR)' || 'ERCBPAMJW(UPR)' ?}
      |? RS.TAB='US'
      || _actions:='ERCBPOMJW(UDPSAOKBRZ)'
      |? RS.TAB='OSOBA'
      || _actions:='EPCBAOMJW(UDPSAKBRZ)'
      |? RS.TAB='OS' | RS.TAB='SLO_OSOB'
      || _actions:='ERCBAOMJW(HUDPSAKBRZ)'
      |? RS.TAB='UD_SKL' & RS.UD_TYP<>null & RS.UD_TYP().SYMBOL='PROJ_REM'
      || _actions:='ERCBAOMJW(UDPNSAOKBZ)'
      |? RS.TAB='B'
      || _actions:='RCAPOMJW'+{? SLO.win_sel('?')<>'BANKI' || 'E' || '' ?}
      |? RS.TAB='SKID_RBK'
      || _actions:='ERPBAOMJW'
      |? RS.TAB='ZL'
      || _actions:='EPRCBAOMJW'
      |? RS.TAB='WAL' | RS.TAB='KRAJE'
      || _actions:='ERCBAOMJW'
      |? RS.TAB='UD_SKL' | RS.TAB='UD_DEF'
      || _actions:='PRCBAOMJW'
      |? RS.TAB='UDT'
      || _actions:='PRCBAOWEJ'
      |? RS.TAB='PROJEKTY'
      || _actions:='PRCBAOWEM'
      |? RS.TAB='FIRMA'
      || _actions:='RCBAOWEMJ'
      || _acrs:={? Plugin.runnable('RS_TSYS_001') || Plugin.run('RS_TSYS_001') || '' ?};
         _next:=1;
         {? _acrs<>''
         || STR.split(_acrs,',');
            {! |?
               _acr:=STR.get_word();
               {? RS.TAB=_acr || _actions:='POABCERMJWS'; _next:=0 ?};
               _tmp:=STR.get_word();
               STR.next() & _next
            !}
         ?}
      ?};
      ZR_DSLO.cntx_psh(); ZR_DSLO.index('WZ_NR'); ZR_DSLO.prefix(RS.ref);
      {? ZR_DSLO.first()
      || {? _actions*'W('
         || _tabtmp:=spli_str(_actions);
            _actions:='';
            _czyn:=0;
            {! _i:=1..obj_len(_tabtmp)
            |! {? _czyn=0 & _tabtmp[_i]='W' & _tabtmp[_i+1]='('
               || _czyn:=1
               ?};
               {? _tabtmp[_i]=')'
               || _czyn:=0
               ?};
               {? _czyn & _tabtmp[_i]='P'
               || _tabtmp[_i]:=''
               ?};
               _actions+=_tabtmp[_i]
            !}
         |? _actions*'W'
         || _actions:=gsub(_actions,'W','W(DNUSHAOKBRT)')
         || _actions+='W(DNUSHAOKBRT)'
         ?}
      ?};
      ZR_DSLO.cntx_pop()
   ?};
   RS.cntx_pop()
?};
{? SLU.ref()<>XINFO.SLP || _actions+='W(T)' ?};
_actions


\init_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Okienko przed tabeli SLO
::----------------------------------------------------------------------------------------------------------------------
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ)
|| _tab:={? RS.TAB='UD_DEF' || 'UD_SKL' || RS.TAB ?};
   {? RS.TAB<>'' || _tpsh:=$(_tab+'.cntx_psh()'); _tpsh() ?}
?};
RS.cntx_pop();
exec('init_szukaj','!zws_par_kslo');
UD_SKL.win_edit('RED'); UD_SKL.win_patt('WZO');
__actsl[BUF_IND][1]:=exec('slo_actions_bl','!zws_par_kslo');
_okn:=SLO.win_sel('?');
{? _okn='ONE_SEL' || __actsl[BUF_IND][1]:=gsub(__actsl[BUF_IND][1],'Z','') ?};
__actsl[BUF_IND][2]:=SLO.actions(SLO.win_sel('?'),__actsl[BUF_IND][1],
                                 {? _okn='ONE_SEL' | _okn='SPR_SEL'
                                 || 'Z:D'
                                 |? _okn='EDKON' | _okn='K_WYR1' | _okn='SLO_FO'
                                 || 'I:D'
                                 |? _okn='ODS'
                                 || 'J:D'
                                 |? _okn='WYR'
                                 || 'T:D'
                                 |? _okn='BANKI'
                                 || 'E:D'
                                 || 'D:D'
                                 ?},1
                                );
1


\done_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Okienko po tabeli SLO
::----------------------------------------------------------------------------------------------------------------------
{? BUF_IND=1
|| SLO.actions('ONE',,,1);
   SLO.actions('ONE_SEL',,,1)
|? BUF_IND>1
|| SLO.actions('ONE',__actsl[BUF_IND-1][1],__actsl[BUF_IND][2],1)
?};
exec('done_szukaj','!zws_par_kslo');
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ)
|| _tab:={? RS.TAB='UD_DEF' || 'UD_SKL' || RS.TAB ?};
   {? RS.TAB<>'' || _tpop:=$(_tab+'.cntx_pop()'); _tpop() ?}
?};
RS.cntx_pop();
1


\init_szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [2010]
:: OPIS: Formuła na 'okienko przed' dla okienek tabeli SLO
::  OLD: \init/szukaj
::----------------------------------------------------------------------------------------------------------------------
szukajkh:=0;
__rb_szuk:=2;
SLUAPPL.cntx_psh();
{? var_pres('BUF_IND')<=0 | BUF_IND<=0
|| BUF_IND:=1;
   VAR_DEL.delete('bRef');
   {? var_pres('ref')>0 || bRef:=ref ?};
   VAR_DEL.delete('tab','ref','find')
|| BUF_IND+=1
?};
{? var_pres('tab')<=0 || tab:=obj_new(50) ?};
{? var_pres('ref')<=0 || ref:=obj_new(50) ?};
{? var_pres('find')<=0 || find:=obj_new(50) ?};
{? var_pres('__actsl')<=0
|| __actsl:=obj_new(50);
   {! _i:=1..50 |! __actsl[_i]:=obj_new(2) !}
?};

tab[BUF_IND]:='SLO';
ref[BUF_IND]:=null; find[BUF_IND]:=0;
RS.index('RS'); RS.prefix();
{? exec('spr_wz','slo_slu',SLU.WZ) & RS.find_key(SLU.WZ)
|| _tab:={? RS.TAB='UD_DEF' || 'UD_SKL' || RS.TAB ?};
   ($('BUF'+$BUF_IND+':=obj_new(@.CLASS.BUFFER,'+_tab+')'))();
   {? _tab='KH'
   || __KH_MOD:=1
   |? _tab='ZL'
   || SLO.win_edit('ZLECENIA'); SLO_AKC:=SLO.actions(SLO.win_sel('?'),'P',,1)
   ?};
   RS.cntx_psh();
   _c:=$(_tab+'.blank(1)'); _c();
   ($('BUF'+$BUF_IND+'.save()'))();
   tab[BUF_IND]:=_tab
|| ($('BUF'+$BUF_IND+':=obj_new(@.CLASS.BUFFER,''SLO'')'))();
   SLO.blank(1);
   ($('BUF'+$BUF_IND+'.save()'))()
?};
exec('slu_okn','slo_slu');
1


\done_szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [2010]
:: OPIS: Formuła na 'okienko po' dla okienek tabeli SLO
::  OLD: \done/szukaj
::----------------------------------------------------------------------------------------------------------------------
_rs_tab:=RS.TAB;
{? _rs_tab='KH' & var_pres('__KH_MOD')>0 & __KH_MOD || VAR_DEL.delete('__KH_MOD') ?};
SLUAPPL.cntx_pop();
ref[BUF_IND]:=null;   find[BUF_IND]:=0;
($('obj_del(BUF'+$BUF_IND+')'))();
{? tab[BUF_IND]<>'SLO' || RS.cntx_pop() ?};
tab[BUF_IND]:='';
BUF_IND-=1;
{? BUF_IND=0
|| VAR_DEL.delete('szukajkh','tab','ref','find','__actsl');
   {? var_pres('bRef')>0
   || ref:=bRef;
      VAR_DEL.delete('bRef')
   ?}
?};
RS.TAB:=_rs_tab;
{? var_pres('SLO_AKC')>0 || SLO.actions(SLO.win_sel('?'),,SLO_AKC,1); &SLO_AKC ?};
1


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: Szukaj w słowniku użytkownika
::  OLD: \szukaj/szukaj
::----------------------------------------------------------------------------------------------------------------------
_rs_tab:=RS.TAB; _rs_kod:=RS.KOD;
RS.index('RS'); RS.prefix();
{? exec('spr_wz','slo_slu',SLU.WZ) || RS.find_key(SLU.WZ) ?};
_edittab:={? _=1 & szukajkh || 0 || 1 ?};
ref[BUF_IND]:=null; find[BUF_IND]:=0;
{? tab[BUF_IND]='SLO'
|| SLO.cntx_psh(); SLO.win_edit('RED');
   _a:=exec('edit_std','!zws_par_kslo');
   SLO.cntx_pop();
   SLO.seek(_a)
|| {? RS.TAB='SLO_OSOB'
   || _ref:=obj_new(50);
      _find:=obj_new(50);
      {! _i:=1..BUF_IND
      |! _ref[_i]:=ref[BUF_IND];
         _find[_i]:=find[BUF_IND]
      !};
      VAR_DEL.delete('ref','find');
      exec('szukaj_oso','slo_slu',0,'OSOBA');
      VAR_DEL.delete('ref','find');
      ref:=obj_new(50);
      find:=obj_new(50);
      {! _i:=1..BUF_IND
      |!
         ref[_i]:=_ref[_i];
         find[_i]:=_find[_i]
      !}
   || RS.cntx_psh();
      {? RS.TAB='KH'
      || _a:=$('KH.win_edit('+'''SZUK'''+')')
      |? RS.TAB='SKID_RBK'
      || _f:=$(RS.TAB+'.index(\'TAB\');'+RS.TAB+'.prefix(REF.INFO,REF.INFO)'); _f();
         _b:=$(RS.TAB+'.cntx_psh()'); _b();
         _a:=$(RS.TAB+'.win_edit('+'''SZUK'''+')')
      |? RS.TAB='UD_DEF'
      || UD_DEF.cntx_psh(); UD_SKL.win_edit('WZO'); _a:="1"
      |? 'ML_CZ,ML_DZ,ML_RZ,ML_PG,ML_PP,ML_ZAD,US'*RS.TAB
      || _b:=$(RS.TAB+'.cntx_psh()'); _b();
         _a:=$(RS.TAB+'.win_edit('+'''RED'''+')')
      |? RS.TAB='ZL'
      || _b:=$(RS.TAB+'.cntx_psh()'); _b();
         _a:=$(RS.TAB+'.win_edit('+'''WZOR_SLO'''+')')
      |? RS.TAB='UD_SKL'
      || _b:=$(RS.TAB+'.cntx_psh()'); _b();
         _a:=$(RS.TAB+'.win_edit('+'''WZO'''+')')
      || _b:=$(RS.TAB+'.cntx_psh()'); _b();
         _a:=$(RS.TAB+'.win_edit('+'''SZUK'''+')')
      ?};
      _a();
      exec('edit_tab','!zws_par_kslo',_edittab);
      RS.cntx_pop();
      {? RS.TAB<>'KH' || _b:=$(RS.TAB+'.cntx_pop()'); _b() ?}
   ?}
?};
RS.TAB:=_rs_tab; RS.KOD:=_rs_kod;
0


\edit_std
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [????]
:: OPIS: Edycja podczas szukania
::  OLD: \edit_std/szukaj
::----------------------------------------------------------------------------------------------------------------------
{! |? ($('BUF'+$BUF_IND+'.restore()'))(); SLO.edit() & exec('find_std','!zws_par_kslo') !};
SLO.ref()


\find_std
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [????]
:: OPIS: Szukanie w słowniku użytkownika
::  OLD: \find_std/szukaj
::----------------------------------------------------------------------------------------------------------------------
($('BUF'+$BUF_IND+'.save()'))(); _a:=0;
{! |?
   {? ($('BUF'+$BUF_IND+'.restore()'))(); SLO.find_rec(1)
   || _a:=FUN.choice('Znaleziono rekord:\n%1\n%2'@[SLO.KOD,SLO.TR],,'Kontynuuj'@,'Edytuj'@)
   || FUN.info('Rekordu nie znaleziono.'@)
   ?};
   {? _a=2 || 0 || _a ?}
!};
_a


\edit_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???  [????]
:: OPIS: Wyszukuje w tabeli wzorcowej i SLO
::   WE: _a - 0/1 - czy edytować dane do szukania
::  OLD: \edit_tab/szukaj
::----------------------------------------------------------------------------------------------------------------------
_tab:={? RS.TAB='UD_DEF' || 'UD_SKL' || RS.TAB ?};
{! |?
   ($('BUF'+$BUF_IND+'.restore()'))();
   RS.cntx_psh();
   _zwrot:={? _a
           || RS.cntx_psh(); SLUAPPL.cntx_psh(); SLU.cntx_psh();
              {? ($(_tab+'.edit()'))()
              || RS.cntx_pop(); SLUAPPL.cntx_pop(); SLU.cntx_pop();
                 {? RS.TAB='SKID_RBK' || SKID_RBK.N:=gsub(SKID_RBK.N,' ','') ?};
                 SLU.cntx_psh(); RS.cntx_psh();
                 {? cur_tab(1)=SLO || SLO.get(); SLO.SLU() || SLUAPPL.SLU() ?};
                 SLU.cntx_pop(); RS.cntx_pop();
                 1
              || RS.cntx_pop(); SLUAPPL.cntx_pop(); SLU.cntx_pop(); 0
              ?}
           || 1
           ?};
   RS.cntx_pop();
   {? _zwrot || szukajkh:=1 ?};
   _zwrot & exec('find','!zws_par_kslo')
!}


\find
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [????]
:: OPIS: Szukanie w słowniku użytkownika
::  OLD: \find/szukaj
::----------------------------------------------------------------------------------------------------------------------
($('BUF'+$BUF_IND+'.save()'))(); _a:=0;
{! |?
   {? exec('replay','!zws_par_kslo')
   || {! |?
         {? RS.TAB<>'KH'
         || _a:=FUN.choice('\nZnaleziono rekord:\n%1\n%2\n'@[SLO.KOD,SLO.TR],,'Kontynuuj'@,'Edytuj'@,'Szczegóły >>'@)
         || _a:=0
         ?};
         {? _a<>3
         || 0
         || {? RS.TAB='UD_DEF'
            || _tab:='UD_SKL'
            || _tab:=RS.TAB
            ?};
            _b:=$(_tab+'.display'); _b();
            {? RS.TAB='UD_DEF' || UD_SKL.cntx_pop() ?};
            1
         ?}
      !}
   || FUN.info('Rekordu nie znaleziono.'@)
   ?};
   {? _a=2 || 0 || _a ?}
!};
_a


\replay
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [?????]
:: OPIS: szuka pozycji w tabeli źródlowej i odpowiednika w slo
::   WY: 1-znaleziono, 0-wpp
::  OLD: \replay/szukaj
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('kh_tab');
kh_tab:=obj_new(31);
($('BUF'+$BUF_IND+'.restore()'))();
{? RS.TAB='KH' &
   KH_DOD.B<>'' & ~(KH.KOD<>'' | KH.FIZYCZNY<>0
   | KH.SNIP<>'' | KH.NIP<>'' | KH.REG<>''
   | KH.SKR<>'' | KH.NAZ<>'' | KH.NAZ_P<>''
   | KH.GRKH<>null | KH.OBS<>null | KH.U<>''
   | KH.KRAJ<>'' | KH.MIASTO<>'' | KH.UL<>'' | KH.KPOCZ<>'' | KH.POCZ<>''
   | KH.TEL<>'' | KH.FAX<>'' | KH.TX<>'' | KH.EM<>'' | KH.WWW<>'' | KH.NASZ_KOD<>''
   | KH.KRAJISO<>null
   | KH.PESEL<>'' | KH.SN_DT<>'' | KH.DATA_WDT<>date(0,0,0)
   | KH.REJ_INFO<>''
   | KH.KRS<>'' | KH.KRS_DATA<>date(0,0,0) | KH.EORI<>'')
|| _khd_b:=KH_DOD.B;
   KH.index('KOD'); KH.clear(); KH.find_key(2,SLO.KOD);
   KH_DOD.index('KH_KOD'); KH_DOD.prefix(REF.FIRMA); KH_DOD.find_key(SLO.KOD);
   _a:=$("{? KH_DOD.find_tab(0,'B',,'=',\'"+_khd_b+"\') || KH_DOD.KH(); 1 ?}")
|? RS.TAB='KH' &
   (KH.KOD<>'' | KH.FIZYCZNY<>0
   | KH.SNIP<>'' | KH.NIP<>'' | KH.REG<>''
   | KH.SKR<>'' | KH.NAZ<>'' | KH.NAZ_P<>''
   | KH.GRKH<>null | KH.OBS<>null | KH.U<>''
   | KH.KRAJ<>'' | KH.MIASTO<>'' | KH.UL<>'' | KH.KPOCZ<>'' | KH.POCZ<>''
   | KH.TEL<>'' | KH.FAX<>'' | KH.TX<>'' | KH.EM<>'' | KH.WWW<>'' | KH.NASZ_KOD<>''
   | KH.KRAJISO<>null
   | KH.PESEL<>'' | KH.SN_DT<>'' | KH.DATA_WDT<>date(0,0,0)
   | KH.REJ_INFO<>''
   | KH.KRS<>'' | KH.KRS_DATA<>date(0,0,0) | KH.EORI<>'')
|| _i:=0;
   kh_tab[_i+=1]:=KH.KOD;
   kh_tab[_i+=1]:=KH.FIZYCZNY;
   kh_tab[_i+=1]:=KH.SNIP;
   kh_tab[_i+=1]:=KH.NIP;
   kh_tab[_i+=1]:=KH.REG;
   kh_tab[_i+=1]:=KH.SKR;
   kh_tab[_i+=1]:=KH.NAZ;
   kh_tab[_i+=1]:=KH.NAZ_P;
   kh_tab[_i+=1]:=KH.GRKH().KOD;
   kh_tab[_i+=1]:=KH.OBS().KOD;
   kh_tab[_i+=1]:=KH.U;
   kh_tab[_i+=1]:=KH.KRAJ;
   kh_tab[_i+=1]:=KH.MIASTO;
   kh_tab[_i+=1]:=KH.UL;
   kh_tab[_i+=1]:=KH.KPOCZ;
   kh_tab[_i+=1]:=KH.POCZ;
   kh_tab[_i+=1]:=KH.TEL;
   kh_tab[_i+=1]:=KH.FAX;
   kh_tab[_i+=1]:=KH.TX;
   kh_tab[_i+=1]:=KH.EM;
   kh_tab[_i+=1]:=KH.WWW;
   kh_tab[_i+=1]:=KH.NASZ_KOD;
   kh_tab[_i+=1]:=KH.KRAJISO().KODISO;
   kh_tab[_i+=1]:=KH.PESEL;
   kh_tab[_i+=1]:=KH.SN_DT;
   kh_tab[_i+=1]:=KH.DATA_WDT;
   kh_tab[_i+=1]:=KH.REJ_INFO;
   kh_tab[_i+=1]:=KH.KRS;
   kh_tab[_i+=1]:=KH.KRS_DATA;
   kh_tab[_i+=1]:=KH.EORI;
   _khd_b:=KH_DOD.B;
   KH.index('KOD'); KH.clear(); KH.find_key(2,SLO.KOD);
   _i:=0;
   _a:=$('_wyn:=KH.find_tab(0,'+
         ''''+'KOD'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'FIZYCZNY'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'SNIP'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'NIP'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'REG'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'SKR'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'NAZ'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'NAZ_P'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'GRKH'+''''+','+''''+'KOD'+''''+','+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'OBS'+''''+','+''''+'KOD'+''''+','+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'U'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'KRAJ'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'MIASTO'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'UL'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'KPOCZ'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'POCZ'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'TEL'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'FAX'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'TX'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'EM'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'WWW'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'NASZ_KOD'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'KRAJISO'+''''+','+''''+'KODISO'+''''+','+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'PESEL'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'SN_DT'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'DATA_WDT'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'REJ_INFO'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'KRS'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'KRS_DATA'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+'],'+
         ''''+'EORI'+''''+',,'+''''+':-'+''''+','+'kh_tab['+$(_i+=1)+']'+
   ');'+
   '_wyn')
|| {? RS.TAB='UD_DEF'
   || _a:="{? UD_SKL.find_rec()
           || UD_DEF.index('PODTEC'); UD_DEF.prefix(RS.UD_SCH);
              UD_DEF.find_key(UD_SKL.ref())
           ?}
          "
   || _a:=$(RS.TAB+'.find_rec()')
   ?}
?};
_b:=$(RS.TAB+'.ref()');
_c:=1;
{!|? _c & _a() & ((ref[BUF_IND]<>_b() & ~find[BUF_IND]) | find[BUF_IND])
|! {? ~ref[BUF_IND] & ~find[BUF_IND] || ref[BUF_IND]:=_b() ?};
   {? exec('find_slo','!zws_par_kslo')
   || find[BUF_IND]:=1; ref[BUF_IND]:=null; _c:=0
   ?};
   {? _c || ($('BUF'+$BUF_IND+'.restore()'))() ?}
!};
obj_del(kh_tab);
~_c


\find_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [????]
:: OPIS: Szukanie w słowniku użytkownika
::  OLD: \find_slo/szukaj
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(SLU.ref());
_b:=$(RS.TAB+'.'+RS.KOD); _c:=0;
_d:=_b();
{? type_of(_d)<>2
|| _d:=exec('szuk_slo_kod','!zws_par_kslo',form(_d,-SLO.SLU().DL,,'99'),SLU.DL)
?};
{? ((type_of(_d)=2 & _d<>'') | type_of(_d)<>2) & SLO.find_key(_d) & SLO.KOD=_d
|| _c:=SLO.ref()
?};
SLO.cntx_pop();
{? _c || SLO.seek(_c); SLO.get(); 1 || 0 ?}


\szuk_slo_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła zwraca zawartość pola SLO.KOD bez symbolu firmy w przypadku instalacji wielofirmowej i słowników
::       opartych na tabelach lokalnych
::   WE: _a - kod
::       _b - długość kodu w słowniku
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
_pref:=REF.S_FIRMA().SYMBOL;
_par300233:=exec('get','#params',300233,2);
_wy:=_a;
{? REF.WFIRM & _par300233='N'
|| RS.cntx_psh(); RS.index('RS'); RS.prefix();
   {? RS.find_key(SLO.SLU().WZ) & RS.TAB<>''
   || {? exec('czy_tab_glob','#table',RS.TAB)=0
      || {? 3+_wy<>_pref
         || {? (+_wy)+3>_b || _wy:=_wy+(_b-3) ?};
            _wy:=_pref+_wy
         ?}

      ?}
   ?};
   RS.cntx_pop()
?};
_wy


\szukajf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.40]
:: OPIS: Ponawia szukanie dla kontrahenta na klawisz F3
::  OLD: \szukajF3/szukaj
::----------------------------------------------------------------------------------------------------------------------
RS.index('RS'); RS.prefix();
{? exec('spr_wz','slo_slu',SLU.WZ) || RS.find_key(SLU.WZ) ?};
{? RS.TAB='KH' & szukajkh || exec('szukaj','!zws_par_kslo',1) ?}


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [7.60]
:: OPIS: Formuła na 'okienko przed' w tabeli SLO przy redakcji poziomów analityki
::  OLD: \init/synt
::----------------------------------------------------------------------------------------------------------------------
_rs_tab:=RS.TAB; _rs_kod:=RS.KOD; RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? SLO.get() || SLO.SLU() ?}; _o:='';
{? SLO.win_dict('?')<>SLO.win_sel('?')
|| _o:=SLO.win_sel('?'); SLO.win_sel(SLO.win_dict('?'))
?};
SLO.hdr_sel();
{? var_pres('hdr_slo')<=0 || SLO.hdr_sel(' %1'@[SLU.NAZ]) || SLO.hdr_sel(hdr_slo) ?};
{? _o<>'' || SLO.win_sel(_o) ?};
SLU.cntx_psh();
{? ~(RS.find_key(SLU.WZ)  & (RS.TAB='SLO_OSOB' | RS.TAB='OS' | RS.TAB='OSOBA' ))
|| exec('init_szukaj','!zws_par_kslo')
?};
SLU.cntx_pop();
RS.index('RS'); RS.prefix(SLU.WZ);
{? RS.first() & RS.TAB='ZL' || SLO_AKC:=SLO.actions(SLO.win_sel('?'),'P',,1) ?};
RS.cntx_pop();
exec('slu_okn','slo_slu');
RS.TAB:=_rs_tab; RS.KOD:=_rs_kod


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Formuła na 'okienko po' w tabeli SLO przy redakcji poziomów analityki
::  OLD: \done/synt
::----------------------------------------------------------------------------------------------------------------------
{? REF.WFIRM & (var_pres('__KS_W')<0 | __KS_W=0)
|| {? exec('test_slo','slo_slu')=0
   || FUN.emsg('Nie można wybrać pozycji związanej z inną firmą.'@);
      return(0)
   ?}
?};
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? SLO.get() || SLO.SLU() ?};
{? ~(RS.find_key(SLU.WZ) & (RS.TAB='SLO_OSOB' | RS.TAB='OS' | RS.TAB='OSOBA'))
|| exec('done_szukaj','!zws_par_kslo')
|| VAR_DEL.delete('tab','ref','find')
?};
RS.cntx_pop();
{? var_pres('SLO_AKC')>0 || SLO.actions(SLO.win_sel('?'),'',SLO_AKC,1); &SLO_AKC ?};
1


\os_spac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Formuła na spacje dla tabeli SLO
::  OLD: \os_spac/synt
::----------------------------------------------------------------------------------------------------------------------
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? ~(RS.find_key(SLO.SLU().WZ) & (RS.TAB='SLO_OSOB' | RS.TAB='OS' | RS.TAB='OSOBA'))
|| exec('pw_kodsl','slo_slu');
   {? RS.TAB='KH'
   || exec('kh_dod_ini','kontrahent');
      KH.win_edit('RED');
      KH.memo_get(,'OPIS');
      KH.memo_get(,'POWIAD');
      KH.memo_get(,'POWTEAMS');
      KH.display()
  || SLO.display()
  ?}
?};
RS.cntx_pop()


\szukaj1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Formuła do szukania rekordu w tabeli SLO
::  OLD: \szukaj/synt
::----------------------------------------------------------------------------------------------------------------------
_rs_tab:=RS.TAB; _rs_kod:=RS.KOD;
SLU.cntx_psh(); RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLO.SLU().WZ)  & (RS.TAB='SLO_OSOB' | RS.TAB='OS' | RS.TAB='OSOBA')
|| exec('szukaj_oso','slo_slu',1,'OSOBA')
|| exec('szukaj','!zws_par_kslo')
?};
SLU.cntx_pop(); RS.cntx_pop();
RS.TAB:=_rs_tab; RS.KOD:=_rs_kod


\pop_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Akcja 'Popraw' z poziomu kartotek, na których oparte są słowniki
::  OLD: \pop_tab/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh();
_del_pop:=(var_pres('slo_pop')<=0);
_del_ted:=(var_pres('tab_ed')<=0);
_tab:=cur_tab(1,1);
slo_pop:=0; tab_ed:=exec('akr_red_tab','slo_slu');
{? tab_ed='WAL'
|| WAL.win_edit('WALUTA');
   _a:=$('WAL.edit("exec(\'chk_wal\',\'slo_slu\')")')
|? tab_ed='US'
|| US.win_edit('RED');
   _a:=$('US.edit("exec(\'spr_us\',\'rachunki\')")')
|? tab_ed='B'
|| B.win_edit('RED'); CurRec:=B.ref;
   _a:=$('B.edit("exec(\'spr_b\',\'hbn\')")')
|? tab_ed='SKID_RBK'
|| RACHBANK.KB_4R:=SKID_RBK.N;
   SKID_RBK.win_edit('RED_RACH');
   SKID_RBK.efld_opt('RED_RACH','enable='+$(SKID_RBK.RS='T'),,'NRS');
   _a:=$('SKID_RBK.edit("exec(\'chk_skid_rbk\',\'!zws_par_kslo\')")')
|| _a:=$(tab_ed+'.edit()')
?};
{? tab_ed='SKID_RBK' || _wal:=SKID_RBK.WAL ?};
{? _a()
|| do();
   _tab.cntx_psh();
   _tab.prefix();
   {? ($(tab_ed+'.put'))()
   || {? ~exec('pop_slo','slo_slu',tab_ed)
      || undo()
      || {? tab_ed='SKID_RBK'
         || {? SKID_RBK.WAL<>_wal || exec('pop_subkonta','slo_slu'); exec('rbk_rd_update','rachunki') ?}
         ?}
      ?}
   ?};
   _tab.cntx_pop();
   end()
?};
{? _del_pop || &slo_pop ?};
{? _del_ted || &tab_ed ?};
SLU.cntx_pop();
1


\chk_skid_rbk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła walidujaca po redakcji rachunku bankowego licencjobiorcy
::----------------------------------------------------------------------------------------------------------------------
{? SKID_RBK.RS='T'
|| _chk:=__CHK.record(SKID_RBK,,'KRAJ','WAL','KOD','NRS')
|| _chk:=__CHK.record(SKID_RBK,,'KRAJ','WAL','KOD')
?};
{? _chk='' || _chk:=RB.chk_rban(SKID_RBK.N,1,'KB_4R') ?};
{? _chk='' & SKID_RBK.VAT='T'
|| {? SKID_RBK.WAL().KOD<>'PLN'
   || FUN.emsg('Konta typu VAT nie mogą być w innej walucie niż PLN.'@);
      _chk:='WAL'
   ?};
   {? _chk='' & SKID_RBK.INNWAL='T'
   || FUN.emsg('Konta typu VAT nie mogą obsługiwać płatności w innej walucie.'@);
      _chk:='INNWAL'
   ?};
   {? _chk='' & SKID_RBK.RD='T'
   || FUN.emsg('Konto typu VAT nie może być kontem domyślnym.'@);
      _chk:='RD'
   ?};
   {? _chk='' & SKID_RBK.BANK().KODISO().KODISO<>'PL'
   || FUN.emsg('Kraj banku dla rachunku VAT nie może być inny niż PL.'@);
      _chk:='KRAJ'
   ?};
   {? _chk='' & SKID_RBK.RS='T'
   || FUN.emsg('Dla rachunku VAT nie można tworzyć subkont.'@);
      _chk:='RS'
   ?}
?};
_chk


\sel_slo_osob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Ustawia okno wertowania SLO_OSOB przy dołączaniu do słownika
::  OLD: \sel_slo_osob/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
_poz:=RS.F_ZATR().OPIS*'-';
{? _poz
|| _tyt:=_poz+F_ZATR.OPIS-1
|| _tyt:=F_ZATR.OPIS
?};
_o:=SLO_OSOB.mk_sel(_tyt,'P',,'#slo_osob_wer');
SLO_OSOB.win_fld(_o,,'ID',,,,,,'Numer id.'@);
SLO_OSOB.win_fld(_o,,'OSOBA','NAZWISKO',,30,,,'Nazwisko'@);
SLO_OSOB.win_fld(_o,,'OSOBA','PIERWSZE',,20,,,'Imię'@);
SLO_OSOB.win_act(_o,,'Formuła','Wybierz'@@,,,"exec('ten_slo','slo_slu')",,1,,,,'W');
SLO_OSOB.win_act(_o,,'Szukaj');
SLO_OSOB.win_act(_o,,'Formuła','Rachunki bankowe'@@,,,"exec('oso_rbk','rachunki')",,,,,,'R');
SLO_OSOB.win_act(_o,,'Kolejność');
SLO_OSOB.win_sel(_o)


\czy_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formuła sprawdza, czy bieżący słownik jest systemowy
::   WY: 0 - nie systemowy, 1 - systemowy i wyświetlenie komunikatu
::  OLD: \czy_sys/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? SLU.SYSTEM='T'
|| FUN.info('Akcja zabroniona dla słownika systemowego.'@); 1
|| 0
?}


\ust_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PB] [8.50]
:: OPIS: Ustawienie parametrów wydruku miejsc wykorzystania słownika
::  OLD: \ust_wyd/m
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
g_tt_w:=obj_new(TMPPOW.fld_num());
g_tt_w[1]:=27; g_tt_w[2]:=7; g_tt_w[3]:=10; g_tt_w[4]:=63; g_tt_w[5]:=6;
PAR_WYDR.TTMPAKR:='TMPPOW';
PAR_WYDR.TITLE:='MIEJSCA WYKORZYSTANIA SŁOWNIKA: %1'@[SLU.NAZ]


\del_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PB] [8.50]
:: OPIS: Usunięcie zmiennej po wydruku miejsc wykorzystania słownika
::  OLD: \del_wyd/m
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
obj_del(g_tt_w);1


\spr_algp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.50]
:: OPIS: Przy poprawianiu nazwy słownika lub usuwaniu rekordu w tabeli SLU
::       modyfikuje tez występowanie jego w ALG_PAR
::   WE: _a - nazwa słownika, _b - 1 (poprawianie), 0 (usuwanie - ze SLUAPPL - tylko FIKS)
::  OLD: \spr_algp/skid_slu
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<=0 || _b:=1 ?};
{? _b=0 || SLUAPPL.SLU() ?};
ind_algp:=ALG_PAR.ndx_tmp(,,'SYSTEM',,0);
ALG_PAR.index(ind_algp); ALG_PAR.prefix('FIKS'); FORMULA.prefix();
{? ALG_PAR.first()
|| {! |?
     _skrot:=ALG_PAR.FORMULA().SKROT;
     {? (_skrot*'SAL'>0 | _skrot*'OBR'>0 | (4+_skrot)*'BO'>0 | _skrot*'ROZNI'>0 | _skrot*'ZM_STA'>0)
        & _skrot<>'R_SALDO' & _skrot<>'R_ROZNIC' & (1+_skrot<>'^') & ALG_PAR.P5=_a
     || {? _b
        || ALG_PAR.P5:=SLU.NAZ
        || ALG_PAR.P5:=ALG_PAR.P6:=''; ALG_PAR.AKTYWNA:='N'
        ?};
        ALG_PAR.put()
     ?};
     ALG_PAR.next()
   !}
?};
ALG_PAR.ndx_drop(); &ind_algp


\slua_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula po na akcje Usun okienek SLUAPPL.WER_F3, SLUAPPL.WER_MAGF
::  OLD: \slua_del/skid_slu
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('ssize_s')>0;
{? _grp | FUN.ask('Czy usunąć bieżący wiersz?'@)
|| {? SLUAPPL.count>0 | exec('czy_stale','slo_slu', SLUAPPL.ref()) | exec('czy_stale','slo_slu', SLUAPPL.SLU)
   || {? ~_grp
      || FUN.info('Słownik powiązany z innymi tabelami lub stałymi systemu.\nUsunięcie niemożliwe.'@)
      || bledy_s+=1
      ?}
   || do();
      SLUAPPL.SLU();
      {? SLUAPPL.del()
      || {? SLOSLU.SLFAK<>''
         || exec('spr_algp','!zws_par_kslo',SLOSLU.SLFAK,0)
         ?};
         exec('slu_del','!zws_par_kslo')
      || {? _grp || bledy_s+=1 ?}
      ?};
      end()
   ?}
?}


\slua_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula przed na akcje grupowa Usun okienek SLUAPPL.WER_F3, SLUAPPL.WER_MAGF
::  OLD: \slua_gpr/skid_slu
::----------------------------------------------------------------------------------------------------------------------
exec('usun_bg','sprfin','SLUAPPL','Usunąć zaznaczone słowniki użytkownika?'@)


\slua_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula przed na akcje grupowa Usun okienek SLUAPPL.WER_F3, SLUAPPL.WER_MAGF
::  OLD: \slua_gpo/skid_slu
::----------------------------------------------------------------------------------------------------------------------
exec('usun_ag','sprfin')


\add_slu_appl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [$12.10]
:: OPIS: Formula przed na akcje Dolacz okien wertowania tabeli SLUAPPL
::  OLD: \slow2bud/fiks
::----------------------------------------------------------------------------------------------------------------------
POMOC.SLUPOP:=0;
SLU.index('NAZ');
SLU.prefix();
SLU.win_edit('RED');
SLU.blank();
{? SLU.edit("exec('slu_chk','slo_slu')")
|| {? SLU.add()
   || exec('ten_slu','!zws_par_kslo')
   ?}
?}


\del_slu_appl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie słownika ze SLUAPPL wraz z powiązanym SLU
::----------------------------------------------------------------------------------------------------------------------
SLU.prefix();
SLUAPPL.SLU();
exec('slu_del','!zws_par_kslo')


\pop_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Poprawianie pozycji w slowniku wyroznikow
::  OLD: \pop_wyr/wspol.fml
::----------------------------------------------------------------------------------------------------------------------
_kod:=SLO.KOD;
SLO.cntx_psh();
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ) & (RS.TAB='SLO_OSOB' | RS.TAB='OS')
|| exec('pop_tab_o','!zws_par_kslo',RS.TAB)
|? RS.find_key(SLU.WZ) & RS.TAB='B'
|| exec('slo_pop1','!zws_par_kslo')
|? SLU.SYSTEM='T'
|| FUN.info('Słownik systemowy.\nPoprawianie niemożliwe.'@)
|| exec('slo_pop','!zws_par_kslo')
?};
RS.cntx_pop();
SLO.cntx_pop();
{? SLO.get() & _kod<>SLO.KOD
|| TPOW.prefix(KS_W.LP,SLU.NAZ,_kod);
   {? TPOW.first || TPOW.prefix; TPOW.SLO:=SLO.KOD; TPOW.put ?}
?}


\slo_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [??]
:: OPIS: Akcja 'Popraw' z poziomu okienek wertowania tabeli SLO
::  OLD: \slo_pop/wspol
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
{? exec('test_slo','slo_slu')=0
|| FUN.info('Poprawienie pozycji słownika niemożliwe w bieżącej firmie.'@);
   return(0)
?};
SLO.SLU();
{? ~exec('czy_sys','!zws_par_kslo')
|| _sp:={? var_pres('slo_pop')>0 || _slo_pop:=slo_pop; 1 || 0 ?};
   _rs:={? var_pres('ref_slo')>0 || _ref_slo:=ref_slo; 1 || 0 ?};
   _slokh:=''; slo_pop:=1; ref_slo:=null; exec('slu_okn','slo_slu');
   {? exec('spr_wz','slo_slu',SLO.SLU().WZ)
   || RS.index('RS'); RS.prefix();
      {? RS.find_key(SLU.WZ)
      || {? RS.TAB<>'SLO_OSOB' & RS.TAB<>'OS'
         || _i:=RS.ref;
            {? (_r_szuk:=exec('wz_szuk','slo_slu'))
            || _b:=$(RS.TAB+ '.edit()');
               _g:=$(RS.TAB+'.put()');
               exec('cn_psh','slo_slu');
               {? _b()
               || exec('cn_pop','slo_slu');
                  {? var_pres('ref_slo')>0 & ref_slo<>null || SLO.seek(ref_slo) ?}; _h:=1;
                  exec('giokhbe','giodo');
                  do();
                  _k:=SLO.KOD; _t:=SLO.TR; _msg:='';
                  {? _g()
                  || exec('giokhae','giodo');
                     {? _r_szuk<>-1 & ~RS.seek(_i)
                     || undo();
                        _msg:='Nie znaleziono rekordu tabeli RS.'
                     || _msg:=exec('akt_slo','slo_slu',_k,_t)
                     ?}
                  ?};
                  end();
                  {? _msg<>'' || FUN.info(_msg) ?}
               || exec('cn_pop','slo_slu'); {? ref_slo<>null || SLO.seek(ref_slo) ?}
               ?}
            ?}
         || FUN.info('Dane używane w systemie kadrowo-płacowym.\nModyfikacje zabronione.'@)
         ?}
      || FUN.info('Błędnie zdefiniowany słownik - nie istnieje wzorzec %1.'@[SLU.WZ])
      ?}
   || __slokod:=SLO.KOD;
      {? SLO.edit("
           {? __slokod<>SLO.KOD & ~exec('be_slo_kod','slo_slu')
           || FUN.info('Rekord został użyty w systemie.\nNiedozwolona zmiana pola Kod'@);
              SLO.KOD:=__slokod;
              win_disp;
              'TR'
           || chk_rec
           ?}
         ")
      || SLO.put
      ?};
      &__slokod
   ?};
   {? _sp || slo_pop:=_slo_pop || VAR_DEL.delete('slo_pop') ?};
   {? _rs || ref_slo:=_ref_slo || VAR_DEL.delete('ref_slo') ?}
?};
exec('tab_f_rfresh','slo_filtr',SLO);
1


\chk_rec_os
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Walidacja rekordu w OS
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? OS.KOD=''
|| FUN.info('Nie wprowadzono kodu osoby.'@); _zwrot:='KOD'
|? ~OS.OSOBA
|| FUN.info('Nie wybrano nazwiska osoby.'@); _zwrot:='OSOBA'
?};
{? _zwrot=''
|| {? __CHK.index(OS,-menu_txt(,)='popraw')<>'' || _zwrot:='KOD' ?}
?};
_zwrot


\rozrach
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Wyswietla rozrachunki projektu - akcja tabeli SLO
::  OLD: \rozrach/skid_prj.fml
::----------------------------------------------------------------------------------------------------------------------
SLO.SLU();
RS.cntx_psh(); RS.index('RS'); RS.prefix(SLU.WZ);
{? RS.first() & RS.TAB='UD_SKL' & RS.UD_TYP<>null & RS.UD_TYP().SYMBOL='PROJ_REM'
|| SLO_PROJ:=SLO.ref();
   OP_PROJ.index('SLO');
   {? OPERATOR.DEPT<>null
   || OP_PROJ.prefix(SLO_PROJ,SSTALE.WAL,OPERATOR.DEPT)
   || OP_PROJ.prefix(SLO_PROJ,SSTALE.WAL)
   ?}; OP_PROJ.first();
   OP_PROJ.win_sel('WER'); OP_PROJ.hdr_sel(' - %1'@[SLO.TR]);
   ZAP_PROJ.win_sel('WER');
   SLO.cntx_psh();
   OP_PROJ.select();
   SLO.cntx_pop();
   &SLO_PROJ
|| FUN.info('Akcja dostępna wyłącznie dla słowników projektów.'@)
?};
RS.cntx_pop()


\wbskl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMA [2006]
:: OPIS: Okienko przed okna ADM tabeli UD_SKL
::  OLD: \wbskl/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
exec('okn_skl','control');
{? UD_SKL.win_sel('?')<>'ADM_CON'
|| _hid:='';
   {? UD_TYP.SYMBOL='PODZORG' || _hid+='dpU:d' ?};
   UD_SKL.actions(UD_SKL.win_sel('?'),_hid,,1)
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 4a22a8d8a6ab18eb177503f00dd4261e37bfd32d439f082c722f3fd48196af4f3c4d208b7e7619560e3b6399568e99544b49822cc9656ee931f03b6b58eeb4ff9b767d2e9915e54090aea2a02ae368fff58ed3858c8e99e79ce1797af0042a10f632bf1dab217c04cdb9868e2cd476ea4e11e01f1d4b4b18ab101b058645aced

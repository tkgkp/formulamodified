:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_zdot.fml
:: Utworzony: 23.08.2017
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_ZDOT - Zestawienie dokumentów zakupu środków trwałych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Zestawienie dokumentów zakupu środków trwałych - główna formuła czynności FKS_DKS_ZDOT.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
exec('srdz_sql_prep','!fks_dks_zdot');
_okres:=SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ;
_win:=__T_SRDK.mk_sel('Dokumenty zakupu środków trwałych w okresie: %1'@[_okres],'P',0,'_t_srdk',10,5,15,,'U',,,,,'normal');
__T_SRDK.win_fld(_win,,'NK',,,30,,,'Symbol dokumentu'@,,'Symbol dokumentu zakupu'@);
__T_SRDK.win_fld(_win,,'REJ',,,10,,,'Rejestr'@,,'Rejestr księgowy'@);
__T_SRDK.win_fld(_win,,'DTO',,,10,,,'Data wystawienia'@,,'Data wystawienia'@);
__T_SRDK.win_fld(_win,,'DTW',,,10,,,'Data otrzymania'@,,'Data otrzymania'@);
__T_SRDK.win_fld(_win,,'OT',,,60,,,'Dokumenty OT'@,,'Lista powiązanych dokumentów przyjęcia OT'@);
__T_SRDK.win_act(_win,,'Formuła','Dokumenty OT'@,,'Lista powiązanych dokumentów przyjęcia OT'@,,
                 "exec('dok_ot','!fks_dks_zdot')",1,,,,'D');
__T_SRDK.win_act(_win,,'Kolejność',);
__T_SRDK.win_btn(_win,'text=%1'['Dokumenty OT'@],'menu:D',,,,,,'noempty');
__T_SRDK.win_sel(_win);
__T_SRDK.select();
exec('srdz_sql_del','!fks_dks_zdot')


\srdz_sql_prep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Przygotowuje tabelę tymczasową dla SQL
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__T_SRDK','__T_SRD1','__T_SRD2');
DOK.cntx_psh(); SRDZ.cntx_psh(); SRDO.cntx_psh();
DOK.prefix();
SRDO.prefix();
_sort:='NK';
_from:='join DOK_REJ using (DOK.DOK_REJ,DOK_REJ.REFERENCE)';
_where:='DOK_REJ.ZAK_SR=''T''';

__T_SRDK:=tab_tmp(1,'NK','STRING[35]','Symbol dokumentu'@,
                    'DTO','DATE','Data wystawienia'@,
                    'DTW','DATE','Data otrzymania'@,
                    'REJ','STRING[10]','Rejestr'@,
                    'REF','STRING[48]','Dokument'@,
                    'OT','STRING[255]','Lista OT'@);
__T_SRD1:=__T_SRDK.ndx_tmp(,,'DTO',,);
__T_SRD2:=__T_SRDK.ndx_tmp(,,'DTW',,);

DOK.f_set(_sort,_from,_where);
{? DOK.f_first()
|| {! |?
      __T_SRDK.blank();
      __T_SRDK.NK:=DOK.NK;
      __T_SRDK.DTO:=DOK.DTO;
      __T_SRDK.DTW:=DOK.DTW;
      __T_SRDK.REJ:=DOK.REJ().KOD;
      __T_SRDK.REF:=DOK.uidref();
      _tmp:='';
      SRDZ.index('SRDD');
      SRDZ.prefix(DOK.uidref(),);
      {? SRDZ.first()
      || {! |?
            {? SRDO.seek(SRDZ.SRDO,SRDO.name())
            || {? +_tmp || _tmp+=', '+SRDO.SYMBOL || _tmp+=SRDO.SYMBOL ?}
            ?};
            SRDZ.next()
         !}
      ?};
      __T_SRDK.OT:=_tmp;
      __T_SRDK.add();
      DOK.f_next()
   !}
?};
DOK.f_clear();
DOK.cntx_pop(); SRDZ.cntx_pop(); SRDO.cntx_pop();
1


\srdz_sql_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Usuwa tabelę tymczasową przygotowaną dla SQL
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TMP_SRDK','__T_SRD1','__T_SRD2')


\dok_ot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła wyświetla listę dokumentów OT
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__TMP_OT');
_sql:='select SRDO.SYMBOL, SRDO.DW, SRDO.DO, SRSR.NRI, SRSR.NST, SRSR.REFERENCE as REF from SRDO join SRDZ using (SRDO.REFERENCE, SRDZ.SRDO) '
      'join SRSR using (SRDO.SRSR, SRSR.REFERENCE) where SRDZ.DOK_REF='':_a'' order by SRDO.SYMBOL';
__TMP_OT:=sql(_sql,__T_SRDK.REF);
{? __TMP_OT.first()
|| _win:=__TMP_OT.mk_sel('Dokumenty przyjęcia OT'@,'P',,'_srdz_sel',12,7,15,,'U',,,,,'normal');
   __TMP_OT.win_fld(_win,,'SYMBOL',,,30,,,'Symbol OT'@,,'Symbol dokumentu przyjęcia'@);
   __TMP_OT.win_fld(_win,,'DW',,,10,,,'Data wystawienia'@,,'Data wystawienia'@);
   __TMP_OT.win_fld(_win,,'DO',,,10,,,'Data operacji'@,,'Data operacji'@);
   __TMP_OT.win_fld(_win,,'NRI',,,18,,,'Nr inwentarzowy'@,,'Nr inwentarzowy środka'@);
   __TMP_OT.win_fld(_win,,'NST',,,50,,,'Nazwa środka'@,,'Nazwa środka'@);
   __TMP_OT.win_act(_win,,'Wyświetl',,,,,"SRSR.cntx_psh();
                                          SRSR.prefix();
                                          {? SRSR.seek(__TMP_OT.REF,ref_name(__TMP_OT.REF))
                                          || {? SRSR.R='T' & SRSR.GRP='N'
                                             || SRSR.win_edit('RED_T')
                                             |? SRSR.R='T' & SRSR.GRP='T'
                                             || SRSR.win_edit('RED_TG')
                                             |? SRSR.R='T' & SRSR.GRP='E'
                                             || SRSR.win_edit('RED_TE')
                                             |? SRSR.R='N' & SRSR.GRP='N'
                                             || SRSR.win_edit('RED_N')
                                             |? SRSR.R='N' & SRSR.GRP='T'
                                             || SRSR.win_edit('RED_NG')
                                             |? SRSR.R='N' & SRSR.GRP='E'
                                             || SRSR.win_edit('RED_NE')
                                             ?};
                                             EDIT_ES.JORG:=SRSR.JORG().SYMBOL;
                                             SRSR.display()
                                          ?};
                                          SRSR.cntx_pop()
                                         ");
   __TMP_OT.win_sel(_win);
   __TMP_OT.select()
|| FUN.emsg('Brak powiązanych dokumentów przyjęcia OT.'@)
?};
VAR_DEL.delete('__TMP_OT')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 dbe08ec40388cb0757e23b03fd1b89635d544a14252826599a6822d7e355f8e38e3444ab0a32f905ccf8efc3f4be5a8f0d73c942e9f4f683601ee5d5f6b0ba52ccec07276d90e6d32f42e4528d6b1e74fe699aaa3545bd7c1f49ccacc7799f85732f30c6e1c84df2f4a07982986732aa21ef8f9ac8a0cc5ab5b4cf1d8a29d749

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zak_dkzb.fml
:: Utworzony: 29.05.2018
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Rejestracja - korekta zabiorcza
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LZK,TARD
::# kind=WE,   symbol=TYPYSP,    type=_TYPYSP,  name=Typ dokumentu zakupu, required=N, fml_val="exec('typysp','!lzk_zak_dkzb')"
::# kind=WY,   symbol=KORS,      type=_FAKS,    name=Korekta zakupu,       required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_zak','lzk');

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_area_lzk_zak:=_mp.pathArea('LZK_ZAK');
_area:=_area_lzk_zak;
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo() | _mp.pathArea() & ~_area;
_grupa:=_mp.isGroup();
_autoakc:=exec('autoAkc','#b__box',_mp,200080,'LZK_ZAK_EZAK');

{? _todo
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('KORS',_out)=type_of(null()) & _out.KORS
:: Jest dokument korekty
   || 1
   |? ~_mp.isMicro()
:: Brak dokumentu korekty
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego KORS.')
   ?}
?};

:: Sprawdzenie uprawnień
{? params_exec('permissions_chk','lzk','!lzk_zak_dkzb')=0 || return() ?};

:: Wyzwalacz, który po dodaniu nagłówka korekty:
:: - add: dodaje rekord kluczowy nagłówka korekty
::   del: usuwa rekord kluczowy nagłówka korekty
:: - add: zapisuje parametr wyjściowy KORS - wskazanie na nagłówek korekty
::   del: zapisuje parametr wyjściowy KORS - null
_mp.trigRef('FAKS',,1,,exec('kind_out','#b_port'),'KORS',,"FAKS.HIDDEN='N'");

{? _akcja='Dołącz'
   | _proc
   | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
|| _typysp_t:=
      {? var_pres('TYPYSP',_in)=type_of(null())
      || exec('FindAndGet','#table',TYPYSP,_in.TYPYSP,,"TYPYSP.T")
      || ''
      ?};

:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
   params_set('out',_out,'mp',_mp,'akcja',_akcja);
   exec('faks_kzn_dod','faktury_nag1',0,_typysp_t);

:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? var_pres('KORS',_outa)<>type_of(~~) & _outa.KORS
:: Dodano dokument
   ||
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      params_exec('seek_dok','faktury_wspolne',_area_lzk_zak,_outa.KORS)
:: Wycofanie czynności bo nie dodano dokumentu
   || _mp.cancel()
   ?}

|? _akcja='Popraw'
   | _todo
|| {? var_pres('KORS',_out)=type_of(null())
:: Uruchomione w procesie
   || FAKS.cntx_psh();
      FAKS.use(ref_name(_out.KORS));
      FAKS.prefix();
      {? FAKS.seek(_out.KORS)
      || exec('faks_win_edit_set','faktury_nag');

         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('fak_pop','faktury_nag');

         _mp.descTodo()
      ?};
      FAKS.cntx_pop()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || {? ~_todo
      || params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('fak_pop','faktury_nag')
      |? _akcja='Zakończ_wer'
      || {? exec('faks_zakoncz','faktury_nag',_autoakc) || _mp.done() ?}
      || _mp.cancel()
      ?}
   |? _todo
:: Uruchomione zadanie z listy todo i brak _out.FAKS wtedy zadanie wycofujemy
   || _mp.cancel()
   ?}

|? _akcja='Usuń'
|| {? var_pres('KORS',_out)=type_of(null())
:: Uruchomione w procesie
   || _faks:=null();
      FAKS.cntx_psh();
      FAKS.use(ref_name(_out.KORS));
      {? ~_area_lzk_zak || FAKS.prefix() ?};
      {? FAKS.seek(_out.KORS)
      ||
         exec('usun_fak','faktury_nag',{? _grupa || 2 || 1 ?});

         {? ~FAKS.seek(_out.KORS)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _faks:=FAKS.ref()
      ?};
      FAKS.cntx_pop();
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _area_lzk_zak || {? _faks || FAKS.seek(_faks) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      exec('usun_fak','faktury_nag',{? _grupa || 2 || 1 ?});

      _mp.done()
   ?}

|? _akcja='Zakończ_wer' & _area_lzk_zak
|| {? exec('faks_zakoncz','faktury_nag',_grupa,_autoakc) || _mp.done() ?}

|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('KORS',_out)=type_of(null()) & _out.KORS
|| 'Zakończ rejestrację korekty zbiorczej zakupu: %1'@@[exec('record','#to_string',_out.KORS)]
|| 'Zarejestruj korektę zbiorczą zakupu'@@
?}


\faks_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Korekta zbiorcza - Dołącz
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LZK_ZAK_DKZB';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';

exec('mp_run','#b__box',_params);

FAKS.f_rfresh()


\typysp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Formuła na wartość parametru TYPYSP
::   WY: TYPYSP.ref()
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:='Z';
exec('tsp_fak','typysp',,,,,,'KZB')


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_wyn:=1;

:: Sprawdzenie uprawnień do typu dokumentu
{? _wyn & var_pres('TYPYSP',_in)=type_of(null()) & _in.TYPYSP
|| _typysp_t:=exec('FindAndGet','#table',TYPYSP,$_in.TYPYSP,,"TYPYSP.T",'');
   _wyn:=
      {? _typysp_t<>''
      || {? exec('FindInSet','#table','T2STS','UNIK',ST.STS)<>null()
         || exec('FindInSet','#table','T2STS','UNIK',_in.TYPYSP,ST.STS)<>null()
         || _where:=exec('tsp_where','typysp',,,,,_typysp_t,'KZB');
            exec('tsp_upr','typysp',_where,,0,,1,,_user)<>null()
         ?}
      || 1
      ?}

?};

_wyn


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Lista zadań TO-DO, podgląd dokumentu
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));
exec('display','faktury_nag',_out.KORS)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 956fa2180408e3c95ff974b7959491c0467c44d395df5a4f81bb93f4489e89864d76b9e8974743200db95288c476289c761d525435c3e73da018e77d7ed16dbc56395ac1e500ca458c3cb0ea9dd2de2c10ab9f1433dfd3fe932574e2c2202501bdcb363aa7c2dec0a5f0a7d1713da0b41c25fa3c754a2dce5ba05593af362e28

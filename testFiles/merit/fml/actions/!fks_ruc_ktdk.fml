:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ruc_ktdk.fml
:: Utworzony: 13.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RUC_KTDK - Dekretacja planu kompensaty
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja planu kompensaty - główna formuła czynności FKS_RUC_KTDK
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_par_nag','rozrach')
::# access=exec('upr_par_nag','rozrach')
::# kind=WE, symbol=PAR_NAG, type=_PAR_NAG, name=Wskazanie na nagłówek planu umowy kompensaty, required=T, keyref=T
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='K';
{? _mp.pathTodo() | _mp.isAutoRun()
|| {? var_press('PAR_NAG',_in)>0
   || _ref:=BB.sqlint($_in.PAR_NAG); _nam:=form(($_in.PAR_NAG)-8);
      {? _ref<>0 & _nam<>''
      || PAR_NAG.cntx_psh(); PAR_NAG.prefix();
         {? PAR_NAG.seek(_ref,_nam)
         || {? PAR_NAG.TYP=SER_KOR.TYP
            || params_exec('kom_dek','rozrach')
            || FUN.info('Wybrana umowa nie jest umową kompensaty.'@); _mp.error()
            ?}
         || {? ~_mp.isAutoRun()
            || FUN.info('Nie znaleziono umowy kompensaty.'@)
            ?};
            _out.DOK:=null;
            _mp.save(,_out);
            _mp.done()
         ?};
         PAR_NAG.cntx_pop()
      || {? ~_mp.isAutoRun()
         || FUN.info('Nie znaleziono umowy kompensaty.'@)
         ?};
         _out.DOK:=null;
         _mp.save(,_out);
         _mp.done()
      ?}
   || {? ~_mp.isAutoRun()
      || FUN.info('Nie znaleziono umowy kompensaty.'@)
      ?};
      _out.DOK:=null;
      _mp.save(,_out);
      _mp.done()
   ?}
|? _akcja='Dekretuj'
|| params_exec('kom_dek','rozrach')
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zadekretuj umowę kompensaty rozrachunków'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('PAR_NAG',_we)
|| _ref:=BB.sqlint($_we.PAR_NAG); _nam:=form(($_we.PAR_NAG)-8);
   {? _ref<>0 & _nam<>''
   || PAR_NAG.cntx_psh(); PAR_NAG.use(_nam); PAR_NAG.prefix();
      {? PAR_NAG.seek(_ref,_nam)
      || _desc:='Zadekretuj umowę kompensaty rozrachunków: %1 z dnia %2'@@[PAR_NAG.PAR_UM().SYM,$PAR_NAG.DATA]
      ?};
      PAR_NAG.cntx_pop()
   ?}
?};
_desc


\kompens_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja planu kompensaty
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RUC_KTDK';
_params.AKCJA:='Dekretuj';
_params.UIDREF:=PAR_NAG.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.PROC_START:='N';
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'PAR_NAG',PAR_NAG.ref());
exec('mp_run','#b__box',_params)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'parnag'>0
         || _record:=exec('FindAndGet','#table',PAR_NAG,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 5001ac0670192abda72789acec88ab40f3daa18b3fa0911982474c0ef84405297400cc31216acedb98dbf34036a2a85d6f8995e2401060533bb765ffcdc339e72ffbcf153b89f1907ed56a551228b9cbc5c6aff1d239c00dc4d302b063e3f6e6cc2896ded2056f1bc530fb490ca681989585d3a08e6c98a1b70bb362c1866ec1

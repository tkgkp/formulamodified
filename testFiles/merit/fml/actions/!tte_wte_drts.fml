:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_wte_drts.fml
:: Utworzony: 01.06.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_WTE_DRTS - Rejestrowanie surowców wzorca technologii
::            Uwaga: większość kodu jest wspólna z czynnością TTE_TEC_DRTS - w przypadku zmian modyfikować oba pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności rejestrowania listy surowców wzorca technologii (TTE_WTE_DRTS)
::       Zadaniem formuły jest zredagowanie listy surowców z zamiennikami i dokumentami
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

:: UPRAWNIENIA
::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=T, keyref=T
{? var_pres('TKTL',_in)<>type_of(~~) & var_pres('TKTL',_in)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_in)=type_of(~~) || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=N
{? var_pres('TKTL',_out)<>type_of(~~) & var_pres('TKTL',_out)<>type_of(null()) || return() ?};

{? _mp.isMicro()
||
:: Obsługa poza procesem
   _akcja:=_mp.akcja();

:: Zakończenie rejestrowania wywołane z menu okna wertowania TKTL
   {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
   || {? exec('valid_mat','tech_mater',_in.TKTL)
            &
         FUN.ask('Czy zakończyć rejestrację surowców dla całego wzorca technologii?'@)

      || exec('FindAndGet','#table',TKTL,_in.TKTL,,"STAT_S:='T'; STAN:='P'; put()")
      ?}

:: Obsługa funkcji 'Surowce' wywołanej z menu okna wertowania TKTL (karta z surowcami przypisanymi do nagłówka)
   |? _akcja='SEL_M'
   || exec('tmat_main','tech_mater',_in.TKTL,null(),0,1,0,0)

:: Obsługa funkcji 'Surowce' wywołanej z menu okna wertowania TKTL (karta z surowcami przypisanymi do operacji)
   |? _akcja='SEL_L'
   || exec('tmat_main','tech_mater',_in.TKTL,null(),1,1,0,0)

:: Obsługa funkcji 'Surowce' wywołanej z menu okna wertowania TOPER (karta z surowcami przypisanymi do operacji)
   |? _akcja='SEL_O'
   || exec('tmat_main','tech_mater',_in.TKTL,_context.oper,0,0,_context.used,_context.zakl)

   ?}
||
:: Obsługa w procesie
   exec('tmat_in_process','!tte_wte_drts',_in,_int,_out,_mp,_context)
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności rejestrowania listy surowców dla wzorca technologii (TTE_WTE_DRTS)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj surowce dla wzorca technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"'Zredaguj surowce dla wzorca technologii %1 wer. %2'[NRK,WER]",'')

:: jest parametr wejściowy to ustawiam odpowiedniego TKTL
|? var_pres('TKTL',_in)
|| _tmp:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj surowce dla wzorca technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"'Zredaguj surowce dla wzorca technologii %1 wer. %2'[NRK,WER]",'')

?};
_desc


\tmat_in_process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła rejestrowania listy surowców dla wzorca technologii
::       Używana przez exec('main','!tte_wte_drts')
::   WE: _a - [obj_new] - parametry wejściowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::       _e - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_int:=_b;
_out:=_c;
_mp:=_d;
_context:=_e;

_torw:=VAR.A_TORW:='W';
_arch:=VAR.ARCH:='N';

_akcja:=_mp.akcja();

_ok:=0;
_res:=~~;

{? _in.TKTL
||
   {? exec('FindAndGet','#table',TKTL,_in.TKTL,,"TORW",'')<>_torw
   || _msg:='Niezgodność wywołania czynności.\nNie można redagować surowców dla karty technologicznej.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   |? exec('FindAndGet','#table',TKTL,_in.TKTL,,"ARCH",'')<>_arch
   || _msg:='Niezgodność wywołania czynności.\nNie można redagować surowców dla wzorca archiwalnego.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   || exec('tktl_cntx_psh','tech_common');
      exec('tktl_use','tech_common',ref_name(_in.TKTL)+3);
      TKTL.clear();

      {? TKTL.seek(_in.TKTL)
      ||
         _mp.keyRef(TKTL.uidref());
         _mp.descTodo();

::       Zmienne
         VAR.A_KTL:=TKTL.ref();
         VAR.GRUPA:='N';

::       Właściwa obsługa
::       Zakończenie rejestrowania wywołane z menu okna wertowania TKTL
         {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
         || {? exec('valid_mat','tech_mater',TKTL.ref())
                  &
               FUN.ask('Czy zakończyć rejestrację surowców dla całego wzorca technologii?'@)

            || TKTL.STAT_S:='T';
               TKTL.STAN:='P';
               TKTL.put()
            ?}

::       Lista surowców dla nagłówka - z menu okna wertowania kart technologicznych
         |? _akcja='SEL_M'
         || exec('tmat_main','tech_mater',TKTL.ref(),null(),0,1,0,0)

::       Lista surowców zbiorczo - z menu okna wertowania kart technologicznych
         |? _akcja='SEL_L'
         || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,0,0)

::       Lista surowców dla operacji - z menu okna wertowania operacji
         |? _akcja='SEL_O'
         || exec('tmat_main','tech_mater',TKTL.ref(),_context.oper,0,0,_context.used,_context.zakl)

::       Lista TODO
         |? _akcja=''
         ||
::          Już wcześniej zakończono redagowanie surowców
            {? TKTL.STAT_S='T'
            || FUN.info('Zakończono rejestrację surowców dla całego wzorca technologii.'@)

::          Nie zakończono jeszcze redagowania nagłówka (być może usunięto zatwierdzenie)
            |? TKTL.STAT_N='N'
            || {? exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTE')
               || _choice:=FUN.choice('Nie zakończono rejestracji nagłówka wzorca.'@,,'Zakończ nagłówek i redaguj'@,'Podgląd'@);
                  {? _choice=1
                  || _args:=exec('mp_run_a','#b__box');
                     _args.ACT_UID:='TTE_WTE_DRTE';
                     _args.UIDREF:=TKTL.uidref();
                     _args.AKCJA:='ZAKOŃCZ_BEZ_PYTANIA';
                     exec('mp_run','#b__box',_args)
                  ?};
                  {? _choice>0
                  ||
::                   Zawsze wyświetla wszystkie surowce
                     {? 1
::                        TKTL.TYP().SUR='K'
                     || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,_choice=2,0)
                     || exec('menu_start','tech_head');
                        exec('tree','tech_oper',4);
                        exec('menu_stop','tech_head')
                     ?}
                  ?}
               || _choice:=FUN.info('Nie zakończono rejestracji nagłówka wzorca.\nModyfikacje niemożliwe.'@,,'Podgląd'@,'Anuluj'@);
                  {? _choice>0
                  ||
::                   Zawsze wyświetla wszystkie surowce
                     {? 1
::                        TKTL.TYP().SUR='K'
                     || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,1,0)
                     || exec('menu_start','tech_head');
                        exec('tree','tech_oper',4);
                        exec('menu_stop','tech_head')
                     ?}
                  ?}
               ?}
            || {? 1
::                  TKTL.TYP().SUR='K'
               || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,0,0)
               || exec('menu_start','tech_head');
                  exec('tree','tech_oper',4);
                  exec('menu_stop','tech_head')
               ?}
            ?}
         ?};

         TKTL.clear();
         {? TKTL.seek(_in.TKTL)
         ||
::          Ustawiony status - czynność zostaje zakończona
            {? TKTL.STAT_S='T'
            || _out.TKTL:=TKTL.ref();
               _mp.save(,_out);
               _mp.done();
               _res:='#CLOSE#'

            ?}
         ?}
      ?};
      exec('tktl_cntx_pop','tech_common')

   ?}

|| _msg:='Nie przekazono albo usunięto wzorzec technologii.';
   FUN.emsg(_msg);
   _mp.error(_msg)
?};

_res


\action_menu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie surowców dla wzorca technologii - akcja z menu okna wertowania wzorców (TKTL)
::       gdy surowce przypisane są do wzorca (pełne możliwości redagowania)
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
:: Teraz zawsze ta ścieżka, wyświetlane wszystkie operacje (bez prefixu)
{? 1
::   TKTL.TYP().SUR='O'
|| exec('action_list','!tte_wte_drts')

|? TKTL.STAT_N='N'
|| set_help(exec('set_help','#help','TTE_WTE_DRTS'));
   exec('tmat_main','tech_mater',TKTL.ref(),null(),0,1,0,0)

|? TKTL.ARCH='N' & TKTL.STAT_S='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTS')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRTS';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='SEL_M';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_WTE_DRTS'));
   exec('tmat_main','tech_mater',TKTL.ref(),null(),0,1,0,0)
?}


\action_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie surowców dla wzorca technologii - akcja z menu okna wertowania wzorców technologii (TKTL)
::       gdy surowce przypisane są do operacji (podgląd i ograniczone możliwości redagowania)
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| VAR.A_OP:=null();
   set_help(exec('set_help','#help','TTE_WTE_DRTS'));
   exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,0,0)

|? TKTL.ARCH='N' & TKTL.STAT_S='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTS')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRTS';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='SEL_L';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_WTE_DRTS'));
   exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,0,0)
?}


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zakończenie rejestrowania surowców dla wzorca technologii - obsługa przycisku w oknie wzorców technologii
::       (obsługa z pytaniem)
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_S='N'
|| {? exec('tktl_lock','tech_common',,'S')
   || _args:=exec('mp_run_a','#b__box');
      _args.ACT_UID:='TTE_WTE_DRTS';
      _args.UIDREF:=TKTL.uidref();
      _args.AKCJA:='ZAKOŃCZ_Z_PYTANIEM';
      _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
      exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

      exec('mp_run','#b__box',_args);

      exec('tktl_unlock','tech_common',,'S')
   ?}

|| FUN.info('Zakończono rejestrację surowców dla wzorca technologii.\nModyfikacje niemożliwe.'@)
?};
'key:Esc'


\tmat_dalej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obsługa przycisku 'Zakończ' w oknie wertowania surowców wzorca technologii
::       Zatwierdza surowce i popycha proces dalej
::       Kontekst wywołania - rekord TKTL
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_S='T'
|| FUN.info('Rejestracja surowców wzorca technologii została już zakończona.'@)
|? TKTL.STAT_N='N'
|| FUN.info('Nie zakończono redagowania nagłówka wzorca technologii.'@)
|? exec('valid_mat','tech_mater',TKTL.ref())
      &
   FUN.ask('Czy zakończyć rejestrację surowców dla całego wzorca technologii?'@)
||
:: Rekord mógł być zmodyfikowany przez inne czynności, więc go ponownie wczytuję
   {? TKTL.get()
   || TKTL.STAT_S:='T';
      TKTL.STAN:='P';
      {? TKTL.put()
      || sel_exit()
      ?}
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 0385f10d4ae3f1d7d40cdb64805be0d1b02c467c289070ec78665e5ef944d66f42a8ad38d6f8d695e08479b0c8406e7c45d8182e9707bb8375fcaa9e27acedd17be2ff059aecd5d5ab65e8490781b03302fb0defaeb047855227ebc3ccec5b57451ad76bbb21e9a64ef3cdb6657600e64f29d99619502ac8ee73140f166eafc3

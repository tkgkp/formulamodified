:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_agpu.fml [22.26]
:: Utworzony: 28.10.2021
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_AGPU - Uprawnienia w grupach.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Uprawnienia w grupach - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
:: utwórz bufor nawigatora po danych UDB_GRP i UDB_SYS
_SYS:=exec('mk_sys_buf','!zws_par_agpu');

:: utwórz panel i okna zarządzania
_wnd:=exec('mk_wnd','!zws_par_agpu',_SYS);

:: lista obsługiwanych użytkowników
_kod:=USERS.ndx_tmp('Kod'@,,'AKT',,,'EKIOSK',,,'KOD',,);

:: kontekst przeglądania tabel
_obj:="
   _obj:=obj_new('key','row');
   _obj.key:=_a;
   _obj.row:=null;
   _obj
";
_ctx:=obj_new('users','sys_usr','usr_sys');
_ctx.users:=_obj(_kod);
_ctx.sys_usr:=_obj('SYS_USR');
_ctx.usr_sys:=_obj('USR_SYS');
params_set('ctx',_ctx);

:: udostępnij uprawnienia
_SYS.win_sel(_wnd);
_SYS.select();

:: porządki
obj_del(_SYS);
USERS.ndx_drop(_kod);
~~


\mk_sys_buf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Tworzy tabelę nawigatora po danych GP_GRP i GP_SYS.
::   WE:
::   WY: alias tabeli
::----------------------------------------------------------------------------------------------------------------------
_max:="${? _a>_b || _a || _b ?}";

_BUF:=tab_tmp(2,
   'TREE_REF','TREE_REF','Nadrzędny'@,
   'SYMBOL','STRING[%1]'[_max(MS.fld_len(GP_GRP,'SYMBOL'),MS.fld_len(GP_DEF,'SYMBOL'))],'Symbol'@,
   'OPIS','STRING[%1]'[_max(MS.fld_len(GP_GRP,'OPIS'),MS.fld_len(GP_DEF,'OPIS'))],'Opis'@,
   'REF','STRING[16]','Wskazanie'@
)


\mk_sys_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Tworzy okienko nawigatora po danych GP_GRP i GP_SYS.
::   WE: _a [TABLE] - alias tabeli nawigatora
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_BUF:=_a;

_wnd:=_BUF.mk_sel('Grupy w dziedzinach'@,'P',,'#gp_sys_naw',,,,1);
_BUF.win_fld(_wnd,,'SYMBOL',,,20,,,,,MS.comment(GP_GRP,'SYMBOL'),,,,,,'mobile_visible=1');
_BUF.win_fld(_wnd,,'OPIS',,,74,,,,,MS.comment(GP_GRP,'OPIS'),,,,,,'mobile_visible=1');

_BUF.win_act(_wnd,,'Rekord',,,,"
   _BUF:=cur_tab();
   {? ref_tab(_BUF.REF)=GP_SYS
   || GP_SYS.seek(_BUF.REF)
   ?};
   ~~
");

_wnd


\mk_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Tworzy panel zarządzania uprawnieniami.
::   WE: _a [TABLE] - alias tabeli nawigatora
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_BUF:=_a;
_div:=12;

:: dedykowane okienko dla tabel GP_GRP i GP_SYS
_sys:=exec('mk_sys_wnd','!zws_par_agpu',_BUF);

:: panel zarządzania uprawnieniami
_wnd:=_BUF.grp_make('Uprawnienia'@,
:: przed otwarciem
   "exec('wnd_bo','!zws_par_agpu')",
:: identyfikator
   '#zws_par_agpu',,,
:: podczas zamykania
   "exec('wnd_oc','!zws_par_agpu')"
);
:: nawigator po grupach dziedzin i typach danych
_BUF.grp_sel(_wnd,,_sys,'Wg grup uprawnień'@,
:: po zmianie bieżącego wiersza
   "params_exec('sys_ar','!zws_par_agpu')",,,_div,
:: przed obsługą
   "params_exec('sys_bs','!zws_par_agpu',_a)"
);
:: uprawnienia użytkowników
_BUF.tab_splt(_wnd,,'horizontal','right');
_BUF.grp_sel(_wnd,GP_UPR,'USERS',,
:: po zmianie bieżącego wiersza
   "params_exec('upr_usr_ar','!zws_par_agpu')",,,,
:: przed obsługą
   "params_exec('upr_usr_bs','!zws_par_agpu',_a)",
:: po obsłudze
   "params_exec('upr_usr_as','!zws_par_agpu',_a)"
);
:: uprawnienia według użytkowników
_BUF.grp_sel(_wnd,USERS,'NAW','Wg użytkowników'@,
:: po zmianie bieżącego wiersza
   "params_exec('usr_ar','!zws_par_agpu')",,,_div,
:: przed obsługą
   "params_exec('usr_bs','!zws_par_agpu',_a)",
:: po obsłudze
   "params_exec('usr_as','!zws_par_agpu',_a)"
);
:: uprawnienia użytkowników
_BUF.tab_splt(_wnd,,'horizontal','right');
_BUF.grp_sel(_wnd,GP_UPR,'GP_SYS',,
:: po zmianie bieżącego wiersza
   "params_exec('upr_sys_ar','!zws_par_agpu')",,,,
:: przed obsługą
   "params_exec('upr_sys_bs','!zws_par_agpu',_a)",
:: po obsłudze
   "params_exec('upr_sys_as','!zws_par_agpu',_a)"
);

_wnd


\wnd_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed otwarciem panelu zarządzania uprawnieniami w grupach.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: zachowaj stan tabel
USERS.cntx_psh();
GP_DEF.cntx_psh();
GP_GRP.cntx_psh();
GP_DOM.cntx_psh();
GP_SYS.cntx_psh();
GP_UPR.cntx_psh();
1


\wnd_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Podczas zamykania panelu zarządzania uprawnieniami w grupach.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: przywróć stan tabel
GP_UPR.cntx_pop();
GP_SYS.cntx_pop();
GP_DOM.cntx_pop();
GP_GRP.cntx_pop();
GP_DEF.cntx_pop();
USERS.cntx_pop();
1


\sys_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po odświeżeniu wiersza w oknie nawigatora po tabelach GP_GRP i GP_SYS.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(GP_UPR,'USERS',1,1);
~~


\sys_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed obsługą okna nawigatora po tabelach GP_GRP i GP_SYS.
::   WE: _a - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('synchronize_sys','!zws_par_agpu',cur_tab());
~~


\upr_usr_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po odświeżeniu wiersza w oknie USERS tabeli GP_UPR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

_aid:='';

{? GP_UPR.sel_size()=0
|| {? GP_UPR.DOSTEP='T' || _aid:='N'
   |? GP_UPR.DOSTEP='N' || _aid:='O'
   ?}
?};

GP_UPR.actions_grayed('USERS',_aid)


\upr_usr_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed obsługą okna USERS tabeli GP_UPR.
::   WE: _a - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab().TREE_REF=0
|| return('#disable')
?};

exec('upr_uzupelnij','gp_api',GP_SYS.ref());

GP_UPR.win_edit('USR_RED');
GP_UPR.win_patt('USR_WZO');

_ctx:=params_get().ctx.sys_usr;
GP_UPR.index(_ctx.key);
GP_UPR.seek(_ctx.row);

GP_UPR.prefix(GP_SYS.ref());
~~


\upr_usr_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po obsłudze okna USERS tabeli GP_UPR.
::   WE: _a - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _a=0 || return() ?};

_ctx:=params_get().ctx.sys_usr;
_ctx.key:=GP_UPR.index('?');
_ctx.row:=GP_UPR.ref();
~~


\usr_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po odświeżeniu wiersza w oknie nawigatora tabeli USERS.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(GP_UPR,'GP_SYS',1,1);
~~


\usr_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed obsługą okna nawigatora tabeli USERS.
::   WE: _a - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_ctx:=params_get().ctx.users;
USERS.index(_ctx.key);
USERS.seek(_ctx.row);

USERS.prefix('T','N');
~~


\usr_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po obsłudze okna nawigatora tabeli USERS.
::   WE: _a - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _a=0 || return() ?};

_ctx:=params_get().ctx.users;
_ctx.key:=USERS.index('?');
_ctx.row:=USERS.ref();
~~


\upr_sys_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po odświeżeniu wiersza w oknie GP_SYS tabeli GP_UPR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

_aid:='';

{? GP_UPR.sel_size()=0
|| {? GP_UPR.DOSTEP='T' || _aid:='N'
   |? GP_UPR.DOSTEP='N' || _aid:='O'
   ?}
?};

GP_UPR.actions_grayed('GP_SYS',_aid)


\upr_sys_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed obsługą okna GP_SYS tabeli GP_UPR.
::   WE: _a - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(USERS,'NAW')
|| return('#disable')
?};

GP_UPR.win_edit('SYS_RED');
GP_UPR.win_patt('SYS_WZO');

_ctx:=params_get().ctx.usr_sys;
GP_UPR.index(_ctx.key);
GP_UPR.seek(_ctx.row);

GP_UPR.prefix(exec('ref_firma','ustawienia'),USERS.ref());
~~


\upr_sys_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po obsłudze okna GP_SYS tabeli GP_UPR.
::   WE: _a - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a=0 || return() ?};

_ctx:=params_get().ctx.usr_sys;
_ctx.key:=GP_UPR.index('?');
_ctx.row:=GP_UPR.ref();
~~


\synchronize_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Synchronizuje zawartość nawigatora z tabelami GP_GRP i GP_SYS
::   WE: _a [TABLE] - alias tabeli nawigatora
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_BUF:=_a;

_BUF.cntx_psh();
GP_DEF.cntx_psh();
GP_GRP.cntx_psh();
GP_GRP.index('SYMBOL');
GP_GRP.prefix(exec('ref_firma','ustawienia'));
GP_SYS.cntx_psh();
GP_SYS.index('UNIQUE');

GP_GRP.prefix();
_loop:=GP_GRP.first();
{!
|? _loop
|! _grp:=null;
   _ref:=$GP_GRP.ref();
   {? _BUF.find_tab(1,'REF',,'=',_ref)
   || _grp:=_BUF.ref()
   || _BUF.blank();
      _BUF.SYMBOL:=GP_GRP.SYMBOL;
      _BUF.OPIS:=GP_GRP.OPIS;
      _BUF.REF:=_ref;
      {? _BUF.add()
      || _grp:=_BUF.ref()
      ?}
   ?};
   {? _grp<>null
   || GP_SYS.prefix(GP_GRP.ref());
      _loop:=GP_SYS.first();
      {!
      |? _loop
      |! _ref:=$GP_SYS.ref();
         {? ~_BUF.find_tab(1,'REF',,'=',_ref)
         || GP_SYS.GP_DEF();
            _BUF.blank();
            _BUF.TREE_REF:=_grp;
            _BUF.SYMBOL:=GP_DEF.SYMBOL;
            _BUF.OPIS:=GP_DEF.OPIS;
            _BUF.REF:=_ref;
            {? _BUF.add()
            || _BUF.ref()
            ?}
         ?};
         _loop:=GP_SYS.next()
      !};
      _loop:=GP_GRP.next()
   ?}
!};

GP_GRP.prefix();
GP_SYS.prefix();

_BUF.f_set(,,'TREE_REF<>0');
_loop:=_BUF.f_first();
{!
|? _loop
|! {? GP_SYS.seek(_BUF.REF)
   || GP_SYS.GP_DEF();
      {? _BUF.SYMBOL<>GP_DEF.SYMBOL | _BUF.OPIS<>GP_DEF.OPIS
      || _BUF.SYMBOL:=GP_DEF.SYMBOL;
         _BUF.OPIS:=GP_DEF.OPIS;
         _BUF.put()
      ?};
      _loop:=_BUF.f_next()
   || _loop:=_BUF.del()
   ?}
!};
_BUF.f_set(,,'TREE_REF=0');
_loop:=_BUF.f_first();
{!
|? _loop
|! {? GP_GRP.seek(_BUF.REF)
   || {? _BUF.SYMBOL<>GP_GRP.SYMBOL | _BUF.OPIS<>GP_GRP.OPIS
      || _BUF.SYMBOL:=GP_GRP.SYMBOL;
         _BUF.OPIS:=GP_GRP.OPIS;
         _BUF.put()
      ?};
      _loop:=_BUF.f_next()
   || _loop:=_BUF.del()
   ?}
!};
_BUF.f_clear();

GP_SYS.cntx_pop();
GP_GRP.cntx_pop();
GP_DEF.cntx_pop();
_BUF.cntx_pop();
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 993a15e47bc09821e58a1117cb15c64a9d621b13ff7c783f66279930b2dc2b4debda346db6284a6483875ba880e0ff8ec9cc77cdbd25753c0762ff3ad6bfbfbcc29bd68a3713a5aab695eab6c00a07640c5b6d63dbf329473c7424d225ea7772a5b85e54ace2d373bb02b598b124e7d9386d75185e6d2d7de12b916118f7ccb2

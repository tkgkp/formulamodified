:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pppl.fml
:: Utworzony: 26.11.2015
:: Autor: PT
::======================================================================================================================
:: Zawartość:
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Edycja stałych obszaru Płace - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_data:=exec('od_daty','#stalesys');
{? _data.OK<>1
|| return()
?};

_klasa:=VAR.KLASA;
_wnd:='PPL';
KST.cntx_psh();
KST.win_edit(_wnd);
KST.hdr_edit('Stałe dziedziny płace na dzień %1'@[$_data.OD]);
exec('kst_efld_opt','stalesys',KST,'PPL');
exec('kst_par_nr_zc_au_ae','stalesys',KST,'PPL');
exec('kst_par_nr_rh_au_ae','stalesys',KST,'PPL');

exec('czytaj','#stalesys',_data.OD,KST,KST_PAR);
{? KST.edit("exec('kst_ae','!zws_par_pppl')")
|| exec('zapisz','#stalesys',_data.OD,KST,KST_PAR)
?};

KST.cntx_pop();
VAR.KLASA:=_klasa;
~~


\kst_wsodbr_licz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: pkoso [2009+]
:: OPIS: Wyliczenie wskaźnika odbruttowienia.
::   WE:
::   WY: 1
::  OLD: \licz_brutto/stalesys.fml
::----------------------------------------------------------------------------------------------------------------------
_pr:=KST.PRFE/2$2+KST.PRFRP+KST.PRFC;
{? KST.WSODBR<>_pr
|| KST.WSODBR:=_pr
?};
1


\kst_prfr_licz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyliczenie stopy procentowej ubezpieczenia rentowego.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_pr:=KST.PRFRP+KST.PRFRF;
{? KST.PRFR<>_pr
|| KST.PRFR:=_pr
?};
1


\kst_prfe_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Po redagowaniu pola KST.PRFE.
::   WE:
::   WY: 0/1
::  OLD: \kst_prfe_ae/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('in_interval','#field',0,100,KST,'PRFE')
|| exec('kst_wsodbr_licz','!zws_par_pppl')
?}


\kst_prfrp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola KST.PRFRP.
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? exec('in_interval','#field',0,100,KST,'PRFRP')
|| exec('kst_prfr_licz','!zws_par_pppl');
   exec('kst_wsodbr_licz','!zws_par_pppl')
?}


\kst_prfrf_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola KST.PRFRF.
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? exec('in_interval','#field',0,100,KST,'PRFRF')
|| exec('kst_prfr_licz','!zws_par_pppl')
?}


\kst_prfc_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola KST.PRFC.
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? exec('in_interval','#field',0,100,KST,'PRFC')
|| exec('kst_wsodbr_licz','!zws_par_pppl')
?}


\kst_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" dla tabeli KST.
::       Kontekst: stałe systemu - płace.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=__CHK.record(KST,,'PRFE','PRFRP','PRFRF','PRFC','PRFW','SK','PRKC','PRKO'))<>''
|| return(_chk)

|? KST.PRKC<KST.PRKO
|| FUN.emsg(
      'Stopa procentowa ubezpieczenia zdrowotnego "Do pobrania" nie może być niższa niż\n'
      'stopa procentowa ubezpieczenia zdrowotnego "Do odliczenia".'@);
   return('PRKO')
?};

{? (_chk:=exec('kst_ae_com01','stalesys'))<>''
|| return(_chk)
?};

{? (_chk:=__CHK.record(KST,,'KU','KUZ','KUS','UL','UL_ROK','NK','EMER'))<>''
|| return(_chk)

|? KST.KU>KST.KUZ
|| FUN.emsg('Wartość pola "%1" nie może być większa niż wartość pola "%2".'@ [MS.name(KST,'KU'),MS.name(KST,'KUZ')]);
   return('KU')

|? KST.UL>KST.UL_ROK
|| FUN.emsg('Wartość pola "%1" nie może być większa niż wartość pola "%2".'@ [MS.name(KST,'UL'),MS.name(KST,'UL_ROK')]);
   return('UL')

|? KST.PRZWYN>0 & KST.PRZWYN<KST.NK
|| FUN.emsg('Wartość pola "%1" nie może być mniejsza niż wartość pola "%2".'@
      [MS.name(KST,'PRZWYN'),MS.name(KST,'NK')]);
   return('PRZWYN')

|? (_chk:=exec('kst_ae_prn','stalesys'))<>''
|| return(_chk)

?};
:: Kontrola uzupełniena wartości w polach dotyczących kosztów oddelegowania.
{? PAR_SKID.get(261)='T' & (_chk:=__CHK.record(KST,,'LKZ','PDBP'))<>''
|| return(_chk)
?};

''


\wsp_wal_mozna_modyfikowac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [2008]
:: OPIS: Funkcja sprawdza dla wybranego roku i kwartału tabeli WSP_WAL czy istnieją w tabeli N zapisy.
::       Jeśli występują to wyświetlany jest odpowiedni komunikat.
::   WE: _a [STRING] - Dodatkowy komunikat wyświetlany w przypadku znalezienia nieobecności wykorzystujących bieżący
::                     współczynnik waloryzacji. Może to być kod komunikatu ([P]opraw / [U]suń) lub sam komunikat.
::   WY: Zapis może być modyfikowany? [1/0]
::  OLD: \spr_swiadcz/stalesys.fml
::----------------------------------------------------------------------------------------------------------------------
_mcod:=date(WSP_WAL.ROK,3*WSP_WAL.KWARTAL-2,1);
_mcdo:=date(WSP_WAL.ROK,3*WSP_WAL.KWARTAL,0);

_SWIADCZ:=sql(
   'select N.OD '+
   'from N join R using(N.NB,R.REFERENCE) '+
   'where N.KOR=\'N\' and N.FIRMA=:_d and to_date(:_a)<=N.OD and N.OD<=to_date(:_b) and R.RN in (:_c) '+
   'order by N.OD',_mcod,_mcdo,__RUB.sys_sql(1222,_mcod),exec('ref_firma','ustawienia'));

{? _SWIADCZ.first()
|| FUN.ask(
      'Istnieją zapisy w kartotece nieobecności dla %1 kwartału %2 roku.'@ [$WSP_WAL.KWARTAL,$WSP_WAL.ROK]+'\n'+
      {? _a='P'
      || 'Należy poprawić istniejące zapisy po zmianie współczynnika waloryzacji.'@
      |? _a='U'
      || 'Usunięcie zapisu współczynnika waloryzacji spowoduje niepoprawne wyliczanie kwoty waloryzacji.'@
      |? type_of(_a)=type_of('')
      || _a
      ?}+'\n'+'Kontynuować?'@
   )
|| 1
?}


\wsp_wal_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [2008]
:: OPIS: Wertowanie tabeli WSP_WAL.
::   WE:
::   WY:
::  OLD: \wsp_wal/stalesys.fml
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

WSP_WAL.cntx_psh();
WSP_WAL.index('WSP_WAL');
WSP_WAL.prefix();
WSP_WAL.win_edit('RED');
WSP_WAL.win_patt('RED');
WSP_WAL.win_sel('WER');
WSP_WAL.select(,1,6);
WSP_WAL.cntx_pop();

: Ze względu na miejsce wywołania (przycisk w oknie redagowania stałych) wynikiem musi być napis.
''


\wsp_wal_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [2008]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie tabeli WSP_WAL.
::  OLD: \be_popraw/stalesys.fml
::----------------------------------------------------------------------------------------------------------------------
exec('wsp_wal_mozna_modyfikowac','!zws_par_pppl','P')


\wsp_wal_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [2008]
:: OPIS: Obsługa akcji "Usuń - przed" w oknie tabeli WSP_WAL.
::  OLD: \usunwsp/stalesys.fml
::----------------------------------------------------------------------------------------------------------------------
exec('wsp_wal_mozna_modyfikowac','!zws_par_pppl','U')


\wsp_wal_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [2008]
:: OPIS: Obsługa akcji "Rekord - po" w oknie tabeli WSP_WAL.
::  OLD: \chk_wspwal/stalesys.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(WSP_WAL,-menu_txt()='popraw',,'ROK','KWARTAL','WSP');
{? (type_of(_chk)=type_of('') & _chk<>'') | (type_of(_chk)=type_of(0) & ~_chk)
|| return(_chk)
?};
_maxr:=5+date()~1;
{? WSP_WAL.ROK<1900 | WSP_WAL.ROK>_maxr
|| return(__CHK.err_fld(WSP_WAL,'ROK',1,'Dozwolone wartości z przedziału %1-%2.'@ ['1900',$_maxr]))
?};
{? WSP_WAL.KWARTAL<1 | WSP_WAL.KWARTAL>4
|| return(__CHK.err_fld(WSP_WAL,'KWARTAL',1,'Dozwolone wartości z przedziału %1-%2.'@ ['1','4']))
?};

{? WSP_WAL.WSP<0
|| return(__CHK.err_fld(WSP_WAL,'WSP',1,'Wartość nie może być ujemna.'@))
?};

''


\wz_time
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Wzorzec dla pól zmiennej KST - R_NTYG, R_ODOB, R_OTYG, R_OTYGZ, R_NNTYG, R_NLIM
::   WY: '999:99&'
::  OLD: \wz_time/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
'999:99&'


\kst_r_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Po redakcji pól zmiennej KST - R_NTYG, R_ODOB, R_NNTYG, R_NLIM
::  OLD: \kst_ntyg_ae/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_time:=exec('str2time','#convert',fld());

STR.split($_time,':');
_godz:=form(STR.get_word());
{? cur_afld()='R_ODOB' & #_godz>24
|| FUN.emsg('Długość odpoczynku dobowego nie może przekroczyć 24 godzin.'@);
   _godz:='000';
   _min:='00';
   _wyn:=0
|| _godz:=('000' + _godz)+3;
   _min:=form(STR.get_word());
   _min:=('00' + _min)+2
?};

_fml:=$("KST." + cur_afld + ":=_a");
_fml(_godz + ':' + _min);

_wyn

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:23 a8e63d48afb14f5a6070d761a8f2b6f0682e88b654dfca34e2a0f86376d253d5ee8e6505a50a44afdc8b3ded74c4e53cbde80ef802b03b5fe5336aface4f940da41cb9f63329c87613ff1fe8b6a3f1123242f466ecc3af256ff2c4c8150337b2662105a1d0ae8aee87e93c88fe4dd633dbb71bbe77e94893ee4858587bbb894b

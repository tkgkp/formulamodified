:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_wspr.fml
:: Utworzony: 30.05.2016
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_EZK_WSPR - Wydruk świadectwa pracy
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Wydruk świadectwa pracy - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd','H_UM','H_UM.P().get()')
::
::# kind=WE, symbol=H_UM, type=_H_UM, name=Wskazanie umowy, required=T
::
_par:=params_get();
params_set(_par);
: czynność może być wywołana z listy raportów na pulpicie
{? type_of(_par)=type_of(~~) |
   var_pres('in',_par)<=0 |
   var_pres('mp',_par)<=0
|| {? __F_ZATR.P='P'
   || exec('print','!pkd_ezk_wspr',null(),null(),'1',date(0,0,0))
   || __F_ZATR.control(1,'P')
   ?}
|| _in:=_par.in;
   _mp:=_par.mp;
   _id:=exec('ref2uid','#table',_in.H_UM);
   _wynik:='';
   {? _id=''
   || _wynik:='Błąd'
   || H_UM.cntx_psh();
      H_UM.prefix();
      {? H_UM.seek(#_in.H_UM,form(8+($_in.H_UM)))
      || _wartosc:=exec('print','!pkd_ezk_wspr',H_UM.ref(),H_UM.P,'1',H_UM.OD);
         {? type_of(_wartosc)=type_of(0)
         || {? _mp.isMicro()
            || _mp.cancel()
            || {? _wartosc=1
               || _mp.done()
               || _mp.keep()
               ?}
            ?}
         || _wynik:=_wartosc
         ?}
      || _wynik:='Brak umowy'
      ?};
      H_UM.cntx_pop()
   ?};
   {? _wynik<>''
:  obsługa błędów
   || _mp.error(_wynik);
      FUN.emsg(_wynik)
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Formuła opisu zadania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().mp.load(exec('kind_in','#b_port'));
_tab:=obj_new('NAZWISKO','PIERWSZE','T');
{! _ii..obj_len(_tab) |! _tab[_ii]:='' !};
_pFound:=0;
_hUmFound:=0;
H_UM.cntx_psh();
H_UM.prefix();
{? H_UM.seek(#_in.H_UM,form(8+($_in.H_UM)))
|| _hUmFound:=1;
   P.cntx_psh();
   P.prefix();
   {? P.seek(H_UM.P)
   || OSOBA.cntx_psh();
      OSOBA.prefix();
      _pFound:=1;
      _tab.NAZWISKO:=P.OSOBA().NAZWISKO;
      _tab.PIERWSZE:=P.OSOBA().PIERWSZE;
      _tab.T:=|P.T;
      OSOBA.cntx_pop()
   ?};
   P.cntx_pop()
?};
H_UM.cntx_pop();
{? _hUmFound
|| {? _pFound
   || 'Wydruk świadectwa pracy dla: %1 %2 nr teczki %3.'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.T]
   || 'Brak pracownika.'@@
   ?}
|| 'Brak umowy o pracę.'@@
?}


\print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Drukowanie świadectwa pracy
::   WE: _a - [_H_UM] umowa o pracę
::       _b - [_P] pracownik
::       _c - informacja czy dołączyć załącznik do świadectwa pracy
::       _d [DATE] - Data rozpoczęcia współpracy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
status:=0;
params_set('H_UM',_a,'P',_b,'ZAL',_c);
rep_exec('pkd_zaswswiadectwopracy');

:: Wydruki informacji o skróconym okresie przechowywania akt pracowniczych.
{? _d>date(1998,12,0)
|| _infAkt:=0;
   {? _d<=date(2018,12,0)
   || P.cntx_psh();
      {? P.seek(_b,,1)
      || KART_DEF.cntx_psh();
         KART_DEF.index('SYMBOL');
         KART_DEF.prefix('ZUS_RIA',);
         KART_DOD.cntx_psh();
         KART_DOD.index('KART_DOD');
         {? KART_DEF.first()
         || KART_DOD.prefix(P.FIRMA,KART_DEF.ref(),P.OSOBA);
            {? KART_DOD.find_tab(,'OD',,'<>',date(0,0,0))
            || _infAkt:=1
            ?}
         ?};
         KART_DOD.cntx_pop();
         KART_DEF.cntx_pop()
      ?};
      P.cntx_pop()

   || _infAkt:=1
   ?};
   {? _infAkt
   || rep_exec('pkd_zaswprzechowywanieakt')
   ?}
?};

&status

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:59 11fc741122a7c253a29c7706f746d6315079f1932161887e5e5451fb86e096d76ab31d23c4234ab572624e87449772c4ad6dddc9f2cc234935c1002221c2a2cc1a716bcb0519ce7b7957e11c4f86802fee684f71818a843c3c5e847593880821e48cc916d32909dbf97bb7fa884f176f966fa52fc57ae810f944f300a2475b67

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_pll_rrzp.fml
:: Utworzony: 06.12.2021
:: Autor: DG
::======================================================================================================================
:: Zawartość: Formuły czynności PPL_PLL_RRZP - Rej. rozl. zwol. przychodu.
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Rej. rozl. zwol. przychodu - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
~~


\os_zwpoz_wyk_bfd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła dla akcji Dołącz wykorzystanie - przed z tabeli OS_ZWPOZ
::   WE:
::   WY:
::  OLD: \os_zwpoz_wyk_bd/kali.fml
::----------------------------------------------------------------------------------------------------------------------
OS_ZWPOZ.blank();
OS_ZWPOZ.win_edit('RED');
OS_ZWSLO.win_sel('SLO');
OS_ZWPOZ.WYK_PRZ:='T';
OS_ZWPOZ.ROK:=date()~1;
OS_ZWPOZ.MC:=0;
OS_ZWSLO.fld_fml('ROZLICZ','BLANK',"'T'");
{? OS_ZWPOZ.edit("exec('os_zwpoz_ar','!ppl_pll_rrzp','D')")
|| {? OS_ZWPOZ.add(1)
   || exec('os_zwpoz_wart','!ppl_pll_rrzp',1);
      win_set('cur_row_pos=-1')
   || FUN.emsg('Nie można dodać nowego zapisu.'@)
   ?}
?};
OS_ZWPOZ.win_edit();
OS_ZWSLO.fld_fml('ROZLICZ','BLANK',"*")


\os_zwpoz_wyk_bfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła dla akcji Popraw wykorzystanie - przed z tabeli OS_ZWPOZ
::   WE:
::   WY:
::  OLD: \os_zwpoz_wyk_bp/kali.fml
::----------------------------------------------------------------------------------------------------------------------
OS_ZWPOZ.win_edit('RED');
OS_ZWSLO.win_sel('SLO');
OS_ZWSLO.fld_fml('ROZLICZ','BLANK',"'T'");
{? OS_ZWPOZ.edit("exec('os_zwpoz_ar','!ppl_pll_rrzp','P')")
|| {? OS_ZWPOZ.put(1)
   || exec('os_zwpoz_wart','!ppl_pll_rrzp');
      win_set('cur_row_pos=-1')
   || FUN.emsg('Nie można zmodyfikować zapisu.'@)
   ?}
?};
OS_ZWPOZ.win_edit();
OS_ZWSLO.fld_fml('ROZLICZ','BLANK',"*")


\os_zwpoz_bfu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła dla akcji Usuń - przed z tabeli OS_ZWPOZ
::   WE:
::   WY:
::  OLD: \os_zwpoz_bu/kali.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('del_conf','#table')
|| OS_ZWPKW.cntx_psh();
   OS_ZWPKW.index('OS_ZWPKW');
   OS_ZWPKW.prefix(exec('ref_firma','ustawienia'),OS_ZWPOZ.ref());
   {? OS_ZWPKW.first()
   || {!
      |? OS_ZWPKW.del()
      !}
   ?};
   OS_ZWPKW.cntx_pop();
   OS_ZWPOZ.del()
?}


\os_zwpoz_bfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła dla akcji Rekord - przed z tabeli OS_ZWPOZ
::   WE:
::   WY:
::  OLD: \os_zwpoz_br/kali.fml
::----------------------------------------------------------------------------------------------------------------------
{? OS_ZWPOZ.WYK_PRZ='T'
|| OS_ZWPOZ.actions_grayed('WER');
   OS_ZWPOZ.win_edit('RED')
|| OS_ZWPOZ.actions_grayed('WER','P');
   OS_ZWPOZ.win_edit('RED2')
?}


\os_zwpoz_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Weryfikacja poprawności wprowadzonych danych dla OS_ZWPOZ (Przychód wykorzystany)
::   WE:
::   WY:
::  OLD: \os_zwpoz_ar/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_action:={? var_pres('_a')=type_of('') || _a || 'P' ?};
_result:=__CHK.record2(OS_ZWPOZ,'OS_ZWSLO',,'ROK');

{? _result=''
|| {? OS_ZWPOZ.ROK<=0
   || FUN.emsg('Wartość w polu "Rok" powinna być większa od 0.'@);
      _result:='ROK'
   ?};
   {? _result=''
::    Końcowa kontrola, czy mamy już wprowadzony inne wykorzystanie
   || _ref:=OS_ZWPOZ.ref();
      _osoba:=OS_ZWPOZ.OSOBA;
      _rok:=OS_ZWPOZ.ROK;
      _kod:=OS_ZWPOZ.OS_ZWSLO;
      OS_ZWPOZ.cntx_psh();
      OS_ZWPOZ.index('OS_RWR');
      OS_ZWPOZ.prefix(exec('ref_firma','ustawienia'),_osoba,_rok,'T',_kod);
      {? OS_ZWPOZ.first() & (_action='D' | OS_ZWPOZ.ref()<>_ref)
      || FUN.emsg('W systemie istnieje już zapis dla wybranego roku i rodzaju zwolnienia.'@);
         _result:='ROK'
      ?};
      OS_ZWPOZ.cntx_pop()
   ?}
?};
_result


\os_zwpoz_bfw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła Okienko przed dla tabeli OS_ZWPOZ.
::   WE:
::   WY:
::  OLD: \os_zw_bw/kali.fml
::----------------------------------------------------------------------------------------------------------------------
win_set('cur_row_pos=-1')


\os_zwpoz_mc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formula wykropkowuje pole OS_ZWPOZ.MC, jeśli dotyczy wykorzystanego przychodu (Wyświetl przed)
::----------------------------------------------------------------------------------------------------------------------
OS_ZWPOZ.WYK_PRZ<>'T'


\os_zwpoz_wyk_kw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formula wykropkowuje pole OS_ZWPOZ.WYK_PRZ, jeśli nie dotyczy wykorzystanego przychodu (Wyświetl przed)
::  OLD: \zwpoz_wyk_prz/kali.fml
::----------------------------------------------------------------------------------------------------------------------
OS_ZWPOZ.WYK_PRZ='T'


:\os_zwpoz_p_bfw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formula wykropkowuje pole zlaczeniowe - przed wyswietleniem OS_ZWPOZ.P
::  OLD: \spr_pole_null/kali.fml
::----------------------------------------------------------------------------------------------------------------------
:OS_ZWPOZ.P<>null


\os_zwpoz_chk_fld_null
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formula wykropkowuje pola zlaczeniowe do tabeli - przed wyswietleniem (RH, O)
::  OLD: \spr_pole_null/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_a<>null


\os_zwpoz_wart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Funkcja wyświetla informacje dotyczące wartości dla wybranej pozycji zwolnienia podatkowego (tabela OS_ZWPOZ)
::  OLD: \zw_pod_wart/kali.fml
::   WE: [_a] [INTEGER] - 0/1 Dołącz? Domyślnie: 0 popraw
::   WY: _ret [INTEGER] - dla redakcji 0 gdy nie utworzono wartości, -1 brak uprawnień. W pozostałych przypadkach 1
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_dolacz:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_firma:=exec('ref_firma','ustawienia');
_reczny:=(OS_ZWPOZ.WYK_PRZ='T');
OS_ZWPKW.cntx_psh();
OS_ZWPKW.index('OS_ZWPKW');
OS_ZWPKW.prefix(_firma,OS_ZWPOZ.ref);
{? _reczny
|| OS_ZWPKW.win_sel('WYK');
   OS_ZWPKW.win_edit('RED');
   R.cntx_psh();
   OS_ZWSLO.cntx_psh();
   RA_DEF.cntx_psh();
   R.f_set(,,'R.RN in (:_a)',__RUB.sys_sql(OS_ZWPOZ.OS_ZWSLO().RA_DEF().SYMBOL));
   RA_DEF.cntx_pop();
   OS_ZWSLO.cntx_pop()
|| OS_ZWPKW.win_sel('WER')
?};
OS_ZWPKW.hdr_sel();
OS_ZWPKW.hdr_sel(' %1 %2'@[OSOBA.NAZWISKO,OSOBA.PIERWSZE]);
{!
|? OS_ZWPKW.select();

   _reczny & ~OS_ZWPKW.size() &
   FUN.ask('%1\n%2\n%3'['Pozycja musi posiadać wartości.'@,
      {? ~OS_ZWPOZ.WYK_KW
      || 'W przypadku rezygnacji pozycja %1.'@[{? _dolacz || 'nie zostanie dołączona'@ || 'zostanie usunięta'@ ?}]
      || ''
      ?},
      'Powrócić do edycji?'@])
!};
:: Jeśli to jest dołączanie (lub poprawianie dołączonego "po nowemu") ręcznego wpisu
:: to usuń (nie dodawaj) taką "pustą" pozycję:
{? (_dolacz | ~OS_ZWPOZ.WYK_KW) & _reczny & ~OS_ZWPKW.size()
|| OS_ZWPOZ.del()
?};
{? _reczny
|| R.f_clear();
   R.cntx_pop()
?};
OS_ZWPKW.cntx_pop();

_ret


\edit_var_nazwalis_bfw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Pobranie nazwy listy na podstawie OS_ZWPOZ
::  OLD: \ev_nazwalis_bd/kali.fml
::  OLD: \edit_var_nazwalis_bfw/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
O.cntx_psh();
EDIT_VAR.NAZWALIS:=~-OS_ZWPOZ.O().LT;
O.cntx_pop();
EDIT_VAR.NAZWALIS<>''


\os_zwpkw_os_zwpoz_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: "Wartość początkowa" pola OS_ZWPKW.OS_ZWPOZ
::   WE:
::   WY:
::  OLD: \os_zwpkw_os_zwpoz_bl/kali.fml
::----------------------------------------------------------------------------------------------------------------------
OS_ZWPOZ.ref()


\edit_var_srok_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Przed wyświetleniem pola SROK tabeli EDIT_VAR (suma wartości w roku)
::   WY: ~~
::  OLD: \edit_var_srok_bd/podatek4.fml
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.SROK:=exec('suma_wart_rok','!ppl_pll_rrzp');
~~


\edit_var_srok_n_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Przed wyświetleniem pola SROK_N tabeli EDIT_VAR (narastająca suma wartości w roku)
::   WY: ~~
::  OLD: \edit_var_srok_n_bd/podatek4.fml
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.SROK_N:=exec('suma_wart_rok','!ppl_pll_rrzp',1);
~~


\edit_var_srec_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Przed wyświetleniem pola SREC tabeli EDIT_VAR (Suma wartości wprowadzonych ręcznie w roku)
::   WY: ~~
::  OLD: \edit_var_srec_bd/podatek4.fml
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.SREC:=exec('suma_wart_rok','!ppl_pll_rrzp',,1);
~~


\suma_wart_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Zwraca sumę wartości w roku w kartotece OS_ZWPKW
::UWAGA: Kontekst tabeli OS_ZWPKW musi być ustalony
::   WE: [_a][INTEGER] - 0/1 Czy narastająco do miesiąca pozycji? Domyślnie: 0 - cały rok pozycji
::       [_b][INTEGER] - 0/1 Czy tylko wartości wprowadzone ręcznie? Domyślnie: 0 - wszystkie wartości
::   WY: _suma [REAL]  - suma wartości
::  OLD: \suma_wart_rok/podatek4.fml
::----------------------------------------------------------------------------------------------------------------------
_nar:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_reczne:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_suma:=0.0;
OS_ZWPOZ.cntx_psh();
OS_ZWSLO.cntx_psh();
RA_DEF.cntx_psh();
R.cntx_psh();
_SUM:=exec('rozlicz_suma','lista_licz',OS_ZWPOZ.OS_ZWSLO().RA_DEF().SYMBOL,OS_ZWPKW.OS_ZWPOZ().OSOBA,OS_ZWPOZ.ROK,,,,,
           {? _nar || OS_ZWPOZ.MC || 12 ?},1);
{? _SUM.find_key(OS_ZWPKW.R().RN)
|| _suma:={? _reczne || _SUM.WART_R || _SUM.WART ?}
?};
obj_del(_SUM);
R.cntx_pop();
RA_DEF.cntx_pop();
OS_ZWSLO.cntx_pop();
OS_ZWPOZ.cntx_pop();
_suma


\os_zwpkw_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli OS_ZWPKW.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;

__CHK.validate(OS_ZWPKW
   ,$("_a.table(_b,"+$_put+",,'FIRMA','OS_ZWPOZ','R')")
)


\os_zwpkw_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Formuła "Rekord - po" tabeli OS_ZWPKW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('os_zwpkw_chk','!ppl_pll_rrzp',-menu_txt()='popraw')


\os_zwpkw_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Wyzwalacz "Dołącz - przed" tabeli OS_ZWPKW
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('os_zwpkw_chk','!ppl_pll_rrzp',0)


\os_zwpkw_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Wyzwalacz "Popraw - przed" tabeli OS_ZWPKW
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('os_zwpkw_chk','!ppl_pll_rrzp',1)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 21d0dcd4a9ea1effb040f7d54896b2d6932851424b19ac1224f06f3a5b84526bc10aa25ec3955dae26fb9f7b0c27b9318cc0a21dd9c1e077ca5da36d28f2a14b10b58355bbc290b404992b9f758a44c523229fa2f7260396515deecbbfb314d6c85f94aa0cb3dcb07e08e44e53faeeb4de55a435fedf1daae719bdf0ff534f6a

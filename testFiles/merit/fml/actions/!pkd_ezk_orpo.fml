:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orpo.fml
:: Utworzony: 06.04.2023
:: Autor: MicKoc
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORPO - Rejestracja oświadczeń
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Rejestracja oświadczeń pracy zdalnej - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_ezk_orpo')

|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność '+_mp.buf_act.UID+' nie obsługuje akcji '+_do+'.'
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orpo',_in.P);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Rejestracja oświadczeń pracy zdalnej- formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj oświadczenie pracy zdalnej: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj oświadczenie pracy zdalnej: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj oświadczenie pracy zdalnej: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj oświadczenie pracy zdalnej'@@
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie pracownika
::   WY:
::  OLD: \ppsf_po_select/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!pkd_ezk_orpo');

{? var_pres('_a')<>type_of(null) | _a=null
|| return(_err_msg)
?};

Cntx.psh(P,OSOBA,PPSF_PO,PPSFROZD,PPSF_NO);

P.prefix();
{? ~P.seek(_a)
|| Cntx.pop(P,OSOBA,PPSF_PO,PPSFROZD,PPSF_NO);
   return(_err_msg)
?};

: ustal osobę
P.OSOBA();

: przeglądanie pozostałych
PPSFROZD.win_sel('WER');
PPSF_NO.win_sel('WER');

: pokaż dane
PPSF_PO.index('PRAC');
PPSF_PO.prefix(_a);
PPSF_PO.win_sel('WERI');
PPSF_PO.win_edit('RED');
PPSF_PO.win_patt('RED');
_result:=PPSF_PO.select();

: porządki
Cntx.pop(P,OSOBA,PPSF_PO,PPSFROZD,PPSF_NO);

_result


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli PPSF_PO z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli PPSF_PO jako składowa okna grupowego tabeli UD_DEF).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=PPSF_PO
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie oświadczeń pracy zdalnej niemożliwe.\nNie znaleziono pracownika.'


\ppsf_po_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli PPSF_PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_addb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_po_chk','!pkd_ezk_orpo',0)


\ppsf_po_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli PPSF_PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_putb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_po_chk','!pkd_ezk_orpo',1)


\ppsf_po_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli PPSF_PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_delb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',PPSF_PO.uidref())


\ppsf_po_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed modyfikacją rekordu.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli PPSF_PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_modb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? PPSF_PO.PPSF_NO
|| PPSF_PO.AKCEPT:=exec('ppsf_po_akcept_val','ppsf')
?};
{? PPSF_PO.WYC='N'
|| PPSF_PO.DO:=date(0,0,0)
?};
1


\ppsf_po_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli PPSF_PO.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::  OLD: \ppsf_po_chk/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;

exec('ppsf_po_modb','!pkd_ezk_orpo');
__CHK.validate(PPSF_PO,
   $("_a.table(PPSF_PO,"+$_put+",,'P','PPSF_NO','OD','WYC')"),
   "_a.in_set(PPSF_PO,'WYC',,'T','N')",
   "{? PPSF_PO.WYC='T' || _a.record(PPSF_PO,,'DO') || '' ?}",
   "  {? PPSF_PO.WYC='T' & PPSF_PO.DO<PPSF_PO.OD
      || _a.err_incorrect('DO','Data nie może być wcześniejsza niż w polu ""Data oświadczenia"".');
         'DO'
      || ''
      ?}
   ",
   $("{? exec('ppsf_po','overlap',"+$_put+") || 'OD' || ''?}"),
   "_a.in_set(PPSF_PO,'AKCEPT',,'T','N')",
   "  _ok:=1;
      _wx_size:=exec('ppsf_no_wx_size','ppsf');
      {! _lp:=1 .. _wx_size
      |? _ok
      |! _ok:=_a.in_set(PPSF_PO,'O%1'[$_lp],,'T','N')
      !};
      _ok
   "
)


\ppsf_po_ppsf_no_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Przed redagowaniem pola PPSF_PO.PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_po_ppsf_no_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
{? var_pres('_par')>100 & var_pres('ppsfNo',_par)>0
|| _par.ppsfNo.PPSF_NO:=PPSF_PO.PPSF_NO
?};
1


\ppsf_po_ppsf_no_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pola PPSF_PO.PPSF_NO.
::   WE:
::   WY:
::  OLD: \ppsf_po_ppsf_no_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
{? var_pres('_par')>100 & var_pres('ppsfNo',_par)>0 & _par.ppsfNo.PPSF_NO<>PPSF_PO.PPSF_NO
|| _wx_size:=exec('ppsf_no_wx_size','ppsf');
   {! _lp:=1 .. _wx_size
   |! ($('PPSF_PO.O%1:=\'N\''[$_lp]))()
   !};
   {? PPSF_PO.PPSF_NO
   || PPSF_PO.AKCEPT:=exec('ppsf_po_akcept_val','ppsf')
   ?}
?};
exec('ppsf_po_efld_opt','ppsf')


\ppsf_po_wyc_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [12.51]
:: OPIS: Po redagowaniu pola PPSF_PO.WYC.
::       Pole wprowadzone na wyraźne polecenie PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_wyc_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_po_efld_opt','ppsf')


\ppsf_po_ox_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Po redagowaniu pól PPSF_PO.Ox.
::   WE:
::   WY:
::  OLD: \ppsf_po_ox_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
{? PPSF_PO.PPSF_NO=null()
|| return(0)
?};

PPSF_PO.AKCEPT:=exec('ppsf_po_akcept_val','ppsf');

exec('ppsf_po_efld_opt','ppsf')


\ppsf_po_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli PPSF_PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_dolacz_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_PO.blank();
exec('ppsf_po_efld_opt','ppsf','*',PPSF_PO,'RED')


\ppsf_po_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli PPSF_PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_popraw_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_po_efld_opt','ppsf','*',PPSF_PO,'RED')


\ppsf_po_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Rekord po tabeli PPSF_PO
::   WE:
::   WY:
::  OLD: \ppsf_po_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_po_chk','!pkd_ezk_orpo',-menu_txt()='popraw')


\ppsf_po_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie WER tabeli PPSF_PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_bw/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_po_efld_opt','ppsf','*',PPSF_PO,'RED');
PPSF_PO.display()


\ppsf_po_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Obsługa akcji "Załączniki - przed" w oknach tabeli PPSF_PO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','P','PPSF_PO',exec('chk_role','#b__box',OPERATOR.USER,'PKD_EZK_ORPO'))


\ppsf_po_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Obsługa "Rekord - przed" w oknie WER tabeli PPSF_PO.
::   WE:
::   WY:
::  OLD: \ppsf_po_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
:: Ustawienie maski EDOKUM
{? PPSF_PO.EDOKUM<>null() & (ref_name(PPSFN.EDOKUM)<>EDOKUM.name())
|| EDOKUM.use(ref_name(PPSF_PO.EDOKUM))
?};
''

:Sign Version 2.0 jowisz:1045 2023/06/27 11:10:45 b0d34859749d9936b75cfaddb691563374ff8a2ac35f766bd761c4f445805e7c58c9b6acceb30b7a2d98aa6c0e7c2cc627ff948c7dd4994bcab7498d2a9fc5804154b0ef69d1a0423110a71a9c7ddab748e080bc35f415bc797469a811f35f545648240511a30cef23aad84d09e93e15947d6254aff59b0cfbdd61e0e1e6e377

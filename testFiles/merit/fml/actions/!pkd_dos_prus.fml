:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prus.fml
:: Utworzony: 22.04.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_DOS_PRUS - Rejestracja danych US.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja danych US - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE,   symbol=OSOBA,     type=_OSOBA,   name=Wskazanie osoby,   required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

exec('os_adres_cfg','osoba','BLANK','OSOBA');
OS_ADRES.win_dict('SLO');

_result:='';

_uidref:=exec('ref2uid','#table',_in.OSOBA);
{? _uidref=''
|| _result:=exec('error','!pkd_dos_prus')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_prus',_in.OSOBA);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};

{? _akcja<>'START'
|| exec('os_adres_cfg','osoba','BLANK')
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja danych US - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj dane US: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj dane US: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj dane US: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj dane US'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie danych US niemożliwe.\nNie znaleziono osoby.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie osoby.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & OSOBA.seek(_a)
|| OS_US.cntx_psh();
   OS_US.index('OD');
   OS_US.prefix(OSOBA.ref());

   OS_US.win_sel('WERI');
   OS_US.win_edit('RED');

   _ret:=OS_US.select();
   OS_US.cntx_pop()
|| _ret:=exec('error','!pkd_dos_prus')
?};
OSOBA.cntx_pop();
_ret


\os_us_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Wyzwalacz "Dodaj - po"  dla tabeli OS_US
::   WE:
::   WY:
::  TAG: <MBUILDER><TRIGGER>
::----------------------------------------------------------------------------------------------------------------------
{? _a & do_state()=1
|| exec('upd_do','osoba',OS_US,'OD',OS_US.OSOBA)
?};
~~


\os_us_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Wyzwalacz "Popraw - po"  dla tabeli OS_US
::   WE:
::   WY:
::  TAG: <MBUILDER><TRIGGER>
::----------------------------------------------------------------------------------------------------------------------
{? _a & do_state()=1
|| exec('upd_do','osoba',OS_US,'OD',OS_US.OSOBA)
?};
~~


\os_us_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Wyzwalacz "Usuń - po"  dla tabeli OS_US
::   WE:
::   WY:
::  TAG: <MBUILDER><TRIGGER>
::----------------------------------------------------------------------------------------------------------------------
{? _a & do_state()=1
|| exec('upd_do','osoba',OS_US,'OD',bfld('OSOBA'))
?};
~~


\os_us_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed".
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.RODZAJ:=exec('rodzaj','osoba',OS_US.OS_ADRES().RODZAJ);
0


\os_us_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [2008]
:: OPIS: Obsługa akcji "Rekord - po". Sprawdzenie poprawności dołączonych danych do tabeli OS_US.
::  OLD: \chkus_os/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=__CHK.record(OS_US,,'OD','US'))<>''
|| return(_chk)
?};

{? OS_US.OS_ADRES=null()
|| __CHK.err_empty('Miejscowość');
   return('OS_ADRES')
?};

_ref:={? -menu_txt()='popraw' || OS_US.ref() || null() ?};
OS_US.cntx_psh();
OS_US.index('OD');
OS_US.prefix(OSOBA.ref());
{? OS_US.find_key(OS_US.OD) & OS_US.ref()<>_ref
|| FUN.info('Zapis na dzień %1 już występuje w kartotece.'@ [OS_US.OD$4]);
   _chk:='OD'
?};
OS_US.cntx_pop();
_chk


\os_us_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli OS_US z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli OS_US jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=OS_US
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\os_us_ipb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Oświadczenia PIT-12" dla tabeli OS_US - wertowanie tabeli PIT12O.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PIT12O.cntx_psh();
PIT12O.index('ORP');
PIT12O.prefix(OSOBA.ref());
PIT12O.win_sel('WER');
PIT12O.win_edit('RED');
PIT12O.win_fml('WER',,'RP',,'ICON_BEFORE',"exec('x_zalacz_icon','zalacz')");
PIT12O.select();
PIT12O.cntx_pop();
~~


\os_us_izb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KOD [17.00]
:: OPIS: Obsługa akcji "Zagraniczny nr identyfikacyjny podatkowej" dla tabeli OS_US - wertowanie tabeli OS_ZNIP.
::   WE:
::   WY:
::  OLD: \os_zmip/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
OS_ZNIP.cntx_psh();
OS_ZNIP.index('OSOBAOD');
OS_ZNIP.prefix(OSOBA.ref());
OS_ZNIP.win_sel('WER');
OS_ZNIP.win_edit('RED');
OS_ZNIP.find_ge(date(,1,1));
OS_ZNIP.win_fml('WER',,'OD',,'ICON_BEFORE',"exec('x_zalacz_icon','zalacz')");
OS_ZNIP.select(,1,3);
OS_ZNIP.cntx_pop();
~~


\os_us_iab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Prawa autorskie - koszty i blokady" dla tabeli OS_US - wertowanie tabeli OS_PAB.
::   WE:
::   WY:
::  OLD: \os_pab/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
OS_PAB.cntx_psh();
OS_PAB.index('OSOBAOD');
OS_PAB.prefix(OSOBA.ref());
OS_PAB.win_sel('WER');
OS_PAB.win_edit('RED');
OS_PAB.find_ge(date(,1,1));
OS_PAB.win_fml('WER',,'OD',,'ICON_BEFORE',"exec('x_zalacz_icon','zalacz')");
OS_PAB.select(,1,3);
OS_PAB.cntx_pop();
~~


\os_us_igb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Działalność gospodarcza" dla tabeli OS_US - wertowanie kartoteki dodatkowej "KART_NIP".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kdef:='KART_NIP';
exec('select4cp','kart_dod',_kdef,'PKD_DOS_PRUS','PKD_DOS_PPUS');
{? OSOBA.NIP='' & (_def:=exec('kart_def','kart_dod',_kdef))<>null()
|| KART_DOD.cntx_psh();
   KART_DOD.index('KART_DOD');
   KART_DOD.prefix(exec('ref_firma','ustawienia'),_def,OSOBA.ref());
   {? KART_DOD.first()
   || FUN.info('W przypadku prowadzenia działalności gospodarczej należy uzupełnić pole NIP w Danych osobowych.'@)
   ?};
   KART_DOD.cntx_pop()
?};
~~


\pit12o_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli PIT12O.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',PIT12O.uidref())


\pit12o_rp_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Wartość początkowa pola PIT12O.RP.
::   WE:
::   WY:
::  OLD: \pit12o_rp_bl/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_dt:=date();
{? _dt<=date(,1,10)
|| (_dt~1)-1
|| _dt~1
?}


\pit12o_rp_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed redagowaniem pola PIT12O.RP.
::   WE:
::   WY:
::  OLD: \pit12o_rp_be/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
PIT12O.STATUS='W'


\pit12o_rp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Po redagowaniu pola PIT12O.RP.
::   WE:
::   WY:
::  OLD: \pit12o_rp_ae/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
PIT12O.RP>1900


\pit12o_data_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed redagowaniem pola PIT12O.DATA.
::   WE:
::   WY:
::  OLD: \pit12o_data_be/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
PIT12O.STATUS='W'


\pit12o_koszt_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed redagowaniem pola PIT12O.KOSZTY.
::   WE:
::   WY:
::  OLD: \pit12o_koszt_be/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
PIT12O.STATUS='W'


\pit12o_odl_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed redagowaniem pola PIT12O.ODLICZ.
::   WE:
::   WY:
::  OLD: \pit12o_odl_be/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
PIT12O.STATUS='W'


\pit12o_mc_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed wyświetleniem pola PIT12O.MC.
::   WE:
::   WY:
::  OLD: \pit12o_mc_bd/osoba.fml

::----------------------------------------------------------------------------------------------------------------------
PIT12O.DOZAP>=0 & PIT12O.NADPL=0


\pit12o_mc_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed redagowaniem pola PIT12O.MC.
::   WE:
::   WY:
::  OLD: \pit12o_mc_be/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
PIT12O.DOZAP>=0 & PIT12O.NADPL=0 & (PIT12O.STATUS='W' | PIT12O.STATUS='O')


\pit12o_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed akcją "Popraw" w okienku WER tabeli PIT12O.
::   WE:
::   WY:
::  OLD: \pit12o_pb/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
{? PIT12O.STATUS='W'
|| 1
|? PIT12O.STATUS='O'
|| {? PIT12O.DOZAP>0
   || FUN.emsg(
         'Podatek został obliczony (do zapłaty: %1 zł).\n'
         'Możliwa jedynie zmiana miesiąca pobrania niedopłaty.'@ [form(PIT12O.DOZAP,,2,'9,')]
      );
      1
   || FUN.emsg('Podatek został obliczony.\nZmiany nie są możliwe.'@);
      0
   ?}
|? PIT12O.STATUS='R'
|| FUN.emsg('Deklaracja podatkowa została już rozliczona na liście płac.\nZmiany nie są możliwe.'@);
   0
?}


\pit12o_ub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed akcją "Usuń" w okienku WER tabeli PIT12O.
::   WE:
::   WY:
::  OLD: \pit12o_ub/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
{? PIT12O.STATUS='W'
|| 1
|| _ask:='';
   {? PIT12O.STATUS='O'
   || _ask+='Podatek został obliczony (wystawiono PIT-40).'@
   |? PIT12O.STATUS='R'
   || _ask+='Podatek został obliczony (wystawiono PIT-40) i rozliczony na liście %1.'@ [PIT12O.LT]
   ?};
   FUN.ask(_ask+'\nCzy na pewno chcesz kontynuować?'@)
?}


\pit12o_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed akcją "Załączniki" w okienku WER tabeli PIT12O.
::   WE:
::   WY:
::  OLD: \pit12o_ab/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','PIT12O',exec('chk_role','#b__box',OPERATOR.USER,'PKD_DOS_PRUS'))


\pit12o_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed" dla tabeli PIT12O.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
POLA_GRP.TXT_1:=
   {? PIT12O.STATUS='W'
   || 'Wniosek wprowadzony'@
   |?  PIT12O.STATUS='O'
   || 'Podatek obliczony'@
   |?  PIT12O.STATUS='R'
   || 'Podatek rozliczony'@
   || ''
   ?};
0


\pit12o_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Obsługa akcji "Rekord - po" dla tabeli PIT12O.
::   WE:
::   WY:
::  OLD: \pit12o_ae/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:={? -menu_txt()='popraw' || PIT12O.ref() || null() ?};

_ret:=__CHK.table(PIT12O,_ref<>null(),,'RP','DATA');
{? (type_of(_ret)=type_of('') & _ret<>'') | (type_of(_ret)=type_of(0) & ~_ret)
|| return(_ret)
?};

{? PIT12O.STATUS='W' & date(PIT12O.RP+1,1,10)<=PIT12O.DATA &
   FUN.ask(
      'Oświadczenie dotyczące roku %1 złożone po 10 stycznia %2r.\n'
      'Czy na pewno chcesz kontynuować?'@
      [$PIT12O.RP,$(PIT12O.RP+1)]
   )=0
|| return('DATA')
?};

_pit11:=0;
DEKL_POD.cntx_psh();
DEKL_POD.use('pod_'+form(PIT12O.RP,-4,0,'9.'));
DEKL_POD.index('DEKL_OSO');
DEKL_POD.prefix(PIT12O.OSOBA);
{? DEKL_POD.first()
|| {!
   |? _pit11:=DEKL_POD.RODZAJ='E' & (DEKL_POD.PIT='PIT-11' | DEKL_POD.PIT='(E)PIT-11');
      ~_pit11 & DEKL_POD.next()
   !}
?};
DEKL_POD.cntx_pop();
{? _pit11 & FUN.ask('Dla podatnika wystawiony już został PIT-11.\nCzy na pewno chcesz kontynuować?'@)=0
|| return(0)
?};

''


\pit12o_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Obsługa akcji "Wyświetl" w okienku WER tabeli PIT12O.
::   WE:
::   WY:
::  OLD: \pit12o_wb/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
_acr:='PIT12O_INFO';
{? (_we:=__WND.EDIT.get(PIT12O,_acr))=''
|| _we:=PIT12O.mk_edit('PIT-12',,'pit12o_info');
   PIT12O.win_ewin(_we,,'RED');
   PIT12O.win_ecol(_we);
   PIT12O.win_ewin(_we,,'INFO');
   __WND.EDIT.put(PIT12O,_acr,_we)
?};
PIT12O.win_edit(_we);
PIT12O.display();
PIT12O.win_edit('RED');
P.cntx_pop();
0


\os_pab_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed modyfikacją rekordu. Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli
::       OS_PAB.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: Porządki - zerowanie pól, które są "niedostępne".
{? OS_PAB.TYP='B'
|| exec('blank','#field',OS_PAB,,'KW')
?};
1


\os_pab_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli OS_PAB.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('os_pab_modb','!pkd_dos_prus')


\os_pab_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli OS_PAB.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('os_pab_modb','!pkd_dos_prus')


\os_pab_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli OS_PAB.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',OS_PAB.uidref())


\os_pab_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli OS_PAB. Formuła wywoływana jest
::       w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE: [_a] [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(OS_PAB)
|| _tab:=_b
|? _tryb='1'
|| _tab:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? _fld='' | _fld='TYP'
|| _sval:=$(OS_PAB.TYP='P');
   _tab.efld_opt(_we,'enable='+_sval,OS_PAB,'KW');
   _tab.efld_opt(_we,'mark='+_sval,OS_PAB,'KW')
?};

1


\os_pab_typ_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Po redagowaniu pola OS_PAB.TYP.
::   WE:
::   WY:
::  OLD: \ospab_typ_ae/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
exec('os_pab_efld_opt','!pkd_dos_prus')


\os_pab_kw_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed wyświetleniem pola OS_PAB.KW.
::   WE:
::   WY:
::  OLD: \ospab_kw_bd/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
OS_PAB.TYP='P'


\os_pab_kw_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Przed redagowaniem pola OS_PAB.KW
::   WE:
::   WY:
::  OLD: \ospab_kw_be/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
OS_PAB.TYP='P'


\os_pab_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli OS_PAB.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OS_PAB.blank();
exec('os_pab_efld_opt','!pkd_dos_prus','*',OS_PAB,'RED')


\os_pab_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli OS_PAB.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('os_pab_efld_opt','!pkd_dos_prus','*',OS_PAB,'RED')


\os_pab_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Obsługa akcji Załączniki w okienku WER tabeli OS_PAB.
::   WE:
::   WY:
::  OLD: \ospab_zb/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','OS_PAB',exec('chk_role','#b__box',OPERATOR.USER,'PKD_DOS_PRUS'))


\os_pab_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie WER tabeli OS_PAB.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('os_pab_efld_opt','!pkd_dos_prus','*',OS_PAB,'RED');
OS_PAB.display()


\osp_ab_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Obsługa akcji "Rekord - po" tabeli OS_PAB.
::   WE:
::   WY:
::  OLD: \ospab_ae/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:={? -menu_txt()='popraw' || OS_PAB.ref() || null() ?};

_ret:=__CHK.table(OS_PAB,_ref<>null(),,'OD');
{? (type_of(_ret)=type_of('') & _ret<>'') | (type_of(_ret)=type_of(0) & ~_ret)
|| return(_ret)
?};

{? OS_PAB.TYP='B'
:  Blokada - sprawdźmy, czy pierwsza w tym roku.
|| _rok:=OS_PAB.OD~1;
   _dp:=date(_rok,1,1);
   _dk:=date(_rok,12,31);
   OS_PAB.cntx_psh();
   {? OS_PAB.find_ge(_dp)
   || {!
      |? {? OS_PAB.OD<=_dk
         || {? OS_PAB.ref()<>_ref & OS_PAB.TYP='B'
            || FUN.emsg('Blokada dotycząca roku %1 została już zarejestrowana z datą %2 roku.'@ [$_rok,OS_PAB.OD$6]);
               _ret:='TYP';
               0
            || OS_PAB.next()
            ?}
         ?}
      !}
   ?};
   OS_PAB.cntx_pop();
   _ret

:  Przychód
|? (_ret:=__CHK.record(OS_PAB,,'KW'))<>''
|| _ret

: Czy przed przychodem jest blokada?
|? _ok:=1;
   _dt:=OS_PAB.OD;
   _rok:=_dt~1;
   _dp:=date(_rok,1,1);
   OS_PAB.cntx_psh();
   {? OS_PAB.find_ge(_dp)
   || {!
      |? {? OS_PAB.OD<_dt
         || {? OS_PAB.ref()<>_ref & OS_PAB.TYP='B'
            || _ok:=FUN.ask('Od %1 roku odliczanie preferencyjnych kosztów zostało zablokowane.\n'
                  'Czy na pewno chcesz wprowadzić kwotę przychodu z datą %2 roku?'@ [OS_PAB.OD$6,OS_PAB.OD$6]
               );
               0
            || OS_PAB.next()
            ?}
         ?}
      !}
   ?};
   OS_PAB.cntx_pop();
   ~_ok

|| 'KW'

|? OS_PAB.KW<0
|| return(__CHK.err_fld(OS_PAB,'KW',1,'Wartość nie może być ujemna.'@))

|? _rok:=OS_PAB.OD~1;
   _lim:=exec('kwota_koszty50_zlec_rh','lista_licz',_rok,1);
   _lim>0 & _lim<OS_PAB.KW &
   FUN.ask(
      'Wprowadzana kwota jest wyższa od kwoty limitu (%1) w roku %2.\n'
      'Czy na pewno akceptujesz wprowadzoną wartość?'@
      [form(_lim,,2,'9,'),$_rok]
   )=0
|| 'KW'

|| ''
?}


\os_znip_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Po redagowaniu rekordu tabeli OS_ZNIP.
::   WE:
::   WY:
::  OLD: \osznip_ae/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:={? -menu_txt()='popraw' || OS_ZNIP.ref() || null() ?};

: Kontrola wypełnienia pól zgodnie z definicją kartoteki
_ret:=__CHK.table(OS_ZNIP,_ref<>null(),,'OD','ROP','ZNIP','RZNIP','KRAJZNIP');
{? (type_of(_ret)=type_of('') & _ret<>'') | (type_of(_ret)=type_of(0) & ~_ret)
|| return(_ret)
?};

exec('check','overlap',_ref,OS_ZNIP,,,,,,'OSOBAOD',OS_ZNIP.OSOBA)


\os_znip_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli OS_ZNIP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',OS_ZNIP.uidref())


\os_znip_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.41]
:: OPIS: Obsługa akcji Załączniki w okienku WER tabeli OS_ZNIP.
::   WE:
::   WY:
::  OLD: \osznip_zb/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','OS_ZNIP',exec('chk_role','#b__box',OPERATOR.USER,'PKD_DOS_PRUS'))

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 155af49c792257627486cdea5b42b605f49e59e9df41e696618401f63cd0e139f6fe7f48514a0fc7e2e3ae7b463fe16430baa07c32a63afc6d09271bddfeba901bfc7cb685f21e9049659b02e8a74a9c28fdd242a4a80e8f2c350d937bfc4521a495f66b3176087241e88b6f661845f4d9108b5681cfebb906fae4ee559d2f28

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pwar.fml
:: Utworzony: 21.10.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PWAR - Osoby realizujące wnioski administracyjne.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Osoby realizujące wnioski administracyjne - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
REM_KATG.cntx_psh();
ETYPY.cntx_psh();
ETYPY.index('RODZ_WP');
ETYPY.prefix(3);
ETYPY.first();
WAOR.cntx_psh();
_sel:=exec('grp_make','!zws_par_pwar');
WAOR.win_sel(_sel);
WAOR.index('TYP_KAT');
WAOR.prefix();
WAOR.select();
REM_KATG.cntx_pop();
ETYPY.cntx_pop();
WAOR.cntx_pop();
~~



\grp_make
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Tworzy okienko grupowe z typami, kategoriami i osobami realizującymi wnioski administracyjne
::   WY: STRING - akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_tab:=WAOR;
_grp:=_tab.grp_make('Typy wniosków administracyjnych'@,,'#wa_waor_grp',1,1,,,'maximized');

_tab.grp_sel(_grp,ETYPY,'SLO_WA',,"exec('etypy_after_rfr','!zws_par_pwar')",,,5,
             "grp_disp(REM_KATG,'WER_WA');grp_disp(WAOR,'WER',1)",,,,'maximized_with_title','wa_etypy');
_tab.grp_splt(_grp,,'horizontal','mid');
_tab.grp_sel(_grp,REM_KATG,'WER_WA',,"exec('rem_katg_after_rfr','!zws_par_pwar')",,,5,,,,,'maximized_with_title',
             'wa_rem_katg');
_tab.grp_splt(_grp,,'horizontal','bot',',66%');
_tab.grp_sel(_grp,WAOR,'WER',,,,,,"{? grp_empty(ETYPY,'SLO_WA') | grp_empty(REM_KATG,'WER_WA') || return('#disable') ?}"
             ,,,,'maximized_with_title','wa_waor');
_grp


\etypy_after_rfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Po odświeżeniu w tabeli ETYPY
::----------------------------------------------------------------------------------------------------------------------
WAOR.prefix(ETYPY.ref(),REM_KATG.ref());
grp_disp(REM_KATG,'WER_WA');
grp_disp(WAOR,'WER',1);
~~


\rem_katg_after_rfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Po odświeżeniu w tabeli REM_KATG
::----------------------------------------------------------------------------------------------------------------------
WAOR.prefix(ETYPY.ref(),REM_KATG.ref());
grp_disp(WAOR,'WER',1);
~~


\waor_add_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WAR tabeli WAOR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh();
USERS.prefix();
P.cntx_psh();
P.prefix();

_sql:=
   'select distinct '
      'USERS.WEBLOGIN, USERS.KOD, USERS.DANE, USERS.REFERENCE as "UREF", '
      'OSOBA.NAZWISKO, OSOBA.PIERWSZE, '
      'STN.ST, '
      'P.T, '
      'UD_SKL.SYMBOL as "WYDS", UD_SKL.OPIS as "WYDO", '
      'P.REFERENCE as "PREF" '
   'from P join OSOBA using(P.OSOBA,OSOBA.REFERENCE) join USERS using(OSOBA.REFERENCE,USERS.OSOBA) '
          'join STN using(P.ST,STN.REFERENCE) '
          'join UD_SKL using(P.WYDZIAL,UD_SKL.REFERENCE) '
          'join B_USRROL using(B_USRROL.USERS,USERS.REFERENCE) '
   'where USERS.AKT=\'T\' '
          'and P.REFERENCE in (select REF from :_a) '
          'and (USERS.REFERENCE,P.REFERENCE) not in '
              '(select WAOR.USERS,WAOR.P from WAOR where WAOR.TYP=:_b and WAOR.KAT=:_c) '
          'and B_USRROL.B_ROLE in '
              '(select distinct B_ROLE.REFERENCE from B_ROLE join B_ACTROL using (B_ACTROL.B_ROLE,B_ROLE.REFERENCE) '
              'where B_ACTROL.B_ACTION=:_d ) '
   'order by  OSOBA.NAZWISKO,OSOBA.PIERWSZE,P.T,STN.ST'
;
:: Znalezienie refa czynności realizacji zgłoszenia remontowego
_action:='';
B_ACTION.cntx_psh();
B_ACTION.index('UNIK');
B_ACTION.prefix('TRE_ZGL_RZGL');
{? B_ACTION.first()
|| _action:=B_ACTION.ref()
?};
B_ACTION.cntx_pop();

_TAB:=sql(_sql,exec('dostepne_p','schemat','POR','*T','T'),ETYPY.ref(),REM_KATG.ref(),
          exec('FindAndGet','#table',B_ACTION,_action,,,null));
_TAB.fld_fml('T','DISPLAY_FORMAT',P.fld_fml('T','*DISPLAY_FORMAT'));

_ws:=_TAB.mk_sel('Użytkownicy'@,,,'#upyto',,,,,'U');
_TAB.win_fld(_ws,,'NAZWISKO',,,-20,,,MS.name(OSOBA,'NAZWISKO'),,MS.comment(OSOBA,'NAZWISKO'));
_TAB.win_fld(_ws,,'PIERWSZE',,,-10,,,MS.name(OSOBA,'PIERWSZE'),,MS.comment(OSOBA,'PIERWSZE'));
_TAB.win_fld(_ws,,'T',,,-9,,,MS.name(P,'T'),,MS.comment(P,'T'));
_TAB.win_fld(_ws,,'WYDS',,,-16,,,'Jednostka organizacyjna - kod'@,,'Kod jednostki organizacyjnej'@);
_TAB.win_fld(_ws,,'ST',,,-20,,,MS.name(P,'ST'),,MS.comment(P,'ST'));
_TAB.win_fld(_ws,,'KOD',,,-10,,,'Użytkownik aplikacji'@,,'Kod użytkownika aplikacji'@);
_TAB.win_fld(_ws,,'WEBLOGIN',,,-20,,,'Użytkownik portalu'@,,'Kod użytkownika portalu'@);
_TAB.win_fld(_ws,,'DANE',,,-20,,,'Dane'@,,'Informacje o użytkowniku'@);
_TAB.win_act(_ws,,'Formuła','Wybierz'@@,,,"
      _TAB:=cur_tab(1,1);
      _ret:=0;
      {? USERS.seek(_TAB.UREF) & P.seek(_TAB.PREF)
      || WAOR.cntx_psh();
         WAOR.index('UNIQ');
         WAOR.prefix(ETYPY.ref(),REM_KATG.ref(),USERS.ref(),P.ref());
         _ref:=null();
         {? WAOR.first()
         || _ref:=WAOR.ref()
         || WAOR.blank();
            WAOR.TYP:=ETYPY.ref();
            WAOR.KAT:=REM_KATG.ref();
            WAOR.USERS:=USERS.ref();
            WAOR.P:=P.ref();
            P.cntx_psh();
            P.prefix();
            WAOR.P();
            WAOR.OSOBA:=P.OSOBA;
            WAOR.WYDZIAL:=P.WYDZIAL;
            WAOR.ST:=P.ST;
            WAOR.CP:=P.CP;
            WAOR.F_ZATR:=P.F_ZATR;
            P.cntx_pop();
            WAOR.add()
         ?};
         WAOR.cntx_pop()
      ?};
      1
   ",
   "  {? cur_tab(1,1).sel_size()=0
      || sel_exit()
      ?}
   ",
   1,
   1,,
   "sel_exit()",
   'W'
);
_TAB.win_act(_ws,,'Kolejność');
_TAB.win_sel(_ws);
_TAB.select();

P.cntx_pop();
USERS.cntx_pop();
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 8eace8022140a93eba57e1b227a189caa7fbfd14bae4b2a1919e78e39277842c9f611dc93db2c434498b304648a97a6a9032553f529305897ea6da7285eb8c421411687043bde1332a07bdfa4f2403f2488c457ea5b80f26f41f2f11e53a77438ac06a660971a893716800908ded4950a72b12e13c44f664178eca85badab973

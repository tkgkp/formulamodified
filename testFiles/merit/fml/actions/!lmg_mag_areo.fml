:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_areo.fml
:: Utworzony: 16.03.2016
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Akceptacja reorganizacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# permissions=ODDZ,LMG
::# parses=exec('parses_dk_ln','lmg')
::# kind=WE,   symbol=DK_LN,      type=_DK_LN,    name=Dokument reorganizacji,   required=T,    keyref=T
::# kind=WE,   symbol=GEN_OPE,    type=STRING,    name=Grupa reorganizacji,      required=N,    keyref=T
::# kind=WY,   symbol=RESULT,     type=STRING,    name=Rezultat działania,       required=N
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_context:=params_get().context;

_akcja:=_mp.akcja();
_service:=_mp.isService();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & (_mp.isAutoRun() | _service));

{? var_pres('RESULT',_out)<>type_of('') || _out.RESULT:='OK'; _mp.save('OUT','RESULT','OK') ?};

{? _service & (var_pres('DK_LN',_in)=type_of('') & _in.GEN_OPE<>'')
|| exec('autoOper','!lmg_mag_areo')
|? ~(var_pres('DK_LN',_in)=type_of(null()) & _in.DK_LN)
|| _mp.error('Brak wymaganego parametru DK_LN.')
||
:: dla serwisu ustalamy jego środowisko
   _msk:=ref_name(_in.DK_LN)+3;
   exec('mag_psh','open_tab');
   {? _service | (_auto & _msk<>(DK_LN.name()+3)) || exec('mag_open','open_tab',(1+_msk),(_msk+2)) ?};

   DK_LN.cntx_psh();
   DK_LN.prefix();
   {? ~DK_LN.seek(_in.DK_LN)
   || _mp.error('Nie znaleziono reorganizacji.')
   || {? (_mp.pathArea() & _akcja='Akceptuj') | _auto | _mp.pathTodo()
      || {? DK_LN.STAT_REJ='T'
         || _mp.done()
         || exec('akcreone','magdok_wymiary',DK_LN.ref(),~_auto);
            DK_LN.get();
            {? DK_LN.STAT_REJ='T' || _mp.done() ?}
         ?};
         {? _service & DK_LN.STAT_REJ<>'T'
         || _out.RESULT:='BŁĄD';
            _mp.save(,_out);
            _mp.done()
         ?}
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   DK_LN.cntx_pop();
   exec('mag_pop','open_tab')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('DK_LN',_in)<>type_of(~~) & _in.DK_LN
|| 'Zaakceptuj reorganizację: %1'@@[exec('record','#to_string',_in.DK_LN)]
|| ''
?}


\dk_ln_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Reorganizacja - Akceptuj
::   WE: params_get()
::----------------------------------------------------------------------------------------------------------------------
exec('dk_ln_akceptuj','magdok_wspolne');
~~


\dk_ln_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Reorganizacja - Wycofanie akceptacji i zakończenia
::----------------------------------------------------------------------------------------------------------------------
_no_sel:=~DK_LN.sel_size();

_upr:=exec('chk_EndOrAcc','#b__box','LMG_MAG_DREO','LMG_MAG_AREO');

{? _no_sel
|| _symbol:=exec('record','#to_string',DK_LN.ref());
   _ask:=0;
   {? _upr=1 & DK_LN.STAT_REJ='T'
   || _ask:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia reorganizacji %1'@[_symbol]
             ,,'Akceptację i &zakończenie','&Akceptację')
   |? (_upr=1 | _upr=2) & DK_LN.STAT_REJ='Z'
   || _ask:=FUN.ask('Wycofać zakończenie redakcji reorganizacji %1?'@[_symbol])
   |? _upr=4 & DK_LN.STAT_REJ='T'
   || _ask:=FUN.ask('Wycofać akceptację reorganizacji %1?'@[_symbol]);
      {? _ask || _ask:=2 ?}
   ?}
|| _ask:=0;
   {? _upr=1
   || _ask:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia zaznaczonych reorganizacji.\n\n'
                       'Uwaga. Wycofanie jest uzależnione od uprawnień do czynności redakcji i akceptacji\n'
                       'oraz od aktualnego statusu danego reorganizacji.'@
             ,,'Akceptację i &zakończenie','&Akceptację')
   |? _upr=2
   || _ask:=FUN.ask('Wycofać zakończenie redakcji zaznaczonych reorganizacji?\n\n'
                    'Uwaga. Wycofanie jest uzależnione od uprawnień do czynności redakcji\n'
                    'oraz od aktualnego statusu danej reorganizacji.'@)
   |? _upr=4
   || _ask:=FUN.ask('Wycofać akceptację zaznaczonych reorganizacji?\n\n'
                    'Uwaga. Wycofanie jest uzależnione od uprawnień do czynności akceptacji\n'
                    'oraz od aktualnego statusu danej reorganizacji.'@);
      {? _ask || _ask:=2 ?}
   ?}
?};

{? _ask || exec('wycreorg','magdok_wymiary',,_ask) ?};
~~


\autoOper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.02]
:: OPIS: Automatycznie akceptuje operacje wg kluca grupującego
::   WY: klucz grupujący lub pusty string
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=params_get().in;
_il_srv:=0;
_wyn:='';

_grp_key:=_in.GEN_OPE;

_czyweb:=var_pres('WEB',_in)=type_of('') & _in.WEB='TAK';
_uzuptw:={? var_pres('CZYTW',_in)=type_of(~~) || 'N' || _in.CZYTW ?}='T';

_dkln:=sql('select '+
           '  DK_LN.REFERENCE REF, '+
           'from @DK_LN '+
           ' where DK_LN.GRP_KEY=:_a',_grp_key);

_dkln.clear();
{? _dkln.first()
|| {!
   |? _oddz:=1+(4-_eann.REF);
      _rok:=form((_eann.DATA~1)-2000,-2,0,'99');
      exec('mag_open','open_tab',_oddz,_rok);
      {? (EANN.clear(); EANN.seek(_eann.REF)) & (_wyp:=exec('ctrlEANN','magazyn_mob','R',1))>0
      || _il_srv+=params_exec('autooper','magazyn_mob',EANN.ref(),_wyp,_grp_key,,_uzuptw)
      ?};
      _eann.next()
   !}
?};
obj_del(_eann);

_mp.save('OUT','RESULT','OK:'+$_il_srv);
_mp.save('OUT','GEN_OPE',_grp_key);
{? _il_srv>0 || _wyn:=_grp_key ?};
_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 90ace4ef1fb9125c2fe0f3836fde0644effd90e5adacb78d3f5110fbe32a50bba24819063feddb66a4a502312988e90af233d08222247e2a278544b319b345ead247d58f7edc7b854b90ac478b856a2acc878711b71be9fea7311e13a78d56a5eb0943480f22ffcb67862f384b0acdcf6446ca89a137f9ecba1bfa76be87b794

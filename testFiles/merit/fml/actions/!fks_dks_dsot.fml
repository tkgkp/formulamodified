:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dsot.fml
:: Utworzony: 27.05.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DSOT - Dekretacja dokumentu OT środka
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dekretacja dokumentu OT środka - główna formuła czynności FKS_DKS_DSOT.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
::# parses=exec('parses_srdo','fst')
:: PARAMETRY WE:
::# kind=WE, symbol=SRDO, type=_SRDO, name=Wskazanie na dokument OT, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na dokument księgowy, required=T

_mp:=params_get().mp;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_akcja:=-_mp.akcja();

{? var_pres('SRDO',_we) & type_of(_we.SRDO)=type_of(null) & _we.SRDO<>null
|| exec('fl_decl','dekret_aut');
   exec('dek_decl','dekret_aut');
   exec('sd_decl','dekret_aut');
   _mp.descTodo();
   _auto:=_mp.isAutoRun();
   exec('SRD','object');
   SRD.FST:=exec('szuk_b_dom','parses','FST');
   SRD.maski('r');
   {? SRDO.seek(_we.SRDO)
   || OKRO_F.cntx_psh();
      _okr:=exec('szuk_okr','okresy',SRDO.DO);
      {? OKRO_F.seek(_okr) & OKRO_F.ZAM='N'
      || SRDO.SRSR();
         {? _akcja='dekretuj'
         || _ref:=SRDO.ref();
            SRDO.cntx_psh();
            exec('dekretuj','!fks_dks_dsot');
            SRDO.cntx_pop();
            {? SRDO.seek(_ref) & SRDO.K='T'
            || _wy.DOK:=DOK.ref();
               _mp.save(,_wy);
               _mp.done()
            ?}
         |? _mp.pathTodo() | _mp.isAutoRun()
         || POMOC.OKR:=SSTALE.AO;
            EKSG.SYS:='FST';
            {? _mp.isAutoRun()
            || BtnDek:=1
            || BtnDek:=0;
               _title:='Dokument OT - dekretowanie'@;
               _win:=SRDO.mk_edit(_title,,'_srdo_dekret_ot');
               SRDO.win_ewin(_win,,'RED_P_DE');
               _par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Dekretuj'@];
               _btndek:=SRDO.win_ebtn(_win,_par,"BtnDek:=1; 'key:Esc'");
               _par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@];
               _btnan:=SRDO.win_ebtn(_win,_par,"BtnDek:=0; 'key:Esc'");
               SRDO.btn_eopt(_win,_btndek,'tooltip='+'Dekretuj dokument OT'@);
               SRDO.btn_eopt(_win,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
               SRDO.win_edit(_win);
               {? SRDO.r_lock(1,1)
               || SRDO.display();
                  SRDO.r_unlock()
               || {? ~_mp.isAutoRun() || FUN.emsg('Dokument zablokowany przez innego użytkownika.'@) ?}
               ?}
            ?};
            {? BtnDek
            || {? exec('initWARL','dok_fks_aut_dok',SSTALE.AR,'FST','ET')
               || exec('init','dok_fks_aut_dok','Informacje o błędach dekretacji dokumentów OT'@);
                  SSTALE.AO();
                  SRDO.cntx_psh();
                  _ref:=SRDO.ref();
                  exec('dekretuj','!fks_dks_dsot');
                  SRDO.cntx_pop();
                  {? SRDO.seek(_ref) & SRDO.K='T'
                  || _wy.DOK:=DOK.ref();
                     _mp.save(,_wy);
                     _mp.done();
                     {? ~_mp.isAutoRun() || FUN.info('Dokument OT zadekretowany.'@) ?}
                  || {? ~_mp.isAutoRun() || FUN.info('Dekretowanie dokumentu OT nie powiodło się.'@) ?}
                  ?}
               ?}
            ?};
            VAR_DEL.delete('BtnDek')
         ?}
      || FUN.info('Okres dekretacji dokumentu OT jest zamknięty w obszarze finanse. \nDekretacja niemożliwa.'@);
         _wy.DOK:=null;
         _mp.save(,_wy);
         _mp.done()
      ?};
      OKRO_F.cntx_pop()
   || {? _akcja<>'dekretuj' || FUN.info('Nie znaleziono dokumentu OT.'@) ?};
      _wy.DOK:=null;
      _mp.save(,_wy);
      _mp.done()
   ?}

|| {? _akcja<>'dekretuj' || FUN.info('Nie znaleziono dokumentu OT.'@) ?};
   _wy.DOK:=null;
   _mp.save(,_wy);
   _mp.done()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

SRDO.cntx_psh(); SRDO.prefix();
{? var_pres('SRDO',_in)=type_of(null()) & _in.SRDO & SRDO.seek(_in.SRDO,ref_name(_in.SRDO),1)
|| _desc:='Zadekretuj dokument przyjęcia OT: %1'@@[exec('record','#to_string',_in.SRDO)]
|| _desc:='Zadekretuj dokument przyjęcia OT'@@
?};
SRDO.cntx_pop();
_desc


\dekretuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Funkcja dekretująca
::----------------------------------------------------------------------------------------------------------------------
_tam:=exec('get_gr_from_srdo','fst');
TAM.prefix();
TAM.seek(_tam);
:: sprawdzenie czy wszystkie dane sa ustawione dla naglowka zgodnie z kontrola bledow DEK_NAG.SPR
:: i ustawienie odpowiedniego schematu dekretacji DEK_NAG i przepisanie z stalych rejestru/oddzialu
{? SRDO.r_lock(1,1,1)
|| {? exec('spr_war','dok_fks_aut_dok',SSTALE.AR,'FST','ET')
   || exec('set_fld','dok_fks_aut_dok','N','ET');
      exec('pozycje','dok_fks_aut_dok',SSTALE.AR,'FST','ET')
   ?};
   SRDO.r_unlock()
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 95a6486934c6e973542fccd9ca7acae051e7b7637c69882b8bde4a56154999f9c0659c8bedca4481fa1048c82e8fec12982bde9b5940c28c9e2153fdd93074f47604f058a926ddab2f3f7a20e604e2ee00e726ca82bd3016a84095bcd11d7a00b7574f64d08bed999e004270d2d4ea709bb9dcf2b7efaa0422cadd9a10d05efb

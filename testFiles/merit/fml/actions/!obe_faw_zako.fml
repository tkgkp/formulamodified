:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obg_faw_zako.fml
:: Utworzony: 03.08.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_ZAKO - Ustalenie statusu wniosku w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustalenie statusu wniosku w obiegu - główna formuła czynności OBE_FAW_ZAKO
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=T, keyref=T
::# kind=WE, symbol=TYP, type=STRING, name=Ustalić status/Wycofać status, required=N, fml_val="exec('edokum_zak','obiegi_log',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status wniosku w obiegu, required=N

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_service:=_mp.isService();
_auto:=_mp.isAutoRun();
{? _we.EDOKUM<>~~ & _we.EDOKUM
|| _name:=ref_name(_we.EDOKUM);
   EDOKUM.cntx_psh(); EDOKUM.use(_name); EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM,ref_name(_we.EDOKUM),1)
   || {? _we.TYP=~~ | _we.TYP='' | _we.TYP='Ustalić status'
      || {? _service | _auto | FUN.ask('Ustalić status wniosku w obiegu?'@)
         || EDOKUM.STATUS:=exec('ustal_status','obiegi_log',EDOKUM.uidref());
            {? EDOKUM.STATUS<>''
            || EDOKUM.B_PREL:=''; EDOKUM.B_PRELS:='';
               exec('add_elog','obiegi_akc','Zakończono obieg dokumentu',1)
            ?};
            EDOKUM.put();
            _wy.EDOKUM:=EDOKUM.ref(); _wy.STATUS:=EDOKUM.STATUS;
            _mp.save(,_wy);
            _mp.done()
         ?}
      || {? _service | _auto | FUN.ask('Wycofać status wniosku w obiegu?'@)
         || EDOKUM.STATUS:=''; EDOKUM.put();
            _wy.EDOKUM:=null; _wy.STATUS:=EDOKUM.STATUS;
            _mp.save(,_wy);
            _mp.done()
         ?}
      ?}
   |? _service
   || _wy.EDOKUM:=null; _wy.STATUS:='';
      _mp.save(,_wy);
      _mp.done()
   ?};
   EDOKUM.cntx_pop()
|| _wy.EDOKUM:=null; _wy.STATUS:='';
   _mp.save(,_wy);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Ustal status'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? _we.EDOKUM<>~~ & _we.EDOKUM
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || _desc:='Ustal status %1: %2, data operacji %3'@@[-EDOKUM.TYP().NAZWA,EDOKUM.ID,$EDOKUM.DOP]
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 b7abdfbce3dcbbc38b0afa36c2fb3eb6ddda46f2a7e79c57d836041cb3b83b8e4c876b77a036d381604b30e82effad5d5cd478f8f6098083ed0f04f4678ff59b860078e42a86fdda56734f60905d3162437c4d286498465ca41e8205200ecb21843501a0e814e3e2ebcd805da4db2d10396720e557b1dd72f87d7fad7a4e6b09

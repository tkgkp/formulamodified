:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pco_mokr.fml
:: Utworzony: 09.02.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PCO_MOKR - Zamknięcie okresu controlingowo
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zamknięcie okresu controllingowo - główna formuła czynności CTR_PCO_MOKR.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::# kind=WE,   symbol=OKRO_F,   type=_OKRO_F,   name=Wskazanie na okres obrachunkowy,  required=N, keyref=T
::# kind=WY,   symbol=OKRO_F,   type=_OKRO_F,   name=Wskazanie na okres obrachunkowy,  required=N
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
_ok:=0;
{? _mp.pathProc()
|| {? ~SSTALE.AO
   || FUN.info('Nie wybrano okresu w stałych systemu.'@)
   || _out.OKRO_F:=SSTALE.AO;
      _mp.save(,_out);
      _ok:=exec('close','!ctr_pco_mokr',SSTALE.AO)
   ?}
|? _mp.pathTodo()
|| _okr:={? var_press('OKRO_F',_in)>0 || _in.OKRO_F |? SSTALE.AO || SSTALE.AO || null ?};
   {? _okr
   || _out.OKRO_F:=_okr;
      _mp.save(,_out);
      _ok:=exec('close','!ctr_pco_mokr',_okr)
   || FUN.info('Nie wskazano okresu do zamknięcia i nie ustawiono okresu w parametrach pracy.'@)
   ?}
|? (7+_akcja)='zamknij'
|| {? var_press('OKRO_F',_in)>0
   || _out.OKRO_F:=_in.OKRO_F;
      _mp.save(,_out);
      _ok:=exec('close','!ctr_pco_mokr',_in.OKRO_F)
   ?}
?};
{? _ok>0
|| _mp.done()
|? _ok<0
|| _mp.error()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zamknij okres obszaru Controlling'@@;
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_okr:={? var_pres('OKRO_F',_in)>0 || _in.OKRO_F |? SSTALE.AO || SSTALE.AO || null ?};
{? _okr
|| OKRO_F.cntx_psh();
   OKRO_F.prefix();
   {? OKRO_F.seek(_okr)
   || _desc:='Zamknij okres obszaru Controlling: %1 %2.'@@[OKRO_F.NAZ,OKRO_F.ROK().NAZ]
   ?};
   OKRO_F.cntx_pop()
?};
_desc


\close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła zamyka okres w obszarze środków trwałych
::   WE: _a - wskazanie na rekord OKRO_F
::   WY: 0 - nie udało się, 1 - udało się
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
OKRO_F.cntx_psh();
OKR_OBSZ.cntx_psh();
{? OKRO_F.seek(_a)
|| _okres:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ;
   OKR_OBSZ.index('OKRO_F2');
   _ctr:=exec('szuk_b_dom','parses','CTR');
   OKR_OBSZ.prefix(_ctr,OKRO_F.ref());
   {? OKR_OBSZ.first()
   || {? OKRO_F.ZAM_CON<>'T'
      || {? FUN.ask('Zamknąć controllingowo okres: %1?'@[_okres])
         || DOK.cntx_psh();
            DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
            {? exec('blok_tab','zam_okr_rok')
            || DOK.index('KON'); DOK.prefix('N');
               _jest1:=DOK.first();
               DOK.index('CON'); DOK.prefix('N');
               _jest2:=DOK.first();
               {? _jest1 | _jest2
               || FUN.info('Istnieją niezaakceptowane '+
                     {? _jest1 || 'księgowo ' || '' ?}+
                     {? _jest2
                     || {? _jest1 || 'i ' || '' ?}+'controllingowo '
                     || ''
                     ?}+
                     'dokumenty.\nZamknięcie okresu controllingowo nie jest możliwe.'
                  )
               || {? OKRO_F.get() & OKRO_F.ZAM_CON<>'T'
                  || _wy:=exec('hist_close','!ctr_pco_mokr',OKRO_F.ref())
                  ?}
               ?};
               exec('odbl_tab','zam_okr_rok')
            || FUN.info('Inny operator blokuje operację.\nZamknięcie okresu controllingowo nie jest możliwe.'@)
            ?};
            DOK.cntx_pop()
         ?}
      || FUN.emsg('Okres %1 jest już zamknięty controllingowo.'@[_okres]);
         _wy:=1
      ?}
   || _wy:=-1;
      FUN.emsg('Nie znaleziono okresu %1 w obszarze controllingu.'@[_okres])
   ?}
|| _wy:=-1;
   FUN.emsg('Nie znaleziono okresu.'@)
?};
OKR_OBSZ.cntx_pop();
OKRO_F.cntx_pop();
_wy


\hist_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Historia zamknięć - środki trwałe
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
_b_dom:=exec('szuk_b_dom','parses','CTR');
{? ~_b_dom
|| FUN.info('W systemie nie znaleziono zdefiniowanego obszaru Controlling.'@);
   return(0)
?};
_dalej:=1;
OKRO_F.cntx_psh(); OKR_OBSZ.cntx_psh();
OKRO_F.index('ROK');
{? _a=1
|| OKRO_F.prefix();
   OKR_OBSZ.index('UNIK');
   OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
   {? _b_dom & OKR_OBSZ.first()
   || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
      OKR_OBSZ.OKRO_F()
   || FUN.info('Nie znaleziono dla wybranego wspólnego okresu odpowiedniego okresu obszaru Controlling.'@);
      _dalej:=0
   ?}
?};
{? _dalej
|| KHIST:=_a;
   OKRESY.ZO:=OKRO_F.ref();
   K__HIST.cntx_psh();
   K__HIST.index('OKRO_F');
   K__HIST.prefix(_b_dom,OKRESY.ZO);
   K__HIST.win_sel('SEL_CTR');
   K__HIST.win_edit('RED1');
   {? OKRO_F.ZAM_CON<>'T'
   || K__HIST.actions('SEL_CTR','O:O',,1)
   || K__HIST.actions('SEL_CTR','',,1)
   ?};
   K__HIST.hdr_sel();
   K__HIST.hdr_sel(' okresu %1'@[OKRESY.ZO().NAZ+' '+OKRESY.ZO().ROK().NAZ]);
   K__HIST.select();
   K__HIST.cntx_pop();
   OKRESY.ZO:=null();
   VAR_DEL.delete('KHIST')
?};
OKRO_F.cntx_pop(); OKR_OBSZ.cntx_pop()


\hist_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zapis w historii zamknięć i awaryjnych otwarć okresów informacji o zamknięciu okresu
::   WE: _a - wskazanie na OKRO_F
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
_ctr:=exec('szuk_b_dom','parses','CTR');
{? _ctr & _a
|| K__HIST.cntx_psh();
   OKRO_F.cntx_psh();
   OKRO_F.prefix();
   K__HIST.prefix();
   K__HIST.blank(1);
   K__HIST.STATUS:='Z';
   K__HIST.STATOP:='Zamknięcie';
   K__HIST.OKRO_F:=_a;
   K__HIST.B_DOMAIN:=_ctr;
   K__HIST.DATA:=date();
   K__HIST.CZAS:=time();
   K__HIST.USER:=OPERATOR.USER;
   {? OKRO_F.seek(_a)
   || OKRO_F.ZAM_CON:='T';
      {? OKRO_F.put() & K__HIST.add() || _wy:=1 ?}
   ?};
   OKRO_F.cntx_pop();
   K__HIST.cntx_pop()
?};
_wy

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:11 e508dec7a91b82ba10b1b61b2df8a0453c1ac1c87c2d3afbacd65be28970da5df2f1c893847c755e8f9636dda11efe08d551b263ed87bbfae3e45239639daede77e861db02ab075623544c17b3dfba1002b92872a7cf2e39d0e2619b7fe970472773f5edb433df44dcbb5f1bc5c10e263e700f34edd7cb5e87c2a8dc731e7ae2

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pkfo.fml
:: Utworzony: 27.07.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PKFO - Kryteria formularzy ocen.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Kryteria formularzy ocen - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
exec('init','poc');

_buf:=exec('save_fml_type','#field',ZZ_KRYT,'BLANK');

ZF_DEF.cntx_psh();
ZF_DEF.win_sel(exec('setup_wnd','!zws_par_pkfo'));
ZF_DEF.select();
ZF_DEF.cntx_pop();

exec('restore_fml_type','#field',ZZ_KRYT,'BLANK',_buf);
~~


\setup_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Tworzy okienko zarządzania kryteriami formularzy ocen.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__WND','#object');

_acr:='KRYT';
{? (_wnd:=__WND.SEL.get(ZF_DEF,_acr))<>''
|| return(_wnd)
?};

: Okienko główne czynności
_wnd:=ZF_DEF.grp_make('Kryteria formularzy ocen'@,
:  po wyświetleniu
   "exec('adm_bf','!zws_par_pkfo')",
:  identyfikator
   'zf_def_kryt',
:  położenie
   ,,
:  zamknięcie
   "exec('adm_oc','!zws_par_pkfo')"
);

: Wybór formularza
ZF_DEF.grp_sel(_wnd,,'WEO',,
:  po odświeżeniu
   "exec('adm_zf_def_ar','!zws_par_pkfo')",
:  położenie i wysokość
   ,,10,
:  przed obsługą
   "",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,'maximized_with_title',
:  identyfikator
   'zf_def'
);

ZF_DEF.grp_splt(_wnd,,'horizontal','bottom');
: Okienko przeglądania kryteriów doboru ocenianych
ZF_DEF.grp_sel(_wnd,ZZ_KRYT,'CFG',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "exec('adm_zz_kryt_bs','!zws_par_pkfo',_a)",
:  po obsłudze
   "exec('adm_zz_kryt_as','!zws_par_pkfo',_a)",
:  utrwalenie, aktywacja, wypełnienie
   0,0,'maximized_with_title'
);

__WND.SEL.put(ZF_DEF,_acr,_wnd);

_wnd


\adm_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed wypełnieniem okienka.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
ZZ_POM.MODUL:='O';

ZF_DEF.index('NAZW');
ZF_DEF.prefix(exec('ref_firma','ustawienia'));

ZZ_KRYT.cntx_psh();
ZZ_KRYT.index('UNIQUE');
ZZ_KRYT.fld_fml('ZZ_LINK','BLANK',"ZF_DEF.ZZ_DOK");
ZZ_KRYT.fld_fml('ZZ_TYP','BLANK',"exec('typ_op','phr_dane')");
ZZ_KRYT.fld_fml('ZZ_KRYT','BLANK',"exec('zz_kryt_zz_kryt_bl','phr_widok')");

ZZ_KRYT.actions('CFG','Y:Y',,1);

1


\adm_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przy zamykaniu okienka.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
ZZ_KRYT.cntx_pop();
ZZ_KRYT.actions('CFG','');
1


\adm_zf_def_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienka WEO tabeli ZF_DEF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
grp_disp(ZZ_KRYT,'CFG',1)


\adm_zz_kryt_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka CFG tabeli ZZ_KRYT.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| exec('zz_pom_psh','phr_widok','ZWS_PAR_PKFO');
   ZZ_KRYT.dnd_sel('CFG',,'records.ZZ_KRYT',"exec('zz_kryt_dnd','phr_widok')")
?};

_ret:='';
_dok:=null;
{? ~grp_empty('zf_def')
|| _dok:=ZF_DEF.ZZ_DOK
|| _ret:='#disable'
?};
ZZ_KRYT.prefix(ref_name(_dok),_dok,exec('typ_op','phr_dane'));

_ret


\adm_zz_kryt_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka CFG tabeli ZZ_KRYT.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| exec('zz_pom_pop','phr_widok');
   ZZ_KRYT.dnd_sel('CFG',,'records.ZZ_KRYT',"")
?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:05 2847f8c4935643d613140e3d0d7f77a9dc11498e783346ec18adac9ff4244cc94a0e9a7a4aeedadac1338fdeff3c65546ed437abd419d0309bf0e1ecbd77c5c86950fefb70f70311664db9efcb61b896b918751cfb39c9e23711e1ee11fc8cd39f88be6b739513eafc5a6476f16908ea9c2fefcaa092f4d0ddd0ae695f283f0b

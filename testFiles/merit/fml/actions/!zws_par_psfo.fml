:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_psfo.fml
:: Utworzony: 14.07.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PSFO - Struktury formularzy ocen.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Struktury formularzy ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','poc');

_modul:='O';
ZZ_POM.MODUL==_modul;
ZZ_POM.CZYNNOSC:='';

_cfg:=exec('cfg','!zws_par_psfo');
params_set('cfg',_cfg);

_tblank:='BLANK';

ZF_DEF.cntx_psh();
ZF_DEF.f_clear();
ZF_DEF.index('NAZW');
ZF_DEF.prefix(exec('ref_firma','ustawienia'));
ZF_DEF.win_sel(_cfg.ZF_DEF.wnd);

ZF_POZ.cntx_psh();
ZF_POZ.f_clear();
ZF_POZ.index('NUMER');

ZF_WID.cntx_psh();
ZF_WID.f_clear();
ZF_WID.index('TYP');
_wid:=exec('save_fml_type','#field',ZF_WID,_tblank);
ZF_WID.fld_fml('ZZ_LINK',_tblank,"ZF_POZ.ZZ_DOK");

ZF_DEF.select();

exec('restore_fml_type','#field',ZF_WID,_tblank,_wid);
ZF_WID.actions_grayed(_cfg.ZF_WID.ws,':');
ZF_WID.cntx_pop();

ZF_POZ.cntx_pop();

ZF_DEF.cntx_pop();

ZZ_POM.MODUL==_modul;
~~


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła tworzy okno grupowe do obsługi elementów formularzy ocen.
::   WE: _a [ARRAY] - Tablica elementów nazwanych z konfiguracją.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=_a;

_fid:="'psfo%1' [_a]";

_acr:=~_fid('_');

{? (_wnd:=__WND.SEL.get(ZF_SKL,_acr))<>''
|| return(_wnd)
?};

_wnd:=ZF_DEF.grp_make('Struktury formularzy ocen'@,,_fid('grp'),,,,,'maximized');
__WND.SEL.put(ZF_DEF,_acr,_wnd);

_mode:='maximized_with_title';

:: Formularze - nagłówek.
ZF_DEF.grp_sel(_wnd,ZF_DEF,_cfg.ZF_DEF.ws,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      grp_disp(ZF_POZ,_cfg.ZF_POZ.ws,1)
   ",,,
   5,,,,,
   _mode,
   _fid('zf_def')
);
:: Formularze - pozycje.
ZF_DEF.grp_splt(_wnd,,'horizontal','zf_poz');
ZF_DEF.grp_sel(_wnd,ZF_POZ,_cfg.ZF_POZ.ws,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      grp_disp(ZF_WID,_cfg.ZF_WID.ws,1)
   ",,,
   15,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? grp_empty(ZF_DEF,_cfg.ZF_DEF.ws)
      || '#disable'
      || ZF_POZ.prefix(ZF_DEF.ref())
      ?}
   ",
   "",,,
   _mode,
   _fid('zf_poz')
);
:: Uprawnienia.
ZF_DEF.grp_splt(_wnd,,'horizontal','zf_wid',',66%');
ZF_DEF.grp_sel(_wnd,ZF_WID,_cfg.ZF_WID.ws,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _ga:={? ZF_WID.WYMOG_B='T' & ZF_WID.OCENA_B='T' & ZF_WID.WIDOK_B='T' & ZF_WID.WYNIK_B='T' || 'p' || ':' ?};
      ZF_WID.actions_grayed(_cfg.ZF_WID.ws,_ga)
   ",,,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      ZF_SKL.cntx_psh();
      _staly:=ZF_POZ.ZF_SKL().STALY='T';
      ZF_SKL.cntx_pop();
      {? grp_empty(ZF_POZ,_cfg.ZF_POZ.ws) | _staly
      || '#disable'
      || ZF_WID.prefix(ZF_POZ.NP_DOK,ZF_POZ.ZZ_DOK)
      ?}
   ",
   "",,,
   _mode,
   _fid('zf_wid')
);

_wnd


\cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła przygotowuje środowisko pracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=obj_new('ACT','ZF_DEF','ZF_POZ','ZF_WID');

_cfg.ACT:='ZWS_PAR_PSFO';

_cfg.ZF_POZ:=obj_new('ws');
_cfg.ZF_POZ.ws:='WER';

_cfg.ZF_WID:=obj_new('ws');
_cfg.ZF_WID.ws:='WER';

_cfg.ZF_DEF:=obj_new('ws','wnd');
_cfg.ZF_DEF.ws:='WER';
_cfg.ZF_DEF.wnd:=exec('wnd','!zws_par_psfo',_cfg);

_cfg


\zf_def_mod_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła wykonywana przed akcjami "Popraw" i "Usuń" w oknach wertowania tabeli ZF_DEF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('zf_test_wyk','phr_widok',ZF_DEF.ref())
|| exec('zf_cmod_pyt','phr_widok')
|| 1
?}


\zf_def_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Popraw - przed" w oknach wertowania tabeli ZF_DEF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zf_def_mod_b','!zws_par_psfo')


\zf_def_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Usuń - przed" w oknach wertowania tabeli ZF_DEF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('zf_def_mod_b','!zws_par_psfo') & (ZF_DEF.sel_size()  | exec('del_conf','#table'))
|| exec('zz_xxx_usun_b','phr_widok')
?}


\zf_def_kopiuj_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Kopiuj - przed" w oknach wertowania tabeli ZF_DEF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_def:=null();
_nazwa:='';
ZF_DEF.cntx_psh();
ZF_DEF.clear();
ZF_DEF.f_set('NAZWA',,'ZF_DEF.FIRMA=:_a and ZF_DEF.REFERENCE<>:_b',exec('ref_firma','ustawienia'),ZF_DEF.ref());
ZF_DEF.win_sel('WYB');
{? ZF_DEF.select()
|| _nazwa:=ZF_DEF.NAZWA;
   _def:=ZF_DEF.ref()
?};
ZF_DEF.f_clear();
ZF_DEF.cntx_pop();
{? _def=null()
|| return()
?};
{? ~FUN.ask('Czy na pewno uzupełnić formularz "%1"\no elementy formularza "%2"?'@ [ZF_DEF.NAZWA,_nazwa])
|| return()
?};
_ref:=ZF_DEF.ref();
ZF_POZ.cntx_psh();
ZF_POZ.index('NUMER');
ZF_POZ.prefix(ZF_DEF.ref());
_numer:={? ZF_POZ.last() || ZF_POZ.NUMER ?};
ZF_POZ.clear();
ZF_POZ.f_set('NUMER',,'ZF_POZ.ZF_DEF=:_a',_def);
_loop:=ZF_POZ.f_first();
{!
|? _loop
|! _skl:=ZF_POZ.ZF_SKL;
   ZF_POZ.blank(1);
   ZF_POZ.ZF_DEF:=_ref;
   ZF_POZ.ZF_SKL:=_skl;
   _jest:=ZF_POZ.find_rec();
   ZF_POZ.f_get();
   {? ~_jest
   || ZF_POZ.ZF_DEF:=_ref;
      ZF_POZ.NUMER:=(_numer+=1);
      ZF_POZ.add(1)
   ?};
   _loop:=ZF_POZ.f_next()
!};
ZF_POZ.f_clear();
ZF_POZ.cntx_pop();
~~


\zf_def_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Rekord - po" w oknach wertowania tabeli ZF_DEF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zf_def_chk','phr_zf_tab',-menu_txt()='popraw')


\zf_poz_zf_def_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wskazanie na nagłówek struktury formularzy ocen.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZF_DEF.ref()


\zf_poz_zf_skl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Po redagowaniu pola ZF_POZ.ZF_SKL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ZF_POZ.ZF_SKL
|| ZF_RPI.cntx_psh();
   {? ZF_POZ.ZF_SKL<>ZF_POZ.ZF_RPI().ZF_SKL
   || ZF_POZ.ZF_RPI:=null()
   ?};
   ZF_RPI.cntx_pop();
   {? -menu_txt()='dołącz'
   || ZF_SKL.cntx_psh();
      ZF_POZ.DOKUMENT:=ZF_POZ.ZF_SKL().DOKUMENT;
      ZF_SKL.cntx_pop()
   ?}
|| ZF_POZ.ZF_RPI:=null()
?};
exec('zf_poz_efld_opt','phr_widok')


\zf_poz_dokument_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Po redagowaniu pola ZF_POZ.DDOKUMENT.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zf_poz_efld_opt','phr_widok')


\zf_poz_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli ZF_POZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('zf_def_mod_b','!zws_par_psfo')
|| ZF_POZ.blank();
   ZF_POZ.efld_opt('RED','enable=1',ZF_POZ,'DOKUMENT');
   exec('zf_poz_efld_opt','phr_widok','*',ZF_POZ,'RED')
?}


\zf_poz_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli ZF_POZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('zf_def_mod_b','!zws_par_psfo')
|| exec('zf_poz_efld_opt','phr_widok','*',ZF_POZ,'RED')
?}


\zf_poz_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Usuń - przed" w oknie WER tabeli ZF_POZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('zf_def_mod_b','!zws_par_psfo') & exec('del_conf','#table')
|| _msg:=no_msg(0);
   {? ~ZF_POZ.del(,1)
   || exec('del_warn','#table')
   ?};
   no_msg(_msg)
?}


\zf_poz_gora_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Przesuń - w górę" w oknie wertowania WER tabeli ZF_POZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
do();
_ref:=ZF_POZ.ref();
_numer:=ZF_POZ.NUMER;
{? ZF_POZ.prev()
|| _prev:=ZF_POZ.ref();
   ZF_POZ.NUMER:=-1;
   {? ZF_POZ.put() & ZF_POZ.seek(_ref)
   || ZF_POZ.NUMER-=1;
      {? ZF_POZ.put() & ZF_POZ.seek(_prev)
      || ZF_POZ.NUMER:=_numer;
         ZF_POZ.put()
      ?}
   ?}
?};
ZF_POZ.seek(_ref);
end()


\zf_poz_dol_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Przesuń - w dół" w oknie wertowania WER tabeli ZF_POZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
do();
_ref:=ZF_POZ.ref();
_numer:=ZF_POZ.NUMER;
{? ZF_POZ.next()
|| _next:=ZF_POZ.ref();
   ZF_POZ.NUMER:=-1;
   {? ZF_POZ.put() & ZF_POZ.seek(_ref)
   || ZF_POZ.NUMER+=1;
      {? ZF_POZ.put() & ZF_POZ.seek(_next)
      || ZF_POZ.NUMER:=_numer;
         ZF_POZ.put()
      ?}
   ?}
?};
ZF_POZ.seek(_ref);
end()


\zf_poz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli ZF_POZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zf_poz_chk','phr_zf_tab',-menu_txt()='popraw')


\zf_poz_dokument_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Przed redagowaniem pola ZF_POZ.DDOKUMENT.
::----------------------------------------------------------------------------------------------------------------------
ZF_POZ.ZF_SKL().KOD<>'KD' & ZF_POZ.ZF_SKL().KOD<>'SZ' & ZF_POZ.ZF_SKL().KOD<>'ST'

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 90f40ca7522b1154886ac6a19ffab554dbc29eaad92df5476a9038817123c17af428cf6ec527ad0e75de5f37bb3d9fc332ce988f1f26b40407d72655b96c9e082b3c24f95a5df8ca20bafa483ea04399b7ce0fd75fd3cfaf6a7520ba0cf1e93cfc76b0084513abb7d4429ff2464fb655d7eb9e9e548b24101936daad1f62d645

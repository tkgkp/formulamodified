:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !rod_req_wsnd.fml
:: Utworzony: 23.04.2018
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ROD_REQ_WSND - Powiadomienia o żądaniach z RODO Utility do obsłużenia
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Powiadomienia o żądaniach z RODO Utility do obsłużenia - formuła główna czynności.
::  OLD: \snd_mail/ro_ws.fml
::----------------------------------------------------------------------------------------------------------------------
::# properties=SEND
:: Parametry WE
::# kind=WE, symbol=EMAIL, type=STRING, name=Adres e-mail odbiorcy powiadomienia, required=N

_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_komunik:=(_mp.pathTodo() | _mp.pathProc()) & ~_mp.isAutoRun() & ~_mp.isService();
_email:={? var_press('EMAIL',_in)>0
        || _in.EMAIL
        || exec('czytaj','#stalesys',,XINFO,'EM_IDO');
           XINFO.EM_IDO
        ?};
_ok:=1;
{? _email=''
|| _ok:=0;
   {? _komunik
   || FUN.info('Nie podano adresu e-mail odbiorcy powiadomienia.'@)
   ?}
?};
{? _ok
|| _ok:=~_komunik |
   FUN.ask('Czy uruchomić wysyłanie powiadomień e-mail\no żądaniach z RODO Utility do obsłużenia?'@)
?};
{? _ok
|| RO_REQN.index('SUPPORT'); RO_REQN.prefix('N','N');
   {? RO_REQN.first()
   || _args:=exec('add_email_a','#mailbox');
      _args.FROM:=_in.FROM;
      _args.SENDER:=_in.SENDER;
      _args.REPLYTO:=_in.REPLYTO;
      _args.RCV:=_email;
      _args.SUB:=exec('header','!rod_req_wsnd');
      _args.BODYH:=exec('body','!rod_req_wsnd');
      exec('add_email','#mailbox',_args)
   ?};
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
'Wyślij powiadomienia o żądaniach do obsłużenia z RODO Utility'@@


\header
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Nagłówek wiadomości e-mail o nieobsłużonych żądaniach z RODO Utility
::  OLD: \mail_hed/ro_ws.fml
::----------------------------------------------------------------------------------------------------------------------
'Nieobsłużone żądania z RODO Utility'


\body
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.52]
:: OPIS: Treść wiadomości e-mail o nieobsłużonych żądaniach z RODO Utility
::  OLD: \mail_bod/ro_ws.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('BODY')>0
|| BODY.erase()
|| BODY:=obj_new('TAB','add','erase','LP');
   BODY.TAB:=tab_tmp(1,
      'LP','INTEGER','Lp',
      'TEKST','STRING[255]','Tekst'
   );
   BODY.LP:=0;
   BODY.add:="
      {!
      |? {? _a<>''
         || .LP+=1;
            .TAB.LP:=.LP;
            .TAB.TEKST:=255+_a;
            .TAB.add();
            _a:=255-_a;
            _a<>''
         ?}
      !}
   ";
   BODY.erase:=".LP:=0;.TAB.erase()"
?};
{? RO_REQN.first()
|| BODY.add('<h4>Nieobsłużone żądania z RODO Utility</h4>');
   BODY.add('<table align=\"center\" valign=\"top\" border=\"1\" cellspacing=\"0\" cellpadding=\"0\"  width=\"100%\">');
   BODY.add(
      '<tr class="head">'+
      '<td width="30" align=center>Data</td>'+
      '<td width="30" align=center>Czas</td>'+
      '<td width="300" align=center>Typ żądania</td>'+
      '<td width="350" align=center>Podmiot danych</td></tr>'
   );
   {!
   |? BODY.add(
         '<tr class="wiersz">'+
         '<td width="30" align=left>'+$RO_REQN.DATE+'</td>'+
         '<td width="30" align=left>'+$RO_REQN.TIME+'</td>'+
         '<td width="300" align=left>'+RO_REQN.RO_REQT().NAME+'</td>'+
         '<td width="400" align=left>'+RO_REQN.SUBJECT+'</td></tr>'
      );
      RO_REQN.next()
   !};
   BODY.add('</table>')
?};
BODY.TAB

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 470f02d2155116611673739b1bc2eb0c87af71aee916d73f7c7746b8ab50580f97509ed941bc364c6363b285469da6d92388cf8b920ad15b6842d749e7f83265f89dd56776d64b3e659f49d79617c6bef9d011a0f225f47d0b7700ec564bbf19c9dbdaa29d59a7252acd4410f81f22383dc4c94733fc70e1b641da9d1368a402

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_hbd_zhar.fml
:: Utworzony: 12.11.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_HBD_ZHAR - Przeglądanie harmonogramów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie harmonogramów - główna formuła czynności CTR_HBD_ZHAR
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
exec('ini_tmp','!ctr_hbd_zhar');
exec('ini_win_harm','control','POZ',UNPAR,'P10');
K_HARM_P.win_sel('POZ');
_b.grp_sel(_c,TT_ROK,TT_ROK.win_sel('?'),_a,"
   exec('filtr','!ctr_hbd_zhar')
",,,,"

",,,,'maximized_with_title');
task_attach('CTR_HBD_ZHAR');
task_attach('CTR_HBD_DHAR');
_b.tab_splt(_c,,'horizontal','bottom',13);
_b.grp_sel(_c,TT_GRUPA,TT_GRUPA.win_sel('?'),,"
   exec('filtr','!ctr_hbd_zhar')
",,,,,,,,'maximized_with_title');
_b.tab_splt(_c,,'vertical','left',',15%');
_b.grp_sel(_c,K_HARM,'WER',,"
   _hid:='';
   K_HARM_P.index('K_HARM');
   {? {? K_HARM.sel_size()<=0 || K_HARM.f_get() ?}
   || K_HARM_P.prefix(K_HARM.ref())
   || K_HARM_P.prefix(null);
      _hid:=':D'
   ?};
   K_HARM_P.actions('POZ',_hid,,1)
",,,,,,,,'maximized_with_title')


\ini_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Tworzy tabele tymczasowe lat i grup modeli
::----------------------------------------------------------------------------------------------------------------------
TT_ROK:=tab_tmp(1,
   'NAZ','STRING[20]','Rok',
   'REF','INTEGER',
);
_o:=TT_ROK.mk_sel('Lata bilansowe'@,'P',,'roksel',,,,,'U');
TT_ROK.win_fld(_o,,'NAZ');
TT_ROK.win_sel(_o);
TT_ROK.NAZ:='Wszystkie';
TT_ROK.REF:=0;
TT_ROK.add();
TT_ROK.cntx_psh();
ROK_F.index('ROKKON'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.first()
|| {!
   |? TT_ROK.NAZ:=ROK_F.NAZ;
      TT_ROK.REF:=#ROK_F.ref();
      TT_ROK.add();
      ROK_F.next()
   !}
?};
TT_ROK.cntx_pop();

TT_GRUPA:=tab_tmp(1,
   'NAZ','STRING[40]','Nazwa',
   'REF','INTEGER',
);
_o:=TT_GRUPA.mk_sel('Grupa modeli'@,'P',,,,,,,'U');
TT_GRUPA.win_fld(_o,,'NAZ',,,20);
TT_GRUPA.win_sel(_o);
TT_GRUPA.NAZ:='Wszystkie';
TT_GRUPA.REF:=0;
TT_GRUPA.add();
TT_GRUPA.cntx_psh();
SKID_MBG.index('NAZ'); SKID_MBG.prefix();
{? SKID_MBG.first()
|| {!
   |? TT_GRUPA.NAZ:=SKID_MBG.NAZ;
      TT_GRUPA.REF:=#SKID_MBG.ref();
      TT_GRUPA.add();
      SKID_MBG.next()
   !}
?};
TT_GRUPA.cntx_pop();
Tmbn:=tab_tmp(1,
   'REF','STRING[16]',
);
Tokro:=tab_tmp(1,
   'REF','STRING[16]',
)


\k_harm_p_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.xx]
:: OPIS: Pozycje Harmonogramu
::----------------------------------------------------------------------------------------------------------------------
K_HARM_P.win_sel('POZ');
K_HARM_P.select(,,,'C')


\filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia filtr w harmonogramach
::----------------------------------------------------------------------------------------------------------------------
Tmbn.erase();
{? TT_GRUPA.REF
|| SKID_MBN.index('KOD');
   SKID_MBN.prefix();
   {? SKID_MBN.first()
   || {!
      |? {? #SKID_MBN.SKID_MBG=TT_GRUPA.REF
         || Tmbn.REF:=$SKID_MBN.ref();
            Tmbn.add()
         ?};
         SKID_MBN.next()
      !}
   ?}
?};
Tokro.erase();
{? TT_ROK.REF
|| OKRO_F.index('ROK'); OKRO_F.prefix(TT_ROK.REF);
   {? OKRO_F.first()
   || {!
      |? Tokro.REF:=$OKRO_F.ref();
         Tokro.add();
         OKRO_F.next()
      !}
   ?}
?};
_where:='';
{? TT_GRUPA.REF | Tmbn.first()
|| _where+='K_HARM.SKID_MBN in (select REF from :_a)'
?};
{? TT_ROK.REF | Tokro.first()
|| _where+={? _where<>'' || ' and' || '' ?}+' K_HARM.OD in (select REF from :_b)'
?};
{? UNPAR.P12<>'W'
|| _where+={? _where<>'' || ' and' || '' ?}+' K_HARM.STATUS_K=\''+UNPAR.P12+'\''
?};
{? _where<>'' || _where+=' and' ?};
_where+=' K_HARM.FIRMA=\''+$REF.FIRMA+'\'';
K_HARM.f_set('SKID_MBN(KOD)',,_where,Tmbn,Tokro);
grp_disp(K_HARM,'WER',1)


\brkharm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord przed okna WER tabeli K_HARM
::  OLD: \brkharm/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
UNPAR.PRF1:=K_HARM.ROK_OD;
UNPAR.PRF2:=K_HARM.ROK_DO;
_gray:='';
{? K_HARM.STATUS='P' | K_HARM.STATUS='K' | K_HARM.STATUS='Z'
|| _gray+='W'
?};
{? K_HARM.STATUS<>'P'
|| _gray+='R'
?};
K_HARM.actions_grayed('WER',_gray);
K_HARM.SKID_MBN();
{? K_HARM.STATUS='T'
|| Color.rekprzed('K_HARM#01#01')
|? K_HARM.STATUS='Z'
|| Color.rekprzed('K_HARM#01#02')
|? K_HARM.STATUS='K'
|| Color.rekprzed('K_HARM#01#03')
|| ''
?}


\kharm_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Zmiana zakres przezentowanych harmonogramu
::  OLD: \kharm_zakres/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_HARM.cntx_psh();
K_HARM.win_edit('ZAKRES');
_ok:=K_HARM.edit();
K_HARM.cntx_pop();
{? _ok
|| exec('filtr','!ctr_hbd_zhar')
?}


\change_user_his
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wyswietla historie
::   WE:
::   WY:
::  OLD: \change_user_his/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_HARM_H.index('K_HARM_P');
K_HARM_H.prefix(K_HARM_P.ref());
K_HARM_H.win_sel('WER');
K_HARM_H.select()


\k_harm_u_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wyswietlenie uzytkownikow z uprawnieniami do przegladania budzetu
::  OLD: \k_harm_u_sel/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh();
USERS.win_dict('SLO1');
K_HARM_U.index('K_HARM_P');
K_HARM_U.prefix(K_HARM_P.ref());
K_HARM_U.win_sel('WER');
K_HARM_U.select(,,,{? K_HARM_P.STATUS='Z' || 'DU:D' || '' ?});
USERS.cntx_pop()


\bd_k_harm_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Wyswietlenie K_HARM_P
::  OLD: \bd_k_harm_p/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_HARM_P.cntx_psh();
{? app_info('web_sesid')<>''
|| K_HARM_W.K_HARM_P();
   K__POZ.use('yb______'); K__POZ.index('RODZAJ'); K__POZ.prefix()
?};
UNPAR.P2:=1;
K__POZ.memo_set(K_HARM_P.UWAGI);
{? app_info('web_sesid')<>''
|| UNPAR.P10_BV:='';
   exec('set_status','control');
   _enabled:=K_HARM_P.ZN_UZU='T';
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'KTO_UZU');
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'D_P_UZU');
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'D_R_UZU');
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'DATA_UZU');
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'UZU');
   _enabled:=K_HARM_P.ZN_AKC='T';
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'KTO_AKC');
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'D_P_AKC');
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'D_R_AKC');
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'DATA_AKC');
   K_HARM_P.web_efld_init('DISP_WEB',,'enable='+$_enabled,K_HARM_P,'AKC');
   K_HARM_P.web_display('DISP_WEB')
|| _enabled:=K_HARM_P.ZN_UZU='T';
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'KTO_UZU');
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'D_P_UZU');
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'D_R_UZU');
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'DATA_UZU');
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'UZU');
   _enabled:=K_HARM_P.ZN_AKC='T';
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'KTO_AKC');
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'D_P_AKC');
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'D_R_AKC');
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'DATA_AKC');
   K_HARM_P.efld_opt('DISP','enable='+$_enabled,K_HARM_P,'AKC');
   K_HARM_P.win_edit('DISP');
   K_HARM_P.display()
?};
UNPAR.P2:=0;
K_HARM_P.cntx_pop()


\www_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia zakres prezentowanych danych w webTermie
::   WE: _a - typ zakresu
::----------------------------------------------------------------------------------------------------------------------
OPERATOR.USER:={? exec('getUser','users',app_info('web_user')) || USERS.ref() || null ?};
K_HARM_W.index('USERS');
K_HARM_W.prefix(OPERATOR.USER,_a);
K_HARM_W.first();
exec('set_web_win_title','ctr_bha',1,_a);
K_HARM_W.web_refresh('WEB');
~~


\k_harm_p_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Otwiera arkusz na www
::----------------------------------------------------------------------------------------------------------------------
K_HARM_W.K_HARM_P();
exec('k_harm_p_open','control',1)

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:48 84bc0f00607b36c809f056445cea86afcc2c25c3614bb0180267f01d45b6e6ec09e10b9dcde9d428d02e65fee613eac0828fb7634297393583c72a8d930a13a6789c066e2f9a0965efa5b3ecc20fba271d97943f6815b5a955422c56b9c0d5aaf57e814c3d636735375b288962580abf37b9dab9436d4c4673d87cc635138967

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_wokr.fml
:: Utworzony: 03.10.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_WOKR - Wybór okresu rozliczeniowego.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Wybór okresu rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy braku wyboru, required=N, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'KEEP' || _a ?})"
::# kind=WE, symbol=STATUS, type=STRING, name=Status zamknięcia rozliczenia, required=N, fml_val="_radio:=exec('radio_choice','prc','R'); exec('edit_radiobutton','#edit',1,'Status rozliczenia',_radio.txt,_radio.var)"
::# kind=WE, symbol=S_PLAN, type=STRING, name=Status blokady planowania, required=N, fml_val="_radio:=exec('radio_choice','prc','P'); exec('edit_radiobutton','#edit',1,'Status rozliczenia',_radio.txt,_radio.var)"
::# kind=WY, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status zamknięcia rozliczenia, required=N
::# kind=WY, symbol=S_PLAN, type=STRING, name=Status blokady planowania, required=N
::
_par:=params_get();
params_set(_par);
_mp:=_par.mp;

{? _mp.pathProc() | _mp.pathTodo()
|| _in:=_par.in;
   _out:=_par.out;
:  Wybór okresu rozliczeniowego.
   exec('select','!prc_czp_wokr',_par);
   {? _out.A_OKR
   || _mp.save(,_out);
      _mp.done()
   |? _in.ON_ESC='KEEP' | _in.ON_ESC=~~
   || _mp.keep()
   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()
   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')
   || _mp.save(,_out);
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Wybór okresu rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wybierz okres rozliczeniowy'@@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Wybór okresu rozliczeniowego.
::   WE: _a - Parametry wyjściowe czynności
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_in:=_a.in;
_out:=_a.out;
_out.A_OKR:=null();
_out.STATUS:=_out.S_PLAN:='';
A_OKR.cntx_psh();
{? _in.STATUS<>~~ & _in.S_PLAN<>~~
|| A_OKR.index('A_OKRZR');
   A_OKR.prefix(_in.S_PLAN,_in.STATUS)
|? _in.STATUS<>~~
|| A_OKR.index('A_OKRZ');
   A_OKR.prefix(_in.STATUS)
|? _in.S_PLAN<>~~
|| A_OKR.index('A_OKRZP');
   A_OKR.prefix(_in.S_PLAN)
|| A_OKR.index('A_OKR');
   A_OKR.prefix()
?};
A_OKR.find_le(date());
A_OKR.win_sel('SLO');
{? A_OKR.select(,1,7)
|| _out.A_OKR:=A_OKR.ref();
   _out.STATUS:=A_OKR.S;
   _out.S_PLAN:=A_OKR.S_PLAN;
   exec('wybierz_okres','okres')
?};
A_OKR.cntx_pop();
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 39c3af9ee56db0c59f60c671e682b397195f703d71d8b20901c9ed8b4d6615b862fb6989d39e64e561b732041731fea6a5f416c677340af4abbeaf5d4febb067205c6f34078e1f3ceedaa90792e52538620d9cf6798fc2d71e32a07ef2d9bcd538467955f36e4ceb80d7a54c6a54863b873dbe64d5d96436f6e62efde522bf32

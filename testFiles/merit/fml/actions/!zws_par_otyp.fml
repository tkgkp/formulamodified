:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_otyp.fml
:: Utworzony: 15.02.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Typy faktur w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności ZWS_PAR_OTYP
::  OLD: \deftypdo/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
POMOC.W_PORTAL:='N';
exec('def_faktur','obiegi_typy')


\edczypodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja i na wyswietl dla pol wniosku z ETYPY.ED_PODZ
::  OLD: \edczypodz/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.ED_PODZ='N'
|| ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=0',ETYPY,'SKID_MB')
|| ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=1',ETYPY,'SKID_MB')
?};
{? ETYPY.FAKT_DEL=2
|| 0
|| OBIEGI.TYPOBIEG().NAZWA<>'Obieg wniosków' |
   (TYPOBIEG.NAZWA='Obieg wniosków' & (ETYPY.CZY_WAR>=2 | ETYPY.W_PORTAL='s'))
?}


\dobaepod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji pola ETYPY.ED_PODZ
::  OLD: \dobaepod/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.ED_PODZ='N' || ETYPY.SKID_MB:=null ?};
{? ETYPY.W_PORTAL='S'
|| ETYPY.efld_opt('RED_SEOD','mark='+$(ETYPY.ED_PODZ='T'),ETYPY,'SKID_MB')
?};
1


\dsp_fakt_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Na wyswietl dla pol ETYPY
::  OLD: \dsp_fakt_del/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.FAKT_DEL=2
|| _fld:=cur_afld();
   {? _fld='SKID_MB'
   || exec('findfnv','#color')
   || 0
   ?}
|| 1
?}


\bedobmod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola ETYPY.SKID_MB
::  OLD: \bedobmod/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPY.FAKT_DEL<>2 & ETYPY.ED_PODZ='T'


\bl_typvatpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula na blank dla pola ETYPY.TYPVATPR
::       Pole TYPVATPR ma wartosc 0 jesli dokument VAT a 1 jesli nie...
::  OLD: \bl_typvatpr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? typobi=3 || 1 || 0 ?}


\bevatpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed redakcją pola ETYPY.TYPVATPR
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? -menu_txt()='popraw'
|| ETYPYFIR.cntx_psh(); ETYPYFIR.index('UNIK'); ETYPYFIR.prefix(ETYPY.ref());
   _zwrot:=~ETYPYFIR.first();
   ETYPYFIR.cntx_pop()
?};
_zwrot


\aevatpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2008]
:: OPIS: Po redakcji pola ETYPY.TYPVATPR
::  OLD: \aevatpr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.TYPVATPR=1
|| ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=0,enable=0',SKID,'PR');
   SKID.PR:='';
   ETYPY.POZF:='N';
   ETYPY.SP_WYM:='N'
|| ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=0,enable=1',SKID,'PR')
?};
win_disp();
1


\edi_fakt_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja pol ETYPY
::  OLD: \edi_fakt_del/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.FAKT_DEL=2 || 0 || 1 ?}


\dspdekrtyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Na wyswietl dla pola ETYPY.DEKR
::  OLD: \dspdekrtyp/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
~ETYPY.ZAPOT


\betdobdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola ETYPY.DEKR, ETYPY.ZAPOT, ETYPY.FAKT_DEL
::  OLD: \betdobdk/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='DEKR' & ETYPY.ZAPOT
|| _zwrot:=0
|| {? cur_afld()='ZAPOT' & ETYPY.OBIEGKON<>1
   || _zwrot:=0
   |? -menu_txt<>'popraw'
   || _zwrot:=1
   || ETYPY.cntx_psh();
      _count:=ETYPY.count(); _zwrot:=~_count;
      {? ~_zwrot
      || ETYP_ATR.cntx_psh();
         ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(ETYPY.ref());
         {? ETYP_ATR.size()=_count || _zwrot:=1 ?};
         ETYP_ATR.cntx_pop()
      ?};
      ETYPY.cntx_pop()
   ?}
?};
_zwrot


\aetdobdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji pola ETYPY.DEKR, ETYPY.FAKT_DEL
::  OLD: \aetdobdk/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.DEKR='N'
|| ETYPYFIR.DOM_RKOR:=ETYPYFIR.DOM_RVAT:=ETYPYFIR.DOMGRVAT:=ETYPYFIR.DOM_REJ:=ETYPYFIR.DOM_RDOK:=null;
   ETYPY.PLATNOSC:=ETYPYFIR.AUTOKSIE:=null
?};
{? ETYPY.FAKT_DEL=2
|| ETYPY.ZAP_REAL:=ETYPY.ED_PODZ:='N'; ETYPY.SKID_MB:=null
?};
win_disp();
1


\beobiegkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja i na wyswietl dla pol wniosku z ETYPY
::  OLD: \beobiegkon/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('typobi')>0
|| {? cur_afld()='CZY_KH'
   || {? typobi=2
      || {? ~ETYPY.CZY_KH
         || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=0, mark=0',SKID,'SL_KH')
         || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=1, mark=1',SKID,'SL_KH')
         ?}
      |? typobi=3
      || {? ~ETYPY.CZY_KH
         || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=0',SKID,'SL_KH')
         || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=1',SKID,'SL_KH')
         ?}
      ?}
   |? cur_afld()='CZY_DATY' & typobi=3
   || {? ~ETYPY.CZY_DATY
      || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=0',ETYPY,'CZY_KNTR')
      || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=1',ETYPY,'CZY_KNTR')
      ?};
      {? ETYPY.CZY_DATY & ETYPY.CZY_KNTR
      || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=1',ETYPY,'KNTR_DNI')
      || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=0',ETYPY,'KNTR_DNI')
      ?}
   ?}
?};
'PD'*ETYPY.W_PORTAL | ETYPY.OBIEGKON=1


\aeczydaty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola ETYPY.CZY_DATY
::----------------------------------------------------------------------------------------------------------------------
{? ~ETYPY.CZY_DATY
|| ETYPY.CZY_KNTR:=ETYPY.KNTR_DNI:=0
?};
win_disp();
1


\aeobiegkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Po redakcji pol wniosku z ETYPY
::  OLD: \aeobiegkon/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? OBIEGI.TYPOBIEG().NAZWA='Obieg wniosków' & ETYPY.CZY_WAR>=2
|| ETYPY.ED_PODZ:='N'
?};
win_disp(); 1


\ae_ed_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2011]
:: OPIS: Po redakcji pola ETYPY.ED_ID
::  OLD: \ae_ed_id/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.ED_ID='N' || ETYPY.AUT_ID:='T' ?}; 1


\be_aut_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2011]
:: OPIS: Przed redakcja pola ETYPY.AUT_ID
::  OLD: \be_aut_id/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPY.ED_ID='T'


\aeczy_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [1210]
:: OPIS: Po redakcji pola ETYPY.CZY_KH
::  OLD: \aeczy_kh/skid_obi.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~ETYPY.CZY_KH
|| SKID.SL_KH:=''; ETYPY.DOM_SL:=null;
   ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=0, mark=0',SKID,'SL_KH')
|| {? typobi=2
   || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=1, mark=1',SKID,'SL_KH')
   || ETYPY.efld_opt(ETYPY.win_edit('?'),'enable=1',SKID,'SL_KH')
   ?}
?};
win_disp();
1


\obiegkonbl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Wartosc poczatkowa pola ETYPY.OBIEGKON
::  OLD: \obiegkonbl/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.OBIEGKON


\be_obiegkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja pola ETYPY.OBIEGKON
::  OLD: \be_obiegkon/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
_zwrot:={? -menu_txt()='popraw' || ETYPY.count()=0 || 1 ?};
ETYPY.cntx_pop();
_zwrot


\ae_obiegkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Po redakcji pola ETYPY.OBIEGKON
::  OLD: \ae_obiegkon/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.OBIEGKON=2
|| ETYPY.AUT_ID:=ETYPY.ED_ID:='N';
   ETYPY.CZY_WAR:=ETYPY.CZY_DATY:=ETYPY.CZY_KH:=ETYPY.CZY_PRAC:=ETYPY.ZAPOT:=0;
   ETYPY.CZY_KH:=1
|| {? ETYPY.ED_ID='N' || ETYPY.AUT_ID:='T' ?}
?};
win_disp();
1


\zapot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Wartosc poczatkowa pola ETYPY.ZAPOT
::  OLD: \zapot/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
SKID.ZAPOT


\bdszapot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed wyswietl dla pola ETYPY.ZAPOT
::  OLD: \bdszapot/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPY.OBIEGKON=1


\aezapot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Po redakcji pola ETYPY.ZAPOT
::  OLD: \aezapot/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.ZAPOT || ETYPY.DEKR:='N' ?};
exec('aetdobdk','!zws_par_otyp');
win_disp();
1


\bs_czy_kntr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed wyswietleniem ETYPY.CZY_KNTR
::  OLD: \bs_czy_kntr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPY.CZY_DATY


\be_czy_kntr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: PRZED REDAGOWANIEM ETYPY.CZY_KNTR
::  OLD: \be_czy_kntr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPY.CZY_DATY


\ae_czy_kntr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: PRZED REDAGOWANIEM ETYPY.CZY_KNTR
::  OLD: \ae_czy_kntr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.CZY_KNTR=0 || ETYPY.KNTR_DNI:=0 ?};
win_disp();
1


\bs_kntr_dni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed wyswietleniem ETYPY.KNTR_DNI
::  OLD: \bs_kntr_dni/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.CZY_DATY & ETYPY.CZY_KNTR || 1 || exec('findfnv','#color') ?}


\be_kntr_dni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: PRZED REDAGOWANIEM ETYPY.KNTR_DNI
::  OLD: \be_kntr_dni/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPY.CZY_DATY & ETYPY.CZY_KNTR


\ae_kntr_dni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redagowaniu ETYPY.KNTR_DNI
::  OLD: \ae_kntr_dni/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.KNTR_DNI<0
|| FUN.info('Liczba dni nie może być mniejsza od zera.'@); 0
|| 1
?}


\fakt_del_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Wartosc poczatkowa pola ETYPY.FAKT_DEL
::  OLD: \fakt_del_bl/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.FAKT_DEL


\berejdo1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola ETYPYFIR.DOM_REJ
::  OLD: \berejdo1/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.DEKR='T' || POMOC.AKC:='E'; 1 || 0 ?}


\aerejdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji pola ETYPYFIR.DOM_REJ
::  OLD: \aerejdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPYFIR.DOM_REJ<>null
|| {? ETYPYFIR.DOM_RDOK<>null | ETYPYFIR.DOM_RKOR<>null
   || DOK_REJ.cntx_psh();
      DOK_REJ.index('NAZ'); DOK_REJ.prefix(ETYPYFIR.DOM_REJ);
      {? ETYPYFIR.DOM_RDOK<>null & ~DOK_REJ.seek(ETYPYFIR.DOM_RDOK) || ETYPYFIR.DOM_RDOK:=null ?};
      {? ETYPYFIR.DOM_RKOR<>null & ~DOK_REJ.seek(ETYPYFIR.DOM_RKOR) || ETYPYFIR.DOM_RKOR:=null ?};
      DOK_REJ.cntx_pop()
   ?};
   {? ETYPYFIR.DOM_RVAT<>null
   || VAT_REJ.cntx_psh();
      VAT_REJ.index('REJ_VREF'); VAT_REJ.prefix(ETYPYFIR.DOM_REJ,ETYPYFIR.DOM_RVAT().RVAT);
      {? ~VAT_REJ.first() || ETYPYFIR.DOM_RVAT:=null ?};
      VAT_REJ.cntx_pop()
   ?};
   {? ETYPYFIR.DOMGRVAT<>null
   || GR_VAT.cntx_psh();
      _grvat:=ETYPYFIR.DOMGRVAT().GRVAT;
      GR_VAT.index('REJ_GR'); GR_VAT.prefix(ETYPYFIR.DOM_REJ,_grvat);
      {? ~GR_VAT.first() || ETYPYFIR.DOMGRVAT:=null ?};
      GR_VAT.cntx_pop()
   ?}
|| ETYPYFIR.DOM_RDOK:=ETYPYFIR.DOM_RKOR:=ETYPYFIR.DOM_RVAT:=ETYPYFIR.DOMGRVAT:=null
?}; 1


\berejdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pol ETYPYFIR.DOM_RDOK
::  OLD: \berejdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPYFIR.DOM_REJ<>null || ETYPYFIR.DOM_REJ(); 1 || 0 ?}


\bedob1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pol ETYPYFIR.DOM_RVAT, ETYPYFIR.DOMGRVAT i ETYPYFIR.DOM_RKOR
::  OLD: \bedob1/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPYFIR.DOM_REJ<>null & ~ETYPY.TYPVATPR || ETYPYFIR.DOM_REJ(); 1 || 0 ?}


\bedob2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola ETYPYFIR.AUTOKSIE
::  OLD: \bedob2/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPYFIR.ROK_F & ETYPY.DEKR='T'
|| ROZNE.WYW:={? typobi=3 || 'L' || 'P' ?};
   SSTALE.AR:=ETYPYFIR.ROK_F; 1
|| 0
?}


\del_typ_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Obsługa akcji Graupa przed
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ask:='Czy usunąć zaznaczone typy dokumentów w obiegu?'@;
ETYPY.cntx_psh();
ETYPY.prefix();
_tab:=ETYPY.sel_aget();
_jest:=0;
{? _tab.first()
|| {!
   |? ETYPY.seek(_tab.REF,);
      {? __RQ_BT.exists(ETYPY.ID_WP_B) || _jest:=1 ?};
      ~_jest & _tab.next()
   !}
?};
ETYPY.cntx_pop();
_text:='';
{? _jest
|| _text:='Wśród zaznaczonych rekordów są rekordy z typem bazowym.'@+'\n\n'+
          'Po usuniętu tych rekordów, ich bazowa definicja \n'
          'będzie dostępna po ponownym pobraniu definicji wniosków z portalu.'@+'\n\n'
?};
FUN.ask('%1%2'[_text,_ask])


\del_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JSz [12.30]
:: OPIS: Funkcja usuwa typ dokumentu w obiegu
::  OLD: \del_typ/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_sel_size:=ETYPY.sel_aget().size();
:: sprawdzamy czy jest to bazowy typ portalowy
_text:='';
{? ETYPY.ID_WP_B<>''
|| _base_type:=__RQ_BT.exists(ETYPY.ID_WP_B);
   {? _base_type
   || _text:='Zaznaczony typ dokumentu jest typem bazowym.'@+'\n\n'+
          'Po usunięciu tego rekordu, jego bazowa definicja \n'
          'będzie dostępna po ponownym pobraniu definicji wniosków z portalu.'+'\n\n'@
   ?}
?};
_text+='Czy usunąć typ dokumentu w obiegu?'@;
:: sprawdzamy czy typ wykorzystywany w dokumentach w obiegu
EDOKUM.cntx_psh();
_name:=EDOKUM.names();
{? _name.first()
|| {! |?
      EDOKUM.use(_name.NAME); EDOKUM.index('TYP'); EDOKUM.prefix(ETYPY.ref());
      {? EDOKUM.first() || _ok:=0 ?};
      _ok & _name.next()
   !}
?};
EDOKUM.cntx_pop();
:: sprawdzam, czy uzywany w innych firmach jako powiązanie
{?  exec('lic','#b_domain','POR')
|| ETYPPROC.cntx_psh(); ETYPPROC.index('DISP'); ETYPPROC.prefix(ETYPY.ref());
   {? ETYPPROC.first()
   || {! |?
         {? REF.FIRMA<>ETYPPROC.FIRMA & ETYPPROC.FIRMA<>null() || _ok:=-1 ?};
         ETYPPROC.next()
      !}
   ?};
   ETYPPROC.cntx_pop()
?};
:: Sprawdzam czy typ używany w kategoriach zasobu remontowego (we wnioskach administracyjnych)
{? _ok>0 & exec('lic','#b_domain','POR')
|| REM_KAT.cntx_psh();
   REM_KAT.index('TYP');
   REM_KAT.prefix(ETYPY.ref());
   {? REM_KAT.first()
   || _ok:=-2
   ?};
   REM_KAT.cntx_pop()
?};
:: jeśli typ niewykorzystywany to można usunąć
{? _ok>0
|| {? _sel_size | _sel_size=0 & FUN.ask(_text)
   || do();
:: usuwanie powiązanych schematów informacji dodatkowych
      ETYP_ATR.cntx_psh(); ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(ETYPY.ref());
      {? ETYP_ATR.first()
      || {! |?
            _delr:=ETYP_ATR.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      ETYP_ATR.cntx_pop();
:: parametry dekretacji
      ETYPYFIR.cntx_psh(); ETYPYFIR.index('UNIK'); ETYPYFIR.prefix(ETYPY.ref());
      {? ETYPYFIR.first()
      || {! |?
            _delr:=ETYPYFIR.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      ETYPYFIR.cntx_pop();
:: definicje tworzenia podziałów dla projektów
      ETYPYDTP.cntx_psh(); ETYPYDTP.index('UNIK'); ETYPYDTP.prefix(ETYPY.ref());
      {? ETYPYDTP.first()
      || {! |?
            _delr:=ETYPYDTP.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      ETYPYDTP.cntx_pop();
:: definicje odtwarzania schematów projektów
      ETYPYPOD.cntx_psh(); ETYPYPOD.index('UNIK'); ETYPYPOD.prefix(ETYPY.ref());
      {? ETYPYPOD.first()
      || {! |?
            _delr:=ETYPYPOD.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      ETYPYPOD.cntx_pop();
:: schematy tworzenia podziałów controllingowych
      ETSCHPDR.cntx_psh(); ETSCHPDR.index('DISP'); ETSCHPDR.prefix(ETYPY.ref());
      {? ETSCHPDR.first()
      || {! |?
            _delr:=ETSCHPDR.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      ETSCHPDR.cntx_pop();
:: Powiązania z procesami
      {? exec('lic','#b_domain','POR')
      || ETYPPROC.cntx_psh(); ETYPPROC.index('UNIK'); ETYPPROC.prefix(FIRMA.ref(),ETYPY.ref());
         {? ETYPPROC.first()
         || {! |?
               _delr:=ETYPPROC.del(,1);
               {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?};
         ETYPPROC.cntx_pop();
:: powiązania z typów z pracownikami
         ETYPY_P.cntx_psh(); ETYPY_P.index('ETYPY'); ETYPY_P.prefix(ETYPY.ref());
         {? ETYPY_P.first()
         || {! |?
               _delr:=ETYPY_P.del(,1);
               {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?};
         ETYPY_P.cntx_pop();
:: powiązania z osobami realizującymi wnioski administracyjne
         WAOR.cntx_psh();
         WAOR.index('TYP');
         WAOR.prefix(ETYPY.ref());
         {? WAOR.first()
         || {!
            |?
               _delr:=WAOR.del(,1);
               {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?};
         WAOR.cntx_pop()
      ?};
      ETYPYATT.cntx_psh(); ETYPYATT.index('SLO_NAZ'); ETYPYATT.prefix(ETYPY.ref());
      {? ETYPYATT.first()
      || {!
         |? _delr:=ETYPYATT.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      ETYPYATT.cntx_pop();
      _delr:=ETYPY.del(,1);
      {? _delr=0 || undo() ?};
      end()
   ?}
|? _ok=0
|| {? _sel_size=0
   || FUN.info('Typ dokumentu wykorzystywany w dokumentach w obiegu. Usunięcie niemożliwe.'@)
   ?}
|? _ok=-1
|| {? _sel_size=0
   || FUN.info('Typ dokumentu wykorzystywany w powiązaniach z procesami w innych firmach. Usunięcie niemożliwe.'@)
   ?}
|? _ok=-2
|| {? _sel_size=0
   || FUN.info('Typ dokumentu wykorzystywany w kategoriach zasobów remontowych. Usunięcie niemożliwe.'@)
   ?}
?}


\rpoetypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji rekordow w tabeli ETYPY
::  OLD: \rpoetypy/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
_wp:='PT'*ETYPY.W_PORTAL;
_wps:='PTs'*ETYPY.W_PORTAL;
{? _zwrot='' & typobi=2 & _wps & ETYPY.ID_WP=''
|| FUN.info('Nie wprowadzono identyfikatora.'@); _zwrot:='ID_WP'
?};
{? _zwrot='' & _wp
|| {? ETYPY.ID_WP_B=''
   || FUN.info('Nie wprowadzono typu bazowego.'@); _zwrot:='ID_WP_B'
   || ETYPY.BT_WP:={? __RQ_BT.exists(ETYPY.ID_WP_B) || 'T' || 'N' ?}
   ?}
?};
{? _zwrot='' & _wps & ETYPY.ID_WP<>'' & (_twp:=exec('etypy_chk_id','obiegi2',ETYPY.ID_WP))<>''
||  FUN.info('Istnieje już typ wniosku "%1" o tym identyfikatorze.'@[{? _twp='P' || 'bez papieru' || 'portal HR' ?}]); _zwrot:='ID_WP'
?};
{? _zwrot='' & ETYPY.NAZWA=''
|| FUN.info('Nie wprowadzono nazwy typu.'@); _zwrot:='NAZWA'
?};
{? _zwrot='' & exec('etypy_chk_na','obiegi2',ETYPY.TYPOBIEG,ETYPY.W_PORTAL,ETYPY.NAZWA)
|| FUN.info('Powielona nazwa typu.'@); _zwrot:='NAZWA'
?};
{? _zwrot='' & typobi=2 & ETYPY.CZY_ZALP='T' & ~ETYPY.ETYPZAL
|| FUN.info('Nie wybrano typu zaliczki.'@); _zwrot:='ETYPZAL'
?};
{? _zwrot='' &
   ((typobi=1 | (typobi=2 & ETYPY.OBIEGKON=2) | (typobi=2 & ETYPY.OBIEGKON=1 & ETYPY.CZY_KH)) & SKID.SL_KH='')
|| FUN.info('Nie wybrano słownika kontrahentów.'@); _zwrot:='SL_KH'
?};
{? _zwrot='' & ETYPY.OBS_PROJ & ~ETYPY.PROJ_RW
|| FUN.info('Nie wybrano schematu rodzajów wartości dla projektów.'@); _zwrot:='PROJ_RW'
?};
{? _zwrot='' & ETYPY.ODD_TYP<>'' & ETYPY.ODD_TYP<>'B'
|| {? ETYPY.ODD_TYP='S'
   || _zwrot:=__CHK.record2(ETYPY,'ODD_S','Domyślna jednostka księgowa - stała')
   || _zwrot:=__CHK.record2(ETYPY,'ODD_F','Domyślna jednostka księgowa - formuła')
   ?};
   exec('bv_etypy_odd_typ','obiegi2')
?};
{? _zwrot='' & -ETYPY.W_PORTAL='s' & ETYPY.ED_PODZ='T' & ETYPY.SKID_MB=null
|| FUN.info('Nie wybrano modelu danych.'@);
   _zwrot:='SKID_MB'
?};
{? _zwrot=''
|| {? __CHK.index(ETYPY,-menu_txt(,)='popraw')<>'' || _zwrot:='NAZWA' ?}
?};
{? _zwrot='' & typobi=2
|| {? ETYPY.WID_WP='T'
   || {? ETYPY.RODZ_WP=0
      || FUN.info('Nie wprowadzono rodzaju wniosku.'@);
         _zwrot:='RODZ_WP'
      ?};
      {? _zwrot='' & +ETYPY.GRUPA_WP
      || {? ~__RQ_GR.exists(ETYPY.GRUPA_WP)
         || FUN.info('Podana grupa wniosku nie widnieje w słowniku grup.'@);
            _zwrot:='GRUPA_WP'
         ?}
      ?}
   || ETYPY.RODZ_WP:=0;
      ETYPY.ZAL_WP:=0;
      ETYPY.GRUPA_WP:='';
      ETYPY.TAGI_WP:=''
   ?}
?};
{? _zwrot='' & ETYPY.W_PORTAL='P' & ETYPY.IN_ERP & -menu_txt()='popraw'
|| ETYP_ATR.cntx_psh();
   ETYP_ATR.index('VISIBLE'); ETYP_ATR.prefix(ETYPY.ref(),1);
   {? ETYP_ATR.first()
   || _uzywany:=exec('czy_dokum4etyp','obiegi',ETYPY.ref(),0,1);
      {? _uzywany=0 & FUN.ask('Wniosek paperless rejestrowany w ERP nie może zawierać widocznych informacji dodatkowych.\nCzy usunąć widoczne informacje dodatkowe?')
      || do();
         {! |? ETYP_ATR.del() !};
         ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(ETYPY.ref());
         {? ETYP_ATR.first()
         || _lp:=0;
            {!
            |? ETYP_ATR.KOL:=(_lp+=1);
               ETYP_ATR.put();
               ETYP_ATR.next()
            !}
         ?};
         end()
      || {? _uzywany
         || FUN.info('Istnieją już wnioski danego typu,\nposiadające widoczne informacje dodatkowe.')
         ?};
         _zwrot:='IN_ERP'
      ?}
   ?};
   ETYP_ATR.cntx_pop()
?};
{? _zwrot='' & 'SsD'*ETYPY.W_PORTAL & ETYPY.SKID_MB<>null
|| UD_SEOD.cntx_psh();
   UD_SEOD.index('LP'); UD_SEOD.prefix(null);
   _lb:=UD_SEOD.size();
   UD_SEOD.cntx_pop();
   _new:=exec('UD_SEOD_new','portal_seod');
   _ok:={? _lb+_new>15 || 0 || 1 ?};
   {? ~_ok
   || FUN.info('Wskazany model spowoduje przekroczenia liczby schematów danych używanych na portalu.');
      _zwrot:='SKID_MB'
   ?}
?};
_zwrot


\rpoetypyf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji rekordow w tabeli ETYPYFIR
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? _zwrot='' & ETYPYFIR.ROK_F=null
|| FUN.info('Nie wybrano roku.'@); _zwrot:='ROK_F'
?};
{? _zwrot='' & ETYPYFIR.ODD=null
|| FUN.info('Nie wybrano jednostki księgowej.'@); _zwrot:='ODD'
?};
{? _zwrot='' & typobi=1 & ETYPY.DEKR='T' & ETYPY.FAKT_DEL=2
|| {? ETYPYFIR.DOM_REJ=null
   || FUN.info('W typach faktur dekretowanych i dotyczących wydatków musi być wskazany rejestr księgowy.'@);
      _zwrot:='DOM_REJ'
   ?};
   {? _zwrot='' & ETYPYFIR.DOM_RDOK=null
   || FUN.info('W typach faktur dekretowanych i dotyczących wydatków musi być wskazany rodzaj dokumentu.'@);
      _zwrot:='DOM_RDOK'
   ?};
   {? _zwrot='' & ~ETYPY.TYPVATPR & ETYPYFIR.DOM_RVAT=null
   || FUN.info('W typach faktur dekretowanych i dotyczących wydatków musi być wskazany rejestr VAT.'@);
      _zwrot:='DOM_RVAT'
   ?};
   {? _zwrot='' & ~ETYPY.TYPVATPR & ETYPYFIR.DOMGRVAT=null
   || FUN.info('W typach faktur dekretowanych i dotyczących wydatków musi być wskazana grupa podatkowa.'@);
      _zwrot:='DOMGRVAT'
   ?};
   {? _zwrot='' & ETYPY.TYPVATPR & ETYPYFIR.AUTOKSIE=null
   || FUN.info('W typach faktur dekretowanych i dotyczących wydatków musi być wskazany schemat dokumentów.'@);
      _zwrot:='AUTOKSIE'
   ?}
?};
{? _zwrot='' & ETYPYFIR.DOM_REJ=null
|| FUN.info('Należy wskazać rejestr księgowy.'@); _zwrot:='DOM_REJ'
?};
{? _zwrot='' & ETYPYFIR.DOM_RDOK=null
|| FUN.info('Należy wskazać rodzaj dokumentu.'@); _zwrot:='DOM_RDOK'
?};
{? _zwrot='' & (typobi=2 | typobi=3) & ETYPYFIR.AUTOKSIE=null
|| FUN.info('Należy wskazać schemat dokumentów.'@);
   _zwrot:='AUTOKSIE'
?};
{? _zwrot='' & typobi=1 & ~ETYPY.TYPVATPR & ETYPYFIR.DOM_RDOK<>null & -ETYPYFIR.DOM_RDOK().SLO().KOD<>'vat'
|| FUN.info('Wybrany rodzaj dokumentu musi być typu "Dokument VAT".'@); _zwrot:='DOM_RDOK'
?};
{? _zwrot='' & (ETYPY.TYPVATPR | typobi<>1) & ETYPYFIR.DOM_RDOK<>null & -ETYPYFIR.DOM_RDOK().SLO().KOD<>'prosty'
|| FUN.info('Wybrany rodzaj dokumentu musi być typu "Dokument prosty".'@); _zwrot:='DOM_RDOK'
?};
{? _zwrot=''
|| {? __CHK.index(ETYPYFIR,-menu_txt(,)='popraw')<>'' || _zwrot:='DOM_REJ' ?}
?};
_zwrot


\rkptdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Rekord przed tabeli ETYPY
::  OLD: \rkptdob/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.SKID_MB=null || '' || 'ETYPY#01#01' ?}


\rb_etyp_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Rekord przed tabeli ETYPY
::  OLD: \rb_etyp_akt/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
_okno:={? typobi=1 || 'WER' || 'WERW'+{? 'TPs'*POMOC.W_PORTAL || 'WP' || '' ?} ?};
{? ETYPY.sel_size()
|| ETYPY.actions_grayed(_okno)
|| _gray:='';
   _gray+={? ETYPY.AKTYWNY='T' || 'A' || 'E' ?};
   {? ETYPY.DEKR<>'T' || _gray+='R' ?};
   {? ~ETYPY.OBS_PROJ || _gray+='O' ?};
   {? ETYPY.ED_PODZ<>'T' || _gray+='S' ?};
   {? POMOC.W_PORTAL='P' & ETYPY.IN_ERP & 0 || _gray+='I' ?};
   {? POMOC.W_PORTAL='S' & 0 || _gray+='I' ?};
   {? POMOC.W_PORTAL='N' & ETYPY.IN_POR=0 || _gray+='C' ?};
   ETYPY.actions_grayed(_okno,_gray);
   _hide:='';
   {? POMOC.W_PORTAL<>'P'
   || _hide+='Z'
   ?};
   {? POMOC.W_PORTAL='s'
   || _hide+='BJ'
   ?};
   {? typobi<>2
   ||_hide+='C'
   ?};
   ETYPY.actions(_okno,_hide,,1)
?};
{? ETYPY.AKTYWNY='T' || '' || 'ETYPY#02#01' ?}


\etyp_aktyw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Formula zmienia aktywnosc typu dokumentu w obiegu
::   WE: _a - 1 - aktywacja
::            0 - dezaktywacja
::  OLD: \etyp_aktyw/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
_grp:=ETYPY.sel_size();
{? _grp |
   (_a=1 & FUN.ask('Aktywować typ dokumentu?'@)) |
   (_a=0 & FUN.ask('Dezaktywować typ dokumentu?'@))
|| {? _a=0
   || {? ETYPY.AKTYWNY='T'
      || ETYPY.AKTYWNY:='N';
         {? ~ETYPY.put() & _grp || bledy_s+=1 ?}
      |? _grp
      || bledy_s+=1
      ?}
   |? _a=1
   || {? ETYPY.AKTYWNY='N'
      || ETYPY.AKTYWNY:='T';
         {? ~ETYPY.put() & _grp || bledy_s+=1 ?}
      |? _grp
      || bledy_s+=1
      ?}
   ?}
?}


\bg_aktyw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Formula przed akcja grupowa Aktywuj/Anuluj okna wertowania
::   WE: _a - 1 - aktywacja
::            0 - dezaktywacja
::  OLD: \bg_aktyw/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_a=1 & FUN.ask('Aktywować zaznaczone typy dokumentów w obiegu?'@)) |
   (_a=0 & FUN.ask('Dezaktywować zaznaczone typy dokumentów w obiegu?'@))
|| sel_nchk();
   ssize_s:=ETYPY.sel_size(); bledy_s:=0; 1
?}


\ag_aktyw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Formula po na akcje grupowa Aktywuj/Anuluj okna wertowania
::   WE: _a - 1 - aktywacja
::            0 - dezaktywacja
::  OLD: \ag_aktyw/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Zaznaczonych typów: '+$ssize_s+'. '+'\n'+
         {? _a=1 || 'Aktywowano: ' || 'Dezaktywowano: ' ?}+' '+$(ssize_s-bledy_s)+'.'
        );
VAR_DEL.delete('ssize_s','bledy_s')


\etypy_param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Parametry dekretacji dla typów dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.DEKR='T'
|| _firma:=REF.FIRMA;
   ETYPYFIR.cntx_psh();
   {? typobi=1 || ETYPYFIR.win_edit('RED1') || ETYPYFIR.win_edit('RED2') ?};
   FIRMA.cntx_psh();
   _fo:=FIRMA.mk_sel('Firmy'@,,,'firmy_etypy');
   FIRMA.win_fld(_fo,,'SYMBOL');
   FIRMA.win_fld(_fo,,'OPIS',,,20);
   _grp:=FIRMA.grp_make('Parametry dekretacji'@,,'firmy_typy1',,,,,'normal');
   FIRMA.grp_sel(_grp,,_fo,,"exec('ae_refr_firma','!zws_par_otyp')",,,10,,,,,'maximized_with_title');
   FIRMA.grp_splt(_grp,,'vertical','panel1');
   {? typobi=1
   || FIRMA.grp_sel(_grp,ETYPYFIR,'WER',,"",,,,,,,,'maximized_with_title')
   || FIRMA.grp_sel(_grp,ETYPYFIR,'WERW',,"",,,,,,,,'maximized_with_title')
   ?};
   FIRMA.win_sel(_grp);
   REF.FIRMA();
   FIRMA.select(,1);
   ETYPYFIR.cntx_pop(); FIRMA.cntx_pop();
   REF.FIRMA:=_firma
|| FUN.info('W parametrach typu nie ustawiono dekretacji.'@)
?}


\ae_refr_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu tabeli FIRMA
::----------------------------------------------------------------------------------------------------------------------
REF.FIRMA:=FIRMA.ref();
ETYPYFIR.index('DISP'); ETYPYFIR.prefix(ETYPY.ref(),FIRMA.ref());
{? ~ETYPYFIR.get() || ETYPYFIR.first() ?};
{? typobi=1
|| grp_disp(ETYPYFIR,'WER',1,1)
|| grp_disp(ETYPYFIR,'WERW',1,1)
?}


\find_etypy_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula zwraca nazwe typu z tabeli ETYPY na podstawie przekazanego refsql
::   WE: _a = refsql dla tabeli ETYPY
::   WY: nazwa typu delegacji
::  OLD: \find_etypy_ref/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
ETYPY.prefix();
_tmp:=BB.sqlint(_a);
_nam:=form(_a-8);
{? ETYPY.seek(_tmp,ETYPY.name()) || _wy:=ETYPY.NAZWA || _wy:='' ?};
ETYPY.cntx_pop();
_wy


\etyp_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wartość początkowa pola ETYPYFIR.ETYPY
::----------------------------------------------------------------------------------------------------------------------
ETYPY.ref()


\etypf_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola ETYPYFIR.ROK_F
::----------------------------------------------------------------------------------------------------------------------
exec('ustaw_param','!zws_par_otyp');
1


\etypf_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola ETYPYFIR.ODD
::----------------------------------------------------------------------------------------------------------------------
exec('ustaw_param','!zws_par_otyp');
1


\ustaw_param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Kasowanie parametrów
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ODD.cntx_psh();
{? ~ETYPYFIR.ROK_F | ~ETYPYFIR.ODD |
   (ETYPYFIR.DOM_REJ & ~(ETYPYFIR.DOM_REJ().ROK=ETYPYFIR.ROK_F & ETYPYFIR.DOM_REJ().ODD=ETYPYFIR.ODD))
|| ETYPYFIR.DOM_REJ:=null
?};
ROK_F.cntx_pop(); ODD.cntx_pop();
exec('aerejdob','!zws_par_otyp');
{? ETYPYFIR.AUTOKSIE & ETYPYFIR.AUTOKSIE().ROK_F<>ETYPYFIR.ROK_F
|| ETYPYFIR.AUTOKSIE:=null
?};
1


\del_typf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie parametrów dekretacji dla ETYPY
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć parametr dekretacji?'@) || ETYPYFIR.del() ?}


\beczyzalp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed redakcją pola ETYPY.CZY_ZALP
::----------------------------------------------------------------------------------------------------------------------
1


\aeczyzalp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola ETYPY.CZY_ZALP
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.CZY_ZALP='N' || ETYPY.ETYPZAL:=null ?}; win_disp(); 1


\beetypzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed redakcją pola ETYPY.ETYPZAL
::----------------------------------------------------------------------------------------------------------------------
1


\copy_etypyfir
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Kopiuje parametry dekretacji między latami
::----------------------------------------------------------------------------------------------------------------------
_edit:=ETYPYFIR.win_edit('?');
ETYPYFIR.win_edit('COPY');
{? ETYPYFIR.edit("
      _r:=__CHK.record2(UNPAR,'PRF1','Rok docelowy');
      {? _r='' & UNPAR.PRF1=ETYPYFIR.ROK_F
      || FUN.info('Rok docelowy taki sam jak źródłowy.'@);
         _r:='PRF1'
      ?};
      {? _r=''
      || ETYPYFIR.cntx_psh();
         ETYPYFIR.index('UNIK'); ETYPYFIR.prefix(ETYPYFIR.ETYPY,ETYPYFIR.FIRMA,UNPAR.PRF1,ETYPYFIR.ODD);
         {? ETYPYFIR.first()
         || FUN.info('Istnieją parametry dekretacji dla roku %1.'@[UNPAR.PRF1().NAZ]);
            _r:='PRF1'
         ?};
         ETYPYFIR.cntx_pop()
      ?};
      _r
   ")
|| _rkor:=ETYPYFIR.DOM_RKOR<>null;
   _rvat:=ETYPYFIR.DOM_RVAT<>null;
   _grvat:=ETYPYFIR.DOMGRVAT<>null;
   _auto:=ETYPYFIR.AUTOKSIE<>null;
   ETYPYFIR.ROK_F:=UNPAR.PRF1;
   ETYPYFIR.DOM_REJ:=exec('get_rej','rejestry',ETYPYFIR.DOM_REJ().KOD,UNPAR.PRF1,ETYPYFIR.ODD);
   ETYPYFIR.DOM_RDOK:={? ETYPYFIR.DOM_RDOK & ETYPYFIR.DOM_REJ
                      || exec('get_dok_rej','rejestry',ETYPYFIR.DOM_REJ,ETYPYFIR.DOM_RDOK().NAZ)
                      || null
                      ?};
   ETYPYFIR.DOM_RKOR:={? ETYPYFIR.DOM_RKOR & ETYPYFIR.DOM_REJ
                      || exec('get_dok_rej','rejestry',ETYPYFIR.DOM_REJ,ETYPYFIR.DOM_RKOR().NAZ)
                      || null
                      ?};
   ETYPYFIR.DOM_RVAT:={? ETYPYFIR.DOM_RVAT & ETYPYFIR.DOM_REJ
                      || exec('get_vat_rej','rejestry',ETYPYFIR.DOM_REJ,ETYPYFIR.DOM_RVAT().SYM)
                      || null
                      ?};
   ETYPYFIR.DOMGRVAT:={? ETYPYFIR.DOMGRVAT & ETYPYFIR.DOM_REJ
                      || exec('get_gr_vat','rejestry',ETYPYFIR.DOM_REJ,ETYPYFIR.DOMGRVAT().KOD)
                      || null
                      ?};
   ETYPYFIR.AUTOKSIE:={? ETYPYFIR.AUTOKSIE
                      || exec('get_autoksie','rejestry',UNPAR.PRF1,ETYPYFIR.AUTOKSIE().NAZ)
                      || null
                      ?};
   {? ETYPYFIR.DOM_REJ &
      ETYPYFIR.DOM_RDOK &
      (_rkor=0 | ETYPYFIR.DOM_RKOR) &
      (_rvat=0 | ETYPYFIR.DOM_RVAT) &
      (_grvat=0 | ETYPYFIR.DOMGRVAT) &
      (_auto=0 | ETYPYFIR.AUTOKSIE)
   || ETYPYFIR.add(1)
   || {? FUN.choice('Nie znaleziono wszystkich wymaganych\nparametrów dekretacji w roku %1.'@[UNPAR.PRF1().NAZ],1,'Szczegóły')
      || ETYPYFIR.win_edit(_edit);
         {? ETYPYFIR.edit("exec('rpoetypyf','!zws_par_otyp')")
         || ETYPYFIR.add(1)
         ?}
      ?}
   ?}
?};
ETYPYFIR.win_edit(_edit)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 a989abd607b26bb5dd4a4baf04ff3bc09cd255971c3d1ac22e1e80fa4ddee748bc01de0dab295c007c559e26d440698d91eb458fa8784d7d2e891660a03ff0cdb17f70eaf9952dd4bfcad551b3460854caa6cbded130e6d4d9df2e15abf10b3c75a9a47a16dd71787da7b97b82103e25f03e5df1923c41c38b4fde09faea24fd

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ppko.fml
:: Utworzony: 19.07.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PPKO - Profile kompetencji.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Profile kompetencji - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
exec('init','poc');

_ctx:=obj_new(6);
params_set('ctx',_ctx);

_ctx[1]:=obj_new(
   'visible',
   'kryt_ndx','kryt_ref'
);
_ctx[2]:=obj_new(
   'visible',
   'kryt_ndx','kryt_ref',
   'komp_ndx','komp_ref',
   'typ_ndx','typ_ref',
   'met_ndx','met_ref'
);
_ctx[3]:=obj_new(
   'visible',
   'komp_ndx','komp_ref',
   'typ_ndx','typ_ref',
   'met_ndx','met_ref'
);
_ctx[4]:=obj_new(
   'visible',
   'komp_ndx','komp_ref'
);
_ctx[5]:=exec('cfg_tab','!zws_par_ppko');
_ctx[6]:=exec('profk_tab','!zws_par_ppko');
_buf_prof:=exec('save_fml_all','#field',ZZ_PROF);
_buf_profk:=exec('save_fml_type','#field',ZZ_PROFK,'BLANK');
_buf_proft:=exec('save_fml_type','#field',ZZ_PROFT,'BLANK');
_buf_profo:=exec('save_fml_type','#field',ZZ_PROFO,'BLANK');
_buf_kryt:=exec('save_fml_type','#field',ZZ_KRYT,'BLANK');

ZZ_PROF.win_sel(exec('setup_wnd','!zws_par_ppko'));
ZZ_PROF.select();

exec('restore_fml_all','#field',ZZ_PROF,_buf_prof);
exec('restore_fml_type','#field',ZZ_PROFK,'BLANK',_buf_profk);
exec('restore_fml_type','#field',ZZ_PROFT,'BLANK',_buf_proft);
exec('restore_fml_type','#field',ZZ_PROFO,'BLANK',_buf_profo);
exec('restore_fml_type','#field',ZZ_KRYT,'BLANK',_buf_kryt);

~~


\zz_prof_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: "Przed dołącz" okienek tabeli ZZ_PROF.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_prof_edytuj_b','!zws_par_ppko');
1


\zz_prof_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: "Przed popraw" okienek tabeli ZZ_PROF.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_prof_edytuj_b','!zws_par_ppko');
1


\zz_prof_edytuj_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Wykorzystywana w akcjach przed dołączeniem i poprawieniem okienek tabeli ZZ_PROF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZZ_PROF.win_edit(
   {? ZZ_PAR.OGR_RODZ='N'
   || {? ZZ_PAR.TRYB='T'
      || {? ZZ_PAR.TRYB_MOD='T'
         || 'RED_R'
         || 'RED_O'
         ?}
      || 'RED_R'
      ?}
   || 'RED_O'
   ?}
);

{? -menu_txt()='popraw'
|| ZZ_PROF.fld_fml('RODZAJ','BEFORE_EDIT',"0")
?};

1


\zz_prof_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: "Rekord po" okienek tabeli ZZ_PROF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_prof_chk','phr_zz_tab',-menu_txt()='popraw')


\zz_prof_rodzaj_b_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła przed akcji "Rodzaj->Bazowy".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_prof_rodzaj_x_b','!zws_par_ppko','B')


\zz_prof_rodzaj_o_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła przed akcji "Rodzaj->Ogólny".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_prof_rodzaj_x_b','!zws_par_ppko','O')


\zz_prof_rodzaj_s_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła przed akcji "Rodzaj->Szczegółowy".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_prof_rodzaj_x_b','!zws_par_ppko','S')


\zz_prof_rodzaj_x_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Ustala wskazanie rekordu tabeli SLO_KOD właściwego dla podanego kodu rodzaju profilu.
::   WE: _a [STRING] - kod rodzaju profilu
::       'B' - bazowy
::       'O' - ogólny
::       'S' - szczegółowy
::   WY: 1 - dla podanego kodu ustalono rekord w tabeli SLO_KOD
::       0 - nie udało się ustalić pozycji dla podanego kodu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | _a='' || return(0)
|? 'BOS'*_a=0 || return(0)
?};

_opis:=
   {? _a='B' || 'bazowy'
   |? _a='O' || 'ogólny'
   |? _a='S' || 'szczegółowy'
   || ''
   ?};

exec('kod_ref','ext_slo','PROFTYP',_a,_opis,1)<>null


\zz_prof_rodzaj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła po akcji "Rodzaj".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_prof_kreatory','!zws_par_ppko');
_par:=params_get();
params_set(_par);
_ctx:=_par.ctx;
ZZ_PROF.prefix(ZZ_HIST.ref(),SLO_KOD.ref());
_wnd:=_ctx[5];
_aid:='';
{? ZZ_PAR.TRYB='T' & SLO_KOD.KOD='S'
|| _aid:='D:D'
?};
ZZ_PROF.actions_grayed(_wnd,_aid)


\zz_prof_kreatory
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Ustala kontekst kreatorów dla okienka profili.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZZ_POM.KREATORY:=
   {? ZZ_PAR.OGR_RODZ='N' || '#ZWS_PAR_PPKO#ZZ_PROF#CFG_X#'
   |? SLO_KOD.KOD='O' || '#ZWS_PAR_PPKO#ZZ_PROF#CFG_X#O#'
   |? SLO_KOD.KOD='S' || '#ZWS_PAR_PPKO#ZZ_PROF#CFG_X#S#'
   || ''
   ?};
~~


\get_proft_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Zwraca akronim okienka właściwy wg ustawienia parametru ZZ_PAR.TYP_PROF
::   WE:
::   WY: 'CFG_R' lub 'CFG_O'
::----------------------------------------------------------------------------------------------------------------------
{? ZZ_PAR.TYP_PROF='N' || 'PROO' || 'PROF' ?}


\zz_profk_xxx_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: "Okienko przed" okienek tabeli ZZ_PROFK.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('setup_icon','phr_widok','ZZ_KOMP','NAZWA');
exec('zz_pom_psh','phr_widok','ZWS_PAR_PPKO');

ZZ_KOMP.cntx_psh();
ZZ_KOMP.win_dict('SLO');

ZZ_PROFK.win_edit('RED');
ZZ_PROFK.win_patt('WZO');

1


\zz_profk_xxx_oa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: "Okienko po" okienek tabeli ZZ_PROFK.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_pop','phr_widok');
ZZ_KOMP.cntx_pop();

1


\zz_profk_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed dołączeniem kompetencji profilu.
::   WE:
::   WY:
::  OLD: \zz_profk_dolacz_b/phr_widok.fml
::----------------------------------------------------------------------------------------------------------------------
ZZ_KOMP.cntx_psh();
ZZ_KOMP.index('NAZWA');
ZZ_KOMP.prefix(ZZ_POM.ZZ_HIST);
ZZ_KOMP.win_sel('WYB');
{? ZZ_PROF.RODZAJ().KOD='O'
|| _zz_dok:=$ZZ_PROF.ZZ_DOK
|? ZZ_PROF.RODZAJ().KOD='S'
|| _zz_dok:=$ZZ_KRYT.ZZ_DOK
|? ZZ_PROF.RODZAJ().KOD='B'
|| _zz_dok:=$ZZ_PROF.ZZ_DOK
?};
_dalej:=0;
{? ZZ_KOMP.select()
|| {? exec('zz_profk_add_check','!zws_par_ppko',ZZ_KOMP.ref(),_zz_dok)=1
   || ZZ_PROFK.blank();
      ZZ_PROFK.ZZ_KOMP:=ZZ_KOMP.ref();
      {? ZZ_PROFK.edit("exec('zz_profk_chk','phr_zz_tab',0)")
      || ZZ_PROFK.add()
      ?}
   |? exec('zz_profk_add_check','!zws_par_ppko',ZZ_KOMP.ref(),_zz_dok)=2
   || FUN.info('W kompetencjach profilu znajdują się kompetencje podrzędne dla wybranej kompetencji nadrzędnej. '+
               'Dołączenie niemożliwe.'@)
   |? exec('zz_profk_add_check','!zws_par_ppko',ZZ_KOMP.ref(),_zz_dok)=3
   || FUN.info('W kompetencjach profilu znajduje się kompetencja nadrzędna dla wybranej kompetencji. '+
               'Dołączenie niemożliwe.'@)
   ?}
?};
ZZ_KOMP.cntx_pop();
_dalej


\zz_profk_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed poprawieniem kompetencji profilu.
::   WE:
::   WY:
::  OLD: \zz_profk_dolacz_b/phr_widok.fml
::----------------------------------------------------------------------------------------------------------------------
ZZ_PROFK.get();
{? ZZ_PROFK.edit("exec('zz_profk_chk','phr_zz_tab',1)")
|| ZZ_PROFK.put();
   0
?}


\zz_profk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: "Rekord po" okienek tabeli ZZ_PROFK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_profk_chk','phr_zz_tab',-menu_txt()='popraw')


\zz_proft_kryterium_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Akcja "Kryterium" okienka tabeli ZZ_PROFT.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_link:=ZZ_KRYT.fld_fml('ZZ_LINK','BLANK',"ZZ_PROFT.ZZ_DOK");
_typ:=ZZ_KRYT.fld_fml('ZZ_TYP','BLANK',"ZZ_PROFT.ZZ_TYP");

ZZ_KRYT.cntx_psh();
ZZ_KRYT.index('UNIQUE');
ZZ_KRYT.prefix(ref_name(ZZ_PROFT.ZZ_DOK),ZZ_PROFT.ZZ_DOK,ZZ_PROFT.ZZ_TYP);
exec('zz_kryt_wstaw_b','phr_widok');
ZZ_KRYT.cntx_pop();

ZZ_KRYT.fld_fml('ZZ_LINK','BLANK',_link);
ZZ_KRYT.fld_fml('ZZ_TYP','BLANK',_typ);
~~


\setup_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Tworzy panel zarządzania profilami.
::   WE:
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
exec('__WND','#object');

_wid:=-('prof%1%2'[ZZ_PAR.OGR_RODZ,ZZ_PAR.MET_PROF]);
_par:=params_get();
params_set(_par);
_ctx:=_par.ctx;

{? (_wnd:=__WND.SEL.get(ZZ_PROF,_wid))<>''
|| return(_wnd)
?};

: Okienko główne czynności
_wnd:=ZZ_PROF.grp_make('Profile kompetencji'@,
:  po wyświetleniu
   "params_exec('adm_bf','!zws_par_ppko')",
:  identyfikator
   _wid,
:  położenie
   ,,
:  zamknięcie
   "params_exec('adm_oc','!zws_par_ppko')"
);

: Okienko przeglądania profili zależne od parametru pracy
_tmp:=_ctx[5];
ZZ_PROF.grp_sel(_wnd,,_ctx[5],,
:  po odświeżeniu
   "params_exec('adm_zz_prof_cfg_x_ar','!zws_par_ppko')",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "params_exec('adm_zz_prof_cfg_x_bs','!zws_par_ppko',_a)",
:  po obsłudze
   "params_exec('adm_zz_prof_cfg_x_as','!zws_par_ppko',_a)",
:  utrwalenie, aktywacja, wypełnienie
   0,0,'maximized_with_title',
:  identyfikator
   'profile',
:  główne
   1
);
ZZ_PROF.grp_splt(_wnd,,'vertical','left');

:-----------------------
:  1. Dobór ocenianych
:-----------------------
: Kryteria doboru (ogólne i szczegółowe)
ZZ_PROF.grp_sel(_wnd,ZZ_KRYT,'CFG','Dobór ocenianych'@,
:  po odświeżeniu
   "  {? params_exec('isTabVisible','!zws_par_ppko',1)
      || params_exec('adm_zz_kryt_1_ar','!zws_par_ppko')
      ?}
   ",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "  {? params_exec('isTabVisible','!zws_par_ppko',1)
      || params_exec('adm_zz_kryt_1_bs','!zws_par_ppko',_a)
      ?}
   ",
:  po obsłudze
   "  {? params_exec('isTabVisible','!zws_par_ppko',1)
      || params_exec('adm_zz_kryt_1_as','!zws_par_ppko',_a)
      ?}
   ",
:  utrwalenie, aktywacja, wypełnienie
   0,0,'maximized_with_title',
:  identyfikator
   'zz_kryt_1',
);

:--------------------------
:  2. Kompetencje profilu
:--------------------------
: Kryteria doboru (szczegółowe)
ZZ_PROF.grp_sel(_wnd,ZZ_KRYT,'KOMO','Kompetencje profilu'@,
:  po odświeżeniu
   "  {? params_exec('isTabVisible','!zws_par_ppko',2)
      || params_exec('adm_zz_kryt_2_ar','!zws_par_ppko')
      ?}
   ",
:  położenie i wysokość
   ,,5,
:  przed obsługą
   "  {? params_exec('isTabVisible','!zws_par_ppko',2)
      || params_exec('adm_zz_kryt_2_bs','!zws_par_ppko',_a)
      ?}
   ",
:  po obsłudze
   "  {? params_exec('isTabVisible','!zws_par_ppko',2)
      || params_exec('adm_zz_kryt_2_as','!zws_par_ppko',_a)
      ?}
   ",
:  utrwalenie, aktywacja, wypelnienie
   ,,'maximized_with_title',
:  identyfikator
   'zz_kryt_2'
);

: kompetencje kryterium
ZZ_PROF.tab_splt(_wnd,,'horizontal','middle');
ZZ_PROF.grp_sel(_wnd,ZZ_PROFK,_ctx[6],,
:  po odświeżeniu
   "  {? params_exec('isTabVisible','!zws_par_ppko',2)
      || params_exec('adm_zz_profk_2_ar','!zws_par_ppko')
      ?}
   ",
:  położenie i wysokość
   ,,5,
:  przed obsługą
   "  {? params_exec('isTabVisible','!zws_par_ppko',2)
      || params_exec('adm_zz_profk_2_bs','!zws_par_ppko',_a)
      ?}
   ",
:  po obsłudze
   "  {? params_exec('isTabVisible','!zws_par_ppko',2)
      || params_exec('adm_zz_profk_2_as','!zws_par_ppko',_a)
      ?}
   ",
:  utrwalenie, aktywacja, wypełnienie
   ,,'maximized_with_title',
:  identyfikator
   'zz_profk_2'
);

:--------------------------
:  3. Kompetencje profilu
:--------------------------
: Kompetencje profilu (ogólne)
ZZ_PROF.grp_sel(_wnd,ZZ_PROFK,_ctx[6],'Kompetencje profilu'@,
:  po odświeżeniu
   "  {? params_exec('isTabVisible','!zws_par_ppko',3)
      || params_exec('adm_zz_profk_3_ar','!zws_par_ppko')
      ?}
   ",
:  położenie i wysokość
   ,,10,
:  przed obsługą
   "  {? params_exec('isTabVisible','!zws_par_ppko',3)
      || params_exec('adm_zz_profk_3_bs','!zws_par_ppko',_a)
      ?}
   ",
:  po obsłudze
   "  {? params_exec('isTabVisible','!zws_par_ppko',3)
      || params_exec('adm_zz_profk_3_as','!zws_par_ppko',_a)
      ?}
   ",
:  utrwalenie, aktywacja, wypełnienie
   ,,'maximized_with_title',
:  identyfikator
   'zz_profk_3'
);

:--------------------------
:  4. Kompetencje profilu
:--------------------------
: Kompetencje profilu (bazowe)
ZZ_PROF.grp_sel(_wnd,ZZ_PROFK,_ctx[6],'Kompetencje profilu'@,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "  {? params_exec('isTabVisible','!zws_par_ppko',4)
      || params_exec('adm_zz_profk_4_bs','!zws_par_ppko',_a)
      ?}
   ",
:  po obsłudze
   "  {? params_exec('isTabVisible','!zws_par_ppko',4)
      || params_exec('adm_zz_profk_4_as','!zws_par_ppko',_a)
      ?}
   ",
:  utrwalenie, aktywacja, wypełnienie
   ,,'maximized_with_title',
:  identyfikator
   'zz_profk_4'
);

_wnd


\adm_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed wypełnieniem okienka.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
ZZ_POM.MODUL:='O';
_par:=params_get();
params_set(_par);
_ctx:=_par.ctx;
exec('zz_pom_psh','phr_widok','ZWS_PAR_PPKO');
exec('zz_prof_rodzaj_x_b','!zws_par_ppko','B');
exec('ustaw_tytul','!zws_par_ppko',_ctx[5]);

ZZ_HIST.cntx_psh();
ZZ_HIST.index('DATA');
ZZ_HIST.prefix(exec('ref_firma','ustawienia'));
ZZ_HIST.find_le(date());

ZZ_PROF.cntx_psh();
{? ZZ_PAR.OGR_RODZ='N'
|| ZZ_PROF.index('NAZWA')
|| ZZ_PROF.index('RODZAJ')
?};

ZZ_KRYT.cntx_psh();
ZZ_PROFK.cntx_psh();
ZZ_PROFT.cntx_psh();
ZZ_PROFO.cntx_psh();
ZZ_TYP.cntx_psh();
ZZ_MET.cntx_psh();
ZF_DEF.cntx_psh();

ZF_DEF.win_dict('WEO');


_ctx[1].kryt_ndx:='UNIQUE';
_ctx[1].kryt_ref:=null;
_ctx[2].kryt_ndx:='DOBOR';
_ctx[2].kryt_ref:=null;
_ctx[2].komp_ndx:='NAZWA';
_ctx[2].komp_ref:=null;
_ctx[2].typ_ndx:='KOD';
_ctx[2].typ_ref:=null;
_ctx[2].met_ndx:='KOD';
_ctx[2].met_ref:=null;
_ctx[3].komp_ndx:='NAZWA';
_ctx[3].komp_ref:=null;
_ctx[3].typ_ndx:='KOD';
_ctx[3].typ_ref:=null;
_ctx[3].met_ndx:='KOD';
_ctx[3].met_ref:=null;
_ctx[4].komp_ndx:='NAZWA';
_ctx[4].komp_ref:=null;

1


\adm_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przy zamykaniu okienka.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_pop','phr_widok');
ZZ_HIST.cntx_pop();
ZZ_PROF.cntx_pop();
ZZ_KRYT.cntx_pop();
ZZ_PROFK.cntx_pop();
ZZ_PROFT.cntx_pop();
ZZ_PROFO.cntx_pop();
ZZ_TYP.cntx_pop();
ZZ_MET.cntx_pop();
ZF_DEF.cntx_pop();

ZZ_TYP.f_clear();
ZZ_PROFT.f_clear();

SLO_KOD.f_clear();

1


\adm_zz_hist_sel_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienka SEL tabeli ZZ_HIS.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_ctx:=_par.ctx;
ZZ_POM.ZZ_HIST:=ZZ_HIST.ref();

grp_disp(ZZ_PROF,_ctx[5],1)


\adm_zz_prof_cfg_x_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienek CFG_R i CFG_O tabeli ZZ_PROF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_ctx:=_par.ctx;
exec('ustaw_tytul','!zws_par_ppko',_ctx[5]);
ZZ_PROF.fld_fml('RODZAJ','BEFORE_EDIT',"1");


{! _ni:=1..4
|! _ctx[_ni].visible:=0
!};

{? grp_empty('profile')
|| tab_hide(,1,'left')

|? ZZ_PROF.RODZAJ().KOD='B'
|| tab_hide(1,,'left');
   tab_hide(2,,'left');
   tab_hide(3,,'left');
   tab_show(4,'left');
   _ctx[4].visible:=1;
   ZZ_PROFK.actions_grayed(_ctx[6],'T');
   grp_disp(ZZ_PROFK,_ctx[6],1)

|? ZZ_PROF.RODZAJ().KOD='O'
|| tab_show(1,'left');
   _ctx[1].visible:=1;
   tab_hide(2,,'left');
   tab_show(3,'left');
   _ctx[3].visible:=1;
   ZZ_PROFK.actions_grayed(_ctx[6],);
   tab_hide(4,,'left');
   grp_disp(ZZ_KRYT,'CFG',1);
   grp_disp(ZZ_PROFK,_ctx[6],1)

|? ZZ_PROF.RODZAJ().KOD='S'
|| tab_show(1,'left');
   _ctx[1].visible:=1;
   tab_show(2,'left');
   _ctx[2].visible:=1;
   ZZ_PROFK.actions_grayed(_ctx[6],);
   tab_hide(3,,'left');
   tab_hide(4,,'left');
   grp_disp(ZZ_KRYT,'CFG',1);
   grp_disp(ZZ_KRYT,'KOMO',1)

|| tab_hide(,1,'left')
?};

_hide:='';
{? ZZ_PAR.TRYB='T' & SLO_KOD.KOD='S'
|| _hide:='D:D'
?};
{? ZZ_PROF.RODZAJ().KOD='S'
|| _hide:='W'+_hide
?};
ZZ_PROF.actions_grayed(_ctx[5],_hide)


\adm_zz_prof_cfg_x_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienek CFG_R i CFG_O tabeli ZZ_PROF.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| exec('zz_prof_kreatory','!zws_par_ppko');
   _typ:=exec('slo_typ','ext_slo','PROFTYP');
   ZZ_PROF.fld_fml('ZZ_HIST','BLANK',"ZZ_HIST.ref()");
   {? ZZ_PAR.OGR_RODZ='N'
   || _rule:="null";
      {? ZZ_PAR.TRYB='T' & ZZ_PAR.TRYB_MOD='N'
      || _rule:="exec('kod','ext_slo','PROFTYP','O')"
      ?};
      ZZ_PROF.fld_fml('RODZAJ','BLANK',_rule);
      SLO_KOD.prefix();
      {? ZZ_PAR.TRYB<>'T'
      || SLO_KOD.f_set('NAZWA',,'SLO_TYP=:_a',_typ)
      || SLO_KOD.f_set('NAZWA',,'SLO_TYP=:_a and KOD<>\'S\'',_typ)
      ?}
   || ZZ_PROF.fld_fml('RODZAJ','BLANK',"SLO_KOD.ref()");
      SLO_KOD.f_clear()
   ?}
?};

{? ZZ_PAR.OGR_RODZ='N'
|| ZZ_PROF.prefix(ZZ_HIST.ref())
|| ZZ_PROF.prefix(ZZ_HIST.ref(),SLO_KOD.ref())
?}


\adm_zz_prof_cfg_x_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienek CFG_R i CFG_O tabeli ZZ_PROF.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| ZZ_POM.KREATORY:=''
?}


\adm_zz_kryt_1_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienka tabeli ZZ_KRYT w drugiej zakładce "Dobór ocenianych".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZZ_KRYT.actions('CFG','Y:Y',,1)


\adm_zz_kryt_1_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_KRYT w czwartej zakładce "Dobór ocenianych".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get:="exec('get_proft','phr_dane',ZZ_PROF.ZZ_DOK,exec('typ_op','phr_dane'),1)";
_dok:=_get();

{? _a
|| ZZ_KRYT.fld_fml('ZZ_LINK','BLANK',_get);
   ZZ_KRYT.fld_fml('ZZ_TYP','BLANK',"exec('typ_op','phr_dane')");
   ZZ_KRYT.fld_fml('ZZ_KRYT','BLANK',"exec('zz_kryt_zz_kryt_bl','phr_widok')");
   ZZ_KRYT.dnd_sel('CFG',,'records.ZZ_KRYT',"exec('zz_kryt_dnd','phr_widok')");
   ZZ_KRYT.actions('CFG','Y:Y',,1)
?};

_ctx:=params_get().ctx[1];
ZZ_KRYT.index(_ctx.kryt_ndx);
ZZ_KRYT.prefix(ref_name(_dok),_dok,exec('typ_op','phr_dane'));
ZZ_KRYT.seek(_ctx.kryt_ref);

{? grp_empty('profile')
|| '#disable'
?}


\adm_zz_kryt_1_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka tabeli ZZ_KRYT w czwartej zakładce "Dobór ocenianych".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| ZZ_KRYT.fld_fml('ZZ_KRYT','BLANK',"*");
   ZZ_KRYT.dnd_sel('CFG',,'records.ZZ_KRYT',"");
   ZZ_KRYT.actions('CFG',,,1);

   _ctx:=params_get().ctx[1];
   _ctx.kryt_ndx:=ZZ_KRYT.index('?');
   _ctx.kryt_ref:=ZZ_KRYT.ref()
?}


\adm_zz_kryt_2_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienka tabeli ZZ_KRYT w drugiej zakładce "Oceniane kompetencje".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_ctx:=_par.ctx;
grp_disp(ZZ_PROFK,_ctx[6],1)


\adm_zz_kryt_2_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_KRYT w drugiej zakładce "Oceniane kompetencje".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get:="exec('get_proft','phr_dane',ZZ_PROF.ZZ_DOK,exec('typ_op','phr_dane'),1)";
_dok:=_get();

_ctx:=params_get().ctx[2];
ZZ_KRYT.index(_ctx.kryt_ndx);
ZZ_KRYT.prefix(ref_name(_dok),_dok,exec('typ_op','phr_dane'),1);
ZZ_KRYT.seek(_ctx.kryt_ref)


\adm_zz_kryt_2_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka tabeli ZZ_KRYT w drugiej zakładce "Oceniane kompetencje".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _ctx:=params_get().ctx[2];
   _ctx.kryt_ndx:=ZZ_KRYT.index('?');
   _ctx.kryt_ref:=ZZ_KRYT.ref()
?}


\adm_zz_profk_2_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienka tabeli ZZ_PROFK w drugiej zakładce "Oceniane kompetencje".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get())


\adm_zz_profk_2_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_PROFK w drugiej zakładce "Oceniane kompetencje".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get:="ZZ_KRYT.ZZ_DOK";
_dok:=_get();

{? _a
|| _aid:={? ZZ_PAR.WAR_PROF<>'T' || 'R' || '' ?};
   cur_tab(1,1).actions(cur_win(1,1),_aid,,1);
   ZZ_PROFK.fld_fml('ZZ_LINK','BLANK',_get);
   ZZ_PROFK.fld_fml('ZZ_KOMP','BLANK',"null");
   ZZ_PROFK.win_edit('RED');
   ZZ_PROFK.win_patt('WZO')
?};

_ctx:=params_get().ctx[2];
ZZ_PROFK.index(_ctx.komp_ndx);
ZZ_PROFK.prefix(ref_name(_dok),_dok);
ZZ_PROFK.seek(_ctx.komp_ref);

{? grp_empty('zz_kryt_2')
|| '#disable'
?}


\adm_zz_profk_2_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka tabeli ZZ_PROFK w drugiej zakładce "Oceniane kompetencje".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _ctx:=params_get().ctx[2];
   _ctx.komp_ndx:=ZZ_PROFK.index('?');
   _ctx.komp_ref:=ZZ_PROFK.ref()
?}


\adm_zz_proft_2_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienka tabeli ZZ_PROFT w drugiej zakładce "Oceniane kompetencje".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? ZZ_PAR.MET_PROF='T'
|| grp_disp(ZZ_PROFO,'PROF',1)
?};

_hide:='';
{? ZZ_PROFT.ZZ_TYP().KOD='PS'
|| _hide:='T'
?};
ZZ_PROFT.actions_grayed(cur_win(1,1),_hide)


\adm_zz_proft_2_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_PROFT w drugiej zakładce "Oceniane kompetencje".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get:="ZZ_PROFK.ZZ_DOK";
_dok:=_get();

{? _a
|| ZZ_PROFT.fld_fml('ZZ_LINK','BLANK',_get);
   ZZ_TYP.win_dict('SLO')
?};

ZZ_TYP.clear();
ZZ_TYP.f_set('KOD',,'TYP=\':_a\' and KOD<>\':_b\'',ZZ_POM.MODUL,'OP');

ZZ_PROFT.clear();
ZZ_PROFT.f_clear();
ZZ_PROFT.f_set('ZZ_TYP(KOD)','join ZZ_TYP','ZZ_LINK=:_a and ZZ_TYP.KOD<>\'OP\'',_dok)


\adm_zz_proft_2_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka tabeli ZZ_PROFT w drugiej zakładce "Oceniane kompetencje".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| ZZ_TYP.f_clear();
   ZZ_PROFT.f_clear()
?}


\adm_zz_profo_2_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_PROFO w drugiej zakładce "Oceniane kompetencje".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get_profk:="ZZ_PROFK.ref()";
_get_proft:="ZZ_PROFT.ref()";

{? _a
|| _aid:={? ZZ_PAR.WAR_PROF<>'T' || 'W' || '' ?};
   cur_tab(1,1).actions(cur_win(1,1),_aid,,1);
   ZZ_PROFO.fld_fml('ZZ_PROFK','BLANK',_get_profk);
   ZZ_PROFO.fld_fml('ZZ_PROFT','BLANK',_get_proft);
   ZZ_MET.win_dict('SLO')
?};

_ctx:=params_get().ctx[2];
ZZ_PROFO.index(_ctx.met_ndx);
ZZ_PROFO.prefix(_get_profk(),_get_proft());
ZZ_PROFO.seek(_ctx.met_ref)


\adm_zz_profo_2_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka tabeli ZZ_PROFO w drugiej zakładce "Oceniane kompetencje".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| cur_tab(1,1).actions(cur_win(1,1),'',,1);
   _ctx:=params_get().ctx[2];
   _ctx.met_ndx:=ZZ_PROFO.index('?');
   _ctx.met_ref:=ZZ_PROFO.ref()
?}


\adm_zz_profk_3_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienka tabeli ZZ_PROFK w drugiej zakładce "Kompetencje profilu".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get())
:grp_disp(ZZ_PROFT,exec('get_proft_wnd','!zws_par_ppko'),1,1)


\adm_zz_profk_3_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_PROFK w drugiej zakładce "Kompetencje profilu".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get:="ZZ_PROF.ZZ_DOK";
_dok:=_get();

{? _a
|| _aid:={? ZZ_PAR.WAR_PROF<>'T' || 'R' || '' ?};
   cur_tab(1,1).actions(cur_win(1,1),_aid,,1);
   ZZ_PROFK.fld_fml('ZZ_LINK','BLANK',_get);
   ZZ_PROFK.fld_fml('ZZ_KOMP','BLANK',"null");
   ZZ_PROFK.win_edit('RED');
   ZZ_PROFK.win_patt('WZO')
?};

_ctx:=params_get().ctx[3];
ZZ_PROFK.index(_ctx.komp_ndx);
ZZ_PROFK.prefix(ref_name(_dok),_dok);
ZZ_PROFK.seek(_ctx.komp_ref)


\adm_zz_profk_3_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka tabeli ZZ_PROFK w drugiej zakładce "Kompetencje profilu".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| cur_tab(1,1).actions(cur_win(1,1),'',,1);
   _ctx:=params_get().ctx[3];
   _ctx.komp_ndx:=ZZ_PROFK.index('?');
   _ctx.komp_ref:=ZZ_PROFK.ref()
?}


\adm_zz_proft_3_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po odświeżeniu okienka tabeli ZZ_PROFT w drugiej zakładce "Kompetencje profilu".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? ZZ_PAR.MET_PROF='T'
|| grp_disp(ZZ_PROFO,'PROF',1)
?};

_hide:='';
{? ZZ_PROFT.ZZ_TYP().KOD='PS'
|| _hide:='T'
?};
ZZ_PROFT.actions_grayed(cur_win(1,1),_hide)


\adm_zz_proft_3_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_PROFT w drugiej zakładce "Kompetencje profilu".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get:="ZZ_PROF.ZZ_DOK";
_dok:=_get();

{? _a
|| ZZ_PROFT.fld_fml('ZZ_LINK','BLANK',_get);
   ZZ_TYP.win_dict('SLO')
?};

ZZ_TYP.clear();
ZZ_TYP.f_set('KOD',,'TYP=\':_a\' and KOD<>\':_b\'',ZZ_POM.MODUL,'OP');

ZZ_PROFT.clear();
ZZ_PROFT.f_clear();
ZZ_PROFT.f_set('ZZ_TYP(KOD)','join ZZ_TYP','ZZ_LINK=:_a and ZZ_TYP.KOD<>\'OP\'',_dok)


\adm_zz_profo_3_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_PROFO w drugiej zakładce "Kompetencje profilu".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get_profk:="ZZ_PROFK.ref()";
_get_proft:="ZZ_PROFT.ref()";

{? _a
|| _aid:={? ZZ_PAR.WAR_PROF<>'T' || 'W' || '' ?};
   cur_tab(1,1).actions(cur_win(1,1),_aid,,1);
   ZZ_PROFO.fld_fml('ZZ_PROFK','BLANK',_get_profk);
   ZZ_PROFO.fld_fml('ZZ_PROFT','BLANK',_get_proft);
   ZZ_MET.win_dict('SLO')
?};

_ctx:=params_get().ctx[3];
ZZ_PROFO.index(_ctx.met_ndx);
ZZ_PROFO.prefix(_get_profk(),_get_proft());
ZZ_PROFO.seek(_ctx.met_ref)


\adm_zz_profo_3_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka tabeli ZZ_PROFO w drugiej zakładce "Kompetencje profilu".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| cur_tab(1,1).actions(cur_win(1,1),'',,1);
   _ctx:=params_get().ctx[3];
   _ctx.met_ndx:=ZZ_PROFO.index('?');
   _ctx.met_ref:=ZZ_PROFO.ref()
?}


\adm_zz_profk_4_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przed obsługą okienka tabeli ZZ_PROFK w czwartej zakładce "Kompetencje profilu".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_get:="ZZ_PROF.ZZ_DOK";
_dok:=_get();

{? _a
|| _aid:={? ZZ_PAR.WAR_PROF<>'T' || 'R' || '' ?};
   cur_tab(1,1).actions(cur_win(1,1),_aid,,1);
   ZZ_PROFK.fld_fml('ZZ_LINK','BLANK',_get);
   ZZ_PROFK.fld_fml('ZZ_KOMP','BLANK',"null");
   ZZ_PROFK.win_edit('RED');
   ZZ_PROFK.win_patt('WZO')
?};

_ctx:=params_get().ctx[4];
ZZ_PROFK.index(_ctx.komp_ndx);
ZZ_PROFK.prefix(ref_name(_dok),_dok);
ZZ_PROFK.seek(_ctx.komp_ref);
{? grp_empty('profile')
|| '#disable'
?}


\adm_zz_profk_4_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po obsłudze okienka tabeli ZZ_PROFK w czwartej zakładce "Kompetencje profilu".
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| cur_tab(1,1).actions(cur_win(1,1),'',,1);
   _ctx:=params_get().ctx[4];
   _ctx.komp_ndx:=ZZ_PROFK.index('?');
   _ctx.komp_ref:=ZZ_PROFK.ref()
?}


\isTabVisible
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Sprawdza, czy zakładka tak naprawdę jest widoczna - obsługa błędnego działania funkcji: tab_show, tab_hide,
::       tab_sel, grp_disp(,,1,1)
::   WE: _a [INTEGER] - numer zakładki
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ctx:=params_get().ctx;
_ctx[_a].visible<>0


\ustaw_tytul
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Zależnie od trybu pracy ustawia tytuł okienka.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ZZ_PAR.OGR_RODZ='T'
|| ZZ_PROF.cntx_psh();
   _org:=ZZ_PROF.win_sel(_a);
   ZZ_PROF.hdr_sel();
   ZZ_PROF.hdr_sel(' (rodzaj: %1, metodyka: %2 (%3))'@[SLO_KOD.NAZWA, ZZ_PROF.ZZ_HIST().OPIS, $ZZ_PROF.ZZ_HIST().DATA]);
   ZZ_PROF.win_sel(_org);
   ZZ_PROF.cntx_pop()
?};
~~


\zz_profk_add_check
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Formuła sprawdza czy można dodać wybraną kompetencję do profilu (czy nie kłóci się z dodaną wcześniej
::       kompetencją nadrzędną, o ile taką posiada).
::   WE: _a - wskazanie na ZZ_KOMP (Kompetencja), _b - wskazanie na właściwy dla profilu ZZ_LINK tabeli ZZ_PROFK.
::   WY: 1 - kompetencja może być dodana, 0 - kompetencji nie można dodać
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(,'REF','STRING[16]',);
_valid:=0;
ZZ_KOMP.cntx_psh();
{? _a<>null()
|| {? ZZ_KOMP.ZZ_KOMP=0
   || ZZ_KOMP.index('NAZWA');
      ZZ_KOMP.prefix(exec('init_hist','phr_dane'),ZZ_KOMP.ref());
      _loop:=ZZ_KOMP.first();
      {!
      |? _loop
      |! _tab.REF:=$ZZ_KOMP.ref();
         _tab.add();
         _loop:=ZZ_KOMP.next()
      !};
      _profk:=sql('
         select
            ZZ_KOMP.REFERENCE as REF
         from
            ZZ_PROFK join ZZ_KOMP
         where
            ZZ_PROFK.ZZ_LINK=\':_a\'',
         _b
      );
      _check:=sql('select REF from :_a where REF IN (select REF from :_b)',_tab,_profk);
      {? ~_check.size()
      || _valid:=1
      || _valid:=2
      ?}
   || _komp:=$ZZ_KOMP.ref();
      _profk:=sql('
         select
            ZZ_KOMP.REFERENCE as REF
         from
            ZZ_PROFK join ZZ_KOMP
         where
            ZZ_PROFK.ZZ_LINK=\':_a\' and ZZ_KOMP.ZZ_KOMP=0',
         _b
      );
      _loop:=_profk.first();
      {!
      |? _loop
      |! _ref:=exec('FindAndGet','#table',ZZ_KOMP,_profk.REF,,,null());
         ZZ_KOMP.prefix(exec('init_hist','phr_dane'),_ref);
         _loop:=ZZ_KOMP.first();
         {!
         |? _loop
         |! _tab.REF:=$ZZ_KOMP.ref();
            _tab.add();
            _loop:=ZZ_KOMP.next()
         !};
         _loop:=_profk.next()
      !};
      _check:=sql('
         select
            REF
         from
            :_a
         where
            REF=\':_b\'',
         _tab,_komp
      );
      {? ~_check.size()
      || _valid:=1
      || _valid:=3
      ?}
   ?}
?};
ZZ_KOMP.cntx_pop();
_valid


\cfg_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Formuła tworzy tymczasowe okno wertowania umożliwiające przeglądanie profili ocen
::   WE:
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_komp:=ZZ_PROF.mk_sel('Profile ocen'@,'P',0,'tmp_zz_komp',,,,,'U');
ZZ_PROF.win_fld(_komp,,'NAZWA',,,50);
ZZ_PROF.win_fld(_komp,,'RODZAJ','NAZWA',,10,,,'Formularz'@);
ZZ_PROF.win_act(_komp,,'Dołącz',,,,"exec('zz_prof_dolacz_b','!zws_par_ppko')");
ZZ_PROF.win_act(_komp,1,'Dołącz',,,,"exec('zz_prof_dolacz_b','!zws_par_ppko')");
ZZ_PROF.win_act(_komp,,'Popraw',,,,"exec('zz_prof_popraw_b','!zws_par_ppko')");
ZZ_PROF.win_act(_komp,,'Formuła','Usuń'@@,,,
                  "exec('zz_xxx_usun_b','phr_widok')",
                  "exec('zz_xxx_usun_a','phr_widok')",,1,
                  "exec('zz_xxx_usun_bg','phr_widok')",
                  "exec('zz_xxx_usun_ag','phr_widok')");
{? ~(ZZ_PAR.OGR_RODZ='N')
|| ZZ_PROF.win_act(_komp,0,'Menu','&Rodzaj'@,,,,"exec('zz_prof_rodzaj_a','!zws_par_ppko')",,,,,'R');
   ZZ_PROF.win_act(_komp,1,'Menu','&Rodzaj'@,,,,"exec('zz_prof_rodzaj_a','!zws_par_ppko')",,,,,'R')
?};
ZZ_PROF.win_act(_komp,,'Formuła','Wstaw kompetencje'@,,,"exec('zz_profk_wstaw_b','phr_widok')");
ZZ_PROF.win_act(_komp,,'Formuła','Kreator&y'@,,,"exec('lista','phr_kreatory')");
exec('menu_wlasciwosci','phr_widok',ZZ_PROF,_komp);
ZZ_PROF.win_act(_komp,,'Formuła','Metodyka'@@,,,"
   ZZ_HIST.win_sel(exec('metod_tab','!zws_par_ppko'));
   {? ZZ_HIST.select() || params_exec('adm_zz_hist_sel_ar','!zws_par_ppko') ?}
");
ZZ_PROF.win_act(_komp,0,'Kolejność');
ZZ_PROF.win_act(_komp,0,'Rekord',,,,,"exec('zz_prof_ae','!zws_par_ppko')");
ZZ_PROF.win_act(_komp,1,'Rekord',,,,,"exec('zz_prof_ae','!zws_par_ppko')");

ZZ_PROF.win_act(_komp,,'Okienko',,,,"exec('zz_prof_bo','phr_widok')","exec('zz_prof_ao','phr_widok')");
ZZ_PROF.win_btn(_komp,'text=Dołącz','menu:D');
ZZ_PROF.win_btn(_komp,'text=Popraw','menu:P');
ZZ_PROF.win_btn(_komp,'text=Usuń','menu:U');

{? ~(ZZ_PAR.OGR_RODZ='N')
|| _btn:=ZZ_PROF.win_btn(_komp,'text=%1,icon=xwin16.png:190'['Rodzaj'@],'Menu');
   _fb:="exec('zz_prof_rodzaj_b_b','!zws_par_ppko')";
   ZZ_PROF.win_act(_komp,0,'Formuła','Bazowy'@@,'#R',,_fb,"params_exec('zz_prof_rodzaj_a','!zws_par_ppko')",,1,,,'B');
   ZZ_PROF.win_act(_komp,1,'Formuła','Bazowy'@@,'#R',,_fb,"params_exec('zz_prof_rodzaj_a','!zws_par_ppko')",,,,,'B');
   ZZ_PROF.btn_menu(_komp,_btn,'menu:RB','empty');
   ZZ_PROF.btn_menu(_komp,_btn,'menu:RB','noempty');
   _fo:="exec('zz_prof_rodzaj_o_b','!zws_par_ppko')";
   ZZ_PROF.win_act(_komp,0,'Formuła','Ogólny'@@,'#R',,_fo,"params_exec('zz_prof_rodzaj_a','!zws_par_ppko')",,1,,,'O');
   ZZ_PROF.win_act(_komp,1,'Formuła','Ogólny'@@,'#R',,_fo,"params_exec('zz_prof_rodzaj_a','!zws_par_ppko')",,,,,'O');
   ZZ_PROF.btn_menu(_komp,_btn,'menu:RO','empty');
   ZZ_PROF.btn_menu(_komp,_btn,'menu:RO','noempty');
   _fs:="exec('zz_prof_rodzaj_s_b','!zws_par_ppko')";
   ZZ_PROF.win_act(_komp,0,'Formuła','Szczegółowy'@@,'#R',,_fs,"params_exec('zz_prof_rodzaj_a','!zws_par_ppko')",,1,,,'S');
   ZZ_PROF.win_act(_komp,1,'Formuła','Szczegółowy'@@,'#R',,_fs,"params_exec('zz_prof_rodzaj_a','!zws_par_ppko')",,,,,'S');
   ZZ_PROF.btn_menu(_komp,_btn,'menu:RS','empty');
   ZZ_PROF.btn_menu(_komp,_btn,'menu:RS','noempty')
?};
ZZ_PROF.win_sel(_komp);
_komp


\metod_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Formuła tworzy tymczasowe okno wertowania umożliwiające zmianę metodyki
::   WE:
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_metod:=ZZ_HIST.mk_sel('Zmiany metodyki'@,,,'zz_hist_tmp');
ZZ_HIST.win_fld(_metod,,'DATA',,,-10);
ZZ_HIST.win_fld(_metod,,'OPIS',,,50);
_m_before:="ZZ_POM.ZZ_HIST:=ZZ_HIST.ref();sel_exit()";
ZZ_HIST.win_act(_metod,,'Formuła','Wybierz'@@,,'Wybór bieżącego zapisu'@,_m_before,,1);
_metod


\profk_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Formuła tworzy tymczasowe okno wertowania umożliwiające przeglądanie właściwości ocen
::   WE:
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_profk_tab:=ZZ_PROFK.mk_sel('Właściwości ocen'@,'P',,'profk_tmp');
ZZ_PROFK.win_fld(_profk_tab,,'ZZ_KOMP','NAZWA',,-40);
ZZ_PROFK.win_fld(_profk_tab,,'WAGA',,,-5);
ZZ_PROFK.win_fld(_profk_tab,,'WYMOG',,,-4);
ZZ_PROFK.win_act(_profk_tab,0,'Dołącz',,,,"exec('zz_profk_dolacz_b','!zws_par_ppko')");
ZZ_PROFK.win_act(_profk_tab,1,'Dołącz',,,,"exec('zz_profk_dolacz_b','!zws_par_ppko')");
ZZ_PROFK.win_act(_profk_tab,,'Popraw',,,,"exec('zz_profk_popraw_b','!zws_par_ppko')");
ZZ_PROFK.win_act(_profk_tab,,'Usuń',,,,"exec('zz_xxx_usun_b','phr_widok')","exec('zz_xxx_usun_a','phr_widok')",,1,"exec('zz_xxx_usun_bg','phr_widok')","exec('zz_xxx_usun_bg','phr_widok')");
ZZ_PROFK.win_act(_profk_tab,,'Formuła','Typy i metody oceny'@@,,,,"params_exec('oceny','!zws_par_ppko',cur_win_id())");
ZZ_PROFK.win_act(_profk_tab,,'Formuła','Wstaw kompetencje'@@,,,"exec('zz_profk_wstaw_b','phr_widok')");
ZZ_PROFK.win_act(_profk_tab,,'Formuła','Wa&rtości'@@,,,"exec('zz_profk_wartosci_b','phr_widok')");
exec('menu_wlasciwosci','phr_widok',ZZ_PROFK,_profk_tab);
ZZ_PROFK.win_act(_profk_tab,0,'Kolejność');
ZZ_PROFK.win_btn(_profk_tab,'text=Dołącz','menu:D');
ZZ_PROFK.win_btn(_profk_tab,'text=Popraw','menu:P');
ZZ_PROFK.win_btn(_profk_tab,'text=Usuń','menu:U');
_profk_tab


\oceny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Formuła tworzy tymczasowe okno grupowe dla typów i metod ocen
::   WE: _a - identyfikator okienka wywołującego formułę
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
: Okienko główne czynności
_wnd:=ZZ_PROF.grp_make('Typy i metody oceny'@,
:  po wyświetleniu
   "params_exec('adm_bf','!zws_par_ppko')",
:  identyfikator
   'bottom',,,
:  zamknięcie
   "params_exec('adm_oc','!zws_par_ppko')"
);
{? _a='zz_profk_2'
|| _s1bs:="params_exec('adm_zz_proft_2_bs','!zws_par_ppko',_a)";
   _s1ar:="params_exec('adm_zz_proft_2_ar','!zws_par_ppko')";
   _s1as:="params_exec('adm_zz_proft_2_as','!zws_par_ppko',_a)";
   _s2bs:="params_exec('adm_zz_profo_2_bs','!zws_par_ppko',_a)";
   _s2ar:="";
   _s2as:="params_exec('adm_zz_profo_2_as','!zws_par_ppko',_a)"
||
   _s1bs:="params_exec('adm_zz_proft_3_bs','!zws_par_ppko',_a)";
   _s1ar:="params_exec('adm_zz_proft_3_ar','!zws_par_ppko')";
   _s1as:="";
   _s2as:="params_exec('adm_zz_profo_3_as','!zws_par_ppko',_a)";
   _s2ar:="";
   _s2bs:="params_exec('adm_zz_profo_3_bs','!zws_par_ppko',_a)"
?};
ZZ_PROF.grp_sel(_wnd,ZZ_PROFT,exec('get_proft_wnd','!zws_par_ppko'),,_s1ar,,,3,_s1bs,_s1as,,,'maximized_with_title',
:  identyfikator
   'zz_proft_2'
);

{? ZZ_PAR.MET_PROF='T'
:  metody oceny kompetencji
|| ZZ_PROF.grp_splt(_wnd,,'horizontal','footer');
   ZZ_PROF.grp_sel(_wnd,ZZ_PROFO,'PROF',,_s2ar,,,3,_s2bs,_s2as,,,'maximized_with_title',
:     identyfikator
      'zz_profo_2'
   )
?};
ZZ_PROF.win_sel(_wnd);
ZZ_PROF.select()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 a556bb290f7905b192683aa4944f6e9de3789e296c9c808b9f5d9ffe899b58b6f1326cdd76dda8d6988b26b667f5b0455087952a0e2b565332aa32b92477c1ab1e0e9ea16d238cc6897e22ae443518f95883d5505bcaa45d2be08dea8ccfe3770885b9a74daa272cad518a5d12c9d510a832fd592987b59df90409d7828e9add

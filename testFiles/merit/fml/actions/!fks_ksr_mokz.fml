:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_mokz.fml [17.00]
:: Utworzony: 2015.12.15
:: Autor: AMK
::======================================================================================================================
:: Zawartosc: Formuły czynności FKS_KSR_MOKZ - Otwarcie okresu/roku - Finanse
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności FKS_KSR_MOKZ - Otwarcie okresu/roku - Finanse
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
~~


\hist_zamkf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Historia zamknięć - finanse
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::       _b - 1 - historia zamknięć okresu
::            2 - historia zamknięć roku
::----------------------------------------------------------------------------------------------------------------------
_b_dom:=exec('szuk_b_dom','parses','FKS'); _dalej:=1;
co_zamk:=_b;
{? ~_b_dom
|| FUN.info('W systemie nie znaleziono zdefiniowanego obszaru Finanse.'@); _dalej:=0
?};
OKRO_F.cntx_psh(); OKRO_F.index('ROK');
ROK_F.cntx_psh(); ROK_F.prefix();
OKR_OBSZ.cntx_psh();
{? _dalej
|| {? _a=1
   || {? co_zamk=1
      || OKRO_F.prefix();
         OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
         {? _b_dom & OKR_OBSZ.first()
         || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
            OKR_OBSZ.OKRO_F()
         || FUN.info('Nie znaleziono dla wybranego okresu wspólnego okresu finansowego.'@);
            _dalej:=0
         ?}
      || OKRO_F.prefix();
         OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
         {? _b_dom & OKR_OBSZ.first()
         || OKRO_F.prefix(OKR_OBSZ.OKRO_F().ROK);
            {? OKRO_F.last() || OKRO_F.ROK() || _dalej:=0 ?}
         || FUN.info('Nie znaleziono dla wybranego okresu wspólnego roku finansowego.'@);
            _dalej:=0
         ?}
      ?}
   || OKRO_F.ROK()
   ?}
?};
{? _dalej
|| {? co_zamk=1
   || OKRESY.ZO:=OKRO_F.ref();
      K__HIST.cntx_psh(); K__HIST.index('OKRO_F'); K__HIST.prefix(_b_dom,OKRESY.ZO);
      K__HIST.win_sel('SEL'); K__HIST.win_edit('RED1');
      {? OKRO_F.ZAM='N'
      || K__HIST.actions('SEL','O:O',,1)
      || K__HIST.actions('SEL','',,1)
      ?};
      K__HIST.hdr_sel(); K__HIST.hdr_sel(' okresu %1 %2'@[OKRESY.ZO().NAZ,OKRESY.ZO().ROK().NAZ]);
      K__HIST.select();
      K__HIST.cntx_pop()
   || OKRESY.ZO:=OKRO_F.ref();
      OKRESY.ZR:=ROK_F.ref();
      K__HIST.cntx_psh(); K__HIST.index('ROK_F'); K__HIST.prefix(_b_dom,OKRESY.ZR);
      K__HIST.win_sel('SEL1'); K__HIST.win_edit('RED1');
      {? ROK_F.ZAM='N'
      || K__HIST.actions('SEL1','O:O',,1)
      || K__HIST.actions('SEL1','',,1)
      ?};
      K__HIST.hdr_sel(); K__HIST.hdr_sel(' roku %1'@[ROK_F.NAZ]);
      K__HIST.select();
      K__HIST.cntx_pop()
   ?}
?};
OKRO_F.cntx_pop(); OKR_OBSZ.cntx_pop(); ROK_F.cntx_pop();
OKRESY.ZO:=OKRESY.ZR:=null;
VAR_DEL.delete('co_zamk')


\fks_ksr_mokz_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Otwieranie okresów i lat finansowych
::   WE: _a - co jest otwierane 1 - okres, 2 - rok
::       _b - w przypadku otwierania okresu czy szukać OKRO_F dla OKRO
::----------------------------------------------------------------------------------------------------------------------
co_zamk:=_a;
{? co_zamk=1
|| _dalej:=1;
   OKRO_F.cntx_psh();
   _b_dom:=exec('szuk_b_dom','parses','FKS'); _dalej:=1;
   {? ~_b_dom
   || FUN.info('W systemie nie znaleziono zdefiniowanego obszaru Finanse.'@);
      _dalej:=0
   ?};
   {? _dalej
   || {? _b
      || OKRO_F.prefix();
         OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
         {? _b_dom & OKR_OBSZ.first()
         || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
            OKR_OBSZ.OKRO_F();
            OKRESY.ZO:=OKRO_F.ref()
         || FUN.info('Nie znaleziono dla wybranego okresu wspólnego okresu finansowego.'@);
            _dalej:=0
         ?}
      || OKRESY.ZO:=OKRO_F.ref()
      ?}
   ?};
   {? _dalej
   || OKRESY.ZO();
      {? OKRO_F.r_lock(1,1)
      || {? _dalej
         || ROK_F.cntx_psh(); ROK_F.prefix(); OKRO_F.ROK();
            _naz_okr:=OKRO_F.NAZ+' '+ROK_F.NAZ;
            ROK_F.cntx_pop();
            ROK_F.cntx_psh(); ROK_F.prefix(); OKRO_F.ROK();
            {? ROK_F.ZAM='T'
            || FUN.info('Wybrany okres pochodzi z zamkniętego roku.'@); _dalej:=0
            ?};
            {? OKRO_F.ZAM='N'
            || FUN.info('Wybrany okres nie jest zamknięty.'@); _dalej:=0
            ?};
            {? _dalej & OKRO_F.NAZ='Bilans zamknięcia'
            || FUN.info('Okresu \"Bilans zamknięcia\" nie można otworzyć tą akcją.'@); _dalej:=0
            ?};
            ROK_F.cntx_pop()
         ?};
         {? _dalej
         || ROK_F.cntx_psh(); ROK_F.prefix();
            OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
            {? OKRO_F.next() & OKRO_F.ZAM='T'
            || FUN.info('Okresu %1 nie można otworzyć.\nKolejny okres (%2 %3) jest zamknięty.'@[_naz_okr,OKRO_F.NAZ,OKRO_F.ROK().NAZ]);
               _dalej:=0
            ?};
            OKRO_F.cntx_pop(); ROK_F.cntx_pop()
         ?};
         {? _dalej
         || ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix();
            OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
            {? OKRO_F.next() & OKRO_F.NAZ='Bilans zamknięcia'
            || OKRO_F.ROK();
               {? ROK_F.next()
               || OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
                  {? OKRO_F.first() & OKRO_F.next() & OKRO_F.ZAM='T'
                  || FUN.info('Okresu %1 nie można otworzyć.\nOkres %2 %3 jest zamknięty.'@[_naz_okr,OKRO_F.NAZ,OKRO_F.ROK().NAZ]);
                     _dalej:=0
                  ?}
               ?}
            ?};
            OKRO_F.cntx_pop(); ROK_F.cntx_pop()
         ?};
         {? _dalej
         || ROK_F.cntx_psh(); 
            _naz_okr:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ; 
            ROK_F.cntx_pop();
            {? FUN.ask('Otworzyć okres %1?'@[_naz_okr])
            || exec('k__hist','zam_okr_rok',OKRESY.ZO,'FKS','O')
            ?}
         ?};
         unlock_r()
      || FUN.info('Inny użytkownik obsługuje okres obrachunkowy.'@)
      ?}
   ?};
   OKRO_F.cntx_pop()
|| _dalej:=1;
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(); OKRO_F.ROK();
   {? ROK_F.r_lock(1,1)
   || ROK_F.cntx_psh();
      {? ROK_F.ZAM='N'
      || FUN.info('Wybrany rok nie jest zamknięty.'@); _dalej:=0
      ?};
      {? _dalej & ROK_F.next() & ROK_F.ZAM='T'
      || FUN.info('Kolejny rok (%1) jest zamknięty.'@[ROK_F.NAZ]);
         _dalej:=0
      ?};
      ROK_F.cntx_pop();
      ROK_F.cntx_psh();
      {? _dalej & ROK_F.next() & ROK_F.ZAM='N'
      || OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
         {? OKRO_F.first() & OKRO_F.ZAM='T'
         || FUN.info('Okres Bilans otwarcia w kolejnym roku (%1) jest zamknięty.'@[ROK_F.NAZ]);
            _dalej:=0
         ?};
         OKRO_F.cntx_pop()
      ?};
      ROK_F.cntx_pop();
      {? _dalej & FUN.ask('Otworzyć rok %1?'@[ROK_F.NAZ])
      || exec('k__histr','zam_okr_rok',OKRESY.ZR,'FKS','O')
      ?};
      unlock_r()
   || FUN.info('Inny użytkownik obsługuje rok bilansowy.'@)
   ?};
   ROK_F.cntx_pop();
   1
?}


\k__hist_rkprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Historia zamknięć - rekord przed
::----------------------------------------------------------------------------------------------------------------------
{? co_zamk=1
|| {? K__HIST.OKRO_F().ZAM='N'
   || K__HIST.actions('SEL','O:O',,1)
   || K__HIST.actions('SEL','',,1)
   ?}
|| {? K__HIST.ROK_F().ZAM='N'
   || K__HIST.actions('SEL1','O:O',,1)
   || K__HIST.actions('SEL1','',,1)
   ?}
?};
0

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:41 7612a08a91b02045592b3d24f95a6b53b0411adba54e3af6342171d5876606e33f84e2f5091dd43e56ec37794d62cd472da4748511b85ea71a3d7418ce0e25765e90d28a26f8e8832fad6727aaf3e6edd0ec2d8ee7f1bf6782fac56be565bdd3838efc73f83c52c51688b4e65133323016d9b08307be9cb8140297b01d28458a

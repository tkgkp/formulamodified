:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !poc_ocw_gdok.fml
:: Utworzony: 15.12.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności POC_OCW_GDOK - Generowanie dokumentu oceny.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Generowanie dokumentu oceny - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# access=exec('access','!poc_ocw_gdok')
::
::# kind=WE, symbol=ZO_TEST, type=_ZO_TEST, name=Ocena całkowita, required=T, keyref=T
::
params_set(_par:=params_get());
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;
_context:=_par.context;
_akcja:=_mp.akcja();

{? _akcja='webTermProcCancel'
|| return(~~)
?};

_ret:=exec('run','!poc_ocw_gdok',_in.ZO_TEST,_akcja);
{? _mp.isMicro()
|| _mp.done()
|? _ret
|| _mp.done()
|| _mp.keep()
?};

~~


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Generowanie dokumentu oceny (webTerm) - główna formuła czynności.
::   WE:  _a  [STRING]    - Miejsce wywołania (Proc, Todo, Area).
::       [_b] [REFERENCE] - Wskazanie na instancję procesu (dla Proc i Todo).
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='Area' | _a='Proc' | _a='Todo')
|| _path:=_a
|| return()
?};

{? _path='Area'
:: Uruchomienie z obszaru roboczego. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
|| _web_params:=web_params_get(1);

   {? ref_tab(_web_params.B_PREL)=BI_PREL
::    Kontynuacja czynności.
   || _bi_prel:=exec('FindAndGet','#table',BI_PREL,_web_params.B_PREL);
      _b_ele:=exec('FindAndGet','#table',BI_PREL,_bi_prel,,"B_PREL().B_ELE",null());

      _kind_in:=exec('kind_in','#b_port');
      _in:=exec('getPorts','#b_port',_b_ele,_kind_in,_bi_prel);
      exec('fillPorts','#bi_port',_bi_prel,_kind_in,_in)

::    Mikroczynność lub start procesu.
   || _bi_prel:=null();
      _b_prel:=exec('FindAndGet','#table',B_PREL,_web_params.B_PREL);
      _b_ele:=exec('FindAndGet','#table',B_PREL,_b_prel,,"B_ELE",null());
      _in:=exec('get_act_proc_params','#b__box',_web_params.ACT_UID,_b_prel,_web_params.PORTS_IN)

   ?};

   _args:=exec('obj_ntab_set','#array',,
      'ZO_TEST',$_in.ZO_TEST,
      'path',_path,
      'web_run',"_par:=web_params_get(_a); exec('web_run','#b__box',_par.B_PREL,_b,_par.PORTS_IN,'web_Area')"
   );
   exec('web_run','!poc_ocw_gdok',_args)

|? _path='Todo'
:: Uruchomienie z listy zadań (czynność w procesie, wywołana z parametrem - wskazaniem formularza).  - - - - - - - - - -
|| {? var_pres('_b')=type_of(null()) & _b<>null() & ref_tab(_b)=BI_PREL
   || _bi_prel:=_b
   || return()
   ?};

   {? exec('env_wt','poc',0)
::    Startujemy z listy zadań - konieczne jest ustawienie środowiska.
   || _b_ele:=exec('FindAndGet','#table',BI_PREL,_bi_prel,,"B_PREL().B_ELE",null());

      _kind_in:=exec('kind_in','#b_port');
      _in:=exec('getPorts','#b_port',_b_ele,_kind_in,_bi_prel);
      exec('fillPorts','#bi_port',_bi_prel,_kind_in,_in);

      _args:=exec('obj_ntab_set','#array',,
         'ZO_TEST',$_in.ZO_TEST,
         'path',_path,
         'web_run',$("exec('web_run','#b__box','"+$_bi_prel+"',_a,,'web_Todo')")
      );
      exec('web_run','!poc_ocw_gdok',_args)
   ?}

|? _path='Proc'
:: Uruchomienie z pulpitu (czynność startowa).   - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: Czynność wymaga parametru wejściowego - nie może być czynnością startową.
|| ~~
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Generowanie dokumentu oceny (webTerm) - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_tab:=exec('init_desc_tab','pracownik');
_addTab:=exec('desc_tab','phr_dane');

{? (app_info('web_tabid')='' | exec('env_wt','poc',1)) & type_of(_in.ZO_TEST)=type_of(null()) & _in.ZO_TEST<>null()
|| ZZ_DOK.cntx_psh();
   ZZ_DOK.prefix();
   ZZ_OSOBA.cntx_psh();
   ZZ_OSOBA.prefix();
   ZO_TEST.cntx_psh();
   ZO_TEST.prefix();
   {? ZO_TEST.seek(_in.ZO_TEST)
   || _dok:=ZO_TEST.ZZ_OSOBA().ZZ_LINK;
      {? _dok<>null()
      || OSOBA.cntx_psh();
         OSOBA.prefix();
         P.cntx_psh();
         P.index('ZZ_DOK');
         P.prefix();
         {? P.find_key(ref_name(_dok),_dok)
         || _tab.ZAW_DANE:='T';
            _addTab.ZAW_DANE:='N';
            _tab.NAZWISKO:=P.OSOBA().NAZWISKO;
            _tab.PIERWSZE:=OSOBA.PIERWSZE;
            _tab.OBCY:=OSOBA.OBCY;
            _tab.PASZPORT:=OSOBA.PASZPORT;
            _tab.PESEL:=OSOBA.PESEL;
            _tab.UR_DATA:=$OSOBA.UR_DATA;
            _tab.T:=P.T;
            _tab.IP:=$P.IP
         || _tab.ZAW_DANE:='N';
            _addTab.ZAW_DANE:='T';
            _addTab.ZZ_NAZWISKO:=ZZ_OSOBA.NAZWISKO;
            _addTab.ZZ_PIERWSZE:=ZZ_OSOBA.PIERWSZE
         ?};
         P.cntx_pop();
         OSOBA.cntx_pop()
      ?}
   ?};
   ZO_TEST.cntx_pop();
   ZZ_OSOBA.cntx_pop();
   ZZ_DOK.cntx_pop()
?};

{? _addTab.ZAW_DANE='T' & _tab.ZAW_DANE='N'
|| 'Wygeneruj dokument oceny dla %1 %2'@@[_addTab.ZZ_NAZWISKO,_addTab.ZZ_PIERWSZE]
|? _addTab.ZAW_DANE='N' & _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Wygeneruj dokument oceny dla %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Wygeneruj dokument oceny dla %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Wygeneruj dokument oceny dla %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Wygeneruj dokument oceny'@@
?}


\access
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Formuła uprawnień, decyduje o tym, czy dany użytkownik może uruchomić bieżącą instancję czynności.
::   WE:
::   WY: 0 - Użytkownik nie ma uprawnień do wykonania bieżącej instancji czynności.
::       1 - Użytkownik ma uprawnienia do wykonania bieżącej instancji czynności.
::----------------------------------------------------------------------------------------------------------------------
exec('init','poc');

params_set(_par:=params_get());

_mp:=_par.mp;
_in:=_par.in;
:: Użytkownik weryfikowany (być może zastępujący).
_user:=_par.user;
:: Użytkownik zastępowany lub ~~.
_user_r:=_par.user_r;

_ret:=0;

_usr:={? _user_r=~~ || _user || _user_r ?};
_osoba:=exec('FindAndGet','#table',USERS,_usr,,"OSOBA",null());
{? _osoba=null()
|| return(_ret)
?};

ZO_TEST.cntx_psh();
ZO_TEST.prefix();
{? var_pres('ZO_TEST',_in)=type_of(null()) & _in.ZO_TEST<>null() & ZO_TEST.seek(_in.ZO_TEST)
:: Dokument oceny może generować osoba oceniana i osoba w roli PB - bezpośredni przełożony.
|| _ret:=exec('zo_test_owner_or_pb','phr_dane',$ZO_TEST.ref(),$_osoba)

:: Wyjątkowo, ze względu na poufność danych, nie ma taryfy ulgowej. Jeżeli nie mamy pewności, że czynność chce uruchomić
:: osoba oceniana lub osoba w roli PB - bezpośredni przełożony, to blokujemy uruchomienie.
|| ~~
?};
ZO_TEST.cntx_pop();

_ret


\isd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Formuła zwraca sygnaturę dokumentu oceny.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'OCPRAC-1'


\zz_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Formuła pomocnicza - dla bieżącej oceny całkowitej zwraca wskazanie załącznika z dokumentem oceny.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.cntx_psh();
_typ:=exec('typzal_rep','phr_dane',ZO_TEST,1).UD_SKL;
UD_DEF.cntx_pop();
ZZ_DOKZ.cntx_psh();
ZZ_DOKZ.index('ISD');
ZZ_DOKZ.prefix(ZO_TEST.NP_DOK,ZO_TEST.ZZ_DOK,_typ,exec('isd','!poc_ocw_gdok'));
_zz_dokz:={? ZZ_DOKZ.first() || ZZ_DOKZ.ref() || null() ?};
ZZ_DOKZ.cntx_pop();
_zz_dokz


\web_run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Formuła odpowiedzialna za interfejs, wywoływana dla ustalonego środowiska.
::   WE: _a [ARRAY] - Tablica z parametrami
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_args:=_a;

ZO_TEST.cntx_psh();
ZO_TEST.prefix();
{? ZO_TEST.seek(_args.ZO_TEST) & ZO_TEST.KOMPLET='T'
|| _zz_dokz:=exec('zz_dok','!poc_ocw_gdok');
   {? _args.path='Area'
   || {? _zz_dokz=null()
      || _args.web_run(1,'GENERUJ')
      || web_params_set(exec('obj_ntab_set','#array',web_params_get(1),'web_run',_args.web_run));
         web_choice(
            "  {? _a=2
               || return()
               |? ~exec('env_wt','poc')
               || FUN.emsg('Ustalenie środowiska pracy nie było możliwe.'@);
                  return()
               ?};
               _par:=web_params_get();
               {? _a=0
               || _par.web_run(0,'GENERUJ')
               |? _a=1
               || _par.web_run(0,'POBIERZ')
               ?}
            ",
            'Dokument oceny już istnieje.'@,
            FUN.TYT,'ASK',,,'Aktualizuj'@,'Pobierz'@,'Anuluj'@
         )
      ?}

   |? _args.path='Todo'
   || ZO_PROG.cntx_psh();
      ZO_PROG.prefix();
      ZO_PROC.cntx_psh();
      ZO_PROC.prefix();
      ZO_OSOBA.cntx_psh();
      ZO_OSOBA.prefix();
      ZZ_OSOBA.cntx_psh();
      ZZ_OSOBA.prefix();
      {? ZO_TEST.ZO_OSOBA().ZO_PROC().ZO_PROG().get() & ZO_TEST.ZZ_OSOBA().get()
      || _szer:=50;
         _def:=web_define();
         web_def_sep(_def,'Program'@);
         web_def_fld(_def,'ZO_PROG',ZO_PROG.NAZWA,'Nazwa'@,'Nazwa programu'@,_szer,,,'editable=0');
         web_def_sep(_def,'Sesja'@);
         web_def_fld(_def,'ZO_PROC_OD1',ZO_PROC.OKRES_OD,'Od dnia (okres)'@,
            'Początek okresu podlegającego ocenie'@,,,,'editable=0');
         web_def_fld(_def,'ZO_PROC_DO1',ZO_PROC.OKRES_DO,'Do dnia (okres)'@,
            'Koniec okresu podlegającego ocenie'@,,,,'editable=0');
         web_def_fld(_def,'ZO_PROC_OD2',ZO_PROC.PLAN_OD,'Od dnia (wykonanie)'@,
            'Początek zaplanowanego okresu przeprowadzenia ocen'@,,,,'editable=0');
         web_def_fld(_def,'ZO_PROC_DO2',ZO_PROC.PLAN_DO,'Do dnia (wykonanie)'@,
            'Koniec zaplanowanego okresu przeprowadzenia ocen'@,,,,'editable=0');
         web_def_sep(_def,'Osoba oceniana'@);
         web_def_fld(_def,'NAZWISKO',ZZ_OSOBA.NAZWISKO,'Nazwisko'@,'Aktualne nazwisko'@,_szer,,,'editable=0');
         web_def_fld(_def,'PIERWSZE',ZZ_OSOBA.PIERWSZE,'Imię'@,'Pierwsze imię'@,_szer,,,'editable=0');

         P.cntx_psh();
         P.index('ZZ_DOK');
         P.prefix(ZZ_OSOBA.NP_DOK,ZZ_OSOBA.ZZ_LINK);
         {? P.first()
         || UD_SKL.cntx_psh();
            UD_SKL.prefix();
            P.WYDZIAL();
            web_def_fld(_def,'JOS',UD_SKL.SYMBOL,'Jednostka organizaycjna - symbol'@,
               'Symbol jednostki organizacyjnej'@,_szer,,,'editable=0');
            web_def_fld(_def,'JON',UD_SKL.OPIS,'Jednostka organizaycjna - nazwa'@,
               'Nazwa jednostki organizacyjnej'@,_szer,,,'editable=0');
            UD_SKL.cntx_pop();
            STN.cntx_psh();
            STN.prefix();
            web_def_fld(_def,'STN',P.ST().ST,'Stanowisko'@,'Nazwa stanowiska'@,_szer,,,'editable=0');
            STN.cntx_pop()
         ?};
         P.cntx_pop();

         {? _zz_dokz=null()
         || web_def_btn(_def,'GENERUJ','text=%1,icon=%2' ['Generuj'@,exec('zakoncz','#icon')])
         || web_def_btn(_def,'GENERUJ','text=%1,icon=%2' ['Aktualizuj'@,exec('zakoncz','#icon')]);
            web_def_btn(_def,'POBIERZ','text=%1,icon=%2' ['Pobierz'@,exec('zakoncz','#icon')])
         ?};
         web_def_btn(_def,'ANULUJ','text=%1,icon=%2' ['Anuluj'@,exec('anuluj','#icon')]);

::       Zapamiętujemy aktualny stan tabeli BI_TODO (zwłaszcza, że jest maskowalna), aby móc go odtworzyć / odświeżyć
::       po zakończeniu czynności.
         BI_TODO.web_cntx_save(1);

         web_params_set(exec('obj_ntab_set','#array',web_params_get(1),'web_run',_args.web_run));
         web_def_edit(_def,
            "  web_def_close();
               {? _a='ANULUJ'
               || return()
               |? ~(exec('env_wt','poc') & BI_TODO.web_cntx_load(1)>0)
               || FUN.emsg('Ustalenie środowiska pracy nie było możliwe.'@);
                  return()
               ?};
::             I tutaj małe oszustwo. Najpierw powinniśmy wywołać web_run(), a potem odświeżyć okno. Ale ze względu
::             na to, że czynność kończy sie pobraniem pliku , co jest traktowane jako użycie funkcji
::             dialogowej, to zrobimy to w odwrotnej kolejności. Co prawda może to mieć znaczenia, bo faktyczne
::             odświeżenie i tak jest kolejkowane i wykona się po web_run() ...
               BI_TODO.web_refresh(web_top_win(1));
               web_params_get().web_run(_a);
               ~~
            ",'Dokument oceny'@
         )

      ?};
      ZZ_OSOBA.cntx_pop();
      ZO_OSOBA.cntx_pop();
      ZO_PROC.cntx_pop();
      ZO_PROG.cntx_pop()
   ?}
?};
ZO_TEST.cntx_pop();

1


\run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Obsługa akcji z czynności.
::   WE:  _a  [REFERENCE] - Ocena całkowita (ZO_TEST).
::       [_b] [STRING]    - Kod akcji do wykonania: GENERUJ*/POBIERZ
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:={? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=ZO_TEST || _a || null() ?};
_akcja:={? var_pres('_b')=type_of('') || _b || 'GENERUJ' ?};

_zz_dokz:=null();
ZO_TEST.cntx_psh();
ZO_TEST.prefix();
{? ZO_TEST.seek(_ref) & ZO_TEST.KOMPLET='T'
|| {? _akcja='GENERUJ'
   || exec('generuj','!poc_ocw_gdok');
      _zz_dokz:=exec('zz_dok','!poc_ocw_gdok')
   |? _akcja='POBIERZ'
   || _zz_dokz:=exec('zz_dok','!poc_ocw_gdok');
      ZZ_DOKZ.cntx_psh();
      ZZ_DOKZ.prefix();
      {? ZZ_DOKZ.seek(_zz_dokz)
      || ZZ_DOKZ.web_bl_download('PLIK')
      ?};
      ZZ_DOKZ.cntx_pop()
   ?}
?};
ZO_TEST.cntx_pop();
_zz_dokz<>null()


\generuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Właściwa akcja generowania dokumentu oceny. Formuła wymaga poprawnie ustawionego środowiska, w szczególności
::       jest wykonywana dla bieżącego rekordu tabeli ZO_TEST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZZ_POM.DOKZ_LNK:=ZO_TEST.ZZ_DOK;
exec('report','phr_kreatory',ZO_TEST,'poc_ocprac1','ocena.pdf','Dokument oceny okresowej pracownika',1,
   exec('isd','!poc_ocw_gdok')
)

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:18 0229e67ceb081446346498d0051d140795db5ab3beac453914f61fef0f8a402faf3731160faf7147c1d880c7e9e9d7aeae8a26e040bb579a20bab3c2d79fbc75e30218714462f2eee725c3ad5e67c6fd5020dedafb2dbd9b6b056f7208a1af7fd5c332bf5301abeffe934b8d1bb4168415f1917f9c304ee8d9d5ec5e7b75f075

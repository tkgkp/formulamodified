:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_pll_pnzp.fml
:: Utworzony: 07.02.2022
:: Autor: DG
::======================================================================================================================
:: Zawartość: Obsługa czynności PPL_PLL_PNZP - Przegl. nadw. zal. na podatek.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Przegl. nadw. zal. na podatek - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
~~


\p_npod_bz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Zmiana parametrów wyświetlania danych z tabeli P_NPOD.
::  OLD: \p_npod_bz/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_PAR:=tab_tmp(,'ROK','INTEGER','Rok podatkowy'@);
_we:=_PAR.mk_edit('Podaj dane'@,,'#p_npod_par');
_PAR.win_efld(_we,,'ROK',,,5,,,'Rok podatkowy'@,,'Wybierz rok podatkowy'@);
exec('ok_esc','#window',_PAR,_we);
_PAR.win_edit(_we);

_PAR.ROK:={? EDIT_VAR.ROK<>0 || EDIT_VAR.ROK || date()~1 ?};

_valid:="
   _PAR:=cur_tab();
   {? _PAR.ROK<2022
   || FUN.emsg('Rok podatkowy nie może być mniejszy niż 2022.'@);
      return('ROK')
   ?};
   ''
";

{? _PAR.edit(_valid)
|| POLA_GRP.REAL_1:=POLA_GRP.REAL_2:=POLA_GRP.REAL_3:=0;
   POLA_GRP.INT_1:=EDIT_VAR.ROK:=_PAR.ROK;
   {? __F_ZATR.P<>'Z'
   || _ind:={? PAR_SKID.get(236)='T' || 'OKNO_OX' || 'OKNO_O' ?};
      _wer:='WER_P'
   || _ind:={? PAR_SKID.get(236)='T' || 'OKNO_RHX' || 'OKNO_RH' ?};
      _wer:='WER_Z'
   ?};
   P_NPOD.index(_ind);
   P_NPOD.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,REF.OSOBA,EDIT_VAR.ROK);
   P_NPOD.first()
?}


\p_npod_br
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła Rekord - przed dla tabeli P_NPOD
::  OLD: \p_npod_br/kali.fml
::----------------------------------------------------------------------------------------------------------------------
:: Uzupełnienie nazwy listy płac (wielkie litery)
O.cntx_psh();
EDIT_VAR.SYM_LT:=~-P_NPOD.O().LT;
O.cntx_pop();

:: Akcja usuń tylko dla zapisów pochodzących z korekt
_wer:={? __F_ZATR.P='P' || 'WER_P' || 'WER_Z' ?};
{? P_NPOD.CZY_KOR='T'
|| P_NPOD.actions(_wer,'',,1)
|| P_NPOD.actions(_wer,'U',,1)
?};

~~


\p_npod_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła Okienko - przed dla tabeli P_NPOD
::  OLD: \p_npod_bw/kali.fml
::----------------------------------------------------------------------------------------------------------------------
:: Wyliczenie bilansu nadwyżki
POLA_GRP.REAL_1:=POLA_GRP.REAL_2:=POLA_GRP.REAL_3:=0;
P_NPOD.cntx_psh();
{? __F_ZATR.P<>'Z'
|| P_NPOD.index('OKNO_OX')
|| P_NPOD.index('OKNO_RHX')
?};
{? EDIT_VAR.ROK<2022 || EDIT_VAR.ROK:=date()~1 ?};
_nadwyzka:=_roznica:=0;
P_NPOD.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,REF.OSOBA,EDIT_VAR.ROK);
{? P_NPOD.first()
|| {!
   |? {? P_NPOD.TYP='N'
      || _nadwyzka+=P_NPOD.NADWYZKA
      || _roznica+=P_NPOD.ROZNICA
      ?};
      P_NPOD.next()
   !}
?};
P_NPOD.cntx_pop();
_bilans:=_nadwyzka - _roznica;
{? _bilans<0
|| _bilans:=0
?};
POLA_GRP.REAL_1:=_nadwyzka;
POLA_GRP.REAL_2:=_roznica;
POLA_GRP.REAL_3:=_bilans;
POLA_GRP.INT_1:=EDIT_VAR.ROK;
win_set('cur_row_pos=-1');
1


:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:53 6f2333a5616fbe43ff4dfa91ee5e8857c8b3c5eaa6d1027af37016348ea6364e3b58139e61410d4971294e0a5130aaa534b24f29e65b64b8ab42d517d335efe7779e464688e6e3031352e9196a0391eef42c700641a072a4cb1169ca65eb51f27b7b71bad90f03c8a59e4d8a307cce126d80e0552b7f62dcbf5fc7cbc362a67f

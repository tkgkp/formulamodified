:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_mcr_dzpm.fml
:: Utworzony: 21.09.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_MCR_DZPM - Zablokowanie plan.dla miesiąca
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Zablokowanie planowania dla miesiąca rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKRM, type=_A_OKRM, name=Wskazanie na miesiąc rozliczeniowy, required=T
::# kind=WY, symbol=S_PLAN, type=STRING, name=Status blokady planowania, required=N
::
_par:=params_get();
params_set(_par);
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;
_uidref:=exec('ref2uid','#table',_in.A_OKRM);
_result:='';

{? _uidref=''
|| _mp.error(exec('error','!prc_mcr_dzpm','Nie znaleziono miesiąca rozliczeniowego.'@))
|? _mp.pathProc() | _mp.pathTodo()
|| _result:=exec('zablokowanie','!prc_mcr_dzpm',_in,_out);
   {? _out.S_PLAN='Z'
   || _mp.save(,_out);
      _mp.done()
   |? var_pres('_result')=type_of('')
   || _mp.error(_result)
   |? _result=2
   || _mp.keep()
   || _mp.save(,_out);
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Zablokowanie planowania dla miesiąca rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a_okrm:=params_get().mp.load(exec('kind_in','#b_port')).A_OKRM;
_tab:=params_exec('opis_mc','okres',_a_okrm);

{? _tab.ZAW_DANE='T'
|| 'Zablokuj planowanie w miesiącu: %1'@@[date(_tab.R,_tab.M,)$8]
|| 'Zablokuj planowanie w miesiącu'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Szczegóły komunikatu o błędzie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Zablokowanie planowania nie jest możliwe.'@,_a]


\zablokowanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Zablokowanie planowania dla miesiąca rozliczeniowego.
::   WE: _a - parametry wejściowe czynności
::       _b - parametry wyjściowe czynności
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:=_result:='';
A_OKRM.cntx_psh();
{? A_OKRM.seek(_a.A_OKRM,,1)
|| {? A_OKRM.S_PLAN='O'
   || _tab:=tab_tmp(,
         'RESULT','INTEGER','',
         'MONTH','STRING[20]','Miesiąc rozliczeniowy'@
      );
      _tab.MONTH:=date(A_OKRM.R,A_OKRM.M,)$8;
      _ed:=_tab.mk_edit('Zablokowanie miesiąca rozliczeniowego'@,0);
      _tab.win_efld(_ed,,'MONTH',,,,,1);
      exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:Esc'",,
         exec('help_red_zakoncz','#window','PKD_D'),exec('text_red_zakoncz','#window','PKD_D'));
      exec('ok_esc','#window',_tab,_ed,1,,"cur_tab().RESULT:=0; 'key:Esc'",0,,
         exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
      _tab.win_edit(_ed);
      _tab.edit();
      {? _tab.RESULT=1
      || exec('a_okrm_wer_bfz','prc_mscrozlicz',0);
         A_OKRM.seek(_a.A_OKRM,,1,1);
         {? A_OKRM.S_PLAN='Z'
         || _b.S_PLAN:='Z';
            _msg:='Miesiąc rozliczeniowy %1 został zablokowany.'@[date(A_OKRM.R,A_OKRM.M,)$8];
            _result:=1
         || _result:=_msg:='Miesiąc rozliczeniowy %1 nie został zablokowany.'@[date(A_OKRM.R,A_OKRM.M,)$8]
         ?}
      || _result:=2
      ?};
      obj_del(_tab)
   || _result:=_msg:=exec('error','!prc_mcr_dzpm','Miesiąc rozliczeniowy jest już zablokowany.'@)
   ?}
|| _result:=_msg:=exec('error','!prc_mcr_dzpm','Nie udało się odnaleźć miesiąca rozliczeniowego do zablokowania.'@)
?};
{? +_msg || FUN.emsg(_msg) ?};
A_OKRM.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 025dfe568288edefeb260cda2c8928cfe58e9babb9038a8d903bb18f121635d0f0da1bbaadab15971fef6c35eb8c5936f2ba3b24c886506634c75b864a3c7f7904329862f78165bb9f5787c903f224860039aa06f17636ebdda6bacebd35405612502d34a5b90e9cc8deb99f6f7b15041bc03d868789475fa6d1f8087fe98cf5

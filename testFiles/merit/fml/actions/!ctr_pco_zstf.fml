:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pco_zstf.fml
:: Utworzony: 30.05.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PCO_ZSTF - Definicje podziałów dla środków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Definicje podziałów dla środków - główna formuła czynności CTR_PCO_ZSTF
::  OLD: \estra_dom/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
zakl_war:=1; dla_srod:=0;
K_ES.OPCJA:='DD';
VAR.ROK_F:=SSTALE.AR; VAR.OKRO_F:=SSTALE.AO; VAR.KONTROL:='T';
VAR.EDITABLE:=1;
K_ES.O:=exec('get_par','#parametr',204); VAR.ROK_F(); VAR.OKRO_F();
exec('maski','srodobj'); SRSR.index('KIND'); SRSR.prefix('T'); SRSR.first();
KTRWP.cntx_psh(); KTRWP.use('kontwa'+SSTALE.AR().KOD);
ZM.KONTROL:='A'; ZM.KDEF:='D';
SKID_MBN.index('EWID'); SKID_MBN.prefix('T','O');
_mo:=exec('mod_win','control',103);
_gr:=SKID_MBN.grp_make('Definicje podziałów controllingowych dla środków trwałych'@,,'skidmbnst');
SKID_MBN.grp_sel(_gr,,_mo,,"
   ZM.SKID_MBN:=SKID_MBN.ref();
   exec('opiszwym','control');
   exec('set_enabled','!ctr_pco_zstf');
   exec('showktrd','!ctr_pco_zstf');
   grp_disp(KTRD,'SELEKCJA',,1)
",,,,,,,,'maximized_with_title');
KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W1],K_ES,'E1');
KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W2],K_ES,'E2');
KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W3],K_ES,'E3');
KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W4],K_ES,'E4');
KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W5],K_ES,'E5');
SKID_MBN.grp_splt(_gr,,'horizontal','bottom',8);
SKID_MBN.grp_sel(_gr,KTRD,'SELEKCJA','Domyślne podziały'@,,,,,"exec('showktrd1','!ctr_pco_zstf',0)",,,,'maximized');
SKID_MBN.grp_sel(_gr,SRSR,'CON','Podziały dla środków'@,"exec('showktrd1','!ctr_pco_zstf',1); grp_disp(KTRD,'SELEKCJA',,1)"
                 ,,,,"exec('showktrd1','!ctr_pco_zstf',1)",,,,'maximized');
SKID_MBN.tab_splt(_gr,,'horizontal','def_dla_sr',20);
SKID_MBN.grp_sel(_gr,KTRD,'SELEKCJA',,,,,3,,,,,'maximized');
SKID_MBN.win_sel(_gr);
exec('set_fml','!ctr_pco_zstf');
_model:=ZM.SKID_MBN;
SKID_MBN.cntx_psh();
exec('set_fml','#field',UD_POM,'SUMA',"exec('bs_saldo_ktrd','!ctr_pco_zstf')");
SKID_MBN.select();
SKID_MBN.cntx_pop();
KTRWP.cntx_pop();
ZM.SKID_MBN:=_model;
exec('opiszwym','control');
VAR_DEL.delete('zakl_war','dla_srod')


\showktrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Funkcja do odswiezenia okienka z podzialami
::  OLD: \showktrd/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
KTRD.index('KTRD');
{? dla_srod=0
|| {? ZM.SKID_MBN
   || KTRD.prefix(null,ZM.SKID_MBN)
   || KTRD.prefix(null,null)
   ?}
|| {? ZM.SKID_MBN & SRSR.get()
   || KTRD.prefix(SRSR.ref(),ZM.SKID_MBN)
   || KTRD.prefix(null,null)
   ?}
?};
exec('saldo_ktrd','!ctr_pco_zstf')


\showktrd1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Funkcja do odswiezenia okienka z podzialami
::   WE: _a - 0 - podziały domyślne
::            1 - podziały dla środka
::  OLD: \showktrd/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
dla_srod:=_a;
exec('showktrd','!ctr_pco_zstf');
1


\saldo_ktrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Ustawia wartosc zmiennej SALDO.PLN w oparciu o dane tabeli KTRD
::  OLD: \saldo_ktrd/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POM.SUMA:=0;
KTRD.cntx_psh();
{? KTRD.first() || {! |? UD_POM.SUMA+=KTRD.UDZIAL; KTRD.next() !} ?};
KTRD.cntx_pop();
UD_POM.POZOST:=100-UD_POM.SUMA


\bs_saldo_ktrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Przed wyświetleniem pola UD_POM.SUM
::----------------------------------------------------------------------------------------------------------------------
{? f_usr_changed()
|| UD_POM.SUMA:=0;
   KTRD.cntx_psh();
   {? KTRD.f_active()
   || {? KTRD.f_first() || {! |? UD_POM.SUMA+=KTRD.UDZIAL; KTRD.f_next() !} ?}
   || {? KTRD.first() || {! |? UD_POM.SUMA+=KTRD.UDZIAL; KTRD.next() !} ?}
   ?};
   KTRD.cntx_pop();
   UD_POM.POZOST:=100-UD_POM.SUMA
?};
''


\rekpktrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MGO  [2006]
:: OPIS: Funkcja ustala wartosc zmiennej pomocniczej - jednostke budzetowa,
::       obiekt kosztów oraz pozycje budzetowa na rekord przed w KTRP
::  OLD: \rekpktrd/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POM.JORG:={? KTRD.JORG<>null || KTRD.JORG().SYMBOL  || '' ?};
UD_POM.OKOSZ:={? KTRD.OKOSZ<>null || KTRD.OKOSZ().SYMBOL || '' ?};
UD_POM.PBUD:={? KTRD.PBUD<>null || KTRD.PBUD().SYMBOL  || '' ?};
UD_POM.WYM4:={? KTRD.WYM4<>null || KTRD.WYM4().SYMBOL  || '' ?};
UD_POM.WYM5:={? KTRD.WYM5<>null || KTRD.WYM5().SYMBOL  || '' ?};
K_ES.E1:={? KTRD.FPBUD<>''
         || KTRD.FPBUD
         || {? K_ES.O='T'
            || KTRD.PBUD().OPIS
            || KTRD.PBUD().SYMBOL
            ?}
         ?};
K_ES.E2:={? KTRD.FJORG<>''
         || KTRD.FJORG
         || {? K_ES.O='T'
            || KTRD.JORG().OPIS
            || KTRD.JORG().SYMBOL
            ?}
         ?};
K_ES.E3:={? KTRD.FOKOSZ<>''
         || KTRD.FOKOSZ
         || {? K_ES.O='T'
            || KTRD.OKOSZ().OPIS
            || KTRD.OKOSZ().SYMBOL
            ?}
         ?};
K_ES.E4:={? KTRD.FWYM4<>''
         || KTRD.FWYM4
         || {? K_ES.O='T'
            || KTRD.WYM4().OPIS
            || KTRD.WYM4().SYMBOL
            ?}
         ?};
K_ES.E5:={? KTRD.FWYM5<>''
         || KTRD.FWYM5
         || {? K_ES.O='T'
            || KTRD.WYM5().OPIS
            || KTRD.WYM5().SYMBOL
            ?}
         ?};
''


\ktrdnew
::----------------------------------------------------------------------------------------------------------------------
:: UTW: MGO  [2006]
:: OPIS: Dodanie nowego podzialu domyslnego do schematu kontrolingowego
::  OLD: \ktrdnew/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBP.index('LP'); SKID_MBP.prefix(ZM.SKID_MBN);
{? SKID_MBP.first()
|| ZM.SJORG:=ZM.SPBUD:=ZM.SOKOSZ:=ZM.SWYM4:=ZM.SWYM5:='T';
   UD_POM.JORG:=UD_POM.OKOSZ:=UD_POM.PBUD:=UD_POM.WYM4:=UD_POM.WYM5:='';
   VAR.EDITABLE:=1;
   KTRD.win_edit('REDAKCJA');
   KTRD.blank();
   {? dla_srod=1 || KTRD.SRSR:=SRSR.ref() ?};
   {? KTRD.edit("exec('ktrd_spr','!ctr_pco_zstf',0,KTRD.UDZIAL)") & KTRD.add()
   || exec('saldo_ktrd','!ctr_pco_zstf')
   ?}
|| FUN.emsg('Nie zdefiniowano wymiarów dla wybranego modelu.\nDodawanie podziałów nie jest możliwe.'@)
?}


\ktrdpop
::----------------------------------------------------------------------------------------------------------------------
:: UTW: MGO  [2006]
:: OPIS: Poprawianie podzialu domyslnego do schematu kontrolingowego
::  OLD: \ktrdpop/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
ZM.SJORG:={? KTRD.JORG<>null || 'T'|| 'N' ?};
ZM.SPBUD:={? KTRD.PBUD<>null || 'T'|| 'N' ?};
ZM.SOKOSZ:={? KTRD.OKOSZ<>null || 'T'|| 'N' ?};
ZM.SWYM4:={? KTRD.WYM4<>null || 'T'|| 'N' ?};
ZM.SWYM5:={? KTRD.WYM5<>null || 'T'|| 'N' ?};
KTRD.win_edit('REDAKCJA');
ile_bylo:=KTRD.UDZIAL;
{? KTRD.edit("exec('ktrd_spr','!ctr_pco_zstf',ile_bylo,KTRD.UDZIAL)") & KTRD.put()
|| exec('saldo_ktrd','!ctr_pco_zstf')
?};
VAR_DEL.delete('ile_bylo')


\ktrdusu
::----------------------------------------------------------------------------------------------------------------------
:: UTW: AMK [2011]
:: OPIS: Usuwanie podzialu domyslnego do schematu kontrolingowego
::  OLD: \ktrdusu/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć podział controllingowy?'@) & KTRD.del()
|| exec('saldo_ktrd','!ctr_pco_zstf')
?}


\ktrd_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW:  AMK  [2011]
:: OPIS: Tabela KTRD - sprawdzanie po okienku
::   WE: _a - udzial - tyle bylo
::       _b - udzial - tyle jest
::  OLD: \ktrd_spr/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? K_ES.W1<>''
|| _zwrot:={? ZM.SPBUD='T'
           || __CHK.record2(UD_POM,'PBUD',K_ES.W1+' - stała wartość')
           || __CHK.record2(KTRD,'FPBUD',K_ES.W1+' - formuła')
           ?}
?};
{? _zwrot='' & K_ES.W2<>''
|| _zwrot:={? ZM.SJORG='T'
           || __CHK.record2(UD_POM,'JORG',K_ES.W2+' - stała wartość')
           || __CHK.record2(KTRD,'FJORG',K_ES.W2+' - formuła')
           ?}
?};
{? _zwrot='' & K_ES.W3<>''
|| _zwrot:={? ZM.SOKOSZ='T'
           || __CHK.record2(UD_POM,'OKOSZ',K_ES.W3+' - stała wartość')
           || __CHK.record2(KTRD,'FOKOSZ',K_ES.W3+' - formuła')
           ?}
?};
{? _zwrot='' & K_ES.W4<>''
|| _zwrot:={? ZM.SWYM4='T'
           || __CHK.record2(UD_POM,'WYM4',K_ES.W4+' - stała wartość')
           || __CHK.record2(KTRD,'FWYM4',K_ES.W4+' - formuła')
           ?}
?};
{? _zwrot='' & K_ES.W5<>''
|| _zwrot:={? ZM.SWYM5='T'
           || __CHK.record2(UD_POM,'WYM5',K_ES.W5+' - stała wartość')
           || __CHK.record2(KTRD,'FWYM5',K_ES.W5+' - formuła')
           ?}
?};
{? _zwrot=''
|| {? exec('ktrd_pr','!ctr_pco_zstf',_a,_b)=0 || 'UDZIAL' || '' ?}
|| _zwrot
?}


\ktrd_pr
::----------------------------------------------------------------------------------------------------------------------
:: UTW:  MGO  [2006]
:: OPIS: Sprawdzenie sumy procentow  w definicji podzialu
::  OLD: \ktrd_pr/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_procent:=0;
KTRD.cntx_psh();
{? KTRD.first
|| {! |?
      _procent+=KTRD.UDZIAL; KTRD.next()
   !}
?};
_procent:=((_procent-_a)+_b)$2;
{? _procent=0
|| FUN.emsg('Nie wprowadzono wartości procentowej.'@); _wynik:=0
|? _procent>0 & _procent<=100
|| _wynik:=1
|| _wynik:=FUN.ask('Zdefiniowano łącznie w pozycjach podziały na %1%% kwoty.\nKontynuować?'@[$_procent])
?};
KTRD.cntx_pop();
_wynik


\set_enabled
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia dostępność pól
::----------------------------------------------------------------------------------------------------------------------
SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(ZM.SKID_MBN);
{! _nr:=1..5
|! _jest:=SKID_MBP.find_key(_nr);
   _pole:={? _nr=1 || 'PBUD'
          |? _nr=2 || 'JORG'
          |? _nr=3 || 'OKOSZ'
          |? _nr=4 || 'WYM4'
          |? _nr=5 || 'WYM5'
          || ''
          ?};
   KTRD.efld_opt('REDAKCJA','enable='+$_jest,K_ES,'W'+$_nr);
   KTRD.efld_opt('REDAKCJA','enable='+$_jest,KTRD,_pole);
   _zm:=($('ZM.S'+_pole))();
   KTRD.efld_opt('REDAKCJA','enable='+$(_jest & _zm='T'),UD_POM,_pole);
   KTRD.efld_opt('REDAKCJA','enable='+$(_jest & _zm<>'T'),KTRD,'F'+_pole)
!};
SKID_MBP.cntx_pop();
~~


\upd_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia dostepnosc pol po redakcji
::   WE: _a - nr pola
::----------------------------------------------------------------------------------------------------------------------
_nr:=_a;
SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(ZM.SKID_MBN);
_jest:=SKID_MBP.find_key(_nr);
_pole:={? _nr=1 || 'PBUD'
       |? _nr=2 || 'JORG'
       |? _nr=3 || 'OKOSZ'
       |? _nr=4 || 'WYM4'
       |? _nr=5 || 'WYM5'
       || ''
       ?};
_zm:=($('ZM.S'+_pole))();
KTRD.efld_opt('REDAKCJA','enable='+$(_jest & _zm='T'),UD_POM,_pole);
KTRD.efld_opt('REDAKCJA','mark='+$(_jest & _zm='T'),UD_POM,_pole);
KTRD.efld_opt('REDAKCJA','enable='+$(_jest & _zm<>'T'),KTRD,'F'+_pole);
KTRD.efld_opt('REDAKCJA','mark='+$(_jest & _zm<>'T'),KTRD,'F'+_pole);
SKID_MBP.cntx_pop()


\w1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Przed wyswietleniem pola zmiennej ZM.SPBUD
::   WY: 0 / ''
::  OLD: \w1/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
exec('upd_fld','!ctr_pco_zstf',1);
{? K_ES.W1<>''
|| ''
|| 0
?}


\w2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Przed wyswietleniem pola zmiennej ZM.SJORG
::   WY: 0 / ''
::  OLD: \w2/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
exec('upd_fld','!ctr_pco_zstf',2);
{? K_ES.W2<>''
|| ''
|| 0
?}


\w3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Przed wyswietleniem pola zmiennej ZM.SOKOSZ
::   WY: 0 / ''
::  OLD: \w3/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
exec('upd_fld','!ctr_pco_zstf',3);
{? K_ES.W3<>''
|| ''
|| 0
?}


\w4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Przed wyswietleniem pola zmiennej ZM.SWYM4
::   WY: 0 / ''
::  OLD: \w4/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
exec('upd_fld','!ctr_pco_zstf',4);
{? K_ES.W4<>''
|| ''
|| 0
?}


\w5
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Przed wyswietleniem pola zmiennej ZM.SWYM5
::   WY: 0 / ''
::  OLD: \w5/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
exec('upd_fld','!ctr_pco_zstf',5);
{? K_ES.W5<>''
|| ''
|| 0
?}


\aesjorg
::----------------------------------------------------------------------------------------------------------------------
:: UTW: MGO  [2006]
:: OPIS: Formula po redakcji pola ZM.SJORG
::  OLD: \aesjorg/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZM.KDEF='D' & ZM.SJORG='T'
|| KTRD.FJORG:=''
|? ZM.KDEF='D' & ZM.SJORG='N'
|| KTRD.JORG:=null; UD_POM.JORG:=''
|? ZM.KDEF='S' & ZM.SJORG='T'
|| KTRP.FJORG:=''
|? ZM.KDEF='S' & ZM.SJORG='N'
|| KTRP.JORG:=null; UD_POM.JORG:=''
?};
exec('upd_fld','!ctr_pco_zstf',2);
1


\aeswym4
::----------------------------------------------------------------------------------------------------------------------
:: UTW: MGO  [2008]
:: OPIS: Formula po redakcji pola ZM.SWYM4
::  OLD: \aeswym4/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZM.KDEF='D' & ZM.SWYM4='T'
|| KTRD.FWYM4:=''
|? ZM.KDEF='D' & ZM.SWYM4='N'
|| KTRD.WYM4:=null; UD_POM.WYM4:=''
|? ZM.KDEF='S' & ZM.SWYM4='T'
|| KTRP.FWYM4:=''
|? ZM.KDEF='S' & ZM.SWYM4='N'
|| KTRP.WYM4:=null; UD_POM.WYM4:=''
?};
exec('upd_fld','!ctr_pco_zstf',4);
1


\aeswym5
::----------------------------------------------------------------------------------------------------------------------
:: UTW: MGO  [2008]
:: OPIS: Formula po redakcji pola ZM.SWYM5
::  OLD: \aeswym5/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZM.KDEF='D' & ZM.SWYM5='T'
|| KTRD.FWYM5:=''
|? ZM.KDEF='D' & ZM.SWYM5='N'
|| KTRD.WYM5:=null; UD_POM.WYM5:=''
|? ZM.KDEF='S' & ZM.SWYM5='T'
|| KTRP.FWYM5:=''
|? ZM.KDEF='S' & ZM.SWYM5='N'
|| KTRP.WYM5:=null; UD_POM.WYM5:=''
?};
exec('upd_fld','!ctr_pco_zstf',5);
1


\aespbud
::----------------------------------------------------------------------------------------------------------------------
:: UTW: MGO  [2006]
:: OPIS: Formula po redakcji pola ZM.SJORG
::  OLD: \aespbud/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZM.KDEF='D' & ZM.SPBUD='T'
|| KTRD.FPBUD:=''
|? ZM.KDEF='D' & ZM.SPBUD='N'
|| KTRD.PBUD:=null; UD_POM.PBUD:=''
|? ZM.KDEF='S' & ZM.SPBUD='T'
|| KTRP.FPBUD:=''
|? ZM.KDEF='S' & ZM.SPBUD='N'
|| KTRP.PBUD:=null; UD_POM.PBUD:=''
?};
exec('upd_fld','!ctr_pco_zstf',1);
1


\aesokosz
::----------------------------------------------------------------------------------------------------------------------
:: UTW: MGO  [2006]
:: OPIS: Formula po redakcji pola ZM.SJORG
::  OLD: \aesokosz/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZM.KDEF='D' & ZM.SOKOSZ='T'
|| KTRD.FOKOSZ:=''
|? ZM.KDEF='D' & ZM.SOKOSZ='N'
|| KTRD.OKOSZ:=null; UD_POM.OKOSZ:=''
|? ZM.KDEF='S' & ZM.SOKOSZ='T'
|| KTRP.FOKOSZ:=''; VAR.EDITABLE:=1
|? ZM.KDEF='S' & ZM.SOKOSZ='N'
|| KTRP.OKOSZ:=null; UD_POM.OKOSZ:=''
?};
exec('upd_fld','!ctr_pco_zstf',3);
1


\set_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia zmienna UD_POM
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','#field',UD_POM,'PBUD',
   "  UD_POM.PBUD:=KTRD.PBUD().SYMBOL; ~~",
   "exec('be_dim','control',ZM.SKID_MBN,1)",
   "exec('f3_dim','control',ZM.SKID_MBN,1)",
   "  {? fld()='' || KTRD.PBUD:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ZM.SKID_MBN,1);
      {? _ref
      || KTRD.PBUD:=_ref;
         fld(KTRD.PBUD().SYMBOL);
         1
      ?}
   "
);
exec('set_fml','#field',UD_POM,'JORG',
   "UD_POM.JORG:=KTRD.JORG().SYMBOL; ~~",
   "exec('be_dim','control',ZM.SKID_MBN,2)",
   "exec('f3_dim','control',ZM.SKID_MBN,2)",
   "  {? fld()='' || KTRD.JORG:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ZM.SKID_MBN,2);
      {? _ref
      || KTRD.JORG:=_ref;
         fld(KTRD.JORG().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'OKOSZ',
   "UD_POM.OKOSZ:=KTRD.OKOSZ().SYMBOL; ~~",
   "exec('be_dim','control',ZM.SKID_MBN,3)",
   "exec('f3_dim','control',ZM.SKID_MBN,3)",
   "  {? fld()='' || KTRD.OKOSZ:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ZM.SKID_MBN,3);
      {? _ref
      || KTRD.OKOSZ:=_ref;
         fld(KTRD.OKOSZ().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'WYM4',
   "UD_POM.WYM4:=KTRD.WYM4().SYMBOL; ~~",
   "exec('be_dim','control',ZM.SKID_MBN,4)",
   "exec('f3_dim','control',ZM.SKID_MBN,4)",
   "  {? fld()='' || KTRD.WYM4:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ZM.SKID_MBN,4);
      {? _ref
      || KTRD.WYM4:=_ref;
         fld(KTRD.WYM4().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'WYM5',
   "UD_POM.WYM5:=KTRD.WYM5().SYMBOL; ~~",
   "exec('be_dim','control',ZM.SKID_MBN,5)",
   "exec('f3_dim','control',ZM.SKID_MBN,5)",
   "  {? fld()='' || KTRD.WYM5:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ZM.SKID_MBN,5);
      {? _ref
      || KTRD.WYM5:=_ref;
         fld(KTRD.WYM5().SYMBOL)
      ?}
   "
)


\procent
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MGO  [2006]
:: OPIS: Funkcja sprawdza czy wartosc w redagowanym poluzawiera sie w przedziale od 0 do 100
::  OLD: \procent/tech_es.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=fld(fld()$2);
{? _a>0 & _a<=100
|| 1
|| FUN.emsg('Wpisana wartość musi być większa od 0 i mniejsza lub równa od 100.'@); 0
?}


\befjorg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcja pola KTRD.FJORG
::  OLD: \befjorg/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
ZM.SJORG='N' & K_ES.W2<>''


\befokosz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcja pola KTRD.FOKOSZ
::  OLD: \befokosz/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
ZM.SOKOSZ='N' & K_ES.W3<>''


\befpbud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcja pola KTRD.FPBUD
::  OLD: \befpbud/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
ZM.SPBUD='N' & K_ES.W1<>''


\befswym4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcja pola FWYM4 (KTRD, KTRP)
::  OLD: \befswym4/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
ZM.SWYM4='N' & K_ES.W4<>''


\befswym5
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Przed redakcja pola FWYM5 (KTRD, KTRP)
::  OLD: \befswym5/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
ZM.SWYM5='N' & K_ES.W5<>''


\ktrd_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Definicje podziałów dla środków trwałych
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.KIND='T'
|| dla_srod:=1;
   SKID_MBN.index('EWID'); SKID_MBN.prefix('T','O');
   _mo:=exec('mod_win','control',103);
   _gr:=SKID_MBN.grp_make('Definicje podziałów controllingowych dla środka %1'@[SRSR.NRI],,'skidmbnst1',,,,,'normal');
   SKID_MBN.grp_sel(_gr,,_mo,,"
      ZM.SKID_MBN:=SKID_MBN.ref();
      exec('opiszwym','control');
      exec('set_enabled','!ctr_pco_zstf');
      exec('showktrd','!ctr_pco_zstf');
      grp_disp(KTRD,'SELEKCJA',,1)
   ",,,,,,,,'maximized_with_title');
   KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W1],K_ES,'E1');
   KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W2],K_ES,'E2');
   KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W3],K_ES,'E3');
   KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W4],K_ES,'E4');
   KTRD.fld_opt('SELEKCJA','col_name="%1"'[K_ES.W5],K_ES,'E5');
   SKID_MBN.grp_splt(_gr,,'horizontal','def_dla_sr',10);
   SKID_MBN.grp_sel(_gr,KTRD,'SELEKCJA',,,,,,,,,,'maximized');
   SKID_MBN.win_sel(_gr);
   exec('set_fml','!ctr_pco_zstf');
   SKID_MBN.cntx_psh();
   SKID_MBN.select();
   SKID_MBN.cntx_pop();
   VAR_DEL.delete('dla_srod')
|| FUN.info('Definicji podziałów nie można wprowadzać dla środków będących elementami.'@)
?}

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:36 857450afc53c72ff33a675f735c1232830dfdb53adf25b22e471f5846e441f0ba3b4e8f54def87e4abfd91b49c105834b478f987a9eb12c2c71116760f7ddb4ce1a53538ca245316bebcaa2843a17517de10c6c91d71a0140dc6e29e66daaccb982c3788a06ae2cdbafe94864e7adfd652410484af2b8e10b860f1fee3e481b6

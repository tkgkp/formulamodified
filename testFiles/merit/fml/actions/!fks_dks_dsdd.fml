:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dsdd.fml
:: Utworzony: 09.05.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DSDD - Dekretacja dokumentu wartościowego środka
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dekretacja dokumentu wartościowego środka - główna formuła czynności FKS_DKS_DSDD.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
::# parses=exec('parses_srdo','fst')
:: PARAMETRY WE:
::# kind=WE, symbol=SRDO, type=_SRDO, name=Wskazanie na dokument wartościowy, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na dokument księgowy, required=T

_mp:=params_get().mp;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_akcja:=-_mp.akcja();

{? var_pres('SRDO',_we) & type_of(_we.SRDO)=type_of(null) & _we.SRDO<>null
|| exec('fl_decl','dekret_aut');
   exec('dek_decl','dekret_aut');
   exec('sd_decl','dekret_aut');
   _mp.descTodo();
   _auto:=_mp.isAutoRun();
   exec('SRD','object');
   SRD.FST:=exec('szuk_b_dom','parses','FST');
   SRD.maski('r');
   {? SRDO.seek(_we.SRDO)
   || OKRO_F.cntx_psh();
      _okr:=exec('szuk_okr','okresy',SRDO.DO);
      {? OKRO_F.seek(_okr) & OKRO_F.ZAM='N'
      || {? _akcja='dekretuj'
         || _ref:=SRDO.ref();
            SRDO.cntx_psh();
            exec('dekretuj','!fks_dks_dsdd');
            SRDO.cntx_pop();
            {? SRDO.seek(_ref) & SRDO.K='T'
            || _wy.DOK:=DOK.ref();
               _mp.save(,_wy);
               _mp.done()
            ?}
         |? _mp.pathTodo() | _mp.isAutoRun()
         || POMOC.OKR:=SSTALE.AO;
            EKSG.SYS:='FST';
            {? _mp.isAutoRun()
            || BtnDek:=1
            || BtnDek:=0;
               _title:='Dokument wartościowy - dekretowanie'@;
               _win:=SRDO.mk_edit(_title,,'_srdo_dekret');
               {? SRDO.TYP().P='S' || SRDO.win_ewin(_win,,'RED_WSDE') || SRDO.win_ewin(_win,,'RED_W_DE') ?};
               _par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Dekretuj'@];
               _btndek:=SRDO.win_ebtn(_win,_par,"BtnDek:=1; 'key:Esc'");
               _par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@];
               _btnan:=SRDO.win_ebtn(_win,_par,"BtnDek:=0; 'key:Esc'");
               SRDO.btn_eopt(_win,_btndek,'tooltip='+'Dekretuj dokument wartościowy'@);
               SRDO.btn_eopt(_win,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
               SRDO.win_edit(_win);
               SRSR.cntx_psh(); SRST.cntx_psh();
               SRDO.SRSR();
               SRST.index('SROD');
               SRST.prefix(SRSR.ref(),SRDO.OKRO_F().ROK,SRDO.OKRO_F);
               {? SRST.first()
               || EDIT_ES.Z_NRI:=SRST.NRI;
                  EDIT_ES.Z_NST:=SRST.NST
               || EDIT_ES.Z_NRI:=SRSR.NRI;
                  EDIT_ES.Z_NST:=SRSR.NST
               ?};
               SRSR.cntx_pop(); SRST.cntx_pop();
               SRDO.display()
            ?};
            {? BtnDek
            || {? exec('initWARL','dok_fks_aut_dok',SSTALE.AR,'FST','EW')
               || exec('init','dok_fks_aut_dok','Informacje o błędach dekretacji dokumentów wartościowych'@);
                  SSTALE.AO();
                  SRDO.cntx_psh();
                  _ref:=SRDO.ref();
                  exec('dekretuj','!fks_dks_dsdd');
                  SRDO.cntx_pop();
                  {? SRDO.seek(_ref) & SRDO.K='T'
                  || _wy.DOK:=DOK.ref();
                     _mp.save(,_wy);
                     _mp.done();
                     {? ~_mp.isAutoRun() || FUN.info('Dokument zadekretowany.'@) ?}
                  || {? ~_mp.isAutoRun() || FUN.info('Dekretowanie dokumentu nie powiodło się.'@) ?}
                  ?}
               ?}
            ?};
            VAR_DEL.delete('BtnDek')
         ?}
      || FUN.info('Okres dekretacji dokumentu wartościowego jest zamknięty w obszarze finanse. \nDekretacja niemożliwa.'@);
         _wy.DOK:=null;
         _mp.save(,_wy);
         _mp.done()
      ?};
      OKRO_F.cntx_pop()
   || {? _akcja<>'dekretuj' || FUN.info('Nie znaleziono dokumentu wartościowego.'@) ?};
      _wy.DOK:=null;
      _mp.save(,_wy);
      _mp.done()
   ?}

|| {? _akcja<>'dekretuj' || FUN.info('Nie znaleziono dokumentu wartościowego.'@) ?};
   _wy.DOK:=null;
   _mp.save(,_wy);
   _mp.done()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zadekretuj dokument wartościowy'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('SRDO',_we)>0 & type_of(_we.SRDO)=type_of(null) & _we.SRDO<>null
|| _ref:=BB.sqlint($_we.SRDO); _nam:=form(($_we.SRDO)-8);
   {? _ref<>0 & _nam<>''
   || SRDO.cntx_psh();
      {? SRDO.seek(_ref,_nam,1)
      || _desc:='Zadekretuj dokument wartościowy: %1.'@@[SRDO.SYMBOL]
      ?};
      SRDO.cntx_pop()
   ?}
?};
_desc


\dekretuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Funkcja dekretująca
::----------------------------------------------------------------------------------------------------------------------
_tam:=exec('get_gr_from_srdo','fst');
TAM.prefix();
TAM.seek(_tam);
:: sprawdzenie czy wszystkie dane sa ustawione dla naglowka zgodnie z kontrola bledow DEK_NAG.SPR
:: i ustawienie odpowiedniego schematu dekretacji DEK_NAG i przepisanie z stalych rejestru/oddzialu
{? SRDO.r_lock(1,1,1)
|| {? exec('spr_war','dok_fks_aut_dok',SSTALE.AR,'FST','EW',$SRDO.TYP)
   || exec('set_fld','dok_fks_aut_dok','N','EW');
      exec('pozycje','dok_fks_aut_dok',SSTALE.AR,'FST','EW',$SRDO.TYP)
   ?};
   SRDO.r_unlock()
?}


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'srdo'>0
         || _record:=exec('FindAndGet','#table',SRDO,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 64e6788d9b42fe38cf099f88dae7f36062a9853e2afb31e9ba98ffad8493a55da116233f513c861ea003d21ec24c7da96b249ef6213bd43a4081443d7c067dfe3a669f18fe350ea4a56f7433c9921452c3c10d684426c2f19b40d8221b2ca0c4ea77b8317653c9a9c6a9b7bda00d9ba5be820e0756ec48192788419c1002ce88

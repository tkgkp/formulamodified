:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ltr_kar_dkar.fml
:: Utworzony: 23.01.2019
:: Autor: MW
::======================================================================================================================
:: Zawartość: Formuły czynności LTR_KAR_DKAR
::            Rejestracja karty drogowej
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Czynność LTR_KAR_DKAR - formuła główna
::   WE: _a - [obj_new] - parametry wejsciowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# kind=WY,   symbol=SAMK,  type=_SAMK,  name=Karta drogowa,     required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_area:=_mp.pathArea('LTR_KAR');
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo() | (_mp.pathArea() & ~_area);

{? _mp.pathProc()
:: Sprawdzenie parametrów pracy dla czynności startowej
|| {? ST.AR=0 || FUN.info('Ustaw parametry pracy.'@); _wyn:=0; return() ?}
?};

exec('init','ltr');

{? _mp.pathTodo()
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('SAMK',_out)=type_of(null()) & _out.SAMK
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego SAMK.')
   ?}
?};

:: Sprawdzenie uprawnień
params_set('in',_in,'user',OPERATOR.USER,'mp',_mp);
{? exec('permissions','!ltr_kar_dkar')=0
|| _mp.error('Brak uprawnień do uruchomienia czynności.'@);
   return()
?};

_mp.trigRef('SAMK',,1,,exec('kind_out','#b_port'),'SAMK');

{? _akcja='Dołącz' | _mp.pathProc() | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
||
:: Parametr 'akcja' wykorzystywany w formułach przycisków ?
   params_set('out',_out,'mp',_mp,'akcja',_akcja);
   {? _mp.pathProc() | _akcja='START_TODO'
   || _poj:= exec('pojazd_wybierz','karty_drog');
      {? _poj<>null
      || exec('add_kart_pojazd','karty_drog',_poj)
      ?}
   || exec('add_kart','karty_drog')
   ?};
   _krefs:=_mp.getRefs();
   {? var_pres('[1]',_krefs)<>type_of(~~)
:: Dodano kartę drogową
   || SAMK.cntx_psh();
      SAMK.prefix();
      {? SAMK.seek(_krefs[1])
::    Zapisanie _out.SAMK jeśli wcześniej nie zakończono rejestracji
      ||  {? type_of(_out)>type_of(~~) & var_pres('SAMK',_out)>=type_of(~~)
          || _out.SAMK:=SAMK.ref();
             _mp.save(,_out)
          ?};
          _mp.done()
::    Brak karty drogowej w tabeli mimo zapisanego parametru wyjściowego SAMK oznacza wycofanie czynności
      || _mp.cancel()
      ?};
      SAMK.cntx_pop()
:: Wycofanie czynności bo nie dodano karty drogowej
   || _mp.cancel()
   ?};
:: Ustawienie się na dodanej karcie w widoku obszaru
   {? _mp.pathArea()
   || _outa:=_mp.load(exec('kind_out','#b_port'));
      {? type_of(_outa)>type_of(~~) & var_pres('SAMK',_outa)>type_of(~~) || SAMK.seek(_outa.SAMK) ?}
   ?}

|? _akcja='Popraw' | _todo
|| {? type_of(_out)>type_of(~~) & var_pres('SAMK',_out)>type_of(~~)
:: Uruchomione w procesie
   || SAMK.cntx_psh();
      SAMK.prefix();
      {? SAMK.seek(_out.SAMK)
      ||
         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         {? exec('ed_kart','karty_drog')
         || _mp.done()
         ?};
         _mp.descTodo()
      ?};
      SAMK.cntx_pop()
   |? _todo
:: Uruchomione zadanie z listy todo i brak _out.SAMK wtedy zadanie wycofujemy
   || _mp.cancel()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || params_set('out',_out,'mp',_mp,'akcja',_akcja);
      exec('ed_kart','karty_drog');
      _mp.done();
      _mp.descTodo()
   ?}

|? _akcja='Usuń'
|| {? type_of(_out)>type_of(~~) & var_pres('SAMK',_out)>type_of(~~)
:: Uruchomione w procesie
   || _samk:=null();
      SAMK.cntx_psh();
      _rec:=exec('rec_after_del','#table',SAMK);
      SAMK.prefix();
      {? SAMK.seek(_out.SAMK)
      ||
         _wyn:=exec('del_kart','karty_drog');
         {? ~SAMK.seek(_out.SAMK)
::       Wycofuję instancję jeśli nie znaleziono karty drogowej
         || _mp.cancel()
         ?};
         _samk:=SAMK.ref()
      ?};
      SAMK.cntx_pop();
      {? _wyn=1 & _rec<>null() || SAMK.seek(_rec) ?};
::    Ustawienie się na kolejnej karcie w widoku obszaru
      {? _area || {? _samk || SAMK.seek(_samk) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || exec('del_kart','karty_drog')
   ?}
|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? type_of(_out)>type_of(~~) & var_pres('SAMK',_out)>type_of(~~)
|| 'Zakończ rejestrację karty drogowej: %1'@@[exec('record','#to_string',_out.SAMK)]
|| 'Zarejestruj kartę drogową'@@
?}


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;
_wyn:=1;
_wyn

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:14 0d332d35c90a2d3334964cc363ff15c5e68129989049c6aaee5a4263f34138081c7ee0a99e4465e6af3ca264e224ab278292b7b64b1c9cba6b54488e177fcac27cf3e7e90627d88bcd8c0ae132d96468c1f36fa581ec73731a0661f28afbcb5b4052480924682aa1c77b8b1e0cb1d499277825587c6f98dba99badc255740d4c

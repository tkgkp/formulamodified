:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pzpt.fml
:: Utworzony: 20.12.2021
:: Autor: DG
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PZPT - Typy zwolnionego przychodu.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Typy zwolnionego przychodu - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OS_ZWSLO.cntx_psh();
OS_ZWSLO.win_sel('SLO');
OS_ZWSLO.index('RODZAJ');
OS_ZWSLO.prefix(exec('ref_firma','ustawienia'));
OS_ZWSLO.select();
OS_ZWSLO.cntx_pop();
~~


\os_zwslo_skl_zdr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formula na zmianę znacznika ZDR z tabeli OS_ZWSLO (czy wyliczana skladka zdrowotna)
::   WE:
::   WY:
::  OLD: \os_zwslo_skl_zdr/kali.fml
::  OLD: \os_zwslo_skl_zdr/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
OS_ZWSLO.ZDR:={? OS_ZWSLO.ZDR='T' || 'N' || 'T' ?};
OS_ZWSLO.put()


\os_zwslo_bfw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła Okienko przed dla tabeli OS_ZWPOD, OS_ZWZAL.
::   WE:
::   WY:
::  OLD: \os_zw_bw/kali.fml
::  OLD: \os_zw_bfw/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
win_set('cur_row_pos=-1')


\os_zwslo_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli OS_ZWSLO.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE:  _a  [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::  OLD: \os_zwslo_efld_opt/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(OS_ZWSLO)
|| _TAB:=_b
|? _tryb='1'
|| _TAB:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

_set:=exec('efld_opt','#window',,_we);

{? _fld='' | _fld='ROZLICZ'
|| _val:=OS_ZWSLO.ROZLICZ='T';
   _set(_TAB,_we,,'enable=%1' [$_val],_TAB,'RA_DEF')
?};

1


\os_zwslo_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "Wyświetl" tabeli OS_ZWSLO
::   WE:
::   WY:
::  OLD: \os_zwslo_bw/kali.fml
::----------------------------------------------------------------------------------------------------------------------
exec('os_zwslo_efld_opt','!zws_par_pzpt','*',OS_ZWSLO,OS_ZWSLO.win_edit('?'));
OS_ZWSLO.display()


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 7d5b21d149ecbd29bf93229b95b4e2809e88475a2242a9f1f783e35bc98c4284773c54f45611d5a3790a2140b98eb132707edf0a35b792689dfdf28f7533738837238e3ade295e6146e76e2d94965f3214caa9fca2e1d5e95dc1a6e2dd45bf622b661856e46ade016d84d4c1294cb55de5f4a11d871836c81c2c9a2c02d9d285

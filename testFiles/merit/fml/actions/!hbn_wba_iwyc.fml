:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !hbn_wba_iwyc.fml
:: Utworzony: 11.06.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności HBN_WBA_IWYC - import wyciągów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Pozycje wyciągów - główna formuła czynności HBN_WBA_IWYC
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,HRB,HRP
exec('import_w','!hbn_wba_iwyc')


\create_er
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008b]
:: OPIS: Formula tworzy tabele tymczasowa na bledy importu
::  OLD: \create_er/proc_01.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('ERR_ST');
ERR_ST:=tab_tmp(1,'WYCIAG','STRING[20]','Nr wyciagu',
                 'FILENAME','STRING[255]','Nazwa pliku',
                 'KOD_BANK','STRING[8]','Kod banku',
                 'NAZ_BANK','STRING[80]','Nazwa banku',
                 'SPEC','STRING[50]','Specyfikacja',
                 'LOC','STRING[1]','Plik lokalny?',
                 'PRZYCZ', 'STRING[255]', 'Przyczyna');
_ste_ed:=ERR_ST.mk_edit(,1, '_ste_ed');
ERR_ST.win_edit(_ste_ed);
_ste_wer:=ERR_ST.mk_sel('Lista błędów:'@,,0,'_ste_wer',,,,,'U');
ERR_ST.win_fld(_ste_wer,ERR_ST,'WYCIAG',,,20,,1,'Nr wyciagu'@);
ERR_ST.win_fld(_ste_wer,ERR_ST,'FILENAME',,,40,,1,'Plik z wyciągiem'@);
ERR_ST.win_fld(_ste_wer,ERR_ST,'PRZYCZ',,,40,,1,'Opis problemu'@);
ERR_ST.win_sel(_ste_wer);
ERR_ST.win_act(_ste_wer,,'Formuła','Druku&j'@@,,'Wydruk listy błędów'@,
               "exec('rep_exec','#b_report','HBN_WBA_IWYC','hbn_err_stfi',,,,,,,,'W')",,1,,,,'J')


\gr_imp_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Grupowy import wyciagow - formula przed
::  OLD: \gr_imp_b/proc_01.fml
::----------------------------------------------------------------------------------------------------------------------
STAT.blank();
exec('create_er','!hbn_wba_iwyc');
STAT.RAZEM:=PWB.sel_size()


\gr_imp_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Grupowy import wyciagow - formula po
::  OLD: \gr_imp_a/proc_01.fml
::----------------------------------------------------------------------------------------------------------------------
_info:='Przetworzono plików: '+$STAT.RAZEM+'\n'
      +'W tym:\n już przetworzonych: '+$STAT.PRZETW+'\n'
      +'z niewskazaną specyfikacją: '+$STAT.SPECUZUP+'\n'
      +'z nieodnalezioną specyfikacją: '+$STAT.SPECBRAK+'\n'
      +'nieprzetworzonych z powodu problemów z plikiem: '+$STAT.PLIK;
{? ERR_ST.first()
|| _ask:=_info+'\n\n'+'Wyświetlić listę napotkanych błędów?';
   {? FUN.ask(_ask)
   || ERR_ST.select()
   ?}
|| FUN.info(_info)
?};
PWB.f_rfresh();
PWN.f_rfresh();
PW.f_rfresh();
VAR_DEL.delete('ERR_ST')


\import_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Import wyciagow bankowych
::  OLD: \import_w/proc_01.fml
::----------------------------------------------------------------------------------------------------------------------
{? PWB.ODD=null | ~exec('usr_fjks','b_perm',PWB.ODD().OD,OPERATOR.USER)
|| {? PWB.sel_size()=0
   || FUN.info('Brak uprawnień do importu wyciągu z danej jednostki księgowej.'@);
      return()
   || exec('err_st_add','homebank','Brak uprawnień do importu wyciągu z danej jednostki księgowej.'@);
      STAT.PRZETW+=1;
      return()
   ?}
?};
{? PWB.Z='T'
|| {? PWB.sel_size()=0
   || FUN.emsg('Wskazany plik (%1) był już przetwarzany!'@[PWB.PLIK().NAME]);
      return()
   || exec('err_st_add','homebank','Plik był już przetwarzany.'@);
      STAT.PRZETW+=1;
      return()
   ?}
|? PWB.Z='B'
|| {? PWB.sel_size()=0
   || FUN.emsg('Dla wskazanego pliku (%1)\nnie znaleziono banku lub specyfikacji importu!'@[PWB.PLIK().NAME]);
      return()
   || exec('err_st_add','homebank','Dla pliku nie znaleziono banku lub specyfikacji importu.'@);
      STAT.SPECBRAK+=1;
      return()
   ?}
?};
{? PWB.SPEC='' & PWB.MULTI='T'
|| {? PWB.sel_size()=0
   || FUN.emsg('Dla wskazanego pliku należy wybrać właściwą specyfikację importu,\n'
               'gdyż dla tego banku w systemie istnieje więcej niż 1 aktywna specyfikacja!'@);
      return()
   || exec('err_st_add','homebank','Dla pliku nie wybrano specyfikacji importu.'@);
      STAT.SPECUZUP+=1;
      return()
   ?}
?};
PW.cntx_psh();
PWN.cntx_psh();
PWN.prefix();
PW.prefix();
VAR_DEL.delete('TMP_RACH');
TMP_RACH:=tab_tmp(1,'RACH','STRING[50]','Rachunek');
_bgk:=PWB.NAZ_BANK*'Bank Gospodarstwa Krajowego';
{? -(PWB.FILENAME+3)='xml' & ~_bgk
|| _rach:=exec('get_rach_xml','hbn',PWB.PLIK,0)
|| _rach:=exec('get_rach','hbn',PWB.PLIK,0)
?};
{? _rach<>''
|| {? 2+_rach='PL' || _rach:=2-_rach ?};
   {? +_rach=10
   ||
:: wyszukiwanie po fragmencie rachunku, by obsłużyć wyciągi z Deutsche Bank Polska
      SKID_RBK.cntx_psh();
      SKID_RBK.index('BANK');
      SKID_RBK.prefix(REF.INFO);
      {? SKID_RBK.first()
      || _find:=0;
         {! |?
            {? (SKID_RBK.BANK().BANORG().ID=191 | SKID_RBK.BANK().BANORG().NAZ='Deutsche Bank Polska SA')
                & SKID_RBK.N+10=_rach
            || _find:=1; _pwn_bl:=SKID_RBK.BANK; _rach:=SKID_RBK.N
            ?};
            _find=0 & SKID_RBK.next()
         !}
      ?};
      SKID_RBK.cntx_pop()
   || RB.getrrban(_rach,REF.INFO,0,null,1);
      _pwn_bl:=SKID_RBK.BANK
   ?}
|| _pwn_bl:=null
?};
{? TMP_RACH.first()
|| _exit:=0;
   {! |?
      SKID_RBK.cntx_psh();
      SKID_RBK.index('WEKTOR');
      {? 2+TMP_RACH.RACH='PL' || TMP_RACH.RACH:=2-TMP_RACH.RACH ?};
      SKID_RBK.prefix(REF.INFO,TMP_RACH.RACH);
      {? ~SKID_RBK.first()
      || _find:=0;
::       jeżeli nie znaleziono, wyszukiwanie po fragmencie rachunku, by obsłużyć wyciągi z Deutsche Bank Polska
         {? +TMP_RACH.RACH=10
         || SKID_RBK.cntx_psh();
            SKID_RBK.prefix(REF.INFO);
            {? SKID_RBK.first()
            || {! |?
                  {? (SKID_RBK.BANK().BANORG().ID=191 | SKID_RBK.BANK().BANORG().NAZ='Deutsche Bank Polska SA')
                     & SKID_RBK.N+10=TMP_RACH.RACH
                  || _find:=1
                  ?};
                  _find=0 & SKID_RBK.next()
               !}
            ?};
            SKID_RBK.cntx_pop()
         ?};
         {? _find=0 & ~FUN.ask('Plik zawiera pozycje wyciągu z nieznanego rachunku licencjobiorcy: %1.\n'
                               'W przypadku kontynuowania importu pozycje te nie zostaną zaimportowane. Kontynuować?'@[TMP_RACH.RACH])
         || _exit:=1
         ?}
      ?};
      SKID_RBK.cntx_pop();
      _exit=0 & TMP_RACH.next()
   !};
   {? _exit=1
   || PW.cntx_pop();
      PWN.cntx_pop();
      return()
   ?}
?};
_vrref:=_vcont:=hbfeof:=pbcn:=pbcw:=0;pbex:=2;hbfnline:=1;_vref:=null;pbnw:='';pwimp:=0;
{? PWB.sel_size()=0 || exec('create_er','!hbn_wba_iwyc') ?};
HBPKI.index('HBPKITP');
HBPKI.prefix(1,'I');
_spec:=0;
{? HBPKI.first()
|| {! |?
      {? HBPKI.TPK=PWB.SPEC & HBPKI.KDB().NUMER=PWB.KOD_BANK
      || _spec:=1; _loop:=1;
         {? TAB_SPEC.first()
         || {! |?
               {? TAB_SPEC.NUMER=HBPKI.KDB().NUMER || _loop:=0 ?};
               _loop & TAB_SPEC.next()
            !}
         ?}
      || _spec:=0
      ?};
      _spec=0 & HBPKI.next()
   !}
?};
{? _spec
|| {? HBPKI.RODZ='I' & HBPKI.KOM<>'W'
   || HBP.index('HBNAZWA');
      HBP.prefix(HBPKI.ref());
      {? ~HBP.first
      || _spec:=0;
         {? PWB.sel_size()=0
         || FUN.emsg('Brak specyfikacji lub pozycji specyfikacji\n do importu wyciągów (%1)'@[PWB.SPEC])
         || exec('err_st_add','homebank','Brak specyfikacji lub pozycji specyfikacji do importu wyciągu.'@);
            STAT.SPECBRAK+=1
         ?}
      ?}
   ?}
?};
_pw_rbl:=PW.RBL;
_pw_del:=0;
_pwb_ref:=PWB.ref();
{? _spec
|| VAR_DEL.delete('hbfile');
   hbfile:=fopen(PWB.PLIK,'r',0,0,1);
   {? hbfile.is_open()
   ||
      {? HBPKI.KOM<>'W'
      || HBP.first();
         _vrref:=_vcont:=hbfeof:=0;
         {! |?
            {? _vrref<>null & _vrref=HBP.ref() & PW.RBL<>_pw_rbl
            || SKID_RBK.cntx_psh();
               SKID_RBK.index('WEKTOR');
               SKID_RBK.prefix(REF.INFO,PW.RBL);
               {? ~SKID_RBK.first()
               || exec('err_st_add','homebank','Pominięto pozycje wyciągu z nieznanego rachunku licencjobiorcy: %1.'@[PW.RBL],PW.NRW)
               ?};
               SKID_RBK.cntx_pop()
            ?};
            {? ~_vrref & HBP.KP().KD='r' || _vrref:=HBP.ref() ?};
            {? HBP.KP().KD='' & HBP.DL='x' || exec('typwyn','hb') || 1 ?};
            {? ~pbex & pwimp=1
            || exec('err_st_add','homebank','Wyciąg bankowy był już wcześniej importowany.'@,PW.NRW);
               {? PWB.sel_size()<>0
               || STAT.IMPORT+=1
               ?}
            ?};
            {? ~pbex & pwimp=1
            || pbex:=2;pwimp=0;HBP.first()
            || HBP.cntx_psh();
               {? ~HBP.next() || _vcont:=1 || _vcont:=0 ?};
               HBP.cntx_pop();
               {? _vcont || HBP.seek(_vrref) & pbex || ~hbfeof & HBP.next() & pbex ?}
            ?}
         !}
      ?};
      fclose(hbfile);
:: weryfikacja i usuwanie pozycji zwiazanych z nieistniejacym w systemie rachunkiem licencjobiorcy
      {? TMP_RACH.first()
      || _pw_tmp:=PW.index('?');
         _pw_ndx:=PW.ndx_tmp('',1,'RBL',,0,'PLIK',,0);
         PW.index(_pw_ndx);
         {! |?
               SKID_RBK.cntx_psh();
               SKID_RBK.index('WEKTOR');
               {? 2+TMP_RACH.RACH='PL' || TMP_RACH.RACH:=2-TMP_RACH.RACH ?};
               SKID_RBK.prefix(REF.INFO,TMP_RACH.RACH);
               {? ~SKID_RBK.first()
               || PW.cntx_psh();
                  PW.prefix(TMP_RACH.RACH,_pwb_ref);
                  {? PW.first()
                  || {! |?
                        _pw_del+=1;
                        PW.del()
                     !}
                  ?};
                  PW.cntx_pop()
               ?};
               SKID_RBK.cntx_pop();
               TMP_RACH.next()
         !};
         PW.index(_pw_tmp);
         PW.ndx_drop(_pw_ndx)
      ?};
:: Uzupełnianie nagłówków wyciągów, w przypadku braku uzupełnienia podczas przetwarzania specyfikacji,
:: Każda specyfikacja importu wyciągu powinna znajdować lub zakładać nagłówek wyciągu (PWN) podczas importu,
:: po przetworzeniu sekcji zawierającej numer wyciągu, tak jak w standardowo dostarczanych specyfikacjach
:: poprzez wywołanie formuły \set_pwn_bufor z hbn.fml.
:: Fragment poniżej uzupełnia ewentualne braki, na podstawie danych pozycji wyciągu, co nie będzie skuteczne
:: w przypadku obsługi płatności masowych!
      {? pbcn
      || PWN.index('PWN');
         PWN.prefix();
         PW.index('PWN');
         PW.prefix(null);
         {? PW.first()
         || {! |?
               PWN.prefix(PW.BL,PW.RBL,PW.NRW);
               {? ~PWN.first()
               || PWN.prefix();
                  PWN.blank();
                  PWN.BL:=PW.BL;
                  PWN.NRW:=PW.NRW;
                  PWN.ODD:=PW.ODD;
                  PWN.PLIK:=PW.PLIK;
:: jeżeli PW.RBL zawiera fragment rachunku to używany jest wcześniej znaleziony rachunek pełny,
:: (by obsłużyć wyciągi z Deutsche Bank Polska)
                  PWN.RBL:=exec('rb_nosp','#string',{? +PW.RBL>10 || PW.RBL || _rach ?});
                  PWN.STATUS:='N';
                  PWN.add()
               ?};
               PW.PWN:=PWN.ref();
               PW.cntx_psh();
               PW.prefix();
               PW.put();
               PW.cntx_pop();
               PW.first()
            !}
         ?}
      ?};
:: Korekta pozycji wyciągów, w przypadku braku informacji dot. Split Payment zmiana znacznika PW.SP na 'N'
      {? pbcn
      || PW.index('PWN');
         PW.prefix(PWN.ref());
         {? PW.first()
         || PW.trig_off('*','*');
            {! |?
               {? PW.SP_V=0 & PW.SP_KHNIP='' & PW.SP_NRFAK='' & PW.SP='T'
               || PW.SP:='N'; PW.put()
               ?};
               PW.next()
            !};
            PW.trig_on('*','*')
         ?}
       ?};
:: koniec weryfikacji
      {? pbex<>0 & pbcn>0
      || PWB.cntx_psh();
         PWB.Z:='T';
         PWB.put();
         PWB.cntx_pop();
         {? PWB.sel_size()=0
         || PWB.f_rfresh();
            PWN.f_rfresh();
            PW.index('PWN');
            PW.prefix(PWN.ref())
         ?}
      ?};
:: podsumowania
      {? pbcn
      || {? PWB.sel_size()=0
         || {? pbcw<=1
            || FUN.info('Liczba przetworzonych z wyciągu %1 pozycji: %2.'@[PW.NRW,$pbcn])
            || FUN.info('Liczba przetworzonych wyciągów: %1\nprzetworzonych pozycji na wyciągach: %2\n'
                        'pominiętych pozycji: %3.'@[$pbcw,$pbcn,$_pw_del])
            ?}
         ?}
      ?}
   || exec('err_st_add','homebank','Błąd otwarcia pliku.'@);
      {? PWB.sel_size()<>0
      || STAT.PLIK+=1
      ?}
   ?};
   {? PWB.sel_size()=0
   || {? ERR_ST.first()
      || ERR_ST.select();
         VAR_DEL.delete('ERR_ST')
      ?}
   ?}
?};
PW.cntx_pop();
PWN.cntx_pop();
VAR_DEL.delete('TMP_RACH','tmp_wal')

:Sign Version 2.0 jowisz:1045 2023/12/07 13:16:52 8d9cc9b98fb0124b9bf35300348ad2f380fa420474f483e56956f357afffe54c39cbc4184f7c4aa8dc673a28072602538c28871c718940c202d55cf7be294eb1404f0040e90d7a009300017e91a7e74967789b3ca9e36edb04611bd70ca18d427974151b2257a27f66946b725862b0bff4f3a7514e9cd0b451845f975711c269

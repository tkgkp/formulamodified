:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_asgr.fml [17.00]
:: Utworzony: 15.03.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_ASGR - Grupa kapitałowa
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Grupa kapitałowa - formula główna
::  OLD: \struktura_grupy/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
FIRMA.win_sel('WER'); FIRMA.win_dict('WER');
W_STR.actions('WER','O','Z',0);
W_STR.win_fml('WER',,'OPIS',,'ICON_BEFORE',"
   {? W_STR.FIRMA().TYP='K' | FIRMA.TYP='C'
   || 'xwin16.png:182'
   |? FIRMA.TYP='W'
   || 'xwin16.png:170'
   || ''
   ?}
");
W_STR.dnd_sel('WER',,'records.W_STR',"exec('dnd_w_str','!zws_par_asgr')");
_grp:=W_SCH.grp_make('Definicja struktury grupy kapitałowej'@);
W_SCH.grp_sel(_grp,,'WER',,"exec('awr_w_sch','!zws_par_asgr')",,,,,,,,'maximized_with_title');
W_SCH.grp_splt(_grp,,'vertical','left');
W_SCH.grp_sel(_grp,W_STR,'WER',,,,,,,,,,'maximized_with_title');
W_SCH.win_sel(_grp);
__F_ref:='K';
W_SCH.index('NAZ'); W_SCH.prefix();
W_SCH.select();
&__F_ref


\dnd_w_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Drag and drop lub akcja w strukturze grupy kapitalowej
::   WE: [_a] - tryb 0(domyślnie)-d&d, 1-akcja
::  OLD: \dnd_w_str/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of(0) || _a || 0 ?};
{? ~_tryb
|| _dest:=dnd_info('dest_record');
   _sel:=dnd_info('dropped_records')
|| {? var_press('dest_record')=-1
   || dest_record:=_dest:=-1;
      W_STR.cntx_psh();
      _win_acr:=W_STR.mk_sel('Wybierz element docelowy','P',,,,,,1);
      W_STR.win_fld(_win_acr,,'OPIS',,,43,,,);
      W_STR.win_fld(_win_acr,,'FIRMA','SYMBOL',,,,,);
      W_STR.win_fld(_win_acr,,'FIRMA','OPIS',,35,,,);
      W_STR.win_act(_win_acr,0,'Formuła','&Wybierz'@@,,'Wybór docelowej pozycji'@,,"sel_exit",1,,,,'W');
      W_STR.win_sel(_win_acr);
      {? W_STR.select() || dest_record:=_dest:=W_STR.ref() ?};
      W_STR.cntx_pop()
   || _dest:=dest_record
   ?};
   {? W_STR.sel_size()<=1 || {? var_press('dest_record')>-1 || &dest_record ?} ?};
   _sel:=tab_tmp(1,'REF','INTEGER','');
   _sel.blank();
   _sel.REF:=#W_STR.ref();
   _sel.add(1)
?};
{? _sel.first()
|| {? _dest<>null & W_STR.seek(_dest)
   || {? W_STR.FIRMA().TYP='K' | FIRMA.TYP='C'
      || {!
         |? {? W_STR.seek(_sel.REF,)
            || W_STR.TREE:=_dest;
               W_STR.put()
            ?};
            _sel.next()
         !}
      || FUN.info('Docelowym elementem nie może być firma fizyczna ani do wyłączeń.'@)
      ?}
   ?}
?}


\awr_w_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Po odswiezeniu okna WER tabeli W_SCH okna grupowego WER_GR
::  OLD: \awr_w_sch/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
W_STR.index('TREE');
{? W_SCH.get()
|| W_STR.prefix(W_SCH.ref());
   W_STR.actions('WER',,,1)
|| W_STR.prefix(null);
   W_STR.actions('WER','D:D',,1)
?};
grp_disp(W_STR,'WER')


\ar_w_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord po oknie WER tabeli W_SCH
::----------------------------------------------------------------------------------------------------------------------
_r:=__CHK.record(W_SCH,,'NAZ');
_ref:=W_SCH.ref();
W_SCH.cntx_psh();
W_SCH.index('NAZ'); W_SCH.prefix(W_SCH.NAZ);
{? W_SCH.first() & (-menu_txt()<>'popraw' | W_SCH.ref()<>_ref)
|| FUN.info('Istnieje struktura grupy o nazwie: %1.'@[W_SCH.NAZ]);
   _r:='NAZ'
?};
W_SCH.cntx_pop();
_r


\bl_w_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Wartosc poczatkowa pola W_STR.W_SCH
::  OLD: \bl_w_sch/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
W_SCH.ref()


\be_w_str_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Przed redakcja pola W_STR.ODD
::  OLD: \be_w_str_odd/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? W_STR.FIRMA<>null
|| W_STR.FIRMA();
   1
?}


\ba_w_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Przed dolacz W_STR
::   WE: _a - czy dolaczenie na glownym poziomie drzewa
::  OLD: \ba_w_str/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=0 & (W_STR.FIRMA().TYP='S' | FIRMA.TYP='W')
|| FUN.info('Nie można dołączyć firmy do firmy fizycznej.'@);
   return()
?};
W_STR.win_edit('RED');
W_STR.blank();
{? W_STR.edit("exec('ar_w_str','!zws_par_asgr')")
|| {? _a
   || W_STR.TREE:=0
   || W_STR.TREE:=W_STR.ref()
   ?};
   {? W_STR.add() & W_STR.FIRMA().TYP='W'
   || W_STR.cntx_psh();
      W_STR.index('TREE'); W_STR.prefix(W_STR.W_SCH,W_STR.TREE);
      {? W_STR.first()
      || {!
         |?
            W_STR.next()
         !}
      ?};
      W_STR.cntx_pop()
   ?}
?}


\ar_w_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Rekord po W_STR
::  OLD: \ar_w_str/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
_r:=__CHK.record(W_STR,,'OPIS','FIRMA','WSP');
{? _r=''
|| _rek:=W_STR.ref();
   W_STR.cntx_psh();
   W_STR.index('FIRMA'); W_STR.prefix(W_SCH.ref());
   {? W_STR.find_key(W_STR.FIRMA,W_STR.ODD) & (-menu_txt()<>'popraw' | _rek<>W_STR.ref())
   || {? W_STR.ODD
      || FUN.info('Firma \'%1\' (%2) jest już dodana do grupy.'@[W_STR.FIRMA().SYMBOL,W_STR.ODD().OD])
      || FUN.info('Firma \'%1\' jest już dodana do grupy.'@[W_STR.FIRMA().SYMBOL])
      ?};
      _r:='FIRMA'
   ?};
   W_STR.cntx_pop();
   {? _r=''
   || {? W_STR.WSP<0 | W_STR.WSP>100
      || FUN.info('Współczynnik powinien pochodzić z zakresu 0 - 100.'@);
         _r:='WSP'
      ?}
   ?}
?};
_r


\add_wylaczenia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie firmy wylaczeniowej
::  OLD: \add_wylaczenia/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
REF.win_edit('F_WYL');
UNPAR.P1_BE:=UNPAR.P2_BE:=UNPAR.P1_AE:=UNPAR.P2_AE:=UNPAR.P1_BV:=UNPAR.P2_BV:='';
UNPAR.P1:=UNPAR.P2:=1;
{? REF.edit() & (UNPAR.P1 | UNPAR.P2)
|| exec('add_wyl','!zws_par_asgr')
?}


\add_wyl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie firmy wylaczeniowej
::  OLD: \add_wyl/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
W_STR.cntx_psh(); W_STR.index('TREE'); W_STR.prefix(W_SCH.ref(),W_STR.ref());
{? W_STR.first()
|| {!
   |? exec('add_wyl','!zws_par_asgr');
      W_STR.next()
   !}
|| exec('add_wyl1','!zws_par_asgr')
?};
W_STR.cntx_pop()


\add_wyl1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie firmy wylaczeniowej
::  OLD: \add_wyl1/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
W_STR.cntx_psh();
{? W_STR.FIRMA().TYP='S'
|| W_STR.index('FIRMA'); W_STR.prefix(W_SCH.ref());
   FIRMA.index('SYMBOL2'); FIRMA.prefix('W',W_STR.FIRMA().SYMBOL+'W',);
   {? ~FIRMA.first()
   || FIRMA.TYP:='W';
      FIRMA.SYMBOL:=FIRMA.SYMBOL+'W';
      FIRMA.OPIS:='Wyłączenia - '+FIRMA.OPIS;
      FIRMA.add()
   ?};
   _firma:=FIRMA.ref();
   _nad:=W_STR.TREE;
   {? UNPAR.P1 & ~W_STR.find_key(FIRMA.ref())
   || W_STR.FIRMA:=FIRMA.ref();
      W_STR.OPIS:='Wyłączenia - '+W_STR.OPIS;
      W_STR.TREE:=W_STR.TREE;
      W_STR.W_SCH:=W_SCH.ref();
      W_STR.add()
   ?};
   {? UNPAR.P2
   || W_STR.index('TREE'); W_STR.prefix(W_SCH.ref(),_nad);
      {? W_STR.first()
      || ODD.index('ODDZIALY'); ODD.prefix(_firma);
         {!
         |? {? W_STR.FIRMA().TYP='S' & ~ODD.find_key('FIRMA'+FIRMA.SYMBOL,)
            || ODD.FIRMA:=_firma;
               ODD.OD:='FIRMA'+FIRMA.SYMBOL;
               ODD.N:='Wyłączenia z firmą '+FIRMA.SYMBOL;
               {? ODD.add() || exec('add_rej','konsolidacja',_firma) ?}
            ?};
            W_STR.next()
         !}
      ?}
   ?}
?};
W_STR.cntx_pop()


\zr_w_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Akcja zwin/rozwin okna WER tabeli W_STR
::  OLD: \zr_w_str/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
exec('zwrw_all','#tree','W_STR','TREE','WER',"W_SCH.ref()")


\set_firma_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Ustawia wartosc wzorca dla pola FIRMA.SYMBOL
::   WE: _a - 1-ustawia 0-usuwa
::  OLD: \set_firma_wz/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| Wz_firma:='Axx#'
|| VAR_DEL.delete('Wz_firma')
?};
1


\bd_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Przed usun okna WER tabeli FIRMA
::  OLD: \bd_firma/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? FIRMA.TYP<>'K' & FIRMA.TYP<>'W'
|| FUN.info('Nie można usunąć firmy fizycznej (spółki).'@); 0
|? FIRMA.count()>0
|| FUN.info('Firma używana w systemie.'@); 0
|| 1
?}


\ba_firma_wyl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie firmy wylaczeniowej
::  OLD: \ba_firma_wyl/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? FIRMA.sel_size()
|| 1
|| FIRMA.cntx_psh();
   FIRMA.index('SYMBOL'); FIRMA.prefix(FIRMA.SYMBOL+'W',);
   _jest:=FIRMA.first();
   FIRMA.cntx_pop();
   {? _jest
   || FUN.info('Istnieje firma do wyłączeń dla firmy \'%1\'.'@[FIRMA.OPIS])
   |? FIRMA.TYP<>'S'
   || FUN.info('Firma do wyłączeń może powstać\ndla firmy istniejącej fizycznie.'@)
   |? FUN.ask('Czy dodać firmę do wyłączeń dla firmy \'%1\'?'@[FIRMA.OPIS])
   || FIRMA.SYMBOL:=FIRMA.SYMBOL+'W';
      FIRMA.OPIS:='Do wyłączeń '+FIRMA.OPIS;
      FIRMA.TYP:='W';
      FIRMA.add()
   ?}
?}


\firma_sprzedaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Ustawia okres sprzedazy firmy
::  OLD: \firma_sprzedaz/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
FIRMA.cntx_psh();
FIRMA.win_edit('SPRZEDAZ');
UNPAR.PRF1_BV:=UNPAR.PRF1_BE:='';
UNPAR.PRF1_AE:=$"exec('firma_sprzedaz_ae','!zws_par_asgr')";
UNPAR.POKR_BE:='UNPAR.PRF1<>null';
UNPAR.PRF1:=FIRMA.ROK_SPRZ;
UNPAR.POKR:=FIRMA.OKR_SPRZ;
exec('firma_sprzedaz_ae','!zws_par_asgr');
_ok:=FIRMA.edit("
   {? UNPAR.PRF1=null & UNPAR.POKR=null
   || 1
   || __CHK.record2(UNPAR,
         'PRF1','Rok',
         'POKR','Okres'
      )
   ?}
");
_nar:=FIRMA.NAR_SPRZ;
FIRMA.cntx_pop();
{? _ok
|| FIRMA.ROK_SPRZ:=UNPAR.PRF1;
   FIRMA.OKR_SPRZ:=UNPAR.POKR;
   FIRMA.NAR_SPRZ:=_nar;
   {? FIRMA.put() & UNPAR.POKR
   || OKRO_F.cntx_psh(); OKRO_F.prefix();
      KH_POW.cntx_psh(); KH_POW.index('FIRMA'); KH_POW.prefix(FIRMA.ref());
      {? KH_POW.first()
      || _znal:=0;
         {! |?
            KH_POW.cntx_psh(); KH_POW.index('DISP'); KH_POW.prefix(KH_POW.KH);
            {? ~KH_POW.next() & UNPAR.POKR().KON<>date(0,0,0) & OKRO_F.KON>=KH_POW.DATAOD
            || _znal:=1
            ?};
            KH_POW.cntx_pop();
            ~_znal & KH_POW.next()
         !};
         {? _znal & FUN.ask('Zamknąć zapisy w powiązanych firmach kontrahenta?'@)
         || KH_POW.index('FIRMA'); KH_POW.prefix(FIRMA.ref());
            {? KH_POW.first()
            || {! |?
                  KH_POW.cntx_psh(); KH_POW.index('DISP'); KH_POW.prefix(KH_POW.KH);
                  {? ~KH_POW.next() & UNPAR.POKR().KON<>date(0,0,0) & OKRO_F.KON>=KH_POW.DATAOD
                  || KH_POW.DATADO:=OKRO_F.KON; KH_POW.put()
                  ?};
                  KH_POW.cntx_pop();
                  KH_POW.next()
               !}
            ?}
         ?}
      ?};
      KH_POW.cntx_pop(); OKRO_F.cntx_pop()
   ?}
?}


\firma_sprzedaz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redakcji roku sprzedaży firmy
::----------------------------------------------------------------------------------------------------------------------
{? UNPAR.POKR().ROK<>UNPAR.PRF1 || UNPAR.POKR:=null ?};
FIRMA.efld_opt('SPRZEDAZ','mark='+$(UNPAR.PRF1<>null),UNPAR,'PRF1');
FIRMA.efld_opt('SPRZEDAZ','mark='+$(UNPAR.PRF1<>null),UNPAR,'POKR');
FIRMA.efld_opt('SPRZEDAZ','enable='+$(UNPAR.PRF1<>null),UNPAR,'POKR');
1


\ar_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord po okna WER tabeli FIRMA
::----------------------------------------------------------------------------------------------------------------------
_r:=__CHK.record(FIRMA,,'SYMBOL','OPIS');
{? _r=''
|| _ref:=FIRMA.ref();
   FIRMA.cntx_psh();
   FIRMA.index('SYMBOL'); FIRMA.prefix(FIRMA.SYMBOL,);
   {? FIRMA.first() & (-menu_txt()<>'popraw' | _ref<>FIRMA.ref())
   || FUN.info('Istnieje firma o symbolu \'%1\'.'@[FIRMA.SYMBOL]);
      _r:='SYMBOL'
   ?};
   FIRMA.cntx_pop()
?};
{? _r=''
|| _ref:=FIRMA.ref();
   FIRMA.cntx_psh();
   FIRMA.index('OPIS'); FIRMA.prefix(FIRMA.OPIS,);
   {? FIRMA.first() & (-menu_txt()<>'popraw' | _ref<>FIRMA.ref())
   || FUN.info('Istnieje firma z opisem \'%1\'.'@[FIRMA.OPIS]);
      _r:='SYMBOL'
   ?};
   FIRMA.cntx_pop()
?};
_r

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 7f7b96bce0cf93251a4a822e234240bbd52dcd9320a04396bbba4a9115a8b879a730cc9fa213967629b95d2d3099dd4184daf877b51c7cfe62856f1417e130a6038234fa7a8cdb8f5e3d0e2a008b32e43e8ad1329e74b8749e19273c86d8e942ee4ca1d00c65f3237fbdbde4d37c81fad96692824a5ff3073ae19b0d49f33dd7

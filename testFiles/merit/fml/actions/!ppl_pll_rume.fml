:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_pll_rume.fml
:: Utworzony: 08.08.2019
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PPL_PLL_RUME - Rej. podstawy ekw. za urlop
::            Czynność jest potrzebna wyłącznie ze względu na uprawnienia.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Rej. podstawy ekw. za urlop - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
~~


\h_um_ekw_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Sprawdza wypełnienie wymaganych pól. Wykorzystywana w wyzwalaczach przed dołącz i popraw.
::   WE: _a [INTEGER] - Tryb modyfikacji danych
::          0 - dołączanie (domyślnie)
::          1 - poprawianie
::   WY: wynik testu poprawności danych zależny od kontekstu wywołania:
::       - jako formuła "rekord po"
::          - akronim niewypełnionego pola
::          - akronim pola o błędnej wartości
::          - 0 w przypadku powielenia unikalnego klucza
::       - jako formuła sprawdzająca dla wyzwalaczy "dołącz przed" i "popraw przed"
::          - 1 wiersz poprawny
::          - 0 niewypełnione wymagane pole, błędna wartość pola, powielony unikalny klucz
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_mode:={? var_pres('_a')=type_of(0) || _a<>0 ?};

__CHK.validate(H_UM_EKW,
   "_a.field(_b,'H_UM')",
   $("_a.table(_b,"+$_mode+",,'R','M')"),
   "{? _b.R<1900 || _a.err_incorrect(H_UM_EKW,'R'); 'R' || 1 ?}",
   "{? _b.M<1 | _b.M>12 || _a.err_incorrect(H_UM_EKW,'M'); 'M' || 1 ?}",
   "{? _b.NORMA<0 || _a.err_incorrect(H_UM_EKW,'NORMA'); 'NORMA' || 1 ?}",
   "{? _b.CZAS<0 || _a.err_incorrect(H_UM_EKW,'CZAS'); 'CZAS' || 1 ?}"
)


\h_um_ekw_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wykorzystywana w wyzwalaczach przed dołączeniem i poprawieniem wiersza tabeli H_UM_EKW.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
H_UM_EKW.ZMIANA:={? H_UM_EKW.KWW<>H_UM_EKW.KW || 'T' || 'N' ?};
1


\h_um_ekw_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza przed dołączeniem wiersza tabeli H_UM_EKW.
::   WE:
::   WY: zgodne ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
exec('h_um_ekw_modb','!ppl_pll_rume');
exec('h_um_ekw_chk','!ppl_pll_rume',0)


\h_um_ekw_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza po dołączeniu wiersza tabeli H_UM_EKW.
::   WE: zgodne ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('koryguj','!ppl_pll_rume',H_UM_EKW.H_UM,H_UM_EKW.K,H_UM_EKW.R,H_UM_EKW.M,H_UM_EKW.KW-H_UM_EKW.KWW);
~~


\h_um_ekw_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza przed poprawieniem wiersza tabeli H_UM_EKW.
::   WE:
::   WY: zgodne ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
H_UM_EKW.KWW:=0;
H_UM_ESK.cntx_psh();
H_UM_ESK.index('UNIQUE');
H_UM_ESK.prefix(H_UM_EKW.H_UM,H_UM_EKW.K,H_UM_EKW.R,H_UM_EKW.M);
_skor:=exec('korekta_sekcja','!ppl_pll_rume',H_UM_EKW.K);
_loop:=H_UM_ESK.first();
{!
|? _loop
|! {? H_UM_ESK.S<>_skor
   || H_UM_EKW.KWW+=H_UM_ESK.KW
   ?};
   _loop:=H_UM_ESK.next()
!};
H_UM_ESK.cntx_pop();
exec('h_um_ekw_modb','!ppl_pll_rume');
exec('h_um_ekw_chk','!ppl_pll_rume',1)


\h_um_ekw_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza po poprawieniu wiersza tabeli H_UM_EKW.
::   WE: zgodne ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_koryguj:=
   {? H_UM_EKW.H_UM<>bfld('H_UM') | H_UM_EKW.R<>bfld('R') | H_UM_EKW.M<>bfld('M')
   || exec('koryguj','!ppl_pll_rume',bfld('H_UM'),bfld('K'),bfld('R'),bfld('M'),0)
   || H_UM_EKW.KW<>bfld('KW')
   ?};
{? _koryguj<>0
|| exec('koryguj','!ppl_pll_rume',H_UM_EKW.H_UM,H_UM_EKW.K,H_UM_EKW.R,H_UM_EKW.M,H_UM_EKW.KW-H_UM_EKW.KWW)
?};
~~


\h_um_ekw_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza przed usunięciem wiersza tabeli H_UM_EKW.
::   WE:
::   WY: zgodne ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
exec('koryguj','!ppl_pll_rume',H_UM_EKW.H_UM,H_UM_EKW.K,H_UM_EKW.R,H_UM_EKW.M,0)


\h_um_esk_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Sprawdza wypełnienie wymaganych pól. Wykorzystywana w wyzwalaczach przed dołącz i popraw.
::   WE: _a [INTEGER] - Tryb modyfikacji danych
::          0 - dołączanie (domyślnie)
::          1 - poprawianie
::   WY: wynik testu poprawności danych zależny od kontekstu wywołania:
::       - jako formuła "rekord po"
::          - akronim niewypełnionego pola
::          - akronim pola o błędnej wartości
::          - 0 w przypadku powielenia unikalnego klucza
::       - jako formuła sprawdzająca dla wyzwalaczy "dołącz przed" i "popraw przed"
::          - 1 wiersz poprawny
::          - 0 niewypełnione wymagane pole, błędna wartość pola, powielony unikalny klucz
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_mode:={? var_pres('_a')=type_of(0) || _a<>0 ?};

{? _mode=0
|| __CHK.validate(H_UM_ESK,"_a.table(_b,0,,'KW')")
|| 1
?}


\h_um_esk_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza przed dołączeniem wiersza tabeli H_UM_ESK.
::   WE:
::   WY: zgodne ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
exec('h_um_esk_chk','!ppl_pll_rume',0)


\h_um_esk_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza po dołączeniu wiersza tabeli H_UM_ESK.
::   WE: zgodne ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('h_um_esk_moda','!ppl_pll_rume',H_UM_ESK.KW);
~~


\h_um_esk_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza po poprawieniu wiersza tabeli H_UM_ESK.
::   WE: zgodne ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? H_UM_ESK.KW=0
|| H_UM_ESK.del()
|| exec('h_um_esk_moda','!ppl_pll_rume',H_UM_ESK.KW)
?};
~~


\h_um_esk_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła wyzwalacza po usunięciu wiersza tabeli H_UM_ESK.
::   WE: zgodne ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('h_um_esk_moda','!ppl_pll_rume',0,1)


\h_um_esk_moda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.42]
:: OPIS: Formuła aktualizująca kwotę miesięczną po zmianach w składnikach. Wykorzystywana w wyzwalaczach po:
::       dołączeniu, poprawieniu, usunięciu wiersza tabeli H_UM_ESK.
::   WE: [_a] [NUMBER] - Aktualna kwota [domyślnie: 0].
::       [_b] [NUMBER] - Wywołanie z wyzwalacza po usunięciu (brak rekordu tabeli H_UM_ESK) [domyślnie: 0].
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_kw:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_del:=var_pres('_b')=type_of(0) & _b;

{? _del
|| _h_um:=bfld('H_UM');
   _kl:=bfld('K');
   _rok:=bfld('R');
   _mc:=bfld('M')
|| _h_um:=H_UM_ESK.H_UM;
   _kl:=H_UM_ESK.K;
   _rok:=H_UM_ESK.R;
   _mc:=H_UM_ESK.M
?};

H_UM_EKW.cntx_psh();
H_UM_EKW.index('UNIQUE');
H_UM_EKW.prefix(_h_um,_kl,_rok,_mc);
{? H_UM_EKW.first()
|| {? H_UM_EKW.KW<>H_UM_EKW.KWW+_kw
   || H_UM_EKW.KW:=H_UM_EKW.KWW+_kw;
      H_UM_EKW.put()
   ?}
?};
H_UM_EKW.cntx_pop();
~~


\grp_params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Zwraca tablicę parametrów dla akcji okienka grupowego.
::   WE:
::   WY: wskazanie na czteroelementową tablicę wektorów [ndx,pos] lub ~~
::----------------------------------------------------------------------------------------------------------------------
_ctx:=~~;
_par:=params_get();
_objt:=type_of(obj_new(1));
{? type_of(_par)=_objt & var_pres('par',_par)=_objt & obj_len(_par.par)=8
|| return(_par.par)
?};
_ctx


\grp_context
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Zwraca tablicę parametrów dla akcji okienka grupowego.
::   WE: _a TABLE - alias tabeli H_UM_EKW lub H_UM_ESK
::       _b STRING - typ zapisów: Z mienne, S stałe, O okresowe
::   WY: wskazanie wektora [ndx,pos] lub ~~
::----------------------------------------------------------------------------------------------------------------------
_ctx:=~~;
_par:=params_exec('grp_params','!ppl_pll_rume');
{? type_of(_par)<>type_of(~~)
|| _ii:=
      {? _a=H_UM_EKW
      || {? _b='Z' || 1
         |? _b='N' || 3
         |? _b='S' || 5
         |? _b='O' || 7
         ?}
      |? _a=H_UM_ESK
      || {? _b='Z' || 2
         |? _b='N' || 4
         |? _b='S' || 6
         |? _b='O' || 8
         ?}
      ?};
   {? _ii<>0
   || _ctx:=_par[_ii]
   ?}
?};
_ctx


\grp_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed wypełnieniem okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: kwoty i wartości do wyliczeń (wymiar zatrudnienia, współczynnik do ekwiwalentu)
POLA_GRP.fld_fml('REAL_1','BEFORE_DISPLAY',"exec('grp_stat_bd','!ppl_pll_rume','WYM')");
POLA_GRP.fld_fml('REAL_2','BEFORE_DISPLAY',"exec('grp_stat_bd','!ppl_pll_rume','WSP')");
POLA_GRP.fld_fml('REAL_3','BEFORE_DISPLAY',"exec('grp_licz_bd','!ppl_pll_rume')");
POLA_GRP.fld_fml('REAL_4','BEFORE_DISPLAY',"exec('grp_lskw_bd','!ppl_pll_rume')");

_ctx:=params_exec('grp_params','!ppl_pll_rume');
{? type_of(_ctx)<>type_of(~~)
|| {! _ii:=1..obj_len(_ctx)
   |! _ctx[_ii].ndx:='UNIQUE';
      _ctx[_ii].pos:=null
   !}
?};

H_UM_ESK.cntx_psh();
H_UM_ESK.index('UNIQUE');
grp_edisp(H_UM_EKW,'NFO');

1


\grp_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przy zamykaniu okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
H_UM_ESK.cntx_pop();

:: przywróć funkcjonalność pól
H_UM_EKW.fld_fml('K','BLANK',"*");
H_UM_ESK.fld_fml('K','BLANK',"*");

POLA_GRP.fld_fml('REAL_1','BEFORE_DISPLAY',"*");
POLA_GRP.fld_fml('REAL_2','BEFORE_DISPLAY',"*");
POLA_GRP.fld_fml('REAL_3','BEFORE_DISPLAY',"*");
POLA_GRP.fld_fml('REAL_4','BEFORE_DISPLAY',"*");

1


\grp_ekw1_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_EKW w pierwszej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_bs','!ppl_pll_rume',_a,'Z')


\grp_ekw1_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_EKW w pierwszej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_as','!ppl_pll_rume',_a,'Z')


\grp_ekw1_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_EKW w pierwszej zakładce okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_ar','!ppl_pll_rume')


\grp_esk1_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_ESK w pierwszej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_bs','!ppl_pll_rume',_a,'Z')


\grp_esk1_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_ESK w pierwszej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_as','!ppl_pll_rume',_a,'Z')


\grp_esk1_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_ESK w pierwszej zakładce okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_ar','!ppl_pll_rume')


\grp_ekw2_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_EKW w drugiej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_bs','!ppl_pll_rume',_a,'N')


\grp_ekw2_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_EKW w drugiej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_as','!ppl_pll_rume',_a,'N')


\grp_ekw2_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_EKW w drugiej zakładce okienka GRP tabeli H_UM_EKW.
::   WE:
::   WYl
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_ar','!ppl_pll_rume')


\grp_esk2_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_ESK w drugiej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_bs','!ppl_pll_rume',_a,'N')


\grp_esk2_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_ESK w drugiej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_as','!ppl_pll_rume',_a,'N')


\grp_esk2_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_ESK w drugiej zakładce okienka GRP tabeli H_UM_EKW.
::   WE:
::   WYl
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_ar','!ppl_pll_rume')


\grp_ekw3_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_EKW w trzeciej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_bs','!ppl_pll_rume',_a,'S')


\grp_ekw3_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_EKW w trzeciej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_as','!ppl_pll_rume',_a,'S')


\grp_ekw3_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_EKW w trzeciej zakładce okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_ar','!ppl_pll_rume')


\grp_esk3_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_ESK w trzeciej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_bs','!ppl_pll_rume',_a,'S')


\grp_esk3_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_ESK w trzeciej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_as','!ppl_pll_rume',_a,'S')


\grp_esk3_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_ESK w trzeciej zakładce okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_ar','!ppl_pll_rume')


\grp_ekwx_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_EKW w zakładkach okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi dla akcji "przed obsługą"
::       _b STRING - klasa składników: Z zmienne, N nominalne, S stałe
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ctx:=params_exec('grp_context','!ppl_pll_rume',H_UM_EKW,_b);
{? type_of(_ctx)=type_of(~~)
|| return('#disable')
?};

H_UM_EKW.fld_fml('K','BLANK',$('\'%1\''[_b]));

_aid:='';
{? ~exec('o_writable','lista_plac',H_UM.LT,0)
|| _aid:='P:'
?};
H_UM_EKW.actions_grayed(cur_win(1,1),_aid);

{? _a<>0
|| H_UM_EKW.index(_ctx.ndx)
?};
H_UM_EKW.prefix(H_UM.ref(),_b);
{? _a<>0
|| {? H_UM_EKW.seek(_ctx.pos)=0
   || H_UM_EKW.first()
   ?}
?}


\grp_ekwx_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_EKW w zakładkach okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi dla akcji "po obsłudze"
::       _b STRING - klasa składników: Z zmienne, N nominalne, S stałe
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| return()
?};
_ctx:=params_exec('grp_context','!ppl_pll_rume',H_UM_EKW,_b);
{? type_of(_ctx)<>type_of(~~)
|| _ctx.ndx:=H_UM_EKW.index('?');
   _ctx.pos:=H_UM_EKW.ref()
?}


\grp_ekwx_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_EKW w zakładkach okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(H_UM_ESK,'WER',1,1);
~~


\grp_eskx_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_ESK w zakładkach okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::       _b STRING - klasa składników: Z zmienne, N nominalne, S stałe, I informacyjne
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(H_UM_EKW,'WER',1)
|| return('#disable')
?};

_ctx:=params_exec('grp_context','!ppl_pll_rume',H_UM_ESK,_b);
{? type_of(_ctx)=type_of(~~)
|| return('#disable')
?};

H_UM_ESK.fld_fml('K','BLANK',$('\'%1\''[_b]));

_aid:='';
{? ~exec('o_writable','lista_plac',H_UM.LT,0)
|| _aid:='J:J'
?};
H_UM_ESK.actions_grayed(cur_win(1,1),_aid);

{? _a<>0
|| H_UM_ESK.index(_ctx.ndx)
?};
H_UM_ESK.prefix(H_UM_EKW.H_UM,_b,H_UM_EKW.R,H_UM_EKW.M);
{? _a<>0
|| {? H_UM_ESK.seek(_ctx.pos)=0
   || H_UM_ESK.first()
   ?}
?}


\grp_eskx_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_ESK w zakładkach okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi dla akcji "po obsłudze"
::       _b STRING - klasa składników: Z zmienne, N nominalne, S stałe, I informacyjne
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| return()
?};
_ctx:=params_exec('grp_context','!ppl_pll_rume',H_UM_ESK,_b);
{? type_of(_ctx)<>type_of(~~)
|| _ctx.ndx:=H_UM_ESK.index('?');
   _ctx.pos:=H_UM_ESK.ref()
?}


\grp_eskx_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_ESK w zakładkach okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(H_UM_EKW,'WER',,1);
~~


\grp_lskw_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizuje zawartość pola informacyjnego.
::   WE: _a STRING - symbol sekcji
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_kw:=0;

{? H_UM.LT<>''
|| LS.cntx_psh();
   LS.use(-H_UM.LT);
   LS.index('PRACNRRU');
   LS.prefix(H_UM.P,440);
   _loop:=LS.first();
   {!
   |? _loop
   |! _kw+=LS.KW;
      _loop:=LS.next()
   !};
   LS.cntx_pop()
?};

fld(_kw);
1


\grp_stat_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizuje zawartość pola informacyjnego.
::   WE: _a STRING - symbol sekcji
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | _a=''
|| return(0)
?};

_RS:=sql('select sum(KW) as KW from H_UM_ESK where H_UM=:_a and K=\'I\' and S=\':_b\'',H_UM.ref(),_a);
{? type_of(_RS)=type_of(SYSLOG) & _RS.first()
|| fld(_RS.KW)
?};

1


\grp_licz_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Aktualizuje zawartość pola informacyjnego.
::   WE: _a STRING - symbol sekcji
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_kw:=exec('ekwiwalent','lista_licz',H_UM.ref(),1);
{? type_of(_kw)=type_of(~~)
|| _kw:=0
?};

fld(_kw);
1


\h_um_ekw_akcja_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po akcjach dołącz/popraw/usuń okienka WER tabeli H_UM_EKW.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab()=H_UM_EKW & cur_win()='GRP'
|| grp_edisp(H_UM_EKW,'NFO')
?};
~~


\h_um_ekw_popraw_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po akcji popraw okienka WER tabeli H_UM_EKW.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('h_um_ekw_akcja_a','!ppl_pll_rume');
~~


\h_um_ekw_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Po edycji wiersza tabeli H_UM_EKW.
::   WE:
::   WY: wynik testu poprawności danych
::----------------------------------------------------------------------------------------------------------------------
exec('h_um_ekw_chk','!ppl_pll_rume',-menu_txt='popraw')


\koryguj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Formuła akcji "Koryguj" okienka WER tabeli H_UM_ESK.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_skor:=exec('korekta_sekcja','!ppl_pll_rume',H_UM_EKW.K);

H_UM_ESK.cntx_psh();
H_UM_ESK.index('UNIQUE');
H_UM_ESK.prefix(H_UM_EKW.H_UM,H_UM_EKW.K,H_UM_EKW.R,H_UM_EKW.M);
{? (_jest:=H_UM_ESK.find_key(_skor,))=0
|| H_UM_ESK.blank();
   H_UM_ESK.K:=H_UM_EKW.K;
   H_UM_ESK.R:=H_UM_EKW.R;
   H_UM_ESK.M:=H_UM_EKW.M;
   H_UM_ESK.S:=_skor;
   H_UM_ESK.SK_NAZWA:=exec('korekta_nazwa','!ppl_pll_rume');
   H_UM_ESK.LT:='ETAT'
?};

H_UM_ESK.win_edit('KOR');
H_UM_ESK.efld_opt('KOR','mark=%1'[$(_jest=0)],,'KW');
{? H_UM_ESK.edit($('exec(\'h_um_esk_chk\',\'!ppl_pll_rume\',%1)'[$_jest]))
|| {? _jest=0
   || H_UM_ESK.add()
   || H_UM_ESK.put()
   ?}
?};

H_UM_ESK.cntx_pop();

exec('h_um_ekw_akcja_a','!ppl_pll_rume');
~~


\koryguj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Zapisuje informacje o korekcie wyliczonej podstawy.
::   WE: _a _H_UM - wskazanie wiersza tabeli H_UM
::       _b STRING - oznaczenie rodzaju zapisu [Z/N/S]
::       _c INTEGER - rok wyliczenia podstawy
::       _d INTEGER - miesiąc wyliczenia podstawy
::       _e REAL - kwota korekty wyliczonej podstawy
::   WY: 0/1 - wynik operacji tworzenia/aktualizacji/usunięcia zapisu
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_h_um:=_a;
_klasa:=_b;
_rok:=_c;
_msc:=_d;
_kw:=_e;

_ret:=0;
_kor:=exec('korekta_sekcja','!ppl_pll_rume',_klasa);

H_UM_ESK.cntx_psh();
H_UM_ESK.index('UNIQUE');
H_UM_ESK.prefix(_h_um,_klasa,_rok,_msc);
{? H_UM_ESK.find_key(_kor,)
|| {? H_UM_ESK.KW<>_kw
   || H_UM_ESK.KW:=_kw;
      _ret:=H_UM_ESK.put()
   || _ret:=1
   ?}
|? _kw=0
|| _ret:=1
|| H_UM_ESK.blank();
   H_UM_ESK.H_UM:=_h_um;
   H_UM_ESK.K:=_klasa;
   H_UM_ESK.R:=_rok;
   H_UM_ESK.M:=_msc;
   H_UM_ESK.S:=_kor;
   H_UM_ESK.SK_NAZWA:=exec('korekta_nazwa','!ppl_pll_rume');
   H_UM_ESK.LT:='ETAT';
   H_UM_ESK.KW:=_kw;
   _ret:=H_UM_ESK.add()
?};
H_UM_ESK.cntx_pop();

_ret


\korekta_sekcja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Oznaczenie sekcji dla korekty podstawy.
::   WE: _a STRING - oznaczenie rodzaju zapisu [Z/N/S]
::   WY: oznaczenie sekcji
::----------------------------------------------------------------------------------------------------------------------
_a+'99'


\korekta_nazwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Nazwa sekcji dla korekty podstawy.
::   WE:
::   WY: nazwa sekcji
::----------------------------------------------------------------------------------------------------------------------
'Korekta wyliczeń'


\szczegoly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.42]
:: OPIS: Akcja przycisku "Szczegóły kalkulacji".
::   WE:
::   WY: ''
::----------------------------------------------------------------------------------------------------------------------
_h_um:=H_UM.ref();

_BUF:=tab_tmp(1,
   'LP','INTEGER','Lp.',
   'OPIS','STRING[255]','Opis',
   'KW','REAL','Kwota'
);
_add:="
   _a.LP:=_a.size()+1;
   _a.OPIS:=_b;
   _a.KW:=_c;
   _a.add()
";

_sum:="
   _sek:={? var_pres('_d')=type_of('') || _d || '' ?};
   _kor:=exec('korekta_sekcja','!ppl_pll_rume',_c);
   _a.SKL:=0;
   _a.KOR:=0;
   H_UM_ESK.prefix(_b,_c);
   _loop:=H_UM_ESK.first();
   {!
   |? _loop
   |! {? _sek='' | H_UM_ESK.S=_sek
      || {? H_UM_ESK.S=_kor
         || _a.KOR+=H_UM_ESK.KW
         || _a.SKL+=H_UM_ESK.KW
         ?}
      ?};
      _loop:=H_UM_ESK.next()
   !}
";
_kw:=obj_new('SKL','KOR');

H_UM_ESK.cntx_psh();
H_UM_ESK.index('UNIQUE');

:: informacje do wyliczeń
_sum(_kw,_h_um,'I','LMC');
_add(_BUF,'Liczba miesięcy uwzględnionych w obliczeniach',_kw.SKL);

:: wykaż składniki zmienne
_sum(_kw,_h_um,'Z');
_add(_BUF,'Suma składników zmiennych',_kw.SKL);
_add(_BUF,'Korekta sumy składników zmiennych',_kw.KOR);
_add(_BUF,'Suma składników zmiennych po korekcie',_kw.SKL+_kw.KOR);
_org:=_kw.SKL+_kw.KOR;

_sum(_kw,_h_um,'I','PRZ');
_add(_BUF,'Składniki zmienne po uzupełnieniu',{? _kw.SKL=0 || _org || _kw.SKL ?});

_sum(_kw,_h_um,'I','MCZ');
_add(_BUF,'Kwota składników zmiennych przypadająca na miesiąc',_kw.SKL);

:: wykaż składniki nominalne
_sum(_kw,_h_um,'N');
_add(_BUF,'Suma składników nominalnych (niepodlegających uzupełnieniu)',_kw.SKL);
_add(_BUF,'Korekta sumy składników nominalnych (niepodlegających uzupełnieniu)',_kw.KOR);
_add(_BUF,'Suma składników nominalnych(niepodlegających uzupełnieniu) po korekcie',_kw.SKL+_kw.KOR);

_sum(_kw,_h_um,'I','MCZ');
_add(_BUF,'Kwota składników nominalnych (niepodlegających uzupełnieniu) przypadająca na miesiąc',_kw.SKL);

_sum(_kw,_h_um,'I','DSN');
_add(_BUF,'Kwota składników zmiennych i nominalnych (niepodlegających uzupełnieniu) przypadająca na miesiąc',_kw.SKL);

:: wykaż składniki stałe
_sum(_kw,_h_um,'S');
_add(_BUF,'Suma składników stałych',_kw.SKL);
_add(_BUF,'Korekta sumy składników stałych',_kw.KOR);
_add(_BUF,'Suma składników stałych po korekcie',_kw.SKL+_kw.KOR);

:: wykaż składniki okresowe
_sum(_kw,_h_um,'O');
_add(_BUF,'Suma składników okresowych',_kw.SKL);
_add(_BUF,'Korekta sumy składników okresowych',_kw.KOR);
_add(_BUF,'Suma składników okresowych po korekcie',_kw.SKL+_kw.KOR);
{? exec('test_par','parametr',280,'T')
|| _sum(_kw,_h_um,'I','ODP');
   _add(_BUF,'Liczba dni przepracowanych w okresie',_kw.SKL);
   _sum(_kw,_h_um,'I','ODN');
   _add(_BUF,'Liczba dni nominalnych w okresie',_kw.SKL);
   _sum(_kw,_h_um,'I','OAU');
   _add(_BUF,'Wartość po uzupełnieniu',_kw.SKL)
?};
_sum(_kw,_h_um,'I','OLM');
_add(_BUF,'Liczba miesięcy uwzględniona dla składników okresowych',_kw.SKL);
_sum(_kw,_h_um,'I','OPR');
_add(_BUF,'Kwota składników okresowych przypadająca na miesiąc',_kw.SKL);


_sum(_kw,_h_um,'I','DSS');
_add(_BUF,'Kwota uwzględniająca wszystkie składniki',_kw.SKL);

_sum(_kw,_h_um,'I','WSP');
_add(_BUF,'Współczynnik do wyliczeń',_kw.SKL);

_sum(_kw,_h_um,'I','WYM');
_add(_BUF,'Wymiar zatrudnienia',_kw.SKL);

_sum(_kw,_h_um,'I','EWS');
_add(_BUF,'Kwota po podzieleniu przez iloczyn współczynnika i wymiaru zatrudnienia',_kw.SKL);

_sum(_kw,_h_um,'I','NGK');
{? _kw.SKL<>1
|| _add(_BUF,'Normatywna liczba godzin',_kw.SKL);
   _sum(_kw,_h_um,'I','KNG');
   _add(_BUF,'Kwota po podzieleniu przez normatywną liczbę godzin',_kw.SKL)
?};

_sum(_kw,_h_um,'I','L72');
_add(_BUF,'Liczba godzin do wypłaty',_kw.SKL);

_sum(_kw,_h_um,'I','RET');
_add(_BUF,'Kwota po pomnożeniu przez liczbę godzin do wypłaty',_kw.SKL);

H_UM_ESK.cntx_pop();

_wnd:=_BUF.mk_sel('Szczegóły kalkulacji'@,'N',0,'#h_um_ekw_licz');
_BUF.win_fld(_wnd,,'OPIS',,,50);
_BUF.win_fld(_wnd,,'KW',,,,3);
_BUF.win_sel(_wnd);
_BUF.select();

''


\grp_ekw4_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: pk [21.14]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_EKW w czwartej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_bs','!ppl_pll_rume',_a,'O')


\grp_ekw4_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: pk [21.14]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_EKW w czwartej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_as','!ppl_pll_rume',_a,'O')


\grp_ekw4_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: pk [21.14]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_EKW w czwartej zakładce okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_ekwx_ar','!ppl_pll_rume')


\grp_esk4_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: pk [21.14]
:: OPIS: Przed obsługą okienka WER tabeli H_UM_ESK w czwartej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_bs','!ppl_pll_rume',_a,'O')


\grp_esk4_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: pk [21.14]
:: OPIS: Po obsłudze okienka WER tabeli H_UM_ESK w czwartej zakładce okienka GRP tabeli H_UM_EKW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_as','!ppl_pll_rume',_a,'O')


\grp_esk4_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: pk [21.14]
:: OPIS: Po odświeżeniu wiersza okienka WER tabeli H_UM_ESK w czwartej zakładce okienka GRP tabeli H_UM_EKW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('grp_eskx_ar','!ppl_pll_rume')

:Sign Version 2.0 jowisz:1045 2023/11/15 14:14:34 61da0faa2d66c4c9cb784f2064accd2ba3703d02cf61b3a249d7e8bf1d37c5f85e44bcdd3375b50d092da41f9e99794e36e10303c536aa1f7dce4a6573d563d38de2b89cb684ed8a00dc135bc6401342b10e7a2ac5c8839a4af28a7743f8726766af70f7dbbf3a9fbb9e80cc9f48198bb5d8e6e106e9e4b68f1691d6eb6bc922

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !hbn_prz_eakc.fml
:: Utworzony: 11.03.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności HBN_PRZ_EAKC - Zatwierdzanie przelewu.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zatwierdzanie przelewu - główna formuła czynności HBN_PRZ_EAKC.
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,   symbol=PB,   type=_PB,   name=Wskazanie na przelew elektroniczny,  required=T, keyref=T
::# kind=WY,   symbol=PB,   type=_PB,   name=Wskazanie na przelew elektroniczny,  required=T
::# permissions=FJKS,HRB,HRP
::# access=exec('uprawnienia','!hbn_prz_eakc')
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_ok:=params_exec('confirm','!hbn_prz_eakc',_in.PB,_mp);
{? _ok=-1
|| _out.PB:=null;
   _mp.save(,_out);
   _mp.error()
|? _ok
|| _out.PB:=_in.PB;
   _mp.save(,_out);
   _mp.done()
|| _mp.cancel()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='';
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('PB',_we) & type_of(_we.PB)=type_of(null) & _we.PB<>null
|| PB.cntx_psh();
   PB.use(ref_name(_we.PB));
   PB.prefix();
   {? PB.seek(_we.PB,)
   || {? PB.ZT='T' || _desc:='Anuluj zatwierdzenie przelewu: %1'@@[PB.TYT] || _desc:='Zatwierdź przelew: %1'@@[PB.TYT] ?}      || _desc:='Uwaga błąd - nie odnaleziono przelewu.'@@
   ?};
   PB.cntx_pop()
|| _desc:='Uwaga: nie znaleziono parametru wejściowego lub znaleziono rekordu kluczowego.'@@
?};
_desc


\confirm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła zatwierdza lub anuluje przelew
::   WE: _a - wskazanie na rekord przelewu, _b-obiekt odpowiedzialny za obługę procesu
::   WY: 0 - nie udało się, 1 - udało się
::  OLD: \f_pbzt/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
_mp:=_b;
_auto:=_mp.isAutoRun() | var_press('g_tot')>0;
{? _>0 & type_of(_a)=type_of(null) & _a<>null
|| {? 8+($_a)<>PB.name() || PB.use(form(8+($_a))); PB.prefix() ?};
   {? ~PB.seek(_a,)
   || {? ~_auto || FUN.info('Nie znaleziono przelewu.'@) ?};
      return(-1)
   ?}
|| {? ~_auto || FUN.info('Nie znaleziono przelewu.'@) ?};
   return(-1)
?};

:: weryfikacja uprawnień - typ przelewu
{? ~exec('usr_hrp','b_perm',PB.KD)
|| {? ~(var_pres('g_tot')>0 & g_tot)
   || FUN.info('Brak uprawnień do typów przelewów '+
         {? PB.KD='HBN: Prosty' || 'krajowych i zagranicznych'
         |? PB.KD='HBN: US' || 'do US'
         |? PB.KD='HBN: ZUS' || 'do ZUS'
         || ''
         ?}+'.')
   ?};
   return(0)
?};


:: weryfikacja uprawnień - jednostka księgowa
{? ~exec('usr_fjks','b_perm',PB.ODD().OD)
|| {? ~(var_pres('g_tot')>0 & g_tot)
   || FUN.info('Brak uprawnień do jednostki księgowej %1.'@[PB.ODD().OD])
   ?};
   return(0)
?};

:: weryfikacja uprawnień - rachnunek bankowy licencjobiorcy
{? ~exec('usr_hrb','b_perm',PB.RD)
|| {? ~(var_pres('g_tot')>0 & g_tot)
   || FUN.info('Brak uprawnień do rachunku bankowego licencjobiorcy: %1.'@[PB.RD])
   ?};
   return(0)
?};

:: obsługa grupowa
{? _mp.isMicro() & var_pres('g_tot')>0 & g_tot
|| exec('pb_akc','hbn',g_akc)
||
:: obsługa dla pojednynczego przelewu
   {? _auto=0 & _mp.pathTodo()
   || exec('okno_pb','homebank',exec('okno_rodz','homebank'),1);
      KONTO.K1:=PB.AN;
      RACHBANK.KB_1R:=PB.RD;
      RACHBANK.KB_1R_BD:='RACHBANK.KB_1R:=RB.get_rbtx(2,RACHBANK.KB_1R,\'\')';
      RACHBANK.RB_LB_PB:=PB.RD;
      RACHBANK.RB_KH_PB:=PB.RW;

      exec('bv_pb_us_zus','rachunki');
      __PBCONF:=0;
      PB.display();
      {? __PBCONF=0 || return(0) ?}
   ?};

   {? PB.ZT='T' & _mp.isMicro()
   || PB.USER_ZTW:='';
      PB.D_ZTW:=date(0,0,0);
      PB.ZT:='N';
      {? _mp.isMicro()
      || PAR_WYDR.RPAR1-=PB.KW;
         PAR_WYDR.RPAR2+=PB.KW
      ?}
   |? PB.ZT='N'
   || {? PB.RW=''
      || {? _auto=0
         || FUN.emsg('Brak numeru rachunku zleceniobiorcy.\nZatwierdzenie przelewu niemożliwe.'@)
         ?};
         return(0)
      ?};
      {? PB.RODZ='W' & PB.RODZZAGR=null
      || {? _auto=0
         || FUN.emsg('Brak rodzaju płatności dla przelewu zagranicznego.\nZatwierdzenie przelewu niemożliwe.'@)
         ?};
         return(0)
      ?};
      {? PB.KW=0
      || {? _auto=0
         || FUN.emsg('Przelew na zerową kwotę.\nZatwierdzenie przelewu niemożliwe.'@)
         ?};
         return(0)
      ?};
      PB.USER_ZTW:=exec('name','users');
      PB.D_ZTW:=date();
      PB.ZT:='T';
      {? _mp.isMicro()
      || PAR_WYDR.RPAR1+=PB.KW;
         PAR_WYDR.RPAR2-=PB.KW
      ?}
   ?}
?};
PB.put();
{? _mp.isMicro()
|| {? PAR_SKID.get(68)='N' & PB.sel_size()=0 || exec('podsuma','hbn') ?};
   PAR_WYDR.RPAR3:=PAR_WYDR.RPAR1+PAR_WYDR.RPAR2
?};
1


\gb_pbzt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.60]
:: OPIS: Formula przed akcja grupowego zatwierdzania/anulowania zatwierdzenia przelewow.
::  OLD: \gb_pbzt/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
g_cnt:=0;
g_akc:=0;
g_tot:=PB.sel_size();
{? g_tot
|| {? -menu_txt()='zatwierdź' || g_akc:=1 || g_akc:=2 ?}
?};
{? ~g_akc || &g_cnt; &g_tot; &g_akc; 0 || 1 ?}


\ga_pbzt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.60]
:: OPIS: Formula po akcji grupowego zatwierdzania/anulowania zatwierdzenia przelewow.
::  OLD: \ga_pbzt/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(68)='N' || exec('podsuma','hbn') ?};
FUN.info('Liczba przelewów, dla których zmieniono status zatwierdzenia: %1.'@[$g_cnt]);
&g_akc; &g_tot; &g_cnt;
1


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
{? var_pres('PB',_in) & var_pres('PB',_in)=type_of(null()) & _in.PB<>null()
|| USERS.cntx_psh();
   USERS.clear();
   {? USERS.seek(_user)
   || PB.cntx_psh();
      PB.prefix();
      {? PB.seek(_in.PB,form(($_in.PB)-8))
      || {? exec('usr_fjks','b_perm',PB.ODD().OD,USERS.ref())
            & exec('usr_hrb','b_perm',PB.RD,USERS.ref())
            & exec('usr_hrp','b_perm',PB.KD,USERS.ref())
         || _result:=1
         ?}
      ?};
      PB.cntx_pop()
   ?};
   USERS.cntx_pop()
||
:: nie ma przelewu wejściowego to znaczy, że należy zwrócić pełne uprawnienia
:: inaczej zadanie do usuniętego przelewu nigdy nie zniknie z todo.
   _result:=1
?};
_result


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'pb'>0
         || _record:=exec('FindAndGet','#table',PB,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 66d91d02e6c6a319a63feb2b1ac833713059d56f10a2d3a607d1057777addd6413cdcc4af9eaf3c0e93f3905f701c1cb41421cbacf03794950b147adf2c996d19974f136f5e395999ddc530da0027b5d901f93a830a6f893edebb4958ae0616b5e849673aaf2892bc74f0c86da9215f423ef5e71e61ca1ad14dd6a79569dfa7a

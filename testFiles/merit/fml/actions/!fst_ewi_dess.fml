:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_dess.fml
:: Utworzony: 12.11.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DESS - Szybka rejestracja środka trwałego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Szybka rejestracja środka trwałego - główna formuła czynności FST_EWI_DESS.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# access=exec('uprawnienia','!fst_ewi_dess')
:: PARAMETRY WE:
::# kind=WE, symbol=EL, type=STRING, name=Czy element, required=N
::# kind=WE, symbol=ZESTAW, type=_SRSR, name=Wskazanie na zestaw, required=N
:: PARAMETRY WY:
::# kind=WY, symbol=SRSR, type=_SRSR, name=Wskazanie na środek dodany w trybie szybkim, required=T
::# kind=WY, symbol=R, type=STRING, name=Rodzaj środka, required=T
::# kind=WY, symbol=SRSR_E, type=_SRSR, name=Wskazanie na element zestawu dodany w trybie szybkim, required=N
::

{? _=0 || _a:='' ?};

_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=-_mp.akcja();

{? ~exec('okr_mod','fst')
|| {? _mp.pathProc() || _mp.cancel() ?};
   return(0)
?};

{? var_pres('EL',_we) & _we.EL='T' || _a:='E' ?};

_srsr_rf:=_srsr_re:=null;

{? _mp.pathProc() | (_mp.pathArea() & _akcja='dołącz' & _a='') | (_mp.pathTodo() & var_pres('EL',_we) & _we.EL='T')
|| {? _mp.pathProc()
   || exec('init','fst');
      EDIT_ES.win_edit('NEW');
      EDIT_ES.hdr_edit();
      EDIT_ES.GRP:='N';
      EDIT_ES.ZESTAW:=null;
      EDIT_ES.efld_opt('NEW','enable=0',,'ZESTAW');
      _mp.trigRef('SRSR');
      _win:=exec('srsr_win','fst');
      SRSR.win_dict(_win);
      exec('filtr_zestawy','fst','U');
      {? EDIT_ES.edit("exec('chk_srsr_proc','fst')")
      || SRSR.f_clear();
         EDIT_ES.R:='';
         {? EDIT_ES.GRP='N'
         || _srsr_rf:=SRD.addSimpleAsset()
         |? EDIT_ES.GRP='T'
         || _srsr_rf:=EDIT_ES.SRSR:=SRD.addSimpleAsset('G')
         |? EDIT_ES.GRP='E'
         || SRSR.prefix();
            EDIT_ES.SRSR:=EDIT_ES.ZESTAW;
            EDIT_ES.SRSR();
            EDIT_ES.Z_NRI:=EDIT_ES.SRSR().NRI;
            EDIT_ES.Z_NST:=EDIT_ES.SRSR().NST;
            _srsr_rf:=SRD.addSimpleAsset('E')
         ?}
      || SRSR.f_clear()
      ?}
   |? _mp.pathTodo() & var_pres('EL',_we) & _we.EL='T'
   || exec('init','fst');
      {? var_pres('ZESTAW',_we)
      || {? _we.ZESTAW<>null || EDIT_ES.SRSR:=_we.ZESTAW || EDIT_ES.SRSR:=null ?};
         SRSR.prefix();
         {? EDIT_ES.SRSR
         || {? SRSR.seek(EDIT_ES.SRSR,SRSR.name())
            || EDIT_ES.Z_NRI:=EDIT_ES.SRSR().NRI;
               EDIT_ES.Z_NST:=EDIT_ES.SRSR().NST;
               _srsr_re:=SRD.addSimpleAsset('E')
            || FUN.emsg('Nie znaleziono zestawu środków.'@)
            ?}
         ?}
      || FUN.emsg('Nie znaleziono zestawu środków.'@)
      ?}
   |? (_mp.pathArea() & _akcja='dołącz' & _a='')
   || _mp.trigRef('SRSR');
      _srsr_rf:=SRD.addSimpleAsset()

   ?};

   {? _srsr_rf
   || _wy.SRSR:=_srsr_rf;
      _wy.SRSR_E:=_srsr_re;
      SRSR.prefix();
      {? SRSR.seek(_srsr_rf) || _wy.R:=SRSR.R ?};
      _mp.save(,_wy);
      _mp.done()
   |? _srsr_re
   || _wy.SRSR:=_we.ZESTAW;
      _wy.SRSR_E:=_srsr_re;
      SRSR.prefix();
      {? SRSR.seek(_srsr_re) || _wy.R:=SRSR.R ?};
      _mp.save(,_wy);
      _mp.done()
   || {? _srsr_re=null & (_mp.pathTodo() & var_pres('EL',_we) & _we.EL='T' & var_pres('ZESTAW',_we) & _we.ZESTAW<>null)
      ||
:: jeżeli zadanie dodania elementu składowego to po Esc pytanie czy zrezygnwać z dodawania
         {? SRSR.seek(_we.ZESTAW,SRSR.name())
         || SRSR.cntx_psh();
            SRSR.index('TREE');
            SRSR.prefix(_we.ZESTAW);
            {? SRSR.size()>0 || _ask:=1 || _ask:=0 ?};
            SRSR.cntx_pop();
            {? _ask
            || {? FUN.ask('Zrezygnować z dodawania kolejnych elementów zestawu?'@)
               || _wy.SRSR:=_wy.SRSR_E:=null;
                  _mp.save(,_wy);
                  _mp.done()
               ?}
            || _mp.cancel()
            ?}
         || _mp.cancel()
         ?}
      ||
:: usunięto zestaw
         _wy.SRSR:=_wy.SRSR_E:=null;
         _mp.save(,_wy);
         _mp.done()
      ?}
   ?}
|? _mp.pathArea()
|| {? _akcja='popraw' || SRD.editSimpleAsset()
   |? _akcja='usuń' || SRD.delSimpleAsset()
   |? _akcja='kopiuj' || SRD.addSimpleAsset(SRST.ref(),SRST.GRP)
   |? _akcja='dołącz zestaw'
   || SRD.addSimpleAsset('G')
   |? _akcja='dołącz element' | (_akcja='dołącz' & _a='E')
   || SRD.addSimpleAsset('E')
   |? _akcja='dołącz serię'
   || {? SRST.size()>0 & SRST.GRP<>'T' & FUN.ask('Skopiować dane z bieżącego środka?'@)
      || {? cur_win(1,1)='WER_UE'
         || SRD.addSimpleSeries(SRST.ref(),'E')
         || SRD.addSimpleSeries(SRST.ref())
         ?}
      || {? cur_win(1,1)='WER_UE'
         || SRD.addSimpleSeries('E')
         || SRD.addSimpleSeries()
         ?}
      ?}
   ?}
?}


\m_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Uruchomienie rejestracji środka w trybie szybkim w obszarze roboczym
::   WE: [_a] - jeżeli _a<>'' & _a='E' to czynności dotyczy dodawania elementu składowego
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FST_EWI_DESS';
_params.AKCJA:=menu_txt();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
{? _>0 & _a='E' || _value:='T' || _value:='N' ?};
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EL',_value);
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\m_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Uruchomienie rejestracji środka w trybie szybkim w obszarze roboczym
::   WE: [_a] - jeżeli _a<>'' & _a='E' to czynności dotyczy dodawania elementu składowego
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FST_EWI_DESS';
_params.AKCJA:=menu_txt();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
{? _>0 & _a='E' || _value:='T' || _value:='N' ?};
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EL',_value);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh(); USERS.prefix();
{? USERS.seek(_user)
||
:: czy w ogóle user ma uprawnienia do jakiejkolwiek jednostki księgowej
   {? exec('usr_fjks_any','b_perm',USERS.ref())
   || _result:=1
   ?}
?};
USERS.cntx_pop();
_result


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('EL',_we) & _we.EL='T'
|| {? var_pres('ZESTAW',_we) & _we.ZESTAW<>null
   || SRSR.cntx_psh();
      SRSR.prefix();
      {? SRSR.seek(_we.ZESTAW,SRSR.name()) || _txt:=SRSR.NRI ?};
      SRSR.cntx_pop()
   || _txt:=''
   ?};
   _desc:='Rejestruj element składowy zestawu %1 w trybie szybkim'@@[_txt]
|| _desc:='Rejestruj środek w trybie szybkim'@@
?};
_desc

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:13 9679dd86c0c1397ff81c6308a5eb61f76b69a4bcc175fb27fc34625423a28900c6d114302dad90982cf284b6b94c7dca72e26cc1b427207a23dfd944249721d02b0d8f734929534789bbb6b2d0f8c7de9c2ed056f5cc946361ed9f8ee917f8c49339a8b3bd32cec496eef830d3b4f22454761a665abb751bd94e6200cf836529

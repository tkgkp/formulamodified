:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zlc_zube.fml
:: Utworzony: 28.06.2016
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PPL_ZLC_ZUBE - Raporty zgłoszeniowe do ZUS
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Przygotowanie raportów zgłoszeniowych do ZUS z obszaru PPL
::   WE:
::   WY:
::  OLD: \menu/rap_zus.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
{? ~__F_ZATR.control(1,'P','Z') || return() ?};
VAR_DEL.delete('zus');
{? (_typ:=exec('wyb_rap_zglo','rap_zus'))<>''
|| _inTerm:=exec('interm','#system');
:: przygotowanie katalogu tymczasowego po stronie serwera dla pracy w inTerm
   _file:='';
   {? _inTerm
   || _tmp_dir:=fmk_tmp_dir(0);
      {? type_of(_tmp_dir)<>type_of(~~)
      || _pth:=_tmp_dir.get_path();
         _file:='';
         _sep:=exec('sep','#file',2);
         _file:=_pth+_sep
      || FUN.emsg('Nie udało się utworzenie katalogu tymczasowego po stronie serwera.'@);
         return()
      ?}
   ?};
:: możliwość wygenerowania wydruku
   {? 'RUD,'*_typ &
      (_choice:=FUN.choice('Wybierz docelowy format raportu ZUS %1'@[~-_typ],,'XML'@,'Wydruk'@))<>1
   || {? _choice=2
      || rep_exec('ppl_zus'+(-_typ))
      ?}
   || ZUS.blank();
      ZUS.PLIK:=-_typ;
      ZUS.FOLDER:={? _inTerm || '' || exec('get','#profile',,OPERATOR.USER().KOD,'lokalny katalog raportów ZUS')?};
      ZUS.DATA:=date();
      ZUS.RAPORT:='ZUS '+_typ;
      ZUS.DATAG:=#0;
      {? exec('zus_dialog','rap_zus',_typ,__F_ZATR.P,0,_file)
      || exec('oZus','rap_zus');
         exec('zus_win_sel','rap_zus',_typ,__F_ZATR.P);
         exec('zus_'+(-_typ)+'_generuj','rap_zus',null(),__F_ZATR.P,'PPL');
         VAR_DEL.delete('zus')
      ?};
      {? ~_inTerm
      || exec('set','#profile',,OPERATOR.USER().KOD,'lokalny katalog raportów ZUS',ZUS.FOLDER)
      ?}
   ?}
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:53 50a6956ba75aebb115da188997677d29b91f39d3741e5cdc2cca274942ca992d43695265bf5d5e94cb8a86584789c3c4012846eb7049cf9a27d77037d11b8e966a696a85def24935efcb56a14ac9f023d292ca4fb62a490e6d0b4b2ed75c5505d0301d6ee8f351503d265bef52de97dcaba6604ef1cac1018d3dcf82f953bbd7

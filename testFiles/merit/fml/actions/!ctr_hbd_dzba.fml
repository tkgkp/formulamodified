:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_hbd_dzba.fml
:: Utworzony: 09.12.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_HBD_DZBA - Akceptacja arkuszy zadań budżetowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akceptacja arkuszy zadań budżetowych - główna formuła czynności CTR_HBD_DZBA.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::# access=exec('access','ctr_bha','B')
:: PARAMETRY WE:
::# kind=WE, symbol=K_HARM_P, type=_K_HARM_P, name=Zadanie budżetowe, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=K_HARM_P, type=_K_HARM_P, name=Zadanie budżetowe, required=T
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
K_HARM_P.cntx_psh();
K_HARM_P.prefix();
_ref:=_mp.getRefs();
{? K_HARM_P.seek(_ref[1])
|| {? _akcja='Zakończ' | _mp.isAutoRun()
   || {? _akcja='Zakończ' | K_HARM_P.ZN_AKC='N'
      || {? K_HARM_P.STATUS='A' | K_HARM_P.ZN_RET='T' & (K_HARM_P.STATUS='P' | K_HARM_P.STATUS='W')
         || _wy.K_HARM_P:=K_HARM_P.ref();
            _mp.save(,_wy);
            _mp.done()
         || _mp.error('Nieoczekiwany status zadania budżetowego do akceptacji: '+K_HARM_P.STAT_NAZ+'.')
         ?}
      ?}
   |? K_HARM_P.STATUS='U'
   || exec('ed_xlsx','control',0);
      {? K_HARM_P.K_HARM().ARKUSZ='G' & K_HARM_P.get() & (K_HARM_P.STATUS='U' | K_HARM_P.STATUS='A')
      || _mp.done()
      ?}
   || FUN.info('Status zadania \'%1\'.\nEdycja arkusza niedozwolona.'@[K_HARM_P.STAT_NAZ]);
      {? K_HARM_P.STATUS='A'
      || _wy.K_HARM_P:=K_HARM_P.ref();
         _mp.save(,_wy);
         _mp.done()
      || _mp.error('Nieoczekiwany status zadania budżetowego do akceptacji: '+K_HARM_P.STAT_NAZ+'.')
      ?}
   ?}
|| FUN.info('Nie znaleziono zadania.'@);
   _wy.K_HARM_P:=null;
   _mp.save(,_wy);
   _mp.done()
?};
K_HARM_P.cntx_pop();
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis na liście TODO czynności
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zaakceptuj zadanie budżetowe'@@;
_mp:=params_get().mp;
_refs:=_mp.getRefs();
{? var_press('_refs')>0 & obj_len(_refs)>0
|| _ref:=_refs[1];
   {? var_press('_ref')>0
   || K_HARM_P.cntx_psh(); K_HARM_P.prefix();
      {? K_HARM_P.seek(_ref)
      || _desc:='Zaakceptuj zadanie budżetowe: %1'@@[exec('record','#to_string',K_HARM_P.ref())]
      ?};
      K_HARM_P.cntx_pop()
   ?}
?};
_desc


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z ToDO z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::       _b - B_PREL.ref() lub BI_PREL.ref() lub exec('mp_run_a','#b__box')
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
_path_param:=_b;
{? _path='Todo'
|| _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   _prel:=_path_param;
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || exec('get_xlsx_www','control',_keyrefs[1],'B')
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:11 d50aabd237f5a09ece8a247425a5afe4410645e20aa28fab024619f42df62887e1bcb664ad562f6a3185b588e85431e1401280285eef2ae770ec38e7883fa1e7cba6df40a90e8d4a797b454357fb3c2f31a49fcda63d147576cc15d01d2befaae246bde949893a0c3a6c0aa9bfc3fd416393a7c725c39a9502117f1e1f7a1f05

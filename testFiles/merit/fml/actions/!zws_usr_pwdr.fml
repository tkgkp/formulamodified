:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zws_usr_pwdr.fml
:: Utworzony: 05.07.2021
:: Autor: WH
::======================================================================================================================
:: Zawartosc: Formuły czynnosci ZWS_USR_PWDR
::            Resetowanie hasła użytkownika
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Formuła główna czynności resetowania hasła użytkownika (ZWS_USR_PWDR)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

::# kind=WE, symbol=MODE,  type=STRING,   name=Tryb działania, required=N, keyref=N
{? var_pres('MODE',_in)<>type_of(~~) & var_pres('MODE',_in)<>type_of('')
|| _msg:='Błędny parametr wejściowy \'MODE\' dla czynności ZWS_USR_PWDR';
   FUN.error(_msg);
   _mp.error(_msg);
   return()
?};

::# kind=WE, symbol=USERS,  type=_USERS,   name=Użytkownik, required=N, keyref=N
{? var_pres('USERS',_in)<>type_of(~~) & var_pres('USERS',_in)<>type_of(null())
|| _msg:='Błędny parametr wejściowy \'USERS\' dla czynności ZWS_USR_PWDR';
   FUN.error(_msg);
   _mp.error(_msg);
   return()
?};
::# kind=WE,   symbol=DIALOG,  type=STRING,   name=Czy ma być pokazywana wynikowa treść html (T/N),  required=N
{? var_pres('DIALOG',_in)=type_of(~~) || _in.DIALOG:='T' ?};
::# kind=WY, symbol=RESULT,   type=STRING,   name=Wynik działania, required=N
::# kind=WY, symbol=SUB,      type=STRING,   name=Temat, required=N
::# kind=WY, symbol=EMAIL,    type=MEMO,     name=Email użytkownika, required=N
::# kind=WY, symbol=BODYH,    type=MEMO,     name=Treść w formacie HTML, required=N
::# kind=WY, symbol=BODY_SMS, type=STRING,   name=Treść SMSa, required=N

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_service:=_mp.isService();

_users:=null();
_mode:='';
_can_continue:=1;

{? var_pres('USERS',_in)=type_of(null())
|| _users:=_in.USERS
?};

{? var_pres('MODE',_in)=type_of('')
|| _mode:=_in.MODE
?};

{? _mode=''
|| _choice:=FUN.choice(
      'Wybierz rodzaj dostępu'@,,
      'j&Term, inTerm i webTerm'@,'&jTerm i inTerm'@,'&webTerm'@
   );
   {? _choice=0
   || _can_continue:=0;
      _mp.cancel()
   |? _choice=1
   || _mode:='O'
   |? _choice=2
   || _mode:='J'
   |? _choice=3
   || _mode:='W'
   ?}
?};

{? _can_continue>0 & _users=null()
|| _users:=exec('select','users')
?};

:: Sekcja sprawdzania
{? _can_continue>0 & _users=null()
|| _can_continue:=0;
   _msg:='Nie przekazano lub nie wybrano użytkownika do inicjowania dostępu.'@;
   {? _mp.isGroup()
   || KOMM.add(_msg,2,,1)
   || FUN.info(_msg)
   ?};
   _mp.error(_msg)
?};
{? _can_continue>0 & _mode=''
|| _can_continue:=0;
   _msg:='Nie wybrano rodzaju dostępu.'@;
   {? _mp.isGroup()
   || KOMM.add(_msg,2,,1)
   || FUN.info(_msg)
   ?};
   _mp.error(_msg)
?};

SYSSUSER.cntx_psh();
USERS.cntx_psh();
USERS.prefix();
{? _can_continue>0
|| _can_continue:=USERS.seek(_users)
?};

{? _can_continue>0 & USERS.AKT<>'T'
|| _can_continue:=0;
   _msg:='Użytkownik: %1 nieaktywny. Inicjowanie dostępu niedozwolone.'@[exec('USERS','#to_string')];
   {? _mp.isGroup()
   || KOMM.add(_msg,2,,1)
   || FUN.info(_msg)
   ?};
   _mp.error(_msg)
?};

_jpass:='';
_wpass:='';

_jlogin:='';
_wlogin:='';

_email:='';
_dane:='';

{? _can_continue>0
||
:: Długość hasła
   _len:=12;
   _sysobj:=exec(,'__sysusr');
:: Wyłączenie komunikatów
   _msg_bef:=_sysobj.showMessages(0);

   {? USERS.seek(_users)
   ||
      {? USERS.GUID<>''
      || {? exec('find_syssuser','users',USERS.GUID)>0
         || _userid:=SYSSUSER.ref();
            _sysusr:=_sysobj.getUser(_userid);
            _email:=USERS.EMAIL;
            _dane:=USERS.DANE;

::          Generowanie haseł
            {? _mode='O'
            || _jpass:=_len+base64('encode',hash($SYSLOG.tm_stamp));
               _wpass:=_len+base64('encode',hash($SYSLOG.tm_stamp))
            |? _mode='J'
            || _jpass:=_len+base64('encode',hash($SYSLOG.tm_stamp))
            |? _mode='W'
            || _wpass:=_len+base64('encode',hash($SYSLOG.tm_stamp))
            ?};

::          Ustawianie uwierzytelniania
            {? _jpass<>''
            || _jlogin:=_sysusr.Login;
               _sysusr.Auth.Pass.Active:=1;
               _sysusr.Auth.Pass.Password:=_jpass;
               _sysusr.Auth.Pass.ForceChange:=1;
               ~~
            ?};
            {? _wpass<>''
            || {? _sysusr.Auth.Web.Login=''
               || _sysusr.Auth.Web.Login:=_sysusr.Login
               ?};
               _wlogin:=_sysusr.Auth.Web.Login;

               _sysusr.Auth.Web.Active:=1;
               _sysusr.Auth.Web.Password:=_wpass;
               _sysusr.Auth.Web.ForceChange:=1;
               ~~
            ?};

::          Zapis do użytkownika SYSSUSERS
            {? _jpass<>'' | _wpass<>''
            || {? _sysobj.putUser(_sysusr,1)<>0
               || _can_continue:=1
               || _can_continue:=0;
                  _msg:=_sysobj.LastErrorMessage;
                  {? _mp.isGroup()
                  || KOMM.add(_msg,2,,1)
                  || FUN.info(_msg)
                  ?};
                  _mp.error(_msg)
               ?}
            ?};

            {? _can_continue>0 & _mp.isMicro()=0 & USERS.EMAIL=''
            || _msg:='Użytkownik: %1 nie ma podanego adresu e-mail. Hasło należy mu dostarczyć inną drogą.'@[exec('USERS','#to_string')];
               {? _mp.isGroup()
               || KOMM.add(_msg,2,,1)
               || FUN.info(_msg)
               ?}
            ?}
         ?}
      ?};
::    Synchronizacja
      exec('synchronize_one','users',,_sysobj);
      ~~
   ?};
   _sysobj.showMessages(_msg_bef);

   {? _can_continue>0
   || {? var_pres('__PWDR')>100
      || _tab:=__PWDR
      || _tab:=exec('tab_pwdr','!zws_usr_pwdr')
      ?};
      _tab.blank();
      _tab.LP:=_tab.size()+1;
      _tab.JLOGIN:=_jlogin;
      _tab.JPASS:=_jpass;
      _tab.WLOGIN:=_wlogin;
      _tab.WPASS:=_wpass;
      _tab.DANE:=_dane;
      _tab.EMAIL:=_email;
      {? _tab.add()
      || _tab_memo:=tab_tmp(1,
                     'LP','INTEGER','Lp',
                     'BODYH','SYS_MEMO','Wygenerowane dane dostępowe');
         _tab_memo.blank();
         {? _tab_memo.add()
         || _bodyh:=exec('bodyh','!zws_usr_pwdr',_tab.JLOGIN
                                                ,_tab.JPASS
                                                ,_tab.WLOGIN
                                                ,_tab.WPASS
                                                ,_tab.DANE
                                                ,_tab.EMAIL);
            _tab_memo.memo_set(_bodyh,'BODYH');
            {? _tab_memo.memo_put(,'BODYH')
            || {? _mp.isGroup()=0 & _in.DIALOG='T'
               || exec('edit_html_sysmemo','#edit',_tab_memo,'BODYH','prwd_reset',,'Wynik inicjowania dostępu'@,0)
               ?}
            ?}
         ?}
      ?}
   ?};
   ~~
?};

{? _can_continue>0
||
   {? _email<>'' & (_jpass<>'' | _wpass<>'')
   || _out.RESULT:='OK';
      _out.EMAIL:=_email;
      _out.BODYH:=exec('bodyh','!zws_usr_pwdr',_jlogin,_jpass,_wlogin,_wpass,_dane,_email);
      _out.SUB:='Nowe hasło dostępowe do systemu: %1 w firmie: %2'@[exec('nazwa','#system'),REF.FIRMA().OPIS];
      _out.BODY_SMS:=exec('body_sms','!zws_usr_pwdr',_jlogin,_jpass,_wlogin,_wpass);
      _mp.save(,_out);
      _mp.done();
      ~~
   ||
      _out.RESULT:='BŁĄD';
      _mp.save(,_out);
      _mp.done();
      ~~
   ?}
?};

USERS.cntx_pop();
SYSSUSER.cntx_pop();
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Opis dla czynności resetowania hasła użytkownika (ZWS_USR_PWDR)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));
_users:=null();

:: jest parametr wejściowy USERS
{? _users=null() & var_pres('USERS',_in)=type_of(null())
|| _users:=_in.USERS
?};

{? _users<>null()
|| _desc:='Zresetuj hasło użytkownika: %1'@@[exec('record','#to_string',_users)]
|| _desc:='Zresetuj hasło użytkownika'@@
?};
_desc


\action_reset
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Akcja Resetuj hasło w głównym oknie użytkowników
::   WE:
::   WY:
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='ZWS_USR_PWDR';
_args.PROC_START:='T';
_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
{? USERS.sel_size()>0
||
:: Akcja grupowa
   _args.GRUPA:='T';
   {? VAR.STRING<>''
   || exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'MODE',VAR.STRING)
   ?}
?};
exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'USERS',USERS.ref());
exec('mp_run','#b__box',_args);
~~


\action_reset_gr1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Akcja Grupa przed Resetuj hasło w głównym oknie użytkowników
::   WY: 0/1
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_result:=0;

VAR_DEL.delete('__PWDR');
__PWDR:=exec('tab_pwdr','!zws_usr_pwdr');
VAR.STRING:='';
_choice:=FUN.choice(
   'Zaznaczono: %1 użytkowników. Wybierz rodzaj dostępu'@[$USERS.sel_size()],,
   'j&Term, inTerm i webTerm'@,'&jTerm i inTerm'@,'&webTerm'@
);
{? _choice>0
|| {? _choice=1
   || _mode:='O'
   |? _choice=2
   || _mode:='J'
   |? _choice=3
   || _mode:='W'
   ?};
   VAR.STRING:=_mode;
   _result:=1;
   KOMM.init(200,,'Inicjowanie dostępu użytkownikom'@,'')
?};
sel_nchk();
_result


\action_reset_gr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Akcja Grupa po Resetuj hasło w głównym oknie użytkowników
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
KOMM.select();
{? var_pres('__PWDR')>100
|| _tab:=__PWDR;
   _pth:='';
   _tmp_dir:=fmk_tmp_dir(0);
   {? type_of(_tmp_dir)<>type_of(~~)
   || _pth:=_tmp_dir.get_path()
   ?};
   _sep:=exec('sep','#file',1);

   {? _pth<>''
   || _filepath:=_pth+_sep+$SYSLOG.tm_stamp()+'.html';
      _file:=fopen(_filepath,'Uw',0);

      {? _file>0
      ||
         _tab.prefix();
         _it:=0;
         {? _tab.first()
         || {!
            |? _it+=1;
               _init:=0;
               _end:=0;
               {? _it=1
               || _init:=1
               ?};
               {? _it=_tab.size()
               || _end:=1
               ?};
               _line:=exec('bodyh','!zws_usr_pwdr' ,_tab.JLOGIN
                                                   ,_tab.JPASS
                                                   ,_tab.WLOGIN
                                                   ,_tab.WPASS
                                                   ,_tab.DANE
                                                   ,_tab.EMAIL
                                                   ,_init
                                                   ,_end);
               {? _line<>''
               || fwrite(_file,_line)
               ?};
               _tab.next()
            !}
         ?};
         fclose(_file)
      ?}
   || FUN.emsg('Nie powiodło się utworzenie katalogu tymczasowego na serwerze'@)
   ?};
   exec('edit_html_file','#edit',_filepath,'prwd_reset','Wynik inicjowania dostępu',0,0)
?};

VAR_DEL.delete('__PWDR');
~~


\bodyh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Zwraca treść maila
::   WE: _a - STRING - login jterm
::       _b - STRING - hasło jterm
::       _c - STRING - login WWW
::       _d - STRING - hasło WWW
::       _e - STRING - dane użytkownika
::       _f - STRING - email
::       [_g] - INTEGER - inicjować nagłówek tabelki HTML?
::       [_h] - INTEGER - kończyć tabelkę HTML?
::   WY: STRING
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_jlogin:=_a;
_jpass:=_b;
_wlogin:=_c;
_wpass:=_d;
_dane:=_e;
_email:=_f;

_result:='';

_init:=1;
{? var_pres('_g')=type_of(0)
|| _init:=_g
?};
_end:=1;
{? var_pres('_h')=type_of(0)
|| _end:=_h
?};

{? _init
|| _result+='Dane dostępowe do systemu: <b>%1</b> w firmie: <b>%2</b>.<br><br>'@[exec('nazwa','#system'),REF.FIRMA().OPIS];
   _result+='
      <table [[STYLE_TABLE]]>
      <tr [[STYLE_TABLE_TR]]>
         <th [[STYLE_TABLE_TH]]>%1 &nbsp;&nbsp;&nbsp;</th>
         <th [[STYLE_TABLE_TH]]>%2 &nbsp;&nbsp;&nbsp;</th>
         <th [[STYLE_TABLE_TH]]>%3 &nbsp;&nbsp;&nbsp;</th>
         <th [[STYLE_TABLE_TH]]>%4 &nbsp;&nbsp;&nbsp;</th>
         <th [[STYLE_TABLE_TH]]>%5 &nbsp;&nbsp;&nbsp;</th>
      </tr>
   '
   ['Rodzaj','Login','Dane użytkownika','Jednorazowe hasło','E-mail']
?};

{? _jlogin<>'' & _jpass<>''
||
   _result+='
      <tr [[STYLE_TABLE_TR]]>
         <td [[STYLE_TABLE_TD]] >%1</td>
         <td [[STYLE_TABLE_TD]] >%2</td>
         <td [[STYLE_TABLE_TD]] >%3</td>
         <td [[STYLE_TABLE_TD]] >%4</td>
         <td [[STYLE_TABLE_TD]] >%5</td>
      </tr>
   '
   ['Dostęp jTerm i inTerm',_jlogin,_dane,_jpass,_email];
   ~~
?};
{? _wlogin<>'' & _wpass<>''
|| _result+='
      <tr [[STYLE_TABLE_TR]]>
         <td [[STYLE_TABLE_TD]] >%1</td>
         <td [[STYLE_TABLE_TD]] >%2</td>
         <td [[STYLE_TABLE_TD]] >%3</td>
         <td [[STYLE_TABLE_TD]] >%4</td>
         <td [[STYLE_TABLE_TD]] >%5</td>
      </tr>
   '
   ['Dostęp webTerm',_wlogin,_dane,_wpass,_email];
   ~~
?};
{? _end>0
|| _result+='</table>'
?};
_result


\tab_pwdr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Zwraca tabelkę tymczasową
::   WY: tab_tmp
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1
   ,'LP','INTEGER','Nazwa pola 1'
   ,'JLOGIN','STRING[30]','Nazwa pola 2'
   ,'JPASS','STRING[255]','Nazwa pola 2'
   ,'WLOGIN','STRING[255]','Nazwa pola 2'
   ,'WPASS','STRING[255]','Nazwa pola 2'
   ,'DANE','STRING[100]','Nazwa pola 2'
   ,'EMAIL','STRING[100]','Nazwa pola 2'
);
_tab


\body_sms
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Zwraca treść wiadomości SMS
::   WE: _a - STRING - login jterm
::       _b - STRING - hasło jterm
::       _c - STRING - login WWW
::       _d - STRING - hasło WWW
::   WY: STRING
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_jlogin:=_a;
_jpass:=_b;
_wlogin:=_c;
_wpass:=_d;
_result:='';
{? (_jlogin<>'' & _jpass<>'') | (_wlogin<>'' & _wpass<>'')
||
   _result:='Dane dostępowe do systemu:%1 w firmie:%2.'@[exec('nazwa','#system'),REF.FIRMA().OPIS];
   {? _jlogin<>'' & _jpass<>'' || _result+='Dostęp jTerm i inTerm-Login:%1,Haslo:%2.'[_jlogin,_jpass] ?};
   {? _wlogin<>'' & _wpass<>'' || _result+='Dostęp webTerm-Login:%1,Haslo:%2.'[_wlogin,_wpass] ?}
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 344863f92c762900b276de07bbf0d320c7ffff3f01b0f1c9a84c6c29a19785ded6dbefeec5ee91c321f05eb9c31779e10419c8005bb3a81e931aa5489f811914e800ec72c8884162f2f5b253263cac8007e2a599e987f4017b6345f0fa20eeab89ffb456ed88afff91ddb73575013d076a8ce799f5aa7cf4a2e9cb42e5ad624a

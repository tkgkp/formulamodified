:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ltwm.fml
:: Utworzony: 07.10.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_LTWM
::            Magazyny do realizacji zamówień wewnętrznych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Czynność ZWS_PAR_LTWM - magazyny do realizacji zamówień wewnętrznych
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejściowe czynności
::       _int - [obj_new] - parametry wewnętrzne czynności
::       _out - [obj_new] - parametry wyjściowe czynności
::       _mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
exec('select','!zws_par_ltwm');
''


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: magazyny do realizacji zamówień sprzedaży
::  OLD: \par_dod/defin.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__zmuprzk');
POM.TYPDOK:='ZAW';
USER.AKR:='ZAW';
_buf:=tab_tmp(1,'MG','STRING[16]','');
__zmuprzk:=2;
_user:=OPERATOR.USER;
OPERATOR.USER:=null();
MG.f_clear();
USERS_UP.dnd_sel('MGPARW',,'records.USERS_UP',"exec('zmnrusmg','!zws_par_ltwm')");
USERS_UP.win_sel('MGPARW');
USERS_UP.index('MGNR');
USERS_UP.prefix(null,POM.TYPDOK);
{? USERS_UP.first()
|| {!
   |? _buf.blank();
      _buf.MG:=$USERS_UP.MG;
      _buf.add(1);
      USERS_UP.next()
   !}
?};
USERS_UP.first();

USERS_UP.select();
OPERATOR.USER:=_user;
exec('usuprmag','parses');
USERS_UP.dnd_sel('MGPARW',,'records.USERS_UP',"");
__zmuprzk:=0;
USERS_UP.index('MGNR');
USERS_UP.prefix(null,POM.TYPDOK);
{? USERS_UP.first()
|| {!
   |? {? _buf.clear(); _buf.find_key($USERS_UP.MG)
      || _buf.del()
      || __zmuprzk:=2
      ?};
      ~__zmuprzk & USERS_UP.next()
   !}
?};
{? ~__zmuprzk & _buf.size() || __zmuprzk:=2 ?};

{? __zmuprzk || exec('aktstzam','zamsiw_wspolne',1) ?};
VAR_DEL.delete('__zmuprzk');
obj_del(_buf);
~~


\zmnrusmg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [12.30]
:: OPIS: zmienia numer USERS_UP.NR
::   WE: [_a] - ''(domyślnie) - drag&drop, 'U','D','N'-akcja do przenumerowania
::  OLD: \zmnrusmg/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (';UDN'*_a)>1 || _a || '' ?};

USERS_UP.cntx_psh();
{? _tryb=''
|| _ref:=dnd_info('dest_record');
   {? USERS_UP.seek(_ref) || exec('zmien_lp','#dragdrop','NR','MGNR') ?}
|| exec('zmien_lpa','#dragdrop','NR','MGNR',,,_tryb)
?};
USERS_UP.cntx_pop();
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 354b33d348cff80137b1002f0593fdc9d1a3a679642be17993b1c37d6a313ab9b15dd0be32c530125423fd1e97195850ae2d5b172b0529107481f895b54a60f46b4ba28a12c2dca62b660e88b8a11f517058c72e8bcec1add2bd65bbe755ab3510b5cd14c2f84ba5da63f87770aca7e100b9565d0bbcf7ed9275063db3d2fb38

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_roz_dcli.fml
:: Utworzony: 08.09.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_ROZ_DCLI - Rozliczanie rozrachunków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rozliczanie rozrachunków - główna formuła czynności FKS_ROZ_DCLI.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
''


\rozlicz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Uruchamia akcje rozliczania rozrachunkow
::       menu: Rozrachunki-->Przegladanie rozrachunkow-->rozliCz
::  OLD: \rozlicz/parow.fml
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); ZAP_OP.cntx_psh();
{? -SSTALE.AR().ZAM='t'
|| FUN.info('Bieżący rok (%1)\njest zamknięty.\nOperacja rozliczania niemożliwa.'@[SSTALE.AR().NAZ])
|? ~exec('rozr_wal','rozrach_kor')
|| FUN.info('Istnieje połączony rozrachunek w walucie obcej.'
            '\nRozliczanie obu rozrachunków możliwe tylko'
            '\nod strony rozrachunku w walucie obcej.'@)
|? exec('op_ok','!fks_roz_dcli')
|| pop_nag:=0; kom_par:='';
   {? (ROZRACH.ZAP_OP:=exec('wyb_zap','rozrach'))<>null
   || PAR_POZ.win_patt('SZU_PAR');
      OPTMP.win_patt('SZU_PAR');
      r_user:=tm_form(OPTMP.tm_stamp())+24;
      exec('wyczysc','rozrach',r_user);
      _roz:=#OP.ref(); aindx:=OP.index('?'); aprfx:=OP.cur_prfx();
      {? OP.next()
      || nex:=#OP.ref();OP.prev()
      |? OP.prev()
      || nex:=#OP.ref(); OP.next()
      || nex:=0
      ?};
      ROZRACH.OP:=OP.ref();
      ROZRACH.KH:=OP.POCH;
      ROZRACH.KONTO:=OP.AN;
      r_str:={? ROZRACH.ZAP_OP().WN$2>ROZRACH.ZAP_OP().MA$2
             || ROZRACH.KW1:=F.SWn(ROZRACH.ZAP_OP().OP().WN,ROZRACH.ZAP_OP().OP().MA); 'Wn'
             || ROZRACH.KW1:=F.SMa(ROZRACH.ZAP_OP().OP().WN,ROZRACH.ZAP_OP().OP().MA); 'Ma'
             ?};
      ROZRACH.OPIS2:='Par. rozrachunków z zapisem rozrachunku '+ROZRACH.OP().SYM;
      ROZRACH.SYMR:='Saldo '+{? r_str='Wn' || 'Ma' || 'Wn'?};
      OP.cntx_psh();
      {? exec('utw_optm','!fks_roz_dcli',1+r_str,r_user) & OPTMP.first()
      || ROZRACH.KW2:=ROZRACH.KW1;
         ROZRACH.SUM3:=ROZRACH.SUM1;
         ROZRACH.SUM2:=0;
         OPTMP.win_sel('PAR');
         OPTMP.win_edit();
         OPTMP.fld_opt('PAR','col_name="%1"'[ROZRACH.SYMR],OPTMP,'SAL');
         {? ~OPTMP.select()
         || 1
         ?}
      || FUN.info('Brak rozrachunków do rozliczenia.'@)
      ?};
      OP.cntx_pop();
      exec('wyczysc','rozrach',r_user);
      {? ~OP.seek(_roz,OP.name())
      || OP.seek(nex,OP.name())
      ?};
      exec('tab_f_rfresh','slo_filtr',OP)
   |? kom_par<>''
   || FUN.info(kom_par)
   ?};
   &pop_nag; &kom_par
?};
VAR_DEL.delete('r_user','r_str');
OP.cntx_pop(); ZAP_OP.cntx_pop();
{? var_press('nex')>0 & nex || OP.seek(nex,) ?};
VAR_DEL.delete('nex','aindx','prfx');
1


\par_popz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Uruchamia mechanizm poprawiania rozliczenia rozrachunków
::  OLD: \par_popz/parow.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_NAG.ZN='T'
|| FUN.info('Rozliczenie zaakceptowane. Wykonaj anulowanie akceptacji.'@)
|| {? exec('czy_wal','!fks_roz_dcli')
   || FUN.info('Rozliczenie związane z rozliczeniem w walucie %1'
               '.\nPoprawę należy wykonać dla rozliczenia w walucie.'@[PAR_NAG.WAL2().KOD])
   || _t:=exec('jest_zap','rozrach');
      {? _t='' &  PAR_NAG.ZAP_OP<>null & exec('zablok','rozrach',1)
      || {? PAR_NAG.get() & PAR_NAG.ZN<>'T'
         || ROZRACH.ZAP_OP:=PAR_NAG.ZAP_OP;
            r_user:=tm_form(OPTMP.tm_stamp())+24;
            ROZRACH.OP:=PAR_NAG.ZAP_OP().OP;
            ROZRACH.KH:=PAR_NAG.POCH;
            ROZRACH.KONTO:=PAR_NAG.KONTO;
            r_str:=PAR_NAG.STR;
            {? ROZRACH.ZAP_OP().WN$2>ROZRACH.ZAP_OP().MA$2
            || ROZRACH.KW1:=F.SWn(ROZRACH.ZAP_OP().WN,ROZRACH.ZAP_OP().MA); 'Wn'
            || ROZRACH.KW1:=F.SMa(ROZRACH.ZAP_OP().WN,ROZRACH.ZAP_OP().MA); 'Ma'
            ?};
            ROZRACH.OPIS2:=PAR_NAG.OPIS;
            ROZRACH.SYMR:='Saldo '+{? r_str='Wn' || 'Ma' || 'Wn'?};
            OP.cntx_psh();
            {? exec('utw_optm2','!fks_roz_dcli',1+r_str,r_user) & OPTMP.first()
            || ROZRACH.KW2:=ROZRACH.KW1$2-ROZRACH.SUM2$2;
               ROZRACH.SUM3:=ROZRACH.SUM1$2-ROZRACH.SUM2$2;
               OPTMP.win_sel('PAR');
               OPTMP.win_edit();
               OPTMP.fld_opt('PAR','col_name="%1"'[ROZRACH.SYMR],OPTMP,'SAL');
               OPTMP.select()
            ?};
            OP.cntx_pop();
            exec('wyczysc','rozrach',r_user)
         || FUN.info('Rozliczenie zaakceptowane. Wykonaj anulowanie akceptacji.'@)
         ?};
         exec('odblok','rozrach')
      || {? _t<>'' || FUN.info(_t) ?}
      ?}
   ?}
?};
VAR_DEL.delete('r_user','r_str');
1


\zak_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zakończ rozliczenie w okienku wertowania OPTMP
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt<>'popraw'
|| {? exec('czy_roz','rozrach',r_user)
   || _p:=FUN.choice('Zakończono wprowadzanie rozliczenia:'@,1,'Akceptuj'@,'Zachowaj bez akceptacji'@);
      {? _p>0
      || sel_exit();
         PAR_POZ.cntx_psh(); PAR_NAG.cntx_psh();
         {? exec('utw_par','!fks_roz_dcli',r_user,r_str)
         || {? _p=1 || exec('par_akc','rozrach',0) ?}
         ?};
         PAR_POZ.cntx_pop(); PAR_NAG.cntx_pop()
      ?}
   || FUN.info('Nie wybrano rozrachunków do rozliczenia.'@)
   ?}
|| {? exec('czy_roz','rozrach',r_user)
   || _p:=FUN.choice('Zakończono poprawianie rozliczenia:'@,3,'Pozostaw bez zmian'@,
                     'Zachowaj bez akceptacji'@,'Akceptuj'@);
      {? _p>0
      || sel_exit();
         {? exec('par_pop2','!fks_roz_dcli',r_user)
         || {? _p=3
            || exec('par_akc','rozrach',0)
            || PAR_NAG.KW_ROZ:=ROZRACH.SUM2$2; PAR_NAG.put()
            ?}
         ?}
      ?}
   || sel_exit();
      FUN.info('Brak pozycji rozliczenia. Zostaną przywrócone poprzednie pozycje rozliczenia.'@)
   ?}
?}


\op_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Sprawdza mozliwosc dokonania rozliczenia zapisu
::  OLD: \op_ok/parow.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:={? OP.WN$2=OP.MA$2
     || FUN.info('Rozrachunek jest już rozliczony.'@); 0
     || 1
     ?};
_par76:=PAR_SKID.get(76);
{? _ok & _par76<>'N'
|| _obj:=exec('obj_op','dok_fks_ks');
   ROK_F.cntx_psh();
   {? ~exec('sp_zap_op','rozrach',_obj) & ~FUN.ask('Rozrachunek jest już rozliczony w roku %1. Czy chcesz kontynuować?'@[ROK_F.NAZ])
   || _ok:=0
   ?};
   ROK_F.cntx_pop()
?};
{? _ok & _par76='B'
|| ROK_F.cntx_psh();
   {? ~exec('sp_poz_next','rozrach',_obj,OP.AN)
   || FUN.info('Rozrachunek nie może być rozliczany.\nNie można ustalić symbolu konta %1 w roku %2.'@[OP.AN,ROK_F.NAZ]);
      _ok:=0
   ?};
   ROK_F.cntx_pop()
?};
{? _ok
|| ZAP_OP.cntx_psh();
   ZAP_OP.index('PAR'); ZAP_OP.prefix(null,OP.ref());
   _ok:=ZAP_OP.first();
   {? ~_ok
   || FUN.info('Rozrachunek nie może być rozliczany.\nBrak zapisów do rozliczania.'@)
   |? exec('sp_poz_next','rozrach')
   || FUN.info('Rozrachunek nie może być rozliczany.\nBrak zapisów do rozliczania.'@)
   || {? ZAP_OP.size=1
      || _ok:=0; ROZRACH.ZAP_OP:=ZAP_OP.ref(); _konto:=OP.AN;
         OP.cntx_psh();
         OP.index('STAN_O');
         _str:={? OP.WN$2>OP.MA$2
               || ROZRACH.KW1:=F.SWn(ROZRACH.ZAP_OP().WN,ROZRACH.ZAP_OP().MA); 'Wn'
               || ROZRACH.KW1:=F.SMa(ROZRACH.ZAP_OP().WN,ROZRACH.ZAP_OP().MA); 'Ma'
               ?};
         OP.prefix(SSTALE.WAL,OP.ODD,'N',_konto);
         {? OP.first()
         || {! |?
               {? (_str='Wn' & OP.MA$2>OP.WN$2) | (_str='Ma' & OP.WN$2>OP.MA$2) || _ok:=1 ?};
               ~_ok & OP.next()
            !}
         ?};
         OP.cntx_pop()
      ?};
      {? _ok
      || OP_PROJ.index('OP'); OP_PROJ.prefix(OP.ref());
         {? OP_PROJ.first()
         || _ok:=0;
            FUN.info('Rozrachunek związany z projektami nie może być rozliczony.'@)
         ?}
      || FUN.info('Rozrachunek nie może być rozliczany. Brak rozrachunków do rozliczania.'@)
      ?}
   ?}; ZAP_OP.cntx_pop()
?};
_ok


\utw_optm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Wprowadzenie rekordow do tabeli OPTMP
::  OLD: \utw_optm/parow.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.SUM1:=0;
OP.index('STAN_O'); OP.prefix(SSTALE.WAL,ROZRACH.OP().ODD,'N',ROZRACH.OP().AN);
{? OP.first()
|| {! |?
      {? ((_a='W' & OP.MA$2>OP.WN$2) | (_a='M' & OP.WN$2>OP.MA$2)) & OP.ref()<>ROZRACH.OP &
         {? SSTALE.WAL=FINFO.NAROD || exec('rozr_wal','rozrach_kor') || 1 ?}
      || _w:={? OP.WN$2>OP.MA$2 || F.SWn(OP.WN,OP.MA) || F.SMa(OP.WN,OP.MA) ?};
         exec('wyp_opt','rozrach',_b,OP.AN,OP.SYM,OP.TZ,OP.TYP,_w,0,'','',0,OP.SYM_ROK,OP.DO,OP.SYM_ZEW);
         ROZRACH.SUM1+=OPTMP.SAL$2
      ?};
      OP.next()
   !}
?};
ROZRACH.SUM1$2<>0


\utw_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Tworzy nagłówek i pozycje rozliczenia
::   WE: _a - user
::       _b - strona
::  OLD: \utw_par/parow.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
do();
{? SSTALE.WAL=FINFO.NAROD
|| {? exec('utw_nag','!fks_roz_dcli',_a,_b,'P') || _ok:=exec('utw_poz','rozrach',_a) ?}
|| {? exec('utw_nag','!fks_roz_dcli',_a,_b,'P')
   || {? exec('utw_poz','rozrach',_a)
      || kurs:=1;
         PAR_NAG.cntx_psh();
         PAR_NAG.ZAP_OP:=exec('zap_wal','rozrach');
         {? PAR_NAG.ZAP_OP<>null
         || PAR_NAG.WAL2:=SSTALE.WAL;
            PAR_NAG.WAL:=FINFO.NAROD;
            {? PAR_NAG.add()
            || {? exec('utw_poz','rozrach',_a,kurs)
               || PAR_NAG.KW_ROZ:=war;
                  _ok:=PAR_NAG.put()
               ?}
            ?}
         ?};
         PAR_NAG.cntx_pop();
         &kurs
      ?}
   ?}
?};
{? ~_ok || undo(); FUN.info('Utworzenie rozliczenia nie powiodło się.'@) ?};
end();
_ok


\utw_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Utworzenie nagłówka rozliczenia
::   WE: _a - unikalny identyfikator
::       _b - strona
::   WY: 1/0
::  OLD: \utw_nag/parow.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_NAG.prefix();
PAR_NAG.blank();
PAR_NAG.UNIK:=_a;
PAR_NAG.STR:=_b;
PAR_NAG.ZAP_OP:=ROZRACH.ZAP_OP;
PAR_NAG.KONTO:=ROZRACH.OP().AN;
PAR_NAG.OP:=OP.SYM;
PAR_NAG.SYM_ZEW:=OP.SYM_ZEW;
PAR_NAG.SYM_ROK:=OP.SYM_ROK;
PAR_NAG.ODD:=OP.ODD;
PAR_NAG.WAL:=OP.WAL;
PAR_NAG.KW_ROZ:=ROZRACH.SUM2;
PAR_NAG.POCH:=OP.POCH;
PAR_NAG.OPIS:=ROZRACH.OPIS2;
PAR_NAG.ZN:='N';
PAR_NAG.TYP:='P';
PAR_NAG.DATA:=OPTMP.DATAO;
{? PAR_NAG.WAL<>FINFO.NAROD || PAR_NAG.WAL2:=FINFO.NAROD ?};
PAR_NAG.add()


\par_pop2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Uruchamia mechanizm poprawiania rozliczenia rozrachunków (wewnętrzna)
::   WE: _a - user
::  OLD: \par_pop2/parow.fml
::----------------------------------------------------------------------------------------------------------------------
do();
_ok:=0;
PAR_POZ.cntx_psh();
PAR_POZ.index('PAR_POZ'); PAR_POZ.prefix(PAR_NAG.ref());
{? PAR_POZ.first() || {! |? PAR_POZ.del() !} ?};
{? exec('utw_poz','rozrach',_a)
|| {? PAR_NAG.WAL<>FINFO.NAROD
   || kurs:=1;
      PAR_NAG.cntx_psh();
      PAR_NAG.index('UNIK'); PAR_NAG.prefix(PAR_NAG.WAL2,'P',PAR_NAG.UNIK);
      {? PAR_NAG.first() & exec('zap_wal','rozrach')<>null
      || PAR_POZ.prefix(PAR_NAG.ref());
         {? PAR_POZ.first() || {! |? PAR_POZ.del() !} ?};
         {? exec('utw_poz','rozrach',_a,kurs)
         || PAR_NAG.KW_ROZ:=war;
            _ok:=PAR_NAG.put()
         ?}
      ?};
      PAR_NAG.cntx_pop()
   || _ok:=1
   ?}
?};
PAR_POZ.cntx_pop();
{? ~_ok || undo(); FUN.info('Operacja zachowania rozliczenia nie powiodła się.'@) ?};
end();
_ok


\czy_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Sprawdza, czy rozliczenie jest w walucie narodowej i powiązane z rozliczeniem w innej walucie
::  OLD: \czy_wal/parow.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_NAG.WAL=FINFO.NAROD & PAR_NAG.WAL2<>null


\utw_optm2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL  [7.53]
:: OPIS: Wprowadzenie rekordow do tabeli OPTMP - poprawianie rozliczenia
::   WE: _a - strona (W, M)
::       _b - user (unikalny identyfikator)
::  OLD: \utw_optm2/parow.fml
::----------------------------------------------------------------------------------------------------------------------
exec('wyczysc','rozrach',_b);
OPTMP.cntx_psh(); OPTMP.index('SYM_UNIK'); OPTMP.prefix(_b);
ROZRACH.SUM1:=ROZRACH.SUM2:=0;
OP.index('KON_O');
PAR_POZ.cntx_psh(); PAR_POZ.index('PAR_POZ'); PAR_POZ.prefix(PAR_NAG.ref());
{? PAR_POZ.first()
|| {! |?
      OP.prefix(PAR_NAG.WAL,PAR_NAG.ODD,PAR_NAG.KONTO,PAR_POZ.OP,PAR_POZ.OP,PAR_POZ.SYM_ROK);
      {? OP.first() || _t:=OP.TYP; _do:=OP.DO || _t:='.'; _do:=date(0,0,0) ?};
      exec('wyp_opt','rozrach',_b,PAR_POZ.KONTO,PAR_POZ.OP,PAR_POZ.TZ,_t,PAR_POZ.SAL,PAR_POZ.KWOTA,'T','',0,PAR_POZ.SYM_ROK,_do,OP.SYM_ZEW);
      ROZRACH.SUM1+=OPTMP.SAL$2;
      ROZRACH.SUM2+=PAR_POZ.KWOTA$2;
      PAR_POZ.next()
   !}
?};
PAR_POZ.cntx_pop();
OP.index('STAN_O'); OP.prefix(SSTALE.WAL,ROZRACH.OP().ODD,'N',ROZRACH.OP().AN);
{? OP.first()
|| {! |?
      {? ((_a='W' & OP.MA$2>OP.WN$2) | (_a='M' & OP.WN$2>OP.MA$2)) & OP.ref()<>ROZRACH.OP
      || {? ~OPTMP.find_key(OP.TZ,OP.SYM,OP.SYM_ROK)
         || _w:={? OP.WN$2>OP.MA$2 || F.SWn(OP.WN,OP.MA) || F.SMa(OP.WN,OP.MA) ?};
            exec('wyp_opt','rozrach',_b,OP.AN,OP.SYM,OP.TZ,OP.TYP,_w,0,'','',0,OP.SYM_ROK,OP.DO,OP.SYM_ZEW);
            ROZRACH.SUM1+=OPTMP.SAL$2
         ?}
      ?};
      OP.next()
   !}
?};
OPTMP.cntx_pop();
ROZRACH.SUM1$2<>0


\par_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Usuwanie rozliczenia rozrachunków
::  OLD: \par_usu/parow.fml
::----------------------------------------------------------------------------------------------------------------------
_selsize:=PAR_NAG.sel_size();
{? PAR_NAG.ZN='T'
|| {? _selsize=0 || FUN.info('Rozliczenie zaakceptowane.\nAnuluj akceptację.'@) ?}
|| _pyt:={? _selsize=0
         || {? exec('czy_wal','!fks_roz_dcli')
            || FUN.ask('Rozliczenie związane z rozliczeniem w walucie %1'
                       '\nUsunąć obydwa rozliczenia?'@[PAR_NAG.WAL2().KOD])
            || FUN.ask('Usunąć rozliczenie?'@)
            ?}
         || 1
         ?};
   {? _pyt & exec('zablok','rozrach',1)
   || _ok:=0; _ref:=PAR_NAG.ref();
      {? PAR_NAG.get() & PAR_NAG.ZN<>'T'
      || do();
         {? PAR_NAG.WAL2=null
         || _ok:=exec('par_usu2','!fks_roz_dcli')
         || PAR_NAG.cntx_psh();
            PAR_NAG.index('UNIK');
            PAR_NAG.prefix(PAR_NAG.WAL2,'P',PAR_NAG.UNIK);
            {? PAR_NAG.first() || _ok:=exec('par_usu2','!fks_roz_dcli') ?};
            PAR_NAG.cntx_pop();
            {? _ok || _ok:=exec('par_usu2','!fks_roz_dcli') ?}
         ?};
         {? ~_ok
         || undo();
            {? _selsize=0 || FUN.info('Operacja usunięcia nie powiodła się.'@) ?}
         || {? var_pres('licz_gr')>0 || licz_gr+=1 ?}
         ?};
         end()
      || {? _selsize=0 || FUN.info('Rozliczenie zaakceptowane.\nAnuluj akceptację.'@) ?}
      ?};
      {? PAR_NAG.seek(_ref) || exec('odblok','rozrach') ?}
   ?}
?};
1


\par_usu2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Usuwanie pozycji rozliczenia rozrachunków
::  OLD: \par_usu2/parow.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_POZ.cntx_psh();
PAR_POZ.index('PAR_POZ'); PAR_POZ.prefix(PAR_NAG.ref());
{? PAR_POZ.first() || {! |? PAR_POZ.del() !} ?};
_ok:={? PAR_POZ.size()=0 || PAR_NAG.del(); 1 || 0 ?};
PAR_POZ.cntx_pop();
_ok


\rozdelbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula poczatkowa dla grupowego usuwania rozliczen
::  OLD: \rozdelbe/parow.fml
::----------------------------------------------------------------------------------------------------------------------
licz_gr:=0; licz_zaz:=PAR_NAG.sel_size();
_zwrot:=FUN.ask('Usunąć zaznaczone rozliczenia?'@);
{? ~_zwrot || VAR_DEL.delete('licz_gr','licz_zaz') ?};
_zwrot


\rozdelae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula koncowa dla grupowego usuwania rozliczen
::  OLD: \rozdelae/parow.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba zaznaczonych rozliczeń: %1'
         '\nLiczba usuniętych rozliczeń: %2'@[$licz_zaz,$licz_gr]);
VAR_DEL.delete('licz_gr','licz_zaz'); 1


\par_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Rekord przed dla okienek wertowania naglowkow rozliczen rozrachunkow
::  OLD: \par_zak/parow.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.SYMR:=(16+PAR_NAG.UNIK);
ROZRACH.DO:=date(0,0,0);
{? PAR_NAG.ZAP_OP<>null
|| ZAP_OP.cntx_psh(); OP.cntx_psh();
   _rn:=ref_name(PAR_NAG.ZAP_OP);
   {? ZAP_OP.name()<>_rn || ZAP_OP.use(_rn); OP.use('operac'+(ZAP_OP.name()+2)) ?};
   ROZRACH.DO:=PAR_NAG.ZAP_OP().OP().DO;
   ZAP_OP.cntx_pop(); OP.cntx_pop()
?};
ROZRACH.STAT_ROZ:={? PAR_NAG.ZN='T' || 'zaakceptowane' |? PAR_NAG.ZN='N' || 'niezaakceptowane' || 'wycofane' ?};
{? PAR_NAG.ZN='T'
|| PAR_NAG.actions_grayed('PAR_WER','APU'); 'PAR_NAG#02#01'
|| PAR_NAG.actions_grayed('PAR_WER','N'); ''
?}


\akcept
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.53]
:: OPIS: Akceptacja rozliczeń rozrachunków
::  OLD: \akcept/parow.fml
::----------------------------------------------------------------------------------------------------------------------
ile_zak:=0; kom_par:='';
opcja:={? PAR_NAG.sel_size()=0
       || FUN.ask('Zaakceptować rozliczenie?'@)
       || 1
       ?};
{? opcja=1 || exec('par_akc','rozrach',0) ?};
&ile_zak; &kom_par


\rozakcbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula poczatkowa dla akceptacji grupy rozliczen
::  OLD: \rozakcbe/parow.fml
::----------------------------------------------------------------------------------------------------------------------
licz_gr:=0; licz_zaz:=PAR_NAG.sel_size();
_zwrot:=FUN.ask('Zaakceptować zaznaczone rozliczenia?'@);
{? ~_zwrot || VAR_DEL.delete('licz_gr','licz_zaz') ?};
_zwrot


\rozakcae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula koncowa dla akceptacji grupy rozliczen
::  OLD: \rozakcae/parow.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba zaznaczonych rozliczeń: %1'
         '\nLiczba zaakceptowanych rozliczeń: %2'@[$licz_zaz,$licz_gr]);
VAR_DEL.delete('licz_gr','licz_zaz'); 1


\par_wyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Wycofanie akceptacji rozliczenia rozrachunków
::  OLD: \par_wyc/parow.fml
::----------------------------------------------------------------------------------------------------------------------
_selsize:=PAR_NAG.sel_size();
{? PAR_NAG.ZN='T'
|| _pyt:={? _selsize=0
         || {? exec('czy_wal','!fks_roz_dcli')
            || FUN.ask('Rozliczenie związane z rozliczeniem w walucie %1'
                       '\nAnulować akceptację dla obydwóch rozliczeń?'@[PAR_NAG.WAL2().KOD])
            || FUN.ask('Anulować akceptację rozliczenia?'@)
            ?}
         || 1
         ?};
   {? _pyt & exec('zablok','rozrach',1)
   || {? PAR_NAG.get() & PAR_NAG.ZN='T'
      || _ok:=0;
         do();
         {? PAR_NAG.WAL2=null
         || _ok:=exec('wyc_p_roz','rozrach','N')
         || PAR_NAG.cntx_psh();
            PAR_NAG.index('UNIK');
            PAR_NAG.prefix(PAR_NAG.WAL2,'P',PAR_NAG.UNIK);
            {? PAR_NAG.first() || _ok:=exec('wyc_p_roz','rozrach','N') ?};
            PAR_NAG.cntx_pop();
            {? _ok || _ok:=exec('wyc_p_roz','rozrach','N') ?}
         ?};
         {? ~_ok
         || undo();
            {? _selsize=0 || FUN.info('Operacja anulowania akceptacji nie powiodła się.'@) ?}
         || {? var_pres('licz_gr')>0 || licz_gr+=1 ?}
         ?};
         end()
      || {? _selsize=0 || FUN.info('Rozliczenie nie jest zaakceptowane.'@) ?}
      ?};
      exec('odblok','rozrach')
   ?}
|| {? _selsize=0 || FUN.info('Rozliczenie nie jest zaakceptowane.'@) ?}
?};
1


\rozwakbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula poczatkowa dla akceptacji grupy rozliczen
::  OLD: \rozwakbe/parow.fml
::----------------------------------------------------------------------------------------------------------------------
licz_gr:=0; licz_zaz:=PAR_NAG.sel_size();
_zwrot:=FUN.ask('Anulować akceptację zaznaczonych rozliczeń?'@);
{? ~_zwrot || VAR_DEL.delete('licz_gr','licz_zaz') ?};
_zwrot


\rozwakae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula koncowa dla akceptacji grupy rozliczen
::  OLD: \rozwakae/parow.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba zaznaczonych rozliczeń: %1'
         '\nLiczba rozliczeń, w których anulowano akceptację: %2'@[$licz_zaz,$licz_gr]);
VAR_DEL.delete('licz_gr','licz_zaz');
1


\par_pref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Ustawia prefiks (dla kont) wyswietlanych rozliczen rozrachunkow
::  OLD: \par_pref/parow.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.win_edit('PREFIX');
{? ROZRACH.edit() ||  exec('ust_ind','!fks_roz_dcli')  ?}


\p_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Zmiana zakresu wyświetlanych rozliczeń rozrachunków
::   WE: _a - ' ' - wszystkie ";
::            'N' - niezaakceptowane ";
::            'T' - zaakceptowane ";
::            'W' - wycofane ";
::  OLD: \p_zakr/parow.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.ZN:=_a;
exec('ust_ind','!fks_roz_dcli')


\ust_ind
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Ustawia indeks i prefiks dla wyświetlanych rozliczeń rozrachunków
::  OLD: \ust_ind/parow.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZRACH.ZN<>''
|| {? OPERATOR.DEPT<>null
   || PAR_NAG.index('ZNACZ_O');
      {? ROZRACH.PREFIX<>''
      || PAR_NAG.prefix(SSTALE.WAL,'P',OPERATOR.DEPT,ROZRACH.ZN,ROZRACH.PREFIX)
      || PAR_NAG.prefix(SSTALE.WAL,'P',OPERATOR.DEPT,ROZRACH.ZN)
      ?}
   || PAR_NAG.index('ZNACZ');
      {? ROZRACH.PREFIX<>''
      || PAR_NAG.prefix(SSTALE.WAL,'P',ROZRACH.ZN,ROZRACH.PREFIX)
      || PAR_NAG.prefix(SSTALE.WAL,'P',ROZRACH.ZN)
      ?}
   ?}
|| {? OPERATOR.DEPT<>null
   || PAR_NAG.index('KONTO_O');
      {? ROZRACH.PREFIX<>''
      || PAR_NAG.prefix(SSTALE.WAL,'P',OPERATOR.DEPT,ROZRACH.PREFIX)
      || PAR_NAG.prefix(SSTALE.WAL,'P',OPERATOR.DEPT)
      ?}
   || PAR_NAG.index('KONTO');
      {? ROZRACH.PREFIX<>''
      || PAR_NAG.prefix(SSTALE.WAL,'P',ROZRACH.PREFIX)
      || PAR_NAG.prefix(SSTALE.WAL,'P')
      ?}
   ?}
?};
PAR_NAG.hdr_sel();
PAR_NAG.hdr_sel({? ROZRACH.PREFIX<>''
                ||' - konta '+(35+(ROZRACH.PREFIX+35*'?'))
                ||''
                ?}+
                {? |ROZRACH.ZN='' || ', wszystkie'
                |? -ROZRACH.ZN='n' || ', niezaakceptowane'
                |? -ROZRACH.ZN='t' || ', zaakceptowane'
                || ', wycofane'
                ?}+' ('+SSTALE.WAL().KOD+')')


\par_dr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.53]
:: OPIS: Wydruk historii rozliczeń
::  OLD: \par_dr/parow.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FKS_ROZ_DCLI','fks_rozl_opr',,,,,,,,'W')


\roz_prz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Techniczne przepisanie kwoty rozliczenia
::  OLD: \roz_prz/parow.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:={? OPTMP.ROZL$2=0 & ROZRACH.KW2$2=0
      || FUN.info('Brak pozostałej kwoty do rozliczenia.'@); 0
      || ROZRACH.KWOTA:=OPTMP.ROZL;
         {? OPTMP.ROZL<>OPTMP.SAL
         || OPTMP.ROZL:={? OPTMP.SAL>ROZRACH.KW2
                        || ROZRACH.KW2+OPTMP.ROZL
                        || OPTMP.SAL
                        ?}
         ?}; 1
      ?};
{? _ret=1
|| OPTMP.win_edit('ROZL_CZ');
   _ret:=OPTMP.edit()
?};
_ret


\roz_cze
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Rozliczanie częściowe
::  OLD: \roz_cze/parow.fml
::----------------------------------------------------------------------------------------------------------------------
OPTMP.ROZL:=OPTMP.ROZL$2; OPTMP.ZNACZ:='T'; OPTMP.put;
ROZRACH.KW2+=ROZRACH.KWOTA$2-OPTMP.ROZL;
ROZRACH.SUM2+=OPTMP.ROZL-ROZRACH.KWOTA$2;
ROZRACH.SUM3:=ROZRACH.SUM1-ROZRACH.SUM2;
1


\roz_cal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Rozliczanie całkowite
::  OLD: \roz_cal/parow.fml
::----------------------------------------------------------------------------------------------------------------------
{? OPTMP.ROZL$2>0
|| ROZRACH.SUM2-=OPTMP.ROZL$2;
   ROZRACH.KW2+=OPTMP.ROZL$2;
   OPTMP.ZNACZ:='';
   OPTMP.ROZL:=0;
   OPTMP.put();
   ROZRACH.SUM3:=(ROZRACH.SUM1-ROZRACH.SUM2)$2
|? ROZRACH.KW2$2=0
|| FUN.info('Brak pozostałej kwoty do rozliczenia.'@)
|? ROZRACH.KW3$2<>0
|| _war:={? ROZRACH.KW2$2<ROZRACH.KW3$2 || ROZRACH.KW2 || ROZRACH.KW3 ?};
   OPTMP.ZNACZ:='T';
   OPTMP.ROZL:=_war$2;
   OPTMP.put();
   ROZRACH.KW2-=_war$2;
   ROZRACH.SUM2+=OPTMP.ROZL$2;
   ROZRACH.SUM3:=(ROZRACH.SUM1-ROZRACH.SUM2)$2
?}


\roz_pok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Rekord przed dla rozliczeń rozrachunków
::  OLD: \roz_pok/parow.fml
::----------------------------------------------------------------------------------------------------------------------
{? (SER_KOR.TYP='S' | SER_KOR.TYP='F' | SER_KOR.TYP='K') & var_pres('um_user')>0
|| OPTMP.actions_grayed(OPTMP.win_sel('?'),'AO');
   {? exec('czy_roz','rozrach',um_user)
   || {? SER_KOR.TYP='K' & ROZRACH.SUM3$2<>ROZRACH.SUM4$2
      || OPTMP.actions_grayed(OPTMP.win_sel('?'),'O')
      || OPTMP.actions_grayed(OPTMP.win_sel('?'))
      ?}
   ?}
?};
{? OPTMP.ZNACZ='T' || 'OPTMP#01#01' || '' ?}


\rec_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Sprawdzenie prawidłowości wpisania sumy przy rozliczaniu częściowym
::  OLD: \rec_par/parow.fml
::----------------------------------------------------------------------------------------------------------------------
{? OPTMP.ROZL$2<=0
|| FUN.info('Kwota musi być większa od 0.'@); 'ROZL'
|? OPTMP.ROZL$2-ROZRACH.KWOTA$2>ROZRACH.KW2$2
|| FUN.info('Zbyt duża kwota do rozliczenia.'@); 'ROZL'
|? OPTMP.ROZL$2<=OPTMP.SAL$2
|| OPTMP.ZNACZ:='T'; ''
|? OPTMP.ROZL$2>OPTMP.SAL$2
|| FUN.info('Maksymalna kwota rozliczenia wynosi: %1'@[form(OPTMP.SAL,,2)]); 'ROZL'
?}


\par_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Edycja opisu rozliczenia
::  OLD: \par_opis/parow.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.win_edit('OPIS');
ROZRACH.edit()


\wyb_zapr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Wybór zapisu do rozliczenia
::  OLD: \wyb_zapr/parow.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('pop_nag')>0
|| {? (F.SWn(ZAP_OP.WN,ZAP_OP.MA)>0 & F.SWn(OP.WN,OP.MA)<=0) |
      (F.SMa(ZAP_OP.WN,ZAP_OP.MA)>0 & F.SMa(OP.WN,OP.MA)<=0)
   || FUN.info('Saldo rozrachunku jest po przeciwnej stronie\nniż saldo wybranego zapisu.'@); 0
   || sel_exit(); 1
   ?}
|| exec('wyb_zapp','rozrach',1)
?}


\zap_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.53]
:: OPIS: Na akcję wyświetl dla nagłówka rozliczenia
::  OLD: \zap_nag/parow.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_NAG.ZAP_OP<>null
|| exec('cntx_psh','rozrach');
   {? PAR_NAG.ZAP_OP<>null || PAR_NAG.ZAP_OP(); exec('zapopuse','rozrach',1) ?};
   PAR_NAG.ZAP_OP().POZDOK().DOK();
   ROZRACH.PAR_REJ:=DOK.REJ().KOD;
   ROZRACH.PAR_NREJ:=DOK.NR;
   ROZRACH.PAR_DPOZ:=POZ.POZ;
   exec('zapopuse','rozrach',0);
   exec('cntx_pop','rozrach')
|| ROZRACH.PAR_REJ:='';
   ROZRACH.PAR_NREJ:=ROZRACH.PAR_DPOZ:=0
?};
1


\par_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Przeglądanie i edycja rozliczeń rozrachunków
::  OLD: \par_sel/parow.fml
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); ZAP_OP.cntx_psh();
{? OPERATOR.DEPT<>null
|| PAR_NAG.index('KONTO_O'); PAR_NAG.prefix(SSTALE.WAL,'P',OPERATOR.DEPT)
|| PAR_NAG.index('KONTO'); PAR_NAG.prefix(SSTALE.WAL,'P')
?};
PAR_NAG.first();
ROZRACH.ZN:=ROZRACH.PREFIX:='';
PAR_NAG.win_edit('PAR_RED');
PAR_NAG.win_patt('PAR_SZUK');
PAR_POZ.win_patt('SZU_PAR');
OPTMP.win_patt('SZU_PAR');
_okno:=PAR_NAG.grp_make('Rozliczenia rozrachunków'@,,'par_nag_wer');
PAR_NAG.grp_sel(_okno,PAR_NAG,{? -SSTALE.AR().ZAM='n' || 'PAR_WER' || 'PAR_WER1' ?},,
                "exec('rfr_par_nag','!fks_roz_dcli')",,,25,,,,,'maximized_with_title');
PAR_NAG.grp_splt(_okno,,'horizontal','left');
PAR_POZ.fld_opt('PAR_WER','col_name="%1"'[ROZRACH.SYMR],PAR_POZ,'SAL');
PAR_POZ.fld_opt('PAR_WER','col_name="%1"'[ROZRACH.OPIS2],ROZRACH,'KW1');
PAR_NAG.grp_sel(_okno,PAR_POZ,'PAR_WER',,"",,15,10,,,,,'maximized_with_title');
PAR_NAG.win_sel(_okno);
PAR_NAG.hdr_sel();
PAR_NAG.hdr_sel(', wszystkie (%1)'@[SSTALE.WAL().KOD] );
PAR_NAG.select(,1);
OP.cntx_pop(); ZAP_OP.cntx_pop();
1


\rfr_par_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu tabeli PAR_NAG (rozliczenia)
::----------------------------------------------------------------------------------------------------------------------
PAR_POZ.index('PAR_POZ');
ROZRACH.SUM1:=0;
ROZRACH.SYMR:=ROZRACH.OPIS2:='';
{? PAR_NAG.get()
|| PAR_POZ.prefix(PAR_NAG.ref());
   {? PAR_POZ.first() || {! |? ROZRACH.SUM1+=PAR_POZ.KWOTA; PAR_POZ.next() !} ?};
   ROZRACH.SYMR:='Saldo '+{? PAR_NAG.STR='Wn' || 'Ma' || 'Wn' ?}+' przed';
   ROZRACH.OPIS2:='Saldo '+{? PAR_NAG.STR='Wn' || 'Ma' || 'Wn' ?}+' po'
|| PAR_POZ.prefix(null)
?};
grp_disp(PAR_POZ,'PAR_WER');
1


\par_zest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [7.53]
:: OPIS: Uaktywnia menu wydrukow w historii rozliczen rozrachunkow
::  OLD: \par_zest/parow.fml
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); ZAP_OP.cntx_psh();
ROZRACH.KONTR:=null;
exec('rep_exec','#b_report','FKS_ROZ_DCLI','fks_par_*',,1);
OP.cntx_pop(); ZAP_OP.cntx_pop()


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 bc3f8acb47d94cbc8295122536e981117fd7a70d1d076381dac398140699162db31f50687acdfd5bb7cf3c921c96a2d39788d0300d5500ab0ed1d995b5974dce821ccf0aac3805791ecb777ed0f54a97def9f499ca5454e1fe98283ac8a84e57ae56f8447f9627432c960a5360529c068f0b1d06fab2354d54986b4908cd9da0

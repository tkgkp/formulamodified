:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pco_zldk.fml
:: Utworzony: 06.06.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PCO_ZLDK - Definicje podziałów faktur i magazynowe
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Definicje podziałów faktur i magazynowe - główna formuła czynności CTR_PCO_ZLDK
::  OLD: \typy_log/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
typ_edit:=''; m4_def:=1;
exec('m4_tmpkh','control_marza');
exec('m4_tmpm','control_marza');
exec('m4_tmpu','control_marza');
::_red:=XPERTIS.mk_edit('Finanse'@,,'finanse_red',,,,'auto');
::XPERTIS.win_ebtn(_red,'text=%1,display=1,panel=left,align=begin'['Schematy danych'@],
::                 "params_exec('main','!zws_par_rsch');''");
::XPERTIS.win_ebtn(_red,'text=%1,display=1,panel=left,align=begin'['Modele controllingowe'@],
::                 "params_exec('main','!zws_par_cmod');''");
::XPERTIS.win_ebtn(_red,'text=%1,display=1,panel=left,align=begin'['Parametry marży IV'@],
::                 "params_exec('param_m4','control_marza');''");
::XPERTIS.win_ebtn(_red,'text=%1,display=1,panel=left,align=begin'['Zestawy kont'@],
::                 "params_exec('m4_zeskon','control_marza');''");
::task_attach('ZWS_PAR_RSCH');
_okn_grp:=TYPYSP.grp_make('Definicje'@,,'tab_rodz_doklog');
TYPYSP.grp_sel(_okn_grp,,'M4_SPRZ','Typy dokumentów sprzedaży'@,,,,,
               "exec('log_dok','!ctr_pco_zldk','S')",,,,'maximized');
TYPYSP.grp_sel(_okn_grp,,'WER_KON','Typy dokumentów zakupu'@,,,,,
               "exec('log_dok','!ctr_pco_zldk','Z')",,,,'maximized');
TYPYSP.grp_sel(_okn_grp,TYPYDOK,'WER_KON','Typy dokumentów magazynowych'@,,,,,
               "exec('log_dok','!ctr_pco_zldk','M')",,,,'maximized');
TYPYSP.grp_sel(_okn_grp,MGR,'M4_SLO','Grupy materiałów'@,,,,,
               "exec('bobs_gr_mat_usl','control_marza','T')",,,,'maximized');
TYPYSP.grp_sel(_okn_grp,TMPMM4,TMPMM4.win_sel('?'),'Materiały'@,,,,,
               "exec('bobs_mat_usl','!ctr_pco_zldk','T')",,,,'maximized');
TYPYSP.grp_sel(_okn_grp,MGR,'M4_SLO','Grupy usług'@,,,,,
              "exec('bobs_gr_mat_usl','control_marza','U')",,,,'maximized');
TYPYSP.grp_sel(_okn_grp,TMPUM4,TMPUM4.win_sel('?'),'Usługi'@,,,,,
              "exec('bobs_mat_usl','!ctr_pco_zldk','U')",,,,'maximized');
TYPYSP.grp_sel(_okn_grp,GRKH,'M4_DEF','Grupy kontrahentów'@,,,,,
              "exec('bobs_grkh','control_marza')",,,,'maximized');
TYPYSP.grp_sel(_okn_grp,TMPKHM4,TMPKHM4.win_sel('?'),'Kontrahenci'@,,,,,
              "exec('bobs_kh','control_marza')",,,,'maximized');
::TYPYSP.grp_edit(_okn_grp,XPERTIS,_red,'Pozostałe'@,,,,,'maximized');
TYPYSP.win_sel(_okn_grp);
TYPYSP.select();
VAR_DEL.delete('typ_edit','m4_def');
VAR_DEL.delete('TMPKHM4','TMPUM4','TMPMM4')


\bobs_mat_usl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Przed obsługą materiałów \ usług
::   WE: _a - 'T' - materiały
::            'U' - usługi
::----------------------------------------------------------------------------------------------------------------------
POMOC.RODZ:=_a;
exec('m4_zakres','control_marza');
cur_tab(1,0).first();
M.first();
typ_edit:=_a;
1


\log_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Typy dokumentow z systemu Logistyka - ustawianie zakresu i okienek
::   WE: _a - 'S' - typy dokumento sprzedazy
::            'Z' - typy dokumentow zakupu
::            'M' - typy dokumentow magazynowych
::  OLD: \log_dok/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='Z'
|| TYPYSP.win_edit('REDZ');
   TYPYSP.index('TYPYSP'); TYPYSP.prefix('T')
|? _a='S'
|| TYPYSP.win_edit('REDS');
   TYPYSP.index('TYPYSP'); TYPYSP.prefix('N')
|| TYPYDOK.win_edit('RED');
   TYPYDOK.index('TYP'); TYPYDOK.prefix()
?};
typ_edit:=_a


\con_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Akcja wyswietlajaca definicje pozycji controllingowych
::   WE: _a - zadeklarowane - ustawianie i kasowanie zmiennej typ_edit
::  OLD: \con_def/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','!ctr_pco_zldk');
{? _ || typ_edit:=_a ?}; _dalej:=0;
exec('source_index','!ctr_pco_zldk');
SKID_MBN.win_dict('SLO');
{? typ_edit='P'
|| ZMIENNE.SYSTEM:='KASA';
   {? POZOPER.PKONTROL='T'
   || CON_DEF.win_sel('WER'); CON_DEF.win_edit('RED');
      CON_DEF.hdr_sel(); CON_DEF.hdr_sel(': %1'@[POZOPER.KOD+' - '+POZOPER.NAZWA]);
      _dalej:=1
   || FUN.info('Transakcja nie zawiera podziałów dla controllingu.\nAkcja niedostępna.'@)
   ?}
|? typ_edit='A'
|| _dalej:=1;
   ZMIENNE.SYSTEM:='PRODUKCJA';
   CON_DEF.win_sel('WER'); CON_DEF.win_edit('RED');
   CON_DEF.hdr_sel(); CON_DEF.hdr_sel(': akord'@)
|? typ_edit='S' | typ_edit='Z' | typ_edit='M'
|| _dalej:=1;
   ZMIENNE.SYSTEM:='EMAG';
   {? typ_edit='S' | typ_edit='Z'
   || CON_DEF.win_sel('WER'); CON_DEF.win_edit('RED_LOG');
      CON_DEF.hdr_sel(); CON_DEF.hdr_sel(': %1'@[TYPYSP.T+' - '+TYPYSP.NAZ])
   || CON_DEF.win_sel('WER'); CON_DEF.win_edit('RED_LOG');
      CON_DEF.hdr_sel(); CON_DEF.hdr_sel(': %1'@[TYPYDOK.T+' - '+TYPYDOK.NAZ])
   ?}
|? typ_edit='T' | typ_edit='U'
|| M.cntx_psh(); M.index('MATKTM'); M.prefix();
   {? cur_tab(1,1).name()='material' | M.seek(cur_tab(1,0).REFM,M.name())
   || ZMIENNE.SYSTEM:='EMAG';
      CON_DEF.win_sel('WER_MU'); CON_DEF.win_edit('RED_MU');
      CON_DEF.hdr_sel(); CON_DEF.hdr_sel(': %1'@[M.N]);
      _dalej:=1
   || _txt:={? cur_tab(1,0)=TMPMM4
            || FUN.info('Nie znaleziono materiału %1.'@[TMPMM4.KTM])
            || FUN.info('Nie znaleziono usługi %1.'@[TMPUM4.KTM])
            ?}
   ?};
   M.cntx_pop()

?};
{? _dalej
|| _pln:=SALDO.PLN; _wal:=SALDO.WAL;
   CON_DEF.select();
   SALDO.PLN:=_pln; SALDO.WAL:=_wal
?};
ZMIENNE.SYSTEM:='';
{? _ || VAR_DEL.delete('typ_edit') ?}


\source_index
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Dziedzina dla rekordow tabeli CON_DEF
::  OLD: \source_index/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? typ_edit='P'
|| CON_DEF.index('LP'); CON_DEF.prefix(POZOPER.ref())
|? typ_edit='M'
|| CON_DEF.index('CON_DEF2'); CON_DEF.prefix(TYPYDOK.ref(),0)
|? (typ_edit='S' | typ_edit='Z')
|| CON_DEF.index('CON_DEF1'); CON_DEF.prefix(TYPYSP.ref(),0)
|? typ_edit='A'
|| CON_DEF.index('CON_DEF5'); CON_DEF.prefix('A')
|? typ_edit='T' | typ_edit='U'
|| CON_DEF.index('CON_DEF6'); CON_DEF.prefix(M.ref(),0)
?}


\bl_rodzaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Wartosc poczatkowa pola CON_DEF.RODZAJ
::  OLD: \bl_rodzaj/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
typ_edit


\bltypcondef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Wartosc poczatkowa pol CON_DEF.POZOPER, TYPYSP, TYPYDOK
::   WE: _a - 1 - pole CON_DEF.POZOPER
::            2 - pole CON_DEF.TYPYSP
::            3 - pole CON_DEF.TYPYDOK
::            4 - pole CON_DEF.M
::  OLD: \bltypcondef/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1 & typ_edit='P'
|| POZOPER.ref()
|? _a=2 & (typ_edit='S' | typ_edit='Z')
|| TYPYSP.ref()
|? _a=3 & typ_edit='M'
|| TYPYDOK.ref()
|? _a=4 & (typ_edit='T' | typ_edit='U')
|| M.ref()
|| null
?}


\bl_cd_lp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Wartosc poczatkowa pola CON_DEF.LP
::  OLD: \bl_cd_lp/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? (6+(-menu_txt()))='szukaj'
|| 0
|| _lp:=0;
   CON_DEF.cntx_psh();
   exec('source_index','!ctr_pco_zldk');
   _lp:={? CON_DEF.last() || CON_DEF.LP+1 || 1 ?};
   CON_DEF.cntx_pop();
   _lp
?}


\be_con_mb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2008]
:: OPIS: Formula przed redakcja pola CON_DEF.SKID_MB
::  OLD: \be_con_mb/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POM.SKID_MBN:=CON_DEF.SKID_MB


\ae_con_mb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2008]
:: OPIS: Formula po redakcji pola CON_DEF.SKID_MB
::  OLD: \ae_con_mb/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? CON_DEF.SKID_MB<>UD_POM.SKID_MBN
|| CON_DEF.JORG_S:=null; CON_DEF.JORG_F:=null;
   CON_DEF.OKOSZ_S:=null; CON_DEF.OKOSZ_F:=null;
   CON_DEF.PBUD_S:=null; CON_DEF.PBUD_F:=null;
   CON_DEF.WYM4_S:=null; CON_DEF.WYM4_F:=null;
   CON_DEF.WYM5_S:=null; CON_DEF.WYM5_F:=null;
   UD_POM.PBUD:=UD_POM.JORG:=UD_POM.OKOSZ:=UD_POM.WYM4:=UD_POM.WYM5:=''
?};
exec('set_enabled','!ctr_pco_zldk');
win_disp()


\bd_cd_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2008]
:: OPIS: Przed wyswietl dla pol z wymiarami
::   WE: _a - wymiar
::  OLD: \bd_cd_w/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? CON_DEF.M4_DEF=1 || return(1) ?};
{? _<1 || _a:=1 ?};
_ref_mb:=CON_DEF.SKID_MB;
SKID_MBP.cntx_psh();
SKID_MBP.index('LP'); SKID_MBP.prefix(_ref_mb,_a);
_zwrot:={? SKID_MBP.first()
        || ''
        || '192:192:192,192:192:192'
        ?};
SKID_MBP.cntx_pop();
_zwrot


\be_cx_zn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2008]
:: OPIS: Przed redakcja pol z wymiarami
::  OLD: \be_cx_zn/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_ref_mb:=CON_DEF.SKID_MB;
SKID_MBP.cntx_psh();
SKID_MBP.index('LP'); SKID_MBP.prefix(_ref_mb);
_zwrot:=((cur_afld()*'PBUD' & SKID_MBP.find_key(1)) |
         (cur_afld()*'JORG' & SKID_MBP.find_key(2)) |
         (cur_afld()*'OKOSZ' & SKID_MBP.find_key(3)) |
         (cur_afld()*'WYM4' & SKID_MBP.find_key(4)) |
         (cur_afld()*'WYM5' & SKID_MBP.find_key(5)));
SKID_MBP.cntx_pop();
_zwrot


\ae_cd_cx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Po redakcji pol typu Check boX okien tabeli CON_DEF
::  OLD: \ae_cd_cx/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()='T'
|| ($('CON_DEF.'+(3-cur_afld)+'_F:=null'))()
|| {? cur_afld='ZN_JORG' || UD_POM.JORG:=''
   |? cur_afld='ZN_PBUD' || UD_POM.PBUD:=''
   |? cur_afld='ZN_OKOSZ' || UD_POM.OKOSZ:=''
   |? cur_afld='ZN_WYM4' || UD_POM.WYM4:=''
   |? cur_afld='ZN_WYM5' || UD_POM.WYM5:=''
   || CON_DEF.KW_P:=0
   ?};
   ($('CON_DEF.'+(3-cur_afld)+'_S:=null'))()
?};
exec('set_enabled','!ctr_pco_zldk');
1


\be_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przed redakcja formulowych pol tabeli CON_DEF
::  OLD: \be_form/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_pole:=cur_afld()-2;
ZMIENNE.RODZAJ:='C';
{? _pole<>'KW' & ($('CON_DEF.ZN_'+_pole))()='T'
|| ($('CON_DEF.'+_pole+'_F:=null'))(); 0
|| 1
?}


\cd_stamp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Wartosc poczatkowa pola CON_DEF.TM_STAMP
::  OLD: \cd_stamp/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
CON_DEF.tm_stamp()


\bacondef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przed dolacz okna wertowania tabeli CON_DEF
::  OLD: \bacondef/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=CON_DEF.ref();
_lp:={? CON_DEF.get() || CON_DEF.LP || 1 ?};
_nza:=-menu_txt()='dołącz';
exec('set_pom','!ctr_pco_zldk',0); CON_DEF.blank();
{? _nza || CON_DEF.LP:=_lp ?};
{? exec('source_lock','!ctr_pco_zldk',1)
|| exec('set_enabled','!ctr_pco_zldk');
   {? CON_DEF.edit("exec('arcondef','!ctr_pco_zldk')")
   || do();
      {? _nza
      || CON_DEF.cntx_psh();
         exec('source_index','!ctr_pco_zldk');
         {? CON_DEF.last()
         || {! |?
               CON_DEF.LP+=1; CON_DEF.put();
               CON_DEF.ref()<>_ref & CON_DEF.prev()
            !}
         ?};
         CON_DEF.cntx_pop()
      ?};
      {? ~CON_DEF.add() || undo() ?};
      end()
   ?};
   exec('source_lock','!ctr_pco_zldk',2)
|| FUN.emsg('Definicje dla controllingu\nobsługuje inny operator.\nZabroniony dostęp.'@)
?}


\set_pom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Ustawia pola zmiennej UD_POM na podstawie tabel CON_DEF
::   WE: _a - 1 wypelnia z bufora  0 - czysci pola
::  OLD: \set_pom/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| UD_POM.JORG:={? CON_DEF.ZN_JORG='T' || CON_DEF.JORG_S().SYMBOL || '' ?};
   UD_POM.PBUD:={? CON_DEF.ZN_PBUD='T' || CON_DEF.PBUD_S().SYMBOL || '' ?};
   UD_POM.OKOSZ:={? CON_DEF.ZN_OKOSZ='T' || CON_DEF.OKOSZ_S().SYMBOL || '' ?};
   UD_POM.WYM4:={? CON_DEF.ZN_WYM4='T' || CON_DEF.WYM4_S().SYMBOL || '' ?};
   UD_POM.WYM5:={? CON_DEF.ZN_WYM5='T' || CON_DEF.WYM5_S().SYMBOL || '' ?}
|| UD_POM.JORG:=UD_POM.PBUD:=UD_POM.OKOSZ:=UD_POM.WYM4:=UD_POM.WYM5:=''
?}


\source_lock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Zablokowanie\odblokowanie tabali bazowej przy edycji tabeli CON_DEF
::   WE: _a - 1 - zablokowanie
::            2 - odblokowanie
::  OLD: \source_lock/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| (typ_edit='P' & POZOPER.r_lock(1,1,1)) |
   (typ_edit='M' & TYPYDOK.r_lock(1,1,1)) |
   ((typ_edit='S' | typ_edit='Z') & TYPYSP.r_lock(1,1,1)) |
   ((typ_edit='T' | typ_edit='U') & M.r_lock(1,1,1)) |
   (typ_edit='A')
|| {? typ_edit='P'
   || POZOPER.r_unlock()
   |? typ_edit='M'
   || TYPYDOK.r_unlock()
   |? typ_edit='S' | typ_edit='Z'
   || TYPYSP.r_unlock()
   |? typ_edit='T' | typ_edit='U'
   || M.r_unlock()
   ?}
?}


\arcondef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Na akcje 'Rekord po' okien tabeli CON_DEF
::  OLD: \arcondef/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? _zwrot='' & CON_DEF.SKID_MB=null
|| FUN.emsg('Nie wybrany model controllingowy.'@); _zwrot:='SKID_MB'
|| _ref_mb:=CON_DEF.SKID_MB
?};
{? _zwrot='' & (typ_edit='T' | typ_edit='U')
|| _ref:={? -menu_txt()='popraw' || CON_DEF.ref() || null ?};
   _dalej:=1;
   CON_DEF.cntx_psh();
   {? CON_DEF.first()
   || {! |?
         {? CON_DEF.SKID_MB=_ref_mb & {? -menu_txt()='popraw' || CON_DEF.ref()<>_ref || 1 ?}
         || FUN.emsg('Znaleziono definicję z wybranym modelem %1.'@[CON_DEF.SKID_MB().KOD]);
            _dalej:=0; _zwrot:='SKID_MB'
         ?};
         _dalej & CON_DEF.next()
      !}
   ?};
   CON_DEF.cntx_pop()
?};
{? _zwrot=''
|| SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(_ref_mb);
   VAR_DEL.delete('_wym','_naz','_typ');
   _wym:=obj_new(5); _naz:=obj_new(5); _typ:=obj_new(5);
   {! _i:=1..5
   |! {? SKID_MBP.find_key(_i)
      || _wym[_i]:=SKID_MBP.UD_SCH().SYMBOL;
         _naz[_i]:=-SKID_MBP.NAZ;
         _typ[_i]:=SKID_MBP.UD_SCH().UD_TYP().SYMBOL
      || _wym[_i]:=_naz[_i]:=_typ[_i]:=''
      ?}
   !};
   SKID_MBP.cntx_pop();
   {? ~(typ_edit='T' | typ_edit='U')
   || {? _wym[1]<>''
      || {? CON_DEF.ZN_PBUD='T' & UD_POM.PBUD=''
         || __CHK.err_empty(UD_POM.NAZ_WYM1+' - stała wartość'); _zwrot:='PBUD'
         |? CON_DEF.ZN_PBUD='N' & CON_DEF.PBUD_F=null
         || __CHK.err_empty(UD_POM.NAZ_WYM1+' - formuła'); _zwrot:='PBUD_F'
         ?}
      ?};
      {? _wym[2]<>'' & _zwrot=''
      || {? CON_DEF.ZN_JORG='T' & UD_POM.JORG=''
         || __CHK.err_empty(UD_POM.NAZ_WYM2+' - stała wartość'); _zwrot:='JORG'
         |? CON_DEF.ZN_JORG='N' & CON_DEF.JORG_F=null
         || __CHK.err_empty(UD_POM.NAZ_WYM2+' - formuła'); _zwrot:='JORG_F'
         ?}
      ?};
      {? _wym[3]<>'' & _zwrot=''
      || {? CON_DEF.ZN_OKOSZ='T' & UD_POM.OKOSZ=''
         || __CHK.err_empty(UD_POM.NAZ_WYM3+' - stała wartość'); _zwrot:='OKOSZ'
         |? CON_DEF.ZN_OKOSZ='N' & CON_DEF.OKOSZ_F=null
         || __CHK.err_empty(UD_POM.NAZ_WYM3+' - formuła'); _zwrot:='OKOSZ_F'
         ?}
      ?};
      {? _wym[4]<>'' & _zwrot=''
      || {? CON_DEF.ZN_WYM4='T' & UD_POM.WYM4=''
         || __CHK.err_empty(UD_POM.NAZ_WYM4+' - stała wartość'); _zwrot:='WYM4'
         |? CON_DEF.ZN_WYM4='N' & CON_DEF.WYM4_F=null
         || __CHK.err_empty(UD_POM.NAZ_WYM4+' - formuła'); _zwrot:='WYM4_F'
         ?}
      ?};
      {? _wym[5]<>'' & _zwrot=''
      || {? CON_DEF.ZN_WYM5='T' & UD_POM.WYM5=''
         || __CHK.err_empty(UD_POM.NAZ_WYM5+' - stała wartość.'); _zwrot:='WYM5'
         |? CON_DEF.ZN_WYM5='N' & CON_DEF.WYM5_F=null
         || __CHK.err_empty(UD_POM.NAZ_WYM5+' - formuła.'); _zwrot:='WYM5_F'
         ?}
      ?};
      {? _zwrot=''
      || {? CON_DEF.KW_P:=CON_DEF.KW_P$2; ~CON_DEF.KW_P
         || __CHK.err_empty('Procent kwoty'); _zwrot:='KW_P'
         |? CON_DEF.KW_P<0 | CON_DEF.KW_P>100
         || FUN.emsg('Nieprawidłowy procent kwoty. Dopuszczalne wartości większe od 0 i nie większe niż 100.'@);
            _zwrot:='KW_P'
         |? CON_DEF.KW_F=null
         || __CHK.err_empty('Kwota - formuła'); _zwrot:='KW_F'
         ?}
      ?}
   ?};
   {? _wym[1]<>'' & CON_DEF.ZN_PBUD='T'
   || CON_DEF.PBUD_S:={? UD_POM.PBUD<>''
                      || exec('szukaj_ud_skl','schemat',_typ[1],UD_POM.PBUD)
                      || null
                      ?};
      CON_DEF.PBUD_F:=null
   || CON_DEF.PBUD_S:=null
   ?};
   {? _wym[2]<>'' & CON_DEF.ZN_JORG='T'
   || CON_DEF.JORG_S:={? UD_POM.JORG<>''
                      || exec('szukaj_ud_skl','schemat',_typ[2],UD_POM.JORG)
                      || null
                      ?};
      CON_DEF.JORG_F:=null
   || CON_DEF.JORG_S:=null
   ?};
   {? _wym[3]<>'' & CON_DEF.ZN_OKOSZ='T'
   || CON_DEF.OKOSZ_S:={? UD_POM.OKOSZ<>''
                       || exec('szukaj_ud_skl','schemat',_typ[3],UD_POM.OKOSZ)
                       || null
                       ?};
      CON_DEF.OKOSZ_F:=null
   || CON_DEF.OKOSZ_S:=null
   ?};
   {? _wym[4]<>'' & CON_DEF.ZN_WYM4='T'
   || CON_DEF.WYM4_S:={? UD_POM.WYM4<>''
                      || exec('szukaj_ud_skl','schemat',_typ[4],UD_POM.WYM4)
                      || null
                      ?};
      CON_DEF.WYM4_F:=null
   || CON_DEF.WYM4_S:=null
   ?};
   {? _wym[5]<>'' & CON_DEF.ZN_WYM5='T'
   || CON_DEF.WYM5_S:={? UD_POM.WYM5<>''
                      || exec('szukaj_ud_skl','schemat',_typ[5],UD_POM.WYM5)
                      || null
                      ?};
      CON_DEF.WYM5_F:=null
   || CON_DEF.WYM5_S:=null
   ?}
?};
win_disp();
_zwrot


\bm_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Akcja przed dolacz, popraw okien tabeli CON_DEF
::  OLD: \bm_con/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('set_pom','!ctr_pco_zldk',-menu_txt='popraw'); 1


\bd_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Przed wyswietl okien tabeli CON_DEF
::  OLD: \bd_con/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('set_pom','!ctr_pco_zldk',1);
CON_DEF.display()


\bdcondef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przed usun okna wertowania tabeli CON_DEF
::  OLD: \bdcondef/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? CON_DEF.sel_size()
|| CON_DEF.del()
|? FUN.ask('Usunąć bieżący wiersz?'@)
|| {? exec('source_lock','!ctr_pco_zldk',1)
   || do();
      _l:=CON_DEF.size(); _lp:=CON_DEF.LP; CON_DEF.del();
      {? _l>1 & _lp<>_l
      || _ref:=CON_DEF.ref();
         {! |?
            CON_DEF.LP-=1; CON_DEF.put();
            CON_DEF.next()
         !};
         CON_DEF.seek(_ref)
      ?};
      end();
      exec('source_lock','!ctr_pco_zldk',2)
   || FUN.emsg('Definicje dla controllingu\nobsługuje inny operator. Zabroniony dostęp'@)
   ?}
?}


\bgdcon_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Grupa przed usun okna wertowania tabeli CON_DEF
::  OLD: \bgdcon_d/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?','Uwaga!')
|| {? exec('source_lock','!ctr_pco_zldk',1)
   || 1
   || FUN.emsg('Definicje dla controllingu\nobsługuje inny operator. Zabroniony dostęp'@); 0
   ?}
?}


\agdcon_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Grupa po usun okna wertowania tabeli CON_DEF
::  OLD: \agdcon_d/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=CON_DEF.ref();
do();
{? CON_DEF.first()
|| _lp:=0;
   {! |?
      CON_DEF.LP:=_lp+=1; CON_DEF.put();
      CON_DEF.next()
   !}
?};
end();
exec('source_lock','!ctr_pco_zldk',2);
CON_DEF.seek(_ref)


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Na akcje szukaj okien tabeli CON_DEF
::  OLD: \szukaj/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('set_pom','!ctr_pco_zldk',0); UD_POM.JORG:='';
CON_DEF.blank(1);
CON_DEF.ZN_PBUD:=CON_DEF.ZN_JORG:=CON_DEF.ZN_OKOSZ:='';
{? CON_DEF.edit() & ~CON_DEF.find_rec(0)
|| FUN.emsg('Nie znaleziono rekordu zgodnego ze wzorcem.'@)
?}


\br_con_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Rekord przed dla typow dokumentow z logistyki
::  OLD: \typ_control/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
exec('rek_typydok','ustawienia');
{? var_pres('typ_edit')>0
|| {? typ_edit='S' | typ_edit='Z'
   || CON_DEF.index('CONDEF13'); CON_DEF.prefix(0,TYPYSP.ref())
   || CON_DEF.index('CONDEF14'); CON_DEF.prefix(0,TYPYDOK.ref())
   ?};
   {? CON_DEF.first() || _zwrot:='CON_DEF#02#01' ?}
|| ''
?};
_zwrot


\set_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia formuły zmiennych
::----------------------------------------------------------------------------------------------------------------------
{! _lp:=1..5
|! exec('set_fml','#field',UD_POM,'NAZ_WYM'+$_lp,"
      _nr:=#(cur_afld()+1);
      fld(exec('dim_name','control',CON_DEF.SKID_MB,_nr))
   ")
!};
exec('set_fml','#field',UD_POM,'PBUD',
   "  UD_POM.PBUD:=CON_DEF.PBUD_S().SYMBOL;
      ~~
   ",
   "exec('be_dim','control',CON_DEF.SKID_MB,1)",
   "exec('f3_dim','control',CON_DEF.SKID_MB,1)",
   "  {? fld()='' || CON_DEF.PBUD_S:=null; return(1) ?};
      _ref:=exec('ae_dim','control',CON_DEF.SKID_MB,1);
      {? _ref
      || CON_DEF.PBUD_S:=_ref;
         fld(CON_DEF.PBUD_S().SYMBOL);
         1
      ?}
   "
);
exec('set_fml','#field',UD_POM,'JORG',
   "UD_POM.JORG:=CON_DEF.JORG_S().SYMBOL; ~~",
   "exec('be_dim','control',CON_DEF.SKID_MB,2)",
   "exec('f3_dim','control',CON_DEF.SKID_MB,2)",
   "  {? fld()='' || CON_DEF.JORG_S:=null; return(1) ?};
      _ref:=exec('ae_dim','control',CON_DEF.SKID_MB,2);
      {? _ref
      || CON_DEF.JORG_S:=_ref;
         fld(CON_DEF.JORG_S().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'OKOSZ',
   "UD_POM.OKOSZ:=CON_DEF.OKOSZ_S().SYMBOL; ~~",
   "exec('be_dim','control',CON_DEF.SKID_MB,3)",
   "exec('f3_dim','control',CON_DEF.SKID_MB,3)",
   "  {? fld()='' || CON_DEF.OKOSZ_S:=null; return(1) ?};
      _ref:=exec('ae_dim','control',CON_DEF.SKID_MB,3);
      {? _ref
      || CON_DEF.OKOSZ_S:=_ref;
         fld(CON_DEF.OKOSZ_S().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'WYM4',
   "UD_POM.WYM4:=CON_DEF.WYM4_S().SYMBOL; ~~",
   "exec('be_dim','control',CON_DEF.SKID_MB,4)",
   "exec('f3_dim','control',CON_DEF.SKID_MB,4)",
   "  {? fld()='' || CON_DEF.WYM4_S:=null; return(1) ?};
      _ref:=exec('ae_dim','control',CON_DEF.SKID_MB,4);
      {? _ref
      || CON_DEF.WYM4_S:=_ref;
         fld(CON_DEF.WYM4_S().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'WYM5',
   "UD_POM.WYM5:=CON_DEF.WYM5_S().SYMBOL; ~~",
   "exec('be_dim','control',CON_DEF.SKID_MB,5)",
   "exec('f3_dim','control',CON_DEF.SKID_MB,5)",
   "  {? fld()='' || CON_DEF.WYM5_S:=null; return(1) ?};
      _ref:=exec('ae_dim','control',CON_DEF.SKID_MB,5);
      {? _ref
      || CON_DEF.WYM5_S:=_ref;
         fld(CON_DEF.WYM5_S().SYMBOL)
      ?}
   "
);
~~


\set_enabled
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia dostępność pól
::   WE: [_a] - akronim okna
::----------------------------------------------------------------------------------------------------------------------
_okno:=CON_DEF.win_edit('?');
SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(CON_DEF.SKID_MB);
{! _nr:=1..5
|! _jest:=SKID_MBP.find_key(_nr);
   _pole:={? _nr=1 || 'PBUD'
          |? _nr=2 || 'JORG'
          |? _nr=3 || 'OKOSZ'
          |? _nr=4 || 'WYM4'
          |? _nr=5 || 'WYM5'
          || ''
          ?};
   _stala:=($('CON_DEF.ZN_'+_pole))()='T';
   _form:=($('CON_DEF.ZN_'+_pole))()='N';
   CON_DEF.efld_opt(_okno,'enable='+$(_jest & _stala),UD_POM,_pole);
   CON_DEF.efld_opt(_okno,'enable='+$(_jest & _stala),CON_DEF,_pole+'_S');
   CON_DEF.efld_opt(_okno,'enable='+$(_jest & _form),CON_DEF,_pole+'_F');
   CON_DEF.efld_opt(_okno,'enable='+$_jest,UD_POM,'NAZ_WYM'+$_nr);
   {? typ_edit='T' | typ_edit='U'
   || CON_DEF.efld_opt(_okno,'mark=0',UD_POM,_pole);
      CON_DEF.efld_opt(_okno,'mark=0',CON_DEF,_pole+'_F')
   || CON_DEF.efld_opt(_okno,'mark='+$(_jest & _stala),UD_POM,_pole);
      CON_DEF.efld_opt(_okno,'mark='+$(_jest & _form),CON_DEF,_pole+'_F')
   ?}
!};
SKID_MBP.cntx_pop();
~~


:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:11 c1bb6b403db13e1706568c96f36acfee3b98deb707b12fa52c283a596caabc81fd606f4f948fb78e8d34b946daf9c3a0a436c5956b96e5c34a2be4ec65eea9ba4139742b1da6af3064b03f9991e7d61b93672f91b13f9f604a4e7e2f83206d6da4c15353908dbf1fb9883e98227fc14fe2275770a852711989793709f4f6e840

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_sms_send.fml
:: Utworzony: 08.02.2022
:: Autor: PD
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_SMS_SEND - Wysyłanie smsów poprzez SMSAPI
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Wysyłanie SMS - główna formuła czynności ZWS_SMS_SEND
::----------------------------------------------------------------------------------------------------------------------

:: UWAGA: Czynność wysłania uruchomiona automatycznie po czynności serwisowej
::        zostanie uruchomiona niejawnie w trybie serwisowym.
::# properties=SEND,GRP_FIRM

:: PARAMETRY WE:
::# kind=WE, symbol=FROM,    type=STRING, name=Autor,                required=N
::# kind=WE, symbol=TO,      type=STRING, name=Adresat,              required=N, fml_val="exec('edit_phone_nr','#mailbox',_a,_b)"
::# kind=WE, symbol=MESSAGE, type=MEMO,   name=Wiadomość,            required=N
::# kind=WE, symbol=FLASH,   type=STRING, name=Wiadomość typu flash, required=N
::# kind=WE, symbol=FAST,    type=STRING, name=Wiadomość typu fast,  required=N
::# kind=WE, symbol=DATE,    type=DATE,   name=Data wysłania,        required=N
::# kind=WE, symbol=TIME,    type=TIME,   name=Czas wysłania,        required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_error:=0;
_uid:='ZWS_SMS_SEND';

{? var_pres('FROM',_in)<0    || _error:=1 ||  {? var_pres('FROM',_in)<>type_of('')      || _in.FROM:=exec('get','#params',700021) ?} ?};
{? var_pres('TO',_in)<0      || _error:=1 ||  {? var_pres('TO',_in)<>type_of('')        || _in.TO:='' ?} ?};
{? var_pres('MESSAGE',_in)<0 || _error:=1 ||  {? var_pres('MESSAGE',_in)<>type_of('')   || _in.MESSAGE:='' ?} ?};
{? var_pres('FLASH',_in)<0   || _error:=1 ||  {? var_pres('FLASH',_in)<>type_of('')     || _in.FLASH:='0' ?} ?};
{? var_pres('FAST',_in)<0    || _error:=1 ||  {? var_pres('FAST',_in)<>type_of('')      || _in.FAST:='0' ?} ?};
{? var_pres('DATE',_in)<0    || _error:=1 ||  {? var_pres('DATE',_in)<>type_of(date())  || _in.DATE:=date() ?} ?};
{? var_pres('TIME',_in)<0    || _error:=1 ||  {? var_pres('TIME',_in)<>type_of(time())  || _in.TIME:=time() ?} ?};

{? exec('get','#params',700020,2)=''
|| _msg:='Brak wypełnionego parametru\n700020 - Token SMSAPI'@;
   {? (_mp.isAutoRun() | _mp.isService()) & ~_mp.pathTodo()
   || _mp.error(_msg)
   || FUN.info(_msg)
   ?};
   return(~~)
?};
{? _error
|| _msg:='Należy zaktualizować czynność %1.'@[_uid];
   {? (_mp.isAutoRun() | _mp.isService()) & ~_mp.pathTodo()
   || _mp.error(_msg)
   || FUN.info(_msg)
   ?};
   return(~~)
?};
::Modyfikacja numeru zgodnie z dokumentacją
_in.TO:=gsub(_in.TO,'-','');
_in.TO:=gsub(_in.TO,'+','');
_in.TO:=gsub(_in.TO,' ','');

{? (_in.TO='' | _in.MESSAGE='' | _in.FROM='') & ((_mp.isAutoRun() | _mp.isService())& ~_mp.pathTodo())
|| _mp.error('Brak wymaganych parametrów'@);
   return(~~)
|? (_in.TO='' | _in.MESSAGE='' | _in.FROM='' | _mp.pathTodo())
   & (type_of(_window:=exec('sms_window','!zws_sms_send',_in))=0)
|| _mp.error('Brak wymaganych parametrów'@);
   return(~~)
?};

_wyn:=exec('send_sms','#sms_send',
      _in.TO,
      _in.MESSAGE,
      _in.FROM,
      _in.FLASH,
      _in.FAST,
      _in.DATE,
      _in.TIME
      );

{? type_of(json_parse(_wyn))=117
& var_pres('list', json_parse(_wyn))>0
& obj_len(json_parse(_wyn).list)>0
& var_pres('error', json_parse(_wyn).list[1])=0
|| FUN.info('Wiadomość została wysłana.'@)
|| _msg:='Wystąpiły błędy przy wysyłce wiadomości.\nWięcej informacji w logach.'@;
   {? (_mp.isAutoRun() | _mp.isService()) & ~_mp.pathTodo()
   || _mp.error(_msg)
   || FUN.info(_msg)
   ?};
   return(~~)
?};

_mp.done();
~~


\sms_window
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Edycja parametrów
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;

_tab:=tab_tmp(1,'TO',      'STRING[20]',  'Do'@,
               'MESSAGE',  'SYS_MEMO',    'Wiadomość'@,
               'FROM',     'STRING[100]', 'Od'@,
               'FLASH',    'STRING[1]',   'Wiadomość typu Flash'@,
               'FAST',     'STRING[1]',   'Wiadomość typu Fast'@,
               'DATE',     'DATE',        'Data'@,
               'TIME',     'TIME',        'Czas'@
);

_tab.TO:=      _in.TO;
_tab.FROM:=    _in.FROM;
_tab.FLASH:=   _in.FLASH;
_tab.FAST:=    _in.FAST;
_tab.DATE:=    _in.DATE;
_tab.TIME:=    _in.TIME;
_tab.memo_set(_in.MESSAGE);
_tab.add();

_edit:=_tab.mk_edit('Wiadomość SMS'@,,,,,'normal');

_tab.win_esep(_edit,'Dane podstawowe'@);
_tab.win_efld(_edit,,'TO',,,20);
_tab.win_efld(_edit,,'MESSAGE',,,50,-3);
_tab.win_efld(_edit,,'FROM',,,20);
_tab.win_efld(_edit,,'FLASH',,,,,,,,,'check-box',,"'1'","'0'");
_tab.win_efld(_edit,,'FAST',,,,,,,,,'check-box',,"'1'","'0'");
_tab.win_efld(_edit,,'DATE',,,20);
_tab.win_efld(_edit,,'TIME',,,23);

_tab.efld_opt(_edit,'mark=1',,'TO');
_tab.efld_opt(_edit,'mark=1',,'MESSAGE');
_tab.efld_opt(_edit,'mark=1',,'FROM');
_tab.efld_opt(_edit,'mark=1',,'DATE');
_tab.efld_opt(_edit,'mark=1',,'TIME');
exec('ok_esc','#window',_tab,_edit);
_tab.win_edit(_edit);

_validate:="
   _tab:=cur_tab();
   {? #_tab.TO<9            || FUN.info('Nieprawidłowy numer.'@); return('TO') ?};
   {? _tab.memo_txt()=''    || FUN.info('Brak treści wiadomości.'@); return('MESSAGE') ?};
   {? +_tab.memo_txt()>670  || FUN.info('Treść wiadomości nie powinna\nmieć więcej niż 670 znaków.'@); return('MESSAGE')?};
   {? _tab.FROM=''          || FUN.info('Brak nadawcy.'@); return('FROM') ?};
   1
";

{? _tab.edit(_validate)
||
   _in.TO:=       _tab.TO;
   _in.FROM:=     _tab.FROM;
   _in.FLASH:=    _tab.FLASH;
   _in.FAST:=     _tab.FAST;
   _in.DATE:=     _tab.DATE;
   _in.TIME:=     _tab.TIME;
   _in.MESSAGE:=  _tab.memo_txt();
   _in
||
   ~~
?}


\chk_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Sprawdzenie uprawnień
::----------------------------------------------------------------------------------------------------------------------
exec('chk_role','#b__box',OPERATOR.USER,'ZWS_SMS_SEND')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 a8673aaf3f8aa2d899244624e8e0801e93d36899dc51476bdea12260709c698f4e2acfda5ce8b11ad9217445b1bd25b454184542da0e218259d6a611d6485cb35a1edb7272660b0d4cfa436951ea8b17b77e0a3d5b51e62531d344babfb80b0a94d88814b266e5206d1fb396fc18706bba2f83dfc879fd4d5442f3241458370a

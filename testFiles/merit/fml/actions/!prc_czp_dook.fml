:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_dook.fml
:: Utworzony: 09.11.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_DOOK - Otworzenie zamkniętego okresu.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Otworzenie zamkniętego okresu rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();
_result:='';

_uid:=exec('ref2uid','#table',_in.A_OKR);
{? _uid=''
|| _result:=exec('error','!prc_czp_dook','Nie znaleziono okresu rozliczeniowego.'@)
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uid);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uid);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('otworz_okr','!prc_czp_dook',_in.A_OKR);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value=2
      || _mp.keep()
      || _mp.done()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Otworzenie zamkniętego okresu rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_tab:=params_exec('opis_okr','okres',_mp.load(exec('kind_in','#b_port')).A_OKR);

{? _tab.ZAW_DANE='T'
|| 'Otwórz okres do ponownego rozliczenia %1 %2 %3'@@[_tab.A_OKRN_NAZWA,_tab.OD,_tab.DO]
|| 'Otwórz okres do ponownego rozliczenia'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Informacja o rodzaju błędu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Otworzenie okresu rozliczeniowego nie jest możliwe.'@,_a]


\opis_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Opis wybranego okresu rozliczeniowego.
::   WE: _a - [REFERENCE] Wskazanie na wybrany okres rozliczeniowy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_okr:='';
A_OKR.cntx_psh();
{? A_OKR.seek(_a,,1)
|| _okr:='%1 %2 %3'@[A_OKR.NAZ().NAZ,A_OKR.OD$1,A_OKR.DO$1]
?};
A_OKR.cntx_pop();
_okr


\otworz_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a - [REFERENCE] Wskazanie na okres rozliczeniowy.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
A_OKR.cntx_psh();
{? A_OKR.seek(_a,,1)
|| {? A_OKR.S='Z'
   || _tab:=tab_tmp(,'RESULT','INTEGER','');
      _ed:=_tab.mk_edit('Okres: %1'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1],0);
      _tab.win_efld(_ed,AH,'H',,,,,,'Okres rozliczeniowy zostanie otworzony.'@);
      exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:Esc'",,
         exec('help_red_zakoncz','#window','PKD_E'),exec('text_red_zakoncz','#window','PKD_E'));
      exec('ok_esc','#window',_tab,_ed,1,,"cur_tab().RESULT:=0; 'key:Esc'",0,,
         exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
      _tab.win_edit(_ed);
      _tab.edit();
      {? _tab.RESULT=1
      || exec('a_okr_wer_bfro','okres',0);
         A_OKR.seek(_a,,1);
         _result:=A_OKR.S='O'
      || _result:=2
      ?};
      obj_del(_tab)
   || _result:=exec('error','!prc_czp_dook','Okres rozliczeniowy nie został jeszcze zamknięty.'@)
   ?}
|| _result:=exec('error','!prc_czp_dook','Nie znaleziono okresu rozliczeniowego.'@)
?};
A_OKR.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 4a77ce15d698116acebf0c0b0e8684d8cd141025a016a04f8dbf93ad53ca7c8e24a143afbd3ab48e3e1ba5aeb7ab075490353c93bacd4981663bf12832399c937e52462e59956f143b696e0b1185b2d8894646f11dd4516b9e84cf1ae0e5814485176e4908003806e05ac5eacbf470ce50a7e4565b4ecb503161a0170730c5d6

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_pzl_dtsz.fml
:: Utworzony: 09.06.2016
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_PZL_DTSZ - Zmiana surowca zlecenia
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności zmiany surowca zlecenia (TTE_PZL_DTSZ)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=ZL, type=_ZL, name=Wskazanie na zlecenie, required=N, keyref=T
{? var_pres('ZL',_in)<>type_of(~~) & var_pres('ZL',_in)<>type_of(null())
|| _msg:='Błędny parametr wejściowy \'%1\' dla czynności %2.'@@['ZL','TTE_PZL_DTSZ'];
   FUN.error(_msg);
   _mp.error(_msg);
   return()
|? _in.ZL=~~
|| _in.ZL:=null()
?};

:: PARAMETRY WY:
::# kind=WY, symbol=TMAT, type=_TMAT, name=Surowiec technologii zlecenia, required=N
{? var_pres('TMAT',_out)<>type_of(~~) & var_pres('TMAT',_out)<>type_of(null()) || return() ?};
::# kind=WY, symbol=GEN_ZL, type=STRING, name="Czy można generować zlecenie półfabrykatowe (T, N)", required=N
{? var_pres('GEN_ZL',_out)<>type_of(~~) & var_pres('GEN_ZL',_out)<>type_of('') || return() ?};
::# kind=WY, symbol=ZL, type=_ZL, name=Wskazanie na zlecenie, required=N
{? var_pres('ZL',_out)<>type_of(~~) & var_pres('ZL',_out)<>type_of(null()) || return() ?};

_clean_result:=params_exec('clean','!tte_pzl_dtsz',_mp,_in);
{? ~_clean_result.RESULT
|| return()
?};

{? _in.ZL
|| {? var_pres('_context')>100 & var_pres('env_mater',_context)>100
   || params_set('env_mater',_context.env_mater)
   || params_set('env_mater',exec('env_mater','tech_mater'))
   ?};

   {? _mp.akcja()='POPRAW'
   || _ok:=exec('modify','!tte_pzl_dtsz',_context.TMAT);
      {? _ok
      || _out.TMAT:=_context.TMAT;
         _out.GEN_ZL:={? exec('tmat4zl','tech_mater',_context.TMAT) || 'T' || 'N' ?};
         _out.ZL:=_in.ZL;
         _mp.save(,_out);
         _mp.done()
      ?}

   |? _mp.akcja()='ZMIEŃ'
   || _ok:=exec('change','!tte_pzl_dtsz',_context.TMAT);
      {? _ok
      || _out.TMAT:=_context.TMAT;
         _out.GEN_ZL:={? exec('tmat4zl','tech_mater',_context.TMAT) || 'T' || 'N' ?};
         _out.ZL:=_in.ZL;
         _mp.save(,_out);
         _mp.done()
      ?}

   || _msg:='Błąd konfiguracji procesu — nieobsługiwany kontekst wywołania czynności.'@@;
      FUN.emsg(_msg);
      _mp.error(_msg)
   ?}

|? _mp.pathProc()
|| ZL.cntx_psh();
   ZL.clear();
   _join:='join ZTP';
   _where:=
      'ZL.STAN=''O'' and '
      'ZTP.WP=''P'' and '
      'ZTP.TECH=''T'' and '
      'ZL.RODZAJ=''P''';
   ZL.f_set('SYM',_join,_where);
   exec('icons_slo_sl','zl_head');
   ZL.win_sel('SLO_SL');
   ZL.actions('SLO_SL','XY','W');
   {? ZL.select()
   || _env:=exec('env','zl_view');
      _env.ZL:=ZL.ref();
      params_set('env',_env);
      exec('tktl_cntx_psh','tech_common');
      exec('tktl_use','tech_common',(8+ZL.RTKTL)+3);
      {? ZL.RTKTL<>'' & TKTL.seek(ZL.RTKTL)
      || VAR.A_TPKTL:=TKTL.TYP;
         VAR.GRUPA:='N';
         _used:=0;
::       Wyświetlane zawsze wszystkie (bez prefixu)
         {? 1
::            TKTL.TYP().SUR='K'
         || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,_used,0)
         || exec('menu_start','tech_head');
            exec('tree','tech_oper',4,~_used);
            exec('menu_stop','tech_head')
         ?}
      ?};
      exec('tktl_cntx_pop','tech_common')
   ?};
   ZL.f_clear();
   ZL.cntx_pop()

|| _msg:='Błąd konfiguracji procesu — parametr %1 musi być podany.'@@['ZL'];
   FUN.emsg(_msg);
   _mp.error(_msg)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności zmiany surowca zlecenia (TTE_PZL_DTSZ)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));
_sym:='';
_ile:=0;
:: jest rekord kluczowy ZL, to ustawiam odpowiednie ZL
{? var_pres('[1]',_keyRefs)>0 & ref_name(_keyRefs[1])=ZL.name()
|| _sym:=exec('FindAndGet','#table',ZL,_keyRefs[1],,"SYM",'')
:: jest parametr wejściowy ZL
|? var_pres('ZL',_in)>0
|| _sym:=exec('FindAndGet','#table',ZL,_in.ZL,,"SYM",'')
?};
{? _sym<>''
|| _desc:='Popraw surowiec do technologii zlecenia %1'@@[_sym]
|| _desc:='Popraw surowiec do technologii zlecenia'@@
?};
_desc


\action_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Akcja 'Modyfikacje -> Popraw' w oknie tabeli TMAT
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;

{? exec('chk_napraw_or_split','zl_common',_env.ZL)=0
|| _env_mater:=params_get().env_mater;

   _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_PZL_DTSZ';
   _args.UIDREF:=exec('FindAndGet','#table',ZL,_env.ZL,,"uidref()",'');
   _args.AKCJA:='POPRAW';
   _args.PROC_START:='T';
   _args.CONTEXT:=obj_new('TMAT','env_mater'); _args.CONTEXT.TMAT:=TMAT.ref(); _args.CONTEXT.env_mater:=_env_mater;
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);

   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'ZL',_env.ZL);

   exec('mp_run','#b__box',_args);

:: Reinicjowanie zmiennej tpar
   exec('start_tpar','tech_param',_env.BUF_ZL.KTM,_env.TKTL_ZL)
?};
~~


\action_change
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Akcja 'Modyfikacje -> Zmień' w oknie tabeli TMAT
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;

{? exec('chk_napraw_or_split','zl_common',_env.ZL)=0
|| _env_mater:=params_get().env_mater;

   _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_PZL_DTSZ';
   _args.UIDREF:=exec('FindAndGet','#table',ZL,_env.ZL,,"uidref()",'');
   _args.AKCJA:='ZMIEŃ';
   _args.PROC_START:='T';
   _args.CONTEXT:=obj_new('TMAT','env_mater'); _args.CONTEXT.TMAT:=TMAT.ref(); _args.CONTEXT.env_mater:=_env_mater;
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);

   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'ZL',_env.ZL);

   exec('mp_run','#b__box',_args);

:: Reinicjowanie zmiennej tpar
   exec('start_tpar','tech_param',_env.BUF_ZL.KTM,_env.TKTL_ZL)
?};
~~


\modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Poprawia surowiec do technologii zlecenia
::   WY: 1 / 0
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

KOMM.init(,,'Modify');
_result:=0;

_zl:=exec('FindAndGet','#table',TKTL,VAR.A_KTL,,"ZL",null());

{? exec('zl_lock','zl_common',#_zl,'T') &
   exec('zl_lock','zl_common',#_zl,'L')
|| {? exec('ptmatpop','tech_mater',1)
   || {? TMAT.edit("params_exec('tchm_edt','tech_mater',TMAT,0)")
      || {? TMAT.put()
         || exec('tmattpop','tech_mater');
            _result:=1
         ?}
      ?}
   ?}
?};
exec('zl_unlock','zl_common',#_zl,'T');
exec('zl_unlock','zl_common',#_zl,'L');

::KOMM.select();
_result


\change
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zmienia surowiec w technologii zlecenia
::   WY: 1 / 0
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

KOMM.init(,,'Change');
_result:=0;

_zl:=exec('FindAndGet','#table',TKTL,VAR.A_KTL,,"ZL",null());

{? exec('zl_lock','zl_common',#_zl,'T') &
   exec('zl_lock','zl_common',#_zl,'L')
||
   {? exec('tmatchng','tech_mater')
   || _result:=1
   ?}
?};
exec('zl_unlock','zl_common',#_zl,'T');
exec('zl_unlock','zl_common',#_zl,'L');

::KOMM.select();
_result


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zl_clean','zl_common',_mp,_in,'ERROR')

:Sign Version 2.0 jowisz:1045 2023/09/29 13:52:56 b7c665f0f585796169cc3a00f21431006ef2f7c2c0fd928028b305a3d60c3a9245e10d75095cca28da51e81a6ce6a7ff00bfdf00cffb5af8c72a4299b45cfc5793d0ab1bb6f0f25a46e853754bc2ace266c4ab8b443e66cf125e0bef17071fed8fbe5c6e3eae6d29c6d9f14ed2c4b31cf46942626076c30da5fbc10fee8ea393

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obg_fzo_flzk.fml
:: Utworzony: 27.07.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBG_FZO_FLZK - Tworzenie faktury w obiegu na podstawie dokumentu zakupu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Tworzenie faktury w obiegu na podstawie dokumentu zakupu - główna formuła czynności OBG_FZO_FLZK
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,ODDZ,LZK

:: PARAMETRY WE:
::# kind=WE, symbol=FAKS, type=_FAKS, name=Wskazanie na nagłówek dokumentu zakupu, required=N, keyref=T
::# kind=WE, symbol=ETYPY, type=STRING, name=Wskazanie na typ obiegu, required=N, fml_val="exec('typ_obi','obiegi_log','Z',_a)", fml_exp="exec('etypy_fml_exp','obiegi2',1,_a)"
::# kind=WE, symbol=WG_DATY, type=STRING, name=Utworzenie dokumentu wg daty, required=N, fml_val="exec('obi_wg_daty','obiegi_log',_a,'Z')"
::# kind=WE, symbol=ODD, type=STRING, name=Domyślna jednostka księgowa, required=N, fml_val="exec('select_odd','obiegi_log',_a)", fml_exp="exec('odd_fml_exp','obiegi_log',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=FAKS, type=_FAKS, name=Wskazanie na nagłówek dokumentu zakupu, required=N, keyref=T

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE,~RUNMICRO

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_service:=_mp.isService();
_auto:=_mp.isAutoRun();

_dalej:=1;
{? _service & (_we.FAKS=~~ | _we.FAKS=null | _we.ETYPY=~~ | _we.ETYPY='')
|| _wy.EDOKUM:=_wy.FAKS:=null;
   _mp.save(,_wy);
   _mp.done();
   _dalej:=0
?};
_faks:=_etypy:=null;
{? _dalej=1
|| {? ~_service
   || {? _akcja='OBSZAR'
      || params_get().context.FAKS; _faks:=FAKS.ref()
      |? _we.FAKS=~~ | _we.FAKS=null
      || _faks:=exec('faks_wybor','obiegi_log','Z');
         {? _faks=null || _dalej:=-1 ?}
      || _faks:=_we.FAKS
      ?}
   || _faks:=_we.FAKS
   ?};
   {? _faks
   || _wy.FAKS:=_faks; _mp.save(,_wy); _mp.descTodo()
   ?}
?};
{? _dalej=1
|| {? ~_service
   || ETYPY.cntx_psh(); ETYPY.index('LOG'); ETYPY.prefix(exec('bl_typ','obiegi',1),'Z');
      {? _we.ETYPY=~~ | _we.ETYPY='' | ~ETYPY.find_key(_we.ETYPY)
      || ETYPY.win_edit('RED'); ETYPY.win_sel('SLO_WYB');
         ETYPY.hdr_sel(''); ETYPY.hdr_sel('Typy faktur w obiegu'@);
         {? ETYPY.select() || _etypy:=ETYPY.ref() ?}
      |? ETYPY.find_key(_we.ETYPY)
      || _etypy:=ETYPY.ref()
      ?};
      {? ~_etypy || _dalej:=-1 ?};
      ETYPY.cntx_pop()
   || ETYPY.cntx_psh(); ETYPY.index('LOG'); ETYPY.prefix(exec('bl_typ','obiegi',1),'Z');
      {? ETYPY.find_key(_we.ETYPY)
      || _etypy:=ETYPY.ref()
      || _dalej:=0
      ?};
      ETYPY.cntx_pop()
   ?}
?};
{? _dalej=1
|| _ref:=BB.sqlint($_faks); _nam:=form(($_faks)-8);
   {? _ref<>0 & _nam<>''
   || FAKS.cntx_psh(); FAKS.use(_nam); FAKS.prefix();
      {? FAKS.seek(_ref,_nam)
      || {? FAKS.OBI_POW<>''
         || {? ~_service
            || FUN.info('Dla dokumentu zakupu został wcześniej uruchomiony obieg.'@)
            || _wy.EDOKUM:=_wy.FAKS:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         |? FAKS.STAT_REJ='T' | FAKS.STAT_REJ='Z'
         || {? _akcja='OBSZAR' | _mp.pathProc() | _mp.pathTodo() | _mp.isAutoRun()
            || menu_txt(,'Dołącz');
               exec('faks_win_edit_set','obiegi_log');
               {? _service
               || BtnObi:=1
               || BtnObi:=0;
                  FAKS.display()
               ?};
               {? BtnObi
               || _edokum:=params_exec('faks_edokum','obiegi_log','Z',_etypy,~_service);
                  {? _edokum
                  || _wy.EDOKUM:=_edokum;
                     _wy.FAKS:=FAKS.ref();
                     _mp.save(,_wy);
                     _mp.done()
                  ?}
               ?}
            ?}
         || {? FAKS.STAT_REJ='A'
            || {? ~_service || FUN.info('Dokument zakupu został anulowany.'@) ?};
               _wy.EDOKUM:=_wy.FAKS:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      || _wy.EDOKUM:=_wy.FAKS:=null;
         _mp.save(,_wy);
         _mp.done()
      ?};
      FAKS.cntx_pop()
   || _wy.EDOKUM:=_wy.FAKS:=null;
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _dalej=0
|| _wy.EDOKUM:=_wy.FAKS:=null;
   _mp.save(,_wy);
   _mp.done()
|? _dalej=-1
|| _mp.cancel()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| 'Zakończ rejestrację faktury w obiegu na podstawie dokumentu zakupu: %1'@@[exec('record','#to_string',_in.FAKS)]
|? var_pres('FAKS',_out)=type_of(null()) & _out.FAKS
|| 'Zakończ rejestrację faktury w obiegu na podstawie dokumentu zakupu: %1'@@[exec('record','#to_string',_out.FAKS)]
|| 'Zakończ rejestrację faktury w obiegu na podstawie dokumentu zakupu'@@
?}


\obieg_obszar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Uruchomienie obiegu dokumentu z obszaru
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='OBG_FZO_FLZK';
_params.AKCJA:='OBSZAR';
_params.PROC_START:='T';
_params.UIDREF:=FAKS.uidref();
_params.CONTEXT:=obj_new('FAKS');
_params.CONTEXT.FAKS:=FAKS.ref();
exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 81ff7815aa43e790fb3574e6674e60b2b2bb13b70c630145a842e0918deb4a659e1d28d131d309be9f62dfac26239bf063ebcd41efd3c1a778d94b6c519e589de5bde528209c1f9ae01ae801f03b4a4a761e70521a4daf601cd3361ca8948899143f8479a3993877d531af4a9c9cacab071ad389442797541e83036dbe77a6b9

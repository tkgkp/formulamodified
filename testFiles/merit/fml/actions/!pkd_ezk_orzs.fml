:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orzs.fml
:: Utworzony: 13.05.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORZS - Rejestracja zastępstw
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja zastępstw - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \init/kart_poz.fml
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=N, keyref=T
::# kind=WE, symbol=OSOBA, type=_OSOBA, name=Wskazanie osoby, required=N, keyref=T
::
::# permissions=UD_SKL

_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_jest_p:=(type_of(_in.P)=type_of(null) & _in.P<>null);
_jest_osoba:=(type_of(_in.OSOBA)=type_of(null) & _in.OSOBA<>null);

_result:='';
_dom:=exec('domain_ref','#b_domain','PKD');
_pkd:=exec('lic','#b_domain',_dom);
{? _pkd<>null & ~_jest_p
|| _result:='Brak wskazania pracownika.'@
|? _pkd=null & ~_jest_osoba
|| _result:='Brak wskazania osoby.'@
|? _jest_p & _jest_osoba
|| _result:='Czynność musi być wywołana ze wskazaniem tylko pracownika lub tylko osoby.'@
?};
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result);
   return()
?};

{? _pkd<>null
|| _tab:=P;
   _ref:=_in.P
|| _tab:=OSOBA;
   _ref:=_in.OSOBA
?};

_id:=exec('ref2uid','#table',_ref);
_do:=_mp.akcja();

{? _id=''
|| _result:=exec('error','!pkd_ezk_orzs')

|? _mp.isMicro()
|| {? _do='VIEW'
   || _mp.keyRef(_id);
      exec('select','!pkd_ezk_orzs',_ref,_mp);
      _mp.delRef(_id);
      _mp.cancel()
   |? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@[_mp.buf_act.UID,_do]
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orzs',_ref,_mp);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja zastępstw - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_todo:='Zarejestruj zastępców'@;
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
{? type_of(_in.P)=type_of(null) & _in.P<>null
|| _tab:=exec('desc','pracownik',params_get().mp);
   {? _tab.ZAW_DANE='T'
   || {? _tab.OBCY='T'
      || 'Zarejestruj zastępców: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
      |? +_tab.PESEL
      || 'Zarejestruj zastępców: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
      || 'Zarejestruj zastępców: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
      ?}
   || 'Zarejestruj zastępców'@@
   ?}
|| _tab:=exec('desc','osoba',params_get().mp);
   {? _tab.ZAW_DANE='T'
   || {? _tab.OBCY='T'
      || 'Zarejestruj zastępców: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
      |? +_tab.PESEL
      || 'Zarejestruj zastępców: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
      || 'Zarejestruj zastępców: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
      ?}
   || 'Zarejestruj zastępców'@@
   ?}
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie pracownika lub osoby
::       _b - wskazanie menadżera procesów
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!pkd_ezk_orzs');

{? var_pres('_a')<>type_of(null) | _a=null
|| return(_err_msg)
?};

_tab:=ref_tab(_a);
_key:='';
_wnd:='';

_tab.cntx_psh();
_tab.prefix();
{? ~_tab.seek(_a)
|| _tab.cntx_pop();
   return(_err_msg)
?};

{? _tab=P
|| _key:='WIDOK_P';
   _wnd:='GRP_ZASP';
   REF.P:=P.ref();
   REF.OSOBA:=P.OSOBA;
   REF.P().OSOBA()

|? _tab=OSOBA
|| _key:='WIDOK_O';
   _wnd:='GRP_ZASO';
   REF.P:=null;
   REF.OSOBA:=OSOBA.ref()

|| _tab.cntx_pop();
   return(_err_msg)
?};

{? _b.isMicro()
|| P_PZ.actions_grayed('ZAS_P','Z:Z');
   P_PZ.actions_grayed('ZAS_O','Z:Z')
?};
: pokaż zastępców
P_PZ.cntx_psh();
P_PZ.index(_key);
P_PZ.prefix('Z',_a);
P_PZ.win_sel(_wnd);
_result:=P_PZ.select();
P_PZ.cntx_pop();

P_PZ.actions_grayed('ZAS_P');
P_PZ.actions_grayed('ZAS_O');

: porządki
_tab.cntx_pop();

_result


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli P_PZ z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli P_PZ jako składowa okna grupowego tabeli UD_DEF).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=P_PZ
|| sel_exit()

|| P.cntx_psh();
   P_PZ.P();
   params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ');
   P.cntx_pop()
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestracja zastępstw niemożliwa.'@


\p_pz_okno_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Okienko przed okna WER_ZAS tabeli P_PZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('p_pz_okno_b','kart_poz','Z')


\p_pz_okno_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Okienko po okna WER_ZAS tabeli P_PZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('p_pz_okno_a','kart_poz')

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:52 3c7c11fac2126aabb4fdfdf8d061a1b3490d8bd920acbd235a7eecdd1602b070de61c0f5132ae9413b30a711bcd3e936de46533351ed6a6fcf0d02c6c22ceee6fd3a06764696ef96bbae79f0fb324acad2ddba2fa76f5406172191cce3a28447191269ff4bb1ca072c010a80c6324db37e3cb80932d0ce9b490363c92d28490a

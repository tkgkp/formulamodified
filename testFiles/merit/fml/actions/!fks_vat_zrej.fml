:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_vat_zrej.fml
:: Utworzony: 14.07.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VAT_ZREJ - Wydruki rejestrów VAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wydruki rejestrów VAT - główna formuła czynności FKS_VAT_ZREJ.
::  OLD: \vat_druk/vat.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
_params:=params_get();
params_set(_params);
_kraj:={? var_press('kraj')>0 || kraj || ~~ ?};
ROK_F.win_sel('WER');
ROK_F.win_dict('WER');
grupa_vat:='';
{? var_pres('rejestr')>0 || &rejestr ?};

_tab:=~~;
{? var_press('_params')=0 | var_press('RAP_ID',_params)<>type_of('') | _params.RAP_ID=''
|| _tab:=exec('REPTAB','#object');
   _tab.
   add('A. Dostawa towarów i świadczenie usług na terytorium kraju'@,'fks_vat_rsp',"exec('bp','!fks_vat_zrej','A')").
   add('B. Nabycie towarów i usług na terytorium kraju'@,'fks_vat_zak',"exec('bp','!fks_vat_zrej','B')").
   add('C. Eksport towarów'@,'fks_vat_rsp',"exec('bp','!fks_vat_zrej','C')").
   add('D. Import towarów'@,'fks_vat_dimp',"exec('bp','!fks_vat_zrej','D')").
   add('E. Import usług (poza wewnątrzwspólnotowy)'@,'fks_vat_uimp',"exec('bp','!fks_vat_zrej','E')").
   add('F. Import usług (wewnątrzwspólnotowy)'@,'fks_vat_zak',"exec('bp','!fks_vat_zrej','F')").
   add('G. Wewnątrzwspólnotowa dostawa towarów'@,'fks_vat_rsp',"exec('bp','!fks_vat_zrej','G')").
   add('H. Wewnątrzwspólnotowe nabycie towarów'@,'fks_vat_zak',"exec('bp','!fks_vat_zrej','H')").
   add('I. Dostawa towarów, dla której podatnikiem jest nabywca (nabywca)'@,'fks_vat_zak',"exec('bp','!fks_vat_zrej','I')").
   add('J. Dostawa towarów z importu - procedura uproszczona'@,'fks_vat_dimp',"exec('bp','!fks_vat_zrej','J')").
   add('K. Dostawa, dla której podatnikiem jest nabywca (dostawca)'@,'fks_vat_rsp',"exec('bp','!fks_vat_zrej','K')").
   add('L. Czynności nieopodatkowane na terytorium kraju'@,'fks_vat_rsp',"exec('bp','!fks_vat_zrej','L')").
   add('M. Czynności nieopodatkowane na rzecz podatnika VAT UE'@,'fks_vat_rsp',"exec('bp','!fks_vat_zrej','M')").
   add('N. Wewnątrzwspólnotowe świadczenie usług'@,'fks_vat_rsp',"exec('bp','!fks_vat_zrej','N')").
   add('N.1 Czynności nieopodatkowane świadczenia usług poza UE'@,'fks_vat_rsp',"exec('bp','!fks_vat_zrej','N.1')").
   add('O. Wydruk rejestru VAT'@,'fks_vat_rej',"exec('bp','!fks_vat_zrej','O')").
   add('P. Grupy podatkowe VAT'@,'fks_vat_grup',"exec('bp','!fks_vat_zrej','P')").
   add('Q. Zestawienie zbiorcze'@,'fks_vat_zest',"exec('bp','!fks_vat_zrej','Q')").
   add('R. Dokumenty elektroniczne w rejestrach VAT'@,'fks_vat_edok',"exec('bp','!fks_vat_zrej','R')").
   add('S. Dokumenty VAT spoza bieżącego okresu VAT'@,'fks_vat_okr',"exec('bp','!fks_vat_zrej','S')")
?};
{? var_press('WAL',VAT7)>0
|| _tab.add('T. Wewnątrzwspólnotowa dostawa towarów i świadczenie usług - procedura upr.'@,'fks_vat_oss',"exec('bp','!fks_vat_zrej','T')")
?};
exec('rep_exec','#b_report','FKS_VAT_ZREJ',,,1,,,,_tab);
VAR_DEL.delete('hdr_vat','rejestr','imp_prfx','spr_zlom');
VAR_DEL.delete('grupa_vat');
{? _kraj<>~~ || kraj:=_kraj ?}


\bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła wołana przed wydrukiem
::   WE: _a - Litera wydruku
::----------------------------------------------------------------------------------------------------------------------
RejWydr:=_a;
grupa_vat:='';
{? _a='A'
|| rejestr:='SprzKraj'; hdr_vat:='Dostawa towarów i świadczenie usług na terytorium kraju'
|? _a='B'
|| rejestr:='ZakupKraj'; hdr_vat:='Nabycie towarów i usług na terytorium kraju'
|? _a='C'
|| rejestr:='SprzEksp'; hdr_vat:='Eksport towarów'
|? _a='D'
|| hdr_vat:='Import towarów'; imp_prfx:='ImpTowar'
|? _a='E'
|| hdr_vat:='Import usług (poza wewnątrzwspólnotowy)'
|? _a='F'
|| rejestr:='_WWspNus'; hdr_vat:='Import usług (wewnątrzwspólnotowy)'
|? _a='G'
|| rejestr:='_WWspD'; hdr_vat:='Wewnątrzwspólnotowa dostawa towarów'
|? _a='H'
|| rejestr:='_WWspN'; hdr_vat:='Wewnątrzwspólnotowe nabycie towarów'
|? _a='I'
|| rejestr:='ZakOpod'; hdr_vat:='Dostawa towarów, dla której podatnikiem jest nabywca'
|? _a='J'
|| hdr_vat:='Import towarów (procedura uproszczona)'; imp_prfx:='ImpTowUp'
|? _a='K'
|| rejestr:='SprzNOp'; hdr_vat:='Dostawa, dla której podatnikiem jest nabywca (dostawca)'
|? _a='L'
|| rejestr:='SprzNOp'; hdr_vat:='Czynności nieopodatkowane na terytorium kraju'
|? _a='M'
|| rejestr:='_SprzNOp'; hdr_vat:='Czynności nieopodatkowane na rzecz podatnika VAT UE'
|? _a='N'
|| rejestr:='_SprzNOp'; grupa_vat:='SprPKWSU'; hdr_vat:='Wewnątrzwspólnotowe świadczenie usług'
|? _a='N.1'
|| rejestr:='SprzEksp'; grupa_vat:='SprPozKr'; hdr_vat:='Czynności nieopodatkowane świadczenia usług poza UE'
|? _a='O'
|| VAR_DEL.delete('hdr_vat')
|? _a='P'
|| VAR_DEL.delete('hdr_vat')
|? _a='Q'
|| hdr_vat:='Zestawienie zbiorcze'
|? _a='R'
|| hdr_vat:='Dokumenty elektroniczne w rejestrach VAT'
|? _a='S'
|| hdr_vat:='Dokumenty VAT spoza bieżącego okresu VAT'
|? _a='T'
|| rejestr:='SprzKraj'; grupa_vat:=''; hdr_vat:='Wewnątrzwspólnotowa dostawa towarów i świadczenie usług - procedura uproszczona'
?};
1


\bp_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2009+]
:: OPIS: Wykonywana w prologu wydruku (sekcja BEGIN)
::   WE: _a - nazwa pliku z wydrukiem
::  OLD: \bp_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='fks_vat_rsp'
|| sstr5:=sstr6:=sstr7:=sstr8:=sstr9:=sstr10:=sstr11:=sstr12:=sstr13:=0; tryb:=4; zak:=4; sklej:='';
   sstr14:=sstr15:=sstr16:=sstr17:=sstr18:=sstr19:=sstr20:=sstr21:=sstr22:=sstr23:=sstr24:=0;
   suma5:=suma6:=suma7:=suma8:=suma9:=suma10:=suma11:=suma12:=suma13:=0;
   suma14:=suma15:=suma16:=suma17:=suma18:=suma19:=suma20:=suma21:=suma22:=suma23:=suma24:=0;
   w55:=w555:=''; w44:=''; wyb:=0;  rek:=0; rejspr:=1;odd:=OPERATOR.DEPT=null;
   {? var_pres('rejestr')<=0 || rejestr:='S' ?}; pom:=1; ok:=ile_l:=0;
   s:=obj_new(20); v:=obj_new(20); vwe:=obj_new(20); n:=obj_new(20); br:=obj_new(20); svat:=obj_new(20);snetto:=obj_new(20);
   suma:=obj_new(20); np:=obj_new(20); vp1:=obj_new(20); param1:=param2:=param3:=param4:=param5:=param6:=param7:=param8:='';
   VAT7.PAR1:=VAT7.PAR3:=VAT7.PAR4:=0; VAT7.PAR2:=3;
   PRN:=obj_new(@.CLASS.PRINT,40); PRN.sl_frame();
   PRN1:=obj_new(@.CLASS.PRINT,20); PRN1.sl_frame();
   linijka:=numer:=0; linecnt:=0; w:=obj_new(52); szer:=obj_new(18); ww:=obj_new(30); www:=obj_new(30); text:=obj_new(18);
   stawka:=obj_new(7);  {!_a:=1 ..25 |! w[_a]:='' !};  {! _a:=1..30 |! ww[_a]:=www[_a]:=0 !}; d_sum1:=d_sum2:=d_sum3:=0;
   dru_rej:=0;  rej_1:=rej_2:=rej_3:=rej_4:=rej_5:=rej_6:=rej_7:=rej_8:=0; kor:=0;
   rej_9:=rej_10:=rej_11:=rej_12:=rej_13:=rej_14:=rej_15:=rej_16:=rej_17:=rej_18:=0;
   rej_naz:=pom_1:=pom_2:=pom_3:=pom_4:=pom_5:=pom_6:=pom_7:='';  pierwszy:=1; VAT7.WY2:=0;
   font_gr:=0; color:=''; prn1:=prn2:='PRN';
   lm:=ll:=lmt:=rsep:=0; vp:=1; rsep1:=PAR_WYDR.RSEP; PAR_WYDR.RSEP:=1; fun:='';
   {? var_press('WAL',VAT7)>0 || VAT7.WAL:=exec('kod_pln','slo_slu') ?}; wal_op:='';
   VAT7.KRAJ:=exec('kod_pl','slo_slu'); kraj:='';
   VAT7.win_edit({? RejWydr='N' || 'REJSPUE2' |? 1+rejestr='_' || 'REJSPUE' || 'REJSP' ?});
   VAT7.win_edit({? var_pres('TYP_VAT',PVAT)>0 & RejWydr='N'
                 || 'REJSPUE2'
                 |? 1+rejestr='_'
                 || {? 1+RejWydr='M'
                    || 'REJSPUE3'
                    || 'REJSPUE'
                    ?}
                 |? rejestr='SprzNOp'
                 || 'REJSPUE1'
                 || {? (RejWydr='C' | RejWydr='L')
                    || 'REJSP3'
                    || 'REJSP'
                    ?}
                 ?});
   exec('vat7_p','!fks_vat_zrej');
   STR2:=obj_new(@.CLASS.STRING); STR3:=obj_new(@.CLASS.STRING);
   VAT7.hdr_edit();
   hdr_vat:={? var_pres('hdr_vat')<0 || ~(-'Rejestr sprzedaży VAT') || ~(-hdr_vat) ?};
   VAT7.WY3:=1; VAT7.WYD_TECH:=0; OKRO_F.win_dict('SLO'); VAT7.WYSTROK:=VAT7.WYSTOKRO:=null;
   VAT7.hdr_edit(hdr_vat);
   edycja:=VAT7.edit("
   _ret:=1;
   {? (1+rejestr='_') & VAT7.PAR1=4 || VAT7.PAR3:=1 ?};
   {? VAT7.WY4=1 & VAT7.JPK_GTU='' || FUN.info('Należy wybrać wartości dla filtra GTU'); _ret:=0
   |? VAT7.WY5=1 & VAT7.JPK_PROC='' || FUN.info('Należy wybrać wartości dla filtra procedur'); _ret:=0
   |? VAT7.WY6=1 & VAT7.JPK_TD='' || FUN.info('Należy wybrać wartości dla filtra typu dokumentów'); _ret:=0
   ?};
   _ret
   ");
   {? rejestr='SprzNOp' || VAT7.PAR10:=3 ?};
   PAR_WYDR.TITLE:=hdr_vat+exec('naz_okr_vat','!fks_vat_zrej'); dokvpoch:=exec('dokvpoch','!fks_vat_zrej');
   okrVat:=exec('okr_vat_wydr','!fks_vat_zrej');
   SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
   {? SLU.find_key('~STAWKI VAT '+VAT7.KRAJ().KOD)
   || SLO.cntx_psh; SLO.index('SL'); SLO.prefix(SLU.ref); sizeslo:=SLO.size; tvat:=obj_new(sizeslo);
      tn:=obj_new(sizeslo); tv:=obj_new(sizeslo); tv1:=obj_new(sizeslo); tb:=obj_new(sizeslo);
      sn:=obj_new(sizeslo); sv:=obj_new(sizeslo); sv1:=obj_new(sizeslo); sb:=obj_new(sizeslo); SLO.first;
      _ile:={? sizeslo<8 || 8 || sizeslo ?}+(VAT7.PAR1=2)+(VAT7.PAR1=3);
      su:=obj_new(_ile); ssu:=obj_new(8); {! _i:=1..8 |! ssu[_i]:=0 !}; is_us:=0;
      {! _a:=1 ..sizeslo
      |! tvat[_a]:=SLO.KOD; tn[_a]:=tv[_a]:=tb[_a]:=sn[_a]:=sv[_a]:=sb[_a]:=tv1[_a]:=sv1[_a]:=0;
         SLO.next()
      !};SLO.cntx_pop();
      {! _a:=1.._ile
      |! su[_a]:=obj_new(4); su[_a][1]:=su[_a][2]:=su[_a][3]:=su[_a][4]:=0
      !}
   ?}; SLU.cntx_pop();
   head:=PAR_WYDR.HEAD; logo:=PAR_WYDR.LOGO; separat:=1; s2c:=0; ROK_F.win_dict('WER'); StrVat:=''
|? _a='fks_vat_oss'
|| sstr5:=sstr6:=sstr7:=sstr8:=sstr9:=sstr10:=sstr11:=sstr12:=sstr13:=0; tryb:=4; zak:=4; sklej:='';
   sstr14:=sstr15:=sstr16:=sstr17:=sstr18:=sstr19:=sstr20:=sstr21:=sstr22:=sstr23:=sstr24:=0;
   suma5:=suma6:=suma7:=suma8:=suma9:=suma10:=suma11:=suma12:=suma13:=0;
   suma14:=suma15:=suma16:=suma17:=suma18:=suma19:=suma20:=suma21:=suma22:=suma23:=suma24:=0;
   w55:=w555:=''; w44:=''; wyb:=0;  rek:=0; rejspr:=1;odd:=OPERATOR.DEPT=null;
   {? var_pres('rejestr')<=0 || rejestr:='S' ?}; pom:=1; ok:=ile_l:=0;
   s:=obj_new(20); v:=obj_new(20); vwe:=obj_new(20); n:=obj_new(20); br:=obj_new(20); svat:=obj_new(20);snetto:=obj_new(20);
   suma:=obj_new(20); np:=obj_new(20); vp1:=obj_new(20); param1:=param2:=param3:=param4:=param5:=param6:=param7:=param8:='';
   VAT7.PAR1:=VAT7.PAR3:=VAT7.PAR4:=0; VAT7.PAR2:=3;
   PRN:=obj_new(@.CLASS.PRINT,40); PRN.sl_frame();
   PRN1:=obj_new(@.CLASS.PRINT,20); PRN1.sl_frame();
   linijka:=numer:=0; linecnt:=0; w:=obj_new(52); szer:=obj_new(18); ww:=obj_new(30); www:=obj_new(30); text:=obj_new(18);
   stawka:=obj_new(7);  {!_a:=1 ..25 |! w[_a]:='' !};  {! _a:=1..30 |! ww[_a]:=www[_a]:=0 !}; d_sum1:=d_sum2:=d_sum3:=0;
   dru_rej:=0;  rej_1:=rej_2:=rej_3:=rej_4:=rej_5:=rej_6:=rej_7:=rej_8:=0; kor:=0;
   rej_9:=rej_10:=rej_11:=rej_12:=rej_13:=rej_14:=rej_15:=rej_16:=rej_17:=rej_18:=0;
   rej_naz:=pom_1:=pom_2:=pom_3:=pom_4:=pom_5:=pom_6:=pom_7:='';  pierwszy:=1; VAT7.WY2:=0;
   font_gr:=0; color:=''; prn1:=prn2:='PRN';
   lm:=ll:=lmt:=rsep:=0; vp:=1; rsep1:=PAR_WYDR.RSEP; PAR_WYDR.RSEP:=1; fun:='';
   VAT7.win_edit('REJSPOSS');
   exec('vat7_p','!fks_vat_zrej');
   STR2:=obj_new(@.CLASS.STRING); STR3:=obj_new(@.CLASS.STRING); __wgkraj:=1;
   VAT7.hdr_edit();
   hdr_vat:={? var_pres('hdr_vat')<0 || ~(-'Rejestr sprzedaży VAT') || ~(-hdr_vat) ?};

   VAT7.WY3:=1; VAT7.WYD_TECH:=0; OKRO_F.win_dict('SLO'); VAT7.WYSTROK:=VAT7.WYSTOKRO:=null;
   VAT7.hdr_edit(hdr_vat); VAT7.DATA_N:=SSTALE.AO().KON; VAT7.PAR3:=1;
   VAT7.WAL:=exec('kod_pln','slo_slu'); wal_op:='';lp:=0;
   VAT7.KRAJ:=exec('kod_pl','slo_slu'); kraj:='';
   exec('tkrs_eb','fks_viudo',1);
   POMOC.WAL_TKRS:=exec('FindInSet','#table','SLO','SL','EUR',FINFO.SLWAL().SLU,,1);
   VAT7.BANK:=FINFO.B_VIUDO; FINFO.B_VIUDO().SLU(); VAT7.fld_fml('BANK','BEFORE_EDIT',"FINFO.B_VIUDO().SLU()");
   VAT7.fld_fml('TKRS','BEFORE_EDIT',"{? VAT7.BANK=null()
                                      || VAT7.efld_opt(VAT7.win_edit('?'),'enable=0',VAT7,'TKRS')
                                      || POMOC.BANKEB:=exec('FindInSet','#table','B','BANKOD',VAT7.BANK().KOD);
                                         VAT7.efld_opt(VAT7.win_edit('?'),'enable=1',VAT7,'TKRS')
                                      ?}");
    VAT7.fld_fml('BANK','AFTER_EDIT',"{? VAT7.BANK=null()
                                      || VAT7.efld_opt(VAT7.win_edit('?'),'enable=0',VAT7,'TKRS')
                                      || VAT7.efld_opt(VAT7.win_edit('?'),'enable=1',VAT7,'TKRS')
                                      ?};
                                      POMOC.BANK:=VAT7.BANK;
                                      POMOC.BANKEB:=exec('FindInSet','#table','B','BANKOD',VAT7.BANK().KOD);
                                      {? VAT7.TKRS().BANK<>VAT7.BANK || VAT7.TKRS:=null; win_disp() ?};
                                      {? VAT7.TKRS=null()
                                      || exec('tkrs_eb','fks_viudo',1);
                                         TKRS.index('TKRS_DT');
                                         TKRS.prefix(VAT7.BANK,POMOC.WAL_TKRS);
                                         {? TKRS.find_le(VAT7.DATA_N) || VAT7.TKRS:=TKRS.ref(); win_disp() ?};
                                         exec('tkrs_eb','fks_viudo',2)
                                      ?};
                                      1
                                     ");
   {? VAT7.BANK=null() || VAT7.efld_opt(VAT7.win_edit('?'),'enable=0',VAT7,'TKRS') || VAT7.efld_opt(VAT7.win_edit('?'),'enable=1',VAT7,'TKRS') ?};
   POMOC.BANK:=VAT7.BANK; VAT7.ZERO:=0;
   TKRS.win_dict('WERGEB');

   edycja:=VAT7.edit("
   _ret:=1;
   {? (1+rejestr='_') & VAT7.PAR1=4 || VAT7.PAR3:=1 ?};
   {? VAT7.WY4=1 & VAT7.JPK_GTU='' || FUN.info('Należy wybrać wartości dla filtra GTU'); _ret:=0
   |? VAT7.WY5=1 & VAT7.JPK_PROC='' || FUN.info('Należy wybrać wartości dla filtra procedur'); _ret:=0
   |? VAT7.WY6=1 & VAT7.JPK_TD='' || FUN.info('Należy wybrać wartości dla filtra typu dokumentów'); _ret:=0
   ?};
   _ret
   ");
   {? rejestr='SprzNOp' || VAT7.PAR10:=3 ?};
   PAR_WYDR.TITLE:=hdr_vat+exec('naz_okr_vat','!fks_vat_zrej'); dokvpoch:=exec('dokvpoch','!fks_vat_zrej');
   okrVat:=exec('okr_vat_wydr','!fks_vat_zrej');
   _fml:=" tn:=obj_new(sizeslo); tv:=obj_new(sizeslo); tv1:=obj_new(sizeslo); tb:=obj_new(sizeslo);
           tne:=obj_new(sizeslo); tve:=obj_new(sizeslo); tv1e:=obj_new(sizeslo); tbe:=obj_new(sizeslo);
           tk:=obj_new(sizeslo); tt:=obj_new(sizeslo); tu:=obj_new(sizeslo); tvat:=obj_new(sizeslo);
           sn:=obj_new(sizeslo); sv:=obj_new(sizeslo); sv1:=obj_new(sizeslo); sb:=obj_new(sizeslo);
           sne:=obj_new(sizeslo); sve:=obj_new(sizeslo); sv1e:=obj_new(sizeslo); sbe:=obj_new(sizeslo)
         ";
   {? VAT7.KRAJ<>null()
   || SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
      {? SLU.find_key('~STAWKI VAT '+VAT7.KRAJ().KOD)
      || SLO.cntx_psh; SLO.index('SL'); SLO.prefix(SLU.ref); sizeslo:=SLO.size;
         _fml(); SLO.first;
         _ile:={? sizeslo<8 || 8 || sizeslo ?}+(VAT7.PAR1=2)+(VAT7.PAR1=3);
         su:=obj_new(_ile); ssu:=obj_new(8); {! _i:=1..8 |! ssu[_i]:=0 !}; is_us:=0;
         {! _a:=1 ..sizeslo
         |! tvat[_a]:=SLO.KOD; tn[_a]:=tv[_a]:=tb[_a]:=sn[_a]:=sv[_a]:=sb[_a]:=tv1[_a]:=sv1[_a]:=tk[_a]:=tt[_a]:=tu[_a]:=0;
            tne[_a]:=tve[_a]:=tbe[_a]:=sne[_a]:=sve[_a]:=sbe[_a]:=tv1e[_a]:=sv1e[_a]:=0;
            SLO.next()
         !};SLO.cntx_pop();
         {! _a:=1.._ile
         |! su[_a]:=obj_new(4); su[_a][1]:=su[_a][2]:=su[_a][3]:=su[_a][4]:=0
         !}
      ?}; SLU.cntx_pop()
   || {? var_pres('TmpStVat')>0 || VAR_DEL.delete('TmpStVat') ?};
      TmpStVat:=sql('select distinct SLO.KOD as KOD, 0 as USE '+
                    'from SLO left join @SLU using (SLO.SLU,SLU.REFERENCE) '+
                    'where substr(SLU.NAZ,1,12) = ''~STAWKI VAT '' order by 1');
      {? TmpStVat.first()
      || TmpStVat.ndx_tmp(,1,'KOD',,0,'USE',,0);
         sizeslo:=TmpStVat.size;
         _fml();
         _ile:={? sizeslo<8 || 8 || sizeslo ?}+(VAT7.PAR1=2)+(VAT7.PAR1=3);
         su:=obj_new(_ile); ssu:=obj_new(8); {! _i:=1..8 |! ssu[_i]:=0 !}; is_us:=0;
         {! _a:=1 ..sizeslo
         |! tvat[_a]:=TmpStVat.KOD; tn[_a]:=tv[_a]:=tb[_a]:=sn[_a]:=sv[_a]:=sb[_a]:=tv1[_a]:=sv1[_a]:=tk[_a]:=tt[_a]:=tu[_a]:=0;
            tne[_a]:=tve[_a]:=tbe[_a]:=sne[_a]:=sve[_a]:=sbe[_a]:=tv1e[_a]:=sv1e[_a]:=0;
            TmpStVat.next()
         !};
         {! _a:=1.._ile
         |! su[_a]:=obj_new(4); su[_a][1]:=su[_a][2]:=su[_a][3]:=su[_a][4]:=0
         !}
      ?}
   ?};
   head:=PAR_WYDR.HEAD; logo:=PAR_WYDR.LOGO; separat:=1; s2c:=0; ROK_F.win_dict('WER')
|? _a='fks_vat_rej'
|| ue:=exec('czy_ue','fks_vat'); VAT7.TYLKOZAP:=0; eksport:=0; zak:=0;
   PRN:=obj_new(@.CLASS.PRINT,40);PRN.sl_frame(); pom:=1;
   PRN1:=obj_new(@.CLASS.PRINT,20);PRN1.sl_frame(); linecnt:=0;
   text:=obj_new(18); opcja:=zakup:=numer:=0; stawka:=obj_new(7);
   w:=obj_new(52); szer:=obj_new(17); ww:=obj_new(30); www:=obj_new(30);
   {!_a:=1..20 |! w[_a]:='' !}; {!_a:=21 ..28 |! w[_a]:=0 !};
   {! _a:=1..30 |! ww[_a]:=www[_a]:=0 !}; tytul:=''; w55:=w555:='';  prosty:=linijka:=0; rejestr:='';
   VAT7.WY1:=VAT7.WY2:=VAT7.PAR1:=VAT7.PAR3:=VAT7.PAR4:=0; VAT7.PAR2:=3; dat_zapl:=date(0,0,0); kor:=0; sizeslo:=0;
   PRN2:=obj_new(@.CLASS.PRINT,7); PRN2.sl_frame(); co:=ile_l:=0;
   {! _i:=1..4 |! ($('param'+$_i+':=\'\''))() !}; {! _i:=6..14 |! ($('w'+$_i+':=0'))() !};
   {! _i:=5..24 |! ($('sstr'+$_i+':=suma'+$_i+':=0'))() !};
   inwestv1:=inwestv2:=0; pozostv1:=pozostv2:=0; in_w:=po_z:=ryc_z:=0;
   {! _i:=0..4 |! ($('inwest'+$_i+':=pozost'+$_i+':=0'))() !};
   w2:=''; w15:=inwest5:=pozost5:=0;  inwestv5:=pozostv5:=w16:=0; dru_rej:=0;
   rycz1:=ryczv1:=rycz2:=ryczv2:=rycz3:=rycz4:=rycz5:=ryczv5:=0;
   param1:=param2:=param3:=param4:=param5:=param6:=param7:=param8:=''; VAT7.TYP_VAT:='';
   STR2:=obj_new(@.CLASS.STRING); STR3:=obj_new(@.CLASS.STRING);
   exec('vat7_p','!fks_vat_zrej');
   {! _i:=7..12 |! ($('wart'+$_i+':=obj_new(3)'))() !};  wart15:=obj_new(3); wart16:=obj_new(3);
   {! _i:=1..18 |! ($('rej_'+$_i+':=0'))() !}; {! _i:=1..7 |! ($('pom_'+$_i+':=0'))() !};
   rej_naz:=''; pierwszy:=1; VAT7.DAT_ZAPL:=date(0,0,0); font_gr:=0; color:=''; prn1:=prn2:='PRN';
   lm:=ll:=lmt:=rsep:=0; vp:=1; rsep:=PAR_WYDR.RSEP; PAR_WYDR.RSEP:=1; fun:='';
   {? var_press('WAL',VAT7)>0 || VAT7.WAL:=exec('kod_pln','slo_slu') ?}; wal_op:='';
   VAT7.KRAJ:=exec('kod_pl','slo_slu'); kraj:='';
   VAT7.WY3:=1; VAT7.WYD_TECH:=0; head:=PAR_WYDR.HEAD; logo:=PAR_WYDR.LOGO;
   exec('tmp_vat','!fks_vat_zrej',1); odd:=OPERATOR.DEPT=null; s2c:=0;
   OKRO_F.win_dict('SLO'); VAT7.WYSTROK:=VAT7.WYSTOKRO:=null; ROK_F.win_dict('WER'); rvat_sym:=''; StrVat:=''
?};
UNPAR.P1:=0;
1


\ap_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2009+]
:: OPIS: Wykonywana na koniec wydruku (sekcja END fks_vat_rsp.rpm)
::   WE: _a - nazwa pliku z wydrukiem
::  OLD: \ap_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='fks_vat_rsp'
|| &hdr_vat; PAR_WYDR.HEAD:=head; &head; PAR_WYDR.LOGO:=logo; &logo; &ile_l; &separat;
   obj_del(PRN); obj_del(PRN1); obj_del(szer); obj_del(w); obj_del(ww); obj_del(www); obj_del(text); obj_del(stawka);
   &sstr5; &sstr6; &sstr7; &sstr8; &sstr9; &sstr10; &sstr11; &sstr12; &sstr13; &sstr14; &sklej;
   &sstr15; &sstr16; &sstr17; &sstr18; &sstr19; &sstr20; &sstr21; &sstr22; &sstr23; &sstr24;
   &suma5; &suma6; &suma7; &suma8; &suma9; &suma10; &suma11; &suma12; &suma13; &suma14;
   &suma15; &suma16; &suma17; &suma18; &suma19; &suma20; &suma21; &suma22; &suma23; &suma24;
   &wyb; &w55; &w555; &w44; &rek; &zak; &rejspr; &tryb; &odd;
   &sizeslo; &rejestr; &pom; &ok; &linijka; &numer; &linecnt;
   obj_del(tvat); obj_del(tn); obj_del(tv); obj_del(tv1); obj_del(tb);
   &d_sum1; &d_sum2; &d_sum3;

   obj_del(sn); obj_del(sv); obj_del(sv1); obj_del(sb); obj_del(np); obj_del(vp1);
   obj_del(s);obj_del(v); obj_del(vwe); obj_del(n); obj_del(br); obj_del(svat);obj_del(snetto); obj_del(suma);
   _a:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2); DOK.use('doku'+_a); POZ.use('pozy'+_a); VPOZ.use('pozv'+_a);
   POW.use('pow_'+_a); &param1; &param2; &param3; &param4; &param5; &param6; &param7; &param8;
   &dru_rej; &rej_1; &rej_2; &rej_3; &rej_4; &rej_5; &rej_6; &rej_7;&rej_8; &rej_9; &rej_10; &rej_11;
   &rej_12; &rej_13; &rej_14; &rej_15; &rej_16; &rej_17; &rej_18;
   &rej_naz; &pom_1; &pom_2; &pom_3; &pom_4; &pom_5; &pom_6; &pom_7; &pierwszy; &kor;
   font_gr:=1; &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; PAR_WYDR.RSEP:=rsep1; &rsep; &rsep1; &fun;
   &wal_op; &kraj; &edycja; VAR_DEL.delete('dokvpoch','s2c','su','ssu','is_us','okrVat','okrVatNr','STR2','STR3');
   exec('vat7_p','!fks_vat_zrej');
   exec('wsu_del','!fks_vat_zrej'); &StrVat
|? _a='fks_vat_oss'
|| &hdr_vat; PAR_WYDR.HEAD:=head; &head; PAR_WYDR.LOGO:=logo; &logo; &ile_l; &separat;
   obj_del(PRN); obj_del(PRN1); obj_del(szer); obj_del(w); obj_del(ww); obj_del(www); obj_del(text); obj_del(stawka);
   &sstr5; &sstr6; &sstr7; &sstr8; &sstr9; &sstr10; &sstr11; &sstr12; &sstr13; &sstr14; &sklej;
   &sstr15; &sstr16; &sstr17; &sstr18; &sstr19; &sstr20; &sstr21; &sstr22; &sstr23; &sstr24;
   &suma5; &suma6; &suma7; &suma8; &suma9; &suma10; &suma11; &suma12; &suma13; &suma14;
   &suma15; &suma16; &suma17; &suma18; &suma19; &suma20; &suma21; &suma22; &suma23; &suma24;
   &wyb; &w55; &w555; &w44; &rek; &zak; &rejspr; &tryb; &odd;
   &sizeslo; &rejestr; &pom; &ok; &linijka; &numer; &linecnt;
   obj_del(tvat); obj_del(tn); obj_del(tv); obj_del(tv1); obj_del(tb); obj_del(tk); obj_del(tt); obj_del(tu); exec('tkrs_eb','fks_viudo',2);
   obj_del(tne); obj_del(tve); obj_del(tv1e); obj_del(tbe);
   &d_sum1; &d_sum2; &d_sum3;
   obj_del(sn); obj_del(sv); obj_del(sv1); obj_del(sb); obj_del(np); obj_del(vp1);
   obj_del(sne); obj_del(sve); obj_del(sv1e); obj_del(sbe);
   obj_del(s);obj_del(v); obj_del(vwe); obj_del(n); obj_del(br); obj_del(svat);obj_del(snetto); obj_del(suma);
   _a:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2); DOK.use('doku'+_a); POZ.use('pozy'+_a); VPOZ.use('pozv'+_a);
   POW.use('pow_'+_a); &param1; &param2; &param3; &param4; &param5; &param6; &param7; &param8;
   &dru_rej; &rej_1; &rej_2; &rej_3; &rej_4; &rej_5; &rej_6; &rej_7;&rej_8; &rej_9; &rej_10; &rej_11;
   &rej_12; &rej_13; &rej_14; &rej_15; &rej_16; &rej_17; &rej_18;
   &rej_naz; &pom_1; &pom_2; &pom_3; &pom_4; &pom_5; &pom_6; &pom_7; &pierwszy; &kor;
   font_gr:=1; &color; &prn1; &prn2; &lm; &ll; &vp; &lmt; PAR_WYDR.RSEP:=rsep1; &rsep; &rsep1; &fun;
   &wal_op; &kraj; &lp; &edycja; VAR_DEL.delete('dokvpoch','s2c','su','ssu','is_us','okrVat','okrVatNr','STR2','STR3');
   exec('vat7_p','!fks_vat_zrej');
   exec('wsu_del','!fks_vat_zrej')
|? _a='fks_vat_rej'
|| PAR_WYDR.HEAD:=head; &head; PAR_WYDR.LOGO:=logo; &logo; {? var_pres('hdr_vat')>0 || &hdr_vat ?}; &ue; &ile_l;
   obj_del(PRN); obj_del(PRN1); obj_del(szer);&param1; &param2; &param3; &param4; &param5; &param6; &param7; &param8; &zakup; &linijka; &numer;
   _a:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2); DOK.use('doku'+_a); POZ.use('pozy'+_a); VPOZ.use('pozv'+_a); POW.use('pow_'+_a); &eksport;
   &w6; &w7; &w8; &w9; &w10; &w11; &w12; &w13; &w14;  obj_del(stawka);  obj_del(ww); obj_del(www); obj_del(text); &zak;
   &sstr5;&sstr6;&sstr7;&sstr8;&sstr9;&sstr10;&sstr11;&sstr12;&sstr13;&sstr14;&sstr15;&sstr16;&sstr17;&sstr18;&sstr19;&sstr20;&sstr21;&sstr22;&sstr23;&sstr24;
   &suma5;&suma6;&suma7;&suma8;&suma9;&suma10;&suma11;&suma12;&suma13;&suma14;&suma15;&suma16;&suma17;&suma18;&suma19;&suma20;
   &suma21;&suma22;&suma23;&suma24; &w55; &w555; &prosty; &sizeslo; &rejestr; &pom; &in_w; &po_z; &ryc_z;
   {? var_pres('tvat')>0
   || obj_del(tvat); obj_del(tn); obj_del(tv); obj_del(tb); obj_del(sn); obj_del(sv); obj_del(sb); obj_del(tv1);  obj_del(sv1)
   ?}; obj_del(w); &tytul;
   &inwest0; &inwest1; &inwest2; &inwest3; &inwest4; &linecnt; &dat_zapl; &opcja;
   &inwestv1; &inwestv2; &pozost0; &pozost1; &pozost2; &pozost3; &pozost4; &pozostv1; &pozostv2;
   obj_del(PRN2);&w2; &w15; &inwest5; &pozost5;  &inwestv5; &pozostv5; &w16;
   &rycz1; &ryczv1; &rycz2; &ryczv2; &rycz3; &rycz4; &rycz5; &ryczv5;
   obj_del(wart7); obj_del(wart8); obj_del(wart9); obj_del(wart10);
   obj_del(wart11); obj_del(wart12); obj_del(wart15); obj_del(wart16);
   {? var_pres('TMP_VAT') || obj_del(TMP_VAT) ?};
   &dru_rej; &rej_1; &rej_2; &rej_3; &rej_4; &rej_5; &rej_6; &rej_7;&rej_8; &rej_9; &rej_10; &rej_11;
   &rej_12; &rej_13; &rej_14; &rej_15; &rej_16; &rej_17; &rej_18;
   &rej_naz; &pom_1; &pom_2; &pom_3; &pom_4; &pom_5; &pom_6; &pom_7; &pierwszy; &kor; font_gr:=1; &color; &prn1; &prn2;
   &lm; &ll; &vp; &lmt; PAR_WYDR.RSEP:=rsep; &rsep; &fun; &wal_op; &kraj; &odd; &rvat_sym;
   exec('vat7_p','!fks_vat_zrej');
   VAR_DEL.delete('dokvpoch','s2c','su','ssu','is_us','okrVat','okrVatNr','STR2','STR3'); &StrVat
?}


\naz_okr_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Nazwa okresu wydrukow VAT
::   WE: [_a] - indeks liter 0-male [1]-duze
::       [_b] - pelny opis 0-nie [1]-tak
::  OLD: \naz_okr_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<=0 || _b:=1 ?};
{? VAT7.TYPOKR='M'
|| _tyt:={? _b || ' ZA OKRES ' || '' ?}+SSTALE.AO().NAZ+' '+SSTALE.AO().ROK().NAZ;
   {? _=0 | _a || ~-_tyt || -_tyt ?}
|| _kw:=exec('kw_vat','fks_vat');
   _kw_str:={? _kw=4 || 'IV' || _kw*'I' ?};
   _tyt:=' KWARTAŁ '+SSTALE.AO().ROK().NAZ;
   {? _b || {? _=0 | _a || ' ZA' || ' za' ?}+' ' || '' ?}+_kw_str+{? _=0 | _a || ~-_tyt || -_tyt ?}
?}


\okr_vat_wydr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzy tablice z okresami do wydrukow VAT
::  OLD: \okr_vat_wydr/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.TYPOKR='M'
|| _tab:=obj_new(1);
   _tab[1]:=SSTALE.AO
|| _data:={? SSTALE.AO().POCZ=date(0,0,0)
          || {? OKRO_F.NR=0
             || OKRO_F.ROK().POCZ_ROK
             || OKRO_F.ROK().KON_ROK
             ?}
          || OKRO_F.POCZ
          ?};
   _tab:=obj_new(3);
   _kw:=exec('kw_vat','fks_vat');
   OKRO_F.cntx_psh();
   OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
   {! _i:=1..3
   |! _pocz:=date(_data~1,(_kw-1)*3+_i,1);
      {? OKRO_F.find_ge(_pocz)
      || _tab[_i]:=OKRO_F.ref()
      || _tab[_i]:=null
      ?}
   !};
   OKRO_F.cntx_pop()
?};
_tab


\dokvpoch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Pochodzenie dokumentu - napis
::  OLD: \dokvpoch/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WYSTOKRO<>null
|| 'Dokumenty pochodzące z okresu: '+VAT7.WYSTOKRO().NAZ+' '+VAT7.WYSTROK().NAZ
|? VAT7.WYSTROK<>null
|| 'Dokumenty pochodzące z roku: '+VAT7.WYSTROK().NAZ
|| ''
?}


\tmp_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [8.60]
:: OPIS: Tworzy tabele tymczasowa z rejestrami VAT
::   WE: _a: wybor akcji okna tymczasowego
::  OLD: \tmp_vat/rejestr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=0 ?};
{? var_pres('TMP_VAT')>0 || obj_del(TMP_VAT) ?};
{? OPERATOR.DEPT
|| TMP_VAT:=sql('select RVAT.SYM,RVAT.NAZ from REJ join VAT_REJ join RVAT where REJ.ROK=:_a and REJ.ODD=:_b group by RVAT.SYM,RVAT.NAZ',SSTALE.AR,OPERATOR.DEPT)
|| TMP_VAT:=sql('select RVAT.SYM,RVAT.NAZ from ODD join REJ join VAT_REJ join RVAT where REJ.ROK=:_a group by RVAT.SYM,RVAT.NAZ',SSTALE.AR)
?};
_wer:=TMP_VAT.mk_sel('Rejestry VAT'@,,,'tmp_vat_wer');
TMP_VAT.win_fld(_wer,,'SYM',,,,,,'Symbol'@);
TMP_VAT.win_fld(_wer,,'NAZ',,,,,,'Nazwa'@);
{? _a=0
|| TMP_VAT.win_act(_wer,,'Formuła','Dokumenty'@@,,'Przeglądanie dokumentów w wybranym rejestrze VAT'@,,
                 "exec('ten_rkv','!fks_vat_zrej')",1,,,,'D')
|? _a=1
|| TMP_VAT.win_act(_wer,,'Formuła','Wybierz'@@,,'Przeglądanie dokumentów w wybranym rejestrze VAT'@,,
                 "exec('ten_vat','!fks_vat_zrej');RVAT.index('SYM');RVAT.prefix(REF.FIRMA);RVAT.find_key(TMP_VAT.SYM,TMP_VAT.SYM)",1,,,,'W')
?};
TMP_VAT.win_sel(_wer)


\wsu_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Tworzy tabele na potrzeby wydruku Wewnatrzwspolnotowego swiadczenia uslug
::  OLD: \wsu_del/vat.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('WSU_DRU','WSU_IND1','WSU_IND2','WSU_NR','WSU_TAB')


\ten_rkv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Wybranie rejestru
::  OLD: \ten_rkv/vat.fml
::----------------------------------------------------------------------------------------------------------------------
RVAT.cntx_psh(); RVAT.index('SYM'); RVAT.prefix(REF.FIRMA);
{? RVAT.find_key(TMP_VAT.SYM,TMP_VAT.SYM)
|| {? (_odd:=OPERATOR.DEPT)
   || DVAT.index('NR_O'); DVAT.prefix(_odd,kraj,SSTALE.AO,RVAT.ref)
   || DVAT.index('NR'); DVAT.prefix(kraj,SSTALE.AO,RVAT.ref)
   ?};
   {? DVAT.first()
   || _k:=RVAT.KVAT().SYM;
      DVAT.win_sel('WER_ZG');
      {? _k='SprzEksp'
      || DVAT.win_edit('RED_E')
      |? 1+_k='S' | 1+_k='_'
      || DVAT.win_edit('RED_S')
      |? 1+_k='Z' | 4+_k='ImpU'
      || DVAT.win_edit('RED_Z')
      |? 4+_k='ImpT'
      || DVAT.win_edit('RED_I')
      ?};
      DVAT.win_fml(DVAT.win_sel('?')-1,,'SYM1',,'ICON_BEFORE',
         "{? DVAT.E_DOC || 'xwin16.png:51' || '' ?}");
      DVAT.win_patt('SZUK_INN');
      DVAT.hdr_sel();
      DVAT.hdr_sel(' w rejestrze: %1 - %2 - Kraj: %3'@[RVAT.SYM,RVAT.NAZ,DVAT.KRAJ().TR]);
      _a:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
      {? _a<>POMOC.USE
      || POMOC.USE:=_a;
         DOK.use('doku'+_a);
         POZ.use('pozy'+_a);
         POW.use('pow_'+_a);
         AN.use('koan__'+ROK_F.KOD);
         AN_SLU.use('koansl'+ROK_F.KOD)
      ?};
      DVAT.DOK(); POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first;
      PVAT.index('NR'); PVAT.prefix(DOK.ref); PVAT.first;
      DVAT.select()
   || FUN.info('Brak dokumentów w rejestrze.'@)
   ?}
|| FUN.info('Brak rejestru VAT %1'@[TMP_VAT.SYM])
?};
RVAT.cntx_pop()


\ten_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: wybor rejestru VAT
::  OLD: \ten_vat/dr_pr.fml
::----------------------------------------------------------------------------------------------------------------------
wyb:=1;
sel_exit


\vat7par3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Opis parametru wydrukow VAT (Wydruk dokumentow:)
::  OLD: \vat7par3/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.PAR3=1
|| 'wszystkich'
|?  VAT7.PAR3=2
|| 'z innych '+{? VAT7.TYPOKR='M' || 'miesięcy' || 'kwartałów' ?}
|| 'z bieżącego '+{? VAT7.TYPOKR='M' || 'miesiąca' || 'kwartału' ?}
?}


\par_1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.40]
:: OPIS: Ustawiane zmiennych kraj i wal_op w wydrukach VAT (1)
::  OLD: \par_1/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
kraj:=VAT7.KRAJ().KOD+' '+VAT7.KRAJ().TR;
RVAT.cntx_psh(); DVAT.cntx_psh();
RVAT.index('RVAT_ESY'); RVAT.prefix(REF.FIRMA,rejestr);
{? RVAT.first()
|| {? ~OPERATOR.DEPT
   || DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO,RVAT.ref())
   || DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,VAT7.KRAJ,SSTALE.AO,RVAT.ref())
   ?};
   {? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?}
?};
RVAT.cntx_pop(); DVAT.cntx_pop()


\par_4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.40]
:: OPIS: Ustawiane zmiennych kraj i wal_op w wydrukach VAT (4)
::  OLD: \par_4/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
kraj:=VAT7.KRAJ().KOD+' '+VAT7.KRAJ().TR; RVAT.cntx_psh(); DVAT.cntx_psh();
{? zak=3 | zak=4
|| RVAT.index('RVAT_ESY'); RVAT.prefix(REF.FIRMA,rejestr);
   {? RVAT.first()
   || {? ~OPERATOR.DEPT
      || DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO,RVAT.ref)
      || DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,VAT7.KRAJ,SSTALE.AO,RVAT.ref)
      ?};
      {? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?}
   ?}
|? zak=2
|| {? ~OPERATOR.DEPT
   || DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO)
   || DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,VAT7.KRAJ,SSTALE.AO)
   ?};
   {? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?}
|| {? ~OPERATOR.DEPT
   || DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO,RVAT.ref)
   || DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,VAT7.KRAJ,SSTALE.AO,RVAT.ref)
   ?};
   {? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?}
?};
RVAT.cntx_pop(); DVAT.cntx_pop()


\vat_nal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.40]
:: OPIS: Sprawdza czy w rejestrze sa jakies dokumenty dotyczace VAT
::       naliczonego\naleznego
::   WE: _a=1 - vat naliczony, 2 - nalezny
::  OLD: \vat_nal/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_znal:=0;
PVAT.cntx_psh();
PVAT.index('DVAT_ST'); PVAT.prefix(DVAT.ref);
{? PVAT.first()
|| {! |?
      {? (_a=1 & (1+PVAT.GRVAT().KOD='Z')) | _a=2
      || _znal:=1
      ?};
      ~_znal & PVAT.next()
   !}
?};
PVAT.cntx_pop();
_znal


\chk_okr_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy dokument VAT zgodny z parametrem wydruku (dokumenty wszystkie, z innych okresow, z biezacego)
::  OLD: \chk_okr_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.TYPOKR='M'
|| _v1:=DVAT.DOKOKRO().NR; _v2:=SSTALE.AO().NR
|| _v1:=exec('kw_vat','fks_vat',DVAT.DOKOKRO);
   _v2:=exec('kw_vat','fks_vat',SSTALE.AO)
?};
_r1:=DVAT.DOKOKRO().ROK().POCZ_ROK; _r2:=SSTALE.AO().ROK().POCZ_ROK;
(VAT7.PAR3=1 | VAT7.PAR3=2 & (_v1<>_v2 | _r1<>_r2)) | (VAT7.PAR3=3 & _v1=_v2 & _r1=_r2 )


\wsu_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Czy pozycja dokument VAT dotyczy wewnatrz wspolnotowego swiadczenia uslug
::  OLD: \wsu_p/vat.fml
::----------------------------------------------------------------------------------------------------------------------
(PVAT.GRVAT().KOD='SprPKWSU' & DVAT.RVAT().KVAT().SYM='_SprzNOp') | (PVAT.GRVAT().KOD='SprPozKr' & DVAT.RVAT().KVAT().SYM='SprzEksp')


\wystrokokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Sprawdzanie dokumentu VAT, czy pochodzi ze wskazanego roku lub okresu
::  OLD: \wystrokokr/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? VAT7.WYSTROK=null & VAT7.WYSTOKRO=null
|| _zwrot:=1
|? VAT7.WYSTOKRO<>null
|| _zwrot:=(VAT7.WYSTOKRO=DVAT.DOKOKRO)
|? VAT7.WYSTROK<>null
|| ROK_F.cntx_psh();
   _zwrot:=(VAT7.WYSTROK=DVAT.DOKOKRO().ROK);
   ROK_F.cntx_pop()
?};
_zwrot


\spr_roz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [$7.60]
:: OPIS: Sprawdzenie czy jest zaplata do faktury
::   WE: _a - dokument (ref DOK)
::       _b - identyfikator
::       _c - kod roku
::       _d - numer okresu
::       _e - sprawdzanie zaplaty do dnia (data zerowa - tylko biezaca kartoteka rozrachunkow)
::       _f - data wystawienia dokumentu VAT
::  OLD: \spr_roz1/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
DOK.cntx_psh(); POZ.cntx_psh();
_zwrot:=date(0,0,0); _znal:=0;
exec('mask','dok_fks_ks',_c,_d);
POZ.index('ROZR2'); POZ.prefix(_a);
{? POZ.last()
|| {! |?
      {? POZ.ODD=null
      || 0
      |? POZ.ID=_b & (1+POZ.KON)='2' & (1+POZ.TID)='Z'
      || _znal:=1; 0
      || POZ.prev()
      ?}
   !}
?};
{? _znal
|| _konto:=POZ.KON; _sum_wn:=0; _kodnext:=_kodprev:='';
   OP.cntx_psh(); ZAP_OP.cntx_psh();
   {! |?
      {? _kodnext<>''
      || ZAP_OP.use('rozzap'+_kodnext);
         OP.use('operac'+_kodnext);
         POZ.cntx_psh();
         POZ.use('pozy'+_kodnext+'00');
         POZ.index('PKON'); POZ.prefix(_konto);
         {? POZ.first() & POZ.PKON=_konto || _konto:=POZ.KON ?};
         POZ.cntx_pop()
      ?};
      OP.index('KON_O'); OP.prefix(FINFO.NAROD,DVAT.DOK().ODD,_konto,_b,_b,POZ.SYM_ROK);
      {? OP.first()
      || ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
         {? ZAP_OP.first
         || {! |?
               {? ZAP_OP.WN>0 | ZAP_OP.MA<0
               || _sum_wn+=ZAP_OP.WN;
                  {? ZAP_OP.MA<0 || _sum_wn+=(|ZAP_OP.MA) ?};
                  {? _sum_wn>=OP.MA || _zwrot:=ZAP_OP.DATA; 0 || ZAP_OP.next() ?}
               || ZAP_OP.next()
               ?}
            !}
         ?}
      || _kodnext:=''
      ?};
      {? _zwrot=date(0,0,0) & _e<>date(0,0,0) & _kodnext=''
      || OKRO_F.cntx_psh();
         OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
         {? OKRO_F.last() & OKRO_F.prev() & OKRO_F.KON<>date(0,0,0) & _e>OKRO_F.KON
         || ROK_F.cntx_psh();
            ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
            SSTALE.AR();
            {? ROK_F.next() || _kodnext:=ROK_F.KOD ?};
            ROK_F.cntx_pop()
         || ROK_F.cntx_psh();
            ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA);
            {? _kodprev<>''
            || ROK_F.find_key(_kodprev)
            || SSTALE.AR()
            ?};
            {? ROK_F.prev() & ROK_F.POCZ_ROK~1>=_f~1 || _kodnext:=ROK_F.KOD; _kodprev:=ROK_F.KOD ?};
            ROK_F.cntx_pop()
         ?};
         OKRO_F.cntx_pop();
         _kodnext<>''
      || 0
      ?}
   !};
   OP.cntx_pop(); ZAP_OP.cntx_pop()
?};
exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR);
DOK.cntx_pop(); POZ.cntx_pop();
_zwrot


\wsu_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Tworzy tabele na potrzeby wydruku Wewnatrzwspolnotowego swiadczenia uslug
::  OLD: \wsu_gen/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('WSU_TAB')<=0
|| WSU_TAB:=tab_tmp(2,
      'ST_VAT','STRING[8]',,
      'NAZ','STRING[100]',,
      'VAT','REAL',,
      'VAT_BEZ','REAL',,
      'NETTO','REAL',,
      'BRUTTO','REAL',,
      'LP','INTEGER',,
      'P0','INTEGER',,
      'P1','STRING['+$szer[1]+']',,
      'P2','STRING['+$szer[2]+']',,
      'P3','STRING['+$szer[3]+']',,
      'P4','STRING['+$szer[4]+']',,
      'P5','STRING['+$szer[5]+']',
   );
   WSU_IND1:=WSU_TAB.index('?');
   WSU_IND2:=WSU_TAB.ndx_tmp('',1,'LP',,0);
   WSU_NR:=0
|| WSU_TAB.erase();
   WSU_TAB.index(WSU_IND1);
   WSU_TAB.prefix()
?};
DVAT.cntx_psh();
WSU_DRU:=DVAT.next();
DVAT.cntx_pop();
_sum1:=0; _vat:=0;
{? _a=0 | DVAT.next()
|| _kl_vat:=DVAT.RVAT().KVAT().SYM;
   PVAT.cntx_psh(); _lp:=1;
   PVAT.index('DVAT_ST'); PVAT.prefix(DVAT.ref());
   {? PVAT.first()
   || {!
      |? _tr:={? PVAT.OP<>'' || PVAT.OP || DOK.TR ?};
         {? ((PVAT.GRVAT().KOD='SprPKWSU' & _kl_vat='_SprzNOp') | (PVAT.GRVAT().KOD='SprPozKr' & _kl_vat='SprzEksp'))
            & exec('wsu_war','!fks_vat_zrej',VAT7.PAR1)
         || {? WSU_TAB.find_key(PVAT.STVAT().KOD,_tr) & WSU_TAB.NAZ=_tr
            || {? VAT7.PAR1=1
               || {? UNPAR.P1 & (tryb=2 | tryb=3)
                  || WSU_TAB.VAT+=PVAT.VAT_ODL; WSU_TAB.VAT_BEZ+=PVAT.VATKOSZT
                  || _grupa:=|(-PVAT.GRVAT().KOD);
                     {? tryb=4 | _grupa='zinwpodz' | _grupa='zinwepod' | _grupa='zakpropz' |
                       _grupa='zakuprop' | _grupa='zakupodz' | _grupa='zakuppod'
                     || WSU_TAB.VAT+=PVAT.VAT
                     || WSU_TAB.VAT_BEZ+=PVAT.VAT
                     ?}
                  ?};
                  WSU_TAB.NETTO+=PVAT.SUM2;  WSU_TAB.BRUTTO+=PVAT.SUM1
               || _sum1+=PVAT.SUM1; _vat+=PVAT.VAT;
                  WSU_TAB.BRUTTO+=PVAT.SUM1;
                  WSU_TAB.VAT_BEZ+=PVAT.VAT;
                  WSU_TAB.NETTO+=PVAT.SUM2;
                  WSU_TAB.VAT+=PVAT.VAT
               ?};
               WSU_TAB.put()
            || WSU_TAB.blank();
               dru_rej+=1;
               WSU_TAB.LP:=_lp; _lp+=1;
               WSU_TAB.ST_VAT:=PVAT.STVAT().KOD;
               WSU_TAB.NAZ:=_tr;
               {? WSU_TAB.LP=1 & var_pres('w55')>0
               || _sym:=w55
               || _sym:=''
               ?};
               _sym+={? _sym<>'' & _tr<>'' || ', ' || '' ?}+_tr;
               {? WSU_TAB.LP=1 || STR.split(_sym) ?};
               WSU_TAB.P5:=STR.line(szer[5]);
               {? VAT7.PAR1=1
               || {? UNPAR.P1 & (tryb=2 | tryb=3)
                  || WSU_TAB.VAT+=PVAT.VAT_ODL; WSU_TAB.VAT_BEZ+=PVAT.VATKOSZT
                  || _grupa:=|(-PVAT.GRVAT().KOD);
                     {? tryb=4 | _grupa='zinwpodz' | _grupa='zinwepod' | _grupa='zakpropz' |
                       _grupa='zakuprop' | _grupa='zakupodz' | _grupa='zakuppod'
                     || WSU_TAB.VAT+=PVAT.VAT
                     || WSU_TAB.VAT_BEZ+=PVAT.VAT
                     ?}
                  ?};
                  WSU_TAB.NETTO+=PVAT.SUM2;  WSU_TAB.BRUTTO+=PVAT.SUM1
               || _sum1+=PVAT.SUM1; _vat+=PVAT.VAT;
                  WSU_TAB.BRUTTO+=PVAT.SUM1;
                  WSU_TAB.VAT_BEZ+=PVAT.VAT;
                  WSU_TAB.NETTO+=PVAT.SUM2;
                  WSU_TAB.VAT+=PVAT.VAT
               ?};

::               {? ~rep_expo()
::               || {!
::                  |? _l:=STR.line(szer[5]);
::                     {? _l<>''
::                     || WSU_TAB.P5:=_l;
::                        1
::                     ?}
::                  !}
::               ?};
               WSU_TAB.add()
            ?}
         ?};
         PVAT.next()
      !};
      WSU_TAB.index(WSU_IND2); WSU_TAB.prefix();
::    w[13]:=DVAT.DAT2$1; w[14]:={? rejestr<>'_WWspN' || DVAT.DAT4$1 || '' ?};
      _m_dok0:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2); _m_dok1:=(DOK.name()+4);
      {? _m_dok0<>_m_dok1 || DOK.use('doku'+_m_dok0); POZ.use('pozy'+_m_dok0); POW.use('pow_'+_m_dok0); DOK.prefix() ?};
      {? WSU_TAB.find_key(1)
      || {? _a=0 || WSU_NR+=1 ?};
         WSU_TAB.P0:=WSU_NR;
         WSU_TAB.P1:=DVAT.RVAT().SYM;
         WSU_TAB.P2:=DVAT.SYM1;
         WSU_TAB.P3:=DVAT.DAT1$1;
         DVAT.DOK().REJ();
         WSU_TAB.P4:={? OPERATOR.DEPT=null
                     || REJ.ODD().OD
                     || REJ.KOD+(8-(+REJ.KOD))*' '+form(DVAT.DOK().NR,szer[4]-8,,'99')
                     ?};
::         {? VAT7.PAR1=3
::         || WSU_TAB.BRUTTO:=_sum1;
::            WSU_TAB.VAT_BEZ:=_vat
::         ?};
         WSU_TAB.put()
      ?};
      {? ~rep_expo()
      || {? WSU_TAB.find_key(2)
         || WSU_TAB.P1:=$DVAT.NR;
            WSU_TAB.P2:=DVAT.DAT3$1;
            WSU_TAB.P3:=DVAT.DAT2$1;
            WSU_TAB.P4:={? OPERATOR.DEPT=null || DVAT.DOK().REJ().KOD+(8-(+DVAT.DOK().REJ().KOD))*' '+form(DVAT.DOK().NR,szer[4]-8,,'99') || ''?};
            WSU_TAB.put()
         || WSU_TAB.blank();
            WSU_TAB.LP:=2;
            WSU_TAB.P1:=$DVAT.NR;
            WSU_TAB.P2:=DVAT.DAT3$1;
            WSU_TAB.P3:=DVAT.DAT2$1;
            WSU_TAB.P4:={? OPERATOR.DEPT=null || DVAT.DOK().REJ().KOD+(8-(+DVAT.DOK().REJ().KOD))*' '+form(DVAT.DOK().NR,szer[4]-8,,'99') || ''?};
            WSU_TAB.P5:=STR.line(szer[5]);
            WSU_TAB.add()
         ?}
      ?}
   ?};
:: WSU_TAB.win_sel(WSU_TAB.mk_sel(,,1)); WSU_TAB.select();
   PVAT.cntx_pop()
?}


\wsu_line
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Tworzy tabele na potrzeby wydruku Wewnatrzwspolnotowego swiadczenia uslug
::  OLD: \wsu_line/vat.fml
::----------------------------------------------------------------------------------------------------------------------
w[1]:=WSU_TAB.P1;
w[2]:=WSU_TAB.P2;
w[3]:=WSU_TAB.P3;
::w[13]:=DVAT.DAT2$1; w[14]:={? rejestr<>'_WWspN' || DVAT.DAT4$1 || '' ?};
w[4]:=WSU_TAB.P4;
w[5]:=WSU_TAB.P5;
{? VAT7.PAR1=1
|| w[6]:=form(WSU_TAB.ST_VAT,,2,' ,');
   {? WSU_TAB.NETTO | WSU_TAB.VAT | WSU_TAB.VAT_BEZ | WSU_TAB.BRUTTO
   || w[7]:=form(WSU_TAB.NETTO,,2,' ,');
      w[8]:=form(WSU_TAB.VAT,,2,' ,');
      w[19]:=form(WSU_TAB.VAT_BEZ,,2,' ,');
      w[9]:=form(WSU_TAB.BRUTTO,,2,' ,')
   || w[7]:=w[8]:=w[19]:=w[9]:=''
   ?};
   {? WSU_TAB.LP=1 || w[18]:=$WSU_TAB.P0 || w[18]:='' ?}
|| {? WSU_TAB.NETTO | WSU_TAB.VAT | WSU_TAB.VAT_BEZ | WSU_TAB.BRUTTO
   || {? WSU_TAB.LP=1
      || w[6]:=form(WSU_TAB.BRUTTO,,2,' ,');
         w[10]:=form(WSU_TAB.VAT_BEZ,,2,' ,')
      || w[6]:=w[10]:=''
      ?};
      w[7]:=form(WSU_TAB.ST_VAT,,2,' ,');
      w[8]:=form(WSU_TAB.NETTO,,2,' ,');
      w[6]:=form(WSU_TAB.BRUTTO,,2,' ,');
      w[10]:=form(WSU_TAB.VAT_BEZ,,2,' ,');
      w[9]:=form(WSU_TAB.VAT,,2,' ,')
   || w[6]:=w[7]:=w[8]:=w[9]:=w[10]:=''
   ?};
   {? WSU_TAB.LP=1 || w[18]:=$WSU_TAB.P0 || w[18]:='' ?}
?}


\wsu_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Tworzy tabele na potrzeby wydruku Wewnatrzwspolnotowego swiadczenia uslug
::  OLD: \wsu_war/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| 1
|? _a=3
|| _ok:=0; _i:=1;
   {!
   |? {? s[_i]<>PVAT.STVAT().KOD
      || {? _i<b || _i+=1 || 0 ?}
      || _ok:=1;
         0
      ?}
   !};
   _ok
?}


\sql_zrodlo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Formula pomocnicza do vatrej6.rpi
::  OLD: \sql_zrodlo/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WYSTOKRO<>null
|| ' and DP_OKR.REFERENCE=\''+$VAT7.WYSTOKRO+'\' '
|? VAT7.WYSTROK<>null
|| ' and DP_ROK.REFERENCE=\''+$VAT7.WYSTROK+'\' '
|| ' '
?}


\par_5
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.40]
:: OPIS: Ustawiane zmiennych kraj i wal_op w wydrukach VAT (5)
::  OLD: \par_5/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
kraj:=VAT7.KRAJ().KOD+' '+VAT7.KRAJ().TR;
RVAT.cntx_psh(); DVAT.cntx_psh();
{? zakup=0
|| {? ~OPERATOR.DEPT
   || DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO,RVAT.ref())
   || DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,VAT7.KRAJ,SSTALE.AO,RVAT.ref())
   ?};
   {? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?}
|| RVAT.index('RVAT_ESY'); RVAT.prefix(REF.FIRMA,rejestr);
   {? RVAT.first()
   || {? ~OPERATOR.DEPT
      || DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO,RVAT.ref())
      || DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,VAT7.KRAJ,SSTALE.AO,RVAT.ref())
      ?};
      {? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?}
   ?}
?};
RVAT.cntx_pop(); DVAT.cntx_pop()


\par_6
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [WHAN] [21.14]
:: OPIS: Ustawiane zmiennych kraj i wal_op w wydrukach VAT (6)
::----------------------------------------------------------------------------------------------------------------------
kraj:=VAT7.KRAJ().KOD+' '+VAT7.KRAJ().TR;
wal_op:=VAT7.WAL().KOD+' '+VAT7.WAL().TR


\in_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2009+]
:: OPIS: Wykonywana w ciele wydruku
::   WE: _a - nazwa pliku z wydrukiem
::  OLD: \in_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='fks_vat_rej'
|| rejestr:=RVAT.SYM; tytul:='WYDRUK REJESTRU VAT - '+rejestr+' - ';
   {? RVAT.KVAT().SYM='ZakOpod' | KVAT.SYM='ZakOpoZ'
      & FUN.ask('Wybrano rejestr z klasą ewidencji VAT %1 używaną w rejestrach VAT sprzedaży i zakupu.\nCzy traktować rejestr VAT jako SPRZEDAŻ?'@[KVAT.SYM])
   || spr_zlom:=1
   || spr_zlom:=0
   ?};
   VAT7.win_edit(
    {? 4+RVAT.KVAT().SYM='Sprz' | 6+RVAT.KVAT().SYM='_WWspD'  | 2+RVAT.KVAT().SYM='_S' | spr_zlom
    || VAT7.TYP_VAT:='Z'; 'REJSP4'
    |? KVAT.SYM='_WWspNus' || 'REJSP5'
    |? 3+RVAT.KVAT().SYM='Zak' | 6+RVAT.KVAT().SYM='_WWspN'
    || UNPAR.P1_BE:=UNPAR.P1_AE:=UNPAR.P1_BV:=''; VAT7.TYP_VAT:='C';
       {? 1+KVAT.SYM='_'
       || 'REJZAKU2'
       |? -KVAT.SYM='zakopod'
       || 'REJZAK7'
       || 'REJZAK5'
       ?}
    |? 4+RVAT.KVAT().SYM='ImpU' || 'REJSP5'
    |? 4+RVAT.KVAT().SYM='ImpT' || 'REJZAK6'
    ?});
   exec('vat7_p','!fks_vat_zrej');
   {? RVAT.KVAT().SYM='SprzEksp' || eksport:=1 ?};
   VAT7.hdr_edit();
   VAT7.hdr_edit({? var_pres('hdr_vat')<=0 || 'WYDRUK REJESTRU VAT' || ~(-hdr_vat) ?});
   {? var_pres('hdr_vat')<=0  || hdr_vat:=tytul || hdr_vat:=~(-hdr_vat) ?};
   rvat_sym:=rejestr
?}


\par_2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.40]
:: OPIS: Ustawiane zmiennych kraj i wal_op w wydrukach VAT (2)
::  OLD: \par_2/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
kraj:=VAT7.KRAJ().KOD+' '+VAT7.KRAJ().TR; DVAT.cntx_psh();
{? ~OPERATOR.DEPT
|| DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO,RVAT.ref())
|| DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,VAT7.KRAJ,SSTALE.AO,RVAT.ref())
?};
{? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?}; DVAT.cntx_pop()


\par_3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.40]
:: OPIS: Ustawiane zmiennych kraj i wal_op w wydrukach VAT (3)
::  OLD: \par_3/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
kraj:=VAT7.KRAJ().KOD+' '+VAT7.KRAJ().TR; DVAT.cntx_psh();
{? ~OPERATOR.DEPT
|| DVAT.index('NR'); DVAT.prefix(VAT7.KRAJ,SSTALE.AO)
|| DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,VAT7.KRAJ,SSTALE.AO)
?};
{? DVAT.first() || wal_op:=DVAT.WAL().KOD+' '+DVAT.WAL().TR ?};
DVAT.cntx_pop()


\wyp_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [7.53] 02.11.2001
:: OPIS: Dopisuje wartosci przedzialow dla poziomow sumowania jesli sa puste
::   WE: _a - Numer poziomu (1,2,3)
::       _b - 0 (przedzial od), 1 (przedzial do)
::  OLD: \wyp_zakr/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_co:='';
{? _a=1
|| _p:="VAT7.POZIOM1";  _p_spr:={? ~_b || "VAT7.POZ1OD" || "VAT7.POZ1DO" ?}
|? _a=2
|| _p:="VAT7.POZIOM2";  _p_spr:={? ~_b || "VAT7.POZ2OD" || "VAT7.POZ2DO" ?}
|| _p:="VAT7.POZIOM3";  _p_spr:={? ~_b || "VAT7.POZ3OD" || "VAT7.POZ3DO" ?}
?};
{? -($_p())='rejestry vat'
|| RVAT.cntx_psh();
   RVAT.index('SYM'); RVAT.prefix(REF.FIRMA);
   {? (~_b & RVAT.first) | (_b & RVAT.last) ||  _co:=RVAT.SYM ?};
   RVAT.cntx_pop()
|? -($_p())='klasy ewidencji'
|| KVAT.cntx_psh();
   KVAT.index('SYM'); KVAT.clear();
   {? (~_b & KVAT.first) | (_b & KVAT.last) ||  _co:=KVAT.SYM ?};
   KVAT.cntx_pop
|? (-($_p())='stawki vat') |  (-($_p())='grupy podatkowe') | (-($_p())='rodzaje ewidencji vat')
|| SLU.index('NAZ');
   {? (-($_p())='rodzaje ewidencji vat')
   || SLU.prefix('~RODZAJE EWIDENCJI')
   |? (-($_p())='stawki vat')
   || SLU.prefix('~STAWKI VAT '+VAT7.KRAJ().KOD)
   || SLU.prefix('~'+~(-($_p())))
   ?};
   {? SLU.first
   || SLO.index('SL'); SLO.prefix(SLU.ref);
      {? (~_b & SLO.first) | (_b & SLO.last) ||  _co:=SLO.KOD ?}
   ?}
|? -($_p())='rodzaje podatku vat'
|| {? ~_b
   || _co:='Należny'
   || _co:='Naliczony'
   ?}
?};
_p_spr():=_co


\kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [7.53] 02.11.2001
:: OPIS: Sprawdza co ma byc w danej kolumnie
::   WE: _a - Numer poziomu (1,2,3)
::       _b - Czy dodatkowa kolumna do poziomu (0,1)
::  OLD: \kol/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1 || _p:="VAT7.POZIOM1"
|? _a=2 || _p:="VAT7.POZIOM2"
|| _p:="VAT7.POZIOM3"
?};
{? -($_p())='rejestry vat'
 ||  _co:={? _b=0 ||"PVAT.DVAT().RVAT().SYM"
                  ||"PVAT.DVAT().RVAT().NAZ"?}
 |?  -($_p())='klasy ewidencji'
 ||  _co:={? _b=0 ||"PVAT.DVAT().RVAT().KVAT().SYM"
                  ||"PVAT.DVAT().RVAT().KVAT().NAZ"?}
 |?  (-($_p())='stawki vat')
 || _co:="PVAT.STVAT().KOD"
 |? (-($_p())='grupy podatkowe')
 || _co:={? _b=0 ||"PVAT.GRVAT().KOD"
                 ||"PVAT.GRVAT().TR"?}
 |? (-($_p())='rodzaje ewidencji vat')
 || _co:={? _b=0 ||"PVAT.DVAT().RVAT().EWID().KOD"
                 ||"PVAT.DVAT().RVAT().EWID().TR"?}
 || _co:="{? ile=0 & (1+PVAT.GRVAT().KOD)='Z'
          || _r:=PVAT.DVAT().RVAT().KVAT().SYM;
             {? 4+_r='ImpU' | _r='ImpTowUp' | 6+_r='_WWspN'  | 4+_r='ZakO' || ile:=1 ?};
             'Naliczony'
          || ile:=0; 'Należny'
          ?}"
?};
_co


\dok_vat_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzy tabele na potrzeby wydruku vat_okr
::   WY: _wyn - 1/0
::  OLD: \dok_vat_okr/vat.fml
::----------------------------------------------------------------------------------------------------------------------
RVAT.cntx_psh();
REJ.cntx_psh();
_w:=0;
{? OPERATOR.DEPT || REJ.win_dict('SLO1') || REJ.win_dict('SLO1_ODD') ?};
RVAT.win_dict('WER3');
VAT7.win_edit('VAT_OKR');
VAT7.TYP_VAT:='';
{? VAT7.edit()
|| VAR_DEL.delete('__DOK');
   _kw:=exec('kw_vat','fks_vat');
   _od:=(_kw-1)*3+1;
   _do:=_od+2;
   __DOK:=sql(
      'select '+exec('mask_okr_vat','!fks_vat_zrej')+
      'VPOZ.REFERENCE as REF, '+
      'RVAT.RT, '+
      'ODD.OD||\'/\'||REJ.KOD as REJ, '+
      'DOK.NR as RNR,'+
      'RVAT.SYM as REJVAT, '+
      'RVAT.NAZ as REJVATN, '+
      'DOK.NK as SYM, '+
      'DOK.DTO as DWYS, '+
      'DOK.D3 as TZ, '+
      'SLO.TR as KH, '+
      'SUBSTR(TO_STRING(OKRO_F.POCZ),6,2) as OVAT, '+
      'SUBSTR(TO_STRING(OKRO_F.POCZ),1,4) as ROK_F, '+
      'VPOZ.NETTO, '+
      'VPOZ.VAT, '+
      'SLO2.KOD as VATP, '+
      'VPOZ.BRUTTO, '+
      'case RVAT.RT '+
         'when \'S\' then  \'Dostawa\' '+
         'when \'Z\' then  \'Nabycie\' '+
         'when \'E\' then  \'Eksport\' '+
         'when \'I\' then  \'Import\' '+
         'when \'W\' then  \'Wewnątrzwsp.\' '+
      'end as RTN, '+
      'case RVAT.RT '+
         'when \'S\' then  \'Dostawa towarów na terytorium kraju\' '+
         'when \'Z\' then  \'Nabycie towarów i usług\' '+
         'when \'E\' then  \'Eksport towarów\' '+
         'when \'I\' then  \'Import towarów\' '+
         'when \'W\' then  \'Operacje wewnątrzwspólnotowe\' '+
      'end as RTNB, '+
      '\' \' as AN_L ' +
      'from @VPOZ '+
      'join @DOK using (DOK.REFERENCE,VPOZ.DOK) '+
      'join VAT_REJ using (VAT_REJ.REFERENCE, VPOZ.RVAT) '+
      'join RVAT using (RVAT.REFERENCE,VAT_REJ.RVAT) '+
      'join REJ using (REJ.REFERENCE,DOK.REJ)  '+
      'join ODD using (ODD.REFERENCE,REJ.ODD)  '+
      'left join SLO using (SLO.REFERENCE, DOK.WYS) '+
      'join SLO as SLO2 using (SLO2.REFERENCE, VPOZ.PR) '+
      'left join OKRO_F using (OKRO_F.REFERENCE, VPOZ.OKRVAT) '+
      'left join ROK_F using (ROK_F.REFERENCE, OKRO_F.ROK) '+
      'where '+
      'DOK.ZP=\'T\' and '+
      {? OPERATOR.DEPT
      || 'ODD.REFERENCE=\''+$OPERATOR.DEPT+'\' and'
      || ''
      ?}+
      {? VAT7.TYPOKR='M'
      || '(OKRO_F.NR<>SUBSTR(DOK.REFERENCE,7,2) OR VPOZ.OKRVAT is null )'
      || '(VPOZ.OKRVAT is null OR '+
         'ROK_F.REFERENCE=\''+$SSTALE.AR+'\' AND (OKRO_F.NR<'+$_od+' OR OKRO_F.NR>'+$_do+') OR '+
         'ROK_F.REFERENCE<>\''+$SSTALE.AR+'\''+
         ')'
      ?}+
      {? VAT7.RTRAN<>'D' || ' and RVAT.RT=\''+VAT7.RTRAN+'\'' || '' ?}+
      {? VAT7.REJ || ' and REJ.REFERENCE=\''+$VAT7.REJ+'\'' || '' ?}+
      {? VAT7.RVAT_N || ' and RVAT.REFERENCE=\''+$VAT7.RVAT_N+'\'' || '' ?}
   );
   {? __DOK.first()
   || VPOZ_ROZ.cntx_psh();
      VPOZ.cntx_psh();
      {!
      |? {? __DOK.OVAT=''
         || VPOZ_ROZ.use('pozr'+(4+(4-__DOK.REF)));
            VPOZ_ROZ.index('VPOZ_ROZ');
            VPOZ_ROZ.prefix(BIT.sqlint(__DOK.REF));
            {? VPOZ_ROZ.first()
            || {!
               |? {? VAT7.TYPOKR='M' & VPOZ_ROZ.OKRO_F<>SSTALE.AO |
                     VAT7.TYPOKR='K' & (
                     VPOZ_ROZ.OKRO_F().ROK=SSTALE.AR & (VPOZ_ROZ.OKRO_F().POCZ~3<_od | OKRO_F.POCZ~3>_do) |
                     OKRO_F.ROK<>SSTALE.AR
                     )
                  || __DOK.cntx_psh();
                     __DOK.NETTO:=VPOZ_ROZ.NETTO;
                     __DOK.VAT:=VPOZ_ROZ.VAT;
                     __DOK.BRUTTO:=VPOZ_ROZ.BRUTTO;
                     __DOK.OVAT:=('0'+$(VPOZ_ROZ.OKRO_F().POCZ~2))+2;
                     __DOK.ROK_F:=$(OKRO_F.ROK().POCZ_ROK~1);
                     __DOK.add();
                     __DOK.cntx_pop()
                  ?};
                  __DOK.NETTO-=VPOZ_ROZ.NETTO;
                  __DOK.VAT-=VPOZ_ROZ.VAT;
                  __DOK.BRUTTO-=VPOZ_ROZ.BRUTTO;
                  VPOZ_ROZ.next()
               !};
               __DOK.put();
               __DOK.next()
            |? VAT7.TYP_VAT<>''
            || VPOZ.use(8+__DOK.REF);
               VPOZ.prefix();
               {? VPOZ.seek(BIT.sqlint(__DOK.REF),)
               || {? VAT7.TYP_VAT='C' & VPOZ.OKR_C=SSTALE.AO | VAT7.TYP_VAT='Z' & VPOZ.OKR_Z=SSTALE.AO
                  || __DOK.del()
                  || __DOK.next()
                  ?}
               || __DOK.next()
               ?}
            || __DOK.next()
            ?}
         || __DOK.next()
         ?}
      !};
      VPOZ_ROZ.cntx_pop();
      VPOZ.cntx_pop();
      _w:=1
   || FUN.info('Brak dokumentów VAT\nspełniających zadane parametry.'@);
      _w:=0
   ?}
?};
REJ.cntx_pop();
RVAT.cntx_pop();
_w


\mask_okr_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca zawartość klauzuli MASK_FILTER
::  OLD: \mask_okr_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('okr_vat_wydr','!fks_vat_zrej');
_mdok:='';
_mvpoz:='';
OKRO_F.prefix();
{! _i:=1..obj_len(_tab)
|! {? OKRO_F.seek(_tab[_i])
   || _mdok+=',\'doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2)+'\'';
      _mvpoz+=',\'pozv'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2)+'\''
   ?}
!};
'/*+MASK_FILTER(DOK'+_mdok+') MASK_FILTER(VPOZ'+_mvpoz+')*/'


\czyt_vat7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: wczytanie danych uzupełniających do deklaracji VAT7
::  OLD: \czyt_vat7/dok2zest.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=exec('czy_ks','fks_vat');
{? _ok
|| w1:=FUN.choice('Rodzaj podatnika'@,1,'Niebędący osobą fizyczną'@,'Osoba fizyczna'@);
   {? w1>0
   || w2:=FUN.choice('Dołączono wniosek o zwrot podatku?'@,2,'Tak'@,'Nie'@)
   || w2:=0
   ?};
   {? w2=0 || _ok:=0 ?};
   {? _ok
   || undefine;
      define('k33',0,'Pozycja 33 w zł: '@,'Podatek należny od tow. objętych spisem z natury'@,,,2);
      define('D1',~~,'D. ROZLICZENIE PODATKU NALICZONEGO '@);
      define('k36',0,'Pozycja 36 w zł: '@,'Kwota nadwyżki z poprzedniej deklaracji',,,2);
      define('k37',0,'Pozycja 37 w zł: '@,'Podatek naliczony wynikający z rachunków uproszczonych'@,,,2);
      define('k38',0,'Pozycja 38 w zł: '@,'Zak. opodatk. (śr. trw.) zw. wył. ze sprz. opodatk. - netto'@,,,2);
      define('k39',0,'Pozycja 39 w zł: '@,'Zak. opodatk. (śr. trw.) zw. wył. ze sprz. opodatk. - podatek'@,,,2);
      define('k40',0,'Pozycja 40 w zł: '@,'Zak. opod. (śr. trw.) zw. ze sprz. zwoln. oraz opod. - netto'@,,,2);
      define('k41',0,'Pozycja 41 w zł: '@,'Zak. opod. (śr. trw.) zw. ze sprz. zwoln. oraz opod. - podatek'@,,,2);
      define('k42',0,'Pozycja 42 w zł: '@,'Zak. opodatk. związane wył. ze sprz. opodatk. - netto'@,,,2);
      define('k43',0,'Pozycja 43 w zł: '@,'Zak. opodatk. związane wył. ze sprz. opodatk. - podatek'@,,,2);
      define('k44',0,'Pozycja 44 w zł: '@,'Zak. opodatk. związane ze sprz. zwoln. oraz opodatk. - netto'@,,,2);
      define('k45',0,'Pozycja 45 w zł: '@,'Zak. opodatk. związane ze sprz. zwoln. oraz opodatk. - podatek'@,,,2);
      define('k54a',0,'Sprzedaż opodatk. netto z ostat. 6 mies.: '@,'Sprzedaż opodatkowana netto z ostatnich 6-ciu miesięcy'@,,,2);
      define('k54b',0,'Sprzedaż ogółem netto z ostat. 6 mies.: '@,'Sprzedaż ogółem netto z ostatnich 6-ciu miesięcy'@,,,2);
      define('E',~~,'E. OBLICZENIE ZOBOWIĄZANIA PODATKOWEGO LUB ZWROTU'@);
      define('k58',0,'Kwota na zakup kas w zł (poz. 58): '@,'Kwota do odliczenia w danym miesiącu'@,,,2);
      define('k59',0,'Pozycja 59 w zł: '@,'Kwota podatku objęta zaniechaniem poboru'@,,,2);
      define('k62',0,'Kwota na zakup kas w zł (poz. 62): '@,'Kwota przysługująca do zwrotu'@,,,2);
      define('k64',0,'Pozycja 64 w zł: '@,'Kwota do zwrotu na rachunek bankowy'@,,,2);
      {? def_edit(,'CZĘŚCI C - E'@)
      || pv[33]:=DEFINE.k33#0;
         pv[36]:=DEFINE.k36#0;
         pv[37]:=DEFINE.k37#0;
         pv[38]:=DEFINE.k38#0;
         pv[39]:=DEFINE.k39#0;
         pv[40]:=DEFINE.k40#0;
         pv[41]:=DEFINE.k41#0;
         pv[42]:=DEFINE.k42#0;
         pv[43]:=DEFINE.k43#0;
         pv[44]:=DEFINE.k44#0;
         pv[45]:=DEFINE.k45#0;
         s_licz:=DEFINE.k54a;
         s_mian:=DEFINE.k54b;
         pv[58]:=DEFINE.k58;
         pv[59]:=DEFINE.k59#0;
         pv[62]:=DEFINE.k62#0;
         pv[64]:=DEFINE.k64#0
      || _ok:=0
      ?};
      undefine
   ?}
?};
_ok


\be_par3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.PAR3
::  OLD: \be_par3/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
VAT7.PAR1<>4


\ae_wystrok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Po redakcji pola VAT7.WYSTROK
::  OLD: \ae_wystrok/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WYSTROK=null
|| VAT7.WYSTOKRO:=null
|? VAT7.WYSTOKRO<>null
|| OKRO_F.cntx_psh();
   OKRO_F.index('ROK'); OKRO_F.prefix(VAT7.WYSTROK);
   {? ~OKRO_F.seek(VAT7.WYSTOKRO) || VAT7.WYSTOKRO:=null ?};
   OKRO_F.cntx_pop()
?};
1


\ae_wystokres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Po redakcji pola VAT7.WYSTOKRO
::  OLD: \ae_wystokres/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? VAT7.WYSTOKRO=null
|| _zwrot:=1
|| {? VAT7.WYSTOKRO().NR=0
   || FUN.info('Proszę wybrać okres inny niż bilans otwarcia.'@)
   || _zwrot:=1
   ?}
?};
_zwrot


\be_wystokres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Przed redakcja pola VAT7.WYSTOKRO
::  OLD: \be_wystokres/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WYSTROK=null
|| 0
|| VAT7.WYSTROK(); 1
?}


\be_wyd_tech
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.WYD_TECH
::  OLD: \be_wyd_tech/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.PAR1=4
|| 0
|| 1
?}


\ae_wyd_tech
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Po redakcji VAT7.WYD_TECH
::  OLD: \ae_wyd_tech/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WYD_TECH=1
::PR_X001 -Fiks - Rejestry Vat - Wydruki [AZ] 2020-05-14
|| VAT7.WY3:=0;
   exec('vat7_p','!fks_vat_zrej')
::koniec
?};
1


\ae_par1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Po redakcji VAT7.PAR1
::  OLD: \ae_par1/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.PAR1=4
|| VAT7.WY3:=VAT7.WYD_TECH:=0; UNPAR.P1:=0
?};
exec('bv_wy1','!fks_vat_zrej');
win_disp();
1


\be_wy3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.WY3
::  OLD: \be_wy3/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WYD_TECH=1 | VAT7.PAR1=4
|| VAT7.WY3:=0; 0
|| 1
?}


\be_wy1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.WY1
::  OLD: \be_wy1/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.PAR1=1
|| 1
|| VAT7.WY1:=0;
   0
?}


\ae_wy2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Po redakcji VAT7.WY2
::  OLD: \ae_wy2/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WY2=0
|| VAT7.DAT_ZAPL:=date(0,0,0);
   VAT7.TYLKOZAP:=0
?};
1


\be_dat_zapl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.DAT_ZAPL
::  OLD: \be_dat_zapl/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WY2
|| VAT7.DAT_ZAPL:=SSTALE.AO().KON;
   1
|| 0
?}


\be_tylkozap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.TYLKOZAPL
::  OLD: \be_tylkozap/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WY2=1
|| 1
|| VAT7.TYLKOZAP:=0;
   0
?}


\be_poziom1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.POZIOM1
::  OLD: \be_poziom1/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
poziom_1:=VAT7.POZIOM1


\ae_poziom1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Po redakcji VAT7.POZIOM1
::  OLD: \ae_poziom1/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=exec('por_poz','!fks_vat_zrej',1);
{? _ok & (poziom_1<>VAT7.POZIOM1)
|| VAT7.POZ1OD:=VAT7.POZ1DO:=''
?};
_ok


\be_poziom2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.POZIOM2
::  OLD: \be_poziom2/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.POZIOM1<>''
|| poziom_2:=VAT7.POZIOM2;
   1
|| 0
?}


\ae_poziom2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Po redakcji VAT7.POZIOM2
::  OLD: \ae_poziom2/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=exec('por_poz','!fks_vat_zrej',2);
{? _ok & (poziom_2<>VAT7.POZIOM2)
|| VAT7.POZ2OD:=VAT7.POZ2DO:=''
?};
_ok


\be_poziom3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.POZIOM3
::  OLD: \be_poziom3/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.POZIOM2=''
|| 0
|| poziom_3:=VAT7.POZIOM3;
   1
?}


\ae_poziom3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Po redakcji VAT7.POZIOM3
::  OLD: \ae_poziom3/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=exec('por_poz','!fks_vat_zrej',3);
{? _ok & (poziom_3<>VAT7.POZIOM3)
|| VAT7.POZ3OD:=VAT7.POZ3DO:=''
?};
_ok


\be_poz1od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.POZ1OD
::  OLD: \be_poz1od/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.POZIOM1=''
|| 0
|| 1
?}


\be_poz2od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.POZ2OD
::  OLD: \be_poz2od/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.POZIOM2=''
|| 0
|| 1
?}


\be_poz3od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.POZ3OD
::  OLD: \be_poz3od/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.POZIOM3=''
|| 0
|| 1
?}


\por_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [7.53] 25.10.2001
:: OPIS: Sprawdza wprowadzony poziom
::   WE: Numer poziomu (1,2,3)
::  OLD: \por_poz/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? _a=1  || _p:="VAT7.POZIOM1" |? _a=2 || _p:="VAT7.POZIOM2" || _p:="VAT7.POZIOM3" ?};
{? (_a=1 & (|($_p()))='')
||  FUN.info('Proszę wybrać wartość ze słownika.'@); _ok:=0
|?  ((|($_p()))<>'')
||  {?  ~(-($_p())='rejestry vat' |  -($_p())='klasy ewidencji' | -($_p())='stawki vat' |
         -($_p())='rodzaje podatku vat' |  -($_p())='grupy podatkowe' | -($_p())='rodzaje ewidencji vat')
    || FUN.info('Proszę wybrać wartość ze słownika.'@); _ok:=0
    ?}
?};
{? _ok & _p()<>''
||  {? (_a=1 & (VAT7.POZIOM1=VAT7.POZIOM2 | VAT7.POZIOM1=VAT7.POZIOM3)) |
        (_a=2 & (VAT7.POZIOM2=VAT7.POZIOM1 | VAT7.POZIOM2=VAT7.POZIOM3)) |
        (_a=3 & (VAT7.POZIOM3=VAT7.POZIOM1 | VAT7.POZIOM3=VAT7.POZIOM2))
    || FUN.info('Kolumny wydruku nie mogą\nsię powtarzać.'@);  _p():=''; _ok:=0
    ?}
?};
_ok


\f3_zvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [7.53] 25.10.2001
:: OPIS: Wyswietla okienko popup (poziomy sumowania - zest. zb. VAT)
::  OLD: \f3_zvat/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
vf:='';
popup(0,'Poziomy sumowania',
      '1) Rejestry VAT',,"vf:='Rejestry VAT'",
      '2) Klasy ewidencji',,"vf:='Klasy ewidencji'",
      '3) Stawki VAT',,"vf:='Stawki VAT'",
      '4) Rodzaje podatku VAT',,"vf:='Rodzaje podatku VAT'",
      '5) Grupy podatkowe',,"vf:='Grupy podatkowe'",
      '6) Rodzaje ewidencji VAT',,"vf:='Rodzaje ewidencji VAT'");
_a:=vf; &vf; _a


\f3_zkv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [7.53] 25.10.2001
:: OPIS: Wyswietla okienko wyboru w zaleznosci od wybranego poziomu
::   WE: Numer poziomu (1,2,3)
::  OLD: \f3_zkv/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? _a=1  || _a:="VAT7.POZIOM1" |? _a=2 || _a:="VAT7.POZIOM2" || _a:="VAT7.POZIOM3" ?};
{? -($_a())='rejestry vat'
|| RVAT.cntx_psh();
   RVAT.index('SYM'); RVAT.prefix(REF.FIRMA); RVAT.win_sel('WYB2');
   {? RVAT.first & RVAT.select()||  _zwrot:=RVAT.SYM ?};
   RVAT.cntx_pop()
|? -($_a())='klasy ewidencji'
|| KVAT.cntx_psh();
   KVAT.index('SYM'); KVAT.clear(); KVAT.win_sel('WERB');
   {? KVAT.first & KVAT.select()||  _zwrot:=KVAT.SYM ?};
   KVAT.cntx_pop()
|? (-($_a())='stawki vat') |  (-($_a())='grupy podatkowe') | (-($_a())='rodzaje ewidencji vat')
|| SLU.index('NAZ');
   {? (-($_a())='rodzaje ewidencji vat')
   || SLU.prefix('~RODZAJE EWIDENCJI')
   |? (-($_a())='stawki vat')
   || SLU.prefix('~STAWKI VAT '+VAT7.KRAJ().KOD)
   || SLU.prefix('~'+~(-($_a())))
   ?};
   {? SLU.first
   || SLO.index('SL'); SLO.prefix(SLU.ref); SLO.win_sel('ONE_SEL');
      {? SLO.first & SLO.select() || _zwrot:=SLO.KOD ?}
   ?}
|| vf:='';
   popup(0,'Rodzaje podatku VAT',
           '1) Należny',,"vf:='Należny'",
           '2) Naliczony',,"vf:='Naliczony'"); _zwrot:=vf; &vf
?};
_zwrot


\por_poz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [7.53] 25.10.2001
:: OPIS: Sprawdza wprowadzony przedzial dla poziomu
::   WE:  _a - Numer poziomu (1,2,3)
::        _b - 0 (przedzial od), 1 (przedzial do)
::  OLD: \por_poz1/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? _a=1
|| _p:="VAT7.POZIOM1"; _p_od:="VAT7.POZ1OD"; _p_do:="VAT7.POZ1DO";
   _p_spr:={? ~_b || "VAT7.POZ1OD" || "VAT7.POZ1DO" ?}
|? _a=2
|| _p:="VAT7.POZIOM2"; _p_od:="VAT7.POZ2OD"; _p_do:="VAT7.POZ2DO";
  _p_spr:={? ~_b || "VAT7.POZ2OD" || "VAT7.POZ2DO" ?}
|| _p:="VAT7.POZIOM3"; _p_od:="VAT7.POZ3OD"; _p_do:="VAT7.POZ3DO";
  _p_spr:={? ~_b || "VAT7.POZ3OD" || "VAT7.POZ3DO" ?}
?};
{? -($_p())='rejestry vat' &  (|($_p_spr()))<>''
|| RVAT.cntx_psh();
   RVAT.index('SYM'); RVAT.prefix(REF.FIRMA,_p_spr(),_p_spr());
   {? ~RVAT.first
   || FUN.info('Proszę wybrać wartość ze słownika'
          '\nlub pozostawić pole niewypełnione.'@); _ok:=0
   ?}; RVAT.cntx_pop()
|? -($_p())='klasy ewidencji' &  (|($_p_spr()))<>''
|| KVAT.cntx_psh();
   KVAT.index('SYM'); KVAT.prefix();
   {? ~KVAT.find_key(_p_spr())
   || FUN.info('Proszę wybrać wartość ze słownika'
          '\nlub pozostawić pole niewypełnione.'@); _ok:=0
   ?};  KVAT.cntx_pop
|? ((-($_p())='stawki vat') |  (-($_p())='grupy podatkowe') | (-($_p())='rodzaje ewidencji vat'))  &  (|($_p_spr()))<>''
|| SLU.index('NAZ');
   {? (-($_p())='rodzaje ewidencji vat')
   || SLU.prefix('~RODZAJE EWIDENCJI')
   |? (-($_p())='stawki vat')
   || SLU.prefix('~STAWKI VAT '+VAT7.KRAJ().KOD)
   || SLU.prefix('~'+~(-($_p())))
   ?};
   {? SLU.first
   || SLO.index('SL'); SLO.prefix(SLU.ref);
      {? ~SLO.find_key(_p_spr())
      || FUN.info('Proszę wybrać wartość ze słownika'
            '\nlub pozostawić pole niewypełnione.'@); _ok:=0
      ?}
   ?}
|? -($_p())='rodzaje podatku vat' &  (|($_p_spr()))<>''
|| {? ~(-_p_spr()='należny' | -_p_spr()='naliczony')
   || FUN.info('Proszę wybrać wartość ze słownika'
         '\nlub pozostawić pole niewypełnione.'@); _ok:=0
   ?}
?};
{? ~_ok || _p_spr():='' ?};
{? _ok & _b & (|($_p_spr()))<>'' & _p_do()<_p_od()
|| FUN.info('Proszę wpisać wartości graniczne'
      '\ndla poziomu w kolejności alfabetycznej.'@); _ok:=0
?};
_ok


\bv_wy2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed wyświetleniem VAT7.WY2
::----------------------------------------------------------------------------------------------------------------------
VAT7.efld_opt(VAT7.win_edit('?'),'enable='+$VAT7.WY2,VAT7,'DAT_ZAPL');
VAT7.efld_opt(VAT7.win_edit('?'),'enable='+$VAT7.WY2,VAT7,'TYLKOZAP');
''


\bv_wystrok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed wyświetleniem VAT7.WYSTROK
::----------------------------------------------------------------------------------------------------------------------
VAT7.efld_opt(VAT7.win_edit('?'),'enable='+($(VAT7.WYSTROK<>null)),VAT7,'WYSTOKRO');
''


\czy_sprznop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy sprzedaż nieopodatkowana
::  OLD: \czy_sprznop/vat.fml
::----------------------------------------------------------------------------------------------------------------------
PVAT.GRVAT().KOD<>'' &
(VAT7.PAR10=3 & 7+SLO.KOD='SprPDos' |
VAT7.PAR10=1 & SLO.KOD='SprPDost' |
VAT7.PAR10=2 & SLO.KOD='SprPDosu')


\szukkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Zwraca dane kontrahenta do wydruku
::  OLD: \szukkh/vat.fml
::----------------------------------------------------------------------------------------------------------------------
DVAT.KH+{? DVAT.MIASTO<>'' || ', '+DVAT.MIASTO || '' ?}+{? +DVAT.NIP || ', '+DVAT.NIP || '' ?}


\bv_wy1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed wyswietleniem VAT7.WY1
::----------------------------------------------------------------------------------------------------------------------
VAT7.efld_opt(VAT7.win_edit('?'),'enable='+$exec('be_wy1','!fks_vat_zrej'),VAT7,'WY1');
~~


\vatzbza1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Formula uzywana do wyznaczania kwot na wydruku rejestru VAT i rejestr zakupow VAT
::       oraz zakupy w podziale na grupy podatkowe. Zawartosc zostala przeniesiona z pliku vatzbza1.rpi.
::  OLD: \vatzbza1/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('typ_okr_vat','!fks_vat_zrej',2)
|| _grupa:=PVAT.GRVAT().KOD;
   {? (_grupa='ZInwePod')|(_grupa='ZInwPods')|(_grupa='ZakupPod')|
      (_grupa='ZakuPRop')|(_grupa='ZakuPods')
   || {? _grupa='ZInwePod'
      || wart7[1]+=PVAT.SUM2; wart8[1]+=PVAT.VAT; wart12[1]+=PVAT.VAT; in_w:=1;
         {? s2c
         || su[1][1]+=PVAT.SUM2;  su[6][1]+=PVAT.VAT;  su[2][1]+=PVAT.VAT;
            ssu[1]+=PVAT.SUM2; ssu[6]+=PVAT.VAT; ssu[2]+=PVAT.VAT
         || inwest1+=PVAT.SUM2;  inwest4+=PVAT.VAT;  inwestv1+=PVAT.VAT
         ?}
      |? _grupa='ZInwPods'
      || {? 6+PVAT.DVAT().RVAT().KVAT().SYM='_WWspN' & VAT7.TYP_VAT='Z'
         || _vat_odl:=PVAT.VAT;
            _net_odl:=PVAT.SUM2$2
         |? PVAT.VAT_ODL<>PVAT.VAT & UNPAR.P1
         || _vat_odl:=PVAT.VAT_ODL;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2
                      |? par84
                      || (PVAT.SUM2/2)$2
                      || PVAT.SUM2$2
                      ?}
         |? UNPAR.P1
         || _vat_odl:=(PVAT.VAT/2)$2;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*_vat_odl/PVAT.VAT)$2
                      |? par84
                      || (PVAT.SUM2/2)$2
                      || PVAT.SUM2$2
                      ?}
         || _vat_odl:=(PVAT.VAT/2)$2;
            _net_odl:={? PVAT.VAT || (PVAT.SUM2*_vat_odl/PVAT.VAT)$2 || PVAT.SUM2$2 ?}
         ?};
         wart7[1]+=_net_odl; wart8[1]+=_vat_odl; wart12[1]+=PVAT.VAT; in_w:=1;
         {? s2c
         || su[1][1]+=_net_odl; su[6][1]+=PVAT.VAT; su[2][1]+=_vat_odl;
            ssu[1]+=_net_odl; ssu[6]+=PVAT.VAT; ssu[2]+=_vat_odl
         || inwest1+=_net_odl; inwest4+=PVAT.VAT; inwestv1+=_vat_odl
         ?}
      |? _grupa='ZakupPod'
      || wart7[2]+=PVAT.SUM2; wart8[2]+=PVAT.VAT; wart12[2]+=PVAT.VAT; po_z:=1;
         {? s2c
         || su[1][3]+=PVAT.SUM2;  su[6][3]+=PVAT.VAT;  su[2][3]+=PVAT.VAT;
            ssu[1]+=PVAT.SUM2; ssu[6]+=PVAT.VAT; ssu[2]+=PVAT.VAT
         || pozost1+=PVAT.SUM2;  pozost4+=PVAT.VAT;  pozostv1+=PVAT.VAT
         ?}
      |? _grupa='ZakuPods'
      || {? 6+PVAT.DVAT().RVAT().KVAT().SYM='_WWspN' & VAT7.TYP_VAT='Z'
         || _vat_odl:=PVAT.VAT;
            _net_odl:=PVAT.SUM2$2
         |? ~UNPAR.P1
         || _vat_odl:=PVAT.VAT_ODL;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2
                      |? par84
                      || (PVAT.SUM2/2)$2
                      || PVAT.SUM2$2
                      ?}
         |? UNPAR.P1
         || _vat_odl:=(PVAT.VAT/2)$2;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*_vat_odl/PVAT.VAT)$2
                      |? par84
                      || (PVAT.SUM2/2)$2
                      || PVAT.SUM2$2
                      ?}
         ?};
         wart7[2]+=_net_odl; wart8[2]+=_vat_odl; wart12[2]+=PVAT.VAT; po_z:=1;
         {? s2c
         || su[1][3]+=_net_odl; su[6][3]+=PVAT.VAT; su[2][3]+=_vat_odl;
            ssu[1]+=_net_odl; ssu[6]+=PVAT.VAT; ssu[2]+=_vat_odl
         || pozost1+=_net_odl;  pozost4+=PVAT.VAT;   pozostv1+=_vat_odl
         ?}
      || wart7[3]+=PVAT.SUM2; wart8[3]+=PVAT.VAT; wart12[3]+=PVAT.VAT; ryc_z:=1;
         {? s2c
         || su[1][2]+=PVAT.SUM2;  su[6][2]+=PVAT.VAT;  su[2][2]+=PVAT.VAT;
            ssu[1]+=PVAT.SUM2; ssu[6]+=PVAT.VAT; ssu[2]+=PVAT.VAT
         || rycz1+=PVAT.SUM2;    rycz4+=PVAT.VAT;    ryczv1+=PVAT.VAT
         ?}
      ?}
   |? (_grupa='ZInwPodZ') | _grupa='ZInwPodS' | (_grupa='ZakuPodZ') |
      _grupa='ZakuPodS' | (_grupa='ZakPRopZ')
   || {? _grupa='ZInwPodZ'
      ||
         {? PVAT.VAT_ODL<>PVAT.VAT & UNPAR.P1
         || _vat_odl:=PVAT.VAT_ODL;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2
                      |? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         (PVAT.SUM2
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2$2
                      ?}
         |? UNPAR.P1
         || _vat_odl:=PVAT.VAT;
            _net_odl:={? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         (PVAT.SUM2
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2
                      ?}
         || _vat_odl:=PVAT.VAT;
            _net_odl:=PVAT.SUM2
         ?};
         wart9[1]+=_net_odl; wart10[1]+=_vat_odl; wart12[1]+=PVAT.VAT; in_w:=1;
         {? s2c
         || su[3][1]+=_net_odl; su[6][1]+=PVAT.VAT; su[4][1]+=_vat_odl;
            ssu[3]+=_net_odl; ssu[6]+=PVAT.VAT; ssu[4]+=_vat_odl
         || inwest2+=_net_odl; inwest4+=PVAT.VAT; inwestv2+=_vat_odl
         ?}
      |? _grupa='ZakPRopZ'
      || {? PVAT.VAT_ODL<>PVAT.VAT & UNPAR.P1
         || _vat_odl:=PVAT.VAT_ODL;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2
                      |? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         (PVAT.SUM2
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2$2
                      ?}
         |? UNPAR.P1
         || _vat_odl:=PVAT.VAT;
            _net_odl:={? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         (PVAT.SUM2
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2
                      ?}
         || _vat_odl:=PVAT.VAT;
            _net_odl:=PVAT.SUM2
         ?};
         wart9[3]+=_net_odl; wart10[3]+=_vat_odl; wart12[3]+=PVAT.VAT; ryc_z:=1;
         {? s2c
         || su[3][2]+=_net_odl; su[6][2]+=PVAT.VAT; su[4][2]+=_vat_odl;
            ssu[3]+=_net_odl; ssu[6]+=PVAT.VAT; ssu[4]+=_vat_odl
         || rycz2+=_net_odl; rycz4+=PVAT.VAT; ryczv2+=_vat_odl
         ?}
      |? _grupa='ZInwPodS'
      || {? 6+PVAT.DVAT().RVAT().KVAT().SYM='_WWspN' & VAT7.TYP_VAT='Z'
         || _vat_odl:=PVAT.VAT;
            _net_odl:=PVAT.SUM2$2
         |? PVAT.VAT_ODL<>PVAT.VAT & UNPAR.P1
         || _vat_odl:=PVAT.VAT_ODL;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2
                      |? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         _net_odl:=(PVAT.SUM2/2)$2;
                         (_net_odl
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2$2
                      ?}
         |? UNPAR.P1
         || _vat_odl:=(PVAT.VAT/2)$2;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*_vat_odl/PVAT.VAT)$2
                      |? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         _net_odl:=(PVAT.SUM2/2)$2;
                         (_net_odl
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                           ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2$2
                      ?}
         || _vat_odl:=(PVAT.VAT/2)$2;
            _net_odl:={? PVAT.VAT || (PVAT.SUM2*_vat_odl/PVAT.VAT)$2 || PVAT.SUM2$2 ?}
         ?};
         wart9[1]+=_net_odl; wart10[1]+=_vat_odl; wart12[1]+=PVAT.VAT; in_w:=1;
         {? s2c
         || su[3][1]+=_net_odl; su[6][1]+=PVAT.VAT; su[4][1]+=_vat_odl;
            ssu[3]+=_net_odl; ssu[6]+=PVAT.VAT; ssu[4]+=_vat_odl
         || inwest2+=_net_odl; inwest4+=PVAT.VAT; inwestv2+=_vat_odl
         ?}
      |? _grupa='ZakuPodZ'
      || {? PVAT.VAT_ODL<>PVAT.VAT & UNPAR.P1
         || _vat_odl:=PVAT.VAT_ODL;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2
                      |? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         (PVAT.SUM2
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2$2
                      ?}
         |? UNPAR.P1
         || _vat_odl:=PVAT.VAT;
            _net_odl:={? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         (PVAT.SUM2
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2
                      ?}
         || _vat_odl:=PVAT.VAT;
            _net_odl:=PVAT.SUM2
         ?};
         wart9[2]+=_net_odl; wart10[2]+=_vat_odl; wart12[2]+=PVAT.VAT; po_z:=1;
         {? s2c
         || su[3][3]+=_net_odl; su[6][3]+=PVAT.VAT; su[4][3]+=_vat_odl;
            ssu[3]+=_net_odl; ssu[6]+=PVAT.VAT; ssu[4]+=_vat_odl
         || pozost2+=_net_odl;  pozost4+=PVAT.VAT;   pozostv2+=_vat_odl
         ?}
      |? _grupa='ZakuPodS'
      || {? 6+PVAT.DVAT().RVAT().KVAT().SYM='_WWspN' & VAT7.TYP_VAT='Z'
         || _vat_odl:=PVAT.VAT;
            _net_odl:=PVAT.SUM2$2
         |? PVAT.VAT_ODL<>PVAT.VAT & UNPAR.P1
         || _vat_odl:=PVAT.VAT_ODL;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2
                      |? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         _net_odl:=(PVAT.SUM2/2)$2;
                         (_net_odl
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2$2
                      ?}
         |? UNPAR.P1
         || _vat_odl:=(PVAT.VAT/2)$2;
            _net_odl:={? PVAT.VAT
                      || (PVAT.SUM2*_vat_odl/PVAT.VAT)$2
                      |? par84
                      || _tab:={? PVAT.VPOZ_ROZ || PVAT.VPOZ_ROZ(); VPOZ_ROZ || PVAT.VPOZ_REF(); VPOZ ?};
                         _net_odl:=(PVAT.SUM2/2)$2;
                         (_net_odl
                          *({? par83
                            || {? _tab.C_PROC_S || procst/100 || 1 ?}
                            || procst/100
                            ?})
                          *({? par83 & _tab.C_PREWSK || prewsk/100 || 1 ?}))$2
                      || PVAT.SUM2$2
                      ?}
         || _vat_odl:=(PVAT.VAT/2)$2;
            _net_odl:={? PVAT.VAT || (PVAT.SUM2*_vat_odl/PVAT.VAT)$2 || PVAT.SUM2$2 ?}
         ?};
         wart9[2]+=_net_odl; wart10[2]+=_vat_odl; wart12[2]+=PVAT.VAT; po_z:=1;
         {? s2c
         || su[3][3]+=_net_odl; su[6][3]+=PVAT.VAT; su[4][3]+=_vat_odl;
            ssu[3]+=_net_odl; ssu[6]+=PVAT.VAT; ssu[4]+=_vat_odl
         || pozost2+=_net_odl;  pozost4+=PVAT.VAT;   pozostv2+=_vat_odl
         ?}
      || wart9[3]+=PVAT.SUM2; wart10[3]+=PVAT.VAT; wart12[3]+=PVAT.VAT; ryc_z:=1;
         {? s2c
         || su[3][2]+=PVAT.SUM2; su[6][2]+=PVAT.VAT; su[4][2]+=PVAT.VAT;
            ssu[3]+=PVAT.SUM2; ssu[6]+=PVAT.VAT; ssu[4]+=PVAT.VAT
         || rycz2+=PVAT.SUM2;    rycz4+=PVAT.VAT;     ryczv2+=PVAT.VAT
         ?}
      ?}
   |? (_grupa='ZInwPodN')|(_grupa='ZakPRopN')|
      (_grupa='ZakuPodN')
   || {? _grupa='ZInwPodN'
      || wart15[1]+=PVAT.SUM2; wart16[1]+=PVAT.VAT; wart12[1]+=PVAT.VAT; in_w:=1;
         {? s2c
         || su[7][1]+=PVAT.SUM2; su[8][1]+=PVAT.VAT; su[6][1]+=PVAT.VAT;
            ssu[7]+=PVAT.SUM2; ssu[8]+=PVAT.VAT; ssu[6]+=PVAT.VAT
         || inwest5+=PVAT.SUM2; inwestv5+=PVAT.VAT; inwest4+=PVAT.VAT
         ?}
      |? _grupa='ZakuPodN'
      || wart15[2]+=PVAT.SUM2; wart16[2]+=PVAT.VAT; wart12[2]+=PVAT.VAT; po_z:=1;
         {? s2c
         || su[7][3]+=PVAT.SUM2; su[8][3]+=PVAT.VAT; su[6][3]+=PVAT.VAT;
            ssu[7]+=PVAT.SUM2; ssu[8]+=PVAT.VAT; ssu[6]+=PVAT.VAT
         || pozost5+=PVAT.SUM2;   pozostv5+=PVAT.VAT;  pozost4+=PVAT.VAT
         ?}
      || wart15[3]+=PVAT.SUM2; wart16[3]+=PVAT.VAT; wart12[3]+=PVAT.VAT; ryc_z:=1;
         {? s2c
         || su[7][2]+=PVAT.SUM2; su[8][2]+=PVAT.VAT; su[6][2]+=PVAT.VAT;
            ssu[7]+=PVAT.SUM2; ssu[8]+=PVAT.VAT; ssu[6]+=PVAT.VAT
         || rycz5+=PVAT.SUM2;     ryczv5+=PVAT.VAT;    rycz4+=PVAT.VAT
         ?}
      ?}
   |? (5+_grupa='ZInwN')|(5+_grupa='ZPozN')|
      (_grupa='ZakupNpr')
   || {? 5+_grupa='ZInwN'
      || wart11[1]+=PVAT.SUM1; {? s2c || su[5][1]+=PVAT.SUM1; ssu[5]+=PVAT.SUM1 || inwest3+=PVAT.SUM1 ?}; in_w:=1
      |? 5+_grupa='ZPozN'
      || wart11[2]+=PVAT.SUM1; {? s2c || su[5][3]+=PVAT.SUM1; ssu[5]+=PVAT.SUM1 || pozost3+=PVAT.SUM1 ?}; po_z:=1
      || wart11[3]+=PVAT.SUM1; {? s2c || su[5][2]+=PVAT.SUM1; ssu[5]+=PVAT.SUM1 || rycz3+=PVAT.SUM1 ?};   ryc_z:=1
      ?}
   ?}
?}


\typ_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Zwraca typ podatku na podstawie wydruku rejestru VAT
::  OLD: \typ_vat/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'ACG'*RejWydr>0 || 'Z'
|? 'B'*RejWydr>0 || 'C'
|? 'DFHIJNOQT'*RejWydr>0 || VAT7.TYP_VAT
|? RejWydr='E' || {? VAT7.PAR4=1 || 'C' || 'Z' ?}
|| ''
?}


\typ_okr_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Sprawdza typ rozliczenia VAT
::   WE: _a - 1-dokument 2-pozycja
::  OLD: \typ_okr_vat/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_press('_b')>0 || _b || exec('typ_vat','!fks_vat_zrej') ?};
{? _typ='' || return(1) ?};
{? _a=1
|| _jest:=0;
   PVAT.cntx_psh();
   PVAT.index('DVAT_ST'); PVAT.prefix(DVAT.ref());
   {? PVAT.first()
   || {!
      |? _jest:=exec('typ_okr_vat','!fks_vat_zrej',2,_typ);
         _jest=0 & PVAT.next()
      !}
   ?};
   PVAT.cntx_pop();
   _jest
|| PVAT.TYP_VAT='' | _typ='' | PVAT.TYP_VAT=_typ
?}


\typ_vat_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Zwraca typ VAT-u prezentowany w parametrach wydruków VAT
::  OLD: \typ_vat_str/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:=exec('typ_vat','!fks_vat_zrej');
{? var_press('_a')<=0 | _a
|| '     '
|| ''
?}+'VAT: '+{? _typ='Z' || 'należny' |? _typ='C' || 'naliczony' || 'należny i naliczony' ?}


\vat7_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Ustawia wartośc początkową dla zmiennej VAT7
::  OLD: \qvat7_p/qceri_fk.fml ze specjalnej paczki od wdrożeń
::----------------------------------------------------------------------------------------------------------------------
VAT7.WY4:=VAT7.WY5:=VAT7.WY6:=0;
VAT7.JPK_TD:=VAT7.JPK_GTU:=VAT7.JPK_PROC:=''


\be_wy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Przed redakcją pola VAT7.WY4, VAT7.WY5, VAT7.WY6
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WYD_TECH=0 || 1 || 0 ?}


\ae_wy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
::   WE: _a - typ pola. GTU\PROC\TD
:: OPIS: Przed redakcją pola VAT7.WY4, VAT7.WY5, VAT7.WY6
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')>0
|| _typ:=_a;
   {? _typ='GTU' & VAT7.WY4=1 || _a:='GTU'
   |? _typ='PROC' & VAT7.WY5=1 || _a:='PROC'
   |? _typ='TD' & VAT7.WY6=1 || _a:='TD_S'; _b:='TD_Z'
   ?};
   {? _a<>''
   || _string:='';
      {? _typ='TD' & VAT7.WY6=1
      || _tab:=sql('select distinct KOD, OPIS, \' \' as W from JPK_SLO where TYP=\':_a\' or TYP=\':_b\'',_a,_b)
      || _tab:=sql('select distinct KOD, OPIS, \' \' as W from JPK_SLO where TYP=\':_a\'',_a)
      ?};
      {? _tab.first
      || _string+=_tab.KOD;
         {? _tab.next()
         || {!
            |? _string+=','+_tab.KOD;
               _tab.next()
            !}
         ?}
      ?};
      _string+=',BRAK';
      {? _a='GTU'
      || VAT7.JPK_GTU:=_string
      |? _a='PROC'
      || VAT7.JPK_PROC:=_string
      |? _a='TD_S'
      || VAT7.JPK_TD:=_string
      ?}
   ?};
   {? _typ='GTU' & VAT7.WY4=0 || VAT7.JPK_GTU:='' ?};
   {? _typ='PROC' & VAT7.WY5=0 || VAT7.JPK_PROC:='' ?};
   {? _typ='TD' & VAT7.WY6=0 || VAT7.JPK_TD:='' ?}
?};
1


\jpk_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
::   WE: _a - Tablica z rekordami tymczasowego słownika
::       _b - typ słownika ('GTU' / 'PROC' / 'TD')
:: OPIS: Wyświetlenie okienka wyboru pozycji dla filtrów JPK_GTU i JPK_PROC
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_typ:=_b;
_wyn:='';
{? _tab.first()
|| _wsel:=_tab.mk_sel('Słownik','N',,,,,,,'U');
   _tab.win_fld(_wsel,,'W',,,,,,'Wybrano',,'Wyb.',2,,"'*'","' '",,'mobile_visible=1');
   _tab.win_fld(_wsel,,'KOD',,,14,,,,,'Kod',,,,,,'mobile_header=1,mobile_visible=1');'max.dl 20';
   _tab.win_fld(_wsel,,'OPIS',,,70,,,,,'Opis',,,,,,'mobile_visible=1');'max.dl 100';
   _tab.win_act(_wsel,0,'Formuła','Wybierz',,,"{? cur_tab(1,1).W='*' || cur_tab(1,1).W:=' ' || cur_tab(1,1).W:='*' ?};cur_tab(1,1).put",,1,1);
   _tab.win_sopt(_wsel,'select_record_checkbox = 0');
   _tab.win_sel(_wsel);
   {? _tab.first()
   || {!
      |? {? VAT7.JPK_GTU<>'' & _typ='GTU'
         || _txt:=VAT7.JPK_GTU;
            _txt:=gsub(_txt,_tab.KOD,'');
            {? _txt<>VAT7.JPK_GTU || _tab.W:='*' || _tab.W:=' ' ?}
         |? VAT7.JPK_PROC<>'' & _typ='PROC'
         || _txt:=VAT7.JPK_PROC;
            _txt:=gsub(_txt,_tab.KOD,'');
            {? _txt<>VAT7.JPK_PROC || _tab.W:='*' || _tab.W:=' ' ?}
         |? VAT7.JPK_TD<>'' & _typ='TD'
         || _txt:=VAT7.JPK_TD;
            _txt:=gsub(_txt,_tab.KOD,'');
            {? _txt<>VAT7.JPK_TD || _tab.W:='*' || _tab.W:=' ' ?}
         || {? _tab.W='*' || _tab.W:=' ' || _tab.W:='*' ?}
         ?};
         _tab.put();
         _tab.next()
      !};
      _tab.first()
   ?};
   _tab.select(,1);
   _tab.first();
   {!|?
      {? _tab.W='*' || _wyn:=_wyn+{? +_wyn || ',' ||'' ?}+_tab.KOD ?};
      _tab.next()
   !}
?};
_wyn


\jpkbgtu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Przed redakcją pola VAT7.QJPK_GTU
::----------------------------------------------------------------------------------------------------------------------
VAT7.WY4


\jpk_gtu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Ustawienie wartości filtra JPK_GTU
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_a:='GTU';
_tab:=sql('select distinct KOD, OPIS, \' \' as W from JPK_SLO where TYP=\':_a\'',_a);
_tab.blank();
_tab.KOD:='BRAK';
_tab.OPIS:='Pozycje bez kodów GTU';
_tab.W:='*';
_tab.add();
{? _tab.first()
|| _wyn:=exec('jpk_f3','!fks_vat_zrej',_tab,'GTU')
?};
_wyn


\jpkbpro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Przed redakcją pola VAT7.QJPK_PRO
::----------------------------------------------------------------------------------------------------------------------
VAT7.WY5


\jpk_pro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Ustawienie wartości filtra JPK_PROC
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_a:='PROC';
_tab:=sql('select distinct KOD, OPIS, \' \' as W from JPK_SLO where TYP=\':_a\'',_a);
_tab.blank();
_tab.KOD:='BRAK';
_tab.OPIS:='Pozycje nieoznaczone';
_tab.W:='*';
_tab.add();
{? _tab.first()
|| _wyn:=exec('jpk_f3','!fks_vat_zrej',_tab,'PROC')
?};
_wyn


\jpkbtd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]]
:: OPIS: Przed redakcją pola VAT7.JPK_TD
::----------------------------------------------------------------------------------------------------------------------
VAT7.WY6


\jpk_td
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Ustawienie wartości filtra JPK_V_T
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_a:='TD_S';
_b:='TD_Z';
_tab:=sql('select distinct KOD, OPIS, \' \' as W from JPK_SLO where JPK_SLO.TYP=\':_a\' or JPK_SLO.TYP=\':_b\'',_a,_b);
_tab.blank();
_tab.KOD:='BRAK';
_tab.OPIS:='Nieoznaczony typ dokumentu';
_tab.W:='*';
_tab.add();
{? _tab.first()
|| _wyn:=exec('jpk_f3','!fks_vat_zrej',_tab,'TD')
?};
_wyn


\test_in
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Sprawdzenie czy istnieje taki kod w dokumencie który znajduje sie w filtrze
::   WE: _a - string VAT7.JPK_GTU
::       _b - string DVAT.DOK().JPK_GTU
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
_filtr:=_a;
_tekst:=_b;
STR.split(_filtr,',');
_tx:=STR.get_word();
_dalej:=1;
{? +_tx>0
|| {!|?
      {? _tekst*_tx || _wy:=1;_dalej:=0  || _tx:=STR.get_word() ?};
      _dalej & +_tx>0
   !}
?};
_wy


\be_vat7_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.14]
:: OPIS: Przed redagowaniem VAT7.WAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SLU.index('NAZ');
SLU.prefix();
SLU.find_key('WALUTY')


\ae_vat7_kraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.14]
:: OPIS: Formula po red. pola VAT7.KRAJ
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_press('__wgkraj')>0 & __wgkraj=1
|| 1
|| {? fld<>null
   || VAT7.WAL:=exec('ust_vwal','kraje',VAT7.KRAJ().KOD);1
   || 0
   ?}
?}



\ae_vat7_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.14]
:: OPIS: Formula po red. pola VAT7.KRAJ
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? fld<>null | (var_press('__wgkraj')>0 & __wgkraj=1)
|| 1
|| 0
?}


:Sign Version 2.0 jowisz:1045 2023/10/12 11:07:04 455ee9cbb52b112847ac1cc8c3bd13a85b6b53375c38434687c2a388134ec8952133de0f05267ae395c5465930f46f13f200d1e066ce224bdc792bc05c176b33b42a4361e107c337b0ca66ddace0c553afa3a322aea15dc10595032e06d465b207a051736ce1a9cbd98759e2efbfa6540a6d7d683d9e933a1338afcd4855c906

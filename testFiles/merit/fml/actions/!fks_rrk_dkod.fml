:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rrk_dkod.fml
:: Utworzony: 13.11.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RRK_DKOD - Tworzenie not odsetkowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tworzenie not odsetkowych - główna formuła czynności FKS_RRK_DKOD
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_ser_nags','rozrach_kor')
::# kind=WE, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WY, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na notę odsetkową, required=T, keyref=T
::# kind=WY, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=LOOP
:: WARUNKI BRAMY
::# condition=Akceptacja, act_uid=FKS_RRK_DKOE, auto=T, formula=_a.SER_NAG<>null & _a.SER_NAG<>~~
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='N';
params_set(params_get());
{? var_pres('UNIK_ID',_in)>0 & _in.UNIK_ID<>''
|| params_exec('par_out','rozrach_kor',_in.UNIK_ID);
   _mp.done()
|| idnagpar:=tm_stamp();
:: czynność startowa
   {? _mp.pathProc()
   || SER_KOR.ZAK_WAL:=SSTALE.WAL;
      ROZRACH.KONTR:=null;
      kh_all:=1;
      exec('bw','rozrach_kor','N','SER_NOTY','NOTA');
      menu_txt(,'Dołącz');
      _mp.trigRef('SER_NAG');
      params_exec('nota_new1','!fks_rrk_dkod');
      VAR_DEL.delete('kh_all')
   |? _akcja='Dołącz'
   || menu_txt(,'Dołącz');
      _mp.trigRef('SER_NAG');
      params_exec('nota_new1','!fks_rrk_dkod')
   ?};
   params_exec('par_out','rozrach_kor',idnagpar);
   VAR_DEL.delete('idnagpar');
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ rejestrację noty odsetkowej'@@;
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('SER_NAG',_wy)
|| _ref:=BB.sqlint($_wy.SER_NAG); _nam:=form(($_wy.SER_NAG)-8);
   {? _ref<>0 & _nam<>''
   || SER_NAG.cntx_psh(); SER_NAG.use(_nam); SER_NAG.prefix();
      KH.cntx_psh(); KH.prefix();
      {? SER_NAG.seek(_ref,_nam)
      || _desc:='Zakończ rejestrację noty odsetkowej dla kontrahenta: %1 z dnia: %2'@@[SER_NAG.KH().SKR,$SER_NAG.DATA]
      ?};
      SER_NAG.cntx_pop(); KH.cntx_pop()
   ?}
?};
_desc


\n_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Czynność FKS_RRK_DKOD - formuła główna w wersji nieprocesowej
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_in:=_args.in;
_akcja:=_args.akcja;
_autoakc:=exec('autoAkc','#b__box',,300580,'FKS_RRK_DKOE');
SER_KOR.TYP:='N';
params_set(params_get());
:: czynność startowa
{? _akcja='Dołącz'
|| menu_txt(,'Dołącz');
   {? _autoakc
   || exec('tab_autoakc','rozrach_kor');
      params_exec('nota_new1','!fks_rrk_dkod');
      exec('autoakc','rozrach_kor',2)
   || params_exec('nota_new1','!fks_rrk_dkod')
   ?}
?};
~~


\nota_new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie noty odsetkowej
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RRK_DKOD';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\nota_new1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie noty odsetkowej - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
exec('gen_kor1','rozrach_kor');
SER_KOR.NAL_ODSD:=0; SER_KOR.DATODODS:=date(0,0,0);
RKS.RWSK:='E';
WSK.cntx_psh(); WSK.index('RODZ'); WSK.prefix(RKS.RWSK,'0');
SER_KOR.L_WSTECZ:={? WSK.first() || WSK.ref() || null ?};
SER_KOR.ROK1:=SER_KOR.ROK2:=null; l_wstecz:=0; maska0:=maska1:=maska2:='';
{? exec('okno1','!fks_rrk_dkod')
|| exec('tab_unik','!fks_rrk_dkod');
   {? ROZNE.UT_DOK || exec('tmp_ods','rozrach_ods'); exec('initmpks','rozrach_kor') ?};
   {? -SER_KOR.KONTR='w'
   || _wybrani:={? SER_KOR.KONTR='W' & (ROZNE.PAR4=0 | ROZNE.PAR4=2)
                || {? ROZNE.PAR4=0
                   || exec('wybr_kh','rozrach_kor',ROZNE.PREFIX)
                   || exec('wybr_kh','rozrach_kor',ROZNE.MASKA)
                   ?}
                || 0
                ?};
      {? SER_KOR.WYB_WAL=1 || exec('wyb_wal','rozrach_kor') ?};
      {? l_wal>0
      || exec('nonotk','!fks_rrk_dkod',_wybrani)
      ?};
      VAR_DEL.delete('TMPKH','ind_kh');
      {? SER_KOR.WYB_WAL=1 || exec('del_wal','rozrach_kor') ?}
   |? -SER_KOR.KONTR='b'
   || exec('initmpkh','rozrach_kor');
      KH.select();
      {? ~TMPKH.first()
      || FUN.info('Nie wybrano żadnego kontrahenta.'@)
      || {? SER_KOR.WYB_WAL=1 || exec('wyb_wal','rozrach_kor') ?};
         {? l_wal>0
         || exec('nonotk','!fks_rrk_dkod',1)
         ?};
         {? SER_KOR.WYB_WAL=1 || exec('del_wal','rozrach_kor') ?}
      ?};
      exec('deltmpkh','rozrach_kor')
   ?};
   exec('del_unik','!fks_rrk_dkod');
   {? ROZNE.UT_DOK || exec('deltmpks','rozrach_kor') ?}
?};
VAR_DEL.delete('l_wstecz','maska0','maska1','maska2'); WSK.cntx_pop();
exec('gen_kor2','rozrach_kor')


\okno1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Okienko przy generowaniu not odsetkowych
::  OLD: \okno1/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.DATA:={? date()>=SSTALE.AO().POCZ & date()<=SSTALE.AO().KON || date() || SSTALE.AO().KON ?};
SER_KOR.KONTR:='W'; SER_KOR.TRYB_NOT:=0;
SER_KOR.SER_DEF:=0;  SER_KOR.SUMFAKT:=0; SER_KOR.SER_SCH:=SER_KOR.TAB_ODS:=null;
ROZNE.PAR4:=0; ROZNE.PREFIX:=ROZNE.MASKA:=''; ROZNE.KS_OD:=ROZNE.KS_DO:=null;
SER_DEF.cntx_psh();
SER_DEF.index('SER_DEFT'); SER_DEF.prefix('N');
{? SER_DEF.size()=1 || SER_DEF.first(); SER_KOR.SER_DEF:=SER_DEF.ref() ?};
SER_DEF.cntx_pop();
exec('odd_filtr','fst');
SER_KOR.win_edit({? kh_all || 'SER_RENK' || 'SER_REN1' ?});
_zwrot:=SER_KOR.edit("exec('spr_not','!fks_rrk_dkod')");
{? _zwrot & l_wstecz>0
|| _bil_rok:=BILANSP.AROK; ROZNE.PAR41:=ROZNE.PAR42:=0;  anal_rok:=null;
   ROZNE.PREFIX1:=ROZNE.PREFIX2:=ROZNE.MASKA1:=ROZNE.MASKA2:='';
   ROZNE.KS_OD1:=ROZNE.KS_DO1:=ROZNE.KS_OD2:=ROZNE.KS_DO2:=null;
   ROZNE.win_edit({? l_wstecz=1 || 'NOTYKON1' || 'NOTYKON2' ?});
   ROK_F.cntx_psh();
   ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA); SSTALE.AR();
   {?  l_wstecz=1
   || SER_KOR.ROK2:=SSTALE.AR; {? ROK_F.prev() || SER_KOR.ROK1:=ROK_F.ref() ?}
   |? l_wstecz=2
   || {? ROK_F.prev() || SER_KOR.ROK2:=ROK_F.ref(); {? ROK_F.prev() || SER_KOR.ROK1:=ROK_F.ref ?} ?}
   ?};
   _zwrot:=ROZNE.edit("exec('spr_not1','!fks_rrk_dkod')");
   ROK_F.cntx_pop();
   &anal_rok; BILANSP.AROK:=_bil_rok
?};
_zwrot


\spr_not
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.62]
:: OPIS: Sprawdzanie parametrów not odsetkowych
::  OLD: \spr_not/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_a:='';
{? ROZNE.PAR4=2
|| WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=ROZNE.MASKA;
   {? +WYDRUKI.MASKA < WYDRUKIN.DLKON
   || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
   ?};
   maska0:=WYDRUKI.MASKA
|? ROZNE.PAR4=4
|| KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR);
   {? ROZNE.KS_OD=null || KS.first(); ROZNE.KS_OD:=KS.ref() ?};
   {? ROZNE.KS_DO=null || KS.last(); ROZNE.KS_DO:=KS.ref() ?};
   KS.cntx_pop()
?};
{? ~exec('dat_not','dok_fks',SER_KOR.DATA)
|| FUN.info('Podana data musi być z bieżącego roku bilansowego.'@); _a:='DATA'
?};
{? _a='' & SER_KOR.SER_DEF=null
|| FUN.info('Nie wypełniony komentarz.'@); _a:='SER_DEF'
?};
{? _a='' & ROZNE.PAR4=4 & ROZNE.KS_OD().SYM>ROZNE.KS_DO().SYM
|| FUN.info('Błędny zakres kont.'@); _a:='KS_OD'
?};
{? _a='' & SER_KOR.WYB_WAL=0 & SER_KOR.WAL=null
|| FUN.info('Nie wybrano waluty.'@); _a:='WYB_WAL'
?};
{? _a='' & SER_KOR.TRYB_NOT=1 & SER_KOR.TAB_ODS=null
|| FUN.info('Nie wypełniony typ tabeli odsetek.'@); _a:='TAB_ODS'
?};
{? _a='' & SER_KOR.NAL_ODSD & SER_KOR.DATODODS=date(0,0,0)
|| FUN.info('Nie wprowadzono początkowej daty sprawdzania spłat rozrachunków.'@); _a:='DATODODS'
?};
{? _a='' & SER_KOR.TRYB_NOT=0
|| SLO.cntx_psh();
   _wal:={? SER_KOR.WYB_WAL=0
         || wal_kod:=SER_KOR.WAL().KOD; SER_KOR.WAL
         || null
         ?};
   SLO.cntx_pop;
   _a:=exec('szuksch1','rozrach_kor',0,0,1,_wal)
?};
{? _a='' & SER_KOR.TRYB_NOT=1 & SER_KOR.WYB_WAL=0
|| SLO.cntx_psh(); ODS.cntx_psh();
   ODS.index('DATA'); ODS.prefix(SER_KOR.TAB_ODS,SER_KOR.WAL);
   {? ~(ODS.first() & SER_KOR.DATA>=ODS.ZM)
   || _a:='TAB_ODS';
      FUN.info('Tabela odsetek na datę '+SER_KOR.DATA$3+
               '\nnie ma wartości dla waluty '+SER_KOR.WAL().KOD+'.')
   ?};
   SLO.cntx_pop(); ODS.cntx_pop()
?};
_a


\spr_not1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Sprawdzanie parametrow not odsetkowych - dla lat wstecz
::  OLD: \spr_not1/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_a:='';
{? ROZNE.PAR41=2
|| WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=ROZNE.MASKA1;
   {? +WYDRUKI.MASKA<WYDRUKIN.DLKON
   || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
   ?};
   maska1:=ROZNE.MASKA1:=WYDRUKI.MASKA
|? ROZNE.PAR41=4
|| KS.cntx_psh(); KS.index('SYM'); KS.prefix(SER_KOR.ROK1);
   {? ROZNE.KS_OD1=null || KS.first(); ROZNE.KS_OD1:=KS.ref() ?};
   {? ROZNE.KS_DO1=null || KS.last();  ROZNE.KS_DO1:=KS.ref() ?};
   KS.cntx_pop()
?};
{? l_wstecz=2
|| {? ROZNE.PAR42=2
   || WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=ROZNE.MASKA2;
      {? +WYDRUKI.MASKA<WYDRUKIN.DLKON
      || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
      ?};
      maska2:=ROZNE.MASKA2:=WYDRUKI.MASKA
   |? ROZNE.PAR42=4
   || KS.cntx_psh(); KS.index('SYM'); KS.prefix(SER_KOR.ROK2);
      {? ROZNE.KS_OD2=null || KS.first(); ROZNE.KS_OD2:=KS.ref() ?};
      {? ROZNE.KS_DO2=null || KS.last();  ROZNE.KS_DO2:=KS.ref() ?};
      KS.cntx_pop()
   ?}
?};
{? ROZNE.PAR41=4 & ROZNE.KS_OD1().SYM>ROZNE.KS_DO1().SYM
|| FUN.info('Błędny zakres kont w roku '+SER_KOR.ROK1().NAZ+'.'); _a:='KS_OD1'
?};
{? _a='' & l_wstecz=2 & ROZNE.KS_OD2().SYM>ROZNE.KS_DO2().SYM
|| FUN.info('Błędny zakres kont w roku '+SER_KOR.ROK2().NAZ+'.'); _a:='KS_OD2'
?};
_a


\tab_unik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Tworzy tabele tymczasowe przy generowaniu not odsetkowych -
::       do przechowywania mapowan kont i uwzglednionych pozycji
::  OLD: \tab_unik/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
UNIK_NOT:=tab_tmp(5,'WAL','INTEGER','Ref waluty',
                    'ODD','INTEGER','Ref jednostki księgowej',
                    'KONTO','STRING[35]','Konto (biezacy rok)',
                    'SYM','STRING[20]','Symbol rozrachunku',
                    'DO','DATE','Data otwarcia'
                 );
UNIK_NOT.prefix();
MAP_KONT:=tab_tmp(1,'KONTO0','STRING[35]','Konto (biezacy rok)',
                    'KONTO1','STRING[35]','Konto (1 rok wstecz)',
                    'KONTO2','STRING[35]','Konto (2 lata wstecz))'
                  );
indmapk2:=MAP_KONT.ndx_tmp('',1,'KONTO2',,0,'KONTO2',,0);
indmapk1:=MAP_KONT.ndx_tmp('',1,'KONTO1',,0,'KONTO1',,0,'KONTO2',,0);
indmapk0:=MAP_KONT.index('?')


\del_unik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Kasuje tabele tymczasowe i zmienne globalne uzywane
::       przy generowaniu not odsetkowych
::  OLD: \del_unik/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('UNIK_NOT','MAP_KONT','indmapk2','indmapk1','indmapk0')


\nonotk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Generowanie nowych pozycji korespondencji seryjnej typu N
::   WE: _a=0 - wszyscy, 1 - wybrani
::  OLD: \nonotk/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); ZAP_OP.cntx_psh(); D_RB.cntx_psh();
echo(); czy_mon:=1; add_nag:=0; ile:=0; analrok:=0; kont_poz:='';
ileprzed:=ilepo:=odsprter:=zwlprter:=0; dprzed:=dpo:=data_od:=date(0,0,0);
{? ~kh_all || _a:=0 ?};
{? kh_all || KH.index('KOD'); KH.prefix(2) ?};
{? ~kh_all | (kh_all & KH.first())
|| {! |?
      wal_snag:=null; wal_kod:=''; add_nag:=0;
      SLO.cntx_psh();
      {? l_wal=1 & SER_KOR.WYB_WAL=0
      || wal_snag:=SER_KOR.WAL; wal_kod:=SER_KOR.WAL().KOD
      |? l_wal=1 & SER_KOR.WYB_WAL=1
      || TMP_WAL.first(); exec('szu_wal','rozrach_kor')
      ?};
      SLO.cntx_pop();
      {? (~_a | (_a & (TMPKH.prefix(); TMPKH.find_key(KH.KOD,KH.KOD))))
      || {? ~exec('czy_kor','rozrach_kor',SER_KOR.TYP,SER_KOR.DATA,null,KH.KOD,null,wal_snag,SER_KOR.ODD().OD)
         || echo('Generowanie not odsetkowych dla kontrahenta: %1'@[KH.SKR]);
            KH.cntx_psh();  czy_mon:=1;
            {? SER_KOR.TRYB_NOT=0 || exec('szuksch','rozrach_kor',1,wal_snag) ?};
            {? czy_mon
            || {? SER_KOR.TRYB_NOT=0 || SER_KOR.ODSPTER:=SER_KOR.SER_SCH().ODSPNOTA ?};
               {? l_wal>1 || TMP_WAL.first() ?};
               {! |?
                   {? l_wal>1 || exec('szu_wal','rozrach_kor') ?};
                   {! analrok:=l_wstecz//-1..0
                   |! ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
                      KS.cntx_psh(); KS.prefix();
                      {? analrok=2
                      || SER_KOR.ROK2();
                         _prefix:=ROZNE.PREFIX2; WYDRUKI.MASKA:=maska2;
                         _ks_od:=ROZNE.KS_OD2().SYM; _ks_do:=ROZNE.KS_DO2().SYM;
                         _par4:=ROZNE.PAR42
                      |? analrok=1
                      || SER_KOR.ROK1();
                         _prefix:=ROZNE.PREFIX1; WYDRUKI.MASKA:=maska1;
                         _ks_od:=ROZNE.KS_OD1().SYM; _ks_do:=ROZNE.KS_DO1().SYM;
                         _par4:=ROZNE.PAR41
                      || SSTALE.AR();
                         _prefix:=ROZNE.PREFIX; WYDRUKI.MASKA:=maska0;
                         _ks_od:=ROZNE.KS_OD().SYM; _ks_do:=ROZNE.KS_DO().SYM;
                         _par4:=ROZNE.PAR4
                      ?};
                      _synt:=ROK_F.SYNT;
                      OP.use('operac' + ROK_F.KOD);
                      ZAP_OP.use('rozzap'+ ROK_F.KOD);
                      D_RB.use('dr__fk'+ROK_F.KOD);
                      KS.cntx_pop(); ROK_F.cntx_pop();
                      {? SER_KOR.ODD=null
                      || OP.index('KH2');
                         {? ~_par4
                         || OP.prefix(wal_snag,KH.ref(),'NAL',_prefix)
                         || OP.prefix(wal_snag,KH.ref(),'NAL')
                         ?}
                      || OP.index('KH3');
                         {? ~_par4
                         || OP.prefix(wal_snag,SER_KOR.ODD,KH.ref(),'NAL',_prefix)
                         || OP.prefix(wal_snag,SER_KOR.ODD,KH.ref(),'NAL')
                         ?}
                      ?};
                      ZAP_OP.cntx_psh();
                      ZAP_OP.index('OP');
                      {? OP.first()
                      || {! |?
                            ZAP_OP.prefix(OP.ref);
                            {? ZAP_OP.first()
                            || _tods:=SER_KOR.TAB_ODS; _suma:=_suma1:=ileprzed:=ilepo:=0; dprzed:=dpo:=date(0,0,0); _raty:=0; _dat_spl:=date(0,0,0);
                               {? OP.ZM$4<>0 || SER_KOR.TAB_ODS:=null ?};
                               {? SER_KOR.TRYB_NOT=0 || SER_KOR.RATY:=SER_KOR.SER_SCH().RATY_NOT ?};
                               OP.cntx_psh();
                               _tp:={? exec('get_par','#parametr',26)='T'
                                    || exec('dzienRob','kalendarz',OP.TZ,1,1)
                                    || OP.TZ
                                    ?};
                               {? _tp=date(0,0,0) || _tp:=OP.TZ ?};
                               {? -OP.CZY_ODS='t' &
                                  {? _par4=2 || exec('mask','konto',OP.AN)
                                  |? _par4=4 || (_synt+OP.AN)>=_ks_od & (_synt+OP.AN)<=_ks_do
                                  || 1
                                  ?} &
                                  {? SER_KOR.RATY
                                  || {? var_pres('DRB',@.CLASS)<0 || exec('obj_drbt','raty') ?};
                                     {? var_pres('DRB')>0 || obj_del(DRB) ?}; DRB:=obj_new(@.CLASS.DRB);
                                     _raty:=DRB.set_tab(OP.ref());
                                     {? _raty
                                     || _data_p:=DRB.TPF;
                                        SER_KOR.DATA>_data_p & _data_p<>date(0,0,0)
                                     || _dat_spl:=exec('dat_spl','rozrach');
                                        {? SER_KOR.ODSPTER
                                        || {? SER_KOR.TRYB_NOT=0
                                           || (SER_KOR.DATA>_tp & (_dat_spl-_tp)>=SER_KOR.SER_SCH().DNI_ODS)
                                           || SER_KOR.DATA>ZAP_OP.DATA+{? var_press('DNI_KH',SER_KOR)>0
                                                                       || {? SER_KOR.DNI_KH
                                                                          || exec('ile_dni','rozrach_ods',OP.KH().WIELKOSC)
                                                                          || SER_KOR.DNI_TP
                                                                          ?}
                                                                       || 30
                                                                       ?} | SER_KOR.DATA>_tp
                                           ?}
                                        || {? SER_KOR.TRYB_NOT=0
                                           || (SER_KOR.DATA>_tp & (_dat_spl-_tp)>=SER_KOR.SER_SCH().DNI_ODS)
                                           || SER_KOR.DATA>_tp
                                           ?}
                                        ?}
                                     ?}
                                  || _dat_spl:=exec('dat_spl','rozrach',SER_KOR.DATA);
                                     {? SER_KOR.ODSPTER
                                     || {? SER_KOR.TRYB_NOT=0
                                        || (SER_KOR.DATA>_tp & (_dat_spl-_tp)>=SER_KOR.SER_SCH().DNI_ODS)
                                        || SER_KOR.DATA>ZAP_OP.DATA+{? var_press('DNI_KH',SER_KOR)>0
                                                                    || {? SER_KOR.DNI_KH
                                                                       || exec('ile_dni','rozrach_ods',OP.KH().WIELKOSC)
                                                                       || SER_KOR.DNI_TP
                                                                       ?}
                                                                    || 30
                                                                    ?} | SER_KOR.DATA>_tp
                                        ?}
                                     || {? SER_KOR.TRYB_NOT=0
                                        || (SER_KOR.DATA>_tp & (_dat_spl-_tp)>=SER_KOR.SER_SCH().DNI_ODS)
                                        || SER_KOR.DATA>_tp
                                        ?}
                                     ?}
                                  ?} &
                                  {? ~(SER_KOR.RATY & _raty)
                                  || _dat_spl<>date(0,0,0) &
                                     {? SER_KOR.NAL_ODSD & SER_KOR.DATODODS<>date(0,0,0) || _dat_spl>=SER_KOR.DATODODS || 1 ?}
                                  || 1
                                  ?} &
                                  (kont_poz:=exec('map_kont','!fks_rrk_dkod',OP.AN);
                                   {? kont_poz='' || kont_poz:=35*'?' ?};
                                   exec('add_unik','!fks_rrk_dkod',OP.WAL,OP.ODD,kont_poz,OP.SYM,OP.DO)) &
                                   {? exec('get_par','#parametr',46)='T'
                                   || {? exec('spr_rwal','rozrach_kor',OP.SYM_ROK,OP.KH)
                                      || ~exec('spr_rkor','rozrach_kor',OP.SYM_ROK,OP.KH,SER_KOR.TYP)
                                      || 1
                                      ?}
                                   || 1
                                   ?}
                               || _nasze:=_wasze:=0;
                                  {? SER_KOR.RATY=0 | (SER_KOR.RATY=1 & ~_raty)
                                  || _nasze:=F.ROw(OP.ref,SER_KOR.DATA); _wasze:=F.ROm(OP.ref,SER_KOR.DATA)
                                  ?};
                                  odsprter:=zwlprter:=0;
                                  {? SER_KOR.RATY & _raty
                                  || SER_KOR.SUMFAKT:=0;
                                     {? SER_KOR.TRYB_NOT=0
                                     || SER_WART.index('SERWART2'); SER_WART.prefix(SER_KOR.SER_SCH,wal_kod,0);
                                        SER_KOR.SUMFAKT:={? SER_WART.first() || SER_WART.KW_GR || 0 ?}
                                     ?};
                                     nal_ods:=0;
                                     exec('nota1','!fks_rrk_dkod',SER_KOR.DATA,wal_snag);
                                     {? nal_ods$2<SER_KOR.SUMFAKT$2 & add_nag
                                     || SER_POZ.index('SER_POZW'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),OP.WAL().KOD);
                                        do();
                                        {! |? {? SER_POZ.SYM_ROK=OP.SYM_ROK || SER_POZ.del() || SER_POZ.next() ?} !};
                                        SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
                                        {? ~SER_POZ.first || exec('serndel','rozrach_kor'); SER_NAG.del(); add_nag:=0; ile-=1 ?};
                                        end()
                                     ?};
                                     &nal_ods
                                  |? _wasze$2>=_nasze$2 & _wasze$2<>0
                                  || {? ~exec('jest_not','!fks_rrk_dkod',OP.AN,OP.SYM,wal_snag,0,OP.ODD,OP.SYM_ROK)
                                     || SER_KOR.SUMFAKT1:=0;
                                        {? SER_KOR.TRYB_NOT=0
                                        || SER_KOR.SUMFAKT:=0;
                                           SER_WART.index('SERWART2'); SER_WART.prefix(SER_KOR.SER_SCH,wal_kod,0);
                                           SER_KOR.SUMFAKT:={? SER_WART.first() || SER_WART.KW_GR || 0 ?};
                                           {? SER_KOR.ODSPTER & FINFO.TODS_UST<>null
                                           || SER_WART.prefix(SER_KOR.SER_SCH,wal_kod,1);
                                              SER_KOR.SUMFAKT1:={? SER_WART.first() || SER_WART.KW_GR || 0 ?};
                                              _suma1:=exec('odsetki1','rozrach_ods',OP.ref())
                                           ?}
                                        |? SER_KOR.ODSPTER & FINFO.TODS_UST<>null
                                        || _suma1:=exec('odsetki1','rozrach_ods',OP.ref())
                                        ?};
                                        _suma:=exec('odsetki','rozrach_ods',OP.ref(),SER_KOR.DATA,1);
                                        _generuj:=(_suma$2>SER_KOR.SUMFAKT$2);
                                        {? ~_generuj || _generuj:=(_suma1$2>SER_KOR.SUMFAKT1$2) ?};
                                        odsprter:=_suma1;
                                        {? _generuj || exec('nota','!fks_rrk_dkod',SER_KOR.DATA,wal_snag) ?}
                                     ?}
                                  ?}
                               ?};
                               SER_KOR.TAB_ODS:=_tods;
                               OP.cntx_pop()
                            ?};
                            OP.next()
                         !}
                      ?};
                      ZAP_OP.cntx_pop()
                   !};
                   {? l_wal>1 || TMP_WAL.next() || 0 ?}
               !}
            ?}; KH.cntx_pop()
         || {? ROZNE.UT_DOK
            || SER_DEF.get();
               exec('addtmpks','rozrach_kor',1,'Kontr.: '+KH.SKR+' generowano korespondencję'+
                   {? SER_KOR.ODD<>null || ' w jedn. ks.: '+SER_KOR.ODD().OD || '' ?});
               {? wal_snag=FINFO.NAROD
               || exec('addtmpks','rozrach_kor',1,'w walucie narodowej ('+FINFO.NAROD().KOD+') na dzień: '+$SER_KOR.DATA,1)
               || exec('addtmpks','rozrach_kor',1,'w walucie obcej na dzień: '+$SER_KOR.DATA,1)
               ?}
            ?}
         ?}
      ?};
      {? add_nag || exec('ser_poz_edit','rozrach_kor') ?};
      {? add_nag & SER_NAG.WAL=null || exec('spr_wal','rozrach_kor') ?};
      kh_all & KH.next()
   !};
   echo();
   {? ROZNE.UT_DOK
   || TMPKS.prefix(1);
      {? TMPKS.first() || TMPKS.select() ?}
   ?}
?};
exec('ile_dok','rozrach_kor');
VAR_DEL.delete('odsprter','zwlprter','ileprzed','ilepo','dprzed','dpo','data_od','ile','czy_mon','add_nag','analrok');
VAR_DEL.delete('kont_poz','DRB');
OP.cntx_pop(); ZAP_OP.cntx_pop(); D_RB.cntx_pop();
0


\map_kont
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Mapuje konta przy generowaniu not odsetkowych
::   WE: _a - symbol konta
::  OLD: \map_kont/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
DOK.cntx_psh(); POZ.cntx_psh();
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.index('ROK');
{? analrok=0
|| _zwrot:=_a
|? analrok=1
|| MAP_KONT.index(indmapk1); MAP_KONT.prefix(_a,_a);
   {? MAP_KONT.first()
   || _zwrot:=MAP_KONT.KONTO0
   || SSTALE.AR(); OKRO_F.prefix(ROK_F.ref());
      {? OKRO_F.first()
      || DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
         POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2));
         POZ.index('PKON'); POZ.prefix(_a);
         {? POZ.first() & POZ.PKON=_a
         || _zwrot:=POZ.KON;
            MAP_KONT.KONTO1:=POZ.PKON; MAP_KONT.KONTO0:=POZ.KON; MAP_KONT.KONTO2:=''; MAP_KONT.add()
         ?}
      ?}
   ?};
   {? _zwrot='' || _zwrot:=exec('sprstkon','!fks_rrk_dkod',_a,SER_KOR.ROK1) ?}
|| MAP_KONT.index(indmapk2); MAP_KONT.prefix(_a,_a);
   {? MAP_KONT.first()
   || _zwrot:=MAP_KONT.KONTO0
   || SER_KOR.ROK1(); OKRO_F.prefix(ROK_F.ref());
      {? OKRO_F.first()
      || DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
         POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2));
         POZ.index('PKON'); POZ.prefix(_a);
         {? POZ.first() & POZ.PKON=_a
         || _konto2:=POZ.PKON; _konto1:=POZ.KON;
            SSTALE.AR(); OKRO_F.prefix(ROK_F.ref());
            {? OKRO_F.first()
            || DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
               POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2));
               POZ.index('PKON'); POZ.prefix(_konto1);
               {? POZ.first() & POZ.PKON=_konto1
               || _zwrot:=POZ.KON;
                  MAP_KONT.KONTO1:=_konto1; MAP_KONT.KONTO0:=POZ.KON; MAP_KONT.KONTO2:=_konto2; MAP_KONT.add()
               ?}
            ?}
         ?}
      ?}
   ?};
   {? _zwrot='' || _zwrot:=exec('sprstkon','!fks_rrk_dkod',_a,SER_KOR.ROK2) ?}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
DOK.cntx_pop(); POZ.cntx_pop();
_zwrot


\sprstkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Sprawdza czy struktura konta (dlugosc syntetyki, budowa)
::       jest identyczna jak w biezacym roku. W przypadku identycznej dlugosci
::       syntetyki a roznych separatorow w latach podmienia go w koncie.
::   WE: _a - symbol konta, _b - ref roku
::   WY: - symbol konta lub pusty string jesli jest inna struktura konta
::  OLD: \sprstkon/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
KS.cntx_psh(); KS.index('SYM'); KS.prefix();
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
BUD.cntx_psh(); BUD.index('KS');
SLU.cntx_psh(); SLU.prefix();
SLUAPPL.cntx_psh(); SLUAPPL.prefix();
SSTALE.AR();
_akt_sep:=ROK_F.SEP; _akt_syn:=ROK_F.SYNT; _akt_rok:=SSTALE.AR;
{? ROK_F.seek(_b)
|| _pop_rok:=ROK_F.ref(); _rok_sep:=ROK_F.SEP; _rok_syn:=ROK_F.SYNT;
   _sym_kon:=_akt_syn+_a;
   {? _akt_syn=_rok_syn &
      (KS.prefix(_pop_rok,_sym_kon); _zwrot1:=(KS.first() & KS.SYM=_sym_kon); BUD.prefix(KS.ref()); _size1:=BUD.size(); _pop_kon:=KS.ref(); _zwrot1) &
      (KS.prefix(_akt_rok,_sym_kon); _zwrot2:=(KS.first() & KS.SYM=_sym_kon); BUD.prefix(KS.ref()); _size2:=BUD.size(); _akt_kon:=KS.ref(); _zwrot2) &
      _size1=_size2
   || _akt_bud:=obj_new(10); _pop_bud:=obj_new(10); _dl_bud:=obj_new(10);
      {! _i:=1..10 |! _akt_bud[_i]:=_pop_bud[_i]:=null; _dl_bud[_i]:=0 !};
      {? _rok_sep<>_akt_sep & _size1>0
      || {? _rok_sep=','
         || BUD.prefix(_pop_kon);
            {? BUD.first() || {! _i:=1.._size1 |! _dl_bud[_i]:=BUD.SLU().SLU().DL; BUD.next() !} ?};
            _konto:=_sym_kon; _a:=_rok_syn-_a;
            {! _i:=1.._size1
            |! _konto+=(_akt_sep+(_dl_bud[_i]+_a));
               _a:=_dl_bud[_i]-_a
            !};
            _a:=_konto
         || {? _akt_sep=',' || _akt_sep:='' ?};
            _a:=STR.gsub(_a,_rok_sep,_akt_sep)
         ?}
      ?};
      BUD.prefix(_pop_kon);
      {? BUD.first() || {! _i:=1.._size1 |! _pop_bud[_i]:=BUD.SLU; BUD.next() !} ?};
      BUD.prefix(_akt_kon);
      {? BUD.first() || {! _i:=1.._size1 |! _akt_bud[_i]:=BUD.SLU; BUD.next() !} ?};
      {? _akt_bud[1]=_pop_bud[1] & _akt_bud[2]=_pop_bud[2] & _akt_bud[3]=_pop_bud[3] &
         _akt_bud[4]=_pop_bud[4] & _akt_bud[5]=_pop_bud[5] & _akt_bud[6]=_pop_bud[6] &
         _akt_bud[7]=_pop_bud[7] & _akt_bud[8]=_pop_bud[8] & _akt_bud[9]=_pop_bud[9] &
         _akt_bud[10]=_pop_bud[10]
      || _zwrot:=_a
      ?}
   ?}
?};
SLU.cntx_pop(); SLUAPPL.cntx_pop(); BUD.cntx_pop(); KS.cntx_pop(); ROK_F.cntx_pop();
_zwrot


\nota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: Generowanie danych do noty odsetkowej
::   WE: _a - data generowania noty odsetkowej
::       _b - waluta
::  OLD: \nota/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_licznik:=0; _kwota:=0; _op_do:=OP.DO;
ZAP_OP.cntx_psh();
ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref);
{? ZAP_OP.first()
|| _ma:=F.ROm(OP.ref,{? SER_KOR.ODSPTER & ileprzed>0
                     || ZAP_OP.DATA+{? var_press('DNI_KH',SER_KOR)>0
                                    || {? SER_KOR.DNI_KH
                                       || exec('ile_dni','rozrach_ods',OP.KH().WIELKOSC)
                                       || SER_KOR.DNI_TP
                                       ?}
                                    || 30
                                    ?}
                     || dpo
                     ?})
?};
ZAP_OP.cntx_pop();
_wn:=F.ROw(OP.ref,_a);
_ilespl:=0; _ilebylo:=F.ROw(OP.ref,dpo);
_w1:=OP.SYM; _symz:=OP.SYM_ZEW; _w3:=OP.TZ; _kwota:=F.SWn(_wn,_ma);
_w2:=_kwota;
{? odsprter>0 &  ileprzed=0 & dprzed<>date(0,0,0)
|| _w4:=date(0,0,0); F.RObr(OP.ref(),dprzed); _w5:=F.SWn(F.rwn,F.rma);
   _kon_d:={? SER_KOR.DATA<OP.TZ || SER_KOR.DATA || OP.TZ ?};
   _w7:=(exec('ods','rozrach_ods',dprzed+1,_kon_d,_w5,0,_b))$2; _w6:=zwlprter;
   {? add_nag=0
   || _wal_n:={? l_wal=1 || _b || null ?};
      _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
      {? exec('dodaj','rozrach_kor',SER_KOR.TYP,SER_DEF.ref,_a,SER_KOR.TAB_ODS,null,_wal_n,null,_obca)
      || add_nag:=1; exec('dodajn','!fks_rrk_dkod',_op_do,_w1,_w2,_w3,_w4,0,_w6,_w7,_b,1,_symz)
      ?}
   || exec('dodajn','!fks_rrk_dkod',_op_do,_w1,_w2,_w3,_w4,0,_w6,_w7,_b,1,_symz)
   ?}
?};
{? _kwota | SER_KOR.ODSPTER
|| ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref);
   {? ZAP_OP.first
   || _op_tz:={? exec('get_par','#parametr',26)='T'
                   || exec('dzienRob','kalendarz',OP.TZ,1,1)
                   || OP.TZ
                   ?};
      {! |?
         {? odsprter>0 & ileprzed>0 & (ZAP_OP.WN<ZAP_OP.MA) & (ZAP_OP.DATA<=_op_tz) & (ZAP_OP.DATA>dprzed)
         || _w4:=ZAP_OP.DATA; _w5:=F.SMa(ZAP_OP.WN,ZAP_OP.MA); _w6:=ZAP_OP.DATA-dprzed;
            _w7:=(exec('ods','rozrach_ods',dprzed+1,ZAP_OP.DATA,_w5,0,_b))$2;
            {? add_nag=0
            || _wal_n:={? l_wal=1 || _b || null ?};
               _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
               {? exec('dodaj','rozrach_kor',SER_KOR.TYP,SER_DEF.ref,_a,SER_KOR.TAB_ODS,null,_wal_n,null,_obca)
               || add_nag:=1; exec('dodajn','!fks_rrk_dkod',_op_do,_w1,_w2,_w3,_w4,_w5,_w6,_w7,_b,1,_symz)
               ?}
            || exec('dodajn','!fks_rrk_dkod',_op_do,_w1,_w2,_w3,_w4,_w5,_w6,_w7,_b,1,_symz)
            ?}
         ?};
         {? (ZAP_OP.WN<ZAP_OP.MA) & (ZAP_OP.DATA<=_a) & (ZAP_OP.DATA>dpo)
         || _w4:=ZAP_OP.DATA; _w5:=F.SMa(ZAP_OP.WN,ZAP_OP.MA);
            _ilespl+=_w5; _w55:=_w5; {? _ilespl>_ilebylo || _w5-=(_ilespl-_ilebylo) ?};
            {? ZAP_OP.DATA-dpo<=0 || _w6:=0 || _w6:=ZAP_OP.DATA-dpo ?};
            _wyn:={? OP.ZM$4<>0
                  || ((_w5*_w6*OP.ZM)/100)$2
                  || exec('ods','rozrach_ods',dpo+1,ZAP_OP.DATA,_w5,1,_b)
                  ?}; _w7:=_wyn$2; _w5:=_w55;
            {? add_nag=0
            || _wal_n:={? l_wal=1 || _b || null ?};
               _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
               {? exec('dodaj','rozrach_kor',SER_KOR.TYP,SER_DEF.ref,_a,SER_KOR.TAB_ODS,null,_wal_n,null,_obca)
               || add_nag:=1; exec('dodajn','!fks_rrk_dkod',_op_do,_w1,_w2,_w3,_w4,_w5,_w6,_w7,_b,0,_symz)
               ?}
            || exec('dodajn','!fks_rrk_dkod',_op_do,_w1,_w2,_w3,_w4,_w5,_w6,_w7,_b,0,_symz)
            ?}
         ?};
         _ilebylo>_ilespl & ZAP_OP.next
      !}
   ?}
?}


\dodajn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: dodanie pozycji noty odsetkowej
::   WE: _a - data otwarcia rozrachunku
::       _b - symbol rozrachunku
::       _c - kwota faktury
::       _d - termin platnosci
::       _e - data wpłaty
::       _f - kwota wpłaty
::       _g - ilosc dni zwloki
::       _h - kwota odsetek
::       _i - ref waluty
::       _j - (0/1) po/przed ter. pl.
::       _k - symbol zewnetrzny
::  OLD: \dodajn/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_POZ.use('ser_pxx')
|| SER_POZ.clear;
   SER_POZ.blank();
   {? SER_NAG.get()
   || SER_POZ.REF:=SER_NAG.ref;
      SER_POZ.T:=SER_KOR.TYP;
      SER_POZ.DATA:=_a;
      SER_POZ.DOK:=_b;
      SER_POZ.KW:=_c;
      SER_POZ.TERM:=_d;
      SER_POZ.DATW:=_e;
      SER_POZ.KWP:=_f;
      SER_POZ.ZWL:=_g;
      SER_POZ.ODS:=_h;
      SER_POZ.WAL:=_i;
      SER_POZ.KON:=kont_poz;
      SER_POZ.ODD:=OP.ODD;
      SER_POZ.ZWLPRTER:=zwlprter;
      SER_POZ.PRZEDPO:=_j;
      SER_POZ.SYM_ROK:=exec('op_unik_sym','rozrach',SER_POZ.DOK,SER_POZ.DATA);
      SER_POZ.SYM_ZEW:=_k;
      SER_POZ.add();
      exec('mod_nag','rozrach_kor')
   ?}
?}


\jest_not
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: sprawdza, czy dla danego rozrachunku byla juz generowana nota
::   WE: _a - symbol konta
::       _b - symbol rozrachunku
::       _c - waluta
::       _d - 1 - szukac SER_NAGA z najwyzsza data, 0 - nie szukac
::       _e - jednostka ksiegowa
::       _f - unikalny symbol rozrachunku
::  OLD: \jest_not/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
SER_POZ.cntx_psh();
SER_POZ.index('SER_POZK'); SER_POZ.prefix(REF.FIRMA,SER_KOR.TYP,_a,_b,_b);
{? SER_POZ.first()
|| SER_NAG.cntx_psh();
   {! |?
      {? exec('get_par','#parametr',26)='T'
      || {? SER_POZ.ODD=_e & SER_POZ.SYM_ROK=_f || _jest:=1 ?}
      || {? SER_POZ.WAL=_c & SER_POZ.ODD=_e & SER_POZ.SYM_ROK=_f || _jest:=1 ?}
      ?};
      _data:=SER_POZ.REF().DATA;
      {? _data>data_od || data_od:=_data ?};
      {? _d || 1 || ~_jest ?} & SER_POZ.next()
   !};
   SER_NAG.cntx_pop()
?};
SER_POZ.cntx_pop();
_jest


\nota1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: Generowanie danych do noty odsetkowej (raty)
::   WE: _a - data generowania noty odsetkowej
::       _b - waluta
::  OLD: \nota1/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_ilespl:=0;  _ilebylo:=F.ROw(OP.ref,DRB.TPL); _op_do:=OP.DO; _w33:=OP.TZ;
last_rat:=obj_new(2); data_od:=date(0,0,0);
: sprawdzenie ostatnio placonej raty i kwoty
: last_rat[1] - ostatnio placona rata, [2] - kwota, jesli zaplacono calkowicie np. pierwsza rate
: bedzie [1] - 2, [2] - 0; niecalkowicie zapl. 1 rata [1] - 1, [2] - kwota dotychczasowej zaplaty
ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref);
last_rat[1]:=1; last_rat[2]:=0;
{? exec('jest_not','!fks_rrk_dkod',OP.AN,OP.SYM,wal_snag,1,OP.ODD,OP.SYM_ROK)
|| {? ZAP_OP.first()
   || {!|?
        {? (ZAP_OP.WN<ZAP_OP.MA) & (ZAP_OP.DATA<=data_od)
        || _splata:=F.SMa(ZAP_OP.WN,ZAP_OP.MA);
           ZAP_OP.cntx_psh(); ZAP_OP.prefix(OP.ref,ZAP_OP.DATA);
           _ref:=ZAP_OP.ref();
           {? ZAP_OP.size()>1
           || ZAP_OP.next();
              {! |?
                 {? ZAP_OP.WN<ZAP_OP.MA || _splata+=F.SMa(ZAP_OP.WN,ZAP_OP.MA) ?};
                 ZAP_OP.next()
              !};
              _ref:=ZAP_OP.ref()
           ?};
           ZAP_OP.cntx_pop(); ZAP_OP.seek(_ref);
           {! _i:=last_rat[1]..DRB.LRAT
           |? _splata>0
           |! _iledosp:=t_drb[_i][1]-last_rat[2];
              _w5:={? _iledosp>_splata || _splata || _iledosp ?};
              _splata-=_w5;
              {? _iledosp=_w5
              || last_rat[1]+=1; last_rat[2]:=0
              || last_rat[2]+=_w5
              ?}
           !}
        ?};
        ZAP_OP.DATA<=data_od & ZAP_OP.next()
      !}
   ?}
?};
_splaci:=0;
{? ZAP_OP.first()
|| {!|?
     {? (ZAP_OP.WN<ZAP_OP.MA) & (ZAP_OP.DATA<=_a) & ZAP_OP.DATA>data_od || _splaci+=F.SMa(ZAP_OP.WN,ZAP_OP.MA) ?};
     ZAP_OP.next()
   !}
?};
_w1:=OP.SYM; _symz:=OP.SYM_ZEW;
{? ZAP_OP.first() & _splaci
|| {!|?
     {? (ZAP_OP.WN<ZAP_OP.MA) & (ZAP_OP.DATA<=_a) & ZAP_OP.DATA>data_od
     || _splata:=F.SMa(ZAP_OP.WN,ZAP_OP.MA);
        ZAP_OP.cntx_psh(); ZAP_OP.prefix(OP.ref,ZAP_OP.DATA);
        _ref:=ZAP_OP.ref();
        {? ZAP_OP.size()>1
        || ZAP_OP.next();
           {! |?
              {? ZAP_OP.WN<ZAP_OP.MA || _splata+=F.SMa(ZAP_OP.WN,ZAP_OP.MA) ?};
              ZAP_OP.next()
           !};
           _ref:=ZAP_OP.ref()
        ?};
        ZAP_OP.cntx_pop(); ZAP_OP.seek(_ref);
        _ilespl+=_splata; _gen:=1;
        {! _i:=last_rat[1]..DRB.LRAT
        |? _splata>0 & _gen
        |! {? {? SER_KOR.NAL_ODSD & SER_KOR.DATODODS<>date(0,0,0) || t_drb[_i][4]>=SER_KOR.DATODODS || 1 ?}
           || _iledosp:=t_drb[_i][1]-last_rat[2];
              _w5:={? _iledosp>_splata || _splata || _iledosp ?};
              _splata-=_w5;
              {? _iledosp=_w5
              || last_rat[1]+=1; last_rat[2]:=0
              |? _splaci>=_iledosp
              || last_rat[2]+=_w5
              || _gen:=0
              ?};
              _splaci-=_w5;
              _w3:={? exec('get_par','#parametr',26)='T'
                   || exec('dzienRob','kalendarz',t_drb[_i][3],1,1)
                   || t_drb[_i][3]
                   ?}; _w33:=t_drb[_i][3];
              {? _w3=date(0,0,0) || _w3:=t_drb[_i][3] ?};
              {? _gen & _w3<>date(0,0,0) &
                 {? SER_KOR.TRYB_NOT=0
                 || t_drb[_i][4]<>date(0,0,0) &  SER_KOR.DATA>_w3 &
                    (t_drb[_i][4]-_w3)>=SER_KOR.SER_SCH().DNI_ODS
                 || SER_KOR.DATA>_w3
                 ?}
              || _w2:=t_drb[_i][1]; _w4:=ZAP_OP.DATA; _w6:=_w4-_w3;
                 {? _w6>0
                 || _w7:={? OP.ZM$4<>0
                         || ((_w5*_w6*OP.ZM)/100)$2
                         || exec('ods','rozrach_ods',_w3+1,ZAP_OP.DATA,_w5,1,_b)
                         ?};
                    nal_ods+=_w7;
                    {? _w7>0
                    || {? add_nag=0
                       || _wal_n:={? l_wal=1 || _b || null ?};
                          _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
                          {? exec('dodaj','rozrach_kor',SER_KOR.TYP,SER_DEF.ref,_a,SER_KOR.TAB_ODS,null,_wal_n,null,_obca)
                          || add_nag:=1; exec('dodajn','!fks_rrk_dkod',_op_do,_w1,_w2,_w33,_w4,_w5,_w6,_w7,_b,0,_symz)
                          ?}
                       || exec('dodajn','!fks_rrk_dkod',_op_do,_w1,_w2,_w33,_w4,_w5,_w6,_w7,_b,0,_symz)
                       ?}
                    ?}
                 ?}
              ?}
           ?}
        !}
     ?};
     _ilebylo>_ilespl & ZAP_OP.next()
  !}
?};
VAR_DEL.delete('last_rat')


\add_unik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Dodaje rekordy do tabeli tymczasowej - generowanie not (uwzglednione pozycje)
::   WE: _a - ref waluty, _b - ref jednostki ksiegowej, _c - konto,
::       _d - symbol rozrachunku, _e - data otwarcia rozrachunku
::   WY: 1/0 - udalo/nie udalo sie dodac rekordu do tabeli tymczasowej
::  OLD: \add_unik/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=#_a; _b:=#_b;
{? ~UNIK_NOT.find_key(_a,_b,_c,_d,_e)
|| UNIK_NOT.WAL:=_a; UNIK_NOT.ODD:=_b; UNIK_NOT.KONTO:=_c; UNIK_NOT.SYM:=_d;
   UNIK_NOT.DO:=_e; _zwrot:=UNIK_NOT.add()
|| _zwrot:=0
?};
_zwrot


\umorznot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Umorzenie pozycji noty
::  OLD: \umorznot/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_size:=SER_POZ.sel_size(); _dalej:=1; _umorz:=0;
SER_POZ.cntx_psh();
{? ~SER_POZ.UMORZNOT & ~_size
|| undefine(); _umorz:=1;
   define('OPIS_UM','','','Uwagi',100,100);
   _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
   _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
   def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
   def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   _dalej:=def_edit(,'Uwagi'@)
?};
{? _size & zm_stnot=1 || _umorz:=1 ?};
{? _dalej
|| SER_POZ.index('SER_OP');
   SER_POZ.prefix(REF.FIRMA,SER_POZ.WAL,SER_POZ.ODD,SER_POZ.KON,SER_POZ.DOK,SER_POZ.DOK,SER_POZ.SYM_ROK,SER_POZ.REF);
   {? SER_POZ.first() & {? _size || SER_POZ.UMORZNOT<>zm_stnot || 1 ?}
   || {? _size || ile_zmie+=1 ?};
      {! |?
         SER_POZ.UMORZNOT:={? ~_size || ~SER_POZ.UMORZNOT || zm_stnot ?};
         SER_POZ.OPISNOTU:={? _umorz || DEFINE.OPIS_UM || '' ?};
         SER_POZ.USER_UM:={? _umorz
                          || {? username()*'~' || (username()*'~'-1)+username() || 10+username() ?}
                          || ''
                          ?};
         SER_POZ.put(); SER_POZ.next()
      !};
      exec('ser_poz_edit','rozrach_kor')
   ?}
?};
SER_POZ.cntx_pop();
{? ~_size
|| {? _dalej || exec('sum_wezw','rozrach_kor') ?};
   undefine()
?}


\beumorzn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed akcja grupowa umorzenia pozycji noty
::  OLD: \beumorzn/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=FUN.choice('Zaznaczone pozycje.'@,,'Umorzyć'@,'Przywrócić'@);
{? _a
|| {? _a=1
   || undefine();
      define('OPIS_UM','','','Uwagi',100,100);
      _ok:=def_btn('text=%1'['Zapisz'@],'key:F2');
      _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
      def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
      def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'))
   ?};
   {? _a=2 | def_edit(,'Uwagi'@)
   || sel_nchk();
      ile_zmie:=0; zm_stnot:={? _a=1 || 1 || 0 ?}; 1
   || undefine(); 0
   ?}
|| 0
?}


\aeumorzn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po akcji grupowej umorzenia pozycji noty
::  OLD: \aeumorzn/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? zm_stnot
|| FUN.info('Dokumentów umorzonych: %1'@[$ile_zmie])
|| FUN.info('Dokumentów przywróconych: %1'@[$ile_zmie])
?};
exec('sum_wezw','rozrach_kor'); undefine();
&ile_zmie; &zm_stnot


\not_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: Usuwanie pozycji not odsetkowych
::  OLD: \not_usu/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
SER_NAG.cntx_psh(); SER_NAG.prefix(REF.FIRMA);
ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
SER_NAGS.SER_NAG();
{? FUN.ask('Usunąć pozycję noty odsetkowej?'@)
|| _odd:=SER_POZ.ODD().OD; _odd_ref:=ODD.ref();
   do();
   SER_POZ.del();
   SER_POZ.cntx_psh(); SER_POZ.index('SER_POZO'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
   _size:=SER_POZ.size(); SER_POZ.cntx_pop(); _ref_old:=SER_NAGS.ref();
   SER_NAGS.cntx_psh();
   _ref_new:={? SER_NAGS.next() | SER_NAGS.prev() || SER_NAGS.ref() || null ?};
   SER_NAGS.cntx_pop();
   {? _size
   || SER_POZ.cntx_psh();
      SER_POZ.index('SER_POZO'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),_odd);
      {? ~(SER_POZ.first() & SER_POZ.ODD=_odd_ref)
      || SER_NAGS.cntx_psh();
         SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref(),_odd_ref);
         {? SER_NAGS.first() || SER_NAGS.del() ?};
         SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref());
         {? SER_NAGS.size()=2
         || SER_NAG.prefix(REF.FIRMA); SER_NAGS.SER_NAG();
            SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
            {? SER_POZ.first() || SER_NAG.ODD:=SER_POZ.ODD; SER_NAG.put() ?}
         ?};
         SER_NAGS.cntx_pop()
      ?};
      SER_POZ.cntx_pop();
      exec('sum_wezw','rozrach_kor')
   || sel_exit;
      SER_NAGS.cntx_psh();
      SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref());
      {? SER_NAGS.first() || {! |? SER_NAGS.del() !} ?};
      SER_NAGS.cntx_pop();
      SER_NAG.del()
   ?};
   end();
   {? ~SER_NAGS.seek(_ref_old)
   || {? _ref_new<>null || SER_NAGS.seek(_ref_new) ?}
   ?}
?};
SER_NAG.cntx_pop(); ODD.cntx_pop()









:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 37fde3a2abc47341329ac283252dfa633d3cce63ccd422416010c354433caa5b784797f9692049d2925e0f206743aead53dec4eb22301a8d5e6423e055170d46dc8c493450f7e387d04fc5266841d6755cb108d83bd2625e9d0c3912646fd7ea5d051ec1fd7337997a1e54a1ce974be770b2754f10dfa447471e6bab6eca5f18

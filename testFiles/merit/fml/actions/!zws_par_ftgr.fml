:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ftgr.fml [17.00]
:: Utworzony: 2015/02/24
:: Autor: PJ
::======================================================================================================================
:: Zawartosc: Formuły czynności definiowania grup środków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Klasyfikacja środków trwałych (grupy środków)
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('krst','!zws_par_ftgr');
~~


\krst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Lista wariantów klasyfikacji środków trwałych
::  OLD: \konw_kst/inne
::----------------------------------------------------------------------------------------------------------------------

::debug();

:: Ustawienie ikon dla benefitów
:: params_set('bnftt_ikona',exec('bnftt_ikona_cfg','portal_benefity'));


TAB_KRST.cntx_psh();
TAB_KRST.index('TAB_KRST');
TAB_KRST.clear();
TAB_KRST.win_sel('WER');
TAB_KRST.select();
TAB_KRST.cntx_pop();
''


\defgrup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAKO [2010]
:: OPIS: Grupy rodzajowe srodkow trwalych
::   WE: _a = wskazania na wariant klasyfikacji środków trwałych
::  OLD: \defgrup/estra
::----------------------------------------------------------------------------------------------------------------------
{? TAM.lock(1,1,1)
|| STER.KST:=TAB_KRST.ref();
   TAM.index('KST');
   TAM.prefix(_a);
   TAM.win_sel('TABELSTR');
   TAM.actions('TABELSTR','C');
   TAM.select();
   TAM.clear();
   TAM.unlock()
|| {? FUN.ask('Tabela klasyfikacji zablokowana przez innego użytkownika.\n'
              'Wyświetlić w trybie tylko do odczytu?'@)
   || STER.KST:=TAB_KRST.ref();
      TAM.index('KST');
      TAM.prefix(_a);
      TAM.win_sel('TABELSTR');
      TAM.actions('TABELSTR','dpU:d');
      TAM.btn_sopt('TABELSTR','btn_tam_add','state=grayed');
      TAM.btn_sopt('TABELSTR','btn_tam_edi','state=grayed');
      TAM.btn_sopt('TABELSTR','btn_tam_del','state=grayed');
      TAM.select();
      TAM.clear();
      TAM.actions('TABELSTR','dpU:d');
      TAM.btn_sopt('TABELSTR','btn_tam_add','state=normal');
      TAM.btn_sopt('TABELSTR','btn_tam_edi','state=normal');
      TAM.btn_sopt('TABELSTR','btn_tam_del','state=normal')
   ?}
?}


\aw_grupa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: -
:: OPIS: rekpo: tabela amortyzacji
::  OLD: \aw_grupa/srodki
::----------------------------------------------------------------------------------------------------------------------
{? TAM.GR<>'' || TAM.GR1:=1+TAM.GR ?};
__CHK.record(TAM,,'GR','NGS')


\ster_kst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS:
::  OLD: \ster_kst/war_tech
::----------------------------------------------------------------------------------------------------------------------
STER.KST


\kst_be_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formułą weryfikuje istnienie tabeli konwersji grup między klasyfikacjami środków trwałych
::       w przypadku istnienia zapisów w tabekli konwersji redakcja roku klasyfikacji jest blokowana
::  OLD: \kst_be_r/inne
::----------------------------------------------------------------------------------------------------------------------
_be_ar:=1;
{? fld()
|| {? fld()<>KONST.RS
   || TAB_KONW.cntx_psh();
      TAB_KONW.index('TAB_KONW');
      TAB_KONW.prefix(fld());
      _be_ar:=~TAB_KONW.first();
      TAB_KONW.cntx_pop()
   ?}
?};
_be_ar


\tam_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie rekordu z tabeli TAM (grupy środków)
::----------------------------------------------------------------------------------------------------------------------
{? TAM.count()>0 | SRSR.GR=TAM.ref()
|| FUN.emsg('Wskazana grupa środków jest wykorzystywana w systemie i nie można jej usunąć.'@)
|| TAM.del()
?}


\tab_krst_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed usunięciem rekordu z tabeli TAB_KRST (klasyfikacje grup środków)
::----------------------------------------------------------------------------------------------------------------------
{? TAB_KRST.count()>0
|| FUN.emsg('Klasyfikacja jest wykorzystywana w systemie i nie można jej usunąć.'@);
   0
|| 1
?}


\tam_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Eksport tabeli klasyfikacji środków trwałych
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask('Eksportować klasyfikację środków do pliku na serwerze?'@) || return(0) ?};
_sep:='@';
_wy:=1;
TAM.cntx_psh();
TAM.index('KST');
TAM.prefix(TAB_KRST.ref());
{? TAM.first()
|| _fname:='tam_'+$TAB_KRST.AR+'.dfg';
   _f:=fopen(_fname,'Uw',1);
   {? _f
   || {! |?
         _line:=TAM.BIL().POZ+_sep;
         _line+=TAM.GR+_sep;
         _line+=TAM.GR1+_sep;
         _line+=TAM.NGS+_sep;
         _line+=$TAM.S+_sep;
         _line+=$TAM.WD;
         fwrite(_f,_line);
         TAM.next()
      !};
      fclose(_f);
      FUN.info('Zakończono eksport klasyfikacji środków trwałych do pliku (%1).'@[_fname])
   || FUN.emsg('Nie udało się wyeksportować klasyfikacji środków trwałych.'@);
      _wy:=0
   ?}
|| FUN.emsg('Brak danych do eksportu.'@);
   _wy:=0
?};
TAM.cntx_pop();
_wy


\tam_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Import klasyfikacji środków trwałych
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask('Zaimportować klasyfikację środków z pliku na serwerze?'@) || return(0) ?};
_sep:='@';
_wy:=0;
_files:=files('tam_*.dfg');
{? _files.first()
|| _win:=_files.mk_sel('Wybór pliku'@,'P',1,,,,,,'U',,,,,'normal');
   _files.win_act(_win,,'Formuła'@,'Wybierz'@,,,,"sel_exit()",1,,,,'W');
   _files.win_sel(_win);
   {? _files.select()
   || _f:=fopen(_files.FILENAME,'ur',1);
      {? _f
      || TAB.cntx_psh();
         TAB.index('POZ');
         TAB.prefix();
         {! |? (_line:=fread(_f))<>'\n'
         |! {? form(_line)<>''
            || _tmp:=spli_str(_line,_sep);
               TAM.blank(1);
               TAM.KST:=TAB_KRST.ref();
               {? form(_tmp[1])<>'' & TAB.find_key(form(_tmp[1])) || TAM.BIL:=TAB.ref() ?};
               TAM.GR:=form(_tmp[2]);
               TAM.GR1:=form(_tmp[3]);
               TAM.NGS:=form(_tmp[4]);
               TAM.S:=#form(_tmp[5]);
               TAM.WD:=#form(_tmp[6]);
               TAM.add(1);
               {? var_pres('_tmp')>0 || obj_del(_tmp) ?}
            ?}
         !};
         fclose(_f);
         TAB.cntx_pop();
         TAM.first();
         FUN.info('Zakończono import klasyfikacji środków trwałych.'@);
         _wy:=1
      || FUN.emsg('Nie udało się odczytać wybranego pliku (%1).'@[_files.FILENAME])
      ?}
   ?}
|| FUN.info('Brak dostępnych plików z klasyfikacją środków trwałych.'@)
?}


\bf_addtam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [20.42]
:: OPIS: przed dołączeniem grupy środków
::----------------------------------------------------------------------------------------------------------------------
::debug();
EDIT_ES.BNFTT:=null();
1


\af_addtam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [20.42]
:: OPIS: po dołączeniu grupy środków
::----------------------------------------------------------------------------------------------------------------------
::debug();
exec('bnftt_akt','!zws_par_ftgr',TAM.ref(),EDIT_ES.BNFTT);
1


\bnftt_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: aktualizacja tabeli TAM_DOD o pole BNFTT
::   WE: _a - TAM.ref()
::       _b - BNFTT.ref()
::----------------------------------------------------------------------------------------------------------------------
_firma:=exec('firma','ustawienia');
TAM_DOD.cntx_psh();
TAM_DOD.index('TAM_DOD');
TAM_DOD.prefix(_firma,_a);
{? TAM_DOD.first()
|| TAM_DOD.BNFTT:=_b;
   TAM_DOD.put(1)
|? _b<>null()
|| TAM_DOD.blank();
   TAM_DOD.TAM:=_a;
   TAM_DOD.FIRMA:=_firma;
   TAM_DOD.BNFTT:=_b;
   TAM_DOD.add(1)
?};
TAM_DOD.cntx_pop();
~~


\bf_edttam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [20.42]
:: OPIS: przed redakcją grupy środków
::----------------------------------------------------------------------------------------------------------------------
::debug();
EDIT_ES.BNFTT:=exec('FindInSet','#table','TAM_DOD','TAM_DOD',TAM.ref(),exec('firma','ustawienia'),"TAM_DOD.BNFTT",,,null());
1


\af_edttam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [20.42]
:: OPIS: po redakcji grupy środków
::----------------------------------------------------------------------------------------------------------------------
::debug();
exec('bnftt_akt','!zws_par_ftgr',TAM.ref(),EDIT_ES.BNFTT);
1


\bf_rec_set_tam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [20.42]
:: OPIS: ustawienie zmiennych dla wyświetlenia
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.BNFTT:=exec('FindInSet','#table','TAM_DOD','TAM_DOD',TAM.ref(),exec('firma','ustawienia'),"TAM_DOD.BNFTT",,,null());
~~


:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:23 a43aa1530a041f5e409c6222f3f0bf372dd272111b89bbd31889baaf7db0baca0786d4876ef717f598b95357ee98bcd65f573b16077ba8ef9ed9d8dbfe0a26960fd65c3cd9705ce597b2887669fe0327c70163d78758ecb70698bb5d17215ffd79be99bb3533c54f15849563d1b87216da7bcd43be29ef0c6a793decc14f01c5

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_oba_pbud.fml
:: Utworzony: 16.06.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_OBA_PBUD - Aktualizacja budżetu z wniosku i dokumentu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Aktualizacja budżetu z wniosku i dokumentu - główna formuła czynności CTR_OBA_PBUD
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_edokum','obiegi')

:: PARAMETRY WE:
::# kind=WE, symbol=DOK, type=_DOK, name=Wskazanie na dokument księgowy, required=N, keyref=T
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N, keyref=T
::# kind=WE, symbol=TYP, type=STRING, name=Rezerwować/Usunąć rezerwację, required=N, fml_val="exec('typ','!ctr_oba_pbud',_a)"
::# kind=WE, symbol=DEKR, type=STRING, name=Dekretacja, required=N

:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na dokument księgowy, required=N
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_edok:=null;
_add:=var_press('TYP',_we)<=0 | _we.TYP='Rezerwacja';
_dekr:='T';
{? var_press('EDOKUM',_we)>0
|| EDOKUM.cntx_psh(); EPODZ.cntx_psh(); EDOKUMPR.cntx_psh();
   _name:=ref_name(_we.EDOKUM);
   EPODZ.use('skid_j'+(_name+2));
   EDOKUMPR.use('skid_i'+(_name+2));
   EDOKUM.use(_name);
   EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM)
   || _edok:=EDOKUM.ref();
      _dekr:=EDOKUM.TYP().DEKR;
      _typ:={? EDOKUM.TYP().ZAPOT || 'Z' || 'F' ?};
      {? _add
      || exec('rez_budzet','!ctr_oba_pbud',_typ)
      || exec('usun_rez_budz','!ctr_oba_pbud',_typ)
      ?}
   ?};
   EDOKUM.cntx_pop(); EPODZ.cntx_pop(); EDOKUMPR.cntx_pop()
|? var_press('DOK',_we)>0
|| DOK.cntx_psh();
   _name:=ref_name(_we.DOK);
   SKIDXD.cntx_psh();
   SKIDXD.use('skxd'+(_name+4));
   DOK.use(_name);
   DOK.prefix();
   {? DOK.seek(_we.DOK)
   || {? _add
      || _rez:=exec('rez_dok','obe_budz');
         {? _rez=0
         || FUN.info('Rezerwacja budżetu na podziałach controllingowych nie powiodła się.'@);
            _akcept:=0; DOK.A:='N'; DOK.ZAA:=''; DOK.DAKC:=date(0,0,0)
         |? _rez=2
         || DOK.WSK_XD:='T'
         ?};
         DOK.prefix(); DOK.put()
      || exec('usun_rez_dok','obe_budz')
      ?}
   ?};
   SKIDXD.cntx_pop();
   DOK.cntx_pop()
?};
_wy.EDOKUM:=_edok;
_wy.DEKR:=_dekr;
_mp.save(,_wy);
_mp.done();
~~


\rez_budzet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Rezerwacja budzetu dla zapotrzebowan\dokumentow w obiegu
::   WE: _a - typ
::  OLD: \rez_budzet/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
{? EPODZ.first()
|| K_WERSJE.index('CZY_SYS'); K_WERSJE.prefix();
   _ref_wer:=null;
   _wer_typ:='';
   {? _a='F' & K_WERSJE.find_key('Z')
   || _ref_wer:=K_WERSJE.ref();
      _wer_typ:='Z'
   ?};
   {? K_WERSJE.find_key(_a)
   || _rok:=null; _okrof:=null;
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.S_FIRMA);
      {? ROK_F.first()
      || {!
         |? {? EDOKUM.DOP>=ROK_F.POCZ_ROK & EDOKUM.DOP<=ROK_F.KON_ROK
            || _rok:=ROK_F.ref();
               _okrof:=null;
               OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
               {? OKRO_F.first()
               || {!
                  |? {? EDOKUM.DOP>=OKRO_F.POCZ & EDOKUM.DOP<=OKRO_F.KON
                     || _okrof:=OKRO_F.ref()
                     ?};
                     _okrof=null & OKRO_F.next()
                  !}
               ?}
            ?};
            _rok=null & ROK_F.next()
         !}
      ?};
      _ok:=0;
      {? _rok & _okrof
      || {!
         |? {? EPODZ.SKID_MB().KONTROLA='T'
            || _ok:=0;
               {? ~EPODZ.EDOKRZAP
               || {? exec('get_knag','obe_budz',SKID_MBN.ref(),K_WERSJE.ref(),_rok,_a)
                  || exec('add_poz','obe_budz',_okrof,1,EPODZ);
                     _ok:=1
                  ?}
               |? EPODZ.EDOKRZAP & ref_name(EPODZ.EDOKRZAP().EZAPOT)=ref_name(EPODZ.EDOKRZAP().EDOKUM)
               || {? exec('get_knag','obe_budz',SKID_MBN.ref(),K_WERSJE.ref(),_rok,_a)
                  || exec('add_poz','obe_budz',_okrof,1,EPODZ);
                     _ok:=1
                  ?};
                  {? _ok & exec('get_knag','obe_budz',SKID_MBN.ref(),_ref_wer,_rok,_wer_typ)
                  || exec('add_poz','obe_budz',_okrof,-1,EPODZ);
                     _ok:=1
                  ?}
               |? EPODZ.EDOKRZAP
               || EDOKUM.cntx_psh();
                  EDOKUM.use(ref_name(EPODZ.EDOKRZAP().EZAPOT));
                  _dop_z:=EPODZ.EDOKRZAP().EZAPOT().DOP;
                  EDOKUM.cntx_pop();
                  _rok_z:=null;
                  ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.S_FIRMA);
                  {? ROK_F.first()
                  || {!
                     |? {? _dop_z>=ROK_F.POCZ_ROK & _dop_z<=ROK_F.KON_ROK
                        || _rok_z:=ROK_F.ref();
                           _okrof_z:=null;
                           OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
                           {? OKRO_F.first()
                           || {!
                              |? {? _dop_z>=OKRO_F.POCZ & _dop_z<=OKRO_F.KON
                                 || _okrof_z:=OKRO_F.ref()
                                 ?};
                                 _okrof_z=null & OKRO_F.next()
                              !}
                           ?}
                        ?};
                        _rok_z=null & ROK_F.next()
                     !}
                  ?};
                  {? _rok_z & _okrof_z
                  || {? exec('get_knag','obe_budz',SKID_MBN.ref(),K_WERSJE.ref(),_rok_z,_a)
                     || exec('add_poz','obe_budz',_okrof_z,1,EPODZ);
                        _ok:=1
                     ?};
                     {? _ok & exec('get_knag','obe_budz',SKID_MBN.ref(),_ref_wer,_rok_z,_wer_typ)
                     || exec('add_poz','obe_budz',_okrof_z,-1,EPODZ);
                        _ok:=1
                     ?}
                  ?}
               || _ok:=1
               ?}
            || _ok:=1
            ?};
            _ok & EPODZ.next()
         !};
         _ok
      ?}
   ?};
   EDOKUM.cntx_psh(); EDOKUM.prefix(); EDOKUM.BUDZ_AKT:='T'; EDOKUM.put(); EDOKUM.cntx_pop()
|| 1
?}


\usun_rez_budz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Zdjecie rezerwacji budzetu dla zapotrzebowan
::   WE: _a - typ usuwanej rezerwacji
::  OLD: \usun_rez_budz/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
{? EPODZ.first()
|| K_WERSJE.index('CZY_SYS'); K_WERSJE.prefix();
   _ref_wer:=null();
   {? _a='F' & K_WERSJE.find_key('Z')
   || _ref_wer:=K_WERSJE.ref()
   ?};
   {? K_WERSJE.find_key(_a)
   || _rok:=null; _okrof:=null;
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.S_FIRMA);
      {? ROK_F.first()
      || {!
         |? {? EDOKUM.DOP>=ROK_F.POCZ_ROK & EDOKUM.DOP<=ROK_F.KON_ROK
            || _rok:=ROK_F.ref()
            ?};
            _rok=null & ROK_F.next()
         !}
      ?};
      {? _rok
      || EDOKRZAP.cntx_psh();
         EDOKRZAP.index('EDOKUM'); EDOKRZAP.prefix(EDOKUM.ref());
         {? EDOKRZAP.first()
         || {? ref_name(EDOKRZAP.EZAPOT)=ref_name(EDOKRZAP.EDOKUM)
            || _ok:=_ok & exec('usun_rez_budz1','obe_budz',K_WERSJE.ref(),_rok,EPODZ);
               {? _ref_wer
               || _ok:=_ok & exec('usun_rez_budz1','obe_budz',_ref_wer,_rok,EPODZ)
               ?}
            || EDOKUM.cntx_psh();
               EDOKUM.use(ref_name(EDOKRZAP.EZAPOT));
               _dop_z:=EDOKRZAP.EZAPOT().DOP;
               EDOKUM.cntx_pop();
               _rok_z:=null;
               ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.S_FIRMA);
               {? ROK_F.first()
               || {!
                  |? {? _dop_z>=ROK_F.POCZ_ROK & _dop_z<=ROK_F.KON_ROK
                     || _rok_z:=ROK_F.ref()
                     ?};
                     _rok_z=null & ROK_F.next()
                  !}
               ?};
               {? _rok_z
               || _ok:=_ok & exec('usun_rez_budz1','obe_budz',K_WERSJE.ref(),_rok_z,EPODZ);
                  {? _ref_wer
                  || _ok:=_ok & exec('usun_rez_budz1','obe_budz',_ref_wer,_rok_z,EPODZ)
                  ?}
               ?}
            ?}
         || _ok:=_ok & exec('usun_rez_budz1','obe_budz',K_WERSJE.ref(),_rok,EPODZ)
         ?};
         EDOKRZAP.cntx_pop()
      ?}
   ?};
   EDOKUM.cntx_psh(); EDOKUM.prefix(); EDOKUM.BUDZ_AKT:='N'; EDOKUM.put(); EDOKUM.cntx_pop()
?};
_ok


\typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia parametr wejsciowy TYP czynności
::   WE: _a - wartość bieżaca pola
::----------------------------------------------------------------------------------------------------------------------
_nr:={? _a='Usunięcie rezerwacji' || 2 || 1 ?};
_pyt:=FUN.choice('Typ operacji',_nr,'Rezerwacja','Usunięcie rezerwacji');
{? _pyt=1
|| 'Rezerwacja'
|? _pyt=2
|| 'Usunięcie rezerwacji'
|| ~~
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_press('TYP',_we)>0
|| _desc:='Aktualizacja %1 budżetu wniosku'@@[_we.TYP]
|| _desc:='Aktualizacja budżetu wniosku'@@
?};
{? var_pres('EDOKUM',_we)>0 & _we.EDOKUM
|| EDOKUM.cntx_psh(); EDOKUM.use(ref_name(_we.EDOKUM)); EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM,)
   || {? var_press('TYP',_we)>0
      || _desc:='Aktualizacja %1 budżetu wniosku: %2, data operacji %3'@@[_we.TYP,EDOKUM.ID,$EDOKUM.DOP]
      || _desc:='Aktualizacja budżetu wniosku: %1, data operacji %2'@@[EDOKUM.ID,$EDOKUM.DOP]
      ?}
   ?};
   EDOKUM.cntx_pop()
?};
_desc

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:11 95fea6361880e26287cb6b1d7ddc58c60b217dc4cfb3d2a252ce3d9c306f212de1935e445666cbd3892ea70f44e9dc1499a631cc8b730ab77ff82b78ff12ece66f187a6d6b273ea2a6fb03f4c7b58fc0ef58a35f40083011719905872a696d8eb439ba08e242b6fe28a954f567869bfd55f97c44db3209ef4a7c455fd3c4ea12

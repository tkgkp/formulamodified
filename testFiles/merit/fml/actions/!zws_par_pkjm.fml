:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pkjm.fml
:: Utworzony: 13.04.2023
:: Autor: MicKoc
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PKJM - Jednostki miar urządzeń do b.trzeźwości
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Jednostki miar urządzeń do b.trzeźwości - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
Cntx.psh(KOT_JM,SLO_KOD);
KOT_JM.index('NUMER');
SLO_KOD.win_dict('SLO2');
KOT_JM.prefix();
KOT_JM.select();
Cntx.pop(KOT_JM,SLO_KOD);
~~


\kot_jm_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Rekord po tabeli KOT_JM
::   WE:
::   WY:
::  OLD: \kot_jm_ra/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(KOT_JM,-menu_txt()='popraw',,'JM','SJM','KOT_RODZ');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & _chk=0)
|| return(_chk)
?};
''


\_kot_jm_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Wyzwalacz na dołącz przed tabeli KOT_JM
::   WE:
::   WY:
::  OLD: \_kot_jm_addb/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOT_JM.LP<=0
|| _numer:=1;
   KOT_JM.cntx_psh();
   KOT_JM.index('NUMER');
   KOT_JM.prefix();
   {? KOT_JM.last()
   || _numer:=KOT_JM.LP+1
   ?};
   KOT_JM.cntx_pop();
   KOT_JM.LP:=_numer
?};
1


\kot_jm_wdol_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Przesuń jednostkę w dół
::  OLD: \kot_jm_wgore_a/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOT_JM.cntx_psh();
KOT_JM.index('NUMER');
do();
_old:=_new:=KOT_JM.LP;
_refo:=_refn:=KOT_JM.ref();
_ok:=KOT_JM.next();
{? _ok
|| _new:=KOT_JM.LP;
   _refn:=KOT_JM.ref()
?};
{? _refn<>_refo
|| KOT_JM.cntx_psh();
   KOT_JM.last();
   _last:=KOT_JM.LP;
   KOT_JM.cntx_pop();
   KOT_JM.LP:=_last+1;
   _ok*=KOT_JM.put(1);
   {? _ok & KOT_JM.seek(_refo)
   || KOT_JM.LP:=_new;
      _ok*=KOT_JM.put(1)
   ?};
   {? _ok & KOT_JM.seek(_refn)
   || KOT_JM.LP:=_old;
      _ok*=KOT_JM.put(1)
   ?}
?};
{? ~_ok || undo() ?};
end();
KOT_JM.cntx_pop()


\kot_jm_wgore_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Przesuń jednostkę w górę
::  OLD: \kot_jm_wgore_a/kontrola_trzezwosci.fml
::----------------------------------------------------------------------------------------------------------------------
KOT_JM.cntx_psh();
KOT_JM.index('NUMER');
do();
_old:=_new:=KOT_JM.LP;
_refo:=_refn:=KOT_JM.ref();
_ok:=KOT_JM.prev();
{? _ok
|| _new:=KOT_JM.LP;
   _refn:=KOT_JM.ref()
?};
{? _refn<>_refo
|| KOT_JM.cntx_psh();
   KOT_JM.first();
   _first:=KOT_JM.LP;
   KOT_JM.cntx_pop();
   KOT_JM.LP:=_first-1;
   _ok*=KOT_JM.put(1);
   {? _ok & KOT_JM.seek(_refo)
   || KOT_JM.LP:=_new;
      _ok*=KOT_JM.put(1)
   ?};
   {? _ok & KOT_JM.seek(_refn)
   || KOT_JM.LP:=_old;
      _ok*=KOT_JM.put(1)
   ?}
?};
{? ~_ok || undo() ?};
end();
KOT_JM.cntx_pop()

:Sign Version 2.0 jowisz:1045 2023/06/27 11:10:45 54cc0597558b8ecc494cd803e9db180440119bbbe23f12d9d3572e6b9742e071c2753ab0f5bd15ef5bcafd5b1f25ea137643f45a007e9007fc57d4fd5cce21667459fa3f4b3cfec80660190d6b27c0f085df001c07ca919d30fc3260a49f12f5f5d17f76631076ac2421172c942c21a5844db55ffc5ee1c01333616e875b2a96

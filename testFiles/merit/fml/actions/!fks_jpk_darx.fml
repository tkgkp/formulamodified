:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_jpk_darx.fml
:: Utworzony: 26.08.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_JPK_DARX - Dołączanie pliku JPK z XML
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dołączanie plików JPK z XML - główna formuła czynności FKS_JPK_DARX.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,HRB
:: PARAMETRY WE:
::# kind=WE, symbol=TYP, type=STRING, name=Typ operacji: jedne plik lub wiele, required=N,fml_val="exec('set_typ','!fks_jpk_darx',_a)"
:: PARAMETRY WY:
::# kind=WY, symbol=JPK, type=_JPK, name=Wskazanie na plik JPK, required=T, keyref=T
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_typ:={? var_press('TYP',_we)>0
      || {? _we.TYP='Pojedynczy' || 1 || 2 ?}
      || 1
      ?};
_jpk:={? _typ=1
      || exec('add_jpk','!fks_jpk_darx')
      || exec('scal_xml','!fks_jpk_darx')
      ?};
{? _jpk
|| _wy.JPK:=_jpk;
   _mp.save(,_wy);
   _mp.done()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis na liście TODO czynności
::----------------------------------------------------------------------------------------------------------------------
_desc:='Dołączanie plików JPK z XML'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_desc


\add_jpk_xml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Dodaje plik xml do plikow JPK
::  OLD: \add_jpk_xml/jpk.fml
::   WE: _a - 1-pojedynczy 2-scalanie
::----------------------------------------------------------------------------------------------------------------------
{? 2+JPK.TYP='V7'
|| FUN.info('Akcja scal xml nie jest dostępna dla JPK_V7.\nScalanie plików JPK_V7 dostępne z okna pozycji deklaracji'@);
   return()
|? 2+JPK.TYP='GV'
|| FUN.info('Akcja scal xml nie jest dostępna dla JPK_GV.'@);
   return()
?};
{? var_press('_a')<=0 || _a:=1 ?};
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_JPK_DARX';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYP',{? _a=1 || 'Pojedynczy' || 'Scalony' ?});
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\add_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Dodaje plik xml do plikow JPK
::----------------------------------------------------------------------------------------------------------------------
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
_sep:=exec('sep','#file',1);
_jpk:=null;
_tmpdir:=fmk_tmp_dir(0);
{? type_of(_tmpdir)<>type_of(~~)
|| _fdir:=_tmpdir.get_path
|| return(0)
?};
_path:=_fdir+_sep+dlg_upload(_fdir,0,'.xml');
{? _path+1<>_sep
|| {? -(_path+4)<>'.xml'
   || FUN.info('Wskazano inny plik niż *.xml.'@)
   || _jpk:=exec('add_jpk2','!fks_jpk_darx',_path)
   ?}
?};
_jpk


\add_jpk2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Dodaje plik xml do plikow JPK
::   WE: _a - ścieżka do pliku
:: ~OST: INFERASE
::----------------------------------------------------------------------------------------------------------------------
_jpk:=null;
_path:=_a;
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
_f:=fopen(_path,'br');
{? _f
|| _size:=fgetsize(_f)>1073741824;
   fclose(_f)
|| _size:=0
?};
{? _size
|| _f:=0;
   FUN.info('Wskazany plik jest zbyt duży (powyżej 1GB).'@)
|| _f:=fopen(_path,'ur')
?};
{? _f
|| _ok:=0; xmlLine:='';
   _end:='/Naglowek';
   _wariant:=0;
   _lp:=-1;
   _rok:=-1;
   {!
   |? _xml:=fread(_f);
      {? _xml<>'\n'
      || xmlLine+=_xml;
         {!
         |? _tag:=exec('get_tag','xml');
            {? _tag<>''
            || {? xmlTag='?xml'
               || _ok:=1
               |? _ok=1 & exec('is_jpk','jpk',xmlTag)
               || _ok:=2;
                  exec('set_jpk_sch','!fks_jpk_darx',_tag);
                  {? 'VAT,V7M,V7K,GV'*HELPJPK.ISTDEF().RODZAJ
                  || _end:='/Podmiot1'
                  ?}
               |? _ok=2
               || {? xmlTag='KodFormularza'
                  || HELPJPK.RODZAJ:=4-xmlValue;
                     {? HELPJPK.RODZAJ='VAT' & HELPJPK.ISTDEF
                     || HELPJPK.RODZAJ:=HELPJPK.ISTDEF().RODZAJ
                     ?}
                  |? xmlTag='DataOd' | xmlTag='OkresOd'
                  || HELPJPK.DATA_OD:=date(#(4+xmlValue),#(5-xmlValue-3),#(xmlValue+2))
                  |? xmlTag='DataDo' | xmlTag='OkresDo'
                  || HELPJPK.DATA_DO:=date(#(4+xmlValue),#(5-xmlValue-3),#(xmlValue+2))
                  |? xmlTag='Rok' & (2+HELPJPK.RODZAJ='V7' | 2+HELPJPK.RODZAJ='GV')
                  || _rok:=#xmlValue
                  |? xmlTag='Miesiac' & (2+HELPJPK.RODZAJ='V7' | 2+HELPJPK.RODZAJ='GV')
                  || {? _rok>0
                     || HELPJPK.DATA_OD:=date(_rok,#xmlValue,1);
                        HELPJPK.DATA_DO:=date(_rok,#xmlValue,0)
                     ?}
                  |? 'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ & xmlTag='Email'
                  || HELPJPK.EMAIL:=xmlValue
                  |? 'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ & xmlTag='CelZlozenia'
                  || _lp:=#xmlValue
                  |? xmlTag='WariantFormularza'
                  || _wariant:=#xmlValue
                  |? xmlTag=_end
                  || _ok:=3
                  ?}
               ?};
               _ok<>3 & xmlLine<>''
            ?}
         !};
         _ok>0 & _ok<3
      ?}
   !};
   fclose(_f);
   {? _ok=0
   || FUN.info('Wskazany plik nie jest formatu XML.'@)
   |? _ok=1
   || FUN.info('Wskazany plik nie jest plikiem JPK.'@)
   |? HELPJPK.ISTDEF=null
   || FUN.info('Brak schematu JPK_%1 (%2).'@[HELPJPK.RODZAJ,$_wariant])
   || VAR_DEL.delete('JpkXml');
      JpkXml:=1;
      HELPJPK.OKR_TYP:='D';
      HELPJPK.OKR_OD:=null;
      HELPJPK.CYKL:={? 'VAT,V7M,V7K'*HELPJPK.RODZAJ || 'T' || 'N' ?};
      HELPJPK.KOR:={? 2+HELPJPK.RODZAJ='V7'
                   || {? _lp=2
                      || HELPJPK.KOR_D:=-1;
                         HELPJPK.KOR_E:=-1;
                         1
                      ?}
                   |? 2+HELPJPK.RODZAJ='GV' & _lp<>2
                   || 0
                   || {? HELPJPK.DATA_OD>=date(2018,1,1) || _lp>0 || _lp=2 ?}
                   ?};
      {? 'V7M,V7K'*HELPJPK.RODZAJ
      || _lp:=exec('get_lp','jpk')
      ?};
      HELPJPK.LP:=_lp;
      HELPJPK.DATE:=date();
      HELPJPK.TIME:=time();
      HELPJPK.win_edit('RED_XML');
      {? HELPJPK.edit("exec('ar_jpk','jpk')")
      || exec('uzu_daty','jpk');
         JPK.cntx_psh();
         JPK.prefix();
         _jpk:=null;
         {? exec('jpk_nag','jpk',HELPJPK.OKR_OD,HELPJPK.DATA_OD,HELPJPK.DATA_DO,HELPJPK.OPIS,'','N',HELPJPK.LP,{? JPK.TYP='SF' || JPK.DO || ~~ ?})
         || _jpk:=JPK.ref();
            _ref:=JPK.uidref();
            exec('SignInfo','object');
            _name:=exec('filename','#string',_path);
            _bp:=0;
            _rs:=0;
            {? var_press('JPK_SIG',JPK)>0 & SignInfo.remove(_path) & fexists(_path+'.s')
            || JPK.bl_put('JPK_SIG',_path+'.s',0,,_name);
               _bp:=JPK.bl_put('PLIK',_path+'.b',0,,_name);
               _path:=_path+'.b';
               _rs:=1
            || _bp:=JPK.bl_put('PLIK',_path)
            ?};
            {? _bp
            || JPK.put();
               ISTDEF.cntx_psh();
               ISTDEF.prefix();
               {? ISTDEF.seek(HELPJPK.ISTDEF)
               || VAR_DEL.delete('ErrFun');
                  ErrFun:="exec('errfun','jpk',_a,_b)";
                  exec('validate','xml',_path);
                  VAR_DEL.delete('ErrFun');
                  exec('upd_jpk','jpk');
                  JPK.put()
               ?};
               ISTDEF.cntx_pop()
            || _jpk:=null;
               {? JPK.seek(_ref)
               || JPK.del()
               ?}
            ?};
            {? _rs
            || ferase('@'+_a+'.s');
               ferase('@'+_a+'.b')
            ?}
         ?};
         JPK.cntx_pop();
         {? _jpk || JPK.seek(_jpk) ?}
      ?};
      VAR_DEL.delete('JpkXml')
   ?};
   VAR_DEL.delete('XmlValid')
|| FUN.info('Nie udało się otworzyć pliku %1.'@[_path])
?};
_jpk


\set_jpk_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawia schemat JPK na podstawie tagu <JPK>
::   WE: _a - tag JPK
::  OLD: \set_jpk_sch/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.ISTDEF:=null;
_tag:=_a;
_ii:=_tag*'JPK';
{? _ii>2
|| _pfx:=1-((_ii-2)+_tag);
   {? _pfx<>''
   || _p:=_tag*('xmlns:'+_pfx+'="');  _par:='xmlns:'+_pfx
   || _p:=_tag*'xmlns="';  _par:='xmlns'
   ?}
|? HELPJPK.RODZAJ='SF'
|| {? 1+_tag='<' || _tag:=1-_tag ?};
   _p:=_tag*':';
   _prfx:={? _p || (_p-1)+_tag || '' ?};
   _par:='xmlns:'+_prfx;
   _p:=_tag*_par
|| _p:=_tag*'xmlns="'; _par:='xmlns'
?};
{? _p
|| _tag:=(_p+(+_par+1))-_tag;
   _p:=_tag*'"';
   {? _p
   || _xmlns:=(_p-1)+_tag;
      _tab:=sql(
         'select REFERENCE as REF from ISTDEFI where NAZ=\':_a\' and REGULY=\':_b\'',
         'xmlns','\'\''+_xmlns+'\'\''
      );
      {? _tab.first()
      || ISTDEFI.cntx_psh(); ISTDEFS.cntx_psh();
         ISTDEFI.prefix();
         {? ISTDEFI.seek(BIT.sqlint(_tab.REF),)
         || HELPJPK.ISTDEF:=ISTDEFI.ISTDEFS().ISTDEF
         ?};
         ISTDEFI.cntx_pop(); ISTDEFS.cntx_pop()
      ?}
   ?}
?}


\set_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Ustawia typ dołaczanego pliku - jeden lub z wielu
::   WE: _a - wartość parametru
::----------------------------------------------------------------------------------------------------------------------
_sel:={? var_press('_a')>0
      || {? _a='Pojedynczy'
         || 1
         || 2
         ?}
      || 1
      ?};
_pyt:=FUN.choice('Typ pliku.'@,_sel,'Pojedynczy'@,'Scalony'@);
{? _pyt=1
|| 'Pojedynczy'
|? _pyt=2
|| 'Scalony'
|| ~~
?}


\scal_xml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Scala pliki JPK_VAT
::  OLD: \scal_xml/jpk.fml
:: ~OST: INFCOPY,INFMKDIR,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
_jpk:=null;
_jar:='jpk_merger.jar';
{? exec('cli_functions','#system')=0
   || FUN.emsg(exec('indevice_nacc_msg','#system'));
      return(0)
?};
_file:='@'+tmp_dir()+'/jpk/jpk_vat_merge.xml';
ferase(_file);
fmkdir('@'+tmp_dir(),'jpk');
_fcopy:=fcopy(_jar,'@'+tmp_dir()+'/jpk/'+_jar,1,0,1);
echo(_jar);
{? _fcopy
|| _files:=files('jpk_vat*.xsd');
   {? _files.first()
   || {!
      |? _fcopy*=fcopy(_files.FILENAME,'@!Tmp/jpk/'+_files.FILENAME,1,0,1);
         echo(_files.FILENAME);
         _fcopy & _files.next()
      !}
   ?}
?};
{? _fcopy
|| echo();
   exec('MBJAR','#object')
?};

{? ~_fcopy
|| FUN.info('Powstał błąd podczas kopiowania pliku.'@);
   echo()
|?


   ~MBJAR.JavaExec(tmp_dir+'/jpk',1,'-jar',_jar,'-sys')

|| {? fexists(_file)
   ||

      _pyt:=FUN.choice('Gdzie zapisać scalony plik?'@,1,'W systemie'@,'Na dysku'@);
      {? _pyt=1
      || _jpk:=exec('add_jpk2','!fks_jpk_darx',1-_file)
      |? _pyt=2
      ||
         dlg_save(1-_file)
      ?}
   ?}
?};
_jpk

:Sign Version 2.0 jowisz:1045 2023/09/07 12:13:11 b7c46ffa20bb3ea2057e8eb913a48b9a58796d6094b74b0dcada167ceaafdaa4bec39ad97689a0f74aff678c5fa43192fc0b679494cb5f7475bacef43f6979bc3ea2578a49cbabe7b828b3966304c11bcf80a8a31d23eb81e794dbb1f327e1ea2d0b1a0340e6f21dda6cb2dbac498b1fa1d4c4fd68f86dbcaa7ae8dd63b60fb6

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_faw_cprz.fml
:: Utworzony: 07.06.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_CPRZ - Przekazanie wniosku w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie wniosku w obiegu - główna formuła czynności OBE_FAW_CPRZ
::----------------------------------------------------------------------------------------------------------------------
::# parses=exec('parses_edokum','obiegi')
::# access=exec('uprawnienia','obiegi')

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=T, keyref=T
::# kind=WE, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WE, symbol=B_PREL, type=STRING, name=Poprzedni element w procesie, required=N
::# kind=WE, symbol=B_PRELS, type=STRING, name=Symbol poprzedniego elementu w procesie, required=N
::# kind=WE, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WE, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WE, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WE, symbol=STATUS, type=STRING, name=Status, required=N

::# kind=WE, symbol=B_ROLEF, type=STRING, name=Rola użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=B_ROLE, type=STRING, name=Domyślna rola użytkowników, required=N, fml_val="exec('rola','obiegi',_a)", fml_exp="exec('b_role_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=BLZMROL, type=STRING, name=Blokada zmiany roli, required=N, fml_val="exec('blzmrol','obiegi')"
::# kind=WE, symbol=FORM_ODR, type=STRING, name=Formuła wykonywana przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORMWODR, type=STRING, name=Formuła walidacyjna przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"

::# kind=WE, symbol=BLZMUSR, type=STRING, name=Blokada zmiany uzytkowników, required=N, fml_val="exec('blzmusers','obiegi')"
::# kind=WE, symbol=USR_FORM, type=STRING, name=Wybór użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=JEDEN_OP, type=STRING, name=Obsługuje jeden operator - formuła, required=N, fml_val="exec('jeden_op','obiegi2')"
::# kind=WE, symbol=UWODRWYM, type=STRING, name=Wymagane wypełnienie uwag przy odrzuceniu, required=N, fml_val="exec('czy_wymoduw','obiegi_akc')"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N
::# kind=WY, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WY, symbol=USERS, type=_USERS, name=Użytkownik, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WY, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WY, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N

:: WŁAŚCIWOŚCI CZYNNOŚCI:
::# properties=LOOP,TODOTRIG,~RELEASE

:: WARUNKI BRAMY:
::# condition=Akceptacja wniosku, act_uid=OBE_FAW_EAKP, auto=T, formula=_a.EDOKUM<>~~ & _a.EDOKUM<>null
exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_context:=_args.context;
_akcja:={? type_of(_context)>100 & var_pres('AKCJA',_context)>0 || _context.AKCJA || _mp.akcja() ?};

{? _akcja='Portal'
|| {? var_pres('EDOKUM',_we)>0 & _we.EDOKUM
   || EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_we.EDOKUM));
      {? EDOKUM.seek(_we.EDOKUM)
      || params_exec('ustaw_bprel','obiegi');
         _wy.EDOKUM:=EDOKUM.ref();
         _wy.WAL:=EDOKUM.WAL;
         _wy.NETTO:=EDOKUM.NETTO;
         _wy.BRUTTO:=EDOKUM.WART;
         _wy.DEKR:=EDOKUM.TYP().DEKR;
         _wy.STATUS:={? EDOKUM.STATUS='D' || 'O' || 'A' ?};
         _wy.B_PREL:=OBIEGI.B_PREL;
         _wy.B_PRELS:=OBIEGI.B_PRELS;

         {? EDOKUM.STATUS='D'
         || {? var_pres('FORM_ODR',_we)>0 & _we.FORM_ODR<>''
            || _fml:=_we.FORM_ODR;
               {? _fml<>''
               || ($_fml)()
               ?}
            ?}
         ?};

         _mp.save(,_wy);
         _mp.keep();
         _mp.done()
      ?};
      EDOKUM.cntx_pop()
   ?};
   return(~~)
?};

OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
zak_akc_ed:=0;

{? _akcja='Przekaż2'
|| params_get().context.EDOKUM;
   params_exec('przekaz2','obiegi_akc')
|| {? ~exec('is_act','#b__box','OBE_FAW_CPRZ')
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków.'@);
      _mp.cancel(); return(~~)
   |? _mp.isMicro()
   || FUN.info('Wniosek nie jest w trakcie przekazywania do akceptacji.'@); return(~~)
   ?};
   {? var_pres('UNIK_ID',_we)>0 & _we.UNIK_ID<>''
   || non_eds:=1;
      params_exec('par_out','obiegi_akc',_we.UNIK_ID);
      VAR_DEL.delete('non_eds')
   |? _we.EDOKUM & EDOKUM.seek(_we.EDOKUM,ref_name(_we.EDOKUM),1)
   || params_exec('ustaw_bprel','obiegi');
      exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
      {? exec('ust_b_role','obiegi_akc',_we.EDOKUM,OBIEGI.B_PREL)=0 || return(~~) ?};
      BPMN.END:=0;
      {? app_info('web_sesid')=''
      || {? _akcja='' & EDOKUM.ZAM<>'T'
         || _akcja:=exec('akc_win','obe_faw','P')
         ?};
         {? _akcja='Odrzuc1'
         || params_exec('odrzuc','obiegi_akc',0);
            _akcja:={? BPMN.END || 'Odrzuc2' || '' ?}
         ?}
      ?};
:: sprawdzenie z poziomu webterma
      {? _akcja='chkWT'
      || params_get().context.SPR_WT:=1
:: akceptacja z pulpitu
      |? _akcja='Przekaż'
      || {? app_info('web_sesid')<>'' || params_get().context.EDOKUM ?};
         params_exec('przekaz','obiegi_akc')
      |? _akcja='Odrzuc2'
      || {? app_info('web_sesid')<>'' || params_get().context.EDOKUM ?};
         EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',5); OBIEGI.EDOKOS();
         {? BPMN.END=0 || params_exec('odrzuc2','obiegi_akc') ?};
         {? EDOKUM.get()
         || VAR_DEL.delete('edokosOset');
            exec('ustaw_out','obiegi_akc',_wy,'O');
            _mp.save(,_wy);
            _mp.done()
         ?}
      ?}
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::  OLD: \desc/!obe_faw_cprz.fml
::----------------------------------------------------------------------------------------------------------------------
_desc:='Przekaż do akceptacji'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));
params_exec('ustaw_bprel','obiegi');
{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam) & EDOKUM.ZAM<>'T'
      || params_exec('ustaw_bprel','obiegi');
         exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
         _desc:='Przekaż do akceptacji %1: %2, data operacji %3'@@[(-EDOKUM.TYP().NAZWA),EDOKUM.ID,$EDOKUM.DOP];
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)
      ?};
      {? var_pres('co_dalej')>0 &
         ((4+web_top_win(0,0))='WPRG' | (4+web_top_win(1,0))='WPRG' | (4+web_top_win(2,0))='WPRG')
      || co_dalej:='P'
      ?};
      {? var_pres('STATUS',_we)>0 & _we.STATUS='O' & _mp.akcja()<>'Przekaż' & _set
      || {? OBIEGI.B_PREL<>''
         || EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
            {? EDOKOS.first()
            || {! |?
                  EDOKOS.STATUS:='O'; EDOKOS.put();
                  EDOKOS.next()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?}
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::       _b - B_PREL.ref() lub BI_PREL.ref() lub exec('mp_run_a','#b__box')
::  OLD: \web_main/!obe_faw_cprz.fml
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
_auto:=0;
:: Inicjuje obiekt Menadżera Procesu, aby sprawdzić czy web_main uruchomiony w pętli:
BI_PREL.cntx_psh();
{? ref_tab(_b)=BI_PREL & BI_PREL.seek(_b,ref_name(_b),1)
|| _class:={? __develop || @.Class.cProMan || @.CLASS.cProMan ?};
   _proMan:=obj_new(_class);
   _proMan.setCntx(_b);
   _auto:=_proMan.loop()
?};
BI_PREL.cntx_pop();
{? _auto
|| exec('web_run','#b__box',_b,'Todo',,'web_Todo',,,,,,'T')
|? _path='Todo'
|| _path_param:=_b;
   _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   _prel:=_path_param;
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || _name:=((_keyrefs[1]-8)+8);
      EDOKUM.use(_name); EDOKUM.prefix();
      {? EDOKUM.seek(_keyrefs[1],,1)
      || exec('maski_w','obiegi',1);
         EDOKUM.memo_get(,'UW_OPDL');
         exec('bobs_edokum','obiegi','W');
         SKID.SL_KH:=SKID.NIP:='';
         web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'ed_kh_ed',0));
:: Okno wyświetla się tylko dynamicznie.
         _red:=exec('win_red_wnioski','obiegi2','P',,0);
         web_def_fld_opt(_red, 'editable=0',);
         EDOKUM.web_cntx_save();
         web_def_disp(_red,"
            EDOKUM.web_cntx_load();
            exec('inne_akcje','obiegi2',_a,1)
         ",'Wniosek w obiegu'@)
      ?}
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
?}


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności OBG_FAW_CPRZ
::  WE: _a - 'add po', 'put przed', 'put po', 'del przed'
::      _b - wynik akcji
::  OLD: \todo_triggers/!obe_faw_cprz.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='add_po' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='add_po'
         || 3
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'OBE_FAW_CPRZ')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [19.22]
:: OPIS: Podglad dokumentu obiegu z listy zadań
::  OLD: \display/!obe_faw_cprz.fml
::----------------------------------------------------------------------------------------------------------------------
params_exec('disp_wni','obiegi2',2)

:Sign Version 2.0 jowisz:1045 2023/09/19 15:38:52 9ae85c83b5d6efe768c0c6d8d769ec57f13f17a4c58fad356e4b4793aa4b6afb9f06012800eedae383115cf65fc672d353ae56472f82250b43bdf40dcd651470ca43dda5e656ad6198e59124550694c154a7ed5825182a7eb834725b83d4184d04042ce20225d7e1cc1f01abafc9586230ac1e8b2bb6e8fd25dc32905881ec1b

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_ords.fml
:: Utworzony: 02.02.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORDS - Rejestracja dyscypliny pracy
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja dyscypliny pracy - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_ezk_ords')

|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność '+_mp.buf_act.UID+' nie obsługuje akcji '+_do+'.'
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_ords',_in.P);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja dyscypliny pracy - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj dyscyplinę pracy: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj dyscyplinę pracy: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj dyscyplinę pracy: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj dyscyplinę pracy'@@
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie pracownika
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!pkd_ezk_ords');

{? var_pres('_a')<>type_of(null) | _a=null
|| return(_err_msg)
?};

P.cntx_psh();
P.prefix();
{? ~P.seek(_a)
|| P.cntx_pop();
   return(_err_msg)
?};

: ustal osobę
OSOBA.cntx_psh();
P.OSOBA();

: przeglądanie rubryk
R.cntx_psh();
R.win_dict('SLO');
R.win_patt('WZO');

: pokaż dyscyplinę
DS.cntx_psh();
DS.index('DYSCYPLI');
DS.prefix(_a);
DS.win_sel('WER_P');
DS.win_edit('RED');
DS.win_patt('WZO');
_result:=DS.select();
DS.cntx_pop();

: porządki
R.cntx_pop();
P.cntx_pop();
OSOBA.cntx_pop();

_result


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli DS z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli DS jako składowa okna grupowego tabeli UD_DEF).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=DS
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie dyscypliny pracy niemożliwe.\nNie znaleziono pracownika.'


\ds_nb_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wzorzec dla pola NB tabeli DS.
::   WE:
::   WY: ''
::----------------------------------------------------------------------------------------------------------------------
exec('slownik_atr','rubryki',197);
''


\ds_nx_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Po redakcji DS.NN
::   WY: 1
::  OLD: \ds_nn_ae/kali.fml
::----------------------------------------------------------------------------------------------------------------------
DS.NP:=DS.NN-DS.NW+DS.NZ;
1


\ds_oblicz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Wywolywana przed/po akcjach w okienkach tabeli DS
::  OLD: \oblicz_ds/kali.fml
::----------------------------------------------------------------------------------------------------------------------
exec('FUNKCJE','object');
{? DS.NB=__RUB.ref(__RUB.sys_kod(1162))
|| N.cntx_psh();
   N.index('NIPRACNB');
   N.prefix('N',P.ref,DS.NB().RN);
   {? N.find_le(date(DS.R,12,0)) & N.OD~1=DS.R
   || FUN.emsg('W kartotece nieobecności w %1 roku istnieją zapisy dla składnika %2 (Kod %3).'@[$DS.R,DS.NB().RT,$R.RN]+
               '\n'+'Należy zweryfikować poprawność zapisów.'@)
   ?};
   N.cntx_pop()
?};
FUNKCJE.OBLICZDS(DS.R)


\ds_rekord_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rekord przed okienka tabeli DS
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
POLA_GRP.TXT_1:=
   {? DS.NR='K' || 'Dni kalendarzowe'
   |? DS.NR='R' || 'Dni robocze'
   |? DS.NR='G' || 'Godziny robocze'
   || ''
   ?};
0


\ds_okno_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Okno po okienek tabeli DS.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
R.f_clear();
1


\ds_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Rekord po tabeli DS
::   WY: zgodna ze specyfikacją dla akcji "rekord po"
::  OLD: \ds_arec/kali.fml
::----------------------------------------------------------------------------------------------------------------------
: sprawdź wypełnienie i unikalność
_chk:=__CHK.table(DS,-menu_txt='popraw',,'R','NB');
{? type_of(_chk)<>type_of(0) | ~_chk
:  porażka podstawowego testu
|| return(_chk)
?};

{? __RUB.sys_attr(DS.NB,1162)
|| exec('KOMM','#object');
   KOMM.init(255);

   _max_lim:=
      {? DS.NR='G'
      || __RUB.sys_val(19712,date(DS.R,1,1),0)
      || __RUB.sys_val(19711,date(DS.R,1,1),0)
      ?};

   OSOBA.cntx_psh();
   OSOBA.prefix();
   _osoba:=DS.P().OSOBA;
   {? OSOBA.seek(_osoba)
   || {? DS.NN>_max_lim
      || KOMM.sect_beg(OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE);
         KOMM.sect_beg('Uwaga',4);
         KOMM.add(
            'Przekroczono dopuszczalny limit (%1 %2)'
            ' opieki nad dzieckiem do lat 14 (Art. 188).'@[$_max_lim,{? DS.NR='G' || 'godzin' || 'dni' ?}]
         );
         KOMM.sect_end();
         exec('md_info','staz',_osoba,DS.R,KOMM,0,0);
         KOMM.sect_end();
         KOMM.select()

      || exec('md_info','staz',_osoba,DS.R)
      ?}
   ?};
   OSOBA.cntx_pop()
|? __RUB.sys_attr(DS.NB,1171)
|| exec('KOMM','#object');
   KOMM.init(255);

   _max_lim:=
      {? DS.NR='R'
      || __RUB.sys_val(19731,date(DS.R,1,1),0)
      ?};

   OSOBA.cntx_psh();
   OSOBA.prefix();
   _osoba:=DS.P().OSOBA;
   {? OSOBA.seek(_osoba)
   || {? DS.NN>_max_lim & DS.NR='R'
      || KOMM.sect_beg(OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE);
         KOMM.sect_beg('Uwaga',4);
         KOMM.add(
            'Przekroczono dopuszczalny limit (%1 %2)'
            ' urlopu rehabilitacyjnego.'@[$_max_lim,'dni']
         );
         KOMM.sect_end();
         exec('dod_info','staz',_osoba,DS.R,KOMM,0,0);
         KOMM.sect_end();
         KOMM.select()

      || exec('dod_info','staz',_osoba,DS.R)
      ?}
   ?};
   OSOBA.cntx_pop()
:: Urlop opiekuńczy lub siła wyższa
|? exec('upgrade2226_nru01','wnioski_urlopowe') & (__RUB.sys_attr(DS.NB,1181) | __RUB.sys_attr(DS.NB,1191))
|| exec('KOMM','#object');
   KOMM.init(255);

   _opiek:=__RUB.sys_attr(DS.NB,1191);
   _max_lim:={? _opiek
             || {? DS.NR='R'
                || __RUB.sys_val(19751,date(DS.R,1,1))
                ?}
             || {? DS.NR='G'
                || __RUB.sys_val(19742,date(DS.R,1,1))
                || __RUB.sys_val(19741,date(DS.R,1,1))
                ?}
             ?};

   OSOBA.cntx_psh();
   OSOBA.prefix();
   _osoba:=DS.P().OSOBA;
   {? OSOBA.seek(_osoba)
   || {? DS.NN>_max_lim
      || KOMM.sect_beg(OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE);
         KOMM.sect_beg('Uwaga'@,4);
         KOMM.add('%1 (%2 %3) %4.'['Przekroczono dopuszczalny limit'@,$_max_lim,
            {? DS.NR='G' || 'godzin'@ || 'dni'@ ?},{? _opiek || 'urlopu opiekuńczego'@ || 'siły wyższej'@ ?}]);
         KOMM.sect_end();
         exec('opwy_info','staz',_osoba,DS.R,KOMM,0,0);
         KOMM.sect_end();
         KOMM.select()

      || exec('opwy_info','staz',_osoba,DS.R,,,,,{? _opiek || 'O' || 'W' ?})
      ?}
   ?};
   OSOBA.cntx_pop()
?};

1


\ds_nb_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Po redakcji DS.NB
::   WY: 1
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
{? __RUB.sys_attr(DS.NB,1171)
|| DS.NR:='R';
   {? DS.NN=0
   || DS.NN:=__RUB.sys_val(19731,date(DS.R,1,1),0);
      _staz:=exec('dod_staz','staz',DS.P().OSOBA,DS.R);
      {? _staz.dni>0
      || DS.NN-=_staz.dni
      ?}
   ?}
?};
1

:Sign Version 2.0 jowisz:1045 2023/06/09 14:39:47 cae0d2ddc53ada806c7af7b3b91f39ce4748a1fe5dc8c3eca7d3a022fed168fd59252713d057df536964250ec6f122b14199133aee4ee7b85ad9a26b936c6fd9154b9af5f4056a98449c6e39175abd9a67356a7fed820e098b97bb08854726da2aada4ce1597df4371a70584409db1eaef641df065e71a4db3bf4820e286a580

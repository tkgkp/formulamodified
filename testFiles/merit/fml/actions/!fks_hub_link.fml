:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_hub_link.fml
:: Utworzony: 08.12.2023
:: Autor: PBS
::======================================================================================================================
:: Zawartość: Formuły wyświetlające dokumenty MeritHUB przekazane linkiem
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła główna obszaru dokumentów MeritHUB
::   WE: _a - link do dokumentu
::----------------------------------------------------------------------------------------------------------------------
exec('ini_icon','!fks_dks_zdok');
exec('fld_prec','!fks_dks_zdok');
DOK.cntx_psh(); REJ.cntx_psh(); REJ.prefix();
{? var_pres('MERITHUB',DOK)>0 & _a<>'' & 4+ref_name(_a)='doku'
|| DOK.use(ref_name(_a)); DOK.cntx_psh(); DOK.prefix();
   {? DOK.seek(_a) & DOK.MERITHUB<>''
   || VAR_DEL.delete('mhubdok');
      mhubdok:=obj_new('NR','REJ','REJ_KOD','ID');
      DOK.REJ();
      mhubdok.NR:=DOK.NR;
      mhubdok.REJ:=DOK.REJ;
      mhubdok.ID:=DOK.IDADD;
      mhubdok.REJ_KOD:=DOK.REJ().KOD
   ?};
   DOK.cntx_pop()
?};
DOK.cntx_pop(); REJ.cntx_pop();
exec('dok_sel','!fks_hub_link',_a)


\link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Funkcja do obsługi przekazanego linku
::   WE: _a - link (STRING[48])
::   WY: 1/0 - czy ustawienia na podstawie linku poprawne
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? ref_tab(_a)<>DOK
|| FUN.info('Link nie jest odnośnikiem do dokumentu księgowego'@); _dalej:=0
|| _maska:=ref_name(_a);
   DOK.use(_maska);
   DOK.prefix();
   {? ~DOK.seek(_a,DOK.name())
   || FUN.info('Nie odnaleziono rekordu zgodnego z przekazanym linkiem.'@);
      _dalej:=0
   ?}
?};
{? _dalej
|| OPERATOR.DEPT:=_odd:=DOK.ODD;
   _okres:=null;
      _tokres:=DOK.name()+2;
      _trok:=2+(DOK.name()+4);
      ROK_F.cntx_psh(); OKRO_F.cntx_psh();
      ROK_F.index('KOD');
      ROK_F.prefix(REF.FIRMA,_trok);
      {? ROK_F.first()
      || SSTALE.AR:=ROK_F.ref();
         OKRO_F.index('ROK');
         OKRO_F.prefix(ROK_F.ref(),#_tokres);
         {? OKRO_F.first() || SSTALE.AO:=_okres:=OKRO_F.ref() ?}
      ?};
      ROK_F.cntx_pop(); OKRO_F.cntx_pop();

      {? _odd & _okres
      || __PARSES.setVal('JednostkaKsięgowa',_odd);
         __PARSES.setVal('OkresRok',_okres)
      || FUN.emsg('Nie udało się ustawić prawidłowych parametrów pracy na podstawie przekazanego linku.'@);
         _dalej:=0
      ?}
?};

_dalej


\dok_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Właściwa formuła wyświetlająca dokument
::----------------------------------------------------------------------------------------------------------------------
_fml:="DOK.index('REJ');
      {? var_pres('mhubdok')<=0
      || DOK.prefix(-1,-1)
      || DOK.prefix(mhubdok.REJ,mhubdok.NR)
      ?};
      {? DOK.first() & DOK.IDADD=mhubdok.ID
      || _maska:=ref_name(DOK.ref())+4;
         P_V.use('p_v_'+_maska);
         VPOZ.use('pozv'+_maska);
         FAK.use('fak_'+_maska);
         VROZ.use('povr'+_maska);
         VPOW.use('pozw'+_maska);
         SKIDXD.use('skxd'+_maska);
         POZ.use('pozy'+_maska);
         POZF.use('pozf'+_maska);
         DOK_JPK.use('dokj'+_maska)
      || DOK.prefix(-1,-1); DOK.blank(1); DOK.first()
      ?};
      grp_disp(DOK,'HUB_LINK',1)
";
_grp:=DOK.grp_make(,_fml,'grp_hub_link',,,"exec('exit','zws')");
_fml:="{? DOK.IDADD<>mhubdok.ID
       || DOK.prefix(-1,-1); DOK.first()
       ?};
       grp_edisp(DOK,'RDOKED')
";
DOK.grp_sel(_grp,DOK,'HUB_LINK','Dokument'@,_fml,,,,"dok_zbl:=0","",0,1,'maximized','fks_dok_all',0);


DOK.grp_splt(_grp,,'horizontal','bottom','20,55%');
DOK.grp_edit(_grp,DOK,'RDOKED','Status'@,,,,,'maximized');
{? exec('upgrade2325_blbc1','zbl')
|| DOK.grp_sel(_grp,DOKUMZBC,'WER_DOK','Rezultat kontroli Businesscheck'@,,,,,,,,,'maximized')
?};
{? PAR_SKID.get(442)='T' || exec('winWebPrvDok','!fks_hub_link',_grp,DOK) ?};

DOK.index('REJ');
{? var_pres('mhubdok')<=0
|| DOK.prefix(-1,-1)
|| DOK.prefix(mhubdok.REJ,mhubdok.NR)
?};
objZakl:=exec('zaklObj','!fks_dks_zdok');
DOK.win_sel(_grp);
AreaTitle.setTabWin(DOK,_grp);
AreaTitle.setTitle();
DOK.select()


\winWebPrvDok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Okno podglądu web browser dla dokumentów księgowych
::   WE: _a - akronim okna grupowego
::       _b - tabela
::----------------------------------------------------------------------------------------------------------------------
_win_grp:=_a;
_tab:={? var_pres('_b')>0 || _b || TREE_REJ ?};
:: Podgląd dokumentu - okno wertowania

exec('web_view_dok','zbl_dok',65,16);

:: podgląd załącznika
{? _tab=DOK
|| DOK.grp_ctr(_win_grp,DOK,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,'Podgląd'@,,,,,,,"",,'maximized')
?}


\br_dok_hub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [RR.xx]
:: OPIS: Rekord przed
::----------------------------------------------------------------------------------------------------------------------
{? DOK.IDADD<>mhubdok.ID
|| DOK.prefix(-1,-1); DOK.blank(1); DOK.first()
?};
{? _a
|| {? ~DOK.sel_size()
   || _hid:=ROZNE.AKC_GRAY;
      _hid_n:='';
      _hid_t:='';
      _hid_o:='';
      {? exec('spr_mod','dok_fks')
      || _hid:='PUAWKFG'+_hid
      |? DOK.A='T'
      || _hid:='PUA'+_hid;
         {? PAR_SKID.get(82)='N' || _hid_t+='S' ?};'E-dokumenty i załączniki dla zaksięgowanych dokumentów?';
         _hid_n+='V';
         {? PAR_SKID.get(82)='N' || _hid_t+=exec('dok_zal','dokum_zal',,,0) ?}
      || _hid:='WKFG'+_hid
      ?};
      {? ~exec('bl_chk_send','dok_fks1') || _hid_n+='W' ?};
      {? DOK.ZP='T'
      || _hid:='WK'+_hid
      || _hid:='FG'+_hid
      ?};
      {? DOK.ZK='T' || _hid:='FG'+_hid ?};
      {? ~DOK.E_DOC || _hid_t+='E' ?};
      {? DOK.DOK_REJ().ZAK_SR<>'T' || _hid_t+='T' ?};
      {? (DOK.DOKZRODL='' | 6+DOK.DOKZRODL='webdok') & DOK.EDOKUM='' || _hid_t+='D' ?};
      {? DOK.DOK_REJ().PR<>'T' || _hid_n+='R' ?};'raty';
      {? DOK.DOK_REJ().AV=null || _hid_n+='V' ?};'schemt VAT';
      _klasa:=DOK.RVAT().RVAT().KVAT().SYM;
      _przel_prosty:=DOK.DOK_REJ().SLO().KOD='PROSTY' & (DOK.DOK_REJ().CZY_DP='T' | DOK.DOK_REJ().CZY_DP='O');
      {? DOK.ZP<>'T' | 5+_klasa<>'Zakup' & 6+_klasa<>'_WWspN' & 3+_klasa<>'Imp' & ~_przel_prosty
      || _hid_n+='P' ; 'przelew'
      ?};
      {? DOK.ZP<>'T' || _hid_n+='C' ; 'rozrachunek' ?};
      {? DOK.DOK_REJ().SLO().KOD='PROSTY' & (DOK.DOK_REJ().CZY_DP='T' | DOK.DOK_REJ().CZY_DP='O')
         & DOK.DOK_REJ().CZY_RRB='T'
      || {? _hid_n*'P'
         || _hid_n:=gsub(_hid_n,'P','')
         ?}
      ?};
      {? ~DOK.RVAT
      || _hid_n+='V';
         _hid_t+='F'
      || exec('bl_dokum_seek','zbl',DOK)
      ?};
      {? exec('usr_rej','b_perm',DOK.REJ)
      || 1
      ?};
      {? DOK.DOK_REJ().ZAK_SR<>'T'
      || _hid_n+='ED'
      ?};
      {? PAR_SKID.get(35)<>'T'
      || _hid_o+='W' ; 'Redakcja wyróżników'
      ?};
      {? ~var_press('DOK_JPK') || _hid_o+='K' ?};
      {? _hid_t<>'' || _hid:='N('+_hid_t+')'+_hid ?};'Dane źródłowe';
      {? _hid_n<>'' || _hid:='C('+_hid_n+')'+_hid ?};'Funkcje';
      {? _hid_o<>'' || _hid:='B('+_hid_o+')'+_hid ?};'Ewidencje obszaru';
      DOK.actions_grayed('HUB_LINK',_hid)
   || DOK.actions_grayed('HUB_LINK','')
   ?};
   exec('hide_act','!fks_ksf_zprz')
?};
exec('rekprzed','color','DOK#01')

:Sign Version 2.0 jowisz:1045 2023/12/21 13:22:33 bf1782df29d58f864cc682fb3e811bd4e67edfc78dc280828ee0cece31f5b26453c55ba221345dd240bc44b46ac5fef25ba75618b402c33677948f2a316e7cc2fde57714659638708f12c4f9fe3f3fa35657e88b03999cad5b0e3e02ec7bdac56bc4bcb49c2c2cf7d632b689e25401bf40df5f728c9c3326885fbc8424d82445

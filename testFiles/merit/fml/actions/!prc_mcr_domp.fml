:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_mcr_domp.fml
:: Utworzony: 11.10.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_MCR_DOMP - Otworzenie zamkniętego mc.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Otworzenie zamkniętego miesiąca rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKRM, type=_A_OKRM, name=Wskazanie na miesiąc rozliczeniowy, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();
_result:='';

_uid_OKRM:=exec('ref2uid','#table',_in.A_OKRM);
{? _uid_OKRM=''
|| _result:=exec('error','!prc_mcr_domp','Nie znaleziono miesiąca rozliczeniowego.'@)
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uid_OKRM);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uid_OKRM);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('otworz_mc','!prc_mcr_domp',_in.A_OKRM);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value=2
      || _mp.keep()
      || _mp.done()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Otworzenie zamkniętego miesiąca rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_tab:=params_exec('opis_mc','okres',_mp.load(exec('kind_in','#b_port')).A_OKRM);

{? _tab.ZAW_DANE='T'
|| 'Otwórz miesiąc do ponownego rozliczenia %1'@@[date(_tab.R,_tab.M,)$8]
|| 'Otwórz miesiąc do ponownego rozliczenia'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Informacja o rodzaju błędu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Otworzenie miesiąca rozliczeniowego nie jest możliwe.'@,_a]


\opis_mc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Opis wybranego miesiąca rozliczeniowego.
::   WE: _a - [REFERENCE] Wskazanie na wybrany miesiąc rozliczeniowy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mc:='';
A_OKRM.cntx_psh();
{? A_OKRM.seek(_a,,1)
|| _mc:='%1'@[date(A_OKRM.R,A_OKRM.M,)$8]
?};
A_OKRM.cntx_pop();
_mc


\otworz_mc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a - [REFERENCE] Wskazanie na miesiąc rozliczeniowy.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
A_OKRM.cntx_psh();
{? A_OKRM.seek(_a,,1)
|| {? A_OKRM.S='Z'
   || _tab:=tab_tmp(,'RESULT','INTEGER','');
      _ed:=_tab.mk_edit('Otworzenie miesiąca rozliczeniowego %1'@[date(A_OKRM.R,A_OKRM.M,)$8],0);
      _tab.win_efld(_ed,AH,'H',,,,,,'Miesiąc rozliczeniowy %1 zostanie otworzony.'@[date(A_OKRM.R,A_OKRM.M,)$8]);
      exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:Esc'",,
         exec('help_red_zakoncz','#window','PKD_G'),exec('text_red_zakoncz','#window','PKD_G'));
      exec('ok_esc','#window',_tab,_ed,1,,"cur_tab().RESULT:=0; 'key:Esc'",0,,
         exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
      _tab.win_edit(_ed);
      _tab.edit();
      {? _tab.RESULT=1
      || exec('a_okrm_wer_bft','prc_mscrozlicz');
         A_OKRM.seek(_a,,1);
         _result:=A_OKRM.S='O'
      || _result:=2
      ?};
      obj_del(_tab)
   || _result:=exec('error','!prc_mcr_domp','Miesiąc rozliczeniowy nie został jeszcze zamknięty.'@)
   ?}
|| _result:=exec('error','!prc_mcr_domp','Nie znaleziono miesiąca rozliczeniowego.'@)
?};
A_OKRM.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 80a00395c189042769eda3ebb266dd989e4d2e1e271bb50f2b88e92d76122d936e94080695458efd7d03ee1d6a59b3bac8f333a6e0ba85e06c0fb32accbbbf86dfd4cd61d433d4965226542485aa4a69cb6bdf12be013e9faf19db6b12cacc31914d8a1217376f124a96a1c37d22f3097a0c92198504cab0f255baebb7383892

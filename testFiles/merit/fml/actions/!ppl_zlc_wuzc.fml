:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zlc_wuzc.fml
:: Utworzony: 20.09.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności PPL_ZLC_WUZC - Wybór umowy cywilnoprawnej.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wybór umowy cywilnoprawnej - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
:: Parametr wejściowy ON_ESC określa sposób działania przy braku "wyboru" zlecenia.
:: Parametr może przyjmować wartości:
::    DONE     - Czynność zostanie zakończona, a parametry wyjściowe przyjmą wartości puste.
::    KEEP     - Czynność zostanie odłożona na listę zadań aby użytkownik mógł do niej wrócić. Oznacza to, że czynności
::               nie da się zakończyć bez wyboru współpracownika [DOMYŚLNIE].
::    CANCEL   - Jeśli czynność jest pierwszą w procesie to po zakończeniu formuły głównej instancja czynności zostanie
::               usunięta. Jeśli czynność jest kolejną w procesie to otrzyma status oczekująca i pozostanie na liście
::               zadań - czyli tak jak dla KEEP.
::    ERROR    - Czynność jest kończona a proces zatrzymywany z ustawioną flagą błędu.
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy rezygnacji, required=T, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'KEEP' || _a ?})"
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie współpracownika, required=T, keyref=T
::
::# kind=WY, symbol=ZC, type=_ZC, name=Wskazanie umowy cywilnoprawnej, required=T
::
_par:=params_get();
_mp:=_par.mp;

_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error('Nieprawidłowy parametr wejściowy.');
   return()
?};

_in:=_par.in;
_out:=_par.out;

_ret:=exec('select','!ppl_zlc_wuzc',_in.P);
{? _ret.err<>''
:: Coś poszło nie tak ...
|| _mp.error(_ret.err)

|? _ret.ZC=null()
:: Wyjście przez ESC.
|| {? _in.ON_ESC='KEEP'
   || _mp.keep()

   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()

   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')

   || _mp.done()
   ?}

:: Umwa wybrana.
|| _out.ZC:=_ret.ZC;
   _mp.save(,_out);
   _mp.done()
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wybór umowy cywilnoprawnej - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Wybierz umowę cywilnoprawną: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Wybierz umowę cywilnoprawną: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Wybierz umowę cywilnoprawną: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Wybierz umowę cywilnoprawną'@@
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Właściwy wybór umowy cywilnoprawnej.
::   WE: _a [REFERENCE] - Wskazanie pracownika.
::   WY: Tablica elementów nazwanych:
::          err - Opis błędu lub ''.
::          ZC  - Wskazanie wybranej umowy lub null().
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new('err','ZC');
_ret.err:='';
_ret.ZC:=null();

P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a)
|| F_ZATR.cntx_psh();
   {? P.F_ZATR().KOD='Z'
   || __F_ZATR.push();
      __F_ZATR.mod('Z');
      RU.cntx_psh();
      RU.prefix();
      OSOBA.cntx_psh();
      P.OSOBA();
      ZC.cntx_psh();
      ZC.index('ZLECPRAC');
      ZC.prefix(P.ref());
      ZC.win_sel('WYB_Z');
      ZC.win_edit('RED');
      ZC.win_patt('WZO');
      exec('zc_icon','zlec_rh');
      {? ZC.select()
      || _ret.ZC:=ZC.ref()
      ?};
      ZC.cntx_pop();
      OSOBA.cntx_pop();
      RU.cntx_pop();
      __F_ZATR.pop()
   || _ret.err:='Współpracownik nie jest zleceniobiorcą.'
   ?};
   F_ZATR.cntx_pop()
|| _ret.err:='Nie znaleziono współpracownika.'
?};
P.cntx_pop();
_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 16242cb165bd032ab61f3f720388507b4e030b2007b649edb89154474693fa4753b622c9507eeacf94d7ebf10139ce934449dddd88f8a50539b90391277f403f387349f5ab619498965e454f23c7af817873e3766f7fa6f29e053a21a9c97495a552aa03251f65a46a65fccd1a6c704fda9787b44938965c5a978278e0cb3f16

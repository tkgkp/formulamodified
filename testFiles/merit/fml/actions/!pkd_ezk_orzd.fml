:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orzd.fml
:: Utworzony: 11.04.2023
:: Autor: MicKoc
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORZD - Rejestracja pr.zdalnej stałej
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Rejestracja pracy zdalnej stałej - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_ezk_orzd')

|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność '+_mp.buf_act.UID+' nie obsługuje akcji '+_do+'.'
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orzd',_in.P);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Rejestracja pracy zdalnej stałej - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj pracę zdalną stałą: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj pracę zdalną stałą: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj pracę zdalną stałą: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj pracę zdalną stałą'@@
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie pracownika
::   WY:
::  OLD: \ppsfh_select/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!pkd_ezk_orzd');

{? var_pres('_a')<>type_of(null) | _a=null
|| return(_err_msg)
?};

Cntx.psh(P,OSOBA,PPSF_H,PPSFROZD,PPSF_SN,PPSFT,EDOKUM);

P.prefix();
{? ~P.seek(_a)
|| Cntx.pop(P,OSOBA,PPSF_H,PPSFROZD,PPSF_SN,PPSFT,EDOKUM);
   return(_err_msg)
?};

: ustal osobę
P.OSOBA();

: przeglądanie danych
PPSFROZD.win_sel('WER');
PPSF_SN.win_sel('WER');
PPSFT.win_sel('WER');

: pokaż dane
PPSF_H.index('PRAC');
PPSF_H.prefix(_a);
PPSF_H.win_sel('WERI');
PPSF_H.win_edit('RED');
PPSF_H.win_patt('WZO');
_result:=PPSF_H.select();

: porządki
Cntx.pop(P,OSOBA,PPSF_H,PPSFROZD,PPSF_SN,PPSFT,EDOKUM);

_result


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli PPSF_H z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli PPSF_H jako składowa okna grupowego tabeli UD_DEF).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=PPSF_H
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie pracy zdalnej stałej niemożliwe.\nNie znaleziono pracownika.'


\ppsf_h_okienko_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Akcja okienko WER przed tabeli PPSF_H
::   WE:
::   WY:
::  OLD: \ppsfh_okienko_b/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_h_okienko_b','ppsf')


\ppsf_h_okienko_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Akcja okienko WER po tabeli PPSF_H
::   WE:
::   WY:
::  OLD: \ppsfh_okienko_a/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_h_okienko_a','ppsf')


\ppsf_h_ppsf_sn_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Formuła "Wartość początkowa" pola PPSF_H.PPSF_SN
::   WE:
::   WY:
::  OLD: \ppsf_h_ppsf_sn_bl/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
PPSF_SN.cntx_psh();
PPSF_SN.index('DOM');
PPSF_SN.prefix(exec('firma','ustawienia'),'T',);
:: Jeśli jest dokładnie jeden domyślny sposób rozliczenia to ustaw go jako wartość początkową pola:
{? PPSF_SN.first() & PPSF_SN.size()=1
|| _ref:=PPSF_SN.ref()
?};
PPSF_SN.cntx_pop();
_ref


\ppsf_h_wyc_data_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Przed redakcją pola PPSF_H.WYC_DATA
::  OLD: \ppsf_h_wyc_data_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_H.WYC='T'


\ppsf_h_wyc_data_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Przed wyświetleniem pola PPSF_H.WYC_DATA
::  OLD: \ppsf_h_wyc_data_bd/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_H.WYC='T'


\ppsf_h_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Rekord po tabeli PPSF_H
::   WE:
::   WY:
::  OLD: \ppsfh_ra/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_popraw:=-menu_txt()='popraw';

_chk:=exec('ppsfh_chk','ppsf',_popraw);
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & _chk=0)
|| return(_chk)
?};

_d0:=date(0,0,0);
_p_dza:=PPSF_H.P().DZA;
_p_dz:=P.DZ;

{? PPSF_H.OD<_p_dza
|| {? ~FUN.ask('Data "%1" wykracza poza okres zatrudnienia pracownika (od %2 do %3).\n'
               [MS.name(PPSF_H,'OD'),$_p_dza,$_p_dz]+
               'Czy chcesz kontynuować?')
   || return('OD')
   ?}
?};
{? _p_dz<>_d0 & (PPSF_H.DO=_d0 | PPSF_H.DO>_p_dz)
|| {? ~FUN.ask('Data "%1" wykracza poza okres zatrudnienia pracownika (od %2 do %3).\n'
               [MS.name(PPSF_H,'DO'),$_p_dza,$_p_dz]+
               'Czy chcesz kontynuować?')
   || return('DO')
   ?}
?};

:: Sprawdzanie nakładania się z innymi wnioskami o pracę zdalną okazjonalną
_firma:=exec('ref_firma','#firma');
_ppsfr:=exec('FindInSet','#table','PPSFR','KOD','ZDALN_OK',_firma,,1,,null());
_wn_ok:=exec('ppsf_wnio_exists','ppsf',PPSF_H.P,PPSF_H.OD,PPSF_H.DO,,1,_ppsfr);
{? _wn_ok.REF<>null()
|| _txt:='W podanym zakresie dat istnieje wniosek o pracę zdalną okazjonalną.'@;
   {? ~FUN.ask('%1\n%2'[_txt,'Czy kontynuować?'@])
   || return('DO')
   ?}
?};

_refCur:={? _popraw || PPSF_H.ref() || null() ?};
_odCur:=PPSF_H.OD;
_doCur:={? PPSF_H.DO=_d0
        || date(2099,12,0)
        || {? PPSF_H.WYC_DATA<>_d0 || PPSF_H.WYC_DATA || PPSF_H.DO ?}
        ?};
PPSF_H.cntx_psh();
PPSF_H.index('PRAC');
PPSF_H.prefix(P.ref());
{? PPSF_H.first()
|| {! |?
      {? _refCur<>PPSF_H.ref()
      || {? _doCur<PPSF_H.OD |
            _odCur>{? PPSF_H.DO=_d0 || date(2099,12,0) || {? PPSF_H.WYC_DATA<>_d0 || PPSF_H.WYC_DATA || PPSF_H.DO ?} ?}
         || _loop:=PPSF_H.next()
         || _info:='Istnieje już zapis dotyczący pracy zdalnej w okresie od '+$PPSF_H.OD+' do '+$PPSF_H.DO+
                   {? PPSF_H.WYC='T' || '\nz datą wycofania: %1'[$PPSF_H.WYC_DATA] || '' ?}+'.';
            {? PPSF_H.DO=_d0 & _odCur>PPSF_H.OD
            || {? FUN.ask(_info+'\n'+'Czy skrócić istniejący zapis do daty '+$(_odCur-1)+
                          ', aby umożliwić wprowadzenie nowych danych?')
               || PPSF_H.DO:=_odCur-1;
                  PPSF_H.put()
               || _chk:='OD'
               ?}
            || FUN.emsg(_info);
               _chk:='OD'
            ?};
            _loop:=0
         ?}
      || _loop:=PPSF_H.next()
      ?};
      _loop
   !}
?};
PPSF_H.cntx_pop();

:: zerowanie daty wycofania jeżeli zapis nie jest wycofany
{? ((type_of(_chk)=type_of('') & _chk='') | (type_of(_chk)=type_of(0) & _chk>0)) & PPSF_H.WYC<>'T'
|| PPSF_H.WYC_DATA:=_d0
?};
_chk


\_ppsf_h_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Wyzwalacz usuń przed tabeli PPSF_H
::   WE:
::   WY:
::  OLD: \_ppsfh_delb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',PPSF_HD,'UNIQ_DZ',PPSF_H.ref()) &
exec('del_ndx','#table',ZALACZ,'NAG',PPSF_H.uidref())


\ppsf_h_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Rekord przed tabeli PPSF_H
::   WE:
::   WY:
::  OLD: \ppsfh_rb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
:: Osoba weryfikująca wniosek.
exec('ppsf_h_pola_grp_txt_1','ppsf');
:: Ustawienie maski EDOKUM
exec('ppsf_h_edokum_mask','ppsf');
''


\ppsf_h_wyc_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Po redakcji pola PPSF_H.WYC
::  OLD: \ppsf_h_wyc_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_H.WYC_DATA:={? PPSF_H.WYC='T' || date() || #0 ?};
win_disp()


\ppsf_h_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Obsługa akcji "Załączniki - przed" w oknach tabeli PPSF_H.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','P','PPSF_H',exec('chk_role','#b__box',OPERATOR.USER,'PKD_EZK_ORZD'))

:Sign Version 2.0 jowisz:1045 2023/09/29 08:41:37 c342ff2ca0b9d5be8921738a61b50e99fbc2bb199cca0963cb9a2281581b78c18ad71b024f51fea3ac59251c7d95dfcf82a5c4a52baec15fa3a24319eed90224e2cbb4d3ba3c229d6dd00db72d1c49892f77d1812dae5d68e38fe16cbd6ac49b5ddc96108bb3a30f1409dc7de719def05547c1466cd3dd4935b55a29724d2028

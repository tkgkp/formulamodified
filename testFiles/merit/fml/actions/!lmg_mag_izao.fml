:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_izao.fml
:: Utworzony: 14.04.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Zamknięcie okresu - Magazyn
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=ROK,       type=NUMBER,   name=Rok,               required=N, fml_val="exec('rok','!lmg_mag_izao')"
::# kind=WE,   symbol=MIESIAC,   type=NUMBER,   name=Miesiąc,           required=N, fml_val="exec('miesiac','!lmg_mag_izao')"
::# kind=WY,   symbol=WYNIK,     type=MEMO,     name=Wynik zamknięcia,  required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: Uruchomiona akcja
_akcja:=_mp.akcja();

:: Uruchomiona automatycznie
_auto:=_akcja<>'Zamknij' & _mp.isAutoRun();

_rok:={? var_press('ROK',_in)=type_of(0) & _in.ROK || _in.ROK || ST.AR ?};
_miesiac:={? var_press('MIESIAC',_in)=type_of(0) & _in.MIESIAC || _in.MIESIAC || ST.AM ?};

_out.WYNIK:='';

OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_rok,_miesiac);
{? OKR.first()
|| _zam_mag:=1+OKR.ZAM;
   _zam_spr:=1+(1-OKR.ZAM);
   _zam_zak:=1+(2-OKR.ZAM);
   _zam_umo:=OKR.ZAM+1;
   _Tab:=sql($"
      select
         ND.ODDZ ODDZ,
         KH.KOD KH,
         ND.SYM SYM,
         MG.SYM MG
      from
         @ND
         left join KH using(ND.KH,KH.REFERENCE)
         join MG using(ND.MAG,MG.REFERENCE)
      where
         ND.D>=to_date(':_a') and ND.D<=to_date(':_b')
         and ND.STAT_REJ<>'T'
         and :_c=1"
      ,$date(OKR.ROK,OKR.MC,1),$date(OKR.ROK,OKR.MC,0),_zam_mag='N');
   {? _Tab.first()
   || _txt:='Niezaakceptowane dokumenty magazynowe'@;
      _out.WYNIK+=_txt+':\n';
      _loop:=_Tab.first();
      {!
      |? _loop
      |!
         _out.WYNIK+='\n'+_Tab.SYM;
         _loop:=_Tab.next()
      !};
      _wer:=_Tab.mk_sel(_txt,,0,'okr_zam_mag');
      _Tab.win_fld(_wer,,'ODDZ',,,10,,,'Oddział'@);
      _Tab.win_fld(_wer,,'KH',,,10,,,'Kontrahent'@);
      _Tab.win_fld(_wer,,'SYM',,,20,,,'Symbol'@);
      _Tab.win_fld(_wer,,'MG',,,20,,,'Magazyn'@);
      _Tab.win_act(_wer,,'Formuła','Zamknij okno'@@,,,"sel_exit()",,1);
      _Tab.win_sel(_wer);
      _Tab.select()
   ?};
   _okr_naz:=OKR.NAZ;
   {? _zam_mag='T'
   || FUN.info('Okres jest już zamknięty.'@);
      _mp.done()
   |? _choice:=FUN.choice('Zamknąć okres %1?'@[_okr_naz],,'Tak'@,'Częściowo'@)
   || OKR.get();
      {? _choice=2
      || exec('zam_dok','lmg')
      || OKR.ZAM:='T'+_zam_spr+_zam_zak+_zam_umo
      ?};
      {? OKR.put()
      || _out.WYNIK+={? +_out.WYNIK || '\n' || '' ?}+'Okres %1 został zamknięty.'@[_okr_naz];
         _mp.done()
      ?}
   ?}
|| _out.WYNIK+='Brak okresu %1/%2'+@[form(_miesiac,-2,,'9 '),form(_rok,-4,,'9 ')];
   _mp.done()
?};
OKR.cntx_pop();
_mp.save(,_out)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

_rok:={? var_press('ROK',_in)=type_of(0) & _in.ROK || _in.ROK || 0 ?};
_miesiac:={? var_press('MIESIAC',_in)=type_of(0) & _in.MIESIAC || _in.MIESIAC || 0 ?};

OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_rok,_miesiac);
_wyn:=
   {? OKR.first()
   || 'Zamknij okres %1 w magazynie'@@[OKR.NAZ]
   || 'Zamknij okres w magazynie'@@
   ?};
OKR.cntx_pop();
_wyn


\zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zamknięcie okresu - Magazyn
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LMG_MAG_IZAO';
_params.AKCJA:='Zamknij';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);

OKR.cntx_psh();
OKR.prefix();
{? OKR.seek(exec('zwr_obs_okres','parses','LMG',OKRO.ref()))
||
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ROK',OKR.ROK);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'MIESIAC',OKR.MC);

   exec('mp_run','#b__box',_params)
|| FUN.info('Okres nie jest otworzony dla magazynów.'@)
?};
OKR.cntx_pop()


\rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła parametru - ROK
::   WE:
::   WY: rok lub ~~ jeśl nie wybrano roku
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
OKR.last();
_wer:=OKR.mk_sel('Wybór roku'@,,0,,,,OKR.size());
OKR.win_fld(_wer,,'ROK',,,20,,,'Rok'@);
OKR.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
OKR.win_sel(_wer);
{? OKR.select(,1,OKR.size()) || _wyn:=OKR.ROK ?};
OKR.cntx_pop();
_wyn


\miesiac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła parametru - OKRES
::   WE:
::   WY: miesiąc lub ~~ jeśl nie wybrano roku
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
_Tab:=tab_tmp(1
   ,'MC'    ,'INTEGER'     ,'Nr miesiąca'
   ,'NAZ'   ,'STRING[20]'  ,'Nazwa miesiąca');
{! _ii:=1..12 |! _Tab.MC:=_ii; _Tab.NAZ:=(date(,_ii)$8)-5; _Tab.add() !};
_Tab.find_key(date()~2);
_wer:=_Tab.mk_sel('Wybór miesiąca'@,,0,,,,12);
_Tab.win_fld(_wer,,'NAZ',,,,,,'Miesiąc'@);
_Tab.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_Tab.win_sel(_wer);
{? _Tab.select(,1,12) || _wyn:=_Tab.MC ?};
_wyn


\dol_okrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: czesciowe zamykanie okresu rozliczeniowego - dolacz
::  OLD: \dol_okrd/defin.fml
::----------------------------------------------------------------------------------------------------------------------
exec('edt_okrd','!lmg_mag_izao','add')


\edt_okrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: czesciowe zamykanie okresu rozliczeniowego - edit
::   WE: _a - operacja do wykonania add, put
::  OLD: \edt_okrd/defin.fml
::----------------------------------------------------------------------------------------------------------------------
OKRD.win_edit('RED');
{? _a='add'
||
   OKRD.blank();
   OKRD.ODDZ:=exec('FindInSet','#table','ODDZ','KOD',ST.ODDZ_KOD)
?};
{? OKRD.edit("exec('chk_okrd','!lmg_mag_izao')")
||
   OKRD.ODDZ:=exec('FindInSet','#table','ODDZ','KOD',ST.ODDZ_KOD);
   OKRD.MG:={? ST.MAG_SYM<>'' || exec('FindInSet','#table','MG','MAGAZYNY',ST.MAG_SYM) || null ?};
   ($('OKRD.'+_a))()
?}


\chk_okrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord po dla OKRD
::  OLD: \chk_okrd/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? ST.MAG_SYM=''
|| FUN.info('Należy podać magazyn.'@);
   _wyn:='MAG_SYM'
?};
{? _wyn='' & OKRD.M='N' & OKRD.D='N' & OKRD.Z='N'
|| FUN.info('Wymagany wybór przynajmniej jednej blokady modyfikacji dokumentów.'@);
   _wyn:='M'
?};
{? _wyn=''
||
   _okr:=OKRD.OKR;
   _oddz:=OKRD.ODDZ;
   _sym:=ST.MAG_SYM;
   _ref:=OKRD.ref();
   OKRD.cntx_psh();
   _ndx:=OKRD.ndx_tmp(,,'OKR',,,'ODDZ',,,'MG','SYM',,'MG','SYM',);
   OKRD.index(_ndx);
   OKRD.prefix(_okr,_oddz,_sym,_sym);
   {? OKRD.first()
   ||
      {!
      |?
         {? OKRD.ref()<>_ref | -(1+menu_txt)='d'
         || FUN.info('Istnieje już zapis dotyczący magazynu %1.'@[_sym]); _wyn:='MAG_SYM'
         ?};
         _wyn='' & OKRD.next()
      !}
   ?};
   OKRD.ndx_drop(_ndx);
   OKRD.cntx_pop()
?};
_wyn


\pop_okrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: czesciowe zamykanie okresu rozliczeniowego - po popraw
::       przypisanie wartosci na podstawie zmiennej ST
::  OLD: \pop_okrd/defin.fml
::----------------------------------------------------------------------------------------------------------------------
exec('edt_okrd','!lmg_mag_izao','put')


\rek_okrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord przed dla OKRD
::  OLD: \rek_okrd/defin.fml
::----------------------------------------------------------------------------------------------------------------------
ST.ODDZ_KOD:=OKRD.ODDZ().KOD;
ST.MAG_SYM:=OKRD.MG().SYM;
0


\okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: OKR.ref
::  OLD: \okr/defin.fml
::----------------------------------------------------------------------------------------------------------------------
OKR.ref


\f3_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: wyswietla uprawnione oddzialy
::  OLD: \f3_oddz/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('sel_upr','users','ODDZ','KOD')
|| ST.ODDZ_KOD:=USERS_UP.ODDZ().KOD;
   {? exec('ae_oddz','!lmg_mag_izao')
   || ST.ODDZ_KOD
   ?}
?}


\ae_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po redakcji oddzialu - sprawdza zgodnosc z uprawnieniami
::   WY: 1/0
::  OLD: \ae_oddz/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? ST.ODDZ_KOD<>''
||
   ST.ODDZ_REF:=exec('FindInSet','#table','ODDZ','KOD',ST.ODDZ_KOD);
   {? exec('spr_upr','users','ODDZ',ST.ODDZ_REF)
   ||
      {? cur_tab.name()='okruzy'
      || {? ST.ODDZ_KOD<>ST.ODDZ || ST.ODDZ:=ST.ODDZ_KOD;ST.STS_KOD:=ST.MAG_SYM:=ST.STAT:=ST.STZ_KOD:='' ?}
      |? cur_tab.name()='okresyd'
      || OKRD.ODDZ:=ST.ODDZ_REF
      ?};
      _wyn:=1
   ?}
|| FUN.info('Niewypełnione pole.'@)
?};
_wyn

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:57 30f4792e46037897239f243aedfa77c40593ae44e1b14af75358295287e05f02ad20421a38f982f1e6e68cac648bd40dca96672750acb548934cad987840f2687d8fedd9503c6c1c2abcbd36c50348eef89e9c0e22e7fef1fbcc9c1eeb96fccc553626cbf76a132c03abc74b92bf8ad4660fe8b366f60e59e8bed8b272d6bd43

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_dlan.fml
:: Utworzony: 24.07.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Obsługa czynności PBA_BPB_DLAN - redakcja listy ankiet.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Definiowanie ankiet - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: aktywacja czynności
:: konfiguruje ankiety w zależności od tego czy wywołujemy dla badań jednorazowych czy cyklicznych
{? cur_tab(1,1)=ZA_INST
|| exec('za_zest_def_act','phr_widok',ZA_INST)
|? cur_tab(1,1)=ZO_PROC
|| exec('za_zest_def_act','phr_widok',ZO_PROC)
|? cur_tab(1,1)=SZK_OPIS
|| exec('za_zest_def_act','phr_widok',SZK_OPIS)
?};
~~


\za_zest_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli ZA_ZEST.
::   WE:
::   WY: akronim pola do redakcji
::----------------------------------------------------------------------------------------------------------------------
{? ZA_ZEST.TYP().KOD='T' & -menu_txt='popraw'
|| _klasa:=ZA_ZEST.ZZ_LINK().KLASA;
   {? (ZA_ZEST.PROG<=0 | ZA_ZEST.PROG>100) & {? _klasa='SZK_OPIS' || SZK_OPIS.TRYB='T' || 1 ?}
   || FUN.info('Wprowadzono błędną wartość progu zdawalności. Dopuszczalna wartość mieści się w granicy 1-100.'@);
      return('PROG')
   |? ZA_ZEST.PROG<=0 & _klasa='SZK_OPIS' & SZK_OPIS.TRYB='N'
   || FUN.info('Wprowadzono błędną wartość progu zdawalności. Dopuszczalna wartość powinna być większa niż zero.'@);
      return('PROG')
   || 1
   ?}
|| 1
?};
exec('za_zest_chk','phr_za_tab',-menu_txt()='popraw')


\za_zest_form_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji 'Formularz' dla tabeli ZA_ZEST
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('report','phr_kreatory',ZA_ZEST,'pba_szkoank','form_ank.pdf','Formularz ankiety',
   exec('print_preview','phr_kreatory'),'FORM-1'
)


\za_zest_szablony_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
::  MOD: RO [19.22]
:: OPIS: Obsługa akcji 'Szablony' dla tabeli ZA_ZEST
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tmp_ref:=null();
_tmp_dok:=null();
_tmp_naz:='';
_tmp_rodz:=null();
_tmp_prog:=0;
_tmp_pkt:='';
_tmp_tryb:='';
_tmp_typ:=null;
ZA_TEST.cntx_psh();
ZA_SKL.cntx_psh();
ZA_ZEST.cntx_psh();
ZA_ZEST.win_sel('WYB_S');
ZA_ZEST.hdr_sel('Szablony ankiet'@);
ZA_ZEST.prefix(ZZ_TMPT.NP_DOK,ZZ_TMPT.ZZ_DOK,exec('ref_firma','ustawienia'));
_old:=ZA_ZEST.fld_fml('ZZ_LINK','BLANK',"ZZ_TMPT.ZZ_DOK");
{? ZA_ZEST.select()
|| _tmp_ref:=ZA_ZEST.ref();
   _tmp_dok:=ZA_ZEST.ZZ_DOK;
   _tmp_naz:=ZA_ZEST.NAZWA;
   _tmp_met:=ZA_ZEST.ZZ_MET;
   _tmp_typ:=ZA_ZEST.TYP;
   _tmp_rodz:=ZA_ZEST.ZA_TYP;
   _tmp_ano:=ZA_ZEST.ANONIM;
   _tmp_gen:=ZA_ZEST.DOBOR;
   _tmp_prog:=ZA_ZEST.PROG;
   _tmp_pkt:=ZA_ZEST.PKT;
   _tmp_tryb:=ZA_ZEST.TRYB
?};
ZA_ZEST.fld_fml('ZZ_LINK','BLANK',_old);
ZA_ZEST.cntx_pop();
ZA_SKL.cntx_pop();
ZA_TEST.cntx_pop();
{? ZZ_TMPT.SYMBOL='SZK_OPIS' & _tmp_typ=exec('kod','ext_slo','ZA_ZEST','T')
|| _test:=sql(
      'select SLO_KOD.KOD from ZA_ZEST join '
      'SLO_KOD using(ZA_ZEST.TYP,SLO_KOD.REFERENCE) '
      'where SLO_KOD.KOD=\'T\' and ZA_ZEST.ZZ_LINK=\':_a\'',$SZK_OPIS.ZZ_DOK);
   {? _test.first()
   || FUN.info('Dla szkolenia został już zdefiniowany egzamin. Dołączenie następnego testu niemożliwe.');
      return(0)
   ?}
?};
{? ZZ_POM.WIDOK='Próg (%)' & _tmp_tryb='N' & _tmp_typ=exec('kod','ext_slo','ZA_ZEST','T')
|| FUN.info('Wybrano szablon testu z progiem zdawalności liczonym w punktach.\n'
            'Wyniki egzaminu dla wybranego szkolenia liczone są w procentach.\n'
            'Proszę wybrać właściwy test.'@);
   return(0)
|? ZZ_POM.WIDOK='Próg (pkt)' & _tmp_tryb='T' & _tmp_typ=exec('kod','ext_slo','ZA_ZEST','T')
|| FUN.info('Wybrano szablon testu z progiem zdawalności liczonym w procentach.\n'
            'Wyniki egzaminu dla wybranego szkolenia liczone są w punktach.\n'
            'Proszę wybrać właściwy test.'@);
   return(0)
?};
{? _tmp_ref
|| _do:=
      {? ZA_ZEST.size()
      || {? ZA_ZEST.get()
         || {? ZA_ZEST.SLO_KOD().KOD='U'
      || FUN.choice(
            'Na podstawie definicji szablonu "%1".'@[_tmp_naz],,
                  'Utwórz ankietę'@)
            || {? ZA_ZEST.TYP=_tmp_typ
               || FUN.choice(
                     'Na podstawie definicji szablonu "%1".'@[_tmp_naz],,
            'Utwórz ankietę','Aktualizuj ankietę'@)
               || FUN.choice(
                     'Na podstawie definicji szablonu "%1".'@[_tmp_naz],,
                     'Utwórz ankietę'@)
               ?}
            ?}
         ?}
      || 1
      ?};
   {? _do=1
   || ZA_ZEST.blank();
      ZA_ZEST.NAZWA:=exec('ank_nazwa','phr_dane',ZA_ZEST.ZZ_LINK,_tmp_naz);
      ZA_ZEST.ZZ_MET:=_tmp_met;
      ZA_ZEST.ZA_TYP:=_tmp_rodz;
      ZA_ZEST.TYP:=_tmp_typ;
      ZA_ZEST.ANONIM:=_tmp_ano;
      ZA_ZEST.DOBOR:=_tmp_gen;
      ZA_ZEST.PROG:=_tmp_prog;
      ZA_ZEST.PKT:=_tmp_pkt;
      _do:=ZA_ZEST.add()
   ?};
   {? _do
   || exec('ank_kop_zes','phr_dane',_tmp_ref,_tmp_dok,ZA_ZEST.ref(),ZA_ZEST.ZZ_DOK)
   ?}
?}


\za_typ_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli ZA_TYP.
::   WY: akronim pola do redakcji
::----------------------------------------------------------------------------------------------------------------------
exec('za_typ_chk','phr_za_tab',-menu_txt()='popraw');
_system:=ZA_TYP.SYSTEM;
ZA_TYP.cntx_psh();
ZA_TYP.index('OPIS');
ZA_TYP.prefix(ZA_TYP.ref());
_loop:=ZA_TYP.first();
{!
|? _loop
|! ZA_TYP.SYSTEM:=_system;
   ZA_TYP.put();
   _loop:=ZA_TYP.next()
!};
ZA_TYP.cntx_pop()


\za_skl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli ZA_SKL.
::   WY: akronim pola do redakcji
::----------------------------------------------------------------------------------------------------------------------
exec('za_skl_chk','phr_za_tab',-menu_txt()='popraw')


\za_typ_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [12.51]
:: OPIS: formuła "Rekord - przed" dla tabeli ZA_TYP. Sprawdzenie czy zapis systemowy.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| {? ZA_TYP.SYSTEM='T'
   || ZA_TYP.actions_grayed('WER','PU:')
   || ZA_TYP.actions_grayed('WER',':')
   ?}
?};
~~


:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:58 314137ac740188456bbc44bbd356a7c64141b4d814ad99f923b67c5977e805da757e6e723c81da62a03cc0eb85a0ca668d4ca5d83ae856f0fa704dfb8f34ce413afe8f975203e205cc1d43708ece61c1f0edf05c0f830aa655d468d5094d05b79da0bc8cf4a978937b4fbf178ccf7e4411a0a41d95ac99b0b5438cd3480edaf0

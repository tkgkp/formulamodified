:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_hbd_dzaa.fml
:: Utworzony: 09.12.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_HBD_DZAA - Uzupełnienie arkuszy zadań budżetowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uzupełnienie arkuszy zadań budżetowych - główna formuła czynności CTR_HBD_DZAA.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::# access=exec('access','ctr_bha','A')
:: PARAMETRY WE:
::# kind=WE, symbol=K_HARM_P, type=_K_HARM_P, name=Zadanie budżetowe, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=K_HARM_P, type=_K_HARM_P, name=Zadanie budżetowe, required=T
::# kind=WY, symbol=DO_AKC, type=STRING, name=Czy kolejna czynność to akceptacja, required=T
:: WARUNKI BRAMY
::# condition=Akceptacja arkuszy, act_uid=CTR_HBD_DZBA, auto=T, formula=_a.K_HARM_P<>null & _a.DO_AKC='T'
::# condition=Generowanie zadań, act_uid=CTR_HBD_DZCA, auto=T, formula=_a.K_HARM_P<>null & _a.DO_AKC='N'
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
K_HARM_P.cntx_psh();
K_HARM_P.prefix();
_ref:=_mp.getRefs();
{? K_HARM_P.seek(_ref[1])
|| _wy.K_HARM_P:=K_HARM_P.ref();
   _wy.DO_AKC:={? K_HARM_P.STATUS='U' || 'T' || 'N' ?};
   _mp.save(,_wy);
   {? _akcja='Zakończ'
   || {? K_HARM_P.STATUS='U' | K_HARM_P.STATUS='A'
      || _mp.done()
      ?}
   |? K_HARM_P.STATUS='W'
   || exec('ed_xlsx','control',0);
      {? K_HARM_P.K_HARM().ARKUSZ='G' & K_HARM_P.get() & (K_HARM_P.STATUS='U' | K_HARM_P.STATUS='A')
      || _mp.done()
      ?}
   || FUN.info('Status zadania \'%1\'.\nEdycja arkusza niedozwolona.'@[K_HARM_P.STAT_NAZ]);
      _mp.error('Nieoczekiwany status zadania budżetowego do uzupełnienia: '+K_HARM_P.STATUS+'.')
   ?}
|| FUN.info('Nie znaleziono zadania.'@);
   _wy.K_HARM_P:=null;
   _mp.save(,_wy);
   _mp.done()
?};
K_HARM_P.cntx_pop();
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis na liście TODO czynności
::----------------------------------------------------------------------------------------------------------------------
_desc:='Uzupełnij zadanie budżetowe'@@;
_mp:=params_get().mp;
_refs:=_mp.getRefs();
{? var_press('_refs')>0 & obj_len(_refs)>0
|| _ref:=_refs[1];
   {? var_press('_ref')>0
   || K_HARM_P.cntx_psh(); K_HARM_P.prefix();
      {? K_HARM_P.seek(_ref)
      || {? K_HARM_P.ZN_RET='T'
         || _desc:='Uzupełnij odrzucone zadanie budżetowe: %1'@@[exec('record','#to_string',K_HARM_P.ref())]
         || _desc:='Uzupełnij zadanie budżetowe: %1'@@[exec('record','#to_string',K_HARM_P.ref())]
         ?}
      ?};
      K_HARM_P.cntx_pop()
   ?}
?};
_desc


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z ToDO z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::       _b - B_PREL.ref() lub BI_PREL.ref() lub exec('mp_run_a','#b__box')
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
{? _path='Todo'
|| _path_param:=_b;
   _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   _prel:=_path_param;
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || exec('get_xlsx_www','control',_keyrefs[1],'A')
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:11 0dc63167c4567ff3722428c42de8d25fa5a34081587c2fa9da31a1a475d9ebdcec75696d3ebf529e3450c63b4977c4717c013fe1639c09f23b34f81636f58e3773c4fdb70834ce87754abede4442989f3a430d9085a2bf9db08deaff305f795fe2bbe0d097af2054ab3848feddae60f5bb51b76d3173faff3da09602087d06e5

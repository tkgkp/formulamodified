:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_faw_pzal.fml
:: Utworzony: 24.06.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_PZAL - Utworzenie zaliczki z wniosku
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Utworzenie zaliczki z wniosku - główna formuła czynności OBE_FAW_PZAL
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_edokum','obiegi')

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N, keyref=T
::# kind=WE, symbol=TYP, type=STRING, name=Utworzenie zaliczki/Usunięcie zaliczki, required=N, fml_val="exec('typ','!obe_faw_pzal',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=T
::# kind=WY, symbol=EZAL, type=_EZAL, name=Wskazanie na zaliczkę, required=N
::# kind=WY, symbol=PRZELEW, type=STRING, name=Zaliczka realizowana przelewem, required=N

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE
exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_ref:=null;
_zal:=null;
_add:=var_press('TYP',_we)<=0 | _we.TYP='Utworzenie zaliczki';
{? var_press('EDOKUM',_we)>0
|| EDOKUM.cntx_psh();
   _name:=ref_name(_we.EDOKUM);
   EDOKUM.use(_name);
   EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM)
   || _ref:=EDOKUM.ref();
      {? _add
      || _zal:=exec('add','!obe_faw_pzal')
      || _zal:=exec('del','!obe_faw_pzal')
      ?}
   ?};
   EDOKUM.cntx_pop()
?};
_wy.EDOKUM:=_ref;
_wy.EZAL:=_zal;
_wy.PRZELEW:={? _zal<>null & EZAL.KASPRZEL='P' || 'T' || 'N' ?};
_mp.save(,_wy);
_mp.done();
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_press('TYP',_we)>0
|| _desc:='Utworzenie zaliczki z %1'@@[_we.TYP]
|| _desc:='Utworzenie zaliczki'@@
?};
{? var_pres('EDOKUM',_we)>0 & _we.EDOKUM
|| EDOKUM.cntx_psh(); EDOKUM.use(ref_name(_we.EDOKUM)); EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM,)
   || {? var_press('TYP',_we)>0
      || _desc:='Utworzenie zaliczki z %1 %2: %3, data operacji %4'@@[_we.TYP,(-EDOKUM.TYP().NAZWA),EDOKUM.ID,$EDOKUM.DOP]
      || _desc:='Utworzenie zaliczki z %1: %2, data operacji %3'@@[(-EDOKUM.TYP().NAZWA),EDOKUM.ID,$EDOKUM.DOP]
      ?}
   ?};
   EDOKUM.cntx_pop()
?};
_desc


\typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia parametr wejsciowy TYP czynności
::   WE: _a - wartość bieżaca pola
::----------------------------------------------------------------------------------------------------------------------
_nr:={? _a='Utworzenie zaliczki' || 1 || 2 ?};
_pyt:=FUN.choice('Typ operacji'@,_nr,'Utworzenie zaliczki'@,'Usunięcie zaliczki'@);
{? _pyt=1
|| 'Utworzenie zaliczki'
|? _pyt=2
|| 'Usunięcie zaliczki'
|| ~~
?}


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Utworzenie zaliczki z dokumentu w obiegu
::   WY: Wskazanie na zaliczkę
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
{? EDOKUM.TYP().CZY_ZALP='T'
|| EZAL.index('EDOKUM'); EZAL.prefix(EDOKUM.ref());
   {? ~EZAL.first()
   || _sym:=_sym0:={? EDOKUM.TYP().ETYPZAL().AUT_SYM='T' || ($ETYPZAL.FORMSYMZ)() || EDOKUM.ID ?};
      _nr:=1;
      EZAL.index('UNIK'); EZAL.prefix(EDOKUM.FIRMA,EDOKUM.DOSTAWCA);
      {!
      |? {? EZAL.find_key(_sym,)
         || _sym:=_sym0+$_nr;
            _nr+=1;
            1
         ?}
      !};
      EZAL.prefix();
      EZAL.blank(1);
      EZAL.EDOKUM:=EDOKUM.ref();
      EZAL.FIRMA:=EDOKUM.FIRMA;
      EZAL.ETYPZAL:=EDOKUM.TYP().ETYPZAL;
      EZAL.DATA_WY:=date();
      EZAL.ZAL_DLA:=EDOKUM.DOSTAWCA;
      EZAL.SYM:=_sym;
      EZAL.TP_PLAN:=EDOKUM.TP;
      EZAL.TRESC:=EDOKUM.TR;
      EZAL.WAL:=EDOKUM.WAL;
      EZAL.KW_PROP:=EDOKUM.WART;
      EZAL.KASPRZEL:=EDOKUM.KASPRZEL;
      EZAL.KASA:={? EZAL.KASPRZEL='K' || 'T' || 'N' ?};
      EZAL.PRZELEW:={? EZAL.KASPRZEL='P' || 'T' || 'N' ?};
      EZAL.N:=EDOKUM.N;
      EZAL.PLACE:=EDOKUM.PLACE;
      EZAL.POTRODDN:=EDOKUM.POTRODDN;
      EZAL.OBIEGI:='T';
      EZAL.ZAKCREJ:='T';
      EZAL.ZAM:='N';
      EZAL.PRZ_P:='N';
      {? EZAL.add(1)
      || _txt:=EDOKUM.memo_txt(0,1,'UW_OPDL');
         {? _txt<>''
         || EZAL.memo_set(_txt,'OPIS');
            EZAL.memo_put(,'OPIS')
         ?};
         _ref:=EZAL.ref()
      ?}
   || _ref:=EZAL.ref()
   ?}
?};
_ref


\del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Usuniecie zaliczki z wniosku w obiegu
::   WE: Wskazanie na zaliczkę lub null gdy usunieto lub nie było zaliczki
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
EZAL.index('EDOKUM'); EZAL.prefix(EDOKUM.ref());
{? EZAL.first()
|| _ref:=EZAL.ref();
   {? EZAL.count()=0
   || {? EZAL.del(,1)
      || _ref:=null
      ?}
   ?}
?};
_ref

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 22aa9aff1d8e9baad2db9dc623c9b6e08116313812c35d1100c3e3969f7e9c0f8c8174949c136b38e7f843e9777c78c54dbd73d38a3f110e07fd4142d8bd1e1292c358fabb393df41617e408476006fdb77d512a4c5f6f4f424ec133e0e5fde227c56679479ab7dbf872f51f749fe08082dbac97825f47f36c722314e8a8d321

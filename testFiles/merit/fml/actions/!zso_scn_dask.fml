:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zso_scn_dask.fml
:: Utworzony: 06.02.2020
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZSO_SCN_DASK - Zeskanowanie dokumentów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła główna czynności ZSO_SCN_DASK
::----------------------------------------------------------------------------------------------------------------------
::# properties=LOOP,RUNMICRO

:: PARAMETRY WE:
::# kind=WE, symbol=TYP, type=STRING, name=Typ dokumentu, required=N, fml_val="exec('typ','skanuj',_a)"
:: PARAMETRY WE:
::# kind=WE, symbol=TM_STAMP, type=STRING, name=Znacznik czasowy, required=N

:: PARAMETRY WY:
::# kind=WY, symbol=SKAN_DOK, type=_SKAN_DOK, name=Wskazanie na zeskanowany dokument, required=N
::# kind=WY, symbol=TM_STAMP, type=STRING, name=Znacznik czasowy, required=N
exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;

{? var_press('TM_STAMP',_we)>0
|| _tm:=_we.TM_STAMP;
   {? (_ok:=exec('first','!zso_scn_dask',_tm,_wy))
   || _wy.TM_STAMP:=_tm;
      _mp.save(,_wy);
      {? _ok=2
      || _mp.loop_continue()
      ?}
   ?};
   _mp.done()
|| _tm:=tm_stamp();
   _typ:={? var_pres('_we')>0 & var_pres('TYP',_we)>0 || _we.TYP || '' ?};
   {? exec('cli_functions','#system')=0
      || FUN.emsg(exec('indevice_nacc_msg','#system'));
          return(0)
      || exec('skan_zb','!zso_scn_dask',_tm,_typ)
   ?};
   {? (_ok:=exec('first','!zso_scn_dask',_tm,_wy))
   || _wy.TM_STAMP:=_tm;
      _mp.save(,_wy);
      {? _ok=2
      || _mp.loop_continue()
      ?};
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła na opis TO DO
::----------------------------------------------------------------------------------------------------------------------
'Skanowanie zbiorcze dokumentów'@@


\first
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Ustawia pierwszą fakturę w obiegu
::   WE: _a - tm_stamp
::       _b - wyjscie czynności
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
SKAN_DOK.cntx_psh();
SKAN_DOK.index('TM_STAMP'); SKAN_DOK.prefix(_a,);
{? SKAN_DOK.first()
|| _b.SKAN_DOK:=SKAN_DOK.ref();
   SKAN_DOK.prefix();
   SKAN_DOK.TM_STAMP+='1';
   _ok:=SKAN_DOK.put();
   {? _ok
   || SKAN_DOK.prefix(_a,);
      _ok:={? SKAN_DOK.first() || 2 || 1 ?}
   ?}
?};
SKAN_DOK.cntx_pop();
_ok


\skan_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Skanowanie zbiorcze faktur wraz z tworzeniem faktur w obiegu i opcjonalnym rozpoznawaniem OCR
::   WE: _a - znacznik czasowy
::       _b - typ: D-dokument księgowy, E-faktura w obiegu, ''-jedno i drugie
::----------------------------------------------------------------------------------------------------------------------
_tm:=_a;
_ref:=SKAN_DOK.ref();
exec('skan_erase','skanuj');
exec('run_java_exec','dok_fks','skaner.jar',0,'-noocr');
_count:=0;
_dir:='@!Tmp';
_skany:=exec('read_skaner_txt','skanuj',_dir);
{? _skany.first()
|| {!
   |? _fn:=_dir+'/'+_skany.FILE;
      _fn_zal:={? _skany.FILE2<>'' || _dir+'/'+_skany.FILE2 || '' ?};
      _ref:=exec('add','skanuj',_fn,_b,_fn_zal);
      {? type_of(_ref)=type_of(null)
      || SKAN_DOK.cntx_psh();
         SKAN_DOK.prefix();
         {? SKAN_DOK.seek(_ref)
         || SKAN_DOK.TM_STAMP:=_tm;
            SKAN_DOK.put();
            _count+=1
         ?};
         SKAN_DOK.cntx_pop()
      ?};
      _skany.next()
   !}
?};
VAR_DEL.delete('PominAll');
:: czyszczenie katalogu tymczasowego ze skanow, jesli sa
exec('skan_erase','skanuj',_dir);
_all:=_skany.size();
&_skany;
{? SKAN_DOK.f_active()
|| SKAN_DOK.f_rfresh();
   SKAN_DOK.f_seek(_ref)
?};
FUN.info(
   'Liczba zeskanowanych dokumentów: '+$_count+'\n'+
   {? _all<>_count
   || 'Liczba istniejących dokumentów: '+$(_all-_count)
   || ''
   ?}
);
1


:Sign Version 2.0 jowisz:1045 2024/01/11 14:03:22 33fa36bfdb2d0fbd9c199b47acb1faa0f9a0992f3452fb6ba3a88d40c95fe11c138bf2718340668c930dc9f3cdf67da7f1d3c12e089af9d276aa27dec36716e67da82368af1a3b7fc7441faf9f240cbcf85c495d3fca12b41101d14b8f73f500ced90ecb1ca8222182ce4c6cfeecab12aaeeb6c2ca332458b4fb712fad05ff36

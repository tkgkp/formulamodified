:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prer.fml
:: Utworzony: 29.08.2017
:: Autor: MK
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_DOS_PRER - Rejestracja przyznanych emerytur i rent
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [17.42]
:: OPIS: Rejestracja przyznanych emerytur i rent - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE,   symbol=OSOBA,     type=_OSOBA,   name=Wskazanie osoby,   required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.OSOBA);
{? _uidref=''
|| _result:=exec('error','!pkd_dos_prer')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_prer',_in.OSOBA);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [17.42]
:: OPIS: Rejestracja emerytury/renty - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj emeryturę/rentę: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj emeryturę/rentę: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj emeryturę/rentę: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj emeryturę/rentę'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [17.42]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie informacji o emeryturze/rencie niemożliwe.\nNie znaleziono osoby.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [17.42]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie osoby.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & OSOBA.seek(_a)
|| EMER_REN.cntx_psh();
   EMER_REN.index('EMER_REN');
   EMER_REN.prefix(exec('ref_firma','ustawienia'),OSOBA.ref());

   EMER_REN.win_sel('WERI');
   EMER_REN.win_edit('RED');
   EMER_REN.win_patt('SZUK');

   _ret:=EMER_REN.select();
   EMER_REN.cntx_pop()
|| _ret:=exec('error','!pkd_dos_prer')
?};
OSOBA.cntx_pop();
_ret


\emer_ren_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [17.42]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli EMER_REN z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli EMER_REN jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=EMER_REN
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\ev_er_ad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [17.42]
:: OPIS: przed wyswietleniem pola EDIT_VAR.AD_ZUS i po redagowaniu pola EMER_REN.ZUS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ad:='';
{? EMER_REN.ZUS<>null()
|| EMER_REN.ZUS();
   _ad:={? |ADRES.ULICA='' | (3+(-ADRES.ULICA)='pl.') | (5+(-ADRES.ULICA)='plac') | 3+(-ADRES.ULICA)='ul.'
        || ''
        || 'ul.'
        ?};
   _ad+=ADRES.ULICA;
   {? +ADRES.DOM || _ad+=' '+ADRES.DOM ?};
   {? +ADRES.LOKAL || _ad+=' m.'+ADRES.LOKAL ?};
   {? +ADRES.KOD_POCZ || _ad+={? +_ad ||'; '||''?}+ADRES.KOD_POCZ ?};
   {? +ADRES.POCZTA
   || _ad+={? +_ad ||' '||''?}+ADRES.POCZTA
   |? +ADRES.MIASTO
   || _ad+={? +_ad ||' '||''?}+ADRES.MIASTO
   ?}
?};
EDIT_VAR.AD_ZUS:=_ad


\er_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [17.42]
:: OPIS: Obsługa akcji "Rekord - po": Weryfikacja informacji o emeryturach rentach; tabela EME_REN; okienko WER
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? {? EMER_REN.E_R='R'
   || (_chk:=__CHK.record(EMER_REN,,'NRE','OD','DO','ZUS'))<>''
   || (_chk:=__CHK.record(EMER_REN,,'NRE','OD','ZUS'))<>''
   ?}
|| return(_chk)
|? EMER_REN.OD<>date(0,0,0) & EMER_REN.DO<>date(0,0,0) & EMER_REN.OD>EMER_REN.DO
|| FUN.emsg('Data początku świadczenia nie może być większa niż data końca świadczenia.'@);
   0
|? exec('check','overlap',{? menu_txt()='Popraw'|| EMER_REN.ref() || null ?},EMER_REN,,,1,,,'EMER_REN',
     exec('ref_firma','ustawienia'),OSOBA.ref())<>''
|| FUN.emsg('Okres redagowanego rekordu nakłada się z istniejącym zapisem.'@);
   0
|| 1
?}


\emer_ren_er_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [17.42]
:: OPIS: Funkcja po edycji pola ER w tabeli EMER_REN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
EMER_REN.efld_opt('RED','mark=%1'[$(EMER_REN.E_R='R')],EMER_REN,'DO')

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 64d30d3c9261704fb82fe5bc1345961265097497b63190a9d7856aa0efb1a53b84c2b6d198f62bb2a42a21ec0ed0f668667d8cb6d513c2c4e19274a9960e674d5b0576aa61c86f09f3cfade257366d246007603c64950637ebad006893a389fddf8900ec06c2c35d761d495e5ad01b2c811832cb88ce4412fc9685e509a1eac3

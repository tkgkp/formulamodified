:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zbl_bil_doko.fml
:: Utworzony: 03.09.2019
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa czynności wysłania rozrachunków do usługi Businesslink
:: UWAGA: do wersji 20.14 czynność ZWS_BIL_DOKO
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Formuła główna czynności wysłania rozrachunków (ZBL_BIL_DOKO)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_action:=_mp.akcja();

::# properties=SERVICE

::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, BŁĄD)", required=N
{? var_pres('RESULT',_out)<>type_of(~~) & var_pres('RESULT',_out)<>type_of('') || return() ?};
::# kind=WY, symbol=DESC, type=MEMO, name="Opis wyniku", required=N
{? var_pres('DESC',_out)<>type_of(~~) & var_pres('DESC',_out)<>type_of('') || return() ?};

_result:=exec('settlements_send','!zbl_bil_doko',~(_mp.isAutoRun() | _mp.isService()));
_out.RESULT:={? _result.STATUS || 'OK' || 'BŁĄD' ?};
_out.DESC:=_result.DESC;
_mp.save(,_out);
_mp.done();
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Opis dla czynności wysłania rozrachunków (ZBL_BIL_DOKO)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_desc:='Wyślij rozrachunki do portalu Businesslink'@@;
_desc


\settlements_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Wysyła rozrachunki do usługi Businesslink.
::   WE: _a - czy wyświetlać dialogi
::   WY: wynik przetwarzania, 1 - jeżeli ok, 0 - w przypadku błędów (np. brak kontaktu z portalem Businesslink)
::----------------------------------------------------------------------------------------------------------------------
_dialog:=_a;
_result:=obj_new('STATUS','DESC');
_result.STATUS:=1;
_result.DESC:='';

_res:=exec('SettlementsSend','businesslink3',_dialog);
_result.STATUS:=_res.STATUS;

{? _result.STATUS
|| _result.DESC:=_res.MESSAGE;
   {? _dialog
   || {? _res.DETAILS.size()>0
      || _choice:=FUN.choice(_result.DESC,,'Szczegóły'@)
      || FUN.info(_result.DESC);
         _choice:=0
      ?}
   ?}
|| _result.DESC:='Nie udało się wysłać wszystkich rozrachunków.'+'\n'+_res.MESSAGE;
   {? _dialog
   || _choice:=FUN.choice('Nie udało się wysłać wszystkich rozrachunków.'@+'\n'+_res.MESSAGE,,'Szczegóły'@)
   ?}
?};

{? _dialog & _choice=1
|| _tab:=_res.DETAILS;
   _wer:=_tab.mk_sel('Szczegóły'@,'N',0,'resdetails',,,,,'U');
   _tab.win_fld(_wer,,'MESSAGE',,,100);
   _tab.win_sel(_wer);
   _tab.select()
?};

_result

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:22 b174a91624af36bc7fa4f099a9d207ed07206903e97da4a86151eb9107883717bfb1c3b5526619b8b294744d9c6c115648293c29c179d79b199d6fedb28c708dcc208c9af2c4297c1e5082fdea94e4de5582fc63f4a5c8d9a889ba1422d5e7ac3a9bed0be0cb8329152b9f77ec24c7901218c46b75c9bbb2e94a3e12a8c7b2f0

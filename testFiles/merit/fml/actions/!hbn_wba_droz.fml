:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !hbn_wba_droz.fml
:: Utworzony: 15.06.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności HBN_WBA_DROZ - rozpoznawanie wyciągów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Rozpoznawanie wyciągów - główna formuła czynności HBN_WBA_DROZ
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,   symbol=PWN,   type=_PWN,   name=Nagłówek wyciągu,  required=N, keyref=T
::# kind=WY,   symbol=PWN,   type=_PWN,   name=Nagłówek wyciągu,  required=T
::# permissions=FJKS,HRB,HRP
::# access=exec('uprawnienia','!hbn_wba_droz')
exec('F','object');
exec('czytaj','#stalesys',,HINFO);
exec('czytaj','#stalesys',,FINFO);
exec('czytaj','#stalesys',,XINFO);

_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_akcja:=_mp.akcja();
_key:=_in.PWN;

{? _key=~~
|| {? _mp.pathTodo()
   || _key:=_out.PWN
   |? _mp.pathArea()
   || _key:=_out.PWN
   ?};
   {? _key=~~ || _key:=null() ?}
?};

{? _key & 6+($_key)<>PWN.name()
|| PWN.use(form(6+($_key)));
   PWN.prefix();
   {? PWN.seek(_key,)
   || PW.use('pw'+(PWN.name()+4));
      PW.index('PWN');
      PW.prefix(PWN.ref());
      PW_OP.use('pwp'+(PWN.name()+4));
      PW_OP.prefix()
   || FUN.info('Nie znaleziono wyciągu.'@);
      _mp.error()
   ?}
|? ~_key & _mp.pathProc()
|| _key:=exec('select','!hbn_wba_droz');
   {? _key
   || _out.PWN:=_key;
      _mp.save(,_out);
      _mp.keyRef(PWN.uidref())
   ?}
|? ~_key & _mp.pathTodo()
|| FUN.info('Nie znaleziono wyciągu.'@);
   _out.PWN:=null;
   _mp.save(,_out);
   _mp.done()
?};

{? _key
|| _dalej:=0;
   {? _mp.pathTodo()
   || BtnRozp:=0;
      exec('set_pwn_win','!hbn_wba_droz');
      PWN.display();
      {? BtnRozp=1 || _dalej:=1 ?}
   || _dalej:=1
   ?};

   {? _dalej
   || exec('fbimpwbr','hbn',0);
      {? _mp.pathArea() & _akcja<>'Przekaż'
      || FUN.info('Przeprowadzono rozpoznawanie pozycji wyciągu: %1 (%2).'@[PWN.NRW,PWN.BL().NB])
      ?};
      {? _mp.pathTodo() | _mp.pathProc() | _mp.pathArea() & _akcja='Przekaż'
      || _dalej:=exec('set_status','!hbn_wba_droz')
      ?};
      {? _dalej
      || _out.PWN:=_key;
         _mp.save(,_out);
         _mp.done()
      || _mp.keep()
      ?}
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Rozpoznaj pozycje wyciągu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_wy:=_mp.load(exec('kind_out','#b_port'));
_maska:='';
{? var_pres('PWN',_we) || _ref:=_we.PWN; _maska:=6+$_we.PWN
|? var_pres('PWN',_wy) || _ref:=_wy.PWN; _maska:=6+$_wy.PWN
|| _ref:=null
?};
{? _ref
|| PWN.cntx_psh();
   {? _maska<>''
   || PWN.use(_maska)
   ?};
   PWN.prefix();
   {? PWN.seek(_ref,)
   || _desc:='Rozpoznaj pozycje wyciągu: %1 (%2)'@@[PWN.NRW,PWN.BL().NB]
   || _desc:='Uwaga błąd - nie odnaleziono wyciągu.'@@
   ?};
   PWN.cntx_pop()
|| _desc:='Uwaga błąd – nie znaleziono rekordu kluczowego, ani parametrów wejścia/wyjścia'@@
?};
_desc


\set_mask
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Ustalenie masek dla czynności
::----------------------------------------------------------------------------------------------------------------------
_maska:='xxxx';
PWN.use('pn'+_maska);
PWN.prefix();
PW.use('pw'+_maska);
PW.index('PWN');
PW.prefix();
PW_OP.use('pwp'+_maska);
PW_OP.prefix()


\m_rozpoznaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Akcja Rozpoznaj z poziomu obszaru roboczego
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='HBN_WBA_DROZ';
_params.AKCJA:='Rozpoznaj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.UIDREF:=PWN.uidref();
_params.PROC_START:='N';
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'PWN',PWN.ref());
exec('mp_run','#b__box',_params)


\pw_rozpoznaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Ponowne rozpoznawanie w oknie czynności startowej po głównym rozpoznawaniu lub z ToDo
::----------------------------------------------------------------------------------------------------------------------
exec('fbimpwbr','hbn',1);
FUN.info('Przeprowadzono rozpoznawanie pozycji wyciągu: %1 (%2)'@ [PWN.NRW,PWN.BL().NB])


\set_pwn_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła ustawia okno tabeli PWN (nagłówki wyciągów)
::----------------------------------------------------------------------------------------------------------------------
_win:='DISP';
_red:=PWN.mk_edit('Wyciąg bankowy'@);
PWN.win_ewin(_red,,_win);
PWN.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Rozpoznaj'@],"BtnRozp:=1;'key:Esc'");
_an:=PWN.win_ebtn(_red,
                  'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],"BtnRozp:=0;'key:Esc'");
PWN.btn_eopt(_red,_an,'tooltip='+exec('help_red_esc','#window','A'));
PWN.win_edit(_red)


\pwn_set_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zmienia status wyciągu
::   WE: _a = 'T' - rozpoznany, przygotowany do dekretacji
::            'N' - nieprzygotowany
::----------------------------------------------------------------------------------------------------------------------
{? PWN.STATUS<>'Z'
|| {? _a='T' & PWN.STATUS='T'
   || FUN.info('Wyciąg posiada już status: przygotowany do zadekretowania.'@)
   |? _a='T' & PWN.STATUS='N'
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='HBN_WBA_DROZ';
      _params.AKCJA:='Przekaż';
      _params.UIDREF:=PWN.uidref();
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'PWN',PWN.ref());
      _params.PROC_START:='T';
      exec('mp_run','#b__box',_params)
   |? _a='N' & PWN.STATUS='N'
   || FUN.info('Wyciąg posiada już status: nieprzygotowany.'@)
   |? _a='N' & PWN.STATUS='T'
   || PWN.STATUS:='N';
      PWN.put()
   ?}
|| FUN.info('Nie można zmienić statusu wyciągu, który został już zadekretowany.'@)
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wybór wyciągu do rozpoznania
::   WY: ref wybranego wyciągu lub null
::----------------------------------------------------------------------------------------------------------------------
exec('set_mask','!hbn_wba_droz');
VAR_DEL.delete('__TPWN');
_key:=null;
PWN.index('STATUS');
PWN.prefix('N');
{? PWN.first()
|| __TPWN:=tab_tmp(1,'NRW','STRING[10]','Numer wyciągu',
                    'BL','STRING[40]','Bank',
                    'RBL','STRING[40]','Rachunek',
                    'REF','INTEGER','Ref');
   _win:=__TPWN.mk_sel('Wybór wyciągu do rozpoznawania'@,'P',0,'__tpwn_select',,,,,'U');
   __TPWN.win_fld(_win,__TPWN,'NRW',,,10,,,'Numer'@);
   __TPWN.win_fld(_win,__TPWN,'BL','KOD',,40,,,'Bank'@);
   __TPWN.win_fld(_win,__TPWN,'RBL',,,40,,,'Rachunek'@);
   __TPWN.win_act(_win,,'Formuła','Wybierz'@@,,,,"sel_exit()",1,,,,'W');
   _fun:="exec('pozycje','!hbn_wba_droz')";
   __TPWN.win_act(_win,,'Formuła','Pozycje'@@,,,,_fun,,,,,'P');
   __TPWN.win_sel(_win);

:: weryfikacja uprawnień - jednostka księgowa i rachnunek bankowy licencjobiorcy
   PWN.cntx_psh();
   {! |?
      {? exec('usr_fjks','b_perm',PWN.ODD().OD) & exec('usr_hrb','b_perm',PWN.RBL)
      || __TPWN.NRW:=PWN.NRW;
         __TPWN.BL:=PWN.BL().NB;
         __TPWN.RBL:=RB.get_rbtx(2,PWN.RBL);
         __TPWN.REF:=#PWN.ref();
         __TPWN.add()
      ?};
      PWN.next()
   !};
   PWN.cntx_pop();

   {? __TPWN.first()
   || {? __TPWN.select()
      || {? PWN.seek(__TPWN.REF,PWN.name())
         || _key:=PWN.ref();
            PW.prefix(PWN.ref());
            {? ~PW.first()
            || FUN.info('Wyciąg nie zawiera pozycji do rozpoznawania.'@);
               _key:=null
            ?}
         || FUN.info('Nie odnaleziono wyciągu, prawdopodobnie został rozpoznany przez innego użytkownika.'@)
         ?}
      ?}
   || FUN.info('Brak wyciągów do rozpoznawania.'@)
   ?}
|| FUN.info('Brak wyciągów do rozpoznawania.'@)
?};
VAR_DEL.delete('__TPWN');
_key


\set_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła pozwala użytkownikowi obejrzeć wyniki rozpoznawania i ustawić status wyciągu
::----------------------------------------------------------------------------------------------------------------------
BtnGotowy:=0;
PW.index('PWN');
PW.prefix(PWN.ref());
{? PW.first()
|| {? var_pres('J_GV_POZ')>0
   || PW.win_sel('DEKRET1')
   || PW.win_sel('DEKRET')
   ?};
   PW.select()
?};
{? BtnGotowy=1
|| PWN.cntx_psh();
   PWN.prefix();
   PWN.STATUS:='T';
   PWN.put();
   PWN.cntx_pop()
?};
BtnGotowy


\btn_pw_gotowy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Obsługa przycisku Przygotowany w oknie przeglądania pozycji po rozpoznaniu
::----------------------------------------------------------------------------------------------------------------------
{? PW.win_sel('?')<>'DEKRET1'
|| BtnGotowy:=1;
   'key:Esc'
|| BtnGotowy:=1; sel_exit()
?}


\pozycje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Pozycje wyciągu do rozpoznania
::----------------------------------------------------------------------------------------------------------------------
PWN.cntx_psh();
PW.cntx_psh();
{? PWN.seek(__TPWN.REF,PWN.name())
|| PW.index('PWN');
   PW.prefix(PWN.ref());
   _win:=PW.mk_sel('Pozycje wyciągu: %1 (%2)'@[PWN.NRW,PWN.BL().NB],'P',0,'pw_dekr_poz',,,,,'U');

   PW.win_fld(_win,PW,'NRW',,,8,,,'Pozycja'@);
   PW.win_fld(_win,PW,'WSK_R',,,5,,,'Rozpoznana'@,,,2,,"'T'","'N'");
   PW.win_fld(_win,PW,'DK',,,14,,,'Data księgowania'@);
   PW.win_fld(_win,PW,'KONTR',,,34,,,'Nazwa strony transakcji'@);
   PW.win_fld(_win,PW,'TYTOP',,,42,,,'Tytuł operacji'@);
   PW.win_fld(_win,PW,'WAL',,,3,,,'Waluta'@);
   PW.win_fld(_win,PW,'KW',,,16,2,,'Kwota'@);

::PW.win_act(_win,0,'Formuła','Rozrachunki',,,"mb_exec('F_PWOP')","",1);
   PW.win_act(_win,0,'Rekord',,,,"mb_exec('br_pw')","");
   PW.win_act(_win,0,'Szukaj',,,,"","");
   PW.win_act(_win,0,'Kolejność',,,,"","");
   PW.win_sel(_win);
   PW.select()
?};
PW.cntx_pop();
PWN.cntx_pop();
''


\grp_pw_af
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [lokalna]
:: OPIS: Odświeżenie okna z danymi nagłówka wyciągu
::----------------------------------------------------------------------------------------------------------------------
grp_edisp(PWN,'PANEL');
1


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh(); USERS.clear();
{? USERS.seek(_user)
|| {? var_pres('PWN',_in) & var_pres('PWN',_in)=type_of(null()) & _in.PWN<>null()
   || PWN.cntx_psh();
      PWN.prefix();
      {? PWN.seek(_in.PWN,form(($_in.PWN)-8))
      || {? exec('usr_fjks','b_perm',PWN.ODD().OD,USERS.ref()) & exec('usr_hrb','b_perm',PWN.RBL,USERS.ref())
            & exec('usr_hrp_any','b_perm',USERS.ref())
         || _result:=1
         ?}
      ?};
      PWN.cntx_pop()
   || {? exec('usr_fjks_any','b_perm',USERS.ref()) & exec('usr_hrb_any','b_perm',USERS.ref())
         & exec('usr_hrp_any','b_perm',USERS.ref())
      || _result:=1
      ?}
   ?}
?};
USERS.cntx_pop();
_result


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'pn'>0
         || _record:=exec('FindAndGet','#table',PWN,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1045 2023/09/07 12:13:11 2b5c1c9e7cc13de7e5e2395cbcc4e85d3a81eb0483748ca8995e20605fa2409b53af21a291dd119b7794f6cd82f6dadbda59b92dbd07cf3a687c4000ea1e699b51ff41480b66e6cb1c43227e3ff389d1d63ec463d5f31435702c8c818bb699bf79b65ed4bf0b215092f65b5c988ec0acf7815fa9611ed8fe4cd70e437e160df0

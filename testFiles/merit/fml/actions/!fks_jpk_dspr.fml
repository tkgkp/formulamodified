:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_jpk_dspr.fml
:: Utworzony: 21.02.2019
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_JPK_DSPR - Definiowanie e-Sprawozdania
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Definiowanie e-Sprawozdania - główna formuła czynności FKS_JPK_DSPR.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
:: PARAMETRY WE:
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres księgowy, required=N, keyref=N
:: PARAMETRY WY:
::# kind=WY, symbol=E_SPR,   type=_E_SPR, name=Wskazanie na definicję e-Sprawozdania, required=T, keyref=T
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
ISTDEF.win_edit('R_JPK');
ISTDEF.win_sel('W_JPK');
ISTDEF.actions('W_JPK','DuPTHOEIA:DIA');
SLU.index('NAZ'); SLU.prefix('~TYPY JEDNOSTEK',); SLU.first();
exec('espr_red','jpk_sf');
{? _akcja='Dołącz' | _mp.pathProc()
|| menu_txt(,'Dołącz');
   EsprStan:=0;
   _ref:=null;
   E_SPR.cntx_psh();
   E_SPR.blank();
   {? E_SPR.ISTDEF
   || exec('ae_espr_sch','jpk_sf')
   ?};
   E_SPR.DATA:=date();
   E_SPR.memo_set('','PKD');
   _mp.trigRef('E_SPR');
   {? E_SPR.edit("exec('ar_espr','jpk_sf')")
   || _ref:=exec('espr_add_put','jpk_sf')
   ?};
   E_SPR.cntx_pop();
   VAR_DEL.delete('EsprStan');
   {? _ref
   || E_SPR.cntx_psh();
      E_SPR.prefix();
      {? E_SPR.seek(_ref)
      || _wy.E_SPR:=_ref;
         _mp.save(,_wy);
         {? E_SPR.AKC='T'
         || _mp.done()
         || _mp.keep()
         ?}
      ?};
      E_SPR.cntx_pop();
      E_SPR.seek(_ref)
   ?}
|? _akcja='Popraw' | _mp.pathTodo()
|| menu_txt(,'Popraw');
   EsprStan:=1;
   {? var_press('E_SPR',_wy)>0
   || E_SPR.cntx_psh();
      E_SPR.prefix();
      {? E_SPR.seek(_wy.E_SPR)
      || E_SPR.memo_get(,'PKD');
         {? E_SPR.edit("exec('ar_espr','jpk_sf')")
         || exec('espr_add_put','jpk_sf')
         ?};
         {? E_SPR.AKC='T'
         || _mp.done()
         ?}
      ?};
      E_SPR.cntx_pop()
   ?};
   VAR_DEL.delete('EsprStan')
|? _akcja='Zakończ'
|| _ref:=null;
   _tab:=_mp.getRefs();
   {! _i:=1..obj_len(_tab)
   |? _ref=null
   |! {? _tab[_i]<>~~ & ref_tab(_tab[_i])=E_SPR
      || E_SPR.cntx_psh();
         E_SPR.prefix();
         {? E_SPR.seek(_tab[_i])
         || _ref:=E_SPR.ref()
         ?};
         E_SPR.cntx_pop()
      ?}
   !};
   {? _ref
   || _wy.E_SPR:=_ref;
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _mp.isMicro()
|| exec('espr_sel','jpk_sf')
?}



\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis na liście TODO czynności
::----------------------------------------------------------------------------------------------------------------------
_desc:='Definiowanie e-Sprawozdania'@@;
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_press('E_SPR',_wy)>0
|| E_SPR.cntx_psh();
   E_SPR.prefix();
   {? E_SPR.seek(_wy.E_SPR)
   || _desc:='Definiowanie e-Sprawozdania: \'%1\''@@[E_SPR.NAZ]
   ?};
   E_SPR.cntx_pop()
?};
_desc

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 5119693aad073169d163ab0dc2bb8687e6054acc29b0e8db5c951397c11925497803a6d6b2e80be8b9df6416f9d5d7da22e18034a07fe75a3c94c10e5f72f0d237fc86f937c87ea199e7f1e772b8152679ff601c48fe1e45b5226ac31bf321970cd039a58093002823338452adc163b2faac0442aba69774b0c05775eefa1d4b

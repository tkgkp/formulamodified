:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zpr_zas_azad.fml
:: Utworzony: 06.09.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Aktualizacja listy zadań
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE,GRP_FIRM
::# kind=WE,   symbol=BI_PREL,   type=STRING,   name=Instancja czynności w procesie,   required=N
_in:=params_get().in;
_mp:=params_get().mp;

_bi_prel:={? var_pres('BI_PREL',_in)=type_of('') || exec('FindAndGet','#table',BI_PREL,_in.BI_PREL,,,null()) || null() ?};
_one_bi_prel:=_bi_prel<>null();
_service:=_mp.isService();

exec('bistat_status','#bi_stat');

_ii:=0; _bi_stat:=obj_new(3);
_ii+=1; _bi_stat[_ii]:=__Status.OCZEKUJACA;
_ii+=1; _bi_stat[_ii]:=__Status.URUCHOMIONA;
_ii+=1; _bi_stat[_ii]:=__Status.ZAWIESZONA;
BI_TODO.cntx_psh();
BI_PREL.cntx_psh();
BI_TODO.use('bi_t____');
BI_PROC.use('bi_p____');
BI_PREL.use('bi_e____');
BI_PREL.index('SIGNAL');
{! _ii:=1..obj_len(_bi_stat)
|!
   _zawieszona:=_bi_stat[_ii]=__Status.ZAWIESZONA;
:: zakres instancji czynności
   _Bi_prel:=tab_tmp(1,'REF','STRING[16]','$BI_PREL.ref');
   {? _one_bi_prel
   ||
      BI_PREL.prefix();
      {? BI_PREL.seek(_bi_prel)
         & BI_PREL.BI_STAT=_bi_stat[_ii]
         & BI_PREL.FIRMA=REF.FIRMA
      ||
         _Bi_prel.REF:=$BI_PREL.ref();
         _Bi_prel.add()
      ?}
   ||
      BI_PREL.prefix(_bi_stat[_ii]);
      _loop:=BI_PREL.first();
      {!
      |? _loop
      |!
         {? BI_PREL.FIRMA=REF.FIRMA
         ||
            _Bi_prel.REF:=$BI_PREL.ref();
            _Bi_prel.add()
         ?};
         _loop:=BI_PREL.next()
      !}
   ?};
   _loop:=_Bi_prel.first();
   {!
   |? _loop
   |!
      {? BI_PREL.seek(_Bi_prel.REF)
      ||
         _bi_prel:=BI_PREL.ref();
         {? exec('lock','#bi_prel',_bi_prel)
         ||
            BI_TODO.index('UNIQUE');
            BI_TODO.prefix(_bi_prel);
            {? BI_TODO.first()
            ||
               _b_action:=exec('FindInSet','#table','B_ACTION','B_ELE',BI_PREL.B_PREL().B_ELE);
               _b_role:=B_PREL.B_ROLE;
               {? BI_TODO.BI_STAT<>__Status.TODO_BREAK
                  &  BI_TODO.BI_STAT<>__Status.TODO_ERROR
                  &  (
                     _zawieszona
                     |  BI_TODO.BI_STAT=__Status.TODO_WOLNE
                     |  BI_TODO.BI_STAT=__Status.TODO_MOJE
                        &  (
                           BI_TODO.USERS=BI_TODO.USERS_R & BI_TODO.MANASSIG='N'
                           |  BI_TODO.USERS<>BI_TODO.USERS_R
                              &  (
                                 BI_TODO.USERS<>exec('zastepujacy','#users',,BI_TODO.USERS_R,_b_action,,_b_role).USER
                                 | exec('user_access_fml','#b_proman',BI_TODO.USERS_R,_bi_prel)=0
                                 )
                           |  BI_TODO.USERS<>BI_TODO.USERS_R
                              & exec('user_access_fml','#b_proman',BI_TODO.USERS,_bi_prel,BI_TODO.USERS_R)
                           |  exec('user_access_fml','#b_proman',BI_TODO.USERS,_bi_prel)
                           )
                     )
               ||
:: Kiedy odtwarzać todo?
:: - nie zakończono awaryjnie procesu
:: - nie zgłoszono błędu do zadania
:: - zadanie wolne
:: - zadanie przydzielone automatycznie i brak informacji o zastępowanym (wykonawca=zastępca)
:: - zadanie przydzielone i skończyło się zastępstwo
:: - zadanie przydzielone i użytkownik zastąpiony stracił uprawnienia
:: - zadanie przydzielone i użytkownik stracił uprawnienia
:: Uwaga
:: - Zastępstwo wielu osób przez jedną zawsze będzie odtwarzane ponieważ w obecnych strukturach nie ma jak zapamiętać
::   informacji o zastępowanych
:: - Podczas manualnego przydzielania zadania zapamiętywany będzie wykonawca i zastępca i znacznik przydziału ręcznego
                  _create:=1;
                  _ope:=null();
                  _UsrsAct:=exec('usersAction','#b_desktop',BI_PREL.ref());
                  _loop:=BI_TODO.first();
                  {? _loop & BI_TODO.BI_STAT=__Status.TODO_MOJE & _zawieszona=0
                  ||
::                   sprawdzenie czy dotychczasowy wykonawca jest nadal na liście użytkowników uprawnionych
::                   z dokładnością do zastępowanego
                     _create:=~_UsrsAct.find_key($BI_TODO.USERS) | _UsrsAct.USER_R<>$BI_TODO.USERS_R;
                     {? _create
::                   ponowny przydział zadania
                     ||
                        {? BI_TODO.USERS_R & _UsrsAct.find_key($BI_TODO.USERS_R)
::                      zastępowany przejmuje zadanie po wygaśnięciu zastępstwa
                        ||
                           _ope:=exec('FindAndGet','#table',USERS,_UsrsAct.USER,,,null())
                        ||
                           _cur_usr:=BI_TODO.USERS;
                           BI_TODO.USERS:=BI_TODO.USERS_R;
                           _user_r:=exec('zastepujacy','#users',,BI_TODO.USERS,_b_action,,_b_role).USER;
                           {? _user_r & _user_r=_cur_usr & _UsrsAct.find_key($_user_r)
                           ||
                              _create:=0
                           ||
                              {? _user_r & _user_r<>BI_TODO.USERS & _UsrsAct.find_key($_user_r)
::                            zastępca przejmie zadanie
                              ||
                                 _UsrsAct.USER_R:=$BI_TODO.USERS;
                                 _UsrsAct.put();
                                 _ope:=_user_r
                              ?}
                           ?}
                        ?}
                     ?}
                  ?};
                  {? _create
                  ||
                     exec('memory','!zpr_zas_azad');
::                     _loop:=BI_TODO.first();
::                     {!
::                     |? _loop
::                     |!
::                        _loop:=exec('delete','#bi_todo',BI_TODO.ref())>0 & BI_TODO.first()
::                     !};
::                     exec('create','#bi_todo',_bi_prel,,_ope,_UsrsAct,0)
                     exec('rebuild','#bi_todo',_bi_prel,_UsrsAct,0,_ope)
                  ?};
                  &_UsrsAct
               ?}
            ?};
            exec('unlock','#bi_prel',_bi_prel)
         ?}
      ?};
      _loop:={? _one_bi_prel || 0 || _Bi_prel.next() ?}
   !};
   obj_del(_Bi_prel)
!};
VAR_DEL.delete('TmpToDo');
BI_TODO.cntx_pop();
BI_PREL.cntx_pop();
_mp.done()


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('BI_PREL',_in)=type_of('') & +_in.BI_PREL
|| 'Zaktualizuj przydział zadania: %1'@@[exec('record','#to_string',exec('FindAndGet','#table',BI_PREL,_in.BI_PREL,,,null()))]
|| 'Zaktualizuj listę zadań wszystkich użytkowników'@@
?}


\find_replacmenet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a


\memory
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Przygotowuje i czyści tabelę do zapamiętania danych zadań
::   WE: _a - 1-przygotowanie 0-czyszczenie
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('TmpToDo')<=0
||  TmpToDo:=tab_tmp(2,
      'B_PREL','STRING[31]','B_PREL',
      'USERS','INTEGER','USERS',
      'DATE','DATE','Data',
      'TIME','TIME','Czas'
   )
?};
{? var_pres('TmpToDo')>0
|| TmpToDo.prefix();
   {? TmpToDo.first()
   || TmpToDo.erase()
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 b5ff17bfaf84bcdb92a08283deb067b0c82e2557a126082177c19bdb677d5fd8476b3a3e6b39d302c4b1b89849a0bd7a5a530d845e5a1e5e6bce5a5ecdfc538ef1f96dc94ef85d570a386ea2e2d41d9113b6c895bc7c21b4c25bbdbdc5ae8209c4fd765fcb040f7708cdecfe9acf0d781c9d6d48f20ca7e5f28c83365b6ce3db

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !luo_kpo_eakp.fml
:: Utworzony: 29.01.2019
:: Autor: MW
::======================================================================================================================
:: Zawartość: Formuły czynności LUO_KPO_EAKP
::            Akceptacja karty przekazania odpadów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Czynność LUO_KPO_EAKP - formuła główna
::   WE: _a - [obj_new] - parametry wejsciowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=KPO,  type=_KPO,  name=Karta przekazania odpadów, required=T, keyref=T
::# kind=WY,   symbol=KPO,  type=_KPO,  name=Karta przekazania odpadów, required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init','luo');

_akcja:=_mp.akcja();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & (_mp.isAutoRun() | _mp.isService()));

:: Przełączam maski na zgodne z _in.KPO
{? var_pres('KPO',_in)=type_of(null()) & _in.KPO
|| _name:=ref_name(_in.KPO);
   _oddz:=(6+_name)+1;
   _rok:=(8+_name)+2;
    exec('kpo_open','open_tab',_oddz,_rok)
?};

:: Sprawdzenie uprawnień
params_set('in',_in,'user',OPERATOR.USER,'mp',_mp);
{? exec('permissions','!luo_kpo_eakp')=0
|| _mp.error('Brak uprawnień do uruchomienia czynności.'@);
   return()
?};

{? type_of(_in)=type_of(~~) | var_pres('KPO',_in)<=type_of(~~)
|| _mp.error('Brak wymaganego parametru KPO.')
|| KPO.cntx_psh();
   KPO.prefix();
   {? ~KPO.seek(_in.KPO)
   || _mp.error('Nie znaleziono karty przekazania odpadów.'@)
   || _out.KPO:=_in.KPO;
      _mp.save(,_out);
      {? _r_lock:=exec('r_lock_one','#table',KPO,KPO.ref())
      || params_set('mp',_mp);
         {? _akcja='Akceptuj' | _auto
         || exec('kpo_akcept','odpady', _auto);
            {? KPO.STATUS='T'
            || _mp.done()
            ?}
         |? _mp.pathTodo() | (_mp.pathArea() & _akcja='')
         || exec('kpo_akcept_todo','odpady');
            {? KPO.STATUS='T'
            || _mp.done()
            ?}
         || _mp.error('Nieobsługiwana ścieżka.')
         ?};
         exec('r_unlock_one','#table',KPO,KPO.ref(),_r_lock)
      || exec('who_rlock_kpo','odpady')
      ?}
   ?};
   KPO.cntx_pop()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? type_of(_in)<>type_of(~~)& var_pres('KPO',_in)<>type_of(~~) & _in.KPO
|| 'Zaakceptuj kartę przekazania odpadów: %1'@@[exec('record','#to_string',_in.KPO)]
|| 'Zaakceptuj kartę przekazania odpadów'@@
?}


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;
_wyn:=1;
_wyn


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Formuła display TO-DO
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
{? type_of(_in)<>type_of(~~) & var_pres('KPO',_in)>type_of(~~)
|| KPO.cntx_psh();
   KPO.prefix();
   {? KPO.seek(_in.KPO)
   || exec('kpo_show','odpady')
   ?};
   KPO.cntx_pop()
?}


\n_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Czynność LUO_KPO_EAKP - formuła główna w wersji nieprocesowej
::----------------------------------------------------------------------------------------------------------------------
:: Parametry wejściowe identyczne jak w wersji procesowej
_in:=params_get().in;

:: Akcja dostępna jako STRING a nie metoda obiektu _mp
_akcja:=params_get().akcja;

{? type_of(_in)=type_of(~~) | var_pres('KPO',_in)<=type_of(~~)
|| FUN.emsg('Brak wymaganego parametru KPO.'@)
|| KPO.cntx_psh();
   KPO.prefix();
   {? ~KPO.seek(_in.KPO)
   || _mp.error('Nie znaleziono karty przekazania odpadów.'@)
   || {? _r_lock:=exec('r_lock_one','#table',KPO,KPO.ref())
      || exec('kpo_akcept','odpady',_akcja='AkceptujAuto');
         exec('r_unlock_one','#table',KPO,KPO.ref(),_r_lock)
      || exec('who_rlock_kpo','odpady')
      ?}
   ?};
   KPO.cntx_pop()
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 62db85ea086f14092d174a5ae1bdd29c565149a8f926824043d3c98503cc0eb7ec0f5c16a4f41e14989f4c223fb09ea9b667c5e72107f96e3458a92b3cb5eb1e7d9107e675966690fe1e15b8a1f48899f2c0923d33c4d7eb5513d02ed492f42dc1cec743c383e792eb8101b6acd5a19df943586498ee9cc56d074a8f0bc79560

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_mokr.fml
:: Utworzony: 14.12.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_MOKR - Zamknięcie okresu śr. trwałych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zamknięcie okresu śr. trwałych - główna formuła czynności FST_EWI_MOKR.
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,   symbol=OKRO_F,   type=_OKRO_F,   name=Wskazanie na okres obrachunkowy,  required=N
::# kind=WY,   symbol=OKRO_F,   type=_OKRO_F,   name=Wskazanie na okres obrachunkowy,  required=N
::# permissions=FJKS
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
_ok:=0;
{? _mp.pathProc()
|| {? ~SSTALE.AO
   || FUN.info('Nie wybrano okresu w stałych systemu.'@)
   || SSTALE.AO(); _mp.keyRef(OKRO_F.uidref());
      _out.OKRO_F:=SSTALE.AO; _mp.save(,_out); SSTALE.AO();
      exec('desc_update','#b__box',OKRO_F.uidref());
      _komunik:=~_mp.isAutoRun();
      _ok:=params_exec('close','!fst_ewi_mokr',SSTALE.AO,_mp,_komunik);
      {? _ok
      || _nazwa:=SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ;
         FUN.info('Zamknięcie okresu środków trwałych: %1 zakończone sukcesem.'@[_nazwa])
      ?}
   ?}
|? _mp.pathTodo()
|| {? var_press('OKRO_F',_in)>0 & _in.OKRO_F
   || _out.OKRO_F:=_in.OKRO_F; _mp.save(,_out);
      OKRO_F.cntx_psh(); OKRO_F.prefix();
      {? OKRO_F.seek(_in.OKRO_F,)
      || _mp.keyRef(OKRO_F.uidref());
         exec('desc_update','#b__box',OKRO_F.uidref())
      ?};
      OKRO_F.cntx_pop();
      _ok:=params_exec('close','!fst_ewi_mokr',_in.OKRO_F,_mp,1)
   |? SSTALE.AO
   || SSTALE.AO();
      _mp.keyRef(OKRO_F.uidref());
      _out.OKRO_F:=SSTALE.AO; _mp.save(,_out);
      exec('desc_update','#b__box',OKRO_F.uidref());
      _komunik:=~_mp.isAutoRun();
      _ok:=params_exec('close','!fst_ewi_mokr',SSTALE.AO,_mp,_komunik)
   || FUN.info('Nie wskazano okresu do zamknięcia.'@)
   ?}
|? (7+_akcja)='zamknij'
|| _ref:=_mp.getRefs();
   OKRO_F.cntx_psh();
   OKRO_F.prefix();
   {? (var_press('OKRO_F',_in)>0 & _in.OKRO_F & OKRO_F.seek(_in.OKRO_F)) | (OKRO_F.seek(_ref[1]))
   || _mp.keyRef(OKRO_F.uidref());
      _out.OKRO_F:=OKRO_F.ref(); _mp.save(,_out);
       exec('desc_update','#b__box',OKRO_F.uidref());
      _ok:=params_exec('close','!fst_ewi_mokr',OKRO_F.ref(),_mp,1)
   || FUN.info('Nie znaleziono okresu do zamknięcia.'@)
   ?};
   OKRO_F.cntx_pop()
?};
{? _ok || _mp.done() ?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='';
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_out','#b_port'));
_okro_f:={? var_pres('OKRO_F',_in)>0 & _in.OKRO_F
         || _in.OKRO_F
         |? var_pres('OKRO_F',_out)>0 & _out.OKRO_F
         || _out.OKRO_F
         || null
         ?};
{? _okro_f
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKRO_F.seek(_okro_f,)
   || _desc:='Zamknij okres obszaru Środki trwałe: %1 %2.'@@[OKRO_F.NAZ,OKRO_F.ROK().NAZ]
   || _desc:='Uwaga błąd - nie odnaleziono okresu obrachunkowego.'@@
   ?};
   OKRO_F.cntx_pop()
|| _desc:='Uwaga błąd – nie znaleziono rekordu kluczowego, ani parametru wejściowego'@@
?};
_desc


\m_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zamykanie okresu w obszarze środków trwałych - Zamknij okres
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| _b_dom:=exec('szuk_b_dom','parses','FST');
   {? ~_b_dom
   || FUN.info('W systemie nie znaleziono zdefiniowanego obszaru Środki trwałe.'@);
      return(0)
   ?};
   OKRO_F.prefix();
   OKR_OBSZ.index('UNIK');
   OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
   {? _b_dom & OKR_OBSZ.first()
   || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
      OKR_OBSZ.OKRO_F()
   || FUN.info('Nie znaleziono dla wybranego wspólnego okresu odpowiedniego okresu obszaru Środki trwałe.'@);
      return(0)
   ?}
?};
{? OKRO_F.AMOR='T'
|| FUN.info('Wskazany okres jest już zamknięty.'@);
   return(0)
?};
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FST_EWI_MOKR';
_params.AKCJA:='zamknij';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.UIDREF:=OKRO_F.uidref();
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',OKRO_F.ref());
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła zamyka okres w obszarze środków trwałych
::   WE: _a - wskazanie na rekord OKRO_F, _b - obiekt odpowiedzialny za obługę procesu
::       _c - pokazywać komunikaty
::   WY: 0 - nie udało się, 1 - udało się
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
OKRO_F.cntx_psh();
OKR_OBSZ.cntx_psh();
{? type_of(_a)=type_of(null) & OKRO_F.seek(_a)
|| OKR_OBSZ.index('OKRO_F2');
   _fst:=exec('szuk_b_dom','parses','FST');
   OKR_OBSZ.prefix(_fst,OKRO_F.ref());
   {? OKR_OBSZ.first()
   || _okres:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ;
      {? ~_c | (_c & FUN.ask('Zamknąć okres: %1?'@[_okres]))
      || {? OKRO_F.AMOR<>'T'
         || {? exec('okr_test_close','!fst_ewi_mokr',_a)
            || {? exec('srst_amort','!fst_ewi_mokr',_a)
               || _wy:=exec('hist_close','!fst_ewi_mokr',_a)
               |? _c
               || FUN.emsg('We wskazanym okresie nie naliczono amortyzacji dla wszystkich środków.\n'
                           'Okres nie może zostać zamknięty.'@)
               ?}
            |? _c
            || FUN.emsg('Wskazany okres nie może zostać zamknięty.\n'
                        'Istnieją wcześniejsze otwarte okresy w obszarze Środki trwałe.'@)
            ?}
         || {? _c || FUN.emsg('Wskazany okres jest już zamknięty'@) ?};
            _wy:=1
         ?}
      ?}
   |? _c
   || FUN.emsg('Nie znaleziono wskazanego okresu w obszarze środków trwałych.'@); _wy:=1
   ?}
|? _c
|| FUN.emsg('Nie znaleziono wskazanego okresu.'@)
?};
OKR_OBSZ.cntx_pop();
OKRO_F.cntx_pop();
_wy


\okr_test_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła weryfikuje czy można zamknąć okres w obszarze FST, przeprowadzając
::       test ciągłości zamykania okresów - sprawdza czy wszystkie poprzednie są zamknięte.
::   WE: ref OKRO_F
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_wy:=1;
OKRO_F.cntx_psh(); OKR_OBSZ.cntx_psh();
OKRO_F.prefix();
{? OKRO_F.seek(_a)
|| _nr:=OKRO_F.NR;
   _rok:=OKRO_F.ROK;
   OKR_OBSZ.index('OKRO_ES2');
   _fst:=exec('szuk_b_dom','parses','FST');
   OKR_OBSZ.prefix(REF.FIRMA,_fst);
   {? OKR_OBSZ.find_key(_rok,_nr)
   || {! |?
:: pomija bilans otwarcia i bilans zamknięcia w przypadku FST
         {? OKR_OBSZ.OKRO_F<>_a & OKR_OBSZ.OKRO_F().POCZ<>date(0,0,0) & OKR_OBSZ.OKRO_F().AMOR<>'T'
         || _wy:=0
         ?};
         _wy & OKR_OBSZ.prev()
      !}
   ?}
?};
OKR_OBSZ.cntx_pop(); OKRO_F.cntx_pop();
_wy


\srst_amort
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła weryfikuje czy dla wszystkich środków we wskazanym okresie naliczono amortyzację
::       Dla okresu obrachunkowego BO automatycznie ustawia znacznik naliczenia amortyzacji na T
::       nie blokując zamykania okresu.
::   WE: ref OKRO_F
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_wy:=1;
SRST.cntx_psh();
OKRO_F.cntx_psh();
OKRO_F.prefix();
{? OKRO_F.seek(_a)
|| SRST.index('ROK');
   SRST.prefix(OKRO_F.ROK,OKRO_F.ref());
   {? SRST.first()
   || {! |?
         {? SRST.NAL<>'T' & SRST.OKRO_F().POCZ<>date(0,0,0)
         || _wy:=0
         |? SRST.NAL<>'T' & SRST.OKRO_F().POCZ=date(0,0,0)
         || SRST.NAL:='T';
            SRST.put()
         ?};
         _wy=1 & SRST.next()
      !}
   ?}
|| _wy:=0
?};
OKRO_F.cntx_pop();
SRST.cntx_pop();
_wy


\hist_zamkt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK, PJ [17.00]
:: OPIS: Historia zamknięć - środki trwałe
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
_b_dom:=exec('szuk_b_dom','parses','FST');
{? ~_b_dom
|| FUN.info('W systemie nie znaleziono zdefiniowanego obszaru Środki trwałe.'@);
   return(0)
?};
_dalej:=1;

OKRO_F.cntx_psh(); OKR_OBSZ.cntx_psh();
OKRO_F.index('ROK');

{? _a=1
|| OKRO_F.prefix();
   OKR_OBSZ.index('UNIK');
   OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
   {? _b_dom & OKR_OBSZ.first()
   || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
      OKR_OBSZ.OKRO_F()
   || FUN.info('Nie znaleziono dla wybranego wspólnego okresu odpowiedniego okresu obszaru Środki trwałe.'@);
      _dalej:=0
   ?}
?};

{? _dalej
|| OKRESY.ZO:=OKRO_F.ref();
   K__HIST.cntx_psh();
   K__HIST.index('OKRO_F');
   K__HIST.prefix(_b_dom,OKRESY.ZO);
   {? _a=1 || K__HIST.win_sel('SEL_FST') |? _a=2 || K__HIST.win_sel('SEL_FST2') ?};
   K__HIST.win_edit('RED1');
   {? OKRO_F.AMOR='N' || K__HIST.actions('SEL_FST','O:O',,1) || K__HIST.actions('SEL_FST','',,1) ?};
   K__HIST.hdr_sel();
   _hdr:=' okresu %1 %2'@[OKRESY.ZO().NAZ,OKRESY.ZO().ROK().NAZ];
   K__HIST.hdr_sel(_hdr);
   K__HIST.select();
   K__HIST.cntx_pop();
   OKRESY.ZO:=null()
?};

OKRO_F.cntx_pop(); OKR_OBSZ.cntx_pop()


\hist_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zapis w historii zamknięć i awaryjnych otwarć okresów informacji o zamknięciu okresu
::   WE: _a - wskazanie na OKRO_F
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
_fst:=exec('szuk_b_dom','parses','FST');
{? _fst & _a
|| K__HIST.cntx_psh();
   OKRO_F.cntx_psh();
   OKRO_F.prefix();
   K__HIST.prefix();
   K__HIST.blank(1);
   K__HIST.STATUS:='Z';
   K__HIST.STATOP:='Zamknięcie';
   K__HIST.OKRO_F:=_a;
   K__HIST.B_DOMAIN:=_fst;
   K__HIST.DATA:=date();
   K__HIST.CZAS:=time();
   K__HIST.USER:=OPERATOR.USER;
   {? OKRO_F.seek(_a)
   || OKRO_F.AMOR:='T';
      {? OKRO_F.put() & K__HIST.add() || _wy:=1 ?}
   ?};
   OKRO_F.cntx_pop();
   K__HIST.cntx_pop()
?};
_wy

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:13 b4c2177a5f68d9c2a530b6afab7e0fc96d69bf2a5e1f0646aec3c27fc21a8b458b66b6e1b11248f6edda993b69a62bb989d33f1c5ea0c2806d175ffa520e23d20128446fcd425ffa11347257e6bc000f813bf85ce2142c6d788a033cf554cc90089a03327b84babd5267cad4b300d2907b5e3d6a6ca82a7bb5cb5055e00d7877

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ltr_tra_wwtr.fml
:: Utworzony: 03.09.2019
:: Autor: Markus
::======================================================================================================================
:: Zawartość: Utworzenie dyspozycji transportu w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,   symbol=TR_NAG,    type=_TR_NAG,  name=Dyspozycja transportu, required=T,    keyref=T
::# kind=WE,   symbol=CZY_PDF, type=STRING, name=Utwórz PDF,           required=N,    keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE,   symbol=PRN_MSK, type=STRING, name=Maska wydruku,        required=N,    keyref=N, fml_val="exec('rep_exec','#b_report','LTR_TRA_WWTR','ltr_tra_*','Wydruki dyspozycji transportu',-1)"
::# kind=WY,   symbol=DOKUM,   type=_DOKUM, name=Załącznik,            required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_akcja:=_mp.akcja();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' ?};
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || {? exec('ctrlMask','#b_report',_in.PRN_MSK,'ltr_tra_') || _in.PRN_MSK || '' ?}
      || ''
      ?};

{? var_pres('TR_NAG',_in)=type_of(null()) & _in.TR_NAG
||
:: Ustawienie kontekstu wg instancji elementu w procesie

   TR_NAG.cntx_psh();

   _use:=ref_name(_in.TR_NAG)<>TR_NAG.name();
   {? _use || exec('opentr','open_tab',(6+ref_name(_in.TR_NAG))+1,(8+ref_name(_in.TR_NAG))+2) ?};
   TR_NAG.prefix();
   {? TR_NAG.seek(_in.TR_NAG)
   ||
      _dokum:=exec('trnag_druk','!ltr_tra_wwtr',_czy_pdf,_msk);
      {? _czy_pdf & _dokum<>null()
      ||
         _out.DOKUM:=_dokum;
         _mp.save(,_out);
         {? ~_keep || _mp.done() ?}

      |? ~_czy_pdf & _dokum
      ||
         {? ~_keep || _mp.done() ?}
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};

   TR_NAG.cntx_pop()

||
   _mp.error('Brak parametru wejściowego TR_NAG.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('TR_NAG',_in)<>type_of(~~) & _in.TR_NAG
|| 'Utwórz PDF dyspozycji transportu: %1'@@[exec('record','#to_string',_in.TR_NAG)]
|| ''
?}


\trnag_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: wydruki dla dyspozycji transportu
::   WE: [_a] - 1-PDF 0-normalny wydruk
::       [_b] - maska wydruku
::   WY: dla _a=1: ref/null _a=0: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
_msk:={? var_pres('_b')=type_of('') || _b || '' ?};
_wyn:={? _a || null || 0 ?};

{? _msk<>''
|| _wydr:=_msk
|| _wydr:='ltr_tra_*'
?};

{? _a=1
||
   _symb:=exec('str_to_pth','#string',TR_NAG.SYM);
   _file:='Dyspozycja transportu '+_symb+'.pdf';
   _wydr:=rep_exec(_wydr,,,_file,1)
||
   _wydr:=exec('rep_exec','#b_report','LTR_TRA_WWTR',_wydr,'Dyspozycja transportu',2,,,,,,'W')
?};

{? _a=1 & _wydr=1
|| _wyn:=exec('save_dok','dokum','TR_NAG',_file,POLA.NAZ_WYDR,TR_NAG.SYM,
      ,'Dyspozycja transportu: '+TR_NAG.SYM,PARAM_W.JEZYK,'N')
|? _a=0
|| _wyn:=_wydr
?};

_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 e37e5815ed8e0ed7bad9855ada61cbd5e95aa897b29c93d8cac4d7438590de272b12eb1f0c3b540ed5d225d9edad1a94fbacfcab87c01a949f7e8b6b52ebb3624652dc84d35ce1f2f5396a2739cf97e7e0c0b49559412d732bb1533ea281c85ac36e5be72c3e42c079ba9a8a05b71580616727296053d94f1b29d90a3f949fb3

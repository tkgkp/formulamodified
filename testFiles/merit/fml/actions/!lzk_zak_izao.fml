:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zak_izao.fml
:: Utworzony: 14.04.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Zamknięcie okresu - Zakup
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=ROK,       type=NUMBER,   name=Rok,               required=N, fml_val="exec('rok','!lzk_zak_izao')"
::# kind=WE,   symbol=MIESIAC,   type=NUMBER,   name=Miesiąc,           required=N, fml_val="exec('miesiac','!lzk_zak_izao')"
::# kind=WY,   symbol=WYNIK,     type=MEMO,     name=Wynik zamknięcia,  required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: Uruchomiona akcja
_akcja:=_mp.akcja();

:: Uruchomiona automatycznie
_auto:=_akcja<>'Zamknij' & _mp.isAutoRun();

_rok:={? var_press('ROK',_in)=type_of(0) & _in.ROK || _in.ROK || ST.AR ?};
_miesiac:={? var_press('MIESIAC',_in)=type_of(0) & _in.MIESIAC || _in.MIESIAC || ST.AM ?};

_out.WYNIK:='';

OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_rok,_miesiac);
{? OKR.first()
|| _zam_mag:=1+OKR.ZAM;
   _zam_spr:=1+(1-OKR.ZAM);
   _zam_zak:=1+(2-OKR.ZAM);
   _zam_umo:=OKR.ZAM+1;
   _Tab:=sql($"
      select
         FAKS.ODDZ ODDZ,
         KH.KOD KH,
         FAKS.SYM SYM
      from
         @FAKS
         left join KH using(FAKS.KH,KH.REFERENCE)
      where
         substr(FAKS.REFERENCE,7,2)<>'hs'
         and FAKS.D>=to_date(':_a') and FAKS.D<=to_date(':_b')
         and (FAKS.STAT_REJ='N' or FAKS.STAT_REJ='Z')
         and FAKS.SZ='Z'
         and :_c=1"
      ,$date(OKR.ROK,OKR.MC,1),$date(OKR.ROK,OKR.MC,0),_zam_zak='N');
   {? _Tab.first()
   || _txt:='Niezaakceptowane dokumenty zakupu'@;
      _out.WYNIK+=':\n';
      _loop:=_Tab.first();
      {!
      |? _loop
      |!
         _out.WYNIK+='\n'+_Tab.SYM;
         _loop:=_Tab.next()
      !};
      _wer:=_Tab.mk_sel(_txt,,0,'okr_zam_zak');
      _Tab.win_fld(_wer,,'ODDZ',,,10,,,'Oddział'@);
      _Tab.win_fld(_wer,,'KH',,,10,,,'Kontrahent'@);
      _Tab.win_fld(_wer,,'SYM',,,20,,,'Symbol'@);
      _Tab.win_act(_wer,,'Formuła','Zamknij okno'@@,,,"sel_exit()",,1);
      _Tab.win_sel(_wer);
      _Tab.select()
   ?};
   {? _zam_zak='T'
   || FUN.info('Okres %1 jest już zamknięty.'@[OKR.NAZ]);
      _mp.done()
   |? FUN.ask('Zamknąć okres %1?'@[OKR.NAZ])
   || OKR.get();
      OKR.ZAM:=_zam_mag+_zam_spr+'T'+_zam_umo;
      {? OKR.put()
      || _out.WYNIK+={? +_out.WYNIK || '\n' || '' ?}+'Okres %1 został zamknięty.'@[OKR.NAZ];
         _mp.done()
      ?}
   ?}
|| _out.WYNIK+='Brak okresu %1/%2'@[form(_miesiac,-2,,'9 '),form(_rok,-4,,'9 ')];
   _mp.done()
?};
OKR.cntx_pop();
_mp.save(,_out)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

_rok:={? var_press('ROK',_in)=type_of(0) & _in.ROK || _in.ROK || 0 ?};
_miesiac:={? var_press('MIESIAC',_in)=type_of(0) & _in.MIESIAC || _in.MIESIAC || 0 ?};

OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_rok,_miesiac);
_wyn:=
   {? OKR.first()
   || 'Zamknij okres %1 w zakupach'@@[OKR.NAZ]
   || 'Zamknij okres w zakupach'@@
   ?};
OKR.cntx_pop();
_wyn


\zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zamknięcie okresu - Zakup
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LZK_ZAK_IZAO';
_params.AKCJA:='Zamknij';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);

OKR.cntx_psh();
OKR.prefix();
{? OKR.seek(exec('zwr_obs_okres','parses','LZK',OKRO.ref()))
||
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ROK',OKR.ROK);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'MIESIAC',OKR.MC);

   exec('mp_run','#b__box',_params)
|| FUN.info('Okres nie jest otworzony dla zakupów.'@)
?};
OKR.cntx_pop()


\rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła parametru - ROK
::   WE:
::   WY: rok lub ~~ jeśl nie wybrano roku
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
OKR.last();
_wer:=OKR.mk_sel('Wybór roku'@,,0,,,,OKR.size());
OKR.win_fld(_wer,,'ROK',,,20,,,'Rok'@);
OKR.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
OKR.win_sel(_wer);
{? OKR.select(,1,OKR.size()) || _wyn:=OKR.ROK ?};
OKR.cntx_pop();
_wyn


\miesiac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła parametru - OKRES
::   WE:
::   WY: miesiąc lub ~~ jeśl nie wybrano roku
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
_Tab:=tab_tmp(1
   ,'MC'    ,'INTEGER'     ,'Nr miesiąca'
   ,'NAZ'   ,'STRING[20]'  ,'Nazwa miesiąca');
{! _ii:=1..12 |! _Tab.MC:=_ii; _Tab.NAZ:=(date(,_ii)$8)-5; _Tab.add() !};
_Tab.find_key(date()~2);
_wer:=_Tab.mk_sel('Wybór miesiąca'@,,0,,,,12);
_Tab.win_fld(_wer,,'NAZ',,,,,,'Miesiąc'@);
_Tab.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_Tab.win_sel(_wer);
{? _Tab.select(,1,12) || _wyn:=_Tab.MC ?};
_wyn

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:58 6753050dea9e79a344e142952900191a364667d8a4bfe4488170ce3644671c9cbd0d54261e89b9777d4a7b80f277978d7bca8b9c1225d6b07c0938f73c86a10fc1df2f99bfef86294976205e414036e4f7e36eeb915684c923a1d71cd3d89bb07f57beeef268dbe1a3425b63ddfd5a5c092de3eb17ae46dd4a461b495236c84b

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_vat_droz.fml
:: Utworzony: 13.07.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VAT_DROZ - Rozliczenie zawieszonego VATu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rozliczenie zawieszonego VATu - główna formuła czynności FKS_VAT_DROZ.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\rozlvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Formula do rozliczenia nie rozliczonych dokumentow VAT
::   WE:  _a - 0 cały dokument, 1 biezaca pozycja VAT dokumentu
::      [_b] - czy dokument do rozliczenia jako operacja nie rozliczana w VAT
::  OLD: \rozlvat/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=1 || _b:=0 ?}; _czesc:=0; POMOC.KNAG:=0;
_ok:={? _a=0 & exec('is_dok_rozl_cz','fks_vat',ROZLVAT.DOP)
     || FUN.info('Dokument zawiera częściowo rozliczone pozycje VAT.'@);
        0
     |? _a=0 & exec('is_vpoz_okr','fks_vat')
     || 0
     |? ROZLVAT.OKRO().ROK<>SSTALE.AR & ~exec('czy_rvat','fks_vat',SSTALE.AR,ROZLVAT.ODD,ROZLVAT.RVAT)
     || FUN.info('W aktywnym roku w jednostce księgowej %1\nrejestr VAT: %2'
                 ' nie istnieje.\nRozliczenie niemożliwe.'@[ROZLVAT.ODD().OD,ROZLVAT.RVAT().SYM]); 0
     |? _a=1 & VPOZ.OKR_C<>null & VPOZ.OKR_Z<>null
     || FUN.info('Rozliczono już podatek należny i naliczony.'@);
        0
     || 1
     ?};
_b_kor:=_b & -ROZLVAT.DOK().DOK_REJ().KOR='t';
_typvat:='';
{? _ok &
      (_b=0 & {? _a=1
              || {? VPOZ.OKR_Z=null & VPOZ.OKR_C<>null
                 || FUN.ask('Czy rozliczyć podatek należny pozycji dokumentu?'@)
                 |? VPOZ.OKR_C=null & VPOZ.OKR_Z<>null
                 || FUN.ask('Czy rozliczyć podatek naliczony pozycji dokumentu?'@)
                 || _pyt:=FUN.choice('Czy rozliczyć pozycję dokumentu?'@,,'Podatek należny i naliczony','Podatek należny','Podatek naliczony');
                    {? _pyt=2
                    || _typvat:='Z'
                    |? _pyt=3
                    || _typvat:='C'
                    ?};
                    _pyt
                 ?}
              || FUN.ask('Czy rozliczyć dokument?'@)
              ?} |
       _b=1 &
              {?  _b_kor
              || FUN.ask('Czy rozliczyć \nkorektę VAT'
                     '\n jako dokument nie rozliczany w VAT.'@)
              || FUN.ask('Czy rozliczyć \nDOKUMENT VAT'
                     '\n jako dokument nie rozliczany w VAT.'@)
              ?}
     )
|| _ok:=(ROZLVAT.OKRO().ROK().POCZ_ROK < SSTALE.AR().POCZ_ROK
        | (ROZLVAT.OKRO().ROK=SSTALE.AR &
        ROZLVAT.OKRO().NR<=SSTALE.AO().NR));
   {? ~_ok
   || _ok:=FUN.ask('\nDokument pochodzi z okresu %1 '
           '.\nCzy rozliczać w okresie %2'
           '\nwcześniejszym (%3 %4)?'@[ROZLVAT.OKRO().NAZ,ROZLVAT.OKRO().ROK().NAZ,SSTALE.AO().NAZ,SSTALE.AR().NAZ])
   ?};
   {? _ok
   || ROK_F.cntx_psh(); OKRO_F.cntx_psh();
      {? ROZLVAT.OKRO().ROK<>SSTALE.AR & ROZLVAT.OKRO().ROK().PROC_VAT<>SSTALE.AR().PROC_VAT
      || {? _a=1
         || {? exec('gr_pod_typ','dok_fks',VPOZ.GRVAT().GRVAT().KOD)=1 || _czesc:=1 ?}
         || VPOZ.cntx_psh();
            VPOZ.index('VDOK'); VPOZ.prefix(ROZLVAT.DOK);
            {? VPOZ.first()
            || {! |?
                  {? VPOZ.OKRVAT=null & exec('gr_pod_typ','dok_fks',VPOZ.GRVAT().GRVAT().KOD)=1 || _czesc:=1 ?};
                  ~_czesc & VPOZ.next()
               !}
            ?};
            VPOZ.cntx_pop()
         ?}
      ?};
      ROK_F.cntx_pop(); OKRO_F.cntx_pop()
   ?};
   {? _ok & ROZLVAT.get()
   || _RozlVatDok:=ROZLVAT.DOK;
      exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
      DOK.prefix();
      ROZLVAT.DOK();
      {? ROZLVAT.DOK().DOK_REJ().KOR_NAG='T' & 4+DOK.KN='doku'
      || _ref:=BB.sqlint(DOK.KN);
         _maska:=(8+DOK.KN)+4
      || _ref:=0
      ?};
      {? exec('kor_spr','dok_fks',1)
      || undefine();
         define('DAT_R',DOK.D4,'Potwierdzenie odbioru:'@,,10);
         _ok:=def_edit("exec('chk_roz','!fks_vat_droz')",'Podaj'@);
         {? _ok
         || DOK.D4:=DEFINE.DAT_R;
            _ok:=DOK.put()
         ?};
         undefine()
      || _ok:=1
      ?};
      {? _ok
      || KOMT:='';
         {? _a=1
         || {? exec('rozvat2','!fks_vat_droz',2,0,_typvat)
            || {? _czesc
               || FUN.info('Pozycja VAT została rozliczona w miesiącu \n%1 %2.'
                           '\n\nUwaga: istnieje przynajmniej jedna pozycja dla której liczono strukturę podatku VAT\n'
                           'wg procentu z innego roku obrachunkowego.'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ])
               || FUN.info('Pozycja VAT została rozliczona w miesiącu \n%1 %2.'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ])
               ?}
            || FUN.info('Pozycja VAT nie została rozliczona.%1'@[KOMT])
            ?}
         || {? exec('rozvat2','!fks_vat_droz',0,_=2 & _b,_typvat)
            || {? _czesc
               || FUN.info('Dokument VAT został rozliczony w miesiącu \n%1 %2.'
                           '\n\nUwaga: W pozycji liczono strukturę podatku VAT\n'
                           'wg procentu z innego roku obrachunkowego.'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ])
               || FUN.info('Dokument VAT został rozliczony w miesiącu \n%1 %2.'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ])
               ?}
            || FUN.info('Dokument VAT nie został rozliczony. %1'@[KOMT])
            ?}
         ?};
         &KOMT
      ?};
      exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR);

:: #VATKN dodatkowo - rozliczenie całkowite dokumentu powiązanego z korektą nagłówkową     
      _CzyTezKor:='';   
      {? _ref
      || POMOC.KNAG:=2;
         DOK.cntx_psh(); VPOZ.cntx_psh();
         DOK.use('doku'+_maska); DOK.prefix();
         VPOZ.use('pozv'+_maska);
         _ok:=_ref<>0 & DOK.seek(_ref,) & DOK.ZP='T';
         {? _ok 
         || _CzyTezKor:={? DOK.DOK_REJ().KOR='T' || DOK.KOR || '' ?};
            OKRO_F.cntx_psh(); ROK_F.cntx_psh(); 
            OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
           _ok:={? OKRO_F.find_ge(DOK.DTW)
                || _okr:=OKRO_F.ref();
                   1
                || 0
                ?};
           OKRO_F.cntx_pop(); ROK_F.cntx_pop();
           {? _ok || exec('rozvat5','!fks_vat_droz',_okr) ?}
         ?};
         DOK.cntx_pop(); VPOZ.cntx_pop();

         {? _ok & _CzyTezKor<>''
         || _ok:=0; _ref:=null;
            DVAT.cntx_psh(); _ind1:=DVAT.ndx_tmp(,1,'SYM1',,0,'SYM1',,0); DVAT.index(_ind1); DVAT.prefix(_CzyTezKor);
            {? DVAT.first()
            || {? exec('FindAndGet','#table','DOK',DVAT.DOK,,"LKN",'')=$_RozlVatDok
               || _ok:=1; _ref:=DVAT.DOK
               ?}
            ?};
            DVAT.cntx_pop(); DVAT.ndx_drop(_ind1);
            DOK.cntx_psh(); VPOZ.cntx_psh();
            {? _ok & _ref<>null
            || _maska:=4-ref_name(_ref);
               DOK.use('doku'+_maska); DOK.prefix();
               VPOZ.use('pozv'+_maska);
               _ok:=_ref<>0 & DOK.seek(_ref,) & DOK.ZP='T'
            ?};
            {? _ok
            || POMOC.KNAG:=2;
               OKRO_F.cntx_psh();
               _okr:=exec('szuk_okr','okresy',DOK.DTW);
               {? _okr
               || exec('rozvat5','!fks_vat_droz',_okr)
               ?};
               OKRO_F.cntx_pop()
            ?};
            DOK.cntx_pop(); VPOZ.cntx_pop()
         ?}
      ?}
    ?}
?}


\chk_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Kontrola daty otrzymania dokumentu
::  OLD: \chk_roz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? DEFINE.DAT_R<>date(0,0,0)
|| {? exec('po_dtd','dok_fks',DEFINE.DAT_R,1)
   || {? DEFINE.DAT_R<DOK.DTO
      || FUN.info('Data otrzymania wcześniejsza od daty wystawienia.'@);0
      || 1
      ?}
   ?}
|| FUN.ask('Nie podano daty potwierdzenia odbioru korekty.\nRozliczyć dokument?'@)
?}


\rozvat2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Rozliczenie z ROZLVAT do rejestrów VAT
::   WE:  _a  - 0 dla dokumentu, 1 automatyczne rozliczenie, 2 dla pozycji dokuemntu
::       [_b] - czy usuniecie z kartoteki VAT
::       [_c] - typ rozliczanego podatku: 'Z'-należny 'C'-naliczony
::  OLD: \rozvat2/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=1 || _b:=0 ?};
{? var_press('_c')<=0 || _c:='' ?};
do();
{? -DOK.ZP<>'t'
|| undo();
   KOMT:='\nDokument '+DOK.NK+' nie jest zaksięgowany.'
|| VPOZ.index('VDRV');
   {? DOK_REJ.KOR_NAG='T' & 4+DOK.KN='doku'
   || VPOZ.prefix(DOK.ref)
   || VPOZ.prefix(DOK.ref,ROZLVAT.RVAT,ROZLVAT.DOP)
   ?};
   {? _a=1 & 1+VAT7.EWI().KOD='Z' & VPOZ.first
   || {! |?
         {? (TYM_ROZ.VPOZ=VPOZ.ref() | TYM_ROZ.VPOZ=0)
            & 1+TYM_ROZ.CO='!' & exec('get_par','slo_slu',VPOZ.A_VAT,5)='T'
         || VPOZ.GRVAT:=exec('zm_gr','!fks_vat_droz',VPOZ.GRVAT().KOD,DOK.REJ);
            VPOZ.put()
         ?};
         VPOZ.next
      !}
   ?};
   {? _a<>2
   || VPOZ.cntx_psh();
      {? VPOZ.first()
      || {? _a=1 & TYM_ROZ.BRUTTO
         || _brutto:=0;
            {!
            |? {? ~exec('is_rozl_cz','fks_vat',SSTALE.AO)
               || _brutto+=VPOZ.BRUTTO
               ?};
               VPOZ.next()
            !};
            VPOZ.first();
            _brutto_cz:=0
         ?};
         {! |?
            {? (_a=0 | TYM_ROZ.VPOZ=VPOZ.ref() | TYM_ROZ.VPOZ=0) &
               ( VPOZ.OKRVAT=null & ~exec('is_rozl_cz','fks_vat',SSTALE.AO))
            || {? _a=1 & TYM_ROZ.BRUTTO
               || _v:=(VPOZ.BRUTTO/_brutto*TYM_ROZ.BRUTTO)$2;
                  _brutto_cz+=_v;
                  exec('vpoz_roz_add','!fks_vat_droz',_v,1)
               |? _a=1 & exec('is_rozl_cz','fks_vat')
               || {? exec('vpoz_roz_add','!fks_vat_droz',0,2)
                  || exec('vat_ks1','dok_fks_ks',ROZLVAT.OKRO,_b,1,'',ROZLVAT.DOP)
                  ?}
               || exec('rozvatpoz','!fks_vat_droz',_b,_c)
               ?}
            ?};
            VPOZ.next
         !};
         {? _a=1 & TYM_ROZ.BRUTTO
         || _r:=TYM_ROZ.BRUTTO-_brutto_cz;
            {? _r
            || exec('vpoz_roz_add','!fks_vat_droz',VPOZ_ROZ.BRUTTO+_r,0)
            ?};
            VPOZ_ROZ.index('VPOZ_ROZ');
            {? VPOZ.first()
            || {!
               |? VPOZ_ROZ.prefix(VPOZ.ref(),SSTALE.AO);
                  {? VPOZ_ROZ.first()
                  || exec('vat_ks1','dok_fks_ks',ROZLVAT.OKRO,_b,1,'',ROZLVAT.DOP)
                  ?};
                  VPOZ.next()
               !}
            ?}
         ?}
      ?};
      VPOZ.cntx_pop()
   || exec('rozvatpoz','!fks_vat_droz',_b,_c)
   ?};
   _rozl:=exec('rozvat3','!fks_vat_droz')
?};
end() & (_a<>0 | _rozl)


\rozvatpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [17.28]
:: OPIS: Rozliczenie z ROZLVAT do rejestrów VAT pozycji VAT
::   WE: _a - czy usuniecie z kartoteki VAT
::       _b - typ podatku
::  OLD: \rozvatpoz/dok_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? VPOZ.OKR_Z=null & VPOZ.OKR_C<>null | _b='Z'
|| {? ~VPOZ.OKRVAT & VPOZ.OKR_C=SSTALE.AO
   || VPOZ.OKRVAT:=SSTALE.AO;
      VPOZ.OKR_Z:=VPOZ.OKR_C:=null
   || VPOZ.OKR_Z:=SSTALE.AO
   ?};
   VPOZ.A_VAT:=null;
   VPOZ.put();
   exec('vat_ks1','dok_fks_ks',ROZLVAT.OKRO,_a,,'Z',ROZLVAT.DOP)
|? VPOZ.OKR_C=null & VPOZ.OKR_Z<>null | _b='C'
|| {? ~VPOZ.OKRVAT & VPOZ.OKR_Z=SSTALE.AO
   || VPOZ.OKRVAT:=SSTALE.AO;
      VPOZ.OKR_Z:=VPOZ.OKR_C:=null
   || VPOZ.OKR_C:=SSTALE.AO
   ?};
   VPOZ.A_VAT:=null;
   VPOZ.put();
   exec('vat_ks1','dok_fks_ks',ROZLVAT.OKRO,_a,,'C',ROZLVAT.DOP)
|| VPOZ.OKRVAT:=SSTALE.AO;
   VPOZ.A_VAT:=null;
   VPOZ.put;
   exec('vat_ks1','dok_fks_ks',ROZLVAT.OKRO,_a,0,'',ROZLVAT.DOP)
?}


\rozvat3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Usuniecie ROZLVAT gdy rozliczono calkowicie dokument
::  OLD: \rozvat3/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
VPOZ.cntx_psh();
VPOZ.index('VDRV'); VPOZ.prefix(DOK.ref,ROZLVAT.RVAT,ROZLVAT.DOP);
_rozl:=1;
{? VPOZ.first()
|| exec('dok_akt','fks_vat');
   {!
   |? {? VPOZ.OKRVAT=null & (VPOZ.OKR_Z=null | VPOZ.OKR_C=null) & ~exec('is_rozl_cz_full','fks_vat') || _rozl:=0 ?};
      _rozl & VPOZ.next
   !}
?};
VPOZ.cntx_pop();
{? _rozl
|| ROZLVAT.del();
   grp_disp(ROZLVAT,'WER',1);
   1
?}


\rozvat5
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.XX]
:: OPIS: Dodatkowy zapis przy rozliczaniu korekty nagłówkowej z ROZLVAT na minus w DVAT pierwotnym #VATKN
::   WE: _a - ref okresu z jakiego pochodzi dokument pierwotny
::----------------------------------------------------------------------------------------------------------------------
VPOZ.cntx_psh();
VPOZ.index('VDRV'); VPOZ.prefix(DOK.ref());
_rozl:=1;
{? VPOZ.first()
|| {!
   |? {? VPOZ.OKRVAT<>null & VPOZ.KNAG
      || exec('vat_ks1','dok_fks_ks',_a,VPOZ.OKRVAT,,,VPOZ.D)
      ?};
      VPOZ.next()
   !}
?};
VPOZ.cntx_pop()



\zm_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Wyszukanie grupy podatkowej bez odliczenia podatku VAT
::   WE: _a - symbol dotyczczasowej grupy
::       _b - rejestr dokumentu (ref REJ)
::  OLD: \zm_gr/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_gr:={? _a='ZInwePod' | _a='ZInwPodZ'
     || 'ZInwNPod'
     |? _a='ZakuPodZ' | _a='ZakupPod'
     || 'ZPozNPod'
     |? _a='ZakPRopZ' | _a='ZakuPRop'
     || 'ZakupNpr'
     || _a
     ?};
GR_VAT.index('REJ_KOD');
GR_VAT.prefix();
{? GR_VAT.find_key(_b,_gr) || GR_VAT.ref() || null ?}


\vpoz_roz_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Dodanie automatycznie czesciowego rozliczenia VAT
::   WE: _a - kwota brutto
::       _b - 1-add 0-put 2-add automatycznie
::   WY: Czy operacja sie udala? 1-tak 0-nie
::  OLD: \vpoz_roz_add/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
VPOZ_ROZ.prefix();
{? _b
|| VPOZ_ROZ.blank(1);
   VPOZ_ROZ.VPOZ:=VPOZ.ref();
   VPOZ_ROZ.OKRO_F:=SSTALE.AO;
   VPOZ_ROZ.GR_VAT:=VPOZ.GRVAT
?};
{? _b=2
|| exec('bv_rozl_cz','fks_vat');
   VPOZ_ROZ.BRUTTO:=ROZLICZ.BRUTTO2;
   VPOZ_ROZ.NETTO:=ROZLICZ.NETTO2;
   VPOZ_ROZ.VAT:=ROZLICZ.VAT2;
   {? ROZLICZ.BRUTTO2=0 & ROZLICZ.NETTO2=0 & ROZLICZ.VAT2=0
   || return(0)
   ?}
|| VPOZ_ROZ.BRUTTO:=_a;
   VPOZ_ROZ.NETTO:=(VPOZ_ROZ.BRUTTO/(1+0.01*exec('vat','dok_fks',VPOZ.PR().KOD)))$2;
   VPOZ_ROZ.VAT:=(VPOZ_ROZ.BRUTTO-VPOZ_ROZ.NETTO)$2
?};
exec('setvatodlroz','!fks_vat_droz');
_vref:=VPOZ_ROZ.VPOZ;
_wyn:={? _b
      || VPOZ_ROZ.add()
      || VPOZ_ROZ.put()
      ?};
exec('is_vpoz_czrozl','fks_vat',_vref);
_wyn


\setvatodlroz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawia wartosci pol VAT_ODL i VATKOSZT tabeli VPOZ_ROZ
::  OLD: \setvatodlroz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:=exec('gr_pod_typ','dok_fks',VPOZ_ROZ.GR_VAT().GRVAT().KOD);
{? _typ=0
|| VPOZ_ROZ.VAT_ODL:=0
|? _typ=2
|| VPOZ_ROZ.VAT_ODL:=VPOZ_ROZ.VAT
|? _typ=1
|| _proc:={? PAR_SKID.get(83)='T' & VPOZ_ROZ.C_PROC_S=0
          || 100
          || {? VPOZ_ROZ.OKRO_F
             || VPOZ_ROZ.OKRO_F().ROK().PROC_VAT
             || SSTALE.AR().PROC_VAT
             ?}
          ?};
   _prewsk:={? PAR_SKID.get(83)='T' & VPOZ_ROZ.C_PREWSK
            || {? VPOZ_ROZ.OKRO_F
               || VPOZ_ROZ.OKRO_F().ROK().PREWSK
               || SSTALE.AR().PREWSK
               ?}
            || 100
            ?};
   {? (SLO.KOD='ZakuPodS' | SLO.KOD='ZInwPodS')
   || VPOZ_ROZ.VAT_ODL:=(VPOZ_ROZ.VAT*(_proc/100)*(_prewsk/100)*(50/100))$2
   |? (SLO.KOD='ZakuPods' | SLO.KOD='ZInwPods')
   || VPOZ_ROZ.VAT_ODL:=(VPOZ_ROZ.VAT*(50/100))$2
   || VPOZ_ROZ.VAT_ODL:=(VPOZ_ROZ.VAT*(_proc/100)*(_prewsk/100))$2
   ?}
|| VPOZ_ROZ.VAT_ODL:=VPOZ_ROZ.VATKOSZT:=0;
   return(0)
?};
VPOZ_ROZ.VATKOSZT:=VPOZ_ROZ.VAT-VPOZ_ROZ.VAT_ODL;
1


\rozlauto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Akcja 'Automatyczne rozl.' w okienku wertowania tabeli ROZLVAT
::   WE: _a zadeklarowane i rowne 0 - uruchomienie "wirtualne"
::  OLD: \rozlauto/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<=0 || _a:=1 ?};
{? var_pres('TYM_ROZ')>0 || obj_del(TYM_ROZ) ?};
_ok:=0;
ROZLVAT.cntx_psh();
VPOZ.cntx_psh();
{? ~_a || ROZLVAT.index('REJ'); ROZLVAT.prefix() ?};
TYM_ROZ:=tab_tmp(2,'OK','STRING[1]','Zaznaczone',
                   'DATA','DATE','Data operacji'@,
                   'ROZLVAT','INTEGER','Ref ROZLVAT',
                   'RVAT_SYM','STRING[8]','Rejestr VAT'@,
                   'SYM','STRING[20]','Symbol dokumentu'@,
                   'CO','STRING[60]','',
                   'DOK','INTEGER','Ref DOK',
                   'VPOZ','INTEGER','Ref VPOZ',
                   'NETTO','REAL','Netto'@,
                   'VAT','REAL','VAT'@,
                   'REJ','STRING[8]','Rejestr'@,
                   'TRYB','STRING[45]','Tryb'@,
                   'DOK_POZ','STRING[1]','Dokument/Pozycja',
                   'DOK_POZ1','STRING[1]','Dokument/Pozycja',
                   'BRUTTO','REAL','Brutto'@,
                   'CZ','INTEGER','Częściowe'@
                );
_ind1:=TYM_ROZ.index('?');
{? ROZLVAT.first()
|| {!
   |? exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
      ROZLVAT.DOK();
      {? DOK.A_VAT<>null
      || {? (_co:=exec('czy_roz','!fks_vat_droz',DOK.A_VAT,DOK.ref()))<>''
         || TYM_ROZ.ROZLVAT:=#ROZLVAT.ref();
            TYM_ROZ.RVAT_SYM:=ROZLVAT.RVAT().SYM;
            TYM_ROZ.DATA:=ROZLVAT.DOP;
            TYM_ROZ.SYM:=ROZLVAT.SYM;
            TYM_ROZ.CO:=_co;
            TYM_ROZ.DOK:=DOK.ref;
            TYM_ROZ.VPOZ:=null;
            TYM_ROZ.NETTO:=0;
            TYM_ROZ.VAT:=0;
            TYM_ROZ.TRYB:=DOK.A_VAT().KOD+' - '+DOK.A_VAT().TR;
            TYM_ROZ.CZ:=0;
            VPOZ.index('VDRV'); VPOZ.prefix(DOK.ref,ROZLVAT.RVAT,ROZLVAT.DOP);
            {? VPOZ.first()
            || VPOZ_ROZ.index('VPOZ_ROZ');
               {!
               |? TYM_ROZ.NETTO+=VPOZ.NETTO;
                  TYM_ROZ.VAT+=VPOZ.VAT;
                  VPOZ_ROZ.prefix(VPOZ.ref());
                  {? VPOZ_ROZ.first()
                  || {? TYM_ROZ.CZ=0
                     || TYM_ROZ.CZ:=exec('is_rozl_cz','fks_vat',SSTALE.AO)
                     ?};
                     {!
                     |? TYM_ROZ.NETTO-=VPOZ_ROZ.NETTO;
                        TYM_ROZ.VAT-=VPOZ_ROZ.VAT;
                        VPOZ_ROZ.next()
                     !}
                  ?};
                  VPOZ.next()
               !}
            ?};
            {? TYM_ROZ.CZ
            || TYM_ROZ.OK:={? 1+TYM_ROZ.CO='!' || '!' || 'N' ?}
            ?};
            TYM_ROZ.REJ:=ROZLVAT.RVAT().SYM;
            TYM_ROZ.DOK_POZ:='D';
            TYM_ROZ.add()
         ?}
      || VPOZ.index('VDRV'); VPOZ.prefix(DOK.ref(),ROZLVAT.RVAT);
         {? VPOZ.first()
         || VPOZ_ROZ.index('VPOZ_ROZ');
            {!
            |? {? VPOZ.A_VAT & ~VPOZ.OKRVAT & ~exec('is_rozl_cz_full','fks_vat')
               || {? (_co:=exec('czy_roz','!fks_vat_droz',VPOZ.A_VAT,VPOZ.ref()))<>''
                  || TYM_ROZ.ROZLVAT:=#ROZLVAT.ref();
                     TYM_ROZ.RVAT_SYM:=ROZLVAT.RVAT().SYM;
                     TYM_ROZ.DATA:=ROZLVAT.DOP;
                     TYM_ROZ.SYM:=ROZLVAT.SYM;
                     TYM_ROZ.CO:=_co;
                     TYM_ROZ.DOK:=DOK.ref;
                     TYM_ROZ.VPOZ:=VPOZ.ref;
                     TYM_ROZ.NETTO:=VPOZ.NETTO;
                     TYM_ROZ.VAT:=VPOZ.VAT;
                     VPOZ_ROZ.prefix(VPOZ.ref());
                     {? VPOZ_ROZ.first()
                     || TYM_ROZ.CZ:=exec('is_rozl_cz','fks_vat',SSTALE.AO);
                        {? TYM_ROZ.CZ
                        || TYM_ROZ.OK:={? 1+TYM_ROZ.CO='!' || '!' || 'N' ?}
                        ?};
                        {!
                        |? TYM_ROZ.NETTO-=VPOZ_ROZ.NETTO;
                           TYM_ROZ.VAT-=VPOZ_ROZ.VAT;
                           VPOZ_ROZ.next()
                        !}
                     ?};
                     TYM_ROZ.REJ:=ROZLVAT.RVAT().SYM;
                     TYM_ROZ.TRYB:=VPOZ.A_VAT().KOD+' - '+VPOZ.A_VAT().TR;
                     TYM_ROZ.DOK_POZ:='P';
                     TYM_ROZ.add()
                  ?}
               ?};
               VPOZ.next()
            !}
         ?}
      ?};
      ROZLVAT.next()
   !};
   _ok:=1;
   {? TYM_ROZ.first()
   || {? _a
      || TYM_ROZ.prefix();
         _wer:=TYM_ROZ.mk_sel('Dokumenty (pozycje) do automatycznego rozliczenia'@,'P',,'tym_roz_wer',,,,,'U');
         TYM_ROZ.win_fld(_wer,,'DATA',,,,,,,,'Data operacji'@);
         TYM_ROZ.win_fld(_wer,,'RVAT_SYM',,,,,,,,'Symbol rejestru VAT'@);
         TYM_ROZ.win_fld(_wer,,'SYM',,,,,,,,'Symbol dokumentu'@);
         TYM_ROZ.win_fld(_wer,,'CO',,,,,,'Sposób kwalifikacji'@,,'Opis sposobu kwalifikacji rozliczenia'@);
         TYM_ROZ.win_fld(_wer,,'NETTO',,,18,2,,,,'Kwota netto do rozliczenia'@);
         TYM_ROZ.win_fld(_wer,,'VAT',,,18,2,,,,'Kwota VAT do rozliczenia'@);
         TYM_ROZ.win_fld(_wer,,'OK',,,3,,,,,'Znacznik kwalifikacji'@,2,,"'T'","'N'");
         TYM_ROZ.win_fld(_wer,,'DOK_POZ1',,,3,,,,,'Dokument / Pozycja dokumentu'@);
         TYM_ROZ.win_fml(_wer,,'DOK_POZ1',,'ICON_BEFORE',"{? TYM_ROZ.DOK_POZ='D' || 'xwin16.png:74' || 'xwin16.png:76' ?}");
         TYM_ROZ.win_sel(_wer);
         TYM_ROZ.win_act(_wer,,'Formuła','Rozlicz zaznaczone'@@,,
                         'Rozliczenie zaznaczonych dokumentów (pozycji)'@,,"sel_exit",,,,,'R');
         TYM_ROZ.win_act(_wer,,'Wyświetl',,,,,
                         "{? ROZLVAT.seek(TYM_ROZ.ROZLVAT,ROZLVAT.name())
                          || exec('rozldok','!fks_vat_zroz')
                          || FUN.info('Brak rozliczenia VAT.'@)
                          ?}");
         TYM_ROZ.win_act(_wer,,'Formuła','&Zaznacz'@@,,'Zaznaczenie dokumentu (pozycji) do roliczenia'@,,
                         "{? TYM_ROZ.CZ
                          || FUN.info('Rozliczenie częściowe już istnieje w okresie %1 %2.'@[SSTALE.AO().NAZ,OKRO_F.ROK().NAZ])
                          |? TYM_ROZ.OK='B'
                          || FUN.info('W aktywnym roku w jednostce księgowej %1\nrejestr VAT: '
                                      '%2 nie istnieje.\nZaznaczenie niemożliwe.'@[ROZLVAT.ODD().OD, ROZLVAT.RVAT().SYM])
                          || TYM_ROZ.OK:='T'
                          ?};
                          TYM_ROZ.put",1,,,,'Z');
         TYM_ROZ.win_act(_wer,,'Formuła','&Odznacz'@@,,'Odznaczenie dokumentu (pozycji) do rozliczenia'@,,
                         "{? TYM_ROZ.CZ
                          || FUN.info('Rozliczenie częściowe już istnieje w okresie %1 %2.'@[SSTALE.AO().NAZ,OKRO_F.ROK().NAZ])
                          |? TYM_ROZ.OK='T' & 1+TYM_ROZ.CO='!'
                          || TYM_ROZ.OK:='!'
                          |? TYM_ROZ.OK='T' & 1+TYM_ROZ.CO<>'!'
                          || TYM_ROZ.OK:='N'
                          ?};
                          TYM_ROZ.put",,,,,'O');
         TYM_ROZ.win_act(_wer,,'Formuła','Pozycje &VAT'@@,,'Pozycje VAT dokumentu'@,,
                         "{? ROZLVAT.seek(TYM_ROZ.ROZLVAT,ROZLVAT.name())
                          || exec('rozlvpoz','!fks_vat_droz')
                          || FUN.info('Brak rozliczenia VAT.'@)
                          ?}",,,,,'V');
         TYM_ROZ.win_act(_wer,,'Formuła','Podsumownie'@@,,'Podsumowanie pozycji'@,,"exec('s_rozl','!fks_vat_droz')",,,,,'P');
         TYM_ROZ.win_act(_wer,,'Formuła','Wydruk'@@,,'Wydruk zakwalifikowanych dokumentów (pozycji)'@,,"exec('rep_exec','#b_report','FKS_VAT_DROZ','fks_tym_roz',,,,,,,,'W')",,,,,'W');
         TYM_ROZ.win_act(_wer,,'Szukaj');
         TYM_ROZ.win_act(_wer,,'Kolejność');
         TYM_ROZ.win_act(_wer,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy i ikon'@,,
                        "exec('legenda','color','$Zaznaczone dokumenty (pozycje)'@,'#TYM_ROZ')",,,,,'L');
         TYM_ROZ.win_act(_wer,,'Rekord',,,,"
            _win:=TYM_ROZ.win_sel('?');
            {? TYM_ROZ.CZ
            || TYM_ROZ.actions_grayed(_win,);
               TYM_ROZ.actions(_win,,'Z')
            |? TYM_ROZ.OK='T'
            || TYM_ROZ.actions_grayed(_win,'Z');
               TYM_ROZ.actions(_win,,'O',1)
            || TYM_ROZ.actions_grayed(TYM_ROZ.win_sel('?'),'O');
               TYM_ROZ.actions(_win,,'Z',1)
            ?};
            {? TYM_ROZ.OK='T' || exec('findtmp','#color') ?}
         ");
         TYM_ROZ.win_btn(_wer,'text='+'&Zaznacz'@+',btn_label_align=center,panel=right,align=begin','menu:Z');
         TYM_ROZ.win_btn(_wer,'text='+'&Odznacz'@+',btn_label_align=center,panel=right,align=begin','menu:O');
         TYM_ROZ.win_btn(_wer,'text='+'&Rozlicz zaznaczone'@+',btn_label_align=center,panel=right,align=begin','menu:R');
         _ind2:=TYM_ROZ.ndx_tmp('',0,'DATA',,0);
         TYM_ROZ.index(_ind2);
         {? TYM_ROZ.select
         || TYM_ROZ.index(_ind1); TYM_ROZ.prefix('T');
            {? TYM_ROZ.first()
            || ROZLVAT.cntx_psh();
               ROZLVAT.index('DOK'); _ile:=0;
               ROZLVAT.prefix();
               VPOZ.index('VDRV');
               {!|? {? ROZLVAT.seek(TYM_ROZ.ROZLVAT,ROZLVAT.name())
                    || exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
                       DOK.prefix(); ROZLVAT.DOK();
                       VPOZ.prefix(DOK.ref(),ROZLVAT.RVAT,ROZLVAT.DOP); {? VPOZ.first() || 1 ?};
                       {? DOK.NK=TYM_ROZ.SYM & ((DOK.DOP=ROZLVAT.DOP & DOK.DSP_WPOZ='N') | VPOZ.D=ROZLVAT.DOP)
                       || _ile+=exec('rozvat2','!fks_vat_droz',1)
                       ?}
                    ?};
                    TYM_ROZ.next()
               !};
               ROZLVAT.cntx_pop()
            || _ok:=0
            ?};
            exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR)
         || _ok:=-1
         ?}
      || {!
         |? exec('add_tmp','fks_mail',2,TYM_ROZ.REJ,$TYM_ROZ.DATA,TYM_ROZ.SYM,TYM_ROZ.CO,form(TYM_ROZ.NETTO,,2,' ,'),form(TYM_ROZ.VAT,,2,' ,'));
            TYM_ROZ.next()
         !}
      ?}
   || _ok:=0
   ?}
?};
obj_del(TYM_ROZ);
VPOZ.cntx_pop();
ROZLVAT.cntx_pop();
grp_disp(ROZLVAT,'WER',1);
{? _a
|| {? _ok=1
   || FUN.info('Liczba rozliczonych dokumentów (pozycji): %1'@[$_ile])
   |? _ok=0
   || FUN.info('Nie zakwalifikowano dokumentów (pozycji) do automatycznego rozliczenia.'@)
   ?}
?}


\czy_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Funkcja sprawdza czy zakwalifikowac dokument do automatycznego rozliczenia
::   WE: _a - tryb rozliczenia VAT (SLO.ref)
::       _b - wskazanie na dokument (DOK) lub pozycje VAT (VPOZ)
::  OLD: \czy_roz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_co:='';
TYM_ROZ.OK:='T';
TYM_ROZ.CZ:=0;
TYM_ROZ.BRUTTO:=0;
_nast:=exec('get_par','slo_slu',_a,1);
{? _nast='T'
|| OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA); ROZLVAT.OKRO();
   _one:=0; _l_okr:={? PAR_SKID.get(12)='T' || exec('m2eoq','fks_vat',ROZLVAT.OKRO)+6 || 2 ?};
   {! |?
      _ok:={? OKRO_F.next() & OKRO_F.POCZ<>date(0,0,0)
           || 1
           |? OKRO_F.next() & OKRO_F.POCZ=date(0,0,0) & OKRO_F.next()
           || 1
           ?};
      _one+=1;
      OKRO_F.cntx_psh();
      {? _ok & SSTALE.AO=OKRO_F.ref()
      || _co:='Odliczenie podatku w następnym okresie'; TYM_ROZ.OK:='T'
      |? _ok & SSTALE.AO<>OKRO_F.ref() & SSTALE.AO().POCZ>ROZLVAT.DATA
      || _co:='!Utrata odliczenia podatku.'; TYM_ROZ.OK:='!'
      ?};
      OKRO_F.cntx_pop();
      ROZLVAT.DATA>=date(2008,12,1) & _ok & _one<_l_okr & 1+_co='!'
   !}
?};
{? _co=''
|| TYM_ROZ.BRUTTO:=0;
   _max:=#exec('get_par','slo_slu',_a,2);
   _term:=exec('get_par','slo_slu',_a,3);
   _zapl:=exec('get_par','slo_slu',_a,4);
   _MaxDSpr:={? Plugin.runnable('MAX_DSPR')
             || Plugin.run('MAX_DSPR')
             || 0
   ?};
   _dop:={? _MaxDSpr || DOK.DOP || date(0,0,0) ?};
   _j_tp:=_j_max:=_j_zap:=_p_tp:=0;_tp:=_d:=date(0,0,0);
   {? _term='T'
   || exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
      DOK.prefix();
      ROZLVAT.DOK();
      _tp:=DOK.D3;
      _j_tp:=SSTALE.AO().POCZ<=_tp & _tp<=SSTALE.AO().KON
   ?};
   {? _max>0
   || _d:=ROZLVAT.DATA+_max;
      _j_max:=SSTALE.AO().POCZ<=_d & _d<=SSTALE.AO().KON
   ?};
   {? _max>0 & SSTALE.AO().POCZ>_d
   || _co:='!Przekroczenie '+$_max+' dni od daty operacji';
      TYM_ROZ.OK:='!'
   |? _term='T' & SSTALE.AO().POCZ>_tp
   || _co:='!Przekroczenie ter. płatności '+$_tp;
      TYM_ROZ.OK:='!'
   |? _term='T' & _tp>=SSTALE.AO().POCZ & _tp<=SSTALE.AO().KON & _zapl='N'
   || _co:='Upływ terminu płatności '+$_tp
   |? _max>0 & _d>=SSTALE.AO().POCZ & _d<=SSTALE.AO().KON
   || _co:='Upływ '+$_max+' dni od daty operacji'
   || {? _zapl='T'
      || {? _j_tp || _d:=_tp || _d:=SSTALE.AO().KON ?};
         kw_zap:=0;
         _RoEwid:=DOK.RVAT().RVAT().KVAT().EWID().KOD;
         _j_zap:=exec('spr_roz','!fks_vat_droz',_d,ROZLVAT.DOK,ROZLVAT.SYM,ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR,_RoEwid);
         kw_zap-=exec('vpoz_roz_brutto','!fks_vat_droz',_b)
      ?};
      {? _j_zap>0
      || _co:={? _max>0 || 'przed upł.'+$_max+' dni' || '' ?};
         _co:={? _tp<>date(0,0,0) || _co:='(term. pł. '+$_tp+')' || _co ?};
         {? _j_zap=1
         || _co:='Całkowita zapłata '+_co
         |? kw_zap
         || TYM_ROZ.BRUTTO:=kw_zap;
            TYM_ROZ.CZ:=1;
            _co:='Częściowa zapłata ('+$kw_zap+' zł brutto) '+_co;
            TYM_ROZ.OK:='N'
         ?}
      || {? _zapl<>'T' || _RoEwid:=DOK.RVAT().RVAT().KVAT().EWID().KOD ?};
         _j_zap:=exec('spr_roz','!fks_vat_droz',SSTALE.AO().KON,ROZLVAT.DOK,ROZLVAT.SYM,ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR,_RoEwid);
         {? _term='T' & _zapl='T' & _tp>=SSTALE.AO().POCZ & _tp<=SSTALE.AO().KON & _j_zap
         ||  _co:='!Zapłata po terminie '+$_tp;
             TYM_ROZ.OK:='!'
         ?};
         {? _MaxDSpr & _dop>=SSTALE.AO().POCZ & _dop<=SSTALE.AO().KON
         || _co:='Upływ w dacie operacji';
            TYM_ROZ.OK:='!'
          ?}
      ?}
   ?}
?};
{? _co<>'' & ~exec('czy_rvat','fks_vat',SSTALE.AR,ROZLVAT.ODD,ROZLVAT.RVAT) ||
   TYM_ROZ.OK:='B'
?};
_co


\rozlvpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Akcja 'pozycje VAT' w okienku wertowania tabeli ROZLVAT
::  OLD: \rozlvpoz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZLVAT.r_lock(1,1)
|| exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
   _sr:=SSTALE.AR; _so:=SSTALE.AO;
   SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref();
   DOK.prefix();
   ROZLVAT.DOK();
   VPOZ.win_sel('R_SEL');
   VPOZ.win_edit();
   VPOZ.index('VDRV');
   VPOZ.prefix(DOK.ref(),ROZLVAT.RVAT,ROZLVAT.DOP);
   exec('suma_vat','dok_fks','VPOZ',1);
   vpozvat:=1;
   VPOZ.select(,,,'RZC');
   &vpozvat;
   SSTALE.AR:=_sr; SSTALE.AO:=_so;
   exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR);
   ROZLVAT.r_unlock()
|| FUN.info('Rozliczenie zablokowane.\nPozycje VAT nie mogą być wyświetlone.'@)
?}


\s_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Obliczenie i prezentacja podsumowania dokumentow do automatycznego rozliczenia
::  OLD: \s_rozl/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
exec('s_rozl1','!fks_vat_droz');
_wer:=STYM_ROZ.mk_sel('Podsumowanie dokumentów (pozycji) do automatycznego rozliczenia'@,,,'stym_roz_wer');
STYM_ROZ.win_fld(_wer,,'OPIS');
STYM_ROZ.win_fld(_wer,,'NETTO');
STYM_ROZ.win_fld(_wer,,'VAT');
STYM_ROZ.win_sel(_wer);
STYM_ROZ.select;
VAR_DEL.delete('STYM_ROZ');
1


\s_rozl1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Podsumowanie dokumentow ujetych przy automatycznym rozliczeniu
::  OLD: \s_rozl1/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
STYM_ROZ:=tab_tmp(1,'LP','INTEGER','Lp.',
                   'OPIS','STRING[50]','Opis',
                   'NETTO','REAL','Netto',
                   'VAT','REAL','VAT');
TYM_ROZ.cntx_psh();
_zn:=_zz:=_nn:=_nz:=_vn:=_vz:=0;
{? TYM_ROZ.first
|| {! |?
      {? TYM_ROZ.OK='T' || _zz:=1; _nz+=TYM_ROZ.NETTO; _vz+=TYM_ROZ.VAT || _zn:=1; _nn+=TYM_ROZ.NETTO; _vn+=TYM_ROZ.VAT ?};
      TYM_ROZ.next()
   !}
?};
TYM_ROZ.cntx_pop();
{? _zz=1
|| STYM_ROZ.LP+=1;
   STYM_ROZ.OPIS:='Podsumowanie dokumentów (pozycji) zaznaczonych';
   STYM_ROZ.NETTO:=_nz;
   STYM_ROZ.VAT:=_vz;
   STYM_ROZ.add()
?};
{? _zn=1
|| STYM_ROZ.LP+=1;
   STYM_ROZ.OPIS:='Podsumowanie dokumentów (pozycji) niezaznaczonych';
   STYM_ROZ.NETTO:=_nn;
   STYM_ROZ.VAT:=_vn;
   STYM_ROZ.add()
?};
{? _zz=1 & _zn=1
|| STYM_ROZ.LP+=1;
   STYM_ROZ.OPIS:='Razem wszystkie dokumenty (pozycje)';
   STYM_ROZ.NETTO:=_nz+_nn;
   STYM_ROZ.VAT:=_vz+_vn;
   STYM_ROZ.add()
?};
1


\spr_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Sprawdzenie czy jest zaplata do faktury
::   WE: _a - do daty
::       _b - dokument (ref DOK)
::       _c - identyfikator
::       _d - maska roku
::       _e - nr okresu
::       _f - rodzaj ewidencji: Sprzedaż, Zakup itp
::  OLD: \spr_roz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0; _str:='';
{? _f='Sprzedaż' | _f='_WWspDos'
|| _str:='W'
|? 3+_f='Zak' | 3+_f='Imp' | _f='_WWspNab'
|| _str:='M'
?};
exec('cntxmask','dok_fks_ks',1);
exec('mask','dok_fks_ks',_d,_e);
POZ.index('ROZR2'); POZ.prefix(_b);
_typ:={? _str='W' || '@NAL@' |? _str='M' || '@ZOB@' || '@ZOB@NAL@' ?};
{? POZ.last() || {! |? {? POZ.ODD=null || 0 |? POZ.ID=_c & _typ*( '@'+POZ.TID+'@' ) || 0 || POZ.prev() ?} !} ?};
{? POZ.ID=_c
|| {? _str='' || _str:=1+POZ.STR ?};
   OP.index('KON_O'); OP.prefix();
   {? OP.find_key({? POZ.WAL<>null || POZ.WAL || FINFO.NAROD ?},POZ.ODD,POZ.KON,_c,_c,POZ.SYM_ROK)
   || ZAP_OP.cntx_psh(); ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
      _wn:=_ma:=0;
      {? ZAP_OP.first
      || {! |?
            {? ZAP_OP.DATA<=_a
            || {? ZAP_OP.WN>0 || _wn+=ZAP_OP.WN || _ma+=ZAP_OP.WN*(-1) ?};
               {? ZAP_OP.MA>0 || _ma+=ZAP_OP.MA || _wn+=ZAP_OP.MA*(-1) ?}
            ?};
            ZAP_OP.next()
         !}
      ?};
      ZAP_OP.cntx_pop();
      {? (_wn$2<>0 & _wn$2=_ma$2) | (_str='W' & _ma$2>_wn$2) | (_str='M' & _wn$2>_ma$2)
      || _ok:=1
      |? (_str='W' & _ma$2>0) | (_str='M' & _wn$2>0)
      || _ok:=2;
         kw_zap:={? _str='W' || _ma$2 || _wn$2 ?}
      ?}
   ?}
?};
exec('cntxmask','dok_fks_ks',0);
_ok


\vpoz_roz_brutto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca wartosc brutto dotychczasowego czesciowego rozliczenia
::   WE: _a - wskazanie do dokument (DOK.ref) lub pozycje VAT (VPOZ.ref)
::  OLD: \vpoz_roz_brutto/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_brutto:=0;
{? 4+ref_name(_a)='doku'
|| VPOZ.cntx_psh();
   VPOZ.index('VDOK'); VPOZ.prefix(_a);
   {? VPOZ.first()
   || {!
      |? _brutto+=exec('vpoz_roz_br_pom','!fks_vat_droz',VPOZ.ref());
         VPOZ.next()
      !}
   ?};
   VPOZ.cntx_pop()
|| _brutto:=exec('vpoz_roz_br_pom','!fks_vat_droz',_a)
?};
_brutto


\vpoz_roz_br_pom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca wartosc brutto dotychczasowego czesciowego rozliczenia
::   WE: _a - wskazanie do pozycje VAT (VPOZ.ref)
::  OLD: \vpoz_roz_br_pom/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_brutto:=0;
VPOZ_ROZ.index('VPOZ_ROZ');
VPOZ_ROZ.prefix(_a);
{? VPOZ_ROZ.first()
|| {!
   |? _brutto+=VPOZ_ROZ.BRUTTO;
      VPOZ_ROZ.next()
   !}
?};
_brutto


\chk_grpv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: Rekord po dla okna VPOZ-a
::  OLD: \chk_grpv/skid.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(VPOZ,,'GRVAT')


\vat_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Rozliczenie pozycji VAT w biezacym okresie
::  OLD: \vat_rozl/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? VPOZ.KNAG=2
|| FUN.info('Pozycja korekty nagłówkowej. Rozliczenie należy przeprowadzić dla dokumentu VAT.'@)
|? VPOZ.OKRVAT
|| FUN.info('Pozycja ma już przypisany okres rozliczenia VAT.'@)
|? exec('is_rozl_cz_full','fks_vat')
|| FUN.info('Pozycja z częściowymi rozliczeniami\njest całkowicie rozliczona.'@)
|? ROZLICZ.NETTO1 | ROZLICZ.VAT1 | ROZLICZ.BRUTTO1
|| FUN.info('Pozycja ma już częściowe rozliczenia VAT.'@)
|| VPOZ.cntx_psh();
   exec('rozlvat','!fks_vat_droz',1);
   VPOZ.cntx_pop()
?};
grp_disp(ROZLVAT,'WER',1);
1


\roz_z_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Zmiana trybu rozliczenia VAT
::  OLD: \roz_z_a/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.SLAVAT<>null
|| {? VPOZ.OKRVAT
   || FUN.info('Niedozwolona zmiana trybu rozliczenia VAT dla rozliczonej pozycji.'@)
   |? exec('is_rozl_cz','fks_vat')
   || FUN.info('Niedozwolona zmiana trybu rozliczenia VAT dla częściowo rozliczonej pozycji.'@)
   || VPOZ.cntx_psh();
      VPOZ.win_edit('RED_TRYB');
      {? VPOZ.edit()
      || VPOZ.put();
         exec('dok_akt','fks_vat')
      ?};
      VPOZ.cntx_pop()
   ?}
|| FUN.info('Ustal słownik Trybów rozliczeń VAT w definicjach systemu.'@); 0
?}


\rozl_cz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja Rozlicz czesciowo tabeli VPOZ
::  OLD: \rozl_cz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
POMOC.KNAG:=0;
{? VPOZ.KNAG=2
|| FUN.info('Pozycja korekty nagłówkowej. Rozliczenie należy przeprowadzić dla dokumentu VAT.')
|? VPOZ.OKRVAT<>null()
|| FUN.info('Pozycja ma już przypisany okres rozliczenia VAT.'@)
|? exec('is_rozl_cz_full','fks_vat')
|| FUN.info('Pozycja jest już rozliczona w całości.'@)
|? ROZLVAT.OKRO().ROK<>SSTALE.AR & ~exec('czy_rvat','fks_vat',SSTALE.AR,ROZLVAT.ODD,ROZLVAT.RVAT)
|| FUN.info('W aktywnym roku w jednostce księgowej %1\nrejestr VAT: %2'
            ' nie istnieje.\nRozliczenie niemożliwe.'@[ROZLVAT.ODD().OD,ROZLVAT.RVAT().SYM])
|| VPOZ_ROZ.index('VPOZ_ROZ'); VPOZ_ROZ.prefix(VPOZ.ref(),SSTALE.AO);
   {? VPOZ_ROZ.first()
   || FUN.info('W okresie %1 %2 jest już częściowe rozliczenie.'@[SSTALE.AO().NAZ,OKRO_F.ROK().NAZ])
   |? (ROZLVAT.OKRO().ROK().POCZ_ROK < SSTALE.AR().POCZ_ROK |
      (ROZLVAT.OKRO().ROK=SSTALE.AR & ROZLVAT.OKRO().NR<=SSTALE.AO().NR)) |
      FUN.ask('\nDokument pochodzi z okresu %1 %2.\n'
              'Czy na pewno rozliczać częściowo w okresie\n'
              'wcześniejszym (%3 %4)?'@[ROZLVAT.OKRO().NAZ,ROZLVAT.OKRO().ROK().NAZ,SSTALE.AO().NAZ,SSTALE.AR().NAZ])
   || ROZLICZ.PAR1:={? exec('czy_ok_dok','!fks_vat_droz') | exec('czy_ok_roz','!fks_vat_droz')
                    || FUN.ask('Czy wspomagać przeliczanie kwot w rozliczeniu?'
                       '\n NIE - opcja dla rozliczenia zamykającego, '
                       '\n TAK - gdy ma pozostać część nierozliczona.'@)
                    || 1
                    ?};
      exec('bv_rozl_cz','fks_vat');
      _vref:=VPOZ.ref();
      VPOZ_ROZ.blank(1);
      VPOZ_ROZ.VPOZ:=VPOZ.ref();
      VPOZ_ROZ.OKRO_F:=SSTALE.AO;
      VPOZ_ROZ.GR_VAT:=VPOZ.GRVAT;
      {? PAR_SKID.get(83)='T' || VPOZ_ROZ.C_PROC_S:=VPOZ.C_PROC_S ?};
      {? PAR_SKID.get(83)='T' || VPOZ_ROZ.C_PREWSK:=VPOZ.C_PREWSK ?};
      VPOZ_ROZ.NETTO:=ROZLICZ.NETTO2;
      VPOZ_ROZ.VAT:=ROZLICZ.VAT2;
      VPOZ_ROZ.BRUTTO:=ROZLICZ.BRUTTO2;
      VPOZ_ROZ.VAT_ODL:=ROZLICZ.VAT_ODL2;
      VPOZ_ROZ.VATKOSZT:=ROZLICZ.KOSZT2;
      exec('bv_rozl_cz1','fks_vat',1);
      {? PAR_SKID.get(83)='T' || VPOZ_ROZ.win_edit('RED_PR') || VPOZ_ROZ.win_edit('RED') ?};
      {? VPOZ_ROZ.edit("exec('ar_vpoz_rozl','!fks_vat_droz')")
      || {? VPOZ_ROZ.add(1)
         || exec('vat_ks1','dok_fks_ks',ROZLVAT.OKRO,0,1,,VPOZ.D);
            exec('rozvat3','!fks_vat_droz')
         || FUN.info('Inny użytkownik rozliczył częściowo\npozycję w tym okresie.'@)
         ?};
         exec('is_vpoz_czrozl','fks_vat',_vref);
         exec('suma_vat','dok_fks','VPOZ',1)
      ?}
   ?}
?}


\ar_vpoz_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Rekord po tabeli VPOZ_ROZ
::  OLD: \ar_vpoz_rozl/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
{? VPOZ.GRVAT<>VPOZ_ROZ.GR_VAT
|| _pyt:=FUN.choice('Zmieniono grupę podatkową.\nCzy zmienić grupę podatkową\nrównież w pozycji VAT?'@,'Uwaga'@,,'Tak'@,'Nie'@);
   {? _pyt=0
   || return('GR_VAT')
   |? _pyt=1
   || VPOZ.cntx_psh();
      VPOZ.prefix();
      VPOZ.GRVAT:=VPOZ_ROZ.GR_VAT;
      exec('setvatodl','dok_fks');
      {? VPOZ.put() || UNPAR.P11:=VPOZ.GRVAT().KOD ?};
      VPOZ.cntx_pop()
   ?}
?};
_odl:=exec('gr_pod_typ','dok_fks',VPOZ_ROZ.GR_VAT().GRVAT().KOD)<>-1;
{? VPOZ_ROZ.NETTO=0 & VPOZ_ROZ.VAT=0 & VPOZ_ROZ.BRUTTO=0
|| FUN.info('Przynajmniej jedna z pozycji:\nNetto, kwota VAT lub Brutto\nmusi być różna od zera.'@);
   _ret:='NETTO'
|? VPOZ.NETTO>0 & VPOZ_ROZ.NETTO<0 |
   VPOZ.NETTO<0 & VPOZ_ROZ.NETTO>0
|| FUN.info('Niezgodne znaki kwot netto pozycji i rozliczenia.'@);
   _ret:='NETTO'
|? VPOZ.VAT>0 & VPOZ_ROZ.VAT<0 |
   VPOZ.VAT<0 & VPOZ_ROZ.VAT>0
|| FUN.info('Niezgodne znaki kwot VAT pozycji i rozliczenia.'@);
   _ret:='VAT'
|? VPOZ.BRUTTO>0 & VPOZ_ROZ.BRUTTO<0 |
   VPOZ.BRUTTO<0 & VPOZ_ROZ.BRUTTO>0
|| FUN.info('Niezgodne znaki kwot brutto pozycji i rozliczenia.'@);
   _ret:='BRUTTO'
|? _odl &
   (VPOZ_ROZ.VAT>0 & VPOZ_ROZ.VAT_ODL<0 |
    VPOZ_ROZ.VAT<0 & VPOZ_ROZ.VAT_ODL>0
   )
|| FUN.info('Niezgodne znaki kwoty VAT i VAT do odliczenia.'@);
   _ret:='VAT_ODL'
|? _odl &
   (VPOZ_ROZ.VAT>0 & VPOZ_ROZ.VAT_ODL>VPOZ_ROZ.VAT |
    VPOZ_ROZ.VAT<0 & VPOZ_ROZ.VAT_ODL<VPOZ_ROZ.VAT
   )
|| {? VPOZ_ROZ.VAT>0
   || FUN.info('Kwota VAT do odliczenia nie może\nbyć większa niż kwota VAT.'@)
   || FUN.info('Kwota VAT do odliczenia nie może\nbyć mniejsza niż kwota VAT.'@)
   ?};
   _ret:='VAT_ODL'
|? VPOZ.NETTO>0 & ROZLICZ.NETTO2<0 | VPOZ.NETTO<0 & ROZLICZ.NETTO2>0 |
   VPOZ.VAT>0 & ROZLICZ.VAT2<0 | VPOZ.VAT<0 & ROZLICZ.VAT2>0 |
   VPOZ.BRUTTO>0 & ROZLICZ.BRUTTO2<0 | VPOZ.BRUTTO<0 & ROZLICZ.BRUTTO2>0
|| FUN.info('Kwoty rozliczane przekraczające kwoty pozostałe do rozliczenia:\n'+
           {? VPOZ.NETTO>0 & ROZLICZ.NETTO2<0 | VPOZ.NETTO<0 & ROZLICZ.NETTO2>0
           || {? _ret='' || _ret:='NETTO' ?}; 'Netto, '
           || ''
           ?}+
           {? VPOZ.VAT>0 & ROZLICZ.VAT2<0 | VPOZ.VAT<0 & ROZLICZ.VAT2>0
           || {? _ret='' || _ret:='VAT' ?}; 'Kwota VAT, '
           || ''
           ?}+
           {? VPOZ.BRUTTO>0 & ROZLICZ.BRUTTO2<0 | VPOZ.BRUTTO<0 & ROZLICZ.BRUTTO2>0
           || {? _ret='' || _ret:='BRUTTO' ?}; 'Brutto, '
           || ''
           ?}-2
   )
?};
{? _ret='' & (VPOZ_ROZ.NETTO+VPOZ_ROZ.VAT)$2<>VPOZ_ROZ.BRUTTO$2
|| _ok:=FUN.ask('\nWartości rozliczanych pozycji netto+VAT nie zgadzają się z'+
        '\nwartością pozycji brutto na kwotę: %1'
        '\nNetto     = %2'
        '\nVAT       = %3'
        '\nNetto+VAT = %4'
        '\nBrutto    = %5'
        '\n\nKontynuować?\n'@[form((VPOZ_ROZ.NETTO$2+VPOZ_ROZ.VAT$2-VPOZ_ROZ.BRUTTO$2)$2,,2),form(VPOZ_ROZ.NETTO$2,16,2),form(VPOZ_ROZ.VAT$2,16,2),form(VPOZ_ROZ.NETTO$2+VPOZ_ROZ.VAT$2,16,2),form(VPOZ_ROZ.BRUTTO$2,16,2)]);
   {? ~_ok
   || _ret:='NETTO'
   ?}
?};
{? _ret='' & _odl & VPOZ_ROZ.VAT<>VPOZ_ROZ.VAT_ODL+VPOZ_ROZ.VATKOSZT
|| _ok:=FUN.ask('\nNieprawidłowe wartości rozliczanych kwot VAT. '
        '\nVAT do odliczenia  = %1'
        '\nVAT bez odliczenia = %2'
        '\nsuma               = %3'
        '\nVAT                = %4'
        '\n\nKontynuować?\n'@[form(VPOZ_ROZ.VAT_ODL$2,16,2),form(VPOZ_ROZ.VATKOSZT$2,16,2),form(VPOZ_ROZ.VAT_ODL$2+VPOZ_ROZ.VATKOSZT$2,16,2),form(VPOZ_ROZ.VAT$2,16,2)]);
   {? ~_ok
   || _ret:='VAT_ODL'
   ?}
?};
_ret


\rozl_vat_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wyswietlenie czesciowych rozliczen VAT
::  OLD: \rozl_vat_sel/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
exec('bv_rozl_cz','fks_vat');
VPOZ_ROZ.index('VPOZ_ROZ');
VPOZ_ROZ.prefix(VPOZ.ref());
VPOZ_ROZ.win_sel('WER');
{? PAR_SKID.get(83)='T'
|| VPOZ_ROZ.win_edit('RED2_PR')
|| VPOZ_ROZ.win_edit('RED2')
?};
VPOZ_ROZ.select()


\br_vpoz_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Rekord przed okna WER tabeli VPOZ_ROZ
::  OLD: \br_vpoz_roz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
ROZLICZ.OKRVAT:=VPOZ_ROZ.OKRO_F().NAZ+' '+OKRO_F.ROK().NAZ;
''


\ae_vpoz_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji pol tabeli VPOZ_ROZL
::  OLD: \ae_vpoz_rozl/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
fld(fld()$2);
{? ROZLICZ.PAR1
|| {? cur_afld()='NETTO'
   || VPOZ_ROZ.VAT:=(0.01*VPOZ_ROZ.NETTO*exec('vat','dok_fks',VPOZ.PR().KOD))$2;
      VPOZ_ROZ.BRUTTO:=(VPOZ_ROZ.NETTO+VPOZ_ROZ.VAT)$2;
      exec('setvatodlroz','!fks_vat_droz')
   |? cur_afld()='BRUTTO'
   || VPOZ_ROZ.NETTO:=(VPOZ_ROZ.BRUTTO/(1+0.01*exec('vat','dok_fks',VPOZ.PR().KOD)))$2;
      VPOZ_ROZ.VAT:=(VPOZ_ROZ.BRUTTO-VPOZ_ROZ.NETTO)$2;
      exec('setvatodlroz','!fks_vat_droz')
   |? cur_afld()='VAT_ODL'
   || VPOZ_ROZ.VATKOSZT:=VPOZ_ROZ.VAT-VPOZ_ROZ.VAT_ODL
   ?}
?};
exec('bv_rozl_cz1','fks_vat',1);
1


\ae_gr_vpoz_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji VPOZ_ROZ.GR_VAT
::  OLD: \ae_gr_vpoz_rozl/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? VPOZ_ROZ.GR_VAT & PAR_SKID.get(83)='T' & ROZNE.GRVAT<>VPOZ_ROZ.GR_VAT
|| {? exec('gr_pod_typ','dok_fks',VPOZ_ROZ.GR_VAT().GRVAT().KOD)=1 & ~(SLO.KOD='ZakuPods' | SLO.KOD='ZInwPods')
   || VPOZ_ROZ.C_PREWSK:=VPOZ_ROZ.C_PROC_S:=1
   || VPOZ_ROZ.C_PREWSK:=VPOZ_ROZ.C_PROC_S:=0
   ?}
?};

ROZNE.GRVAT:=VPOZ_ROZ.GR_VAT;
ROZNE.GR_VAT:=VPOZ_ROZ.GR_VAT().GRVAT;

exec('setvatodlroz','!fks_vat_droz');
exec('bv_rozl_cz1','fks_vat',1);
1


\be_vpoz_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja pol tabeli VPOZ_ROZL
::  OLD: \be_vpoz_rozl/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:=exec('gr_pod_typ','dok_fks',VPOZ_ROZ.GR_VAT().GRVAT().KOD);
{? _typ=-1
|| 0
|? cur_afld()='VATKOSZT'
|| 0
|| 1
?}


\czy_ok_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Przed czesciowym rozliczeniem, sprawdza czy dokument nie ma niezgodnosci kwot netto, vat, brutto
::  OLD: \czy_ok_dok/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
{? VPOZ.get
|| {? (VPOZ.NETTO+VPOZ.VAT)$2<>VPOZ.BRUTTO$2
   || FUN.emsg('\nW dokumencie źródłowym wartości pozycji netto+VAT nie zgadzają się z'
               '\nwartością pozycji brutto na kwotę: %1'
               '\n\nnetto     = %2'
               '\nvat       = %3'
               '\nnetto+vat = %4'
               '\nbrutto    = %5'@[form(VPOZ.NETTO$2+VPOZ.VAT$2-VPOZ.BRUTTO$2)$2,form(VPOZ.NETTO$2,16,2),form(VPOZ.VAT$2,16,2),form(VPOZ.NETTO$2+VPOZ.VAT$2,16,2),form(VPOZ.BRUTTO$2,16,2)]); _ret:=1
   || {? VPOZ.VAT$2<>(0.01*VPOZ.NETTO*exec('vat','dok_fks',VPOZ.PR().KOD))$2
      || FUN.emsg('\nW dokumencie źródłowym wartość pozycji VAT nie zgadza się ze stawką.'@); _ret:=1
      ?}
   ?};
   {? _ret & 1+VPOZ.GRVAT().GRVAT().KOD='Z' & VPOZ.VAT$2<>(VPOZ.VAT_ODL+VPOZ.VATKOSZT)$2
   || _a:=FUN.emsg('\nW dokumencie źródłowym wartości pozycji VAT do odliczenia, VAT bez odliczenia i VAT całkowity nie zgadzają się.'
                   '\nRóżnica wynosi: %1 zł'@[form(fabs((VPOZ.VAT_ODL+VPOZ.VATKOSZT)$2-VPOZ.VAT$2),,2)]);
      _ret:=1
   ?}
?};
_ret


\czy_ok_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Przed czesciowym rozliczeniem, sprawdza czy pozycje nie mają niezgodnosci kwot netto, vat, brutto
::  OLD: \czy_ok_roz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
{? VPOZ.get
|| {? (ROZLICZ.NETTO2+ROZLICZ.VAT2)$2<>ROZLICZ.BRUTTO2$2
      | ROZLICZ.VAT2$2<>(0.01*ROZLICZ.NETTO2*exec('vat','dok_fks',VPOZ.PR().KOD))$2
      | (1+VPOZ.GRVAT().GRVAT().KOD='Z' & ROZLICZ.VAT2$2<>(ROZLICZ.VAT_ODL2+ROZLICZ.KOSZT2)$2)
   || FUN.info('W rozliczeniu występują różnice groszowe.'
               '\nZalecana opcja bez automatycznego wspomagania rozliczenia.'@); _ret:=1
   ?}
?};
_ret

:Sign Version 2.0 jowisz:1045 2023/12/22 11:36:03 75d42cbf946df034381399f827017bf65f9a52f5460b7e8ae8767e710d31d57012d0ffe21618f6f89782116357019b581228d080c615ba309f873ea8fb3fb3fc0b098319e45a8fae622874d76aefd00c68dafb2a0ec0629e9a3033083e252980c210bf5ea1e4b32551b77e38d6326fb1fbf57ddb62d10961f452dd4418c2834f

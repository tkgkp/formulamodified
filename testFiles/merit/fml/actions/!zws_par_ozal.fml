:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ozal.fml
:: Utworzony: 31.05.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Typy zaliczek
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności ZWS_PAR_OZAL
::  OLD: \deftypza/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
exec('typy_zal_noob','zaliczki');
ETYPZAL.cntx_psh();
ETYPZAL.index('DISP'); ETYPZAL.prefix(REF.FIRMA);
ETYPZAL.win_sel('WER');
ETYPZAL.select();
ETYPZAL.cntx_pop()


\zltypsys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Wartosc poczatkowa pola ETYPZAL.SYSTEM
::  OLD: \zltypsys/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
SKID.ZLTYPSYS


\zalkaspr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Edycja pol typu zaliczki w zaleznosci od tego, czy typ jest systemowy
::  OLD: \zalkaspr/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPZAL.SYSTEM='N'


\aesymzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Po redakcji pola ETYPZAL.EDSYMZAL
::  OLD: \aesymzal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPZAL.EDSYMZAL='N' || ETYPZAL.AUT_SYM:='T' ?}; win_disp(); 1


\beautzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Przed redakcja pola ETYPZAL.AUT_SYM
::  OLD: \beautzal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPZAL.EDSYMZAL='T'


\aeautzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Po redakcji pola ETYPZAL.AUT_SYM
::  OLD: \aeautzal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPZAL.AUT_SYM='N' || ETYPZAL.FORMSYMZ:='' ?}; win_disp(); 1


\bedsasza
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Na wyswietl dla pola ETYPZAL.FORMSYMZ
::  OLD: \bedsasza/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPZAL.AUT_SYM='N'
|| exec('findfnv','#color')
|| ''
?}


\berfsymz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Przed redakcja pola ETYPZAL.FORMSYMZ
::  OLD: \berfsymz/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPZAL.AUT_SYM='T'


\doltypza
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Dolaczanie typow zaliczek
::  OLD: \doltypza/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPZAL.win_edit('RED');
SKID.ZLTYPSYS:='N';
ETYPZAL.blank();
{? ETYPZAL.edit("exec('rpotzal','!zws_par_ozal')") || ETYPZAL.add() ?}


\poptypza
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Poprawianie typow zaliczek
::  OLD: \poptypza/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPZAL.win_edit('RED');
{? ETYPZAL.edit("exec('rpotzal','!zws_par_ozal')") || ETYPZAL.put() ?}


\rpotzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Po redakcji typu zaliczki
::  OLD: \rpotzal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:='';
{? ETYPZAL.NAZ=''
|| FUN.info('Nie wprowadzono nazwy typu zaliczki.'@); _dalej:='NAZ'
?};
{? _dalej=''
|| _popraw:=(-menu_txt()='popraw');
   {? _popraw || _ref:=ETYPZAL.ref() ?};
   ETYPZAL.cntx_psh();
   ETYPZAL.index('UNIK'); ETYPZAL.prefix(REF.FIRMA,'N',ETYPZAL.NAZ,ETYPZAL.NAZ);
   {? ETYPZAL.first() & {? _popraw || ETYPZAL.ref()<>_ref || 1 ?}
   || FUN.info('Istnieje już typ zaliczki o podanej nazwie.'@); _dalej:='NAZ'
   ?};
   ETYPZAL.cntx_pop()
?};
{? _dalej='' & ETYPZAL.AUT_SYM='T' & ETYPZAL.FORMSYMZ=''
|| FUN.info('Nie wprowadzono formuły do automatycznego nadawania symbolu zaliczki.'@);
   _dalej:='FORMSYMZ'
?};
{? _dalej='' & ETYPZAL.DOMLDNIR<0
|| FUN.info('Domyślna liczba dni realizacji musi być większa bądź równa zero.'@);
   _dalej:='DOMLDNIR'
?};
_dalej


\usutypza
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Usuwanie typow zaliczek
::  OLD: \usutypza/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPZAL.SYSTEM<>'N'
|| FUN.info('Typ zaliczki bez obiegu. Usunięcie niemożliwe.'@)
|? ETYPZAL.count()<>0
|| FUN.info('Typ zaliczki wykorzystywany w ewidencji. Usunięcie niemożliwe.'@)
|? FUN.ask('Usunąć typ zaliczki?'@)
|| ETYPZAL.del()
?}


\rkptzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Rekord przed - typy zaliczek
::  OLD: \rkptzal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPZAL.SYSTEM<>'N'
|| 'ETYPZAL#01#01'
|| ''
?}


\dsptpzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Typy zaliczek - na spacje
::  OLD: \dsptpzal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPZAL.win_edit({? ETYPZAL.SYSTEM='N' | ETYPZAL.NAZ*'PRZELEW - BEZ OBIEGU'>0
                 || 'RED'
                 || 'RED_KASA'
                 ?});
ETYPZAL.display()

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:05 4aa27e6becb5959d8a272601c5e064f160fecbf6840ba7879768ea21a3134ead154fd940009f1c2794e18c4d98c2819123831b6eea3cdcf044297379f6ac4d50877e8427303b78fd758c3dcffa1829ba43b1ec950911fedba6ed76a16b624f0a6d1f481c3bd6b567c8633b9a62158e50a0dedbd921401357b7a7640f8a1a82fd

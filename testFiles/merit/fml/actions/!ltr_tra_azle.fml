:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ltr_tra_razl.fml
:: Utworzony: 16.09.2019
:: Autor: [MW]
::======================================================================================================================
:: Zawartość: Formuły czynności LTR_TRA_AZLE - Archiwizacja transportu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,TR_RODZ
::# kind=WE, symbol=TR_NAG,  type=_TR_NAG,  name=Transport,                          required=N,    keyref=T
::# kind=WE, symbol=TR_NZL,  type=_TR_NZL,  name=Dyspozycja bez transportu,          required=N,    keyref=T
::# kind=WE, symbol=DNI,     type=NUMBER,   name=Ilość dni wstecz do archiwizacji,   required=N
::# kind=WE, symbol=ODDZIAL, type=STRING,   name=Kod oddziału do archiwizacji,       required=N,    fml_val="exec('oddzial','!ltr_tra_azle')"
::# kind=WY, symbol=TR_NAG,  type=_TR_NAG,  name=Transport,                          required=N
::# kind=WY, symbol=TR_NZL,  type=_TR_NZL,  name=Dyspozycja bez transportu,          required=N
::# kind=WY, symbol=IL_SERV, type=NUMBER,   name=Ilość zarchiwizowanych transportów, required=N
::# properties=SERVICE

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

{? var_pres('DNI',_in)<>type_of(0) | _in.DNI<0
|| _in.DNI:=-1;
   _mp.save('IN')
?};
_typ_oddzial:=var_pres('ODDZIAL',_in)<>type_of('');
{? _typ_oddzial | (+_in.ODDZIAL)>1 | ~exec('isLetter','#string',_in.ODDZIAL,1)
|| _in.ODDZIAL:={? _typ_oddzial || '' || '!' ?};
   _mp.save('IN')
?};

_akcja:=_mp.akcja();
_auto:=_akcja<>'Archiwizuj' & _mp.isAutoRun();
_service:=_mp.isService();
_grupa:=_mp.isGroup();
_archtr:={? var_pres('TR_NAG',_in)=type_of(null()) & _in.TR_NAG>0
         || 1
         |? var_pres('TR_NZL',_in)=type_of(null()) & _in.TR_NZL>0
         || 2
         || 0
         ?};
_servdysp:=_service & _archtr=2;

exec('init','ltr');

{? _service & ~_servdysp & (_in.DNI<0 | _in.ODDZIAL='!')
|| _mp.error('Nieprawidłowe parametry dla czynności serwisowej.'@)
|? ~_service & ~_archtr
|| _mp.error('Brak wymaganego parametru TR_NAG.')
|| _msk:={? _archtr=1 || ref_name(_in.TR_NAG)+4 || ref_name(_in.TR_NZL)+4 ?};
   TR_NZL.cntx_psh();
   TR_NAG.cntx_psh();
   TR_NZL.prefix();
   TR_NAG.prefix();
   _przywr:=0;
   _ok:=1;
   {? ~_service | _servdysp
   || exec('opentr','open_tab',_msk);
      TR_NZL.prefix();
      TR_NAG.prefix();
      _przywr:=(TR_NAG.name()+4<>'____');
      {? _archtr=1 & ~TR_NAG.seek(_in.TR_NAG)
      || _ok:=0;
         _mp.error('Nie znaleziono transportu.'@)
      |? _archtr=2 & ~TR_NZL.seek(_in.TR_NZL)
      || _ok:=0;
         _mp.error('Nie znaleziono dyspozycji.'@)
      ?}
   ?};
   {? _ok
   || {? _archtr=1
      || _mp.trigRef('TR_NAG',,0,,exec('kind_out','#b_port'),'TR_NAG')
      |? _archtr=2
      || _mp.trigRef('TR_NAG',,0,,exec('kind_out','#b_port'),'TR_NZL')
      ?};
      params_set('mp',_mp,'in',_in,'out',_out);
      {? _akcja='Archiwizuj' | _auto | _service
      || exec('archiwizuj','!ltr_tra_azle',{? _service || 2 || _auto ?}
          ,_in.DNI,_in.ODDZIAL,_grupa,{? _servdysp || 3 || _archtr ?})
      |? _mp.pathTodo() | _mp.pathArea() & _akcja='' & _archtr=1
      || _win_red:=exec('tr_nag_win_edit','transport','RED');
         _ff:="params_exec('trnag_archiwum_todo','!ltr_tra_azle'); 'key:Esc'";
         {? _przywr
         || TR_NAG.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Przywróć'@],_ff)
         || TR_NAG.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Arc&hiwizuj'@],_ff)
         ?};
         _ff:="'key:Esc'";
         TR_NAG.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
         TR_NAG.win_edit(_win_red);
         TR_NAG.display()
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   TR_NZL.cntx_pop();
   TR_NAG.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('TR_NAG',_in)<>type_of(~~) & _in.TR_NAG<>null()
|| {? ref_name(_in.TR_NAG)+4='____'
   || 'Zarchiwizuj transport: %1'@@[exec('record','#to_string',_in.TR_NAG)]
   || 'Przywróć z archiwum transport: %1'@@[exec('record','#to_string',_in.TR_NAG)]
   ?}
|? var_pres('TR_NZL',_in)<>type_of(~~) & _in.TR_NZL<>null()
|| {? ref_name(_in.TR_NZL)+4='____'
   || 'Zarchiwizuj dyspozycje transportu: %1'@@[exec('record','#to_string',_in.TR_NZL)]
   || 'Przywróć z archiwum dyspozycje transportu: %1'@@[exec('record','#to_string',_in.TR_NZL)]
   ?}
|| ''
?}


\trnag_archiwizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Transport - Archiwizuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_przywr:=(TR_NAG.name()+4<>'____');

_tab:=TR_NAG.sel_aget();
TR_NAG.sel_adel();

{? _tab.size()
|| {? _przywr
   || _tx:='Ilość transportów do przywrócenia z archiwum'@
    ||_tx:='Ilość transportów do archiwizacji'@
   ?};
   _ok:=FUN.ask('%1: %2.\n'
                'Operacja może być czasochłonna.\n\nCzy kontynuować?'@[_tx,$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LTR_TRA_AZLE';
   _params.UIDREF:=TR_NAG.uidref();
   _params.AKCJA:='Archiwizuj';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TR_NAG',TR_NAG.ref());
   exec('mp_run','#b__box',_params)
|? _ok=1
|| {? ~_przywr
   || exec('ini_kom','#message','Archiwizacja transportu'@)
   || exec('ini_kom','#message','Przywrócenie transportu z archiwum'@)
   ?};
   TR_NAG.cntx_psh();
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? (TR_NAG.clear(); TR_NAG.seek(_tab.REF,))
         || _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:='LTR_TRA_AZLE';
            _params.UIDREF:=TR_NAG.uidref();
            _params.GRUPA:='T';
            _params.AKCJA:='Archiwizuj';
            _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
            exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TR_NAG',TR_NAG.ref());
            exec('mp_run','#b__box',_params);
            obj_del(_params)
         ?};
         _tab.next()
      !}
   ?};
   TR_NAG.cntx_pop();
   exec('end_kom','#message')
?};
obj_del(_tab)


\trnag_archiwum_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Transport - Archiwizuj z todo
::   WE: params_get()
::----------------------------------------------------------------------------------------------------------------------
params_exec('archiwizuj','!ltr_tra_azle');
''


\archiwizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Transport - Archiwizacja transportu
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie) 2-serwisowa
::       [_b] - ilość dni wstecz
::       [_c] - oddzial lub pusty string
::       [_d] - czy grupa rekordów
::       [_e] - tryb archiwizacji 1-transport 2-dyspozycja bez transportu 3-serwis dla dyspozycji bez transportu
::       params_get()
::  OLD: \zam_arch/zk2.fml
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_dni:={? var_pres('_b')=type_of(1) || _b || 0 ?};
_oddzial:={? var_pres('_c')=type_of('') || _c || '' ?};
_grupa:={? var_pres('_d')=type_of(0) || _d || 0 ?};
_archtr:={? var_pres('_e')=type_of(0) || _e || 1 ?};

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_result:=0;

_out.TR_NAG:=null();
_out.TR_NZL:=null();
_out.IL_SERV:=0;
_mp.save(,_out);

TR_NAG.cntx_psh();
TR_NZL.cntx_psh();
TR_ZL.cntx_psh();
TR_LOG.cntx_psh();

{? _auto=2 & _archtr<>3
||
:: czynność serwisowa
   exec('opentr_psh','open_tab');
   _odd:=ST.ODDZ;
   _today:=date();
   ODDZ.cntx_psh();
   ODDZ.index('KOD2');
   {? _oddzial<>''
   || ODDZ.prefix(_oddzial,);
      {? ODDZ.first()
      || _oddz:=ODDZ.ref()
      || _oddz:=null()
      ?}
   || _oddz:=null()
   ?};
   {? type_of(_oddz)=type_of(null())
   || exec('opentr','open_tab','____');
      TR_NAG.cntx_psh();
      TR_NAG.index('SYM');
      {? _oddz=null() || TR_NAG.prefix() || TR_NAG.prefix(_oddzial,) ?};
      {? TR_NAG.first()
      || {!
         |? _ref:=TR_NAG.ref();
            _oki:=TR_NAG.next();
            TR_NAG.cntx_psh();
            TR_NAG.clear();
            {? (#_ref)>0 & TR_NAG.r_lock(1,1,1,#_ref,ref_name(_ref)) & (TR_NAG.seek(_ref);TR_NAG.r_unlock();1)
              & {? _dni>=0 || TR_NAG.DZ<=(_today-_dni) || 1 ?}
            || _old:=TR_NAG.uidref();
               _new:=exec('trnag_arch','transport_wspolne');
               {? _new<>''
               || _result+=1
               ?}
            ?};
            TR_NAG.cntx_pop();
            _oki
         !}
      ?};
      TR_NAG.cntx_pop()
   ?};
   ODDZ.cntx_pop();
   exec('opentr_pop','open_tab');
   ST.ODDZ:=_odd;
   _out.TR_NAG:=null();
   _out.TR_NZL:=null();
   _out.IL_SERV:=_result;
   _mp.save(,_out);
   _mp.done()
|? _auto | _grupa |
   ({? _archtr=1
    || {? TR_NAG.name()+4='____'
       || FUN.ask('Zarchiwizować transport %1?'@[TR_NAG.SYM])
       || FUN.ask('Przywrócić z archiwum transport %1?'@[TR_NAG.SYM])
       ?}
    || {? TR_PACK.name()+4='____'
       || FUN.ask('Zarchiwizować dyspozycję %1?'@[TR_NZL.SYM])
       || FUN.ask('Przywrócić z archiwum dyspozycję %1?'@[TR_NZL.SYM])
       ?}
    ?})
|| _old:={? _archtr=1 || TR_NAG.uidref() || TR_NZL.uidref() ?};
   {? _archtr=3 || _archtr:=2 ?};
   _new:=exec('trnag_arch','transport_wspolne',{? _grupa || 2 || 1 ?},_archtr);
   TR_NZL.cntx_psh();
   TR_NAG.cntx_psh();
   {? _new<>''
   || _result:=1;
      {? _archtr=1 || _out.TR_NAG:=exec('FindAndGet','#table',TR_NAG,_new,,,null())
      |? _archtr=2 || _out.TR_NZL:=exec('FindAndGet','#table',TR_NZL,_new,,,null()) ?}
   ?};
   _out.IL_SERV:=_result;
   _mp.save(,_out);
   TR_NZL.cntx_pop();
   TR_NAG.cntx_pop();
   _mp.done()
?};

TR_NAG.cntx_pop();
TR_NZL.cntx_pop();
TR_ZL.cntx_pop();
TR_LOG.cntx_pop();

_result


\trnag_przywroc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Transport - Przywróc z archiwum
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('trnag_archiwizuj','!ltr_tra_azle')


\oddzial
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Formuła na wartość parametru ODDZIAL
::   WY: ODDZ.KOD
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
ODDZ.cntx_psh();
ODDZ.index('KOD');
ODDZ.prefix();
ODDZ.win_sel('SEL');
{? ODDZ.select() || _wyn:=ODDZ.KOD ?};
ODDZ.cntx_pop();
_wyn


\tr_arch_pack
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: archiwizacja przesyłek wraz z dyspozycjami
::----------------------------------------------------------------------------------------------------------------------
_przywr:=(TR_NAG.name()+4<>'____');

_tab:=TR_PACK.sel_aget();
TR_PACK.sel_adel();

{? _tab.size()
|| {? _przywr
   || _tx:='Ilość przesyłek do przywrócenia z archiwum'@
   || _tx:='Ilość przesyłek do archiwizacji'@
   ?};
   _ok:=FUN.ask('%1: %2.\n'
                'Operacja może być czasochłonna.\n\nCzy kontynuować?'@[_tx,$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LTR_TRA_AZLE';
   _params.UIDREF:=TR_PACK.TR_NZL().uidref();
   _params.AKCJA:='Archiwizuj';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TR_NZL',TR_PACK.TR_NZL);
   exec('mp_run','#b__box',_params)
|? _ok=1
|| {? ~_przywr
   || exec('ini_kom','#message','Archiwizacja przesyłki'@)
   || exec('ini_kom','#message','Przywrócenie przesyłki z archiwum'@)
   ?};
   _buf:=tab_tmp(1,'TR_NZL','STRING[16]','');
   TR_NZL.cntx_psh();
   TR_PACK.cntx_psh();
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? (TR_PACK.clear(); TR_PACK.seek(_tab.REF,)) & (_buf.clear(); _buf.prefix($TR_PACK.TR_NZL); ~_buf.first())
         || _buf.blank();
            _buf.TR_NZL:=$TR_PACK.TR_NZL;
            _buf.add(1);
            _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:='LTR_TRA_AZLE';
            _params.UIDREF:=TR_PACK.TR_NZL;
            _params.GRUPA:='T';
            _params.AKCJA:='Archiwizuj';
            _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
            exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TR_NZL',TR_PACK.TR_NZL);
            exec('mp_run','#b__box',_params);
            obj_del(_params)
         ?};
         _tab.next()
      !}
   ?};
   obj_del(_buf);
   TR_NZL.cntx_pop();
   TR_PACK.cntx_pop();
   exec('end_kom','#message')
?};
obj_del(_tab);
{? _ok
|| TR_PACK.index('ALL');
   TR_PACK.prefix();
   TR_PACK.first()
?};

~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 176e857f1504e231a5e410f0b9c3274755f056ff77c61d1032f592a8707958380a2812d5fc2669e4e27094d98684e62ebccfda1aa0024ceb68d76d54ad807ef002050f401c96e945100832899d75ef45e9962f62e30a5486d302a60c4d4e9ce26eb0ebdc94808c9fad01cbd37b090a1847bc0787409e8011d7050eed5290d72d

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_dzpo.fml
:: Utworzony: 31.10.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_DZPO - Blokada plan. w okr. rozl.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Zablokowanie planowania dla okresu rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=T
::# kind=WY, symbol=S_PLAN, type=STRING, name=Status blokady planowania, required=N
::
_par:=params_get();
params_set(_par);
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;
_uidref:=exec('ref2uid','#table',_in.A_OKR);
_result:='';

{? _uidref=''
|| _mp.error(exec('error','!prc_czp_dzpo','Nie znaleziono okresu rozliczeniowego.'@))
|? _mp.pathProc() | _mp.pathTodo()
|| _result:=exec('zablokowanie','!prc_czp_dzpo',_in,_out);
   {? _out.S_PLAN='Z'
   || _mp.save(,_out);
      _mp.done()
   |? var_pres('_result')=type_of('')
   || _mp.error(_result)
   |? _result:=2
   || _mp.keep()
   || _mp.save(,_out);
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Zablokowanie planowania dla okresu rozliczeniowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a_okr:=params_get().mp.load(exec('kind_in','#b_port')).A_OKR;
_tab:=exec('opis_okr','okres',_a_okr);

{? _tab.ZAW_DANE='T'
|| 'Zablokuj planowanie w okresie: %1 %2 %3'@@[_tab.A_OKRN_NAZWA,_tab.OD,_tab.DO]
|| 'Zablokuj planowanie w okresie'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Szczegóły komunikatu o błędzie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Zablokowanie okresu do planowania nie jest możliwe.'@,_a]


\zablokowanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Zablokowanie planowania dla okresu rozliczeniowego.
::   WE: _a - parametry wejściowe czynności
::       _b - parametry wyjściowe czynności
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:=_result:='';
A_OKR.cntx_psh();
{? A_OKR.seek(_a.A_OKR,,1)
|| {? A_OKR.S_PLAN='O'
   || _tab:=tab_tmp(,'RESULT','INTEGER','');
      _ed:=_tab.mk_edit('Okres: %1'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1],0);
      _tab.win_efld(_ed,AH,'H',,,,,,'Okres rozliczeniowy zostanie zablokowany.'@);
      exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:Esc'",,
         exec('help_red_zakoncz','#window','PKD_J'),exec('text_red_zakoncz','#window','PKD_J'));
      exec('ok_esc','#window',_tab,_ed,1,,"cur_tab().RESULT:=0; 'key:Esc'",0,,
         exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
      _tab.win_edit(_ed);
      _tab.edit();
      {? _tab.RESULT=1
      || params_exec('a_okr_wer_bflz','okres',0);
         A_OKR.seek(_a.A_OKR,,1,1);
         {? A_OKR.S_PLAN='Z'
         || _b.S_PLAN:='Z';
            _msg:='Okres rozliczeniowy %1 został zablokowany.'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1];
            _result:=1
         || _msg:='Okres rozliczeniowy %1 nie został zablokowany.'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1];
            _result:=_msg
         ?}
      || _result:=2
      ?};
      obj_del(_tab)
   || _result:=_msg:=exec('error','!prc_czp_dzpo','Okres rozliczeniowy jest już zablokowany.'@)
   ?}
|| _result:=_msg:=exec('error','!prc_czp_dzpo','Nie udało się odnaleźć okresu rozliczeniowego do zablokowania.'@)
?};
{? +_msg || FUN.emsg(_msg) ?};
A_OKR.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 278fb42c3c988558d81735c2cc08e26cfceb22c89a2e82afbd484e56c0f37c8052a8fdc7580ae6d57ffc60f353b703847f1ef9543d9bbb8dd265e83d13ec1e44c7bce9157db33dd6c7033576fa418c0b33c84ce353508e19d83f8613c478fc857c2c9de208c2ee87f3299169e0022675e65ffa464ac53717a4b12ffe7e6500cb

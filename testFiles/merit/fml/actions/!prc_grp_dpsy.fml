:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_grp_dpok.fml
:: Utworzony: 27.06.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_GRP_DPSY - Przypisanie systemu czasu pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Czynność umożliwiająca grupowe przypisanie systemu czasu pracy wybranym pracownikom.
::   WE:
::   WY:
::  OLD: \grup_scp/okres.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PRC';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_args.F_ZATR:={? P_FILTER.F_ZATR().KOD='' || 'P,Z,K,T,R' || P_FILTER.F_ZATR().KOD ?};
_args.VIEW:=P_FILTER.STATUS;

P.cntx_psh();
P.first();
_PRAC:=exec('wybierz','pracownik',_args).P;
P.cntx_pop();

_par:=obj_new('NAZ','REF','EDIT');
_par.REF:=null();
_par.EDIT:=0;
VAR_EDIT.win_edit('WEWY_DAT');
VAR_EDIT.DATA:=date();

{? _PRAC.first()
|| A_SCP.cntx_psh();
   A_SCP.win_sel('WER');
   A_SCP.actions('WER','');
:: Wybór systemu czasu pracy
   {!
   |? {? ~A_SCP.select()
      || {? FUN.ask('Czy na pewno chcesz zrezygnować z wprowadzania danych?'@)
         || A_SCP.cntx_pop();
            return(~~)
         || 1
         ?}
      || _par.NAZ:=A_SCP.NAZ;
         _par.REF:=A_SCP.ref();
         0
      ?}
   !};
   {!
   |? {? ~VAR_EDIT.edit("__CHK.record(VAR_EDIT,,'DATA')")
      || {? FUN.ask('Czy na pewno chcesz zrezygnować z wprowadzania danych?'@)
         || A_SCP.cntx_pop();
            return(~~)
         || 1
         ?}
       || _par.EDIT:=1;
          0
      ?}
   !};
:: Przypisanie wybranego systemu czasu pracy grupie współpracowników
   {? _par.EDIT
   || {? FUN.ask('Czy na pewno rozpocząć proces wprowadzania danych dla wybranej grupie pracowników?'@)
      || {? _PRAC.first()
         || PROGRESS.set(_PRAC.size(),'%1 %2'['Trwa przetwarzanie danych.'@,'Proszę czekać.'@]);
            exec('ini_kom','#message','Informacje o zapisie specyfikacji'@,,65535);
            A_SCPP.cntx_psh();
            A_SCPP.index('POD');
            A_SCPP.prefix();
            {!
            |? PROGRESS.next();
               P.seek(_PRAC.REF,,1);
               {? ~A_SCPP.find_key(P.ref(),VAR_EDIT.DATA)
               || A_SCPP.blank();
                  A_SCPP.P:=P.ref();
                  A_SCPP.OD:=VAR_EDIT.DATA;
                  A_SCPP.A_SCP:=_par.REF;
                  {? A_SCPP.add(1)
                  || exec('add_kom','#message',
                        'Domyślny system czasu pracy %1 został przypisany dla współpracownika %2 od %3.'@
                           [_par.NAZ,$A_SCPP.P().IP+' '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,A_SCPP.OD$1
                           ],38,'Zakończone powodzeniem'@
                     )
                  || exec('add_kom','#message',
                        'Nie powiodło się dodanie domyślnego systemu czasu pracy %1 dla współpracownika %2 od daty %3.'@
                           [_par.NAZ,$A_SCPP.P().IP+' '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,A_SCPP.OD$1
                           ],14,'Nie zakończone powodzeniem'@
                     )
                  ?}
               || exec('add_kom','#message',
                     'Domyślny system czasu pracy %1 dla współpracownika %2 od daty %3 został dodany wcześniej.'@
                        [_par.NAZ,$A_SCPP.P().IP+' '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,A_SCPP.OD$1
                        ],184,'Już istnieje'@
                  )
               ?};
               _PRAC.next()
            !};
            exec('end_kom','#message');
            A_SCPP.cntx_pop();
            PROGRESS.close();
            FUN.info('Zakończono proces wprowadzania danych.'@);
            KOMM.select()
         || FUN.info('Nie wybrano żadnego pracownika.'@)
         ?}
      ?}
   ?};
   A_SCP.cntx_pop()
|| FUN.info('Nie wybrano żadnego pracownika.'@)
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:58 3eb41cb751bbad4661f1f6ad0727b96d61ea5e8be86ddbbaf56cdb0f39bf86e257a6b804e1f07e4d123fc0bb052f05fa6706b1c98bbbc28d41fd70700cdf30d34390e47bafcafb66cfa8baa7e2cdb27fe229ab3cca17ea5c7d7a26e0f261a03b4004b07003f800ef19cc8c5aa17adb19419d4762ab874db9c328aec6dc3de4d3

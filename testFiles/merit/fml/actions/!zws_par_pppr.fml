:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pppr.fml
:: Utworzony: 02.03.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PPPR - Progi premii za publ. naukowe
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Progi premii za publ. naukowe - główna formuła czynności.
::  OLD: \pp_rokbl/dnauk.fml
::  OLD: \ppwer/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_rok:=
   {? var_pres('_par')>100 & var_pres('in',_par)>100 & var_pres('FP_INS_NUM',_par.in)=type_of(0)
   || _par.in.FP_INS_NUM
   || date()~1
   ?};

_LATA:=sql('select distinct ROK from PROGPREM where FIRMA=:_a order by 1 desc',exec('ref_firma','ustawienia'));
_LATA.fld_fml('ROK','BLANK',"
   _LATA:=cur_tfld();
   _LATA.cntx_psh();
   _bl:={? _LATA.first() || _LATA.ROK+1 || date()~1 ?};
   _LATA.cntx_pop();
   _bl
");

{? ~_LATA.find_key(_rok)
|| _LATA.blank();
   _LATA.ROK:=_rok;
   _LATA.add()
?};

: Wszystkie dodawane akcji i przycisk dla tabeli _LATA powinny być związane z bieżącą czynnością (patrz: task_attach()).
: Ponieważ jednak kod ten będzie uruchamiany przez menadżera procesu, to już na tym etapie nastąpi weryfikacja
: uprawnień - użytkownik, który nie będzie miał dostępu do czynności nie uruchomi jej. Nie ma więc potrzeby weryfikacji
: uprawnień już  po uruchomieniu czynności.

_fml:='"params_set(params_get()); exec(\'"+_a+"\',\'!zws_par_pppr\')"';

_ws:=_LATA.mk_sel(,'N',,'#pppr_lataw',,,,,'U');
_LATA.win_fld(_ws,,'ROK',,,-5,0,,'Rok'@,,'Rok kalendarzowy'@);
{! _empty:=0 .. 1
|! _LATA.win_act(_ws,_empty,'Dołącz',,,,,"0",1)
!};
_LATA.win_act(_ws,,'Formuła','Usuń'@@,,'Usunięcie bieżącego zapisu'@,$($_fml)('lata_usun_b'),,,,,,'U');
_LATA.win_act(_ws,,'Formuła','Kopiuj'@@,,'Kopiowanie zapisu bieżącego roku do wskazanego'@,
   $($_fml)('lata_kopiuj_b'),,,,,,'K');
{! _empty:=0 .. 1
|! _LATA.win_act(_ws,_empty,'Rekord',,,,,$($_fml)('lata_ae'))
!};
_LATA.win_btn(_ws,'text='+'Dołącz'@,'menu:D');
_LATA.win_btn(_ws,'text='+'Usuń'@,'menu:U',,,,,,'noempty');
_LATA.win_btn(_ws,'text='+'Kopiuj'@,'menu:K',,,,,,'noempty');

_we:=_LATA.mk_edit('Rok'@,,'#pppr_latar');
_LATA.win_esep(_we,'Dane podstawowe'@);
_LATA.win_efld(_we,,'ROK',,,5,0,,'Rok'@,,'Rok kalendarzowy'@);
_LATA.efld_opt(_we,'mark=1',,'ROK');
exec('ok_esc','#window',_LATA,_we,,,,,,,exec('text_red_ok','#window'));
_LATA.win_edit(_we);

_gwer:=_LATA.grp_make('Progi premii za publikacje naukowe'@,,'#pppr_latag',,,,,'normal');
_LATA.grp_sel(_gwer,,_ws,,"params_set(params_get()); grp_disp(PROGPREM,'WER')",,,12,,,,,'maximized');
_LATA.grp_splt(_gwer,,'vertical','panel1');
_LATA.grp_sel(_gwer,PROGPREM,'WER',,,,,12,
   "  _par:=params_get();
      {? grp_empty(_par.LATA,_par.ws)
      || '#disable'
      || PROGPREM.prefix(exec('ref_firma','ustawienia'),params_get().LATA.ROK)
      ?}
   ",,,,'maximized');

_LATA.win_sel(_gwer);

params_set('LATA',_LATA,'ws',_ws);

PROGPREM.cntx_psh();
PROGPREM.index('ROKPKT');
PROGPREM.win_edit('RED');
_fblank:=PROGPREM.fld_fml('ROK','BLANK',"params_get().LATA.ROK");
_LATA.select(,1,-1);
PROGPREM.fld_fml('ROK','BLANK',_fblank);
PROGPREM.cntx_pop();

~~


\lata_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń - przed"  dla tabeli tymczasowej z latami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_LATA:=params_get().LATA;

PROGPREM.cntx_psh();
PROGPREM.index('ROKPKT');
PROGPREM.prefix(exec('ref_firma','ustawienia'),_LATA.ROK);
{? {? PROGPREM.first()
   || exec('del_conf','#table')
   || exec('del_ask','#table')
   ?}
|| do();
   {? PROGPREM.first()
   || {! |? PROGPREM.del() !}
   ?};
   _LATA.del();
   {? ~end()
   || FUN.info('Usunięcie bieżącego zapisu nie powiodło się.'@)
   ?}
?};
PROGPREM.cntx_pop();
~~


\lata_kopiuj_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Kopiuj - przed" dla tabeli tymczasowej z latami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_LATA:=_par.LATA;
{? PROGPREM.size() |
   FUN.ask('Brak progów premii za publikacje naukowe za rok %1.\nNa pewno chcesz kontynuować kopiowanie?'@ [$_LATA.ROK])
|| _src:=_LATA.ROK;
   _LATA.blank();
   {? _LATA.edit("params_set(params_get()); exec('lata_ae','!zws_par_pppr')")
   || _dest:=_LATA.ROK;
      do();
      {? ~_LATA.find_key(_dest)
      || _LATA.blank();
         _LATA.ROK:=_dest;
         _LATA.add()
      ?};
      PROGPREM.cntx_psh();
      PROGPREM.index('ROKPKT');
      PROGPREM.prefix(exec('ref_firma','ustawienia'),_src);
      {? PROGPREM.first()
      || {!
         |? PROGPREM.cntx_psh();
            PROGPREM.prefix();
            PROGPREM.ROK:=_dest;
            PROGPREM.add();
            PROGPREM.cntx_pop();
            PROGPREM.next()
         !}
      ?};
      PROGPREM.cntx_pop();
      {? ~end()
      || FUN.info('Kopiowanie progów premii za publikacje naukowe nie powiodło się.'@)
      ?}
   ?}
?};
~~


\lata_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" dla tabeli tymczasowej z latami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_LATA:=params_get().LATA;
{? (_chk:=__CHK.record(_LATA,,'ROK'))<>''
|| return(_chk)
?};

_act:=-menu_txt();
{? _act='dołącz'
|| _LATA.cntx_psh();
   _dubel:=_LATA.find_key(_LATA.ROK);
   _LATA.cntx_pop();
   {? _dubel
   || __CHK.err_ndx(_LATA,_LATA.index('?'),0);
      return('ROK')
   ?}

|? _act='kopiuj'
|| PROGPREM.cntx_psh();
   PROGPREM.index('ROKPKT');
   PROGPREM.prefix(exec('ref_firma','ustawienia'),_LATA.ROK);
   _dubel:=PROGPREM.first();
   PROGPREM.cntx_pop();
   {? _dubel
   || __CHK.err_ndx(_LATA,_LATA.index('?'),0);
      return('ROK')
   ?}
?};

''


\progprem_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.30]
:: OPIS: ObsŁuga akcji "Rekord - po" dla tabeli PROGPREM.
::   WE:
::   WY:
::  OLD: \ppae/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(PROGPREM,-menu_txt()='popraw',,'PREMIA');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & ~_chk)
|| return(_chk)
?};

{? PROGPREM.PUNKTY<0
|| return(__CHK.err_fld(PROGPREM,'PUNKTY',1,'Wartość nie może być ujemna.'@))
?};

{? PROGPREM.PUNKTY=0 &
   FUN.ask(
      'Wprowadzenie progu z zerową liczbą punktów może doprowadzić\n'
      'do naliczenia premii za publikacje nawet dla pracowników bez żadnej publikacji.\n'
      'Czy akceptujesz dane?'
   )=0
|| return('PUNKTY')
?};

{? PROGPREM.PREMIA<0
|| return(__CHK.err_fld(PROGPREM,'PREMIA',1,'Wartość nie może być ujemna.'@))
?};

''

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 bb306790386dfeb8517fb524c18753387cf5ab71f0471a9aac77fba68e38ef27f35b524c485e10c3b52c0c4e44f6071ccfea2d7789bc7da79fa5a9c8bcd11bdd751fd8d69a4bf6db9ac48115446c3fc9fbf08ee0b7e7e100c48b186ae3cce8369dca43482caa503f1c926e59ad927a637ab8b51fd4e766562d02bde4b2663920

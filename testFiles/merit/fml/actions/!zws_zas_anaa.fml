:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_zas_anaa.fml
:: Utworzony: 2017.05.30
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_ZAS_ANAA - Akceptacja analiz zasobów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Formuła główna czynności ZWS_ZAS_ANAA
::----------------------------------------------------------------------------------------------------------------------
:: PARAMETRY WE:
::# kind=WE, symbol=ANZASPE, type=_ANZASPE, name=Wskazanie na nagłowek analizy, required=N, keyref=T

:: PARAMETRY WY:
::# kind=WY, symbol=ANZASPE, type=_ANZASPE, name=Wskazanie na nagłowek analizy, required=N, keyref=T
::# kind=WY, symbol=POTR, type=STRING, name=Analiza do potrąceń kadrowo-płacowych, required=N

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

:: akceptuj z pulpitu
{? _akcja='Akceptuj'
|| params_get().context.ANZASPE;
   {? ANZASPE.ZAK_REJ='N'
   || FUN.info('Rejestracja analizy nie została zakończona.'@)
   || params_exec('akc_anzaspe2','!zws_zas_anaa')
   ?}
:: TODO lub auto
|? _mp.pathTodo() | _mp.isAutoRun()
|| menu_txt(,'Popraw');
   __PARSES.setEnv('FKS',0);
   {? var_pres('ANZASPE',_we)>0
   || _ref:=BB.sqlint($_we.ANZASPE);
      {? _ref<>0
      || ANZASPE.cntx_psh(); ANZASPE.prefix();
         {? ANZASPE.seek(_ref,ANZASPE.name())
         || {? ANZASPE.ZAK_REJ='N'
            || FUN.info('Rejestracja analizy nie została zakończona.'@)
            || params_exec('akc_anzaspe1','!zws_zas_anaa')
            ?}
         ?};
         ANZASPE.cntx_pop()
      ?}
   || FUN.info('Nie znaleziono analizy zasobów.'@);
      _wy.ANZASPE:=null;
      _wy.POTR:='N';
      _mp.save(,_wy);
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Formuła na opis TO-DO czynności ZWS_ZAS_ANAA
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_akcja:=_mp.akcja();
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('ANZASPE',_we) & _we.ANZASPE
|| _ref:=BB.sqlint($_we.ANZASPE);
   {? _ref<>0 & ANZASPE.seek(_ref,ANZASPE.name())
   || _desc:='Zakończ akceptację analizy zasobów: %1'@@[ANZASPE.OPIS]
   || _desc:='Zakończ akceptację analizy zasobów'@@
   ?}
|| _desc:='Zakończ akceptację analizy zasobów'@@
?};
_desc


\akc_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Akceptacja analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_ZAS_ANAA';
_params.AKCJA:='Akceptuj';
_params.UIDREF:=ANZASPE.uidref();
_params.CONTEXT:=obj_new('ANZASPE');
_params.CONTEXT.ANZASPE:=ANZASPE.ref();
exec('mp_run','#b__box',_params)


\akc_anzaspe1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Akceptacja analizy zasobów - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
{? ANZASPE.get()
|| {? ANZASPE.r_lock(1,1)
   || akc_an:=0;
      exec('anzaspe_edit','!zws_zas_anaa');
      ANZASPE.display();
      {? akc_an
      || _args:=params_get();
         _wy:=_args.out;
         _mp:=_args.mp;
         _wy.ANZASPE:=ANZASPE.ref();
         _wy.POTR:=ANZASPE.DEFAN().WYK_POTR;
         _mp.save(,_wy);
         _mp.descTodo();
         {? akc_an
         || ANZASPE.AKC_ANAL:='T';
            ANZASPE.AKC_DATA:=date();
            ANZASPE.AKC_TIME:=time();
            ANZASPE.AKC_USER:=OPERATOR.USER;
            ANZASPE.put();
            _mp.done()
         ?}
      ?};
      VAR_DEL.delete('zak_an');
      unlock_r()
   || FUN.info('Analiza zasobów wykorzystywana przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono analizy zasobów.'@)
?}


\akc_anzaspe2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Akceptacja analizy zasobów - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
{? ANZASPE.get()
|| {? ANZASPE.r_lock(1,1)
   || _args:=params_get();
      _wy:=_args.out;
      _mp:=_args.mp;
      _wy.ANZASPE:=ANZASPE.ref();
      _wy.POTR:=ANZASPE.DEFAN().WYK_POTR;
      _mp.save(,_wy);
      _mp.descTodo();
      ANZASPE.AKC_ANAL:='T';
      ANZASPE.AKC_DATA:=date();
      ANZASPE.AKC_TIME:=time();
      ANZASPE.AKC_USER:=OPERATOR.USER;
      ANZASPE.put();
      _mp.done();
      unlock_r()
   || FUN.info('Analiza zasobów wykorzystywana przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono analizy zasobów.'@)
?}


\wakc_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wycofanie akceptacji analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
ANZASPE.AKC_ANAL:='N';
ANZASPE.AKC_DATA:=date(0,0,0);
ANZASPE.AKC_TIME:=time(0,0,0);
ANZASPE.AKC_USER:=null;
ANZASPE.put()


\anzaspe_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Tworzy okno redagowania analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
_okn:=ANZASPE.mk_edit('Analiza zasobów'@);
ANZASPE.win_ewin(_okn,,'RED_POP');
_btn0:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['P&ozycje'@],
                        "params_exec('poz_anzaspe','zasoby_wspolne');''"
                       );
ANZASPE.btn_eopt(_okn,_btn0,'tooltip='+'Przeglądanie pozycji analizy zasobów'@);
_btn1:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['P&rzelicz'@],
                        "params_exec('przel_anzaspe','zasoby_wspolne');''"
                       );
ANZASPE.btn_eopt(_okn,_btn1,'tooltip='+'Przeliczanie analizy zasobów'@);
_btnakc:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Akceptuj'@],
                "akc_an:=1; 'key:F2'");
ANZASPE.btn_eopt(_okn,_btnakc,'tooltip='+'Akceptuj analizę'@);
_btnan:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],'key:Esc');
ANZASPE.btn_eopt(_okn,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
ANZASPE.win_edit(_okn)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 4ecfbfa7c04f68caec4f4802a6235b34783e027a4fdef58527d8ef718297e5040229681368dcefa80024f072bb23b57f96789b2e153f7c06a40cdff7ba3ef1b393cd9a967d1d8bba13d47bd46b6036003238ebf9ed2d7d4d29cce5810f25b0a61ff1270998d2e0e05bebeefa5e4543e5a0fb14f21f50ba0649d85d4df17a20ff

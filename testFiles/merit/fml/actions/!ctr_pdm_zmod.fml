:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pdm_zmod.fml
:: Utworzony: 16.12.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PDM_ZMOD - Przegląd wartości dla modeli
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przegląd wartości dla modeli - główna formuła czynności CTR_PDM_ZMOD
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::  OLD: \przegwar/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
_b.grp_sel(_c,K__NAG,'WER',_a,"",,,,"
   K__NAG.index('DISP'); K__NAG.prefix(REF.S_FIRMA);
   K__NAG.win_edit('RED_POP');
   1
",,,,'maximized_with_title');
task_attach('CTR_PDM_ZMOD');
task_attach('CTR_PDM_ZMOA');
task_attach('CTR_PDM_ZMOP');
~~


\br_k_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed tabeli K__NAG
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
{? K__NAG.SKID_MB().TYP='O'
|| _gray+='T'; _mark:='W'
|| _gray+='W'; _mark:='T'
?};
K__NAG.actions_grayed('WER',_gray);
K__NAG.actions('WER',,_mark,1);
''


\war_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Wartosci dla modeli
::  OLD: \war_mod/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
{? K__NAG.SKID_MB().TYP='P' & ~exec('chk_upr_plac','control')
|| FUN.info('Brak uprawnień do przeglądania wartości podziałów\ndla controllingu opartych o model kadrowo-płacowy.'@)
|? K__NAG.r_lock(1,1)
|| exec('war_mod_ini','control',1);
   exec('war_mod_del','control');
   K__NAG.r_unlock()
|| exec('msgmoderr','control')
?}


\rprz_tab_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2011]
:: OPIS: Tabela TAB_WAR - rekord przed
::  OLD: \rprz_tab_war/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
{? TAB_WAR.RODZAJ=1
|| 'TAB_WAR#01#01'
|? TAB_WAR.RODZAJ=2
|| 'TAB_WAR#01#02'
|? TAB_WAR.RODZAJ=3
|| 'TAB_WAR#01#03'
|? TAB_WAR.RODZAJ=5
|| 'TAB_WAR#01#05'
|? TAB_WAR.RODZAJ=6
|| 'TAB_WAR#01#06'
|? TAB_WAR.RODZAJ=7
|| 'TAB_WAR#01#07'
|| ''
?}


\dsp_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Na wyswietl dla pol UD_POM.SKL01-UD_POM.SKL15
::   WE: _a - numer pola
::  OLD: \dsp_skl/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('ile_wym')>0
|| {? _a>ile_wym || exec('findfnv','#color') || '' ?}
|| ''
?}


\zmokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Zmiana okresu dla zapisow
::  OLD: \zmokrzap/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POM.win_edit('OKRZAP');
_okr_zap:=UD_POM.OKR_ZAP; UD_POM.OKR_ZAP().ROK();
{? UD_POM.edit("exec('spr_okr','!ctr_pdm_zmod')") & _okr_zap<>UD_POM.OKR_ZAP
|| exec('k_war_zakr','control',zakl_war)
?}


\spr_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Sprawdzanie roku i okresu dla zapisow
::  OLD: \spr_okr/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? UD_POM.OKR_ZAP=null
|| FUN.info('Nie wybranu okresu obrachunkowego.'@); _zwrot:='OKR_ZAP'
?};
_zwrot


\moddok_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Tabela K__WAR akcja "Zapisy dokumentu"
::  OLD: \moddok_p/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('usr_fjks','b_perm',DOK.ODD().OD,OPERATOR.USER,K__WAR.REJ().ROK().FIRMA)
|| exec('zapisy','dok_fks',form(K__WAR.REF_DOK-8)+4,0,0,K__WAR.REJ,K__WAR.POZ,'ZC')
|| FUN.info('Brak uprawnień do jednostki księgowej: %1'@[DOK.ODD().OD])
?}


\moddok_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Tabela K__WAR akcja "Naglowek dokumentu" i "Podzialy controllingowe"
::   WE: _a = 1 - naglowek dokumentu, 2 - podzialy controllingowe
::  OLD: \moddok_n/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
_error:=0;
{? K__WAR.REF_DOK<>'' & K__WAR.REJ<>null & K__WAR.POZ<>0
|| DOK.cntx_psh();
   _ref:=BB.sqlint(K__WAR.REF_DOK); _nam:=form(K__WAR.REF_DOK-8);
   {? _ref<>0 & _nam<>''
   || DOK.use(_nam); DOK.prefix();
      {? DOK.seek(_ref,_nam)
      || {? _a=1
         || exec('dok_spac','dok_fks')
         || _maska:=DOK.name()+4;
            SKIDXD.cntx_psh(); VPOZ.cntx_psh(); POZ.cntx_psh(); POW.cntx_psh();
            POZ.use('pozy'+_maska);
            POW.use('pow_'+_maska);
            VPOZ.use('pozv'+_maska);
            SKIDXD.use('skxd'+_maska);
            exec('podzktrl','control',0,'DPUAZ');
            SKIDXD.cntx_pop(); VPOZ.cntx_pop(); POZ.cntx_pop(); POW.cntx_pop()
         ?}
      || _error:=1
      ?}
   || _error:=1
   ?};
   DOK.cntx_pop()
|| _error:=1
?};
{? _error || FUN.info('Nie znaleziono powiązanego dokumentu.'@) ?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 307a090fb449145fd560c3ffb54666f34076c114ac7229df3365a4357453db0bef609f045b11a21dec894c25c0ee8d9615b47b2d6eb505d66383aa58f4917856fdeab940af0581825a90e0540aa150091264765d8f6d1a37e2567c083a0c70be079c476e5d668e389408fea5fc6169117d07f78a8042a3f7bee3b55cdf8cb488

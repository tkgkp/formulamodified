:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_kkgb.fml [17.00]
:: Utworzony: 2015.12.09
:: Autor: AMK
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_KKGB - Blokowanie sprzedaży do kontrahenta
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności ZWS_PAR_KKGB - Blokowanie sprzedaży do kontrahenta
::----------------------------------------------------------------------------------------------------------------------
ZDARZT.cntx_psh(); ZDARZ.cntx_psh();
__PAR188:=PAR_SKID.get(188)='T';
ZDARZT.win_fml('ZDARZT2',,'ICON',,'ICON_BEFORE',"ZDARZT.ICON");
ZDARZ.win_sel('ZDBKH'); ZDARZ.actions('ZDBKH','',,1);
ZDARZ.win_edit('ZDKBLO'); ZDARZ.win_patt('');
ZDARZ.hdr_sel(); ZDARZ.hdr_sel(' sprzedaży kontrahenta: %1'@[KH.SKR]);
ROZNE.R:='B';
ZDARZ.index('ZDTKH'); ZDARZ.prefix('B',KH.ref());
ZDARZ.select();
ZDARZT.cntx_pop(); ZDARZ.cntx_pop();
~~


\kh_blok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ISZ [8.40]
:: OPIS: Zmiana znacznika blokady kontrahenta
::   WE: _a - 1 - blokowanie
::            2 - odblokowanie
::  OLD: \kh_blok/kh_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_kh_b:=__War_f('KH_DOD','B',KH.ref); {? _kh_b='' || _kh_b:='N' ?};
_blok:=0;
{? _a=2
|| {? _kh_b='N'
   || FUN.info('Aktualny status kontrahenta: brak blokady.'@)
   || _blok:=1
   ?}
|? _a=1 & _kh_b='B'
|| {? FUN.ask('Aktualny status kontrahenta: blokada bezwarunkowa.\nZablokować warunkowo kontrahenta: %1?'@[KH.SKR])
   || _blok:=3
   ?}
|? _a=1 & _kh_b='W'
|| {? FUN.ask('Aktualny status kontrahenta: blokada warunkowa.\nZablokować bezwarunkowo kontrahenta: %1?'@[KH.SKR])
   || _blok:=2
   ?}
|? _a=1 & _kh_b='N'
|| _blok:=FUN.choice('Aktualny status kontrahenta: brak blokady'
   '\nZmienić znacznik blokady dla kontrahenta: %1?'@[KH.SKR],,'Blokuj warunkowo'@,'Blokuj bezwarunkowo'@);
   {? _blok=1 || _blok:=3 ?}
?};
{? _blok>0
|| ROZNE.R:='B';
   ZDARZ.blank();
   ZDARZ.memo_set(,'OP');
   ZDARZ.win_edit('ZDKBLO');
   {? _a=1
   || ZDARZ.hdr_edit('Podaj powód zablokowania kontrahenta %1'@[KH.KOD])
   || ZDARZ.hdr_edit('Podaj powód odblokowania kontrahenta %1'@[KH.KOD])
   ?};
   {? ZDARZ.edit("__CHK.record(ZDARZ,,'KR_OP')")
   || {? _blok=1
      || exec('szukaj','rozrach_kor','T','ODBLOKOWANIE','N',1)
      || {? _blok=2
         || exec('szukaj','rozrach_kor','T','BL_BEZWARUNKOWE','B',1)
         || exec('szukaj','rozrach_kor','T','BL_WARUNKOWE','W',1)
         ?}
      ?}
   ?}
?};
0


\beokn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Okienko przed tabela ZDARZ (kontakty)
::  OLD: \beokn/kh_ser.fml
::----------------------------------------------------------------------------------------------------------------------
ZDARZ.index('ZDTKH');  ZDARZ.win_sel({? -_a='b' || 'ZDBKH' || 'ZDKKH' ?});
ZDARZ.win_patt({? -_a='h' || 'SZUK_KON' || '' ?});
{? var_pres('pulpit')>0 & WIND_KH.get()
|| WIND_KH.KH();
   ZDARZ.actions('ZDBKH','',,1); ZDARZ.actions('ZDKKH','',,1);
   ZDARZ.hdr_sel(); ZDARZ.hdr_sel('  kontrahenta: %1'@[KH.SKR]);
   ROZNE.R:=_a;
   ZDARZ.prefix(_a,KH.ref());
   ZDARZ.win_edit(_b)
|? var_pres('pulpit')>0
|| ZDARZ.prefix('X',null);
   ZDARZ.actions('ZDBKH',':B',,1);
   ZDARZ.actions('ZDKKH',':d',,1)
|| ZDARZ.actions('ZDBKH'); ZDARZ.actions('ZDKKH')
?}; 1


\skid_zop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Opis zdarzenia
::  OLD: \skid_zop/gen_ser.fml
::----------------------------------------------------------------------------------------------------------------------
ZDARZ.memo_get(,'OP');
{? ZDARZ.memo_edit(,'OP')
|| ZDARZ.memo_put(,'OP')
?};
0

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:04 0f6a3b408b006022013af497ff6be360b8a9f4dc96cb65ccf158f03474b8964cdb5176020996e1ce6124e7de7ac434ff274319838ac57e92006fa84ef782c7c94046116bced5f649da4101313d6e00f921660f152a70d696724fad8b905cea3a231c7d5f374ba707e9086d4beee93815f32d63524c15038e684d3695027dda3c

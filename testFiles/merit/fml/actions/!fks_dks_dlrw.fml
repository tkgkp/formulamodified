:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dlzk.fml
:: Utworzony: 10.08.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DLRW - Zbiorcza dekretacja paragonów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zbiorcza dekretacja paragonów - główna formuła czynności FKS_DKS_DLRW.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
::# parses=exec('parses','!fks_dks_dlrw')
::# access=exec('uprawnienia','!fks_dks_dlrw')
::
:: PARAMETRY WE:
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WE, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
:: PARAMETRY WY:
::# kind=WY, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WY, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=N
:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=LOOP
_args:=params_get();
_we:=_args.in;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_wew:=_args.int;
_wy:=_args.out;
params_set(params_get());
{? var_pres('UNIK_ID',_we)>0 & _we.UNIK_ID<>'' & var_pres('OKRO_F',_we)>0 & _we.OKRO_F<>null
|| params_exec('par_out','!fks_dks_dlrw',_we.OKRO_F,_we.UNIK_ID,_mp,_wy);
   _mp.done()
|| {? _akcja='Dekretuj' | ~var_pres('OKRO_F',_we) | _we.OKRO_F=null
   || _we.OKRO_F:=SSTALE.AO;
      _mp.save(,_we)
   ?};
   {? _we.OKRO_F<>null
   || zap_par:=''; iddekpar:=tm_stamp();
      {? _mp.pathProc()
      || SSTALE.AO:=_we.OKRO_F; SSTALE.AR:=SSTALE.AO().ROK;
         BILANSP.ASYNT:=SSTALE.AR().SYNT;
         BILANSP.ASEP:=SSTALE.AR().SEP;
         BILANSP.AROK:=SSTALE.AR;
         POMOC.OKR:=SSTALE.AO;
         EKSG.ODD:=POMOC.OKR().POCZ;
         EKSG.DOD:=POMOC.OKR().KON;
         EKSG.SYS:='LSP';
         EKSG.WS:=EKSG.WT:='N';
         exec('open_tabele','open_tab');
         start_pr:=1;
         exec('sprz_zbiorcz','dok_fks_aut_dok',1,null,'LP');
         VAR_DEL.delete('start_pr')
      || exec('sprz_zbiorcz','dok_fks_aut_dok',ust_fir,ust_odd,'LP')
      ?};
      _wy.UNIK_ID:=zap_par-1;
      params_exec('par_out','!fks_dks_dlrw',SSTALE.AO,zap_par-1,_mp,_wy);
      VAR_DEL.delete('iddekpar','zap_par');
      _mp.done()
   || FUN.info('Brak parametru wejściowego - okres obrachunkowy.'@); _mp.error()
   ?}
?};
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
'Zadekretuj zbiorczo paragony'@@


\par_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych czynności
::   WE: _a - ref okresu (OKRO_F)
::       _b - UNIK_ID
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_wy:=_args.out;
OKRO_F.cntx_psh(); OKRO_F.prefix();
{? OKRO_F.seek(_a)
|| DOK.cntx_psh(); ROK_F.cntx_psh();
   DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
   DOK.index('DOKZRODL'); DOK.prefix(_b+'1',);
   {? DOK.first()
   || _wy.DOK:=DOK.ref();
      {? DOK.size()>1
      || _wy.UNIK_ID:=_b; _wy.OKRO_F:=_a
      || _wy.UNIK_ID:=''; _wy.OKRO_F:=null
      ?};
      DOK.DOKZRODL:=(DOK.DOKZRODL-1)+'2';
      DOK.cntx_psh(); DOK.prefix(); DOK.put(); DOK.cntx_pop()
   || _wy.UNIK_ID:=''; _wy.DOK:=_wy.OKRO_F:=null
   ?};
   _mp.save(,_wy);
   {? _wy.UNIK_ID<>'' || _mp.loop_continue() ?};
   DOK.cntx_pop(); ROK_F.cntx_pop()
?};
OKRO_F.cntx_pop()


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła ustala PARSES dla czynności na podstawie rekordu tabeli OKRO_F,
::       wskazanie na rekord tabeli OKRO_F jest przekazane poprzez parametr OKRO_F czynności.
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_in:=params_get().in;
_out:=params_get().out;
{? var_pres('OKRO_F',_in) & var_pres('OKRO_F',_in)=type_of(null()) & _in.OKRO_F<>null()
|| __PARSES.setVal('OkresRok',_in.OKRO_F);
   _result:=1
:: jeżeli czynność startowa to parametry z bieżących ustawień pulpitu
|| _result:=1
?};
_result


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma niezbędnych uprawnień do danych czynności
::       1 - użytkownik ma uprawnienia do dancyh czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh();
USERS.clear();
{? USERS.seek(_user)
|| {? exec('usr_fjks_any','b_perm',USERS.ref())
   || _result:=1
   ?}
?};
USERS.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 9f86b08675fe3fe591e233cef7ff02b9cb04ef367496d2b844f90b3bead1654c109c6ba91b8a7f99c60e8d325a19914951b3ed5b8c65c306fb737082f4b5079335f933335f8f524192ed075e57768e1e3cf424eadd1c95202404e6e9e5dc911ae9690377d467b11cbdc8cf1d5cc89d6656401991ac426232a65340623cd536c8

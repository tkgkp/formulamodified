:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_grp_drpn.fml
:: Utworzony: 20.05.2021
:: Autor: DG
::======================================================================================================================
:: Zawartość: Obsługa czynności PRC_GRP_DRPN - Gr. przegl. godzin pracy nocnej.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Gr. rej. godzin pracy nocnej - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

:: wybór pracowników
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PKD';
_args.F_ZATR:={? +P_FILTER.F_ZATR().KOD || P_FILTER.F_ZATR().KOD || '*T' ?};
_args.VIEW:='';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_ret:=exec('wybierz','pracownik',_args);
: wybrano kogoś?
{? _ret.P.size()=0
|| return()
?};

exec('KOMM','#object');
KOMM.init(,,'Błędy przy wprowadzaniu indywidualnej pracy w porze nocnej'@);

_gdpn:=obj_new('DOD','DDO','GOD','GDO');

GDPN.cntx_psh();
GDPN.prefix();
GDPN.win_edit('RED');
GDPN.blank();

{? GDPN.edit("exec('gdpn_ae','prc','T')")
|| _gdpn.DOD:=GDPN.DOD;
   _gdpn.DDO:=GDPN.DDO;
   _gdpn.GOD:=GDPN.GOD;
   _gdpn.GDO:=GDPN.GDO;

   _loop:=_ret.P.first();
   P.cntx_psh();
   P.prefix();
   echo('Trwa wprowadzanie zapisów dla pory nocnej...'@);
   {!
   |? _loop
   |! {? P.seek(_ret.P.SQL)
      || _add:=1;
         _gdpnREF:=0;
         _sect:=' %1 %2 %3:'[P.OSOBA().NAZWISKO,P.OSOBA().PIERWSZE,'[nr teczki - %1]'@[form(P.T)]];
         GDPN.cntx_psh();
         GDPN.index('GDPNPD');

::       Walidacja rekordu
         _do:=_gdpn.DDO;
         {? exec('gdpn_over','prc',_gdpn.DOD,_gdpn.DDO,P.ref())<>''
         || GDPN.prefix(P.ref);
            {? GDPN.find_le(_gdpn.DOD+1)
            || {? GDPN.DOD<=_gdpn.DDO | _gdpn.DDO=date(0,0,0)
               || _do:=GDPN.DOD-1;
                  _res:='Data zakończenia została zmieniona na %1, ponieważ w podanym przedziale istnieje już '
                        'inny zapis.'@[$_do];
                  KOMM.sect_beg(_sect,'xwin16.png:100');
                  KOMM.add(gsub(_res,'\n',' '),4);
                  KOMM.sect_end()
               ?}
            ?}
         ?};

         GDPN.prefix(P.ref,_gdpn.DOD);
         {? GDPN.first()
         || _add:=0;
            _gdpnREF:=#GDPN.ref()
         ?};
         GDPN.prefix();
         {? _gdpnREF<>0
         || {? GDPN.seek(_gdpnREF)
            || GDPN.DDO:=_do;
               GDPN.GOD:=_gdpn.GOD;
               GDPN.GDO:=_gdpn.GDO;
               {? GDPN.put()
               || _res:='Zapis dla dnia %1 już istnieje, dlatego został zaktualizowany o nowe parametry.'@[$GDPN.DOD];
                  {? KOMM.find_msg(_sect) || KOMM.set_root(_sect) || KOMM.sect_beg(_sect,'xwin16.png:100') ?};
                  KOMM.add(gsub(_res,'\n',' '),4);
                  KOMM.sect_end()
               ?}
            ?}
         ?};
         GDPN.cntx_pop();
         {? _add
         || GDPN.P:=P.ref();
            GDPN.DOD:=_gdpn.DOD;
            GDPN.DDO:=_do;
            GDPN.GOD:=_gdpn.GOD;
            GDPN.GDO:=_gdpn.GDO;
            GDPN.add()
         ?}
      ?};
      _loop:=_ret.P.next()
   !};
   echo('');
   P.cntx_pop();

   {? ~KOMM.empty()
   || KOMM.select()
   ?};

   FUN.info('Operacja zakończona pomyślnie.'@)
|| FUN.info('Operacja anulowana'@)
?};

GDPN.cntx_pop();

~~

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:01 b2e2eacaef3653f682e8a5f44c4314f67495938470731e82982b7a5b0a142f235639f4e80751af67acad2ad0fb442fcc077b63686380bf6c33bd816ae3ec4fc0484df1eca1948855f116f5a6114d30c648f3c3c09f41a31fb1938b3cd3abfe156b60b9f405b611b4470afa187d9a1bf9b26b663b96e0b4d4c7736a7cd8908bde

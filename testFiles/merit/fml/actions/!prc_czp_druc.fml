:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_druc.fml
:: Utworzony: 06.07.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_DRUC - Ewidencja ruch. - prac.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Ewidencja ruchomego czasu pracy dla współpracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();
_result:='';

_uidref:=exec('ref2uid','#table',_in.P);
{? _uidref=''
|| _result:=exec('error','!prc_czp_druc')
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!prc_czp_druc',_in.P);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Redakcja marginesów pracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Dodaj informację o ruchomym czasie pracy: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Dodaj informację o ruchomym czasie pracy: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Dodaj informację o ruchomym czasie pracy: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Dodaj informację o ruchomym czasie pracy'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Komunikat o błędzie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Dodanie ruchomego czasu pracy nie jest możliwe.'@,'Nie znaleziono pracownika.'@]


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a [REFERENCE] - Wskazanie pracownika.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a,,1)
|| OSOBA.cntx_psh();
   P.OSOBA();
   A_RWN.cntx_psh();
   A_RWN.win_sel(exec('wnd','!prc_czp_druc'));
   A_RWN.win_edit('RED');
   A_RWN.index('A_RWN');
   A_RWN.prefix(_a);
   _ret:=A_RWN.select();
   A_RWN.cntx_pop();
   OSOBA.cntx_pop()
|| _ret:=exec('error','!prc_czp_druc')
?};
P.cntx_pop();
_ret


\a_rwn_bfz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Obsługa akcji "Zakończ".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=A_RWN
|| sel_exit()
|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Przygotowanie okna wertowania ruchomego czasu pracy na wniosek pracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized';

_wnd:=A_RWN.grp_make('Ruchomy czas pracy',"mb_exec('GRP_EDISP_P_INFO_Z')",'a_rwn_wer',,,,,_mode);
A_RWN.grp_edit(_wnd,P,'INFO_Z',,,,,,_mode);
A_RWN.grp_splt(_wnd,,'horizontal','a_rwn');
A_RWN.grp_sel(_wnd,,'WER',,,,,,,,,,_mode,,1);

A_RWN.win_fml('WER',,'OPIS',,'ICON_BEFORE',"exec('x_zalacz_icon','zalacz')");

_wnd


\a_rwn_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.42]
:: OPIS: Obsługa akcji "Załączniki - przed" w oknach tabeli A_RWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','P','A_RWN',exec('chk_role','#b__box',OPERATOR.USER,'PRC_CZP_DRUC'))


\a_rwn_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.42]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli A_RWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',A_RWN.uidref())

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 8ad5030eacc064cd5b99b233914a6c6fad55a41bbdac41f86bd2af4fa94c57c71ab8b7dfbbef70bfe404576ab490d5d94902bb8c19cc63dba0e9506918d38b3c5cfc5c644e4c7dc0a62f5b65411762c6cd57c84693c3d28867d9c97b23aff63b19fd55ff71b696586a629af24b89f33a33bd0f3475cec61b3042584ad7f9ecf2

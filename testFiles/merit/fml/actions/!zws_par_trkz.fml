:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_trkz.fml
:: Utworzony: 31.01.2019
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_TRKZ - Kategorie zasobów remontowych
::            Obsługa tabeli REM_KAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Formuła główna czynności parametryzacyjnej (nieprocesowej) - kategorie zasobów remontowych
::----------------------------------------------------------------------------------------------------------------------
REM_KAT.cntx_psh();
_sel:=exec('grp_make','!zws_par_trkz');
REM_KAT.win_sel(_sel);
_prev:=POMOC.RODZ_WP;
{? exec('lic','#b_domain','POR')
|| REM_KAT.win_edit('RED');
   typobi:=2;
   POMOC.RODZ_WP:=3
?};
REM_KAT.index('SYMBOL');
REM_KAT.prefix();
REM_KAT.select();
REM_KAT.cntx_pop();
POMOC.RODZ_WP:=_prev;
~~


\action_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Akcja 'Usuń' w oknie REM_KAT.WER
::----------------------------------------------------------------------------------------------------------------------
{? REM_KAT.count()>0
|| FUN.info('Usunięcie nie jest możliwe — wskazana kategoria zasobu remontowego jest już użyta w systemie.'@)
|| {? FUN.ask('Czy usunąć bieżący wiersz?'@) || REM_KAT.del() ?}
?};
~~


\chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Akcja 'Rekord po' w oknie REM_KAT.WER
::----------------------------------------------------------------------------------------------------------------------
exec('chk_rem_kat','zasoby',-menu_txt()='popraw')


\rem_kato_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Akcja 'Dołącz' w oknie wertowania tabeli REM_KATO
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_can_continue:=1;

REM_KATO.win_edit('RED');
REM_KATO.blank();
REM_KATO.REM_KAT:=REM_KAT.ref();
{? REM_KATO.edit("exec('rem_kato_valid','remonty_zgloszenia',0)")
|| _can_continue:=REM_KATO.add()
?};
{? _can_continue>0
|| _result:=1
?};
_result


\rem_kato_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Akcja 'Popraw' w oknie wertowania tabeli REM_KATO
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_can_continue:=1;
{? REM_KATO.r_lock(1,1,1)
||
   REM_KATO.win_edit('RED');
   REM_KATO.cntx_psh();
   {? REM_KATO.edit("exec('rem_kato_valid','remonty_zgloszenia',1)")
   || _can_continue:=REM_KATO.put()
   ?};
   REM_KATO.cntx_pop();
   REM_KATO.get();
   REM_KATO.r_unlock()
?};
{? _can_continue>0
|| _result:=1
?};
_result


\rem_kato_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Akcja 'Usuń' w oknie wertowania tabeli REM_KATO
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_user:=exec('record','#to_string',REM_KATO.USERS);
_can_continue:=FUN.ask('Czy usunąć opiekuna: %1 kategorii zasobów: %2'@[_user,REM_KAT.SYMBOL]);
{? _can_continue>0
|| _can_continue:=REM_KATO.del(,1)
?};
{? _can_continue>0
|| _result:=1
?};
_result



\grp_make
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Tworzy okienko grupowe z kategoriami i opiekunami
::   WY: STRING - akronim okienka
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_tab:=REM_KAT;
_grp:=_tab.grp_make('Kategorie zasobów'@,,'#rem_kat_grp',1,1,,,'normal');

_tab.grp_sel(_grp,REM_KAT,'WER',,"exec('rem_kat_after_rfr','!zws_par_trkz')",,,15,,,,,'maximized_with_title');

_tab.grp_splt(_grp,,'horizontal','bottom');

_before:="
   {? grp_empty(REM_KAT,'WER')
   || return('#disable')
   ?}
";
_tab.grp_sel(_grp,REM_KATO,'WER',,,,,15,_before,,,,'maximized_with_title');
_grp


\rem_kat_after_rfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Po odświeżeniu w tabeli REM_KAT
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
REM_KATO.index('REM_KAT');
REM_KATO.prefix(REM_KAT.ref());
grp_disp(REM_KATO,'WER');
~~


\rem_kat_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Wyzwalacz "Dołącz - po" dla tabeli REM_KAT.
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1 | ~exec('lic','#b_domain','POR') || return() ?};
{? REM_KAT.TYP<>null()
|| ETYPY.cntx_psh();
   _customField:=exec('customField','portal_slowniki');
   _porslo:=exec('porslo_add','portal_slowniki',_customField,'',REM_KAT.TYP().ID_WP,
                 'Określa zasoby remontowe, które użytkownik portalu może wybrać we wniosku administracyjnym',,'T','N');
   {? _porslo
   || exec('uzup_rem_kat','portal_slowniki',_porslo,REM_KAT.ref())
   ?};
   ETYPY.cntx_pop()
?};
~~


\rem_kat_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Wyzwalacz "Popraw - po" dla tabeli REM_KAT.
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1 | ~exec('lic','#b_domain','POR') || return() ?};

_typ_old:=bfld('TYP');
{? _typ_old<>REM_KAT.TYP
|| _customField:=exec('customField','portal_slowniki');
:: Usuwam słownik powiązany z poprzednim typem
   {? _typ_old<>null()
   || _id_wp_old:=exec('FindAndGet','#table',ETYPY,_typ_old,,"ETYPY.ID_WP",'');
      ETYPY.cntx_psh();
      PORSLO.cntx_psh();
      PORSLO.index('FIELD');
      PORSLO.prefix(null(),_customField,'',_id_wp_old,);
      {? PORSLO.first()
      || exec('porslo_del','portal_slowniki',PORSLO.ref())
      ?};
      ETYPY.cntx_pop();
      PORSLO.cntx_pop()
   ?};
:: Dodaje nowy słownik

   {? REM_KAT.TYP<>null()
   || ETYPY.cntx_psh();
      _porslo:=exec('porslo_add','portal_slowniki',_customField,'',REM_KAT.TYP().ID_WP,
                    'Określa zasoby remontowe, które użytkownik portalu może wybrać we wniosku administracyjnym',,
                    'T','N');
      {? _porslo
      || exec('uzup_rem_kat','portal_slowniki',_porslo,REM_KAT.ref())
      ?};
      ETYPY.cntx_pop()
   ?}
?};
~~


\rem_kat_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli REM_KAT.
::----------------------------------------------------------------------------------------------------------------------
{? exec('lic','#b_domain','POR')
|| _res:=1;
   {? REM_KAT.TYP<>null()
   || ETYPY.cntx_psh();
      PORSLO.cntx_psh();
      PORSLO.index('FIELD');
      PORSLO.prefix(null(),exec('customField','portal_slowniki'),'',REM_KAT.TYP().ID_WP,);
::    Usuwam słownik przypisany do usuwanej kategorii zasobu remontowego
      {? PORSLO.first()
      || _res:=exec('porslo_del','portal_slowniki',PORSLO.ref())
      ?};
      ETYPY.cntx_pop();
      PORSLO.cntx_pop()
   ?};
   _res
|| 1
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 1464d4244d542ea545abc122167ed873cfb1bfc1e089009660616b1b8d684ede2a99661cf68440285398750f0734616789b1b356956bd2106d2b207fa455ee3d62b02189a40d49e550ea2c220463b9118978ae7150f977b00f1fe42d06af799a695663890fc7f4034e1e2a8269ef1a78ac5092ef403604d4ceb05088ae73537f

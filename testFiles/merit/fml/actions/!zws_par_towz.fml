:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_towz.fml
:: Utworzony: 11.04.2016
:: Autor: MLAK
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_TOWZ - Widoki planu operacyjnego
::=====================================================================================================================+


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [17.00]
:: OPIS: Formuła główna czynności parametryzacyjnej (nieprocesowej) -Widoku planu operacyjnego
::  TAG: <CZYNNOŚĆ><LISTA>
::----------------------------------------------------------------------------------------------------------------------
PL_WZOR.cntx_psh();
PL_WZOR.index('LP');
PL_WZOR.clear();
_grp:=PL_WZOR.grp_make('Widoki planu operacyjnego'@,,'#plwzorgrp');
PL_WZOR.grp_sel(_grp,PL_WZOR,'WER',,"params_set(params_get());exec('after_rfr_wzor','!zws_par_towz')",,,20,,,,,'maximized_with_title');
PL_WZOR.grp_splt(_grp,,'vertical','prawy');

_before:="
   _result:='';
   {? grp_empty(PL_WZOR,'WER')=1
   || _result:='#disable'
   ?};
   _result
";

PL_WZOR.grp_sel(_grp,PL_WZORU,'WER',,"",,,,_before,,,,'maximized_with_title');
PL_WZOR.win_sel(_grp);

:: Obsługa dnd
PL_WZOR.dnd_sel('WER',,'records.PL_WZOR',"exec('dnd_renum','po_param','LP','LP')");

PL_WZOR.select();
PL_WZOR.cntx_pop();
~~


\after_rfr_wzor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Formuła po odświeżeniu w PL_WZOR
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
PL_WZORU.index('VIEW');
PL_WZORU.prefix(PL_WZOR.ref());
PL_WZORU.first();
grp_disp(PL_WZORU,'WER',1);
PL_WZORU.hdr_sel('');
PL_WZORU.hdr_sel(': %1'[PL_WZOR.NAZWA]);
~~


\ae_ad_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2009]
:: OPIS: Wywolywana po dolaczeniu okresu planowania.
::  OLD: \ae_ad_wz/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.LEVEL:=1;
PL_OPIS.cntx_psh();
PL_OPIS.index('PL_OPIS');
PL_OPIS.win_sel('WER');
PL_OPIS.prefix(PL_WZOR.ref);
PL_OPIS.select();
PL_OPIS.cntx_pop();
~~


\pl_wzor_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2009]
:: OPIS: Usuniecie widoku  razem z pozycjami
::   WE: [_a] - INTEGER - 0/[1] - czy wyświetlać dialogi
::  OLD: \del_plwz/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_dialog:=1;
{? var_pres('_a')=type_of(0)
|| _dialog:=_a
?};

_result:=0;
_can_continue:=1;

{? _dialog>0
|| _can_continue:=FUN.ask('Czy usunąć widok razem z zasobami i uprawnieniami?'@)
?};

{? _can_continue>0
|| PL_OPIS.cntx_psh();
   PL_OPIS.index('PL_OPIS');
   PL_OPIS.prefix(PL_WZOR.ref());
   {? PL_OPIS.first()
   || {!
      |? _can_continue:=PL_OPIS.del(,1);
         PL_OPIS.first() & _can_continue>0
      !}
   ?};
   PL_OPIS.cntx_pop()
?};
{? _can_continue>0
|| KAL_UD.cntx_psh();
   KAL_UD.index('PL_WZOR');
   KAL_UD.prefix(PL_WZOR.ref());
   {? KAL_UD.first()
   || {!
      |? KAL_UD.cntx_psh();
         KAL_UD.prefix();
         KAL_UD.PL_WZOR:=null();
         _can_continue:=KAL_UD.put();
         KAL_UD.cntx_pop();
         KAL_UD.first() & _can_continue>0
      !}
   ?};
   KAL_UD.cntx_pop()
?};
{? _can_continue>0
|| PL_WZORU.cntx_psh();
   PL_WZORU.index('PL_WZOR');
   PL_WZORU.prefix(PL_WZOR.ref());
   {? PL_WZORU.first()
   || {!
      |? _can_continue:=exec('pl_wzoru_del','po_param',0);
         PL_WZORU.first() & _can_continue>0
      !}
   ?};
   PL_WZORU.cntx_pop()
?};
{? _can_continue>0
|| _can_continue:=PL_WZOR.del(,1)
?};

{? _can_continue>0 & PL_WZOR.size()>0
|| exec('reNrDel','#table','PL_WZOR','LP')
?};

{? _can_continue>0
|| _result:=1
?};
_result


\czyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2009]
:: OPIS: sprawdza czy widok moze byc redagowany.
::  WE: _a - PL_WZOR.ref()
::  WY: 0/1
::  OLD: \czyr/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_czyr:=obj_new(2);
_czyr[1]:=1;
_czyr[2]:=0;
{? _a<>null()
|| KAL_UD.cntx_psh();
   _ind:=KAL_UD.ndx_tmp(,1,'PL_WZOR',,);
   KAL_UD.index(_ind);
   KAL_UD.prefix(_a);
   {? KAL_UD.first()
   || _czyr[1]:=2
   ?};
   KAL_UD.ndx_drop(_ind);
   KAL_UD.cntx_pop()
?};
_czyr


\plwzor_cp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [12.10]
:: OPIS: Kopiowanie widoku planowania
::  OLD: \plwzor_cp/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Skopiować widok: %1?'@[PL_WZOR.NAZWA])
||
   _ref_new:=null;
   _ref_old:=PL_WZOR.ref();
   PL_WZOR.cntx_psh();
   PL_WZOR.NAZWA:='kopia-'+PL_WZOR.NAZWA;

   PL_WZOR.cntx_psh();
   PL_WZOR.prefix();
   _size:=PL_WZOR.size();
   PL_WZOR.cntx_pop();
   PL_WZOR.LP:=_size+1;

   {? PL_WZOR.add(1)
   || _ref_new:=PL_WZOR.ref();
      PL_OPIS.cntx_psh();
      PL_OPIS.index('PL_OPIS');
      PL_OPIS.prefix(_ref_old);
      {? PL_OPIS.first()
      || {!
         |?
            PL_OPIS.cntx_psh();
            PL_OPIS.clear();
            PL_OPIS.WZORZEC:=_ref_new;
            PL_OPIS.add();
            PL_OPIS.cntx_pop();
            PL_OPIS.next()
         !}
      ?};
      PL_OPIS.cntx_pop()
   || FUN.emsg('Widok o nazwie: %1 już istnieje.'@[PL_WZOR.NAZWA])
   ?};
   PL_WZOR.cntx_pop();

   {? _ref_new<>null()
   || PL_WZOR.seek(_ref_new)
   ?}
?};
~~


\opisplwz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2009]
:: OPIS: Selekcja tabeli z opisem widoku.
::  OLD: \opisplwz/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.LEVEL:=1;
PL_OPIS.cntx_psh();
PL_OPIS.index('PL_OPIS');
PL_OPIS.prefix(PL_WZOR.ref());
PL_OPIS.win_sel('WER');

:: Obsługa dnd
PL_OPIS.dnd_sel('WER',,'records.PL_OPIS',"exec('dnd_renum','po_param','NUMER','PL_OPIS')");

PL_OPIS.select();
PL_OPIS.cntx_pop();
~~


\pl_opis_delete_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Akcja w oknie - usunięcie rekordu
::   WE: [_a] - INTEGER - 0/[1] - czy wyświetlać dialogi
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PRIVATE><AKCJA><DEL>
::----------------------------------------------------------------------------------------------------------------------
_dialog:=1;
{? var_pres('_a')=type_of(0)
|| _dialog:=_a
?};

_result:=0;
_can_continue:=1;

_tab:=cur_tab(1,1);

_group:=0;
{? _tab.sel_size()>0
|| _group:=1;
   _dialog:=0
?};

:: Sprawdzam czy można usunąć
{? _group>0
|| _can_continue:=exec('pl_opis_can_delete','!zws_par_towz',,2)
|| _can_continue:=exec('pl_opis_can_delete','!zws_par_towz',,1)
?};


{? _dialog>0 & _can_continue>0
|| _can_continue:=FUN.ask('Usunąć zasób: %1 z widoku?'@[PL_OPIS.PL_RES().SYM])
?};
{? _can_continue>0
|| _numer:=PL_OPIS.NUMER;
   {? PL_OPIS.del(,1)
   || {? PL_OPIS.size()>0 & _numer<PL_OPIS.NUMER
      || exec('reNrDel','#table','PL_OPIS','NUMER')
      ?}
   ?}
?};
_result


\pl_opis_delete_act_gr1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Akcja usuń grupa przed w oknie wertowania tabeli PL_OPIS
::   WY: 0 - porazka
::       1 - sukces
::  TAG: <PRIVATE><AKCJA><GRUPA><DEL>
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_tab:=cur_tab(1,1);
{? FUN.ask('Zaznaczono: %1 zasobów. Czy usunąć je wszystkie?'@[$_tab.sel_size()])
|| _result:=1;
   sel_nchk();
   KOMM.init(200,,'Usuwanie zasobów z widoku planu'@,'')
?};
_result


\pl_opis_delete_act_gr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Akcja usuń grupa po w oknie wertowania tabeli PL_OPIS
::  TAG: <PRIVATE><AKCJA><GRUPA><DEL>
::----------------------------------------------------------------------------------------------------------------------
KOMM.select(,,,,,0);
~~


\pl_opis_can_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Sprawdza możliwość usunięcia rekordu PL_OPIS
::   WE: [_a] - PL_OPIS.ref, jeśli nie podany to aktualny rekord
::       [_b] - INTEGER - czy wyswietlac komunikaty:  0  - nie
::                                                   [1] - wyskakujący komunikat
::                                                    2  - w KOMM
::   WY: 0 - nie można usunąć rekordu
::       1 - można usuwać
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();

_display:=1;
{? var_pres('_b')=type_of(0)
|| _display:=_b
?};

_result:=1;
_can_continue:=1;


PL_OPIS.cntx_psh();
{? var_pres('_a')=type_of(PL_OPIS.ref())
|| _ref:=_a;
   _can_continue:=0;
   PL_OPIS.clear();
   {? PL_OPIS.seek(_ref)
   || _can_continue:=1
   ?}
?};

{? _can_continue>0
|| ~~
?};

PL_OPIS.cntx_pop();
_result


\plopis_add_res
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2011]
:: OPIS: Akcja. Dodaje zasoby do widoku planowania
::  OLD: \plopis_add_res/pl_okr3.fml
::----------------------------------------------------------------------------------------------------------------------
::{? exec('check_zmiana','pl_okr3',PL_WZOR.ref())=0
::|| FUN.emsg('Akcja możliwa tylko dla wzorców planowania, które mają zdefiniowany poziom \'Zmiana\'');
::   return()
::?};
PL_RES.cntx_psh();
PL_RES.index('SYM');
PL_RES.win_sel('ADD');
PL_RES.win_fml('ADD',,'TYP',,'ICON_BEFORE', exec('plres_icon_bef','!zws_par_towz'));
exec('__KAL','object');
PL_RES.select();
PL_RES.cntx_pop();
~~


\plres_icon_bef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2011]
:: OPIS: Zwraca formule ICON_BEFORE dla tabeli PL_RES
::   WY: Formula typu ICON_BEFORE
::  OLD: \plres_icon_bef/pl_okr3.fml
::----------------------------------------------------------------------------------------------------------------------
"'xwin16.png:'+$exec('ikona_res','po_plan',PL_RES.TYP)"


\blplwzor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2010]
:: OPIS: Formula na blank pola PL_OPIS.WZORZEC
::  OLD: \blplwzor/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
PL_WZOR.ref()


\bl_plpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2010]
:: OPIS: Zwraca VAR.LEVEL
::   WY:
::  OLD: \bl_plpoz/varget.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.LEVEL


\chk_pl_wzor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: PL_WZOR - po rekord w oknie WER
::----------------------------------------------------------------------------------------------------------------------
exec('chk_pl_wzor','po_param',-menu_txt()='popraw')


\pl_wzor_fields_blank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Formuły na wartość początkową dla pól w tabeli PL_WZOR
::   WY:
::  TAG: <MBUILDER><BL><TAB:PL_WZOR>
::----------------------------------------------------------------------------------------------------------------------
_result:=~~;
_fld:=cur_afld();
{? _fld='AKT'
|| _result:='T'
|? _fld='LP'
|| PL_WZOR.cntx_psh();
   PL_WZOR.prefix();
   _size:=0;
   {? PL_WZOR.last()
   || _size:=PL_WZOR.LP
   ?};
   PL_WZOR.cntx_pop();
   _result:=_size+1
?};
_result


\pl_wzor_fields_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Formuły przed wyświetleniem dla pól w tabeli PL_WZOR
::   WY:
::  TAG: <MBUILDER><BD><TAB:PL_WZOR>
::----------------------------------------------------------------------------------------------------------------------
''


\pl_wzor_fields_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Formuły przed redkacją dla pól w tabeli PL_WZOR
::   WY:
::  TAG: <MBUILDER><BE><TAB:PL_WZOR>
::----------------------------------------------------------------------------------------------------------------------
1


\pl_wzor_fields_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Formuły po redakcji dla pól w tabeli PL_WZOR
::   WY:
::  TAG: <MBUILDER><AE><TAB:PL_WZOR>
::----------------------------------------------------------------------------------------------------------------------
1


\pl_wzor_fields_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Formuły obsługi na F3 dla pól w tabeli PL_WZOR
::   WY:
::  TAG: <MBUILDER><F3><TAB:PL_WZOR>
::----------------------------------------------------------------------------------------------------------------------
~~


\pl_wzor_fields_fd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Formuły na format wyświetlania dla pól w tabeli PL_WZOR
::   WY:
::  TAG: <MBUILDER><FD><TAB:PL_WZOR>
::----------------------------------------------------------------------------------------------------------------------
''


\pl_wzor_fields_fe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Formuły na format redagowania dla pól w tabeli PL_WZOR
::   WY:
::  TAG: <MBUILDER><FE><TAB:PL_WZOR>
::----------------------------------------------------------------------------------------------------------------------
''

:Sign Version 2.0 jowisz:1045 2020/04/03 17:04:56 859cd7c2d68f5452392860cc46eb47c67324a81d018d6607b610e31c3891a573b801e65ddc93c1a62562af915eb1436d5ce0be839767d76c8aa3d87741393d68bfb173633b27027211ef4b3f6f85b05b21e9f89ee3adb4b48c42c7c003bfbb3edec78993e64b8e49ba5e63c9552c3911429d919305bd83bb9a93160cb0484da9

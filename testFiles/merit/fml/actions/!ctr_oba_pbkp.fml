:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_oba_pbkp.fml
:: Utworzony: 17.06.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_OBA_PBKP - Kontrola budżetu dla wniosków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Prezentuje wykonanie budzetu
::   WE: [_a] - czy webterm? 1-tak [0]-nie
::  OLD: \pokaz_budzet/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POM.KN1:=UD_POM.KN2:=UD_POM.KN3:=UD_POM.KN4:=UD_POM.KN5:='';
{? var_press('_a')<=0 || _a:=0 ?};
skid_mbn_kon:='T'; modbust:=null;
{? _a=0
|| UD_POM.fld_fml('K_MODEL','BEFORE_EDIT',"
      modbust:=UD_POM.K_MODEL; 1
   ")
?};
_fml_be:="
   {? UD_POM.K_MODEL<>null
   || SKID_MBP.index('LP');
      SKID_MBP.prefix(UD_POM.K_MODEL,#(cur_afld()+1));
      SKID_MBP.first() & SKID_MBP.KONTROLA='T'
   || 0
   ?}
";
_fml_bv:="
   {? UD_POM.K_MODEL<>null
   || _lp:={? cur_afld()+3='SKL'
           || #((cur_afld()-4)+1)
           |? cur_afld()+4='OPIS'
           || #((cur_afld()-5)+1)
           || #(cur_afld()+1)
           ?};
      SKID_MBP.index('LP');
      SKID_MBP.prefix(UD_POM.K_MODEL,_lp);
      {? SKID_MBP.first() & SKID_MBP.KONTROLA='T'
      || ''
      || exec('findfnrd','color')
      ?}
   || fld('');
      exec('findfnrd','color')
   ?}
";
{? _a=0
|| {! _i:=1..5
   |! UD_POM.fld_fml('KN'+$_i,'BEFORE_DISPLAY',_fml_bv);
      UD_POM.fld_fml('K'+$_i,'BEFORE_DISPLAY',_fml_bv);
      UD_POM.fld_fml('K'+$_i+'_OPIS','BEFORE_DISPLAY',_fml_bv);
      UD_POM.fld_fml('K'+$_i,'BEFORE_EDIT',_fml_be)
   !}
|| UD_POM.K_MODEL:=null;
   {! _i:=1..5
   |! ($('UD_POM.K'+$_i+':=\'\''))();
      ($('UD_POM.K'+$_i+'_OPIS:=\'\''))()
   !}
?};
SKID_MBN.index('KONTROLA'); SKID_MBN.prefix('T');
{? SKID_MBN.first() & SKID_MBN.size()=1
|| UD_POM.K_MODEL:=SKID_MBN.ref();
   SKID_MBP.index('LP'); SKID_MBP.prefix(UD_POM.K_MODEL);
   {? SKID_MBP.first()
   || {!
      |? ($('UD_POM.KN'+$SKID_MBP.LP+':=_a'))(SKID_MBP.NAZ);
         _enable:=SKID_MBP.KONTROLA='T';
         {? app_info('web_sesid')<>''
         || UD_POM.web_efld_init('BUDZET2',,'enable='+$_enable,,'K'+$SKID_MBP.LP);
            UD_POM.web_efld_init('BUDZET2',,'enable='+$_enable,,'K'+$SKID_MBP.LP+'_OPIS');
            UD_POM.web_efld_init('BUDZET2',,'enable=1',,'KN'+$SKID_MBP.LP)
         ?};
         SKID_MBP.next()
      !};
      {! _i:=SKID_MBP.LP+1..5
      |! {? app_info('web_sesid')<>''
         || UD_POM.web_efld_init('BUDZET2',,'enable=0',,'K'+$_i);
            UD_POM.web_efld_init('BUDZET2',,'enable=0',,'K'+$_i+'_OPIS');
            UD_POM.web_efld_init('BUDZET2',,'enable=0',,'KN'+$_i)
         ?}
      !}
   ?}
?};
{? _a=1
|| web_params_set(exec('obj_ntab_set','#array',web_params_get(),
                         'KONTROLA','T'
                        ));
   UD_POM.web_edit('BUDZET2',,,,"
      {? _a='ANULUJ'
      || UD_POM.web_eclose()
      |? _a='OK'
      || {? UD_POM.K_MODEL=null
         || __CHK.err_empty('Model')
         || UD_POM.web_eclose();
            exec('show_budzet','obe_budz',1)
         ?}
      ?}
   ")
|| UD_POM.win_edit('BUDZET');
   {? SKID_MBN.f_active() || SKID_MBN.f_clear() ?};
   SKID_MBN.win_sel('WER');
   {? UD_POM.edit("chk_rec('K_MODEL')")
   || exec('show_budzet','obe_budz',0)
   ?};
   {! _i:=1..5
   |! UD_POM.fld_fml('KN'+$_i,'BEFORE_DISPLAY',"*");
      UD_POM.fld_fml('K'+$_i,'BEFORE_DISPLAY',"*");
      UD_POM.fld_fml('K'+$_i+'_SKL','BEFORE_DISPLAY',"*");
      UD_POM.fld_fml('K'+$_i,'BEFORE_EDIT',"*");
      UD_POM.fld_fml('K'+$_i,'F3',"*");
      UD_POM.fld_fml('K'+$_i,'AFTER_EDIT',"*")
   !};
   exec('tab_con_del','obe_budz','modbust')
?}


\ae_k_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redakcji UD_POM.K_MODEL
::----------------------------------------------------------------------------------------------------------------------
{? UD_POM.K_MODEL
|| {? var_press('modbust')<=0 | UD_POM.K_MODEL<>modbust & modbust<>null
   || {! _i:=1..5
      |! ($('UD_POM.KN'+$_i+':=_a'))('');
         ($('UD_POM.K'+$_i+':=_a'))('');
         ($('UD_POM.K'+$_i+'_SKL:=_a'))(null);
         ($('UD_POM.K'+$_i+'_OPIS:=_a'))('')
      !}
   ?};
   {? app_info('web_sesid')<>''
   || {! _i:=1..5
      |! exec('ae_k_wym','!ctr_oba_pbkp',_i,1)
      !}
   ?};
   SKID_MBP.index('LP'); SKID_MBP.prefix(UD_POM.K_MODEL);
   {? SKID_MBP.first()
   || {! _i:=1..5
      |! {? SKID_MBP.find_key(_i)
         || ($('UD_POM.KN'+$SKID_MBP.LP+':=_a'))(SKID_MBP.NAZ);
            _enable:=SKID_MBP.KONTROLA='T';
            {? app_info('web_sesid')<>''
            || UD_POM.web_efld_opt('BUDZET2',,'enable='+$_enable,,'K'+$_i);
               UD_POM.web_efld_opt('BUDZET2',,'enable='+$_enable,,'K'+$_i+'_OPIS');
               UD_POM.web_efld_opt('BUDZET2',,'enable=1',,'KN'+$_i)
            || UD_POM.efld_opt('BUDZET','enable='+$_enable,,'K'+$_i);
               UD_POM.efld_opt('BUDZET','enable='+$_enable,,'K'+$_i+'_OPIS');
               UD_POM.efld_opt('BUDZET','enable=1',,'KN'+$_i)
            ?}
         || ($('UD_POM.KN'+$_i+':=_a'))('');
            {? app_info('web_sesid')<>''
            || UD_POM.web_efld_opt('BUDZET2',,'enable=0',,'K'+$_i);
               UD_POM.web_efld_opt('BUDZET2',,'enable=0',,'K'+$_i+'_OPIS');
               UD_POM.web_efld_opt('BUDZET2',,'enable=0',,'KN'+$_i)
            || UD_POM.efld_opt('BUDZET','enable=0',,'K'+$_i);
               UD_POM.efld_opt('BUDZET','enable=0',,'K'+$_i+'_OPIS');
               UD_POM.efld_opt('BUDZET','enable=0',,'KN'+$_i)
            ?}
         ?}
      !}
   ?};
   {? app_info('web_sesid')='' || win_disp() ?};
   SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(UD_POM.K_MODEL);
   _jest:=SKID_MBP.first();
   SKID_MBP.cntx_pop();
   {? ~_jest
   || FUN.info('Model nie zawiera pozycji.'@); 0
   || 1
   ?}
|| {! _i:=1..5
   |! {? app_info('web_sesid')<>''
      || UD_POM.web_efld_opt('BUDZET2',,'enable=0',,'K'+$_i);
         UD_POM.web_efld_opt('BUDZET2',,'enable=0',,'K'+$_i+'_OPIS');
         UD_POM.web_efld_opt('BUDZET2',,'enable=0',,'KN'+$_i)
      || UD_POM.efld_opt('BUDZET','enable=0',,'K'+$_i);
         UD_POM.efld_opt('BUDZET','enable=0',,'K'+$_i+'_OPIS');
         UD_POM.efld_opt('BUDZET','enable=0',,'KN'+$_i)
      ?};
      ($('UD_POM.KN'+$_i+':=_a'))('');
      ($('UD_POM.K'+$_i+':=_a'))('');
      ($('UD_POM.K'+$_i+'_OPIS:=_a'))('');
      ($('UD_POM.K'+$_i+'_SKL:=_a'))(null)
   !};
   {? app_info('web_sesid')='' || win_disp() ?};
   1
?}


\f3_k_wym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Klawisz F3 dla wymiarów modelu kontroli budżetu
::   WE: _a - nr wymiaru
::----------------------------------------------------------------------------------------------------------------------
{? UD_POM.K_MODEL<>null
|| _lp:=_a;
   SKID_MBP.index('LP');
   SKID_MBP.prefix(UD_POM.K_MODEL,_lp);
   {? SKID_MBP.first() & SKID_MBP.KONTROLA='T'
   || wart_wym:=($('UD_POM.K'+$_lp))();
      exec('ud_def_symbol_f3','schemat',SKID_MBP.UD_SCH().UD_TYP().SYMBOL,UD_SCH.SYMBOL)
   ?}
?}


\ae_k_wym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redkacji pól wymiarów modelu kontroli budżetu
::   WE:  _a  - nr wymiaru
::       [_b] - tylko sprawdzenie
::----------------------------------------------------------------------------------------------------------------------
{? UD_POM.K_MODEL<>null
|| _lps:=$_a;
   _fld:=($('UD_POM.K'+_lps))();
   {? _fld=''
   || ($('UD_POM.K'+_lps+'_SKL:=_a'))(null);
      ($('UD_POM.K'+_lps+'_OPIS:=_a'))('');
      1
   || _lp:=_a;
      SKID_MBP.index('LP');
      SKID_MBP.prefix(UD_POM.K_MODEL,_lp);
      {? SKID_MBP.first() & SKID_MBP.KONTROLA='T'
      || UD_DEF.index('SCHSYM');
         UD_DEF.prefix(SKID_MBP.UD_SCH,_fld);
         {? UD_DEF.first()
         || ($('UD_POM.K'+_lps+':=_a'))(UD_DEF.SYMBOL);
            ($('UD_POM.K'+_lps+'_SKL:=_a'))(UD_DEF.UD_SKL);
            ($('UD_POM.K'+_lps+'_OPIS:=_a'))(UD_DEF.UD_SKL().OPIS);
            1
         || {? var_press('_b')<=0 | _b=0
            || FUN.info('Nie znaleziono elementu o symbolu: %1.'@[_fld]);
               0
            || ($('UD_POM.K'+_lps+':=_a'))('');
               ($('UD_POM.K'+_lps+'_SKL:=_a'))(null);
               ($('UD_POM.K'+_lps+'_OPIS:=_a'))('');
               1
            ?}
         ?}
      ?}
   ?}
?}


\bu_k_budp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed obsluga okna pozycji kontroli budżetu
::----------------------------------------------------------------------------------------------------------------------
K_BUDN.prefix();
_ref:=web_win_ref('k_budn');
K_BUDP.index('UNIK');
{? type_of(_ref)<>type_of(~~) & _ref & K_BUDN.seek(_ref,)
|| K_BUDP.prefix(K_BUDN.ref())
|| K_BUDP.prefix(null)
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:11 40834bef72c541cde69c9754cf03f6493aa724e8904fdf1f8b73abfc7a68eeb07b1ca30cb06254f8c5c57308f47c95e515ba65f502a99eb403010e8beb399b4816319809dfd6d7aed9748ae5df1609b2dea444ac5ac6bd5cf50941dcc29f2beef839ad4c141d180d91db2065b5e6426f47c46d980c4d3c8c8f71828d70cf61e5

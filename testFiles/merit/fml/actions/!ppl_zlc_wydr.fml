:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zlc_wydr.fml
:: Utworzony: 09.06.2016
:: Autor: KOD
::======================================================================================================================
:: Zawartość: Formuły czynności PPL_ZLC_WYDR - Wydruk zestawień zleceniobiorców
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KOD [17.00]
:: OPIS: Wydruk zestawień zleceniobiorców - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
_par:=params_get();
{? var_pres('_par')>0 & var_pres('TYP',_par)=type_of('')
:: Wywołanie z obszaru (lub z pulpitu, ale już ustalonego typu zestawień).
|| _typ:=_par.TYP;
   {? _typ='rachunkiUmowy'
   || exec('rep_exec','#b_report','PPL_ZLC_WYDR','ppl_zlep*',,1)
   |? _typ='ListyWyplat'
   || O.cntx_psh();
      _lista:=exec('select','lista_plac','PPL','Z',,,'R');
      {?  _lista.OK
      || FUNKCJE.OTWOLIST();
         exec('dekl','lista_plac');
         exec('rep_exec','#b_report','PPL_ZLC_WYDR','ppl_uz_ul*',,1)
      ?};
      O.cntx_pop();
      FUNKCJE.OTWOLIST()
   |? _typ='ZestawieniaWyplat'
   || exec('rep_exec','#b_report','PPL_ZLC_WYDR','ppl_uz_uw*',,1)
   |? _typ='PozostaleWydruki'
   || exec('rep_exec','#b_report','PPL_ZLC_WYDR','ppl_uz_up*',,1)
   || return()
   ?}

:: Wywołanie z pulpitu.
|| {? ~exec('dostepny_u','f_zatr',,,'Z',1)
   || return()
   |? __PARSES.getVal('F_ZATR').KOD<>'Z'
   || _args:=__PARSES.args('F_ZATR');
      _args.KOD:='Z';
      __PARSES.setVal('F_ZATR',_args)
   ?};
   exec('okno_pulp','!ppl_zlc_wydr')
?}


\okno_pulp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KOD [17.00]
:: OPIS: Wydruk zestawień zleceniobiorców - formuła wybranych grup wydruków na przycisk. Uruchomienie z pulpitu.
::----------------------------------------------------------------------------------------------------------------------
_wydr:=
   (exec('chk_role','#b__box',OPERATOR.USER,'PPL_ZLC_PRAC') |
    exec('chk_role','#b__box',OPERATOR.USER,'PPL_ZLC_RRAC')) &
   (exec('chk_role','#b__box',OPERATOR.USER,'PPL_ZLC_PUZC') |
    exec('chk_role','#b__box',OPERATOR.USER,'PPL_ZLC_RUZC'));

:: utwórz zestaw formuł
_set:=exec('fun_set','object');

P.cntx_psh();
OSOBA.cntx_psh();
exec('xxx_grp_act','personel',_set,'PPL_ZLC_WYDR','Rachunki do umowy'@,,,
   "params_set('TYP','rachunkiUmowy'); exec('main','!ppl_zlc_wydr'); ''",_wydr);
exec('xxx_grp_act','personel',_set,'PPL_ZLC_WYDR','Listy wypłat'@,,,
   "params_set('TYP','ListyWyplat'); exec('main','!ppl_zlc_wydr'); ''");
exec('xxx_grp_act','personel',_set,'PPL_ZLC_WYDR','Zestawienia wypłat'@,,,
   "params_set('TYP','ZestawieniaWyplat'); exec('main','!ppl_zlc_wydr'); ''");
exec('xxx_grp_act','personel',_set,'PPL_ZLC_WYDR','Pozostałe wydruki'@,,,
   "params_set('TYP','PozostaleWydruki'); exec('main','!ppl_zlc_wydr'); ''");

_set.run('Zestawienia i raporty'@);

OSOBA.cntx_pop();
P.cntx_pop();
~~


\rach_do_umowy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Wybór aneksów do umowy o pracę do drukowania zaświadczeń.
::   WE:
::   WY: tabela z aneksami do wydrukowania
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.win_edit('RED_Z');
ZC.cntx_psh();
ZC.index('ZLECPRZ');
RH.cntx_psh();
RH.index('RACHUNKI');
UD_DEF.cntx_psh();

{? +P_FILTER.STATUS
|| UD_FIR.cntx_psh();
   UD_FIR.index('UD_SKL');
   UD_FIR.prefix(exec('firma','ustawienia'));
   _ud_skl:={? UD_FIR.first() || UD_FIR.UD_SKL || null() ?};
   UD_FIR.cntx_pop()
|| _ud_skl:=__PARSES.getVal('JednostkaOrganizacyjna').REF
?};

UD_DEF.seek(exec('szukaj_ud_def','schemat',exec('domyslny','schemat','PODZORG'),_ud_skl).REF,,1);

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PPL';
_args.F_ZATR:=__PARSES.getVal('F_ZATR').KOD;
_args.VIEW:={? +P_FILTER.STATUS || P_FILTER.STATUS || __PARSES.getVal('ZakresDanych') ?};

_TAB:=tab_tmp(1,
   'P','STRING[16]','Zleceniobiorca',
   'ZC','STRING[16]','Umowa',
   'RH','STRING[16]','Rachunek do umowy',
   'LP','INTEGER','Kolejność',
   'UMOWA','INTEGER','Wydruk umowy',
   'RMUA','INTEGER','Wydruk informacji RMUA'
);
_rh:=_TAB.ndx_tmp(,1,'RH',,);
_zlec:=_TAB.ndx_tmp(,1,'P',,);
_zc:=_TAB.ndx_tmp(,1,'ZC',,);
_lp:=_TAB.ndx_tmp(,1,'LP',,);
:: okno - zleceniobiorcy
_wz:=P.mk_sel('Zleceniobiorcy'@,,0,'rachzleceniob',0,0,30,,'U',,,,,'maximized');
P.win_fld(_wz,,'T',,,-10,,1,'Identyfikator'@);
P.win_fld(_wz,,'OSOBA','NAZWISKO',,20,,1,'Nazwisko'@);
P.win_fld(_wz,,'OSOBA','PIERWSZE',,20,,1,'Imię'@);
P.win_fld(_wz,,'WYDZIAL','SYMBOL',,-20,,1,'Jednostka organizacyjna'@);
P.win_fld(_wz,,'ZA',,,3,,1,'Aktywny'@,,,2,,"'T'","'N'");
P.win_act(_wz,,'Formuła','Drukuj'@@,,,"sel_exit()",,,,,,,,'icon=print');
P.win_act(_wz,,'Kolejność');
P.win_act(_wz,,'Szukaj');
P.win_act(_wz,,'Rekord',,,,
   "   params_set(_par:=params_get());
      _par.TAB.index(_par.zlec);
      _par.TAB.find_key($P.ref())
   "
);
P.win_btn(_wz,'text=%1,panel=bottom,align=end'['Drukuj'@],'menu:D');
P.win_btn(_wz,'text=%1,icon=xwin16.png:14,panel=bottom,align=end'['Anuluj'@],'key:Esc');
:: okno - umowy cywilnoprawne
_wzc:=ZC.mk_sel('Umowy cywilnoprawne'@,,0,'rachumowyzc',90,0,10,,'U',,,,,'maximized');
ZC.win_fld(_wzc,,'NU',,,-20,,1,'Umowa'@);
ZC.win_fld(_wzc,,'RU','K',,-13,,1,'Rodzaj umowy'@);
ZC.win_fld(_wzc,,'DU',,,-11,,1,'Data rozpoczęcia'@);
ZC.win_fld(_wzc,,'DW',,,-11,,1,'Data zakończenia'@);
ZC.win_fld(_wzc,,'KU',,,12,,1,'Kwota/stawka'@);
ZC.win_fld(_wzc,,'CZYWAL',,,6,,1,'Waluta'@);
ZC.win_fld(_wzc,,'RU','PKU',,6,,1,'Koszty uzyskania'@);
ZC.win_act(_wzc,,'Okienko',,,,
   "  win_set('cur_row_pos=-1',ZC,params_get().wzc)
   "
);
ZC.win_act(_wzc,,'Rekord',,,,
   "  params_set(_par:=params_get());
      _par.TAB.index(_par.zc);
      _par.TAB.find_key($ZC.ref())
   "
);
:: okno - rachunki do umowy
_mob:='mobile_visible=1';
_wrh:=RH.mk_sel('Rachunki do umów'@,,0,'rachunkidouz',90,25,10,,'U',,,,,'maximized');
RH.win_fld(_wrh,,'BL',,,-8,,1,'Blokada'@,,,2,,"'T'","'N'");
RH.win_fld(_wrh,,'KOR',,,-8,,1,'Korekta'@,,,2,,"'T'","'N'");
RH.win_fld(_wrh,,'ZAS',,,-8,,1,'Zasiłek'@,,,2,,"'T'","'N'");
RH.win_fld(_wrh,,'DWY',,,-12,,1,'Data wypłaty'@,,,,,,,,_mob);
RH.win_fld(_wrh,,'PR',,,-6,,1,'Przelew'@,,,2,,"'T'","'N'",,_mob);
RH.win_fld(_wrh,,'PPD',,,-6,,1,'Procent podatku'@,,,,,,,,_mob);
RH.win_fld(_wrh,,'KK','SYM',,-20,,1,'Konto kosztów'@,,,,,,,,_mob);
RH.win_fld(_wrh,_TAB,'UMOWA',,,-12,,1,'Wydruk umowy'@,,,2,,"1","0");
RH.win_fld(_wrh,_TAB,'RMUA',,,-12,,1,'Wydruk RMUA'@,,,2,,"1","0");
RH.win_act(_wrh,,'Formuła','Wybierz'@@,,,
   "   params_set(_par:=params_get());
      _par.TAB.P:=$RH.ZLE().P;
      _par.TAB.ZC:=$RH.ZLE;
      _par.TAB.RH:=$RH.ref();
      _par.TAB.LP:=_par.TAB.size()+1;
      {? _par.TAB.add() || P.actions_grayed(_par.wz,'') ?};
      grp_disp(P,_par.wz)
   ",,1,0,,,'W'
);
RH.win_act(_wrh,,'Formuła','W&ycofaj'@@,,,
   "   params_set(_par:=params_get());
       _par.TAB.index(_par.rh);
      {? _par.TAB.find_key($RH.ref()) || _par.TAB.del() ?};
      P.actions_grayed(_par.wz,{? _par.TAB.size() || '' || 'D' ?});
      grp_disp(P,_par.wz)
   ",,,0,,,'Y'
);
RH.win_act(_wrh,,'Formuła','Wydruk &umowy'@@,,,
   "  _par:=params_get();
      _par.TAB.UMOWA:=~_par.TAB.UMOWA;
      _par.TAB.put()
   ",,,0,,,'U'
);
RH.win_act(_wrh,,'Formuła','Informacja RMUA'@@,,,
   "  _par:=params_get();
      _par.TAB.RMUA:=~_par.TAB.RMUA;
      _par.TAB.put()
   ",,,0,,,'I'
);
RH.win_act(_wrh,,'Okienko',,,,
   "  win_set('cur_row_pos=-1',RH,params_get().wrh)
   "
);
RH.win_act(_wrh,,'Rekord',,,,
   "  _par:=params_get();
      _par.TAB.index(_par.rh);
      _gray:={? _par.TAB.find_key($RH.ref()) || 'W' || 'YUI' ?};
      RH.actions_grayed(_par.wrh,_gray);
      _par.TAB.index(_par.rh);
      {? _par.TAB.find_key($RH.ref())
      || 1
      || _par.TAB.UMOWA:=_par.TAB.RMUA:=0; 0
      ?}
   "
);
RH.win_btn(_wrh,'text=%1,panel=right,align=begin'['Wybierz'@],'menu:W');
RH.win_btn(_wrh,'text=%1,panel=right,align=begin'['Wycofaj'@],'menu:Y');
RH.win_btn(_wrh,'text=%1,panel=right,align=begin'['Wydruk umowy'@],'menu:U');
RH.win_btn(_wrh,'text=%1,panel=right,align=begin'['Informacja RMUA'@],'menu:I');
:: okno grupowe
_gr:=P.grp_make('Rachunki do umów cywilnoprawnych'@,
   "  params_set(_par:=params_get());
      P.actions_grayed(_par.wz,'D');
      exec('filtruj_p','schemat',_par.args.DOMAIN,UD_DEF.ref(),_par.args.F_ZATR,'',_par.args.VIEW);
      P.f_first()
   ",'wybaneksum',,,,,'maximized'
);

P.grp_sel(_gr,P,_wz,,
   "  params_set(_par:=params_get());
      ZC.prefix({? ~P.sel_size() & {? P.f_active() || P.f_get() || P.get() ?} || P.ref() || null() ?});
      ZC.last();
      grp_disp(ZC,_par.wzc);
      RH.prefix({? ~ZC.sel_size() & {? ZC.f_active() || ZC.f_get() || ZC.get() ?} || ZC.ref() || null() ?});
      RH.last();
      grp_disp(RH,_par.wrh)
   ",,,30,,,,,'maximized'
);
P.grp_splt(_gr,,'vertical','umowy');
P.grp_sel(_gr,ZC,_wzc,,
   "  params_set(_par:=params_get());
      RH.prefix({? ~ZC.sel_size() & {? ZC.f_active() || ZC.f_get() || ZC.get() ?} || ZC.ref() || null() ?});
      RH.last();
      grp_disp(RH,_par.wrh)
   ",,,12,
   "  params_set(_par:=params_get());
      RH.prefix({? ZC.get() || ZC.ref() || null() ?});
      RH.last();
      grp_disp(RH,_par.wrh)
   ",,,,'maximized'
);
P.grp_splt(_gr,'umowy','horizontal','rachunki');
P.grp_sel(_gr,RH,_wrh,,,,,15,,,,,'maximized');

params_set('args',_args,'TAB',_TAB,'zlec',_zlec,'zc',_zc,'rh',_rh ,'wz',_wz,'wzc',_wzc,'wrh',_wrh);

P.win_sel(_gr);
{? ~P.select()
   & _TAB.size()
   & FUN.ask('Niektóre rachunki do umowy zostały wybrane do wydruku.'@+'\n'+'Czy na pewno zrezygnować?'@)
|| _TAB.erase()
?};

UD_DEF.cntx_pop();
RH.cntx_pop();
ZC.cntx_pop();
P.cntx_pop();
_TAB.index(_lp);
_TAB

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:53 2278387087e19a2db793ea414a550762a2b27922e104959b991dfc280b8b8402b0c70d385bdd2e0a4224a4a8725090fc3acca9f556b7e7aadfac50dd248b5f739d9bd23d9a0c50a83ac6185cd45b9c8a78952d4b510f8c8cbd70a1ded2dcc11fd69ab2be95e0023f7f62c65c88f80169acbd978151e9c5cd620babbed138aaaa

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !poc_ofo_poce.fml
:: Utworzony: 05.09.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności POC_OFO_POCE - Przeglądanie ocen
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Przeglądanie ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::
::# kind=WE, symbol=ZO_PROC, type=_ZO_PROC, name=Wskazanie sesji ocen, required=N
::# kind=WE, symbol=ZO_TEST, type=_ZO_TEST, name=Wskazanie oceny całkowitej, required=N

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;

_proc_id:=exec('ref2uid','#table',_in.ZO_PROC);
_test_id:=exec('ref2uid','#table',_in.ZO_TEST);

_id:=
   {? _proc_id<>'' & _test_id='' || _proc_id
   |? _proc_id='' & _test_id<>'' || _test_id
   || ''
   ?};

_result:='';

{? _id=''
|| _result:=exec('error','!poc_ofo_poce')

|? _mp.isMicro()
|| _mp.keyRef(_id);
   exec('select','!poc_ofo_poce',_id,_mp);
   _mp.delRef(_id);
   _mp.cancel()

|| _value:=exec('select','!poc_ofo_poce',_id,_mp);
   {? type_of(_value)=type_of(0)
   || {? _value<>0
      || _mp.done()
      || _mp.keep()
      ?}
   || _result:=_value
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg('%1'@[_result])
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Wybór sesji ocen - formuła opisu zadania.
::   WE:
::   WY: tekst do wyświetlenia na liście zadań
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_tab_p:=exec('init_desc_tab','pracownik');
_tab_phr:=exec('desc_tab','phr_dane');

_ret:='Przeglądanie ocen'@@;
{? type_of(_in.ZO_PROC)=type_of(null) & _in.ZO_PROC<>null
|| ZO_PROC.cntx_psh();
   ZO_PROC.prefix();
   {? ZO_PROC.seek(_in.ZO_PROC)
   || _tab_phr.ZAW_DANE:='T';
      _tab_phr.OKRES_OD:=$ZO_PROC.OKRES_OD;
      _tab_phr.OKRES_DO:=$ZO_PROC.OKRES_DO
   ?};
   ZO_PROC.cntx_pop();
   {? _tab_phr.ZAW_DANE='T'
   || 'Przeglądanie ocen sesji %1 - %2'@@[$ZO_PROC.OKRES_OD,$ZO_PROC.OKRES_DO]
   || 'Przeglądanie ocen sesji'@@
   ?}
|? type_of(_in.ZO_TEST)=type_of(null) & _in.ZO_TEST<>null
|| ZO_TEST.cntx_psh();
   ZO_TEST.prefix();
   ZO_TEST.ZO_OSOBA().ZO_PROC();
   {? ZO_TEST.seek(_in.ZO_TEST,,1)
   || ZZ_DOK.cntx_psh();
      ZZ_DOK.prefix();
      ZO_PROC.cntx_psh();
      ZO_PROC.prefix();
      ZO_OSOBA.cntx_psh();
      ZO_OSOBA.prefix();
      UD_SKL.cntx_psh();
      UD_SKL.prefix();
      STN.cntx_psh();
      STN.prefix();
      _tab_phr.ZAW_DANE:='T';
      _tab_phr.OKRES_OD:=$ZO_PROC.OKRES_OD;
      _tab_phr.OKRES_DO:=$ZO_PROC.OKRES_DO;
      _dok:=ZO_OSOBA.ZZ_OSOBA().ZZ_LINK;
      {? _dok<>null()
      || P.cntx_psh();
         P.index('ZZ_DOK');
         P.prefix();
         {? P.find_key(ref_name(_dok),_dok)
         || _tab_p.ZAW_DANE:='T';
            _tab_p.NAZWISKO:=P.OSOBA().NAZWISKO;
            _tab_p.PIERWSZE:=OSOBA.PIERWSZE;
            _tab_p.OBCY:=OSOBA.OBCY;
            _tab_p.PASZPORT:=OSOBA.PASZPORT;
            _tab_p.PESEL:=OSOBA.PESEL;
            _tab_p.UR_DATA:=$OSOBA.UR_DATA;
            _tab_p.T:=P.T;
            _tab_p.IP:=$P.IP
         || _tab_phr.ZZ_NAZWISKO:=ZZ_OSOBA.NAZWISKO;
            _tab_phr.ZZ_PIERWSZE:=ZZ_OSOBA.PIERWSZE
         ?};
         P.cntx_pop()
      ?};
      _tab_p.UD_SKL:=ZO_TEST.UD_SKL().SYMBOL;
      _tab_p.STN:=ZO_TEST.STN().ST;
      STN.cntx_pop();
      UD_SKL.cntx_pop();
      ZO_OSOBA.cntx_pop();
      ZO_PROC.cntx_pop();
      ZZ_DOK.cntx_pop();
      {? _tab_phr.ZAW_DANE='T'
      || {? _tab_p.ZAW_DANE='T'
         || {? _tab_p.OBCY='T'
            || _ret:=
                  'Przeglądanie ocen %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5, %6, %7'@@
                  [_tab_p.NAZWISKO,_tab_p.PIERWSZE,_tab_p.PASZPORT,_tab_p.T,_tab_p.IP,_tab_p.UD_SKL,_tab_p.STN]
            |? +_tab_p.PESEL
            || _ret:=
                  'Przeglądanie ocen %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5, %6, %7'@@
                  [_tab_p.NAZWISKO,_tab_p.PIERWSZE,_tab_p.PESEL,_tab_p.T,_tab_p.IP,_tab_p.UD_SKL,_tab_p.STN]
            || _ret:=
                  'Przeglądanie ocen %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5, %6, %7'@@
                  [_tab_p.NAZWISKO,_tab_p.PIERWSZE,_tab_p.UR_DATA,_tab_p.T,_tab_p.IP,_tab_p.UD_SKL,_tab_p.STN]
            ?}
         || _ret:='Przeglądanie ocen %1, %2'@@[_tab_p.UD_SKL,_tab_p.STN]
         ?}
      ?}
   ?};
   ZO_TEST.cntx_pop()
?};
_ret


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Przeglądanie formularzy ocen niemożliwa.\nNie znaleziono wskazanego zapisu.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Wybór sesji ocen właściwych dla programu.
::   WE: _a [UIDREF] - uidref sesji ocen lub oceny końcowej
::       _b [OBJECT] - wskazanie obiektu menadżera procesów
::   WY: 1 - czynność została zakończona
::       0 - czynność należy podtrzymać na liście zadań
::       [STRING] - komunikat błędu
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
exec('init','poc');

_SRC:=ref_tab(_a);
_TAB:={? _SRC=ZO_PROC || ZO_TEST
      |? _SRC=ZO_TEST || ZO_OCENA
      || ~~
      ?};

{? type_of(_TAB)=type_of(~~)
|| return(0)
?};

ZZ_POM.cntx_psh();
_SRC.cntx_psh();
_SRC.prefix();
{? _SRC.seek(_a) & (_wnd:=exec('setup_wnd','!poc_ofo_poce',_a,_b))<>''
|| _TAB.cntx_psh();
   _TAB.win_sel(_wnd);
   _ret:=_TAB.select();
   _TAB.cntx_pop()
?};
_SRC.cntx_pop();
ZZ_POM.cntx_pop();

_ret


\setup_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Tworzy okienko czynności.
::   WE: _a [UIDREF] - uidref sesji ocen lub oceny końcowej
::       _b [OBJECT] - wskazanie obiektu menadżera procesów
::   WY: akronim okienka lub tekst pusty
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';
_TAB:=~~;

{? ref_tab(_a)=ZO_PROC
|| _TAB:=ZO_TEST;
   _wnd:=_TAB.grp_make('Oceny'@,
:     przed wypełnieniem
      "  ZO_TEST.cntx_psh();
         ZO_OCENA.cntx_psh();
         ZO_ASP.cntx_psh();
         ZO_DOD.cntx_psh();

         ZO_TEST.index('ZO_TEST');
         ZO_OCENA.index('NAZWA');
         ZO_ASP.index('NAZWA');
         ZO_DOD.index('NAZWA');
         1
      ",
:     identyfikator
      'zo_proc_ocena',
:     położenie
      ,,
:     przy zamknięciu
      "  ZO_TEST.cntx_pop();
         ZO_OCENA.cntx_pop();
         ZO_ASP.cntx_pop();
         ZO_DOD.cntx_pop();
         1
      ",,
:     tryb wyświetlania
      'maximized'
   );

:  formularze ocen
   _TAB.grp_sel(_wnd,,'TEST',,
:     po odświeżeniu
      "
         {? ~exec('form_komp','phr_dane',ZO_TEST.ZZ_DOK) & ~grp_empty(ZO_TEST,'TEST')
         || tab_hide(1,,'content');
            tab_sel(2,'content')
         || tab_show(1,'content');
            tab_sel(1,'content')
         ?};
         grp_disp(ZO_OCENA,'WER',1,1);
         grp_disp(ZO_ASP,'WER',1,1);
         ~~
      ",
:     położenie i wysokość
      ,,10,
:     przed obsługą
      "ZO_TEST.prefix(ZO_PROC.ref())",
:     po obsłudze
      "",
:     utrwalenie, aktywacja, wypełnienie
      0,0,_mode
   );
   _TAB.grp_splt(_wnd,,'horizontal','content')

|? ref_tab(_a)=ZO_TEST
|| _TAB:=ZO_OCENA;
   _wnd:=_TAB.grp_make('Oceny'@,
:     przed wypełnieniem
      "  ZO_OCENA.cntx_psh();
         ZO_ASP.cntx_psh();
         ZO_DOD.cntx_psh();

         ZO_OCENA.index('NAZWA');
         ZO_ASP.index('NAZWA');
         ZO_DOD.index('NAZWA');

         {? ~exec('form_komp','phr_dane',ZO_TEST.ZZ_DOK)
         || tab_hide(1,,'');
            tab_sel(2,'')
         || tab_show(1,'');
            tab_sel(1,'')
         ?};

         1
      ",
:     identyfikator
      'zo_test_ocena',
:     położenie
      ,,
:     przy zamknięciu
      "  ZO_OCENA.cntx_pop();
         ZO_ASP.cntx_pop();
         ZO_DOD.cntx_pop();
         1
      ",,
:     tryb wyświetlania
      'maximized'
   )

|| return('')
?};

: formularze ocen
_TAB.grp_sel(_wnd,ZO_OCENA,'WER','Kompetencje'@,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "  ZO_OCENA.prefix(ZO_TEST.ref());
      {? cur_tab()=ZO_TEST & grp_empty(ZO_TEST,'TEST')
      || '#disable'
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

: pozycje formularza
_TAB.grp_sel(_wnd,ZO_ASP,'WER','Dodatkowe'@,
:  po odświeżeniu
   "grp_disp(ZO_DOD,'WER')",
:  położenie i wysokość
   ,,7,
:  przed obsługą
   "  ZO_ASP.prefix(ref_name(ZO_TEST.ZZ_DOK),ZO_TEST.ZZ_DOK,'T','T');
      {? cur_tab()=ZO_TEST & grp_empty(ZO_TEST,'TEST')
      || '#disable'
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);
_TAB.tab_splt(_wnd,,'horizontal','bottom');

: wyniki oceny
_TAB.grp_sel(_wnd,ZO_DOD,'WER',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "  ZO_DOD.prefix(ZO_ASP.ref());
      {? grp_empty(ZO_ASP,'WER')
      || '#disable'
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

: dla czynności w procesie dołącz pasek przycisków
{? _b.isMicro()=0
|| _bar:=ZZ_POM.mk_edit(,0);
   _bid:=ZZ_POM.win_ebtn(_bar,'text=%1,display=1'[exec('text_red_zakoncz','#window','PKD_A')],
      "sel_exit();''"
   );
   ZZ_POM.btn_opt(_bid,'tooltip='+exec('help_act_zakoncz','#window'));
   _bid:=ZZ_POM.win_ebtn(_bar,'text=%1,display=1'['&Anuluj'@],
      "'key:Esc'"
   );
   ZZ_POM.btn_opt(_bid,'tooltip='+exec('help_act_esc','#window'));

   _TAB.grp_splt(_wnd,,'horizontal','bar');
   _TAB.grp_edit(_wnd,ZZ_POM,_bar,,,,,,'maximized')
?};

_wnd


\zo_ocena_xxx_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Okienko przed okien tabeli ZO_OCENA.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
ZO_OCENA.win_fml(cur_win(1,1),ZZ_POM,'OCENA',,'ICON_BEFORE',
"  {? ZO_OCENA.ZZ_TYP
   || exec('zz_tree_icon','phr_widok',ZZ_TYP,ZO_OCENA.ZZ_TYP)
   || exec('zz_tree_icon','phr_widok',ZZ_KOMP,ZO_OCENA.ZZ_KOMP)
   ?}
");
1


\zo_ocena_xxx_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Rekord przed okien tabeli ZO_OCENA.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY: 0
::----------------------------------------------------------------------------------------------------------------------
ZZ_POM.OCENA:=
   {? ZO_OCENA.ZZ_TYP
   || ZO_OCENA.ZZ_TYP().NAZWA
   || ZO_OCENA.ZZ_KOMP().NAZWA
   ?};
0


\zo_asp_xxx_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Okienko przed okienek tabeli ZO_DOD.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('setup_icon','phr_widok','ZF_SKL','NAZWA');
exec('setup_icon','phr_widok','ZZ_MET','NAZWA');
1


\zo_dod_xxx_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Okienko przed okienek tabeli ZO_DOD.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('setup_icon','phr_widok','ZZ_TYP','NAZWA');
1


\zo_dod_xxx_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Rekord przed okienek tabeli ZO_DOD.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY: 0
::----------------------------------------------------------------------------------------------------------------------
ZZ_POM.OCENA:='<brak oceny>';

{? ZO_DOD.ZO_ASP().ZZ_MET().KOD='OO'
|| ZO_DOD.ZZ_DOK();
   _nota:=ZZ_DOK.memo_txt(,1,'NOTA');
   {? _nota<>''
   || ZZ_POM.OCENA:=_nota
   ?}

|? ZO_DOD.WARTOSC>0
|| ZZ_POM.OCENA:=$ZO_DOD.WARTOSC
?};

0

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 3d72a4a0d6508b854d03b01d2793d693276e8b8b2dbbe9d48a5a38111ec0d6fc4c95460c880c50cec2bcd89b3a9248ffeabfccf595923766f0397019a2e0c4360309a389771974eb78abd76008876a3b7c569f1c936bdbd507aed3678ebdc66fdea436afb2459b74f0765ff027e7c226fda5004cce335fed403dffc496a27e28

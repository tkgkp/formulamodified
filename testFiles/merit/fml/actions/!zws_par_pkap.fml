:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pkap.fml
:: Utworzony: 15.05.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PKAP - Karty pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Karty do czytników RCP.
::   WE:
::   WY:
::  OLD: \karty_pracy/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
R_KARTY.cntx_psh();
R_KARTY.index('R_KARTY');
R_KARTY.prefix();
R_KARTY.win_edit('RED');
R_KARTY.win_sel('WER');
R_KARHIS.use('r_karhis');
R_KARTY.select();
R_KARTY.cntx_pop();
~~


\r_karty_b_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Formuła przed akcją usuń w tabeli R_KARTY.
::   WE:
::   WY:
::  OLD: \RKRuspr/karty.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? R_KARTY.count()
|| FUN.emsg('%1\n%2'['Karta pracy %1 jest wykorzystywana.'@[R_KARTY.N],'Usunięcie niemożliwe.'@]);
   _result:=0
|| _result:=1
?};
_result


\r_karty_be_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS: Formuła przed redakcją wiersza w tabeli R_KARTY.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::  OLD: \RKRrekpr/karty.fml
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
R_KARHIS.cntx_psh();
R_KARHIS.index('ZW_KAR_D');
R_KARHIS.prefix('T',R_KARTY.ref());
{? R_KARHIS.first()
|| {!
   |? {? R_KARHIS.OD<=date() & (R_KARHIS.DO>=date() | R_KARHIS.DO=#0)
      || _result:=Color.fnd_kol('R_KARTY#01#02');
         0
      || _result:=Color.fnd_kol('R_KARTY#01#01');
         1
      ?} & R_KARHIS.next()
   !}
|| R_KARHIS.prefix('N',R_KARTY.ref());
   _result:={? R_KARHIS.first() || Color.fnd_kol('R_KARTY#01#01') || _gray:='H'; 0 ?}
?};
R_KARHIS.cntx_pop();
{? _a
|| R_KARTY.actions_grayed('WER',_gray)
?};
_result


\r_karty_ae_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Formuła po redakcji wiersza w tabeli R_KARTY.
::   WE:
::   WY:
::  OLD: \RKRrekpo/karty.fml
::----------------------------------------------------------------------------------------------------------------------
_spr:=__CHK.record(R_KARTY,,'N');
{? _spr=''
|| {? -(1+menu_txt())='p' || _ref:=R_KARTY.ref() || _ref:=null() ?};
   R_KARTY.cntx_psh();
   R_KARTY.index('R_KARTY');
   R_KARTY.prefix(R_KARTY.N,);
   {? R_KARTY.first() & R_KARTY.ref()<>_ref
   || FUN.emsg('Karta o podanym numerze już występuje w słowniku.'@);
      _spr:='N'
   || _spr:=1
   ?};
   R_KARTY.cntx_pop()
?};
_spr


\r_karty_bfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS: Formuła przed akcją generuj w tabeli R_KARTY. Generowanie kart pracy RCP z podanego zakresu.
::   WE:
::   WY:
::  OLD: \RKRgenpr/karty.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.ask('Czy wygenerować nowe karty pracy?'@)


\r_karty_afg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Formuła po akcji generuj w tabeli R_KARTY. Generowanie kart pracy RCP z podanego zakresu.
::   WE:
::   WY:
::  OLD: \RKRgenpo/karty.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_EDIT.DL:=13;
VAR_EDIT.win_edit('RED_GEN');
{? VAR_EDIT.edit(
      "  {? VAR_EDIT.DL>13
         || FUN.emsg('Wartość w polu \"Długość\" nie może być większa od 13.'@);
            VAR_EDIT.DL:=13;
            'DL'
         |? VAR_EDIT.N_OD>VAR_EDIT.N_DO
         || FUN.emsg('Niepoprawny zakres numerów kart pracy'@);
            VAR_EDIT.N_OD:=VAR_EDIT.N_DO:=0;
            'N_OD'
         || __CHK.record(VAR_EDIT,,'DL','N_OD','N_DO')
         ?}
      ")
|| {! _ii:=VAR_EDIT.N_OD..VAR_EDIT.N_DO
   |! _nr:=(VAR_EDIT.DL*'0')+($_ii)+VAR_EDIT.DL;
      {? ~R_KARTY.find_key(_nr,)
      || R_KARTY.N:=_nr;
         R_KARTY.add()
      ?}
   !}
?}


\r_karty_bfh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS: Formuła przed akcją historia w tabeli R_KARTY. Przeglądanie histori kart pracy RCP
::   WE:
::   WY:
::  OLD: \historia/karty.fml
::----------------------------------------------------------------------------------------------------------------------
R_KARHIS.cntx_psh();
R_KARHIS.use('r_karhis');
R_KARHIS.win_sel('SLO');
R_KARHIS.index('KAR_DATA');
R_KARHIS.prefix(R_KARTY.ref());
R_KARHIS.hdr_sel(' '+R_KARTY.N);
R_KARHIS.select();
R_KARHIS.cntx_pop()


\r_karty_bfl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS: Formuła przed akcją legenda w tabeli R_KARTY.
::   WE:
::   WY:
::  OLD: \leg_rcp_karty/menu.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','R_KARTY#01#')

:Sign Version 2.0 jowisz:1028 2019/10/14 09:20:09 062307865c059ea961f94ac3a9388d0aec9d675891f4ae9a5a9ecbc4376d543ba0d5a6cb593ce947db6f09482fa197b4e054a2a1a7cb1a5a022680b7983c6c977dcc1c60fa18f3a9466c474fac74ff96ca0a93679739fc90064e003b0f6097b16a7bf48fafeb8dac328b2c302e5a3698cb85e0e72f96ba12cfc12f9f4b906825

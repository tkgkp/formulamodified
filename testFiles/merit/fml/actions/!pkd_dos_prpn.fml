:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prpn.fml
:: Utworzony: 02.06.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_DOS_PRPN - Rej. dorobku naukowego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja dorobku naukowego - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE,   symbol=OSOBA,     type=_OSOBA,   name=Wskazanie osoby,   required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.OSOBA);
{? _uidref=''
|| _result:=exec('error','!pkd_dos_prpn')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_prpn',_in.OSOBA);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja dorobku naukowego - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj dorobek naukowy: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj dorobek naukowy: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj dorobek naukowy: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj dorobek naukowy'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie dorobku naukowego niemożliwe.\nNie znaleziono pracownika.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie osoby.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::  OLD: \oswer/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & OSOBA.seek(_a)
|| OS_DNAUK.cntx_psh();
   OS_DNAUK.index('OSDTTYP');
   OS_DNAUK.prefix(OSOBA.ref());
   OS_DNAUK.win_sel('WERI');
   OS_DNAUK.win_edit('RED');
   OS_DNAUK.win_patt('RED');
   _ret:=OS_DNAUK.select();
   OS_DNAUK.cntx_pop()
|| _ret:=exec('error','!pkd_dos_prpn')
?};
OSOBA.cntx_pop();
_ret


\os_dnauk_typ_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: "Przed redagowaniem" dla pola OS_DNAUK.TYP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()<>'popraw' | OS_DNAUK.TYP=null()
|| return(1)
?};

_ret:=1;
SLO_KOD.cntx_psh();
{? OS_DNAUK.TYP().KOD='PUBLIK'
|| P_DNAUK.cntx_psh();
   P_DNAUK.index('OS_DNAUK');
   P_DNAUK.prefix(OS_DNAUK.ref());
   _ret:=~P_DNAUK.first();
   P_DNAUK.cntx_pop()
?};
SLO_KOD.cntx_pop();
_ret


\os_dnauk_da
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz - po" dla tabeli OS_DNAUK w oknie SLO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? OS_DNAUK.f_active()
|| OS_DNAUK.f_add()
?}


\os_dnauk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" - weryfikacja wypełnienia pól tabeli OS_DNAUK.
::   WE:
::   WY:
::  OLD: \os_dnaukae/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=__CHK.record(OS_DNAUK,,'OSOBA','DATA','TYP','OPIS'))<>''
|| return(_chk)
?};

{? OS_DNAUK.PUNKTY<0
|| __CHK.err_fld(OS_DNAUK,'PUNKTY',2,'Wartość nie może być ujemna.'@);
   return('PUNKTY')
?};

''


\os_dnauk_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli OS_DNAUK z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli OS_DNAUK jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=OS_DNAUK
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 7859b794eba9d7200f65c10935a96d845e0d675196d3250bc456d74c256908d0f6cc79a359e8aa1f8c1ebd74a9679cd50cf747cd1ed3b5b9a97eab6f234b91260e564349746f1aaaa1a916d699c35beed712ac5f5062a429e3d395f2feea8abb1b7de59437ffc3af8a25321af1904fe47b1f55b7c1e14d30151fdeac41c44ea8

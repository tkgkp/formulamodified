:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_phis.fml
:: Utworzony: 24.06.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PHIS — Historia stałych systemu.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Historia stałych systemu - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \hist/stalesys.fml
::  OLD: \hist_cfg/stalesys.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');

:: utwórz panel i okna zarządzania
_wnd:=exec('mk_wnd','!zws_par_phis');

B_DOMAIN.cntx_psh();
B_DOMAIN.win_sel(_wnd.GRP);
B_DOMAIN.select();
B_DOMAIN.cntx_pop();

:: porządki
B_DOMAIN.win_del(_wnd.GRP);
KST_DOM.win_del(_wnd.DOM);
~~


\mk_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy panel historii stałych.
::   WE:
::   WY: wskazanie tablicy zawierającej utorzone okienka
::----------------------------------------------------------------------------------------------------------------------
_inTerm:=exec('interm','#system');
_div:=12;

:: dedykowane okienko dla tabeli KST_DOM
_dom:=exec('mk_wnd_dom','!zws_par_phis');

:: panel zarządzania uprawnieniami
_grp:=B_DOMAIN.grp_make('Stałe systemu'@,
:: przed otwarciem
   "exec('adm_bf','!zws_par_phis')",
:: identyfikator
   '#zws_par_phis',,,
:: podczas zamykania
   "exec('adm_oc','!zws_par_phis')"
);

{? _inTerm=0
||
:: lista dziedzin
   B_DOMAIN.grp_sel(_grp,,'SLO',,
::    po zmianie bieżącego wiersza
      "exec('adm_b_domain_ar','!zws_par_phis','ZES')",,,_div
   );
:: lista zestawów stałych
   B_DOMAIN.grp_splt(_grp,,'vertical','right_upper');
   B_DOMAIN.grp_sel(_grp,KST_DOM,'ZES',,
::    po zmianie bieżącego wiersza
      "exec('adm_kst_dom_ar','!zws_par_phis')",,,_div,
::    przed obsługą
      "exec('adm_kst_dom_bs','!zws_par_phis',_a)"
   );
:: stałe w wybranym zestawie
   B_DOMAIN.grp_splt(_grp,,'horizontal','bottom');
   B_DOMAIN.grp_sel(_grp,KST_LST,'ZES',,
::    po zmianie bieżącego wiersza
      "exec('adm_kst_lst_ar','!zws_par_phis')",,,_div,
::    przed obsługą
      "exec('adm_kst_lst_bs','!zws_par_phis',_a)"
   );
:: wartości wybranej stałej
   B_DOMAIN.grp_splt(_grp,'bottom','vertical','right_lower');
   B_DOMAIN.grp_sel(_grp,KST_WAR,'WER',,,,,,
::    przed obsługą
      "exec('adm_kst_war_bs','!zws_par_phis',_a)"
   )
||
:: lista dziedzin
   B_DOMAIN.grp_sel(_grp,,'SLO',,
::    po zmianie bieżącego wiersza
      $'exec(\'adm_b_domain_ar\',\'!zws_par_phis\',\'%1\')'[_dom]
   );
:: lista typów danych
   B_DOMAIN.grp_splt(_grp,,'vertical','right');
   B_DOMAIN.grp_sel(_grp,KST_DOM,_dom,,,,,,
::    przed obsługą
      "exec('adm_kst_dom_bs','!zws_par_phis',_a)"
   )
?};

_wnd:=obj_new('GRP','DOM');
_wnd.GRP:=_grp;
_wnd.DOM:=_dom;
_wnd


\mk_wnd_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy dedykowane okienko dla tabeli KST_DOM.
::   WE:
::   WY: akronim okna
::----------------------------------------------------------------------------------------------------------------------
_act:='Wartości'@;
_tip:='Przeglądzanie wartości stałych'@;
_wnd:=KST_DOM.mk_sel('Zestawy'@,'P',,'#kst_dom_zes_i');
KST_DOM.win_fld(_wnd,,'KST_ZES','SYMBOL',,8,,,,,MS.comment(KST_ZES,'SYMBOL'));
KST_DOM.win_fld(_wnd,,'KST_ZES','OPIS',,50,,,,,MS.comment(KST_ZES,'OPIS'));
KST_DOM.win_act(_wnd,,'Formuła',_act,,_tip,,"exec('adm_war','!zws_par_phis')",1);
KST_DOM.win_btn(_wnd,'text=%1,panel=right,align=begin'[_act],'menu:W');
_wnd


\adm_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Wartości stałych w zestawie.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: panel wartości stałych
_wnd:=KST_LST.grp_make('Wartości'@,
:: przed otwarciem
   "exec('adm_bf','!zws_par_phis')",
:: identyfikator
   '#zws_par_phis_x',,,
:: podczas zamykania
   "exec('adm_oc','!zws_par_phis')"
);
:: stałe w wybranym zestawie
KST_LST.grp_sel(_wnd,KST_LST,'ZES',,
:: po zmianie bieżącego wiersza
   "exec('adm_kst_lst_ar','!zws_par_phis')",,,,
::    przed obsługą
   "KST_LST.prefix(KST_DOM.ref())"
);
:: wartości wybranej stałej
KST_LST.grp_splt(_wnd,,'vertical','right');
KST_LST.grp_sel(_wnd,KST_WAR,'WER',,,,,,
:: przed obsługą
   "exec('adm_kst_war_bs','!zws_par_phis',_a)"
);

KST_LST.cntx_psh();
KST_LST.win_sel(_wnd);
KST_LST.select();
KST_LST.cntx_pop();
KST_LST.win_del(_wnd);
~~


\adm_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wypełnieniem okna KST_HIS tabeli B_DOMAIN
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
B_DOMAIN.clear();
B_DOMAIN.f_clear();
KST_DOM.cntx_psh();
KST_DOM.index('ZES_SYM');
KST_LST.cntx_psh();
KST_LST.index('NUMER');
KST_WAR.cntx_psh();
KST_WAR.index('KST_DEF');

KST_ZES.win_dict('SLO');

1


\adm_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przy zamknięciu okna KST_HIS tabeli B_DOMAIN
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
KST_DOM.cntx_pop();
KST_LST.cntx_pop();
KST_WAR.cntx_pop();
B_DOMAIN.f_clear();
1


\adm_b_domain_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka SLO tabeli B_DOMAIN
::   WE: _a [STRING] - akronim okienka tabeli KST_DOM
::   WY:
::----------------------------------------------------------------------------------------------------------------------
grp_disp(KST_DOM,_a,1)


\adm_b_domain_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka SLO tabeli B_DOMAIN
::   WE: _a [INTEGER] - tryb odrysowywania okna
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('hist_dom_filter','!zws_par_phis')


\adm_kst_dom_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka ZES tabeli KST_DOM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
grp_disp(KST_LST,'ZES',1)


\adm_kst_dom_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka ZES tabeli KST_DOM
::   WE: _a [INTEGER] - tryb odrysowywania okna
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~grp_empty(B_DOMAIN,'SLO')
|| KST_DOM.prefix(B_DOMAIN.ref())
|| KST_DOM.prefix(null);
   '#disable'
?}


\adm_kst_lst_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka ZES tabeli KST_LST
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
grp_disp(KST_WAR,'WER')


\adm_kst_lst_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka ZES tabeli KST_LST
::   WE: _a [INTEGER] - tryb odrysowywania okna
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~grp_empty(KST_DOM,'ZES')
|| KST_LST.prefix(KST_DOM.ref())
|| KST_LST.prefix(null);
   '#disable'
?}


\adm_kst_war_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli KST_WAR
::   WE: _a [INTEGER] - tryb odrysowywania okna
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~grp_empty(KST_LST,'ZES')
|| KST_WAR.prefix({? KST_LST.KST_MAP().KST_DEF().WSPOLNA<>'T' || REF.FIRMA || null ?},KST_DEF.ref());
   _typ:=exec('kst_typ','#stalesys');
   {? _typ>0 & _typ<100
   || KST_WAR.actions_grayed('WER')
   || KST_WAR.actions_grayed('WER','DP:D')
   ?}
|| KST_WAR.prefix(null,null);
   '#disable'
?}


\hist_dom_filter
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Filtrowanie listy dziedzin
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tmp:=sql('select distinct KST_DOM.B_DOMAIN as DOM from KST_DOM');
B_DOMAIN.f_set('SYMBOL',,'B_DOMAIN.REFERENCE in (select :_a.DOM from :_a)',_tmp)


\kst_pom_wartosc_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wyświetleniem pola WARTOSC zmiennej KST_POM
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wartosc:=exec('str2val','#convert',KST_WAR.WARTOSC);
{? type_of(_wartosc)=type_of(null)
|| KST_POM.WARTOSC:=
      {? _wartosc<>null
      || exec('record','#to_string',_wartosc)
      || 'null'
      ?}
|? type_of(_wartosc)>type_of(null)
|| KST_POM.WARTOSC:='[wartość złożona]'
|| KST_POM.WARTOSC:=form(_wartosc)
?};
1


\kst_war_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Edycja wartości stałej
::   WE: _a [INTEGER] - tryb modyfikacji danych
::          0 - dołączanie
::          1 - poprawianie
::   WY: 1 - zatwierdzono edycję
::       0 - zrezygnowano z modyfikacji
::----------------------------------------------------------------------------------------------------------------------
_ZES:=KST_LST.KST_MAP().KST_ZES().SYMBOL;
_KST:=KST_MAP.SYMBOL;
_TYP:='PT_'+$type_of(($(_ZES+'.'+_KST))());

{? _a
|| ($('KST_POM.'+_TYP+':=_a'))(exec('str2val','#convert',KST_WAR.WARTOSC))
|| KST_POM.blank(1)
?};

KST_POM.win_edit(_TYP);
{? KST_POM.edit()
|| _org:=($(_ZES+'.'+_KST))();
   ($(_ZES+'.'+_KST+':=KST_POM.'+_TYP))();
   KST_WAR.WARTOSC:=exec('wartosc','#stalesys',($('KST_POM.'+_TYP))(),_ZES,_KST);
   ($(_ZES+'.'+_KST+':=_a'))(_org);
   1
?}


\kst_war_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed dołączeniem zapisu tabeli KST_WAR
::   WE:
::   WY: 1 - zatwierdzono edycję
::       0 - zrezygnowano z modyfikacji
::----------------------------------------------------------------------------------------------------------------------
KST_WAR.blank();
exec('kst_war_edit','!zws_par_phis',0)


\kst_war_ad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po dołączeniem zapisu tabeli KST_WAR
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KST_WAR.add()


\kst_war_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed poprawieniem zapisu tabeli KST_WAR
::   WE:
::   WY: 1 - zatwierdzono edycję
::       0 - zrezygnowano z modyfikacji
::----------------------------------------------------------------------------------------------------------------------
KST_WAR.get();
exec('kst_war_edit','!zws_par_phis',1)


\kst_war_ap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po poprawieniu zapisu tabeli KST_WAR
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KST_WAR.put()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 658e36da9012a57fadfc17db2021d35090b0c0bd085b8440ac04068c211d7c83f24319128b6d1ebce931b16f05cbd7bafd5d9c5f69c457267d81a3d3f987f6d91d2d9215c91d8e52b1c98cc3f15f09de0e2fc12af96b1daf0b95400d6f51367d809d7aa4e55b316b839e974db68336443b3417e1a6396ec7c0b4eecb214b0747

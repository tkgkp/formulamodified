:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pdpm.fml
:: Utworzony: 27.04.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PDPM - Dysponenci premii uznaniowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Dysponenci premii uznaniowych - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
exec('__RUB','object');
exec('__F_ZATR','object');
exec('__WND','object');

:: Czynności z obszaru ZWS nie mają ustawianych parametrów - należy "ręcznie" sprawdzić, czy użytkownik ma dostęp do
:: choć jednej formy współpracy.

USERS_FZ.cntx_psh();
USERS_FZ.index('DOSTEP');
USERS_FZ.prefix('T',exec('ref_firma','ustawienia'),exec('operatorUser','#users'));
_ret:=USERS_FZ.first();
USERS_FZ.cntx_pop();
{? ~_ret
|| FUN.info('Brak aktywnych uprawnień do form współpracy.'@);
   return()
?};

_atr:=exec('pm_atr','pm_wspolne','Parametryzacja dysponentów premii uznaniowych nie jest możliwa.'@);
{? ~_atr.OK
|| return()
?};

_cfg:=obj_new('pm_dysp_last_mod','pm_rodz_last_mod','pm_domp_last_mod','pm_dysp_ref','r_ref');
_cfg.pm_dysp_last_mod:=0;
_cfg.pm_rodz_last_mod:=0;
_cfg.pm_domp_last_mod:=0;
_cfg.pm_dysp_ref:=null();
_cfg.r_ref:=null();

params_set(
   'cfg',_cfg,
   'atr',_atr,
   'PUPR',exec('dostepne_p','schemat','PPL','*T',,)
);

CP.cntx_psh();
F_ZATR.cntx_psh();
KK.cntx_psh();
OSOBA.cntx_psh();
P.cntx_psh();
STN.cntx_psh();
UD_SKL.cntx_psh();
R.cntx_psh();
R.prefix();
PM_RODZ.cntx_psh();
PM_RODZ.prefix();
PM_DOMP.cntx_psh();
PM_DOMP.prefix();
PM_DYSP.cntx_psh();
PM_DYSP.prefix();
PM_DYSP.win_sel(exec('wnd','!zws_par_pdpm'));
PM_DYSP.win_edit('');
PM_DYSP.select();
{? PM_DYSP.f_active()
|| PM_DYSP.f_clear()
?};
PM_DYSP.cntx_pop();
{? PM_DOMP.f_active()
|| PM_DOMP.f_clear()
?};
PM_DOMP.cntx_pop();
{? PM_RODZ.f_active()
|| PM_RODZ.f_clear()
?};
PM_RODZ.cntx_pop();
{? R.f_active()
|| R.f_clear()
?};
R.cntx_pop();
UD_SKL.cntx_pop();
STN.cntx_pop();
P.cntx_pop();
OSOBA.cntx_pop();
KK.cntx_pop();
F_ZATR.cntx_pop();
CP.cntx_pop();

~~


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła buduje okno grupowe do parametryzacji premii uznaniowych (dysponenci, rodzaje premii, ...).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';
_grp:=PM_DYSP.grp_make('Premie uznaniowe'@,,'pm_dysp_grp',,,
   "  PM_DYSP.sel_adel();
      PM_DYSP.f_clear();
      PM_RODZ.sel_adel();
      PM_RODZ.f_clear();
      PM_DOMP.sel_adel();
      PM_DOMP.f_clear();
      1
   "
);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
PM_DYSP.grp_sel(_grp,PM_DYSP,'WER','Premie wg dysponentów'@,
   "  params_set(_par:=params_get());
      {? ~PM_DYSP.sel_size()
      || _cfg:=_par.cfg;
         _lm:=exec('last_mod_get','#table',PM_DYSP);
         {? _cfg.pm_dysp_last_mod<>_lm
         || {? _cfg.pm_dysp_last_mod
            || PM_DYSP.f_rfresh();
               win_disp()
            ?};
            _cfg.pm_dysp_last_mod:=_lm
         ?}
      ?};
      PM_RODZ.actions_grayed('R',{? PM_DYSP.AKT='T' || '' || 'D:D' ?});
      grp_disp(PM_RODZ,'R')
   ",,,,
   "  {? ~PM_DYSP.sel_size()
      || _par:=params_get();
         _cfg:=_par.cfg;
         PM_DYSP.prefix();
         PM_DYSP.f_set('OSOBA(NAZWISKO),OSOBA(PIERWSZE),OSOBA(PESEL)',,'\"PM_DYSP\".\"P\" in (select REF from :_a)',
            _par.PUPR
         );
         {? ~_cfg.pm_dysp_ref | ~PM_DYSP.f_seek(_cfg.pm_dysp_ref)
         || PM_DYSP.f_first()
         ?}
      ?}
   ",
   "  params_get().cfg.pm_dysp_ref:=PM_DYSP.ref()
   ",,1,_mode,'pm_dysp_grp_dysp1'
);
PM_DYSP.tab_splt(_grp,,'vertical','rodz',',70%');
PM_DYSP.grp_sel(_grp,PM_RODZ,'R',,,,,,
   "  {? grp_empty(PM_DYSP,'WER',1)
      || return('#disable')
      ?};
      PM_RODZ.f_clear();
      PM_RODZ.index('DYSPRN');
      PM_RODZ.prefix(PM_DYSP.ref())
   ",,,,_mode,'pm_dysp_grp_rodz1'
);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
PM_DYSP.grp_sel(_grp,R,'SLO','Dysponenci wg premii'@,
   "  params_set(_par:=params_get());
      PM_RODZ.actions_grayed('D',{? __RUB.sys_attr(R.RN,_par.atr.SYMBOL) || '' || 'D:D' ?});
      grp_disp(PM_RODZ,'D')
   ",,,,
   "  _par:=params_get();
      _cfg:=_par.cfg;
      R.prefix();
      _rub:=__RUB.sys_sql(_par.atr.SYMBOL);
      {? _rub=''
      || _rub:='0'
      ?};
      R.f_set('RN',,'\"R\".\"RN\" in (:_a) or \"R\".\"REFERENCE\" in (select distinct PM_RODZ.R from PM_RODZ)',_rub);
      {? ~_cfg.r_ref | ~R.f_seek(_cfg.r_ref)
      || R.f_first()
      ?}
   ",
   "  params_get().cfg.r_ref:=R.ref();
      R.f_clear()
   ",,,_mode,'pm_dysp_grp_r2'
);
PM_DYSP.tab_splt(_grp,,'vertical','dysp',',40');
PM_DYSP.grp_sel(_grp,PM_RODZ,'D',,
   "  {? ~PM_RODZ.sel_size()
      || _cfg:=params_get().cfg;
         _lm:=exec('last_mod_get','#table',PM_RODZ);
         {? _cfg.pm_rodz_last_mod<>_lm
         || {? _cfg.pm_rodz_last_mod
            || PM_RODZ.f_rfresh();
               win_disp()
            ?};
            _cfg.pm_rodz_last_mod:=_lm
         ?}
      ?}
   ",,,,
   "  {? grp_empty(R,'SLO')
      || return('#disable')
      ?};
      {? ~PM_RODZ.sel_size()
      || PM_RODZ.prefix();
         PM_RODZ.f_set('OSOBA(NAZWISKO),OSOBA(PIERWSZE),OSOBA(PESEL)',,
            '\"PM_RODZ\".\"R\"=:_a and \"PM_RODZ\".\"P\" in (select REF from :_b)',
            R.ref(),params_get().PUPR
         )
      ?}
   ",,,,_mode,'pm_dysp_grp_rodz2'
);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
PM_DYSP.grp_sel(_grp,PM_DYSP,'WER','Pracownicy domyślni wg dysponentów'@,
   "  params_set(_par:=params_get());
      {? ~PM_DYSP.sel_size()
      || _cfg:=_par.cfg;
         _lm:=exec('last_mod_get','#table',PM_DYSP);
         {? _cfg.pm_dysp_last_mod<>_lm
         || {? _cfg.pm_dysp_last_mod
            || PM_DYSP.f_rfresh();
               win_disp()
            ?};
            _cfg.pm_dysp_last_mod:=_lm
         ?}
      ?};
      grp_disp(PM_DOMP,'WER')
   ",,,,
   "  {? ~PM_DYSP.sel_size()
      || _par:=params_get();
         _cfg:=_par.cfg;
         PM_DYSP.prefix();
         PM_DYSP.f_set('OSOBA(NAZWISKO),OSOBA(PIERWSZE),OSOBA(PESEL)',,'\"PM_DYSP\".\"P\" in (select REF from :_a)',
            _par.PUPR
         );
         {? ~_cfg.pm_dysp_ref | ~PM_DYSP.f_seek(_cfg.pm_dysp_ref)
         || PM_DYSP.f_first()
         ?}
      ?}
   ",
   "  params_get().cfg.pm_dysp_ref:=PM_DYSP.ref()
   ",,1,_mode,'pm_dysp_grp_dysp3'
);
PM_DYSP.tab_splt(_grp,,'horizontal','domp',12);
PM_DYSP.grp_sel(_grp,PM_DOMP,'WER',,
   "  {? ~PM_DOMP.sel_size()
      || _cfg:=params_get().cfg;
         _lm:=exec('last_mod_get','#table',PM_DOMP);
         {? _cfg.pm_domp_last_mod<>_lm
         || {? _cfg.pm_domp_last_mod
            || PM_DOMP.f_rfresh();
               win_disp()
            ?};
            _cfg.pm_domp_last_mod:=_lm
         ?}
      ?}
   ",,,,
   "  {? grp_empty(PM_DYSP,'WER',1)
      || return('#disable')
      ?};
      {? ~PM_DOMP.sel_size()
      || PM_DOMP.prefix();
         PM_DOMP.f_set('OSOBA(NAZWISKO),OSOBA(PIERWSZE),OSOBA(PESEL)',,'\"PM_DOMP\".\"PM_DYSP\"=:_a',PM_DYSP.ref())
      ?}
   ",,,,_mode,'pm_dysp_grp_domp3'
);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_grp


\pm_dysp_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli PM_DYSP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PPL';
_args.F_ZATR:=exec('kody','f_zatr');
_args.SQL_FROM:='';
_args.SQL_WHERE:='"P"."REFERENCE" not in (select PM_DYSP.P from PM_DYSP)';
_args.WIELU:=1;
_args.GRP_IDENT:='p_wybierz';

_ret:=exec('wybierz','pracownik',_args);
{? _ret.STATUS<>''
|| FUN.info(_ret.STATUS);
   return()
|? ~_ret.P.first()
|| return()
?};

P.cntx_psh();
P.prefix();
{!
|? {? P.seek(_ret.P.UID)
   || PM_DYSP.blank();
      {? exec('add','#table',PM_DYSP)
      || PM_DYSP.f_add()
      ?}
   ?};
   _ret.P.next()
!};
P.cntx_pop();
~~


\pm_dysp_akt_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Aktywuj/Dezaktywuj - grupa przed" w oknie WER tabeli PM_DYSP.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Aktywuj.
::                         0 - Dezaktywuj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=var_pres('_a')=type_of(0) & _a;
{? _tryb
|| FUN.ask('Czy na pewno chcesz aktywować zaznaczonych dysponentów?'@)
|| FUN.ask('Czy na pewno chcesz dezaktywować zaznaczonych dysponentów?'@)
?}


\pm_dysp_akt_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Aktywuj/Dezaktywuj - przed" w oknie WER tabeli PM_DYSP.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Aktywuj.
::                         0 - Dezaktywuj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=var_pres('_a')=type_of(0) & _a;
{? PM_DYSP.sel_size() |
   {? _tryb
   || FUN.ask('Czy na pewno chcesz aktywować bieżącego dysponenta?'@)
   || FUN.ask('Czy na pewno chcesz dezaktywować bieżącego dysponenta?'@)
   ?}
|| _nz:={? _tryb || 'T' || 'N' ?};
   {? PM_DYSP.AKT<>_nz
   || PM_DYSP.AKT:=_nz;
      PM_DYSP.put()
   ?}
?}


\pm_dysp_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie WER tabeli PM_DYSP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
PM_DYSP.P();
exec('p_bw','pracownik');
P.cntx_pop();
~~


\pm_dysp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER tabeli PM_DYSP.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| PM_DYSP.actions_grayed('WER',{? PM_DYSP.sel_size() || '' |? PM_DYSP.AKT='T' || 'A' || 'E' ?})
?};
~~


\pm_rodz_r_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie R tabeli PM_RODZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_atr:=params_get().atr;

_rub:=__RUB.sys_sql(_atr.SYMBOL);
{? _rub=''
|| _rub:='0'
?};
R.cntx_psh();
R.prefix();
R.f_set('RN',,
   '"R"."RN" in (:_a) and "R"."REFERENCE" not in (select PM_RODZ.R from PM_RODZ where PM_RODZ.PM_DYSP=:_b)',
   _rub,PM_DYSP.ref()
);
_acr:='PM_R_WYBX';
{? (_ws:=__WND.SEL.get(R,_acr))=''
|| _ws:=R.mk_sel('Rubryki'@,'N',,-_acr,,,,,'U');
   R.win_fld(_ws,,'RN',,,,,,MS.name(R,'RN'),,MS.comment(R,'RN'));
   R.win_fld(_ws,,'RT',,,,,,MS.name(R,'RT'),,MS.comment(R,'RT'));
   R.win_act(_ws,,'Formuła','Wybierz'@@,,,
      "  PM_RODZ.blank();
         exec('add','#table',PM_RODZ)
      ",
      "  {? ~R.sel_size()
         || sel_exit()
         ?}
      ",
      1,1,
      "FUN.ask('Czy dodać zaznaczone rubryki?'@)",
      "sel_exit()",
      'W'
   );
   __WND.SEL.put(R,_acr,_ws)
?};
R.win_sel(_ws);
R.select();
R.f_clear();
R.cntx_pop();
0


\pm_rodz_d_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie D tabeli PM_RODZ.
::   WE: _a [STRING] - Zakres rekordów źródłowych:
::                      'D' - Dysponenci.
::                      'P' - Pracownicy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='D' | _a='P')
|| _tryb:=_a
|| return()
?};

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PPL';
_args.SQL_FROM:='';
_args.SQL_WHERE:=
   {? _tryb='D'
   || '"P"."REFERENCE" in (select PM_DYSP.P from PM_DYSP where PM_DYSP.AKT=\'T\') and '
   || ''
   ?}+
   '"P"."REFERENCE" not in (select PM_RODZ.P from PM_RODZ where PM_RODZ.R=\'%1\')' [$R.ref()];
_args.WIELU:=1;

_ret:=exec('wybierz','pracownik',_args);
{? _ret.STATUS<>''
|| FUN.info(_ret.STATUS);
   return()
|? ~_ret.P.first()
|| return()
?};

P.cntx_psh();
P.prefix();
PM_DYSP.cntx_psh();
PM_DYSP.index('DYSP');
PM_DYSP.prefix();
{!
|? {? P.seek(_ret.P.UID)
   || _dysp:=null();
      {? PM_DYSP.find_key(P.ref())
      || _dysp:=PM_DYSP.ref()
      || PM_DYSP.blank();
         PM_DYSP.P:=P.ref();
         {? exec('add','#table',PM_DYSP)
         || _dysp:=PM_DYSP.ref()
         ?}
      ?};
      {? _dysp<>null()
      || PM_RODZ.blank();
         exec('add','#table',PM_RODZ)
      ?}
   ?};
   _ret.P.next()
!};
P.cntx_pop();
PM_DYSP.cntx_pop();
0


\pm_rodz_d_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie D tabeli PM_RODZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
PM_RODZ.P();
exec('p_bw','pracownik');
P.cntx_pop();
~~


\pm_domp_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli PM_DOMP.
::   WE: _a [STRING] - Zakres rekordów źródłowych:
::                      'D' - Podwładni.
::                      'P' - Pracownicy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='D' | _a='P')
|| _tryb:=_a
|| return()
?};

_where:='';
{? _tryb='D'
|| progress(,'%1 %2 ...' ['Przygotowanie listy podwładnych.'@,'Proszę czekać'@],FUN.TYT,1);
   _TAB:=exec('prac_pod','stanprac',PM_DYSP.OSOBA,'TYPPOZ','DOSTINF');
   {? _TAB.first()
   || {!
      |? _where+='\'%1\',' [_TAB.P_SQL];
         _TAB.next()
      !};
      _where:=' and "P"."REFERENCE" in (%1)' [_where-1]
   ?};
   prgs_clr();
   {? _where=''
   || FUN.info('Brak pracowników podwładnych.'@);
      return()
   ?}
?};

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PPL';
_args.SQL_WHERE:=
   '"P"."REFERENCE" not in (select PM_DOMP.P from PM_DOMP where PM_DOMP.PM_DYSP=\'%1\')' [$PM_DYSP.ref()]+
   _where;
_args.WIELU:=1;
_args.GRP_IDENT:='p_wybierz';

_ret:=exec('wybierz','pracownik',_args);
{? _ret.STATUS<>''
|| FUN.info(_ret.STATUS);
   return()
|? ~_ret.P.first()
|| return()
?};

P.cntx_psh();
P.prefix();
{!
|? {? P.seek(_ret.P.UID)
   || PM_DOMP.blank();
      {? exec('add','#table',PM_DOMP)
      || PM_DOMP.f_add()
      ?}
   ?};
   _ret.P.next()
!};
P.cntx_pop();

0

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 1994fc43ae3c15643e1080620f69b58217ce2f217b2771442e567e6b973caf302366d520b79990bf369a2029e0ba5612cd990a934c2f9ce1d2533b11403f51032c3fc1db5480926c07121f15bbbd983365b16615e3d0d5fa5520873ab4e1462e354221767fe61963eac539e21ca3f6b09b4d19383dfc5e269504d98fb511c587

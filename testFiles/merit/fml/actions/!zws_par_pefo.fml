:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pefo.fml
:: Utworzony: 12.07.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PEFO - Elementy formularzy ocen.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Elementy formularzy ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','poc');

_modul:='O';
ZZ_POM.MODUL==_modul;
ZZ_POM.CZYNNOSC:='';

_cfg:=exec('cfg','!zws_par_pefo');
params_set('cfg',_cfg);

ZF_SKL.cntx_psh();
ZF_SKL.f_clear();
ZF_SKL.index('KOD');
ZF_SKL.prefix(exec('ref_firma','ustawienia'));
ZF_SKL.win_sel(_cfg.ZF_SKL.wnd);

ZF_RPI.cntx_psh();
ZF_RPI.f_clear();
ZF_RPI.index('NAZWA');

ZF_WID.cntx_psh();
ZF_WID.f_clear();
ZF_WID.index('TYP');

ZF_SKL.select();

ZF_WID.cntx_pop();

ZF_RPI.cntx_pop();

ZF_SKL.cntx_pop();

ZZ_POM.MODUL==_modul;
~~


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła tworzy okno grupowe do obsługi elementów formularzy ocen.
::   WE: _a [ARRAY] - Tablica elementów nazwanych z konfiguracją.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=_a;

_fid:="'pefo%1' [_a]";

_acr:=~_fid('_');

{? (_wnd:=__WND.SEL.get(ZF_SKL,_acr))<>''
|| return(_wnd)
?};

_wnd:=ZF_SKL.grp_make('Elementy formularzy ocen'@,,_fid('grp'),,,,,'maximized');
__WND.SEL.put(ZF_SKL,_acr,_wnd);

_mode:='maximized_with_title';

:: Elementy.
ZF_SKL.grp_sel(_wnd,ZF_SKL,_cfg.ZF_SKL.ws,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      grp_disp(ZF_RPI,_cfg.ZF_RPI.ws,1,1);
      grp_disp(ZF_WID,_cfg.ZF_WID.ws,1,1)
   ",,,
   20,,,,,
   _mode,
   _fid('zf_skl')
);
:: Formaty.
ZF_SKL.grp_splt(_wnd,,'horizontal','bottom');
ZF_SKL.grp_sel(_wnd,ZF_RPI,_cfg.ZF_RPI.ws,'Formaty'@,,,,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? grp_empty(ZF_SKL,_cfg.ZF_SKL.ws) | ZF_SKL.KOD='SZ' | ZF_SKL.KOD='ST' | ZF_SKL.KOD='KD'
      || '#disable'
      || ZF_RPI.prefix(ZF_SKL.ref())
      ?}
   ",,,,
   'maximized',
   _fid('zf_rpi')
);
:: Uprawnienia
ZF_SKL.grp_sel(_wnd,ZF_WID,_cfg.ZF_WID.ws,'Uprawnienia'@,,,,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? _a
      || _cfg.ZF_WID.ZZ_LINK_blank:=ZF_WID.fld_fml('ZZ_LINK','BLANK',$'ZF_SKL.ZZ_DOK');
         exec('zz_pom_psh','phr_widok',_cfg.ACT)
      ?};
      {? grp_empty(ZF_SKL,_cfg.ZF_SKL.ws) | ZF_SKL.STALY='T'
      || '#disable'
      || ZF_WID.prefix(ZF_SKL.NP_DOK,ZF_SKL.ZZ_DOK)
      ?}
   ",
   "  {? _a
      || params_set(_par:=params_get());
         _cfg:=_par.cfg;
         ZF_WID.fld_fml('ZZ_LINK','BLANK',_cfg.ZF_WID.ZZ_LINK_blank);
         exec('zz_pom_pop','phr_widok')
      ?}
   ",,,
   'maximized',
   _fid('zf_wid')
);
_wnd


\cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła przygotowuje środowisko pracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=obj_new('ACT','ZF_RPI','ZF_WID','ZF_SKL');

_cfg.ACT:='ZWS_PAR_PEFO';

_cfg.ZF_RPI:=obj_new('ws');
_cfg.ZF_RPI.ws:='WER';

_cfg.ZF_WID:=obj_new('ws','ZZ_LINK_blank');
_cfg.ZF_WID.ws:='WER';

_cfg.ZF_SKL:=obj_new('ws','wnd');
_cfg.ZF_SKL.ws:='WER';
_cfg.ZF_SKL.wnd:=exec('wnd','!zws_par_pefo',_cfg);

_cfg


\zf_skl_kod_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Przed redagowaniem pola ZF_SKL.KOD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZF_SKL.SYSTEM='N'


\zf_skl_zz_met_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Przed redagowaniem pola ZF_SKL.ZZ_MET.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZF_SKL.SYSTEM='N' & ZF_SKL.STALY='N'


\zf_skl_staly_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Przed redagowaniem pola ZF_SKL.STALY.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZF_SKL.SYSTEM='N' & ~ZF_SKL.ZZ_MET


\zf_skl_staly_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Po redagowaniu pola ZF_SKL.STALY.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ZF_SKL.STALY='T'
|| ZF_SKL.WALIDUJ:='N'
?};
1


\zf_skl_waliduj_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Przed redagowaniem pola ZF_SKL.WALIDUJ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZF_SKL.STALY='N' & ZF_SKL.SYSTEM='N'


\zf_skl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Rekord - po" w w oknach wertowania tabeli ZF_SKL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zf_skl_chk','phr_zf_tab',-menu_txt()='popraw')


\zf_rpi_sx_prfx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła zwraca obowiązkowy prefiks nazw szablonów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'poc_'


\zf_rpi_sx_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [12.30]
:: OPIS: Domyślny szablon dla elementu wydruku - wartość początkowa dla pól: ZF_RPI.SZABLON, ZF_RPI.SZABLON2.
::   WE: [_a] [STRING] - Sub-prefiks nazwy [domyślnie: '']
::   WY: nazwa pliku szablonu, jesli udalo sie znalezc odpowiedni
::  OLD: \dom_rpi/zz_data.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZF_RPI.ZF_SKL=null()
|| return('')
?};

_prefiks:={? var_pres('_a')=type_of('') || _a || '' ?};
ZF_SKL.cntx_psh();
_kod:=-ZF_RPI.ZF_SKL().KOD;
_system:=ZF_RPI.ZF_SKL().SYSTEM;
ZF_SKL.cntx_pop();

_TMP:=files(_prefiks+_kod+'*.rpi');

{? _TMP.first() & (_TMP.size()=1 | _TMP.FILENAME=_prefiks+_kod+'01.rpi')
|| _TMP.FILENAME

|? _system<>'T'
|| obj_del(_TMP);
   _TMP:=files(_prefiks+'dod*.rpi');
   {? _TMP.first() & _TMP.size()=1
   || _TMP.FILENAME
   || ''
   ?}

|| ''
?}


\zf_rpi_sx_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [12.30]
:: OPIS: Wyświetla listę plików o nazwach zgodnych z maską.
::   WE: [_a] [STRING] - Nazwa "bieżącego" pliku [domyślnie: ''].
::        _b  [STRING] - Maska nazw plików.
::       [_c] [STRING] - Maska nazw plików.
::       [_d] [STRING] - Maska nazw plików.
::       [_e] [STRING] - Maska nazw plików.
::       [_f] [STRING] - Maska nazw plików.
::   WY: Nazwa pliku, jeśli został on wybrany lub pusty ciąg znaków.
::  OLD: \get_file/zz_view.fml
::----------------------------------------------------------------------------------------------------------------------
_fn:={? var_pres('_a')=type_of('') || _a || '' ?};
{? var_pres('_b')<>type_of('')
|| return('')
?};

_TAB:=tab_tmp(2,
   'ORDER','INTEGER',,
   'FILENAME','STRING[64]','Nazwa'@,
   'DIR','STRING[255]','Ścieżka'@,
   'TEXT','SYS_MEMO','Treść'@
);
_masks:='';
{! _arg:=2 .. _
|! {? _masks*('%1, ' [_[_arg]])=0
   || _masks+=_[_arg]+', ';
      _BUF:=files(_[_arg]);
      _loop:=_BUF.first();
      {!
      |? _loop
      |! _TAB.blank();
         _TAB.FILENAME:=_BUF.FILENAME;
         {? ~_TAB.find_rec()
         || _TAB.ORDER:=_arg;
            _TAB.FILENAME:=_BUF.FILENAME;
            _TAB.DIR:=pth_dir(_TAB.FILENAME);
            do();
            {? _TAB.add()
            || _fh:=fopen(_TAB.FILENAME,'ur',1,0,1);
               {? _fh.is_open()
               || _TAB.memo_put(_fh,'TEXT');
                  _fh.fclose()
               || undo()
               ?};
               obj_del(_fh)
            ?};
            end()
         ?};
         _loop:=_BUF.next()
      !};
      obj_del(_BUF)
   ?}
!};
_masks-=2;

_h1:='Nazwa pliku dyskowego'@;

_ws:=_TAB.mk_sel('Wybór pliku (%1)'@ [_masks],,0,'#pefos',,,,,'U');
_TAB.win_fld(_ws,,'FILENAME',,,,,,,,_h1);
_TAB.win_act(_ws,0,'Formuła','Wybierz'@@,,,,"sel_exit()",1);
_TAB.win_act(_ws,0,'Formuła','Szcze&góły'@@,,,"cur_tab(1,1).display()",,,,,,'G');
_TAB.win_act(_ws,0,'Szukaj');
_TAB.win_btn(_ws,'text=%1' ['Szcze&góły'@],'menu:G');
_TAB.win_sel(_ws);

_width:=64;
_we:=_TAB.mk_edit('Plik ',,'#pefoe');
_TAB.win_esep(_we,'Dane podstawowe'@);
_TAB.win_efld(_we,,'FILENAME',,,_width,,,,,_h1);
_TAB.win_efld(_we,,'DIR',,,_width,,,,,'Katalog, w którym plik będzie poszukiwany'@);
_TAB.win_efld(_we,,'TEXT',,,_width,-12,,,,'Treść pliku'@);
_TAB.win_edit(_we);

{? _fn=''
|| _TAB.first()
|| _TAB.blank();
   _TAB.FILENAME:=_fn;
   _TAB.find_rec()
?};

{? _TAB.select(,1,-1)
|| _TAB.FILENAME
|| ~~
?}


\zf_rpi_zf_skl_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wartość początkowa pola ZF_RPI.ZF_SKL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZF_SKL.ref()


\zf_rpi_szablon_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wartość początkowa pola ZF_RPI.SZABLON.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zf_rpi_sx_bl','!zws_par_pefo',exec('zf_rpi_sx_prfx','!zws_par_pefo')+'zf')


\zf_rpi_szablon_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wybór F3 dla pola ZF_RPI.SZABLON.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_prfx:=exec('zf_rpi_sx_prfx','!zws_par_pefo');
{? ZF_RPI.ZF_SKL
|| exec('zf_rpi_sx_f3','!zws_par_pefo',fld(),_prfx+'zf'+-ZF_RPI.ZF_SKL().KOD+'*.rpi',_prfx+'zf*.rpi')
|| exec('zf_rpi_sx_f3','!zws_par_pefo',fld(),_prfx+'zf*.rpi')
?}


\zf_rpi_szablon2_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wartość początkowa pola ZF_RPI.SZABLON2.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zf_rpi_sx_bl','!zws_par_pefo',exec('zf_rpi_sx_prfx','!zws_par_pefo')+'zr')


\zf_rpi_szablon2_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wybór F3 dla pola ZF_RPI.SZABLON.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_prfx:=exec('zf_rpi_sx_prfx','!zws_par_pefo');
{? ZF_RPI.ZF_SKL
|| exec('zf_rpi_sx_f3','!zws_par_pefo',fld(),_prfx+'zr'+-ZF_RPI.ZF_SKL().KOD+'*.rpi',_prfx+'zr*.rpi')
|| exec('zf_rpi_sx_f3','!zws_par_pefo',fld(),_prfx+'zr*.rpi')
?}


\zf_rpi_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli ZF_RPI.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zf_rpi_chk','phr_zf_tab',-menu_txt()='popraw')


\zf_skl_kod_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Formuła 'Po redagowaniu' pola KOD tabeli ZF_SKL. Ustawia flagę pola DOKUMENT w zależności od wybranego kodu.
::----------------------------------------------------------------------------------------------------------------------
{? ZF_SKL.KOD='SZ' | ZF_SKL.KOD='ST' | ZF_SKL.KOD='KD'
|| ZF_SKL.DOKUMENT:='N'
|| ZF_SKL.DOKUMENT:='T'
?}


\zf_skl_dokument_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.42]
:: OPIS: Po redagowaniu pola ZF_SKL.DOKUMENT.
::----------------------------------------------------------------------------------------------------------------------
ZF_SKL.KOD<>'KD' & ZF_SKL.KOD<>'SZ' & ZF_SKL.KOD<>'ST'

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:05 b317f10d362e5b02ba62dbdbc77c24892170a36ab7d7a5bc9cee42fbdd1bf019527097cce21954b6d605a3d628d6b5bb3946274b69d7c5da955974b1b4c6c6bfd4d481908a8b97f6e35e40683664342f987433a9e2fc35bf1f03405b3d7952998edd5a1843b646b3082ea6437f5325b118c8007f6af8fbca376ffbdadc961dd6

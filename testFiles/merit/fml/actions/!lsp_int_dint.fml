:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_int_dint.fml
:: Utworzony: 18.01.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Rejestracja deklaracji INTRASTAT wywóz
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WY,   symbol=IST,       type=_IST,     name=Deklaracja INTRASTAT, required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init','lmg');

:: Uruchomiona akcja
_akcja:=_mp.akcja();

:: Uruchomiona automatycznie
_auto:=_akcja<>'Akceptuj' & _mp.isAutoRun();

{? _mp.pathTodo()
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('IST',_out)=type_of(null()) & _out.IST
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego INT.')
   ?}
?};

:: Wyzwalacz, który po dodaniu nagłówka deklaracji:
:: - add: dodaje rekord kluczowy nagłówka deklaracji
::   del: usuwa rekord kluczowy nagłówka deklaracji
:: - add: zapisuje parametr wyjściowy IST = wskazanie na nagłówek deklaracji
::   del: zapisuje parametr wyjściowy IST = null
_mp.trigRef('IST',,1,,exec('kind_out','#b_port'),'IST');

{? _akcja='Dołącz'
   | _mp.pathProc()
   | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
||

:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
   params_set('out',_out,'mp',_mp,'akcja',_akcja);
   exec('dolacz','intrastat','W',_mp.pathArea());

:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? var_pres('IST',_outa)<>type_of(~~) & _outa.IST
:: Dodano deklarację
   ||
::    Ustawienie się na dodanej deklaracji w widoku obszaru
      {? _mp.pathArea() || IST.seek(_outa.IST) ?}
:: Wycofanie czynności bo nie dodano deklaracji
   || _mp.cancel()
   ?}

|? _akcja='Popraw'
   | _mp.pathTodo()
|| {? var_pres('IST',_out)=type_of(null())
:: Uruchomione w procesie
   || IST.cntx_psh();
      IST.prefix();
      {? IST.seek(_out.IST)
      ||
         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('popraw','intrastat',_mp.pathArea());

         _mp.descTodo()
      ?};
      IST.cntx_pop()
   |? _mp.pathTodo()
:: Uruchomione zadanie z listy todo i brak _out.FAKS wtedy zadanie wycofujemy
   || _mp.cancel()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      exec('popraw','intrastat',_mp.pathArea())

   ?}

|? _akcja='Usuń'
|| {? var_pres('IST',_out)=type_of(null())
:: Uruchomione w procesie
   || _ist:=null();
      IST.cntx_psh();
      {? _mp.pathArea()=0 || IST.prefix() ?};
      {? IST.seek(_out.IST)
      ||
         exec('usun','intrastat');

         {? ~IST.seek(_out.IST)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _ist:=IST.ref()
      ?};
      IST.cntx_pop();
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _mp.pathArea() || {? _ist || IST.seek(_ist) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      exec('usun','intrastat');

      _mp.done()
   ?}

|? _akcja='Zakończ_wer' & _mp.pathArea()
|| {? exec('zakoncz','intrastat') || _mp.done() ?}

|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('IST',_out)=type_of(null()) & _out.IST
|| 'Zakończ rejestrację deklaracji Intrastat wywóz: %1'@@[exec('record','#to_string',_out.IST)]
|| 'Zarejestruj deklarację Intrastat wywóz'@@
?}


\ist_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: Deklaracja INTRASTAT - Dołącz
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_INT_DINT';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';

exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:14 58c355a5fbc851ba1f0b322b2f37b8109ac514b679d7e05ce72bd68444c8e07c4c60aff980d9df7212b6807db8e6248e5e8be02590fbe7c7c4461b8c084f63ff35459f27bf530fac9ac96db09a5d62bc91310e8834521cb09994e84c92af4292104e45919d05f3fde3d5cd430dc4b8fe1d86be555c8f318096dd83c6ba030079

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zso_scn_ocrd.fml
:: Utworzony: 06.02.2020
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZSO_SCN_OCRD - OCR dokumentu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła główna czynności ZSO_SCN_OCRD
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
:: PARAMETRY WE:
::# kind=WE, symbol=SKAN_DOK,   type=_SKAN_DOK, name=Wskazanie na zeskanowany dokument, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=SKAN_DOK,   type=_SKAN_DOK, name=Wskazanie na zeskanowany dokument, required=T
_args:=params_get();
_in:=_args.in;
_out:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_brak:=1;
{? var_pres('SKAN_DOK',_in)>0 & _in.SKAN_DOK
|| {? var_pres('SKAN_DOK',_out)<=0 | _out.SKAN_DOK=null
   || _out.SKAN_DOK:=_in.SKAN_DOK;
      _mp.save(,_out)
   ?};
   _ref:=BB.sqlint($_in.SKAN_DOK);
   {? _ref<>0
   || SKAN_DOK.cntx_psh();
      SKAN_DOK.prefix();
      {? SKAN_DOK.seek(_ref,)
      || _brak:=0;
         _status:=SKAN_DOK.STATUS;
         {? _mp.isAutoRun() | _mp.isService()
         || SKAN_DOK.TYP:={? SKAN_DOK.TYP='D' || 'd' |? SKAN_DOK.TYP='F' || 'f' || 'a' ?};
            SKAN_DOK.put();
            exec('akc_send','zso_scn',0)
         |? _akcja=''
         || exec('set_edit','!zso_scn_ocrd');
            exec('upd_btn','!zso_scn_ocrd');
            SKAN_DOK.display()
         ?};
         {? SKAN_DOK.seek(_ref) & SKAN_DOK.STATUS='D'
         || _out.SKAN_DOK:=SKAN_DOK.ref();
            _mp.save(,_out);
            _mp.done()
         ?}
      ?};
      SKAN_DOK.cntx_pop()
   ?}
?};
{? _brak
|| FUN.info('Nie znaleziono zeskanowanego dokumentu.'@);
   _in.SKAN_DOK:=null;
   _mp.save(,_in);
   _mp.done()
?};
{? _mp.isService()
|| _out.SKAN_DOK:=_in.SKAN_DOK;
   _mp.save(,_out);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła na opis TO DO
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('SKAN_DOK',_we)>0 & _we.SKAN_DOK
|| SKAN_DOK.cntx_psh();
   SKAN_DOK.prefix();
   {? SKAN_DOK.seek(_we.SKAN_DOK)
   || _desc:='Rozpoznanie dokumentu OCR: %1'@@[SKAN_DOK.NAZ]
   || _desc:='Rozpoznanie dokumentu OCR'@@
   ?};
   SKAN_DOK.cntx_pop()
|| _desc:='Rozpoznanie dokumentu OCR'@@
?};
_desc


\set_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Ustawia okno edycyjne
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('BtnEdit');
BtnEdit:=obj_new(3);
_okn:=SKAN_DOK.mk_edit('Zeskanowany dokument'@);
SKAN_DOK.win_ewin(_okn,,'INFO');
BtnEdit[1]:=SKAN_DOK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['W&yślij'@],"
   exec('akc_send','zso_scn');
   exec('upd_btn','!zso_scn_ocrd');
   ''
");
BtnEdit[2]:=SKAN_DOK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Weryfikuj'@],"
   exec('akc_ver','zso_scn',0);
   exec('upd_btn','!zso_scn_ocrd');
   {? SKAN_DOK.STATUS='D'
   || 'key:Esc'
   || ''
   ?}
");
BtnEdit[3]:=SKAN_DOK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Pobierz'@],"
   exec('akc_get','zso_scn',0);
   exec('upd_btn','!zso_scn_ocrd');
   {? SKAN_DOK.STATUS='D'
   || 'key:Esc'
   || ''
   ?}
");
_an:=SKAN_DOK.win_ebtn(_okn,'text=%1, btn_label_align=center, panel=bottom, align=end, display=1'['&Anuluj'@],'key:Esc');
SKAN_DOK.btn_eopt(_okn,_an,'tooltip='+exec('help_red_esc','#window','A'));
SKAN_DOK.win_edit(_okn)


\upd_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Aktualizuje status przycisków
::----------------------------------------------------------------------------------------------------------------------
SKAN_DOK.btn_eopt(SKAN_DOK.win_edit('?'),BtnEdit[1],'state='+{? SKAN_DOK.STATUS='R' || 'normal' || 'grayed' ?});
SKAN_DOK.btn_eopt(SKAN_DOK.win_edit('?'),BtnEdit[2],'state='+{? SKAN_DOK.STATUS='S' || 'normal' || 'grayed' ?});
SKAN_DOK.btn_eopt(SKAN_DOK.win_edit('?'),BtnEdit[3],'state='+{? SKAN_DOK.STATUS='V' || 'normal' || 'grayed' ?});
~~


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? 8+ref_name(_kref)='skan_dok'
         || _ref:=exec('FindAndGet','#table',SKAN_DOK,_kref,,,null());
            {? _ref=null
            || _wy.SKAN_DOK:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 43890212ff3e640f82ee64addd4ca11d18c3938b72cff28c8caa3bc37a33ea8e692acb36cb3787024036c8618a791b5489371ce5c0877d82de336e6c26a772a50ed9a19ad35693440762d29503834ecf74da42e2732ce08e6e963655277515788fe23095b0850114af44de320b1d6bf30e3d7b279a40d4aa2d591d76390f4ed0

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_grp_dbww.fml
:: Utworzony: 22.11.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PRC_GRP_DBWW - Błędy danych wejścia/wyjścia.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Przeglądanie błędów danych wejścia i wyjścia.
::   WE:
::   WY:
::  OLD: \ERR_KART/okienka.fml
::       \ERR_KAL/okienka.fml
::       \ERR_MAX/okienka.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

_mode:='maximized';
_cfg:=obj_new('tab','bs');
_cfg.tab:='R_ERRKAL';
_cfg.bs:=0;
params_set('cfg',_cfg);

VAR_EDIT.ROK:=date()~1;
VAR_EDIT.MSC:=date()~2;
A_OKR.cntx_psh();
{? A_OKR.seek(__HARM.OKR_REF,,1)
|| {? date()<A_OKR.OD
   || VAR_EDIT.ROK:=A_OKR.OD~1;
      VAR_EDIT.MSC:=A_OKR.OD~2
   |? date()>A_OKR.DO
   || VAR_EDIT.ROK:=A_OKR.DO~1;
      VAR_EDIT.MSC:=A_OKR.DO~2
   ?}
?};
VAR_EDIT.efld_opt('PRDN_DAT','mark=1',,'ROK');
VAR_EDIT.efld_opt('PRDN_DAT','mark=1',,'MSC');

A_OKR.cntx_pop();

R_ERRKAL.cntx_psh();
R_ERRKAR.cntx_psh();
R_ERRMAX.cntx_psh();
:: Przygotowanie okna grupowego do wyświetlenia informacji o błędach.
_wnd:=R_ERRKAL.grp_make('Błędy danych z importu wejść i wyjść'@,
   "  params_exec('init_tab','!prc_grp_dbww')
   "
);

:: Okno wyboru okresu do prezentacji danych
R_ERRKAL.grp_edit(_wnd,VAR_EDIT,'PRDN_DAT',,,
   "  params_set(_par:=params_get());
      {? _par.cfg.tab='R_ERRKAL'
      || R_ERRKAL.btn_sopt('WER','Anuluj','state=grayed')
      |? _par.cfg.tab='R_ERRMAX'
      || R_ERRMAX.btn_sopt('WER','Anuluj','state=grayed')
      |? _par.cfg.tab='R_ERRKAR'
      || R_ERRKAR.btn_sopt('WER','Anuluj','state=grayed');
         R_ERRKAR.btn_sopt('WER','Odczyt','state=grayed')
      ?};
      {? edit_start()
      || exec('init_tab','!prc_grp_dbww')
      || sel_exit()
      ?}
   ",,,_mode);

:: Dodanie zakładek z oknami opisu błędów we/wy.
R_ERRKAL.grp_splt(_wnd,,'vertical','errors',',20%');

:: Błędy kalendarza.
R_ERRKAL.grp_sel(_wnd,,'WER','Błędy kalendarza'@,,,,,
   "  params_get().cfg.tab:='R_ERRKAL';
      params_get().cfg.bs:=1;
      exec('refresh','!prc_grp_dbww',params_get())
   ",,,,_mode,'r_errkal_wer'
);

:: Błędy przekroczenia czasu pracy.
R_ERRKAL.grp_sel(_wnd,R_ERRMAX,'WER','Błędy maksymalnego czasu pracy'@,,,,,
   "  params_get().cfg.tab:='R_ERRMAX';
      params_get().cfg.bs:=1;
      exec('refresh','!prc_grp_dbww',params_get())
   ",,,,_mode,'r_errmax_wer'
);

:: Błędy braku karty pracy
R_ERRKAL.grp_sel(_wnd,R_ERRKAR,'WER','Błędy kart pracy'@,,,,,
   "  params_get().cfg.tab:='R_ERRKAR';
      params_get().cfg.bs:=1;
      exec('refresh','!prc_grp_dbww',params_get())
   ",,,,_mode,'r_errkar_wer'
);

:: Wyświetlenie okna z błędami danych we/wy.
R_ERRKAL.win_sel(_wnd);
R_ERRKAL.select();

{? R_ERRMAX.f_active() || R_ERRMAX.f_clear() ?};
{? R_ERRKAL.f_active() || R_ERRKAL.f_clear() ?};
{? P.f_active() || P.f_clear() ?};
VAR_EDIT.efld_opt('PRDN_DAT','mark=0',,'ROK');
VAR_EDIT.efld_opt('PRDN_DAT','mark=0',,'MSC');
R_ERRMAX.cntx_pop();
R_ERRKAR.cntx_pop();
R_ERRKAL.cntx_pop();
~~


\init_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Otworzenie zbiorów tabel z błędami danych we/wy na podstawie podanych parametów
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(_par:=params_get());

:: Ustalenie listy współpracowników, dla których zostaną zweryfikowane kalendarze i zapisane błędy
P.cntx_psh();
P.clear();
{? P.f_active() || P.f_clear() ?};
exec('filtruj_p','schemat','PRC',UD_DEF.ref(),P_FILTER.F_ZATR().KOD,'T',P_FILTER.STATUS,
     'join A_OKRP using(A_OKRP.P, P.REFERENCE)',
     'A_OKRP.OKR=\''+$__HARM.OKR_REF+'\''
);
_PRAC:=tab_tmp(,'REF','STRING[16]',);

:: Błędy przypisania kalendarzy do współpracownika.
MASK.Use('R_ERRKAL',VAR_EDIT.ROK,VAR_EDIT.MSC);
R_ERRKAL.clear();
{? P.f_first()
|| R_KARHIS.cntx_psh();
   R_KARHIS.index('PR_ZW_OD');
   R_WZCZ.cntx_psh();
   R_WZCZ.index('R_WZCZ');
   _dt:=date(VAR_EDIT.ROK,VAR_EDIT.MSC,1);
   {!
   |? R_WZCZ.prefix(P.name(),P.ref());
      {? {? ~R_WZCZ.find_le(_dt) || ~P.KAL ?}
      || R_ERRKAL.blank(1);
         R_ERRKAL.P:=P.ref();
         R_KARHIS.prefix(P.ref,'T');
         R_ERRKAL.K:={? R_KARHIS.last() || R_KARHIS.K().N || '' ?};
         {? ~R_ERRKAL.find_rec()
         || R_ERRKAL.D:=mb_exec('DATA');
            R_ERRKAL.add()
         ?}
      ?};
      _PRAC.REF:=$P.ref();
      _PRAC.add();
      P.f_next()
   !};
   R_WZCZ.cntx_pop();
   R_KARHIS.cntx_pop()
?};
{? R_ERRKAL.f_active() || R_ERRKAL.f_clear() ?};
R_ERRKAL.f_set('P(T)',,'R_ERRKAL.P in (select :_a.REF from :_a )',_PRAC);
R_ERRKAL.f_first();

:: Błędy przekroczenia maksymalnego czasu pracy.
MASK.Use('R_ERRMAX',VAR_EDIT.ROK,VAR_EDIT.MSC);
R_ERRMAX.clear();
{? R_ERRMAX.f_active() || R_ERRMAX.f_clear() ?};
R_ERRMAX.f_set('P(T)',,'R_ERRMAX.P in (select :_a.REF from :_a)',_PRAC);
R_ERRMAX.f_first();

:: Błędy kart pracy RCP.
MASK.Use('R_ERRKAR',VAR_EDIT.ROK,VAR_EDIT.MSC);
R_ERRKAR.index('R_ERRKAR');
R_ERRKAR.prefix();
R_ERRKAR.first();

:: Odświeżenie widoku danych.
exec('refresh','!prc_grp_dbww',params_get(),1);
P.cntx_pop();
~~


\refresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.02]
:: OPIS: Odświeżanie widoku danych
::   WE: _a  [OBJECT]  - obiekt parametrów konfiguracyjnych
::      [_b] [INTEGER] - czy wykonywać grp_disp (domyślnie nie)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
     _par:={? var_pres('_a')=117        || _a || return(~~) ?};
_grp_disp:={? var_pres('_b')=type_of(0) || _b || 0          ?};

params_set(_par);
{? _par.cfg.tab='R_ERRKAL'
|| R_ERRKAL.btn_sopt('WER','Anuluj','state=normal');
   {? _grp_disp || grp_disp(R_ERRKAL,'WER') ?};
   {? ~_par.cfg.bs || win_activate('r_errkal_wer') ?}
|? _par.cfg.tab='R_ERRMAX'
|| R_ERRMAX.btn_sopt('WER','Anuluj','state=normal');
   {? _grp_disp || grp_disp(R_ERRMAX,'WER') ?};
   {? ~_par.cfg.bs || win_activate('r_errmax_wer') ?}
|? _par.cfg.tab='R_ERRKAR'
|| R_ERRKAR.btn_sopt('WER','Anuluj','state=normal');
   R_ERRKAR.btn_sopt('WER','Odczyt','state=normal');
   {? _grp_disp || grp_disp(R_ERRKAR,'WER') ?};
   {? ~_par.cfg.bs || win_activate('r_errkar_wer') ?}
?};
_par.cfg.bs:=0;
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:53 37ce953fe37cfc9896d9069d8baaf8410708a2eb5d5cd6d1fc35cbdc5e245c9171e32f9d8c0d9a5793bf08dd01cecc296129eacad6fd05efaed28b3d8b4752c2be5400be266c393ed5e383ee4521ba44dc5244344b6cef608871256bcce9e7ea3aa24d31dfe80ad4211c4a28ee4f74b92b252cc9187698cc4ca6f8861dbd2bd9

:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppk_ewi_arnw.fml
:: Utworzony: 02.07.2019
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PPK_EWI_ARNW - Wznowienie wpłat do PPK.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.22]
:: OPIS: Uczestnicy PPK, którym wznowiono naliczanie wpłat.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::
:: Parametr wejściowy PODM określa ogranicznie rodzaju podmiotu (współpracownika).
::    '0' - wszyscy (osoby i uczestnicy) - domyślnie
::    '1' - tylko uczestnicy
::    '2' - tylko osoby
:: Domyślnie przyjmuje wartość '0'.
::# kind=WE, symbol=PODM, type=STRING, name=Rodzaj współpracowników, required=N, ^
::#    fml_val="exec('edit_radiobutton','#edit',{? _a=~~ || '0' || _a ?},'Rodzaj współpracowników', ^
::#             spli_str('Wszyscy,Uczestnicy,Osoby',','),spli_str('0,1,2',','))"
::
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
::
::# kind=WY, symbol=SUB, type=STRING, name=Temat, required=N
::# kind=WY, symbol=TO, type=MEMO, name=Odbiorca, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,BŁĄD)", required=N
::
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_par:=params_get();
_in:=_par.in;
:: Przypisanie domyślnej wartości '0' jeśli nie podano, lub podano błędną:
{? var_pres('PODM',_in)<>type_of('') | ~(',0,1,2,'*',%1,'[_in.PODM])
|| _in.PODM:='0'
?};
_args:=exec('add_email_a','#mailbox');
_args.SUB:='Informacja o automatycznym wznowieniu naliczania wpłat do PPK';
_args.RCV:=exec('odbiorcy','ppk_eml',_par.mp.buf_act.UID);
_args.BODYH:=exec('tresc','!ppk_ewi_arnw',exec('wznowienie_wplat','ppk_eml',,#(_in.PODM),_par.mp.isService()));
params_exec('main','ppk_eml',_args)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.22]
:: OPIS: Przygotowanie informacji z listą uczestników PPK, którym wznowiono naliczanie wpłat - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wysłanie popwiadomienia o uczestnikach PPK, którym wznowiono naliczanie wpłat'@@


\tresc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.22]
:: OPIS: Formuła formatująca teść wysyłanej informacji w HTML.
::   WE: _a [TABLE] - tabela z listą osób zatrudnionych, w imieniu których należy zawrzeć umowę o prowadzenie PPK.
::   WY:
::  OLD: \bd_wznow/ppk_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_PPK:=_a;
_ok:='T';
_tresc:='';

{? _PPK.first()
|| _th:="'<th [[STYLE_TABLE_TH]]>'+_a+'</th>'";
   _td:="'<td [[STYLE_TABLE_TD]]>'+_a+'</td>'";
   _tresc:=
      '<center><h4>Wznowienie naliczania wpłat do PPK</h4></center><br>'+
      '<h5>Lista uczestników, którym wznowiono naliczanie wpłat do PPK:</h5><br><br>'+
      '<table [[STYLE_TABLE]]>\n'+
         '<tr [[STYLE_TABLE_TR]]>'+
            _th('Nazwisko'@)+
            _th('Imię'@)+
            _th('PESEL'@)+
            _th('Paszport'@)+
            _th('Data komunikatu'@)+
            _th('Rodzaj odbiorcy'@)+
         '</tr>';
   {!
   |? {? _PPK.OK='T'
      || _tresc+=
            '<tr [[STYLE_TABLE_TR]]>'+
               _td(_PPK.NAZWISKO)+
               _td(_PPK.PIERWSZE)+
               _td(_PPK.PESEL)+
               _td(_PPK.PASZPORT)+
               _td($_PPK.DATA_UTW)+
               _td(_PPK.RODZAJ)+
            '</tr>'
      || _ok:='N'
      ?};
      _PPK.next()
   !};
   _tresc+='</table><br>';
   {? _ok='N'
   || _PPK.first();
      _tresc+=
         '<h5>Lista uczestników, którym nie udało się wznowionowić naliczania wpłat do PPK:</h5><br><br>'+
         '<table [[STYLE_TABLE]]>\n'+
            '<tr [[STYLE_TABLE_TR]]>'+
               _th('Nazwisko'@)+
               _th('Imię'@)+
               _th('PESEL'@)+
               _th('Paszport'@)+
               _th('Data komunikatu'@)+
               _th('Rodzaj odbiorcy'@)+
            '</tr>';
      {!
      |? {? _PPK.OK='N'
         || _tresc+=
               '<tr [[STYLE_TABLE_TR]]>'+
                  _td(_PPK.NAZWISKO)+
                  _td(_PPK.PIERWSZE)+
                  _td(_PPK.PESEL)+
                  _td(_PPK.PASZPORT)+
                  _td($_PPK.DATA_UTW)+
                  _td(_PPK.RODZAJ)+
               '</tr>'
         || _ok:='N'
         ?};
         _PPK.next()
      !};
      _tresc+='</table><br>'
   ?};
   _tresc+='<p>Ta wiadomość została wygenerowana automatycznie - prosimy na nią nie odpowiadać.</p>'
?};

_tresc

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 94028511c5ea23e486ae6786f7e7e0111abbc278766a597e38ca65fe4dea69806d82dbe2e81888dfbdd341cc878084ff2f2dc189d53fbc4e59fc7945ba813a310a95e720c9f2951ca9c4c8b663e14e6fc57ef80f77940e315342e2c7429d5ae15d5f67187e6aac153e54e931a481b32404ea179283e9e718b916e7992a5336db

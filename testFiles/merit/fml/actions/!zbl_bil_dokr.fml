:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zbl_bil_dokr.fml
:: Utworzony: 05.02.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa czynności odebrania potwierdzeń dokumentów z usługi Businesslink
:: UWAGA: do wersji 20.14 czynność ZWS_BIL_DOKR
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Formuła główna czynności odebrania potwierdzeń dokumentów (ZBL_BIL_DOKR)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_action:=_mp.akcja();

::# properties=SERVICE

::# kind=WE, symbol=NOTIFY_TOKEN, type=STRING, name="Token notyfikacyjny", required=N
{? var_pres('NOTIFY_TOKEN',_in)=type_of('') & +_in.NOTIFY_TOKEN>2
|| _NotificationClientToken:=_in.NOTIFY_TOKEN
|| _NotificationClientToken:=''
?};

::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, BŁĄD)", required=N
{? var_pres('RESULT',_out)<>type_of(~~) & var_pres('RESULT',_out)<>type_of('') || return() ?};
::# kind=WY, symbol=DESC, type=MEMO, name="Opis wyniku", required=N
{? var_pres('DESC',_out)<>type_of(~~) & var_pres('DESC',_out)<>type_of('') || return() ?};

{? _mp.isService() || KOMM.init(,,'documents_receive') ?};
SL_api.set_notify_token(_NotificationClientToken);
_result:=exec('documents_receive','!zbl_bil_dokr',~(_mp.isAutoRun() | _mp.isService() | _mp.isGroup()));
SL_api.set_notify_token('');
{? _result.STATUS | _mp.isService()
|| _out.RESULT:={? _result.STATUS || 'OK' || 'BŁĄD' ?};
   _out.DESC:=_result.DESC;
   _mp.save(,_out);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Opis dla czynności wysłania załącznika (ZBL_BIL_DOKR)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_desc:='Odbierz informacje o załącznikach z portalu Businesslink'@@;
_desc


\documents_receive
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Odbiera potwierdzenia dokumentów w usłudze Businesslink.
::   WE: _a - czy wyświetlać dialogi
::   WY: wynik przetwarzania, 1 - jeżeli ok, 0 - w przypadku błędów (np. brak kontaktu z portalem Businesslink)
::----------------------------------------------------------------------------------------------------------------------
_dialog:=_a;
_result:=obj_new('STATUS','DESC');
_result.STATUS:=1;
_result.DESC:='';

{!
|? {? var_pres('_res')>100 || obj_del(_res) ?};
   _res:=exec('DocumentReport','businesslink3',0);
   _result.STATUS*=_res.STATUS;
   _res.STATUS & var_pres('DETAILS',_res)=type_of(0) & _res.DETAILS
!};

_header:='Odbieranie potwierdzeń dokumentów wychodzących i nagłówków dokumentów przychodzących.'@;
{? _result.STATUS
|| _result.DESC:='Dokumenty zostały przetworzone.';
   KOMM.info(_header+'\n'+'Dokumenty zostały przetworzone.'@,_dialog)
|| _result.DESC:=
      'Nie udało się przetworzyć wszystkich dokumentów.'+
      {? _res.MESSAGE<>'' || '\n'+'Ostatni komunikat:'+'\n'+_res.MESSAGE || '' ?};
   KOMM.info(
      _header+'\n'+'Nie udało się przetworzyć wszystkich dokumentów.'@+
      {? _res.MESSAGE<>'' || '\n'+'Ostatni komunikat:'@+'\n'+_res.MESSAGE || '' ?},
      _dialog
   )
?};

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 893e0070984891b929be477202413d024aa046ae27907695c1e5d3dd14f72ca127d99a5897cc0be6d96656189d3b6bd37573423f148894c0e8b6abda77a45495ff13a3ff5fee694d23029247df05abfc572f1bcdd0d0afad085498dd38fa0d20b2e7594a18eb906e154cc164c69e4f4febfd072f43b115c4fb1980554eba8a09

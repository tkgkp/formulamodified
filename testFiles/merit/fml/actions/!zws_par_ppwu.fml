:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ppwu.fml
:: Utworzony: 29.04.2022
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PPWU - Parametry wniosków o urlop
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Parametry wniosków o urlop - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('portalwu_init','portal_nieobecnosci');

:: Przygotowanie do obsługi wniosków ...
typobi:=2;
_typobieg:=exec('bl_typ','obiegi',typobi);
_w_portal:='X';
OBIEGI.TYPOBIEG==_typobieg;
POMOC.W_PORTAL==_w_portal;
_etypy_dict:=ETYPY.win_dict('?');
ETYPY.win_dict('SLO');

PORTALWU.cntx_psh();
PORTALWU.f_clear();
PORTALWU.index('RN');
PORTALWU.prefix();
PORTALWU.win_edit('RED');
PORTALWU.win_sel('WER');
PORTALWU.select();
PORTALWU.cntx_pop();

ETYPY.win_dict(_etypy_dict);
POMOC.W_PORTAL==_w_portal;
OBIEGI.TYPOBIEG==_typobieg;
&typobi;
~~


\portalwu_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli PORTALWU.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('portalwu_chk','!zws_par_ppwu',0)


\portalwu_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wyzwalacz "Dołącz - po" dla tabeli PORTALWU.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
exec('portalwu_moda','!zws_par_ppwu',PORTALWU.R);
~~


\portalwu_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli PORTALWU.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('portalwu_chk','!zws_par_ppwu',1)


\portalwu_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wyzwalacz "Popraw - po" dla tabeli PORTALWU.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
exec('portalwu_moda','!zws_par_ppwu',PORTALWU.R);
~~


\portalwu_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wyzwalacz "Usuń - po" dla tabeli PORTALWU.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
exec('portalwu_moda','!zws_par_ppwu',bfld('R'));
~~


\portalwu_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli PORTALWU.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
__CHK.validate(PORTALWU
   ,$("_a.table(_b,"+$_put+",,'R','ETYPY')")
   ,$("{? PORTALWU.LDOP<0
      || FUN.emsg('"+
            'Liczba dni dopuszczalnego opóźnienia wprowadzania wniosku o nieobecność nie może być ujemna.'@+
         "');
         'LDOP'
      || ''
      ?}
   ")
)


\portalwu_moda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Formuła wykonywana po (szeroko rozumianej) modyfikacji rekordu tabeli PORTALWU.
::   WE: _a [REFERENCE] - Wskazanie rubryki związanej ze zmodyfikowanym rekordem.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
R.prefix();
{? R.seek(_a)
:: Zmiana w parametryzacji powinna zostać wysłana na portal.
|| {? exec('_map_erase','portal_nieobecnosci')
   || R.put(,1)
   || undo('Aktualizacja mapy nieobecności na portalu nie powiodła się.'@)
   ?}
?};
R.cntx_pop();
~~


\portalwu_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli PORTALWU.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Redakcja - tylko przez "Popraw".
exec('portalwu_chk','!zws_par_ppwu',1)


:Sign Version 2.0 jowisz:1045 2022/07/29 13:47:20 c08068504ff18491420d51c71699742698b6e9f41bf89df381cf8b7238ede27e87e168cbed216af92968e5057ed005dddaf59efcd258014cafb42191f9e7df67c7a7621fdc6cd03513a48efa8453e8689ef988e8f4225aea015fa2d9c8d33b28e7eb3b511a4f1bf92ea87aac00b8dcebe941a0f50fb81598f3ede2f0e213e2ba

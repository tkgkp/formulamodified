:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_wprb.fml
:: Utworzony: 03.10.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Obsługa czynności PBA_BPB_WPRB - Wybór programu badań.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Wybór programu badań - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Parametr wejściowy ON_ESC określa sposób działania akcji wyboru programu badań przy braku wyboru.
:: Parametr może przyjmować wartości:
::    DONE     - Czynność zostanie zakończona, a parametry wyjściowe przyjmą wartości puste [DOMYŚLNIE].
::    KEEP     - Czynność zostanie odłożona na listę zadań aby użytkownik mógł do niej wrócić. Oznacza to, że czynności
::               nie da się zakończyć bez wyboru programu oceny.
::    CANCEL   - Jeśli czynność jest pierwszą w procesie to po zakończeniu formuły głównej instancja czynności zostanie
::               usunięta. Jeśli czynność jest kolejną w procesie to otrzyma status oczekująca i pozostanie na liście
::               zadań - czyli tak jak dla KEEP.
::    ERROR    - Czynność jest kończona a proces zatrzymywany z ustawioną flagą błędu.
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy rezygnacji, required=N, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'DONE' || _a ?})"
::
:: Parametry wejściowe: AKTYWNY i WIDOCZNY umożliwiają filtrowanie prezentowanego zakresu programu badań.
::# kind=WE, symbol=AKTYWNY, type=STRING, name=Tylko aktywne programy (T/N), required=N, fml_val="exec('edit_boolean','#edit',_a,'Tylko aktywne programy')"
::# kind=WE, symbol=WIDOCZNY, type=STRING, name=Tylko widoczne programy (T/N), required=N, fml_val="exec('edit_boolean','#edit',_a,'Tylko widoczne programy')"
::
::# kind=WY, symbol=ZO_PROG, type=_ZO_PROG, name=Wskazanie programu badań, required=N
::
_par:=params_get();
_mp:=_par.mp;

{? _mp.pathProc() | _mp.pathTodo()
|| _in:=_par.in;
   _out:=_par.out;

   _args:=obj_new('AKTYWNY','WIDOCZNY');
   _args.AKTYWNY:={? _in.AKTYWNY=~~ | (_in.AKTYWNY<>'T' & _in.AKTYWNY<>'N') || '' || _in.AKTYWNY ?};
   _args.WIDOCZNY:={? _in.WIDOCZNY=~~ | (_in.WIDOCZNY<>'T' & _in.WIDOCZNY<>'N') || '' || _in.WIDOCZNY ?};

   _ret:=exec('select','!pba_bpb_wprb',_args);

   _out.ZO_PROG:=_ret.ZO_PROG;

   {? _ret.ret
   || _mp.save(,_out);
      _mp.done()

   |? _in.ON_ESC='KEEP'
   || _mp.keep()

   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()

   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')

   || _mp.save(,_out);
      _mp.done()
   ?}
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Wybór programu badań - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wybierz program badań'@@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Właściwy wybór programu badań.
::   WE: _a [ARRAY] - Tablica z parametrami:
::          AKTYWNY  [STRING] - Prezentować tylko programy aktywne? [T/N]
::          WIDOCZNY [STRING] - Prezentować tylko programy widoczne? [T/N]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_args:=_a;

_ret:=obj_new('ret','ZO_PROG');

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROG.f_set('NAZWA',,
   '"ZO_PROG".FIRMA=:_a and "ZO_PROG".TYP=\':_b\''+
   {? _args.AKTYWNY=''
   || ''
   || ' and "ZO_PROG".AKTYWNY=\'%1\''@ [_args.AKTYWNY]
   ?}+
   {? _args.WIDOCZNY=''
   || ''
   || ' and "ZO_PROG".WIDOCZNY=\'%1\''@ [_args.WIDOCZNY]
   ?},
   exec('ref_firma','ustawienia'),ZZ_POM.MODUL
);
ZO_PROG.win_sel('WYBA');

{? _ret.ret:=ZO_PROG.select()
|| _ret.ZO_PROG:=ZO_PROG.ref()
?};

ZO_PROG.cntx_pop();

_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 439745684177c32542d5aef70ea02cf9e2c88268bfc54add49a440adbf5d66efe1693e15568fb6ee58f7195a80663cadbec738911fcd8e3906d4d6c131b2349d29aa6569b8ae4634885ec37dc7a649db3a351a6f11dd4dda44645ccce94a24666138c208a0fb54d6cafcb65060b52a4d76a806df1f4810a8f66ae120bea3ffc1

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_wjba.fml
:: Utworzony: 23.08.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Formuły czynności PBA_BPB_WJBA - Wybór badania ankietowego.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Wybór badania ankietowego - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Parametr wejściowy ON_ESC określa sposób działania akcji wyboru badania ankietowego przy braku wyboru.
:: Parametr może przyjmować wartości:
::    DONE     - Czynność zostanie zakończona, a parametry wyjściowe przyjmą wartości puste [DOMYŚLNIE].
::    KEEP     - Czynność zostanie odłożona na listę zadań aby użytkownik mógł do niej wrócić. Oznacza to, że czynności
::               nie da się zakończyć bez wyboru badania ankietowego.
::    CANCEL   - Jeśli czynność jest pierwszą w procesie to po zakończeniu formuły głównej instancja czynności zostanie
::               usunięta. Jeśli czynność jest kolejną w procesie to otrzyma status oczekująca i pozostanie na liście
::               zadań - czyli tak jak dla KEEP.
::    ERROR    - Czynność jest kończona a proces zatrzymywany z ustawioną flagą błędu.
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy rezygnacji, required=N, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'DONE' || _a ?})"
::
::# kind=WY, symbol=ZA_INST, type=_ZA_INST, name=Wskazanie badania ankietowego, required=N
::
_par:=params_get();
_mp:=_par.mp;

{? _mp.pathProc() | _mp.pathTodo()
|| _in:=_par.in;
   _out:=_par.out;

   _ret:=exec('select','!pba_bpb_wjba');

   {? _ret.ret
   || _out.ZA_INST:=_ret.ZA_INST;
      _mp.save(,_out);
      _mp.done()

   |? _in.ON_ESC='KEEP'
   || _mp.keep()

   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()

   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')

   || _mp.save(,_out);
      _mp.done()
   ?}
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Wybór badania ankietowego - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wybierz badanie ankietowe'@@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Właściwy wybór badania ankietowego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new('ret','ZA_INST');

ZA_INST.cntx_psh();
ZA_INST.index('DATA');
ZA_INST.prefix();
ZA_INST.win_sel('WYB');

{? _ret.ret:=ZA_INST.select()
|| _ret.ZA_INST:=ZA_INST.ref()
?};

ZA_INST.cntx_pop();

_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 9e6871eebde5a6540cceb58260b5571f5f4c56550f81099468883d62c521d1d3bfad8a236df3a1443c75120892cbaa9ae6268d5da8238e50a0594d4027933d194e2972f645f7daefa241fb7886e9c6ed63c86ab7ebbbb5f270d5a1bc7dd7c6585ab7add2932d31f704747a68aed49e96bf1924361a0e229917e9678f0027996c

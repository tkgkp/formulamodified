:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_mcr_wmro.fml
:: Utworzony: 03.10.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_MCR_WMRO - Wybór miesięcy rozliczeniowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Wybór miesięcy rozliczeniowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy braku wyboru, required=N, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'KEEP' || _a ?})"
::# kind=WE, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=N
::# kind=WE, symbol=STATUS, type=STRING, name=Status zamknięcia rozliczenia, required=N, fml_val="_radio:=exec('radio_choice','prc','R'); exec('edit_radiobutton','#edit',1,'Status miesiąca',_radio.txt,_radio.var)"
::# kind=WE, symbol=S_PLAN, type=STRING, name=Status blokady planowania, required=N, fml_val="_radio:=exec('radio_choice','prc','P'); exec('edit_radiobutton','#edit',1,'Status planowania',_radio.txt,_radio.var)"
::# kind=WY, symbol=A_OKRM, type=_A_OKRM, name=Wskazanie na miesiąc rozliczeniowy, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status zamknięcia rozliczenia, required=N
::# kind=WY, symbol=S_PLAN, type=STRING, name=Status blokady planowania, required=N
::
_par:=params_get();
params_set(_par);
_mp:=_par.mp;

{? _mp.pathProc() | _mp.pathTodo()
|| _in:=_par.in;
   _out:=_par.out;
:  Wybór miesiąca rozliczeniowego.
   exec('select','!prc_mcr_wmro',_in,_out);
   {? _out.A_OKRM
   || _mp.save(,_out);
      _mp.done()
   |? _in.ON_ESC='KEEP' | _in.ON_ESC=~~
   || _mp.keep()
   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()
   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')
   || _mp.save(,_out);
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Wybór miesięcy rozliczeniowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wybierz miesiąc rozliczeniowy'@@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Wybór okresu rozliczeniowego.
::   WE: _a - parametry wejściowe czynności
::       _b - parametry wyjściowe czynności
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a.A_OKR<>~~
|| A_OKR.cntx_psh();
   A_OKR.clear();
   {? A_OKR.seek(_a.A_OKR,,1)
   || _od:=A_OKR.OD;
      _do:=A_OKR.DO
   ?};
   A_OKR.cntx_pop()
|| _od:=_do:=date(0,0,0)
?};

_b.A_OKRM:=null();
_b.STATUS:=_b.S_PLAN:='';
_ref:=A_OKRM.ref();
_where:='';
{? #_od & #_do
|| _where+='((A_OKRM.R=:_a and A_OKRM.M>=:_b and A_OKRM.M<=:_c) or (A_OKRM.R=:_d and A_OKRM.M>=:_e and A_OKRM.M<=:_f))'
?};
{? _a.STATUS<>~~
|| {? +_where || _where+=' and ' ?};
   _where+='A_OKRM.S=\':_g\''
?};
{? _a.S_PLAN<>~~
|| {? +_where || _where+=' and ' ?};
   _where+='A_OKRM.S_PLAN=\':_h\''
?};

A_OKRM.cntx_psh();
A_OKRM.index('A_OKRMR');
A_OKRM.clear();
A_OKRM.win_sel('SLO');
{? A_OKRM.f_active() || A_OKRM.f_clear() ?};
A_OKRM.f_set(
   'R,M',,
   _where,
   _od~1,_od~2,{? _od~1=_do~1 || _od~2 || 12 ?},_do~1,{? _od~1=_do~1 || _do~2 || 1 ?},_do~2,_a.STATUS,_a.S_PLAN
);
A_OKRM.f_find(date()~1,date()~2);
{? A_OKRM.select(,1,6)
|| _b.A_OKRM:=A_OKRM.ref();
   _b.STATUS:=A_OKRM.S;
   _b.S_PLAN:=A_OKRM.S_PLAN
?};
{? A_OKRM.f_active() || A_OKRM.f_clear() ?};
A_OKRM.cntx_pop();
A_OKRM.seek(_ref,,1);
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 9f2a6616140e795bffac197b286fbe92a5637262cfd0bf0019420b6825a90ae0984e3af24a723bbe6ffdaa9df1322afccbec64a978263e318ffb54db4e5aec277bfc6460ae372f989d82b2ec076f4040b73cff611fa92fce0f4a18810d234308efc3d22d0244f5a6ae1df53bfdd0a4f7e243c4c3ad5916deef50c30698bc1551

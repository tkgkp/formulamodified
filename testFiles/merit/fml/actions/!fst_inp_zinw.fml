:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fst_inp_zinw.fml
:: Utworzony: 02.03.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_INP_ZINW - Przegląd - inwentaryzacje śr.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przegląd - inwentaryzacje śr. - główna formuła czynności FST_INP_ZINW.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
SRD.selInw()


\srxi_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowej inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
exec('odd_filtr','fst');
SRXI.win_edit('RED');
SRXI.blank();
SRXI.NR:=exec('get_nr','!fst_inp_zinw');
SRXI.DW:=date();
{? SRXI.edit("exec('chk_srxi','!fst_inp_zinw','ADD')")
|| SRXI.add()
?}


\chk_srxi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja poprawności wypełnienia okienka
::   WE: _a - Tryb sprawdzania 'ADD' - podczas dodawania, 'EDIT' - podczas poprawiania
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
_wy:=__CHK.record(SRXI,,'NR','DW','DS','OKRO_F');
{? _wy='' & SRXI.NR<0
|| FUN.emsg('Numer inwentaryzacji powinien być większy od zera.'@);
   _wy:='NR'
?};

{? _wy=''
|| _pocz:=exec('first_rok_pocz','fst');
   {? _pocz<>date(0,0,0)
   || {? SRXI.DW<_pocz
      || FUN.emsg('Data wprowadzenia inwentaryzacji (%1) jest wcześniejsza\nniż początek '
                  'pierwszego okresu obszaru FST w systemie (%2).'@[$SRXI.DW,$_pocz]);
         _wy:='DW'
      ?}
   ?}
?};

{? _wy=''
|| {? SRXI.DS<SRXI.OKRO_F().POCZ | SRXI.DS>SRXI.OKRO_F().KON
   || FUN.emsg('Data Stan na dzień poza zakresem wskazanym w polu Dane z okresu.'@);
      _wy:='DS'
   ?}
?};

{? _wy='' & SRXI.DZ<>date(0,0,0) & SRXI.DZ<SRXI.DW
|| FUN.emsg('Data wykonania nie powinna być wcześniejsza od daty wprowadzenia.'@);
   {? SRXI.win_edit('?')='RED' || _wy:='DW' || _wy:='DZ' ?}
?};

{? _wy=''
|| {? _a='EDIT' || _ref:=SRXI.ref() || _ref:=null ?};
   _nr:=SRXI.NR;
   SRXI.cntx_psh();
   SRXI.index('NUMER');
   SRXI.prefix(_nr);
   {? SRXI.first() & (_a='ADD' | (_a='EDIT' & SRXI.ref()<>_ref))
   || FUN.emsg('Inwentaryzacja o podanym numerze już istnieje.'@);
      _wy:='NR'
   ?};
   SRXI.cntx_pop()
?};
_wy


\get_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła zwraca kolejny wolny numer w ramach firmy
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
SRXI.cntx_psh();
SRXI.index('NUMER');
{? SRXI.last() || _wy:=SRXI.NR+1 || _wy:=1 ?};
SRXI.cntx_pop();
_wy


\srxi_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja danych inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.info('Nie można modyfikować zamkniętej inwentaryzacji.'@);
   return()
?};

_edit:=1;
SRXA.cntx_psh();
SRXA.index('SRXI');
SRXA.prefix(SRXI.ref());
{? SRXA.first() || _edit:=0 ?};
SRXA.cntx_pop();

{? _edit=0
|| FUN.emsg('Inwentaryzacja zawiera dane, zmiana parametrów niemożliwa.'@);
   return()
?};

{? SRXI.r_lock(1,1,1)
|| exec('odd_filtr','fst');
   SRXI.win_edit('RED');
   {? SRXI.edit("exec('chk_srxi','!fst_inp_zinw','EDIT')")
   || SRXI.put()
   ?};
   SRXI.r_unlock()
|| FUN.emsg('Inwentaryzacja zablokowana przez innego użytkownika.'@)
?}


\srxi_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.info('Nie można modyfikować zamkniętej inwentaryzacji.'@);
   return()
?};

SRXA.cntx_psh();
SRXA.index('SRXI');
SRXA.prefix(SRXI.ref());
{? SRXA.first()
|| FUN.emsg('Bieżąca inwentaryzacja zawiera dane (arkusze inwentaryzacyjne) i nie można jej usunąć.'@);
   SRXA.cntx_pop();
   return()
?};
SRXA.cntx_pop();

SRXK.cntx_psh();
SRXK.index('SRXI');
SRXK.prefix(SRXI.ref());
{? SRXK.first()
|| FUN.emsg('Bieżąca inwentaryzacja zawiera dane (komisje inwentaryzacyjne) i nie można jej usunąć.'@);
   SRXK.cntx_pop();
   return()
?};
SRXK.cntx_pop();

{? SRXI.count()>0
|| FUN.emsg('Bieżąca inwentaryzacja jest wykorzystywana w systemie i nie można jej usunąć.'@);
   return()
|| {? SRXI.r_lock(1,1,1)
   || {? FUN.ask('Usunąć inwentaryzację?'@)
      || SRXI.r_unlock();
         SRXI.del(1,1)
      || SRXI.r_unlock()
      ?}
   || FUN.emsg('Inwentaryzacja zablokowana przez innego użytkownika.'@)
   ?}
?}


\srxi_wykonanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wprowadzanie daty wykonania inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.info('Nie można modyfikować zamkniętej inwentaryzacji.'@);
   return()
?};
_win:=SRXI.win_edit('?');
SRXI.win_edit('RED_W');
{? SRXI.edit("exec('chk_srxi','!fst_inp_zinw','EDIT')")
|| SRXI.put()
?};
SRXI.win_edit(_win)


\srxi_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zamykanie inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.info('Bieżąca inwentaryzacja jest już zamknięta.'@);
   return()
?};
{? SRXI.DZ=date(0,0,0)
|| FUN.emsg('Nie wypełniono daty wykonania inwentaryzacji.'@);
   return()
?};

SRXD.cntx_psh();
SRXD.index('SRXIDOK');
SRXD.prefix(SRXI.ref());
_dalej:=1;
{? SRXD.first()
|| {! |?
      {? (SRXD.STATUS='Z' | SRXD.STATUS='P') & SRXD.SRDO=null
      || _dalej:=0
      ?};
      _dalej & SRXD.next()
   !}
?};
SRXD.cntx_pop();
{? _dalej=0
|| FUN.emsg('Nie wygenerowano wszystkich dokumentów poinwentaryzacyjnych, zamknięcie inwentaryzacji niemożliwe.'@);
   return()
?};

_find:=0;
_open:=0;
SRXA.cntx_psh();
SRXA.index('SRXI');
SRXA.prefix(SRXI.ref());
{? SRXA.first()
|| _find:=1;
   {! |?
      {? SRXA.STATUS='N' || _open:=1 ?};
      _open=0 & SRXA.next()
   !}
?};
SRXA.cntx_pop();

:: pusta inwentaryzacja
{? _find=0
|| {? ~FUN.ask('Inwentaryzacja nie zawiera danych (arkuszy inwentaryzacyjnych). Kontynuować?'@)
   || return()
   ?}
?};
:: niezamknięte arkusze inwentaryzacyjne
{? _open=1
|| FUN.emsg('Inwentaryzacja zawiera niezamknięte arkusze. Zamknięcie niemożliwe.'@);
   return()
?};

{? SRXI.r_lock(1)
|| {? FUN.ask('Zamknąć bieżącą inwentaryzację?'@)
   || SRXI.STATUS:='T';
      SRXI.put()
   ?};
   SRXI.r_unlock()
?}


\srxi_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Otwieranie inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='N'
|| FUN.info('Bieżąca inwentaryzacja jest już otwarta.'@);
   return()
?};

{? SRXI.r_lock(1)
|| {? FUN.ask('Otworzyć bieżącą inwentaryzację?'@)
   || SRXI.STATUS:='N';
      SRXI.put()
   ?};
   SRXI.r_unlock()
?}


\ae_srxi_okro_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła po redakcji pola SRXI.OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.OKRO_F<>null
|| SRXI.ROK_F:=SRXI.OKRO_F().ROK;
   {? SRXI.DS=date(0,0,0)
   || SRXI.DS:=SRXI.OKRO_F().KON
   ?}
|| SRXI.ROK_F:=null
?};
1


\create_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Tworzenie okna grupowego do przeglądania inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
_win:=SRXA.grp_make('Arkusze spisowe'@,"",'_win_srxa');
SRXA.grp_sel(_win,SRXA,'WER','Arkusze spisowe'@,"exec('stany','!fst_inp_zinw')",,,,"","",,,'maximized');
SRXA.tab_splt(_win,,'horizontal','srxd_poz');
SRXA.grp_sel(_win,SRXD,'WER',,"",,,,,,,,'maximized_with_title');
_form:="SRXN.index('AUTO');
        SRXN.prefix(SRXI.ref());
        {? ~SRXN.ref() || SRXN.first() ?};
        SRXP.index('SRXP');
        {? SRXN.size()>0
        || SRXP.prefix(SRXN.ref());
           SRXP.btn_sopt('WER','btn_srxp_imp','state=normal');
           SRXP.btn_sopt('WER','btn_srxp_add','state=normal');
           SRXP.actions_grayed('WER','')
        || SRXP.prefix(null);
           SRXP.btn_sopt('WER','btn_srxp_imp','state=grayed');
           SRXP.btn_sopt('WER','btn_srxp_add','state=grayed');
           SRXP.actions_grayed('WER','DI:DI')
        ?};
        grp_disp(SRXP,'WER')
       ";
_form2:="SRXP.index('SRXP');
         {? SRXN.size()>0
         || SRXP.prefix(SRXN.ref());
            SRXP.btn_sopt('WER','btn_srxp_imp','state=normal');
            SRXP.btn_sopt('WER','btn_srxp_add','state=normal');
            SRXP.actions_grayed('WER','')
         || SRXP.prefix(null);
            SRXP.btn_sopt('WER','btn_srxp_imp','state=grayed');
            SRXP.btn_sopt('WER','btn_srxp_add','state=grayed');
            SRXP.actions_grayed('WER','DI:DI')
         ?};
         grp_disp(SRXP,'WER')
       ";
SRXA.grp_sel(_win,SRXN,'WER','Arkusze z natury'@,_form2,,,,_form,"",,,'maximized');
SRXA.tab_splt(_win,,'horizontal','srxp_poz',12);
_form:="{? SRXN.size()>0
        || SRXP.btn_sopt('WER','btn_srxp_imp','state=normal');
           SRXP.btn_sopt('WER','btn_srxp_add','state=normal');
           SRXP.actions_grayed('WER','')
        || SRXP.btn_sopt('WER','btn_srxp_imp','state=grayed');
           SRXP.btn_sopt('WER','btn_srxp_add','state=grayed');
           SRXP.actions_grayed('WER','DI:DI')
        ?}
       ";
SRXA.grp_sel(_win,SRXP,'WER',,_form,,,,,,,,'maximized_with_title');
_form:="SRDO.index('SRXI');
        SRDO.prefix(SRXI.ref())
       ";
SRXA.grp_sel(_win,SRDO,'INW','Dokumenty poinwentaryzacyjne'@,"",,,,_form,"",,,'maximized');

_form:="SRXK.index('SRXI');
        SRXK.prefix(SRXI.ref());
        SRXL.index('SRXL');
        {? SRXK.size()>0 || SRXL.prefix(SRXK.ref()) || SRXL.prefix(null) ?};
        grp_disp(SRXL,'WER')";
_forml:="SRXL.index('SRXL');
       {? SRXK.size()>0 || SRXL.prefix(SRXK.ref()) || SRXL.prefix(null) ?};
       SRXL.first();
        grp_disp(SRXL,'WER')
       ";
SRXA.grp_sel(_win,SRXK,'WER','Komisje'@,_forml,,,,_form,"",,,'maximized');
SRXA.tab_splt(_win,,'horizontal','srxl_skl',12);
SRXA.grp_sel(_win,SRXL,'WER',,_form,,,,,,,,'maximized_with_title');

SRXA.win_sel(_win)


\arkusze
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła filtruje listę arkuszy inwentaryzacyjnych bieżącej inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
SRXA.win_edit('RED');
SRXA.index('SRXI');
SRXA.prefix(SRXI.ref());
SRXA.first()


\stany
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla zawartość arkuszy inwentaryzacyjnych bieżącej inwentaryzacji
::----------------------------------------------------------------------------------------------------------------------
SRXD.win_edit('RED');
SRXD.index('SRXD');
{? SRXA.size()>0
|| SRXD.prefix(SRXA.ref());
   SRXD.btn_sopt('WER','btn_srxd_nadm','state=normal');
   SRXD.actions_grayed('WER','')
|| SRXD.prefix(null);
   SRXD.btn_sopt('WER','btn_srxd_nadm','state=grayed');
   SRXD.actions_grayed('WER','N:N')
?};
grp_disp(SRXD,'WER')


\srxa_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetlanie arkuszy i pozycji
::----------------------------------------------------------------------------------------------------------------------
exec('create_win','!fst_inp_zinw');
exec('arkusze','!fst_inp_zinw');
_txt:=':  '+$SRXI.OKRO_F().KON;
SRXD.hdr_sel();
SRXD.hdr_sel(_txt);
SRXA.select()


\create_win_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Tworzy okno gropowe do obsługi komisji inwentaryzacyjnych
::----------------------------------------------------------------------------------------------------------------------
_win:=SRXK.grp_make('Komisje inwentaryzacyjne'@,"",'_srxk_sel');
SRXK.grp_sel(_win,SRXK,'WER',,"exec('srxl_rfresh','!fst_inp_zinw')",,,10,,,,,'maximized');
SRXK.grp_splt(_win,,'horizontal','sklad');
SRXK.grp_sel(_win,SRXL,'WER',,,,,,,,,,'maximized_with_title');
SRXK.win_sel(_win)


\srxl_rfresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła odświeża dolne okno z listą członków komisji
::----------------------------------------------------------------------------------------------------------------------
SRXL.index('SRXL');
{? SRXK.size()>0
|| SRXL.prefix(SRXK.ref());
   SRXL.btn_sopt('WER','btn_srxl_add','state=normal')
|| SRXL.prefix(null);
   SRXL.btn_sopt('WER','btn_srxl_add','state=grayed')
?};
grp_disp(SRXL,'WER')


\srxk_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowej komisji inwentaryzacyjnej
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikacje nie są możliwe.'@);
   return()
?};
SRXK.blank();
SRXK.RODZAJ:='I';
{? SRXK.edit("exec('chk_srxk','!fst_inp_zinw','ADD')")
|| SRXK.add()
?}


\srxk_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja komisji inwentaryzacyjnej
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikacje nie są możliwe.'@);
   return()
?};
{? exec('srxk_arch','!fst_inp_zinw',SRXK.ref())
|| FUN.emsg('Komisja związana z zamkniętym arkuszem, modyfikacje niemożliwe.'@);
   return()
?};
{? SRXK.edit("exec('chk_srxk','!fst_inp_zinw','EDIT')")
|| SRXK.put()
?}


\srxk_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie komisji inwentaryzacyjnej
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikacje nie są możliwe.'@);
   return()
?};
{? exec('srxk_arch','!fst_inp_zinw',SRXK.ref())
|| FUN.emsg('Komisja związana z zamkniętym arkuszem, modyfikacje niemożliwe.'@);
   return()
?};
{? SRXK.count()>0
|| FUN.emsg('Komisja jest wykorzystywana w systemie (lub skład komisji nie jest pusty) i nie można jej usunąć.'@);
   return()
?};
{? FUN.ask('Usunąć bieżącą komisję?'@) || SRXK.del() ?}


\chk_srxk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła kontrolna dla okna edycji komisji
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRXK,,'KOD');
{? _wy=''
|| {? __CHK.index(SRXK,_a='EDIT')<>''
   || _wy:='KOD'
   ?}
?};
_wy


\srxl_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła dołącza nową osobę do składu komisji
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikacje nie są możliwe.'@);
   return()
?};
{? exec('srxk_arch','!fst_inp_zinw',SRXK.ref())
|| FUN.emsg('Komisja związana z zamkniętym arkuszem, modyfikacje niemożliwe.'@);
   return()
?};
SRXL.blank();
SRXL.SRXK:=SRXK.ref();
SRXL.P:='N';
{? SRXL.edit("exec('chk_srxl','!fst_inp_zinw','ADD')")
|| SRXL.add()
?}


\srxl_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła poprawia dane osoby należącej składu komisji
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikacje nie są możliwe.'@);
   return()
?};
{? exec('srxk_arch','!fst_inp_zinw',SRXK.ref())
|| FUN.emsg('Komisja związana z zamkniętym arkuszem, modyfikacje niemożliwe.'@);
   return()
?};
{? SRXL.edit("exec('chk_srxl','!fst_inp_zinw','EDIT')")
|| SRXL.put()
?}


\srxl_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła usuwa osobę ze składu komisji
::----------------------------------------------------------------------------------------------------------------------
{? SRXI.STATUS='T'
|| FUN.emsg('Bieżąca inwentaryzacja jest zamknięta, modyfikacje nie są możliwe.'@);
   return()
?};
{? exec('srxk_arch','!fst_inp_zinw',SRXK.ref())
|| FUN.emsg('Komisja związana z zamkniętym arkuszem, modyfikacje niemożliwe.'@);
   return()
?};
{? FUN.ask('Usunąć bieżący zapis?'@)
|| SRXL.del()
?}


\chk_srxl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła kontrolna dla okna edycji członka(-ini) komisji
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRXL,,'OSOBA');
{? _wy=''
|| {? __CHK.index(SRXL,_a='EDIT')<>''
   || _wy:='OSOBA'
   ?}
?};
_wy


\srxk_arch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: WEryfikacja czy komisja jest przypisana choć do 1 arkusza z natury którey został zamkniętych
::   WE: SRXK.ref()
::----------------------------------------------------------------------------------------------------------------------
SRXN.cntx_psh();
SRXN.index('KOMISJA');
SRXN.prefix(_a,'T');
{? SRXN.first() || _wy:=1 || _wy:=0 ?};
SRXN.cntx_pop();
_wy


\srxl_przew
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Obsługa wyboru przewodniczącego
::----------------------------------------------------------------------------------------------------------------------
{? exec('srxk_arch','!fst_inp_zinw',SRXK.ref())
|| FUN.emsg('Komisja związana z zamkniętym arkuszem, modyfikacje niemożliwe.'@);
   return()
?};
_ref:=SRXL.ref();
SRXL.cntx_psh();
SRXL.index('P');
SRXL.prefix(SRXL.SRXK,'T');
{? ~SRXL.first()
|| {? FUN.ask('Przypisać funkcję przewodniczącego komisji?'@)
   || SRXL.P:='T';
      SRXL.clear();
      SRXL.put()
   ?}
|? SRXL.ref=_ref
|| {? FUN.ask('Wskazana osoba już przewodniczy komisji.\n'
             +'Zmienić na członka komisji?'@)
   || SRXL.P:='N';
      SRXL.clear();
      SRXL.put()
   ?}
|| {? FUN.ask('Inna osoba już przewodniczy komisji.\n'
             +'Zmienić osobę przewodniczącą komisji?'@)
   || SRXL.P:='N';
      SRXL.clear();
      SRXL.put();
      {? SRXL.seek(_ref)
      || SRXL.P:='T';
         SRXL.put()
      ?}
   ?}
?};
SRXL.cntx_pop();
1


\srxa_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wydruki arkuszy inwnetaryzacyjnych środków
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FST_INP_ZINW','fst_inp*')


\bl_srxk_srxi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na blank pola SRXK.SRXI
::----------------------------------------------------------------------------------------------------------------------
SRXI.ref()

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:13 00034a2c6cd7e8a76a7506f3c46c7d6ec3ff8c17c1c41252f1775a594114d80f32fb7813514b0ea8f9b8568748fb853801ae76d391c72302e1b9fa63527030a8d06cc9b1fea351fa20acf998da00b5a0632183181ded127fa936a6d715a19e32e0c49dd73c179518eb06b11415b7db245307f8d796abcc4ece2a0c1d03930c1d

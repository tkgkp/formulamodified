:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_int_dint.fml
:: Utworzony: 18.01.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Wysłanie deklaracji INTRASTAT do UC
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=IST,       type=_IST,     name=Deklaracja INTRASTAT, required=T, keyref=T
::# kind=WY,   symbol=FILE,      type=STRING,   name=Deklaracja INTRASTAT, required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init','lmg');

:: Uruchomiona akcja
_akcja:=_mp.akcja();

:: Uruchomiona automatycznie
_auto:=_mp.isAutoRun();

{? _mp.pathTodo()
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('IST',_in)=type_of(null()) & _in.IST
   || 1
   || _mp.error('Brak parametru wyjściowego INT.')
   ?}
?};

{? _akcja='Xml'
   | _mp.pathTodo()
   | _mp.pathArea() & _akcja='' & _auto
:: Obsługa Xml - dołącz w oknie obszaru i start procesu z pulpitu
||
   IST.cntx_psh();
   IST.prefix();
   {? IST.seek(_in.IST)
   ||
::    Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      _xml:=exec('xml','intrastat',_auto);

      {? IST.Z
::    Utworzono xml
      || {? _xml=1 || _mp.done() |? ~_mp.isMicro() || _mp.keep() ?}
::    Nie utworzono xml
      || _mp.cancel()
      ?}
   ?};
   IST.cntx_pop()

|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('IST',_in)=type_of(null()) & _in.IST
|| 'Utwórz deklarację Intrastat przywóz xml: %1'@@[exec('record','#to_string',_in.IST)]
|| ''
?}


\ist_xml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Deklaracja INTRASTAT - Xml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LMG_INT_WXML';
_params.UIDREF:=IST.uidref();
_params.AKCJA:='Xml';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'IST',IST.ref());

exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:13 34cb0a7ead529b134cacb5bd8e13df55f266a8ad95207f62d80aec2585b20fb6c4aaf84b95f031b68ac4fdfb13702c082c46bde9d559c7d7fd7601a415b5cc87042bcc7a34ce494fbd4ded659ec08582c22780ef167233bb49ec69e559f91b9454ebb81769eb9ccc1b4fddd9e7a6b2ab672db5a2c60f5411d68902e114bff0be

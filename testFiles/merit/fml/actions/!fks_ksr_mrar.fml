:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_mrar.fml [17.00]
:: Utworzony: 2016.01.19
:: Autor: AMK
::======================================================================================================================
:: Zawartosc: Formuły czynności FKS_KSR_MRAR - Aktualizacja rozrachunków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności FKS_KSR_MRAR - Aktualizacja rozrachunków
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WE, symbol=AN_SYM, type=STRING, name=Prefiks konta analitycznego, required=N
::# kind=WY, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
_args:=params_get();
_we:=_args.in;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_wew:=_args.int;
_wy:=_args.out;
params_set(params_get());
_ar:=SSTALE.AR; _ao:=SSTALE.AO;
:: obszar roboczy lub czynność startowa
{? _akcja='aktroz' | _mp.pathProc()
|| exec('ustaw_bo','zam_okr_rok',SSTALE.AR);
   OKRESY.ZO();
   {? OKRESY.ZO
   || _wy.OKRO_F:=OKRESY.ZO; _mp.save(,_wy);
      SSTALE.AR:=OKRESY.ZR; SSTALE.AO:=OKRESY.ZO;
      {? SSTALE.AR & SSTALE.AO
      || exec('open_tabele','open_tab');
         params_exec('akt_roz1','!fks_ksr_mrar',1,'')
      ?}
   ?}
|? _mp.pathTodo() & _we.OKRO_F
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKRO_F.seek(_we.OKRO_F)
   || exec('ustaw_bo','zam_okr_rok',OKRO_F.ROK);
      {? OKRESY.ZO
      || _wy.OKRO_F:=OKRESY.ZO; _mp.save(,_wy);
         SSTALE.AR:=OKRESY.ZR; SSTALE.AO:=OKRESY.ZO;
         {? SSTALE.AR & SSTALE.AO
         || exec('open_tabele','open_tab');
            _konto:={? var_pres('AN_SYM',_we) || _we.AN_SYM || '' ?};
            params_exec('akt_roz1','!fks_ksr_mrar',~_mp.isAutoRun(),_konto)
         ?}
      ?}
   ?};
   OKRO_F.cntx_pop()
?};
SSTALE.AR:=_ar; SSTALE.AO:=_ao;
{? SSTALE.AR & SSTALE.AO
|| exec('open_tabele','open_tab')
?};
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Aktualizacja rozrachunków'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('OKRO_F',_we)
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKRO_F.seek(_we.OKRO_F)
   || ROK_F.cntx_psh(); ROK_F.prefix(); OKRO_F.ROK();
      _desc:='Aktualizacja rozrachunków: %1'@@[ROK_F.NAZ];
      ROK_F.cntx_pop()
   ?};
   OKRO_F.cntx_pop()
?};
_desc


\akt_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [$7.60]
:: OPIS: Aktualizacja rozrachunkow - akcja w okienku
::----------------------------------------------------------------------------------------------------------------------
DOK.cntx_psh();
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_KSR_MRAR';
_params.AKCJA:='aktroz';
ROK_F.cntx_psh(); ROK_F.prefix();
OKRO_F.cntx_psh(); OKRO_F.prefix();
SSTALE.AO();
OKRESY.ZO:=OKRO_F.ref();
OKRESY.ZR:=OKRO_F.ROK;
_params.UIDREF:=OKRO_F.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',OKRO_F.ref());
_params.PROC_START:='T';
exec('mp_run','#b__box',_params);
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
DOK.cntx_pop()


\akt_roz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [$7.60]
:: OPIS: Aktualizacja rozrachunkow
::   WE: _a - 0/1 - czy pokazywać dialogi
::       _b - prefiks konta analitycznego
::  OLD: \akt_roz/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); _ar_naz:=SSTALE.AR().NAZ;
{? ~ROK_F.prev() | exec('first_rok_obsz','zam_okr_rok','FKS',SSTALE.AR)
|| {? _a
   || FUN.info('Bieżący rok %1 jest pierwszym dla obszaru FKS.'
               '\nOperację aktualizacji rozrachunków można przeprowadzić'
               '\ndla kolejnych lat bilansowych.'@[_ar_naz])
   ?}
|? ROK_F.ZAM='N' |
   ~_a |
   (_a & FUN.ask('Poprzedni rok (%1) jest już zamknięty.\n'
                 'Zaktualizować rozrachunki w roku %2.'@[ROK_F.NAZ,_ar_naz]))
|| _ok:=0;
   _rok_naz:=ROK_F.NAZ;
   SSTALE.AR();
   DOK.cntx_psh();
   OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref);
   {? OKRO_F.first()
   || DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
      DOK.index('REJ'); DOK.prefix(); _ok:=DOK.first()
   ?};
   DOK.cntx_pop();
   {? _ok
   || SSTALE.AR();
      ROZNE.PAR4:=0;
      ROZNE.PREFIX:=_b;
      {? _a
      || _okno:=ROZNE.mk_edit('Aktualizacja rozrachunków w roku %1 na podstawie roku %2'@[ROK_F.NAZ,_rok_naz]);
         ROZNE.win_efld(_okno,,'PREFIX',,,60,,,'Prefiks konta:'@,,,,'F3_button=1');
         _btnok:=ROZNE.win_ebtn(_okno,'text=%1,panel=bottom,align=end'['OK'@],'key:F2');
         _btnan:=ROZNE.win_ebtn(_okno,'text=%1,panel=bottom,align=end'['Anuluj'@],'key:Esc');
         ROZNE.btn_eopt(_okno,_btnok,'tooltip='+exec('help_red_ok','#window','P'));
         ROZNE.btn_eopt(_okno,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
         ROZNE.win_edit(_okno)
      ?};
      {? ~_a | (_a & ROZNE.edit())
      || _k:=ROZNE.PREFIX;
         _p:='Wybrano aktualizację wszystkich rozrachunków w roku '+ROK_F.NAZ
            +' na podstawie roku '+_rok_naz+'. \n Kontynuować?'+8*' ';
         {? +_k>0 |
            +_k=0 & (~_a | (_a & FUN.ask(_p)))
         || {? _w:={? +_k
                   || exec('tran_op','!fks_ksr_mrar',_k,2)
                   || exec('tran_op','!fks_ksr_mrar',,2)
                   ?}
            || {? ROK_F.AKT_ROZ<>'T'
               || ROK_F.AKT_ROZ:='T';
                  {? ROK_F.put()
                  || exec('uzup_akt_roz','okresy',ROK_F.ref);
                     {? _a
                     || FUN.info('Parametr aktualizacji rozrachunków w roku %1 został automatycznie włączony.'@[ROK_F.NAZ])
                     ?}
                  ?}
               ?};
               {? +_k=0
               || _p:='Operacja przenoszenia wszystkich rozrachunków'
               || _p:='Operacja przenoszenia rozrachunków dla prefiksu konta: '''+_k+''''
               ?};
               {? _w=2
               || {? _a || FUN.info(_p+' została wykonana.\nWystąpiły błędy.'@) ?}
               || {? _a || FUN.info(_p+' została wykonana pomyślnie.'@) ?};
                  _mp.done()
               ?}
            |? _a
            || FUN.info('Przeniesienie rozrachunków nie powiodło się.'@)
            ?}
         ?}
      ?}
   |? _a
   || FUN.info('W okresie Bilans Otwarcia nie wprowadzono żadnego dokumentu.'
               '\nNależy wcześniej zaktualizować bilans otwarcia.'@)
   ?}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop()


\tran_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przeniesienie rozrachunkow
::   WE: _a  - prefix konta dla aktualizacji rozrachunkow
::       _b  - miejsce  wywolania
::             (0 - ksiegowanie wszystkich dokumentow
::              1 - ksiegowanie pojedynczego dokumentu
::              2 - aktualizacja rozrachunkow
::              3 - akcja grupowa )
::  OLD: \tran_op/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
pocz:=kon:=_pocz_s:=_kon_s:=date(0,0,0);
{? var_pres('_a')<0 | type_of(_a)<>2 || _a:='' ?};
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
BILANSP.PROK:=BILANSP.NROK:=null;
{? SSTALE.AO().NAZ<>'Bilans otwarcia'
|| FUN.info('Niewłaściwy okres.'@); _ok:=0
|| SSTALE.AO().ROK();
   _dlsynt:=ROK_F.SYNT;
   ROK_F.cntx_psh;
   {? ROK_F.prev || _dlsynt:=ROK_F.SYNT ?};
   ROK_F.cntx_pop;
   BILANSP.NROK:=SSTALE.AR;
   {? OKRO_F.prev() & OKRO_F.NAZ='Bilans zamknięcia'
   || BILANSP.PROK:=OKRO_F.ROK
   || _ok:=0
   ?}
?};
OKRO_F.index('ROK');
{? _ok
|| OKRO_F.prefix(BILANSP.PROK);
   {? OKRO_F.first() & OKRO_F.next() || _pocz_s:=OKRO_F.POCZ ?};
   {? OKRO_F.last() & OKRO_F.prev() || _kon_s:=OKRO_F.KON ?};
   {? (_pocz_s=date(0,0,0)) | (_kon_s=date(0,0,0)) || _ok:=0 ?};
   OKRO_F.prefix(BILANSP.NROK);
   {? OKRO_F.first() & OKRO_F.next() || pocz:=OKRO_F.POCZ ?};
   {? OKRO_F.last() & OKRO_F.prev() || kon:=OKRO_F.KON ?};
   {? (pocz=date(0,0,0)) | (kon=date(0,0,0)) || _ok:=0 ?}
?};
{? _ok
|| {? var_pres('TTMP')>0 || obj_del(TTMP) ?};
   REF_PARN:=tab_tmp(2,'REF','INTEGER','ref',
                       'MASKA','STRING[8]' ,'maska',
                       'ZN','STRING[1]' ,'znacznik');
   TTMP:=tab_tmp(4,'LP','INTEGER','Lp.',
                   'DOKUMENT','STRING[35]','Dokument/Błąd',
                   'KONTO','STRING[35]','Konto poprz.',
                   'OPIS','STRING[80]','Opis' );
   I_T1:=TTMP.ndx_tmp('',0,'LP',,0);
   I_T2:=TTMP.ndx_tmp('',0,'DOKUMENT',,0,'KONTO',,0,'OPIS',,0);
   RMK.use('rmkop_'+BILANSP.PROK().KOD);  RMK.prefix();
   opbezwal:=OP.ndx_tmp('',0,'ODD',,0,'AN',,0,'SYM',,0,'SYM',,0,'SYM_ROK',,0);
   {? RMK.first()
   || _tym_poz:=POZ.ndx_tmp('',1,'DOK','ODD',0,'PKON',,0,'PKON',,0);
      {! |?
         {? {? _a<>'' || ((+_a)+RMK.KONP)=_a || 1 ?}
         || RMK.cntx_psh(); _konto:=RMK.KONP; _odd:=RMK.ODD;
            POZ.cntx_psh();
            POZ.index(_tym_poz); POZ.prefix(_odd,_konto,_konto);
            {? POZ.first || _konto:=POZ.KON ?};
            POZ.cntx_pop();
            _sr:=_nr:=0;
            RMK.KONP:=_konto; _sr:=#RMK.ref();
            RMK.use('rmkop_'+BILANSP.NROK().KOD); RMK.prefix();
            {? ~RMK.find_key(RMK.ODD,RMK.KONP,RMK.SYM,RMK.SYM,RMK.SYM_ROK)
            || RMK.add(); _nr:=#RMK.ref()
            || _nr:=#RMK.ref()
            ?};
            RRMK.use('rozrmk'+BILANSP.NROK().KOD); RRMK.prefix();
            {? _nr>0 & ~RRMK.find_key(RMK.ref())
            || RRMK.cntx_psh();
               RRMK.use('rozrmk'+BILANSP.PROK().KOD); RRMK.prefix();
               {? RMK.seek(_sr,'rmkop_'+BILANSP.PROK().KOD) & (RRMK.prefix(RMK.ref()); RRMK.first())
               || {! |?
                     RRMK.cntx_psh();
                     RRMK.use('rozrmk'+BILANSP.NROK().KOD); RRMK.prefix();
                     {? RMK.seek(_nr,'rmkop_'+BILANSP.NROK().KOD)
                     || RRMK.RMK:=RMK.ref();
                        {? ~RRMK.add(1)
                        || exec('add_ttmp','!fks_ksr_mrar','#rozr. '+RMK.SYM,_konto,'Nie dodano Rozdzielnika Rozliczenia Międzyokresowego Kosztów - '+RRMK.KONTO)
                        ?}
                     ?};
                     RRMK.cntx_pop();
                     RRMK.next()
                  !}
               ?};
               RRMK.cntx_pop()
            ?};
            RMK.cntx_pop()
         ?};
         RMK.next()
      !};
      POZ.ndx_drop()
   ?};
   {? _a<>''
   || exec('clear_op','!fks_ksr_mrar',SSTALE.AR().POCZ_ROK,_a)
   || exec('clear_op','!fks_ksr_mrar',SSTALE.AR().POCZ_ROK)
   ?};
   {? _b =2 & _a <> ''
   || _tt_poz:=sql('select distinct POZ.PKON from POZ '+
                   'join DOK using (POZ.DOK, DOK.REFERENCE) '+
                   'where DOK.ZP=\'T\' and POZ.PKON<> \'\' and POZ.KON like \':_a%\' ',_a);
      {? _tt_poz.first
      || {! |?
            exec('poz','!fks_ksr_mrar',_tt_poz.PKON,_b);
            _tt_poz.next
         !}
      ?}
   || exec('poz','!fks_ksr_mrar',_a,_b)
   ?};

:: sprawdzenie czy istnieja rozrachunki nie przeniesione do nowego roku
   exec('old2new','!fks_ksr_mrar',pocz,kon,2,_a);
   exec('open_tabele','open_tab');
   {? REF_PARN.first
   || {! |?
         PAR_NAG.cntx_psh();
         PAR_NAG.prefix();
         {? PAR_NAG.seek(REF_PARN.REF,REF_PARN.MASKA)
         || {? REF_PARN.ZN='T'
            || ile_zak:=0; kom_par:=''; rozr_akt:=1;
               exec('par_akc','rozrach',-1);
               &ile_zak; &kom_par; &rozr_akt
            || PAR_NAG.ZN:=REF_PARN.ZN;
               PAR_NAG.put()
            ?}
         ?};
         PAR_NAG.cntx_pop();
         REF_PARN.next()
      !}
   ?};
:: usuniecie RMK nie powiazanych z OP
   RMK.cntx_psh(); RRMK.cntx_psh(); OP.cntx_psh();
   RMK.index('NAZ'); RMK.prefix();OP.index('KON'); OP.prefix(FINFO.NAROD);
   RRMK.index('RRMKRK'); RRMK.prefix();
   {? RMK.first()
   || {! |? _jest:=OP.find_key(RMK.KONP,RMK.SYM,RMK.SYM,RMK.SYM_ROK,) & (-OP.TYP='rmk' | -OP.TYP='rmp');
            {? ~_jest
            || RRMK.prefix(RMK.ref());
               {? RRMK.first() || {! |? RRMK.del() !} ?};
               RMK.del()
            || RMK.next()
            ?}
      !}
   ?};
   RMK.cntx_pop(); RRMK.cntx_pop(); OP.cntx_pop();
   {? TTMP.first()
   || _OKT:=TTMP.mk_sel('Błędy powstałe przy przenoszeniu (aktualizacji) rozrachunków'@,,,'ttmp_wer1');
      TTMP.win_fld(_OKT,,'LP',,,7,,0);
      TTMP.win_fld(_OKT,,'DOKUMENT',,,35,,0);
      TTMP.win_fld(_OKT,,'KONTO',,,35,,0);
      TTMP.win_fld(_OKT,,'OPIS',,,35,,0);
      TTMP.win_act(_OKT,0,'Formuła','Wydruk'@@,,'Wydruk błędów przenoszenia (aktualizacji) rozrachunków'@,,
                   "VAR_DEL.delete('g_tt_w'); g_tt_w:=obj_new(TTMP.fld_num());
                    g_tt_w[1]:=7; g_tt_w[2]:=30; g_tt_w[3]:=35; g_tt_w[4]:=35;
                    PAR_WYDR.TTMPAKR:='TTMP';
                    PAR_WYDR.TITLE:='BŁĘDY PO PRZENOSZENIU (AKTUALIZACJI) rozrachunków';
                    exec('rep_exec','#b_report','FKS_KSR_MRAR','wsp_tt',,,,,,,,'W'); 0",1,,,,'W');
      TTMP.win_sel(_OKT);
      TTMP.index(I_T1);_ok:=2;
      TTMP.select()
   ?};
   &opbezwal; OP.ndx_drop(); &I_T1; &I_T2; obj_del(TTMP); obj_del(REF_PARN); &pocz; &kon
?};
_ok


\clear_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Czyszczenie rozrachunków z poprzednich lat
::   WE: _a - data poczatku bieżącego roku
::  OLD: \clear_op/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
OP.index('KON_O'); OP.prefix();
STANY.index('STAN'); STANY.prefix();
ZAP_OP.index('OP');
{? OP.first()
|| ZAP_OP.trig_off('del','');
   {!
   |? {? {? _=2 || ((+_b)+OP.AN)=_b || 1 ?}
      || echo('wycofywanie zapisów dla konta %1'@[OP.AN]);
         ZAP_OP.prefix(OP.ref());  STANY.prefix();
         {? ZAP_OP.first()
         || {!
            |? {? ZAP_OP.OKRO().ROK().POCZ_ROK<_a
               || sys_util:=''; exec('wycofaj','rozrach'); &sys_util;
                  {? ZAP_OP.get() & OP.get() &
                     (ZAP_OP.PAR_POZ=null | (ZAP_OP.PAR_POZ<>null & ref_name(ZAP_OP.PAR_POZ)<>PAR_POZ.name()))
                  || {? STANY.find_key(OP.ref(),ZAP_OP.OKRO)
                     || STANY.WN-=ZAP_OP.WN; STANY.MA-=ZAP_OP.MA; STANY.put()
                     ?};
                     OP.WN-=ZAP_OP.WN; OP.MA-=ZAP_OP.MA;
                     OP.STAN:={? OP.WN$2=OP.MA$2 || 'R' || 'N' ?}; OP.put();
                     ZAP_OP.del()
                  || ZAP_OP.next()
                  ?}
               || ZAP_OP.next()
               ?}
            !};
            {? ~ZAP_OP.first()
            || exec('usun_op','!fks_ksr_mrar')
            || exec('pop_optz','rozrach');
               exec('set_op_sp','fks_sp');
               OP.next()
            ?}
         || exec('usun_op','!fks_ksr_mrar')
         ?}
      || OP.next()
      ?}
   !};
   ZAP_OP.trig_on('del','')
?};
1


\usun_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [8.70]
:: OPIS: Usuwanie rozrachunku
::  OLD: \usun_op/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
D_RB.cntx_psh();
D_RB.use('dr__fk'+(OP.name+2));
D_RB.index(exec('ndxD_RB','raty',OP)); D_RB.prefix(OP.ref());
{? D_RB.first() || {! |? D_RB.del() !} ?};
D_RB.cntx_pop();
STANY.prefix(OP.ref());
{? STANY.first() || {!|? STANY.del() !} ?};
OP_PROJ.index('OP'); OP_PROJ.prefix(OP.ref());
ZAP_PROJ.index('OP_PROJ');
{? OP_PROJ.first()
|| {!
   |? ZAP_PROJ.prefix(OP_PROJ.ref());
      {? ZAP_PROJ.first() || {! |? ZAP_PROJ.del() !} ?};
      OP_PROJ.del()
   !}
?};
OP.del()


\old2new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [8.70]
:: OPIS: Kopiowanie rozrachunków
:: WE: _a: data poczatku
::     _b: data konca
::     _c: [1] - dla copy_op, [2] - dla spr_op
::     _d: prefix konta dla _c=2
::  OLD: \old2new/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _
|| _l:=0;
   SLU.prefix(); FINFO.SLWAL().SLU();
   SLO.index('SL');  SLO.prefix(SLU.ref); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? SLO.first() & FINFO.SLWAL
   || {!
      |? {? _c=1
         || _l+=exec('copy_op','!fks_ksr_mrar',_a,_b)
         |? _c=2
         || exec('spr_op','!fks_ksr_mrar',_a,_b,_d)
         ?};
         SLO.next()
      !}
   ?};
   {? _c=1 & _l=0
   || {? -POZ.KOM().KS().ROZR='r' | (-POZ.KOM().KS().ROZR='p' & +|POZ.ID)
      || exec('add_ttmp','!fks_ksr_mrar',POZ.DOK().NK,POZ.PKON,'Brak rozrachunków do tego konta. (pozycja '+$POZ.POZ+')')
      ?}
   ?}
?}


\copy_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [8.70]
:: OPIS: Kopiowanie rozrachunków
:: WE: _a, _b - data poczatku i konca roku
::  OLD: \copy_op/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_test:=0;
_newop:=null;
OP.use('operac'+BILANSP.PROK().KOD);
STANY.use('stany_'+BILANSP.PROK().KOD); STANY.index('STAN'); STANY.prefix();
ZAP_OP.use('rozzap'+BILANSP.PROK().KOD);
ZAP_OP.index('OP'); ZAP_OP.prefix();
D_RB.use('dr__fk'+BILANSP.PROK().KOD); D_RB.index('OP');
RMK.use('rmkop_'+BILANSP.PROK().KOD); RMK.prefix();
OP.index('KON');
OP.prefix(SLO.ref(),POZ.PKON);
{? OP.first()
|| {! |?
      {? exec('test_dat','zam_okr_rok', _a, _b)
      || OP.cntx_psh(); _tz:=OP.TZ; _do:=OP.DO;
         OP.use('operac'+BILANSP.NROK().KOD);
         OP.index('KON_O'); OP.prefix();
         OP.AN:=Konto;
         {? -POZ.TID<>'rmk' & -POZ.TID<>'rmp'
         || SLO.cntx_psh(); KH.cntx_psh();
            OP.POCH:=exec('wys_op','dok_fks_ks',Konto);
            OP.KH:=exec('kh_op','dok_fks_ks');
            SLO.cntx_pop(); KH.cntx_pop()
         ?};
         SLO.cntx_psh();
         {? ~OP.find_key(OP.WAL,OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK)
         || OP.WN:=OP.MA:=0; OP.add()
         || _j:=0; {? _do<OP.DO || OP.DO:=_do; _j+=1 ?};
            {? _tz<OP.TZ || OP.TZ:=_tz; _j+=1 ?};
            {? _j || OP.put(); exec('pop_all','rozrach',0) ?}
         ?};
         SLO.cntx_pop();
         _newop:=OP.ref();
         OP.cntx_pop();
         STANY.prefix(OP.ref());
         {? STANY.first()
         || {! |?
               STANY.cntx_psh();
               STANY.use('stany_'+BILANSP.NROK().KOD);
               STANY.index('STAN'); STANY.prefix();
               STANY.OP:=_newop;
               {? ~STANY.find_key(STANY.OP, STANY.OKRO)
               || STANY.WN:=STANY.MA:=0; STANY.add()
               ?};
               STANY.cntx_pop();
               STANY.next()
            !}
         ?};
         exec('zap_op','!fks_ksr_mrar',_newop);
         exec('op_proj','!fks_ksr_mrar',_newop);
         exec('d_rb','!fks_ksr_mrar',_newop)
      ?};
      OP.next()
   !}; 1
|| 0
?}


\zap_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Kopiuje zapisy na rozrachunku
::   WE: _a - ref nowego OP
::  OLD: \zap_op/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
 ZAP_OP.prefix(OP.ref());
{? ZAP_OP.first()
|| {! |?
      _waluta:=ZAP_OP.OP().WAL;
      OP.cntx_psh();  ZAP_OP.cntx_psh(); STANY.cntx_psh(); SLO.cntx_psh();
      OP.use('operac'+BILANSP.NROK().KOD); OP.index('KON_O'); OP.prefix();
      STANY.use('stany_'+BILANSP.NROK().KOD);  STANY.index('STAN');
      STANY.prefix();
      ZAP_OP.use('rozzap'+BILANSP.NROK().KOD);  ZAP_OP.index('OP'); ZAP_OP.prefix();
      ZAP_OP.OP:=_a; ZAP_OP.add();
      {? ~STANY.find_key(ZAP_OP.OP,ZAP_OP.OKRO)
      || STANY.blank(1); STANY.OP:=ZAP_OP.OP; STANY.OKRO:=ZAP_OP.OKRO;
         STANY.WN:=ZAP_OP.WN; STANY.MA:=ZAP_OP.MA; STANY.add()
      || STANY.WN+=ZAP_OP.WN; STANY.MA+=ZAP_OP.MA; STANY.put()
      ?};
      ZAP_OP.OP();
      OP.WN+=ZAP_OP.WN; OP.MA+=ZAP_OP.MA;
      OP.STAN:={? OP.WN$2 = OP.MA$2 || 'R' || 'N' ?};
      OP.put();
      OP.cntx_pop(); ZAP_OP.cntx_pop();  STANY.cntx_pop(); SLO.cntx_pop();
      ZAP_OP.next()
   !}
?};
1


\op_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Przeniesienie rozrachunkow projektow do nowego roku
::   WE: _a - wskazanie na rozrachunek w nowym roku (OP.ref())
::  OLD: \op_proj/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
OP_PROJ.use('rozpro'+BILANSP.PROK().KOD); OP_PROJ.index('OP'); OP_PROJ.prefix(OP.ref());
ZAP_PROJ.use('rozprz'+BILANSP.PROK().KOD); ZAP_PROJ.index('OP_PROJ'); ZAP_PROJ.prefix();
{? OP_PROJ.first()
|| {! |?
      OP_PROJ.cntx_psh();
      OP_PROJ.use('rozpro'+BILANSP.NROK().KOD); OP_PROJ.prefix();
      OP_PROJ.OP:=_a;
      _ref:={? OP_PROJ.add() || OP_PROJ.ref() || null ?};
      OP_PROJ.cntx_pop();
      {? _ref
      || ZAP_PROJ.prefix(OP_PROJ.ref());
         {? ZAP_PROJ.first()
         || {!
            |? ZAP_PROJ.cntx_psh();
               ZAP_PROJ.use('rozprz'+BILANSP.NROK().KOD); ZAP_PROJ.prefix();
               ZAP_PROJ.OP_PROJ:=_ref;
               ZAP_PROJ.add();
               ZAP_PROJ.cntx_pop();
               ZAP_PROJ.next()
            !}
         ?}
      ?};
      OP_PROJ.next()
   !}
?}


\d_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Przenosi platnosci ratalne przenoszonych rozrachunkow.
::   WE: _a: ref nowego OP.
::  OLD: \d_rb/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
D_RB.cntx_psh();
D_RB.use('dr__fk'+BILANSP.NROK().KOD);
D_RB.index(exec('ndxD_RB','raty',OP)); D_RB.prefix(_a);
_jest:=D_RB.first();
D_RB.cntx_pop();
{? ~_jest
|| D_RB.prefix(OP.ref());
   {? D_RB.first()
   || {! |?
         D_RB.use('dr__fk'+BILANSP.NROK().KOD); D_RB.clear();
         D_RB.OP:=_a; D_RB.add();
         D_RB.use('dr__fk'+BILANSP.PROK().KOD);
         D_RB.next()
      !}
   ?}
?}; 1


\add_ttmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM   [8.50]
:: OPIS: Dodanie rekordu do tabeli tymczasowej TTMP
::   WE: a - dokument/blad
::       b - konto
::       c - opis
::  OLD: \add_ttmp/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=3
|| TTMP.index(I_T2);
   {? ~(TTMP.find_key(_a,_b,_c) & TTMP.OPIS=_c & TTMP.KONTO=_b & TTMP.DOKUMENT=_a)
   || TTMP.index(I_T1);
      _l:={? TTMP.last() || TTMP.LP+1 || 1?};
      TTMP.LP:=_l;
      TTMP.DOKUMENT:=_a;
      TTMP.KONTO:=_b;
      TTMP.OPIS:=_c;
      TTMP.add()
   ?}
?}


\spr_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzanie przenoszonych rozrachunków
::   WE: _a, _b - data poczatku i konca roku
::       _c - prefix dla OP.AN
::  OLD: \spr_op/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
OP.use('operac'+BILANSP.PROK().KOD); OP.index('STAN');
OP.prefix(SLO.ref(),'N',_c);
POZ.index('PKON'); POZ.prefix();
{? OP.first()
|| _size:=OP.size();  _akt:=1;
   {! |?
    echo('Sprawdzanie rozrachunków nierozliczonych w roku %1 Wykonano ...  %2 %% '@[BILANSP.PROK().NAZ,form((100*_akt)/_size,6,2)]);
      {? exec('test_dat','zam_okr_rok',_a,_b)
      || _konto:={? POZ.find_key(OP.AN) & OP.AN=POZ.PKON & POZ.PKON<>POZ.KON || POZ.KON || OP.AN ?};
         _kp:=OP.AN; _waluta:=OP.WAL; _sym:=OP.SYM; _usym:=OP.SYM_ROK; _odd:=OP.ODD;
         OP.cntx_psh();
         OP.use('operac'+BILANSP.NROK().KOD);
         OP.index('KON_O'); OP.prefix();
         {? ~OP.find_key(_waluta,_odd,_konto,_sym,_sym,_usym) & OP.WN<>OP.MA
         || TTMP.index(I_T2); TTMP.prefix();
            {? ~TTMP.find_key('#rozrachunek: '+_sym,_kp,'Nie został przeniesiony.')
            || _t:='Nie został przeniesiony.';
               {? POZ.find_key(OP.AN) & OP.AN=POZ.PKON
               || _t+=' (Dokument '+POZ.DOK().NK+' nie został zaksięgowany próbnie)'
               ?};
               exec('add_ttmp','!fks_ksr_mrar', '#rozrachunek: '+_sym, _kp , _t)
            ?}
         ?};
         OP.cntx_pop()
      ?}; _akt+=1;
      OP.next()
   !}; echo(); 1
|| 0
?}


\poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.10]
:: OPIS: Przeniesienie rozrachunkow -c.d.
::   WE: _a  - prefix konta dla aktualizacji rozrachunkow
::       _b  - miejsce  wywolania
::             (0 - ksiegowanie wszystkich dokumentow
::              1 - ksiegowanie pojedynczego dokumentu
::              2 - aktualizacja rozrachunkow
::              3 - akcja grupowa )
::  OLD: \poz/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
POZ.index('PKON'); {? _a<>'' || POZ.prefix(_a) || POZ.prefix() ?};
{? POZ.first() & BILANSP.PROK & BILANSP.NROK
|| _size:=POZ.size(); _akt:=1; _sym:=''; Konto:='';
   DOK.cntx_psh();
   {! |?
          echo('Przenoszenie rozrachunków. Wykonano ...  %1 %% '@[form((100*_akt)/_size,6,2)]);
         {? -POZ.DOK().ZP='t'
         || {? _sym<>POZ.PKON
            || _sym:=POZ.PKON;
               Konto:=POZ.KON;
               exec('old2new','!fks_ksr_mrar',pocz,kon,1); 1
            || _akt+=1; POZ.next()
            ?}
         || {? _b<>1 & _b<>3 & POZ.PKON<>'' & ( -POZ.KOM().KS().ROZR='r' | (-POZ.KOM().KS().ROZR='p' & +|POZ.ID))
            || {? TTMP.index(I_T2); TTMP.prefix();
                  ~TTMP.find_key(POZ.DOK().NK,'-','Dokument nie został zaksięgowany próbnie.')
               || exec('add_ttmp','!fks_ksr_mrar',POZ.DOK().NK,'-', 'Dokument nie został zaksięgowany próbnie.' )
               ?}
            ?};
            _akt+=1; POZ.next()
         ?}
   !};
   DOK.cntx_pop(); &Konto;
   echo()
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 cbe2cd8800995e3ee1396a225986cfbe9d511afbbc5946d385287f0e1759ffd95fa3a7f59baffb01803052a5b70b7f6c98a2bf34e1048579350afc9f2f07d706154363a31d1c57015dc17d62e123a29eef4f62a1cd4122081264a71ae2fd893bca7da5e48f84eb7be89035e9772720b5f5b29542429d78f73929e1dcf59a256e

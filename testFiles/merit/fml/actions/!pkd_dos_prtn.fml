:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prtn.fml
:: Utworzony: 29.04.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_DOS_PRTN - Rejestracja tytułów naukowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja tytułów naukowych - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \os_tyt_sel/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE,   symbol=OSOBA,     type=_OSOBA,   name=Wskazanie osoby,   required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.OSOBA);
{? _uidref=''
|| _result:=exec('error','!pkd_dos_prtn')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_prtn',_in.OSOBA);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja tytułów naukowych - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj tytuły naukowe: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj tytuły naukowe: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj tytuły naukowe: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj tytuły naukowe'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie tytułów naukowych niemożliwe.\nNie znaleziono osoby.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie osoby.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & OSOBA.seek(_a)
|| OS_TYT.cntx_psh();
   OS_TYT.index('UNIQ');
   OS_TYT.prefix(OSOBA.ref());
   OS_TYT.index('NDX1');
   OS_TYT.prefix(OSOBA.ref());
   OS_TYT.index('NDX2');
   OS_TYT.prefix(OSOBA.ref());

   OS_TYT.win_sel('WERI');
   OS_TYT.win_edit('RED');
   OS_TYT.win_patt('RED');

   _ret:=OS_TYT.select();
   OS_TYT.cntx_pop()
|| _ret:=exec('error','!pkd_dos_prtn')
?};
OSOBA.cntx_pop();
_ret


\os_tyt_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Modyfikacja rekordów tabeli OS_TYT.
::   WE: _a - Czy poprawianie rekordu?
::          1 - Tak.
::          0 - Nie, poprawianie [domyślnie].
::   WY:
::  OLD: \os_tyt_bea/osoba.fml
::  OLD: \os_tyt_aea/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_put:={? var_pres('_a')=type_of(0) || _a<>0 ?};

{? ~_put
|| OS_TYT.blank()
?};
{? OS_TYT.edit("exec('os_tyt_ae','!pkd_dos_prtn')")
|| do();
   {? {? _put || OS_TYT.put(1) || OS_TYT.add(1) ?}
   || {? OS_TYT.PRIO=1
      || {? ~exec('update_tyt','osoba','N',OS_TYT.OSOBA,OS_TYT.TYTULN,OS_TYT.DZTN) || undo() ?}
      ?}
   || undo()
   ?};
   {? end()
   || 1
   || FUN.info({? _put || 'Tytułu nie udało się poprawić.'@ || 'Tytułu nie udało się dołączyć.'@ ?});
      0
   ?}
?}


\os_tyt_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz" tabeli OS_TYT.
::   WE:
::   WY:
::  OLD: \os_tyt_bea/osoba.fml
::  OLD: \os_tyt_aea/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
exec('os_tyt_mod','!pkd_dos_prtn',0)


\os_tyt_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw" tabeli OS_TYT.
::   WE:
::   WY:
::  OLD: \os_tyt_bea/osoba.fml
::  OLD: \os_tyt_aea/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
exec('os_tyt_mod','!pkd_dos_prtn',1)


\os_tyt_ubg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Usuń - przed" tabeli OS_TYT.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ask','#table')


\os_tyt_ub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.30]
:: OPIS: Obsługa akcji "Usuń" tabeli OS_TYT.
::   WE:
::   WY:
::  OLD: \os_tyt_del/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_grp:=OS_TYT.sel_size();

{? ~_grp & OS_TYT.count()
|| FUN.info('Pozycja wykorzystywana w systemie.\nUsunięcie niedozwolone.'@)
|? _grp | exec('del_ask','#table')
|| do();
   {? OS_TYT.PRIO=1
   || {? ~exec('update_tyt','osoba','N',OS_TYT.OSOBA,null(),null()) || undo() ?}
   ?};
   OS_TYT.del();
   {? ~end() & ~_grp || FUN.emsg('Usunięcie nie powiodło się.'@) ?}
?}


\os_tyt_mb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KGS [12.10]
:: OPIS: Ustawienie znacznika rekordu OS_TYT dla potrzeb prezentacji przed nazwiskiem.
::   WE:
::   WY:
::  OLD: \os_tyt_prio/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
{? OS_TYT.PRIO=1
|| {? FUN.ask('Zmiana spowoduje przełączenie na automatyczny sposób prezentacji tytułu.\nKontynuować?'@)
   || do();
      OS_TYT.PRIO:=0;
      {? ~OS_TYT.put(1)
      || undo()
      |? ~exec('update_tyt','osoba','N',OS_TYT.OSOBA,null(),null())
      || undo()
      ?};
      {? ~end() || FUN.emsg('Zmiana nie powiodła się.'@) ?}
   ?}
|| {? FUN.ask('Zmiana spowoduje ustawienie tytułu jako prezentowanego przed nazwiskiem\n'
              'podczas prezentacji w wybranych zestawieniach i raportach.\n\n'
              'Dotychczasowa prezentacja zostanie anulowana.\n\n'
              'Kontynuować?'@)
   || do();
      OS_TYT.cntx_psh();
      OS_TYT.index('NDX1');
      OS_TYT.prefix(OS_TYT.OSOBA,1);
      {? OS_TYT.first()
      || OS_TYT.prefix(OS_TYT.OSOBA);
         OS_TYT.PRIO:=0;
         {? ~OS_TYT.put(1) || undo() ?}
      ?};
      OS_TYT.cntx_pop();
      OS_TYT.get();
      OS_TYT.PRIO:=1;
      {? ~OS_TYT.put(1) || undo() ?};
      {? ~exec('update_tyt','osoba','N',OS_TYT.OSOBA,OS_TYT.TYTULN,OS_TYT.DZTN) || undo() ?};
      {? ~end() || FUN.emsg('Zmiana nie powiodła się.'@) ?}
   ?}
?};
1


\os_tyt_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli OS_TYT z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli OS_TYT jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=OS_TYT
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\os_tyt_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" - weryfikacja wypełnienia pól tabeli OS_TYT.
::   WE:
::   WY:
::  OLD: \os_dnaukae/dnauk.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(OS_TYT,-menu_txt()='popraw',,'TYTULN');
{? type_of(_chk)<>type_of(0) | ~_chk
|| return(_chk)
?};

{? OS_TYT.DO<>date(0,0,0) & OS_TYT.OD>OS_TYT.DO
|| FUN.info('Data "%1" nie może być wcześniejsza niż "%2".'@ [MS.name(OS_TYT,'DO'),MS.name(OS_TYT,'OD')]);
   return('OD')
?};

''

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 8f2b37cc0078222df87bae6d46d840ca7305ff9ee1d1bb7d30a53810bad87d95d13a3c0e7ad3af46235d411df227c14fcaa1378225238c8f920d1cf3e3e3015199aeb06902bb45182356951c0b0e805f939b9d82d8245a2bc58b250fe2cc1cfa88bbf36edf8fd8f0c6c7323b0af667861e0670f4b833804a07f292ac3d38bc5c

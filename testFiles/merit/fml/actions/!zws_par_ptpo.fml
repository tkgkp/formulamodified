:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ptpo.fml
:: Utworzony: 20.01.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PTPO - Redagowanie tabeli podatkowej.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Redagowanie tabeli podatkowej - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~TP.lock(1,1,1)
|| FUN.info('Czynność jest blokowana przez innego użytkownika.'@+'\n'+'Spróbuj ponownie za chwilę.'@);
   return()
?};

_par:=params_get();
_rok:=
   {? var_pres('_par')>100 & var_pres('in',_par)>100 & var_pres('FP_INS_NUM',_par.in)=type_of(0)
   || _par.in.FP_INS_NUM
   || date()~1
   ?};

TP.cntx_psh(); TP.index('TABPODAT'); TP.prefix();
TP.win_edit('RED');

_tabR:=sql('select distinct TP.R from TP order by 1 desc');

: [TO DO]
: Jeżeli wybrana jest lista - podpowiedzieć rok podatkowy z listy.

_tabR.fld_fml('R','BLANK',"
   _tabR:=cur_tfld();
   _tabR.cntx_psh();
   _bl:={? _tabR.first() || _tabR.R+1 || date()~1 ?};
   _tabR.cntx_pop();
   _bl");

_akcja:='"exec(\'"+_a+"\',\'!zws_par_ptpo\')"';

_cfg:=',btn_label_align=center,panel=bottom,align=end';

_weR:=_tabR.mk_edit('|--|'+'Rok podatkowy'@,,'#rtpoe');
_tabR.win_esep(_weR,'Dane podstawowe'@);
_tabR.win_efld(_weR,,'R',,,4,-1,,'Rok podatkowy'@,,'Rok'@);
_tabR.efld_opt(_weR,'mark=1',,'R');
exec('ok_esc','#window',_tabR,_weR,,,,,,,exec('text_red_ok','#window'));
_tabR.win_edit(_weR);

_wsR:=_tabR.mk_sel('Lata'@,'N',,'#rtpos');
_tabR.win_fld(_wsR,,'R',,,4,-1,,'Rok'@,,'Rok pdatkowy'@);
_tabR.win_act(_wsR,1,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu@',$($_akcja)('tabr_dolacz'),,1,,,,'D');
_tabR.win_act(_wsR,,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_akcja)('tabr_dolacz'),,1,,,,'D');
_tabR.win_act(_wsR,,'Formuła','Usuń'@@,,'Usunięcie bieżącego zapisu'@,$($_akcja)('tabr_usun'),,,,,,'U');
_tabR.win_act(_wsR,,'Formuła','Kopiuj'@@,,
   'Kopiowanie zapisu bieżącego roku do wskazanego'@,$($_akcja)('tabr_kopiuj'),,,,,,'K');

_tabR.win_act(_wsR,,'Szukaj');
_btn1:=_tabR.win_btn(_wsR,'text='+'&Dołącz'@+',panel=right,align=begin','menu:D',);
_btn2:=_tabR.win_btn(_wsR,'text='+'&Usuń'@+',panel=right,align=begin','menu:U',,,,,,'noempty');
_btn3:=_tabR.win_btn(_wsR,'text='+'&Kopiuj'@+',panel=right,align=begin','menu:K',,,,,,'noempty');
_tabR.btn_sopt(_wsR,_btn1,'tooltip='+'Dołączenie nowego zapisu'@);
_tabR.btn_sopt(_wsR,_btn2,'tooltip='+'Usunięcie bieżącego zapisu'@);
_tabR.btn_sopt(_wsR,_btn3,'tooltip='+'Kopiowanie zapisu bieżącego roku do wskazanego'@);
_tabR.win_edit(_weR);

_mode:='maximized_with_title';

_gwer:=_tabR.grp_make('Tabele podatkowe'@,,'#rtpog',,,,,'normal');
_tabR.grp_sel(_gwer,_tabR,_wsR,,$($_akcja)('tabr_ar'),,,6,,,,,_mode);
_tabR.grp_splt(_gwer,,'vertical','TP');
_tabR.grp_sel(_gwer,TP,'WER',,"",,,6,,,,,_mode);

_tabR.win_sel(_gwer);

_fml:=TP.fld_fml('R','*BLANK');

:[TO DO]
:Jeżeli lista jest wybrana - ustawmy się na roku podatkowym listy.
{? ~_tabR.find_key(_rok)
|| _tabR.cntx_psh();
   _tabR.prefix();
   _tabR.R:=_rok;
   _tabR.add();
   _tabR.cntx_pop();
   ''
?};

_tabR.find_ge(_rok);
_tabR.select(,1,-1);

TP.fld_fml('R','BLANK',_fml);
TP.cntx_pop();
TP.unlock();

~~


\tabr_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Dołącz dla tabeli tymczasowej tabR (z latami podatkowymi).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
_tabR.blank();
{? _tabR.edit("exec('tabr_ae','!zws_par_ptpo','D')")
|| _tabR.add()
?}


\tabr_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Usuń dla tabeli tymczasowej tabR (z latami podatkowymi).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? {? TP.first() || exec('del_conf','#table') || exec('del_ask','#table') ?}
|| do();
   {? TP.first() || {! |? TP.del() !} ?};
   cur_tab(1,1).del();
   {? ~end()
   || FUN.emsg('Usuniecię bieżącego zapisu nie powiodło się.'@)
   ?}
?}


\tabr_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Kopiuj dla tabeli tymczasowej tabR (z latami podatkowymi).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
{? TP.size()=0 &
   FUN.ask(
      'Tabela podatkowa dla roku %1 nie zawiera definicji progów.'@ [$_tabR.R]+'\n'+
      'Czy na pewno chcesz kontynuować kopiowanie?'@
   )=0
|| return(0)
?};
_src:=_tabR.R;
_tabR.blank();
{? _tabR.edit("exec('tabr_ae','!zws_par_ptpo','K')")
|| _dest:=_tabR.R;
   do();
   TP.cntx_psh();
   {? ~_tabR.find_key(_dest)
   || _tabR.R:=_dest; _tabR.add()
   ?};
   TP.prefix(_src);
   {? TP.first()
   || {!
      |? TP.cntx_psh();
         TP.prefix();
         TP.R:=_dest;
         TP.add();
         TP.cntx_pop();
         TP.next()
      !}
   ?};
   TP.cntx_pop();
   {? ~end()
   || FUN.emsg('Kopiowanie bieżącego zapisu nie powiodło się.'@)
   ?}
?}


\tabr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu rekordu tabeli tymczasowej tabR (z latami podatkowymi).
::   WE: _a - Kontekst wywołania:
::            'D' - Dołączanie;
::            'K' - Kopiowanie.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
{? _tabR.R<=0 | _tabR.R>9999
|| FUN.emsg('Błędny rok podatkowy.'@);
   return('R')
?};

{? _a='D'
|| _tabR.cntx_psh();
   _dubelek:=_tabR.find_key(_tabR.R);
   _tabR.cntx_pop();
   {? _dubelek
   || __CHK.err_ndx(_tabR,'',menu_txt()='Popraw');
      'R'
   || ''
   ?}

|? _a='K'
|| TP.cntx_psh();
   TP.prefix(_tabR.R);
   _pusty:=TP.first();
   TP.cntx_pop();
   {? _pusty
   || __CHK.err_ndx(_tabR,'',menu_txt()='Popraw');
      'R'
   || ''
   ?}
?}


\tabr_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po odświeżeniu okna tabeli tymczasowej tabR (z latami podatkowymi).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
{? _tabR.get()
|| TP.prefix(_tabR.R);
   TP.fld_fml('R','BLANK',$$_tabR.R);
   _hid:=''
|| TP.prefix(-1);
   _hid:=':d'
?};
TP.actions('WER',_hid,,1);
grp_disp(TP,'WER')


\tp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Weryfikuje procent podatku tabeli podatkowej - procent podatku musi być mniejszy od 100.
::   WE:
::   WY: ''   - weryfikacja przebiegła pomyślnie
::       'K1' - tabela podatkowa zawiera błędy
::       'PR' - tabela podatkowa zawiera błędny procent
::----------------------------------------------------------------------------------------------------------------------
{? __CHK.index(TP,menu_txt()='Popraw')<>''
|| _ret:='K1'
|? TP.PR>=100
|| FUN.emsg('Procent podatku musi być mniejszy od 100.'@);
   _ret:='PR'
|| _ret:=exec('spr_tp','!zws_par_ptpo')
?};
_ret


\spr_tp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Weryfikuje spójność tabeli podatkowej - niedopuszczalne jest aby od wyższych dochodów płacony był
::       mniejszy podatek.
::   WE:
::   WY: ''   - weryfikacja przebiegła pomyślnie
::       'K1' - tabela podatkowa zawiera błędy
::  OLD: \spr_tp/kali.fml
::----------------------------------------------------------------------------------------------------------------------
TP.cntx_psh();
do();
_pr:=-1;
:Próbnie zaktualizujmy/dodajmy rekord, tak aby analiza była całościowa.
_ok:={? 1+menu_txt()='P' || TP.put(1) || TP.add(1) ?};
{? _ok & TP.first()
|| {!
   |? {? _pr<TP.PR || _pr:=TP.PR; TP.next() || _ok:=0 ?}
   !}
?};
:Wycofajmy się z próbnego zaktualizowania/dodania rekordu.
undo();
end();
TP.cntx_pop();
{? _ok
|| ''
|| FUN.emsg(
      'Tabela podatkowa powinna być budowana w ten sposób,\n'
      'aby od wyższych dochodów płacony był wyższy podatek.\n'
      'Oznacza to, procent podatku musi rosnąć wraz z progami.'@
   );
   'K1'
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 adbc5d2560c912c50ef1b5a511cb9f8d42ae7a74aac83e3bd5dd2b85e93802e164a6a28907f3ba6eb9ae33a94c9be1f414ffdafe63650027d353758ec30aeb8f08978237ba28144f50c1ce860840661ea29f9b3e56a61e5e005a46411584bb2aa7c2957b6feeab8bf72dff20a8321574b08848d0bf129ca0cf9e528e303d2340

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ruc_kdar.fml
:: Utworzony: 13.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RUC_KDAR - Rejestracja planu kompensaty
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rejestracja planu kompensaty - główna formuła czynności FKS_RUC_KDAR
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_par_nag','rozrach')
::# kind=WY, symbol=PAR_NAG, type=_PAR_NAG, name=Wskazanie na nagłówek planu umowy kompensaty, required=T, keyref=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='K';
:: czynność startowa
{? _mp.pathProc()
|| menu_txt(,'Dołącz');
   _mp.trigRef('PAR_NAG');
   params_exec('kom_dol','rozrach')
|? _mp.pathTodo()
|| menu_txt(,'Popraw');
   {? var_press('PAR_NAG',_out)>0
   || _ref:=BB.sqlint($_out.PAR_NAG); _nam:=form(($_out.PAR_NAG)-8);
      {? _ref<>0 & _nam<>''
      || PAR_NAG.cntx_psh(); PAR_NAG.prefix();
         {? PAR_NAG.seek(_ref,_nam)
         || {? PAR_NAG.TYP=SER_KOR.TYP
            || params_exec('kom_pop','rozrach',1)
            || FUN.info('Wybrana umowa nie jest umową kompensaty.'@); _mp.error()
            ?}
         || FUN.info('Nie znaleziono umowy kompensaty.'@); _mp.error()
         ?};
         PAR_NAG.cntx_pop()
      || FUN.info('Nie znaleziono umowy kompensaty.'@); _mp.error()
      ?}
   || FUN.info('Nie znaleziono umowy kompensaty.'@); _mp.error()
   ?}
|? _akcja='Dołącz'
|| menu_txt(,'Dołącz');
   _mp.trigRef('PAR_NAG');
   params_exec('kom_dol','rozrach')
|? _akcja='Popraw0'
|| menu_txt(,'Popraw');
   params_exec('kom_pop','rozrach',0)
|? _akcja='Popraw1'
|| menu_txt(,'Popraw');
   params_exec('kom_pop','rozrach',1)
|? _akcja='Zakoncz'
|| menu_txt(,'Popraw');
   params_exec('kom_zak_plan','rozrach')
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ rejestrację umowy kompensaty rozrachunków'@@;
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('PAR_NAG',_wy)
|| _ref:=BB.sqlint($_wy.PAR_NAG); _nam:=form(($_wy.PAR_NAG)-8);
   {? _ref<>0 & _nam<>''
   || PAR_NAG.cntx_psh(); PAR_NAG.use(_nam); PAR_NAG.prefix();
      {? PAR_NAG.seek(_ref,_nam)
      || _desc:='Zakończ rejestrację umowy kompensaty rozrachunków: %1 z dnia %2'@@[PAR_NAG.PAR_UM().SYM,$PAR_NAG.DATA]
      ?};
      PAR_NAG.cntx_pop()
   ?}
?};
_desc


\kompens_new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie planu kompensaty
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RUC_KDAR';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\kompens_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie planu kompensaty
::   WE: _a - 0 - nagłówek
::            1 - pozycje
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RUC_KDAR';
_params.AKCJA:={? _a=0 || 'Popraw0' || 'Popraw1' ?};
_params.UIDREF:=PAR_NAG.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
exec('mp_run','#b__box',_params)


\kompens_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Kończenie planu kompensaty
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RUC_KDAR';
_params.AKCJA:='Zakoncz';
_params.UIDREF:=PAR_NAG.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
exec('mp_run','#b__box',_params)


\kom_prz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Formuła wykonywana przy akcji 'Kompensuj częściowo' w okienku wertowania pozycji do kompensaty
::  OLD: \kom_prz/kompens.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.KWOTA:=0;
{? -OPTMP.STR='wn'
|| ROZRACH.KWOTA:=OPTMP.KOMPMA;
   OPTMP.KOMPMA:={? ROZRACH.SUM4>=ROZRACH.SUM3 || ROZRACH.SUM4-ROZRACH.SUM3+ROZRACH.KWOTA || 0 ?};
   {? OPTMP.KOMPMA>ROZRACH.KW4 || OPTMP.KOMPMA:=ROZRACH.KW4 ?}
|| ROZRACH.KWOTA:=OPTMP.KOMPWN;
   OPTMP.KOMPWN:={? ROZRACH.SUM3>=ROZRACH.SUM4 || ROZRACH.SUM3-ROZRACH.SUM4+ROZRACH.KWOTA || 0 ?};
   {? OPTMP.KOMPWN>ROZRACH.KW5 || OPTMP.KOMPWN:=ROZRACH.KW5 ?}
?};
ROZRACH.SUM8:=F.SWn(ROZRACH.SUM4,ROZRACH.SUM3);
ROZRACH.SUM7:=F.SMa(ROZRACH.SUM4,ROZRACH.SUM3);
1


\kom_cze
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Formuła wykonywana przy akcji 'Kompensuj częściowo' w okienku wertowania pozycji do kompensaty
::  OLD: \kom_cze/kompens.fml
::----------------------------------------------------------------------------------------------------------------------
OPTMP.KOMPWN:=OPTMP.KOMPWN$2; OPTMP.KOMPMA:=OPTMP.KOMPMA$2; OPTMP.put;
{? -OPTMP.STR='wn'
|| ROZRACH.SUM3+=OPTMP.KOMPMA-ROZRACH.KWOTA$2;
   ROZRACH.SUM5:=(ROZRACH.SUM1-ROZRACH.SUM3)$2
|| ROZRACH.SUM4+=OPTMP.KOMPWN-ROZRACH.KWOTA$2;
   ROZRACH.SUM6:=(ROZRACH.SUM2-ROZRACH.SUM4)$2
?};
ROZRACH.SUM8:=F.SWn(ROZRACH.SUM4,ROZRACH.SUM3);
ROZRACH.SUM7:=F.SMa(ROZRACH.SUM4,ROZRACH.SUM3);
1


\kom_cal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Formuła wykonywana przy akcji 'Kompensuj całkowicie' w okienku wertowania pozycji do kompensaty
::  OLD: \kom_cal/kompens.fml
::----------------------------------------------------------------------------------------------------------------------
{? OPTMP.KOMPWN$2>0 | OPTMP.KOMPMA$2>0
|| {? -OPTMP.STR='wn'
   || ROZRACH.SUM3-=OPTMP.KOMPMA$2; ROZRACH.KW3+=OPTMP.KOMPMA$2
   || ROZRACH.SUM4-=OPTMP.KOMPWN$2; ROZRACH.KW11+=OPTMP.KOMPWN$2
   ?};
   OPTMP.ZNACZ:='';
   OPTMP.KOMPWN:=OPTMP.KOMPMA:=0; OPTMP.put();
   {? -OPTMP.STR='wn'
   || ROZRACH.SUM5:=(ROZRACH.SUM1-ROZRACH.SUM3)$2
   || ROZRACH.SUM6:=(ROZRACH.SUM2-ROZRACH.SUM4)$2
   ?}
|? ROZRACH.KW3$2<>0 | ROZRACH.KW11$2<>0
|| OPTMP.ZNACZ:='T';
   {? -OPTMP.STR='wn'
   || OPTMP.KOMPMA:=OPTMP.SAL$2;
      ROZRACH.SUM3+=OPTMP.KOMPMA$2;
      ROZRACH.SUM5:=(ROZRACH.SUM1-ROZRACH.SUM3)$2
   || OPTMP.KOMPWN:=OPTMP.SAL$2;
      ROZRACH.SUM4+=OPTMP.KOMPWN$2;
      ROZRACH.SUM6:=(ROZRACH.SUM2-ROZRACH.SUM4)$2
   ?};
   OPTMP.put()
?};
ROZRACH.SUM8:=F.SWn(ROZRACH.SUM4,ROZRACH.SUM3);
ROZRACH.SUM7:=F.SMa(ROZRACH.SUM4,ROZRACH.SUM3);
1


\rec_kom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.53]
:: OPIS: Formuła wykonywana przy akcji 'Kompensuj częściowo w okienku wertowania pozycji do kompensaty -
::       sprawdzenie prawidłowości wpisanej sumy
::  OLD: \rec_kom/kompens.fml
::----------------------------------------------------------------------------------------------------------------------
{? OPTMP.KOMPWN$2<=0 & OPTMP.KOMPMA$2<=0
|| FUN.info('Kwota musi być większa od 0.'@);
   {? -OPTMP.STR='wn' || 'KOMPMA' || 'KOMPWN' ?}
|? (OPTMP.KOMPWN$2>0 & OPTMP.KOMPWN$2<=OPTMP.SAL$2) | (OPTMP.KOMPMA$2>0 & OPTMP.KOMPMA$2<=OPTMP.SAL$2)
|| OPTMP.ZNACZ:='T'; ''
|? OPTMP.KOMPWN$2>OPTMP.SAL$2 | OPTMP.KOMPMA$2>OPTMP.SAL$2
|| FUN.info('Maksymalna kwota kompensaty wynosi: %1'@[form(OPTMP.SAL,,2)]);
   {? OPTMP.KOMPWN$2>0 || 'KOMPWN' || 'KOMPMA' ?}
?}



:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:12 c4830df009b82fed2411a2ffc147b4dc1ab2e59f210651e20450e2fcce63435b53a225bf4376fc656c935d145299b471a8f2749c8b77323836681b9dd8212062462bdb806a9fbc234df7e41fcff7b3ab4d45381955e3ff5ec2e32577b8267d25b0baeeab57d045389fe73c1d7f6a3d8f56e5291c5f827a095fe124a6baefcde9

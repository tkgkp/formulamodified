:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_zes_zzua.fml
:: Utworzony: 28.06.2016
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_ZES_ZZUA - Przygotowanie raportu zgłoszeniowego ZUS ZUA
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Przygotowanie raportu zgłoszeniowego ZUS ZUA - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::# kind=WE, symbol=ZC, type=_ZC, name=Wskazanie umowy cywilnoprawnej, required=N, keyref=N
::
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, ERROR)", required=N
::
::# kind=WEW, symbol=FOLDER, type=STRING, name=Folder lokalny, required=N, keyref=N
::# kind=WEW, symbol=PLIK, type=STRING, name=Nazwa pliku, required=N, keyref=N
::# kind=WEW, symbol=DATA, type=DATE, name=Data wypełnienia, required=N, keyref=N
::# kind=WEW, symbol=RODZAJ, type=NUMBER, name=Rodzaj zgłoszenia, required=N, keyref=N
::# kind=WEW, symbol=ZUA, type=NUMBER, name=Uwzględniać zarejestrowanych, required=N, keyref=N
::# kind=WEW, symbol=GIODO, type=STRING, name=Przekazanie danych osobowych, required=N, keyref=N
::# kind=WEW, symbol=F_ZATR, type=STRING, name=Forma zatrudnienia, required=N, keyref=N
::
_par:=params_get();
params_set(_par);
_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;
_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';
{? var_pres('RESULT',_rv)<>type_of(~~) & var_pres('RESULT',_rv)<>type_of('') || return() ?};

{? _id=''
|| _result:=exec('error','!pkd_zes_zzua')
|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@[_mp.buf_act.UID,_do]
   ?}
|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('generuj','!pkd_zes_zzua');
      {? type_of(_value)=type_of(0)
      || _rv.RESULT:='OK';
         _mp.save(,_rv);
         {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value;
         _rv.RESULT:='ERROR';
         _mp.save(,_rv);
         _mp.done()
      ?}
   ?}
?};

{? _result<>''
::  obsługa błędów
|| FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Opis do zadania na liście zadań do zrobienia
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Przygotuj raport zgłoszenia do ubezpieczenia ZUS ZUA: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Przygotuj raport zgłoszenia do ubezpieczenia ZUS ZUA: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Przygotuj raport zgłoszenia do ubezpieczenia ZUS ZUA: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Przygotuj raport zgłoszenia do ubezpieczenia ZUS ZUA'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Komunikat o błędzie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zgłoszenie do ubezpieczenia ZUS ZUA nie jest możliwe.'@+'\n'+'Nie znaleziono pracownika.'@


\generuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: Generowanie raportu ZUS ZUA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
VAR_DEL.delete('ZUA');
_param:=exec('init','!pkd_zes_zzua');
_result:=_param.VALUE;
{? _result=2
|| {? ZUA.first()
   || ZUS_RAP.cntx_psh();
      ZUS_RAP.clear();
      {? _out:=exec('otworz','rap_zus','ZUS ZUA')
      || exec('xml_zua_zapisz','rap_zus',ZUA,_out,_param.F_ZATR);
         fclose(_out);
         exec('info','rap_zus',3,'ZUS ZUA')
      ?};
      ZUS_RAP.cntx_pop()
   || exec('info','rap_zus',2,'ZUS ZUA')
   ?}
|? _result=3
|| _result:='Współpracownik jest już zgłoszony do ubezpieczenia.'@+'\n'+'Plik nie zostanie utworzony.'@
|? _result=4
|| _result:='Współpracownik nie ma wprowadzonych danych ubezpieczeniowych.'@+'\n'+'Plik nie zostanie utworzony.'@
|? _result=5
|| _result:='Nie znaleziono wskazanej umowy cywilnoprawnej.'@+'\n'+'Plik nie zostanie utworzony.'@
|? _result=6
|| _result:='Nie wskazano umowy cywilnoprawnej.'@+'\n'+'Plik nie zostanie utworzony.'@
|| _result:=0
?};
exec('set','#profile',,OPERATOR.USER().KOD,'lokalny katalog raportów ZUS',ZUS.FOLDER);
VAR_DEL.delete('ZUA');
_result


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.00]
:: OPIS: ustawienie wartości początkowych dla raportu ZUS ZUA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_in:=_par.in;
_ib:=_par.int;
_mp:=_par.mp;
_result:=obj_new('F_ZATR','VALUE');
P.cntx_psh();
P.clear();
P.seek(_in.P,,1);
OSOBA.cntx_psh();
ZUS_RAP.cntx_psh();
ZC.cntx_psh();
ZUS.blank();
{? type_of(_ib.FOLDER)=type_of(ZUS.FOLDER)
|| ZUS.FOLDER:=_ib.FOLDER
|| ZUS.FOLDER:=exec('get','#profile',,OPERATOR.USER().KOD,'lokalny katalog raportów ZUS')
?};
{? type_of(_ib.PLIK)=type_of(ZUS.PLIK)
|| ZUS.PLIK:=_ib.PLIK
|| ZUS.PLIK:='zua_'+P.OSOBA().NAZWISKO+'_'+OSOBA.PIERWSZE+'_'+$OSOBA.PESEL
?};
{? type_of(_ib.DATA)=type_of(ZUS.DATA) || ZUS.DATA:=_ib.DATA || ZUS.DATA:={? date()<P.DZA || P.DZA || date() ?} ?};
{? type_of(_ib.RODZAJ)=type_of(ZUS.RODZZUA) || ZUS.RODZZUA:=_ib.RODZAJ ?};
{? type_of(_ib.ZUA)=type_of(ZUS.ZUA) || ZUS.ZUA:=_ib.ZUA ?};
{? type_of(_ib.GIODO)=type_of(ZUS.GIODO) || ZUS.GIODO:=_ib.GIODO ?};
{? type_of(_ib.F_ZATR)=type_of('') || _result.F_ZATR:=_ib.F_ZATR || _result.F_ZATR:=P.F_ZATR().KOD ?};
ZUS.RAPORT:='ZUS ZUA';
ZUS.DATAG:=#0;

_ok:={? _result.F_ZATR='P'
     || 1
     |? _result.F_ZATR='Z'
     || {? var_pres('ZC',_in)=type_of(null()) & _in.ZC<>null()
        || ZC.prefix();
           {? ZC.seek(_in.ZC)
           ||  exec('get_ZC_INFO','zlec_rh',ZUS.DATA)
           || _result.VALUE:=5;
              0
           ?}
        || _result.VALUE:=6;
           0
        ?}
     || 0
     ?};

{? _ok
|| _result.VALUE:=exec('zus_dialog','rap_zus','ZUA',_result.F_ZATR,1);
   ZUA:=exec('tabela_zus_zua','rap_zus',_result.F_ZATR);
   {? {? _result.F_ZATR='P'
      || exec('zn_ttub','pracownik',P.OSOBA,ZUS.DATA)=null()
      |? _result.F_ZATR='Z'
      || ~exec('chk_ubez','rap_zus','ZUA',1)
      ?}
   || _result.VALUE:=4
   |? _ok & (ZUS.ZUA | exec('repeat_zua','rap_zus',~ZUS.ZUA,0,_result.F_ZATR)<>(_result.F_ZATR='P'))
   || {? _result.VALUE=1
      || _ib.FOLDER:=ZUS.FOLDER;
         _ib.PLIK:=ZUS.PLIK;
         _ib.DATA:=ZUS.DATA;
         _ib.RODZAJ:=ZUS.RODZZUA;
         _ib.ZUA:=ZUS.ZUA;
         _ib.GIODO:=ZUS.GIODO;
         _ib.F_ZATR:=_result.F_ZATR;
         _mp.save(_ib)
      |? _result.VALUE=2
      || _emd:=P.DZA;
         {? PAR_SKID.get(261)='T'
         || _oddBz:=exec('czy_odd_bzus','oddelegowanie',P.ref(),ZUS.DATA);
            {? _oddBz.st
            || _emd:=_oddBz.od
            || _emd:=P.DZA;
::             Czy ostatnim raportem jest ZWUA:
               _zwuaLast:=exec('zwua_last','rap_zus',P.OSOBA);
               {? _zwuaLast.st=1 | _zwuaLast.st=-1
::             Sprawdzenie czy wrócił z oddelegowania bez zus:
               || _oddBzPowr:=exec('czy_odd_bzus','oddelegowanie',P.ref(),
                     {? _zwuaLast.st=1 || _zwuaLast.dt || P.DZA ?});
::                   Jeśli był wysłany na oddelegowanie zaraz po zatrudnieniu to nie ma ZWUA, sprawdzamy na datę zatr.
                  {? _oddBzPowr.st
                  || _emd:=_oddBzPowr.do+1
                  ?};
                  obj_del(_oddBzPowr)
               ?};
               obj_del(_zwuaLast)
            ?};
            obj_del(_oddBz)
         ?};
         exec('zus_zua_add','rap_zus',#_par.in.P,_result.F_ZATR,_emd)
      ?}
   || _result.VALUE:=3
   ?}
?};
ZC.cntx_pop();
ZUS_RAP.cntx_pop();
OSOBA.cntx_pop();
P.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 83107df4009c731338d9ec59ea6cbefedcb1987db5a909802b38e18a8655905735ae5085d9fe13b8670e38ab893dbabda4783d67711b333c49d6f7612e1a376c679a9150525ef58a6fb693246573c7843029c9e87369688fecc6170af1467fe9f2313b570e1a2259e96f15a78ebb764f60ded4cb56724312eccdc4df7711a9fa

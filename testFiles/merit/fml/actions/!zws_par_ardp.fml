:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ardp.fml
:: Utworzony: 20.04.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_ARDP - RODO: Parametry.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Parametry programu
::  OLD: \parampro/rodo.fml
::----------------------------------------------------------------------------------------------------------------------
_ru:=exec('is_aip','rodo');
_red:=XINFO.mk_edit('Parametry programu'@,,'id');
{? _ru
|| XINFO.win_ewin(_red,,'ZDO_AIP')
?};
XINFO.win_ewin(_red,,'ZDO_IDO');
_btn:=XINFO.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['OK'],"'key:F2'");
XINFO.btn_eopt(_red,_btn,'tooltip='+exec('help_red_ok','#window','P'));
_an:=XINFO.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'],'key:Esc');
XINFO.btn_eopt(_red,_an,'tooltip='+exec('help_red_esc','#window','A'));
_ae:={? _ru || "__CHK.record(XINFO,,'AIP_SYS','AIP_URL','AIP_USR','AIP_PASS')" || "" ?};
exec('edytuj','#stalesys',XINFO,_red,_ae,1)


\getwsaipconfig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Pobiera konfigurację webservice RODO Utility z pliku konfiguracyjnego
::  OLD: \getwsaipconfig/rodo.fml
::----------------------------------------------------------------------------------------------------------------------
__tmp_dir:=fmk_tmp_dir(0);
_file:=__tmp_dir.get_path();
_file+='\\'+dlg_upload(_file,0,'.xml');

{? _file<>''
|| {? _file+4<>'.xml'
   || FUN.info('Wskazano plik inny niż *.xml.'@)
   || exec('getConfig','!zws_par_ardp',_file)
   ?}
?};
VAR_DEL.delete('__tmp_dir')


\getConfig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Pobiera konfigurację z pliku *.xml
::   WE: _a - ścieżka do pliku
::  OLD: \getConfig/ro_ws.fml
::----------------------------------------------------------------------------------------------------------------------
_file:=fopen(_a,'ur',0,,1);
{? _file.is_open()
|| VAR_DEL.delete('__obj');
   __obj:=obj_new('STAT','STR','ID','ADRES','USER','PASS','NAME','UUID','SYSTAB');
   __obj[3]:=__obj[5]:=__obj[6]:='';
   __obj[9]:=tab_tmp(2,
      'NAME','STRING[100]','System',
      'UUID','STRING[100]','UUID'
   );
   __obj[1]:=0;
   __obj[2]:='';
   x_parse(_file,,,"
      {? __obj[1]=0
      || {? _a='Config'
         || __obj[1]:=1
         ?}
      |? __obj[1]=1
      || {? _a='System'
         || __obj[1]:=2
         |? _a='APIUser'
         || __obj[1]:=3
         |? _a='Urls'
         || __obj[1]:=4
         ?}
      |? __obj[1]>1
      || __obj[2]:=''
      ?};
      1
   ","
      {? __obj[1]=2
      || {? _a='Name'
         || __obj[7]:=__obj[2]
         |? _a='UUID'
         || __obj[8]:=__obj[2]
         |? _a='System'
         || __obj[9].NAME:=__obj[7];
            __obj[9].UUID:=__obj[8];
            __obj[9].add()
         |? _a='Systems'
         || __obj[1]:=1
         ?}
      |? __obj[1]=3
      || {? _a='Login'
         || __obj[5]:=__obj[2]
         |? _a='Password'
         || __obj[6]:=__obj[2]
         |? _a='APIUsers'
         || __obj[1]:=1
         ?}
      |? __obj[1]=4
      || {? _a='Api'
         || __obj[4]:=__obj[2]
         |? _a='Urls'
         || __obj[1]:=1
         ?}
      ?};
      1
   ","
      {? __obj[1]>1
      || __obj[2]+=_a
      ?};
      1
   "
   );
   __obj[9].prefix('MACROLOGIC_MERIT',);
   {? __obj[9].first()
   || __obj[3]:=__obj[9].UUID
   ?};
   _file.fclose();
   &_file;
   XINFO.AIP_SYS:=__obj[3];
   XINFO.AIP_USR:=__obj[5];
   XINFO.AIP_PASS:=__obj[6];
   XINFO.AIP_URL:=__obj[4];
   win_disp();
   &__obj
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 3c8bb5aa4dc375a4d688a5375b7d5a64577162f0df6e9196926978086d492118de2748c1dde5ea25bbcc5ec53f478d49597868f5189b3696987efbaf97b6735dd78f1864d5096496d034dc464b42b491cde6fb7f7d9d8cba7b8cd712210a0103b75e6f167051a56773d6b9d7d2377f5f1f6bf54390caba066393b0b9782e64c3

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pkst.fml
:: Utworzony: 04.07.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PKST - Kompetencje a stanowiska.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Kompetencje a stanowiska - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','poc');

_modul:='O';
ZZ_POM.MODUL==_modul;
ZZ_POM.CZYNNOSC:='';

_cfg:=exec('cfg','!zws_par_pkst');
params_set('cfg',_cfg);

_tblank:='BLANK';

ZZ_HIST.cntx_psh();
ZZ_HIST.f_clear();
ZZ_HIST.index('DATA');
ZZ_HIST.prefix(exec('ref_firma','ustawienia'));
ZZ_HIST.win_sel(_cfg.ZZ_HIST.wnd);

ZZ_KOMP.cntx_psh();
ZZ_KOMP.f_clear();
ZZ_KOMP.index('NAZWA');
ZZ_KOMP.prefix();
ZZ_KOMP.actions(_cfg.ZZ_KOMP.ws1,_cfg.ZZ_KOMP.act_hid1);

ZZ_KRYT.cntx_psh();
ZZ_KRYT.index('TYPNAZWA');
_kryt:=exec('save_fml_type','#field',ZZ_KRYT,_tblank);
ZZ_KRYT.fld_fml('ZZ_LINK',_tblank,"ZZ_KOMP.ZZ_DOK");
ZZ_KRYT.fld_fml('ZZ_TYP',_tblank,"exec('typ_op','phr_dane')");
ZZ_KRYT.win_sel(_cfg.ZZ_KRYT.ws);
ZZ_KRYT.hdr_sel();
ZZ_KRYT.hdr_sel('Stanowiska'@);

STN.cntx_psh();
STN.f_clear();
STN.index('STANONAZ');
STN.prefix();

ZZ_KOMPS.cntx_psh();
ZZ_KOMPS.f_clear();
ZZ_KOMPS.index('WARTOSC');
ZZ_KOMPS.prefix();
_komps:=exec('save_fml_type','#field',ZZ_KOMPS,_tblank);
ZZ_KOMPS.fld_fml('ZZ_KOMP',_tblank,"ZZ_KOMP.ref()");
ZZ_KOMPS.actions(_cfg.ZZ_KOMPS.ws,,'Z');
ZZ_KOMPS.actions_grayed(_cfg.ZZ_KOMPS.ws);

ZZ_KOMPK.cntx_psh();
ZZ_KOMPK.f_clear();
ZZ_KOMPK.index('WARTOSC');
ZZ_KOMPK.prefix();
_kompk:=exec('save_fml_type','#field',ZZ_KOMPK,_tblank);
ZZ_KOMPK.fld_fml('ZZ_KOMP',_tblank,"ZZ_KOMP.ref()");
ZZ_KOMPK.actions(_cfg.ZZ_KOMPK.ws,,'Z');
ZZ_KOMPK.actions_grayed(_cfg.ZZ_KOMPK.ws);

ZZ_KOMPZ.cntx_psh();
ZZ_KOMPZ.f_clear();
ZZ_KOMPZ.prefix();
_kompz:=exec('save_fml_type','#field',ZZ_KOMPZ,_tblank);
ZZ_KOMPZ.fld_fml('ZZ_KOMP',_tblank,"ZZ_KOMP.ref()");

ZZ_KOMZB.actions(_cfg.ZZ_KOMZB.ws(),,'p:W');
ZZ_KOMZB.actions_grayed(_cfg.ZZ_KOMZB.ws());

ZZ_HIST.select();

ZZ_KOMZB.actions(_cfg.ZZ_KOMZB.ws(),,'p:W');
ZZ_KOMZB.actions_grayed(_cfg.ZZ_KOMZB.ws());

exec('restore_fml_type','#field',ZZ_KOMPZ,_tblank,_kompz);
ZZ_KOMPZ.f_clear();
ZZ_KOMPZ.cntx_pop();

ZZ_KOMPK.actions(_cfg.ZZ_KOMPK.ws,,'Z');
ZZ_KOMPK.actions_grayed(_cfg.ZZ_KOMPK.ws);
exec('restore_fml_type','#field',ZZ_KOMPK,_tblank,_kompk);
ZZ_KOMPK.f_clear();
ZZ_KOMPK.cntx_pop();

ZZ_KOMPS.actions(_cfg.ZZ_KOMPS.ws,,'Z');
ZZ_KOMPS.actions_grayed(_cfg.ZZ_KOMPS.ws);
exec('restore_fml_type','#field',ZZ_KOMPS,_tblank,_komps);
ZZ_KOMPS.f_clear();
ZZ_KOMPS.cntx_pop();

STN.cntx_pop();

exec('restore_fml_type','#field',ZZ_KRYT,_tblank,_kryt);
ZZ_KRYT.cntx_pop();

ZZ_KOMP.actions(_cfg.ZZ_KOMP.ws1);
ZZ_KOMP.f_clear();
ZZ_KOMP.cntx_pop();

ZZ_HIST.f_clear();
ZZ_HIST.cntx_pop();

ZZ_POM.MODUL==_modul;
~~


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła tworzy okno grupowe do obsługi ocen dla kompetencji.
::   WE: _a [ARRAY] - Tablica elementów nazwanych z konfiguracją.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=_a;

_fid:="'pkst%1%2%3' [_a,-ZZ_PAR.WAR_KRYT,-ZZ_PAR.WAR_KRYW]";

_acr:=~_fid('_');

_wnd:=ZZ_HIST.grp_make('Kompetencje a stanowiska'@,,_fid('grp'),,,,,'maximized');
__WND.SEL.put(ZZ_HIST,_acr,_wnd);

_mode:='maximized_with_title';

:: Liczba wierszy w oknach wertowania.
_dh:=obj_new(2);
_dh[1]:=6;
_dh[2]:=6;

_komp:=ZZ_KOMP.mk_sel(,'P',0,'tmp_zz_komp',,,,1);
ZZ_KOMP.win_fld(_komp,,'NAZWA',,,50);
ZZ_KOMP.win_fld(_komp,,'WAGA',,,5);
ZZ_KOMP.win_act(_komp,,'Formuła','Usuń',,,"exec('zz_komp_usun_b','phr_widok')");
exec('menu_wlasciwosci','phr_widok',ZZ_KOMP,_komp);
ZZ_KOMP.win_act(_komp,,'Formuła','Metodyka',,'Wybór metodyki'@,"
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      params_exec('zz_hist_choose','phr_widok')
");
ZZ_KOMP.win_act(_komp,1,'Formuła','Metodyka',,'Wybór metodyki'@,"
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      params_exec('zz_hist_choose','phr_widok')
");
ZZ_KOMP.win_act(_komp,,'Okienko',,,,"exec('zz_komp_xxx_ob','phr_widok');
                                     exec('zz_komp_xxx_oa','phr_widok');
                                     cur_tab(1,1).actions(cur_win(1,1),'',,1)");
ZZ_KOMP.win_act(_komp,0,'Szukaj');
ZZ_KOMP.win_act(_komp,0,'Kolejność');
_cfg.ZZ_KOMP.ws1:=_komp;
:: Kompetencje (drzewko)
ZZ_HIST.grp_sel(_wnd,ZZ_KOMP,_cfg.ZZ_KOMP.ws1,'Stanowiska wg kompetencji'@,
   "  params_set(_par:=params_get());
      ZZ_KOMP.prefix(ZZ_POM.ZZ_HIST);
      _cfg:=_par.cfg;
      grp_disp(ZZ_KRYT,_cfg.ZZ_KRYT.ws,1,1)
   ",,,
   _dh[1],
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? _par.cfg.tab<>1
      || _par.cfg.tab:=1;
::       Ponieważ tabela ZZ_KOMP jest prezentowana w bieżącej zakładce w oknie hierarchicznym - musimy korzystać z
::       indeksu typu "drzewo", czyli indeksu NAZWA. Indeks tabeli ZZ_KOMP może być zmieniopny na zakładce drugiej.
         ZZ_KOMP.index('NAZWA')
      ?};
      {? _a
      || ZZ_POM.KREATORY:='#ZWS_PAR_PKST#ZZ_KOMP#WEO#'
      ?};
      ZZ_POM.ZZ_HIST();
      ZZ_KOMP.cntx_psh();
      ZZ_KOMP.win_sel(_cfg.ZZ_KOMP.ws1);
      ZZ_KOMP.hdr_sel();
      ZZ_KOMP.hdr_sel('Kompetencje - %1 (%2)'@[ZZ_HIST.OPIS,$ZZ_HIST.DATA]);
      ZZ_KOMP.cntx_pop();
      ZZ_KOMP.prefix(ZZ_POM.ZZ_HIST)
   ",
   "{? _a
    || ZZ_POM.KREATORY:=''
    ?}",,,
   _mode,
   _fid('zz_komp1')
);
:: Stanowiska (kryterium ogólne)
ZZ_HIST.tab_splt(_wnd,,'horizontal','zz_kryt1');
ZZ_HIST.grp_sel(_wnd,ZZ_KRYT,_cfg.ZZ_KRYT.ws,,
   "  {? ZZ_PAR.WAR_KRYT='T'
      || params_set(_par:=params_get());
         _cfg:=_par.cfg;
         grp_disp(ZZ_KOMPS,_cfg.ZZ_KOMPS.ws,1,1);
         grp_disp(ZZ_KOMPK,_cfg.ZZ_KOMPK.ws,1,1);
         grp_disp(ZZ_KOMZB,_cfg.ZZ_KOMZB.ws(),1,1)
      ?}
   ",,,
   _dh[2],
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? _a
      || ZZ_POM.KREATORY:='#ZWS_PAR_PKST#ZZ_KRYT#KOMP#';
         exec('zz_pom_psh','phr_widok',_cfg.ACT)
      ?};
      {? grp_empty(ZZ_KOMP,_cfg.ZZ_KOMP.ws1,1)
      || '#disable'
      || _kryt_fml:=$'ZZ_KRYT.ZZ_DOK';
         ZZ_KOMPS.fld_fml('ZZ_LINK','BLANK',_kryt_fml);
         ZZ_KOMPK.fld_fml('ZZ_LINK','BLANK',_kryt_fml);
         ZZ_KOMPZ.fld_fml('ZZ_LINK','BLANK',_kryt_fml);
         ZZ_KRYT.prefix(ZZ_KOMP.NP_DOK,ZZ_KOMP.ZZ_DOK,exec('typ_op','phr_dane'))
      ?}
   ",
   "  {? _a
      || exec('zz_pom_pop','phr_widok');
         ZZ_POM.KREATORY:=''
      ?}
   ",,,
   _mode,
   _fid('zz_kryt1')
);

:: Kompetencje według stanowisk (zakładka II)  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: Okno zmiany metodyki - zakładka 2
_tmpst:= STN.mk_sel(,'P',0,'tmp_zz_komp2',,,,0);
STN.win_fld(_tmpst,,'ST',,,50);
STN.win_act(_tmpst,,'Formuła','Metodyka',,'Wybór metodyki'@,"
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      params_exec('zz_hist_choose','phr_widok')
");
STN.win_act(_tmpst,1,'Formuła','Metodyka',,'Wybór metodyki'@,"
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      params_exec('zz_hist_choose','phr_widok')
");
_cfg.STN.ws:=_tmpst;
ZZ_HIST.grp_sel(_wnd,STN,_cfg.STN.ws,'Kompetencje wg stanowisk'@,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _par.cfg.tab:=2;
      grp_disp(ZZ_KOMP,_cfg.ZZ_KOMP.ws2,1,1)
   ",,,
   _dh[1],
   "
   params_set(_par:=params_get());
      _cfg:=_par.cfg;
   STN.cntx_psh();
   STN.win_sel(_cfg.STN.ws);
   STN.hdr_sel();
   STN.hdr_sel('Stanowiska - %1 (%2)'@[ZZ_HIST.OPIS,$ZZ_HIST.DATA]);
   STN.cntx_pop()
   "
   ,,,,
   _mode,
   _fid('stn')
);
:: Kompetencje (lista)
ZZ_HIST.tab_splt(_wnd,,'horizontal','zz_komp2');
ZZ_HIST.grp_sel(_wnd,ZZ_KOMP,_cfg.ZZ_KOMP.ws2,,
   "  {? ZZ_PAR.WAR_KRYT='T'
      || params_set(_par:=params_get());
         _cfg:=_par.cfg;
         grp_disp(ZZ_KOMPS,_cfg.ZZ_KOMPS.ws,1,1);
         grp_disp(ZZ_KOMPK,_cfg.ZZ_KOMPK.ws,1,1);
         grp_disp(ZZ_KOMZB,_cfg.ZZ_KOMZB.ws(),1,1)
      ?}
   ",,,
   _dh[2],
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
     {? _a
      || exec('zz_pom_psh','phr_widok',_cfg.ACT)
      ?};
         _kryt_fml:=$'exec(\\\'stn2krytdok\\\',\\\'phr_dane\\\',STN.ref(),ZZ_KOMP.ZZ_DOK)';
         ZZ_KOMPS.fld_fml('ZZ_LINK','BLANK',_kryt_fml);
         ZZ_KOMPK.fld_fml('ZZ_LINK','BLANK',_kryt_fml);
         ZZ_KOMPZ.fld_fml('ZZ_LINK','BLANK',_kryt_fml);
         exec('komp4stn','phr_dane',STN.ref(),exec('typ_op','phr_dane'))
   ",
   "  {? _a
      || exec('zz_pom_pop','phr_widok')
      ?}
   ",,,
   _mode,
   _fid('zz_komp2')
);

{? ZZ_PAR.WAR_KRYT='T'
:: Oceny - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
|| _mode:='maximized';
   ZZ_HIST.grp_splt(_wnd,,'horizontal','oceny',',66%');
:: Oceny / Skala punktowa
   ZZ_HIST.grp_sel(_wnd,ZZ_KOMPS,_cfg.ZZ_KOMPS.ws,'Skala punktowa'@,
      "   params_set(_par:=params_get());
         _cfg:=_par.cfg;
         _ag:=':';
         {? ZZ_PAR.WAR_KRYW<>'T' & ZZ_PAR.WAR_KRYN<>'T'
         || _ag:='pd:d'
         |? ZZ_PAR.WAR_KRYW<>'T' | ZZ_PAR.WAR_KRYN<>'T'
         || _ag:='d:d'
         ?};
         ZZ_KOMPS.actions_grayed(_cfg.ZZ_KOMPS.ws,_ag)
      ",,,,
      "  params_set(_par:=params_get());
         _cfg:=_par.cfg;
         {? _a & (ZZ_PAR.WAR_KRYW='T' | ZZ_PAR.WAR_KRYN='T')
         || exec('zz_pom_psh','phr_widok',_cfg.ACT)
         ?};
         {? (_cfg.tab=1 & grp_empty(ZZ_KRYT,_cfg.ZZ_KRYT.ws)) | (_cfg.tab=2 & grp_empty(ZZ_KOMP,_cfg.ZZ_KOMP.ws2))
         || '#disable'
         || _lnk:=ZZ_KOMPS.fld_fml('ZZ_LINK','*BLANK')();
            ZZ_KOMPS.prefix(ref_name(_lnk),_lnk,ZZ_KOMP.ref())
         ?}
      ",
      "  {? _a & (ZZ_PAR.WAR_KRYW='T' | ZZ_PAR.WAR_KRYN='T')
         || exec('zz_pom_pop','phr_widok')
         ?}
      ",,,
      _mode,
      _fid('zz_komps')
   );
:: Oceny / Wartości kodowane
   ZZ_HIST.grp_sel(_wnd,ZZ_KOMPK,_cfg.ZZ_KOMPK.ws,'Wartości kodowane'@,
      "  params_set(_par:=params_get());
         _cfg:=_par.cfg;
         _ag:=':';
         {? ZZ_PAR.WAR_KRYW<>'T' & ZZ_PAR.WAR_KRYN<>'T'
         || _ag:='pd:d'
         |? ZZ_PAR.WAR_KRYW<>'T' | ZZ_PAR.WAR_KRYN<>'T'
         || _ag:='d:d'
         ?};
         ZZ_KOMPK.actions_grayed(_cfg.ZZ_KOMPK.ws,_ag)
      ",,,,
      "  params_set(_par:=params_get());
         _cfg:=_par.cfg;
         {? _a & (ZZ_PAR.WAR_KRYW='T' | ZZ_PAR.WAR_KRYN='T')
         || exec('zz_pom_psh','phr_widok',_cfg.ACT)
         ?};
         {? (_cfg.tab=1 & grp_empty(ZZ_KRYT,_cfg.ZZ_KRYT.ws)) | (_cfg.tab=2 & grp_empty(ZZ_KOMP,_cfg.ZZ_KOMP.ws2))
         || '#disable'
         || _lnk:=ZZ_KOMPK.fld_fml('ZZ_LINK','*BLANK')();
            ZZ_KOMPK.prefix(ref_name(_lnk),_lnk,ZZ_KOMP.ref())
         ?}
      ",
      "  {? _a & (ZZ_PAR.WAR_KRYW='T' | ZZ_PAR.WAR_KRYN='T')
         || exec('zz_pom_pop','phr_widok')
         ?}
      ",,,
      _mode,
      _fid('zz_kompk')
   );
:: Oceny / Wartości bezpośrednie
   ZZ_HIST.grp_sel(_wnd,ZZ_KOMZB,_cfg.ZZ_KOMZB.ws(),'Wartości bezpośrednie'@,,,,,
      "  params_set(_par:=params_get());
         _cfg:=_par.cfg;
         {? (_cfg.tab=1 & grp_empty(ZZ_KRYT,_cfg.ZZ_KRYT.ws)) | (_cfg.tab=2 & grp_empty(ZZ_KOMP,_cfg.ZZ_KOMP.ws2))
         || '#disable'
         || exec('zz_komzb_fill','phr_widok',ZZ_KOMPZ.fld_fml('ZZ_LINK','*BLANK')(),ZZ_KOMP.ref())
         ?}
      ",,,,
      _mode,
      _fid('zz_komzb')
   )

?};

_wnd


\cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła przygotowuje środowisko pracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=obj_new('ACT','tab','ZZ_HIST','ZZ_KOMP','ZZ_KRYT','STN','ZZ_KOMPS','ZZ_KOMPK','ZZ_KOMZB');

_cfg.ACT:='ZWS_PAR_PKST';
_cfg.tab:=0;

_cfg.ZZ_KOMP:=obj_new('ws1','act_hid1','ws2');
_cfg.ZZ_KOMP.ws1:='WEO';
_cfg.ZZ_KOMP.act_hid1:='W';
_cfg.ZZ_KOMP.ws2:='STN';

_cfg.ZZ_KRYT:=obj_new('ws');
_cfg.ZZ_KRYT.ws:='KOMP';

_cfg.STN:=obj_new('ws');
_cfg.STN.ws:='KOMP';

_cfg.ZZ_KOMPS:=obj_new('ws');
_cfg.ZZ_KOMPS.ws:='CFG';

_cfg.ZZ_KOMPK:=obj_new('ws');
_cfg.ZZ_KOMPK.ws:='CFG';

_cfg.ZZ_KOMZB:=obj_new('ws');
_cfg.ZZ_KOMZB.ws:="__WND.SEL.get(ZZ_KOMZB,'CFG')";

_cfg.ZZ_HIST:=obj_new('ws','wnd');
_cfg.ZZ_HIST.ws:='SEL';
_cfg.ZZ_HIST.wnd:=exec('wnd','!zws_par_pkst',_cfg);

_cfg


\zz_kryt_komp_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie KOMP tabeli ZZ_KRYT.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
STN.f_set('ST',,
   '"STN".REFERENCE not in '
   '( '
      'select distinct ZZ_KRYTS.REKORD '
      'from ZZ_KRYTS join ZZ_KRYT using(ZZ_KRYTS.ZZ_KRYT,ZZ_KRYT.REFERENCE) '
      'where ZZ_KRYT.ZZ_LINK=:_a '
   ')',
   ZZ_KOMP.ZZ_DOK
);
exec('zz_kryt_dolacz_b','phr_widok');
STN.f_clear();
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 6b0a7bcf2733639c8f8cffd370bfe45b288cf9873511d110766812347411e0c6a0b4a8282238af7f0f11a5daa00f401af5063dbc6e730afcfbd5ad3e39f82da47289503b693774d678130b407cdc0208a5d69f5a8eccdaabd1af56d7ee3e0fa7b736f787fc423ef30e59c08778f8fd74040529a3d1d0d71a2c20036f10056ad9

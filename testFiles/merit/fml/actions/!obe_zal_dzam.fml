:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_zal_dzam.fml
:: Utworzony: 01.06.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_ZAL_DZAM - Zamknięcie zaliczki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamknięcie lub otwarcie zaliczki - główna formuła czynności OBE_ZAL_DZAM
::   WE: _a - 1 - zamknięcie
::            2 - otwarcie
::----------------------------------------------------------------------------------------------------------------------
_size:=EZAL.sel_size();
{? _a=1
|| {? EZAL.ZAM<>'T'
   || {? ~_size
      || EZAL.win_edit('ZAM');
         {? EZAL.edit()
         || EZAL.ZAM:='T'; EZAL.DATA_ZAM:=date(); EZAL.OPERZAM:=OPERATOR.USER;
            EZAL.cntx_psh(); EZAL.prefix(); EZAL.put(); EZAL.cntx_pop()
         ?}
      || EZAL.ZAM:='T'; EZAL.DATA_ZAM:=date(); EZAL.UWOPZAM:=uwopzam; EZAL.OPERZAM:=OPERATOR.USER;
         EZAL.cntx_psh(); EZAL.prefix();
         {? EZAL.put() || ile_zam+=1 ?};
         EZAL.cntx_pop()
      ?}
   |? ~_size
   || FUN.info('Zaliczka jest zamknięta wcześniej.'@)
   ?}
|| {? EZAL.ZAM='T'
   || _dalej:=1;
      {? EZAL.ZAM='T' & EZAL.KW_PROP$2>0 & EZAL.KW_PROP$2=EZAL.ROZCHOD$2
      || _dalej:=0;
         {? ~_size
         || FUN.info('Wypłacona kwota zaliczki jest równa kwocie zaliczki. Otwarcie zaliczki niemożliwe.'@)
         ?}
      ?};
      {? _dalej
      || {? ~_size
         || {? FUN.ask('Otworzyć zaliczkę?'@)
            || EZAL.ZAM:='N'; EZAL.DATA_ZAM:=date(0,0,0); EZAL.OPERZAM:=null;
               EZAL.cntx_psh(); EZAL.prefix(); EZAL.put(); EZAL.cntx_pop()
            ?}
         || EZAL.ZAM:='N'; EZAL.DATA_ZAM:=date(0,0,0); EZAL.UWOPZAM:=''; EZAL.OPERZAM:=null;
            EZAL.cntx_psh(); EZAL.prefix();
            {? EZAL.put() || ile_otw+=1 ?};
            EZAL.cntx_pop()
         ?}
      ?}
   |? ~_size
   || FUN.info('Zaliczka jest otwarta.'@)
   ?}
?}


\bg_zam_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie zaliczek - grupa przed
::----------------------------------------------------------------------------------------------------------------------
EZAL.win_edit('ZAM');
{? FUN.ask('Zamknąć zaznaczone zaliczki?'@) & EZAL.edit()
|| ile_zam:=0; uwopzam:=EZAL.UWOPZAM; 1
|| 0
?}


\ag_zal_dzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie zaliczek - grupa po
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba zamkniętych zaliczek: %1'@[$ile_zam]);
VAR_DEL.delete('ile_zam','uwopzam')


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
''@@


\bg_otw_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Otwieranie zaliczek - grupa przed
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Otworzyć zaznaczone zaliczki?'@)
|| ile_otw:=0; 1
|| 0
?}


\ag_otw_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Otwieranie zaliczek - grupa po
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba otwartych zaliczek: %1'@[$ile_otw]);
VAR_DEL.delete('ile_otw')

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 9180a75cdf229a91aeef704b19f1178ba8f76df35a301efd1d250038730d3da0b5db5355d941912c3b35466200f7e914a07a66b2d738cf9078d18a4c4e45e59d6b97e2cf31337269d914794c0d385d92e2389ba25adae8c7bebc712a4f05adea51b77df938deb2456bc5caf514b6a91eafa29b326f078a64d6e29708d7750978

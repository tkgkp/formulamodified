:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_zas_anat.fml
:: Utworzony: 2017.05.30
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_ZAS_ANAT - Tworzenie analiz zasobów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Formuła główna czynności ZWS_ZAS_ANAT
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS

:: PARAMETRY WE:
::# kind=WE, symbol=RODZZAS, type=STRING, name=Rodzaj zasobu, required=N

:: PARAMETRY WY:
::# kind=WY, symbol=ANZASPE, type=_ANZASPE, name=Wskazanie na nagłówek analizy, required=N, keyref=T

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

:: czynność startowa
{? _mp.pathProc()
|| menu_txt(,'Dołącz');
   _mp.trigRef('ANZASPE');
   __PARSES.setEnv('FKS',0);
   params_exec('dol_anal','!zws_zas_anat')
:: dołącz z pulpitu
|? _akcja='Dołącz'
|| menu_txt(,'Dołącz');
   _mp.trigRef('ANZASPE');
   params_exec('dol_anal','!zws_zas_anat')
:: popraw z pulpitu
|? _akcja='Popraw'
|| menu_txt(,'Popraw');
   params_get().context.ANZASPE;
   params_exec('anzaspe_pop','!zws_zas_anat')
:: usuń z pulpitu
|? _akcja='Usuń'
|| menu_txt(,'Usuń');
   params_get().context.ANZASPE;
   params_exec('usu_anal','!zws_zas_anat')
:: zakończ z pulpitu
|? _akcja='Zakończ'
|| params_get().context.ANZASPE;
   params_exec('zak_anzaspe1','!zws_zas_anat')
:: TODO
|? _mp.pathTodo()
|| menu_txt(,'Popraw');
   __PARSES.setEnv('FKS',0);
   {? var_pres('ANZASPE',_wy)>0
   || _ref:=BB.sqlint($_wy.ANZASPE);
      {? _ref<>0
      || ANZASPE.cntx_psh(); ANZASPE.prefix();
         {? ANZASPE.seek(_ref,ANZASPE.name())
         || params_exec('anzaspe_pop','!zws_zas_anat')
         ?};
         ANZASPE.cntx_pop()
      ?}
   || FUN.info('Nie znaleziono analizy zasobów.'@);
      _wy.ANZASPE:=null;
      _mp.save(,_wy);
      _mp.done()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Formuła na opis TO-DO czynności ZWS_ZAS_ANAT
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_akcja:=_mp.akcja();
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('ANZASPE',_wy) & _wy.ANZASPE
|| _ref:=BB.sqlint($_wy.ANZASPE);
   {? _ref<>0
   || {? ANZASPE.seek(_ref,ANZASPE.name())
      || _desc:='Zakończ rejestrację wykonania analizy zasobów: %1'@@[ANZASPE.OPIS]
      || _desc:='Zakończ rejestrację wykonania analizy zasobów'@@
      ?}
   || _desc:='Zakończ rejestrację wykonania analizy zasobów'@@
   ?}
|| _desc:='Zakończ rejestrację wykonania analizy zasobów'@@
?};
_desc


\dol_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Dołączanie analizy zasobów - na guzik
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_ZAS_ANAT';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';
{? var_pres('rodzzas')>0 & type_of(rodzzas)=type_of('')
|| _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'RODZZAS',rodzzas)
?};
exec('mp_run','#b__box',_params)


\pop_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Poprawianie analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_ZAS_ANAT';
_params.AKCJA:='Popraw';
_params.UIDREF:=ANZASPE.uidref();
_params.CONTEXT:=obj_new('ANZASPE');
_params.CONTEXT.ANZASPE:=ANZASPE.ref();
exec('mp_run','#b__box',_params)


\zak_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Zakończenie analizy zasobów - na guzik w okienku
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_ZAS_ANAT';
_params.AKCJA:='Zakończ';
_params.UIDREF:=ANZASPE.uidref();
_params.CONTEXT:=obj_new('ANZASPE');
_params.CONTEXT.ANZASPE:=ANZASPE.ref();
exec('mp_run','#b__box',_params)


\wyc_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wycofanie zakończenia analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
{? ANZASPE.AKC_ANAL='T'
|| FUN.info('Analiza zaakceptowana.'@)
|| ANZASPE.ZAK_REJ:='N'; ANZASPE.put()
?}


\del_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Usuwanie analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_ZAS_ANAT';
_params.AKCJA:='Usuń';
_params.UIDREF:=ANZASPE.uidref();
_params.CONTEXT:=obj_new('ANZASPE');
_params.CONTEXT.ANZASPE:=ANZASPE.ref();
exec('mp_run','#b__box',_params)


\rodz_zas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wybór rodzaju zasobu
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Zasoby'@,1,
                    'Pojazdy'@,'Karty SIM'@);
{? _choice
|| {? _choice=1
   || 'Pojazdy'
   |? _choice=2
   || 'Karty SIM'
   ?}
|| ~~
?}


\dol_anal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Dołączanie analiz zasobów
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_dalej:=1;
{? ~SSTALE.AO
|| FUN.info('Nie wybrano okresu w stałych systemu.'@); _mp.cancel(); _dalej:=0
?};
{? _dalej & var_pres('RODZZAS',_we)>0 & type_of(_we.RODZZAS)=type_of('') &
   (_we.RODZZAS='Pojazdy' | _we.RODZZAS='Karty SIM')
|| _rodzzas:=_we.RODZZAS
|| _choice:=FUN.choice('Zasoby'@,1,'Pojazdy'@,'Karty SIM'@);
   {? _choice=1
   || _rodzzas:='Pojazdy'
   |? _choice=2
   || _rodzzas:='Karty SIM'
   || _dalej:=0; _mp.cancel()
   ?}
?};
{? _dalej
|| ZM_ZASOB.KODRODZZ:={? _rodzzas='Pojazdy' || 'P' || 'S' ?};
   AN_ZAS.cntx_psh(); AN_ZAS.index('DISP2'); AN_ZAS.prefix(REF.FIRMA,ZM_ZASOB.KODRODZZ,'T');
   {? AN_ZAS.first()
   || AN_ZAS.win_sel('SLO'); AN_ZAS.win_dict('SLO');
      ANZASPE.cntx_psh(); ANZASPE.prefix();
      ANZASPE.blank(1);
      ANZASPE.OKAN:=SSTALE.AO;
      ANZASPE.ZAK_REJ:=ANZASPE.AKC_ANAL:=ANZASPE.POT:=ANZASPE.WYS_POW:='N';
      ANZASPE.DEFAN:=AN_ZAS.ref();
      ANZASPE.UTW_DATA:=date();
      ANZASPE.UTW_TIME:=time();
      ANZASPE.UTW_USER:=OPERATOR.USER;
      zak_an:=0;
      exec('anzaspe_edit','!zws_zas_anat','RED_NEW');
      {? ANZASPE.edit("exec('spr_anzaspe','!zws_zas_anat')") & ANZASPE.add(1)
      || _wy.ANZASPE:=ANZASPE.ref();
         _mp.save(,_wy);
         _mp.descTodo();
         params_exec('anzaspe_poz','zasoby_wspolne',ANZASPE.ref());
         {? zak_an
         || ANZASPE.ZAK_REJ:='T'; ANZASPE.put();
            _mp.done()
         ?}
      || _mp.cancel()
      ?};
      VAR_DEL.delete('zak_an');
      ANZASPE.cntx_pop()
   || {? ZM_ZASOB.KODRODZZ='P'
      || FUN.info('Nie znaleziono aktywnych definicji analiz zasobów typu "Pojazdy".'@)
      || FUN.info('Nie znaleziono aktywnych definicji analiz zasobów typu "Karty SIM".'@)
      ?};
      _mp.cancel()
   ?};
   AN_ZAS.cntx_pop()
?}


\spr_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Sprawdzenie parametrów analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? ~ANZASPE.DEFAN
|| FUN.info('Nie wybrano definicji analizy zasobów.'@); _zwrot:='DEFAN'
?};
{? _zwrot='' & -menu_txt()<>'popraw'
|| ANZASPE.DEFAN();
   ANZASPE.cntx_psh(); ANZASPE.index('UNIK'); ANZASPE.prefix(AN_ZAS.ref(),SSTALE.AO);
   {? ANZASPE.first()
   || _okr:=SSTALE.AO().NAZ+' '+SSTALE.AO().ROK().NAZ;
      FUN.info('Znaleziono wykonanie dla wybranej analizy zasobów w okresie %1'@[_okr]);
      _zwrot:='DEFAN'
   ?};
   ANZASPE.cntx_pop()
?};
{? _zwrot='' & ANZASPE.OPIS=''
|| FUN.info('Nie wprowadzono opisu analizy.'@); _zwrot:='OPIS'
?};
_zwrot


\usu_anal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Usuwanie analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
{? ANZASPE.get()
|| {? ANZASPE.r_lock(1,1)
   || {? FUN.ask('Usunąć analizę zasobów?'@)
      || do();
         POZAN.cntx_psh(); exec('use_pozan','zasoby_wspolne');
         POZAN.index('UNIK'); POZAN.prefix(ANZASPE.ref());
         {? POZAN.first()
         || {! |?
               _delr:=POZAN.del(,1);
               {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?};
         _mp:=params_get().mp;
         _mp.cancel();
         _delr:=ANZASPE.del(,1);
         {? _delr=0 || undo() ?};
         POZAN.cntx_pop();
         end()
      || unlock_r()
      ?}
   || FUN.info('Analiza zasobów wykorzystywana przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono analizy zasobów.'@)
?}


\rkprz_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Rekord przed dla zasobów
::----------------------------------------------------------------------------------------------------------------------
_akcje:='';
{? ANZASPE.ZAK_REJ='T' | ANZASPE.AKC_ANAL='T' || _akcje+='PUZ' ?};
{? ANZASPE.ZAK_REJ='N' || _akcje+='WAC' ?};
{? ANZASPE.ZAK_REJ='T'
|| _akcje+={? ANZASPE.AKC_ANAL='T' || 'AR' || 'C' ?}
?};
{? ANZASPE.AKC_ANAL='T' || _akcje+='W' || _akcje+='Y' ?};
{? ANZASPE.WYS_POW='T' || _akcje+='Y' || _akcje+='F' ?};
{? ANZASPE.POT='T' | ANZASPE.WYS_POW='T' || _akcje+='C' ?};
{? ANZASPE.AKC_ANAL<>'T' | ANZASPE.POT='T' || _akcje+='G' ?};
{? ANZASPE.POT<>'T' || _akcje+='T' ?};
ANZASPE.actions_grayed('WER',_akcje);
0


\ae_def_anzaspe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Po redakcji pola ANZASPE.DEFAN
::----------------------------------------------------------------------------------------------------------------------
{? ANZASPE.OPIS='' & ANZASPE.DEFAN
|| ANZASPE.OPIS:=ANZASPE.DEFAN().NAZ+' - '+SSTALE.AO().NAZ+' '+SSTALE.AO().ROK().NAZ
?};
1


\zak_anzaspe1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Zakończenie analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
{? ANZASPE.get()
|| {? ANZASPE.r_lock(1,1)
   || _args:=params_get();
      _mp:=_args.mp;
      ANZASPE.ZAK_REJ:='T'; ANZASPE.put();
      _mp.done();
      unlock_r()
   || FUN.info('Analiza zasobów wykorzystywana przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono analizy zasobów.'@)
?}


\anzaspe_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Tworzy okno redagowania analizy zasobów
::   WE: _a - akronim okienka redakcji
::----------------------------------------------------------------------------------------------------------------------
_okn:=ANZASPE.mk_edit('Analiza zasobów'@);
ANZASPE.win_ewin(_okn,,_a);
{? _a='RED_POP'
|| _btn0:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['P&ozycje'@],
                           "params_exec('poz_anzaspe','zasoby_wspolne');''"
                          );
   ANZASPE.btn_eopt(_okn,_btn0,'tooltip='+'Przeglądanie pozycji analizy zasobów'@);
   _btn1:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['P&rzelicz'@],
                           "params_exec('przel_anzaspe','zasoby_wspolne');''"
                          );
   ANZASPE.btn_eopt(_okn,_btn1,'tooltip='+'Przeliczanie analizy zasobów'@)
?};
_btnzak:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Zakończ analizę'@],
                "zak_an:=1; 'key:F2'");
ANZASPE.btn_eopt(_okn,_btnzak,'tooltip='+'"Zakończ analizę zasobów, aby była gotowa do akceptacji"'@);
_btn2:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Zapi&sz'@],"'key:F2'");
ANZASPE.btn_eopt(_okn,_btn2,'tooltip='+exec('help_red_ok','#window','Z'));
_btnan:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],'key:Esc');
ANZASPE.btn_eopt(_okn,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
ANZASPE.win_edit(_okn)


\anzaspe_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Poprawianie analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
{? ANZASPE.get()
|| {? ANZASPE.r_lock(1,1)
   || zak_an:=0;
      exec('anzaspe_edit','!zws_zas_anat','RED_POP');
      {? ANZASPE.edit("exec('spr_anzaspe','!zws_zas_anat')") & ANZASPE.put(1)
      || _args:=params_get();
         _mp:=_args.mp;
         _mp.descTodo();
         {? zak_an
         || ANZASPE.ZAK_REJ:='T'; ANZASPE.put();
            _mp.done()
         ?}
      ?};
      VAR_DEL.delete('zak_an');
      unlock_r()
   || FUN.info('Analiza zasobów wykorzystywana przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono analizy zasobów.'@)
?}


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 154b5faa49b43f349159d12d5cf6df06f2641478ecaa0d7032d55d9cd2b7947bad1c09b7be2ec513b689b47634a28facd3f358d17328912c7785fef9610a48db4fb15a22960bcf138e385255ce65285613d26ef972287c44c89474bf30dcd10ad07c252692f8f77ab3dbc49d89260c62eea09ca67ac116848bc7445916398386

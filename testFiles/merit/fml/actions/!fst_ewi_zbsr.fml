:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fst_ewi_zbsr.fml
:: Utworzony: 11.09.2017
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_ZBSR - Przegląd środków trwałych zbytych w grupie kapitałowej
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Przegląd środków trwałych zbytych w grupie kapitałowej - główna formuła czynności FST_EWI_ZBSR.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
:: parametry domyślnie wyświetlania
exec('srsfirma','!fst_ewi_zbsr');
1


\amortp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Amortyzacja środka zbytego, w firmie pierwotnej
::----------------------------------------------------------------------------------------------------------------------
_firma:=(ref_name( __T_SRSF.ORG_REF))+3;
_ref_firma:=null;
{? +_firma=3
|| FIRMA.index('SYMBOL');
   FIRMA.prefix(_firma,_firma);
   {? FIRMA.first()
   || _ref_firma:=REF.FIRMA;
      REF.FIRMA:=FIRMA.ref()
   ?}
?};
{? _ref_firma
|| exec('fst_cntx_psh','srodobj');
   exec('maski','srodobj');
   SRSR.prefix();
   {? SRSR.seek( __T_SRSF.ORG_REF,ref_name( __T_SRSF.ORG_REF))
   || SRST.index('PODAT');
      SRST.prefix(SRSR.ref());
      {? SRST.first()
      || exec('srsr_tab_amo','fst','READONLY')
      || FUN.emsg('Brak danych amortyzacji dla środka w pierwotnej firmie.'@)
      ?}
   || FUN.emsg('Nie znaleziono środka w pierwotnej firmie.'@)
   ?};
   exec('fst_cntx_pop','srodobj')
|| FUN.emsg('Nie udało się ustalić firmy pierwotnej dla środka.'@)
?}


\amortn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Amortyzacja środka zbytego, w firmie w której nabyto środek
::----------------------------------------------------------------------------------------------------------------------
{? __T_SRSF.NRI_D=''
|| FUN.info('Środek jeszcze nie został nabyty.'@);
   return(0)
?};
_firma:=__T_SRSF.FIRMA_D;
_ref_firma:=null;
{? +_firma=3
|| FIRMA.index('SYMBOL');
   FIRMA.prefix(_firma,_firma);
   {? FIRMA.first()
   || _ref_firma:=REF.FIRMA;
      REF.FIRMA:=FIRMA.ref()
   ?}
?};
{? _ref_firma
|| exec('fst_cntx_psh','srodobj');
   exec('maski','srodobj');
   SRSR.prefix();
   {? SRSR.seek(__T_SRSF.SRSR_D,ref_name( __T_SRSF.SRSR_D))
   || SRST.index('PODAT');
      SRST.prefix(SRSR.ref());
      {? SRST.first()
      || exec('srsr_tab_amo','fst','READONLY')
      || FUN.emsg('Brak danych amortyzacji dla środka w docelowej firmie.'@)
      ?}
   || FUN.emsg('Nie znaleziono środka w bieżącej firmie.'@)
   ?};
   exec('fst_cntx_pop','srodobj')
|| FUN.emsg('Nie udało się ustalić firmy bieżącej dla środka.'@)
?}


\zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Zakres wyświetlanych zapisów zbycia: wszyskie\tylko zbyte w firmie pierwotnej
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.win_edit('ZB_PAR');
{? EDIT_ES.ZB_TRYB='Z'
|| EDIT_ES.efld_opt(EDIT_ES.win_edit('?'),'enable=1',,'ZB_ZAKR')
|? EDIT_ES.ZB_TRYB='S'
|| EDIT_ES.efld_opt(EDIT_ES.win_edit('?'),'enable=0',,'ZB_ZAKR')
?};
{? EDIT_ES.edit("exec('chk_zakres','!fst_ewi_zbsr')") || sel_exit() ?}


\chk_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Kontrola zakresu wyświetlanych zapisów zbycia
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? EDIT_ES.ZB_OD<>date(0,0,0) & EDIT_ES.ZB_DO<>date(0,0,0) & EDIT_ES.ZB_OD>EDIT_ES.ZB_DO
|| FUN.emsg('Data OD nie powinna być późniejsza niż data DO.'@);
   _wy:='ZB_OD'
?};
_wy


\srszapisy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła wyświetla listę zapisów zbycia
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__T_SRSF');
SRSFIRMA.index('SRDO');
SRSFIRMA.prefix();

__T_SRSF:=tab_tmp(2,'DATA_Z','DATE','Data zbycia'@,
                    'FIRMA_Z','STRING[10]','Firma zbywająca'@,
                    'DATA_D','DATE','Data przyjęcia'@,
                    'FIRMA_D','STRING[10]','Firma docelowa'@,
                    'SRDO_Z','STRING[30]','Symbol dokumentu zbycia'@,
                    'NRI_Z','STRING[18]','Nr w firmie zbywającej'@,
                    'NRI_D','STRING[18]','Nr docelowy'@,
                    'NST_Z','STRING[100]','Nazwa w firmie zbywającej'@,
                    'NST_D','STRING[100]','Nazwa docelowa'@,
                    'ORG_REF','STRING[48]','Środek pierwotny'@,
                    'SRSR_D','STRING[48]','Środek docelowy'@,
                    'SRSR_Z','STRING[48]','Środek zbywany'@,
                    'OKR_ZB','STRING[25]','Okres zbycia'@);
{? _>0 & type_of(_a)=type_of(0) & _a=1 || _tryb:='maximized' || _tryb:='normal' ?};
_win:=__T_SRSF.mk_sel('','P',0,'_t_srsf',10,5,15,0,'U',,,,,_tryb);
__T_SRSF.win_fld(_win,,'DATA_Z',,,10,,,'Data zbycia'@,,'Data zbycia środka'@);
__T_SRSF.win_fld(_win,,'OKR_ZB',,,20,,,'Okres zbycia'@,,'Okres obowiązywania zbycia środka'@);
__T_SRSF.win_fld(_win,,'FIRMA_Z',,,8,,,'Firma zbywająca'@,,'Symbol firmy zbywającej środek'@);
__T_SRSF.win_fld(_win,,'DATA_D',,,10,,,'Data przyjęcia'@,,'Data przyjęcia w firmie docelowej'@);
__T_SRSF.win_fld(_win,,'FIRMA_D',,,8,,,'Firma docelowa'@,,'Data przyjęcia środka w firmie docelowej'@);
__T_SRSF.win_fld(_win,,'SRDO_Z',,,10,,,'Dokument zbycia'@,,'Symbol dokumentu zbycia środka'@);
__T_SRSF.win_fld(_win,,'NRI_Z',,,18,,,'Nr pierwotnie'@,,'Nr inwentarzowy w firmie zbywającej'@);
__T_SRSF.win_fld(_win,,'NRI_D',,,18,,,'Nr docelowy'@,,'Nr inwentarzowy w firmie docelowej'@);
__T_SRSF.win_act(_win,,'Wyświetl',,,,,"__T_SRSF.display()");
__T_SRSF.win_act(_win,,'Formuła','Amortyzacja pierwotna'@,,'Amortyzacja w firmie pierwotnej'@,,"exec('amortp','!fst_ewi_zbsr')",,,,,'A');
__T_SRSF.win_act(_win,,'Formuła','Amortyzacja doce&lowa'@,,'Amortyzacja w firmie docelowej'@,,"exec('amortn','!fst_ewi_zbsr')",,,,,'L');
__T_SRSF.win_act(_win,,'Formuła','Parametry wyświetlania'@,,'Zakres wyświetlanych zapisów zbycia'@,,"exec('zakres','!fst_ewi_zbsr')",,,,,'W');
__T_SRSF.win_act(_win,1,'Formuła','Parametry wyświetlania'@,,'Zakres wyświetlanych zapisów zbycia'@,,"exec('zakres','!fst_ewi_zbsr')",1,,,,'W');
__T_SRSF.win_act(_win,,'Formuła','Druku&j'@,,'Raporty dotyczące środków zbytych w grupie'@,,"exec('drukuj','!fst_ewi_zbsr')",,,,,'J',,'icon=print');
__T_SRSF.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Amortyzacja pierwotna'@],'menu:A',,,,,,'');
__T_SRSF.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Amortyzacja doce&lowa'@],'menu:L',,,,,,'');
__T_SRSF.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Parametry wyświetlania'@],'menu:W',,,,,,'');

__T_SRSF.win_sel(_win);
__T_SRSF.hdr_sel();
__T_SRSF.hdr_sel(': wszystkie zapisy zbycia'@);

_red:=__T_SRSF.mk_edit('Dane dotyczące zbycia środka'@,0,'_srsf_edi',12,7);
__T_SRSF.win_efld(_red,__T_SRSF,'DATA_Z',,,10,,,'Data zbycia'@,,'Data zbycia środka'@);
__T_SRSF.win_efld(_red,__T_SRSF,'OKR_ZB',,,20,,,'Okres zbycia'@,,'Okres obowiązywania zbycia środka'@);
__T_SRSF.win_efld(_red,__T_SRSF,'FIRMA_Z',,,8,,,'Firma zbywająca'@,,'Symbol firmy zbywającej środek'@);
__T_SRSF.win_efld(_red,__T_SRSF,'SRDO_Z',,,10,,,'Dokument zbycia'@,,'Symbol dokumentu zbycia środka'@);
__T_SRSF.win_efld(_red,__T_SRSF,'NRI_Z',,,18,,,'Nr pierwotnie'@,,'Nr inwentarzowy w firmie zbywającej'@);
__T_SRSF.win_efld(_red,__T_SRSF,'NST_Z',,,40,,,'Nazwa pierwotnie'@,,'Nazwa w firmie zbywającej'@);
__T_SRSF.win_efld(_red,__T_SRSF,'DATA_D',,,10,,,'Data przyjęcia'@,,'Data przyjęcia środka w nowej firmie'@);
__T_SRSF.win_efld(_red,__T_SRSF,'FIRMA_D',,,8,,,'Firma docelowa'@,,'Symbol firmy przyjmującej środek'@);
__T_SRSF.win_efld(_red,__T_SRSF,'NRI_D',,,18,,,'Nr docelowy'@,,'Nr inwentarzowy w firmie przyjmującej'@);
__T_SRSF.win_efld(_red,__T_SRSF,'NST_D',,,40,,,'Nazwa docelowy'@,,'Nazwa w firmie przyjmującej'@);
__T_SRSF.win_edit(_red)


\srsfirma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła wyświetla listę środków sprzedanych w obrębie grupy kapitałowej
::   WE: [_a]  - tryb wyświetlania, jeżeli 1 to zmaksymalizowany, jeżeli brak lub <> 1 to normalny
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.SPR_GRP<>'T'
|| FUN.emsg('System nie jest skonfigurowany do obsługi zbycia środków w obrębie grupy kapitałowej.'@);
   return(0)
?};

{? _=0 || _a:=0 ?};
EDIT_ES.ZB_TRYB:='Z';
EDIT_ES.ZB_ZAKR:='W';
EDIT_ES.ZB_OD:=date(0,0,0);
EDIT_ES.ZB_DO:=date(0,0,0);

exec('srszapisy','!fst_ewi_zbsr',_a);
exec('refresh','!fst_ewi_zbsr',1);
__T_SRSF.hdr_sel(); __T_SRSF.hdr_sel('Wszystkie zapisy zbycia środków'@);

{? __T_SRSF.first()
|| {! |?
      {? __T_SRSF.select() || _dalej:=1 || _dalej:=0 ?};
      {? _dalej
      || {? EDIT_ES.ZB_TRYB='Z'
         || exec('srszapisy','!fst_ewi_zbsr',_a);
            {? EDIT_ES.ZB_OD<>date(0,0,0) || _od:=' od %1'@[$EDIT_ES.ZB_OD] || _od:='' ?};
            {? EDIT_ES.ZB_DO<>date(0,0,0) || _do:=' do %1'@[$EDIT_ES.ZB_DO] || _do:='' ?};
            {? EDIT_ES.ZB_ZAKR='W'
            || __T_SRSF.hdr_sel(); __T_SRSF.hdr_sel('Wszystkie zapisy zbycia środków %1 %2'@[_od,_do]);
               exec('refresh','!fst_ewi_zbsr',1)
            |? EDIT_ES.ZB_ZAKR='P'
            || __T_SRSF.hdr_sel(); __T_SRSF.hdr_sel('Tylko zapisy zbycia środków z pierwotnych firm %1 %2'@[_od,_do]);
               exec('refresh','!fst_ewi_zbsr',2)
            ?}
         |? EDIT_ES.ZB_TRYB='S'
         || exec('srsamort','!fst_ewi_zbsr',_a);
            {? EDIT_ES.ZB_OD<>date(0,0,0) || _od:=' od %1'@[$EDIT_ES.ZB_OD] || _od:='' ?};
            {? EDIT_ES.ZB_DO<>date(0,0,0) || _do:=' do %1'@[$EDIT_ES.ZB_DO] || _do:='' ?};
            _okres:=SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ;
            __T_SRSF.hdr_sel(); __T_SRSF.hdr_sel('Zbyte środki trwałe %1 %2, amortyzacja za okres: %3'@[_od,_do,_okres]);
            exec('refresh','!fst_ewi_zbsr',3)
         ?}
      ?};
      _dalej
   !}
|| FUN.info('W systemie brak danych o środkach sprzedanych w obrębie grupy kapitałowej.'@)
?}


\refresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Odświeżenie zawartości kartoteki środków zbytych zgodnie z ustalonym zakresem
::   WE: _a = 1 - wszystkie, _a = 2 - tylko zbyte z pierwotnej firmy, _c - środki zbyte
::       _b = zakres dat od
::       _c = zakres dat do
::----------------------------------------------------------------------------------------------------------------------
{? __T_SRSF.first() || __T_SRSF.erase() ?};
{? SRSFIRMA.first()
||
   SRDO.cntx_psh(); SRSR.cntx_psh(); SRST.cntx_psh();
   SRDO.prefix(); SRST.prefix();
   {? _a<3
   || {! |?
         {? (_a=1 | (_a=2 & SRSFIRMA.SRSR_Z=SRSFIRMA.ORG_REF))
            & (EDIT_ES.ZB_OD=date(0,0,0) | SRSFIRMA.DATA_Z>=EDIT_ES.ZB_OD)
            & (EDIT_ES.ZB_DO=date(0,0,0) | SRSFIRMA.DATA_Z<=EDIT_ES.ZB_DO)
         || __T_SRSF.blank();
            __T_SRSF.DATA_Z:=SRSFIRMA.DATA_Z;
            __T_SRSF.FIRMA_Z:=SRSFIRMA.FIRMA_Z().SYMBOL;
            __T_SRSF.DATA_D:=SRSFIRMA.DATA_D;
            __T_SRSF.FIRMA_D:=SRSFIRMA.FIRMA_D().SYMBOL;
            __T_SRSF.ORG_REF:=SRSFIRMA.ORG_REF;
            __T_SRSF.SRSR_D:=SRSFIRMA.SRSR_D;
            __T_SRSF.SRSR_Z:=SRSFIRMA.SRSR_Z;

            {? SRDO.seek(SRSFIRMA.SRDO_REF,ref_name(SRSFIRMA.SRDO_REF))
            || __T_SRSF.SRDO_Z:=SRDO.SYMBOL;
               __T_SRSF.OKR_ZB:=SRDO.OKRO_F().NAZ+' '+SRDO.OKRO_F().ROK().NAZ
            ?};
            {? SRSR.seek(SRSFIRMA.SRSR_Z,ref_name(SRSFIRMA.SRSR_Z))
            || __T_SRSF.NRI_Z:=SRSR.NRI;
               __T_SRSF.NST_Z:=SRSR.NST
            ?};
            {? SRSR.seek(SRSFIRMA.SRSR_D,ref_name(SRSFIRMA.SRSR_D))
            || __T_SRSF.NRI_D:=SRSR.NRI;
               __T_SRSF.NST_D:=SRSR.NST
            ?};
            __T_SRSF.add()
         ?};
         SRSFIRMA.next()
      !}
   |? _a=3
   || {! |?
         {? SRSFIRMA.ORG_REF=SRSFIRMA.SRSR_Z & ~__T_SRSF.find_key(SRSFIRMA.ORG_REF)
            & (EDIT_ES.ZB_OD=date(0,0,0) | SRSFIRMA.DATA_Z>=EDIT_ES.ZB_OD)
            & (EDIT_ES.ZB_DO=date(0,0,0) | SRSFIRMA.DATA_Z<=EDIT_ES.ZB_DO)
         || __T_SRSF.blank();
            __T_SRSF.DATA_Z:=SRSFIRMA.DATA_Z;
            __T_SRSF.FIRMA_Z:=SRSFIRMA.FIRMA_Z().SYMBOL;
            __T_SRSF.DATA_D:=SRSFIRMA.DATA_D;
            __T_SRSF.FIRMA_D:=SRSFIRMA.FIRMA_D().SYMBOL;
            __T_SRSF.ORG_REF:=SRSFIRMA.ORG_REF;

            {? SRDO.seek(SRSFIRMA.SRDO_REF,ref_name(SRSFIRMA.SRDO_REF))
            || __T_SRSF.OKR_ZB:=SRDO.OKRO_F().NAZ+' '+SRDO.OKRO_F().ROK().NAZ
            ?};
            {? SRSR.seek(SRSFIRMA.SRSR_D,ref_name(SRSFIRMA.SRSR_D))
            || __T_SRSF.NRI_D:=SRSR.NRI;
               __T_SRSF.NST_D:=SRSR.NST;
               SRST.index('SROD');
               SRST.prefix(SRSR.ref(),SSTALE.AR,SSTALE.AO);
               {? SRST.first()
               || __T_SRSF.AMOR_B:=SRST.AMOP;
                  __T_SRSF.AMOR_P:=SRST.AMOF
               ?};
               __T_SRSF.SRSR_D:=SRSR.uidref()
            ?};
            __T_SRSF.add();
:: kontrola czy nie było kolejnej sprzedazy w grupie dla tego środka
            SRSFIRMA.cntx_psh();
            SRSFIRMA.index('ORGDATA');
            SRSFIRMA.prefix(__T_SRSF.ORG_REF);
            {? SRSFIRMA.last() & SRSFIRMA.SRSR_D<>__T_SRSF.SRSR_D
            || __T_SRSF.DATA_D:=SRSFIRMA.DATA_D;
               __T_SRSF.FIRMA_D:=SRSFIRMA.FIRMA_D().SYMBOL;
               {? SRSR.seek(SRSFIRMA.SRSR_D,ref_name(SRSFIRMA.SRSR_D))
               || __T_SRSF.NRI_D:=SRSR.NRI;
                  __T_SRSF.NST_D:=SRSR.NST;
                  SRST.index('SROD');
                  SRST.prefix(SRSR.ref(),SSTALE.AR,SSTALE.AO);
                  {? SRST.first()
                  || __T_SRSF.AMOR_B:=SRST.AMOP;
                     __T_SRSF.AMOR_P:=SRST.AMOF
                  ?};
                  __T_SRSF.SRSR_D:=SRSR.uidref()
               ?};
               __T_SRSF.put()
            ?};
            SRSFIRMA.cntx_pop()
         ?};
         SRSFIRMA.next()
      !};
:: weryfikacja listy środków - wyświetlane tylko nabyte
      __T_SRSF.prefix();
      __T_SRSF.for_each("{? __T_SRSF.DATA_D=date(0,0,0) || __T_SRSF.del() ?}")
   ?};
   SRDO.cntx_pop(); SRSR.cntx_pop(); SRST.cntx_pop()
?};
__T_SRSF.first()


\ae_zb_tryb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła po redakcji pola EDIT_ES.ZB_TRYB
::----------------------------------------------------------------------------------------------------------------------
{? EDIT_ES.ZB_TRYB='Z'
|| EDIT_ES.efld_opt(EDIT_ES.win_edit('?'),'enable=1',,'ZB_ZAKR');
   win_disp()
|? EDIT_ES.ZB_TRYB='S'
|| EDIT_ES.efld_opt(EDIT_ES.win_edit('?'),'enable=0',,'ZB_ZAKR');
   win_disp()
?};
1


\be_zb_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła przed redagowaniem pola EDIT_ES.ZB_ZAKR
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? EDIT_ES.ZB_TRYB='Z' || 1 || 0 ?}


\drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Raporty związane z zapisami zbycia środków w grupie kapitałowej
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FST_EWI_ZBSR','fst_grp_*')


\srsamort
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła wyświetla listę środków zbytych z pierwotnych firm wraz z amortyzacją w firmie pierwotnej i w bieżącej
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__T_SRSF');
SRSFIRMA.index('SRDO');
SRSFIRMA.prefix();

__T_SRSF:=tab_tmp(3,'ORG_REF','STRING[48]','Środek pierwotny'@,
                    'FIRMA_Z','STRING[10]','Firma pierwotna'@,
                    'DATA_Z','DATE','Data zbycia'@,
                    'FIRMA_D','STRING[10]','Firma bieżąca'@,
                    'DATA_D','DATE','Data nabycia'@,
                    'NRI_D','STRING[18]','Nr inwentarzowy'@,
                    'NST_D','STRING[100]','Nazwa'@,
                    'SRSR_D','STRING[48]','Środek docelowy'@,
                    'AMOR_P','REAL','Amortyzacja pierwotna'@,
                    'AMOR_B','REAL','Amortyzacja bieżąca'@,
                    'OKR_ZB','STRING[25]','Okres zbycia'@
                    );

{? _>0 & type_of(_a)=type_of(0) & _a=1 || _tryb:='maximized' || _tryb:='normal' ?};
_win:=__T_SRSF.mk_sel('','P',0,'_t_srsf',10,5,15,0,'U',,,,,_tryb);
__T_SRSF.win_fld(_win,,'DATA_Z',,,10,,,'Data zbycia'@,,'Data zbycia środka z firmy pierwotnej'@);
__T_SRSF.win_fld(_win,,'FIRMA_Z',,,8,,,'Firma pierwotna'@,,'Symbol firmy pierwotnej dla środka'@);
__T_SRSF.win_fld(_win,,'DATA_D',,,10,,,'Data nabycia'@,,'Data nabycia w firmie bieżącej'@);
__T_SRSF.win_fld(_win,,'FIRMA_D',,,8,,,'Firma bieżąca'@,,'Symbol firmy bieżącej środka'@);
__T_SRSF.win_fld(_win,,'NRI_D',,,18,,,'Nr inwentarzowy'@,,'Nr inwentarzowy w firmie bieżącej'@);
__T_SRSF.win_fld(_win,,'NST_D',,,25,,,'Nazwa'@,,'Nazwa w firmie bieżącej'@);
__T_SRSF.win_fld(_win,,'AMOR_P',,,12,,,'Amortyzacja pierwotna'@,,'Amortyzacja w firmie pierwotnej'@);
__T_SRSF.win_fld(_win,,'AMOR_B',,,12,,,'Amortyzacja bieżąca'@,,'Amortyzacja w firmie bieżącej'@);

__T_SRSF.win_act(_win,,'Wyświetl',,,,,"__T_SRSF.display()");
__T_SRSF.win_act(_win,,'Formuła','Parametry wyświetlania'@,,'Parametry wyświetlania'@,,"exec('zakres','!fst_ewi_zbsr')",,,,,'W');
__T_SRSF.win_act(_win,1,'Formuła','Parametry wyświetlania'@,,'Parametry wyświetlania'@,,"exec('zakres','!fst_ewi_zbsr')",1,,,,'W');
__T_SRSF.win_act(_win,,'Formuła','Amortyzacja'@,,'Amortyzacja'@,,"exec('amortyzacja','!fst_ewi_zbsr')",1,,,,'A');
__T_SRSF.win_act(_win,,'Formuła','Druku&j'@,,'Raporty dotyczące środków zbytych w grupie'@,,"exec('drukuj','!fst_ewi_zbsr')",,,,,'J',,'icon=print');
__T_SRSF.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Amortyzacja'@],'menu:A',,,,,,'');
__T_SRSF.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Parametry wyświetlania'@],'menu:W',,,,,,'');

__T_SRSF.win_sel(_win);
_okres:=SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ;
__T_SRSF.hdr_sel(); __T_SRSF.hdr_sel('Środki zbyte w grupie, amortyzacja za okres %1'@[_okres]);

_red:=__T_SRSF.mk_edit('Dane dotyczące środka'@,0,'_srsf_edi_am',12,7);
__T_SRSF.win_efld(_red,__T_SRSF,'DATA_Z',,,10,,,'Data zbycia'@,,'Data zbycia środka'@);
__T_SRSF.win_efld(_red,__T_SRSF,'OKR_ZB',,,20,,,'Okres zbycia'@,,'Okres obowiązywania zbycia środka'@);
__T_SRSF.win_efld(_red,__T_SRSF,'FIRMA_Z',,,8,,,'Firma zbywająca'@,,'Symbol firmy zbywającej środek'@);
__T_SRSF.win_efld(_red,__T_SRSF,'DATA_D',,,10,,,'Data nabycia'@,,'Data nabycia środka w nowej firmie'@);
__T_SRSF.win_efld(_red,__T_SRSF,'FIRMA_D',,,8,,,'Firma docelowa'@,,'Symbol firmy przyjmującej środek'@);
__T_SRSF.win_efld(_red,__T_SRSF,'NRI_D',,,18,,,'Nr docelowy'@,,'Nr inwentarzowy w firmie przyjmującej'@);
__T_SRSF.win_efld(_red,__T_SRSF,'NST_D',,,40,,,'Nazwa docelowa'@,,'Nazwa w firmie przyjmującej'@);
__T_SRSF.win_efld(_red,__T_SRSF,'AMOR_P',,,12,2,,'Amortyzacja pierwotna'@,,'Amortyzacja w firmie pierwotnej'@);
__T_SRSF.win_efld(_red,__T_SRSF,'AMOR_B',,,12,2,,'Amortyzacja bieżąca'@,,'Amortyzacja w firmie bieżącej'@);

__T_SRSF.win_edit(_red)


\amortyzacja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła wyświetla amortyzację środka w firmie pierwotnej i w firmie bieżącej
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__T_AMOR');
__T_AMOR:=tab_tmp(2,'ROK','INTEGER','Rok'@,
                    'OKRES','INTEGER','Okres'@,
                    'AMOP_P','REAL','Amortyzacja pierwotna podatkowa'@,
                    'AMOP_B','REAL','Amortyzacja bieżąca podatkowa'@,
                    'AMOP_R','REAL','Różnica podatkowa'@,
                    'AMOF_P','REAL','Amortyzacja pierwotna bilansowa'@,
                    'AMOF_B','REAL','Amortyzacja bieżąca bilansowa'@,
                    'AMOF_R','REAL','Różnica bilansowa'@,
                    'P','STRING[1]','Tylko w firmie pierwotnej'@
                    );

_win:=__T_AMOR.mk_sel('Amortyzacja środka %1 w firmie pierwotnej i w bieżącej'@[__T_SRSF.NRI_D],'P',0,'_t_amor',10,5,15,0,'U',,,,,'normal');
__T_AMOR.win_fld(_win,,'ROK',,,6,,,'Rok'@,,'Data zbycia środka z firmy pierwotnej'@);
__T_AMOR.win_fld(_win,,'OKRES',,,6,,,'Okres'@,,'Symbol firmy pierwotnej dla środka'@);
__T_AMOR.win_fld(_win,,'AMOP_P',,,12,2,,'Amortyzacja pod. pierwotna'@,,'Amortyzacja podatkowa w firmie pierwotnej'@);
__T_AMOR.win_fld(_win,,'AMOP_B',,,12,2,,'Amortyzacja pod. bieżąca'@,,'Amortyzacja podatkowa w firmie bieżącej'@);
__T_AMOR.win_fld(_win,,'AMOP_R',,,12,2,,'Różnica podatkowa'@,,'Różnica dla amortyzacji podatkowej'@);
__T_AMOR.win_fld(_win,,'AMOF_P',,,12,2,,'Amortyzacja bil. pierwotna'@,,'Amortyzacja bilansowa w firmie pierwotnej'@);
__T_AMOR.win_fld(_win,,'AMOF_B',,,12,2,,'Amortyzacja bil. bieżąca'@,,'Amortyzacja bilansowa w firmie bieżącej'@);
__T_AMOR.win_fld(_win,,'AMOF_R',,,12,2,,'Różnica bilansowa'@,,'Różnica dla amortyzacji bilansowej'@);

__T_AMOR.win_act(_win,,'Formuła','Druku&j'@,,'Wydruk amortyzacji dla bieżącego środka'@,,"exec('cur_drukuj','!fst_ewi_zbsr')",1,,,,'J',,'icon=print');
__T_AMOR.win_act(_win,,'Wyświetl',,,,,"__T_AMOR.display()");
__T_AMOR.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Drukuj'@],'menu:J',,,,,,'');
_form:="{? __T_AMOR.P='T' || {? var_pres('Color')>0 || Color.fnd_kol(Color.id_nrd) || '' ?} || 1 ?}";
__T_AMOR.fld_fml('AMOP_R','BEFORE_DISPLAY',_form);
__T_AMOR.fld_fml('AMOF_R','BEFORE_DISPLAY',_form);
__T_AMOR.fld_fml('AMOP_B','BEFORE_DISPLAY',_form);
__T_AMOR.fld_fml('AMOF_B','BEFORE_DISPLAY',_form);
__T_AMOR.win_sel(_win);

_red:=__T_AMOR.mk_edit('Amortyzacja środka'@,1,'_amor_edi_am',12,7);
__T_AMOR.win_edit(_red);

SRST.cntx_psh(); SRSR.cntx_psh();
SRSR.prefix();
{? SRSR.seek(__T_SRSF.SRSR_D,ref_name(__T_SRSF.SRSR_D))
|| {? SRST.name()<>'srstr'+(SRSR.name()+3) || SRST.use('srstr'+(SRSR.name()+3)) ?};
   SRST.index('PODAT');
   SRST.prefix(SRSR.ref());
   {? SRST.first()
   || {! |?
         {? SRST.OKRO_F().POCZ<>date(0,0,0)
         || {? ~__T_AMOR.find_key(SRST.ROK,SRST.OKRES)
            || __T_AMOR.blank();
               __T_AMOR.P:='N';
               __T_AMOR.ROK:=SRST.ROK;
               __T_AMOR.OKRES:=SRST.OKRES;
               __T_AMOR.AMOP_B:=SRST.AMOP;
               __T_AMOR.AMOF_B:=SRST.AMOF;
               __T_AMOR.add()
            ?}
         ?};
         SRST.next()
      !}
   ?}
?};
{? SRSR.seek(__T_SRSF.ORG_REF,ref_name(__T_SRSF.ORG_REF))
|| {? SRST.name()<>'srstr'+(SRSR.name()+3) || SRST.use('srstr'+(SRSR.name()+3)) ?};
   SRST.index('PODAT');
   SRST.prefix(SRSR.ref());
   {? SRST.first()
   || {! |?
         {? SRST.OKRO_F().POCZ<>date(0,0,0)
         || {? ~__T_AMOR.find_key(SRST.ROK,SRST.OKRES)
            || __T_AMOR.blank();
               __T_AMOR.ROK:=SRST.ROK;
               __T_AMOR.OKRES:=SRST.OKRES;
               __T_AMOR.AMOP_P:=SRST.AMOP;
               __T_AMOR.AMOF_P:=SRST.AMOF;
               __T_AMOR.P:='T';
               __T_AMOR.add()
            || __T_AMOR.AMOP_P:=SRST.AMOP;
               __T_AMOR.AMOF_P:=SRST.AMOF;
               __T_AMOR.AMOP_R:=__T_AMOR.AMOP_B-__T_AMOR.AMOP_P;
               __T_AMOR.AMOF_R:=__T_AMOR.AMOF_B-__T_AMOR.AMOF_P;
               __T_AMOR.put()
            ?}
         ?};
         SRST.next()
      !}
   ?}
?};

SRST.cntx_pop(); SRSR.cntx_pop();

__T_AMOR.select()


\cur_drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Drukowanie amortyzacji bieżącego środka zbytego w grupie
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FST_EWI_ZBSR','fst_cur_grp_amor',,,,,,,,'W')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 90e4bbb2ddae537e121114f87c3d874328357cfe69a8659837aee29d5378c02038df91b64a687ddb1332ea7db3c17800e776bbbae7bad86bc0fc3641804bd3f7f6a29c42b80603493c3a9d2dea919dd83c450068e7b38b4d3c758e7883f61dce7c1ee648794b212ace9be9c44dac5ba4f9d4b42efc96ffe30c3d16565923ab56

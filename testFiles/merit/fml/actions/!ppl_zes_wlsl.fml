:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zes_wlsl.fml
:: Utworzony: 11.05.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PPL_ZES_WLSL - Wydruk listy płac
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wydruk listy płac - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=O, type=_O, name=Wskazanie listy płac, required=T, keyref=T
::
::# permissions=F_ZATR,UD_SKL

_par:=params_get();

: czynność może być wywołana z listy raportów na pulpicie
{? type_of(_par)=type_of(~~) |
   var_pres('in',_par)<=0 |
   var_pres('mp',_par)<=0
||
:  wykonuj jako pełnoprawną czynność
   _O:=__PARSES.getVal('ListaPłac').REF;
   {? _O<>null
   || exec('ppl_zes_wlsl','ppl',_O)
   || FUN.emsg(
         'W parametrach pracy nie określono listy płac.\n%1'@[exec('error','!ppl_zes_wlsl')]
      )
   ?};
   return()
?};

params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.O);
_result:='';

{? _id=''
|| _result:=exec('error','!ppl_zes_wlsl')

|| _mp.save(_ib,_rv);
   _mp.keyRef(_id,0,0);
   _value:=exec('print','!ppl_zes_wlsl',_in.O);
   {? type_of(_value)=type_of(0)
   || {? _mp.isMicro()
      || _mp.cancel()
      || {? _value=1
         || _mp.done()
         || _mp.keep()
         ?}
      ?}
   || _result:=_value
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wydruk listy płac - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().mp.load(exec('kind_in','#b_port'));
_lt:='';

SEEK.O:=_in.O;
{? SEEK.O<>null
|| _lt+=~SEEK.O().LT
?};

{? +_lt
|| 'Wydruk listy płac %1'@@[_lt]
|| 'Wydruk listy płac'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca treść komunikatu błędu.
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Wydruk listy płac niemożliwy.'@


\print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wydruk listy płac.
::   WE: _a [_O] - wskazanie nagłówka listy płac
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SEEK.O:=_a;
SEEK.O();

FUNKCJE.OTWOLIST();

_todo:=1;
{? params_get().mp.pathTodo()
|| _todo:=FUN.choice('Wydruk listy płac %1'@[~-O.LT],,'Zakończ','Drukuj')
?};

{? _todo<>0
|| rep_exec('ppl_listplac')
?};

_todo

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 868f5a10a96b612c3fe45db2782a39c747fb1e1304b2ef558122009f99b80f444064cfa14decc8fb75a6cefeeed7e4074ae3c9f23df0af8f31b4d8ab4a375191655765f34fe2b7fdfe1a750919e16c455bf3d6ef22cb7c890eaccbc5ce2ac150ecc379e372449109157d2bef9f4aad55fd42db3c02d9c129719a9d4357369db3

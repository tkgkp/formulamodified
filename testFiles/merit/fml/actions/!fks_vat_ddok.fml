:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_vat_ddok.fml
:: Utworzony: 07.07.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VAT_DDOK - Wycofaj z VAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wycofaj z VAT - główna formuła czynności FKS_VAT_DDOK.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\wyc_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Akcja 'Wycofaj z VAT' w okienkach wertowania tabeli DVAT
::   WY: czy wycofano dokument z VAT? 1-tak 0-nie
::  OLD: \wyc_vat/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
KOMT:='';
_auto:=null;
PVAT.prefix(DVAT.ref);
_fkn:=0; PVAT.cntx_psh();{? PVAT.first() || {! |? {? PVAT.KNAG=2 || _fkn:=1;0 || PVAT.next() ?} !} ?};PVAT.cntx_pop();
{?  _fkn
|| FUN.info('Do dokumentu wystawiono korektę nagłówkową.\nWycofanie z VAT możliwe jest tylko po usunięciu korekty.');
   return(0)
?};
_kn:=0;
DOK.cntx_psh(); _rn:=ref_name(DVAT.DOK); DOK.use(_rn); DOK.prefix();
{? DOK.seek(DVAT.DOK) 
|| {? DOK.DOK_REJ().KOR_NAG='T'
   || _kn:=1
   || {? 3+DOK.ZAR='FKS' & DOK.KN<>'' |  3+DOK.ZAR='LSP' & DOK.LKN<>'' || _kn:=2 ?}
   ?}
|| FUN.info('Nie znaleziono dokumentu księgowego.');
   return(0)
?};
DOK.cntx_pop();

{? _kn=1
|| {? FUN.choice('Dokument jest korektą nagłówkową.\nCzy na pewno wycofać dokument z rejestru VAT?'@,1,'Wycofaj'@)
   || _ok:=exec('wyc_vatkn','!fks_vat_ddok'); return(_ok)
   || return(0)
   ?}
|? _kn=2
|| FUN.info('Do dokumentu wystawiono korektę nagłówkową.\nWycofanie z VAT możliwe jest tylko po usunięciu korekty.');
   return(0)
?};
{? FINFO.SLAVAT<>null
|| _p:=FUN.choice('Wycofać dokument z rejestru VAT?'@,1,'&Tak'@,'Tak ze zmi&aną trybu'@);
   {? _p=2
   || SLO.index('SL'); FINFO.SLAVAT().SLU();
      SLO.prefix(SLU.ref());
      SLO.win_edit('RED');
      SLO.win_sel('ONE_SEL');
      SLO.hdr_sel(); SLO.hdr_sel('Tryby rozliczeń VAT'@);
      _p:=SLO.select();
      SLO.hdr_sel();
      _auto:=SLO.ref()
   ?}
|| _p:=FUN.choice('Wycofać dokument z rejestru VAT?'@,1,'Wycofaj'@)
?};
_po:=null;
DVAT.cntx_psh();
{? _p
|| DVAT.cntx_psh(); _po:={? DVAT.next() | DVAT.prev() || DVAT.ref() || null ?}; DVAT.cntx_pop();
   do();
   DVAT.cntx_psh();
   exec('mask','dok_fks_ks',DVAT.DOKOKRO().ROK().KOD,DVAT.DOKOKRO().NR);
   DOK.prefix();
   DVAT.DOK();
   VPOZ.index('VDRV'); VPOZ.prefix(DOK.ref,DVAT.RVAT,DVAT.DAT5);
   _rowne:=1;
   {? VPOZ.first()
   || {!
      |? {? VPOZ.OKRVAT<>null | VPOZ.A_VAT<>null | VPOZ.OKR_Z<>null | VPOZ.OKR_C<>null
         || {? VPOZ.OKRVAT=DVAT.OBR
            || VPOZ.OKRVAT:=null;
               VPOZ.A_VAT:=_auto;
               VPOZ.put()
            |? VPOZ.OKR_Z=DVAT.OBR & VPOZ.OKR_C=DVAT.OBR
            || VPOZ.OKR_Z:=VPOZ.OKR_C:=null;
               VPOZ.put()
            |? VPOZ.OKR_Z=DVAT.OBR
            || VPOZ.OKR_Z:=null;
               VPOZ.A_VAT:={? VPOZ.OKR_C<>null || _rowne:=0; null || _auto ?};
               VPOZ.put()
            |? VPOZ.OKR_C=DVAT.OBR
            || VPOZ.OKR_C:=null;
               VPOZ.A_VAT:={? VPOZ.OKR_Z<>null || _rowne:=0; null || _auto ?};
               VPOZ.put()
            || _rowne:=_rowne*(VPOZ.A_VAT=_auto)
            ?}
         ?};
         VPOZ.next()
      !}
   ?};
   DOK.A_VAT:={? _rowne || _auto || null ?};
   DOK.OKRVAT:=null;
   okrDok:=1;
   {? ~DOK.put()
   || VAR_DEL.delete('okrDok');
      undo();
      KOMT:='\nBłąd zapisu do tabeli dokumentów!'
   || VAR_DEL.delete('okrDok');
      ROZLVAT.index('DOK');
      VPOZ.cntx_psh();
      VPOZ.index('VDSP'); VPOZ.prefix(DOK.ref(),DVAT.DAT5);
      {? VPOZ.first()
      || {! |?
            ROZLVAT.prefix(DVAT.DOKOKRO,DOK.ref,DVAT.RVAT,VPOZ.D);
            {? ~ROZLVAT.first()
            || ROZLVAT.prefix(); ROZLVAT.blank(1);
               ROZLVAT.DOK:=DOK.ref; ROZLVAT.OKRO:=DVAT.DOKOKRO;
               ROZLVAT.REJ:=DOK.REJ; ROZLVAT.SYM:=DOK.NK;
               ROZLVAT.DATA:=DOK.DTW;
               ROZLVAT.DOP:=VPOZ.D; ROZLVAT.KH:=DOK.KH;
               ROZLVAT.RVAT:=DVAT.RVAT;
               ROZLVAT.ODD:=DOK.ODD;
               ROZLVAT.SP_WYM:=DOK.SP_WYM;
               {? ~ROZLVAT.add() || undo(); {? KOMT='' || KOMT:='\nBłąd zapisu tabeli ROZLVAT.' ?} ?}
            ?};
            VPOZ.next()
         !}
      ?};
      VPOZ.cntx_pop();
      exec('wyc_vat','dok_fks_ks')

   ?};
   {? ~end() || FUN.info('Operacja nie powiodła się.%1'@[KOMT]); _po:=null || _ok:=1 ?};
   exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR);
   POMOC.USE:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2);
   DVAT.cntx_pop()
?};
DVAT.cntx_pop();
{? _po || DVAT.seek(_po) ?};
&KOMT;
_ok


\wyc_vatkn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: Wycofuje z VAT korektę nagłówkową
::   WY: czy wycofano dokument z VAT? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
KOMT:='';
_auto:=null;
POMOC.KNAG:=0;
_po:=null;
_p:=1;
{? _p
|| DVAT.cntx_psh();
   {? 1
   || DVAT.cntx_psh(); _po:={? DVAT.next() | DVAT.prev() || DVAT.ref() || null ?}; DVAT.cntx_pop();
      do();
      DVAT.cntx_psh();
      exec('mask','dok_fks_ks',DVAT.DOKOKRO().ROK().KOD,DVAT.DOKOKRO().NR);
      DOK.prefix();
      DVAT.DOK();
      VPOZ.index('VDRV'); VPOZ.prefix(DOK.ref);
      _rowne:=1;
      {? VPOZ.first()
      || {!
         |? TmpKn:=1;
            {? VPOZ.OKRVAT
            || exec('wyc_vatg1','dok_fks_ks',VPOZ.OKRVAT().ROK().KOD,ROK.ref(),VPOZ.OKRVAT);
               {? VPOZ.OKRVAT<>null
               || {? VPOZ.OKRVAT=DVAT.OBR
                  || VPOZ.OKRVAT:=null;
                     VPOZ.A_VAT:=_auto;
                     VPOZ.put()
                  ?}
               ?};
               VPOZ.next()
            ?}
         !}
      ?};
      VAR_DEL.delete('TmpKn');

      DOK.A_VAT:={? _rowne || _auto || null ?};
      DOK.OKRVAT:=null;
      {? ~DOK.put()
      || undo();
         KOMT:='\nBłąd zapisu do tabeli dokumentów!'
      || ROZLVAT.index('DOK');
         VPOZ.cntx_psh();
         VPOZ.index('VDSP'); VPOZ.prefix(DOK.ref(),DVAT.DAT5);
         {? VPOZ.first()
         || {! |?
               ROZLVAT.prefix(DVAT.DOKOKRO,DOK.ref,DVAT.RVAT);
               {? ~ROZLVAT.first()
               || ROZLVAT.prefix(); ROZLVAT.blank(1);
                  ROZLVAT.DOK:=DOK.ref; ROZLVAT.OKRO:=DVAT.DOKOKRO;
                  ROZLVAT.REJ:=DOK.REJ; ROZLVAT.SYM:=DOK.NK;
                  ROZLVAT.DATA:=DOK.DTW; ROZLVAT.KH:=DOK.KH;
                  ROZLVAT.DOP:=VPOZ.D;
                  ROZLVAT.RVAT:=DVAT.RVAT;
                  ROZLVAT.ODD:=DOK.ODD;
                  ROZLVAT.SP_WYM:=DOK.SP_WYM;
                  {? ~ROZLVAT.add() || undo(); {? KOMT='' || KOMT:='\nBłąd zapisu tabeli ROZLVAT.' ?} ?}
               ?};
               VPOZ.next()
            !}
         ?};
         VPOZ.cntx_pop();
:: #VATKN wycofywanie z VAT faktury powiązanej z wycofywaną korektą nagłówkową
         POMOC.KNAG:=1; _CzyTezKor:='';
         DOK.cntx_psh(); VPOZ.cntx_psh();
         _ref:=BB.sqlint(DOK.KN);
         _maska:=(8+DOK.KN)+4;
         DOK.use('doku'+_maska); DOK.prefix();
         VPOZ.use('pozv'+_maska);
         _ok:=_ref<>0 & DOK.seek(_ref,) & DOK.ZP='T';
         {? _ok
         || _CzyTezKor:={? DOK.DOK_REJ().KOR='T' || DOK.KOR || '' ?};
            exec('wyc_vatg','dok_fks_ks')
         ?};
         DOK.cntx_pop(); VPOZ.cntx_pop();
         {? _ok & _CzyTezKor<>''
         || _ok:=0; _ref:=null;
            DVAT.cntx_psh(); _ind1:=DVAT.ndx_tmp(,1,'SYM1',,0,'SYM1',,0); DVAT.index(_ind1); DVAT.prefix(_CzyTezKor);
            {? DVAT.first()
            || {? exec('FindAndGet','#table','DOK',DVAT.DOK,,"LKN",'')=$DOK.ref
               || _ok:=1; _ref:=DVAT.DOK
               ?}
            ?};
            DVAT.cntx_pop(); DVAT.ndx_drop(_ind1);
            DOK.cntx_psh(); VPOZ.cntx_psh();
            {? _ok & _ref<>null
            || _maska:=4-ref_name(_ref);
               DOK.use('doku'+_maska); DOK.prefix();
               VPOZ.use('pozv'+_maska);
               _ok:=_ref<>0 & DOK.seek(_ref,) & DOK.ZP='T'
            ?};
            {? _ok
            || POMOC.KNAG:=1;
               exec('wyc_vatg','dok_fks_ks')
            ?};
            DOK.cntx_pop(); VPOZ.cntx_pop()
         ?}
      ?};
      {? ~end() || FUN.info('Operacja nie powiodła się.'+KOMT); _po:=null || _ok:=1 ?};
      exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR);
      POMOC.USE:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2);
      DVAT.cntx_pop()
   ?};
   DVAT.cntx_pop();
   {? _po || DVAT.seek(_po) ?};
   &KOMT
?};
_ok


\wyc_vat1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Akcja 'Wycofaj z VAT' w okienkach wertowania tabeli PVAT
::  OLD: \wyc_vat1/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_fkn:=0; PVAT.cntx_psh();{? PVAT.first() || {! |? {? PVAT.KNAG=2 || _fkn:=1;0 || PVAT.next() ?} !} ?};PVAT.cntx_pop();
{?  _fkn
|| FUN.info('Do dokumentu wystawiono korektę nagłówkową.\nWycofanie z VAT możliwe jest tylko po usunięciu korekty.');
   return(0)
?};
_kn:=exec('FindAndGet','#table',DOK,DVAT.DOK,,"KN",0)<>'';
_kn:=0;
DOK.cntx_psh(); _rn:=ref_name(DVAT.DOK); DOK.use(_rn); DOK.prefix();
{? DOK.seek(DVAT.DOK) 
|| {? DOK.DOK_REJ().KOR_NAG='T'
   || _kn:=1
   || {? 3+DOK.ZAR='FKS' & DOK.KN<>'' |  3+DOK.ZAR='LSP' & DOK.LKN<>'' || _kn:=2 ?}
   ?}
|| FUN.info('Nie znaleziono dokumentu księgowego.');
   return(0)
?};
DOK.cntx_pop();

{? _kn=1
|| FUN.info('Pozycja korekty nagłówkowej.\nWycofanie z VAT możliwe jest tylko dla całego dokumentu korekty nagłówkowej.');
   return(0)
|? _kn=2
|| FUN.info('Do dokumentu wystawiono korektę nagłówkową.\nWycofanie z VAT możliwe jest tylko po usunięciu korekty.');
   return(0)
?};
KOMT:='';
_auto:=null;
{? FINFO.SLAVAT<>null & PVAT.TYP_VAT=''
|| _p:=FUN.choice('Wycofać pozycję dokumentu VAT z rejestru VAT?'@,1,'&Tak'@,'Tak ze zmi&aną trybu'@);
   {? _p=2
   || SLO.index('SL'); FINFO.SLAVAT().SLU();
      SLO.prefix(SLU.ref());
      SLO.win_edit('RED');
      SLO.win_sel('ONE_SEL');
      SLO.hdr_sel(); SLO.hdr_sel('Tryby rozliczeń VAT'@);
      _p:=SLO.select();
      _auto:=SLO.ref()
   ?}
|| _p:=FUN.choice('Wycofać pozycję dokumentu VAT z rejestru VAT?'@,1,'Wycofaj'@)
?};
_ref:=DVAT.ref();
DVAT.cntx_psh();
{? _p
|| do();
   DVAT.cntx_psh();
   exec('mask','dok_fks_ks',DVAT.DOKOKRO().ROK().KOD,DVAT.DOKOKRO().NR);
   DOK.prefix();
   DVAT.DOK();
   VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref);
   {? PVAT.VPOZ_REF
   || {? VPOZ.seek(PVAT.VPOZ_REF)
      || {? PVAT.TYP_VAT=''
         || VPOZ.OKRVAT:=null;
            VPOZ.A_VAT:=_auto;
            VPOZ.put()
         |? PVAT.TYP_VAT='Z'
         || VPOZ.OKR_Z:=null;
            VPOZ.A_VAT:=_auto;
            VPOZ.put()
         |? PVAT.TYP_VAT='C'
         || VPOZ.OKR_C:=null;
            VPOZ.A_VAT:=_auto;
            VPOZ.put()
         ?}
      || KOMT:='\nNie znaleziono pozycji VAT w dokumencie źródłowym.';
         undo
      ?}
   |? PVAT.VPOZ_ROZ
   || {? ~VPOZ.seek(PVAT.VPOZ_ROZ().VPOZ)
      || KOMT:='\nNie znaleziono pozycji VAT w dokumencie źródłowym.';
         undo
      ?}
   ?};
   ROZLVAT.index('DOK');
   ROZLVAT.prefix(DVAT.DOKOKRO,DOK.ref(),VPOZ.RVAT().RVAT,VPOZ.D);
   {? ~ROZLVAT.first()
   || ROZLVAT.prefix(); ROZLVAT.blank(1);
      ROZLVAT.DOK:=DOK.ref; ROZLVAT.OKRO:=DVAT.DOKOKRO;
      ROZLVAT.REJ:=DOK.REJ; ROZLVAT.SYM:=DOK.NK;
      ROZLVAT.DATA:=DOK.DTW;
      ROZLVAT.DOP:=VPOZ.D; ROZLVAT.KH:=DOK.KH;
      ROZLVAT.RVAT:=VPOZ.RVAT().RVAT;
      ROZLVAT.ODD:=DOK.ODD;
      ROZLVAT.SP_WYM:=DOK.SP_WYM;
      {? ~ROZLVAT.add() || undo(); {? KOMT='' || KOMT:='\nBłąd zapisu tabeli ROZLVAT.' ?} ?}
   ?};
   {? PVAT.size=1
   || DVAT.cntx_psh();
      {? ~DVAT.next() || DVAT.prev() ?};
      _ref:=DVAT.ref();
      DVAT.cntx_pop();
      exec('wyc_vat','dok_fks_ks')
   || _vpoz_roz:=PVAT.VPOZ_ROZ;
      _del:=PVAT.del(,1);
      {? _del & _vpoz_roz
      || VPOZ_ROZ.prefix();
         {? VPOZ_ROZ.seek(_vpoz_roz)
         || {? ~VPOZ_ROZ.del(,1)
            || _del:=0
            ?}
         || _del:=0
         ?}
      ?};
      {? _del=2
      || PVAT.cntx_psh();
         {! |? PVAT.NR:=PVAT.NR-1; PVAT.put(); PVAT.next !};
         PVAT.cntx_pop()
      |? _del>3
      || KOMT:='\nPozycje VAT zablokowane przez innego użytkownika'
      ?}
   ?};
   exec('dok_akt','fks_vat');
   {? ~end() || FUN.info('Operacja nie powiodła się.%1'@[KOMT])
   |? var_pres('TTVAT')>0 || exec('ana_vat_wyc','!fks_vat_zrev')
   ?};
   exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR);
   POMOC.USE:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2);
   DVAT.cntx_pop()
?};
DVAT.cntx_pop();
DVAT.seek(_ref);
_tab:=cur_tab(1,1);
{? _tab=DVAT
|| {? __wgkraj || grp_disp(DVAT,'WER_KRA2',1) || grp_disp(DVAT,'WER',1) ?}
|? _tab=PVAT
|| {? var_press('TTVAT')>0
   || {? PVAT.size()=0 || sel_exit() ?}
   || {? __wgkraj || grp_disp(DVAT,'WER_KRA2',1) || grp_disp(DVAT,'WER',1) ?}
   ?};
   exec('suma_vat','dok_fks','PVAT')
|? _tab<>PVAT
|| grp_disp(TREE_VAT,w_acr,1)
?};
&KOMT;
1


:Sign Version 2.0 jowisz:1045 2023/12/22 11:36:03 9cf00334a50db5d2f4b5775e42e626a86a5832ddb5aa1fdd73444cbcda6bc327dd385477f609c706c0b297ed6846e03e3e094bf945641527a091d1f1d3227ea6bd30e6b002b58b9cbd851e8d884bf8aae77ec1561179ba59ef866b1198974f5a261fb349e5c2c261a4c204899b812485fdf918c4645829872490b218b68ee402

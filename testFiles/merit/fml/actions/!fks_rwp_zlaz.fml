:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rwp_zlaz.fml
:: Utworzony: 24.11.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RWP_ZLAZ - Przeglądanie analiz w pullpicie windykatora
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przeglądanie analiz w pullpicie windykatora - główna formuła czynności FKS_RWP_ZLAZ
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
exec('wind_nag_ini','!fks_rwp_zlaz');
_ref_an:=null;
WIND_N.cntx_psh();
WIND_N.index('OPER_O');
WIND_N.prefix(REF.FIRMA,SSTALE.WAL,OPERATOR.DEPT,OPERATOR.USER().KOD,OPERATOR.USER().KOD);
{? WIND_N.last() || _ref_an:=WIND_N.ref() ?};
WIND_N.cntx_pop();
{? OPERATOR.DEPT=null
|| WIND_N.index('DATA'); WIND_N.prefix(REF.FIRMA,SSTALE.WAL)
|| WIND_N.index('DATA_O'); WIND_N.prefix(REF.FIRMA,SSTALE.WAL,OPERATOR.DEPT)
?};
_first:=1;
VAR_DEL.delete('znal_an');
znal_an:={? _ref_an<>null || WIND_N.seek(_ref_an) || _first:=WIND_N.first(); 0 ?};
{? PAR_SKID.get(27)='N' || znal_an:=0 ?};

_win_sel:='WER';
_grp:=WIND_N.grp_make('GRP',,'#windzial01',,,"exec('exit','zws')");
WIND_N.grp_sel(_grp,,_win_sel,,
               "{? var_pres('znal_an')>0 & znal_an=1
                || exec('wndn_poz','rozrach',1,{? WIND_N.DATA_GEN<date() || 1 || 0 ?}); znal_an:=0
                ?}",,,,,,,,,'wind_n_01');
WIND_N.win_sel(_grp);
AreaTitle.setTabWin(WIND_N,~~);
AreaTitle.setTitle();
WIND_N.select(,1,10);
WIND_N.actions(_win_sel,'');
exec('wind_nag_del','!fks_rwp_zlaz');
~~


\wind_nag_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Inicjowanie analiz w pulpicie windykatora
::----------------------------------------------------------------------------------------------------------------------
selexit:=0; wind_akt:=0;
exec('kont_tmp','rozrach');
exec('gen_tmp2','rozrach');
exec('zdarzt_add','rozrach')


\wind_nag_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Kasowanie zmiennych - analiz w pulpicie windykatora
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('selexit','wind_akt');
exec('kont_tmp_del','rozrach');
exec('del_tmp','rozrach');
exec('deltree','rozrach')


\windnpat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Szukaj przed, tabela WIND_N
::  OLD: \windnpat/windyk1.fml
::----------------------------------------------------------------------------------------------------------------------
WIND_N.win_edit(); WIND_N.win_patt(); 1


\wind_kh_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Na wyswietl dla kolumn w okienkach wertowania tabeli WIND_KH
::  OLD: \wind_kh/kh_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? ($('PAR_WYDR.IPAR'+$windpole+'=\'\''))() || 'WIND_KH#WINDSUM#01' || '' ?}


\dsp00010
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula przed wyswietl pola WIND_KH.PRZ1
::  OLD: \dsp00010/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
windpole:=1; exec('flddisp','color','WIND_KH#WINDSUM')


\dsp00011
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula przed wyswietl pola WIND_KH.PRZ2
::  OLD: \dsp00011/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
windpole:=2; exec('flddisp','color','WIND_KH#WINDSUM')


\dsp00012
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula przed wyswietl pola WIND_KH.PRZ3
::  OLD: \dsp00012/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
windpole:=3; exec('flddisp','color','WIND_KH#WINDSUM')


\dsp00013
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula przed wyswietl pola WIND_KH.PRZ4
::  OLD: \dsp00013/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
windpole:=4; exec('flddisp','color','WIND_KH#WINDSUM')


\dsp00014
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula przed wyswietl pola WIND_KH.PRZ5
::  OLD: \dsp00014/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
windpole:=5; exec('flddisp','color','WIND_KH#WINDSUM')


\dsp00015
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula przed wyswietl pola WIND_KH.PRZ6
::  OLD: \dsp00015/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
windpole:=6; exec('flddisp','color','WIND_KH#WINDSUM')


\dsp00016
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula przed wyswietl pola WIND_KH.PRZ7
::  OLD: \dsp00016/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
windpole:=7; exec('flddisp','color','WIND_KH#WINDSUM')


\dsp00017
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Przed wyświetl dla pola WIND_KH.PRZ8
::  OLD: \dsp00017/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
windpole:=8; exec('flddisp','color','WIND_KH#WINDSUM')


\pw_w_sr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed wyświetl dla pola WIND_KH.SR_CZAS
::  OLD: \pw_w_sr/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
WIND_KH.KH();
WIND_KH.SR_CZAS<>-1000000


\dsp_ods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK][8.70]
:: OPIS: Przed wyświetl dla pola WIND_KH.ODSPOTER
::  OLD: \dsp_ods/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
WIND_N.NAL_ODS


\bv_w_sw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Przed wyświetl dla pola WIND_KH.SR_WAZ
::  OLD: \bv_w_sw/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
WIND_KH.SR_WAZ<>-1000000


\wind_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Dane kontrahenta w tabeli WIND_KH
::  OLD: \wind_kh/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
WIND_KH.KH(); __KH_MOD:=1;
exec('kh_dod_ini','kontrahent');
KH.win_edit('RED');
KH.display();
&__KH_MOD


\wind_obr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Obroty i salda - dane kontrahenta
::  OLD: \wind_obr/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('salda','!fks_rwp_zlaz');
PAR_WYDR.RPAR5:=WIND_KH.SWN_N+WIND_KH.SWN_Z;
PAR_WYDR.RPAR6:=WIND_KH.SMA_N+WIND_KH.SMA_Z;
PAR_WYDR.RPAR7:=F.SWn(PAR_WYDR.RPAR5,PAR_WYDR.RPAR6);
PAR_WYDR.RPAR8:=F.SMa(PAR_WYDR.RPAR5,PAR_WYDR.RPAR6);
WIND_KH.win_edit('OBROTY');
WIND_KH.hdr_edit();
WIND_KH.hdr_edit(': %1'@[KH.KOD]);
WIND_KH.display();
WIND_KH.win_edit()


\salda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Obliczenie sald dla pozycji analizy
::  OLD: \salda/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_WYDR.RPAR1:=F.SWn(WIND_KH.SWN_N,WIND_KH.SMA_N);
PAR_WYDR.RPAR2:=F.SMa(WIND_KH.SWN_N,WIND_KH.SMA_N);
PAR_WYDR.RPAR3:=F.SWn(WIND_KH.SWN_Z,WIND_KH.SMA_Z);
PAR_WYDR.RPAR4:=F.SMa(WIND_KH.SWN_Z,WIND_KH.SMA_Z);
1


\obl_obr1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Obliczenie obrotow dla pozycji - analizy działań windykacyjnych
::  OLD: \obl_obr1/windyk1.fml
::----------------------------------------------------------------------------------------------------------------------
{? WIND_KH.sel_size()=0
|| exec('def_okn1','!fks_rwp_zlaz');
   exec('add_war1','!fks_rwp_zlaz');
   def_disp('Obroty zaznaczonych pozycji'@);
   undefine()
|| exec('add_war1','!fks_rwp_zlaz')
?}


\def_okn1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Definiowanie okienka tymczasowego - analizy działań windykacyjnych
::  OLD: \def_okn1/windyk1.fml
::----------------------------------------------------------------------------------------------------------------------
undefine();
define('PRZEDTER',0,form('Przed terminem',15),'',,,2);
{! _i:=1..8 |!
   {? _prz:=($('PAR_WYDR.IPAR'+$(_i)))(); _prz<>''
   || define(('P'+$_i),0,form(_prz,15),'',,,2)
   ?}
!};
define('OGOLEM',0,form('Ogółem',15),'',,,2)


\add_war1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Obroty (analizy działań windykacyjnych) - naliczanie wartosci
::  OLD: \add_war1/windyk1.fml
::----------------------------------------------------------------------------------------------------------------------
DEFINE.PRZEDTER+=WIND_KH.PRZEDTER;
DEFINE.OGOLEM+=WIND_KH.RAZEM;
{! _i:=1..8 |!
   {? _prz:=($('PAR_WYDR.IPAR'+$(_i)))(); _prz<>''
   || ($('DEFINE.P'+$_i+'+=WIND_KH.PRZ'+$_i))()
   ?}
!}


\begoblo1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Grupa przed dla liczenia obrotow - analizy działań windykacyjnych
::  OLD: \begoblo1/windyk1.fml
::----------------------------------------------------------------------------------------------------------------------
exec('def_okn1','!fks_rwp_zlaz')


\wndn_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [7.62]
:: OPIS: Wydruk analizy naleznosci i zobowiazan
::  OLD: \wndn_wyd/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FKS_RWP_ZLAZ','fks_wndnwyd*',,1)

\windp_rp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Rekord przed w okienku WIND_KH
::  OLD: \windp_rp/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
WIND_KH.KH();
exec('kh_dod_ini','kontrahent');
SER_KOR.KTO:=KH.POCZ+' '+{? KH.POCZ*KH.MIASTO=0 || KH.MIASTO || '' ?}+
             {? KH.UL<>'' || ' ul.'+KH.UL || '' ?};
exec('salda','!fks_rwp_zlaz');
exec('rekprzed','color','KH#01#')


\oknwindk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.70]
:: OPIS: Okienko przed dla pustej dziedziny w okienkach tabeli WIND_KH
::  OLD: \oknwindk/windyk1.fml
::----------------------------------------------------------------------------------------------------------------------
SER_KOR.KTO:='';
PAR_WYDR.RPAR1:=PAR_WYDR.RPAR2:=PAR_WYDR.RPAR3:=PAR_WYDR.RPAR4:=0;
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 7575382d722c0fc24448d5ec845cb08e2ec43a2e661364d62ce7eb75051c0b4450301eb76fbb34e00938b32b7bc1c2ee2608f14f7718cee5ac54f2fb6be600acd4bcc4382f18f88269db43c192e3ab27705071a5f56270708965a610d4f572bf5ef1d537bd2d9cf864d428bcfdceeee358b6dc3eb14e71e20e1a5da2f14bb617

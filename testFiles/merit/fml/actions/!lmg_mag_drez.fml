:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_drez.fml
:: Utworzony: 29.03.2016
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły czynności LMG_MAG_DREZ - Usunięcie przeterminowanych rezerwacji, zmiana rezerwacji warunkowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WY, symbol=IL_PREZ,  type=NUMBER, name=Ilość przeterminowanych rezerwacji,    required=N,    keyref=N
::# kind=WY, symbol=IL_WREZ,  type=NUMBER, name=Ilość zmienionych rezerwacji,          required=N,    keyref=N
::# kind=WY, symbol=IL_TREZ,  type=NUMBER, name=Ilość tymczasowych rezerwacji,         required=N,    keyref=N
::# properties=SERVICE

_mp:=params_get().mp;
_out:=params_get().out;

_service:=_mp.isService();

{? ~_service
|| _mp.error('Czynność wyłącznie serwisowa.');
   _mp.done()
|| ZK_N.cntx_psh();
   ZK_N.prefix();
   params_set('mp',_mp);
   exec('aktRez','!lmg_mag_drez');
   ZK_N.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rezerwacji'@@


\aktRez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Aktualizuje rezerwacje
::  OLD: \del_dodt/zk2.fml
::       \autowrez/zk2.fml
::       \killrezt/zk2.fml
::----------------------------------------------------------------------------------------------------------------------
exec('openz_psh','open_tab');
SM.cntx_psh();
_mp:=params_get().mp;

_prez:=0;
_wrez:=0;
_trez:=0;
_oddz:=ST.ODDZ;

BEER.MJSLOG:='DET';
:: sprawdzenie czy powołane triggery
_newtr:=0;
{? exec('get','#params',300800,2)='T'
|| _tabtr:=REZ.triggers();
   {? _tabtr.first()
   || {!
      |? _newtr:=(7+_tabtr.ID)='rezsprl';
         ~_newtr & _tabtr.next()
      !};
      _newtr:=~_newtr
   || _newtr:=1
   ?};
   {? _newtr || exec('OnOffSPRLOG','faktury_wspolne','1x') ?}
?};

ODDZ.cntx_psh();
ODDZ.index('KOD2');
ODDZ.prefix();
{? ODDZ.first()
|| {!
   |? ST.ODDZ:=ODDZ.KOD;
      ST.ODDZ_KOD:=ODDZ.KOD;
      exec('openz','open_tab',ODDZ.KOD+'__');
      SM.use('stm__'+ODDZ.KOD+'zb');
::    usuniecie zapisow po zamknieciu krzyzykiem systemu
      exec('delerrez','zamsiw_wspolne');
::    usuniecie przeterminowanych rezerwacji na zamowieniach i tymczasowych
      _prez+=exec('del_dodt','rezerwacje');
::    zalozenie rezerwacji bezwarunkowych na podstawie rezerwacji warunkowych
      _wrez+=exec('autowrez','rezerwacje');
::    usuniecie historii przeterminowanych rezerwacji tymczasowych
      _trez+=exec('killrezt','rezerwacje');
      ODDZ.next()
   !}
?};
ODDZ.cntx_pop();
exec('openz_pop','open_tab');
SM.cntx_pop();
ST.ODDZ:=_oddz;
ST.ODDZ_KOD:=_oddz;
BEER.MJSLOG:='';
{? _newtr || exec('OnOffSPRLOG','faktury_wspolne','0x') ?};

_mp.save('OUT','IL_PREZ',_prez);
_mp.save('OUT','IL_WREZ',_wrez);
_mp.save('OUT','IL_TREZ',_trez);
_mp.done();
~~

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:38 33d91043edaeb7b3b53790f4164a055697c52e379ba38dcfba7b00be3a63ca757590c1c43ccc56a2040ceb530fb6d91fd670f5bad8d3cc991d2726d267f3c7c11e9f0503b6992980aee64faa5aa42e8a6293a1fa18eb6abe71f15169f5c5215e1d8c2b8ee18631fd23bd20f165da8e734673f5be0a66026cf0bbc1c9e2d1304c

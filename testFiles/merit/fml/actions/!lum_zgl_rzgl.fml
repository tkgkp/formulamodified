:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lum_zgl_rzgl.fml
:: Utworzony: 19.02.2019
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa realizacji zgłoszenia jednorazowego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP
::# properties=SERVICE
::# parses=exec('parses','!lum_zgl_rzgl')
::# kind=WE,   symbol=ZLP,    type=_ZLP,  name=Zgłoszenie jednorazowe,  required=T, keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

_akcja:=_mp.akcja();
_auto:=_akcja<>'Akceptuj' & (_mp.isAutoRun() | _mp.isService());
_group:=_mp.isGroup();

{? ~(var_pres('ZLP',_in)=type_of(null()) & _in.ZLP)
||
   _mp.error('Brak wymaganego parametru ZLP.')
||
   ZLE.cntx_psh();
   ZLP.cntx_psh();
   _x:=ref_name(_in.ZLP)+5;
   exec('zle_open','open_tab',1+_x,2+(_x+4),_x+2);
   ZLE.cntx_psh();
   ZLP.cntx_psh();
   ZLP.prefix();
   {? ~ZLP.seek(_in.ZLP)
   ||
      _mp.error('Nie znaleziono zgłoszenia jednorazowego.'@)
   ||
      ZLP.ZLE();
      ZLEC.MIA:=ZLE.POS().UL().MIA;
      ZLEC.UL:=ZLE.POS().UL;
      {? _akcja='Realizuj' | _auto | _group
      ||
         _realizuj:=exec('zlp_realizuj','!lum_zgl_rzgl',_auto,_group);
         {? _realizuj='done' || {? ZLP.put() || _mp.done() ?}
         |? +_realizuj  || _mp.error(_realizuj)
         ?}

      |? _mp.pathTodo()
         | _mp.pathArea() & _akcja=''
      ||
         exec('zle_win_edit_set','!lum_zgl_rzgl');
         _win_red:=ZLE.win_edit('?');
         _cntx:=obj_new('REALIZUJ');
         _cntx.REALIZUJ:=0;
         params_set('cntx',_cntx);
         ZLE.display();
         {? _cntx.REALIZUJ
         || exec('stanPlan','umowy_zlecenia');
            {? ZLP.put() || _mp.done() ?}
         ?};
::       Usunięcie definicji tymczasowych okien
         ZLE.win_edit(''); ZLE.win_edel(_win_red)
      ||
         _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   ZLP.cntx_pop();
   ZLE.cntx_pop();
   ZLP.cntx_pop();
   ZLE.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZLP',_in)=type_of(null()) & _in.ZLP
|| 'Zrealizuj zgłoszenie: %1'@@[exec('record','#to_string',_in.ZLP)]
|| ''
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('ZLP',_in)=type_of(null()) & _in.ZLP
||
   ZLP.cntx_psh();
   ZLP.use(ref_name(_in.ZLP));
   ZLP.cntx_psh();
   ZLP.prefix();
   {? ZLP.seek(_in.ZLP) & ZLP.ZLE
   ||
      _name:=ZLP.name();
      _oddz:=1+(3-_name);
      _rok:=2000+#(2+(4-_name));
      _mc:=#(_name+2);
      __PARSES.setVal('OddzialLogProd',_oddz);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LUM';
      _args.AR:=_rok;
      _args.AM:=_mc;
      __PARSES.setVal('OkresRok',_args)
   ?};
   ZLP.cntx_pop();
   ZLP.cntx_pop()
?};

1


\zlp_realizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Zgłoszenie jednorazowe - Realizacja
::   WE: _a - automatycznie 0/1
::       _b - przetwarzanie grupy rekordów 0/1
::       params_get()   - ustawiane w exec('main','!lum_zgl_rzgl')
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _>=1
|| _auto:=_a
|| _auto:=0
?};
{? _>=2
|| _group:=_b
|| _group:=0
?};

_result:='';

_zgl:=exec('record','#to_string',ZLP.ref());
_txt:='';
_info:=~_group;
_put:=0;

{? ZLP.SR='Z'
||
   _txt:='Zgłoszenie jest już zrealizowane.'@;
   _result:='done'

|? ZLP.STAT_REJ='N' & ZLP.ZLE().CZUM='N'
||
   _txt:='Nie zakończono jeszcze rejestracji zgłoszenia.'@;
   _result:=_txt

|? ZLP.PORT='T'
||
   _txt:='Zgłoszenie internetowe. Należy je przyjąć do realizacji.'@;
   _result:=_txt
|? exec('zlp_czy_anulowane','umowy_zlecenia')
|| _txt:='Zgłoszenie jest anulowane.'@;
   _result:='done'
|? ZLP.DW=date(0,0,0)
|| _txt:='Zgłoszenie nie ma daty wykonania.'@;
   _result:=_txt
|? _auto | _group | FUN.ask('Zrealizować zgłoszenie %1?'@[exec('record','#to_string',ZLP.ref())])
||
   _txt:='Zrealizowano zgłoszenie.'@;
   _info:=0;
   ZLP.SR:='Z';
   _result:='done'
?};

{? _txt<>''
||
   _txt:={? _put || '' || 'Realizacja niemożliwa.'@+{? _group || ' ' || '\n' ?} ?}+_txt;
   {? ~_auto || {? _group || exec('add_kom','#message',_zgl,1,_txt) |? _info || FUN.info(_txt) ?} ?}
?};

_result


\zlp_realizuj_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Zgłoszenie jednorazowe - Realizuj w oknie redakcji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('zlp_realizuj','!lum_zgl_rzgl',0)='done'
||
   params_get().cntx.REALIZUJ:=1;
   'key:F2'
||
   ''
?}


\zlp_realizuj_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Zgłoszenie jednorazowe - Realizuj w oknie wertowania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZLP.cntx_psh();
_one:=0;
_Sel:=ZLP.sel_aget();
{? ~_Sel.first() || _one:=1; _Sel.REF:=#ZLP.ref(); _Sel.add() ?};

_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LUM_ZGL_RZGL';
_params.UIDREF:=ZLP.uidref();
_params.AKCJA:='Realizuj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
{? _Sel.first() & _Sel.next()
||
   _params.GRUPA:='T';
   {? ~FUN.ask('Realizować zaznaczone zgłoszenia?'@) || ZLP.cntx_pop(); return(0) ?};
   exec('ini_kom','#message','Grupowe akceptuj'@)
?};

_loop:=_Sel.first();
{!
|? _loop
|!
   {? ZLP.seek(_Sel.REF,) & ZLP.SR<>'Z'
   ||
      _params.UIDREF:=ZLP.uidref();
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZLP',ZLP.ref());
      exec('mp_run','#b__box',_params)
   ?};
   _loop:=_Sel.next()
!};
{? _params.GRUPA='T'
||
   exec('end_kom','#message');
   ZLP.sel_adel()
?};
ZLP.cntx_pop();
0


\zle_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Ustawia okno redakcji tabeli ZLE, wymagane pola, okna słowników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_red:=exec('zle_win_edit','umowy_zlecenia');
_ff:="params_exec('zlp_realizuj_red','!lum_zgl_rzgl')";
ZLE.win_ebtn(_win_red,
   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Realizuj'@],_ff);
_ff:='key:Esc';
ZLE.win_ebtn(_win_red,
   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
ZLE.win_edit(_win_red);
exec('zle_set_efld_opt','umowy_zlecenia',_win_red);
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 68e0b1ab4e5f72d6130fd3d5d0c3d7659ad4ac4c7d4ba9a87c60e59ef851e3a68127832703be12377cff1e9e7c978fdc0b6cc7e7480c33ba2a8883b1e6a9fa3cc0b5168564d104ba5124fe93f898f0844aa46275444dd0a5e6795b345fecbe12920ebfe6b955fe660c4e4d8ab854ec266340100ac99b62974f686595b45bfef0

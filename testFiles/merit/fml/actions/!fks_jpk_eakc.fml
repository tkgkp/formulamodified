:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_jpk_eakc.fml
:: Utworzony: 29.08.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_JPK_EAKC - Akceptacja plików JPK
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akceptacja plików JPK - główna formuła czynności FKS_JPK_EAKC.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,HRB
:: PARAMETRY WE:
::# kind=WE, symbol=JPK, type=_JPK, name=Wskazanie na plik JPK, required=N, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=JPK, type=_JPK, name=Wskazanie na plik JPK, required=T
::# kind=WY, symbol=TYP, type=STRING, name=Typ pliku JPK, required=T
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
JPK.cntx_psh();
JPK.prefix();
{? var_pres('JPK',_we)<=0 | ~JPK.seek(_we.JPK)
|| {? ~_mp.isAutoRun()
   || FUN.info('Nie znaleziono pliku JPK.'@)
   ?};
   _wy.JPK:=null;
   _mp.save(,_wy);
   _mp.done()
|| exec('akc_jpk','!fks_jpk_eakc',_mp.isAutoRun(),_mp.pathTodo());
   {? JPK.AKC='T'
   || _wy.JPK:=JPK.ref();
      _wy.TYP:=JPK.TYP;
      _mp.save(,_wy);
      _mp.done()
   ?}
?};
JPK.cntx_pop();
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis na liście TODO czynności
::----------------------------------------------------------------------------------------------------------------------
_desc:='Akceptuj pliku JPK'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_press('JPK',_we)>0
|| JPK.cntx_psh();
   JPK.prefix();
   {? JPK.seek(_we.JPK)
   || _desc:='Akceptuj %1'@@[exec('record','#to_string',JPK.ref())]
   ?};
   JPK.cntx_pop()
?};
_desc


\set_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przygotowuje okno
::----------------------------------------------------------------------------------------------------------------------
_okn:=JPK.mk_edit('Jednolity Plik Kontrolny'@);
JPK.win_ewin(_okn,,'RED');
JPK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pokaż dane'@],"
   exec('show_jpk','!fks_jpk_zprz');''
");
_akc:=JPK.win_ebtn(_okn,
             'text=%1, btn_label_align=center, panel=bottom, align=end, display=1'['Akceptuj'@],"JpkBtn:=1; 'key:Esc'");
_an:=JPK.win_ebtn(_okn,'text=%1, btn_label_align=center, panel=bottom, align=end, display=1'['&Anuluj'@],'key:Esc');
JPK.btn_eopt(_okn,_akc,'tooltip='+'Akceptuj JPK'@);
JPK.btn_eopt(_okn,_an,'tooltip='+exec('help_red_esc','#window','A'));
JPK.win_edit(_okn)


\akc_jpk_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja akceptacji JPK
::  OLD: \akc_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JPK.AKC='T'
|| FUN.info('JPK jest już zaakceptowany.'@)
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='FKS_JPK_EAKC';
   _params.UIDREF:=JPK.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'JPK',JPK.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params)
?}


\akc_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja akceptacji JPK
::   WE: _a - czy automatyczna? 1-tak 0-nie
::       _b - czy z ToDO? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
{? JPK.AKC<>'T'
|| JpkBtn:=0;
   {? _a=0 & _b
   || exec('set_win','!fks_jpk_eakc');
      JPK.display()
   || JpkBtn:=1
   ?};
   {? JpkBtn=1 & exec('chk_jpk_kom','!fks_jpk_eakc',1,_a,_b=0)
   || _ok:=1;
      {? JPK.GEN<>'N' & exec('jpk_ok','!fks_jpk_eakc')=0
      || {? _a
         || _ok:=0
         || _ok:=FUN.ask(
               'Dane, na podstawie których utworzono plik JPK,\nzmieniły się od momentu jego utworzenia.\n\n'
               'Czy mimo to zaakceptować plik JPK?'@,
               'EXCLAM'
            )
         ?};
         {? _ok
         || exec('addkom','jpk','N','Zaakceptowano plik niezgodny z danymi.')
         ?}
      ?};
      {? _ok
      || JPK.AKC:='T';
         JPK.D_AKC:=date();
         JPK.T_AKC:=time();
         JPK.U_AKC:=OPERATOR.USER().KOD;
         JPK.put();
         {? _a=0
         || FUN.info('Plik JPK został zaakceptowany.'@)
         ?}
      ?}
   ?}
|? _a=0
|| FUN.info('JPK jest już zaakceptowany.'@)
?}


\chk_jpk_kom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Informacja o błedach i uwaga po generowaniu JPK
::   WE: _a - 1-akceptacja 2-przekazanie
::       _b - czynność automatyczna? 1-tak 0-nie
::       _c - Pytać gdy ok? 1-tak 0-nie
::  OLD: \chk_jpk_kom/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
JPK_KOM.index('TYP');
JPK_KOM.prefix(JPK.ref(),'E');
_err:=JPK_KOM.first();
JPK_KOM.prefix(JPK.ref(),'W');
_wrn:=JPK_KOM.first();
_pyt:={? _a=1
      || 'Czy mimo to zaakceptować plik JPK?'@
      || 'Czy mimo to przekazać plik JPK do kontroli?'@
      ?};
{? _err | _wrn
|| {? _b=1
   || _ok:=~_err
   || _kom:={? _err & _wrn
            || 'Podczas generowania pliku JPK\nzostały zgłoszone błędy i uwagi'@
            |? _err
            || 'Podczas generowania pliku JPK\nzostały zgłoszone błędy'@
            || 'Podczas generowania pliku JPK\nzostały zgłoszone uwagi'@
            ?};
      _ok:=FUN.ask(_kom+'.\n\n'+_pyt,{? _err || 'EXCLAM' || ~~ ?})
   ?}
|? _b=0 & _c
|| _ok:=FUN.ask('Czy zaakceptować plik JPK?'@)
?};
_ok


\jpk_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Sprawdza zgodnosc pliku z danymi w programie
::  OLD: \jpk_ok/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ar:=SSTALE.AR;
_ao:=SSTALE.AO;
SSTALE.AO:=JPK.OKRO_F;
SSTALE.AR:=JPK.OKRO_F().ROK;
DataOd:=JPK.OD;
DataDo:=JPK.DO;
exec('open_tabele','open_tab');
NoKom:=1;
{? JPK.TYP='WB'
|| VAR_DEL.delete('JPK_RB','TabRej2');
   JPK_RB:=JPK.DODAT;
   _nrb:=RB.get_rbtx(2,2-JPK_RB,2+JPK_RB);
   TabRej2:=sql('select DOK_REJ.REFERENCE as REF from DOK_REJ join REJ where REJ.ROK=\':_a\' and DOK_REJ.JPK_RB=\':_b\'',$JPK.OKRO_F().ROK,_nrb);
   {? TabRej2.first()
   || DOK_REJ.seek(BIT.sqlint(TabRej2.REF),)
   ?};
   SKID_RBK.index('TRN');
   SKID_RBK.prefix(REF.INFO,0,null,STR.gsub(_nrb,' ',''),);
   {? SKID_RBK.first()
   || HELPJPK.WAL:={? SKID_RBK.WAL=null || FINFO.NAROD || SKID_RBK.WAL ?}
   || HELPJPK.WAL:=FINFO.NAROD
   ?}
|? 'VAT,V7M,V7K'*JPK.TYP
|| JPK.OKRO_F();
   VAT_DEK.prefix();
   {? VAT_DEK.seek(BIT.sqlint(JPK.DODAT),)
   || VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD)
   ?};
   exec('czytaj','#stalesys',DataOd,XINFO)
|? JPK.TYP='FA'
|| VAR_DEL.delete('TabRej2');
   HELPJPK.OKR_TYP:=2+JPK.DODAT+1;
   SLO.index('SL'); SLO.prefix();
   HELPJPK.WAL:={? SLO.seek(BIT.sqlint(2-JPK.DODAT),) || SLO.ref() || null ?};
   _rok:=OKRO_F.ROK;
   _ttrok:=tab_tmp(1,'REF','STRING[16]',,'NAZ','STRING[4]',);
   ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ');
   ROK_F.prefix(REF.FIRMA);
   {? ROK_F.seek(_rok)
   || _ttrok.REF:=$ROK_F.ref; _ttrok.NAZ:=ROK_F.NAZ; _ttrok.add();
      _par85:=#PAR_SKID.get(85);
      _l_wstecz:={? _par85>-1
                 || _par85
                 || ROK_F.size()-1
                 ?};
      {! _i:= 1 .. _l_wstecz |!  {? ROK_F.prev() || _ttrok.REF:=$ROK_F.ref; _ttrok.NAZ:=ROK_F.NAZ; _ttrok.add() ?} !}
   ?};
   ROK_F.cntx_pop();
   TabRej2:=sql('select REJ.ROK, DOK_REJ.REFERENCE as REF from DOK_REJ join REJ '+
      'where DOK_REJ.JPK_TYP=\'F\' '+
      {? 1+JPK.DODAT<>'W'
      || 'and DOK_REJ.JPK_FA_T=\''+(1+JPK.DODAT)+'\' '
      || ''
      ?}+' and REJ.ROK in (select REF from :_a) order by 1',_ttrok
   )
|? JPK.TYP='MAG'
|| VAR_DEL.delete('MagNag','JPK_MAG');
   MagNag:=obj_new(9);
   {! _i:=1..obj_len(MagNag) |! MagNag[_i]:='' !};
   JPK_MAG:=JPK.DODAT
|? JPK.TYP='SF'
|| VAR_DEL.delete('eSprOkr');
   eSprOkr:=obj_new('RokB','RokP','OsprB','OsprP','MaskaB','MaskaP');
   eSprOkr.RokB:=SSTALE.AR;
   ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   eSprOkr.MaskaB:=SSTALE.AR().KOD;
   {? ROK_F.prev()
   || eSprOkr.RokP:=ROK_F.ref();
      eSprOkr.MaskaP:=ROK_F.KOD
   || eSprOkr.RokP:=null;
      eSprOkr.MaskaP:='__'
   ?};
   ROK_F.cntx_pop();
   OSPR.cntx_psh();
   OSPR.index('OKRES1'); OSPR.prefix(REF.FIRMA,'M',SSTALE.AR,JPK.OKRO_F().NR,JPK.OKRO_F().NR);
   OKRESY.OKRES1:=eSprOkr.OsprB:={? OSPR.first() || OSPR.ref() || null ?};
   {? eSprOkr.RokP
   || OSPR.prefix(REF.FIRMA,'M',eSprOkr.RokP,JPK.OKRO_F().NR,JPK.OKRO_F().NR);
      eSprOkr.OsprP:={? OSPR.first() || OSPR.ref() || null ?}
   || eSprOkr.OsprP:=null
   ?};
   OSPR.cntx_pop()
|? JPK.TYP='GV'
|| VAT_DEK.prefix();
   {? VAT_DEK.seek(BIT.sqlint(JPK.DODAT),)
   || J_GV_POZ.use(exec('maska_poz','!fks_ved_dvgd'))
   ?};
   exec('czytaj','#stalesys',DataOd,XINFO);
   HELPJPK.DATE:=date();
   exec('czytaj','#stalesys',{? var_pres('DATE',HELPJPK)>0 || HELPJPK.DATE || date() ?},XINFO,'URZAD')
?};
exec('gen_jpk','jpk',0);
{? 'VAT,V7M,V7K,GV'*JPK.TYP
|| exec('czytaj','#stalesys',date(),XINFO)
?};
VAR_DEL.delete('NoKom','JPK_RB','TabRej2','MagNag','JPK_MAG');
SSTALE.AR:=_ar;
SSTALE.AO:=_ao;
exec('open_tabele','open_tab');
_sha:=exec('shaDigest','jpk');
_sha=JPK.SHA


\wyc_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wycofanie akceptacji pliku JPK
::  OLD: \wyc_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_sig:=0;
{? JPK.TYP='SF'
|| _sig:=exec('is_sig','jpk_sf')
?};
{? JPK.STAT<>'N' & JPK.STAT<>'P'
|| FUN.info('Plik JPK został już wysłany.'@)
|? JPK.AKC<>'T'
|| FUN.info('Plik JPK nie jest zaakceptowany.'@)
|? JPK.TYP<>'SF' & FUN.ask('Czy wycofać akceptację pliku JPK?'@) |
   JPK.TYP='SF' & FUN.ask(
      {? _sig || 'Istnieją podpisy e-Sprawozdania.\nWycofanie akceptacji spowoduje ich usunięcie.\n\n'@ || '' ?}
      +'Czy wycofać akceptację pliku JPK?'@
   )
|| do();
   JPK_KOM.index('TYP'); JPK_KOM.prefix(JPK.ref(),'N');
   {? JPK_KOM.first()
   || JPK_KOM.del()
   ?};
   JPK.AKC:='N';
   JPK.D_AKC:=date(0,0,0);
   JPK.T_AKC:=time(0,0,0);
   JPK.U_AKC:='';
   {? JPK.STAT='P' || JPK.STAT:='N' ?};
   JPK.put();
   {? _sig
   || exec('jpk_sig_del','jpk_sf',0)
   ?};
   end()
?}

\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)='jpk_plik'
         || _jpk:=exec('FindAndGet','#table',JPK,_kref,,,null);
            {? _jpk=null
            || _wy.JPK:=null;
               {? var_pres('TYP',_wy)>0 || _wy.TYP:='' ?};
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1


:Sign Version 2.0 jowisz:1045 2023/09/07 12:13:11 424bd0ee99b3bc112e89b2ebc5a07008198385131f9b827ff216e851c51ee36ff9a75abaa42ce6c656236cdd79eb3d263c49bd425eab8e9e68b891ec10ce01277adcc64351962617621dd181035eb5a28b9885781b5a0c9191551455eca8760f6e57aca80332720c37dae3dacd74c38ffe09db064fbdf951c82cd2413e926d17

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmo_mob_dmag.fml
:: Utworzony: 12.02.2020
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Czynność LMO_MOB_DMAG - Potwierdzenie dyspozycji w magazynie
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# permissions=ODDZ
::# kind=WE,  symbol=EANN,    type=_EANN,    name=Operacja mobilna,              required=N,   keyref=T
::# kind=WE,  symbol=MG,      type=_MG,      name=Magazyn dla operacji,          required=N,   fml_val="exec('mg','!lmo_mob_dinw')"
::# kind=WE,  symbol=OKR,     type=NUMBER,   name=O ile przesunąć datę,          required=N
::# kind=WY,  symbol=TR_NZL,  type=_TR_NZL,  name=Dyspozcja w magazynie,         required=N
::# kind=WY,  symbol=IL_SRV,  type=NUMBER,   name=Ilość zatwierdzonych operacji, required=N

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

exec('init','lmo');

_akcja:=_mp.akcja();
_auto:=_mp.isAutoRun();
_service:=_mp.isService();
_proces:=_mp.pathProc();
_area:=_mp.pathArea();
_grp:=_mp.isGroup();

_mp.trigRef('TR_NZL',,1,,'INT','TR_NZL');

_select:=1;

:: ewentualny wybór operacji
{? ~_service & ~(var_pres('EANN',_in)=type_of(null()) & _in.EANN) & ~_area
|| _opc:=FUN.choice('Wybierz typ operacji'@,,'&Przyjęcie dostawy'@,'&Wydanie magazynowe'@,'&Kompletacja wysyłki'@);
   {? _opc
   || _typ:={? _opc=1 || 'P'
            |? _opc=2 || 'W'
            |? _opc=3 || 'Z'
            || 'X'
            ?};
      exec('openean','open_tab',ST.ODDZ+'__');

      {? exec('operacje','magazyn_mob',_typ,,,0)
      || _in.EANN:=EANN.ref();
         _mp.save('IN','EANN',EANN.ref())
      || _select:=0
      ?}
   || _select:=0
   ?}
?};

{? ~_service & ~(var_pres('EANN',_in)=type_of(null()) & _in.EANN)
|| {? ~_select
   || _mp.cancel()
   || _mp.error('Brak wymaganego parametru EANN.')
   ?}
|? ~_service
 & (~((';PZW'*exec('FindAndGet','#table',EANN,_in.EANN,,"TYP",''))>1)
    | exec('FindAndGet','#table',EANN,_in.EANN,,"4+REFSQL",'')<>'trnl')
|| _mp.error('Parametr EANN wskazuję na inną operację mobilną - niezwiązaną z dyspozycją w magazynie.')
|? _service
|| {? var_pres('MG',_in)<>type_of(0) || _in.MG:=null() ?};
   {? var_pres('OKR',_in)<>type_of(0) | _in.OKR<0 || _in.OKR:=0 ?};
   _mp.save('IN',_in);
   EANN.cntx_psh();
   EANN.prefix();
   params_set('mp',_mp,'in',_in,'out',_out);
   exec('autoOper','!lmo_mob_dmag');
   EANN.cntx_pop()
|| {? var_pres('MG',_in)<>type_of(0) || _in.MG:=null() ?};
   {? var_pres('OKR',_in)<>type_of(0) | _in.OKR<0 || _in.OKR:=0 ?};
   _mp.save('IN',_in);
   EANN.cntx_psh();
   _jest:=EANN.seek(_in.EANN);
   _typo:=EANN.TYP;
   {? ~_jest
   || _mp.error('Nie znaleziono operacji mobilnej.')
   |? (_wyp:=exec('ctrlEANN','magazyn_mob',_typo,0))<=0
   || _mp.cancel()
   || {? _akcja='Generuj_wg_ope'
         | _proces
         | _mp.pathTodo()
      || params_set('mp',_mp,'in',_in);
         _tr_nzl:=exec('realeann_dysp','magazyn_mobi',EANN.ref(),{? _grp || 1 || _auto ?});
         {? _tr_nzl<>null()
         || _mp.save(exec('kind_out','#b_port'),'TR_NZL',_tr_nzl);
            _mp.done()
         || _mp.cancel()
         ?}
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   EANN.cntx_pop()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_in','#b_port'));

_wgr:={? var_pres('EANN',_in)<>type_of(~~) & _in.EANN
      || _in.EANN
      || null()
      ?};

{? _wgr<>null()
|| 'Potwierdź dyspozycję w magazynie na podstawie operacji mobilnej %1'@@[exec('record','#to_string',_wgr)]
|| 'Potwierdź dyspozycję w magazynie'@@
?}


\mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Formuła na wartość parametru MG
::   WY: TYPYDOK.ref()
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null();
MG.cntx_psh();
MG.index('SL_ALL');
MG.prefix('T');
{? MG.first()
|| MG.win_sel('SLO');
   {? MG.select() || _wyn:=MG.ref() ?}
?};
MG.cntx_pop();
_wyn


\autoOper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Automatycznie realizuje operacje
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_il_srv:=0;

_eann:=sql('select '+
           '  EANN.REFERENCE REF, '+
           '  EANN.DATA DATA '+
           'from @EANN '+
           ' where EANN.REFERENCE like '+'''eann_\\_\\_%'' escape ''\\'''+
           '   and EANN.AKT=''T'''+
           '   and EANN.TYP in (''Z'',''P'',''W'')'+
           '   and substr(EANN.REFSQL,1,4)=''trnl'''+
           '   and EANN.STAN=''Z''');

_eann.clear();
{? _eann.first()
|| {!
   |? _oddz:=1+(4-_eann.REF);
      _rok:=form((_eann.DATA~1)-2000,-2,0,'99');
      exec('open','open_tab',_oddz,_rok);
      {? (EANN.clear(); EANN.seek(_eann.REF))
      || _mag:=exec('one_mag','magazyn_mob',EANN.ref());
         _wyb:=0;
         _typ:=EANN.TYP;
         {? _mag<>null()
         || EANN.MG:=_mag;
            EANN.put(1)
         ?};
         {? (_wyp:=exec('ctrlEANN','magazyn_mob',_typ,1))>0
         || _il_srv+=params_exec('autooper','magazyn_mob',EANN.ref(),_wyp,,0,,1)
         ?}
      ?};
      _eann.next()
   !}
?};
obj_del(_eann);

_out.IL_SRV:=_il_srv;
_mp.save(,_out);
_mp.done();
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 0fbc8ab2570763dec15d3d335ce7b01572c9e47704b72bacff3f4f5477fa792f86b93b3414d82498bfd406ee7196f147377dcccf51cc80d94dde98338b54cab3fdb3879f0aa3329d6b10543cefd8af0e76988302169c09b5a1ebad2aceb43e00a81fd90be7b9c3891d9fc284e75ade82405474ff5172e5c9c4ed1838328685da

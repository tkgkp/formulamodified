:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_zam_dazs.fml
:: Utworzony: 13.11.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły czynności LMG_ZAM_DAZS - Archiwizacja zamówienia wewnętrznego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE, symbol=ZK_N,     type=_ZK_N,  name=Zamówienie wewnętrzne,              required=N,    keyref=T
::# kind=WE, symbol=DNI,      type=NUMBER, name=Ilość dni wstecz do archiwizacji,   required=N
::# kind=WE, symbol=PROCENT,  type=NUMBER, name=Procent realizacji do archiwizacji, required=N
::# kind=WE, symbol=ODDZIAL,  type=STRING, name=Kod oddziału do archiwizacji,       required=N,    fml_val="exec('oddzial','!lmg_zam_dazs')"
::# kind=WE, symbol=MAXZAM,   type=NUMBER, name=Maksymalna liczba do archiwizacji,  required=N
::# kind=WY, symbol=ZK_N,     type=_ZK_N,  name=Zamówienie wewnętrzne,              required=N,    keyref=T
::# kind=WY, symbol=IL_SERV,  type=NUMBER, name=Ilość zarchiwizowanych zamówień,    required=N,    keyref=N
::# properties=SERVICE

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

{? var_pres('DNI',_in)<>type_of(0) | _in.DNI<0
|| _in.DNI:=-1;
   _mp.save('IN')
?};
{? var_pres('PROCENT',_in)<>type_of(0) | _in.PROCENT<0 | _in.PROCENT>100
|| _in.PROCENT:=-1;
   _mp.save('IN')
?};
{? var_pres('MAXZAM',_in)<>type_of(0) | _in.MAXZAM<=0
|| _in.MAXZAM:=-1;
   _mp.save('IN')
?};
_typ_oddzial:=var_pres('ODDZIAL',_in)<>type_of('');
{? _typ_oddzial | (+_in.ODDZIAL)>1 | ~exec('isLetter','#string',_in.ODDZIAL,1)
|| _in.ODDZIAL:={? _typ_oddzial || '' || '!' ?};
   _mp.save('IN')
?};
_max:=_in.MAXZAM;

_akcja:=_mp.akcja();
_auto:=_akcja<>'Archiwizuj' & _mp.isAutoRun();
_service:=_mp.isService();
_grupa:=_mp.isGroup();

{? _service & ((_in.DNI<0 & _in.PROCENT<0) | _in.ODDZIAL='!')
|| _mp.error('Nieprawidłowe parametry dla czynności serwisowej.')
|? ~_service & ~(var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N)
|| _mp.error('Brak wymaganego parametru ZK_N.')
|| ZK_N.cntx_psh();
   ZK_N.prefix();
   _ok:=1;
   {? ~_service
   || {? ~ZK_N.seek(_in.ZK_N)
      || _ok:=0;
         _mp.error('Nie znaleziono zamówienia wewnętrznego.')
      |? ZK_N.T().R<>'W'
      || _ok:=0;
         _mp.error('Błędny parametr wejściowy ZK_N. Wymagane zamówienie wewnętrzne.')
      ?}
   ?};
   {? _ok
   || _mp.trigRef('ZK_N',,0,,exec('kind_out','#b_port'),'ZK_N');
      params_set('mp',_mp,'in',_in,'out',_out);
      {? _akcja='Archiwizuj' | _auto | _service
      || exec('archiwizuj','!lmg_zam_dazs',{? _service || 2 || _auto ?},_in.DNI,_in.PROCENT,_in.ODDZIAL,_grupa,_max)
      |? _mp.pathTodo() | _mp.pathArea() & _akcja=''
      || _win_red:=exec('zknag_win_edit','zamsiw_nag','REDW');
         _ff:="params_exec('zknag_pozycje_todo','!lmg_zam_dazs')";
         ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
         _ff:="params_exec('zknag_archiwum_todo','!lmg_zam_dazs'); 'key:Esc'";
         ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Arc&hiwizuj'@],_ff);
         _ff:="'key:Esc'";
         ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
         ZK_N.win_edit(_win_red);
         ZK_N.display()
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   ZK_N.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZK_N',_in)<>type_of(~~) & _in.ZK_N<>null()
|| 'Zarchiwizuj zamówienie wewnętrzne: %1'@@[exec('record','#to_string',_in.ZK_N)]
|| ''
?}


\zknag_archiwizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie wewnętrzne - Archiwizuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_tab:=ZK_N.sel_aget();
ZK_N.sel_adel();

{? _tab.size()
|| _ok:=FUN.ask('Ilość zamówień do archiwizacji: %1.\n'
                'Operacja może być czasochłonna.\n\nCzy kontynuować?'@[$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LMG_ZAM_DAZS';
   _params.UIDREF:=ZK_N.uidref();
   _params.AKCJA:='Archiwizuj';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZK_N',ZK_N.ref());

   exec('mp_run','#b__box',_params)
|? _ok=1
|| exec('ini_kom','#message','Archiwizacja zamówień'@);
   exec('zam_arch_InitRes','zamsiw_wspolne');
   ZK_N.cntx_psh();
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? (ZK_N.clear(); ZK_N.seek(_tab.REF,))
         || _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:='LMG_ZAM_DAZS';
            _params.UIDREF:=ZK_N.uidref();
            _params.GRUPA:='T';
            _params.AKCJA:='Archiwizuj';
            _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
            exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZK_N',ZK_N.ref());

            exec('mp_run','#b__box',_params);
            obj_del(_params)
         ?};
         _tab.next()
      !}
   ?};
   exec('zam_arch_DoneRes','zamsiw_wspolne');
   ZK_N.cntx_pop();
   exec('end_kom','#message')
?};
obj_del(_tab)


\zknag_archiwum_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie wewnętrzne - Archiwizuj z todo
::   WE: params_get()
::----------------------------------------------------------------------------------------------------------------------
params_exec('archiwizuj','!lmg_zam_dazs');
''


\zknag_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie wewnętrzne - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('zam_poz','zamsiw_poz',1,0);
''


\archiwizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie wewnętrzne - Archiwizacja zamówienia wewnętrznego
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie) 2-serwisowa
::       [_b] - ilość dni wstecz
::       [_c] - procent realizacji zamówienia
::       [_d] - oddzial lub pusty string
::       [_e] - czy grupa rekordów
::       [_f] - maksymalna liczba zamówień do pojedynczej archiwizacji
::       params_get()
::  OLD: \zam_arch/zk2.fml
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_dni:={? var_pres('_b')=type_of(1) || _b || 0 ?};
_procent:={? var_pres('_c')=type_of(1) || _c || 0 ?};
_oddzial:={? var_pres('_d')=type_of('') || _d || '' ?};
_grupa:={? var_pres('_e')=type_of(0) || _e || 0 ?};
_maxzam:={? var_pres('_f')=type_of(0) & _auto=2 || _f || -1 ?};

exec('declare','tech_doc');

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_result:=0;

_txt:={? _auto<>2 & ZK_N.A='A' & ZK_N.OBI_POW<>''
      || '\n\nUwaga. Zamówienie powiązane z dokumentem w obiegu.'@
      || ''
      ?};
{? _auto=2
||
:: czynność serwisowa
   exec('openz_psh','open_tab');
   _oddz:=ST.ODDZ;
   _today:=date();
   ODDZ.cntx_psh();
   ODDZ.index('KOD2');
   {? _oddzial<>'' || ODDZ.prefix(_oddzial,) || ODDZ.prefix() ?};
   {? ODDZ.first()
   || {!
      |? ST.ODDZ:=ODDZ.KOD;
         exec('openz','open_tab',ODDZ.KOD+'__');
         ZK_N.cntx_psh();
         ZK_N.index('ZAM_KL');
         ZK_N.prefix('W');
         {? ZK_N.first()
         || {!
            |? _ref:=ZK_N.ref();
               _oki:=ZK_N.next();
               ZK_N.cntx_psh();
               ZK_N.clear();
               {? (#_ref)>0 & ZK_N.r_lock(1,1,1,#_ref,ref_name(_ref)) & (ZK_N.seek(_ref);ZK_N.r_unlock();1)
                 & {? _dni>=0 || ZK_N.DP<=(_today-_dni) || 1 ?} & {? _procent>=0 || ZK_N.PR>=_procent || 1 ?}
               || _old:=ZK_N.uidref();
                  _result+=(_new:=exec('zam_arch','zamsiw_wspolne'))<>'';
                  exec('uidref_update','#b__box',_old,exec('FindAndGet','#table',ZK_N,_new,,"uidref()",''))
               ?};
               ZK_N.cntx_pop();
               _oki & (_maxzam<=0 | _result<_maxzam)
            !}
         ?};
         ZK_N.cntx_pop();
         ODDZ.next()
      !}
   ?};
   ODDZ.cntx_pop();
   exec('openz_pop','open_tab');
   ST.ODDZ:=_oddz;
   _out.ZK_N:=null();
   _out.IL_SERV:=_result;
   _mp.save(,_out);
   _mp.done()
|? _auto | _grupa | FUN.ask('Zarchiwizować zamówienie wewnętrzne %1?%2'@[ZK_N.SYM,_txt])
|| _old:=ZK_N.uidref();
   _new:=exec('zam_arch','zamsiw_wspolne',{? _grupa || 2 || 1 ?});
   ZK_N.cntx_psh();
   {? _new<>''
   || _result:=1;
      exec('uidref_update','#b__box',_old,exec('FindAndGet','#table',ZK_N,_new,,"uidref()",''));
      _out.ZK_N:=exec('FindAndGet','#table',ZK_N,_new,,,null())
   ?};
   _out.IL_SERV:=_result;
   _mp.save(,_out);
   ZK_N.cntx_pop();
   _mp.done()
?};
_result


\zknag_przywroc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie wewnętrzne - Archiwizuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_tab:=ZK_N.sel_aget();
ZK_N.sel_adel();

{? _tab.size()
|| _ok:=FUN.ask('Ilość zamówień do przeniesienia z archiwum: %1.\n'
               'Operacja może być czasochłonna.\n\nCzy kontynuować?'@[$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| exec('declare','tech_doc');
   exec('zam_arch','zamsiw_wspolne')
|? _ok=1
|| exec('ini_kom','#message','Przywrócenie zamówień');
   exec('zam_arch_InitRes','zamsiw_wspolne');
   exec('declare','tech_doc');
   ZK_N.cntx_psh();
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? (ZK_N.clear(); ZK_N.seek(_tab.REF,))
         || exec('zam_arch','zamsiw_wspolne',2)
         ?};
         _tab.next()
      !}
   ?};
   exec('zam_arch_DoneRes','zamsiw_wspolne');
   ZK_N.cntx_pop();
   exec('end_kom','#message')
?};
obj_del(_tab)


\oddzial
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na wartość parametru ODDZIAL
::   WY: ODDZ.KOD
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
ODDZ.cntx_psh();
ODDZ.index('KOD');
ODDZ.prefix();
ODDZ.win_sel('SEL');
{? ODDZ.select() || _wyn:=ODDZ.KOD ?};
ODDZ.cntx_pop();
_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 91458614852877b3f2e87f7ff9619b7f4ce8ad3016c313fc1a683804fe698e60be51a71c018cec937ab30cde3d9f948f792e3a986b469895415a1b6b48a9c8591eefe7e18c39ba941a01ab3bf8fff2ca6e70c1d797481f0d066ea4437e36f8901040742a75271946be5b4e58633c2d328018b7f7a331f5b48acadbbaf8534209

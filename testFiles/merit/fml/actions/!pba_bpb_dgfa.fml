:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_dgfa.fml
:: Utworzony: 03.08.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Obsługa czynności - PBA_BPB_DGFA - Generowanie formularzy ankiet.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Generowanie formularzy ankiet - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=ZA_ZEST, type=_ZA_ZEST, name=Opis ankiety, required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;

_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error(exec('error','!pba_bpb_dgfa'));
   return()
?};

_id:=exec('ref2uid','#table',_in.ZA_ZEST);

_result:='';

{? _id=''
|| _result:=exec('error','!pba_bpb_dgfa')

|? _mp.isMicro()
|| _mp.keyRef(_id);
   exec('select','!pba_bpb_dgfa',_in.ZA_ZEST,_mp);
   _mp.delRef(_id);
   _mp.cancel()

|| _value:=exec('select','!pba_bpb_dgfa',_in.ZA_ZEST,_mp);
   {? type_of(_value)=type_of(0)
   || {? _value<>0
      || _mp.done()
      || _mp.keep()
      ?}
   || _result:=_value
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg('%1'@[_result])
?};

~~


\za_form_generuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [18.02]
:: OPIS: Generowanie formularzy ankiet na podstawie kryteriow doboru
::  OLD: \ank_kryt_gen/zz_data.fml
::----------------------------------------------------------------------------------------------------------------------
P.index('FIRMA_ZA');
P.prefix(ZA_ZEST.FIRMA);
_count:=0;
_size:=P.size;

_data:=date;
_link:=ZA_ZEST.ZZ_DOK;
_typ_op:=exec('typ_op','phr_dane');
_info:='Proszę czekać. Trwa przygotowanie formularzy...'@;
ZZ_OSOBA.cntx_psh;

: przegladaj pracownikow
{? ZA_ZEST.ZZ_LINK().KLASA<>'SZK_OPIS'
|| _ask:=FUN.ask('Wygenerować formularze na podstawie zdefiniowanych kryteriów doboru?'@);
   _loop:=P.first;
   {? _ask
   || {!
      |? _loop
      |! progress(100*(_count+=1)/_size,_info,FUN.TYT);
:     uwzglednij, jesli pracownik spelnia kryterium doboru
         {? exec('test_kryt','phr_dane',_link,_typ_op,0,_data,P)>0 &
            ~exec('za_form_jest','phr_dane',P.ZZ_DOK,ZA_ZEST.ref)
         || ZA_FORM.blank;
:     ankieta wlasciwa dla respondenta
            ZA_FORM.ZZ_LINK:=P.ZZ_DOK;
            ZA_FORM.ZZ_KTO:=exec('czy_osoba','phr_dane',P,1);
            ZA_FORM.DRUK:='N';
            ZA_FORM.add
         ?};
         _loop:=P.next
      !}
   ?}
|? ZA_ZEST.ZZ_LINK().KLASA='SZK_OPIS' &  (ZA_ZEST.ZA_TYP().SYMBOL='ES' | ZA_ZEST.ZA_TYP().SYMBOL='PO')
|| exec('ank_szk_po','phr_dane')
?};
prgs_clr;
ZZ_OSOBA.cntx_pop()


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestracja formularzy ankiet niemożliwa.\nNie znaleziono udostępnionej ankiety.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Wybór  ocen właściwych dla programu.
::   WE: _a [_ZO_PROC] - wskazanie sesji ocen
::       _b [OBJECT] - wskazanie menadżera procesów
::   WY: 1 - czynność została zakończona
::       0 - czynność należy podtrzymać na liście zadań
::       [STRING] - komunikat błędu
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
exec('init','pba');

ZA_ZEST.cntx_psh();
ZA_ZEST.prefix();
{? ZA_ZEST.seek(_a)
||
   _TAB:=($ZA_ZEST.ZZ_LINK().KLASA)();
   _typ:=ZA_ZEST.ZA_TYP().SYMBOL;

   ZA_FORM.cntx_psh();
   ZA_FORM.index('ZA_ZEST');
   ZA_FORM.prefix(ZA_ZEST.ref());
   ZA_NOTA.cntx_psh();
   ZA_NOTA.index('ZA_NOTA');
   _cfg:=exec('setup_wnd','!pba_bpb_dgfa',_b,ZA_ZEST.ref(),_TAB);
   params_set('cfg',_cfg);
   ZA_FORM.win_sel(_cfg.window);
   ZA_FORM.hdr_sel(' (%1)'[exec('record','#to_string',_a,1)]);
   _ret:=ZA_FORM.select();
   ZA_NOTA.cntx_pop();
   ZA_FORM.cntx_pop()
?};
ZA_ZEST.cntx_pop();
_ret


\setup_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [19.22]
:: OPIS: Tworzy okienko czynności.
::   WE: _a [OBJECT] - wskazanie menadżera procesów
::       _b - wskazanie na ankietę
::       _c - wskazanie na miejsce pochodzenia ankiety
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_bar:=ZZ_POM.mk_edit(,0);
_bid:=ZZ_POM.win_ebtn(_bar,'text=%1,display=1'[exec('text_red_zakoncz','#window','PKD_A')],
   "sel_exit();''"
);
ZZ_POM.btn_opt(_bid,'tooltip='+exec('help_act_zakoncz','#window'));

_bid:=ZZ_POM.win_ebtn(_bar,'text=%1,display=1'['&Anuluj'@],
   "'key:Esc'"
);
ZZ_POM.btn_opt(_bid,'tooltip='+exec('help_act_esc','#window'));

_typ:='';
_tryb:='';
_nazwa:='';
ZA_ZEST.cntx_psh();
ZA_ZEST.prefix();
{? ZA_ZEST.seek(_b)
|| _typ:=ZA_ZEST.TYP().KOD;
   _tryb:=ZA_ZEST.TRYB;
   _nazwa:=ZA_ZEST.NAZWA;
   _anonim:=ZA_ZEST.ANONIM='T'
?};
ZA_ZEST.cntx_pop();

_cfg:=obj_new('window','ZA_NOTA','ZA_FORM');

_cfg.ZA_FORM:=obj_new('wv','wh','wt','wnd');
_cfg.ZA_FORM.wv:='WER_VP';
_cfg.ZA_FORM.wh:='WER_HP';
_cfg.ZA_FORM.wt:={? _c<>SZK_OPIS | (_c=SZK_OPIS & SZK_OPIS.TRYB='T') || 'PROC_P' || 'TESTPP' ?};
_cfg.ZA_FORM.wnd:=
   {? _typ='T'
   || _cfg.ZA_FORM.wt
   || {? _anonim
      || _cfg.ZA_FORM.wv
      || _cfg.ZA_FORM.wh
      ?}
   ?};

_cfg.ZA_NOTA:=obj_new('wv','wh','wt','wnd');
_cfg.ZA_NOTA.wv:='WER_VP';
_cfg.ZA_NOTA.wh:='WER_HP';
_cfg.ZA_NOTA.wt:='TESTP';
_cfg.ZA_NOTA.wnd:=
   {? _cfg.ZA_FORM.wnd='WER_VP'
   || _cfg.ZA_NOTA.wv
   |? _cfg.ZA_FORM.wnd='WER_HP'
   || _cfg.ZA_NOTA.wh
   |? _cfg.ZA_FORM.wnd=_cfg.ZA_FORM.wt
   || _cfg.ZA_NOTA.wt
   ?};
params_set('cfg',_cfg);
_mode:='maximized';
: grupowe okienko modulu ankiet
_wnd:=ZA_FORM.grp_make('Formularze ankiety '+_nazwa,
   "
       params_set(_par:=params_get());
      _cfg:=_par.cfg
   ",
   'form_ank',,,
   "1"
);
ZA_FORM.grp_sel(_wnd,ZA_FORM,_cfg.ZA_FORM.wnd,,
   "
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _ag:='';
      ZA_FORM.actions_grayed(_cfg.ZA_FORM.wnd,_ag);
      grp_disp(ZA_NOTA,_cfg.ZA_NOTA.wnd)
   ",,,10,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg
   ",,,,'maximized'
);
{? _cfg.ZA_FORM.wnd=_cfg.ZA_FORM.wv
|| ZA_FORM.grp_splt(_wnd,,'vertical','right_1');
   ZA_FORM.grp_sel(_wnd,ZA_NOTA,_cfg.ZA_NOTA.wnd,,
      "  params_set(_par:=params_get());
         _cfg:=_par.cfg;
         _ag:='';
         win_disp();
         ZA_NOTA.actions_grayed(_cfg.ZA_NOTA.wnd,_ag)
      ",,,6,
      "   params_set(_par:=params_get());
         _cfg:=_par.cfg;
         ZA_NOTA.index('ZA_NOTA');
         {? grp_empty(ZA_FORM,_cfg.ZA_FORM.wnd)
         || ZA_NOTA.prefix(null())
         || ZA_NOTA.prefix(ZA_FORM.ref)
         ?}
      "
   )
|| ZA_FORM.grp_splt(_wnd,,'horizontal','bottom');
   ZA_FORM.grp_sel(_wnd,ZA_NOTA,_cfg.ZA_NOTA.wnd,,
      "  params_set(_par:=params_get());
         _cfg:=_par.cfg;
         _ag:='';
         win_disp();
         ZA_NOTA.actions_grayed(_cfg.ZA_NOTA.wnd,_ag)
      ",,,6,
      "   params_set(_par:=params_get());
         _cfg:=_par.cfg;
         ZA_NOTA.index('ZA_NOTA');
         {? grp_empty(ZA_FORM,_cfg.ZA_FORM.wnd)
         || ZA_NOTA.prefix(null())
         || ZA_NOTA.prefix(ZA_FORM.ref)
         ?}
      ",,,,
      'maximized_with_title'
   )
?};
{? ~_a.isMicro()
|| {? _cfg.ZA_FORM.wnd=_cfg.ZA_FORM.wv
   || ZA_FORM.grp_splt(_wnd,,'vertical','bar')
   || ZA_FORM.grp_splt(_wnd,,'horizontal','bar')
   ?};
   ZA_FORM.grp_edit(_wnd,ZZ_POM,_bar,,,,,,'maximized')
?};

_cfg.window:=_wnd;
_cfg

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 42856a43552acbe8c2e5e6c799fd7a995b912c34b4d77593fc9035048142736a336fc18340e604ef4ca5d6f7b56ce8fe90e94626c7943a21f407e22d0f4f23450a8ab4ebfc8cf2a14f8d53c976f5e823d8121ca6614d3a854e32db8345472c6c2c56396fca481d3ebaa0da7ab039c80d90ff34cd1d82172e38661043dd767fda

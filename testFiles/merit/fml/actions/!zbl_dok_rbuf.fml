:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zbl_dok_rbuf.fml
:: Utworzony: 22.09.2020
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Obsługa dokumentów Businesslink
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Formuła główna czynności
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,   symbol=ACT,    type=STRING,   name=Akcja                ,   required=N, fml_val="exec('bl_act_slo','zbl_dok',_a)"
::# kind=WE,   symbol=DOKUM,  type=_DOKUM,   name=Dokument Businesslink,   required=N
::# kind=WY,   symbol=DOKUM,  type=_DOKUM,   name=Dokument Businesslink,   required=N
::# properties=SERVICE
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_akcja:=_mp.akcja();
_auto:=_akcja<>'Akceptuj' & (_mp.isAutoRun() | _mp.isService());

_dokum:=_in.DOKUM;
{? type_of(_dokum)<>type_of(null()) || _dokum:=null() ?};
_rodzaj_k:=exec('FindAndGet','#table',DOKUM,_dokum,,"@.DOKUM.RODZAJ_K",'');
_act:=_in.ACT;
{? type_of(_act)<>type_of('') || _act:={? _rodzaj_k='P' || 'AUT' || '' ?} ?};
_act:=exec('bl_act_kod','zbl_dok',_act);

_out.DOKUM:=_in.DOKUM;
_mp.save(,_out);

_proc:=_mp.pathProc();

{? _mp.isService()
||
   _user:=OPERATOR.USER;
   _user_kod:=exec('get','#params',100363);
   OPERATOR.USER:=exec('FindInSet','#table','USERS','USR_KKOD',_user_kod,_user_kod,,1,,null())
?};

{? OPERATOR.USER=null()
||
   exec('FindAndGet','#table',DOKUM,_dokum,,"@.DOKUM.BL_LOG:=null(); @.DOKUM.put()");
   _edi:=exec('env','edi_wspolne');
   {? ~_edi.init()
   ||
      FUN.info('Nie powiodło się zainicjowanie środowiska.'@)
   ||
      _kr_op:=exec('FindAndGet','#table',DOKUM,_dokum,,"@.DOKUM.KR_OP",'');
      {? _kr_op<>'' || _edi.log(_kr_op) ?};
      _edi.log('Nieokreślony użytkownik systemu.'@);
      {? _mp.isService()
      ||
         _edi.log('Użytkownika dla przetwarzania wsadowego należy podać w parametrze 100363.'@)
      ?};
      exec('FindAndGet','#table',DOKUM,_dokum,,"_edi:=_b; @.DOKUM.bl_put('BL_LOG',_edi.LOG,,,'log.txt')",,_edi)
   ?};
   _mp.done();
   return()
?};

_continue:=1;

DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
||
   FUN.info('Nie znaleziono dokumentu Businesslink.'@);
   _continue:=0
?};
:: Dokument przychodzący/wychodzący Businesslink
{? _continue>0
||
:: Przypisanie kontrahenta w przypadku braku, gdy jest dostępny jego tenant_id
   {? DOKUM.KH=null() & DOKUM.BL_KHUID<>''
   || exec('kh_repair','businesslink3',DOKUM.ref())
   ?};
   _sza:=_auto | _mp.pathProc();
   {? DOKUM.RODZAJ_K='P'
   ||
      exec('plugins_bl_in','zbl_dok',DOKUM.ref(),_sza,_act)

   |? DOKUM.RODZAJ_K='W'
   ||
      exec('plugins_bl_out','zbl_dok',DOKUM.ref(),_sza,_act)
   ?}
?};
DOKUM.cntx_pop();
{? _mp.isService() || OPERATOR.USER:=_user ?};
_mp.done();
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 66d35e800b15dbd72c1ed6c759ea720182919f43980976bd8164928c113b60f445e072a38607ee05ce45d179ff434734cf715a4b34bf8b3837bb6e8806717ab4e8b5000a51f940ab98724fdf72a813b33bb9befed6ebd10a846c8dfce28ff0e073d995e9ef77b622ed357fcecf1c3cb6e52acf4380cf6a82fd87301ecf03254c

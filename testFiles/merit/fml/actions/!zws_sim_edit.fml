:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_sim_edit.fml
:: Utworzony: 2017.05.17
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_SIM_EDIT - Edycja danych o kartach SIM
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Formuła główna czynności ZWS_SIM_EDIT
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Formuła na opis TO-DO czynności ZWS_SIM_EDIT
::----------------------------------------------------------------------------------------------------------------------
''@@


\sim_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Dołączanie danych kart SIM
::----------------------------------------------------------------------------------------------------------------------
NRKRTSIM.win_edit('RED'); NRKRTSIM.blank();
NRKRTSIM.memo_set(,'UWAGI');
{? NRKRTSIM.edit("exec('spr_krt_sim','!zws_sim_edit')") & NRKRTSIM.add()
|| NRKRTSIM.memo_put(,'UWAGI')
?}


\sim_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Poprawianie danych kart SIM
::----------------------------------------------------------------------------------------------------------------------
{? NRKRTSIM.get()
|| {? NRKRTSIM.r_lock(1,1)
   || NRKRTSIM.win_edit('RED');
      NRKRTSIM.memo_get(,'UWAGI');
      {? NRKRTSIM.edit("exec('spr_krt_sim','!zws_sim_edit')") & NRKRTSIM.put()
      || NRKRTSIM.memo_put(,'UWAGI')
      ?};
      unlock_r()
   || FUN.info('Dane karty SIM wykorzystywane przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono karty SIM.'@)
?}


\sim_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Usuwanie danych kart SIM
::----------------------------------------------------------------------------------------------------------------------
{? NRKRTSIM.get()
|| {? NRKRTSIM.r_lock(1,1)
   || {? FUN.ask('Usunąć dane karty SIM?'@)
      || do();
         _delr:=exec('del_zasob','zasoby_wspolne',2);
         {? _delr
         || exec('del_att','dok_fks',$NRKRTSIM.ref());
            _delr:=NRKRTSIM.del(,1);
            {? _delr=0 || undo() ?}
         ?};
         end();
         {? ~_delr
         || FUN.info('Nie można usunąć danych karty SIM. Istnieją powiązane struktury.'@)
         ?}
      ?};
      unlock_r()
   || FUN.info('Dane karty SIM wykorzystywane przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono karty SIM.'@)
?}


\spr_krt_sim
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Sprawdzenie rekordów w numerach kart SIM
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? +NRKRTSIM.NR_KARTY<9
|| FUN.info('Numer karty SIM musi zawierać od 9 do 12 znaków.'@); _zwrot:='NR_KARTY'
?};
{? _zwrot='' & NRKRTSIM.NR_KARTY<>''
|| _ref:={? -menu_txt()='popraw' || NRKRTSIM.ref() || null ?};
   NRKRTSIM.cntx_psh(); NRKRTSIM.index('DISP'); NRKRTSIM.prefix(REF.FIRMA,NRKRTSIM.NR_KARTY,);
   {? NRKRTSIM.first() & (_ref=null | _ref<>NRKRTSIM.ref())
   || FUN.info('Znaleziono pozycję z podanym numerem karty SIM.'@); _zwrot:='NR_KARTY'
   ?};
   NRKRTSIM.cntx_pop()
?};
_zwrot


\wz_nr_sim
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wzorzec numeru karty SIM
::----------------------------------------------------------------------------------------------------------------------
'I!'


\nrkrtsim_trig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Triggery dla tabeli NRKRTSIM
::   WE: 1 - add po
::       2 - put po
::----------------------------------------------------------------------------------------------------------------------
ZASOBY.cntx_psh(); ZASOBY.index('ZASOBY'); ZASOBY.prefix(NRKRTSIM.ID);
{? ~ZASOBY.first()
|| ZASOBY.TYP:='S';
   ZASOBY.KARTOTEK:=NRKRTSIM.ID;
   ZASOBY.add(1)
?};
ZASOBY.cntx_pop();
~~





:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:24 0a23c6286c3acd3a272b6fc03640b209a3ff4378547a3e5b29623e433e17d5217de2622978b5b98b71ef8c60832bb09029f527263f0997b53542246f7b5962bfc5f4f31051ad13ebac3138b18ff6b575c527a8ca637b4f64970cf7664a61c6c7c8323ae33af3f16d29f023b69ac0b5b980750d529fea2c09fcd51c64f548ffaa

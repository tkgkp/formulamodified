:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zui_cli_ager.fml
:: Utworzony: 20.01.2020
:: Autor: TS
::======================================================================================================================
:: Zawartość: MacroWebApiCLIENT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Formuła główna czynności
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

::# properties=SERVICE,GRP_FIRM

::# kind=WE, symbol=PD, type=STRING, name="Przeznaczenie danych", required=T, fml_val="exec('pd_select','!zui_cli_ager',_a)"
::# kind=WE, symbol=TAB, type=STRING, name="Akronim tabeli", required=N, fml_val="exec('tab_select','!zui_cli_ager',_a)"
::# kind=WE, symbol=TYPE, type=STRING, name="Typ obsługi", required=N, fml_val="exec('type_select','!zui_cli_ager',_a)"
::# kind=WE, symbol=METHOD, type=STRING, name="Metoda API", required=N
::# kind=WE, symbol=METHODS, type=MEMO, name="Metody API (lista oddzielona przecinkami)", required=N
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, BŁĄD)", required=N

_pd:=_in.PD;
_tab_acr:={? var_pres('TAB',_in)=type_of('') || _in.TAB || '' ?};
_type:={? var_pres('TYPE',_in)=type_of('') || _in.TYPE || '' ?};
_method:={? var_pres('METHOD',_in)=type_of('') || _in.METHOD || '' ?};
_method+={? _method<>'' || _method+',' ?};
_method+={? var_pres('METHODS',_in)=type_of('') || _in.METHODS || '' ?};

:: W trybie serwisowym uruchamiane są definicje dla wskazanego przeznaczenia danych:
::    najpierw dla wysyłania danych a następnie dla odbierania danych.
:: Jeżeli wskazany jest typ obsługi, to wykonywane jest tylko wysyłanie albo tylko odbieranie danych.
:: Jeżeli brak jest jakiejkolwiek zaakceptowanej definicji dla przeznaczenia danych o podanym symbolu,
::   to zgłaszany jest błąd.
_no_dialog:=(_mp.isService() | _mp.akcja()='UruchomBezdialogowo');
{? _no_dialog |
   FUN.ask(
      'Czy wykonać synchronizację z następującymi parametrami:\n\n'
      '- przeznaczenie danych: %1\n'
      '- akronim tabeli: %2\n'
      '- typ obsługi: %3\n'
      '- metody: %4'@
      [_pd,_tab_acr,_type,_method]
   )
|| _can_continue:=1;

   SYNC_MWA.cntx_psh();
   {? _mp.isService()
   || KOMM.init(,,'ZUI_CLI_AGER - uruchomienie serwisowe')
   ?};

   {? _tab_acr=''
   || {? _can_continue>0 & (_type='' | _type=exec('mwac_type_send','synchro'))
      || _res:=exec('process_send','sync_mwa',_pd,,,,_method,,,~_no_dialog);
         {? _res.RESULT=0
         || _can_continue:=0
         ?};
         obj_del(_res)
      ?};
      {? _can_continue>0 & (_type='' | _type=exec('mwac_type_receive','synchro'))
      || _res:=exec('process_receive','sync_mwa',_pd,,,,,~_no_dialog,_method);
         {? _res.RESULT=0
         || _can_continue:=0
         ?};
         obj_del(_res)
      ?}
   || {? _can_continue>0 & (_type='' | _type=exec('mwac_type_send','synchro'))
      || _res:=exec('process_send','sync_mwa',_pd,,,_tab_acr,_method,,,~_no_dialog);
         {? _res.RESULT=0
         || _can_continue:=0
         ?};
         obj_del(_res)
      ?};
      {? _can_continue>0 & (_type='' | _type=exec('mwac_type_receive','synchro'))
      || _res:=exec('process_receive','sync_mwa',_pd,,,_tab_acr,,~_no_dialog,_method);
         {? _res.RESULT=0
         || _can_continue:=0
         ?};
         obj_del(_res)
      ?}
   ?};

   SYNC_MWA.cntx_pop();

   {? _can_continue>0
   || _out.RESULT:='OK'
   || _out.RESULT:='BŁĄD'
   ?};
   _mp.save(,_out);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Formuła na opis czynności na liście zadań
::----------------------------------------------------------------------------------------------------------------------
'Uruchom komunikację z usługą'@@


\pd_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Formuła służąca do redagowania parametru PD czynności
::   WE: _a - aktualnie ustawiona wartość parametru
::   WY: SYNC_PD.SYM / ~~
::----------------------------------------------------------------------------------------------------------------------
_pd:=exec('pd_select','sync_mwa',_a);
{? _pd='' || ~~ || _pd ?}


\tab_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Formuła służąca do redagowania parametru TAB czynności
::   WE: aktualnie ustawiona wartość parametru
::   WY: akronim tabeli
::----------------------------------------------------------------------------------------------------------------------
_tab_acr:=exec('sel_tabel','synchro',{?  var_pres('_a')=type_of('') || _a || '' ?});
{? _tab_acr='' || _tab_acr:=~~ ?};
_tab_acr


\type_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Formuła służąca do redagowania parametru TYPE czynności
::   WE: aktualnie ustawiona wartość parametru
::   WY: typ obsługi
::----------------------------------------------------------------------------------------------------------------------
_type:=~~;
_default:=0;
{? _a=exec('mwac_type_send','synchro')
|| _default:=1
|? _a=exec('mwac_type_receive','synchro')
|| _default:=2
?};
_choice:=FUN.choice('Wybierz typ obsługi'@,_default,'Wysyłanie'@,'Odbieranie'@);
{? _choice=1
|| _type:=exec('mwac_type_send','synchro')
|? _choice=2
|| _type:=exec('mwac_type_receive','synchro')
?};
_type

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 12a158d702582a1b14bd89c9c84d7a0a161489570361f8a150e655579065c89eaba1783fb20ee8d3bf7fc26f9d2d21b82c15360a256af365b671de0fd1b678d9ccd43f38b030f5dcb6c4bb276b4c35bafdaec2d8bf11988f9411c3e8cd6de4f4ea5b7e2d6cd8191b0340130b3bd5855c4ffc61b4a627f86d43fa2dd2eff12654

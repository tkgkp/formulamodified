:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_jpk_dark.fml
:: Utworzony: 26.08.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_JPK_DARK - Przeglądanie plików JPK
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie plików JPK - główna formuła czynności FKS_JPK_ZPRZ.
::  OLD: \jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('myDIALOG','object');
exec('RB','object');
JPK.win_fml('WER',JPK,'OPIS',,'ICON_BEFORE',"
   {? JPK.KOM='E'
   || 'xwin16.png:8'
   |? JPK.KOM='W'
   || 'xwin16.png:3'
   || ''
   ?}
");
SKID.ISTDEF:='J';
ISTDEF.win_sel('W_JPK');
ISTDEF.win_edit('R_JPK');
ISTDEF.actions('W_JPK','dpUTHOEIA:dAI');
JPK.win_sel('WER');
_tmpdir:=fmk_tmp_dir(0);
{? type_of(_tmpdir) <> type_of(~~)
|| __fdir:=_tmpdir.get_path
|| __fdir:=''
?};
{? var_press('JpkOkres')>0 || VAR_DEL.delete('JpkOkres') ?};
{? SSTALE.AO().POCZ=date(0,0,0)
|| FUN.info('Wybierz inny okres niż Bilans otwarcia i Bilans zamknięcia.'@)
|| exec('zakres','!fks_jpk_zprz',1);
   _win_sel:='WER';
   _grp:=JPK.grp_make(,,'jpkwertgp',,,"exec('exit','zws')");
   JPK.grp_sel(_grp,,_win_sel);
   JPK.win_sel(_grp);
   AreaTitle.setTabWin(JPK,_grp);
   AreaTitle.setTitle();
   JPK.select(,1,10);
   JPK.actions(_win_sel,'')
?};
ISTDEF.actions('W_JPK')


\zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawia zakres prezentowanych plikow JPK
::   WE: _a - bez redakcji
::  OLD: \zakres/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('JpkTyp')<=0 || JpkTyp:='' ?};
{? var_press('JpkOkres')<=0
|| JpkOkres:=exec('okres2','jpk',SSTALE.AO().POCZ)
?};
{? _=0
|| HELPJPK.RODZAJ:=JpkTyp;
   HELPJPK.OKRES:=JpkOkres;
   HELPJPK.win_edit('ZAKRES');
   _tmp:=sql('select distinct SUBSTR(to_string(OKRO_F.POCZ),1,7) as OKRES '
            +'from OKRO_F join ROK_F using(OKRO_F.ROK,ROK_F.REFERENCE) '
            +'where ROK_F.FIRMA=\''+$REF.FIRMA+'\' and OKRO_F.POCZ>=to_date(:_a) order by 1',date(2016,7,1));
   ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   {? ROK_F.first()
   || {!
      |? {? ROK_F.POCZ_ROK~1>2015
         || _tmp.OKRES:=$(ROK_F.POCZ_ROK~1)+'/BZ';
            _tmp.add()
         ?};
         ROK_F.next()
      !}
   ?};
   ROK_F.cntx_pop();
   _o:=_tmp.mk_sel('Okres'@,'P',,'f3jpkoko',,,,,'U');
   _tmp.win_fld(_o,,'OKRES',,,14);
   _tmp.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
   _tmp.win_sel(_o);
   JpkF3o:=_tmp;
   {? HELPJPK.edit()
   || JpkTyp:=HELPJPK.RODZAJ;
      JpkOkres:=HELPJPK.OKRES
   ?};
   _cur_win:=cur_win(1,1);
   {? cur_tab(1,1)=JPK & _cur_win<>''
   || JPK.win_sel(_cur_win)
   ?};
   VAR_DEL.delete('JpkF3o')
?};
JPK.hdr_sel();
{? JpkTyp<>'' & JpkOkres<>''
|| JPK.index('OKRESTYP'); JPK.prefix(REF.FIRMA,JpkOkres,JpkTyp,);
   JPK.hdr_sel(' - typ: %1, okres: %2'@[JpkTyp,JpkOkres])
|? JpkTyp<>''
|| JPK.index('TYP'); JPK.prefix(REF.FIRMA,JpkTyp,);
   JPK.hdr_sel(' - typ: '@+JpkTyp)
|? JpkOkres<>''
|| JPK.index('OKRES'); JPK.prefix(REF.FIRMA,JpkOkres,);
   JPK.hdr_sel(' - okres: '@+JpkOkres)
|| JPK.index('DATA'); JPK.prefix(REF.FIRMA);
   JPK.hdr_sel(' - wszystkie'@)
?}


\show_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja pokaz dane okna WER tabeli JPK
::  OLD: \show_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('can_view','jpk')
|| {? JPK.ISTDEF().R='T'
   || exec('parse','xml',JPK.PLIK,JPK.ISTDEF().VER+{? JPK.OPIS<>'' || ' - '+JPK.OPIS || '' ?},JPK.LINII)
   || _fun_akc:={? JPK.GEN<>'N' || "exec('show_jpk_akc','!fks_jpk_zprz',_a,_b,_c)" || ~~ ?};
::sprawdza naniesienie aktualizacji 22.26_09
      _akt09:=0;
      ISTDEF.cntx_psh(); ISTDEF.index('JPK'); ISTDEF.prefix('J','KR','JPK_KR(1)_v1-0',);
      ISTDEFS.cntx_psh(); ISTDEFS.index('LP');
      {? ISTDEF.first()
      || ISTDEFS.prefix(ISTDEF.ref(),6);
         {? ISTDEFS.first() & ISTDEFS.REGULY='{? var_press(\'Xml\')>0 || exec(\'dateTime\',\'jpk\',date(),time()) || exec(\'dateTime\',\'jpk\',JPK.D_GEN,JPK.T_GEN) ?}'
         || _akt09:=1
         ?};
         ISTDEF.next()
      ?};
      ISTDEFS.cntx_pop(); ISTDEF.cntx_pop();

      {? _akt09 & (JPK.TYP='KR' | JPK.TYP='WB')
      || _nag:=FUN.choice('Wybierz zakres plików',,'Z aktualnego rekordu','Z wielu plików')
      || _nag:=1
      ?};
      {? _nag=1
      || exec('imp_dane','xml',JPK.PLIK,JPK.ISTDEF().VER+{? JPK.OPIS<>'' || ' - '+JPK.OPIS || '' ?},JPK.LINII,_fun_akc)
      |? _nag=0
      || 0
      |? _nag=2
      || JPK.cntx_psh();
         JPK.index('OKRES');
         JPK.prefix(REF.FIRMA,4+JPK.OKRES);
         _tab:=sql('select JPK.TYP, JPK.KOR, '+{? var_press('LP',JPK)>0 || 'JPK.LP,' || '' ?}+
                    'ISTDEF.VER, JPK.OD, JPK.DO, JPK.OPIS, JPK.GEN, JPK.U_GEN, JPK.D_GEN,
                    JPK.T_GEN, JPK.AKC, JPK.U_AKC, JPK.STATOPIS, \' \' as W, JPK.REFERENCE
                    from JPK join ISTDEF where JPK.TYP=\'%1\' order by JPK.OD'[JPK.TYP]);
         _o:=_tab.mk_sel('Zaznacz pliki do scalenia','P',,'#jpkwinsel',,,,,'U',,,,,'html_maximized');
         _tab.win_sopt(_o,'select_record_checkbox = 0');
         _tab.win_fld(_o,,'W',,,,,,'Wybrany',,'Wybrany',2,,"'T'","'N'");
         _tab.win_fld(_o,,'TYP',,,5,,,'Rodzaj');
         _tab.win_fld(_o,,'KOR',,,3,,,'Kor.',,,2,,"1","0");
         {? var_press('LP',JPK)>0
         || _tab.win_fld(_o,,'LP',,,5,,,'Numer')
         ?};
         _tab.win_fld(_o,,'OD',,,10,,,'Od');
         _tab.win_fld(_o,,'DO',,,10,,,'Do');
         _tab.win_fld(_o,,'OPIS',,,31,,,'Opis');
         _tab.win_fld(_o,,'GEN',,,3,,,'Zewn.',,,2,,"'N'","'T'");
         _tab.win_fld(_o,,'U_GEN',,,10,,,'Utworzył');
         _tab.win_fld(_o,,'D_GEN',,,10,,,'Data utw.');
         _tab.win_fld(_o,,'T_GEN',,,5,,,'Czas utw.');
         _tab.win_fld(_o,,'AKC',,,3,,,'A',,,2,,"'T'","'N'");
         _tab.win_fld(_o,,'U_AKC',,,10,,,'Zaakceptował');
         _tab.win_fld(_o,,'STATOPIS',,,11,,,'Status');
         _tab.win_act(_o,,'Formuła','Kontynuuj',,,"sel_exit()",,1);
         _tab.win_act(_o,0,'Formuła','Wybierz',,,"{? cur_tab(1,1).W='T' || cur_tab(1,1).W:='N' || cur_tab(1,1).W:='T' ?};cur_tab(1,1).put",,,1);
         _tab.win_sel(_o);
         {? _tab.first()
         || {!
            |? _tab.W:='N';
               _tab.put();
               _tab.next()
            !}
         ?};
         _tab.first();
         _ndx2:=_tab.index('?');
         {!
         |? {? _tab.select()
            || _break:=0;
               {? var_press('_ndx')<=0 || _ndx:=_tab.ndx_tmp(,,'OD',,0) ?};
               _tab.index(_ndx);
               {? _tab.first()
               || {! |? {? _tab.W<>'T' || _tab.next() || 0 ?} !};
                  _dat1:=_tab.DO;
                  _nrrach:=_tab.OPIS;
                  {? _tab.next()
                  || {!
                     |? {? _tab.W='T'
                        || {? _dat1+1<>_tab.OD & (_tab.TYP<>'KR' | (_dat1<>_tab.OD & _dat1<>date(0,0,0) & _tab.OD<>date(0,0,0))) || _break:=1 || _dat1:=_tab.DO ?};
                           {? _tab.TYP='WB' & _nrrach<>_tab.OPIS || _break:=2 ?}
                        ?};
                        _break=0 & _tab.next()
                     !}
                  ?}
               ?};
               {? _break>0
               || {? _break=1 || FUN.info('Należy wybrać pliki JPK z kolejnymi okresami') ?};
                  {? _break=2 || FUN.info('Należy wybrać pliki JPK dotyczące tego samego rachunku bankowego') ?};
                  _tab.index(_ndx2);
                  1
               || {? _tab.first()
                  || {? _tab.last()
                     || {!
                        |? {? _tab.W='T'
                           || _last:=_tab.REFERENCE;
                              0
                           || _tab.prev()
                           ?}
                        !}
                     ?};
                     _tab.first();
                     _first:=1;
                     {? var_press('_last')>0
                     || {!
                        |? {? _tab.W='T' & JPK.seek(_tab.REFERENCE)
                           || exec('imp_dane','xml',JPK.PLIK,JPK.ISTDEF().VER+{? JPK.OPIS<>'' || ' - '+JPK.OPIS || '' ?},JPK.LINII,_fun_akc,,_first,_last=_tab.REFERENCE);
                              _first:=0
                           ?};
                           _tab.next()
                        !}
                     ?}
                  ?};
                  0
               ?}
            ?}
         !};
         JPK.cntx_pop
      ?}
   ?}
?}


\show_jpk_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Dodaje akcje do okien podgladu plikow JPK
::   WE: _a - tabela
::       _b - akronim okna
::       _c - 1-początkowe 2-końcowe
::  OLD: \show_jpk_akc/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _c=2
|| {? XmlTab.TAG='ZakupWiersz' & JPK.OD>=date(2019,9,1)
   || _a.win_act(_b,,'Formuła','Legenda',,,"exec('legenda','color','JPK_VAT#01')");
      _a.win_fml(_b,,'KH_NIP',,'ICON_BEFORE',"
         _tab:=cur_tab(1,1);
         _snip:=exec('niptostr','#string',_tab.KH_NIP);
         {? -_snip='brak nip'
         || 'xwin16.png:170'
         |? exec('spr_typ','blp',_snip)
         || {? VAT_DEK.TYP='V7M' | VAT_DEK.TYP='V7K'
            || _sdata:={? _tab[6]<>'' || _tab[6] || _tab[5] ?}
            || _sdata:={? _tab[7]<>'' || _tab[7] || _tab[6] ?}
            ?};
            _data:=date(#(4+_sdata),#(5-_sdata-3),#(_sdata+2));
            {? var_pres('Blp')<=0
            || Blp:=exec('obj','blp')
            ?};
            Blp.check('K',_snip,_data,,1);
            exec('icona_svat','blp',Blp.ACTIVE)
         || 'xwin16.png:170'
         ?}
      ")
   ?}
|? XmlTab.COMM
|| {? XmlTab.TAG='Dziennik' | XmlTab.TAG='Faktura' | XmlTab.TAG='Zamowienie'
   || _a.win_act(_b,,'Formuła','Dokument źródłowy'@@,,,"exec('show_jpk_dok','!fks_jpk_zprz')",,1)
   |? XmlTab.TAG='KontoZapis' | XmlTab.TAG='WyciagWiersz'
   || _a.win_act(_b,,'Formuła','Dokument źródłowy'@@,,,"exec('show_jpk_dok','!fks_jpk_zprz',1)",,1);
      _a.win_act(_b,,'Formuła','Pozycja dokumentu'@@,,,"exec('show_jpk_dok','!fks_jpk_zprz',2)",,1)
   |? XmlTab.TAG='SprzedazWiersz' | XmlTab.TAG='ZakupWiersz'
   || _a.win_act(_b,,'Formuła','Dokument źródłowy'@@,,,"exec('show_jpk_dok','!fks_jpk_zprz',0)",,1);
      _a.win_act(_b,,'Formuła','dokument VAT'@@,,,"exec('show_jpk_dok','!fks_jpk_zprz',2)",,1)
   |? XmlTab.TAG='FakturaWiersz' | XmlTab.TAG='ZamowienieWiersz'
   || _a.win_act(_b,,'Formuła','Dokument źródłowy'@@,,,"exec('show_jpk_dok','!fks_jpk_zprz',1)",,1);
      _a.win_act(_b,,'Formuła','Pozycja dokumentu'@@,,,"exec('show_jpk_dok','!fks_jpk_zprz',2)",,1)
   |? JPK.TYP='MAG'
   || _a.win_act(_b,,'Formuła','Dokument źródłowy'@@,,,"exec('show_jpk_dok','!fks_jpk_zprz')",,1)
   ?}
?}


\show_jpk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja prezentująca dokument źródłowy
::   WE: _a - dodatkowy parametr
::  OLD: \show_jpk_dok/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=16+cur_tab(1,1).R;
_crc:=16-cur_tab(1,1).R;
_ver:='';
{? +_crc=42
|| _ver:=2+_crc
?};
{? 4+_ref='doku'
|| DOK.cntx_psh();
   DOK.use(8+_ref);
   DOK.prefix();
   {? DOK.seek(BIT.sqlint(_ref),)
   || {? +_crc=42 & _crc<>exec('sha1','#to_sha1','DOK',_ver)
      || FUN.info('Dokument zmienił się od czasu tworzenia pliku JPK.'@)
      ?};
      exec('RB','object');
      exec('dok_spac','dok_fks')
   || FUN.info('Nie znaleziono dokumentu.'@)
   ?};
   DOK.cntx_pop()
|? 4+_ref='pozy'
|| POZ.cntx_psh();
   POZ.use(8+_ref);
   POZ.prefix();
   {? POZ.seek(BIT.sqlint(_ref),)
   || {? +_crc=42 & _crc<>exec('sha1','#to_sha1','POZ',_ver)
      || FUN.info('Pozycja dokumentu zmieniła się\nod czasu tworzenia pliku JPK.'@)
      ?};
      {? _a=1
      || exec('naglowek','dok_fks',4-(8+_ref),BB.sqlint(_ref))
      |? _a=2
      || exec('wyswietl','dok_fks',4-(8+_ref),BB.sqlint(_ref))
      ?}
   || FUN.info('Nie znaleziono pozycji dokumentu.'@)
   ?};
   POZ.cntx_pop()
|? 6+_ref='vat_ps'
|| VAT_PS.use(8+_ref);
   VAT_PS.prefix();
   {? VAT_PS.seek(BIT.sqlint(_ref),)
   || {? +_crc=42 & _crc<>exec('sha1','#to_sha1','VAT_PS',_ver)
      || FUN.info('Szczegóły VAT zmieniły się\nod czasu tworzenia pliku JPK.'@)
      ?};
      exec('wysw_dok','fks_ved',_a)
   || FUN.info('Nie znaleziono szczegółów VAT.'@)
   ?}
|? 4+_ref='pozf'
|| POZF.cntx_psh();
   POZF.use(8+_ref);
   POZF.prefix();
   {? POZF.seek(BIT.sqlint(_ref),)
   || {? +_crc=42 & _crc<>exec('sha1','#to_sha1','POZF',_ver)
      || FUN.info('Pozycja faktury zmieniła się\nod czasu tworzenia pliku JPK.'@)
      ?};
      DOK.cntx_psh();
      DOK.use(8+$POZF.DOK);
      DOK.prefix();
      {? DOK.seek(POZF.DOK)
      || {? _a=1
         || exec('dok_spac','dok_fks')
         || POZF.index('DOK');
            POZF.prefix(DOK.ref());
            POZF.win_sel('WER');
            POZF.hdr_sel(); POZF.hdr_sel(' w '+{? DOK.WAL || DOK.WAL().KOD || 'PLN' ?});
            POZF.fld_opt('WER','col_name="%1"'[HELPJPK.KOL_CEN],POZF,'C');
            POZF.select(,1,5,'dpUTE:dE')
         ?}
      || FUN.info('Nie znaleziono dokumentu.'@)
      ?};
      DOK.cntx_pop()
   || FUN.info('Nie znaleziono pozycji faktury.'@)
   ?};
   POZF.cntx_pop()
|? 5+_ref='nagdo'
|| ND.cntx_psh(); DK.cntx_psh();
   ND.use(8+_ref);
   ND.prefix();
   DK.use('dokma'+((8+_ref)+3));
   DK.index('DOKMAG');
   DK_L.use('doklk'+((8+_ref)+3));
   {? ND.seek(BIT.sqlint(_ref),)
   || {? +_crc=42 & _crc<>exec('sha1','#to_sha1','ND',_ver)
      || FUN.info('Dokument magazynowy zmienił się\nod czasu tworzenia pliku JPK.'@)
      ?};
      DK.prefix(ND.ref());
      DK.win_patt('WZOR');
      M.win_dict('SLO_M'); M.win_edit(); M.win_patt();
      DK.hdr_sel();
      DK.hdr_sel(' '+ND.TYP().T+', '+ND.SYM);
      DK.select()
   || FUN.info('Nie znaleziono dokumentu magazynowego.'@)
   ?};
   ND.cntx_pop(); DK.cntx_pop()
|? 5+_ref='dokma'
|| ND.cntx_psh(); DK.cntx_psh();
   DK.use(8+_ref);
   DK.prefix();
   _maska:=(8+_ref)+3;
   {? DK.seek(BIT.sqlint(_ref),)
   || {? +_crc=42 & _crc<>exec('sha1','#to_sha1','DK',_ver)
      || FUN.info('Pozycja dokumentu  magazynowego zmieniła się\nod czasu tworzenia pliku JPK.'@)
      ?};
      DK_L.use('doklk'+_maska);
      ND.use('nagdo'+_maska);
      ND.prefix();
      DK.N();
      DK.index('DOKMAG'); DK.prefix(DK.N);
      DK.win_patt('WZOR');
      M.win_dict('SLO_M'); M.win_edit(); M.win_patt();
      DK.hdr_sel();
      DK.hdr_sel(' '+ND.TYP().T+', '+ND.SYM);
      DK.select(,1,5)
   || FUN.info('Nie znaleziono pozycji dokumentu magazynowego.'@)
   ?};
   ND.cntx_pop(); DK.cntx_pop()
|? 5+_ref='fakpo'
|| FAKS.cntx_psh(); FAP.cntx_psh(); FAKSPL.cntx_psh();
   FAP.use(8+_ref);
   _maska:=8+_ref+3;
   FAKS.use('faktu'+_maska);
   FAKSPL.use('fakpl'+_maska);
   FAP.prefix();
   FAKS.prefix();
   {? FAP.seek(BIT.sqlint(_ref),)
   || {? +_crc=42 & _crc<>exec('sha1','#to_sha1','FAP',_ver)
      || FUN.info('Pozycja faktury zmieniła się od czasu tworzenia pliku JPK.'@)
      ?};
      FAP.FAKS();
      {? _a=1
      || _tab:=sql('select DOK.REFERENCE as REF from @DOK where DOK.DOKZRODL=\':_a\'','D'+_maska+$(#FAKS.ref()));
         {? _tab.first()
         || DOK.cntx_psh();
            DOK.use(8+_tab.REF);
            DOK.prefix();
            {? DOK.seek(BIT.sqlint(_tab.REF),)
            || exec('dok_spac','dok_fks')
            || FUN.info('Nie znaleziono nagłówka dokumentu księgowego.'@)
            ?};
            DOK.cntx_pop()
         || FUN.info('Nie znaleziono nagłówka dokumentu księgowego.'@)
         ?}
      || hdr_dok:='Pozycje dokumentu '+FAKS.SYM;
         exec('pozycje_fak','faktury_poz',1);
         VAR_DEL.delete('hdr_dok')
      ?}
   || FUN.info('Nie znaleziono pozycji faktury.'@)
   ?};
   FAKS.cntx_pop(); FAP.cntx_pop(); FAKSPL.cntx_pop()
|? 2+_ref='pw'
|| PW.cntx_psh();
   PW.use(6+_ref);
   {? PW.seek(BIT.sqlint(_ref),)
   || {? +_crc=42 & _crc<>exec('sha1','#to_sha1','PW',_ver)
      || FUN.info('Pozycja wyciągu zmieniła się od czasu tworzenia pliku JPK.'@)
      ?};
      _wed:=PW.mk_edit('Transakcja'@,,'#ttpb_red1');
      PW.win_efld(_wed,,'KONTR',,,,,,'Kontrahent'@);
      PW.win_efld(_wed,,'RBK',,,,,,'Rachunek bankowy'@);
      PW.win_efld(_wed,,'RBL',,,,,,'Rach. bank. licencj.'@);
      PW.win_efld(_wed,,'KW',,,,2,,'Kwota'@);
      PW.win_efld(_wed,,'WAL',,,,,,'Waluta'@);
      PW.win_efld(_wed,,'STR',,,3,,,'Strona'@);
      PW.win_efld(_wed,,'TYTOP',,,,,,'Tytuł operacji'@);
      PW.win_efld(_wed,,'TYPTRAN',,,,,,'Opis transakcji'@);
      PW.win_efld(_wed,,'DK',,,,,,'Data księg.'@);
      PW.win_efld(_wed,,'AN',,,,,,'Konto'@);
      PW.win_efld(_wed,,'NRW',,,5,,,'Wyciąg bankowy'@);
      PW.win_efld(_wed,,'ODD','OD',,8,,,'Jednostka księgowa'@);
      PW.win_efld(_wed,,'REJ','KOD',,8,,,'Rejestr'@);
      PW.win_efld(_wed,,'NRD',,,5,,,'Numer w rejestrze'@);
      PW.win_edit(_wed);
      PW.display
   || FUN.info('Nie znaleziono wyciągu.'@)
   ?};
   PW.cntx_pop()
|| FUN.info('Nie znaleziono informacji o elemencie źródłowym.'@)
?}


\show_xml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja pokaz xml
::  OLD: \show_xml/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JPK.STAT='T'
|| _pyt:=FUN.choice('Który plik wyświetlić?'@,,'Plik JPK'@,'plik UPO'@)
|| _pyt:=1
?};
{? _pyt=1
|| {? exec('can_view','jpk')
   || exec('bl_view','#blob',JPK,'PLIK')
   ?}
|? _pyt=2
|| exec('bl_view','#blob',JPK,'UPO')
?}


\cmp_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wskazanie aplikacji do porownywania
::  OLD: \cmp_sel/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_path:=fmk_tmp_dir(0).get_path()+'Aplikacja.exe';
dlg_upload(_path,0,'.exe');
: Rzeczona funkcja nie może i tak zostać wywołana dla interma, nie mniej zmieniono to by ułatwić ew przynoszenie na interma w przyszlości
{? _path<>''
|| fld(_path)
?};
0


\cmp_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja porownania plikow JPK
::  OLD: \cmp_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JPK.sel_size()=0
|| FUN.info('Do porównania należy zaznaczyć 2 pliki.'@)
|| 1
?}


\bg_cmp_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed akcja grupowa porownania plikow JPK
::  OLD: \bg_cmp_jpk/jpk.fml
:: ~OST: INBLGET,INFERASE,INSYSTEM,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
|| FUN.emsg(exec('interm_nacc_msg','#system'));
   return()
?};
{? exec('can_view','jpk')=0
|| 0
|? (_cmp:=exec('cmp_ok','!fks_jpk_zprz'))=''
|| 0
|? JPK.sel_size()<>2
|| FUN.info('Do porównania należy zaznaczyć 2 pliki.'@)
|| _tab:=JPK.sel_aget();
   JPK.cntx_psh();
   JPK.prefix();
   {? _tab.first()
   || _file:=obj_new(2);
      _file[1]:=_file[2]:='';
      _dir:=tmp_dir();
      _lp:=0;
      _rodz:='';
      _r_ok:=-1;
      _dalej:=1;
      {!
      |? {? JPK.seek(_tab.REF,)
         || _lp+=1;
            _file[_lp]:=_dir+'/'+JPK.bl_info('PLIK','NAME');
            {? exec('can_view','jpk')
            || JPK.bl_get('PLIK','@'+_file[_lp],0);
               {? _rodz=''
               || _rodz:=JPK.TYP
               || _r_ok:=_rodz=JPK.TYP
               ?}
            || _dalej:=0
            ?}
         ?};
         _dalej & _tab.next()
      !};
      {? _r_ok=0
      || _r_ok:=FUN.ask('Porównywane pliki JPK są innego rodzaju.\nKontynuować?'@)
      ?};
      {? _r_ok>0
      || _cmp:=STR.gsub(_cmp,'%1',_file[1]);
         _cmp:=STR.gsub(_cmp,'%2',_file[2]);
         system('@'+_cmp,1);
         ferase('@'+_file[1],0);
         ferase('@'+_file[2],0)
      ?}
   ?};
   JPK.cntx_pop()
?}


\cmp_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Sprawdza czy ustawiono aplikacje porownujaca
::  OLD: \cmp_ok/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('cmp_get','!fks_jpk_zprz');
{!
|? {? HELPJPK.CMP_FILE<>''
   || 0
   || _pyt:=FUN.choice('Nie wybrano aplikacji porównującej.'@,,'Wybierz'@);
      {? _pyt=1
      || exec('cmp_set','!fks_jpk_zprz');
         1
      || 0
      ?}
   ?}
!};
HELPJPK.CMP_FILE


\cmp_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawienie scieżki do aplikacji porownujacej pliki
::  OLD: \cmp_set/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('cmp_get','!fks_jpk_zprz');
HELPJPK.win_edit('CMP_FILE');
{? HELPJPK.edit("
      {? 1+sys_name(0)='W' & -HELPJPK.CMP_FILE*'.exe'=0
      || FUN.info('Nie wskazano aplikacji.'@); 'CMP_FILE'
      |? HELPJPK.CMP_FILE*'%1'=0 & HELPJPK.CMP_FILE*'%2'=0
      || FUN.info('Nie użyto parametrów: %%1 %%2.'@); 'CMP_FILE'
      || ''
      ?}
   ")
|| _f:=fopen('cmp_file.hst','w',1);
   {? _f
   || fwrite(_f,HELPJPK.CMP_FILE);
      fclose(_f)
   ?}
?}


\cmp_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Pobiera sciezke do aplikacji porownujacej
::  OLD: \cmp_get/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_apk:='';
_f:=fopen('cmp_file.hst','r',1);
{? _f
|| _apk:=fread(_f);
   fclose(_f)
?};
HELPJPK.CMP_FILE:=_apk


\bg_save_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed akcja grupową zapisywania plików JPK
::  OLD: \bg_save_jpk/jpk.fml
:: ~OST: INFILECHOOSER
::----------------------------------------------------------------------------------------------------------------------
{? var_press('LastKat')<=0
|| _f:=fopen('last_kat.hst','r',1);
   {? _f
   || LastKat:=fread(_f);
      fclose(_f)
   || LastKat:='c:\\'
   ?}
?};

__Interm:=exec('interm','#system');
{? __Interm
|| __czy:=FUN.choice('Chcesz chcesz spakować pliki'@,,'Tak'@);
   __dir:=fmk_tmp_dir(0);
   __kat:=fmkdir(__dir.get_path(),'com')
|| __czy:=0;
   __kat:=exec('filechooser','#file','Wybierz folder dla plików JPK'@,0,,,,'DIRECTORIES_ONLY',1)
?};

{? __kat<>''
|| {? LastKat<>__kat
   || LastKat:=__kat;
      _f:=fopen('last_kat.hst','w',1);
      {? _f
      || fwrite(_f,LastKat);
         fclose(_f)
      ?}
   ?};
   JpkKat:=__kat;
   JpkSel:=JPK.sel_size();
   JpkOk:=0;
   1
?}


\save_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja pokaz xml
::  OLD: \save_jpk/jpk.fml
:: ~OST: INBLGET
::----------------------------------------------------------------------------------------------------------------------
_gr:=JPK.sel_size();
{? exec('can_view','jpk',~_gr)=0 | ~JPK.PLIK || return() ?};
_fn:=JPK.bl_info('PLIK','NAME');
_typ:=1;
{? _gr
|| _fn:=JpkKat+'/'+_fn
|| {? JPK.TYP='SF'
   || {? exec('is_sig','jpk_sf') | JPK.GEN='N' & var_press('JPK_SIG',JPK)>0 & JPK.JPK_SIG
      || _typ:=FUN.choice('Jaki plik zapisać?'@,,'Bez podpisów'@,'Z podpisami'@)
      ?}
   ?};
   {? _typ
   || _fn:=fmk_tmp_dir(0).get_path()+'/jpk.xml';
      {? -(_fn+4)<>'.xml'
      || _fn+='.xml'
      ?}
   || _fn:=''
   ?}
?};
{? var_press('JpkSel') <0
|| __czy:=0
?};
_end:=1;
{? var_pres('JpkSel')>0 & var_pres('__Interm')>0 & ~__Interm
|| _fld:={? _typ=2 || 'JPK_SIG' || 'PLIK' ?};
   {? JPK.bl_get(_fld,'@'+_fn,0)
   || {? _gr || JpkOk+=1 ?}
   ?};
   _end:=0
?};
{? _end & _fn<>'' & __czy=1
|| _fld:={? _typ=2 || 'JPK_SIG' || 'PLIK' ?};
   {? JPK.bl_get(_fld,'@'+_fn,0)
   || {? _gr || JpkOk+=1 ?}
   ?}
?};
{? _end & __czy=0
|| _fld:={? _typ=2 || 'JPK_SIG' || 'PLIK' ?};
   {? exec('bl_save','#blob',JPK,_fld)<>''
   || {? _gr || JpkOk+=1 ?}
   ?}
?};
{? var_press('JpkSel')<0
|| VAR_DEL.delete('__czy')
?}


\ag_save_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po akcji grupowej zapisywania plików JPK
::  OLD: \ag_save_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info(
   'Liczba zaznaczonych plików: %1\n'
   'Liczba zapisanych plików: %2'@[$JpkSel,$JpkOk]
);
{? __czy=1
||
   _zipname:=(__kat-5)+'\Archiwum.zip';
   fpack_add(_zipname,__kat);
   dlg_save(_zipname,0)
?};
VAR_DEL.delete('JpkKat','JpkSel','JpkOk','__kat','__Interm','__czy','__dir')


\kom_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja komunikaty
::  OLD: \kom_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
JPK_KOM.index('JPK');
JPK_KOM.prefix(JPK.ref());
JPK_KOM.win_sel('WER');
JPK_KOM.select()


\bv_jpk_kom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietleniem zmiennej HELPJPK.KOM_OPIS
::  OLD: \bv_jpk_kom/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.KOM_OPIS:={? JPK_KOM.TYP='E' || 'Błąd' || 'Uwaga' ?}


\show_kom_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Prezentuje szczegółowe informacje o błędzie
::----------------------------------------------------------------------------------------------------------------------
{? JPK_KOM.MEMO
|| JPK_KOM.memo_get(,'MEMO');
   JPK_KOM.memo_view(,'MEMO')
|| JPK_KOM.memo_set(JPK_KOM.OPIS);
   JPK_KOM.memo_view()
?}

:Sign Version 2.0 jowisz:1045 2024/02/12 11:19:09 399902342f2b140b39d1600cd76111644e6a3d2addc9a7521463d3ffcccc050314885a5b308bda6a963d631d1272927d1661e35774b55cc563c73e129dbe51012235d6da9e74c19c8c2ee108ee4dd9d233077481fe283034f3443d119f574f77b38f29e8fd08d5f17f49bedbefdf871f3afe2ed5224204cd67fff62c57d3f0ee

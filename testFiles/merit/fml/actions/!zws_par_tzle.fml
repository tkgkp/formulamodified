:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_tzle.fml
:: Utworzony: 09.02.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_TZLE - Definicje zleceń
::            Obsługa tabel: ZTP, ZTPF (TAG: <ZTP>), ANRUB, ANWAR, ... (TAG: <ANWAR>)
::                           ZPWYD, ZLSTW
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła główna czynności parametryzacyjnej (nieprocesowej) - redagowanie typów zleceń
::  TAG: <ZTP><ANWAR><CZYNNOŚĆ><LISTA>
::----------------------------------------------------------------------------------------------------------------------
Cntx.psh(ZTP,ANRUB,ANWAR,ZPWYD,ZLSTW);

BEER.NRDOK_AB:='ZL';

ZTP.win_sel('GRP');
ZTP.win_edit('RED');

ZTP.index('TP');
ZTP.prefix();

ANRUB.index('RUBR');
ANRUB.prefix();

ANWAR.actions('SLO0','L');
ANWAR.actions('SLO1','L');

ZPWYD.index('ZPWYD_PW');
ZPWYD.prefix();

ZLSTW.index('KN');
ZLSTW.prefix();

:: tablice zmiennych służących do kontroli redakcji pól
_edit:=obj_new('mod');
_edit.mod:=1;
_fld:=obj_new('RODZAJ');
_fld.RODZAJ:='';
params_set('edit',_edit,'fld',_fld);

ZTP.select();
Cntx.pop(ZTP,ANRUB,ANWAR,ZPWYD,ZLSTW);
~~


\grp_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Formuła "before" - wypełnianie okna grupowego
::----------------------------------------------------------------------------------------------------------------------
grp_disp(ZLSTW,'WER');
~~


::======================================================================================================================
:: Formuły obsługi tabeli ZTP (typy zleceń)
::======================================================================================================================


\pred_ztp_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: ZTP - Przed redakcją pola TYP
::   WY: 1 / 0
::  OLD: \pred_ztp_typ/zlec3.fml
::  TAG: <ZTP><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
{? exec('type_reserved','!zws_par_tzle') || 0 || 1 ?}


\ae_ztp_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: ZTP - Po redakcji pola TYP
::  OLD: \ae_ztp_typ/zlec3.fml
::  TAG: <ZTP><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? exec('type_reserved','!zws_par_tzle')
|| FUN.info('Typ zlecenia nie może się zaczynać od znaku ''~''.'@); 0
|| 1
?}


\predwp
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: ZTP - Przed redakcją wybranych pól (WP, TECH)
::  OLD: \predwp/zlecenia.fml
::  TAG: <ZTP><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
{? exec('type_reserved','!zws_par_tzle') & cur_afld()='WP'
|| 0
|| params_get().edit.mod=1
?}


\poredwp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Po redakcji pola ZTP.WP
::   WY: 1
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
win_disp();
{? ZTP.WP='W'
|| ZTP.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty');
   PROJZ.PROJZAKR:=exec('projzakr_nie_dotyczy','projekty');
   ZTP.PROJWJO:='N';
   ZTP.PROJZKH:='N'
?};
exec('typ_projzakr_bd','projekty');
exec('set_efld_opt','!zws_par_tzle');
~~


\f3_anrub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MKO [7.53] Uwaga 69
:: OPIS: ZTP - Obsługa na F3 dla pól CRUB*
::  OLD: \f3_anrub/zlecanal.fml
::  TAG: <ZTP><ANWAR><MBUILDER><F3>
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();
ANRUB.index('RUBR');
ANRUB.prefix();
ANRUB.find_key(_fld);
ANRUB.win_sel('WYB');
{? ANRUB.select(0,1,5)<>0
|| fld(ANRUB.NR)
|| fld(_fld)
?}


\ztp_rub_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: ZTP - Po redakcji dla pól CRUB*
::  TAG: <ZTP><ANWAR><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();
_res:=0;
{? _fld=0
|| ($('VAR1.N'+(1-cur_afld())))():='';
   _res:=1
|| ANRUB.cntx_psh();
   ANRUB.index('RUBR');
   ANRUB.prefix(_fld);
   {? ANRUB.first()
   || ($('VAR1.N'+(1-cur_afld())))():=ANRUB.OPIS;
      _res:=1
   || FUN.info('Brak rubryki o tym numerze.'@)
   ?};
   ANRUB.cntx_pop()
?};
_res


\czymod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: ZTP - Sprawdza, czy wybrane pola są modyfikowalne
::   WY: 0 / 1
::  OLD: \czymod/zlecenia.fml
::  TAG: <ZTP><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
params_get().edit.mod=1


\type_reserved
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TPKTL - Sprawdza, czy typ zlecenia jest zastrzeżony
::       Operuje na bieżącym buforze tabeli ZTP
::   WY: 1 - typ zastrzeżony, 0 - typ niezastrzeżony
::  OLD: \ztp_nonsystem/zlecenia.fml
::  TAG: <ZTP>
::----------------------------------------------------------------------------------------------------------------------
1+ZTP.TYP='~'


\ztp_mg_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: ZTP - Przed wyświetleniem pola MG
::  OLD: \ztp_mg_bd/zlec3.fml
::  TAG: <ZTP><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? ZTP.WP='W' || exec('findfnrd','color') || 1 ?}


\ztp_mg_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: ZTP - Przed redakcją pola MG
::  OLD: \ztp_mg_be/zlec3.fml
::  TAG: <ZTP><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
ZTP.WP='P'


\dolacztp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS
:: OPIS: ZTP - 'Dołącz' dla typu zlecenia
::   WY: 0 / 1
::  OLD: \dolacztp/zlec1.fml
::  TAG: <ZTP><MBUILDER><AKCJA><ADD>
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
params_get().edit.mod:=1;
VAR1.NRUBALL:='';
VAR1.NRUBMAT:='';
VAR1.NRUBROB:='';
VAR1.NRUBJEDN:='';
VAR1.NRUBIL0:='';
VAR1.NRUBIL:='';
ZTP.blank();
ZTP.win_edit('RED');
exec('set_efld_opt','!zws_par_tzle');
{? ZTP.edit("exec('po_ztp','!zws_par_tzle')") || ZTP.add() ?}


\usun_ztp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: ZTP - Usuń
::  OLD: \usun_ztp/slownik1.fml
::  TAG: <ZTP><AKCJA><MBUILDER><DEL>
::----------------------------------------------------------------------------------------------------------------------
_link:=ZTP.testlink(2);
{? _link.first()
|| FUN.info('Usunięcie nie jest możliwe.\nWskazany typ zlecenia jest już użyty w systemie.'@);
   0
|| {? FUN.ask('Czy usunąć wskazany typ zlecenia?'@)
   || ZTP.del()
   ?}
?};
~~


\poprztp
::----------------------------------------------------------------------------------------------------------------------
::  MOD: TS [8.60] okna z zakladkami
:: OPIS: 'Popraw' dla typu zlecenia
::   WY: 1
::  OLD: \poprztp/zlecenia.fml
::  TAG: <ZTP><MBUILDER><AKCJA><PUT>
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
ZL.cntx_psh();
ZL.clear();
ZL.index('TSSA');
ZL.prefix(ZTP.ref());
{? ZL.first()
|| params_get().edit.mod:=0
?};
ZL.cntx_pop();
{? exec('type_reserved','!zws_par_tzle')
|| FUN.info('Zastrzeżony typ zlecenia — możliwości redagowania będą ograniczone.'@)
?};
ZTP.win_edit('RED');
exec('set_efld_opt','!zws_par_tzle');
{? ZTP.edit("exec('po_ztp','!zws_par_tzle')") || ZTP.put() ?};
params_get().edit.mod:=1;
1


\prn_ztp
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: ZTP - Druk tabeli
::  OLD: \prn_ztp/drukujp.fml
::  TAG: <ZTP><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_drukztp',,1);
1


\ztpwysw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS
:: OPIS: ZTP - 'Wyświetl' w oknie typów zleceń
::  OLD: \ztpwysw\zlec1.fml
::  TAG: <ZTP><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
ZTP.win_edit('RED');
ZTP.display();
~~


\ztp_br
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: ZTP - przed rekord w oknach wertowania
::----------------------------------------------------------------------------------------------------------------------
VAR1.NRUBALL:=exec('opis_anrub','!zws_par_tzle',ZTP.CRUBALL);
VAR1.NRUBMAT:=exec('opis_anrub','!zws_par_tzle',ZTP.CRUBMAT);
VAR1.NRUBROB:=exec('opis_anrub','!zws_par_tzle',ZTP.CRUBROB);
VAR1.NRUBJEDN:=exec('opis_anrub','!zws_par_tzle',ZTP.CRUBJEDN);
VAR1.NRUBIL0:=exec('opis_anrub','!zws_par_tzle',ZTP.CRUBIL0);
VAR1.NRUBIL:=exec('opis_anrub','!zws_par_tzle',ZTP.CRUBIL);
~~


\po_ztp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS
:: OPIS: ZTP - Formuła sprawdząjaca rekord po redakcji
::   WY: 0 / 1
::  OLD: \po_ztp/zlec1.fml
::  TAG: <ZTP><MBUILDER><CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_ztp','zl_common',-menu_txt()='popraw')


\ztp_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Zwraca ZTP.ref()
::  OLD: \ztp_ref/zlecenia.fml
::  TAG: <ZTP>
::----------------------------------------------------------------------------------------------------------------------
ZTP.ref()


\set_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.14]
:: OPIS: Ustawia właściwości dla pól ZTP w okienku redagowania
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_red:=ZTP.win_edit('?');
{? ZTP.WP='W'
|| ZTP.efld_opt(_red,'enable=0',,'PROJZAKR');
   ZTP.efld_opt(_red,'mark=0',,'KOD_ZLP');
   ZTP.efld_opt(_red,'mark=0',,'KOD_PAR')
|| ZTP.efld_opt(_red,'enable=1',,'PROJZAKR');
   ZTP.efld_opt(_red,'mark=1',,'KOD_ZLP');
   ZTP.efld_opt(_red,'mark=1',,'KOD_PAR')
?};
~~


::======================================================================================================================
:: Formuły obsługi tabeli ZTPF (warianty kalkulacji/analiz dla typów zleceń)
::======================================================================================================================


\ztp_war
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: ZTPF - warianty typu kalkulacji/analizy dla typu zlecenia
::   WE: _a - '0' kalkulacja wstępna
::            '1' analiza zlecenia
::  OLD: \ztp_war/zlecenia.fml
::  TAG: <ZTP><MBUILDER><LISTA>
::----------------------------------------------------------------------------------------------------------------------
VAR.A01:=_a;
ANWAR.f_clear();
ANWAR.win_dict('SLO'+VAR.A01);
ZTPF.clear();
ZTPF.index('TW');
ZTPF.prefix(VAR.A01,ZTP.ref());
ZTPF.first();
ZTPF.win_sel('WER');
ZTPF.win_edit();
{!
|? ZTPF.hdr_sel();
   ZTPF.hdr_sel({? VAR.A01='0' || 'Warianty kalkulacji zleceń'@ || 'Warianty analizy zleceń'@ ?});
   ZTPF.select();
   {? ZTPF.size()=1
   || ZTPF.first();
      {? VAR.A01='0'
      || ZTP.DEF_OPCK:=ZTPF.OPC
      || ZTP.DEF_OPCA:=ZTPF.OPC
      ?};
      ZTP.put();
      0
   |? ZTPF.first()
   || _loop:=1;
      {!
      |?
         {? VAR.A01='0'
         ||
            {? ZTPF.OPC=ZTPF.TYP().DEF_OPCK
            || _loop:=0
            ?}
         ||
            {? ZTPF.OPC=ZTPF.TYP().DEF_OPCA
            || _loop:=0
            ?}
         ?};
         ZTPF.next()
      !};
      {? _loop
      || FUN.emsg({? VAR.A01='0' || 'Wskaż domyślny wariant kalkulacji.'@ || 'Wskaż domyślny wariant analizy.'@ ?})
      ?};
      _loop
   || 0
   ?}
!};
_is:=0;
_var:={? VAR.A01='0' || ZTP.DEF_OPCK || ZTP.DEF_OPCA ?};
{? ZTPF.first()
|| {!
   |?
      {? ZTPF.OPC=_var || _is:=1 ?};
      ZTPF.next()
   !}
?};
{? ~_is || {? VAR.A01='0' || ZTP.DEF_OPCK:=null() || ZTP.DEF_OPCA:=null() ?}; ZTP.put() ?};
~~


\ztpfdrep
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: ZTPF - Przed rekord w wariantach
::  OLD: \ztpfdrep/zlecenia.fml
::  TAG: <ZTP><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? ZTPF.OPC={? VAR.A01='0' || ZTPF.TYP().DEF_OPCK || ZTPF.TYP().DEF_OPCA ?}
|| echo('Wariant domyślny typu'@);
   'ZTPF#01#01'
|| echo('','');
   ''
?}


\chk_ztpf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: ZTPF - po rekordzie
::  TAG: <ZTP><MBUILDER><CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_ztpf','zl_common',-menu_txt()='popraw')


\ztpf_pusu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [ER/WRT/PRODUKCJ/8.70/0032]
:: OPIS: ZTPF - akcja 'usuń' wariantu analizy/kalkulacji zlecenia
::  OLD: \ztpf_pusu/slownik1.fml
::   WY: 0 / 1 - czy usunięto wariant
::  TAG: <ZTP><MBUILDER><AKCJA><DEL>
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_var:={? VAR.A01='0' || ZTPF.TYP().DEF_OPCK || ZTPF.TYP().DEF_OPCA ?};
{? ZTPF.OPC=_var
|| {? FUN.ask('Wybrany wariant jest wariantem domyślnym.\n\nCzy na pewno usunąć?'@)
   || {? VAR.A01='0'
      || ZTPF.TYP().DEF_OPCK:=null()
      || ZTPF.TYP().DEF_OPCA:=null()
      ?};
      ZTP.put();
      ZTPF.del()
   || _ok:=0
   ?}
|| {? FUN.ask('Czy na pewno usunąć?'@)
   || ZTPF.del()
   ?}
?};
0


\ztpf_ppop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: ZTPF - przed 'popraw' wariantu analizy/kalkulacji zlecenia
::   WY: 1 / 0 - czy można poprawić wariant
::  OLD: \ztpf_ppop/slownik1.fml
::  TAG: <ZTP><MBUILDER><AKCJA><PUT>
::----------------------------------------------------------------------------------------------------------------------
_var:={? VAR.A01='0' || ZTP.DEF_OPCK || ZTP.DEF_OPCA ?};
{? _var=ZTPF.OPC
|| FUN.info(
      {? VAR.A01='1'
      || 'Nie można poprawiać domyślnego wariantu analizy.'@
      || 'Nie można poprawiać domyślnego wariantu kalkulacji.'@
      ?}
   );
   0
|| 1
?}


\ztpfdefa
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: ZTPF - ustala domyślny wariant dla danego typu zlecenia
::  OLD: \ztpfdefa/slownik1.fml
::  TAG: <ZTP><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
_new:=ZTPF.OPC;
_ok:={? {? VAR.A01='0' || ZTPF.TYP().DEF_OPCK || ZTPF.TYP().DEF_OPCA ?}=null()
     || 2
     || ZTPF.OPC <> {? VAR.A01='0' || ZTPF.TYP().DEF_OPCK || ZTPF.TYP().DEF_OPCA ?}
     ?};

{? _ok=1
|| {? FUN.ask('Czy na pewno zmienić domyślny wariant?'@)
   || ZTP.seek(ZTPF.TYP);
      {? VAR.A01='0' || ZTP.DEF_OPCK:=_new || ZTP.DEF_OPCA:=_new ?};
      ZTP.put()
   ?}
|? _ok=2
|| ZTP.seek(ZTPF.TYP);
   {? VAR.A01='0' || ZTP.DEF_OPCK:=_new || ZTP.DEF_OPCA:=_new ?};
   ZTP.put()
|| FUN.info('Ten wariant jest wariantem domyślnym.'@)
?}


\leg_ztpf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [2010]
:: OPIS: ZTPF - Akcja Legenda
::  OLD: \leg_ztpf/slownik1.fml
::  TAG: <ZTP><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','ZTPF#01#')


::======================================================================================================================
:: Formuły obsługi tabeli ANRUB (rubruki kalkulacji/analiz zleceń)
::======================================================================================================================


\usun_anrub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: ANRUB - Usunięcie
::  OLD: \usun_anrub/zlecanal.fml
::  TAG: <ANWAR><MBUILDER><AKCJA><DEL>
::----------------------------------------------------------------------------------------------------------------------
_link:=ANRUB.testlink(2);
{? _link.first()
|| FUN.info('Usunięcie nie jest możliwe.\nWskazana rubryka jest już użyta w systemie.'@);
   0
|| {? FUN.ask('Czy usunąć wskazaną rubrykę?'@)
   || ANRUB.del()
   ?}
?};
~~


\prn_arub
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: ANRUB - Druk tabeli
::  OLD: \prn_arub/drukujp.fml
::  TAG: <ANWAR><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_drukarub',,1);
1


\ar_anrub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: ANRUB - Po rekordzie w oknie WER
::  OLD: \ar_anrub/zlecanal.fml
::  TAG: <ANWAR><MBUILDER><CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_anrub','zl_kalk',-menu_txt()='popraw')


\opis_anrub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: ANRUB - Opis rubryki kalkulacyjnej
::   WE: _a - numer rubryki
::  TAG: <ANWAR>
::----------------------------------------------------------------------------------------------------------------------
ANRUB.cntx_psh();
ANRUB.index('RUBR');
ANRUB.prefix(_a);
{? ANRUB.first()
|| _res:=ANRUB.OPIS
|| _res:=''
?};
ANRUB.cntx_pop();
_res


::======================================================================================================================
:: Formuły obsługi tabeli ANWAR (warianty kalkulacji/analiz zleceń)
::======================================================================================================================


\anwar_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: ANWAR - Obsługa przed aktywowaniem okienka tabeli w oknie grupowym ZTP.GRP
::   WE: _a - '0' kalkulacja wstępna zlecenia
::            '1' analiza zlecenia
::  TAG: <ANWAR><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
VAR.A01:=_a;
ANWAR.index('NA');
ANWAR.prefix(VAR.A01);
~~


\usun_anwar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: ANWAR - Usunięcie ANWAR
::  OLD: \usun_anwar/zlecanal.fml
::  TAG: <ANWAR><MBUILDER><AKCJA><DEL>
::----------------------------------------------------------------------------------------------------------------------
_ask:='';
_link:=ANWAR.testlink(2);
{? _link.first()
|| {!
   |? {? _link.TABELA='AFORM'
      || _ask+='formuły';
         _link.del()
      |? _link.TABELA='ANKON'
      || _ask+={? _ask<>'' || ' i ' || '' ?}+'stałe';
         _link.del()
      || _link.next()
      ?}
   !}
?};
{? _link.first()
|| FUN.info('Usunięcie nie jest możliwe.\nWskazany wariant jest już użyty w systemie.'@);
   0
|| {? FUN.ask(
         'Czy usunąć wskazany wariant?\n\n'+
         {? _ask<>'' || 'Uwaga: usunięte także zostaną '+_ask+'\n przypisane do wariantu.' || '' ?},
      )
   || AFORM.index('OR');
      AFORM.prefix(ANWAR.ref());
      {? AFORM.first() || {! |? AFORM.del() !} ?};
      ANKON.index('OR');
      ANKON.prefix(ANWAR.ref());
      {? ANKON.first() || {! |? ANKON.del() !} ?};
      ANWAR.del()
   ?}
?};
~~


\anwarcopy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: ANWAR - Kopiowanie wariantu kalkulacji/analizy
::  OLD: \anwarcopy/zlecanal.fml
::  TAG: <ANWAR><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
_anwar:=ANWAR.ref();
undefine();
define('WAR',ANWAR.NA,'Nowy wariant'@,'Nazwa nowego wariantu'@,40,40);
def_btn('text=%1'['Zapisz'@],'key:F2');
def_btn('text=%1'['Anuluj'@],'key:Esc');
{? def_edit("chk_rec()",FUN.TYT)
|| ANWAR.NA:=DEFINE.WAR;
   {? ANWAR.add(1)
   || AFORM.index('OR');
      AFORM.prefix(_anwar);
      {? AFORM.first()
      || {!
         |? AFORM.cntx_psh();
            AFORM.OPC:=ANWAR.ref();
            AFORM.prefix();
            AFORM.add();
            AFORM.cntx_pop();
            AFORM.next()
         !}
      ?};
      ANKON.index('OR');
      ANKON.prefix(_anwar);
      {? ANKON.first()
      || {!
         |? ANKON.cntx_psh();
            ANKON.OPC:=ANWAR.ref();
            ANKON.prefix();
            ANKON.add();
            ANKON.cntx_pop();
            ANKON.next()
         !}
      ?}
   || FUN.info('"'+ANWAR.NA+'"\n\n'+'Wariant o tej nazwie już istnieje.'@)
   ?}
?};
~~


\anwar_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: ANWAR - Przed rekord
::   WY: Schemat kolorowania [STRING]
::  OLD: \anwar_rec/zlec3.fml
::  TAG: <ANWAR><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? 1+menu_pth()='P'
|| {? {? VAR.A01='0' || ZL.DEF_OPCK || ZL.DEF_OPCA ?}=ANWAR.ref()
   || 'ANWAR#01#01'
   || ''
   ?}
|| ''
?}


\chk_anwar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: ANWAR - Kontrola wypełnienia rekordu w słowniku wariantów kalkulacji/analizy zlecenia
::   WY: wynik jak dla chk_rec()
::  OLD: \chk_anwar/zlecanal.fml
::  TAG: <ANWAR><MBUILDER><CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_anwar','zl_kalk',-menu_txt()='popraw')


\anwar_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: ANWAR - Zwraca wartość ref()
::   WY: ANWAR.ref()
::  OLD: \anwar_ref/zlecanal.fml
::  TAG: <ANWAR>
::----------------------------------------------------------------------------------------------------------------------
ANWAR.ref()


::======================================================================================================================
:: Formuły obsługi tabeli AFORM (formuły wariantów kalkulacji/analiz zleceń)
::======================================================================================================================


\wan_form
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: AFORM - Wyświetlenie formuł wariantu kalkulacji/analizy zlecenia
::  OLD: \wan_form/slownik1.fml
::  TAG: <ANWAR><LISTA>
::----------------------------------------------------------------------------------------------------------------------
AFORM.index('OR');
AFORM.prefix(ANWAR.ref());
AFORM.first();
AFORM.win_sel('WER');
AFORM.hdr_sel({? VAR.A01='0' || 'Formuły kalkulacji zleceń'@
              |? VAR.A01='1' || 'Formuły analiz zleceń'@
              || ''
              ?});
AFORM.win_edit('RED'+VAR.A01);
ANRUB.win_dict('WYB');
_default:=ANRUB.actions('WYB','W');
AFORM.select();
ANRUB.actions('WYB','',_default);
AFORM.clear();
~~


\fa_prred
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2011]
:: OPIS: AFORM - przed redakcją formuły kalkulacji zleceń [fkz]
::  OLD: \fa_prred/fkalk.fml
::  TAG: <ANWAR><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
FRMK_TMP:=AFORM.FRM;
1


\fa_pored
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2011]
:: OPIS: AFORM - po redakcji formuły kalkulacji zleceń [fkz]
::  OLD: \fa_pored/fkalk.fml
::  TAG: <ANWAR><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('FRMK_TMP')>0) & (FRMK_TMP=AFORM.FRM)
||
:: jeśli nie wprowadzono zmian to ok
   &FRMK_TMP;
   1
||
:: wprowadzono zmiany
   _ret:=exec('fparse','kalkulator',AFORM.FRM,'fkz');
   {? _ret=1
   ||
      &FRMK_TMP
   ?};
   _ret
?}


\fasprred
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2011]
:: OPIS: AFORM - przed redagowaniem formuły do wyświetlenia szczegółów pozycji kalkulacji zleceń
::  OLD: \fasprred/fkalk.fml
::  TAG: <ANWAR><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
FRMK_TMP:=AFORM.FRS;
1


\faspored
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2011]
:: OPIS: AFORM - po redagowaniu formuły do wyświetlenia szczegółów pozycji kalkulacji zleceń
::  OLD: \faspored/fkalk.fml
::  TAG: <ANWAR><MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('FRMK_TMP')>0) & (FRMK_TMP=AFORM.FRS)
||
:: jeśli nie wprowadzono zmian to ok
   &FRMK_TMP;
   1
||
:: wprowadzono zmiany
:: sprawdzenie czy zrobił to administrator, dla testów można zrobić tutaj *0
   _ret:=sec_superuser()>0;
   {? _ret=1
   ||
      &FRMK_TMP
   ||
      {? FUN.ask('Treść tego pola może zmieniać tylko osoba z uprawnieniami administratora.\n'
                 'Czy przywrócić poprzednią wartość tego pola?'@)
      ||
         AFORM.FRS:=FRMK_TMP
      ?}
   ?};
   _ret
?}


\bd_aform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: AFORM - Przed 'dołącz' w oknie WER
::   WY: 1
::  OLD: \bd_aform/zlecanal.fml
::  TAG: <ANWAR><MBUILDER><AKCJA><ADD>
::----------------------------------------------------------------------------------------------------------------------
exec('bx_aform','!zws_par_tzle')


\bp_aform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: AFORM - Przed 'popraw' w oknie WER
::   WY: 1
::  OLD: \bp_aform/zlecanal.fml
::  TAG: <ANWAR><MBUILDER><AKCJA><PUT>
::----------------------------------------------------------------------------------------------------------------------
exec('bx_aform','!zws_par_tzle')


\prn_afrm
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Druk tabeli AFORM
::  OLD: \prn_afrm/drukujp.fml
::  TAG: <ANWAR><MBUILDER><AKCJA>
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_drukafrm',,1);
1


\ar_aform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: AFORM - Po rekordzie w oknie WER
::  OLD: \ar_aform/zlecanal.fml
::  TAG: <ANWAR><MBUILDER><CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_aform','zl_kalk',-menu_txt()='popraw')


\bw_aform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: AFORM - Przed 'wyświetl' w oknie WER
::   WY: 1
::  OLD: \bw_aform/zlecanal.fml
::  TAG: <ANWAR><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
exec('bx_aform','!zws_par_tzle');
AFORM.display();
~~


\bx_aform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: AFORM - Przed 'dołącz/popraw/wyświetl' w oknie WER
::   WY: 1
::  OLD: \bx_aform/zlecanal.fml
::  TAG: <ANWAR>
::----------------------------------------------------------------------------------------------------------------------
AFORM.hdr_edit({? VAR.A01='0' || 'Formuła kalkulacji zleceń'@ || 'Formuła analiz zleceń'@ ?});
1


::======================================================================================================================
:: Formuły obsługi tabeli ANKON (stałe wariantów kalkulacji/analiz zleceń)
::======================================================================================================================


\wan_cons
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: ANKON - Wyświetlanie stałych do wariantu kalkulacji/analizy zlecenia
::  OLD: \wan_cons/slownik1.fml
::  TAG: <ANWAR><LISTA>
::----------------------------------------------------------------------------------------------------------------------
ANKON.index('OR');
ANKON.prefix(ANWAR.ref());
ANKON.first();
ANKON.win_sel('WER');
ANKON.hdr_sel({? VAR.A01='0' || 'Stałe wariantu kalkulacji zlecenia'@ || 'Stałe wariantu analizy zlecenia'@ ?});
ANKON.win_edit();
ANRUB.win_dict('WYB');
_default:=ANRUB.actions('WYB','W');
ANKON.select();
ANRUB.actions('WYB','',_default);
ANKON.clear();
~~


\chk_ankon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: ANKON - po rekord w oknie WER
::  TAG: <ANWAR><MBUILDER><CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_ankon','zl_kalk',-menu_txt()='popraw')


::======================================================================================================================
:: Formuły obsługi tabeli ZPWYD
::======================================================================================================================


\chk_zpwyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: ZPWYD - po rekord w oknie WER
::----------------------------------------------------------------------------------------------------------------------
exec('chk_zpwyd','zl_head',-menu_txt()='popraw')


::======================================================================================================================
:: Formuły obsługi tabeli ZLSTW
::======================================================================================================================


\chk_zlstw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: ZLSTW - po rekord w oknie WER
::----------------------------------------------------------------------------------------------------------------------
exec('chk_zlstw','zl_limit',-menu_txt()='popraw')


\zlstw_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Przed popraw zapisu ZLSTW
::   WY: 1 / 0
::----------------------------------------------------------------------------------------------------------------------
{? 1+ZLSTW.KOD='~'
|| FUN.emsg('Zapis zastrzeżony.\n\nModyfikacje nie są możliwe.'@);
   0
|| 1
?}


\zlstw_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.02]
:: OPIS: Przed usuń zapisu ZLSTW
::   WY: 1 / 0
::----------------------------------------------------------------------------------------------------------------------
{? 1+ZLSTW.KOD='~'
|| FUN.emsg('Zapis zastrzeżony.\n\nModyfikacje nie są możliwe.'@);
   0
|| 1
?}


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 1bfb2d0810b86978c61b24155d22e28c22949592bcf0a2fca6da4eba76a7e72c7e2adc2efa4267987c9b62ef6ea1fbb04eb78ffa780378a9600c9338ff19a8a402b5cf82ac4a342cfdd75cbb5e63c143ddbf709112a2b5e299d8b635c5daec36ffe18f4d144d0e92e2f9fd6b8a546eadb800c7ca5523d57851a97645c1ffad88

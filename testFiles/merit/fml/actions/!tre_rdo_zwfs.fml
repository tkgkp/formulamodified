:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tre_rdo_zwfs.fml
:: Utworzony: 09.03.2021
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: TRE_RDO_ZWFS - Utworzenie reklamacji do dostawców w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE, symbol=REK_N,   type=_REK_N, name=Reklamacja klienta, required=T, keyref=T
::# kind=WE, symbol=CZY_PDF, type=STRING, name=Utwórz PDF,         required=N, keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE, symbol=PRN_MSK, type=STRING, name=Maska wydruku,      required=N, keyref=N
::# kind=WY, symbol=DOKUM,   type=_DOKUM, name=Załącznik,          required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init','tre');

_akcja:=_mp.akcja();
_auto:=_mp.isAutoRun();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' ?};
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || _in.PRN_MSK
      || ''
      ?};

{? var_pres('REK_N',_in)=type_of(null()) & _in.REK_N
||
:: Ustawienie kontekstu wg instancji elementu w procesie
   REK_N.cntx_psh();
   REK_N.prefix();
   {? REK_N.seek(_in.REK_N)
   ||
      _result:=exec('rek_n_druk','reklamacje',_czy_pdf,_msk,_auto);
      {? _czy_pdf & _result.WYN
      ||
         _out.DOKUM:=_result.REF;
         _mp.save(,_out);
         {? ~_keep || _mp.done() ?}

      |? ~_czy_pdf & _result.WYN
      ||
         {? ~_keep || _mp.done() ?}
      ?}
   ||
      _mp.error('Nie znaleziono reklamacji.')
   ?};
   REK_N.cntx_pop()
||
   _mp.error('Brak parametru wejściowego REK_N.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('REK_N',_in)=type_of(null()) & _in.REK_N
||
   {? _in.CZY_PDF='NIE'
   || 'Wydrukuj reklamację do dostawców: %1'@@[exec('record','#to_string',_in.REK_N)]
   || 'Utwórz PDF reklamacji do dostawców: %1'@@[exec('record','#to_string',_in.REK_N)]
   ?}
|| ''
?}


\rdo_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Reklamacja do dostawców - Utwórz PDF
::----------------------------------------------------------------------------------------------------------------------
exec('rek_n_druk_gr','reklamacje',1,'TRE_RDO_ZWFS','Area Utwórz PDF')


\rdo_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Reklamacja do dostawców - Drukuj
::----------------------------------------------------------------------------------------------------------------------
exec('rek_n_druk_gr','reklamacje',0,'TRE_RDO_ZWFS','Area Drukuj')

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:41 8bacd9b249e0d4aef580ef67f70465e816156758ff4143ea4bde5a79a295a27cc12e40a3654e03169f548ac2e4e7a15ed5c187d9a6553e2f18157a25dca95cb924ad87a0b1ec9538a55a1487859fe36627518d25b5d160865e67fe45ea0d478b85d5a51183fa54978c444d10d4d4e8022e376a9c1958d07b7934a65b9caf4434

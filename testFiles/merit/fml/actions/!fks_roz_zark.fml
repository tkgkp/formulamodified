:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_roz_zark.fml
:: Utworzony: 10.03.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_ROZ_ZARK - Przeglądanie rozrachunków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przeglądanie rozrachunków wg kont - główna formuła czynności FKS_ROZ_ZARK.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::
{? _>0 || exec('op_przeg','!fks_roz_zark',_a) || exec('op_przeg','!fks_roz_zark') ?}


\op_przeg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Wyswietlanie rozrachunkow
::   WE: [_a] - parametr z linkiem do rozrachunku lub pozycji rozrachunku
::       [_b] - ukrywane akcje
::  OLD: \op_przeg/oper.fml
::----------------------------------------------------------------------------------------------------------------------
:: obsługa linku jeżeli przekazano
{? _>0 & type_of(_a)=type_of('') & +_a=48
|| zakladka:=1;
   {? ~exec('link_roz','fks',_a) || return(0) ?}
?};
_hide:={? var_pres('_b')>0 || _b || '' ?};
ROZRACH.fld_fml('SYMR','BEFORE_DISPLAY',"
   {? f_usr_changed()
   || exec('zap_op_sum','rozrach')
   ?};
   1
");
exec('set_rbop','rozrach_przel','OP');
formikon:="exec('ikony','fks_sp',OP.SP)";
OP.win_fml('WER2',,'SP',,'ICON_BEFORE',formikon);
formikon:="exec('ikony','fks_sp',ZAP_OP.SP_WYM)";
ZAP_OP.win_fml('ROZL1',,'SP_WYM',,'ICON_BEFORE',formikon);
formikon:="exec('ikony','fks_sp',OPTMP.SPP)";
OPTMP.win_fml('PB_GEN',,'SPP',,'ICON_BEFORE',formikon);
OPTMP.win_fml('PB_ZB',,'SPP',,'ICON_BEFORE',formikon);
formikon:="{? OPTMP.ZB='T' || 'xwin16.png:3' || '' ?}";
OPTMP.win_fml('PB_GEN',,'ROZL',,'ICON_BEFORE',formikon);
ROZRACH.TABELA:='OP'; ZAP_OP.first();
ROZNE.PREFIX:=ROZNE.MASKA:=ROZNE.TYP_DOK:='';
ROZNE.ZAKR_ROZ:=1;
ROZNE.KH_WYB:=ROZNE.KON_OD:=ROZNE.KON_DO:=null;
ROZNE.KOMP_DOD:=ROZNE.KOMP_DDO:=ROZNE.DATA_OD:=ROZNE.DATA_DO:=date(0,0,0);
_wer_slo:=SLO.mk_sel('Projekty'@,'P',,'slo_wer2',,,,,'U');
SLO.win_fld(_wer_slo,,'KOD',,,8,,,'Kod'@);
SLO.win_fld(_wer_slo,,'TR',,,20,,,'Treść'@);
_okn_grp:=OP.grp_make('Rozrachunki'@,,'tab_rozr_grup',,,"exec('exit','zws')");
OP.grp_sel(_okn_grp,,'WER2','Wg kont'@,"exec('zap_opod','rozrach')",,,,"zakladka:=1;OP.hdr_sel(); OP.hdr_sel(tyt_op)",
           "1",,,'maximized');
{? exec('interm','#system')
|| OP.tab_splt(_okn_grp,,'horizontal','panel1',',55%');
   OP.grp_sel(_okn_grp,ZAP_OP,'ROZL3',,,,,,,,,,'maximized','zap_op_rozl');
   OP.tab_splt(_okn_grp,,'horizontal','panel2',',65%');
   OP.grp_edit(_okn_grp,ZAP_OP,'POSUM',,,,,,'maximized','zap_op_pods')
|| OP.tab_splt(_okn_grp,,'horizontal','panel1',20);
   OP.grp_sel(_okn_grp,ZAP_OP,'ROZL1',,,,,,,,,,'maximized','zap_op_rozl')
?};
OP.grp_sel(_okn_grp,KH,'WER_ROZ','Wg kontrahentów'@,,,,,"zakladka:=2;echo(); OP.cntx_psh(); OP.hdr_sel(); 1",
           "OP.cntx_pop(); 1",,,'maximized');
OP.grp_sel(_okn_grp,SLO,_wer_slo,'Wg projektów'@,"exec('arfr_proj','!fks_roz_zark')"
           ,,,,"zakladka:=3;exec('before_slo_proj','!fks_roz_zark'); OP.cntx_psh(); OP.hdr_sel(); 1",
           "OP.cntx_pop(); 1",,,'maximized');
OP.tab_splt(_okn_grp,,'vertical','panel2',',25%');
OP.grp_sel(_okn_grp,OP_PROJ,'WER',,"exec('aropproj','!fks_roz_zark')",,,,,,,,'maximized');
OP.tab_splt(_okn_grp,'panel2','horizontal','panel3',20);
OP.grp_sel(_okn_grp,ZAP_PROJ,'WER',,,,,,,,,,'maximized');
OP.win_sel(_okn_grp);
{? SSTALE.AR<>0
|| {? SSTALE.WAL=0
   || FUN.info('Niewypełniony parametr pracy: waluta aktywna.'@)
   || tyt_op:='';
      exec('nier','rozrach');
      {? _
      || OP.find_key(OPTMP.KONTO,OPTMP.DATA,OPTMP.SYM); _m:='AECZ:Z'
      || {? PB.f_active() || _m:='E' || _m:='X:X' ?}
      ?};
      _akronim:='WER2';
      _default:=OP.actions(_akronim,_hide+_m);
      AreaTitle.setTabWin(OP,~~);
      AreaTitle.setTitle();

::    Obsługa jeżeli przekazano link
      {? _>0 & type_of(_a)=type_of('') & +_a=48
      || {? ref_tab(_a)=OP
         || OP.seek(_a,OP.name());
            POMOC.STAN:=OP.STAN;
            exec('zakr_op','rozrach');
            OP.select(,_,2,_m)
         |? ref_tab(_a)=ZAP_OP
         || {? ZAP_OP.seek(_a,ZAP_OP.name())
            || OP.seek(ZAP_OP.OP,OP.name())
            ?};
            POMOC.STAN:=OP.STAN;
            exec('zakr_op','rozrach');
            OP.select(,_,,_m,,"win_activate('zap_op_rozl')")
         ?}
      || {? _
         || POMOC.STAN:=OP.STAN;
            exec('zakr_op','rozrach')
         ?};
         OP.select(,_,,_m)
      ?};

      OP.f_clear();
      OP.actions(_akronim,,_default);
      VAR_DEL.delete('tyt_op','zakladka')
   ?}
|| FUN.info('Niewypełniony parametr pracy: rok aktywny.'@)
?};
1


\before_slo_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obslugą słownika projektów
::----------------------------------------------------------------------------------------------------------------------
echo();
SLO.index('SL'); SLO.prefix(XINFO.SLU_PROJ); {? ~SLO.get() || SLO.first() ?};
exec('arfr_proj','!fks_roz_zark');
1


\arfr_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu projektu
::----------------------------------------------------------------------------------------------------------------------
SLO_PROJ:=SLO.ref();
SLO.cntx_psh();
OP_PROJ.index('SLO');
{? OPERATOR.DEPT<>null
|| OP_PROJ.prefix(SLO_PROJ,SSTALE.WAL,OPERATOR.DEPT)
|| OP_PROJ.prefix(SLO_PROJ,SSTALE.WAL)
?};
{? ~OP_PROJ.get() || OP_PROJ.first() ?};
grp_disp(OP_PROJ,'WER');
exec('aropproj','!fks_roz_zark');
VAR_DEL.delete('SLO_PROJ');
SLO.cntx_pop();
1


\aropproj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Po odswiezeniu okna WER tabeli OP_PROJ okna grupowego WER_GR
::  OLD: \aropproj/skid_prj.fml
::----------------------------------------------------------------------------------------------------------------------
ZAP_PROJ.index('OP_PROJ');
{? OP_PROJ.get()
|| ZAP_PROJ.prefix(OP_PROJ.ref()); _tyt:=' - '+OP_PROJ.OP().SYM
|| ZAP_PROJ.prefix(null); _tyt:=''
?};
SUM.SUMRWN:=SUM.SUMRMA:=SUM.TSUMWN:=SUM.TSUMMA:=0;
{? ZAP_PROJ.first()
|| {!
   |? SUM.SUMRWN+=ZAP_PROJ.WN;
      SUM.SUMRMA+=ZAP_PROJ.MA;
      {? ~exec('czy_nar','waluty',ZAP_PROJ.WAL2().KOD,0)
      || SUM.TSUMWN+=ZAP_PROJ.WN2; SUM.TSUMMA+=ZAP_PROJ.MA2
      ?};
      ZAP_PROJ.next()
   !}
?};
SUM.SALRWN:=F.SWn(SUM.SUMRWN,SUM.SUMRMA);
SUM.SALRMA:=F.SMa(SUM.SUMRWN,SUM.SUMRMA);
SUM.TSALWN:=F.SWn(SUM.TSUMWN,SUM.TSUMMA);
SUM.TSALMA:=F.SMa(SUM.TSUMWN,SUM.TSUMMA);
ZAP_PROJ.hdr_sel(); ZAP_PROJ.hdr_sel(_tyt);
grp_disp(ZAP_PROJ,'WER');
1


\param_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zmiana parametrów pracy
::----------------------------------------------------------------------------------------------------------------------
_rok:=SSTALE.AR;
_odd:=OPERATOR.DEPT;
_wal:=SSTALE.WAL;
{? __PARSES.editDom('FKS')
|| {? _rok<>SSTALE.AR | _odd<>OPERATOR.DEPT | _wal<>SSTALE.WAL
   || _an:=OP.AN; _tz:=OP.TZ; _sym:=OP.SYM; _do:=OP.DO; _st:=OP.STAN; _usym:=OP.SYM_ROK;
      _p:=ROZNE.PREFIX; ROZNE.PREFIX:='';
      _m:=ROZNE.MASKA; ROZNE.MASKA:='';
      exec('zakr_op','rozrach');
      {? POMOC.STAN='W' | POMOC.STAN=''
      || OP.find_key(_an,_tz,_sym,_do)
      || OP.find_key(_an,_tz,_sym,_usym,_do)
      ?};
      ROZNE.PREFIX:=_p;
      ROZNE.MASKA:=_m;
      AreaTitle.setTitle();
      exec('op_przeglad1','rozrach')
   ?}
?}


\kores_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przegladanie korespondencji dla rozrachunku
::  OLD: \kores_op/oper.fml
::----------------------------------------------------------------------------------------------------------------------
SER_POZ.cntx_psh();
SER_POZ.index('SER_OP'); SER_POZ.prefix(REF.FIRMA,OP.WAL,OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK);
{? SER_POZ.first()
|| KOR_OP:=tab_tmp(2,'DATA','DATE','Sporządzono',
                     'WZKOD','STRING[3]','Kod',
                     'WZOPIS','STRING[20]','',
                     'ODD','STRING[8]','Jed. ks.',
                     'TYP_ODS','STRING[8]','Typ ods.',
                     'ID','STRING[30]','Numer noty/wezwania',
                     'WAL_KOD','STRING[4]','Wal.',
                     'WAL_TR','STRING[30]','',
                     'REF_SNAG','INTEGER',''
                  );
   _wer:=KOR_OP.mk_sel('Korespondencja dla rozrachunku %1, symbol konta %2'@[OP.SYM,OP.AN],,,'kor_op_wer',,,,,'U');
   KOR_OP.win_fld(_wer,,'ODD');
   KOR_OP.win_fld(_wer,,'DATA',,,'11,14');
   KOR_OP.win_fld(_wer,,'WZKOD');
   KOR_OP.win_fld(_wer,,'WZOPIS');
   KOR_OP.win_fld(_wer,,'TYP_ODS',,,',9');
   KOR_OP.win_fld(_wer,,'ID');
   KOR_OP.win_fld(_wer,,'WAL_KOD');
   KOR_OP.win_fld(_wer,,'WAL_TR',,,30);
   KOR_OP.win_act(_wer,,'Formuła','Pozycje'@@,,,,"exec('poz_kor','!fks_roz_zark')",1,,,,'P');
   SER_NAG.cntx_psh(); SER_DEF.cntx_psh(); SLO.cntx_psh();
   {! |?
      SER_POZ.REF();
      KOR_OP.ODD:=SER_NAG.ODD().OD;
      KOR_OP.DATA:=SER_NAG.DATA;
      KOR_OP.WZKOD:=SER_NAG.KOD().KOD;
      KOR_OP.WZOPIS:=SER_DEF.OP;
      KOR_OP.TYP_ODS:=SER_NAG.TTAB_ODS().KOD;
      KOR_OP.ID:=SER_NAG.ID;
      KOR_OP.WAL_KOD:=SER_NAG.WAL().KOD;
      KOR_OP.WAL_TR:=SLO.TR;
      KOR_OP.REF_SNAG:=#SER_NAG.ref();
      KOR_OP.add();
      SER_POZ.next()
   !};
   SER_NAG.cntx_pop(); SER_DEF.cntx_pop(); SLO.cntx_pop();
   KOR_OP.win_sel(_wer);
   {? KOR_OP.first() || KOR_OP.select() ?};
   VAR_DEL.delete('KOR_OP')
|| FUN.info('Brak korespondencji dla bieżącego rozrachunku.'@)
?};
SER_POZ.cntx_pop()


\poz_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Pozycje korespondencji dla rozrachunkow
::  OLD: \poz_kor/oper.fml
::----------------------------------------------------------------------------------------------------------------------
SER_NAG.prefix(REF.FIRMA);
{? SER_NAG.seek(KOR_OP.REF_SNAG,SER_NAG.name())
|| exec('poz_kor_rozr','rozrach_kor',1)
|| FUN.info('Nie znaleziono pozycji.'@)
?}


\dvat_rozr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_09]
:: OPIS: Akcja Rozrachunki dla DVATa
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); DVAT.cntx_psh(); DOK.cntx_psh(); POZ.cntx_psh();
ROK_F.cntx_psh(); rok_hed:=SSTALE.AR().NAZ; _zm_rok:=null;
_obj:=exec('obj_op','dok_fks_ks');
{? PAR_SKID.get(77)='T'
|| _zm_rok:=_obj.getACTYEAR(SSTALE.AR().POCZ_ROK);
   ROK_F.prefix();
   {? _zm_rok & ROK_F.seek(_zm_rok)
   || rok_hed:=ROK_F.NAZ;
      _obj.use()
   ?}
?};
ROK_F.cntx_pop();
_rn:=ref_name(DVAT.DOK); DOK.use(_rn); DOK.prefix();
_rn:=4-_rn; POZ.use('pozy'+_rn); POZ.index('DOK'); POZ.prefix(DVAT.DOK);
_where:='';
{? POZ.first()
|| {!
   |? {? POZ.WAL().KOD=''
      || _wal:=FINFO.NAROD().KOD
      || _wal:=POZ.WAL().KOD
      ?};
      {?  _wal<>'' & POZ.ODD().OD<>'' & POZ.KON<>'' & POZ.ID<>''
      || {? _where<>''
         || _where+=' or '
         ?};
         _where+='(SLO.KOD=\'%1\' and ODD.OD=\'%2\' and OP.AN=\'%3\' and OP.SYM=\'%4\')'[_wal,POZ.ODD().OD,POZ.KON,POZ.ID]
      ?};
      POZ.next()
   !}
?};
_okn_grp:=OP.grp_make('Rozrachunki'@,,'tab_rozr_grup',,,);
OP.grp_sel(_okn_grp,,'WER_POZ','Rozrachunki',"exec('zap_opod','rozrach')",,,,"",
           "1",,,'maximized');
OP.tab_splt(_okn_grp,,'horizontal','panel1',20);
OP.grp_sel(_okn_grp,ZAP_OP,'ROZL1',,,,,,,,,,'maximized','zap_op_rozl');
OP.win_sel(_okn_grp);
OP.index('SYM2');
OP.prefix();
OP.f_clear();
OP.f_set(,'left join SLO using (OP.WAL,SLO.REFERENCE) left join ODD using(OP.ODD,ODD.REFERENCE)',_where);
OP.select();
{? _zm_rok
|| ROK_F.cntx_psh(); ROK_F.prefix(); SSTALE.AR();
   _obj.use();
   ROK_F.cntx_pop()
?};
OP.cntx_pop(); DVAT.cntx_pop(); DOK.cntx_pop(); POZ.cntx_pop()


\dok_ksg_dis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_09]
:: OPIS: Akcja Dokument księgowy dla DVAT - tylko display
::   WE: 1 - wyswietla korektę nagłówkową, 0 - wyświetla dokument źródłowy, z którego powstał dokument VAT
::----------------------------------------------------------------------------------------------------------------------
{? ~_ || _a:=0 ?};
DOK.cntx_psh; _rn:=ref_name(DVAT.DOK); DOK.use(_rn); DOK.prefix();
DOK.seek(DVAT.DOK);
{? _a
|| {? DOK.DOK_REJ().KOR_NAG='N' & (3+DOK.ZAR='FKS' & DOK.KN<>'' | 3+DOK.ZAR='LSP' & DOK.LKN<>'')
   || exec('dok2dokn','knag');
      _kn:={? 3+DOK.ZAR='LSP' || DOK.LKN || DOK.KN ?};
      _ref:=BB.sqlint(_kn);
      _maska:=(8+_kn)+4;
      DOK.use('doku'+_maska); DOK.prefix();
      {? _ref<>0 & DOK.seek(_ref,)
      || ''
      || FUN.info('Nie znaleziono powiązanego dokumentu'@); DOK.cntx_pop(); return()
      ?}
   || FUN.info('Do dokumentu nie wystawiono korekty nagłówkowej'@); DOK.cntx_pop(); return()
   ?}
?};
POMOC.V:=DOK.RVAT;
_okno:=exec('okna_r','dok_fks',DOK.DOK_REJ().SLO().KOD);
_red:=DOK.mk_edit('Dokument księgowy'@);
DOK.win_etab(_red,'Nagłówek dokumentu'@);
DOK.win_ewin(_red,,_okno);
DOK.win_etab(_red,'Status'@);
DOK.win_ewin(_red,,'DOK_STAT');
DOK.win_ebtn(_red,'text=%1,panel=bottom,align=begin,display=1'['&Pozycje'@],"exec('dvat_zapisy','!fks_vat_zdoz');''");
DOK.win_edit(_red);
{? exec('lic','#b_domain',exec('domain_ref','#b_domain','CTR'))
|| DOK.efld_opt('DOK_STAT','enable=1',DOK,'WSK_XD')
|| DOK.efld_opt('DOK_STAT','enable=0',DOK,'WSK_XD')
?};
DOK.display();
DOK.cntx_pop()


\edokum_rozr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Akcja Rozrachunki dla faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); EDOKUM.cntx_psh(); DOK.cntx_psh(); POZ.cntx_psh();
ROK_F.cntx_psh(); rok_hed:=SSTALE.AR().NAZ; _zm_rok:=null;
_obj:=exec('obj_op','dok_fks_ks');
{? PAR_SKID.get(77)='T'
|| _zm_rok:=_obj.getACTYEAR(SSTALE.AR().POCZ_ROK);
   ROK_F.prefix();
   {? _zm_rok & ROK_F.seek(_zm_rok)
   || rok_hed:=ROK_F.NAZ;
      _obj.use()
   ?}
?};
ROK_F.cntx_pop();
_ref:=EDOKUM.DOK_KS;
{? _ref=''
|| FUN.info('Faktura w obiegu nie jest zaksięgowana próbnie.'@)
|| _maska:=ref_name(_ref);
   DOK.cntx_psh();
   DOK.use(_maska); _ndx:=DOK.ndx_tmp(,,'EDOKUM',,,'ZP',,);
   DOK.index(_ndx); DOK.prefix($EDOKUM.ref(),'T',);
   {? DOK.seek(_ref)
   || _reference:=DOK.ref()
   || _reference:=null
   ?};
   DOK.ndx_drop(_ndx); DOK.cntx_pop();
   {? _reference=null
   || FUN.info('Faktura w obiegu nie jest zaksięgowana próbnie.'@)
   || DOK.use(_maska); DOK.prefix();
      _rn:=4-_maska; POZ.use('pozy'+_rn); POZ.index('DOK'); POZ.prefix(_reference);
      _where:='';
      exec('F','object');
      {? POZ.first()
      || {!
         |? {? POZ.WAL().KOD=''
            || _wal:=FINFO.NAROD().KOD
            || _wal:=POZ.WAL().KOD
            ?};
            {?  _wal<>'' & POZ.ODD().OD<>'' & POZ.KON<>'' & POZ.ID<>''
            || {? _where<>''
               || _where+=' or '
               ?};
               _where+='(SLO.KOD=\'%1\' and ODD.OD=\'%2\' and OP.AN=\'%3\' and OP.SYM=\'%4\')'[_wal,POZ.ODD().OD,POZ.KON,POZ.ID]
            ?};
            POZ.next()
         !}
      ?};
      _okn_grp:=OP.grp_make('Rozrachunki'@,,'tab_rozr_grup',,,);
      OP.grp_sel(_okn_grp,,'WER_POZ','Rozrachunki',"exec('zap_opod','rozrach')",,,,"",
                 "1",,,'maximized');
      {? ~exec('interm','#system')
      || OP.tab_splt(_okn_grp,,'horizontal','panel1',20);
         OP.grp_sel(_okn_grp,ZAP_OP,'ROZL1',,,,,,,,,,'maximized','zap_op_rozl')
      || OP.tab_splt(_okn_grp,,'horizontal','panel1',',55%');
         OP.grp_sel(_okn_grp,ZAP_OP,'ROZL3',,,,,,,,,,'maximized','zap_op_rozl');
         OP.tab_splt(_okn_grp,,'horizontal','panel2',',65%');
         OP.grp_edit(_okn_grp,ZAP_OP,'POSUM',,,,,,'maximized','zap_op_pods')
      ?};
      OP.win_sel(_okn_grp);
      OP.index('SYM2');
      OP.prefix();
      OP.f_clear();
      OP.f_set(,'left join SLO using (OP.WAL,SLO.REFERENCE) left join ODD using(OP.ODD,ODD.REFERENCE)',_where);
      OP.select();
      {? _zm_rok
      || ROK_F.cntx_psh(); ROK_F.prefix(); SSTALE.AR();
         _obj.use();
         ROK_F.cntx_pop()
      ?}
   ?}
?};
OP.cntx_pop(); EDOKUM.cntx_pop(); DOK.cntx_pop(); POZ.cntx_pop()


\pow_faksnd_rozr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Widok rozrachunku w dokumentach powiązanych
::   WE: _a: 1 - dokument sprzedaży; 2-dokument magazynowy
::----------------------------------------------------------------------------------------------------------------------
_maska:=exec('maska','dok_fks_aut_dok',{? _a=1 || {? FAKS.T().KOR<>'Z' || 'F' || 'K' ?} || 'M' ?})
        +{? _a=1 & FAKS.T().KOR='Z' || form(#FAKS.ref,,,'99') || '' ?};
{? var_pres('X_Xa')>0 || obj_del(X_Xa) ?};
X_Xa:=sql('select DOK.REFERENCE as REF from @DOK join DOK_REJ where DOK.DOKZRODL = \':_a\' ',_maska);
_rdok:=null;
{? X_Xa.first()
|| _mask:=4+(4-X_Xa.REF);
   DOK.use('doku'+_mask);
   POZ.use('pozy'+_mask);
   {? DOK.seek(X_Xa.REF) || _rdok:=DOK.ref() ?}
?};
DOK.prefix();
POZ.index('DOK'); POZ.prefix(_rdok);
_where:='';
{? POZ.first()
|| {!
   |? {? POZ.WAL().KOD=''
      || _wal:=FINFO.NAROD().KOD
      || _wal:=POZ.WAL().KOD
      ?};
      {?  _wal<>'' & POZ.ODD().OD<>'' & POZ.KON<>'' & POZ.ID<>''
      || {? _where<>''
         || _where+=' or '
         ?};
         _where+='(SLO.KOD=\'%1\' and ODD.OD=\'%2\' and OP.AN=\'%3\' and OP.SYM=\'%4\')'[_wal,POZ.ODD().OD,POZ.KON,POZ.ID]
      ?};
      POZ.next()
   !}
?};
OP.index('SYM2');
OP.prefix();
OP.f_clear();
OP.f_set(,'left join SLO using (OP.WAL,SLO.REFERENCE) left join ODD using(OP.ODD,ODD.REFERENCE)',_where);
{? OP.f_first() || 1 ?};
grp_disp(OP,'WER_POZ'); exec('zap_opod2','rozrach',1); grp_disp(ZAP_OP,'ROZL4')


\dok_rozr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Akcja Rozrachunek dla DOK
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); DOK.cntx_psh(); POZ.cntx_psh();
ROK_F.cntx_psh(); rok_hed:=SSTALE.AR().NAZ; _zm_rok:=null;
_obj:=exec('obj_op','dok_fks_ks');
{? PAR_SKID.get(77)='T'
|| _zm_rok:=_obj.getACTYEAR(SSTALE.AR().POCZ_ROK);
   ROK_F.prefix();
   {? _zm_rok & ROK_F.seek(_zm_rok)
   || rok_hed:=ROK_F.NAZ;
      _obj.use()
   ?}
?};
ROK_F.cntx_pop();
_rn:=ref_name(DOK.ref());
_rn:=4-_rn; POZ.use('pozy'+_rn); POZ.index('DOK'); POZ.prefix(DOK.ref());
_where:='';
{? POZ.first()
|| {!
   |? {? POZ.WAL().KOD=''
      || _wal:=FINFO.NAROD().KOD
      || _wal:=POZ.WAL().KOD
      ?};
      {?  _wal<>'' & POZ.ODD().OD<>'' & POZ.KON<>'' & POZ.ID<>''
      || {? _where<>''
         || _where+=' or '
         ?};
         _where+='(SLO.KOD=\'%1\' and ODD.OD=\'%2\' and OP.AN=\'%3\' and OP.SYM=\'%4\')'[_wal,POZ.ODD().OD,POZ.KON,POZ.ID]
      ?};
      POZ.next()
   !}
?};
{? _where<>''
|| _okn_grp:=OP.grp_make('Rozrachunki'@,,'tab_rozr_grup2',,,);
   OP.grp_sel(_okn_grp,,'WER_POZ','Rozrachunki',"exec('zap_opod2','rozrach')",,,,"",
               "1",,,'maximized');
   OP.tab_splt(_okn_grp,,'horizontal','panel1',20);
   OP.grp_sel(_okn_grp,ZAP_OP,'ROZL1',,,,,,,,,,'maximized','zap_op_rozl2');
   OP.win_sel(_okn_grp);
   OP.index('SYM2');
   OP.prefix();
   OP.f_clear();
   OP.f_set(,'left join SLO using (OP.WAL,SLO.REFERENCE) left join ODD using(OP.ODD,ODD.REFERENCE)',_where);
   OP.select()
|| FUN.info('Brak rozrachunków do wyświetlenia'@)
?};
{? _zm_rok
|| ROK_F.cntx_psh(); ROK_F.prefix(); SSTALE.AR();
   _obj.use();
   ROK_F.cntx_pop()
?};
OP.cntx_pop(); DOK.cntx_pop(); POZ.cntx_pop()


:Sign Version 2.0 jowisz:1045 2023/12/01 13:05:04 4998fe1a7bb4c365ee8425588c64ce661d43e44f7c61a25c78d4c4aaa8466a726a29b4170137ff45f57f165535bb9626bf641acbe695c83f45b740fbb751c0ca3f5cb13e9ce25fa0677e6bc15102511ed8ca665818fee8316c410e27763307d41d9bf6b9559f61d3e6cbb3816bd174f98bb0b37d09f05ccdf42f3f3f2ac56dc4

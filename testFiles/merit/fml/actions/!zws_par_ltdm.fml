:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ltdm.fml
:: Utworzony: 23.02.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_LTDM
::            Definiowanie typów dokumentów magazynowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Czynność ZWS_PAR_LTDM - definiowanie typów dokumentów magazynowych
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejsciowe czynnosci
::       _int - [obj_new] - parametry wewnetrzne czynnosci
::       _out - [obj_new] - parametry wyjsciowe czynnosci
::       _mp  - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('select','!zws_par_ltdm');
''


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: definiowanie typów dokumentów magazynowych
::  OLD: \typy_sp/defin.fml
::----------------------------------------------------------------------------------------------------------------------
BPMN.KK_DOM:='LMG';
BEER.NRDOK_AB:='ND';
HELP.WHERE:='';
TYPYDOK.win_sel('WER');
TYPYDOK.win_edit('RED');
TYPYDOK.index('TYP');
TYPYDOK.prefix();
exec('set_efld','!zws_par_ltdm');
TYPYDOK.select();
~~


\pr_typnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: przed redakcją typu dokumentu magazynowego
::  OLD: \pr_typnd/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:=TYPYDOK.T;
_grp:='Informacje dotyczące typu '+_typ+'.';
KOMM.init(,,'Modyfikacja typu niemożliwa.'@,'');
KOMM.set_root(_grp);

_wyn:=1;
{? -(1+menu_txt())<>'d'
|| TYPYDOK.cntx_psh();
   {? exec('dk_count','typydok',0)=0
   || KOMM.add('Typ wykorzystywany w systemie.'@,7,_grp);
      _wyn:=0
   || KOMM.add('W systemie nie wystawiono jeszcze dokumentów typu %1.'@[_typ],7,_grp)
   ?};
   TYPYDOK.cntx_pop();

   _fo:=1001;

   USERS.index('USR_KKOD');
   USERS.prefix();
   {? USERS.first()
   ||
      _licz:=0;
      {!
      |?
         _upr:=exec('get','#params',_fo,2,USERS.ref());
         {? _upr<>''
         ||
            _stop:={? _upr*(' '+_typ+' ')>0 || 1 || _upr*(_typ+' ')=1 ?};
            {? _stop=1
            ||
               {? _licz=0
               || KOMM.add('Przypisano uprawnienia do danego typu użytkownikom:'@,26,_grp)
               ?};
               KOMM.add('- '+USERS.DANE,110,_grp);
               _licz+=1;
               _wyn:=0
            ?}
         ?};
         USERS.next()
      !}
   ?};
   USERS.index('USR_KKOD');
   USERS.prefix();
   {? USERS.first()
   ||
      _licz:=0;
      {!
      |?
         _upr:=exec('get','#params',1002,2,USERS.ref());
         {? _upr<>''
         ||
            {? _upr=_typ
            ||
               {? _licz=0
               || KOMM.add('Domyślny typ dokumentu magazynowego dla użytkowników:'@,26,_grp)
               ?};
               KOMM.add('- '+USERS.DANE,110,_grp);
               _licz+=1;
               _wyn:=0
            ?}
         ?};
         USERS.next()
      !}
   ?};
::    sprawdzenie Formuł globalnych
::    Produkcja
   {? (' '+exec('get','#params',500701,2,null()))*(' '+_typ+' ')
   || KOMM.add('Typ %1 jest wybrany dla dokumentów raportujących produkcję (%2).'@[_typ,'500701'],7,_grp)

   |? (' '+exec('get','#params',500703,2,null()))*(' '+_typ+' ')
   || KOMM.add('Typ %1 jest wybrany dla dokumentów zwrotu produktu na produkcję (%2).'@[_typ,'500703'],7,_grp)

   |? (' '+exec('get','#params',500705,2,null()))*(' '+_typ+' ')
   || KOMM.add('Typ %1 jest wybrany dla dokumentów rejestrujących odpady/koprodukty (%2).'@[_typ,'500705'],7,_grp)

   |? (' '+exec('get','#params',500707,2,null()))*(' '+_typ+' ')
   || KOMM.add('Typ %1 jest wybrany dla dokumentów rozchodowych na zlecenie (%2).'@[_typ,'500707'],7,_grp)

   |? (' '+exec('get','#params',500709,2,null()))*(' '+_typ+' ')
   || KOMM.add('Typ %1 jest wybrany dla dokumentów zwrotu z produkcji (%2).'@[_typ,'500709'],7,_grp)
   ?}
?};
{? _wyn=0
|| KOMM.select(,,,,)
?};
{? _wyn & TYPYDOK.WYR='U' | HELP.RRU='T' || _wyn:=0 ?};
_wyn


\po_typnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdzenie typu dokumentu magazynowego
::  OLD: \po_typnd/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? TYPYDOK.T*' '
|| FUN.info('W typie dokumentu nie może być znaku SPACJI.'@);
   0
|| 1
?}


\z_count
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: formuła przed redakcją pól TYPYDOK
::  OLD: \z_count/firma.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=(exec('dk_count','typydok') & TYPYDOK.ZLEC='N');
{? _wyn=1 & cur_afld='DS' & (TYPYDOK.P='T' | TYPYDOK.Z='N')
|| _wyn:=0
|? _wyn=1 & cur_afld='Z' & TYPYDOK.P='T' & TYPYDOK.TD<>''
|| _wyn:=0
?};
{? TYPYDOK.Z='N' || TYPYDOK.DS:='N'; TYPYDOK.CS:='N' ?};
{? TYPYDOK.Z='N' || TYPYDOK.KOOP:='N' ?};
_wyn


\typyd_z_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TYPYDOK.Z
::  OLD: \typyd_z_ae/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()='T' || TYPYDOK.STATS_O:=TYPYDOK.STATS_N:='N' ?};
_enable:={? TYPYDOK.Z='T' || TYPYDOK.TD:=''; 'enable=0' || 'enable=1' ?};
TYPYDOK.efld_opt(TYPYDOK.win_edit('?'),_enable,,'TD');
exec('set_efld','!zws_par_ltdm');
1


\ae_td_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.20]
:: OPIS: po redakcji TYPYDOK.P
::  OLD: \ae_td_p/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('t_or_n','#field')
|| {? fld()='T' || TYPYDOK.STATS_Z:=TYPYDOK.STATS_O:=TYPYDOK.STATS_N:='N' ?};
   exec('set_efld','!zws_par_ltdm');
   1
|| exec('set_efld','!zws_par_ltdm');
   0
?}


\ae_td_ds
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po redakcji (w typie dokumentu magazynowego) pola dok. sprzedaży
::  OLD: \ae_td_ds/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? TYPYDOK.DS='N' || TYPYDOK.CS:='N'; win_disp() ?};
1


\td_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2008]
:: OPIS: formuła na F3 pola TYPYDOK.TD
::  OLD: \td_f3/firma.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:=TYPYDOK.TD; _akt:=TYPYDOK.T; _czy_p:=TYPYDOK.P; _czy_z:=TYPYDOK.Z;
TYPYDOK.cntx_psh();
TYPYDOK.clear();
TYPYDOK.f_set('T,NAZ',,,);
{? TYPYDOK.f_first()
|| {!
   |? {? TYPYDOK.T=_akt | _czy_p=TYPYDOK.P | TYPYDOK.DS='T' | (TYPYDOK.P='T' & TYPYDOK.UE='T') | TYPYDOK.Z<>_czy_z
      || TYPYDOK.f_del()
      || TYPYDOK.f_next()
      ?}
   !}
?};
TYPYDOK.index('TYP');
TYPYDOK.find_key(_typ,_typ);
TYPYDOK.win_sel('WYSTPOW');
{? TYPYDOK.select(,1) || _typ:=TYPYDOK.T ?};
TYPYDOK.f_clear();
TYPYDOK.cntx_pop();
TYPYDOK.TD:=_typ


\td_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: formuła po redakcji pola TYPYDOK.TD
::   WE: [_a] - 0(domyślnie) fld 1-wartość pola TD
::  OLD: \td_po/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
exec('td_po','typydok',_a)


\uprw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2008]
:: OPIS: formuła przed redakcją pola TYPYDOK.UPRW
::  OLD: \uprw/firma.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? TYPYDOK.P='N' & TYPYDOK.AFIFO=''
|| {? exec('dk_count','typydok') || _wyn:=1 ?}
?};
_wyn


\dn_count
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2008]
:: OPIS: formuła przed redakcją pola TYPYDOK.DN
::  OLD: \dn_count/firma.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('dk_count','typydok');
{? _wyn=1
|| {? TYPYDOK.ZLEC='T' & ~((';NT'*TYPYDOK.DN)>1) || TYPYDOK.DN:='N' ?};
   {? TYPYDOK.ZLEC='N' | (';NT'*TYPYDOK.DN)>1
   || _wyn:=1
   || _wyn:=0
   ?}
?};
_wyn


\ue_count
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: formuła przed redakcją pól TYPYDOK
::  OLD: \be_typydok_ue/firma.fml
::       \ue_count/firma.fml
::----------------------------------------------------------------------------------------------------------------------
TYPYDOK.ZLEC='N' & TYPYDOK.Z='T'


\be_td_cs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcją (w typie dokumentu magazynowego) pola redakcja cen sprzedaży
::  OLD: \be_td_cs/defin.fml
::----------------------------------------------------------------------------------------------------------------------
TYPYDOK.DS='T' & TYPYDOK.P='N' & TYPYDOK.Z='T'


\ae_td_cs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: po redakcji (w typie dokumentu magazynowego) pola cen sprzedaży
::  OLD: \ae_td_cs/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? TYPYDOK.CS='N' || win_disp() ?};
1


\afifo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2008]
:: OPIS: formula przed redakacją pola TYPYDOK.AFIFO
::  OLD: \afifo/firma.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? TYPYDOK.P='N' & TYPYDOK.UPRW<>'T'
|| {? exec('dk_count','typydok') || _wyn:=1 ?}
?};
_wyn


\tdo_rtransak_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: klawisz F3 TYPY.RTRANSAK
::  OLD: \tdo_rtransak_f3/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('slownik','intrastat','004',TYPYDOK.RTRANSAK)


\tdo_rtransak_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: po redakcji TYPYDOK.RTRANSAK
::   WY: 1 - spełnione warunki, 0 - wpp
::  OLD: \tdo_rtransak_pop/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_return:=1;

:: sprawdzenie czy wartość istnieje w słowniku 004
{? TYPYDOK.RTRANSAK<>'' & exec('find_sl','intrastat','004',TYPYDOK.RTRANSAK)=null || _return:=0 ?};

{? _return=0
|| FUN.info('Niedozwolona wartość pola Rodzaj transakcji.'@)
?};

_return


\prtdzl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: Przed redakcją pola TYPYDOK.ZLEC
::  OLd: \prtdzl/polap
::----------------------------------------------------------------------------------------------------------------------
_wyn:=(exec('dk_count','typydok') & TYPYDOK.WYR='N' & TYPYDOK.Z='N');
_wyn


\typydzl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: Po redakcji pola TYPYDOK.ZLEC
::  OLD: \typydzl/polap.fml
::----------------------------------------------------------------------------------------------------------------------
{? TYPYDOK.ZLEC<>'N'
|| TYPYDOK.Z:='N';
   TYPYDOK.DK:='N';
   TYPYDOK.UE:='N';
   TYPYDOK.DS:='N';
   {? (';NT'*TYPYDOK.DN)<=1 || TYPYDOK.DN:='N' ?};
   TYPYDOK.WAL:='N';
   TYPYDOK.RTRANSAK:=''
?};
1


\prtdwyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: Przed redkacją pola TYPYDOK.WYR
::  OLD: \prtdwyr/polap.fml
::----------------------------------------------------------------------------------------------------------------------
_prod:=exec('tte_lic','tte');
_wyn:=(_prod='T' & exec('dk_count','typydok') & TYPYDOK.Z='N');
{? _wyn & HELP.RRU='T' || _wyn:=0 ?};
_wyn


\prtdwys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: Przed wyświetleniem pola TYPYDOK.WYR
::  OLD: \prtdwys/polap.fml
::----------------------------------------------------------------------------------------------------------------------
_prod:=exec('tte_lic','tte');
{? _prod<>'T'
||'192:192:192'
|| 1
?}


\typydwyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: Po redakcji pola TYPYDOK.WYR
::  OLD: \typydwyr/polap.fml
::----------------------------------------------------------------------------------------------------------------------
{? TYPYDOK.WYR='T'
|| TYPYDOK.ZLEC:='T';
   exec('typydzl','!zws_par_ltdm')
?};
1


\prtypsys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: przed edycją pola TYPYDOK.SYS
::   WY: 1-można edytować 0-nie można
::  OLD: \prtypsys/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('dk_count','typydok') & TYPYDOK.Z='N';
_wyn


\TYPYDOK_STATS_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: Przed redakcją pól TYPYDOK.STATS_*
::       (Dla wersji branżowej redakcja statusów na wszystkich dokumentach)
::   WY: 1/0 redagować/nieredagować
::  OLD: \TYPYDOK_STATS_b/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('get_par','#parametr',141)<>'T'
|| {? TYPYDOK.P='N' & (TYPYDOK.Z='N' | TYPYDOK.Z='T' & cur_afld='STATS_Z')
   || 1
   || 0
   ?}
|| 1
?}


\be_typydok_koop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [12.41]
:: OPIS: przed redakcja TYPYSP.KOOP
::  OLD: \be_typydok_koop/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_prod:=exec('tte_lic','tte')='T';
exec('dk_count','typydok') & TYPYDOK.ZLEC='N' & TYPYDOK.Z='T' & _prod


\typydok_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: popraw TYPYDOK
::  OLD: \typydok_pop/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_lock:=exec('dk_count','typydok',0);
TYPYDOK.get();
HELP.DN:={? TYPYDOK.DN='D' || 'D' || 'N' ?};
HELP.RRU:={? TYPYDOK.WYR='U' || 'T' || 'N' ?};
{? HELP.RRU='T' || TYPYDOK.WYR:='T' ?};
{? _lock=0 | TYPYDOK.r_lock(1,1,1)
|| TYPYDOK.efld_opt(TYPYDOK.win_edit('?'),{? TYPYDOK.Z='T' || 'enable=0' || 'enable=1' ?},,'TD');
   exec('set_efld','!zws_par_ltdm');
   {? TYPYDOK.edit("exec('chk_typd','!zws_par_ltdm')")
   || {? HELP.RRU='T' || TYPYDOK.P='N'; TYPYDOK.WYR:='U' ?};
      TYPYDOK.put
   ?};
   {? _lock || TYPYDOK.r_unlock ?}
|| {? _lock & FUN.ask('Dokument zablokowany przez innego użytkownika.\n\nCzy chcesz sprawdzić przez kogo?'@)
      & TYPYDOK.r_lock(1,,1)
   || TYPYDOK.r_unlock
   ?}
?}


\chk_typd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po redakcji TYPYDOK
::  OLD: \chk_typd/firma.fml
::----------------------------------------------------------------------------------------------------------------------
exec('chk_typd','typydok')


\typd_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja przed usuń dla typów dokumentów magazynowych TYPYDOK
::   WY: czy usuwać typ dokumentu magazynowego
::  OLD: \typd_del/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? exec('pr_typnd','!zws_par_ltdm')
|| exec('delfakso','fakso','faktodef','TYM',TYPYDOK.ref());
   _wyn:=1
?};
_wyn


\typm_inf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: informacje dodatkowe dla typu dokumentu magazynowego
::   WY: ''
::  OLD: \typm_inf/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~(';INW-;INW+;PRC-;PRC+;KN+;KN-;'*TYPYDOK.T>1)
|| POM.TYPYDOK:=TYPYDOK.ref();
   POM.TYPYSP:=BEER.TYPYZAM:=null;

   FAKSO.cntx_psh();
   FAKSO.use('faktodef');
   FAKSO.win_sel('WERT');
   FAKSO.index('TYM');
   FAKSO.prefix(TYPYDOK.ref());
   FAKSO.select();
   FAKSO.cntx_pop();

   POM.TYPYDOK:=null()
|| FUN.info('Brak możliwości definiowania informacji dodatkowych\ndla dokumentu typu: %1.'@[TYPYDOK.T])
?};
''


\mag_t2mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: magazyny dla typów dokumentów
::  OLD: \mag_t2mg/defin.fml
::----------------------------------------------------------------------------------------------------------------------
T2MG.index('TYP');
T2MG.prefix(TYPYDOK.ref());
T2MG.win_sel('MAG');
T2MG.select();
1


\addt2mg2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: dodanie MG dp T2MG
::  OLD: \addt2mg2/defin.fml
::----------------------------------------------------------------------------------------------------------------------

VAR_DEL.delete('typ_dok');
typ_dok:=TYPYDOK.ref();
_sel_acr:=MG.mk_sel('Magazyny'@,'P',,'mag_t2mg');

MG.win_fld(_sel_acr,,'SYM',,,8,,1,'Typ'@);
MG.win_fld(_sel_acr,,'NAZ',,,60,,1,'Nazwa'@);
MG.win_act(_sel_acr,0,'Formuła','&Wybierz'@@,,,,"exec('akc_typ2mg','magdok_wspolne',typ_dok)",1,1
 ,"exec('akc_typ2mg','magdok_wspolne',typ_dok)",,'W');
MG.win_act(_sel_acr,0,'Rekord',,,,"exec('rekprzed','color','T2MG#01')");
MG.win_act(_sel_acr,0,'Formuła','&Legenda'@@,,,"exec('legenda','color','T2MG#01#02')",,,,,,'L');
MG.win_act(_sel_acr,0,'Szukaj');
MG.win_act(_sel_acr,0,'Kolejność');
MG.win_sel(_sel_acr);

_skl:=((TYPYDOK.P='T' & TYPYDOK.Z='T' & TYPYDOK.DN='N') |
        (TYPYDOK.P='N' & TYPYDOK.Z='T') |
        (TYPYDOK.P='N' & TYPYDOK.Z='N' & TYPYDOK.TD<>'')) &
       TYPYDOK.UE='N' & TYPYDOK.UPRW='N' & TYPYDOK.AFIFO='' &
       TYPYDOK.DK='N' & TYPYDOK.WYR='N' & TYPYDOK.ZLEC='N';
_koop:=TYPYDOK.KOOP;

{? _skl
|| MG.index('KOOPALL');
   MG.prefix(_koop)
|| MG.index('SKL');
   MG.prefix('N')
?};
MG.find_key(T2MG.MG().SYM,T2MG.MG().SYM);
MG.select(,1,10);
VAR_DEL.delete('typ_dok');
''


\rekt2mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: podświetlenie rekordów przypisanych
::   WE: _a - ref TYPYDOK lub MG
::  OLD: \rekt2mg/defin.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rek_typydok','ustawienia');
{? var_pres('magazyn')=7
|| {? exec('FindInSet','#table','T2MG','T',TYPYDOK.ref(),magazyn)<>null || 'T2MG#01#01' ?}
|? var_pres('typ_dok')=7
|| {? exec('FindInSet','#table','T2MG','T',typ_dok,MG.ref())<>null || 'T2MG#01#02' ?}
|| ''
?}


\po_t2mg2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: sprawdzenie wypelnienia pola T2MG.T
::  OLD: \po_t2mg2/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? T2MG.MG<>null
|| _ref:={? -(1+menu_txt())='p' || T2MG.ref() || null ?};
   _fld:=T2MG.MG().SYM;
   T2MG.cntx_psh();
   T2MG.index('TYP');
   T2MG.prefix(TYPYDOK.ref(),_fld,_fld);
   {? T2MG.first() & (_ref=null | _ref<>T2MG.ref())
   || FUN.info('Podany magazyn dodano już do uprawnień.'@);
      _wyn:='MG'
   ?};
   T2MG.cntx_pop()
|| FUN.info('Należy podać magazyn dla typu dokumentu.'@);
   _wyn:='MG'
?};
_wyn


\typm_kop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009+]
:: OPIS: kopiowanie typu dokumentu magazynowego
::  OLD: \typm_kop/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_typ_old:=TYPYDOK.ref();
ND_Z.TYPD_T:='';
ND_Z.win_edit('TYPD_KOP');
{? ND_Z.edit("
      _wyn:='~~';
      {? ND_Z.TYPD_T=''
      || FUN.info('Niewypełnione pole.'@);
         _wyn:=0
      |? ND_Z.TYPD_T*' '
      || FUN.info('W typie dokumentu nie może być znaku SPACJI.'@);
         _wyn:=0
      || TYPYDOK.cntx_psh();
         TYPYDOK.index('TYP'); TYPYDOK.prefix(ND_Z.TYPD_T,ND_Z.TYPD_T);
         {? TYPYDOK.first()
         || FUN.info('Typ dokumentu %1 już istnieje.\n'
                     'Kopiowanie na ten sam typ jest niedostępne.'@[TYPYDOK.T]);
            _wyn:='TYPD_T'
         ?};
         TYPYDOK.cntx_pop()
      ?};
      _wyn")
|| TYPYDOK.T:=ND_Z.TYPD_T;
   {? TYPYDOK.add()
   || _typ_new:=TYPYDOK.ref();
      {? ND_Z.KOP_INFD=1
      || FAKSO.cntx_psh();
         FAKSO.use('faktodef');
         FAKSO.index('TYM');
         FAKSO.prefix(_typ_old);
         {? FAKSO.first()
         || {!
            |? FAKSO.cntx_psh();
               FAKSO.TYPYDOK:=_typ_new;
               FAKSO.prefix();
               FAKSO.add();
               FAKSO.cntx_pop();
               FAKSO.next()
            !}
         ?};
         FAKSO.cntx_pop()
      ?};
      {? ND_Z.KOP_MAGS=1
      || T2MG.cntx_psh();
         T2MG.index('TYP');
         T2MG.prefix(_typ_old);
         {? T2MG.first()
         || {!
            |? T2MG.cntx_psh();
               T2MG.T:=_typ_new;
               T2MG.prefix();
               T2MG.add();
               T2MG.cntx_pop();
               T2MG.next()
            !}
         ?};
         T2MG.cntx_pop()
      ?}
   ?}
?};
~~


\set_efld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: wygasza znacznik DN
::----------------------------------------------------------------------------------------------------------------------
{? TYPYDOK.P='T'
|| HELP.DN:='N';
   HELP.RRU:='N';
   TYPYDOK.efld_opt('RED','enable=0',HELP,'DN');
   TYPYDOK.efld_opt('RED','enable=1',,'DN')
|? TYPYDOK.P='N' & TYPYDOK.Z='T'
|| HELP.DN:='N';
   HELP.RRU:='N';
   TYPYDOK.DN:='N';
   TYPYDOK.efld_opt('RED','enable=0',HELP,'DN');
   TYPYDOK.efld_opt('RED','enable=0',,'DN')
|| TYPYDOK.efld_opt('RED','enable=1',HELP,'DN');
   TYPYDOK.efld_opt('RED','enable=0',,'DN')
?};
exec('ae_helpdn','!zws_par_ltdm');
:: dokument do rejestracji realizacji usług
{? HELP.RRU='T'
|| TYPYDOK.efld_opt('RED','editable=0',,'P');
   TYPYDOK.efld_opt('RED','editable=0',,'Z');
   TYPYDOK.efld_opt('RED','enable=0',,'DK');
   TYPYDOK.efld_opt('RED','enable=0',,'AFIFO');
   TYPYDOK.efld_opt('RED','enable=0',,'UPRW');
   TYPYDOK.efld_opt('RED','enable=0',,'STATS_Z');
   TYPYDOK.efld_opt('RED','enable=0',,'STATS_N');
   TYPYDOK.efld_opt('RED','enable=0',,'STATS_O');
   TYPYDOK.efld_opt('RED','enable=0',HELP,'DN')
|| TYPYDOK.efld_opt('RED','editable=1',,'P');
   TYPYDOK.efld_opt('RED','editable=1',,'Z');
   TYPYDOK.efld_opt('RED','enable=1',,'DK');
   TYPYDOK.efld_opt('RED','enable=1',,'AFIFO');
   TYPYDOK.efld_opt('RED','enable=1',,'UPRW');
   TYPYDOK.efld_opt('RED','enable=1',,'STATS_Z');
   TYPYDOK.efld_opt('RED','enable=1',,'STATS_N');
   TYPYDOK.efld_opt('RED','enable=1',,'STATS_O')
?};
{? TYPYDOK.INW<>'N'
|| HELP.DN:='N';
   TYPYDOK.efld_opt('RED','enable=0',HELP,'DN')
?};
~~


\ae_helpdn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: po redakcji HELP.DN
::----------------------------------------------------------------------------------------------------------------------
{? HELP.DN='D' | TYPYDOK.P='N' || TYPYDOK.DN:=HELP.DN ?};
{? TYPYDOK.DN='D'
|| TYPYDOK.efld_opt('RED','mark=1',,'TD');
   TYPYDOK.INW:='N';
   TYPYDOK.efld_opt('RED','enable=0',,'INW')
|| TYPYDOK.efld_opt('RED','mark=0',,'TD');
   TYPYDOK.efld_opt('RED','enable=1',,'INW')
?};
1


\pr_addtypydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: przed dołączeniem typydok
::----------------------------------------------------------------------------------------------------------------------
HELP.DN:='N';
HELP.RRU:='N';
exec('set_efld','!zws_par_ltdm');
~~


\be_helpdn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: po redakcji HELP.DN
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('dk_count','typydok');
_wyn


\disp_typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: wyświetl dla TYPYDOK
::----------------------------------------------------------------------------------------------------------------------
HELP.DN:={? TYPYDOK.DN='D' || 'D' || 'N' ?};
exec('set_efld','!zws_par_ltdm');
TYPYDOK.display();
~~

:Sign Version 2.0 jowisz:1045 2023/11/21 14:46:44 62ea004e3fe100f8e42a9bc2381c2cb3ef512ab2a3d51cce40ffa66a190319a160141423f576155d188debaed87ee9024aaa6a2a07b5b0ff137d567bfecbe9ad994e0a1c2ec30df0935a1c11f7ebdcba6c25b5631ebbc147e9c5d637df2536f56e63c4517a1acc13ec1b2970a5d86a3bf62f83e21ea8383ceade484e76789e57

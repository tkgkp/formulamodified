:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prgp.fml
:: Utworzony: 07.06.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_DOS_PRGP - Rej. godzin projektowych.
::            Czynność jest potrzebna wyłącznie ze względu na uprawnienia.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Rej. godzin projektowych - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
~~


\projgd_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('projgd_modb','!pkd_dos_prgp')


\projgd_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('projgd_modb','!pkd_dos_prgp')


\projgd_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Przed modyfikacją rekordu.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PROJGD.FIRMA=null() | PROJGD.DATA=date(0,0,0) |
   (PROJGD.OSOBA=null() & PROJGD.P=null()) | (PROJGD.PROJEKTY=null() & PROJGD.KH=null())
|| undo('Brak wypełnienia wymaganych pól.'@);
   return(0)
?};
{? #(PROJGD.name()+4)<>PROJGD.DATA~1
|| undo('Brak zgodności daty z maską tabeli.');
   return(0)
?};

:: Autouzupełnianie danych.
PROJGD.ROK:=PROJGD.DATA~1;
PROJGD.MC:=PROJGD.DATA~2;

{? PROJGD.P<>null() & PROJGD.OSOBA=null()
|| P.cntx_psh();
   PROJGD.OSOBA:=PROJGD.P().OSOBA;
   P.cntx_pop()
?};

{? PROJGD.PROJEKTY<>null()
|| PROJEKTY.cntx_psh();
   {? PROJGD.KH=null()
   || PROJGD.KH:=PROJGD.PROJEKTY().KH
   ?};
   {? PROJGD.HAN=null()
   || PROJGD.HAN:=PROJGD.PROJEKTY().HAN
   ?};
   PROJEKTY.cntx_pop()
?};

1


\edit_var_nazwisko_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Przed wyświetleniem pola EDIT_VAR.NAZWISKO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
EDIT_VAR.NAZWISKO:=PROJGD.HAN().NAZWISKO;
OSOBA.cntx_pop();
1


\edit_var_nazwisko_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Wybór F3 dla pola EDIT_VAR.NAZWISKO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_na:=EDIT_VAR.NAZWISKO;
OSOBA.cntx_psh();
OSOBA.index('OSOBA');
OSOBA.prefix();
OSOBA.win_sel('WYB');
{? PROJGD.HAN().NAZWISKO<>_na
|| PROJGD.HAN:=null();
   {? ~(OSOBA.find_key(_na) & exec('osoba_x_bd','osoba'))
   || OSOBA.first()
   ?}
?};
_ref:=REF.OSOBA;
{? _disp:=OSOBA.select(,1)
|| PROJGD.HAN:=OSOBA.ref()
?};
REF.OSOBA:=_ref;
OSOBA.cntx_pop();
{? _disp
|| win_disp()
?};
~~


\edit_var_nazwisko_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Po redagowaniu pola EDIT_VAR.NAZWISKO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? EDIT_VAR.NAZWISKO=''
|| PROJGD.HAN:=null();
   return(1)
?};

_ret:=0;
OSOBA.cntx_psh();
OSOBA.index('OSOBA');
OSOBA.prefix();
{? PROJGD.HAN=null() | PROJGD.HAN().NAZWISKO<>EDIT_VAR.NAZWISKO
|| {? OSOBA.find_key(EDIT_VAR.NAZWISKO) & exec('osoba_x_bd','osoba')
   || PROJGD.HAN:=OSOBA.ref();
      _ret:=1
   || _na:=EDIT_VAR.NAZWISKO;
      PROJGD.HAN:=null();
      win_disp();
      EDIT_VAR.NAZWISKO:=_na
   ?}
|| _ret:=1
?};
OSOBA.cntx_pop();
_ret


\projgd_projekty_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Po redagowaniu pola PROJGD.PROJEKTY.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('projekty_ae','projekty')
|| PROJGD.KH:=PROJGD.PROJEKTY().KH;
   {? PROJGD.PROJEKTY().HAN<>null()
   || OSOBA.cntx_psh();
      OSOBA.prefix();
      PROJGD.PROJEKTY().HAN();
      {? exec('osoba_x_bd','osoba')
      || PROJGD.HAN:=PROJGD.PROJEKTY().HAN;
         EDIT_VAR.NAZWISKO:=PROJGD.HAN().NAZWISKO
      ?};
      OSOBA.cntx_pop()
   ?};
   1
?}


\projgd_os_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Okienko - przed" dla okna WER4OS tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
OSOBA.win_dict('SLO');
P.cntx_psh();
P.prefix();
P.f_set('T',,'"P"."REFERENCE" in (select REF from :_a)',exec('dostepne_p_osoba','schemat','PKD','*T',,$OSOBA.ref()));
P.win_dict('SLO');
SLO_TYP.cntx_psh();
SLO_TYP.prefix();
SLO_NAZ.cntx_psh();
SLO_NAZ.prefix();
PROJEKTY.cntx_psh();
PROJEKTY.prefix();
KH.cntx_psh();
KH.prefix();
:: Ponieważ w tabeli PROJGD znajdują się dwa pola zrelacjonowane z tabelą OSOBA, formułę początkową dla pola
:: PROJGD.OSOBA ustalamy na podstawie BIEŻĄCEGO rekordu tabeli OSOBA, poprzez zapamiętanie jego SQL-ref'a.
PROJGD.fld_fml('OSOBA','BLANK',$("OSOBA.prefix(); {? OSOBA.seek('"+$OSOBA.ref()+"') || OSOBA.ref() || null() ?}"));
~~


\projgd_os_ao
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Okienko - po" dla okna WER4OS tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PROJGD.fld_fml('OSOBA','BLANK',"*");

KH.cntx_pop();
PROJEKTY.f_clear();
PROJEKTY.cntx_pop();
SLO_NAZ.cntx_pop();
SLO_TYP.cntx_pop();
P.f_clear();
P.cntx_pop();
OSOBA.cntx_pop();
~~


\projgd_hdr_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła dokleja aktualnie wybrany rok do tytułu okna redagowania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PROJGD.hdr_edit();
PROJGD.hdr_edit(': %1' [PROJGD.name()+4]);
1


\projgd_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Dołącz - przed" dla okien wertowania tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('projgd_hdr_edit','!pkd_dos_prgp')


\projgd_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Popraw - przed" dla okien wertowania tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PROJGD.IMPORT:='N';
exec('projgd_hdr_edit','!pkd_dos_prgp')


\projgd_usun_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - grupa przed" dla okien wertowania tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('del_ask','#table')
|| params_get().cfg.extra:=0;
   1
?}


\projgd_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - przed" dla okien wertowania tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_gr:=PROJGD.sel_size();
{? PROJGD.BLOKADA='T'
|| {? _gr
   || params_get().cfg.extra+=1
   || FUN.info('Informacja o godzinach projektowanych jest zablokowana. Zmiany niemożliwe.'@)
   ?}
|? _gr | exec('del_ask','#table')
|| PROJGD.del()
?}


\projgd_usun_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Usuń - grupa po" dla okien wertowania tabeli PROJGD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? params_get().cfg.extra
|| FUN.info('Usunięcie wszystkich zaznaczonych wierszy nie było możliwe.'@)
?}


\projgd_rok_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji Rok (zmiana przeglądanego okresu).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_PAR:=tab_tmp(1,'ROK','INTEGER','Rok'@);
_we:=_PAR.mk_edit('Godziny projektowe'@);
_PAR.win_esep(_we,'Okres'@);
_PAR.win_efld(_we,,'ROK',,,5,0,,,,'Rok');
exec('ok_esc','#window',_PAR,_we);
_PAR.win_edit(_we);
_PAR.efld_opt(_we,'mark=1',,'ROK');

_rn:=_PAR.ROK:=#(PROJGD.name()+4);
{? _PAR.edit(
      "  _PAR:=cur_tab(1,1);
         {? (_chk:=__CHK.record(_PAR,,'ROK'))<>''
         || return(_chk)
         |? _min:=1900;
            _max:=exec('max_rok','#datetime');
            _PAR.ROK<_min | _max<_PAR.ROK
         || __CHK.err_fld(PROJGD,'ROK',1,'Dozwolone wartości z przedziału %1-%2.'@ [$_min,$_max])
         || ''
         ?}
      "
   ) & _PAR.ROK<>_rn
|| _ndx:=PROJGD.index('?');
   PROJGD.use('pj_g%1' [$(_PAR.ROK)+4]);
   PROJGD.index(_ndx)
?}


\projgd_blok_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Zablokuj/Odblokuj - grupa przed" w oknach wertowania tabeli PROJGD.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Blokuj.
::                         0 - Odblokuj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=var_pres('_a')=type_of(0) & _a;
{? _tryb
|| FUN.ask('Czy na pewno chcesz zablokować możliwość zmian w zaznaczonych wierszach?'@)
|| FUN.ask('Czy na pewno chcesz odblokować możliwość zmian w zaznaczonych wierszach'@)
?}


\projgd_blok_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Zablokuj/Odblokuj - przed" w oknach wertowania tabeli PROJGD.
::   WE: [_a] [INTEGER] - Tryb pracy:
::                         1 - Blokuj.
::                         0 - Odblokuj [domyślnie].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=var_pres('_a')=type_of(0) & _a;
_blok:={? _tryb || 'T' || 'N' ?};

{? PROJGD.BLOKADA=_blok
|| return(1)
?};

_gr:=PROJGD.sel_size();
{? _gr |
   {? _tryb
   || FUN.ask('Czy na pewno chcesz zablokować możliwość zmian w bieżącym wierszu?'@)
   || FUN.ask('Czy na pewno chcesz odblokować możliwość zmian w bieżącym wierszu?'@)
   ?}
|| PROJGD.BLOKADA:=_blok;
   PROJGD.put()
?}


\projgd_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER4OS tabeli PROJGD.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| PROJGD.actions_grayed('WER4OS',{? PROJGD.sel_size() || '' |? PROJGD.BLOKADA='T' || 'PUB(Z)' || 'B(O)' ?})
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 79c3664ee5b833354d0875f9c1fce1fe3cef905168695fa50433109f0a51adfcf097cdcbdb7387e3ebac04e46e06d9aba2012c90a3026587906466277db183cb8e8216365304dcf654141578209376e78b1d715e63d3afebd453c57bb68d71399738f37df2f1ace4c52ceae32886d55dac02fc34ee4b372dece0fec22f0b18ba

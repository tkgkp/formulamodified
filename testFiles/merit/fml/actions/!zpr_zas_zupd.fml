:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zpr_zas_zupd.fml
:: Utworzony: 25.06.2018
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZPR_ZAS_ZUPD — Aktualizacja zastępstw własnych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Aktualizacja zastępstw własnych - główna formuła czynności ZPR_ZAS_DARK.
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# kind=WE, symbol=DNI_WSTECZ, type=NUMBER, name=Ile dni wstecz ma być aktualizowanych, required=N, keyref=N
_args:=params_get();
_we:=_args.in;
_mp:=_args.mp;
_ok:={? _mp.pathTodo() | _mp.pathProc() || FUN.ask('Zaktualizować zastępstwa?'@) || 1 ?};
{? _ok
|| _dzis:=date();
   _dni:={? var_press('DNI_WSTECZ',_we)>0 || _we.DNI_WSTECZ || 1 ?};
   {? _dni=0 || _dni:=1 ?};
   _max:=_dzis-_dni;
   ZAST_NAG.cntx_psh();
   ZAST_NAG.index('DATA_DO');
   ZAST_NAG.prefix();
   {? ZAST_NAG.find_le(_dzis)
   || {!
      |? {? ZAST_NAG.DATA_DO>=_max
         || do();
            exec('del_upr_users','zastepstwa');
            end
         ?};
         ZAST_NAG.prev() & ZAST_NAG.DATA_DO>=_max
      !}
   ?};
   ZAST_NAG.cntx_pop();
   _users:=OBIEGI.USERS;
   ZAST_NAG.cntx_psh();
   ZAST_NAG.index('DATA_DO');
   ZAST_NAG.prefix();
   {? ZAST_NAG.first()
   || ZAST_NAG.find_ge(date());
      {!
      |? {? ZAST_NAG.DATA_OD=date()
         || OBIEGI.USERS:=ZAST_NAG.ZASTEPCA;
            OBIEGI.B_USRROL:=ZAST_NAG.B_ROLE().NAME;
            x_user:=ZAST_NAG.USERS;
            B_ROLE.cntx_psh(); B_ROLE.index('UNIK'); B_ROLE.prefix(ZAST_NAG.FIRMA,OBIEGI.B_USRROL,);
            {? B_ROLE.first() || OBIEGI.B_ROLE:=B_ROLE.ref() ?};
            B_ROLE.cntx_pop();
            exec('add_upr_zast','zastepstwa')
         ?};
         ZAST_NAG.next()
      !}
   ?};
   ZAST_NAG.cntx_pop();
   OBIEGI.USERS:=_users;
   _mp.done()
?};
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Aktualizacja zastępstw'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_dni:={? var_press('DNI_WSTECZ',_we)>0 || _we.DNI_WSTECZ ?};
{? _dni
|| _desc:='Aktualizacja zastępstw (dni wstecz: %1 )'@@[$_dni]
?};
_desc

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 a7a456e795a82091f4d327a28ec1f4498dc100b3c68ed3bdf7d943a127d6d690a17bd89ac599fd86980d11606ecd425684983e6955d14746a85fa101a43c8cd0063ebb26528ac6c2b0f3c1dad42750c7a81c5e8e2aa2e10a129000c48c5edd73880af9e95d7c5a3909a89aaa925793247cb452cc1d49ced76b8560ca5ca2f16b

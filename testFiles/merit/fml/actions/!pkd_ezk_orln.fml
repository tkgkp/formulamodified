:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orln.fml
:: Utworzony: 12.08.2020
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORLN - Rejestracja przełożonych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Rejestracja przełożonych - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=P, type=_P, name=Wskazanie współpracownika, required=T, keyref=T
::
::# permissions=UD_SKL

_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_ezk_orln')

|? _mp.isMicro()
|| {? _do='VIEW'
   || _mp.keyRef(_id);
      exec('select','!pkd_ezk_orln',_in.P,_mp);
      _mp.delRef(_id);
      _mp.cancel()
   |? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@[_mp.buf_act.UID,_do]
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orln',_in.P,_mp);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Rejestracja przełożonych - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? type_of(_in.P)=type_of(null) & _in.P<>null
|| _tab:=exec('desc','pracownik',params_get().mp);
   {? _tab.ZAW_DANE='T'
   || {? _tab.OBCY='T'
      || 'Zarejestruj przełożonych: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
      |? +_tab.PESEL
      || 'Zarejestruj przełożonych: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
      || 'Zarejestruj przełożonych: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
      ?}
   || 'Zarejestruj przełożonych'@@
   ?}
?}


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli ZS_TYP z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli ZS_TYP jako składowa okna grupowego tabeli UD_DEF).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=ZS_TYP
|| sel_exit()

|| P.cntx_psh();
   REF.P();
   params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ');
   P.cntx_pop()
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestracja przełożonych niemożliwa.'@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie współpracownika lub osoby
::       _b - wskazanie menadżera procesów
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(null) | _a=null
|| return(exec('error','!pkd_ezk_orln'))
?};

REF.P:=_a;
REF.OSOBA:=REF.P().OSOBA;

:: przekaż parametry do formuł grupy
params_set(params_get());

:: utwórz okno zarządzania zależnościami
_wnd:=exec('mk_wnd','!pkd_ezk_orln');

ZS_TYP.cntx_psh();
ZS_TYP.win_sel(_wnd);
_result:=ZS_TYP.select();
ZS_TYP.cntx_pop();

_result


\mk_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Tworzy okno zarządzania przełożonymi współpracownika.
::   WE:
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized';
:: utwórz okno przeglądania zależności służbowych współpracownika
_wnd:=ZS_TYP.grp_make('Przełożeni współpracownika'@,
:: przed otwarciem
   "exec('wnd_bo','!pkd_ezk_orln')",
:: identyfikator
   '#pkd_ezk_orln',,,
:: podczas zamykania
   "exec('wnd_oc','!pkd_ezk_orln')"
);

:: dodaj winietkę z danymi współpracownika
ZS_TYP.grp_edit(_wnd,P,'INFO_P',,,,,,_mode);

:: dodaj poziomy podział okienka
ZS_TYP.grp_splt(_wnd,,'horizontal','bottom');
:: dodaj nawigator dla typów zależności służbowych
ZS_TYP.grp_sel(_wnd,ZS_TYP,'NAD',,
:: po zmianie bieżącego wiersza
   "exec('zs_typ_nad_ar','zs_lib')",,,,,,,,
:: tryb wyświetlania
   _mode
);

_wnd


\wnd_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Przed otwarciem okna przełożonych współpracownika.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: zachowaj stan kartoteki osobowej
P.cntx_psh();
OSOBA.cntx_psh();

:: przygotuj tabele
exec('wnd_bo','zs_lib');
REF.OSOBA();
REF.P();

:: odrysuj okna
grp_edisp(P,'INFO_P');
grp_disp(ZS_TYP,'NAD',1);
1


\wnd_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Podczas zamykania okna przełożonych współpracownika.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: przywróć stan tabel
exec('wnd_oc','zs_lib');
OSOBA.cntx_pop();
P.cntx_pop();
1


\rekord_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Formuła dla akcji "Rekord przed" okienka NAD tabeli ZS_TYP
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
ZS_POM.ZS_TYP:=null;
ZS_POM.ZS_DEF:=null;
ZS_POM.WYDZIAL:=null;
ZS_POM.ST:=null;
ZS_POM.F_ZATR:=null;
ZS_POM.ZA:='';
ZS_POM.OK:='';

:: dla współpracownika wskazywanego przez w REF.P
:: ustal jego przełożonego i ustaw wartości ZS_POM
_kod:=exec('slo_kod','zs_typ');
_ret:=exec('szukaj','zs_def',REF.P,_kod,1);
{? ZS_DEF.seek(_ret.REF_NAD,,1)
:: znaleziono zależność
|| ZS_DEF.P().OSOBA();
   ZS_POM.ZS_TYP:=ZS_DEF.ZS_TYP;
   ZS_POM.ZS_DEF:=ZS_DEF.ref();
   ZS_POM.WYDZIAL:=P.WYDZIAL;
   ZS_POM.ST:=P.ST;
   ZS_POM.F_ZATR:=P.F_ZATR;
   ZS_POM.ZA:=P.ZA;
   ZS_POM.OK:=ZS_DEF.OK
?};
~~


\wskaz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Obsługa akcji "Wskaż" o oknach przełożonych.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_kod:=exec('slo_kod','zs_typ');
_def:=exec('wybierz','zs_def',_kod,ZS_POM.ZS_DEF,1);

{? _def.REF=null
:: nie wybrano
|| return()
?};

ZS_DEF.cntx_psh();
{? ZS_POM.ZS_DEF=null
|| ZS_DEF.blank();
   ZS_DEF.ZS_TYP:=_def.ZS_TYP;
   ZS_DEF.ZS_DEF:=#_def.REF;
   ZS_DEF.P:=REF.P;
   ZS_DEF.prefix();
   ZS_DEF.add()
|| {? exec('szukaj','zs_def',REF.P,_kod,,1)<>null
   || ZS_DEF.ZS_DEF:=#_def.REF;
      ZS_DEF.put()
   ?}
?};
ZS_DEF.cntx_pop();
~~


\usun_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Obsługa akcji "Usuń" o oknach przełożonych.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? REF.P=null
|| return()
?};

ZS_DEF.cntx_psh();
_kod:=exec('slo_kod','zs_typ');
{? exec('szukaj','zs_def',REF.P,_kod,,1)<>null
:: usuń znaleziona zależność służbową
|| exec('usun_a','zs_def')
?};
ZS_DEF.cntx_pop();
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 3dbd4d39d10d3254ce03e9bcb1774c823441820c52b8d32a1b403b960765eb13f8e988b25764cd4f654b25b4fcaa7344426d5305c1272cd36010071a61e8f80bcb921c1a6717b1fb86601a3983bf2be6600eb93416622cece82dbddbaa289c2db31060f68a2816a9a9b34949a89ee09635959ddaf8a9dc0a5cccb8a6f548e261

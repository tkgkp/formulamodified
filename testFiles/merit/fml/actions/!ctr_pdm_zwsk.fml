:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pdm_zwsk.fml
:: Utworzony: 10.02.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PDM_ZWSK - Rejestracja wskźników pomocniczych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rejestracja wskaźników pomocniczych - główna formuła czynności CTR_PDM_ZWSK
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego (Definiowanie wskaznikow pomocniczych)
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::  OLD: \con_def_wsk/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
wybntree:=1;
_firma:=REF.FIRMA; REF.FIRMA:=REF.S_FIRMA;
ROK_F.win_dict('WER'); ROK_F.win_sel('WER');
ROK_F.win_edit('RED'); ROK_F.win_patt('RED');
K_WERSJE.win_dict('SLO');
CON_WSK.win_edit('RED');
CON_NWSK.index('DISP'); CON_NWSK.prefix(REF.FIRMA);
{? CON_NWSK.first()
|| OKRO_F.index('ROK');
   OKRO_F.prefix(CON_NWSK.ROK_F)
?};
OKRO_F.first();
SKID_MBN.win_dict('SLO');
{? exec('hasAction','users','CTR_PDM_DWSK')
|| OKRO_F.dnd_sel('WER_CON',,'records.CON_WSK',"exec('con_wsk_copy','!ctr_pdm_zwsk',0)
   ")
?};
_b.grp_sel(_c,CON_NWSK,'WER',_a,"exec('con_nwsk_arfr','!ctr_pdm_zwsk')",,,,"exec('bw','!ctr_pdm_zwsk')",,,,'maximized_with_title');
task_attach('CTR_PDM_ZWSK');
task_attach('CTR_PDM_DWSK');
_b.tab_splt(_c,,'horizontal','panel1',15);
_b.grp_sel(_c,OKRO_F,'WER_CON',,"exec('con_wsk_arfr','!ctr_pdm_zwsk')",,,,"exec('okr_nwsk_bo','!ctr_pdm_zwsk')",,,,'maximized_with_title');
_b.tab_splt(_c,'panel1','vertical','panel2');
_b.grp_sel(_c,CON_WSK,'WER',,,,,,"exec('wsk_bo','!ctr_pdm_zwsk')",,,,'maximized_with_title');
ROZNE.MOD_DISP:=null


\con_wsk_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: kopiowanie wskaźnika na inny okres
::   WE: [_a] - tryb 0(domyślnie)-d&d, 1-akcja
::   WY:
::----------------------------------------------------------------------------------------------------------------------
CON_WSK.cntx_psh(); CON_WSK.prefix();
_tryb:={? var_pres('_a')=type_of(0) || _a || 0 ?};
{? ~_tryb
|| _dest:=dnd_info('dest_record');
   _sel:=dnd_info('dropped_records')
|| {? var_press('dest_record')=-1
   || dest_record:=_dest:=-1;
      OKRO_F.cntx_psh();
      _win_acr:=OKRO_F.mk_sel('Wybierz element docelowy','P');
      OKRO_F.win_fld(_win_acr,,'NR',,,,,,);
      OKRO_F.win_fld(_win_acr,,'NAZ',,,,,,);
      OKRO_F.win_act(_win_acr,0,'Formuła','&Wybierz'@@,,'Wybór docelowej pozycji'@,,"sel_exit",1,,,,'W');
      OKRO_F.win_sel(_win_acr);
      {? OKRO_F.select() || dest_record:=_dest:=OKRO_F.ref() ?};
      OKRO_F.cntx_pop()
   || _dest:=dest_record
   ?};
   {? CON_WSK.sel_size()<=1 || {? var_press('dest_record')>-1 || &dest_record ?} ?};
   _sel:=tab_tmp(1,'REF','INTEGER','');
   _sel.blank();
   _sel.REF:=#CON_WSK.ref();
   _sel.add(1)
?};
OKRO_F.cntx_psh(); OKRO_F.prefix();
{? OKRO_F.seek(_dest) & _sel.first()
|| {! |?
      {? CON_WSK.seek(_sel.REF) & CON_WSK.OKRO_F<>OKRO_F.ref()
      || CON_WSK.OKRO_F:=OKRO_F.ref(); CON_WSK.add(1)
      ?};
      _sel.next()
   !}
?};
OKRO_F.cntx_pop(); CON_WSK.cntx_pop();
{? cur_win(1,1)<>'WER_CON' & var_press('dest_record')=-1 || grp_disp(OKRO_F,'WER_CON') ?}


\con_nwsk_arfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Po odswiezeniu okienka tabeli CON_NWSK
::  OLD: \con_nwsk_arfr/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_max:=0;
SKID_MBP.cntx_psh();
SKID_MBP.index('LP'); SKID_MBP.prefix(CON_NWSK.SKID_MBN);
{? SKID_MBP.last()
|| _max:=SKID_MBP.LP
?};
SKID_MBP.cntx_pop();
{! _i:=1..5
|! CON_WSK.efld_opt('RED','enable='+$(_i<=_max),ROZNE,'UD_SCH0'+$_i);
   CON_WSK.efld_opt('RED','mark='+$(_i<=_max),ROZNE,'UD_SCH0'+$_i);
   CON_WSK.efld_opt('RED','enable='+$(_i<=_max),UD_POM,exec('dim2field','control',_i));
   CON_WSK.efld_opt('RED','enable='+$(_i<=_max),CON_WSK,exec('dim2field','control',_i))
!};
grp_disp(OKRO_F,'WER_CON');
grp_disp(CON_WSK,'WER');
1


\con_wsk_arfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Po odswiezeniu okienka tabeli OKRO_F (definiowanie wskaznikow)
::  OLD: \con_wsk_arfr/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
grp_disp(CON_WSK,'WER');
1


\okr_nwsk_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Przed obsluga okienka WER_CON tabeli OKRO_F
::  OLD: \okr_nwsk_bo/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.index('ROK');
{? CON_NWSK.get()
|| OKRO_F.prefix(CON_NWSK.ROK_F);
   {? OKRO_F.get()
   || win_set('cur_row_pos='+$(OKRO_F.NR+1),OKRO_F,'WER_CON')
   || OKRO_F.first()
   ?}
|| OKRO_F.prefix(null)
?};
1


\wsk_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Przed obsluga okienka WER tabeli CON_WSK
::  OLD: \wsk_bo/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? OKRO_F.get()
|| ROZNE.MOD_DISP:=CON_NWSK.SKID_MBN;
   SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(CON_NWSK.SKID_MBN);
   ROZNE.UD_SCH01:={? SKID_MBP.find_key(1) || SKID_MBP.ref() || null ?};
   ROZNE.UD_SCH02:={? SKID_MBP.find_key(2) || SKID_MBP.ref() || null ?};
   ROZNE.UD_SCH03:={? SKID_MBP.find_key(3) || SKID_MBP.ref() || null ?};
   ROZNE.UD_SCH04:={? SKID_MBP.find_key(4) || SKID_MBP.ref() || null ?};
   ROZNE.UD_SCH05:={? SKID_MBP.find_key(5) || SKID_MBP.ref() || null ?};
   SKID_MBP.cntx_pop();
   CON_WSK.actions('WER','','P:D',1)
|| CON_WSK.actions('WER','DUP:D','',1)
?};
CON_WSK.index('DISP'); CON_WSK.prefix(CON_NWSK.ref(),OKRO_F.ref());
{? ~CON_WSK.get() || CON_WSK.first() ?};
1


\sch_wyr_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Na wyswietl dla pol CON_WYR.PBUD, JORG, OKOSZ, WYM4, WYM5
::  OLD: \sch_wyr_dsp/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? ROZNE.MOD_DISP & ~cur_tab(1,1).sel_size()
|| _nr:={? cur_afld()='PBUD' || 1
        |? cur_afld()='JORG' || 2
        |? cur_afld()='OKOSZ' || 3
        |? cur_afld()='WYM4' || 4
        || 5
        ?};
   SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(CON_NWSK.SKID_MBN);
   {? ~SKID_MBP.find_key(_nr) || _zwrot:=exec('findfnv','#color') ?};
   SKID_MBP.cntx_pop()
?};
_zwrot


\usu_nwsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Usuwanie wskaznikow uzytkownika
::  OLD: \usu_nwsk/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć definicję wskaźników?'@)
|| do();
   CON_WSK.cntx_psh();
   CON_WSK.index('DISP'); CON_WSK.prefix(CON_NWSK.ref());
   {? CON_WSK.first()
   || {!
      |? _delr:=CON_WSK.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?};
   CON_WSK.cntx_pop();
   _delr:=CON_NWSK.del(,1);
   {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?};
   end()
?}


\kop_def_wsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Kopiowanie definicji wskaznikow
::  OLD: \kop_def_wsk/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.win_edit('KOP_WSK');
ROZNE.ROK_DOC:=null; ROZNE.WER_DOC:=CON_NWSK.K_WERSJE; _source:=CON_NWSK.ref(); _rok:=CON_NWSK.ROK_F;
{? ROZNE.edit("exec('spr_kop_wsk','!ctr_pdm_zwsk')")
|| _model:=CON_NWSK.SKID_MBN;
   CON_NWSK.cntx_psh();
   CON_NWSK.ROK_F:=ROZNE.ROK_DOC;
   CON_NWSK.K_WERSJE:=ROZNE.WER_DOC;
   CON_NWSK.FIRMA:=REF.FIRMA;
   {? CON_NWSK.add(1)
   || OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
      CON_WSK.cntx_psh(); CON_WSK.index('UNIK'); CON_WSK.prefix(_source);
      {? CON_WSK.first()
      || {! |?
            _nrokr:=CON_WSK.OKRO_F().NR;
            {? OKRO_F.find_key(CON_NWSK.ROK_F,_nrokr)
            || CON_WSK.cntx_psh(); CON_WSK.prefix();
               CON_WSK.OKRO_F:=OKRO_F.ref();
               CON_WSK.CON_NWSK:=CON_NWSK.ref();
               CON_WSK.add(1);
               CON_WSK.cntx_pop()
            ?};
            CON_WSK.next()
         !}
      ?};
      CON_WSK.cntx_pop(); OKRO_F.cntx_pop()
   ?};
   CON_NWSK.cntx_pop()
?}


\spr_kop_wsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Kopiowanie definicji wskaznikow - sprawdzanie parametrow po okienku
::  OLD: \spr_kop_wsk/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=''; _model:=CON_NWSK.SKID_MBN; _mod_kod:=CON_NWSK.SKID_MBN().KOD;
{? ROZNE.ROK_DOC=null
|| FUN.emsg('Nie wybrano roku docelowego.'@); _zwrot:='ROK_DOC'
?};
{? _zwrot='' & ROZNE.WER_DOC=null
|| FUN.emsg('Nie wybrano wersji docelowej.'@); _zwrot:='WER_DOC'
?};
{? _zwrot=''
|| CON_NWSK.cntx_psh();
   CON_NWSK.index('UNIK'); CON_NWSK.prefix(REF.FIRMA,ROZNE.ROK_DOC,_model,ROZNE.WER_DOC);
   {? CON_NWSK.first()
   || FUN.emsg('Istnieje definicja wskaźników dla roku %1\nmodelu %2 i wersji %3.'@[ROK_F.NAZ,_mod_kod,K_WERSJE.SYM]);
      _zwrot:='ROK_DOC'
   ?};
   CON_NWSK.cntx_pop()
?};
_zwrot


\con_wsk_rprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: CON_WSK - rekord przed
::  OLD: \con_wsk_rprz/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
echo(CON_WSK.PBUD().OPIS+
     {? CON_WSK.JORG || ' / '+CON_WSK.JORG().OPIS || '' ?}+
     {? CON_WSK.OKOSZ || ' / '+CON_WSK.OKOSZ().OPIS || '' ?}+
     {? CON_WSK.WYM4 || ' / '+CON_WSK.WYM4().OPIS || '' ?}+
     {? CON_WSK.WYM5 || ' / '+CON_WSK.WYM5().OPIS || '' ?});
0


\okr_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Rekord przed okienka WER_CON tabeli OKRO_F
::  OLD: \okr_con/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? CON_NWSK.get()
|| CON_WSK.cntx_psh();
   CON_WSK.index('UNIK'); CON_WSK.prefix(CON_NWSK.ref(),OKRO_F.ref());
   {? CON_WSK.first() || _zwrot:='OKRO_F#04#02' ?};
   CON_WSK.cntx_pop()
?};
_zwrot


\bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed obslugą okna CON_NWSK w grupie
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','#field',UD_POM,'PBUD',
   "UD_POM.PBUD:=CON_WSK.PBUD().SYMBOL;~~",
   "exec('be_dim','control',CON_NWSK.SKID_MBN,1)",
   "  _sym:=exec('f3_dim','control',CON_NWSK.SKID_MBN,1);
      {? _sym<>''
      || fld(_sym)
      ?}
   ",
   "  _ref:=exec('ae_dim','control',CON_NWSK.SKID_MBN,1);
      {? _ref
      || CON_WSK.PBUD:=_ref;
         fld(CON_WSK.PBUD().SYMBOL);
         1
      || CON_WSK.PBUD:=null;
         fld('');
         1
      ?}
   "
);
exec('set_fml','#field',UD_POM,'JORG',
   "UD_POM.JORG:=CON_WSK.JORG().SYMBOL;~~",
   "exec('be_dim','control',CON_NWSK.SKID_MBN,2)",
   "  _sym:=exec('f3_dim','control',CON_NWSK.SKID_MBN,2);
      {? _sym<>''
      || fld(_sym)
      ?}
   ",
   "  _ref:=exec('ae_dim','control',CON_NWSK.SKID_MBN,2);
      {? _ref
      || CON_WSK.JORG:=_ref;
         fld(CON_WSK.JORG().SYMBOL);
         1
      || CON_WSK.JORG:=null;
         fld('');
         1
      ?}
   "
);
exec('set_fml','#field',UD_POM,'OKOSZ',
   "UD_POM.OKOSZ:=CON_WSK.OKOSZ().SYMBOL;~~",
   "exec('be_dim','control',CON_NWSK.SKID_MBN,3)",
   "  _sym:=exec('f3_dim','control',CON_NWSK.SKID_MBN,3);
      {? _sym<>''
      || fld(_sym)
      ?}
   ",
   "  _ref:=exec('ae_dim','control',CON_NWSK.SKID_MBN,3);
      {? _ref
      || CON_WSK.OKOSZ:=_ref;
         fld(CON_WSK.OKOSZ().SYMBOL);
         1
      || CON_WSK.OKOSZ:=null;
         fld('');
         1
      ?}
   "
);
exec('set_fml','#field',UD_POM,'WYM4',
   "UD_POM.WYM4:=CON_WSK.WYM4().SYMBOL;~~",
   "exec('be_dim','control',CON_NWSK.SKID_MBN,4)",
   "  _sym:=exec('f3_dim','control',CON_NWSK.SKID_MBN,4);
      {? _sym<>''
      || fld(_sym)
      ?}
   ",
   "  _ref:=exec('ae_dim','control',CON_NWSK.SKID_MBN,4);
      {? _ref
      || CON_WSK.WYM4:=_ref;
         fld(CON_WSK.WYM4().SYMBOL);
         1
      || CON_WSK.WYM4:=null;
         fld('');
         1
      ?}
   "
);
exec('set_fml','#field',UD_POM,'WYM5',
   "UD_POM.WYM5:=CON_WSK.WYM5().SYMBOL;~~",
   "exec('be_dim','control',CON_NWSK.SKID_MBN,5)",
   "  _sym:=exec('f3_dim','control',CON_NWSK.SKID_MBN,5);
      {? _sym<>''
      || fld(_sym)
      ?}
   ",
   "  _ref:=exec('ae_dim','control',CON_NWSK.SKID_MBN,5);
      {? _ref
      || CON_WSK.WYM5:=_ref;
         fld(CON_WSK.WYM5().SYMBOL);
         1
      || CON_WSK.WYM5:=null;
         fld('');
         1
      ?}
   "
)

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:48 53bfe6a3d16e3c153ffc078d0ab4c86527d8b3806a23f4aea05f08b048cee81097d4c47437a9d1a577f6539b72b5aaddc01574c6cfe283e125f7cb2982fd5473e40caac3e357553c8a4396d1dfbcce734fb025c90915d09c359d7421fc525c1cc4a290bd1a745cde9098be4ebfe38f8f31057b91d3cc02c11b079973e004dfc8

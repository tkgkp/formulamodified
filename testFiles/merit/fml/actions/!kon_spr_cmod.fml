:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !kon_spr_cmod.fml
:: Utworzony: 25.07.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności KON_SPR_CMOD - Modele sprawozdań konsolidacyjnych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Modele sprawozdań konsolidacyjnych - główna formuła czynności KON_SPR_CMOD
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
__MBNSPR:=1;
__SMBN_L:=__SMBN_D:='N';
SKID_MBN.win_edit('RED_KON');
W_SCH.win_sel('SLO');
_win:=exec('win_skid_mbn','!kon_spr_cmod');
_b.grp_sel(_c,SKID_MBN,_win,_a,"
   SKID_MBP.index('LP');
   W_MODEL.index('SKID_MBN');
   {? SKID_MBN.get()
   || SKID_MBP.prefix(SKID_MBN.ref());
      W_MODEL.prefix(SKID_MBN.ref());
      W_MODEL.actions_grayed('WER')
   || SKID_MBP.prefix(0);
      W_MODEL.prefix(0);
      W_MODEL.actions_grayed('WER','D:D')
   ?};
   grp_disp(SKID_MBP,'KONTROL',1);
   grp_disp(W_MODEL,'WER',1)
",,,,"
   AreaTitle.setArea('KON_SPR');
   AreaTitle.setTitle();
   SKID_MBN.index('SPR'); SKID_MBN.prefix('T')
",,,,'maximized_with_title');
task_attach('KON_SPR_CMOD');
_b.tab_splt(_c,,'horizontal','bottom',10);
_b.grp_sel(_c,SKID_MBP,'KONTROL',,"",0,0,,"","",0,,'maximized_with_title');
SKID_MBP.actions('KONTROL','DpUCEI:D');

_b.tab_splt(_c,'bottom','vertical','left');
_b.grp_sel(_c,W_MODEL,'WER',,"",0,0,,"","",0,,'maximized_with_title');
~~


\win_skid_mbn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Tworzy okno wertowania dla tabeli SKID_MBN
::----------------------------------------------------------------------------------------------------------------------
_o:=SKID_MBN.mk_sel('Modele sprawozdań','P',,'#skidmbnspr',,,,,'U');
SKID_MBN.win_fld(_o,,'KOD',,,15,,,'Kod'@);
SKID_MBN.win_fld(_o,,'NAZ',,,51,,,'Nazwa'@);
SKID_MBN.win_fld(_o,,'SKID_MBG','NAZ',,17,,,'Grupa modeli'@);
SKID_MBN.win_fld(_o,UD_POM,'TYP',,,10,,,'Typ'@);
SKID_MBN.win_fld(_o,,'W_SCH','NAZ',,40,,,'Struktura grupy');
SKID_MBN.win_act(_o,,'Dołącz');
SKID_MBN.win_act(_o,1,'Dołącz');
SKID_MBN.win_act(_o,,'Formuła','Popraw'@@,,,"exec('bm_skid_mbn','control')");
SKID_MBN.win_act(_o,,'Formuła','Usuń'@@,,,"exec('usun_skid_mbn','control')");
SKID_MBN.win_act(_o,,'Formuła','Aktualizuj de&finicję'@@,,,"exec('gen_model','!kon_spr_cmod')");
SKID_MBN.win_act(_o,,'Kolejność');
SKID_MBN.win_act(_o,,'Rekord',,,,"exec('br_skid_mbn','control')","exec('ar_skid_mbn','control')");
SKID_MBN.win_act(_o,1,'Rekord',,,,"exec('br_skid_mbn','control')","exec('ar_skid_mbn','control')");
SKID_MBN.win_btn(_o,'text=Dołącz,btn_label_align=center,panel=right,align=begin','menu:D');
SKID_MBN.win_btn(_o,'text=Usuń,btn_label_align=center,panel=right,align=begin','menu:U');
SKID_MBN.win_btn(_o,'text=Aktualizuj de&finicję,btn_label_align=center,panel=right,align=begin','menu:F');
_o


\ba_w_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Przed dolacz okna WER tabeli W_MODEL
::  OLD: \ba_w_model/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__SPR');
__SPR:=tab_tmp(2,
   'SYSTEM','STRING[8]','System',
   'SKROT','STRING[10]','Skrot',
   'NAZWA','STRING[40]','Nazwa',
   'REF','INTEGER',,
   'SEL','INTEGER',
);
W_MODEL.cntx_psh();
W_MODEL.index('SKID_MBN');
W_MODEL.prefix(SKID_MBN.ref());
GR_SIK.index('SKROT'); GR_SIK.prefix(REF.GRUPA);
{? GR_SIK.first()
|| {!
   |? __SPR.SYSTEM:=GR_SIK.SYSTEM;
      __SPR.SKROT:=GR_SIK.SKROT;
      __SPR.NAZWA:=GR_SIK.NAZWA;
      __SPR.REF:=GR_SIK.ref();
      __SPR.SEL:={? W_MODEL.find_key(GR_SIK.SKROT,) || 2 ?};
      __SPR.add();
      GR_SIK.next()
   !}
?};
W_MODEL.cntx_pop();
_i1:=__SPR.index('?');
_i2:=__SPR.ndx_tmp('',1,'SEL',,0);
_o:=__SPR.mk_sel('Sprawozdania'@,'P',0,'tab_spr_model1',,,,,'U');
__SPR.win_fld(_o,,'SEL',,,3,,,,,,2,,"1","0","2");
__SPR.win_fld(_o,,'SYSTEM');
__SPR.win_fld(_o,,'SKROT',,,60);
_fml:="
{? __SPR.SEL<>2
|| GR_SIK.prefix();
   {? GR_SIK.seek(__SPR.REF,)
   || {? __SPR.SEL=1
      || __SPR.SEL:=0;
         __SPR.put()
      |? __SPR.SEL=0 & exec('spr_spr','!kon_spr_cmod')
      || __SPR.SEL:=1;
         __SPR.put()
      ?}
   || FUN.info('Brak sprawozdania.'@)
   ?}
?}
";
__SPR.win_act(_o,,'Formuła','Zaznacz'@@,,,_fml,,1,,,,'Z');
__SPR.win_act(_o,,'Formuła','Odznacz'@@,,,_fml,,1,,,,'O');
__SPR.win_act(_o,,'Rekord',,,,"
   _gray:={? __SPR.SEL=1 || 'Z' |? __SPR.SEL=0 || 'O' || 'OZ' ?};
   __SPR.actions_grayed(__SPR.win_sel('?'),_gray);
   _mark:={? __SPR.SEL=1 || 'O' |? __SPR.SEL=0 || 'Z' || '' ?};
   __SPR.actions(__SPR.win_sel('?'),,_mark,1);
   ~~
");
__SPR.win_act(_o,,'Formuła','Kontynuuj'@@,,,"sel_exit",,,,,,'K');
__SPR.win_btn(_o,'text='+'Zaznacz'@+',btn_label_align=center,panel=right,align=begin','menu:Z');
__SPR.win_btn(_o,'text='+'Odznacz'@+',btn_label_align=center,panel=right,align=begin','menu:O');
__SPR.win_btn(_o,'text='+'Kontynułuj'@+',btn_label_align=center,panel=bottom,align=end','menu:K');
__SPR.win_sel(_o);
{? __SPR.select()
|| __SPR.index(_i2); __SPR.prefix(1);
   {? __SPR.first()
   || {!
      |? {? GR_SIK.seek(__SPR.REF,)
         || W_MODEL.SKID_MBN:=SKID_MBN.ref();
            W_MODEL.GR_SIK:=GR_SIK.ref();
            W_MODEL.add()
         ?};
         __SPR.next()
      !}
   ?}

?}


\spr_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Sprawdzenie czy mozna utworzyc model na podstawie sprawozdania
::  OLD: \spr_spr/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TTErr');
TTErr:=tab_tmp(4,
   'T','STRING[8]','Rodzaj',
   'I','INTEGER','Typ',
   'E','STRING[15]','Element',
   'W','STRING[15]',,
   'O','STRING[100]','Opis'
);
_fun:="
   {? ~TTErr.find_key(_a,_b,_c,_d,)
   || TTErr.T:=_a;
      TTErr.I:=_b;
      TTErr.E:=_c;
      TTErr.W:=_d;
      TTErr.O:=
      {? _b=1 || 'Brak wypełnionego pola UID'
      |? _b=2 || 'Wiersz użyty w algorytmach wierszy bieżącego sprawozdania'
      |? _b=3 || 'Wiersz korzysta z wierszy innych sprawozdań'
      ?};
      TTErr.add()
   ?}
";
{? GR_SIK.UID=''
|| _fun('Uwaga',1,'Sprawozdanie',GR_SIK.SKROT)
?};
DEFW.cntx_psh();
DEFW.index('LP'); DEFW.prefix(GR_SIK.ref());
{? DEFW.first()
|| {!
   |? {? DEFW.UID=''
      || _fun('Uwaga',1,'Wiersz',DEFW.KOD)
      ?};
      DEFA.index('SKL_SPR'); DEFA.prefix(GR_SIK.ref(),DEFW.ref());
      {? DEFA.first()
      || _ref:=DEFA.WYR;
         {? DEFA.last()
         || {? DEFA.WYR<>_ref
            || _fun('Uwaga',2,'Wiersz',DEFW.KOD)
            ?}
         ?}
      ?};
      DEFW.cntx_psh();
      DEFA.index('WIERSZ'); DEFA.prefix(DEFW.ref());
      {? DEFA.first()
      || {!
         |? {? DEFA.ARGUMENT().GRUPA<>GR_SIK.ref()
            || _fun('Inny',3,'Wiersz',DEFW.KOD)
            ?};
            DEFA.next()
         !}
      ?};
      DEFW.cntx_pop();
      DEFW.next()
   !}
?};
{? TTErr.first()
|| _o:=TTErr.mk_sel('Informacje'@,,1);
   TTErr.win_sel(_o);
   _pyt:=
   {? TTErr.find_key('Uwaga')
   || FUN.ask('Nie można dodać sprawozdania.\nWyświetlić dodatkowe informacje?'@)
   || FUN.ask('Sprawozdanie odwołujące się do innych sprawozdań.\nWyświetlić dodatkowe informacje?'@)
   ?};
   {? _pyt
   || TTErr.select()
   ?}
?};
DEFW.cntx_pop();
_ok:=~TTErr.find_key('Uwaga');
VAR_DEL.delete('TTErr');
_ok


\bd_w_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Tworzenie modelu danych na podstawie definicji sprawozdan
::  OLD: \bd_w_model/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć sprawozdanie z definicji modelu?'@)
|| W_MODEL.del()
?}


\gen_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Tworzenie modelu danych na podstawie definicji sprawozdan
::  OLD: \gen_model/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
W_MODEL.index('SKID_MBN'); W_MODEL.prefix(SKID_MBN.ref());
{? W_MODEL.first()
|| UD_TYP.index('SYMBOL'); UD_TYP.prefix();
   UD_SCH.index('SYMBOL'); UD_SCH.prefix();
   typ_spr:=sch_spr:=typ_kol:=sch_kol:=typ_fir:=sch_fir:=null;
   {? UD_TYP.find_key('SPR_PB',)
   || typ_spr:=UD_TYP.ref();
      {? UD_SCH.find_key(UD_TYP.ref(),SKID_MBN.KOD,)
      || sch_spr:=UD_SCH.ref()
      || UD_SCH.blank(1);
         UD_SCH.UD_TYP:=UD_TYP.ref();
         UD_SCH.SYMBOL:=SKID_MBN.KOD;
         UD_SCH.OPIS:=SKID_MBN.NAZ;
         UD_SCH.SYSTEM:='T';
         {? UD_SCH.add()
         || sch_spr:=UD_SCH.ref()
         ?}
      ?}
   ?};
   {? UD_TYP.find_key('SPR_KOL',)
   || typ_kol:=UD_TYP.ref();
      {? UD_SCH.find_key(UD_TYP.ref(),SKID_MBN.KOD,)
      || sch_kol:=UD_SCH.ref()
      || UD_SCH.blank(1);
         UD_SCH.UD_TYP:=UD_TYP.ref();
         UD_SCH.SYMBOL:=SKID_MBN.KOD;
         UD_SCH.OPIS:=SKID_MBN.NAZ;
         UD_SCH.SYSTEM:='T';
         {? UD_SCH.add()
         || sch_kol:=UD_SCH.ref()
         ?}
      ?}
   ?};
   {? UD_TYP.find_key('SPR_FIR',)
   || typ_fir:=UD_TYP.ref();
      {? UD_SCH.find_key(UD_TYP.ref(),SKID_MBN.KOD,)
      || sch_fir:=UD_SCH.ref()
      || UD_SCH.blank(1);
         UD_SCH.UD_TYP:=UD_TYP.ref();
         UD_SCH.SYMBOL:=SKID_MBN.KOD;
         UD_SCH.OPIS:=SKID_MBN.NAZ;
         UD_SCH.SYSTEM:='T';
         {? UD_SCH.add()
         || sch_fir:=UD_SCH.ref()
         ?}
      ?}
   ?};
   {? UD_TYP.find_key('SPR_TYP',)
   || typ_typ:=UD_TYP.ref();
      {? UD_SCH.find_key(UD_TYP.ref(),'SPR_TYP',)
      || sch_typ:=UD_SCH.ref()
      ?}
   ?};
   {? UD_TYP.find_key('SPR_FWYL',)
   || typ_fwyl:=UD_TYP.ref();
      {? UD_SCH.find_key(UD_TYP.ref(),'SPR_FWYL',)
      || sch_fwyl:=UD_SCH.ref()
      ?}
   ?};
   {? typ_spr & sch_spr & typ_kol & sch_kol & typ_fir & sch_fir & typ_typ & sch_typ & typ_fwyl & sch_fwyl
   || SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
      {? ~SKID_MBP.find_key(1)
      || SKID_MBP.SKID_MBN:=SKID_MBN.ref();
         SKID_MBP.LP:=1;
         SKID_MBP.NAZ:='Sprawozdania i wiersze';
         SKID_MBP.UD_SCH:=sch_spr;
         SKID_MBP.add()
      ?};
      {? ~SKID_MBP.find_key(2)
      || SKID_MBP.SKID_MBN:=SKID_MBN.ref();
         SKID_MBP.LP:=2;
         SKID_MBP.NAZ:='Kolumny sprawozdań';
         SKID_MBP.UD_SCH:=sch_kol;
         SKID_MBP.add()
      ?};
      {? ~SKID_MBP.find_key(3)
      || SKID_MBP.SKID_MBN:=SKID_MBN.ref();
         SKID_MBP.LP:=3;
         SKID_MBP.NAZ:='Firmy';
         SKID_MBP.UD_SCH:=sch_fir;
         SKID_MBP.add()
      ?};
      {? ~SKID_MBP.find_key(4)
      || SKID_MBP.SKID_MBN:=SKID_MBN.ref();
         SKID_MBP.LP:=4;
         SKID_MBP.NAZ:='Typ wartości';
         SKID_MBP.UD_SCH:=sch_typ;
         SKID_MBP.add()
      ?};
      {? ~SKID_MBP.find_key(5)
      || SKID_MBP.SKID_MBN:=SKID_MBN.ref();
         SKID_MBP.LP:=5;
         SKID_MBP.NAZ:='Firma wyłączeniowa';
         SKID_MBP.UD_SCH:=sch_fwyl;
         SKID_MBP.add()
      ?};
      {!
      |? {? exec('add_ud_skl','!kon_spr_cmod',typ_spr,W_MODEL.GR_SIK().UID,GR_SIK.NAZWA) &
            exec('add_ud_def','!kon_spr_cmod',sch_spr,UD_SKL.ref(),0,'~')
         || _tree_spr:=UD_DEF.ref();
            DEFW.index('LP'); DEFW.prefix(W_MODEL.GR_SIK);
            {? DEFW.first()
            || {!
               |? DEFA.index('SKLADNIK'); DEFA.prefix(DEFW.ref());
                  {? DEFA.first()
                  || _ok:=1;
                     DEFW.cntx_psh();
                     {!
                     |? {? DEFA.WYR().GRUPA=GR_SIK.ref() || _ok:=0 ?};
                        _ok & DEFA.next()
                     !};
                     DEFW.cntx_pop()
                  || _ok:=DEFW.DEFW_NAD=0
                  ?};
                  {? _ok & DEFW.ALGORYTM<>'P'
                  || {? exec('add_ud_skl','!kon_spr_cmod',typ_spr,DEFW.UID,DEFW.NAZWA,W_MODEL.GR_SIK,DEFW.ref(),null) &
                        exec('add_ud_def','!kon_spr_cmod',sch_spr,UD_SKL.ref(),_tree_spr,'+')
                     || exec('gen_model1','!kon_spr_cmod',UD_DEF.ref())
                     ?}
                  ?};
                  DEFW.next()
               !}
            ?};
            {? DEFW.first()
            || {!
               |? {? DEFW.DEFW_NAD
                  || _tree_spr:=null;
                     DEFW.cntx_psh();
                     DEFW.prefix();
                     {? DEFW.seek(DEFW.DEFW_NAD,)
                     || W_UD_SPR.cntx_psh();
                        W_UD_SPR.index('DEFW'); W_UD_SPR.prefix(DEFW.GRUPA,DEFW.ref());
                        {? W_UD_SPR.first()
                        || UD_DEF.cntx_psh();
                           UD_DEF.index('PODTEC'); UD_DEF.prefix(sch_spr,W_UD_SPR.UD_SKL);
                           {? UD_DEF.first()
                           || _tree_spr:=UD_DEF.ref()
                           ?};
                           UD_DEF.cntx_pop()
                        ?};
                        W_UD_SPR.cntx_pop()
                     ?};
                     DEFW.cntx_pop();
                     {? _tree_spr
                     || {? exec('add_ud_skl','!kon_spr_cmod',typ_spr,DEFW.UID,DEFW.NAZWA,W_MODEL.GR_SIK,DEFW.ref(),null)
                        || exec('add_ud_def','!kon_spr_cmod',sch_spr,UD_SKL.ref(),_tree_spr,'~')
                        ?}
                     ?}
                  ?};
                  DEFW.next()
               !}
            ?}
         ?};
         {? exec('add_ud_skl','!kon_spr_cmod',typ_kol,W_MODEL.GR_SIK().UID,GR_SIK.NAZWA) &
            exec('add_ud_def','!kon_spr_cmod',sch_kol,UD_SKL.ref(),0)
         || _tree_kol:=UD_DEF.ref();
            GR_KOL.index('LP2'); GR_KOL.prefix(W_MODEL.GR_SIK,'F');
            {? GR_KOL.first()
            || {!
               |? {? exec('add_ud_skl','!kon_spr_cmod',typ_kol,GR_SIK.UID+$GR_KOL.LP,GR_KOL.NAZWA,W_MODEL.GR_SIK,null,GR_KOL.ref())
                  || exec('add_ud_def','!kon_spr_cmod',sch_kol,UD_SKL.ref(),_tree_kol)
                  ?};
                  GR_KOL.next()
               !}
            ?}
         ?};
         W_MODEL.next()
      !};
      W_STR.index('TREE'); W_STR.prefix(SKID_MBN.W_SCH,0);
      {? W_STR.first()
      || {!
         |? {? exec('add_ud_skl','!kon_spr_cmod',typ_fir,W_STR.FIRMA().SYMBOL,FIRMA.OPIS) &
               exec('add_ud_def','!kon_spr_cmod',sch_fir,UD_SKL.ref(),0)
            || exec('gen_model2','!kon_spr_cmod',UD_DEF.ref())
            ?};
            W_STR.next()
         !}
      ?}
   || FUN.info('Nie znaleziono schematów danych.'@)
   ?}
|| FUN.info('Nie wybrano sprawozdań.'@)
?};
VAR_DEL.delete('typ_spr','sch_spr','typ_kol','sch_kol','typ_fir','sch_spr')


\add_ud_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie elementu schematu danych do modelu wg sprawozdan
::   WE: _a  - wskazanie na UD_TYP
::       _b  - symbol
::       _c  - opis
::      [_d] - sprawozdanie
::      [_e] - wiersz
::      [_f] - kolumna
::  OLD: \add_ud_skl/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? _>3
|| {? _f<>null
   || W_UD_SPR.index('DEFW'); W_UD_SPR.prefix();
      {? W_UD_SPR.find_key(_d,_f)
      || UD_SKL.cntx_psh(); UD_SKL.prefix();
         W_UD_SPR.UD_SKL();
         UD_SKL.SYMBOL:=_b;
         UD_SKL.OPIS:=_c;
         UD_SKL.put();
         UD_SKL.cntx_pop();
         _ok:=0
      ?}
   || W_UD_SPR.index('GR_KOL'); W_UD_SPR.prefix();
      {? W_UD_SPR.find_key(_d,_e)
      || UD_SKL.cntx_psh(); UD_SKL.prefix();
         W_UD_SPR.UD_SKL();
         UD_SKL.SYMBOL:=_b;
         UD_SKL.OPIS:=_c;
         UD_SKL.put();
         UD_SKL.cntx_pop();
         _ok:=0
      ?}
   ?}
?};
{? _ok
|| UD_SKL.index('SYMBOL'); UD_SKL.prefix(_a);
   {? ~UD_SKL.find_key(_b,)
   || UD_SKL.blank(1);
      UD_SKL.AKTYWNY:='T';
      UD_SKL.UD_TYP:=_a;
      UD_SKL.SYMBOL:=_b;
      UD_SKL.OPIS:=_c;
      {? UD_SKL.add()
      || {? _>3
         || W_UD_SPR.GR_SIK:=_d;
            W_UD_SPR.DEFW:=_e;
            W_UD_SPR.GR_KOL:=_f;
            W_UD_SPR.UD_SKL:=UD_SKL.ref();
            W_UD_SPR.add()
         ?};
         1
      ?}
   |? _>3
   || W_UD_SPR.prefix();
      W_UD_SPR.GR_SIK:=_d;
      W_UD_SPR.DEFW:=_e;
      W_UD_SPR.GR_KOL:=_f;
      W_UD_SPR.UD_SKL:=UD_SKL.ref();
      W_UD_SPR.add()
   || 1
   ?}
|| 1
?}


\add_ud_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie elementu schematu danych do modelu wg sprawozdan
::   WE: _a - wskazanie na UD_SCH
::       _b - wskazanie na UD_SKL
::       _c - rodzic (UD_DEF)
::       _d - znak operacji uzywany przy agregacji
::  OLD: \add_ud_def/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.index('PODTEC'); UD_DEF.prefix(_a);
{? ~UD_DEF.find_key(_b)
|| UD_SKL.cntx_psh();
   UD_SKL.prefix();
   _ok:=0;
   {? UD_SKL.seek(_b)
   || UD_DEF.prefix();
      UD_DEF.blank(1);
      UD_DEF.UD_SCH:=_a;
      UD_DEF.UD_SKL:=_b;
      UD_DEF.SYMBOL:=UD_DEF.UD_SKL().SYMBOL;
      UD_DEF.OPIS:=UD_DEF.UD_SKL().OPIS;
      UD_DEF.UD_DEF:=_c;
      {? _a=sch_spr
      || UD_DEF.ZN_AGR:=_d
      ?};
      _ok:=UD_DEF.add()
   ?};
   UD_SKL.cntx_pop();
   _ok
|| {? _a=sch_spr
   || UD_DEF.ZN_AGR:=_d;
      UD_DEF.put()
   ?};
   1
?}


\gen_model1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Tworzenie modelu danych na podstawie definicji sprawozdan
::   WE:  _a  - rodzic (UD_DEF.ref())
::       [_b] - LP argumentu kolumny
::  OLD: \gen_model1/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
DEFW.cntx_psh(); DEFA.cntx_psh();
DEFA.index('WIERSZ');
{? _=1
|| DEFA.prefix(DEFW.ref())
|| DEFA.prefix(DEFW.ref(),_b)
?};
_zn:=
{? DEFW.ALGORYTM='I'
|| '*'
|| '+'
?};
{? DEFA.first()
|| {!
   |? {? DEFA.ARGUMENT().GRUPA=GR_SIK.ref()
      || {? exec('add_ud_skl','!kon_spr_cmod',typ_spr,DEFA.ARGUMENT().UID,DEFW.NAZWA,W_MODEL.GR_SIK,DEFW.ref(),null) &
            exec('add_ud_def','!kon_spr_cmod',sch_spr,UD_SKL.ref(),_a,{? _zn='+' || {? DEFA.WSP<0 || '-' || '+' ?} || _zn ?})
         || exec('gen_model1','!kon_spr_cmod',UD_DEF.ref(),DEFA.ARG_KOL().LP)
         ?}
      ?};
      DEFA.next()
   !}
?};
DEFW.cntx_pop(); DEFA.cntx_pop()


\gen_model2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Tworzenie modelu danych na podstawie definicji sprawozdan
::   WE:  _a  - rodzic (UD_DEF.ref())
::  OLD: \gen_model2/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
W_STR.cntx_psh();
W_STR.index('TREE'); W_STR.prefix(SKID_MBN.W_SCH,W_STR.ref());
{? W_STR.first()
|| _ref:=UD_DEF.ref();
   {!
   |? {? exec('add_ud_skl','!kon_spr_cmod',typ_fir,W_STR.FIRMA().SYMBOL,FIRMA.OPIS) &
         exec('add_ud_def','!kon_spr_cmod',sch_fir,UD_SKL.ref(),_a)
      || exec('gen_model2','!kon_spr_cmod',UD_DEF.ref())
      ?};
      W_STR.next()
   !}
?};
W_STR.cntx_pop()

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:45 b9c1d8278acd5a0af5c5e7f1c4776e500dc7c6b364f1c697a26e191cb5ca9e17b7401db39c9e60bead8c78a1a263ae8eb55cb04db7a69bd2f9265aec99dd1b0b9103b9a2c7086f8397d08482f9264f50720a5a42a9d5cbda76d6daba446b8cff020c97ab2a2361232aee2cc92a107a810c2c0570d053aef683317dba5e5daf26

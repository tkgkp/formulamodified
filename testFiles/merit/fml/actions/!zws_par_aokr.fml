:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_aokr.fml [17.00]
:: Utworzony: 2015/10/19
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności rejestracji okresow
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rejestracja okresów - formula główna
::   WE: _a - [obj_new] - parametry wejściowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
exec('init','!zws_par_aokr');
exec('zws_par_aokr_g','!zws_par_aokr');
VAR_DEL.delete('__LIC','lic_ind1');
~~


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła powołująca obiekty i ustawiająca wartości zmiennych
::----------------------------------------------------------------------------------------------------------------------
exec('lic','zam_okr_rok');
exec('rapobsz','!zws_par_aokr');
exec('ini_rokf','okresy');
1


\zws_par_aokr_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności ZWS_PAR_AOKR
::----------------------------------------------------------------------------------------------------------------------
rok_ospr:=null;
_firma:=REF.FIRMA; _sfirma:=REF.S_FIRMA;
REF.FIRMA:=REF.S_FIRMA;
sfirmatm:=REF.S_FIRMA; firmatm:=REF.FIRMA;
firma0:=(REF.S_FIRMA().SYMBOL='000');
exec('ustal_licencje','!zws_par_aokr');
usrtab:=exec('usr_tab','!zws_par_aokr');
usrwin:=usrtab.mk_sel('Użytkownicy blokowani warunkowo'@,,,,1,,,,'U');
usrtab.win_fld(usrwin,,'LOGIN',,,10,,,'Login'@);
usrtab.win_fld(usrwin,,'DANE',,,10,,,'Dane użytkownika'@);
usrtab.win_fld(usrwin,,'ZAM_WAR',,,3,,,'Zablokowany'@,,,2,,"'T'","'N'");
usrtab.win_fld(usrwin,,'OST_ZAM',,,10,,,'Ostatnie zablokowanie'@);
::usrtab.win_fld(usrwin,,'OST_OTW',,,10,,,'Ostatnie otwarcie'@);
usrtab.win_act(usrwin,0,'Formuła','Zablokuj warunkowo użyt.'@@,,'Zablokuj warunkowo użytkownika'@,"exec('zabl','!zws_par_aokr')",,,1,,,'Z');
task_attach('FKS_DKS_BOKR');
usrtab.win_act(usrwin,0,'Formuła','Odblokuj warunkowo użyt.'@@,,'Odblokuj warunkowo użytkownika'@,"exec('odbl','!zws_par_aokr')",,,1,,,'O');
task_attach('FKS_DKS_BOKR');
usrtab.win_act(usrwin,,'Kolejność');

OKRO_F.cntx_psh(); OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(); OKRO_F.win_edit('POP');
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); ROK_F.first(); ROK_F.win_edit('POP1');
OSPR.cntx_psh(); OSPR.prefix(); OSPR.win_edit('REDI');

firmaw1:=FIRMA.mk_sel('Firmy'@,'P',,'firmawer',,,,,'U');
FIRMA.win_fld(firmaw1,,'SYMBOL',,,6,,,'Symbol'@,,'Symbol firmy'@);
FIRMA.win_fld(firmaw1,,'OPIS',,,146,,,'Opis'@,,'Opis firmy'@);

rokf1:=ROK_F.mk_sel('Lata bilansowe %1'@[{? firma0 || ' grupy' || '' ?}],'P',,'okrowerrok',,,,,'U');
ROK_F.win_fld(rokf1,,'NAZ',,,20,,,'Nazwa'@,,'Nazwa roku obrachunkowego'@);
ROK_F.win_fld(rokf1,,'LOBR',,,7,,,'Liczba okresów'@,,'Liczba okresów w roku obrachunkowym'@);
ROK_F.win_fld(rokf1,,'SYNT',,,9,,,'Długość syntetyki'@,,'Długość syntetyki konta w roku obrachunkowym'@);
ROK_F.win_fld(rokf1,,'SEP',,,4,,,'Separator'@,,'Znak separatora w roku obrachunkowym (znak "," to brak sep.)'@);
ROK_F.win_fld(rokf1,ROZNE,'ROZL_VAT',,,10,,,'Rozliczenie VAT'@,,'Sposób rozliczania VAT'@);
ROK_F.win_fld(rokf1,,'AKT_ROZ',,,4,,,'Rozrachunki'@,,'Znacznik, czy aktualizować rozrachunki w roku bilansowym'@,2,,"'T'","'N'");
ROK_F.win_fld(rokf1,,'ZAM',,,4,,,'Zamknięty'@,,'Znacznik, czy rok zamknięty'@,2,,"'T'","'N'");
ROK_F.win_fld(rokf1,UNPAR,'P10',,,3,,,' '@,,'Plan kont taki jak grupy'@);
ROK_F.win_act(rokf1,0,'Formuła','Popraw'@@,,'Poprawianie danych roku'@,"exec('pp_rok','okresy')",,,,,,'P');
ROK_F.win_act(rokf1,0,'Formuła','Zmień rozliczenie VAT'@@,,'Zmiana trybu rozliczania VAT'@,
              "exec('zm_rozl_vat','fks_ved')",,,,,,'Z');
{? firma0
|| ROK_F.win_act(rokf1,,'Formuła','Przypis&anie okresów firm do grupy'@@,,'Automatyczne powiązania okresów'@,
                 ,"exec('okro_f_g_automa','konsolidacja')",,,,,'A')
?};
::OKRO.grp_sel(_okn_grp,OSPR,ospr1,,,78,,,"OKRO_F.cntx_psh()","OKRO_F.cntx_pop()",,,'maximized_with_title')
ROK_F.win_act(rokf1,,'Formuła','Okresy raportowania'@@,,'Okresy raportowania'@,
                 ,"OKRO_F.cntx_psh(); OSPR.win_sel(ospr1); OSPR.select(); OKRO_F.cntx_pop()",,,,,'O');
ROK_F.win_act(rokf1,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,,
              "exec('legenda','color','$Zamknięty rok bilansowy','ROK_F#KS')",,,,,'L');
ROK_F.win_fml(rokf1,UNPAR,'P10',,'ICON_BEFORE',"{? ROK_F.PLAN_GR='T' || 'xwin16.png:182' || '' ?}");
ROK_F.win_act(rokf1,0,'Rekord',,,,
              "ROZNE.ROZL_VAT:={? ROK_F.ROZL_VAT='M' || 'miesięczne' || 'kwartalne' ?};
               {? ROK_F.ZAM='T' || exec('findtmp','#color') || '' ?}
              ");

{? firma0
|| rokf2:=ROK_F.mk_sel('Lata bilansowe firmy'@,'P',,'okrowerrok',,,,,'U');
   ROK_F.win_fld(rokf2,,'NAZ',,,20,,,'Nazwa'@,,'Nazwa roku obrachunkowego'@);
   ROK_F.win_fld(rokf2,,'LOBR',,,7,,,'Liczba okresów'@,,'Liczba okresów w roku obrachunkowym'@);
   ROK_F.win_fld(rokf2,,'SYNT',,,9,,,'Długość syntetyki'@,,'Długość syntetyki konta w roku obrachunkowym'@);
   ROK_F.win_fld(rokf2,,'SEP',,,4,,,'Separator'@,,'Znak separatora w roku obrachunkowym (znak "," to brak sep.)'@);
   ROK_F.win_fld(rokf2,,'ZAM',,,4,,,'Zamknięty'@,,'Znacznik, czy rok zamknięty'@,2,,"'T'","'N'");
   ROK_F.win_fld(rokf2,UNPAR,'P10',,,3,,,' '@,,'Plan kont taki jak grupy'@);
   ROK_F.win_act(rokf2,,'Formuła','Popraw'@@,,'Zmiana parametrów roku',,"exec('pop_rok','!zws_par_aokr')",1);
   ROK_F.win_act(rokf2,,'Formuła','Dołącz'@@,,'Dołączanie lat firmy wyłączeniowej'@,,
                 "exec('add_rok_firmy_w','konsolidacja')");
   ROK_F.win_act(rokf2,1,'Formuła','Dołącz'@@,,'Dołączanie lat firmy wyłączeniowej'@,,
                 "exec('add_rok_firmy_w','konsolidacja')",1);
   ROK_F.win_act(rokf2,,'Formuła','Usuń'@@,,'Usuwanie lat firmy wyłączeniowej'@,,
                 "exec('del_rok_firmy_w','!zws_par_aokr')");
   ROK_F.win_act(rokf2,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,,
                 "exec('legenda','color','$Zamknięty rok bilansowy','ROK_F#KS')");
   ROK_F.win_fml(rokf2,UNPAR,'P10',,'ICON_BEFORE',"{? ROK_F.PLAN_GR='T' || 'xwin16.png:182' || '' ?}");
   ROK_F.win_act(rokf2,0,'Rekord',,,,"{? ROK_F.ZAM='T' || exec('findtmp','#color') || '' ?} ")
?};


okro1:=OKRO_F.mk_sel('Okresy obrachunkowe %1'@[{? firma0 || ' grupy' || '' ?}],,,'okrowerokr',,,,,'U');
OKRO_F.win_fld(okro1,,'NR',,,3,,,'Nr'@,,'Kolejny numer okresu'@);
OKRO_F.win_fld(okro1,,'NAZ',,,{? firma0 || 30 || 16 ?},,,'Nazwa'@,,'Nazwa okresu obrachunkowego'@);
OKRO_F.win_fld(okro1,,'POCZ',,,10,,,'Początek'@,,'Data początku okresu obrachunkowego'@);
OKRO_F.win_fld(okro1,,'KON',,,10,,,'Koniec'@,,'Data końca okresu obrachunkowego'@);
OKRO_F.win_fld(okro1,,'KWARTAL',,,4,,,'Kwartał'@,,'Numer kwartału (1-4), którego dotyczy dany okres'@);
OKRO_F.win_fld(okro1,,'POLROCZE',,,4,,,'Półrocze'@,,'Numer półrocza (1 lub 2) którego dotyczy dany okres'@);
OKRO_F.win_fld(okro1,,'OES',,,4,,,'Okres pod.'@,,'Numer okresu podatkowego np. dla środków trwałych'@);
OKRO_F.win_fld(okro1,,'RES',,,4,,,'Rok pod.'@,,'Numer okresu podatkowego np. dla środków trwałych'@);
{? ~firma0
|| OKRO_F.win_fld(okro1,,'ZAM',,,4,,,'Zam. fin.'@,,'Znacznik, czy okres zamknięty w obszarze Finanse'@,2,,"'T'","'N'");
   OKRO_F.win_fld(okro1,,'AMOR',,,4,,,'Zam. śro.'@,,'Znacznik, czy okres zamknięty w obszarze Środki trwałe'@,2,,"'T'","'N'");
   OKRO_F.win_fld(okro1,,'ZAM_CON',,,4,,,'Zam. con.'@,,'Znacznik, czy okres zamknięty w obszarze Controlling'@,2,,"'T'","'N'");
   OKRO_F.win_fld(okro1,,'ZAM_WAR',,,4,,,'Zablokowany'@,,'Znacznik, czy okres zamknięty warunkowo'@,2,,"'T'","'N'")
?};
OKRO_F.win_act(okro1,,'Formuła','Popraw'@@,,'Zmiana parametrów okresu',,"exec('pop_okrof','!zws_par_aokr')",1,,,,'P');
OKRO_F.win_act(okro1,,'Menu','Wielojęzyczność'@);
OKRO_F.win_act(okro1,,'Formuła','Ustaw język'@@,'Wielojęzyczność'@,'Ustawia język tłumaczeń'@,"exec(\'set_lang\',\'lang\')",,,,,,'U');
OKRO_F.win_act(okro1,,'Formuła','Tłumacz'@@,'Wielojęzyczność'@,'Wyświetla tłumaczenia'@,"exec(\'sel_lang\',\'lang\',\'NAZ\')",,,,,,'T');

OKRO_F.win_act(okro1,,'Menu','Zamknij okres'@);
OKRO_F.win_act(okro1,,'Formuła','Finanse'@@,'Zamknij okres'@,'Zamknięcie okresu - Finanse'@,
               "exec(\'zam_okrf\',\'!fks_ksr_mokr\',2)",,,,,,'F');
task_attach('FKS_KSR_MOKR');
OKRO_F.win_act(okro1,,'Formuła','Środki &trwałe'@@,'Zamknij okres'@,'Zamknięcie okresu - Środki trwałe'@,
               "exec(\'m_close\',\'!fst_ewi_mokr\',2)",,,,,,'T');
task_attach('FST_EWI_MOKR');
OKRO_F.win_act(okro1,,'Formuła','Controlling'@@,'Zamknij okres'@,'Zamknięcie okresu - Controlling'@,
               "exec('akc_close','okresy',2)",,,,,,'C');
OKRO_F.win_act(okro1,,'Menu','Blokowanie warunkowe'@);
task_attach('FKS_DKS_BOKR');
OKRO_F.win_act(okro1,,'Formuła','Zablokuj okres'@@,'Blokowanie warunkowe'@,'Zablokowanie okresu'@,
               "exec(\'zabl_okro\',\'!zws_par_aokr\',2)",,,1,,,'Z');
OKRO_F.win_act(okro1,,'Formuła','Odblokuj okres'@@,'Blokowanie warunkowe'@,'Odblokowanie okresu'@,
               "exec(\'odbl_okro\',\'!zws_par_aokr\',2)",,,1,,,'O');
task_attach('CTR_PCO_MOKR');

OKRO_F.win_act(okro1,,'Menu','Otwórz okres'@);
OKRO_F.win_act(okro1,,'Formuła','Finanse'@@,'Otwórz okres'@,'Awaryjne otwarcie okresu - Finanse'@,
               "exec(\'fks_ksr_mokz_ob\',\'!fks_ksr_mokz\',1,0)",,,,,,'F');
task_attach('FKS_KSR_MOKZ');
OKRO_F.win_act(okro1,,'Formuła','Środki &trwałe'@@,'Otwórz okres'@,'Awaryjne otwarcie okresu - Środki trwałe'@,
               "exec(\'m_open\',\'!fst_ewi_mokz\',2)",,,,,,'T');
task_attach('FST_EWI_MOKZ');
OKRO_F.win_act(okro1,,'Formuła','Controlling'@@,'Otwórz okres'@,'Awaryjne otwarcie okresu - Controlling'@,
               "exec('akc_open','okresy',2)",,,,,,'C');
task_attach('CTR_PCO_MOKZ');

OKRO_F.win_act(okro1,,'Menu','Historia'@);
OKRO_F.win_act(okro1,,'Formuła','Finanse'@@,'Historia'@,'Historia zamknięć - Finanse'@,
               "exec(\'hist_zamkf\',\'!fks_ksr_mokz\',2,1)",,,,,,'F');
OKRO_F.win_act(okro1,,'Formuła','Środki &trwałe'@@,'Historia'@,'Historia zamknięć - Środki trwałe'@,
               "exec(\'hist_zamkt\',\'!fst_ewi_mokr\',2)",,,,,,'T');
task_attach('FST_EWI_MOKR');
task_attach('FST_EWI_MOKZ');
OKRO_F.win_act(okro1,,'Formuła','Controlling'@@,'Historia'@,'Historia zamknięć - Controlling'@,
               "exec(\'hist_zam\',\'!ctr_pco_mokr\',2)",,,,,,'C');
task_attach('CTR_PCO_MOKR');
task_attach('CTR_PCO_MOKZ');

OKRO_F.win_act(okro1,,'Menu','Zamknij &rok'@);
OKRO_F.win_act(okro1,,'Formuła','Finanse'@@,'Zamknij &rok'@,'Zamknięcie roku - Finanse'@,
               "exec(\'zam_rokf\',\'!fks_ksr_mrok\',2)",,,,,,'F');
task_attach('FKS_KSR_MROK');
OKRO_F.win_act(okro1,,'Menu','Historia zamknięć'@,'Zamknij &rok'@,'Historia zamknięć roku'@);
OKRO_F.win_act(okro1,,'Formuła','Finanse'@@,'Zamknij &rok'@+','+'Historia zamknięć'@,'Historia zamknięć - Finanse'@,
               "exec(\'hist_zamkf\',\'!fks_ksr_mokz\',2,2)",,,,,,'F');
OKRO_F.win_sel(okro1);
{? firma0
|| okro2:=OKRO_F.mk_sel('Okresy obrachunkowe firmy'@,,,'okrowerokr1',,,,,'U');
   OKRO_F.win_fld(okro2,,'NR',,,3,,,'Nr'@,,'Kolejny numer okresu'@);
   OKRO_F.win_fld(okro2,,'NAZ',,,16,,,'Nazwa'@,,'Nazwa okresu obrachunkowego'@);
   OKRO_F.win_fld(okro2,,'POCZ',,,10,,,'Początek'@,,'Data początku okresu obrachunkowego'@);
   OKRO_F.win_fld(okro2,,'KON',,,10,,,'Koniec'@,,'Data końca okresu obrachunkowego'@);
   OKRO_F.win_fld(okro2,,'KWARTAL',,,4,,,'Kwartał'@,,'Numer kwartału (1-4), którego dotyczy dany okres'@);
   OKRO_F.win_fld(okro2,,'POLROCZE',,,4,,,'Półrocze'@,,'Numer półrocza (1 lub 2) którego dotyczy dany okres'@);
   OKRO_F.win_fld(okro2,,'ZAM',,,4,,,'Zamknięty'@,,'Znacznik, czy rok zamknięty'@,2,,"'T'","'N'");
   OKRO_F.win_act(okro2,,'Formuła','Popraw'@@,,'Zmiana parametrów okresu'@,,"exec('pop_okrof','!zws_par_aokr')",1,,,,'P');
   OKRO_F.win_act(okro2,,'Menu','Wielojęzyczność'@);
   OKRO_F.win_act(okro2,,'Formuła','Ustaw język'@@,'Wielojęzyczność'@,'Ustawia język tłumaczeń'@,"exec(\'set_lang\',\'lang\')",,,,,,'U');
   OKRO_F.win_act(okro2,,'Formuła','Tłumacz'@@,'Wielojęzyczność'@,'Wyświetla tłumaczenia'@,"exec(\'set_lang\',\'lang\',\'NAZ\')",,,,,,'T');
   OKRO_F.win_sel(okro2)
?};

ospr1:=OSPR.mk_sel('Okresy raportowania %1'@[{? firma0 || ' grupy' || '' ?}],,,'okrowerospr',,,,,'U');
OSPR.win_fld(ospr1,,'OKRES_OD','NAZ',,20,,,'Okres od'@,,'Okres obrachunkowy rozpoczynający okres raportowania'@);
OSPR.win_fld(ospr1,,'OKRES_DO','NAZ',,20,,,'Okres do'@,,'Okres obrachunkowy kończący okres raportowania'@);
OSPR.win_fld(ospr1,SKID,'LANG_WER',,,41,,,'Nazwa okresu'@,,'Nazwa okresu'@);
OSPR.win_fld(ospr1,,'SKROT',,,6,,,'Skrót'@,,'Skrót nazwy okresu raportowania'@);
OSPR.win_fld(ospr1,OKRESY,'TYPOKR',,,8,,,'Typ'@,,'Typ okresu raportowania'@);
OSPR.win_fld(ospr1,,'ZAM',,,4,,,'Zamknięty'@,,'Znacznik, czy okres zamknięty'@,2,,"'T'","'N'");
OSPR.win_act(ospr1,0,'Rekord',,,,"exec('ospr_rok','!zws_par_aokr')");
OSPR.win_act(ospr1,,'Formuła','Popraw'@@,,'Poprawianie okresu raportowania'@,,"exec('pop_ospr','!zws_par_aokr')",1,,,,'P');
OSPR.win_act(ospr1,,'Formuła','Dołącz &inny'@@,,'Dołączanie innych okresów'@,,"exec('ospr_inn_add','!zws_par_aokr')",,,,,'I');
OSPR.win_act(ospr1,1,'Formuła','Dołącz &inny'@@,,'Dołączanie innych okresów'@,,"exec('ospr_inn_add','!zws_par_aokr')",1,,,,'I');
OSPR.win_act(ospr1,,'Formuła','Usuń'@@,,'Usuwanie okresów innych'@,,"exec('ospr_del','!zws_par_aokr')",,,,,'U');
OSPR.win_act(ospr1,,'Formuła','Zamknij'@@,,'Zamknięcie okresu sprawozdawczego'@,,"exec('zak_ospr','!zws_par_aokr',1)",,,,,'Z');
OSPR.win_act(ospr1,,'Formuła','Otwórz'@@,,'Otwarcie okresu sprawozdawczego'@,,"exec('zak_ospr','!zws_par_aokr',2)",,,,,'O');
OSPR.win_act(ospr1,,'Menu','Wielojęzyczność'@);
OSPR.win_act(ospr1,,'Formuła','Ustaw język'@@,'Wielojęzyczność'@,'Ustawia język tłumaczeń'@,"exec('set_lang','lang')",,,,,,'U');
OSPR.win_act(ospr1,,'Formuła','Tłumacz'@@,'Wielojęzyczność'@,'Wyświetla tłumaczenia'@,"exec('sel_lang','lang')",,,,,,'T');
OSPR.win_sel(ospr1);
_okn_grp:=OKRO.grp_make('Okresy'@,,'okro_sel');
OKRO.grp_sel(_okn_grp,OKRO,{? firma0 || 'WER0' || 'WER' ?},'Okresy'@,,,,,,"exec('bobs_okro','!zws_par_aokr')",,,'maximized');
OKRO.grp_sel(_okn_grp,ROK_F,rokf1,'Lata bilansowe %1'@[{? firma0 || ' grupy' || '' ?}],
             "exec('arfr_rokf','!zws_par_aokr')",,,,"exec('bobs_rok_f','!zws_par_aokr')","exec('aobs_rok_f','!zws_par_aokr')",,,'maximized_with_title');
{? firma0
|| OKRO.tab_splt(_okn_grp,'tab0','vertical','tab6');
   OKRO.grp_sel(_okn_grp,OSPR,ospr1,,,78,,,"OKRO_F.cntx_psh()","OKRO_F.cntx_pop()",,,'maximized_with_title');
   OKRO.tab_splt(_okn_grp,,'horizontal','tab2',15);
   OKRO.grp_sel(_okn_grp,OKRO_F,okro1,,"exec('awr_okrof','konsolidacja')",,,,,,,,'maximized_with_title');
   OKRO.tab_splt(_okn_grp,'tab2','vertical','tab3');
   OKRO.grp_sel(_okn_grp,OKRO_F_G,'WER',,,,,,"exec('bu_okro_f_g','konsolidacja')","exec('au_okro_f_g','konsolidacja')",,,'maximized_with_title');
   OKRO.grp_sel(_okn_grp,FIRMA,firmaw1,'[icon:xwin16.png:182]Lata bilansowe firm grupy'@,"exec('arfr_firmy_lata','!zws_par_aokr')",,,,
                "exec('bobs_firma','!zws_par_aokr')",,,,'maximized_with_title');
   OKRO.tab_splt(_okn_grp,,'horizontal','tab4',15);
   OKRO.grp_sel(_okn_grp,ROK_F,rokf2,,"exec('arfr_rokf1','!zws_par_aokr')",,,,"exec('bobs_rok_fir','!zws_par_aokr')",,,,'maximized_with_title');
   OKRO.tab_splt(_okn_grp,'tab4','vertical','tab5');
   OKRO.grp_sel(_okn_grp,OKRO_F,okro2,,,,,,,,,,'maximized_with_title')
|| OKRO.tab_splt(_okn_grp,,'horizontal','tab2',15);
   OKRO.grp_sel(_okn_grp,OKRO_F,okro1,,"
                exec('usr_tab_upd','!zws_par_aokr');
                grp_disp(usrtab,usrwin,1,1)",,,,,,,,'maximized_with_title');
   OKRO.tab_splt(_okn_grp,,'vertical','tab6',130);
   OKRO.grp_sel(_okn_grp,usrtab,usrwin,,,,,,"","",,,'maximized_with_title')
?};
OKRO.cntx_psh();
_def:=OKRO.actions('WER','T');
OKRO.index('UNIK'); OKRO.prefix(REF.FIRMA);
OKRO.win_sel(_okn_grp);
exec('ini_lang','lang');
OKRO.select();
OKRO.actions('WER',,_def);
OKRO.cntx_pop();

OKRO_F.cntx_pop(); ROK_F.cntx_pop(); OSPR.cntx_pop();
VAR_DEL.delete('lic_lmg','lic_lsp','lic_lzk','lic_fks','lic_obg','lic_kas','lic_ctr','lic_fst');
VAR_DEL.delete('o_lmg','o_lsp','o_lzk','o_fks','o_ctr','o_obg','o_obe','o_kas','o_fst');
REF.FIRMA:=_firma; REF.S_FIRMA:=_sfirma;
VAR_DEL.delete('firma0','rokf1','rokf2','okro1','ospr1','rok_ospr','usrwin','usrtab');
VAR_DEL.delete('firmatm','sfirmatm','firmaw1');
{? _ || _d.done() ?};
~~


\ospr_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rekord przed tabela OSPR
::----------------------------------------------------------------------------------------------------------------------
OKRESY.ROK1:=OSPR.OKRES_OD().ROK;
OKRESY.TYPOKR:={? OSPR.TYP='I'
               || 'Inny'
               |? OSPR.TYP='R'
               || 'Rok'
               |? OSPR.TYP='P'
               || 'Półrocze'
               |? OSPR.TYP='K'
               || 'Kwartał'
               |? OSPR.TYP='M'
               || 'Miesiąc'
               || ''
               ?};
exec('bv_lang','lang');
{? OSPR.TYP='I' || OSPR.actions(ospr1,'',,1) || OSPR.actions(ospr1,'U',,1) ?};
0


\pop_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie ROK_F
::----------------------------------------------------------------------------------------------------------------------
{? ROK_F.r_lock(1,1)
|| ROK_F.win_edit('POP2');
   {? ROK_F.edit("exec('spr_rok','!zws_par_aokr')") || ROK_F.put() ?};
   unlock_r()
|| FUN.info('Inny użytkownik obsługuje rok bilansowy.'@)
?}


\pop_okrof
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? OKRO_F.r_lock(1,1)
|| OKRO_F.win_edit('POP');
   {? OKRO_F.edit() || OKRO_F.put() ?};
   unlock_r()
|| FUN.info('Inny użytkownik obsługuje okres obrachunkowy.'@)
?}


\spr_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzanie ROK_F
::----------------------------------------------------------------------------------------------------------------------
_ref:=ROK_F.ref(); _zwrot:=1;
ROK_F.cntx_psh(); ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,ROK_F.NAZ,);
{? ROK_F.first() & ROK_F.ref()<>_ref
|| FUN.info('Istnieje już rok o podanej nazwie.'@); _zwrot:=0
?};
ROK_F.cntx_pop();
_zwrot


\pop_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie OSPR
::----------------------------------------------------------------------------------------------------------------------
{? OSPR.ZAM='T'
|| FUN.info('Okres jest zamknięty.'@); return(0)
?};
SLO.win_sel('ONE'); SLO.win_dict('ONE');
_rok:=OSPR.ROK().KOD;
WARTOSCI.cntx_psh(); WARTOSCI.use('wsik_'+_rok); WARTOSCI.index('OKRES'); WARTOSCI.prefix(OSPR.ref());
W_ALGPAR.cntx_psh(); W_ALGPAR.use('walg_'+_rok); W_ALGPAR.index('OSPR'); W_ALGPAR.prefix(OSPR.ref());
{? WARTOSCI.find_key(REF.WYKON) | W_ALGPAR.first()
|| FUN.info('Istnieją obliczenia dla tego okresu.\nModyfikacja niemożliwa.'@); 0
|? WARTOSCI.first()
|| FUN.info('Istnieją wartości wersji planów dla tego okresu.\nModyfikacja niemożliwa.'@); 0
|| OSPR.win_edit('REDI');
   {? OSPR.edit("exec('ospr_por','!zws_par_aokr')") & OSPR.put()
   || exec('upd_lang','lang')
   ?}
?};
WARTOSCI.cntx_pop(); W_ALGPAR.cntx_pop()


\ospr_inn_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie OSPR
::----------------------------------------------------------------------------------------------------------------------
OSPR.blank(); OSPR.TYP:='I'; OSPR.ROK:=rok_ospr;
OSPR.win_edit('REDI');
{? OSPR.edit("exec('ospr_por','!zws_par_aokr')") & OSPR.add()
|| exec('upd_lang','lang')
?}


\ospr_por
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzanie parametrów okresu
::----------------------------------------------------------------------------------------------------------------------
{? OSPR.ROK<>null
|| {? OSPR.OKRES_OD<>null & OSPR.OKRES_DO<>null
   || {? OSPR.OKRES_OD().NR>OSPR.OKRES_DO().NR
      || FUN.info('Nieprawidłowo zdefiniowany okres'@); 'OKRES_OD'
      |? OSPR.OKRES_OD().NR<OSPR.OKRES_DO().NR & OSPR.TYP='M'
      || FUN.info('Nieprawidłowo zdefiniowany okres'@); 'OKRES_OD'
      || {? OSPR.TYP='I'
         || OSPR.OKRES:=null;
            __CHK.record(OSPR,,'NAZWA','SKROT')
         |? OSPR.OKRES_OD=OSPR.OKRES_DO & OSPR.OKRES_OD().POCZ=date(0,0,0)
         || OSPR.OKRES:=null;
            __CHK.record(OSPR,,'NAZWA','SKROT')
         || __CHK.record(OSPR,,'NAZWA','OKRES','SKROT')
         ?}
      ?}
   || 'OKRES_OD'
   ?}
|| 'ROK'
?}


\bobs_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą firm
::----------------------------------------------------------------------------------------------------------------------
REF.S_FIRMA:=sfirmatm; REF.FIRMA:=firmatm;
FIRMA.index('SYMBOL'); FIRMA.prefix();
{? ~FIRMA.get() || FIRMA.first() ?}


\bobs_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą okresów
::----------------------------------------------------------------------------------------------------------------------
REF.S_FIRMA:=sfirmatm; REF.FIRMA:=firmatm; 1


\bobs_rok_fir
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą lat dla firm
::----------------------------------------------------------------------------------------------------------------------
ROK_F.index('ROKPOCZ');
{? FIRMA.get()
|| ROK_F.prefix(FIRMA.ref())
|| ROK_F.prefix(null)
?}; 1


\arfr_firmy_lata
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu firm
::----------------------------------------------------------------------------------------------------------------------
ROK_F.index('ROKPOCZ'); ROK_F.prefix(FIRMA.ref());
REF.S_FIRMA:=FIRMA.ref();
REF.FIRMA:=FIRMA.ref();
{? REF.S_FIRMA().TYP<>'W' | ~FIRMA.size()
|| ROK_F.actions(rokf2,'DU:D',,1)
|| ROK_F.actions(rokf2,,,1)
?};
grp_disp(ROK_F,rokf2);
exec('arfr_rokf1','!zws_par_aokr')


\bobs_rok_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą lat bilansowych
::----------------------------------------------------------------------------------------------------------------------
REF.S_FIRMA:=sfirmatm; REF.FIRMA:=firmatm;
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ~ROK_F.get() || ROK_F.first() ?}


\aobs_rok_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po obsłudze lat bilansowych
::----------------------------------------------------------------------------------------------------------------------
1


\arfr_rokf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu lat bilansowych
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.index('ROK'); rok_ospr:=ROK_F.ref();
{? ROK_F.get()
|| OKRO_F.prefix(ROK_F.ref())
|| OKRO_F.prefix(null)
?};
{? ~OKRO_F.get() || OKRO_F.first() ?};
grp_disp(OKRO_F,okro1,1,1);
OSPR.index('ROK_NAZ');
{? ROK_F.get()
|| OSPR.prefix(REF.FIRMA,ROK_F.ref())
|| OSPR.prefix(null,null)
?};
{? ~OSPR.get() || OSPR.first() ?};
exec('usr_tab_upd','!zws_par_aokr');
{? firma0
|| grp_disp(OSPR,ospr1);
   ROK_F.cntx_psh();
   OKRO_F_G.index('OKRO_F_G');
   {? OKRO_F.get()
   || OKRO_F_G.prefix(OKRO_F.ref())
   || OKRO_F_G.prefix(null)
   ?};
   {? ~OKRO_F_G.get() || OKRO_F_G.first() ?};
   grp_disp(OKRO_F_G,'WER');
   ROK_F.cntx_pop()
|| grp_disp(usrtab,usrwin,1,1)
?};
1


\arfr_rokf1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu lat bilansowych
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.index('ROK');
{? ROK_F.get()
|| OKRO_F.prefix(ROK_F.ref())
|| OKRO_F.prefix(null)
?};
{? ~OKRO_F.get() || OKRO_F.first() ?};
grp_disp(OKRO_F,okro1,1,1);
grp_disp(OKRO_F,okro2,1,1);
1


\del_rok_firmy_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie roku firmy wyłączeniowej
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
{? ROK_F.next()
|| FUN.info('Wybrany rok nie jest ostatni.'@)
|| delrok_f:=0;
   exec('czy_del_rok_fin','!zws_par_aokr',2);
   {? delrok_f
   || {? FUN.ask('Usunąć rok?'@)
      || rokf:=ROK_F.ref();
         exec('del_okr_fin1','!zws_par_aokr')
      ?}
   || FUN.info('Nie można usunąć roku - jest wykorzystywany w danych.'@)
   ?};
   VAR_DEL.delete('delrok_f','rokf')
?};
ROK_F.cntx_pop()


\okr_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Napełnia tabele OKRO i OKR_OBSZ po transferze (tylko po transferze!)
::       Formuła uruchamiana dla każdej firmy oddzielnie.
::----------------------------------------------------------------------------------------------------------------------
_fks:=exec('szuk_b_dom','parses','FKS');
_ctr:=exec('szuk_b_dom','parses','CTR');
_obg:=exec('szuk_b_dom','parses','OBG');
_lmg:=exec('szuk_b_dom','parses','LMG');
_lsp:=exec('szuk_b_dom','parses','LSP');
_lzk:=exec('szuk_b_dom','parses','LZK');
_fst:=exec('szuk_b_dom','parses','FST');
_kas:=exec('szuk_b_dom','parses','KAS');

OKR_OBSZ.cntx_psh();
:: FKS
:: sprawdza czy jest BO w pierwszym roku bo tylko tak mógł być fiks
OKR_OBSZ.index('UNIK1'); OKR_OBSZ.prefix(REF.FIRMA,_fks);
{? ~OKR_OBSZ.first()
|| OKRO_F.cntx_psh();
   OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
   {? OKRO_F.first()
   || ROK_F.cntx_psh(); ROK_F.prefix();
      DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2)); DOK.prefix();
      _ok:=DOK.first();
      ROK_F.cntx_pop();
      {? _ok
      || {? _fks
         || {! |?
               OKRO_F.cntx_psh();
               _pocz:=date(0,0,0);
               {? (OKRO_F.NAZ='Bilans otwarcia' & OKRO_F.next()) | (OKRO_F.NAZ='Bilans zamknięcia' & OKRO_F.prev()) |
                  OKRO_F.POCZ<>date(0,0,0)
               || _pocz:=OKRO_F.POCZ;
                   exec('rok_add','!zws_par_aokr',_pocz~1)
               ?};
               OKRO_F.cntx_pop();
               {? _pocz<>date(0,0,0)
               || exec('add_okr_obsz','!zws_par_aokr',_pocz,_fks,OKRO_F.ref(),null)
               ?};
               OKRO_F.next()
            !}
         ?}
      ?}
   ?};
   OKRO_F.cntx_pop()
?};

:: CTR
:: sprawdza czy jest K__NAG lub K_HARM
OKR_OBSZ.index('UNIK1'); OKR_OBSZ.prefix(REF.FIRMA,_ctr);
{? ~OKR_OBSZ.first()
|| _generuj:=0;
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   {? ROK_F.first()
   || {! |?
         {? ~_generuj
         || K__NAG.cntx_psh();
            K__NAG.index('ROK'); K__NAG.prefix(REF.FIRMA,ROK_F.ref());
            _generuj:=K__NAG.first();
            {? ~_generuj
            || K_HARM.cntx_psh();
               K_HARM.index('ROKPOCZ'); K_HARM.prefix(REF.FIRMA,ROK_F.ref());
               _generuj:=K_HARM.first();
               {? ~_generuj
               || K_HARM.index('ROKKON'); K_HARM.prefix(REF.FIRMA,ROK_F.ref());
                   _generuj:=K_HARM.first()
               ?};
               K_HARM.cntx_pop()
            ?};
            K__NAG.cntx_pop()
         ?};
         {? _generuj
         || OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
            {? OKRO_F.first()
            || {! |?
                  OKRO_F.cntx_psh();
                  _pocz:=date(0,0,0);
                  {? (OKRO_F.NAZ='Bilans otwarcia' & OKRO_F.next()) | (OKRO_F.NAZ='Bilans zamknięcia' & OKRO_F.prev()) |
                     OKRO_F.POCZ<>date(0,0,0)
                  || _pocz:=OKRO_F.POCZ;
                     exec('rok_add','!zws_par_aokr',_pocz~1)
                  ?};
                  OKRO_F.cntx_pop();
                  {? _pocz<>date(0,0,0)
                  || exec('add_okr_obsz','!zws_par_aokr',_pocz,_ctr,OKRO_F.ref(),null)
                  ?};
                  OKRO_F.next()
               !}
            ?};
            OKRO_F.cntx_pop()
         ?};
         ROK_F.next()
      !}
   ?};
   ROK_F.cntx_pop()
?};

:: OBG
:: sprawdza czy jest EDOKUM
OKR_OBSZ.index('UNIK1'); OKR_OBSZ.prefix(REF.FIRMA,_obg);
{? ~OKR_OBSZ.first()
|| _generuj:=0;
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   {? ROK_F.first()
   || {! |?
         {? ~_generuj
         || EDOKUM.cntx_psh();
            EDOKUM.use('skid_v'+ROK_F.KOD); EDOKUM.prefix();
            _generuj:=EDOKUM.first();
            EDOKUM.cntx_pop()
         ?};
         {? _generuj
         || OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
            {? OKRO_F.first()
            || {! |?
                  OKRO_F.cntx_psh();
                  _pocz:=date(0,0,0);
                  {? (OKRO_F.NAZ='Bilans otwarcia' & OKRO_F.next()) | (OKRO_F.NAZ='Bilans zamknięcia' & OKRO_F.prev()) |
                     OKRO_F.POCZ<>date(0,0,0)
                  || _pocz:=OKRO_F.POCZ;
                     exec('rok_add','!zws_par_aokr',_pocz~1)
                  ?};
                  OKRO_F.cntx_pop();
                  {? _pocz<>date(0,0,0)
                  || exec('add_okr_obsz','!zws_par_aokr',_pocz,_obg,OKRO_F.ref(),null)
                  ?};
                  OKRO_F.next()
               !}
            ?};
            OKRO_F.cntx_pop()
         ?};
         ROK_F.next()
      !}
   ?};
   ROK_F.cntx_pop()
?};

:: LMG, LSP, LZK
:: sprawdza czy jest OKR
OKR_OBSZ.index('UNIK1'); OKR_OBSZ.prefix(REF.FIRMA);
{? ~OKR_OBSZ.find_key(_lmg) & ~OKR_OBSZ.find_key(_lsp) & ~OKR_OBSZ.find_key(_lzk)
|| OKR.cntx_psh(); OKR.index('OKR'); OKR.prefix(REF.FIRMA);
   {? OKR.first()
   || {! |?
         exec('rok_add','!zws_par_aokr',OKR.ROK);
         exec('add_okr_obsz','!zws_par_aokr',date(OKR.ROK,OKR.MC,1),_lmg,null,OKR.ref());
         exec('add_okr_obsz','!zws_par_aokr',date(OKR.ROK,OKR.MC,1),_lsp,null,OKR.ref());
         exec('add_okr_obsz','!zws_par_aokr',date(OKR.ROK,OKR.MC,1),_lzk,null,OKR.ref());
         OKR.next()
      !}
   ?};
   OKR.cntx_pop()
?};

:: FST
:: sprawdza czy jest środek trwały
OKR_OBSZ.index('UNIK1'); OKR_OBSZ.prefix(REF.FIRMA,_fst);
{? ~OKR_OBSZ.first()
|| _generuj:=0;
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   {? ROK_F.first()
   || {! |?
         {? ~_generuj
         || {? SRSR.name()='srsr'+'r'+REF.FIRMA().SYMBOL
            || SRSR.cntx_psh();
               SRSR.prefix();
               _generuj:=SRSR.first();
               SRSR.cntx_pop()
            ?}
         ?};
         {? _generuj
         || OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
            {? OKRO_F.first()
            || {! |?
                  OKRO_F.cntx_psh();
                  _pocz:=date(0,0,0);
                  {? (OKRO_F.NAZ='Bilans otwarcia' & OKRO_F.next()) | (OKRO_F.NAZ='Bilans zamknięcia' & OKRO_F.prev()) |
                     OKRO_F.POCZ<>date(0,0,0)
                  || _pocz:=OKRO_F.POCZ;
                     exec('rok_add','!zws_par_aokr',_pocz~1)
                  ?};
                  OKRO_F.cntx_pop();
                  {? _pocz<>date(0,0,0)
                  || exec('add_okr_obsz','!zws_par_aokr',_pocz,_fst,OKRO_F.ref(),null)
                  ?};
                  OKRO_F.next()
               !}
            ?};
            OKRO_F.cntx_pop()
         ?};
         ROK_F.next()
      !}
   ?};
   ROK_F.cntx_pop()
?};


:: KAS
:: sprawdza czy są zapisy w tabeli RAPORT
OKR_OBSZ.index('UNIK1'); OKR_OBSZ.prefix(REF.FIRMA,_kas);
{? ~OKR_OBSZ.first()
|| _generuj:=0;
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   {? ROK_F.first()
   || {! |?
         {? ~_generuj
         || RAPORT.cntx_psh();
            STANKAS.cntx_psh();
            STANKAS.index('KOD'); STANKAS.prefix();
            {? STANKAS.first()
            || {! |?
                  RAPORT.use('krp'+form(STANKAS.KOD,-3)+(form(ROK_F.KOD)+2)); RAPORT.prefix();
                  _generuj:=RAPORT.first();
                  ~_generuj & STANKAS.next()
               !}
            ?};
            STANKAS.cntx_pop(); RAPORT.cntx_pop()
         ?};
         {? _generuj
         || OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
            {? OKRO_F.first()
            || {! |?
                  OKRO_F.cntx_psh();
                  _pocz:=date(0,0,0);
                  {? (OKRO_F.NAZ='Bilans otwarcia' & OKRO_F.next()) | (OKRO_F.NAZ='Bilans zamknięcia' & OKRO_F.prev()) |
                     OKRO_F.POCZ<>date(0,0,0)
                  || _pocz:=OKRO_F.POCZ;
                     exec('rok_add','!zws_par_aokr',_pocz~1)
                  ?};
                  OKRO_F.cntx_pop();
                  {? _pocz<>date(0,0,0)
                  || exec('add_okr_obsz','!zws_par_aokr',_pocz,_kas,OKRO_F.ref(),null)
                  ?};
                  OKRO_F.next()
               !}
            ?};
            OKRO_F.cntx_pop()
         ?};
         ROK_F.next()
      !}
   ?};
   ROK_F.cntx_pop()
?};

OKR_OBSZ.cntx_pop();
1


\add_okr_obsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodaje rekordy do tabeli OKR_OBSZ
::   WE: _a - data początku okresu
::       _b - ref obszaru (B_DOMAIN)
::       _c - ref OKRO_F
::       _d - ref OKR
::----------------------------------------------------------------------------------------------------------------------
_okro:=exec('szuk_okro','okresy',_a);
{? _okro
|| OKR_OBSZ.cntx_psh();
   OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix();
   _dalej:={? _c=null || ~OKR_OBSZ.find_key(_okro,_b) || ~OKR_OBSZ.find_key(_okro,_b,_c) ?};
   {? _dalej
   || OKR_OBSZ.blank(1);
      OKR_OBSZ.OKRO:=_okro;
      OKR_OBSZ.B_DOMAIN:=_b;
      OKR_OBSZ.OKRO_F:=_c;
      OKR_OBSZ.OKR:=_d;
      {? OKR_OBSZ.add(1)
      || {? OKR_OBSZ.OKRO_F<>null
         || OKR_OBSG.index('B_DOMAIN');
            OKR_OBSG.prefix(OKR_OBSZ.OKRO_F().ROK().FIRMA,OKR_OBSZ.B_DOMAIN,OKR_OBSZ.OKRO_F);
            {? ~OKR_OBSG.first()
            || OKR_OBSG.B_DOMAIN:=OKR_OBSZ.B_DOMAIN;
               OKR_OBSG.FIRMA:=OKR_OBSZ.OKRO_F().ROK().FIRMA;
               OKR_OBSG.OKRO_F:=OKR_OBSZ.OKRO_F;
               OKR_OBSG.add(1)
            ?}
         ?}
      ?}
   ?};
   OKR_OBSZ.cntx_pop()
?}


\rok_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodaje całe lata do tabeli OKRO
::   WE: _a - numer roku
::----------------------------------------------------------------------------------------------------------------------
OKRO.cntx_psh();
OKRO.index('UNIK'); OKRO.prefix(REF.FIRMA,_a);
{? ~OKRO.first()
|| do();
   {! _i:=1..12
   |! OKRO.blank(1);
      OKRO.FIRMA:=REF.FIRMA;
      OKRO.ROK:=_a;
      OKRO.NR_OKR:=_i;
      OKRO.NAZWA:={? _i=1 || 'styczeń'
                  |? _i=2 || 'luty'
                  |? _i=3 || 'marzec'
                  |? _i=4 || 'kwiecień'
                  |? _i=5 || 'maj'
                  |? _i=6 || 'czerwiec'
                  |? _i=7 || 'lipiec'
                  |? _i=8 || 'sierpień'
                  |? _i=9 || 'wrzesień'
                  |? _i=10 || 'październik'
                  |? _i=11 || 'listopad'
                  || 'grudzień'
                  ?};
      OKRO.DATA_OD:=date(_a,_i,1);
      OKRO.DATA_DO:=date(_a,_i,0);
      {? ~OKRO.add(1) || undo() ?}
   !};
   end()
?};
OKRO.cntx_pop()


\rkprz_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rekord przed dla okienka WER tabeli OKRO
::----------------------------------------------------------------------------------------------------------------------
PAR_OKR.STATFKS:=PAR_OKR.STATCTR:=PAR_OKR.STATFST:=PAR_OKR.STATOBG:=PAR_OKR.STATKAS:=PAR_OKR.STATLMG:=20*'-';
PAR_OKR.STATLSP:=PAR_OKR.STATLZK:=PAR_OKR.STATLUM:=20*'-';
OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('UNIK');
OKR_OBSZ.prefix(OKRO.ref(),o_fks);
{? o_fks & OKR_OBSZ.first()
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
   PAR_OKR.STATFKS:={? OKR_OBSZ.OKRO_F<>null & OKR_OBSZ.OKRO_F().ZAM='T'
                    || 'zamknięty'
                    || 'otwarty'
                    ?};
   OKRO_F.cntx_pop()
?};
OKR_OBSZ.prefix(OKRO.ref(),o_ctr);
{? o_ctr & OKR_OBSZ.first()
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
   PAR_OKR.STATCTR:={? OKR_OBSZ.OKRO_F<>null & OKR_OBSZ.OKRO_F().ZAM_CON='T'
                    || 'zamknięty'
                    || 'otwarty'
                    ?};
   OKRO_F.cntx_pop()
?};
OKR_OBSZ.prefix(OKRO.ref(),o_obg);
{? o_obg & OKR_OBSZ.first()
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
   PAR_OKR.STATOBG:={? OKR_OBSZ.OKRO_F<>null & OKR_OBSZ.OKRO_F().ZAM='T'
                    || 'zamknięty'
                    || 'otwarty'
                    ?};
   OKRO_F.cntx_pop()
?};
OKR_OBSZ.prefix(OKRO.ref(),o_lmg);
{? o_lmg & OKR_OBSZ.first()
|| PAR_OKR.STATLMG:={? OKR_OBSZ.OKR<>null & (1+OKR_OBSZ.OKR().ZAM)='T'
                    || 'zamknięty'
                    |? exec('FindInSet','#table','OKRD','ODDZ',OKR_OBSZ.OKR().MC,OKR.ROK)<>null()
                    || 'częściowo'
                    || 'otwarty'
                    ?}
?};
OKR_OBSZ.prefix(OKRO.ref(),o_lsp);
{? o_lsp & OKR_OBSZ.first()
|| PAR_OKR.STATLSP:={? OKR_OBSZ.OKR<>null & (1+(1-OKR_OBSZ.OKR().ZAM))='T'
                    || 'zamknięty'
                    || 'otwarty'
                    ?}
?};
OKR_OBSZ.prefix(OKRO.ref(),o_lzk);
{? o_lzk & OKR_OBSZ.first()
|| PAR_OKR.STATLZK:={? OKR_OBSZ.OKR<>null & (1+(2-OKR_OBSZ.OKR().ZAM))='T'
                    || 'zamknięty'
                    || 'otwarty'
                    ?}
?};
OKR_OBSZ.prefix(OKRO.ref(),o_lum);
{? o_lum & OKR_OBSZ.first()
|| PAR_OKR.STATLUM:={? OKR_OBSZ.OKR<>null & OKR_OBSZ.OKR().ZAM+1='T'
                    || 'zamknięty'
                    || 'otwarty'
                    ?}
?};
OKR_OBSZ.prefix(OKRO.ref(),o_fst);
{? o_fst & OKR_OBSZ.first()
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
   PAR_OKR.STATFST:={? OKR_OBSZ.OKRO_F<>null & OKR_OBSZ.OKRO_F().AMOR='T'
                    || 'zamknięty'
                    || 'otwarty'
                    ?};
   OKRO_F.cntx_pop()
?};
OKR_OBSZ.prefix(OKRO.ref(),o_kas);
{? o_kas & OKR_OBSZ.first()
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
   PAR_OKR.STATKAS:={? OKR_OBSZ.OKRO_F<>null & OKR_OBSZ.OKRO_F().ZAM='T'
                    || 'zamknięty'
                    || 'otwarty'
                    ?};
   OKRO_F.cntx_pop()
?};
OKR_OBSZ.cntx_pop();
0


\add_obsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodaje zapisy w OKR_OBSZ jeśli obszar jest uruchomiony i jest licencja
::   WE: _a - obszar
::       _b - data początku
::----------------------------------------------------------------------------------------------------------------------
_obszar:=exec('szuk_b_dom','parses',_a);
{? (_a='OBG' & (exec('a_lic','zam_okr_rok','OBG') | exec('a_lic','zam_okr_rok','OBE'))) |
   (_a='LUM' & (exec('a_lic','zam_okr_rok','LUM') | exec('a_lic','zam_okr_rok','LUO'))) |
   (_a<>'OBG' & _a<>'LUM' & exec('a_lic','zam_okr_rok',_a))
|| {? _a='FKS' | _a='FST' | _a='OBG' | _a='CTR' | _a='KAS'
   || exec('add_okr_obsz','!zws_par_aokr',_b,_obszar,OKRO_F.ref(),null)
   |? _a='LMG' | _a='LSP' | _a='LZK' | _a='LUM'
   || exec('add_okr_obsz','!zws_par_aokr',_b,_obszar,null,OKR.ref())
   ?}
?}


\tab_krst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ, AMK [17.00]
:: OPIS: Uzupełnianie tabeli wariantów klasyfikacji środków trwałych
::       _a - numer roku
::       Uwaga: formuła do wykorzystania
::----------------------------------------------------------------------------------------------------------------------
TAB_KRST.cntx_psh();
TAB_KRST.index('TABKRST');
TAB_KRST.prefix();
_tk:=null;
{? TAB_KRST.last()
|| {! |?
      {? TAB_KRST.AR<=_a || _tk:=TAB_KRST.ref() ?};
      _tk=null() & TAB_KRST.prev()
   !}
?};
:: jezeli brak klasyfikacji to tworzona jest pierwsza automatyczna klasyfikacja
:: jeżeli jest ale obowiązująca od późniejszego roku niż tworzony to zmienia się
:: początkowy rok w klasyfikacji
{? _tk=null & TAB_KRST.size()=0
|| _tk:=exec('krst_add','!zws_par_aokr','A','Klasyfikacja utworzona automatycznie',_a)
|? _tk=null & TAB_KRST.first()
|| TAB_KRST.AR:=_a;
   TAB_KRST.put();
   _tk:=TAB_KRST.ref()
?};
TAB_KRST.cntx_pop();
_tk


\krst_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Zakładanie klasyfikacji środków trwałych
::  OLD: \krst_add/inne.fml
::   WE: _a - symbol klasyfikacji
::       _b - opis klasyfikacji
::       _c - rok od którego obowiązuje klasyfikacja
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
_add_ok:=null;
{? _=3
|| TAB_KRST.blank(1);
   TAB_KRST.KST:=_a;
   TAB_KRST.O:=_b;
   TAB_KRST.AR:=_c;
   _add_ok:={? TAB_KRST.add(1) || TAB_KRST.ref() ?}
?};
OKRO_F.cntx_pop();
_add_ok


\zwr_obs_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [17.00]
:: OPIS: Formuła ustawia okres dla obszaru z parametru _a
::   WE: _a - obszar
::       _b - okres z tabeli OKRO
::       _c - rodzaj okresu używany dla systemów z BO i BZ
::   WY: ref odpowiedniego okresu
::----------------------------------------------------------------------------------------------------------------------
_wy:=null();
_obszar:=exec('szuk_b_dom','parses',_a);
OKRO.cntx_psh(); OKR_OBSZ.cntx_psh();
OKRO.index('UNIK'); OKRO.prefix();
OKR_OBSZ.index('UNIK');
{? OKR_OBSZ.find_key(_b,_obszar)
|| {? _a='FKS' | _a='CTR' | _a='OBG' | _a='KAS' | _a='FST'
   || _wy:=OKR_OBSZ.OKRO_F;
      {? _c='W' & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) & OKR_OBSZ.next()
      || _wy:=OKR_OBSZ.OKRO_F
      |? _c='O' | _c='Z'
      || OKRO_F.cntx_psh();
         OKRO_F.index('ROK');
         OKRO_F.prefix(OKR_OBSZ.OKRO_F().ROK);
         {? _c='O' & OKRO_F.first() || _wy:=OKRO_F.ref()
         |? _c='Z' & OKRO_F.last() || _wy:=OKRO_F.ref()
         ?};
         OKRO_F.cntx_pop()
      ?}
   |? _a='LMG' | _a='LSP' | _a='LZK'
   || _wy:=OKR_OBSZ.OKR
   ?}
?};
OKRO.cntx_pop(); OKR_OBSZ.cntx_pop();
_wy


\bl_okro_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [17.00]
:: OPIS: Formuła na wartość początkową pola OKRO.ROK
::----------------------------------------------------------------------------------------------------------------------
::PARSES.ROK
0


\a_domain
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza, czy obszar jest aktywny na podaną datę
::   WE: _a - ref obszaru (B_DOMAIN) lub skrócona nazwa obszaru (B_DOMAIN.SYMBOL)
::       _b - data
::   WY: 1/0 - obszar aktywny, nieaktywny
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0; _dom:=null;
B_DOMAIN.cntx_psh(); B_DOMAIN.index('SYMBOL'); B_DOMAIN.prefix();
{? type_of(_a)=type_of(null)
|| {? B_DOMAIN.seek(_a) || _dom:=B_DOMAIN.ref() ?}
|? type_of(_a)=type_of('')
|| {? B_DOMAIN.find_key(_a,) || _dom:=B_DOMAIN.ref() ?}
?};
B_DOMAIN.cntx_pop();
{? _dom & type_of(_b)=type_of(date()) & _b<>date(0,0,0)
|| _pocz:=date(_b~1,_b~2,1);
   OKRO.cntx_psh(); OKRO.index('POCZ'); OKRO.prefix(REF.FIRMA);
   {? OKRO.find_key(_pocz) & _b>=OKRO.DATA_OD & _b<=OKRO.DATA_DO
   || OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),_dom);
      _zwrot:=OKR_OBSZ.first();
      OKR_OBSZ.cntx_pop()
   ?};
   OKRO.cntx_pop()
?};
_zwrot


\a_domain_okro_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza, czy obszar jest aktywny dla podanego OKRO_F
::   WE: _a - ref obszaru (B_DOMAIN) lub skrócona nazwa obszaru (B_DOMAIN.SYMBOL)
::       _b - ref_OKRO_F
::   WY: 1/0 - obszar aktywny, nieaktywny
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0; _dom:=null;
B_DOMAIN.cntx_psh(); B_DOMAIN.index('SYMBOL'); B_DOMAIN.prefix();
{? type_of(_a)=type_of(null)
|| {? B_DOMAIN.seek(_a) || _dom:=B_DOMAIN.ref() ?}
|? type_of(_a)=type_of('')
|| {? B_DOMAIN.find_key(_a,) || _dom:=B_DOMAIN.ref() ?}
?};
B_DOMAIN.cntx_pop();
{? _dom & _b<>null
|| OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKRO_F2'); OKR_OBSZ.prefix(_dom,_b);
   _zwrot:=OKR_OBSZ.first();
   OKR_OBSZ.cntx_pop()
?};
_zwrot


\be_bd_obsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed redakcja i na wyświetl dla obszarów przy dodawaniu okresów
::----------------------------------------------------------------------------------------------------------------------
($('ed'+(-cur_afld())))()


\ae_obsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji dla obszarów przy dodawaniu okresów
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('be_lokr','!zws_par_aokr') || PAR_OKR.LOKR:=12 ?};
win_disp();
1


\spr_zal_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzanie, czy można założyć okresy
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? (firma0 & ~edfks & ~edctr & ~edfst) |
   (~firma0 & ~edfks & ~edfst & ~edctr & ~edobg & ~edkas & ~edlmg & ~edlsp & ~edlzk & ~edlum)
|| FUN.info('Roku %1 nie można założyć w żadnym obszarze.'@[$PAR_OKR.ROK]);
   _zwrot:={? -menu_txt()='popraw' || 0 || 'ROK' ?}
|? ((~firma0 & ~PAR_OKR.FKS & ~PAR_OKR.CTR & ~PAR_OKR.OBG & ~PAR_OKR.KAS & ~PAR_OKR.FST &
     ~PAR_OKR.LMG & ~PAR_OKR.LSP & ~PAR_OKR.LZK & ~PAR_OKR.LUM) |
     (firma0 & ~PAR_OKR.FKS & ~PAR_OKR.CTR & ~PAR_OKR.FST)
   )
|| FUN.info('Nie wybrano żadnej dziedziny dla roku %1.'@[$PAR_OKR.ROK]);
   _zwrot:={? -menu_txt()='popraw' || 0 || 'ROK' ?}
?};
_zwrot


\dol_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie i poprawianie okresów
::----------------------------------------------------------------------------------------------------------------------
{? firma0 || PAR_OKR.win_edit('EDITOKR0') || PAR_OKR.win_edit('EDIT_OKR') ?};
edfks:=edfst:=edctr:=edobg:=edkas:=edlmg:=edlsp:=edlzk:=edlum:=0;
PAR_OKR.FKS:=PAR_OKR.OBG:=PAR_OKR.KAS:=PAR_OKR.CTR:=PAR_OKR.FST:=PAR_OKR.LSP:=PAR_OKR.LZK:=PAR_OKR.LMG:=PAR_OKR.LUM:=0;
PAR_OKR.LOKR:=12; PAR_OKR.MSTART:=1;
exec('ustal_licencje','!zws_par_aokr');
OKRO.cntx_psh(); OKRO.index('UNIK'); OKRO.prefix(REF.FIRMA);
PAR_OKR.ROK:={? -menu_txt()='popraw'
             || OKRO.ROK
             || {? OKRO.last() || OKRO.ROK+1 || date()~1 ?}
             ?};
OKRO.cntx_pop();
exec('ustaw_obszary','!zws_par_aokr');
exec('ustaw_obszary1','!zws_par_aokr');
{? PAR_OKR.edit("exec('spr_zal_okr','!zws_par_aokr')")
|| {? PAR_OKR.FKS || exec('fin_rok','!zws_par_aokr','FKS',PAR_OKR.ROK,PAR_OKR.MSTART,PAR_OKR.LOKR) ?};
   {? PAR_OKR.CTR || exec('fin_rok','!zws_par_aokr','CTR',PAR_OKR.ROK,PAR_OKR.MSTART,PAR_OKR.LOKR) ?};
   {? PAR_OKR.FST
   || _firma:=REF.FIRMA().SYMBOL;
      SRSR.cntx_psh(); SRST.cntx_psh();
      SRSR.use('srsrr'+_firma); SRST.use('srstr'+_firma);
      {? SRSR.lock(2,1) & SRST.lock(2,1)
      || exec('fin_rok','!zws_par_aokr','FST',PAR_OKR.ROK,PAR_OKR.MSTART,PAR_OKR.LOKR)
      || FUN.info('Dołączenie nowego roku dla dziedziny środki trwałe niemożliwe.\n'
                  'Przed tą operacją należy wstrzymać wszystkie prace w obszarze kartoteka środków.'@)
      ?};
      SRSR.unlock(); SRST.unlock();
      SRSR.cntx_pop(); SRST.cntx_pop()
   ?};
   {? ~firma0
   || {? PAR_OKR.KAS || exec('fin_rok','!zws_par_aokr','KAS',PAR_OKR.ROK,PAR_OKR.MSTART,PAR_OKR.LOKR) ?};
      {? PAR_OKR.OBG || exec('fin_rok','!zws_par_aokr','OBG',PAR_OKR.ROK,PAR_OKR.MSTART,PAR_OKR.LOKR) ?};
      {? PAR_OKR.LMG || exec('log_rok','!zws_par_aokr','LMG') ?};
      {? PAR_OKR.LSP || exec('log_rok','!zws_par_aokr','LSP') ?};
      {? PAR_OKR.LZK || exec('log_rok','!zws_par_aokr','LZK') ?};
      {? PAR_OKR.LUM || exec('log_rok','!zws_par_aokr','LUM') ?}
   ?}
?};
VAR_DEL.delete('edfks','edfst','edctr','edobg','edkas','edlmg','edlsp','edlzk','edlum')


\ustal_licencje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustala aktywne licencje
::----------------------------------------------------------------------------------------------------------------------
o_lmg:=exec('szuk_b_dom','parses','LMG');
o_lsp:=exec('szuk_b_dom','parses','LSP');
o_lzk:=exec('szuk_b_dom','parses','LZK');
o_fks:=exec('szuk_b_dom','parses','FKS');
o_ctr:=exec('szuk_b_dom','parses','CTR');
o_obg:=exec('szuk_b_dom','parses','OBG');
o_obe:=exec('szuk_b_dom','parses','OBE');
o_kas:=exec('szuk_b_dom','parses','KAS');
o_fst:=exec('szuk_b_dom','parses','FST');
o_lum:=exec('szuk_b_dom','parses','LUM');
o_luo:=exec('szuk_b_dom','parses','LUO');
lic_lmg:=exec('a_lic','zam_okr_rok',o_lmg);
lic_lsp:=exec('a_lic','zam_okr_rok',o_lsp);
lic_lzk:=exec('a_lic','zam_okr_rok',o_lzk);
lic_fks:=exec('a_lic','zam_okr_rok',o_fks);
lic_obg:=(exec('a_lic','zam_okr_rok',o_obg) | exec('a_lic','zam_okr_rok',o_obe));
lic_kas:=exec('a_lic','zam_okr_rok',o_kas);
lic_ctr:=exec('a_lic','zam_okr_rok',o_ctr);
lic_fst:=exec('a_lic','zam_okr_rok',o_fst);
lic_lum:=exec('a_lic','zam_okr_rok',o_lum) | exec('a_lic','zam_okr_rok',o_luo);
~~


\ustaw_obszary
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawia parametry, czy założyć dla obszarów logistycznych
::----------------------------------------------------------------------------------------------------------------------
{? firma0
|| edlmg:=edlsp:=edlzk:=edlum:=0
|| edlmg:=lic_lmg & exec('czy_zal_rok_log','!zws_par_aokr',PAR_OKR.ROK,o_lmg); {? ~edlmg || PAR_OKR.LMG:=0 ?};
   edlsp:=lic_lsp & exec('czy_zal_rok_log','!zws_par_aokr',PAR_OKR.ROK,o_lsp); {? ~edlsp || PAR_OKR.LSP:=0 ?};
   edlzk:=lic_lzk & exec('czy_zal_rok_log','!zws_par_aokr',PAR_OKR.ROK,o_lzk); {? ~edlzk || PAR_OKR.LZK:=0 ?};
   edlum:=lic_lum & exec('czy_zal_rok_log','!zws_par_aokr',PAR_OKR.ROK,o_lum); {? ~edlum || PAR_OKR.LUM:=0 ?}
?}


\ustaw_obszary1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawia parametry, czy założyć dla obszarów finansowych i środków trwałych
::----------------------------------------------------------------------------------------------------------------------
{? ~PAR_OKR.MSTART | ~PAR_OKR.LOKR
|| PAR_OKR.FKS:=edfks:=PAR_OKR.OBG:=edobg:=PAR_OKR.KAS:=edkas:=PAR_OKR.CTR:=edctr:=PAR_OKR.FST:=edfst:=0
|| edfks:=lic_fks & exec('czy_zal_rok_fin','!zws_par_aokr',o_fks); {? ~edfks || PAR_OKR.FKS:=0 ?};
   edctr:=lic_ctr & exec('czy_zal_rok_fin','!zws_par_aokr',o_ctr); {? ~edctr || PAR_OKR.CTR:=0 ?};
   edfst:=lic_fst & exec('czy_zal_rok_fin','!zws_par_aokr',o_fst); {? ~edfst || PAR_OKR.FST:=0 ?};
   {? firma0
   || PAR_OKR.OBG:=edobg:=PAR_OKR.KAS:=edkas:=0
   || edobg:=lic_obg & exec('czy_zal_rok_fin','!zws_par_aokr',o_obg); {? ~edobg || PAR_OKR.OBG:=0 ?};
      edkas:=lic_kas & exec('czy_zal_rok_fin','!zws_par_aokr',o_kas); {? ~edkas || PAR_OKR.KAS:=0 ?}
   ?}
?}


\czy_zal_rok_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzenie, czy można załozyć rok w logistyce
::   WE: _a - rok (INTEGER)
::       _b - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
OKR.cntx_psh();
OKR.index('OKR'); OKR.prefix(REF.FIRMA);
{? OKR.last()
|| OKR_OBSZ.cntx_psh();
   OKR_OBSZ.index('OKR'); OKR_OBSZ.prefix(REF.FIRMA,_b);
   {? OKR_OBSZ.first()
   || OKR_OBSZ.prefix(REF.FIRMA,_b,_a);
      {? ~OKR_OBSZ.first()
      || OKR_OBSZ.prefix(REF.FIRMA,_b);
         {? OKR_OBSZ.last() & OKR_OBSZ.OKR().ROK=(_a-1) || _zwrot:=1 ?}
      ?}
   || _zwrot:=1
   ?};
   OKR_OBSZ.cntx_pop()
|| _zwrot:=1
?};
OKR.cntx_pop();
_zwrot


\czy_zal_rok_fin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzenie, czy można założyć rok finansowy
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
OKRO_F.cntx_psh(); OKRO_F.prefix();
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ~ROK_F.first()
|| _zwrot:=1
|| OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKRO_F'); OKR_OBSZ.prefix(REF.FIRMA,_a);
   {? OKR_OBSZ.last()
   || OKR_OBSZ.OKRO_F().ROK();
      {? ROK_F.KON_ROK=date(PAR_OKR.ROK,PAR_OKR.MSTART,1)-1
      || _zwrot:={? ROK_F.next() || ROK_F.LOBR=PAR_OKR.LOKR || 1 ?}
      ?}
   || ROK_F.prefix(REF.FIRMA);
      {? ROK_F.find_key(date(PAR_OKR.ROK,PAR_OKR.MSTART,1))
      || _zwrot:=ROK_F.LOBR=PAR_OKR.LOKR
      || _czy_obs:=0;
         OKR_OBSZ.prefix(REF.FIRMA,o_fks); _czy_obs:=OKR_OBSZ.first();
         {? ~_czy_obs || OKR_OBSZ.prefix(REF.FIRMA,o_obg); _czy_obs:=OKR_OBSZ.first() ?};
         {? ~_czy_obs || OKR_OBSZ.prefix(REF.FIRMA,o_kas); _czy_obs:=OKR_OBSZ.first() ?};
         {? ~_czy_obs || OKR_OBSZ.prefix(REF.FIRMA,o_ctr); _czy_obs:=OKR_OBSZ.first() ?};
         {? ~_czy_obs || OKR_OBSZ.prefix(REF.FIRMA,o_fst); _czy_obs:=OKR_OBSZ.first() ?};
         {? _czy_obs
         || ROK_F.index('ROKKON'); ROK_F.prefix(REF.FIRMA);
            {? ROK_F.find_key(date(PAR_OKR.ROK,PAR_OKR.MSTART,1)-1)
            || ROK_F.index('ROKPOCZ'); _zwrot:=~ROK_F.next()
            ?}
         ?}
      ?}
   ?};
   OKR_OBSZ.cntx_pop()
?};
OKRO_F.cntx_pop(); ROK_F.cntx_pop();
_zwrot


\bd_mstart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed wyświetl dla pola PAR_OKR.MSTART
::----------------------------------------------------------------------------------------------------------------------
{? exec('be_mstart','!zws_par_aokr') || 1 || exec('findfnv','#color') ?}


\be_mstart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed redakcją pola PAR_OKR.MSTART
::----------------------------------------------------------------------------------------------------------------------
{? firma0
|| lic_fks | lic_ctr | lic_fst
|| lic_fks | lic_obg | lic_kas | lic_ctr | lic_fst
?}


\ae_mstart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola PAR_OKR.MSTART
::----------------------------------------------------------------------------------------------------------------------
{? PAR_OKR.MSTART<1 | PAR_OKR.MSTART>12
|| FUN.info('Miesiąc startowy może być z przedziału 1-12.'@); 0
|| exec('ustaw_obszary1','!zws_par_aokr'); win_disp(); 1
?}


\bdsp_lokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed wyświetl dla pola PAR_OKR.LOKR
::----------------------------------------------------------------------------------------------------------------------
{? exec('be_lokr','!zws_par_aokr')
|| 1
|| exec('findfnv','#color')
?}


\be_lokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed redakcją pola PAR_OKR.LOKR
::----------------------------------------------------------------------------------------------------------------------
{? firma0
|| lic_fks | lic_ctr | lic_fst
|| lic_fks | lic_obg | lic_kas | lic_ctr | lic_fst
?}


\ae_lokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola PAR_OKR.LOKR
::----------------------------------------------------------------------------------------------------------------------
{? PAR_OKR.LOKR<1 | PAR_OKR.LOKR>23
|| FUN.info('Liczba okresów może być z przedziału 1-23.'@); 0
|| exec('ustaw_obszary1','!zws_par_aokr'); win_disp(); 1
?}


\be_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed redakcją pola PAR_OKR.ROK
::----------------------------------------------------------------------------------------------------------------------
-menu_txt()<>'popraw'


\ae_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola PAR_OKR.ROK
::----------------------------------------------------------------------------------------------------------------------
{? PAR_OKR.ROK<=1900
|| FUN.info('Nie podano poprawnie roku.'@); 0
|| exec('ustaw_obszary','!zws_par_aokr');
   exec('ustaw_obszary1','!zws_par_aokr');
   win_disp(); 1
?}


\log_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tworzenie nowego roku dla logistyki
::   WE: _a - kod obszaru
::----------------------------------------------------------------------------------------------------------------------
OKR.cntx_psh(); OKR.index('OKR'); OKR.prefix(REF.FIRMA);
_progp:=_progw:='N';
OKR.prefix(REF.FIRMA,PAR_OKR.ROK-1);
{? OKR.last()
|| _progp:=OKR.ISTPP;
   _progw:=OKR.ISTPW
?};
OKR.prefix(REF.FIRMA);
{? ~OKR.find_key(PAR_OKR.ROK) || exec('nowy_rok','!zws_par_aokr',_progp,_progw) ?};
{? PAR_OKR.LOKR>12 & ~OKR.find_key(PAR_OKR.ROK+1)
|| _rok:=PAR_OKR.ROK; PAR_OKR.ROK+=1;
   exec('nowy_rok','!zws_par_aokr',_progp,_progw);
   PAR_OKR.ROK:=_rok
?};
OKR.cntx_pop();
exec('okr_obsz_log','!zws_par_aokr',_a);
{? PAR_OKR.LOKR>12
|| _rok:=PAR_OKR.ROK; PAR_OKR.ROK+=1;
   exec('okr_obsz_log','!zws_par_aokr',_a);
   PAR_OKR.ROK:=_rok
?}


\nowy_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JF [2008]
:: OPIS: Zakłada rok
::   WE: _a - INTRASTAT-próg w przywozie
::       _c - INTRASTAT-próg w wywozie
::   WY: OKR.ref()
::----------------------------------------------------------------------------------------------------------------------
{! _x:=12//-1..1
|! OKR.blank();
   OKR.ROK:=PAR_OKR.ROK;
   OKR.MC:=_x;
   OKR.NAZ:=date(PAR_OKR.ROK,_x,1)$8;
   OKR.ISTPP:=_a;
   OKR.ISTPW:=_b;
   OKR.add(1)
!};
OKR.ref()


\okr_obsz_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodaje zapisy w OKR_OBSZ dla tabeli OKR
::   WE: _a - kod obszaru
::----------------------------------------------------------------------------------------------------------------------
OKR.cntx_psh(); OKR.index('OKR'); OKR.prefix(REF.FIRMA,PAR_OKR.ROK);
{? OKR.first()
|| {! |?
      exec('rok_add','!zws_par_aokr',OKR.ROK);
      exec('add_obsz','!zws_par_aokr',_a,date(OKR.ROK,OKR.MC,1));
      OKR.next()
   !}
?};
OKR.cntx_pop()


\fin_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tworzenie nowego roku dla finansów
::   WE: _a - kod obszaru
::       _b - numer roku
::       _c - numer okresu startowego
::       _d - liczba okresów
::----------------------------------------------------------------------------------------------------------------------
_ok:=0; _nazr:='';
ROK_F.cntx_psh(); ROK_F.index('NAZWA'); ROK_F.prefix();
OKRO_F.cntx_psh(); OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA,date(_b,_c,1));
{? OKRO_F.first()
|| exec('okro_f_obsz','!zws_par_aokr',OKRO_F.ROK,_a)
|| ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,$_b);
   {? ~ROK_F.first()
   || _nazr:=$_b; _ok:=1
   || _lp:=1;
      {! |?
         _nazr:=$_b+'\\'+form(_lp,-2);
         ROK_F.prefix(REF.FIRMA,_nazr);
         {? ROK_F.first()
         || _lp+=1
         || _ok:=1
         ?};
         ~_ok
      !}
   ?};
   {? _ok
   || ROK_F_K.cntx_psh();
      ROK_F_K.index('LP'); ROK_F_K.prefix(null);
      _ok:={? ROK_F_K.first()
           || ROK_F.blank();
              ROK_F.FIRMA:=REF.S_FIRMA;
              ROK_F.NAZ:=_nazr;
              ROK_F.KOD:=ROK_F_K.KOD;
              ROK_F.LOBR:=_d;
              ROK_F.ZAM:='N';
              {? ROK_F.add()
              || ROK_F_K.prefix();
                 ROK_F_K.ROK_F:=ROK_F.ref();
                 ROK_F_K.put()
              ?}
           || FUN.info('Wyczerpano dwuznakowe kody lat.\nDodanie roku niemożliwe'@); 0
           ?};
      ROK_F_K.cntx_pop()
   ?};
   {? _ok
   || _mies:=obj_new(12);
      _mies[1]:='styczeń'; _mies[2]:='luty'; _mies[3]:='marzec';
      _mies[4]:='kwiecień'; _mies[5]:='maj'; _mies[6]:='czerwiec';
      _mies[7]:='lipiec'; _mies[8]:='sierpień'; _mies[9]:='wrzesień';
      _mies[10]:='październik'; _mies[11]:='listopad'; _mies[12]:='grudzień';
      exec('okro_add','!zws_par_aokr',0,'Bilans otwarcia',date(0,0,0),date(0,0,0),1,1);
      {! _i:=1.._d
      |! _okres:=_i+_c-1;
         {? _okres<=12
         || _m:=_okres; _rok:=_b
         |? _okres>12 & _okres<=24
         || _m:=_okres-12; _rok:=_b+1
         |? _okres>24
         || _m:=_okres-24; _rok:=_b+2
         ?};
         _kwartal:={? _i<=3 || 1 |? _i>3 & _i<=6 || 2 |? _i>6 & _i<=9 || 3 || 4 ?};
         _polrocz:={? _i<=6 || 1 || 2  ?};
         exec('okro_add','!zws_par_aokr',_i,_mies[_m],date(_rok,_m,1),date(_rok,_m,0),_kwartal,_polrocz)
      !};
      exec('okro_add','!zws_par_aokr',_i+1,'Bilans zamknięcia',date(0,0,0),date(0,0,0),4,2);
      exec('daty_rok','!zws_par_aokr');
      exec('add_bobz','rejestry',ROK_F.ref());
      exec('add_rok_view','rejestry');
      exec('add_ospr','!zws_par_aokr',ROK_F.ref());
      exec('okro_f_obsz','!zws_par_aokr',ROK_F.ref(),_a)
   ?}
?};
:: uzupełnienie informacji o numerach lat i okresów środków trwałych
exec('set_okr_srodki','!zws_par_aokr');
OKRO_F.cntx_pop(); ROK_F.cntx_pop()


\okro_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tworzenie okresu obrachunkowego
::   WE: _a - numer okresu
::       _b - nazwa okresu
::       _c - data początku
::       _d - data końca
::       _e - numer półrocza
::       _f - numer kwartału
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh(); OKRO_F.prefix();
OKRO_F.blank(1);
OKRO_F.ROK:=ROK_F.ref();
OKRO_F.KOD:=(24-_a)*'1'+_a*'0';
OKRO_F.NR:=_a;
OKRO_F.NAZ:=_b;
OKRO_F.POCZ:=_c;
OKRO_F.KON:=_d;
OKRO_F.KWARTAL:=_e;
OKRO_F.POLROCZE:=_f;
OKRO_F.ZAM:='N';
OKRO_F.AMOR:='N';
{? OKRO_F.add() || exec('okr_lang','!zws_par_aokr') ?};
OKRO_F.cntx_pop()


\add_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodawanie okresów raportowania dla roku (ROK_F)
::   WE: _a - ref ROK_F
::----------------------------------------------------------------------------------------------------------------------
OKRESY.WOKRES:='Z';
ROK_F.cntx_psh(); ROK_F.prefix();
{? ROK_F.seek(_a)
|| OKRESY.ROK1:=ROK_F.ref();
   exec('add_ospr_m','!zws_par_aokr');
   exec('add_ospr_k','!zws_par_aokr');
   exec('add_ospr_p','!zws_par_aokr');
   exec('add_ospr_l','!zws_par_aokr')
?};
ROK_F.cntx_pop()


\add_ospr_l
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodawanie okresów raportowania dla roku (ROK_F) - lata
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(OKRESY.ROK1);
{? OKRO_F.first()
|| OKRESY.OKRES_OD:=OKRO_F.ref
|| OKRESY.OKRES_OD:=null
?};
{? OKRO_F.last()
|| OKRESY.OKRES_DO:=OKRO_F.ref
|| OKRESY.OKRES_DO:=null
?};
{? OKRESY.OKRES_OD<>null & OKRESY.OKRES_DO<>null
|| exec('addokr','!zws_par_aokr','R')
?};
OKRO_F.cntx_pop()


\add_ospr_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodawanie okresów raportowania dla roku (ROK_F) - miesiące
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(OKRESY.ROK1);
{? OKRO_F.first()
|| {! |?
      OKRESY.OKRES_OD:=OKRO_F.ref();
      OKRESY.OKRES_DO:=OKRO_F.ref();
      exec('addokr','!zws_par_aokr','M');
      OKRO_F.next()
   !}
?};
OKRO_F.cntx_pop()


\add_ospr_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodawanie okresów raportowania dla roku (ROK_F) - kwartały
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
_indtym:=OKRO_F.ndx_tmp('',1,'ROK',,0,'KWARTAL',,0,'NR',,1);
OKRO_F.index(_indtym); OKRO_F.prefix(OKRESY.ROK1);
_q:=0;
{? OKRO_F.first()
|| {! |?
      OKRESY.OKRES_DO:=OKRO_F.ref;
      _nr:=OKRO_F.KWARTAL;
      {! |?
         {? OKRO_F.KWARTAL=_nr
         || OKRESY.OKRES_OD:=OKRO_F.ref;
            _q:=OKRO_F.next()
         || _q:=1; 0
         ?}
      !};
      {? OKRESY.OKRES_OD<>null & OKRESY.OKRES_DO<>null
      || exec('addokr','!zws_par_aokr','K')
      ?};
      {? _q=1 || 1 || 0 ?}
   !}
|| 0
?};
OKRO_F.cntx_pop()


\add_ospr_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodawanie okresów raportowania dla roku (ROK_F) - półrocza
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
_indtym:=OKRO_F.ndx_tmp('',1,'ROK',,0,'POLROCZE',,0,'NR',,1);
OKRO_F.index(_indtym); OKRO_F.prefix(OKRESY.ROK1);
_q:=0;
{? OKRO_F.first()
|| {! |?
      OKRESY.OKRES_DO:=OKRO_F.ref;
      _nr:=OKRO_F.POLROCZE;
      {! |?
         {? OKRO_F.POLROCZE=_nr
         || OKRESY.OKRES_OD:=OKRO_F.ref;
            _q:=OKRO_F.next()
         || _q:=1; 0
         ?}
      !};
      {? OKRESY.OKRES_OD<>null & OKRESY.OKRES_DO<>null
      || exec('addokr','!zws_par_aokr','P')
      ?};
      {? _q=1 || 1 || 0 ?}
   !}
|| 0
?};
OKRO_F.cntx_pop()


\addokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Funkcja tworząca okres sprawozdawczy (dodanie rekordu do OSPR)
::   WE: _a - typ okresu
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh(); OSPR.cntx_psh(); OSPR.prefix();
OSPR.blank();
OSPR.OKRES_OD:=OKRESY.OKRES_OD;
OSPR.OKRES_DO:=OKRESY.OKRES_DO;
OSPR.TYP:=_a;
OSPR.NAZWA:={? _a='R'
            || 'ROK: '+form(OKRESY.ROK1().NAZ,,,'1')
            |? _a='P'
            || OKRESY.ROK1().NAZ+' '+form(OKRESY.OKRES_OD().POLROCZE)+' PÓŁROCZE '
            |? _a='K'
            || OKRESY.ROK1().NAZ+' '+form(OKRESY.OKRES_OD().KWARTAL)+' KWARTAŁ '
            |? _a='M'
            || OKRESY.ROK1().NAZ+'-'+form(OKRESY.OKRES_OD().NR,2,0)+' ('+OKRESY.OKRES_OD().NAZ+')'
            ?};
exec('okr_ospr','okresy');
OSPR.add(1);
OKRO_F.cntx_pop(); OSPR.cntx_pop()


\okr_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Przypisuje tłumaczenie dla okresu
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.S_FIRMA);
OKRO_F.ROK(); _rok:=null;
{! |?
   {? ROK_F.prev()
   || {? ROK_F.LOBR=12 || _rok:=ROK_F.ref(); 0 || 1 ?}
   ?}
!};
ROK_F.cntx_pop();
{? _rok
|| _nr:=OKRO_F.NR;
   _ref:=OKRO_F.ref();
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(_rok,_nr);
   {? OKRO_F.first()
   || LANG_SIK.cntx_psh();
      LANG_SIK.index('JEZYK'); LANG_SIK.prefix(OKRO_F.name(),#OKRO_F.ref());
      {? LANG_SIK.first()
      || {! |?
            LANG_SIK.cntx_psh(); LANG_SIK.prefix();
            LANG_SIK.REF:=#_ref;
            LANG_SIK.add();
            LANG_SIK.cntx_pop();
            LANG_SIK.next()
         !}
      ?};
      LANG_SIK.cntx_pop()
   ?};
   OKRO_F.cntx_pop()
?}


\daty_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [1201]
:: OPIS: Ustawianie daty początku i końca roku
::----------------------------------------------------------------------------------------------------------------------
{? ROK_F.get()
|| ROK_F.cntx_psh(); ROK_F.prefix();
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
   {? OKRO_F.first() & OKRO_F.next() & OKRO_F.POCZ<>date(0,0,0) || ROK_F.POCZ_ROK:=OKRO_F.POCZ ?};
   {? OKRO_F.last() & OKRO_F.prev() & OKRO_F.KON<>date(0,0,0) || ROK_F.KON_ROK:=OKRO_F.KON ?};
   ROK_F.put();
   ROK_F.cntx_pop(); OKRO_F.cntx_pop()
?}


\okro_f_obsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodaje zapisy w OKR_OBSZ dla tabeli OKRO_F
::   WE: _a - ref ROK_F
::       _b - kod obszaru
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(_a);
{? OKRO_F.first()
|| {! |?
      OKRO_F.cntx_psh();
      _pocz:=date(0,0,0);
      {? (OKRO_F.NAZ='Bilans otwarcia' & OKRO_F.next()) | (OKRO_F.NAZ='Bilans zamknięcia' & OKRO_F.prev()) |
         OKRO_F.POCZ<>date(0,0,0)
      || _pocz:=OKRO_F.POCZ;
         exec('rok_add','!zws_par_aokr',_pocz~1)
      ?};
      OKRO_F.cntx_pop();
      {? _pocz<>date(0,0,0)
      || exec('add_obsz','!zws_par_aokr',_b,_pocz)
      ?};
      OKRO_F.next()
   !}
?};
OKRO_F.cntx_pop()


\prred_par_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [17.00]
:: OPIS: Formuła przed redakcją pola PARAM.OKRO ustawia okres zgodny z rokiem wybranym w polu PARAM.OKRO_ROK
::----------------------------------------------------------------------------------------------------------------------
PARSES.OKRO_R='W'


\bds_znacznik_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Na wyświetl dla znaczników okresów
::----------------------------------------------------------------------------------------------------------------------
_pole:=cur_afld()+3;
{? _pole='FKS'
|| {? ~lic_fks
   || exec('findfnv','#color')
   || exec('pocz_rok_fin','!zws_par_aokr',o_fks)
   ?}
|? _pole='OBG'
|| {? ~lic_obg
   || exec('findfnv','#color')
   || exec('pocz_rok_fin','!zws_par_aokr',o_obg)
   ?}
|? _pole='KAS'
|| {? ~lic_kas
   || exec('findfnv','#color')
   || exec('pocz_rok_fin','!zws_par_aokr',o_kas)
   ?}
|? _pole='CTR'
|| {? ~lic_ctr
   || exec('findfnv','#color')
   || exec('pocz_rok_fin','!zws_par_aokr',o_ctr)
   ?}
|? _pole='FST'
|| {? ~lic_fst
   || exec('findfnv','#color')
   || exec('pocz_rok_fin','!zws_par_aokr',o_fst)
   ?}
|? _pole='LZK'
|| {? ~lic_lzk
   || exec('findfnv','#color')
   || exec('pocz_rok_log','!zws_par_aokr',o_lzk)
   ?}
|? _pole='LMG'
|| {? ~lic_lmg
   || exec('findfnv','#color')
   || exec('pocz_rok_log','!zws_par_aokr',o_lmg)
   ?}
|? _pole='LSP'
|| {? ~lic_lsp
   || exec('findfnv','#color')
   || exec('pocz_rok_log','!zws_par_aokr',o_lsp)
   ?}
|? _pole='LUM'
|| {? ~lic_lum
   || exec('findfnv','#color')
   || exec('pocz_rok_log','!zws_par_aokr',o_lum)
   ?}
|| 0
?}


\pocz_rok_fin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzenie, czy OKRO jest miesiącem startowym dla finansów
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
OKRO_F.cntx_psh(); OKRO_F.prefix();
OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),_a);
{? OKR_OBSZ.first()
|| {! |?
      {? OKR_OBSZ.OKRO_F & OKR_OBSZ.OKRO_F().NAZ='Bilans otwarcia'
      || _zwrot:=exec('flddisp','color','OKRO#01#00')
      ?};
      _zwrot='' & OKR_OBSZ.next()
   !}
?};
OKR_OBSZ.cntx_pop(); OKRO_F.cntx_pop();
_zwrot


\pocz_rok_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzenie, czy OKRO jest miesiącem startowym dla logistyki
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
OKR.cntx_psh(); OKR.prefix();
OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),_a);
{? OKR_OBSZ.first() & OKR_OBSZ.OKR & OKR_OBSZ.OKR().MC=1
|| _zwrot:=exec('flddisp','color','OKRO#01#00')
?};
OKR_OBSZ.cntx_pop(); OKR.cntx_pop();
_zwrot


\leg_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Legenda dla OKRO
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','OKRO#01#')


\del_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie lat dla obszarów
::----------------------------------------------------------------------------------------------------------------------
{? firma0 || PAR_OKR.win_edit('DEL_OKR0') || PAR_OKR.win_edit('DEL_OKR') ?};
PAR_OKR.LMG:=PAR_OKR.LSP:=PAR_OKR.LZK:=PAR_OKR.FKS:=PAR_OKR.OBG:=PAR_OKR.KAS:=PAR_OKR.CTR:=PAR_OKR.FST:=PAR_OKR.LUM:=0;
edfks:=edfst:=edctr:=edobg:=edkas:=edlmg:=edlsp:=edlzk:=edlum:=0; rokf:=rokl:=null;
delrok_f:=delrok_l:=0;
exec('czy_del_rok_fin','!zws_par_aokr',1);
{? ~firma0 || exec('czy_del_rok_log','!zws_par_aokr') ?};
{? ~edfks & ~edfst & ~edctr & ~edobg & ~edkas & ~edlmg & ~edlsp & ~edlzk & ~edlum
|| FUN.info('W żadnym obszarze nie można usunąć roku.\n'+
            'Obszary są uruchomione i zawierają dane, istnieją kolejne lata\n'+
            'lub były wybrane w parametrach pracy dla użytkowników.'@)
|? PAR_OKR.edit()
|| {? (PAR_OKR.FST | PAR_OKR.FKS | PAR_OKR.OBG | PAR_OKR.KAS | PAR_OKR.KAS | PAR_OKR.CTR |
       PAR_OKR.LMG | PAR_OKR.LSP | PAR_OKR.LZK | PAR_OKR.LUM) &
       FUN.ask('Na pewno usunąć rok?'@)
   || {? PAR_OKR.FKS | PAR_OKR.FST | PAR_OKR.OBG | PAR_OKR.KAS | PAR_OKR.KAS | PAR_OKR.CTR
      || exec('del_okr_fin','!zws_par_aokr')
      ?};
      {? PAR_OKR.LMG | PAR_OKR.LSP | PAR_OKR.LZK | PAR_OKR.LUM
      || exec('del_okr_log','!zws_par_aokr')
      ?};
      exec('del_okro','!zws_par_aokr')
   ?}
?};
VAR_DEL.delete('delrok_f','rokf','rokl','delrok_l')


\del_okr_fin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie roku dla obszarów FKS, OBG, CTR, KAS
::----------------------------------------------------------------------------------------------------------------------
{? PAR_OKR.FKS || exec('del_okr_obszfin','!zws_par_aokr',o_fks) ?};
{? PAR_OKR.OBG || exec('del_okr_obszfin','!zws_par_aokr',o_obg) ?};
{? PAR_OKR.KAS || exec('del_okr_obszfin','!zws_par_aokr',o_kas) ?};
{? PAR_OKR.CTR || exec('del_okr_obszfin','!zws_par_aokr',o_ctr) ?};
{? PAR_OKR.FST || exec('del_okr_obszfin','!zws_par_aokr',o_fst) ?};
{? delrok_f
|| delrok_f:=~exec('czy_okr_obszfin','!zws_par_aokr',o_fks);
   {? delrok_f || delrok_f:=~exec('czy_okr_obszfin','!zws_par_aokr',o_obg) ?};
   {? delrok_f || delrok_f:=~exec('czy_okr_obszfin','!zws_par_aokr',o_kas) ?};
   {? delrok_f || delrok_f:=~exec('czy_okr_obszfin','!zws_par_aokr',o_ctr) ?};
   {? delrok_f || delrok_f:=~exec('czy_okr_obszfin','!zws_par_aokr',o_fst) ?}
?};
exec('del_okr_fin1','!zws_par_aokr')


\del_okr_fin1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie roku dla obszarów FKS, OBG, CTR, KAS
::----------------------------------------------------------------------------------------------------------------------
do();
LANG_SIK.cntx_psh(); REJ.cntx_psh(); AV.cntx_psh(); DEK.cntx_psh(); SKID_WYR.cntx_psh(); FORM.cntx_psh();
REJ_VIEW.cntx_psh(); USERS_KS.cntx_psh();
OKRO_F.cntx_psh(); OKRO_F.prefix();
ROK_F.cntx_psh(); ROK_F.prefix();
OKRO_F_G.cntx_psh();
{? delrok_f & ROK_F.seek(rokf)
|| ROK_F_K.index('LP'); ROK_F_K.prefix(rokf);
   {? ROK_F_K.first()
   || ROK_F_K.prefix();
      ROK_F_K.ROK_F:=null;
      {? ~ROK_F_K.put() || undo() ?}
   ?};
   OSPR.cntx_psh(); OSPR.index('ROK1'); OSPR.prefix(ROK_F.ref());
   {? OSPR.first()
   || {! |?
         {? OSPR.count()>0
         || undo(); 0
         || _delr:=OSPR.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         ?}
      !}
   ?};
   OSPR.cntx_pop();
   OKRO_F.index('ROK'); OKRO_F.prefix(rokf);
   {? OKRO_F.first()
   || {! |?
         LANG_SIK.index('JEZYK'); LANG_SIK.prefix(ref_name(OKRO_F.ref()),#OKRO_F.ref());
         {? LANG_SIK.first()
         || {! |?
               _delr:=LANG_SIK.del(,1);
               {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?};
         OKRO_F_G.index('OKRO_F'); OKRO_F_G.prefix(OKRO_F.ref());
         {? OKRO_F_G.first()
         || {! |?
               {? OKRO_F_G.count()>0
               || undo(); 0
               || _delr:=OKRO_F_G.del(,1);
                  {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
               ?}
            !}
         ?};
         OKRO_F_G.index('OKRO_F_G'); OKRO_F_G.prefix(OKRO_F.ref());
         {? OKRO_F_G.first()
         || {! |?
               {? OKRO_F_G.count()>0
               || undo(); 0
               || _delr:=OKRO_F_G.del(,1);
                  {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
               ?}
            !}
         ?};
         {? OKRO_F.count()<>0
         || undo(); 0
         || _delr:=OKRO_F.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         ?}
      !}
   ?};
   REJ.index('KOD'); REJ.prefix(rokf);
   {? REJ.first() || {! |? exec('del_rej','rejestry') !} ?};
   AV.index('AV'); AV.prefix(rokf);
   {? AV.first()
   || {! |?
         DEK.index('DEK'); DEK.prefix(AV.ref);
         {? DEK.first()
         || {! |?
               SKID_WYR.index('SKID_WYR'); SKID_WYR.prefix(REF.FIRMA,'DEK',DEK.name(),#DEK.ref());
               {? SKID_WYR.first()
               || {! |?
                     _delr:=SKID_WYR.del(,1);
                     {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
                  !}
               ?};
               _delr:=DEK.del(,1);
               {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?};
         _delr:=AV.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?};
   AUTOKSIE.index('NAZ'); AUTOKSIE.prefix(rokf);
   {? AUTOKSIE.first()
   || {! |?
         FORM.index('AUTOKSIE'); FORM.prefix(AUTOKSIE.ref());
         {? FORM.first()
         || {! |?
               SKID_WYR.index('SKID_WYR'); SKID_WYR.prefix(REF.FIRMA,'FORM',FORM.name(),#FORM.ref());
               {? SKID_WYR.first()
               || {! |?
                     _delr:=SKID_WYR.del(,1);
                     {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
                  !}
               ?};
               _delr:=FORM.del(,1);
               {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?};
         _delr:=AUTOKSIE.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?};
   REJ_VIEW.index('DRZEWO'); REJ_VIEW.prefix(rokf,0);
   {? REJ_VIEW.first()
   || fun_del:="REJ_VIEW.cntx_psh(); REJ_VIEW.prefix(_a,#REJ_VIEW.ref());
               {? REJ_VIEW.first()
               || {! |?
                     fun_del(_a);
                     _delr:=REJ_VIEW.del(,1);
                     {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
                  !}
               ?};
               REJ_VIEW.cntx_pop()";
      {! |?
         fun_del(rokf);
         _delr:=REJ_VIEW.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !};
      &fun_del
   ?};
   USERS_KS.index('ROK_F1'); USERS_KS.prefix(rokf);
   {? USERS_KS.first()
   || {! |?
         _delr:=USERS_KS.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?};
   {? ROK_F.count()>0
   || undo()
   || {? ~ROK_F.del(,1) || undo() ?}
   ?}
?};
OKRO_F.cntx_pop(); ROK_F.cntx_pop(); OKRO_F_G.cntx_pop();
LANG_SIK.cntx_pop(); REJ.cntx_pop(); AV.cntx_pop(); DEK.cntx_pop(); SKID_WYR.cntx_pop(); FORM.cntx_pop();
REJ_VIEW.cntx_pop(); USERS_KS.cntx_pop();
end()


\del_okr_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie roku dla obszarów LSP, LZK, LMG
::---------------------------------------------------------------------------------------------------------------------
OKR.cntx_psh();
do();
{? PAR_OKR.LSP || exec('del_okr_obszlog','!zws_par_aokr',o_lsp) ?};
{? PAR_OKR.LZK || exec('del_okr_obszlog','!zws_par_aokr',o_lzk) ?};
{? PAR_OKR.LMG || exec('del_okr_obszlog','!zws_par_aokr',o_lmg) ?};
{? PAR_OKR.LUM || exec('del_okr_obszlog','!zws_par_aokr',o_lum) ?};
{? delrok_l
|| delrok_l:=~exec('czy_okr_obszlog','!zws_par_aokr',o_lsp);
   {? delrok_l || delrok_l:=~exec('czy_okr_obszlog','!zws_par_aokr',o_lzk) ?};
   {? delrok_l || delrok_l:=~exec('czy_okr_obszlog','!zws_par_aokr',o_lmg) ?};
   {? delrok_l || delrok_l:=~exec('czy_okr_obszlog','!zws_par_aokr',o_lum) ?}
?};
{? delrok_l & rokl
|| OKR.index('OKR'); OKR.prefix(REF.FIRMA,rokl);
   {? OKR.first()
   || {! |?
         delrok_l:=~OKR.count();
         delrok_l & OKR.next()
      !}
   ?};
   {? delrok_l & OKR.first()
   ||  {! |?
         _delr:=OKR.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?}
?};
end();
OKR.cntx_pop()


\del_okr_obszlog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie OKR_OBSZ dla OKR i obszaru
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKR2');
OKR.cntx_psh(); OKR.index('OKR'); OKR.prefix(REF.FIRMA,rokl);
{? OKR.first()
|| {! |?
      OKR_OBSZ.prefix(_a,OKR.ref());
      {? OKR_OBSZ.first()
      || {! |?
            {? OKR_OBSZ.OKRO_F<>null
            || OKR_OBSG.index('B_DOMAIN');
               OKR_OBSG.prefix(OKR_OBSZ.OKRO_F().ROK().FIRMA,OKR_OBSZ.B_DOMAIN,OKR_OBSZ.OKRO_F);
               {? OKR_OBSG.first() || OKR_OBSG.del(,1) ?}
            ?};
            _delr:=OKR_OBSZ.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      OKR.next()
   !}
?};
OKR.cntx_pop(); OKR_OBSZ.cntx_pop()


\czy_okr_obszlog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza czy sa OKR_OBSZ dla OKR i obszaru
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKR2');
OKR.cntx_psh(); OKR.index('OKR'); OKR.prefix(REF.FIRMA,rokl);
{? OKR.first()
|| {! |?
      OKR_OBSZ.prefix(_a,OKR.ref());
      _zwrot:=OKR_OBSZ.first();
      ~_zwrot & OKR.next()
   !}
?};
OKR.cntx_pop(); OKR_OBSZ.cntx_pop();
_zwrot


\del_okr_obszfin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie OKR_OBSZ dla OKRO_F i obszaru
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKRO_F2');
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(rokf);
{? OKRO_F.first()
|| {! |?
      OKR_OBSZ.prefix(_a,OKRO_F.ref());
      {? OKR_OBSZ.first()
      || {! |?
            {? OKR_OBSZ.OKRO_F<>null
            || OKR_OBSG.index('B_DOMAIN');
               OKR_OBSG.prefix(OKR_OBSZ.OKRO_F().ROK().FIRMA,OKR_OBSZ.B_DOMAIN,OKR_OBSZ.OKRO_F);
               {? OKR_OBSG.first() || OKR_OBSG.del(,1) ?}
            ?};
            _delr:=OKR_OBSZ.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      OKRO_F.next()
   !}
?};
OKRO_F.cntx_pop(); OKR_OBSZ.cntx_pop()


\czy_okr_obszfin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza czy sa OKR_OBSZ dla OKRO_F i obszaru
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKRO_F2');
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(rokf);
{? OKRO_F.first()
|| {! |?
      OKR_OBSZ.prefix(_a,OKRO_F.ref());
      _zwrot:=OKR_OBSZ.first();
      ~_zwrot & OKRO_F.next()
   !}
?};
OKRO_F.cntx_pop(); OKR_OBSZ.cntx_pop();
_zwrot


\del_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie roku, jeśli jest nieużywany
::----------------------------------------------------------------------------------------------------------------------
_pocz:=_kon:=_delokr:=0;
_okro:=OKRO.ref();
OKROURAP.cntx_psh(); OKROURAP.index('UNIK');
OKRO.cntx_psh(); OKRO.index('UNIK'); OKRO.prefix(OKRO.FIRMA);
{? OKRO.first() || _pocz:=OKRO.ROK ?};
{? OKRO.last() || _kon:=OKRO.ROK ?};
{? _pocz & _kon
|| {! _rok:=_pocz.._kon
   |! OKRO.prefix(OKRO.FIRMA,_rok);
      do();
      {? OKRO.first()
      || _del:=1;
         {! |?
            _del:=(OKRO.count()=0);
            {? ~_del
            || OKROURAP.prefix(OKRO.ref());
               {? OKRO.count()=OKROURAP.size() || _del:=1 ?}
            ?};
            _del & OKRO.next()
         !};
         {? _del & OKRO.first()
         || {! |?
                OKROURAP.prefix(OKRO.ref());
                {? OKROURAP.first()
                || {! |?
                      _delr:=OKROURAP.del(,1);
                      {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
                   !}
                ?};
                _delr:=OKRO.del(,1);
                {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?}
      ?};
      {? do_state()=1 || _delokr:=1 ?};
      end()
   !}
?};
OKRO.cntx_pop(); OKROURAP.cntx_pop();
{? _delokr & ~OKRO.seek(_okro) || OKRO.last() ?}


\czy_del_rok_fin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza, czy można usunąć rok finansów
::   WE: _a - 1 z poziomu usuwania OKRO
::            2 usuwanie lat firmy wyłączeniowej
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| OKR_OBSZ.cntx_psh(); _okro_f:=null;
   {? o_fks
   || OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_fks);
      {? OKR_OBSZ.first()
      || _okro_f:=OKR_OBSZ.OKRO_F;
         edfks:=~exec('next_okro','!zws_par_aokr',o_fks)
      ?}
   ?};
   {? o_obg
   || OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_obg);
      {? OKR_OBSZ.first()
      || {? ~_okro_f || _okro_f:=OKR_OBSZ.OKRO_F ?};
         edobg:=~exec('next_okro','!zws_par_aokr',o_obg)
      ?}
   ?};
   {? o_ctr
   || OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_ctr);
      {? OKR_OBSZ.first()
      || {? ~_okro_f || _okro_f:=OKR_OBSZ.OKRO_F ?};
         edctr:=~exec('next_okro','!zws_par_aokr',o_ctr)
      ?}
   ?};
   {? o_kas
   || OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_kas);
      {? OKR_OBSZ.first()
      || {? ~_okro_f || _okro_f:=OKR_OBSZ.OKRO_F ?};
         edkas:=~exec('next_okro','!zws_par_aokr',o_kas)
      ?}
   ?};
   {? o_fst
   || OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_fst);
      {? OKR_OBSZ.first()
      || {? ~_okro_f || _okro_f:=OKR_OBSZ.OKRO_F ?};
         edfst:=~exec('next_okro','!zws_par_aokr',o_fst)
      ?}
   ?};
   OKR_OBSZ.cntx_pop()
?};
OKRO_F.cntx_psh(); OKRO_F.prefix();
{? (_a=1 & _okro_f & OKRO_F.seek(_okro_f)) | _a=2
|| ROK_F.cntx_psh(); ROK_F.prefix();
   {? _a=1 || OKRO_F.ROK() ?};
   rokf:=ROK_F.ref();
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   delrok_f:=~ROK_F.next();
   ROK_F.cntx_pop();
   {? delrok_f
   || KS.cntx_psh(); KS.index('SYM'); KS.prefix(ROK_F.ref());
      delrok_f:=~KS.first();
      KS.cntx_pop()
   ?};
   {? delrok_f
   || OSPR.cntx_psh(); OSPR.index('ROK1'); OSPR.prefix(ROK_F.ref());
      {? OSPR.first()
      || {! |?
            delrok_f:=(OSPR.count()=0);
            delrok_f & OSPR.next()
         !}
      ?};
      OSPR.cntx_pop()
   ?};
   {? delrok_f
   || DOK.cntx_psh(); EDOKUM.cntx_psh();
      OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
      OKRO_F_G.cntx_psh(); OKRO_F_G.index('OKRO_F_G');
      {? OKRO_F.first()
      || {! |?
            DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2)); DOK.prefix(); delrok_f:=~DOK.first();
            {? delrok_f || EDOKUM.use('skid_v'+ROK_F.KOD); EDOKUM.prefix(); delrok_f:=~EDOKUM.first() ?};
            {? delrok_f
            || {? SRST.name()='srst'+'r'+REF.FIRMA().SYMBOL
               || SRST.index('ROK');
                  SRST.prefix(ROK_F.ref());
                  delrok_f:=~SRST.first()
               ?}
            ?};
            {? delrok_f
            || OKRO_F_G.prefix(OKRO_F.ref());
               {? OKRO_F_G.first()
               || {! |?
                     delrok_f:=(OKRO_F_G.count()=0);
                     delrok_f & OKRO_F_G.next()
                  !}
               ?}
            ?};
            delrok_f & OKRO_F.next()
         !}
      ?};
      OKRO_F.cntx_pop(); DOK.cntx_pop(); EDOKUM.cntx_pop(); OKRO_F_G.cntx_pop()
   ?};
   {? delrok_f
   || REJ.cntx_psh(); REJ.index('KOD'); REJ.prefix(ROK_F.ref());
      {? REJ.first()
      || {! |?
            {? REJ.KOD<>'~BO' & REJ.KOD<>'~BZ' & REJ.KOD<>'~NOTY_WY' & REJ.KOD<>'~NOTY_W2' || delrok_f:=0 ?};
            delrok_f & REJ.next()
         !}
      ?};
      REJ.cntx_pop()
   ?};
   {? delrok_f
   || RAPORT.cntx_psh();
      STANKAS.cntx_psh(); STANKAS.index('KOD'); STANKAS.prefix();
      {? STANKAS.first()
      || {! |?
            RAPORT.use('krp'+form(STANKAS.KOD,-3)+(form(ROK_F.KOD)+2)); RAPORT.prefix();
            delrok_f:=~RAPORT.first();
            delrok_f & STANKAS.next()
         !}
      ?};
      STANKAS.cntx_pop(); RAPORT.cntx_pop()
   ?};
   {? delrok_f
   || K__NAG.cntx_psh(); K__NAG.index('ROK'); K__NAG.prefix(REF.FIRMA,ROK_F.ref());
      delrok_f:=~K__NAG.first();
      K__NAG.cntx_pop()
   ?};
   {? delrok_f
   || K_HARM.cntx_psh(); K_HARM.index('ROKPOCZ'); K_HARM.prefix(REF.FIRMA,ROK_F.ref());
      delrok_f:=~K_HARM.first();
      {? delrok_f
      || K_HARM.index('ROKKON'); K_HARM.prefix(REF.FIRMA,ROK_F.ref());
          delrok_f:=~K_HARM.first()
      ?};
      K_HARM.cntx_pop()
   ?};
   ROK_F.cntx_pop()
?};
OKRO_F.cntx_pop()


\next_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza, czy jest następne dowiązanie okresów do obszarow (dla kolejnego roku obrachunkowego)
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0; _rokn:=null;
OKRO_F.cntx_psh(); OKRO_F.prefix();
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKR_OBSZ.OKRO_F().ROK();
{? ROK_F.next() || _zwrot:=1; _rokn:=ROK_F.ref() ?};
OKRO_F.cntx_pop(); ROK_F.cntx_pop();
{? _zwrot
|| OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKRO_F3'); OKR_OBSZ.prefix(OKRO.FIRMA,_a,_rokn);
   _zwrot:=OKR_OBSZ.first();
   OKR_OBSZ.cntx_pop()
?};
_zwrot


\czy_del_rok_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza, czy można usunąć rok logistyki
::----------------------------------------------------------------------------------------------------------------------
OKR_OBSZ.cntx_psh(); _okr:=null;
{? o_lmg
|| OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_lmg);
   {? OKR_OBSZ.first()
   || _okr:=OKR_OBSZ.OKR;
      edlmg:=~exec('next_okro_log','!zws_par_aokr',o_lmg)
   ?}
?};
{? o_lsp
|| OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_lsp);
   {? OKR_OBSZ.first()
   || {? ~_okr || _okr:=OKR_OBSZ.OKR ?};
      edlsp:=~exec('next_okro_log','!zws_par_aokr',o_lsp)
   ?}
?};
{? o_lzk
|| OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_lzk);
   {? OKR_OBSZ.first()
   || {? ~_okr || _okr:=OKR_OBSZ.OKR ?};
      edlzk:=~exec('next_okro_log','!zws_par_aokr',o_lzk)
   ?}
?};
{? o_lum
|| OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),o_lum);
   {? OKR_OBSZ.first()
   || {? ~_okr || _okr:=OKR_OBSZ.OKR ?};
      edlum:=~exec('next_okro_log','!zws_par_aokr',o_lum)
   ?}
?};
OKR.cntx_psh(); OKR.prefix();
{? _okr & OKR.seek(_okr)
|| _next:=OKR.ROK+1;
   OKR.cntx_psh(); OKR.index('OKR'); OKR.prefix(REF.FIRMA,_next);
   delrok_l:=~OKR.first();
   OKR.cntx_pop();
   rokl:=OKR.ROK;
   {? delrok_l
   || OKR.cntx_psh();
      delrok_l:=(exec('FindInSet','#table','PS','R',rokl)=null);
      OKR.cntx_pop()
   ?};
   {? delrok_l
   || OKR.cntx_psh(); OKR.index('OKR'); OKR.prefix(REF.FIRMA,rokl);
      {? OKR.first()
      || {! |?
            delrok_l:=~(OKR.ZAM='TTTT');
            {? delrok_l
            || OKRD.cntx_psh(); OKRD.index('OKR'); OKRD.prefix(OKR.ref());
               delrok_l:=~OKRD.first();
               OKRD.cntx_pop()
            ?};
            delrok_l & OKR.next()
         !}
      ?};
      OKR.cntx_pop()
   ?};
   {? delrok_l
   || ODDZ.cntx_psh(); ODDZ.index('KOD'); ODDZ.prefix();
      {? ODDZ.first()
      || {! |?
            ND.cntx_psh();
            ND.use('nagdo'+ODDZ.KOD+(2-$rokl)); ND.prefix(); delrok_l:=~ND.size();
            ND.cntx_pop();
            {? delrok_l
            || FAKS.cntx_psh();
               FAKS.use('faktu'+ODDZ.KOD+(2-$rokl)); FAKS.prefix(); delrok_l:=~FAKS.size();
               FAKS.cntx_pop()
            ?};
            {? delrok_l
            || ZLP.cntx_psh();
               _mc:=0;
               {!
               |? _mc+=1;
                  ZLP.use('zlp'+ODDZ.KOD+(2-$rokl)+form(_mc,-2,0,'99')); ZLP.prefix(); delrok_l:=~ZLP.size();
                  delrok_l & _mc<12
               !};
               ZLP.cntx_pop()
            ?};
            delrok_l & ODDZ.next
         !}
      ?};
      ODDZ.cntx_pop()
   ?}
?};
OKR_OBSZ.cntx_pop(); OKR.cntx_pop()


\next_okro_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza, czy jest następne dowiązanie okresów do obszarów (dla kolejnego roku logistycznego)
::   WE: _a - ref obszaru
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
OKR.cntx_psh(); OKR.index('OKR');
_next:=OKR_OBSZ.OKR().ROK+1;
OKR.prefix(REF.FIRMA,_next);
{? OKR.first() || _zwrot:=1 ?};
OKR.cntx_pop();
{? _zwrot
|| OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKR1'); OKR_OBSZ.prefix(OKRO.FIRMA,_a,OKRO.ROK+1,_next);
   _zwrot:=OKR_OBSZ.first();
   OKR_OBSZ.cntx_pop()
?};
_zwrot


\spr_rapobsz_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzenie, czy dla RAPOBSZ jest włączona licencja
::----------------------------------------------------------------------------------------------------------------------
1


\rkprz_rapobsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rekord przed - obszary analiz
::----------------------------------------------------------------------------------------------------------------------
RAPOBSZ.cntx_psh(); RAPOBSZ.prefix();
OKROURAP.cntx_psh(); OKROURAP.index('UNIK');
{! _i:=1..size_o
|! {? RAPOBSZ.seek(rap_obj[_i])
   || OKROURAP.prefix(OKRO.ref(),RAPOBSZ.ref());
      ($('PAR_OKR.OBSZAR'+form(_i,-2)+':=OKROURAP.first()'))()
   ?}
!};
OKROURAP.cntx_pop(); RAPOBSZ.cntx_pop();
OKRO.NR_OKR=1


\edit_rapobsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów obszarów analiz
::----------------------------------------------------------------------------------------------------------------------
{? OKRO.sel_size()
|| OKROURAP.cntx_psh(); OKROURAP.index('UNIK');
   RAPOBSZ.cntx_psh(); RAPOBSZ.index('UNIK');
   {? TAB_TMP.first()
   || {! |?
         exec('check_rapobsz','!zws_par_aokr');
         TAB_TMP.next()
      !}
   ?};
   RAPOBSZ.cntx_pop(); OKROURAP.cntx_pop()
|| exec('tab_tmp','!zws_par_aokr');
   VAR_DEL.delete('TAB_TMP')
?}


\tab_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów obszarów analiz - tabelka tymczasowa
::----------------------------------------------------------------------------------------------------------------------
TAB_TMP:=tab_tmp(1,'NAZWA','STRING[12]','Nazwa obszaru',
                   'OBSZ_WL','INTEGER',''
                );
RAPOBSZ.cntx_psh(); RAPOBSZ.index('UNIK'); RAPOBSZ.prefix({? zakl_tmp=1 || 'O' || 'C' ?});
{? RAPOBSZ.first()
|| {! |?
      {? exec('spr_rapobsz_lic','!zws_par_aokr')
      || {? ~OKRO.sel_size()
         || OKROURAP.cntx_psh(); OKROURAP.index('UNIK'); OKROURAP.prefix(OKRO.ref(),RAPOBSZ.ref());
            TAB_TMP.OBSZ_WL:=OKROURAP.first();
            OKROURAP.cntx_pop()
         ?};
         TAB_TMP.NAZWA:=RAPOBSZ.NAZWA; TAB_TMP.add()
      ?};
      RAPOBSZ.next()
   !}
?};
RAPOBSZ.cntx_pop();
_okno:=TAB_TMP.mk_sel({? zakl_tmp=1 || 'OLAP' || 'QlikView' ?},,,'rapobsztmp_wer',,,TAB_TMP.size());
TAB_TMP.win_fld(_okno,,'NAZWA',,,12,,,'Nazwa obszaru'@);
TAB_TMP.win_fld(_okno,,'OBSZ_WL',,,,,,'Wł.'@,,,2,,"1","0");
TAB_TMP.win_act(_okno,,'Formuła','Włącz/wyłącz'@@,,'Włączanie / wyłączanie obszaru dla okresów'@,,
                "TAB_TMP.OBSZ_WL:=~TAB_TMP.OBSZ_WL; TAB_TMP.put();
                {? ~OKRO.sel_size() ||  exec('check_rapobsz','!zws_par_aokr') ?}",1,,,,'W');
TAB_TMP.win_sel(_okno);
TAB_TMP.select()


\edit_rapobsz_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów obszarów analiz - grupa przed
::----------------------------------------------------------------------------------------------------------------------
exec('tab_tmp','!zws_par_aokr');
{? FUN.ask('Ustawić parametry dla obszarów?'@)
|| 1
|| VAR_DEL.delete('TAB_TMP'); 0
?}


\edit_rapobsz_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów obszarów analiz - grupa po
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TAB_TMP'); 1


\check_rapobsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów obszarów analiz
::----------------------------------------------------------------------------------------------------------------------
OKROURAP.cntx_psh(); OKROURAP.index('UNIK');
RAPOBSZ.cntx_psh(); RAPOBSZ.index('UNIK');
RAPOBSZ.prefix({? zakl_tmp=1 || 'O' || 'C' ?},TAB_TMP.NAZWA,);
{? RAPOBSZ.first()
|| OKROURAP.prefix(OKRO.ref(),RAPOBSZ.ref());
   {? ~TAB_TMP.OBSZ_WL & OKROURAP.first()
   || OKROURAP.del()
   |? TAB_TMP.OBSZ_WL & ~OKROURAP.first()
   || OKROURAP.blank(1);
      OKROURAP.OKRO:=OKRO.ref();
      OKROURAP.RAPOBSZ:=RAPOBSZ.ref();
      OKROURAP.add(1)
   ?}
?};
OKROURAP.cntx_pop(); RAPOBSZ.cntx_pop()


\rapobsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodawanie rekordów w tabeli RAPOBSZ
::----------------------------------------------------------------------------------------------------------------------
RAPOBSZ.cntx_psh(); RAPOBSZ.index('UNIK'); RAPOBSZ.prefix();
{? ~RAPOBSZ.find_key('O','Finanse',)
|| RAPOBSZ.RODZAJ:='O'; RAPOBSZ.NAZWA:='Finanse'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('O','Sprzedaż',)
|| RAPOBSZ.RODZAJ:='O'; RAPOBSZ.NAZWA:='Sprzedaż'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('O','Zakup',)
|| RAPOBSZ.RODZAJ:='O'; RAPOBSZ.NAZWA:='Zakup'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('O','Produkcja',)
|| RAPOBSZ.RODZAJ:='O'; RAPOBSZ.NAZWA:='Produkcja'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('O','Magazyny',)
|| RAPOBSZ.RODZAJ:='O'; RAPOBSZ.NAZWA:='Magazyny'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('O','Zam. sprz.',)
|| RAPOBSZ.RODZAJ:='O'; RAPOBSZ.NAZWA:='Zam. sprz.'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('O','Personel',)
|| RAPOBSZ.RODZAJ:='O'; RAPOBSZ.NAZWA:='Personel'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('C','Finanse',)
|| RAPOBSZ.RODZAJ:='C'; RAPOBSZ.NAZWA:='Finanse'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('C','Sprzedaż',)
|| RAPOBSZ.RODZAJ:='C'; RAPOBSZ.NAZWA:='Sprzedaż'; RAPOBSZ.add(1)
?};
{? ~RAPOBSZ.find_key('C','Personel',)
|| RAPOBSZ.RODZAJ:='C'; RAPOBSZ.NAZWA:='Personel'; RAPOBSZ.add(1)
?};
RAPOBSZ.cntx_pop()


\BL_AM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: blank: zwraca aktualny miesiąc
::  OLD: \BL_AM/estra.fml
::----------------------------------------------------------------------------------------------------------------------
SSTALE.AO().OES


\BL_AR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: blank: zwraca aktualny rok
::  OLD: \BL_AR/estra.fml
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.AO<>null || SSTALE.AO().RES || 0 ?}


\ospr_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Usuwanie rekordu w tabeli OSPR
::  OLD: \ospr_del/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? OSPR.sel_size() |
   FUN.ask('Podczas operacji usuwania okresu raportowania\n'
           'zostaną również usunięte wartości wierszy.'@)
|| _err:=0;
   do();
   echo('Trwa usuwanie okresu raportowania "%1"'@[OSPR.NAZWA]);
   _rok:=OSPR.ROK().KOD;
   WARTOSCI.cntx_psh();
   WARTOSCI.use('wsik_'+_rok); WARTOSCI.index('OKRES'); WARTOSCI.prefix(OSPR.ref());
   {? WARTOSCI.first() || {! |? WARTOSCI.del() !} ?};
   WARTOSCI.use('wsik___'); WARTOSCI.index('OKRES'); WARTOSCI.prefix(OSPR.ref());
   {? WARTOSCI.first() || {! |? WARTOSCI.del() !} ?};
   W_ALGPAR.cntx_psh();
   W_ALGPAR.use('walg_'+_rok); W_ALGPAR.index('OSPR'); W_ALGPAR.prefix(OSPR.ref());
   {? W_ALGPAR.first() || {! |? W_ALGPAR.del() !} ?};
   WARIANTY.cntx_psh(); WARIANTY.index('STALE'); WARIANTY.prefix(REF.FIRMA);
   {? WARIANTY.first()
   || {! |?
         _p:=0;
         {? WARIANTY.OSPR=OSPR.ref()  || WARIANTY.OSPR:=null; _p:=1 ?};
         {? WARIANTY.OSPR2=OSPR.ref() || WARIANTY.OSPR2:=null; _p:=1 ?};
         {? WARIANTY.SOSPR=OSPR.ref() || WARIANTY.SOSPR:=null; _p:=1 ?};
         {? WARIANTY.OKRESANA=OSPR.ref() || WARIANTY.OKRESANA:=null; _p:=1 ?};
         {? SSTALE.OSPR=OSPR.ref()    || SSTALE.OSPR:=null ?};
         {? SSTALE.OSPR2=OSPR.ref()   || SSTALE.OSPR2:=null ?};
         {? _p || WARIANTY.put() ?};
         WARIANTY.next()
      !}
   ?};
   WARIANTY.cntx_pop();
   WERSJE.cntx_psh(); WERSJE.index('KOD'); WERSJE.prefix();
   {? WERSJE.first()
   || {!
      |? {? WERSJE.OSPR=OSPR.ref() || WERSJE.OSPR:=null; WERSJE.put() ?};
         WERSJE.next()
      !}
   ?};
   WERSJE.cntx_pop();
   exec('del_lang','lang',OSPR.ref());
   {? OSPR.count()
   || _err:=1; undo()
   || OSPR.del()
   ?};
   W_ALGPAR.cntx_pop(); WARTOSCI.cntx_pop();
   echo();
   {? OSPR.sel_size()
   || ospr_ok+=end()
   || end();
      {? _err || FUN.info('Okres raportowania jest wykorzystywany w innym systemie.'@) ?}
   ?}
?}


\typokres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Wartość początkowa OSPR.TYP
::  OLD: \typokres/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? type_of(cur_tab)>100 & cur_tab=WARIANTY
|| {? SSTALE.WARIANT=3 || '' || SSTALE.TYP ?}
|| OKRESY.TYP
?}


\okres_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: po redakcji  pola OSPR.OKRES_OD i OSPR_OKRES_DO
::  OLD: \okres_po/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld=null
|| FUN.info('Nie wybrano okresu ze słownika.'@); 0
|| {? OSPR.OKRES_OD=OSPR.OKRES_DO
   || OSPR.NAZWA:=OSPR.ROK().NAZ+'-'+form(OSPR.OKRES_OD().NR,2,0)+' ('+OSPR.OKRES_OD().NAZ+')'
   || OSPR.NAZWA:=OSPR.ROK().NAZ+' '+form(OSPR.OKRES_OD().NR)+ '-'+form(OSPR.OKRES_DO().NR)
   ?};
   exec('skr_ospr','okresy',1); 1
?}


\ae_osprt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Po redakcji pola TYP tabeli OSPR
::  OLD: \ae_osprt/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? OSPR.TYP<>1+OSPR.OKRES().KOD || exec('okr_ospr','okresy'); win_disp() ?}; 1


\be_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przed redakcją pole OKRES tabeli OSPR
::  OLD: \be_okres/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? OSPR.OKRES=null & OSPR.TYP<>'I'
|| exec('okr_ospr','okresy')
|? OSPR.TYP='I'
|| OSPR.OKRES:=null
?};
_r:=OSPR.TYP<>'I' & (OSPR.OKRES_OD().POCZ<>date(0,0,0) | OSPR.OKRES_OD<>OSPR.OKRES_DO);
{? _r || SLU.index('WZORZEC'); SLU.prefix('prosty','~TYPY OKRESÓW'); SLU.first() ?}


\zak_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Zakończenie i otwarcie okresu sprawozdawczego
::   WE: _a - 1 - zamknięcie okresu
::            2 - otwarcie okresu
::  OLD: \zak_ospr/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? OSPR.ZAM='T'
   || FUN.info('Okres jest zamknięty.'@)
   || {? FUN.ask('Zamknąć okres raportowania?'@) || OSPR.ZAM:='T'; OSPR.put() ?}
   ?}
|| {? OSPR.ZAM='N'
   || FUN.info('Okres jest otwarty.'@)
   || {? FUN.ask('Otworzyć okres raportowania?'@) || OSPR.ZAM:='N'; OSPR.put() ?}
   ?}
?}


\bv_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja przed wyświetl okna OKRO tabeli OKRO
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('OkrTab','__LIC','lic_ind1');
exec('proenv','#b_proman');
__LIC:=__proenv.DOM_LIC;
__LIC.cntx_psh();
lic_ind1:=__LIC.ndx_tmp(,1,'SYMBOL',,); __LIC.index(lic_ind1);
exec('ustal_licencje','!zws_par_aokr');
OkrTab:=tab_tmp(1,
   'DOMAIN','STRING[35]','Obszar',
   'U','STRING[1]','Użytkowany',
   'Z','STRING[1]','Zamknięty'
);
_add:="
   OkrTab.blank(1);
   OkrTab.DOMAIN:=_a;
   OkrTab.U:={? _b='otwarty' || 'T' || 'N' ?};
   OkrTab.Z:={? _b='zamknięty' || 'T' || 'N' ?};
   OkrTab.add()
";
exec('rkprz_okro','!zws_par_aokr');
_add('Finanse',PAR_OKR.STATFKS);
_add('Controlling',PAR_OKR.STATCTR);
_add('Obiegi',PAR_OKR.STATOBG);
_add('Magazyny',PAR_OKR.STATLMG);
_add('Sprzedaż',PAR_OKR.STATLSP);
_add('Zakupy',PAR_OKR.STATLZK);
_add('Środki trwałe',PAR_OKR.STATFST);
_add('Kasa',PAR_OKR.STATKAS);
_o:=OkrTab.mk_sel('Status okresu'@);
OkrTab.win_fld(_o,,'DOMAIN');
OkrTab.win_fld(_o,,'U',,,,,,,,,2,,"'T'","'N'");
OkrTab.win_fld(_o,,'Z',,,,,,,,,2,,"'T'","'N'");
OkrTab.win_sel(_o);
_grp:=OKRO.grp_make('Okres'@,"OkrTab.first();grp_disp(OkrTab,OkrTab.win_sel('?'),1)",'#okrodisp1',,,,,'normal');
OKRO.grp_edit(_grp,,'DISP',,,,,,'maximized');
OKRO.grp_splt(_grp,,'horizontal','down');
OKRO.grp_sel(_grp,OkrTab,_o,,,,,,,,,,'maximized');
OKRO.cntx_psh();
OKRO.win_sel(_grp);
OKRO.select();
OKRO.cntx_pop();
__LIC.cntx_pop();
VAR_DEL.delete('OkrTab','__LIC','lic_ind1')


\set_okr_srodki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła ustawia okresy i lata podatkowe dla środków trwałych
::       w tabeli okresów obszaru FKS (OKRO_F)
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
OKRO_F.index('FIRMA_NR');
OKRO_F.prefix(REF.FIRMA,);
{? OKRO_F.first()
|| _res:=OKRO_F.ROK().POCZ_ROK~1;
   _rok:=null;
   {! |?
:: jak długi rok?
      {? _rok<>OKRO_F.ROK
      || {? _rok<>null || _res+=1 ?};
         _rok:=OKRO_F.ROK
      ?};
      OKRO_F.RES:=_res;
      OKRO_F.cntx_psh();
      OKRO_F.index('ROK');
      OKRO_F.prefix(_rok);
      _size:=OKRO_F.size()-2;
      OKRO_F.cntx_pop();
      {? _size=12
      || {? OKRO_F.NR<13 || OKRO_F.OES:=OKRO_F.NR || OKRO_F.OES:=13 ?}
      |? _size<12
      || {? OKRO_F.NR=0
         || OKRO_F.OES:=0
         |? OKRO_F.NR=_size+1
         || OKRO_F.OES:=13
         || OKRO_F.OES:=OKRO_F.NR+(12-_size)
         ?}
      |? _size>12
      || {? OKRO_F.NR=0
         || OKRO_F.OES:=0
         |? OKRO_F.NR=_size+1
         || OKRO_F.OES:=13
         || {? OKRO_F.NR<=(_size-12)
            || OKRO_F.OES:=OKRO_F.NR+(12-(_size-12));
               {? OKRO_F.NR=(_size-12) || _res+=1 ?}
            || OKRO_F.OES:=OKRO_F.NR-(_size-12)
            ?}
         ?}
      ?};
      OKRO_F.TAB_KRST:=exec('tab_krst','!zws_par_aokr',OKRO_F.RES);
      OKRO_F.cntx_psh(); OKRO_F.prefix(); OKRO_F.put(); OKRO_F.cntx_pop();
      OKRO_F.next()
   !}
?};
OKRO_F.cntx_pop()


\be_okro_oes
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją numeru roku i okresu dla środków trwałych
::   WY: 1/0 (czy można poprawiać wartość pola)
::----------------------------------------------------------------------------------------------------------------------
_wy:=1;
{? SRST.name()='srst'+'r'+REF.FIRMA().SYMBOL
|| SRST.cntx_psh();
   SRST.index('ROK');
   SRST.prefix(OKRO_F.ROK);
   {? SRST.size()>0 || _wy:=0 ?};
   SRST.cntx_pop()
?};
_wy


\ae_okro_f_g_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Po redakcji OKRO_F_G.FIRMA
::  OLD: \ae_okro_f_g_f/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? OKRO_F_G.FIRMA<>null
|| {? OKRO_F_G.ROK_F<>null & OKRO_F_G.ROK_F().FIRMA<>OKRO_F_G.FIRMA
   || OKRO_F_G.ROK_F:=null;
      OKRO_F_G.OKRO_F:=null
   ?};
   {? OKRO_F_G.ROK_F=null
   || ROK_F.cntx_psh();
      ROK_F.seek(__RokF);
      _d1:=ROK_F.POCZ_ROK;
      _d2:=ROK_F.KON_ROK;
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(OKRO_F_G.FIRMA,_d1,_d2);
      {? ROK_F.first()
      || OKRO_F_G.ROK_F:=ROK_F.ref();
         OKRO_F.seek(__OkroF);
         _d1:=OKRO_F.POCZ;
         _d2:=OKRO_F.KON;
         OKRO_F.index('KON'); OKRO_F.prefix(OKRO_F_G.FIRMA,_d2,_d1);
         {? OKRO_F.first()
         || OKRO_F_G.OKRO_F:=OKRO_F.ref()
         ?}
      ?};
      ROK_F.cntx_pop()
   ?};
   REF.FIRMA:=OKRO_F_G.FIRMA
?};
exec('okro_f_g_enabled','!zws_par_aokr');
1


\be_okro_f_g_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Po redakcji OKRO_F_G.OKRO_F
::  OLD: \be_okro_f_g_r/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F_G.FIRMA<>null


\ae_okro_f_g_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Po redakcji OKRO_F_G.ROK_F
::  OLD: \ae_okro_f_g_r/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F_G.ROK_F();
{? OKRO_F_G.OKRO_F().ROK<>ROK_F.ref()
|| OKRO_F_G.OKRO_F:=null
?};
exec('okro_f_g_enabled','!zws_par_aokr');
1


\be_okro_f_g_o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Po redakcji OKRO_F_G.OKRO_F
::  OLD: \be_okro_f_g_o/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F_G.ROK_F<>null


\ar_okro_f_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Rekord po OKRO_F_G
::  OLD: \ar_okro_f_g/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.record2(OKRO_F_G,
   'FIRMA','Firma',
   'ROK_F','Rok',
   'OKRO_F','Okres'
)


\bl_okro_f_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Wartosc poczatkowa OKRO_F_G.OKRO_F_G
::  OLD: \bl_okro_f_g/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.ref()


\bm_okro_f_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dolacz/popraw OKRO_F_G
::  OLD: \bm_okro_f_g/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
REF.cntx_psh();
_pop:=-menu_txt()='popraw';
OKRO_F_G.win_edit('RED');
{? ~_pop
|| OKRO_F_G.blank(1);
   OKRO_F_G.ROK_F_G:=__RokF;
   OKRO_F_G.OKRO_F_G:=__OkroF
?};
OKRO_F.cntx_psh();
exec('okro_f_g_enabled','!zws_par_aokr');
{? OKRO_F_G.edit("exec('ar_okro_f_g','!zws_par_aokr')")
|| {? _pop
   || OKRO_F_G.put()
   || OKRO_F_G.add()
   ?}
?};
OKRO_F.cntx_pop();
REF.cntx_pop()


\okro_f_g_enabled
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawienie dostępności pól okna OKRO_F_G
::----------------------------------------------------------------------------------------------------------------------
OKRO_F_G.efld_opt('RED','enable='+$(OKRO_F_G.FIRMA<>null),OKRO_F_G,'ROK_F');
OKRO_F_G.efld_opt('RED','enable='+$(OKRO_F_G.ROK_F<>null),OKRO_F_G,'OKRO_F');
~~


\usr_tab_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: Formuła uzupełniająca tabelę z użytkownikami
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh();
USERS.index('WEBLOGIN');
USERS.prefix();
B_OKRO_F.cntx_psh();
B_OKRO_F.index('USERS');
B_OKRO_F.prefix();
usrtab.erase();
{? USERS.first()
|| {!
   |? usrtab.blank();
      usrtab.LOGIN:=USERS.WEBLOGIN;
      usrtab.DANE:=USERS.DANE;
      B_OKRO_F.prefix(USERS.ref(),OKRO_F.ref());
      {? B_OKRO_F.first()
      || usrtab.ZAM_WAR:='T';
         usrtab.OST_ZAM:=B_OKRO_F.ZAM;
         usrtab.OST_OTW:=B_OKRO_F.OTW
      || usrtab.ZAM_WAR:='N';
         usrtab.OST_ZAM:='';
         usrtab.OST_OTW:=''
      ?};
      usrtab.REF:=USERS.uidref();
      usrtab.add();
      USERS.next()
   !}
?};
B_OKRO_F.cntx_pop();
USERS.cntx_pop();
usrtab.first()


\usr_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: Formuła tworząca tabelę z użytkownikami
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,'LOGIN','STRING[100]','Login','DANE','STRING[50]','Dane użytkownika',
      'ZAM_WAR','STRING[1]','Zablokowany','OST_ZAM','STRING[48]','Ostatnie zablokowanie',
      'OST_OTW','STRING[48]','Ostatnie otwarcie','REF','STRING[48]','Ref');
_tab


\zabl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: blokuje użytkownika we wskazanym okresie
::   WE: _a - USERS.ref()
::       _b - OKRO_F.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
B_OKRO_F.cntx_psh(); USERS.cntx_psh();
B_OKRO_F.index('USERS'); B_OKRO_F.prefix();
USERS.prefix();
{? usrtab.ZAM_WAR<>'T' & USERS.seek(usrtab.REF)
|| B_OKRO_F.blank();
   B_OKRO_F.OKRO_F:=OKRO_F.ref();
   B_OKRO_F.USERS:=USERS.ref();
   B_OKRO_F.STATUS:='T';
   USERS.cntx_psh();
   B_OKRO_F.ZAM:=$date()+'|'+time()$3+'|'+OPERATOR.USER().WEBLOGIN;
   USERS.cntx_pop();
   B_OKRO_F.add();
   usrtab.ZAM_WAR:='T';
   usrtab.OST_ZAM:=B_OKRO_F.ZAM;
   usrtab.put()
?};
B_OKRO_F.cntx_pop(); USERS.cntx_pop();
1


\odbl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: odblokowuje użytkownika we wskazanym okresie
::   WE: _a - USERS.ref()
::       _b - OKRO_F.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
B_OKRO_F.cntx_psh(); USERS.cntx_psh();
B_OKRO_F.index('USERS'); B_OKRO_F.prefix();
USERS.prefix();
{? usrtab.ZAM_WAR<>'N' & USERS.seek(usrtab.REF)
|| B_OKRO_F.prefix(USERS.ref(),OKRO_F.ref());
   {? B_OKRO_F.first()
   || B_OKRO_F.del()
   ?};
   usrtab.ZAM_WAR:='N';
   usrtab.OST_ZAM:='';
::   usrtab.OST_OTW:=B_OKRO_F.OTW;
   usrtab.put()
?};
B_OKRO_F.cntx_pop(); USERS.cntx_pop();
1


\zabl_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: blokuje wskazany okres
::   WE: _a - OKRO_F.ref()
::       _b - USERS.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.ZAM_WAR:='T';
OKRO_F.put();
1


\odbl_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: odblokowuje wskazany okres
::   WE: _a - OKRO_F.ref()
::       _b - USERS.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.ZAM_WAR:='N';
OKRO_F.put();
1


\is_okr_blck
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: Sprawdza, czy wskazany okres jest zablokowany/użytkownik jest zablokowany we wskazanym okresie
::   WE: _a - OKRO_F.ref()
::       _b - USERS.ref()
::       [_c] - czy wyświetlać komunikat (domyślnie 1)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
{? var_press('_c')>0 || _msg:=_c || _msg:=1 ?};
OKRO_F.cntx_psh();
OKRO_F.prefix();
{? OKRO_F.seek(_a)
|| {? -OKRO_F.ZAM_WAR='t'
   || {? exec('chk_role','#b__box',_b,'FKS_DKS_BOKR')
      || _ret:=0
      || {? _msg || FUN.info('Okres zamknięty warunkowo.'@) ?};
         _ret:=1
      ?}
   || B_OKRO_F.cntx_psh();
      B_OKRO_F.index('USERS');
      B_OKRO_F.prefix(_b,_a);
      {? B_OKRO_F.first() & -B_OKRO_F.STATUS='t'
      || {? _msg || FUN.info('Masz zablokowany warunkowo dostęp do tego okresu.\nSzczegóły: %1'@[B_OKRO_F.ZAM]) ?};
         _ret:=1
      ?};
      B_OKRO_F.cntx_pop()
   ?}
?};
OKRO_F.cntx_pop();
_ret


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 4d7e3f576da728d302ffaeb29fbeb0d6aa5a91384f508b303a14cb0e85a1df074e9810e9735f4ce76cc8b45ff6cf44566336ece8dd4dc4f7257dfe72f0a02572434c3dd7d1687a39c1463779312c1b543a24642b8322c98614fc642edfa3c4ed1f4c87707a2d42d10179839b42d5f967ff065fdbfce2df442a82cd6c825d6783

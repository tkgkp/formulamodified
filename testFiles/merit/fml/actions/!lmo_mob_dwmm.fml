:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmo_mob_dwmm.fml
:: Utworzony: 06.02.2023
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Czynność LMO_MOB_DWMM - Operacja przesunięcia w magazynie
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# parses=exec('parses_nd','lmg')
::# kind=WE,  symbol=ND,      type=_ND,      name=Dokument magazynowy,           required=T,   keyref=T
::# kind=WE,  symbol=NRC,     type=NUMBER,   name=Nr czytnika,                   required=N,   fml_val="EANX.NRC_Z:=0;exec('wyb_czyt','magazyn_mob',1,'EANX.NRC_Z');EANX.NRC_Z"
::# kind=WEW, symbol=EANN,    type=_EANN,    name=Operacja mobilna,              required=N
::# kind=WY,  symbol=EANN,    type=_EANN,    name=Operacja mobilna,              required=N
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

exec('init','lmo');

_akcja:=_mp.akcja();
_auto:=_akcja<>'Do wysyłki' & _mp.isAutoRun();
_proc:=~_mp.isMicro();

_mp.trigRef('EANN',,1,,exec('kind_internal','#b_port'),'EANN');

_nrc:={? var_pres('NRC',_in)=type_of(0) || _in.NRC || -1 ?};

{? ~(var_pres('ND',_in)=type_of(null()) & _in.ND)
|| _mp.error('Brak wymaganego parametru ND.')
||
   _continue:=0;
   _eann:=null();
   _ctrl:=1;

   ND.cntx_psh();
   _jest:=ND.seek(_in.ND);
   {? ~_jest || ND.prefix(); _jest:=ND.seek(_in.ND) ?};
   {? ~_jest
   || _mp.error('Nie znaleziono dokumentu magazynowego.')
   |? ~exec('ctrlND','magazyn_mobi',0)
   || _mp.cancel()
   || {? _akcja='Do wysyłki'
         | _auto
         | _mp.pathTodo()
      || _eann:=exec('wysND','magazyn_mobi',_auto,_nrc,0,_proc)
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   ND.cntx_pop();

   {? _eann<>null()
:: Zapisanie parametru wyjściowego EANN, wykluczenie kolejnej realizacji z pętli, zakończenie czynności
   || _mp.save(exec('kind_out','#b_port'),'EANN',EANN.ref());
      _mp.done()
   ?}
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_in','#b_port'));

_wgr:={? var_pres('ND',_in)<>type_of(~~) & _in.ND
      || _in.ND
      || null()
      ?};

{? _wgr<>null()
|| 'Zarejestruj operację przesunięcia w magazynie dla dokumentu %1'@@[exec('record','#to_string',_wgr)]
|| 'Zarejestruj operację przesunięcia w magazynie'@@
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 3d33a57f280fb0ddffe6f69c25801cc226386fdf03ca22f96af26ba3f3ec6f1825fd5c0d11a17961194ac8cb6696e5e2c0a3d2147162529bef72f03821e8ee5a9d5c1ae557edf71063aa713bcf4c5c12c28ea1c4313358c3271e58fe36adecb774ab30c245ca789f332cd1caa421250e6e173d51968a2351980ed7ed8f3f84f9

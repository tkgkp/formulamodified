:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ruc_stdk.fml
:: Utworzony: 20.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RUC_STDK - Dekretacja planu umowy sprzedaży wierzytelności
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja planu umowy sprzedaży wierzytelności - główna formuła czynności FKS_RUC_STDK
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_par_nag','rozrach')
::# access=exec('upr_par_nag','rozrach')
::# kind=WE, symbol=PAR_NAG, type=_PAR_NAG, name=Wskazanie na nagłówek planu sprzedaży wierzytelności, required=T, keyref=T
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='S';
{? _mp.pathTodo() | _mp.isAutoRun()
|| {? var_press('PAR_NAG',_in)>0
   || _ref:=BB.sqlint($_in.PAR_NAG); _nam:=form(($_in.PAR_NAG)-8);
      {? _ref<>0 & _nam<>''
      || PAR_NAG.cntx_psh(); PAR_NAG.prefix();
         {? PAR_NAG.seek(_ref,_nam)
         || {? PAR_NAG.TYP=SER_KOR.TYP
            || params_exec('kom_dek','rozrach')
            || FUN.info('Wybrana umowa nie jest umową sprzedaży wierzytelności.'@); _mp.error()
            ?}
         || {? ~_mp.isAutoRun()
            || FUN.info('Nie znaleziono umowy sprzedaży wierzytelności.'@)
            ?};
            _out.DOK:=null;
            _mp.save(,_out);
            _mp.done()
         ?};
         PAR_NAG.cntx_pop()
      || {? ~_mp.isAutoRun()
         || FUN.info('Nie znaleziono umowy sprzedaży wierzytelności.'@)
         ?};
         _out.DOK:=null;
         _mp.save(,_out);
         _mp.done()
      ?}
   || {? ~_mp.isAutoRun()
      || FUN.info('Nie znaleziono umowy sprzedaży wierzytelności.'@)
      ?};
      _out.DOK:=null;
      _mp.save(,_out);
      _mp.done()
   ?}
|? _akcja='Dekretuj'
|| params_exec('kom_dek','rozrach')
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zadekretuj umowę sprzedaży wierzytelności'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('PAR_NAG',_we)
|| _ref:=BB.sqlint($_we.PAR_NAG); _nam:=form(($_we.PAR_NAG)-8);
   {? _ref<>0 & _nam<>''
   || PAR_NAG.cntx_psh(); PAR_NAG.use(_nam); PAR_NAG.prefix();
      {? PAR_NAG.seek(_ref,_nam)
      || _desc:='Zadekretuj umowę sprzedaży wierzytelności: %1 z dnia %2'@@[PAR_NAG.PAR_UM().SYM,$PAR_NAG.DATA]
      ?};
      PAR_NAG.cntx_pop()
   ?}
?};
_desc


\umsprw_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja planu umowy sprzedaży wierzytelności
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RUC_STDK';
_params.AKCJA:='Dekretuj';
_params.UIDREF:=PAR_NAG.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.PROC_START:='N';
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'PAR_NAG',PAR_NAG.ref());
exec('mp_run','#b__box',_params)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'parnag'>0
         || _record:=exec('FindAndGet','#table',PAR_NAG,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 a143226e0838e0a16b249681198a3887998bec311e0132b3781596466a6501077fbbf600f2e01cc8d6cff18346a983c226fa3c8a976afb7551629d2dee65f63a2e083a96b8640c17b4422707a6d5911600c2a4518cfed55134c9d5f2da210d5165ec545066df9ee99aab2c332172bb06cefb41cadd7e4309fcf115e0a0fa4bf5

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_faw_flzw.fml
:: Utworzony: 15.12.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_FLZW - Tworzenie wniosku w obiegu na podstawie zamówienia wewnętrznego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Tworzenie wniosku w obiegu na podstawie zamówienia wewnętrznego - główna formuła czynności OBE_FAW_FLZW
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,ODDZ,LZK

:: PARAMETRY WE:
::# kind=WE, symbol=ZK_N, type=_ZK_N, name=Wskazanie na nagłówek zamówienia zewnętrznego, required=N, keyref=T
::# kind=WE, symbol=ETYPY, type=STRING, name=Wskazanie na typ obiegu, required=N, fml_val="exec('typ_obi','obiegi_log','M',_a)", fml_exp="exec('etypy_fml_exp','obiegi2',2,_a)"
::# kind=WE, symbol=ODD, type=STRING, name=Domyślna jednostka księgowa, required=N, fml_val="exec('select_odd','obiegi_log',_a)", fml_exp="exec('odd_fml_exp','obiegi_log',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=ZK_N, type=_ZK_N, name=Wskazanie na nagłówek zamówienia zewnętrznego, required=N, keyref=T

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_service:=_mp.isService();
_auto:=_mp.isAutoRun();
{? ~exec('is_act','#b__box','OBE_FAW_FLZW')
|| {? ~_service
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
   ?};
   _mp.cancel(); return(~~)
|? _mp.isMicro()
|| {? ~_service
   || {? _akcja='Dołącz' | _mp.pathProc()
      || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
      || FUN.info('Wniosek nie jest w trakcie rejestracji.'@)
      ?}
   ?};
   return(~~)
?};

_dalej:=1;
{? _service & (_we.ZK_N=~~ | _we.ZK_N=null | _we.ETYPY=~~ | _we.ETYPY='')
|| _wy.EDOKUM:=_wy.ZK_N:=null;
   _mp.save(,_wy);
   _mp.done();
   _dalej:=0
?};
_zk_n:=_etypy:=null;
{? _dalej=1
|| {? ~_service
   || {? _akcja='OBSZAR'
      || params_get().context.ZK_N; _zk_n:=ZK_N.ref()
      |? _we.ZK_N=~~ | _we.ZK_N=null
      || _zk_n:=exec('zk_n_wybor','obiegi_log','W');
         {? _zk_n=null || _dalej:=-1 ?}
      || _zk_n:=_we.ZK_N
      ?}
   || _zk_n:=_we.ZK_N
   ?};
   {? _zk_n
   || _wy.ZK_N:=_zk_n; _mp.save(,_wy); _mp.descTodo()
   ?}
?};
{? _dalej=1
|| {? ~_service
   || ETYPY.cntx_psh(); ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',2));
      {? _we.ETYPY=~~ | _we.ETYPY='' | ~ETYPY.find_key(_we.ETYPY)
      || ETYPY.hdr_sel('Typy wniosków w obiegu'@); ETYPY.win_edit('REDW'); ETYPY.win_sel('SLO_WYB');
         {? ETYPY.select() || _etypy:=ETYPY.ref() ?}
      |? ETYPY.find_key(_we.ETYPY)
      || _etypy:=ETYPY.ref()
      ?};
      {? ~_etypy || _dalej:=-1 ?};
      ETYPY.cntx_pop()
   || ETYPY.cntx_psh(); ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',2));
      {? ETYPY.find_key(_we.ETYPY)
      || _etypy:=ETYPY.ref()
      || _dalej:=0
      ?};
      ETYPY.cntx_pop()
   ?}
?};
{? _dalej=1
|| _ref:=BB.sqlint($_zk_n); _nam:=form(($_zk_n)-8);
   {? _ref<>0 & _nam<>''
   || ZK_N.cntx_psh(); ZK_N.use(_nam); ZK_N.prefix();
      ZK_P.cntx_psh(); ZK_P.use('zkpoz'+(ZK_N.name()+3));
      {? ZK_N.seek(_ref,_nam)
      || {? ZK_N.OBI_POW<>''
         || {? ~_service
            || FUN.info('Dla zamówienia wewnętrznego został wcześniej uruchomiony obieg.'@)
            || _wy.EDOKUM:=_wy.ZK_N:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         |? ZK_N.STAT_REJ='T' | ZK_N.STAT_REJ='Z'
         || {? _akcja='OBSZAR' | _mp.pathProc() | _mp.pathTodo() | _mp.isAutoRun()
            || menu_txt(,'Dołącz');
               exec('zk_n_win_edit_set','obiegi_log',1,'W');
               {? _service
               || BtnObi:=1
               || BtnObi:=0;
                  ZK_N.display()
               ?};
               {? BtnObi
               || _edokum:=params_exec('zk_n_edokum','obiegi_log',_etypy,~_service,'W');
                  {? _edokum
                  || _wy.EDOKUM:=_edokum;
                     _wy.ZK_N:=ZK_N.ref();
                     _mp.save(,_wy);
                     _mp.done()
                  ?}
               ?}
            ?}
         || {? ZK_N.STAT_REJ='A'
            || {? ~_service || FUN.info('Zamówienie wewnętrzne zostało anulowane.'@) ?};
               _wy.EDOKUM:=_wy.ZK_N:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      || _wy.EDOKUM:=_wy.ZK_N:=null;
         _mp.save(,_wy);
         _mp.done()
      ?};
      ZK_N.cntx_pop(); ZK_P.cntx_pop()
   || _wy.EDOKUM:=_wy.ZK_N:=null;
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _dalej=0
|| _wy.EDOKUM:=_wy.ZK_N:=null;
   _mp.save(,_wy);
   _mp.done()
|? _dalej=-1
|| _mp.cancel()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia wewnętrznego: %1'@@[exec('record','#to_string',_in.ZK_N)]
|? var_pres('ZK_N',_out)=type_of(null()) & _out.ZK_N
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia wewnętrznego: %1'@@[exec('record','#to_string',_out.ZK_N)]
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia wewnętrznego'@@
?}


\obieg_obszarzw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Uruchomienie obiegu dokumentu z obszaru
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='OBE_FAW_FLZW';
_params.AKCJA:='OBSZAR';
_params.PROC_START:='T';
_params.UIDREF:=ZK_N.uidref();
_params.CONTEXT:=obj_new('ZK_N');
_params.CONTEXT.ZK_N:=ZK_N.ref();
exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 6c6f991629ce44d82acaaa6627746de4fc2d5a56953265aaaf7341072a7b5c530e0a1285c63ff2198dae20c76e832d050e526cf08e98d393639d45ff655b6c0a9596d7d430e1150cdae481e18ca8be99b11bb16157e48ae7b586e128086161d8051b6aa26efada5ef7cb1a21e522255e8df3aadc00211b70282b5d9387d7a257

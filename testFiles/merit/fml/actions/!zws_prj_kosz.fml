:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_prj_kosz.fml
:: Utworzony: 16.11.2021
:: Autor: PD
:: Systemy:
::======================================================================================================================
:: Zawartość: Planowane koszty projektu
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Czynność ZWS_PAR_UPRA - magazyny do realizacji zamówień wewnętrznych
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejściowe czynności
::       _int - [obj_new] - parametry wewnętrzne czynności
::       _out - [obj_new] - parametry wyjściowe czynności
::       _mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
exec('select','!zws_prj_kosz');
''


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z kosztami
::----------------------------------------------------------------------------------------------------------------------
PR_KOS.cntx_psh();
PR_KOS.index('PR_KOS_P');
PR_KOS.prefix(PROJEKTY.ref());
PR_KOS.first();
PR_KOS.win_sel('WEP');
_act:={? exec('chk_role','!zws_prj_kosz') & PROJEKTY.KAL_POD='N'
      || ''
      || 'DPU:D'
      ?};
PR_KOS.actions('WEP',_act);
exec('czytaj','#stalesys',,XINFO,INFO);
exec('ustaw_ww','eurusd',BEER.MW);
PR_KOS.select(,1);
{? PROJEKTY.KAL_POD='N'
|| exec('pr_kos_cal','!zws_prj_kosz') ;
   exec('projekty_plan_up_all','!zws_par_kprr',PROJEKTY.RODZIC)
?};
PR_KOS.cntx_pop();
~~


\chk_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z kosztami
::----------------------------------------------------------------------------------------------------------------------
exec('chk_role','#b__box',OPERATOR.USER,'ZWS_PRJ_KOSZ')


\m_f_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Filtr na materiale
::----------------------------------------------------------------------------------------------------------------------
{? M.f_active() || M.f_clear() ?};
M.win_dict('NL_WERI');
_plug:={? Plugin.runnable('ZWS_PRKOS_001')
       || Plugin.run('ZWS_PRKOS_001','M_BE',$PR_KOS.M,$PR_KOS.GR_K)
       || ''
       ?};
_mgr:={? POMOC.MGR_KPR<>null || 'MGR=\'%1\''[$POMOC.MGR_KPR] || '' ?};
_where:={? _mgr<>'' & _plug<>'' || _mgr + ' and ' + _plug || _mgr+_plug ?};
M.f_set('KTM, N',,_where);
1


\powal_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: po redakcji pola waluta
::----------------------------------------------------------------------------------------------------------------------
_wyn:={? PR_KOS.WAL<>null
      || 1
      || FUN.info('Należy podać walutę.'@);
         0
      ?};
_win_red:=PR_KOS.win_edit('?');
_czy_wal:=(PR_KOS.WAL<>exec('bl_nwal','ustawienia') | PR_KOS.WAL=null());
{? _czy_wal || {? PROJEKTY.DPP <> date(0,0,0) & PR_KOS.DTK = date(0,0,0) || PR_KOS.DTK:=PROJEKTY.DPP ?} ?};
_enwal:={? _czy_wal || 'enable=1' || 'enable=0' ?};
PR_KOS.efld_opt(_win_red,_enwal,,'KRS');
PR_KOS.efld_opt(_win_red,_enwal,,'DTK');
PR_KOS.efld_opt(_win_red,_enwal,,'CW');
PR_KOS.efld_opt(_win_red,_enwal,,'WW');
_wyn


\gr_k_slo_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Formuła przed redagowaniem dla KH_OSOB.SLO_RODZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'SLU_GK');
{? XINFO.SLU_GK<>null()
||
   SLU.seek(XINFO.SLU_GK,ref_name(XINFO.SLU_GK))
?}


\prww_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: przed redakcja pola wartosci w walucie
::   WY: jesli waluta rozna od narodowej to mozna edytowac
::----------------------------------------------------------------------------------------------------------------------
PR_KOS.WAL<>exec('bl_wal','ustawienia')


\poww_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: po redakcji pola wartosc w walucie
::----------------------------------------------------------------------------------------------------------------------
{? PR_KOS.WW>0
|| PR_KOS.WN:={? PR_KOS.KRS<>0 || PR_KOS.WN:=PR_KOS.WW*PR_KOS.KRS || PR_KOS.WW ?} $2;
   1
|| FUN.info('Należy podać wartość w walucie.'@);
   0
?}


\prkrs_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: przed redakcja pola kurs dla waluty
::   WY: jesli waluta rozna od narodowej to mozna edytowac
::----------------------------------------------------------------------------------------------------------------------
PR_KOS.WAL<>INFO.NAROD


\pokrs_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: po redakcji pola kurs na koszcie transportu
::----------------------------------------------------------------------------------------------------------------------
{? PR_KOS.KRS>0
|| PR_KOS.WN:={? PR_KOS.KRS<>0 || PR_KOS.WN:=PR_KOS.WW*PR_KOS.KRS || PR_KOS.WW ?} $2;
   1
|| FUN.info('Należy podać wartość kursu.'@);
   0
?}


\projekty_wp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: wartośc pocżatkowa pola PROJEKTY
::----------------------------------------------------------------------------------------------------------------------
PROJEKTY.ref()


\posv_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: po redakcji pola stawka VAT
::----------------------------------------------------------------------------------------------------------------------
{? PR_KOS.SV<>null
|| exec('suma_kpr','!zws_prj_kosz');
   1
|| FUN.info('Należy podać stawkę VAT.'@);
   0
?}


\suma_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: oblicza wartosc po redakcji pola
::----------------------------------------------------------------------------------------------------------------------
_wsp:=(#((PR_KOS.SV().KOD*'%')+PR_KOS.SV().KOD-1))*0.01;
{? _wsp<>0
|| PR_KOS.WV:=PR_KOS.WN*_wsp $2
|| PR_KOS.WV:=0
?};
PR_KOS.WB:=PR_KOS.WN+PR_KOS.WV;
1


\prwn_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: przed redakcja pola PR_KOS.WN
::----------------------------------------------------------------------------------------------------------------------
PR_KOS.WAL=INFO.NAROD


\pown_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: po redakcji pola wartosc netto
::----------------------------------------------------------------------------------------------------------------------
{? PR_KOS.WN>0
|| exec('suma_kpr','!zws_prj_kosz');
   1
|| FUN.info('Należy podać wartość netto.'@);
   0
?}


\powv_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: po redakcji pola wartosc VAT
::----------------------------------------------------------------------------------------------------------------------
exec('suma_kpr','!zws_prj_kosz');
1


\powb_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: po redakcji pola wartosc brutto
::----------------------------------------------------------------------------------------------------------------------
exec('suma_kpr','!zws_prj_kosz');
1


\pr_dfk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: przed redakcją danych do fakturowania
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='CW'
|| PR_KOS.WAL<>exec('bl_wal','ustawienia')
|? _fld='CN'
|| {? Plugin.runnable('ZWS_PRKOS_001')
   || Plugin.run('ZWS_PRKOS_001','CN_BE',$PR_KOS.M,$PR_KOS.GR_K)
   ?};
   PR_KOS.WAL=exec('bl_wal','ustawienia')
|| 1
?}


\po_dfk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: po redakcji danych do fakturowania
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_res:=1;
{? _fld='IL'
|| {? PR_KOS.IL<=0
   || _res:=0;
      FUN.info('Należy podać ilość.'@)
   || _res:=1;
      {? PR_KOS.WAL<>exec('bl_wal','ustawienia')
      || {? PR_KOS.CW=0
         || PR_KOS.CW:=PR_KOS.WW/PR_KOS.IL $PR_KOS.M().DOKL_C;
            PR_KOS.CN:=PR_KOS.CW*PR_KOS.KRS $PR_KOS.M().DOKL_C
         ?}
      || {? PR_KOS.CN=0 || PR_KOS.CN:=PR_KOS.WN/PR_KOS.IL $PR_KOS.M().DOKL_C ?}
      ?};
      exec('ceny_kpr','!zws_prj_kosz')
   ?}
|? _fld='CN'
|| {? PR_KOS.CN<=0
   || _res:=0;
      FUN.info('Cena netto musi być większa od zera.'@)
   || _res:=1;
      exec('ceny_kpr','!zws_prj_kosz')
   ?}
|? _fld='CB'
|| {? PR_KOS.CB<=0
   || _res:=0;
      FUN.info('Cena brutto musi być większa od zera.'@)
   || _res:=1;
      exec('ceny_kpr','!zws_prj_kosz')
   ?}
|? _fld='CW'
|| {? PR_KOS.CW<=0
   || _res:=0;
      FUN.info('Cena w walucie musi być większa od zera.'@)
   || _res:=1;
      _cn:=PR_KOS.CW*PR_KOS.KRS $PR_KOS.M().DOKL_C;
      {? _cn<>PR_KOS.CN || PR_KOS.CN:=_cn ?};
      exec('ceny_kpr','!zws_prj_kosz')
   ?}
?};
_res


\ceny_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: oblicza wartosc po redakcji pola
::----------------------------------------------------------------------------------------------------------------------
_wsp:=(#((PR_KOS.SV().KOD*'%')+PR_KOS.SV().KOD-1))*0.01;
{? _wsp<>0
|| _cv:=PR_KOS.CN*_wsp $2
|| _cv:=0
?};
PR_KOS.CB:=PR_KOS.CN+_cv;
{? PR_KOS.IL>0 & PR_KOS.CN>0
|| _sum:=0;
   _ww:=PR_KOS.IL*PR_KOS.CW$2;
   _wn:=PR_KOS.IL*PR_KOS.CN$2;
   {? PR_KOS.WW<>_ww || _sum:=1; PR_KOS.WW:=_ww ?};
   {? PR_KOS.WN<>_wn || _sum:=1; PR_KOS.WN:=_wn ?};
   {? _sum || exec('suma_kpr','!zws_prj_kosz') ?}
?};
1


\chk_kpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: sprawdza czy wypelniono dla PR_KOS pola Opis i Wartosc netto
::----------------------------------------------------------------------------------------------------------------------
_res:='';
{? form(PR_KOS.OPIS)=''
|| FUN.info('Należy podać opis kosztu.'@);
   _res:='OPIS'
|? PR_KOS.SV=null
|| FUN.info('Należy podać stawkę VAT.'@);
   _res:='SV'
|? PR_KOS.GR_K=null
|| FUN.info('Należy podać Grupę kosztową.'@);
   _res:='GR_K'
|| ''
?};
_res


\prk_col_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Kolorowanie kosztów projektów
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'SLU_GK');
SLU.cntx_psh();
SLU.prefix();
{? XINFO.SLU_GK<>null()
||
   {? SLU.seek(XINFO.SLU_GK,ref_name(XINFO.SLU_GK))
   ||
      SLO.cntx_psh();
      SLO.index('SL');
      SLO.prefix(SLU.ref());
      {? SLO.first()
      ||
         _int:=1;
         {!|?
            Color.add('PROJEKTY#03#'+('0'+$_int+2),'exec(\'kol_k_proj\',\'!zws_prj_kosz\')',('Grupa kosztów - '+SLO.TR),'~Standardowy');
            _int+=1;
            SLO.next()
         !}
      ?};
      SLO.cntx_pop()
   ?}
?};
SLU.cntx_pop();
~~


\kol_k_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Kolorowanie projektów
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
SLO.cntx_psh();
{? PR_KOS.ref() & PR_KOS.GR_K
||
   PR_KOS.GR_K();
   UST_MWYK.cntx_psh();
   UST_MWYK.index('OPIS');
   UST_MWYK.prefix('Grupa kosztów - '+SLO.TR,);
   {? UST_MWYK.first()
   ||
      _wyn:=UST_MWYK.ID
   ?};
   UST_MWYK.cntx_pop()
?};
SLO.cntx_pop();
_wyn

\pr_kos_cal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Podliczenie kosztów
::----------------------------------------------------------------------------------------------------------------------
PR_KOS.cntx_psh();
PR_KOS.index('PR_KOS_P');
PR_KOS.prefix(PROJEKTY.ref());
PROJEKTY.cntx_psh();
PROJEKTY.get();
_plankos:=0;
{? PR_KOS.first()
||
   {!|?
      _plankos+=PR_KOS.WB;
      PR_KOS.next()
   !}
?};
PROJEKTY.PLANKOS:=_plankos;
PROJEKTY.put();
PR_KOS.cntx_pop();
PROJEKTY.cntx_pop();
PROJEKTY.PLANKOS:=_plankos


\prk_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Edycja okienka RED
::----------------------------------------------------------------------------------------------------------------------
_win_red:=PR_KOS.win_edit('?');
_enwal:='enable=0';
PR_KOS.efld_opt(_win_red,_enwal,,'KRS');
PR_KOS.efld_opt(_win_red,_enwal,,'DTK');
PR_KOS.efld_opt(_win_red,_enwal,,'CW');
PR_KOS.efld_opt(_win_red,_enwal,,'WW');
POMOC.MGR_KPR:=null();
exec('czytaj','#stalesys',,XINFO,'SLU_GK');
{? XINFO.SLU_GK=null()
|| FUN.info('Niewypełniony Słownik grup kosztowych.'@); 0
|| 1
?}


\prk_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Edycja okienka RED
::----------------------------------------------------------------------------------------------------------------------
PR_KOS.win_edit('RED');
_czy_wal:=(PR_KOS.WAL<>exec('bl_nwal','ustawienia') | PR_KOS.WAL=null());
_enwal:={? _czy_wal || 'enable=1' || 'enable=0' ?};
PR_KOS.efld_opt('RED',_enwal,,'KRS');
PR_KOS.efld_opt('RED',_enwal,,'CW');
PR_KOS.efld_opt('RED',_enwal,,'WW');
PR_KOS.display();
1

\prk_m_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Edycja okienka RED
::----------------------------------------------------------------------------------------------------------------------
{? Plugin.runnable('ZWS_PRKOS_001')
|| Plugin.run('ZWS_PRKOS_001','M_AE',$PR_KOS.M,$PR_KOS.GR_K)
?};
1


\mgr_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Przed edycją grupy materiałowej
::----------------------------------------------------------------------------------------------------------------------
1


\mgr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Po edycji grupy materiałowej
::----------------------------------------------------------------------------------------------------------------------
1

:Sign Version 2.0 jowisz:1045 2023/11/09 09:07:36 1c6b2b3766ec806ee9438f250620bcb85a0d301065d72644c76a9f15c920523ec7ac3a3d39e5c2178ee37239892576b791cbb862b0238c0797d3749e903c8b108c08f0574c9668d23640ef2d7697a82cc83ff2032413560606f70d007e674a1d18b385c3b82c7c61caa1d7118d886b4ad2ab0a02d2545dfb25697551faf6b371

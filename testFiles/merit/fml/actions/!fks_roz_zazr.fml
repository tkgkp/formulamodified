:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_roz_zazr.fml
:: Utworzony: 29.06.2015
:: Autor: MB
::======================================================================================================================
:: Zawartosc: Formuły czynności FKS_ROZ_ZAZR - Zestawienia z rozrachunków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zestawienia z rozrachunków - główna formuła czynności raportującej
::   WE: [_a] - [1]- z raportów
::               0 - z obszaru
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
{? _=0 || _a:=1 ?};
params_set(params_get());
{? _a
|| exec('F','object');
   exec('RB','object')
?};
ROZRACH.cntx_psh();
ROZRACH.KONTR:=null();
BILANSP.ASEP:=SSTALE.AR().SEP;
exec('rpm_init','dok_fks');
{? KS.prefix(SSTALE.AR); KS.first()
|| PAR_WYDR.fld_fml('WSK_WAL','AFTER_EDIT',"exec('ae_rws_sp','!fks_roz_zazr')");
   OP.cntx_psh(); 
   exec('rep_exec','#b_report','FKS_ROZ_ZAZR','fks_rw*',,1);
   OP.cntx_pop(); 
   PAR_WYDR.fld_fml('WSK_WAL','AFTER_EDIT',"*")
|| FUN.info('Nie zdefiniowano planu kont w bieżącym roku bilansowym.'@)
?};
ROZRACH.cntx_pop();
VAR_DEL.delete('synt','an','se_p','kol')


\rwsyntan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.50]
:: OPIS: Sprawdza poprawnosc wypelnienia parametrow w oknie
::       wydruku rwsyntan.rpm
::  OLD: \rwsyntan/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_WYDR.T_ROZ:=~-PAR_WYDR.T_ROZ;
_t:='Błędny zakres kont!';
{? UNPAR.P1=3 & KONTO.K3=''
|| FUN.info(_t); 'K3'
|? UNPAR.P1=3 & KONTO.K4=''
|| FUN.info(_t); 'K4'
|? UNPAR.P1=6
|| _a:=__CHK.record2(ROZNE,'KON_WYDR','Zestaw kont');
   {? _a=''
   || KON_SCH.prefix(KON_WYDR.ref);
      _a:={? KON_SCH.first()
          || ''
          || FUN.info('Wybrany zestaw kont nie ma zdefiniowanej definicji kont.'@); 'KON_WYDR'
          ?}
   ?};
_a
|| _d:=date(0,0,0);
   {? (UNPAR.P2=1 & UNPAR.P4=_d) | (UNPAR.P2=2 & (UNPAR.P5=_d | UNPAR.P6=_d))
   || FUN.info('Zerowa data w zakresie rozrachunków.'@);
      {? UNPAR.P2=1 || 'P4' || {? UNPAR.P5=_d || 'P5' || 'P6' ?} ?}
   || _t:='Data spoza aktualnego roku bilansowego!';
      {? UNPAR.P4<>_d & ~exec('dat_not','dok_fks',UNPAR.P4)
      || FUN.info(_t); 'P4'
      |? UNPAR.P5<>_d & ~exec('dat_not','dok_fks',UNPAR.P5)
      || FUN.info(_t); 'P5'
      |? UNPAR.P6<>_d & ~exec('dat_not','dok_fks',UNPAR.P6)
      || FUN.info(_t); 'P6'
      |? UNPAR.P5>UNPAR.P6
      || FUN.info('Zakres dat dla rozrachunków rozliczonych\npodany nieprawidłowo.'@);
         'P5'
      |? UNPAR.P2=2 & (UNPAR.P3=2 | UNPAR.P3=3)
      || FUN.info('Zakres dat zapisów może być podany tylko dla rozrachunków '
                 'rozliczonych lub wszystkich.'@); 'P2'
      |? UNPAR.P15 & (UNPAR.P13=_d | UNPAR.P14=_d)
      || FUN.info('Zerowa data w zakresie dat otwarcia.'@);
         {? UNPAR.P13=_d || 'P13' || 'P14' ?}
      |? UNPAR.P13>UNPAR.P14
      || FUN.info('Zakres dat otwarcia dla rozrachunków\npodany nieprawidłowo.'@);
         'P13'
      |? UNPAR.P34 & UNPAR.P42=_d
      || FUN.info('Nie podano dnia bilansowego.'@); 'P42'
      |? UNPAR.P34 & PAR_WYDR.T_ROZ=''
      || FUN.info('Nie podano typu rozrachunków.'@); 'T_ROZ'
      || ''
      ?}
   ?}
?}


\prefix_maska
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia zmienne na podstawie pola z maską
::----------------------------------------------------------------------------------------------------------------------
_prefix:=exec('maska2prefix','konto',KONTO.K1);
{? _prefix<>'?'
|| UNPAR.P1:=2;
   KONTO.K2:=_prefix;
   KONTO.K1:=''
?}


\rwsynt_0
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMA [8.60]
:: OPIS: Usuwa z tabeli tymczasowej rozrachunki bez zapisow z danego zakresu dat
::       oraz nie spelniajace kryterium zakresu (rozliczone, nierozliczone)
::  OLD: \rwsynt_0/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? TAB_SQL.first()
|| TAB_SQL.cntx_psh();
   _ind:=TAB_SQL.ndx_tmp(,1,'WAL',,,'AN',,,'OD',,,'SYM_ROK',,,'SYM_ROK',,,'DATA',,); TAB_SQL.index(_ind); TAB_SQL.prefix();
   {? TAB_SQL.first()
   || _vwal:=_van:=_vodd:=_vsym:='';
      {! |?
         {? ~(_vwal=TAB_SQL.WAL & _van=TAB_SQL.AN & _vodd=TAB_SQL.OD & _vsym=TAB_SQL.SYM_ROK)
         || _vwal:=TAB_SQL.WAL; _van:=TAB_SQL.AN; _vodd:=TAB_SQL.OD; _vsym:=TAB_SQL.SYM_ROK;
            _vdel:=1; _wn:=_ma:=_wn_data:=_ma_data:=0;
            _vwn:=_vma:=0;
            TAB_SQL.cntx_psh();
            TAB_SQL.prefix(_vwal,_van,_vodd,_vsym,_vsym);
            {? TAB_SQL.first()
            || _vref:=null;
               {! |?
                  {? (UNPAR.P2=1 & TAB_SQL.DATA<=gdzap) | (TAB_SQL.DATA>=UNPAR.P5 & TAB_SQL.DATA<=gdzap)
                  || _vdel:=0;
                     {? _vref<>TAB_SQL.ref()
                     || _wn+=TAB_SQL.WN; _ma+=TAB_SQL.MA;
                        {? UNPAR.P34 & TAB_SQL.DATA<=UNPAR.P42
                        || _wn_data+=TAB_SQL.WN; _ma_data+=TAB_SQL.MA
                        ?};
                        {? PAR_WYDR.NO_SPVAT<>'W'
                        || _vwn+=TAB_SQL.VWN;
                           _vma+=TAB_SQL.VMA
                        ?}
                     ?};
                     _vref:=TAB_SQL.ref(); TAB_SQL.next()
                  || TAB_SQL.del()
                  ?}
               !};
               {? TAB_SQL.size()
               || {? ~( (UNPAR.P3=1 & _wn$2=_ma$2)
                        | ((UNPAR.P3=2 | UNPAR.P3=3) & _wn$2<>_ma$2)
                        | (UNPAR.P3=4)
                      ) | UNPAR.P34 & _wn_data=_ma_data |
                      | PAR_WYDR.NO_SPVAT='T' & _vwn<>_vma
                      | PAR_WYDR.NO_SPVAT='N' & _vwn=_vma
                  || _vdel:=1
                  ?}
               || _vdel:=0
               ?}
            ?};
            TAB_SQL.cntx_pop()
         ?};
         {? _vdel & TAB_SQL.get() || TAB_SQL.del() || TAB_SQL.next() ?}
      !}
   ?};
   TAB_SQL.cntx_pop()
?}


\p1_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redakcji UNPAR.P1 - selekcja kont
::----------------------------------------------------------------------------------------------------------------------
{? UNPAR.P1=1
|| KONTO.K2:=KONTO.K3:=KONTO.K4:=ROZNE.KON_WYDR().KOD:=''
|? UNPAR.P1=2
|| KONTO.K1:=KONTO.K3:=KONTO.K4:=ROZNE.KON_WYDR().KOD:=''
|? UNPAR.P1=3
|| KONTO.K1:=KONTO.K2:=''
|? UNPAR.P1=4
|| KONTO.K1:=KONTO.K2:=KONTO.K3:=KONTO.K4:=ROZNE.KON_WYDR().KOD:=''
|? UNPAR.P1=5
|| KONTO.K1:=KONTO.K2:=KONTO.K3:=KONTO.K4:=ROZNE.KON_WYDR().KOD:=''
|? UNPAR.P1=6
|| KONTO.K1:=KONTO.K2:=KONTO.K3:=KONTO.K4:=''
?};
exec('p1_enable','!fks_roz_zazr')


\p1_enable
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Selekcja kont - ustawienie dostępności pól
::----------------------------------------------------------------------------------------------------------------------
_okno:=PAR_WYDR.win_edit('?');
{? _okno = ''
|| _okno := 'OBR_ANOP'
?};
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P1=1),KONTO,'K1');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P1=2),KONTO,'K2');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P1=3),KONTO,'K3');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P1=3),KONTO,'K4');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P1=6),ROZNE,'KON_WYDR','KOD');
1


\p2_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redakcji UNPAR.P2 - stan na dzien/z zapisami
::----------------------------------------------------------------------------------------------------------------------
{? UNPAR.P2=1 & UNPAR.P4=date(0,0,0)
|| UNPAR.P5:=UNPAR.P6:=date(0,0,0); UNPAR.P4:=gdzap
|? UNPAR.P2=2 & UNPAR.P6=date(0,0,0)
|| UNPAR.P4:=UNPAR.P5:=date(0,0,0); UNPAR.P6:=gdzap
|| 1
?};
exec('p2_enable','!fks_roz_zazr')


\p2_enable
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zakres rozrachunkow (czas) - ustawienie dostępności pól
::----------------------------------------------------------------------------------------------------------------------
_okno:=PAR_WYDR.win_edit('?');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P2=1),UNPAR,'P4');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P2=2),UNPAR,'P5');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P2=2),UNPAR,'P6');
1


\p15_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redakcji UNPAR.P15 - data otwarcia
::----------------------------------------------------------------------------------------------------------------------
{? ~UNPAR.P15 || UNPAR.P13:=UNPAR.P14:=date(0,0,0) ?};
exec('p15_enable','!fks_roz_zazr');
1


\p15_enable
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zakres rozrachunkow (czas) - ustawienie dostępności pól
::----------------------------------------------------------------------------------------------------------------------
_okno:=PAR_WYDR.win_edit('?');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P15=1),UNPAR,'P13');
PAR_WYDR.efld_opt(_okno,'enable='+$(UNPAR.P15=1),UNPAR,'P14');
1


\be_stand
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [7.41] 18.06.2001
:: OPIS: Udostepnia lub blokuje do redakcji pole PAR_WYDR.STANDZ
::  OLD: \be_stand/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_WYDR.ROZR=2 || 1 || 0 ?}


\ar_rozr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redakcji pola PAR_WYDR.ROZR
::----------------------------------------------------------------------------------------------------------------------
PAR_WYDR.efld_opt(PAR_WYDR.win_edit('?'),'enable='+$(PAR_WYDR.ROZR=2),PAR_WYDR,'STANDZ');
1


\ae_pw_sp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Po redakcji pola PAR_WYDR.SP
::----------------------------------------------------------------------------------------------------------------------
{? PAR_WYDR.SP='T'
|| PAR_WYDR.WSK_WAL:='V'
?};
exec('ae_rws_sp','!fks_roz_zazr');
1


\ae_rws_sp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Po redakcji pól związanych z podzieloną płatności
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab() <> PAR_WYDR || return(1) ?};
{? PAR_WYDR.WSK_WAL<>'V'
|| PAR_WYDR.NO_SPVAT:='W'
?};
PAR_WYDR.efld_opt(PAR_WYDR.win_edit('?'),'enable='+$(PAR_WYDR.WSK_WAL='V'),PAR_WYDR,'NO_SPVAT');
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:49 4ae7e86f5f187fc70d4a857d78eb284f48f9209d5bc5e491a9800ab6690b81a0e7663e8bc94bd284c0e41ab80a5666eea5db7d0b56d00dae2842e0e740a4b5603e0e7b7f3092da3404f38c857cf2024629db5415476d5914255ffad5daba78b9514f351b44f618e6c417300cb8ef28b653bdd98fab691a06477bcf669dbaa388

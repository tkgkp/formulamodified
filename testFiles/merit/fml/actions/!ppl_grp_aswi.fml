:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_grp_aswi.fml
:: Utworzony: 20.01.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PPL_GRP_ASWI - Rejestracja świadczeń
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja świadczeń - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

{? exec('get_par','#parametr',295)<>'T' & ~exec('no_limit','schemat','PPL')
|| return()
?};
exec('select','!ppl_grp_aswi');
~~


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca treść komunikatu błędu.
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie świadczeń niemożliwe.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!ppl_grp_aswi');

_cfg:=obj_new('WND','Z','P');

_obj:="
   _obj:=obj_new('KEY','REF');
   _ind:={? PAR_SKID.get(236)='T' || 'OPISX' || 'OPIS' ?};
   _obj.KEY:=_ind;
   _obj.REF:=null;
   _obj
";

_cfg.Z:=_obj();
_cfg.P:=_obj();

params_set('cfg',_cfg);

OSOBA.cntx_psh();
OSOBA.win_dict('IS_WYPL');

O.cntx_psh();
O.prefix();

_result:=0;
IS_OPIS.cntx_psh();
IS_OPIS.index(_cfg.Z.KEY);
IS_OPIS.prefix('Z');
IS_OPIS.win_sel('GRP');

: czy zakończona?
_result:=IS_OPIS.select();

IS_OPIS.cntx_pop();
OSOBA.cntx_pop();
O.cntx_pop();
_result


\is_opis_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed dołączeniem rekordu tabeli IS_OPIS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
IS_OPIS.GSUM:=0;
IS_OPIS.PSUM:=0;
1


\is_opis_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed poprawieniem rekordu tabeli IS_OPIS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
IS_OPIS.GSUM:=0;
IS_WYPL.cntx_psh();
IS_WYPL.index('UNIQUE');
IS_WYPL.prefix(IS_OPIS.ref());
_loop:=IS_WYPL.first();
{!
|? _loop
|! IS_OPIS.GSUM+=IS_WYPL.SUM;
   _loop:=IS_WYPL.next()
!};
IS_WYPL.cntx_pop();

IS_OPIS.PSUM:={? IS_OPIS.GLIM>0 || IS_OPIS.GLIM-IS_OPIS.GSUM ?};
1


\is_wypl_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed dołączeniem rekordu tabeli IS_WYPL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
IS_WYPL.SUM:={? IS_WYPL.LT<>'' & IS_WYPL.LT<>exec('nie_lista_isplac','lista_licz') || IS_WYPL.KW ?};
1


\is_wypl_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po dołączeniu rekordu tabeli IS_WYPL
::   WE: _a - zgodna ze specyfikacją wyzwalacza po dołączeniu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

{? exec('lic','#b_domain','POR')
|| exec('is_wypl2bnftpn','portal_benefity')
?};

exec('is_wypl_moda','!ppl_grp_aswi',IS_WYPL.IS_OPIS);
~~


\is_wypl_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed poprawieniem rekordu tabeli IS_WYPL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
IS_WYPL.SUM:=0;

{? IS_WYPL.MIES='N'
|| {? IS_WYPL.LT<>''
   || IS_WYPL.SUM:=IS_WYPL.KW
   ?}

|| IS_ROZL.cntx_psh();
   IS_ROZL.index('DATA');
   IS_ROZL.prefix(IS_WYPL.ref());
   _loop:=IS_ROZL.first();
   {!
   |? _loop
   |! IS_WYPL.SUM+=IS_ROZL.KW;
      _loop:=IS_ROZL.next()
   !};
   IS_ROZL.cntx_pop()
?};
1


\is_wypl_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po poprawieniu rekordu tabeli IS_WYPL
::   WE: _a - zgodna ze specyfikacją wyzwalacza po poprawieniu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

{? IS_WYPL.IS_OPIS<>bfld('IS_OPIS')
|| exec('is_wypl_moda','!ppl_grp_aswi',bfld('IS_OPIS'))
?};

{? exec('lic','#b_domain','POR')
|| IS_DEF.cntx_psh();
   IS_DEF.prefix();
   _bnftt_before:={? IS_DEF.seek(bfld('IS_DEF')) || IS_DEF.BNFTT || null() ?};
   _bnftt_after:=IS_WYPL.IS_DEF().BNFTT;
   {? _bnftt_before<>null() & _bnftt_after=null()
   || exec('del_ndx','#table',BNFTP,'OWNER',IS_WYPL.uidref())
   |? _bnftt_before=null() & _bnftt_after<>null()
   || exec('is_wypl2bnftpn','portal_benefity')
   |? _bnftt_before<>null() & _bnftt_after<>null()
   || {? IS_WYPL.OSOBA<>bfld('OSOBA')
      || exec('del_ndx','#table',BNFTP,'OWNER',IS_WYPL.uidref());
         exec('is_wypl2bnftpn','portal_benefity')
      || BNFTP.cntx_psh();
         BNFTP.index('OWNER');
         BNFTP.prefix(IS_WYPL.uidref());
         {? BNFTP.first()
         || {!
            |? exec('is_wypl2bnftp1','portal_benefity',BNFTP.FIRMA,BNFTP.P, BNFTP.BNFTT,BNFTP.NOMINAL<>bfld('KW'));
               BNFTP.put();
               BNFTP.next()
            !}
         ?};
         BNFTP.cntx_pop()
      ?}
   ?};
   IS_DEF.cntx_pop()
?};

exec('is_wypl_moda','!ppl_grp_aswi',IS_WYPL.IS_OPIS);
~~


\is_wypl_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed usunięciem rekordu tabeli IS_WYPL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',IS_ROZL,'DATA',IS_WYPL.ref()) &
exec('del_ndx','#table',BNFTP,'OWNER',IS_WYPL.uidref())


\is_wypl_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po usunięciu rekordu tabeli IS_WYPL
::   WE: _a - zgodna ze specyfikacją wyzwalacza po usunięciu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

exec('is_wypl_moda','!ppl_grp_aswi',bfld('IS_OPIS'));
~~


\is_wypl_moda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po zmianie zawartości tabeli IS_WYPL.
::   WE: _a - wskazanie zapisu tabeli IS_OPIS
::   WY:
::----------------------------------------------------------------------------------------------------------------------
IS_OPIS.cntx_psh();
IS_OPIS.prefix();
{? IS_OPIS.seek(_a)
|| IS_OPIS.put()
?};
IS_OPIS.cntx_pop();
IS_OPIS.get();
~~


\is_rozl_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po dołączeniu rekordu tabeli IS_ROZL
::   WE: _a - zgodna ze specyfikacją wyzwalacza po dołączeniu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

exec('is_rozl_moda','!ppl_grp_aswi',IS_ROZL.IS_WYPL);
~~


\is_rozl_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po poprawieniu rekordu tabeli IS_ROZL
::   WE: _a - zgodna ze specyfikacją wyzwalacza po poprawieniu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

{? IS_ROZL.IS_WYPL<>bfld('IS_WYPL')
|| exec('is_rozl_moda','!ppl_grp_aswi',bfld('IS_WYPL'))
?};
exec('is_rozl_moda','!ppl_grp_aswi',IS_ROZL.IS_WYPL);
~~


\is_rozl_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po usunięciu rekordu tabeli IS_ROZL
::   WE: _a - zgodna ze specyfikacją wyzwalacza po usunięciu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};

exec('is_rozl_moda','!ppl_grp_aswi',bfld('IS_WYPL'));
~~


\is_rozl_moda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po zmianie zawartości tabeli IS_ROZL.
::   WE: _a - wskazanie zapisu tabeli IS_WYPL
::   WY:
::----------------------------------------------------------------------------------------------------------------------
IS_WYPL.cntx_psh();
IS_WYPL.prefix();
{? IS_WYPL.seek(_a)
|| IS_WYPL.put()
?};
IS_WYPL.cntx_pop();
IS_WYPL.get();
~~


\grp_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wypełnieniem okienka GRP tabeli IS_OPIS
::----------------------------------------------------------------------------------------------------------------------
IS_OPIS.cntx_psh();
IS_ROZL.cntx_psh();
IS_WYPL.cntx_psh();

_ind:={? PAR_SKID.get(236)='T' || 'ODX' || 'OD' ?};
IS_WYPL.index(_ind);

~~


\grp_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przy zamykaniu okienka GRP tabeli IS_OPIS
::----------------------------------------------------------------------------------------------------------------------
IS_OPIS.cntx_pop();
IS_ROZL.cntx_pop();
IS_WYPL.cntx_pop();
1


\grp_opis_z_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka WER tabeli IS_OPIS w pierwszej zakładce okienka GRP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('grp_opis_com_ar','!ppl_grp_aswi')


\grp_opis_z_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli IS_OPIS w pierwszej zakładce okienka GRP
::   WE: _a - zgodna ze specyfikacją akcji przed obsługą
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('grp_opis_com_bs','!ppl_grp_aswi',_a,'Z',params_get().cfg.Z)


\grp_opis_z_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okienka WER tabeli IS_OPIS w pierwszej zakładce okienka GRP
::   WE: _a - zgodna ze specyfikacją akcji po obsłudze
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('grp_opis_com_as','!ppl_grp_aswi',_a,params_get().cfg.Z)


\grp_opis_p_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka WER tabeli IS_OPIS w drugiej zakładce okienka GRP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('grp_opis_com_ar','!ppl_grp_aswi')


\grp_opis_p_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli IS_OPIS w drugiej zakładce okienka GRP
::   WE: _a - zgodna ze specyfikacją akcji przed obsługą
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('grp_opis_com_bs','!ppl_grp_aswi',_a,'P',params_get().cfg.P)


\grp_opis_p_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po obsłudze okienka WER tabeli IS_OPIS w drugiej zakładce okienka GRP
::   WE: _a - zgodna ze specyfikacją akcji po obsłudze
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('grp_opis_com_as','!ppl_grp_aswi',_a,params_get().cfg.P)


\grp_opis_com_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Formuła wspólna dla akcji po odświeżeniu okinek WER tabeli IS_OPIS w zakładkach okienka GRP
::   WE:
::   WY:
::  OLD: \odswiez/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(IS_WYPL,'WER',1)


\grp_opis_com_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Formuła wspólna dla akcji przed obsługą okienek WER tabeli IS_OPIS
::   WE: _a - zgodna ze specyfikacją akcji przed obsługą
::       _b - rodzaj zapisów:
::          'Z' - Zakładowy Fundusz Świadczeń Socjalnych
::          'P' - Pozostałe świadczenia
::       _c - wskazanie tablicy stanów
::   WY:
::  OLD: \odswiez/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
IS_VAR.RODZAJ:=_b;

IS_OPIS.index(_c.KEY);
IS_OPIS.prefix(_b);
IS_OPIS.seek(_c.REF)


\grp_opis_com_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Formuła wspólna dla akcji po obsłudze okienek WER tabeli IS_OPIS
::   WE: _a - zgodna ze specyfikacją akcji po obsłudze
::       _b - wskazanie tablicy stanów
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _b.KEY:=IS_OPIS.index('?');
   _b.REF:=IS_OPIS.ref()
?}


\grp_wypl_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okinka WER tabeli IS_WYPL w okienku GRP tabeli IS_OPIS
::   WE:
::   WY:
::  OLD: \odswiez/isplac.fml
::----------------------------------------------------------------------------------------------------------------------


\grp_wypl_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okienka WER tabeli IS_WYPL w okienku GRP tabeli IS_OPIS
::   WE: _a - zgodna ze specyfikacją akcji po obsłudze
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
{? ~exec('dom_empty','#table',IS_OPIS)
|| _ref:=IS_OPIS.ref()
?};
IS_WYPL.IS_OPIS:=_ref;
IS_WYPL.IS_OPIS();

IS_WYPL.prefix(_ref)


\is_opis_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed usunięciem rekordu w okienku WER tabeli IS_OPIS.
::   WE:
::   WY:
::  OLD: \mod_opis/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? IS_OPIS.count()=0
|| return(1)
?};

exec('del_warn','#table');
0


\is_opis_import_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ATA [2011]
:: OPIS: Import danych z pliku generowanego przez MyBenefit
::  OLD: \imp_swia/benefity.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.ask(
   'Funkcja importuje dane z systemu kafeteryjnego.\n'
   'W kolejnym kroku należy wskazać plik zawierający rozliczenie świadczeń.'@
)


\is_opis_import_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ATA [2011]
:: OPIS: Import danych z pliku generowanego przez MyBenefit
::       Opis kolumn importowanego pliku: (ER/WRT/XP/23.25/2401/0016)
::       1. Indentyfikator pracownika [WYMAGANE]
::       2. Nazwisko [OPCJONALNE]
::       3. Imię [OPCJONALNE]
::       4. Jednostka organizacyjna [OPCJONALNE]
::       5. Numer rubryki płacowej (identyfikuje świadczenie) [WYMAGANE]
::       6. Nazwa rubryki płacowej [OPCJONALNE]
::       7. Stała "Z" [WYMAGANE]
::       8. Kwota [WYMAGANE]
::  OLD: \imp_swia/benefity.fml
::----------------------------------------------------------------------------------------------------------------------
_plik:=0;
_tmp_dir:=fmk_tmp_dir(0);
{? type_of(_tmp_dir)<>type_of(~~)
|| _pth:=_tmp_dir.get_path();
   {? _pth<>''
   || _nazwa:=dlg_upload(_pth,0,);
      _sep:=exec('sep','#file',1);
      {? _nazwa<>''
      || _plik:=fopen(_pth+_sep+_nazwa,'r')
      ?}
   ?}
|| FUN.emsg('Nie udało się utworzenie katalogu tymczasowego po stronie serwera.'@);
   return()
?};

{? _plik=0
|| return()
?};

exec('__RUB','object');

P.cntx_psh;
P.f_clear();
P.index('PRACOIP');
P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P);
IS_WYPL.cntx_psh();
IS_WYPL.clear();

_buf:=obj_new(8);

{!
|? (_wiersz:=fread(_plik))<>'\n'
|! {! _ndx:=1..8
   |! _buf[_ndx]:=''
   !};
   _ndx:=0;
   {!
   |? {? _wiersz<>''
      || _ndx+=1;
         {? _ndx<=8
         || _poz:=_wiersz*';';
            {? _poz>0
            || _buf[_ndx]:=form((_poz-1)+_wiersz);
               _wiersz:=_poz-_wiersz;
               1
            || _buf[_ndx]:=form(_wiersz);
               0
            ?}
         ?}
      ?}
   !};
   _wiersz:=STR.w952maz(_wiersz);
   _buf[1]:=#_buf[1];
   _buf[5]:=#_buf[5];

   _buf[8]:=gsub(_buf[8],',','.');
   _buf[8]:=#_buf[8];

   {? (1+_buf[7])='Z' & P.find_key(_buf[1])
   || {? (_def:=exec('szukaj_is_def','!ppl_grp_aswi',_buf[5]))<>null()
      || IS_WYPL.blank(1);
         IS_WYPL.IS_OPIS:=IS_OPIS.ref();
         IS_WYPL.OSOBA:=P.OSOBA;
         IS_WYPL.IS_DEF:=_def;
         IS_WYPL.MIES:='T';
         IS_WYPL.OD:=date(O.R,O.M,1);
         IS_WYPL.DO:=date(O.R,O.M,0);
         IS_WYPL.KW:=_buf[8];
         IS_WYPL.LT:=exec('nie_lista_isplac','lista_licz');
         IS_WYPL.add(1)
      ?}
   ?}
!};
fclose(_plik);

P.cntx_pop();
IS_WYPL.cntx_pop();

1


\szukaj_is_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ATA [2011]
:: OPIS: Ustala świadczenie na podstawie numeru składnika
::  OLD: \ust_swia/benefity.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
IS_DEF.cntx_psh();
IS_DEF.index('RN');
IS_DEF.prefix(_a);
{? IS_DEF.first()
|| _ref:=IS_DEF.ref()
?};
IS_DEF.cntx_pop();
_ref


\is_opis_przypisz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ATA [2012]
:: OPIS: Przypisanie pracowników do wybranego świadczenia.
::  OLD: \prac_przy/benefity.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_EDIT.IS_DEF:=null;
VAR_EDIT.MC:='N';
VAR_EDIT.D_OD:=date(0,0,0);
VAR_EDIT.D_DO:=date(0,0,0);
VAR_EDIT.KW:=0;

: nadpisane formuły po edycji muszą być później odtworzone
_ae:=exec('save_fml_type','#field',VAR_EDIT,'AFTER_EDIT');
VAR_EDIT.fld_fml('D_OD','AFTER_EDIT',"
   exec('f_day_fld','#field');
   {? VAR_EDIT.D_OD=date(0,0,0)
   || VAR_EDIT.D_OD:=date(O.R,O.M,1)
   ?};
   1
");
VAR_EDIT.fld_fml('D_DO','AFTER_EDIT',"
   exec('l_day_fld','#field');
   {? VAR_EDIT.D_DO=date(0,0,0)
   || VAR_EDIT.D_DO:=date(O.R,O.M,0)
   ?};
   1
");

VAR_EDIT.win_edit('IS_RED');
{? ~VAR_EDIT.edit("exec('is_opis_przypisz_ae','!ppl_grp_aswi')")
|| return()
?};

: filtrownie pracowników według parametrów sesji
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PPL';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_args.F_ZATR:=__PARSES.getVal('F_ZATR').REF;
_args.VIEW:=__PARSES.getVal('ZakresDanych');

: wybór wielu pracowników
_ret:=exec('wybierz','pracownik',_args);
: znieś nałożone ograniczenie
P.f_clear();

{? _ret.STATUS<>''
|| FUN.error(_ret.STATUS);
   return()
?};

_loop:=_ret.P.first();
{!
|? _loop
|! {? P.seek(_ret.P.SQL)
:     dodaj świadczenie
   || IS_WYPL.blank(1);
      IS_WYPL.IS_OPIS:=IS_OPIS.ref();
      IS_WYPL.OSOBA:=P.OSOBA;
      IS_WYPL.IS_DEF:=VAR_EDIT.IS_DEF;
      IS_WYPL.MIES:=~(-VAR_EDIT.MC);
      IS_WYPL.OD:=VAR_EDIT.D_OD;
      IS_WYPL.DO:=VAR_EDIT.D_DO;
      IS_WYPL.KW:=VAR_EDIT.KW;
      {? IS_WYPL.MIES='T'
      || IS_WYPL.LT:=exec('nie_lista_isplac','lista_licz')
      || IS_WYPL.LT:=''
      ?};
      IS_WYPL.add(1)
   ?};
   _loop:=_ret.P.next()
!};

exec('restore_fml_type','#field',VAR_EDIT,'AFTER_EDIT',_ae);
~~


\is_opis_przypisz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdzenie poprawności parametrów grupowego wprowadzania świadczeń.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=__CHK.record(VAR_EDIT,,'IS_DEF','D_OD','D_DO','KW'))<>''
|| return(_chk)
?};

{? VAR_EDIT.MC='N' & (VAR_EDIT.D_OD~1<>VAR_EDIT.D_DO~1 | VAR_EDIT.D_OD~2<>VAR_EDIT.D_DO~2)
|| __CHK.err_fld(VAR_EDIT,'D_DO',1,'Świadczenie jednorazowe musi być wypłacone w ciągu jednego miesiąca'@)

|? VAR_EDIT.D_DO<VAR_EDIT.D_OD
|| __CHK.err_fld(VAR_EDIT,'D_OD',1)

|? VAR_EDIT.D_OD<IS_OPIS.OD
|| __CHK.err_fld(VAR_EDIT,'D_OD',1)

|? IS_OPIS.DO<VAR_EDIT.D_DO
|| __CHK.err_fld(VAR_EDIT,'D_DO',1)

|? VAR_EDIT.KW<0
|| __CHK.err_fld(VAR_EDIT,'KW',1)

|| ''
?}


\is_opis_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdzenie poprawności danych w tabeli IS_OPIS.
::   WE:
::   WY: zgodne ze specyfikacją dla akcji "rekord po"
::  OLD: \spr_opis/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=__CHK.record(IS_OPIS,,'OD','DO','SLO_NAZ'))<>''
|| return(_chk)
?};

{? IS_OPIS.DO<IS_OPIS.OD
:  koniec wcześniejszy od początku
|| exec('alert','overlap');
   return('OD')

|? IS_OPIS.GLIM<0
:  ujemna kwota ograniczenia
|| __CHK.err_fld(IS_OPIS,'GLIM',1,'Kwota nie może być ujemna.');
   return('GLIM')

|? IS_OPIS.SLIM<0
:  ujemna kwota limitu świadczenia
|| __CHK.err_fld(IS_OPIS,'SLIM',1,'Kwota nie może być ujemna.');
   return('SLIM')

|| _ref:={? -menu_txt='popraw' || IS_OPIS.ref() || null ?};
   _chk:=exec('check','overlap',_ref,IS_OPIS,,,,$('$IS_OPIS.SLO_NAZ=\''+$IS_OPIS.SLO_NAZ+'\''),,'UNIQUE',IS_VAR.RODZAJ);
   {? _chk<>''
   || return(_chk)
   ?}
?};

IS_WYPL.cntx_psh();
IS_WYPL.index('UNIQUE');
IS_WYPL.prefix(_ref);
_loop:=IS_WYPL.first();
{!
|? _loop
|! {? IS_WYPL.OD<IS_OPIS.OD
   || FUN.emsg(
         'Wykryto konflikt z zapisami w kartotece świadczeń.\n'
         'Data "Od dnia" nie może być późniejsza od %1.'@[$IS_WYPL.OD]
      );
      IS_WYPL.cntx_pop();
      return('OD')

   |? IS_OPIS.DO<IS_WYPL.DO
   || FUN.emsg(
         'Wykryto konflikt z zapisami w kartotece świadczeń.\n'
         'Data "Do dnia" nie może być wcześniejsza od %1.'@[$IS_WYPL.DO]
      );
      IS_WYPL.cntx_pop();
      return('DO')
   ?};
   _loop:=IS_WYPL.next()
!};
IS_WYPL.cntx_pop();
1


\is_var_sum_glim_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wyświetleniem pierwszego pola wiersza podsumowania wybranych rekordów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
IS_VAR.SUM_GLIM:=0;
IS_VAR.SUM_GSUM:=0;
IS_VAR.SUM_PSUM:=0;

_empty:="'empty=1'";
{? IS_OPIS.sel_size()
|| _RS:=sql(
      'select sum(IS_OPIS.GLIM) as SUM_GLIM, sum(IS_OPIS.GSUM) as SUM_GSUM, sum(IS_OPIS.PSUM) as SUM_PSUM '+
      'from IS_OPIS where reference_num(IS_OPIS.REFERENCE) in (select Q.REF from :_a as Q)',
      IS_OPIS.sel_aget()
   );
   IS_VAR.SUM_GLIM:=_RS.SUM_GLIM;
   IS_VAR.SUM_GSUM:=_RS.SUM_GSUM;
   IS_VAR.SUM_PSUM:=_RS.SUM_PSUM;
   _empty:="'empty=0'"
?};

IS_VAR.fld_fml('SUM_GLIM','DISPLAY_FORMAT',_empty);
IS_VAR.fld_fml('SUM_GSUM','DISPLAY_FORMAT',_empty);
IS_VAR.fld_fml('SUM_PSUM','DISPLAY_FORMAT',_empty);

1


\is_var_sum_zwyp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wyświetleniem pierwszego pola wiersza podsumowania wybranych rekordów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
IS_VAR.SUM_ZWYP:=0;

_zemp:="'empty=1'";
_wemp:="'empty=1'";

{? IS_WYPL.sel_size()
|| _RS:=sql(
      'select sum(IS_WYPL.SUM) as SUM_ZWYP '
      'from IS_WYPL '
      'where IS_WYPL.IS_OPIS=:_a and reference_num(IS_WYPL.REFERENCE) in (select Q.REF from :_b as Q)',
      IS_OPIS.ref(),
      IS_WYPL.sel_aget()
   );
   IS_VAR.SUM_ZWYP:=_RS.SUM_ZWYP;
   _zemp:="'empty=0'"

|? IS_WYPL.f_active()
|| IS_VAR.SUM_WWYP:=0;
   IS_WYPL.cntx_psh();
   _ref:=IS_WYPL.ref();
   IS_WYPL.f_each("IS_VAR.SUM_WWYP+=IS_WYPL.SUM",0);
   IS_WYPL.f_seek(_ref);
   IS_WYPL.cntx_pop()

|| IS_VAR.SUM_WWYP:=IS_OPIS.GSUM
?};

{? ~exec('dom_empty','#table',IS_WYPL)
|| _wemp:="'empty=0'"
?};

IS_VAR.fld_fml('SUM_ZWYP','DISPLAY_FORMAT',_zemp);
IS_VAR.fld_fml('SUM_WWYP','DISPLAY_FORMAT',_wemp);

1


\is_wypl_opis_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Wartosc poczatkowa IS_WYPL.IS_OPIS
::   WY: IS_OPIS.ref()
::  OLD: \is_wypl_opis_bl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
IS_OPIS.ref()


\jest_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdź, czy dla bieżącego rekordu w tabeli IS_WYPL istnieją powiązane z nim rekordy w tabeli IS_ROZL.
::   WE:
::   WY:
::  OLD: \czy_rozl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
IS_ROZL.index('DATA');
IS_ROZL.prefix(IS_WYPL.ref());
IS_ROZL.first()


\test_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawda, czy na liście płac wskazywanej osoby istnieje jakiekolwiek składnik o podanym kodzie.
::   WE: _a [_OSOBA] - wskazanie osoby
::       _b [INTEGER] - kod składnika
::       _c [STRING] - nazwa listy płac
::   WY: 0 - brak składnika
::       1 - znaleziono składnik
::  OLD: \spr_list/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? _c=exec('nie_lista_isplac','lista_licz') | _c=''
|| return(0)
?};

LS.cntx_psh();
LS.use(-_c);
LS.index('OSOBAKOD');
LS.prefix(exec('ref_firma','ustawienia'),_a,_b);
_test:=LS.first();
LS.cntx_pop();
_test


\test_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS:
::   WE:
::   WY:
::  OLD: \rozl_spr/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? IS_WYPL.MIES<>'N'
|| IS_ROZL.cntx_psh();
   _test:=exec('jest_rozl','!ppl_grp_aswi');
   IS_ROZL.cntx_pop();
   _test=0

|| {? IS_WYPL.LT<>''
   || {? exec('test_list','!ppl_grp_aswi',IS_WYPL.OSOBA,IS_WYPL.IS_DEF().R().RN,IS_WYPL.LT)
      || exec('o_writable','lista_plac',IS_WYPL.LT,0)
      || 1
      ?}
   || 1
   ?}
?}


\is_wypl_osoba_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed redakcją pola OSOBA tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppr/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
REF.OSOBA<>null & IS_VAR.SPR_ROZL


\is_wypl_osoba_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po redakcji pola OSOBA tabeli IS_WYPL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
REF.OSOBA<>null


\is_wypl_is_def_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed redakcją pola IS_DEF tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppr/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
IS_VAR.SPR_ROZL


\is_wypl_is_def_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po redakcji pola IS_DEF tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppo/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
exec('is_wypl_kw_be','!ppl_grp_aswi');

{? IS_WYPL.IS_DEF().R=null
|| IS_WYPL.LT:=exec('nie_lista_isplac','lista_licz')
?};

1


\is_wypl_mies_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed redakcją pola MIES tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppr/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()='popraw'
|| ~exec('jest_rozl','!ppl_grp_aswi')
|| 1
?}


\is_wypl_mies_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po redakcji pola MIES tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppo/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
_zab_lt:=exec('nie_lista_isplac','lista_licz');

{? IS_WYPL.OD=date(0,0,0)
|| IS_WYPL.OD:=date(O.R,O.M,1)
?};

{? IS_WYPL.MIES='T' | ~IS_WYPL.IS_DEF().R
|| IS_WYPL.LT:=_zab_lt

|? IS_WYPL.MIES='N' & IS_WYPL.LT=_zab_lt
|| IS_WYPL.LT:=''

|| {? IS_WYPL.LT='' | IS_WYPL.LT=_zab_lt
   || {? exec('o_writable','lista_plac',VAR.NAZWALIS,0)
      || IS_WYPL.LT:=VAR.NAZWALIS
      || IS_WYPL.LT:=''
      ?}
   ?};
   {? IS_WYPL.OD~1<>IS_WYPL.DO~1 | IS_WYPL.OD~2<>IS_WYPL.DO~2
   || IS_WYPL.DO:=date(IS_WYPL.OD~1,IS_WYPL.OD~2,0)
   ?}
?};

exec('is_wypl_x_opt','!ppl_grp_aswi');

1


\is_wypl_od_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po redakcji pola OD tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppo/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
exec('f_day_fld','#field');

{? IS_WYPL.OD=date(0,0,0)
|| IS_WYPL.OD:=date(O.R,O.M,1)
?};

{? IS_WYPL.MIES='N' & (IS_WYPL.OD~1<>IS_WYPL.DO~1 | IS_WYPL.OD~2<>IS_WYPL.DO~2)
|| IS_WYPL.DO:=date(IS_WYPL.OD~1,IS_WYPL.OD~2,0)
?};

{? IS_WYPL.LT='' & IS_WYPL.MIES='N' & IS_WYPL.IS_DEF().R=null ||
   IS_WYPL.LT:=VAR.NAZWALIS
?};

1


\is_wypl_do_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po redakcji pola DO tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppo/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
exec('l_day_fld','#field');

{? IS_WYPL.MIES='N' & (IS_WYPL.OD~1<>IS_WYPL.DO~1 | IS_WYPL.OD~2<>IS_WYPL.DO~2)
|| IS_WYPL.DO:=date(IS_WYPL.OD~1,IS_WYPL.OD~2,0)
?};

1


\is_wypl_kw_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed redakcją pola KW tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppr/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? IS_WYPL.KW=0 & IS_WYPL.IS_DEF().KW
|| IS_WYPL.KW:=IS_DEF.KW
?};

1


\is_wypl_kw_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po redakcji pola KW tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppo/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? IS_WYPL.KW<=0
|| IS_WYPL.KW:=0
?};

exec('is_wypl_kw_be','!ppl_grp_aswi')


\is_wypl_lt_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed redakcją pola LT tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \wypl_ppr/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
IS_VAR.SPR_ROZL


\is_wypl_x_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Ustala stan pól w bieżącym okienku tabeli IS_WYPL
::   WE: _a [INTEGER] - 0 lub brak - ustawienie opcji, 1 - przywrócenie stanu domyślengo
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0) | ~_a
||
   _zab_lt:=exec('nie_lista_isplac','lista_licz');
   IS_WYPL.efld_opt(IS_WYPL.win_edit('?'),'enable='+$(IS_WYPL.LT<>_zab_lt),,'LT')
||
   IS_WYPL.efld_opt(IS_WYPL.win_edit('?'),'enable=1',,'LT')
?};
~~


\is_wypl_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed dołączeniem wiersza tabeli IS_WYPL
::   WE:
::   WY: 1
::  OLD: \mod_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
IS_VAR.SPR_ROZL:=1;

exec('is_wypl_edytuj_b','!ppl_grp_aswi');

1


\is_wypl_dolacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Dołączenie wiersza tabeli IS_WYPL
::   WE:
::   WY: 0
::----------------------------------------------------------------------------------------------------------------------
IS_WYPL.blank();
exec('is_wypl_x_opt','!ppl_grp_aswi',0);
{? IS_WYPL.edit("exec('is_wypl_ae','!ppl_grp_aswi')")
|| IS_WYPL.add()
?};
exec('is_wypl_x_opt','!ppl_grp_aswi',1);
0


\is_wypl_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed poprawieniem wiersza tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \mod_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? REF.OSOBA=null
|| FUN.emsg('Brak uprawnień do modyfikacji bieżącego wiersza.'@);
   return(0)
?};

IS_VAR.SPR_ROZL:=exec('test_rozl','!ppl_grp_aswi');

_ok:=0;

{? IS_WYPL.LT='' | IS_WYPL.LT=exec('nie_lista_isplac','lista_licz')
|| _ok:=1

|? IS_WYPL.LT=VAR.NAZWALIS
|| _ok:=exec('o_writable','lista_plac',IS_WYPL.LT)

|| FUN.info(
      'Lista płac %1 nie rozlicza świadczenia.\n'
      'Należy wybrać listę płac %2.'@[VAR.NAZWALIS,IS_WYPL.LT]
   )
?};

{? _ok
|| exec('is_wypl_edytuj_b','!ppl_grp_aswi')
?};

_ok


\is_wypl_popraw_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Poprawienie wiersza tabeli IS_WYPL
::   WE:
::   WY: 0
::----------------------------------------------------------------------------------------------------------------------
IS_WYPL.get();
exec('is_wypl_x_opt','!ppl_grp_aswi',0);
{? IS_WYPL.edit("exec('is_wypl_ae','!ppl_grp_aswi')")
|| IS_WYPL.put()
?};
exec('is_wypl_x_opt','!ppl_grp_aswi',1);
0


\is_wypl_edytuj_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed dołączeniem i poprawieniem wiersza tabeli IS_WYPL - nałożenie filtra na tabelę osób.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OSOBA.prefix();
{? PAR_SKID.get(188)='T'
|| OSOBA.f_set(
      'NAZWISKO,PIERWSZE,PESEL',,
      'REFERENCE in (select distinct :_a.OSOBA from :_a)',
      exec('dostepne_p','schemat','PPL','P','*')
   )
|| OSOBA.f_set(
      'NAZWISKO,PIERWSZE,PESEL',,
      'REFERENCE in (select P.OSOBA from P where P.FIRMA=:_a and P.F_ZATR=\':_b\')',
      exec('ref_firma','ustawienia'),__F_ZATR.REF
   )
?}


\is_wypl_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed usunięciem wiersza tabeli IS_WYPL
::   WE:
::   WY:
::  OLD: \mod_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? REF.OSOBA=null
|| FUN.emsg('Brak uprawnień do usunięcia bieżącego wiersza.'@);
   return(0)
?};

{? IS_WYPL.MIES='N' & IS_WYPL.IS_DEF().OSOBA='T'
|| exec('o_writable','lista_plac',IS_WYPL.LT)

|? IS_WYPL.LT=exec('nie_lista_isplac','lista_licz')
|| {? exec('jest_rozl','!ppl_grp_aswi')
   || exec('del_warn','#table');
      0
   || 1
   ?}

|? ~exec('o_writable','lista_plac',IS_WYPL.LT)
|| 0

|| 1
?}


\is_wypl_okno_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Formuła na okno po tabeli IS_WYPL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OSOBA.f_clear();
1


\is_wypl_rozlicz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyświetla informacje o rozliczeniu świadczenia
::   WE:
::   WY:
::  OLD: \mod_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? IS_WYPL.MIES<>'T'
|| FUN.info(
      'Funkcja jest dostępna tylko dla świadczeń\n'
      'zdefiniowanych jako wypłacane miesięcznie.'@,
   );
   return()
?};

IS_ROZL.cntx_psh();
{? IS_WYPL.IS_DEF().R
|| IS_ROZL.win_sel('GRP_LT');
   IS_ROZL.win_edit('LISTA')
|| IS_ROZL.win_sel('GRP_D');
   IS_ROZL.win_edit('DATA')
?};

IS_ROZL.index('DATA');
IS_ROZL.prefix(IS_WYPL.ref());
IS_ROZL.select();
IS_ROZL.cntx_pop();
~~


\is_wypl_rekord_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Rekord przed tabeli IS_WYPL
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY: Napis oznaczjący kolor
::  OLD: \rkprz_is_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
IS_WYPL.OSOBA().NAZWISKO;
exec('chk_upr','osoba')


\is_wypl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws
:: OPIS: Rekord po IS_WYPL. Sprawdzenie poprawności danych wprowadzanych do tabeli IS_WYPL
::   WY: zgodny ze specyfikacją akcji rekord po
::  OLD: \spr_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
: sprawdź wypełnienie wymaganych pól
{? (_spr:=__CHK.record(IS_WYPL,,'OSOBA','IS_DEF','OD','DO','KW'))<>''
|| return(_spr)
?};

{? ~exec('czy_prac','osoba',IS_WYPL.OSOBA)
|| FUN.emsg(
      'Osobie, która nie jest zatrudniona, nie mogą być\n'
      'wypłacane świadczenia należne pracownikom.'@,
   );
   return('OSOBA')
?};

{? IS_WYPL.DO<IS_WYPL.OD
|| exec('alert','overlap');
   return('OD')

|? IS_WYPL.OD<IS_OPIS.OD
|| __CHK.err_fld(IS_WYPL,'OD',1,'Data nie może być wcześniejsza od %1.'@[$IS_OPIS.OD]);
   return('OD')

|? IS_OPIS.DO<IS_WYPL.DO
|| __CHK.err_fld(IS_WYPL,'DO',1,'Data nie może być późniejsza od %1.'@[$IS_OPIS.DO]);
   return('DO')

|? IS_WYPL.LT<>''
|| O.cntx_psh();
   O.index('LISTYPZN');
   O.prefix(exec('ref_firma','ustawienia'));
   {? O.find_key(-IS_WYPL.LT,)
   || {? (date(O.R,O.M,0)<date(IS_WYPL.OD~1,IS_WYPL.OD~2,1) |
         date(IS_WYPL.DO~1,IS_WYPL.DO~2,0)<date(O.R,O.M,0)) &
         ~FUN.ask(
            'Data rozliczenia listy płac %1 jest niezgodna z okresem wypłaty świadczenia.\n'
            'Czy pozostawić wprowadzoną informację?'@
            [IS_WYPL.LT]
         )
      || O.cntx_pop();
         return('LT')
      ?}
   ?};
   O.cntx_pop()
?};

{? exec('lic','#b_domain','POR') & IS_WYPL.IS_DEF().BNFTT<>null()
:: Weryfikacja okresu dostępności benefitu - okreś świadczenia musi się w całości w nim zawierać.
|| BNFTT.cntx_psh();
   BNFTT.prefix();
   IS_WYPL.IS_DEF().BNFTT();
   _ok:=BNFTT.OD<=IS_WYPL.OD & (BNFTT.DO=date(0,0,0) | IS_WYPL.DO<=BNFTT.DO);
   _bnftt:=BNFTT.AKRONIM;
   _ob:='%1 - %2' [$BNFTT.OD,$BNFTT.DO];
   BNFTT.cntx_pop();
   {? ~_ok
   || FUN.info('Okres świadczenia wykracza poza okres dostępności (%1) związanego z nim benefitem (%2).'@ [_ob,_bnftt]);
      return('OD')
   ?}
?};

_ref:={? -menu_txt()='popraw' || IS_WYPL.ref() || null ?};

{? exec('check','overlap',_ref,IS_WYPL,,,,,,'UNIQUE',IS_OPIS.ref(),IS_WYPL.OSOBA,IS_WYPL.IS_DEF)<>''
|| return('OD')
?};

{? _ref & exec('jest_rozl','!ppl_grp_aswi')
|| {? IS_ROZL.D<IS_WYPL.OD
   || __CHK.err_fld(IS_WYPL,'OD',1,'Data nie może być późniejsza od %1.'@[$IS_ROZL.D]);
      return('OD')
   ?};
   IS_ROZL.last();
   {? IS_WYPL.DO<IS_ROZL.D
   || __CHK.err_fld(IS_WYPL,'OD',1,'Data nie może być wcześniejsza od %1.'@[$IS_ROZL.D]);
      return('DO')
   ?}
?};
1


\is_wypl_szukaj_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Dokładne wyszukiwanie zapisów
::   WE:
::   WY:
::  OLD: \szd_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
exec('is_wypl_szukaj','!ppl_grp_aswi',':*')


\is_wypl_szukaj_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Kontekstowe wyszukiwanie zapisów
::   WE:
::   WY:
::  OLD: \szk_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
exec('is_wypl_szukaj','!ppl_grp_aswi',':-')


\is_wypl_szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyszukiwanie zapisów dokładnie lub kontekstowo zależnie od argumentu
::   WE:
::   WY:
::  OLD: \szuk_wypl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
SZUKAJ.win_edit('IS_WYPL');
SZUKAJ.blank;
{? SZUKAJ.edit
|| {? ~IS_WYPL.find_tab(0,
         'OSOBA', 'NAZWISKO',_a,SZUKAJ.NAZWISKO,
         'OSOBA', 'PIERWSZE',_a,SZUKAJ.IMIE,
         'OSOBA', 'PESEL',   _a,SZUKAJ.PESEL,
         'IS_DEF',,          _a,SZUKAJ.ISW_OPIS,
         'MIES',,            _a,SZUKAJ.ISW_MIES,
         'OD',,              _a,SZUKAJ.OD,
         'DO',,              _a,SZUKAJ.DO,
         'KW',,              _a,SZUKAJ.KW,
         'LT',,              _a,SZUKAJ.LT
      )
   || FUN.info('Nie znaleziono zapisu zgodnego ze wzorcem.'@)
   ?}
?};
1


\is_wypl_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAKO [2010]
:: OPIS: Wartosc poczatkowa pola IS_ROZL.IS_WYPL
::  OLD: \is_wypl/kali.fml
::----------------------------------------------------------------------------------------------------------------------
IS_WYPL.ref()


\is_wypl_kw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAKO [2010]
:: OPIS: Wartosc poczatkowa pola IS_ROZL.KW
::  OLD: \isr_kw/kali.fml
::----------------------------------------------------------------------------------------------------------------------
IS_WYPL.KW


\is_rozl_lt_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wartość początkowa pola LT (lista płac) tabeli IS_ROZL
::   WE:
::   WY:
::  OLD: \rozl_lt/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~IS_WYPL.IS_DEF().R
|| exec('nie_lista_isplac','lista_licz')
|| VAR.NAZWALIS
?}


\is_rozl_kw_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Funkcja wywoływana po redakcji pola KW tabeli IS_ROZL
::   WE:
::   WY:
::  OLD: \rozl_ppo/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? IS_ROZL.KW<=0
|| IS_ROZL.KW:=IS_WYPL().KW
?};
1


\is_rozl_edisp_nfo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Odświaża zawartość wizytówki.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
grp_edisp(IS_WYPL,'NFO')


\is_rozl_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed poprawieniem rekordu tabeli IS_ROZL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('is_rozl_mod_b','!ppl_grp_aswi')


\is_rozl_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed usunięciem rekordu tabeli IS_ROZL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('is_rozl_mod_b','!ppl_grp_aswi')


\is_rozl_mod_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa akcji modyfikujących rozliczenie świadczenia.
::   WE:
::   WY:
::  OLD: \mod_rozl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('test_list','!ppl_grp_aswi',IS_WYPL.OSOBA,IS_WYPL.IS_DEF().R().RN,IS_ROZL.LT)
|| exec('o_writable','lista_plac',IS_ROZL.LT,1)
|| 1
?}


\is_rozl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdz poprawność danych wprowadzanych do tabeli IS_ROZL
::   WE:
::   WY:
::  OLD: \spr_rozl/isplac.fml
::----------------------------------------------------------------------------------------------------------------------
_pref:=null;
:  czy rozliczać na liście płac
{? IS_ROZL.LT<>exec('nie_lista_isplac','lista_licz')
:  uzupełnij niewidoczną w tym przypadku datę
:  ustal ją na koniec miesiąca listy płac
|| O.cntx_psh();
   O.index('LISTYPZN');
   O.prefix(exec('ref_firma','ustawienia'));
   {? IS_ROZL.LT=''
   || IS_ROZL.D:=date(0,0,0)
   |? O.find_key(-IS_ROZL.LT)
   || IS_ROZL.D:=date(O.R,O.M,0)
   ?};
   O.cntx_pop()
:  sprawdź, czy podana data nie wykracza poza
:  okres, na który przyznano świadczenie
|| _od:=IS_WYPL.OD;
   _do:=IS_WYPL.DO;
   {? IS_ROZL.D<_od
   || __CHK.err_fld(IS_ROZL,'D',1,'Data nie może być wcześniejsza od %1.'@[$_od]);
      return('D')
   |? _do<IS_ROZL.D
   || __CHK.err_fld(IS_ROZL,'D',1,'Data nie może być późniejsza od %1.'@[$_do]);
      return('D')
   ?}
?};

_mod:=0;
_ref:=null;
_pref:=null;

{? -menu_txt='popraw'
|| _mod:=1;
   _ref:=IS_ROZL.ref();
   _pref:=IS_ROZL.P
?};

{? ~_ref & IS_ROZL.IS_WYPL().IS_DEF().OSOBA<>'T'
|| P.cntx_psh();
   P.index('PRACOSOW');
   P.prefix(exec('ref_firma','ustawienia'),OSOBA.ref());
   {? P.first()
   || IS_ROZL.P:=P.ref();
      {? P.size()>1
      || _menu:='popup(0,_a';
         {!
         |? _menu+=
               ',\''+P.ZA+' '+$P.DZA+' '+$P.DZ+' '+
               form(P.WYDZIAL().SYMBOL,18)+' '+
               P.ST().ST+'\',,'+
               '"P.seek('+$#P.ref+',P.name);'+
               'IS_ROZL.P:=P.ref"';
            P.next()
         !};
         _menu+=')';
         ($_menu)(OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE)
      ?};
      P.cntx_pop()
   || P.cntx_pop();
      FUN.emsg(
         'Osobie, która nie jest zatrudniona, nie mogą być\n'
         'wypłacane świadczenia należne pracownikom.'@
      );
      return(0)
   ?}
?};

{? __CHK.index(IS_ROZL,_mod)<>''
|| return(0)
?};

1

:Sign Version 2.0 jowisz:1045 2024/01/15 11:35:52 742d70813822ccb9d732b8de3ad9daaec83554bc8cb2bc18a1f69452ce0a8b1b9d2b90f73ef4b6cae94a49785f76d622713874eac330be36f511eef6985643dd5654a616090f01020a260058a20d32ac7054a43e86e57691abf19e347b82b5e89da01f40004776a56a293ee939aa492aca67613a019978b7090915b8ddb3bef6

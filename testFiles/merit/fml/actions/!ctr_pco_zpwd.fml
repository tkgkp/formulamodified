:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pco_zpwd.fml
:: Utworzony: 20.05.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PCO_ZPWD - Definicje podziałów dla wynagrodzeń
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Definicje podziałów dla wynagrodzeń - główna formuła czynności CTR_PCO_ZPWD
::----------------------------------------------------------------------------------------------------------------------
::# permissions=UD_SKL,F_ZATR


\firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [2006]
:: OPIS: Kontroling dla firmy.
::  OLD: \hist_fir/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('CON','object');
exec('set_fml','control');
CON_KW.index('CON_KWH');
_vc:=VAR.CON; VAR.CON:='F';
_o:= exec('mod_win','control');
_con_wer:=SKID_MBN.grp_make('Historia schematów dla firmy'@,"
   SKID_MBN.index('EWID'); SKID_MBN.prefix('T','O');
   ~~
",'con_kh_gf');
SKID_MBN.grp_sel(_con_wer,SKID_MBN,_o,,"
   UD_POM.SKID_MB:=SKID_MBN.ref();
   CON_KH.index('CON_KHF');
   CON_KH.prefix('F',UD_POM.SKID_MB);
   grp_disp(CON_KH,'WER_W',1);
   ~~
",,,,,,,,'maximized_with_title');
SKID_MBN.grp_splt(_con_wer,,'vertical','left');
SKID_MBN.grp_sel(_con_wer,CON_KH,'WER_W',,"
   exec('set_enabled','control');
   {? CON_KH.get()
   || CON_KW.prefix(CON_KH.ref());
      CON_KW.actions('WER',,,1)
   || CON_KW.prefix(null);
      CON_KW.actions('WER',':D',,1)
   ?};
   grp_disp(CON_KW,'WER');
   ~~
",0,0,7,,,,,'maximized_with_title');
SKID_MBN.grp_splt(_con_wer,'left','horizontal','panel1');
SKID_MBN.grp_sel(_con_wer,CON_KW,'WER',,"CON_KW.prefix(CON_KH.ref)",,,,,,,,'maximized_with_title');
SKID_MBN.actions('WER','');
CON_KW.win_edit('RED');
CON_KH.win_patt('REDF'); CON_KH.win_edit('REDF');
SKID_MBN.cntx_psh();
SKID_MBN.win_sel(_con_wer);
SKID_MBN.select();
CON_KH.win_patt('WZO');
VAR.CON:=_vc;
SKID_MBN.cntx_pop()


\ba_con_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KGS [2010]
:: OPIS: Przed dołącz tabeli CON_KH
::----------------------------------------------------------------------------------------------------------------------
VAR.P:=0;
1


\bm_con_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: darokr [2010]
:: OPIS: Przed popraw tabeli CON_KH
::----------------------------------------------------------------------------------------------------------------------
VAR.P:=1


\po_red_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [2006]
:: OPIS: Po redakcji pola CON_KW.W
::   WE: _a - wartosc pola CON_KW.W
::  OLD: \po_red_w/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()='T'
|| CON_KW.N_POZ_B:=null
?};
win_disp();
1


\po_red_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Po redakcji CON_KK.G
::   WY: 1
::  OLD: \po_red_g/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()='T'
|| exec('null_ud_pom','control');
   CON_KW.PROC:=100
?};
1


\skl_zus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [2006]
:: OPIS: Po redakcji pola CON_KW.ZUS  oraz na rekord po
::   WE: _a - wartosc pola CON_KW.ZUS
::  OLD: \skl_zus/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:=fld ?};
{? _a='T' ||
   CON_KW.OB_KOSZ:=null; CON_KW.PODZORG:=null; CON_KW.N_POZ_B:=null; CON_KW.WYM4:=null;
   CON_KW.WYM5:=null; CON_KW.W:='N'; CON_KW.G:='N'; CON_KW.PROC:=100;
   UD_POM.JORG:=''; UD_POM.OKOSZ:=''; UD_POM.WYM4:=''; UD_POM.WYM5:='';
   win_disp
?};
1


\prof_okno_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [2006]
:: OPIS: Sprawdzenie poprawnosci przypisania podzialu procentowego - nie moze byc rozna od 100
::       dla tych samych rubryk i pozycji budzetowej
::  OLD: \prof_okno_po/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,0)<>CON_KW || return ?};
_tab:=sql('select UD_SKL.SYMBOL, R.RN, R.RT, sum(PROC) PROC
         from CON_KW join R left join UD_SKL using(CON_KW.POZ_BUD,UD_SKL.REFERENCE)
         where CON_KW.CON_KH=:_a
         group by UD_SKL.SYMBOL, R.RN, R.RT
         having sum(PROC)<>100
         order by 1,2',CON_KH.ref);

{? _tab.first()
|| FUN.info(
      'Wartość procentowa rubryki płacowej: \n'+'(%1) %2'
      ' dla pozycji budżetowej: %3'
      '\n jest różna od 100. Proszę poprawić dane.'@[$_tab.RN,_tab.RT,{? _tab.SYMBOL='' || 'FORMUŁA' || _tab.SYMBOL ?}]
   );
   &_tab; 0
|| &_tab; 1
?}


\null_ud_pom2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: dolacz - schematy kontrolingu
::  OLD: \null_ud_pom2/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
exec('null_ud_pom','control');
VAR.P:=0; 1


\ar_con_kw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [2006]
:: OPIS: Formula na rekord po tabeli CON_KW
::  OLD: \prof_rek_po/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAR.CON='R' | VAR.CON='Z' || CON_KW.G:='N' ?};
{? CON_KW.ZUS='T'
|| exec('skl_zus','!ctr_pco_zpwd','T');
   _spr:=__CHK.record(CON_KW,,'R')
|| _spr:=__CHK.record(CON_KW,,'R');
   {? _spr<>'' || return(_spr) ?};
   {? CON_KW.G='T'
   || {? CON_KW.OB_KOSZ | CON_KW.PODZORG | CON_KW.WYM4 | CON_KW.WYM5
      || FUN.info(
            'Niedozwolone jest ustawienie dodatkowego wymiaru\n'
            'dla zaznaczonej opcji - Wynika z godzin.'@
         );
         return('G')
      ?}
   || CON.ustaw(CON_KW.SKID_MB);
      {? CON.T_MBP.first()
      || _wym:=''; CON.T_MBP.next();
         {! |? _wym+={? CON.T_MBP.UD_TYP<>'PODZORG' || ',\''+CON.T_MBP.POLE+'\'' || '' ?}; CON.T_MBP.next() !};
         {? +_wym
         || _spr:=($('__CHK.record(CON_KW,'+_wym+')'))();
            {? _spr<>''
            || {? _spr='POZ_BUD'
               || return('POZ_BUD')
               || return(CON.spr_t(_spr,'POLE','UD_POM'))
               ?}
            ?}
         ?}
      ?};
      {? ~exec('ustaw_proc','control',1) || return('PROC') ?}
   ?}
?};
{? _spr<>'' || return(_spr) ?};
{? CON_KW.W='N' || CON_KW.N_POZ_B:=null ?};
{? CON_KW.CZY_FOR='T'
|| _spr:=__CHK.record(CON_KW,,'FORM')
|| _spr:=__CHK.record(CON_KW,,'POZ_BUD')
?};
_spr


\pracownicy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Definicje schematów podziałów dla pracowników
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','control');
SKID_MBN.actions('WER','');
CON_KW.index('CON_KWH'); CON_KW.win_edit('REDP');
CON_KH.win_edit('RED');
_okno:=SKID_MBN.grp_make('Pracownicy',"
   UD_POM.SKID_MB:={? SKID_MBN.first() || SKID_MBN.ref() || null ?};
   exec('set_enabled2','control')
",'uddeforg');
_om:=exec('mod_win','control');
SKID_MBN.win_sel(_om);
SKID_MBN.grp_sel(_okno,SKID_MBN,_om,,"
   VAR.CON:='D';
   exec('filtruj_p','schemat','CTR',UD_DEF.ref(),'P','T','W');
   UD_POM.SKID_MB:=SKID_MBN.ref();
   exec('set_enabled2','control');
   grp_disp(P,'CTR',1)
",,,,,,,,'maximized_with_title');
SKID_MBN.grp_splt(_okno,,'vertical','left');
SKID_MBN.grp_sel(_okno,P,'CTR',,"
   exec('ar_p','control')
",,,8,"",,,,'maximized_with_title');
SKID_MBN.grp_splt(_okno,'left','horizontal','bottom');
SKID_MBN.grp_sel(_okno,CON_KH,'WER_P',,"
   _ref:={? CON_KH.get() || CON_KH.ref() || null ?};
   CON_KW.index('CON_KWH');
   CON_KW.prefix(_ref);
   CON_KW.actions('WER',{? _ref || '' || 'D:D' ?},,1)
",,,8,"
   exec('set_enabled2','control')
",,,,'maximized_with_title');
exec('domyslny','schemat','PODZORG');
UD_DEF.index('SYMBOL'); UD_DEF.prefix(UD_SCH.ref()); UD_DEF.first();
exec('__F_ZATR','object');
exec('CON','object');
_vc:=VAR.CON;
VAR.CON:='P'; def_prac:=1;
__F_ZATR.mod('P');
SKID_MBN.cntx_psh();
SKID_MBN.index('EWID'); SKID_MBN.prefix('T','O');
SKID_MBN.win_sel(_okno);
SKID_MBN.select();
SKID_MBN.cntx_pop();
VAR.CON:=_vc;
CON_KW.win_edit('RED');
CON_KH.actions('WER_P');
VAR_DEL.delete('def_prac')


\kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [2006]
:: OPIS:  Kopiowanie schematu kontrolingu
::  OLD: \kopiuj/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('kopia_kon_p','!ctr_pco_zpwd',CON_KH.ref,CON_KH.SKID_MB,DEFINE.OD,P.F_ZATR().KOD)
|| FUN.info('Skopiowano schemat do pracownika %1.'@[P.OSOBA().PIERWSZE+' '+P.OSOBA().NAZWISKO])
|| FUN.info('Kopiowanie schematu do pracownika %1 jest zbędne.'@[P.OSOBA().PIERWSZE+' '+P.OSOBA().NAZWISKO])
?}


\kopia_kon_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [2006]
:: OPIS: Dodanie nowego schematu kontrolingu dla pracownika podczas kopiowania
::   WE: _a - ref rekordu tabeli CON_KH (wzor)
::       _b - ref SKID_MBN
::       _c - data od
::       _d - P - pracownik; Z - zleceniobiorca
::  OLD: \kopia_kon_p/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>7 || return(0) ?};
{? var_pres('_b')<>7 || return(0) ?};
{? var_pres('_c')<>type_of(date) || return(0) ?};
{? var_pres('_d')<>type_of('') || return(0) ?};
CON_KH.prefix();
CON_KH.P:=P.ref(); {? CON_KH.find_key(_d,CON_KH.P,_b,_c) || return(0) ?};
CON_KH.F:=_d; CON_KH.OD:=_c;
{? CON_KH.add()
|| exec('kopia_kontr','control',CON_KH.ref(),_a)
?}


\poz_schem
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Pokazuje okno z pozycjami schematu controllingu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
CON_KW.select()


:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:55 c7b6ce6bd07323b1e8aa2b99f21280c868b4a924debe58a01eb143146d2aa7012d50a8c616c5c95826248ade4a4d47f7ead837c71363d380becc3943e53b18f3ab7ac6392b5820c5e457b2b42931fd57dcbb8c3afab4e117f2b933b00f6cc4217d1c370e0d1265dd964c15a32dbc48596ce0f3929b919cae9b3487bd0cfdddfa

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_fak_easp.fml
:: Utworzony: 19.02.2019
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa akceptacji paczki faktur
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP
::# properties=SERVICE
::# parses=exec('parses','!lum_fak_eapa')
::# kind=WE,   symbol=FPACZ,  type=_FPACZ,   name=Paczka faktur,  required=T, keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

_akcja:=_mp.akcja();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & (_mp.isAutoRun() | _mp.isService()));

{? ~(var_pres('FPACZ',_in)=type_of(null()) & _in.FPACZ)
||
   _mp.error('Brak wymaganego parametru FPACZ.')
||
   FPACZ.cntx_psh();
   FPACZ.prefix();
   {? ~FPACZ.seek(_in.FPACZ)
   ||
      _mp.error('Nie znaleziono paczki faktur.')
   ||
      {? _akcja='Akceptuj' | _auto
      ||
         _akceptuj:=exec('fpacz_akceptuj','!lum_fak_eapa',_auto);
         {? _akceptuj='done' || {? FPACZ.put() || _mp.done() ?}
         |? +_akceptuj  || _mp.error(_akceptuj)
         ?}

      |? _mp.pathTodo()
         | _mp.pathArea() & _akcja=''
      ||
         exec('fpacz_win_edit_set','!lum_fak_eapa');
         _win_red:=FPACZ.win_edit('?');
         _cntx:=obj_new('AKCEPTUJ');
         _cntx.AKCEPTUJ:=0;
         params_set('cntx',_cntx);
         FPACZ.display();
         {? _cntx.AKCEPTUJ
         ||
            {? FPACZ.put() || _mp.done() ?}
         ?};
::       Usunięcie definicji tymczasowych okien
         FPACZ.win_edit(''); FPACZ.win_edel(_win_red)
      ||
         _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   FPACZ.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FPACZ',_in)=type_of(null()) & _in.FPACZ
|| 'Zaakceptuj paczkę faktur: %1'@@[exec('record','#to_string',_in.FPACZ)]
|| ''
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('FPACZ',_in)=type_of(null()) & _in.FPACZ
||
   FPACZ.cntx_psh();
   FPACZ.prefix();
   {? FPACZ.seek(_in.FPACZ)
   ||
      __PARSES.setVal('OddzialLogProd',FPACZ.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=FPACZ.AR;
      _args.AM:=FPACZ.DR~2;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FPACZ.cntx_pop()
?};

1


\fpacz_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Akceptacja
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       params_get()   - ustawiane w exec('main','!lsp_fak_easp')
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};

_result:='';

{? FPACZ.STAT_REJ='T'
||
   {? ~_auto || FUN.info('Paczka faktur został już zaakceptowana.'@) ?};
   _result:='done'

|? FPACZ.STAT_REJ='N'
||
   {? ~_auto || FUN.info('Nie zakończono jeszcze rejestracji paczki faktur.\nAkceptacja niemożliwa.'@) ?};
   _result:='Nie zakończono jeszcze rejestracji paczki faktur. Akceptacja niemożliwa.'

|? _auto | FUN.ask('Zaakceptować paczkę faktur %1?'@[exec('record','#to_string',FPACZ.ref())])
||
   FPACZ.STAT_REJ:='T';
   FPACZ.STAT_GEN:=exec('stat_gen','umowy_faktury',0);
   _result:='done'
?};

_result


\fpacz_akceptuj_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Akeptuj w oknie redakcji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('fpacz_akceptuj','!lum_fak_eapa')='done'
||
   params_get().cntx.AKCEPTUJ:=1;
   'key:F2'
||
   ''
?}


\fpacz_akceptuj_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Akceptuj w oknie wertowania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('fpacz_akceptuj','faktury_wspolne')


\fpacz_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Ustawia okno redakcji tabeli FPACZ, wymagane pola, okna słowników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_red:=exec('fpacz_win_edit','umowy_faktury');
_ff:="params_exec('fpacz_akceptuj_red','!lum_fak_eapa')";
FPACZ.win_ebtn(_win_red,
   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
_ff:='key:Esc';
FPACZ.win_ebtn(_win_red,
   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
FPACZ.win_edit(_win_red);
exec('fpacz_set_efld_opt','umowy_faktury',_win_red);
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 089436dbc298e683c5ecfc5c00c1f6fb15d087c099c30ad93624320d35a1336ca5e3c3a3cf2581db2cc0c3a5f0a29e860c233718c9a2e787fc9db2c6922fad5f580fbcb446386e138498c5307d9f16e4671fa82693c0f785d869ad9b842ad19966580ea659c0b89c1df6adbef997cd625263b8123dc8d642a2aa339c9977bcb2

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pkal.fml
:: Utworzony: 29.01.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PKAL - Kalendarze.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Kalendarze - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::
params_set(params_get);
exec('run','!zws_par_pkal','__KAL_ADM');
~~


\run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Kalendarze - utworzenie, skonfigurowanie i wyświetlenie menadżera.
::       _a [STRING] - identyfikator dla menadżera kalendarzy
::----------------------------------------------------------------------------------------------------------------------
::deklaracje potrzebnych obiektów
exec('__KAL','object');
: deklaruj klasę i utwórz obiekt menadżera
exec('declare_con','!zws_par_pkal');
_adm:=obj_new(@.CLASS.KAL_ADM);

: ustaw właściwości nawigatorów i menadżera
exec('setup_nav','!zws_par_pkal',_adm.YEARS,_a,'#years_wnd');
exec('setup_nav','!zws_par_pkal',_adm.NAMES,_a,'#names_wnd');
exec('setup_con','!zws_par_pkal',_adm,_a);
($(_a+':=_a'))(_adm);

_par:=params_get;
: rok kalendarza
_rok:=
   {? var_pres('_par')>100 & var_pres('in',_par)>100 & var_pres('FP_INS_NUM',_par.in)=type_of(0)
   || _par.in.FP_INS_NUM
   || date~1
   ?};

: utwórz kalendarz standard na dany rok
_std:='standard';
KAL_ROK.cntx_psh;
KAL_ROK.index('KAL_NAZ');
KAL_ROK.prefix(exec('ref_firma','ustawienia'));
{? ~KAL_ROK.find_key(_std,_std,_rok)
|| KAL_ROK.blank(1);
   KAL_ROK.NAZWA:=exec('nazwa_ref','kalendarz',_std);
   KAL_ROK.ROK:=_rok;
   KAL_ROK.WZORZEC:=_std;
   KAL_ROK.MAXTIME:=time(8,0,0);
   KAL_ROK.add
?};
KAL_ROK.cntx_pop;

: ustaw okna w zależności od parametrów
exec('set_win','kaledit');

_kalend_czas_be:=KALEND.fld_fml('CZAS','BEFORE_DISPLAY',"params_exec('sum_time','kaledit');1");

: wyświetl
_adm.show(_rok);

KALEND.fld_fml('CZAS','BEFORE_DISPLAY',_kalend_czas_be);

: usuń obiekt menadżera
($('obj_del('+_a+')'))();
($('&'+_a))();
~~


\kal_wzor_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wybór wzorca kalendarza.
::   WE: _a - domyślna wartość (opcjonalny typu STRING)
::  OLD: \wyb_wzor/kaledit.fml
::----------------------------------------------------------------------------------------------------------------------
_nazwa:={? var_pres('_a')<>type_of('') || '' || _a ?};

KAL_WZOR.cntx_psh;
KAL_WZOR.index('KAL_WZOR');
KAL_WZOR.prefix(exec('ref_firma','ustawienia'));
KAL_WZOR.find_key(_nazwa,_nazwa);
KAL_WZOR.win_sel('WYB');
{? KAL_WZOR.select(,1)
|| _nazwa:=KAL_WZOR.NAZWA
?};
KAL_WZOR.cntx_pop;

_nazwa


\kal_wzor_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Akcja dla klawisza F3 pola wzorzec tabeli KAL_ROK.
::  OLD: \wyb_wzor_kal/kaledit.fml
::----------------------------------------------------------------------------------------------------------------------
exec('kal_wzor_wyb','!zws_par_pkal',fld)


\kal_wzor_nazwa_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Po redagowaniu pola 'NAZWA'
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? KAL_WZOR.NAZWA='standard'
|| KAL_WZOR.H_MOD:='N'
|| 1
?}


\kal_wzor_h_mod_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Przed redagowaniem pola H_MOD
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
KAL_WZOR.NAZWA<>'standard'


\kal_opis_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Przed dołączeniem rekordu KAL_OPIS
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;

_fml_chk:="
   _par:=params_get();
   _fld:=__CHK.record(EDIT_VAR,,'DATA');
   {? _fld='' || _fld:=__CHK.record(KAL_OPIS,,'DZIEŃ') ?};
   {? _fld<>''
   || _fld
   |? EDIT_VAR.DATA~1<>_par.rok
   || FUN.emsg('Podana data musi być z roku: %1'@[$_par.rok]);
      'DATA'
   || 1
   ?}
";

:: pobieram dane o KAL_ROK z widoku okna parametryzatora
_view:=__KAL_ADM.get_view();
_kal_rok:=_view.year_ref();
KAL_ROK.cntx_psh();
KAL_ROK.prefix();
{? KAL_ROK.seek(_kal_rok)
|| _rok:=KAL_ROK.ROK;
   KAL_WZOR.cntx_psh();
   KAL_WZOR.index('KAL_WZOR');
   KAL_WZOR.prefix(exec('ref_firma','ustawienia'),KAL_ROK.WZORZEC,KAL_ROK.WZORZEC);
   {? KAL_WZOR.first()
   || _wzorzec:=KAL_WZOR.ref();
      EDIT_VAR.DATA:=date(_rok,1,1);
      KAL_OPIS.cntx_psh();
      KAL_OPIS.index('KAL_OPIS');
      KAL_OPIS.prefix(_wzorzec,_rok);
      KAL_OPIS.blank();
      KAL_OPIS.WZORZEC:=_wzorzec;
      KAL_OPIS.ROK:=_rok;
      KAL_OPIS.TYP:='S';
      KAL_OPIS.STATUS:='R';
      _old_win:=KAL_OPIS.win_edit('?');
      KAL_OPIS.win_edit('RED_H');
      params_set('rok',_rok);
      {? KAL_OPIS.edit(_fml_chk)
      || KAL_OPIS.DATA:=$EDIT_VAR.DATA;
         _buffer:=exec('KAL_OPIS','buffer');
         _buffer.get();
         _result:=exec('add_to_kal_opis','kalendarz',_buffer);
         {? _result=0
         || FUN.emsg('Nie udało się dodać rekordu.'@)
         |? _result=-2
         || FUN.emsg('Na podaną date %1 występuje już rekord w tabeli.\nNie dokonano zapisu.'@[$EDIT_VAR.DATA])
         ?}
      ?};
      KAL_OPIS.cntx_pop();
      KAL_OPIS.win_edit(_old_win)
   ?};
   KAL_WZOR.cntx_pop()
?};
KAL_ROK.cntx_pop();
{? _result>0
|| {? KAL_OPIS.f_active() || KAL_OPIS.f_rfresh() ?};
:: konieczna aktualizacja informacji w obiekcie __KAL
   __KAL.ws.dirty()
?};
_result


\kal_opis_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Formuła usuwająca rekordy z tabeli KAL_OPIS
::   WE: _a [REFERENCE] - wskazanie na wzorzec (KAL_WZOR)
::       _b [NUMBER]    - rok
::   WY: -1/0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_wzorzec:={? var_pres('_a')=type_of(null) || _a || return(0) ?};
    _rok:={? var_pres('_b')=type_of(0)    || _b || return(0) ?};
_result:=0;

:: zalozenie transakcji
_mydo:=do_state()=0;
{? _mydo || do() ?};
KAL_OPIS.cntx_psh();
KAL_OPIS.index('KAL_OPIS');
KAL_OPIS.prefix(_wzorzec,_rok);
{? KAL_OPIS.first()
|| {! |? KAL_OPIS.del() !}
?};
{? KAL_OPIS.first()
|| undo()
|| _result:=1
?};
KAL_OPIS.cntx_pop();
{? _mydo || end() ?};
_result


\kal_opis_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Przed usunięciem rekordu KAL_OPIS
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? KAL_OPIS.NUMER>=0 & FUN.ask('Czy usunąć bieżący wiersz?'@)
|| _nr:=KAL_OPIS.NUMER;
   _rok:=KAL_OPIS.ROK;
   _wzorzec:=KAL_OPIS.WZORZEC;
:: zalozenie transakcji
   _mydo:=do_state()=0;
   {? _mydo || do() ?};
   _result:=KAL_OPIS.del();
:: należy przenumerować wszystkie rekordy po usuniętym rekordzie
   {? _result
   || _lput:=0;
      _lrec:=0;
      KAL_OPIS.cntx_psh();
      KAL_OPIS.index('KAL_OPIS');
      KAL_OPIS.prefix(_wzorzec,_rok);
      {? KAL_OPIS.first()
      || {!
         |? {? KAL_OPIS.NUMER>_nr
            || _lrec+=1;
               KAL_OPIS.NUMER:=KAL_OPIS.NUMER-1;
               _lput+=KAL_OPIS.put()
            ?};
            KAL_OPIS.next()
         !}
      ?};
      KAL_OPIS.cntx_pop();
      {? _lput<>_lrec || undo() ?}
   ?};
   {? _mydo || end() ?};

   {? _result>0
   || {? KAL_OPIS.f_active() || KAL_OPIS.f_rfresh() ?};
::    konieczne aktualizacja informacji w obiekcie __KAL
      __KAL.ws.dirty()
   ?}
?}


\kal_opis_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Przed poprawieniem rekodru KAL_OPIS
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
KAL_OPIS.cntx_psh();
{? KAL_OPIS.NUMER<8 | KAL_OPIS.WZORZEC().H_MOD<>'T'
|| {? exec('isEnabled','prc_przerwy')
   || KAL_OPIS.win_edit('RED_P')
   || KAL_OPIS.win_edit('RED')
   ?}
|| KAL_OPIS.win_edit('RED_H');
   EDIT_VAR.DATA:=exec('str2date','#convert',KAL_OPIS.DATA)
?};
{? KAL_OPIS.edit("exec('kal_opis_ae','!zws_par_pkal')")
|| {? KAL_OPIS.NUMER>8
   || KAL_OPIS.DATA:=$EDIT_VAR.DATA
   ?};
   _result:=KAL_OPIS.put()
?};
KAL_OPIS.cntx_pop();
{? _result>0
|| {? KAL_OPIS.f_active() || KAL_OPIS.f_rfresh() ?};
:: konieczna aktualizacja informacji w obiekcie __KAL
   __KAL.ws.dirty()
?};
_result


\kal_opis_przywroc_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Przed "Przywróć" wzorzec - przywraca wzorzec na podstawie wzorca standard
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy przywrócić zdefiniowany wzorzec kalendarza do stanu początkowego?'@)
||
:: pobieram dane o KAL_ROK z widoku okna parametryzatora
   _view:=__KAL_ADM.get_view();
   _kal_rok:=_view.year_ref();
   KAL_ROK.cntx_psh();
   KAL_ROK.prefix();
   {? KAL_ROK.seek(_kal_rok)
   || _rok:=KAL_ROK.ROK;
      KAL_WZOR.cntx_psh();
      KAL_WZOR.index('KAL_WZOR');
      KAL_WZOR.prefix(exec('ref_firma','ustawienia'),KAL_ROK.WZORZEC,KAL_ROK.WZORZEC);
      {? KAL_WZOR.first()
      || _wzorzec:=KAL_WZOR.ref();
         exec('kal_opis_usun','!zws_par_pkal',_wzorzec,_rok);
         exec('kal_wzor_gen','!zws_par_pkal',KAL_ROK.WZORZEC,KAL_ROK.ROK);
         {? KAL_OPIS.f_active() || KAL_OPIS.f_rfresh() ?};
::       konieczna aktualizacja informacji w obiekcie __KAL
         __KAL.ws.dirty()
      ?};
      KAL_WZOR.cntx_pop()
   ?};
   KAL_ROK.cntx_pop()
?}


\kal_opis_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Formuła na "rekord przed"
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _akc:='';
   KAL_WZOR.cntx_psh();
   {? KAL_OPIS.WZORZEC().H_MOD<>'T'
   || _akc+='DU'
   ?};
   {? KAL_OPIS.NUMER<8
   || _akc+='U'
   ?};
   KAL_WZOR.cntx_pop();
   KAL_OPIS.actions_grayed(cur_win(1,1),_akc)
?};
~~


\kal_opis_typ_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Przed redakcją pola TYP tabeli KAL_OPIS
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
KAL_OPIS.NUMER<>0 & KAL_OPIS.NUMER<8


\kal_opis_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
::  MOD: MicKoc [22.26]
:: OPIS: Kontrola poprawności danych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_red:=KAL_OPIS.win_edit('?');
{? KAL_OPIS.TYP='R' & KAL_OPIS.CZAS=time(0,0,0)
|| FUN.emsg('Dla dnia roboczego czas pracy musi być określony.'@);
   'POCZATEK'
|? _red='RED_H'
|| _wyn:=__CHK.record(EDIT_VAR,,'DATA');
   {? _wyn='' || _wyn:=__CHK.record(KAL_OPIS,,'DZIEŃ') ?};
   {? _wyn<>'' || _wyn || 1 ?}
|? exec('isEnabled','prc_przerwy') & (KAL_OPIS.P_OD>*0 | KAL_OPIS.P_CZAS>*0)
|| _ret:='';
:: przerwy niepłatne
   {? KAL_OPIS.P_OD>time(24,0,0)
   || FUN.emsg('Godzina rozpoczęcia przerwy nie może być większa od %1.'@['24:00:00']);
      _ret:='P_OD'
   ?};
   {? KAL_OPIS.P_OD=time(24,0,0)
   || KAL_OPIS.P_OD_ND:='T';
      KAL_OPIS.P_OD:=*0
   ?};
   {? _ret='' & KAL_OPIS.P_CZAS<=*0
   || FUN.info('Należy określić czas trwania przerwy.'@);
      _ret:='P_CZAS'
   ?};
   {? _ret='' & KAL_OPIS.P_CZAS>*60
   || FUN.info('Czas trwania przerwy nie może przekraczać jednej godziny.'@);
      _ret:='P_CZAS'
   ?};
   {? _ret='' & KAL_OPIS.TYP<>'R'
   || FUN.emsg('Przerwa musi przypadać na dzień roboczy.'@);
      _ret:='TYP'
   ?};
   {? _ret=''
   || _prz_od:=KAL_OPIS.P_OD+*((KAL_OPIS.P_OD_ND*'T')*(*time(24,0,0)));
      _zawiera:=exec('zawiera','prc_przerwy',KAL_OPIS.POCZATEK,KAL_OPIS.CZAS+KAL_OPIS.P_CZAS,_prz_od,KAL_OPIS.P_CZAS);
      {? _zawiera=0
      || FUN.emsg('Przerwa musi przypadać na okres czasu pracy (od %1 do %2).'@[$KAL_OPIS.POCZATEK,$KAL_OPIS.KONIEC]);
         _ret:='P_OD'
      ?}
   ?};
:: przerwa zaplanowana na początek czasu pracy lub na koniec (czas pracy bez przerwy - ostrzeżenie)
   {? _ret='' & (KAL_OPIS.P_OD=KAL_OPIS.POCZATEK | KAL_OPIS.P_OD=KAL_OPIS.KONIEC-KAL_DEF.P_CZAS)
   || FUN.emsg('Przerwa przypada na początek czasu pracy, co w rzeczywistości skutkuje czasem pracy bez przerwy.'@)
   ?};
   _ret
|| 1
?}


\kal_opis_zab_czas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed redakcją pól DNI, POCZATEK, KONIEC tabeli KAL_OPIS
::----------------------------------------------------------------------------------------------------------------------
exec('kal_zab_czas','kaledit',KAL_OPIS)


\kal_opis_akt_czas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
::  MOD: MicKoc [22.26]
:: OPIS: Po redakcji pól TYP, DNI, POCZATEK, KONIEC tabeli KAL_OPIS
::----------------------------------------------------------------------------------------------------------------------
{? KAL_OPIS.DNI<0
|| KAL_OPIS.DNI:=0
?};

_ztime:=time(0,0,0);

{? KAL_OPIS.TYP='W' | KAL_OPIS.TYP='S'
|| KAL_OPIS.POCZATEK:=KAL_OPIS.KONIEC:=KAL_OPIS.CZAS:=time(0,0,0);
   KAL_OPIS.DNI:=0

|? KAL_OPIS.TYP='R'
|| {? KAL_OPIS.POCZATEK<=KAL_OPIS.KONIEC
   || KAL_OPIS.CZAS:=KAL_OPIS.KONIEC-KAL_OPIS.POCZATEK;
      KAL_OPIS.CZAS+=*(KAL_OPIS.DNI*60*24)

   || {? KAL_OPIS.DNI=0
      || KAL_OPIS.DNI:=1
      ?};
      KAL_OPIS.CZAS:=*(KAL_OPIS.DNI*60*24);
      KAL_OPIS.CZAS-=KAL_OPIS.POCZATEK-KAL_OPIS.KONIEC
   ?};
   {? exec('isEnabled','prc_przerwy') & (KAL_OPIS.P_OD>*0 | KAL_OPIS.P_CZAS>*0)
   || {? KAL_OPIS.P_CZAS>*0 || KAL_OPIS.CZAS-=KAL_OPIS.P_CZAS ?}
   ?}
?};

1


\kal_nazw_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed zapisem danych do tabeli KAL_NAZW
::----------------------------------------------------------------------------------------------------------------------
{? KAL_NAZW.GRAFIK='T' & KAL_NAZW.OPIS=''
|| KAL_NAZW.OPIS:='grafik indywidualny'
|? KAL_NAZW.OPIS<>KAL_NAZW.NAZWA
|| KAL_NAZW.OPIS:=KAL_NAZW.NAZWA
?};
1


\kal_nazw_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dołącz przed" tabeli KAL_NAZW
::----------------------------------------------------------------------------------------------------------------------
exec('kal_nazw_bw','!zws_par_pkal')


\kal_nazw_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw przed" tabeli KAL_NAZW
::----------------------------------------------------------------------------------------------------------------------
exec('kal_nazw_bw','!zws_par_pkal')


\kal_nazw_ap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Wyzwalacz "popraw po" tabeli KAL_NAZW
::----------------------------------------------------------------------------------------------------------------------
{? TRIG_OFF.KAL_BUFF<>'T' & KAL_NAZW.NORMA<>bfld('NORMA')
|| exec('aktKalNorm4Plan','grafik',KAL_NAZW.ref())
?};
~~


\kal_nazw_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli KAL_NAZW
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',KAL_ROK,'KAL_ROK',KAL_NAZW.ref)


\kal_nazw_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca wskazanie na nazwę kalendarza. Wykorzystywana głównie jako wartość początkowa.
::  OLD: \KALNAZbl/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
KAL_NAZW.ref


\kal_nazw_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdzenie poprawności danych nazwy kalendarza.
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(KAL_NAZW,-menu_txt='popraw',,'NAZWA','NORMA')


\kal_rok_aa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dodaj po" tabeli KAL_ROK
::----------------------------------------------------------------------------------------------------------------------
{? KAL_ROK.NAZWA().CZESC='T'
|| return
?};
{? do_state=1
|| exec('kal_wzor_gen','!zws_par_pkal',KAL_ROK.WZORZEC,KAL_ROK.ROK);
   exec('kal_def_gen','!zws_par_pkal',KAL_ROK.ref)
?};
~~


\kal_rok_ap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw po" tabeli KAL_ROK
::----------------------------------------------------------------------------------------------------------------------
{? KAL_ROK.NAZWA().CZESC='T'
|| return
?};
{? do_state=1 & KAL_ROK.WZORZEC<>bfld('WZORZEC') & KAL_ROK.WZORZEC<>''
|| exec('kal_wzor_gen','!zws_par_pkal',KAL_ROK.WZORZEC,KAL_ROK.ROK);
   exec('del_ndx','#table',KAL_DEF,'KAL_DEF',KAL_ROK.ref);
   exec('kal_def_gen','!zws_par_pkal',KAL_ROK.ref)
?};
~~


\kal_rok_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli KAL_ROK
::----------------------------------------------------------------------------------------------------------------------
:: Czy użyty wzorzec (w bieżącym roku) związany jest tylko z usuwanym kalendarzem?
KAL_ROK.cntx_psh();
_year:=KAL_ROK.ROK;
_ref:=KAL_ROK.ref();
KAL_ROK.index('KAL_WZOR');
KAL_ROK.prefix(KAL_ROK.WZORZEC,KAL_ROK.ROK);
_size:=KAL_ROK.size();

{? _size=1
|| KAL_NAZW.cntx_psh();
   _firma:=KAL_ROK.NAZWA().FIRMA;
   KAL_NAZW.cntx_pop();
   {? _firma=null()
   || KAL_ROK.cntx_pop();
      return(0)
   ?};

   KAL_WZOR.cntx_psh();
   KAL_WZOR.index('KAL_WZOR');
   KAL_WZOR.prefix(_firma,KAL_ROK.WZORZEC,);
   _wzorzec:={? KAL_WZOR.first() || KAL_WZOR.ref() || null() ?};
   KAL_WZOR.cntx_pop();
   {? _wzorzec=null()
   || KAL_ROK.cntx_pop();
      return(0)
   ?};

   _ok:=exec('del_ndx','#table',KAL_OPIS,'KAL_OPIS',_wzorzec,_year)
|| _ok:=1
?};

_result:= _ok & exec('del_ndx','#table',KAL_DEF,'KAL_DEF',_ref);
KAL_ROK.cntx_pop();
_result


\kal_rok_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdzenie poprawności danych roku kalendarza.
::   WE: _a - tryb modyfikacji dołączenie/poprawianie
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(KAL_ROK,_a,,'NAZWA','ROK','WZORZEC');
{? type_of(_chk)=type_of('')
|| return(_chk)
|? _chk=0
|| return(0)
|| {? KAL_ROK.ROK<1900 | KAL_ROK.ROK>9999
   || __CHK.err_fld(KAL_ROK,'ROK',1,'Dozwolone wartości z przedziału %1-%2.'@ ['1900','9999'])
   |? exec('kal_wzor_szukaj','kaledit',KAL_ROK.WZORZEC)=null()
   || __CHK.err_fld(KAL_ROK,'WZORZEC',1,'Brak wzorca o podanej nazwie.'@)
   || 1
   ?}
?}


\kal_wzor_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli KAL_WZOR
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',KAL_OPIS,'KAL_OPIS',KAL_WZOR.ref)


\kal_wzor_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdzenie poprawności danych wzorca kalendarza.
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(KAL_WZOR,-menu_txt='popraw')


\kal_wzor_dodaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy wzorzec o podanej nazwie.
::   WE: _a - nazwa wzorca
::   WY: wynik metody add dla tabeli KAL_WZOR
::----------------------------------------------------------------------------------------------------------------------
KAL_WZOR.index('KAL_WZOR');
KAL_WZOR.prefix(exec('ref_firma','ustawienia'));
KAL_WZOR.blank();
KAL_WZOR.NAZWA:=_a;
KAL_WZOR.add(1)


\kal_opis_jest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdza, czy istnieje definicja wzorca na podany rok.
::   WE: _a - wskazanie na wzorzec
::       _b - rok kalendarzowy
::----------------------------------------------------------------------------------------------------------------------
KAL_OPIS.index('KAL_OPIS');
KAL_OPIS.prefix(_a,_b);
KAL_OPIS.first


\kal_opis_szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyszukuje zapis w definicji wzorca kalendarza
::   WE: _a - wskazanie na wzorzec
::       _b - rok kalendarzowy
::       _c - numer dnia
::----------------------------------------------------------------------------------------------------------------------
KAL_OPIS.index('KAL_OPIS');
KAL_OPIS.prefix(_a,_b);
KAL_OPIS.find_key(_c)


\kal_wzor_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy wzorzec kalendarza.
::   WE: _a - nazwa wzorca
::       _b - rok kalendarzowy
::----------------------------------------------------------------------------------------------------------------------
: znajdź wzorzec o podanej nazwie
_wzorzec:=exec('kal_wzor_szukaj','kaledit',_a);
{? _wzorzec=null & exec('kal_wzor_dodaj','!zws_par_pkal',_a)
|| _wzorzec:=KAL_WZOR.ref
?};
: zakończ jeśli już istnieje definicja wzorca
{? exec('kal_opis_jest','!zws_par_pkal',_wzorzec,_b)
|| return
?};
:: wypełnij pozycje wzorca
exec('kal_opis_fill','kalendarz',_wzorzec,_b);

KAL_OPIS.index('KAL_OPIS');
KAL_OPIS.prefix(_wzorzec);
KAL_OPIS.first();
~~


\wielkanoc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca datę Świąt Wielkiej Nocy w podanym roku
::   WE: _a - rok, dla którego ma być ustalona data święta
::   WY: data Świąt Wielkiej Nocy
::----------------------------------------------------------------------------------------------------------------------
_rok:=_a;
_A:=24;
_B:=5;
_a:=_rok%*19;
_b:=_rok%*4;
_c:=_rok%*7;
_d:=(_a*19+_A)%*30;
_e:=(2*_b+4*_c+6*_d+_B)%*7;
_wynik:=date(_rok,3,22)+_d+_e;
{? _d=29 & _e=6
|| _wynik:=date(_rok,4,26)
|? _d=28 & _e=6 & (11*_A+11)%*30 < 19
|| _wynik:=date(_rok,4,18)
?};
_wynik


\kal_def_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
::  MOD: MicKoc [22.26]
:: OPIS: Tworzy definicję kalendarza.
::   WE: _a - wskazanie na rok kalendarza
::  OLD: \genkal/kaledit.fml
::----------------------------------------------------------------------------------------------------------------------
KAL_DEF.ROK:=_a;
_rok:=KAL_DEF.ROK().ROK;

: zakończ, jeśli dla wskazanego roku brak definicji wzorca
_wzo_naz:=KAL_ROK.WZORZEC;
_wzorzec:=exec('kal_wzor_szukaj','kaledit',_wzo_naz);
{? exec('kal_opis_jest','!zws_par_pkal',_wzorzec,_rok)=null
|| return
?};

KAL_DEF.index('KAL_DEF');
KAL_DEF.prefix(_a);
{? KAL_DEF.first
|| return
?};

:: wyłączenie trigera na potrzeby synchronizacji HR Portal
:: dopiero po dodaniu całego roku będzie zbiorcza aktualizacja pracowników
TRIG_OFF.cntx_psh();
_lic_por:=exec('lic','#b_domain','POR');
{? _lic_por || TRIG_OFF.KAL_DEF:='T' ?};
_przerwy:=exec('isEnabled','prc_przerwy');

KAL_OPIS.cntx_psh();
KAL_OPIS.index('KAL_OPIS');
KAL_OPIS.prefix(_wzorzec,_rok);
_data:=date(_rok,1,1);
_stop:=date(_rok,12,0);
{!
|? _data<=_stop
|! {? ~KAL_OPIS.find_key(_data~4)
   || {? KAL_DEF.first() || {! |? KAL_DEF.del() !} ?};
      KAL_OPIS.cntx_pop();
      TRIG_OFF.cntx_pop();
      return()
   || KAL_DEF.DATA:=_data;
      KAL_DEF.DATAW:=_data+KAL_OPIS.DNI;
      KAL_DEF.TYP:=KAL_OPIS.TYP;
      KAL_DEF.RODZAJ:=KAL_OPIS.RODZAJ;
      KAL_DEF.POCZATEK:=KAL_OPIS.POCZATEK;
      KAL_DEF.KONIEC:=KAL_OPIS.KONIEC;
      KAL_DEF.CZAS:=KAL_OPIS.CZAS;
      KAL_DEF.OPIS:=KAL_OPIS.OPIS;
      KAL_DEF.TYPWS:=exec('getTypWS','kaledit',KAL_DEF.TYP,KAL_DEF.DATA,_wzo_naz);
      {? _przerwy
      || {? KAL_OPIS.P_CZAS>*0
         || KAL_DEF.P_DATA:=KAL_DEF.DATA+(KAL_OPIS.P_OD_ND*'T');
            KAL_DEF.P_START:=KAL_OPIS.P_OD;
            KAL_DEF.P_CZAS:=KAL_OPIS.P_CZAS
         || KAL_DEF.P_DATA:=#0;
            KAL_DEF.P_START:=*0;
            KAL_DEF.P_CZAS:=*0
         ?}
      ?};
      KAL_DEF.add();
      _data+=1
   ?}
!};
{? KAL_OPIS.find_key(8)
|| {!
   |? _data:=KAL_OPIS.DATA;
      _data:=date(#(4+_data),#(2+(_data+5)),#(_data+2));
      {? KAL_DEF.find_key(_data)
      || KAL_DEF.DATA:=_data;
         KAL_DEF.DATAW:=_data+KAL_OPIS.DNI;
         KAL_DEF.TYP:=KAL_OPIS.TYP;
         KAL_DEF.RODZAJ:=KAL_OPIS.RODZAJ;
         KAL_DEF.POCZATEK:=KAL_OPIS.POCZATEK;
         KAL_DEF.KONIEC:=KAL_OPIS.KONIEC;
         KAL_DEF.CZAS:=KAL_OPIS.CZAS;
         KAL_DEF.OPIS:=KAL_OPIS.OPIS;
         KAL_DEF.TYPWS:=exec('getTypWS','kaledit',KAL_DEF.TYP,KAL_DEF.DATA,_wzo_naz);
         {? _przerwy
         || KAL_DEF.P_DATA:=#0;
            KAL_DEF.P_START:=*0;
            KAL_DEF.P_CZAS:=*0
         ?};
         KAL_DEF.put()
      ?};
      KAL_OPIS.next()
   !}
?};
KAL_OPIS.cntx_pop();
{? _lic_por || exec('update4kal_nazw','pkalsync',KAL_ROK.NAZWA,date(_rok,1,1),date(_rok,12,0),1) ?};
TRIG_OFF.cntx_pop();
exec('akt_rok','kaledit',KAL_ROK.NAZWA,KAL_ROK.ROK)


\setup_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Konfiguruje panel administracji kalendarzami.
::   WE: _a - wskazanie na obiekt klasy KAL_ADM
::       _b - nazwa zmiennej globalnej wskazującej na obiekt
::----------------------------------------------------------------------------------------------------------------------
: ustaw właściwości nawigatora wg lat
_years:=_a.YEARS;
_years.EDIT_WND:='RED_ROK';
_years.DISP_STD:='WID_ROK';
_years.DISP_EXT:='WID_R_M';
_YEARS:=_years.TAB;

: ustaw właściwości nawigatora wg nazw
_names:=_a.NAMES;
_names.EDIT_WND:='RED_NAZW';
_names.DISP_STD:='WID_NAZW';
_names.DISP_EXT:='WID_N_MS';
_NAMES:=_names.TAB;

{? exec('isEnabled','prc_przerwy')
|| _win_sel_DEF:='ADM_P';
   _win_sel_OPIS:='ADM_P'
|| _win_sel_DEF:='ADM';
   _win_sel_OPIS:='ADM'
?};

: okno konsoli zarządzania kalendarzami
_wnd:=_YEARS.grp_make('Kalendarze',
:  po wyswietleniu
   "  {? exec('isEnabled','prc_przerwy')
      || KAL_DEF.win_edit('RED_P')
      || KAL_DEF.win_edit('RED')
      ?};
      KAL_DEF.win_patt('WZO');
      KAL_NAZW.win_dict('ADM');
      KAL_WZOR.win_sel('WYB');
      KAL_DEF.win_patt('WZO');
      1
   ",
:  identyfikator
   'kal_adm_kon',
:  polozenie
   ,,
:  zamkniecie
   "  KAL_NAZW.f_clear();
      KAL_ROK.f_clear();
      KAL_DEF.f_clear();
      KAL_WZOR.f_clear();
      KAL_OPIS.f_clear();
      1
   "
);

: widok wg lat
_YEARS.grp_sel(_wnd,_YEARS,_years.SEL_WND,'wg Lat',
:  po odswiezeniu
   $('exec(\'kal_naw_ar\',\'!zws_par_pkal\','+_b+',\'YEARS\',\''+_win_sel_DEF+'\',\''+_win_sel_OPIS+'\')'),
:  polozenie i wysokosc
   ,,,
:  przed obsluga
   $(_b+'.set_view(\'YEARS\')'),
:  po obsludze
   "",
:  utrwalenie, aktywacja, wypelnienie
   ,,'maximized'
);
: widok wg nazw
_YEARS.grp_sel(_wnd,_NAMES,_names.SEL_WND,'wg Nazw',
:  po odswiezeniu
   $('exec(\'kal_naw_ar\',\'!zws_par_pkal\','+_b+',\'NAMES\',\''+_win_sel_DEF+'\',\''+_win_sel_OPIS+'\')'),
:  polozenie i wysokosc
   ,,,
:  przed obsluga
   $(_b+'.set_view(\'NAMES\')'),
:  po obsludze
   "",
:  utrwalenie, aktywacja, wypelnienie
   ,,'maximized'
);
: podzial pionowy: definicja, wzorzec, ...
_YEARS.grp_splt(_wnd,,'vertical','right',',25%');
: definicja kalendarza na wybrany rok
_YEARS.grp_sel(_wnd,KAL_DEF,_win_sel_DEF,'Kalendarz',
:  po odswiezeniu
   ,
:  polozenie i wysokosc
   ,,,
:  przed obsluga
   $('exec(\'kal_def_adm_bs\',\'!zws_par_pkal\',_a,'+_b+')'),
:  po obsludze
   "",
:  utrwalenie, aktywacja, wypelnienie
   ,,'maximized'
);
_YEARS.grp_sel(_wnd,KAL_OPIS,_win_sel_OPIS,'Wzorzec',
:  po odswiezeniu
   "",
:  polozenie i wysokosc
   ,,,
:  przed obsluga
   "",
:  po obsludze
   "",
:  utrwalenie, aktywacja, wypelnienie
   ,,'maximized'
);

_a.PANEL:=_wnd;
_YEARS.win_sel(_wnd);

: ----------------------------------------------------------------------------------------------------------------------
: ustawienia obslugi

set_virt(_names,'blank',"
   _nazwa:=null;
   _wzorzec:='';
   _rok:=date~1;
   _czas:=time(0,0,0);
   {? .TAB.TYPE=!KAL_NAZW
   || KAL_NAZW.cntx_psh;
      KAL_NAZW.index('KAL_NAZW');
      KAL_NAZW.prefix(exec('ref_firma','ustawienia'));
      {? KAL_NAZW.seek(.TAB.DATA,)
      || _nazwa:=KAL_NAZW.ref
      ?};
      KAL_NAZW.cntx_pop
   |? .seek()
   || _nazwa:=KAL_NAZW.ref
   ?};
   KAL_ROK.cntx_psh;
   KAL_ROK.index('KAL_ROK');
   KAL_ROK.prefix(_nazwa);
   {? KAL_ROK.last
   || _nazwa:=KAL_ROK.NAZWA;
      _wzorzec:=KAL_ROK.WZORZEC;
      _rok:=KAL_ROK.ROK+1;
      _czas:=KAL_ROK.MAXTIME
   ?};
   KAL_ROK.cntx_pop;
   KAL_ROK.blank;
   KAL_ROK.NAZWA:=_nazwa;
   KAL_ROK.WZORZEC:=_wzorzec;
   KAL_ROK.ROK:=_rok;
   KAL_ROK.MAXTIME:=_czas
");

set_virt(_names,'add_year',"
   .TAB.cntx_psh;
   .TAB.clear;
   .TAB.index(.KEY.DATA);
   {? ~.TAB.find_key(!KAL_ROK,KAL_ROK.ref)
   || {? ~.TAB.find_key(!KAL_NAZW,KAL_ROK.NAZWA)
      || .TAB.blank(1);
         .TAB.NAME:=KAL_ROK.NAZWA().NAZWA;
         .TAB.TYPE:=!KAL_NAZW;
         .TAB.DATA:=KAL_NAZW.ref;
         .TAB.add
      ?};
      .TAB.blank(1);
      .TAB.TREE:=.TAB.ref;
      .TAB.NAME:=form(KAL_ROK.ROK,,,'99');
      .TAB.TYPE:=!KAL_ROK;
      .TAB.DATA:=KAL_ROK.ref;
      .TAB.YEAR:=KAL_ROK.ROK;
      .TAB.ONUM:=-.TAB.YEAR;
      .TAB.add
   ?};
   .TAB.cntx_pop
");

set_virt(_years,'blank',"
   KAL_ROK.blank;
   KAL_ROK.NAZWA:=null;
   {? .TAB.TYPE=.ST_YEAR
   || KAL_ROK.ROK:=.TAB.DATA
   || KAL_ROK.ROK:=.year_num()
   ?}
");

set_virt(_years,'add_year',"
   .TAB.cntx_psh;
   .TAB.clear;
   .TAB.index(.KEY.DATA);
   {? ~.TAB.find_key(!KAL_ROK,KAL_ROK.ref)
   || {? ~.TAB.find_key(.ST_YEAR,KAL_ROK.ROK)
      || .TAB.blank(1);
         .TAB.NAME:=form(KAL_ROK.ROK,,,'99');
         .TAB.TYPE:=.ST_YEAR;
         .TAB.DATA:=KAL_ROK.ROK;
         .TAB.YEAR:=KAL_ROK.ROK;
         .TAB.ONUM:=-.TAB.YEAR;
         .TAB.add
      ?};
      .TAB.blank(1);
      .TAB.TREE:=.TAB.ref;
      .TAB.NAME:=KAL_ROK.NAZWA().NAZWA;
      .TAB.TYPE:=!KAL_ROK;
      .TAB.DATA:=KAL_ROK.ref;
      .TAB.YEAR:=KAL_ROK.ROK;
      .TAB.add
   ?};
   .TAB.cntx_pop
");

~~


\setup_nav
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Konfiguruje nawigatora.
::   WE: _a - wskazanie na obiekt klasy KAL_WID
::       _b - nazwa zmiennej globalnej wskazującej na menadżera
::       _c [STRING] - identyfikator tymczasowego okienka selekcji
::----------------------------------------------------------------------------------------------------------------------
_id_wnd:={? var_pres('_c')<>type_of('') | _c='' || '#setup_nav' || _c ?};
_TAB:=_a.TAB;
: utwórz okienko nawigatora
_wnd:=_TAB.mk_sel(,'P',0,_id_wnd,,,,1,'N','N',,);
_TAB.win_fld(_wnd,,'NAME',,,30,,,,1);
_TAB.win_act(_wnd,1,'Formuła','Dołącz'@@,,,$(_b+'.insert()'),,1,,,,'D');
_TAB.win_act(_wnd,0,'Formuła','Dołącz'@@,,,$(_b+'.insert()'),,1,,,,'D');
_TAB.win_act(_wnd,0,'Formuła','Popraw'@@,,,$(_b+'.update()'),,,,,,'P');
_TAB.win_act(_wnd,0,'Formuła','Usuń'@@,,,$(_b+'.delete()'),,,,,,'U');
_TAB.win_act(_wnd,0,'Formuła','Generuj'@@,,,$(_b+'.create()'),,,,,,'G');
_menu:='Edytuj'@;
_TAB.win_act(_wnd,0,'Menu',_menu);
_TAB.win_act(_wnd,0,'Formuła','Dni na podstawie wzorca'@@,_menu,,$(_b+'.grp_edit(1,1)'),,,,,,'D');
_TAB.win_act(_wnd,0,'Formuła','Tygodnie na podstawie wzorca'@@,_menu,,$(_b+'.grp_edit(1,2)'),,,,,,'T');
_TAB.win_act(_wnd,0,'Formuła','Miesiące na podstawie wzorca'@@,_menu,,$(_b+'.grp_edit(1,3)'),,,,,,'M');
_TAB.win_act(_wnd,0,'Formuła','--X',_menu);
_TAB.win_act(_wnd,0,'Formuła','Przypisz wartości dla dni'@@,_menu,,$(_b+'.grp_edit(2,1)'),,,,,,'P');
_TAB.win_act(_wnd,0,'Formuła','P&rzypisz wartości dla tygodni'@@,_menu,,$(_b+'.grp_edit(2,2)'),,,,,,'R');
_TAB.win_act(_wnd,0,'Formuła','Pr&zypisz wartości dla miesięcy'@@,_menu,,$(_b+'.grp_edit(2,3)'),,,,,,'Z');
_TAB.win_act(_wnd,0,'Formuła','Użytkownic&y'@@,,,$('exec(\'kal_prac_akc\',\'!zws_par_pkal\','+_b+')'),,,,,,'Y');
_txt:='Aktualizacja danych po zmianie definicji kalendarza.'@;
_TAB.win_act(_wnd,0,'Formuła','Aktualizuj'@@,,_txt,$('exec(\'aktualizacja\',\'kal_dane_akt\','+_b+')'),,,,,,'A');
_TAB.win_act(_wnd,0,'Szukaj');
_TAB.win_act(_wnd,0,'Formuła','Legenda'@@,,,$('exec(\'legenda\',\'color\',\'#KAL_ROK#01\')'),,,,,,'L',,'target=window');
_TAB.win_act(_wnd,0,'Wyświetl',,,,$(_b+'.display()'));

_btn:=_TAB.win_btn(_wnd,'text=%1' ['&Dołącz'@],'menu:D',);
_TAB.btn_opt(_btn,'tooltip=%1' ['Dołączenie nowego zapisu'@]);
_btn:=_TAB.win_btn(_wnd,'text=%1' ['&Popraw'@],'menu:P',,,,,,'noempty');
_TAB.btn_opt(_btn,'tooltip=%1' ['Poprawienie bieżącego zapisu'@]);
_btn:=_TAB.win_btn(_wnd,'text=%1' ['&Usuń'@],'menu:U',,,,,,'noempty');
_TAB.btn_opt(_btn,'tooltip=%1' ['Usunięcie bieżącego zapisu'@]);

_a.SEL_WND:=_wnd;

_a.TAB.win_fml(_a.SEL_WND,,'NAME',,'ICON_BEFORE',$('
   _obj:='+_b+'.get_view();
   _TAB:=_obj.TAB;
   \'xwin16.png:\'+
      {? _TAB.MARK=\'N\'
      || {? _TAB.TYPE<>!KAL_ROK
         || \'2\'
         || \'81\'
         ?}
      |? _TAB.TYPE=\'_MIESIAC\'
      || \'190\'
      |? _TAB.TYPE=!KAL_ROK
      || \'38\'
      || \'83\'
      ?}
'))


\declare_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Deklaruje klasę KAL_ADM odpowiedzialną za obsługę konsoli edycji kalendarzy.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('KAL_ADM',@.CLASS)>0
|| return
?};

: deklaracja klasy nawigatora
exec('declare_nav','!zws_par_pkal');

: klasa konsoli
obj_decl('KAL_ADM',

   obj_fld('INITR',~~),

   obj_fld('CVIEW',~~),
   obj_fld('NAMES',~~),
   obj_fld('YEARS',~~),
   obj_fld('PANEL',''),

   obj_meth('__init',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy obiekt konsoli.
::----------------------------------------------------------------------------------------------------------------------
   "  .NAMES:=obj_new(@.CLASS.KAL_NAW);
      .YEARS:=obj_new(@.CLASS.KAL_NAW);
      ~~
   "),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyświetla konsolę zarządzania kalendarzami.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('show',"
      .INITR:={? var_pres('_a')=type_of(0) & _a || _a || date~1 ?};
      .YEARS.TAB.select(,1,-1)
   "),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca obiekt aktywnego nawigatora.
::   WY: Wskazanie na obiekt klasy KAL_NAW właściwy dla aktywnego nawigatora.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('get_view',"($('_a.'+.CVIEW))(.)"),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustawia aktywny nawigator i odświeża zawartość konsoli.
::   WE: _a - nazwa pola przechowującego wskazanie na nawigatora ('NAMES'/'YEARS')
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('set_view',".CVIEW:=_a;.reload()",type_of('')),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Synchronizuje dane aktywnego nawigatora z zawartością tabel KAL_NAZW i KAL_ROK.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('reload',"
      _CVIEW:=.get_view();
      _CVIEW.reload();
      {? type_of(.INITR)=type_of(0)
      || _CVIEW.TAB.find_key(0,-.INITR);
         .INITR:=~~
      ?}
   "),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Nakłada filtry na tabele KAL_DEF i KAL_OPIS zgodnie z wyborem w nawigatorze.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('filter',".get_view().filter()"),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Dodaje nodę do aktywnego nawigatora - dołącz.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('insert',".get_view().insert()"),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Modyfikuje nodę w aktywnym nawigatorze - popraw.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('update',".get_view().update()"),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Usuwa nodę w aktywnym nawigatorze - usuń.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('delete',".get_view().delete()"),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Generuje kalendarz w aktywnym nawigatorze - generuj.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('create',".get_view().create()"),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyswietla informacje o nodzie w aktywnym nawigatorze - wyświetl.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('display',".get_view().display()"),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Wywołuje funkcje grupowej edycji.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('grp_edit',".get_view().grp_edit(_a,_b)",type_of(0),type_of(0)),

::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zlicza łączny czas pracy w wybranym okresie.
::----------------------------------------------------------------------------------------------------------------------
   obj_meth('sum_time',"exec('sum_time','kaledit')")
)


\declare_nav
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Deklaruje klase KAL_WID pomocnicza dla klasy KAL_ADM
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('KAL_NAW',@.CLASS)>0
|| return
?};

: klasa nawigatora
obj_decl('KAL_NAW',

   obj_fld('TAB', ~~),
   obj_fld('KEY', ~~),
   obj_fld('SEL_WND', ~~),
   obj_fld('EDIT_WND', ~~),
   obj_fld('DISP_STD', ~~),
   obj_fld('DISP_EXT', ~~),

:  stałe
   obj_fld('ST_YEAR','_ROK'),
   obj_fld('ST_MONTH','_MIESIAC'),

   obj_meth('__init',"
      .TAB:=tab_tmp(3,
         'TREE','TREE_REF','Nadrzędny',
         'ONUM','INTEGER','Porządek',
         'NAME','STRING[20]','Opis',
         'TYPE','STRING[10]','Typ',
         'DATA','INTEGER','Dane',
         'YEAR','INTEGER','Rok',
         'MARK','STRING[1]','Status'
      );
      .TAB.fld_attr(,2);
      .KEY:=obj_new('TREE','DATA','MARK','REVN','NAME');
      .KEY.TREE:=.TAB.index('?');
      .KEY.DATA:=.TAB.ndx_tmp(,,'TYPE',,,'DATA',,);
      .KEY.MARK:=.TAB.ndx_tmp(,,'TREE',,,'MARK',,);
      .KEY.REVN:=.TAB.ndx_tmp(,,'TYPE',,,'NAME',,1);
      .KEY.NAME:=.TAB.ndx_tmp(,,'NAME',,);
      ~~
   "),

   obj_meth('norm_val',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Normalizacja wartości zwracanej z rzutowaniem na wartość pustą
::----------------------------------------------------------------------------------------------------------------------
      "{? type_of(_a)=type_of(_b) || _a || _b ?}"
   ),

   obj_meth('year_get',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Odszukuje w tabeli KAL_ROK rekord właściwy dla wybranej nody nawigatora i zwraca wartość zgodną z bieżącą
::       definicja metody year_get
::----------------------------------------------------------------------------------------------------------------------
   "  _val:=~~;
      {? .TAB.TYPE=!KAL_ROK
      || KAL_ROK.cntx_psh;
         KAL_ROK.prefix;
         {? KAL_ROK.seek(.TAB.DATA,)
         || _val:=.year_val()
         ?};
         KAL_ROK.cntx_pop
      |? .TAB.TREE
      || .TAB.cntx_psh;
         {? .TAB.seek(.TAB.TREE,)
         || _val:=.year_get()
         ?};
         .TAB.cntx_pop
      ?};
      _val
   ",-1),

   obj_meth('year_ref',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca wskazanie na rekord w tabeli KAL_ROK właściwy dla wybranej nody drzewka
::----------------------------------------------------------------------------------------------------------------------
   "  set_virt(.,'year_val',$'KAL_ROK.ref');
      .norm_val(.year_get(),null)
   ",-1),

   obj_meth('year_num',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca rok właściwy dla wybranej nody drzewka
::----------------------------------------------------------------------------------------------------------------------
   "  set_virt(.,'year_val',$'KAL_ROK.ROK');
      .norm_val(.year_get(),0)
   ",-1),

   obj_virt('year_val',"~~"),

   obj_meth('get_dbeg',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca datę początku wybranego okresu (roku lub miesiąca)
::----------------------------------------------------------------------------------------------------------------------
   "  _date:=date(0,0,0);
      _year:=.year_num();
      {? _year
      || _month:=0;
         {? .TAB.TYPE=.ST_MONTH
         || _month:=.TAB.DATA
         |? .TAB.TYPE=!KAL_ROK
         || _month:=1
         ?};
         {? _month
         || _date:=date(_year,_month,1)
         ?}
      ?};
      _date
   ",-1),

   obj_meth('get_dend',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca datę końca wybranego okresu (roku lub miesiąca)
::----------------------------------------------------------------------------------------------------------------------
   "  _date:=date(0,0,0);
      _year:=.year_num();
      {? _year
      || _month:=0;
         {? .TAB.TYPE=.ST_MONTH
         || _month:=.TAB.DATA
         |? .TAB.TYPE=!KAL_ROK
         || _month:=12
         ?};
         {? _month
         || _date:=date(_year,_month,0)
         ?}
      ?};
      _date
   ",-1),

   obj_meth('reload',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Synchronizuje zawartość drzewka widoku z zawartością tabel przechowujących informację o kalendarzach brane są
::       pod uwagę jedynie kalendarze "rzeczywiste".
::----------------------------------------------------------------------------------------------------------------------
   "  .remove();
      KAL_NAZW.cntx_psh;
      KAL_ROK.cntx_psh;
      KAL_ROK.index('KAL_CZES');
      KAL_ROK.prefix(exec('ref_firma','ustawienia'),'N');
      .TAB.cntx_psh;
      _loop:=KAL_ROK.first;
      {!
      |? _loop
      |! .add_year();
         _loop:=KAL_ROK.next
      !};
      .TAB.cntx_pop;
      KAL_ROK.cntx_pop;
      KAL_NAZW.cntx_pop;
      .validate()
   "),

   obj_meth('remove',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Usuwa z widoku dane, które mogły być w międzyczasie usunięte z tabel źrodłowych (lata, kalendarze).
::----------------------------------------------------------------------------------------------------------------------
   "  KAL_NAZW.cntx_psh;
      KAL_ROK.cntx_psh;
      .TAB.cntx_psh;
      .TAB.clear;
      KAL_ROK.clear;
      KAL_NAZW.clear;
      {? .TAB.first
      || {!
         |? {? .TAB.TYPE=!KAL_ROK | .TAB.TYPE=!KAL_NAZW
            || {? ($(.TAB.TYPE+'.seek(_a,)'))(.TAB.DATA)
               || .TAB.next
               || .cascade(1)
               ?}
            |? .TAB.TYPE=.ST_YEAR
            || .TAB.cntx_psh;
               .TAB.index(.KEY.TREE);
               .TAB.prefix(#.TAB.ref);
               _size:=.TAB.size;
               .TAB.cntx_pop;
               {? _size
               || .TAB.next
               || .TAB.del
               ?}
            || .TAB.next
            ?}
         !}
      ?};
      .TAB.cntx_pop;
      KAL_ROK.cntx_pop;
      KAL_NAZW.cntx_pop;
      ~~
   "),

   obj_virt('add_year',""),

   obj_meth('validate',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Weryfikuje nody drzewka. Sprawdza istnienie kalendarzy.
::----------------------------------------------------------------------------------------------------------------------
   "  KAL_DEF.cntx_psh;
      KAL_DEF.index('KAL_DEF');
      .TAB.cntx_psh;
      .TAB.index(.KEY.DATA);
      .TAB.prefix(!KAL_ROK);
      _loop:=.TAB.first;
      {!
      |? _loop
      |! KAL_DEF.prefix(.TAB.DATA);
         {? KAL_DEF.size
         || .add_months(.TAB.ref,.TAB.YEAR);
            .TAB.MARK:='T'
         || .cascade(0);
            .TAB.MARK:='N'
         ?};
         .TAB.put;
         _loop:=.TAB.next
      !};
      .TAB.index(.KEY.TREE);
      _loop:=.TAB.first;
      {!
      |? _loop
      |! {? .TAB.TYPE<>.ST_MONTH & .TAB.TYPE<>!KAL_ROK
         || .TAB.cntx_psh;
            .TAB.index(.KEY.MARK);
            .TAB.prefix(#.TAB.ref,'N');
            _mark:={? .TAB.size || 'N' || 'T' ?};
            .TAB.cntx_pop;
            {? .TAB.MARK<>_mark
            || .TAB.MARK:=_mark;
               .TAB.put
            ?}
         ?};
         _loop:=.TAB.next
      !};
      .TAB.cntx_pop;
      KAL_DEF.cntx_pop
   "),

   obj_meth('cascade',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wykorzystywana podczas pielęgnacji widoku jeśli zachodzi konieczność usuwania gałęzi. Usuwa gałęzie/liście
::       powiązane z bieżącym zapisem. Zależnie od argumentu _a bieżący zapis jest usuwany lub pozostawiany.
::----------------------------------------------------------------------------------------------------------------------
   "  .TAB.cntx_psh;
      .TAB.index(.KEY.TREE);
      .TAB.prefix(#.TAB.ref);
      {!
      |? .TAB.first
      |! .cascade(1)
      !};
      .TAB.cntx_pop;
      {? _a
      || .TAB.del
      ?}
   ",type_of(0)),

   obj_meth('add_months',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Do wskazanej przez _a nody dopisuje miesiące roku podanego w _b dodawane są kolejno miesiące od stycznia do
::       grudnia, jeśli zostanie znaleziony jakikolwiek zapis dla podanych argumentow, to metoda kończy działanie (ze
::       wzgledu na szybkość działania metody reload)
::----------------------------------------------------------------------------------------------------------------------
   "  .TAB.cntx_psh;
      .TAB.clear;
      {! _n:=1..12
      |! _name:=date(_b,_n,1)$8;
         _name:=(_name*' '-1)+_name;
         .TAB.blank(1);
         .TAB.TREE:=_a;
         .TAB.NAME:=_name;
         .TAB.ONUM:=_n;
         .TAB.TYPE:=.ST_MONTH;
         .TAB.DATA:=_n;
         .TAB.MARK:='T';
         {? .TAB.find_rec
         || .TAB.cntx_pop;
            return
         || .TAB.add
         ?}
      !};
      .TAB.cntx_pop
   ",type_of(null),type_of(0)),

   obj_meth('filter',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Nakłada ograniczenie na zakres prezentowanych danych w okienkach definicji kalendarza i wzorca. Uwzględniany
::       jest rodzaj informacji wybranej w nawigatorze, o ile istnieją dane, to zostaną one zaprezentowane z
::       uwzględnieniem ograniczenia czasowego wynikającego ze wskazywanej w nawigatorze informacji: do roku dla lat
::       kalendarza, do miesiaca dla miesiąca roku kalendarza, dla pozostałych przypadków (rok kalendarza w pierwszej
::       zakładce, nazwa w grugiej zakładce) filtr jest nanoszony, ale argumenty przekazywane do zapytania (wskazanie
::       na rok kalendarzowy null, zerowe daty początku i końca okresu) powodują, że w oknach definicji kalendarza
::       i wzorca nie widać żadnych danych
::----------------------------------------------------------------------------------------------------------------------
   "  _kal_ref:=null;
      _kal_rok:=0;
      _wzorzec:='';
      _dbeg:=date(0,0,0);
      _dend:=date(0,0,0);

      {? .seek()
      || _kal_ref:=KAL_ROK.ref;
         _kal_rok:=KAL_ROK.ROK;
         _wzorzec:=KAL_ROK.WZORZEC;
         _dbeg:=.get_dbeg();
         _dend:=.get_dend()
      ?};

      {? KAL_DEF.cur_prfx<>''
      || KAL_DEF.clear
      ?};
      KAL_DEF.f_set(
         'DATA',,
         'ROK=:_a and DATA between to_date(:_b) and to_date(:_c)',
         _kal_ref,_dbeg,_dend
      );
      {? ~KAL_DEF.f_test
      || KAL_DEF.f_first
      ?};

      {? KAL_OPIS.cur_prfx<>''
      || KAL_OPIS.clear
      ?};
      KAL_OPIS.f_set(
         'NUMER',
         'join KAL_WZOR using (KAL_OPIS.WZORZEC,KAL_WZOR.REFERENCE)',
         'KAL_WZOR.FIRMA=:_a and KAL_WZOR.NAZWA=\\\':_b\\\' and KAL_OPIS.ROK=:_c',
         exec('ref_firma','ustawienia'),_wzorzec,_kal_rok
      )
   "),

   obj_meth('seek',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustala pozycje w tabelach kalendarzy i lat kalendarzy zwraca wskazanie na rok kalendarza lub null jeśli
::       ustalenie pozycji nie jest możliwe
::----------------------------------------------------------------------------------------------------------------------
   "  KAL_ROK.clear;
      {? KAL_ROK.seek(.year_ref())
      || KAL_ROK.NAZWA();
         KAL_ROK.ref
      || null
      ?}
   "),

   obj_virt('blank',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Odpowiada za ustalenie początkowych wartości podczas dodawania informacji o roku kalendarza zależnie od
::       kontekstu wywołania.
::----------------------------------------------------------------------------------------------------------------------
      ""
   ),

   obj_meth('edit',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Edycja informacji o roku kalendarza. Udostępnia do edycji bufor tabeli w oknie własciwym wg kontekstu wywołania
::       sprawdza poprawność edytowanych danych: wypełnienie wymaganych pól, unikalność zapisu.
::----------------------------------------------------------------------------------------------------------------------
   "  KAL_WZOR.actions('WYB','R');
      _wyb:=1;
      {? _b='POPRAW' || _wyb:=FUN.choice('Zakres modyfikacji?',1,'Wzorzec','Norma') ?};
      {? _wyb=1
      || KAL_ROK.win_edit(_b);
         KAL_ROK.edit($(\"exec('kal_rok_spr','!zws_par_pkal',\"+$_a+\")\"))
      |? _wyb=2
      || KAL_NAZW.win_edit('RED');
         _prevN:=KAL_NAZW.fld_fml('NAZWA','BEFORE_EDIT',\"0\");
         _prevG:=KAL_NAZW.fld_fml('GRAFIK','BEFORE_EDIT',\"0\");
         _edit:=KAL_NAZW.edit();
         {? _edit & exec('kal_nazw_spr','!zws_par_pkal') || KAL_NAZW.put() ?};
         KAL_NAZW.fld_fml('NAZWA','BEFORE_EDIT',_prevN);
         KAL_NAZW.fld_fml('GRAFIK','BEFORE_EDIT',_prevG);
         _edit
      ?}
   ",type_of(0),type_of('')),

   obj_meth('insert',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Dołączenie roku kalendarzowego. Akcja wywołująca metode zawsze dostepną, zależnie od ustawien dla widoku
::       wykorzystywane sa różne okna redakcji (zmieniona jest kolejność pol nazwa <-> rok tak aby odpowiadała poziomom
::       grupowania informacji w oknie nawigatora) i zakres podpowiadanych informacji (przedefiniowanie metody blank
::       zależnie od kontekstu).
::       Po dołączeniu roku kalendarza tworzony jest dla niego wzorzec (jeśli nie istnieje) i definicja wzorca na rok
::       (jesli nie istnieje).
::       Na podstawie wzorca generowany jest kalendarz.
::----------------------------------------------------------------------------------------------------------------------
   "  _rok:=null;
      .blank();
      {? .edit(0,.EDIT_WND)
      || {? KAL_ROK.add
         || _rok:=KAL_ROK.ref
         ?}
      ?};
      .reload();
      {? _rok<>null
      || _new:=.TAB.ref;
         .TAB.cntx_psh;
         .TAB.index(.KEY.DATA);
         .TAB.prefix(!KAL_ROK);
         {? .TAB.find_key(#_rok)
         || _new:=.TAB.ref
         ?};
         .TAB.cntx_pop;
         .TAB.seek(_new)
      ?}
   "),

   obj_meth('update',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Edycja wybranej w nawigatorze informacji. Przypadki ograniczone wyłącznie do gałęzi reprezentującej rok
::       kalendarzowy - dla wszystkich pozostałych akcja wywołująca metodę jest ukrywana (albo "zbyt gleboko" - miesiac,
::       albo zbyt "płytko" - rok w pierwszej zakladce lub nazwa kalendarza w drugiej)
::----------------------------------------------------------------------------------------------------------------------
   "  .seek();
      {? .edit(1,'POPRAW')
      || KAL_ROK.put
      ?};
      .reload()
   "),

   obj_meth('delete',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Usunięcie wybranej w nawigatorze informacji. Zależnie od tego co reprezentuje wybrany zapis usuwany będzie rok
::       kalendarzowy lub cały kalendarz. Pozostałe przypadki są pomijane ponieważ akcja i tak zostanie dla nich ukryta
::       jako "bezsensowna" (rok kalendarzowy usuwany jest w calosci a nie tylko jego wybrane miesiące, defacto
::       nieistniejace)
::----------------------------------------------------------------------------------------------------------------------
   "  {? .TAB.TYPE=!KAL_NAZW & exec('del_conf','#table')
      || _ok:=1;
         KAL_ROK.cntx_psh();
         KAL_ROK.index('KAL_ROK');
         KAL_ROK.prefix(KAL_ROK.ref());
         _loop:=KAL_ROK.first();
         {!
         |? _loop & _ok
         |! {? ~exec('kal_rok_usun_b','kaledit',KAL_ROK.ref())
            || _ok:=0
            ?};
            _loop:=KAL_ROK.next()
         !};
         KAL_ROK.cntx_pop();
         {? _ok=1
         || KAL_NAZW.del()
         ?}
      |? .TAB.TYPE=!KAL_ROK & exec('del_conf','#table')
      || {? exec('kal_rok_usun_b','kaledit',KAL_ROK.ref())
         || KAL_ROK.del()
         ?}
      ?};
      .reload()
   "),

   obj_meth('create',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ponowny zapis definicji kalendarza. Jeśli dla roku kalendarza istnieje już definicja, to wymagane potwierdzenie
::       uzytkownika.
::----------------------------------------------------------------------------------------------------------------------
   "  KAL_DEF.f_clear;
      KAL_DEF.index('KAL_DEF');
      KAL_DEF.prefix(.TAB.DATA);
      {? {? KAL_DEF.first || FUN.ask('Czy na pewno nadpisać zdefiniowany kalendarz?'@) || 1 ?}
      || exec('del_ndx','#table',KAL_DEF,'KAL_DEF',KAL_ROK.ref);
         exec('kal_wzor_gen','!zws_par_pkal',KAL_ROK.WZORZEC,KAL_ROK.ROK);
         exec('kal_def_gen','!zws_par_pkal',KAL_ROK.ref);
         FUN.info('Zakończono zapis kalendarza.'@)
      ?};
      KAL_DEF.clear;
      .reload()
   "),

   obj_meth('display',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyświetla informacje o kalendarzu. Zależnie od kontekstu wykorzystywane są okna z zakresem i kolejnością pól
::       dostosowaną do budowy i poziomu drzewa nawigatora: zamiana kolejności rok <-> nazwa, informacja o miesiącu.
::----------------------------------------------------------------------------------------------------------------------
   "  {? ~.seek()
      || return
      ?};
      _wnd:=.DISP_STD;
      {? .TAB.TYPE=.ST_MONTH
      || POLA_GRP.TXT_1:=.TAB.NAME;
         _wnd:=.DISP_EXT
      ?};
      KAL_ROK.win_edit(_wnd);
      {? .TAB.TYPE=!KAL_ROK | .TAB.TYPE=.ST_MONTH
      || KAL_ROK.display
      ?}
   "),

   obj_meth('grp_edit',
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Wywołuje funkcje grupowej edycji kalendarza.
::   WE: _a INTEGER - numer grupy:
::          1 - nadpisanie na podstawie wzorca
::          2 - przypisanie podanych wartości
::       _b INTEGER - oznaczenie okresu:
::          1 - dzień
::          2 - tydzień
::          3 - miesiąc
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
   "  _val:=exec('grp_edit','!zws_par_pkal',.seek(),_a,_b);
      {? type_of(_val)=type_of('')
      || FUN.emsg(_val)
      ?};
      ~~
   ",type_of(0),type_of(0))
)


\kal_naw_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odswiezeniu okienka nawigatora kalendarzy.
::   WE: _a - wskazanie na menadżera
::       _c - akronim okna KAL_DEF
::       _d - akronim okna KAL_OPIS
::----------------------------------------------------------------------------------------------------------------------
_view:=_a.get_view();
_view.reload();
_view.seek();

_type:=_view.TAB.TYPE;

: zmodyfikuj menu nawigatora zależnie od wybranej informacji
: - dla roku kalendarza dostępny pełen zestaw
: - dla kalendarzy (poziom 1 w widoku wg nazw) ukryj popraw i generuj
: - dla pozostalych (poziom 1 w widoku wg lat i miesiące w obu widokach) ukryj popraw, usuń i generuj
_hide:=
   {? _type=!KAL_NAZW || 'PGE'
   |? _type=!KAL_ROK || ''
   || 'UPGE'
   ?};

_view.TAB.actions(_view.SEL_WND,_hide,,1);

: ogranicz kalendarz do wybranego okresu
_a.filter();

: odrysuj okna definicji i wzorca kalendarza
grp_disp(KAL_DEF,_c);
grp_disp(KAL_OPIS,_d);

~~


\kal_def_adm_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsluga okienka definicji kalendarza.
::   WE: _a - tryb odswiezania okienka w grupie
::       _b - wskazanie na menadżera
::----------------------------------------------------------------------------------------------------------------------
: jesli z jakiegos powodu filtr zostal zdjety,
: to przywroc filtrowanie do wybranego okresu
{? ~cur_tab(1,1).f_active
|| _b.filter()
?};
{? _a
|| return
?};

: aktualizacja sumy godzin w wybranym okresie (roku lub miesiacu)
_b.sum_time()


\kal_prac_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Formuła akcji "użytkownicy".
::   WE: _a - wskazanie na instancję obiektu KAL_ADM
::----------------------------------------------------------------------------------------------------------------------
KALEND.ROK:=_a.get_view().year_ref();
_od:=_a.get_view().get_dbeg();
_do:=_a.get_view().get_dend();

{? KALEND.ROK<>null & _od<>date(0,0,0) & _do<>date(0,0,0)
|| exec('kal_prac_rap','!zws_par_pkal',KALEND.ROK().NAZWA,_od,_do)
|| FUN.info('Funkcja dostępna dla roku lub miesiąca kalendarza.'@)
?}


\kal_prac_rap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Raport "użytkownicy kalendarzy".
::   WE: _a - wskazanie na rekord w tabeli KAL_NAZW
::       _b - data od
::       _c - data do
::----------------------------------------------------------------------------------------------------------------------
exec('__F_ZATR','object');

_TAB:=tab_tmp(4,
   'NAZWISKO','STRING['+$MS.fld_len('OSOBA','NAZWISKO')+']',MS.name(OSOBA,'NAZWISKO'),
   'PIERWSZE','STRING['+$MS.fld_len('OSOBA','PIERWSZE')+']',MS.name(OSOBA,'PIERWSZE'),
   'PESEL','STRING['+$MS.fld_len('OSOBA','PESEL')+']',MS.name(OSOBA,'PESEL'),
   'OD','DATE','Od dnia'@,
   'DO','DATE','Do dnia'@,
   'T','STRING['+$MS.fld_len('P','T')+']',MS.name(P,'T'),
   'IP','INTEGER',MS.name(P,'IP'),
   'F_ZATR','STRING['+$MS.fld_len('F_ZATR','KOD')+']','Forma współpracy'@,
   'WYDZIAL','STRING['+$MS.fld_len('UD_SKL','SYMBOL')+']',MS.name(P,'WYDZIAL'),
   'STN','STRING['+$MS.fld_len('STN','ST')+']',MS.name(P,'ST'),
   'CP','STRING['+$MS.fld_len('CP','CP')+']',MS.name(P,'CP'),
   'DZA','DATE',MS.name(P,'DZA'),
   'DZ','DATE',MS.name(P,'DZ'),
   'ZA','STRING['+$MS.fld_len('P','ZA')+']',MS.name(P,'ZA'),
   'KIN','STRING['+$MS.fld_len('R_WZCZ','KIN')+']','Wzorzec indywidualny'@,
   'GRAFIK','STRING['+$MS.fld_len('R_WZCZ','GRAFIK')+']','Grafik'@,
   'REF','INTEGER','Rekord'
);
_TAB.fld_fml('T','DISPLAY_FORMAT',P.fld_fml('T','*DISPLAY_FORMAT'));

_sch:=exec('domyslny','schemat','PODZORG');
_skl:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_def:=exec('szukaj_ud_def','schemat',_sch,_skl).REF;
_kod:=__PARSES.getVal('F_ZATR').KOD;
_zat:=__PARSES.getVal('ZakresDanych');

_ok:=0;
{? type_of(_sch)<>type_of(null) | _sch=null
|| FUN.emsg('Brak domyślnego schematu podziału organizacyjnego.'@)
|? type_of(_skl)<>type_of(null) | _skl=null
|| FUN.emsg('Nieokreślona jednostka organizacyjna.'@)
|? type_of(_def)<>type_of(null) | _def=null
|| FUN.emsg('Jednostka nie występuje w schemacie podziału organizacyjnego.'@)
|? type_of(_kod)<>type_of('')
|| FUN.emsg('Nieokreślona forma współpracy.'@)
|? type_of(_zat)<>type_of('')
|| FUN.emsg('Nieokreślony zakres przeglądania danych współpracowników.'@)
|| _ok:=1
?};
{? ~_ok
|| return()
?};

P.cntx_psh();
P.clear();
exec('filtruj_p','schemat','PKD',_def,_kod,,_zat);

R_WZCZ.cntx_psh();
R_WZCZ.use('r_wzczas');
R_WZCZ.index('R_WZWND');
_loop:=P.f_first();
_size:=P.f_size();
_cnt:=0;
{!
|? _loop
|! R_WZCZ.prefix('pracowni',P.ref(),'N','N');
   {? progress(100*_cnt/_size,'Proszę czekać. Trwa analiza danych...'@,FUN.TYT,,,,,'Anuluj'@)<>-1 &
      FUN.ask('Czy na pewno przerwać analizę danych?'@)
   || R_WZCZ.cntx_pop();
      P.f_clear();
      P.cntx_pop();
      prgs_clr();
      return()
   ?};
   {? R_WZCZ.find_le(_c)
   || P.OSOBA();
      _TAB.blank(1);
      _TAB.NAZWISKO:=OSOBA.NAZWISKO;
      _TAB.PIERWSZE:=OSOBA.PIERWSZE;
      _TAB.PESEL:=OSOBA.PESEL;
      _TAB.T:=P.T;
      _TAB.IP:=P.IP;
      _TAB.F_ZATR:=P.F_ZATR().KOD;
      _TAB.WYDZIAL:=P.WYDZIAL().SYMBOL;
      _TAB.STN:=P.ST().ST;
      _TAB.CP:=P.CP().CP;
      _TAB.DZA:=P.DZA;
      _TAB.DZ:=P.DZ;
      _TAB.ZA:=P.ZA;
      _TAB.REF:=#P.ref();
      {!
      |? {? R_WZCZ.KAL=_a
         || _TAB.OD:=R_WZCZ.OD;
            _TAB.KIN:=R_WZCZ.KIN;
            _TAB.GRAFIK:=R_WZCZ.GRAFIK;
            _ref:=R_WZCZ.ref();
            {? R_WZCZ.next()
            || _TAB.DO:=R_WZCZ.OD-1
            || _TAB.DO:=date(0,0,0)
            ?};
            R_WZCZ.seek(_ref);
            _TAB.add()
         ?};
         _b<=R_WZCZ.OD & R_WZCZ.prev()
      !}
   ?};
   _cnt+=1;
   _loop:=P.f_next()
!};

P.f_clear();
P.cntx_pop();
R_WZCZ.cntx_pop();

prgs_clr();

KAL_NAZW.cntx_psh();
KAL_NAZW.index('KAL_NAZW');
KAL_NAZW.prefix(exec('ref_firma','ustawienia'));
_nazwa:='';
{? KAL_NAZW.seek(_a)
|| _nazwa:=KAL_NAZW.NAZWA
?};
KAL_NAZW.cntx_pop();

_wnd:=_TAB.mk_sel('Użytkownicy kalendarza "%1" w dniach %2 - %3'@ [_nazwa,_b$4,_c$4],
                  'T',0,'kal_nazw_rap',,,,0,'U','T',,,,'html_maximized');
_TAB.win_fld(_wnd,,'NAZWISKO',,,20);
_TAB.win_fld(_wnd,,'PIERWSZE',,,20);
_TAB.win_fld(_wnd,,'PESEL');
_TAB.win_fld(_wnd,,'T');
_TAB.win_fld(_wnd,,'IP',,,-5);
_TAB.win_fld(_wnd,,'WYDZIAL',,,-16);
_TAB.win_fld(_wnd,,'STN',,,20);
_TAB.win_fld(_wnd,,'CP',,,-9);
_TAB.win_fld(_wnd,,'ZA',,,-3,,,,,,2,,"'T'","'N'","''");
_TAB.win_fld(_wnd,,'OD',,,10);
_TAB.win_fld(_wnd,,'DO',,,10);
_TAB.win_fld(_wnd,,'KIN',,,-3,,,,,,2,,"'T'","'N'","''");
_TAB.win_fld(_wnd,,'GRAFIK',,,-3,,,,,,2,,"'T'","'N'","''");
_TAB.win_act(_wnd,0,'Szukaj');
_TAB.win_act(_wnd,0,'Kolejność');
_TAB.win_sel(_wnd);
_TAB.select()


\grp_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Funkcje grupowej edycji kalendarza.
::   WE: _a _KAL_ROK - wskazanie wiersza tabeli KAL_ROK lub null
::       _b INTEGER - numer grupy:
::          1 - nadpisanie na podstawie wzorca
::          2 - przypisanie podanych wartości
::       _c INTEGER - oznaczenie okresu:
::          1 - dzień
::          2 - tydzień
::          3 - miesiąc
::   WY: ~~ lub treść komunikatu o błędzie
::----------------------------------------------------------------------------------------------------------------------
_fml:='grp_edit';
_lib:='!zws_par_pkal';

: kontrola poprawności argumentów wywołania
{? var_pres('_a')<>type_of(null) | _a=null | ref_tab(_a)<>KAL_ROK
|| return(
      'Błędny argument %1 formuły %2 z biblioteki %3.\n%4'@
      ['_a',_fml,_lib,'Wymagane wskazanie wiersza tabeli KAL_ROK.'@]
   )
|? var_pres('_b')<>type_of(0) | _b<1 | _b>2
|| return(
      'Błędny argument %1 formuły %2 z biblioteki %3.\n%4'@
      ['_b',_fml,_lib,'Dozwolone wartości: 1, 2.'@]
   )
|? var_pres('_c')<>type_of(0) | _c<1 | _c>3
|| return(
      'Błędny argument %1 formuły %2 z biblioteki %3.\n%4'@
      ['_b',_fml,_lib,'Dozwolone wartości: 1, 2, 3.'@]
   )
?};

: formuła wywołująca dedykowaną formułę do obsługi akcji grupowej
_exe:=$(
   '_par:=obj_new(\'cyr\',\'CAL\',\'OBJ\');'
   'params_set(\'par\',_par);'
   '_par.cyr:=_a;'
   '_par.CAL:=_b;'
   '_par.OBJ:=~~;'
   'exec(\'%1_%3%4\',\'%2\')'
   [_fml,_lib,$_b,$_c]
);
: zweryfikuj poprawność
_CHK:=form_chk(_exe);
{? _CHK.first()
|| return(_CHK.ERR_DESC)
?};
obj_del(_CHK);
&_CHK;

: mapa argumentów
_year:=_a;
_mode:=_b;
_part:=_c;

_epilog:="
   KAL_NAZW.cntx_pop();
   KAL_WZOR.cntx_pop();
   KAL_OPIS.cntx_pop();
   KAL_ROK.cntx_pop();
   KAL_DEF.cntx_pop();
   ~~
";

KAL_NAZW.cntx_psh();
KAL_WZOR.cntx_psh();
KAL_OPIS.cntx_psh();
KAL_ROK.cntx_psh();
KAL_DEF.cntx_psh();

KAL_ROK.prefix();
: dla formalności...
{? ~KAL_ROK.seek(_year)
|| _epilog();
   return('Nie znaleziono wiersza "%1" tabeli KAL_ROK.'[$_year])
?};

: bufor zmian wprowadzonych do kalendarza
: aktualizacja kalendarza po potwierdzeniu
_CAL:=sql(
   'select '
   '  DATA, DATAW, '
   '  TYP, TYPWS, RODZAJ, '
   '  POCZATEK, KONIEC, CZAS, '
   '  OPIS, '
   '  0 as ZMIANA, REFERENCE as KAL_DEF '
   'from KAL_DEF '
   'where ROK=:_a '
   'order by DATA',
   _year
);

: pobierz teksty etykiet i podpowiedzi
_res:=exec('grp_edit_txt','!zws_par_pkal');

: utwórz okienko podglądu modyfikacji
_wnd:=_CAL.mk_sel(_res.hdr.KALEND,'P',0,,,,,,'U');
_CAL.win_sel(_wnd);
: dołącz kolumny
_CAL.win_fld(_wnd,,'DATA',,,-10,,,_res.txt.DATA,,_res.com.DATA);
_CAL.win_fld(_wnd,,'TYP',,,-3,,,_res.txt.TYP,,_res.com.TYP);
_CAL.win_fld(_wnd,,'TYPWS',,,-3,,,_res.txt.TYPWS,,_res.com.TYPWS);
_CAL.win_fld(_wnd,,'RODZAJ',,,-3,,,_res.txt.RODZAJ,,_res.com.RODZAJ);
_CAL.win_fld(_wnd,,'POCZATEK',,,-8,,,_res.txt.POCZATEK,,_res.com.POCZATEK);
_CAL.win_fld(_wnd,,'KONIEC',,,-8,,,_res.txt.KONIEC,,_res.com.KONIEC);
_CAL.win_fld(_wnd,,'CZAS',,,-8,,,_res.txt.CZAS,,_res.com.CZAS);
_CAL.win_fld(_wnd,,'OPIS',,,35,,,_res.txt.OPIS,,_res.com.OPIS);
: dołącz akcje
_CAL.win_act(_wnd,,'Szukaj');
_CAL.win_act(_wnd,,'Kolejność');
_CAL.win_act(_wnd,,'Wyświetl',,,,"exec('grp_edit_cx_wb','!zws_par_pkal')");
_CAL.win_act(_wnd,,'Rekord',,,,"exec('grp_edit_cx_rb','!zws_par_pkal')");
: dołącz przyciski "OK" i "Anuluj"
exec('grp_edit_btn','!zws_par_pkal',_CAL,_wnd);

: utwórz okienko redakcji do wyszukiwania danych
_wnd:=_CAL.mk_edit(_res.hdr.D1_MOD,0);
_CAL.win_esep(_wnd,_res.hdr.D1_KAL);
_CAL.win_efld(_wnd,,'DATA',,,,,,_res.txt.DATA,,_res.com.DATA);
_CAL.win_efld(_wnd,,'OPIS',,,20,,,_res.txt.OPIS,,_res.com.OPIS);
_CAL.win_efld(_wnd,AH,'H',,,,,1);
_CAL.win_efld(_wnd,,'TYPWS',,,20,,,_res.txt.TYPWS,,_res.com.TYPWS);
_CAL.win_efld(_wnd,,'TYP',,,,,,_res.txt.TYP,1,_res.com.TYP,'radio-buttons',,
   _res.val.TYP[4],"''",
   _res.val.TYP[1],"'R'",
   _res.val.TYP[2],"'W'",
   _res.val.TYP[3],"'S'"
);
_CAL.win_ecol(_wnd);
_CAL.win_esep(_wnd,_res.hdr.CZAS);
_CAL.win_efld(_wnd,,'RODZAJ',,,,,,_res.txt.RODZAJ,,_res.com.RODZAJ,'radio-buttons',,
   _res.val.RODZAJ[3],"''",
   _res.val.RODZAJ[1],"'W'",
   _res.val.RODZAJ[2],"'G'"
);
_CAL.win_efld(_wnd,AH,'H',,,,,1);
_CAL.win_efld(_wnd,,'POCZATEK',,,13,,,_res.txt.POCZATEK,,_res.com.POCZATEK);
_CAL.win_efld(_wnd,,'KONIEC',,,13,,,_res.txt.KONIEC,,_res.com.KONIEC);
_CAL.win_efld(_wnd,,'DATAW',,,,,,_res.txt.DATAW,,_res.com.DATAW);
_CAL.win_efld(_wnd,,'CZAS',,,13,,,_res.txt.CZAS,,_res.com.CZAS);
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_CAL,_wnd);
_CAL.win_edit(_wnd);

: wywołaj obsługę akcji
_ret:=_exe(_year,_CAL);
{? type_of(_ret)=type_of(0) & _ret<>0
:  aktualizuj definicję kalendarza
|| KAL_DEF.index('KAL_DEF');
   KAL_DEF.prefix(_year);
   _loop:=_CAL.first();
   _test:=0;
   {!
   |? _loop
   |! {? _CAL.ZMIANA<>0
      || {? KAL_DEF.find_key(_CAL.DATA)
         || KAL_DEF.DATAW:=_CAL.DATAW;
            KAL_DEF.TYPWS:=_CAL.TYPWS;
            KAL_DEF.TYP:=_CAL.TYP;
            KAL_DEF.RODZAJ:=_CAL.RODZAJ;
            KAL_DEF.POCZATEK:=_CAL.POCZATEK;
            KAL_DEF.KONIEC:=_CAL.KONIEC;
            KAL_DEF.CZAS:=_CAL.CZAS;
            KAL_DEF.OPIS:=_CAL.OPIS;
            _test+=KAL_DEF.put()
         ?}
      ?};
      _loop:=_CAL.next()
   !};
   {? _test
   || exec('akt_rok','kaledit',KAL_ROK.NAZWA,KAL_ROK.ROK)
   ?}
?};

_epilog();
~~


\grp_edit_11
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Grupowa edycja dni kalendarza - przypisanie ze wzorca.
::   WE: _a _KAL_ROK - rok kalendarzowy
::       _b TABLE - alias bufora edytowanych danych
::       _c TABLE - alias migawki z definicji kalendarza
::   WY: wynik edycji: 0 - rezygnacja, 1 - zatwierdzenie zmian
::----------------------------------------------------------------------------------------------------------------------
: mapa parametrów wywołania
_par:=params_get().par;
params_set(_par);
_cyr:=_par.cyr;
_CAL:=_par.CAL;

: pobierz teksty etykiet i podpowiedzi
_res:=exec('grp_edit_txt','!zws_par_pkal');

: utwórz i ustaw listę dni
_BUF:=tab_tmp(1,
   'DATA',    'DATE',_res.txt.DATA,
   'DZIEN',   'INTEGER',_res.txt.DZIEN,
   'ZMIANA',  'STRING[1]',_res.txt.ZMIANA,
   'KAL_DEF', 'STRING[16]',_res.txt.KAL_DEF,
   'POCZATEK','TIME',_res.txt.POCZATEK,
   'KONIEC',  'TIME',_res.txt.KONIEC,
   'CZAS',    'TIME',_res.txt.CZAS,
   'OPIS',    'STRING[%1]'[$MS.fld_len(KAL_DEF,'OPIS')],_res.txt.OPIS,
   'KAL_WZOR','STRING[16]',_res.txt.KAL_WZOR,
   'WZORZEC' ,'STRING[%1]'[$MS.fld_len(KAL_WZOR,'NAZWA')],_res.txt.WZORZEC
);
_wnd:=_BUF.mk_sel('Dni'@,'P',0,,,,,,'U');
: dodaj kolumny
_BUF.win_fld(_wnd,,'DZIEN',,,-2,,,,,_res.com.DZIEN);
_BUF.win_fld(_wnd,,'DATA',,,-10,,,,,_res.com.DATA);
_BUF.win_fld(_wnd,,'POCZATEK',,,-8,,,,,_res.com.POCZATEK);
_BUF.win_fld(_wnd,,'KONIEC',,,-8,,,,,_res.com.KONIEC);
_BUF.win_fld(_wnd,,'CZAS',,,-8,,,,,_res.com.CZAS);
_BUF.win_fld(_wnd,,'OPIS',,,20,,,,,_res.com.OPIS);
_BUF.win_fld(_wnd,,'WZORZEC',,,-19,,,,,_res.com.WZORZEC);
_BUF.win_fld(_wnd,,'ZMIANA',,,-3,,,,,_res.com.ZMIANA,2,,"'T'","'N'");
: dodaj akcje
_BUF.win_act(_wnd,,'Formuła',_res.act.ZAZNACZ.text,,_res.act.ZAZNACZ.help,
   "  {? cur_tab(1,1).sel_size()>0
:        zachowaj zaznaczenie
      || return(0)
      ?};
      exec('grp_edit_x1_zb','!zws_par_pkal')
   ",,1,1,
   "  exec('grp_edit_x1_zb','!zws_par_pkal');
:     zabezpiecz przed "odznaczeniem"
      0
   ",,'Z'
);
_BUF.win_act(_wnd,,'Formuła',_res.act.WZORZEC.text,,_res.act.WZORZEC.help,
:  aktualizacja dni
   "exec('grp_edit_zap_wzo','!zws_par_pkal')",
:  aktualizacja kalendarza
   "  _TAB:=cur_tab(1,1);
      _CAL:=params_get().CAL;
      _cal:=_CAL.ref();
      {? _CAL.find_tab(,'DATA',,'=',_TAB.DATA)
      || KAL_OPIS.index('KAL_OPIS');
         KAL_OPIS.prefix(KAL_WZOR.ref(),_TAB.DATA~1);
         exec('grp_edit_ust_wzo','!zws_par_pkal',_CAL,_TAB.DATA)
      ?};
      _CAL.seek(_cal)
   ",,1,
:  wybór wzorca
   "exec('grp_edit_wyb_wzo','!zws_par_pkal',cur_tab(1,1).DATA~1)",,
   'W'
);
_BUF.win_act(_wnd,,'Formuła',_res.act.OK.text,,_res.act.OK.help,"sel_exit()");
_BUF.win_act(_wnd,,'Szukaj');
_BUF.win_act(_wnd,,'Kolejność');
_BUF.win_act(_wnd,,'Wyświetl',,,,"exec('grp_edit_cx_wb','!zws_par_pkal')");
_BUF.win_act(_wnd,,'Rekord',,,,
   "  exec('grp_edit_cx_rb','!zws_par_pkal');
      {? _a=0 || return() ?};
      _aid:='';
      _TAB:=cur_tab(1,1);
      {? _TAB.sel_size()=0
      || _aid+='W'
      ?};
      _TAB.actions_grayed(cur_win(1,1),_aid)
   "
);
: dołącz przyciski "OK" i "Anuluj"
exec('grp_edit_btn','!zws_par_pkal',_BUF,_wnd);

: utwórz okienko redakcji do wyszukiwania danych
_wne:=_BUF.mk_edit(_res.hdr.D1_MOD,0);
_BUF.win_esep(_wne,_res.hdr.D1_KAL);
_BUF.win_efld(_wne,,'DATA',,,,,,_res.txt.DATA,,_res.com.DATA);
_BUF.win_efld(_wne,,'OPIS',,,20,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_efld(_wne,,'WZORZEC',,,20,,,,,_res.com.WZORZEC);
_BUF.win_ecol(_wne);
_BUF.win_esep(_wne,_res.hdr.CZAS);
_BUF.win_efld(_wne,,'POCZATEK',,,13,,,_res.txt.POCZATEK,,_res.com.POCZATEK);
_BUF.win_efld(_wne,,'KONIEC',,,13,,,_res.txt.KONIEC,,_res.com.KONIEC);
_BUF.win_efld(_wne,,'CZAS',,,13,,,_res.txt.CZAS,,_res.com.CZAS);
_BUF.win_ecol(_wne);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,,'radio-buttons',,
   _res.val.ZMIANA[3],"''",
   _res.val.ZMIANA[1],"'N'",
   _res.val.ZMIANA[2],"'T'"
);
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_edit(_wne);

: utwórz okno panelu edycji
_grp:=_BUF.grp_make(menu_txt(),,,40,,,,'normal');
_BUF.grp_sel(_grp,_BUF,_wnd,_res.hdr.DX_MOD,,,,,,,,,'maximized');
_BUF.grp_sel(_grp,_CAL,_CAL.win_sel('?'),_res.hdr.KALEND,,,,,,,,,'maximized');
_BUF.win_sel(_grp);

: wypełnij bufor danymi
exec('grp_edit_rkald','!zws_par_pkal',_BUF,_CAL);

_CAL.first();
_BUF.first();
_BUF.select()


\grp_edit_12
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Grupowa edycja tygodni kalendarza - przypisanie ze wzorca.
::   WE: _a _KAL_ROK - rok kalendarzowy
::       _b TABLE - alias bufora edytowanych danych
::       _c TABLE - alias migawki z definicji kalendarza
::   WY: wynik edycji: 0 - rezygnacja, 1 - zatwierdzenie zmian
::----------------------------------------------------------------------------------------------------------------------
: mapa parametrów wywołania
_par:=params_get().par;
params_set(_par);
_cyr:=_par.cyr;
_CAL:=_par.CAL;

: pobierz teksty etykiet i podpowiedzi
_res:=exec('grp_edit_txt','!zws_par_pkal');

: utwórz i ustaw listę dni
_BUF:=tab_tmp(1,
   'NUMER',   'INTEGER',_res.txt.NUMER,
   'ZMIANA',  'STRING[1]',_res.txt.ZMIANA,
   'OD',      'DATE',_res.txt.OD,
   'DO',      'DATE',_res.txt.DO,
   'OPIS',    'STRING[30]',_res.txt.OPIS,
   'KAL_WZOR','STRING[16]',_res.txt.KAL_WZOR,
   'WZORZEC' ,'STRING[%1]'[$MS.fld_len(KAL_WZOR,'NAZWA')],_res.txt.WZORZEC
);
_wnd:=_BUF.mk_sel(_res.hdr.TYGODN,'P',0,,,,,,'U');
: dodaj kolumny
_BUF.win_fld(_wnd,,'NUMER',,,-3,,,,,_res.com.NUMER);
_BUF.win_fld(_wnd,,'OD',,,-10,,,,,_res.com.OD);
_BUF.win_fld(_wnd,,'DO',,,-10,,,,,_res.com.DO);
_BUF.win_fld(_wnd,,'OPIS',,,25,,,,,_res.com.OPIS);
_BUF.win_fld(_wnd,,'WZORZEC',,,26,,,,,_res.com.WZORZEC);
_BUF.win_fld(_wnd,,'ZMIANA',,,-3,,,,,_res.com.ZMIANA,2,,"'T'","'N'");
: dodaj akcje
_BUF.win_act(_wnd,,'Formuła',_res.act.WZORZEC.text,,_res.act.WZORZEC.help,
:  aktualizacja dni
   "exec('grp_edit_zap_wzo','!zws_par_pkal')",
:  aktualizacja kalendarza
   "  _TAB:=cur_tab(1,1);
      _CAL:=params_get().CAL;
      _cal:=_CAL.ref();
      _CAL.prefix();
      _CAL.f_set('DATA',,'to_date(:_a)<=DATA and DATA<=to_date(:_b)',_TAB.OD,_TAB.DO);
      _loop:=_CAL.f_first();
      KAL_OPIS.index('KAL_OPIS');
      KAL_OPIS.prefix(KAL_WZOR.ref(),_CAL.DATA~1);
      {!
      |? _loop
      |! exec('grp_edit_ust_wzo','!zws_par_pkal',_CAL,_CAL.DATA);
         _loop:=_CAL.f_next()
      !};
      _CAL.f_clear();
      _CAL.seek(_cal)
   ",,1,
:  wybór wzorca
   "exec('grp_edit_wyb_wzo','!zws_par_pkal',cur_tab(1,1).OD~1)",,
   'W'
);
_BUF.win_act(_wnd,,'Formuła',_res.act.OK.text,,_res.act.OK.help,"sel_exit()");
_BUF.win_act(_wnd,,'Szukaj');
_BUF.win_act(_wnd,,'Kolejność');
_BUF.win_act(_wnd,,'Rekord',,,,
   "  {? _a=0 || return() ?};
      _aid:='';
      _TAB:=cur_tab(1,1);
      {? _TAB.sel_size()=0
      || _aid+='W'
      ?};
      _TAB.actions_grayed(cur_win(1,1),_aid)
   "
);
: dołącz przyciski "OK" i "Anuluj"
exec('grp_edit_btn','!zws_par_pkal',_BUF,_wnd);

: utwórz okienko redakcji do wyświetlania danych
_wne:=_BUF.mk_edit(_res.hdr.TYDZIEN,0);
_BUF.win_esep(_wne,_res.hdr.DANE_POD);
_BUF.win_efld(_wne,,'NUMER',,,,,,_res.txt.NUMER,,_res.com.NUMER);
_BUF.win_efld(_wne,,'OD',,,,,,_res.txt.OD,,_res.com.OD);
_BUF.win_efld(_wne,,'DO',,,,,,_res.txt.DO,,_res.com.DO);
_BUF.win_efld(_wne,,'OPIS',,,20,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_efld(_wne,,'WZORZEC',,,20,,,,,_res.com.WZORZEC);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,_res.com.ZMIANA,'check-box',,"'T'","'N'");
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_edit(_wne);

: utwórz okienko redakcji do wyszukiwania danych
_wne:=_BUF.mk_edit(_res.hdr.TYDZIEN,0);
_BUF.win_esep(_wne,_res.hdr.DANE_POD);
_BUF.win_efld(_wne,,'NUMER',,,,,,_res.txt.NUMER,,_res.com.NUMER);
_BUF.win_efld(_wne,,'OD',,,,,,_res.txt.OD,,_res.com.OD);
_BUF.win_efld(_wne,,'DO',,,,,,_res.txt.DO,,_res.com.DO);
_BUF.win_efld(_wne,,'OPIS',,,20,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_efld(_wne,,'WZORZEC',,,20,,,,,_res.com.WZORZEC);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,_res.com.ZMIANA,'radio-buttons',,
   _res.val.ZMIANA[3],"''",
   _res.val.ZMIANA[1],"'N'",
   _res.val.ZMIANA[2],"'T'"
);
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_patt(_wne);

: utwórz okno panelu edycji
_grp:=_BUF.grp_make(menu_txt(),,,40,,,,'normal');
_BUF.grp_sel(_grp,_BUF,_wnd,_res.hdr.TYGODN,,,,,,,,,'maximized');
_BUF.grp_sel(_grp,_CAL,_CAL.win_sel('?'),_res.hdr.KALEND,,,,,,,,,'maximized');
_BUF.win_sel(_grp);

: wypełnij bufor danymi
exec('grp_edit_rtydz','!zws_par_pkal',_BUF,_cyr);

: wyświetl panel
_CAL.first();
_BUF.first();
_BUF.select()


\grp_edit_13
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Grupowa edycja miesięcy kalendarza - przypisanie ze wzorca.
::   WE: _a _KAL_ROK - rok kalendarzowy
::       _b TABLE - alias bufora edytowanych danych
::       _c TABLE - alias migawki z definicji kalendarza
::   WY: wynik edycji: 0 - rezygnacja, 1 - zatwierdzenie zmian
::----------------------------------------------------------------------------------------------------------------------
: mapa parametrów wywołania
_par:=params_get().par;
params_set(_par);
_cyr:=_par.cyr;
_CAL:=_par.CAL;

: pobierz teksty etykiet i podpowiedzi
_res:=exec('grp_edit_txt','!zws_par_pkal');

: utwórz i ustaw listę dni
_BUF:=tab_tmp(1,
   'NUMER',   'INTEGER',_res.txt.NUMER,
   'ZMIANA',  'STRING[1]',_res.txt.ZMIANA,
   'OD',      'DATE',_res.txt.OD,
   'DO',      'DATE',_res.txt.DO,
   'OPIS',    'STRING[30]',_res.txt.OPIS,
   'KAL_WZOR','STRING[16]',_res.txt.KAL_WZOR,
   'WZORZEC' ,'STRING[%1]'[$MS.fld_len(KAL_WZOR,'NAZWA')],_res.txt.WZORZEC
);
_wnd:=_BUF.mk_sel(_res.hdr.MIESX,'P',0,,,,,,'U');
: dodaj kolumny
_BUF.win_fld(_wnd,,'NUMER',,,-3,,,,,_res.com.NUMER);
_BUF.win_fld(_wnd,,'OD',,,-10,,,,,_res.com.OD);
_BUF.win_fld(_wnd,,'DO',,,-10,,,,,_res.com.DO);
_BUF.win_fld(_wnd,,'OPIS',,,25,,,,,_res.com.OPIS);
_BUF.win_fld(_wnd,,'WZORZEC',,,26,,,,,_res.com.WZORZEC);
_BUF.win_fld(_wnd,,'ZMIANA',,,-3,,,,,_res.com.ZMIANA,2,,"'T'","'N'");
: dodaj akcje
_BUF.win_act(_wnd,,'Formuła',_res.act.WZORZEC.text,,_res.act.WZORZEC.help,
:  aktualizacja dni
   "exec('grp_edit_zap_wzo','!zws_par_pkal')",
:  aktualizacja kalendarza
   "  _TAB:=cur_tab(1,1);
      _CAL:=params_get().CAL;
      _cal:=_CAL.ref();
      _CAL.prefix();
      _CAL.f_set('DATA',,'to_date(:_a)<=DATA and DATA<=to_date(:_b)',_TAB.OD,_TAB.DO);
      _loop:=_CAL.f_first();
      KAL_OPIS.index('KAL_OPIS');
      KAL_OPIS.prefix(KAL_WZOR.ref(),_CAL.DATA~1);
      {!
      |? _loop
      |! exec('grp_edit_ust_wzo','!zws_par_pkal',_CAL,_CAL.DATA);
         _loop:=_CAL.f_next()
      !};
      _CAL.f_clear();
      _CAL.seek(_cal)
   ",,1,
:  wybór wzorca
   "exec('grp_edit_wyb_wzo','!zws_par_pkal',cur_tab(1,1).OD~1)",,
   'W'
);
_BUF.win_act(_wnd,,'Formuła',_res.act.OK.text,,_res.act.OK.help,"sel_exit()");
_BUF.win_act(_wnd,,'Szukaj');
_BUF.win_act(_wnd,,'Kolejność');
_BUF.win_act(_wnd,,'Rekord',,,,
   "  {? _a=0 || return() ?};
      _aid:='';
      _TAB:=cur_tab(1,1);
      {? _TAB.sel_size()=0
      || _aid+='W'
      ?};
      _TAB.actions_grayed(cur_win(1,1),_aid)
   "
);
: dołącz przyciski "OK" i "Anuluj"
exec('grp_edit_btn','!zws_par_pkal',_BUF,_wnd);

: utwórz okienko redakcji do wyświetlania danych
_wne:=_BUF.mk_edit(_res.hdr.MIES1,0);
_BUF.win_esep(_wne,_res.hdr.DANE_POD);
_BUF.win_efld(_wne,,'NUMER',,,,,,_res.txt.NUMER,,_res.com.NUMER);
_BUF.win_efld(_wne,,'OD',,,,,,_res.txt.OD,,_res.com.OD);
_BUF.win_efld(_wne,,'DO',,,,,,_res.txt.DO,,_res.com.DO);
_BUF.win_efld(_wne,,'OPIS',,,20,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_efld(_wne,,'WZORZEC',,,20,,,,,_res.com.WZORZEC);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,_res.com.ZMIANA,'check-box',,"'T'","'N'");
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_edit(_wne);

: utwórz okienko redakcji do wyszukiwania danych
_wne:=_BUF.mk_edit(_res.hdr.MIES1,0);
_BUF.win_esep(_wne,_res.hdr.DANE_POD);
_BUF.win_efld(_wne,,'NUMER',,,,,,_res.txt.NUMER,,_res.com.NUMER);
_BUF.win_efld(_wne,,'OD',,,,,,_res.txt.OD,,_res.com.OD);
_BUF.win_efld(_wne,,'DO',,,,,,_res.txt.DO,,_res.com.DO);
_BUF.win_efld(_wne,,'OPIS',,,20,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_efld(_wne,,'WZORZEC',,,20,,,,,_res.com.WZORZEC);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,_res.com.ZMIANA,'radio-buttons',,
   _res.val.ZMIANA[3],"''",
   _res.val.ZMIANA[1],"'N'",
   _res.val.ZMIANA[2],"'T'"
);
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_patt(_wne);

: utwórz okno panelu edycji
_grp:=_BUF.grp_make(menu_txt(),,,40,,,,'normal');
_BUF.grp_sel(_grp,_BUF,_wnd,_res.hdr.MIESX,,,,,,,,,'maximized');
_BUF.grp_sel(_grp,_CAL,_CAL.win_sel('?'),_res.hdr.KALEND,,,,,,,,,'maximized');
_BUF.win_sel(_grp);

: wypełnij bufor danymi
exec('grp_edit_rmies','!zws_par_pkal',_BUF,_cyr);

: wyświetl panel
_CAL.first();
_BUF.first();
_BUF.select()


\grp_edit_21
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Grupowa edycja dni kalendarza - przypisanie wartości.
::   WE: _a _KAL_ROK - rok kalendarzowy
::       _b TABLE - alias bufora edytowanych danych
::       _c TABLE - alias migawki z definicji kalendarza
::   WY: wynik edycji: 0 - rezygnacja, 1 - zatwierdzenie zmian
::----------------------------------------------------------------------------------------------------------------------
: mapa parametrów wywołania
_par:=params_get().par;
_cyr:=_par.cyr;
_CAL:=_par.CAL;
_par.OBJ:=exec('grp_edit_red_buf','!zws_par_pkal');
params_set(_par);

: pobierz teksty etykiet i podpowiedzi
_res:=exec('grp_edit_txt','!zws_par_pkal');

: utwórz i ustaw listę dni
_BUF:=tab_tmp(1,
   'DATA',    'DATE',_res.txt.DATA,
   'DZIEN',   'INTEGER',_res.txt.DZIEN,
   'ZMIANA',  'STRING[1]',_res.txt.ZMIANA,
   'KAL_DEF', 'STRING[16]',_res.txt.KAL_DEF,
   'POCZATEK','TIME',_res.txt.POCZATEK,
   'KONIEC',  'TIME',_res.txt.KONIEC,
   'CZAS',    'TIME',_res.txt.CZAS,
   'OPIS',    'STRING[%1]'[$MS.fld_len(KAL_DEF,'OPIS')],_res.txt.OPIS,
   'Z_POCZ',  'TIME',_res.txt.POCZATEK,
   'Z_KON',   'TIME',_res.txt.KONIEC,
   'Z_CZ',    'TIME',_res.txt.CZAS
);
_wnd:=_BUF.mk_sel(_res.hdr.DNI,'P',0,,,,,,'U');
: dodaj kolumny
_BUF.win_fld(_wnd,,'DZIEN',,,-2,,,,,_res.com.DZIEN);
_BUF.win_fld(_wnd,,'DATA',,,-10,,,,,_res.com.DATA);
_BUF.win_fld(_wnd,,'POCZATEK',,,-8,,,,,_res.com.POCZATEK);
_BUF.win_fld(_wnd,,'KONIEC',,,-8,,,,,_res.com.KONIEC);
_BUF.win_fld(_wnd,,'CZAS',,,-8,,,,,_res.com.CZAS);
_BUF.win_fld(_wnd,,'OPIS',,,-16,,,,,_res.com.OPIS);
_BUF.win_fld(_wnd,,'Z_POCZ',,,-8,,,,,_res.com.POCZATEK);
_BUF.win_fld(_wnd,,'Z_KON',,,-8,,,,,_res.com.KONIEC);
_BUF.win_fld(_wnd,,'Z_CZ',,,-8,,,,,_res.com.CZAS);
_BUF.win_fld(_wnd,,'ZMIANA',,,-3,,,,,_res.com.ZMIANA,2,,"'T'","'N'");
: dodaj akcje
_BUF.win_act(_wnd,,'Formuła',_res.act.ZAZNACZ.text,,_res.act.ZAZNACZ.help,
   "  {? cur_tab(1,1).sel_size()>0
:        zachowaj zaznaczenie
      || return(0)
      ?};
      exec('grp_edit_x1_zb','!zws_par_pkal')
   ",,1,1,
   "  exec('grp_edit_x1_zb','!zws_par_pkal');
:     zabezpiecz przed "odznaczeniem"
      0
   ",,'Z'
);
_BUF.win_act(_wnd,,'Formuła',_res.act.WARTOSC.text,,_res.act.WARTOSC.help,
:  aktualizacja dni
   "  params_set(params_get());
      exec('grp_edit_zap_war','!zws_par_pkal')
   ",
:  aktualizacja kalendarza
   "  _par:=params_get();
      params_set(_par);
      _CAL:=_par.CAL;
      _cal:=_CAL.ref();
      {? _CAL.find_tab(,'DATA',,'=',KAL_DEF.DATA)
      || exec('grp_edit_ust_war','!zws_par_pkal',_CAL,_par.OBJ)
      ?};
      _CAL.seek(_cal)
   ",,1,
:  redakcja wartości
   "  params_set(params_get());
      exec('grp_edit_red_war','!zws_par_pkal')
   ",,'W'
);
_BUF.win_act(_wnd,,'Formuła',_res.act.OK.text,,_res.act.OK.help,"sel_exit()");
_BUF.win_act(_wnd,,'Szukaj');
_BUF.win_act(_wnd,,'Kolejność');
_BUF.win_act(_wnd,,'Wyświetl',,,,"exec('grp_edit_cx_wb','!zws_par_pkal')");
_BUF.win_act(_wnd,,'Rekord',,,,
   "  exec('grp_edit_cx_rb','!zws_par_pkal');
      {? _a=0 || return() ?};
      _aid:='';
      _TAB:=cur_tab(1,1);
      {? _TAB.sel_size()=0
      || _aid+='W'
      ?};
      _TAB.actions_grayed(cur_win(1,1),_aid)
   "
);
: dołącz przyciski "OK" i "Anuluj"
exec('grp_edit_btn','!zws_par_pkal',_BUF,_wnd);

: utwórz okienko redakcji do wyszukiwania danych
_wne:=_BUF.mk_edit(_res.hdr.D1_MOD,0);
_BUF.win_esep(_wne,_res.hdr.D1_KAL);
_BUF.win_efld(_wne,,'DATA',,,,,,_res.txt.DATA,,_res.com.DATA);
_BUF.win_efld(_wne,,'OPIS',,,20,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_ecol(_wne);
_BUF.win_esep(_wne,_res.hdr.CZAS);
_BUF.win_efld(_wne,,'POCZATEK',,,13,,,_res.txt.POCZATEK,,_res.com.POCZATEK);
_BUF.win_efld(_wne,,'KONIEC',,,13,,,_res.txt.KONIEC,,_res.com.KONIEC);
_BUF.win_efld(_wne,,'CZAS',,,13,,,_res.txt.CZAS,,_res.com.CZAS);
_BUF.win_ecol(_wne);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,,'radio-buttons',,
   _res.val.ZMIANA[3],"''",
   _res.val.ZMIANA[1],"'N'",
   _res.val.ZMIANA[2],"'T'"
);
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_edit(_wne);

: utwórz okno panelu edycji
_grp:=_BUF.grp_make(menu_txt(),,,40,,,,'normal');
_BUF.grp_sel(_grp,_BUF,_wnd,_res.hdr.DX_MOD,,,,,,,,,'maximized');
_BUF.grp_sel(_grp,_CAL,_CAL.win_sel('?'),_res.hdr.KALEND,,,,,,,,,'maximized');
_BUF.win_sel(_grp);

: wypełnij bufor danymi
exec('grp_edit_rkald','!zws_par_pkal',_BUF,_CAL);

_CAL.first();
_BUF.first();
_BUF.select()


\grp_edit_22
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Grupowa edycja tygodni kalendarza - przypisanie wartości.
::   WE: _a _KAL_ROK - rok kalendarzowy
::       _b TABLE - alias bufora edytowanych danych
::       _c TABLE - alias migawki z definicji kalendarza
::   WY: wynik edycji: 0 - rezygnacja, 1 - zatwierdzenie zmian
::----------------------------------------------------------------------------------------------------------------------
: mapa parametrów wywołania
_par:=params_get().par;
_cyr:=_par.cyr;
_CAL:=_par.CAL;
_par.OBJ:=exec('grp_edit_red_buf','!zws_par_pkal');
params_set(_par);

: pobierz teksty etykiet i podpowiedzi
_res:=exec('grp_edit_txt','!zws_par_pkal');

: utwórz i ustaw listę dni
_BUF:=tab_tmp(1,
   'NUMER',   'INTEGER',_res.txt.NUMER,
   'ZMIANA',  'STRING[1]',_res.txt.ZMIANA,
   'OD',      'DATE',_res.txt.OD,
   'DO',      'DATE',_res.txt.DO,
   'OPIS',    'STRING[30]',_res.txt.OPIS,
   'Z_POCZ',  'TIME',_res.txt.POCZATEK,
   'Z_KON',   'TIME',_res.txt.KONIEC,
   'Z_CZ',    'TIME',_res.txt.CZAS
);
_wnd:=_BUF.mk_sel(_res.hdr.TYGODN,'P',0,,,,,,'U');
: dodaj kolumny
_BUF.win_fld(_wnd,,'NUMER',,,-3,,,,,_res.com.NUMER);
_BUF.win_fld(_wnd,,'OD',,,-10,,,,,_res.com.OD);
_BUF.win_fld(_wnd,,'DO',,,-10,,,,,_res.com.DO);
_BUF.win_fld(_wnd,,'OPIS',,,25,,,,,_res.com.OPIS);
_BUF.win_fld(_wnd,,'Z_POCZ',,,-8,,,,,_res.com.POCZATEK);
_BUF.win_fld(_wnd,,'Z_KON',,,-8,,,,,_res.com.KONIEC);
_BUF.win_fld(_wnd,,'Z_CZ',,,-8,,,,,_res.com.CZAS);
_BUF.win_fld(_wnd,,'ZMIANA',,,-3,,,,,_res.com.ZMIANA,2,,"'T'","'N'");
: dodaj akcje
_BUF.win_act(_wnd,,'Formuła',_res.act.WARTOSC.text,,_res.act.WARTOSC.help,
:  aktualizacja dni
   "  params_set(params_get());
      exec('grp_edit_zap_war','!zws_par_pkal')
   ",
:  aktualizacja kalendarza
   "  _par:=params_get();
      params_set(_par);
      _TAB:=cur_tab(1,1);
      _CAL:=_par.CAL;
      _cal:=_CAL.ref();
      _CAL.prefix();
      _CAL.f_set('DATA',,'to_date(:_a)<=DATA and DATA<=to_date(:_b)',_TAB.OD,_TAB.DO);
      _loop:=_CAL.f_first();
      {!
      |? _loop
      |! exec('grp_edit_ust_war','!zws_par_pkal',_CAL,_par.OBJ);
         _loop:=_CAL.f_next()
      !};
      _CAL.f_clear();
      _CAL.seek(_cal)
   ",,1,
:  redakcja wartości
   "  params_set(params_get());
      exec('grp_edit_red_war','!zws_par_pkal')
   ",,'W'
);
_BUF.win_act(_wnd,,'Formuła',_res.act.OK.text,,_res.act.OK.help,"sel_exit()");
_BUF.win_act(_wnd,,'Szukaj');
_BUF.win_act(_wnd,,'Kolejność');
_BUF.win_act(_wnd,,'Rekord',,,,
   "  {? _a=0 || return() ?};
      _aid:='';
      _TAB:=cur_tab(1,1);
      {? _TAB.sel_size()=0
      || _aid+='W'
      ?};
      _TAB.actions_grayed(cur_win(1,1),_aid)
   "
);
: dołącz przyciski "OK" i "Anuluj"
exec('grp_edit_btn','!zws_par_pkal',_BUF,_wnd);

: utwórz okienko redakcji do wyświetlania danych
_wne:=_BUF.mk_edit(_res.hdr.TYDZIEN,0);
_BUF.win_esep(_wne,_res.hdr.DANE_POD);
_BUF.win_efld(_wne,,'NUMER',,,,,,_res.txt.NUMER,,_res.com.NUMER);
_BUF.win_efld(_wne,,'OD',,,,,,_res.txt.OD,,_res.com.OD);
_BUF.win_efld(_wne,,'DO',,,,,,_res.txt.DO,,_res.com.DO);
_BUF.win_efld(_wne,,'OPIS',,,,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,_res.com.ZMIANA,'check-box',,"'T'","'N'");
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_edit(_wne);

: utwórz okienko redakcji do wyszukiwania danych
_wne:=_BUF.mk_edit(_res.hdr.TYDZIEN,0);
_BUF.win_esep(_wne,_res.hdr.DANE_POD);
_BUF.win_efld(_wne,,'NUMER',,,,,,_res.txt.NUMER,,_res.com.NUMER);
_BUF.win_efld(_wne,,'OD',,,,,,_res.txt.OD,,_res.com.OD);
_BUF.win_efld(_wne,,'DO',,,,,,_res.txt.DO,,_res.com.DO);
_BUF.win_efld(_wne,,'OPIS',,,,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,_res.com.ZMIANA,'radio-buttons',,
   _res.val.ZMIANA[3],"''",
   _res.val.ZMIANA[1],"'N'",
   _res.val.ZMIANA[2],"'T'"
);
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_patt(_wne);

: utwórz okno panelu edycji
_grp:=_BUF.grp_make(menu_txt(),,,40,,,,'normal');
_BUF.grp_sel(_grp,_BUF,_wnd,_res.hdr.TYGODN,,,,,,,,,'maximized');
_BUF.grp_sel(_grp,_CAL,_CAL.win_sel('?'),_res.hdr.KALEND,,,,,,,,,'maximized');
_BUF.win_sel(_grp);

: wypełnij bufor danymi
exec('grp_edit_rtydz','!zws_par_pkal',_BUF,_cyr);

: wyświetl panel
_CAL.first();
_BUF.first();
_BUF.select()


\grp_edit_23
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Grupowa edycja miesięcy kalendarza - przypisanie wartości.
::   WE: _a _KAL_ROK - rok kalendarzowy
::       _b TABLE - alias bufora edytowanych danych
::       _c TABLE - alias migawki z definicji kalendarza
::   WY: wynik edycji: 0 - rezygnacja, 1 - zatwierdzenie zmial
::----------------------------------------------------------------------------------------------------------------------
: mapa parametrów wywołania
_par:=params_get().par;
_cyr:=_par.cyr;
_CAL:=_par.CAL;
_par.OBJ:=exec('grp_edit_red_buf','!zws_par_pkal');
params_set(_par);

: pobierz teksty etykiet i podpowiedzi
_res:=exec('grp_edit_txt','!zws_par_pkal');

: utwórz i ustaw listę dni
_BUF:=tab_tmp(1,
   'NUMER',   'INTEGER',_res.txt.NUMER,
   'ZMIANA',  'STRING[1]',_res.txt.ZMIANA,
   'OD',      'DATE',_res.txt.OD,
   'DO',      'DATE',_res.txt.DO,
   'OPIS',    'STRING[30]',_res.txt.OPIS,
   'Z_POCZ',  'TIME',_res.txt.POCZATEK,
   'Z_KON',   'TIME',_res.txt.KONIEC,
   'Z_CZ',    'TIME',_res.txt.CZAS
);
_wnd:=_BUF.mk_sel(_res.hdr.MIESX,'P',0,,,,,,'U');
: dodaj kolumny
_BUF.win_fld(_wnd,,'NUMER',,,-3,,,,,_res.com.NUMER);
_BUF.win_fld(_wnd,,'OD',,,-10,,,,,_res.com.OD);
_BUF.win_fld(_wnd,,'DO',,,-10,,,,,_res.com.DO);
_BUF.win_fld(_wnd,,'OPIS',,,25,,,,,_res.com.OPIS);
_BUF.win_fld(_wnd,,'Z_POCZ',,,-8,,,,,_res.com.POCZATEK);
_BUF.win_fld(_wnd,,'Z_KON',,,-8,,,,,_res.com.KONIEC);
_BUF.win_fld(_wnd,,'Z_CZ',,,-8,,,,,_res.com.CZAS);
_BUF.win_fld(_wnd,,'ZMIANA',,,-3,,,,,_res.com.ZMIANA,2,,"'T'","'N'");
: dodaj akcje
_BUF.win_act(_wnd,,'Formuła',_res.act.WARTOSC.text,,_res.act.WARTOSC.help,
:  aktualizacja dni
   "  params_set(params_get());
      exec('grp_edit_zap_war','!zws_par_pkal')
   ",
:  aktualizacja kalendarza
   "  _par:=params_get();
      params_set(_par);
      _TAB:=cur_tab(1,1);
      _CAL:=_par.CAL;
      _cal:=_CAL.ref();
      _CAL.prefix();
      _CAL.f_set('DATA',,'to_date(:_a)<=DATA and DATA<=to_date(:_b)',_TAB.OD,_TAB.DO);
      _loop:=_CAL.f_first();
      {!
      |? _loop
      |! exec('grp_edit_ust_war','!zws_par_pkal',_CAL,_par.OBJ);
         _loop:=_CAL.f_next()
      !};
      _CAL.f_clear();
      _CAL.seek(_cal)
   ",,1,
:  redakcja wartości
   "  params_set(params_get());
      exec('grp_edit_red_war','!zws_par_pkal')
   ",,'W'
);
_BUF.win_act(_wnd,,'Formuła',_res.act.OK.text,,_res.act.OK.help,"sel_exit()");
_BUF.win_act(_wnd,,'Szukaj');
_BUF.win_act(_wnd,,'Kolejność');
_BUF.win_act(_wnd,,'Rekord',,,,
   "  {? _a=0 || return() ?};
      _aid:='';
      _TAB:=cur_tab(1,1);
      {? _TAB.sel_size()=0
      || _aid+='W'
      ?};
      _TAB.actions_grayed(cur_win(1,1),_aid)
   "
);
: dołącz przyciski "OK" i "Anuluj"
exec('grp_edit_btn','!zws_par_pkal',_BUF,_wnd);

: utwórz okienko redakcji do wyświetlania danych
_wne:=_BUF.mk_edit(_res.hdr.MIES1,0);
_BUF.win_esep(_wne,_res.hdr.DANE_POD);
_BUF.win_efld(_wne,,'NUMER',,,,,,_res.txt.NUMER,,_res.com.NUMER);
_BUF.win_efld(_wne,,'OD',,,,,,_res.txt.OD,,_res.com.OD);
_BUF.win_efld(_wne,,'DO',,,,,,_res.txt.DO,,_res.com.DO);
_BUF.win_efld(_wne,,'OPIS',,,,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,_res.com.ZMIANA,'check-box',,"'T'","'N'");
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_edit(_wne);

: utwórz okienko redakcji do wyszukiwania danych
_wne:=_BUF.mk_edit(_res.hdr.MIES1,0);
_BUF.win_esep(_wne,_res.hdr.DANE_POD);
_BUF.win_efld(_wne,,'NUMER',,,,,,_res.txt.NUMER,,_res.com.NUMER);
_BUF.win_efld(_wne,,'OD',,,,,,_res.txt.OD,,_res.com.OD);
_BUF.win_efld(_wne,,'DO',,,,,,_res.txt.DO,,_res.com.DO);
_BUF.win_efld(_wne,,'OPIS',,,,,,_res.txt.OPIS,,_res.com.OPIS);
_BUF.win_esep(_wne,_res.hdr.ZMIANA);
_BUF.win_efld(_wne,,'ZMIANA',,,,,,,,_res.com.ZMIANA,'radio-buttons',,
   _res.val.ZMIANA[3],"''",
   _res.val.ZMIANA[1],"'N'",
   _res.val.ZMIANA[2],"'T'"
);
: dołącz przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_BUF,_wne);
_BUF.win_patt(_wne);

: utwórz okno panelu edycji
_grp:=_BUF.grp_make(menu_txt(),,,40,,,,'normal');
_BUF.grp_sel(_grp,_BUF,_wnd,_res.hdr.TYGODN,,,,,,,,,'maximized');
_BUF.grp_sel(_grp,_CAL,_CAL.win_sel('?'),_res.hdr.KALEND,,,,,,,,,'maximized');
_BUF.win_sel(_grp);

: wypełnij bufor danymi
exec('grp_edit_rmies','!zws_par_pkal',_BUF,_cyr);

: wyświetl panel
_CAL.first();
_BUF.first();
_BUF.select()


\grp_edit_wyb_wzo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Wybór wzorca kalendarza.
::   WE: _a INTEGER - rok
::   WY: 1/0 - wybrano/zrezygnowoano
::----------------------------------------------------------------------------------------------------------------------
KAL_WZOR.prefix();
KAL_WZOR.f_set(
   'NAZWA',,
   'KAL_WZOR.FIRMA=:_a and '
   'KAL_WZOR.REFERENCE in ( '
   '  select distinct KAL_OPIS.WZORZEC as REF '
   '  from KAL_OPIS '
   '  where KAL_OPIS.ROK=:_b '
   '  order by REF '
   ')',
   exec('ref_firma','#firma'),_a
);
_ref:=null;
KAL_WZOR.win_sel('WYB');
{? KAL_WZOR.select()
|| _ref:=KAL_WZOR.ref()
?};
KAL_WZOR.f_clear();
KAL_WZOR.index('KAL_WZOR');
KAL_WZOR.prefix(exec('ref_firma','#firma'));

_ref<>null & KAL_WZOR.seek(_ref)


\grp_edit_red_buf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Tworzy tabelkę do edycji wartości.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
: pobierz teksty etykiet i podpowiedzi
_res:=exec('grp_edit_txt','!zws_par_pkal');

: bufor dla edycji
_TMP:=tab_tmp(1,
   'TYP',     'STRING[1]',_res.txt.TYP,
   'RODZAJ',  'STRING[1]',_res.txt.RODZAJ,
   'POCZATEK','TIME',     _res.txt.POCZATEK,
   'KONIEC',  'TIME',     _res.txt.KONIEC,
   'DNI',     'INTEGER',  _res.txt.DNI,
   'CZAS',    'TIME',     _res.txt.CZAS
);
: dodaj funkcjonalność pól
_fat:='BEFORE_EDIT';
_fml:="cur_tab(1,1).TYP='R'";
_TMP.fld_fml('RODZAJ',_fat,_fml);
_TMP.fld_fml('POCZATEK',_fat,_fml);
_TMP.fld_fml('KONIEC',_fat,_fml);
_TMP.fld_fml('DNI',_fat,_fml);
_TMP.fld_fml('CZAS',_fat,"0");
_fat:='AFTER_EDIT';
_TMP.fld_fml('TYP',_fat,"
   _TAB:=cur_tab(1,1);
   {? _TAB.TYP<>'R'
   || _TAB.RODZAJ:='W';
      _TAB.POCZATEK:=_TAB.KONIEC:=_TAB.CZAS:=time(0,0,0);
      _TAB.DNI:=0
   ?};
   1
");
_fml:="
   _TAB:=cur_tab(1,1);
   {? _TAB.DNI<0
   || _TAB.DNI:=0
   ?};
   {? _TAB.POCZATEK<=_TAB.KONIEC
   || _TAB.CZAS:=_TAB.KONIEC-_TAB.POCZATEK;
      _TAB.CZAS+=*(_TAB.DNI*60*24)
   || {? _TAB.DNI=0
      || _TAB.DNI:=1
      ?};
      _TAB.CZAS:=*(_TAB.DNI*60*24);
      _TAB.CZAS-=_TAB.POCZATEK-_TAB.KONIEC
   ?};
   1
";
_TMP.fld_fml('POCZATEK',_fat,_fml);
_TMP.fld_fml('KONIEC',_fat,_fml);
_TMP.fld_fml('DNI',_fat,_fml);

: utwórz zredukowane okienko redakcji dnia
_wnd:=_TMP.mk_edit(_res.hdr.D1_KAL,0);
_TMP.win_efld(_wnd,,'TYP',,,,,,,,_res.com.TYP,'radio-buttons',,
   _res.val.TYP[1],"'R'",
   _res.val.TYP[2],"'W'",
   _res.val.TYP[3],"'S'"
);
_TMP.win_ecol(_wnd);
_TMP.win_efld(_wnd,,'RODZAJ',,,,,,,,_res.com.RODZAJ,'radio-buttons',,
   _res.val.RODZAJ[1],"'W'",
   _res.val.RODZAJ[2],"'G'"
);
_TMP.win_ecol(_wnd);
_TMP.win_efld(_wnd,,'POCZATEK',,,10,,,,,_res.com.POCZATEK);
_TMP.win_efld(_wnd,,'KONIEC',,,10,,,,,_res.com.KONIEC);
_TMP.win_efld(_wnd,,'DNI',,,10,,,,,_res.com.DNI);
_TMP.win_efld(_wnd,,'CZAS',,,10,,1,,,_res.com.CZAS);
: dodaj przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_TMP,_wnd,,,,,,,exec('text_red_ok','#window'));
_TMP.win_edit(_wnd);

_TMP


\grp_edit_red_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Edycja wartości.
::   WE:
::   WY: 1/0 - zatwierdzono/zrezygnowoano
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_TMP:=_par.OBJ;

_TMP.edit()


\grp_edit_zap_wzo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Przypisanie dniu/tygodniowi/miesiącowi bieżącego wzorca kalendarza.
::   WE:
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
_TAB:=cur_tab(1,1);
{? _TAB.sel_size()=0
|| return(0)
?};
_TAB.KAL_WZOR:=$KAL_WZOR.ref();
_TAB.WZORZEC:=KAL_WZOR.NAZWA;
_TAB.ZMIANA:='T';
_TAB.put()


\grp_edit_zap_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Przypisanie dniu/tygodniowi/miesiącowi wartości z dnia kalendarza.
::   WE:
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);

_TMP:=_par.OBJ;
_TAB:=cur_tab(1,1);
{? _TAB.sel_size()=0
|| return(0)
?};

_TAB.Z_POCZ:=_TMP.POCZATEK;
_TAB.Z_KON:=_TMP.KONIEC;
_TAB.Z_CZ:=_TMP.CZAS;
_TAB.ZMIANA:='T';
_TAB.put()


\grp_edit_ust_wzo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Odszukuje dzień w definicji wzorca i ustawia wartości w tabeli.
::   WE: _a TABLE - alias tabeli tymczasowej z definicją kalendarza
::       _b DATE - dzień we wzorcu z którego mają być pobrane informacje
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_CAL:=_a;
_data:=_b;

KAL_WZOR.cntx_psh();
{? KAL_OPIS.find_tab(,'DATA',,'=',$_data) |
   KAL_OPIS.find_tab(,'NUMER',,'=',_data~4)
|| _CAL.TYP:=KAL_OPIS.TYP;
   _CAL.RODZAJ:=KAL_OPIS.RODZAJ;
   _CAL.POCZATEK:=KAL_OPIS.POCZATEK;
   _CAL.KONIEC:=KAL_OPIS.KONIEC;
   _CAL.CZAS:=KAL_OPIS.CZAS;
   _CAL.OPIS:=KAL_OPIS.OPIS;
   _CAL.DATAW:=_CAL.DATA+KAL_OPIS.DNI;
   _CAL.TYPWS:=exec('getTypWS','kaledit',_CAL.TYP,_CAL.DATA,KAL_OPIS.WZORZEC().NAZWA);
   _CAL.ZMIANA:=1;
   _CAL.put()
?};
KAL_WZOR.cntx_pop();
~~


\grp_edit_ust_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Ustawia w tabeli wartości na podstawie definicji dnia.
::   WE: _a TABLE - alias tabeli tymczasowej z definicją kalendarza
::       _b TABLE - alias tabeli tymczasowej z wartościami
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_CAL:=_a;
_TMP:=_b;

_CAL.TYP:=_TMP.TYP;
_CAL.RODZAJ:=_TMP.RODZAJ;
_CAL.POCZATEK:=_TMP.POCZATEK;
_CAL.KONIEC:=_TMP.KONIEC;
_CAL.CZAS:=_TMP.CZAS;
_CAL.DATAW:=_CAL.DATA+_TMP.DNI;
_CAL.TYPWS:=exec('getTypWS','kaledit',_CAL.TYP,_CAL.DATA);

_CAL.ZMIANA:=1;
_CAL.put()


\grp_edit_x1_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Akcja "Wybierz" dla okienek edycji według dni.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: kontekst działania
_TAB:=cur_tab(1,1);

_TAB.cntx_psh();
_od:={? _TAB.first() || _TAB.DATA || date(0,0,0) ?};
_do:={? _TAB.last()  || _TAB.DATA || date(0,0,0) ?};
_TAB.cntx_pop();

: tabela dla wyboru dni
_PAR:=tab_tmp(1,
   'OD', 'DATE',   'Od dnia'@,
   'DO', 'DATE',   'Do dnia'@,
   'DNI','INTEGER','Rodzaj'@,
   'PON','INTEGER','Poniedziałek'@,
   'WTO','INTEGER','Wtorek'@,
   'SRO','INTEGER','Środa'@,
   'CZW','INTEGER','Czwartek'@,
   'PIA','INTEGER','Piątek'@,
   'SOB','INTEGER','Sobota'@,
   'NIE','INTEGER','Niedziela'@
);
: dodaj funkcjonalność pól
_PAR.fld_fml('DNI','AFTER_EDIT',"
   _PAR:=cur_tab();
   {? _PAR.DNI<>1
   || _PAR.PON:=_PAR.WTO:=_PAR.SRO:=_PAR.CZW:=_PAR.PIA:=(_PAR.DNI=2 | _PAR.DNI=4);
      _PAR.SOB:=_PAR.NIE:=(_PAR.DNI=3 | _PAR.DNI=4)
   ?};
   1
");
{! _ii:=4.._PAR.fld_num()
|! _PAR.fld_fml(_PAR.fld_acr(_ii),'BEFORE_EDIT',"cur_tab().DNI=1")
!};
: utwórz i ustaw okienko edycji parametrów wyboru
_wnd:=_PAR.mk_edit(menu_txt(),0,,,,'normal');
_PAR.win_edit(_wnd);
_PAR.win_esep(_wnd,'Wybór'@);
_PAR.win_efld(_wnd,,'OD');
_PAR.win_efld(_wnd,,'DO');
_PAR.win_efld(_wnd,AH,'H');
_PAR.win_efld(_wnd,,'DNI',,,,,,,,,'radio-buttons',,
   'Dowolne dni',"1",
   'Dni robocze',"2",
   'Dni wolne',"3",
   'Wszystkie dni',"4"
);
: nowa kolumna
_PAR.win_ecol(_wnd);
_PAR.win_esep(_wnd,'Dni'@);
{! _ii:=4.._PAR.fld_num()
:  dodaj pola znacznikowe reprezentujące dni tygodnia
|! _PAR.win_efld(_wnd,,_PAR.fld_acr(_ii),,,,,,,,,'check-box',,"1","0")
!};
: dodaj przyciski "OK" i "Anuluj"
exec('ok_esc','#window',_PAR,_wnd);

_PAR.blank();
_PAR.OD:=_od;
_PAR.DO:=_do;
{? ~_PAR.edit($(
      '_PAR:=cur_tab(); '
      '_chk:=__CHK.record(_PAR,,\'OD\',\'DO\'); '
      '{? _chk<>\'\'      || _chk '
      '|? _PAR.DO<_PAR.OD || FUN.emsg(\'%1\'); \'OD\' '
      '|? _PAR.OD<#%2     || FUN.emsg(\'%3\'); \'OD\' '
      '|? _PAR.DO>#%4     || FUN.emsg(\'%5\'); \'DO\' '
      '|| 1 '
      '?}'[
         'Data "Od dnia" nie może być późniejsza od daty "Do dnia".'@,
         $#_od,'Data "Od dnia" nie może być wcześniejsza od daty %1.'@[$_od],
         $#_do,'Data "Do dnia" nie może być późniejsza od daty %1.'@[$_do]
      ]
   ))
|| return()
?};

_TAB.cntx_psh();
_loop:=_TAB.first();
{!
|? _loop
|! {? _PAR.OD<=_TAB.DATA & _TAB.DATA<=_PAR.DO & KAL_DEF.seek(_TAB.KAL_DEF)
   || {? _PAR[KAL_DEF.DATA~4+3]<>0
      || sel_add()
      ?}
   ?};
   _loop:=_TAB.next()
!};
_TAB.cntx_pop();
~~


\grp_edit_cx_wb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Obsługa akcji "wyświetl przed" dla okienek tabel zawierających wskazanie na definicję dnia kalendarza.
::   WE:
::   WY: 0
::----------------------------------------------------------------------------------------------------------------------
KAL_DEF.hdr_edit(' (oryginalny)');
KAL_DEF.display();
KAL_DEF.hdr_edit()


\grp_edit_cx_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Obsługa akcji "rekord przed" dla okienek tabel zawierających wskazanie na definicję dnia kalendarza.
::   WE:
::   WY: 0
::----------------------------------------------------------------------------------------------------------------------
_TAB:=cur_tab(1,1);
KAL_DEF.seek(_TAB.KAL_DEF);
0


\grp_edit_rtydz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Wypełnia listę tygodni w roku o danym wskazaniu.
::   WE: _a TABLE - alias tabeli tymczasowej
::       _b _KAL_ROK - wskazanie roku kalendarzowego
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_BUF:=_a;
_cyr:=_b;

KAL_ROK.cntx_psh();
KAL_ROK.prefix();
{? ~KAL_ROK.seek(_cyr)
|| KAL_ROK.cntx_pop();
   return(0)
?};
_od:=date(KAL_ROK.ROK,1,1);
_do:=date(KAL_ROK.ROK,12,0);
_BUF.ZMIANA:='N';
_BUF.NUMER:=1;
_BUF.OD:=_od;
_BUF.DO:=_BUF.OD+(7-_BUF.OD~4);
_BUF.OPIS:='%1 - %2'[spli_str(_BUF.OD$7,',')[1],spli_str(_BUF.DO$7,',')[1]];
_BUF.add();
{!
|? _BUF.OD+7<=_do
|! _BUF.NUMER+=1;
   _BUF.OD:=_BUF.DO+1;
   _BUF.DO+=7;
   {? _do<=_BUF.DO
   || _BUF.DO:=_do
   ?};
   _BUF.OPIS:='%1 - %2'[spli_str(_BUF.OD$7,',')[1],spli_str(_BUF.DO$7,',')[1]];
   _BUF.add()
!};
KAL_ROK.cntx_pop();
~~


\grp_edit_rkald
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Wypełnia listę dni w roku.
::   WE: _a TABLE - alias tabeli tymczasowej widoku dni
::       _b TABLE - alias tabeli tymczasowej widoku kalendarza
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_BUF:=_a;
_CAL:=_b;

_BUF.ZMIANA:='N';
{? _CAL.first()
|| {! _ii:=1
   |? _BUF.DATA:=_CAL.DATA;
      _BUF.DZIEN:=_BUF.DATA~4;
      _BUF.KAL_DEF:=_CAL.KAL_DEF;
      _BUF.POCZATEK:=_CAL.POCZATEK;
      _BUF.KONIEC:=_CAL.KONIEC;
      _BUF.CZAS:=_CAL.CZAS;
      _BUF.OPIS:=_CAL.OPIS;
      _BUF.add();
      _CAL.next()
   !}
?};
~~


\grp_edit_rmies
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Wypełnia listę miesięcy w roku o danym wskazaniu.
::   WE: _a TABLE - alias tabeli tymczasowej
::       _b _KAL_ROK - wskazanie roku kalendarzowego
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa argumentów
_BUF:=_a;
_cyr:=_b;

KAL_ROK.cntx_psh();
KAL_ROK.prefix();
{? ~KAL_ROK.seek(_cyr)
|| KAL_ROK.cntx_pop();
   return(0)
?};
_BUF.ZMIANA:='N';
{! _ii:=1..12
|! _BUF.NUMER:=_ii;
   _BUF.OD:=date(KAL_ROK.ROK,_ii,1);
   _BUF.DO:=date(KAL_ROK.ROK,_ii,0);
   _BUF.OPIS:=spli_str(_BUF.OD$8,' ')[1];
   _BUF.add()
!};
KAL_ROK.cntx_pop();
~~


\grp_edit_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Tworzy tablice nazwane zawierające teksty etykiet i podpowiedzi dla pól.
::   WE:
::   WY: tablica nazwana z polami txt (teksty etykiet) i com (treści podpowiedzi)
::----------------------------------------------------------------------------------------------------------------------
: teksty etykiet i podpowiedzi pól
_def:="obj_new(
   'DZIEN','DATA','TYP','TYPWS','RODZAJ','POCZATEK','KONIEC','CZAS','DATAW','DNI',
   'NUMER','OD','DO',
   'OPIS','ZMIANA',
   'KAL_DEF','KAL_WZOR','WZORZEC'
)";
_txt:=_def();
_com:=_def();

_txt.DZIEN:='Dzień tygodnia'@; _com.DZIEN:='Numer dnia w tygodniu'@;
_txt.DATA:='Data'@; _com.DATA:='Dzień kalendarza'@;
_txt.TYP:='Rodzaj'@; _com.TYP:=exec('fldComment','#field',KAL_DEF,'TYP');
_txt.TYPWS:='Rodzaj'@; _com.TYPWS:=exec('fldComment','#field',KAL_DEF,'TYPWS');
_txt.RODZAJ:='Rozliczenie'@; _com.RODZAJ:=exec('fldComment','#field',KAL_DEF,'RODZAJ');
_txt.POCZATEK:=exec('fldLabel','#field',KAL_DEF,'POCZATEK');
_com.POCZATEK:=exec('fldComment','#field',KAL_DEF,'POCZATEK');
_txt.KONIEC:=exec('fldLabel','#field',KAL_DEF,'KONIEC');
_com.KONIEC:=exec('fldComment','#field',KAL_DEF,'KONIEC');
_txt.CZAS:=exec('fldLabel','#field',KAL_DEF,'CZAS');
_com.CZAS:=exec('fldComment','#field',KAL_DEF,'CZAS');
_txt.DATAW:=exec('fldLabel','#field',KAL_DEF,'DATAW');
_com.DATAW:=exec('fldComment','#field',KAL_DEF,'DATAW');
_txt.DNI:=exec('fldLabel','#field',KAL_OPIS,'DNI');
_com.DNI:=exec('fldComment','#field',KAL_OPIS,'DNI');
_txt.NUMER:='Numer'@; _com.NUMER:='Numer w kolejności'@;
_txt.OD:='Od dnia'@; _com.OD:='Data rozpoczęcia'@;
_txt.DO:='Do dnia'@; _com.DO:='Data zakończenia'@;
_txt.OPIS:='Opis'@; _com.OPIS:='Krótki opis'@;
_txt.ZMIANA:='Zmiana'@; _com.ZMIANA:='Zmiana wartości [T/N]'@;
_txt.KAL_DEF:='Dzień'@; _com.KAL_DEF:='Wskazanie dnia'@;
_txt.KAL_WZOR:='Wzorzec'@; _com.KAL_WZOR:='Wskazanie wzorca'@;
_txt.WZORZEC:='Wzorzec'@; _com.WZORZEC:='Nazwa wzorca kalendarza'@;

: nazwy i podpowiedzi akcji
_def:="_obj:=obj_new('text','help'); _obj.text:=_a; _obj.help:=_b; _obj";
_act:=obj_new('OK','ANULUJ','ZAZNACZ','WZORZEC','WARTOSC');
_act.OK:=_def('Dalej'@@,'Kontynuuj działanie funkcji programu'@);
_act.ANULUJ:=_def('Anuluj'@@,'Przerwij działanie funkcji programu'@);
_act.ZAZNACZ:=_def('Zaznacz'@@,'Zaznaczenie wierszy według podanych warunków'@);
_act.WZORZEC:=_def('Wypełnij ze wzorca'@@,'Ustalenie wartości na postawie wzorca'@);
_act.WARTOSC:=_def('Wypełnij wartościami'@@,'Wprowadzenie podanych wartości'@);

: nagłowki okien i zakładek
_hdr:=obj_new(
   'KALEND','DZIEN','DNI','TYDZIEN','TYGODN','MIES1','MIESX',
   'D1_MOD','DX_MOD','D1_KAL','DX_KAL',
   'CZAS','DANE_POD','ZMIANA'
);
_hdr.KALEND:='Zmodyfikowany kalendarz'@;
_hdr.DZIEN:='Dzień'@;
_hdr.DNI:='Dni'@;
_hdr.TYDZIEN:='Tydzień'@;
_hdr.TYGODN:='Tygodnie'@;
_hdr.MIES1:='Miesiąc'@;
_hdr.MIESX:='Miesiące'@;
_hdr.D1_MOD:='Modyfikowany dzień'@;
_hdr.DX_MOD:='Modyfikowane dni'@;
_hdr.D1_KAL:='Dzień kalendarza'@;
_hdr.DX_KAL:='Dni kalendarza'@;
_hdr.DANE_POD:='Dane podstawowe'@;
_hdr.CZAS:='Czas pracy'@;
_hdr.ZMIANA:='Zmiana wartości'@;

: etykiety wartości
_val:=obj_new('TYP','RODZAJ','ZMIANA');
_val.TYP:=spli_str('%1|%2|%3|%4'['Roboczy'@,'Wolny'@,'Świąteczny'@,'Dowolny'@],'|');
_val.RODZAJ:=spli_str('%1|%2|%3'['Widełkowe'@,'Godzinowe'@,'Dowolne'@],'|');
_val.ZMIANA:=spli_str('%1|%2|%3'['Nie zmienione'@,'Zmienione'@,'Bez znaczenia'@],'|');

: wartość zwracana
_ret:=obj_new('txt','com','act','hdr','val');
_ret.txt:=_txt;
_ret.com:=_com;
_ret.act:=_act;
_ret.hdr:=_hdr;
_ret.val:=_val;
_ret


\grp_edit_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [19.22]
:: OPIS: Dodaje do okienka wertowania przyciski "OK" i "Anuluj"
::   WE: _a TABLE - alias tabeli
::       _b STRING - akronim okienka wertowania
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: mapa parametrów
_TAB:=_a;
_wnd:=_b;

_cfg:='panel=bottom,align=end';

_btn:=_TAB.win_btn(_wnd,'text=%1,icon=%2,%3'['&OK'@,'xwin16.png:13',_cfg],'menu:D');
_TAB.btn_opt(_btn,'tooltip=%1'[exec('help_red_ok','#window')]);

_btn:=_TAB.win_btn(_wnd,'text=%1,icon=%2,%3'['&Anuluj'@,'xwin16.png:14',_cfg],'key:Esc');
_TAB.btn_opt(_btn,'tooltip=%1'[exec('help_red_esc','#window')]);
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 947c3b4276be532ab626145211245831db057a0dd6726e1573bcdf37fad294e3cb01c86a0d41b883e2ca3c04f94eebc7df462346310cb6e50d6e12463645708d2f68694aeec0df6c1cdfbcb691edf5f5217c46d30be7ff93a93e76f42675ce6c3e516c5706df8446d2d9b01a4af4d81faa56a9a4bbf79748384f3291bb937887

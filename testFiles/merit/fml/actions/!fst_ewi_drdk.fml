:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_drdk.fml
:: Utworzony: 30.10.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DRDK - Redakcja dokumentu innych zmian środka
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Redakcja dokumentu innych zmian środka - główna formuła czynności FST_EWI_DRDK.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
{? ~exec('okr_mod','fst') || return(0) ?};

{? ~SRD.isDataCompleted()
|| FUN.emsg('Środek wymaga uzupełnienia danych. Dołączanie i modyfikacja dokumentów nie jest możliwa.'@);
   return(0)
?};

{? SRD.isElement()
|| FUN.emsg('Nie można dodawać i modyfikować dokumentów innych zmian dla elementów składowych zestawu.\n'
            'Dane tego typu są wspólne dla zestawu dlatego taki dokument należy dodawać na poziomie zestawu.'@);
   return(0)
?};

_tmp:=params_get().drdk_akcja;
{? _tmp<>'' || _a:=_tmp ?};

{? (_=0 & _tmp='') | _a='dołącz' || exec('dok_add','!fst_ewi_drdk')
|? _a='popraw' || exec('dok_edit','!fst_ewi_drdk')
|? _a='usuń' || exec('dok_del','!fst_ewi_drdk')
?}


\dok_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowego dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
_ref:=null;
_tmp:=EDIT_ES.RODZ;

{? SRSR.r_lock(1,1,1)
|| EDIT_ES.RODZ:='I';
   SRDT.cntx_psh();
   SRDT.index('SRDT');
   SRDT.prefix(EDIT_ES.RODZ);
   {? SRDT.first()
   || SRDT.win_sel('SELECT');
      {? SRDT.select()
      || _ref:=SRDT.ref()
      ?}
   || FUN.emsg('W systemie nie zdefiniowano żadnego typu dokumentu innej zmiany środka.\n'
              'Należy uzupełnić brakujące dane w obszarze Ustawienia i parametryzacja.'@);
      SRSR.r_unlock();
      EDIT_ES.RODZ:=_tmp;
      SRDT.cntx_pop();
      return(0)
   ?};

   {? _ref<>null
   || SRDO.blank();
      SRDO_POM.blank();
      SRDO.TYP:=_ref;
      SRDO.ODD_DOK:=SRST.ODD;
      SRDO.NR:=exec('bl_srdo_nr','fst');
      SRDO.SYMBOL:=exec('symdok','fst');
      {? SRDO.TYP().ULWK='T' || SRDO.ULWK:=SRST.ULWK ?};
      {? SRDO.TYP().SEZONOWY='T'
      || SRDO_POM.SEZ:=SRDO.SEZ:=SRST.S;
         SRDO_POM.SCH_SEZ:=SRDO.SCH_SEZ:=SRST.SCH_SEZ
      ?};

      SRDO.win_edit('RED_I');
      exec('set_field_drdk','fst');
      {? SRDO.edit("exec('chk_dok','!fst_ewi_drdk',0)")
      || exec('mod_rec','fst');
         SRSR.r_unlock();
         {? SRDO.add()
         || SRD.updateRecState(SRDO.OKRO_F);
            {? SRD.isSet() || SRD.updateElements() ?}
         ?}
      || SRSR.r_unlock()
      ?}
   ?};
   SRDT.cntx_pop()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?};
EDIT_ES.RODZ:=_tmp


\dok_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
SRDO_POM.blank();
{? SRDO.r_lock(1,1)
|| _tmp:=EDIT_ES.RODZ;
   EDIT_ES.RODZ:='I';
   SRDO.win_edit('RED_I');
   SRDO_POM.EDIT:='T';
   exec('set_srdo_pom','fst2');

   exec('set_field_drdk','fst');
   {? SRDO.edit("exec('chk_dok','!fst_ewi_drdk',1)")
   || exec('mod_rec','fst');
      {? SRDO.put()
      || SRD.updateRecState(SRDO.OKRO_F);
         {? SRD.isSet() || SRD.updateElements() ?}
      ?}
   ?};
   EDIT_ES.RODZ:=_tmp;
   SRDO.r_unlock()
|| FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
?}


\dok_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
{? SRDO.count()=0
|| {? SRDO.r_lock(1,1)
   || {? FUN.ask('Usunąć dokument innej zmiany o symbolu: %1?'@[SRDO.SYMBOL])
      || SRDO.r_unlock();
         _okr:=SRDO.OKRO_F;
         {? SRDO.del(1,1)
         || SRD.updateRecState(_okr);
            {? SRD.isSet() || SRD.updateElements() ?};
            exec('srst_rfresh','!fst_ewi_drdk')
         ?}
      || SRDO.r_unlock()
      ?}
   || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
   ?}
|| FUN.emsg('Bieżący dokument jest wykorzystywany w systemie i nie można go usunąć.'@)
?}


\srst_rfresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Odświeżanie okna rejestru stanów środków po zmianach w dokumentach
::----------------------------------------------------------------------------------------------------------------------
_odd:=exec('odd_count','fst');
{? EDIT_ES.R='T'
|| {? var_press('__DTREE')>0 & __DTREE>0
   || {? OPERATOR.DEPT | _odd=1 || grp_disp(SRST,'DWER_O') || grp_disp(SRST,'DWER') ?}
   || grp_disp(SRST,'WER')
   ?}
|? EDIT_ES.R='N'
|| {? var_press('__DTREE')>0 & __DTREE>0
   || {? OPERATOR.DEPT | _odd=1 || grp_disp(SRST,'DWER_N_O') || grp_disp(SRST,'DWER_N') ?}
   || grp_disp(SRST,'WER_N')
   ?}
|? EDIT_ES.R=''
|| grp_disp(SRST,'WER_U')
?}


\chk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja poprawności dokumentu
::   WE: _a = 1 - popraw, 0 - dołącz
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRDO,,'SYMBOL','DW','DO');
{? _wy=''
|| {? SRDO.OKRO_F=null
   || FUN.emsg('Nie uzupełnione pole Okres obowiązywania\n'
               '(pole uzupełni się automatycznie po redakcji pola Data obowiązywania).'@);
      _wy:='DO'
   ?}
?};
:: kontrola unikalności numeru i symbolu dokumentu
{? _wy=''
|| {? __CHK.index(SRDO,_a)<>''
   || {? FUN.ask('Wygenerować nowy numer dokumentu?'@)
      || SRDO.NR:=exec('bl_srdo_nr','fst',SRDO.NR);
         SRDO.SYMBOL:=exec('symdok','fst')
      ?};
      _wy:='SYMBOL'
   ?}
?};
:: kontrola daty operacji
{? _wy='' & SRDO.OKRO_F<>null
|| {? exec('test_new_year','fst',SRDO.OKRO_F().ROK)
   || FUN.emsg('Wskazana data operacji pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='DO'
   ?}
?};
:: kontrola czy okres jest zamknięty
{? _wy='' & SRDO.DW<>date(0,0,0)
|| _okres:=exec('find_okro_f','fst', SRDO.DW);
   {? ~exec('okr_mod','fst',_okres)
   || _wy:='DW'
   ?}
?};
:: kontrola miesiąca obowiązywania
{? _wy='' & ~exec('chk_dok_okres','fst') || _wy:='DO' ?};
:: kontrola daty wystawienia
{? _wy='' & exec('ae_srdo_dw','fst')=0 || _wy:='DW' ?};
{? _wy='' & SRDO.SEZ='T' & SRDO.SCH_SEZ=null
|| FUN.emsg('Nie określono schematu sezonów amortyzacji.'@);
   _wy:='SCH_SEZ'
?};
{? _wy='' & (SRDO.SEZ='T' & SRDO.SCH_SEZ<>SRST.SCH_SEZ)
|| _rok:=SRDO.OKRO_F().RES;
   _ref:=SRDO.SRSR;
   _nal:=0;
   SRST.cntx_psh();
   SRST.index('PODAT');
   SRST.prefix(_ref,_rok);
   {? SRST.first()
   || {! |?
         {? SRST.NAL='T' || _nal:=1 ?};
         _nal=0 & SRST.next()
      !}
   ?};
   SRST.cntx_pop();
   {? _nal
   || FUN.emsg('Zmiana schematu amortyzacji sezonowej nie jest możliwa w roku podatkowym,\n'
               'w którym naliczono już amortyzację dla środka.'@);
      _wy:='SCH_SEZ'
   ?}
?};
:: kontrola czy zmiana nie dotyczy zadekretowanej amortyzacji
{? _wy=''
|| _dekret:=0;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRSR.ref());
   {? SRST.find_key(SRDO.ROK_F,SRDO.OKRO_F)
   || {! |?
         {? SRST.DEKRET='T' || _dekret:=1 ?};
         _dekret=0 & SRST.next()
      !}
   ?};
   SRST.cntx_pop();
   {? _dekret=1
   ||  FUN.emsg('Amortyzacja w okresie obowiązywania dokumentu lub w późniejszym została już\n'
                'zadekretowana dla co najmniej jednego toru amortyzacji. Usuwanie amortyzacji\n'
                'lub wprowadzanie zmian mogących wpłynąć na amortyzację nie jest możliwe.'@);
      _wy:='DO'
   ?}
?};
_wy

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:49 5dfaca8072c99a3b415c55f3e3f63b98349beb0e57e05a64382894e46f101875877957f90661fb2b05e673d7b265f3e7f89458ef514f2228e157d4e277bbea2aa299b3e8ed6d65c5d5e0dd13c8d4b6a007fcfee83a2574129bb497f31452d7ce332e400244b4450b62f9cf9923654d74636724c4996759f076d5254391554e38

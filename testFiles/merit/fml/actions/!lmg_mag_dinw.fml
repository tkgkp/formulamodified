:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_dinw.fml
:: Utworzony: 30.11.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Rejestracja inwentaryzacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LMG
::# kind=WE,   symbol=INN,       type=_INN,     name=Inwentaryzacja,               required=N
::# kind=WEW,  symbol=INN,       type=_INN,     name=Inwentaryzacja,               required=N
::# kind=WY,   symbol=INWR,      type=_ND,      name=Inwentarzyacja rozchód,       required=N
::# kind=WY,   symbol=INWP,      type=_ND,      name=Inwentarzyacja przychód,      required=N
::# kind=WY,   symbol=DK_LN,     type=_DK_LN,   name=Inwentarzyacja reorganizacja, required=N
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;

_area:='LMG_MAG';
:: Uruchomiona akcja
_akcja:=_mp.akcja();
:: Wskazanie na konkretną już istniejącą inwentaryzację
{? var_pres('INN',_in)=type_of(null()) & _in.INN
|| _int.INN:=_in.INN;
   _mp.save('INT','INN',_in.INN);
   _in.INN
|| null()
?};

{? _mp.pathTodo()
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('INN',_int)=type_of(null()) & _int.INN
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wewnętrznego INN.')
   ?}
?};

:: Wyzwalacz, który po dodaniu nagłówka dokumentu:
:: - add: dodaje rekord kluczowy:
::       - inwentaryzacji
::       - nagłówka rozchodu z inwentaryzacji
::       - nagłówka przychodu z inwentaryzacji
::   del: usuwa rekord kluczowy:
::       - inwentaryzacji
::       - nagłówka rozchodu z inwentaryzacji
::       - nagłówka przychodu z inwentaryzacji
:: - add: zapisuje parametr:
::       - wewnętrzny: INN = wskazanie na nagłówek inwentaryzacji
::       - wyjściowy: INWR = wskazanie na nagłówek dokumentu magazynowego (inwentaryzacja rozchód)
::       - wyjściowy: INWP = wskazanie na nagłówek dokumentu magazynowego (inwentaryzacja przychód)
::   del: zapisuje parametr: wewnętrzny:
::       - wewnętrzny: INN = null
::       - wyjściowy: INWR = null
::       - wyjściowy: INWP = null
_mp.trigRef('INN',,1,,exec('kind_internal','#b_port'),'INN');
_mp.trigRef('ND',,1,,exec('kind_out','#b_port'),'INWR',,"ND.TYP().P='N'");
_mp.trigRef('ND',,1,,exec('kind_out','#b_port'),'INWP',,"ND.TYP().P='T'");
_mp.trigRef('DK_LN',,1,,exec('kind_out','#b_port'),'DK_LN',,"DK_LN.RODZ='I'");

{? _akcja='Dołącz'
   | _mp.pathProc()
   | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
|| {? ~_mp.pathArea(_area) || INN.prefix() ?};
:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
   params_set('out',_out,'mp',_mp,'akcja',_akcja);
   exec('dol_inn','magazyn_inw');

:: Podczytanie parametrów wyjściowych
   _inta:=_mp.load(exec('kind_internal','#b_port'));
   {? var_pres('INN',_inta)<>type_of(~~) & _inta.INN
:: Dodano dokument
   ||
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      {? _mp.pathArea(_area) || INN.seek(_inta.INN) ?}
:: Wycofanie czynności bo nie dodano dokumentu
   || _mp.cancel()
   ?}

|? _akcja='Popraw'
   | _mp.pathTodo()
|| {? var_pres('INN',_int)=type_of(null())
:: Uruchomione w procesie
   || INN.cntx_psh();
      INN.prefix();
      {? INN.seek(_int.INN)
      ||
         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('pop_inn','magazyn_inw');

         _mp.descTodo()
      ?};
      INN.cntx_pop()
   |? _mp.pathTodo()
:: Uruchomione zadanie z listy todo i brak _int.INN wtedy zadanie wycofujemy
   || _mp.cancel()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      exec('pop_inn','magazyn_inw')

   ?}

|? _akcja='Usuń'
|| {? var_pres('INN',_int)=type_of(null())
:: Uruchomione w procesie
   || _inn:=null();
      INN.cntx_psh();
      {? _mp.pathArea(_area)=0 || INN.prefix() ?};
      {? INN.seek(_int.INN)
      ||
         exec('del_inn','magazyn_inw');
         {? ~INN.seek(_int.INN)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _inn:=INN.ref()
      ?};
      INN.cntx_pop();
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _mp.pathArea(_area) || {? _inn || INN.seek(_inn) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      exec('del_inn','magazyn_inw');

      _mp.done()
   ?}

|? _akcja='Zakończ_wer' & _mp.pathArea(_area)
|| _autoakc:=exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_DWYD')
           & exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_DAPZ')
           & {? INN.MG().SL='T' || exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_AREO') || 1 ?}
           & exec('autoAkc','#b__box',_mp,600480,'LMG_MAG_EAMG');
   {? exec('inn_zakoncz','magazyn_inw',1,_autoakc) || _mp.done() ?}

|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_int:=_mp.load(exec('kind_internal','#b_port'));

{? var_pres('INN',_int)=type_of(null()) & _int.INN
|| 'Zakończ rejestrację inwentaryzacji: %1'@@[exec('record','#to_string',_int.INN)]
|| 'Zarejestruj inwentaryzację'@@
?}


\inn_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Inwentaryzacja - Dołącz
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LMG_MAG_DINW';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';

exec('mp_run','#b__box',_params)


\inn_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Inwentaryzacja - Popraw
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LMG_MAG_DINW';
_params.UIDREF:=INN.uidref();
_params.AKCJA:='Popraw';

exec('mp_run','#b__box',_params)


\inn_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Inwentaryzacja - Usuń
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LMG_MAG_DINW';
_params.UIDREF:=INN.uidref();
_params.AKCJA:='Usuń';

exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 a40abac3f88d70905afeab70274353bcd0e67281c88acec1d19d861c859126715df5e41621bb7ec63f369034687250a2e96310ca240da13916ac1ecae1d6a91e77df506fc7e637f29dba34cc796fe887bf06c9110f9d3a4582e85e072c5aa63fcb40aad66340745b737130a6785b643afe5eefb1be553fc5f6b23cc3fc4d6cd3

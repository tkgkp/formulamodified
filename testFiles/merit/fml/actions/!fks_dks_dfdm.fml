:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dfdm.fml
:: Utworzony: 23.06.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DFDM - Dekretacja delegacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja delegacji - główna formuła czynności FKS_DKS_DFDM
::----------------------------------------------------------------------------------------------------------------------
::# parses=exec('parses_edokum','obiegi')
::# access=exec('uprawnienia','obiegi',0)
::# properties=TODOTRIG
::# permissions=FJKS
::
:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na delegację w obiegu, required=T, keyref=T
::# kind=WE, symbol=B_PREL, type=STRING, name=Poprzedni element w procesie, required=N
::# kind=WE, symbol=B_PRELS, type=STRING, name=Symbol poprzedniego elementu w procesie, required=N

:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=N
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na delegację w obiegu, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

{? ~exec('is_act','#b__box','FKS_DKS_DFDM')
|| FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu delegacji.'@);
   _mp.cancel(); return(~~)
|? _mp.isMicro() & _akcja='Odrzuć'
|| FUN.info('Dla wniosku nie znaleziono aktywnego procesu z czynnością dekretacji.'@); return(~~)
?};

zak_dek:=0;
OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1; dolndok:=0;
{? (~_mp.isMicro() & _we.EDOKUM & EDOKUM.seek(_we.EDOKUM,,1)) |
   (_mp.isMicro() & EDOKUM.get())
|| params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
:: akceptacja z pulpitu
   {? _akcja='Dekretuj'
   || params_get().context.EDOKUM;
      _edokum:=edokum_ref:=EDOKUM.ref(); dok_ref:=null;
      exec('fl_decl','dekret_aut');
      exec('dek_decl','dekret_aut');
      exec('sd_decl','dekret_aut');
      {? var_pres('SD_OB')<=0 || SD_OB:=obj_new(@.CLASS.SD_CLASS,'F') ?};
      exec('dob_dek_del','obiegi_dekr',0);
      {? dok_ref & EDOKUM.seek(_edokum) & EDOKUM.STDEKRD
      || exec('ustaw_out','obiegi_dekr',_wy,'D',dok_ref,EDOKUM.ref());
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _akcja='Odrzuć'
   || params_get().context.EDOKUM;
      params_exec('odrzuc','obiegi_dekr');
      _edokum:=EDOKUM.ref();
      {? zak_dek=2 & EDOKUM.seek(_edokum)
      || exec('ustaw_out','obiegi_dekr',_wy,'O',null,EDOKUM.ref());
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _mp.pathTodo() | _mp.isAutoRun()
   || menu_txt(,'Popraw');
      {? var_pres('EDOKUM',_we)
      || _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
         {? _ref<>0 & _nam<>''
         || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
            {? EDOKUM.seek(_ref,_nam)
            || exec('init','obg'); zakres:=3;
               {? var_pres('SD_OB')<=0 || SD_OB:=obj_new(@.CLASS.SD_CLASS,'F') ?};
               exec('rkprz','obiegi');
               _edokum:=EDOKUM.ref(); dok_ref:=null;
               typobi:=3;

               params_exec('przekaz_todo','obiegi_akc','D');

               VAR_DEL.delete('typobi');
               {? zak_dek=1 & dok_ref & EDOKUM.seek(_edokum) & EDOKUM.STDEKRD
               || exec('ustaw_out','obiegi_dekr',_wy,'D',dok_ref,EDOKUM.ref());
                  _mp.save(,_wy);
                  _mp.done()
               |? zak_dek=2 & EDOKUM.seek(_edokum)
               || exec('ustaw_out','obiegi_dekr',_wy,'O',null,EDOKUM.ref());
                  _mp.save(,_wy);
                  _mp.done()
               ?}
            ?};
            EDOKUM.cntx_pop()
         ?}
      ?}
   ?}
?};
VAR_DEL.delete('dolndok','dok_ref','zawin','zak_dek');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zadekretuj delegację w obiegu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));
{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || params_exec('ustaw_bprel','obiegi');
      EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || _desc:='Zadekretuj delegację w obiegu: %1, data polecenia %2, delegowany %3 %4'@@[EDOKUM.ID,$EDOKUM.DATAW,EDOKUM.DOSTAWCA().NAZWISKO,OSOBA.PIERWSZE];
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc


\dekretuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja delegacji w obiegu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_DKS_DFDM';
_params.AKCJA:='Dekretuj';
_params.UIDREF:=EDOKUM.uidref();
_params.CONTEXT:=obj_new('EDOKUM');
_params.CONTEXT.EDOKUM:=EDOKUM.ref();
exec('mp_run','#b__box',_params);
1


\odrzuc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Odrzucanie delegacji w obiegu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_DKS_DFDM';
_params.AKCJA:='Odrzuć';
_params.UIDREF:=EDOKUM.uidref();
_params.CONTEXT:=obj_new('EDOKUM');
_params.CONTEXT.EDOKUM:=EDOKUM.ref();
exec('mp_run','#b__box',_params);
1


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności FKS_DKS_DFDM
::  WE: _a - 'add po', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='add_po' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='add_po'
         || 3
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'FKS_DKS_DFDM')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Podglad dokumentu obiegu z listy zadań
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
_rfd:=null();
{? _wy.EDOKUM <> ~~
|| _rfd:=_wy.EDOKUM
|| _we:=_mp.load(exec('kind_in','#b_port'));
   _rfd:=_we.EDOKUM
?};

{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || EDOKOS.cntx_psh(); EDOKOS.use('skid_y'+(EDOKUM.name()+2));
 exec('init','obg'); zakres:=3;
         {? var_pres('SD_OB')<=0 || SD_OB:=obj_new(@.CLASS.SD_CLASS,'F') ?};
         exec('rkprz','obiegi');
         _edokum:=EDOKUM.ref(); dok_ref:=null;
         typobi:=3;
         _okn:=EDOKUM.mk_edit('Delegacja w obiegu'@);
         EDOKUM.win_ewin(_okn,,'KSD');
         EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],
         "zawin:=10; 'key:F2'");
         _btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Podział&y'@],
         "zawin:=3; 'key:F2'");
         EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Podziały controllingowe dokumentu w obiegu'@);

         _btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Historia'@],
         "zawin:=11; 'key:F2'");
         EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Etapy obiegu - szczegóły'@);
         exec('szuk_okr','okresy',EDOKUM.DOP);
         _zamknij:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'
         ['&Zamknij okno'@],'key:Esc');
         EDOKUM.btn_eopt(_okn,_zamknij,'tooltip='+exec('help_red_esc','#window','A'));
         EDOKUM.win_edit(_okn);
         zawin:=0;
         {? EDOKUM.EDOKUM || OBIEGI.PREVIEW:=EDOKUM.EDOKUM ?};
         {! |?
            {? zawin || EDOKUM.get() ?};
            EDOKUM.display();
            {? zawin=10
            || params_exec('poz_del','obiegi')
            |? zawin=3
            || params_exec('podz_dob','obiegi')
            |? zawin=11
            || exec('okn_opis','obiegi')
            ?};
            {? zawin || zawin:=0; 1 ?}
         !};
         VAR_DEL.delete('typobi');
         EDOKOS.cntx_pop()
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
VAR_DEL.delete('dolndok','dok_ref','zawin','zak_dek');
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 9e76de6d428e2dff35c07b7a9cd7fdf45802d9ae3d6a37f414e9a49783b026cf677fc8b34bec8213f356d6865451f564dab8cf4b29e9f82c693da26685e3859a8d0575699d9e05ac1d6a2388ec3f0977889a529b72bc8c7805f1ddf325459a2f614acf3d236172d01fe39924614b474623f794deebb2ca58fbae4fa76d476e59

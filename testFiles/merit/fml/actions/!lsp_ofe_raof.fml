:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_ofe_raof.fml
:: Utworzony: 23.06.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły czynności LSP_OFE_RAOF - Archiwizacja oferty
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=OFE,      type=_OFE,    name=Oferta,   required=T,    keyref=T
::# kind=WY,   symbol=OFE,      type=_OFE,    name=Oferta,   required=N
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_akcja:=_mp.akcja();
_auto:=_akcja<>'Archiwizuj' & _mp.isAutoRun();
_grupa:=_mp.isGroup();

exec('init_ofe','lsp');
{? ~(var_pres('OFE',_in)=type_of(null()) & _in.OFE)
|| _mp.error('Brak wymaganego parametru OFE.')
|| OFE.cntx_psh();
   OFE.prefix();
   {? ~OFE.seek(_in.OFE)
   || _mp.error('Nie znaleziono oferty.')
   || params_set('mp',_mp);
      {? _akcja='Archiwizuj' | _auto
      || params_exec('archiwizuj','!lsp_ofe_raof',_auto,_grupa);
         {? exec('FindAndGet','#table',OFE,_in.OFE,,"A",'')='Z'
         || _out.OFE:=_in.OFE;
            _mp.save(,_out)
         ?}
      |? _mp.pathTodo() | _mp.pathArea() & _akcja=''
      || _win_red:=exec('ofe_win_edit','!lsp_ofe_raof');
         _ff:="params_exec('ofe_pozycje_todo','!lsp_ofe_raof')";
         OFE.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
         _ff:="params_exec('ofe_archiwum_todo','!lsp_ofe_raof'); 'key:Esc'";
         OFE.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Arc&hiwizuj'@],_ff);
         _ff:="'key:Esc'";
         OFE.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
         OFE.win_edit(_win_red);
         OFE.display();
         {? exec('FindAndGet','#table',OFE,_in.OFE,,"A",'')='Z'
         || _out.OFE:=_in.OFE;
            _mp.save(,_out)
         ?}
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   OFE.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('OFE',_in)<>type_of(~~) & _in.OFE
|| 'Zaarchiwizuj ofertę: %1'@@[exec('record','#to_string',_in.OFE)]
|| ''
?}


\oferta_archiwum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - Archiwizuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_tab:=OFE.sel_aget();
OFE.sel_adel();

{? _tab.size()
|| _ok:=FUN.ask('Ilość ofert do archiwizacji: %1.\n'
                '\nCzy kontynuować?'@[$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LSP_OFE_RAOF';
   _params.UIDREF:=OFE.uidref();
   _params.AKCJA:='Archiwizuj';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OFE',OFE.ref());

   exec('mp_run','#b__box',_params)
|? _ok=1
|| exec('ini_kom','#message','Przywrócenie ofert');
   OFE.cntx_psh();
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? (OFE.clear(); OFE.seek(_tab.REF,)) & OFE.A='A'
         || _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:='LSP_OFE_RAOF';
            _params.UIDREF:=OFE.uidref();
            _params.AKCJA:='Archiwizuj';
            _params.GRUPA:='T';
            _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
            exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OFE',OFE.ref());

            exec('mp_run','#b__box',_params);
            obj_del(_params)
         ?};
         _tab.next()
      !}
   ?};
   OFE.cntx_pop();
   exec('end_kom','#message')
?};
exec('ofe_f_rfresh','lsp');
obj_del(_tab)


\ofe_archiwum_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument sprzedaży - Archiwizuj z todo
::   WE: params_get()   - ustawiane w exec('main','!lsp_ofe_raof')
::----------------------------------------------------------------------------------------------------------------------
params_exec('archiwizuj','!lsp_ofe_raof');
''


\ofe_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('ofe_poz','lsp',0);
''


\archiwizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - Archiwizacja oferty
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       [_b] - czy grupa rekordów
::  OLD: \ofe_stat/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_grupa:={? var_pres('_b')=type_of(1) || _b || 0 ?};

_mp:=params_get().mp;

_result:=0;

{? var_pres('_a')<>type_of(1) || _a:=1 ?};

_lock:=OFE.r_lock(1,1,1);

{? _lock=0
|| {? ~_grupa
   || {? FUN.ask('Ofertę obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
      || OFE.r_unlock()
      ?}
   || exec('add_kom','#message'
        ,'Ofertę %1 obsługuje inny operator.'@[OFE.SYM]
        ,4,'Archiwizacja ofert',__lp+=1)
   ?}
|? OFE.A<>'A'
|| {? ~_grupa
   || FUN.info('Oferta została już zaarchiwizowana.'@)
   || exec('add_kom','#message'
        ,'Oferta %1 została już zaarchiwizowana.'@[OFE.SYM]
        ,2,'Archiwizacja ofert',__lp+=1)
   ?};
   _result:=1;
   _mp.done()
|? OFE.STAT_REJ='N'
|| {? ~_grupa
   || FUN.info('Nie zakończono jeszcze rejestracji oferty.\nArchiwizacja niemożliwa.'@)
   || exec('add_kom','#message'
        ,'Nie zakończono jeszcze rejestracji oferty %1. Archiwizacja niemożliwa.'@[OFE.SYM]
        ,2,'Archiwizacja ofert',__lp+=1)
   ?};
   _mp.error('Nie zakończono jeszcze rejestracji oferty. Archiwizacja niemożliwa.'@);
   _result:=1
|? _auto | _grupa | FUN.ask('Zarchiwizować ofertę %1?'@[OFE.SYM])
|| OFE.cntx_psh();
   OFE.prefix();
   OFE.A:='Z';
   {? OFE.put()
   || _result:=1;
      {? _grupa
      || exec('add_kom','#message'
           ,'Ofertę %1 przeniesiono do archiwum.'@[OFE.SYM]
           ,1,'Archiwizacja ofert',__lp+=1)
      ?};
      _mp.done()
   ?};
   OFE.cntx_pop()
?};
{? _lock || OFE.r_unlock() ?};
_result


\ofe_win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - tymczasowe okienko redakcji
::----------------------------------------------------------------------------------------------------------------------
_win_akr:='RED';
:: Tymczasowe okno redakcji
_title:='Dane oferty';
_win_red:=OFE.mk_edit(_title);
OFE.win_etab(_win_red,'Dane podstawowe');
OFE.win_ewin(_win_red,,_win_akr);
_win_red

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 52bbc8da2158d549f61c1cd81afc7cfb1bf2378952e8518dffbc188ecbcf65b7e11e020034ee70a4caa8a45ddaedaf0cf9f9b12f35b742cc8f37db1673ce881d3bbb8146306efc48ae99a33e7ec4b01d324881485e3726ede47b611ff45defbe8fbbebd280fabaa53c7e0f5d7484d3d29956d8ba7198a6984755d9014a459e3e

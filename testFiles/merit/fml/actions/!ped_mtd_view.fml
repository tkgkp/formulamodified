:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ped_mtd_view.fml
:: Utworzony: 01.10.2021
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PED_MTD_VIEW - Metadane elektr. dokum. prac.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Metadane elektronicznej dokumentacji pracowniczej.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

exec('__F_ZATR','object');

SLO_NAZ.cntx_psh();
SLO_NAZ.prefix();
ZALACZ.cntx_psh();
ZALACZ.prefix();

:: Uwaga:
::    Dokumentacja pracownicza prezentowana jest w obszarze PKD_EZK - Zdarzenia kadrowe, a więc w ramach dziedziny
::    PKD - Kadry. A tutaj badamy dostępność do danych pracowników w ramach dziedziny PED, która może byc inna!
_PDOST:=exec('dostepne_p','schemat','PED','P');
:: Z tabeli powyżej powinniśmy usunąć rekordy związane z OSOBA, dla której nie mamy dostępu do wszystkich P.
:: Najpierw szukamy P, do których nie mamy dostępu.
_PTMP:=sql(
   'select P.REFERENCE as REF, P.OSOBA from P left join F_ZATR where P.FIRMA=:_a and F_ZATR.KOD=\'P\' '
   'except '
   'select REF, OSOBA from :_b'
   ,exec('ref_firma','ustawienia'),_PDOST
);
{? _PTMP.first()
|| _ndx1:=_PDOST.index('?');
   _ndx2:=_PDOST.ndx_tmp(,,'OSOBA',,);
   _PDOST.index(_ndx2);
   {!
   |? _PDOST.prefix(_PTMP.OSOBA,);
      {? _PDOST.first()
      || {! |? _PDOST.del() !}
      ?};
      _PTMP.next()
   !};
   _PDOST.index(_ndx1)
?};

_where:='"ZAOMEDA".CZESC<>\'\' and "ZAOMEDA".OSOBA in (select OSOBA from :_a order by 1)';
ZAOMEDA.cntx_psh();
ZAOMEDA.prefix();
ZAOMEDA.f_set('NAZWISKO,PIERWSZE,DRUGIE,PESEL',,_where,_PDOST);

_cfg:=exec('_cfg','!ped_mtd_view',);
params_set('cfg',_cfg);
ZAOMEDA.win_patt(_cfg.we.s);
ZAOMEDA.win_sel(_cfg.ws);
ZAOMEDA.select();
ZAOMEDA.f_clear();
ZAOMEDA.cntx_pop();

ZALACZ.cntx_pop();
SLO_NAZ.cntx_pop();
~~


\_cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Procedura tworząca okna.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Tabela z "wolnymi" polami.
_TD:=tab_tmp(1
   ,'CRT_DATE','DATE','Data utworzenia'@
   ,'CRT_TIME','TIME','Godzina utworzenia'@
   ,'MOD_DATE','DATE','Data modyfikacji'@
   ,'MOD_TIME','TIME','Godzina modyfikacji'@
   ,'ZAL','BLOBIMAGE','Załącznik'
);

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: Okno wertowania z akcjami.
_ws:=ZAOMEDA.mk_sel('Metadane dokumentacji pracowniczej'@,'P',,'zaomedaqwer',,,,,'U',,,,,'maximized');
ZAOMEDA.win_fld(_ws,,'NAZWISKO',,,30,,,,,MS.comment(ZAOMEDA,'NAZWISKO'));
ZAOMEDA.win_fld(_ws,,'PIERWSZE',,,20,,,,,MS.comment(ZAOMEDA,'PIERWSZE'));
ZAOMEDA.win_fld(_ws,,'DRUGIE',,,20,,,,,MS.comment(ZAOMEDA,'DRUGIE'));
ZAOMEDA.win_fld(_ws,,'PESEL',,,11,,,,,MS.comment(ZAOMEDA,'PESEL'));
ZAOMEDA.win_fld(_ws,,'DTCREATE',,,10,,,,,MS.comment(ZAOMEDA,'DTCREATE'));
ZAOMEDA.win_fld(_ws,,'DTSIGN',,,10,,,,,MS.comment(ZAOMEDA,'DTSIGN'));
ZAOMEDA.win_fld(_ws,,'CZESC',,,8,,,,,MS.comment(ZAOMEDA,'CZESC'));
ZAOMEDA.win_fld(_ws,,'RODZAJ',,,2,,,,,MS.comment(ZAOMEDA,'RODZAJ'));
ZAOMEDA.win_act(_ws,,'Formuła','Zmian&y'@@,,'Informacje o modyfikacjach bieżącego zapisu'@
   ,"exec('zmiany','#syslog')",,,,,
   ,'Y'
);
ZAOMEDA.win_act(_ws,,'Szukaj');
ZAOMEDA.win_act(_ws,,'Kolejność');
ZAOMEDA.win_act(_ws,,'Rekord',,,,"
   _err:=1;
   _cfg:=params_get().cfg;
   _TD:=_cfg.TD;
   _TD.blank();
   {? ref_tab(ZAOMEDA.NAG)=ZALACZ
   || {? ZALACZ.seek(ZAOMEDA.NAG)
      || _TD.CRT_DATE:=ZALACZ.bl_info('ZAL','CREAT_DATE');
         _TD.CRT_TIME:=ZALACZ.bl_info('ZAL','CREAT_TIME');
         _TD.MOD_DATE:=ZALACZ.bl_info('ZAL','MODIFY_DATE');
         _TD.MOD_TIME:=ZALACZ.bl_info('ZAL','MODIFY_TIME');
         {? ZALACZ.bl_info('ZAL','IMAGE')
         || _TD.ZAL:=ZALACZ.ZAL
         ?};
         ZAOMEDA.win_edit(_cfg.we.eZALACZ);
         _err:=0
      ?}
   || ZAOMEDA.win_edit(_cfg.we.s)
   ?};
   _err"
);

::
_szer:=40;
:: Kolejność pól w tablicy jest istotna!
_we:=obj_new('s','eZALACZ');
{! _lp:=1 .. 2
|! _we[_lp]:=ZAOMEDA.mk_edit('Metadane'@,,'#zaomeda',,,'html_maximized');
   ZAOMEDA.win_esep(_we[_lp],'Pracownik'@);
   ZAOMEDA.win_efld(_we[_lp],,'NAZWISKO',,,_szer,,,,,MS.comment(ZAOMEDA,'NAZWISKO'));
   ZAOMEDA.win_efld(_we[_lp],,'PIERWSZE',,,_szer,,,,,MS.comment(ZAOMEDA,'PIERWSZE'));
   ZAOMEDA.win_efld(_we[_lp],,'DRUGIE',,,_szer,,,,,MS.comment(ZAOMEDA,'DRUGIE'));
   ZAOMEDA.win_efld(_we[_lp],,'PESEL',,,,,,,,MS.comment(ZAOMEDA,'PESEL'));
   ZAOMEDA.win_efld(_we[_lp],,'DOKRODZ',,,_szer,,,,,MS.comment(ZAOMEDA,'DOKRODZ'));
   ZAOMEDA.win_efld(_we[_lp],,'DOKNR',,,_szer,,,,,MS.comment(ZAOMEDA,'DOKNR'));
   ZAOMEDA.win_esep(_we[_lp],'Pracodawca'@);
   ZAOMEDA.win_efld(_we[_lp],,'NAZWA',,,_szer,,,,,MS.comment(ZAOMEDA,'NAZWA'));
   ZAOMEDA.win_esep(_we[_lp],'Metadane'@);
   ZAOMEDA.win_efld(_we[_lp],,'ID',,,_szer,,,,,MS.comment(ZAOMEDA,'ID'));
   ZAOMEDA.win_efld(_we[_lp],,'NAG',,,_szer,,,'Identyfikator dokumentu'@,,'Identyfikator dokumentu'@);
   ZAOMEDA.win_efld(_we[_lp],,'CZESC',,,8,,,,,MS.comment(ZAOMEDA,'CZESC'));
   ZAOMEDA.win_efld(_we[_lp],,'RODZAJ',,,8,,,,,MS.comment(ZAOMEDA,'RODZAJ'));
   ZAOMEDA.win_efld(_we[_lp],,'DTCREATE',,,,,,,,MS.comment(ZAOMEDA,'DTCREATE'));
   ZAOMEDA.win_efld(_we[_lp],,'DTSIGN',,,,,,,,MS.comment(ZAOMEDA,'DTSIGN'));
   {? _lp=2
   || ZAOMEDA.win_esep(_we[_lp],'Plik'@);
      ZAOMEDA.win_efld(_we[_lp],ZALACZ,'ZAL_NAME',,,_szer,,,,,MS.comment(ZALACZ,'ZAL_NAME'));
      ZAOMEDA.win_efld(_we[_lp],_TD,'CRT_DATE',,,,,,,,'Data utworzenia załączonego pliku'@);
      ZAOMEDA.win_efld(_we[_lp],_TD,'CRT_TIME',,,10,,,,,'Godzina utworzenia załączonego pliku'@);
      ZAOMEDA.win_efld(_we[_lp],_TD,'MOD_DATE',,,,,,,,'Data modyfikacji załączonego pliku'@);
      ZAOMEDA.win_efld(_we[_lp],_TD,'MOD_TIME',,,10,,,,,'Godzina modyfikacji załączonego pliku'@);
      ZAOMEDA.win_efld(_we[_lp],ZALACZ,'SIZE',,,,,,,,MS.comment(ZALACZ,'SIZE'));
      ZAOMEDA.win_esep(_we[_lp],'Dane rozszerzone'@);
      ZAOMEDA.win_efld(_we[_lp],ZALACZ,'TYP_ZAL','NAZWA',,_szer,,,'Typ załącznika'@,,MS.comment(SLO_NAZ,'NAZWA'));
      ZAOMEDA.win_efld(_we[_lp],ZALACZ,'LOK',,,_szer,,,,,MS.comment(ZALACZ,'LOK'));
      ZAOMEDA.win_ecol(_we[_lp]);
      ZAOMEDA.win_efld(_we[_lp],_TD,'ZAL',,,67,34,,,1)
   ?};
   exec('ok_esc','#window',ZAOMEDA,_we[_lp],,,,,,,exec('text_red_ok','#window'));
   ~~
!};

_cfg:=obj_new('TD','ws','we');
_cfg.TD:=_TD;
_cfg.ws:=_ws;
_cfg.we:=_we;
_cfg

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 87d6d1ffc2cb2f7c774552d2ad6d8eeedf6b6058ea8694fd51b77b6558ad1df5bffdd5862e9a00488738f303b30439f91b72936b489b035ae6ac30a1a8413dda58b3693f83ebf8908bbf34bbf15bdd00e193e9df3962194c6472a89d263a33f0ef88b2877689e16e236cd7ab685ec64f654d89ca0ecfc63b90f363a911a4b255

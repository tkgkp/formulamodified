:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prwy.fml
:: Utworzony: 03.06.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_DOS_PRWY - Rejestracja wykształcenia.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja wykształcenia - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE,   symbol=OSOBA,     type=_OSOBA,   name=Wskazanie osoby,   required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.OSOBA);
{? _uidref=''
|| _result:=exec('error','!pkd_dos_prwy')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_prwy',_in.OSOBA);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja wykształcenia - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj dane o wykształceniu: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj dane o wykształceniu: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj dane o wykształceniu: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj dane o wykształceniu'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie danych o wykształceniu niemożliwe.\nNie znaleziono osoby.'@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie osoby.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & OSOBA.seek(_a)
|| OS_SZKOL.cntx_psh();
   OS_SZKOL.index('DO');
   OS_SZKOL.prefix(OSOBA.ref());
   OS_SZKOL.index('PRIO');
   OS_SZKOL.prefix(OSOBA.ref());
   OS_SZKOL.index('OD');
   OS_SZKOL.prefix(OSOBA.ref());
   OS_SZKOL.win_sel(exec('wnd','!pkd_dos_prwy'));
   OS_SZKOL.win_edit('RED');
   _ret:=OS_SZKOL.select();
   OS_SZKOL.cntx_pop()
|| _ret:=exec('error','!pkd_dos_prwy')
?};
OSOBA.cntx_pop();
_ret


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przygotowanie okna wertowania ukończonych szkół.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized';

_wnd:=OS_SZKOL.grp_make('Ukończone szkoły'@,"mb_exec('GRP_EDISP_OSOBA_INFO')",'os_szkol_weri',,,,,_mode);
OS_SZKOL.grp_edit(_wnd,OSOBA,'INFO',,,,,,_mode);
OS_SZKOL.grp_splt(_wnd,,'horizontal','os_szkol');
OS_SZKOL.grp_sel(_wnd,,'WER',,,,,,,,,,_mode,,1);

OS_SZKOL.win_fml('WER',,'OD',,'ICON_BEFORE',"exec('x_zalacz_icon','zalacz')");

_wnd


\os_szkol_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',OS_SZKOL.uidref())


\os_szkol_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::  OLD: \mod_szk/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
OS_SZKOL.blank();
{? OS_SZKOL.edit("exec('os_szkol_ae','!pkd_dos_prwy')")
|| OS_SZKOL.add();
   EDIT_VAR.AKT_URL:=!OS_SZKOL
?}


\os_szkol_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::  OLD: \mod_szk/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
OS_SZKOL.get();
{? OS_SZKOL.edit("exec('os_szkol_ae','!pkd_dos_prwy')")
|| do();
   {? OS_SZKOL.PRIO=1 & ~exec('update_tyt','osoba','Z',OS_SZKOL.OSOBA,OS_SZKOL.TYTULZ,OS_SZKOL.DZTZ)
   || undo()
   ?};
   OS_SZKOL.put();
   {? end()
   || EDIT_VAR.AKT_URL:=!OS_SZKOL
   || FUN.emsg('Zmiana nie powiodła się.'@)
   ?}
?}


\os_szkol_ubg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Usuń - przed" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ask','#table')


\os_szkol_ub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń - przed" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::  OLD: \mod_szk/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_grp:=OS_SZKOL.sel_size();

{? _grp | exec('del_ask','#table')
|| do();
   {? OS_SZKOL.PRIO=1 & ~exec('update_tyt','osoba','Z',OS_SZKOL.OSOBA,null(),null())
   || undo()
   ?};
   OS_SZKOL.del();
   {? end()
   || EDIT_VAR.AKT_URL:=!OS_SZKOL
   |? ~_grp
   || FUN.emsg('Usunięcie nie powiodło się.'@)
   ?}
?}


\os_szkol_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
::  UTW: GS
:: OPIS: Obsługa akcji "Urlop" dla tabeli OS_SZKOL.
::       Dla osoby, która nie jest pracownikiem funkcja ma działanie puste.
::       Jeśli osoba występuje tylko raz w kartotece pracowników, to wywoływana jest funkcja symulująca wyliczenie
::       urlopu wypoczynkowego.
::       Dla osoby, która występuje kilka razy w tabeli pracowników przed przeprowadzeniem symulacji należy wskazać
::       etat.
::   WE:
::   WY:
::  OLD: \mod_szk/osoba.fml
::  OLD: \test_szk/kart_url.fml
::----------------------------------------------------------------------------------------------------------------------
_where:=
   'P.OSOBA=\'%1\' and P.DZA<=to_date(\'%2\') and (P.DZ is null or to_date(\'%3\')<=P.DZ) and P.FIRMA=\'%4\''
   [$OSOBA.ref(),date(,12,0)$1,date(,1,1)$1,$exec('ref_firma','ustawienia')];

P.cntx_psh();
P.prefix();
P.f_set(,'join F_ZATR','F_ZATR.KOD=\'%1\' and '[__F_ZATR.P]+_where);
{? _ok:=P.f_first()
|| _fsize:=P.f_size();
   P.f_clear();
   {? _fsize=1
   || exec('kart_url_show_os_szkol','kart_url')
   || _args:=exec('wybierz_args','pracownik');
      _args.DOMAIN:='PKD';
      _args.SQL_WHERE:=_where;
      _args.FML_TEST:=$"exec('kart_url_show_os_szkol','kart_url'); 0";
      _args.HDR_SEL:='Symulacja wymiaru urlopu';
      _args.GRP_IDENT:='urlop_prwy';
      _args.GRP_MODE:='normal';
      _args.WIELU:=0;
      exec('wybierz','pracownik',_args);
      _ret:=1
   ?}
?};
P.f_clear();
P.cntx_pop();

{? ~_ok
|| FUN.info('Określenie wymiaru urlopu wypoczynkowego nie jest możliwe.'@)
?};
~~


\os_szkol_mb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Domyślny" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::  OLD: \mod_szk/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
{? OS_SZKOL.PRIO=1
|| {? FUN.ask('Zmiana spowoduje wyłączenie prezentacji tytułu.\nKontynuować?'@)
   || do();
      OS_SZKOL.PRIO:=0;
      {? ~OS_SZKOL.put(1)
      || undo()
      |? ~exec('update_tyt','osoba','Z',OS_SZKOL.OSOBA,null(),null())
      || undo()
      ?};
      {? ~end()
      || FUN.emsg('Zmiana nie powiodła się.'@)
      ?}
   ?}
|| {? FUN.ask(
         'Zmiana spowoduje ustawienie tytułu jako prezentowanego przed nazwiskiem\n'
         'podczas prezentacji w wybranych zestawieniach i raportach.\n\n'
         'Dotychczasowa prezentacja zostanie anulowana.\n\n'
         'Kontynuować?'@
      )
   || do();
      OS_SZKOL.cntx_psh();
      OS_SZKOL.index('PRIO');
      OS_SZKOL.prefix(OS_SZKOL.OSOBA,1);
      {? OS_SZKOL.first()
      || OS_SZKOL.prefix(OS_SZKOL.OSOBA);
         OS_SZKOL.PRIO:=0;
         {? ~OS_SZKOL.put(1)
         || undo()
         ?}
      ?};
      OS_SZKOL.cntx_pop();
      OS_SZKOL.get();
      OS_SZKOL.PRIO:=1;
      {? ~OS_SZKOL.put(1)
      || undo()
      ?};
      {? ~exec('update_tyt','osoba','Z',OS_SZKOL.OSOBA,OS_SZKOL.TYTULZ,OS_SZKOL.DZTZ)
      || undo()
      ?};
      {? ~end()
      || FUN.emsg('Zmiana nie powiodła się.'@)
      ?}
   ?}
?}


\os_szkol_lb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Legenda" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','OS_SZKOL#01')


\os_szkol_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed" dla tabeli OS_SZKOL.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? OS_SZKOL.POMIN='T'
|| Color.fnd_kol('OS_SZKOL#01#01')
?}


\os_szkol_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::  OLD: \spr_szk/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=__CHK.record(OS_SZKOL,,'OD','DO','RODZAJ','SZKOLA','WYK'))<>''
|| return(_chk)
?};

{? OS_SZKOL.DO<OS_SZKOL.OD
|| FUN.emsg('Data "%1" nie może być wcześniejsza niż data "%2".'@ [MS.name(OS_SZKOL,'DO'),MS.name(OS_SZKOL,'OD')]);
   return('DO')
?};

{? OS_SZKOL.KOSZT<0
|| FUN.emsg('Pole "%1" nie może być ujemne.'@ [MS.name(OS_SZKOL,'KOSZT')]);
   return('KOSZT')
?};

''


\os_szkol_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Okienko przed" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.AKT_URL:=''


\os_szkol_ao
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Okienko po" dla tabeli OS_SZKOL.
::   WE:
::   WY:
::  OLD: \szkolokp/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDIT_VAR.AKT_URL='' | REF.OSOBA=null()
|| return()
?};

exec('akt_wyk','osoba');
exec('aktualizuj','kart_url',REF.OSOBA);
EDIT_VAR.AKT_URL:=''


\os_szkol_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli OS_SZKOL z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli OS_SZKOL jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=OS_SZKOL
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\os_szkol_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.42]
:: OPIS: Obsługa akcji "Załączniki - przed" w oknach tabeli OS_SZKOL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','OS_SZKOL',exec('chk_role','#b__box',OPERATOR.USER,'PKD_DOS_PRWY'))

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 754e13312039f2bcce6b8e5469f5f2431ddedc90ad25c6d3f54c67646e5b505f5fb57c440f603cf47b74b0b19760b64a37ec01e23c1a22aecad8d459118bd84c47ce3a52ee2ade51f2682aa04b263154851ef9724872eab0d264386a0fa81c6841cbfe10e50d73dbf58710d5d7d3c1b49638752774b831936c91852a590ca0db

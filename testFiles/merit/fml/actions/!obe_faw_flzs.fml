:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_faw_flzs.fml
:: Utworzony: 14.12.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_FLZS - Tworzenie wniosku w obiegu na podstawie zamówienia sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Tworzenie wniosku w obiegu na podstawie zamówienia sprzedaży - główna formuła czynności OBE_FAW_FLZS
::----------------------------------------------------------------------------------------------------------------------
:: PARAMETRY WE:
::# kind=WE, symbol=ZK_N, type=_ZK_N, name=Wskazanie na nagłówek zamówienia sprzedaży, required=N, keyref=T
::# kind=WE, symbol=ETYPY, type=STRING, name=Wskazanie na typ obiegu, required=N, fml_val="exec('typ_obi','obiegi_log','M',_a)", fml_exp="exec('etypy_fml_exp','obiegi2',2,_a)"
::# kind=WE, symbol=ODD, type=STRING, name=Domyślna jednostka księgowa, required=N, fml_val="exec('select_odd','obiegi_log',_a)", fml_exp="exec('odd_fml_exp','obiegi_log',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=ZK_N, type=_ZK_N, name=Wskazanie na nagłówek zamówienia sprzedaży, required=N, keyref=T

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_service:=_mp.isService();
_auto:=_mp.isAutoRun();
{? ~exec('is_act','#b__box','OBE_FAW_FLZS')
|| {? ~_service
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
   ?};
   _mp.cancel(); return(~~)
|? _mp.isMicro()
|| {? ~_service
   || {? _akcja='Dołącz' | _mp.pathProc()
      || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
      || FUN.info('Wniosek nie jest w trakcie rejestracji.'@)
      ?}
   ?};
   return(~~)
?};

_dalej:=1;
{? _service & (_we.ZK_N=~~ | _we.ZK_N=null | _we.ETYPY=~~ | _we.ETYPY='')
|| _wy.EDOKUM:=_wy.ZK_N:=null;
   _mp.save(,_wy);
   _mp.done();
   _dalej:=0
?};
_zk_n:=_etypy:=null;
{? _dalej=1
|| {? ~_service
   || {? _akcja='OBSZAR'
      || params_get().context.ZK_N; _zk_n:=ZK_N.ref()
      |? _we.ZK_N=~~ | _we.ZK_N=null
      || _zk_n:=exec('zk_n_wybor','obiegi_log','S');
         {? _zk_n=null || _dalej:=-1 ?}
      || _zk_n:=_we.ZK_N
      ?}
   || _zk_n:=_we.ZK_N
   ?};
   {? _zk_n
   || _wy.ZK_N:=_zk_n; _mp.save(,_wy); _mp.descTodo()
   ?}
?};
{? _dalej=1
|| {? ~_service
   || ETYPY.cntx_psh(); ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',2));
      {? _we.ETYPY=~~ | _we.ETYPY='' | ~ETYPY.find_key(_we.ETYPY)
      || ETYPY.hdr_sel('Typy wniosków w obiegu'@); ETYPY.win_edit('REDW'); ETYPY.win_sel('SLO_WYB');
         {? ETYPY.select() || _etypy:=ETYPY.ref() ?}
      |? ETYPY.find_key(_we.ETYPY)
      || _etypy:=ETYPY.ref()
      ?};
      {? ~_etypy || _dalej:=-1 ?};
      ETYPY.cntx_pop()
   || ETYPY.cntx_psh(); ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',2));
      {? ETYPY.find_key(_we.ETYPY)
      || _etypy:=ETYPY.ref()
      || _dalej:=0
      ?};
      ETYPY.cntx_pop()
   ?}
?};
{? _dalej=1
|| _ref:=BB.sqlint($_zk_n); _nam:=form(($_zk_n)-8);
   {? _ref<>0 & _nam<>''
   || ZK_N.cntx_psh(); ZK_N.use(_nam); ZK_N.prefix();
      ZK_P.cntx_psh(); ZK_P.use('zkpoz'+(ZK_N.name()+3));
      {? ZK_N.seek(_ref,_nam)
      || {? ZK_N.OBI_POW<>''
         || {? ~_service
            || FUN.info('Dla zamówienia sprzedaży został wcześniej uruchomiony obieg.'@)
            || _wy.EDOKUM:=_wy.ZK_N:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         |? ZK_N.STAT_REJ='T' | ZK_N.STAT_REJ='Z'
         || {? _akcja='OBSZAR' | _mp.pathProc() | _mp.pathTodo() | _mp.isAutoRun()
            || menu_txt(,'Dołącz');
               exec('zk_n_win_edit_set','obiegi_log',1,'S');
               {? _service
               || BtnObi:=1
               || BtnObi:=0;
                  ZK_N.display()
               ?};
               {? BtnObi
               || _edokum:=params_exec('zk_n_edokum','obiegi_log',_etypy,~_service,'S');
                  {? _edokum
                  || _wy.EDOKUM:=_edokum;
                     _wy.ZK_N:=ZK_N.ref();
                     _mp.save(,_wy);
                     _mp.done()
                  ?}
               ?}
            ?}
         || {? ZK_N.STAT_REJ='A'
            || {? ~_service || FUN.info('Zamówienie sprzedaży zostało anulowane.'@) ?};
               _wy.EDOKUM:=_wy.ZK_N:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      || _wy.EDOKUM:=_wy.ZK_N:=null;
         _mp.save(,_wy);
         _mp.done()
      ?};
      ZK_N.cntx_pop(); ZK_P.cntx_pop()
   || _wy.EDOKUM:=_wy.ZK_N:=null;
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _dalej=0
|| _wy.EDOKUM:=_wy.ZK_N:=null;
   _mp.save(,_wy);
   _mp.done()
|? _dalej=-1
|| _mp.cancel()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia sprzedaży: %1'@@[exec('record','#to_string',_in.ZK_N)]
|? var_pres('ZK_N',_out)=type_of(null()) & _out.ZK_N
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia sprzedaży: %1'@@[exec('record','#to_string',_out.ZK_N)]
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia sprzedaży'@@
?}


\obieg_obszarzs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Uruchomienie obiegu dokumentu z obszaru
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='OBE_FAW_FLZS';
_params.AKCJA:='OBSZAR';
_params.PROC_START:='T';
_params.UIDREF:=ZK_N.uidref();
_params.CONTEXT:=obj_new('ZK_N');
_params.CONTEXT.ZK_N:=ZK_N.ref();
exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 94d5cdea8850d9de09b49feafcf91389de1a2533f20b9edcd383bf85b2d4c1433708bcd86b32f2e7ceb367946067d905140603bbb8a7c134acbe5ba3ceedad9fbe84ca4ebdb20531d4568492ef71cf52861faeef684eb4ff0b0e8f747e1aebda252edd3a7d78fbf8511d80614cccfc600a56cfe63ec2213e3b94ba5e4f6af778

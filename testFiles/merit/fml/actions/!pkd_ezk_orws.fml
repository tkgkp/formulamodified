:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orws.fml
:: Utworzony: 23.07.2021
:: Autor: JAKOLTUN
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORWS - Rej. współczynników.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Rej. współczynników - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.P);
{? _uidref=''
|| _result:=exec('error','!pkd_ezk_orws')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orws',_in.P,_in.O);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a [REFERENCE] - Wskazanie pracownika.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a)
|| OSOBA.cntx_psh();
   P.OSOBA();
   VAR.O();
   WYN_WSP.cntx_psh();
   WYN_WSP.index('WYN_WSP');
   WYN_WSP.prefix(P.ref());
   WYN_WSP.win_sel('WER');
   WYN_WSP.win_edit('RED');
   _ret:=WYN_WSP.select();
   WYN_WSP.cntx_pop();
   OSOBA.cntx_pop()
|| _ret:=exec('error','!pkd_ezk_orws')
?};
P.cntx_pop();
_ret


\wyn_wsp_ae
{? WYN_WSP.DO<>date(0,0,0) & WYN_WSP.DO<WYN_WSP.OD
|| FUN.emsg('Data "Od dnia" musi być wcześniejsza od daty "Do dnia".');
   'OD'
|| ''
?}

\wyn_wsp_okres_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Kontrola poprawności wprowadzonego okresu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? WYN_WSP.DO<>date(0,0,0) & WYN_WSP.DO<WYN_WSP.OD
|| FUN.emsg('Data "Od dnia" musi być wcześniejsza od daty "Do dnia".')
?};

1


\wyn_wsp_done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli WYN_WSP z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli WYN_WSP jako składowa okna grupowego tabeli UD_DEF).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=WYN_WSP
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie współczynników do wynagrodzenia niemożliwe.\nNie znaleziono pracownika.'


\wyn_wsp_okno_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Obsługa "przed" okienka WER tabeli WYN_WSP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
R.win_dict('SLO_LS');
R.f_set('RN',,'RK=\'S\'');
1


\wyn_wsp_okno_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Obsługa "po" okienka WER tabeli WYN_WSP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.cntx_pop();
R.f_clear();
1

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:59 d9802182b10e0108cc0e46de922084f4a3acff0166a2f6047fe36ccfbdd81886657cad45dceb0c789b00eba009804870edbc630b4d8d61b837cc0e7d13ca052821bc2671ecc91ba4c16ad2ef6742a2b249c4dfdcf748150cf796c0837268211245b9992a053d2f7e839c29d2c0dafee72bcf7350252d02c773622b61ed3b4f2a

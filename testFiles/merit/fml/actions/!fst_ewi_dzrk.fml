:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_zkla.fml
:: Utworzony: 15.12.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DZRK - Rej. klasyfikacji dod. środków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Rej. klasyfikacji dod. środków - główna formuła czynności FST_EWI_DZRK.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\tst_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wybór środków dołączanych do bieżącego typu bieżącej klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
_class:='';
{? TSK.TREE=0
|| _class:=TSK.NAZ;
   TSK.cntx_psh();
   TSK.index('MAIN');
   TSK.prefix(TSK.ref());
   _title:='Wybierz typ z bieżącej klasyfikacji (%1)'@[_class];
   _win:=TSK.mk_sel(_title,,0,'_tsk_wyb',20,8,12);
   TSK.win_fld(_win,TSK,'NAZ',,,20);
   TSK.win_fld(_win,TSK,'OPIS',,,25);
   TSK.win_act(_win,,'Formuła','Wybierz'@@,,'Wybierz typ z bieżącej klasyfikacji'@,,"sel_exit()",1,,,,'W');
   TSK.win_btn(_win,'text=%1, panel=bottom, align=end'['Wybierz'@],'menu:W');
   _anuluj:=TSK.win_btn(_win,'text=%1, panel=bottom, align=end'['Anuluj'@],'key:Esc');
   TSK.btn_opt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   TSK.win_sel(_win);
   {? ~TSK.select() || _dalej:=0 ?}
?};
{? _dalej
|| {? TSK.r_lock(0,1,1)
   || SRSR.cntx_psh();
      _sel:=SRSR.win_sel('?');
      {? OPERATOR.DEPT=null
      || SRSR.index('NRI');
         SRSR.prefix('T')
      || SRSR.index('NRI_O');
         SRSR.prefix(OPERATOR.DEPT,'T')
      ?};
      _win:=SRSR.mk_sel('Dołączanie środków do typu: %1'@[TSK.NAZ],'P',,'_srsr_wyb',8,8,20,,'U');
      SRSR.win_fld(_win,SRSR,'NRI',,,20,,,'Nr inwentarzowy'@);
      SRSR.win_fld(_win,SRSR,'NST',,,30,,,'Nazwa'@);
      SRSR.win_fld(_win,SRSR,'DZ',,,10,,,'Data zakupu'@);
      SRSR.win_fld(_win,SRSR,'DE',,,10,,,'Data eksploatacji'@);
      SRSR.win_fld(_win,SRSR,'Z',,,10,,,'Zlikwidowany?'@,,,2,,"'T'","'N'");
      SRSR.win_act(_win,,'Formuła','Dołącz do typu'@@,,,"","exec('add','!fst_ewi_dzrk')",1,1,
                   "exec('grp_before','!fst_ewi_dzrk')","exec('grp_after','!fst_ewi_dzrk')",'D');
      SRSR.win_act(_win,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,,"exec('legenda','color','SRSR#01')",,,,,'L');
      SRSR.win_act(_win,,'Wyświetl',,,,,"");
      SRSR.win_act(_win,,'Rekord',,,,"Color.rekprzed('SRSR#01#01')","");
      _zamknij:=SRSR.win_btn(_win,'text=%1, panel=bottom, align=end'['Zamknij'@],'key:Esc');
      SRSR.btn_opt(_zamknij,'tooltip=Zapisz zmiany i zamknij okno');
      SRSR.win_sel(_win);
      SRSR.select();
      SRSR.cntx_pop();
      SRSR.win_sel(_sel);
      TSK.r_unlock()
   || FUN.emsg('Wystąpił problem z obsługą dołączania środków do wskazanego typu.'@)
   ?}
?};
{? _class<>'' || TSK.cntx_pop() ?}


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie środka do typu
::----------------------------------------------------------------------------------------------------------------------
{? ~SRSR.get()
|| _tekst:='Środek %1 nie jest już dostępny.'@[SRSR.NRI];
   {? SRSR.sel_size()>0 || KOMM.add(_tekst) || FUN.emsg(_tekst) ?};
   return(0)
?};
TST.cntx_psh();
_find:=exec('test_dzrk','fst',SRSR.ref(),TSK.ref());
{? _find<>''
|| _tekst:='Środek %1 jest już przypisany do bieżącej klasyfikacji (typ: %2).'@[SRSR.NRI,_find];
   {? SRSR.sel_size()>0 || KOMM.add(_tekst) || FUN.emsg(_tekst) ?}
|| TSR.index('TYPK'); TSR.prefix();
   TST.index('SRST'); TST.prefix();
   TSR.SRSR:=SRSR.ref();
   TSR.TYP:=TSK.ref();
   {? TSR.add()
   || _widoczny:=0;
      SRST.cntx_psh();
      SRST.index('SROD');
      SRST.prefix(SRSR.ref());
      {? SRST.first()
      || {! |?
            {? ~TST.find_key(SRST.ref(),TSK.ref())
            || TST.SRST:=SRST.ref();
               TST.TSK:=TSK.ref();
               TST.CLASS:=TST.TSK().TREE;
               TST.ODD:=TST.SRST().ODD;
               TST.JORG:=TST.SRST().JORG;
               TST.OSOBA:=TST.SRST().OSOBA;
               TST.POM:=TST.SRST().POM;
               TST.GR:=TST.SRST().GR;
               TST.add()
            ?};
            {? TST.SRST().OKRO_F=SSTALE.AO || _widoczny:=1 ?};
            SRST.next()
         !}
      ?};
      SRST.cntx_pop();
      {? ~_widoczny
      || _tekst:='Środek: %1 dodany do typu: %2 nie będzie widoczny dla bieżących parametrów pracy.'@[SRSR.NRI,TSK.NAZ];
         {? SRSR.sel_size()>0 || KOMM.add(_tekst) || FUN.emsg(_tekst) ?}
      ?}
   || _tekst:='Błąd podczas próby dodania środka %1 do typu (%2).'@[SRSR.NRI,TSK.NAZ];
      {? SRSR.sel_size()>0 || KOMM.add(_tekst) || FUN.emsg(_tekst) ?}
   ?}
?};
TST.cntx_pop()


\grp_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Grupa przed, w oknie z listą środków podczas dodawania do typów klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
KOMM.init(100,,'Uwagi dotyczące przypisywania środków do typów w klasyfikacjach.'@)


\grp_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Grupa po, w oknie z listą środków podczas dodawania do typów klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
{? ~KOMM.empty() || KOMM.select() ?}


\gb_tst_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie przypisań do typu - grupa przed
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć przypisanie zaznaczonych środków (%1) do typu %2?'@[$TST.sel_size(),TST.TSK().NAZ])
|| KOMM.init(100,,'Uwagi dotyczące przypisywania środków do typów w klasyfikacjach.'@);
   1
|| 0
?}


\tst_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła usuwa przypisanie wskazanego środka do typu
::----------------------------------------------------------------------------------------------------------------------
{? TST.sel_size()=0
|| _ask:=FUN.ask('Usunąć przypisanie środka %1 (%2)\n'
                 'do typu %3?'@[TST.SRST().NRI,TST.SRST().NST,TST.TSK().NAZ])
|| _ask:=1
?};
{? _ask
|| TST.cntx_psh(); TSR.cntx_psh(); TSK.cntx_psh();
   do();
      _ref:=TST.SRST().SRSR;
      _typ:=TST.TSK;
      _tree:=TST.TSK().TREE;
      TST.index('TST');
      TST.prefix(_typ,_ref);
      {? TST.first()
      || {! |? TST.del() !}
      ?};
      {? TST.size()=0
      || TSR.index('TYPK');
         TSR.prefix(_ref,_typ);
         {? TSR.first() || TSR.del() ?}
      ?};
      {? do_state()=1 || _sukces:=1 || _sukces:=0 ?};
   end();
   TST.cntx_pop(); TSR.cntx_pop(); TSK.cntx_pop();
   {? ~_sukces
   || _tekst:='Błąd podczas usuwania środka: %1 (%2)z typu: %3.'@[TST.SRST().NRI,TST.SRST().NST,TSK.NAZ];
      {? TST.sel_size()>0 || KOMM.add(_tekst) || FUN.emsg(_tekst) ?}
   ?}
?}


\ga_tst_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie przypisań do typu - grupa po
::----------------------------------------------------------------------------------------------------------------------
{? ~KOMM.empty() || KOMM.select() ?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 58cab8f081103c640a1317e9da8efb00e4f9a889ae5387ba22e6ec36d926126c968e1d7a0722996eced0c5d71f2f9f88b080f6cda941a83a787338a3fc82bf0a0227a75706e74d2ba42017249ecc731ddfd17e7aa082a51314859882bf9e11961bdc40568a40538132e6b386e60001e464b1c5fc588171c7c814e6126aebfe5b

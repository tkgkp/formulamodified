:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_pzl_dzlp.fml
:: Utworzony: 13.07.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_PZL_DZLP - Rejestracja zlecenia produkcyjnego złożonego z półfabrykatami
::            Uwaga: większość kodu jest wspólna z czynnościami:
::                   - TTE_PZL_DWAR
::                   - TTE_PZL_DZLE
::                   - TTE_PZL_DZLN
::            w przypadku zmian modyfikować wszystkie pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła główna czynności rejestracji zlecenia produkcyjnego złożonego z półfabrykatami (TTE_PZL_DZLP)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

::# permissions=ODDZ,LMG
::# properties=SERVICE
::# parses=exec('parses','!tte_pzl_dzlp')

:: PARAMETRY WE:
::# kind=WE, symbol=ZTP, type=_ZTP, name=Typ zlecenia, required=N, fml_val="exec('ztp_select','zl_head','P',0)", fml_exp="exec('ztp_export','zl_head',_a)"
{? var_pres('ZTP',_in)<>type_of(~~) & var_pres('ZTP',_in)<>type_of(null()) || return() ?};
{? var_pres('ZTP',_in)=type_of(~~) || _in.ZTP:=null() ?};
::# kind=WE, symbol=ZL, type=_ZL, name=Wskazanie na zlecenie, required=N, keyref=T
{? var_pres('ZL',_in)<>type_of(~~) & var_pres('ZL',_in)<>type_of(null()) || return() ?};
::# kind=WE, symbol=TKTL, type=_TKTL, name=Technologia zlecenia, required=N
{? var_pres('TKTL',_in)<>type_of(~~) & var_pres('TKTL',_in)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_in)=type_of(~~) || _in.TKTL:=null() ?};
::# kind=WE, symbol=KTM, type=_M, name=Produkt zlecenia, required=N
{? var_pres('KTM',_in)<>type_of(~~) & var_pres('KTM',_in)<>type_of(null()) || return() ?};
{? var_pres('KTM',_in)=type_of(~~) || _in.KTM:=null() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=ZL, type=_ZL, name=Wskazanie na zlecenie, required=N
{? var_pres('ZL',_out)<>type_of(~~) & var_pres('ZL',_out)<>type_of(null()) || return() ?};
::# kind=WY, symbol=TECH, type=STRING, name=Redagowanie technologii zlecenia, required=N
{? var_pres('TECH',_out)<>type_of('') || _out.TECH:='N' ?};
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik działania (OK, BŁĄD)", required=N

_clean_result:=params_exec('clean','!tte_pzl_dzlp',_mp,_in);
{? ~_clean_result.RESULT
|| return()
?};

:: Sprawdzenie typu zlecenia
{? _in.ZTP
|| {? exec('FindAndGet','#table',ZTP,_in.ZTP,,"1+TYP",'')='~'
   || _msg:='Niezgodność wywołania czynności.\nPrzekazany typ nie może być zastrzeżony.';
      {? ~_mp.isService()
      || FUN.emsg(_msg)
      ?};
      _mp.error(_msg);
      return()
   |? exec('FindAndGet','#table',ZTP,_in.ZTP,,"WP",'')<>'P'
   || _msg:='Niezgodność wywołania czynności.\nPrzekazany typ musi dotyczyć zleceń produkcyjnych.';
      FUN.emsg(_msg);
      _mp.error(_msg);
      return()
   ?}
?};

:: Sprawdzenie zlecenia i ustawienie parametru wyjściowego
{? var_pres('ZL',_in)=type_of(null())
|| {? _in.ZL
   || {? exec('FindAndGet','#table',ZL,_in.ZL,,"RODZAJ",'')<>'Z'
      || _msg:='Niezgodność wywołania czynności.\nPrzekazane zlecenie musi być zleceniem złożonym z półfabrykatami.';
         {? ~_mp.isService()
         || FUN.emsg(_msg)
         ?};
         _mp.error(_msg);
         return()
      || _mp.save(exec('kind_out','#b_port'),'ZL',_in.ZL)
      ?}
   || _msg:='Niezgodność wywołania czynności.\nPrzekazany parametr ZL nie może mieć wartości null().';
      {? ~_mp.isService()
      || FUN.emsg(_msg)
      ?};
      _mp.error(_msg);
      return()
   ?}
?};

:: Sprawdzenie produktu
{? _in.KTM
|| _err:=exec('chk_ktm4zl','zl_common',_in.KTM,0,'Z');
   {? _err
   || {? _err=1
      || _msg:='Niezgodność wywołania czynności.\nNie udało się odnaleźć przekazanego materiału.'@
      |? _err=2
      || _msg:='Niezgodność wywołania czynności.\nPrzekazany indeks materiałowy jest nieaktywny.'@
      |? _err=3
      || _msg:='Niezgodność wywołania czynności.\nPrzekazany indeks materiałowy nie jest produktem.'@
      |? _err=4
      || _msg:=
            'Niezgodność wywołania czynności.\n'
            'Przekazany indeks materiałowy nie posiada zdefiniowanej aktywnej technologii.'@
      || _msg:='Niezgodność wywołania czynności.'@
      ?};
      {? ~_mp.isService()
      || FUN.emsg(_msg)
      ?};
      _mp.error(_msg);
      return()
   ?}
?};

:: Sprawdzenie technologii
{? _in.TKTL
|| _err:=exec('chk_tktl4zl','zl_common',_in.TKTL,0);
   {? _err
   || {? _err=1
      || _msg:='Niezgodność wywołania czynności.\nNie udało się odnaleźć przekazanej technologii.'@
      |? _err=2
      || _msg:='Niezgodność wywołania czynności.\nPrzekazana technologia jest niezaakceptowana.'@
      |? _err=3
      || _msg:='Niezgodność wywołania czynności.\nPrzekazana technologia jest archiwalna.'@
      |? _err=4
      || _msg:='Niezgodność wywołania czynności.\nPrzekazana technologia jest nieaktualna.'@
      || _msg:='Niezgodność wywołania czynności.'@
      ?};
      {? ~_mp.isService()
      || FUN.emsg(_msg)
      ?};
      _mp.error(_msg);
      return()
   ?}
?};

:: Sprawdzenie zgodności technologi z produktem zlecenia
{? _in.TKTL & _in.KTM
|| {? ~exec('chk_ktm4tktlw','tech_common',_in.TKTL,_in.KTM)
   ||
::    Brak produktu w danej technologii, czyszcenie wartości
      _msg:=
         'Niezgodność wywołania czynności.\n'
         'Przekazany indeks materiałowy musi być zdefiniowany jako produkt przekazanej technologii'@;
      {? ~_mp.isService()
      || FUN.emsg(_msg)
      ?};
      _mp.error(_msg);
      return()
   ?}
?};

_mp.trigRef('ZL',1,1,1,exec('kind_out','#b_port'),'ZL');

_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)
|| ZL.cntx_psh();
   ZL.clear();
   {? ZL.seek(_keyRefs[1])
   || _zl:=ZL.ref();
      _zl_uid:=ZL.uidref()
   || _zl:=null();
      _zl_uid:=''
   ?};
   ZL.cntx_pop()
|? _mp.akcja()='KOPIUJ' | _mp.akcja()='POPRAW' | _mp.akcja()='USUŃ' | _mp.akcja()='ZAKOŃCZ'
|| _zl:=_context.ZL;
   _zl_uid:=exec('FindAndGet','#table',ZL,_context.ZL,,"uidref()",'')
|| _zl:=null();
   _zl_uid:=''
?};

{? _mp.isService() & _zl=null()
|| _msg:='Błędna konfiguracja procesu — nie przekazano zlecenia.'@;
   _mp.error(_msg);
   return()
?};

_result:=~~;

:: Wywołanie automatyczne, jest odpowiedni KeyRef, więc bezwarunkowe zakończenie redagowania nagłówka
:: (chyba, że data realizacji jest zaległa)
{? (_mp.isAutoRun() | _mp.isService()) & _zl<>null()
|| ZL.cntx_psh();
   ZL.clear();
   {? ZL.seek(_zl)
   || {? ZL.DTR=date(0,0,0) | ZL.DTR>=date()
      || ZL.STAT_N:='T';
         {? ZL.put()
         || _mp.save(exec('kind_out','#b_port'),'TECH',_out.TECH);
            _mp.save(exec('kind_out','#b_port'),'RESULT','OK');
            _mp.done()
         ?}
      || {? _mp.isService()
         || _mp.save(exec('kind_out','#b_port'),'RESULT','BŁĄD');
            _mp.done()
         || _mp.cancel()
         ?}
      ?}
   || {? _mp.isService()
      || _mp.save(exec('kind_out','#b_port'),'RESULT','BŁĄD');
         _mp.done()
      || _mp.cancel()
      ?}
   ?};
   ZL.cntx_pop()

:: Wywołanie z listy ToDo, jest odpowiedni KeyRef, więc kontynuacja redagowania nagłówka
|? _mp.pathTodo() & _zl<>null()
|| {? exec('FindAndGet','#table',ZL,_zl,,"STAT_N",'N')='T'
   || FUN.info('Zakończono rejestrację nagłówka zlecenia.'@);
      _mp.save(exec('kind_out','#b_port'),'TECH',_out.TECH);
      _mp.save(exec('kind_out','#b_port'),'RESULT','OK');
      _mp.done()
   || _ok:=exec('zl_popraw','zl_head',_zl,1);
      {? _mp.isMicro()
      || _mp.cancel()
      |? _ok
      || {? exec('FindAndGet','#table',ZL,_zl,,"STAT_N",'N')='T'
         || _mp.save(exec('kind_out','#b_port'),'TECH',_out.TECH);
            _mp.save(exec('kind_out','#b_port'),'RESULT','OK');
            _mp.done()
         ?}
      ?}
   ?}

:: Poprawianie zlecenia (obszar roboczy - zlecenie przekazane kontekstem)
|? _mp.akcja()='POPRAW'
|| _ok:=exec('zl_popraw','zl_head',_zl,exec('FindAndGet','#table',ZL,_zl,,"STAT_N",'N')='N');
   {? _mp.isMicro()
   || _mp.cancel()
   |? _ok
   || {? exec('FindAndGet','#table',ZL,_zl,,"STAT_N",'N')='T'
      || _mp.save(exec('kind_out','#b_port'),'TECH',_out.TECH);
         _mp.save(exec('kind_out','#b_port'),'RESULT','OK');
         _mp.done()
      ?}
   ?}

:: Wywołanie z panelu czynności - nowy proces, więc nowe zlecenie
:: Wywołanie z listy ToDo, nie ma KeyRef, więc nowe zlecenia
:: Dołączanie nowego zlecenia (obszar roboczy)
|? _mp.pathProc() | _mp.pathTodo() | _mp.akcja()='DOŁĄCZ'
|| _zl:=exec('zl_dolacz','zl_head','P',_in.ZTP,'Z',,_in.TKTL,_in.KTM);
   {? _mp.isMicro()
   || _mp.cancel()
   |? _zl<>null()
   || {? (_in.KTM<>null() | _in.TKTL<>null()) & ~_mp.isAutoRun()
      || FUN.info('Wygenerowano zlecenie produkcyjne o symbolu %1.'@[exec('FindAndGet','#table',ZL,_zl,,"SYM",'')])
      ?};
      {? exec('FindAndGet','#table',ZL,_zl,,"STAT_N",'N')='T'
      || _mp.save(exec('kind_out','#b_port'),'TECH',_out.TECH);
         _mp.save(exec('kind_out','#b_port'),'RESULT','OK');
         _mp.done()
      ?}
   || _mp.cancel()
   ?}

:: Usuwanie zlecenia (obszar roboczy - zlecenie przekazane kontekstem)
|? _mp.akcja()='USUŃ'
|| _context.RESULT:=exec('zl_usun','zl_head',_zl,_mp.isGroup());
   {? _context.RESULT
   || _mp.cancel()
   ?}

:: Kopiowanie na nowe zlecenie (obszar roboczy - źródłowe zlecenie przekazane kontekstem)
|? _mp.akcja()='KOPIUJ'
|| {? _in.ZTP<>null() & _in.ZTP<>exec('FindAndGet','#table',ZL,_zl,,"TYP",null())
   || FUN.info('Nie można uruchomić procesu — niezgodność typu zlecenia.'@);
      _mp.cancel()
   || _zl:=exec('zl_kopiuj','zl_head',_zl);
      {? _mp.isMicro()
      || _mp.cancel()
      |? _zl<>null()
      || {? exec('FindAndGet','#table',ZL,_zl,,"STAT_N",'N')='T'
         || _mp.save(exec('kind_out','#b_port'),'TECH',_out.TECH);
            _mp.save(exec('kind_out','#b_port'),'RESULT','OK');
            _mp.done()
         ?}
      || _mp.cancel()
      ?}
   ?}

:: Zakończenie redagowania nagłówka (obszar roboczy - zlecenie przekazane kontekstem)
|? _mp.akcja()='ZAKOŃCZ'
|| ZL.cntx_psh();
   ZL.clear();
   {? ZL.seek(_zl)
   || {? {? _mp.isGroup()
         || 1
         || FUN.ask('Czy zakończyć rejestrację nagłówka zlecenia?'@)
         ?}
      || _ok:=1;

         {? ZL.KTL().ARCH<>'N'
         || _msg:='Dla zlecenia %1 przypisana jest archiwalna technologia'@[ZL.SYM];
            FUN.info(_msg);
            _ok:=0
         ?};
         {? ~exec('tktl_act','tech_head',1)
         || _msg:='Dla zlecenia %1 przypisana jest nieaktualna technologia'@[ZL.SYM];
            FUN.info(_msg);
            _ok:=0
         ?};

         {? _ok
         || ZL.STAT_N:='T';
            {? ZL.put()
            || {? _mp.isGroup() || KOMM.add('Zakończono rejestrację nagłówka zlecenia %1.'@[ZL.SYM]) ?};
               _mp.save(exec('kind_out','#b_port'),'TECH',_out.TECH);
               _mp.save(exec('kind_out','#b_port'),'RESULT','OK');
               _mp.done()
            ?}
         || _mp.cancel()
         ?}
      ?}
   || _mp.cancel()
   ?};
   ZL.cntx_pop()

:: Tutaj nie powinno dojść, więc błąd
|| _mp.error('Nieobsłużony kontekst wywołania czynności.')
?};
_result


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności rejestracji nagłówka zlecenia produkcyjnego złożonego z półfabrykatami (TTE_PZL_DZLP)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();

:: jest rekord kluczowy to ustawiam odpowiednie ZL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',ZL,_keyRefs[1],,"SYM",'');
   _desc:={? _tmp<>'' || 'Zredaguj zlecenie produkcyjne złożone z półfabrykatami %1'@@[_tmp] || '' ?}
::|| _desc:=exec('FindAndGet','#table',ZL,_keyRefs[1],,"'Zredaguj zlecenie produkcyjne złożone z półfabrykatami %1'[SYM]",'')
|| _desc:='Zredaguj nowe zlecenie produkcyjne złożone z półfabrykatami'@@
?};
_desc


\action_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja nagłówka zlecenia produkcyjnego złożonego z półfabrykatami
::       - akcja Dołącz -> Złożone z półfabrykatami
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_PZL_DZLP';
_args.AKCJA:='DOŁĄCZ';
_args.PROC_START:='T';

exec('mp_run','#b__box',_args);
~~


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zl_clean','zl_common',_mp,_in,'ERROR')


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Formuła ustalająca PARSES
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
params_exec('zl_parses','zl_common')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 da7b955f5c4dd21d6d6e888b61ba13b6f0a0fa92d3b442969e16aa399273693d8f5f3e981b516be762d991c79a2c2d90acc3d3de0722f55da809d2a56315269cf471584f9db6546380e0763a2dd07ed85822cdaf2fb005fb936e1a7192fa4c0f475dd8eaca2ff33aa4f708e655b811ac9131660e749b78368ea4574d252f403b

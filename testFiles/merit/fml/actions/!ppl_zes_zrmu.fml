:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zes_zrmu.fml
:: Utworzony: 22.06.2016
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PPL_ZES_ZRMU - Załączniki RMUA
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Generuje załączniki dokumentów RMUA
::   WE:
::   WY:
::  OLD: \gen_rmua/gen_zal.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
O.cntx_psh();
O.clear();
{? O.seek(__PARSES.getVal('ListaPłac').REF)
|| exec('wybierz','lista_plac',O.ref());
   {? exec('otwarte_ubezp','lista_plac',O.RU,O.MU)
   || FUN.emsg('W miesiącu %1 rozliczenia z ZUS występują otwarte listy płac.'@[date(O.RU,O.MU,)$8]);
      O.cntx_pop();
      return()
   ?}
|| FUN.emsg('Lista płac nie została określona.'@);
   O.cntx_pop();
   return()
?};

{? FUN.ask('Uruchomienie tej funkcji spowoduje wygenerowanie dokumentów RMUA.'@+'\n'+'Czy kontynuować?'@)
|| _prac:=exec('wybierz_parses','pracownik','PPL');
   {? _prac.P.first()
   || _os_tab:=tab_tmp(,'REF','INTEGER','Osoba');
::    wybór pliku podpisu
      exec('ask4File','podpis','bmp',1,0,'rmua');
      {!
      |? {? P.seek(_prac.P.SQL,,1)
         || _wydruk:=username+$P.tm_stamp+'.pdf';
            {? 1+_wydruk='@' || _wydruk:=1-_wydruk ?};
            {? ~_os_tab.find_key(#P.OSOBA)
            || {? rep_exec('ppl_xxx_rmua',,0,_wydruk,1)
               || _exists:=0;
                  P.cntx_psh();
                  P.index('PRACOSOB');
                  P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,P.OSOBA);
                  {? P.size()=1
                  || exec('add_zal','zal','rm',,_wydruk);
                     _os_tab.REF:=#P.OSOBA;
                     _os_tab.add()
                  || {? P.find_le(date(O.RU,O.MU,0))
                     || {!
                        |? {? P.DZ=date(0,0,0) | P.DZ>=date(O.RU,O.MU,1)
                           || exec('add_zal','zal','rm',,_wydruk);
                              _os_tab.REF:=#P.OSOBA;
                              _os_tab.add();
                              _exists:=1
                           ?};
                           ~_exists & P.prev
                        !};
::                      Jesli nie mamy pracownika zatrudnionego i zwolnionego w miesiącu RMUA to wybieramy
::                      pracownika najpozniej zatrudnionego i zapisujemy zalacznik RMUA
                        {? ~_exists
                        || {? P.find_le(date(O.RU,O.MU,0))
                           || exec('add_zal','zal','rm',,_wydruk);
                              _os_tab.REF:=#P.OSOBA;
                              _os_tab.add()
                           ?}
                        ?}
                     ?}
                  ?};
                  P.cntx_pop()
               ?}
            ?}
         ?};
         _prac.P.next()
      !};
      obj_del(_os_tab)
   ?}
?};
O.cntx_pop();
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:57 5d90fd778b113828af2fbb63341406fca723cf5a0150429906c9929549f016f38e4f4fd8c4941dd842d58dfbad0ced4e824b6e946edc55c9a68acb55092ce4af3b8a2c155415002deece94c62417ffbc7cf51c9183a5c3b5eed4e59fae9f17abb8dab0f17350e36508ff1f2767587366a7ea87010eae559e4190bfc5ccecfb65

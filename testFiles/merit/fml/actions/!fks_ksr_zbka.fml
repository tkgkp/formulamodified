:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_zbka.fml
:: Utworzony: 08.04.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_KSR_ZBKA - Przeglądanie obrotów kont
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przeglądanie obrotów kont - główna formuła czynności FKS_KSR_ZBKA.
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
mask_kon:=0;
pa:=SSTALE.WAL;

_tab:=_a;
OKRO_F.index('ROK');
OKRO_F.prefix(SSTALE.AR);
OKRO_F.first();
{? PARWYD.REDANTRE
|| {? var_press('TT_STKON')<=0
   || exec('tt_stan_kon','!fks_ksr_zbka')
   ?};
   _b.grp_sel(_c,TT_STKON,__wsel,_tab,"
      exec('upd_okro_f','!fks_ksr_zbka',TT_STKON.KONTO);
      {? TT_STKON.REF_AN<>''
      || {? TT_STKON.STAN='N'
         || TT_STKON.actions_grayed(__wsel,'O')
         || TT_STKON.actions_grayed(__wsel,'B')
         ?}
      || TT_STKON.actions_grayed(__wsel,'OBUKP')
      ?};
      grp_disp(TT_STKON,__wsel);
      {? exec('interm','#system')
      || grp_disp(OKRO_F,'OBR',1);
         grp_edisp(OKRO_F,'SZG',1)
      || grp_disp(OKRO_F,'OBR2',1)
      ?}
   ",,,,"dwiewalo:=0;zakładka:=1;~~",,,,'maximized_with_title');
   task_attach('FKS_KSR_ZBKA');
   task_attach('FKS_KSR_ZBKB');
   _b.tab_splt(_c,,'vertical','right');
   {? exec('interm','#system')
   || _b.grp_sel(_c,OKRO_F,'OBR',,"",0,0,,"zakladka:=1","",0,,'maximized_with_title');
      _b.tab_splt(_c,'right','horizontal','bottom','25,75%');
      _b.grp_edit(_c,OKRO_F,'SZG',,,,,,'maximized')
   || _b.grp_sel(_c,OKRO_F,'OBR2',,"",0,0,,"zakladka:=1","",0,,'maximized')
   ?}
|| KS.index('SYM');
   KS.prefix(SSTALE.AR);
   AN.win_sel('WER');
   AN.index('WALSYM');
   AN.prefix(pa);
   _b.grp_sel(_c,,'WER',_tab,"
      exec('upd_okro_f','!fks_ksr_zbka',AN.SYM);
      {? exec('interm','#system')
      || grp_disp(OKRO_F,'OBR',1);
         grp_edisp(OKRO_F,'SZG',1)
      || grp_disp(OKRO_F,'OBR2',1)
      ?}
   ",,,,"dwiewalo:=0;~~",,,,'maximized_with_title');
   task_attach('FKS_KSR_ZBKA');
   task_attach('FKS_KSR_ZBKB');
   _b.tab_splt(_c,,'vertical','right');
   {? exec('interm','#system')
   || _b.grp_sel(_c,OKRO_F,'OBR',,"",0,0,,"zakladka:=1","",0,,'maximized_with_title');
      _b.tab_splt(_c,'right','horizontal','bottom','25,75%');
      _b.grp_edit(_c,OKRO_F,'SZG',,,,,,'maximized')
   || _b.grp_sel(_c,OKRO_F,'OBR2',,"",0,0,,"zakladka:=1","",0,,'maximized_with_title')
   ?}
?};
1


\tt_stan_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2008]
:: OPIS: Formula wyswietla konta o obroty na kontach w postaci hierarchicznej
::  OLD: \tt_stan_kon/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
TT_STKON:=tab_tmp(2,'TREE_REF','TREE_REF',,'AN','STRING[35]',,'KONTO','STRING[35]','Konto','KS',
   'STRING[6]',,'POZ','INTEGER',,'OP','STRING[60]','Opis','REF_AN','STRING[16]','Ref AN',
   'STAN','STRING[1]','Stan blokady');
tt_sndx1:=TT_STKON.index('?');
__wsel:=TT_STKON.mk_sel('Konta w '@,'P',0,'tt_stan_kont',,,,1);
TT_STKON.win_sel(__wsel);
TT_STKON.hdr_sel(SSTALE.WAL().KOD);
exec('tt_fill','!fks_ksr_zbka');
TT_STKON.win_fld(__wsel,,'KONTO',,,36);
TT_STKON.win_fld(__wsel,,'OP',,,60);
TT_STKON.win_fld(__wsel,,'STAN',,,3,,,'Blokada'@,,,2,,"'T'","'N'");
TT_STKON.tr_fml(__wsel,"exec('zr','!fks_ksr_zbka',_a)","exec('is_ntlev','!fks_ksr_zbka',TT_STKON.ref())",
      "exec('is_n_lev','!fks_ksr_zbka',TT_STKON.POZ,TT_STKON.KS)");
TT_STKON.win_act(__wsel,,'Rekord',,,,"
   KS.index('SYM');
   {? KS.prefix(SSTALE.AR,TT_STKON.KS); KS.first()
   || exec('op_konta','konto',TT_STKON.AN,KS.ref(),40);
      exec('ka_opis','konto')
   ?};
   POZ.cntx_psh();
   {? OPERATOR.DEPT
   || {? SSTALE.WAL<>FINFO.NAROD
      || POZ.index('OKWAL_O'); POZ.prefix('T','T',SSTALE.WAL,OPERATOR.DEPT,TT_STKON.AN)
      || POZ.index('OKWAL_O1'); POZ.prefix('T','T',OPERATOR.DEPT,TT_STKON.AN)
      ?}
   || {? SSTALE.WAL<>FINFO.NAROD
      || POZ.index('OKWAL'); POZ.prefix('T','T',SSTALE.WAL,TT_STKON.AN)
      || POZ.index('OKWAL1'); POZ.prefix('T','T',TT_STKON.AN)
      ?}
   ?};
   _zwrot:={? POZ.first() || Color.fnd_kol('AN#01#01') || '' ?};
   POZ.cntx_pop();
   _zwrot
");
_fun:="
   OKRO_F.cntx_psh();
   exec('ks_kon','!fks_ksr_zbka');
   OKRO_F.cntx_pop()
";
TT_STKON.win_act(__wsel,,'Formuła','Zapisy bieżącego okresu'@@,,'Zapisy bieżącego okresu'@,"",_fun,1,,,,'Z');
:: TODO: [MB] blokowanie kont - akcja niedostępna gdy brak uprawnień do czynności FKS_KSR_ZBKB
{? exec('chk_role','#b__box',OPERATOR.USER,'FKS_KSR_ZBKB')
|| TT_STKON.win_act(__wsel,,'Formuła','&Blokuj'@@,,'Blokowanie konta'@,"
     exec('blok_an_tt','!fks_ksr_zbkb','T')
   ",,0,,,,'B');
   TT_STKON.win_act(__wsel,,'Formuła','&Odblokuj'@@,,'Odblokowanie konta'@,"
      exec('blok_an_tt','!fks_ksr_zbkb','N')
   ",,0,,,,'O')
?};
TT_STKON.win_act(__wsel,,'Formuła','Usuń'@@,,'Usunięcie bieżącego konta jeśli są dla niego zerowe obroty'@,"
   OKRO_F.cntx_psh();
   AN.prefix();
   _rr:={? AN.seek(BB.sqlint(TT_STKON.REF_AN),form(8+TT_STKON.REF_AN)) || exec('an_del','!fks_ksr_zbka') ?};
   OKRO_F.cntx_pop();
   _rr
","
   _ref:=TT_STKON.TREE_REF;
   {? TT_STKON.del(,1)
   || {!|?
         {? _ref & ~TT_STKON.find_key(_ref)
         || {? TT_STKON.seek(_ref,TT_STKON.name)
            || _ref:=TT_STKON.TREE_REF; TT_STKON.del(,1)
            || 0
            ?}
         || 0
         ?}
      !}
   ?}
",0,,,,'U');
_akt_wal:="
   _pa_old:=pa;
   _tt_old:=TT_STKON.AN;
   _tt_poz:=TT_STKON.POZ;
   _tt_ks:=TT_STKON.KS;
   _rok:=SSTALE.AR;
   _odd:=OPERATOR.DEPT;
   exec('fks_ksr_params','dok_fks_ks');
   {? _pa_old<>pa | _rok<>SSTALE.AR | _odd<>OPERATOR.DEPT
   || TT_STKON.erase();
      exec('tt_fill','!fks_ksr_zbka');
      TT_STKON.win_sel(__wsel);
      TT_STKON.hdr_sel();
      TT_STKON.hdr_sel(SSTALE.WAL().KOD);
      AN.cntx_psh();
      AN.index('WALSYM');
      AN.prefix();
      {? AN.find_key(pa,_tt_old)
      || TT_STKON.blank; TT_STKON.AN:=(SSTALE.AR().SYNT+_tt_old);
         {? TT_STKON.find_rec(1) & _tt_poz
         || exec('tt_rozwin','!fks_ksr_zbka',_tt_poz,_tt_ks);
            TT_STKON.blank; TT_STKON.AN:=_tt_old;
            {? ~TT_STKON.find_rec(1) || TT_STKON.first() ?}
         ?}
      ?};
      AN.cntx_pop()
   ?}
";
TT_STKON.win_act(__wsel,,'Formuła','Druku&j'@@,,'Wydruk danych dla wybranego konta'@,,"exec('druk_an','!fks_ksr_zbka')",0,,,,'J');
TT_STKON.win_act(__wsel,,'Formuła','O&pis konta'@@,,'Opis konta'@,"
   AN.prefix();
   {? AN.seek(BB.sqlint(TT_STKON.REF_AN),form(8+TT_STKON.REF_AN))
   || exec('op_konta','konto',AN.SYM)
   ?}
","",0,,,,'P');
TT_STKON.win_act(__wsel,,'Formuła','Budowa &konta'@@,,'Szczegóły budowy konta analitycznego'@,"
   AN.prefix();
   {? AN.seek(BB.sqlint(TT_STKON.REF_AN),form(8+TT_STKON.REF_AN)) || exec('szcz_bud','konto') ?}
","",0,,,,'K');
_menu:='Raport&y'@;
{! _mi:=0..1
|! TT_STKON.win_act(__wsel,_mi,'Menu',_menu);
   TT_STKON.win_act(__wsel,_mi,'Formuła','&Wydruki kartoteki księgowań'@@,_menu,'Wydruk kartoteki księgowań'@,,
   "exec('main','!fks_ksr_zakk',0)",,,,,'W');
   task_attach('FKS_KSR_ZAKK');
   TT_STKON.win_act(__wsel,_mi,'Formuła','&Sprawozdania według analityk'@@,_menu,'Sprawozdania według analityk'@,,
   "exec('main','!fks_ksr_zbwa',0)",,,,,'S');
   task_attach('FKS_KSR_ZBWA');
   TT_STKON.win_act(__wsel,_mi,'Formuła','&Zestawienie obrotów i sald'@@,_menu,'Wydruki zestawień obrotów i sald'@,,
   "exec('main','!fks_ksr_zbos',0)",,,,,'Z');
   task_attach('FKS_KSR_ZBOS');
   TT_STKON.win_act(__wsel,_mi,'Formuła','Zapytania S&QL'@@,_menu,'Zapytania SQL'@,,
   "exec('main','!fks_ksr_zsql')",,,,,'Q');
   task_attach('FKS_KSR_ZSQL')
!};
TT_STKON.win_act(__wsel,,'Formuła','P&arametry pracy'@@,,'Zmiana parametrów pracy systemu'@,"",_akt_wal,0,,,,'A');
TT_STKON.win_act(__wsel,1,'Formuła','P&arametry pracy'@@,,'Zmiana parametrów pracy systemu'@,"",_akt_wal,1,,,,'A');
TT_STKON.win_act(__wsel,,'Szukaj');
TT_STKON.win_act(__wsel,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy i ikon'@,"","
   exec('legenda','color','AN#01#01')
",0,,,,'L');
TT_STKON.index(tt_sndx1);
TT_STKON.first()


\upd_okro_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uaktualnia wolne pola w oknie okresów
::   WE: _a - konto
::----------------------------------------------------------------------------------------------------------------------
 _wal:=SSTALE.WAL;
_kod:=SSTALE.WAL().KOD;
SUM.SUMWN:=SUM.SUMMA:=SUM.SALWN:=SUM.SALMA:=SUM.TSUMWN:=SUM.TSUMMA:=SUM.TSALWN:=SUM.TSALMA:=0;
{? _a<> ''
|| OBR.index('AN_T');
   OBR.prefix(_wal,_a);
   OKRO_F.cntx_psh();
   {? OBR.first
   || {!
      |? {? ~OPERATOR.DEPT | OPERATOR.DEPT=OBR.ODD
         || {? OBR.OKRO().NR<=SSTALE.AO().NR || SUM.SUMWN+=OBR.WN; SUM.SUMMA+=OBR.MA ?};
            SUM.TSUMWN+=OBR.WN; SUM.TSUMMA+=OBR.MA
         ?};
         OBR.next()
      !}
   ?};
   OKRO_F.cntx_pop();
   SUM.SALWN:=F.SWn(SUM.SUMWN,SUM.SUMMA);
   SUM.SALMA:=F.SMa(SUM.SUMWN,SUM.SUMMA);
   SUM.TSALWN:=F.SWn(SUM.TSUMWN,SUM.TSUMMA);
   SUM.TSALMA:=F.SMa(SUM.TSUMWN,SUM.TSUMMA);
   OKRO_F.actions_grayed('OBR','')
|| OKRO_F.actions_grayed('OBR','Z:Z')
?};
OKRO_F.hdr_sel();
OKRO_F.hdr_sel(' %1 w %2'@[_a,_kod])


\tt_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2008]
:: OPIS: Formula wypelnia tabele tymczasowa sysntetykami na postawie tabeli AN
::  OLD: \tt_fill/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
KSQ:=sql('select distinct KS.SYM, KS.NAZ, AN.KS as REF from AN join KS where AN.WAL=:_a order by 1',pa);
AN.cntx_psh();
AN.index('WALSYM');
AN.prefix(pa);
{? KSQ.first()
|| {!
   |? TT_STKON.TREE_REF:=0;
      TT_STKON.AN:=TT_STKON.KONTO:=TT_STKON.KS:=KSQ.SYM;
      TT_STKON.POZ:=0;
      TT_STKON.OP:=KSQ.NAZ;
      TT_STKON.REF_AN:={? AN.find_key(TT_STKON.KS) & AN.SYM=TT_STKON.KS || $AN.ref || '' ?};
      TT_STKON.STAN:={? TT_STKON.REF_AN<>'' || AN.STAN || '' ?};
      TT_STKON.add();
      KSQ.next()
   !}
?};
AN.cntx_pop();
VAR_DEL.delete('KSQ')


\tt_rozwin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2008]
:: OPIS: Formula rozwija galaz dla syntetyki _b do poziomu _a
::   WE: _a - poziom do ktorego nalezy rowinac galaz
::       _b - symbol konta syntetycznego
::  OLD: \tt_rozwin/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
_i:=0;
{!
|? _i+=1;
   exec('fill_an','!fks_ksr_zbka',_b,_i,0);
   _i<=_a
!}


\zr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [BZ] [2008]
:: OPIS: Rozwija analityki konta (dodaje rekordy do tabeli tymczasowej) do kolejnego poziomu
::   WE: _a: tryb pracy: 0 - zwijanie; 1 - rozwijanie
::  OLD: \zr/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| {? ~exec('is_ntlev','!fks_ksr_zbka',TT_STKON.ref())
   || exec('fill_an','!fks_ksr_zbka',TT_STKON.KS,TT_STKON.POZ+1,1)
   ?}
|| exec('del_an','!fks_ksr_zbka',TT_STKON.ref(),TT_STKON.POZ+1)
?}


\fill_an
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [BZ] [2008]
:: OPIS: Dodaje do tabeli tymczasowej rekordy dla kolejnego poziomu
::   WE: _a: symbol konta syntetycznego
::       _b: numer dodawanego poziomu
::       _c: 1/0 - czy ustawiac sie na poprzednim rekordzie tabeli TT_STKON
::  OLD: \fill_an/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
_van:=TT_STKON.AN; _vkon:=TT_STKON.KONTO; _vref:=TT_STKON.ref();
_vsep:=SSTALE.AR().SEP; {? _vsep=',' || _vsep:='' ?};
_last:=null;
KS.cntx_psh(); AN.cntx_psh(); AN_SLU.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR,_a);
{? KS.first()
|| AN.index('KS'); AN.prefix(KS.ref(),(_vkon+_vsep));
   {? AN.first()
   || {!
      |? {? AN.WAL=pa
         || AN_SLU.index('DISP');
            AN_SLU.prefix(AN.ref,_b);
            {? AN_SLU.first() & _last<>AN_SLU.SLO & AN.SYM*(_vkon+_vsep+AN_SLU.SLO().KOD)
            || TT_STKON.TREE_REF:=#_vref;
               TT_STKON.KS:=_a;
               TT_STKON.AN:=_van+_vsep+AN_SLU.SLO().KOD; TT_STKON.KONTO:=form(_vkon+_vsep+SLO.KOD);
               TT_STKON.POZ:=_b;
               TT_STKON.OP:=SLO.TR;
               TT_STKON.REF_AN:={? TT_STKON.AN=AN.SYM || $AN.ref || '' ?};
               TT_STKON.STAN:={? TT_STKON.REF_AN<>'' || AN.STAN || '' ?};
               TT_STKON.add();
               _last:=AN_SLU.SLO
            |? AN_SLU.first() & _last<>AN_SLU.SLO
            || FUN.info('Niezgodność między analityką: %1'
                     '\na budową konta: %2'
                     '\nna poziomie: %3'+@[AN.SYM,KS.SYM,$_b])
            ?}
         ?};
         AN.next()
      !}
   ?}
?};
KS.cntx_pop(); AN.cntx_pop(); AN_SLU.cntx_pop();
{? _c || TT_STKON.seek(_vref) ?}


\del_an
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [BZ] [2008]
:: OPIS: Usuwa z tabeli tymczasowej rekordy dla kolejnego poziomu
::   WE: _a: numer rekordu wezla, ktorego elementy usunac
::       _b: numer usuwanego poziomu
::  OLD: \del_an/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
TT_STKON.cntx_psh();
TT_STKON.index(tt_sndx1);
TT_STKON.prefix(_a);
{? TT_STKON.first()
|| {!
   |? {? TT_STKON.POZ=_b
      || {? exec('is_ntlev','!fks_ksr_zbka',TT_STKON.ref())
         || exec('del_an','!fks_ksr_zbka',TT_STKON.ref(),_b+1); TT_STKON.del()
         || TT_STKON.del()
         ?}
      || TT_STKON.next()
      ?}
   !}
?};
TT_STKON.cntx_pop()


\is_n_lev
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [BZ] [2008]
:: OPIS: Sprawdza czy jest nastepny poziom w budowie konta po poziomie przekazanym jako _a
::   WE: _a: poziom; _b: symbol konta
::   WY: 0 - brak nastepnego poziomu, liczba - dlugosc nastepnego poziomu
::  OLD: \is_n_lev/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=0; AN.cntx_psh(); AN_SLU.cntx_psh(); KS.cntx_psh();
AN.index('KS');
AN_SLU.index('DISP');
AN_SLU.prefix();
KS.index('SYM'); KS.prefix(SSTALE.AR,_b);
{? KS.first()
|| AN.prefix(KS.ref());
   {? AN.first()
   || {!
      |? {? AN.WAL=pa & AN_SLU.find_key(AN.ref,_a+1)
         || _ret:=AN_SLU.SLU().DL; 0
         || AN.next()
         ?}
      !}
   ?}
?};
AN.cntx_pop(); AN_SLU.cntx_pop(); KS.cntx_pop(); _ret


\is_ntlev
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [BZ] [2008]
:: OPIS: Sprawdza czy jest nastepny poziom w drzewie dla elementu przekazanego jako _a
::   WE: _a: element drzewa (reference)
::  OLD: \is_ntlev/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
TT_STKON.cntx_psh();
TT_STKON.index(tt_sndx1);
TT_STKON.prefix(_a);
_ret:=TT_STKON.first();
TT_STKON.cntx_pop(); _ret


\ks_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Wyswietlanie ksiegowan dla biezacego rekordu tabeli AN
::  OLD: \ks_kon/obr_an.fml
::----------------------------------------------------------------------------------------------------------------------
_konto:={? PARWYD.REDANTRE & var_pres('TT_STKON')>0 & var_pres('AN',TT_STKON)=27
        || TT_STKON.KONTO
        || AN.SYM
        ?};
ROZNE.UT_OKROD:=ROZNE.UT_OKRDO:=SSTALE.AO;
exec('zapisy','dok_fks_ks',_konto)


\an_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Usuwa rekord AN, jesli sa zerowe obroty dla tego konta.
::       Usuwa rowniez rekordy w tabeli OBR zwiazane z danym rekordem AN.
::   WY: 1 usunieto AN
::       0 nie usunieto AN
::  OLD: \an_del/param.fml
::----------------------------------------------------------------------------------------------------------------------
_kom:='';_rr:=0;
{? FUN.ask('Na pewno usunąć?'@) & AN.lock(1)
|| {? OBR.lock(1)
   || _wn:=_ma:=0;
      OBR.index('AN'); OBR.prefix(AN.ref());
      {? OBR.first() || {! |? _wn+=OBR.WN; _ma+=OBR.MA; OBR.next() !} ?};
      {? _wn=0 & _ma=0
      || {? exec('an_zap','!fks_ksr_zbka')
         || do();
            {? OBR.first() || {! |? OBR.del() !} ?};
            MAGWALP.index('KON'); MAGWALR.index('MW');
            MAGWALP.prefix(AN.SYM,);
            {? MAGWALP.first()
            || {! |? MAGWALR.prefix(MAGWALP.ref());
                     {? MAGWALR.first() || {! |? MAGWALR.del() !} ?};
                     MAGWALP.del()
               !}
            ?};
            MAGWALR.index('KON');
            MAGWALR.prefix(AN.SYM,);
            {? MAGWALR.first() || {! |? MAGWALR.del() !} ?};
            _sym:={? AN.WAL=FINFO.NAROD || AN.SYM || '' ?};
            exec('delanslu','!fks_ksr_zbka',AN.ref());
            _rr:=AN.del(,1);
            {? _rr & _sym<>''
            || AN.cntx_psh(); AN.index('SYM'); AN.prefix(_sym);
               {? AN.first()
               || {!
                  |? _wn:=_ma:=0;
                     OBR.index('AN'); OBR.prefix(AN.ref());
                     {? OBR.first() || {! |? _wn+=OBR.WN; _ma+=OBR.MA; OBR.next() !} ?};
                     {? _wn=0 & _ma=0
                     || {? OBR.first() || {! |? OBR.del() !} ?};
                        exec('delanslu','!fks_ksr_zbka',AN.ref());
                        AN.del()
                     || AN.next()
                     ?}
                  !}
               ?};
               AN.cntx_pop()
            ?};
            end()
         || _kom:='Konto posiada zapisy księgowe.\nUsunięcie niemożliwe.'
         ?}
      || _kom:='Konto posiada obroty.\nUsunięcie niemożliwe.'
      ?};
      OBR.unlock()
   ?};
   AN.unlock()
?};
{? _kom<>'' || FUN.info(_kom) ?};
_rr


\an_zap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: sprawdza czy nie ma zapisow dla danego konta
::  OLD: \an_zap/param.fml
::----------------------------------------------------------------------------------------------------------------------
_znacz:=1;
POZ.cntx_psh(); OKRO_F.cntx_psh();
OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR);
{? OKRO_F.first()
|| {!
   |? POZ.use('pozy'+SSTALE.AR().KOD+form(OKRO_F.NR,-2)); POZ.index('SYMKON');
      {? AN.WAL<>FINFO.NAROD
      || POZ.prefix('T','T',AN.SYM,AN.WAL)
      || POZ.prefix('T','T',AN.SYM)
      ?};
      _znacz:=~POZ.first();
      _znacz & OKRO_F.next()
   !}
 ?};
OKRO_F.cntx_pop(); POZ.cntx_pop();
_znacz


\delanslu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Usuniecie budowy konta analitycznego
::   WE: _a - ref rekordu w tabeli AN
::  OLD: \delanslu/opis_kon.fml
::----------------------------------------------------------------------------------------------------------------------
AN.cntx_psh(); AN.index('SYM'); AN.prefix();
{? AN.seek(_a)
|| AN_SLU.cntx_psh();
   AN_SLU.index('DISP'); AN_SLU.prefix(AN.ref());
   {? AN_SLU.first || {! |? AN_SLU.del() !} ?};
   AN_SLU.cntx_pop()
?};
AN.cntx_pop()


\spr_zap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Rekord przed w tabeli AN
::  OLD: \spr_zap/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
AN.KS();
exec('op_konta','konto',AN.SYM,AN.KS,2);
exec('ka_opis','konto');
POZ.cntx_psh();
{? OPERATOR.DEPT
|| {? AN.WAL<>FINFO.NAROD
   || POZ.index('SYMKON'); POZ.prefix('T','T',AN.SYM,AN.WAL,OPERATOR.DEPT)
   || POZ.index('SYMKONBW'); POZ.prefix('T','T',AN.SYM,OPERATOR.DEPT)
   ?}
|| {? AN.WAL<>FINFO.NAROD
   || POZ.index('SYMKON'); POZ.prefix('T','T',AN.SYM,AN.WAL)
   || POZ.index('SYMKONBW'); POZ.prefix('T','T',AN.SYM)
   ?}
?};
_zwrot:=POZ.first();
POZ.cntx_pop();
_gray:='';
{? AN.STAN='T'
|| _gray:='B'
|| _gray:='O'
?};
{? KS.MW<>'T' | SSTALE.WAL=FINFO.NAROD
|| _gray+='M'
?};
AN.actions_grayed('WER',_gray);
{? _zwrot || 'AN#01#01' || '' ?}


\obr_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Przedstawia obroty konta w walutach.
::  OLD: \obr_kon/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
OKRO_F.win_sel('OBR');
OKRO_F.index('ROK');  OKRO_F.prefix(SSTALE.AR);
wal:=SSTALE.WAL;
kod:=SSTALE.WAL().KOD;
poz:=AN.ref;
{? OPERATOR.DEPT
|| OBR.index('AN_O'); OBR.prefix(AN.ref,OPERATOR.DEPT)
|| OBR.index('AN'); OBR.prefix(AN.ref)
?};
exec('suma_obr','!fks_ksr_zbka');
OKRO_F.hdr_sel();
OKRO_F.hdr_sel(' %1 w %2'@[AN.SYM,kod]);
OKRO_F.select();
OKRO_F.cntx_pop();
&wal;
&kod;
&poz;
1


\suma_obr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Funkcja podsumowuje obroty konta i wyświetla je na ekranie.
::  OLD: \suma_obr/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
SUM.SUMWN:=SUM.SUMMA:=SUM.SALWN:=SUM.SALMA:=SUM.TSUMWN:=SUM.TSUMMA:=SUM.TSALWN:=SUM.TSALMA:=0;
{? OBR.first
|| {!
   |? {? OBR.OKRO().NR<=SSTALE.AO().NR
      || SUM.SUMWN+=OBR.WN; SUM.SUMMA+=OBR.MA
      ?};
      SUM.TSUMWN+=OBR.WN; SUM.TSUMMA+=OBR.MA;
      OBR.next()
   !}
?};
SUM.SALWN:=F.SWn(SUM.SUMWN,SUM.SUMMA);
SUM.SALMA:=F.SMa(SUM.SUMWN,SUM.SUMMA);
SUM.TSALWN:=F.SWn(SUM.TSUMWN,SUM.TSUMMA);
SUM.TSALMA:=F.SMa(SUM.TSUMWN,SUM.TSUMMA);
1


\obroty_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Formula zlicza obroty do zmiennych SUM.OBRWN i SUM.OBRMA dla pojedynczej
::       jednostki ksiegowej lub dla calego zakladu.
::       Formula jest wywolywana przed wyswietleniem dla pola OBRWN zmiennej SUM
::  OLD: \obroty_k/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
 _wn:=_ma:=0;
OBR.cntx_psh();
{? PARWYD.REDANTRE &
::Poczatek modyfikacji dla Maclex Fiks 14-10-2009 Pawelm [1120]
   (var_pres('TT_STKON')> 0)&
::Koniec modyfikacji dla Maclex
   var_pres('KONTO',TT_STKON)=27
|| OBR.index('OKR_T');
   OBR.prefix(OKRO_F.NR,SSTALE.WAL,TT_STKON.KONTO);
   {? OBR.first()
   || {!
      |? {? ~OPERATOR.DEPT | OPERATOR.DEPT=OBR.ODD || _wn+=OBR.WN; _ma+=OBR.MA ?};
         OBR.next()
      !}
   ?};
   exec('upd_okro_f','!fks_ksr_zbka',TT_STKON.KONTO)
|? AN.SYM<>''
|| OBR.index('OKR');
   {? OPERATOR.DEPT
   || OBR.prefix(OKRO_F.NR,AN.ref,OPERATOR.DEPT)
   || OBR.prefix(OKRO_F.NR,AN.ref)
   ?};
   {? OBR.first()
   || {!|? _wn+=OBR.WN; _ma+=OBR.MA; OBR.next() !}
   ?};
   exec('upd_okro_f','!fks_ksr_zbka',AN.SYM)
?};
SUM.OBRWN:=_wn;  SUM.OBRMA:=_ma;
SUM.SALWNOKR:=F.SWn(SUM.OBRWN,SUM.OBRMA); SUM.SALMAOKR:=F.SMa(SUM.OBRWN,SUM.OBRMA);
{? AN.size()=0 || SUM.OBRWN:=0;  SUM.OBRMA:=0 ?};
OBR.cntx_pop()


\opis_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Tworzenie opisu konta
::  OLD: \opis_kon/opis_kon.fml
::----------------------------------------------------------------------------------------------------------------------
_hid_d:=''; _hid:='';
{? POZ.DOK().get()
||  {? PAR_SKID.get(82)='N'
       ||     DOKUM.index('DOKUM');
              DOKUM.prefix(REF.FIRMA,$DOK.ref);
              {? DOKUM.first()
              ||  ''
              || _hid_d+='Z'
              ?}
       ?};'załącznik';
       _sys:={? +DOK.ZAR & DOK.ZAR*':' || (DOK.ZAR*':'-1)+DOK.ZAR || '' ?};
       {? DOK.DOKZRODL='' | _sys='' || _hid_d+='D' ?};'dokument źródłowy';
       {? ~DOK.E_DOC || _hid_d+='E' ?};'skan ';
       {? _hid_d<>'' || _hid:='E('+_hid_d+')' || _hid:='' ?}
?};
:: 'menu_pth do zmiany nie wie w jakim oknie jest do poprawy  ';
::msg('okno: '+POZ.win_sel('?') +' menu: '+ menu_pth );
{? zakladka=1 | zakladka=3 | zakladka=4
|| _okno:='C_WYS';
   _tab:=POZ
|?  zakladka=2
|| _okno:=ZapPoz.WIN;
   _tab:=ZapPoz.TAB
|? zakladka=10
|| _okno:=cur_tab(1,1).win_sel('?');
   _tab:=cur_tab(1,1)
|| _okno:=POZ.win_sel('?');
   _tab:=POZ
?};
_tab.actions_grayed(_okno,_hid);
POZ.KOM().KS();
exec('opis','konto',POZ.KON, POZ.KOM().KS);
exec('ka_opis','konto')


\wys_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Formula wypelnia pola związane z walutą i kwotami w walucie obcej
::  OLD: \wys_sum/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.ZAP_WN:=ROZNE.ZAP_MA:=0;
{? var_pres('wys_sum')<=0
|| wys_sum:={? SSTALE.WAL<>FINFO.NAROD || 1 || 0 ?}
?};
{? wys_sum=0
|| {? -POZ.STR='wn' || ROZNE.ZAP_WN:=POZ.SUM || ROZNE.ZAP_MA:=POZ.SUM ?}
|| {? -POZ.STR='wn' || ROZNE.ZAP_WN:=POZ.SUMW || ROZNE.ZAP_MA:=POZ.SUMW ?}
?}; 1


\rec_obr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Rekord przed okienka OBR tabeli OKRO_F
::  OLD: \rec_obr/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
_a:=(OKRO_F.NR>SSTALE.AO().NR);
OKRO_F.cntx_pop();
{? _a || 'OKRO_F#03#01' || '' ?}


\okro_zap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2008]
:: OPIS: Zapisy dla okresu konta (okno OBR tabeli OKRO_F)
::  OLD: \okro_zap/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
SUM.cntx_psh();
_tab:=ROZRACH.TABELA;
ROZRACH.TABELA:='POZ';
exec('zapisy','konto',{? var_pres('TT_STKON')>100 || TT_STKON.KONTO || AN.SYM ?},OKRO_F.ref());
ROZRACH.TABELA:=_tab;
SUM.cntx_pop();
OKRO_F.cntx_pop()


\zap_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: Przed wyswietleniem ROZNE.ZAP_ODD
::  OLD: \zap_odd/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.ZAP_ODD:=POZ.DOK().ODD; 1


\pw_rejz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS:
::  OLD: \pw_rejz/rejestr.fml
::----------------------------------------------------------------------------------------------------------------------
REJESTR.REJZ:=POZ.DOK().REJ


\druk_zap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Wydruk wybranych pozycji (ksiegowania)
::  OLD: \druk_zap/dok_zrd.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FKS_KSR_ZBKA','fks_w_ksieg',,,,,,,,'W')


\druk_zap2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Wydruk wybranych pozycji (ksiegowania)
::----------------------------------------------------------------------------------------------------------------------
exec('obj_kon','dok_fks_ks');
{? cur_tab(1)<>POZ
|| hlp:=exec('obj_hlp','fks_ksr',cur_tab(1))
?};
cur_tab(1).cntx_psh();
exec('rep_exec','#b_report','FKS_KSR_ZBKA','fks_w_ksieg',,,,,,,,'W');
cur_tab(1).cntx_pop();
VAR_DEL.delete('hlp')


\druk_an
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Akcja wydruk okna WER tabeli AN
::  OLD: \druk_an/opis_kon.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
exec('rep_exec','#b_report','FKS_KSR_ZBKA','fks_kk_*',,1);
OKRO_F.cntx_pop()


\zap_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Akcja Rozrachunki w okienku wertowania zapisów księgowych
::  OLD: \zap_roz/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
_v:=ROZRACH.SYMR;
ZAP_OP.cntx_psh(); POZ.cntx_psh(); DOK.cntx_psh();
ZAP_OP.index('ZAP');
{? OPERATOR.DEPT
|| ZAP_OP.prefix(POZ.ref,SSTALE.WAL,SSTALE.AO,OPERATOR.DEPT)
|| ZAP_OP.prefix(POZ.ref,SSTALE.WAL,SSTALE.AO)
?};
ZAP_OP.win_sel('ZAP'); ZAP_OP.select();
ROZRACH.SYMR:=_v;
ZAP_OP.cntx_pop(); POZ.cntx_pop(); DOK.cntx_pop()


\obr_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009+]
:: OPIS: Obroty dla okresow
::   WE: [_a] - tabela pozycji - domyślnie POZ
::  OLD: \obr_okr/dok_zrd4.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=POZ ?};
_prfx:=~_a.f_active();
_a.cntx_psh();
{? _prfx & _a.first() | ~_prfx & _a.f_first()
|| OBR_OKR:=tab_tmp(1,
      'OKRO_NR','INTEGER','',
      'OKRO_NAZ','STRING[20]','',
      'WN','REAL','Obroty Wn',
      'MA','REAL','Obroty Ma',
      'SWN','REAL','Saldo Wn',
      'SMA','REAL','Saldo Ma'
   );
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
   {!
   |? _ref:={? _a=POZ || POZ.ref || _a.REF ?};
      _okres:=#(2+(6-$_ref));
      {? OBR_OKR.find_key(_okres)
      || {? SSTALE.WAL=FINFO.NAROD
         || {? -_a.STR='wn'
            || OBR_OKR.WN+=_a.SUM
            || OBR_OKR.MA+=_a.SUM
            ?}
         || {? -_a.STR='wn'
            || OBR_OKR.WN+=_a.SUMW
            || OBR_OKR.MA+=_a.SUMW
            ?}
         ?};
         OBR_OKR.put()
      || OKRO_F.prefix(SSTALE.AR,_okres);
         {? OKRO_F.first()
         || OBR_OKR.OKRO_NR:=OKRO_F.NR; OBR_OKR.OKRO_NAZ:=OKRO_F.NAZ;
            {? SSTALE.WAL=FINFO.NAROD
            || {? -_a.STR='wn'
               || OBR_OKR.WN:=_a.SUM;
                  OBR_OKR.MA:=0
               || OBR_OKR.MA:=_a.SUM;
                  OBR_OKR.WN:=0
               ?}
            || {? -_a.STR='wn'
               || OBR_OKR.WN:=_a.SUMW;
                  OBR_OKR.MA:=0
               || OBR_OKR.MA:=_a.SUMW;
                  OBR_OKR.WN:=0
               ?}
            ?};
            OBR_OKR.add()
         ?}
      ?};
      _prfx & _a.next() | ~_prfx & _a.f_next()
   !};
   OKRO_F.cntx_pop();
   {? OBR_OKR.first()
   || _wn:=_ma:=0;
      {! |?
         OBR_OKR.SWN:=F.SWn(OBR_OKR.WN,OBR_OKR.MA);
         OBR_OKR.SMA:=F.SMa(OBR_OKR.WN,OBR_OKR.MA);
         OBR_OKR.put();
         _wn+=OBR_OKR.WN; _ma+=OBR_OKR.MA;
         OBR_OKR.next()
      !};
      OBR_OKR.OKRO_NR:=50; OBR_OKR.OKRO_NAZ:='RAZEM';
      OBR_OKR.WN:=_wn; OBR_OKR.MA:=_ma; OBR_OKR.SWN:=F.SWn(_wn,_ma); OBR_OKR.SMA:=F.SMa(_wn,_ma);
      OBR_OKR.add();
      _wer:=OBR_OKR.mk_sel(
         'Obroty dla okresów, '+
         {? ROZNE.TYP_WYS='M'
         || 'maska konta \''+ROZNE.MASKA+'\''
         || 'prefiks konta \''+ROZNE.PREFIX+'\''
         ?},'T',0,'tab_obr_okr_wer',,,OBR_OKR.size());
      OBR_OKR.win_fld(_wer,,'OKRO_NAZ');
      {? SSTALE.WAL=FINFO.NAROD || FINFO.NAROD() || SSTALE.WAL() ?};
      OBR_OKR.win_fld(_wer,,'WN',,,18,,,'Obroty Wn (%1)'@[SLO.KOD]);
      OBR_OKR.win_fld(_wer,,'MA',,,18,,,'Obroty Ma (%1)'@[SLO.KOD]);
      OBR_OKR.win_fld(_wer,,'SWN',,,18,,,'Saldo Wn (%1)'@[SLO.KOD]);
      OBR_OKR.win_fld(_wer,,'SMA',,,18,,,'Saldo Ma (%1)'@[SLO.KOD]);
      OBR_OKR.win_act(_wer,,'Rekord',,,,"{? OBR_OKR.OKRO_NR=50 || exec('findtmp','#color') || 0 ?}");
      OBR_OKR.win_sel(_wer); OBR_OKR.select()
   ?};
   VAR_DEL.delete('OBR_OKR')
?};
_a.cntx_pop()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 67001f5ef8b27bd04616e8e5213fa1f3977e5b394f915d03a8d0b43ca7689513c996292a939b14017977250daabd97653f3a99551429cb4b8534f1c259f6eb6b87a7447ad776c6259568a2ba6271fb048d6e53e02ecad86fdccb587365a7fae0add56212d7ef52d83dc7a57609ba488fc58bd9dd68043d179386911a95b5c067

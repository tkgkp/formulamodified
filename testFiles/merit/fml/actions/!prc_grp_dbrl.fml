:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_grp_dbrl.fml
:: Utworzony: 28.10.2020
:: Autor: DG
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_GRP_DBRL - Blokada rozliczeń.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.14]
:: OPIS: Blokada rozliczeń z zakresu podanych dat dla wybranych współpracowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

: bezpiecznik w przypadku kiedy okres zamknięty - brak możliwości blokady
{? A_OKR.S='Z'
|| FUN.info('Wybrany okres jest już zamknięty. Blokada rozliczeń niemożliwa.'@);
   return()
?};

_okr:=__HARM.OKR_REF;
_zdate:=date(0,0,0);
data_od:=data_do:=_zdate;

: wybór wielu pracowników z ograniczeniem do danego okresu rozliczeniowego
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PRC';
_args.F_ZATR:={? P_FILTER.F_ZATR().KOD='' || 'P,Z,K,T,R' || P_FILTER.F_ZATR().KOD ?};
_args.VIEW:=P_FILTER.STATUS;
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
{? _okr
|| _args.SQL_FROM:='join A_OKRP using(A_OKRP.P, P.REFERENCE)';
   _args.SQL_WHERE:='A_OKRP.OKR=\''+$_okr+'\''
?};

P.cntx_psh();
P.first();
_ret:=exec('wybierz','pracownik',_args).P;
P.cntx_pop();

: wybrano kogoś?
{? _ret.size()=0
|| R.f_clear();
   FUN.info('Nie wybrano żadnego pracownika.'@);
   return()
?};
R.f_clear();

:założenie kontekstów
exec('startup','!prc_grp_dbrl');

:: zakres dat
_PAR:=tab_tmp(,'OD','DATE','Data od'@,
               'DO','DATE','Data do'@);
_we:=_PAR.mk_edit('Podaj dane'@,,'blr_zakr_dat');
_PAR.win_esep(_we,'Zakres dat'@);
_PAR.win_efld(_we,,'OD',,,12,0,,,,'Data, od której wprowadzana będzie blokada rozliczeń.'@);
_PAR.win_efld(_we,,'DO',,,12,0,,,,'Data, do której wprowadzana będzie blokada rozliczeń.'@);
_PAR.efld_opt(_we,'mark=1',,'OD');
_PAR.efld_opt(_we,'mark=1',,'DO');
exec('ok_esc','#window',_PAR,_we);
_PAR.win_edit(_we);
:_PAR.OD:=date(,,1);
::_PAR.OD:=A_OKR.OD;
:_PAR.DO:=date;
_PAR.OD:={? date(,,1)>A_OKR.DO || date(A_OKR.DO~1,A_OKR.DO~2,1) || date(,,1) ?};
_PAR.DO:={? date>A_OKR.DO || A_OKR.DO || date ?};
{! |?
   {? ~_PAR.edit(
      "  _PAR:=cur_tab();
         {? (_chk:=__CHK.record(_PAR,,'OD'))<>'' | (_chk:=__CHK.record(_PAR,,'DO'))<>''
         || return(_chk); 0
         |? _PAR.OD<=date(0,0,0)
         || return(__CHK.err_fld(_PAR,'OD',1,'Wprowadzono błędną datę.'@ )); 0
         |? _PAR.OD<A_OKR.OD | _PAR.OD>A_OKR.DO
         || return(__CHK.err_fld(_PAR,'OD',1,'Data wykracza poza ustawiony okres rozliczeniowy.'@)); 0
         |? _PAR.DO>A_OKR.DO | _PAR.DO<A_OKR.OD
         || return(__CHK.err_fld(_PAR,'DO',1,'Data wykracza poza ustawiony okres rozliczeniowy.'@)); 0
         |? _PAR.OD>_PAR.DO
         || return(__CHK.err_fld(_PAR,'DO',1,'Data nie może być wcześniejsza niż: \"Data od\".'@)); 0
         || data_od:=_PAR.OD; data_do:=_PAR.DO; 1
         ?}
      "
      )
   || {? FUN.ask('Czy na pewno chcesz zrezygnować z wprowadzania danych?'@)
      || exec('cleanup','!prc_grp_dbrl');
         return
      || 1
      ?}
   || 0
   ?}
!};
&_we;
obj_del(_PAR);
&_PAR;

:: wybór akcji do wykonania
{! |?
   {? _wyb:=FUN.choice('Wybierz akcję dla rozliczeń zaznaczonych pracowników:'@,1,'Zablokowanie'@,'Odblokowanie'@,,,,
                       ' - Wybór akcji'@)
   || 0
   || {? FUN.ask('Czy na pewno chcesz zrezygnować z wprowadzania danych?'@)
      || exec('cleanup','!prc_grp_dbrl');
         return
      || 1
      ?}
   ?}
!};

:: przetwarzanie rekordów
{? _ret.first
|| {! |?
      _ref:=exec('FindAndGet','#table',P,_ret.SQL,,,null());
      P.seek(_ref);
      _result:=sql('select R_PRACDN.REFERENCE REF, R_PRACDN.DT DT from @R_PRACDN '
                   'where DT >= to_date(:_a) and DT <= to_date(:_b) and R_PRACDN.P = :_c',
                   data_od, data_do,P.ref());

      {? _result.first()
      || {! |?
            MASK.Use('R_PRACDN',_result.DT~1,_result.DT~2);
            _refRP:=exec('FindAndGet','#table',R_PRACDN,_result.REF,,,null());
            R_PRACDN.seek(_refRP,,1);
            exec('blokada_gr','prc_rozlicz',R_PRACDN.ref(),{? _wyb=1 || 1 || 0 ?});
            _result.next
         !}
      ?};
      &_result;
      _ret.next
   !}
?};

FUN.info('Zakończono wprowadzanie blokady rozliczeń.'@);

: czyszczenie kontekstów
exec('cleanup','!prc_grp_dbrl');
~~


\startup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.14]
:: OPIS: Zakładanie kontekstów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
R_PRACDN.cntx_psh();
A_OKRP.cntx_psh()


\cleanup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.14]
:: OPIS: Czyszczenie kontekstów i zmiennych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P.cntx_pop();
R_PRACDN.cntx_pop();
A_OKRP.cntx_pop();
&data_od; &data_do
:obj_del(_PAR)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 86c304401a18f64615081efb2797de77b6289c032fd544d8fb2ddf542087c53a2066edf001271c5a82608139d408f877a1674d46a0053a881744c25da5f80d47054147136865ce3478f6a5e6b6f2264782ae715a498f6699eda5077afcf191697f03ba7d46f10e11b62f39f2f496bbd9c82e986cd11414958e07209a56ee4d3e

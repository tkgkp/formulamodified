:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !poc_ofo_rfoc.fml
:: Utworzony: 10.08.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności POC_OFO_RFOC - Rejestracja formularzy ocen
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Rejestracja formularzy ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE, symbol=ZO_PROC, type=_ZO_PROC, name=Wskazanie sesji ocen, required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;

_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error(exec('error','!poc_ofo_rfoc'));
   return()
?};

_id:=exec('ref2uid','#table',_in.ZO_PROC);

_result:='';

{? _id=''
|| _result:=exec('error','!poc_ofo_rfoc')

|? _mp.isMicro()
|| _mp.keyRef(_id);
   exec('select','!poc_ofo_rfoc',_in.ZO_PROC,_mp);
   _mp.delRef(_id);
   _mp.cancel()

|| _value:=exec('select','!poc_ofo_rfoc',_in.ZO_PROC,_mp);
   {? type_of(_value)=type_of(0)
   || {? _value<>0
      || _mp.done()
      || _mp.keep()
      ?}
   || _result:=_value
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg('%1'@[_result])
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Rejestracja formularzy ocen - formuła opisu zadania.
::   WE:
::   WY: tekst do wyświetlenia na liście zadań
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_tab:=exec('desc_tab','phr_dane');

ZO_PROG.cntx_psh();
ZO_PROC.cntx_psh();
ZO_PROC.prefix();
{? ZO_PROC.seek(_in.ZO_PROC)
|| ZO_PROC.ZO_PROG();
   _tab.ZAW_DANE:='T';
   _tab.NAZWA:=ZO_PROG.NAZWA;
   _tab.OKRES_OD:=$ZO_PROC.OKRES_OD;
   _tab.OKRES_DO:=$ZO_PROC.OKRES_DO
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();

{? _tab.ZAW_DANE='T'
|| 'Rejestracja formularzy ocen sesji od %1 do %2 programu "%3"'@@[_tab.OKRES_OD,_tab.OKRES_DO,_tab.NAZWA]
|| 'Rejestracja formularzy ocen. Błędne określenie sesji.'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestracja formularzy ocen niemożliwa.\nNie znaleziono sesji ocen.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Wybór sesji ocen właściwych dla programu.
::   WE: _a [_ZO_PROC] - wskazanie sesji ocen
::       _b [OBJECT] - wskazanie menadżera procesów
::   WY: 1 - czynność została zakończona
::       0 - czynność należy podtrzymać na liście zadań
::       [STRING] - komunikat błędu
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
exec('init','poc');

_ctx:=obj_new(
   'ZO_OSOBA','ZO_TEST',
   'FORM_NDX',
   'BTN_DONE',
   'ZO_KOMP_WND'
);
_ctx.ZO_OSOBA:=null;
_ctx.ZO_TEST:=null;
_ctx.FORM_NDX:=obj_new('KOGO','KTO');
_ctx.FORM_NDX.KOGO:='KOGO';
_ctx.FORM_NDX.KTO:='KTO';
_ctx.ZO_KOMP_WND:=exec('zo_komp_wnd','!poc_ofo_rfoc');
params_set('ctx',_ctx);

ZZ_POM.cntx_psh();
ZO_PROC.cntx_psh();
ZO_PROC.prefix();
{? ZO_PROC.seek(_a)
|| _wnd:=exec('setup_wnd','!poc_ofo_rfoc',_b);
   ZO_OSOBA.cntx_psh();
   ZO_OSOBA.win_sel(_wnd);
   ZO_OSOBA.hdr_sel(' (%1)'[exec('record','#to_string',_a,1)]);
   _ret:=ZO_OSOBA.select();
   ZO_OSOBA.cntx_pop()
?};
ZO_PROC.cntx_pop();
ZZ_POM.cntx_pop();

_ret


\setup_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Tworzy okienko czynności.
::   WE: _a [OBJECT] - wskazanie menadżera procesów
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';

_bar:=ZZ_POM.mk_edit(,0);
_bid:=ZZ_POM.win_ebtn(_bar,'text=%1,display=1'[exec('text_red_zakoncz','#window','PKD_A')],
   "sel_exit();''"
);
params_get().ctx.BTN_DONE:=_bid;
ZZ_POM.btn_opt(_bid,'tooltip='+exec('help_act_zakoncz','#window'));
_bid:=ZZ_POM.win_ebtn(_bar,'text=%1,display=1'['&Anuluj'@],
   "'key:Esc'"
);
ZZ_POM.btn_opt(_bid,'tooltip='+exec('help_act_esc','#window'));

_wnd:=ZO_OSOBA.grp_make('Formularze ocen'@,
:  przed wypełnieniem
   "  params_set(params_get());
      ZO_OSOBA.index('ZO_OSOBA');
      ZO_TEST.index('ZO_TEST');
      ZZ_KOMP.cntx_psh();
      1
   ",
:  identyfikator
   ,
:  położenie
   ,,
:  przy zamknięciu
   "  ZZ_KOMP.f_clear();
      ZZ_KOMP.cntx_pop()
   ",,
:  tryb wyświetlania
   'maximized'
);

: ----------
: Oceniający
: ----------
ZO_OSOBA.grp_sel(_wnd,ZO_OSOBA,'WER_C','Oceniający'@,
:  po odświeżeniu
   "  _par:=params_get();
      params_set(_par);
      ZO_OSOBA.actions_grayed('WER_C',{? ZO_PROC.Z='T' || 'GDUY:GDY' || '' ?});
      grp_disp(ZO_FORM,'KOGO_C',1,1)
   ",
:  położenie i wysokość
   ,,12,
:  przed obsługą
   "  _par:=params_get();
      params_set(_par);
      ZO_OSOBA.prefix('E',ZO_PROC.ref());
      {? _a
      || ZO_OSOBA.seek(_par.ctx.ZO_OSOBA);
         ZZ_POM.KREATORY:='#POC_OFO_RFOC#ZO_OSOBA#WER#'
      ?}
   ",
:  po obsłudze
   "  {? _a
      || params_get().ctx.ZO_OSOBA:=ZO_OSOBA.ref();
         ZZ_POM.KREATORY:=''
      ?}
   ",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode,
:  identyfikator
   'zo_osoba_wer_c',
:  okno główne
   1
);

: formularze ocen
ZO_OSOBA.tab_splt(_wnd,,'horizontal','bottom');
ZO_OSOBA.grp_sel(_wnd,ZO_FORM,'KOGO_C',,
:  po odświeżeniu
   "  params_set(params_get());
      exec('zo_form_akcje','poc','KOGO_C');
      grp_disp(ZO_KOMP,params_get().ctx.ZO_KOMP_WND,1,1)
   ",
:  położenie i wysokość
   ,,8,
:  przed obsługą
   "  _par:=params_get();
      params_set(_par);
      ZO_FORM.index(_par.ctx.FORM_NDX.KOGO);
      {? grp_empty(ZO_OSOBA,'WER_C',1)=1
      || ZO_FORM.prefix(null);
         return('#disable')
      || ZO_FORM.prefix(ZO_OSOBA.ref())
      ?}
   ",
:  po obsłudze
   "params_get().ctx.FORM_NDX.KOGO:=ZO_FORM.index('?')",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

: oceniane kompetencje
ZO_OSOBA.tab_splt(_wnd,'bottom','vertical','right');
ZO_OSOBA.grp_sel(_wnd,ZO_KOMP,params_get().ctx.ZO_KOMP_WND,,
:  po odświeżeniu
   "  params_set(params_get());
      _hide:={? ZO_FORM.Z='T' | SEEK.ZO_PROC().Z='T' || 'DU:D' || '' ?};
      ZO_FORM.cntx_psh();
      {? ZO_FORM.first()
      || {? ~exec('form_komp','phr_dane',ZO_FORM.ZO_TEST().ZZ_DOK)
         || {? _hide=''
            || _hide+='D:D'
            ?}
         ?}
      ?};
      ZO_FORM.cntx_pop();
      ZO_KOMP.actions_grayed(params_get().ctx.ZO_KOMP_WND,_hide)
::      grp_disp(ZO_NOTA,'WER_C',1,1)
   ",
:  położenie i wysokość
   ,,8,
:  przed obsługą
   "  params_set(params_get());
      {? grp_empty(ZO_FORM,'KOGO_C',1)
      || ZO_KOMP.prefix(null);
         return('#disable')
      || ZO_KOMP.prefix(ZO_FORM.ref())
      ?};
      {? _a
      || exec('zz_komp_ogranicz','phr_widok',ZO_PROC.OKRES_DO,'ZZ_HIST')
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

: --------
: Oceniani
: --------
ZO_OSOBA.grp_sel(_wnd,ZO_TEST,'WER_C','Oceniani'@,
:  po odświeżeniu
   "  params_set(params_get());
      ZO_TEST.actions_grayed('WER_C',{? ZO_PROC.Z='T' || 'GDUY:GDY' || '' ?});
      grp_disp(ZO_FORM,'KTO_C',1,1)
   ",
:  położenie i wysokość
   ,,12,
:  przed obsługą
   "  _par:=params_get();
      params_set(_par);
      ZO_TEST.prefix(ZO_PROC.ref());
      {? _a
      || ZO_TEST.seek(_par.ctx.ZO_TEST);
         ZZ_POM.KREATORY:='#POC_OFO_RFOC#ZO_TEST#WER#'
      ?}
   ",
:  po obsłudze
   "  {? _a
      || params_get().ctx.ZO_TEST:=ZO_TEST.ref();
         ZZ_POM.KREATORY:=''
      ?}
   ",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode,
:  identyfikator
   'zo_test_wer'
);

: formularze ocen
ZO_OSOBA.tab_splt(_wnd,,'horizontal','bottom');
ZO_OSOBA.grp_sel(_wnd,ZO_FORM,'KTO_C',,
:  po odświeżeniu
   "  params_set(params_get());
      exec('zo_form_akcje','poc','KTO_C');
      grp_disp(ZO_KOMP,params_get().ctx.ZO_KOMP_WND,1,1)
   ",
:  położenie i wysokość
   ,,8,
:  przed obsługą
   "  _par:=params_get();
      params_set(_par);
      ZO_FORM.index(_par.ctx.FORM_NDX.KTO);
      {? grp_empty(ZO_TEST,'WER_C',1)=1
      || ZO_FORM.prefix(null);
         return('#disable')
      || ZO_FORM.prefix(ZO_TEST.ZO_OSOBA)
      ?}
   ",
:  po obsłudze
   "params_get().ctx.FORM_NDX.KTO:=ZO_FORM.index('?')",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

: oceniane kompetencje
ZO_OSOBA.tab_splt(_wnd,'bottom','vertical','right');
ZO_OSOBA.grp_sel(_wnd,ZO_KOMP,params_get().ctx.ZO_KOMP_WND,,
:  po odświeżeniu
   "  params_set(params_get());
      _hide:={? ZO_FORM.Z='T' | SEEK.ZO_PROC().Z='T' || 'DU:D' || '' ?};
      ZO_FORM.cntx_psh();
      {? ZO_FORM.first()
      || {? ~exec('form_komp','phr_dane',ZO_FORM.ZO_TEST().ZZ_DOK)
         || {? _hide=''
            || _hide+='D:D'
            ?}
         ?}
      ?};
      ZO_FORM.cntx_pop();
      ZO_KOMP.actions_grayed(params_get().ctx.ZO_KOMP_WND,_hide)
::      grp_disp(ZO_NOTA,'WER_C',1,1)
   ",
:  położenie i wysokość
   ,,8,
:  przed obsługą
   "  params_set(params_get());
      {? grp_empty(ZO_FORM,'KTO_C',1)
      || ZO_KOMP.prefix(null);
         return('#disable')
      || ZO_KOMP.prefix(ZO_FORM.ref())
      ?};
      {? _a
      || exec('zz_komp_ogranicz','phr_widok',ZO_PROC.OKRES_DO,'ZZ_HIST')
      ?}
   ",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

{? ~_a.isMicro()
|| ZO_OSOBA.grp_splt(_wnd,,'horizontal','bar');
   ZO_OSOBA.grp_edit(_wnd,ZZ_POM,_bar,,,,,,'maximized')
?};

_wnd


\setup_btn_done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Ustala stan przycisku "Zakończ".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_on:=(ZO_FORM.size()<>0 | ZO_TEST.size()<>0 | ZO_OSOBA.size()<>0);
ZZ_POM.btn_opt(
   params_get().ctx.BTN_DONE,
   'tooltip=%1,state=%2'[exec('help_act_zakoncz','#window'),{? _on || 'normal' || 'grayed' ?}]
);
~~

\zo_komp_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Formuła tworzy tymczasowe okno wertowania dla tabeli ZZ_KOMP umożliwiające przeglądanie zestawu ocenianych
::       kompetencji.
::   WE:
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_win:=ZO_KOMP.mk_sel('Oceniane kompetencje'@,'P',,'oc_komp');
ZO_KOMP.win_fld(_win,,'ZZ_KOMP','NAZWA',,-30);
ZO_KOMP.win_fld(_win,,'W_TYP',,,-4);
ZO_KOMP.win_fld(_win,,'W_KOMP',,,-4);
ZO_KOMP.win_act(_win,,'Formuła','Dołącz'@,,,,"params_exec('zo_komp_dolacz_a','poc')");
ZO_KOMP.win_act(_win,1,'Formuła','Dołącz'@,,,,"params_exec('zo_komp_dolacz_a','poc')");
ZO_KOMP.win_act(_win,,'Formuła','Usuń'@,,,"params_exec('zz_xxx_usun_b','phr_widok')",
                                          "params_exec('zz_xxx_usun_a','phr_widok')",
                                          ,1,
                                          "params_exec('zz_xxx_usun_bg','phr_widok')",
                                          "params_exec('zz_xxx_usun_ag','phr_widok')");
ZO_KOMP.win_act(_win,,'Formuła','Ocena kompetencji'@,,,,
               "ZO_NOTA.actions_grayed('WER_C',
                                        {? ZO_FORM.Z='T' | SEEK.ZO_PROC().Z='T' || 'DU:D'
                                        |? ZO_NOTA.size()>1 || 'D'
                                        || ''
                                        ?});
                ZO_NOTA.prefix(ZO_KOMP.ref());
                ZO_NOTA.win_sel('WER_C');
                ZO_NOTA.select()");
ZO_KOMP.win_act(_win,,'Kolejność');
ZO_KOMP.win_act(_win,,'Okienko',,,,"exec('zo_komp_xxx_ob','poc')","exec('zo_komp_xxx_oa','poc')");
ZO_KOMP.win_btn(_win,'text=Dołącz','menu:D');
ZO_KOMP.win_btn(_win,'text=Usuń','menu:U');
_win

:Sign Version 2.0 jowisz:1045 2023/11/15 14:14:34 72350e133b0ae4ab8e36bb8c52736c127316500159c3725f714522c7cc31a8f45dbb7b7c126ac0933a36a2fb2b184c7972286cd731b0a064e0cd25e420280132206e27f796e521501cadff84d08de56b1d7a087308aa6e127a4bfa2e091edf1e7ae9d89d8a8b944594bdaead00c286366a56d3b4199201e6679b567c418ac078

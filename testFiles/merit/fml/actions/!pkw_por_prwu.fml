:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkw_por_prwu.fml
:: Utworzony: 06.12.2016
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Obsługa czynności webTerm-owej PKW_POR_PRWU - Rej. wn. url. - podwładny
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Rejestracja wniosku o urlop podwładnego(webTerm) - obsługa procesowości główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::  Parametr nie jest obowiązkowy, aby czynność mogła być startowa.
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=N, keyref=N
::
::# kind=WEW, symbol=NWU, type=_NWU, name=Wskazanie wniosku, required=N
::
::# kind=WY, symbol=NWU, type=_NWU, name=Wskazanie wniosku, required=N
::
params_exec('nwu_main','wnioski_urlopowe')


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja wniosku o urlop (webTerm) - główna formuła czynności.
::   WE:  _a  [STRING]    - Miejsce wywołania (Proc, Todo, Area).
::       [_b] [REFERENCE] - Wskazanie na instancję procesu (dla Proc i Todo).
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='Area' | _a='Proc' | _a='Todo')
|| _path:=_a
|| return()
?};

{? _path='Area'
:: Uruchomienie z obszaru roboczego. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
|| exec('run_from_area','wnioski_urlopowe')

|? _path='Todo'
:: Uruchomienie z listy zadań (czynność w procesie, wywołana z parametrem - wskazaniem pracownika).  - - - - - - - - - -
|| {? var_pres('_b')=type_of(null()) & _b<>null() & ref_tab(_b)=BI_PREL & exec('env_wt','b_proces') & exec('init','pkw')
   || _bi_prel:=_b
   || return()
   ?};

   exec('run_from_todo','wnioski_urlopowe',_bi_prel)

|? _path='Proc'
:: Uruchomienie z pulpitu (czynność startowa).   - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
|| {? var_pres('_b')=type_of(null()) & _b<>null() & ref_tab(_b)=B_PREL & exec('env_wt','b_proces') & exec('init','pkw')
   || _b_prel:=_b
   || return()
   ?};

:: Zakładamy, że czynność uruchamiana z pulpitu zawsze wykonywana jest na rzecz użytkownika podwładnego, a nie
:: zalogowanego pracownika (bez względu na ustawienia w P_WEB_CX).
:: Dlatego też obsługa jest wzbogacona o wybór kontekstu pracownika podwładnego - rekord (umów) typu P.

   _fml_b:="exec('p_web_cx_set_prefix','p_web',,'RODZAJ','P');
::       jeżeli ścieżka to start procesu to pokazujemy okna wyboru z przebiegami pracowników podwładnych
         {? P_WEB_CX.size()=0
         || FUN.info('Brak pracowników podwładnych. Czynność nie może zostać uruchomiona.'@);
            0
         || 1
         ?}";

   exec('run_from_proc','wnioski_urlopowe','PKW_POR_PRWU',_b_prel,_fml_b)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Rejestracja wniosku o urlop podwładnego (webTerm) - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=params_exec('desc','wnioski_urlopowe',1);
{? _tab.ZAW_DANE='T'
|| 'Zarejestruj wniosek o urlop: %1 %2, Numer teczki - %3, Jednostka - %4, Stanowisko %5'@@
      [_tab.NAZWISKO,_tab.PIERWSZE,_tab.T,_tab.UD_SKL,_tab.STN]
|| 'Zarejestruj wniosek o urlop'@@
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:18 b6f601ee52b25c21b90f2f035665ef7ada44144116ebff8da0de0eb27baeb40e7569366669af913cd78c3b9cb9b38d0ef1b8c333a3c18196d2559bfc1b5ac0a54d15b012fca8a3c700c616dfe7c9de9e7197921994100512e54daff8e1e524e1d6ed79eebabeda8016b9715543f9b6d6e0bd553d339c76b61b1f9b458966351a

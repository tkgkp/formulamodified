:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_tec_drts.fml
:: Utworzony: 02.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_TEC_DRTS - Rejestrowanie surowców karty technologicznej
::            Uwaga: większość kodu jest wspólna z czynnością TTE_WTE_DRTS - w przypadku zmian modyfikować oba pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności rejestrowania listy surowców karty technologicznej (TTE_TEC_DRTS)
::       Zadaniem formuły jest zredagowanie listy surowców z zamiennikami i dokumentami
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

:: UPRAWNIENIA
::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na kartę technologiczną, required=T, keyref=T
{? var_pres('TKTL',_in)<>type_of(~~) & var_pres('TKTL',_in)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_in)=type_of(~~) || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=TKTL, type=_TKTL, name=Wskazanie na kartę technologiczną, required=N
{? var_pres('TKTL',_out)<>type_of(~~) & var_pres('TKTL',_out)<>type_of(null()) || return() ?};

_akcja:=_mp.akcja();
{? _akcja='START'
||
:: Wejście do okienka w ramach zakładek - uruchomienie czynności
   _uidref:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"TKTL.uidref()",'');
   {? _uidref<>''
   || _mp.keyRef(_uidref);
      _mp.lock()
   ?};
   ~~

|? _akcja='STOP'
||
:: Wyjście z okienka w ramach zakładek - anulowanie czynności
   {? _mp.isMicro()
   || _uidref:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"TKTL.uidref()",'');
      {? _uidref<>''
      || _mp.delRef(_uidref);
         _mp.cancel()
      ?}
   ?};
   _mp.unlock()
|? _mp.isMicro()
||
:: Obsługa poza procesem

:: Zakończenie rejestrowania wywołane z menu okna wertowania TKTL
   {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
   || {? (_ok:=exec('valid_mat','tech_mater',_in.TKTL))
            &
         (_ok=2 | FUN.ask('Czy zakończyć rejestrację surowców dla całej karty technologicznej?'@))

      || exec('FindAndGet','#table',TKTL,_in.TKTL,,"STAT_S:='T'; STAN:='P'; put()")
      ?}

:: Obsługa funkcji 'Surowce' wywołanej z menu okna wertowania TKTL (karta z surowcami przypisanymi do nagłówka)
   |? _akcja='SEL_M'
   || exec('tmat_main','tech_mater',_in.TKTL,null(),0,1,0,0)

:: Obsługa funkcji 'Surowce' wywołanej z menu okna wertowania TKTL (karta z surowcami przypisanymi do operacji)
   |? _akcja='SEL_L'
   || exec('tmat_main','tech_mater',_in.TKTL,null(),1,1,0,0)

:: Obsługa funkcji 'Surowce' wywołanej z menu okna wertowania TOPER (karta z surowcami przypisanymi do operacji)
   |? _akcja='SEL_O'
   || exec('tmat_main','tech_mater',_in.TKTL,_context.oper,0,0,_context.used,_context.zakl)
   ?}
||
:: Obsługa w procesie
   exec('tmat_in_process','!tte_tec_drts',_in,_int,_out,_mp,_context)
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności rejestrowania listy surowców dla karty technologicznej (TTE_TEC_DRTS)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj surowce dla karty technologicznej %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"'Zredaguj surowce dla karty technologicznej %1 wer. %2'[NRK,WER]",'')

:: jest parametr wejściowy to ustawiam odpowiedniego TKTL
|? var_pres('TKTL',_in)
|| _tmp:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj surowce dla karty technologicznej %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"'Zredaguj surowce dla karty technologicznej %1 wer. %2'[NRK,WER]",'')

?};
_desc


\tmat_in_process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła rejestrowania listy surowców dla karty technologicznej
::       Używana przez exec('main','!tte_tec_drts')
::   WE: _a - [obj_new] - parametry wejściowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::       _e - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_int:=_b;
_out:=_c;
_mp:=_d;
_context:=_e;

_torw:=VAR.A_TORW:='T';
_arch:=VAR.ARCH:='N';

_akcja:=_mp.akcja();

_can_continue:=1;
_clean_result:=params_exec('clean','!tte_tec_drts',_mp,_in);
_can_continue:=_clean_result.RESULT;
_tktl:=_clean_result.TKTL;

_ok:=0;
_res:=~~;

{? _can_continue>0
||
   {? _tktl<>null()
   ||
      {? exec('FindAndGet','#table',TKTL,_tktl,,"TORW",'')<>_torw
      || _msg:='Niezgodność wywołania czynności.\nNie można redagować surowców dla wzorca technologii.';
         FUN.emsg(_msg);
         _mp.error(_msg)

      |? exec('FindAndGet','#table',TKTL,_tktl,,"ARCH",'')<>_arch
      || _msg:='Niezgodność wywołania czynności.\nNie można redagować surowców dla karty archiwalnej.';
         FUN.emsg(_msg);
         _mp.error(_msg)

      || exec('tktl_cntx_psh','tech_common');
         exec('tktl_use','tech_common',ref_name(_tktl)+3);
         TKTL.clear();

         {? TKTL.seek(_tktl)
         ||
            _mp.keyRef(TKTL.uidref());
            _mp.descTodo();

::          Zmienne
            VAR.A_KTL:=TKTL.ref();
            VAR.GRUPA:='N';

::          Właściwa obsługa
::          Zakończenie rejestrowania wywołane z menu okna wertowania TKTL
            {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
            || {? (_ok:=exec('valid_mat','tech_mater',TKTL.ref()))
                     &
                  (_ok=2 | FUN.ask('Czy zakończyć rejestrację surowców dla całej karty technologicznej?'@))

               || TKTL.STAT_S:='T';
                  TKTL.STAN:='P';
                  TKTL.put()
               ?}

::          Lista surowców dla nagłówka - z menu okna wertowania kart technologicznych
            |? _akcja='SEL_M'
            || exec('tmat_main','tech_mater',TKTL.ref(),null(),0,1,0,0)

::          Lista surowców zbiorczo - z menu okna wertowania kart technologicznych
            |? _akcja='SEL_L'
            || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,0,0)

::          Lista surowców dla operacji - z menu okna wertowania operacji
            |? _akcja='SEL_O'
            || exec('tmat_main','tech_mater',TKTL.ref(),_context.oper,0,0,_context.used,_context.zakl)

::          Lista TODO
            |? _akcja=''
            ||
::             Już wcześniej zakończono redagowanie surowców
               {? TKTL.STAT_S='T'
               || FUN.info('Zakończono rejestrację surowców dla całej karty technologicznej.'@)

::             Nie zakończono jeszcze redagowania nagłówka (być może usunięto zatwierdzenie)
               |? TKTL.STAT_N='N'
               || {? exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTE')
                  || _choice:=FUN.choice('Nie zakończono rejestracji nagłówka karty.'@,,'Zakończ nagłówek i redaguj'@,'Podgląd'@);
                     {? _choice=1
                     || _args:=exec('mp_run_a','#b__box');
                        _args.ACT_UID:='TTE_TEC_DRTE';
                        _args.UIDREF:=TKTL.uidref();
                        _args.AKCJA:='ZAKOŃCZ_BEZ_PYTANIA';
                        exec('mp_run','#b__box',_args)
                     ?};
                     {? _choice>0
                     ||
::                      Zawsze wyświetla wszystkie surowce
                        {? 1
::                           TKTL.TYP().SUR='K'
                        || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,_choice=2,0)
                        || exec('menu_start','tech_head');
                           exec('tree','tech_oper',4);
                           exec('menu_stop','tech_head')
                        ?}
                     ?}
                  || _choice:=FUN.info('Nie zakończono rejestracji nagłówka karty.\nModyfikacje niemożliwe.'@,,'Podgląd'@,'Anuluj'@);
                     {? _choice>0
                     ||
::                      Zawsze wyświetla wszystkie surowce
                        {? 1
::                           TKTL.TYP().SUR='K'
                        || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,1,0)
                        || exec('menu_start','tech_head');
                           exec('tree','tech_oper',4);
                           exec('menu_stop','tech_head')
                        ?}
                     ?}
                  ?}
               ||
::                Zawsze wyświetla wszystkie surowce
                  {? 1
::                     TKTL.TYP().SUR='K'
                  || exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,0,0)
                  || exec('menu_start','tech_head');
                     exec('tree','tech_oper',4);
                     exec('menu_stop','tech_head')
                  ?}
               ?}
            ?};

            TKTL.clear();
            {? TKTL.seek(_in.TKTL)
            ||
::             Ustawiony status - czynność zostaje zakończona
               {? TKTL.STAT_S='T'
               || _out.TKTL:=TKTL.ref();
                  _mp.save(,_out);
                  _mp.done();
                  _res:='#CLOSE#'

               ?}
            ?}
         ?};
         exec('tktl_cntx_pop','tech_common')

      ?}

   || _msg:='Nie przekazono karty technologicznej.';
      FUN.emsg(_msg);
      _mp.error(_msg)
   ?}
?};
_res


\action_menu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie surowców dla karty technologicznej - akcja z menu okna wertowania kart technologicznych (TKTL)
::       gdy surowce przypisane są do karty (pełne możliwości redagowania)
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
:: Teraz zawsze ta ścieżka, wyświetlane wszystkie operacje (bez prefixu)
{? 1
:: TKTL.TYP().SUR='O'
|| exec('action_list','!tte_tec_drts')

|? TKTL.STAT_N='N'
|| set_help(exec('set_help','#help','TTE_TEC_DRTS'));
   exec('tmat_main','tech_mater',TKTL.ref(),null(),0,1,0,0)

|? TKTL.ARCH='N' & TKTL.STAT_S='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTS')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_TEC_DRTS';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='SEL_M';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_TEC_DRTS'));
   exec('tmat_main','tech_mater',TKTL.ref(),null(),0,1,0,0)
?}


\action_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie surowców dla karty technologicznej - akcja z menu okna wertowania kart technolgicznych (TKTL)
::       gdy surowce przypisane są do operacji (podgląd i ograniczone możliwości redagowania)
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| VAR.A_OP:=null();
   set_help(exec('set_help','#help','TTE_TEC_DRTS'));
   exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,0,0)

|? TKTL.ARCH='N' & TKTL.STAT_S='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTS')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_TEC_DRTS';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='SEL_L';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_TEC_DRTS'));
   exec('tmat_main','tech_mater',TKTL.ref(),null(),1,1,0,0)
?}


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zakończenie rejestrowania surowców dla karty technologicznej - obsługa przycisku w oknie kart technologicznych
::       (obsługa z pytaniem)
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_S='N'
|| {? exec('tktl_lock','tech_common',,'S')
   || _args:=exec('mp_run_a','#b__box');
      _args.ACT_UID:='TTE_TEC_DRTS';
      _args.UIDREF:=TKTL.uidref();
      _args.AKCJA:='ZAKOŃCZ_Z_PYTANIEM';
      _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
      exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

      exec('mp_run','#b__box',_args);

      exec('tktl_unlock','tech_common',,'S')
   ?}

|| FUN.info('Zakończono rejestrację surowców dla karty technologicznej.\nModyfikacje niemożliwe.'@)
?};
'key:Esc'


\tmat_dalej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obsługa przycisku 'Zakończ' w oknie wertowania surowców karty technologicznej
::       Zatwierdza surowce i popycha proces dalej
::       Kontekst wywołania - rekord TKTL
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_S='T'
|| FUN.info('Rejestracja surowców karty technologicznej została już zakończona.'@)
|? TKTL.STAT_N='N'
|| FUN.info('Nie zakończono redagowania nagłówka karty technologicznej.'@)
|? (_ok:=exec('valid_mat','tech_mater',TKTL.ref()))
      &
   (_ok=2 | FUN.ask('Czy zakończyć rejestrację surowców dla całej karty technologicznej?'@))
||
:: Rekord mógł być zmodyfikowany przez inne czynności, więc go ponownie wczytuję
   {? TKTL.get()
   || TKTL.STAT_S:='T';
      TKTL.STAN:='P';
      {? TKTL.put()
      || sel_exit()
      ?}
   ?}
?};
~~


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('tech_clean','tech_common',_mp,_in)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 3b5f4618e9b865c47c11e435a492d610189a1ecc8cc53a9abf66d1d4016acd72775071c5407e5db0b5c4bd43744429174fea4232bf357139a3f3fa4b249a2faa0576f0ec79bcf1ccf0ecbecd65745747ba392618858760bb5da801a2a40575f7b650738069ce14fefb268a51333c5933a00ed865b357ccbb443ecb6b984d651e

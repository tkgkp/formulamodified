:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_faw_darp.fml
:: Utworzony: 07.06.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_DARP - Rejestracja wniosku w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rejestracja wniosku w obiegu - główna formuła czynności OBE_FAW_DARP
::----------------------------------------------------------------------------------------------------------------------
::# portal=STD_PC_E_LeaveLimit4Home_NewAbsRequest_PRC; ^
::# STD_PC_E_Family_RegisterMember_SLO;STD_PC_E_PayoutInfo_ChangeAccount_PRC; ^
::# STD_PC_E_PersonalInfo_NewRequest_PRC; ^
::# STD_PC_E_TaxOfficesInfo_ChangeTaxOffice_PRC; ^
::# STD_PC_E_AbsenceRequest_Edit_PRC;STD_PC_E_REQUESTTYPE_SLO; ^
::# STD_PC_E_AbsenceRequest_New_PRC;STD_PC_E_AbsenceRequest_SetReadyForAccept_PRC; ^
::# STD_PC_E_AbsenceRequest_SLO;STD_PC_E_AbsenceRequest_SLO_D;STD_PC_E_AbsenceRequestDetail_SLO; ^
::# STD_PC_E_PERSONATTACHMENT_ADD_SLO_PRC;STD_PC_E_PERSONATTACHMENT_SLO;STD_PC_E_PERSONATTACHMENT_SLO_D; ^
::# STD_PC_E_PERSONREQUESTDETAIL_SLO;STD_PC_E_PERSONREQUESTSTATUS_SLO;STD_PC_E_REQUEST_SLO;STD_PC_E_REQUEST_SEND_PRC; ^
::# STD_PC_E_PERSONATTACHMENT_GETSIG_SLO_PRC;STD_PC_E_PERSONATTACHMENT_OVR_SLO_PRC; ^
::# STD_PC_E_PERSONATTACHMENT_SIG_SLO_PRC;STD_PC_E_PERSONATTACHMENT_SIGHND_SLO_PRC; ^
::# STD_PC_E_PERSONATTACHMENT_SIGSMP_SLO_PRC; ^
::# STD_PC_E_PERSONATTACHMENT_MYCERT_SLO_PRC;STD_PC_E_PERSONMYCERT_ADDPDF_SLO_PRC; ^
::# STD_PC_E_PERSONMYCERT_ADDSIMPLY_SLO_PRC;STD_PC_E_PERSONMYCERT_DEL_SLO_PRC;STD_PC_E_PERSONMYCERT_SLO; ^
::# STD_PC_E_PersonRequest_GenerateReport_PRC; ^
::# STD_PC_S_PersonRequestCostShare_SLO;STD_PC_S_PersonRequestCostShare_SLO_I;STD_PC_S_PersonRequestCostShare_SLO_U; ^
::# STD_PC_S_PersonRequestInfo_SLO;STD_PC_S_PersonRequestCostShare_PRC_UndoDocStatus; ^
::# STD_PC_S_CostShare_SLO;STD_PC_S_CostShare_SLO_D;STD_PC_S_CostShare_SLO_I;STD_PC_S_CostShare_SLO_U; ^
::# STD_PC_E_PersonOtherFamily_SLO;STD_PC_E_PersonOtherFamily_SELECT_PRC

::# parses=exec('parses_edokum','obiegi')
::# access=exec('uprawnienia_rej','obiegi')
::# properties=TODOTRIG,~RELEASE

:: PARAMETRY WE:
::# kind=WE, symbol=FORM_PRZ, type=STRING, name=Formuła wykonywana przy kończeniu rejestracji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_WAL, type=STRING, name=Formuła walidacyjna przy kończeniu rejestracji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_ADD, type=STRING, name=Formuła wykonywana po wprowadzeniu nagłówka dokumentu, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=ED_PODZ, type=STRING, name=Edycja podziałów controllingowych, required=N, fml_val="exec('czy_ed_podz','obiegi')"
::# kind=WE, symbol=ED_INND, type=STRING, name=Edycja informacji dodatkowych, required=N, fml_val="exec('czy_inn_dan','obiegi')"
::# kind=WE, symbol=ED_PROJ, type=STRING, name=Edycja projektów, required=N, fml_val="exec('czy_ed_proj','obiegi')"
::# kind=WE, symbol=TYP_DOK, type=STRING, name=Typ dokumentu, required=N, fml_val="exec('wybor_typu','obiegi',2,_a)", fml_exp="exec('etypy_fml_exp','obiegi2',2,_a)"
::# kind=WE, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N, keyref=T
::# kind=WE, symbol=KONT_BUD, type=STRING, name=Kontrola budżetu, required=N, fml_val="exec('kont_bud','obiegi',_a)"
::# kind=WE, symbol=M_WYW, type=STRING, name=Miejsce wywołania, required=N, fml_val="exec('wyw_miejsce','obiegi2')"
::# kind=WE, symbol=R_WYW, type=STRING, name=Rodzaj wywołania, required=N, fml_val="exec('wyw_rodzaj','obiegi2')"

::# kind=WE, symbol=TYPY_ZAL, type=MEMO, name=Typy załączników i ich podpisy, required=N, fml_val="exec('typy_zal_akc','portal_paperless',_a)"

::# kind=WE, symbol=B_ROLEF, type=STRING, name=Rola użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=B_ROLE, type=STRING, name=Domyślna rola użytkowników, required=N, fml_val="exec('rola','obiegi',_a)", fml_exp="exec('b_role_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=BLZMROL, type=STRING, name=Blokada zmiany roli, required=N, fml_val="exec('blzmrol','obiegi')"
::# kind=WE, symbol=BLZMUSR, type=STRING, name=Blokada zmiany uzytkowników, required=N, fml_val="exec('blzmusers','obiegi')"
::# kind=WE, symbol=USR_FORM, type=STRING, name=Wybór użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=JEDEN_OP, type=STRING, name=Obsługuje jeden operator - formuła, required=N, fml_val="exec('jeden_op','obiegi2')"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N
::# kind=WY, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WY, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WY, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WY, symbol=EM_NAD, type=STRING, name=Adres e-mail użytkownika rejestrującego (nadawca), required=N
::# kind=WY, symbol=EM_ODB, type=MEMO, name=Adres e-mail użytkownika rejestrującego (odbiorca), required=N
::# kind=WY, symbol=EM_NAG, type=STRING, name=Nagłówek wiadomości e-mail, required=N
::# kind=WY, symbol=BUDZ_COND, type=NUMBER, name=Budżet przekroczony warunkowo?, required=N


:: PARAMETRY WEW:
::# kind=WEW, symbol=CZY_ZAK, type=STRING, name=Czy zakończenie czynności, required=N

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_wew.CZY_ZAK:='N'; _mp.save(_wew);
{? _akcja='webTermProcCancel' || return(~~) ?};

{? _akcja='Portal'
|| {? ~_mp.isMicro()
   || _ref:=null;
      {? var_pres('EDOKUM',_we)>0
      || _ref:=_we.EDOKUM
      || _refs:=_mp.getRefs();
         {? obj_len(_refs)>0
         || {! _ii:=1..obj_len(_ref)
            |? _ref=null
            |! {? _refs[_ii]*'skid_v'
               || _ref:=_refs[_ii]
               ?}
            !}
         ?}
      ?};
      params_exec('ustaw_bprel','obiegi');
      EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_ref));
      EDOKUM.prefix();
      {? EDOKUM.seek(_ref)
      || _done:=1;
         {? EDOKUM.PAPERLES
         || exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL);
            _done:=exec('chk_zal','portal_paperless',_we.TYPY_ZAL,0);
            {? _done
            || EDOKUM.STATUS:='s';
               EDOKUM.put()
            ?}
         ?};
         _mp.keyRef(EDOKUM.uidref(),1,0);
         _wy.EDOKUM:=EDOKUM.ref();
         _wy.WAL:=EDOKUM.WAL;
         _wy.NETTO:=EDOKUM.NETTO;
         _wy.BRUTTO:=EDOKUM.WART;
         _wy.DEKR:=EDOKUM.TYP().DEKR;
         _wy.STATUS:='W';
         _wy.EM_NAD:=_wy.EM_ODB:=OPERATOR.USER().EMAIL;
         _wy.EM_NAG:='Zarejestrowano wniosek: '+EDOKUM.ID+', data operacji '+$EDOKUM.DOP;
         _wy.B_PREL:=OBIEGI.B_PREL;
         _wy.B_PRELS:=OBIEGI.B_PRELS;
         _args.context.B_PREL:=OBIEGI.B_PREL;
         {? var_pres('B_PRELS',_args.context)>0
         || _args.context.B_PRELS:=OBIEGI.B_PRELS
         ?};
         EDOKUSER.cntx_psh();
         EDOKUSER.use('skidk_'+(EDOKUM.name()+2));
         EDOKUSER.index('UNIK');
         EDOKUSER.prefix(EDOKUM.ref(),'OBE_FAW_DARP',);
         {? EDOKUSER.first()
         || {!
            |? EDOKUSER.cntx_psh();
               EDOKUSER.prefix();
               EDOKUSER.B_PREL:=OBIEGI.B_PREL;
               EDOKUSER.B_PRELS:=OBIEGI.B_PRELS;
               EDOKUSER.put();
               EDOKUSER.cntx_pop();
               EDOKUSER.first()
            !}
         ?};
         EDOKUSER.cntx_pop();
         _mp.save(,_wy);
         _mp.keep();
         {? _done || _mp.done() ?};
         _args.context.STATUS:=1;
         {? var_pres('FORM_PRZ',_we)>0 & _we.FORM_PRZ<>''
         || _fml:=_we.FORM_PRZ;
            ($_fml)()
         ?}
      ?};
      EDOKUM.cntx_pop()
   || _mp.cancel()
   ?};
   return(~~)
?};
zak_akc_ed:=0;
:: obsługa w jTermie
{? app_info('web_sesid')=''
|| BPMN.END:=0;
   {? ~exec('is_act','#b__box','OBE_FAW_DARP')
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@);
      _mp.cancel(); return(~~)
   |? _mp.isMicro()
   || {? _akcja='Dołącz' | _mp.pathProc()
      || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
      || FUN.info('Wniosek nie jest w trakcie rejestracji lub nie jest obecnie obsługiwany przez zalogowanego użytkownika.'@)
      ?};
      _mp.cancel();
      return(~~)
   ?};
   params_exec('ustaw_bprel','obiegi');
   zakres:=1;
   {? var_pres('EDOKUM',_we)>0 & var_pres('EDOKUM',_wy)<=0
   || _wy.EDOKUM:=_we.EDOKUM;
      _mp.save(,_wy)
   ?};
   {? _akcja='Dołącz' | _mp.pathProc() | _akcja='Dol_dokum'
   ||
:      exec('dol_edokos','obiegi',_we);
      _ref:=exec('add','obe_faw',_we,_akcja='Dol_dokum');
      {? _ref
      || _uidref:=exec('FindAndGet','#table',EDOKUM,_ref,,'uidref()');
         exec('ustaw_edokpar','obiegi',_we,_ref,OBIEGI.B_PREL,OBIEGI.B_PRELS);
         {? var_pres('context',_args)>0 & var_pres('EDOKUM',_args.context)>0
         || _args.context.EDOKUM:=_ref
         ?};
         _mp.keyRef(_uidref);
         _wy.EDOKUM:=_ref;
         _mp.save(,_wy);
         _mp.descTodo();
         _mp.keep()
      || _mp.cancel()
      ?}
   |? _akcja='Popraw' | _mp.pathTodo() | _akcja='Pop_dokum' | _mp.isAutoRun() & var_pres('EDOKUM',_wy)>0
   || exec('ustaw_edokpar','obiegi',_we,_wy.EDOKUM,OBIEGI.B_PREL,OBIEGI.B_PRELS);
      _ref:=exec('put','obe_faw',_we,_wy,_mp.isAutoRun());
      {? _mp.pathTodo() & _ref=null()
      || FUN.info('Nie znaleziono wniosku.'@);
         _we.EDOKUM:=null;
         _mp.save(,_we);
         _mp.error()
      |? _akcja='Pop_dokum'
      || _mp.save(,_wy);
         _mp.keep()
      ?}
   |? _akcja='Usuń'
   || {? exec('del','obe_faw',_we)
      || {? ~(var_pres('STATUS',_we) & _we.STATUS='O')
         || _mp.cancel()
         ?}
      ?}
   |? _akcja='Zakończ' | _akcja='Zamknij'
   || BPMN.END:=1;
      _ref:=_wy.EDOKUM
   |? _akcja='Businesslink'
   || exec('ustaw_edokpar','obiegi',_we,_wy.EDOKUM,OBIEGI.B_PREL,OBIEGI.B_PRELS);
      exec('put','obe_faw',_we,_wy,1);
      BPMN.END:=0;
      {? var_pres('EDOKUM',_we)=type_of(null()) & _we.EDOKUM
         &
         (var_pres('EDOKUM',_wy)<>type_of(null()) | _wy.EDOKUM=null())
      || _wy.EDOKUM:=_we.EDOKUM;
         _mp.save(,_wy)
      ?};
      _mp.keep()
   ?};
   {? BPMN.END & var_pres('_ref')>0 & _ref<>null()
   || EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_ref));
      EDOKUM.prefix();
      {? EDOKUM.seek(_ref)
      || EDOKOS.cntx_psh();
         EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
         {? _akcja='Zamknij'
         || params_exec('zamknij2','obiegi_akc')
         || params_exec('akceptuj','obiegi_akc','W',{? _akcja*'dokum'>0 || 0 || 1 ?})
         ?};
         {? zak_akc_ed & EDOKUM.get()
         || {? EDOKUM.PAPERLES & EDOKUM.OSOBAWWW=''
            || exec('EDOKOS_add','portal_wnioski',EDOKUM.ref(),EDOKUM.USERS,'R');
               exec('EDOKOS_add','portal_wnioski',EDOKUM.ref(),EDOKUM.USERS,'S');
               EDOKUM.STATUS:='s';
               P.cntx_psh();
               P.index('OSOZATR');
               P.prefix(EDOKUM.FIRMA,EDOKUM.DOSTAWCA,);
               {? P.first()
               || EDOKUM.OSOBAWWW:=$P.ref()
               ?};
               P.cntx_pop();
               EDOKUM.put();
               StartProc:=1
            ?};
            exec('ustaw_outw','obiegi_akc',_wy,{? _akcja='Zamknij' || 'Z' || 'A' ?});
            {? EDOKUM.PAPERLES
            || VAR_DEL.delete('StartProc')
            ?};
            _mp.done();
            _mp.save(,_wy)
         ?};
         EDOKOS.cntx_pop()
      ?};
      EDOKUM.cntx_pop()
   ?};
   return(~~)
?};

zak_akc_ed:=0;
budz_cond:=0;
OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
params_exec('ustaw_bprel','obiegi');
{? _akcja='Popraw'
|| params_get().context.EDOKUM;
   _mp.descTodo();
   _mp.keep()
|? _akcja='Usuń'
|| params_get().context.EDOKUM;
   exec('us_dob1','obiegi');
   {? var_pres('STATUS',_we) & _we.STATUS='O'
   || _mp.error()
   || _mp.cancel()
   ?}
|| {? ~exec('is_act','#b__box','OBE_FAW_DARP')
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@);
      _mp.cancel(); return(~~)
   |? _mp.isMicro()
   || {? _akcja='Dołącz' | _mp.pathProc()
      || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
      || FUN.info('Wniosek nie jest w trakcie rejestracji lub nie jest obecnie obsługiwany przez zalogowanego użytkownika.'@)
      ?};
      _mp.cancel();
      return(~~)
   ?};
   SSTALE.AO:=null;
   exec('env_wt','b_proces');
   {? _mp.pathProc()
   || {? SSTALE.AO=null
      || FUN.info('Nie ustawiono parametrów pracy systemu.'@); _mp.cancel(); return(~~)
      ?}
   ?};
   params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
:: sprawdzenie z poziomu webterma
   {? _akcja='chkWT'
   || params_get().context.SPR_WT:=1
:: czynność startowa
   |? _akcja='Dołącz' | _mp.pathProc()
   || exec('dol_edokos','obiegi',_we);
      _mp.keyRef(EDOKUM.uidref());
      _wy.EDOKUM:=EDOKUM.ref(); _mp.save(,_wy);
      _mp.descTodo();
      web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                            'EDOKUM',#EDOKUM.ref()
                            ));
      _mp.keep()
   |? _akcja='Zakończ'
   || _wew.CZY_ZAK:='T'; _mp.save(_wew);
      params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
      params_exec('akceptuj2','obiegi_akc','W');
      {? zak_akc_ed=1 & EDOKUM.get()
      || exec('ustaw_outw','obiegi_akc',_wy,'A');
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _akcja='Zamknij2'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
      params_exec('zamknij2','obiegi_akc');
      {? zak_akc_ed & EDOKUM.get()
      || exec('ustaw_outw','obiegi_akc',_wy,'Z');
         _mp.save(,_wy);
         _mp.done()
      ?}
   ?}
?};
VAR_DEL.delete('zak_akc_ed','budz_cond');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ rejestrację'@@;
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
_we:=_mp.load(exec('kind_in','#b_port'));
_wew:=_mp.load(exec('kind_internal','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));
_edok:=null;
{? var_pres('EDOKUM',_wy)
|| _edok:=_wy.EDOKUM
|? var_pres('EDOKUM',_we)
|| _edok:=_we.EDOKUM
?};
{? _edok
|| _cur:=EDOKUM.ref();
   _ref:=BB.sqlint($_edok); _nam:=form(($_edok)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      EDOKOS.cntx_psh(); EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      EDOKPAR.cntx_psh(); EDOKPAR.use('skidh_'+(EDOKUM.name()+2));
      {? EDOKUM.seek(_ref,_nam)
      || _desc:={? EDOKUM.TYP().W_PORTAL<>'N' | ETYPY.IN_POR
                || exec('obe_faw_darp_desc','portal_wnioski')
                || 'Zakończ rejestrację %1: %2 data operacji %3'@@[(-EDOKUM.TYP().NAZWA),EDOKUM.ID,$EDOKUM.DOP]
                ?};
         params_exec('ustaw_bprel','obiegi');
         {? _set
         || {? var_press('prz_act')<=0 || exec('dol_edokos','obiegi',_we) ?};
            EDOKOS.cntx_psh();
            EDOKOS.index('SZUK'); EDOKOS.prefix(EDOKUM.ref(),EDOKUM.USERS,'W');
            {? EDOKOS.first()
            || _bprel:=EDOKOS.B_PREL; _bprels:=EDOKOS.B_PRELS;
               EDOKOS.index('SZUK2'); EDOKOS.prefix(EDOKUM.ref(),'W');
               {? EDOKOS.size()>1 & EDOKOS.first()
               || {! |?
                     EDOKOS.B_PREL:=_bprel; EDOKOS.B_PRELS:=_bprels; EDOKOS.put();
                     EDOKOS.next()
                  !}
               ?}
            ?};
            EDOKOS.cntx_pop()
         ?};
         {? 3+OBIEGI.B_PRELS<>'OBE'
         || exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set); EDOKUM.get()
         ?}
      ?};
      {? var_pres('CZY_ZAK',_wew)=0 & _we.STATUS='O' || _wew.CZY_ZAK:='N'; _mp.save(_wew) ?};
      {? var_pres('STATUS',_we)>0 & _we.STATUS='O' &
         (var_pres('CZY_ZAK',_wew)<= 0 | (var_pres('CZY_ZAK',_wew)>0 & _wew.CZY_ZAK<>'T'))
      || {? OBIEGI.B_PREL<>'' & var_pres('edokosOset')<=0
         || edokosOset:=1;
            EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
            {? EDOKOS.first()
            || {! |?
                  {? 'TN'*EDOKOS.WID
                  || EDOKOS.STATUS:='O'; EDOKOS.put()
                  ?};
                  EDOKOS.next()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?}
      ?};
      _wp:=EDOKUM.TYP().W_PORTAL;
      {? (_wp<>'N' | ETYPY.IN_POR) & _set & (~EDOKUM.PAPERLES | ~BPMN.END)
      || _stat:={? var_pres('StartProc')>0 || 'T' || 'N' ?};
         EDOKOS.cntx_psh();
         EDOKOS.index('PORTAL'); EDOKOS.prefix(EDOKUM.ref(),'S',OBIEGI.B_PREL,EDOKUM.USERS,_stat);
         {? ~EDOKOS.first()
         || EDOKOS.cntx_psh();
            EDOKOS.index('DISP'); EDOKOS.prefix('S',EDOKUM.ref(),);
            {? EDOKOS.last() & EDOKOS.OPERACJA='Y' & EDOKOS.USERS=EDOKUM.USERS & 1+EDOKOS.B_PREL='R'
            || EDOKOS.del()
            ?};
            EDOKOS.cntx_pop();
            EDOKOS.cntx_psh();
            EDOKOS.index('DISP'); EDOKOS.prefix('S',EDOKUM.ref());
            _oper_nr:={? EDOKOS.last() || EDOKOS.OPER_NUM+1 || 1 ?};
            EDOKOS.cntx_pop();
            EDOKOS.blank(1);
            EDOKOS.EDOKUM:=EDOKUM.ref();
            EDOKOS.USERS:=EDOKUM.USERS;
            EDOKOS.OPERACJA:='Y';
            EDOKOS.DATA:=date();
            EDOKOS.TIME:=time();
            EDOKOS.B_PREL:={? _stat='T' || 'R'+$_oper_nr || OBIEGI.B_PREL ?};
            EDOKOS.B_PRELS:=OBIEGI.B_PRELS;
            EDOKOS.STATUS:=_stat;
            EDOKOS.WID:='S';
            EDOKOS.OPER_NUM:=_oper_nr;
            EDOKOS.prefix();
            EDOKOS.add()
         ?};
         {? EDOKUM.STATUS='D'
         || _reason:=~~;
            EDOKOS.cntx_psh();
            EDOKOS.index('SZUK2');
            EDOKOS.prefix(EDOKUM.ref(),'D');
            {? EDOKOS.last()
            || _reason:=EDOKOS.memo_txt(,1,'UW_DL')
            ?};
            EDOKOS.cntx_pop();
            EDOKOS.prefix();
            EDOKOS.blank(1);
            EDOKOS.EDOKUM:=EDOKUM.ref();
            EDOKOS.USERS:=EDOKUM.USERS;
            EDOKOS.OPERACJA:='P';
            EDOKOS.DATA:=date();
            EDOKOS.TIME:=time();
            EDOKOS.WID:='P';
            EDOKOS.add();
            {? var_pres('_reason')>0
            || EDOKOS.memo_set(_reason,'UW_DL');
               EDOKOS.memo_put();
               EDOKOS.put()
            ?};

            EDOKUM.STATUS:='P';
            EDOKUM.put()
         ?};
         EDOKOS.cntx_pop()
      ?};
      EDOKOS.cntx_pop(); EDOKUM.cntx_pop(); EDOKPAR.cntx_pop();
      {? _cur || EDOKUM.seek(_cur) ?}
   ?}
?};
_desc


\dol_wni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie wniosku
::----------------------------------------------------------------------------------------------------------------------
{? exec('chk_okres','obiegi2',1,1)>=0
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_DARP';
   _params.AKCJA:='Dołącz';
   _params.PROC_START:='T';
   exec('mp_run','#b__box',_params)
?}


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
exec('env_wt','b_proces');
exec('set_wt_par','obiegi2','path_run',_path);
:: uruchomienie z pulpitu
{? _path='Proc'
||  web_params_set('prel',_b);
   _result:=exec('szuk_okr','okresy',date())<>null;
   _rok_f:=exec('FindAndGet','#table',OKRO_F,__PARSES.getVal('OkresRok').OKRO_O,,"@.OKRO_F.ROK",null);
   {? _result & _rok_f<>ROZNE.UT_OKROD().ROK
   || _form:="exec('form_proc','!obe_faw_darp',1,_a)";
      _ask:='W parametrach pracy wybrałeś rok: %1\nBieżący rok kalendarzowy: %2\n\nCzy chcesz zmienić rok w parametrach pracy?'@
            [SSTALE.AR().NAZ,ROZNE.UT_OKROD().ROK().NAZ];
      web_ask(_form,_ask,FUN.TYT)
   || {? ~_result
      || FUN.info('Wybrany rok w parametrach pracy jest nieaktualny.\nRok %1 nie został jeszcze założony w danych systemu.'@[$(date()~1)])
      ?};
      exec('form_proc','!obe_faw_darp',0,0)
   ?}
|? _path='Todo'
|| _path_param:=_b;
   _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || _name:=((_keyrefs[1]-8)+8);
      EDOKUM.use(_name); EDOKUM.prefix();
      {? EDOKUM.seek(_keyrefs[1],,1)
      || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
         params_exec('pop_dob_wt','obiegi',2,1)
      ?}
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
:: uruchomienie z obszaru roboczego
|? _path='Area'
|| _get:=web_params_get(1);
   {? ~exec('is_act','#b__box','OBE_FAW_DARP') |
      exec('FindAndGet','#table',B_PREL,_get.B_PREL,,"B_PREL.CLASS='B_ACTION'",0)
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
   || _web_params:=web_params_get(1);
      _prel:=_web_params.B_PREL;
      {? ref_tab(_prel)=B_PREL
      || _result:=exec('szuk_okr','okresy',date())<>null;
         _rok_f:=exec('FindAndGet','#table',OKRO_F,__PARSES.getVal('OkresRok').OKRO_O,,"@.OKRO_F.ROK",null);
         {? _result & _rok_f<>ROZNE.UT_OKROD().ROK
         || _form:="exec('form_obsz','!obe_faw_darp',1,_a)";
            _ask:='W parametrach pracy wybrałeś rok: %1\nBieżący rok kalendarzowy: %2\n\nCzy chcesz zmienić rok w parametrach pracy?'@
                  [SSTALE.AR().NAZ,ROZNE.UT_OKROD().ROK().NAZ];
            web_ask(_form,_ask,FUN.TYT)
         || {? ~_result
            || FUN.info('Wybrany rok w parametrach pracy jest nieaktualny.\nRok %1 nie został jeszcze założony w danych systemu.'@[$(date()~1)])
            ?};
            exec('form_obsz','!obe_faw_darp',0,0)
         ?}
      || _prel:=_web_params.B_PREL;
         _akcja:=_web_params.AKCJA;
         _portsIn:=_web_params.PORTS_IN;
         _context:=_web_params.CONTEXT;
         exec('web_run','#b__box',_prel,_akcja,_portsIn,'web_Area',,_context)
      ?}
   ?}
?}


\form_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła wykonywana podczas uruchomienia procesu z pulpitu
::   WE: _a - 0/1 - scieżka ze zmianą okresu
::       _b - 0/1 - odpowiedź na pytanie
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<0 || _a:=0 ?};
{? var_press('_b')<0 || _b:=0 ?};
{? _b || exec('chk_okres','obiegi2',0,1) ?};
_get:={? _a || web_params_get(0) || web_params_get(1) ?}; web_params_set(_get);
exec('todo_run','obiegi',0);
web_params_set(exec('obj_ntab_set','#array',_get,'B_PREL',$_get.prel));
zakres:=1;
exec('init','obe');
_par1:=exec('get_act_proc_params','#b__box','OBE_FAW_DARP',_get.prel,"");
params_exec('dol_dob_wt','obiegi',_par1,2,2)


\form_obsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła wykonywana podczas uruchomienia z obszaru roboczego
::   WE: _a - 0/1 - scieżka ze zmianą okresu
::       _b - 0/1 - odpowiedź na pytanie
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<0 || _a:=0 ?};
{? var_press('_b')<0 || _b:=0 ?};
{? _b || exec('chk_okres','obiegi2',1,1) ?};
_get:={? _a || web_params_get(0) || web_params_get(1) ?}; web_params_set(_get);
_wt_bprel:=exec('FindAndGet','#table',B_PREL,_get.B_PREL);
_parr:=exec('get_act_proc_params','#b__box',_get.ACT_UID,_wt_bprel,_get.PORTS_IN);
params_exec('dol_dob_wt','obiegi',_parr,2,0)


\pop_wni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie wniosku
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| {? EDOKUMW.get()
   || EDOKUM.use('skid_v'+(EDOKUMW.name()+2)); EDOKUM.prefix(); EDOKUMW.EDOKUM();
      exec('todo_run','obiegi',0);
      _dalej:=exec('env_edokum','obiegi','WW',0);
      VAR_DEL.delete('todo_run');
      {? _dalej
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='OBE_FAW_DARP';
         _params.AKCJA:='chkWT';
         _params.UIDREF:=EDOKUM.uidref();
         _params.WT_SKIP:=1;
         _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
         _params.CONTEXT.EDOKUM:=EDOKUM.ref();
         _params.CONTEXT.SPR_WT:=0;
         exec('mp_run','#b__box',_params);
         {? _params.CONTEXT.SPR_WT
         || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
            exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
            params_exec('pop_dob_wt','obiegi',2,0)
         ?}
      ?}
   || FUN.info('Nie znaleziono wniosku.'@);
      exec('dob_refresh','obiegi','W','W')
   ?}
|| _we:=~~;
   _wy:=obj_new('EDOKUM');
   _wy.EDOKUM:=EDOKUM.ref();
   _ref:=exec('put','obe_faw',_we,_wy,0)
?}


\usu_wni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie wniosku
::----------------------------------------------------------------------------------------------------------------------
exec('us_dob_wt','obiegi',2)


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Trigger dla czynności OBE_FAW_DARP
::  WE: _a - 'add po', 'put przed', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'OBE_FAW_DARP')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Podglad dokumentu obiegu z listy zadań
::----------------------------------------------------------------------------------------------------------------------
params_exec('disp_wni','obiegi2',2)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zamknięty
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? 6+ref_name(_kref)='skid_v'
         || _edok:=exec('FindAndGet','#table',EDOKUM,_kref,,,null());
            {? _edok=null()
            || _mp.done()
            |? exec('FindAndGet','#table',EDOKUM,_kref,,"@.EDOKUM.ZAM='T'",1)
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1045 2023/09/25 14:14:24 0990cb1ce963ed5bd19fcd1e0cb852e3b17cdc997ffba08fda4146eb6212a10d01fd81c4e2cacce4d66d71caba4557c984d9d5958dbf79b0cd3f3ab85985223ca2ac039dc7f5f2aac7a7a243d03a8cb3c6cd59f84f37ce648b26f80d1d6db8498479ae3365c1967a1115c69ff3bf166646ce68a0778aabf609090a0dba5c3ed5

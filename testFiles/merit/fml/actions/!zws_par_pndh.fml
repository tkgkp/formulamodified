:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pndh.fml
:: Utworzony: 13.02.2018
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PNDH - Niedziele handlowe.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Formuła główna czynności.
::   WE:
::   WY:
::  OLD: \select/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~R_NDHAN.lock(1,1,1)
|| FUN.info('Czynność jest blokowana przez innego użytkownika.'@+'\n'+'Spróbuj ponownie za chwilę.'@);
   return()
?};

_par:=params_get();
_rok:=
   {? var_pres('_par')>100 & var_pres('in',_par)>100 & var_pres('FP_INS_NUM',_par.in)=type_of(0)
   || _par.in.FP_INS_NUM
   || date()~1
   ?};

R_NDHAN.cntx_psh();
R_NDHAN.index('ROK');
R_NDHAN.prefix();

_tabR:=sql('select distinct R_NDHAN.ROK from R_NDHAN order by 1 desc');

_tabR.fld_fml('ROK','BLANK',"
   _tabR:=cur_tfld();
   _tabR.cntx_psh();
   _bl:={? _tabR.first() || _tabR.ROK+1 || date()~1 ?};
   _tabR.cntx_pop();
   _bl");

_akcja:='"exec(\'"+_a+"\',\'!zws_par_pndh\')"';

_cfg:=',btn_label_align=center,panel=bottom,align=end';

_weR:=_tabR.mk_edit('|--|'+'Rok'@,,'#rtpndh');
_tabR.win_esep(_weR,'Dane podstawowe'@);
_tabR.win_efld(_weR,,'ROK',,,4,-1,,'Rok'@,,'Rok'@);
_tabR.efld_opt(_weR,'mark=1',,'ROK');
exec('ok_esc','#window',_tabR,_weR,,,,,,,exec('text_red_ok','#window'));
_tabR.win_edit(_weR);

_wsR:=_tabR.mk_sel('Lata'@,'N',,'#rtpndh',,,,,'U');
_tabR.win_fld(_wsR,,'ROK',,,5,-1,,'Rok'@,,'Rok'@);
_tabR.win_act(_wsR,1,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu@',$($_akcja)('tabr_dolacz'),,1,,,,'D');
_tabR.win_act(_wsR,,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_akcja)('tabr_dolacz'),,1,,,,'D');
_tabR.win_act(_wsR,,'Formuła','Usuń'@@,,'Usunięcie bieżącego zapisu'@,$($_akcja)('tabr_usun'),,,,,,'U');

_tabR.win_act(_wsR,,'Szukaj');
_btn1:=_tabR.win_btn(_wsR,'text='+'&Dołącz'@+',panel=right,align=begin','menu:D',);
_btn2:=_tabR.win_btn(_wsR,'text='+'&Usuń'@+',panel=right,align=begin','menu:U',,,,,,'noempty');
_tabR.btn_sopt(_wsR,_btn1,'tooltip='+'Dołączenie nowego zapisu'@);
_tabR.btn_sopt(_wsR,_btn2,'tooltip='+'Usunięcie bieżącego zapisu'@);
_tabR.win_edit(_weR);

_mode:='maximized_with_title';

_gwer:=_tabR.grp_make('Niedziele handlowe'@,,'#rtpndh',,,$($_akcja)('tabr_bc'),,'normal');
_tabR.grp_sel(_gwer,_tabR,_wsR,,$($_akcja)('tabr_ar'),,,6,,,,,_mode);
_tabR.grp_splt(_gwer,,'vertical','R_NDHAN');
_tabR.grp_sel(_gwer,R_NDHAN,'WER',,"",,,6,,,,,_mode);

_tabR.win_sel(_gwer);

_fml:=R_NDHAN.fld_fml('ROK','*BLANK');

{? ~_tabR.find_key(_rok)
|| _tabR.cntx_psh();
   _tabR.prefix();
   _tabR.ROK:=_rok;
   _tabR.add();
   _tabR.cntx_pop();
   ''
?};

_tabR.find_ge(_rok);
_tabR.select(,1,-1);

R_NDHAN.fld_fml('ROK','BLANK',_fml);
R_NDHAN.cntx_pop();
R_NDHAN.unlock();
~~


\tabr_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Obsługa akcji Dołącz dla tabeli tymczasowej tabR.
::   WE:
::   WY:
::  OLD: \tabr_dolacz/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
_tabR.blank();
{? _tabR.edit("exec('tabr_ae','!zws_par_pndh')")
|| _tabR.add()
?}


\tabr_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Obsługa akcji Usuń dla tabeli tymczasowej tabR.
::   WE:
::   WY:
::  OLD: \tabr_usun/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
{? {? R_NDHAN.first() || exec('del_conf','#table') || exec('del_ask','#table') ?}
|| do();
   {? R_NDHAN.first() || {! |? R_NDHAN.del() !} ?};
   cur_tab(1,1).del();
   {? ~end()
   || FUN.emsg('Usuniecię bieżącego zapisu nie powiodło się.'@)
   ?}
?}


\tabr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Po redagowaniu rekordu tabeli tymczasowej tabR.
::   WE:
::   WY:
::  OLD: \tabr_ae/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
{? _tabR.ROK<=0 | _tabR.ROK>exec('max_rok','#datetime')
|| FUN.emsg('Błędny rok.'@);
   return('ROK')
?};

_tabR.cntx_psh();
_exist:=_tabR.find_key(_tabR.ROK);
_tabR.cntx_pop();
{? _exist
|| FUN.emsg('Dołączenie zapisu nie jest możliwe.'@+'\n'+
            'Znaleziono rekord o podanym kluczu.'@+'\n\n'+
            'Powielone informacje: %1'@['Rok']);
   'ROK'
|| ''
?}


\tabr_bc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Przed zamknięciem okna grupowego tabeli tymczasowej tabR.
::   WE:
::   WY:
::  OLD: \tabr_bc/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
_isEmpty:=0;
_r_ndhan:=sql('select distinct R_NDHAN.ROK from R_NDHAN order by 1 desc');

_tabR:=cur_tab();
_tabR.cntx_psh();
{? _tabR.first()
|| {!
   |? _isEmpty+=~_r_ndhan.find_key(_tabR.ROK);
      ~_isEmpty & _tabR.next()
   !}
?};
_tabR.cntx_pop();

{? _isEmpty
|| FUN.ask('Rok, dla którego nie wprowadzono niedziel handlowych, zostanie usunięty.'@+'\n'+
           'Czy chcesz zakończyć redakcję?'@)
|| 1
?}


\tabr_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Po odświeżeniu okna tabeli tymczasowej tabR.
::   WE:
::   WY:
::  OLD: \tabr_ar/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
{? _tabR.get()
|| R_NDHAN.prefix(_tabR.ROK);
   R_NDHAN.fld_fml('ROK','BLANK',$$_tabR.ROK);
   _hid:=''
|| R_NDHAN.prefix(-1);
   _hid:=':d'
?};
R_NDHAN.actions('WER',_hid,,1);
grp_disp(R_NDHAN,'WER')


\r_ndhan_rec_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Funkcja rekord po w tabeli R_NDHAN.
::   WE:
::   WY:
::  OLD: \r_ndhan_rec_ae/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=__CHK.record2(R_NDHAN,'DATA','Data');
{? _ok=''
|| _rok:=cur_tab(1,1).ROK;
   {? R_NDHAN.DATA~1<>_rok
   || FUN.emsg('Dzień tygodnia odpowiadający wybranej dacie musi należeć do aktualnie wybranego roku: %1'@[$_rok]);
      _ok:='DATA'
   |? R_NDHAN.DATA~4<>7
   || FUN.emsg('Dzień tygodnia odpowiadający wybranej dacie nie jest niedzielą'@);
      _ok:='DATA'
   ?}
?};
{? _ok=''
|| _add:=1+menu_txt()='D';
   _ref:=exec('exist','!zws_par_pndh',R_NDHAN.DATA);
   {? _ref & (_add | _ref<>R_NDHAN.ref())
   || FUN.emsg('Istnieje już zapis na wskazaną datę.'@);
      _ok:='DATA'
   ?}
?};
{? _ok='' & R_NDHAN.DATA<date(2018,3,1)
|| FUN.info('Wprowadzony rekord nie będzie uwzględniany w weryfikacji.'@+'\n\n'+
            'Ustawa ograniczająca handel w niedzielę obowiązuje od dnia 1 marca 2018 r.'@)
?};
_ok


\exist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Sprawdzenie czy istnieje już rekord na wskazaną datę
::   WE: _a [DATE] - data badania
::   WY: null/ref rekordu
::  OLD: \exist/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
_data:={? var_pres('_a')=type_of(date()) || _a || return(_result) ?};
R_NDHAN.cntx_psh();
R_NDHAN.index('DATA');
R_NDHAN.prefix(_data);
{? R_NDHAN.first()
|| _result:=R_NDHAN.ref()
?};
R_NDHAN.cntx_pop();
_result


\r_ndhan_data_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Po edycji pola "Data"
::   WE:
::   WY:
::  OLD: \r_ndhan_data_ae/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
_sOpis:='';
_rok:=cur_tab(1,1).ROK;
exec('__KAL','object');
:: zmienna potrzebna do wyłączenia komunikatów z obiektu KAL
__KAL.NO_ERR:=1;
__KAL.set_cal('standard',_rok);
{? __KAL.get_day(R_NDHAN.DATA)
|| _sOpis:=KAL_DEF.OPIS
?};
__KAL.NO_ERR:=0;

{? +_sOpis & (_sOpis<>R_NDHAN.OPIS) &
   (R_NDHAN.OPIS='' | FUN.ask('Czy nadpisać pole \'Opis\' wartością: \'%1\'?'@[_sOpis]))
|| R_NDHAN.OPIS:=_sOpis
?};
''


\r_ndhan_trig_add_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Trigger przed dołącz
::   WE:
::   WY:
::  OLD: \r_ndhan_trig_add_b/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
exec('trig_add_put_b','!zws_par_pndh')


\r_ndhan_trig_put_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Trigger przed popraw
::   WE:
::   WY:
::  OLD: \r_ndhan_trig_put_b/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
exec('trig_add_put_b','!zws_par_pndh')


\trig_add_put_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Wspólna formuła dla triggerów put i add
::   WE:
::   WY:
::  OLD: \trig_add_put_b/r_ndhan.fml
::----------------------------------------------------------------------------------------------------------------------
R_NDHAN.ROK:=R_NDHAN.DATA~1;
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 8d801c9c976afe643eb436ab53e0064c62f9c6b366272d7a214f0660d477e9041e0fbf7849a8075bafa92a8f1942706ed87b6d75699d25cca3ba372f9eed5936b496256484e96b090738862cf6595f4cd2f36fdaba809867767b8fb11d1aca05350d71bceb48fa19d5710c6094805823f8738dd04912aa06ddb306e8342152a9

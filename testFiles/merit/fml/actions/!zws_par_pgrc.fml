:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pgrc.fml
:: Utworzony: 27.06.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PGRC - Grupy ruchomego czasu pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Definicja ruchomego czasu pracy zdefiniowanego przez pracodawcę.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
A_RWN.cntx_psh();
A_RWN.win_edit('REDF');
A_RWN.win_sel('WERF');
A_RWN.index('WN_FIRMA');
A_RWN.prefix('T');
A_RWN.select();
A_RWN.cntx_pop();
~~


\a_rwn_g_afd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.30]
:: OPIS: Dodanie nagłówka dla ruchomego czasu pracy grupy.
::   WE: _a = 1 - akcja popraw
::   WY:
::  OLD: \rcp_dod_nag_A/okres.fml
::----------------------------------------------------------------------------------------------------------------------
_put:={? var_pres('_a')=type_of(0) || _a ?};
_ok:=1;
exec('__HARM','object');

:: Ustawienie wartości początkowych przed wyświetleniem okna do edycji definicji ruchomego czasu pracy.
{? ~_put
|| A_RWN.blank();
   A_RWN.WP:='N';
   A_RWN.F:='T';
   A_RWN.P:=null()
|| _b_od:=A_RWN.OD;
   _b_do:=A_RWN.DO;
   _b_opis:=A_RWN.OPIS
?};
exec('rcp_Czytaj','prc',_put);

:: Sprawdzenie czy poprawiana definicja ruchomego czasu pracy nie jest powiązana z współpracownikami.
{? _put
|| A_RWN.cntx_psh();
   A_RWN.index('GROUP');
   A_RWN.prefix(#A_RWN.ref());
   {? A_RWN.size()
   || _ok:=FUN.ask('%1\n%2'
         [  'Do wybranej grupy ruchomego czasu pracy zostali przypisani współpracownicy.'@,
            'Czy napewno kontynuować?'@
         ]
      )
   ?};
   A_RWN.cntx_pop()
?};

:: Edycja definicji ruchomego czasu pracy.
{? _ok
   &
   A_RWN.edit(
      "  _wyn:=__CHK.record2(A_RWN,'OPIS','Nazwa'@,'OD','Data rozpoczęcia'@,'DO','Data zakończenia'@);
         {? _wyn=''
         || {? A_RWN.OD>A_RWN.DO
            || FUN.emsg('Data rozpoczęcia nie może być późniejsza od daty zakończenia.'@);
               return('OD')
            || _wyn:=0;
               {! _ind:=1..7
               |! _wyn+=('GZ'*($('RCP.ND'+($_ind)))());
                  {? ($('RCP.W'+($_ind)))()='T' & ($('RCP.GDO'+($_ind)))()=*0
                  || FUN.emsg('Pole \"Do godziny\" w dniu: \"%1\", musi być uzupełnione.'@
                        [MS.name(RCP,'ND'+($_ind))]
                     );
                     return('GDO'+($_ind))
                  ?};
                  {? ($('RCP.W'+($_ind)))()='T' & ($('RCP.GOD'+($_ind)))()>($('RCP.GDO'+($_ind)))()
                  || FUN.emsg('%1 \"%2\"'
                        [  'Wartość w polu \"Od godziny\" nie może być większa od wartości \"Do godziny\" w dniu:'@,
                           MS.name(RCP,'ND'+($_ind))
                        ]
                     );
                     return('GOD'+($_ind))
                  ?}
               !}
            ?}
         || _wyn
         ?}
      "
   )
|| _ref:={? _put || A_RWN.ref() || null() ?};
   A_RWN.cntx_psh();
   _od:=A_RWN.OD;
   _do:=A_RWN.DO;
   _opis:=A_RWN.OPIS;
   _ok:=1;
   A_RWN.index('WN_FIRMA');
   A_RWN.prefix('T');
   {? A_RWN.first()
   || {!
      |? {? _opis=A_RWN.OPIS & _ref<>A_RWN.ref() & A_RWN.OD<=_do & A_RWN.DO>=_od || _ok:=0 ?};
         _ok & A_RWN.next()
      !}
   ?};
   _msg:='';
   {? ~_ok || _msg:='%1\n%2'['Istnieje zapis w danym terminie.'@,'Proszę wprowadzić poprawne dane.'@] ?};
   A_RWN.cntx_pop();

   {? _ok & _put
   || A_RWN.cntx_psh();
      A_RWN.index('TREE');
      A_RWN.prefix(A_RWN.ref());
      {? (_b_od<>A_RWN.OD | _b_do>A_RWN.DO) & A_RWN.first()
      || _msg:='Istnieje tylko możliwość przedłużenia okresu trwania zapisu dla grupy.'@;
         _ok:=0
      ?};
      A_RWN.cntx_pop()
   ?};

   {? ~_ok
   || FUN.error(_msg);
      return(0)
   ?};

   {? {? _put
      || {? A_RWN.put(1) || exec('rcp_aktual_prac','!zws_par_pgrc') ?};
         1
      || A_RWN.add(1)
      ?}
   || exec('rcp_edit_prac','prc')
   ?}
?};
1


\a_rwn_g_afu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.30]
:: OPIS: Usuwa zapis nagłówkowy wraz z pozycjami dla grupy.
::   WE:
::   WY:
::  OLD: \rcp_del_All/okres.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
_nazwa:=A_RWN.OPIS;
A_RWN.cntx_psh();
A_RWN.index('TREE');
A_RWN.prefix(#A_RWN.ref());
{? A_RWN.first()
|| FUN.emsg('%1\n%2'
         [  'Ruchomy czas pracy \"%1\" wprowadzony przez pracodawcę powiązany jest z pracownikami.'@[_nazwa],
            'Usunięcie nie jest możliwe.'@
         ]
      );
   _dalej:=0
?};
A_RWN.cntx_pop();
{? _dalej & FUN.ask('Czy na pewno usunąć grupę ruchomego czasu pracy?'@)
|| A_RWP.index('A_RWP');
   A_RWP.prefix(A_RWN.ref());
   do();
   {? A_RWP.first()
   || {! |? A_RWP.del() !}
   ?};
   A_RWN.cntx_psh();
   A_RWN.index('TREE');
   A_RWN.prefix(#A_RWN.ref());
   {? ~A_RWP.first() & ~A_RWN.first()
   || A_RWN.cntx_pop();
      {? ~A_RWN.del(,1)  || undo() ?}
   || undo();
      A_RWN.cntx_pop()
   ?};
   end()
?};
~~


\rcp_aktual_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.30]
:: OPIS: Aktualizacja zapisów nagłówkow ruchomego czasu pracy dla pracowników w grupie.
::   WE:
::   WY:
::  OLD: \rcp_aktual_prac/okres.fml
::----------------------------------------------------------------------------------------------------------------------
A_RWN.cntx_psh();
A_RWN.index('TREE');
A_RWN.prefix(#A_RWN.ref());
_od:=A_RWN.OD;
_do:=A_RWN.DO;
{? A_RWN.first()
|| _tab:=obj_new(A_RWN.size());
   _ind:=0;
   {!
   |? _ind+=1;
      _tab[_ind]:=A_RWN.ref();
      A_RWN.next()
   !};
   A_RWN.index('WN_PRAC');
   {! _ind:=1..obj_len(_tab)
   |! A_RWN.prefix();
      {? A_RWN.seek(_tab[_ind])
      || _ref:=A_RWN.ref();
         _ok:=1;
         A_RWN.cntx_psh();
         A_RWN.prefix(A_RWN.WP,A_RWN.P);
         {? A_RWN.first()
         || {!
            |? {? _ref<>A_RWN.ref() & A_RWN.OD<=_do & A_RWN.DO>=_od || _ok:=0 ?};
               _ok & A_RWN.next()
            !}
         ?};
         A_RWN.cntx_pop();
         {? _ok
         || A_RWN.DO:=_do;
            A_RWN.WP:='N';
            A_RWN.put()
         ?}
      ?}
   !}
?};
A_RWN.cntx_pop();
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:05 2c159e6b5bea1dd6091a73e15e23a291e03c71195c6f1b16d023dceb528f65ea255c17494461feb1c25f9e6089cc6fd5a75420ba0690c976e555c56014cfce5774fc2452baf4d3acce9c39b648c0da530c3626740374b6771736c311031bd71f322b5bf50fa1b223bdcd2280b8a5a972b299414b9006d1dffcbc8a9ced0328d9

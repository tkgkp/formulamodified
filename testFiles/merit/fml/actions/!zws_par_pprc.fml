:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pprc.fml
:: Utworzony: 21.04.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PPRC - Stałe systemu związane z czasem pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Edycja stałych obszaru związanych z czasem pracy - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_data:=exec('od_daty','#stalesys');
{? _data.OK<>1
|| return()
?};

KST.cntx_psh();
KST.win_edit('PRC');
KST.hdr_edit('Stałe dziedziny czas pracy na dzień %1'@[$_data.OD]);
R_CZYT.win_sel('WER');
R_CZYT.win_edit('RED');
R_KLKW.win_sel('WER');
_inTerm:=~exec('interm','#system');
KST.efld_opt('PRC',{? KST.R_RC || _opt:='mark=0,enable=1' || _opt:='mark=0,enable=0' ?},,'R_KW','O');
KST.efld_opt('PRC',{? KST.R_RC & _inTerm || _opt:='mark=1,enable=1' || _opt:='mark=0,enable=0' ?},,'R_ASCII');
KST.efld_opt('PRC',{? KST.R_RC || _opt:='mark=0,enable=1' || _opt:='mark=0,enable=0' ?},,'R_DO');

exec('czytaj','#stalesys',_data.OD,KST);
{? KST.edit("exec('kst_ae_rec','!zws_par_pprc')")
|| {? ~KST.R_RC || KST.R_ASCII:='' ?};
   exec('zapisz','#stalesys',_data.OD,KST)
?};

KST.cntx_pop();
~~


\kst_ae_r_rc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS: Formuła po redakcji pola R_RC w zmiennej KST.
::   WE:
::   WY:
::  OLD: \RSTRCpor/stale.fml
::----------------------------------------------------------------------------------------------------------------------
{? KST.R_RC
|| KST.R_KW:=KST.R_RC().K
|| KST.R_KW:=null()
?};
_inTerm:=~exec('interm','#system');
KST.efld_opt('PRC',{? KST.R_RC || _opt:='mark=0,enable=1' || _opt:='mark=0,enable=0' ?},,'R_KW','O');
KST.efld_opt('PRC',{? KST.R_RC & _inTerm || _opt:='mark=1,enable=1' || _opt:='mark=0,enable=0' ?},,'R_ASCII');
KST.efld_opt('PRC',{? KST.R_RC || _opt:='mark=0,enable=1' || _opt:='mark=0,enable=0' ?},,'R_DO');
1


\kst_f3_r_ascii
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Ustalenie ścieżki do pliku z danymi.
::   WE:
::   WY: ścieżka do pliku
:: ~OST: INFILECHOOSER
::  OLD: \ascii/stale.fml
::----------------------------------------------------------------------------------------------------------------------
_old:=KST.R_ASCII;
_new:=exec('filechooser','#file',KST.R_RC().P,0,,,,'DIRECTORIES_ONLY',,KST.R_ASCII);

{? _new='' || _old || _new ?}


\kst_ae_r_ascii
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS: Sprawdzenie, czy jest możliwy odczyt i zapis.
::   WE:
::   WY: 1 jest dostęp, 0 brak dostępu
::  OLD: \file/stale.fml
::----------------------------------------------------------------------------------------------------------------------
_zapis:=fopen(KST.R_ASCII+'test.test','w');
{? _zapis
|| fclose(_zapis)
|| FUN.emsg('Brak dostępu do zapisu'@);
   return(0)
?};
_od:=fopen(KST.R_ASCII+'test.test','r');
{? _od
|| fclose(_od)
|| FUN.emsg('Brak dostępu do odczytu'@);
   return(0)
?};
ferase(KST.R_ASCII+'test.test',0);
1


\kst_f3_r_tz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Wybór parametru obsługi trzeciej zmiany.
::       Formuła nie jest już wykorzystywana w Merit.
::   WE:
::   WY:
::  OLD: \tzmiana/stale.fml
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(1,
   'KOD','STRING[1]','Kod rodzaju trzeciej zmiany',
   'OPIS','STRING[60]','Opis rodzaju trzeciej zmiany'
);
_ws:=_TAB.mk_sel('Rodzaj trzeciej zmiany'@,,,'rodz3zmiany',,,,,'U',,,,,'maximized');
_TAB.win_fld(_ws,,'KOD',,,3,,1,'Kod'@,,'Kod rodzaju trzeciej zmiany'@);
_TAB.win_fld(_ws,,'OPIS',,,60,,1,'Opis'@,,'Opis rodzaju trzeciej zmiany'@);
_TAB.win_act(_ws,,'Formuła','Wybierz'@,,,,"sel_exit()",1,,,,'W');
_TAB.win_sel(_ws);
_TAB.KOD:='B'; _TAB.OPIS:='Trzecia zmiana - nie występuje'; _TAB.add();
_TAB.KOD:='D'; _TAB.OPIS:='Trzecia zmiana - prezentacja według daty rejestracji'; _TAB.add();
_TAB.KOD:='K'; _TAB.OPIS:='Trzecia zmiana - prezentacja według dnia rozliczenia'; _TAB.add();
{? _TAB.select()
|| KST.R_TZ:=_TAB.KOD
?}


\kst_ae_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Formuła po edycji stałych systemu związanych z czasem pracy.
::   WE:
::   WY:
::  OLD: \spr_kst/stale.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('str2time','#convert',KST.R_NLIM)=*0
|| FUN.emsg('%1\n%2'['Pole "Roczny limit nadgodzin" musi być wypełnione.'@,'Należy uzupełnić brakujące informacje.'@]);
   'R_NLIM'
|? exec('str2time','#convert',KST.R_NTYG)=*0
|| FUN.emsg('%1\n%2'['Pole "Norma średniotygodniowa" musi być wypełnione.'@,'Należy uzupełnić brakujące informacje.'@]);
   'R_NTYG'
|| {? KST.R_RC
   || __CHK.record2(
         KST,
         'R_PNOD',,
         'R_PNDO',,
         'R_ASCII','Ścieżka do pliku',
         'R_KG','Godziny pracy',
         'R_ODBIOR','Odbiór czasu wolnego'
      )
   || __CHK.record2(KST,'R_PNOD',,'R_PNDO',,'R_KG','Godziny pracy','R_ODBIOR','Odbiór czasu wolnego')
   ?}
?}


\opt_r_pwe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [20.xx]
:: OPIS: Formuła po redakcji pola R_WEWY_R i przed wyświetleniem R_PWE.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? KST.R_WEWY_R='P'
|| KST.efld_opt('PRC','enable=1',,'R_PWE');
   KST.efld_opt('PRC','enable=1',,'R_PWY')
||  KST.R_PWE:=*0;
   KST.R_PWY:=*0;
   KST.efld_opt('PRC','enable=0',,'R_PWE');
   KST.efld_opt('PRC','enable=0',,'R_PWY')
?};
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 291b33b6024d52c9263ac5fa4bbf2fd250348dbf234d33ed8a581150a34a64764bc4132f8719c7e7cf8a72f3e83ce492bc6a2a3139e232cd3d59fa5802e851f688895f6b9f320871455c832b097ff5760c36881a1e7405a3bc165c44cd0bdfbf971def33bdbce09af081f9d94c9ccef1d78c33c364b1cd5bbdd2cda8072bd626

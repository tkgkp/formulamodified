:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkw_por_irpm.fml
:: Utworzony: 11.06.2017
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Obsługa czynności webTerm-owej PKW_POR_IRPM - Rejestracja premii uznaniowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Rejestracja premii uznaniowych - (webTerm) - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('set_env','pkw_por')
|| _atr:=exec('pm_atr','pm_wspolne','Czynność niedostępna.');
   {? ~_atr.OK
   || return()
   ?};
   _dysp_ref:=exec('get_akt_dysp','!pkw_por_irpm',P.ref());
   {? _dysp_ref
   || _act_uid:='PKW_POR_IRPM';
      exec('web_global_params_set','pkw',_act_uid,'DYSP_REF',_dysp_ref);
      exec('web_global_params_set','pkw',_act_uid,'PM_PREM',null());
      PM_NAG.web_select('WT_GRP',,,,"
         exec('set_env','pkw_por');
         web_top_close();
         exec('web_title_clear','areatitle')
      ")
   || FUN.info('Użytkownik nie jest aktywnym dysponentem premii.'@)
   ?}
?};
~~


\pm_dysp_wt_redn_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Przed obsługą okna redakcyjnego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Zawsze zaczynamy, od przekazania parametrów "dalej".
web_params_set(web_params_get());
:: Ustawiam środowisko.
exec('env_wt','b_proces');
~~


\pm_nag_wt_nag_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Przed obsługa dla okna WT_NAG tabeli PM_NAG w oknie grupowym WT_GRP (tabeli PM_NAG)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','pkw_por');
exec('pm_nag_wt_prefix','!pkw_por_irpm');
~~


\pm_nag_wt_prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Ustawia prefix na tabeli PM_NAG
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_act_uid:='PKW_POR_IRPM';
_dysp_ref:=exec('web_global_params_get','pkw',_act_uid,'DYSP_REF',null());
_rok:=exec('web_global_params_get','pkw',_act_uid,'ROK',date~1);
_msc:=exec('web_global_params_get','pkw',_act_uid,'MSC',date~2);

PM_NAG.index('UNIQUE');
PM_NAG.prefix(_dysp_ref,_rok,_msc);
~~


\pm_prem_wt_wer_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Przed obsługą dla okna WT_WER tabeli PM_PREM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

:: ponieważ filtry zmieniają bufor tabeli musimy zapamiętać aktualny rekord
_ref:=PM_PREM.ref();

{? PM_PREM.f_active()
||
:: wyliczenie sumy dla wszystkich rekordów w filtrze
   SUMA.R01S:=0;
   {? PM_PREM.f_first()
   || {!
      |? SUMA.R01S+=PM_PREM.KW;
         PM_PREM.f_next()
      !}
   ?};

:: konieczne jest ponowne ustawienie się na pierwszym rekordzie filtra inaczej obsługa się zapętla
   PM_PREM.f_first()
?};

PM_PREM.seek(_ref);
~~


\pm_prem_wt_wer_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Przed obsługą dla okna WT_WER tabeli PM_PREM w oknie grupowym WT_GRP (tabeli PM_NAG).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','pkw_por');
_ref:=web_win_ref('PM_NAG_WT_NAG');

{? type_of(_ref)
|| _act_uid:='PKW_POR_IRPM';
   exec('web_global_params_set','pkw',_act_uid,'NAG_REF',_ref);
   SUMA.R01S:=0;
:: wyliczenie sumy dla wszystkich rekordów
   PM_PREM.index('UNIQUE');
   PM_PREM.prefix(_ref);
   PM_PREM.cntx_psh();
   {? PM_PREM.first()
   || {!
      |? SUMA.R01S+=PM_PREM.KW;
         PM_PREM.next()
      !}
   ?};
   PM_PREM.cntx_pop();
   PM_PREM.seek(exec('web_global_params_get','pkw',_act_uid,'PM_PREM',null()))
?};
~~


\pm_prem_wt_grp_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Przed obsługą dla okna WT_GRP tabeli PM_PREM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
~~


\pm_nag_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa edycji nagłówka premii
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('set_env','pkw_por')
|| _act_uid:='PKW_POR_IRPM';
   _dysp_ref:=exec('web_global_params_get','pkw',_act_uid,'DYSP_REF',null());
   _rok:=exec('web_global_params_get','pkw',_act_uid,'ROK',date~1);
   _msc:=exec('web_global_params_get','pkw',_act_uid,'MSC',date~2);

   _txt:=exec('pm_nag_modify_b','pm_wspolne',_rok,_msc);
   {? _txt<>''
   || FUN.info(_txt+'\n'+'Dołączenie nagłówka nie jest możliwe.'@);
      return()
   ?};

   {? _dysp_ref
   || exec('wt_pbudz_del','pkw',3);

      PM_BUDZ.cntx_psh();
      PM_BUDZ.index('OKRES');
      PM_BUDZ.prefix(_dysp_ref);
      {? PM_BUDZ.first()
      || PM_NAG.cntx_psh();
         PM_NAG.index('BRM');
         PM_NAG.prefix();
         _d0:=date(0,0,0);
         _dp:=date(_rok,_msc,1);
         _dk:=date(_rok,_msc,0);
         {!
         |? {? (PM_BUDZ.LIMIT='N' | PM_BUDZ.POZOST>0) &
               PM_BUDZ.OD<=_dk & (PM_BUDZ.DO=_d0 | PM_BUDZ.DO>=_dp) &
               ~PM_NAG.find_key(PM_BUDZ.ref(),_rok,_msc)
            || WT_PBUDZ.blank();
               WT_PBUDZ.PM_BUDZ:=PM_BUDZ.ref();
               WT_PBUDZ.R:=PM_BUDZ.R;
               WT_PBUDZ.P:=PM_BUDZ.P;
               WT_PBUDZ.OSOBA:=PM_BUDZ.OSOBA;
               WT_PBUDZ.WYDZIAL:=PM_BUDZ.WYDZIAL;
               WT_PBUDZ.add()
            ?};
            PM_BUDZ.next()
         !};
         PM_NAG.cntx_pop()
      ?};
      PM_BUDZ.cntx_pop();

      WT_PBUDZ.web_select('WT_WYB',,,,"web_top_close(); web_top_refresh(1)");

      ~~

   || FUN.emsg('Błąd ustalenia dysponenta premii.'@)
   ?}
?}


\pm_nag_wt_nag_add_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Dołącz" okienka WT_NAG tabeli PM_NAG w oknie grupowym  WT_GRP (tabeli PM_NAG)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_act_uid:='PKW_POR_IRPM';
_rok:=exec('web_global_params_get','pkw',_act_uid,'ROK',date~1);
_msc:=exec('web_global_params_get','pkw',_act_uid,'MSC',date~2);
_txt:=exec('pm_nag_chk_blok','pm_wspolne',_rok,_msc);
{? +_txt
|| PM_NAG.web_cntx_save(1);
   web_ask("
        PM_NAG.web_cntx_load(1);
        {? _a=1
        || exec('pm_nag_edit','!pkw_por_irpm')
        ?}
     ",'%1\nKontynuować?'@[_txt],'Premie uznaniowe'@)
|| exec('pm_nag_edit','!pkw_por_irpm')
?};
~~


\pm_nag_wt_nag_put_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Popraw" okienka WT_NAG tabeli PM_NAG w oknie grupowym  WT_GRP (tabeli PM_NAG)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record')
|| return()
?};

PM_NAG.web_edit('WT_RED','#pm_nag_wt_nag_',,,"
   {? _a='OK'
   || PM_NAG.put()
   ?};
   PM_NAG.web_eclose();
   web_top_refresh(1)
");
~~


\pm_nag_wt_nag_del_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Usuń" okienka WT_NAG tabeli PM_NAG w oknie grupowym  WT_GRP (tabeli PM_NAG)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PM_NAG.AKC='T'
|| FUN.info('Nagłówek premii został zaakceptowany, usunięcie nagłówka nie jest możliwe.'@);
   return(~~)
||
   PM_NAG.web_cntx_save(1);
   web_ask("
      PM_NAG.web_cntx_load(1);
      {? _a=1
      || PM_NAG.del();
         web_top_refresh()
      ?}
   ",'Czy na pewno chcesz usunąć bieżący nagłówek premii miesięcznych?'@,'Premie uznaniowe'@)
?};
~~


\pm_nag_wt_nag_del_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Usuń" okienka WT_NAG tabeli PM_NAG w oknie grupowym  WT_GRP (tabeli PM_NAG) - grupowa
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

:: zapamiętuje zaznaczone rekordy
_sel_rec:=web_sel_aget(1);
web_params_set('sel_rec',_sel_rec);

PM_NAG.web_cntx_save(1);
_text:='Usunięcie wszystkich zaznaczonych wierszy nie było możliwe.'@;
web_ask($("
   _del:=0;
   {? _a=1
   || PM_NAG.cntx_psh();
      PM_NAG.prefix();
::    odczytuje zaznaczone rekordy
      _tab:=web_sel_aget(0,web_params_get().sel_rec);
      _not_all:=0;

      {? _tab.first()
      || {!
         |? {? PM_NAG.seek(_tab.REF)
            || {? PM_NAG.AKC='T'
               || _not_all+=1
               || _del+=PM_NAG.del()
               ?}
            ?};
            _tab.next()
         !}
      ?};
      &_tab;
      PM_NAG.cntx_pop();

      {? _not_all
      || FUN.info('"+_text+"')
      ?}
   ?};

   PM_NAG.web_cntx_load(1);
   {? _del
   || web_top_sel_adel(0,web_top_ident())
   ?};
   web_top_refresh()
"),'Czy usunąć zaznaczone wiersze?'@,'Premie uznaniowe'@);
0


\pm_nag_wt_nag_msc_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Miesiąc" okienka WT_NAG tabeli PM_NAG w oknie grupowym  WT_GRP (tabeli PM_NAG)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_act_uid:='PKW_POR_IRPM';
_rok:=exec('web_global_params_get','pkw',_act_uid,'ROK',date~1);
_msc:=exec('web_global_params_get','pkw',_act_uid,'MSC',date~2);

_def:=web_define();
web_def_fld(_def,'ROK',_rok,'Rok'@,'Rok premii'@,,10,0,'mark=1');
web_def_fld(_def,'MSC',_msc,'Miesiąc'@,'Miesiąc premii'@,,10,0,'mark=1');
web_def_btn(_def,'OK','text=%1,tooltip=%2'['OK'@,'Zatwierdzenie okienka'@]);
_year:='Należy podać poprawny rok premii uznaniowej [%1 - %2].'@['1',$exec('max_rok','#datetime')];
_month:='Należy podać poprawny miesiąc premii uznaniowej [%1 - %2].'@['1','12'];
_fml:=''+"
   exec('set_env','pkw_por');
   _def_par:=web_def_get();
   _max_rok:=exec('max_rok','#datetime');
   {? _def_par.ROK<=0 | _def_par.ROK>_max_rok
   || FUN.emsg('"+_year+"');
      return('ROK')
   ?};
   {? _def_par.MSC<=0 | _def_par.MSC>12
   || FUN.emsg('"+_month+"');
      return('MSC')
   ?};
   exec('web_global_params_set','pkw','"+_act_uid+"','ROK',_def_par.ROK);
   exec('web_global_params_set','pkw','"+_act_uid+"','MSC',_def_par.MSC);

:: przed odświeżeniem musimy najpierw zamkąć okno web_define
   web_def_close();

:: odrysowanie główego okna w grupie
   PM_NAG.web_cntx_load(1);
   web_top_refresh(1);

:: ustawienie informacji w tytule okna o wybranym miesiącu premii
   exec('pm_nag_hdr_rok','!pkw_por_irpm',1)
";

PM_NAG.web_cntx_save(1);
web_def_edit(_def,$_fml,'Miesiąc premii'@);
~~


\pm_nag_wt_nag_akceptuj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Akceptuj" okienka WT_NAG tabeli PM_NAG w oknie grupowym  WT_GRP (tabeli PM_NAG)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record')
|| return()
?};

PM_NAG.web_cntx_save(1);
web_ask("
   PM_NAG.web_cntx_load(1);
   {? _a=1
   || exec('set_env','pkw_por');
      {? exec('pm_nag_akc','pm_wspolne',1)
      || web_top_refresh()
      ?}
   ?}
",'Czy na pewno chcesz zaakceptować bieżący nagłówek premii miesięcznych?'@,'Premie uznaniowe'@);
~~


\pm_nag_wt_nag_wycofaj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Wycofaj" okienka WT_NAG tabeli PM_NAG w oknie grupowym  WT_GRP (tabeli PM_NAG)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record')
|| return()
?};

PM_NAG.web_cntx_save(1);
web_ask("
   PM_NAG.web_cntx_load(1);
   {? _a=1
   || exec('set_env','pkw_por');
      {? exec('pm_nag_akc','pm_wspolne',0)
      || web_top_refresh()
      ?}
   ?}
",'Czy na pewno chcesz wycofać akceptację bieżącego nagłówka premii miesięcznych?'@,'Premie uznaniowe'@);
~~


\pm_nag_hdr_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Dodaje informacje o roku dla okna grupowego premii miesięcznych
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_depth:={? var_pres('_a')=type_of(0) || _a || 0 ?};

_act_uid:='PKW_POR_IRPM';
_rok:=exec('web_global_params_get','pkw',_act_uid,'ROK',date~1);
_msc:=exec('web_global_params_get','pkw',_act_uid,'MSC',date~2);
_title:=date(_rok,_msc,0)$8;
PM_NAG.web_win_opt(web_top_win(_depth),,'title= - %1'[_title]);
~~


\get_akt_dysp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Pobiera aktualnego dysponenta dla wskazanego pracownika
::   WE: _a [REFERENCE] - wskazanie na pracownika
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
_p_ref:={? var_pres('_a')=type_of(null) || _a || return(_result) ?};

PM_DYSP.cntx_psh();
PM_DYSP.index('AKT');
PM_DYSP.prefix('T',_p_ref);
{? PM_DYSP.first()
|| _result:=PM_DYSP.ref()
?};
PM_DYSP.cntx_pop();
_result


\pm_nag_wt_redn_wt_var_wt_r_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: "Po redagowaniu" pola WT_VAR.WT_R w oknie WT_REDN tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PM_NAG.R:=WT_VAR.WT_R().R;
~~


\pm_nag_wt_nag_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Po odświeżeniu" okna WEB_NAG tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_rok:=exec('web_global_params_get','pkw','PKW_POR_IRPM','ROK',date~1);
_msc:=exec('web_global_params_get','pkw','PKW_POR_IRPM','MSC',date~2);
_txt:=exec('pm_nag_modify_b','pm_wspolne',_rok,_msc);

_ag:=_ag_emp:={? +_txt || 'D' || '' ?};

{? PM_NAG.size()
|| _ag:={? PM_NAG.BLOK='T'
        || 'PUAW'
        |? PM_NAG.AKC='T'
        || 'PUA'
        || 'W'
        ?}
?};
_ag:='%1:%2'[_ag,_ag_emp];
PM_NAG.web_win_opt(web_top_win(),web_top_ident(),'grayed=%1'[_ag]);
exec('pm_nag_hdr_rok','!pkw_por_irpm')


\pm_prem_wt_wer_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Po odświeżeniu" okna WEB_NAG tabeli PM_NAG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:=exec('web_global_params_get','pkw','PKW_POR_IRPM','NAG_REF',null());

_ag:='';
PM_NAG.cntx_psh();
PM_NAG.prefix();
{? PM_NAG.seek(_ref) & PM_NAG.AKC='T'
|| _ag:='DPU'
?};
PM_NAG.cntx_pop();
PM_NAG.web_win_opt('WT_GRP','PM_PREM_WT_WER','grayed='+_ag);
~~


\pm_prem_wt_wer_dolacz_w_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Dołącz - z listy wszystkich pracowników" dla okna WT_WER tabeli PM_PREM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pm_prem_wt_wer_dolacz_a','!pkw_por_irpm','PM_ALL')


\pm_prem_wt_wer_dolacz_d_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Dołącz - z listy pracowników domyślnych" dla okna WT_WER tabeli PM_PREM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pm_prem_wt_wer_dolacz_a','!pkw_por_irpm','PM_DOM')


\pm_prem_wt_wer_dolacz_p_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Obsługa akcji "Dołącz - z listy pracowników podwładnych" dla okna WT_WER tabeli PM_PREM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pm_prem_wt_wer_dolacz_a','!pkw_por_irpm','PM_POD')


\pm_prem_wt_wer_dolacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Dołącz" dla okna WT_WER tabeli PM_PREM
::   WE: _a [STRING] - kod do prefiksowania się na wszystkich pracowników [PM_ALL] lub domyślnych [PM_DOM]
::                      PM_ALL - wszyscy pracownicy [w]
::                      PM_DOM - pracownicy domyślni [d]
::                      PM_POD - pracownicy podwładni [p]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kod:={? var_pres('_a')=type_of('') || _a || 'PM_ALL' ?};
exec('web_global_params_set','pkw','P_WEB_CX','TYPPOZ',_kod);
_ref:=exec('web_global_params_get','pkw','PKW_POR_IRPM','NAG_REF',null());
_tab:=sql(''+"select P from PM_PREM where PM_PREM.PM_NAG=:_a",_ref);
exec('p_web_cx_set','!pkw_por_irpm',_tab);
exec('p_web_cx_set_prefix','p_web',_kod);

_fml:="
   _kod:=exec('web_global_params_get','pkw','P_WEB_CX','TYPPOZ','');
   {? _kod<>''
   || exec('set_env','pkw_por');
      P_WEB_CX.cntx_psh();
      P_WEB_CX.prefix();
      _lTab:=exec('get','wt_group','WPU');
      {? _lTab.first()
      || _ref:=exec('web_global_params_get','pkw','PKW_POR_IRPM','NAG_REF',null());
         {? type_of(_ref)
         || PM_PREM.cntx_psh();
            PM_PREM.index('UNIQUE');
            PM_PREM.prefix();
            {!
            |? {? P_WEB_CX.seek(_lTab.KEY)
               || PM_PREM.blank(1);
                  PM_PREM.PM_NAG:=_ref;
                  PM_PREM.P:=P_WEB_CX.P;
                  {? ~PM_PREM.find_rec()
                  || PM_PREM.add()
                  ?}
               ?};
               _lTab.next()
            !};
            PM_PREM.cntx_pop()
         ?}
      ?};
      P_WEB_CX.cntx_pop()
   ?};
   PM_PREM.web_cntx_load(1);
   web_top_refresh(1)
";

web_params_set('fml',_fml);
PM_PREM.web_cntx_save(1);
P_WEB_CX.web_select('WEB_SEL')


\pm_prem_wt_wer_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Popraw" - grupowa
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_limit:=PM_PREM.PM_NAG().PM_BUDZ().LIMIT='T';
{? _limit
|| _red:='WT_GRPL';
   _SEL:=web_sel_aget(0);

   POLA_GRP.REAL_1:=PM_PREM.PM_NAG().PM_BUDZ().POZOST;
   {? _SEL.first()
   || PM_PREM.cntx_psh();
      PM_PREM.prefix();
      {!
      |? {? PM_PREM.seek(_SEL.REF)
         || POLA_GRP.REAL_1+=PM_PREM.KW
         ?};
         _SEL.next()
      !};
      PM_PREM.cntx_pop()
   ?};

   POLA_GRP.INT_1:=_SEL.size();
   {? POLA_GRP.INT_1
   || _zaokr:="roundmeth(1); _a$2";
      POLA_GRP.REAL_2:=_zaokr(POLA_GRP.REAL_1/POLA_GRP.INT_1)
   || FUN.error('Próba obsługi akcji grupowej nie powiodła się.'@);
      return(0)
   ?}

|| _red:='WT_GRP'
?};

web_params_set(
   'sel_rec',web_sel_aget(1),
   'limit',_limit
);

POLA_GRP.TXT_1:='N';
POLA_GRP.TXT_2:='N';
PM_PREM.web_efld_init(_red,,'enable=0',,'KW');
PM_PREM.web_efld_init(_red,,'enable=0',,'UWAGI');
PM_PREM.blank(1);
PM_PREM.web_cntx_save(1);
PM_PREM.web_edit(_red,~_red,,,$("
   _put:=0;
   {? _a='OK'
   || exec('set_env','pkw_por');
      _par:=web_params_get();
      _limit:=_par.limit;
      _SEL:=web_sel_aget(0,_par.sel_rec);

      {? PM_PREM.KW<0
      || return(__CHK.err_fld(PM_PREM,'KW',1,'"+'Wartość nie może być ujemna.'@+"'))
      |? _limit & POLA_GRP.REAL_2<PM_PREM.KW
      || return(__CHK.err_fld(PM_PREM,'KW',1,'"+'Kwota nie powinna być większa niż %1.'@ [form(POLA_GRP.REAL_2,,2)]+"'))
      ?};
      _kw:=PM_PREM.KW;
      _uwagi:=PM_PREM.UWAGI;

      _TAB:=tab_tmp(2,
         'KW','REAL','KWOTA',
         'UIDREF','STRING[48]','uidref'
      );
      {? _SEL.first()
      || PM_PREM.cntx_psh();
         PM_PREM.prefix();
         {!
         |? {? PM_PREM.seek(_SEL.REF)
            || _TAB.blank();
               _TAB.UIDREF:=PM_PREM.uidref();
               _TAB.KW:=PM_PREM.KW;
               _TAB.add()
            ?};
            _SEL.next()
         !};
         PM_PREM.cntx_pop()
      ?};

      {? _TAB.last()
      || do();
         {!
         |? {? PM_PREM.seek(_TAB.UIDREF)
            || _mod:=exec('pm_prem_modify_b','pm_wspolne');
               {? _mod=''
               || {? POLA_GRP.TXT_2='T'
                  || PM_PREM.UWAGI:=_uwagi
                  ?};
                  {? POLA_GRP.TXT_1='T' & PM_PREM.KW<>_kw
                  || {? PM_PREM.PM_NAG().PM_BUDZ().WYKON+_kw-PM_PREM.KW<=PM_BUDZ.PLAN
                     || PM_PREM.KW:=_kw
                     || undo()
                     ?}
                  ?};
                  _put+=PM_PREM.put()
               ?}
            ?};
            _TAB.prev()
         !};
         {? ~end()
         || FUN.info('"+'Poprawienie wszystkich zaznaczonych wierszy nie było możliwe.'@+"')
         ?}
      ?}
   ?};
   web_top_close();
   PM_PREM.web_cntx_load(1);
   {? _put
   || web_top_sel_adel(1,web_top_ident(1))
   ?};
   {? _limit
   || web_top_refresh(1,'PM_NAG_WT_NAG');
      _act_uid:='PKW_POR_IRPM';
      exec('web_global_params_set','pkw',_act_uid,'PM_PREM',PM_PREM.ref())
   || web_top_refresh(1)
   ?}
"));
0


\pm_prem_wt_wer_popraw_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Popraw" w oknie WT_WER tabeli PM_PREM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record')
|| return()
?};

web_params_set('kw',PM_PREM.KW);
PM_PREM.web_cntx_save(1);
PM_PREM.web_edit('WT_RED',,,,$("
   _put:=0;
   _par:=web_params_get();
   {? _a='OK'
   || exec('set_env','pkw_por');
      _mod:=exec('pm_prem_modify_b','pm_wspolne');
      {? _mod<>''
      || FUN.info(_mod)
      |? PM_PREM.KW<0
      || return(__CHK.err_fld(PM_PREM,'KW',1,'"+'Wartość nie może być ujemna.'@+"'))
      |? PM_PREM.PM_NAG().PM_BUDZ().LIMIT='N' | PM_BUDZ.WYKON+PM_PREM.KW-_par.kw<=PM_BUDZ.PLAN
      || _put:=PM_PREM.put()
      || return(__CHK.err_fld(PM_PREM,'KW',1,'"+'Budżet premii uznaniowych nie może być przekroczony.'@+"'))
      ?}
   ?};
   PM_PREM.web_cntx_load(1);
   web_top_close();
   {? _put
   || {? PM_PREM.PM_NAG().PM_BUDZ().LIMIT='T'
      || web_top_refresh(1,'PM_NAG_WT_NAG');
         _act_uid:='PKW_POR_IRPM';
         exec('web_global_params_set','pkw',_act_uid,'PM_PREM',PM_PREM.ref())
      || web_top_refresh(1)
      ?}
   ?}
"))


\pm_prem_wt_wer_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Usuń" - grupowa
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: zapamiętuje zaznaczone rekordy
_sel_rec:=web_sel_aget(1);
web_params_set('sel_rec',_sel_rec);

PM_PREM.web_cntx_save(1);
_text:='Usunięcie wszystkich zaznaczonych wierszy nie było możliwe.'@;
web_ask($("
   PM_PREM.web_cntx_load(1);
   exec('set_env','pkw_por');
   _del:=0;
   {? _a=1
   || PM_PREM.cntx_psh();
      PM_PREM.prefix();
::    odczytuje zaznaczone rekordy
      _tab:=web_sel_aget(0,web_params_get().sel_rec);
      _not_all:=0;
      _limit:=0;

      {? _tab.first()
      || {!
         |? {? PM_PREM.seek(_tab.REF)
            || _mod:=exec('pm_prem_modify_b','pm_wspolne');
               {? _mod<>''
               || _not_all+=1
               || _limit+=PM_PREM.PM_NAG().PM_BUDZ().LIMIT='T';
                  _del+=PM_PREM.del()
               ?}
            ?};
            _tab.next()
         !}
      ?};
      &_tab;
      PM_PREM.cntx_pop();

      {? _not_all
      || FUN.info('"+_text+"')
      ?}
   ?};

   {? _del
   || web_top_sel_adel(0,web_top_ident());
      {? _limit
      || web_top_refresh(,'PM_NAG_WT_NAG');
         _act_uid:='PKW_POR_IRPM';
         exec('web_global_params_set','pkw',_act_uid,'PM_PREM',PM_PREM.ref())
      || web_top_refresh()
      ?}
   ?}

"),'Czy usunąć zaznaczone wiersze?'@,'Premie uznaniowe'@);
0


\pm_prem_wt_wer_usun_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Usuń"
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PM_PREM.web_cntx_save(1);
web_ask("
   PM_PREM.web_cntx_load(1);
   {? _a=1
   || _limit:=0;
      _del:=0;
      exec('set_env','pkw_por');
      _mod:=exec('pm_prem_modify_b','pm_wspolne');
      {? _mod<>''
      || FUN.info(_mod)
      || _limit:=PM_PREM.PM_NAG().PM_BUDZ().LIMIT='T';
         _del:=PM_PREM.del(,1)
      ?}
   ?};
   {? _del
   || {? _limit
      || web_top_refresh(,'PM_NAG_WT_NAG');
         _act_uid:='PKW_POR_IRPM';
         exec('web_global_params_set','pkw',_act_uid,'PM_PREM',PM_PREM.ref())
      || web_top_refresh()
      ?}
   ?}
",'Czy usunąć bieżący wiersz?'@,'Premie uznaniowe'@)


\p_web_cx_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Ustala rekordy pracowników możliwych do wyboru.
::   WE: [_a] [DICT] - tabela z refami do pominięcia
::   WY: zwraca liczbę>0 lub 0 w przypadku braku powiązania pracownika z zalogowaną osobą
::----------------------------------------------------------------------------------------------------------------------
_tab:={? var_pres('_a')=type_of(SYSLOG) || _a || ~~ ?};
_kod:=exec('web_global_params_get','pkw','P_WEB_CX','TYPPOZ','PM_ALL');
_dysp_ref:=exec('web_global_params_get','pkw','PKW_POR_IRPM','DYSP_REF',null());

_tcid:=app_info('web_tcid');
_sesid:=app_info('web_sesid');
_tabid:=app_info('web_tabid');

_result:=0;
_jest_tab:=type_of(_tab) & var_pres('P',_tab)>0;

_add:="
   _result:=0;
   {? P.F_ZATR().DOCELOWA='T'
   || P_WEB_CX.P:=P.ref();
      P_WEB_CX.OSOBA:=P.OSOBA;
      P_WEB_CX.UD_SKL:=P.WYDZIAL;
      P_WEB_CX.STN:=P.ST;
      P_WEB_CX.CP:=P.CP;
      P_WEB_CX.F_ZATR:=P.F_ZATR;
      P_WEB_CX.ZA:=P.ZA;
      P_WEB_CX.RODZAJ:='P';
      _result:=P_WEB_CX.add(1)
   ?};
   _result
";

P_WEB_CX.cntx_psh();
exec('p_web_cx_set_prefix','p_web',_kod);

{? P_WEB_CX.first()
|| {! |? P_WEB_CX.del() !}
?};

{? ~P_WEB_CX.first()
|| {? _kod='PM_ALL'
   || P.cntx_psh();
      P.index('PRACOSOW');
      _firma:=exec('ref_firma','ustawienia');
      P.prefix(_firma);
      {? P.first()
      || F_ZATR.cntx_psh();
         P_WEB_CX.blank();
         P_WEB_CX.TCID:=_tcid;
         P_WEB_CX.SESID:=_sesid;
         P_WEB_CX.TABID:=_tabid;
         P_WEB_CX.TYPPOZ:=_kod;
         {!
         |? {? (~_jest_tab) | (_jest_tab & (~_tab.find_tab(,'P',,'=',$P.ref())))
            || _result+=_add()
            ?};
            P.next()
         !};
         F_ZATR.cntx_pop()
      ?};
      P.cntx_pop()
   |? _kod='PM_DOM'
   || PM_DOMP.cntx_psh();
      PM_DOMP.index('UNIQUE');
      PM_DOMP.prefix(_dysp_ref);
      {? PM_DOMP.first()
      || P.cntx_psh();
         F_ZATR.cntx_psh();
         P_WEB_CX.blank();
         P_WEB_CX.TCID:=_tcid;
         P_WEB_CX.SESID:=_sesid;
         P_WEB_CX.TABID:=_tabid;
         P_WEB_CX.TYPPOZ:=_kod;
         {!
         |? PM_DOMP.P();
            {? (~_jest_tab) | (_jest_tab & (~_tab.find_tab(,'P',,'=',$P.ref())))
            || _result+=_add()
            ?};
            PM_DOMP.next()
         !};
         F_ZATR.cntx_pop();
         P.cntx_pop()
      ?};
      PM_DOMP.cntx_pop()

   |? _kod='PM_POD'
   || PM_DYSP.cntx_psh();
      PM_DYSP.prefix();
      {? PM_DYSP.seek(_dysp_ref)
      || _TAB:=exec('prac_pod','stanprac',PM_DYSP.OSOBA,'TYPPOZ','DOSTINF');
         {? _TAB.first()
         || P.cntx_psh();
            P.index('OSOBA');
            P.prefix();
            F_ZATR.cntx_psh();
            P_WEB_CX.blank();
            P_WEB_CX.TCID:=_tcid;
            P_WEB_CX.SESID:=_sesid;
            P_WEB_CX.TABID:=_tabid;
            P_WEB_CX.TYPPOZ:=_kod;
            {!
            |? {? P.seek(_TAB.P_SQL)
               || {? (~_jest_tab) | (_jest_tab & (~_tab.find_tab(,'P',,'=',$P.ref())))
                  || _result+=_add()
                  ?}
               ?};
               _TAB.next()
            !};
            F_ZATR.cntx_pop();
            P.cntx_pop()
         ?}
      ?};
      PM_DYSP.cntx_pop()
   ?}
|| _result:=1
?};
P_WEB_CX.cntx_pop();
_result


\pm_prem_wt_grp_txt_1_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Po redakcji pola POLA_GRP.TXT_1 w okienku WT_RGP tabeli PM_PREM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pm_prem_efld_opt','!pkw_por_irpm')


\pm_prem_wt_grp_txt_2_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Po redakcji pola POLA_GRP.TXT_1 w okienku WT_RGP tabeli PM_PREM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pm_prem_efld_opt','!pkw_por_irpm')


\pm_prem_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli PM_PREM.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE:  _a  [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Identyfikator okna w grupie (parametr obowiązkowy wyłącznie dla webTerma).
::       [_e] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::----------------------------------------------------------------------------------------------------------------------
_webTerm:=app_info('web_sesid')<>'';

_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(NWU)
|| _TAB:=_b
|? _tryb='1'
|| _TAB:={? _webTerm || web_top_tab() || cur_tab(1,1) ?}
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:={? _webTerm || web_top_win() || cur_win(1,1) ?}
|| return(0)
?};
_grp_ident:={? var_pres('_d')=type_of('') || _d || '' ?};
{? var_pres('_e')=type_of('')
|| _fld:=_e
|? _tryb='1'
|| _fld:={? _webTerm || web_top_fld_acr() || cur_afld() ?}
|| _fld:=''
?};

_set:=exec('efld_opt','#window',_webTerm,_we,_grp_ident);

{? _fld='' | _fld='TXT_1'
||
   _sval:=$(POLA_GRP.TXT_1='T');
   _set(_TAB,_we,_grp_ident,'enable=%1' [_sval],,'KW')
?};
{? _fld='' | _fld='TXT_2'
||
   _sval:=$(POLA_GRP.TXT_2='T');
   _set(_TAB,_we,_grp_ident,'enable=%1' [_sval],,'UWAGI')
?};

1


\pm_nag_wt_nag_prac_dom_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Wyświetla listę pracowników domyślnych
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:=exec('web_global_params_get','pkw','PKW_POR_IRPM','DYSP_REF',null());
PM_DOMP.index('UNIQUE');
PM_DOMP.prefix(_ref);
PM_DOMP.web_select('WT_WER');
~~


\pm_domp_wt_wer_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Przed obsługą okna WT_WER tabeli PM_DOMP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','pkw_por')


\pm_domp_wt_wer_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Usuń" - grupowa
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

:: zapamiętuje zaznaczone rekordy
_sel_rec:=web_sel_aget(1);
web_params_set('sel_rec',_sel_rec);

PM_DOMP.web_cntx_save(1);
_text:='Usunięcie wszystkich zaznaczonych wierszy nie było możliwe.'@;
web_ask($("
   PM_DOMP.web_cntx_load(1);
   _del:=0;
   {? _a=1
   || PM_DOMP.cntx_psh();
      PM_DOMP.prefix();
::    odczytuje zaznaczone rekordy
      _tab:=web_sel_aget(0,web_params_get().sel_rec);
      _not_all:=0;

      {? _tab.first()
      || {!
         |? {? PM_DOMP.seek(_tab.REF)
            || {? PM_DOMP.del(,1)
               || _del+=1
               || _not_all+=1
               ?}
            ?};
            _tab.next()
         !}
      ?};
      &_tab;
      PM_DOMP.cntx_pop();

      {? _not_all
      || FUN.info('"+_text+"')
      ?}
   ?};

   {? _del
   || web_top_sel_adel(0,web_top_ident())
   ?};
   web_top_refresh()
"),'Czy usunąć zaznaczone wiersze?'@,'Pracownicy domyślni'@);
0


\pm_domp_wt_wer_usun_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Usuń"
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PM_DOMP.web_cntx_save(1);
web_ask("
   PM_DOMP.web_cntx_load(1);
   {? _a=1
   || PM_DOMP.del()
   ?};
   web_top_refresh()
",'Czy usunąć bieżący wiersz?'@,'Pracownicy domyślni'@)


\pm_domp_wt_wer_dolacz_pr_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Dołącz - z listy pracowników"
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pm_domp_wt_wer_dolacz_a','!pkw_por_irpm','PM_ALL')


\pm_domp_wt_wer_dolacz_pod_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Dołącz - z listy podwładnych"
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pm_domp_wt_wer_dolacz_a','!pkw_por_irpm','PM_POD')


\pm_domp_wt_wer_dolacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Obsługa akcji "Dołącz" dla okna WT_WER tabeli PM_DOMP
::   WE: _a [STRING] - kod do prefiksowania się na wszystkich pracowników [PM_ALL] lub podwładnych [PM_POD]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kod:={? var_pres('_a')=type_of('') || _a || 'PM_ALL' ?};
exec('web_global_params_set','pkw','P_WEB_CX','TYPPOZ',_kod);
_ref:=exec('web_global_params_get','pkw','PKW_POR_IRPM','DYSP_REF',null());
_tab:=sql(''+"select P from PM_DOMP where PM_DOMP.PM_DYSP=:_a",_ref);
exec('p_web_cx_set','!pkw_por_irpm',_tab);
exec('p_web_cx_set_prefix','p_web',_kod);

_fml:="
   _add:=0;
   _kod:=exec('web_global_params_get','pkw','P_WEB_CX','TYPPOZ','');
   {? _kod<>''
   || exec('set_env','pkw_por');
      P_WEB_CX.cntx_psh();
      P_WEB_CX.prefix();
      _lTab:=exec('get','wt_group','WPU');
      {? _lTab.first()
      || _ref:=exec('web_global_params_get','pkw','PKW_POR_IRPM','DYSP_REF',null());
         {? type_of(_ref)
         || PM_DOMP.cntx_psh();
            PM_DOMP.index('UNIQUE');
            PM_DOMP.prefix();
            {!
            |? {? P_WEB_CX.seek(_lTab.KEY)
               || PM_DOMP.blank(1);
                  PM_DOMP.PM_DYSP:=_ref;
                  PM_DOMP.P:=P_WEB_CX.P;
                  {? ~PM_DOMP.find_rec()
                  || _add+=PM_DOMP.add()
                  ?}
               ?};
               _lTab.next()
            !};
            PM_DOMP.cntx_pop()
         ?}
      ?};
      P_WEB_CX.cntx_pop()
   ?};
   PM_DOMP.web_cntx_load(1);
   {? _add
   || web_top_refresh(1)
   ?}
";

web_params_set('fml',_fml);
PM_DOMP.web_cntx_save(1);
P_WEB_CX.web_select('WEB_SEL')


\wt_pbudz_wt_wyb_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Przed obsługą okna WT_WYB tabeli WT_PBUDZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Zawsze zaczynamy, od przekazania parametrów "dalej".
web_params_set(web_params_get());
:: Ustawiam środowisko.
exec('set_env','pkw_por');

_tcid:=app_info('web_tcid');
_sesid:=app_info('web_sesid');
_tabid:=app_info('web_tabid');

WT_PBUDZ.index('SKLOD');
WT_PBUDZ.prefix(_tcid,_sesid,_tabid,);

~~


\wt_pbudz_wybierz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz" w oknie WT_WYB tabeli WT_PBUDZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record')
|| return()
?};

_act_uid:='PKW_POR_IRPM';
_rok:=exec('web_global_params_get','pkw',_act_uid,'ROK',date~1);
_msc:=exec('web_global_params_get','pkw',_act_uid,'MSC',date~2);

PM_BUDZ.cntx_psh();
PM_BUDZ.prefix();
WT_PBUDZ.PM_BUDZ();
PM_NAG.cntx_psh();
PM_NAG.prefix();
PM_NAG.blank();
PM_NAG.PM_BUDZ:=PM_BUDZ.ref();
PM_NAG.ROK:=_rok;
PM_NAG.MSC:=_msc;
exec('pm_nag_add','pm_wspolne',1);
web_top_refresh(1);
PM_NAG.cntx_pop();
PM_BUDZ.cntx_pop();

web_top_close();

~~

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:59 6b6c3788e77f8f229532a14ec02f2f77a2f994a01c2ce819692c246dda8efe7836d75e5715efe8d2702c2acd1392c8ad430ed1d780d3c987dcce44c7d120904516f28ab8b05a1ee6b0d64deae9a6c9285f28022f0b477a9a5d6f2f81595ce6cf838846137e576630818d1101e78beee17bd281e78879bf967085fe81a61220fa

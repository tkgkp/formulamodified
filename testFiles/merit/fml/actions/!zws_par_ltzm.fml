:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ltzm.fml
:: Utworzony: 07.10.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_LTZM
::            Magazyny do realizacji zamówień sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Czynność ZWS_PAR_LTZM - magazyny do realizacji zamówień sprzedaży
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejściowe czynności
::       _int - [obj_new] - parametry wewnętrzne czynności
::       _out - [obj_new] - parametry wyjściowe czynności
::       _mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
exec('select','!zws_par_ltzm');
''


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: magazyny do realizacji zamówień sprzedaży
::  OLD: \par_dod/defin.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__zmuprzk');
POM.TYPDOK:='ZAM';
USER.AKR:='ZAM';
_buf:=tab_tmp(1,'MG','STRING[16]','');
__zmuprzk:=1;
_user:=OPERATOR.USER;
OPERATOR.USER:=null();
MG.f_clear();
USERS_UP.dnd_sel('MGPAR',,'records.USERS_UP',"exec('zmnrusmg','!zws_par_ltzm')");
USERS_UP.win_sel('MGPAR');
USERS_UP.index('MGNR');
USERS_UP.prefix(null,POM.TYPDOK);
{? USERS_UP.first()
|| {!
   |? _buf.blank();
      _buf.MG:=$USERS_UP.MG;
      _buf.add(1);
      USERS_UP.next()
   !}
?};
USERS_UP.first();

USERS_UP.select();
OPERATOR.USER:=_user;
exec('usuprmag','parses');
USERS_UP.dnd_sel('MGPAR',,'records.USERS_UP',"");
__zmuprzk:=0;
USERS_UP.index('MGNR');
USERS_UP.prefix(null,POM.TYPDOK);
{? USERS_UP.first()
|| {!
   |? {? _buf.clear(); _buf.find_key($USERS_UP.MG)
      || _buf.del()
      || __zmuprzk:=1
      ?};
      ~__zmuprzk & USERS_UP.next()
   !}
?};
{? ~__zmuprzk & _buf.size() || __zmuprzk:=1 ?};

{? __zmuprzk || exec('aktstzam','zamsiw_wspolne',0) ?};
VAR_DEL.delete('__zmuprzk');
obj_del(_buf);
~~


\zmnrusmg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [12.30]
:: OPIS: zmienia numer USERS_UP.NR
::   WE: [_a] - ''(domyślnie) - drag&drop, 'U','D','N'-akcja do przenumerowania
::  OLD: \zmnrusmg/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (';UDN'*_a)>1 || _a || '' ?};

USERS_UP.cntx_psh();
{? _tryb=''
|| _ref:=dnd_info('dest_record');
   {? USERS_UP.seek(_ref) || exec('zmien_lp','#dragdrop','NR','MGNR') ?}
|| exec('zmien_lpa','#dragdrop','NR','MGNR',,,_tryb)
?};
USERS_UP.cntx_pop();
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 2fb4c17972cb08d8af6c9a0918fe640524ca099da89e46787a6db06ec57ce70413dbb5f67ce5a9405ae8999833a124c07828c46aa623f5b23a03bd4003714dae645170e96adad5f4a4016815fce45c3f6cd36deebf9f88d5a59ed93446231c9797228d78e6737f46ddeb107949ad4c85af41c81f469e74c55f11ae008e666870

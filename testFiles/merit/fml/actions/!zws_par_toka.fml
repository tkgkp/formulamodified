:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_toza.fml
:: Utworzony: 11.04.2016
:: Autor: MLAK
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_TOKA - Kalendarze wydziałów
::             Czynność parametryzacyjna
::=====================================================================================================================+


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [17.00]
:: OPIS: Formuła główna czynności parametryzacyjnej (nieprocesowej) - Kalendarze wydziałów
::  TAG: <CZYNNOŚĆ><LISTA>
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.cntx_psh();
_typ:=exec('szukaj_ud_typ','schemat','PODZORG');
_sch:=exec('domyslny','schemat',_typ);
UD_DEF.win_sel('WER_PO');
UD_DEF.index('SYMBOL');
UD_DEF.prefix(_sch);
: odcięcie drzewka do poziomu firmy
: (o ile firma została określona)
_skl:=exec('ud_skl_firma','schemat',_typ);
exec('ud_def_root','schemat',UD_DEF.win_sel('?'),_sch,_skl);
UD_DEF.select();
UD_DEF.cntx_pop();
~~


\chk_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2009]
:: OPIS: Sprawdzenie czy mozna redagowac strukture widoku planu operacyjnego.
::  OLD: \chk_str/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_red:=KAL_UD.mk_edit(FUN.TYT);
KAL_UD.win_efld(_red,,'KAL','NAZWA','KAL_CZES',15);
exec('ok_esc','#window',KAL_UD,_red);
KAL_UD.win_edit(_red);
KAL_UD.index('RESORWYD');
KAL_UD.prefix(UD_SKL.ref(),'W');
{? KAL_UD.first()
|| _last:=KAL_UD.PL_RODZ;
   {? KAL_UD.edit()
   || KAL_UD.put();
      exec('kalud_res_pop','!zws_par_toka',_last)
   ?}
|| KAL_UD.blank();
   KAL_UD.UD_SKL:=UD_SKL.ref();
   {? KAL_UD.edit()
   || KAL_UD.add()
   ?}
?};
exec('reset_kal_ud','po_param',KAL_UD.ref());
~~


\kalud_res_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2011]
:: OPIS: Po popraw na KAL_UDzie - jesli poprawilem rodzaj planowania na KAL_UDzie wydzialowym to pytam sie czy poprawic
::       tez ten rodzaj na wszystkich KAL_UDach zasobowych na tym wydziale
::       Pracuje w kontekcie aktualnego KAL_UDa
::       _a - wartosc KAL_UD.PL_RODZ przed poprawieniem
::  OLD: \kalud_res_pop/pl_okr3.fml
::----------------------------------------------------------------------------------------------------------------------
_last:='';
_current:=KAL_UD.PL_RODZ;
{? _>0
|| _last:=_a
?};
{? KAL_UD.RESORWYD='W'
|| KAL_UD.cntx_psh();
   UD_SKL.cntx_psh();
   KAL_UD.index('RESORWYD');
   KAL_UD.prefix(KAL_UD.UD_SKL,'R');
   KAL_UD.first();
::jesli sie rozni od ostatniej i sa kaludy zasobowe to pytam czy poprawke naniesc w dol
   {? _last<>_current & KAL_UD.size()>0
   || {? FUN.ask(
            'Zmieniono rodzaj planowania.\n'
            'Czy poprawić rodzaj planowania na wszystkich zasobach do planowania na tym wydziale?'@
         )
      || {? KAL_UD.first()
         || {!
            |? KAL_UD.PL_RODZ:=_current;
               KAL_UD.put();
               KAL_UD.next()
            !}
         ?}
      ?}
   ?};
   KAL_UD.cntx_pop();
   UD_SKL.cntx_pop
?};
~~


\czy_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2009]
:: OPIS: Czy element struktury moze byc redagowany.
::   WE: _a: (0/1) Czy dawac komunikaty na ekran
::   WY: 0/1
::  OLD: \czy_red/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh();
_kod:=UD_DEF.UD_SKL().SYMBOL;
SLO.index('SL');
SLO.prefix(XINFO.SLWYDZIA,_kod,);
{? SLO.first()
|| SLO.cntx_pop();
   1
|| {? _a=1
   || FUN.info('Akcja dostępna tylko dla wydziałów.'@)
   ?};
   SLO.cntx_pop();
   0
?}


\pop_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2009]
:: OPIS: Sprawdzenie czy mozna redagowac strukture widoku planu okresowego.
::  OLD: \pop_str/pl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=#UD_DEF.ref();
UD_DEF.cntx_psh();
UD_SKL.cntx_psh();
KAL_UD.cntx_psh();
UD_DEF.index('SYMBOL');
UD_DEF.prefix(UD_SCH.ref(),_ref);
{? UD_DEF.first()
|| {!
   |? UD_DEF.UD_SKL();
      KAL_UD.index('KAL');
      KAL_UD.prefix(UD_SKL.ref());
      {? ~KAL_UD.first()
      || KAL_UD.blank();
         KAL_UD.UD_SKL:=UD_SKL.ref();
         KAL_UD.KAL:=_a;
         KAL_UD.PL_WZOR:=_b;
         KAL_UD.add()
      || KAL_UD.KAL:=_a;
         KAL_UD.PL_WZOR:=_b;
         KAL_UD.put()
      ?};
      exec('pop_str','!zws_par_toka',KAL_UD.KAL,KAL_UD.PL_WZOR);
      exec('reset_kal_ud','po_param',KAL_UD.ref());
      UD_DEF.next()
   !}
?};
KAL_UD.cntx_pop();
UD_SKL.cntx_pop();
UD_DEF.cntx_pop();
~~


\uddef_az
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2011]
:: OPIS: Wyswietla tabelke z zasobami przypisanymi do widoku uzywanego na wydziale
::  OLD: \plres_powers/pl_okr3.fml
::----------------------------------------------------------------------------------------------------------------------
KAL_UD.cntx_psh();
KAL_UD.index('RESORWYD');
KAL_UD.prefix(UD_SKL.ref(),'R');

_okno:=KAL_UD.mk_sel('Zasoby do planowania'@,,,'plrespowers');
KAL_UD.win_fld(_okno,,'PL_RES','TYP',,5);
KAL_UD.win_fld(_okno,,'PL_RES','SYM',,10);
KAL_UD.win_fld(_okno,,'PL_WZOR','NAZWA',,15);

::formula na wyswietlanie ikon
KAL_UD.win_fml(_okno,,'PL_RES','TYP','ICON_BEFORE',"'xwin16.png:'+$exec('ikona_res','po_plan',KAL_UD.PL_RES().TYP)");

KAL_UD.win_act(_okno,,'Rekord',,,,"KAL_UD.PL_RES()");
KAL_UD.win_act(_okno,,'Formuła','Miesiąc pracy'@,,,"exec('start_res','kal_mies',PL_RES.TYP,PL_RES.REF)");
KAL_UD.win_act(_okno,,'Formuła','&Legenda'@@,,'Legenda'@,
"exec('legenda','color','#PL_RES#01')",,,,,,'L');
exec('__KAL','object');
KAL_UD.win_sel(_okno);
KAL_UD.select();

KAL_UD.cntx_pop();
~~


\uddef_berec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [17.00]
:: OPIS: Rekord przed dla okna UD_DEF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.UD_SKL();
KAL_UD.index('RESORWYD');
KAL_UD.prefix(UD_SKL.ref(),'W');
{? ~KAL_UD.first()
|| KAL_UD.blank();
   KAL_UD.UD_SKL:=UD_SKL.ref()
?};
~~


\uddef_ap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [17.00]
:: OPIS: Przpisanie kalendarza i zseatwu do wydziału
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('chk_str','!zws_par_toka');
{? exec('czy_red','!zws_par_toka',0)=0
|| {? FUN.ask('Czy dokonać poprawek na wszystkich podrzędnych elementach struktury'
              '\n dla których nie wygenerowano  planu?'@)
   || exec('pop_str','!zws_par_toka',KAL_UD.KAL,KAL_UD.PL_WZOR)
   ?}
|| exec('pop_str','!zws_par_toka',KAL_UD.KAL,KAL_UD.PL_WZOR)
?};
~~

:Sign Version 2.0 jowisz:1045 2020/04/03 17:04:56 b8535456c7d9ef4ca6954c46cfc00dbd9587947308da74d36866034f996eead4ef602ab49af3344679336ef9eee8a0475b79c7e420d90d37711275c8eb10699ee6eb5a0a7def543480fc79388f6c2c9bca6a57f3b320137db211894dc552d805c75f442d43879c5058e3d7e066ce74bfb24913ed302216b184bbcd2df17bd516

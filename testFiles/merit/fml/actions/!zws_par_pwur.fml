:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pwur.fml
:: Utworzony: 12.02.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PWUR - Wymiary urlopu.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Redagowanie wymiarów urlopów - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \wyb_rok/kart_url.fml
::  OLD: \tab_lim/kart_url.fml
::  OLD: \lim_nsp/kart_url.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~KST_URL.lock(1,1,1)
|| FUN.info('Czynność jest blokowana przez innego użytkownika.'@+'\n'+'Spróbuj ponownie za chwilę.'@);
   return()
?};

_par:=params_get();
_rok:=
   {? var_pres('_par')>100 & var_pres('in',_par)>100 & var_pres('FP_INS_NUM',_par.in)=type_of(0)
   || _par.in.FP_INS_NUM
   || date()~1
   ?};

KST_URL.cntx_psh();
KST_URL.index('KST_URL');
KST_URL.win_edit('RED');

_sql:='select distinct KST_URL.ROK, 0 URL_LIM, 0 LIM_NSP, 0 LIM_NAUK from KST_URL order by 1 desc';
_sql+=' union distinct ';
_sql+='select distinct KART_URL.ROK, 0 URL_LIM, 0 LIM_NSP, 0 LIM_NAUK from KART_URL order by 1 desc';

KST_DEF.cntx_psh();
KST_DEF.index('SYMBOL');
{! _lp:=1..3
|! _zm:={? _lp=1 || 'KST_PAR.URL_LIM' |? _lp=2 || 'KST.LIM_NSP' || 'KST.LIM_NAUK' ?};
   KST_DEF.prefix(_zm,);
   {? KST_DEF.first()
   || _sql+=' union distinct ';
      _sql+='select distinct extract(year from KST_WAR.DATA) ROK, 0 URL_LIM, 0 LIM_NSP, 0 LIM_NAUK ';
      _sql+='from KST_WAR ';
      _sql+='where KST_WAR.KST_DEF=\''+$KST_DEF.ref()+'\' ';
      {? KST_DEF.WSPOLNA<>'T'
      || _sql+=' and KST_WAR.FIRMA=\''+$REF.FIRMA+'\' '
      ?};
      _sql+='order by 1 desc'
   ?}
!};
KST_DEF.cntx_pop();

_tabR:=sql(_sql);
{? ~_tabR.find_key(_rok)
|| _tabR.blank();
   _tabR.ROK:=_rok;
   _tabR.add()
?};

{? _tabR.last() || exec('kst_sync','!zws_par_pwur',_tabR,'k2t') ?};

_tabR.fld_fml('ROK','BLANK',"
   _tabR:=cur_tfld(); _tabR.cntx_psh(); _bl:={? _tabR.first() || _tabR.ROK+1 || date()~1 ?}; _tabR.cntx_pop(); _bl");

_tabR.fld_fml('ROK','BEFORE_EDIT',"-menu_txt()<>'popraw'");

: Formuły obsługująca akcje, domyślnie wywoływane z bieżącego pliku.
_fml0:='"params_set(params_get()); '+
       'exec(\'"+_a+"\',\'"+{? var_pres(\'_b\')=type_of(\'\') || _b || \'!zws_par_pwur\' ?}+"\')"';
_fml1:='"params_set(params_get()); '+
       'exec(\'"+_a+"\',\'"+{? var_pres(\'_b\')=type_of(\'\') || _b || \'!zws_par_pwur\' ?}+"\',_a"+'+
       '{? var_pres(\'_c\')=type_of(\'\') || ",\'"+_c+"\'" || "" ?}+")"';

: Okna tabeli tymczasowej _tabR
_wsR:=_tabR.mk_sel('Lata'@,'N',,'#pklm',,,,,'U');
_tabR.win_fld(_wsR,,'ROK',,,-4,-1,,'Rok'@,,'Rok kalendarzowy'@);
_tabR.win_fld(_wsR,,'URL_LIM',,,-7,-1,,'Urlop na żądanie'@,,
   'Wymiar urlopu udzielanego na żądanie pracownika (w ramach urlopu wypoczynkowego)'@);
_tabR.win_fld(_wsR,,'LIM_NSP',,,-7,-1,,'Urlop dodatkowy'@,,
   'Wymiar urlopu dodatkowego dla pracownika ze znacznym lub umiarkowanym stopniem niepełnosprawności'@);
_tabR.win_fld(_wsR,,'LIM_NAUK',,,-7,-1,,'Urlop wypoczynkowy pracowników naukowych'@,,
   'Wymiar urlopu wypoczynkowego pracowników naukowych i badawczo-technicznych'@);
_tabR.win_act(_wsR,1,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_fml0)('tabr_dolacz'),,1,,,,'D');
_tabR.win_act(_wsR,0,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_fml0)('tabr_dolacz'),,,,,,'D');
_tabR.win_act(_wsR,,'Formuła','Popraw'@@,,'Poprawienie bieżącego zapisu'@,$($_fml0)('tabr_popraw'),,,,,,'P');
_tabR.win_act(_wsR,,'Formuła','Usuń'@@,,'Usunięcie bieżącego zapisu'@,$($_fml0)('tabr_usun'),,,,,,'U');
_tabR.win_act(_wsR,,'Formuła','Kopiuj'@@,,
   'Kopiowanie zapisu bieżącego roku do wskazanego'@,$($_fml0)('tabr_kopiuj'),,,,,,'K');
_tabR.win_act(_wsR,,'Szukaj',,,'Szukanie zapisu zgodnego ze zredagowanym wzorcem'@);
_btnD:=_tabR.win_btn(_wsR,'text='+'&Dołącz'@+',panel=right,align=begin','menu:D',);
_tabR.btn_opt(_btnD,'tooltip='+'Dołączenie nowego zapisu'@);
_btnP:=_tabR.win_btn(_wsR,'text='+'&Popraw'@+',panel=right,align=begin','menu:P',,,,,,'noempty');
_tabR.btn_opt(_btnP,'tooltip='+'Poprawienie bieżącego zapisu'@);
_btnU:=_tabR.win_btn(_wsR,'text='+'&Usuń'@+',panel=right,align=begin','menu:U',,,,,,'noempty');
_tabR.btn_opt(_btnU,'tooltip='+'Usunięcie bieżącego zapisu'@);
_btnK:=_tabR.win_btn(_wsR,'text='+'&Kopiuj'@+',panel=right,align=begin','menu:K',,,,,,'noempty');
_tabR.btn_opt(_btnK,'tooltip='+'Kopiowanie zapisu bieżącego roku do wskazanego'@);

_cfg:=',btn_label_align=center,panel=bottom,align=end';

_weR1:=_tabR.mk_edit('|--|'+'Rok'@,,'#pklme1');
_tabR.win_esep(_weR1,'Dane podstawowe'@);
_tabR.win_efld(_weR1,,'ROK',,,4,-1,,'Rok kalendarzowy'@,,'Rok kalendarzowy'@);
_tabR.efld_opt(_weR1,'mark=1',,'ROK');
_tabR.win_esep(_weR1,'Wymiary roczne'@);
_tabR.win_efld(_weR1,,'URL_LIM',,,4,-1,,'Urlop na żądanie'@,,
   'Wymiar urlopu udzielanego na żądanie pracownika (w ramach urlopu wypoczynkowego)'@);
_tabR.win_efld(_weR1,,'LIM_NSP',,,4,-1,,'Urlop dodatkowy'@,,
   'Wymiar urlopu dodatkowego dla pracownika ze znacznym lub umiarkowanym stopniem niepełnosprawności'@);
_tabR.win_efld(_weR1,,'LIM_NAUK',,,4,-1,,'Urlop wypoczynkowy pracowników naukowych'@,,
   'Wymiar urlopu wypoczynkowego pracowników naukowych i badawczo-technicznych'@);
exec('ok_esc','#window',_tabR,_weR1,,,,,,,exec('text_red_ok','#window'));
_tabR.win_edit(_weR1);

_weR2:=_tabR.mk_edit('|--|'+'Rok kalendarzowy'@,,'#pklme2');
_tabR.win_esep(_weR2,'Dane podstawowe'@);
_tabR.win_efld(_weR2,,'ROK',,,4,-1,,'Rok kalendarzowy'@,,'Rok, na który zostaną skopiowane wymiary bieżącego roku'@);
_tabR.efld_opt(_weR2,'mark=1',,'ROK');
exec('ok_esc','#window',_tabR,_weR2,,,,,,,exec('text_red_ok','#window'));

: Budowa okienka grupowego
_mode:='maximized_with_title';
_gwer:=_tabR.grp_make('Wymiary urlopów'@,,'#pklmg',,,,,'normal');
_tabR.grp_sel(_gwer,_tabR,_wsR,,$($_fml0)('tabr_ar'),,,10,,,,,_mode);
_tabR.grp_splt(_gwer,,'vertical','kst_url');
_tabR.grp_sel(_gwer,KST_URL,'WER',,"",,,10,"","",,,_mode);
_tabR.win_sel(_gwer);

_fml:=KST_URL.fld_fml('ROK','*BLANK');

params_set('weR1',_weR1,'weR2',_weR2);
_tabR.find_le(_rok);
_tabR.select(,1,-1);

KST_URL.fld_fml('ROK','BLANK',_fml);

KST_URL.cntx_pop();
KST_URL.unlock();
~~


\kart_url_first
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła sprawdza, czy dla danego roku istnieje choć jeden zapis w karcie urlopowej.
::   WE: _a - Badany rok.
::   WY: 1 - Zapisy istnieją.
::       0 - Brak zapisów dla wskazanego roku.
::  OLD: \spr_kart/kart_url.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:=_a;

KART_URL.cntx_psh();
KART_URL.index('ROK_PRAC');
KART_URL.prefix(_rok);
{? KART_URL.first()
|| _ret:=1;
   FUN.info(
      'W systemie istnieją już karty urlopowe dla roku %1.\n'
      'Po wprowadzeniu zmian zalecana jest ich aktualizacja.'@ [$_rok]
   )
|| _ret:=0
?};
KART_URL.cntx_pop();
_ret


\kst_url_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz - przed".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('kart_url_first','!zws_par_pwur',cur_tab(0,0).ROK);
1


\kst_url_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw - przed".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('kart_url_first','!zws_par_pwur',cur_tab(0,0).ROK);
1


\kst_url_ub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń - przed".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('kart_url_first','!zws_par_pwur',cur_tab(0,0).ROK);
1


\kst_url_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu rekordu tabeli KST_UR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.record(KST_URL,,'LIMIT');
{? _chk<>'' || return(_chk) ?};
{? KST_URL.STAZ<0 | KST_URL.STAZ>=100
|| FUN.emsg('Błędny staż pracy.'@);
   return('STAZ')
|? KST_URL.LIMIT<=0 | KST_URL.LIMIT>=365
|| FUN.emsg('Błędny wymiar urlopu.'@);
   return('LIMIT')
?};
__CHK.index(KST_URL,-menu_txt()='popraw')=''


\kst_sync
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Synchronizuje zapisy w stałych systemu (KST) i tabeli tymczasowej tabR. Operacja dotyczy wszystkich lat,
::       poczynając od bieżącego roku.
::   WE: _a - Uchwyt do tabeli.
::       _b - Kierunek synchronizacji:
::            'k2t' - KST -> _tabR
::            't2k' - _tabR -> KST
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_tabR:=_a;

_sync:=
   {? _b='k2t'
   || "_a.URL_LIM:=KST_PAR.URL_LIM; _a.LIM_NSP:=KST.LIM_NSP; _a.LIM_NAUK:=KST.LIM_NAUK; _a.put()"
   |? _b='t2k'
   || "
      KST.LIM_NSP:=_a.LIM_NSP; KST.LIM_NAUK:=_a.LIM_NAUK; exec('zapisz','#stalesys',_b,KST,'LIM_NSP','LIM_NAUK');
      KST_PAR.URL_LIM:=_a.URL_LIM; exec('zapisz','#stalesys',_b,KST_PAR,'URL_LIM')
      "
   || ""
   ?};

_tabR.cntx_psh();
{!
|? _dt:={? _tabR.ROK || date(_tabR.ROK,1,1) || date(0,0,0) ?};
   KST_PAR.URL_LIM:=KST.LIM_NSP:=KST.LIM_NAUK:=0;
   exec('czytaj','#stalesys',_dt,KST,'LIM_NSP','LIM_NAUK');
   exec('czytaj','#stalesys',_dt,KST_PAR,'URL_LIM');
   {? _tabR.URL_LIM<>KST_PAR.URL_LIM | _tabR.LIM_NSP<>KST.LIM_NSP | _tabR.LIM_NAUK<>KST.LIM_NAUK || _sync(_tabR,_dt) ?};
   _tabR.prev()
!};
_tabR.cntx_pop();
~~


\tabr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu rekordu tabeli tymczasowej tabR (nawigator lat).
::   WE: _a - Specyfikacja testu
::            0 - Dołącz;
::            1 - Popraw;
::            2 - Kopiuj.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=_a;
_tabR:=cur_tab(1,1);
_chk:=__CHK.record(_tabR,,'ROK');
{? _chk='' & (_tabR.ROK<=0 | _tabR.ROK>9999)
|| FUN.emsg('Błędny rok kalendarzowy.'@);
   'ROK'

|? _chk='' & (_tabR.URL_LIM<0 | _tabR.URL_LIM>=365)
|| FUN.emsg('Błędny wymiar urlopu na żądanie.'@);
   'URL_LIM'

|? _chk='' & (_tabR.LIM_NSP<0 | _tabR.LIM_NSP>=365)
|| FUN.emsg('Błędny wymiar urlopu dodatkowego.'@);
   'LIM_NSP'

|? _chk='' & (_tabR.LIM_NAUK<0 | _tabR.LIM_NAUK>=365)
|| FUN.emsg('Błędny wymiar urlopu wypoczynkowego pracowników naukowych.'@);
   'LIM_NAUK'

|? _chk='' & _tryb<2
:  Dołącz, Popraw.
|| _ref:={? _tryb=1 || _tabR.ref() || null() ?};
   _tabR.cntx_psh();
   _tabR.prefix(_tabR.ROK);
   {? _tabR.first() & _tabR.ref()<>_ref
   || __CHK.err_ndx(_tabR,_tabR.index('?'),_tryb=1);
      _chk:='ROK'
   ?};
   _tabR.cntx_pop();
   _chk

|? _chk='' & _tryb=2
:  Kopiuj.
|| KST_URL.cntx_psh();
   KST_URL.index('KST_URL');
   KST_URL.prefix(_tabR.ROK);
   _jest:=KST_URL.first();
   KST_URL.cntx_pop();
   {? _jest
   || __CHK.err_ndx(_tabR,_tabR.index('?'),_tryb=1);
      _chk:='ROK'
   ?};
   _chk
?}


\tabr_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Dołącz dla tabeli tymczasowej tabR (nawigator lat).
::       Formuła oprócz rekordu w tabeli tymczasowej tworzy również zapis w stałych systemu, gdzie przechowywane są:
::        - wymiar urlopu udzielanego na żądanie pracownika (w ramach urlopu wypoczynkowego);
::        - wymiar urlopu dodatkowego dla pracownika ze znacznym lub umiarkowanym stopniem niepełnosprawności;
::        - wymiar urlopu wypoczynkowego pracowników naukowych i badawczo-technicznych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
_tabR.blank();
{? _tabR.edit("exec('tabr_ae','!zws_par_pwur',0)")
|| do();
   _tabR.add();
   exec('kst_sync','!zws_par_pwur',_tabR,'t2k');
   {? end()
   || exec('kart_url_first','!zws_par_pwur',_tabR.ROK)
   || FUN.emsg('Dołączenie nowego zapisu nie powiodło się.'@)
   ?}
?}


\tabr_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Popraw dla tabeli tymczasowej tabR (nawigator lat).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
exec('kart_url_first','!zws_par_pwur',_tabR.ROK);
{? _tabR.edit("exec('tabr_ae','!zws_par_pwur',1)")
|| do();
   _tabR.put();
   exec('kst_sync','!zws_par_pwur',_tabR,'t2k');
   {? ~end()
   || FUN.emsg('Poprawienie bieżącego zapisu nie powiodło się.'@)
   ?}
?}


\tabr_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Usuń dla tabeli tymczasowej tabR (nawigator lat).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
exec('kart_url_first','!zws_par_pwur',_tabR.ROK);
{? exec('del_conf','#table')
|| _dt:={? _tabR.ROK || date(_tabR.ROK,1,1) || date(0,0,0) ?};
   do();
:  Urlop na żądanie
   {? exec('usun','#stalesys',_dt,0,KST_PAR,'URL_LIM')<>'' || undo() ?};
:  Urlop dodatkowy i Urlop wypoczynkowy pracowników naukowych
   {? exec('usun','#stalesys',_dt,0,KST,'LIM_NSP','LIM_NAUK')<>'' || undo() ?};
:  Urlop wypoczynkowy
   KST_URL.cntx_psh();
   KST_URL.index('KST_URL');
   KST_URL.prefix(_tabR.ROK);
   {? KST_URL.first() || {! |? KST_URL.del() !} ?};
   KST_URL.cntx_pop();
:  Rok
   _tabR.del();
   exec('kst_sync','!zws_par_pwur',_tabR,'t2k');
   {? ~end()
   || FUN.emsg('Usunięcie bieżącego zapisu nie powiodło się.'@)
   ?}
?}


\tabr_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Kopiuj dla tabeli tymczasowej tabR (nawigator lat). Kopiuje zapisy bieżącego roku do wskazanego.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabR:=cur_tab(1,1);
{? KST_URL.size() |
   FUN.ask('Brak wymiarów urlopów na rok %1.'@ [$_tabR.ROK]+'.\n'+'Na pewno chcesz kontynuować kopiowanie?'@)
|| _par:=params_get();
   _src:=obj_new('ROK','URL_LIM','LIM_NSP','LIM_NAUK');
   _src.ROK:=_tabR.ROK;
   _src.URL_LIM:=_tabR.URL_LIM;
   _src.LIM_NSP:=_tabR.LIM_NSP;
   _src.LIM_NAUK:=_tabR.LIM_NAUK;
   _tabR.win_edit(_par.weR2);
   _tabR.blank();
   {? _tabR.edit("exec('tabr_ae','!zws_par_pwur',2)")
   || _dest:=_tabR.ROK;
      do();
      KST_URL.cntx_psh();
      KST_URL.prefix(_src.ROK);
      {? KST_URL.first()
      || {!
         |? KST_URL.cntx_psh();
            KST_URL.prefix();
            KST_URL.ROK:=_dest;
            KST_URL.add();
            KST_URL.cntx_pop();
            KST_URL.next()
         !}
      ?};
      KST_URL.cntx_pop();
      {? _tabR.find_key(_dest)
      || _new:=0
      || _tabR.blank();
         _tabR.ROK:=_dest;
         _new:=1
      ?};
      _tabR.URL_LIM:=_src.URL_LIM;
      _tabR.LIM_NSP:=_src.LIM_NSP;
      _tabR.LIM_NAUK:=_src.LIM_NAUK;
      {? _new || _tabR.add() || _tabR.put() ?};
      exec('kst_sync','!zws_par_pwur',_tabR,'t2k');
      {? ~end()
      || FUN.emsg('Kopiowanie wymiarów nie powiodło się.'@)
      ?}
   ?};
   _tabR.win_edit(_par.weR1)
?}


\tabr_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po odświeżeniu okienka tabeli tymczasowej _tabR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab(1,1);
{? _tab.get()
|| KST_URL.fld_fml('ROK','BLANK',$$_tab.ROK);
   KST_URL.prefix(_tab.ROK);
   _hid:=''
|| KST_URL.prefix(-1);
   _hid:=':d'
?};
KST_URL.actions('WER',_hid,,1);
grp_disp(KST_URL,'WER')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 2f6bcd7c875f9e89f5b538220fabe530278470881b1c3142b679bf1b8119367adc78bc3edf5c0c55ab122d72c18c8bb4c109e5c2a8f8fb8e8387b04bad6cfa20d73dc98701a6b35f5a70b101104a069659874762ce1facfba89495125a38113b1763b9919672c69e4e9d6c04f1eda5bbbace012254a9671cb3b609ed32616350

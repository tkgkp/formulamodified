:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dlmg.fml
:: Utworzony: 21.04.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DFZO - Dekretacja faktur w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja faktur w obiegu - główna formuła czynności FKS_DKS_DFZO.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
::# parses=exec('parses_edokum','obiegi')
::# access=exec('uprawnienia','obiegi',0)
::# properties=TODOTRIG,~RUNMICRO
::
:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=T, keyref=T
::# kind=WE, symbol=B_PREL, type=STRING, name=Poprzedni element w procesie, required=N
::# kind=WE, symbol=B_PRELS, type=STRING, name=Symbol poprzedniego elementu w procesie, required=N
::# kind=WE, symbol=DEKR, type=STRING, name=Dekretacja, required=N

:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=N
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

{? ~exec('is_act','#b__box','FKS_DKS_DFZO')
|| FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu faktur.'@);
   _mp.cancel(); return(~~)
|? _mp.isMicro() & _akcja='Odrzuć'
|| FUN.info('Dla faktury nie znaleziono aktywnego procesu z czynnością dekretacji.'@); return(~~)
?};

zak_dek:=0;
OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1; dolndok:=0;
{? (~_mp.isMicro() & _we.EDOKUM & EDOKUM.seek(_we.EDOKUM,,1)) |
   (_mp.isMicro() & EDOKUM.get())
|| params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
:: akceptacja z pulpitu
   {? _akcja='Dekretuj'
   || params_get().context.EDOKUM;
      _edokum:=EDOKUM.ref(); dok_ref:=null;
      {? var_pres('SD_OB')<=0 || SD_OB:=obj_new(@.CLASS.SD_CLASS,'F') ?};
      exec('dob_dek','obiegi_dekr');
      {? dok_ref & EDOKUM.seek(_edokum) & EDOKUM.STDEKRD
      || exec('ustaw_out','obiegi_dekr',_wy,'D',dok_ref,EDOKUM.ref());
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _akcja='Odrzuć'
   || params_get().context.EDOKUM;
      params_exec('odrzuc','obiegi_dekr');
      _edokum:=EDOKUM.ref();
      {? zak_dek=2 & EDOKUM.seek(_edokum)
      || exec('ustaw_out','obiegi_dekr',_wy,'O',null,EDOKUM.ref());
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _mp.pathTodo() | _mp.isAutoRun()
   || menu_txt(,'Popraw');
      {? var_pres('EDOKUM',_we)
      || _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
         {? _ref<>0 & _nam<>''
         || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
            {? EDOKUM.seek(_ref,_nam)
            || exec('init','obg'); zakres:=3;
               {? var_pres('SD_OB')<=0 || SD_OB:=obj_new(@.CLASS.SD_CLASS,'F') ?};
               exec('rkprz','obiegi');
               _edokum:=EDOKUM.ref(); dok_ref:=null;
               params_exec('przekaz_todo','obiegi_akc','D');
               {? zak_dek=1 & dok_ref & EDOKUM.seek(_edokum) & EDOKUM.STDEKRD
               || exec('ustaw_out','obiegi_dekr',_wy,'D',dok_ref,EDOKUM.ref());
                  _mp.save(,_wy);
                  _mp.done()
               |? zak_dek=2 & EDOKUM.seek(_edokum)
               || exec('ustaw_out','obiegi_dekr',_wy,'O',null,EDOKUM.ref());
                  _mp.save(,_wy);
                  _mp.done()
               ?}
            ?};
            EDOKUM.cntx_pop()
         ?}
      ?}
   ?}
?};
VAR_DEL.delete('dolndok','dok_ref','zawin','zak_dek');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zadekretuj fakturę w obiegu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat')) & ~EDOKUM.STDEKRD;
{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || params_exec('ustaw_bprel','obiegi');
         _desc:='Zadekretuj fakturę w obiegu: %1, data sprzedaży %2, otrzymano %3'@@[EDOKUM.SYM,$EDOKUM.DOP,$EDOKUM.DO];
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc


\fak_ob_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_DKS_DFZO';
_params.AKCJA:='Dekretuj';
_params.UIDREF:=EDOKUM.uidref();
_params.CONTEXT:=obj_new('EDOKUM');
_params.CONTEXT.EDOKUM:=EDOKUM.ref();
exec('mp_run','#b__box',_params);
1


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności FKS_DKS_DFZO
::  WE: _a - 'add po', 'put przed', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='add_po' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='add_po'
         || 3
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'FKS_DKS_DFZO')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Podglad faktury w obiegu z listy zadań
::----------------------------------------------------------------------------------------------------------------------
_webterm:=+app_info('web_sesid');

_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
_rfd:=null();
{? _wy.EDOKUM <> ~~
|| _rfd:=_wy.EDOKUM
|| _in:=_mp.load(exec('kind_in','#b_port'));
   _rfd:=_in.EDOKUM
?};
{? _webterm
|| EDOKUM.use(8+$_rfd);
   EDOKUM.seek(_rfd);
   EDOKUM.web_cntx_save();
   EDOKUM.web_display('WF03',,"EDOKUM.web_cntx_load(); EDOKUM.web_eclose(); web_top_refresh(1)")
|| exec('dok_spac','obg',_rfd)
?}


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'skid_v'>0
         || _edokum:=exec('FindAndGet','#table',EDOKUM,_kref,,,null());
            {? _edokum=null()
            || _mp.error()
            || EDOKUM.cntx_psh();
               {? EDOKUM.seek(_edokum,ref_name(_edokum),1)
               || {? EDOKUM.STDEKRD & EDOKUM.DOK_KS<>''
                  || _dok:=exec('FindAndGet','#table',DOK,EDOKUM.DOK_KS,,,null());
                     {? _dok<>null()
                     || DOK.cntx_psh();
                        {? DOK.seek(_dok,ref_name(_dok),1) & DOK.EDOKUM=$_edokum
                        || _wy.DOK:=DOK.ref;
                           _mp.save(,_wy);
                           _mp.done()
                        ?};
                        DOK.cntx_pop()
                     ?}
                  ?}
               ?};
               EDOKUM.cntx_pop()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 ba448b1484019cd5ad2980c32689f88cffee0c5bc1cec70b93ca9b35bd7484be1dafda7b6589977106d0be5103c9a97c1f4fb8733cd6fe89bf8d3d57639b7dcab2c22abbae5390512c2156067cab1519b42d07d60b21c663a4c802ed49cbd56fea9974bcbb7531acc0def4dcefc59ec4b5bfd5245e8c1484ee79668734a9c394

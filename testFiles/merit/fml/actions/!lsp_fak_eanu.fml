:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_fak_eanu.fml
:: Utworzony: 24.07.2017
:: Autor: Mario
::======================================================================================================================
:: Zawartość: Obsługa anulowania dokumentu sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP
::# properties=SERVICE
::# parses=exec('parses','!lsp_fak_eanu')
::# kind=WE,   symbol=FAKS,      type=_FAKS,    name=Dokument sprzedaży,   required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

exec('init_fak','lsp');

_akcja:=_mp.akcja();
_auto:=_akcja<>'Anuluj' & _akcja<>'Zrezygnuj' & (_mp.isAutoRun() | _mp.isService());

{? ~(var_pres('FAKS',_in)=type_of(null()) & _in.FAKS)
|| _mp.error('Brak wymaganego parametru FAKS.')
|| FAKS.cntx_psh();
   FAKS.prefix();
   {? ~FAKS.seek(_in.FAKS)
   || _mp.error('Nie znaleziono dokumentu.')
   || {? _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref())
      || params_set('mp',_mp);
         {? _akcja='Anuluj' | _auto
         || exec('anuluj','!lsp_fak_eanu',_auto)
         |? _akcja='Zrezygnuj'
         || exec('anuluj','!lsp_fak_eanu',-1)
         |? _mp.pathTodo()
            | _mp.pathArea() & _akcja=''
         || _win_red:=exec('faks_win_edit','faktury_nag');
            _ff:="params_exec('faks_pozycje_todo','!lsp_fak_eanu')";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
            {? FAKS.WHERE='K'
            || _ff:="params_exec('faks_kzf_red','!lsp_fak_eanu')";
               FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Korygowane dokumenty'@],_ff)
            ?};
            _ff:="params_exec('faks_anuluj_todo','!lsp_fak_eanu'); 'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Anuluj &dokument'@],_ff);
            _ff:="'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
            FAKS.win_edit(_win_red);
            exec('set_efld_opt','faktury_nag',_win_red);
::          Dalsza obsługa anulowania dokumentu w exec('anuluj','!lsp_fak_eanu')
            FAKS.display();
::          Usunięcie definicji tymczasowych okien
            FAKS.win_edit(''); FAKS.win_edel(_win_red)
         || _mp.error('Nieobsługiwana ścieżka.')
         ?};
         exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
      || exec('who_rlock_faks','faktury_nag')
      ?}
   ?};
   FAKS.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| _kor:=exec('FindAndGet','#table',FAKS,_in.FAKS,,"FAKS.T().KOR",'');
   {? _kor='T'
   || 'Anuluj korektę sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   |? _kor='Z'
   || 'Anuluj korektę zbiorczą: %1'@@[exec('record','#to_string',_in.FAKS)]
   || 'Anuluj dokument sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   ?}
|| ''
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| FAKS.cntx_psh();
   FAKS.use(ref_name(_in.FAKS));
   FAKS.prefix();
   {? FAKS.seek(_in.FAKS)
   || __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=FAKS.AR;
      _args.AM:=FAKS.AM;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FAKS.cntx_pop()
?};
1


\faks_anuluj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument sprzedaży - Anuluj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_FAK_EANU';
_params.UIDREF:=FAKS.uidref();
_params.AKCJA:='Anuluj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'FAKS',FAKS.ref());

exec('mp_run','#b__box',_params)


\faks_anuluj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument sprzedaży - Anuluj z todo
::   WE: params_get()   - ustawiane w exec('main','!lsp_fak_eanu')
::----------------------------------------------------------------------------------------------------------------------
params_exec('anuluj','!lsp_fak_eanu');
''


\faks_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument sprzedaży - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('pozycje_fak','faktury_poz',0);
''


\faks_kzf_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument sprzedaży - Korygowane pozycje z todo
::----------------------------------------------------------------------------------------------------------------------
exec('faks_kzf','faktury_nag');
''


\anuluj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Dokument sprzedaży - Anulowanie dokumentu
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie) -1-rezygnacja z anulacji
::       params_get()   - ustawiane w exec('main','!lsp_fak_eanu')
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_mp:=params_get().mp;

_result:=0;

{? ~exec('czy_z_ok','okresy',-2)
|| 1
|? FAKS.STAT_REJ='A'
|| {? _auto=-1
   || {? FUN.ask('Rezygnacja z anulowania dokumentu %1?\n'
                 'Uwaga. Zostanie ustawiony status zakończonej redakcji dokumentu.'@[FAKS.SYM])
      || exec('faks_anuluj','faktury_nag',0)
      ?}
   |? ~_auto
   || FUN.info('Dokument został anulowany.'@)
   ?};
   _result:=1;
   _mp.done()
|? FAKS.ZAK='T'
|| {? ~_auto || FUN.info('Dokument został zaksięgowany.'@) ?};
   _result:=1;
   _mp.done()
|? FAKS.STAT_REJ='N'
|| FUN.info('Nie zakończono jeszcze rejestracji dokumentu.\nAnulowanie niemożliwe.'@);
   _mp.error('Nie zakończono jeszcze rejestracji dokumentu. Anulowanie niemożliwe.');
   _result:=1
|? FAKS.KORYG='T'
|| {? ~_auto || FUN.info('Dokument został skorygowany.\nAnulowanie niemożliwe.'@) ?};
   _result:=1;
   _mp.done()
|? FAKS.WHERE='L' & FAKS.T().KOR='T'
|| {? ~_auto || FUN.info('Korekta zaliczki.\nAnulowanie niemożliwe.'@) ?};
   _result:=1;
   _mp.done()
|? exec('wydaFaks','faktury_wspolne')
|| {? ~_auto || FUN.info('Dokument powiązany z dokumentem magazynowym.\nAnulowanie niemożliwe.'@) ?};
   _result:=1;
   _mp.done()
|? exec('doc_in_ksef','faktury_nag1')
|| FUN.info('Dokument ma status KSeF: %1.\nAnulowanie niemożliwe.'@[FAKS.STATKSEF]);
   _result:=1;
   _mp.done()
|? FAKS.EDI_W='T' & FAKS.EDOKUMEN='N'
|| FUN.info('Utworzono dokument EDI z dokumentu.\n'
      'Należy anulować powiązane zdarzenie w dzienniku EDI przed anulowaniem dokumentu.'@);
   _result:=1;
   _mp.done()
|? FAKS.INTRA='T'
|| FUN.info('Dokument użyty w deklaracji Intrastat.\n'
      'Należy usunąć dokument z deklaracji przed anulowaniem dokumentu.'@);
   _result:=1;
   _mp.done()
|? FAKS.T().VALACC<>null() & ~exec('validate','wzorce',FAKS.T().VALACC,FAKS,FAKS.ref())
|| _result:=0
|? _auto
 | FUN.ask('Anulować dokument %1?\nOperację można odwrócić po wybraniu akcji "Zrezygnuj z anulowania".'@[FAKS.SYM])
||
  {? exec('faks_anuluj','faktury_nag')
  ||
     _result:=1;
     _mp.done()
  ||
     _result:=0
  ?}
?};
_result


\faks_noanul
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Dokument sprzedaży - Rezygnacja z Anulacji
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_FAK_EANU';
_params.UIDREF:=FAKS.uidref();
_params.AKCJA:='Zrezygnuj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'FAKS',FAKS.ref());

exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 c64fedabc792ffba27bdaedf490a431b127a664dc414e162695bd43c968a3e7a8547a5954fa49ca44a54a8368f1a4cbc0ec827f328af327173c44d00dd2d5d65b65237a3d084e27160b73462c97da2b77dfba0e5aad988202263b58c84be12d2b30f6ec2d55302bb7d43c7b83d45e6278329536620ee9ff18089a7ea7329730e

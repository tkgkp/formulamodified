:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zes_wkar.fml
:: Utworzony: 01.03.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności PPL_ZES_WKAR - Wydruk kartotek płacowych
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KOD [17.00]
:: OPIS: Wydruk listy płac - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

SEEK.O:=__PARSES.getVal('ListaPłac').REF;
SEEK.O();

P.cntx_psh();
{? SEEK.O<>null
|| FUNKCJE.OTWOLIST();
   exec('kzup_nazwy','kart_pla',KZ);
   exec('kzup_nazwy','kart_pla',KP);
   exec('kzup_nazwy','kart_pla',KU);
: niezbędne ze względu na wyliczenia składników
   exec('dekl','lista_plac');
   {? var_pres('LICZ')>100 || LICZ.Build() ?};
   P.clear();
   exec('rep_exec','#b_report','PPL_ZES_WKAR','ppl_p*',,1)
|| FUN.info('Nie można wykonać wydruków kartotek płacowych, bez wybranej listy płac w parametrach pracy.')
?};
P.cntx_pop()


\historia_splaty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.02]
:: OPIS: Wybór parametrów do wydruków:
::          - Historia spłaty kredytów (wybranych),
::          - Historia spłaty potrąceń (wybranych).
::   WE: _a - Tabela ze składnikami płacowymi do wydruku.
::       _b - Tytuł okna wertowania ze składnikami płacowymi.
::       _c - Dodatkowe warunki do wyświetlenia pracowników.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_SKL:=_a;
_tytul:=_b;
_mode:='maximized';
_two:='target=window';
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PPL';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_args.F_ZATR:='P';
_args.VIEW:=__PARSES.getVal('ZakresDanych');
_args.SQL_WHERE:=_c;

:: Okno składników płacowych.
_wskl:=_SKL.mk_sel(_tytul,,0,'skladniki',,,,,'U',,,,,_mode);
_SKL.win_fld(_wskl,,'RN',,,5,,1,'Kod'@,,'Kod składnika płacowego'@);
_SKL.win_fld(_wskl,,'RT',,,20,,1,'Nazwa'@,,'Opis składnika płacowego'@);
::_SKL.win_fld(_wskl,,'WB',,,3,,1,'Wybrany?'@,,'Składnik wybrany do zestawienia (T/N)'@,2,,"'T'","'N'");

_SKL.win_act(_wskl,0,'Formuła','Wybierz'@@,,'Wybór składnika'@,
   "  _SKL:=cur_tab(1,1);
      _SKL.WB:='T';
      _SKL.put();
      _SKL.cntx_psh();
      _SKL.first();
      _skl:='';
      {!
      |? {? _SKL.WB='T'
         || _skl+=$_SKL.RN+','
         ?};
         _SKL.next()
      !};
      _SKL.cntx_pop();
      params_set(params_get());
      _args:=params_get().args;
      exec('filtruj_p','schemat',_args.DOMAIN,UD_DEF.ref(),_args.F_ZATR,,_args.VIEW,,_args.SQL_WHERE(_skl-1));
      P.first();
      grp_disp(P,params_get().werp)
   ",,1,1,,,'W'
);
_SKL.win_act(_wskl,0,'Formuła','Wybierz wsz&ystkie'@@,,'Wybór wszystkich składników'@,
   "  _SKL:=cur_tab(1,1);
      _SKL.cntx_psh();
      _SKL.first();
      _skl:='';
      {!
      |? _SKL.WB:='T';
         _SKL.put();
         _skl+=$_SKL.RN+',';
         _SKL.next()
      !};
      _SKL.cntx_pop();
      params_set(params_get());
      _args:=params_get().args;
      exec('filtruj_p','schemat',_args.DOMAIN,UD_DEF.ref(),_args.F_ZATR,,_args.VIEW,,_args.SQL_WHERE(_skl-1));
      P.first();
      grp_disp(P,params_get().werp)
   ",,,,,,'Y',,_two
);
_SKL.win_act(_wskl,0,'Formuła','Pomiń'@@,,'Pominięcie składnika'@,
   "  _SKL:=cur_tab(1,1);
      _SKL.WB:='N';
      _SKL.put();
      _SKL.cntx_psh();
      _SKL.first();
      _skl:='';
      {!
      |? {? _SKL.WB='T'
         || _skl+=$_SKL.RN+','
         ?};
         _SKL.next()
      !};
      {? _skl=''
      || _skl:='0,'
      ?};
      _SKL.cntx_pop();
      params_set(params_get());
      _args:=params_get().args;
      exec('filtruj_p','schemat',_args.DOMAIN,UD_DEF.ref(),_args.F_ZATR,,_args.VIEW,,_args.SQL_WHERE(_skl-1));
      P.first();
      grp_disp(P,params_get().werp)
   ",,,1,,,'P'
);
_SKL.win_act(_wskl,0,'Rekord',,,,
   "  _SKL:=cur_tab(1,1);
      _wskl:=cur_win(1,1);
      _SKL.cntx_psh();
      {? _SKL.sel_size()
      || _gray:=''
      || _gray:={? _SKL.WB='T' || 'W' || 'P' ?};
         _SKL.first();
         {!
         |? {? _SKL.WB='N'
            || 0
            || {? _SKL.next()
               || 1
               || _gray+='Y'; 0
               ?}
            ?}
         !}
      ?};
      _SKL.cntx_pop();
      _kolor:={? _SKL.WB='T' || Color.fnd_kol('GKOMZ#01#01') || 0 ?};
      _SKL.actions_grayed(_wskl,_gray);
      _kolor
   "
);
_SKL.win_act(_wskl,,'Formuła','Legenda'@@,,'Wyświetlenie legendy'@,"exec('legenda','color','GKOMZ#01#01')",,,,,,
   'L',,'target=window'
);

_SKL.win_btn(_wskl,'text=%1,align=begin'['Wybierz'@],'menu:W');
_SKL.win_btn(_wskl,'text=%1,align=begin'['Pomiń'@],'menu:P');
_SKL.win_btn(_wskl,'text=%1,panel=bottom,align=begin'['Wybierz wsz&ystkie'@],'menu:Y');

:: Okno z pracownikami.
_werp:=P.mk_sel('Pracownicy',,0,'pracownicy',,,,,'U',,,,,_mode);
P.win_fld(_werp,,'T',,,-10,,1,'Identyfikator'@);
P.win_fld(_werp,,'OSOBA','NAZWISKO',,20,,1,'Nazwisko'@);
P.win_fld(_werp,,'OSOBA','PIERWSZE',,20,,1,'Imię'@);
P.win_fld(_werp,,'WYDZIAL','SYMBOL',,-20,,1,'Jednostka organizacyjna'@);
P.win_fld(_werp,,'ZA',,,3,,1,'Zatrudniony'@,,,2,,"'T'","'N'");

P.win_act(_werp,0,'Formuła','Wybierz'@@,,'Wybór pracownika'@,
   "  _P:=params_get().P;
      {? ~_P.find_key($P.ref())
      || _P.REF:=$P.ref();
         _P.LP:=_P.size()+1;
         _P.add()
      ?}
   ",,1,1,,,'W'
);
P.win_act(_werp,0,'Formuła','Wybierz wsz&ystkich'@@,,'Wybór wszystkich pracowników'@,
   "  _P:=params_get().P;
      _prac:=P.ref();
      P.f_first();
      {!
      |? {? ~_P.find_key($P.ref())
         || _P.REF:=$P.ref();
         _P.LP:=_P.size()+1;
            _P.add()
         ?};
         P.f_next()
      !};
      P.f_seek(_prac,)
   ",,,,,,'Y',,_two
);
P.win_act(_werp,0,'Formuła','Pomiń'@@,,'Pominięcie wybranego pracownika'@,
   "  _P:=params_get().P;
      {? _P.find_key($P.ref())
      || _P.REF:=$P.ref();
         _P.del()
      ?}
   ",,,1,,,'P');
P.win_act(_werp,0,'Formuła','Drukuj'@@,,'Kontynuacja wydruku'@,
   "  _P:=params_get().P;
      _P.first();
      {!
      |? {? P.f_seek(_P.REF,)
         || _P.next()
         || _P.del()
         ?}
      !};
      sel_exit()
   ",,,1,,,'D',,'icon=print,'+_two
);
P.win_act(_werp,0,'Formuła','Anuluj'@@,,'Rezygnacja z wydruku'@,
   "  _P:=params_get().P;
      _P.erase();
      sel_exit()
   ",,,1,,,'A',,_two
);
P.win_act(_werp,0,'Rekord',,,,
   "  _P:=params_get().P;
      _gray:={? ~_P.size() || 'D' || '' ?};
      {? ~_P.find_key($P.ref())
      || _gray+={? P.sel_size() || '' || 'P' ?};
         _kolor:=0
      || _gray+={? P.sel_size() || '' || 'W' ?};
         _kolor:=Color.fnd_kol('GKOMZ#01#01')
      ?};
      P.actions_grayed(params_get().werp,_gray);
      _kolor
   "
);
P.win_act(_werp,,'Formuła','Legenda'@@,,'Wyświetlenie legendy'@,"exec('legenda','color','GKOMZ#01#01')",,,,,,
   'L',,'target=window'
);
P.win_act(_werp,,'Kolejność');

P.win_btn(_werp,'text=%1,align=begin'['Wybierz'@],'menu:W');
P.win_btn(_werp,'text=%1,align=begin'['Pomiń'@],'menu:P');
P.win_btn(_werp,'text=%1,panel=bottom,align=begin'['Wybierz wsz&ystkich'@],'menu:Y');
P.win_btn(_werp,'text=%1,panel=bottom,align=end'['Drukuj'@],'menu:D');
P.win_btn(_werp,'text=%1,panel=bottom,align=end'['Anuluj'@],'menu:A');

:: Tabela z wybranymi pracownikami.
_P:=tab_tmp(1,
   'REF','STRING[16]','Wskazanie wybranego pracownika',
   'LP','INTEGER','Kolejność'
);
params_set('args',_args,'werp',_werp,'P',_P);
:: Okno wyboru do wydruku.
_wgrp:=_SKL.grp_make(_tytul,
   "  params_set(params_get());
      _args:=params_get().args;
      exec('filtruj_p','schemat',_args.DOMAIN,UD_DEF.ref(),_args.F_ZATR,,_args.VIEW,,_args.SQL_WHERE('0'));
      grp_disp(P,params_get().werp)
   ",'grpskladniki',,,,,_mode);
_SKL.win_sel(_wgrp);
_SKL.grp_sel(_wgrp,_SKL,_wskl,,,,,,,,,,_mode);
_SKL.grp_splt(_wgrp,,'vertical','pracownicy',,'#');
_SKL.grp_sel(_wgrp,P,_werp,,,,,,,,,,_mode);

{? ~_SKL.select()
|| _P.erase()
?};
_P

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 d44291c957a905f6069badd14eaf7f066118f574954b8b00a82454e8e2c1b91eea4600628a5efefec5541f0961aa3b57dc9837e490dae1a8f2d9d1fa7237bffaa187ba4b41268ae9a02b5f6ba17b10b435c560cac4c5e01169127f7b2fd149c02d44b83c38d8a59fc16c7fc1a332f04edfcf76f1a0cf5ad50b2dba11e3e18371

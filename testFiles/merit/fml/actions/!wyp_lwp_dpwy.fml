:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !wyp_lwp_dpwy.fml
:: Utworzony: 04.09.2017
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności WYP_LWP_DPWY - Rejestracja polecenia wydania wyposażenia
::            (obsługa tabel POLWO i PWOPOZ)
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Formuła główna czynności rejestracji polecenia wydania wyposażenia (WYP_LWP_DPWY)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

::# permissions=ODDZ,F_ZATR,UD_SKL
::# properties=SERVICE

::# kind=WE, symbol=POLWO, type=_POLWO, name=Polecenie wydania wyposażenia, required=N, keyref=T
{? var_pres('POLWO',_in)<>type_of(~~) & var_pres('POLWO',_in)<>type_of(null())
|| _msg:='Błędny parametr wejściowy \'%1\' dla czynności %2.'@@['KARO','WYP_LWP_DPWY'];
   FUN.error(_msg);
   _mp.error(_msg);
   return()
?};
{? var_pres('POLWO',_in)=type_of(~~) || _in.POLWO:=null() ?};
::# kind=WY, symbol=POLWO,    type=_POLWO,   name=Polecenie wydania wyposażenia, required=N
::# kind=WY, symbol=RESULT,   type=STRING,   name=Wynik działania,               required=N

_mp.trigRef('POLWO');

_mp.save(exec('kind_out','#b_port'),'RESULT','OK');

:: ustalenie bieżącego rekordu POLWO
_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)=type_of('')
|| POLWO.cntx_psh();
   POLWO.clear();
   {? POLWO.seek(_keyRefs[1])
   || _polwo:=POLWO.ref()
   || _polwo:=null()
   ?};
   POLWO.cntx_pop();
   {? _polwo=null()
   ||
::    Nie znaleziono rekordu kluczowego powiązanego z POLWO, więc robię error
      exec('polwo_deleted_in_proc','wyp_polwo',_mp);
      return()
   ?}
|? _in.POLWO<>null()
|| _polwo:=_in.POLWO
|| _polwo:=null()
?};

:: Uruchomienie automatyczne lub serwisowe
{? _polwo=null() & _mp.isService()
|| _msg:=
      'Brak wymaganego parametru wejściowego dla uruchomienia jako usługa: %1 - %2'@
      ['POLWO','Polecenie wydania wyposażenia'@];
   _mp.save(exec('kind_out','#b_port'),'RESULT','BŁĄD');
   _mp.error(_msg)
|? _polwo & (_mp.isAutoRun() | _mp.isService())
|| {? exec('blk_lock','#table','POLWO',_polwo,,,{? _mp.isGroup() || '' || 'Polecenie wydania jest redagowane'@ ?})
   ||
      {? exec('czy_poz','wyp_polwo',_polwo)
      || {? exec('FindAndGet','#table',POLWO,_polwo,,"STAT_REJ:='T'; put()",0)
         || _mp.save(exec('kind_out','#b_port'),'POLWO',_polwo);
            _mp.done()
         || _mp.save(exec('kind_out','#b_port'),'RESULT','BŁĄD')
         ?}
      ||
         _msg:='Wskazane polecenie wydania nie zawiera pozycji.'@;
         {? _mp.isGroup()
         || KOMM.add(_msg,2,,1)
         || FUN.emsg(_msg)
         ?};
         _mp.save(exec('kind_out','#b_port'),'RESULT','BŁĄD');
         {? _mp.isService()
         || _mp.done()
         ?}
      ?};
      exec('blk_unlock','#table','POLWO',_polwo)
   ||
::    Nie udało się zablokować
      _mp.save(exec('kind_out','#b_port'),'RESULT','BŁĄD');
      {? _mp.isService()
      || _mp.done()
      ?}
   ?}

:: Nowy zapis ze startu procesu albo akcja DOŁĄCZ
|? _mp.pathProc() | _mp.akcja()='DOŁĄCZ' | (_polwo=null() & _mp.pathTodo())
|| KOMM.init(,,'Dołączenie polecenia wydania wyposażenia'@);
   _polwo:=exec('polwo_dolacz','!wyp_lwp_dpwy');
   {? _polwo<>null()
   || {? exec('FindAndGet','#table',POLWO,_polwo,,"STAT_REJ='T'",0)
      || _mp.save(exec('kind_out','#b_port'),'POLWO',_polwo);
         _mp.done()
      ?}
   || _mp.cancel()
   ?}

:: Kontynuacja z poziomu listy zadań albo akcja POPRAW
|? _mp.pathTodo() | _mp.akcja()='POPRAW'
||
   {? exec('polwo_popraw','!wyp_lwp_dpwy',_polwo)
   || {? exec('FindAndGet','#table',POLWO,_polwo,,"STAT_REJ='T'",0)
      || _mp.save(exec('kind_out','#b_port'),'POLWO',_polwo);
         _mp.done()
      ?}
   ?}

:: Akcja ZAKOŃCZ
|? _mp.akcja()='ZAKOŃCZ'
||
   {? exec('blk_lock','#table','POLWO',_polwo,,,{? _mp.isGroup() || '' || 'Polecenie wydania jest redagowane'@ ?})
   || {? _mp.isGroup() | FUN.ask('Czy zakończyć rejestrowanie polecenia wydania wyposażenia?'@)
      || {? exec('FindAndGet','#table',POLWO,_polwo,,"STAT_REJ:='T'; put()",0)
         || _mp.save(exec('kind_out','#b_port'),'POLWO',_polwo);
            _mp.done()
         ?}
      ?}
   ?};
   exec('blk_unlock','#table','POLWO',_polwo)

?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Opis dla czynności rejestracji polecenia wydania wyposażenia (WYP_LWP_DPWY)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

:: Jest rekord kluczowy to ustawiam odpowiednie POLWO
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',POLWO,_keyRefs[1],,"exec('record','#to_string',ref())",'');
   _desc:={? _tmp<>'' || 'Zakończ rejestrację polecenia wydania wyposażenia: %1'@@[_tmp] || '' ?}
::|| _desc:=exec('FindAndGet','#table',POLWO,_keyRefs[1],,
::      "'Zakończ rejestrację polecenia wydania wyposażenia: %1'[exec('record','#to_string',ref())]",''
::   )

:: nie ma nic...
|| _desc:='Zarejestruj nowe polecenie wydania wyposażenia'@@
?};

_desc


\action_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Akcja Dołącz w oknie wertowania POLWO
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='WYP_LWP_DPWY';
_args.AKCJA:='DOŁĄCZ';
_args.PROC_START:='T';

exec('mp_run','#b__box',_args);
~~


\action_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Akcja Popraw w oknie wertowania POLWO
::----------------------------------------------------------------------------------------------------------------------
_result:=0;

{? exec('czy_akc','!wyp_lwp_dpwy')
|| FUN.info('Polecenie wydania wyposażenia jest zaakceptowane.\nZmiana danych niedozwolona.'@)
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='WYP_LWP_DPWY';
   _args.UIDREF:=POLWO.uidref();
   _args.AKCJA:='POPRAW';
   _args.PROC_START:='N';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'POLWO',POLWO.ref());

   exec('mp_run','#b__box',_args)
?};
_result


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Akcja Zakończ w oknie wertowania POLWO
::----------------------------------------------------------------------------------------------------------------------
_result:=0;

{? exec('czy_poz','wyp_polwo',POLWO.ref())
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='WYP_LWP_DPWY';
   _args.UIDREF:=POLWO.uidref();
   _args.AKCJA:='ZAKOŃCZ';
   _args.PROC_START:='N';
   _args.GRUPA:={? POLWO.sel_size()>0 || 'T' || 'N' ?};
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'POLWO',POLWO.ref());

   exec('mp_run','#b__box',_args)
|| {? POLWO.sel_size()>0
   || KOMM.add('Polecenie wydania %1 nie ma pozycji.'@[POLWO.SYM])
   || FUN.info('Polecenie wydania nie ma pozycji.'@)
   ?}
?};
_result


\action_end_group_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Akcja Zakończ w oknie wertowania POLWO (przed grupą rekordów)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy zakończyć rejestrację zaznaczonych poleceń wydania wyposażenia?'@)
|| KOMM.init(,,'Zakończenie rejestracji poleceń wydania'@);
   1
|| 0
?}


\action_end_group_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Akcja Zakończ w oknie wertowania POLWO (po grupie rekordów)
::----------------------------------------------------------------------------------------------------------------------
KOMM.select();
~~


\env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Środowisko dla rejestracji polecenia wydania
::----------------------------------------------------------------------------------------------------------------------
_env:=obj_new('save');
_env.save:=0;
_env


\win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Definiuje okno redagowania polecenia wydania
::----------------------------------------------------------------------------------------------------------------------
_red:=POLWO.mk_edit('Polecenie wydania'@,,'#polwo_red');
POLWO.win_ewin(_red,,'RED');

POLWO.win_ebtn(_red,'text=%1,align=begin,display=0'['Poz&ycje'@],"params_exec('btn_pwopoz','!wyp_lwp_dpwy')");

exec('zakoncz','#window',POLWO,_red,1,"params_exec('btn_end','!wyp_lwp_dpwy')",0,exec('help_red_zakoncz','#window','L'),
   exec('text_red_zakoncz','#window','L'));
exec('ok_esc','#window',POLWO,_red,1,,"params_exec('btn_escape','!wyp_lwp_dpwy')",,,exec('help_red_ok','#window','Z'),
   exec('text_red_ok','#window'), exec('help_red_esc','#window','A'));

_red


\btn_pwopoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Selekcja pozycji polecenia wydania (z poziomu okna rejestrowania nagłówka polecenia wydania)
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;

_result:='';
_fld:=exec('polwo_ra','!wyp_lwp_dpwy');
{? _fld=''
|| {? POLWO.put()
   || PWOPOZ.index('PRODZOK');
      PWOPOZ.prefix(POLWO.ref());
      PWOPOZ.win_sel('WER');
      PWOPOZ.win_edit('RED');
      PWOPOZ.actions_grayed('WER','');
      exec('prep_ctril','wyp_polwo');
      PWOPOZ.select();
      exec('drop_ctril','wyp_polwo');
      {? exec('czy_poz','wyp_polwo') || _env.save:=1 ?}
   ?}
|| _result:='edit:'+_fld
?};
_result


\btn_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Zakończenie rejestrowania polecenia wydania (z poziomu okna rejestrowania nagłówka polecenia wydania)
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_fld:=exec('polwo_ra','!wyp_lwp_dpwy');
{? _fld=''
|| {? exec('czy_poz','wyp_polwo',POLWO.ref())
   || POLWO.STAT_REJ:='T';
      {? POLWO.put()
      || _result:='key:F2'
      ?}
   || FUN.info('Brak pozycji polecenia wydania wyposażenia.'@)
   ?}
|| _result:='edit:'+_fld
?};
_result


\btn_escape
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Rezygnacja z rejestrowania wydania (z poziomu okna rejestrowania nagłówka polecenia wydania)
::----------------------------------------------------------------------------------------------------------------------
'key:Esc'


::======================================================================================================================
:: POLWO - obsługa podstawowa
::======================================================================================================================


\polworef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Zwraca ref POLWO
::   WY: POLWO.ref()
::  OLD: \polworef/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
POLWO.ref()


\czy_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Czy biezace POLWO zaakceptowane
::   WY: 1/0 tak/nie
::  OLD: \czy_akc/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
POLWO.AKCEPT<>'N'


\pwosymbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Przed redakcja POLWO.SYM
::       W tej wersji niedostepna redakcja tego pola
::  OLD: \pwosymbe/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
0


\pwodw_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Przed redakcja POLWO.DATA_W
::  OLD: \pwodw_be/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
{? POLWO.DATA_W<POLWO.DATA || POLWO.DATA_W=POLWO.DATA ?};
~~


\pwodw_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Po redakcji POLWO.DATA_W
::   WY: 1/0 - czy mozna opuscic
::  OLD: \pwodw_ae/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
{? POLWO.DATA_W>=POLWO.DATA
|| 1
|| FUN.emsg('Data wydania nie może być wczesniejsza od daty wystawienia polecenia.'@);
   0
?}


\polwo_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Dolacz POLWO niestandardowa
::   WY: POLWO.ref() / null()
::  OLD: \polwo_dolacz/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=null();

_env:=exec('env','!wyp_lwp_dpwy');
params_set('env',_env);

_numeracja:=exec('get','#params',700502);
{? _numeracja=''
|| FUN.error('Brak zdefiniowanej grupy numeracji dla poleceń w parametrach programu.'@)
||
   exec('znad_blank','!wyp_lwp_dpwy');
   POM.TYPDOK:=_numeracja;
   POM.TAB:='POLWO';
   exec('add_grnr','numery','POL');
   POLWO.SYM:='~'+$POLWO.tm_stamp();
   POLWO.NR:=0;
   POLWO.blank();
   {? POLWO.add()
   || _polwo_ref:=POLWO.ref();
      {? exec('blk_lock','#table','POLWO',_polwo_ref)
      || POLWO.win_edit(exec('win_edit','!wyp_lwp_dpwy'));
         {? POLWO.edit("exec('polwo_ra','!wyp_lwp_dpwy')")
         || {? POLWO.put() || _result:=POLWO.ref() ?}
         ||
            _czy_poz:=exec('czy_poz','wyp_polwo',POLWO.ref());
            {? _czy_poz
            || {? FUN.ask('Polecenie wydania ma pozycje.\nCzy na pewno usunąć polecenie?'@)
               || _env.save:=0
               ?}
            || _env.save:=0
            ?};

            {? _env.save
            || _result:=POLWO.ref()
            ||
::             >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
               do();
               PWOPOZ.cntx_psh(); PWOPOZ.index('PRODZO'); PWOPOZ.prefix(POLWO.ref());
               {? PWOPOZ.first() || {! |? PWOPOZ.del() !} ?};
               PWOPOZ.cntx_pop();
               POM.TAB:='POLWO';
               POM.TYPDOK:=exec('get','#params',700502);
               oldnumer:=1;
               numer:=POLWO.NR;
               exec('nr_old','numery');
               POLWO.del();
               end()
::             <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
            ?}
         ?}
      ?};
      exec('blk_unlock','#table','POLWO',_polwo_ref)
   ?}
?};
_result


\polwo_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Popraw POLWO
::   WE: _a - POLWO.ref()
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_polwo:=_a;

_env:=exec('env','!wyp_lwp_dpwy');
params_set('env',_env);

_result:=0;
POLWO.cntx_psh();
POLWO.prefix();
{? POLWO.seek(_polwo)
|| {? exec('blk_lock','#table','POLWO',POLWO.ref(),,,'Polecenie wydania jest redagowane'@)
   || POLWO.win_edit(exec('win_edit','!wyp_lwp_dpwy'));
      {? POLWO.edit("exec('polwo_ra','!wyp_lwp_dpwy')")
      || _result:=POLWO.put()
      ?}
   ?};
   exec('blk_unlock','#table','POLWO',POLWO.ref())
?};
POLWO.cntx_pop();
_result


\znad_load
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Zaladowanie danych niezbednych do pokazania w oknie do ZNADZOR
::       dla biezacego rekordu POLWO
::  OLD: \znad_load/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
ZNADZOR.P:=POLWO.P;
ZNADZOR.OSOBA:=POLWO.P().OSOBA;
{? ZNADZOR.P<>null()
||
   ZNADZOR.NAZWIM:=exec('osoba_npd','wyp',ZNADZOR.OSOBA);
   ZNADZOR.INFO:=exec('set_p_info','wyp',ZNADZOR.P);
   ~~
|| exec('znad_blank','!wyp_lwp_dpwy')
?};
~~


\polwo_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Rekord przed POLWO
::       ustawienie pol wyliczanych i kolor
::   WE: _a - parametr systemowy
::  OLD: \polwo_rb/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
exec('znad_load','!wyp_lwp_dpwy');
{? _a || exec('polwo_grayed','wyp_polwo') ?};
Color.rekprzed('POLWO#01#00')


\polwo_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Rekord po POLWO
::  OLD: \polwo_ra/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
_odp:=__CHK.record(POLWO,,'DATA','DATA_W');
{? _odp=''
||
   {? POLWO.P<>null()
   ||
      {? POLWO.DATA<=POLWO.DATA_W
      ||
         {? POLWO.P(); exec('test_zatr','phr_dane',POLWO.DATA_W)
         || ''
         || FUN.emsg('Data wydania nie może być póżniejsza od dnia zakończenia pracy pracownika.'@);
            _odp:='DATA_W'
         ?}
      || FUN.emsg('Data wydania nie może być wcześniejsza od daty wydania polecenia.'@);
         _odp:='DATA_W'
      ?}
   || FUN.emsg('Należy wskazać pracownika.'@);
      _odp:='NAZWISKO'
   ?}
?};
_odp


\znad_blank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Usuniecie danych pokazywanych w oknie w ZNADZOR
::  OLD: \znad_blank/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
ZNADZOR.P:=null();
ZNADZOR.OSOBA:=null();
ZNADZOR.NAZWIM:='';
ZNADZOR.INFO:='';
~~


\polwo_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Usun POLWO
::  OLD: \polwo_usun/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
_odp:=0;
_selected:=(POLWO.sel_size()>0);

{? _selected | FUN.ask('Usunąć polecenie wydania wyposażenia?'@)
||
   {? exec('czy_akc','!wyp_lwp_dpwy')
   || {? _selected
      || KOMM.add('Polecenie %1 jest zaakceptowane. Usunięcie zabronione.'@[POLWO.SYM])
      || FUN.info('Nie można usunąć zaakceptowanego polecenia.'@)
      ?}

   |? POLWO.STAT_REJ='T'
   || {? _selected
      || KOMM.add('Zakończono rejestrację polecenia %1. Usunięcie zabronione.'@[POLWO.SYM])
      || FUN.info('Nie można usunąć polecenia - rejestracja została zakończona.'@)
      ?}

   || _polwo:=POLWO.ref();
      {? exec('blk_lock','#table','POLWO',_polwo)
      ||
::       >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
         do();
         PWOPOZ.cntx_psh(); PWOPOZ.index('PRODZO'); PWOPOZ.prefix(POLWO.ref());
         {? PWOPOZ.first() || {! |? PWOPOZ.del() !} ?};
         PWOPOZ.cntx_pop();
         POM.TAB:='POLWO';
         POM.TYPDOK:=exec('get','#params',700502);
         numer:=POLWO.NR;
         oldnumer:=1;
         exec('nr_old','numery');
         _odp:=POLWO.del();
         end();
::       <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
::         {? POLWO.sel_size() || KOMM.add('Polecenie %1 usunięto.'@[POLWO.SYM],117) ?};
         ~~
      ||
         {? _selected
         || KOMM.add('Polecenie %1 jest zablokowane przez innego użytkownika. Usunięcie niemożliwe.'@[POLWO.SYM])
         || FUN.info('Nie można usunąć z powodu blokowania przez innego użytkownika.'@)
         ?}
      ?};
      exec('blk_unlock','#table','POLWO',_polwo)
   ?}
?}


\polwo_ugb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Grupa Usun przed POLWO
::   WY: 1/0 - czy kontynuowac
::  OLD: \polwo_ugb/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone polecenia wydania wyposażenia?'@)
|| KOMM.init(,,'Usuwanie poleceń wydania wyposażenia'@);
   1
|| 0
?}


\polwo_uga
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Grupa Usun po POLWO
::  OLD: \polwo_uga/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
KOMM.select();
~~


\polwo_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Wydruk polecenia
::  OLD: \polwo_druk/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
rep_exec('wyp_polwodru');
~~


\polwo_af
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Po odświeżeniu POLWO
::  OLD: \polwo_af/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
PWOPOZ.prefix(POLWO.ref());
PWOPOZ.first();
grp_disp(PWOPOZ,'WER',1);
~~


::======================================================================================================================
:: PWOPOZ - obsługa podstawowa
::======================================================================================================================


\pwopoz_mgrp_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: przed redakcja pola PWOPOZ.MGRP
::       zapamietanie poprzedniej wartosci
::  OLD: \pwopgtbe/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
ZNADZOR.MGRP:=PWOPOZ.MGRP;
MGRP.actions('WER','dpuDPU:dD');
~~


\pwopoz_mgrp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: po redakcji pola PWOPOZ.MGRP
::       podpowiedz wartosci z normatywu
::   WY: 0 - towar niedozwolony (tylko gdy kontrola ilosci) 1 - towar dozwolony
::  OLD: \pwopgtae/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()
|| _ok:=1;
   {? (-menu_txt())='dołącz'
   || PWOPOZ.cntx_psh();
      PWOPOZ.index('PRODZO');
      PWOPOZ.prefix(POLWO.ref(),PWOPOZ.MGRP);
      {? PWOPOZ.first()
      || FUN.emsg('Wybrana grupa materiałowa jest już wybrana na innej pozycji tego polecenia wydania.'@);
         _ok:=0
      || _ok:=1
      ?};
      PWOPOZ.cntx_pop()
   ?};
:: ustawienie wartości domyslnych z RODZO w polach TOLER CZAS ZWROT
   _ff:="{? (-menu_txt())='dołącz'
         ||
            PWOPOZ.ZWROT:=PWOPOZ.MGRP().ZWROT;
            {? PWOPOZ.ZWROT<>'B'
            ||
               PWOPOZ.CZAS:=PWOPOZ.MGRP().DCU;
               PWOPOZ.TOLER:=int(PWOPOZ.CZAS*PWOPOZ.MGRP().TOLER/100)
           ?}
         ?}";
:: kontrola daty i dalsze czynnosci pod warunkiem poprawnosci
:: zabezpieczenie redundantne - data zawsze powinna byc
   {? _ok
   || {? #POLWO.DATA_W
      ||
         _ff();
         _norm:=exec('find_norm','normatyw',#PWOPOZ.MGRP);
         _ilp:=_ilo:=0;
         {? (_norm=1) | (_norm=3) || _ilp:=__WYNNOP.IL ?};
         {? _norm>1 || _ilo:=__WYNORO.IL ?};
::    dopuszczenie do dalszej edycji w zaleznosci od rodzaju kontroli
::    w przypadku wystepowania kontroli musi byc normatyw pracownika i wg niego utawienia
         _ok:=0;
         {? (PWOPOZ.MGRP().CTRLIL='N') | (exec('get','#params',700510,2,null())='N')
         || _ok:=1
         |? (PWOPOZ.MGRP().CTRLIL='P') & _ilp
         || _ok:=1
         |? (PWOPOZ.MGRP().CTRLIL='O') & _ilo & _ilp
         ||
::       jednoczesnie do _ilp zaladowanie mniejszej ilosci
            {? _ilp>_ilo || _ilp:=_ilo ?};
            _ok:=1
         ?};
::    jezeli mozna edytowac to
         {? _ok
         ||
            {? (ZNADZOR.MGRP<>PWOPOZ.MGRP) | ((-menu_txt())='dołącz') | (PWOPOZ.ZWROT<>__WYNNOP.ZWROT)
            ||
::          ustawienie wartosci domyslnych
               PWOPOZ.ZWROT:=__WYNNOP.ZWROT;
               {? PWOPOZ.ZWROT='B'
               || PWOPOZ.TOLER:=PWOPOZ.CZAS:=0
               || PWOPOZ.TOLER:=__WYNNOP.TOLER; PWOPOZ.CZAS:=__WYNNOP.CZAS
               ?};
               PWOPOZ.IL:=_ilp
            ?}
         ||
            _kom:={? (PWOPOZ.MGRP().CTRLIL<>'O') & (_ilp=0)
                  || 'Brak grupy materiałowej %1 w normatywie pracownika.'@[PWOPOZ.MGRP().KOD]
                  || ''
                  ?};
            _kom+={? (PWOPOZ.MGRP().CTRLIL='O') & (_ilo=0)
                  || 'Brak grupy materiałowej %1 w normatywie łącznym osoby.'@[PWOPOZ.MGRP().KOD]
                  || ''
                  ?};
            FUN.info(_kom)
         ?}
      ||
         _ok:={? (PWOPOZ.MGRP().CTRLIL='N') | (exec('get','#params',700510,2,null())='N')
              || FUN.emsg('Nieprawidłowa data wydania w poleceniu.'@);
                 0
              || _ff();1
              ?}
      ?}
   ?};
:: zapamietanie biezacej wartosci pola
   ZNADZOR.MGRP:=PWOPOZ.MGRP;
:: ustawienie grupy
   {? ZNADZOR.MGR=null() || ZNADZOR.MGR:=HELP.MGR:=PWOPOZ.MGRP().MGR ?};
   _ok
|| FUN.emsg('Pole musi być wypełnione.'@);
   0
?}


\pwopoz_il_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Po redakcji PWOPOZ.IL
::       Sprawdzenie, czy nie przekracza dozwolonej ilosci
::   WY: 1/0 - czy mozna opuscic
::  OLD: \pwopoz_il_ae/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
exec('pwopoz_chk_il','wyp_polwo')


\pwopozildokl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Dokladnosc wyswietlania/redagowania PWOPOZ.IL
::  OLD: \pwopozildokl/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
'_prec='+$exec('mgrp_m_dokl','wyp',PWOPOZ.MGRP().M)


\pwopoz_il_df
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Display format PWOPOZ.IL
::   WY: (std)
::  OLD: \pwopoz_il_df/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
'out'+exec('pwopozildokl','!wyp_lwp_dpwy')


\pwopoz_il_ef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Edit format PWOPOZ.IL
::   WY: (std)
::  OLD: \pwopoz_il_ef/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
'in'+exec('pwopozildokl','!wyp_lwp_dpwy')


\pwopoz_czas_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Przed redakcja PWOPOZ.CZAS
::   WY: 1/0 - czy mozna edytowac
::  OLD: \pwopoz_czas_be/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
PWOPOZ.ZWROT<>'B'


\pwopoz_czas_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Po redakcji PWOPOZ.CZAS
::   WY: 1/0 - czy mozna opuscic
::  OLD: \pwopoz_czas_ae/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
{? PWOPOZ.ZWROT='B'
|| 1
|| fld()>=0
?}


\pwopoz_zwrot_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Przed redakcja PWOPOZ.ZWROT
::   WY: 1/0 - mozna redagowac gdy pozycja nie podlega kontroli ilosci
::  OLD: \pwopoz_zwrot_be/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
(PWOPOZ.MGRP().CTRLIL='N') | (exec('get','#params',700510,2,null())='N')


\pwopoz_zwrot_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Przed redakcja PWOPOZ.ZWROT
::       wyzerowanie czasu i tolerancji dla 'B'
::  OLD: \pwopoz_zwrot_ae/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
{? PWOPOZ.ZWROT='B' || PWOPOZ.TOLER:=PWOPOZ.CZAS:=0 ?};
exec('pwopoz_efld_opt','!wyp_lwp_dpwy');
~~


\pwopoz_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Rekord przed PWOPOZ
::  OLD: \pwopoz_rb/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
ZNADZOR.ILG:=exec('pwopoz_licz_lg','wyp_polwo');
ZNADZOR.MGR:=PWOPOZ.MGRP().MGR;
Color.rekprzed('PWOPOZ#01#00')


\pwopoz_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Rekord po PWOPOZ
::       Sprawdzenie wypelnienia pol i poprawnosci ilosci
::   WY: (standard)
::  OLD: \pwopoz_ra/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
_chkil:="{? exec('pwopoz_chk_il','wyp_polwo') || '' || 'IL' ?}";
_odp:=__CHK.record(PWOPOZ,,'MGRP','IL');
{? _odp=''
|| {? PWOPOZ.ZWROT<>'B'
   || _odp:=__CHK.record(PWOPOZ,,'CZAS');
      {? _odp=''
      || _odp:=_chkil()
      ?}
   || _odp:=_chkil()
   ?}
?};
_odp


\pwopoz_dpu_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Przed akcjami aktywnymi (dpu)
::   WY: 0/1 - czy mozna wykonac akcje
::  OLD: \pwopoz_dpu_b/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
{? POLWO.AKCEPT='N'
|| {? -(menu_txt())<>'popraw' || PWOPOZ.ZWROT:='T' ?};
   exec('pwopoz_efld_opt','!wyp_lwp_dpwy');
   1
|| FUN.emsg('Polecenie zaakceptowane. Zmiany niemożliwe.'@);
   0
?}


\pwopoz_mb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: przed obsługą PWOPOZ
::   WE: _a  (standard dla formuły przed obsługa)
::  OLD: \pwopoz_mb/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
::jezeli wejscie aktywne do okna to przygotowanie normatywow
_result:=~~;
{? grp_empty(POLWO,'WER')
|| _result:='#disable'
|| {? _a
   || {? exec('blk_lock','#table','POLWO',POLWO.ref()) & POLWO.STAT_REJ='N'
      || exec('prep_ctril','wyp_polwo');
         PWOPOZ.actions_grayed('WER',':')
      || PWOPOZ.actions_grayed('WER','DPU:DG')
      ?}
   ?}
?};
_result


\pwopoz_ma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: po obsłudze PWOPOZ
::   WE: _a  (standard dla formuły po obsłudze)
::  OLD: \pwopoz_ma/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
::jezeli wejscie aktywne do usuniecie normatywow
{? _a
|| exec('blk_unlock','#table','POLWO',POLWO.ref());
   exec('drop_ctril','wyp_polwo')
?};
~~


\pwopoz_af
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Po odswiez PWOPOZ
::  OLD: \pwopoz_af/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
:: Odświeżanie okienka KARO - obecnie brak
~~


\znad_ni_bv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Przed wyswietleniem ZNADZOR.NAZWIM
::  OLD: \znad_ni_bv/nadzor.fml
::----------------------------------------------------------------------------------------------------------------------
{? POLWO.P<>null()
|| exec('znad_load','!wyp_lwp_dpwy')
|| exec('znad_blank','!wyp_lwp_dpwy')
?};
~~


\pwopoz_mgr_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Przed wyświetleniem grupy materiałowej (ZNADZOR.MGR) w oknie PWOPOZ
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
1


\pwopoz_mgr_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Przed redakcją grupy materiałowej (ZNADZOR.MGR) w oknie PWOPOZ
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
POMOC.RODZ:='T';
ZNADZOR.MGR:=HELP.MGR:=PWOPOZ.MGRP().MGR;
win_disp();
MGRP.actions('WER','');
1


\pwopoz_mgr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Po redakcji grupy materiałowej (ZNADZOR.MGR) w oknie PWOPOZ
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
{? HELP.MGR<>ZNADZOR.MGR || PWOPOZ.MGRP:=null() ?};
HELP.MGR:=ZNADZOR.MGR;
1


\action_generate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Akcja generowania pozycji polecenia wydania wyposażenia
::       Kontekst wywołania: rekord POLWO oraz utworzone tabele tymczasowe normatywów pracownika i osoby
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy generować pozycje polecenia wydania wyposażenia?'@)
|| _result:=exec('polp_fga','wyp_polwo');
   {? _result.ILEGEN=0
   || FUN.info('Nie wygenerowano pozycji polecenia wydania wyposażenia.'@)
   ?}
?};
~~


\pwopoz_find_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Przed Szukaj w oknie wertowania PWOPOZ
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
MGRP.actions('WER','dpuDPU:dD');
1


\pwopoz_find_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Po Szukaj w oknie wertowania PWOPOZ
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
1


\pwopoz_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Opcje pól w oknie redagowania PWOPOZ
::----------------------------------------------------------------------------------------------------------------------
{? PWOPOZ.ZWROT='B'
|| PWOPOZ.efld_opt('RED','mark=0',,'CZAS')
|| PWOPOZ.efld_opt('RED','mark=1',,'CZAS')
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 8c721b0497551369c9eaff016696cf7a0c1eaee1e0cf3a6991274d527eeda64154c038f83dde67edde193fcf46035b517505c37d7eb49b691de36039e176ceb7a19dfd7aea4d37fbe838c619052653831d1648b177198e00c10a0beeeb06327d9ba8c9325eda9d531acead27941dde3fbf97c7c4888156ece6d1e363afa2c774

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zws_par_fttd.fml [17.00]
:: Utworzony: 2015/10/27
:: Autor: PJ
::======================================================================================================================
:: Zawartosc: Formuły czynności definiowania typów dokumentów dla środków trwałych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Typy dokumentów dla środków trwałych
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('srdt_select','!zws_par_fttd');
~~


\srdt_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla listę definicji dokumentów środków trwałych
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.name()<>'srdtr'+REF.FIRMA().SYMBOL
|| SRDT.use('srdtr'+REF.FIRMA().SYMBOL)
?};
SRDT.index('SRDT');
SRDT.prefix();
SRDT.win_sel('WER');
SRDT.win_edit('RED');
SRDT.win_patt('SZUKAJ');
SRDT.select()


\srdt_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dodawanie nowego typu dokumentu dla środków
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,FINFO);
{? FINFO.SPR_GRP='T' || SRDT.win_edit('RED_G') || SRDT.win_edit('RED') ?};
EDIT_ES.RODZ:='P';
SRDT.blank();
exec('enable_fields','!zws_par_fttd');
{? SRDT.edit("exec('chk_srdt','!zws_par_fttd',0)")
|| SRDT.add()
?}


\srdt_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie typu dokumentu środków
::----------------------------------------------------------------------------------------------------------------------
{? exec('is_srdt_used','!zws_par_fttd')
|| FUN.emsg('Istnieją dokumenty stworzone na podstawie bieżącego typu, usunięcie nie jest możliwe.'@)
|| {? SRDT.r_lock(1,1,1)
   || {? FUN.ask('Usunąć definicję typu dokumentu: %1 (%2).'@[SRDT.TYP,SRDT.NAZWA])
      || SRDT.r_unlock();
         SRDT.del()
      || SRDT.r_unlock()
      ?}
   || FUN.emsg('Typ dokumentu zabllokowany przez innego użytkownika.'@)
   ?}
?}


\srdt_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja typu dokumentu dla środków
::----------------------------------------------------------------------------------------------------------------------
_sym:=0; _dalej:=1;
{? exec('is_srdt_used','!zws_par_fttd')
|| _dalej:=FUN.ask('Istnieją dokumenty stworzone na podstawie bieżącego typu, modyfikacje są możliwe '
           'tylko w zakresie ustalania numeracji/symbolu dokumentu. Kontynuować?'@);
   _sym:=1
?};
{? _dalej
|| {? SRDT.r_lock(1,1,1)
   || exec('czytaj','#stalesys',,FINFO);
      {? ~_sym
      || {? FINFO.SPR_GRP='T' || SRDT.win_edit('RED_G') || SRDT.win_edit('RED') ?}
      || SRDT.win_edit('RED_SYM')
      ?};
      exec('enable_fields','!zws_par_fttd');
      SRDT.hdr_edit(); SRDT.hdr_edit(': '+SRDT.TYP);
      {? SRDT.edit("exec('chk_srdt','!zws_par_fttd',1)")
      || SRDT.put()
      ?};
      SRDT.r_unlock()
   || FUN.emsg('Typ dokumentu zabllokowany przez innego użytkownika.'@)
   ?}
?}


\chk_srdt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja poprawności danych w rekordzie typu dokumentu dla środków
::   WE: _a - jeżeli 1 to w trybie poprawiania
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=0 ?};
_out:=__CHK.record(SRDT,,'TYP','NAZWA');
{? _out='' & SRDT.RODZ='L' & SRDT.R='T' & PAR_SKID.get(207)='T' & SRDT.NRI<>'T'
|| FUN.emsg('W przypadu odrębnej numeracji środków trwałych i niskocennych zmiana rodzaju\n'
            'środka na trwały lub niskocenny wiąże się ze zmianą numeru inwentarzowego, dlatego\n'
            'w typie dokumentu należy również zaznaczyć pole Numer inwetarzowy.'@);
   _out:='NRI'
?};
{? SRDT.NRI='N' & SRDT.NST='N' & SRDT.JORG='N' & SRDT.OSOBA='N' & SRDT.POM='N' & SRDT.KONPOD='N' & SRDT.KONFIN='N' & SRDT.KONDOD='N' & SRDT.GR='N' & SRDT.R='N'
|| _pola:=0
|| _pola:=1
?};
{? _out='' & SRDT.RODZ='L' & _pola=0
|| FUN.emsg('Nie wybrano żadnych szczegółów zmain właściwości.\n'
            'Zatwierdzenie typu dokumentu niemożliwe.\n'@);
   _out:='NRI'
?};
{? SRDT.RODZ_NUM<>'S' & SRDT.FORM_SYM=''
|| FUN.emsg('W przypadku niestandardowej numeracji dokumentów należy podać formułę nadającą symbol.'@);
   _out:='FORM_SYM'
?};
{? _out='' & __CHK.index(SRDT,_a)<>'' || _out:='TYP' ?};
_out


\is_srdt_used
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja czy istnieją dokumenty oparte na typie
::----------------------------------------------------------------------------------------------------------------------
_typ:=SRDT.TYP;
_wy:=SRDT.count()<>0;
{? ~_wy
|| SRDT.cntx_psh();
   _name:=SRDT.names();
   {? _name.first()
   || {! |?
         SRDT.use(_name.NAME);
         SRDT.index('TYP');
         SRDT.prefix(_typ);
         {? SRDT.first()
         || _wy:=SRDT.count()<>0
         ?};
         _wy=0 & _name.next()
      !}
   ?};
   SRDT.cntx_pop()
?};
_wy


\be_srdt_przychod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRDT.PRZYCHOD
::----------------------------------------------------------------------------------------------------------------------
exec('srdt_zmiana_w','!zws_par_fttd') & SRDT.P='+'


\be_srdt_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola SRDT.ROZ
::----------------------------------------------------------------------------------------------------------------------
exec('srdt_zmiana_w','!zws_par_fttd') & SRDT.P='-'


\srdt_zmiana_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pól SRDT związanych ze zmianami wartościowymi
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='Z'
|| {? SRDT.RODZ='W' & SRDT.P='-' || 1 || 0 ?}
|| {? SRDT.RODZ='W' || 1 || 0 ?}
?}


\srdt_zmiana_l
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pól SRDT związanych ze zmianami właściwości
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.RODZ='L' | SRDT.RODZ='D' || 1 || 0 ?}


\srdt_zmiana_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pól SRDT związanych ze zmianami metod amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.RODZ='M' || 1 || 0 ?}


\srdt_zmiana_i
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pól SRDT związanych ze zmianami innymi
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.RODZ='I' || 1 || 0 ?}


\enable_fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła ustawia dostępność pól okna redagowania typów dokumentów
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.RODZ='W' || _enable:=1 || _enable:=0 ?};
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'P');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'PRZYCHOD');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'ROZ');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'Z');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'WARP');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'WARF');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'WARD');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'UMOP');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'UMOF');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'UMOD');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'WP');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'OSTATEK');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'OKE');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'MAX');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'WART_ZB');

{? SRDT.RODZ='M' || _enable:=1 || _enable:=0 ?};
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'MP');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'MF');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'MD');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'STAP');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'STAF');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'STAD');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'WKP');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'WKF');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'WKD');

{? SRDT.RODZ='L' || _enable:=1 || _enable:=0 ?};
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'NRI');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'NST');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'JORG');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'ODD');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'OSOBA');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'POM');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'GR');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'R');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'ZESTAW');
{? (SRDT.RODZ='L' | SRDT.RODZ='D') || _enable:=1 || _enable:=0 ?};
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'KONPOD');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'KONFIN');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'KONDOD');

{? SRDT.RODZ='I' || _enable:=1 || _enable:=0 ?};
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'ULWK');
SRDT.efld_opt(SRDT.win_edit('?'), 'enable='+$_enable,, 'SEZONOWY');

{? SRDT.RODZ_NUM='S'
|| SRDT.efld_opt(SRDT.win_edit('?'),'enable=0',SRDT,'FORM_SYM');
   SRDT.efld_opt(SRDT.win_edit('?'),'enable=0',SRDT,'AKT_SYM')
|| SRDT.efld_opt(SRDT.win_edit('?'),'enable=1',SRDT,'FORM_SYM');
   SRDT.efld_opt(SRDT.win_edit('?'),'enable=1',SRDT,'AKT_SYM')
?}


\ae_srdt_rodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła po redakcji pola SRDT.RODZ
::----------------------------------------------------------------------------------------------------------------------
exec('enable_fields','!zws_par_fttd');
{? SRDT.RODZ='P' || SRDT.O_RODZ:='Przyjęcie środka'
|? SRDT.RODZ='W' || SRDT.O_RODZ:='Wartość'
|? SRDT.RODZ='L' || SRDT.O_RODZ:='Właściwość'
|? SRDT.RODZ='M' || SRDT.O_RODZ:='Metoda amortyzacji'
|? SRDT.RODZ='I' || SRDT.O_RODZ:='Inna'
|? SRDT.RODZ='D' || SRDT.O_RODZ:='Właściwość dofin.'
?};
1


\srdt_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na wyświetl w oknie wertowania definicji typów dokumentów (SRDT)
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.SPR_GRP='T' || SRDT.win_edit('RED_G') || SRDT.win_edit('RED') ?} ;
exec('enable_fields','!zws_par_fttd');
SRDT.display()


\bl_srdt_orodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na wartość początkową pola SRDT.O_RODZ
::----------------------------------------------------------------------------------------------------------------------
'Przyjęcie'


\ae_srdt_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [lokalna]
:: OPIS: Formuła po redakcji pola SRDT.P
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.P='+'
|| SRDT.ROZ:=null;
   SRDT.Z:='N'
|? SRDT.P='-'
|| SRDT.PRZYCHOD:=null
|| SRDT.PRZYCHOD:=null;
   SRDT.ROZ:=null;
   SRDT.Z:='N'
?};
1


\bl_srdt_rodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na wartość początkową pola SRDT.RODZ
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZ


\ae_srdt_rnum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Po redakcji pola SRDT.RODZ_NUM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.RODZ_NUM='S'
|| SRDT.efld_opt(SRDT.win_edit('?'),'enable=0',SRDT,'FORM_SYM');
   SRDT.efld_opt(SRDT.win_edit('?'),'enable=0',SRDT,'AKT_SYM')
|| SRDT.efld_opt(SRDT.win_edit('?'),'enable=1',SRDT,'FORM_SYM');
   SRDT.efld_opt(SRDT.win_edit('?'),'enable=1',SRDT,'AKT_SYM')
?}


\be_srdt_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Przed redakcją pola SRDT.FORM_SYM
::   WE:
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.RODZ_NUM='S' || 0 || 1 ?}


\be_srdt_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Przed redakcją pola SRDT.AKT_SYM
::   WE:
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? SRDT.RODZ_NUM='S' || 0 || 1 ?}


:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 dbf50f9a9074c9c51790ba2da9a399c8a185e9a346339f4eed6a0a6e8546e2cf91caf95db3368e9c3670d06c846af588e250e784c1bcbe4934985cc3f3b7fdc3f1ce41236fb4c6c9fcca9678dbdd4d5eac792dcee583f0d25dd6891c4d9c2d42de8890d2f1592307e3297695d750c84dab72f704c73611beb13037d61df07105

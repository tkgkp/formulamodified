:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_dokr.fml
:: Utworzony: 08.06.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_DOKR - Redakcja okresów rozliczeniowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Redakcja okresów rozliczeniowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy braku wyboru, required=N, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'DONE' || _a ?})"
::# kind=WEW, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=N
::# kind=WY, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=N
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_int:=_par.int;
_out:=_par.out;
_context:=_par.context;
_cfg:=obj_new('AKCJA');
_cfg.AKCJA:='';
_akcja:=_mp.akcja();

params_set('cfg',_cfg);

A_OKR.cntx_psh();
A_OKR.index('A_OKRNN');
A_OKR.clear();
A_OKR.win_sel('WER');
A_OKR.win_edit('RED');
_a_okr:=null();
{? type_of(_int.A_OKR)=type_of(null()) & _int.A_OKR<>null() & A_OKR.seek(_int.A_OKR)
|| _a_okr:=A_OKR.ref();
   {? _akcja='' | _akcja='POPRAW'
   || exec('popraw_okres','!prc_czp_dokr')
   ?}
|? _akcja='' | _akcja='DOŁĄCZ'
|| _a_okr:=exec('dodaj_okres','!prc_czp_dokr');
   _int.A_OKR:=_a_okr;
   _mp.save(_int)
?};
_out.A_OKR:=_a_okr;
_mp.save(,_out);
{? _a_okr=null()
|| {? _in.ON_ESC='KEEP'
   || _mp.keep()
   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()
   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')
   || _mp.done()
   ?}
|? _cfg.AKCJA='ZAKOŃCZ'
|| _mp.done()
|| _mp.keyRef(A_OKR.uidref());
   _mp.keep()
?};
A_OKR.cntx_pop();
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Redakcja okresów rozliczeniowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_int:=params_get().mp.load(exec('kind_internal','#b_port'));
_aOkrNaz:='';

A_OKR.cntx_psh();
A_OKR.prefix();
{? type_of(_int.A_OKR)=type_of(null()) & _int.A_OKR<>null() & A_OKR.seek(_int.A_OKR)
|| _aOkrNaz:=A_OKR.NAZ().NAZ
?};
A_OKR.cntx_pop();

{? +_aOkrNaz
|| 'Dodaj nowy okres rozliczeniowy %1'@@[_aOkrNaz]
|| 'Dodaj nowy okres rozliczeniowy'@@
?}


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Formuła przygotowuje okno redagowania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_we:=A_OKR.mk_edit('Okres rozliczeniowy'@,,'red_okr_roz');
A_OKR.win_ewin(_we,A_OKR,'RED');
exec('zakoncz','#window',A_OKR,_we,1,"params_get().cfg.AKCJA:='ZAKOŃCZ'; 'key:F2'");
exec('ok_esc','#window',A_OKR,_we,1,"params_get().cfg.AKCJA:='OK'; 'key:F2'",,,,,exec('text_red_ok','#window'));
_we


\dodaj_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Redakcja okresów rozliczeniowych w procesie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_we:=exec('wnd','!prc_czp_dokr');
_a_okr:=null();
A_OKR.win_edit(_we);
A_OKR.blank();
{? A_OKR.edit(
      "  _chk:=exec('rekpookr','okres');
         {? (type_of(_chk)=type_of('') & _chk<>'') | (type_of(_chk)=type_of(0) & _chk=0)
         || params_get().cfg.AKCJA:=''
         ?};
         _chk
      "
   )
|| {? A_OKR.add() || _a_okr:=A_OKR.ref() ?}
?};
_a_okr


\popraw_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Redakcja okresów rozliczeniowych w procesie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
A_OKR.win_edit(exec('wnd','!prc_czp_dokr'));
{? A_OKR.edit(
      "  _chk:=exec('rekpookr','okres');
         {? (type_of(_chk)=type_of('') & _chk<>'') | (type_of(_chk)=type_of(0) & _chk=0)
         || params_get().cfg.AKCJA:=''
         ?};
         _chk
      "
   )
|| A_OKR.put()
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 dd2e7ec449e4dc806bc345f25e6e0e86e092e06c81aba7fc3774f0c28b9d5cadccb85c178783eb94e1623b36f7a67969dbb4f99f6d5c0383acf04e5b65a1d82954292e4f54fedbdf44768a1b8abc15c34fb5061b743697b845ae4be58c149749c3839107486545c9925feca9263fecacce7ea7d04db9209b2f65a49eb638ccb0

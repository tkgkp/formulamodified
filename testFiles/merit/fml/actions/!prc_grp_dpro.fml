:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_grp_dpro.fml
:: Utworzony: 13.06.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PRC_GRP_DPRO - Przyp. rodzaju okresu rozl.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Przypisanie rodzaju okresu rozliczeniowego grupie współpracowników.
::   WE:
::   WY:
::  OLD: \grup_or/okres.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

P.cntx_psh();
_TAB:=exec('wybierz_parses','pracownik','PRC').P;
_okr:=obj_new('NAZ','REF');
_ok:=0;

{? _TAB.first()
||
:: wybór okresu rozliczeniowego do przypisania współpracownikom
   A_OKRN.cntx_psh();
   A_OKRN.index('A_OKRN');
   A_OKRN.prefix();
   A_OKRN.win_sel('WER');
   A_OKRN.actions('WER','','W');
   {!
   |? {? ~A_OKRN.select()
      || {? FUN.ask('Czy na pewno chcesz zrezygnować z wprowadzania danych?'@)
         || A_OKRN.cntx_pop();
            P.cntx_pop();
            return(~~)
         || 1
         ?}
      || _okr.NAZ:=A_OKRN.NAZ;
         _okr.REF:=A_OKRN.ref(); 0
      ?}
   !};
   A_OKRN.actions('WER','W:','A');
   A_OKRN.cntx_pop();
:: ustalenie daty obowiązywania nowego okresu rozliczeniowego dla grupy współpracowników
   VAR_EDIT.DATA:=date();
   VAR_EDIT.win_edit('WEWY_DAT');
   {!
   |? {? ~VAR_EDIT.edit("__CHK.record(VAR_EDIT,,'D')")
      || {? FUN.ask('Czy na pewno chcesz zrezygnować z wprowadzania danych?'@)
         || P.cntx_pop();
            return(~~)
         || 1
         ?}
      || _ok:=1; 0
      ?}
   !};
:: przypisanie okresu rozliczeniowego grupie współpracowników
   {? _ok
      &
      FUN.ask('Czy na pewno rozpocząć proces wprowadzania danych do domyślnych okresów wybranej grupie pracowników?'@)
   || FUN.prg_start(_TAB.size(),,,,1);
      A_OKRD.cntx_psh();
      A_OKRD.index('POD');
      A_OKRD.prefix();
      exec('ini_kom','#message','Grupowe wprowadzanie rodzaju okresu rozliczeniowego'@,,65535);
      {!
      |? {? P.seek(_TAB.SQL,,1)
         || {? ~A_OKRD.find_key(P.ref(),VAR_EDIT.DATA)
            || A_OKRD.blank();
               A_OKRD.P:=P.ref();
               A_OKRD.OD:=VAR_EDIT.DATA;
               A_OKRD.A_OKRN:=_okr.REF;
               {? A_OKRD.add(1)
               || exec('norm_okrd','okres',P.ref(),0);
                  exec('aktBuffForOkr','okres',A_OKRD.P);
                  exec('add_kom','#message',
                     'Domyślny okres rozliczeniowy %1 został przypisany dla współpracownika %2 od %3.'@
                        [_okr.NAZ,$A_OKRD.P().IP+' '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,A_OKRD.OD$1
                        ],38,'Zakończone powodzeniem'@
                  )
               || exec('add_kom','#message',
                     'Nie powiodło się dodanie domyślnego okresu %1 dla współpracownika %2 od daty %3.'@
                        [_okr.NAZ,$A_OKRD.P().IP+' '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,A_OKRD.OD$1
                        ],14,'Nie zakończone powodzeniem'@
                  )
               ?}
            || exec('add_kom','#message',
                  'Dla współpracownika %1 zapis nie został dodany, ponieważ od daty %2 obowiązuje już okres %3.'@
                     [  $A_OKRD.P().IP+' '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE,A_OKRD.OD$1,A_OKRD.A_OKRN().NAZ
                     ],184,'Już istnieje'@
               )
            ?}
         ?};
         _TAB.next()
      !};
      exec('end_kom','#message');
      FUN.prg_stop();
      FUN.info('Zakończono proces wprowadzania danych do domyślnych okresów.'@);
      A_OKRD.cntx_pop()
   ?}
|| FUN.emsg('Nie wybrano żadnego pracownika.'@)
?};
P.cntx_pop();
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:58 4d157749de66b4dfd77e6cefe06b712caddb9637868314c13544cac4ab5964ee71534b135bc7f2074073a7002f143dcf2330297bb3907a508bfb2f2df1df6382ea4a23ed3b71c3bc3830deedab7ae95d14e5854cd06e2b5bcfe00efdb4bd892330207aa6f17db7e8e12f8c252ba8db09a1ce00c9965fa0cf1d3505464005d27f

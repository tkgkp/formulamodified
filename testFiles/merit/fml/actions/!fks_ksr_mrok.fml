:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_mrok.fml [17.00]
:: Utworzony: 2015.12.23
:: Autor: AMK
::======================================================================================================================
:: Zawartosc: Formuły czynności FKS_KSR_MROK - Zamknięcie roku - Finanse
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności FKS_KSR_MROK - Zamknięcie roku - Finanse
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WY, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
_args:=params_get();
_we:=_args.in;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_wew:=_args.int;
_wy:=_args.out;
params_set(params_get());
OKR_OBSZ.cntx_psh(); OKRO.cntx_psh(); OKRO_F.cntx_psh(); ROK_F.cntx_psh();
:: czynność startowa
{? _mp.pathProc()
|| {? ~SSTALE.AR
   || FUN.info('Nie wybrano roku w stałych systemu.'@)
   || exec('lic','zam_okr_rok');
      exec('ustaw_bz','zam_okr_rok',SSTALE.AO().ROK);
      exec('init','zam_okr_rok');
      {? OKRESY.ZO
      || OKRESY.ZR();
         {? ROK_F.r_lock(1,1)
         || _wy.OKRO_F:=OKRESY.ZO; _mp.save(,_wy);
            zam_okr:=0;
            params_exec('zam_okr1','zam_okr_rok',1,1);
            params_exec('zam_okr2','zam_okr_rok',1);
            params_exec('zam_okr3','zam_okr_rok',1,1);
            VAR_DEL.delete('zam_okr');
            unlock_r()
         || FUN.info('Inny użytkownik obsługuje rok bilansowy.'@)
         ?}
      ?}
   ?}
|? _mp.pathTodo()
|| exec('lic','zam_okr_rok');
   _komunik:=~_mp.isAutoRun();
   _dalej:=1;
   {? var_press('OKRO_F',_we)>0 & _we.OKRO_F
   || exec('ustaw_bz','zam_okr_rok',_we.OKRO_F().ROK);
      _wy.OKRO_F:=_we.OKRO_F; _mp.save(,_wy)
   |? SSTALE.AO
   || SSTALE.AO();
      exec('ustaw_bz','zam_okr_rok',SSTALE.AO().ROK);
      _wy.OKRO_F:=SSTALE.AO; _mp.save(,_wy)
   || FUN.info('Nie podano okresu z roku zamykanego.'@); _mp.error(); _dalej:=0
   ?};
   {? _dalej
   || exec('init','zam_okr_rok');
      {? OKRESY.ZO
      || OKRESY.ZR();
         {? ROK_F.r_lock(1,1)
         || _wy.OKRO_F:=OKRESY.ZO; _mp.save(,_wy);
            zam_okr:=0;
            params_exec('zam_okr1','zam_okr_rok',1,_komunik);
            params_exec('zam_okr2','zam_okr_rok',1);
            params_exec('zam_okr3','zam_okr_rok',1,_komunik);
            VAR_DEL.delete('zam_okr');
            unlock_r()
         |? _komunik
         || FUN.info('Inny użytkownik obsługuje rok bilansowy.'@)
         ?}
      ?}
   ?}
|? (7+_akcja)='zamknij'
|| exec('init','zam_okr_rok');
   {? ROK_F.r_lock(1,1)
   || _wy.OKRO_F:=OKRO_F.ref(); _mp.save(,_wy);
      zam_okr:=0;
      params_exec('zam_okr1','zam_okr_rok',1,1);
      params_exec('zam_okr2','zam_okr_rok',1);
      params_exec('zam_okr3','zam_okr_rok',1,1);
      VAR_DEL.delete('zam_okr');
      unlock_r()
   || FUN.info('Inny użytkownik obsługuje rok bilansowy.'@)
   ?}
?};
OKR_OBSZ.cntx_pop(); OKRO.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop();
VAR_DEL.delete('spr_bo');
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zamykanie roku dla Finansów'@@;
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_out','#b_port'));
_okro_f:={? var_pres('OKRO_F',_in)>0 & _in.OKRO_F
         || _in.OKRO_F
         |? var_pres('OKRO_F',_out)>0 & _out.OKRO_F
         || _out.OKRO_F
         || null
         ?};
{? _okro_f
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKRO_F.seek(_okro_f,)
   || ROK_F.cntx_psh(); ROK_F.prefix(); OKRO_F.ROK();
      _desc:='Zamykanie roku dla Finansów: %1'@@[ROK_F.NAZ];
      ROK_F.cntx_pop()
   ?};
   OKRO_F.cntx_pop()
?};
_desc


\zam_rokf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamknięcie roku
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_KSR_MROK';
{? _a=1
|| _params.AKCJA:='zamknij1'
|| _params.AKCJA:='zamknij2'
?};
_dalej:=1;
OKRO_F.cntx_psh(); OKR_OBSZ.cntx_psh(); ROK_F.cntx_psh(); ROK_F.prefix();
{? _a=1
|| _obszar:=exec('szuk_b_dom','parses','FKS');
   OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),_obszar);
   {? _obszar & OKR_OBSZ.first()
   || OKRO_F.index('ROK'); OKRO_F.prefix(OKR_OBSZ.OKRO_F().ROK);
      {? OKRO_F.last() || OKRO_F.ROK() || _dalej:=0 ?}
   || FUN.info('Nie znaleziono dla wybranego okresu wspólnego roku finansowego.'@);
      _dalej:=0
   ?}
|| OKRO_F.index('ROK'); OKRO_F.prefix(OKRO_F.ROK);
   {? OKRO_F.last() || OKRO_F.ROK() || _dalej:=0 ?}
?};
{? _dalej
|| OKRESY.ZO:=OKRO_F.ref();
   OKRESY.ZR:=OKRO_F.ROK;
   _params.UIDREF:=OKRO_F.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',OKRO_F.ref());
   exec('mp_run','#b__box',_params)
?};
OKRO_F.cntx_pop(); OKR_OBSZ.cntx_pop(); ROK_F.cntx_pop()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:48 b55f822984eb09785911f4a6a81cc820197cb9bc87fdee0fcb2c52d5f01fc2b5dff399114f7e2953274665bb56eca504431f35ef5028b92577f6712db1995f85517f0c472a46d2501c910b5e8821683e7dd13b58175d4a65ffe6020171e63b2bf4e9ae3c5d8a1133017081f0e34e2b5b9bd18b02b421f81b7fa8102806625035

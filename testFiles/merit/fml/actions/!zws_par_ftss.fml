:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ftss.fml [17.00]
:: Utworzony: 2015/03/03
:: Autor: PJ
::======================================================================================================================
:: Zawartosc: Formuły czynności definiowania siedzib i pomieszczeń dla środków trwałych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Lista siedzib i pomieszczeń
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('siedziby','!zws_par_ftss');
~~


\siedziby
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetlanie okna z listą siedzib i pomieszczeń
::----------------------------------------------------------------------------------------------------------------------
exec('odd_filtr','fst');
SIED.index('SS');
SIED.prefix(REF.FIRMA);
SIED.win_sel('WER_GRP');
SIED.select();
1


\pom_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetlenie pomieszczeń w oknie z listą siedzib
::  OLD: \pom_disp/slowniki
::----------------------------------------------------------------------------------------------------------------------
{? SIED.size()>0
|| VAR.SIED:=SIED.ref();
   POMI.btn_sopt('WER','POMI_WER_DOLACZ','state=normal');
   POMI.actions_grayed('WER','')
|| VAR.SIED:=null;
   POMI.btn_sopt('WER','POMI_WER_DOLACZ','state=grayed');
   POMI.actions_grayed('WER','D:D')
?};
VAR.REF:='POMI.ref()';
VAR.DRUKUJ:='POMI';
VAR.INDEKS:='SRPO';
VAR.PREF:='POMI.S';
POMI.index('POMSIED');
POMI.prefix(REF.FIRMA,SIED.ODD().OD,SIED.S,);
grp_disp(POMI,'WER')


\chk_sied
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: Sprawdzanie wypelnienia pol
::  OLD: \chk_sied/tech_es
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SIED,,'S','N','ODD');
{? _wy=''
|| _wy:=__CHK.index(SIED,-menu_txt()='popraw')='';
   {? _wy=0 || _wy:='S' ?}
?};
_wy


\bl_sied
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: wartosc na blanku
::  OLD: \bl_sied/tech_es
::----------------------------------------------------------------------------------------------------------------------
VAR.SIED


\czy_sied
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: formula przed redakcja pola
::  OLD: \czy_sied/tech_es
::----------------------------------------------------------------------------------------------------------------------
VAR.SIED=0


\ae_siedp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: formula po redakcji pola
::  OLD: \ae_siedp/tech_es
::----------------------------------------------------------------------------------------------------------------------
{? fld || POMI.ODD:=SIED.ODD ?};
1


\bl_pom_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [8.50]
:: OPIS: Na blank pola ODD tabeli POMI
::  OLD: \bl_pom_odd/tech_es
::----------------------------------------------------------------------------------------------------------------------
{? cli_ver()='cgi'
|| null
|? OPERATOR.DEPT<>null & cur_tab()<>SRXN
|| OPERATOR.DEPT
|| {? cur_tab()=SRSR
   || SRSR.ODD
   |? cur_tab()=SRDO
   || SRDO.ODD
   |? cur_tab()=SRXN
   || SRXN.ODD
   |? cur_tab()=SRXP
   || SRXP.ODD
   |? cur_tab()=POMI | cur_tab()=SIED
   || POMI.SIED().ODD
   || null
   ?}
?}


\odd_sied
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: Formula przed redakcją pola POMI.ODD
::  OLD: \odd_sied/tech_es
::----------------------------------------------------------------------------------------------------------------------
POMI.SIED=null


\pooddpom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: formula po redakcji pola
::  OLD: \pooddpom/tech_es
::----------------------------------------------------------------------------------------------------------------------
{? OPERATOR.DEPT=null
|| OPERATOR.DEPT:=fld();
   oper_dep:=null
?};
1


\chk_pom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: kontrola po redakcji
::  OLD: \ae_pom/tech_es
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(POMI,,'S','SIED','O');
{? _wy=''
|| _wy:=__CHK.index(POMI,-menu_txt()='popraw')='';
   {? _wy=0 || _wy:='S' ?}
?};
_wy


\sied_del_przed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formula przed usunięciem Siedziby
::----------------------------------------------------------------------------------------------------------------------
_wy:=1;
POMI.cntx_psh();
POMI.index('POMSIED');
POMI.prefix(REF.FIRMA,SIED.ODD().OD,SIED.S,);
{? POMI.first()
|| FUN.info('Przed usunięciem siedziby należy usunąć przypisane do niej pomieszczenia.'@);
   _wy:=0
?};
POMI.cntx_pop();
_wy


\sied_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowej siedziby do słownika siedzib (SIED)
::  OLD: \sied_add/fst.fml
::----------------------------------------------------------------------------------------------------------------------
SIED.win_edit('RED');
SIED.blank();
{? SIED.edit("exec('chk_sied','!zws_par_ftss')")
|| SIED.add()
?}


\sied_edi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja siedziby w słowniku siedzib (SIED)
::  OLD: \sied_edi/fst.fml
::----------------------------------------------------------------------------------------------------------------------
{? SIED.r_lock(1,1,1)
|| SIED.win_edit('RED');
   {? SIED.edit("exec('chk_sied','!zws_par_ftss')")
   || SIED.put()
   ?};
   SIED.r_unlock()
|| FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
?}


\sied_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie siedziby ze słownika siedzib (SIED)
::  OLD: \sied_del/fst.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('sied_del_przed','!zws_par_ftss')
|| {? SIED.count()=0
   || {? FUN.ask('Usunąć siedzibę %1?'@[SIED.S])
      || {? SIED.r_lock(1,1,1)
         || SIED.r_unlock();
            SIED.del()
         || FUN.emsg('Zapis jest używany przez innego użytkownika i nie można go usunąć.'@)
         ?}
      ?}
   || FUN.emsg('Bieżąca siedziba (%1) jest wykorzystywana w systemie i nie można jej usunąć.'@[SIED.S])
   ?}
?}


\pomi_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formula dołączająca pomieszczenie
::----------------------------------------------------------------------------------------------------------------------
{? cur_win()='SLOWNIK' || VAR.SIED:=null ?};
POMI.win_edit('RED');
POMI.blank();
{? POMI.edit("exec('chk_pom','!zws_par_ftss')")
|| POMI.add()
?}


\pomi_edi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formula edytująca pomieszczenie
::----------------------------------------------------------------------------------------------------------------------
{? POMI.r_lock(1,1,1)
|| {? cur_win()='SLOWNIK' || VAR.SIED:=null ?};
   POMI.win_edit('RED');
   {? POMI.edit("exec('chk_pom','!zws_par_ftss')")
   || POMI.put()
   ?};
   POMI.r_unlock()
|| FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
?}


\pomi_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formula przed usunięciem Pomieszczenia
::----------------------------------------------------------------------------------------------------------------------
{? POMI.count()>0
|| FUN.emsg('Pomieszczenie: %1 jest wykorzystywane w systemie i nie można go usunąć.'@[POMI.S])
|| {? FUN.ask('Usunąć pomieszczenie %1?'@[POMI.S])
   || {? POMI.r_lock(1,1,1)
      || POMI.r_unlock();
         POMI.del()
      || FUN.emsg('Zapis jest używany przez innego użytkownika i nie można go usunąć.'@)
      ?}
   ?}
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 99ba220ef9d7ebf7183bf9a90b3aaf0fef8a93a7c599b4f9d34cad24ed2bd6e055136708f0972176d9f9adadefc5c3f2f3155c5087df41ddf0b309aa59fb2d3956cfda4f4c76bff3af4a389b9658b379367022f4e4ba0577c4809461866c68aec692fe9a2dba29831d6e55411c9093ffa88f25cf95980ab75c31852aac2fde90

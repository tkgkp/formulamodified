:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_drdm.fml
:: Utworzony: 30.10.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DRDM - Redakcja dokumentu zmiany metody amortyzacji środka
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Redakcja dokumentu zmiany metody amortyzacji środka - główna formuła czynności FST_EWI_DRDM.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS

{? ~exec('okr_mod','fst') || return(0) ?};

{? ~SRD.isDataCompleted()
|| FUN.emsg('Środek wymaga uzupełnienia danych. Dołączanie i modyfikacja dokumentów nie jest możliwa.'@);
   return(0)
?};

{? SRD.isElement()
|| FUN.emsg('Nie można dodawać i modyfikować dokumentów zmiany metody dla elementów składowych zestawu.\n'
            'Dane tego typu są wspólne dla zestawu dlatego taki dokument należy dodawać na poziomie zestawu.'@);
   return(0)
?};

_tmp:=params_get().drdm_akcja;
{? _tmp<>'' || _a:=_tmp ?};

{? (_=0 & _tmp='') | _a='dołącz' || exec('dok_add','!fst_ewi_drdm')
|? _a='popraw' || exec('dok_edit','!fst_ewi_drdm')
|? _a='usuń' || exec('dok_del','!fst_ewi_drdm')
?}


\dok_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dołączanie nowego dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
_ref:=null;
_tmp:=EDIT_ES.RODZ;

{? SRSR.r_lock(1,1,1)
|| EDIT_ES.RODZ:='M';
   SRDT.cntx_psh();
   SRDT.index('SRDT');
   SRDT.prefix(EDIT_ES.RODZ);
   {? SRDT.first()
   || SRDT.win_sel('SELECT');
      {? SRDT.select()
      || _ref:=SRDT.ref()
      ?}
   || FUN.emsg('W systemie nie zdefiniowano żadnego typu dokumentu zmiany metody amortyzacji.\n'
               'Należy uzupełnić brakujące dane w obszarze Ustawienia i parametryzacja.'@);
      SRSR.r_unlock();
      SRDT.cntx_pop();
      EDIT_ES.RODZ:=_tmp;
      return(0)
   ?};

   {? _ref<>null
   || SRDO.blank();
      SRDO_POM.blank();
      SRDO.TYP:=_ref;
      SRDO.ODD_DOK:=SRST.ODD;
      SRDO.NR:=exec('bl_srdo_nr','fst');
      SRDO.SYMBOL:=exec('symdok','fst');
:: stan z bieżącego miesiąca
      {? SRDO.TYP().MP='T' || SRDO_POM.MP:=SRDO.MP:=SRST.MP ?};
      {? SRDO.TYP().STAP='T' || SRDO_POM.STAP:=SRDO.STAP:=SRST.STAP ?};
      {? SRDO.TYP().WKP='T' || SRDO_POM.WKP:=SRDO.WKP:=SRST.WKP ?};
      {? SRDO.TYP().MP='T' || SRDO_POM.DPLP:=SRDO.DPLP:=SRST.DPLP ?};

      {? SRDO.TYP().MF='T' || SRDO_POM.MB:=SRDO.MF:=SRST.MF ?};
      {? SRDO.TYP().STAF='T' || SRDO_POM.STAB:=SRDO.STAF:=SRST.STAF ?};
      {? SRDO.TYP().WKF='T' || SRDO_POM.WKF:=SRDO.WKF:=SRST.WKF ?};
      {? SRDO.TYP().MF='T' || SRDO_POM.DPLF:=SRDO.DPLF:=SRST.DPLF ?};

      {? FINFO.TOR_D='T'
      || {? SRDO.TYP().MD='T' || SRDO_POM.MD:=SRDO.MD:=SRST.MD ?};
         {? SRDO.TYP().STAD='T' || SRDO_POM.STAD:=SRDO.STAD:=SRST.STAD ?};
         {? SRDO.TYP().WKD='T' || SRDO_POM.WKD:=SRDO.WKD:=SRST.WKD ?};
         {? SRDO.TYP().MD='T' || SRDO_POM.DPLD:=SRDO.DPLD:=SRST.DPLD ?}
      ?};

:: jeżeli środek niskocenny ma metodę amortyzacji nieodpowiednią (degresywną lub naturalną)
:: np. po zmianie rodzaju środka to system zeruje przypisanie metody.
      {? SRST.R='N' & 1+SRDO.MP().T='D'
      || SRDO.MP:=null
      ?};
      {? SRST.R='N' & (1+SRDO.MF().T='D' | 1+SRDO.MF().T='N')
      || SRDO.MF:=null
      ?};
      {? FINFO.TOR_D='T'
      || {? SRST.R='N' & 1+SRDO.MD().T='D'
         || SRDO.MD:=null
         ?}
      ?};

      {? EDIT_ES.NISKO='T'
      || SRDO.win_edit('RED_MN')
      || SRDO.win_edit('RED_M')
      ?};
      {? EDIT_ES.R='N' || EDIT_ES.NISKO:='T' || EDIT_ES.NISKO:='' ?};
      exec('set_field_drdm','fst');
      {? SRDO.edit("exec('chk_dok','!fst_ewi_drdm',0)")
      || exec('mod_rec','fst');
         SRSR.r_unlock();
         {? SRDO.add()
         || SRD.updateRecState(SRDO.OKRO_F);
:: jeżeli grupa kapitałowa i dotyczy środka nabytego to kopia dokumentu dla środka pierwotnego
            {? FINFO.SPR_GRP='T' & SRDO.SRSR().ORG_REF<>''
            || {? ~exec('copy_org','fst',SRDO.ref(),'M')
               || FUN.emsg('Nie udało się odświeżyć danych w firmie pierwotnej bieżącego środka.'@)
               ?}
            ?};
:: jeżeli zestaw to odświeżenie danych elementów
            {? SRD.isSet() || SRD.updateElements() ?}
         ?}
      || SRSR.r_unlock()
      ?}
   ?};
   SRDT.cntx_pop()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?};
EDIT_ES.RODZ:=_tmp


\dok_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
{? SRDO.AUTO='T'
|| FUN.emsg('Dokument wygenerowany automatycznie podczas zmiany metody degresywnej na liniową.\n'
            'Może być usunięty tylko poprzez usunięcie naliczonej amortyzacji w okresie od którego\n'
            'obowiązuje dokument - %1 %2.'@[SRDO.OKRO_F().NAZ,SRDO.ROK_F().NAZ]);
   return(0)
?};
{? SRSR.r_lock(1,1,1)
|| {? SRDO.r_lock(1,1,1)
   || _tmp:=EDIT_ES.RODZ;
      EDIT_ES.RODZ:='M';
       {? EDIT_ES.NISKO='T'
      || SRDO.win_edit('RED_MN')
      || SRDO.win_edit('RED_M')
      ?};
      SRDO_POM.EDIT:='T';
      exec('set_srdo_pom','fst2');

      exec('set_field_drdm','fst');
      {? EDIT_ES.R='N' || EDIT_ES.NISKO:='T' || EDIT_ES.NISKO:='' ?};
      {? SRDO.edit("exec('chk_dok','!fst_ewi_drdm',1)")
      || exec('mod_rec','fst');
         SRSR.r_unlock();
         {? SRDO.put()
         || SRD.updateRecState(SRDO.OKRO_F);
:: jeżeli grupa kapitałowa i dotyczy środka nabytego to aktualizacja kopii dokumentu dla środka pierwotnego
            {? FINFO.SPR_GRP='T' & SRDO.SRSR().ORG_REF<>''
            || {? ~exec('update_org','fst',SRDO.ref(),'M')
               || FUN.emsg('Nie udało się odświeżyć danych w firmie pierwotnej bieżącego środka.'@)
               ?}
            ?};

            {? SRD.isSet() || SRD.updateElements() ?}
         ?}
      || SRSR.r_unlock()
      ?};
      EDIT_ES.RODZ:=_tmp;
      SRDO.r_unlock()
   || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
   ?}
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?}


\dok_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SRD.isDefunct() || return(0) ?};
{? SRDO.AUTO='T'
|| FUN.emsg('Dokument wygenerowany automatycznie podczas zmiany metody degresywnej na liniową.\n'
            'Może być usunięty tylko poprzez usunięcie naliczonej amortyzacji w okresie od którego\n'
            'obowiązuje dokument - %1 %2.'@[SRDO.OKRO_F().NAZ,SRDO.ROK_F().NAZ]);
   return(0)
?};
:: kontrola czy zmiana nie dotyczy zadekretowanej amortyzacji
_dekret:=0;
SRST.cntx_psh(); SRSR.cntx_psh();
SRST.index('SROD');
SRST.prefix(SRSR.ref());
{? SRST.find_key(SRDO.ROK_F,SRDO.OKRO_F)
|| {! |?
      {? SRST.DEKRET='T' || _dekret:=1 ?};
      _dekret=0 & SRST.next()
   !}
?};
SRST.cntx_pop(); SRSR.cntx_pop();
{? _dekret=1
||  FUN.emsg('Amortyzacja w okresie obowiązywania dokumentu lub w późniejszym została już\n'
             'zadekretowana dla co najmniej jednego toru amortyzacji. Usuwanie amortyzacji\n'
             'lub wprowadzanie zmian mogących wpłynąć na amortyzację nie jest możliwe.'@);
   return(0)
?};
{? SRDO.count()=0
|| {? SRDO.r_lock(1,1,1)
   || {? FUN.ask('Usunąć dokument zmiany metody o symbolu: %1?'@[SRDO.SYMBOL])
      || SRSR.r_unlock(); SRDO.r_unlock();
         _okr:=SRDO.OKRO_F;
         _uidref:=SRDO.uidref();
         {? SRDO.del(1,1)
         || SRD.updateRecState(_okr);
:: jeżeli grupa kapitałowa i dokument dotyczy środka nabytego to usunięcie kopii dokumentu dla środka pierwotnego
            {? FINFO.SPR_GRP='T' & SRDO.SRSR().ORG_REF<>''
            || {? ~exec('delete_org','fst',_uidref,'M',SRDO.SRSR().ORG_REF)
               || FUN.emsg('Nie udało się odświeżyć danych w firmie pierwotnej bieżącego środka.'@)
               ?}
            ?};

            {? SRD.isSet() || SRD.updateElements() ?};
            _odd:=ODD.f_size();
            {? var_pres('__DTREE') & __DTREE>0 & _odd>1
            || _oknsrst:='DWER'
            |? var_pres('__DTREE') & __DTREE>0 & _odd<=1
            || _oknsrst:='DWER_O'
            || _oknsrst:='WER'
            ?};
            grp_disp(SRST,_oknsrst)
         ?}
      || SRSR.r_unlock(); SRDO.r_unlock()
      ?}
   || FUN.emsg('Zapis zablokowany przez innego użytkownika.'@)
   ?}
|| FUN.emsg('Bieżący dokument jest wykorzystywany w systemie i nie można go usunąć.'@)
?}


\chk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja poprawności dokumentu
::   WE: _a = 1 - popraw, 0 - dołącz
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRDO,,'SYMBOL','DW','DO');
{? _wy=''
|| {? SRDO.OKRO_F=null
   || FUN.emsg('Nie uzupełnione pole Okres obowiązywania\n'
               '(pole uzupełni się automatycznie po redakcji pola Data obowiązywania).'@);
      _wy:='DO'
   ?}
?};
:: kontrola unikalności numeru i symbolu dokumentu
{? _wy=''
|| {? __CHK.index(SRDO,_a)<>''
   || {? FUN.ask('Wygenerować nowy numer dokumentu?'@)
      || SRDO.NR:=exec('bl_srdo_nr','fst',SRDO.NR);
         SRDO.SYMBOL:=exec('symdok','fst')
      ?};
      _wy:='SYMBOL'
   ?}
?};
:: kontrola daty operacji
{? _wy='' & SRDO.OKRO_F<>null
|| {? exec('test_new_year','fst',SRDO.OKRO_F().ROK)
   || FUN.emsg('Wskazana data operacji pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='DO'
   ?}
?};
:: kontrola czy okres jest zamknięty
{? _wy='' & SRDO.DW<>date(0,0,0)
|| _okres:=exec('find_okro_f','fst', SRDO.DW);
   {? ~exec('okr_mod','fst',_okres)
   || _wy:='DW'
   ?}
?};
:: kontrola miesiąca obowiązywania
{? _wy='' & ~exec('chk_dok_okres','fst') || _wy:='DO' ?};
:: kontrola daty wystawienia
{? _wy='' & exec('ae_srdo_dw','fst')=0 || _wy:='DW' ?};
:: kontrola zmiany metody
{? _wy='' & SRDO.TYP().MF='T' & 1+SRDO.MF().T='N'
|| FUN.emsg('Nie można zmienić metody na naturalną w trakcie amortyzowania środka.\n'
            'Metodę naturalną bilansową można wybrać dla nowo wprowadzonego środka.'@);
   _wy:='MF'
?};
{? _wy='' & SRDO.TYP().MF='T' & 1+SRST.MF().T='N' & 1+SRDO.MF().T<>'N'
|| FUN.emsg('Nie można zmienić metody naturalnej na inną w trakcie amortyzowania środka.'@);
   _wy:='MF'
?};
{? _wy='' & SRDO.TYP().MP='T' & 1+SRST.MP().T='L' & 1+SRDO.MP().T='D'
|| FUN.emsg('Nie można zmienić metody liniowej na degresywną w trakcie amortyzowania środka.'@);
   _wy:='MP'
?};
{? _wy='' & SRDO.TYP().MF='T' & 1+SRST.MF().T='L' & 1+SRDO.MF().T='D'
|| FUN.emsg('Nie można zmienić metody liniowej na degresywną w trakcie amortyzowania środka.'@);
   _wy:='MF'
?};
{? FINFO.TOR_D='T'
|| {? _wy='' & SRDO.TYP().MD='T' & 1+SRST.MD().T='L' & 1+SRDO.MD().T='D'
   || FUN.emsg('Nie można zmienić metody liniowej na degresywną w trakcie amortyzowania środka.'@);
      _wy:='MD'
   ?}
?};
{? _wy='' & SRDO.TYP().STAP='T' & SRDO.STAP>100
|| FUN.emsg('Stawka podatkowa amortyzacji nie może być większa niż 100%%.'@);
   _wy:='STAP'
?};
{? _wy='' & SRDO.TYP().STAP='T' & SRDO.STAP<0
|| FUN.emsg('Stawka podatkowa amortyzacji nie może być mniejsza niż 0%%.'@);
   _wy:='STAP'
?};
{? _wy='' & SRDO.TYP().STAF='T' & SRDO.STAF>100
|| FUN.emsg('Stawka bilansowa amortyzacji nie może być większa niż 100%%.'@);
   _wy:='STAF'
?};
{? _wy='' & SRDO.TYP().STAF='T' & SRDO.STAF<0
|| FUN.emsg('Stawka bilansowa amortyzacji nie może być mniejsza niż 0%%.'@);
   _wy:='STAF'
?};
{? FINFO.TOR_D='T'
|| {? _wy='' & SRDO.TYP().STAD='T' & SRDO.STAD>100
   || FUN.emsg('Stawka dodatkowa amortyzacji nie może być większa niż 100%%.'@);
      _wy:='STAD'
   ?};
   {? _wy='' & SRDO.TYP().STAD='T' & SRDO.STAD<0
   || FUN.emsg('Stawka dodatkowa amortyzacji nie może być mniejsza niż 0%%.'@);
      _wy:='STAD'
   ?}
?};
:: kontrola czy zmiana nie dotyczy zadekretowanej amortyzacji
{? _wy=''
|| _dekret:=0;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRSR.ref());
   {? SRST.find_key(SRDO.ROK_F,SRDO.OKRO_F)
   || {! |?
         {? SRST.DEKRET='T' || _dekret:=1 ?};
         _dekret=0 & SRST.next()
      !}
   ?};
   SRST.cntx_pop();
   {? _dekret=1
   ||  FUN.emsg('Amortyzacja w okresie obowiązywania dokumentu lub w późniejszym została już\n'
                'zadekretowana dla co najmniej jednego toru amortyzacji. Usuwanie amortyzacji\n'
                'lub wprowadzanie zmian mogących wpłynąć na amortyzację nie jest możliwe.'@);
      _wy:='DO'
   ?}
?};
_wy


\be_srdo_mp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRDO.MP
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='P';
1


\be_srdo_mf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRDO.MF
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='F';
1


\be_srdo_md
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed redakcją pola SRDO.MD
::----------------------------------------------------------------------------------------------------------------------
EDIT_ES.RODZAJ:='D';
1


\be_srdo_dplp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed radakcją pola SRDO.DPLP
::----------------------------------------------------------------------------------------------------------------------
exec('data_przejscia','fst','P','SRDO')


\be_srdo_dplf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed radakcją pola SRDO.DPLF
::----------------------------------------------------------------------------------------------------------------------
exec('data_przejscia','fst','F','SRDO')


\be_srdo_dpld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed radakcją pola SRDO.DPLD
::----------------------------------------------------------------------------------------------------------------------
exec('data_przejscia','fst','D','SRDO')


\be_srdo_wkp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed radakcją pola SRDO.WKP
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.MP=null | (1+SRDO.MP().T<>'D' & 1+SRDO.MP().T<>'L') || 0 || 1 ?}


\be_srdo_wkf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed radakcją pola SRDO.WKF
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.MF=null | (1+SRDO.MF().T<>'D' & 1+SRDO.MF().T<>'L') || 0 || 1 ?}


\be_srdo_wkd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed radakcją pola SRDO.WKD
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.MD=null | (1+SRDO.MD().T<>'D' & 1+SRDO.MD().T<>'L')|| 0 || 1 ?}


\ae_srdo_wkp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji pola tabeli SRDO.WKP
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? 1+SRDO.MP().T='D' || exec('data_przejscia','fst','P','SRDO')
 ?};
1


\ae_srdo_wkf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji pola tabeli SRDO.WKF
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? 1+SRDO.MF().T='D' || exec('data_przejscia','fst','F','SRDO')
 ?};
1


\ae_srdo_wkd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji pola tabeli SRDO.WKD
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? 1+SRDO.MD().T='D' || exec('data_przejscia','fst','D','SRDO')
 ?};
1


\ae_srdo_mp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redagowaniu pola SRDO.MP
::----------------------------------------------------------------------------------------------------------------------
{? 1+SRDO.MP().T='D' & SRDO.WKP<=1
|| SRDO.WKP:=SRDO.SRSR().GR().WD
|? 1+SRDO.MP().T<>'D' & SRDO.WKP<>1
|| SRDO.WKP:=1;
   SRDO.DPLP:=date(0,0,0)
?};
1


\ae_srdo_mf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redagowaniu pola SRDO.MF
::----------------------------------------------------------------------------------------------------------------------
{? 1+SRDO.MF().T='D' & SRDO.WKF<=1
|| SRDO.WKF:=SRDO.SRSR().GR().WD
|? 1+SRDO.MF().T<>'D' & SRDO.WKF<>1
|| SRDO.WKF:=1;
   SRDO.DPLF:=date(0,0,0)
?};
1


\ae_srdo_md
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redagowaniu pola SRDO.MD
::----------------------------------------------------------------------------------------------------------------------
{? 1+SRDO.MD().T='D' & SRDO.WKD<=1
|| SRDO.WKP:=SRDO.SRSR().GR().WD
|? 1+SRDO.MD().T<>'D' & SRDO.WKD<>1
|| SRDO.WKD:=1;
   SRDO.DPLD:=date(0,0,0)
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 cb73dba2d1c0f9979504a8856b946937fec1e17084e8e6d669f2d9eeeeab30c3f5420ae4c1a3698d6f6745b7a8bc2b15c26dfed78de6a8b9c7f8ffcffa5e5d74bcada4bf050f9d12a0fda2c3f126fa439782597e58c927bebde2569cdc15401cf2b2fe7b9b68db0b8cdea7f4322ddabede31e128189879b0d675076658264b6f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_zes_zecp.fml
:: Utworzony: 02.09.2019
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_ZES_ZECP - Załączniki kart czasu pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.42]
:: OPIS: Generowanie załączników z kartami ewidencji czasu pracy.
::   WE:
::   WY:
::  OLD: \gen_ecp/gen_zal.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
{? ~FUN.ask(
      'Uruchomienie tej funkcji spowoduje wygenerowanie załączników\n'
      'z kartami ewidencji miesięcznej czasu pracy,\n'
      'które dotyczą wskazanego okresu.'@+'\n'
      'Kontynuować?'@
   )
|| return()
?};

_PAR:=exec('p_wkartapracywewy','pkd','W','Generowanie załączników'@);
{? ~_PAR.first()
|| return()
?};

_prac:=exec('wybierz_parses','pracownik','PKD');
{? _prac.P.first()
|| _RAPORT:=exec('raport','zal','Miesięczna karta ewidencji czasu pracy'@);
   _prg:=obj_new('size','step','proc','text','title','twait');
   _prg.size:=_prac.P.size();
   _prg.step:=0;
   _prg.proc:=-1;
   _prg.text:='Generowanie załączników na podstawie kart ewidencji czasu pracy.'@;
   _prg.title:='Przetwarzanie danych'@;
   _prg.twait:='Proszę czekać ...'@;
   progress(,'%1 %2'[_prg.text,_prg.twait],_prg.title);
   VAR_DEL.delete('batch');
   batch:=obj_new('id','par');
   batch.id:='pkd_wkartapracywewy';
   batch.par:=_PAR;

   _rok:=_PAR.ROK;
   _msc:=_PAR.MC;
   _okres:=form(_rok,-4,0,'9.')+form(_msc,-2,0,'9.');
   _d0:=date(0,0,0);
   _user:=username();

   _args:=exec('add_args','zal');
   _args.SLO_NAZ:='Karta ewidencji czasu pracy';
   _args.FILE_PTH:=1;
   _args.ZAL_NAME:='emcp_%1_%2.pdf' [4+_okres,_okres+2];
   _args.ZAL_DESC:='Ewidencja miesięczna czasu pracy: %1 r.' [date(_rok,_msc,1)$8];
   _args.OVR:=
      {? _PAR.OVR=0 || "0"
      |? _PAR.OVR=1 || "~exec('isNotEmpty','zal_pobh',ZALACZ.ref())"
      |? _PAR.OVR=2 || "1"
      |? _PAR.OVR=3 || "2"
      ?};

   OSOBA.cntx_psh();
   OSOBA.prefix();
   P.cntx_psh();
   P.prefix();
   {!
   |? _prg.step+=1;
      _proc:=(100*_prg.step/_prg.size)$0;
      {? _prg.proc<>_proc
      || _prg.proc:=_proc;
         progress(_prg.proc,'%1 %2 [%3%%]'[_prg.text,_prg.twait,form(_prg.proc,3,0)],_prg.title,1)
      ?};
      {? P.seek(_prac.P.SQL,,1) & P.DZA<=date(_rok,_msc,0) & (P.DZ=_d0 | P.DZ>=date(_rok,_msc,1))
      || _args.FILE_PATH:=_user+$P.tm_stamp()+_okres+'.pdf';
         {? rep_exec('pkd_wkartapracywewy',,0,_args.FILE_PATH,1)
         || _RAPORT.add(exec('add','zal',_args))
         ?}
      ?};
      _prac.P.next()
   !};
   P.cntx_pop();
   OSOBA.cntx_pop();

   VAR_DEL.delete('batch');
   prgs_clr();
   _RAPORT.show()
?};

~~

:Sign Version 2.0 jowisz:1045 2021/11/18 14:54:34 67ea27067ddc398cd0ced5b521604c191cc0f721f13430657d808307b072cfbaed03e9d84835d2605c738760bc7a073d760a54c58c5712176d7c4ba4a3608e0c7edbb3db4b612a89ec6454fc7a6c5dabcc506e8f700cb560ceb6949939542d7e10d62966954a2c6f05cfba37af6aa749080b7204497d6b27f1afea70074df1ec

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_ntc_send.fml
:: Utworzony: 09.03.2023
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_NTC_SEND - Wysyłanie powiadomień
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wysyłanie powiadomień - główna formuła czynności ZWS_NTC_SEND
::----------------------------------------------------------------------------------------------------------------------

:: UWAGA: Czynność wysłania uruchomiona automatycznie po czynności serwisowej
::        zostanie uruchomiona niejawnie w trybie serwisowym.
::# properties=SEND,GRP_FIRM

:: PARAMETRY WE:
::# kind=WE, symbol=USER,       type=STRING, name=Nazwa użytkownika,  required=N
::# kind=WE, symbol=USERS,      type=MEMO,   name=Lista użytkowników, required=N
::# kind=WE, symbol=TITLE,      type=STRING, name=Tytuł,              required=N
::# kind=WE, symbol=DESC,       type=MEMO,   name=Treść,              required=N
::# kind=WE, symbol=VARIANT,    type=STRING, name=Wariant,            required=N, fml_val="exec('variant_select','#ntc',_a,_b)"
::# kind=WE, symbol=ICON,       type=STRING, name=Ikona,              required=N, fml_val="exec('icon_select','#ntc',_a,_b)"
::# kind=WE, symbol=INFO,       type=STRING, name=Informacja,         required=N
::# kind=WE, symbol=BUTTON_1,   type=STRING, name=Przycisk 1,         required=N
::# kind=WE, symbol=BUTTON_2,   type=STRING, name=Przycisk 2,         required=N
::# kind=WE, symbol=BUTTON_3,   type=STRING, name=Przycisk 3,         required=N
::# kind=WE, symbol=BUTTON_4,   type=STRING, name=Przycisk 4,         required=N
::# kind=WE, symbol=BUTTON_5,   type=STRING, name=Przycisk 5,         required=N
::# kind=WE, symbol=ACTION_1,   type=STRING, name=Akcja 1,            required=N
::# kind=WE, symbol=ACTION_2,   type=STRING, name=Akcja 2,            required=N
::# kind=WE, symbol=ACTION_3,   type=STRING, name=Akcja 3,            required=N
::# kind=WE, symbol=ACTION_4,   type=STRING, name=Akcja 4,            required=N
::# kind=WE, symbol=ACTION_5,   type=STRING, name=Akcja 5,            required=N
::# kind=WE, symbol=PARAM_1,    type=STRING, name=Parametr 1,         required=N
::# kind=WE, symbol=PARAM_2,    type=STRING, name=Parametr 2,         required=N
::# kind=WE, symbol=PARAM_3,    type=STRING, name=Parametr 3,         required=N
::# kind=WE, symbol=PARAM_4,    type=STRING, name=Parametr 4,         required=N
::# kind=WE, symbol=PARAM_5,    type=STRING, name=Parametr 5,         required=N

_uid:='ZWS_NTC_SEND';

params_set(params_get());
_mp:=params_get().mp;
_in:=params_get().in;
_error:=0;

:: Kontrola parametrów
{? var_pres('USER',_in)<0 || _error:=1 || {? var_pres('USER',_in)<>type_of('') || _in.USER:='' ?} ?};
{? var_pres('USERS',_in)<0 || _error:=1 || {? var_pres('USERS',_in)<>type_of('') || _in.USERS:='' ?} ?};
{? var_pres('TITLE',_in)<0 || _error:=1 || {? var_pres('TITLE',_in)<>type_of('') || _in.TITLE:='' ?} ?};
{? var_pres('DESC',_in)<0 || _error:=1 || {? var_pres('DESC',_in)<>type_of('') || _in.DESC:='' ?} ?};
{? var_pres('VARIANT',_in)<0 || _error:=1 || {? var_pres('VARIANT',_in)<>type_of('') || _in.VARIANT:='info' ?} ?};
{? var_pres('ICON',_in)<0 || _error:=1 || {? var_pres('ICON',_in)<>type_of('') || _in.ICON:='' ?} ?};
{? var_pres('INFO',_in)<0 || _error:=1 || {? var_pres('INFO',_in)<>type_of('') || _in.INFO:='' ?} ?};
{! _it:=1..5
|! {? var_pres('BUTTON_%1'[$_it],_in)<0
   || _error:=1
   || {? var_pres('BUTTON_%1'[$_it],_in)<>type_of('') || ($'_a.BUTTON_%1'[$_it])(_in):='' ?}
   ?};
   {? var_pres('ACTION_%1'[$_it],_in)<0
   || _error:=1
   || {? var_pres('ACTION_%1'[$_it],_in)<>type_of('') || ($'_a.ACTION_%1'[$_it])(_in):='' ?}
   ?};
   {? var_pres('PARAM_%1'[$_it],_in)<0
   || _error:=1
   || {? var_pres('PARAM_%1'[$_it],_in)<>type_of('') || ($'_a.PARAM_%1'[$_it])(_in):='' ?}
   ?}
!};

{? _error || FUN.info('Należy zaktualizować czynność %1.'@[_uid]); return(~~) ?};

:: Wykonanie formuł przekształacających parametry wejściowe
{? 1+_in.USER='$' || _in.USER:=($(1-_in.USER))() ?};
{? 1+_in.USERS='$' || _in.USERS:=($(1-_in.USERS))() ?};
{? 1+_in.TITLE='$' || _in.TITLE:=($(1-_in.TITLE))() ?};
{? 1+_in.DESC='$' || _in.DESC:=($(1-_in.DESC))() ?};
{? 1+_in.VARIANT='$' || _in.VARIANT:=($(1-_in.VARIANT))() ?};
{? 1+_in.ICON='$' || _in.ICON:=($(1-_in.ICON))() ?};
{? 1+_in.INFO='$' || _in.INFO:=($(1-_in.INFO))() ?};
{! _it:=1..5
|! {? 1+($'_a.BUTTON_%1'[$_it])(_in)='$'
   || ($'params_set(params_get());_a.BUTTON_%1:=($(1-_a.BUTTON_%1))()'[$_it])(_in)
   ?};
   {? 1+($'_a.ACTION_%1'[$_it])(_in)='$'
   || ($'params_set(params_get());_a.ACTION_%1:=($(1-_a.ACTION_%1))()'[$_it])(_in)
   ?};
   {? 1+($'_a.PARAM_%1'[$_it])(_in)='$'
   || ($'params_set(params_get());_a.PARAM_%1:=($(1-_a.PARAM_%1))()'[$_it])(_in)
   ?}
!};

{? _mp.isAutoRun() | _mp.isService() | _mp.akcja()='WYŚLIJ'
|| {? exec('send','!zws_ntc_send',_in)
   || _mp.done()
   ?}
|| {? exec('edit','!zws_ntc_send',_in)
   || {? exec('send','!zws_ntc_send',_in)
      || _mp.done()
      ?}
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wysyłanie powiadomień - opis na liście zadań dla czynności ZWS_NTC_SEND
::----------------------------------------------------------------------------------------------------------------------
'Wyślij powiadomienie w aplikacji'@@


\edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Edycja parametrów powiadomienia
::   WE: _a - parametry wejściowe czynności
::   WY: 0/1 - czy potwierdzona edycja
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;

_tab:=tab_tmp(1
   ,'USER','STRING[20]','Użytkownik'@
   ,'USERS','SYS_MEMO','Lista użytkowników'@
   ,'TITLE','STRING[255]','Tytuł'@
   ,'DESC','SYS_MEMO','Treść'@
   ,'VARIANT','STRING[255]','Wariant'@
   ,'ICON','STRING[255]','Ikona'@
   ,'INFO','STRING[255]','Informacja'@
   ,'BUTTON_1','STRING[100]','Przycisk 1'@
   ,'ACTION_1','STRING[255]','Akcja 1'@
   ,'BUTTON_2','STRING[100]','Przycisk 2'@
   ,'ACTION_2','STRING[255]','Akcja 2'@
   ,'BUTTON_3','STRING[100]','Przycisk 3'@
   ,'ACTION_3','STRING[255]','Akcja 3'@
   ,'BUTTON_4','STRING[100]','Przycisk 4'@
   ,'ACTION_4','STRING[255]','Akcja 4'@
   ,'BUTTON_5','STRING[100]','Przycisk 5'@
   ,'ACTION_5','STRING[255]','Akcja 5'@
   ,'PARAM_1','STRING[100]','Parametr 1'@
   ,'PARAM_2','STRING[100]','Parametr 2'@
   ,'PARAM_3','STRING[100]','Parametr 3'@
   ,'PARAM_4','STRING[100]','Parametr 4'@
   ,'PARAM_5','STRING[100]','Parametr 5'@
);

_formula:="exec('variant_select','#ntc',fld())";
_tab.fld_fml('VARIANT','F3',_formula);
_formula:="exec('icon_select','#ntc',fld())";
_tab.fld_fml('ICON','F3',_formula);

_tab.USER:=_in.USER;
_tab.memo_set(_in.USERS,'USERS');
_tab.TITLE:=_in.TITLE;
_tab.memo_set(_in.DESC,'DESC');
_tab.VARIANT:=_in.VARIANT;
_tab.ICON:=_in.ICON;
_tab.INFO:=_in.INFO;
{! _it:=1..5
|! ($('_a.BUTTON_%1:=_b.BUTTON_%1; _a.ACTION_%1:=_b.ACTION_%1; _a.PARAM_%1:=_b.PARAM_%1'[$_it]))(_tab,_in)
!};
_tab.add();

_edit:=_tab.mk_edit('Powiadomienie'@,,,,,'normal');

_width:=50;
_tab.win_esep(_edit,'Dane podstawowe'@);
_tab.win_efld(_edit,,'USER',,,_width);
_tab.win_efld(_edit,,'USERS',,,_width,-2);
_tab.win_efld(_edit,,'TITLE',,,_width);
_tab.win_efld(_edit,,'DESC',,,_width,-5);
_tab.win_efld(_edit,,'VARIANT',,,_width-3,,,,,,,'F3_button=1');
_tab.win_efld(_edit,,'ICON',,,_width-3,,,,,,,'F3_button=1');
_tab.win_efld(_edit,,'INFO',,,_width);
_tab.win_ecol(_edit);
_tab.win_esep(_edit,'Przyciski'@);
{! _it:=1..5
|! _tab.win_efld(_edit,,'BUTTON_%1'[$_it],,,_width);
   _tab.win_efld(_edit,,'ACTION_%1'[$_it],,,_width)
!};
_tab.win_esep(_edit,'Parametry'@);
{! _it:=1..5
|! _tab.win_efld(_edit,,'PARAM_%1'[$_it],,,_width)
!};

_tab.efld_opt(_edit,'mark=1',,'TITLE');
exec('ok_esc','#window',_tab,_edit);
_tab.win_edit(_edit);

_validate:="
   _tab:=cur_tab(1,1);
   __CHK.record(_tab,,'TITLE')
";

{? _tab.edit(_validate)
||
   _in.USER:=_tab.USER;
   _in.USERS:=_tab.memo_txt(,,'USERS');
   _in.TITLE:=_tab.TITLE;
   _in.DESC:=_tab.memo_txt(,,'DESC');
   _in.VARIANT:=_tab.VARIANT;
   _in.ICON:=_tab.ICON;
   _in.INFO:=_tab.INFO;
   {! _it:=1..5
   |! ($('_a.BUTTON_%1:=_b.BUTTON_%1; _a.ACTION_%1:=_b.ACTION_%1; _a.PARAM_%1:=_b.PARAM_%1'[$_it]))(_in,_tab)
   !};
   1
||
   0
?}


\send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wysłanie powiadomienia w aplikacji
::   WE: _a - parametry wejściowe czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;

_users:='%1,%2'[_in.USERS,_in.USER];

_users:=spli_str(_users,',');

_obj:=obj_new('variant','icon','info');
_obj.variant:=_in.VARIANT;
_obj.icon:=_in.ICON;
_obj.info:=_in.INFO;
_priority:=json_obj(_obj);

{! _it:=1.. obj_len(_users)
|! {? _users[_it]<>''
   || ($'ntc_send(%1,%2,%3,%4,%5,%6,%7,%8,%9,%10,%11,%12,%13,%14,%15,%16,%17,%18,%19,%20,%21,%22,%23,%24)'
      [  '\'%1\''[_users[_it]],
         '\'%1\''[exec('app_ident','#ntc')],
         '',
         '\'%1\''[_in.TITLE],
         '\'%1\''[_in.DESC],
         '',
         '\'%1\''[_priority],
         '',
         '',
         {? _in.BUTTON_1='' || '' || '\'%1\''[_in.BUTTON_1] ?},
         {? _in.ACTION_1='' || '' || '\'%1\''[_in.ACTION_1] ?},
         {? _in.BUTTON_2='' || '' || '\'%1\''[_in.BUTTON_2] ?},
         {? _in.ACTION_2='' || '' || '\'%1\''[_in.ACTION_2] ?},
         {? _in.BUTTON_3='' || '' || '\'%1\''[_in.BUTTON_3] ?},
         {? _in.ACTION_3='' || '' || '\'%1\''[_in.ACTION_3] ?},
         {? _in.BUTTON_4='' || '' || '\'%1\''[_in.BUTTON_4] ?},
         {? _in.ACTION_4='' || '' || '\'%1\''[_in.ACTION_4] ?},
         {? _in.BUTTON_5='' || '' || '\'%1\''[_in.BUTTON_5] ?},
         {? _in.ACTION_5='' || '' || '\'%1\''[_in.ACTION_5] ?},
         {? _in.PARAM_1='' || '' || '\'%1\''[_in.PARAM_5] ?},
         {? _in.PARAM_2='' || '' || '\'%1\''[_in.PARAM_2] ?},
         {? _in.PARAM_3='' || '' || '\'%1\''[_in.PARAM_3] ?},
         {? _in.PARAM_4='' || '' || '\'%1\''[_in.PARAM_4] ?},
         {? _in.PARAM_5='' || '' || '\'%1\''[_in.PARAM_5] ?}
      ])()
   ?}
!};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 5ad6e4981031f789d431cbf8c7b0d113d3706a499a7105e0056bd42b64edf63b23449421dc56be9410b7b8800679e1398a9b1d014d4605a6bc5edc929e85d31311e795a038f6521c35ac3f9062f657f4ea6868715774178abfeb6d3adb79b6c1d49ac77c6d0fa2deff1bd21b392858c045ee3947a956cb0d9be0554f9ab83f97

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zui_abs_sync.fml
:: Utworzony: 19.11.2018
:: Autor: Markus
:: Systemy: merit
::======================================================================================================================
:: Zawartość: Obsługa synchronizacji AbStore
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
:: PARAMETRY WE:
::# kind=WE,   symbol=NAME_CONF, type=STRING,   name=Nazwa konfiguracji,   required=T, fml_val="exec('name_conf','!zui_abs_sync')"
::# kind=WE,   symbol=FUNC, type=STRING,   name=Funkcja do wykonania,   required=T, fml_val="exec('func','!zui_abs_sync')"
:: PARAMETRY WY:
::# kind=WY,   symbol=RESP,      type=NUMBER,   name=Wynik synchronizacji, required=N
:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_lsp_abs_sync:=_mp.pathArea('LSP_ABS');
_area:=_lsp_abs_sync;
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo() | _mp.pathArea() & ~_area;
_grupa:=_mp.isGroup();

_name_conf:='Abstoreb2b';
{? type_of(_in.NAME_CONF)=type_of('')
||
   {? _in.NAME_CONF='Abstoreb2b'
   ||
      _name_conf:='Abstoreb2b'
   ?};
   {? _in.NAME_CONF='Abstoreb2c'
   ||
      _name_conf:='Abstoreb2c'
   ?}
?};

_func:='Synchro';
{? type_of(_in.FUNC)=type_of('')
||
   {? _in.FUNC='Synchro'
   ||
      _func:='Synchro'
   ?};
   {? _in.FUNC='Oferty'
   ||
      _func:='Oferty'
   ?};
   {? _in.FUNC='Magazyny'
   ||
      _func:='Magazyny'
   ?};
   {? _in.FUNC='NoSync'
   ||
      _func:='NoSync'
   ?};
   {? _in.FUNC='Inv'
   ||
      _func:='Inv'
   ?}
?};

_result:=200;

{? _func='Synchro'
||
   _result:=exec('executeSync','abstore', _name_conf)
?};

{? _func='Oferty'
||
   _result:=exec('executeSendOffer','abstore', _name_conf,~_todo);
   {? _result<>200 & _result<>201 || _result:=325 ?}
?};

{? _func='NoSync'
||
   _result:=exec('executeNoSync','abstore', _name_conf)
?};

{? _func='Magazyny'
||
   exec('setupAbstoreb2b','abstore');
   _result:=exec('send_store','abstore');
   {? _result<>200 & _result<>201 || _result:=326 ?}
?};
{? _func='Inv'
||
   _result:=exec('execSendInv','abstore')
?};

_out.RESP:=_result;

_mp.save(,_out);

_mp.done()
::_mp.cancel()
::_mp.error('Nieobsługiwana ścieżka.'@)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
'Synchronizacja AbStore'@@


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_wyn:=1;

_wyn


\name_conf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła na wartość parametru NAME_CONF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Którą konfigurację zastosować?\nDomyślnie zostanie przypisana Abstoreb2b.'@
   ,,'Abstoreb2&b','Abstoreb2&c');
{? _choice=1 || 'Abstoreb2b'
|? _choice=2 || 'Abstoreb2c'
|| 'Abstoreb2b'
?}


\func
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Formuła na wartość parametru FUNC
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Funkcja do wykonania: '@
   ,,'Oferty','Magazyny','NoSync','Inv','Synchro');
{? _choice=0 || 'Synchro'
|? _choice=1 || 'Oferty'
|? _choice=2 || 'Magazyny'
|? _choice=3 || 'NoSync'
|? _choice=4 || 'Inv'
|| 'Synchro'
?}


\merge_param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Formuła łączy parametry z tej samej czynności do jednej
::   WE: _a - 0 (domyślnie) / 1 - czy wyświetlać komunikat o błędzie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_param:=params_get().in;
_mess:={? _>=1 & type_of(_a)=1 || _a || 0 ?};
_result:='OK';

_code:=200;
{? type_of(_param.p01)=1 || _code:=_param.p01
|? type_of(_param.p02)=1 || _code:=_param.p02
|? var_pres('p03',_param)>0 & type_of(_param.p03)=1 || _code:=_param.p03
?};

{? _code<>200 & _code<>201
||
::Problem z przesyłem
   _msg:='';
:błedy z abstore\executeSync
   {? _code=301 || _msg:='jednostek miar'@
   |? _code=302 || _msg:='grup towarowych'@
   |? _code=303 || _msg:='etykiet materiałów'@
   |? _code=304 || _msg:='materiałów'@
   |? _code=305 || _msg:='powiązań materiał-etykieta'@
   |? _code=306 || _msg:='zdjęć materiałów'@
   |? _code=307 || _msg:='załączników do materiałów'@
   |? _code=308 || _msg:='cenników ogólnych'@
   |? _code=309 || _msg:='stanów magazynowych (wersja wielomogazynowa)'@
   |? _code=310 || _msg:='stanów magazynowych materiałów zmodyfikowanych'@
   |? _code=311 || _msg:='komunikatów aktywacji produktu'@
   |? _code=312 || _msg:='rozrachunków'@
   |? _code=313 || _msg:='aktualizacji statusu zamówienia'@
   |? _code=314 || _msg:='nowo dodanych faktur'@
   |? _code=315 || _msg:='nowo dodanych faktur'@
   |? _code=316 || _msg:='grup kontrahentów'@
   |? _code=317 || _msg:='kontrahentów'@
   |? _code=318 || _msg:='cenników zmodyfikowanych kontrahentów'@
   |? _code=319 || _msg:='oddziałów kontrahenta'@
   |? _code=320 || _msg:='powiązań oddziału z magazynem'@
   |? _code=321 || _msg:='użytkowników Merit'@
   |? _code=322 || _msg:='powiązań klienta z oddziałami'@
   |? _code=323 || _msg:='cenników grupowych'@
   |? _code=324 || _msg:='cenników indywidualnych kontrahentów'@
:błedy z metody main
   |? _code=325 || _msg:='ofert'@
   |? _code=326 || _msg:='informacji o magazynach'@
   ?};

   _result:='Problem dotyczy synchronizacji <strong>%1</strong>.'@[_msg];

   {? _mess ||
::      FUN.info('Błąd synchronizacji z AbStore - szczegóły w tabeli WSLOG.'@)
      FUN.info(_result)
   ?}
?};

_result


\test_email
'Treść maila do wysłania.'


\content_email
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [20.14]
:: OPIS: Formuła łączy parametry z tej samej czynności do jednej
::   WE: _a - string - treść komunikatu o błędzie
::   WY: treść email
::----------------------------------------------------------------------------------------------------------------------
_mail:='<p>Wystąpił błąd w integracji z systemem AbStore.</p>';
_mail+={? var_pres('_a')=2 || '<p>' + _a + '</p>' || '' ?};
_mail+='<p>Więcej informacji dotyczące błędu można znaleźć w logach.</p>';
_mail


:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 32b875b49d0b95167e747bb3fbffd647989a0f6857919f7f8d9deae8a2e37be8fc71e5807a5249b15001086348fb53091b01e915f22e74457959cc916c700764e4bfa8534e5a9e64b6c7d8c70f204b231cfe2863e41622b3034ef5863614462f8a10467c4ccaa324d56be4aab3ee7f2a0010ec965568eb69ebcff847319546c4

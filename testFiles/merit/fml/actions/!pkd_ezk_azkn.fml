:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_azkn.fml
:: Utworzony: 09.12.2019
:: Autor: IS
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_AZKN - Powiadomienie o umowach z zakazem konkurencji.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Powiadomienie o umowach z zakazem konkurencji - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE,~USER
::# permissions=F_ZATR,UD_SKL
::
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
::
:: Czynność sprawdza, czy dany pracownik ma zarejestrowane umowy o zakazie konkurencji i generuje powiadomienie do
:: użytkowników, którzy mają uprawnienia do roli obsługującej w procesie daną czynność lub ich adres został wpisany
:: do parametru EMLADR. Czynność sprawdza czy dany pracownik ma zarejestrowane:
:: • umowy o zakazie konkurencji w aplikacji (Zdarzenia kadrowe -> Umowy lojalnościowe), których data zakończenia
::   jeszcze nie upłynęła oraz zarejestrowane
:: • załączniki w dokumentacji pracowniczej typu AktOsobB - s "Umowy o zakazie konkurencji po ustaniu stosunku pracy,
:    jeżeli strony zawarły takie umowy (art. 101² § 1 Kp)"
:: • załączniki w dokumentacji pracowniczej typu AktOsobC - f "Umowy o zakazie konkurencji, jeżeli strony zawarły
:    taką umowę w okresie pozostawania w stosunku pracy (art. 101¹ § 1 Kp)
::
:: Jeżeli pracownik ma takie załączniki bądź umowy podpisane w trakcie bądź po ustaniu stosunku pracy,
:: czynność przygotowuje treść powiadomienia.
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie na pracownika, required=T
::
:: Parametr wejściowy EMLADR pozwala ręcznie określić odbiorcę wiadomości przed wywołaniem czynności. Jeżeli parametr
:: zostanie podany czynność przekaże go jako listę odbiorców w parametrze wyjsciowym RCV. Jeżeli parametr EMLADR
:: nie zostanie podany czynność zbierze adresy e-mail użytkowników mających odpowiednią rolę w celu uruchomienia
:: czynności.
::# kind=WE, symbol=EMLADR, type=STRING, name=Adres e-mail odbiorcy, required=N
::
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
::
:: Wynik działania czynności.
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (ZAKAZ,BRAK,BŁĄD)", required=N
::
::# kind=WY, symbol=SUB, type=STRING, name=Temat, required=N
::# kind=WY, symbol=RCV, type=MEMO, name=Lista odbiorców, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
params_set(_par:=params_get());
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;

P.cntx_psh();
P.clear();
{? P.seek(_in.P)
|| _ret:=exec('run','!pkd_ezk_azkn',_in,_mp.buf_act.UID);

   _out.SUB:='Powiadomienie o zwolnieniu pracownika z umową o zakazie konkurencji'@;
   _out.RESULT:=_ret.RESULT;
   _out.RCV:=_ret.RCV;
   _out.BODYH:=_ret.BODYH
|| _out.RESULT:='BŁĄD'
?};
P.cntx_pop();
_mp.save(,_out);
_mp.done();

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Powiadomienie o umowach z zakazem konkurencji - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Powiadomienie o umowach z zakazem konkurencji'@@


\run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [20.14]
:: OPIS: Formuła odpowiedzialna za przetwarzanie danych. Formuła sprawdza istnienie odpowiednich załączników dla
::       podanego pracownika. W przypdaku znalezienia odpowiednich załączników generuje treść powiadomienia.
::   WE: _a [OBJ] - parametry wejściowe czynności (in)
::       _b [STRING] - uid czynności
::   WY: _ret [OBJ]  - tablica elementów nazwanych ('RESULT','RCV','BODYH')
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_prac:=_in.P;
_actUid:=_b;

_ret:=obj_new('RESULT','RCV','BODYH');
_ret.RESULT:='BRAK';
_ret.RCV:=_ret.BODYH:='';

_zal:=obj_new(4);
:: Umowy o zakazie konkurencji po ustaniu stosunku pracy, jeżeli strony zawarły takie umowy (art. 101² § 1 Kp):
_zal[1]:='s'; _zal[2]:='AktOsobB';
:: Umowy o zakazie konkurencji, jeżeli strony zawarły taką umowę w okresie
:: pozostawania w stosunku pracy (art. 101¹ § 1 Kp):
_zal[3]:='f'; _zal[4]:='AktOsobC';

:: Sprawdzenie załączników pracownika:
_pZal:=exec('p_zal_chk','personel_alerty',_prac,_zal);

::Sprawdzenie umów o zakazie konkurencji (tabela UMLOJN):
_umZak:=exec('um_zak_kon_chk','personel_alerty',_prac,'Z');

{? _pZal.size() | _umZak.size()
|| _ret.RESULT:='ZAKAZ';
   P.OSOBA();
:: Pobranie listy adresatów:
   {? var_pres('EMLADR',_in)<>type_of('') | +_in.EMLADR<5
   || _ret.RCV:=exec('emls_w_perm','personel_alerty',_actUid)
   || _ret.RCV:=_in.EMLADR
   ?};

:: Tworzenie treści wiadomości:
   _th:="'<th [[STYLE_TABLE_TH]]>'+_a+'</th>'";
   _td:="'<td [[STYLE_TABLE_TD]]>'+_a+'</td>'";

   _ret.BODYH:=
      '<h3>'+
      'Współpracownik - %1 z podpisaną umową o zakazie konkurencji'@ [(OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO)] +
      '</h3>\n'+

      '<table [[STYLE_TABLE]]>\n'
      '<tr [[STYLE_TABLE_TR]]>'+
         _th('Nazwisko'@)+
         _th('Imię'@)+
         _th('PESEL'@)+
         _th('Nr teczki'@)+
         _th('Data zatrudnienia'@)+
         _th('Data zwolnienia'@)+
         _th('Jednostka organizacyjna'@)+
      '</tr>\n';

   _ret.BODYH+=
      '<tr [[STYLE_TABLE_TR]]>'+
         _td(OSOBA.NAZWISKO)+
         _td(OSOBA.PIERWSZE)+
         _td(OSOBA.PESEL)+
         _td(P.T,'wiersz2')+
         _td(P.DZA$1)+
         _td(P.DZ$1)+
         _td(P.WYDZIAL().SYMBOL)+
      '</tr>\n'
      '</table>\n';

   {? _pZal.first()
   || _ret.BODYH+=
         '<h4>'+
         'Załączniki o zakazie konkurencji - %1'@ [(OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO)] +
         '</h4>\n'+

         '<table [[STYLE_TABLE]]>\n'
         '<tr [[STYLE_TABLE_TR]]>'+
            _th('Lp.'@)+
            _th('Typ załącznika'@)+
            _th('Data załączenia'@)+
            _th('Nazwa pliku załącznika'@)+
         '</tr>\n';

::    Wypisanie wszystkich uwzględnionych załączników pracownika:
      _lp:=0;
      {!
      |? _lp+=1;
         _ret.BODYH+=
            '<tr [[STYLE_TABLE_TR]]>'+
               _td($_lp)+
               _td(_pZal.TYP)+
               _td(_pZal.DATA$1)+
               _td(_pZal.NAZWA)+
            '</tr>\n';
         _pZal.next()
      !};
      _ret.BODYH+=
      '</table>\n'
   ?};

   {? _umZak.first()
   || _ret.BODYH+=
         '<h4>'+
         'Umowy o zakazie konkurencji - %1'@ [(OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO)] +
         '</h4>\n'+

         '<table [[STYLE_TABLE]]>\n'
         '<tr [[STYLE_TABLE_TR]]>'+
            _th('Lp.'@,'head2')+
            _th('Numer umowy'@)+
            _th('Data zawarcia'@)+
            _th('Umowa po ustaniu stosunku pracy'@)+
         '</tr>\n';

::    Wypisanie wszystkich uwzględnionych umów o zakazie konkurecji pracownika:
      _lp:=0;
      {!
      |? _lp+=1;
         _ret.BODYH+=
            '<tr [[STYLE_TABLE_TR]]>'+
               _td($_lp,'wiersz2')+
               _td(_umZak.NUMER)+
               _td(_umZak.DATAZAW$1)+
               _td(_umZak.PO)+
            '</tr>\n';
         _umZak.next()
      !};
      _ret.BODYH+=
      '</table>\n'
   ?};

   _ret.BODYH+=
      '<p>'+'Ta wiadomość została wygenerowana automatycznie - prosimy na nią nie odpowiadać.'@+'</p>'
?};

_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 21683bc7018ce174386efd89651c975f659d6a6bbfcf09f76af5052788953e5368067e90f8e2feef8a38f9ad8b888a17109de7b38eceed99133a7679253d35e27dce06d67e5f90b97ce2339a3ceac1ef6cad0ee9fafd1c3932cb5dbcb75b802121702fc792a441f1f5706e6477e4b595bc12475dcaeb0c606a4c553cf24b9144

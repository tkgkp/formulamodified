:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ltr_tra_rzle.fml
:: Utworzony: 03.09.2019
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły czynności LTR_TRA_RZLE - Realizacja transportu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Formuła główna czynności LTR_TRA_RZLE
::   WE: _a - [obj_new] - parametry wejsciowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=TR_RODZ
::# properties=SERVICE
::# kind=WE,   symbol=TR_NAG,   type=_TR_NAG,  name=Transport,                 required=N
::# kind=WY,   symbol=TR_NAG,   type=_TR_NAG,  name=Transport,                 required=N, keyref=T

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_area:=_mp.pathArea('LTR_ZLE');
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo() | (_mp.pathArea() & ~_area);
_auto:=_mp.isAutoRun();
_service:=_mp.isService();

_tr_nag:={? var_pres('TR_NAG',_in)=type_of(null()) & _in.TR_NAG
         || _in.TR_NAG
         |? var_pres('TR_NAG',_out)=type_of(null()) & _out.TR_NAG
         || _out.TR_NAG
         || null()
         ?};
:: Sprawdzenie parametrów pracy dla czynności startowej
::{? _mp.pathProc()
::|| {? ST.AR=0 || FUN.info('Ustaw parametry pracy.'@); _wyn:=0; return() ?}
::?};

exec('init','ltr');

:: Ustawienie kontekstu wg instancji elementu w procesie
{? _mp.pathTodo()
|| {? var_pres('TR_NAG',_out)=type_of(null()) & _out.TR_NAG
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego TR_NAG.')
   ?}
?};

:: Sprawdzenie uprawnień
params_set('in',_in,'user',OPERATOR.USER,'mp',_mp);
{? exec('permissions','!ltr_tra_rzle')=0
|| _mp.error('Brak uprawnień do uruchomienia czynności.'@);
   return()
?};

_mp.trigRef('TR_NAG',,1,,exec('kind_out','#b_port'),'TR_NAG');

{? _service & _tr_nag
||
:: uruchomione w serwisie
   {? ref_name(_tr_nag)*'____'
   || exec('opentr_psh','open_tab');
      exec('opentr','open_tab','____');
      TR_NAG.prefix();
      {? TR_NAG.seek(_tr_nag) & exec('tr_nag_end','!ltr_tra_rzle',1) || _mp.done() ?};
      exec('opentr_pop','open_tab')
   || _mp.done()
   ?}

|? _akcja='Zakończ_wer' & _area
||
:: uruchomione z poziomu obszaru
   {? exec('tr_nag_end','!ltr_tra_rzle') || _mp.done() ?}

|? _akcja='Realizuj'
   | _todo
|| {? _tr_nag<>null()
:: Uruchomione w procesie
   || TR_NAG.cntx_psh();
      TR_NAG.prefix();
      {? TR_NAG.seek(_tr_nag)
      || {? _todo | TR_NAG.STAT_REJ='Z'
         || {? exec('tr_nag_end','!ltr_tra_rzle',_auto) || _mp.done() ?}
         || FUN.info('Transport nie podlega edycji.'@);
            _mp.done()
         ?}
      ?};
      TR_NAG.cntx_pop()
   |? _todo & _tr_nag=null()
:: Uruchomione zadanie z listy todo i brak _out.TR_NAG wtedy zadanie wycofujemy
   || _mp.cancel()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || {? exec('tr_nag_end','!ltr_tra_rzle') || _mp.done() ?}
   ?}
||
   _mp.error('Nieobsługiwana ścieżka.'@)
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Opis czynności LTR_TRA_DZLE
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));
_in:=_mp.load(exec('kind_in','#b_port'));

_tr_nag:={? var_pres('TR_NAG',_out)=type_of(null()) & _out.TR_NAG
         || _out.TR_NAG
         |? var_pres('TR_NAG',_in)=type_of(null()) & _in.TR_NAG
         || _in.TR_NAG
         || null()
         ?};

{? _tr_nag<>null()
|| 'Zrealizuj transport: %1'@@[exec('record','#to_string',_tr_nag)]
|| 'Zrealizuj transport'@@
?}


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;
_wyn:=1;
_wyn


\tr_nag_endreal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Zakończenie realizacji transportu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LTR_TRA_RZLE';
_params.AKCJA:='Zakończ_wer';
_params.UIDREF:=TR_NAG.uidref();
exec('mp_run','#b__box',_params);
~~


\tr_nag_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: zakończenie realizacji transportu
::   WE: [_a] - 0-z pytaniami(domyślnie) 1-bez
::   WY: 1-zakończono rejestrację 0-nie
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(0) || _a || 0 ?};

_wyn:=0;
_fld:='';
TR_NAG.get();
TR_NAG.cntx_psh();
TR_NZL.cntx_psh();
TR_LOG.cntx_psh();
exec('ini_kom','#message','Informacje o zakończeniu realizacji transportu.'@);
__kom_gr:='Transport nr: %1'@[TR_NAG.SYM];
{? exec('tr_nag_lock','transport') & TR_NAG.r_lock(1,1,1)
||
   {? ~_auto & ~exec('tr_rodz_ope','transport_wspolne',TR_NAG.TR_RODZ,'Transport'@,'Zakończenie realizacji niemożliwe.'@,1)
   || _ok:=0
   |? TR_NAG.STAT_REJ='Z'
   || _ok:={? _auto || fabs(exec('ENDreaTran','transport',TR_NAG.ref()))=1 || 1 ?};

      {? ~_auto || exec('tr_nagend_win_edit_set','!ltr_tra_rzle') ?};

      {? _ok & (_auto | TR_NAG.edit("__CHK.record(TR_NAG,,'DZ')"))
      || TR_NAG.prefix();
         TR_NAG.STAT_REJ:='T';
         {? TR_NAG.DZ=date(0,0,0)
         || TR_NAG.DZ:=date();
            TR_NAG.GZ:=time()
         ?};
         {? TR_NAG.put()
         || exec('refreshTR_NZL','transport',TR_NAG.ref());
            exec('statTR_NAG','transport',TR_NAG.ref());
            TR_NAG.get();
            _wyn:=1;
            _auto:=1;
            exec('add_kom','#message','Zakończono realizację transportu'@,1)
         ?}
      ?}
   || exec('add_kom','#message'
        ,{? TR_NAG.STAT_REJ='T'
         || 'Transport ma status zakończenia realizacji'@
         || 'Nieprawidłowy status rejestracji transportu'@
         ?},2);
      _wyn:=1
   ?};
   TR_NAG.r_unlock()
?};
{? _wyn & _auto & var_pres('__kom_on')=type_of(1)
||
   __kom_on:=0
?};
exec('end_kom','#message');
TR_NAG.cntx_pop();
TR_NZL.cntx_pop();
TR_LOG.cntx_pop();
TR_NAG.get();
_wyn


\tr_nagend_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Ustawia okno redakcji tabeli TR_NAG, wymagane pola, okna słowników
::----------------------------------------------------------------------------------------------------------------------
_win_red:=exec('tr_nag_win_edit','transport','REA');
{? TR_NAG.STAT_REJ='Z'
|| _ff:="params_exec('tr_nagend_zakoncz_red','!ltr_tra_rzle')";
         TR_NAG.win_ebtn(_win_red
           ,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Zakończ realizację'@],_ff);
   exec('ok_esc','#window',TR_NAG,_win_red,1,,,0)
?};
TR_NAG.win_edit(_win_red);
~~


\tr_nagend_zakoncz_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Transport - Zakończ realizację - okno redagowania
::   WE: params_get()
::   WY: 'key:F2' - jeśli pola nagłówka zamówienia sprzedaży prawidłowo wypełnione
::       'edit:ACRONYM' - akronim niewłaściwie wypełnionego pola nagłówka zamówienia sprzedaży
::       '' - wartość domyślna jeśli main nie zmieni na powyższe
::----------------------------------------------------------------------------------------------------------------------
_btnRuleResult:='';

_fld:=__CHK.record(TR_NAG,,'DZ');
{? _fld<>''
|| _btnRuleResult:='edit:'+_fld
|| _btnRuleResult:='key:F2'
?};

_btnRuleResult


\tr_nzl_zakoncz_tr_zl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Dyspozycja transportu - Zakończ - okno redagowania
::   WE: params_get()
::   WY: 'key:F2' - jeśli pola nagłówka dyspozycji transportu prawidłowo wypełnione
::       'edit:ACRONYM' - akronim niewłaściwie wypełnionego pola nagłówka dyspozycji transportu
::       '' - wartość domyślna jeśli main nie zmieni na powyższe
::----------------------------------------------------------------------------------------------------------------------
_act_uid:='LTR_TRA_RZLE';
exec('btn_EndPosition','okienka',_act_uid,TR_NZL.uidref(),TR_NZL
 ,'Zakończ_wer','Zakończyć realizację dyspozycji w magazynie %1?'@[TR_NZL.SYM])

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 2059e54ddcf190bc5ff177e616a0e833986ebe0c50417cb56e7dcd048518132ff671816ac4943a7735af6c5b9b4d87db60dd0ccd919ac862ffd21bb933fbf80b6905db0f5570ec20dbc48e52b8de1be1ba45989c69835e987900834f381e3e7d26f98c8148b88cf754ac988c24fdd20679b093c27ba175ad8d62bfc6ebcf334e

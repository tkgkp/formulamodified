:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_fdl_gpla.fml
:: Utworzony: 06.04.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FDL_GPLA - Ustawianie znacznika, że delegacja jest gotowa do uwzględnienia w płacach
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Ustawianie znacznika, że delegacja jest gotowa do płac - główna formuła czynności OBE_FDL_GPLA
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_edokum','obiegi')

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N, keyref=T
::# kind=WE, symbol=TYP, type=STRING, name=Delegacja gotowa/Niegotowa do płac, required=N, fml_val="exec('typ','!obe_fdl_gpla')"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_add:=(var_press('TYP',_we)<=0 | _we.TYP='Gotowa');
_edok:=null;
{? var_press('EDOKUM',_we)>0
|| _name:=ref_name(_we.EDOKUM);
   EDOKUM.cntx_psh(); EDOKUM.use(_name); EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM,_name,1) || _edok:=EDOKUM.ref(); EDOKUM.GPL:=_add; EDOKUM.put() ?};
   EDOKUM.cntx_pop()
?};
_wy.EDOKUM:=_edok;
_mp.save(,_wy);
_mp.done();
~~


\typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Ustawia parametr wejsciowy TYP czynności
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Delegacja gotowa\\niegotowa do uwzglednienia w płacach'@,1,
                    'Gotowa'@,'Niegotowa'@);
{? _choice
|| {? _choice=1
   || 'Gotowa'
   |? _choice=2
   || 'Niegotowa'
   ?}
|| ~~
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_press('TYP',_we)>0
|| _desc:='Delegacja %1 do płac'@@[-_we.TYP]
|| _desc:='Delegacja do płac'@@
?};
{? var_pres('EDOKUM',_we)>0 & _we.EDOKUM
|| EDOKUM.cntx_psh(); EDOKUM.use(ref_name(_we.EDOKUM)); EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM,)
   || {? var_press('TYP',_we)>0
      || _desc:='Delegacja %1 do płac: %2 data polecenia %3, delegowany %4 %5'@@[-_we.TYP,EDOKUM.ID,$EDOKUM.DATAW,EDOKUM.DOSTAWCA().NAZWISKO,OSOBA.PIERWSZE]
      || _desc:='Delegacja do płac: %1 data polecenia %2, delegowany %3 %4'@@[EDOKUM.ID,$EDOKUM.DATAW,EDOKUM.DOSTAWCA().NAZWISKO,OSOBA.PIERWSZE]
      ?}
   ?};
   EDOKUM.cntx_pop()
?};
_desc

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 8b6f4650adb9379c2c3f3cc90edbe2bc50392b6e3359b51048cf1f85799ae12b7bdb2acd0b3e946ddabb4b89f72b8384879816b7800a58f9e6d307307684eb438a87124d7a0d02f88618076d2a2431bd2a43c1efc7b80d0a1bd9cad86f7d50aabfce13b77da82acef4ff4fa5bf493155c39978f59c88114a929e7c3f582a2794

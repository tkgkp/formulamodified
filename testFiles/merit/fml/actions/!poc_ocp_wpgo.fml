:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !poc_ocp_wpgo.fml
:: Utworzony: 07.08.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności POC_OCP_WPGO - Wybór programu ocen.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wybór programu ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Parametr wejściowy ON_ESC określa sposób działania akcji wyboru programu ocen przy braku wyboru.
:: Parametr może przyjmować wartości:
::    DONE     - Czynność zostanie zakończona, a parametry wyjściowe przyjmą wartości puste [DOMYŚLNIE].
::    KEEP     - Czynność zostanie odłożona na listę zadań aby użytkownik mógł do niej wrócić. Oznacza to, że czynności
::               nie da się zakończyć bez wyboru programu ocen.
::    CANCEL   - Jeśli czynność jest pierwszą w procesie to po zakończeniu formuły głównej instancja czynności zostanie
::               usunięta. Jeśli czynność jest kolejną w procesie to otrzyma status oczekująca i pozostanie na liście
::               zadań - czyli tak jak dla KEEP.
::    ERROR    - Czynność jest kończona a proces zatrzymywany z ustawioną flagą błędu.
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy rezygnacji, required=N, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'DONE' || _a ?})"
::
:: Parametry wejściowe: AKTYWNY i WIDOCZNY umożliwiają filtrowanie prezentowanego zakresu programu ocen.
::# kind=WE, symbol=AKTYWNY, type=STRING, name=Tylko aktywne programy (T/N), required=N, fml_val="exec('edit_boolean','#edit',_a,'Tylko aktywne programy')"
::# kind=WE, symbol=WIDOCZNY, type=STRING, name=Tylko widoczne programy (T/N), required=N, fml_val="exec('edit_boolean','#edit',_a,'Tylko widoczne programy')"
::
::# kind=WY, symbol=ZO_PROG, type=_ZO_PROG, name=Wskazanie programu ocen, required=N
::
_par:=params_get();
_mp:=_par.mp;

{? _mp.pathProc() | _mp.pathTodo()
|| _in:=_par.in;
   _out:=_par.out;

   _args:=obj_new('AKTYWNY','WIDOCZNY');
   _args.AKTYWNY:={? _in.AKTYWNY=~~ | (_in.AKTYWNY<>'T' & _in.AKTYWNY<>'N') || '' || _in.AKTYWNY ?};
   _args.WIDOCZNY:={? _in.WIDOCZNY=~~ | (_in.WIDOCZNY<>'T' & _in.WIDOCZNY<>'N') || '' || _in.WIDOCZNY ?};

   _ret:=exec('select','!poc_ocp_wpgo',_args);

   _out.ZO_PROG:=_ret.ZO_PROG;

   {? _ret.ret
   || _mp.save(,_out);
      _mp.done()

   |? _in.ON_ESC='KEEP'
   || _mp.keep()

   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()

   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')

   || _mp.save(,_out);
      _mp.done()
   ?}
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wybór programu ocen - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wybierz program ocen'@@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Właściwy wybór programu ocen.
::   WE: _a [ARRAY] - Tablica z parametrami:
::          AKTYWNY  [STRING] - Prezentować tylko programy aktywne? [T/N]
::          WIDOCZNY [STRING] - Prezentować tylko programy widoczne? [T/N]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_args:=_a;

_ret:=obj_new('ret','ZO_PROG');

_cfg:=exec('cfg','!poc_ocp_wpgo');
params_set('cfg',_cfg);

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROG.f_set('NAZWA',,
   '"ZO_PROG".FIRMA=:_a and "ZO_PROG".TYP=\':_b\''+
   {? _args.AKTYWNY=''
   || ''
   || ' and "ZO_PROG".AKTYWNY=\'%1\'' [_args.AKTYWNY]
   ?}+
   {? _args.WIDOCZNY=''
   || ''
   || ' and "ZO_PROG".WIDOCZNY=\'%1\'' [_args.WIDOCZNY]
   ?},
   exec('ref_firma','ustawienia'),ZZ_POM.MODUL
);
ZO_PROG.win_sel(_cfg.window);

{? _ret.ret:=ZO_PROG.select()
|| _ret.ZO_PROG:=ZO_PROG.ref()
?};

ZO_PROG.cntx_pop();

_ret


\cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła konfigurująca widok.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=obj_new('window','panel','ZO_PROG','ZO_PROF','ZF_WYK');

_cfg.panel:='zakładki';

_cfg.ZO_PROG:=obj_new('ws');
_cfg.ZO_PROG.ws:='WYBO';

_cfg.ZO_PROF:=obj_new('ws');
_cfg.ZO_PROF.ws:='WEO';

_cfg.ZF_WYK:=obj_new('ws');
_cfg.ZF_WYK.ws:='WEO';

_mode:='maximized';
_wnd:=ZO_PROG.grp_make('Oceny pracownicze'@,
   "  ZO_PROF.cntx_psh();
      ZO_PROF.f_clear();
      ZO_PROF.index('NAZWA');
      ZF_WYK.cntx_psh();
      ZF_WYK.f_clear();
      ZF_WYK.index('NAZWA');
      ~~
   ",
   'poc_ocp_wpgo',,,
   "  ZF_WYK.cntx_pop();
      ZO_PROF.cntx_pop();
      1
   ",,_mode);
ZO_PROG.grp_sel(_wnd,ZO_PROG,_cfg.ZO_PROG.ws,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      grp_disp(ZO_PROF,_cfg.ZO_PROF.ws);
      grp_disp(ZF_WYK,_cfg.ZF_WYK.ws)
   ",,,
   6,,,,,
   'maximized_with_title'
);
ZO_PROG.grp_splt(_wnd,,'horizontal',_cfg.panel,15);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

ZO_PROG.grp_sel(_wnd,ZO_PROF,_cfg.ZO_PROF.ws,'Profile'@,,,,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? grp_empty(ZO_PROG,_cfg.ZO_PROG.ws)
      || '#disable'
      || ZO_PROF.prefix(ZO_PROG.ref())
      ?}
   ",
   ,,,
   _mode
);

ZO_PROG.grp_sel(_wnd,ZF_WYK,_cfg.ZF_WYK.ws,'Formularze'@,,,,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? grp_empty(ZO_PROG,_cfg.ZO_PROG.ws)
      || '#disable'
      || ZF_WYK.prefix(ZO_PROG.NP_DOK,ZO_PROG.ZZ_DOK)
      ?}
   ",,,,
   _mode
);

_cfg.window:=_wnd;
_cfg

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:18 08804010b9b2163e012bf2cf7974e245b44cdaa6548d637907a165886f8686494ee7fcfc5a242a06479489636ad03ee57c43db6907dfdaa9f5827b70143f9259c359a908178728c99882ac2d72568001bfc0760f83b560f76bb7732b963f5cdca109173df002215887a37d3016cfd7a6a2f0cfdecddc4f8e888fb4701e84de9a

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_desn.fml
:: Utworzony: 09.10.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DESN - Rejestracja środka trwałego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Rejestracja środka trwałego - główna formuła czynności FST_EWI_DESN.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# access=exec('uprawnienia','!fst_ewi_desn')
:: PARAMETRY WE:
::# kind=WE, symbol=EL, type=STRING, name=Czy element, required=N
::# kind=WE, symbol=ZESTAW, type=_SRSR, name=Wskazanie na zestaw, required=N
::# kind=WE, symbol=DOK, type=_DOK, name=Wskazanie na dokument, required=N
::# kind=WE, symbol=SRSR, type=_SRSR, name=Wskazanie na środek, required=N, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=SRSR, type=_SRSR, name=Wskazanie na środek trwały, required=T
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na dokument, required=N
::# kind=WY, symbol=SRSR_E, type=_SRSR, name=Wskazanie na element zestawu, required=N
::
{? _=0 || _a:='' ?};

_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=-_mp.akcja();

{? ~exec('okr_mod','fst')
|| {? _mp.pathProc() || _mp.cancel() ?};
   return(0)
?};

{? var_pres('EL',_we) & _we.EL='T' || _a:='E' ?};

_srsr_rf:=null;
EDIT_ES.NAB_GRP:='N';
EDIT_ES.ORG_REF:='';
EDIT_ES.SRC_REF:='';
EDIT_ES.PRCNAZ:='';

VAR_DEL.delete('__doktosrsr');
{? var_pres('DOK',_we)>0 & _we.DOK<>null || __doktosrsr:=_wy.DOK:=_we.DOK ?};

{? _mp.pathProc() | _mp.pathTodo() |
   (_mp.pathArea() & (_akcja='dołącz' | _akcja='fkstofst' | _akcja='dołącz z grupy') & _a='')
|| {? _mp.pathProc() | _akcja='fkstofst'
   || exec('init','fst');
      SRD.maski('r');
      STER.KST:=exec('cur_krst','fst');
      {? FINFO.SPR_GRP='T' || EDIT_ES.win_edit('NEW_S') || EDIT_ES.win_edit('NEW') ?};
      EDIT_ES.hdr_edit();
      EDIT_ES.hdr_edit(' trwałego'@);
      EDIT_ES.GRP:='N';
      EDIT_ES.ZESTAW:=null;
      EDIT_ES.efld_opt('NEW','enable=0',,'ZESTAW');
      _mp.trigRef('SRSR');
      _win:=exec('srsr_win','fst');
      SRSR.win_dict(_win);
      _loop:=0;
      {! |?
         exec('filtr_zestawy','fst','T');
         {? EDIT_ES.edit("exec('chk_srsr_proc','fst')")
         || SRSR.f_clear();
:: obsługa nabycia wewnątrz grupy kapitałowej
            {? EDIT_ES.NAB_GRP='T'
            || {? EDIT_ES.GRP='T'
               || FUN.emsg('System nie umożliwia zbycia i nabycia zestawów w obrębie grupy kapitałowej.'@);
                  _loop:=1
               |? EDIT_ES.GRP='E'
               || FUN.emsg('System nie umożliwia zbycia i nabycia elementów zestawów w obrębie grupy kapitałowej.'@);
                  _loop:=1
               || {? ~exec('srs_sel','fst',EDIT_ES.GRP) || _loop:=1 || _loop:=0; _dalej:=1 ?}
               ?}
            || _loop:=0; _dalej:=1
            ?}
         || _loop:=0; _dalej:=0
         ?};
         _loop
      !};
      {? _dalej
      || EDIT_ES.R:='T';
         {? EDIT_ES.GRP='N'
         || _srsr_rf:=SRD.addAsset()
         |? EDIT_ES.GRP='T'
         || _srsr_rf:=EDIT_ES.SRSR:=SRD.addAsset('G')
         |? EDIT_ES.GRP='E'
         || SRSR.prefix();
            EDIT_ES.SRSR:=EDIT_ES.ZESTAW;
            EDIT_ES.SRSR();
            EDIT_ES.Z_NRI:=EDIT_ES.SRSR().NRI;
            EDIT_ES.Z_NST:=EDIT_ES.SRSR().NST;
            _srsr_rf:=SRD.addAsset('E')
         ?}
      || SRSR.f_clear()
      ?};
      {? _srsr_rf
      || _wy.SRSR:=_srsr_rf;
         _mp.save(,_wy);
         _mp.done()
      || _mp.cancel()
      ?}
   |? _mp.pathTodo() & var_pres('EL',_we) & _we.EL='T'
   || exec('init','fst');
      SRSR.prefix();
      _srsr_re:=null;
      {? var_pres('ZESTAW',_we) & _we.ZESTAW<>null || EDIT_ES.SRSR:=_we.ZESTAW || EDIT_ES.SRSR:=null ?};
      {? EDIT_ES.SRSR & SRSR.seek(EDIT_ES.SRSR,SRSR.name())
      || EDIT_ES.Z_NRI:=EDIT_ES.SRSR().NRI;
         EDIT_ES.Z_NST:=EDIT_ES.SRSR().NST;
         EDIT_ES.R:='T';
         _srsr_re:=SRD.addAsset('E')
      ?};
      {? _srsr_re
      || _wy.SRSR:=_we.ZESTAW;
         _wy.SRSR_E:=_srsr_re;
         SRSR.prefix();
::         {? SRSR.seek(_srsr_re) || _wy.R:=SRSR.R ?};
         _mp.save(,_wy);
         _mp.done()
      ||
         {? _srsr_re=null & (_mp.pathTodo() & var_pres('EL',_we) & _we.EL='T' & var_pres('ZESTAW',_we) & _we.ZESTAW<>null)
         ||
:: jeżeli zadanie dodania elementu składowego to po Esc pytanie czy zrezygnwać z dodawania
            SRSR.cntx_psh();
            SRSR.index('TREE');
            SRSR.prefix(_we.ZESTAW);
            {? SRSR.size()>0 || _ask:=1 || _ask:=0 ?};
            SRSR.cntx_pop();
            {? _ask
            || {? FUN.ask('Zrezygnować z dodawania kolejnych elementów zestawu?'@)
               || _wy.SRSR:=_wy.SRSR_E:=null;
                  _mp.save(,_wy);
                  _mp.done();
                  EDIT_ES.SRSR:=null
               ?}
            || _mp.cancel()
            ?}
         || {? _we.ZESTAW=null
            || _wy.SRSR:=_wy.SRSR_E:=null;
               _mp.save(,_wy);
               _mp.done()
            || _mp.cancel()
            ?}
         ?}
      ?}
   |? _mp.pathTodo()
   || exec('init','fst');
      SRD.maski('r');
      SRSR.prefix();
      {? var_pres('SRSR',_we) & type_of(null()) & _we.SRSR<>null & SRSR.seek(_we.SRSR)
      || EDIT_ES.R:='T';
         _srsr_rf:=null;
         SRST.prefix();
         SRST.index('SROD');
         SRST.prefix(SRSR.ref,SRSR.ROK_F,SRSR.OKRO_F);
         {? SRST.first() || _srsr_rf:=SRD.editAsset() ?};
         {? _srsr_rf
         || _wy.SRSR:=_srsr_rf;
            _mp.save(,_wy);
            _mp.done()
         || _mp.cancel()
         ?}
      || FUN.emsg('Nie znaleziono środka.'@);
         _wy.SRSR:=null;
         _mp.save(,_wy);
         _mp.done()
      ?}
:: dołączanie środka nabytego z grupy kapitałowej
   |? (_mp.pathArea() & _akcja='dołącz z grupy' & _a='')
   || {? exec('srs_sel','fst','N')
      || _mp.trigRef('SRSR');
         _srsr_rf:=SRD.addAsset();
         {? _srsr_rf
         || _wy.SRSR:=_srsr_rf;
            _mp.save(,_wy);
            _mp.done()
         || _mp.cancel()
         ?}
      || _mp.cancel()
      ?}
   |? (_mp.pathArea() & _akcja='dołącz' & _a='')
   || _mp.trigRef('SRSR');
      _srsr_rf:=SRD.addAsset();
      {? _srsr_rf
      || _wy.SRSR:=_srsr_rf;
         _mp.save(,_wy);
         _mp.done()
      || _mp.cancel()
      ?}
   ?}
|? _mp.pathArea()
|| {? _akcja='popraw' || SRD.editAsset()
   |? _akcja='usuń'   || SRD.delAsset()
   |? _akcja='kopiuj'
   || {? SRST.GRP='T' || SRD.addAsset('G',SRST.ref())
      |? SRST.GRP='E' || SRD.addAsset('E',SRST.ref())
      || SRD.addAsset(SRST.ref())
      ?}
   |? _akcja='dołącz zestaw'
   || SRD.addAsset('G')
   |? _akcja='dołącz element' | (_akcja='dołącz' & _a='E')
   || SRD.addAsset('E')
   |? _akcja='dołącz z grupy' & _a='E'
   || {? exec('srs_sel','fst','E')
      || SRD.addAsset('E')
      ?}
   |? _akcja='dołącz serię'
   || {? SRST.size()>0 & (SRST.GRP='T')
      || _ask:='Dodać serię '@; _btn1:='Środków'@; _btn2:='Elementów'@;
         _choice:=FUN.choice(_ask,,_btn1,_btn2)
      |? SRST.size()>0 & SRST.GRP='E'
      || _choice:=2
      |? SRST.size()>0
      || _choice:=1
      |? SRST.size()=0
      || {? (_>0 | (var_pres('EL',_we) & _we.EL='T')) & _a='E' || _choice:=2 || _choice:=1 ?}
      ?};
      {? _choice
      || {? SRST.size()>0
         || {? SRST.GRP<>'T' & ((_choice=1 & SRST.GRP<>'E') | (_choice=2 & SRST.GRP='E'))
               & FUN.ask('Skopiować dane z bieżącego zapisu?'@)
            || SRD.addSeries({? _choice=1 || 'S' || 'E' ?},SRST.ref())
            || SRD.addSeries({? _choice=1 || 'S' || 'E' ?})
            ?}
         || SRD.addSeries({? _choice=1 || 'S' || 'E' ?})
         ?}
      ?}
   |? _akcja='konwersja na zestaw'
   || SRD.assetToGroup()
   |? _akcja='dołącz do zestawu'
   || SRD.addToGroup()
   |? _akcja='wycofaj zestaw'
   || SRD.groupToAsset()
   ?}
?};
VAR_DEL.delete('__doktosrsr')


\m_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Uruchomienie rejestracji środka w obszarze roboczym
::   WE: [_a] - jeżeli _a<>'' & _a='E' to czynności dotyczy dodawania elementu składowego
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FST_EWI_DESN';
_params.AKCJA:=menu_txt();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
{? _>0 & _a='E' || _value:='T' || _value:='N' ?};
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EL',_value);
_params.PROC_START:='N';
exec('mp_run','#b__box',_params);
{? __DTREE || SRST.index('TREE') ?}


\m_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Uruchomienie rejestracji środka w obszarze roboczym
::   WE: [_a] - jeżeli _a<>'' & _a='E' to czynności dotyczy dodawania elementu składowego
::----------------------------------------------------------------------------------------------------------------------
B_PROC.cntx_psh(); B_PROC.index('ACCEPTED'); B_PROC.prefix('N','T','T',REF.FIRMA,'RZC_NOW');
{? B_PROC.first() || _version:=B_PROC.VER || _version:='' ?};
B_PROC.cntx_pop();
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FST_EWI_DESN';
_params.AKCJA:=menu_txt();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
{? _>0 & _a='E' || _value:='T' || _value:='N' ?};
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EL',_value);
_params.PROC_START:='T';
{? _version<>''
|| _params.PROC_SYM:='RZC_NOW';
   _params.PROC_VER:=_version
?};
exec('mp_run','#b__box',_params);
{? __DTREE || SRST.index('TREE') ?}


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh(); USERS.prefix();
{? USERS.seek(_user)
||
:: czy w ogóle user ma uprawnienia do jakiejkolwiek jednostki księgowej
   {? exec('usr_fjks_any','b_perm',USERS.ref())
   || _result:=1
   ?}
?};
USERS.cntx_pop();
_result


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('EL',_in) & _in.EL='T'
|| {? var_pres('ZESTAW',_in) & _in.ZESTAW<>null
   || SRSR.cntx_psh();
      SRSR.prefix();
      {? SRSR.seek(_in.ZESTAW,SRSR.name()) || _txt:=SRSR.NRI ?};
      SRSR.cntx_pop()
   || _txt:=''
   ?};
   _desc:='Rejestruj element składowy zestawu trwałego %1'@@[_txt]
|? var_pres('SRSR',_in)=type_of(null()) & _in.SRSR & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
|| 'Kontynuuj rejestrację środka trwałego: %1'@@ [exec('record','#to_string',_in.SRSR)]
|| 'Rejestruj środek trwały'@@
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 db3469a8345d265dd5c02159bdd346eae57071127ef4fb42dfd111ff19176154471aa22114e64b5b1f8468cdfbf02a563b805c35ff23cb98493ed44f9505b6321edde9c3dfed2e7be60150b4b0ffdf14bae04118c486d56541cca6ec485b761d5cd791b10c6d86f8fd3f3ad74e10dc14f3b02909d94668659d70d7ed8087105a

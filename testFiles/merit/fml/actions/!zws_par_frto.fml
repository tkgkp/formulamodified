:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_frto.fml
:: Utworzony: 30.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_FRTO - Tabele odsetek
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tabele odsetek - główna formuła czynności ZWS_PAR_FRTO.
::  OLD: \ods_tab/oper.fml
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys');
{? FINFO.SL_ODS<>null
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(FINFO.SL_ODS().SLU); SLO.first();
   ROZNE.WAL_ODS:=FINFO.NAROD;
   exec('arfr_slwal','!zws_par_frto');
   SLO.win_edit('RED');
   _gr:=SLO.grp_make('Tabele odsetek'@,,'#slowyrana');
   _wer:=SLO.mk_sel('Tabele odsetek'@,'P',,'slo_tab_ods',,,,,'U');
   SLO.win_fld(_wer,,'KOD',,,8,,1,'Kod'@,,'Kod pozycji słownika walut'@);
   SLO.win_fld(_wer,,'TR',,,40,,1,,,'Treść pozycji słownika walut'@);
   SLO.win_act(_wer,,'Kolejność');
   SLO.grp_sel(_gr,,_wer,,"exec('arfr_slwal','!zws_par_frto'); grp_disp(ODS,'WER'); 1"
               ,,,,,,,,'maximized_with_title');
   SLO.grp_splt(_gr,,'vertical','left',',30%');
   SLO.grp_sel(_gr,ODS,'WER',,,,,,,,,,'maximized_with_title');
   SLO.win_sel(_gr);
   SLO.select();
   SLO.cntx_pop()
|| FUN.info('Nie zdefiniowano słownika typów tabeli odsetek.'@)
?};
1


\arfr_slwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu tabel odsetkowych
::----------------------------------------------------------------------------------------------------------------------
ODS.index('DATA');
{? SLO.get()
|| ODS.prefix(SLO.ref(),ROZNE.WAL_ODS)
|| ODS.prefix(null,ROZNE.WAL_ODS)
?};
SLO.cntx_psh();
ODS.hdr_sel(); ODS.hdr_sel('  Typ: %1  Waluta: %2'@[SLO.KOD,ROZNE.WAL_ODS().KOD]);
SLO.cntx_pop();
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
''


\be_slu_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.62]
:: OPIS: Przed redakcją pola SLOSLU.SL_ODS
::  OLD: \be_slu_od/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
1


\pr_sl_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formuła po redakcji słownika typów tabel odsetek
::  OLD: \pr_sl_od/odsetki.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=exec('pored_slu','slo_slu','FINFO.SL_ODS','SLOSLU.SL_ODS','');
{? _zwrot
|| {? FINFO.SL_ODS<>null
   || SLU.cntx_psh(); SLUAPPL.cntx_psh(); SLO.cntx_psh();
      SLU.prefix(); SLUAPPL.prefix();
      FINFO.SL_ODS().SLU();
      SLO.index('SL'); SLO.prefix(FINFO.SL_ODS().SLU);
      {? SLO.first()
      || {? ~SLO.seek(FINFO.TODS_DOM) || FINFO.TODS_DOM:=null ?};
         {? ~SLO.seek(FINFO.TODS_UST) || FINFO.TODS_UST:=null ?}
      || FINFO.TODS_DOM:=FINFO.TODS_UST:=null
      ?};
      SLU.cntx_pop(); SLUAPPL.cntx_pop(); SLO.cntx_pop()
   || FINFO.TODS_DOM:=FINFO.TODS_UST:=null
   ?}
?};
_zwrot


\betodsdm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed redakcją pól FINFO.TODS_DOM i FINFO.TODS_DOM
::  OLD: \betodsdm/skid_pw.fml
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.SL_ODS().SLU<>null
|| FINFO.SL_ODS().SLU(); 1
|| 0
?}


\aeodswsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Po redakcji pola ODS.WSK
::  OLD: \aeodswsk/odsetki.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()<=0
|| FUN.info('Wartość powinna być większa od 0.'@); 0
|| ZM_UMOWY.PWU:=UDT.WUM-UDT.CKW;1
?}


\pocz_mia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: wartosc poczatkowa mianownik tabela odsetki
::  OLD: \pocz_mia/param.fml
::----------------------------------------------------------------------------------------------------------------------
365


\ae_mia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Po redakcji pola ODS.MIAN
:----------------------------------------------------------------------------------------------------------------------
{? ODS.MIAN>0
|| ODS.DWSK:=ODS.WSK/ODS.MIAN$4;
   {? var_pres('WSK_PL',ODS)>0
   || ODS.DWSK_PL:=ODS.WSK_PL/ODS.MIAN$4
   ?};
   1
|| FUN.info('Liczba dni w roku musi być większa od zera.'@); 0
?}


\blwalods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Wartość początkowa pola ODS.WAL
::  OLD: \blwalods/odsetki.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.WAL_ODS


\spr_ods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [????]
:: OPIS: Rekord po w tabeli ODS
::  OLD: \spr_ods/rejoper.fml
::----------------------------------------------------------------------------------------------------------------------
ODS.DWSK:=ODS.WSK/ODS.MIAN$4;
{? var_pres('WSK_PL',ODS)>0
|| ODS.DWSK_PL:=ODS.WSK_PL/ODS.MIAN$4;
   _zwrot:={? ODS.WSK>0 | ODS.WSK_PL>0
         || __CHK.record(ODS,,'ZM','MIAN')
         || __CHK.record(ODS,,'ZM','WSK','MIAN')
         ?}
|| _zwrot:=__CHK.record(ODS,,'ZM','WSK','MIAN')
?};
{? _zwrot=''
|| {? __CHK.index(ODS,-menu_txt(,)='popraw')<>'' || _zwrot:='ZM' ?}
?};
_zwrot


\zmwalods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.40]
:: OPIS: Zmienia walutę tabeli odsetek
::  OLD: \zmwalods/odsetki.fml
::----------------------------------------------------------------------------------------------------------------------
wyb:=0;
{? FINFO.SLWAL=null
|| FUN.info('Nie zdefiniowano słownika walut.'@);   0
|| ODS_TMP:=tab_tmp(,'KOD','STRING[8]','Kod'@,
                     'TR','STRING[80]','Treść'@);
   _wer:=ODS_TMP.mk_sel('Waluty'@,'T',1,'ods_tmp_wer');
   ODS_TMP.win_act(_wer,0,'Formuła','Wybierz'@@,,'Wybór waluty'@,,"exec('ten_walu','waluty')",1,,,,'W');
   ODS_TMP.win_sel(_wer);
   SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(FINFO.SLWAL().SLU);
   FINFO.SLWAL().SLU();
   {? SLO.first()
   || {! |?
         ODS_TMP.KOD:=SLO.KOD;
         ODS_TMP.TR:=SLO.TR;
         ODS_TMP.add();
         SLO.next()
      !}
   ?};
   ODS_TMP.find_key(ROZNE.WAL_ODS().KOD);
   ODS_TMP.select(,1);
   {? wyb & SLO.find_key(ODS_TMP.KOD)
   || ROZNE.WAL_ODS:=SLO.ref(); _wal:=SLO.KOD
   || wyb:=0
   ?};
   SLO.cntx_pop();
   {? wyb
   || ODS.hdr_sel();
      ODS.hdr_sel('  Typ: %1  Waluta: %2'@[SLO.KOD,_wal]);
      ODS.prefix(SLO.ref(),ROZNE.WAL_ODS)
   ?};
   VAR_DEL.delete('ODS_TMP')
?}


\pw_mies
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Przed wyświetleniem pola MIES.MIES
::  OLD: \pw_mies/oper.fml
::----------------------------------------------------------------------------------------------------------------------
MIES.MIES:=ODS.WSK/12;
1


\pw_mies_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Przed wyświetleniem pola MIES.MIES_PL
::----------------------------------------------------------------------------------------------------------------------
MIES.MIES_PL:=ODS.WSK_PL/12;
1


\ae_wsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Formuła po redakcji pola ODS.WSK
::----------------------------------------------------------------------------------------------------------------------
MIES.MIES:=ODS.WSK/12;
ODS.DWSK:=ODS.WSK/ODS.MIAN$4


\ae_wsk_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Formuła po redakcji pola ODS.WSK_PL
::----------------------------------------------------------------------------------------------------------------------
MIES.MIES_PL:=ODS.WSK_PL/12;
ODS.DWSK_PL:=ODS.WSK_PL/ODS.MIAN$4

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 9a3886170ac3cddbc6e77a7391da4f89daf5b6427706815d6a846a3e42b3d4452e62e5e3b2677727b98c8546250f90a84f739bab174ab60755aeee70b4762dbd420a8fd9c26f3be497396b2af641b5fd6b116eb8dc4f13f73602eacdbe9b63f64696055c8d1db3e4a58d2b6faa5f0029446150467ac1c5bf10b6510ef81dc1f5

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_dzok.fml
:: Utworzony: 07.11.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_DZOK - Zam. okresu z przen. danych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Zamknięcie rozliczenia okresu i przeniesienie danych dla wszystkich współpracowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=T, keyref=T
::# kind=WEW, symbol=OKRES, type=STRING, name=Data do ustalenia okresu rozliczenia przekroczeń, required=N
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_int:=_par.int;
_akcja:=_mp.akcja();
_result:='';

_uid:=exec('ref2uid','#table',_in.A_OKR);
{? _uid=''
|| _result:=exec('error','!prc_czp_dzok','Nie znaleziono okresu rozliczeniowego.'@)
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uid);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uid);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('zamknij_okr','!prc_czp_dzok',_in,_int);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _par.mp.save(_int);
         _mp.keep()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Zamknięcie rozliczenia okresu i przeniesienie danych dla wszystkich współpracowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_tab:=params_exec('opis_okr','okres',_mp.load(exec('kind_in','#b_port')).A_OKR);

{? _tab.ZAW_DANE='T'
|| 'Zamknij rozliczenie okresu %1 %2 %3'@@[_tab.A_OKRN_NAZWA,_tab.OD,_tab.DO]
|| 'Zamknij rozliczenie okresu'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Informacja o rodzaju błędu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Zamknięcie okresu rozliczeniowego nie jest możliwe.'@,_a]


\opis_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Opis wybranego okresu rozliczeniowego.
::   WE: _a - [REFERENCE] Wskazanie na wybrany okres rozliczeniowy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_okr:='';
A_OKR.cntx_psh();
{? A_OKR.seek(_a,,1)
|| _okr:='%1 %2 %3'@[A_OKR.NAZ().NAZ,A_OKR.OD$1,A_OKR.DO$1]
?};
A_OKR.cntx_pop();
_okr


\zamknij_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a - Tablica z parametrami wejściowymi.
::       _b - Tablica z parametrami wewnętrznymi.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
A_OKR.cntx_psh();
{? A_OKR.seek(_a.A_OKR,,1)
|| {? A_OKR.S_PLAN='O'
   || _result:=exec('error','!prc_czp_dzok','Okres rozliczeniowy jest odblokowany do planowania.'@)
   |? A_OKR.S='Z'
   || _result:=exec('error','!prc_czp_dzok','Okres rozliczeniowy jest już zamknięty.'@)
   || _tab:=tab_tmp(,
         'RESULT','INTEGER','',
         'OKRES','STRING[1]','Wybrany okres'
      );
      _tab.OKRES:={? var_pres('OKRES',_b)=type_of('') || _b.OKRES || 'O' ?};
      _ed:=_tab.mk_edit('Okres: %1'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1],0);
      _tab.win_efld(_ed,AH,'H',,,,,,'Okres rozliczeniowy zostanie zamknięty.'@);
      _tab.win_efld(_ed,,'OKRES',,,10,,,
         'Rozliczenie przekroczeń średniotygodniwych w miesiącu:'@,,
         'Miesiąc i rok, w którym zostaną rozliczone przekroczenia z tytułu średniotygdniowego czasu pracy.'@,
         'radio-buttons',,
         A_OKR.DO$8,"'O'",
         (A_OKR.DO+1)$8,"'N'"
      );
      exec('zakoncz','#window',_tab,_ed,1,"cur_tab().RESULT:=1; 'key:F2'",,
         exec('help_red_zakoncz','#window','PKD_F'),exec('text_red_zakoncz','#window','PKD_F'));
      exec('ok_esc','#window',_tab,_ed,1,"cur_tab().RESULT:=2; 'key:F2'","cur_tab().RESULT:=0; 'key:Esc'",,,
         exec('help_red_ok','#window','Z'),exec('text_red_ok','#window','Z'),exec('help_red_esc','#window','A'));
      _tab.win_edit(_ed);
      _tab.edit();
      {? _tab.RESULT=1
      || exec('a_okr_wer_bfrz','okres',_tab.OKRES);
         A_OKR.seek(_a.A_OKR,,1);
         _result:=A_OKR.S='Z'
      |? _tab.RESULT=2
      || _b.OKRES:=_tab.OKRES;
         _result:=0
      || _result:='Zrezygnowano z zamknięcia okresu rozliczeniowego %1.'@[A_OKR.NAZ().NAZ+' '+A_OKR.OD$1+' '+A_OKR.DO$1]
      ?};
      obj_del(_tab)
   ?}
|| _result:=exec('error','!prc_czp_dzok','Nie znaleziono okresu rozliczeniowego.'@)
?};
A_OKR.cntx_pop();
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 0cabe80fd75fac2d916b5ba4b0d8abc922aaed47c0c1b9e10b2ebbf01f504061b2ff1673f2a3382579f50f665b3e196106e3cd071c0e046ecbfc510afb8de42c46f579aae1c17740b44588c427f046ec57cd61bfe9a542b7bc31237a6cda32bd1fc0df8313ae51002600c60af01db59491d38ced446410d0547e416fe17838e1

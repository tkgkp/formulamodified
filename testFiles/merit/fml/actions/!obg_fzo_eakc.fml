:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obg_fzo_eakc.fml
:: Utworzony: 10.03.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBG_FZO_EAKC - Akceptacja faktury w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja faktury w obiegu - główna formuła czynności OBG_FZO_EAKC
::----------------------------------------------------------------------------------------------------------------------
:: STD_PortalCloud_SEOD
::# portal=STD_PC_S_AcceptanceExtend_PRC_Accept;STD_PC_S_AcceptanceExtend_PRC_Close;STD_PC_S_AcceptanceExtend_PRC_Reject; ^
::# STD_PC_S_AcceptanceExtend_PRC_Revoke;STD_PC_S_AcceptanceExtend_SLO;STD_PC_S_AcceptanceHistory_SLO; ^
::# STD_PC_S_AcceptanceKPIs_SLO;STD_PC_S_AcceptanceSimple_PRC_Accept;STD_PC_S_AcceptanceSimple_PRC_Close; ^
::# STD_PC_S_AcceptanceSimple_PRC_Reject;STD_PC_S_AcceptanceSimple_PRC_Revoke;STD_PC_S_AcceptanceSimple_SLO; ^
::# STD_PC_S_AcceptanceSummary_SLO;STD_PC_S_Acceptance_PRC_Accept;STD_PC_S_Acceptance_PRC_Close; ^
::# STD_PC_S_Acceptance_PRC_Reject;STD_PC_S_Acceptance_PRC_Revoke;STD_PC_S_Acceptance_SEC_AcceptanceBase; ^
::# STD_PC_S_Acceptance_SEC_AcceptanceOwn_3;STD_PC_S_Acceptance_SLO;STD_PC_S_AdvancePaymentType_SLO; ^
::# STD_PC_S_AttachLight_PRC_AttachAdd;STD_PC_S_AttachLight_PRC_AttachDel;STD_PC_S_AttachLight_PRC_AttachDelete; ^
::# STD_PC_S_AttachLight_PRC_AttachUpd;STD_PC_S_AttachLight_PRC_AttachsUpdateSlo; STD_PC_S_AttachLight_SLO; ^
::# STD_PC_S_BusinessContactInfo_SLO;STD_PC_S_BusinessContact_SLO;STD_PC_S_BusinessContact_SLO_I; ^
::# STD_PC_S_BusinessContact_SLO_U;STD_PC_S_CostCategory_SLO;STD_PC_S_CostCenterType_SLO;STD_PC_S_CostCenter_SLO; ^
::# STD_PC_S_CostShare_SLO;STD_PC_S_CostShare_SLO_D;STD_PC_S_CostShare_SLO_I;STD_PC_S_CostShare_SLO_U; ^
::# STD_PC_S_CurrencyCode_SLO; ^
::# STD_PC_S_NoteWithAttach_SLO;STD_PC_S_NoteWithAttach_SLO_D;STD_PC_S_NoteWithAttach_SLO_I; ^
::# STD_PC_S_NoteWithAttach_SLO_U; ^
::# STD_PC_S_OtherAcceptanceDahboardAdditionalInfo_SLO;STD_PC_S_OtherAcceptanceDahboardDetailInfo_SLO; ^
::# STD_PC_S_OtherAcceptanceDahboardExtendedInfo_SLO;STD_PC_S_PaymentType_SLO;STD_PC_S_PurchaseDocHeaderInfo_SLO; ^
::# STD_PC_S_PurchaseDocLines_SLO;STD_PC_S_PurchaseDocLines_SLO_D;STD_PC_S_PurchaseDocLines_SLO_I; ^
::# STD_PC_S_PurchaseDocLines_SLO_U;STD_PC_S_PurchaseDocPaymentInfo_SLO;STD_PC_S_PurchaseDocType_SLO; ^
::# STD_PC_S_PurchaseDoc_PRC_Accept;STD_PC_S_PurchaseDoc_PRC_Close;STD_PC_S_PurchaseDoc_PRC_PurchaseDocAttachUpload; ^
::# STD_PC_S_PurchaseDoc_PRC_PurchaseDocRegisterBill;STD_PC_S_PurchaseDoc_PRC_PurchaseDocRegisterInvoice; ^
::# STD_PC_S_PurchaseDoc_PRC_PushDocStatus;STD_PC_S_PurchaseDoc_PRC_Reject;STD_PC_S_PurchaseDoc_PRC_Revoke; ^
::# STD_PC_S_PurchaseDoc_PRC_UndoDocStatus;STD_PC_S_PurchaseDoc_SLO;STD_PC_S_PurchaseDoc_SLO_U; ^
::# STD_PC_S_ShowAttach_SLO;STD_PC_S_VatRate_SLO; ^
::# STD_PC_S_PurchaseDocLinked_ZapMat_SLO; STD_PC_S_PurchaseDocLinked_ZapMat_SLO_D; ^
::# STD_PC_S_PurchaseDocLinked_ZapMat_SLO_I; STD_PC_S_PurchaseDocLinked_ZapMat_SLO_U; ^
::# STD_PC_S_PurchaseDocSimple_SLO_U; STD_PC_S_PurchaseDocSimple_PRC_UndoDocStatus; ^
::# STD_PC_S_PurchaseDocSimple_PRC_Reject; STD_PC_S_PurchaseDocSimple_PRC_Revoke; ^
::# STD_PC_S_PurchaseDocSimple_PRC_Accept; STD_PC_S_PurchaseDocSimple_PRC_PurchaseDocAttachUpload; ^
::# STD_PC_S_PurchaseDocSimple_PRC_PushDocStatus; STD_PC_S_PurchaseDocSimple_SLO_U

::# permissions=FJKS
::# parses=exec('parses_edokum','obiegi')
::# access=exec('upr_oper','obiegi_akc')
::# properties=TODOTRIG,~RUNMICRO,~RELEASE

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=T, keyref=T
::# kind=WE, symbol=USERS, type=_USERS, name=Użytkownik, required=N
::# kind=WE, symbol=B_PREL, type=STRING, name=Poprzedni element w procesie, required=N
::# kind=WE, symbol=B_PRELS, type=STRING, name=Symbol poprzedniego elementu w procesie, required=N
::# kind=WE, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WE, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WE, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WE, symbol=STATUS, type=STRING, name=Status, required=N

::# kind=WE, symbol=FORM_PRZ, type=STRING, name=Formuła wykonywana przy kończeniu akceptacji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_WAL, type=STRING, name=Formuła walidacyjna przy kończeniu akceptacji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_ODR, type=STRING, name=Formuła wykonywana przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORMWODR, type=STRING, name=Formuła walidacyjna przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"

::# kind=WE, symbol=ED_PODZ, type=STRING, name=Edycja podziałów controllingowych, required=N, fml_val="exec('czy_ed_podz','obiegi')"
::# kind=WE, symbol=ED_INND, type=STRING, name=Edycja informacji dodatkowych, required=N, fml_val="exec('czy_inn_dan','obiegi')"
::# kind=WE, symbol=ED_PROJ, type=STRING, name=Edycja projektów, required=N, fml_val="exec('czy_ed_proj','obiegi')"
::# kind=WE, symbol=ED_OPOP, type=STRING, name=Edycja opisu operacji podczas akceptacji faktury, required=N, fml_val="exec('czy_ed_opop','obiegi')"
::# kind=WE, symbol=UWODRWYM, type=STRING, name=Wymagane wypełnienie uwag przy odrzuceniu, required=N, fml_val="exec('czy_wymoduw','obiegi_akc')"
::# kind=WE, symbol=KONT_BUD, type=STRING, name=Kontrola budżetu, required=N, fml_val="exec('kont_bud','obiegi',_a)"
::# kind=WE, symbol=ED_POZF, type=STRING, name=Edycja pozycji faktury podczas akceptacji, required=N, fml_val="exec('czy_ed_pozf','obiegi_akc')"
::# kind=WE, symbol=ED_POZV, type=STRING, name=Edycja pozycji VAT podczas akceptacji, required=N, fml_val="exec('czy_ed_pozv','obiegi_akc')"

::# kind=WE, symbol=B_ROLEF, type=STRING, name=Rola użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=B_ROLE, type=STRING, name=Domyślna rola użytkowników, required=N, fml_val="exec('rola','obiegi',_a)", fml_exp="exec('b_role_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=BLZMROL, type=STRING, name=Blokada zmiany roli, required=N, fml_val="exec('blzmrol','obiegi')"
::# kind=WE, symbol=BLZMUSR, type=STRING, name=Blokada zmiany uzytkowników, required=N, fml_val="exec('blzmusers','obiegi')"
::# kind=WE, symbol=USR_FORM, type=STRING, name=Wybór użytkowników - formuła, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=JEDEN_OP, type=STRING, name=Obsługuje jeden operator - formuła, required=N, fml_val="exec('jeden_op','obiegi2')"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=USERS, type=_USERS, name=Użytkownik, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N
::# kind=WY, symbol=WAL, type=_SLO, name=Waluta, required=N
::# kind=WY, symbol=NETTO, type=NUMBER, name=Wartość netto, required=N
::# kind=WY, symbol=BRUTTO, type=NUMBER, name=Wartość brutto, required=N
::# kind=WY, symbol=EM_NAD, type=STRING, name=Adres e-mail użytkownika akceptujacego (nadawca), required=N
::# kind=WY, symbol=EM_ODB, type=MEMO, name=Adres e-mail użytkownika akceptującego (odbiorca), required=N
::# kind=WY, symbol=EM_NAG, type=STRING, name=Nagłówek wiadomości e-mail, required=N
::# kind=WY, symbol=EM_TR, type=MEMO, name=Treść wiadomości e-mail, required=N
::# kind=WY, symbol=BUDZ_COND, type=NUMBER, name=Budżet przekroczony warunkowo?, required=N

:: PARAMETRY WEW:
::# kind=WEW, symbol=USERSN, type=_USERS, name=Użytkownik, required=N

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

{? _akcja='Portal'
|| {? var_pres('EDOKUM',_we)>0 & _we.EDOKUM
   || EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_we.EDOKUM));
      {? EDOKUM.seek(_we.EDOKUM)
      || params_exec('ustaw_bprel','obiegi');
         _wy.EDOKUM:=EDOKUM.ref();
         _wy.WAL:=EDOKUM.WAL;
         _wy.NETTO:=EDOKUM.NETTO;
         _wy.BRUTTO:=EDOKUM.WART;
         _wy.DEKR:=EDOKUM.TYP().DEKR;
         _wy.STATUS:={? EDOKUM.STATUS='D' || 'O' || 'A' ?};
         _wy.EM_NAD:=_wy.EM_ODB:=OPERATOR.USER().EMAIL;
         _wy.EM_NAG:={? EDOKUM.STATUS='Y' || 'Zaakceptowano' || 'Odrzucono' ?}+
                     ' wniosek: '+EDOKUM.ID+', data operacji '+$EDOKUM.DOP;
         _wy.B_PREL:=OBIEGI.B_PREL;
         _wy.B_PRELS:=OBIEGI.B_PRELS;
         {? EDOKUM.STATUS='D'
         || {? var_pres('FORM_ODR',_we)>0 & _we.FORM_ODR<>''
            || _fml:=_we.FORM_ODR;
               {? _fml<>''
               || ($_fml)()
               ?}
            ?}
         || {? var_pres('FORM_PRZ',_we)>0 & _we.FORM_PRZ<>''
            || _fml:=_we.FORM_PRZ;
               {? _fml<>''
               || ($_fml)()
               ?}
            ?}
         ?};
         {? var_pres('_args')>0 & var_pres('context',_args)>0 & var_pres('DONE',_args.context)>0
         || _args.context.B_PREL:=OBIEGI.B_PREL;
            _args.context.DONE:=1
         ?};
         _mp.save(,_wy);
         _mp.keep();
         _mp.done()
      ?};
      EDOKUM.cntx_pop()
   ?};
   return(~~)
|? _akcja='PortalWalidacja'
|| {? var_pres('EDOKUM',_we)>0 & _we.EDOKUM
   || _args.context.RET:=~~;
      EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_we.EDOKUM));
      {? EDOKUM.seek(_we.EDOKUM)
      || params_exec('ustaw_bprel','obiegi');
         _args.context.B_PREL:=OBIEGI.B_PREL;
         {? var_pres('FORM_WAL',_we)>0 & _we.FORM_WAL<>''
         || _fml:=_we.FORM_WAL;
            {? _fml<>''
            || _args.context.RET:=($_fml)()
            ?}
         ?}
      ?};
      EDOKUM.cntx_pop()
   ?};
   return(~~)
?};

{? _we.EDOKUM & EDOKUM.seek(_we.EDOKUM,,1)
|| zak_akc_ed:=0; OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
   budz_cond:=0;
   params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
   {? _akcja='Akceptuj2'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2)); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref());
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('akceptuj2','obiegi_akc','A');
      {? zak_akc_ed=1 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'A');
         no_bprel:=_nobp:=exec('no_bprel','obiegi_akc',_wy.B_PREL);
         _mp.save(,_wy);
         _mp.done();
         no_bprel:=_nobp
      ?}
   |? _akcja='Odrzuc2'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2)); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref());
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('odrzuc2','obiegi_akc');
      {? zak_akc_ed=2 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'O');
         _mp.save(,_wy);
         _mp.done()
      ?}
:: sprawdzenie z poziomu webterma
   |? _akcja='chkWT'
   || {? app_info('web_sesid')<>''
      || web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                               'popetap',_we.B_PRELS
                               ))
      ?};
      params_get().context.SPR_WT:=1
:: akceptacja z pulpitu
   |? _akcja='Akceptuj'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('akceptuj','obiegi_akc','A');
      {? zak_akc_ed=1 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'A');
         no_bprel:=_nobp:=exec('no_bprel','obiegi_akc',_wy.B_PREL);
         _mp.save(,_wy);
         _mp.done();
         no_bprel:=_nobp
      ?}
   |? _akcja='Zamknij'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('zamknij','obiegi_akc');
      {? zak_akc_ed=3 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'Z');
         no_bprel:=1;
         _mp.save(,_wy);
         _mp.done();
         no_bprel:=1
      ?}
   |? _akcja='Odrzuć'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('odrzuc','obiegi_akc');
      {? zak_akc_ed=2 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'O');
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _mp.pathTodo() | _mp.isAutoRun()
   || menu_txt(,'Akceptuj');
      {? var_pres('EDOKUM',_we)
      || _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
         {? _ref<>0 & _nam<>''
         || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
            {? EDOKUM.seek(_ref,_nam)
            || exec('init','obg'); zakres:=2;
               exec('rkprz','obiegi');
               params_exec('przekaz_todo','obiegi_akc','A');
               {? EDOKUM.get()
               || {? zak_akc_ed=1
                  || exec('ustaw_out','obiegi_akc',_wy,'A');
                     no_bprel:=_nobp:=exec('no_bprel','obiegi_akc',_wy.B_PREL);
                     _mp.save(,_wy);
                     _mp.done();
                     no_bprel:=_nobp
                  |? zak_akc_ed=2
                  || exec('ustaw_out','obiegi_akc',_wy,'O');
                     _mp.save(,_wy);
                     _mp.done()
                  ?}
               ?}
            ?};
            EDOKUM.cntx_pop()
         ?}
      ?}
   ?}
?};
VAR_DEL.delete('zak_akc_ed','zawin','budz_cond');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zaakceptuj fakturę w obiegu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
params_exec('ustaw_bprel','obiegi');
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));
{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
         KH.cntx_psh();
         {? EDOKUM.KHKH
         || _desc:='Zaakceptuj fakturę w obiegu: %1, data sprzedaży %2, otrzymano %3, brutto: %4, kontrahent: %5'@@[EDOKUM.SYM,$EDOKUM.DOP,$EDOKUM.DO,form(EDOKUM.WART,,2,' ,'),EDOKUM.KHKH().SKR]
         || _desc:='Zaakceptuj fakturę w obiegu: %1, data sprzedaży %2, otrzymano %3, brutto: %4'@@[EDOKUM.SYM,$EDOKUM.DOP,$EDOKUM.DO,form(EDOKUM.WART,,2,' ,')]
         ?};
         KH.cntx_pop();
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)
      ?};
      {? var_pres('co_dalej')>0 &
         ((4+web_top_win(0,0))='WPRG' | (4+web_top_win(1,0))='WPRG' | (4+web_top_win(2,0))='WPRG')
      || co_dalej:='A'
      ?};
      {? var_pres('STATUS',_we)>0 & _we.STATUS='O' & _mp.akcja()<>'Akceptuj' & -menu_txt()<>'akceptuj'
         & -menu_txt()<>'rozpocznij' & _set
      || {? OBIEGI.B_PREL<>''
         || EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
            {? EDOKOS.first()
            || {! |?
                  EDOKOS.STATUS:='O'; EDOKOS.put();
                  EDOKOS.next()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?}
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc


\fak_ob_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja faktur w obiegu
::   WE: _a - zadeklarowane i równe 1 - odpalanie z TODO na WEBTERM
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| {? ~_ || _a:=0 ?};
   {? _a
   || exec('todo_run','obiegi',1); todo_run:=1
   || exec('todo_run','obiegi',0)
   ?};
   _dalej:=exec('env_edokum','obiegi','AF',0);
   VAR_DEL.delete('todo_run');
   {? _dalej
   || _dalej:=exec('is_act_user','#b__box','OBG_FZO_EAKC',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
      {? ~_dalej
      || FUN.info('Akceptacja faktury nie jest na liście zadań użytkownika.'@)
      ?}
   ?};
   {? _dalej
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='OBG_FZO_EAKC';
      _params.AKCJA:='chkWT';
      _params.UIDREF:=EDOKUM.uidref();
      _params.WT_SKIP:=1;
      _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
      _params.CONTEXT.EDOKUM:=EDOKUM.ref();
      _params.CONTEXT.SPR_WT:=0;
      exec('maski_w','obiegi',1);
      exec('mp_run','#b__box',_params);
      {? _params.CONTEXT.SPR_WT
      || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
         params_exec('akceptuj','obiegi_akc','A')
      ?}
   ?}
|| _dalej:=exec('is_act_user','#b__box','OBG_FZO_EAKC',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
   {? _dalej
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='OBG_FZO_EAKC';
      _params.AKCJA:='Akceptuj';
      _params.UIDREF:=EDOKUM.uidref();
      _params.CONTEXT:=obj_new('EDOKUM');
      _params.CONTEXT.EDOKUM:=EDOKUM.ref();
      exec('mp_run','#b__box',_params)
   || FUN.info('Akceptacja faktury nie jest na liście zadań użytkownika.'@)
   ?}
?}


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::       _b - B_PREL.ref() lub BI_PREL.ref() lub exec('mp_run_a','#b__box')
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
{? _path='Todo'
|| _path_param:=_b;
   _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   _prel:=_path_param;
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || _name:=((_keyrefs[1]-8)+8);
      EDOKUM.use(_name); EDOKUM.prefix();
      {? EDOKUM.seek(_keyrefs[1],,1)
      || exec('maski_w','obiegi',1);
         EDOKUM.memo_get(,'UW_OPDL');
         exec('bobs_edokum','obiegi','F');
         SKID.SL_KH:=SKID.NIP:='';
         web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'ed_kh_ed',0));
         EDOKUM.web_display('WF02',,"EDOKUM.web_eclose()")
      ?}
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
?}


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności OBG_FZO_EAKC
::  WE: _a - 'add po', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='add_po' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='add_po'
         || 3
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'OBG_FZO_EAKC')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Podglad dokumentu księgowego z listy zadań
::  OLD: \display/!fks_dks_dark.fml
::----------------------------------------------------------------------------------------------------------------------
_webterm:=+app_info('web_sesid');

_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
_rfd:=null();
{? _wy.EDOKUM <> ~~
|| _rfd:=_wy.EDOKUM
|| _in:=_mp.load(exec('kind_in','#b_port'));
   _rfd:=_in.EDOKUM
?};
{? _webterm
|| EDOKUM.use(8+$_rfd);
   EDOKUM.seek(_rfd);
   EDOKUM.web_cntx_save();
   EDOKUM.web_display('WF03',,"EDOKUM.web_cntx_load(); EDOKUM.web_eclose(); web_top_refresh(1)")
|| exec('dok_spac','obg',_rfd)
?}

:Sign Version 2.0 jowisz:1045 2023/12/08 07:41:16 2bc8a1b9f3a609a756424272c20375d3eb8f413922e1aa48815826ba89bac9b414efe256a15b7055f5a35aeb95256c2980787f7f1a80cfb78b21c3317030023a0f30ace772828528a81e258e2ef4274bc54cfd88265ea4f1371eaaa9bfaafe599bdd174a9db1e750407cd963518a613d1a2780fd4910fd9ce5f1f4200c336eb2

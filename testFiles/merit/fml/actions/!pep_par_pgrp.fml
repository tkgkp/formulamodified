:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pep_par_pgrp.fml
:: Utworzony: 17.05.2021
:: Autor: AFI
::======================================================================================================================
:: Zawartość: Grupy wniosków paperless w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Formuła główna czynności PEP_PAR_PGRP
::----------------------------------------------------------------------------------------------------------------------
ETYP_GRP.cntx_psh();
ETYP_GRP.index('N'); ETYP_GRP.prefix();
ETYP_GRP.win_sel('WERWWP');
ETYP_GRP.select();
ETYP_GRP.cntx_pop()


\add_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Dołącz grupę dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
ETYP_GRP.win_edit('REDWWP');
ETYP_GRP.blank();
{? ETYP_GRP.edit("exec('valid_grp','!pep_par_pgrp')") || ETYP_GRP.add() ?}


\valid_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Walidacja po redakcji grupy
::----------------------------------------------------------------------------------------------------------------------
_dalej:='';
{? ETYP_GRP.N=''
|| FUN.info('Nie wprowadzono skrótu.'@); _dalej:='N'
?};
{? ETYP_GRP.NAZ='' & _dalej=''
|| FUN.info('Nie wprowadzono nazwy grupy.'@); _dalej:='NAZ'
?};
{? _dalej=''
|| _popraw:=(-menu_txt()='popraw');
   {? _popraw || _ref:=ETYP_GRP.ref() ?};
   ETYP_GRP.cntx_psh();
   ETYP_GRP.index('N'); ETYP_GRP.prefix(REF.FIRMA,ETYP_GRP.N,ETYP_GRP.N);
   {? ETYP_GRP.first() & {? _popraw || ETYP_GRP.ref()<>_ref || 1 ?}
   || FUN.info('Istnieje już skrót o podanej nazwie.'@); _dalej:='N'
   ?};
   ETYP_GRP.cntx_pop()
?};
_dalej


\edt_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Edycja grupy
::----------------------------------------------------------------------------------------------------------------------
ETYP_GRP.win_edit('REDWWP');
{? ETYP_GRP.edit("exec('valid_grp','!pep_par_pgrp')") || ETYP_GRP.put() ?}


\del_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Usuwanie grupy
::----------------------------------------------------------------------------------------------------------------------
{? ETYP_GRP.count()<>0
|| FUN.info('Grupa wykorzystywana w systemie. Usunięcie niemożliwe.'@)
|? FUN.ask('Usunąć grupę?'@)
|| ETYP_GRP.del()
?}

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:58 5ac13d95b46ace5e5a436fe44be161043f941da780d7ab7f76c5d70991e114f4dc5f6555b3df34e10c738055dcb00e8425a701ab1ca3cf1766dfabc735886e3014df1ccdea536e33b68824188f70e2ab427c90fefdaa4b5b4b90bbd691e3f2a70f9d15c3316e07f7acf70142bb5b7874048f451d92be78b4d2b0c9a603b41513

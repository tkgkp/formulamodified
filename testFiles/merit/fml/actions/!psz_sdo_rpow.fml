:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdo_rpow.fml
:: Utworzony: 14.12.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PSZ_SDO_RPOW - Powiadomienie o szkoleniu.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Powiadomienie o szkoleniu - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::
::# kind=WE, symbol=SZK_PRAC, type=_SZK_PRAC, name=Wskazanie uczestnika szkolenia, required=T, keyref=T
::
:: Wynik działania czynności. Parametr szczególnie istotny w przypadku uruchomienia czynności usługowej.
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności ((OK,BŁĄD,POMIŃ|xxx))", required=N
::
::# kind=WY, symbol=SUB, type=STRING, name=Temat, required=N
::# kind=WY, symbol=TO, type=MEMO, name=Odbiorca, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::
_args:=exec('szk_prac_main_a','phr_poczta');
_args.warunek:="exec('warunek','!psz_sdo_rpow',{? var_pres('_a')=type_of(0) & _a || _a || 0 ?})";
_args.tytul:="exec('tytul','!psz_sdo_rpow')";
_args.tresc:="exec('tresc','!psz_sdo_rpow')";
params_exec('szk_prac_main','phr_poczta',_args)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Powiadomienie o szkoleniu - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=params_exec('szk_prac_desc','phr_poczta',,1);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Wyślij powiadomienie o szkoleniu: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Wyślij powiadomienie o szkoleniu: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Wyślij powiadomienie o szkoleniu: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Wyślij powiadomienie o szkoleniu'@@
?}


\warunek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Formuła weryfikuje, czy bieżący rekord tabeli SZK_PRAC może być podstawą wysłania powiadomienia.
::   WE: [_a] [INTEGER] - tryb wykonania formuły:
::                          0(domyślnie)   - bez podania niespełnionego warunku (zwraca 0/1)
::                        <>0(opcjonalnie) - zostanie zwrócona tablica z wynikiem sprawdzenia oraz ewentualnej
::                                           informacji o pierwszym warunku jaki nie został spełniony
::   WY: 0/1 gdy nie podano argumentu _a lub gdy przekazano 0 lub tablica z elementami RESULT - wynik sprawdzenia oraz
::       MSG - ewentualna informacja o pierwszym warunku jaki nie został spełniony
::----------------------------------------------------------------------------------------------------------------------
_mode:={? var_pres('_a')=type_of(0) & _a || _a || 0 ?};
_ret:={? _mode || obj_new('RESULT','MSG') || 0 ?};

_dt:=date();
SZK_OPIS.cntx_psh();
SZK_OPIS.prefix();
{? SZK_PRAC.SZKOL().get()
|| _email:=SZK_PRAC.EMAIL<>'T';
   _email_po:=SZK_PRAC.EMAIL_PO='X';
   _dt_b:=_dt<=SZK_OPIS.OD;
   _monit_b:=SZK_OPIS.MONIT_OD<=_dt;
   _potw_do:=(SZK_OPIS.POTW_DO=date(0,0,0) | _dt<=SZK_OPIS.POTW_DO);
   _result:=_email & _email_po & _dt_b & _monit_b & _potw_do;
   {? ~_mode
   || _ret:=_result
   || _ret.MSG:='';
      {? ~_result
      || _continue:=_email;
         {? ~_continue
         || _ret.MSG:='Powiadomienie o szkoleniu zostało już wysłane do uczestnika.'@
         || {? ~(_continue:=_email_po)
            || _ret.MSG:='Uczestnik otrzymał już potwierdzenie uczestnictwa.'@
            || _info:='Powiadomienie o wybranym szkoleniu nie zostanie wysłane na adres: %1.'@[USERS.EMAIL];
               {? ~(_continue:=_dt_b)
               || _ret.MSG:='%1\n%2'@['Szkolenie zostało już rozpoczęte.'@,_info]
               || {? ~(_continue:=_monit_b)
                  || _ret.MSG:='%1\n%2'@
                        ['Okres wysyłania monitów do uczestników szkolenia został już rozpoczęty.'@,_info]
                  || {? ~(_continue:=_potw_do)
                     || _ret.MSG:='%1\n%2'@['Upłynął termin możliwości potwierdzenia uczestnictwa w szkoleniu.'@,_info]
                     ?}
                  ?}
               ?}
            ?}
         ?}
      ?};
      _ret.RESULT:=_result
   ?}
?};
SZK_OPIS.cntx_pop();

_ret


\tytul
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Formuła, dla bieżącego rekordu tabeli SZK_PRAC, zwraca napis będący tytułem powiadomienia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tytul:='Szkolenie - informacja dla uczestnika';
OSOBA.cntx_psh();
OSOBA.prefix();
P.cntx_psh();
P.prefix();
{? SZK_PRAC.P().OSOBA().get()
|| _tytul:=
      {? OSOBA.PLEC='K'
      || 'Zostałaś zapisana na szkolenie'
      || 'Zostałeś zapisany na szkolenie'
      ?};
   SZK_OPIS.cntx_psh();
   SZK_OPIS.prefix();
   {? SZK_PRAC.SZKOL().get()
   || _tytul+=' ('+SZK_OPIS.OD$4+{? SZK_OPIS.OD=SZK_OPIS.DO || '' || ' - '+SZK_OPIS.DO$4 ?}+')'
   ?};
   SZK_OPIS.cntx_pop()
?};
P.cntx_pop();
OSOBA.cntx_pop();
_tytul


\tresc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Formuła, dla bieżącego rekordu tabeli SZK_PRAC, zwraca napis będący treścią powiadomienia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SZK_OPIS.cntx_psh();
SZK_OPIS.prefix();
_ret:=
   '<center><h4>Szkolenie</h4></center>'
   'Dzień dobry.<br>'
   'Zgodnie z procedurami obowiązującymi w <b>'+KST.NAZWA+'</b> w zakresie szkoleń pracowniczych, '
   'informujemy, że zapisano Cię na szkolenie.<br>'+
   exec('szk_opis_info','phr_poczta',SZK_PRAC.SZKOL)+
   {? SZK_PRAC.SZKOL().POTW_DO=date(0,0,0)
   || ''
   || '<br><br>'
      '<b>Do dnia <span style="font-weight: 1.2em;">'+$SZK_PRAC.SZKOL().POTW_DO+'</span> '
      'proszę o potwierdzenie na portalu pracowniczym chęci uczestnictwa w szkoleniu.</b><br>'
   ?}+
   '<br><br>'
   'Ta wiadomość została wygenerowana automatycznie - prosimy na nią nie odpowiadać.';
SZK_OPIS.cntx_pop();

_ret

:Sign Version 2.0 jowisz:1055 2023/11/27 12:32:02 09bf0f5bb19118f74f1e2650de0e9a1bacac364d01c6b9c25876ec463b1a95ade585cadc4ed4d541554f9cb033db6bc4331e978a1d800a97b787145305640f2f90de5e3189d716efd15e28719c05c0e2b84b507870543bf44b43718f175aaa903aacf0709af6ef8eee0bf83ea6b09559f51e56b9c6fbc59af32c7c43fde16a87

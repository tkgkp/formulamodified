:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zds_razs.fml
:: Utworzony: 26.10.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły czynności LSP_ZKN_RAZS - Archiwizacja zamówienia sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE, symbol=ZK_N,     type=_ZK_N,  name=Zamówienie sprzedaży,               required=N,    keyref=T
::# kind=WE, symbol=DNI,      type=NUMBER, name=Ilość dni wstecz do archiwizacji,   required=N
::# kind=WE, symbol=PROCENT,  type=NUMBER, name=Procent realizacji do archiwizacji, required=N
::# kind=WE, symbol=ODDZIAL,  type=STRING, name=Kod oddziału do archiwizacji,       required=N,    fml_val="exec('oddzial','!lsp_zkn_razs')"
::# kind=WE, symbol=MAXZAM,   type=NUMBER, name=Maksymalna liczba do archiwizacji,  required=N
::# kind=WY, symbol=ZK_N,     type=_ZK_N,  name=Zamówienie sprzedaży,               required=N,    keyref=T
::# kind=WY, symbol=IL_SERV,  type=NUMBER, name=Ilość zarchiwizowanych zamówień,    required=N,    keyref=N
::# properties=SERVICE

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

exec('init_zkn','lsp');
{? var_pres('DNI',_in)<>type_of(0) | _in.DNI<0
|| _in.DNI:=-1;
   _mp.save('IN')
?};
{? var_pres('PROCENT',_in)<>type_of(0) | _in.PROCENT<0 | _in.PROCENT>100
|| _in.PROCENT:=-1;
   _mp.save('IN')
?};
{? var_pres('MAXZAM',_in)<>type_of(0) | _in.MAXZAM<=0
|| _in.MAXZAM:=-1;
   _mp.save('IN')
?};
_typ_oddzial:=var_pres('ODDZIAL',_in)<>type_of('');
{? _typ_oddzial | (+_in.ODDZIAL)>1 | ~exec('isLetter','#string',_in.ODDZIAL,1)
|| _in.ODDZIAL:={? _typ_oddzial || '' || '!' ?};
   _mp.save('IN')
?};
_max:=_in.MAXZAM;

:: WSTĘPNE WALIDACJE
_clean_result:=params_exec('clean','!lsp_zkn_razs',_mp,_in);
_can_continue:=_clean_result.RESULT;
{? _can_continue=0
|| return()
?};

_akcja:=_mp.akcja();
_auto:=_akcja<>'Archiwizuj' & _mp.isAutoRun();
_service:=_mp.isService();
_grupa:=_mp.isGroup();

{? _service & ((_in.DNI<0 & _in.PROCENT<0) | _in.ODDZIAL='!')
|| _mp.error('Nieprawidłowe parametry dla czynności serwisowej.')
|? ~_service & ~(var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N)
|| _mp.error('Brak wymaganego parametru ZK_N.')
|| ZK_N.cntx_psh();
   ZK_N.prefix();
   _ok:=1;
   {? ~_service
   || {? ~ZK_N.seek(_in.ZK_N)
      || _ok:=0;
         _mp.error('Nie znaleziono zamówienia sprzedaży.')
      |? ZK_N.T().R<>'Z'
      || _ok:=0;
         _mp.error('Błędny parametr wejściowy ZK_N. Wymagane zamówienie sprzedaży.')
      ?}
   ?};
   {? _ok
   || _mp.trigRef('ZK_N',,0,,exec('kind_out','#b_port'),'ZK_N');
      params_set('mp',_mp,'in',_in,'out',_out);
      {? _akcja='Archiwizuj' | _auto | _service
      || exec('archiwizuj','!lsp_zkn_razs',{? _service || 2 || _auto ?},_in.DNI,_in.PROCENT,_in.ODDZIAL,_grupa,_max)
      |? _mp.pathTodo() | _mp.pathArea() & _akcja=''
      || _win_red:=exec('zknag_win_edit','zamsiw_nag','RED');
         _ff:="params_exec('zknag_pozycje_todo','!lsp_zkn_razs')";
         ZK_N.win_ebtn(_win_red,'text=&Pozycje,btn_label_align=center,panel=bottom,align=begin,display=1',_ff);
         _ff:="params_exec('zknag_archiwum_todo','!lsp_zkn_razs'); 'key:Esc'";
         ZK_N.win_ebtn(_win_red,'text=Arc&hiwizuj,btn_label_align=center,panel=bottom,align=end,display=1',_ff);
         _ff:="'key:Esc'";
         ZK_N.win_ebtn(_win_red,'text=&Anuluj,btn_label_align=center,panel=bottom,align=end,display=1',_ff);
         ZK_N.win_edit(_win_red);
         ZK_N.display()
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   ZK_N.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZK_N',_in)<>type_of(~~) & _in.ZK_N
|| 'Zarchiwizuj zamówienie sprzedaży: %1'@@[exec('record','#to_string',_in.ZK_N)]
|| ''
?}


\zknag_archiwizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Archiwizuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_tab:=ZK_N.sel_aget();
ZK_N.sel_adel();

{? _tab.size()
|| _ok:=FUN.ask('Ilość zamówień do archiwizacji: %1.\n'
               'Operacja może być czasochłonna.\n\nCzy kontynuować?'@[$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LSP_ZKN_RAZS';
   _params.UIDREF:=ZK_N.uidref();
   _params.AKCJA:='Archiwizuj';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZK_N',ZK_N.ref());

   exec('mp_run','#b__box',_params)
|? _ok=1
|| exec('ini_kom','#message','Archiwizacja zamówień'@);
   KOMM.init(250,,'Archiwizacja zamówień - menadżer procesów');
   exec('zam_arch_InitRes','zamsiw_wspolne');
   ZK_N.cntx_psh();
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? (ZK_N.clear(); ZK_N.seek(_tab.REF,))
         || _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:='LSP_ZKN_RAZS';
            _params.UIDREF:=ZK_N.uidref();
            _params.GRUPA:='T';
            _params.AKCJA:='Archiwizuj';
            _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
            exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZK_N',ZK_N.ref());

            exec('mp_run','#b__box',_params);
            obj_del(_params)
         ?};
         _tab.next()
      !}
   ?};
   exec('zam_arch_DoneRes','zamsiw_wspolne');
   ZK_N.cntx_pop();
   exec('end_kom','#message');
   KOMM.select()
?};
obj_del(_tab)


\zknag_archiwum_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument sprzedaży - Archiwizuj z todo
::   WE: params_get()
::----------------------------------------------------------------------------------------------------------------------
params_exec('archiwizuj','!lsp_zkn_razs');
''


\zknag_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('zam_poz','zamsiw_poz',1,0);
''


\archiwizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Archiwizacja zamówienia sprzedaży
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie) 2-serwisowa
::       [_b] - ilość dni wstecz
::       [_c] - procent realizacji zamówienia
::       [_d] - oddzial lub pusty string
::       [_e] - czy grupa rekordów
::       [_f] - maksymalna liczba zamówień do pojedynczej archiwizacji
::       params_get()
::  OLD: \zam_arch/zk2.fml
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_dni:={? var_pres('_b')=type_of(1) || _b || 0 ?};
_procent:={? var_pres('_c')=type_of(1) || _c || 0 ?};
_oddzial:={? var_pres('_d')=type_of('') || _d || '' ?};
_grupa:={? var_pres('_e')=type_of(0) || _e || 0 ?};
_maxzam:={? var_pres('_f')=type_of(0) & _auto=2 || _f || -1 ?};

exec('declare','tech_doc');

_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_result:=0;

_txt:={? _auto<>2 & ZK_N.A='A' & ZK_N.OBI_POW<>''
      || '\n\nUwaga. Zamówienie powiązane z dokumentem w obiegu.'@
      || ''
      ?};
{? _auto=2
||
:: czynność serwisowa
   exec('openz_psh','open_tab');
   _today:=date();
   _oddz:=ST.ODDZ;
   ODDZ.cntx_psh();
   ODDZ.index('KOD2');
   {? _oddzial<>'' || ODDZ.prefix(_oddzial,) || ODDZ.prefix() ?};
   {? ODDZ.first()
   || {!
      |? ST.ODDZ:=ODDZ.KOD;
         exec('openz','open_tab',ODDZ.KOD+'__');
         ZK_N.cntx_psh();
         ZK_N.index('ZAM_KL');
         ZK_N.prefix('Z');
         {? ZK_N.first()
         || {!
            |? _ref:=ZK_N.ref();
               _oki:=ZK_N.next();
               ZK_N.cntx_psh();
               ZK_N.clear();
               {? (#_ref)>0 & ZK_N.r_lock(1,1,1,#_ref,ref_name(_ref)) & (ZK_N.seek(_ref);ZK_N.r_unlock();1)
                 & {? _dni>=0 || ZK_N.DP<=(_today-_dni) || 1 ?} & {? _procent>=0 || ZK_N.PR>=_procent || 1 ?}
               || _old:=ZK_N.uidref();
                  _result+=(_new:=exec('zam_arch','zamsiw_wspolne'))<>'';
                  exec('uidref_update','#b__box',_old,exec('FindAndGet','#table',ZK_N,_new,,"uidref()",''));
                  {? _new<>''
                  ||
:                    Uruchamian cleanera - czyszcze pozostałe po zamówieniu zadania na TODO
                     exec('clean_record','#b__box',exec('FindAndGet','#table',ZK_N,_new,,"uidref()",''),0)
                  ?}
               ?};
               ZK_N.cntx_pop();
               _oki & (_maxzam<=0 | _result<_maxzam)
            !}
         ?};
         ZK_N.cntx_pop();
         ODDZ.next()
      !}
   ?};
   ODDZ.cntx_pop();
   exec('openz_pop','open_tab');
   ST.ODDZ:=_oddz;
   _out.ZK_N:=null();
   _out.IL_SERV:=_result;
   _mp.save(,_out);
   _mp.done()
|? _auto | _grupa | FUN.ask('Zarchiwizować zamówienie sprzedaży %1?%2'@[ZK_N.SYM,_txt])
|| _old:=ZK_N.uidref();
   _new:=exec('zam_arch','zamsiw_wspolne',{? _grupa || 2 || 1 ?});
   ZK_N.cntx_psh();
   {? _new<>''
   || _result:=1;
      exec('uidref_update','#b__box',_old,exec('FindAndGet','#table',ZK_N,_new,,"uidref()",''));
:     Uruchamian cleanera - czyszcze pozostałe po zamówieniu zadania na TODO
      exec('clean_record','#b__box',exec('FindAndGet','#table',ZK_N,_new,,"uidref()",''),0);
      _out.ZK_N:=exec('FindAndGet','#table',ZK_N,_new,,,null())
   ?};
   _out.IL_SERV:=_result;
   _mp.save(,_out);
   ZK_N.cntx_pop();
   _mp.done()
?};
_result


\zknag_przywroc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Archiwizuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_tab:=ZK_N.sel_aget();
ZK_N.sel_adel();

{? _tab.size()
|| _ok:=FUN.ask('Ilość zamówień do przeniesienia z archiwum: %1.\n'
               'Operacja może być czasochłonna.\n\nCzy kontynuować?'@[$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| exec('declare','tech_doc');
   exec('zam_arch','zamsiw_wspolne')
|? _ok=1
|| exec('ini_kom','#message','Przywrócenie zamówień');
   exec('zam_arch_InitRes','zamsiw_wspolne');
   exec('declare','tech_doc');
   ZK_N.cntx_psh();
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? (ZK_N.clear(); ZK_N.seek(_tab.REF,))
         || exec('zam_arch','zamsiw_wspolne',2)
         ?};
         _tab.next()
      !}
   ?};
   exec('zam_arch_DoneRes','zamsiw_wspolne');
   ZK_N.cntx_pop();
   exec('end_kom','#message')
?};
obj_del(_tab)


\oddzial
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na wartość parametru ODDZIAL
::   WY: ODDZ.KOD
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
ODDZ.cntx_psh();
ODDZ.index('KOD');
ODDZ.prefix();
ODDZ.win_sel('SEL');
{? ODDZ.select() || _wyn:=ODDZ.KOD ?};
ODDZ.cntx_pop();
_wyn


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zam_clean','zamsiw_nag',_mp,_in)


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 0d2a2a58d765d6b750026b8a2404401b340f3f2a2e77fff0efb7166d37e030969bc3c1b6ccb90e2f61d1d89cdea2847ab77b3149f8f87fae14ac2fa97499485380397358c95b3152b9b8c414724f6b2ccf970f4980c09f23f47ed21a3b9771adf5170e6d3e18be5df1c78e29a38e9872f4f4ef31ebd9f1b4a3dafb48e5603fb4

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prjo.fml
:: Utworzony: 11.05.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_DOS_PRJO - Rej. znajomości języków obcych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rej. znajomości języków obcych - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE, symbol=OSOBA, type=_OSOBA, name=Wskazanie osoby, required=T, keyref=T

_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.OSOBA);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_dos_prjo')

|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność '+_mp.buf_act.UID+' nie obsługuje akcji '+_do+'.'
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_prjo',_in.OSOBA);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rej. znajomości języków obcych - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj znajomość języków obcych: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj znajomość języków obcych: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj znajomość języków obcych: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj znajomość języków obcych'@@
?}


\zj_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Kontrola poprawności danych.
::   WE:
::   WY: zgodna ze specyfikacją dla akcji "rekord po"
::----------------------------------------------------------------------------------------------------------------------
: sprawdź wypełnienie i unikalność
_chk:=__CHK.table(ZJ,-menu_txt='popraw',,'JO','SZ');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & ~_chk)
:  porażka podstawowego testu
|| return(_chk)
?};

: dodatkowe testy poprawności
_nomin:=' nie może być mniejsza od zera.';
{? ZJ.OC<0   || FUN.emsg('Ocena ogólna znajomości'+_nomin); 'OC'
|? ZJ.OC_M<0 || FUN.emsg('Ocena znajomości w mowie'+_nomin); 'OC_M'
|? ZJ.OC_P<0 || FUN.emsg('Ocena znajomości w piśmie'+_nomin); 'OC_P'
|| ''
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie osoby
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!pkd_dos_prjo');

{? var_pres('_a')<>type_of(null) | _a=null
|| return(_err_msg)
?};

OSOBA.cntx_psh();
OSOBA.prefix();
{? ~OSOBA.seek(_a)
|| OSOBA.cntx_pop();
   return(_err_msg)
?};

ZJ.cntx_psh();
ZJ.index('ZNJEZ');
ZJ.prefix(_a);
ZJ.win_sel('WERI');
ZJ.win_edit('RED');
_result:=ZJ.select();
ZJ.cntx_pop();
OSOBA.cntx_pop();
_result


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli ZJ z doklejonym oknem redagowania tabeli OSOBA);
::       - w ramach obszaru roboczego (okno wertowania tabeli ZJ jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=ZJ
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie znajomości języków obcych niemożliwe.\nBłędne wskazanie osoby.'


\zj_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed"
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',ZJ.uidref())


\zj_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.42]
:: OPIS: Obsługa akcji "Załączniki - przed" w oknach tabeli ZJ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','ZJ',exec('chk_role','#b__box',OPERATOR.USER,'PKD_DOS_PRJO'))


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.14]
:: OPIS: Przygotowanie okna wertowania znajomości języków obcych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized';
_wnd:=ZJ.grp_make('Znajomość języków obcych'@,"grp_edisp(OSOBA,'INFO')",,,,,,_mode);
ZJ.grp_edit(_wnd,OSOBA,'INFO',,,,,,_mode);
ZJ.grp_splt(_wnd,,'horizontal','zj');
ZJ.grp_sel(_wnd,,'WER',,,,,,,,,,_mode,,1);
_wnd

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 26b424e17b486cb4679641a0c571c8fccaaf19b5bca1801b7e6e8ff1aa3e1d7496db7570b9882f1752eafc4c88ae00ee35dd238e0cdb7fc68abed60d23482ab07998a7f0d8552132f39d999986a1337fc205681d3e4db520bf168af10ab13576f3c9cfd615d0be631e881ea5847baffc37f633cad2fc99763d6e1cc3cd6ad000

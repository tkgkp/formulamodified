:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_hbd_ddpw.fml
:: Utworzony: 13.11.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_HBD_DDPW - Definicje podwersji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Definicje podwersji - główna formuła czynności CTR_HBD_DDPW
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\be_k_podwer_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakacji K_PODWER.WRITE
::  OLD: \be_k_podwer_w/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_PODWER.READ<>'N'


\k_podwer_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed dolacz, popraw K_PODWER
::   WE: _a - 1-dolacz 0-popraw
::  OLD: \k_podwer_edit/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_PODWER.win_edit('RED');
{? _a
|| K_PODWER.blank(1);
   K_PODWER.USERS:=OPERATOR.USER;
   K_PODWER.K_WERSJE:=K_WERSJE.ref()
|? K_PODWER.USERS<>OPERATOR.USER & 0
|| FUN.info('Podwersja innego użytkownika.\nPoprawienie niemożliwe.'@);
   return(0)
?};
{? (_a=1 |
    _a=0 &
    (K_PODWER.USERS=OPERATOR.USER |
     K_PODWER.USERS<>OPERATOR.USER & FUN.ask('Podwersja innego użytkownika.\nCzy chcesz ją edytować?'@)
    )
   ) &
   K_PODWER.edit("exec('ar_k_podwer','!ctr_hbd_ddpw')")
|| {? _a
   || K_PODWER.add()
   || K_PODWER.put()
   ?}
?}


\ar_k_podwer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Rekord pod K_PODWER
::  OLD: \ar_k_podwer/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.record(K_PODWER,,'SYM');
{? _chk=''
|| _ref:=K_PODWER.ref();
   _pop:=-menu_txt()='popraw';
   K_PODWER.cntx_psh();
   K_PODWER.index('UNIK');
   K_PODWER.prefix(K_WERSJE.ref(),K_PODWER.USERS,K_PODWER.SYM,);
   {? K_PODWER.first() & (~_pop | _ref<>K_PODWER.ref())
   || FUN.info('Istnieje wersja %1.'@ [K_PODWER.SYM]);
      _chk:='SYM'
   ?};
   K_PODWER.cntx_pop()
?};
_chk


\bd_k_podwer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed usun K_POD_WER
::  OLD: \bd_k_podwer/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_PODWER.USERS<>OPERATOR.USER
|| FUN.info('Podwersja innego użytkownika.\nUsunięcie niemożliwe.'@)
|| K__NAG.index('K_PODWER');
   K__NAG.prefix(K_PODWER.ref());
   _jest:=K__NAG.first();
    {? _jest
    || {? K_PODWER.USERS<>OPERATOR.USER
       || FUN.ask('Podwersja innego użytkownika zawiera wartości.\n'
                  'Czy usunąć podwersję  z wartościami ?'@)
       ||  FUN.ask('Podwersja  zawiera wartości.\n Czy usunąć podwersję  z wartościami ?'@)
       ?}
    || FUN.ask('Czy usunąć podwersję ?'@)
    ?};
    do();
      {? K__NAG.first()
      || _tabarch:=K__POZ.names();
         _tabwart:=K__WAR.names();
         {!
         |? exec('usu_mod0','control',_tabarch,_tabwart,1);
            K__NAG.first()
         !}
      ?};
      K_PODWER.del();
      end()
?}


\ae_k_podwer_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakacji K_PODWER.READ
::  OLD: \ae_k_podwer_r/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_PODWER.READ='N'
|| K_PODWER.WRITE:='N';
   win_disp()
?};
1

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:39 71de4a1b3c3ddd9503b012b5b185e27e285796157d8dcca9d782f680ce65e8309a3586a862b94c6eeccc47536b8ff3b2283caf2acf91402d22c334839b8b208a5e677c956c71872371a0e2c374ddc8d182fb87ed1e204f79e8a5eaa8de30efae80f09dfcb4410729b622ccef94517631847c30d124e9e3d4510547f4341a2500

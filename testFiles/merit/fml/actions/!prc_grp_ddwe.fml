:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_grp_ddwe.fml
:: Utworzony: 30.05.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PRC_GRP_DDWE - We/wy dla grupy pracowników.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Wybór pracowników, dla których ma być wykonane grupowe wprowadzenie wejść i wyjść.
::   WE:
::   WY:
::  OLD: \start/grupwewy.fml
::       \funkcja/grupwewy.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PRC';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_args.F_ZATR:={? +P_FILTER.F_ZATR().KOD || P_FILTER.F_ZATR().KOD || '*T' ?};
_args.VIEW:=P_FILTER.STATUS;
_PRAC:=exec('wybierz','pracownik',_args).P;

{? _PRAC.first()
||
:: tabela pomocnicza do grupowego wprowadzania wejść i wyjść
   _TAB:=tab_tmp(3,
      'WYKONAJ','STRING[1]','Wykonaj',
      'NAZWISKO','STRING[20]','Nazwisko',
      'PIERWSZE','STRING[20]','Imię',
      'PESEL','STRING[11]','PESEL',
      'T','STRING[10]','Nr teczki',
      'REF','INTEGER','Ref',
      'GD','TIME','Godzina wejścia',
      'GDW','TIME','Godzina wyjścia',
      'BL','STRING[1]','Zablokowany (T/N)'
   );
:: okno przeglądania wprowadzanych grupowo wejść i wyjść
   _TAB.fld_attr(,2);
   _wer:=_TAB.mk_sel('Wejścia i Wyjścia współpracowników'@,'N',0,'#grupwewy_wer',,,,,'U');
   _TAB.win_fld(_wer,,'T',,,,,,'Nr teczki'@,,'Identyfikator współpracownika'@);
   _TAB.win_fld(_wer,,'NAZWISKO',,,,,,'Nazwisko'@,,'Nazwisko współpracownika'@);
   _TAB.win_fld(_wer,,'PIERWSZE',,,,,,'Imię'@,,'Imię współpracownika'@);
   _TAB.win_fld(_wer,,'GD',,,,,,'Godzina wejścia'@,,'Godzina wejścia do rejestracji czasu pracy'@);
   _TAB.win_fld(_wer,,'GDW',,,,,,'Godzina wyjścia'@,,'Godzina wyjścia do rejestracji czasu pracy'@);
   _TAB.win_fld(_wer,,'BL',,,,,,'Zablokowany'@,,'Założona blokada (T/N)'@,2,,"'T'","'N'");
   _TAB.win_act(_wer,0,'Formuła','Zmień'@@,,'Zmiana godziny wejścia lub wyjścia'@,
      "  cur_tab().win_edit(params_get().WINDOW);
         {? cur_tab().edit($(params_get().CHECK))
         || cur_tab().put()
         ?}
      ",,1,,,,'Z'
   );
   _TAB.win_act(_wer,,'Rekord',,,,
      "  cur_tab().actions_grayed(cur_win(),{? cur_tab().BL='T' || 'Z' || '' ?})
      "
   );
   _TAB.win_act(_wer,0,'Formuła','Zat&wierdź'@@,,,,"sel_exit()");
   _TAB.win_act(_wer,,'Szukaj');
   _TAB.win_act(_wer,,'Kolejność');
   _TAB.win_btn(_wer,'text=%1,icon=xwin16.png:13,panel=bottom,align=end'['&OK'@],'menu:W');
   _TAB.win_btn(_wer,'text=%1,icon=xwin16.png:14,panel=bottom,align=end'['&Anuluj'@],'key:Esc');
   _TAB.win_sel(_wer);
:: okno redagowania do poporawiania godziny wejścia i wyjścia w grupowym wprowadzaniu wejść i wyjść
   _red:=_TAB.mk_edit('Godzina wejścia i wyjścia'@,,'#grupwewy_red');
   _TAB.win_esep(_red,'Parametry'@);
   _TAB.win_efld(_red,,'GD',,,8,,,'Godzina wejścia'@,,'Godzina wejścia do rejestracji czasu pracy'@);
   _TAB.win_efld(_red,,'GDW',,,8,,,'Godzina wyjścia'@,,'Godzina wyjścia do rejestracji czasu pracy'@);
   exec('ok_esc','#window',_TAB,_red);
:: sprawdzenie poprawności wprowadzonych godzin
   _ae:='{? cur_tab().GD>time(24,0,0) '+
        '|| FUN.emsg(\'%1\'); \'GD\' '['Godzina wejścia nie może być większa od godziny 24.'@]+
        '|? cur_tab().GDW>time(24,0,0) '+
        '|| FUN.emsg(\'%1\'); \'GDW\' '['Godzina wyjścia nie może być większa od godziny 24.'@]+
        '|? cur_tab().GDW<=cur_tab().GD '+
        '|| FUN.emsg(\'%1\'); \'GDW\' '['Godzina wejścia nie może być większa od godziny wyjścia.'@]+
        '|| 1 '+
        '?}';
   params_set('WINDOW',_red,'CHECK',_ae);
:: uzupełnienie tabeli do grupowego wprowadzania wejść i wyjść
   P.cntx_psh();
   {!
   |? {? P.seek(_PRAC.SQL,,1)
      || _TAB.NAZWISKO:=P.OSOBA().NAZWISKO;
         _TAB.PIERWSZE:=OSOBA.PIERWSZE;
         _TAB.PESEL:=OSOBA.PESEL;
         _TAB.T:=P.T;
         _TAB.REF:=#P.ref();
         _TAB.WYKONAJ:='T';
         _TAB.GD:=_TAB.GDW:=time(0,0,0);
         _TAB.add()
      ?};
      _PRAC.next()
   !};
:: edycja parametrów dla akcji grupowego wprowadzania wejść i wyjść
   R_OPCZYT.cntx_psh();
   R_OPCZYT.win_sel('SLO');
   R_OPCZYT.index('R_OPCZSL');
   R_OPCZYT.prefix('T');
   exec('ustaw','!prc_grp_ddwe');
   REJ_WEWY.win_edit('RED');
   {? REJ_WEWY.edit(
         "  _spr:=__CHK.record2(REJ_WEWY,'DZ',,'DZK',,'CZ',);
            {? _spr=''
            || _spr:=($(params_get().CHECK))()
            ?};
            _spr
         "
      )
   || exec('ustaw','!prc_grp_ddwe');
      {? _TAB.last()
      || {!
         |? _TAB.GD:=REJ_WEWY.GD;
            _TAB.GDW:=REJ_WEWY.GDW;
            _prac:={? P.seek(_TAB.REF,,1) || P.ref()|| null() ?};
            _TAB.BL:={? exec('isBlokada','prc_rozlicz',_prac,REJ_WEWY.DZ) || 'T' || 'N' ?};
            _TAB.put();
            _TAB.prev()
         !};
         {? _TAB.select()
         || {? FUN.ask('Czy na pewno rozpocząć proces wprowadzania danych dla określonej grupy współpracowników?'@)
            || exec('zapisz','!prc_grp_ddwe',_TAB)
            ?}
         || FUN.info('Operacja przerwana przez operatora.'@)
         ?}
      ?}
   || FUN.info('Operacja przerwana przez operatora.'@)
   ?};
   P.cntx_pop();
   R_OPCZYT.cntx_pop()
?};
~~


\ustaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS: Ustawienie domyślnych wartości dla zmiennej REJ_WEWY.
::   WE:
::   WY:
::  OLD: \ustaw/grupwewy.fml
::----------------------------------------------------------------------------------------------------------------------
R_WEWY.cntx_psh();
R_WEWY.index('R_TP_RD');
R_WEWY.prefix();
_wejwyj:='WEJŚCIE';
_w:='';
{! _ind:=0..1
|! {? ($('REJ_WEWY.ST'+_w))()=0
   || {? R_WEWY.find_key(_wejwyj,'normalne')
      || ($('REJ_WEWY.ST'+_w))():=R_WEWY.ST;
         ($('REJ_WEWY.TP'+_w))():=R_WEWY.TP;
         ($('REJ_WEWY.RD'+_w))():=R_WEWY.RD
      ?}
   ?};
   _wejwyj:='WYJŚCIE';
   _w:='W'
!};
R_WEWY.cntx_pop();
~~


\zapisz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS:   Wprowadzenie danych do tabeli wejsc wyjsc R_REJ_WW.
::   WE:   _a - tabela tymczasowa obsługująca czynność grupowego przypisania wejść i wyjść
::   WY:
::  OLD: \zapisz/grupwewy.fml
::----------------------------------------------------------------------------------------------------------------------
_TAB:=_a;
_TAB.first();
PROGRESS.set(_TAB.size(),'Trwa aktualizacja tabeli wejść/wyjść.'@);
R_REJ_WW.cntx_psh();
MASK.Use('R_REJ_WW',REJ_WEWY.DZ~1,REJ_WEWY.DZ~2);
R_REJ_WW.clear();
P.index('PRACOIP');
P.prefix(exec('ref_firma','ustawienia'));
{!
|? {? _TAB.GD<>*0 & _TAB.BL='N'
   || {? P.seek(_TAB.REF,)
      || _wewy:='';
         {! _ind:=0..1
         |! R_REJ_WW.blank();
            R_REJ_WW.ZM:='R';
            R_REJ_WW.CZ:=REJ_WEWY.CZ;
            R_REJ_WW.DZ:=REJ_WEWY.DZ;
            R_REJ_WW.GD:={? +_wewy || _TAB.GDW || _TAB.GD ?};
            R_REJ_WW.ST:=($('REJ_WEWY.ST'+_wewy))();
            R_REJ_WW.TP:=($('REJ_WEWY.TP'+_wewy))();
            R_REJ_WW.RD:=($('REJ_WEWY.RD'+_wewy))();
            R_REJ_WW.DZK:=REJ_WEWY.DZK;
            _wewy:='W';
            R_REJ_WW.add(1)
         !}
      ?}
   ?};
   PROGRESS.next();
   _TAB.next()
!};
PROGRESS.close();
VAR_EDIT.D_OD:=VAR_EDIT.D_DO:=REJ_WEWY.DZ;
exec('ak_tabdz','prc_wewy',1,_TAB);
R_REJ_WW.cntx_pop();
~~


\rej_wewy_st_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS:  Wyświetlenie słownika statusów wejścia i wyjścia pola ST zmiennej REJ_WEWY.
::   WE:
::   WY:
::  OLD: \WYBWW_F3/grupwewy.fml
::----------------------------------------------------------------------------------------------------------------------
R_WEWY.cntx_psh();
R_WEWY.win_sel('SLO');
{? R_WEWY.select()
|| {? cur_afld()='ST'
   || REJ_WEWY.ST:=R_WEWY.ST;
      REJ_WEWY.TP:=R_WEWY.TP;
      REJ_WEWY.RD:=R_WEWY.RD
   || REJ_WEWY.STW:=R_WEWY.ST;
      REJ_WEWY.TPW:=R_WEWY.TP;
      REJ_WEWY.RDW:=R_WEWY.RD
   ?}
|| {? cur_afld()='ST'
   || REJ_WEWY.TP:='';
      REJ_WEWY.RD:='';
      REJ_WEWY.ST:=0
   || REJ_WEWY.TPW:='';
      REJ_WEWY.RDW:='';
      REJ_WEWY.STW:=0
      ?}
?};
R_WEWY.cntx_pop();
{? cur_afld()='ST'
|| REJ_WEWY.ST
|| REJ_WEWY.STW
?}


\rej_wewy_st_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS:  Formuła po redakcji pola ST i STW zmiennej REJ_WEWY, kontrola poprawnosci edytowanych danych.
::   WE:
::   WY:
::  OLD: \RWWSTpor/grupwewy.fml
::----------------------------------------------------------------------------------------------------------------------
R_WEWY.cntx_psh();
R_WEWY.index('R_STATUS');
_st:={? cur_afld()='ST' || REJ_WEWY.ST || REJ_WEWY.STW ?};
{? R_WEWY.find_key(_st)
|| {? cur_afld()='ST'
   || REJ_WEWY.TP:=R_WEWY.TP;
      REJ_WEWY.RD:=R_WEWY.RD
   || REJ_WEWY.TPW:=R_WEWY.TP;
      REJ_WEWY.RDW:=R_WEWY.RD
   ?};
   _result:=1
|| _ask:=FUN.choice('Brak pozycji w słowniku.'@,2,'Powtórzenie'@,'Wyświetlenie &słownika'@);
   {? _ask=1
   || _result:=0
   |? _ask=2
   || _result:=+exec('rej_wewy_st_f3','!prc_grp_ddwe')
   || {? cur_afld()='ST'
      || REJ_WEWY.TP:='';
         REJ_WEWY.RD:='';
         REJ_WEWY.ST:=0
      || REJ_WEWY.TPW:='';
         REJ_WEWY.RDW:='';
         REJ_WEWY.STW:=0
      ?};
      _result:=1
   ?}
?};
R_WEWY.cntx_pop();
_result


\rej_wewy_cz_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.28]
:: OPIS: Wyświetlenie słownika czytników w polu CZ zmiennej REJ_WEWY.
::   WE:
::   WY:
::  OLD: \WYBCZYTN/grupwewy.fml
::----------------------------------------------------------------------------------------------------------------------
{? R_OPCZYT.select()
|| REJ_WEWY.CZ:=R_OPCZYT.K
?}

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:01 abc14bce4d42cdf1350b70d1eb298f5ec2ae013d996aa4573d758e968e8edee42e81ce2b2aebd31f37d421a9895a00eadddddde6801d28b76caaa0b200b3dac799b9f2b8590d96c462ccc9af6c502fb8e3cf1043ba5135a060dded380527d1203b9758f7963ef7b60324fdf68ffee32a84302ff724ecf42c1d1260bf4ab6a18e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ttar.fml
:: Utworzony: 02.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartosc: Formuły czynności ZWS_PAR_TTAR - Taryfikatory kosztów
::            Obsługa tabel: TTOPER, TTOUT, TKAT, TKATHIS
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła główna czynności parametryzacyjnej (nieprocesowej) - taryfikatory kosztów
::  TAG: <CZYNNOŚĆ><LISTA>
::----------------------------------------------------------------------------------------------------------------------
Cntx.psh(TTOPER,TTOUT,TKAT);

_his:=exec('tab_his','!zws_par_ttar');
TTOPER.index('KOD'); TTOPER.prefix(); TTOPER.first();
TTOUT.index('KOD'); TTOUT.prefix(); TTOUT.first();
TKAT.index('K'); TKAT.prefix(); TKAT.first();

TTOPER.win_edit('RED'); TTOPER.win_patt('SZUK');
TTOUT.win_edit('RED'); TTOUT.win_patt('SZUK');
TKAT.win_edit('');

TWRKPLC.win_dict('SLO');
TWRKZBR.win_dict('SLO');
TWRKZPO.win_dict('SLO');

:: Ustawienie okien dla słownika kontrahentów
exec('ustaw_okna','kontrahent');

_grp:=TTOPER.grp_make('Taryfikatory'@,,'ttoper_grp',,,,,'normal');
TTOPER.grp_sel(_grp,,'WER','Taryfikator operacji'@,,,,,,,,,'maximized');
TTOPER.grp_sel(_grp,TTOUT,'WER','Taryfikator usług (kooperacji)'@,,,,,,,,,'maximized');
_after_refresh:="
   exec('reloadst','!zws_par_ttar',params_get().his,TKAT.KAT);
   params_get().his.TAB.prefix(TKAT.KAT);
   grp_disp(params_get().his.TAB,params_get().his.WER)
";
TTOPER.grp_sel(_grp,TKAT,'WER','Kategorie zaszeregowania'@,_after_refresh,,,,,,,,'maximized');
TTOPER.tab_splt(_grp,,'horizontal','historia',10);
TTOPER.grp_sel(_grp,_his.TAB,_his.WER,,,,,10,,,,,'maximized');

TTOPER.win_sel(_grp);

params_set('his',_his);
TTOPER.select();

Cntx.pop(TTOPER,TTOUT,TKAT);
~~


::======================================================================================================================
:: Formuły obsługi tabeli TTOPER
::======================================================================================================================


\beplatn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MKO] [8.50]
:: OPIS: TTOPER - Przed redakcją STAWKA
::   WY: 0 / 1
::  OLD: \beplatn/polap.fml
::  TAG: <MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
TTOPER.KAT='N'


\tkatpred
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MKO] [8.50]
:: OPIS: TTOPER - Przed redakcją pola TKAT
::   WY: 0 / 1
::  OLD: \tkatpred/polap.fml
::  TAG: <MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
TTOPER.KAT='T'


\tkatpore
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MKO] [8.50]
:: OPIS: TTOPER - Po redakcji pola TKAT
::   WY: 1
::  OLD: \tkatpore/polap.fml
::  TAG: <MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
TTOPER.STAWKA:=TTOPER.TKAT().PLNH;
1


\katpore
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MKO] [8.50]
:: OPIS: TTOPER - Po redakcji pola KAT
::   WY: 1
::  OLD: \katpore/polap.fml
::  TAG: <MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? TTOPER.KAT='N' || TTOPER.TKAT:=null() ?};
exec('ttoper_efld_opt','!zws_par_ttar');
1


\ttoper_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TTOPER - Opcje pól dla okna redagowania
::----------------------------------------------------------------------------------------------------------------------
{? TTOPER.KAT='N'
|| TTOPER.efld_opt('RED','mark=0,enable=0',,'TKAT');
   TTOPER.efld_opt('RED','enable=0',TKAT,'OPIS');
   TTOPER.efld_opt('RED','mark=1',,'STAWKA')
|| TTOPER.efld_opt('RED','mark=1,enable=1',,'TKAT');
   TTOPER.efld_opt('RED','enable=1',TKAT,'OPIS');
   TTOPER.efld_opt('RED','mark=0',,'STAWKA')
?};
{? TTOPER.GRPOJ='G'
|| TTOPER.efld_opt('RED','enable=0',,'PLACE');
   TTOPER.efld_opt('RED','enable=0',TWRKPLC,'WYD');
   TTOPER.efld_opt('RED','enable=1',,'GRUPA');
   TTOPER.efld_opt('RED','enable=1',,'TWRKPLG')
|| TTOPER.efld_opt('RED','enable=1',,'PLACE');
   TTOPER.efld_opt('RED','enable=1',TWRKPLC,'WYD');
   TTOPER.efld_opt('RED','enable=0',,'GRUPA');
   TTOPER.efld_opt('RED','enable=0',,'TWRKPLG')
?};
~~


\ttoper_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TTOPER - dołącz w oknie wertowania
::----------------------------------------------------------------------------------------------------------------------
TTOPER.KAT:='T';
TTOPER.GRPOJ:='S';
exec('ttoper_efld_opt','!zws_par_ttar');
1


\ttoper_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TTOPER - popraw w oknie wertowania
::----------------------------------------------------------------------------------------------------------------------
exec('ttoper_efld_opt','!zws_par_ttar');
1


\ttoper_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: TTOPER - wyświetl w oknie wertowania
::----------------------------------------------------------------------------------------------------------------------
exec('ttoper_efld_opt','!zws_par_ttar');
TTOPER.display();
~~


\chk_ttoper
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: 'Rekord po' w oknach tabeli TTOPER
::   WY: Akronim pola do ponownego wypełnienia lub ciąg pusty
::  OLD: \chk_ttoper/polap.fml
::  TAG: <CHK>
::----------------------------------------------------------------------------------------------------------------------
exec('chk_ttoper','prod_tar',-menu_txt()='popraw')


\po_grpoj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: TTOPER - Po redakcji pola GRPOJ
::  OLD: \po_grpoj/tex_ope1.fml
::  TAG: <MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
{? TTOPER.GRPOJ='G'
|| TTOPER.PLACE:=null()
|| TTOPER.GRUPA:=null();
   TTOPER.TWRKPLG:=null()
?};
exec('ttoper_efld_opt','!zws_par_ttar');
1


\bd_twrkplc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: TTOPER - Przed wyświetleniem pola PLACE
::  OLD: \bd_twrkplc/tex_ope1.fml
::----------------------------------------------------------------------------------------------------------------------
1


\bf_ttoppl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2011]
:: OPIS: TTOPER - Przed redagowaniem pola PLACE.
::  OLD: \bf_ttoppl/wrkplc.fml
::  TAG: <MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
{? TTOPER.GRPOJ='S'
|| TWRKPLC.f_set('KOD');
   TWRKPLC.actions('SLO','Fk');
   1
|| 0
?}


\bd_grupa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: TTOPER - Przed wyświetleniem pola GRUPA
::  OLD: \bd_grupa/tex_ope1.fml
::----------------------------------------------------------------------------------------------------------------------
1


\be_ttopgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2011]
:: OPIS: TTOPER - Przed redagowaniem pola GRUPA.
::  OLD:\be_ttopgr/wrkplc.fml
::  TAG: <MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
{? TTOPER.GRPOJ='G'
|| TWRKZBR.f_set('SYMBOL');
   1
|| 0
?}


\ae_ttopgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2011]
:: OPIS: TTOPER - Po redakcji pola GRUPA.
::  OLD: \ae_ttopgr/wrkplc.fml
::  TAG: <MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
TWRKZPO.clear();
{? TTOPER.TWRKPLG<>null() & TTOPER.TWRKPLG().GRUPA<>TTOPER.GRUPA
|| TTOPER.TWRKPLG:=null()
?};
{? TTOPER.TWRKPLG=null()
|| TWRKZPO.cntx_psh();
   TWRKZPO.index('DEFAULT');
   TWRKZPO.prefix(TTOPER.GRUPA,'T');
   {? TWRKZPO.first()
   || TTOPER.TWRKPLG:=TWRKZPO.ref()
   ?};
   TWRKZPO.cntx_pop()
?};
TWRKZBR.f_clear()


\bd_twrkplg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: TTOPER - Przed wyświetleniem pola TWRKPLG
::  OLD: \bd_twrkplg/tex_op1.fml
::----------------------------------------------------------------------------------------------------------------------
1


\be_ttoplg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2011]
:: OPIS: TTOPER - Przed redakcją pola TWRKPLG
::  OLD: \be_ttoplg/wrkplc.fml
::  TAG: <MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
{? TTOPER.GRPOJ='G' & TTOPER.GRUPA<>null()
||
   exec('twrkzpo_filter_all','zasoby');
   1
|| 0
?}


\usun_ttoper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Usunięcie TTOPER
::  OLD: \usun_ttoper/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
_link:=TTOPER.testlink(2);
{? _link.first()
|| FUN.info('Usunięcie nie jest możliwe.\nWskazana pozycja taryfikatora jest już użyta w systemie.'@);
   0
|| {? FUN.ask('Czy usunąć wskazaną pozycję?'@)
   || TTOPER.del()
   ?}
?};
~~


\prn_ttop
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: wydruk taryfikatora operacji
::  OLD: \prn_ttop/drukujp.fml
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_drukttop',,1);
1


\used_prices
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.70]
:: OPIS: Wykaz operacji w których została użyta pozycja taryfikatora TTOPER
::  OLD: \used_prices/tex_oper.fml
::----------------------------------------------------------------------------------------------------------------------
exec('used_op','!zws_par_ttar',TTOPER);
~~


\predtkat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MKO [2006]
:: OPIS: Kolorowanie stawek zaszeregowania
::   WY: Schemat kolorowania
::  OLD: \predtkat/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
{? TKAT.AKT='T'
|| 'TKAT#01#01'
|| ''
?}


\ttoper_szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Przed akcją 'Szukaj' w oknie wertowania
::----------------------------------------------------------------------------------------------------------------------
exec('twrkplc_filter_all','zasoby');
exec('twrkzbr_filter_all','zasoby');
1


\be_ttopko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: TTOPER - Przed redagowaniem pola KONTROLA.
::----------------------------------------------------------------------------------------------------------------------
exec('toper_kontrola_be','tech_oper',TTOPER)


\ae_ttopplgrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: TTOPER - Po redakcji pola PL_GRP.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('plgrp_ae','tech_oper',TTOPER)


::======================================================================================================================
:: Formuły obsługi tabeli TTOUT
::======================================================================================================================


\sprttout
::----------------------------------------------------------------------------------------------------------------------
::  UTW: aj [2010]
:: OPIS: rekord po okienka SLO tabeli TTOUT
::   WY: 0/1
::  OLD: \sprttout/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
exec('chk_ttout','prod_tar',-menu_txt()='popraw')


\usun_ttout
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Usuniecie TTOUT
::  OLD: \usun_ttout/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
_link:=TTOUT.testlink(2);
{? _link.first()
|| FUN.info('Usunięcie nie jest możliwe.\nWskazana pozycja taryfikatora jest już użyta w systemie.'@);
   0
|| {? FUN.ask('Czy usunąć wskazaną pozycję?'@)
   || TTOUT.del()
   ?}
?};
~~


\prn_ttout
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: wydruk taryfikatora operacji zewnetrznych (kooperacji)
::  OLD:\prn_ttout/drukujp.fml
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_drukttou',,1);
1


\used_zewn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.70]
:: OPIS: Wykaz operacji w których została użyta pozycja taryfikatora TTOUT
::  OLD: \used_zewn/tex_oper.fml
::----------------------------------------------------------------------------------------------------------------------
exec('used_op','!zws_par_ttar',TTOUT);
~~


\ttout_szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Przed akcją 'Szukaj' w oknie wertowania
::----------------------------------------------------------------------------------------------------------------------
1


::======================================================================================================================
:: Formuły obsługi tabeli TKAT
::======================================================================================================================


\tkat_dp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Dołącz przed w okienku WER tabeli TKAT
::  OLD: \tkat_dp/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.KAT:=0;
VAR.STAWKA:=0;
VAR.OPIS:='';
~~


\sprkat
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Przed redagowaniem stawki zapamiętuje w zmiennej wartości które mają być przepisane do tabeli z historią
::  OLD: \sprkat/slownik1.fml
::  TAG: <MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
VAR.KAT:=TKAT.KAT;
VAR.STAWKA:=TKAT.PLNH;
VAR.OPIS:=TKAT.OPIS;
1


\usunhist
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Podczas usuwania stawki, usuwa całą historię zwiazaną z tą stawką
::   WY: 1
::  OLD: \usunhist/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
TKATHIS.clear();
TKATHIS.index('KAT');
TKATHIS.prefix(TKAT.KAT);
{? TKATHIS.first() || {! |? TKATHIS.del() !} ?};
1


\drukkat
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: drukowanie słownika kategorii zaszeregowania
::  OLD: \drukkat/drukujp.fml
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_druktkat',,1);
1


\katpo
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Po redakcji sprawdza czy zmieniono numer stawki.
::       Jeżeli tak, to zmienia numery stawek w historii.
::       Jeżeli zmieniły się pozostałe dane to przenosi aktualne to tabeli
::       z historią. Jeżeli dodawany jest nowy rekod nie podejmuje żadnych akcji.
::   WY: 0/1 lub akronim pola od którego rozpocząć powtórną redakcję
::  OLD: \katpo/slownik1.fml
::  TAG: <MBUILDER><AE>
::----------------------------------------------------------------------------------------------------------------------
_chk:=exec('chk_tkat','prod_tar',-menu_txt()='popraw');
{? _chk=''
||
   _exclam:='Wprowadzono już modyfikację kategorii z dzisiejszą datą.\n'
            'Aktualne dane nie zostaną zapamiętane w historii.'@;
   TKAT.AKT:='N';
   TKAT.DAKT:=date(0,0,0);
   TKAT.cntx_psh();
   {? VAR.KAT<>0
   ||
      {? VAR.KAT<>TKAT.KAT & ~TKAT.find_key(TKAT.KAT)
      ||
         TKATHIS.clear();
         TKATHIS.index('KAT');
         TKATHIS.prefix(VAR.KAT);
         {? TKATHIS.first()
         ||
            {!
            |? TKATHIS.cntx_psh();
               TKATHIS.clear();
               TKATHIS.KAT:=TKAT.KAT;
               TKATHIS.put();
               TKATHIS.cntx_pop();
               TKATHIS.first()
            !}
         ?};
         {? VAR.STAWKA<>TKAT.PLNH | VAR.OPIS<>TKAT.OPIS
         ||
            TKATHIS.clear();
            TKATHIS.index('KAT');
            TKATHIS.prefix(TKAT.KAT,date());
            {? TKATHIS.first()
            || FUN.emsg(_exclam)
            || TKATHIS.KAT:=TKAT.KAT;
               TKATHIS.PLNH:=VAR.STAWKA;
               TKATHIS.OPIS:=VAR.OPIS;
               TKATHIS.KTO:=OPERATOR.USER;
               TKATHIS.DATAZ:=date();
               TKATHIS.add()
            ?}
         ?};
         1
      |? VAR.KAT=TKAT.KAT & (VAR.STAWKA<>TKAT.PLNH |VAR.OPIS<>TKAT.OPIS) ||
         TKATHIS.clear();
         TKATHIS.index('KAT');
         TKATHIS.prefix(TKAT.KAT,date());
         {? TKATHIS.first()
         || FUN.emsg(_exclam)
         || TKATHIS.KAT:=TKAT.KAT;
            TKATHIS.PLNH:=VAR.STAWKA;
            TKATHIS.OPIS:=VAR.OPIS;
            TKATHIS.KTO:=OPERATOR.USER;
            TKATHIS.DATAZ:=date();
            TKATHIS.add()
         ?}
      ?}
   ?};
   TKAT.cntx_pop();
   1
|| _chk
?}


\leg_tkat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [2010]
:: OPIS: Akcja Legenda dla TKAT
::  OLD: \leg_tkat/slownik.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','TKAT#01#')


::======================================================================================================================
:: Formuły obsługi tabeli TKATHIS
::======================================================================================================================


\tab_his
::----------------------------------------------------------------------------------------------------------------------
::  MOD: MKO [2006]
:: OPIS: Zwraca tabelę tymczasową historii zmian stawek
::  OLD: \_def_kat/slownik1.fml
::   WY: obj_new('TAB','WER')
::----------------------------------------------------------------------------------------------------------------------
_his:=obj_new('TAB','WER');
_his.TAB:=tab_tmp(1,
   'KAT','INTEGER','Numer kategorii zaszeregowania',
   'PLNH','REAL','Płaca',
   'OD','DATE','Od daty',
   'DO','DATE','Do daty',
   'KTO','STRING[30]','Modyfikował',
   'AKT','STRING[1]','Aktywna'
);
_his.WER:=_his.TAB.mk_sel('Historia zmian stawki zaszeregowania'@,,0,'hiskat_tmp');
_his.TAB.win_fld(_his.WER,,'PLNH',,,12,4,,'Stawka'@);
_his.TAB.win_fld(_his.WER,,'OD',,,10,,,'Od'@);
_his.TAB.win_fld(_his.WER,,'DO',,,10,,,'Do'@);
_his.TAB.win_fld(_his.WER,,'KTO',,,42,,,'Modyfikował'@);
_his.TAB.win_fld(_his.WER,,'AKT',,,10,,,'Aktywna'@);
_his.TAB.win_act(_his.WER,0,'Rekord',,,,"{? cur_tab(1,1).AKT='T' || Color.rekprzed('TKZASZA#01#01') || '' ?}",,1);
_his.TAB.win_act(_his.WER,,'Formuła','&Legenda'@@,,,"exec('legenda','color','TKZASZA#01')",,0,,,,'L');
_his


\reloadst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MKO [2006]
:: OPIS: Wypełnienie tabeli tymczasowej historii zmian stawek
::   WE: _a - tymczasowa tabela historii
::       [_b] - TKAT.KAT
::  OLD: \reloadst/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
_his:=_a.TAB;
{? var_pres('_b')=type_of(0) || _tkat:=_b || _tkat:=0 ?};

TKATHIS.cntx_psh();
TKATHIS.index('KAT');

TKAT.cntx_psh();
TKAT.index('K');
{? _tkat=0 || TKAT.prefix() || TKAT.prefix(_tkat) ?};

{? TKAT.first()
|| {!
   |? _his.prefix(TKAT.KAT);
      {? _his.first() || {! |? _his.del() !} ?};
      _od:=date(0,0,0);
      _do:=date(0,0,0);
      TKATHIS.prefix(TKAT.KAT);
      {? TKATHIS.first()
      || {!
         |? _his.KAT:=TKATHIS.KAT;
            _his.PLNH:=TKATHIS.PLNH;
            _his.OD:=_od;
            _his.DO:=TKATHIS.DATAZ-1;
            _his.KTO:=TKATHIS.KTO().DANE;
            _his.AKT:='N';
            _his.add();
            _od:=TKATHIS.DATAZ;
            TKATHIS.next()
         !};
::       Ostatni zapis bezpośrednio z TKAT
         _his.KAT:=TKAT.KAT;
         _his.PLNH:=TKAT.PLNH;
         _his.OD:=_od;
         _his.DO:=date(0,0,0);
         _his.KTO:='';
         _his.AKT:='T';
         _his.add()
      ?};
      TKAT.next()
   !}
?};

_his.first();
TKAT.cntx_pop();
TKATHIS.cntx_pop();
~~


\cettoper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MKO [8.50]
:: OPIS: aktualizacja stawek w taryfikatorze operacji
::  OLD: \cettoper/polap.fml
::----------------------------------------------------------------------------------------------------------------------
TKAT.cntx_psh();
{? FUN.ask('Czy zaktualizować taryfikator (aktualizowane będą wszystkie pozycje taryfikatora)?'@)
|| TTOPER.clear();
   TTOPER.index('KOD');
   _size:=TTOPER.size();
   {? TTOPER.first()
   || PROGRESS.set(_size,'Aktualizacja operacji wg taryfikatora');
      {!
      |? {? TTOPER.KAT='T'
         || TTOPER.STAWKA:=TTOPER.TKAT().PLNH;
            TTOPER.put();
            TKAT.AKT:='T';
            TKAT.DAKT:=date();
            TKAT.put()
         ?};
         PROGRESS.next();
         TTOPER.next()
      !};
      PROGRESS.close()
   ?}
?};
TKAT.cntx_pop();
~~


::======================================================================================================================
:: Formuły wspólne
::======================================================================================================================


\used_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.70]
:: OPIS: Wykaz operacji/badań w których została użyta pozycja taryfikatora
::   WE: _a - TTOPER, TTOUT
::  OLD: \used_op/tex_oper.fml
::----------------------------------------------------------------------------------------------------------------------
_op:=_a;

_tabt:=tab_tmp(3,
   'NRK','STRING[20]','Technologia',
   'WER','STRING[6]','Wersja',
   'OPER','STRING[30]','Operacja',
   'REF','STRING[16]','$TOPER.ref()',
   'ACT','STRING[1]','Aktywny element'
);
_tabt.fld_attr('REF',2);

_tabz:=tab_tmp(2,
   'ZL','STRING[20]','Zlecenie',
   'OPER','STRING[37]','Operacja',
   'REF','STRING[16]','$TOPER.ref()'
);
_tabz.fld_attr('REF',2);

_tabb:=tab_tmp(2,
   'BAD','STRING[60]','Badanie',
   'GR','STRING[30]','Grupa materiałowa',
   'TORP','STRING[20]','Miejsce definiowania',
   'BAD_OP','STRING[1]','Rodzaj badania',
   'REF','STRING[16]','$BADSEH.ref()'
);
_tabb.fld_attr('REF',2);

exec('tktl_use','tech_common');
{? _op=TTOPER || TOPER.index('TAR') || TOPER.index('OUT') ?};
TOPER.prefix(_op.ref());
{? TOPER.first()
|| {!
   |?
      {? TOPER.NRK().ZL<>null()
      || _tabz.ZL:=TOPER.NRK().ZL().SYM;
         _tabz.OPER:=exec('get_oper_nr','tech_oper',TOPER.UNROP);
         _tabz.REF:=$TOPER.ref();
         _tabz.add()
      |? TOPER.NRK().PLRELWYR=null()
      || _tabt.NRK:=TOPER.NRK().NRK;
         _tabt.WER:=TKTL.WER;
         _tabt.OPER:=exec('get_oper_nr','tech_oper',TOPER.UNROP);
         _tabt.REF:=$TOPER.ref();
         _tabt.ACT:=TOPER.ACT;
         _tabt.add()
      ?};
      TOPER.next()
   !}
?};
:: Wypełnienie tabeli z badaniami (tylko dla TTOPER)
{? _op=TTOPER
|| BADSEH.cntx_psh();
   BADSEH.index('TTOPER');
   BADSEH.prefix(_op.ref());
   {? BADSEH.first()
   || MGR.cntx_psh();
      MGR.prefix();
      {!
      |?
         _tabb.BAD:=BADSEH.N;
         _tabb.GR:=BADSEH.MGR().NAZ;
         _tabb.TORP:={? BADSEH.TORP='P' || 'Przewodnik'@ |? BADSEH.TORP='T' || 'Technologia'@ || '' ?};
         _tabb.BAD_OP:=BADSEH.BAD_OP;
         _tabb.REF:=$BADSEH.ref();
         _tabb.add();
         BADSEH.next()
      !};
      MGR.cntx_pop()
   ?};
   BADSEH.cntx_pop()
?};


_wert:=_tabt.mk_sel('Technologie'@,,0,'used_t_'+_op.name(1),,,,,'U');
_tabt.win_fld(_wert,,'NRK');
_tabt.win_fld(_wert,,'WER');
_tabt.win_fld(_wert,,'OPER');
_tabt.win_act(_wert,,'Rekord',,,,"{? cur_tab(1,1).ACT='N' || exec('findfnrd','color') || ~~ ?}");
_tabt.win_sel(_wert);

_werz:=_tabz.mk_sel('Zlecenia'@,,0,'used_z_'+_op.name(1),,,,,'U');
_tabz.win_fld(_werz,,'ZL');
_tabz.win_fld(_werz,,'OPER');
_tabz.win_sel(_werz);

_werb:=_tabb.mk_sel('Badania'@,,0,'used_b_'+_op.name(1),,,,,'U');
_tabb.win_fld(_werb,,'BAD',,,20);
_tabb.win_fld(_werb,,'GR',,,17,,,'Grupa mat.'@,,'Grupa materiałowa'@);
_tabb.win_fld(_werb,,'TORP',,,17,,,'Miejsce def.'@,,'Miejsce definiowania: technologia/przewodnik'@);
_tabb.win_fld(_werb,,'BAD_OP',,,-3,,,'Badanie w trakcie operacji?',
   ,'Czy badanie przeprowadzane w trakcie wykonywania operacji',2,,"'T'","'N'");
_tabb.win_sel(_werb);

_before:="params_get().tabz.first(); grp_disp(params_get().tabz,params_get().werz)";
{? _op=TTOPER
|| _before:="params_get().tabb.first(); grp_disp(params_get().tabb,params_get().werb);"+_before;
   _before:=$_before
?};

_wer:=_tabt.grp_make('Lista operacji'+{?_op=TTOPER || '/badań ' || ''?}+'korzystających z: "'@+_op.KOD+' - '+_op.NA+'"',
   _before,'#used_'+_op.name(1),25,5,,,'normal');
_tabt.grp_sel(_wer,_tabt,_wert,'Technologie'@,,0,0,25,,,,,'maximized');
_tabt.grp_sel(_wer,_tabz,_werz,'Zlecenia'@,,0,0,25,,,,,'maximized');
{? _op=TTOPER
|| _tabt.grp_sel(_wer,_tabb,_werb,'Badania'@,,0,0,25,,,,,'maximized')
?};
_tabt.win_sel(_wer);

params_set('tabz',_tabz,'werz',_werz,'tabb',_tabb,'werb',_werb);
_tabt.select();
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 50bcaddd44e63c803726b9d521603bbf4e5068975512b508e636120cdcf1e57d3ec4d534b85b2208f8b2761d84b7b881a60fc122ac926799f608c7485241849c88ce69cf39b11f50a7efc74e53abd8a18eccb9af9ab4348c4bb5d1866616cce96376f450b302f0a6a73040373e4b8db615ea91e78039612805e692b66bede49d

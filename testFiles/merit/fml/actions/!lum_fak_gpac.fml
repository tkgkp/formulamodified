:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lum_fak_gpac.fml
:: Utworzony: 20.02.2019
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa generacji dokumentów z paczki faktur
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP
::# properties=LOOP
::# parses=exec('parses','!lum_fak_gpac')
::# kind=WE,   symbol=FPACZ,  type=_FPACZ,   name=Paczka faktur,        required=N, keyref=T
::# kind=WE,   symbol=RODZ,   type=STRING,   name=Rodzaj paczki,  required=T, fml_val="exec('rodz','umowy_faktury')"
::# kind=WY,   symbol=FAKS,   type=_FAKS,    name=Dokument sprzedaży,   required=N
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_akcja:=_mp.akcja();

_out.GRPKEY:=_mp.grpkey(_in.GRPKEY,_out.GRPKEY);

{? ~(var_pres('FPACZ',_in)=type_of(null()) & _in.FPACZ)
||
   _mp.error('Brak wymaganego parametru FPACZ.')
?};

{? _mp.loop()=0
:: Pierwsze uruchomienie
||
   _error:=0;
   _cancel:=0;
   FPACZ.cntx_psh();
   FPACZ.prefix();
   {? ~FPACZ.seek(_in.FPACZ)
   ||
      _mp.error('Nie znaleziono paczki faktur.');
      _error:=1
   ||
      {? _akcja='Generuj'
      ||
         params_set('mp',_mp,'grpkey',_out.GRPKEY,'pp',0);
         _cancel:=exec('fpacz_gen_dok','!lum_fak_gpac')=0

      |? _mp.pathTodo()
         | _mp.pathArea() & _akcja=''
      ||
         exec('fpacz_win_edit_set','!lum_fak_gpac');
         _win_red:=FPACZ.win_edit('?');
         _cntx:=obj_new('AKCEPTUJ');
         _cntx.AKCEPTUJ:=0;
         params_set('mp',_mp,'grpkey',_out.GRPKEY,'cntx',_cntx,'pp',0);
         FPACZ.display();
         _cancel:=~_cntx.AKCEPTUJ;
::       Usunięcie definicji tymczasowych okien
         FPACZ.win_edit(''); FPACZ.win_edel(_win_red)
      ||
         _mp.error('Nieobsługiwana ścieżka.');
         _error
      ?}
   ?};
   FPACZ.cntx_pop();
   {? _error || return() ?};
   {? _cancel || return() ?}
?};
do();
_ref:=_mp.grpkeyGet();
_ref:={? _ref=~~ || null() || exec('FindAndGet','#table',FAKS,_ref,,,null()) ?};
{? _ref=null()
||
:: Brak elementu lub nie znaleziono elementu
   _mp.save(exec('kind_out','#b_port'),'FAKS',null())
||
   _mp.save(exec('kind_out','#b_port'),'FAKS',_ref)
?};
:: Wykluczenie elementu z pętli, zakończenie czynności
:: grpkey - _port - del
{? _mp.grpkeyDel()
||
   {? _mp.grpkeyGet()<>~~
   ||
::    kontynuacja pętli
      _mp.loop_continue()
   ||
::    lub nie
      _mp.save(exec('kind_out','#b_port'),'LOOP','N')
   ?}
?};
_mp.done();
end()


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FPACZ',_in)=type_of(null()) & _in.FPACZ
|| 'Generuj dokumenty wg paczki faktur: %1'@@[exec('record','#to_string',_in.FPACZ)]
|| ''
?}


\fpacz_gen_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: generuj dokumenty - Generuj dokumenty
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('pacz_gen','umowy_faktury')


\fpacz_gen_dok_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Generuj dokumenty w oknie redakcji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? params_exec('fpacz_gen_dok','!lum_fak_gpac')
||
   params_get().cntx.AKCEPTUJ:=1;
   'key:F2'
||
   ''
?}


\fpacz_gen_dok_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Generuj dokumenty w oknie wertowania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LUM_FAK_GPAC';
_params.UIDREF:=FPACZ.uidref();
_params.AKCJA:='Generuj';
_params.PROC_START:='T';
::_params.AKCJA:='';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'FPACZ',FPACZ.ref());
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'RODZ',FPACZ.RODZ);

exec('mp_run','#b__box',_params)


\fpacz_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Ustawia okno redakcji tabeli FPACZ, wymagane pola, okna słowników
::   WE: _a - akronim okna redakcji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_akr:={? var_pres('_a')=type_of('') || _a || 'RED' ?};
_win_red:=exec('fpacz_win_edit','umowy_faktury',_win_akr);
_result:=obj_new('ACR','BTN');
_result.ACR:=_win_red;
_result.BTN:=obj_new('GENERUJ','ANULUJ');
{? _win_akr='RED'
||
   _ff:="params_exec('fpacz_gen_dok_red','!lum_fak_gpac')"

|? _win_akr='KOM'
||
   _ff:='key:F2'
?};
_result.BTN.GENERUJ:=FPACZ.win_ebtn(_win_red,
   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Generuj dokumenty'@],_ff);
_ff:='key:Esc';
_result.BTN.ANULUJ:=FPACZ.win_ebtn(_win_red,
   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
FPACZ.win_edit(_win_red);
exec('fpacz_set_efld_opt','umowy_faktury',_win_red);
_result


\fpocz_gen_err
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Wyświetl błędy generacji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::FPACZ.memo_get(,'MEMO');
::FPACZ.memo_vie(,'MEMO');
FPACZLOG.cntx_psh();
_msk:=FPACZ.ODDZ+($(FPACZ.AR)+2);
{? (FPACZLOG.name()+3)<>_msk || FPACZLOG.use('fplog'+_msk) ?};
FPACZLOG.index('LP');
FPACZLOG.prefix(FPACZ.ref());
:: Jeżeli nie ma rekordów FPACZLOG a pole MEMO jest wypełnione to przenoszę dane z pola memo do struktury FPACZLOG
{? FPACZLOG.size()=0 & FPACZ.memo_line('MEMO')<>'\n'
|| exec('fpacz_log_build','umowy_faktury',FPACZ.ref())
?};
FPACZLOG.win_sel('WER');
FPACZLOG.select();
FPACZLOG.cntx_pop()


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('FPACZ',_in)=type_of(null()) & _in.FPACZ
||
   FPACZ.cntx_psh();
   FPACZ.use(ref_name(_in.FPACZ));
   FPACZ.prefix();
   {? FPACZ.seek(_in.FPACZ)
   ||
      __PARSES.setVal('OddzialLogProd',FPACZ.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LUM';
      _args.AR:=FPACZ.DW~1;
      _args.AM:=FPACZ.DW~2;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FPACZ.cntx_pop()
?};
1


\fpacz_czy_gen_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Akcja Generuj dokumenty w oknie wertowania błędów generacji. Ustala czy generować dokumenty pomimo
::       błędów generacji.
::----------------------------------------------------------------------------------------------------------------------
__czy_gen_dok:=1;
sel_exit()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 4cfb488f73553175ff8ddb696e484a3e1cc8783e817a5588663739cc96978d77cac5bb62e85143fe3460700127757add92f984b272f090d488987983d354c444f54719bb57588b93241adf7750197384de584fd14082e5db77ddbbb4caac62463f42ce9e5342bb677ae36a9cb51c1e124ee3c6701a03e526f96cc5d161acb62a

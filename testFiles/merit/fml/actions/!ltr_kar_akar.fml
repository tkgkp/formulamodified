:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ltr_kar_akar.fml
:: Utworzony: 29.01.2019
:: Autor: MW
::======================================================================================================================
:: Zawartość: Formuły czynności LTR_KAR_AKAR
::            Akceptacja karty drogowej
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Czynność LTR_KAR_AKAR - formuła główna
::   WE: _a - [obj_new] - parametry wejsciowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,   symbol=SAMK,  type=_SAMK,  name=Karta drogowa,     required=T, keyref=T
::# kind=WY,   symbol=SAMK,  type=_SAMK,  name=Karta drogowa,     required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init','ltr');

_akcja:=_mp.akcja();
_auto:=_akcja<>'Akceptuj' & (_mp.isAutoRun() | _mp.isService());

:: Sprawdzenie uprawnień
params_set('in',_in,'user',OPERATOR.USER,'mp',_mp);
{? exec('permissions','!ltr_kar_akar')=0
|| _mp.error('Brak uprawnień do uruchomienia czynności.'@);
   return()
?};

{? type_of(_in)=type_of(~~) | var_pres('SAMK',_in)<=type_of(~~)
|| _mp.error('Brak wymaganego parametru SAMK.')
|| POJAZDY.cntx_psh();
   POJAZDY.f_clear();
   POJAZDY.index('FIRAKTNR');
   POJAZDY.prefix(REF.FIRMA,'T');
   SAMK.cntx_psh();
   SAMK.prefix();
   {? ~SAMK.seek(_in.SAMK) | ~POJAZDY.seek(SAMK.POJAZD)
   || _mp.error('Nie znaleziono karty drogowej.'@)
   || {? _r_lock:=exec('r_lock_one','#table',SAMK,SAMK.ref())
      || params_set('mp',_mp);
         {? _akcja='Akceptuj' | _auto
         || exec('samk_akcept','karty_drog', 0, _auto);
            {? SAMK.STATUS='T' | SAMK.STATUS='S'
            || _mp.done()
            ?}
         |? _mp.pathTodo() | (_mp.pathArea() & _akcja='')
         || exec('samk_akcept_todo','karty_drog');
            {? SAMK.STATUS='T' | SAMK.STATUS='S'
            || _mp.done()
            ?}
         || _mp.error('Nieobsługiwana ścieżka.')
         ?};
         exec('r_unlock_one','#table',SAMK,SAMK.ref(),_r_lock)
      || exec('who_rlock_samk','karty_drog')
      ?}
   ?};
   SAMK.cntx_pop();
   POJAZDY.cntx_pop()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? type_of(_in)<>type_of(~~)& var_pres('SAMK',_in)<>type_of(~~) & _in.SAMK
|| 'Zaakceptuj kartę drogową: %1'@@[exec('record','#to_string',_in.SAMK)]
|| 'Zaakceptuj kartę drogową'@@
?}


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;
_wyn:=1;
_wyn


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Formuła display TO-DO
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
{? type_of(_in)<>type_of(~~) & var_pres('SAMK',_in)>type_of(~~)
|| SAMK.cntx_psh();
   SAMK.prefix();
   {? SAMK.seek(_in.SAMK)
   || exec('samk_wys','karty_drog')
   ?};
   SAMK.cntx_pop()
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:14 79f4254f921bc453718ce2e8d0ed2df8f12e99c904e19104cf66c106912c11125af5ab8e1a7a02d90fac7de8d9a170e8c9232f684c65ccb9cab61e1a6e5777b0e1a730cf998f03d68be885f4b49c190f9cd2f343d7d0221509ae96549d3b5d5fdda84da8c9546925af22d65df48f230cef24f3f8ebb6b36ad38aa5306710d12e

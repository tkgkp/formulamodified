:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_dos_prda.fml
:: Utworzony: 30.03.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_DOS_PRDA - Rejestracja danych adresowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja danych adresowych - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=OSOBA, type=_OSOBA, name=Wskazanie osoby, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

exec('os_adres_cfg','osoba','BLANK','OSOBA');

_result:='';

_uidref:=exec('ref2uid','#table',_in.OSOBA);
{? _uidref=''
|| _result:=exec('error','!zws_dos_prda')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!zws_dos_prda',_in.OSOBA);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};

{? _akcja<>'START'
|| exec('os_adres_cfg','osoba','BLANK')
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja danych adresowych - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj dane adresowe: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj dane adresowe: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj dane adresowe: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj dane adresowe'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie danych adresowych niemożliwe.\nNie znaleziono osoby.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie osoby.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & OSOBA.seek(_a)
|| exec('czytaj','#stalesys',,XINFO,'TER_AKT','TER_KNT');
   OS_ADRES.cntx_psh();
   OS_ADRES.index('RODZAJ');
   OS_ADRES.prefix(OSOBA.ref());
   OS_ADRES.index('OD');
   OS_ADRES.prefix(OSOBA.ref());
   OS_ADRES.win_sel('WERI');
   OS_ADRES.win_edit('RED');
   OS_ADRES.win_patt('SZUK');
   _ret:=OS_ADRES.select();
   OS_ADRES.cntx_pop()
|| _ret:=exec('error','!zws_dos_prda')
?};
OSOBA.cntx_pop();
_ret


\os_adres_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli OS_ADRES z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli OS_ADRES jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=OS_ADRES
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\os_woj_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: przycisk pola OS_ADRES.MIASTO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_woj_wybierz','teryt','OS_ADRES','WOJEWODZ')
?}


\os_pow_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: przycisk pola OS_ADRES.POWIAT
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_pow_wybierz','teryt','OS_ADRES','POWIAT','WOJEWODZ')
?}


\os_gmi_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: przycisk pola OS_ADRES.GMINA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_gmi_wybierz','teryt','OS_ADRES','GMINA','POWIAT','WOJEWODZ')
?}


\os_msc_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: przycisk pola OS_ADRES.MIASTO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_msc_wybierz','teryt','OS_ADRES','MIASTO','GMINA','POWIAT','WOJEWODZ')
?}


\os_ul_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: przycisk pola OS_ADRES.UL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_uli_wybierz','teryt','OS_ADRES','ULICA','MIASTO','GMINA','POWIAT','WOJEWODZ')
?}


:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:22 21870d038c894138ce6e994da7cc777238d1cb6f41f1d45e80a2d6d65c1eeea03e6090e176b493834a2c1db92bb33751c1d73a04d378783705282fa41edacde3ba8bedec7c17236fd134b8c5c2e825402ab3fce2f45c0d8e9a60e5ab82aa85bdd8a755702360c5f3d1774c3e4ceecaefcca615ad1458bacdf4e321458fd1bab5

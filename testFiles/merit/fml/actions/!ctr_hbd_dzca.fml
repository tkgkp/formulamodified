:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_hbd_dzba.fml
:: Utworzony: 09.12.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_HBD_DZCA - Generowanie zadań budżetowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Generowanie zadań budżetowych - główna formuła czynności CTR_HBD_DZCA.
::----------------------------------------------------------------------------------------------------------------------
::# properties=LOOP,GRP_FIRM
:: PARAMETRY WE:
::# kind=WE, symbol=TM_STAMP, type=STRING, name=Znacznik czasowy, required=N
::# kind=WE, symbol=K_HARM_P, type=_K_HARM_P, name=Zadanie budżetowe, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=TM_STAMP, type=STRING, name=Znacznik czasowy, required=N
::# kind=WY, symbol=K_HARM_P, type=_K_HARM_P, name=Zadanie budżetowe, required=T
::# kind=WY, symbol=TYP, type=STRING, name=Typ zadania [UA], required=T
:: WARUNKI BRAMY:
::# condition=Uzupełnienie arkuszy, act_uid=CTR_HBD_DZAA, auto=T, formula=_a.K_HARM_P<>null & _a.K_HARM_P<>~~ & _a.TYP='U'
::# condition=Akceptacja arkuszy, act_uid=CTR_HBD_DZBA, auto=T, formula=_a.K_HARM_P<>null & _a.K_HARM_P<>~~ & _a.TYP='A'
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
{? _we.TM_STAMP<>~~
|| exec('k_harm_p_first','control',_wy,_we.TM_STAMP);
   _wy.TM_STAMP:=_we.TM_STAMP;
   _mp.save(,_wy);
   {? exec('k_harm_p_next','control',_we.TM_STAMP)
   || _mp.loop_continue()
   ?};
   _mp.done()
|| K_HARM_P.cntx_psh();
   K_HARM_P.prefix();
   _ref:=_mp.getRefs();
   {? K_HARM_P.seek(_ref[1])
   || {? K_HARM_P.STATUS='A' | K_HARM_P.STATUS='P'
      || _wy.TM_STAMP:=tm_stamp();
         exec('upd_stat','control',K_HARM_P.ref(),OPERATOR.USER().KOD,_wy.TM_STAMP);
         exec('k_harm_p_first','control',_wy,_wy.TM_STAMP);
         _mp.save(,_wy);
         {? exec('k_harm_p_next','control',_wy.TM_STAMP)
         || _mp.loop_continue()
         ?};
         _mp.done()
      |? K_HARM_P.STATUS='W'
      || _wy.K_HARM_P:=K_HARM_P.ref();
          _wy.TYP:='U';
          _mp.save(,_wy);
          _mp.done()
      ?}
   || {? ~_mp.isAutoRun()
      || FUN.info('Nie znaleziono zadania.'@)
      ?};
      _wy.K_HARM_P:=null;
      _mp.save(,_wy);
      _mp.done()
   ?};
   K_HARM_P.cntx_pop();
   ~~
?};
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis na liście TODO czynności
::----------------------------------------------------------------------------------------------------------------------
_desc:='Pobieraj zadanie budżetowe'@@;
_mp:=params_get().mp;
_refs:=_mp.getRefs();
{? var_press('_refs')>0 & obj_len(_refs)>0
|| _ref:=_refs[1];
   {? var_press('_ref')>0
   || K_HARM_P.cntx_psh(); K_HARM_P.prefix();
      {? K_HARM_P.seek(_ref)
      || _desc:='Pobieraj zadanie budżetowe: %1'@@[exec('record','#to_string',K_HARM_P.ref())]
      ?};
      K_HARM_P.cntx_pop()
   ?}
?};
_desc

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:11 06faeae88874e1464c15e4614b5281328097fdc15459d725eaa55364a3b0c1f51a2ac5d33fb3c356063a19e9ffca64068c26c246e595367c45a819dcb06832f5c06ba55c4f22610da6d6be4fdc4292dbebea5b0f9e33c54d0cff5ed210017629fff4bea6b81abafe8fa44d69dcdb9cbff1a488055e54566d199223f23df96b52

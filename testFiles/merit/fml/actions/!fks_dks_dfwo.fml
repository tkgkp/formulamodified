:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dfwo.fml
:: Utworzony: 07.06.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DFWO - Dekretacja wniosków w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja wniosków w obiegu - główna formuła czynności FKS_DKS_DFWO.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_edokum','obiegi')
::# access=exec('uprawnienia','obiegi',0)
::# properties=TODOTRIG
::
:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=T, keyref=T
::# kind=WE, symbol=B_PREL, type=STRING, name=Poprzedni element w procesie, required=N
::# kind=WE, symbol=B_PRELS, type=STRING, name=Symbol poprzedniego elementu w procesie, required=N

:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=N
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na wniosek w obiegu, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

{? ~exec('is_act','#b__box','FKS_DKS_DFWO')
|| FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków.'@);
   _mp.cancel(); return(~~)
|? _mp.isMicro() & _akcja='Odrzuć'
|| FUN.info('Dla wniosku nie znaleziono aktywnego procesu z czynnością dekretacji.'@); return(~~)
?};

zak_dek:=0;
OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1; dolndok:=0;
{? (~_mp.isMicro() & _we.EDOKUM & EDOKUM.seek(_we.EDOKUM,,1)) |
   (_mp.isMicro() & EDOKUM.get())
|| params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
:: akceptacja z pulpitu
   {? _akcja='Dekretuj'
   || params_get().context.EDOKUM;
      _edokum:=EDOKUM.ref(); dok_ref:=null;
      exec('fl_decl','dekret_aut');
      exec('dek_decl','dekret_aut');
      exec('sd_decl','dekret_aut');
      {? var_pres('SD_OB')<=0 || SD_OB:=obj_new(@.CLASS.SD_CLASS,'F') ?};
      exec('dob_dek','obiegi_dekr');
      {? dok_ref & EDOKUM.seek(_edokum) & EDOKUM.STDEKRD
      || exec('ustaw_out','obiegi_dekr',_wy,'D',dok_ref,EDOKUM.ref());
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _akcja='Odrzuć'
   || params_get().context.EDOKUM;
      params_exec('odrzuc','obiegi_dekr');
      _edokum:=EDOKUM.ref();
      {? zak_dek=2 & EDOKUM.seek(_edokum)
      || exec('ustaw_out','obiegi_dekr',_wy,'O',null,EDOKUM.ref());
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _mp.pathTodo() | _mp.isAutoRun()
   || menu_txt(,'Popraw');
      {? var_pres('EDOKUM',_we)
      || _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
         {? _ref<>0 & _nam<>''
         || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
            {? EDOKUM.seek(_ref,_nam)
            || exec('init','obg'); zakres:=3;
               {? var_pres('SD_OB')<=0 || SD_OB:=obj_new(@.CLASS.SD_CLASS,'F') ?};
               exec('rkprz','obiegi');
               _edokum:=EDOKUM.ref(); dok_ref:=null;
               typobi:=2;
               params_exec('przekaz_todo','obiegi_akc','D');
               VAR_DEL.delete('typobi');
               {? zak_dek=1 & dok_ref & EDOKUM.seek(_edokum) & EDOKUM.STDEKRD
               || exec('ustaw_out','obiegi_dekr',_wy,'D',dok_ref,EDOKUM.ref());
                  _mp.save(,_wy);
                  _mp.done()
               |? zak_dek=2 & EDOKUM.seek(_edokum)
               || exec('ustaw_out','obiegi_dekr',_wy,'O',null,EDOKUM.ref());
                  _mp.save(,_wy);
                  _mp.done()
               ?}
            ?};
            EDOKUM.cntx_pop()
         ?}
      ?}
   ?}
?};
VAR_DEL.delete('dolndok','dok_ref','zawin','zak_dek');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zadekretuj wniosek w obiegu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat')) & ~EDOKUM.STDEKRD;
{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || params_exec('ustaw_bprel','obiegi');
        {? _set || exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS) ?};
         _desc:='Zadekretuj wniosek w obiegu: %1, data operacji %2'@@[EDOKUM.ID,$EDOKUM.DOP];
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc


\dekretuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dekretacja wniosków w obiegu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_DKS_DFWO';
_params.AKCJA:='Dekretuj';
_params.UIDREF:=EDOKUM.uidref();
_params.CONTEXT:=obj_new('EDOKUM');
_params.CONTEXT.EDOKUM:=EDOKUM.ref();
exec('mp_run','#b__box',_params);
1


\odrzuc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dekretacja wniosków w obiegu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_DKS_DFWO';
_params.AKCJA:='Odrzuć';
_params.UIDREF:=EDOKUM.uidref();
_params.CONTEXT:=obj_new('EDOKUM');
_params.CONTEXT.EDOKUM:=EDOKUM.ref();
exec('mp_run','#b__box',_params);
1


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności FKS_DKS_DFWO
::  WE: _a - 'add po', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='add_po' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='add_po'
         || 3
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'FKS_DKS_DFWO')
?}


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'skid_v'>0
         || _edokum:=exec('FindAndGet','#table',EDOKUM,_kref,,,null());
            {? _edokum=null()
            || _mp.error()
            || EDOKUM.cntx_psh();
               {? EDOKUM.seek(_edokum,ref_name(_edokum),1)
               || {? EDOKUM.STDEKRD & EDOKUM.DOK_KS<>''
                  || _dok:=exec('FindAndGet','#table',DOK,EDOKUM.DOK_KS,,,null());
                     {? _dok<>null()
                     || DOK.cntx_psh();
                        {? DOK.seek(_dok,ref_name(_dok),1) & DOK.EDOKUM=$_edokum
                        || _wy.DOK:=DOK.ref;
                           _mp.save(,_wy);
                           _mp.done()
                        ?};
                        DOK.cntx_pop()
                     ?}
                  ?}
               ?};
               EDOKUM.cntx_pop()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 02cd5128ae16fd8cd0c6357773f4a7e953c7749e1fc494ec671f4852b4f5f49375f98664faa7fcb7c4d3f4df5f980dfeff9295e50b977faa45518c7c47a2245e86d32617c1234a6ffbd1bbbf8c678c094e324057b1b693ba3d9b0f08f29233e4072ba4984db0d720195446cd3f851b2465698359c9f689745aba0675ff5348af

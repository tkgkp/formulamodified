:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_fak_dkor.fml
:: Utworzony: 11.05.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Rejestracja korekty dokumentu sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP,TARS
::# parses=exec('parses','!lsp_fak_dkor')
::# kind=WE,   symbol=TYPYSP,    type=_TYPYSP,  name=Typ dokumentu sprzedaży,       required=N, fml_val="exec('typysp','!lsp_fak_dkor')", fml_exp="exec('typysp_export','typysp',_a)"
::# kind=WE,   symbol=FAKS,      type=_FAKS,    name=Dokument sprzedaży,            required=N, keyref=T
::# kind=WE,   symbol=ND,        type=_ND,      name=Dokument zwrotu,               required=N
::# kind=WE,   symbol=ZLF,       type=STRING,   name=Dotyczy zlecenia fakturowania, required=N, fml_val="exec('zlf','faktury_wspolne')"
::# kind=WE,   symbol=FAKZ,      type=_FAKZ,    name=Zlecenie fakturowania,         required=N
::# kind=WE,   symbol=EDOKUM,    type=_EDOKUM,  name=Dokument w obiegu,             required=N
::# kind=WE,   symbol=KORNAG,    type=STRING,   name=Korekta nagłówkowa,            required=T fml_val="exec('kor_nag','!lsp_fak_dkor')"
::# kind=WY,   symbol=KORS,      type=_FAKS,    name=Korekta sprzedaży,             required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_fak','lsp');

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_area_lsp_fak:=_mp.pathArea('LSP_FAK') | _mp.pathArea('LSS_POS');
_area:=_area_lsp_fak;
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo() | _mp.pathArea() & ~_area;
_grupa:=_mp.isGroup();
_autoakc:=exec('autoAkc','#b__box',_mp,300080,'LSP_FAK_EASP');

_kornag:={? var_pres('KORNAG',_in)=type_of('') || _in.KORNAG || 'N' ?};

:: odblokowane w ramach obsługi błędu ER/WRT/XP/21.37/2207/0018
{? 0& var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
   & _kornag='T' & (
      exec('FindInSet','#table','FAPOW','FAKS_K','N',$_in.FAKS,,,,null())
      | exec('FindInSet','#table','FAPOW','END_K','N',$_in.FAKS,,,,null())
   )
||
   FUN.info('Korekta nagłówkowa jest niedostępna dla dokumentów zaliczkowych i rozliczających zaliczki.'@);
   _mp.done();
   return()
?};

_ndref:={? var_pres('ND',_in)=type_of(null()) & _in.ND || _in.ND || null() ?};

{? _todo
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('KORS',_out)=type_of(null()) & _out.KORS
:: Jest dokument korekty
   || 1
   |? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
:: Brak dokumentu korekty i jest na wejściu dokument sprzedaży do korekty
   || FAKS.use(ref_name(_in.FAKS));
      FAKS.prefix();
      {? FAKS.seek(_in.FAKS)
      || exec('open','open_tab',FAKS.ODDZ,form(FAKS.AR-2000,-2))
      ?};
      _akcja:='START_TODO'
   |? ~_mp.isMicro()
:: Brak dokumentu korekty i brak na wejściu dokumentu sprzedaży do korekty
   || _akcja:='START_TODO'
   || _mp.error('Brak parametrów wejściowego FAKS, wyjściowego KORS.')
   ?}
?};

:: Sprawdzenie uprawnień
{? params_exec('permissions_chk','lsp','!lsp_fak_dkor')=0 || return() ?};

:: Pobranie uidref korygowanej faktury
_keyRef:='';
{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| _keyRef:=exec('FindAndGet','#table',FAKS,$_in.FAKS,,"FAKS.uidref()",'')
?};

:: Wyzwalacz, który po dodaniu nagłówka korekty:
:: - add: dodaje rekord kluczowy nagłówka korekty
::   del: usuwa rekord kluczowy nagłówka korekty
:: - add: zapisuje parametr wyjściowy KORS - wskazanie na nagłówek korekty
::   del: zapisuje parametr wyjściowy KORS - null
:: - add: usuwa kluczowy rekord nagłówka dokumentu korygowanego
::   del: dodaje rekord kluczowy nagłówka dokumentu korygowanego
_mp.trigRef('FAKS',,1,,exec('kind_out','#b_port'),'KORS',_keyRef,"FAKS.HIDDEN='N'");

_typysp_t:={? var_pres('TYPYSP',_in)=type_of(null())
           || exec('FindAndGet','#table',TYPYSP,_in.TYPYSP,,"TYPYSP.T")
           || ''
           ?};
_edokum:={? var_pres('EDOKUM',_in)=type_of(null()) || _in.EDOKUM || null() ?};
_fakz:={? var_pres('FAKZ',_in)=type_of(null()) || _in.FAKZ || null() ?};
_wybfakz:={? var_pres('ZLF',_in)=type_of('') || _in.ZLF='TAK' || 0 ?};
_zafakt:=var_pres('KORS',_out)=type_of(null()) & _out.KORS;

{? ~_zafakt & _edokum<>null()
|| _ctrl:=exec('ctrlEDOKUM','faktury_nag',_edokum,'S','K');
   {? ~_ctrl
   || _mp.error('Niepoprawny parametr EDOKUM - typ dokumentu w obiegu niezgodny z czynnością.'@)
   |? _ctrl=-1
   || _mp.done()
   |? _ctrl=2
   || FUN.info('Dokument w obiegu nie został zaakceptowany.');
      _mp.cancel()
   || params_set('out',_out,'mp',_mp,'akcja',_akcja);
      _faks:=params_exec('GenWgEDOKUM','faktury_nag',_edokum,'S','K',_typysp_t)
   ?}
|? ~_zafakt & (_fakz<>null() | _wybfakz)
|| _ctrl:={? _fakz<>null() || exec('ctrlFAKZ','faktury_nag',_fakz) || 1 ?};
   {? ~_ctrl
   || _mp.error('Niepoprawny parametr FAKZ - zlecenie fakturowania (korekta,firma,oddział).'@)
   |? _ctrl=-1
   || _mp.done()
   |? _ctrl=2
   || FUN.info('Zlecenie fakturowania nie zostało zaakceptowane.'@);
      _mp.cancel()
   || params_set('out',_out,'mp',_mp,'akcja',_akcja);
      _faks:=params_exec('GenWgFAKZ','faktury_nag',_fakz,_typysp_t)
   ?}
|? _akcja='Dołącz'
   | _proc
   | _akcja='START_TODO'
   | _akcja='WgZwrotu'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
|| _typysp_t:=
      {? var_pres('TYPYSP',_in)=type_of(null())
      || exec('FindAndGet','#table',TYPYSP,_in.TYPYSP,,"TYPYSP.T")
      || ''
      ?};
   TAZ.RAB_N_BE:=0;
   BEER.WYSTFAKS:=null;
   HELP.WHERE:='K';
   HELP.WMAG:=0;

:: Korygowany dokument
   _faks:={? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS || _in.FAKS || null() ?};

:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
   params_set('in',_in,'out',_out,'mp',_mp,'akcja',_akcja);
   {? _ndref<>null()
   || exec('add_powzwr','powdok',_ndref,_typysp_t)
   || exec('korekty','faktury_nag',_faks,_typysp_t)
   ?};

:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? (var_pres('KORS',_out)=type_of(~~) | var_pres('KORS',_out)=type_of(null()) & _out.KORS=null())
      & var_pres('KORS',_outa)<>type_of(~~) & _outa.KORS
:: Dodano dokument
   ||
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      params_exec('seek_dok','faktury_wspolne',_area_lsp_fak,_outa.KORS)
:: Wycofanie czynności bo nie dodano dokumentu
   || _mp.cancel()
   ?}

|? _akcja='Popraw'
   | _todo
|| {? var_pres('KORS',_out)=type_of(null())
:: Uruchomione w procesie
   || FAKS.cntx_psh();
      FAKS.use(ref_name(_out.KORS));
      FAKS.prefix();
      {? FAKS.seek(_out.KORS)
      || exec('faks_win_edit_set','faktury_nag');

         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('fak_pop','faktury_nag');

         _mp.descTodo()
      ?};
      FAKS.cntx_pop()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || {? ~_todo
      || params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('fak_pop','faktury_nag')
      |? _akcja='Zakończ_wer'
      || {? exec('faks_zakoncz','faktury_nag',,_autoakc) || _mp.done() ?}
      || _mp.cancel()
      ?}
   |? _todo
:: Uruchomione zadanie z listy todo i brak _out.FAKS wtedy zadanie wycofujemy
   || _mp.cancel()
   ?}

|? _akcja='Usuń'
|| {? var_pres('KORS',_out)=type_of(null())
:: Uruchomione w procesie
   || _faks:=null();
      FAKS.cntx_psh();
      FAKS.use(ref_name(_out.KORS));
      {? ~_area_lsp_fak || FAKS.prefix() ?};
      {? FAKS.seek(_out.KORS)
      ||
         exec('usun_fak','faktury_nag',{? _grupa || 2 || 1 ?});

         {? ~FAKS.seek(_out.KORS)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _faks:=FAKS.ref()
      ?};
      FAKS.cntx_pop();
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _area_lsp_fak || {? _faks || FAKS.seek(_faks) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      exec('usun_fak','faktury_nag',{? _grupa || 2 || 1 ?});

      _mp.done()
   ?}

|? _akcja='Zakończ_wer' & _area_lsp_fak
|| {? exec('faks_zakoncz','faktury_nag',_grupa,_autoakc) || _mp.done() ?}

|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('KORS',_out)=type_of(null()) & _out.KORS
|| 'Zakończ rejestrację korekty sprzedaży: %1'@@[exec('record','#to_string',_out.KORS)]
|| _in:=_mp.load(exec('kind_in','#b_port'));
   {? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
   || 'Wystaw korektę do dokumentu sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   |? var_pres('EDOKUM',_in)=type_of(null()) & _in.EDOKUM
   || 'Wystaw korektę sprzedaży wg dokumentu w obiegu: %1'@@ [exec('record','#to_string',_in.EDOKUM)]
   || 'Zarejestruj korektę sprzedaży'@@
   ?}
?}


\faks_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Korekta - Dołącz
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_kor_nag:={? var_pres('_a')=type_of('') || _a || 'N' ?};
_faks:=FAKS.ref();
{? _r_lock:=exec('r_lock_one','#table',FAKS,_faks)
||
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LSP_FAK_DKOR';
   _params.UIDREF:=FAKS.uidref();
   _params.AKCJA:='Dołącz';
   _params.PROC_START:='T';
   _params.PORTS_ADJ:='T';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'FAKS',FAKS.ref());
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'KORNAG',_kor_nag);

   exec('mp_run','#b__box',_params);

   FAKS.f_rfresh();

   exec('r_unlock_one','#table',FAKS,_faks,_r_lock)
||
   exec('who_rlock_faks','faktury_nag',0)
?}


\kornag_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Korekta nagłówkowa - Dołacz
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',300211,2)=''
||
   FUN.info('Dostępne korygowanie tylko podmiotów.'
      '\n\nNie wskazano pól uruchamiających korektę nagłówkową w parametrze nr 300211.'@)
?};
exec('faks_dolacz','!lsp_fak_dkor','T')


\typysp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła na wartość parametru TYPYSP
::   WY: TYPYSP.ref()
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:='S';
exec('tsp_fak','typysp',,,,,,'KOR')


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=exec('permissions_res','lsp');
_result.RES:=1;
_result.TXT:='';

_faks_where:=_ue:=_fis:=_spp:=_vatzpoz:='';
_typysp_t:='';

:: Sprawdzenie uprawnień do typu dokumentu
{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| _faks_where:=exec('FindAndGet','#table',FAKS,$_in.FAKS,,"FAKS.WHERE",'');
   _typysp:=exec('FindAndGet','#table',FAKS,$_in.FAKS,,"$FAKS.T",'');
   _ue:=exec('FindAndGet','#table',TYPYSP,_typysp,,"TYPYSP.UE",'');
   _fis:=exec('FindAndGet','#table',TYPYSP,_typysp,,"TYPYSP.FIS",'');
   _spp:=exec('FindAndGet','#table',TYPYSP,_typysp,,"TYPYSP.SPP",'');
   _vatzpoz:=exec('FindAndGet','#table',TYPYSP,_typysp,,"TYPYSP.VATZPOZ",'')
?};
{? var_pres('TYPYSP',_in)=type_of(null()) & _in.TYPYSP
|| _typysp_t:=exec('FindAndGet','#table',TYPYSP,$_in.TYPYSP,,"TYPYSP.T",'')
?};
{? _typysp_t<>''
   | _faks_where<>'' & _ue<>'' & _fis<>'' & _spp<>'' & _vatzpoz<>''
|| _where:=exec('tsp_where','typysp',_faks_where,_fis,,_spp,_typysp_t,'KOR',_ue,_vatzpoz);
   {? {? _typysp_t<>''
      || {? exec('FindInSet','#table','T2STS','UNIK',ST.STS)<>null()
         || exec('FindInSet','#table','T2STS','UNIK',_in.TYPYSP,ST.STS)<>null()
         || exec('tsp_upr','typysp',_where,,0,,1,,_user)<>null()
         ?}
      || exec('tsp_upr','typysp',_where,2,0,,,,_user)<>''
      ?}
   || 1
   || _result.RES:=0;
      _result.TXT:='Brak uprawnionych typów dokumentów '@+{? BEER.SZ='S' || 'sprzedaży.'@ || 'zakupu.'@ ?}
   ?}
?};
_result


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('EDOKUM',_in)=type_of(null()) & _in.EDOKUM
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(_in.EDOKUM));
   EDOKUM.prefix();
   {? EDOKUM.seek(_in.EDOKUM)
   ||
::      __PARSES.setVal('OddzialLogProd',FAKZ.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=EDOKUM.DW~1;
      _args.AM:=EDOKUM.DW~2;
      __PARSES.setVal('OkresRok',_args)
   ?};
   EDOKUM.cntx_pop()
?};

1


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Lista zadań TO-DO, podgląd dokumentu
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));
exec('display','faktury_nag',_out.KORS)


\zwrot2kors
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: Korekta na podstawie dokumentu zwrotu
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_FAK_DKOR';
_params.UIDREF:=ND.uidref();
_params.AKCJA:='WgZwrotu';
_params.PROC_START:='N';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ND',ND.ref());

exec('mp_run','#b__box',_params);
~~


\kor_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Formuła na wartość parametru TYPYSP
::   WE:
::   WY: T/N
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy rejestracja korekty nagłówkowej?'@)
|| 'T'
|| 'N'
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 b16f39af235b88d1e3ea626c61a9580b775b7573e42b86bc17812170d66fabd6f3d23d0d90e6a1895c70148dff068acba37c0b27548da58089aacf20783f9290ef0b4fd5407e586045ca4eca80a1260399687d473cfd1fa9af423fde7cf713d4d8697bb6195aebaa5a2cfd940aa38267557551c350d931b2cf56761da1621050

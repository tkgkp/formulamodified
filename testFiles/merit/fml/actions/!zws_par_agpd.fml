:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_agpd.fml [22.26]
:: Utworzony: 28.10.2021
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_AGPD - Grupy uprawnień.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Grupy uprawnień - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
:: utwórz panel zarządzania grupami
_wnd:=exec('mk_wnd','!zws_par_agpd');

_ctx:=obj_new('GP_DEF');
params_set('ctx',_ctx);
: kontekst tabeli GP_DEF
_ctx.GP_DEF:=obj_new('key','row');
_ctx.GP_DEF.key:='SYMBOL';
_ctx.GP_DEF.row:=null;

:: udostępnij dane
GP_DEF.cntx_psh();
GP_DEF.win_sel(_wnd);
GP_DEF.select();
GP_DEF.cntx_pop();
GP_DEF.win_del(_wnd);
~~


\mk_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Tworzy panel zarządzania grupami uprawnień.
::   WE:
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
:: utwórz panel zarządzania grupami uprawnień
_wnd:=GP_DEF.grp_make('Grupy uprawnień'@,
:: przed otwarciem
   "params_exec('wnd_bo','!zws_par_agpd')",
:: identyfikator
   '#zws_par_agpd',,,
:: podczas zamykania
   "params_exec('wnd_oc','!zws_par_agpd')"
);

:: dodaj okno definicji grup
GP_DEF.grp_sel(_wnd,GP_DEF,'WER','Definicja'@,,
:: położenie, wysokość
   ,,10,
:: przed obsługą
   "params_exec('def_bs','!zws_par_agpd',_a)",
:: po obsłudze
   "params_exec('def_as','!zws_par_agpd',_a)"
);

:: dodaj okno obszarów
GP_DEF.grp_sel(_wnd,GP_GRP,'WER','Wykorzystanie'@,
:: po zmianie bieżącego wiersza
   "params_exec('grp_ar','!zws_par_agpd')"
);

:: dodaj poziomy podział zakładki
GP_DEF.tab_splt(_wnd,,'horizontal','bottom');
:: dodaj okno dziedzin
GP_DEF.grp_sel(_wnd,GP_DOM,'WER',,
:: po zmianie bieżącego wiersza
   "params_exec('dom_ar','!zws_par_agpd')",,,,
:: przed obsługą
   "params_exec('dom_bs','!zws_par_agpd',_a)"
);

:: dodaj pionowy podział zakładki
GP_DEF.tab_splt(_wnd,'bottom','vertical','right');
:: dodaj okno wykorzystywanych grup
GP_DEF.grp_sel(_wnd,GP_SYS,'WER',,,,,,
:: przed obsługą
   "params_exec('sys_bs','!zws_par_agpd',_a)"
);

:: panel
_wnd


\wnd_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed otwarciem panelu zarządzania grupami uprawnień.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: zachowaj stan tabel i ustaw kontekst przeglądania
GP_DEF.cntx_psh();
GP_DEF.index('SYMBOL');
GP_DEF.prefix(exec('ref_firma','ustawienia'));
GP_DEF.win_dict('SLO');
GP_DEF.win_edit('RED');
GP_DEF.win_patt('WZO');

GP_GRP.cntx_psh();
GP_GRP.index('SYMBOL');
GP_GRP.prefix(exec('ref_firma','ustawienia'));
GP_GRP.win_dict('SLO');
GP_GRP.win_edit('RED');
GP_GRP.win_patt('WZO');

GP_DOM.cntx_psh();
GP_DOM.index('SYMBOL');
GP_DOM.win_edit('RED');

GP_SYS.cntx_psh();
GP_SYS.index('DEF_SYM');
GP_SYS.win_edit('RED');

B_DOMAIN.cntx_psh();
B_DOMAIN.win_dict('SLO');
1


\wnd_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Podczas zamykania panelu zarządzania grupami uprawnień.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: przywróć stan tabel
exec('dom_f_clr','gp_api');
B_DOMAIN.cntx_pop();
GP_SYS.cntx_pop();
GP_DOM.cntx_pop();
GP_GRP.cntx_pop();
GP_DEF.cntx_pop();

1


\def_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed obsługą okna WER tabeli GP_DEF.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: ignoruj odrysowania systemowe
{? _a=0 || return() ?};

_ctx:=params_get().ctx.GP_DEF;
:: ustal kontekst przeglądania
GP_DEF.index(_ctx.key);
GP_DEF.seek(_ctx.row);
~~


\def_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po obsłudze okna WER tabeli GP_DEF.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: ignoruj odrysowania systemowe
{? _a=0 || return() ?};

_ctx:=params_get().ctx.GP_DEF;
:: zapamiętaj kontekst przeglądania
_ctx.key:=GP_DEF.index('?');
_ctx.row:=GP_DEF.ref();
~~


\grp_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po odświeżeniu wiersza w oknie WER tabeli GP_GRP.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: odrysuj powiązane okna
grp_disp(GP_DOM,'WER',1,1);
grp_disp(GP_SYS,'WER',1,1);
~~


\dom_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Po odświeżeniu wiersza w oknie WER tabeli GP_DOM.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('dom_f_set','gp_api',1);
~~


\dom_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed obsługą okna WER tabeli GP_DOM.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: zgodne ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(GP_GRP,'WER',1)
|| return('#disable')
?};

GP_DOM.prefix(GP_GRP.ref());
~~


\sys_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed obsługą okna WER tabeli GP_DOM.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: zgodne ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(GP_GRP,'WER',1)
|| return('#disable')
?};

GP_SYS.prefix(GP_GRP.ref());
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 2b61c8a076c2a4330d4b5efa7748e1e1ea00458a8a08c469581d8287c655ffb2b7f04de8c1592e86c5fcccea6d698b1ea6daa71cd13ac3a00ce2e7afb6f3e153f5c3e46f81a7ec058849cf1a43eba90b60caa511ecc889f493f8133ffd3e98d89a50cf11bb3610187803adc6cc53a09aef24a2884f4268b5773a52437f98fcfa

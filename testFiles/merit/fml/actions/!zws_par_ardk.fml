:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ardk.fml
:: Utworzony: 12.04.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_ARDK - RODO: Konfiguracja.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: RODO: Konfiguracja - główna formuła czynności.
::   WE:
::   WY: ~~
::  OLD: \admin/ro_cfg.fml
::----------------------------------------------------------------------------------------------------------------------
RO_FML.cntx_psh();
RO_FML.win_sel('WER');
RO_FML.win_edit('RED');
RO_CFG.cntx_psh();
RO_CFG.win_sel('WER');
RO_CFG.win_edit('RED');
RO_CFG.index('NUMER');
RO_CFG.prefix();

RO_CFG.select();

RO_CFG.cntx_pop();
RO_FML.cntx_pop();
~~


\ro_cfg_fml_x_pt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Formuła na wzorzec pól FML_* tabeli RO_CFG.
::   WE:
::   WY: ''
::  OLD: \ro_cfg_fml_x_pt/ro_cfg.fml
::----------------------------------------------------------------------------------------------------------------------
RO_FML.fld_fml('RODZAJ','BLANK',$('\''+cur_afld()+'\''));
''


\ro_cfg_fml_x_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Po redagowaniu pól FML_* tabeli RO_CFG.
::   WE:
::   WY:
::  OLD: \ro_cfg_fml_x_ae/ro_cfg.fml
::----------------------------------------------------------------------------------------------------------------------
RO_FML.fld_fml('RODZAJ','BLANK',"*");
1


\ro_cfg_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Obsługa akcji "Dołącz - przed" dla tabeli RO_CFG.
::   WE: [_a] [NUMBER] - Korekta liczby porządkującej.
::   WY:
::  OLD: \ro_cfg_dolacz_b/ro_cfg.fml
::----------------------------------------------------------------------------------------------------------------------
_delta:={? var_pres('_a')=type_of(0) || _a || 0 ?};

:: Docelowy numer.
_dnr:={? RO_CFG.get() || RO_CFG.NUMER+_delta || 1 ?};
RO_CFG.blank();
RO_CFG.NUMER:=_dnr;
{? RO_CFG.edit("exec('ro_cfg_chk','ro_cfg')")
|| do();
   RO_CFG.cntx_psh();
   RO_CFG.index('NUMER');
   {? RO_CFG.last()
   || {!
      |? {? RO_CFG.NUMER>=_dnr
         || RO_CFG.NUMER+=1;
            RO_CFG.put();
            RO_CFG.prev()
         ?}
      !}
   ?};
   RO_CFG.cntx_pop();
   RO_CFG.add();
   end()
?}


\ro_cfg_dolaczvb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Obsługa akcji "Dołącz za - przed" dla tabeli RO_CFG.
::   WE:
::   WY:
::  OLD: \ro_cfg_dolaczvb/ro_cfg.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ro_cfg_dolacz_b','!zws_par_ardk',1)


\ro_cfg_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Obsługa akcji "Popraw - przed" dla tabeli RO_CFG.
::   WE:
::   WY:
::  OLD: \ro_cfg_popraw_b/ro_cfg.fml
::----------------------------------------------------------------------------------------------------------------------
{? RO_CFG.edit("exec('ro_cfg_chk','ro_cfg')")
|| RO_CFG.put()
?}


\ro_cfg_przesun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Obsługa akcji "Przesuń w górę" i "Przesuń w dół".
::   WE: _a [INTEGER] - Kierunek: -1 - w górę, 1 - w dół.
::   WY:
::  OLD: \ro_cfg_przesun/ro_cfg.fml
::----------------------------------------------------------------------------------------------------------------------
_put:=0;
_n_org:=RO_CFG.NUMER;
_n_new:=RO_CFG.NUMER+_a;
_n_tmp:=-1;

RO_CFG.cntx_psh();
RO_CFG.index('NUMER');
RO_CFG.prefix();
do();
{? RO_CFG.find_key(_n_new)
|| RO_CFG.NUMER:=_n_tmp;
   {? RO_CFG.put(1) & RO_CFG.find_key(_n_org)
   || RO_CFG.NUMER:=_n_new;
      {? RO_CFG.put(1) & RO_CFG.find_key(_n_tmp)
      || RO_CFG.NUMER:=_n_org;
         RO_CFG.put(1)
      ?}
   ?}
?};
end();
RO_CFG.cntx_pop();
~~


\ro_cfg_czytaj_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Formuła akcji Archiwum->Czytaj okienka WER tabeli RO_CFG.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('import_cfg','rodo')


\ro_cfg_zapisz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Formuła akcji Archiwum->Zapisz okienka WER tabeli RO_CFG.
::   WE:
::   WY:
::  OLD: \ro_cfg_zapisz_b/ro_cfg.fml
::----------------------------------------------------------------------------------------------------------------------
: zapis formuł
_name:=RO_FML.name()+'.txt';
{? fexists(_name,1)=0 | FUN.ask('Plik "%1" istnieje.\nCzy zastąpić jego zawartość?'@[_name])
|| RO_FML.cntx_psh();
   RO_FML.index('SYMBOL');
   RO_FML.prefix();
   RO_FML.export(_name,,,,,
      'RODZAJ',,1,,
      'SYMBOL',,2,,
      'OPIS',,3,,
      'UWAGI',,4,,
      'FORMULA',,5,
   );
   RO_FML.cntx_pop()
?};
: zapis konfiguracji
_name:=RO_CFG.name()+'.txt';
{? fexists(_name,1)=0 | FUN.ask('Plik "%1" istnieje.\nCzy zastąpić jego zawartość?'@[_name])
|| RO_CFG.cntx_psh();
   RO_CFG.index('NUMER');
   RO_CFG.prefix();
   RO_CFG.export(_name,,,,,
      'RO_REQC','CODE',1,,
      'KAT',,2,,
      'KOMENT',,3,,
      'FML_WCEL','SYMBOL',4,,
      'FML_SZUK','SYMBOL',5,,
      'FML_INFO','SYMBOL',6,,
      'FML_RET','SYMBOL',7,,
      'FML_ZAP','SYMBOL',8,,
      'FML_ZGO','SYMBOL',9,
   );
   RO_CFG.cntx_pop()
?};

FUN.info('Zakończono zapis archiwum.'@);
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:03 b101ea817c9c4fea207e8900b3b2a97f7064e699dd303e3bab54a79df56b72ce7548a6117977487e1da08642874bbac6de1ae4949a07a9c52ee2b71002959807a014a5bc5c6f9e2f90751783442731d372338b8a9b18e46660861c248e29c7f3b964d6b98e8c5bf5576d7e0abaa4bc2f013f206f68e323a81325ad4b6aeb9dd7

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zlc_rump.fml
:: Utworzony: 31.05.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PPL_ZLC_RUMP - Rejestracja miejsc pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja miejsc pracy - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_int:=_par.int;
_out:=_par.out;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.P);
{? _uidref=''
|| _result:=exec('error','!ppl_zlc_rump')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!ppl_zlc_rump',_in.P);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR[17.00]
:: OPIS: Rejestracja miejsc pracy - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj miejsce pracy zleceniobiorcy: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj miejsce pracy zleceniobiorcy: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj miejsce pracy zleceniobiorcy: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj miejsce pracy zleceniobiorcy'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie miejsc pracy niemożliwe.\nNie znaleziono zleceniobiorcy.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a [REFERENCE] - Wskazanie zleceniobiorcy.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a)
|| OSOBA.cntx_psh();
   P.OSOBA();

   H_UM.cntx_psh();
   H_UM.index('HUM_RUM');
   H_UM.prefix(P.ref());
   H_UM.index('LISTA');
   H_UM.prefix(P.ref());
   H_UM.index('OD');
   H_UM.prefix(P.ref());

   H.cntx_psh();
   H.index('HISTUM');

   H_UM.win_sel('GRP_ZR');
   exec('pkd_conf_h_um_icon','pkd');

   _ret:=H_UM.select();

   H.cntx_pop();

   H_UM.cntx_pop();

   OSOBA.cntx_pop()
|| _ret:=exec('error','!ppl_zlc_rump')
?};
P.cntx_pop();
_ret


\h_um_grp_zr_h_um_werz_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: "Po odświeżeniu" okna WERZ tabeli H_UM w oknie grupowym GRP_ZR tabeli H_UM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('h_um_z_act','ppl');
grp_disp(H,'WERZ',1)


\h_um_grp_zr_h_werz_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: "Przed obsługą" okna WERZ tabeli H w oknie grupowym GRP_ZR tabeli H_UM.
::   WE: _a [NUMBER] Tryb pracy:
::             0 - Odrysowanie.
::             1 - Aktywacja.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
H.prefix(H_UM.ref());
H.first();
:: Ustalenie dostępnych akcji, a przede wszystkim wyszarzenie akcji i przycisku "Zakończ", wykonywane jest
:: nie tylko podczas aktywowania okna (_a=1), ale również podczas zwykłego odrysowywania (_a=0).
:: Stąd poniższe wywołanie przed sprawdzeniem wartośc _a.
{? grp_empty(H_UM,'WERZ')
|| '#disable'
|| exec('h_z_act','ppl','PPL_ZLC_RUMP',1)
?}


\h_um_grp_zr_h_werz_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: "Po odświeżeniu" okna WERZ tabeli H w oknie grupowym GRP_ZR tabeli H_UM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Patrz komentarz dla formuły "Przed obsługą".
{? ~grp_empty(H_UM,'WERZ')
|| exec('h_z_act','ppl','PPL_ZLC_RUMP',1)
?}


\h_um_z_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiedzialna za utworzenie okna redagowania "miejsc pracy" dla zleceniobiorców i innych form
::       współpracy. Wykonywana dla ustalonego rekordu tabeli P.
::   WE: [_a] [NUMBER] - Zastosowanie okna:
::          0 - Okno do redagowania [domyślnie];
::          1 - Okno do wyświetlania.
::   WY: Akronim okna.
::----------------------------------------------------------------------------------------------------------------------
_display:=var_pres('_a')=type_of(0) & _a;

_f_zatr:=exec('dest','f_zatr',P.F_ZATR,'KOD');

_acr:={? _display || 'DISP_' || 'RED_' ?}+_f_zatr;
{? 1 | (_we:=__WND.EDIT.get(H_UM,_acr))=''
|| _we:=H_UM.mk_edit('Miejsce pracy'@,,,,,'html_maximized');
   {? _f_zatr='Z'
   || H_UM.win_ewin(_we,H_UM,'DOD_Z');
      {? _display
      || H_UM.win_ewin(_we,H,'DOD_ZD')
      || H_UM.win_ewin(_we,H,'DOD_ZE')
      ?}
   |? _f_zatr='K' | _f_zatr='R' | _f_zatr='T'
   || H_UM.win_ewin(_we,H_UM,'DOD_K');
      {? _display
      || H_UM.win_ewin(_we,H,'DOD_ZD')
      || H_UM.win_ewin(_we,H,'DOD_ZE')
      ?}
   ?};
   exec('ok_esc','#window',H_UM,_we,,,,,,,exec('text_red_ok','#window'));
   __WND.EDIT.put(H_UM,_acr,_we)
?};
H_UM.efld_opt(_we,'enable=%1,mark=%1' [$(PAR_SKID.get(313)='T')],EDIT_VAR,'H_PSTO');
_we


\h_um_z_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz" w oknie WERZ tabeli H_UM (dla zleceniobiorców).
::   WE:
::   WY:
::  OLD: \add_hum/umowy.fml - Spora zmiana założeń.
::----------------------------------------------------------------------------------------------------------------------
H_UM.cntx_psh();
H_UM.index('OD');
H_UM.prefix(P.ref());
{? H_UM.last() & H_UM.DO=date(0,0,0)
|| FUN.info('Nie można dołączyć informacji o nowym miejscu pracy, jeżeli poprzednie jest aktywne.'@)
|| H.cntx_psh();
   H.index('HISTUM');
   H.prefix(H_UM.ref());
   {? H.last()
   || _d0:=date(0,0,0);
      _miejsce:=H.MIEJSCE;
      _od:={? H_UM.DO=_d0 || _d0 || H_UM.DO+1 ?};
      H_UM.blank();
      H_UM.OD:=_od;
      H.prefix();
      H.blank();
      H.memo_set(,'INNE');
      H.MIEJSCE:=_miejsce;
      H_UM.win_edit(exec('h_um_z_red','!ppl_zlc_rump'));
      exec('h_um_bp_fld','pracownik',1);
      exec('h_um_bp_fld','pracownik',0,3);
      {? H_UM.edit("exec('h_um_p_ae','pracownik',0)")
      || do();
         {? H_UM.add()
         || H.UMOWA:=H_UM.ref();
            H.add();
            H.memo_put(,'INNE')
         ?};
         end()
      ?};
      exec('h_um_bp_fld','pracownik',1)
   ?};
   H.cntx_pop()
?};
H_UM.cntx_pop();
~~


\h_um_z_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw" w oknie WERZ tabeli H_UM (dla zleceniobiorców).
::   WE:
::   WY:
::  OLD: \popraw/umowy.fml - Spora zmiana założeń.
::----------------------------------------------------------------------------------------------------------------------
H.cntx_psh();
H.index('HISTUM');
H.prefix(H_UM.ref());
{? H.first()
|| H_UM.win_edit(exec('h_um_z_red','!ppl_zlc_rump'));
   H.memo_get(,'INNE');
   exec('h_um_bp_fld','pracownik',1);
   exec('h_um_bp_fld','pracownik',0,exec('h_um_mod_pos','pracownik',H_UM.ref(),H_UM.P));
   {? H_UM.edit("exec('h_um_p_ae','pracownik',1)")
   || do();
      H_UM.put();
      H.put();
      H.memo_put(,'INNE');
      H.prefix(H_UM.ref());
      {? H.first() & H.OD<>H_UM.OD
      || H.OD:=H_UM.OD;
         H.put()
      ?};
      {? H.last() & H.DO<>H_UM.DO
      || H.DO:=H_UM.DO;
         H.put()
      ?};

      end()
   ?};
   exec('h_um_bp_fld','pracownik',1)
?};
H.cntx_pop();
~~


\h_um_z_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń" w oknie WERZ tabeli H_UM (dla zleceniobiorców).
::   WE:
::   WY:
::  OLD: \del_hum/umowy.fml
::----------------------------------------------------------------------------------------------------------------------
{? H_UM.size()=1
|| FUN.emsg('Usunięcie umowy podstawowej nie jest możliwe.'@)

|? exec('del_conf','#table',H_UM)
|| H_UM.del(,1)

?}


\h_um_z_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Załączniki" w oknie WERZ tabeli H_UM (dla zleceniobiorców).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','P','H_UM',exec('chk_role','#b__box',OPERATOR.USER,'PPL_ZLC_RUMP'))


\h_um_z_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl" w oknie WERZ tabeli H_UM (dla zleceniobiorców).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
H.cntx_psh();
H.index('HISTUM');
H.prefix(H_UM.ref());
{? H.first() & H.memo_get(,'INNE')
|| H_UM.win_edit(exec('h_um_z_red','!ppl_zlc_rump',1));
   H_UM.display()
?};
H.cntx_pop();
~~


\h_z_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Załączniki" w oknie WERZ tabeli H (dla zleceniobiorców).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','P','H',exec('chk_role','#b__box',OPERATOR.USER,'PPL_ZLC_RUMP'))


\h_z_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiedzialna za utworzenie okna redagowania "miejsc pracy" dla zleceniobiorców i innych form
::       współpracy. Wykonywana dla ustalonego rekordu tabeli P.
::   WE: [_a] [NUMBER] - Zastosowanie okna:
::          0 - Okno do redagowania [domyślnie];
::          1 - Okno do wyświetlania.
::   WY: Akronim okna.
::----------------------------------------------------------------------------------------------------------------------
_display:=var_pres('_a')=type_of(0) & _a;

_f_zatr:=exec('dest','f_zatr',P.F_ZATR,'KOD');

_acr:={? _display || 'DISP_' || 'RED_' ?}+_f_zatr;
{? 1 | (_we:=__WND.EDIT.get(H,_acr))=''
|| _we:=H.mk_edit('Miejsce pracy'@,,,,,'html_maximized');
   {? _f_zatr='Z'
   || H.win_ewin(_we,H,'POP_Z','Współpraca'@);
      {? _display
      || H.win_ewin(_we,H,'DOD_ZD')
      || H.win_ewin(_we,H,'DOD_ZE')
      ?}
   |? _f_zatr='K' | _f_zatr='R' | _f_zatr='T'
   || H.win_ewin(_we,H,'POP_P','Współpraca'@);
      {? _display
      || H.win_ewin(_we,H,'DOD_ZD')
      || H.win_ewin(_we,H,'DOD_ZE')
      ?}
   ?};
   exec('ok_esc','#window',H,_we,,,,,,,exec('text_red_ok','#window'));
   __WND.EDIT.put(H,_acr,_we)
?};

: Przed modyfikacją (Dołącz, Popraw) ustawiana jest wartość formuł "przed redagowaniem".
: Ustawiamy gwiazdki na podstawie wyników tych foprmuł.
H.efld_opt(_we,'mark=%1' [$(H.fld_fml('OD','*BEFORE_EDIT')())],,'OD');
H.efld_opt(_we,'mark=%1' [$(H.fld_fml('DO','*BEFORE_EDIT')())],,'DO');

H.efld_opt(_we,'enable=%1,mark=%1' [$(PAR_SKID.get(313)='T')],EDIT_VAR,'H_PSTO');
_we


\h_z_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz" w oknie WERZ tabeli H - dodaje przebieg zatrudnienia.
::       - Dodawany przebieg traktowany jest jako ostatni.
::       - Data rozpoczęcia przebiegu nie może być wcześniejsza od daty ostatniego, istniejącego.
::       - Jeśli umowa została zakończona, to data zakończenia przebiegu jest zablokowana do edycji.
::  OLD: \add_h/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=H.ref();

: Wartości do przeniesienia z ostatniego przebiegu.
_inne:='';
_miejsce:='';

H.cntx_psh();
H.index('HISTUM');
H.prefix(H_UM.ref());
{? H.last()
|| _miejsce:=H.MIEJSCE;
   _inne:=H.memo_txt(,1,'INNE')
?};

H.blank();
H.DO:=H.UMOWA().DO;
H.MIEJSCE:=_miejsce;
H.memo_set(_inne,'INNE');

: Dostępność pól.
H.fld_fml('OD','BEFORE_EDIT',"1");
H.fld_fml('DO','BEFORE_EDIT',"0");

: edycja
_ok:=0;
errno();
H.win_edit(exec('h_z_red','!ppl_zlc_rump'));
{? H.edit("exec('h_ae','pracownik',0)")
|| _msg:=no_msg(1);
   do();
   {? H.add(1) & H.memo_put(,'INNE')
   || _ref:=H.ref();
      _do:=H.OD-1;
      {? H.prev()
      || H.DO:=_do;
         {? H.put(1)
         || _ok:=1
         ?}
      ?}
   ?};
   {? ~_ok
   || undo()
   ?};
   end();
   no_msg(_msg);
   {? (_errno:=errno())<>0
   || FUN.error('Wystąpił błąd podczas próby zapisu danych. (Kod błędu: %1)'@ [$_errno])
   ?}
?};

H.fld_fml('OD','BEFORE_EDIT',"*");
H.fld_fml('DO','BEFORE_EDIT',"*");

H.cntx_pop();

H.seek(_ref);
~~


\h_z_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw"  w oknie WERZ tabeli H.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
H.cntx_psh();
H.index('HISTUM');
H.prefix(H_UM.ref());

: Dostępność pól.
_pos:=exec('h_mod_pos','pracownik',H.ref(),H.UMOWA);
H.fld_fml('OD','BEFORE_EDIT',$$(_pos=2 | _pos=3));
H.fld_fml('DO','BEFORE_EDIT',$$(_pos=1 | _pos=2));

: Edycja
_ok:=0;
errno();
H.memo_get(,'INNE');
H.win_edit(exec('h_z_red','!ppl_zlc_rump'));
{? H.edit("exec('h_ae','pracownik',1)")
|| do();
   {? H.memo_put(,'INNE') & H.put(1)
   || _ok:=1;
      _ref:=H.ref();
      _do:=H.OD-1;
      _od:=H.DO+1;
      {? H.prev()
      || H.DO:=_do;
         {? ~H.put()
         || _ok:=0
         ?}
      ?};
      {? H.seek(_ref) & H.next()
      || H.OD:=_od;
         {? ~H.put()
         || _ok:=0
         ?}
      ?}
   ?};
   {? ~_ok
   || undo()
   ?};
   end();
   {? (_errno:=errno())<>0
   || FUN.error('Wystąpił błąd podczas próby zapisu danych. (Kod błędu: %1)'@ [$_errno])
   || _ok:=1
   ?}
?};

H.fld_fml('OD','BEFORE_EDIT',"*");
H.fld_fml('DO','BEFORE_EDIT',"*");

H.cntx_pop();
H.get();
~~


\h_z_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń" w oknie WERZ tabeli H.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('del_ask','#table',H) & H.del()
|| exec('norm_h_oddo','pracownik')
?}


\h_z_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl" w oknie WERZ tabeli H (dla zleceniobiorców).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
H.cntx_psh();
H.win_edit(exec('h_z_red','!ppl_zlc_rump',1));
H.display();
H.cntx_pop();
~~


\h_z_zakoncz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (grupowe okno wertowania tabeli H_UM i H z doklejoną wizytówką);
::          - w ramach obszaru roboczego (okna wertowania tabel H_UM i H w jednej zakładce).
::       Właściwie akcja dotyczy czynności związanej z tabelą H_UM a nie H, ale zaproponowane rozwiązanie ("Zakończ"
::       w oknie H) ma uzasadnienie interfejsowe.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=H_UM
:: Zupełnie wyjątkowa sytuacja - akcja "Zakończ" znajduje się w oknie grupowym zbudowanym dla tabeli H_UM
:: a nie dla tabeli H.
|| sel_exit()

|| params_set(_par:=params_get());
:: Z uwagi na sposób prezentacji w obszarze roboczym (dwie czynnosci na jednej zakładce) obsługa akcji jest inna niż
:: w przypadku pozostałych czynności.
   exec('run_act','ppl',P,'PPL_ZLC_RUMP','ZAKOŃCZ',_par.cfg.nav.main)
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 bc5079c1cc85a11f360b6d70d0a770145ad8cb32988193f9d44eb3b41798fc87e3582bf7b28f656d2b618526c100de8633f24f099d348c1c2d484c1162636ca4616ab7fd4355567e8eb82e9815d11f080a6238ded42cf8495a10e9f25aadb36ff8e2241301d5d9fb7d373d956d46490950e32f669cd56e5ef261bd226f6551b8

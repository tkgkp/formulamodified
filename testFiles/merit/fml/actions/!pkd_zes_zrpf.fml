:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_zes_zrpf.fml
:: Utworzony: 11.05.2016
:: Autor: DAROKR
::======================================================================================================================
:: Zawartość: PKD_ZES_ZRPF - Raporty rozliczeniowe PFRON.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [17.00]
:: OPIS: Raporty rozliczeniowe PFRON - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
: warunek podstawowy działania tylko dla formy zatrudnienia 'P'
{? __PARSES.getVal('F_ZATR').KOD<>'P'
|| FUN.emsg('Obszar niedostępny dla formy współpracy "%1".'@[__PARSES.getVal('F_ZATR').OPIS]);
   return()
?};
exec('inf_o_zatr','!pkd_zes_zrpf');
~~


\inf_o_zatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.30]
:: OPIS: Informacja o zatrudnieniu
::  OLD: \inf_o_zatr/pfron.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('no_limit','schemat','PKD') || return ?};

_cfg:=tab_tmp(,'ROK','INTEGER','Rok'@,
   'MC','INTEGER','Miesiąc'@,
   'METODA','INTEGER','Obliczanie średniego zatrudnienia'@);
_cfg.ROK:=date()~1;
_cfg.MC:=date()~2;
_cfg.METODA:=1;
_red:=_cfg.mk_edit('Raport rozliczeniowy PFRON'@,0,'pfron2');
_cfg.win_esep(_red,'Okres sprawozdawczy'@);
_cfg.win_efld(_red,,'ROK',,,5,,0,'Rok'@);
_cfg.win_efld(_red,,'MC',,,5,,0,'Miesiąc'@);
_cfg.win_esep(_red,'Obliczanie średniego zatrudnienia'@);
_cfg.win_efld(_red,,'METODA',,,,,0,'Metoda'@,,,'radio-buttons',,'Pełna'@,"0",'Uproszczona'@,"1");
exec('ok_esc','#window',_cfg,_red);
_cfg.win_edit(_red);
_cfg.efld_opt(_red,'mark=1',_cfg,'ROK');
_cfg.efld_opt(_red,'mark=1',_cfg,'MC');
params_set('cfg',_cfg);
{? _cfg.edit("_par:=params_get(); _cfg:=_par.cfg;
       {? _cfg.ROK<1900
       || FUN.emsg('Proszę podać poprawny rok do przygotowania deklaracji.'@); 'ROK'
       |? _cfg.MC<1 | _cfg.MC>12 || FUN.emsg('Błędny miesiąc.'@);'MC'
       || 1
       ?}")
|| VAR_DEL.delete('__PFRONZAT');
   __PFRONZAT:=obj_new(12);
   {! _lp:=1 .. obj_len(__PFRONZAT) |! __PFRONZAT[_lp]:=0 !};

   exec('zatrudnienie','pfron',date(_cfg.ROK,_cfg.MC,1),date(_cfg.ROK,_cfg.MC,0),_cfg.METODA);

   _tab:=tab_tmp(1,
      'LP','INTEGER','Lp.'@,
      'OPIS','STRING[70]','Opis'@,
      'ETATY','REAL','Etatów'@,
      'OSOBY','REAL','Osób'@
   );
   _add:="_a.blank(); _a.LP:=_a.size()+1; _a.OPIS:=_b; _a.ETATY:=_c; _a.OSOBY:=_d; _a.add()";

   _add(_tab,'Stopień niepełnosprawności: ZNACZNY; Szczególne schorzenie: TAK'@,__PFRONZAT[2],__PFRONZAT[8]);
   _add(_tab,'Stopień niepełnosprawności: ZNACZNY; Szczególne schorzenie: NIE'@,__PFRONZAT[3],__PFRONZAT[9]);
   _add(_tab,'Stopień niepełnosprawności: UMIARKOWANY; Szczególne schorzenie: TAK'@,__PFRONZAT[4],__PFRONZAT[10]);
   _add(_tab,'Stopień niepełnosprawności: UMIARKOWANY; Szczególne schorzenie: NIE'@,__PFRONZAT[5],__PFRONZAT[11]);
   _add(_tab,'Stopień niepełnosprawności: LEKKI'@,__PFRONZAT[6],__PFRONZAT[12]);
   _add(_tab,'Zatrudnienie OGÓŁEM'@,__PFRONZAT[1],__PFRONZAT[7]);

   _lw:=_tab.size()+1;
   {? _lw<5 || 5 |? _lw>30 || 30 ?};
   _ws:=_tab.mk_sel('Dane o zatrudnieniu - %1 r.'@[date(_cfg.ROK,_cfg.MC,1)$8],'N',,'#epfron2',,,_lw);
   _tab.win_fld(_ws,,'OPIS');
   _tab.win_fld(_ws,,'ETATY',,,,2);
   _tab.win_fld(_ws,,'OSOBY',,,,2);
   _tab.win_act(_ws,,'Szukaj');
   _tab.win_sel(_ws);

   _tab.select();

   VAR_DEL.delete('__PFRONZAT');
   ~~
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 71c6771d4336809690580e1bac72a6a9e7ceb681111362e36cf63a91695f0ba7b6389770114abed0f36748d649fb5738183bb6964628098929222d2616dcc8406720b255d1d375eaba3549b5a263cc4f2de4ff3ba55ca927ba020761d9ec8b452d2437c714ba2af945cd51c6931cacdf52e066dc47962501808134ff3b0ee956

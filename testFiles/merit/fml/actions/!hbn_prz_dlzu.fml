:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !hbn_prz_dlzu.fml
:: Utworzony: 06.12.2017
:: Autor: DAROKR
::======================================================================================================================
:: Zawartość: Obsługa czynności HBN_PRZ_DLZU - Utw. przelewu do ZUS z wynagr.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Utw. przelewu do ZUS z wynagr. - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,  symbol=SYMBOL,  type=STRING, name=Symbol raportu, required=T
::# kind=WE,  symbol=RU,      type=NUMBER, name=Rok ubezpieczeniowy, required=T, keyref=N
::# kind=WE,  symbol=MU,      type=NUMBER, name=Miesiąc ubezpieczeniowy, required=T, keyref=N
::# kind=WE,  symbol=KWOTA,   type=NUMBER, name=Łączna kwota do przelewu ZUS, required=N
::
::# kind=WEW, symbol=P_ZLEC,  type=DATE,   name=Data zlecenia, required=N, keyref=N
::# kind=WEW, symbol=P_DATA,  type=DATE,   name=Data przelewu, required=N, keyref=N
::# kind=WEW, symbol=DONE,    type=NUMBER, name=Status przetwarzania, required=N, keyref=N
::
::# kind=WY,  symbol=PB, type=_PB, name=Wskazanie polecenia przelewu, required=N, keyref=N
::
::# permissions=F_ZATR,UD_SKL,HRB,HRP,FJKS

_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:='_'+_in.SYMBOL;
_result:='';

{? _id=''
|| _result:=exec('error','!hbn_prz_dlzu')

|| _mp.keyRef(_id,0,0);
   exec('init','!hbn_prz_dlzu');
   _kwota:=_in.KWOTA;
   _value:=exec('send','!hbn_prz_dlzu','ZUS',_kwota,_in.SYMBOL);
   {? type_of(_value)=type_of(0)
   || exec('save','!hbn_prz_dlzu');
      {? _mp.isMicro()
      || _mp.cancel()
      || _mp.done()
      ?}
   || _result:=_value
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Utw. przelewu do ZUS z wynagr. - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().mp.load(exec('kind_in','#b_port'));
'Przelew składek do ZUS z raportu %1'@@[_in.SYMBOL]


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Zwraca treść komunikatu błędu.
::   WE:
::   WY: Treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Utworzenie przelewu do ZUS niemożliwe.'@


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Przywróć zapamiętane wartości parametrów raportu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_in:=_par.in;
_ib:=_par.int;

exec('setup_buttons','#window',ZMIENNE,'PRZ_ZUSN',_par.mp.isMicro(),'DONE','OK','ANULUJ');

_koniec_msc:=date(_in.RU,_in.MU,0);
_data:=_koniec_msc+1;
exec('czytaj','#stalesys',_koniec_msc,KST,KST_PAR);

{? type_of(_ib.P_ZLEC)=type_of(ZMIENNE.ZLEC)
|| ZMIENNE.ZLEC:=_ib.P_ZLEC
|| ZMIENNE.ZLEC:=_data
?};
{? type_of(_ib.P_DATA)=type_of(ZMIENNE.PRZE)
|| ZMIENNE.PRZE:=_ib.P_DATA
|| ZMIENNE.PRZE:=_data
?};
~~


\save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Zapamiętuje informacje podane przez użytkownika na potrzeby ponownego uruchomienia czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_ib:=_par.int;
_rv:=_par.out;

_ib.P_ZLEC:=ZMIENNE.ZLEC;
_ib.P_DATA:=ZMIENNE.PRZE;

_par.mp.save(_ib,_rv);
~~


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Obsługa akcji Zakończ - przejdź do następnego etapu procesu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_get().int.DONE:=1;
'key:F2'


\keep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Obsługa akcji Zapisz - podtrzymaj czynność na liście zadań.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_get().int.DONE:=0;
'key:F2'


\send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Tworzenie polecenia przelewu dla ZUSu
::   WE: _a - ZUS jedna kwota przelewu od 01.01.2018r.
::       _b - kwota
::       _c - identyfikator deklaracji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_in:=_par.in;
_rv:=_par.out;

SKID_RBK.cntx_psh();
SKID_RBK.prefix();

_ref_rbk:=exec('find','!hbn_prz_dlzu',_a);
{? ~_ref_rbk | ~SKID_RBK.seek(_ref_rbk)
|| SKID_RBK.cntx_pop();
   return('Brak rachunku bankowego dla jednego przelewu ZUS.'@)
|? _ref_rbk<>null() & SKID_RBK.seek(_ref_rbk)
|| _rw:=gsub(SKID_RBK.N,' ','');
   {? _rw=''
   || SKID_RBK.cntx_pop();
      return('Brak rachunku bankowego dla jednego przelewu ZUS.'@)
   ?}
?};
{? ~KST.ODDZ_ZUS
|| SKID_RBK.cntx_pop();
   return('Oddział ZUS nie został określony w stałych systemu.'@)
?};

ZMIENNE.win_edit('PRZ_ZUSN');
ZMIENNE.TYTUL:='Wpłata do ZUS';

{? ~exec('rachunki_banki','rachunki',exec('desc','!hbn_prz_dlzu')) |
   ~ZMIENNE.edit("
      {? (_chk:=__CHK.record(ZMIENNE,,'ZLEC','PRZE'))<>''
      || return(_chk)
      ?};
      {? ZMIENNE.PRZE<>date(0,0,0) & ZMIENNE.PRZE<date(2018,1,1)
      || FUN.emsg('Data przelewu nie może być wcześniejsza niż %1.'@[date(2018,1,1)$6]);
         'PRZE'
      || ''
      ?}
   ")
|| SKID_RBK.cntx_pop();
   return(0)
?};

_kid:=_a+_c;
_pb:=exec('get_pb_by_id','hbn',_kid);
{? _pb.ACTION='none'
:  przelew zablokowany
|| exec('cant_gen','hbn');
   _rv.PB:=null

|| PB.cntx_psh();
   PB.use('pbxxxx');
   PB.prefix();
   {? _pb.ACTION='put'
   || PB.seek(_pb.REF)
   || PB.blank()
   ?};
   exec('ustaw_pb','rachunki','F');
   PB.NR:=0;                     'numer w paczce';
   PB.NBD:=SKID_RBK.BANK;        'bank dłużnika';
   PB.RD:=SKID_RBK.N;            'numer rachunku dłużnika';
   PB.W:=KST.ODDZ_ZUS().NAZWA;   'nazwa wierzyciela';
   PB.UL:=ADRES.ULICA+' '+ADRES.DOM+' '+ADRES.LOKAL;  'ulica dom lokal';
   SKID_RBK.seek(_ref_rbk);
   PB.NBW:=SKID_RBK.BANK;        'bank wierzyciela';
   'numer rachunku wierzyciela';
   PB.RW:=gsub(SKID_RBK.N,' ','');
   PB.TYT:=ZMIENNE.TYTUL;
   PB.DZ:=ZMIENNE.ZLEC;           'data zlecenia';
   PB.DP:=ZMIENNE.PRZE;           'data przelewu';
   PB.KW:={? var_pres('_b')=type_of(0) || _b ?};
   PB.KD:='PPL: '+_a;           'Kod HB';
   PB.PR:='N';                  'Przekazany?';
   PB.KID:=_kid;
   PB.WAL:=KST_PAR.NAROD;
   PB.RODZ:='K';
   _rv.PB:=null;
   {? _pb.ACTION='put'
   || {? PB.KW<>0
      || {? PB.put()
         || _rv.PB:=PB.ref()
         ?}
      || PB.del()
      ?}
   || {? PB.KW<>0 & PB.add()
      || _rv.PB:=PB.ref()
      ?}
   ?};
   PB.cntx_pop()
?};

obj_del(_pb);
SKID_RBK.cntx_pop();
1


\find
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [18.02]
:: OPIS: Znalezienie odpowiedniego rachunku zusowskiego
::   WE: _a - ZUS - jedna kwota przelewu od 01.01.2018r.
::   WY: ref znalezionego rachunku, badz null jesli nie znaleziono
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
SKID_RBK.cntx_psh();
SKID_RBK.index('TAB');
SKID_RBK.prefix(exec('ref_firma','ustawienia'),'ZUS','ZUS');
{? SKID_RBK.first()
|| {!
   |? {? SKID_RBK.SLO().KOD=_a
      || _ref:=SKID_RBK.ref();
         0
      || SKID_RBK.next()
      ?}
   !}
?};
SKID_RBK.cntx_pop();
_ref

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:37 c88279016f65cadd99c38c050def6857995e67db3fc2c2999b3ec41156ee1316e55f8f3af931a62563123593a25afd1acd2db8680023eb16d54819a9daef4f46dcb8d6a11a600a6a8e154f0618e77fac5dfadcafedd1234a84a4ba160b000fe9d7637cd385536bae0e7fd370864dad979b03977f5d6061e47807d0bd98b64bf9

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_artl.fml
:: Utworzony: 07.05.2020
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_ARTL - Zawartość rejestru tłumaczeń.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Zawartość rejestru tłumaczeń - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
RT_TEKST.cntx_psh();
RT_TEKST.f_clear();
RT_TEKST.clear();
RT_TEKST.index('OPIS');

RT_TEKST.win_sel('WER');
RT_TEKST.select();

RT_TEKST.cntx_pop();
~~


\_rt_tekst_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Przed zapisem wiersza tabeli RT_TEKST.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
RT_TEKST.STATUS:={? form(RT_TEKST.OBCY)<>'' || 'T' || 'N' ?};
RT_TEKST.JEST:={? RT_TEKST.REKORD<>'' || 'T' || 'N' ?};
1


\_rt_tekst_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli RT_TEKST.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_rt_tekst_modb','!zws_par_artl')


\_rt_tekst_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli RT_TEKST.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_rt_tekst_modb','!zws_par_artl')


\rt_tekst_jest_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Przed wyświetleniem pola JEST w wierszu tabeli RT_KONF. Weryfikuje, czy tekst występuje w danych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
{? RT_TEKST.REKORD=''
:: nie istnieje
|| return(1)
?};

RT_TEKST.RT_KONF();
{? exec('rt_konf_pole_chk','rt_lib')<>''
:: kolumna lub tabela już nie istnieją
|| RT_TEKST.REKORD:='';
   RT_TEKST.put();
   return(1)
?};

_TAB:=($RT_KONF.TABELA)();
_TAB.cntx_psh();
{? _TAB.seek(RT_TEKST.REKORD,,1)
|| {? ($('_a.%1'[RT_KONF.POLE]))(_TAB)<>RT_TEKST.ORYGINAL
   || RT_TEKST.REKORD:='';
      RT_TEKST.put()
   ?}
|| RT_TEKST.REKORD:='';
   RT_TEKST.put()
?};
_TAB.cntx_pop();

1


\edit_var_rt_caly_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Przed wyświetleniem pola RT_CALY zmiennej EDIT_VAR. Steruje dostępnością pozostałych pól.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_enable:=$('EDIT_VAR.efld_opt(EDIT_VAR.win_edit(\'?\'),\'enable=%1\',,_a)'
   [$(EDIT_VAR.RT_CALY<>'T')]
);

_enable('RT_KONF');
_enable('RT_JEZYK');

{? EDIT_VAR.RT_CALY='T'
|| EDIT_VAR.RT_KONF:=null;
   EDIT_VAR.RT_JEZYK:=null
?};

1


\edit_var_rt_caly_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Po edycji pola RT_CALY zmiennej EDIT_VAR.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('edit_var_rt_caly_bd','!zws_par_artl');
1


\rt_tekst_obcy_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Obsługa przycisku pola OBCY w wierszu tabeli RT_TEKST. Pozwala na wybór zdefiniowanej tabeli.
::   WE:
::   WY: wybrany tekst lub ~~ w przypadku rezygnacji
::----------------------------------------------------------------------------------------------------------------------
:: lista tekstów dostępnych do wyboru
_BUF:=sql(
   'select distinct RT_TEKST.OBCY '
   'from RT_TEKST '
   'where RT_TEKST.SLO_KOD=:_a and RT_TEKST.OBCY<>\'\' '
   'order by OBCY',
   RT_TEKST.SLO_KOD
);
_wnd:=_BUF.mk_sel('Zawartość rejestru (%1)'@[RT_TEKST.SLO_KOD().KOD],'P',0,'#rt_tekst_obcy',60,,,,'U');
_BUF.win_fld(_wnd,,'OBCY',,,-60,,,exec('fldLabel','#field',RT_TEKST,'OBCY'));
_BUF.win_act(_wnd,,'Formuła','Wybierz'@,,,"sel_exit()",,1);
_BUF.win_sel(_wnd);

: wybór zestawu
{? _BUF.select(,1)
|| _BUF.OBCY
|| ~~
?}


\rt_tekst_wb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Przed wyświetleniem okienka tabeli RT_TEKST.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
RT_KONF.win_dict('SLO');
RT_TEKST.win_edit('RED');
RT_TEKST.win_patt('WZO');

exec('aktualizuj','rt_lib',,,1);
1


\rt_tekst_tlumacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Akcja "Przetłumacz" okienka tabeli RT_TEKST.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{!
|? _next:=0;
   RT_TEKST.RT_KONF();
   RT_TEKST.SLO_KOD();
   _BUF:=sql(
      'select distinct RT_TEKST.OBCY '
      'from RT_TEKST '
      'where '
      '  RT_TEKST.SLO_KOD=:_a and RT_TEKST.OBCY<>\'\' and '
      '  lower(trim(RT_TEKST.ORYGINAL))=\':_b\'',
      RT_TEKST.SLO_KOD,gsub(-form(RT_TEKST.ORYGINAL),'\'','\'\'')
   );
   {? _BUF.first()
   || {? _BUF.size()=1
      || RT_TEKST.OBCY:=_BUF.OBCY
      || _wnd:=_BUF.mk_sel('Sugestie'@,,0,'#rt_sugestie',,,,,'U');
         _BUF.win_fld(_wnd,,'OBCY',,,60,,,MS.name(RT_TEKST,'OBCY'));
         _BUF.win_act(_wnd,,'Formuła','Wybierz'@,,'Wybór bieżącego zapisu'@,,"sel_exit()",1);
         _BUF.win_act(_wnd,,'Szukaj');
         _BUF.win_act(_wnd,,'Kolejność');
         _BUF.win_sel(_wnd)
      ?}
   ?};
   params_set('BUF',_BUF);
   RT_TEKST.btn_eopt(RT_TEKST.win_edit('?'),'SUGESTIE','state=%1'[{? _BUF.size()>1 || 'normal' || 'grayed' ?}]);
   {? RT_TEKST.edit("
         {? RT_KONF.DL_SPR<>'N'
         || {? +form(RT_TEKST.OBCY)>RT_KONF.DL_OBCY
            || _text:='Przekroczona długość tekstu tłumaczenia (dostępne znaki: %1).'@[$RT_KONF.DL_OBCY];
               {? RT_KONF.DL_SPR='N'
               || FUN.emsg(_text);
                  return('OBCY')
               || {? ~FUN.ask(_text+'\nCzy zapisać nadmiarowy tekst?')
                  || return('OBCY')
                  ?}
               ?}
            ?}
         ?};
         1
      ")
   || RT_TEKST.put();
      {? ~RT_TEKST.f_active()
      || _next:=RT_TEKST.next()
      || _next:=RT_TEKST.f_next();
         RT_TEKST.f_rfresh()
      ?}
   ?};
   obj_del(_BUF);
   _next
!};
~~


\rt_tekst_sugestie_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Formuła przycisku "Sugestie".
::   WE:
::   WY: '' lub 'key:F2' w przypadku wyboru
::----------------------------------------------------------------------------------------------------------------------
_BUF:=params_get().BUF;

{? _BUF.select()
|| RT_TEKST.OBCY:=_BUF.OBCY;
   'key:F2'
|| ''
?}


\rt_tekst_aktualizuj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Akcja "Aktualizuj" okienka tabeli RT_TEKST. Uzupełnia zawartość rejestru na podstawie ustawień.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? EDIT_VAR.RT_CALY=''
|| EDIT_VAR.RT_CALY:='T'
?};

EDIT_VAR.win_edit('RT_AKT');
{? EDIT_VAR.edit("
      {? EDIT_VAR.RT_CALY<>'T' & EDIT_VAR.RT_KONF=null & EDIT_VAR.RT_JEZYK=null
      || FUN.ask('Czy na pewno aktualizować całą zawartość rejestru tłumaczeń?'@)
      || 1
      ?}
   ")=0
|| return()
?};

:: aktualizuj zawartość rejestru tłumaczeń
_val:=exec('aktualizuj','rt_lib',EDIT_VAR.RT_KONF,EDIT_VAR.RT_JEZYK,1);

:: raport
{? _val.STATUS=0
|| FUN.emsg(_val.INFO)
|| FUN.info(_val.INFO)
?};
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:22 e0be92ad2bf08f417958ef676e68119963b82b68b179f1f2edc28d3d5c6c4ff46ea832a91af4799c43738bac7d8bcae541a386a5d03edae353dc980f5158f59c7d697635c2cb4a7bd2c47ac7dded64465795125fbe1b9513eb87b356618f73a87c68778dc10df9f92feab1e25c9bc2f04546fc767cea4e019ddbcc0570b66385

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_vat_zdoz.fml
:: Utworzony: 06.07.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VAT_ZDOZ - Rejestry VAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przeglądanie rejestru VAT - główna formuła czynności FKS_VAT_ZDOZ.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodanie zakładki do okna grupowego obszaru evidencji VAT (FKS_VAT)
::   WE: _a - nazwa zakładki
::       _b - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
_grp:=_b;
_win_fml:="{? DVAT.USUNIETY || 'xwin16.png:81' |? DVAT.DOK().E_DOC || 'xwin16.png:51' || '' ?}";
DVAT.win_fml('WER',,'SYM1',,'ICON_BEFORE',_win_fml);
_win_fml:="_rn:=ref_name(DVAT.DOK);
           DOK.cntx_psh(); 
           DOK.use(_rn); DVAT.DOK();
           _kn:=DOK.DOK_REJ().KOR_NAG='N' & (3+DOK.ZAR='FKS' & DOK.KN<>'' | 3+DOK.ZAR='LSP' & DOK.LKN<>'');
           DOK.cntx_pop();
           {? _kn || 'xwin16.png:11' || '' ?} ";
DVAT.win_fml('WER',,'DAT2',,'ICON_BEFORE',_win_fml);
_odspvat:="DVAT.DOK(); PVAT.index('NR'); PVAT.prefix(DVAT.ref); PVAT.first; grp_disp(PVAT,'WER_WORK',1,1)";
TREE_VAT.win_sopt(w_acr,'select_record_checkbox = 0');
TREE_VAT.grp_sel(_grp,,w_acr,_a,"exec('arw_tree_vat','!fks_vat_zdoz')",0,0,,"__wgkraj:=0","",,,'maximized_with_title');
task_attach('FKS_VAT_ZDOZ');
task_attach('FKS_VAT_DTRO');
task_attach('FKS_VAT_DDOK');
task_attach('FKS_VAT_DDNR');
task_attach('FKS_VAT_ZREJ');
task_attach('FKS_VAT_ZREV');
task_attach('FKS_VAT_ZSQL');
TREE_VAT.tab_splt(_grp,,'vertical','left',',28%');
TREE_VAT.grp_sel(_grp,DVAT,'WER',,_odspvat,,,20,"","",,,'maximized_with_title');
TREE_VAT.tab_splt(_grp,'left','horizontal','bottom');
TREE_VAT.grp_sel(_grp,PVAT,'WER_WORK',,"exec('vatpozod','!fks_vat_zdoz')",,,,"","",,,'maximized_with_title')


\add_tab2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_09]
:: OPIS: Dodanie zakładki wg krajów do okna grupowego obszaru evidencji VAT (FKS_VAT)
::   WE: _a - nazwa zakładki
::       _b - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
_grp:=_b;
_win_fml:="{? DVAT.USUNIETY || 'xwin16.png:81' |? DVAT.DOK().E_DOC || 'xwin16.png:51' || '' ?}";
DVAT.win_fml('WER_KRA2',,'SYM1',,'ICON_BEFORE',_win_fml);
_win_fml:="_rn:=ref_name(DVAT.DOK);
           DOK.cntx_psh(); 
           DOK.use(_rn); DVAT.DOK();
           _kn:=DOK.DOK_REJ().KOR_NAG='N' & (3+DOK.ZAR='FKS' & DOK.KN<>'' | 3+DOK.ZAR='LSP' & DOK.LKN<>'');
           DOK.cntx_pop();
           {? _kn || 'xwin16.png:11' || '' ?} ";
DVAT.win_fml('WER_KRA2',,'DAT2',,'ICON_BEFORE',_win_fml);
_odspvat:="DVAT.DOK(); PVAT.index('NR'); PVAT.prefix(DVAT.ref); PVAT.first; exec('upd_sum_ecom','!fks_vat_zdoz'); grp_disp(DVAT,'WER_KRA2'); grp_disp(PVAT,'WER_WORK',1,1)";
TREE_VAT.grp_sel(_grp,,w_acr,_a,"exec('arw_tree_vat2','!fks_vat_zdoz')",0,0,,"__wgkraj:=1","",,,'maximized_with_title');
task_attach('FKS_VAT_ZDOZ');
task_attach('FKS_VAT_DTRO');
task_attach('FKS_VAT_DDOK');
task_attach('FKS_VAT_DDNR');
task_attach('FKS_VAT_ZREJ');
task_attach('FKS_VAT_ZREV');
task_attach('FKS_VAT_ZSQL');
TREE_VAT.tab_splt(_grp,,'vertical','left',',28%');
TREE_VAT.grp_sel(_grp,DVAT,'WER_KRA2',,_odspvat,,,20,"","",,,'maximized_with_title');
TREE_VAT.tab_splt(_grp,'left','horizontal','bottom');
TREE_VAT.grp_sel(_grp,PVAT,'WER_WORK',,"exec('vatpozod','!fks_vat_zdoz')",,,,"","",,,'maximized_with_title')


\arw_tree_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po odświeżeniu okna z rejestrami VAT
::----------------------------------------------------------------------------------------------------------------------
__wgkraj:=0;
{? TREE_VAT.POZ=2 & TREE_VAT.RVAT_REF<>''
|| _byl_rvt:=($RVAT.ref=TREE_VAT.RVAT_REF);
   {? _byl_rvt | RVAT.seek(BB.sqlint(TREE_VAT.RVAT_REF),form(8+TREE_VAT.RVAT_REF))
   || {? TREE_VAT.ODD_REF<>''
      || {? $OPERATOR.DEPT<>TREE_VAT.ODD_REF
         || OPERATOR.DEPT:={? ODD.seek(BB.sqlint(TREE_VAT.ODD_REF),form(8+TREE_VAT.ODD_REF)) || ODD.ref || null ?}
         ?}
      || OPERATOR.DEPT:=null
      ?};
      {? (_odd:=OPERATOR.DEPT)
      || DVAT.index('NR_O'); DVAT.prefix(_odd,kraj,SSTALE.AO,RVAT.ref)
      || DVAT.index('NR'); DVAT.prefix(kraj,SSTALE.AO,RVAT.ref)
      ?};
      DVAT.win_patt('SZUK_INN');
      DVAT.hdr_sel();
      DVAT.hdr_sel(' w rejestrze: %1 - %2 - Kraj: %3'@[RVAT.SYM,RVAT.NAZ,DVAT.KRAJ().TR]);
      {? {? _byl_rvt || DVAT.get() || DVAT.first() ?}
      || _a:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
         {? _a<>DOK.name()+4
         || POMOC.USE:=_a;
            DOK.use('doku'+_a);
            POZ.use('pozy'+_a);
            VPOZ.use('pozv'+_a);
            POW.use('pow_'+_a);
            AN.use('koan__'+ROK_F.KOD);
            AN_SLU.use('koansl'+ROK_F.KOD)
         ?};
         DVAT.DOK(); POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first;
         PVAT.index('NR'); PVAT.prefix(DVAT.ref); PVAT.first
      || POZ.index('DOK'); POZ.prefix(null); POZ.first;
         PVAT.index('NR'); PVAT.prefix(null); PVAT.first
      ?};
      _k:=RVAT.KVAT().SYM;
      {? _k='SprzEksp'
      || DVAT.win_edit('RED_E')
      |? 1+_k='S'
      || DVAT.win_edit('RED_S')
      |? 1+_k='Z' | 4+_k='ImpU'
      || DVAT.win_edit('RED_Z')
      |? 4+_k='ImpT'
      || DVAT.win_edit('RED_I')
      |? 1+_k='_'
      || DVAT.win_edit('RED_S')
      || DVAT.win_edit('RED_S')
      ?};
      DVAT.win_patt('SZUK_INN');
      DVAT.hdr_sel();
      DVAT.hdr_sel(' w rejestrze: %1 - %2 - Kraj: %3'@[RVAT.SYM,RVAT.NAZ,DVAT.KRAJ().TR]);
      grp_disp(DVAT,'WER');
      grp_disp(PVAT,'WER_WORK',1,1)
   || FUN.info('Brak rejestru VAT %1 .'@[TREE_VAT.RVAT])
   ?}
|? TREE_VAT.POZ=1 & TREE_VAT.ODD_REF<>''
|| _byl_odd:=($ODD.ref=TREE_VAT.ODD_REF);
   {? _byl_odd | ODD.seek(BB.sqlint(TREE_VAT.ODD_REF),form(8+TREE_VAT.ODD_REF))
   || OPERATOR.DEPT:=ODD.ref;
      DVAT.index('NR_O');
      DVAT.prefix(OPERATOR.DEPT,kraj,SSTALE.AO);
      {? DVAT.get()
      || _a:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
         {? _a<>DOK.name()+4
         || POMOC.USE:=_a;
            DOK.use('doku'+_a);
            POZ.use('pozy'+_a);
            VPOZ.use('pozv'+_a);
            POW.use('pow_'+_a);
            AN.use('koan__'+ROK_F.KOD);
            AN_SLU.use('koansl'+ROK_F.KOD)
         ?};
         DVAT.DOK(); POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first;
         PVAT.index('NR'); PVAT.prefix(DVAT.ref); PVAT.first
      || POZ.index('DOK'); POZ.prefix(null); POZ.first;
         PVAT.index('NR'); PVAT.prefix(null); PVAT.first
      ?};
      grp_disp(DVAT,'WER');
      grp_disp(PVAT,'WER_WORK',1,1)
   || FUN.info('Brak oddziału %1.'@[TREE_VAT.RVAT])
   ?}
|? TREE_VAT.POZ=0
|| DVAT.index('NR'); DVAT.prefix(kraj,SSTALE.AO);
   {? DVAT.get()
   || _a:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
      {? _a<>DOK.name()+4
      || POMOC.USE:=_a;
         DOK.use('doku'+_a);
         POZ.use('pozy'+_a);
         VPOZ.use('pozv'+_a);
         POW.use('pow_'+_a);
         AN.use('koan__'+ROK_F.KOD);
         AN_SLU.use('koansl'+ROK_F.KOD)
      ?};
      DVAT.DOK(); POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first;
      PVAT.index('NR'); PVAT.prefix(DVAT.ref); PVAT.first
   || POZ.index('DOK'); POZ.prefix(null); POZ.first();
      PVAT.index('NR'); PVAT.prefix(null); PVAT.first()
   ?};
   grp_disp(DVAT,'WER');
   grp_disp(PVAT,'WER_WORK',1,1)
?}


\arw_tree_vat2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_09]
:: OPIS: Po odświeżeniu okna z rejestrami VAT
::----------------------------------------------------------------------------------------------------------------------
__wgkraj:=1;
{? TREE_VAT.POZ=2 & TREE_VAT.RVAT_REF<>''
|| _byl_rvt:=($RVAT.ref=TREE_VAT.RVAT_REF);
   {? _byl_rvt | RVAT.seek(BB.sqlint(TREE_VAT.RVAT_REF),form(8+TREE_VAT.RVAT_REF))
   || {? TREE_VAT.ODD_REF<>''
      || {? $OPERATOR.DEPT<>TREE_VAT.ODD_REF
         || OPERATOR.DEPT:={? ODD.seek(BB.sqlint(TREE_VAT.ODD_REF),form(8+TREE_VAT.ODD_REF)) || ODD.ref || null ?}
         ?}
      || OPERATOR.DEPT:=null
      ?};
      {? (_odd:=OPERATOR.DEPT)
      || DVAT.index('NRW2_O'); DVAT.prefix(_odd,SSTALE.WAL,SSTALE.AO().ROK,RVAT.ref)
      || DVAT.index('NRWK'); DVAT.prefix(SSTALE.WAL,SSTALE.AO().ROK,RVAT.ref())
      ?};
      DVAT.win_patt('SZUK_INN');
      DVAT.hdr_sel();
      DVAT.hdr_sel(' w rejestrze: %1 - %2 - Kraj: %3'@[RVAT.SYM,RVAT.NAZ,DVAT.KRAJ().TR]);
      {? {? _byl_rvt || DVAT.get() || DVAT.first() ?}
      || _a:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
         {? _a<>DOK.name()+4
         || POMOC.USE:=_a;
            DOK.use('doku'+_a);
            POZ.use('pozy'+_a);
            VPOZ.use('pozv'+_a);
            POW.use('pow_'+_a);
            AN.use('koan__'+ROK_F.KOD);
            AN_SLU.use('koansl'+ROK_F.KOD)
         ?};
         DVAT.DOK(); POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first;
         PVAT.index('NR'); PVAT.prefix(DVAT.ref); PVAT.first
      || POZ.index('DOK'); POZ.prefix(null); POZ.first;
         PVAT.index('NR'); PVAT.prefix(null); PVAT.first
      ?};
      _k:=RVAT.KVAT().SYM;
      {? _k='SprzEksp'
      || DVAT.win_edit('RED_E')
      |? 1+_k='S'
      || DVAT.win_edit('RED_S')
      |? 1+_k='Z' | 4+_k='ImpU'
      || DVAT.win_edit('RED_Z')
      |? 4+_k='ImpT'
      || DVAT.win_edit('RED_I')
      |? 1+_k='_'
      || DVAT.win_edit('RED_S')
      || DVAT.win_edit('RED_S')
      ?};
      DVAT.win_patt('SZUK_INN');
      DVAT.hdr_sel();
      DVAT.hdr_sel(' w rejestrze: %1 - %2 - Kraj: %3'@[RVAT.SYM,RVAT.NAZ,DVAT.KRAJ().TR]);
      exec('upd_sum_ecom','!fks_vat_zdoz');
      grp_disp(DVAT,'WER_KRA2');
      grp_disp(PVAT,'WER_WORK',1,1)
   || FUN.info('Brak rejestru VAT %1 .'@[TREE_VAT.RVAT])
   ?}
|? TREE_VAT.POZ=1 & TREE_VAT.ODD_REF<>''
|| _byl_odd:=($ODD.ref=TREE_VAT.ODD_REF);
   {? _byl_odd | ODD.seek(BB.sqlint(TREE_VAT.ODD_REF),form(8+TREE_VAT.ODD_REF))
   || OPERATOR.DEPT:=ODD.ref;
      DVAT.index('NRW2_O'); DVAT.prefix(OPERATOR.DEPT,SSTALE.WAL,SSTALE.AO().ROK);
      {? DVAT.get()
      || _a:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
         {? _a<>DOK.name()+4
         || POMOC.USE:=_a;
            DOK.use('doku'+_a);
            POZ.use('pozy'+_a);
            VPOZ.use('pozv'+_a);
            POW.use('pow_'+_a);
            AN.use('koan__'+ROK_F.KOD);
            AN_SLU.use('koansl'+ROK_F.KOD)
         ?};
         DVAT.DOK(); POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first;
         PVAT.index('NR'); PVAT.prefix(DVAT.ref); PVAT.first
      || POZ.index('DOK'); POZ.prefix(null); POZ.first;
         PVAT.index('NR'); PVAT.prefix(null); PVAT.first
      ?};
      exec('upd_sum_ecom','!fks_vat_zdoz');
      grp_disp(DVAT,'WER_KRA2');
      grp_disp(PVAT,'WER_WORK',1,1)
   || FUN.info('Brak oddziału %1.'@[TREE_VAT.RVAT])
   ?}
|? TREE_VAT.POZ=0
|| DVAT.index('NRWK'); DVAT.prefix(SSTALE.WAL,SSTALE.AO().ROK);
   {? DVAT.get()
   || _a:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
      {? _a<>DOK.name()+4
      || POMOC.USE:=_a;
         DOK.use('doku'+_a);
         POZ.use('pozy'+_a);
         VPOZ.use('pozv'+_a);
         POW.use('pow_'+_a);
         AN.use('koan__'+ROK_F.KOD);
         AN_SLU.use('koansl'+ROK_F.KOD)
      ?};
      DVAT.DOK(); POZ.index('DOK'); POZ.prefix(DOK.ref); POZ.first;
      PVAT.index('NR'); PVAT.prefix(DVAT.ref); PVAT.first
   || POZ.index('DOK'); POZ.prefix(null); POZ.first();
      PVAT.index('NR'); PVAT.prefix(null); PVAT.first()
   ?};
   exec('upd_sum_ecom','!fks_vat_zdoz');
   grp_disp(DVAT,'WER_KRA2');
   grp_disp(PVAT,'WER_WORK',1,1)
?}


\upd_sum_ecom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MP] [21.14_09]
:: OPIS: aktualizuje dla zmiennej POMOC pole NETTO, VAT, BRUTTO oraz PLAT
::----------------------------------------------------------------------------------------------------------------------
PVAT.cntx_psh();
POMOC.NETTO:=POMOC.BRUTTO:=POMOC.VAT:=POMOC.PLAT:=0;
{? PVAT.first()
|| {!
   |? POMOC.NETTO+=PVAT.SUM2;
      POMOC.BRUTTO+=PVAT.SUM1;
      POMOC.VAT+=PVAT.VAT;
::      POMOC.PLAT:=
      PVAT.next()
   !}
?};
OP.cntx_psh();
OP.index('SYM2');
OP.prefix(DOK.NK,DOK.NK);
{? OP.first()
|| {!
   |? {? OP.WN=0
      || POMOC.PLAT+=OP.MA
      || POMOC.PLAT+=OP.WN
      ?};
      OP.next()
   !}
?};
OP.cntx_pop();
PVAT.cntx_pop()


\vatpozod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PB] [8.70]
:: OPIS: formula po odswiezeniu okienka wertowania tabeli PVAT w oknach grupowych
::       tabeli DVAT
::  OLD: \vatpozod/vat.fml
::----------------------------------------------------------------------------------------------------------------------
PVAT.index('NR');
{? DVAT.get || PVAT.prefix(DVAT.ref) || PVAT.prefix(0) ?};
grp_disp(PVAT,'WER_WORK');
1


\br_dvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed okien DVAT
::  OLD: \vat_ds/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,1)=DOK | cur_tab(1,1)=DOKN
|| {? DOK.OKRVAT<>SSTALE.AO || 'DVAT#01#01' || '' ?}
|| _rn:=ref_name(DVAT.DOK);
   DOK.use(_rn); DVAT.DOK();
   _kn:=DOK.DOK_REJ().KOR_NAG='N' & (3+DOK.ZAR='FKS' & DOK.KN<>'' | 3+DOK.ZAR='LSP' & DOK.LKN<>'');
   _rn:=4-_rn;
   POZ.use('pozy'+_rn);
   VPOZ.use('pozv'+_rn);
   SKIDXD.use('skxd'+_rn);
   POW.use('pow_'+_rn);
   _rn:=2+_rn;
   AN.use('koan__'+_rn);
   AN_SLU.use('koansl'+_rn);
   _hid_d:='';
 {? PAR_SKID.get(82)='N'
 ||     DOKUM.index('DOKUM');
         DOKUM.prefix(REF.FIRMA,$DVAT.DOK().ref);
         {? DOKUM.first()
         ||  ''
         || _hid_d+='Z'
         ?}
   ?};'załącznik';
  {? DVAT.DOK().DOKZRODL='' || _hid_d+='D' ?};'dokument źródłowy';
   {? ~DVAT.DOK().E_DOC || _hid_d+='E' ?};'skan ';
   {? _hid_d<>'' || _hid:='D('+_hid_d+')' || _hid:='' ?};
   DVAT.actions_grayed({? __wgkraj || 'WER_KRA2' || 'WER' ?},_hid);
   DVAT.DOKOKRO().ROK();

   {? DVAT.DOKOKRO<>SSTALE.AO  || Color.fnd_kol('DVAT#01#01') || '' ?}
?}


\dvat_zapisy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zapisy dokumentu
::----------------------------------------------------------------------------------------------------------------------
POZ.index('DOK');
DVAT.DOK();
{? DVAT.DOK
|| POZ.prefix(DVAT.DOK)
|| POZ.prefix(0); DOK.blank(1)
?};
exec('suma_dok','dok_fks');
exec('ok_vsel','dok_fks',1);
{? exec('dokaut','dok_fks')
|| POZ.actions('VSEL','ATDvpU:AD','O:Z',1)
|| POZ.actions('VSEL','ATDvpUF:ADF','O:Z',1)
?};
{? PAR_SKID.get(36)='T' & PAR_SKID.get(35)='T'
|| POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?}+'2')
|| POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?})
?};
POZ.hdr_sel();
POZ.hdr_sel('Pozycje dokumentu %1'@[DOK.DOK_REJ().NAZ]);
PozView:=1;
POZ.select();
VAR_DEL.delete('PozView')


\dvat_set_kraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia kraj opodatkowania
::----------------------------------------------------------------------------------------------------------------------
SLU.index('NAZ'); SLU.prefix('~KRAJE UE');
{? SLU.first()
|| SLO.index('SL'); SLO.prefix(SLU.ref());
   SLO.win_sel('ONE_SEL');
   SLO.seek(kraj);
   {? SLO.select(,1)
   || kraj:=SLO.ref();
      exec('arw_tree_vat','!fks_vat_zdoz');
      exec('title','fks_vat')
   ?}
?}


\dokzrddv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula przed na akcje Infor. dla dok. aut. okien wetowania tabeli DVAT
::  OLD: \dokzrddv/dok_zrd.fml
::----------------------------------------------------------------------------------------------------------------------
DVAT.DOK();
exec('dok_zrd','dok_fks')


\sz_pvat1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS:
::  OLD: \sz_pvat1/dok_zrd2.fml
::----------------------------------------------------------------------------------------------------------------------
PVAT.win_patt('RED_ZS')


\br_pvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Rekord przed PVAT
::  OLD: \br_pvat/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? PVAT.TYP_VAT='Z'
|| Color.fnd_kol('PVAT#01#Z')

|? PVAT.TYP_VAT='C'
|| Color.fnd_kol('PVAT#01#C')

|| ''
?}


\add_tab_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodanie zakładki z pozycjami VAT do okna grupowego obszaru evidencji VAT (FKS_VAT)
::   WE: _a - nazwa zakładki
::       _b - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
_be:="VPOZ.cntx_psh(); DOK.cntx_psh();
      _maska:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2);
      {? VPOZ.use('pozv'+_maska) & DOK.use('doku'+_maska)
      || {? var_pres('vpozpar')>0 || VPOZ.f_clear() ?};
         {? OPERATOR.DEPT=null
         || {? var_pres('vpozndx')<=0 || VPOZ.index('KS_PR') || VPOZ.index(vpozndx) ?}; VPOZ.prefix('T','N');
            {? VPOZ.first() || 1 ?}
         || {? var_pres('vpozndx')<=0 || VPOZ.index('KS_PR') || VPOZ.index(vpozndx) ?};
            VPOZ.prefix('T','N',OPERATOR.DEPT().OD);
            {? VPOZ.first() || 1 ?}
         ?};
         {? var_pres('vpozrefaf')>0 & VPOZ.f_active()=0 & var_pres('vpozpar')<=0
         || {? VPOZ.seek(#vpozrefaf,'pozv'+_maska) || 1 ?}
         |? var_pres('vpozrefaf')>0 & VPOZ.f_active()<>0 & var_pres('vpozpar')<=0
         || {? VPOZ.f_seek(#vpozrefaf,'pozv'+_maska) || 1 ?}
         ?}
      ?}";
_af:="VAR_DEL.delete('vpozpar','vpozrefaf','vpozndx'); vpozndx:=VPOZ.index('?'); vpozrefaf:=VPOZ.ref();
      VPOZ.cntx_pop(); DOK.cntx_pop()";
TREE_VAT.grp_sel(_b,VPOZ,'WER_T',_a,,,,,_be,_af,,,,,);
task_attach('FKS_VAT_ZDOZ');
task_attach('FKS_VAT_DTRO');
task_attach('FKS_VAT_DDOK');
task_attach('FKS_VAT_DDNR');
task_attach('FKS_VAT_ZREJ');
task_attach('FKS_VAT_ZREV');
task_attach('FKS_VAT_ZSQL')


\add_tab_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Dodanie zakładki z Ewidencja prowadzona przez członków grupy VAT do okna grupowego obszaru evidencji VAT (FKS_VAT)
::   WE: _a - nazwa zakładki
::       _b - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
_be:="_maska:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2);
      _zm:=DOK.name<>'doku'+_maska;
      {? DOK.use('doku'+_maska)
      || {? _zm || DOK.f_clear() ?};
         {? OPERATOR.DEPT=null
         || {? _zm=0 & var_pres('dokndx')>0
            || DOK.index(dokndx)
            || DOK.index('GV')
            ?};
            DOK.prefix('GL','T');
            {? var_pres('dokref')>0
            || {? DOK.seek(#dokref,'doku'+_maska) || DOK.DOK_REJ(); 1 ?}
            || {? DOK.first() || DOK.DOK_REJ(); 1 ?}
            ?}
         || {? _zm=0 & var_pres('dokndx')>0
            || DOK.index(dokndx)
            || DOK.index('GV_O')
            ?};
            DOK.prefix(OPERATOR.DEPT,'GL','T');
            {? var_pres('dokref')>0
            || {? DOK.seek(#dokref,'doku'+_maska) || DOK.DOK_REJ(); 1 ?}
            || {? DOK.first() || DOK.DOK_REJ(); 1 ?}
            ?}
         ?};
         exec('czytaj','#stalesys',DOK.DTO,KST,'GRVAT')
      ?}";
_af:="VAR_DEL.delete('dokref','dokndx'); dokndx:=DOK.index('?'); dokref:=DOK.ref();exec('czytaj','#stalesys',,KST,'GRVAT')";
TREE_VAT.grp_sel(_b,DOK,'C_GRVAT',_a,,,,,_be,_af,,,,,);
task_attach('FKS_VAT_ZDOZ');
task_attach('FKS_VAT_DTRO');
task_attach('FKS_VAT_DDOK');
task_attach('FKS_VAT_DDNR');
task_attach('FKS_VAT_ZREJ');
task_attach('FKS_VAT_ZREV');
task_attach('FKS_VAT_ZSQL')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 e4f99632703e7fbc7650b2d0f74601567c53c168658d8de1dd2ec849d3e202912bc3170cd9f52b6282757ece8426ac47c60d31c0216aaf56b08001675821bf9a11bc96a58dcf8f3f1744b8d94217358549579e760dd518e57c6e768f04ef3c02f3dfdb5f43f8a7f12584b06f00e44ab8dec7dae9f10e70aa04015bb1e0b28c65

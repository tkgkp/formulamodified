:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_grp_gruc.fml
:: Utworzony: 26.07.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_GRP_GRUC - Ruchomy czas pracy - grupa.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Grupowe przypisanie pracowników do ruchomego czasu pracy obowiązującego w firmie.
::   WE:
::   WY:
::  OLD: \rcp_sel_group/okres.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

A_RWN.cntx_psh();
P.cntx_psh();
P.clear();
{? P.f_active() || P.f_clear() ?};
A_RWN.actions('WERF','DPU:D');
A_RWN.win_sel('WERG');
A_RWN.index('WN_FIRMA');
A_RWN.prefix('T');
A_RWN.select();
A_RWN.actions('WERF','');
P.cntx_pop();
{? P.f_active() || P.f_clear() ?};
A_RWN.cntx_pop();
~~


\wspolpracownicy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Formuła dla dołącz w grupowym przypisaniu współpracowników do ruchomego czasu pracy obowiązującego w firmie.
::   WE: _a - grupa do wyświetlenia listy współpracowników
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('filtruj_p','schemat',
   'PRC',
   UD_DEF.ref(),
   P_FILTER.F_ZATR().KOD,'T',
   P_FILTER.STATUS,
   ,
::  uwzględnij współpracowników powiązanych z wybraną grupą ruchomego czasu pracy
   'P.REFERENCE in (select A_RWN.P from A_RWN where A_RWN.GROUP=\''+_a+'\' order by 1)'
)


\a_rwn_gw_afd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Formuła dla dołącz w grupowym przypisaniu współpracowników do ruchomego czasu pracy obowiązującego w firmie.
::   WE:
::   WY:
::  OLD: \rcp_dod_prac/okres.fml
::  OLD: \rcp_group/okres.fml
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PRC';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_args.F_ZATR:={? P_FILTER.F_ZATR().KOD='' || '*T' || P_FILTER.F_ZATR().KOD ?};
_args.VIEW:=P_FILTER.STATUS;
_args.SQL_WHERE:=
      'P.REFERENCE not in (select A_RWN.P from A_RWN where A_RWN.GROUP=\'%1\' order by 1)'
      [A_RWN.OPIS];

P.cntx_psh();
P.clear();
{? P.f_active() || P.f_clear() ?};
_PRAC:=exec('wybierz','pracownik',_args).P;
P.cntx_pop();

A_RWN.cntx_psh();
{? _PRAC.first()
|| _od:=A_RWN.OD;
   _do:=A_RWN.DO;
   _a_rwn:=A_RWN.ref();
   _grupa:=A_RWN.OPIS;
   A_RWN.clear();
   {!
   |? {? P.seek(_PRAC.SQL,,1)
      || A_RWN.blank();
         A_RWN.WP:='N';
         A_RWN.P:=P.ref();
         A_RWN.OPIS:=P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' IP:'+$P.IP+' ('+P.F_ZATR().OPIS+')';
         A_RWN.OD:=_od;
         A_RWN.DO:=_do;
         A_RWN.A_RWN:=_a_rwn;
         A_RWN.GROUP:=_grupa;
         A_RWN.add(1)
      ?};
      _PRAC.next()
   !}
|| FUN.info('Nie wybrano żadnego pracownika.'@)
?};
A_RWN.cntx_pop();

exec('wspolpracownicy','!prc_grp_gruc',A_RWN.GROUP);
~~


\a_rwn_gw_afu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Formuła dla usuń w grupowym przypisaniu współpracowników do ruchomego czasu pracy obowiązującego w firmie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy napewno usunąć ruchomy czas pracy współpracownika: %1 %2?'@[P.OSOBA().NAZWISKO,OSOBA.PIERWSZE])
|| _error:=0;
   _a_rwn:=#A_RWN.ref();
   A_RWN.cntx_psh();
   A_RWN.index('GROUP');
   A_RWN.prefix(_a_rwn,P.ref());
   {? A_RWN.first()
   || A_RWP.cntx_psh();
      A_RWP.index('A_RWP');
      A_RWP.prefix(A_RWN.ref());
      do();
      {? A_RWP.first()
      || {! |! A_RWP.del() !}
      ?};
      {? ~A_RWP.first() || exec('del_ndx','#table',ZALACZ,'NAG',A_RWN.uidref()) ?};
      {? ~A_RWP.first() & A_RWN.del(,1)
      || P.f_del()
      || undo();
         _error:=1
      ?};
      end();
      {? _error
      || FUN.info('Nie można usunąć współpracownika z ruchomego czasu pracy: \"%1\".'@[A_RWN.GROUP])
      ?};
      A_RWP.cntx_pop()
   ?};
   A_RWN.cntx_pop()
?};
~~


\a_rwn_werf_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Formuła przed obsługą listy współpracowników dla wybranej grupy ruchomego czasu pracy obowiązującego w firmie.
::       Okno WERF z tabeli A_RCP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('wspolpracownicy','!prc_grp_gruc',A_RWN.OPIS)


\a_rwn_werf_rf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Formuła do odświeżenia listy współpracowników dla wybranej grupy ruchomego czasu pracy obowiązującego w firmie.
::       Okno WERF z tabeli A_RCP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('wspolpracownicy','!prc_grp_gruc',A_RWN.OPIS);
grp_disp(P,'PRC_SRCP')


\a_rwn_prc_srcp_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Formuła przed obsługą okna PRC_SRCP z tableli P w oknie grupowym tabeli A_RWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(A_RWN,'WERFG')
|| '#disable'
?}


\a_rwn_prc_srcp_rf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Formuła do odświeżenia listy współpracowników dla wybranej grupy ruchomego czasu pracy obowiązującego w firmie.
::       Okno PRC_SRCP z tabeli P
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('wspolpracownicy','!prc_grp_gruc',A_RWN.OPIS);
win_disp()

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:58 43652ead93793c12d042786649eef0077c1110786a0539b6128ab6bae0bbf2234ef2109aa5919c091403ab5739472cc870f6667d2cbffedebdaa407068dad089064f8de6457c2bd256fcabf532ce2e8a42c8bca5185640182a3a65c0f8c7e9a75efb6082a0b4c40fec7c4b1fff2d7172a96692834b2198bcd8c02355bdc52d60

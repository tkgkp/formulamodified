:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_ofe_dofe.fml
:: Utworzony: 12.01.2015
:: Autor: mario
::======================================================================================================================
:: Zawartość: Formuły czynności LSP_OFE_DOFE
::            Rejestracja ofery
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Czynność LSP_OFE_DOFE - formuła główna
::   WE: _a - [obj_new] - parametry wejsciowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,TARS,TARD
::# kind=WY,   symbol=OFE,       type=_OFE,     name=Oferta,      required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_ofe','lsp');

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_autoakc:=exec('autoAkc','#b__box',_mp,300280,'LSP_OFE_AAOF');

exec('init_ofe','lsp');
{? _mp.pathProc()
:: Sprawdzenie parametrów pracy dla czynności startowej
|| {? ST.AR=0 | ST.ODDZ_KOD='' || FUN.info('Ustaw parametry pracy.'@); _wyn:=0; return() ?}
?};

{? _mp.pathTodo()
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('OFE',_out)=type_of(null()) & _out.OFE
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego OFE.')
   ?}
?};

:: Sprawdzenie uprawnień
params_set('in',_in,'user',OPERATOR.USER,'mp',_mp);
{? exec('permissions','!lsp_ofe_dofe')=0
|| _mp.error('Brak uprawnień do uruchomienia czynności.');
   return()
?};

:: Walutowość dla ofert
BEER.MW:='O';
:: Ustawia BEER.WW
exec('ustaw_ww','eurusd','O');

:: Wyzwalacz, który po dodaniu nagłówka oferty:
:: - add: dodaje rekord kluczowy nagłówka oferty
::   del: usuwa rekord kluczowy nagłówka oferty
_mp.trigRef('OFE',,1,,exec('kind_out','#b_port'),'OFE');

{? _akcja='Dołącz'
   | _mp.pathProc()
   | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
||
::   {? var_pres('TYPYZAM',_in)=type_of(null()) & _in.TYPYZAM<>null()
::   || BEER.TYPYZAM:=_in.TYPYZAM
   {? _mp.pathProc() | _akcja='START_TODO'
:: Wybór typu zamówienia
   || BEER.TYPYZAM:=null();
      _p3009:=exec('get','#params',3009,2,OPERATOR.USER);
      exec('typ_zamm','zamowienia',_p3009,'O')
   ?};
   {? BEER.TYPYZAM
   ||
:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      {? _mp.pathProc() | _akcja='START_TODO' || ZAKR.T:='A' ?};
      exec('ofe_win_edit_set','!lsp_ofe_dofe');
      exec('ofeaddpr','!lsp_ofe_dofe');

      _krefs:=_mp.getRefs();
      {? var_pres('[1]',_krefs)<>type_of(~~)
:: Dodano dokument
      || OFE.cntx_psh();
         OFE.prefix();
         {? OFE.seek(_krefs[1])
::    Zapisanie _out.OFE jeśli wcześniej nie zakończono rejestracji dokumentu
         || {? OFE.STAT_REJ='N'
            || _out.OFE:=OFE.ref();
               _mp.save(,_out)
            ?}
::    Brak dokumentu w tabeli mimo zapisanego parametru wyjściowego OFE oznacza wycofanie czynności
         || _mp.cancel()
         ?};
         OFE.cntx_pop()
:: Wycofanie czynności bo nie dodano dokumentu
      || _mp.cancel()
      ?};
:: Ustawienie się na dodanym dokumencie w widoku obszaru
      {? _mp.pathArea()
      || _outa:=_mp.load(exec('kind_out','#b_port'));
         {? var_pres('OFE',_outa)=type_of(null()) & _outa.OFE || OFE.seek(_outa.OFE) ?}
      ?}
   ?}
|? _akcja='Kopiuj'
   | _mp.pathProc()
||
   params_set('out',_out,'mp',_mp,'akcja',_akcja);
   exec('ofe_kop','!lsp_ofe_dofe');

   _krefs:=_mp.getRefs();
   {? var_pres('[1]',_krefs)<>type_of(~~)
:: Dodano dokument
   || OFE.cntx_psh();
      OFE.prefix();
      {? OFE.seek(_krefs[1])
::    Zapisanie _out.OFE jeśli wcześniej nie zakończono rejestracji dokumentu
      || {? OFE.STAT_REJ='N'
         || _out.OFE:=OFE.ref();
            _mp.save(,_out)
         |? OFE.STAT_REJ='Z'
         || _out.OFE:=OFE.ref();
            _mp.save(,_out);
            _mp.done()
         ?}
::    Brak dokumentu w tabeli mimo zapisanego parametru wyjściowego OFE oznacza wycofanie czynności
      || _mp.cancel()
      ?};
      OFE.cntx_pop()
:: Wycofanie czynności bo nie dodano dokumentu
   || _mp.cancel()
   ?};
:: Ustawienie się na dodanym dokumencie w widoku obszaru
   {? _mp.pathArea() || {? var_pres('OFE',_out)=type_of(null()) & _out.OFE || OFE.seek(_out.OFE) ?} ?}
|? _akcja='Popraw'
   | _mp.pathTodo()
|| VAR_DEL.delete('OFE_POPRAW');
   OFE_POPRAW:=1;
   {? var_pres('OFE',_out)=type_of(null())
:: Uruchomione w procesie
   || OFE.cntx_psh();
      OFE.prefix();
      {? OFE.seek(_out.OFE)
      || exec('ofe_win_edit_set','!lsp_ofe_dofe',OFE.STAT_REJ<>'N');
         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('ofe_edit','!lsp_ofe_dofe');

         _mp.descTodo()
      ?};
      OFE.cntx_pop()
   |? _mp.pathTodo()
:: Uruchomione zadanie z listy todo i brak _out.OFE wtedy zadanie wycofujemy
   || _mp.cancel()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || exec('ofe_win_edit_set','!lsp_ofe_dofe',OFE.STAT_REJ<>'N');
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      exec('ofe_edit','!lsp_ofe_dofe')

   ?};
   VAR_DEL.delete('OFE_POPRAW')
|? _akcja='Usuń'
|| {? var_pres('OFE',_out)=type_of(null())
:: Uruchomione w procesie
   || _ofe:=null();
      OFE.cntx_psh();
      _rec:=exec('rec_after_del','#table',OFE);
      OFE.prefix();
      {? OFE.seek(_out.OFE)
      ||
         _wyn:=exec('ofe_del','!lsp_ofe_dofe');

         {? ~OFE.seek(_out.OFE)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _ofe:=OFE.ref()
      ?};
      OFE.cntx_pop();
      {? _wyn=1 & _rec<>null() || OFE.seek(_rec) ?};
::    Ustawienie się na kolejnej ofercie w widoku obszaru
      {? _mp.pathArea() || {? _ofe || OFE.seek(_ofe) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || exec('ofe_del','!lsp_ofe_dofe')
   ?}
|? _akcja='Zakończ_wer' & _mp.pathArea()
|| exec('oferta_zakoncz','!lsp_ofe_dofe',1,_autoakc)
|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('OFE',_out)<>type_of(~~) & _out.OFE
|| 'Zakończ rejestrację oferty: %1'@@[exec('record','#to_string',_out.OFE)]
|| 'Zarejestruj ofertę'@@
?}


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;
_wyn:=1;
_wyn


\oferta_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dołączenie oferty
::  [rr] ofeaddprOFE
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_OFE_DOFE';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';
_typyzam:=BEER.TYPYZAM;
_ret:=0;

{? BEER.TYPYZAM=null()
||
   TYPYZAM.cntx_psh();
   TYPYZAM.prefix();
   PROJTYPY.cntx_psh();
   _sql:='select TYPYZAM.T, TYPYZAM.NAZ, TYPYZAM.REFERENCE as REF
   from TYPYZAM
   where (TYPYZAM.PROJZAKR=\'Wszystkie\' or TYPYZAM.PROJZAKR=\'%1\')
   and TYPYZAM.R=\'O\''
   [{? PROJEKTY.T<>null & PROJEKTY.T().R='Wewnętrzny' || 'Wewnętrzne' || 'Zewnętrzne' ?}];
   PROJTYPY.cntx_pop();

   _tab:=sql(_sql);
   _win:=_tab.mk_sel('Typy ofert'@,,0,,,,,,'U');
   _tab.win_act(_win,,'Formuła','Wybierz'@@,,,"_tab:=cur_tab();TYPYZAM.seek(_tab.REF);sel_exit()",,1);
   _tab.win_fld(_win,,'T',,,20,,,'Typ'@);
   _tab.win_fld(_win,,'NAZ',,,40,,,'Nazwa'@);
   _tab.win_sel(_win);
   {? _tab.select()
   || BEER.TYPYZAM:=TYPYZAM.ref()
   || _ret:=1
   ?};
   TYPYZAM.cntx_pop()
?};

{? ~_ret
|| DISP.ZAM_OFE:='';
   exec('mp_run','#b__box',_params);
   exec('ofe_f_rfresh','lsp')
?};
BEER.TYPYZAM:=_typyzam


\oferta_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Modyfikacja oferty
::  [rr] ofe_edit
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_OFE_DOFE';
_params.UIDREF:=OFE.uidref();
_params.AKCJA:='Popraw';

DISP.ZAM_OFE:='';
exec('mp_run','#b__box',_params)


\oferta_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Usunięcie oferty
::  [rr] ofe_del
::----------------------------------------------------------------------------------------------------------------------
{? OFE.STAT_REJ<>'T'
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LSP_OFE_DOFE';
   _params.UIDREF:=OFE.uidref();
   _params.AKCJA:='Usuń';

   DISP.ZAM_OFE:='';
   exec('mp_run','#b__box',_params)
|| FUN.info('Oferta zaakceptowana.\nUsunięcie niemożliwe.'@)
?}


\oferta_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Kopiowanie oferty
::  [rr] ofe_kop
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_OFE_DOFE';
_params.AKCJA:='Kopiuj';
_params.PROC_START:='T';

DISP.ZAM_OFE:='';
exec('mp_run','#b__box',_params);

exec('ofe_f_rfresh','lsp')


\bl_cbofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: wartość początkowa dla pola OFE.CB
::  OLD: \bl_cbofe/defin.fml
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',300151,2)


\ofeaddpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja dołącz ofertę
::  OLD: \ofeaddpr/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_prod:=exec('tte_lic','tte');
{? ZAKR.T='A'
||
   _bie:=#OFE.ref();
   _new:=null;

   POM.TAB:='OFE';
   POM.TYPDOK:=BEER.TYPYZAM().KOD;

   OFE.cntx_psh();
   OFE.prefix();
   OFE.blank();
   {? OFE.add() & (_blok:=OFE.r_lock(1,1,1))>0
   ||
      TAZ.RAB_N_BE:=0;
      HELP.PL:=null();
      HELP.PROJEKTY:=null();
      HELP.KH_ODB:=null();
      OFE.memo_set('','OPIS');
      params_set(params_get());
      _del:=0;
      {? type_of(cur_tab)<>0 & 5+cur_tab.name()='pj_np'
      || OFE.PROJEKTY:=PROJEKTY.ref();
         OFE.KH:=PROJEKTY.KH;
         OFE.ODB:=PROJEKTY.KH_ODB;
         exec('kh_dod_ini','kontrahent',PROJEKTY.KH);
         OFE.HAN:=KH_DOD.HAN
      ?};
      exec('set_efld_opt','!lsp_ofe_dofe');
      {? OFE.edit("params_exec('oferta_valid','!lsp_ofe_dofe',1)")
      || _bez_poz:=exec('FindInSet','#table','OFP','OFE',OFE.ref())=null();
         exec('oferta_after','!lsp_ofe_dofe',{? _bez_poz || -1 || 0 ?});
         {? _bez_poz & OFE.STAT_REJ='N'
         || BPMN.END:=1;
            params_exec('ofe_poz','lsp',1);
            {? BPMN.END=2
            || exec('oferta_zakoncz','!lsp_ofe_dofe')
            ?}
         ?}
      |? exec('FindInSet','#table','OFP','OFE',OFE.ref())<>null()
      || {? FUN.ask('Oferta ma pozycje.\nCzy na pewno usunąć ofertę?'@) || _del:=1 || OFE.r_unlock() ?}
      || _del:=1
      ?};
      {? _del
      ||
::       usuwanie numeru
         exec('ofe_del','!lsp_ofe_dofe',1);
         {? do_state()<2 & ~Plugin.run('BEFORE_DELTAB_001',OFE.ref()) & do_state() || undo() ?};
         OFE.r_unlock();
         numer:=OFE.NR;
         oldnumer:=1;
         exec('nr_old','numery');
         OFE.del(1)
      || _new:=OFE.ref();
         OFE.r_unlock()
      ?}
   || FUN.info('Nieudana próba zablokowania tabeli nagłówka ofert.\nSpróbuj ponownie.'@)
   ?};
   OFE.cntx_pop();

   {? _new=null
   ||
      {? _bie>0 || OFE.seek(_bie,) ?}
   ||
      {? OFE.seek(_new)=0
      ||
         FUN.info('Aktualnie zredagowana oferta nie jest dostępna w ustawionym zakresie widoku ofert.'@);
         {? _bie>0 || OFE.seek(_bie,) ?}
      ?}
   ?}
|| FUN.info('Zmień zakres na Aktywne.'@)
?};
''


\ofe_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: usuwanie oferty
::   WE: [_a]- 0-(domyslnie) usuwanie oferty 1-usuwanie pozycji powiązanych
::   WY: (0/1) - czy usunięto
::  OLD: \ofe_del/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
_zam:=exec('czy_zam','zamowienia');
_wyn:=0;

{? {? ~_a
   || {? _zam=1
      || FUN.info('Z oferty zostało wygenerowane zamówienie %1.\nUsunięcie niemożliwe.'@[ZK_N.SYM]);
         0
      || {? OFE.r_lock(1,1,1)
         || {? FUN.ask('Usunąć bieżący wiersz?'@)
            || 1
            || OFE.r_unlock();
               0
             ?}
         || {? FUN.ask('Dokument obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
            || OFE.r_unlock()
            ?};
            0
         ?}
      ?}
   || 1
   ?}
||
   do();

::  pozycje
   OFP.index('OFE_P');
   OFP.prefix(OFE.ref());
   {? OFP.first()
   || {! |? OFP.del() !}
   ?};

::  koszty
   OFK.index('OFE');
   OFK.prefix(OFE.ref());
   {? OFK.first()
   || {! |? OFK.del() !}
   ?};

:: zalaczniki
   DOKUM.index('DOKUM');
   DOKUM.prefix(REF.FIRMA,$OFE.ref);
   {? DOKUM.first
   || {!
      |? exec('dokumz_del','dokum_zal');
         _uid:=DOKUM.uidref(); _rr:=DOKUM.del(); exec('dokumlog_del','zbl_dok',_uid);
         _rr
      !}
   ?};
:: TODO: koniec

   {? ~_a
   || {? OFE.count()=0
      || {? do_state()<2 & ~Plugin.run('BEFORE_DELTAB_001',OFE.ref()) & do_state() || undo() ?};
         POM.TAB:='OFE';
         POM.TYPDOK:=OFE.T().KOD;
         numer:=OFE.NR;
         oldnumer:=1;
         exec('nr_old','numery');
         _wyn:=OFE.del()
      || undo()
      ?}
   ?};
   end();
   {? ~_a || OFE.r_unlock() ?}
?};
_wyn


\ofe_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: popraw dla oferty
::  OLD: \ofe_edit/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_zam:=exec('czy_zam','zamowienia');

{? _zam=1 & OFE.STAT_REJ<>'T'
||
   FUN.info('Z oferty zostało wygenerowane zamówienie %1. \nModyfikacja niemożliwa.'@[ZK_N.SYM])
||
   _bie:=#OFE.ref();

   OFE.cntx_psh();
   OFE.prefix();
   {? OFE.r_lock(1,1,1)
   ||
      TAZ.RAB_N_BE:=OFE.RAB;
      HELP.PL:=OFE.PL;
      HELP.PROJEKTY:=OFE.PROJEKTY;
      HELP.KH_ODB:=OFE.ODB;
      params_set(params_get());
      exec('set_efld_opt','!lsp_ofe_dofe');
      OFE.memo_get(,'OPIS');
      {? OFE.STAT_REJ='T'
      || _ofpr0:=OFE.OFPR;
         _ofpp0:=OFE.OFPP;
         {? OFE.edit() & (OFE.OFPR<>_ofpr0 | OFE.OFPP<>_ofpp0)
         || OFE.put(1)
         ?}
      |? _wyn:=OFE.edit("params_exec('oferta_valid','!lsp_ofe_dofe',1)")
      ||
::       aktualizacja cen
         exec('akt_cen','!lsp_ofe_dofe')
      ?};
      OFE.r_unlock()
   || {? FUN.ask('Dokument obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
      || OFE.r_unlock()
      ?}
   ?};
   OFE.cntx_pop();

   {? OFE.seek(_bie)=0
   ||
      FUN.info('Aktualnie zredagowana oferta nie jest dostępna w ustawionym zakresie widoku ofert.'@)
   ?}
?};
_wyn


\be_ofekh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: przed redakcją OFE.KH
::  OLD: \be_ofekh/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
ZK_Z.KH:=OFE.KH; 1


\be_ofodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: przed redakcją odbiorcy na ofercie
::   WY: 1/0
::  OLD: \be_ofodb/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? OFE.KH=null
|| _wyn:=0
?};
_wyn


\pr_cbofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: przed redakcją OFE.CB
::  OLD: \pr_cbofe/defin.fml
::----------------------------------------------------------------------------------------------------------------------
(1+menu_txt())='D' | exec('FindInSet','#table','OFP','OFE',OFE.ref())=null


\oferta_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdzanie poprawności wypełnienia oferty
::   WE: _a - miejsce wywołania [0] - akcja zakończ, 1 - dołącz/popraw (okno redagowania)
::  OLD: \chk_ofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(OFE,,'SYM','KH','DU','TW');
_edit:={? var_press('_a')=type_of(0) || _a || 0 ?};
{? _wyn='' & OFE.RAB<0
|| FUN.info('Rabat nie może być ujemny.'@);
   _wyn:='RAB'
|? _wyn='' & OFE.RAB>100
|| FUN.info('Nie można podać %% rabatu większego niż 100%%.'@);
   _wyn:='RAB'
?};
{? _wyn='' & OFE.TW<OFE.DU
|| FUN.info('Termin ważności nie może być wcześniejszy od daty utworzenia.'@);
   _wyn:='TW'
?};
{? _wyn='' & OFE.RAB<>TAZ.RAB_N_BE
|| _wyn:=params_exec('chk_rab_n','ceny',OFE)
?};
{? _wyn='' & OFE.WAL<>INFO.NAROD
|| {? OFE.KRS>0 & OFE.DTK=date(0,0,0)
   || FUN.info('Należy podać datę kursu.'@);
      _wyn:='DTK'
   ?}
?};
{? _edit & _wyn='' & (OFE.PROJEKTY<>HELP.PROJEKTY | OFE.PROJEKTY & (OFE.ODB<>HELP.KH_ODB | OFE.PL<>HELP.PL))
|| _wyn:=exec('tabdok_chk','projekty',OFE)
?};
_wyn


\ofp_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: dołącz pozycje oferty
::  OLD: \ofp_dol/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
ATR.MJS:='OFP';
{! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !};
{? exec('czy_zam','zamowienia')=0
|| OFP.blank();
   {? POMOC.RODZO='' || POMOC.RODZO:='T' ?};
   OFP.win_edit('RED');
   {? OFP.edit("exec('chk_ofp','!lsp_ofe_dofe')")
   || _wyn:=OFP.add();
      {? _wyn & OFP.f_active() || OFP.f_rfresh() ?}
   ?}
?};
_wyn


\bl_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: wartość na blanku
::  OLD: \bl_ofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
OFE.ref()


\bl_p_ofp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [2008]
:: OPIS: blank na numerze pozycji w pozycjach oferty
::  OLD: \bl_p_ofp/oferty.fml
::----------------------------------------------------------------------------------------------------------------------

OFP.cntx_psh();
OFP.index('OFE_P');
OFP.prefix(OFE.ref());
_wyn:={? OFP.last() || OFP.POZ || 0 ?}+1;
OFP.cntx_pop();
{? D_HELP.MOFE<>''
|| D_HELP.MR:=OFP.M:=exec('FindInSet','#table','M','MATKTM',D_HELP.MOFE,D_HELP.MOFE)
|| D_HELP.MR:=OFP.M:=null
?};
_wyn


\bl_m_ofp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: Wartość początkowa dla pola OFP.M
::  OLD: \bl_m_ofp/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFP.size()=0; 1
||
   D_HELP.MOFE:='';
   null
||
   _poz:=OFP.POZ;
   {? OFP.get() || _pop:=OFP.M().ref() || _pop:=null ?};
   OFP.POZ:=_poz;
   {? D_HELP.MOFE<>''
   || D_HELP.MR:=_pop:=exec('FindInSet','#table','M','MATKTM',D_HELP.MOFE,D_HELP.MOFE)
   || D_HELP.MR:=_pop:=null
   ?};
   _pop
?}


\bl_walo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: waluta na blanku
::  OLD: \bl_walo/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
OFE.WAL


\bl_krso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: ustawienie kursu na pozycjach oferty
::  OLD: \bl_krso/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
OFE.KRS


\chk_ofp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord po dla pozycji oferty tab. OFP
::  OLD: \chk_ofp/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:={? __CHK.record(D_HELP,,'MOFE')<>'' || {? POMOC.RODZO='' || 'RODZ' || 'MOFE' ?} || ''  ?};
{? _wyn='' || _wyn:=__CHK.record(OFP,,'IL') ?};
{? _wyn='' & OFP.IL<=0 || FUN.info('Ilość musi być większa od zera.'@);_wyn:='IL' ?};
{? _wyn='' & (OFP.C<0 | OFP.CWAL<0)
|| FUN.info('Cena nie może być ujemna.'@);
   _wyn:={? OFP.OFE().WAL<>INFO.NAROD || 'CWAL' || 'C' ?}
?};
{? _wyn='' & (OFP.C | OFP.CWAL | DISP.RABP) & exec('czy_cena_z_tap','ceny','OFP').WYN
|| _wyn:={? OFP.OFE().WAL<>INFO.NAROD || 'CWAL' || 'C' ?}
?};
{? _wyn='' & OFP.SV=null || FUN.info('Stawka VAT musi być wypełniona.'@);_wyn:='SV' ?};
{? _wyn='' & ~exec('spr_rab','ceny',DISP.RABP,0)
|| win_disp;
   _wyn:=exec('rab_2130','ceny',OFP,'RABP')
?};
{? _wyn='' & ATR.FLAG_ED & ATR.FLAG<>1
|| exec('akcepatr','mat_atr',0,1)
?};
:: sprawdzenie wdrożeniowe
{? _wyn='' & Plugin.runnable('VAL_POZLOG_001')
|| _wyn:=Plugin.run('VAL_POZLOG_001','OFP')
?};
_wyn


\ae_ofekh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji kontrahenta w ofercie
::   WY: czy opuścić redakcję pola
::  OLD: \ae_ofekh/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()=null
|| exec('find_sql','#table','KH','SNIP;KOD;NAZ;SKR;UL;DOM;LOKAL','','OFE.KH','13;5;25;10;15','RED','','')
?};
{? fld()=OFE.PROJEKTY
|| _fld:=OFE.KH
|| _fld:=fld()
?};
:: TODO: sprawdzanie kontrahenta do dokończenia
::_wyn:=exec('ae_kh','firma');
_wyn:=exec('ae_kh','kontrahent');
{? _wyn
||
   {? ZK_Z.KH<>OFE.KH
   ||
      OFE.PL:=exec('sp_plat','kontrahent',_fld);
      {? Plugin.runnable('ZWS_HAN_001')
      || OFE.HAN:=Plugin.run('ZWS_HAN_001','OFE')
      || OFE.HAN:=__War_f('KH_DOD','HAN',OFE.KH)
      ?};
      OFE.RAB:=exec('sp_plat','kontrahent',_fld,1);
      {? TAZ.RAB_N_BE=0 || TAZ.RAB_N_BE:=OFE.RAB ?};

::    typ rabatu
      exec('rab_typ_bd','ceny','OFE');
      {? DPOZ.WPR_R
      || OFE.RAB_TYP:={? OFE.KH().RAB_TYP='' || exec('rab_typ_bl','ceny') || OFE.KH().RAB_TYP ?}
      ?}
   ?}
?};
exec('spr_rab','ceny',OFE.RAB);
_wyn


\chk_ofpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja rekord po dla przyczyn rezygnacji z oferty
::  OLD: \chk_ofpr/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(OFPR,,'KOD');
{? _wyn=''
||
   _ref:=OFPR.ref();
   _kod:=OFPR.KOD;
   _ndx:=OFPR.ndx_tmp(,,'KOD',,,'KOD',,);
   OFPR.cntx_psh();
   OFPR.index(_ndx);
   OFPR.prefix(_kod,_kod);
   {? OFPR.first()
   ||
      {!
      |?
         {? OFPR.ref()<>_ref | -(1+menu_txt())='d'
         || FUN.info('Istnieje już przyczyna rezygnacji z kodem %1.'@[_kod]);
            _wyn:='KOD'
         ?};
         _wyn='' & OFPR.next()
      !}
   ?};
   OFPR.ndx_drop(_ndx);
   OFPR.cntx_pop()
?};
_wyn


\be_m_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: przed redakcją pola D_HELP.MOFE
::  OLD: \be_m_ofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
HELP.MOD_IND:=OFP.M;
D_HELP.MOFE:=OFP.M().KTM;
1


\bd_m_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed wyświetleniem zmiennej materiał/usługa w pozycjach oferty
::  OLD: \bd_m_ofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? (1+menu_txt())='D'
|| ''
|| POMOC.RODZO:=POMOC.RODZ:=OFP.M().RODZ;
   D_HELP.MOFE:=OFP.M().KTM ;
   BEER.JM:=OFP.M().J
?};
1


\chk_ofgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord po dla rozdziałów tab. OFEGR
::  OLD: \chk_ofgr/defin.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(OFEGR,,'KOD')


\ae_pom_rodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji zmiennej POMOC.RODZO
::  OLD: \ae_pom_rodz/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? fld()<>'T' & fld()<>'U'
|| FUN.info('Wpisz: T-Materiał lub U-Usługa.'@)
|| _rodz:=exec('FindInSet','#table','M','MATKTM',D_HELP.MOFE,D_HELP.MOFE,"M.RODZ");
   {? _rodz<>fld || OFP.M:=null; D_HELP.MOFE:='' ?};
   _wyn:=1
?};
POMOC.RODZ:=POMOC.RODZO;
_wyn


\wz_ofodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: wzorzec OFE.KH_ODB
::  OLD: \wz_ofodb/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.KH:=OFE.KH;
exec('kh_odb_f_set','kontrahent');
''


\ae_ofp_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: po redakcji pola OFP.M
::   WY: 1/0
::  OLD: \ae_ofp_m/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? fld()<>null
|| OFP.SV:=exec('m_vat','material',OFP.M,OFP.OFE().KH,,OFE.DU~1,OFE.DU~2);
   D_HELP.MOFE:={? OFP.M<>null || OFP.M().KTM || '' ?}
|| FUN.info('Niewypełnione pole.'@)
?};
_wyn


\be_ofesy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed edycja symbolu oferty numeracja rok-wersja OFE-000001
::  OLD: \be_ofesy/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFE.SYM=''
||
   _sym:='';
   OFE.cntx_psh();
   OFE.index('ODDZ');
   OFE.prefix(ST.ODDZ);
   {? OFE.last
   || _nr:=#(6+(4-(OFE.SYM)));
      _nr+=1
   || _nr:=1
   ?};
   _sym:='OFE-'+form(_nr,-6,,'99');
   OFE.cntx_pop();
   OFE.SYM:=_sym
?};
1


\ofe_pl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji OFE.PL
::  OLD: \ofe_pl_ae/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? DPOZ.WPR_S<>$OFE.PL
|| OFE.RAB:=exec('sp_plat','kontrahent',OFE.KH,1);
   {? TAZ.RAB_N_BE=0 || TAZ.RAB_N_BE:=OFE.RAB ?}
?};
1


\ae_ofe_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji pola OFE.WAL
::   WY: czy można opuścić redakcję pola 1-tak 0-nie
::  OLD: \ae_ofe_w/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? chk_fld()
|| {? OFE.WAL=INFO.NAROD | DPOZ.WPR_S<>$OFE.WAL
   || OFE.SWAL:=2;
      OFE.KRS:=0;
      OFE.DTK:=date(0,0,0);
      OFE.BTK:='';
      OFE.RTK:=0;
      OFE.NTK:=''
   ?};
   exec('set_efld_opt','!lsp_ofe_dofe');
   1
|| 0
?}


\ae_ofpsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji stawki VAT na pozycji oferty
::  OLD: \ae_ofpsv/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld<>null
|| exec('ae_ofpww','ceny_dok')
?};
1


\be_ofpkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcja pola OFP.KH (dostawca)
::  OLD: \be_ofpkh/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_S:=$OFP.KH;
1


\ae_ofpkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: po redakcji pola OFP.KH (dostawca)
::  OLD: \ae_ofpkh/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()<>null & OFP.M<>null & ({? OFP.ZWAL<>INFO.NAROD || OFP.ZCWAL=0 || OFP.CZAK=0 ?} | DPOZ.WPR_S<>$fld)
|| {? {? OFP.ZWAL<>INFO.NAROD || OFP.ZCWAL=0 || OFP.CZAK=0 ?}
      | FUN.ask('Zmienił się dostawca czy wyznaczyć cenę zakupu?'@)
   || OFP.CZAK:=OFP.ZCWAL:=0;
      exec('licz_cens_zak','!lsp_ofe_dofe');
      exec('ae_ofpww','ceny_dok');
      win_disp()
   ?}
?};
1


\licz_cens_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: liczy ceny OFP.ZCWAL, OFP.CZAK
::  OLD: \licz_cens_zak/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:='Z'; exec('taz_sd_set','ceny');
{? OFP.ZWAL<>exec('bl_nwal','ustawienia')
||
   {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?}
   ||
      KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1;
      exec('stplicz','ceny',OFP.IL);
      exec('ceny_mat','ceny_mat',OFP.M,OFP.KH,'OFP.ZCWAL',0,,'','N',OFP.ZWAL,'',,0,,,0,0,0,0);
      {? OFP.ZCWAL
      || OFP.CZAK:=OFP.ZCWAL*OFP.ZKRS/exec('FindInSet','#table','WAL','WAL_SYM',OFP.ZWAL().KOD,,"WAL.J")$OFP.M().DOKL_Z
      ?}
   ?}
||
   {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?}
   ||
      KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1;
      exec('stplicz','ceny',OFP.IL);
      exec('ceny_mat','ceny_mat',OFP.M,OFP.KH,'OFP.CZAK',0,,'','N',exec('bl_nwal','ustawienia'),'',,0,,,0,0,0,0)
   ?}
?};
BEER.SZ:='S'; exec('taz_sd_set','ceny')


\be_cenas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: przed redakcja ceny sprzedazy na pozycji oferty, OFP.C
::  OLD: \be_cenas/oferty.fml
::   WE: [_a]  - 0/1/2 - wartośc parametru CZY_SEL
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>type_of(1) || _a:=0 ?} || _a:=0 ?};
_wyn:=exec('czy_rwal','eurusd','O');
{? _wyn
||
   DPOZ.WPR_R:={? var_pres('_a')=type_of(1) || _a || fld ?};
   BEER.SZ:='S';
   exec('taz_sd_set','ceny');
   {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & OFP.C=0
   || exec('f3_cenas','!lsp_ofe_dofe',_a);
      {? OFP.C || _wyn:=exec('licz','ceny',OFP) ?}
   ?}
?};
_wyn


\f3_cenas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: f3 dla ceny sprzedazy na pozycji oferty, OFP.C
::   WE: [_a] 1-F3 (domyślnie), 0-nie
::  OLD: \f3_cenas/oferty.fml
::       \be_ofpc/ceny.fml
::   WE: [_a]  - 0/1/2 - wartośc parametru CZY_SEL
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
exec('st_licz_wz','ceny','OFE');
exec('stplicz','ceny',OFP.IL);
KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1;
exec('ceny_mat','ceny_mat',OFP.M,OFE.KH,'OFP.C',OFE.ODB,,'OFP.RAB','N',exec('bl_nwal','ustawienia'),'',,OFP.OFE().CB='T'
   ,,,_a,,,,OFP.SV,,,OFE.PL);
~~


\ae_cenas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: po redakcja ceny sprzedazy na pozycji oferty, OFP.C
::   WY: 1/0
::  OLD: \ae_cenas/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? DPOZ.WPR_R<>fld
||
   exec('clr_promo_tap','ceny',,fld,exec('bl_nwal','ustawienia'));
   exec('ae_ofpww','ceny_dok');
   {? OFP.WAL<>exec('bl_nwal','ustawienia') || exec('clr_promo_tap','ceny',,OFP.CWAL,OFE.WAL) ?};
   win_disp;
   MDOST.cntx_psh;
   MDOST.index('M');
   MDOST.prefix('S',OFP.M,null,OFP.OFE().WAL);
   {? MDOST.first & OFP.C<MDOST.CMIN & MDOST.CMIN>0
   || _wyn:=FUN.ask('Sprzedajesz poniżej ceny minimalnej.\nCzy zatwierdzić cenę?'@)
   ?};
   MDOST.cntx_pop
?};
_wyn


\ae_ofpil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji OFP.IL
::  OLD: \ae_ofpil/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('licz','ceny',OFP)
|| win_disp();
   1
?}


\be_ofpcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: przed redagowaniem OFP.CZAK
::  OLD: \be_ofpcz/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_R:=fld;
BEER.SZ:='Z'; exec('taz_sd_set','ceny');
{? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?}
||
   KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1;
   exec('stplicz','ceny',OFP.IL);
   exec('ceny_mat','ceny_mat',OFP.M,OFP.KH,'OFP.CZAK',0,,'','N',exec('bl_nwal','ustawienia'),'',,0,,,0,0,0)
?};
BEER.SZ:='S'; exec('taz_sd_set','ceny');
1


\f3_ofpcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: ceny zakupu wg dokumentow dostaw
::   WY: zwraca cene
::  OLD: \fe_ofpcz/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:='Z'; exec('taz_sd_set','ceny');
:: wylaczenie pol rabatowych w cenniku
TAZ.RABOFF:=1;
{? OFP.KH<>null
|| exec('f3_zdcen','ceny_mat',OFP.M,OFP.KH,OFP.CZAK,,'OFP',exec('bl_nwal','ustawienia'))
|| exec('f3_zdcen','ceny_mat',OFP.M,,OFP.CZAK,,'OFP',exec('bl_nwal','ustawienia'))
?};
BEER.SZ:='S'; exec('taz_sd_set','ceny');
TAZ.RABOFF:=0;
''


\ae_ofpcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: po redakcji ceny zakupu
::  OLD: \ae_ofpcz/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? DPOZ.WPR_R<>fld || exec('ae_ofpww','ceny_dok') ?};
1


\ae_ofp_koszt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji pola OFP.KOSZT
::  OLD: \ae_ofp_koszt/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ae_ofpww','ceny_dok');
1


\pr_cenaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: przed redakcja OFP.CWAL
::  OLD: \pr_cenaw/oferty.fml
::       \be_ofpcwal/ceny.fml
::   WE: [_a]  - 0/1/2 - wartośc parametru CZY_SEL
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>type_of(1) || _a:=0 ?} || _a:=0 ?};
{? exec('spr_npoz','eurusd')
|| DPOZ.WPR_R:={? var_pres('_a')=type_of(1) || _a || fld ?};
   BEER.SZ:='S';
   {? OFP.CWAL=0
   || exec('st_licz_wz','ceny','OFE');
      exec('stplicz','ceny',OFP.IL);
      KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1;
      exec('ceny_mat','ceny_mat',OFP.M,OFE.KH,'OFP.C',OFE.ODB,,'OFP.RAB','T',OFP.WAL,'OFP.CWAL',OFP.KRS,OFP.OFE().CB='T'
         ,,,_a,,,,OFP.SV,,,OFE.PL);
      {? OFP.CWAL || DPOZ.WPR_R:=-1; exec('po_cenaw','ceny',0); DPOZ.WPR_R:={? var_pres('_a')=type_of(1) || _a || fld ?}?}
   ?};
   1
?}


\f3_ofpcw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: f3 dla ceny sprzedazy na pozycji oferty, OFP.CWAL
::  OLD: \f3_ofpcw/oferty.fml
::       \f3_ofpcwal/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
exec('st_licz_wz','ceny','OFE');
exec('stplicz','ceny',OFP.IL);
KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1;
exec('ceny_mat','ceny_mat',OFP.M,OFE.KH,'OFP.C',OFE.ODB,,'OFP.RAB','T',OFP.WAL,'OFP.CWAL',OFP.KRS,OFP.OFE().CB='T',,,1
   ,,,,OFP.SV,,,OFE.PL);
''


\ae_ozwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009+]
:: OPIS: po edycji pola waluta zakupu
::  OLD: \ae_ozwal/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? OFP.ZWAL<>null
|| {? OFP.ZWAL=exec('bl_nwal','ustawienia')
   || OFP.ZCWAL:=OFP.ZKRS:=0
   ?};
   _wyn:=1
|| FUN.info('Należy podać walutę.'@)
?};
_wyn


\be_zak_cwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed edycja pola cena zakupowa w walucie
::  OLD: \be_zak_cwal/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_R:=fld;

_wyn:=OFP.ZWAL<>exec('bl_nwal','ustawienia');
{? _wyn
||
   BEER.SZ:='Z'; exec('taz_sd_set','ceny');
   {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & OFP.ZCWAL=0
   ||
      KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1;
      exec('stplicz','ceny',OFP.IL);
      exec('ceny_mat','ceny_mat',OFP.M,OFP.KH,'OFP.ZCWAL',0,,'','N',OFP.ZWAL,'',,0,,,0,0,0);
      {? OFP.ZCWAL
      || OFP.CZAK:=OFP.ZCWAL*OFP.ZKRS/exec('FindInSet','#table','WAL','WAL_SYM',OFP.ZWAL().KOD,,"WAL.J")$OFP.M().DOKL_Z;
         win_disp
      ?}
   ?};
   BEER.SZ:='Z'; exec('taz_sd_set','ceny')
?};
_wyn


\f3_zcwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: F3 pola OFP.ZCWAL
::  OLD: \f3_zcwal/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:='Z'; exec('taz_sd_set','ceny');
KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1;
:: wylaczenie pol rabatowych w cenniku
TAZ.RABOFF:=1;
exec('f3_zdcen','ceny_mat',OFP.M,OFP.KH,OFP.ZCWAL,,'OFP',OFP.ZWAL);
TAZ.RABOFF:=0;
BEER.SZ:='S'; exec('taz_sd_set','ceny');
''


\ae_zcwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji ceny walutowej zakupu na pozycji oferty, OFP.ZCWAL, OFP.ZKRS
::   WY: 1
::  OLD: \ae_zcwal/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
{? DPOZ.WPR_R<>fld
|| OFP.CZAK:=OFP.ZCWAL*OFP.ZKRS/exec('FindInSet','#table','WAL','WAL_SYM',OFP.ZWAL().KOD,,"WAL.J")$OFP.M().DOKL_Z;
   exec('ae_ofpww','ceny_dok')
?};
1


\be_zak_kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed edycja pola kurs zakupu w walucie
::  OLD: \be_zak_kurs/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_R:=fld;
_wyn:=OFP.ZWAL<>exec('bl_nwal','ustawienia');
_wyn


\ofe_kop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: Kopiowanie oferty
::  OLD: \ofe_kop/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? ST.AR<>date()~1
|| FUN.info('Aktualny rok nie jest zgodny\nz rokiem ustawionym w parametrach systemu.\n\n'
            'Wymagany rok to: %1.\n'
            'Kopiowanie niemożliwe.'@[form(date()~1,,0,'99')]);
   _choice:=0
|? OFE.r_lock(1,1,1)
|| _choice:=FUN.choice('Skopiować bieżącą ofertę'@,,'Jako &nową'@,'Kolejny wariant'@);
   {? _choice=0 || OFE.r_unlock() ?}
|| _choice:=0;
   {? FUN.ask('Dokument obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
   || OFE.r_unlock()
   ?}
?};

{? _choice>0
||
   _ref_old:=OFE.ref();
   _sym:=OFE.SYM;

   _ofe:=obj_new(OFE.fld_num());
   _ofp:=obj_new(OFP.fld_num());
   _ofk:=obj_new(OFK.fld_num());

   {! _licz:=1..(OFE.fld_num()) |! _ofe[_licz]:=OFE[_licz] !};
   _memo:=OFE.memo_get('r','OPIS');

   do();

   _refofe:=$OFE.ref();
   _symw:={? OFE.SYMW<>OFE.SYM || OFE.SYMW-6 || OFE.SYM ?};
   OFE.blank();
   {! _licz:=1..(OFE.fld_num())
   |! {? var_pres(OFE.fld_acr(_licz),OFE)<>25 & var_pres(OFE.fld_acr(_licz),OFE)<>36 || OFE[_licz]:=_ofe[_licz] ?}
   !};
   OFE.STAT_REJ:='Z';
:: wyznaczenie nowego numeru
   _da:=date();
   _du:=OFE.DU;
   _tw:=OFE.TW;
   _dr:=OFE.DR;
   {? (_da~1)<>(_du~1)
   || _odstep:=_da-_du;
      OFE.AR:=_da~1;
      OFE.AM:=_da~2;
      OFE.DU:=_da;
      {? _tw<>date(0,0,0) || OFE.TW:=_tw+_odstep ?};
      {? _dr<>date(0,0,0) || OFE.DR:=_dr+_odstep ?}
   ?};

   POM.TAB:='OFE';
   POM.TYPDOK:=OFE.T().KOD;
   OFE.NR:=0;
   OFE.A:='A';

   OFE.REFOFE:=_refofe;
   {? OFE.add()
   || OFE.NR:=exec('numer_new','numery','PACZKA');
      exec('znak','numery','OFE');
      {? _choice=2
      || OFE.WO:='T';
         OFE.cntx_psh();
         OFE.index('SYMW');
         OFE.prefix(ST.ODDZ,_symw);
         {? OFE.last()
         || _nr:=#(((+_symw)+1)-OFE.SYMW)+1
         || _nr:=1
         ?};
         OFE.cntx_pop();
         OFE.SYMW:=_symw+'-'+form(_nr,-5,,'99');
         OFE.STAT_REJ:='N'
      |? _choice=1
      || OFE.SYMW:=OFE.SYM;
         OFE.STAT_REJ:='N';
         OFE.WO:='N';
         exec('be_ofesy','!lsp_ofe_dofe')
      ?};
      OFE.put();
      _ref_new:=OFE.ref();
      {? _memo.is_open || OFE.memo_put(_memo,'OPIS') ?}
   || _ref_new:=null;
      undo()
   ?};

:: pozycje
   OFP.index('OFE_P');
   OFP.prefix(_ref_old);
   {? OFP.first()
   ||
      {!
      |?
         {! _licz:=1..OFP.fld_num() |! _ofp[_licz]:=OFP[_licz] !};
         OFP.cntx_psh();
         OFP.prefix();
         OFP.blank();
         {! _licz:=1..OFP.fld_num() |! OFP[_licz]:=_ofp[_licz] !};
         OFP.OFE:=_ref_new;
         OFP.add();
         OFP.cntx_pop();
         OFP.next()
      !}
   ?};

:: koszty
   OFK.index('OFE');
   OFK.prefix(_ref_old);
   {? OFK.first()
   ||
      {!
      |?
         {! _licz:=1..OFK.fld_num() |! _ofk[_licz]:=OFK[_licz] !};
         OFK.cntx_psh();
         OFK.prefix();
         OFK.blank();
         {! _licz:=1..OFK.fld_num() |! OFK[_licz]:=_ofk[_licz] !};
         OFK.OFE:=_ref_new;
         OFK.add();
         OFK.cntx_pop();
         OFK.next()
      !}
   ?};
:: przypisanie aktualnego operatora

   OFE.cntx_psh();
   OFE.clear();
   {? OFE.seek(_ref_old) || OFE.r_unlock() ?};
   {? OFE.seek(_ref_new) || OFE.US:=OPERATOR.USER; OFE.put() ?};
   OFE.cntx_pop();

   end()

?};
_inf:=0;
{? _choice=1
|| _old_kh:=OFE.KH;
   KH.win_sel('WER_OFE');
   KH.win_edit('RED');
   KH.index('KOD');
   KH.prefix(2);
   _selkh:=
      {? FUN.ask('Zmiana danych na kopiowanej ofercie?'@)
      || OFE.STAT_REJ:='N';
         OFE.put(1);
         exec('ofe_win_edit_set','!lsp_ofe_dofe');

         params_set(params_get());

         {? exec('ofe_edit','!lsp_ofe_dofe')
         || 1
         || FUN.info('Zrezygnowano ze zmiany kontrahenta na ofercie.'@);
            0
         ?}
      || 0
      ?};
   {? _selkh & ZAKR.KH<>null & KH.ref<>_old_kh
   || FUN.info('Oferta została skopiowana.\n\n'
               'Aktualnie zredagowana oferta nie jest dostępna w ustawionym zakresie widoku ofert.'@);
      _inf:=1
   ?}
?};
{? ZAKR.US<>null & ZAKR.US<>OPERATOR.USER & ~_inf
|| FUN.info('Oferta została skopiowana.\n\n'
            'Aktualnie zredagowana oferta nie jest dostępna w ustawionym zakresie widoku ofert.'@)
?};
1


\oferta_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Po edycji okienka
::   WE: _a - 0-dołącz 1-popraw -1-aktualizuj
::----------------------------------------------------------------------------------------------------------------------
_new:=OFE.ref();
OFE.REFOFE:=$OFE.ref();
{? OFE.WO='N' || OFE.SYMW:=OFE.SYM ?};
OFE.SPOBLRAB:=exec('get','#params',800810,2);
exec('akt_cen','!lsp_ofe_dofe');
{? _a<>-1 || OFE.r_unlock() ?}


\oferta_pozycje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferty - Pozycje - okno redagowania
::   WE: params_get()
::   WY: 'key:F2' - jeśli pola nagłówka oferty prawidłowo wypełnione
::       'edit:ACRONYM' - akronim niewłaściwie wypełnionego pola nagłówka oferty
::       '' - wartość domyślna jeśli main nie zmieni na powyższe
::----------------------------------------------------------------------------------------------------------------------
_btnRuleResult:='';

_fld:=params_exec('oferta_valid','!lsp_ofe_dofe');
{? _fld<>''
|| _btnRuleResult:='edit:'+_fld
|| exec('oferta_after','!lsp_ofe_dofe',-1);
   BPMN.END:=1;
   exec('ofe_poz','lsp',1);
   {? BPMN.END=2 || _btnRuleResult:=params_exec('oferta_zakoncz','!lsp_ofe_dofe') ?}
?};

_btnRuleResult


\oferta_zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zakonczenie redakcji oferty - zmiana statusu
::   WE: [_a] - powołane z okienka=1, domyślnie nie=0
::       [_b] - 1-automatyczna akceptacja 0(domyślnie)-nie
::   WY: klawisz zatwierdzenia
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
_autoakc:={? var_pres('_b')=type_of(0) || _b || 0 ?};

_mp:=params_get().mp;
_btnRuleResult:='';

_wyn:=0;
_fld:=params_exec('oferta_valid','!lsp_ofe_dofe');
{? _fld=''
|| {? OFE.STAT_REJ='T'
   || FUN.info('Oferta jest zaakceptowana.\nZadanie zostanie zakończone.'@);
      _wyn:=1
   |? OFE.STAT_REJ='Z'
   || {? BPMN.END<>2 || FUN.info('Zakończono rejestrację oferty.\nZadanie zostanie zakończone.'@) ?};
      _wyn:=1
   |? OFE.T().VALIDATE<>null() & ~exec('validate','wzorce',OFE.T().VALIDATE,OFE,OFE.ref())
   || _wyn:=0
   |? OFE.STAT_REJ='N' & {? BPMN.END<>2 || FUN.ask('Zakończyć rejestrację oferty %1'@[OFE.SYM]) || 1 ?}
   || SUM.SUM:=exec('kosztofe','lsp');
      exec('sumofe','lsp',SUM.SUM);
      OFE.STAT_REJ:='Z';
      _wyn:=OFE.put(1)
   ?};
   {? _wyn
   || {? ~_a || _btnRuleResult:='key:F2' ?};
      _mp.done()
   ?}
|| _btnRuleResult:='edit:'+_fld
?};
{? _wyn & _autoakc || exec('oferta_akceptuj','oferty',1) ?};
_btnRuleResult


\oferta_zakoncz_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zakonczenie redakcji oferty - zmiana statusu
::   WY: klawisz zatwierdzenia
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_OFE_DOFE';
_params.UIDREF:=OFE.uidref();
_params.AKCJA:='Zakończ_wer';

exec('mp_run','#b__box',_params)


\ofe_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Ustawia okno redakcji tabeli OFE, wymagane pola, okna słowników
::   WE: [_a] - 1-bez zakończ 0-(domyślnie) z zakończ
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
BPMN.END:=0;
_win_red:=exec('ofe_win_edit','!lsp_ofe_dofe');
{? OFE.STAT_REJ<>'T' | (1+menu_txt()='D')
|| _ff:="params_exec('oferta_pozycje','!lsp_ofe_dofe')";
   OFE.win_ebtn(_win_red,'text='+'Poz&ycje'@+',btn_label_align=center,panel=bottom,align=begin',_ff)
?};
{? ~_a
|| _ff:="params_exec('oferta_zakoncz','!lsp_ofe_dofe')";
   exec('zakoncz','#window',OFE,_win_red,1,_ff,0, exec('help_red_zakoncz','#window','O'),
      exec('text_red_zakoncz','#window','O'))
?};
exec('ok_esc','#window',OFE,_win_red,1,,,,,exec('help_red_ok','#window','Z'),exec('text_red_ok','#window')
   ,exec('help_red_esc','#window','A'));
OFE.win_edit(_win_red);
exec('sel_win_kh','ustawienia')


\ofe_win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - tymczasowe okienko redakcji
::----------------------------------------------------------------------------------------------------------------------
_win_akr:='RED';
:: Tymczasowe okno redakcji
_title:='Dane oferty';
_win_red:=OFE.mk_edit(_title,,,,,'html_maximized');
OFE.win_etab(_win_red,'Dane podstawowe');
OFE.win_ewin(_win_red,,_win_akr);
{? var_pres('OFE_POPRAW')>0 & OFE_POPRAW=1 & OFE.STAT_REJ='T'
|| OFE.efld_opt(_win_red,'editable=0',,'PROJEKTY');
   OFE.efld_opt(_win_red,'editable=0',,'KH');
   OFE.efld_opt(_win_red,'editable=0',,'ODB');
   OFE.efld_opt(_win_red,'editable=0',,'HAN');
   OFE.efld_opt(_win_red,'editable=0',,'DU');
   OFE.efld_opt(_win_red,'editable=0',,'DR');
   OFE.efld_opt(_win_red,'editable=0',,'TW');
   OFE.efld_opt(_win_red,'editable=0',,'CB');
   OFE.efld_opt(_win_red,'editable=0',,'PL');
   OFE.efld_opt(_win_red,'editable=0',,'POJAZD');
   OFE.efld_opt(_win_red,'editable=0',,'RAB');
   OFE.efld_opt(_win_red,'editable=0',,'RAB_TYP');
   OFE.efld_opt(_win_red,'editable=0',,'WAL');
   OFE.efld_opt(_win_red,'editable=0',,'SWAL');
   OFE.efld_opt(_win_red,'editable=0',,'KRS');
   OFE.efld_opt(_win_red,'editable=0',,'DTK');
   OFE.efld_opt(_win_red,'editable=0',,'SPP');
   OFE.efld_opt(_win_red,'editable=0',,'OPIS');
   OFE.efld_opt(_win_red,'editable=0',,'U')
?};
_win_red


\f3_m_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: f3 dla zmiennej material/usluga w pozycjach oferty
::  OLD: \f3_m_ofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
M.index('ARODZ');
M.prefix('T',POMOC.RODZO);
M.find_key(D_HELP.MOFE);
_sel:='';
{? POMOC.RODZO='U'
|| _sel:='NL_WERU'; _red:='REDU'; _szuk:='SZUKU'
|| _sel:='NL_WER';  _red:='RED';  _szuk:='SZUK'
?};
M.win_sel(_sel);
M.win_edit(_red);
M.win_patt(_szuk);
M.actions(_sel,'RE:RE','W',1);
{? M.select(,1,10) || D_HELP.MOFE:=M.KTM; _wyn:=M.KTM; POMOC.RODZO:=M.RODZ || _wyn:=D_HELP.MOFE ?};
M.f_clear();
_wyn


\ae_m_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji zmiennej material/usluga w pozycjach oferty
::  OLD: \ae_m_ofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
ATR.MJS:='OFP';
_wyn:=0;

{? fld()=''
|| D_HELP.MR:=null;
   {? POMOC.RODZO='U'
   || exec('find_sql','#table','M','KTM;N;KODK','M.A=\'\'T\'\' and M.RODZ=\'\'U\'\' ','D_HELP.MR',
         '20@Kod materiału;60@Nazwa materiału;20@Kod kreskowy','RED','','')
   || exec('find_sql','#table','M','KTM;N;KODK','M.A=\'\'T\'\' and M.RODZ=\'\'T\'\'','D_HELP.MR',
         '20@Kod materiału;60@Nazwa materiału;20@Kod kreskowy','RED','','')
   ?};
   OFP.M:=D_HELP.MR; D_HELP.MOFE:=D_HELP.MR().KTM;
   {? OFP.M<>null & OFP.SV=null
   || OFP.SV:=exec('m_vat','material',OFP.M,OFP.OFE().KH,,OFE.DU~1,OFE.DU~2)
   ?}
?};

{? fld()<>''
||
   OFP.cntx_psh();
   _mat:=null;_gru:=null;_jm:=null;
   M.cntx_psh();
   M.index('ARODZ');
   M.prefix('T',POMOC.RODZO,fld,fld);
   {? M.first()
   || _mat:=M.ref(); D_HELP.MOFE:=M.KTM; _wyn:=1
   ||
::    dodatkowo poszukujemy pierwszego indeksu wg czesci KTM-u
      {? form(D_HELP.MOFE)<>''
      || M.index('ARODZ');
         M.prefix('T',POMOC.RODZ);
         {? M.find_key(D_HELP.MOFE)
         || _mat:=M.ref(); D_HELP.MOFE:=M.KTM; _wyn:=1
         || FUN.info('Brak indeksu w słowniku.'@)
         ?}
      || FUN.info('Brak indeksu w słowniku.'@)
      ?}
   ?};
   M.cntx_pop();
   OFP.cntx_pop();
   OFP.M:=_mat;
   BEER.JM:=OFP.M().J;
   {? OFP.M<>null & OFP.SV=null || OFP.SV:=exec('m_vat','material',OFP.M,OFP.OFE().KH,,OFE.DU~1,OFE.DU~2) ?};
   _dost_d:=exec('dost','ceny_mat',OFP.M,date(),OFP.ZWAL,0).KH;
   {? HELP.MOD_IND<>OFP.M
   ||
::    zerowanie wartosci zakupowych
      OFP.ZCWAL:=OFP.CZAK:=0;
      OFP.KH:=_dost_d;
      {? OFP.KH || exec('licz_cens_zak','!lsp_ofe_dofe') ?}
   ?};
   {? _wyn=1
   || DPOZ.M:=HELP.KTM:=OFP.M;
      OFP.SV:=exec('m_vat','material',M.ref(),OFP.OFE().KH,,OFE.DU~1,OFE.DU~2)
   ?};

   {? HELP.MOD_IND<>OFP.M
   ||
::    zerowanie wartosci sprzedazowych
      OFP.CWAL:=OFP.C:=OFP.RAB:=OFP.RABK:=0;
      exec('clr_promo_tap','ceny');
      _wyn:=exec('ae_ofpww','ceny_dok');
      {? _wyn || win_disp ?}
   ?};

   _wyn
|| OFP.M:=null;
   win_disp();
   FUN.info('Niewypełnione pole.'@)
?};
{? _wyn || exec('juz_jest','material','O',OFP.OFE,OFP.M,{? ~-(6+menu_txt())='POPRAW' || OFP.ref() || null ?}) ?};
{? _wyn
|| {? OFP.M().M_ATR<>null & #OFP.M().M_ATR<>#ATR.M_ATR
   || {! _i..10 |! ($('ATR.WAR'+form(_i,-2,0,'99')))():='' !};
      OFP.DK_C:=null
   ?};
   ATR.M_ATR:=OFP.M().M_ATR;
   ATR.FLAG_ED:=ATR.CZY_ATR & ATR.M_ATR().EDIT;
   ATR.FLAG:={? ATR.FLAG_ED & OFP.M().M_ATR<>null() || 2 || 0 ?};
   {? ATR.FLAG_ED || {? OFP.M().M_ATR<>null() || ATR.FLAG_ED:=2 ?} ?}
?};
_wyn


\ofp_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: popraw pozycje oferty
::  OLD: \ofp_pop/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
ATR.MJS:='OFP';
ATR.M_ATR:=OFP.M().M_ATR;
ATR.UZUP:=exec('wz_uzup','mat_atr',ATR.M_ATR);
{? OFP.DK_C<>null() & OFP.DK_C().M_ATR<>null()
|| {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():=($('OFP.DK_C().WAR'+form(_i,-2,,'99')))() !}
|| {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !}
?};
{? exec('czy_zam','zamowienia')=0
|| OFP.win_edit('RED');
   {? OFP.edit("exec('chk_ofp','!lsp_ofe_dofe')")
   || _wyn:=OFP.put()
   ?}
?};
_wyn


\ofp_usus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: Formula przed na akcje Usun okna OFP.WER
::  OLD: \ofp_usus/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFP.sel_size()=0
|| _zam:=exec('czy_zam','zamowienia');
   {? _zam=1 || FUN.info('Utworzono zamówienie na podstwie oferty.\nUsunięcie pozycji niemożliwe.'@) ?};
   {? _zam=0 & FUN.ask('Czy usunąć pozycję oferty?'@)
   ||
      OFP.del();
      OFP.cntx_psh();
      OFP.index('OFE_P');
      OFP.prefix(OFE.ref);
      exec('ReNumAfterDel','#table','OFP','POZ');
      exec('sumofe','lsp',SUM.SUM);
      OFE.put();
      OFP.cntx_pop()
   ?}
|| OFP.del();
   OFP.cntx_psh();
   OFP.index('OFE_P');
   OFP.prefix(OFE.ref);
   exec('ReNumAfterDel','#table','OFP','POZ');
   OFP.cntx_pop()
?};
''


\ofp_usup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: Formula grupa przed na akcje Usun okna OFP.WER
::  OLD: \ofp_usup/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_zam:=exec('czy_zam','zamowienia');
{? _zam
|| FUN.info('Utworzono zamówienie na podstwie oferty.\nUsunięcie zaznaczonych pozycji niemożliwe.'@);0
|| sel_nchk();
   FUN.ask('Czy usunąć zaznaczone pozycje oferty?'@)
?}


\ofp_usuo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: Formula grupa po na akcje Usun okna OFP.WER
::  OLD: \ofp_usuo/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
exec('sumofe','lsp',SUM.SUM);
OFE.put();
1


\ofp_marz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: Formula przed na akcje przypisz Cene okna OFP.WER
::  OLD: \ofp_marz/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? ~exec('czy_zam','zamowienia',1)
||
   _form:="
      {? OFP.CZAK>0 & (OFP.WAL=INFO.NAROD | OFP.KRS)
      ||
         _walj:=exec('FindInSet','#table','WAL','WAL_SYM',OFP.WAL().KOD,,\"WAL.J\",,,1);
         _vat:=#((OFP.SV().KOD*'%')+OFP.SV().KOD-1)/100;
         OFP.C:=({? _a<1 & OFP.IL>0
                 || ((OFP.CZAK+(OFP.KOSZT/OFP.IL))/(1-_a)/(1-(OFP.RAB+OFE.RAB)/100))$OFP.M().DOKL_S
                 || OFP.C
                 ?});
         {? OFP.OFE().CB='T' || OFP.C:=(OFP.C*(1+_vat))$OFP.M().DOKL_S ?};
         {? OFP.KRS>0 || OFP.CWAL:=(OFP.C/OFP.KRS*_walj)$OFP.M().DOKL_S ?};
         exec('ae_ofpww','ceny_dok');
         {? OFP.TAR_H
         ||
            TAR_H.cntx_psh();
            TAR_H.use(ref_name(OFP.TAR_H));
            exec('clr_promo_tap','ceny',OFP,{? OFP.TAR_H().WAL=OFP.WAL & OFP.WAL<>INFO.NAROD || OFP.CWAL || OFP.C ?},
               OFP.TAR_H().WAL);
            TAR_H.cntx_pop()
         ?};
         OFP.put()
      ?}";

   {? OFP.sel_size<>0
   || {? OFP.MPR<>marz_gr || _form(marz_gr) ?}
   || ZAKR.OFZM:=OFP.MPR;
      ZAKR.win_edit('OFPM');
      ZAKR.fld_fml('OFZM','EDIT_FORMAT',"'in_prec=2'");
      {? ZAKR.edit("{? ZAKR.OFZM<0
                    || FUN.info('Marża nie może być ujemna.'@); 'OFZM'
                    |? ZAKR.OFZM>=100
                    || FUN.info('Marża musi być mniejsza od 100.'@); 'OFZM'
                    || ''
                    ?}")
      || {? OFP.MPR<>ZAKR.OFZM || _form(ZAKR.OFZM/100) ?}
      ?}
   ?};
   _wyn:=1
?};
_wyn


\ofp_mapr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: Formula Grupa przed na akcje przypisz Cene okna OFP.WER
::  OLD: \ofp_mapr/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? ~exec('czy_zam','zamowienia',1)
|| ZAKR.OFZM:=0;
   ZAKR.win_edit('OFPM');
   ZAKR.fld_fml('OFZM','EDIT_FORMAT',"'in_prec=2'");
   {? ZAKR.edit("{? ZAKR.OFZM<0
                 || FUN.info('Marża nie może być ujemna.'@); 'OFZM'
                 |? ZAKR.OFZM>=100
                 || FUN.info('Marża musi być mniejsza od 100.'@); 'OFZM'
                 || ''
                 ?}")
   || marz_gr:=ZAKR.OFZM/100;
      _wyn:=1
   ?}
?};
_wyn


\ofp_mapo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: Formula Grupa po na akcje przypisz Cene okna OFP.WER
::  OLD: \ofp_mapo/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('marz_gr')=1 || &marz_gr ?}


\ofp_rapr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: przypisywanie rabatu na pozycjach oferty
::  OLD: \ofp_rapr/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('upr_cs','ceny')
||
   FUN.info('Brak upranień do wartości sprzedaży.'@)
|? ~exec('czy_zam','zamowienia',1)
||
   _p2130:=exec('get','#params',2130,2,OPERATOR.USER);
   __rabp:=exec('czyrabp','ceny',OFE.RAB_TYP);
   {? __rabp
   || ZAKR.win_edit('OFPR');
      ZAKR.OFZM:=OFP.RAB
   || ZAKR.win_edit('OFPK');
      ZAKR.OFZM:=OFP.RABK
   ?};
   ZAKR.fld_fml('OFZM','EDIT_FORMAT',"'in_prec=2'");
   {? ZAKR.edit("{? ZAKR.OFZM<0
                 || FUN.info('Rabat nie może być ujemny.'@); 'OFZM'
                 || ''
                 ?}")
   || _Tab:=OFP.sel_aget();
      {? ~_Tab.first || _Tab.REF:=OFP.ref; _Tab.add ?};
      {? _Tab.first
      || exec('ini_kom','#message','Zmiana rabatu');
         OFP.cntx_psh(); TAR_H.cntx_psh();
         _Tab.clear();
         {? _Tab.first()
         || {!
            |? {? (OFP.clear(); OFP.seek(_Tab.REF,OFP.name()))
               || {? _p2130='B' | _p2130='N' | OFP.TAR_H().RAB_B='T'
                  ||
                     exec('add_kom','#message','Poz.%1. Brak uprawnień do edycji rabatu.'@[$OFP.POZ],7)
                  ||
                     {? {? __rabp || OFP.RAB<>ZAKR.OFZM || OFP.RABK<>ZAKR.OFZM ?}
                     || {? __rabp
                        || OFP.RAB:=ZAKR.OFZM
                        || OFP.RABK:=ZAKR.OFZM
                        ?};
                        exec('ae_ofpww','ceny_dok');
                        _war:=exec('dok_pozwa','ceny_dok',OFP);
                        exec('clr_promo_tap','ceny',OFP,{? __rabp || OFP.RAB || OFP.RABK ?});
                        __txt:='';
                        {? exec('spr_rab','ceny',{? __rabp || OFP.RAB || OFP.RABK ?},0,!__txt)=0
                        || exec('add_kom','#message','Poz.'+$OFP.POZ+'. '+__txt,7)
                        |? OFP.TAR_H & TAR_H.use(ref_name(OFP.TAR_H))
                           & exec('ilwar_chk','ceny_dok',OFP.TAR_H().TAP,OFP.IL,_war,OFE.CB,OFP)=0
                        || exec('add_kom','#message',
                                'Poz.%1. Niespełnione kryterium ilościowo-wartościowe.'@[$OFP.POZ],7)
                        || OFP.put()
                        ?}
                     ?}
                  ?}
               ?};
               _Tab.next()
            !}
         ?};
         OFP.cntx_pop(); TAR_H.cntx_pop();
         OFP.sel_adel();
         exec('end_kom','#message')
      ?}
   ?};
   VAR_DEL.delete('__rabp')
?};
0


\ofp_rapi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: przypisywanie ilosci na pozycjach oferty
::  OLD: \ofp_rapi/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('czy_zam','zamowienia',1)
||
   ZAKR.win_edit('OFPI');
   ZAKR.OFZM:=OFP.IL;
   ZAKR.fld_fml('OFZM','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $OFP.M().DOKL || '2' ?}");
   {? ZAKR.edit("{? ZAKR.OFZM<=0
                 || FUN.info('Ilość musi być większa od zera.'@); 'OFZM'
                 || ''
                 ?}")
   || OFP.cntx_psh(); TAR_H.cntx_psh();
      OFP.prefix;
      _Tab:=OFP.sel_aget();
      {? ~_Tab.first || _Tab.REF:=OFP.ref; _Tab.add ?};

      exec('ini_kom','#message','Zmiana ilości');

      _loop:=_Tab.first();
      {!
      |? _loop
      |!
         {? OFP.seek(_Tab.REF,)
         ||
            OFP.IL:=ZAKR.OFZM;
            exec('ae_ofpww','ceny_dok');
            _war:=exec('dok_pozwa','ceny_dok',OFP);
            {? OFP.TAR_H & TAR_H.use(ref_name(OFP.TAR_H))
               & exec('ilwar_chk','ceny_dok',OFP.TAR_H().TAP,OFP.IL,_war,OFE.CB,OFP)=0
            || exec('add_kom','#message','Poz.'+$OFP.POZ+'. '+'Niespełnione kryterium ilościowo-wartościowe.',7)
            || OFP.put()
            ?}
         ?};
         _loop:=_Tab.next()
      !};

      exec('end_kom','#message');

      OFP.cntx_pop(); TAR_H.cntx_pop()
   ?}
?};
0


\dostawcy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: utworzenie tabeli dostawcow
::   WE: _[1]-zmienna do ktorej zostanie przypisana cena dostawy
::   WY: zwraca ref kontrahneta
::  OLD: \dostawcy/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:='Z';
:: wylaczenie pol rabatowych w cenniku
TAZ.RABOFF:=1;

_Sel:=OFP.sel_aget;
_sel:=_Sel.first;

{? ~_Sel.first || _Sel.REF:=#OFP.ref; _Sel.add ?};

OFP.cntx_psh;
{? _Sel.first & OFP.seek(_Sel.REF,)
||
   _cena_d:=exec('f3dostaw','!lsp_ofe_dofe',OFP.M);

   {? TAZ.WYB
   ||
      {!
      |?
         {? OFP.seek(_Sel.REF,)
         ||
            OFP.KH:=BEER.KH;
            OFP.CZAK:={? _cena_d>0 || _cena_d || 0 ?};
            OFP.ZCWAL:=OFP.ZKRS:=0;
            OFP.ZWAL:=exec('bl_nwal_mg','ustawienia');
            exec('licz_cens_zak','!lsp_ofe_dofe');
            {? OFP.KH<>null || exec('ae_ofpww','ceny_dok') ?};
            OFP.put
         ?};
         _Sel.next
      !}
   ?}
?};
OFP.cntx_pop;

OFP.sel_adel;
{? _sel & TAZ.WYB=0 || OFP.sel_sres(_Sel) ?};

TAZ.RABOFF:=0;

0


\f3dostaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: podpowiadanie dostawcow do danego materialu na planie zamowien dostaw i w ofertach
::   WE: _a - M.reference
::   WY: cena
::  OLD: \f3dostaw/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_czysc:="VAR_DEL.delete('__khwin','__khwer','__dostwe')";
_czysc();

TAZ.WYB:=_wyn:=0;

MDOST.index('M'); MDOST.prefix('D',_a);
__dostwe:=MDOST.mk_sel('Dostawcy');
MDOST.win_fld(__dostwe,,'KH','KOD',,10,,,'Kod'@);
MDOST.win_fld(__dostwe,,'KH','NAZ',,30,,,'Nazwa'@);
MDOST.win_fld(__dostwe,,'WAL','KOD',,10,,,'Waluta'@);
_fb:="sel_exit";
MDOST.win_act(__dostwe,,'Formuła','&Wybierz'@@,,,_fb,,1,,,,'W');

__khwer:=KH.mk_sel('Kontrahenci');
KH.win_fld(__khwer,,'KOD',,,10,,,'Kod'@);
KH.win_fld(__khwer,,'SKR',,,10,,,'Skrót'@);
KH.win_fld(__khwer,,'NAZ_P',,,30,,,'Nazwa pełna'@);
KH.win_fld(__khwer,,'NIP',,,20,,,'NIP'@);
_fb:="sel_exit";
KH.win_act(__khwer,,'Formuła','&Wybierz'@@,,,_fb,,1,,,,'W');
KH.win_act(__khwer,,'Szukaj',,,,,,);
KH.win_act(__khwer,,'Kolejność',,,,,,);

:: sprawdzenie czy zaznaczono rozne materialy
_ceny:=1;
OFP.cntx_psh();
{? OFP.sel_size()
||
   _sel:=OFP.sel_aget();
   {? _sel.first()
   || _ilm:=0; _mat:='';
      {!
      |? {? OFP.seek(_sel.REF,)
         ||
            {? _mat<>OFP.M().KTM || _ilm+=1; _mat:=OFP.M().KTM; {? _ilm>1 || _ceny:=0 ?} ?}
         ?};
         _ceny=1 & _sel.next()
      !}
   ?}
?};
OFP.cntx_pop();

KH.index('KOD');
KH.prefix(2);
{? _ceny=1
|| _wyn:=exec('f3_zdcen','ceny_mat',_a,OFP.KH,OFP.CZAK,2,'OFP');
   TAZ.WYB:=TAZ.WYB & _wyn<>OFP.CZAK | BEER.KH<>OFP.KH
|| KH.win_sel(__khwer);
   {? KH.select
   || TAZ.WYB:=1;
      BEER.KH:=KH.ref
   ?}
?};

_czysc();

_wyn


\ofe_wydr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdzenie po redakcji kontrahenta czy nie jest zablokowany na wydruku oferty ze sprawdzenie warosci
::       z ofert i akturalnie drukowanej oferty na podstawie parametru w systemie KSTE.B
::   WE: miejsce wywolania
::  OLD: \ofe_wydr/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
OFE.KH;
_wyn:=0;
KSTE.get();
{? KSTE.B='N'
|| {? 1
   || _wyn:=1
   || FUN.info('Niewypełnione pole.'@)
   ?}
||
   {? 1
   || _kh_b:=__War_f('KH_DOD','B',KH.ref);
      {? KSTE.B='T' & (_kh_b='B' | _kh_b='W')
      || FUN.info({? _kh_b='W' || 'Kontrahent jest zablokowany warunkowo.'
                  |? _kh_b='B' || 'Kontrahent jest zablokowany bezwarunkowo.' ?});
         {? _kh_b='W'
         || _wyn:=1
         ?}
      || _wyn:=1
      ?}
   || FUN.info('Niewypełnione pole.'@)
   ?}
?};

:: sprawdzanie limitu kredytowego
_klim:=null;
{? __War_f('KH_DOD','KLIM',OFE.KH)<>null
|| _klim:=__War_f('KH_DOD','KLIM',OFE.KH)
|| _klim:=OFE.KH().GRKH().KLIM
?};
_wyn:=exec('lim_kred','limit_kredyt',OFE.KH,date(),INFO.NAROD,1,_klim,OFE.BRT,,OFE.name());
_wyn


\sn_ofe_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.00]
:: OPIS: Przed wyświetl dla pola DISP.SN_OFE
::----------------------------------------------------------------------------------------------------------------------
_sumator:=exec('sumator','#table',OFE,'NET','ZYSK');
DISP.SN_OFE:=_sumator[1];
DISP.SZ_OFE:=_sumator[2];
''


\sn_ofp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.00]
:: OPIS: Przed wyświetl dla pola DISP.SN_OFP
::----------------------------------------------------------------------------------------------------------------------
_sumator:=exec('sumator','#table',OFP,'WN','MKW');
DISP.SN_OFP:=_sumator[1];
DISP.SM_OFP:=_sumator[2];
''


\ofk_rekord
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja rekord dla tabeli OFK
::  OLD: \ofk_rekord/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(OFK,,'WN')


\ae_of_sa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po edycji samochodu w kosztach oferty
::  OLD: \ae_of_sa/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFK.POJAZD<>0
|| {? OFK.ST_KM>0 & OFK.IL_KM>0
   || OFK.WN:=OFK.ST_KM*OFK.IL_KM
   ?};
   OFK.OP:=OFK.POJAZD().NAZ
|| OFK.ST_KM:=0
?};
1


\ae_of_st
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po edycji stawki kilometrowej w kosztach oferty
::  OLD: \ae_of_st/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFK.ST_KM>0 & OFK.IL_KM>0
|| OFK.WN:=OFK.ST_KM*OFK.IL_KM
?};
1


\ae_of_km
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po edycji kilometrow w kosztach oferty
::  OLD: \ae_of_km/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFK.ST_KM>0 & OFK.IL_KM>0
|| OFK.WN:=OFK.ST_KM*OFK.IL_KM
?};
1


\chk_ofetr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja rekord po dla srodkow transportu z oferty
::  OLD: \chk_ofetr/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(OFETR,,'KOD');
{? _wyn=''
||
   _ref:=OFETR.ref();
   _kod:=OFETR.KOD;
   _ndx:=OFETR.ndx_tmp(,,'KOD',,,'KOD',,);
   OFETR.cntx_psh();
   OFETR.index(_ndx);
   OFETR.prefix(_kod,_kod);
   {? OFETR.first()
   ||
      {!
      |?
         {? OFETR.ref()<>_ref | -(1+menu_txt)='d'
         || FUN.info('Istnieje już środek transportu z kodem %1.'@[_kod]);
            _wyn:='KOD'
         ?};
         _wyn='' & OFETR.next()
      !}
   ?};
   OFETR.ndx_drop(_ndx);
   OFETR.cntx_pop()
?};
_wyn


\oferta_zakoncz_ofp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zakonczenie redakcji oferty - zmiana statusu
::   WE: [_a] - powołane z okienka=1, domyślnie nie=0
::   WY: klawisz zatwierdzenia
::----------------------------------------------------------------------------------------------------------------------
exec('btn_EndPosition','okienka','LSP_OFE_DOFE',OFE.uidref(),OFE
 ,'Zakończ_wer','Zakończyć rejestrację oferty %1?'@[OFE.SYM])


\oferta_pozycje_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Pozycje oferty - dla obszaru
::----------------------------------------------------------------------------------------------------------------------
BPMN.END:=0;
params_exec('ofe_poz','lsp')


\ofe_reko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja rekord dla oferty
::   WE: - 0 - kolejny rekord, 1 - ostatni rekord w dziedzinie
::  OLD: \ofe_reko/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
exec('dispzofe','zamsiw_wspolne');
_no_ok:=OFE.f_active() & OFE.sel_size();
_wer:={? (type_of(params_get())=117 & var_press('env_prj',params_get())=117) || 'WER_P' || 'WER' ?};
{? ~_no_ok & {? OFE.f_active() || OFE.f_get() || OFE.get() ?}
|| OFE.actions_grayed(_wer,'');
   _czywyc:={? OFE.A='Z'
             | DISP.ZAM_OFE<>''
             | OFE.STAT_REJ='T' & ~exec('chk_role','#b__box',OPERATOR.USER,'LSP_OFE_AAOF')
             | OFE.STAT_REJ='Z' & ~exec('chk_role','#b__box',OPERATOR.USER,'LSP_OFE_DOFE')
             | OFE.STAT_REJ='N'
            || 'W'
            || ''
            ?};
   _actions:={? OFE.A='Z'        || 'DPUOZAMN('+_czywyc+'LYQOXC):D'
             |? OFE.STAT_REJ='N' || 'MWAN('+_czywyc+'HO)'
             |? OFE.STAT_REJ='Z' || 'MZN('+_czywyc+'HO)'
             |? OFE.STAT_REJ='T' || 'UZAN('+_czywyc+'H)'
             || ''
             ?};
   OFE.actions_grayed(_wer,_actions)
?};
{? _a || exec('set_efld_opt','!lsp_ofe_dofe') ?};

Color.rekprzed('OFE#01')


\set_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Zaznacza wymagalne pola w nagłówku oferty
::   WE: _a - akronim okna redakcji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_red:={? var_pres('_a')=type_of('') || _a || OFE.win_edit('?') ?};

_czy_wal:=exec('spr_ww','eurusd',0);
_enwal:={? _czy_wal || 'enable=1' || 'enable=0' ?};
OFE.efld_opt(_win_red,_enwal,,'WAL');
_czy_wal:=OFE.WAL<>exec('bl_nwal','ustawienia');
_enwal:={? _czy_wal || 'enable=1' || 'enable=0' ?};
OFE.efld_opt(_win_red,_enwal,,'KRS');
OFE.efld_opt(_win_red,_enwal,,'DTK');
OFE.efld_opt(_win_red,_enwal,,'SWAL');
OFE.efld_opt(_win_red,_enwal,,'BTK');

:: Projekt
{? OFE.PROJEKTY=null() & (OFE.T().PROJZAKR='' | TYPYZAM.PROJZAKR=exec('projzakr_nie_dotyczy','projekty'))
|| OFE.efld_opt(_win_red,'enable=0',,'PROJEKTY')
|| OFE.efld_opt(_win_red,'enable=1',,'PROJEKTY')
?};

:: Wyłączanie akcji w oknach słownikowych
POJAZDY.actions('SLO_J','W:W');
''


\chk_ofpp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: akcja rekord po dla słownika prawdopodobieństwa przyjęcia oferty
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(OFPP,,'KOD');
{? _wyn=''
|| {? OFPP.PP<0 | OFPP.PP>100
   || FUN.info('Prawdopodobieństwo może mieć wartość od 0%% do 100%%.'@);
      _wyn:='PP'
   ?}
?};
{? _wyn=''
|| _ref:=OFPP.ref();
   _kod:=OFPP.KOD;
   _ndx:=OFPP.ndx_tmp(,,'KOD',,,'KOD',,);
   OFPP.cntx_psh();
   OFPP.index(_ndx);
   OFPP.prefix(_kod,_kod);
   {? OFPP.first()
   ||
      {!
      |?
         {? OFPP.ref()<>_ref | -(1+menu_txt())='d'
         || FUN.info('Istnieje już prawdopodobieństwo przyjęcia z kodem %1.'@[_kod]);
            _wyn:='KOD'
         ?};
         _wyn='' & OFPP.next()
      !}
   ?};
   OFPP.ndx_drop(_ndx);
   OFPP.cntx_pop()
?};
{? _wyn=''
|| _ref:=OFPP.ref();
   _pp:=OFPP.PP;
   _ndx:=OFPP.ndx_tmp(,,'PP',,);
   OFPP.cntx_psh();
   OFPP.index(_ndx);
   OFPP.prefix(_pp);
   {? OFPP.first()
   ||
      {!
      |?
         {? OFPP.ref()<>_ref | -(1+menu_txt())='d'
         || FUN.info('Istnieje już prawdopodobieństwo przyjęcia %1%%.'@[$_pp]);
            _wyn:='PP'
         ?};
         _wyn='' & OFPP.next()
      !}
   ?};
   OFPP.ndx_drop(_ndx);
   OFPP.cntx_pop()
?};
_wyn


\akt_cen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Aktualizacja cen
::----------------------------------------------------------------------------------------------------------------------
OFE.cntx_psh();
OFE.get();
_wal:=#OFE.WAL;
_krs:=OFE.KRS;
_rab:=OFE.RAB;
_ctrlCN:=exec('get','#params',800850,2);
_kh:=OFE.KH;
_odb:=OFE.ODB;
_wal:=OFE.WAL;
_pl:=OFE.PL;
OFE.cntx_pop();

TAZ.RAB_N_BE:=OFE.RAB;
HELP.PL:=OFE.PL;
HELP.PROJEKTY:=OFE.PROJEKTY;
HELP.KH_ODB:=OFE.ODB;

_ok:=1;
OFE.put(1);
OFP.index('OFE_P');
OFP.prefix(OFE.ref());
::Usunięcie powiązanego cennika jeśli nie jest już aktualny
{? OFP.first & (_kh<>OFE.KH | _odb<>OFE.ODB | _wal<>OFE.WAL | _pl<>OFE.PL)
||
   {!|?
      {? OFP.TAR_H<>null()
      || TAR_H.cntx_psh();
         TAR_H.use(ref_name(OFP.TAR_H));
         OFP.cntx_psh();
         _tar_h_tap:=$OFP.TAR_H().TAP;
         OFP.C:=0;
         OFP.CWAL:=0;
         VAR_DEL.delete('__TAP_LIST');
         exec('pr_cenaw','!lsp_ofe_dofe',2);
         exec('be_cenas','!lsp_ofe_dofe',2);
         {? var_pres('__TAP_LIST')=type_of('')
         & ( __TAP_LIST*_tar_h_tap)=0
         || OFP.cntx_pop();
            OFP.TAR_H:=null();
            OFP.TAR_TMS:=0;
            OFP.put()
         || OFP.cntx_pop()
         ?};
         VAR_DEL.delete('__TAP_LIST');
         TAR_H.cntx_pop()
      ?};
      OFP.next()
   !}
?};
{? OFP.first & (_kh<>OFE.KH | _odb<>OFE.ODB | _wal<>OFE.WAL | _pl<>OFE.PL)
& _ctrlCN<>'B' & (_ctrlCN='T' | exec('ceny_mat_dok','ceny_dok',2,,OFE,1))
||
   _ma_cennik:='N';
   _choice:=2;
   _result:=spli_str(exec('fap_czy_cennik','faktury_wspolne',_kh,_odb,_pl,_wal,OFP),' ');
   _ma_cennik:=_result[1];
   _choice:=#_result[2];

   {? _ma_cennik='N' & _choice<>2
   || _ok:=1
   || {? _choice=0
      || _choice:=FUN.choice('Zmieniły się kryteria cenników.\nNależy wyznaczyć ponownie ceny.'@,,
         'Wg &cenników'@,'Wg &dokumentu'@)
      |? _ma_cennik='N' & _choice=2
      || _msg:='Zmieniły się kryteria cenników.\nBrak cennika zgodnego z aktualnymi kryteriami.'@;
         FUN.info(_msg)
      ?};
      _ok:=exec('ceny_mat_dok','ceny_dok',1,,OFE,1,,_choice=2,_wal<>OFE.WAL)
   ?}
?};
_new:=OFE.ref();
{? _ok=0
|| ~~
|? OFE.put()
||
   OFE.memo_put(,'OPIS');
   {? _wal<>#OFE.WAL | _krs<>OFE.KRS
   ||
      OFP.index('OFE_P');
      OFP.prefix(OFE.ref());
      {? OFP.first()
      ||
         {!
         |?
            OFP.WAL:=OFE.WAL;
            OFP.KRS:=OFE.KRS;
            _walj:=exec('FindInSet','#table','WAL','WAL_SYM',OFP.WAL().KOD,,"WAL.J",,,1);
            {? OFE.WAL=exec('bl_nwal','ustawienia')
            || OFP.CWAL:=0
            |? _wal<>#OFE.WAL & OFP.CWAL=0 & OFP.KRS>0
            || OFP.CWAL:=(OFP.C/OFP.KRS*_walj)$OFP.M().DOKL_S;
               OFP.C:=(OFP.CWAL*OFP.KRS/_walj)$OFP.M().DOKL_S
            |? OFP.WAL<>exec('bl_nwal','ustawienia')
            || OFP.C:=(OFP.CWAL*OFP.KRS/_walj)$OFP.M().DOKL_S
            ?};
            exec('ae_ofpww','ceny_dok',1);
            OFP.put();
            OFP.next()
         !}
      ?}
   |? _rab<>OFE.RAB
   ||
      OFP.index('OFE_P');
      OFP.prefix(OFE.ref());
      {? OFP.first()
      ||
         {!
         |?
            exec('ae_ofpww','ceny_dok',1);
            OFP.put();
            OFP.next()
         !}
      ?}
   ?};
::          aktualizacja stawki VAT wg historii
   OFP.index('OFE_P');
   OFP.prefix(OFE.ref);
   {? OFP.first
   || _akt:=2;
      {!
      |?
         _sv:=exec('m_vat','material',OFP.M,OFE.KH,,OFE.DU~1,OFE.DU~2,OFP.SV,2);
         {? _sv<>OFP.SV
         ||
            {? _akt=2 || _akt:=FUN.ask('Aktualizować stawkę VAT w pozycjach oferty?'@) ?};
            {? _akt
            ||
               OFP.SV:=_sv;
               exec('ae_ofpww','ceny_dok',1);
               OFP.put
            ?}
         ?};
         _akt & OFP.next
      !}
   ?};
::          aktualizacja projektów
   {? OFE.PROJEKTY
   || OFP.index('OFE_P');
      OFP.prefix(OFE.ref);
      {? OFP.first()
      || {!
         |?
            OFP.PROJEKTY:=OFE.PROJEKTY;
            OFP.put();
            OFP.next()
         !}
      ?}
   ?};
:: aktualizacja kontrahenta, odbiorcy i jm na pozycjach
   OFP.index('OFE_P');
   OFP.prefix(OFE.ref);
   {? OFP.first()
   || {!
      |?
         OFP.KH_N:=OFE.KH;
         OFP.KH_ODB:=OFE.ODB;
         M.cntx_psh();
         OFP.JM:=OFP.M().J;
         M.cntx_pop();
         OFP.put();
         OFP.next()
      !}
   ?}
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 1067b2c21ec396646b53bfd491c5d939743dff3020447e72956c7e9e93e791b5858ee98738f5a093d441f46b9a7432608ddc6f133df42e30aeb2734c6e7968943212383a0cc5ede4fa0769723966f43b5c160eb871341d38f855b228c2cdef9e2bcbf0f80ccd619ac9bae6ba7737e5082d8ac6fab7486551063f61ef8a31c6a6

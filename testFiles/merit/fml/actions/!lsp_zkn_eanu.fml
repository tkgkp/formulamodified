:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_zkn_eanu.fml
:: Utworzony: 17.10.2017
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa anulowania zamówienia sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# properties=SERVICE
::# parses=exec('parses','!lsp_zkn_eanu')
::# kind=WE,   symbol=ZK_N,      type=_ZK_N,    name=Zamówienie sprzedaży,    required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

exec('init_zkn','lsp');

:: WSTĘPNE WALIDACJE
_clean_result:=params_exec('clean','!lsp_zkn_eanu',_mp,_in);
_can_continue:=_clean_result.RESULT;
{? _can_continue=0
|| return()
?};

_akcja:=_mp.akcja();
_auto:=_akcja<>'Anuluj' & (_mp.isAutoRun() | _mp.isService());

{? ~(var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N)
||
   _mp.error('Brak wymaganego parametru ZK_N.')
||
   ZK_N.cntx_psh();
   ZK_N.prefix();
   {? ~ZK_N.seek(_in.ZK_N)
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ||
      {? _r_lock:=exec('r_lock_one','#table',ZK_N,ZK_N.ref())
      ||
         params_set('mp',_mp);
         {? _akcja='Anuluj' | _auto
         ||
            exec('anuluj','!lsp_zkn_eanu',_auto)

         |? _mp.pathTodo()
            | _mp.pathArea() & _akcja=''
         ||
            _win_akr:='RED';
            _win_red:=ZK_N.mk_edit('Zamówienie sprzedaży'@,,'zkn_redanu');
            ZK_N.win_ewin(_win_red,,_win_akr);
            _ff:="params_exec('zkn_pozycje_todo','!lsp_zkn_eanu')";
            ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
            _ff:="params_exec('zkn_anuluj_todo','!lsp_zkn_eanu'); 'key:Esc'";
            ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Anuluj &dokument'@],_ff);
            _ff:="'key:Esc'";
            ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
            ZK_N.win_edit(_win_red);
            exec('set_efld_opt','zamsiw_nag',_win_red);
::          Dalsza obsługa anulowania dokumentu w exec('anuluj','!lsp_zkn_eanu')
            ZK_N.display();
::          Usunięcie definicji tymczasowych okien
            ZK_N.win_edit(''); ZK_N.win_edel(_win_red)
         ||
            _mp.error('Nieobsługiwana ścieżka.')
         ?};
         exec('r_unlock_one','#table',ZK_N,ZK_N.ref(),_r_lock)
      ||
         exec('who_rlock_zkn','zamsiw_nag')
      ?}
   ?};
   ZK_N.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N
|| 'Anuluj zamówienie sprzedaży: %1'@@[exec('record','#to_string',_in.ZK_N)]
|| ''
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N
|| ZK_N.cntx_psh();
   ZK_N.use(ref_name(_in.ZK_N));
   ZK_N.prefix();
   {? ZK_N.seek(_in.ZK_N)
   || __PARSES.setVal('OddzialLogProd',ZK_N.ODDZ)
   ?};
   ZK_N.cntx_pop()
?};
1


\zkn_anuluj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Zamówienie sprzedaży - Anuluj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_ZKN_EANU';
_params.UIDREF:=ZK_N.uidref();
_params.AKCJA:='Anuluj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZK_N',ZK_N.ref());

exec('mp_run','#b__box',_params)


\zkn_anuluj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Dokument sprzedaży - Anuluj z todo
::   WE: params_get()   - ustawiane w exec('main','!lsp_zkn_eanu')
::----------------------------------------------------------------------------------------------------------------------
params_exec('anuluj','!lsp_zkn_eanu');
''


\zkn_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Zamówienie sprzedaży - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
BPMN.END:=0;
exec('zam_poz','zamsiw_poz',1);

''


\anuluj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.02]
:: OPIS: Zamówienie sprzedaży - Anulowanie dokumentu
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       params_get()   - ustawiane w exec('main','!lsp_zkn_eanu')
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_mp:=params_get().mp;

_result:=0;

{? ~exec('czy_z_ok','okresy',-2)
|| _result:=1

|? ZK_N.STAT_REJ='A'
|| {? ~_auto || FUN.info('Zamówienie zostało anulowane.'@) ?};
   _mp.done();
   _result:=1

|? ZK_N.STAT_REJ='N'
|| _msg:='Nie zakończono jeszcze rejestracji zamówienia.\nAnulowanie niemożliwe.'@@;
   FUN.info(_msg);
   _mp.error(_msg);
   _result:=1

|? exec('kontrol_zl','zamsiw_nag')
|| _msg:='Do jednej z pozycji zamówienia są już wystawione zlecenia.\nAnulowanie niemożliwe.'@@;
   FUN.info(_msg);
   _mp.error(_msg);
   _result:=1

|? exec('kontrol_res','zamsiw_nag')
|| _msg:='Jedna z pozycji zamówienia znajduje się w planie zasobów.\nAnulowanie niemożliwe.'@@;
   FUN.info(_msg);
   _mp.error(_msg);
   _result:=1

|? exec('kontrol_px','zamsiw_nag')
|| _msg:='Do jednej z pozycji zamówienia jest przypisany aktywny obiekt planistyczny.\nAnulowanie niemożliwe.'@@;
   FUN.info(_msg);
   _mp.error(_msg);
   _result:=1

|? ZK_N.OBI_POW<>'' & exec('edokum_status','obiegi_log',ZK_N.OBI_POW)=''
|| _msg:='Nie zakończono jeszcze obiegu zamówienia.\nAnulowanie niemożliwe.'@@;
   FUN.info(_msg);
   _mp.error(_msg);
   _result:=0

|? ZK_N.T().VALACC<>null() & ~exec('validate','wzorce',ZK_N.T().VALACC,ZK_N,ZK_N.ref())
|| _result:=0

|? _auto | FUN.ask('Anulować zamówienie %1?\nOperacja nieodwracalna.'@[ZK_N.SYM])
|| {? exec('zkn_anuluj','zamsiw_nag')
   || _mp.done();
      _result:=1
   ?}
?};
_result


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zam_clean','zamsiw_nag',_mp,_in)


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 965828f945ca48d26e03b5a95dacea7118a09d8ef6821156329d9e043547fad9eaf5bbb1a31f04cd650e322c660b33d7c3033eb9e251ea0550c1efb2fabd18ae2d41695de086b0b21b22ca4f1a6be5f786b6d4fb53bdcc0c3d05bd72825a122df11069beb26e76bc9ddee967ea86c97c390edbf8d4f0d11ce5523084b6aaf451

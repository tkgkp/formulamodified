:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pba_bpb_wdan.fml
:: Utworzony: 30.08.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Obsługa czynności PBA_BPB_WDAN - Wybór ankiety
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Wybór ankiety - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Parametr wejściowy ON_ESC określa sposób czynności przy braku wyboru.
:: Parametr może przyjmować wartości:
::    DONE     - Czynność zostanie zakończona, a parametry wyjściowe przyjmą wartości puste [DOMYŚLNIE].
::    KEEP     - Czynność zostanie odłożona na listę zadań aby użytkownik mógł do niej wrócić. Oznacza to, że czynności
::               nie da się zakończyć bez wyboru badania.
::    CANCEL   - Jeśli czynność jest pierwszą w procesie to po zakończeniu formuły głównej instancja czynności zostanie
::               usunięta. Jeśli czynność jest kolejną w procesie to otrzyma status oczekująca i pozostanie na liście
::               zadań - czyli tak jak dla KEEP.
::    ERROR    - Czynność jest kończona a proces zatrzymywany z ustawioną flagą błędu.
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy rezygnacji, required=N, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'DONE' || _a ?})"
::
::# kind=WE, symbol=ZA_INST, type=_ZA_INST, name=Wskazanie badania ankietowego, required=N, keyref=T
::# kind=WE, symbol=ZO_PROC, type=_ZO_PROC, name=Wskazanie sesji badania ankietowego, required=N, keyref=T
::
::# kind=WY, symbol=ZA_ZEST, type=_ZA_ZEST, name=Wskazanie definicji ankiety, required=N

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error(exec('error','!pba_bpb_wdan'));
   return()
?};

_ref:=null();
_id:=
   {? _in.ZA_INST<>~~
   || _ref:=_in.ZA_INST;
      exec('ref2uid','#table',_in.ZA_INST)
   || _ref:=_in.ZO_PROC;
      exec('ref2uid','#table',_in.ZO_PROC)
   ?};

_result:='';

{? _id=''
|| _result:=exec('error','!pba_bpb_wdan')

|? _mp.pathProc() | _mp.pathTodo()
|| _out:=_par.out;

   _ref:=exec('select','!pba_bpb_wdan',_ref);

   _out.ZA_ZEST:=_ref;

   {? _ref<>null
   || _mp.save(,_out);
      _mp.done()

   |? _in.ON_ESC='KEEP'
   || _mp.keep()

   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()

   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')

   || _mp.save(,_out);
      _mp.done()
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg('%1'@[_result])
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Wybór ankiety - formuła opisu zadania.
::   WE:
::   WY: tekst do wyświetlenia na liście zadań
::----------------------------------------------------------------------------------------------------------------------
'Wybór definicji ankiety'@@


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Wybór definicji ankiety niemożliwy.\nNie znaleziono badania ankietowego.'@


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Wybór definicji ankiety właściwych dla badania.
::   WE: _a [_ZA_INST] - wskazanie jednorazowego badania ankietowego/sesji badań ankietowych
::   WY: wskazanie definicji ankiety lub null w przypadku rezygnacji
::----------------------------------------------------------------------------------------------------------------------
_ret:=null;
{? ref_tab(_a)=ZA_INST || _tab:=ZA_INST || _tab:=ZO_PROC ?};
_tab.cntx_psh();
_tab.prefix();
{? _tab.seek(_a)
|| ZA_ZEST.clear();
   ZA_ZEST.f_set(
      'NAZWA',
      'join SLO_KOD using(ZA_ZEST.SLO_KOD,SLO_KOD.REFERENCE)',
      'ZA_ZEST.ZZ_LINK=\':_a\' and SLO_KOD.KOD=\'U\'',
   $_tab.ZZ_DOK);
   ZA_ZEST.win_sel('WYB');
   {? _tab=ZA_INST
   || ZA_ZEST.hdr_sel('Ankiety badania "%1"'@[ZA_INST.NAZWA])
   || ZA_ZEST.hdr_sel('Ankiety sesji badań ankietowych')
   ?};
   {? ZA_ZEST.select()
   || _ret:=ZA_ZEST.ref()
   ?};
   ZA_ZEST.f_clear()
?};
_tab.cntx_pop();
_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:16 a8d034edcbfa6401c577772d538dc49d37dac0e998173c7bf94b86fbad59f0923fb4deac33a5296f134f47832b14a72e7cd85d775a423dcf59001c8eb4127088ceda4d06f01bf881dcf97af1916c56fac444ace08933a653362c271caf6b21b71900c0e95eaecefa14fb803cf80bc6d51df0739cf56fd2905a22a3b6850a58eb

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orwp.fml
:: Utworzony: 17.03.2022
:: Autor: DG
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORWP - Rejestracja wniosków dotyczących podatków.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Przeglądanie informacji o emeryturach i rentach - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
~~


\os_zwzal_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła sprawdzająca poprawność wiersza w tabeli ze zwolnieniami od podatku.
::   WE: [_a] [STRING] - Znacznik operacji (D-dołącz/P-popraw, domyślnie 'P')
::   WY:
::  OLD: \os_zwzal_ar/kali.fml
::  OLD: \os_zwzal_ar/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
_action:={? var_pres('_a')=type_of('') || _a || 'P' ?};

OS_ZWSLO.cntx_psh();
_roczne:=(OS_ZWZAL.OS_ZWSLO().ROCZNE='T');
OS_ZWSLO.cntx_pop();
_result:=__CHK.record2(OS_ZWZAL,'OS_ZWSLO',,{? _roczne || 'ROK' || '' ?},,'D_ZO',,'D_OB');

{? _result='' & _roczne
|| _d_ob:=date(OS_ZWZAL.ROK,1,1);
   {? OS_ZWZAL.ROK<2022
   || FUN.emsg('Wartość w polu "Rok" nie może być mniejsza niż 2022.'@);
      _result:='ROK'
   |? OS_ZWZAL.D_ZO~1>OS_ZWZAL.ROK
   || FUN.emsg('Data w polu "Data złożenia" nie może być późniejsza od daty %1.'@[date(OS_ZWZAL.ROK,12,0)$1]);
      _result:='D_ZO'
   |? OS_ZWZAL.D_OB<_d_ob
   || FUN.emsg('Data w polu "Data stosowania" nie może być wcześniejsza od daty %1.'@[_d_ob$1]);
      _result:='D_OB'
   |? OS_ZWZAL.D_OB~1>OS_ZWZAL.ROK
   || FUN.emsg('Data w polu "Data stosowania" nie może być późniejsza od daty %1.'@[date(OS_ZWZAL.ROK,12,0)$1]);
      _result:='D_OB'
   ?}
?};
{? _result=''
|| {? OS_ZWZAL.D_ZO>OS_ZWZAL.D_OB
   || FUN.emsg('Data w polu "Data stosowania" nie może być wcześniejsza od daty w polu "Data złożenia".'@);
      _result:='D_OB'
   |? exec('os_zwzal_exist','!pkd_ezk_orwp',_action)
   || _txt:='W systemie istnieje już zapis dla podanej wartości pola "Data stosowania" i rodzaju wniosku.'@;
      {? ~FUN.ask('%1\n%2'[_txt,'Czy kontynuować?'@])
      || _result:='D_OB'
      ?}
   ?}
?};
:: Kontrola wycofania w stosunku do wycofywanego:
{? _result='' & OS_ZWZAL.WYCOF='T'
|| _dZoOjca:=_dObOjca:=date(0,0,0);
   OS_ZWZAL.cntx_psh();
   {? OS_ZWZAL.seek(OS_ZWZAL.DOK_WYC,,1)
   || _dZoOjca:=OS_ZWZAL.D_ZO;
      _dObOjca:=OS_ZWZAL.D_OB
   ?};
   OS_ZWZAL.cntx_pop();
   {? OS_ZWZAL.D_ZO<=_dZoOjca
   || FUN.emsg('Data złożenia wycofania nie może być mniejsza od daty złożenia wycofywanego dokumentu.'@);
      _result:='D_ZO'
   |? OS_ZWZAL.D_OB<=_dObOjca
   || FUN.emsg('Data stosowania wycofania nie może być mniejsza od daty stosowania wycofywanego dokumentu.'@);
      _result:='D_OB'
   ?}
?};

_result


\os_zwzal_bfd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła przed dołączeniem wiersza tabeli OS_ZWZAL.
::   WE:
::   WY:
::  OLD: \os_zwzal_bd/kali.fml
::  OLD: \os_zwzal_bfd/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
OS_ZWZAL.blank();
OS_ZWSLO.fld_fml('TYP','BLANK',"'W'");
exec('os_zwzal_efld_opt','!pkd_ezk_orwp','*',OS_ZWZAL,OS_ZWZAL.win_edit('?'));
{? OS_ZWZAL.edit("exec('os_zwzal_ar','!pkd_ezk_orwp','D')")
|| {? ~OS_ZWZAL.add(1)
   || FUN.emsg('Nie można dodać nowego zapisu.'@)
   || {? OS_ZWZAL.D_OB>=date(2023,1,1)
      || exec('grupy_przychodow','rodzaje_platnikow','OS_ZWZAL',1)
      ?};
      win_set('cur_row_pos=-1')
   ?}
?};
OS_ZWSLO.fld_fml('TYP','BLANK',"*")


\os_zwzal_bfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła na popraw wiersza tabeli OS_ZWZAL.
::   WE:
::   WY:
::  OLD: \os_zwzal_bp/kali.fml
::  OLD: \os_zwzal_bfp/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
{? OS_ZWZAL.WYCOFANY='T'
|| FUN.info('Dokument posiada powiązane wycofanie, zostanie wyświetlony do podglądu.'@);
   OS_ZWZAL.display();
   return(0)
?};
OS_ZWSLO.fld_fml('TYP','BLANK',"'W'");
exec('os_zwzal_efld_opt','!pkd_ezk_orwp','*',OS_ZWZAL,OS_ZWZAL.win_edit('?'));
{? OS_ZWZAL.edit("exec('os_zwzal_ar','!pkd_ezk_orwp','P')")
|| {? ~OS_ZWZAL.put(1)
   || FUN.emsg('Nie można zmodyfikować zapisu.'@)
   || win_set('cur_row_pos=-1')
   ?}
?};
OS_ZWSLO.fld_fml('TYP','BLANK',"*")


\os_zwzal_bfz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Załączniki do wniosków dotyczących podatków
::   WE:
::   WY:
::  OLD: \os_zwzal_bfz/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','OS_ZWZAL',exec('chk_role','#b__box',OPERATOR.USER,'PKD_EZK_ORWP'))


\os_zwzal_bfw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła Okienko przed dla tabeli OS_ZWZAL.
::   WE:
::   WY:
::  OLD: \os_zw_bw/kali.fml
::  OLD: \os_zw_bfw/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
win_set('cur_row_pos=-1')


\os_zwzal_d_ob_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła przed redakcją pola D_OB z tabeli OS_ZWZAL.
::   WE:
::   WY:
::  OLD: \os_zz_do_bl/kali.fml
::  OLD: \os_zwzal_d_ob_bl/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
date()


\os_zwzal_slo_kod_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Po redagowaniu pola SLO_KOD tabeli OS_ZWZAL.
::   WE:
::   WY:
::  OLD: \os_zwzal_slo_ae/kali.fml
::  OLD: \os_zwzal_slo_kod_ae/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
{? OS_ZWZAL.ROK=0
|| OS_ZWZAL.ROK:=date()~1
?};
{? OS_ZWZAL.D_ZO=date(0,0,0)
|| OS_ZWZAL.D_ZO:=date()
?};
{? OS_ZWZAL.D_OB=date(0,0,0)
|| OS_ZWZAL.D_OB:=OS_ZWZAL.D_ZO
?};

1


\os_zwzal_slo_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Po redagowaniu pola OS_ZWSLO tabeli OS_ZWZAL.
::   WE:
::   WY:
::  OLD: \os_zwzal_slo_ae/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
:: Data złożenia:
{? OS_ZWZAL.D_ZO=date(0,0,0)
|| OS_ZWZAL.D_ZO:=date()
?};
:: Data stosowania:
{? OS_ZWZAL.D_OB=date(0,0,0)
|| OS_ZWZAL.D_OB:=OS_ZWZAL.D_ZO
?};

exec('os_zwzal_efld_opt','!pkd_ezk_orwp');
_dtUst:=date(2022,7,1);
OS_ZWSLO.cntx_psh();
OS_ZWZAL.OS_ZWSLO();
{? OS_ZWSLO.ROCZNE='N' & ~(-menu_txt()='popraw' & OS_ZWSLO.KOD='UZ_POD' & OS_ZWZAL.ROK<>0 & OS_ZWZAL.ROK<2022)
|| OS_ZWZAL.ROK:=0
|? OS_ZWZAL.ROK=0
|| OS_ZWZAL.ROK:=date()~1
?};
{? OS_ZWZAL.D_OB>=_dtUst & ',TP_ZAL_P,TP_ZAL_Z,KL_SRED,'*',%1,'[OS_ZWSLO.KOD]
|| _ret:=FUN.ask('Wniosek "%1"\nobowiązywał przed %2.\nCzy kontynuować?'@[OS_ZWSLO.RODZAJ,_dtUst$2])
?};
OS_ZWSLO.cntx_pop();

_ret


\os_zwzal_exist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.37]
:: OPIS: Formuła sprawdzająca unikalność pozycji w tabeli wniosków dotyczących podatków.
::   WE: [_a] [STRING] - Znacznik operacji (D-dołącz/P-popraw, domyślnie 'P')
::   WY:
::  OLD: \os_zwpoz_exist/kali.fml
::  OLD: \os_zwzal_exist/!pkd_dos_prus.fml
::----------------------------------------------------------------------------------------------------------------------
_action:={? var_pres('_a')=type_of('')    || _a || 'P' ?};
_rodz:=OS_ZWZAL.OS_ZWSLO;
_rodz:=OS_ZWZAL.SLO_KOD;
_rok:=OS_ZWZAL.ROK;
_dtS:=OS_ZWZAL.D_OB;
_ref:=OS_ZWZAL.ref;
_osoba:=OS_ZWZAL.OSOBA;
_firma:=exec('ref_firma','ustawienia');

_err:=0;
OS_ZWZAL.cntx_psh();
OS_ZWZAL.index('OS_RSAD');
OS_ZWZAL.prefix(_firma,_osoba,_dtS,_rodz);
{? OS_ZWZAL.first()
|| {!
   |? {? _action='D' | OS_ZWZAL.ref()<>_ref
      || _err+=1
      ?};
      OS_ZWZAL.next() & ~_err
   !}
?};
OS_ZWZAL.cntx_pop();
_err


\os_zwzal_os_zwslo_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed redagowaniem pola OS_ZWZAL.OS_ZWSLO
::   WE:
::   WY: 0/1
::  OLD: \os_zwzal_os_zwslo_be/kali.fml
::----------------------------------------------------------------------------------------------------------------------
1


\os_zwzal_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli OS_ZWZAL.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE:  _a  [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::  OLD: \os_zwzal_efld_opt/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(OS_ZWZAL)
|| _TAB:=_b
|? _tryb='1'
|| _TAB:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

_set:=exec('efld_opt','#window',,_we);

{? _fld='' | _fld='OS_ZWSLO'
|| OS_ZWSLO.cntx_psh();
   _val:=OS_ZWZAL.OS_ZWSLO().ROCZNE='T';
   OS_ZWSLO.cntx_pop();
   _set(_TAB,_we,,'enable=%1'[$_val],_TAB,'ROK')
?};

{? _fld=''
|| _val:=OS_ZWZAL.WYCOF='N';
   _set(_TAB,_we,,'editable=%1'[$_val],_TAB,'ROK');
   _set(_TAB,_we,,'editable=%1'[$_val],_TAB,'OS_ZWSLO');
   _set(_TAB,_we,,'editable=%1'[$_val],_TAB,'AKTYWNY');
   _set(_TAB,_we,,'editable=grayed',_TAB,'WYCOF');
   _set(_TAB,_we,,'editable=grayed',_TAB,'WYCOFANY');
   _set(_TAB,_we,,'editable=grayed',EDIT_VAR,'RP')
?};

1


\os_zwzal_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "Wyświetl" tabeli OS_ZWZAL
::   WE:
::   WY:
::  OLD: \os_zwzal_bw/kali.fml
::----------------------------------------------------------------------------------------------------------------------
exec('os_zwzal_efld_opt','!pkd_ezk_orwp','*',OS_ZWZAL,OS_ZWZAL.win_edit('?'));
OS_ZWZAL.display()


\os_zwzal_bgrprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "rodzaje płaTników" tabeli OS_ZWZAL
::   WE:
::   WY:
::  OLD: \os_zwzal_bgrprz/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_redakcja:=exec('chk_role','#b__box',OPERATOR.USER,'PKD_EZK_ORWP');
exec('grupy_przychodow','rodzaje_platnikow','OS_ZWZAL',,_redakcja)


\os_zwzal_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Wyzwalacz przed usunięciem wiersza tabeli OS_ZWZAL.
::   WE:
::   WY: 0/1 - wiersz może/nie może być usunięty
::  OLD: \os_zwzal_delb/kali.fml
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',OS_GRPRZ,'OS_ZWZAL',OS_ZWZAL.ref())


\os_zwzal_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "wYcofaj" tabeli OS_ZWZAL
::   WE:
::   WY:
::  OLD: \os_zwzal_wycofaj/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_err:='';
{? OS_ZWZAL.WYCOFANY='T'
|| _err:='Dokument został już wycofany.'@
|? OS_ZWZAL.WYCOF='T'
|| _err:='Nie można wycofać wycofania.'@
|? OS_ZWZAL.AKTYWNY='N'
|| _err:='Nie można wycofać nieaktywnego dokumentu.'@
?};
{? +_err
|| FUN.info(_err);
   return(0)
?};
_refOjca:=OS_ZWZAL.ref();
OS_ZWZAL.DOK_WYC:=#_refOjca;
OS_ZWZAL.WYCOF:='T';
_dzis:=date();
{? OS_ZWZAL.D_ZO>=_dzis
|| OS_ZWZAL.D_ZO+=1
|| OS_ZWZAL.D_ZO:=_dzis
?};
{? OS_ZWZAL.D_OB>=_dzis
|| OS_ZWZAL.D_OB+=1
|| OS_ZWZAL.D_OB:=_dzis
?};
OS_ZWZAL.win_edit('RED');
exec('os_zwzal_efld_opt','!pkd_ezk_orwp','*',OS_ZWZAL,OS_ZWZAL.win_edit('?'));
{? OS_ZWZAL.edit("exec('os_zwzal_ar','!pkd_ezk_orwp','D')")
|| {? ~OS_ZWZAL.add(1)
   || FUN.emsg('Nie można dodać nowego zapisu.'@)
   || OS_ZWZAL.cntx_psh();
      {? OS_ZWZAL.seek(_refOjca,,1)
      || OS_ZWZAL.WYCOFANY:='T';
         OS_ZWZAL.put()
      ?};
      OS_ZWZAL.cntx_pop();
      win_set('cur_row_pos=-1')
   ?}
?}


\os_zwzal_dokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "dokumEnt powiązany" tabeli OS_ZWZAL
::   WE:
::   WY:
::  OLD: \os_zwzal_dokument/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_err:='';
{? OS_ZWZAL.WYCOF='N' & OS_ZWZAL.WYCOFANY='N'
|| _err:='Brak dokumentu wycofanego lub wycofującego.'@
?};
{? +_err
|| FUN.info(_err);
   return(0)
?};
OS_ZWZAL.cntx_psh();
{? OS_ZWZAL.WYCOF='T'
|| {? OS_ZWZAL.seek(OS_ZWZAL.DOK_WYC,,1)
   || OS_ZWZAL.hdr_edit(' - dokument wycofany'@);
      OS_ZWZAL.display()
   || _err:='Brak dokumentu wycofanego.'@
   ?}
|? OS_ZWZAL.WYCOFANY='T'
|| OS_ZWZAL.index('DOK_WYC');
   OS_ZWZAL.prefix(exec('ref_firma','ustawienia'),#OS_ZWZAL.ref());
   {? OS_ZWZAL.first()
   || OS_ZWZAL.hdr_edit(' - dokument wycofujący'@);
      OS_ZWZAL.display()
   || _err:='Brak dokumentu wycofującego.'@
   ?}
?};
OS_ZWZAL.cntx_pop();
{? +_err
|| FUN.info(_err);
   return(0)
?};
~~


\os_zwzal_bu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Formuła dla akcji Usuń - przed z tabeli OS_ZWZAL
::   WE:
::   WY:
::  OLD: \os_zwzal_bu/kali.fml
::----------------------------------------------------------------------------------------------------------------------
{? OS_ZWZAL.WYCOFANY='T'
|| FUN.info('Dokument posiada powiązane wycofanie. Usunięcie niemożliwe.'@);
   return(0)
|? OS_ZWZAL.WYCOF='T'
|| _refOjca:=OS_ZWZAL.DOK_WYC;
   {? FUN.ask('Czy usunąć bieżący wiersz?'@) & OS_ZWZAL.del()
   || OS_ZWZAL.cntx_psh();
      {? OS_ZWZAL.seek(_refOjca,,1)
      || OS_ZWZAL.WYCOFANY:='N';
         OS_ZWZAL.put()
      ?};
      OS_ZWZAL.cntx_pop()
   ?}
|? OS_ZWZAL.WYCOF<>'T' & exec('zwzal_has_zwpoz','!pkd_ezk_orwp')
|| {? FUN.ask('Wybrany wniosek mógł już zostać wykorzystany do rozliczenia.'+
              '\nCzy na pewno usunąć?'@)
   || OS_ZWZAL.del()
   ?}
|| {? FUN.ask('Czy usunąć bieżący wiersz?'@)
   || OS_ZWZAL.del()
   ?}
?}


\zwzal_has_zwpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Sprawdza, czy informacje z tabeli OS_ZWZAL zostały wykorzystane do rozliczenia w OS_ZWPOZ.
::   WE:
::   WY:
::  OLD: \zwzal_has_zwpoz/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_firma:=exec('ref_firma','ustawienia');
OS_ZWPOZ.cntx_psh();
OS_ZWPOZ.index('OS_RWRM');
OS_ZWPOZ.prefix(_firma,OS_ZWZAL.OSOBA,OS_ZWZAL.OS_ZWSLO,'N');
_rok:={? OS_ZWZAL.ROK || OS_ZWZAL.ROK || OS_ZWZAL.D_OB~1 ?};
{? OS_ZWPOZ.find_ge(_rok)
|| {!
   |? _dtPoz:=date(OS_ZWPOZ.ROK,OS_ZWPOZ.MC,1);
      {? _dtPoz>=OS_ZWZAL.D_OB
      || _result:=1
      ?};
      ~_result & OS_ZWPOZ.next()
   !}
?};
OS_ZWPOZ.cntx_pop();

_result


\os_zwzal_br
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: "Rekord - przed" dla okienek wertowania tabeli OS_ZWZAL
::UWAGA: Nazwa formuły ma końcówkę _br niezgodną ze standardem z uwagi na istnienie formuły z końcówką _bd
::   WE: _a [INTEGER] - 0/1 czy rekord, dla którego wywołana jest formuła, jest ostatnim odrysowywanym rekordem
::   WY: ~~
::  OLD: \os_zwzal_br/kali.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| OS_ZWZAL.actions_grayed(OS_ZWZAL.win_sel('?'),{? OS_ZWZAL.WYCOF='T' || 'CT' || '' ?})
?};
~~


:Sign Version 2.0 jowisz:1045 2023/11/15 14:14:34 3f40a45a43ceb1049a057bd215bbb81c23cbf0823cc1dfbd982fbea9782afe8ebf89e8616eae7e9e2c4899c34faf83e2087702d3846143fc66d5d11ebf6aed1c887cb0093c1de75bfd07903843d934edf61228be631b36a67847508b2ea925c1b56172e9e341605abcb1bdda62e8a86f5e73a69dc166749ea612bf0610d11faf

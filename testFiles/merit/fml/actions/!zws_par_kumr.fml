:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_kumr.fml
:: Utworzony: 20.03.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_KUMR - Rejestracja umów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Rejestracja umów - główna formuła czynności ZWS_PAR_KUMR.
::----------------------------------------------------------------------------------------------------------------------
:: PARAMETRY WE:
:: PARAMETRY WY:
::# kind=WY, symbol=UDT,   type=_UDT, name=Wskazanie na umowę, required=T
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_dialog:={? _mp.isService() || 0 || 1 ?};
SSTALE.AO:=__PARSES.getVal('OkresRodzaj').OKRO_F;
SSTALE.AR:=SSTALE.AO().ROK;
_ref:=null;
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0 & ref_tab(_kref)=UDT & UDT.seek(_kref)
      || _ref:=UDT.ref()
      ?}
   !}
?};

{? _akcja='Dołącz' | _mp.pathProc() | _mp.pathTodo() & _ref=null
|| exec('udt_edit','!zws_par_kumr',1,_wy,_mp,_dialog)
|? _akcja='Popraw' | _mp.pathTodo() & _ref<>null
|| exec('udt_edit','!zws_par_kumr',0,_wy,_mp,_dialog)
|? _akcja='Usuń'
|| exec('udt_del','!zws_par_kumr',_dialog)
?}


\udt_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Edycja umów
::   WE: _a - Dołączanie? 1-tak 0-nie
::       _b - wyjście czynności
::       _c - manager procesu
::       _d - z dialogiem
::----------------------------------------------------------------------------------------------------------------------
POM.TAB:='UDT';
POM.TYPDOK:='UDT';
POM.NRT:=0;
{? ~_d || _wsenv:=exec('wsenv','#mwapi') ?};
{? ~exec('is_nrdok','numery')
||
::{? ~_d || _wsenv.add_error('Brak ustawienia numeracji typu %1.'@[POM.TYPDOK]) ?};
   return()
?};
_wy:=_b;
_mp:=_c;
{? _d
|| exec('set_fml','#field',UD_POM,'JORG',
      "UD_POM.JORG:=UDT.JORG().SYMBOL; ~~",
      "exec('domyslny','schemat','PODZORG')<>null",
      "exec('ud_def_symbol_f3','schemat','PODZORG')",
      "  {? fld()='' || UDT.JORG:=null; return(1) ?};
         _ref:=exec('ud_def_symbol_ae','schemat',UD_SCH.ref());
         {? _ref
         || UDT.JORG:=_ref;
            fld(UDT.JORG().SYMBOL)
         ?}
      "
   );
   KH.win_dict('WER_ROZ');
   KH.actions('WER_ROZ','R');
   UDT.win_edit(exec('udt_win_red','!zws_par_kumr',_a=1,_mp.pathTodo()))
?};
{? _a=1
|| _old_ref:=UDT.ref();
   UdtNr:=1;
   {? _d || UDT.blank() ?};
   UDT.SYM:=exec('symbol','numery','UDT');
   VAR_DEL.delete('UdtNr');
   UDT.memo_set(,'MEMO');
   UDT.U_CREATE:=OPERATOR.USER().KOD
|| UDT.memo_get(,'MEMO',0)
?};
{? _d || exec('bv_udt_slo','!zws_par_kumr') ?};
UdtAdd:=_a=0;
_mp.trigRef('UDT');
{? ~_d & exec('ar_udt','!zws_par_kumr',0,_a)='' |
    _d & (UDT.edit("exec('ar_udt','!zws_par_kumr',1)"))
|| {? _a=1 | _mp.pathTodo()
   || {? (_ref:=exec('udt_add','!zws_par_kumr'))<>null
      || _wy.UDT:=_ref;
         _mp.save(,_wy);
         _mp.done();
         {? ~_d
         || UDT.prefix();
            UDT.seek(_ref)
         ?}
      ?}
   || exec('udt_mod','!zws_par_kumr',0);
      {? UDT.put()
      || UDT.memo_put()
      ?}
   ?}
|| {? UdtAdd=2
   || exec('udt_del','!zws_par_kumr',2);
      _mp.cancel();
      {? _a
      || UDT.seek(_old_ref)
      ?}
   ?}
?};
KH.actions('WER_ROZ');
1


\udt_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Dodanie umowy
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
{? UdtAdd=0
|| {? UDT.add()
   || _ref:=UDT.ref();
      UDT.NR:=0;
      UDT.NR:=exec('numer_new','numery',,,0);
      exec('znak','numery','UDT');
      UDT.memo_put();
      UDT.put();
      UdtAdd:=2
   ?}
|? UdtAdd=1
|| {? UDT.put()
   || {? UDT.memo_put()
      || _ref:=UDT.ref()
      ?}
   ?}
|| _ref:=UDT.ref()
?};
_ref


\udt_win_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Okno redagowania
::   WE: _a - przycisk zakoncz? 1-tak 0-nie
::       _b - z todo? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_win:=UDT.mk_edit('Umowa'@,,,,,'html_maximized');
UDT.win_etab(_win,'Umowa'@);
UDT.win_ewin(_win,,'RED1');
UDT.win_etab(_win,'Opis'@);
UDT.win_ewin(_win,,'RED2');
UDT.win_ebtn(_win,'text=%1, btn_label_align=center, panel=bottom, align=begin'['Projekty'@],"
   {? (_ret:=exec('ar_udt','!zws_par_kumr'))<>''
   || 'edit:'+_ret
   || {? (_ref:=exec('udt_add','!zws_par_kumr'))<>null
      || PROJEKTY.cntx_psh();
         PROJEKTY.index('UDT'); PROJEKTY.prefix(REF.FIRMA,_ref);
         PROJEKTY.win_sel('UDT');
         PROJEKTY.hdr_sel();
         PROJEKTY.hdr_sel(': '+UDT.SYM+' - '+UDT.OPIS);
         PROJEKTY.actions('UDT');
         PROJEKTY.select();
         PROJEKTY.hdr_sel();
         PROJEKTY.cntx_pop()
      ?};
      ''
   ?}
");
{? var_press('Udt')>0 | _b
|| UDT.win_ebtn(_win,'text=%1, btn_label_align=center, panel=bottom, align=begin'['Załą&czniki'@],"
      {? (_ret:=exec('ar_udt','!zws_par_kumr'))<>''
      || 'edit:'+_ret
      || {? (_ref:=exec('udt_add','!zws_par_kumr'))<>null()
         || DOKUM.index('DOKUM'); DOKUM.prefix(REF.FIRMA,$_ref,);
            {? var_press('DokumSel')>0
            || DOKUM.win_sel(DokumSel)
            || exec('win_zal','!zws_par_kumr')
            ?};
            DOKUM.hdr_sel();
            DOKUM.hdr_sel(': '+UDT.SYM+' - '+UDT.OPIS);
            DOKUM.select()
         ?};
         ''
      ?}
   ")
?};
{? _a=1 | _b
|| _btn:=UDT.win_ebtn(_win,'text=%1, btn_label_align=center, panel=bottom, align=end'[exec('text_red_zakoncz','#window','U')],"'key:F2'");
   UDT.btn_eopt(_win,_btn,'tooltip='+exec('help_red_zakoncz','#window','U'))
|| _btn:=UDT.win_ebtn(_win,'text=%1, btn_label_align=center, panel=bottom, align=end'[exec('text_red_ok','#window')],"'key:F2'");
   UDT.btn_eopt(_win,_btn,'tooltip='+exec('help_red_ok','#window','Z'))
?};
_btnan:=UDT.win_ebtn(_win,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
UDT.btn_eopt(_win,_btn,'tooltip='+exec('help_red_esc','#window','A'));
_win


\win_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Ustawia okno załączników
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB','BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND');
__CTR.BTAB:='DOKUM';
__CTR.TAB:='UDT';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','srsr_zal','utd_dokum_ctr',':');
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('sep','#file',1);
__CTR.DND:=1;
_bf:=$('exec(\'bobs_dokum\',\'srsr_zal\',exec(\'chk_role\',\'#b__box\',OPERATOR.USER,\'ZWS_PAR_KUMR\')=0 | UDT.ZAK<>\'N\')');
_fml:="exec('udt_zal_ar','rozrach')";
_grp:=DOKUM.grp_make('Załączniki umowy'@,,'#udt_zal');
DOKUM.grp_ctr(_grp,DOKUM,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,_fml,,,,,_bf,,,'maximized');
DOKUM.win_sel(_grp);
DokumSel:=_grp;
~~


\udt_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Ustawia informacje o modyfikacji umowy
::   WE: _a - put? 1-tak o-nie
::----------------------------------------------------------------------------------------------------------------------
UDT.D_MODIFI:=date();
UDT.T_MODIFI:=time();
UDT.U_MODIFI:=OPERATOR.USER().KOD;
{? UDT.ZAK='X'
|| UDT.D_CANCEL:=date();
   UDT.T_CANCEL:=time();
   UDT.U_CANCEL:=OPERATOR.USER().KOD
|? UDT.ZAK='T'
|| UDT.D_END:=date();
   UDT.T_END:=time();
   UDT.U_END:=OPERATOR.USER().KOD
?};
{? _a || UDT.put() ?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Opis na liście TODO czynności
::----------------------------------------------------------------------------------------------------------------------
_desc:='Rejestracja umowy'@@;
_desc


\bl_udt_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [1210]
:: OPIS: Wartosc poczatkowa pola UDT.KOD
::  OLD: \bl_udt_kod/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
_lp:=0;
UDT.cntx_psh();
UDT.index('KOD'); UDT.prefix();
{!
|? _lp+=1;
   _kod:=('00000000'+$_lp)+8;
   UDT.find_key(_kod) & _kod<>'99999999'
!};
UDT.cntx_pop();
_kod


\bl_udt_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Wartosc poczatkowa pola UDT.TYP
::  OLD: \bl_udt_typ/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
'N'


\ba_udt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Przed dołącz UDT
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_PAR_KUMR';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params);
{? UDT.f_active()
|| UDT.f_rfresh();
   UDT.seek(UDT.ref())
?}


\bm_udt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Przed popraw UDT
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_PAR_KUMR';
_params.AKCJA:='Popraw';
_params.UIDREF:=UDT.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
exec('mp_run','#b__box',_params)


\bd_udt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Przed usuń UDT
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_PAR_KUMR';
_params.AKCJA:='Usuń';
_params.UIDREF:=UDT.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
exec('mp_run','#b__box',_params)


\add_udt_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Dodanie projektu do umowy
::----------------------------------------------------------------------------------------------------------------------
PROJEKTY.cntx_psh();
PROJEKTY.win_sel('WER');
exec('projekty_f_set','projekty',UDT);
ProjBez:='M:M';
exec('projekty_jorg','obiegi_proj');
PROJEKTY.select();
VAR_DEL.delete('ProjBez');
{? PROJEKTY.f_active() || PROJEKTY.f_clear() ?};
PROJEKTY.cntx_pop()


\proj2udt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Dodaje projekt do umowy
::----------------------------------------------------------------------------------------------------------------------
{? PROJEKTY.UDT=null
|| PROJEKTY.cntx_psh();
   PROJEKTY.prefix();
   PROJEKTY.UDT:=UDT.ref();
   PROJEKTY.put();
   PROJEKTY.cntx_pop()
?};
sel_exit();
1


\del_udt_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Usunięcie projektu z umowy
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć projekt z umowy?'@)
|| PROJEKTY.cntx_psh();
   PROJEKTY.prefix();
   PROJEKTY.UDT:=null;
   PROJEKTY.put();
   PROJEKTY.cntx_pop()
?}


\ar_udt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Rekord po tabeli UDT
::   WE: [_a] - dialogowo? [1]-tak 0-nie
::       [_b] - dołaczanie? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<0 || _a:=1 ?};
{? var_press('_b')<0 || _b:=-(6+menu_txt())<>'popraw' ?};
{? _a
|| _ret:=__CHK.record2(UDT,'ZLEC','Kod kontrahenta')
|| _ret:='';
   _wsenv:=exec('wsenv','#mwapi');
   {? UDT.ZLEC=null
   || _ret:='ZLEC'
   ?};
   {? _ret<>''
   || _wsenv.add_error('Nie wypełniono pola %1 lub nieznaleziono rekordu.'@[_ret])
   || {? UDT.ODD & ~exec('usr_fjks','b_perm',UDT.ODD)
      || ODD.cntx_psh();
         _wsenv.add_error('Brak uprawnień do jednostki księgowej: %1.'@[UDT.ODD().OD]);
         ODD.cntx_pop();
         _ref:='ODD'
      ?}
   ?}
?};
{? _ret='' & UDT.ID_KSG
|| _ref:=UDT.ref();
   UDT.cntx_psh();
   UDT.index('ID_KSG'); UDT.prefix(UDT.ID_KSG);
   {? UDT.first() & (_b & UdtAdd=0 | UDT.ref()<>_ref)
   || _msg:='Istnieje umowa o identyfikatorze księgowym: %1 w firmie %2.'@[$UDT.ID_KSG,UDT.FIRMA().SYMBOL];
      {? _a || FUN.info(_msg) || _wsenv.add_error(_msg) ?};
      _ret:='ID_KSG'
   ?};
   UDT.cntx_pop()
?};
{? _ret='' & UDT.DATAO<>date(0,0,0) & UDT.DATAZ<>date(0,0,0)
|| {? UDT.DATAO>UDT.DATAZ
   || _msg:='Data rozpoczęcia późniejsza niż data zakończenia.'@;
      {? _a || FUN.info(_msg) || _wsenv.add_error(_msg) ?};
      _ret:='DATAO'
   ?}
?};
{? _ret='' & UDT.DATAO<>date(0,0,0) & UDT.DATAP<>date(0,0,0)
|| {? UDT.DATAP>UDT.DATAO
   || _msg:='Data podpisania późniejsza niż data rozpoczęcia.'@;
      {? _a || FUN.info(_msg) || _wsenv.add_error(_msg) ?};
      _ret:='DATAP'
   ?}
?};
{? _a & _ret<>'' & -(6+menu_txt())<>'popraw'
|| UDT.ID_KSG:=exec('bl_udt_id','projekty')
?};
{? _ret='' & _a=0 & UDT.ID<>''
|| UDT.cntx_psh();
   UDT.index('ID'); UDT.prefix(UDT.ID,);
   {? UDT.first() & (_b | UDT.ref()<>_ref)
   || _msg:='Istnieje umowa o identyfikatorze: %1.'@[UDT.ID];
      _wsenv.add_error(_msg);
      _ret:='ID'
   ?};
   UDT.cntx_pop()
?};
{? _ret='' & _a=0
|| exec('czytaj','#stalesys',,XINFO,'SLUMKAT','SLUMRODZ','SLUMTYP');
   {? UDT.SLO_KAT
   || {? XINFO.SLUMKAT=null
      || _wsenv.add_error('Wskazano pozycję w słowniku kategorii umów gdy nie ustawiono tego słownika w %1.'@
                          [exec('nazwa','#system')]
         );
         _ret:='SLO_KAT'
      |? UDT.SLO_KAT().SLU<>XINFO.SLUMKAT
      || _wsenv.add_error('Brak pozycji w słowniku kategorii umów.'@);
         _ret:='SLO_KAT'
      ?}
   ?};
   {? UDT.SLO_RODZ
   || {? XINFO.SLUMRODZ=null
      || _wsenv.add_error('Wskazano pozycję w słowniku rodzajów umów gdy nie ustawiono tego słownika w %1.'@
                          [exec('nazwa','#system')]
         );
         _ret:='SLO_RODZ'
      |? UDT.SLO_RODZ().SLU<>XINFO.SLUMRODZ
      || _wsenv.add_error('Brak pozycji w słowniku rodzaju umów.'@);
         _ret:='SLO_RODZ'
      ?}
   ?};
   {? UDT.SLO_TYP
   || {? XINFO.SLUMTYP=null
      || _wsenv.add_error('Wskazano pozycję w słowniku typów umów gdy nie ustawiono tego słownika w %1.'@
                          [exec('nazwa','#system')]
         );
         _ret:='SLO_RODZ'
      |? UDT.SLO_TYP().SLU<>XINFO.SLUMTYP
      || _wsenv.add_error('Brak pozycji w słowniku typów umów.'@);
         _ret:='SLO_TYP'
      ?}
   ?}
?};
_ret


\udt_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Usuwa umowę
::   WE: _a - z dialogami? 2-usuwanie automatyczne 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
{? ~_a || _wsenv:=exec('wsenv','#mwapi') ?};
{? exec('czy_slo','slo_slu','UDT')
|| _msg:='Rekord wykorzystywany w słownikach użytkownika.'@;
   {? _a || FUN.info(_msg) || _wsenv.add_error(_msg) ?};
   return()
?};
_is:=UDT.count()>0;
{? ~_is
|| DOKUM.cntx_psh();
   DOKUM.index('TYP'); DOKUM.prefix($UDT.ref());
   _is:=DOKUM.first();
   DOKUM.cntx_pop()
?};
_pyt:={? _is
      || 'Czy usunąć umowę o symbolu: %1 z jej powiązaniami?'@[UDT.SYM]
      || 'Czy usunąć umowę o symbolu: %1?'@[UDT.SYM]
      ?};
{? _a=2 | _a=0 | FUN.ask(_pyt)
|| PROJEKTY.cntx_psh();
   PROJEKTY.index('UDT'); PROJEKTY.prefix(REF.FIRMA,UDT.ref());
   {? PROJEKTY.first()
   || {!
      |? PROJEKTY.cntx_psh();
         PROJEKTY.prefix();
         PROJEKTY.UDT:=null;
         PROJEKTY.put();
         PROJEKTY.cntx_pop();
         PROJEKTY.first()
      !}
   ?};
   PROJEKTY.cntx_pop();
   exec('deladok','dokum','UDT');
   numer:=UDT.NR;
   oldnumer:=1;
   exec('nr_old','numery');
   UDT.del()
?}


\upd_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Ustawia opis statusu umowy
::----------------------------------------------------------------------------------------------------------------------
UDT.STATUSOP:={? UDT.ZAK='N'
              || 'Aktywna'
              |? UDT.ZAK='X'
              || 'Anulowana'
              |? UDT.ZAK='T'
              || 'Zakończona'
              || ''
              ?}


\trigb_udt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Triger przed add i put tabeli UDT
::----------------------------------------------------------------------------------------------------------------------
exec('upd_status','!zws_par_kumr');
1


\winedit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Tworzy okno edycji umów
::----------------------------------------------------------------------------------------------------------------------


\bv_udt_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Przed wyświetleniem pól słownikowych tabeli UDT
::----------------------------------------------------------------------------------------------------------------------
_red:=UDT.win_edit('?');
UDT.efld_opt(_red,'enable='+$(XINFO.SLUMTYP<>null),UDT,'SLO_TYP');
UDT.efld_opt(_red,'enable='+$(XINFO.SLUMRODZ<>null),UDT,'SLO_RODZ');
UDT.efld_opt(_red,'enable='+$(XINFO.SLUMKAT<>null),UDT,'SLO_KAT');
_enabled:=UDT.ZAK='X';
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'D_CANCEL');
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'T_CANCEL');
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'U_CANCEL');
_enabled:=UDT.ZAK='T';
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'D_END');
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'T_END');
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'U_END');
_enabled:=UDT.U_MODIFI<>'';
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'D_MODIFI');
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'T_MODIFI');
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'U_MODIFI');
_enabled:=exec('domyslny','schemat','PODZORG')<>null;
UDT.efld_opt(_red,'enable='+$_enabled,UD_POM,'JORG');
UDT.efld_opt(_red,'enable='+$_enabled,UDT,'JORG');
~~


\be_udt_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Przed redagowaniem pól słownikowych tabeli UDT
::----------------------------------------------------------------------------------------------------------------------
_pole:=4-cur_afld();
{? ($('XINFO.SLUM'+_pole))()
|| ($('XINFO.SLUM'+_pole+'()'))()
?}


\ae_udt_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Po redakcji pól słownikowych tabeli UDT
::----------------------------------------------------------------------------------------------------------------------
{? fld()
|| _pole:=4-cur_afld();
   _slu:=($('XINFO.SLUM'+_pole));
   1
?};
1


\set_udt_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Akcja zmiany statusu umowy
::----------------------------------------------------------------------------------------------------------------------
_status:='';
{? UDT.ZAK='N'
|| _wyn:=FUN.choice('Wybierz status umowy: %1.'@[UDT.SYM],,'Anulowana'@,'Zakończona'@);
   {? _wyn=1
   || _status:='X'
   |? _wyn=2
   || _status:='T'
   ?}
|| {? FUN.ask('Czy zmienić status umowy: %1 na aktywny.'@[UDT.SYM])
   || _status:='N'
   ?}
?};
{? _status<>''
|| UDT.cntx_psh();
   UDT.prefix();
   UDT.ZAK:=_status;
   exec('udt_mod','!zws_par_kumr',0);
   UDT.put();
   UDT.cntx_pop()
?}


\slo_pop_udt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Poprawienie umowy z poziomu słownika użytkownika
::----------------------------------------------------------------------------------------------------------------------
{? exec('test_slo','slo_slu')=0
|| FUN.info('Poprawienie pozycji słownika niemożliwe w bieżącej firmie.'@);
   return(0)
?};
SLO.SLU();
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? RS.find_key(SLU.WZ,) & RS.TAB='UDT'
|| _sp:={? var_pres('slo_pop')>0 || _slo_pop:=slo_pop; 1 || 0 ?};
   _rs:={? var_pres('ref_slo')>0 || _ref_slo:=ref_slo; 1 || 0 ?};
   slo_pop:=1; ref_slo:=null; exec('slu_okn','slo_slu');
   _i:=RS.ref();
   {? (_r_szuk:=exec('wz_szuk','slo_slu'))
   || exec('set_fml','#field',UD_POM,'JORG',
         "UD_POM.JORG:=UDT.JORG().SYMBOL; ~~",
         "exec('domyslny','schemat','PODZORG')<>null",
         "exec('ud_def_symbol_f3','schemat','PODZORG')",
         "  {? fld()='' || UDT.JORG:=null; return(1) ?};
            _ref:=exec('ud_def_symbol_ae','schemat',UD_SCH.ref());
            {? _ref
            || UDT.JORG:=_ref;
               fld(UDT.JORG().SYMBOL)
            ?}
         "
      );
      UdtAdd:=1;
      UDT.win_edit(exec('udt_win_red','!zws_par_kumr',0));
      _g:=$(RS.TAB+'.put()');
      exec('cn_psh','slo_slu');
      {? UDT.edit("exec('ar_udt','!zws_par_kumr')")
      || exec('cn_pop','slo_slu');
         {? var_pres('ref_slo')>0 & ref_slo<>null || SLO.seek(ref_slo) ?}; _h:=1;
         do();
         _k:=SLO.KOD; _t:=SLO.TR; _msg:='';
         {? _g()
         || {? _r_szuk<>-1 & ~RS.seek(_i)
            || undo();
               _msg:='Nie znaleziono rekordu tabeli RS.'@
            || _msg:=exec('akt_slo','slo_slu',_k,_t)
            ?}
         ?};
         end();
         {? _msg<>'' || FUN.info(_msg) ?}
      || exec('cn_pop','slo_slu'); {? ref_slo<>null || SLO.seek(ref_slo) ?}
      ?}
   ?};
   {? _sp || slo_pop:=_slo_pop || VAR_DEL.delete('slo_pop') ?};
   {? _rs || ref_slo:=_ref_slo || VAR_DEL.delete('ref_slo') ?}
|| FUN.info('Akcja dostępna wyłącznie dla słowników o wzorcu Umowy.'@)
?};
exec('tab_f_rfresh','slo_filtr',SLO);
RS.cntx_pop();
1


\bl_udt_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.14]
:: OPIS: Wartość poczatkowa UDT.NR
::----------------------------------------------------------------------------------------------------------------------
{? var_press('UdtNr')<=0
|| 0
|| exec('numer_new','numery',,,0)
?}


\idz2proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Podgląd projektu
:: ~OST: INFORK
::----------------------------------------------------------------------------------------------------------------------
_uidref:=exec('FindAndGet','#table',PROJEKTY,PROJEKTY.ref(),,"PROJEKTY.uidref()",'');

{? _uidref='' || FUN.info('Nie znaleziono projektu.'@); return() ?};
{? exec('interm','#system')
|| _params:=exec('obj_ntab_set','#array',,'LINK',_uidref);
   _link_interm:=link_uri(_params);
   cli_open_link(_link_interm)
||
   _domain:='ZWS';
   _color:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_domain,,"B_DOMAIN.COLOR",1,,'');
   mb_fork(,'START_PROCES'
      ,'color='+exec('color_conv','#convert',_color)
      ,'LINK',_uidref)
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 07e7c520ef22faf4677238dd291112afdf6d0ae6648f3f26eb8e163055d64bc6d9204018cf351df66906aa18ce008c5c11dc4756a69429fb5ae466132047c4f1861835ce848b1a3b8e7c50b82b42f70905e2d42d9f57f39b4aaea6a0a48729a31d07598cbca372656b5f490f78dc32dd697ec4525d83551f3f30018f3604a0af

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_zkn_drzk.fml
:: Utworzony: 01.10.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Rejestracja zamówienia sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,ZSMG,TARS
::# access=exec('access','!lsp_zkn_drzk')
::# parses=exec('parses','!lsp_zkn_drzk')
::# condition=Nie wszystko zlecone, act_uid=TTE_PZL_DZWE, auto=N, formula=exec('nie_wszystko_zlecone','%gate',_a.ZK_N)

::# kind=WE,   symbol=ZK_N,    type=_ZK_N,    name=Zamówienie sprzedaży,      required=N,    keyref=T
::# kind=WE,   symbol=OFE,     type=_OFE,     name=Oferta sprzedaży,          required=N,    keyref=T
::# kind=WE,   symbol=REK_N,   type=_REK_N,   name=Reklamcja klienta,         required=N,    keyref=T
::# kind=WE,   symbol=TYPYZAM, type=_TYPYZAM, name=Typ zamówienia sprzedaży,  required=N,    keyref=N, fml_val="exec('wyb_zam','zamowienia','Z')", fml_exp="exec('typyzam_export','typyzam',_a,'Z')"
::# kind=WY,   symbol=ZK_N,    type=_ZK_N,    name=Zamówienie sprzedaży,      required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

exec('init_zkn','lsp');

:: WSTĘPNE WALIDACJE
_clean_result:=params_exec('clean','!lsp_zkn_drzk',_mp,_in);
_can_continue:=_clean_result.RESULT;
{? _can_continue=0
|| return()
?};

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_auto:=0;
_area_lsp_zkn:=_mp.pathArea('LSP_ZKN');
_area_tre_rek:=_mp.pathArea('TRE_REK');
_area_zbl_dok:=_mp.pathArea('ZBL_DOK');
_area:=_area_lsp_zkn | _area_tre_rek | _area_zbl_dok;
_proc:=_mp.pathProc();
_todo:=exec('on','#mwapi')=0 & (_mp.pathTodo() | _mp.pathArea() & ~_area);
_area_lsp_zkn:=_area_lsp_zkn & exec('on','#mwapi')=0;
_autoakc:=exec('autoAkc','#b__box',_mp,300180,'LSP_ZKN_EANU');

:: Przepisanie portu wejściowego ZK_N na wyjściowy ZK_N
{? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N
   & (var_press('ZK_N',_out)<>type_of(null()) | _out.ZK_N=null())
|| _out.ZK_N:=_in.ZK_N;
   _mp.save(,_out)
?};

_czy_zam:=var_pres('ZK_N',_out)=type_of(null()) & _out.ZK_N;

{? _todo
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? _czy_zam
   || 1
   || {? ~_mp.isMicro()
      || _akcja:='START_TODO'
      || _mp.error('Brak parametru wyjściowego ZK_N.')
      ?}
   ?}
?};

:: Oferta
{? ~_czy_zam & var_pres('OFE',_in)=type_of(null()) & _in.OFE
|| _akcja:='Oferta';
   _auto:=_mp.isAutoRun()
?};
:: Reklamacja
{? ~_czy_zam & var_pres('REK_N',_in)=type_of(null()) & _in.REK_N
|| _akcja:='Reklamacja';
   _auto:=_mp.isAutoRun()
?};

:: Sprawdzenie uprawnień
:: DRO_TODO_[rr]: oprogramować dla zamówienia sprzedaży
{? params_exec('permissions_chk','lsp','')=0 || return() ?};

:: Wyzwalacz, który po dodaniu nagłówka dokumentu:
:: - add: dodaje rekord kluczowy nagłówka dokumentu
::   del: usuwa rekord kluczowy nagłówka dokumentu
:: - add: zapisuje parametr wyjściowy ZK_N = wskazanie na nagłówek zamówienia sprzedaży
::   del: zapisuje parametr wyjściowy ZK_N = null
_mp.trigRef('ZK_N',,0,,exec('kind_out','#b_port'),'ZK_N');

{? _czy_zam & ~(exec('FindAndGet','#table',ZK_N,_out.ZK_N,,"name()",'')*'__')
||
   _mp.error('Zamówienie '+exec('FindAndGet','#table',ZK_N,_out.ZK_N,,"SYM",'')+' przesłane do archiwum. '
             'Redakcja niemożliwa.')
|? _akcja='Oferta'
|| {? var_pres('TYPYZAM',_in)=type_of(null()) & _in.TYPYZAM<>null() || BEER.TYPYZAM:=_in.TYPYZAM ?};
   _ctrl:=exec('ctrl_Ofe2Zam','!lsp_zkn_drzk',_in.OFE);
   {? _ctrl=1
   ||
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      exec('zk_oferta','!lsp_zkn_drzk',_in.OFE,_auto,_in.TYPYZAM,_autoakc);
:: Podczytanie parametrów wyjściowych
      _outa:=_mp.load(exec('kind_out','#b_port'));
      {? var_pres('ZK_N',_outa)<>type_of(~~) & _outa.ZK_N
:: Dodano dokument
      ||
::    ustawienie się na zamówieniu lub zakończenie procesu dla czynności automatycznej
         {? _auto & {? ZK_N.seek(_outa.ZK_N) || ZK_N.STAT_REJ<>'N' || 0 ?}
         || _mp.done()
         |? _area_lsp_zkn
         || ZK_N.seek(_outa.ZK_N)
         ?}
::    Wycofanie czynności bo nie dodano dokumentu
      || _mp.cancel()
      ?}
   |? _ctrl=-1
   || {? var_pres('X_Xa')>0 & X_Xa.first()
      || _outa:=_mp.load(exec('kind_out','#b_port'));
         _outa.ZK_N:=exec('FindAndGet','#table',ZK_N,X_Xa.REF,,,null());
         _mp.save(,_outa)
      ?};
      _mp.done()
   || _mp.cancel()
   ?}

|? _akcja='Reklamacja' & var_pres('REK_N',_in)=type_of(null()) & _in.REK_N
|| {? var_pres('TYPYZAM',_in)=type_of(null()) & _in.TYPYZAM<>null() || BEER.TYPYZAM:=_in.TYPYZAM ?};
   _ctrl:=exec('ctrl_Rek2Zam','!lsp_zkn_drzk',_in.REK_N);
   {? _ctrl=1
   ||
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      exec('zk_reklam','!lsp_zkn_drzk',_in.REK_N,_auto,_in.TYPYZAM,_autoakc);
:: Podczytanie parametrów wyjściowych
      _outa:=_mp.load(exec('kind_out','#b_port'));
      {? var_pres('ZK_N',_outa)<>type_of(~~) & _outa.ZK_N
:: Dodano dokument
      ||
::    ustawienie się na zamówieniu lub zakończenie procesu dla czynności automatycznej
         {? _auto & {? ZK_N.seek(_outa.ZK_N) || ZK_N.STAT_REJ<>'N' || 0 ?}
         || _mp.done()
         |? _area_lsp_zkn
         || ZK_N.seek(_outa.ZK_N)
         ?}
::    Wycofanie czynności bo nie dodano dokumentu
      || _mp.cancel()
      ?}
   |? _ctrl=-1
   || {? var_pres('X_Xa')>0 & X_Xa.first()
      || _outa:=_mp.load(exec('kind_out','#b_port'));
         _outa.ZK_N:=exec('FindAndGet','#table',ZK_N,X_Xa.REF,,,null());
         _mp.save(,_outa)
      ?};
      _mp.done()
   || _mp.cancel()
   ?}

|? _akcja='Dołącz'
   | _proc
   | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
|| {? var_pres('TYPYZAM',_in)=type_of(null()) & _in.TYPYZAM<>null()
   || BEER.TYPYZAM:=_in.TYPYZAM
   |? _proc | _akcja='START_TODO'
:: Wybór typu zamówienia
   || BEER.TYPYZAM:=null();
      _p3004:=exec('get','#params',3004,2,OPERATOR.USER);
      exec('typ_zamm','zamowienia',_p3004,'Z')
   ?};

   {? BEER.TYPYZAM
   ||
::    Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
      params_set('out',_out,'mp',_mp,'akcja',_akcja,'context',_context);
      exec('zam_dol','zamsiw_nag');

::    Podczytanie parametrów wyjściowych
      _outa:=_mp.load(exec('kind_out','#b_port'));
      {? var_pres('ZK_N',_outa)<>type_of(~~) & _outa.ZK_N
::    Dodano dokument
      ||
::       Ustawienie się na dodanym dokumencie w widoku obszaru
         {? _area_lsp_zkn || ZK_N.seek(_outa.ZK_N) ?}
::    Wycofanie czynności bo nie dodano dokumentu
      || _mp.cancel()
      ?}
   ?}

|? _akcja='DołączMWA'
||
   _wsenv:=exec('wsenv','#mwapi');
   _params:=_context.PARAMS;
   _buffers:=_context.BUFFERS;
   {? var_pres('POZ',_buffers)<=100
   ||
      FUN.emsg('Brak pozycji zamówienia.'@)
   ||
      VAR_DEL.delete('__matakt');
      __matakt:=tab_tmp(2
         ,'REF','STRING[16]',''
         ,'NRK','INTEGER',''
         ,'ILR','REAL',''
         ,'ZKP','STRING[16]',''
         ,'ZKN','STRING[16]','');
      _name_conf:={? type_of(_params.NAME_CONF)=type_of('') || _params.NAME_CONF || '' ?};
      {? _buffers.ZK_N.T=null()
      ||
         _typ_zam:=
            {? _name_conf='ABSTOREB2B' || Plugin.run('ZKN_T_B2B_001')
            |? _name_conf='ABSTOREB2C' || Plugin.run('ZKN_T_B2C_001')
            || ''
            ?};
         {? _typ_zam=''
         ||
            _typ_zam:=
               {? _name_conf='ABSTOREB2B' || exec('get','#params',800112)
               |? _name_conf='ABSTOREB2C' || exec('get','#params',800212)
               || ''
               ?}
         ?};
         _typ_zam:=form(_typ_zam);
         _buffers.ZK_N.T:=exec('FindInSet','#table','TYPYZAM','TYP',_typ_zam,'Z',,1,,null())
      ?};
      do();
      {? ~exec('add','zamsiw_nag','webservice',_buffers.ZK_N,_name_conf)
      ||
         undo()
      ||
         ZK_P.prefix();
         _ilp:=obj_len(_buffers.POZ);
         _ilpadd:=0;
         {! _ii:=1.._ilp
         |!
            {? ~exec('add','zamsiw_poz','webservice',_buffers.POZ[_ii],_name_conf)
            ||
               undo()
            ||
               _ilpadd+=1
            ?}
         !};
         {? _ilp=_ilpadd || __wsenv.IDADD:=ZK_N.IDADD ?}
      ?};
      end();
      {? +_wsenv.IDADD
      ||
         exec('end_zkn','edi_form');
         {? exec('zam_akc','zamsiw_nag',1,_autoakc) || _mp.done() ?}
      ?};
      VAR_DEL.delete('__matakt')
   ?}

|? _akcja='Kopiuj'
|| params_set('context',params_get());
   exec('zk_kopiuj','!lsp_zkn_drzk')

|? _akcja='Popraw'
   | _todo
|| {? var_pres('ZK_N',_out)=type_of(null())
:: Uruchomione w procesie
   || ZK_N.cntx_psh();
      ZK_N.prefix();
      {? ZK_N.seek(_out.ZK_N)
      || {? ~_todo | ZK_N.STAT_REJ='N'
         || params_set('out',_out,'mp',_mp,'akcja',_akcja);
            exec('zam_pop','zamsiw_nag');
            _mp.descTodo()
         || FUN.info('Zamówienie już jest %1.'@[{? ZK_N.STAT_REJ='T' || 'zaakceptowane' || 'zakończone' ?}]);
            _mp.done()
         ?}
      ?};
      ZK_N.cntx_pop()
   |? _todo
:: Uruchomione zadanie z listy todo i brak _out.FAKS wtedy zadanie wycofujemy
   || _mp.cancel()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      exec('zam_pop','zamsiw_nag')

   ?}

|? _akcja='Usuń'
|| {? var_pres('ZK_N',_out)=type_of(null())
:: Uruchomione w procesie
   || _zknag:=null();
      ZK_N.cntx_psh();
      {? ~_area_lsp_zkn || ZK_N.prefix() ?};
      {? ZK_N.seek(_out.ZK_N)
      ||
         exec('zam_usu','zamsiw_nag');

         {? ~ZK_N.seek(_out.ZK_N)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _zknag:=ZK_N.ref()
      ?};
      ZK_N.cntx_pop();
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _area_lsp_zkn || {? _zknag || ZK_N.seek(_zknag) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      exec('zam_usu','zamsiw_nag');

      _mp.done()
   ?}

|? _akcja='Zakończ_wer' & _area_lsp_zkn
|| {? exec('zam_akc','zamsiw_nag',,_autoakc) || _mp.done() ?}

|| _mp.error('Nieobsługiwana ścieżka.')
?};

{? _area_lsp_zkn || exec('zakr_set','zamsiw_nag') ?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('ZK_N',_out)<>type_of(~~) & _out.ZK_N
|| 'Zakończ rejestrację zamówienia sprzedaży: %1'@@[exec('record','#to_string',_out.ZK_N)]
|| _in:=_mp.load(exec('kind_in','#b_port'));
   {? var_pres('ZK_N',_in)<>type_of(~~) & _in.ZK_N
   || 'Zakończ rejestrację zamówienia sprzedaży: %1'@@[exec('record','#to_string',_in.ZK_N)]
   |? var_pres('OFE',_in)<>type_of(~~)
   || {? _in.OFE
      || 'Zarejestruj zamówienie sprzedaży na podstawie oferty: %1'@@[exec('record','#to_string',_in.OFE)]
      || ''
      ?}
   || 'Zarejestruj zamówienie sprzedaży'@@
   ?}
?}


\zknag_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Dołącz
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_ZKN_DRZK';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYPYZAM',BEER.TYPYZAM);


exec('mp_run','#b__box',_params)


\zknag_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Popraw
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_ZKN_DRZK';
_params.UIDREF:=ZK_N.uidref();
_params.AKCJA:='Popraw';

exec('mp_run','#b__box',_params)


\zknag_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Usuń
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_ZKN_DRZK';
_params.UIDREF:=ZK_N.uidref();
_params.AKCJA:='Usuń';
_params.GRUPA:=ZK_N.sel_size()>0;

exec('mp_run','#b__box',_params)


\zknag_pozycje_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienia sprzedaży - Pozycje - okno wertowania
::----------------------------------------------------------------------------------------------------------------------
BPMN.END:=0;
params_exec('zam_poz','zamsiw_poz');
{? AreaTitle.area='LSP_ZKN' || exec('zakr_set','zamsiw_nag') ?}


\zknag_zakoncz_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Zakocz - okno wertowania
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_ZKN_DRZK';
_params.UIDREF:=ZK_N.uidref();
_params.AKCJA:='Zakończ_wer';

exec('mp_run','#b__box',_params)


\zknag_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Kopiuj
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_ZKN_DRZK';
_params.AKCJA:='Kopiuj';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYPYZAM',ZK_N.T);

exec('mp_run','#b__box',_params)


\zk_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed kopiuj okna WER tabeli ZK_N, kopiowanie zamowienia
::  OLD: \zk_kopiuj/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_msg:='';
_ok:=1;
USERS_UP.index('MG');
USERS_UP.prefix(null,'ZAM');
{? ~USERS_UP.first()
|| _msg:='Brak magazynów do realizacji zamówień sprzedaży.\nDołączenie zamówienia niemożliwe.'@;
   _ok:=0
?};

{? _ok=1
|| _tab:=obj_new('UPR_WWW','ZAM_WWW','COM_WWW');
   _upr_www:=_tab.UPR_WWW:='N';
   _zam_www:=_tab.ZAM_WWW:='';
   _com_www:=_tab.COM_WWW:='';
   {? Plugin.run('TYP_ZAM_WWW_001',_tab)
   || _upr_www:=_tab.UPR_WWW;
      _zam_www:=_tab.ZAM_WWW;
      _com_www:=_tab.COM_WWW
   ?};
   obj_del(_tab);
   _msg:='';
   {? _zam_www<>'' & ZK_N.T().T=_zam_www
   || {? _upr_www='N'
      || _msg+='Brak uprawnień do kopiowania zamówienia internetowego.'@;
         _res:=0
      |? _upr_www<>'T' & _com_www<>''
      || _msg+=_com_www;
         _res:=0
      ?}
   ?}
?};
_klim:=__War_f('KH_DOD','KLIM',ZK_N.KH); {? ~_klim || _klim:=ZK_N.KH().GRKH().KLIM ?};

KH_DOD.B:=__War_f('KH_DOD','B',ZK_N.KH);
{? KH_DOD.B='W' || FUN.info('Kontrahent zablokowany warunkowo.'@) ?};
{? KH_DOD.B='B' | KH_DOD.B='T'
|| FUN.info('Kontrahent zablokowany.'@)
|? _msg<>''
|| FUN.info(_msg)
|? exec('zam_lock','zamsiw_nag') & FUN.ask('Skopiować zamówienie?'@)
|| params_exec('zk_kopiuj1','!lsp_zkn_drzk',ZK_N.ref())
?}


\zk_kopiuj1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: kopiowanie zamowienia
::   WE: _a  - ZK_N.ref
::   WY: ''
::  OLD: \zk_kopiuj1/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

VAR_DEL.delete('__aktmat');
__aktmat:=tab_tmp(1,'MAT','STRING[16]',''
           ,'ILR','REAL',''
           ,'NRK','INTEGER','');
_zkp:=tab_tmp(1,'MAT','STRING[16]','','ZKP','STRING[16]','');

__gen:=1;

ZK_N.cntx_psh();
_mask_bie:=5-ZK_N.name();
_rodzaj:=ZK_N.T().R;

_zk_n:=obj_new(ZK_N.fld_num());
_zk_p:=obj_new(ZK_P.fld_num());
_zk_rn:=obj_new(ZK_RN.fld_num());
_zk_rp:=obj_new(ZK_RP.fld_num());
_fakso:=obj_new(FAKSO.fld_num());

_ref_old:=_a;
_ref_new:=null;
exec('plat_sel','faktury_plat',ZK_N);

:: ref'y materialow do odtworzenia statusow zamowien
_mats:='';

:: kopiowanie naglowka

ZK_N.prefix();
{? ZK_N.seek(_a)
||
   {! _licz:=1..ZK_N.fld_num() |! _zk_n[_licz]:=ZK_N[_licz] !}
?};

:: obsluga memo
_memo:=ZK_N.memo_get('r','UW');
ZK_N.memo_get(,'UW');

ZK_N.blank();
{! _licz:=1..ZK_N.fld_num()
|! {? var_pres(ZK_N.fld_acr(_licz),ZK_N)<>var_pres('UW',ZK_N) || ZK_N[_licz]:=_zk_n[_licz] ?}
!};

:: wyznaczenie nowego numeru

BEER.TYPYZAM:=exec('FindInSet','#table','TYPYZAM','TYP',ZK_N.T().T,_rodzaj);
POM.TAB:='ZK_N';
POM.TYPDOK:=ZK_N.T().KOD;
ZK_N.NR:=0;
ZK_N.SYM:='';
ZK_N.R:=ST.AR;
ZK_N.M:=ST.AM;
_dp_change:=ZK_N.DP<>date();
ZK_N.DP:=date();
ZK_N.DT=ZK_N.DP;
ZK_N.DT:=ZK_N.DP+{? ~ZK_N.KH().DREA
                 || {? ZK_N.T().ILREA>0 || ZK_N.T().ILREA || 0 ?}
                 || {? KH.DREA>0 || KH.DREA || 0 ?}
                 ?};
ZK_N.PL_DATA:=ZK_N.DT;
ZK_N.PL_TIME:=time(0,0,0);
ZK_N.RR:=ZK_N.DT~1;
ZK_N.RM:=ZK_N.DT~2;
ZK_N.RT:=exec('NumberOfWeek','#datetime',ZK_N.DT);
_tran:=ZK_N.TRN;
_sam:=ZK_N.POJAZD;

:: zmiana wartosci pol naglowka tworzonego zamowienia
ZK_N.US:=OPERATOR.USER;
ZK_N.PR:=0;
ZK_N.AKC:='N';
ZK_N.STAT_REJ:='N';
ZK_N.KOLOR:=exec('kolor_rand','#color');
ZK_N.MG:=null();
ZK_N.EZAPOT:='';
ZK_N.OBI_POW:='';
ZK_N.OFE:=null;
ZK_N.LOTUSID:='';

_wal:=#ZK_N.WAL;
_krs:=ZK_N.KRS;
_kh:=ZK_N.KH;
_odb:=ZK_N.ODB;
_pl:=ZK_N.PL;
_kraj_vat:=ZK_N.KRAJ_VAT;

{? ZK_N.add() & ZK_N.r_lock(1,1,1)
||
:: obluga memo
   {? _memo.is_open || ZK_N.memo_put(_memo,'UW') ?};

   _ref_new:=ZK_N.ref();

   {? BEER.TYPYZAM().R='W'
   || ZK_N.win_edit('REDW')
   || ZK_N.win_edit('RED')
   ?};
   TAZ.RAB_N_BE:=ZK_N.RAB;

:: kopiowanie płatności
   exec('copyPlatZkn','faktury_plat',_ref_old,_ref_new);
   {? _dp_change || exec('akt_tz','faktury_plat','DP') ?};

   {? ZK_N.edit("params_exec('spr_nzam','zamsiw_nag')")
   || {? ZK_N.NR=0 || exec('fak_numer','numery','',0); exec('znak','numery','ZK_N') ?};
      do();
      ZK_N.ZNB:=ZK_N.ZNW:=ZK_N.ZNR:=ZK_N.ZBB:=ZK_N.ZBZ:='N';
      ZK_N.put();

      ZK_N.memo_put(,'UW');

::    kopiowanie pozycji
      ZK_P.index('TYPN');
      ZK_P.prefix(ZK_N.A,'Z',_ref_old,1);
      {? ZK_P.first()
      ||
         {!
         |? _zk_p_o:=ZK_P.ref();
            {! _licz:=1..ZK_P.fld_num() |! _zk_p[_licz]:=ZK_P[_licz] !};
            ZK_P.cntx_psh();
            ZK_P.prefix();
            {! _licz:=1..ZK_P.fld_num() |! ZK_P[_licz]:=_zk_p[_licz] !};

::          zmiana wartosci pol pozycji tworzonego zamowienia
            ZK_P.N:=_ref_new;
            ZK_P.A:='A';
            ZK_P.REZ:=0;
            ZK_P.SR:=0;
            ZK_P.ILR:=0;
            {? (ZK_P.RODZ='Z' | ZK_P.M().RODZ<>'U') & ZK_P.N().MG<>null || ZK_P.RMAG:=ZK_P.MG:=ZK_P.N().MG ?};
            ZK_P.ILP:=ZK_P.ILZ;
            _ldn:=exec('FindInSet','#table','USERREZ','UNIK',ZK_P.M().MGR,OPERATOR.USER,"@.USERREZ.LDNI",,,0);
            {? ~_ldn || _ldn:=exec('FindInSet','#table','USERREZ','UNIK',null(),OPERATOR.USER,"@.USERREZ.LDNI",,,0) ?};
            _zkndt:={? _ldn>0 || ZK_P.N().DP+_ldn || ZK_P.N().DT ?};
            {? ZK_P.N().DT>_zkndt || _zkndt:=ZK_P.N().DT ?};
            ZK_P.DT:={? _zkndt<date() || date() || _zkndt ?};
            ZK_P.PL_DATA:={? ZK_P.N().PL_DATA<date() || date() || ZK_P.N().PL_DATA ?};
::          aktualizacja transportu
            ZK_P.RR:=ZK_N.RR;
            ZK_P.RM:=ZK_N.RM;
            ZK_P.RT:=ZK_N.RT;
            {? _tran<>ZK_N.TRN || ZK_P.TRN:=ZK_N.TRN ?};
            {? _sam<>ZK_N.POJAZD || ZK_P.POJAZD:=ZK_N.POJAZD ?};
            ZK_P.SV:=exec('m_vat','material',ZK_P.M,ZK_N.KH,,ZK_N.DP~1,ZK_N.DP~2,
               {? ZK_N.KRAJ_VAT<>_kraj_vat || null() || ZK_P.SV ?},,,,ZK_N.KRAJ_VAT);
            ZK_P.ZD_POZ:='';
            ZK_P.ILRB:=ZK_P.ILRW:=ZK_P.ILRD:=ZK_P.DOR:=0;
            ZK_P.ZNB:=ZK_P.ZNW:=ZK_P.ZNR:=ZK_P.ZBB:=ZK_P.ZBZ:='N';
            ZK_P.CZYBEZ:='T';
            ZK_P.ILBEZ:=ZK_P.ILP;
            ZK_P.ODDT:=ZK_P.DODT:=date(0,0,0);
            {? (1+ZK_P.RMAG().TYP)<>'D' & ZK_P.DK_C<>null() || ZK_P.DK_C:=null() ?};
            ZK_P.US:=ZK_P.N().US;
            {? ZK_P.N().ZL<>null() || ZK_P.ZL:=ZK_P.N().ZL ?};
            ZK_P.NR:=exec('blnr_kol','zamsiw_poz',ZK_P.M);
            ZK_P.KH:=ZK_P.N().KH;
            ZK_P.KH_ODB:=ZK_P.N().ODB;
            {? ZK_P.RMAG<>null() & ZK_P.RMAG().PAL='T' & ZK_P.N().MG=null() || ZK_P.RMAG:=ZK_P.MG:=null() ?};
            ZK_P.IL_ZT:=ZK_P.IL_TR:=0;
            ZK_P.PROJEKTY:=ZK_P.N().PROJEKTY;
            ZK_P.add();
            _zkp.blank(); _zkp.MAT:=$ZK_P.M; _zkp.ZKP:=$ZK_P.ref(); _zkp.add(1);
            __aktmat.clear();
            __aktmat.clear();
            {? __aktmat.find_key($ZK_P.M)
            || __aktmat.ILR+=ZK_P.ILP;
               {? __aktmat.NRK>ZK_P.NR || __aktmat.NRK:=ZK_P.NR ?};
               __aktmat.put(1)
            || __aktmat.blank();
               __aktmat.MAT:=$ZK_P.M;
               __aktmat.ILR:=ZK_P.ILP;
               __aktmat.NRK:=ZK_P.NR;
               __aktmat.add(1)
            ?};
            _mats+=$ZK_P.M;
            _zk_p_n:=ZK_P.ref();

::          kopiowanie informacji dodatkowych dla pozycji zamowienia klienta
            FAKSO.cntx_psh();
            FAKSO.use('fakto'+_mask_bie);
            FAKSO.index('ZK_P');
            FAKSO.prefix(_zk_p_o);
            {? FAKSO.first()
            ||
               {!
               |? {! _licz:=1 .. FAKSO.fld_num() |! _fakso[_licz]:=FAKSO[_licz] !};
                  FAKSO.cntx_psh();
                  FAKSO.prefix();
                  {! _licz:=1 .. FAKSO.fld_num() |! FAKSO[_licz]:=_fakso[_licz] !};
                  FAKSO.ZK_P:=_zk_p_n;
                  FAKSO.add();
                  FAKSO.cntx_pop();
                  FAKSO.next()
               !}
            ?};
            FAKSO.cntx_pop();

            ZK_P.cntx_pop();
            ZK_P.next()
         !}
      ?};

::    kopiowanie informacji dodatkowych dla naglowka zamowienia klienta
      FAKSO.cntx_psh();
      FAKSO.use('fakto'+_mask_bie);
      FAKSO.index('ZK_N');
      FAKSO.prefix(_ref_old);
      {? FAKSO.first()
      ||
         {!
         |? {! _licz:=1 .. FAKSO.fld_num() |! _fakso[_licz]:=FAKSO[_licz] !};
            FAKSO.cntx_psh();
            FAKSO.prefix();
            {! _licz:=1 .. FAKSO.fld_num() |! FAKSO[_licz]:=_fakso[_licz] !};
            FAKSO.ZK_N:=_ref_new;
            FAKSO.add();
            FAKSO.cntx_pop();
            FAKSO.next()
         !}
      ?};
      FAKSO.cntx_pop();
      _wyn:=do_state;
      end();
      ZK_N.r_unlock()
   ||
      exec('del_plat','faktury_plat',ZK_N.ref());
      {? do_state()<2 & ~Plugin.run('BEFORE_DELTAB_001',ZK_N.ref()) & do_state() || undo() ?};
::    usuwanie numeru
      numer:=ZK_N.NR;
      oldnumer:=1;
      exec('nr_old','numery');
      ZK_N.del(1);
      ZK_N.r_unlock()
   ?};
   MG.f_clear()
||
   FUN.info('Kopiowanie zamówienia nie powiodło się.'@)
?};
ZK_N.cntx_pop();

{? _wyn=1
||
   ZK_N.cntx_psh;
   ZK_N.prefix;
   {? ZK_N.seek(_ref_new) & ZK_N.r_lock(1,1,1)
   ||
::    przywrocenie rezerwacji kopiowanego zamowienia
      {? exec('aktywpoz','zamsiw_poz',ZK_N.ref(),1)
      || ZK_N.A:='A'; ZK_N.put(1)
      ?};
::    aktualizacja statusow zamowien
      __aktmat.clear();
      {? __aktmat.first()
      || {!
         |? _refm:=exec('FindAndGet','#table',M,__aktmat.MAT,,,null());
            exec('aktu_rez','rezerwacje',_refm,__aktmat.NRK,__aktmat.ILR,$_ref_new,_zkp);
            __aktmat.next()
         !}
      ?};
      _ctrlCN:=exec('get','#params',800850,2);
::    aktualizacja zamowienia
      exec('obl_warz','zamsiw_nag',_ref_new,1,0);
      ZK_P.index('TYPN');
      ZK_P.prefix('A','Z',_ref_new,1);
::Usunięcie powiązanego cennika jeśli nie jest już aktualny
      {? ZK_P.first & ZK_N.T().R='Z' & (_kh<>ZK_N.KH | _odb<>ZK_N.ODB | _wal<>ZK_N.WAL | _pl<>ZK_N.PL)
      ||
         {!|?
            {? ZK_P.TAR_H<>null()
            ||
               ZK_P.cntx_psh();
               TAR_H.cntx_psh();
               TAR_H.use(ref_name(ZK_P.TAR_H));
               _tar_h_tap:=$ZK_P.TAR_H().TAP;
               ZK_P.CENA:=0;
               ZK_P.CWAL:=0;
               VAR_DEL.delete('__TAP_LIST');
               exec('f3_ceny','zamsiw_poz',2);
               exec('bezpkwce','zamsiw_poz',1);
               {? var_pres('__TAP_LIST')=type_of('')
               & ( __TAP_LIST*_tar_h_tap)=0
               || ZK_P.cntx_pop();
                  ZK_P.TAR_H:=null();
                  ZK_P.TAR_TMS:=0;
                  ZK_P.put()
               || ZK_P.cntx_pop()
               ?};
               VAR_DEL.delete('__TAP_LIST');
               TAR_H.cntx_pop()
            ?};
            ZK_P.next()
         !}
      ?};
      {? ZK_P.first & ZK_N.T().R='Z' & (_kh<>ZK_N.KH | _odb<>ZK_N.ODB | _wal<>ZK_N.WAL | _pl<>ZK_N.PL)
       & _ctrlCN<>'B' & (_ctrlCN='T' | exec('ceny_mat_dok','ceny_dok',2,,ZK_N,1))
      ||
         _ma_cennik:='N';
         _choice:=2;
         _result:=spli_str(exec('fap_czy_cennik','faktury_wspolne',_kh,_odb,_pl,_wal,ZK_P),' ');
         _ma_cennik:=_result[1];
         _choice:=#_result[2];

         {? _ma_cennik='N' & _choice<>2
         || _ok:=1
         ||
            {? _choice=0 ||
               _choice:=FUN.choice('Zmieniły się kryteria cenników.\nNależy wyznaczyć ponownie ceny.'@,,
                  'Wg &cenników'@,'Wg &dokumentu'@)
            |? _ma_cennik='N' & _choice=2
            || _msg:='Zmieniły się kryteria cenników.\nBrak cennika zgodnego z aktualnymi kryteriami.'@;
               FUN.info(_msg)
            ?};
            {!
            |?
               _ok:={? _choice || exec('ceny_mat_dok','ceny_dok',1,,ZK_N,,0,_choice=2,ZK_N.WAL<>OFE.WAL) || 0 ?};
               {? _ok=0
               || {? FUN.ask('Należy ustalić nowe ceny.\nW przeciwnym razie kopiowanie będzie anulowane.\n'
                             'Czy anulować kopiowanie?'@)
                  || exec('zam_usu','zamsiw_nag',1);
                     0
                  || 1
                  ?}
               || 0
               ?}
            !}
         ?}
      ?};
      ZK_N.r_unlock
   ?};
   ZK_N.cntx_pop;
   {? ~ZK_N.seek(_ref_new)
   ||
      FUN.info('Aktualnie zredagowane zamówienie nie jest dostępne w ustawionym zakresie widoku zamówień.'@)
   ?}
?};
obj_del(_zkp);
VAR_DEL.delete('__gen');
VAR_DEL.delete('__aktmat');
''


\zknag_oferta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Oferta
::----------------------------------------------------------------------------------------------------------------------
{? exec('ctrl_Ofe2Zam','!lsp_zkn_drzk',OFE.uidref())=1
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LSP_ZKN_DRZK';
   _params.AKCJA:='Oferta';
   _params.PROC_START:='T';
   _params.UIDREF:=OFE.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OFE',OFE.ref());
   _typyzam:=BEER.TYPYZAM;
   exec('mp_run','#b__box',_params);
   BEER.TYPYZAM:=_typyzam
?};
~~


\zk_oferta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: tworzenie zamowienia na podstawie oferty
::   WE: [_a] - ref oferty
::       [_b] - czy automatyczna
::       [_c] - typ zamówienia
::       [_d] - 1-automatyczna akceptacja 0(domyślnie)-nie
::  OLD: \ofe_zam/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) || _ofe:=_a || _ofe:=OFE.ref() ?};
{? var_pres('_b')=type_of(0) || _auto:=_b || _auto:=0 ?};
{? var_pres('_c')=type_of(null()) & _c<>null() || BEER.TYPYZAM:=_c || BEER.TYPYZAM:=null() ?};
_autoakc:={? var_pres('_d')=type_of(0) || _d || 0 ?};

OFE.cntx_psh();
OFE.clear();
_ok:={? OFE.seek(_ofe) || 1 || 0 ?};

VAR_DEL.delete('__matakt','__only_u');
__matakt:=tab_tmp(2,'REF','STRING[16]',''
           ,'NRK','INTEGER',''
           ,'ILR','REAL',''
           ,'ZKP','STRING[16]',''
           ,'ZKN','STRING[16]','');


_ofe:=OFE.ref();
__only_u:=1;

_ok:={? _ok || exec('ctrl_Ofe2Zam','!lsp_zkn_drzk',OFE.ref())=1 || 0 ?};

{? _ok=1
||
   OFP.index('OFE_P');
   OFP.prefix(_ofe);
   {? OFP.first()
   ||
      {!
      |?
         {? OFP.IL=0
         ||
            FUN.info('Pozycja %1 - nieuzupełniona ilość.'@[$OFP.POZ]);
            _ok:=0
         ?};
         {? OFP.M().RODZ='U' || __only_u:=0 ?};
         OFP.next() & _ok=1
      !}
   ||
      FUN.info('Brak pozycji oferty.'@);
      _ok:=0
   ?}
?};

{? _ok=1
||
   {? OFE.r_lock(1,1,1)
   ||
      {? (_auto | FUN.ask('Utworzyć zamówienie na podstawie oferty %1?'@[OFE.SYM]))
        & (BEER.TYPYZAM<>null() | exec('wyb_zam','zamowienia','Z',0)<>null)
      ||
         BEER.DOL:=1;
         HELP.POP:=0;
         {? BEER.TYPYZAM=null() || BEER.TYPYZAM:=TYPYZAM.ref() ?};
         POM.TAB:='ZK_N';
         POM.TYPDOK:=BEER.TYPYZAM().KOD;
         BEER.RMAG:=null;
         BEER.SZ:='S';
         ZAKR.T:='A'; ZAKR.WG:='O'; ZAKR.US:=OPERATOR.USER;
         ZK_N.prefix();
         ZK_N.blank();
         ZK_N.DP:=date();
         ZK_N.KH:=OFE.KH;
         ZK_N.DT:=OFE.DR;
         ZK_N.SPOBLRAB:=OFE.SPOBLRAB;
         {? ZK_N.DT=date(0,0,0) || ZK_N.DT:=ZK_N.DP+{? ZK_N.KH().DREA>0 || ZK_N.KH().DREA || 0 ?} ?};
         ZK_N.POJAZD:=OFE.POJAZD;

         ZK_N.ODB:=OFE.ODB;
         ZK_N.HAN:=OFE.HAN;
         ZK_N.PL:=OFE.PL;
         _ldni:=#exec('FindInSet','#table','ZR_SLO','SLO_NR',2,OFE.PL,"ZR_SLO.WAR",,,'');
         ZK_N.RAB:=OFE.RAB;
         TAZ.RAB_N_BE:=OFE.RAB;
         ZK_N.RAB_TYP:=OFE.RAB_TYP;
         ZK_N.OFE:=OFE.ref();

         _ww:=BEER.WW;
         exec('ustaw_ww','eurusd','Z');
         _czywal:=exec('spr_ww','eurusd',0);
         BEER.WW:=0;
         BEER.MW:='Z';
         ZK_N.WAL:={? _czywal || OFE.WAL || INFO.NAROD ?};
         ZK_N.SWAL:={? _czywal || OFE.SWAL || 2 ?};
         ZK_N.KRS:={? _czywal || OFE.KRS || 0 ?};
         ZK_N.RTK:={? _czywal || OFE.RTK || 0 ?};
         ZK_N.NTK:={? _czywal || OFE.NTK || '' ?};
         ZK_N.DTK:={? _czywal || OFE.DTK || date(0,0,0) ?};
         ZK_N.BTK:={? _czywal || OFE.BTK || '' ?};
         ZK_N.CB:=OFE.CB;
         ZK_N.SPP:=OFE.SPP;
         ZK_N.GRATISY:=OFE.GRATISY;
         ZK_N.PL_FORCE:=ZK_N.T().PL_FORCE;
         ZK_N.PL_DIR:=ZK_N.T().PL_DIR;
         ZK_N.PL_DATA:=ZK_N.DT;
         ZK_N.PL_TIME:=time(0,0,0);

         PROJEKTY.cntx_psh();TYPYZAM.cntx_psh();PROJTYPY.cntx_psh();
         PROJEKTY.prefix();TYPYZAM.prefix();PROJTYPY.prefix();
         ZK_N.PROJEKTY:=
         {? OFE.PROJEKTY<>null & PROJEKTY.seek(OFE.PROJEKTY) & (ZK_N.T().PROJZAKR='Wszystkie' | (4+PROJEKTY.T().R)=(4+ZK_N.T().PROJZAKR))
         || OFE.PROJEKTY
         || null
         ?};
         PROJEKTY.cntx_pop();TYPYZAM.cntx_pop();PROJTYPY.cntx_pop();

         {? ZK_N.add() & ZK_N.r_lock(1)
         || exec('inf_dod','fakso',0,'zknag');
            FZ.FORMAPLA:=ZK_N.PL().KOD;
            FZ.PL:=ZK_N.PL;
            FZ.TERMPLAT:=exec('term_plat','faktury_plat','PL','N',ZK_N.ref());
            ZK_N.TZ:=FZ.TERMPLAT;
            exec('plat_one','faktury_plat',ZK_N.ref());

::          sprawdzenie uprawnien do magazynow
            BEER.ZAMUPR:=exec('jakieupr','zamsiw_wspolne');
::          obsluga memo
            _memo:=OFE.memo_get('r','OPIS');
            {? _memo.is_open || ZK_N.memo_put(_memo,'UW'); ZK_N.memo_get() ?};
            &_memo;
::          ustawiamy filtr
            MG.f_clear();
            MG.clear();
::          sprawdzam czy uprawnienia ogolne czy uzytkownika
            USERS_UP.cntx_psh();
            USERS_UP.index('MG');
            USERS_UP.prefix(OPERATOR.USER,'ZAM',ST.ODDZ_KOD);
            _upr_us:=USERS_UP.first();
            USERS_UP.cntx_pop();
            {? _upr_us
            ||
               MG.f_set('SYM,NAZ','join USERS_UP','USERS_UP.USERS=:_a and USERS_UP.AKR=\'ZAM\' and \"MG\".ODDZ=\':_b\''
                ,OPERATOR.USER,ST.ODDZ)
            ||
               USERS_UP.cntx_psh();
               MG.f_set('SYM,NAZ','','',OPERATOR.USER,ST.ODDZ);
               {? MG.f_first()
               || {!
                  |?
                     USERS_UP.index('MG');
                     USERS_UP.prefix(null,'ZAM',ST.ODDZ_KOD,MG.ref());
                     {? USERS_UP.first() || MG.f_next() || MG.f_del() ?}
                  !}
               ?};
               USERS_UP.cntx_pop()
            ?};
            __zmst:='';
            exec('ofp2zkp','!lsp_zkn_drzk',_ofe,ZK_N.ref());
            _var_zknagpop:=exec('var_zknagpop','zamsiw_nag');
            _var_zknagpop.ZK_N.get();
            _var_zknagpop.KH:=ZK_N.KH;
            _var_zknagpop.WAL:=ZK_N.WAL;
            _var_zknagpop.KRS:=ZK_N.KRS;
            _var_zknagpop.MG:=ZK_N.MG;
            _var_zknagpop.PL:=ZK_N.PL;
            _var_zknagpop.ODB:=ZK_N.ODB;
            _var_zknagpop.DT:=ZK_N.DT;
            _var_zknagpop.PL_DATA:=ZK_N.PL_DATA;
            _var_zknagpop.PL_TIME:=ZK_N.PL_TIME;
            _var_zknagpop.TRN:=ZK_N.TRN;
            _var_zknagpop.SAM:=ZK_N.POJAZD;
            _var_zknagpop.RAB:=ZK_N.RAB;
            _var_zknagpop.KRAJ_VAT:=ZK_N.KRAJ_VAT;
            _var_zknagpop.PROJEKTY:=ZK_N.PROJEKTY;
            _var_zknagpop.PO_FIRST:=0;

            params_set('context',params_get(),'var_zknagpop',_var_zknagpop);

            exec('zknag_win_edit_set','zamsiw_nag',,,1);
            _spr:='nie automatyczna';
            _nag_edit:=0;

            {? _auto
            || __only_u:=1;
               _spr:=params_exec('spr_nzam','zamsiw_nag');
               {? _spr=''
               || ZK_P.index('TYPN');
                  ZK_P.prefix('A','Z',ZK_N.ref(),1);
                  {? BEER.ZAMUPR
                  || {!
                     |? {? _spr='' & ZK_P.M().RODZ='T' & ZK_N.MG=null() & ZK_P.RMAG=null
                        || FUN.info('Wymagane wypełnienie magazynu do realizacji zamówienia '
                                    '(w nagłówku lub we wszystkich pozycjach).'@);
                           _spr:='MG'
                        |? _spr='' & ZK_P.M().RODZ='U'
                        || __only_u:=0
                        ?};
                        _spr='' & ZK_P.next()
                     !}
                  ?}
               ?};
               {? _spr=''
               || {? ZK_N.MG<>null & ZK_N.MG().U<>'T' & ~__only_u
                  || FUN.info('W nagłówku zamówienia podano magazyn\n'
                              'bez zastosowania usług na pozycjach dokumentów rozchodowych,\n'
                              'a dane zamówienie ma pozycje usługowe.\n'
                              'Należy usunąć lub zmienić zmienić magazyn w nagłówku zamówienia.'@);
                     _spr:='MG'
                  ?}
               ?};
               {? _spr=''
               || {? ZK_N.NR=0 || exec('fak_numer','numery','',0); exec('znak','numery','ZK_N'); ZK_N.put(1) ?};
                  {? ~exec('zam_akc','zamsiw_nag',_auto,_autoakc) || _spr:='nie można zakończyć' ?}
               ?}
            ?};
            {? _spr<>''
            || _nag_edit:=ZK_N.edit("_wyn:=params_exec('spr_nzam','zamsiw_nag');
                                     __only_u:=1;
                                     {? _wyn=''
                                     || ZK_P.index('TYPN');
                                        ZK_P.prefix('A','Z',ZK_N.ref(),1);
                                        {? ZK_P.first() & BEER.ZAMUPR
                                        || {!
                                           |? {? _wyn='' & ZK_P.M().RODZ='T' & ZK_N.MG=null() & ZK_P.RMAG=null()
                                              || FUN.info('Wymagane wypełnienie magazynu do realizacji zamówienia '
                                                          '(w nagłówku lub we wszystkich pozycjach).'@);
                                                 _wyn:='MG'
                                              |? _wyn='' & ZK_P.M ().RODZ='U'
                                              || __only_u:=0
                                              ?};
                                              _wyn='' & ZK_P.next()
                                           !}
                                        ?}
                                     ?};
                                     {? _wyn=''
                                     || {? ZK_N.MG<>null & ZK_N.MG().U<>'T' & ~__only_u
                                        || FUN.info('W nagłówku zamówienia podano magazyn\n'
                                            'bez zastosowania usług na pozycjach dokumentów rozchodowych,\n'
                                            'a dane zamówienie ma pozycje usługowe.\n'
                                            'Należy usunąć lub zmienić magazyn w nagłówku zamówienia.'@);
                                           _wyn:='MG'
                                        ?}
                                     ?};
                                     _wyn")
            ?};
            {? _nag_edit & _spr<>''
            || {? ZK_N.NR=0 || exec('fak_numer','numery','',0); exec('znak','numery','ZK_N'); ZK_N.put(1) ?};
               params_set('context',params_get(),'var_zknagpop',_var_zknagpop);
               {? ZK_N.STAT_REJ='N' || exec('zknag_pop_po','zamsiw_nag',_nag_edit,1) ?};
               exec('aktznzkn','zamsiw_nag',ZK_N.ref,1);
               exec('obl_warz','zamsiw_nag',ZK_N.ref,0,,0);
               exec('plat_przel','faktury_plat',ZK_N.ref())

            |? _spr=''
            || exec('aktznzkn','zamsiw_nag',ZK_N.ref,1);
               exec('obl_warz','zamsiw_nag',ZK_N.ref,0,,0);
               exec('plat_przel','faktury_plat',ZK_N.ref())

            || exec('zam_usu','zamsiw_nag',1)
            ?};
            ZK_N.r_unlock();
            MG.f_clear()
         ||
            FUN.info('Nieudana próba zablokowania tabeli nagłówków zamówień.\nSpróbuj ponownie.'@)
         ?};
         BEER.WW:=_ww;
         BEER.MW:='O'
      ?};
      OFE.r_unlock()
   ||
      {? FUN.ask('Dokument obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
      || OFE.r_unlock()
      ?}
   ?}
?};
VAR_DEL.delete('__smmag','__newmag');
VAR_DEL.delete('__matakt','__only_u');
OFE.cntx_pop();
1


\ofp2zkp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Kopiuje pozycje z oferty na zamówienie sprzedaży
::   WE: _a - ref oferty
::       _b - ref zamówienia
::----------------------------------------------------------------------------------------------------------------------
BEER.ZK_N:=_b;
BEER.WG:=BEER.ZK_N().WG;
BEER.TYP:='Z';
exec('st_licz_wz','ceny','ZK_N');
__only_u:=1;
do();
ZK_P.cntx_psh();
OFP.index('OFE_P');
OFP.prefix(_a);
{? OFP.first()
|| exec('ustaw_ww','eurusd','Z');
   _czywal:=exec('spr_ww','eurusd',BEER.WW<>2);
   BEER.CZY_REZT:=0;
   exec('ustazprz','zamsiw_poz');

   {!
   |? ZK_P.prefix();
      ZK_P.blank();
      ZK_P.T:='Z';
      ZK_P.N:=BEER.ZK_N;
      ZK_P.TOP:=1;
      ZK_P.M:=OFP.M;
      ZK_P.ILZ:=ZK_P.ILP:=OFP.IL;
      ZK_P.SV:=exec('m_vat','material',ZK_P.M,ZK_N.KH,,ZK_N.DP~1,ZK_N.DP~2,OFP.SV,2,!__zmst);
      _vat:=#((ZK_P.SV().KOD*'%')+ZK_P.SV().KOD-1)/100;
      ZK_P.CENA:=OFP.C*{? OFE.CB=ZK_N.CB
                       || 1
                       || {? OFE.CB='N' & ZK_N.CB='T' || 1+_vat || 1/(1+_vat) ?}
                       ?} $ZK_P.M().DOKL_S;
      ZK_P.RAB:=OFP.RAB;
      ZK_P.RABK:=OFP.RABK;
      ZK_P.TAR_H:=exec('tar_h_copy','ceny_dok',OFP.TAR_H,ZK_N.ODDZ+($(ZK_N.DP~1)+2));
      ZK_P.TAR_TMS:=OFP.TAR_TMS;
      ZK_P.PROMO:=OFP.PROMO;
      ZK_P.CWAL:={? _czywal
                 || OFP.CWAL*
                      {? OFE.CB=ZK_N.CB
                      || 1
                      || {? OFE.CB='N' & ZK_N.CB='T' || 1+_vat || 1/(1+_vat) ?}
                      ?}$ZK_P.M().DOKL_S
                 || 0
                 ?};
      ZK_P.WAL:={? _czywal || OFP.WAL || INFO.NAROD ?};
      ZK_P.KRS:={? _czywal || OFP.KRS || 0 ?};

      _war:={? ZK_P.M().RODZ='U' | (ZK_P.N().MG<>null & ZK_P.N().MG().PAL='T')
            || 'N'
            || {? date<(ZK_P.DT+1) || exec('get','#params',3001,2,OPERATOR.USER) || 'N' ?}
            ?};
      ZK_P.REZ:={? _war='T' || 1 |? _war='N' || 0 || 1 ?};
      ZK_P.JM:=ZK_P.M().J;
      {? ZK_P.M().J2<>null()
      || ZK_P.T2:='M';
         ZK_P.J2:=ZK_P.M().J2;
         ZK_P.WS2:=exec('oblWSP','jm',ZK_P.M);
         _dokl_s:=exec('jaka_dok_mjm','jm',ZK_P.M,ZK_P.J2,ZK_P.M().J);
         _dokl_m:=exec('jaka_dok_m','jm',ZK_P.M);
         {? _dokl_s<0 || _dokl_s:=_dokl_m ?};
         roundmet(BEER.MDOKL);
         ZK_P.IL2:=ZK_P.ILZ/ZK_P.WS2 $_dokl_s
      || ZK_P.T2:=(1+(1-BEER.ZMJM));
         ZK_P.WS2:=1;
         ZK_P.J2:=ZK_P.JM;
         ZK_P.IL2:=ZK_P.ILZ
      ?};
      ZK_P.POJAZD:=OFE.POJAZD;
      ZK_P.DK_C:=OFP.DK_C;
      ZK_P.ILRB:=ZK_P.ILRW:=ZK_P.ILRD:=ZK_P.DOR:=0;
      ZK_P.ZNB:=ZK_P.ZNW:=ZK_P.ZNR:=ZK_P.ZBB:=ZK_P.ZBZ:='N';
      ZK_P.CZYBEZ:='T';
      ZK_P.ILBEZ:=ZK_P.ILP;
      ZK_P.KH:=ZK_P.N().KH;
      ZK_P.KH_ODB:=ZK_P.N().ODB;

      PROJEKTY.cntx_psh();TYPYZAM.cntx_psh();PROJTYPY.cntx_psh();
      PROJEKTY.prefix();TYPYZAM.prefix();PROJTYPY.prefix();

      ZK_P.PROJEKTY:=
      {? OFP.PROJEKTY<>null & PROJEKTY.seek(OFP.PROJEKTY) & (ZK_N.T().PROJZAKR='Wszystkie' | (4+PROJEKTY.T().R)=(4+ZK_N.T().PROJZAKR))
      || OFP.PROJEKTY
      || null
      ?};
      PROJEKTY.cntx_pop();TYPYZAM.cntx_pop();PROJTYPY.cntx_pop();

      {? ZK_P.add(1)
      || {? __only_u & ZK_P.M().RODZ='T' || __only_u:=0 ?};
         ZK_P.NR:=exec('blnr_kol','zamsiw_poz',ZK_P.M);
         {? ZK_P.REZ
         || ZK_P.ILR:=ZK_P.ILP:=ZK_P.ILZ
         || ZK_P.ILP:=ZK_P.ILZ; ZK_P.ILR:=0
         ?};
         ZK_P.put(1);
         exec('inf_dod','fakso',0,'zkpoz');
         exec('admatzkp','rezerwacje',$ZK_P.M,ZK_P.NR,ZK_P.ILP,$ZK_P.N,$ZK_P.ref());
         {? ZK_P.REZ
         || ZK_P.cntx_psh();
            ZK_P.REZ:=0;
            exec('rez_pozy','rezerwacje',0,,,,,0);
            ZK_P.cntx_pop();
            ZK_P.get()
         ?};
         exec('war_pozz','zamsiw_poz');
         ZK_P.put(1);
         ZK_P.cntx_psh();
         exec('akt_rezy','rezerwacje');
         ZK_P.cntx_pop()
      ?};
      OFP.next()
   !}
?};
ZK_P.cntx_pop();
end()


\access
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=1;
{? var_pres('TYPYZAM',_in)=type_of(null()) & _in.TYPYZAM<>null()
|| BEER.TYPYZAM:=_in.TYPYZAM;
   _p3004:=exec('get','#params',3004,2,_user);
   {? form(_p3004)<>'' & ~((' '+_p3004)*(' '+BEER.TYPYZAM().T+' '))
   || _result:=0;
      BEER.TYPYZAM:=null()
   ?}
?};
_result


\ctrl_Ofe2Zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: sprawdzenie czy można wygenerować zamówienie do oferty
::   WE: _a - ref Oferty
::   WY: 1 - tak
::       0 - nie
::       -1 - nie bo już wygenerowano ofertę lub jej nie znaleziono
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
OFE.cntx_psh();
OFE.clear();
_ok:={? OFE.seek(_a) || 1 || 0 ?};

{? ~_ok
|| FUN.info('Oferta nie została odnaleziona.\nGeneracja zamówienia jest niemożliwa.'@);
   _wyn:=-1
|? OFE.STAT_REJ<>'T'
|| FUN.info('Oferta %1 nie została zaakceptowana.\nGeneracja zamówienia jest niemożliwa.'@[OFE.SYM]);
   _wyn:=0
|| {? var_pres('X_Xa')>0 || obj_del(X_Xa) ?};
   X_Xa:=sql('select ZK_N.REFERENCE ref from @ZK_N where ZK_N.OFE =  :_a ',OFE.ref());
   {? X_Xa.first()
   ||
      FUN.info('Do oferty %1 utworzono już zamówienie.\nPonowna generacja zamówienia jest niemożliwa.'@[OFE.SYM]);
      _wyn:=-1
   ?}
?};
OFE.cntx_pop();
_wyn


\zknag_zakoncz_zkp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Zakocz - okno wertowania - dla pozycji
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('btn_EndPosition','okienka','LSP_ZKN_DRZK',BEER.ZK_N().uidref(),ZK_N
 ,'Zakończ_wer','Zakończyć rejestrację zamówienia %1?'@[BEER.ZK_N().SYM])


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_out:=params_get().out;

{? var_pres('ZK_N',_out)=type_of(null()) & _out.ZK_N
|| ZK_N.cntx_psh();
   ZK_N.use(ref_name(_out.ZK_N));
   ZK_N.prefix();
   {? ZK_N.seek(_out.ZK_N)
   || __PARSES.setVal('OddzialLogProd',ZK_N.ODDZ)
   ?};
   ZK_N.cntx_pop()
?};

1


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zam_clean','zamsiw_nag',_mp,_in)


\zknag_reklam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Zamówienie sprzedaży - z reklamacji
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_ZKN_DRZK';
_params.AKCJA:='Reklamacja';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'REK_N',REK_N.ref());

exec('mp_run','#b__box',_params)


\zk_reklam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: tworzenie zamowienia na podstawie reklamacji
::   WE: [_a] - ref reklamacji
::       [_b] - czy automatyczna
::       [_c] - typ zamówienia
::       [_d] - 1-automatyczna akceptacja 0(domyślnie)-nie
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) || _rek_n:=_a || _rek_n:=REK_N.ref() ?};
{? var_pres('_b')=type_of(0) || _auto:=_b || _auto:=0 ?};
{? var_pres('_c')=type_of(null()) & _c<>null() || BEER.TYPYZAM:=_c || BEER.TYPYZAM:=null() ?};
_autoakc:={? var_pres('_d')=type_of(0) || _d || 0 ?};

REK_N.cntx_psh();
REK_N.clear();
_ok:={? REK_N.seek(_rek_n) || 1 || 0 ?};

VAR_DEL.delete('__matakt','__only_u','__rekndod');
__matakt:=tab_tmp(2,'REF','STRING[16]',''
           ,'NRK','INTEGER',''
           ,'ILR','REAL',''
           ,'ZKP','STRING[16]',''
           ,'ZKN','STRING[16]','');

__only_u:=1;

_ok:={? _ok || exec('ctrl_Rek2Zam','!lsp_zkn_drzk',_rek_n)=1 || 0 ?};

{? _ok=1
||
   exec('initREK_Ndod','reklamacje',REK_N.ref(),REK_N.KH,REK_N.KH_ODB);
   _ilerek:=__rekndod.size();
   {? ~_ilerek
   || FUN.info('Żadna reklamacja klienta nie została wybrana.\nZamówienie nie zostanie utworzone.'@)
   |? __rekndod.first() & (REK_N.prefix(); REK_N.seek(__rekndod.UIDREK); REK_N.r_lock(1,1,1))
   ||
      _rek_n:=REK_N.ref();
      {? (_auto
        | {? _ilerek=1
          || FUN.ask('Utworzyć zamówienie na podstawie reklamacji klienta %1?'@[REK_N.SYM])
          || FUN.ask('Utworzyć zamówienie na podstawie wybranych reklamacji klienta %1?'@[REK_N.KH().SKR])
          ?})
        & (BEER.TYPYZAM<>null() | exec('wyb_zam','zamowienia','Z',0)<>null)
      ||
         BEER.DOL:=1;
         HELP.POP:=0;
         {? BEER.TYPYZAM=null() || BEER.TYPYZAM:=TYPYZAM.ref() ?};
         POM.TAB:='ZK_N';
         POM.TYPDOK:=BEER.TYPYZAM().KOD;
         BEER.RMAG:=null;
         BEER.SZ:='S';
         ZAKR.T:='A'; ZAKR.WG:='O'; ZAKR.US:=OPERATOR.USER;
         ZK_N.prefix();
         ZK_N.blank();
         ZK_N.DP:=date();
         ZK_N.KH:=REK_N.KH;
         ZK_N.DT:=date()+14;
         {? ZK_N.DT=date(0,0,0) || ZK_N.DT:=ZK_N.DP+{? ZK_N.KH().DREA>0 || ZK_N.KH().DREA || 0 ?} ?};

         ZK_N.ODB:=REK_N.KH_ODB;

         _ww:=BEER.WW;
         exec('ustaw_ww','eurusd','Z');
         _czywal:=exec('spr_ww','eurusd',0);
         BEER.WW:=0;
         BEER.MW:='Z';
         ZK_N.WAL:=INFO.NAROD;
         ZK_N.SWAL:=2;
         ZK_N.KRS:=0;
         ZK_N.RTK:=0;
         ZK_N.NTK:='';
         ZK_N.DTK:=date(0,0,0);
         ZK_N.BTK:='';
         ZK_N.PL_FORCE:=ZK_N.T().PL_FORCE;
         ZK_N.PL_DIR:=ZK_N.T().PL_DIR;
         ZK_N.PL_DATA:=ZK_N.DT;
         ZK_N.PL_TIME:=time(0,0,0);
         _prj_all:=1;
         {? _ilerek<>1
         ||
            __rekndod.cntx_psh();REK_N.cntx_psh();
            REK_N.prefix();
            __rekndod.first();
            REK_N.seek(__rekndod.UIDREK);
            _projekty:=REK_N.PROJEKTY;
            {? __rekndod.next() & REK_N.seek(__rekndod.UIDREK)
            ||
               {!|?
                  {? _projekty<>REK_N.PROJEKTY || _prj_all:=0 ?};
                  _prj_all & __rekndod.next()
               !}
            ?};
            __rekndod.cntx_pop();REK_N.cntx_pop()
         ?};

         {? _prj_all
         ||
            PROJEKTY.cntx_psh();TYPYZAM.cntx_psh();PROJTYPY.cntx_psh();
            PROJEKTY.prefix();TYPYZAM.prefix();PROJTYPY.prefix();
            ZK_N.PROJEKTY:=
            {? REK_N.PROJEKTY<>null & PROJEKTY.seek(REK_N.PROJEKTY)
            & (ZK_N.T().PROJZAKR='Wszystkie' | (4+PROJEKTY.T().R)=(4+ZK_N.T().PROJZAKR))
            || REK_N.PROJEKTY
            || null
            ?};
            PROJEKTY.cntx_pop();TYPYZAM.cntx_pop();PROJTYPY.cntx_pop()
         ?};

         {? ZK_N.add() & ZK_N.r_lock(1)
         || exec('poz_kh','zamsiw_nag',1);
            exec('inf_dod','fakso',0,'zknag');
            {? ZK_N.NR=0 || exec('fak_numer','numery','',0); exec('znak','numery','ZK_N'); ZK_N.put(1) ?};
            FZ.FORMAPLA:=ZK_N.PL().KOD;
            FZ.PL:=ZK_N.PL;
            FZ.TERMPLAT:=exec('term_plat','faktury_plat','PL','N',ZK_N.ref());
            ZK_N.TZ:=FZ.TERMPLAT;
            exec('plat_one','faktury_plat',ZK_N.ref());

::          sprawdzenie uprawnien do magazynow
            BEER.ZAMUPR:=exec('jakieupr','zamsiw_wspolne');
::          obsluga memo
            _memo:=REK_N.memo_get('r','OPIS');
            {? _memo.is_open || ZK_N.memo_put(_memo,'UW'); ZK_N.memo_get() ?};
            &_memo;
::          ustawiamy filtr
            MG.f_clear();
            MG.clear();
::          sprawdzam czy uprawnienia ogolne czy uzytkownika
            USERS_UP.cntx_psh();
            USERS_UP.index('MG');
            USERS_UP.prefix(OPERATOR.USER,'ZAM',ST.ODDZ_KOD);
            _upr_us:=USERS_UP.first();
            USERS_UP.cntx_pop();
            {? _upr_us
            ||
               MG.f_set('SYM,NAZ','join USERS_UP','USERS_UP.USERS=:_a and USERS_UP.AKR=\'ZAM\' and \"MG\".ODDZ=\':_b\''
                ,OPERATOR.USER,ST.ODDZ)
            ||
               USERS_UP.cntx_psh();
               MG.f_set('SYM,NAZ','','',OPERATOR.USER,ST.ODDZ);
               {? MG.f_first()
               || {!
                  |?
                     USERS_UP.index('MG');
                     USERS_UP.prefix(null,'ZAM',ST.ODDZ_KOD,MG.ref());
                     {? USERS_UP.first() || MG.f_next() || MG.f_del() ?}
                  !}
               ?};
               USERS_UP.cntx_pop()
            ?};
            __zmst:='';
            exec('rek_n2zkp','!lsp_zkn_drzk',REK_N.ref(),ZK_N.ref());
            _var_zknagpop:=exec('var_zknagpop','zamsiw_nag');
            _var_zknagpop.ZK_N.get();
            _var_zknagpop.KH:=ZK_N.KH;
            _var_zknagpop.WAL:=ZK_N.WAL;
            _var_zknagpop.KRS:=ZK_N.KRS;
            _var_zknagpop.MG:=ZK_N.MG;
            _var_zknagpop.PL:=ZK_N.PL;
            _var_zknagpop.ODB:=ZK_N.ODB;
            _var_zknagpop.DT:=ZK_N.DT;
            _var_zknagpop.PL_DATA:=ZK_N.PL_DATA;
            _var_zknagpop.PL_TIME:=ZK_N.PL_TIME;
            _var_zknagpop.TRN:=ZK_N.TRN;
            _var_zknagpop.SAM:=ZK_N.POJAZD;
            _var_zknagpop.RAB:=ZK_N.RAB;
            _var_zknagpop.KRAJ_VAT:=ZK_N.KRAJ_VAT;
            _var_zknagpop.PROJEKTY:=ZK_N.PROJEKTY;
            _var_zknagpop.PO_FIRST:=0;

            params_set('context',params_get(),'var_zknagpop',_var_zknagpop);

            exec('zknag_win_edit_set','zamsiw_nag',,,1);
            _spr:='nie automatyczna';
            _nag_edit:=0;

            {? _auto
            || __only_u:=1;
               _spr:=params_exec('spr_nzam','zamsiw_nag');
               {? _spr=''
               || ZK_P.index('TYPN');
                  ZK_P.prefix('A','Z',ZK_N.ref(),1);
                  {? BEER.ZAMUPR
                  || {!
                     |? {? _spr='' & ZK_P.M().RODZ='T' & ZK_N.MG=null() & ZK_P.RMAG=null
                        || FUN.info('Wymagane wypełnienie magazynu do realizacji zamówienia '
                                    '(w nagłówku lub we wszystkich pozycjach).'@);
                           _spr:='MG'
                        |? _spr='' & ZK_P.M().RODZ='U'
                        || __only_u:=0
                        ?};
                        _spr='' & ZK_P.next()
                     !}
                  ?}
               ?};
               {? _spr=''
               || {? ZK_N.MG<>null & ZK_N.MG().U<>'T' & ~__only_u
                  || FUN.info('W nagłówku zamówienia podano magazyn\n'
                              'bez zastosowania usług na pozycjach dokumentów rozchodowych,\n'
                              'a dane zamówienie ma pozycje usługowe.\n'
                              'Należy usunąć lub zmienić zmienić magazyn w nagłówku zamówienia.'@);
                     _spr:='MG'
                  ?}
               ?};
               {? _spr=''
               || {? ZK_N.NR=0 || exec('fak_numer','numery','',0); exec('znak','numery','ZK_N'); ZK_N.put(1) ?};
                  {? ~exec('zam_akc','zamsiw_nag',_auto,_autoakc) || _spr:='nie można zakończyć' ?}
               ?}
            ?};
            {? _spr<>''
            || _nag_edit:=ZK_N.edit("_wyn:=params_exec('spr_nzam','zamsiw_nag');
                                     __only_u:=1;
                                     {? _wyn=''
                                     || ZK_P.index('TYPN');
                                        ZK_P.prefix('A','Z',ZK_N.ref(),1);
                                        {? ZK_P.first() & BEER.ZAMUPR
                                        || {!
                                           |? {? _wyn='' & ZK_P.M().RODZ='T' & ZK_N.MG=null() & ZK_P.RMAG=null()
                                              || FUN.info('Wymagane wypełnienie magazynu do realizacji zamówienia '
                                                          '(w nagłówku lub we wszystkich pozycjach).'@);
                                                 _wyn:='MG'
                                              |? _wyn='' & ZK_P.M ().RODZ='U'
                                              || __only_u:=0
                                              ?};
                                              _wyn='' & ZK_P.next()
                                           !}
                                        ?}
                                     ?};
                                     {? _wyn=''
                                     || {? ZK_N.MG<>null & ZK_N.MG().U<>'T' & ~__only_u
                                        || FUN.info('W nagłówku zamówienia podano magazyn\n'
                                            'bez zastosowania usług na pozycjach dokumentów rozchodowych,\n'
                                            'a dane zamówienie ma pozycje usługowe.\n'
                                            'Należy usunąć lub zmienić magazyn w nagłówku zamówienia.'@);
                                           _wyn:='MG'
                                        ?}
                                     ?};
                                     _wyn")
            ?};
            {? _nag_edit & _spr<>''
            || {? ZK_N.NR=0 || exec('fak_numer','numery','',0); exec('znak','numery','ZK_N'); ZK_N.put(1) ?};
               params_set('context',params_get(),'var_zknagpop',_var_zknagpop);
               {? ZK_N.STAT_REJ='N' || exec('zknag_pop_po','zamsiw_nag',_nag_edit,1) ?};
               exec('aktznzkn','zamsiw_nag',ZK_N.ref,1);
               exec('obl_warz','zamsiw_nag',ZK_N.ref,0,,0);
               exec('plat_przel','faktury_plat',ZK_N.ref());
               _opis:='Zamówienie sprzedaży z reklamacji klienta';
               _uidz:=ZK_N.uidref();
               _ndx:=__rekndod.ndx_tmp('',0,'ZKP',,);
               ZK_P.index('TYPN');
               ZK_P.prefix('A','Z',ZK_N.ref(),1);
               {? ZK_P.first()
               || {!
                  |? __rekndod.index(_ndx);
                     __rekndod.prefix(ZK_P.uidref());
                     {? __rekndod.first()
                     || _rek_nn:=exec('FindAndGet','#table',REK_N,__rekndod.UIDREK,,,null())
                     || _rek_nn:=_rek_n
                     ?};
                     exec('autoRekD','reklamacje',_rek_nn,_uidz,ZK_P.uidref(),ZK_P.ILZ,_opis,3);
                     ZK_P.next()
                  !}
               ?}
;
               __rekndod.ndx_drop(_ndx)

            |? _spr=''
            || exec('aktznzkn','zamsiw_nag',ZK_N.ref,1);
               exec('obl_warz','zamsiw_nag',ZK_N.ref,0,,0);
               exec('plat_przel','faktury_plat',ZK_N.ref())

            || exec('zam_usu','zamsiw_nag',1)
            ?};
            ZK_N.r_unlock();
            MG.f_clear()
         ||
            FUN.info('Nieudana próba zablokowania tabeli nagłówków zamówień.\nSpróbuj ponownie.'@)
         ?};
         BEER.WW:=_ww;
         BEER.MW:='O'
      ?};
      REK_N.r_unlock()
   ||
      {? FUN.ask('Dokument obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@) & REK_N.r_lock()
      || REK_N.r_unlock()
      ?}
   ?}
?};
VAR_DEL.delete('__smmag','__newmag','__rekndod');
VAR_DEL.delete('__matakt','__only_u');
REK_N.cntx_pop();
1


\rek_n2zkp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kopiuje pozycje z reklamacji na zamówienie sprzedaży
::   WE: _a - ref reklamacji
::       _b - ref zamówienia
::----------------------------------------------------------------------------------------------------------------------
_rek_n:=_a;
BEER.ZK_N:=_b;
BEER.WG:=BEER.ZK_N().WG;
BEER.TYP:='Z';
exec('st_licz_wz','ceny','ZK_N');
__only_u:=1;
do();
ZK_P.cntx_psh();
REK_N.cntx_psh();
__rekndod.clear();
{? __rekndod.first()
|| {!
   |? _blk:=0;
      REK_N.prefix();
      {? REK_N.seek(__rekndod.UIDREK) & REK_N.M<>null()
       & {? _rek_n<>REK_N.ref() || _blk:=REK_N.r_lock(1,1,1); _blk || 1 ?}
      || exec('ustaw_ww','eurusd','Z');
         _czywal:=exec('spr_ww','eurusd',BEER.WW<>2);
         BEER.CZY_REZT:=0;
         exec('ustazprz','zamsiw_poz');

         ZK_P.prefix();
         ZK_P.blank();
         ZK_P.T:='Z';
         ZK_P.N:=BEER.ZK_N;
         ZK_P.TOP:=1;
         ZK_P.M:=REK_N.M;
         ZK_P.ILZ:=ZK_P.ILP:=REK_N.IL;
         ZK_P.SV:=exec('m_vat','material',ZK_P.M,ZK_N.KH,,ZK_N.DP~1,ZK_N.DP~2,ZK_P.M().VAT,2,!__zmst);
         _vat:=#((ZK_P.SV().KOD*'%')+ZK_P.SV().KOD-1)/100;
         ZK_P.CENA:=0;
         ZK_P.CWAL:=0;
         ZK_P.WAL:=INFO.NAROD;
         ZK_P.KRS:=0;

         _war:={? ZK_P.M().RODZ='U' | (ZK_P.N().MG<>null & ZK_P.N().MG().PAL='T')
               || 'N'
               || {? date<(ZK_P.DT+1) || exec('get','#params',3001,2,OPERATOR.USER) || 'N' ?}
               ?};
         ZK_P.REZ:={? _war='T' || 1 |? _war='N' || 0 || 1 ?};
         ZK_P.JM:=ZK_P.M().J;
         ZK_P.T2:=(1+(1-BEER.ZMJM));
         ZK_P.WS2:=1;
         ZK_P.J2:=ZK_P.JM;
         ZK_P.IL2:=ZK_P.ILZ;
::       ZK_P.DK_C:=null();
         ZK_P.ILRB:=ZK_P.ILRW:=ZK_P.ILRD:=ZK_P.DOR:=0;
         ZK_P.ZNB:=ZK_P.ZNW:=ZK_P.ZNR:=ZK_P.ZBB:=ZK_P.ZBZ:='N';
         ZK_P.CZYBEZ:='T';
         ZK_P.ILBEZ:=ZK_P.ILP;
         ZK_P.KH:=ZK_P.N().KH;
         ZK_P.KH_ODB:=ZK_P.N().ODB;
         PROJEKTY.cntx_psh();TYPYZAM.cntx_psh();PROJTYPY.cntx_psh();
         PROJEKTY.prefix();TYPYZAM.prefix();PROJTYPY.prefix();
         ZK_P.PROJEKTY:=
         {? REK_N.PROJEKTY<>null & (REK_N.PROJEKTY();(ZK_N.T().PROJZAKR='Wszystkie' | (4+PROJEKTY.T().R)=(4+ZK_N.T().PROJZAKR)))
         || REK_N.PROJEKTY
         || null
         ?};
         PROJEKTY.cntx_pop();TYPYZAM.cntx_pop();PROJTYPY.cntx_pop();
         exec('f3_ceny','zamsiw_poz',0);
         exec('czy_rwal','eurusd','Z');
         {? ZK_P.add(1)
         || {? __only_u & ZK_P.M().RODZ='T' || __only_u:=0 ?};
            ZK_P.NR:=exec('blnr_kol','zamsiw_poz',ZK_P.M);
            {? ZK_P.REZ
            || ZK_P.ILR:=ZK_P.ILP:=ZK_P.ILZ
            || ZK_P.ILP:=ZK_P.ILZ; ZK_P.ILR:=0
            ?};
            ZK_P.put(1);
            exec('inf_dod','fakso',0,'zkpoz');
            exec('admatzkp','rezerwacje',$ZK_P.M,ZK_P.NR,ZK_P.ILP,$ZK_P.N,$ZK_P.ref());
            {? ZK_P.REZ
            || ZK_P.cntx_psh();
               ZK_P.REZ:=0;
               exec('rez_pozy','rezerwacje',0,,,,,0);
               ZK_P.cntx_pop();
               ZK_P.get()
            ?};
            exec('war_pozz','zamsiw_poz');
            ZK_P.put(1);
            ZK_P.cntx_psh();
            exec('akt_rezy','rezerwacje');
            ZK_P.cntx_pop();
            __rekndod.ZKP:=ZK_P.uidref();
            __rekndod.put(1)
         ?}
      ?};
      {? _blk || REK_N.r_unlock() ?};
      __rekndod.next()
   !}
?};
ZK_P.cntx_pop();
REK_N.cntx_pop();
end()


\ctrl_Rek2Zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: sprawdzenie czy można wygenerować zamówienie do reklamacji
::   WE: _a - ref Reklamacji
::   WY: 1 - tak
::       0 - nie
::       -1 - nie bo już wygenerowano ofertę lub jej nie znaleziono
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
REK_N.cntx_psh();
REK_N.clear();
_ok:={? REK_N.seek(_a) || 1 || 0 ?};

{? ~_ok
|| FUN.info('Reklamacja klienta nie została odnaleziona.\nGeneracja zamówienia jest niemożliwa.'@);
   _res:=-1
|? REK_N.STAT_REJ='N'
|| FUN.info('Reklamacja klienta %1 nie została zaakceptowana.\nGeneracja zamówienia jest niemożliwa.'@[REK_N.SYM]);
   _res:=0
|? REK_N.M=null()
|| FUN.info('Reklamacja klienta %1 nie dotyczy materiału lub usługi.\nGeneracja zamówienia jest niemożliwa.'@[REK_N.SYM]);
   _res:=0
|? REK_N.IL<=0
|| FUN.info('Nie można wygenerować zamówienia dla reklamacji z ilością zerową lub mniejszą od zera.'@);
   _res:=0
?};
REK_N.cntx_pop();
_res


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 6c24d202d8793b3d07ebb984cbfabfc7951b8fb539e6ba9df6ed229aab3820d488a7fc9e36c2fb043bc59a93dae794ca3f1b30ff3e3da452d1324f166f86359ba248ca5ccec28624ac259ee8275c49a6587e79ecf970eaf0419a78cb24fd40910dd59dd4d9f0842a8a0a3b1fe4e7dadf4a321f0b3d3bbc2ba025cb7b4d97c979

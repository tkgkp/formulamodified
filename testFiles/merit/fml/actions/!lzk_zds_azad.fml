:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zds_azad.fml
:: Utworzony: 18.09.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Akceptacja zamówienia dostaw
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LZK
::# parses=exec('parses','!lzk_zds_azad')
::# kind=WE,   symbol=ZD_NAG,    type=_ZD_NAG,  name=Zamówienie dostaw,    required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

exec('init_zds','lzk');

:: WSTĘPNE WALIDACJE
_clean_result:=params_exec('clean','!lzk_zds_azad',_mp,_in);
_can_continue:=_clean_result.RESULT;
{? _can_continue=0
|| return()
?};

_akcja:=_mp.akcja();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & _mp.isAutoRun());

{? ~(var_pres('ZD_NAG',_in)=type_of(null()) & _in.ZD_NAG)
|| _mp.error('Brak wymaganego parametru ZD_NAG.')
|? ref_name(_in.ZD_NAG)<>ZD_NAG.name()
|| _mp.error('Zamówienie jest w archiwum. Akceptacja niedostępna.')
|| ZD_NAG.cntx_psh();
   ZD_NAG.prefix();
   {? ~ZD_NAG.seek(_in.ZD_NAG)
   || _mp.error('Nie znaleziono zamówienia.')
   || params_set('mp',_mp);

      {? _akcja='Zamknij'
      || exec('akceptuj','!lzk_zds_azad',,1)
      |? _akcja='Akceptuj' | _auto
      || exec('akceptuj','!lzk_zds_azad',_auto)
      |? _mp.pathTodo()
         | _mp.pathArea() & _akcja=''
      || _win_red:=exec('zdnag_win_edit','zamdst_nag','RED');
         _ff:="params_exec('zdnag_pozycje_todo','!lzk_zds_azad')";
         ZD_NAG.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
         _ff:="params_exec('zdnag_akceptuj_todo','!lzk_zds_azad'); 'key:Esc'";
         ZD_NAG.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
         _ff:="'key:Esc'";
         ZD_NAG.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
         ZD_NAG.win_edit(_win_red);
         exec('set_efld_opt','zamdst_nag',_win_red);
         ZD_NAG.display();
::       Usunięcie definicji tymczasowych okien
         ZD_NAG.win_edit(''); ZD_NAG.win_edel(_win_red)
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   ZD_NAG.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZD_NAG',_in)<>type_of(~~) & _in.ZD_NAG
|| 'Zaakceptuj zamówienie dostaw: %1'@@[exec('record','#to_string',_in.ZD_NAG)]
|| ''
?}


\zdnag_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zakupu - Akceptuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('zdnag_akceptuj','zamdst_nag')


\zdnag_akceptuj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zakupu - Akceptuj z todo
::   WE: params_get()   - ustawiane w exec('main','!lzk_zds_azad')
::----------------------------------------------------------------------------------------------------------------------
params_exec('akceptuj','!lzk_zds_azad')


\zdnag_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zakupu - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('zd_poz','zamdst_poz',1);
''


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zamówienie dostaw - Zakończenie rejestracji
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       [_b] - 1-zamknięcie 0-akceptacja
::   WE: params_get()   - ustawiane w exec('main','!lzk_zds_azad')
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_end:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_mp:=params_get().mp;

_result:=0;
{? ZD_NAG.STAT_REJ='T' & _end
|| _result:=exec('zd_stan','zamdst_nag','Zamknij');

   {? _result
   || _mp.done()
   ?}
|? ZD_NAG.STAT_REJ='T'
|| {? ~_auto || FUN.info('Zamówienie zostało już zaakceptowane.'@) ?};
   _result:=1;
   _mp.done()
|? ZD_NAG.STAT_REJ='N'
|| FUN.info('Nie zakończono jeszcze rejestracji zamówienia.\nAkceptacja niemożliwa.'@);
   _mp.error('Nie zakończono jeszcze rejestracji zamówienia. Akceptacja niemożliwa.');
   _result:=1
|? ZD_NAG.T().VALACC<>null() & ~exec('validate','wzorce',ZD_NAG.T().VALACC,ZD_NAG,ZD_NAG.ref())
|| _result:=0
|| {? _auto
   || ZD_NAG.STAN:=ZD_NAG.ST:='A';
      ZD_NAG.PR:=exec('obl_limi','zamdst_nag',ZD_NAG.ref());
      ZD_NAG.STAT_REJ:='T';
      ZD_NAG.BL_STAT:=exec('zdnag_blstat_bl','zamdst_nag');
      _result:=ZD_NAG.put();
      {? _result || exec('aktu_zam','zamdst_wspolne',ZD_NAG.ref()); ZD_NAG.get() ?}
   || _result:=exec('zd_stan','zamdst_nag','Akceptuj')
   ?};

   {? _result
   || _mp.done()
   ?}
?};

_result


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;


{? var_pres('ZD_NAG',_in)=type_of(null()) & _in.ZD_NAG
|| ZD_NAG.cntx_psh();
   ZD_NAG.use(ref_name(_in.ZD_NAG));
   ZD_NAG.prefix();
   {? ZD_NAG.seek(_in.ZD_NAG)
   || __PARSES.setVal('OddzialLogProd',ZD_NAG.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LZK';
      _args.AR:=ZD_NAG.DATA~1;
      _args.AM:=ZD_NAG.DATA~2;
      __PARSES.setVal('OkresRok',_args)
   ?};
   ZD_NAG.cntx_pop()
?};

1


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zd_clean','zamdst_nag',_mp,_in)


\zdnag_zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie dostaw - Zamknij
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LZK_ZDS_AZAD';
_params.UIDREF:=ZD_NAG.uidref();
_params.AKCJA:='Zamknij';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZD_NAG',ZD_NAG.ref());

exec('mp_run','#b__box',_params);

exec('zakr_set','zamdst_nag')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 d7c2351596b5a0b960eb7d92028be799f2a0683f128eddbae11755cdee00f2ee0d97e3ac0906fd602c5204ee5bf16e7b6d5e7f52a18151be8c7b6f5bb18716e096caf54bb76347f5b44bf4d22832b95d90f261c7ecdbff5fb6be464dce2817a43de5c877162d20b5586cdcf3365bd3e5bed1483f8b658993405c2c805e58d513

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !poc_ocp_rpak.fml
:: Utworzony: 22.08.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności POC_OCP_RPAK - Otwarcie sesji ocen.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Otwarcie sesji ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=ZO_PROC, type=_ZO_PROC, name=Wskazanie sesji ocen, required=T, keyref=T
::
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, OTWARTA, REZYGNACJA, FORMULARZE)", required=N
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_out:=_par.out;

:: Jedyny parametr wejściowy ma ustawioną flagę keyref=T, co oznacza, że przekazana wartość automatycznie stanie się
:: rekordem kluczowym.
_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error('Nieprawidłowy parametr wejściowy.'@);
   return()
?};

_ret:=exec('otworz','!poc_ocp_rpak',_keyRefs[1],~_mp.isMicro());

{? _ret.errmsg<>''
|| FUN.emsg(_ret.errmsg);
   _mp.error(_ret.errmsg)

|? _ret.status='X'
|| _mp.keep()

|| _out.RESULT:=_ret.status;
   _mp.save(,_out);
   _mp.done()
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Otwarcie sesji ocen - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_keyRefs:=_mp.getRefs();
_ref:={? var_pres('[1]',_keyRefs) || _keyRefs[1] || _mp.load(exec('kind_in','#b_port')).ZO_PROC ?};
_tab:=exec('desc_tab','phr_dane');
ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROC.cntx_psh();
ZO_PROC.prefix();
{? ZO_PROC.seek(_ref)
|| _tab.ZAW_DANE:='T';
   _tab.NAZWA:=ZO_PROC.ZO_PROG().NAZWA;
   _tab.RODZAJ:=ZO_PROC.ZO_PROG().RODZAJ;
   _tab.OKRES_OD:=$ZO_PROC.OKRES_OD;
   _tab.OKRES_DO:=$ZO_PROC.OKRES_DO
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();
{? _tab.ZAW_DANE='T'
|| 'W ramach programu "%1" (%2) otwórz sesję ocen od %3 do %4.'@@[_tab.NAZWA,_tab.RODZAJ,_tab.OKRES_OD,_tab.OKRES_DO]
|| 'Otwórz sesję ocen'@@
?}


\otworz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Główna formuła zajmująca się otwarciem sesji ocen.
::   WE: _a [REFERENCE] - Wskazanie sesji icen.
::       _b [NUMBER]    - Czy obsługa w procesie? [1/0]
::   WY: Tablica nazwana m.in. ze statusem operacji.
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_proces:=_b;

_ret:=obj_new('errmsg','status');
_ret.errmsg:='';
_ret.status:='';

ZO_PROG.cntx_psh();
ZO_PROG.prefix();
ZO_PROC.cntx_psh();
ZO_PROC.f_clear();
ZO_PROC.prefix();
{? ZO_PROC.seek(_ref)
|| {? ZO_PROC.A='T'
   || _ret.status:='OTWARTA'
   || _TEST:=sql(
         'select count(*) as ERR '
         'from   ZO_TEST join ZO_OSOBA '
         'where  ZO_OSOBA.ZO_PROC=:_a and ZO_TEST.L_FORM<>1',
         ZO_PROC.ref()
      );
      _error:=sql(
         'select * '
         'from ZO_FORM join ZO_TEST using(ZO_FORM.ZO_TEST,ZO_TEST.REFERENCE) join '
         'ZO_OSOBA using(ZO_TEST.ZO_OSOBA,ZO_OSOBA.REFERENCE) '
         'where ZO_FORM.VALID=\'N\' and ZO_OSOBA.ZO_PROC=:_a',
         ZO_PROC.ref()
      );
      {? _TEST.first() & _TEST.ERR>0 | _error.size()
::       Błędy w formularzach uniemożliwiają otwarcie. Nie zgłaszamy błędu (errmsg), ale pozostawiamy result.
      || FUN.emsg(
            'Wykryto błędy na liście formularzy ocen pracowników.\n'
            'Otwarcie sesji ocen nie jest możliwe.'@
         );
         _ret.status:='FORMULARZE'
      || _akcja:=exec('dialog','!poc_ocp_rpak',_proces);
         {? _akcja='T'
         || ZO_PROC.A:='T';
            ZO_PROC.Z:='N';
            {? ZO_PROC.put()
            || _ret.status:='OK'
            || _ret.errmsg:='Otwarcie sesji ocen nie powiodło się.'
            ?}
         |? _akcja='N'
         || _ret.status:='REZYGNACJA'
         || _ret.status:='X'
         ?}
      ?}
   ?}
:: Brak rekordu przekazanego parametrem.
|| _ret.errmsg:='Otwarcie sesji ocen niemożliwe.'
?};
ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();

_ret


\dialog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła przygotowuje okno redagowania.
::   WE: _a [NUMBER] - Czy obsługa w procesie? [1/0]
::   WY: T / N / X
::----------------------------------------------------------------------------------------------------------------------
_proces:=_a;

{? _proces
|| _ret:=obj_new(1);
   _ret[1]:='X';
   params_set('ret',_ret);

   _info:='Otwarcie sesji ocen'@;
   _we:=ZO_PROC.mk_edit(_info);
   ZO_PROC.win_esep(_we,'Program oceny'@);
   ZO_PROC.win_efld(_we,ZO_PROG,'NAZWA',,,40,,,,,MS.comment(ZO_PROG,'NAZWA'));
   ZO_PROC.win_efld(_we,ZO_PROG,'RODZAJ',,,,,,,,'Rodzaj oceny'@,'radio-buttons',,
      'Roczna'@,"'R'",
      'Półroczna'@,"'P'",
      'Kwartalna'@,"'K'",
      'Miesięczna'@,"'M'",
      'Tygodniowa'@,"'T'",
      'Ad hoc'@,"'A'"
   );
   ZO_PROC.win_ecol(_we);
   ZO_PROC.win_esep(_we,'Sesja ocen'@);
   ZO_PROC.win_efld(_we,,'R',,,5,,,,,MS.comment(ZO_PROC,'R'));
   ZO_PROC.win_efld(_we,,'M',,,5,,,,,MS.comment(ZO_PROC,'M'));
   ZO_PROC.win_efld(_we,,'OKRES_OD',,,,,,,,MS.comment(ZO_PROC,'OKRES_OD'));
   ZO_PROC.win_efld(_we,,'OKRES_DO',,,,,,,,MS.comment(ZO_PROC,'OKRES_DO'));
   ZO_PROC.win_efld(_we,,'PLAN_OD',,,,,,,,MS.comment(ZO_PROC,'PLAN_OD'));
   ZO_PROC.win_efld(_we,,'PLAN_DO',,,,,,,,MS.comment(ZO_PROC,'PLAN_DO'));

   _p1:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['Otwórz'@],
      "params_get().ret[1]:='T'; 'key:F2'");
   ZO_PROC.btn_eopt(_we,_p1,'tooltip="%1"' ['Otwarcie wskazanej sesji ocen i zakończenie bieżącej czynności'@]);
   _p2:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['&Nie otwieraj'@],
      "params_get().ret[1]:='N'; 'key:F2'");
   ZO_PROC.btn_eopt(_we,_p2,'tooltip="%1"' ['Zakończenie czynności bez otwarcia wskazanej sesji ocen'@]);
   _p3:=ZO_PROC.win_ebtn(_we,'text=%1,display=1' ['&Anuluj'@],'key:Esc');
   ZO_PROC.btn_eopt(_we,_p3,'tooltip="%1",default=1' ['Odłożenie czynności na listę zadań'@]);

   ZO_PROC.win_edit(_we);

   ZO_PROC.ZO_PROG();
   ZO_PROC.display(,,_info);

   _ret[1]

:: Mikroproces
|| {? {? ZO_PROC.Z='N'
      || FUN.ask('Czy na pewno otworzyć sesję ocen?'@)
      || FUN.ask('Czy na pewno anulować zamknięcie sesji ocen?'@)
      ?}
   || 'T'
   || 'N'
   ?}

?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 17f1c66da1f5849957942af53283f45f7b2294f3e744b70030b02a450dab34d770b71d129eabea3efa0a768b1c75065931e0088c95d6236e558324d15bee0b029ccbe3b347c074702d2be4c6a8e787cdb9c4bd81bd4a1e84bf7cd7d6d866e6b9e89dccad3fb85d3bd25a6f0b36a06ccac26b30d59353f096ad1ec8288bb0135e

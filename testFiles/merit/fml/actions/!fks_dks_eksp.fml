:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_eksp.fml
:: Utworzony: 27.03.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_EKSP - Księgowanie próbne dokumentu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Księgowanie próbne dokumentu - główna formuła czynności FKS_DKS_EKSP.
::----------------------------------------------------------------------------------------------------------------------
::# parses=exec('parses_dok','dok_fks')
::# access=exec('uprawnienia','!fks_dks_eksp')
::# kind=WE, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=T, keyref=T
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=T
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
::# permissions=FJKS,FREJ
:: WARUNKI BRAMY:
::# condition=Księgowanie końcowe, act_uid=FKS_DKS_EKSZ, auto=T, formula=_a.DOK<>null

:: parametr wyjściowy status - jeśli ma wartość 'R' to dokument jest na etapie rejestracji
::                             wpw ma wartość ''
_mp:=params_get().mp;
_akcja:=_mp.akcja();
_auto:=(_akcja='AkceptujAuto' | _mp.isAutoRun());

_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('DOK',_we)>0
|| {? _we.DOK=null
   || {? ~_auto
      || FUN.info('Nie znaleziono dokumentu księgowego.'@)
      ?};
      _wy:=params_get().out;
      _wy.DOK:=null;
      {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
      _mp.save(,_wy);
      _mp.done()
   || _ref:=BB.sqlint($_we.DOK); _nam:=form(($_we.DOK)-8);
      {? _ref<>0 & _nam<>''
      || DOK.cntx_psh();
         DOK.use(_nam);
         DOK.prefix();
         {? DOK.seek(_ref,_nam)
         || _maski:=_mp.pathTodo() & ~_auto;
            _ok:=exec('dok_ks_chk','dok_fks','P',_maski);
            {? _ok=0
            || _wy:=params_get().out;
               _wy.DOK:=DOK.ref();
               {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='R' ?};
               _mp.save(,_wy);
               _mp.done()
            || {? _maski & _ok=1
               || BtnKsg:=0;
                  exec('set_okno_todo','!fks_dks_eksp');
                  _ok:=BtnKsg;
                  VAR_DEL.delete('BtnKsg')
               ?};
               {? _ok
               || _wyn:=exec('ks_dok1','dok_fks_ks',~_mp.pathTodo() & ~_auto,~_auto);
                  {? _wyn
                  || _wyn:=0;
                     DOK.cntx_psh(); _refsql:=$DOK.ref();
                     DOK.index('DOKZRODL'); DOK.prefix(_refsql,_refsql);
                     {? DOK.first()
                     || _wyn:=1;
                        {! |?
                           _wyn:=exec('ks_dok1','dok_fks_ks',0);
                           _wyn & DOK.next()
                        !}
                     || _wyn:=1
                     ?};
                     DOK.cntx_pop()
                  ?};
                  {? _wyn
                  || _wy:=params_get().out;
                     _wy.DOK:={? _wyn=1 || DOK.ref() || null ?};
                     {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
                     _mp.save(,_wy);
                     _mp.done()
                  ?}
               ?}
            ?}
         || FUN.info('Nie znaleziono dokumentu księgowego.'@);
            _wy:=params_get().out;
            _wy.DOK:=null;
            {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
            _mp.save(,_wy);
            _mp.done()
         ?};
         DOK.cntx_pop()
      ?}
   ?}
|| {? ~_mp.isAutoRun()
   || FUN.info('Nie znaleziono dokumentu księgowego.'@)
   ?};
   _wy:=params_get().out;
   _wy.DOK:=null;
   {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
   _mp.save(,_wy);
   _mp.done()
?};
1


\n_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Czynność FKS_DKS_EKSP - formuła główna w wersji nieprocesowej
::----------------------------------------------------------------------------------------------------------------------
:: Parametry wejściowe identyczne jak w wersji procesowej
_args:=params_get();
_in:=_args.in;
_akcja:=_args.akcja;
_auto:=_akcja='AkceptujAuto';

{? type_of(_in)=type_of(~~) | var_pres('DOK',_in)<=type_of(~~)
|| FUN.emsg('Brak wymaganego parametru DOK'@)
|| _grp:=DOK.sel_size()>0;
   _ref:=BB.sqlint($_in.DOK); _nam:=form(($_in.DOK)-8);
   {? _ref<>0 & _nam<>''
   || DOK.cntx_psh();
      DOK.use(_nam);
      DOK.prefix();
      {? ~DOK.seek(_ref,_nam)
      || {? ~_auto || FUN.error('Nie znaleziono dokumentu księgowego.'@) ?}
      |? exec('dok_ks_chk','dok_fks','P',~_grp)
      || {? exec('ks_dok1','dok_fks_ks',~_auto,~_auto)
         || DOK.cntx_psh(); _refsql:=$DOK.ref();
            DOK.index('DOKZRODL'); DOK.prefix(_refsql,_refsql);
            {? DOK.first()
            || {! |? exec('ks_dok1','dok_fks_ks',0) & DOK.next() !}
            ?};
            DOK.cntx_pop()
         ?}
      ?};
      DOK.cntx_pop()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie dokumentów księgowych - formuła na opis w TODO
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zaksięguj próbnie dokument'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('DOK',_we)
|| _ref:=BB.sqlint($_we.DOK); _nam:=form(($_we.DOK)-8);
   {? _ref<>0 & _nam<>''
   || DOK.cntx_psh(); DOK.use(_nam); DOK.prefix();
      {? DOK.seek(_ref,_nam)
      || {? DOK.NK<>'' & DOK.DTO<>date(0,0,0)
         || _desc:='Zaksięguj próbnie dokument: %1 z dnia %2: (%3/%4)'@@[DOK.NK,$DOK.DTO,DOK.REJ().KOD,$DOK.NR]
         || _desc:='Zaksięguj próbnie dokument: %1 z dnia %2 (%3/%4)'@@[DOK.NK,$DOK.DTO,DOK.REJ().KOD,$DOK.NR]
         ?}
      ?};
      DOK.cntx_pop()
   ?}
?};
_desc


\ksieguj_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja Księguj próbnie okien tabeli DOK
::----------------------------------------------------------------------------------------------------------------------
{? exec('spr_mod','dok_fks')
|| {? ~DOK.sel_size()
   || FUN.info('Dokument wtórny dla wielu jednostek księgowych. Księgowanie możliwe od strony dokumentu pierwotnego.'@)
   ?}
|? DOK.ZP='T'
|| {? ~DOK.sel_size()
   || FUN.info('Dokument został wcześniej zaksięgowany próbnie.'@)
   ?}
|| _proces:=1;
   {? DOK.sel_size() & Plugin.exists('DOK_KS_P_001')
   || _proces:=Plugin.run('DOK_KS_P_001')
   ?};
   {? _proces
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='FKS_DKS_EKSP';
      _params.AKCJA:='Księguj próbnie';
      _params.UIDREF:=DOK.uidref();
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOK',DOK.ref());
      _params.PROC_START:='N';
      exec('mp_run','#b__box',_params)
   || _wyn:=exec('ks_dok1','dok_fks_ks',0);
      {? _wyn
      || _wyn:=0;
         DOK.cntx_psh(); _refsql:=$DOK.ref();
         DOK.index('DOKZRODL'); DOK.prefix(_refsql,_refsql);
         {? DOK.first()
         || _wyn:=1;
            {! |?
               _wyn:=exec('ks_dok1','dok_fks_ks',0);
               _wyn & DOK.next()
            !}
         || _wyn:=1
         ?};
         DOK.cntx_pop()
      ?}
   ?}
?}


\bg_ksp_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Grupa przed akcja ksiegowania dokumentow
::  OLD: \bg_ks_dok/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy księgować próbnie zaznaczone dokumenty?'@)
|| sel_nchk();
   DKS_SEL:=DOK.sel_size();
   DKS_OK:=0;
   1
?}


\ag_ksp_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Grupa po akcji ksiegowania dokumentow
::  OLD: \ag_ks_dok/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba zaznaczonych dokumentów: %1\n'
         'Liczba zaksięgowanych dokumentów: %2\n'
         'Liczba nie zaksięgowanych dokumentów: %3.'@[$DKS_SEL,$DKS_OK,$(DKS_SEL-DKS_OK)]);
VAR_DEL.delete('DKS_SEL','DKS_OK')


\set_okno_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przygotowuje okno do wyświetlenia dokumentu z odpowiednimi przyciskami
::----------------------------------------------------------------------------------------------------------------------
_okno:=exec('okna_r','dok_fks',DOK.DOK_REJ().SLO().KOD);
_red:=DOK.mk_edit('Dokument księgowy'@);
DOK.win_etab(_red,'Nagłówek dokumentu'@);
DOK.win_ewin(_red,,_okno);
DOK.win_etab(_red,'Status'@);
DOK.win_ewin(_red,,'DOK_STAT');
DOK.win_ebtn(_red,'text=%1,panel=bottom,align=begin,display=1'['&Pozycje'@],"exec('poz_dok','dok_fks');''");
DOK.win_ebtn(_red,'text=%1,panel=bottom,align=end,display=1'['&Księguj próbnie'@],"BtnKsg:=1; 'key:Esc'");
_btnan:=DOK.win_ebtn(_red,'text=%1,panel=bottom,align=end,display=1'['&Anuluj'@],"'key:Esc'");
DOK.btn_eopt(_red,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
DOK.win_edit(_red);
{? exec('lic','#b_domain',exec('domain_ref','#b_domain','CTR'))
|| DOK.efld_opt('DOK_STAT','enable=1',DOK,'WSK_XD')
|| DOK.efld_opt('DOK_STAT','enable=0',DOK,'WSK_XD')
?};
DOK.display()


\wyc_ks_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wycofanie księgowania próbnego dokumentu
::----------------------------------------------------------------------------------------------------------------------
KOMT:='';
{? exec('czy_okr_zam_con','konsolidacja',DOK.sel_size()=0) | exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER,1)
|| return(0)
|? DOK.sel_size()=0
|| _ok:=1;
   {? DOK.ZK='T'
   || FUN.info('Dokument został zaksięgowany końcowo.\nWycofanie księgowania nie jest możliwe.'@);
      _ok:=0
   |? DOK.ZP='N'
   || FUN.info('Dokument nie jest zaksięgowany próbnie.'@);
      _ok:=0
   |? DOK.ZP='T'
   || _cz_roz:=exec('is_dok_rozl_cz','fks_vat');
      {? _cz_roz
      || _ok:=FUN.ask('Dokument posiada częściowe rozliczenia'
                      '\npozycji VAT, które zostaną usunięte.\n'
                      '\nCzy wycofać księgowanie dokumentu?'@)
      ?};
      _war:={? 3+DOK.ZAR='LSP'
            || DOK.LKN=''
            |? 3+DOK.ZAR='FKS'
            || DOK.KN='' | DOK.DOK_REJ().KOR_NAG='T'
            || 1
            ?};
      {? _ok & ~_war
      || undo;
         KOMT:='\nDo dokumentu została wystawiona korekta nagłówkowa.'@;
         _ok:=0
      ?};
      {? _ok
      || _ok:=exec('spr_dok','dok_fks1',1,'wycofywanie')
      ?}
   ?};
   {? _ok
   || {? exec('czy_rozl','rozrach',1)
      || _ok:=0;
         {? DOK.get() & DOK.ZP='T'
         || echo('Proszę czekać ...'@);
            {? DOK.r_lock(1,1)
            || DOK.get();
               {? DOK.ZK='T'
               || undo;
                  KOMT:='\nDokument został zaksięgowany końcowo\n'+
                        'wycofanie księgowania próbnego nie jest możliwe'
               |? DOK.ZP='T'
               || {? exec('wycofaj','dok_fks_ks',1)
                  || _ok:=1
                  || undo()
                  ?}
               ?};
               echo();
               DOK.r_unlock()
            || KOMT:='\nDokument zablokowany przez innego operatora\n'+
                     'wycofanie akceptacji nie jest możliwe';
               _ok:=0
            ?}
         ?}
      ?}
   ?};
   {? ~_ok
   || FUN.info('Dokument %1.\nWycofanie akceptacji nie powiodło się.%2'@[DOK.NK,KOMT])
   ?}
?};
VAR_DEL.delete('KOMT');
~~


\bg_wyc_ks_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formula przed akcja grupowa wycofania dokumentow
::----------------------------------------------------------------------------------------------------------------------
_as:={? exec('czy_okr_zam_con','konsolidacja',1)
     || _as:=0
     || FUN.ask('Wycofać księgowanie próbne dla zaznaczonych dokumentów?'@)
     ?};
{? _as
|| sel_nchk();
   TT_WYC:=DOK.sel_aget();
   {? TT_WYC.last()
   || ile:=DOK.sel_size(); kon:=byl:=licz:=licz2:=spr_rozl:=kn:=0; wyc:=1; sel_nchk(); 1
   || VAR_DEL.delete('TT_WYC'); 0
   ?}
|| 0
?}


\ag_wyc_ks_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formula po akcji grupowej wycofania księgowania dokumentow
::----------------------------------------------------------------------------------------------------------------------
DOK.cntx_psh();
{? TT_WYC.last()
|| {!
   |? {? DOK.seek(TT_WYC.REF,DOK.name())
      || {? DOK.ZP='N' || byl+=1 |? DOK.ZK='T' || kon+=1 ?};
         _war:={? 3+DOK.ZAR='LSP'
               || DOK.LKN=''
               |? 3+DOK.ZAR='FKS'
               || DOK.KN='' | DOK.DOK_REJ().KOR_NAG='T'
               || 1
               ?};
         {? ~_war || kn+=1 ?};
         {? ~exec('spr_mod','dok_fks') &
            ((-DOK.ZK='n' & -DOK.ZP='t' & {? spr_rozl=0 || exec('czy_rozl','rozrach',0) || 1 ?} &
               exec('spr_dok','dok_fks1',0,'wycofywanie'))) & _war
         || licz+=exec('wycofaj','dok_fks_ks',0)
         ?}
      ?};
      TT_WYC.prev()
   !}
?};
DOK.cntx_pop();
_lm:=+form(ile);
{? licz=ile
||  {? licz2>0
    || FUN.info('Operacja zakończona. \n'+
      'Wycofano akceptację dla wszystkich zaznaczonych dokumentów.\n'
      'Liczba wycofanych dokumentów: %1'
      '\nLiczba usniętych dokumentów powstałych dla wielu jednostek księgowych: %2'@[form(licz,_lm,,' ,'),form(licz2,_lm,,' ,')])
     || FUN.info('Operacja zakończona. \n'+
      'Wycofano akceptację dla wszystkich zaznaczonych dokumentów.\n'
      'Liczba wycofanych dokumentów: %1'@[form(licz,_lm,,' ,')])
    ?}
||
    {? licz<ile
    ||  {? byl>0
        ||   {? kon>0
             || {? kn>0
                || FUN.info(  'Operacja zakończona. Wycofano akceptację dla dokumentów: %1'
                              '\n\n       Dokumenty, dla których operacja się nie powiodła: %2'
                              '\n            w tym dokumenty wcześniej nie zaakceptowane: %3'
                              '\n                   w tym dokumenty zaksięgowane końcowo: %4'
                              '\n                          w tym dokumenty z powiązanymi korektami nagłówkowymi: %5'@[form(licz,_lm,,' ,'),form(ile-licz,_lm,,' ,'),form(byl,_lm,,' ,'),form(kon,_lm,,' ,'),form(kn,_lm,,' ,')])
                || FUN.info(  'Operacja zakończona. Wycofano akceptację dla dokumentów: %1'
                              '\n\n       Dokumenty, dla których operacja się nie powiodła: %2'
                              '\n            w tym dokumenty wcześniej nie zaakceptowane: %3'
                              '\n                   w tym dokumenty zaksięgowane końcowo: %4'@[form(licz,_lm,,' ,'),form(ile-licz,_lm,,' ,'),form(byl,_lm,,' ,'),form(kon,_lm,,' ,')])
                ?}
             || {? kn>0
                || FUN.info(  'Operacja zakończona. Wycofano akceptację dla dokumentów: %1'
                              '\n\n       Dokumenty, dla których operacja się nie powiodła: %2'
                              '\n            w tym dokumenty wcześniej nie zaakceptowane: %3'
                              '\n               w tym dokumenty z powiązanymi korektami nagłówkowymi: %4'@[form(licz,_lm,,' ,'),form(ile-licz,_lm,,' ,'),form(byl,_lm,,' ,'),form(kn,_lm,,' ,')])
                || FUN.info(  'Operacja zakończona. Wycofano akceptację dla dokumentów: %1'
                              '\n\n       Dokumenty, dla których operacja się nie powiodła: %2'
                              '\n            w tym dokumenty wcześniej nie zaakceptowane: %3'@[form(licz,_lm,,' ,'),form(ile-licz,_lm,,' ,'),form(byl,_lm,,' ,')])
                ?}
            ?}
        |? kn>0
        || FUN.info(  'Operacja zakończona. Wycofano akceptację dla dokumentów: %1'
                       '\n\n       Dokumenty, dla których operacja się nie powiodła: %2'
                       '\n            w tym dokumenty z powiązanymi korektami nagłówkowymi: %3'@[form(licz,_lm,,' ,'),form(ile-licz,_lm,,' ,'),form(kn,_lm,,' ,')])

        || FUN.info(  'Operacja zakończona. Wycofano akceptację dla dokumentów: %1'
                       '\n\n       Dokumenty, dla których operacja się nie powiodła: %2'@[form(licz,_lm,,' ,'),form(ile-licz,_lm,,' ,')])
        ?}
    ||  FUN.info(  'Operacja zakończona. Wycofano akceptację dla dokumentów: %1'@[form(licz,_lm,,' ,')])
    ?}
?};
echo();
VAR_DEL.delete('TT_WYC', kon);
&ile; &licz; &byl; &spr_rozl; &wyc; &kn


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
{? var_pres('DOK',_in) & var_pres('DOK',_in)=type_of(null()) & _in.DOK<>null()
|| USERS.cntx_psh(); USERS.clear();
   {? USERS.seek(_user)
   || DOK.cntx_psh();
      DOK.prefix();
      {? DOK.seek(_in.DOK,form(($_in.DOK)-8))
      || {? exec('usr_fjks','b_perm',DOK.ODD().OD,USERS.ref())
         || _result:=1
         ?}
      ?};
      DOK.cntx_pop()
   ?};
   USERS.cntx_pop()
||
:: nie ma dokumentu wejściowego to znaczy, że należy zwrócić pełne uprawnienia
:: inaczej zadanie do usuniętego dokumentu nigdy nie zniknie z todo.
   _result:=1
?};
_result


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Podglad dokumentu księgowego z listy zadań
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
exec('dok_spac','dok_fks',_we.DOK)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};

{? var_pres('_b')>100
|| _we:=_b
|| _we:=params_get().in
?};

_can_continue:=1;
_obj:=obj_new('RESULT','DOK');
_obj.RESULT:=0;
_obj.DOK:=null();

_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? 4+ref_name(_kref)='doku'
         || _obj.DOK:=exec('FindAndGet','#table',DOK,_kref,,,null());
            {? _obj.DOK=null()
            || _can_continue:=0;
               _msg:='Dokument nie został odnaleziony, prawdopodobnie został usunięty.'@;
               {? _mp.isService()=0 & _mp.CLEANER=0
               || {? _mp.isGroup()
                  || KOMM.add(_msg,2,,1)
                  || FUN.emsg(_msg)
                  ?}
               ?};
               _wy.DOK:=null;
               {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
               _mp.save(,_wy);
               _mp.done()
            |? DOK.seek(_kref,ref_name(_kref),1) & DOK.ZP='T'
            ||
::             Dokument już zaakceptowany, robimy done()
               _can_continue:=0;
               _msg:='Dokument księgowy jest już zaksięgowany próbnie, proces będzie kontynuowany.'@;
               {? _mp.isService()=0 & _mp.CLEANER=0
               || {? _mp.isGroup()
                  || KOMM.add(_msg,2,,1)
                  || FUN.emsg(_msg)
                  ?}
               ?};
               _wy.DOK:=DOK.ref();
               {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      ?}
   !}
?};

{? _can_continue>0
||
:: jest parametr wejściowy DOK
   {? _obj.DOK=null() & var_pres('DOK',_we)=type_of(null())
   || _obj.DOK:=_we.DOK
   ?}
?};

{? _can_continue>0
||
   {? _obj.DOK=null()
   ||
::    Nie było key refa ani parametru wejściowego, robię error
      _can_continue:=0;
      _msg:='Dokument nie został przekazany do czynności.'@;
      {? _mp.isService()=0 & _mp.CLEANER=0
      || {? _mp.isGroup()
         || KOMM.add(_msg,2,,1)
         || FUN.emsg(_msg)
         ?}
      ?};
      _mp.done()
   ?}
?};

{? _can_continue>0
|| _obj.RESULT:=1
?};
_obj

:Sign Version 2.0 jowisz:1045 2023/11/28 13:41:36 39aca707c93acc5d48196c36f2e11ea9ba904da3e5d14304d8ddc242762461c5cb4981ac67765d6d93cbf6473e3053dc62025776a9af747568eaa24f7248867594ff2c5a95e67b6f40f6324135e56a3f035300fd4ff39bcd3a404769cedd4d607560e575764cf46b8892496c76125fcab190dfbb9558a37ea508cfb0b054cbf2

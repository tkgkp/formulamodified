:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !poc_ofo_aroc.fml
:: Utworzony: 11.09.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności POC_OFO_AROC - Informacja o rozpoczęciu ocen.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Informacja o rozpoczęciu ocen - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::
::# kind=WE, symbol=ZO_OSOBA, type=_ZO_OSOBA, name=Wskazanie uczestnika sesji ocen, required=T, keyref=T
::
:: Wynik działania czynności. Parametr szczególnie istotny w przypadku uruchomienia czynności usługowej.
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,BŁĄD,POMIŃ|xxx)", required=N
::
::# kind=WY, symbol=SUB, type=STRING, name=Temat, required=N
::# kind=WY, symbol=TO, type=MEMO, name=Odbiorca, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::
_args:=exec('zo_osoba_main_a','phr_poczta');
_args.warunek:="exec('warunek','!poc_ofo_aroc')";
_args.tytul:="exec('tytul','!poc_ofo_aroc')";
_args.tresc:="exec('tresc','!poc_ofo_aroc')";
params_exec('zo_osoba_main','phr_poczta',_args)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Informacja o rozpoczęciu ocen - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=params_exec('zo_osoba_desc','phr_poczta',,1);
{? _tab.ZAW_DANE='T'
|| 'Wyślij powiadomienie o rozpoczęciu ocen [Program: %1 (%2) | Oceniany okres: %3 - %4 | Termin: %5 | %6 %7 (%8)]'@@
      [_tab.NAZWA,_tab.RODZAJ,_tab.OKRES_OD,_tab.OKRES_DO,_tab.PLAN_DO,_tab.ZZ_NAZWISKO,_tab.ZZ_PIERWSZE,_tab.ZZ_PESEL]
|| 'Wyślij powiadomienie o rozpoczęciu ocen'@@
?}


\warunek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła weryfikuje, czy bieżący rekord tabeli ZO_OSOBA może być podstawą wysłania powiadomienia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZO_OSOBA.RODZAJ='E' & ZO_OSOBA.KOMPLET='N' & ZO_OSOBA.ZO_PROC().A='T' & ZO_PROC.Z<>'T'


\tytul
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła, dla bieżącego rekordu tabeli ZO_OSOBA, zwraca napis będący tytułem powiadomienia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Oceny pracowników - rozpoczęcie'


\tresc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Formuła, dla bieżącego rekordu tabeli ZO_OSOBA, zwraca napis będący treścią powiadomienia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kobieta:=(ZZ_OSOBA.PLEC='K');

'<center><h4>Ocena pracownicza</h4></center>'+
'Dzień dobry.<br>'+
'Pragniemy poinformować, że zgodnie z obowiązującymi w <b>'+KST.NAZWA+'</b> procedurami w zakresie ocen pracowniczych '+
'został'+{? _kobieta || 'a' || '' ?}+' Pan'+{? _kobieta || 'i' || '' ?}+' wyznaczon'+{? _kobieta || 'a' || 'y' ?}+
' jako osoba odpowiedzialna za dokonanie ocen w ramach programu <b>"'+ZO_PROC.ZO_PROG().NAZWA+'"</b>. '+
'Oceny dotyczą okresu od <b>'+ZO_PROC.OKRES_OD$6+' roku</b> do <b>'+ZO_PROC.OKRES_DO$6+' roku</b> i powinny zostać '+
'dokonane najpóźniej do dnia <b>'+ZO_PROC.PLAN_DO$6+' roku</b>.'+
'<br><br>'+
'Formularze ocen zostały udostępnione Pan'+{? _kobieta || 'i' || 'u' ?}+' na stronach naszego portalu pracowniczego.'+
'<br><br>'+
'Ta wiadomość została wygenerowana automatycznie - prosimy na nią nie odpowiadać.'

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:18 29918f2909eb7129c6cfa09fc474da6e963194ae8dea6769b243f62ce23570b1f762f755737051fb52cab4f97edd7e85fb24af399f9d413ea1010d99d4f564a20de9d67f5a79c573f8cb47e06f0bcf90de8bd5406784bf803d17780cf6acc59e605c359c8f7b1720ae55320c460b7f4f2a9be83e576597f58b2d26d70212bf5b

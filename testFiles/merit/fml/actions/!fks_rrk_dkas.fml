:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rrk_dkas.fml
:: Utworzony: 04.11.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RRK_DKAS - Utworzenie PDF do wezwania do zapłaty
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wysyłanie wezwań do zapłaty - główna formuła czynności FKS_RRK_DKAS
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_ser_nags','rozrach_kor')
::# kind=WE, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na wezwanie do zapłaty, required=T, keyref=T
::# kind=WE, symbol=TYP_KON, type=STRING, name=Nazwa typu kontaktu z kontrahentem, required=N, fml_val="exec('zdarzt_sel','dokum_zal')"
::# kind=WE, symbol=NAL_ODS, type=STRING, name=Naliczać odsetki [T/N]?, required=N
::# kind=WE, symbol=PRZED_TERM, type=STRING, name=Odsetki przed terminem płatności [T/N]?, required=N
::# kind=WY, symbol=DOKUM, type=_DOKUM, name=Wskazanie na załącznik z plikiem pdf, required=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='W';
_dokum:=null;
RACHBANK.KB_1R:='';
ToPDF:=_auto:=_mp.isAutoRun();
ToPDF1:={? var_press('NAL_ODS',_in)>0 || _in.NAL_ODS='T' || 1 ?};
ToPDF2:={? var_press('PRZED_TERM',_in)>0 || _in.PRZED_TERM='T' || 0 ?};
_typ:={? var_press('TYP_KON',_in)>0 || _in.TYP_KON || '' ?};

{? _mp.pathTodo() | _auto
|| {? var_press('SER_NAG',_in)>0
   || _ref:=BB.sqlint($_in.SER_NAG); _nam:=form(($_in.SER_NAG)-8);
      {? _ref<>0 & _nam<>''
      || SER_NAG.cntx_psh(); SER_NAG.prefix();
         {? SER_NAG.seek(_ref,_nam)
         || {? SER_NAG.T<>SER_KOR.TYP
            || {? _auto
               || _mp.error('Wybrana korespondencja nie jest wezwaniem do zapłaty.'@)
               || FUN.info('Wybrana korespondencja nie jest wezwaniem do zapłaty.'@); _mp.error()
               ?}
            |? SER_NAG.ST_AKC<>'T'
            || {? ~_auto || FUN.info('Wezwanie do zapłaty nie jest zaakceptowane.'@) ?}
            |? SER_NAG.KH().JEZYK & ~exec('sprwzwyd','rozrach','fks_km_p',SER_NAG.KH().JEZYK().KOD,0)
            || {? ~_auto || FUN.info('Brak wzorca dla pliku PDF w języku %1 do kontaktu z kontrahentem %2.'@[SLO.KOD,KH.SKR]) ?}
            |? RACHBANK.KB_1R=''
            || SKID_RBK.index('TAB'); SKID_RBK.prefix(null,REF.INFO,REF.INFO);
               {? SKID_RBK.first
               || _il:=1;
                  {! |? {? SKID_RBK.WAL=SER_NAG.WAL & SKID_RBK.RD='T'|| _il:=0; RACHBANK.KB_1R:=SKID_RBK.N ?};
                        _il & SKID_RBK.next()
                  !}
               ?};
               {? ~_auto & RACHBANK.KB_1R=''
               || FUN.info('Brak ustawionego domyślnego rachunku bankowego licencjobiorcy.'@)
               ?};
               {?  RACHBANK.KB_1R<>'' & (_auto | FUN.ask('Czy utworzyć plik PDF dla wezwania do zapłaty\ndla kontrahenta: %1 z dnia: %2?'@[SER_NAG.KH().SKR,$SER_NAG.DATA]))
               || _dokum:=exec('add_pdf','!fks_rrk_dkas',{? _auto || 2 || 1 ?},_typ)
               ?}
            |? _auto | FUN.ask('Czy utworzyć plik PDF dla wezwania do zapłaty\ndla kontrahenta: %1 z dnia: %2?'@[SER_NAG.KH().SKR,$SER_NAG.DATA])
            || _dokum:=exec('add_pdf','!fks_rrk_dkas',{? _auto || 2 || 1 ?},_typ)
            ?}
         || {? ~_auto
            || FUN.info('Nie znaleziono korespondencji z rozrachunków.'@)
            ?};
            _out.DOKUM:=null;
            _mp.save(,_out);
            _mp.done()
         ?};
         SER_NAG.cntx_pop()
      || {? ~_auto
         || FUN.info('Nie znaleziono korespondencji z rozrachunków.'@)
         ?};
         _out.DOKUM:=null;
         _mp.save(,_out);
         _mp.done()
      ?}
   || {? ~_auto
      || FUN.info('Nie znaleziono korespondencji z rozrachunków.'@)
      ?};
      _out.DOKUM:=null;
      _mp.save(,_out);
      _mp.done()
   ?}
|? _akcja='AddPDF'
|| {? SER_NAG.ST_AKC='T'
   || _il:=1;
      RACHBANK.KB_1R:='';
      SKID_RBK.index('TAB'); SKID_RBK.prefix(null,REF.INFO,REF.INFO);
      {? SKID_RBK.first() & SKID_RBK.WAL<>null
      || {!
         |? {? SKID_RBK.WAL=SER_NAG.WAL & SKID_RBK.RD='T' || _il:=0; RACHBANK.KB_1R:=SKID_RBK.N ?};
            _il & SKID_RBK.next()
         !}
      ?};
      {? _il & SKID_RBK.first() & SER_NAG.WAL=null()
      || {!
         |? {? SKID_RBK.INNWAL='T' & SKID_RBK.RD='T' || _il:=0; RACHBANK.KB_1R:=SKID_RBK.N ?};
            _il & SKID_RBK.next()
         !}
      ?}
   ?};
   {? SER_NAG.ST_AKC<>'T'
   || FUN.info('Wezwanie do zapłaty nie jest zaakceptowane.'@)
   |? SER_NAG.KH().JEZYK & ~exec('sprwzwyd','rozrach','fks_km_p',SER_NAG.KH().JEZYK().KOD,0)
   || FUN.info('Brak wzorca dla pliku PDF w języku %1 do kontaktu z kontrahentem %2.'@[SLO.KOD,KH.SKR])
   |? RACHBANK.KB_1R=''
   || {? FUN.ask('Brak ustawionego domyślnego rachunku bankowego licencjobiorcy. Wyświetlić listę dostępnych rachunków?'@)
      || {? exec('rachunki_banki','rachunki')
         || RACHBANK.KB_1R:=SKID_RBK.N;
            {? FUN.ask('Czy utworzyć plik PDF dla wezwania do zapłaty\ndla kontrahenta: %1 z dnia: %2?'@[SER_NAG.KH().SKR,$SER_NAG.DATA])
            || _dokum:=exec('add_pdf','!fks_rrk_dkas',1,_typ)
            ?}
         ?}
      ?}
   |? FUN.ask('Czy utworzyć plik PDF dla wezwania do zapłaty\ndla kontrahenta: %1 z dnia: %2?'@[SER_NAG.KH().SKR,$SER_NAG.DATA])
   || _dokum:=exec('add_pdf','!fks_rrk_dkas',1,_typ)
   ?}
?};
VAR_DEL.delete('ToPDF','ToPDF1','ToPDF2');
{? _dokum
|| _out.DOKUM:=_dokum;
   _mp.save(,_out);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Utwórz PDF do wezwania do zapłaty'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('SER_NAG',_we)>0
|| _ref:=BB.sqlint($_we.SER_NAG); _nam:=form(($_we.SER_NAG)-8);
   {? _ref<>0 & _nam<>''
   || SER_NAG.cntx_psh(); SER_NAG.use(_nam); SER_NAG.prefix();
      {? SER_NAG.seek(_ref,_nam)
      || _desc:='Utwórz PDF do wezwania do zapłaty dla kontrahenta: %1 z dnia: %2'@@[SER_NAG.KH().SKR,$SER_NAG.DATA]
      ?};
      SER_NAG.cntx_pop()
   ?}
?};
_desc


\add_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodanie załacznika w postacji wydruku do PDF
::   WE: _a - nadpisać? 0-nie [1]-pytać 2-tak
::       _b - nazwa typu kontaktu
::   WY: Wskazanie na załacznik do wysłania (DOKUM.ref)
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
_ref:=null;
_dalej:=1;
_nag:=$SER_NAG.ref();
DOKUM.cntx_psh();
DOKUM.index('TYP'); DOKUM.prefix(_nag,'P');
_jest:=DOKUM.first();
{? _jest
|| _blob:=DOKUM.DOKUM;
   {? _a=1
   || _dalej:=FUN.ask('Istnieje już plik PDF.\nCzy nadpisać istniejący?'@)
   |? _a=2
   || _dalej:=1
   || _dalej:=0
   ?}
?};
{? _dalej
|| _plik:='wezwanie_do_zaplaty'+$DOKUM.tm_stamp()+'.pdf';
   _plik_baza:='wezwanie_do_zaplaty.pdf';
   _tab:=exec('email_os_kon_t','kontrahent',SER_NAG.KH,'Windykacja');
   ZDARZT.cntx_psh();
   ZDARZT.index('ZDARZT'); ZDARZT.prefix('N',_b,);
   _typ_kon:={? ZDARZT.first() || ZDARZT.ref() || null ?};
   ZDARZT.cntx_pop();
   _ok:=exec('rep_exec','#b_report','FKS_RRK_DKAS','fks_wezwkor',,0,_plik,0,,,,'W');
   {? var_pres('ObjForMerge')>0
   || _plik:=ObjForMerge.doc2pdf()
   ?};
   {? _ok | var_pres('ObjForMerge')>0
   || {? _tab.first() & (_tab.size()>1)
      || _email:='';
         _ok:=exec('add','dokum_zal',_nag,_plik,~_jest,_email,_plik_baza,
                   SER_NAG.KH,{? _email<>'' || KH_OSOB.NAZWISKO+' '+KH_OSOB.IMIE || '' ?},
                   SER_NAG.ODD,_typ_kon,'Wezwanie do zapłaty nr '+SER_NAG.ID);
         {? _ok
         || {? _jest
            || _jest:=exec('find_dokumz','bloby',DOKUM.ref(),_blob)
            ?};
            DOKUMZ.prefix();
            exec('add_dokumz2','bloby',_plik_baza,~_jest);
            _ref:=DOKUM.ref()
         ?}
      |? _tab.first() | ~_tab.size
      || _email:=_tab.EMAIL;
         _ok:=exec('add','dokum_zal',_nag,_plik,~_jest,_email,_plik_baza,
                   SER_NAG.KH,{? _email<>'' || KH_OSOB.NAZWISKO+' '+KH_OSOB.IMIE || '' ?},
                   SER_NAG.ODD,_typ_kon,'Wezwanie do zapłaty nr '+SER_NAG.ID);
         {? _ok
         || {? _jest
            || _jest:=exec('find_dokumz','bloby',DOKUM.ref(),_blob)
            ?};
            DOKUMZ.prefix();
            exec('add_dokumz2','bloby',_plik_baza,~_jest);
            _ref:=DOKUM.ref()
         ?}
      ?}
   ?}
?};
DOKUM.cntx_pop();

{? var_pres('ObjForMerge')>0
|| ObjForMerge.addToTab(ObjForMerge.TAB.FILENAME,'');
   ObjForMerge.addToTab(ObjForMerge.RESFILE,'');
   ObjForMerge.delFiles(0);
   VAR_DEL.delete('ObjForMerge')
?};

_ref


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'ser_nag'>0
         || _record:=exec('FindAndGet','#table',SER_NAG,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1045 2023/10/26 08:07:32 6bc8f802a7a0f45bbbcd779faf50a9d94b3e91fd659c3ab26748a708988daca18f9cbf0c459720570288330add0a5d67b05de0d93d835104e4fd2886e8775575f028dbce2047ab00d1a818d513e31bf1401d7fd55b1a52f34f1067ec3c5dae68be848bf58115b1fbf4884c5500512379f73f650915a446eca8911716b5737650

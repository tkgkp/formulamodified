:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pdm_dwsk.fml
:: Utworzony: 10.02.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PDM_DWSK - Rejestracja wskaźników pomocniczych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rejestracja wskaźników pomocniczych - główna formuła czynności CTR_PDM_DWSK
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\be_nwsk_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Przed redakcja pola CON_NWSK.SKID_MBN
::  OLD: \be_nwsk_mod/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? -menu_txt='popraw'
|| CON_WSK.cntx_psh();
   CON_WSK.index('UNIK'); CON_WSK.prefix(CON_NWSK.ref());
   _zwrot:=~CON_WSK.first();
   CON_WSK.cntx_pop()
?};
_zwrot


\rpo_nwsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Definiowanie wskaznikow uzytkownika - rekord po
::  OLD: \rpo_nwsk/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=__CHK.record(CON_NWSK,,'ROK_F','SKID_MBN','K_WERSJE');
{? _zwrot=''
|| {? -menu_txt='popraw' || _ref:=CON_NWSK.ref() ?};
   CON_NWSK.cntx_psh();
   CON_NWSK.index('UNIK'); CON_NWSK.prefix(REF.FIRMA,CON_NWSK.ROK_F,CON_NWSK.SKID_MBN,CON_NWSK.K_WERSJE);
   {? CON_NWSK.first() & {? -menu_txt='popraw' || CON_NWSK.ref()<>_ref || 1 ?}
   || FUN.info('Istnieje już definicja wskaźników dla wybranego roku, modelu i wersji.'@); _zwrot:='K_WERSJE'
   ?};
   CON_NWSK.cntx_pop()
?};
_zwrot


\dol_wsk_pom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Dolaczanie rekordow do tabeli CON_WSK
::  OLD: \dol_wsk_pom/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
CON_WSK.blank();
CON_WSK.win_edit('RED');
{? CON_WSK.edit("exec('spr_wsk_pom','!ctr_pdm_dwsk')")
|| CON_WSK.CON_NWSK:=CON_NWSK.ref();
   CON_WSK.OKRO_F:=OKRO_F.ref();
   CON_WSK.add()
?};
grp_disp(OKRO_F,'WER_CON')


\spr_wsk_pom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Sprawdzanie rekordow z tabeli CON_WSK
::  OLD: \spr_wsk_pom/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
SKID_MBP.cntx_psh();
SKID_MBP.index('LP'); SKID_MBP.prefix(CON_NWSK.SKID_MBN);
{? SKID_MBP.find_key(1) & CON_WSK.PBUD=null || FUN.emsg('Nie wybrano pozycji budżetowej.'@); _zwrot:='PBUD' ?};
{? _zwrot='' & SKID_MBP.find_key(2) & CON_WSK.JORG=null || FUN.emsg('Nie wybrano elementu wymiaru drugiego.'@); _zwrot:='JORG' ?};
{? _zwrot='' & SKID_MBP.find_key(3) & CON_WSK.OKOSZ=null || FUN.emsg('Nie wybrano elementu wymiaru trzeciego.'@); _zwrot:='OKOSZ' ?};
{? _zwrot='' & SKID_MBP.find_key(4) & CON_WSK.WYM4=null || FUN.emsg('Nie wybrano elementu wymiaru czwartego.'@); _zwrot:='WYM4' ?};
{? _zwrot='' & SKID_MBP.find_key(5) & CON_WSK.WYM5=null || FUN.emsg('Nie wybrano elementu wymiaru piątego.'@); _zwrot:='WYM5' ?};
SKID_MBP.cntx_pop();
_zwrot


\pop_wsk_pom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Poprawianie rekordow w tabeli CON_WSK
::  OLD: \pop_wsk_pom/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
CON_WSK.win_edit('RED');
{? CON_WSK.edit("exec('spr_wsk_pom','!ctr_pdm_dwsk')") || CON_WSK.put() ?}


\usu_wsk_pom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Usuwanie rekordow z tabeli CON_WSK
::  OLD: \usu_wsk_pom/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? CON_WSK.sel_size()
|| CON_WSK.del()
|? FUN.ask('Czy usunąć zaznaczony wskaźnik?'@)
|| CON_WSK.del();
   grp_disp(OKRO_F,'WER_CON')
?}


\bg_usuwpom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Grupa przed usun okna wertowania tabeli CON_WSK
::  OLD: \bg_usuwpom/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.ask('Czy usunąć zaznaczone wskaźniki?'@)


\ag_usuwpom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Grupa po usun okna wertowania tabeli CON_WSK
::  OLD: \ag_usuwpom/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
grp_disp(OKRO_F,'WER_CON')

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:39 bd990b4df00780932a228d1a54cb53dce1ce8e0ad77ee1f634ffa6ac2aa3d15eafa10f504c09607c22327b408e6b2b4612d98ecb2e0647724ff7a88968c9e9a47298f37dfab9227b0d0a76b0f7d39ce69b8c40bc468c4f357644ab636ceba9939a9de938cfe6b203f12c2551ddc261e9bd208a17397b2c789e5ca01df2209d63

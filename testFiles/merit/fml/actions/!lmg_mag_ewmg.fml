:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_ewmg.fml
:: Utworzony: 03.09.2019
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Wycofanie akceptacji lub zakończenia dokumentu magazynowego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# permissions=ODDZ,LMG,TARD
::# parses=exec('parses_nd','lmg')
::# access=exec('sprUprMag','%todo')
::# kind=WE, symbol=ND,      type=_ND,      name=Dokument magazynowy,           required=N, keyref=T
::# kind=WY, symbol=ND,      type=_ND,      name=Dokument magazynowy,           required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_context:=params_get().context;

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_service:=_mp.isService();
_proces:=_mp.pathProc();
_auto:=_mp.isAutoRun() | _service;

{? var_pres('ND',_in)=type_of(null()) & _in.ND
|| _nd_ref:=_in.ND
|| _nd_ref:=null()
?};

{? _nd_ref=null()

|| _mp.error('Brak wymaganego parametru ND.')

|| _out.ND:=_nd_ref;
   _mp.save('OUT','ND',_nd_ref);

   exec('mag_psh','open_tab');

   ND.prefix();
   {? ~ND.seek(_nd_ref)
   || _mp.error('Nie znaleziono dokumentu.')
   || {? _akcja='Wycofaj' | _auto
      || _arg:=
            {? var_pres('_context')<>type_of(~~) & var_pres('WYCOFAJ',_context)=type_of(obj_new('a'))
            || _context.WYCOFAJ
            || exec('wycofaj_a','!lmg_mag_ewmg')
            ?};
         _arg.MP:=_mp;
         _arg.AUTO:=_auto;
         {? (_oki:=exec('wycofaj','!lmg_mag_ewmg',_arg))<=0
         || {? _service
            || _mp.done()
            |? _oki<0
            || FUN.info('Nie można wycofać dokumentu.'@);
               _mp.error('Nie można wycofać dokumentu.')
            ?}
         ?}
      |? _mp.pathTodo() | (_mp.pathArea() & _akcja='')
      || params_set(params_get());
         _win_red:=exec('nd_win_edit','magdok_nag');
         _ff:="params_exec('nd_pozycje_todo','!lmg_mag_ewmg')";
         ND.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
         _ff:="params_exec('nd_wycofaj_todo','!lmg_mag_ewmg'); 'key:Esc'";
         ND.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Wycofaj'@],_ff);
         _ff:="'key:Esc'";
         ND.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
         ND.win_edit(_win_red);
         exec('set_efld_opt','magdok_nag',_win_red);
::       Dalsza obsługa akceptacji w exec('wycofaj','!lmg_mag_ewmg')
         ND.display();
::       Usunięcie definicji tymczasowych okien
         ND.win_edit(''); ND.win_edel(_win_red)
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   exec('mag_pop','open_tab')

?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ND',_in)<>type_of(~~) & _in.ND
|| 'Wycofaj dokument magazynowy: %1'@@[exec('record','#to_string',_in.ND)]
|| ''
?}


\nd_wycofaj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Dokument magazynowy - Wycofaj z todo
::   WE: params_get()   - ustawiane w exec('main','!lmg_mag_ewmg')
::----------------------------------------------------------------------------------------------------------------------
_arg:=exec('wycofaj_a','!lmg_mag_ewmg');
_arg.MP:=params_get().mp;
exec('wycofaj','!lmg_mag_ewmg',_arg);
''


\nd_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Dokument magazynowy - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('poz_dok','magdok_poz',0);
''


\wycofaj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [19.42]
:: OPIS: Parametry funkcji exec('wycofaj','!lmg_mag_ewmg')
::   WE:
::   WY: tablica argumentów
::----------------------------------------------------------------------------------------------------------------------
_arg:=obj_new('MP','AUTO','ND_UNLOCK');
:: =1-automatyczne wycofanie, 0-wpp
_arg.AUTO:=0;
:: =1-odblokować ND, =0-wpp
_arg.ND_UNLOCK:=1;
:: argumenty
_arg


\wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MM] [19.42]
:: OPIS: Dokument magazynowy - Wycofanie dokumentu
::   WE: _a - exec('wycofaj_a','!lmg_mag_ewmg')
::   WY: 1-wycofano 0-nie -1-dokument niezakończony
::----------------------------------------------------------------------------------------------------------------------
_mp:=_a.MP;
_auto:=_a.AUTO;
_nd_unlock:=_a.ND_UNLOCK;

{? _auto
|| _result:=exec('wyc_dok','magdok_nag',0,'N',_nd_unlock,1,1)
|| _result:=exec('wyc_dok','magdok_nag',1,'T',_nd_unlock)
?};

{? _result
|| _mp.done()
?};

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 7c753c40f63808756f014cdf5b55c03e5e1114d022e73d47b8d15d3aed779b5b5c5be634905871b7999db79ced68b65be0ec83eddadc0ecc0170b541f69854e81222b52eea81719cdfdd541eef0af111bf9886cb56bc58fe1e06298c93f0ddbac1b28543f4eaa976225018fe0a94a2442711c5002f7a8d37a8a903bbf18469b4

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_grp_awcp.fml
:: Utworzony: 19.10.2020
:: Autor: DG
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_GRP_AWCP - Wprowadzanie wzorców czasu pracy
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.14]
:: OPIS: Wprowadzanie wzorców czasu pracy - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \ust_kal/kaledit.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

exec('init','pkd');

{? ~__F_ZATR.upr('P')
|| FUN.info('Brak wymaganego dostępu do formy współpracy "%1".'@ ['P']);
   return()
?};

{? var_pres('parWCP')>100 || obj_del(parWCP) ?};
_zdate:=date(0,0,0);
data_od:=data_do:=_zdate;

: wybór wielu pracowników
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PKD';
:_args.F_ZATR:='P';
_args.F_ZATR:={? +P_FILTER.F_ZATR().KOD || P_FILTER.F_ZATR().KOD || '*T' ?};
_args.VIEW:='';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_ret:=exec('wybierz','pracownik',_args);
: wybrano kogoś?
{? _ret.P.size()=0
|| R.f_clear();
   exec('msg_cancel','!pkd_grp_awcp');
   return()
?};
R.f_clear();

exec('startup','!pkd_grp_awcp');
R_WZCZ.index('R_WZCZ');

parWCP:=obj_new(5);
parWCP[1]:=obj_new(4);

parWCP[1][1]:=tab_tmp(3,
   'WYD',     'STRING[16]','Jednostka organizacyjna',
   'NAZWISKO','STRING[30]','Nazwisko',
   'PIERWSZE','STRING[20]','Imię',
   'T',       'STRING[9]', 'Numer teczki',
   'REF',     'INTEGER',   'Ref',
   'WYKONAJ', 'STRING[1]', 'Wykonaj'
);
parWCP[1][1].fld_attr(,2);
parWCP[1][2]:=parWCP[1][1].mk_sel('Pracownicy'@,'T',0,'wcp_prac_wyb1',,,,,'U');
parWCP[1][1].win_fld(parWCP[1][2],,'T');
parWCP[1][1].win_fld(parWCP[1][2],,'NAZWISKO');
parWCP[1][1].win_fld(parWCP[1][2],,'PIERWSZE');
parWCP[1][1].win_fld(parWCP[1][2],,'WYD');

parWCP[1][1].win_act(parWCP[1][2],0,'Formuła','Pomiń'@,,,
   "parWCP[1][1].WYKONAJ:={? parWCP[1][1].WYKONAJ='T' || 'N' || 'T' ?};
    parWCP[1][1].put();
    parWCP[1][1].next()
    ",,1);
parWCP[1][1].win_act(parWCP[1][2],0,'Formuła','Odwróć zaznaczenie'@,,,
   "_ref:=parWCP[1][1].ref;
    {? parWCP[1][1].first()
    || {!
       |? parWCP[1][1].WYKONAJ:={? parWCP[1][1].WYKONAJ='T' || 'N' || 'T' ?};
          parWCP[1][1].put();
          parWCP[1][1].next()
       !}
    ?};
    parWCP[1][1].seek(_ref)");
parWCP[1][1].win_act(parWCP[1][2],0,'Formuła','pomiń Wszystkich'@,,,
   "_ref:=parWCP[1][1].ref();
    {? parWCP[1][1].first()
    || {!
       |? parWCP[1][1].WYKONAJ:='N';
          parWCP[1][1].put();
          parWCP[1][1].next()
       !}
    ?};
    parWCP[1][1].seek(_ref)");
parWCP[1][1].win_act(parWCP[1][2],0,'Kolejność');
parWCP[1][1].win_act(parWCP[1][2],0,'Rekord',,,,"parWCP[1][1].WYKONAJ<>'T'");
parWCP[1][1].win_sel(parWCP[1][2]);

parWCP[1][3]:=parWCP[1][1].mk_sel('Pracownicy'@,'T',0,'wcp_prac_wyb2',,,,,'U');
parWCP[1][1].win_fld(parWCP[1][3],,'T');
parWCP[1][1].win_fld(parWCP[1][3],,'NAZWISKO');
parWCP[1][1].win_fld(parWCP[1][3],,'PIERWSZE');
parWCP[1][1].win_fld(parWCP[1][3],,'WYD');

parWCP[1][1].win_act(parWCP[1][3],0,'Formuła','Zmiany'@,,,
   "P.seek(parWCP[1][1].REF,P.name);
    exec('zm_kal','kaledit',P.ref);
    parWCP[2].select();
    parWCP[2].erase()",,1);
parWCP[1][1].win_act(parWCP[1][3],0,'Formuła','Przed zmianami'@,,,
   "P.seek(parWCP[1][1].REF,P.name);
    R_WZCZ.index('R_WZWND');
    R_WZCZ.prefix(P.name,#P.ref,'N','N');
    {? R_WZCZ.first()
    || {!
       |? parWCP[2].NAZWA:=R_WZCZ.KAL().NAZWA;
          parWCP[2].OD:=R_WZCZ.OD;
          parWCP[2].KIN:=R_WZCZ.KIN;
          parWCP[2].GRAFIK:=R_WZCZ.GRAFIK;
          parWCP[2].add();
          R_WZCZ.next()
       !}
    || parWCP[2].NAZWA:=P.KAL().NAZWA;
       parWCP[2].OD:=date(P.DZA~1,P.DZA~2,1);
       parWCP[2].add()
    ?};
    parWCP[2].select();
    parWCP[2].erase()");
parWCP[1][1].win_act(parWCP[1][3],0,'Formuła','Wycofaj'@,,,
   "{? FUN.ask('Czy na pewno wycofać pracownika?'@)
    || parWCP[1][1].del()
    ?}");
parWCP[1][1].win_act(parWCP[1][3],0,'Formuła','Dalej'@,,
   'Rozpoczyna wprowadzanie danych'@,,
   "sel_exit()",,,,,'D',,'target=window'
);

parWCP[1][1].win_btn(parWCP[1][3],'text=%1,panel=bottom,align=end'['Dalej'@],'menu:D');

parWCP[1][1].win_act(parWCP[1][3],0,'Kolejność');

parWCP[1][4]:=parWCP[1][1].ndx_tmp(,1,'WYKONAJ',,0);

parWCP[2]:=tab_tmp(1,
   'OD',    'DATE',      'Data',
   'NAZWA', 'STRING[20]','Nazwa',
   'REF',   'INTEGER',   'Ref',
   'KIN',   'STRING[1]', 'Wzorzec indywidualny',
   'GRAFIK','STRING[1]', 'Grafik'
);
_wnd:=parWCP[2].mk_sel('Kalendarze'@,'N',0,'wcp_kal_wyb1',,,,,'U');
parWCP[2].win_fld(_wnd,,'NAZWA');
parWCP[2].win_fld(_wnd,,'OD');
parWCP[2].win_sel(_wnd);

parWCP[5]:=tab_tmp(1,
   'NAZWA','STRING[20]','Nazwa',
   'REF',  'INTEGER',   'Ref'
);
_wnd:=parWCP[5].mk_sel('Kalendarze'@,'N',0,'wcp_kal_wyb2',,,,,'U');
parWCP[5].win_fld(_wnd,,'NAZWA');
parWCP[5].win_act(_wnd,,'Formuła','Wybierz'@,,,,"sel_exit",1);
parWCP[5].win_sel(_wnd);

KAL_NAZW.cntx_psh();
KAL_NAZW.index('KAL_CZES');
KAL_NAZW.prefix(exec('ref_firma','ustawienia'),'N');
{? KAL_NAZW.first()
|| {!
   |? parWCP[5].NAZWA:=KAL_NAZW.NAZWA;
      parWCP[5].REF:=KAL_NAZW.ref;
      parWCP[5].add();
      KAL_NAZW.next()
   !}
?};
KAL_NAZW.cntx_pop();

{? _ret.P.first()
|| echo('Trwa określanie grupwy pracowników...'@);
   P.prefix();
   {!
   |? {? P.seek(_ret.P.SQL)
      || parWCP[1][1].NAZWISKO:=P.OSOBA().NAZWISKO;
         parWCP[1][1].PIERWSZE:=OSOBA.PIERWSZE;
         parWCP[1][1].WYD     :=P.WYDZIAL().SYMBOL;
         parWCP[1][1].T       :=P.T;
         parWCP[1][1].REF     :=P.ref;
         parWCP[1][1].WYKONAJ :='T';
         parWCP[1][1].add()
      ?};
      _ret.P.next()
   !};
   echo('')
?};

:: zakres dat
_PAR:=tab_tmp(,'OD','DATE','Data od'@,
               'DO','DATE','Data do'@);
_we:=_PAR.mk_edit('Podaj dane'@,,'wcp_zakr_dat');
_PAR.win_esep(_we,'Zakres dat'@);
_PAR.win_efld(_we,,'OD',,,12,0,,,,'Data, od której wprowadzane będą wzorce czasu pracy.'@);
_PAR.win_efld(_we,,'DO',,,12,0,,,,'Data, do której wprowadzane będą wzorce czasu pracy.'@);
_PAR.efld_opt(_we,'mark=1',,'OD');
:_PAR.efld_opt(_we,'mark=1',,'DO');
exec('ok_esc','#window',_PAR,_we);
_PAR.win_edit(_we);
_PAR.OD:=date;
_PAR.DO:=date(,12,0);
{? ~_PAR.edit(
      "  _PAR:=cur_tab();
         {? (_chk:=__CHK.record(_PAR,,'OD'))<>''
         || return(_chk); 0
         |? _PAR.OD<=date(0,0,0)
         || return(__CHK.err_fld(_PAR,'OD',1,'Wprowadzono błędną datę.'@ )); 0
         |? _PAR.OD>_PAR.DO & _PAR.DO<>date(0,0,0)
         || return(__CHK.err_fld(_PAR,'DO',1,'Data nie może być wcześniejsza niż: \"Data od\".'@)); 0
         |? _PAR.OD~1<date()~1 & ~FUN.ask('Wprowadzony zakres dat dotyczy lat poprzednich. Czy na pewno kontynuować?'@)
         || 0
         || data_od:=_PAR.OD; data_do:=_PAR.DO; 1
         ?}
      "
   )
|| exec('cleanup','!pkd_grp_awcp');
   return
?};
&_we;
obj_del(_PAR);
&_PAR;

{? ~parWCP[5].select()
|| exec('cleanup','!pkd_grp_awcp');
   return
?};
KAL_NAZW.seek(parWCP[5].REF,KAL_NAZW.name);
parWCP[4]:=KAL_NAZW.ref;

parWCP[1][1].win_sel(parWCP[1][3]);
parWCP[1][1].index(parWCP[1][4]);
parWCP[1][1].prefix('T');
{!
|? {? parWCP[1][1].select()
   || {? ~parWCP[1][1].first()
      || FUN.info('Nie wybrano pracownika.'@);
         exec('cleanup','!pkd_grp_awcp');
         return
      ?};

      {? ~FUN.ask(
            'Zapisy właściwe dla podanego okresu zostaną zmodyfikowane.\n'
            'Czy na pewno kontynuować?'@)
      || exec('cleanup','!pkd_grp_awcp');
         return
      ?};

      KAL_NAZW.clear();
      KAL_NAZW.index('KAL_NAZW');

      _lp:=0;
      _size:=parWCP[1][1].size;
      {!
      |? _lp+=1;
         _proc:=ceil(100*_lp/_size);
         progress(_proc,'Trwa aktualizacja wzorców czasu pracy %1%%'@ [form(_proc,3,0)],'Przetwarzanie'@);

         parWCP[2].erase();
         P.seek(parWCP[1][1].REF,P.name);
         exec('r_wzcz_fld_fml','kaledit',P);
         exec('usun_ukr','kaledit',P);
         exec('zm_kal','kaledit',P.ref);
         {? parWCP[2].first()
         || _nazwa:=%255+$P.ref;
            KAL_NAZW.prefix();
            {? ~KAL_NAZW.find_key(exec('ref_firma','ustawienia'),_nazwa,_nazwa)
            || KAL_NAZW.blank();
               KAL_NAZW.NAZWA:=_nazwa;
               KAL_NAZW.CZESC:='T';
               KAL_NAZW.add();
               P.KAL:=KAL_NAZW.ref;
               P.put()
            ?};
            exec('update_kal','kaledit',P.ref);
            exec('norm_kal','kaledit',P)
         ?};
         parWCP[1][1].next()
      !};
      prgs_clr();

      FUN.info('Zakończono wprowadzanie wzorców czasu pracy.'@);
      0
   || {? FUN.ask('Czy na pewno chcesz zrezygnować z wprowadzania danych?'@)
      || 0
      || 1
      ?}
   ?}
!};
exec('cleanup','!pkd_grp_awcp');
~~


\msg_cancel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.14]
:: OPIS: Wyświetla komunikat o zaprzestaniu działania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Anulowano wprowadzanie wzorców czasu pracy.'@)


\startup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.14]
:: OPIS: Zakładanie kontekstów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
R_WZCZ.cntx_psh();
KAL_NAZW.cntx_psh()


\cleanup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [21.14]
:: OPIS: Czyszczenie kontekstów i zmiennych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
P.cntx_pop();
R_WZCZ.cntx_pop();
KAL_NAZW.cntx_pop();
&data_od; &data_do;
obj_del(parWCP)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 8127d6dfa0b7a49ccf141d0f366c129d7359609ed5b0ea829615a42dbc0481586fdd8c3d29202c13fe3320eb2c595c63a7a7df24946ccb2788e9a089ab1bbd6216563ac1a8fcf3c1375a781a37bdd1f8435bf79c6ecd7ddf752d73efa8c9737e86f06d261330ca4ab58602b6e26fd66dca1eee35f589636f5ae4ec48171c04e3

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lum_fak_dpac.fml
:: Utworzony: 14.02.2019
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Rejestracja paczki faktur
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP,TARS
::# kind=WE,   symbol=RODZ,   type=STRING,   name=Rodzaj paczki,  required=T, fml_val="exec('rodz','umowy_faktury')"
::# kind=WY,   symbol=FPACZ,  type=_FPACZ,   name=Paczka faktur,  required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;
{? var_pres('_context')=type_of(~~)
||
   _context:=obj_new('ZAKONCZ');
   _context.ZAKONCZ:=0
?};

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_area_lum_fak:=_mp.pathArea('LUM_FAK');
_area:=_area_lum_fak;
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo() | _mp.pathArea() & ~_area;
_grupa:=_mp.isGroup();
_autoakc:=exec('autoAkc','#b__box',_mp,100180,'LUM_FAK_EAPA');

{? _todo
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('FPACZ',_out)=type_of(null()) & _out.FPACZ
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego FPACZ.'@)
   ?}
?};

{? var_pres('RODZ',_in)<>type_of('') | _in.RODZ<>'U' & _in.RODZ<>'Z'
||
   _txt:='Błędna wartość parametru RODZ.'@;
   FUN.info(_txt);
   {? ~_proc & _akcja<>'Dołącz' || _mp.error(_txt) ?};
   return(~~)
?};

TAZ.SD:='S';
BEER.SZ:='S';
BEER.MW:='F';
F_HELP.RODZ:=_in.RODZ;
exec('ustaw_ww','eurusd',BEER.MW);
jaka_tp:='FPACZ.SLU';

:: DRO_TODO_AWI: Sprawdzenie uprawnień
:: Sprawdzenie uprawnień
::{? params_exec('permissions_chk','lsp','!lsp_fak_drfp')=0 || return() ?};

:: Wyzwalacz, który po dodaniu paczki faktur:
:: - add: dodaje rekord kluczowy paczki faktur
::   del: usuwa rekord kluczowy paczki faktur
:: - add: zapisuje parametr wyjściowy FPACZ = wskazanie na nagłówek korekty
::   del: zapisuje parametr wyjściowy FPACZ = null
_mp.trigRef('FPACZ',,1,,exec('kind_out','#b_port'),'FPACZ');

{? _akcja='Dołącz'
   | _proc
   | _akcja='START_TODO'
||
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
   exec('fpacz_win_edit_set','!lum_fak_dpac');
   params_set('context',_context);
   FPACZ.blank();
   {? FPACZ.edit("{? params_get().context.ZAKONCZ || '' || exec('chk_fpacz','!lum_fak_dpac') ?}")
   ||
      FPACZ.NR:=exec('nr_pacz','!lum_fak_dpac');
      {? FPACZ.add()
      ||
         {? _context.ZAKONCZ || _mp.done() ?}
      ?}
   ?};
:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? var_pres('FPACZ',_outa)<>type_of(~~) & _outa.FPACZ
   ||
::    Dodano paczkę faktur
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      {? _area_lum_fak || FPACZ.seek(_outa.FPACZ) ?}
   ||
::    Wycofanie czynności bo nie dodano paczki faktur
      _mp.cancel()
   ?}

|? _akcja='Popraw'
   | _todo
||
   exec('fpacz_win_edit_set','!lum_fak_dpac');
   params_set('context',_context);
   _fpacz:={? _todo || _out.FPACZ || FPACZ.ref() ?};
   {? _fpacz
   ||
      FPACZ.cntx_psh();
      FPACZ.prefix();
      {? FPACZ.seek(_fpacz)
      ||
         {? FPACZ.IL=0
         ||
            {? FPACZ.edit("{? params_get().context.ZAKONCZ || '' || exec('chk_fpacz','!lum_fak_dpac') ?}")
            ||
               {? FPACZ.put()
               ||
                  {? _context.ZAKONCZ || _mp.done() ?};
                  _mp.descTodo()
               ?}
            ?}
         ||
            FUN.info('Wygenerowano dokumenty.\nEdycja niemożliwa.'@)
         ?}
      ?};
      FPACZ.cntx_pop()
   ?}

|? _akcja='Usuń'
||
   _fpacz:={? _todo || _out.FPACZ || FPACZ.ref() ?};
   {? _fpacz
   ||
      FPACZ.cntx_psh();
      {? ~_area_lum_fak || FPACZ.prefix() ?};
      {? ~FPACZ.seek(_fpacz)
      ||
         _fpacz:=null()
      ||
         {? FPACZ.IL=0
         ||
            {? FUN.ask('Usunąć bieżący wiersz?'@)
            ||
::             Czyszczę logi błędów generacji (FPACZLOG)
               exec('fpacz_log_del','umowy_faktury',FPACZ.ref());
               FPACZ.del()
            ?}
         ||
            FUN.info('Wygenerowano dokumenty, usunięcie niemożliwe.'@)
         ?};
         {? ~FPACZ.seek(_fpacz)
         ||
::          Wycofuję instancję jeśli nie znaleziono paczki faktur
            _mp.cancel()
         ?};
         _fpacz:=FPACZ.ref()
      ?};
      FPACZ.cntx_pop();
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _area_lum_fak || {? _fpacz || FPACZ.seek(_fpacz) ?} ?}
   ?}

|? _akcja='Zakończ_wer'
||
   {? exec('fpacz_zakoncz','faktury_wspolne',_autoakc)
   ||
      {? _autoakc | FPACZ.put() || _mp.done() ?}
   ?}

|? _akcja='Kopiuj' & _area_lum_fak
||
   exec('fpacz_win_edit_set','!lum_fak_dpac');
   params_set('context',_context);
   exec('fpacz_kopiuj0','!lum_fak_dpac');
   exec('fpacz_term_ae','!lum_fak_dpac');
   {? FPACZ.edit("{? params_get().context.ZAKONCZ || '' || exec('chk_fpacz','!lum_fak_dpac') ?}")
   ||
      FPACZ.NR:=exec('nr_pacz','!lum_fak_dpac');
      {? FPACZ.add()
      ||
         {? _context.ZAKONCZ || _mp.done() ?}
      ?}
   ?};
:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? var_pres('FPACZ',_outa)<>type_of(~~) & _outa.FPACZ
   ||
::    Dodano paczkę faktur
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      FPACZ.seek(_outa.FPACZ)
   ||
::    Wycofanie czynności bo nie dodano paczki faktur
      _mp.cancel()
   ?}

||
   _mp.error('Nieobsługiwana ścieżka.'@)
?};
VAR_DEL.delete('jaka_tp');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('FPACZ',_out)=type_of(null()) & _out.FPACZ
||
   'Zakończ rejestrację paczki faktur: %1'@@ [exec('record','#to_string',_out.FPACZ)]
||
   ''
?}


\nr_pacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: nadanie kolejnego numeru dla paczki faktur
::  OLD: \nr_pacz/um.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
FPACZ.cntx_psh();
FPACZ.index('ROK');
FPACZ.prefix(ST.ODDZ,ST.AR,F_HELP.RODZ);
{? FPACZ.last()
|| _wyn:=FPACZ.NR+1
|| _wyn:=1
?};
FPACZ.cntx_pop();
_wyn


\be_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: przed redakcja daty sprawdzanie czy nie byla wpisana automatycznie
::  OLD: \be_data/um.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? FPACZ.FAS().KOD<>'M' & FPACZ.FAS().KOD<>'K' & FPACZ.FAS().KOD<>'P' & FPACZ.FAS().KOD<>'R'
|| _wyn:=1
?};
_wyn


\be_fas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: przed redakcją FPACZ.FAS
::  OLD: \ae_fas/um.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
UMO.WYS:='T';
{? FPACZ.FPACZ='' | FPACZ.FPACZ_DO='' || _wyn:=1 ?};
_wyn


\ae_fas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: po redakcji FPACZ.FAS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FPACZ.FAS().KOD='N'
||
   FUN.info('Niedozwolony sposób fakturowania.'@);
   0
||
   exec('fpacz_set_efld_opt','umowy_faktury');
   1
?}


\be_pomfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: przed redakcja pola POM.fpacz - zmiennej pomocniczej do sposobow fakturowania
::  OLD: \be_pomfp/um.fml
::----------------------------------------------------------------------------------------------------------------------
POM.FPACZ:=FPACZ.FPACZ;
_wyn:=0;
{? FPACZ.FAS().KOD='M' | FPACZ.FAS().KOD='K' | FPACZ.FAS().KOD='P' |  FPACZ.FAS().KOD='R'
|| _wyn:=1
?};
_wyn


\f3_pomfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: slownik pola POM.fpacz - zmiennej pomocniczej do sposobow fakturowania
::       dla miesiecznego, kwartalnego i polrocznego fakturowania
::  OLD: \f3_pomfp/um.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('data');
{? cur_afld='FPACZ'
|| data:="FPACZ.OD:=date(FPACZ.AR,_a,1)"
|| data:="FPACZ.DO:=date(FPACZ.AR,_b,0)"
?};

{? FPACZ.FAS().KOD='M'
|| popup(0,'Wybierz miesiąc:',
           'styczeń',,"($('FPACZ.'+cur_afld))():='styczeń';data(1,1)",
              'luty',,"($('FPACZ.'+cur_afld))():='luty';data(2,2)",
            'marzec',,"($('FPACZ.'+cur_afld))():='marzec';data(3,3)",
          'kwiecień',,"($('FPACZ.'+cur_afld))():='kwiecień';data(4,4)",
               'maj',,"($('FPACZ.'+cur_afld))():='maj';data(5,5)",
          'czerwiec',,"($('FPACZ.'+cur_afld))():='czerwiec';data(6,6)",
            'lipiec',,"($('FPACZ.'+cur_afld))():='lipiec';data(7,7)",
          'sierpień',,"($('FPACZ.'+cur_afld))():='sierpień';data(8,8)",
          'wrzesień',,"($('FPACZ.'+cur_afld))():='wrzesień';data(9,9)",
       'październik',,"($('FPACZ.'+cur_afld))():='październik';data(10,10)",
          'listopad',,"($('FPACZ.'+cur_afld))():='listopad';data(11,11)",
          'grudzień',,"($('FPACZ.'+cur_afld))():='grudzień';data(12,12)")
|? FPACZ.FAS().KOD='K'
|| popup(0,'Wybierz kwartał:',
         'I kwartał',,"($('FPACZ.'+cur_afld))():='I kwartał';data(1,3)",
        'II kwartał',,"($('FPACZ.'+cur_afld))():='II kwartał';data(4,6)",
       'III kwartał',,"($('FPACZ.'+cur_afld))():='III kwartał';data(7,9)",
        'IV kwartał',,"($('FPACZ.'+cur_afld))():='IV kwartał';data(10,12)")
|? FPACZ.FAS().KOD='P'
|| popup(0,'Wybierz półrocze:',
        'I półrocze',,"($('FPACZ.'+cur_afld))():='I półrocze';data(1,6)",
       'II półrocze',,"($('FPACZ.'+cur_afld))():='II półrocze';data(7,12)")
|? FPACZ.FAS().KOD='R'
|| ($('FPACZ.'+cur_afld))():='rok';data(1,12)
?};

VAR_DEL.delete('data');
($('FPACZ.'+cur_afld))()


\ae_pomfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: po redakcji pola POM.fpacz - zmiennej pomocniczej do sposobow fakturowania
::  OLD: \ae_pomfp/um.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;

:: funkcje do zapisu pola data od/do
VAR_DEL.delete('data');
{? cur_afld='FPACZ'
|| data:="FPACZ.OD:=date(FPACZ.AR,_a,1)"
|| data:="FPACZ.DO:=date(FPACZ.AR,_b,0)"
?};

{? FPACZ.FAS().KOD='M'
||
   {? ($('FPACZ.'+cur_afld))()='styczeń'
   || data(1,1)
   |? ($('FPACZ.'+cur_afld))()='luty'
   || data(2,2)
   |? ($('FPACZ.'+cur_afld))()='marzec'
   || data(3,3)
   |? ($('FPACZ.'+cur_afld))()='kwiecień'
   || data(4,4)
   |? ($('FPACZ.'+cur_afld))()='maj'
   || data(5,5)
   |? ($('FPACZ.'+cur_afld))()='czerwiec'
   || data(6,6)
   |? ($('FPACZ.'+cur_afld))()='lipiec'
   || data(7,7)
   |? ($('FPACZ.'+cur_afld))()='sierpień'
   || data(8,8)
   |? ($('FPACZ.'+cur_afld))()='wrzesień'
   || data(9,9)
   |? ($('FPACZ.'+cur_afld))()='październik'
   || data(10,10)
   |? ($('FPACZ.'+cur_afld))()='listopad'
   || data(11,11)
   |? ($('FPACZ.'+cur_afld))()='grudzień'
   || data(12,12)
   |? +($('FPACZ.'+cur_afld))()
   || FUN.info('Brak pozycji w słowniku.'@);
      _wyn:=0
   ?}
|? FPACZ.FAS().KOD='K'
||
   {? ($('FPACZ.'+cur_afld))()='I kwartał'
   || data(1,3)
   |? ($('FPACZ.'+cur_afld))()='II kwartał'
   || data(4,6)
   |? ($('FPACZ.'+cur_afld))()='III kwartał'
   || data(7,9)
   |? ($('FPACZ.'+cur_afld))()='IV kwartał'
   || data(10,12)
   |? +($('FPACZ.'+cur_afld))()
   || FUN.info('Brak pozycji w słowniku.'@);
      _wyn:=0
   ?}
|? FPACZ.FAS().KOD='P'
||
   {? ($('FPACZ.'+cur_afld))()='I półrocze'
   || data(1,6)
   |? ($('FPACZ.'+cur_afld))()='II półrocze'
   || data(7,12)
   |? +($('FPACZ.'+cur_afld))()
   || FUN.info('Brak pozycji w słowniku.'@);
      _wyn:=0
   ?}
|? FPACZ.FAS().KOD='R'
||
   {? ($('FPACZ.'+cur_afld))()='rok'
   || data(1,12)
   |? +($('FPACZ.'+cur_afld))()
   || FUN.info('Brak pozycji w słowniku.'@);
      _wyn:=0
   ?}
?};

VAR_DEL.delete('data');
_wyn


\wz_typsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: filtr - typy sprzedazy
::  OLD: \wz_typsp/um.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.ZAK:='N';
BEER.KOR:='N';
BEER.ZAL:='N';
_upr:=exec('get','#params',2002,2,OPERATOR.USER);
{? _upr<>'' || _where:=' and position("TYPYSP".T in \''+_upr+'\')<>0' || _where:='' ?};
TYPYSP.prefix();
TYPYSP.f_clear();
TYPYSP.f_set('T',,'"TYPYSP".ZAL_ROZ<>\'S\' and "TYPYSP".ZAK=\'N\''
   ' and "TYPYSP".KOR=\'N\' and "TYPYSP".ZAL=\'N\' and "TYPYSP".HIS=\'N\''+_where);
TYPYSP.win_dict('WER');
''


\fp_ae_tz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: kontrola po wprowadzeniu terminu zaplaty
::  OLD: \fp_ae_tz/um.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_wyn


\be_fp_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: przed redakcja FPACZ.PL
::  OLD: \be_fp_pl/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
FZ.FORMAPLB:=FPACZ.PL().KOD;
FPACZ.TERM='P' | FPACZ.TERM='A'


\ae_fp_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: okresla termin platnosci na podstawie daty wystawienia, podpowiada gdy pole nie jest uzupelnione
::   WE: [_a] 0 - pytanie (domyślnie), 1 - bez pytania
::  OLD: \ae_fp_pl/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_bez_pyt:=0;
{? var_press('_a')=type_of(0)
|| _bez_pyt:=_a
?};
FZ.FORMAPLA:=FPACZ.PL().KOD;
{? FZ.FORMAPLB<>FZ.FORMAPLA
||
   _tz:=FPACZ.TZ;
   _ldni:=#exec('FindInSet','#table','ZR_SLO','SLO_NR',2,FPACZ.PL,"ZR_SLO.WAR",,,'0');
   _tn:=FPACZ.DW+_ldni;
   {? exec('get','#params',300202,2)='T'
   || {! |? exec('spr_kal','faktury_plat',_tn)=1
      |! _tn+=1
      !}
   ?};
   _txt:='Zmieniono liczbę dni płatności na %1.\nCzy zaaktualizować termin płatności?'@[form(_ldni,,0,'99')];
   {? _tz<>_tn & (_bez_pyt | FUN.ask(_txt))
   || FPACZ.TZ:=_tn
   ?}
?};
''


\pacz_brd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009+]
:: OPIS: przed redakcja domyslnego rachunku dla faktur
::  OLD: \pacz_brd/um.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ps_65','rachunki')='T'


\bl_par_pos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: wartosc poczatkowa parametru
::  OLD: \bl_par_pos/um.fml
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',100112,2)


\bl_par_zgl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: wartosc poczatkowa parametru
::  OLD: \bl_par_zgl/um.fml
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',100116,2)


\chk_fpacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: po rekord tab. FPACZ
::  OLD: \chk_fpacz/um.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? FPACZ.FAS=null | FPACZ.FAS().KOD='N'
||
   FUN.info('Należy podać sposób fakturowania.'@);
   _wyn:='FAS'
?};
{? _wyn='' & FPACZ.TYPYSP=null
||
   FUN.info('Należy podać typ dokumentu sprzedaży.'@);
   _wyn:='TYPYSP'
?};
{? _wyn='' & FPACZ.DS=date(0,0,0)
||
   FUN.info('Należy podać datę sprzedaży.'@);
   _wyn:='DS'
?};
{? _wyn='' & exec('be_pomfp','!lum_fak_dpac') & FPACZ.FPACZ=''
||
   FUN.info('Należy podać okres fakturowania.'@);
   _wyn:='FPACZ'
?};
{? _wyn='' & exec('be_pomfp','!lum_fak_dpac') & FPACZ.FPACZ_DO=''
||
   FUN.info('Należy podać okres fakturowania.'@);
   _wyn:='FPACZ_DO'
?};
{? _wyn='' & exec('be_data','!lum_fak_dpac')
||
   {? FPACZ.OD=date(0,0,0)
   ||
      FUN.info('Należy wypełnić pole Od daty.'@);
      _wyn:='OD'

   |? FPACZ.DO=date(0,0,0)
   ||
      FUN.info('Należy wypełnić pole Do daty.'@);
      _wyn:='DO'
   ?}
?};
{? _wyn='' & FPACZ.WAL=null()
||
   FUN.info('Należy podać walutę.'@);
   _wyn:='WAL'
?};
{? _wyn='' & FPACZ.WAL<>exec('bl_nwal','ustawienia') & FPACZ.DTK=date(0,0,0)
||
   FUN.info('Należy podać datę kursu.'@);
   _wyn:='DTK'
?};
{? _wyn='' & (FPACZ.TERM='P' | FPACZ.TERM='A') & FPACZ.PL=null
||
   FUN.info('Należy podać sposób płatności.'@);
   _wyn:='PL'
?};
{? _wyn='' & (FPACZ.TERM='P' | FPACZ.TERM='A') & FPACZ.TZ=date(0,0,0)
||
   FUN.info('Należy podać termin płatności.'@);
   _wyn:='TZ'

|? _wyn='' & (FPACZ.TERM='P' | FPACZ.TERM='A') & FPACZ.TZ<>date(0,0,0)
   & exec('get_par','#parametr',38)='N' & FPACZ.TZ<FPACZ.DW
||
   FUN.info('Termin zapłaty nie może być wcześniejszy od daty wystawienia.'@);
   _wyn:='TZ'
?};
{? _wyn='' & exec('itsPositive','#field',,,FPACZ.RAB)=0
||
   _wyn:='RAB'

|? _wyn='' & FPACZ.RAB>=100
||
   FUN.info('Rabat nie może być wiekszy od wartości 99,99%%.'@);
   _wyn:='RAB'
?};
{? _wyn='' & (FPACZ.DW=date(0,0,0) | FPACZ.DW~1<>ST.AR | FPACZ.DW~2<>ST.AM)
|| _oki:=0;
:: kontrola okresu na popraw i ewentualna zmiana PARSES
   {? FPACZ.DW<>date(0,0,0) & EANX.OKR<>null()
    & (FPACZ.DW~1)=EANX.OKR().ROK & (FPACZ.DW~2)=EANX.OKR().MC
   || _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=ST.AR:=FPACZ.DW~1;
      _args.AM:=ST.AM:=FPACZ.DW~2;
      __PARSES.setVal('OkresRok',_args);
      _oki:=1
   ?};
   {? ~_oki
   || FUN.info('Data wystawienia musi być zgodna z bieżącym okresem obrachunkowym.'@);
      _wyn:='DW'
   ?}
?};
_wyn


\fpacz_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Dołącz
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env_fak:=params_get().env_fak;

_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LUM_FAK_DPAC';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'RODZ',_env_fak.RODZ);
_params.CONTEXT:=obj_new('ZAKONCZ');
_params.CONTEXT.ZAKONCZ:=0;

exec('mp_run','#b__box',_params)


\fpacz_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Popraw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LUM_FAK_DPAC';
_params.UIDREF:=FPACZ.uidref();
_params.AKCJA:='Popraw';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'RODZ',FPACZ.RODZ);
_params.CONTEXT:=obj_new('ZAKONCZ');
_params.CONTEXT.ZAKONCZ:=0;
_eanxokr:=EANX.OKR;
EANX.OKR:=ST.OKR_REF;

exec('mp_run','#b__box',_params);
EANX.OKR:=_eanxokr


\fpacz_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Usuń
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LUM_FAK_DPAC';
_params.UIDREF:=FPACZ.uidref();
_params.AKCJA:='Usuń';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'RODZ',FPACZ.RODZ);

exec('mp_run','#b__box',_params)


\fpacz_zakoncz_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Zakończ w oknie redakcji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_chk:=exec('chk_fpacz','!lum_fak_dpac');
{? _chk<>''
||
   'edit:'+_chk
||
   _env:=params_get();
   _context:=_env.context;
   _context.ZAKONCZ:=exec('fpacz_zakoncz','faktury_wspolne');
   {? _context.ZAKONCZ || 'key:F2' || '' ?}
?}


\fpacz_zakoncz_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Zakończ w oknie wertowania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LUM_FAK_DPAC';
_params.UIDREF:=FPACZ.uidref();
_params.AKCJA:='Zakończ_wer';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'RODZ',FPACZ.RODZ);

exec('mp_run','#b__box',_params)


\fpacz_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Ustawia okno redakcji tabeli FPACZ, wymagane pola, okna słowników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_red:=exec('fpacz_win_edit','umowy_faktury');
_ff:="params_exec('priority_action_red','#b__box')";
FPACZ.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Pr&iorytet'@],_ff);
_ff:="params_exec('fpacz_zakoncz_red','!lum_fak_dpac')";
exec('zakoncz','#window',FPACZ,_win_red,1,_ff,1, exec('help_red_zakoncz','#window','A'),
   exec('text_red_zakoncz','#window','A'));
exec('ok_esc','#window',FPACZ,_win_red,1,,,,,exec('help_red_ok','#window','Z'),exec('text_red_ok','#window'),
   exec('help_red_esc','#window','A'));
FPACZ.win_edit(_win_red);
exec('fpacz_set_efld_opt','umowy_faktury',_win_red);
:: TYPYSP
TYPYSP.win_dict('WER');
:: UM
UM.win_dict('WER');
~~


\bl_fprod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2010]
:: OPIS:
::  OLD: \bl_fprod/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
F_HELP.RODZ


\bd_typkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: formula przed wyswietl dla KODSLU.PTYP
::  OLD: \bd_typkh/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? FPACZ.TYP='R' || KODSLU.PTYP:='Rolnik ryczałtowy'
|? FPACZ.TYP='P' || KODSLU.PTYP:='Płatnik podatku VAT'
|? FPACZ.TYP='I' || KODSLU.PTYP:='Inny'
|| KODSLU.PTYP:=''
?}


\fpacz_rekb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: przed rekord tabeli FPACZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
||
   exec('fpacz_actions','!lum_fak_dpac');
   exec('fpacz_win_edit_set','!lum_fak_dpac');
   exec('fpacz_set_efld_opt','umowy_faktury');
   jaka_tp:='FPACZ.SLU'
?};
~~


\fpacz_actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: FPACZ.actions & FPACZ.actions_grayed
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_generuj:={? FPACZ.IL>0 || 'GF(WP)' || '' ?};
_act:=
   {? FPACZ.STAT_REJ='N' || 'AGF(WP)'
   |? FPACZ.STAT_REJ='T' || 'PUZA'
   || 'PUZG'
   ?}
   +
   _generuj;
FPACZ.actions_grayed('WER',_act)


\fpacz_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Wycofaj
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_stat_rej:='';
{? FPACZ.IL>0
||
   FUN.info('Wygenerowano dokumenty, wycofanie akceptacji niemożliwe.'@)

|? FPACZ.STAT_REJ='Z'
||
   {? FUN.ask('Wycofać zakończenie paczki faktur?'@) || _stat_rej:='N' ?}

|? FPACZ.STAT_REJ='T'
||
   _choice:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia paczki faktur.'@,,
      'Akceptację i &zakończenie'@,'&Akceptację'@);
   {? _choice=1 || _stat_rej:='N' |? _choice=2 || _stat_rej:='Z' ?}
?};
{? _stat_rej<>''
||
   exec('fpacz_pp_del','umowy_faktury');
   FPACZ.STAT_REJ:=_stat_rej;
   FPACZ.STAT_GEN:=exec('stat_gen','umowy_faktury',0);
   FPACZ.put()
?}


\fpacz_wal_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: po redakcji FPACZ.WAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('fpacz_set_efld_opt','umowy_faktury');
1


\fpacz_term_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: po redakcji FPACZ.TERM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('fpacz_set_efld_opt','umowy_faktury');
{? FPACZ.TERM='D'
||
   FPACZ.PL:=null();
   FZ.FORMAPLA:='';
   FPACZ.TZ:=date(0,0,0)
?};
1


\fpocz_drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Drukuj błędy generacji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','','lum_umxfp_*','Błędy generacji',2,,,,,,'W')


\fpacz_dokumenty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Dokumenty
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
tab_sel(3)


\faks_fpacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Dokumenty - Paczka faktur
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env_fak:=params_get().env_fak;
tab_sel({? _env_fak.RODZ='U' || 1 || 2 ?})


\leg_fpacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Paczka faktur - Legenda
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','FPACZ#01')


\fpacz_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Paczka faktur - Kopiuj
::----------------------------------------------------------------------------------------------------------------------
_env_fak:=params_get().env_fak;

_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LUM_FAK_DPAC';
_params.AKCJA:='Kopiuj';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'RODZ',_env_fak.RODZ);
_params.CONTEXT:=obj_new('ZAKONCZ');
_params.CONTEXT.ZAKONCZ:=0;

exec('mp_run','#b__box',_params)


\fpacz_kopiuj0
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Paczka faktur - Kopiuj - przygotowanie
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__fpacz0');
__fpacz0:=exec('obj_new','#buf','FPACZ');
__fpacz0.get();
FPACZ.blank();
_nf:=FPACZ.fld_num();
{! _if:=1.._nf
|! _pole:=FPACZ.fld_acr(_if);
   {? _pole='TFAK' | _pole='FAS' | _pole='FPACZ' | _pole='FPACZ_DO' | _pole='PL' | _pole='TERM' | _pole='WAL' |
      _pole='SWAL' | _pole='DRB' | _pole='PAR_POS' | _pole='PAR_ZGL' | _pole='PAR_PL' | _pole='ZAL' | _pole='RAB_NAL'
   || _kopiuj:=1
   |? _pole='IL' | _pole='MEMO' | 2+_pole='OP' | _pole='IDADD'
   || _kopiuj:=0
   || _kopiuj:=(FPACZ.fld_fml(_pole,'*BLANK')="")
   ?};
   {? _kopiuj
   || _fml:='FPACZ.'+_pole+':=__fpacz0.'+_pole;
      _fml:=$_fml;
     _fml()
   ?}
!};
{? FPACZ.TERM<>'D' & FPACZ.DW<>__fpacz0.DW
|| exec('ae_fp_pl','!lum_fak_dpac',1)
?};
VAR_DEL.delete('__fpacz0');
~~


\fpacz_rab_nal_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wartość domyślna FPACZ.RAB_NAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',100118)


\fpacz_rab_nal_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Po redakcji FPACZ.RAB_NAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('fpacz_set_efld_opt','umowy_faktury');
1


\fpacz_rab_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Po redakcji FPACZ.RAB
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? fld()>99.99
|| FUN.info('Możliwe wprowadzenie maksymalnego rabatu o wartości: 99.99 \%'@); 0
|| 1
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 a5a239d2b08e73da2e8b48eb4d78bb617cc774f40ef2a9e64cc3739f9436b3d511ae9dabc119519435b0ca3a35f8994270206596ad19c521974fe16e446c9974d5369fcb62650a45ba380d9f43014c010594a30463c2845e6bd01fe781fa6108e22ebd33d16e186debae9ea97d431b3a6de97e8678591010036e09cf4701b352

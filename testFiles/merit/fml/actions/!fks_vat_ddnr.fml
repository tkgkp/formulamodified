:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_vat_ddnr.fml
:: Utworzony: 07.07.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VAT_DDNR - Przenumerowanie w rejestrach VAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przenumerowanie dokumentów w rejestrze VAT - główna formuła czynności FKS_VAT_DDNR.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
_pyt:=FUN.choice('Wybierz sposób przenumerowania.'@,0,
   'Wg &daty'@,
   'Wg &numeru w rejestrze księgowym'@,
   'Wg dodatkowej &formuły'@
);
{? _pyt=1
|| {? TREE_VAT.POZ=2
   || exec('num_vat','!fks_vat_ddnr',1)
   || exec('num_vat','!fks_vat_ddnr')
   ?}
|? _pyt=2
|| exec('num_rej_ks','!fks_vat_ddnr')
|? _pyt=3
|| {? Plugin.run('VAT_T_DVAT_001')=0
   || FUN.info('Nie zdefiniowano dodatkowej formuły na przenumerowanie dokumentów w rejestrze VAT.'@)
   ?}
?}


\num_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja 'przenumeruj' w okienkach wertowania tabeli DVAT
::   WE: [_a - jest jesli akcja wywolywana z wybranego rejestru VAT]
::  OLD: \num_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
DVAT.cntx_psh();
{? _=1
|| exec('utw_dat','!fks_vat_ddnr',DVAT.RVAT().KVAT().SYM);
   {? TAB_DAT.select()
   || DVAT.index(DVAT.ndx_tmp('',1,'OBR',,0,'RVAT',,0,TAB_DAT.POLE,,0,'DAT1',,0));
      DVAT.prefix(SSTALE.AO,RVAT.ref());
      _lp:=1;
      {? DVAT.first() || do(); {! |? DVAT.NR:=_lp; _lp+=1; DVAT.put(); DVAT.next() !}; end() ?}
   ?};
   obj_del(TAB_DAT); DVAT.ndx_drop()
|| _kraj:=DVAT.KRAJ().KOD; _odd:=DVAT.ODD().OD;
   MVAT:=sql('select KVAT.SYM as KLVAT, KVAT.NAZ as KLNAZ, '''+30*' '+''' as DD, '''+4*' '+'''as POLE'+
             ' from DVAT '+
             'join ODD using (DVAT.ODD, ODD.REFERENCE) '+
             'join RVAT using (DVAT.RVAT, RVAT.REFERENCE) '+
             'join KVAT using (RVAT.KVAT, KVAT.REFERENCE) '+
             'join OKRO_F using (DVAT.OBR, OKRO_F.REFERENCE) '+
             'join SLO using (DVAT.KRAJ, SLO.REFERENCE) '+
             ' where SLO.KOD='''+_kraj+''' AND OKRO_F.NR='+$SSTALE.AO().NR+
             ' AND ODD.OD='''+_odd+''' '+
             ' group by KVAT.SYM, KVAT.NAZ');
   {? MVAT.first()
   || {!
      |? exec('utw_dat','!fks_vat_ddnr',MVAT.KLVAT);
         TAB_DAT.prefix(MVAT.KLVAT);
         {? TAB_DAT.first() || MVAT.DD:=TAB_DAT.DATA; MVAT.POLE:=TAB_DAT.POLE; MVAT.put() ?};
         MVAT.next()
      !};
      _w:=MVAT.mk_sel('Kolejności numerowania'@,'P',,'mvat_wer',,,,,'U','T');
      MVAT.win_fld(_w,,'KLVAT',,,10,,,'Klasa ewidencji - kod'@);
      MVAT.win_fld(_w,,'KLNAZ',,,30,,,'Klasa ewidencji - nazwa'@);
      MVAT.win_fld(_w,,'DD',,,30,,,'Data do kolejności'@);
      MVAT.win_act(_w,0,'Formuła','Prze&numeruj'@@,,,,"sel_exit()",1,,,,'N');
      MVAT.win_act(_w,0,'Formuła','&Popraw datę'@@,,,,
                  "TAB_DAT.prefix(MVAT.KLVAT);
                  {? TAB_DAT.select || MVAT.DD:=TAB_DAT.DATA; MVAT.POLE:=TAB_DAT.POLE; MVAT.put()?}",,,,,'P');
      MVAT.win_act(_w,0,'Kolejność');
      MVAT.win_sel(_w);
      {? MVAT.select()
      || {? OPERATOR.DEPT
         || _RV:=sql('select RVAT.SYM as RSYM, ODD.OD as ODZ from REJ join ODD join VAT_REJ join RVAT where REJ.ROK=:_a and REJ.ODD=:_b group by RVAT.SYM, ODD.OD',SSTALE.AR,OPERATOR.DEPT)
         || _RV:=sql('select ODD.OD as ODZ,RVAT.SYM as RSYM from ODD join REJ join VAT_REJ join RVAT where REJ.ROK=:_a group by ODD.OD,RVAT.SYM',SSTALE.AR)
         ?};
         {? _RV.first()
         || DAT2:=DVAT.ndx_tmp('',1,'OBR',,0,'RVAT',,0,'DAT2',,0,'DAT1',,0);
            DAT3:=DVAT.ndx_tmp('',1,'OBR',,0,'RVAT',,0,'DAT3',,0,'DAT1',,0);
            DAT4:=DVAT.ndx_tmp('',1,'OBR',,0,'RVAT',,0,'DAT4',,0,'DAT1',,0);
            MVAT.index(MVAT.ndx_tmp('',1,'KLVAT',,0));
            RVAT.index('SYM'); RVAT.prefix(REF.FIRMA);
            {!
            |? {? RVAT.find_key(_RV.RSYM,_RV.RSYM)
               || {? MVAT.find_key(RVAT.KVAT().SYM)
                  || ($('DVAT.index('+MVAT.POLE+')'))();
                     DVAT.prefix(SSTALE.AO,RVAT.ref());
                     do();
                     {? DVAT.first()
                     || _lp:=1; {! |? DVAT.NR:=_lp; _lp+=1; DVAT.put(); DVAT.next() !}
                     ?};
                     end()
                  ?}
               ?};
               _RV.next()
            !};
            &DAT4; &DAT2; &DAT3; DVAT.ndx_drop()
         ?}
      ?}
   ?};
   obj_del(MVAT); obj_del(TAB_DAT)
?};
DVAT.cntx_pop();
return(1)


\utw_dat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Utworzenie tabeli TAB_DAT dla akcji 'przenumeruj' w okienkach DVAT
::   WE: _a - symbol klasy ewidencji VAT
::  OLD: \utw_dat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('TAB_DAT')<100
|| TAB_DAT:=tab_tmp(2,'KVAT','STRING[10]','Klasa ewidencji','DATA','STRING[30]','Wedlug daty',
                      'POLE','STRING[8]','Pole');
   _wer:=TAB_DAT.mk_sel('Wybierz datę kolejności:'@,,,'tab_dat_wer',,,,,'U');
   TAB_DAT.win_fld(_wer,,'DATA');
   TAB_DAT.win_act(_wer,0,'Formuła','&Wybierz'@@,,,,"sel_exit()",1,,,,'W');
   TAB_DAT.win_sel(_wer)
?};
TAB_DAT.prefix();
TAB_DAT.KVAT:=_a;
{? 4+_a='ImpT'
|| TAB_DAT.DATA:='Data odprawy celnej';
   TAB_DAT.POLE:='DAT2';
   TAB_DAT.add();
   TAB_DAT.DATA:='Data przekroczenia granicy';
   TAB_DAT.POLE:='DAT4';
   TAB_DAT.add()
|? 4+_a='Zaku' | 4+_a='ImpU'
|| TAB_DAT.DATA:='Data otrzymania';
   TAB_DAT.POLE:='DAT4';
   TAB_DAT.add();
   TAB_DAT.DATA:='Data wystawienia';
   TAB_DAT.POLE:='DAT2';
   TAB_DAT.add()
|? 5+_a='SprzE'
|| TAB_DAT.DATA:='Data przekroczenia granicy';
   TAB_DAT.POLE:='DAT4';
   TAB_DAT.add();
   TAB_DAT.DATA:='Data wystawienia';
   TAB_DAT.POLE:='DAT2';
   TAB_DAT.add()
|| TAB_DAT.DATA:='Data wystawienia';
   TAB_DAT.POLE:='DAT2';
   TAB_DAT.add()
?};
TAB_DAT.DATA:='Termin płatności';
TAB_DAT.POLE:='DAT3';
TAB_DAT.add()


\num_rej_ks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Przenumerowanie wg numeru w rejestrze księgowym
::----------------------------------------------------------------------------------------------------------------------
DVAT.cntx_psh();
_irej:=DVAT.ndx_tmp('',1, 'KRAJ',,0, 'OBR',,0, 'ODD',,0, 'RVAT',,0, 'REJ',,0, 'NR_W_REJ',,0);
DVAT.index(_irej);
{? TREE_VAT.POZ=0
|| DVAT.prefix(kraj,SSTALE.AO)
|? TREE_VAT.POZ=1
|| DVAT.prefix(kraj,SSTALE.AO,OPERATOR.DEPT)
|| DVAT.prefix(kraj,SSTALE.AO,OPERATOR.DEPT,RVAT.ref())
?};
{? DVAT.first()
|| {!
   |? {? TREE_VAT.POZ=0
      || DVAT.prefix(kraj,SSTALE.AO,DVAT.ODD,DVAT.RVAT)
      |? TREE_VAT.POZ=1
      || DVAT.prefix(kraj,SSTALE.AO,OPERATOR.DEPT,DVAT.RVAT)
      || DVAT.prefix(kraj,SSTALE.AO,OPERATOR.DEPT,RVAT.ref())
      ?};
      _first:={? DVAT.first() || DVAT.REJ || null ?};
      _last:={? DVAT.last() || DVAT.REJ || null ?};
      {? _first<>_last
      || _first:={? DVAT.first() || DVAT.REJ().KOD+' '+DVAT.REJ().NAZ || '' ?};
         _last:={? DVAT.last() || DVAT.REJ().KOD+' '+DVAT.REJ().NAZ || '' ?}
      ?};
      {? _first=_last
      || do();
         {? DVAT.first()
         || _lp:=0;
            {!
            |? DVAT.NR:=(_lp+=1);
               DVAT.put();
               DVAT.next()
            !}
         ?};
         end()
      ?};
      DVAT.last();
      {? TREE_VAT.POZ=0
      || DVAT.prefix(kraj,SSTALE.AO)
      |? TREE_VAT.POZ=1
      || DVAT.prefix(kraj,SSTALE.AO,OPERATOR.DEPT)
      || DVAT.prefix(kraj,SSTALE.AO,OPERATOR.DEPT,RVAT.ref())
      ?};
      DVAT.next()
   !}
?};
DVAT.ndx_drop(_irej);
DVAT.cntx_pop();
return(1)

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:43 c873ad554a07b69c4a2480f6150daff2ec7c7be04fdfb7db38041bfebbeb6fab1b88975a739d74e1aa9098071039e463f3ee515e50cb1034ef77194b99fd19f513a2d62cbe61cb9fab943631af4e3ab3cce9781a17cd6c2c6970d189f9cf94949a8641ba17d7a7b09a67e1200ea82b4e4305dcfb670b5f8f37796020d2fdee62

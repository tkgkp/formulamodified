:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_zwro.fml
:: Utworzony: 27.08.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_ZWRO - Analizy zapisów wyróżników
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Analizy zapisów wyróżników - główna formuła czynności FKS_DKS_ZWRO.
::       Wyswietla zapisy księgowe z wyróżnikami według słowników
::  OLD: \sl_wyr/wyrozn.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
{? exec('czy_wyroz','wyrozniki','Wyróżniki')
|| SLUAPPL.index('NAZ');
   SLUAPPL.prefix('F');
   SLO.index('SL');
   SLU.index('NAZ');
   exec('tab_s','wyrozniki');
   SSTALE.AR();
   _fpow:='\'pow_'+ROK_F.KOD+'*\'';
   _fpoz:='\'pozy'+ROK_F.KOD+'*\'';
   _fdok:='\'doku'+ROK_F.KOD+'*\'';
   _dept:=OPERATOR.DEPT;
   {? var_pres('WYR')>0 || obj_del(WYR) ?};
   WYR:=sql('select '+
   '/*+MASK_FILTER(POW,:_a) MASK_FILTER(POZ,:_b) MASK_FILTER(DOK,:_c) '+
   'INDEX (POW,POZ) */ '+
   'SLU.NAZ, SLO.KOD, SLO.TR, POZ.KON, DOK.DTW, DOK.DOP, DOK.NK, POW.KW,'+
   'case when POZ.STR=\'Wn\' then POW.KW else 0 end as WN,'+
   'case when POZ.STR=\'Ma\' then POW.KW else 0 end as MA,'+
   'POZ.OP, ODD.OD as OD, REJ.KOD as REJ, DOK.NR, KS.SYM as KS_SYM, KS.NAZ as KS_NAZ, POZ.POZ, POZ.REFERENCE as REF '+
   'from @POW join SLO using (POW.SLO, SLO.REFERENCE) '+
   'join SLUAPPL using (POW.SLU, SLUAPPL.REFERENCE) '+
   'join SLU using (SLUAPPL.SLU, SLU.REFERENCE) '+
   'join @POZ using (POW.POZ, POZ.REFERENCE) '+
   'join KOM using (POZ.KOM,KOM.REFERENCE) '+
   'join KS using (KOM.KS,KS.REFERENCE) '+
   'join @DOK using (POZ.DOK, DOK.REFERENCE) '+
   'join ODD using (DOK.ODD, ODD.REFERENCE) '+
   'join REJ using (DOK.REJ,REJ.REFERENCE) '+
   'where POZ.ZP=''T'' '+
   {? OPERATOR.DEPT
   || ' AND ODD.OD='+''''+OPERATOR.DEPT().OD+''''+' '
   || ''
   ?}+
   ' AND POZ.REFERENCE<\'pozy'+ROK_F.KOD+form(SSTALE.AO().NR+1,-2)+'\' '+
   'order by SLU.NAZ, SLO.KOD, POZ.KON, DOK.DTW',_fpow,_fpoz,_fdok);
   _wer:=WYR.mk_sel('Zapisy z wyróżnikami'@,'P',,'wyr_wer',,,,,'U');
   WYR.win_fld(_wer,,'OD',,,8,,,'Jednostka księgowa'@);
   WYR.win_fld(_wer,,'REJ',,,8,,,'Rejestr'@);
   WYR.win_fld(_wer,,'NR',,,5,,,'Nr'@);
   WYR.win_fld(_wer,,'POZ',,,5,,,'Pozycja'@);
   WYR.win_fld(_wer,,'DTW',,,10,,,'Data zapisu'@);
   WYR.win_fld(_wer,,'KON',,,35,,,'Konto'@);
   WYR.win_fld(_wer,,'OP',,,45,,,'Opis'@);
   WYR.win_fld(_wer,,'KW',,,16,2,,'Kwota'@);
   WYR.win_act(_wer,,'Formuła','Zestawienia dla słownika'@@,,,,"
      SLO.cntx_psh();
      SLO.win_sel('ONE_SEL');
      exec('rep_exec','#b_report','FKS_DKS_ZWRO','fks_zap_w*',,1);
      SLO.cntx_pop()
   ",,,,,'Z');
   WYR.win_act(_wer,,'Szukaj');
   WYR.win_act(_wer,,'Kolejność');
   WYR.win_sel(_wer);
   _ed:=WYR.mk_edit('Zapis'@,0,'wyr_edit');
   WYR.win_efld(_ed,,'KON',,,45,,,'Konto'@);
   WYR.win_efld(_ed,,'DTW',,,42,,,'Data zapisu'@);
   WYR.win_efld(_ed,,'OD',,,45,,,'Jednostka księgowa'@);
   WYR.win_efld(_ed,,'REJ',,,45,,,'Rejestr'@);
   WYR.win_efld(_ed,,'NR',,,45,,,'Nr'@);
   WYR.win_efld(_ed,,'KW',,,45,2,,'Wn'@);
   WYR.win_efld(_ed,,'OP',,,45,,,'Opis'@);
   _btnzap:=WYR.win_ebtn(_ed,'text=%1'['Zapi&sz'@],'key:F2');
   _btnan:=WYR.win_ebtn(_ed,'text=%1'['Anuluj'@],'key:Esc');
   WYR.btn_eopt(_ed,_btnzap,'tooltip='+exec('help_red_ok','#window','Z'));
   WYR.btn_eopt(_ed,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
   WYR.win_edit(_ed);
   _wer_slo:=SLO.mk_sel('Pozycje słownika'@,'P',,'slo_wer2',,,,,'U');
   SLO.win_fld(_wer_slo,,'KOD',,,8,,,'Kod'@);
   SLO.win_fld(_wer_slo,,'TR',,,30,,,'Treść'@);
   SLO.win_sel(_wer_slo);
   SLO.win_act(_wer_slo,,'Rekord',,,,"
      {? WYR.prefix(SLU.NAZ,SLO.KOD); WYR.first
      || exec('findtmp','#color')
      ?}
   ");
   SLO.win_act(_wer_slo,0,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,,"
      exec('legenda','color','$Wyróżniki z wprowadzonymi zapisami'@)
   ",,,,,'L');
   SLO.win_act(_wer_slo,,'Szukaj');
   SLO.win_act(_wer_slo,,'Kolejność');
   _gr:=SLO.grp_make('Analiza wyróżników ze słownika: '@,,'#slowyrana');
   SLO.grp_sel(_gr,,_wer_slo,,"
      WYR.prefix(SLU.NAZ,SLO.KOD);
      WYR.first();
      grp_disp(WYR,WYR.win_sel('?'))
   ",,,,,,,,'maximized_with_title');
   SLO.grp_splt(_gr,,'vertical','left');
   SLO.grp_sel(_gr,WYR,_wer,,,,,,,,,,'maximized_with_title');
   SLO.win_sel(_gr);
   TAB_S.first();
   {!
   |? {? TAB_S.select(,1)
      || SLU.prefix(TAB_S.NAZ);
         {? SLU.first() & SLU.NAZ=TAB_S.NAZ & SLUAPPL.find_key(SLU.NAZ)
         || SLO.prefix(SLU.ref);
            SLO.hdr_sel(); SLO.hdr_sel(SLU.NAZ);
            SLO.select(,1)
         ?};
         1
      ?}
   !};
   VAR_DEL.delete('WYR','TAB_S');
   SLO.hdr_sel()
?}


\spr_zwyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Arkadiusz Maczka AMA [7.20] 20.11.2000
:: OPIS: Funkcja sprawdza, czy podano zakres wyroznikow
::       w odpowiedniej kolejnosci
::  OLD: \spr_zwyr/dok_zest.fml
::----------------------------------------------------------------------------------------------------------------------
{? WYROZN.WYROD<>null & WYROZN.WYRDO<>null & WYROZN.WYROD().KOD>WYROZN.WYRDO().KOD
|| FUN.info('Wybrano zakres wyróżników w odwrotnej kolejności.'@); 'WYROD'
|| ''
?}


\spr_zdat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Funkcja sprawdza, czy podano poprawnie zakres dat
::  OLD: \spr_zdat/dok_zest.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? WYROZN.DATA_OD=date(0,0,0) || FUN.info('Nie wypełniono daty początkowej.'@); _zwrot:='DATA_OD'
|? WYROZN.DATA_DO=date(0,0,0) || FUN.info('Nie wypełniono daty końcowej.'@); _zwrot:='DATA_DO'
|? WYROZN.DATA_OD>WYROZN.DATA_DO || FUN.info('Niepoprawny zakres dat.'@); _zwrot:='DATA_OD'
|? ~(WYROZN.DATA_OD>=dat_od & WYROZN.DATA_OD<=dat_do)
|| _zwrot:='DATA_OD';
   FUN.info('Data początkowa musi się zawierać w przedziale'+
       '\n od: %1 do: %2.'@[$dat_od,$dat_do])
|? ~(WYROZN.DATA_DO>=dat_od & WYROZN.DATA_DO<=dat_do)
|| _zwrot:='DATA_DO';
   FUN.info('Data końcowa musi się zawierać w przedziale'+
       '\n od: %1 do: %2.'@[$dat_od,$dat_do])
?};
_zwrot


\ae_ansyn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: formula po edycji analityki/syntetyki
::  OLD: \ae_ansyn/skid_slu.fml
::----------------------------------------------------------------------------------------------------------------------
{? -WYROZN.ANALSYNT<>'a' || WYROZN.OPBUDKON:=0 ?}; 1


\be_opbud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: przed radakcja opisu budowy konta
::  OLD: \be_opbud/skid_slu.fml
::----------------------------------------------------------------------------------------------------------------------
{? -WYROZN.ANALSYNT='a' || 1 || 0 ?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 040207f29d77fbaa8e20ed99961a017e91b6130b6d328bfebbb43c8779e082d7d08dde478697a49a23f586de8a3ab17655c0b8d7230267449dd3333276c0bdb0d0752ee4185ccd5d5f24bdbccd2f8da3d765f9dfe0d0355cb2776bb6eb3354c77f08b7492dba5063698db272945686ef114d602752c5d70591edef7079898539

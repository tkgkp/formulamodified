:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_hbd_ddef.fml
:: Utworzony: 12.11.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_HBD_DDEF - Definicje budżetowe
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Definicje budżetowe - główna formuła czynności CTR_HBD_DDEF
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::       _d - 1-szablony modeli 2-wersje planow
::  OLD: \widoki/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? _d=1
|| K_S_VIEW.win_edit('RED');
   _b.grp_sel(_c,SKID_MBN,'WER',_a,"exec('arsmbnw','!ctr_hbd_ddef','WER')",,,,"
      SKID_MBN.index('KOD'); SKID_MBN.prefix()
   ",,,,'maximized_with_title');
   task_attach('CTR_HBD_DDEF');
   _b.tab_splt(_c,,'horizontal','bottom',13);
   _b.grp_sel(_c,K_N_VIEW,'WER',,,,,,,,,,'maximized_with_title');
   K_S_VIEW.index('K_S_VIEW'); K_S_VIEW.prefix()
|? _d=2
|| _b.grp_sel(_c,K_WERSJE,'WER',_a,"exec('ar_k_wersje','!ctr_hbd_ddef')",,,,"
      K_WERSJE.index('SYM'); K_WERSJE.prefix()
   ",,,,'maximized_with_title');
   task_attach('CTR_HBD_DDEF');
   task_attach('CTR_HBD_DDPW');
   _b.tab_splt(_c,,'horizontal','bottom',13);
   _b.grp_sel(_c,K_PODWER,'WER',,,,,,,,,,'maximized_with_title')
?}


\arsmbnw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Po odswiezeniu okna WER tabeli SKID_MBN okna grupowego tabeli SKID_MBN
::   WE: _a - okno wertowania do odswiezenia
::  OLD: \arsmbnw/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_N_VIEW.index('SKID_MBN');
{? SKID_MBN.get()
|| K_N_VIEW.prefix(SKID_MBN.ref(),null,);
   K_N_VIEW.actions(_a,,,1)
|| K_N_VIEW.prefix(null,null,);
   K_N_VIEW.actions(_a,':D',,1)
?};
K_HARM.K_WERSJE:=null;
grp_disp(K_N_VIEW, _a);
~~


\blknvmbn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Wartosc poczatkowa pola K_N_VIEW.SKID_MBN
::  OLD: \blknvmbn/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBN.ref()


\blknvobl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Wartosc poczatkowa pola K_N_VIEW.K_W_OBL
::  OLD: \blknvobl/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_HARM.K_WERSJE().K_W_OBL


\be_knview_obl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Przed redakcja K_N_VIEW.K_W_OBL
::  OLD: \be_knview_obl/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt='popraw'
|| _ok:=1;
   K_P_VIEW.index('K_P_VIEW'); K_P_VIEW.prefix(K_N_VIEW.ref());
   K_E_VIEW.index('K_E_VIEW');
   {? K_P_VIEW.first()
   || {!
      |? K_E_VIEW.prefix(K_P_VIEW.ref());
         _ok:=~K_E_VIEW.first();
         _ok & K_P_VIEW.next()
      !}
   ?};
   _ok
|| 1
?}


\blknview_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Wartosc poczatkowa K_N_VIEW.AKC
::  OLD: \blknview_akc/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('KNViewAkc')>0
|| KNViewAkc
|| 'N'
?}


\bdknview
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Akcja usun okna WER tabeli K_N_VIEW
::  OLD: \bdknview/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_HARM_P.index('K_N_VIEW');
K_HARM_P.prefix(K_N_VIEW.ref());
{? K_HARM_P.first()
|| FUN.info('Szablon widoku użyty w pozycjach harmonogramu.'@);
   return
?};
{? FUN.ask('Czy usunąć widok danych?'@)
|| K_P_VIEW.index('SKID_MBP'); K_P_VIEW.prefix(K_N_VIEW.ref());
   {? K_P_VIEW.first()
   || {!
      |? K_E_VIEW.index('K_E_VIEW'); K_E_VIEW.prefix(K_P_VIEW.ref());
         {? K_E_VIEW.first() || {! |? K_E_VIEW.del() !} ?};
         K_P_VIEW.del()
      !}
   ?};
   K_N_VIEW.del()
?}


\br_knv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord przed tabeli K_N_VIEW
::  OLD: \br_knv/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_N_VIEW.AKC='T'
|| K_N_VIEW.actions_grayed('WER','A')
|| K_N_VIEW.actions_grayed('WER','N')
?};
exec('rekprzed','color','K_N_VIEW#01')


\clknview
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord przed tabeli K_N_VIEW - c.d.
::  OLD: \clknview/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_N_VIEW.AKC='T' || 'K_N_VIEW#01#01'
|| ''
?}


\ar_knv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord po tabeli K_N_VIEW
::  OLD: \ar_knv/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_r:={? cur_win(1)<>'WER' & ~exec('spr_widok_be','konsolidacja')
    || {? K_N_VIEW.PODGLAD
       || __CHK.record(K_N_VIEW,,'NAZ','K_W_OBL','K_S_VIEW','PROC')
       || __CHK.record(K_N_VIEW,,'NAZ','K_W_OBL','K_S_VIEW')
       ?}
    || {? K_N_VIEW.PODGLAD
       || __CHK.record(K_N_VIEW,,'NAZ','K_W_OBL','K_S_VIEW','P10','PROC')
       || __CHK.record(K_N_VIEW,,'NAZ','K_W_OBL','K_S_VIEW','P10')
       ?}
    ?};
{? _r=''
|| _naz:=K_N_VIEW.NAZ; _ref:=K_N_VIEW.ref();
   K_N_VIEW.cntx_psh(); K_N_VIEW.index('SKID_MBN'); K_N_VIEW.prefix(SKID_MBN.ref(),null,K_N_VIEW.NAZ);
   {? K_N_VIEW.first() & K_N_VIEW.NAZ=_naz & (-menu_txt()<>'popraw' | K_N_VIEW.ref()<>_ref)
   || FUN.info('Istnieje widok o nazwie: %1.'@[_naz]);
      _r:='NAZ'
   ?};
   K_N_VIEW.cntx_pop()
?};
_r


\knv_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Akcja kopiuj okna WER tabeli K_N_VIEW
::  OLD: \knv_copy/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_N_VIEW.cntx_psh();
K_N_VIEW.index('SKID_MBN'); K_N_VIEW.prefix(SKID_MBN.ref());
K_N_VIEW.win_edit('RED_COPY');
{!
|? _ok:=0;
   {? K_N_VIEW.edit("__CHK.record(K_N_VIEW,,'NAZ')")
   || _naz:=K_N_VIEW.NAZ;
      K_N_VIEW.cntx_psh();
      _dalej:=
      {? K_N_VIEW.find_key(null,_naz) & K_N_VIEW.NAZ=_naz
      || FUN.info('Istnieje widok o nazwie: %1.'@[_naz]);
         1
      || _ok:=1; 0
      ?};
      K_N_VIEW.cntx_pop();
      _dalej
   ?}
!};
{? _ok
|| exec('knv_copy1','control',_naz,null,'S','N')
?};
K_N_VIEW.cntx_pop()


\k_n_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Podglad widoku danych modelu
::  OLD: \k_n_view/skid_con.fml
:: ~OST: INBLGET,INFCOPY,INFERASE,INFOPEN,INSYSEXEC,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? K_N_VIEW.AKC='T'
|| {? exec('cli_functions','#system')=0
   || FUN.emsg(exec('indevice_nacc_msg','#system'));
      return(0)
   || _f:=fopen('@'+tmp_dir()+'\\config.txt','w');
      {? _f
      || _szablon:='ps.xlsx';
         _plik:='podglad.xlsx';
         K_N_VIEW.K_S_VIEW().bl_get('SZABLON','@'+tmp_dir()+'\\'+_szablon,0);
         fwrite(_f,'[Param]');
         fwrite(_f,'odbc='+odbc_dsn(1));
::      fwrite(_f,'auto');
         fwrite(_f,'[Plan]');
         fwrite(_f,'widok='+_szablon+'@'+_plik+'@'+$K_N_VIEW.ref()+'@'+STR.maz2w95(K_N_VIEW.NAZ)+'@'+$0+'@'+$0);
         fclose(_f);
         {? exec('run_exe','control')=0
         || fcopy('@'+tmp_dir()+'\\'+_plik,_plik,0,1,1);
            sys_exec(_plik,,1);
            ferase('@'+tmp_dir()+'\\'+_plik);
            ferase('@'+tmp_dir()+'\\'+_szablon)
         ?}
      ?}
   ?}
|| FUN.info('Widok nie jest zaakceptowany.'@)
?}


\ae_podglad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Po redakcji pola K_N_VIEW.PODGLAD. Formula czysci zawartosc pola K_N_VIEW.PROC
::  OLD: \ae_podglad/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_N_VIEW.PODGLAD=0 || K_N_VIEW.PROC:='' ?};
1


\be_proc_knv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Przed redakcja pola K_N_VIEW.PROC. Formula umozliwia redakcje pola jedynie wtedy gdy zaznaczone jest
::       pole K_N_VIEW.PODGLAD
::  OLD: \be_proc_knv/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_N_VIEW.PODGLAD


\baksview
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Dolaczanie szablonu widoku danych
::  OLD: \baksview/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_S_VIEW.blank();
K_S_VIEW.win_edit('RED');
{? K_S_VIEW.edit("exec('arksview','!ctr_hbd_ddef')") & K_S_VIEW.add()
|| K_S_VIEW.bl_put('SZABLON','szablon.xlsx',1)
?}


\arksview
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord po tabeli K_S_VIEW
::  OLD: \arksview/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_S_VIEW.NAZ=''
|| FUN.info('Nie podano nazwy szablonu.'@); 0
|| K_S_VIEW.cntx_psh(); _ref:=K_S_VIEW.ref(); _ok:=1;
   K_S_VIEW.index('K_S_VIEW'); K_S_VIEW.prefix();
   {? K_S_VIEW.find_key(K_S_VIEW.NAZ,K_S_VIEW.NAZ) & (-menu_txt<>'popraw' | _ref<>K_S_VIEW.ref())
   || FUN.info('Istnieje szablon o nazwie: %1.'@[K_S_VIEW.NAZ]);
      _ok:=0
   ?};
   K_S_VIEW.cntx_pop();
   _ok
?}


\ksvedit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Edycja szablonu widoku danych
::  OLD: \ksvedit/skid_con.fml
:: ~OST: INBLEDIT
::----------------------------------------------------------------------------------------------------------------------
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
K_S_VIEW.bl_edit('SZABLON')


\k_s_view_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Tworzenie kopi szablonu widoku danych
::  OLD: \k_s_view_copy/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_S_VIEW.cntx_psh();
K_S_VIEW.index('K_S_VIEW'); K_S_VIEW.prefix();
_naz:=K_S_VIEW.NAZ;
_kod:=#(_naz+2);
{? _kod<>0
|| _naz:=_naz-2
|? +_naz>28
|| _naz:=_naz-{? +_naz=30 || 2 || 1 ?}
?};
{!
|? _kod+=1;
   _naz_new:=_naz+(('0'+$_kod)+2);
   K_S_VIEW.find_key(_naz_new,_naz_new)
!};
K_S_VIEW.cntx_pop();
K_S_VIEW.win_edit('RED');
K_S_VIEW.NAZ:=_naz_new;
{? K_S_VIEW.edit("exec('arksview','!ctr_hbd_ddef')")
|| K_S_VIEW.add()
?}


\brkeview
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord przed okien tabeli K_E_VIEW
::  OLD: \brkeview/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
TT_KEV.cntx_psh(); TT_KEV.index(TT_KEVI2); TT_KEV.prefix();
_kol:=0;
_jest:=
{? K_E_VIEW.UD_SKL
|| TT_KEV.find_key(K_E_VIEW.UD_SKL)
|| TT_KEV.find_key(K_E_VIEW.SLO)
?};
{? _jest
|| TT_KEV.index(TT_KEVI1); TT_KEV.prefix(TT_KEV.ref());
   _kol:=TT_KEV.first()
?};
TT_KEV.cntx_pop();
{? K_E_VIEW.SUMA
|| UD_POM.CSYM:=K_E_VIEW.SYMBOL;
   UD_POM.COPIS:=K_E_VIEW.OPIS2;
   _kol:=1
|? K_E_VIEW.UD_SKL
|| UD_POM.CSYM:=K_E_VIEW.UD_SKL().SYMBOL;
   UD_POM.COPIS:=UD_SKL.OPIS
|| UD_POM.CSYM:=K_E_VIEW.SLO().KOD;
   UD_POM.COPIS:=SLO.TR
?};
_gray:='';
{? K_E_VIEW.SUMA
|| _gray+='T'
|| _gray+='P'
?};
K_E_VIEW.actions_grayed('WER_C',_gray);
K_E_VIEW.actions_grayed('WER_P',_gray);
{? _kol
|| 'K_E_VIEW#01#01'
|? K_E_VIEW.READONLY='T'
|| 'K_E_VIEW#01#02'
|| ''
?}


\add_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja Dolacz/popraw sume okien K_E_VIEW
::   WE: [_a] - [1]-dolacz 0-popraw
::  OLD: \add_sum/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=1 ?};
{? _a=0 & K_E_VIEW.SUMA=0
|| FUN.info('Akcja dostępna tylko dla elementu sumacyjnego.'@);
   return()
?};
_edit:=K_E_VIEW.win_edit('?');
K_E_VIEW.win_edit('SUMA');
{? _a
|| _lp:=K_E_VIEW.LP;
   K_E_VIEW.blank(1);
   K_E_VIEW.K_P_VIEW:=K_P_VIEW.ref();
   K_E_VIEW.SYMBOL:='Suma';
   K_E_VIEW.OPIS2:='Suma';
   K_E_VIEW.SUMA:=1;
   K_E_VIEW.LP:=_lp+1;
   K_E_VIEW.SUMATYP:='N';
   K_E_VIEW.READONLY:='T'
?};
{? K_E_VIEW.edit("
      __CHK.record(K_E_VIEW,,'SYMBOL','OPIS2')
   ")
|| {? _a
   || K_E_VIEW.cntx_psh();
      K_E_VIEW.index('K_E_VIEW');
      K_E_VIEW.prefix(K_P_VIEW.ref());
      {? K_E_VIEW.last()
      || {!
         |? {? K_E_VIEW.LP<>_lp
            || K_E_VIEW.LP+=1;
               K_E_VIEW.put();
               K_E_VIEW.prev()
            ?}
         !}
      ?};
      K_E_VIEW.cntx_pop()
   ?};
   {? _a || K_E_VIEW.add() || K_E_VIEW.put() ?}
?};
K_E_VIEW.win_edit(_edit)


\kev_readonly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zmiana trybu elementu - tylko do odczytu
::  OLD: \kev_readonly/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_E_VIEW.SUMA=0
|| K_E_VIEW.READONLY:={? K_E_VIEW.READONLY='T' || 'N' || 'T' ?};
   K_E_VIEW.put()
?}


\bv_k_e_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja wyswietl przed okien tabeli K_E_VIEW
::  OLD: \bv_k_e_view/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_E_VIEW.SUMATYP<>''
|| K_E_VIEW.cntx_psh();
   K_E_VIEW.win_edit('SUMA');
   K_E_VIEW.display();
   K_E_VIEW.cntx_pop()
?}


\be_kw_sym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Przed redakcja K_WERSJE.SYM
::  OLD: \be_kw_sym/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_WERSJE.CZY_SYS='N'


\bl_kwersje_obl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Wartosc poczatkowa pola K_WERSJE.K_W_OBL
::  OLD: \bl_kwersje_obl/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab()=K_HARM & K_HARM.win_edit('?')='COPY'
|| K_HARM.cntx_psh(); K_WERSJE.cntx_psh();
   K_HARM.get();
   _obl:=K_HARM.K_WERSJE().K_W_OBL;
   K_HARM.cntx_pop(); K_WERSJE.cntx_pop();
   _obl
|| null
?}


\be_k_wersje_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Przed redakcja pola K_WERSJE.CZY_KALK
::  OLD: \be_k_wersje_czy/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_WERSJE.cntx_psh();
_ok:=
{? -menu_txt()='popraw' & K_WERSJE.count() | K_WERSJE.CZY_SYS<>'N'
|| 0
|| 1
?};
K_WERSJE.cntx_pop();
_ok


\ae_k_wersje_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Po redakcji pola K_WERSJE.CZY_KALK
::  OLD: \ae_k_wersje_czy/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_WERSJE.CZY_KALK='N'
|| K_WERSJE.memo_set('')
?};
1


\be_k_wersje_mdx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Przed redakcja pola K_WERSJE.MDX
::  OLD: \be_k_wersje_mdx/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K_WERSJE.CZY_KALK='T'


\bd_kwersje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Przed usunieciem rekordu tabeli K_WERSJE
::  OLD: \bd_kwersje/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_WERSJE.CZY_SYS<>'N'
|| FUN.info('Wersja systemowa.\nUsunięcie niemożliwe.'@); 0
|? K_WERSJE.count()>0
|| FUN.info('Wersja użyta w danych.\nUsunięcie niemożliwe.'@); 0
|| 1
?}


\bg_k_wersje_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed grupowa akcja eksportu wersji controllingowych
::  OLD: \bg_k_wersje_exp/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy wykonać eksport zaznaczonych wersji?'@)
|| exec('k_wersje_exptab','!ctr_hbd_ddef')
?}


\k_wersje_exptab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed grupowa akcja eksportu wersji controllingowych
::  OLD: \k_wersje_exptab/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__Tkwer');
__Tkwer:=tab_tmp(1,'REF','INTEGER',)


\ag_k_wersje_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Po grupowej akcja eksportu wersji controllingowych
::  OLD: \ag_k_wersje_exp/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
exec('k_wersje_exp1','!ctr_hbd_ddef')


\k_wersje_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Eksport wersji controllingowych
::  OLD: \k_wersje_exp/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_WERSJE.sel_size()
|| __Tkwer.REF:=#K_WERSJE.ref(); __Tkwer.add()
|? K_WERSJE.CZY_SYS<>'N'
|| FUN.info('Wersja systemowa.\nEksport nie będzie wykonywany.'@)
|? FUN.ask('Czy wykonać eksport bieżącej wersji?'@)
|| exec('k_wersje_exptab','!ctr_hbd_ddef');
   __Tkwer.REF:=#K_WERSJE.ref(); __Tkwer.add();
   exec('k_wersje_exp1','!ctr_hbd_ddef')
?}


\k_wersje_exp1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Eksport wersji controllingowych - pomocnicza
::  OLD: \k_wersje_exp1/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_filtr:='Wersje'@+' (*.txt)|*.txt';
_tmpdir:=fmk_tmp_dir(0);
{? type_of(_tmpdir) <> type_of(~~)
|| _fdir:=_tmpdir.get_path
|| FUN.emsg('Nie udało się utworzyć katalogu tymczasowego po stronie serwera.'@);
   return()
?};
_file:=_fdir+exec('sep','#file')+'wersje.txt';
{? _file<>''
|| _f:=fopen(_file,'w');
   {? _f
   || fwrite(_f,'Macrologic Controlling WERSJE export file');
      K_WERSJE.cntx_psh();
      K_WERSJE.index('CZY_SYS'); K_WERSJE.prefix('N');
      {? __Tkwer.first()
      || {!
         |? {? K_WERSJE.seek(__Tkwer.REF,)
            || _fml:=K_WERSJE.memo_txt(0,1,'MDX');
               _fml:=STR.gsub(_fml,'\n','┐');
               fwrite(_f,K_WERSJE.SYM+'@'+K_WERSJE.OPIS+'@'+K_WERSJE.CZY_KALK+'@'+
                      K_WERSJE.K_W_OBL().NAZ+'@'+_fml)
            ?};
            __Tkwer.next()
         !}
      ?};
      K_WERSJE.cntx_pop();
      fclose(_f)
   ?}
?};
dlg_save(_file,0)


\k_wersje_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Import wersji controllingowych
::  OLD: \k_wersje_imp/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_filtr:='Wersje'@+' (*.txt)|*.txt';
_file:=fmk_tmp_dir(0).get_path()+'wersje.txt';
dlg_upload(_file,0,'.txt');
{? _file<>''
|| _f:=fopen(_file,'r');
   {? _f
   || _ok:=0; _v1:=_v2:=_v3:=0;
      _l:=fread(_f);
      {? _l='Macrologic Controlling WERSJE export file'
      || K_WERSJE.cntx_psh();
         K_WERSJE.index('SYM'); K_WERSJE.prefix();
         K_W_OBL.index('NAZ'); K_W_OBL.prefix();
         {!
         |? _l:=fread(_f);
            {? _l<>'\n'
            || _v1+=1;
               _t:=spli_str(_l,'@');
               {? ~K_WERSJE.find_key(_t[1],)
               || K_WERSJE.blank(1);
                  K_WERSJE.SYM:=_t[1];
                  K_WERSJE.OPIS:=_t[2];
                  K_WERSJE.CZY_KALK:=_t[3];
                  K_WERSJE.CZY_SYS:='N';
                  {? ~K_W_OBL.find_key(_t[4],)
                  || K_W_OBL.blank(1);
                     K_W_OBL.NAZ:=_t[4];
                     K_W_OBL.add()
                  ?};
                  K_WERSJE.K_W_OBL:=K_W_OBL.ref();
                  _v3+=_ok:=K_WERSJE.add();
                  {? _ok
                  || _fml:=STR.gsub(_t[5],'┐','\n');
                     K_WERSJE.memo_set(_fml);
                     K_WERSJE.memo_put()
                  ?}
               || _v2+=1
               ?};
               obj_del(_t);
               1
            ?}
         !};
         K_WERSJE.cntx_pop()
      || FUN.info('Nieprawidłowy plik z danymi do importu.'@)
      ?};
      fclose(_f);
      {? _v1
      || FUN.info(
            'Liczba wersji w pliku: %1\n'
            'Liczba dodanych wersji: %2\n'
            'Liczba istniejacych wersji: %3'@[$_v1,$_v3,$_v2])
      ?}
   || FUN.error('Błąd podczas otwarcia pliku do importu.'@)
   ?}
?}


\ar_kwersje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Rekord po tabeli K_WERSJE
::  OLD: \ar_kwersje/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
exec('chk_k_wersje','control',-menu_txt()='popraw',K_WERSJE.memo_txt(,0,'MDX'))


\ar_k_wersje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po odswiezeniu okna grupowego WER_GR tabeli K_WERSJE
::  OLD: \ar_k_wersje/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_hid:='';
K_PODWER.index('UNIK');
{? K_WERSJE.get()
|| K_PODWER.prefix(K_WERSJE.ref());
   {? K_WERSJE.CZY_SYS<>'N'
   || _hid:='DPU:D'
   ?}
|| K_PODWER.prefix(null);
   _hid:='DPU:D'
?};
K_PODWER.actions('WER',_hid,,1);
grp_disp(K_PODWER,'WER')


\t_skid_mbn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add i put tabeli SKID_MBN
::   WE: _a - 1-add 2-put
::  OLD: \t_skid_mbn/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| K_INSMBN.prefix();
   K_INSMBN.FIRMA:=REF.FIRMA;
   K_INSMBN.SKID_MBN:=SKID_MBN.ref();
   K_INSMBN.CZY_INST:='N';
   K_INSMBN.add();
   ~~
|? _a=2
|| K_INSMBN.put();
   {? SKID_MBN.CZY_SPR='T' & (SKID_MBN.KOD<>bfld('KOD') | SKID_MBN.NAZ<>bfld('NAZ'))
   || SKID_MBP.cntx_psh();
      SKID_MBP.index('LP');
      SKID_MBP.prefix(SKID_MBN.ref());
      {? SKID_MBP.first()
      || UD_SCH.prefix();
         {!
         |? {? UD_SCH.seek(SKID_MBP.UD_SCH)
            || UD_SCH.SYMBOL:=SKID_MBN.KOD;
               UD_SCH.OPIS:=SKID_MBN.NAZ;
               UD_SCH.put()
            ?};
            SKID_MBP.LP<3 & SKID_MBP.next()
         !}
      ?};
      SKID_MBP.cntx_pop()
   ?};
   ~~
?}


\br_kwersje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed K_WERSJE
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
{? K_WERSJE.CZY_SYS='T' & K_WERSJE.sel_size()=0
|| _gray+='uE'
?};
K_WERSJE.actions_grayed('WER',_gray);
''


\bd_kwobl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed usunięciem wariantu obliczeń
::----------------------------------------------------------------------------------------------------------------------
{? K_W_OBL.count()>0
|| FUN.emsg('Wariant obliczeń jest użyty w danych.\nUsunięcie niemożliwe.'@);
   0
|| 1
?}


\tb_skid_mbn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Triger przed add i put dla tabeli SKID_MBN
::----------------------------------------------------------------------------------------------------------------------
SKID_MBN.KONTR_OP:={? SKID_MBN.KONTRPOZ='F'
                   || 'dla firmy'
                   |? SKID_MBN.KONTRPOZ='G'
                   || 'dla grupy'
                   || 'brak'
                   ?};
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:48 a2cd4d28a0f4687878e1adf08be4355ff5151f94b9297727417098c5527a73b1ca9f786335861f579e989e91e978e728bafd6df2a7133733a58396e9f6b367ace3692c971db4ab986bc9ebb8e62c4bd7b4284d02d1d0fc05ea86ec204e9a9b906e6cfafa74a09c4677a8eeb0e3cd9c9d44609272059335e72af0675de57fbfac

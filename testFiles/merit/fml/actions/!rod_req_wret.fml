:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !rod_req_wret.fml
:: Utworzony: 23.04.2018
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ROD_REQ_WRET - Wysyłanie żądań cyklicznej retencji z Rodo Utility
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Wysyłanie żądań cyklicznej retencji z Rodo Utility - formuła główna czynności.
::  OLD: \alertret/ro_ws.fml
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
:: PARAMETRY WE:
::# kind=WE, symbol=PLUS_DNI, type=NUMBER, name=Liczba dodanych dni do bieżacej daty, required=N
:: PARAMETRY WY:
::# kind=WY, symbol=OK, type=STRING, name=Czy pobieranie i przetwarzanie zakończone sukcesem [T/N], required=T
::# kind=WY, symbol=SUB, type=STRING, name=Temat wiadomości o błędzie, required=T
::# kind=WY, symbol=BODYT, type=MEMO, name=Treść wiadomości o błędzie, required=T

_args:=params_get();
_in:=_args.in;
_out:=_args.out;
_mp:=_args.mp;

_ok:=1;
_info:='';
{? exec('is_aip','rodo')
|| _dni:={? var_press('PLUS_DNI',_in)>0 || _in.PLUS_DNI ?};
   exec('auto_ret','ro_req',_dni);
   _info:='OK.';
   RO_REQN.index('SUPPORT'); RO_REQN.prefix('T','n');
   {? RO_REQN.first()
   || VAR_DEL.delete('WsLog');
      WsLog:=exec('log','ro_ws');
      {!
      |? _id:=exec('ws_sendr','!rod_req_wret');
         {? _id=''
         || _ok:=0;
            _info:='';
            {? WsLog.err<>''
            || _info:='Powstał błąd podczas komunikacji z RODO Utility: \n'+WsLog.err
            ?};
            RO_REQN.next()
         || RO_REQN.cntx_psh();
            RO_REQN.prefix();
            RO_REQN.STATUS:='s';
            RO_REQN.AIP_ID:=_id;
            RO_REQN.put();
            RO_REQN.cntx_pop();
            RO_REQN.first()
         ?}
      !};
      WsLog.close();
      VAR_DEL.delete('WsLog')
   ?}
|| _info:='Współpraca z RODO Utility jest wyłączona (parametr 500).'
?};
_out.OK:={? _ok || 'T' || 'N' ?};
_out.SUB:={? _ok
          || 'Komunikacja z RODO Utility przeprowadzona poprawnie'
          || 'Powstał błąd podczas komunikacji z RODO Utility'
          ?};
_out.BODYT:=_info;
_mp.save(,_out);
_mp.done()


\ws_sendr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Zadanie alertowe wysyłające prośbę o usunięcie danych związanych z przekroczeniem czasu retencji
::  OLD: \ws_sendr/ro_ws.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_id:='';
_fnin:='ws_rodo_postreq.xml';
_file:=fopen(_fnin,'uw',1,,1);
{? _file.is_open()
|| exec('czytaj','#stalesys',,XINFO,'AIP_SYS');
   fwrite(_file,'<?xml version="1.0" encoding="utf-8"?>');
   fwrite(_file,exec('koperta2','ro_ws',0));
   fwrite(_file,
      '<ass:PostRequests>'+
      '<ass:PostRequestParams>'+
      '<ass:SystemIdentifier>'+XINFO.AIP_SYS+'</ass:SystemIdentifier>'+
      '<ass:PostRequest>'+
      '<ass:RequestTypeName>'+RO_REQN.RO_REQT().KEY+'</ass:RequestTypeName>'+
      '<ass:RequestDate>'+STR.gsub(RO_REQN.DATE$1,'/','-')+'T'+RO_REQN.TIME$3+'.0</ass:RequestDate>'
   );
   _cel:=null;
   _odp:=null;
   RO_REQR.index('RO_REQN');
   RO_REQR.prefix(RO_REQN.ref());
   {? RO_REQR.first()
   || _odp:=RO_REQR.ref();
      _cel:=RO_REQR.RO_REQC;
      fwrite(_file,'<ass:Purposes>');
      {!
      |? fwrite(_file,
            '<ass:Purpose>'+
            '<ass:Name>'+RO_REQR.RO_REQC().CODE+'</ass:Name>'+
            '</ass:Purpose>'
         );
         RO_REQR.next()
      !};
      fwrite(_file,'</ass:Purposes>')
   ?};
   exec('write_param','ro_ws',_file,RO_REQN.ref(),_cel,'ass:Parameters','ass:Parameter');
   exec('write_att','ro_ws',_file,'ass:Attachments','ass:Attachment',_odp);
   fwrite(_file,
      '</ass:PostRequest>'+
      '</ass:PostRequestParams>'+
      '</ass:PostRequests>'
   );
   fwrite(_file,exec('koperta2','ro_ws',1));
   _file.fclose();
   &_file;

   _fin:=fopen(_fnin,'br',1,,1);
   {? _fin.is_open()
   || _fnout:='ws_rodo_getreq.xml';
      _fout:=fopen(_fnout,'bw',1,,1);
      {? _fout.is_open()
      || _res:=exec('send','ro_ws',_fin,_fout);
         _fout.fclose()
      || _res:=-1;
         WsLog.log('Błąd zapisu do pliku: '+_fnout)
      ?};
      _fin.fclose()
   || _res:=-1
   ?};
   {? _res=200
   || _file:=fopen(_fnout,'ur',1,,1);
      {? _file.is_open()
      || _id:=exec('ws_sendr3','!rod_req_wret',_file);
         _file.fclose()
      || _ret:=0;
         WsLog.log('Błąd odczytu z pliku: '+_fnout)
      ?}
   || WsLog.log('PostRequest, HTTP: '+$_res);
      _ret:=0
   ?}
|| _ret:=0;
   WsLog.log('Błąd zapisu do pliku: '+_fnin)
?};
_id


\ws_sendr3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Pobranie odpowiedzi po wysłaniu informacji o przetworzonych danych
::   WE: _a - uchwyt z plikiem z odpowiedzią
::  OLD: \ws_sendr3/ro_ws.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__obj');
__obj:=obj_new(6);
__obj[1]:=0;
__obj[4]:='';
__obj[6]:='';
x_parse(_a,,,"
   {? __obj[1]=0
   || {? _a='ArrayPostRequestsResult'
      || __obj[1]:=1
      ?}
   ?};
   {? __obj[1]
   || __obj[2]:=''
   ?};
   1
","
   {? __obj[1]=1
   || {? _a='Status'
      || __obj[3]:=#__obj[2]
      |? _a='ErrorCode'
      || __obj[4]:=__obj[2]
      |? _a='ErrorMessage'
      || __obj[5]:=__obj[2]
      |? _a='RequestRegId'
      || __obj[6]:=__obj[2]
      ?}
   ?};
   1
","
   {? __obj[1]
   || __obj[2]+=_a
   ?};
   1
"
);
_status:=__obj[3];
{? _status=0 | __obj[4]<>''
|| WsLog.log('PostRequest\r\nCODE: '+__obj[4]+', MESSAGE: '+__obj[5])
?};
_id:=__obj[6];
VAR_DEL.delete('__obj');
_id

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:00 ace6cc0aae095c475550f24da32defd4bbe32b8560c78f7f21627518489b49ba8a749f841f72df14664ff7ff83652cd63c36266ad42a8d470539a11e905ac54109593d3ed5397b70bb22bb6d0e1c549d24950003342e981c25342513ca62183dc0538025fd1f918bbd66af7c6d9da52a9ea6c961ca8c256e7a1262402bd6788f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zds_wpdf.fml
:: Utworzony: 21.09.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Utworzenie zamówienia dostaw w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LZK
::# kind=WE, symbol=ZD_NAG,  type=_ZD_NAG, name=Zamówienie dostaw, required=T, keyref=T
::# kind=WE, symbol=CZY_PDF, type=STRING,  name=Utwórz PDF,        required=N, keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE, symbol=PRN_MSK, type=STRING,  name=Maska wydruku,     required=N, keyref=N, fml_val="exec('rep_exec','#b_report','LZK_ZDS_WPDF','lzk_zdzam*','Wydruki zamówienia dostaw',-1)"
::# kind=WY, symbol=DOKUM,   type=_DOKUM,  name=Załącznik,         required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_zds','lzk');

:: WSTĘPNE WALIDACJE
_clean_result:=params_exec('clean','!lzk_zds_wpdf',_mp,_in);
_can_continue:=_clean_result.RESULT;
{? _can_continue=0
|| return()
?};

_akcja:=_mp.akcja();
_group:=_mp.isGroup();
_auto:=_mp.isAutoRun();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' | _in_pdf='E-DOKUMENT' ?};
_czy_edokument:={? _akcja<>'' || (_akcja*'E-dokument')>0 || _in_pdf='E-DOKUMENT' ?};
_rodzaje_edokumentu:='';
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || {? exec('ctrlMask','#b_report',_in.PRN_MSK,'lzk_zdzam') || _in.PRN_MSK || '' ?}
      || ''
      ?};

{? var_pres('ZD_NAG',_in)=type_of(null()) & _in.ZD_NAG
||
:: Ustawienie kontekstu wg instancji elementu w procesie
   exec('openzd_psh','open_tab');
   _use:=ref_name(_in.ZD_NAG)<>ZD_NAG.name();
   {? _use || ZD_NAG.use(ref_name(_in.ZD_NAG)) ?};
   ZD_NAG.prefix();
   {? ZD_NAG.seek(_in.ZD_NAG)
   ||
::    Nie ustawiamy wcześniej parses ponieważ i tak domyślnie otwierane są maski '__'
::    a zamówienie może być w archiwum
      {? _use
      || exec('openzd','open_tab',ZD_NAG.name()+3)
      ?};
      _continue:=1;
      _rodzaje_edokumentu:=exec('rodzaje_zam_edokumentu','zbl_dok',ZD_NAG.KH,ZD_NAG.T,ZD_NAG.DATA);
      _zdnag_to_string:=exec('record','#to_string',ZD_NAG.ref());
      {? (_czy_pdf | ~_group) & ~_czy_edokument & _rodzaje_edokumentu<>''
      || {? _group | _auto
         || _continue:=0;
            exec('add_kom','#message','Należy użyć opcji E-dokument.'@,7,_zdnag_to_string)
         || _continue:= FUN.ask('W celu utworzenia e-dokumentu należy wskazać opcję E-dokument.'
               '\nCzy kontynuować drukowanie bez tworzenia e-dokumentu?'@)
         ?};
         {? ~_continue
         || _mp.done()
         ?}
      ?};
      {? _continue & _czy_edokument & _rodzaje_edokumentu=''
      || exec('add_kom','#message','Należy użyć opcji Drukuj lub Utwórz PDF.'@,7,_zdnag_to_string);
         _mp.done();
         _continue:=0
      ?};
      {? _continue>0 & _czy_edokument & exec('chk_role','#b__box',OPERATOR.USER,'ZBL_DOK_RBUF')=0
      || exec('add_kom','#message','Brak uprawnień do czynności: ZBL_DOK_RBUF Obsługa dokumentów Businesslink.'@,
              7,_zdnag_to_string);
         _mp.done();
         _continue:=0
      ?};
      {? _continue>0 & _czy_edokument & ZD_NAG.name()+2<>'__'
      || exec('add_kom','#message','Opcja E-dokument nie jest dostępna dla archiwalnego zamówienia.'@,
              7,_zdnag_to_string);
         _mp.done();
         _continue:=0
      ?};
      {? _continue>0 & _czy_edokument & (ZD_NAG.STAT_REJ<>'T' | ZD_NAG.STAN='M' | ZD_NAG.STAN='U')
      || exec('add_kom','#message','Opcja E-dokument jest dostępna tylko dla zaakceptowanego zamówienia.'@,
              7,_zdnag_to_string);
         _mp.done();
         _continue:=0
      ?};
      {? _continue & _czy_edokument & ZD_NAG.BL_STAT<>'' & ZD_NAG.BL_STAT<>exec('not_created','zbl_stat')
      || exec('add_kom','#message',
             'Opcja E-dokument jest dostępna tylko dla zamówienia o statusie Businesslink "%1". Obecny status to "%2"'@
             [exec('not_created','zbl_stat'),ZD_NAG.BL_STAT],
              7,_zdnag_to_string);
            _mp.done();
            _continue:=0
      ?};
      {? _continue & _czy_edokument
      || DOKUM.cntx_psh();
         _dokum_seek:=exec('bl_dokum_seek','zbl',ZD_NAG,1);
         DOKUM.cntx_pop();
         {? _dokum_seek
         || exec('add_kom','#message','Już istnieje załącznik e-dokument dla zamówienia.'@,
              7,_zdnag_to_string);
            _mp.done();
            _continue:=0
         ?}
      ?};
      {? _continue>0 & (_msk='' | _msk*'?' | _msk*'*' ) & _czy_edokument
      || _msk_dp:=exec('msk_zd','dp_wydrukow',ZD_NAG.ref(),exec('typ_edokument','dp_wydrukow'));
         {? _msk_dp=''
         || exec('add_kom','#message','Parametry wydruku podane przez użytkownika, nie znaleziono domyślnych.'@,
              7,_zdnag_to_string)
         || _msk:=_msk_dp
         ?}
      ?};
      {? _continue
      || _dokum:=exec('zd_druk','!lzk_zds_wpdf',_czy_pdf,_msk,_czy_edokument,_rodzaje_edokumentu);
         {? _czy_pdf & _dokum<>null()
         || _out.DOKUM:=_dokum;
            _mp.save(,_out);
            {? ~_keep
            || _mp.done()
            ?}
         |? ~_czy_pdf & _dokum
         || {? ~_keep
            || _mp.done()
            ?}
         ?}
      || {? var_pres('__GRP_DR')>100 & var_pres('FIRST',__GRP_DR)=type_of(0)
         || __GRP_DR.FIRST:=-1
         ?}
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};
   exec('openzd_pop','open_tab')
||
   _mp.error('Brak parametru wejściowego ZD_NAG.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZD_NAG',_in)<>type_of(~~) & _in.ZD_NAG
|| 'Utwórz PDF zamówienia dostaw: %1'@@[exec('record','#to_string',_in.ZD_NAG)]
|| ''
?}


\zdnag_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zamówienie dostaw - Utwórz PDF
::   WE: [_a] - akcja
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:='Area Utwórz PDF' ?};

_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LZK_ZDS_WPDF';
_params.UIDREF:=ZD_NAG.uidref();
_params.AKCJA:=_a;
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZD_NAG',ZD_NAG.ref());

exec('mp_run','#b__box',_params)


\zd_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: wydruki dla zamowienia dostaw
::   WE: [_a] - czy zapisywac dokument 0 - nie 1 - tak
::       [_b] - maska wydruku
::       [_c] - [0]/1 - akcja E-dokument
::       [_d] - rodzaje e-dokumetnu
::   WY: dla _a=1: ref/null _a=0: 0/1
::  OLD: \zd_druk/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
_msk:={? var_pres('_b')=type_of('') || _b || '' ?};
_czy_edokument:={? var_pres('_c')=type_of(0) || _c || 0 ?};
_rodzaje_edokumentu:={? var_pres('_d')=type_of('') || _d || '' ?};
_wyn:={? _a || null() || 0 ?};

{? _msk<>''
|| _wydr:=_msk
|? _czy_edokument
|| _wydr:='lzk_zdzam_01'
|| _wydr:='lzk_zdzam*'
?};

{? _a=1
|| NRDOK.index('NRDOK');
   NRDOK.prefix('SYS','');
   {? ~NRDOK.first()
   || _ok:=0;
      {? _czy_edokument | (var_pres('__GRP_DR')>0 & var_pres('GR',__GPR_DR)>0 & __GRP_DR.GR)
      || exec('add_kom','#message','Nie zdefiniowano numeracji SYS dla dokumentów powiązanych.'@,7,'Ogólne'@)
      || FUN.info('Nie zdefiniowano numeracji SYS dla dokumentów powiązanych.'@)
      ?}
   ?}
?};

POLA.NAZ_WYDR:='';

_txt:='Zamówienie dostaw';
{? _a=1
||
   {? var_pres('__GRP_DR')>100&__GRP_DR.GR & __GRP_DR.FIRST=0 & __GRP_DR.WZ<>''
   ||
      _symb:=exec('str_to_pth','#string',ZD_NAG.SYM);
      _file:='%1 %2.pdf'[_txt,_symb];
      _wydr:=rep_exec(__GRP_DR.WZ,,,_file,1)
   ||
      _symb:=exec('str_to_pth','#string',ZD_NAG.SYM);
      _file:='%1 %2.pdf'[_txt,_symb];
      _wydr:=rep_exec(_wydr,,,_file,1)
   ?}
||
   _wydr:=exec('rep_exec','#b_report','LZK_ZDS_WPDF',_wydr,_txt,2,,,,,,'W')
?};

{? var_pres('__GRP_DR')>100 & _wydr=0
||
   __GRP_DR.ESC:=1
?};

{? POLA.NAZ_WYDR<>'' & _a=1 & _wydr=1
|| _wyn:=exec('save_dok','dokum','ZD_NAG',_file,POLA.NAZ_WYDR,ZD_NAG.SYM,ZD_NAG.KH().EM
      ,'Zamówienie o symbolu: '+ZD_NAG.SYM+' wystawca: '+XINFO.NAZ,,ZD_NAG.STAT_REJ,
      _czy_edokument,_rodzaje_edokumentu)
|? _a=0
|| _wyn:=_wydr
?};
_wyn


\zdnag_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zamówienie dostaw - Drukuj
::   WE: [_a] - akcja
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:='Area Drukuj' ?};

   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LZK_ZDS_WPDF';
   _params.UIDREF:=ZD_NAG.uidref();
   _params.AKCJA:=_a;
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZD_NAG',ZD_NAG.ref());

   exec('mp_run','#b__box',_params)


\zd_pdf_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [19.42]
:: OPIS: Zamówienie dostaw - Utwórz PDF
::----------------------------------------------------------------------------------------------------------------------
exec('zd_druk_gr','!lzk_zds_wpdf',1,'LZK_ZDS_WPDF','Area Utwórz PDF')


\zd_prn_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [19.42]
:: OPIS: Zamówienie dostaw - Drukuj
::----------------------------------------------------------------------------------------------------------------------
exec('zd_druk_gr','!lzk_zds_wpdf',0,'LZK_ZDS_WPDF','Area Drukuj')


\zd_druk_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [19.42]
:: OPIS: Wydruk zamówień dostaw
::   WE: _a - utworz pdf 0-nie 1-tak
::       _b - UID czynności
::       _c - nazwa akcji
::       [_d] - maska wydruku
::---------------------------------------------------------------------------------------------------------------------
_czy_pdf:=_a;
_act_uid:=_b;
_akcja:=_c;
_msk:={? var_pres('_d')=type_of('') || _d || '' ?};

_czy_edokument:=_akcja='Area E-dokument';

_czysc:="VAR_DEL.delete('__GRP_DR')";
_czysc();
:: __GRP_DR - srodowisko grupowego drukowania
__GRP_DR:=obj_new('GR','SEL','SIZE','FIRST','ESC','WZ','PDF','SHOW_PDF','DR_N_AKC','JEZYK','JEZYK_KH','DR_N_CAN',
          'EDOKUMENT');
:: __GRP_DR.SEL, _Sel - wybrane dokumenty
__GRP_DR.SEL:=_Sel:=ZD_NAG.sel_aget();

{? _Sel.first()
|| __GRP_DR.GR:=1
|| __GRP_DR.GR:=0;
   _Sel.REF:=#ZD_NAG.ref(); _Sel.CRC:=ZD_NAG.crc(); _Sel.add()
?};
__GRP_DR.SIZE:=_Sel.size();
:: __GRP_DR.FIRST - drukowanie pierwszego dokumentu sposrod zaznaczonych 0-nie 1-tak
__GRP_DR.FIRST:=1;
:: __GRP_DR.SHOW_PDF - wyswietlac utworzone pdf 0-nie 1-tak
__GRP_DR.SHOW_PDF:=0;
:: __GRP_DR.PDF - utworz pdf 0-nie 1-tak
__GRP_DR.PDF:=_czy_pdf;
:: __GRP_DR.WZ - wybrany wzorzec wydruku
__GRP_DR.WZ:=_msk;
:: __GRP_DR.ESC - zrezygnowano z wypelnienia parametrow wydruku 0-nie 1-tak
__GRP_DR.ESC:=0;
:: __GRP_DR.EDOKUMENT - akcja e-dokument 0-nie 1-tak
__GRP_DR.EDOKUMENT:=_czy_edokument;
:: _ok - stan dzialania funkcji 0-nie ok 1-ok
_ok:=1;

_kom_tyt:='';
{? _czy_edokument
|| _kom_tyt:='E-dokument dla zamówień dostaw'@
|? __GRP_DR.GR
|| {? _czy_pdf
   || _kom_tyt:='Wydruk PDF dla zamówień dostaw'@
   || _kom_tyt:='Wydruk zamówień dostaw'@
   ?}
?};
{? _kom_tyt <>''
|| exec('ini_kom','#message',_kom_tyt)
?};

ZD_NAG.cntx_psh();

{? __GRP_DR.GR & ~_czy_edokument & ~_czy_pdf & _Sel.first()
|| {!
   |? _edok:=0;
      {? ZD_NAG.seek(_Sel.REF,)
      || _rodzaje_edokumentu:=exec('rodzaje_zam_edokumentu','zbl_dok',ZD_NAG.KH,ZD_NAG.T,ZD_NAG.DATA);
         {? _rodzaje_edokumentu<>''
         || _zdnag_to_string:=exec('record','#to_string',ZD_NAG.ref());
            _edok:=1
         ?}
      ?};
      {? _edok
      || exec('add_kom','#message','Należy użyć opcji E-dokument.'@,7,_zdnag_to_string);
         _Sel.cntx_psh();
         _loop:=_Sel.next();
         _Sel.cntx_pop();
         _Sel.del(1,1);
         ZD_NAG.sel_del();
         _loop
      || _Sel.next()
      ?}
   !};
   {? _Sel.size()=0
   || _ok:=0
   ?}
?};

{? _ok=1
|| {? __GRP_DR.GR
   || __GRP_DR.SHOW_PDF:={? __GRP_DR.PDF || FUN.ask('Wyświetlić wygenerowane dokumenty?'@) ?}
   ?};
   {? __GRP_DR.DR_N_AKC=-1 || __GRP_DR.DR_N_AKC:=0 ?}
?};
:: Drukowanie
{? _ok
||
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      {? ZD_NAG.seek(_Sel.REF,)
      ||
         _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:=_act_uid;
         _params.UIDREF:=ZD_NAG.uidref();
         _params.AKCJA:=_akcja;
         _params.GRUPA:={? __GRP_DR.GR || 'T' || 'N' ?};
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZD_NAG',ZD_NAG.ref());
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'CZY_PDF',
             {? _czy_edokument || 'EDOKUMENT' |?_czy_pdf || 'TAK' || 'NIE' ?});

         exec('mp_run','#b__box',_params);

         obj_del(_params);
         {? __GRP_DR.FIRST=-1
         || __GRP_DR.FIRST:=1
::          pozycja pominięta, nie spełniła warunków
         || __GRP_DR.FIRST:=0
         ?}
      ?};
      _loop:=__GRP_DR.ESC=0 & {? _czy_pdf || _Sel.next() || 0 ?}
   !}
?};
ZD_NAG.sel_adel();
ZD_NAG.cntx_pop();
{? __GRP_DR.ESC=0 & _kom_tyt<>''
|| exec('end_kom','#message')
?};
_czysc();
0


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zd_clean','zamdst_nag',_mp,_in)


\zd_edokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Dokument sprzedaży - Edokument
::----------------------------------------------------------------------------------------------------------------------
exec('zd_druk_gr','!lzk_zds_wpdf',1,'LZK_ZDS_WPDF','Area E-dokument')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 5abf4a1b3fdd51ab52eade55f52549d6719ea8dec441b32f48cbace54336023446f37308733774e8f788efebccab465a01bcf73a26db16b23fc46037dd855149b7f34bc11202c032d195bb83c4d67d7691994e001260d591e688566f340276a32935f82e152c29b3e6dcae01eae00142d07aadf5bba1cc32226ca8b50a75918b

:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppk_ewi_akrw.fml
:: Utworzony: 02.07.2019
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności PPK_EWI_AKRW - Info.o wznowieniu wpłat do PPK.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.22]
:: OPIS: Uczestnicy PPK, którym należy wznowić naliczanie wpłat.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::
::# kind=WY, symbol=SUB, type=STRING, name=Temat, required=N
::# kind=WY, symbol=TO, type=MEMO, name=Odbiorca, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,BŁĄD)", required=N
::
params_set(_par:=params_get());
_mp:=_par.mp;
_service:=_mp.isService();

_args:=exec('add_email_a','#mailbox');
_args.SUB:='Informacja o konieczności wznowienia naliczania wpłat do PPK';
_args.RCV:=exec('odbiorcy','ppk_eml',_mp.buf_act.UID);
_args.BODYH:=exec('tresc','!ppk_ewi_akrw',exec('informacja_wznowienie','ppk_eml',_service));
exec('main','ppk_eml',_args)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.22]
:: OPIS: Uczestnicy PPK, którym należy wznowić naliczanie wpłat - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wysłanie popwiadomienia o uczestnikach PPK, którym należy ponownie rozpocząć odprowadzanie wpłat'@@


\tresc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.22]
:: OPIS: Formuła formatująca teść wysyłanej informacji w HTML.
::   WE: _a [TABLE] - tabela z listą osób zatrudnionych, w imieniu których należy zawrzeć umowę o prowadzenie PPK.
::   WY:
::  OLD: \bd_iwzno/ppk_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_PPK:=_a;
_tresc:='';

{? _PPK.first()
|| _th:="'<th [[STYLE_TABLE_TH]]>'+_a+'</th>'";
   _td:="'<td [[STYLE_TABLE_TD]]>'+_a+'</td>'";
   _tresc:=
      '<center><h4>Informacja o zbliżającym się terminie wznowienia naliczania wpłat do PPK</h4></center><br>'+
      '<h5>Wymagane jest przekazanie informacji o zbliżającym się terminie automatycznego wznowienia naliczania wpłat '+
      'do PPK następującym osobom:</h5><br><br>'+
      '<table [[STYLE_TABLE]]>\n'+
         '<tr [[STYLE_TABLE_TR]]>'+
            _th('Nazwisko'@)+
            _th('Imię'@)+
            _th('PESEL'@)+
            _th('Paszport'@)+
            _th('Data komunikatu'@)+
            _th('Rodzaj odbiorcy'@)+
         '</tr>';
   {!
   |? _tresc+=
         '<tr [[STYLE_TABLE_TR]]>'+
            _td(_PPK.NAZWISKO)+
            _td(_PPK.PIERWSZE)+
            _td(_PPK.PESEL)+
            _td(_PPK.PASZPORT)+
            _td($_PPK.DATA_UTW)+
            _td(_PPK.RODZAJ)+
         '</tr>';
      _PPK.next()
   !};
   _tresc+='</table>';
   _tresc+='<p>Wysłane komunikaty można zobaczyć w <i>Dane zbiorcze PPK->Komunikaty dla osób/uczestników</i><br>'+
           'oraz w wysłanych powiadomieniach <i>Administracja->Poczta.</i></p>';
   _tresc+='<p>Ta wiadomość została wygenerowana automatycznie - prosimy na nią nie odpowiadać.</p>'
?};

_tresc

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 88432324345f1ef547416670746762e09068cc86b182d96b350cdaf4f2cbf99cb428089f0936c0565b3edefb36ca7875cff7e952d57b31e35df45bee5638e43f855aff6f6010e6ff5778b592fbb8f07b8133080778023b8579404dd0ecf109aad2e77ac1366bd63ec276ef4ed27d5a091865e99adc7b043dab98a9f72aa0ec11

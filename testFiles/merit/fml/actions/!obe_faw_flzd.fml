:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_faw_flzd.fml
:: Utworzony: 30.11.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FAW_FLZD - Tworzenie wniosku w obiegu na podstawie zamówienia dostaw
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Tworzenie wniosku w obiegu na podstawie zamówienia dostaw - główna formuła czynności OBE_FAW_FLZD
::----------------------------------------------------------------------------------------------------------------------
:: PARAMETRY WE:
::# kind=WE, symbol=ZD_NAG, type=_ZD_NAG, name=Wskazanie na nagłówek zamówienia dostaw, required=N, keyref=T
::# kind=WE, symbol=ETYPY, type=STRING, name=Wskazanie na typ obiegu, required=N, fml_val="exec('typ_obi','obiegi_log','M',_a)", fml_exp="exec('etypy_fml_exp','obiegi2',2,_a)"
::# kind=WE, symbol=ODD, type=STRING, name=Domyślna jednostka księgowa, required=N, fml_val="exec('select_odd','obiegi_log',_a)", fml_exp="exec('odd_fml_exp','obiegi_log',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na fakturę w obiegu, required=N
::# kind=WY, symbol=ZD_NAG, type=_ZD_NAG, name=Wskazanie na nagłówek zamówienia dostaw, required=N, keyref=T

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_service:=_mp.isService();
_auto:=_mp.isAutoRun();
{? ~exec('is_act','#b__box','OBE_FAW_FLZD')
|| {? ~_service
   || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
   ?};
   _mp.cancel(); return(~~)
|? _mp.isMicro()
|| {? ~_service
   || {? _akcja='Dołącz' | _mp.pathProc()
      || FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu wniosków dla bieżącej czynności.'@)
      || FUN.info('Wniosek nie jest w trakcie rejestracji.'@)
      ?}
   ?};
   return(~~)
?};

_dalej:=1;
{? _service
|| {? _we.ZD_NAG=~~ | _we.ZD_NAG=null
   || _dalej:=0;
      _mp.error('Nie znaleziono zamówienia dostawy.')
   |? _we.ETYPY=~~ | _we.ETYPY=''
   || _dalej:=0;
      _mp.error('Nie wskazano typu dokumentu w obiegu.')
   ?}
?};
_zd_nag:=_etypy:=null;
{? _dalej=1
|| {? ~_service
   || {? _akcja='OBSZAR'
      || params_get().context.ZD_NAG; _zd_nag:=ZD_NAG.ref()
      |? _we.ZD_NAG=~~ | _we.ZD_NAG=null
      || _zd_nag:=exec('zd_nag_wybor','obiegi_log');
         {? _zd_nag=null || _dalej:=-1 ?}
      || _zd_nag:=_we.ZD_NAG
      ?}
   || _zd_nag:=_we.ZD_NAG
   ?};
   {? _zd_nag
   || _wy.ZD_NAG:=_zd_nag; _mp.save(,_wy); _mp.descTodo()
   ?}
?};
{? _dalej=1
|| {? ~_service
   || ETYPY.cntx_psh(); ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',2));
      {? _we.ETYPY=~~ | _we.ETYPY='' | ~ETYPY.find_key(_we.ETYPY)
      || ETYPY.hdr_sel('Typy wniosków w obiegu'@); ETYPY.win_edit('REDW'); ETYPY.win_sel('SLO_WYB');
         {? ETYPY.select() || _etypy:=ETYPY.ref() ?}
      |? ETYPY.find_key(_we.ETYPY)
      || _etypy:=ETYPY.ref()
      ?};
      {? ~_etypy || _dalej:=-1 ?};
      ETYPY.cntx_pop()
   || ETYPY.cntx_psh(); ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',2));
      {? ETYPY.find_key(_we.ETYPY)
      || _etypy:=ETYPY.ref()
      || _dalej:=0;
         _mp.error('Nie znaleziono typu dokumentu w obiegu.')
      ?};
      ETYPY.cntx_pop()
   ?}
?};
{? _dalej=1
|| _ref:=BB.sqlint($_zd_nag); _nam:=form(($_zd_nag)-8);
   {? _ref<>0 & _nam<>''
   || ZD_NAG.cntx_psh(); ZD_NAG.use(_nam); ZD_NAG.prefix();
      ZD_POZ.cntx_psh(); ZD_POZ.use('zdpoz'+(ZD_NAG.name()+3));
      {? ZD_NAG.seek(_ref,_nam)
      || {? ZD_NAG.OBI_POW<>''
         || {? ~_service
            || FUN.info('Dla zamówienia dostaw został wcześniej uruchomiony obieg.'@)
            || _wy.EDOKUM:=_wy.ZD_NAG:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         |? ZD_NAG.STAT_REJ='T' | ZD_NAG.STAT_REJ='Z'
         || {? _akcja='OBSZAR' | _mp.pathProc() | _mp.pathTodo() | _mp.isAutoRun()
            || menu_txt(,'Dołącz');
               exec('zd_nag_win_edit_set','obiegi_log',1);
               {? _service
               || BtnObi:=1
               || BtnObi:=0;
                  ZD_NAG.display()
               ?};
               {? BtnObi
               || _edokum:=params_exec('zd_nag_edokum','obiegi_log',_etypy,~_service);
                  {? _edokum
                  || _wy.EDOKUM:=_edokum;
                     _wy.ZD_NAG:=ZD_NAG.ref();
                     _mp.save(,_wy);
                     _mp.done()
                  ?}
               ?}
            ?}
         || {? ZD_NAG.STAT_REJ='A'
            || {? ~_service || FUN.info('Zamówienie dostaw zostało anulowane.'@) ?};
               _wy.EDOKUM:=_wy.ZD_NAG:=null;
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      || _wy.EDOKUM:=_wy.ZD_NAG:=null;
         _mp.save(,_wy);
         _mp.done()
      ?};
      ZD_NAG.cntx_pop(); ZD_POZ.cntx_pop()
   || _wy.EDOKUM:=_wy.ZD_NAG:=null;
      _mp.save(,_wy);
      _mp.done()
   ?}
|? _dalej=-1
|| _mp.cancel()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('ZD_NAG',_in)=type_of(null()) & _in.ZD_NAG
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia dostaw: %1'@@[exec('record','#to_string',_in.ZD_NAG)]
|? var_pres('ZD_NAG',_out)=type_of(null()) & _out.ZD_NAG
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia dostaw: %1'@@[exec('record','#to_string',_out.ZD_NAG)]
|| 'Zakończ rejestrację wniosku w obiegu na podstawie zamówienia dostaw'@@
?}


\obieg_obszarzd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Uruchomienie obiegu dokumentu z obszaru
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='OBE_FAW_FLZD';
_params.AKCJA:='OBSZAR';
_params.PROC_START:='T';
_params.UIDREF:=ZD_NAG.uidref();
_params.CONTEXT:=obj_new('ZD_NAG');
_params.CONTEXT.ZD_NAG:=ZD_NAG.ref();
exec('mp_run','#b__box',_params)

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 d678b55644c8f9174505f8354be134a91e7befc0093f4b7369f5974aecdb69b6fac044d6a1c803299673a4a25a9c879fd44e4d741094276b6e005b94bb9c4b788e3f756414f4c0b2c493c15c0adb3ffce21e245a7725266af2289b6663e5703954e9c24691827590ac6e34da9b4226750d7debe50704f8064c6b9607b2e7431a

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rrk_dkpd.fml
:: Utworzony: 19.11.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RRK_DKPD - Tworzenie potwierdzeń sald
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tworzenie potwierdzeń sald - główna formuła czynności FKS_RRK_DKPD
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,HRB
::# parses=exec('parses_ser_nags','rozrach_kor')
::# kind=WE, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WY, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na potwierdzenie salda, required=T, keyref=T
::# kind=WY, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=LOOP
:: WARUNKI BRAMY
::# condition=Akceptacja, act_uid=FKS_RRK_DKPE, auto=T, formula=_a.SER_NAG<>null & _a.SER_NAG<>~~

_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='P';
params_set(params_get());
{? var_pres('UNIK_ID',_in)>0 & _in.UNIK_ID<>''
|| params_exec('par_out','rozrach_kor',_in.UNIK_ID);
   _mp.done()
|| idnagpar:=tm_stamp();
:: czynność startowa
   {? _mp.pathProc()
   || SER_KOR.ZAK_WAL:=SSTALE.WAL;
      ROZRACH.KONTR:=null;
      kh_all:=1;
      exec('bw','rozrach_kor','P','SER_NAGN','POTW');
      menu_txt(,'Dołącz');
      _mp.trigRef('SER_NAG');
      params_exec('potw_new1','!fks_rrk_dkpd');
      VAR_DEL.delete('kh_all')
   |? _akcja='Dołącz'
   || menu_txt(,'Dołącz');
      _mp.trigRef('SER_NAG');
      params_exec('potw_new1','!fks_rrk_dkpd')
   ?};
   params_exec('par_out','rozrach_kor',idnagpar);
   VAR_DEL.delete('idnagpar');
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ rejestrację potwierdzenia salda'@@;
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('SER_NAG',_wy)
|| _ref:=BB.sqlint($_wy.SER_NAG); _nam:=form(($_wy.SER_NAG)-8);
   {? _ref<>0 & _nam<>''
   || SER_NAG.cntx_psh(); SER_NAG.use(_nam); SER_NAG.prefix();
      KH.cntx_psh(); KH.prefix();
      {? SER_NAG.seek(_ref,_nam)
      || _desc:='Zakończ rejestrację potwierdzenia salda dla kontrahenta: %1 z dnia: %2'@@[SER_NAG.KH().SKR,$SER_NAG.DATA]
      ?};
      SER_NAG.cntx_pop(); KH.cntx_pop()
   ?}
?};
_desc


\n_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS:  Czynność FKS_RRK_DKPD - formuła główna w wersji nieprocesowej
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_in:=_args.in;
_akcja:=_args.akcja;
_autoakc:=exec('autoAkc','#b__box',,300780,'FKS_RRK_DKPE');
SER_KOR.TYP:='P';
params_set(params_get());
:: czynność startowa
{? _akcja='Dołącz'
|| menu_txt(,'Dołącz');
   {? _autoakc
   || exec('tab_autoakc','rozrach_kor');
      params_exec('potw_new1','!fks_rrk_dkpd');
      exec('autoakc','rozrach_kor',3)
   || params_exec('potw_new1','!fks_rrk_dkpd')
   ?}
?};
~~


\potw_new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie noty odsetkowej
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RRK_DKPD';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\potw_new1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie potwierdzenia salda - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
exec('gen_kor1','rozrach_kor');
{? exec('okno','!fks_rrk_dkpd')
|| {? ROZNE.UT_DOK || exec('initmpks','rozrach_kor') ?};
   SER_KOR.SER_DEF();
   {? -SER_KOR.KONTR='w'
   || _wybrani:={? SER_KOR.KONTR='W' & (ROZNE.PAR4=0 | ROZNE.PAR4=2)
                || {? ROZNE.PAR4=0
                   || exec('wybr_kh','rozrach_kor',ROZNE.PREFIX)
                   || exec('wybr_kh','rozrach_kor',ROZNE.MASKA)
                   ?}
                || 0
                ?};
      {? SER_KOR.WYB_WAL=1 || exec('wyb_wal','rozrach_kor') ?};
      {? l_wal>0 || exec('potwier','!fks_rrk_dkpd',_wybrani) ?};
      VAR_DEL.delete('TMPKH','ind_kh');
      {? SER_KOR.WYB_WAL=1 || exec('del_wal','rozrach_kor') ?}
   |? -SER_KOR.KONTR='b'
   || exec('initmpkh','rozrach_kor');
      KH.select();
      {? ~TMPKH.first()
      || FUN.info('Nie wybrano żadnego kontrahenta.'@)
      || {? SER_KOR.WYB_WAL=1 || exec('wyb_wal','rozrach_kor') ?};
         {? l_wal>0 || exec('potwier','!fks_rrk_dkpd',1) ?};
         {? SER_KOR.WYB_WAL=1 || exec('del_wal','rozrach_kor') ?}
      ?};
      exec('deltmpkh','rozrach_kor')
   ?};
   {? ROZNE.UT_DOK || exec('deltmpks','rozrach_kor') ?}
?};
exec('gen_kor2','rozrach_kor')


\okno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: Ustawia okienko dla dołączania potwierdzeń sald
::  OLD: \okno/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? date()>=SSTALE.AO().POCZ & date()<=SSTALE.AO().KON
|| SER_KOR.DATA:=date()
|| SER_KOR.DATA:=SSTALE.AO().KON
?};
SER_KOR.KONTR:='W';  SER_KOR.SER_DEF:=0; SER_KOR.NADOBROW:=SER_KOR.NADOBRON:=0;
ROZNE.PAR4:=0; ROZNE.PREFIX:=ROZNE.MASKA:=''; ROZNE.KS_OD:=ROZNE.KS_DO:=null;
exec('odd_filtr','fst');
{? kh_all
|| SER_KOR.win_edit('SER_RED1')
|| SER_KOR.win_edit('SER_RED2')
?};
SER_KOR.hdr_edit(' potwierdzenia salda'@);
SER_KOR.edit("exec('spr_pot','!fks_rrk_dkpd')")


\spr_pot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.62]
:: OPIS: Sprawdzanie parametrów potwierdzeń sald
::  OLD: \spr_pot/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
_a:='';
{? ROZNE.PAR4=2
|| WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=ROZNE.MASKA;
   {? +WYDRUKI.MASKA < WYDRUKIN.DLKON
   || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
   ?}
|? ROZNE.PAR4=4
|| KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR);
   {? ROZNE.KS_OD=null || KS.first(); ROZNE.KS_OD:=KS.ref() ?};
   {? ROZNE.KS_DO=null || KS.last(); ROZNE.KS_DO:=KS.ref() ?};
   KS.cntx_pop()
?};
{? ~exec('dataps','!fks_rrk_dkpd',SER_KOR.DATA)  || _a:='DATA'  ?};
{? _a='' & SER_KOR.SER_DEF=null
|| FUN.info('Nie wypełniony komentarz.'@); _a:='SER_DEF'
?};
{? _a='' & ROZNE.PAR4=4 & ROZNE.KS_OD().SYM>ROZNE.KS_DO().SYM
|| FUN.info('Błędny zakres kont.'@); _a:='KS_OD'
?};
{? _a='' & SER_KOR.WYB_WAL=0 & SER_KOR.WAL=null
|| FUN.info('Nie wybrano waluty.'@); _a:='WYB_WAL'
?};
{? _a='' & SER_KOR.NADOBRON<0
|| FUN.info('Minimalna kwota Na dobro Nasze nie może być ujemna.'@); _a:='NADOBRON'
?};
{? _a='' & SER_KOR.NADOBROW<0
|| FUN.info('Minimalna kwota Na dobro Wasze nie może być ujemna.'@); _a:='NADOBROW'
?};
_a


\dataps
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: Sprawdzanie daty dla potwierdzeń sald
::  OLD: \dataps/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_KOR.DATA<SSTALE.AO().POCZ | SER_KOR.DATA>SSTALE.AO().KON
|| FUN.info('Podana data musi być z bieżącego okresu obrachunkowego.'@); 0
|| 1
?}


\potwier
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.62]
:: OPIS: Generowanie nowych pozycji koresp. seryjnej typu P (wszystkie rozr.)
::   WE: _a=0 - wszyscy, 1 - wybrani
::  OLD: \potwier/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
pierwszy:=0; ile:=0; echo(); _synt:=SSTALE.AR().SYNT; _dl_pref:=+(|ROZNE.PREFIX);
{? ~kh_all || _a:=0 ?}; add_nag:=0;
{? kh_all || KH.index('KOD'); KH.prefix(2) ?};
{? ~kh_all | (kh_all & KH.first)
|| {!|?
     wal_snag:=null; wal_kod:=''; add_nag:=0;
     SLO.cntx_psh();
     {? l_wal=1 & SER_KOR.WYB_WAL=0
     || wal_snag:=SER_KOR.WAL; wal_kod:=SER_KOR.WAL().KOD
     |? l_wal=1 & SER_KOR.WYB_WAL=1
     || TMP_WAL.first(); exec('szu_wal','rozrach_kor')
     ?};
     SLO.cntx_pop();
     {? (~_a | (_a & (TMPKH.prefix(); TMPKH.find_key(KH.KOD,KH.KOD))))
     || {? ~exec('czy_kor','rozrach_kor',SER_KOR.TYP,SER_KOR.DATA,SER_KOR.SER_DEF,KH.KOD,null,wal_snag,SER_KOR.ODD().OD)
        || echo('Generowanie potwierdzeń sald dla kontrahenta: %1'@[KH.SKR]);
           KH.cntx_psh();  pierwszy:=0;  {? l_wal>1 || TMP_WAL.first() ?};
           {! |?
              {? l_wal>1 || exec('szu_wal','rozrach_kor') ?};
              {? SER_KOR.ODD=null
              || OP.index('KH2'); OP.prefix(wal_snag,KH.ref())
              || OP.index('KH3'); OP.prefix(wal_snag,SER_KOR.ODD,KH.ref())
              ?};
              {? OP.first()
              || {! |?
:: jezeli rozr. jest w wal. narodowej to nalezy sprawdzic, czy nie istnieje powiazany rozrach. w walucie obcej
                    {? (SER_KOR.WAL<>FINFO.NAROD |
                       {? exec('get_par','#parametr',40)='T' || exec('rozr_wal','rozrach_kor') || 1 ?}) &
                       {? ROZNE.PAR4=2 || exec('mask','konto',OP.AN)
                       |? ROZNE.PAR4=4 || (_synt+OP.AN)>=ROZNE.KS_OD().SYM & (_synt+OP.AN)<=ROZNE.KS_DO().SYM
                       |? ROZNE.PAR4=0
                       || (_dl_pref+OP.AN)=|ROZNE.PREFIX
                       || 1
                       ?} &
                       ((1+OP.TYP)='N' | (1+OP.TYP)='Z') &
                       {? exec('get_par','#parametr',46)='T'
                       || {? exec('spr_rwal','rozrach_kor',OP.SYM_ROK,OP.KH)
                          || ~exec('spr_rkor','rozrach_kor',OP.SYM_ROK,OP.KH,SER_KOR.TYP)
                          || 1
                          ?}
                       || 1
                       ?}
                    || {? SER_KOR.SER_DEF().ST=0
                       || exec('potwier1','!fks_rrk_dkpd',wal_snag)
                       || exec('potwier2','!fks_rrk_dkpd',wal_snag)
                       ?}
                    ?};
                    OP.next()
                 !}
              ?};
              {? l_wal>1 || TMP_WAL.next() || 0 ?}
           !};
           KH.cntx_pop()
        || {? ROZNE.UT_DOK
           || exec('addtmpks','rozrach_kor',1,'Kontr.: '+KH.SKR+' generowano korespondencję typu: '+SER_DEF.KOD+
              {? SER_KOR.ODD<>null || ' w jedn. ks.: '+SER_KOR.ODD().OD || '' ?});
              {? wal_snag=FINFO.NAROD
              || exec('addtmpks','rozrach_kor',1,'w walucie narodowej ('+FINFO.NAROD().KOD+') na dzień: '+$SER_KOR.DATA,1)
              || exec('addtmpks','rozrach_kor',1,'w walucie obcej na dzień: '+$SER_KOR.DATA,1)
              ?}
           ?}
        ?}
     ?};
     {? add_nag & SER_NAG.WAL=null || exec('spr_wal','rozrach_kor') ?};
     SER_NAG.cntx_psh();
     {? SER_NAG.get()
     || _tab_mail:=exec('email_os_kon_t','kontrahent',SER_NAG.KH,'Windykacja');
        {? _tab_mail.size()=1
        || SER_NAG.EMAIL:='N'
        |? _tab_mail.size()>1
        || SER_NAG.EMAIL:='W'
        || SER_NAG.EMAIL:='B'
        ?}; SER_NAG.put(); &_tab_mail
     ?};
     SER_NAG.cntx_pop();
     kh_all & KH.next()
   !};
   echo()
?};
{? ROZNE.UT_DOK
|| TMPKS.prefix(1);
   {? TMPKS.first() || TMPKS.select() ?}
?};
exec('ile_dok','rozrach_kor');
&ile; &pierwszy;  &add_nag;
0


\potwier1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.62]
:: OPIS: Generowanie nowych pozycji koresp. seryjnej typu P (wszystkie rozr.)
::   WE: _a - waluta
::  OLD: \potwier1/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_nasze:=_wasze:=0; _sym:=OP.SYM; _dat:=OP.DO; _dalej:=0; _druk:=0; _symz:=OP.SYM_ZEW;
ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref);
_war:="{? _a>0
       || _a>=SER_KOR.NADOBRON
       || {? _a=0
          || {? _b=0
             || SER_KOR.NADOBRON=0 & SER_KOR.NADOBROW=0
             || _b>=SER_KOR.NADOBROW
             ?}
          ?}
       ?}";
{? ZAP_OP.first()
|| {! |? {? ZAP_OP.DATA~1=SER_KOR.DATA~1 || _dalej:=1 ?}; ZAP_OP.next() !}
?};
{? OP.STAN='N' | (OP.STAN='R' & _dalej=1)
|| {? ZAP_OP.first
   || {! |?
         {? ZAP_OP.DATA<=SER_KOR.DATA
         || _nasze+=ZAP_OP.WN; _wasze+=ZAP_OP.MA; _druk:=1
         ?};
         ZAP_OP.next()
       !}
   ?};
   {? _druk=1
   || _wn:=F.SWn(_nasze,_wasze); _wm:=F.SMa(_nasze,_wasze);
      {?  _war(_wn,_wm)
      || {? pierwszy=0
         || _wal_n:={? l_wal=1 || _a || null ?};
            _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
            {? exec('dodaj','rozrach_kor',SER_KOR.TYP,SER_DEF.ref,SER_KOR.DATA,null,null,_wal_n,null,_obca)
            || add_nag:=1; exec('dodajp','!fks_rrk_dkpd',_sym,_dat,_wn,_wm,_a,_symz); pierwszy:=1
            ?}
         || exec('dodajp','!fks_rrk_dkpd',_sym,_dat,_wn,_wm,_a,_symz)
         ?}
      ?}
   ?}
?}


\potwier2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.62]
:: OPIS: Generowanie nowych pozycji koresp. seryjnej typu P (nierozl. rozr.)
::   WE: _a - waluta
::  OLD: \potwier2/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_war:="{? _a>0
       || _a>=SER_KOR.NADOBRON
       || {? _a=0
          || _b>=SER_KOR.NADOBROW
          ?}
       ?}";
_nasze:=_wasze:=0; _sym:=OP.SYM; _dat:=OP.DO; _symz:=OP.SYM_ZEW;
ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref); _druk:=0;
{? ZAP_OP.first()
|| {! |?
      {? ZAP_OP.DATA<=SER_KOR.DATA
      || _nasze+=ZAP_OP.WN;     _wasze+=ZAP_OP.MA; _druk:=1
      ?};
      ZAP_OP.next()
   !}
?};
{? _druk=1 & (_nasze$2<>_wasze$2)
|| _wn:=F.SWn(_nasze,_wasze); _wm:=F.SMa(_nasze,_wasze);
   {?  _war(_wn,_wm)
   || {? pierwszy=0
      || _wal_n:={? l_wal=1 || _a || null ?};
         _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
         {? exec('dodaj','rozrach_kor',SER_KOR.TYP,SER_DEF.ref,SER_KOR.DATA,null,null,_wal_n,null,_obca)
         || add_nag:=1; exec('dodajp','!fks_rrk_dkpd',_sym,_dat,_wn,_wm,_a,_symz); pierwszy:=1
         ?}
      || exec('dodajp','!fks_rrk_dkpd',_sym,_dat,_wn,_wm,_a,_symz)
      ?}
   ?}
?}


\dodajp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: dodanie pozycji potwierdzenia salda
::   WE: _a - symbol rozrachunku
::       _b - data rozrachunku
::       _c - kwota dobro nasze
::       _d - kwota dobro wasze
::       _e - waluta
::       _f - symbol zewnetrzny rozrachunku
::  OLD: \dodajp/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? SER_POZ.use('ser_pxx')
|| SER_POZ.prefix();
   SER_POZ.blank();
   SER_POZ.REF:=SER_NAG.ref;
   SER_POZ.T:=SER_KOR.TYP;
   SER_POZ.DOK:=_a;
   SER_POZ.DATA:=_b;
   SER_POZ.DNASZ:=_c;
   SER_POZ.DWASZ:=_d;
   SER_POZ.KON:=OP.AN;
   SER_POZ.ODD:=OP.ODD;
   SER_POZ.WAL:=_e;
   SER_POZ.SYM_ZEW:=_f;
   SER_POZ.SYM_ROK:=exec('op_unik_sym','rozrach',SER_POZ.DOK,SER_POZ.DATA);
   SER_POZ.add();
   exec('mod_nag','rozrach_kor')
?}


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 8d530eb0cf5992be39c20d944cf0dfe81043812a0e520b53dfbf4ccae33fdaa7f3c318b7d9b6d8d65748ad0832aaf6512802b517ffd336ba6b22b293ede827ab8f22680a026bc465fb814cbfb6030100b8795c96b21107f8fe1b176f1d2bb351d2696ef424a3dea13e3e3a76c4dd03d9957d103b5413860770c949e1e19b0dcb

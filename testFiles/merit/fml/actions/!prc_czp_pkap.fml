:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_pkap.fml
:: Utworzony: 19.09.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: Czynność PRC_CZP_PKAP - Przypisanie karty pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Przypisanie karty pracy dla współpracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();
_result:='';

_uidref:=exec('ref2uid','#table',_in.P);
{? _uidref=''
|| _result:=exec('error','!prc_czp_pkap')
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!prc_czp_pkap',_in.P);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Przypisanie karty pracy dla współpracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Przypisz kartę pracy do współpracownika: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Przypisz kartę pracy do współpracownika: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Przypisz kartę pracy do współpracownika: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Przypisz kartę pracy do współpracownika'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Komunikat o błędzie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Przypisanie karty pracy nie jest możliwe.'@,'Nie znaleziono współpracownika.'@]


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a [REFERENCE] - Wskazanie pracownika.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a,,1)
|| OSOBA.cntx_psh();
   P.OSOBA();
   R_KARHIS.cntx_psh();
   R_KARHIS.win_sel('WER_P');
   R_KARHIS.win_edit('RED');
   R_KARHIS.index('PR_DATA');
   R_KARHIS.prefix(P.ref());
   _result:=R_KARHIS.select();
   R_KARHIS.cntx_pop();
   OSOBA.cntx_pop()
|| _result:=exec('error','!prc_czp_pkap')
?};
P.cntx_pop();
_result


\r_karhis_wer_bfz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Obsługa akcji "Zakończ".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=R_KARHIS
|| sel_exit()
|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 5c1b9043744dea3a7d2afca10e20ebdc9895a4a80721f820391ea7388bf7a5821952e5715595b357df524368ac4c894e9f9fbb126afbf2399f2a91625b66c574c96ad7bb49d754e7dea3870a13dcd325cb0d124eeeaa5838952acbf025c805b8d5d946ea5ae7d48e095b6507e0e8acce2c1ac0ef734c7f240e7f3acdc528fb3b

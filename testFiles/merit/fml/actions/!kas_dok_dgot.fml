:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !kas_dok_dgot.fml
:: Utworzony: 24.05.2016
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Rejestracja wpłaty gotówkowej
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,KAS
::# kind=WY,   symbol=MBWPL,    type=_MBWPL,  name=Wpłata gotówkowa,    required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init','kas');

:: Uruchomiona akcja
_akcja:=_mp.akcja();

{? _mp.pathTodo()
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('MBWPL',_out)=type_of(null()) & _out.MBWPL
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego MBWPL.'@)
   ?}
?};

_mp.trigRef('MBWPL',,1,,exec('kind_out','#b_port'),'MBWPL');

{? _akcja='Dołącz'
   | _mp.pathProc()
   | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
||
:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
   params_set('out',_out,'mp',_mp,'akcja',_akcja);
   {? exec('addmbwpl','!kas_dok_dgot') || _mp.done() ?};

:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? var_pres('MBWPL',_outa)<>type_of(~~) & _outa.MBWPL
:: Dodano dokument
   ||
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      {? _mp.pathArea() || MBWPL.seek(_outa.MBWPL) ?}
:: Wycofanie czynności bo nie dodano dokumentu
   || _mp.cancel()
   ?}

|? _akcja='Popraw'
   | _mp.pathTodo()
|| {? var_pres('MBWPL',_out)=type_of(null())
:: Uruchomione w procesie
   || MBWPL.cntx_psh();
      MBWPL.prefix();
      {? MBWPL.seek(_out.MBWPL)
      ||
         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('popmbwpl','!kas_dok_dgot');
         _mp.done()
      ?};
      MBWPL.cntx_pop()
   |? _mp.pathTodo()
:: Uruchomione zadanie z listy todo i brak _out.RAPORT wtedy zadanie wycofujemy
   || _mp.cancel()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      exec('popmbwpl','!kas_dok_dgot');
      _mp.done()
   ?}

|? _akcja='Usuń'
|| {? var_pres('MBWPL',_out)=type_of(null())
:: Uruchomione w procesie
   || _mbwpl:=null();
      MBWPL.cntx_psh();
      _rec:=exec('rec_after_del','#table',MBWPL);
      {? _mp.pathArea()=0 || MBWPL.prefix() ?};
      {? MBWPL.seek(_out.MBWPL)
      ||
         _wyn:=exec('delwplgt','!kas_dok_dgot');

         {? ~MBWPL.seek(_out.MBWPL)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _mbwpl:=MBWPL.ref()
      ?};
      MBWPL.cntx_pop();
      {? _wyn=1 & _rec<>null() || MBWPL.seek(_rec) ?};
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _mp.pathArea() || {? _mbwpl || MBWPL.seek(_mbwpl) ?} ?};
      _mp.done()

   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      _rec:=exec('rec_after_del','#table',MBWPL);
      _wyn:=exec('delwplgt','!kas_dok_dgot');
      {? _wyn=1 & _rec<>null() || MBWPL.seek(_rec) ?};

      _mp.done()
   ?}

|| _mp.error('Nieobsługiwana ścieżka.'@)
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('MBWPL',_out)=type_of(null()) & _out.MBWPL
|| 'Zakończ rejestrację wpłaty gotówkowej: %1'@@[exec('record','#to_string',_out.MBWPL)]
|| 'Zarejestruj wpłatę gotówkową'@@
?}


\zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Raport kasowy - Zakończ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Zakończyć rejestrację raportu kasowego?'@)
|| exec('set_lock','kasa_raport',1)
?}


\mbwpl_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Rejestracja wpłaty gotówkowej - Dołącz
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='KAS_DOK_DGOT';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';

exec('mp_run','#b__box',_params)


\mbwpl_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Rejestracja wpłaty gotówkowej - Popraw
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='KAS_DOK_DGOT';
_params.UIDREF:=MBWPL.uidref();
_params.AKCJA:='Popraw';

exec('mp_run','#b__box',_params)


\mbwpl_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Rejestracja wpłaty gotówkowej - Usuń
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='KAS_DOK_DGOT';
_params.UIDREF:=MBWPL.uidref();
_params.AKCJA:='Usuń';

exec('mp_run','#b__box',_params)


\mbwpl_zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Rejestracja raportu kasowego - Zakończ
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='KAS_DOK_DGOT';
_params.UIDREF:=MBWPL.uidref();
_params.AKCJA:='Zakończ_wer';

exec('mp_run','#b__box',_params)


\popmbwpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: poprawia wplate gotowkowa
::  OLD: \popmbwpl/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
{? MBWPL.STAN='A'
|| FUN.info('Wpłata gotówkowa anulowana.\nModyfikacja niemożliwa.'@);
   0
|? MBWPL.STAN='R'
|| FUN.info('Wpłata gotówkowa zarejestrowana w raporcie kasowym.\nModyfikacja niemożliwa.'@);
   0
|? MBWPL.STAN='P'
|| {? MBWPL.NRPTW
   || FUN.info('Wystawiono potwierdzenie do wpłaty gotówkowej.\nModyfikacja niemożliwa.'@);
      0
   |? FUN.ask('Czy poprawić wpłatę gotówkową?'@) & MBWPL.edit("exec('chkwplgt','!kas_dok_dgot')")
   || MBWPL.put(1)
   ?}
|| 0
?}


\delwplgt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: usuniecie wpłat gotowkowych
::   WY: (0/1) czy usunięto
::  OLD: \delwplgt/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=MBWPL.sel_aget();
MBWPL.sel_adel();
MBWPL.cntx_psh();
_wyn:=0;
{? _tab.size()
|| {? _tab.first() & FUN.ask('Czy usunąć zaznaczone wpłaty gotówkowe?'@)
   || {!
      |? {? (MBWPL.clear; MBWPL.seek(_tab.REF,)) & MBWPL.STAN<>'R'
         || MBWPL.del();
            _wyn:=1
         ?};
         _tab.next()
      !}
   ?}
|| {? MBWPL.STAN='R'
   || FUN.info('Wpłata zarejestrowana w raporcie kasowym.\nUsunięcie niemożliwe.'@)
   |? FUN.ask({? MBWPL.MOB='T'
              || 'Uwaga. Wpłata zarejestrowana na urządzeniu mobilnym.\n'
                 'Czy usunąć wpłatę gotówkową?'@
              || 'Czy usunąć wpłatę gotówkową?'@
              ?})
   || MBWPL.del();
      _wyn:=1
   ?}
?};
MBWPL.cntx_pop();
_wyn


\addmbwpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: dołączenie wpłaty gotówkowej
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
MBWPL.blank();
{? MBWPL.edit("exec('chkwplgt','!kas_dok_dgot')") || _wyn:=MBWPL.add(1) ?};
_wyn


\chkwplgt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: sprawdzenie wplat gotowkowych
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? MBWPL.DATA=date(0,0,0)
|| FUN.info('Należy podać datę pobrania.'@);
   _wyn:='DATA'
::|? MBWPL.HAN=null
::|| FUN.info('Należy podać handlowca.'@);
::   _wyn:='HAN'
|? MBWPL.KH=null
|| FUN.info('Należy podać kontrahenta.'@);
   _wyn:='KH'
|? MBWPL.WAR<=0
|| FUN.info('Należy podać kwotę rozliczenia (wartość większa od zera).'@);
   _wyn:='WAR'
|? MBWPL.GOEMAIL='T' & MBWPL.EMAIL=''
|| FUN.info('Należy podać adres e-mail.'@);
   _wyn:='EMAIL'
?};
_wyn


\prdt_wpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: przed redakcja pola data rozliczenia
::  OLD: \prdt_wpl/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
MBWPL.MOB<>'T' & MBWPL.STAN='N'


\pokh_wpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: po kontrahencie wplaty - podpowiadanie email-a
::  OLD: \pokh_wpl/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
{? MBWPL.EMAIL='' || MBWPL.EMAIL:=MBWPL.KH().EM ?};
1


\po_goemail
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: po zaznaczeniu, że wymagany e-mail
::----------------------------------------------------------------------------------------------------------------------
MBWPL.efld_opt('RED','mark='+{? MBWPL.GOEMAIL='T' || '1' || '0' ?},,'EMAIL');
1


\anulmwpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: anulowanie zaznaczonych wplat
::   WE: [_a] - domyslnie 1-anulacja 0-przywrocenie
::  OLD: \anulmwpl/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};

_tab:=MBWPL.sel_aget();
MBWPL.sel_adel();
MBWPL.cntx_psh();
{? _tab.size()
|| _tab.clear();
   {? _tab.first() & FUN.ask({? _a
                             || 'Czy anulować zaznaczone wpłaty gotówkowe?'@
                             || 'Czy przywrócić zaznaczone wpłaty gotówkowe?'@
                             ?})
   || {!
      |? {? (MBWPL.clear(); MBWPL.seek(_tab.REF,)) & {? _a || MBWPL.STAN<>'R' || MBWPL.STAN='A' ?}
         || MBWPL.STAN:={? _a || 'A' || 'P' ?};
            MBWPL.put(1)
         ?};
         _tab.next()
      !}
   ?}
|| {? MBWPL.STAN='R'
   || {? _a
      || FUN.info('Wpłata zarejestrowana w raporcie kasowym.\nAnulacja niemożliwa.'@)
      || FUN.info('Wpłata zarejestrowana w raporcie kasowym.\nPrzywrócenie niemożliwe.'@)
      ?}
   |? ~_a & MBWPL.STAN<>'A'
   || FUN.info('Wpłata nie została anulowana.\Przywrócenie niemożliwe.'@)
   |? FUN.ask({? _a
              || 'Czy anulować wpłatę gotówkową?'@
              || 'Czy przywrócić wpłatę gotówkową?'@
              ?})
   || MBWPL.clear();
      MBWPL.STAN:={? _a || 'A' || 'P' ?};
      MBWPL.put(1)
   ?}
?};
MBWPL.cntx_pop();
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:13 ad004ef11705e81a60712265229954f8f6b56248ec26a751649ac4c2fa0bf030c65cebc1c691edb058ea6d056d5ac1a5782b7d810916a2d03ddc08592f27fb2672134f5d6707a6958efc206f0cf814969cb04a5b019445fece96ee55b5433accc3dc809e43db0299393ecbee6e765b2b9bbc876104e6d830a890884cede6d2ce

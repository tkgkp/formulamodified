:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orad.fml
:: Utworzony: 06.04.2023
:: Autor: MicKoc
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORAD - Rejestracja adresów pr.zdalnej
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Rejestracja adresów świadczenia pracy zdalnej - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_ezk_orad')

|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność '+_mp.buf_act.UID+' nie obsługuje akcji '+_do+'.'
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orad',_in.P);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Rejestracja adresów świadczenia pracy zdalnej - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj adres świadczenia pracy zdalnej: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj adres świadczenia pracy zdalnej: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj adres świadczenia pracy zdalnej: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj adres świadczenia pracy zdalnej'@@
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie pracownika
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!pkd_ezk_orad');

{? var_pres('_a')<>type_of(null) | _a=null
|| return(_err_msg)
?};

Cntx.psh(P,OSOBA,PPSF_ADR,KRAJE,PPSF_TA);

P.prefix();
{? ~P.seek(_a)
|| Cntx.pop(P,OSOBA,PPSF_ADR,KRAJE,PPSF_TA);
   return(_err_msg)
?};

: ustal osobę
P.OSOBA();

: przeglądanie danych
PPSF_TA.win_sel('WER');
KRAJE.win_sel('SLO');

: pokaż dane
PPSF_ADR.index('PRAC');
PPSF_ADR.prefix(_a);
PPSF_ADR.win_sel('WERI');
PPSF_ADR.win_edit('RED');
PPSF_ADR.win_patt('RED');
exec('ppsf_adr_eopt','ppsf',1);
_result:=PPSF_ADR.select();
exec('ppsf_adr_eopt','ppsf',0);
: porządki
Cntx.pop(P,OSOBA,PPSF_ADR,KRAJE,PPSF_TA);

_result


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli PPSF_ADR z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli PPSF_ADR jako składowa okna grupowego tabeli UD_DEF).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=PPSF_ADR
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie adresów świadczenia pracy zdalnej niemożliwe.\nNie znaleziono pracownika.'


\ppsf_adr_pobierz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Pobiera adres z OS_ADRES do PPSF_ADR
::   WE:
::   WY:
::  OLD: \ppsf_adr_pobierz_a/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
OS_ADRES.cntx_psh();
_win:=OS_ADRES.mk_sel('Wybór adresu'@,'P',0,'#ppsf_adr_adres',,,,,'U',,,,,'normal');
OS_ADRES.win_fld(_win,,'OD');
OS_ADRES.win_fld(_win,,'DO');
OS_ADRES.win_fld(_win,,'RODZAJ');
OS_ADRES.win_fld(_win,,'MIASTO',,,20);
OS_ADRES.win_fld(_win,,'ULICA',,,20);
OS_ADRES.win_fld(_win,,'DOM');
OS_ADRES.win_fld(_win,,'LOKAL');
OS_ADRES.win_act(_win,0,'Formuła','Wybierz'@@,,,,"sel_exit()",1,,,,'W');
OS_ADRES.win_sel(_win);
OS_ADRES.index('OD');
OS_ADRES.prefix(P.OSOBA);
{? OS_ADRES.select()
|| _len:=obj_len(OS_ADRES);
   PPSF_ADR.blank();
   {! _licz:=1.._len |!
      _acr:=OS_ADRES.fld_acr(_licz);
      {? var_pres(_acr,PPSF_ADR)=27
      || ($('PPSF_ADR.'+_acr))():=OS_ADRES[_licz]
      ?}
   !};
   PPSF_ADR.KRAJ:=OS_ADRES.KRAJ;
   {? PPSF_ADR.edit("exec('ppsf_adr_ra','!pkd_ezk_orad')") || PPSF_ADR.add() ?}
?};
OS_ADRES.cntx_pop()


\ppsf_adr_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Rekord po tabeli PPSF_ADR
::   WE:
::   WY:
::  OLD: \ppsf_adr_ra/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_adr_chk','ppsf',-menu_txt()='popraw')


\ppsf_adr_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IGPTASZE [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Obsługa wyzwalacza "Dołącz - przed" tabeli PPSF_ADR.
::  OLD: \ppsf_adr_addb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_adr_chk','ppsf',0)


\ppsf_adr_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IGPTASZE [12.51]
::  MOD: MicKoc [22.26]
:: OPIS: Obsługa wyzwalacza "Popraw - przed" tabeli PPSF_ADR.
::  OLD: \ppsf_adr_putb/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_adr_chk','ppsf',1)

:Sign Version 2.0 jowisz:1045 2023/10/02 09:18:54 39e758b6de78184b47da6e3bbf77d8a1fc6fc53b70f5cc1c29c293ba78fa97927b5d50c61ec835445e9714c4d7e16065e4393e7179f6fc56cefa21cd0417f2d3aad754cc1e1dc90d81b1b0f29976bd85583d9da70fbc33ae46c3883cb25d00743cb8d49b95be704283c2fc4e09fb8f12a65e4c8c27dfb0fa225104a91bbe2462

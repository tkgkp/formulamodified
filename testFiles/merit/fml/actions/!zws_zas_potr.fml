:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_zas_potr.fml
:: Utworzony: 08.06.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_ZAS_POTR - Potrącenia z analizy zasobów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Potrącenia z analizy zasobów - główna formuła czynności ZWS_ZAS_POTR
::----------------------------------------------------------------------------------------------------------------------

:: PARAMETRY WE:
::# kind=WE, symbol=RN, type=NUMBER, name=Kod rubryki na liście płac, required=N, fml_val="exec('r_select','!zws_zas_potr',_a)"
::# kind=WE, symbol=ANZASPE, type=_ANZASPE, name=Wskazanie na nagłowek analizy, required=N, keyref=T

:: PARAMETRY WY:
::# kind=WY, symbol=ANZASPE, type=_ANZASPE, name=Wskazanie na nagłowek analizy, required=N, keyref=T


:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_anzaspe:=null;
_service:=_mp.isService(); _rubryka:=0; _done:=1;
{? var_press('RN',_we)>0 & _we.RN>0
|| R.cntx_psh(); R.index('RUBKOD'); R.prefix(_we.RN);
   {? R.first() || _rubryka:=R.ref() ?};
   R.cntx_pop()
|| {? _service
   || _rubryka:=0
   || R.index('RUBKLKOD'); R.prefix('S'); R.win_sel('WYB');
      {? R.select()
      || _rubryka:=R.ref()
      || _done:=0
      ?}
   ?}
?};
{? _rubryka
|| __PARSES.setEnv('FKS',0);
:: dołącz z pulpitu
   {? _akcja='Generuj'
   || _mp.trigRef('ANZASPE');
      _anzaspe:=ANZASPE.ref();
      params_exec('zasoby_potr','!zws_zas_potr',_rubryka)
:: TODO
   |? ~_service & _mp.pathTodo()
   || {? var_pres('ANZASPE',_we)>0
      || _ref:=BB.sqlint($_we.ANZASPE);
         {? _ref<>0
         || ANZASPE.cntx_psh(); ANZASPE.prefix();
            {? ANZASPE.seek(_ref,ANZASPE.name())
            || _done:=0; _anzaspe:=ANZASPE.ref();
               exec('anzaspe_potr_todo','!zws_zas_potr');
               {? var_pres('pot_an')>0
               || {? pot_an
                  || params_exec('zasoby_potr','!zws_zas_potr',_rubryka);
                     _done:=1
                  ?};
                  VAR_DEL.delete('pot_an')
               ?}
            ?};
            ANZASPE.cntx_pop()
         ?}
      || FUN.info('Nie znaleziono analizy zasobów.'@);
         _anzaspe:=null
      ?}
   |? var_press('ANZASPE',_we)>0 & _we.ANZASPE
   || _ref:=BB.sqlint($_we.ANZASPE);
      {? _ref<>0
      || ANZASPE.cntx_psh(); ANZASPE.prefix();
         {? ANZASPE.seek(_ref,ANZASPE.name())
         || _anzaspe:=ANZASPE.ref();
            params_exec('zasoby_potr','!zws_zas_potr',_rubryka)
         ?};
         ANZASPE.cntx_pop()
      ?}
   ?}
?};
{? _anzaspe
|| ANZASPE.cntx_psh(); ANZASPE.prefix();
   {? ANZASPE.seek(_anzaspe,ANZASPE.name())
   || ANZASPE.POT:='T';
      ANZASPE.POT_DATA:=date();
      ANZASPE.POT_TIME:=time();
      ANZASPE.POT_USER:=OPERATOR.USER;
      ANZASPE.put()
   ?};
   ANZASPE.cntx_pop()
?};
_wy.ANZASPE:=_anzaspe;
_mp.save(,_wy);
{? _done || _mp.done() ?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Formuła na opis TO-DO czynności ZWS_ZAS_POTR
::----------------------------------------------------------------------------------------------------------------------

_mp:=params_get().mp;
_akcja:=_mp.akcja();
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('ANZASPE',_we) & _we.ANZASPE
|| _ref:=BB.sqlint($_we.ANZASPE);
   {? _ref<>0 & ANZASPE.seek(_ref,ANZASPE.name())
   || _desc:='Zakończ wysyłanie potrąceń z analizy zasobów: %1'@@[ANZASPE.OPIS]
   || _desc:='Zakończ wysyłanie potrąceń z analizy zasobów'@@
   ?}
|| _desc:='Zakończ wysyłanie potrąceń z analizy zasobów'@@
?};
_desc


\r_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wybranie rubryki dla analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
R.index('RUBKLKOD'); R.prefix('S'); R.win_sel('WYB');
{? R.select()
|| R.RN
|| ~~
?}


\zasoby_potr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wygenerowanie potrąceń dla analizy zasobów
::   WE: _a [REFERENCE] - Wskazanie na rubrykę
::----------------------------------------------------------------------------------------------------------------------
AN_ZAS.cntx_psh(); AN_ZAS.prefix();
POJAZDY.cntx_psh(); POJAZDY.index('ID');
NRKRTSIM.cntx_psh(); NRKRTSIM.index('ID');
AN_ZASP.cntx_psh(); AN_ZASP.prefix();
ZASOBY.cntx_psh(); ZASOBY.prefix();
POZAN.cntx_psh(); exec('use_pozan','zasoby_wspolne');
POZAN.index('POTR'); POZAN.prefix(ANZASPE.ref(),'T','T');
{? POZAN.first()
|| tmppozan:=tab_tmp(1,'OSOBA','STRING[16]',,
                       'POTR','REAL',
                    );
   {! |?
      {? POZAN.DEF().RODZ_ZWR<>1
      || POZAN.ZAS();
         {? ZASOBY.TYP='P'
         || POJAZDY.prefix(ZASOBY.KARTOTEK);
            {? POJAZDY.first() & POJAZDY.OSOBA
            || params_exec('zasoby_potr1','!zws_zas_potr',POJAZDY.OSOBA)
            ?}
         |? ZASOBY.TYP='S'
         || NRKRTSIM.prefix(ZASOBY.KARTOTEK);
            {? NRKRTSIM.first() & NRKRTSIM.OSOBA
            || params_exec('zasoby_potr1','!zws_zas_potr',NRKRTSIM.OSOBA)
            ?}
         ?}
      ?};
      POZAN.next()
   !};
   OSOBA.cntx_psh(); OSOBA.prefix();
   tmppozan.prefix();
   {? tmppozan.first()
   || {! |?
         {? OSOBA.seek(tmppozan.OSOBA,OSOBA.name())
         || params_exec('zasoby_potr2','!zws_zas_potr',ANZASPE.DEFAN().FIRMA,OSOBA.ref(),
                        ANZASPE.OKAN().POCZ,tmppozan.POTR,_a)
         ?};
         tmppozan.next()
      !}
   ?};
   VAR_DEL.delete('tmppozan');
   OSOBA.cntx_pop()
?};
POZAN.cntx_pop(); AN_ZASP.cntx_pop(); ZASOBY.cntx_pop(); POJAZDY.cntx_pop(); NRKRTSIM.cntx_pop(); AN_ZAS.cntx_pop();
1


\zasoby_potr1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wygenerowanie potrąceń dla analizy zasobów - wewnętrzna
::   WE: _a - OSOBA.ref()
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| tmppozan.prefix($_a);
   {? tmppozan.first()
   || tmppozan.POTR+={? AN_ZASP.RODZ_ZWR=2
                     || POZAN.WART_03
                     |? AN_ZASP.RODZ_ZWR=3
                     || POZAN.WART_02
                     ?};
      tmppozan.put()
   || tmppozan.OSOBA:=$_a;
      tmppozan.POTR:={? AN_ZASP.RODZ_ZWR=2
                     || POZAN.WART_03
                     |? AN_ZASP.RODZ_ZWR=3
                     || POZAN.WART_02
                     ?};
      tmppozan.add()
   ?}
?}


\zasoby_potr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wygenerowanie potrąceń dla analizy zasobów
::   WE:  _a  [REFERENCE] - Wskazanie firmy, w której ma powstać zapis [domyslnie: ]
::        _b  [REFERENCE] - Wskazanie osoby.
::        _c  [DATE]      - Data początkowa spłaty.
::        _d  [NUMBER]    - Kwota zadłużenia (musi być dodatnia).
::        _e  [REFERENCE] - Wskazanie na rubrykę
::   WY: Status operacji:
::          1 - Sukces (potrącenie wygenerowane).
::          0 - Porażka (być może błąd parametrów wywołania).
::----------------------------------------------------------------------------------------------------------------------
_firma:={? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=FIRMA || _a || exec('ref_firma','ustawienia') ?};
{? var_pres('_b')<>type_of(null()) | var_pres('_c')<>type_of(date()) | var_pres('_d')<>type_of(0)
|| return(0)
?};
{? _d<=0 | ~_e || return(0) ?};
_ret:=0;
KOM_RP.cntx_psh(); KOM_RP.index('KOM_RR'); KOM_RP.prefix(_e);
{? KOM_RP.first()
|| KOM_OS.cntx_psh();
   KOM_OS.prefix();
   KOM_OS.blank();
   KOM_OS.FIRMA:=_firma;
   KOM_OS.OSOBA:=_b;
   KOM_OS.KOM_RP:=KOM_RP.ref();
   KOM_OS.OD:=_c;
   KOM_OS.A:='T';
   KOM_OS.P:=KOM_RP.P;
   KOM_OS.WART:=_d;
   KOM_OS.AUTO:='T';
   _ret:=KOM_OS.add(1);
   KOM_OS.cntx_pop()
?};
KOM_RP.cntx_pop();
_ret


\gen_zas_potr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Generowanie potrąceń dla zasobu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_ZAS_POTR';
_params.AKCJA:='Generuj';
_params.UIDREF:=ANZASPE.uidref();
_params.CONTEXT:=obj_new('ANZASPE');
_params.CONTEXT.ANZASPE:=ANZASPE.ref();
exec('mp_run','#b__box',_params)


\wyc_zas_potr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wycofanie znacznika potrąceń dla analizy
::----------------------------------------------------------------------------------------------------------------------
ANZASPE.POT:='N';
ANZASPE.POT_DATA:=date(0,0,0);
ANZASPE.POT_TIME:=time(0,0,0);
ANZASPE.POT_USER:=null;
ANZASPE.put()


\anzaspe_potr_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Potrącenie z analizy zasobów - TODO
::----------------------------------------------------------------------------------------------------------------------
{? ANZASPE.get()
|| {? ANZASPE.r_lock(1,1)
   || pot_an:=0;
      exec('anzaspe_edit','!zws_zas_potr');
      ANZASPE.display();

      unlock_r()
   || FUN.info('Analiza zasobów wykorzystywana przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono analizy zasobów.'@)
?}


\anzaspe_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Tworzy okno redagowania analizy zasobów
::----------------------------------------------------------------------------------------------------------------------
_okn:=ANZASPE.mk_edit('Analiza zasobów'@);
ANZASPE.win_ewin(_okn,,'RED_POP');
_btn0:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['P&ozycje'@],
                        "params_exec('poz_anzaspe','zasoby_wspolne');''"
                       );
ANZASPE.btn_eopt(_okn,_btn0,'tooltip='+'Przeglądanie pozycji analizy zasobów'@);
ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Generuj potrącenia'@],
                 "pot_an:=1; 'key:F2'");
_btnan:=ANZASPE.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],'key:Esc');
ANZASPE.btn_eopt(_okn,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
ANZASPE.win_edit(_okn)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 bf5369b9cd6bb8269ef1f9993270cd112e5f8444f173adbdfaafb62153d20c458f2f00b90ebb7dc906feb3df38e50097a1d03ae479904d5b1815ff3b6c1879fee4f835671fb98d43ee625f4b622c089734d01d6b46fd7210951aabb3d97e1f7d0b88902a26505f75346f55d7c34d79ba99a8f66c42fdd78dffaf15ba416bbb74

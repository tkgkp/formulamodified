:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ped_grp_azip.fml
:: Utworzony: 24.09.2021
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PED_GRP_AZIP - Eksport dokument. pracowniczej.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Eksport dokumentacji pracowniczej.
::   WE:
::   WY:
:: ~OST: INFILECHOOSER
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

exec('__F_ZATR','object');

_inTerm:=exec('interm','#system');
_sep0:=exec('sep','#file',0);
_pth:='';
_tmp_dir:=fmk_tmp_dir(0);
{? type_of(_tmp_dir)<>type_of(~~)
|| _pth:=_tmp_dir.get_path()
|| FUN.emsg('Nie udało się utworzyć katalogu tymczasowego po stronie serwera.'@)
?};
{? ~_inTerm
|| {!
   |? _again:=0;
      _dest:='@'+exec('filechooser','#file','Wskaż PUSTY katalog',,,,,'DIRECTORIES_ONLY',1);
      {? _dest='@'
      || return()
      || _FILE:=fdir(_dest,1,0);
         {? _FILE.first()
         || FUN.emsg((1-_dest)+'\n'+'Katalog nie jest PUSTY.'@);
            _again:=1
         |? _dest+1<>_sep0
         || _dest+=_sep0
         ?};
         obj_del(_FILE)
      ?};
      _again
   !};
   _baseDIR:=_dest
?};
_dest:=_pth+exec('sep','#file',2);
_prac:=exec('wybierz_parses','pracownik','PED');
{? ~_prac.P.first()
|| return()
?};

:: Do eksportu użyte zostaną mechanizmy stosowane w widokach. Dlatego część kodu poniżej będzie miała za zadanie
:: "odtworzenie" środowiska z okienek.

_TAB:=tab_tmp(1
   ,'OSOBA','STRING[16]','$OSOBA.ref()'
   ,'FORG','STRING[64]','Nazwa archiwum'@
   ,'FS','STRING[64]','Nazwa podpisanego archiwum'@
   ,'ZIP','STRING[1]','Archiwum'@
   ,'SIGN','STRING[1]','Podpis'@
   ,'MSG','SYS_MEMO','Komunikat'@
);
_NDX:=obj_new('OSOBA','FORG');
_NDX.OSOBA:=_TAB.index('?');
_NDX.FORG:=_TAB.ndx_tmp(,,'FORG',,);

OSOBA.cntx_psh();
OSOBA.prefix();
P.cntx_psh();
P.prefix();
ZAOH.cntx_psh();
ZAOH.prefix();
ZAOHI.cntx_psh();
ZAOHI.index('OSLP');

_files:=0;
_prg:=obj_new('size','step','proc');
_prg.size:=_prac.P.size();
_prg.step:=0;
_prg.proc:=-1;
{!
|? _prg.step+=1;
   _proc:=100*_prg.step/_prg.size$0;
   {? _prg.proc<>_proc
   || _prg.proc:=_proc;
      progress(_prg.proc,'Eksport dokumentacji pracowniczej [%1%%]'@ [form(_prg.proc,3,0)],'Przetwarzanie danych')
   ?};
   {? P.seek(_prac.P.UID)
   || P.OSOBA();
      {? ~_TAB.find_key($OSOBA.ref(),)
      || ZAOHI.prefix(OSOBA.ref());
         {? ZAOHI.first() & ZAOHI.ZAOH().get()
         || _array:=exec('tzalacz_get','zalacz','P');
            {? _array.err=''
            || _zip:=exec('get_unique_file_name','#file',OSOBA.NAZWISKO+'_'+OSOBA.PIERWSZE+'.zip',_dest);
               _err:=exec('eksportuj_zip','zalacz',_array.ZALACZ,'REF',_dest+_zip);
               {? ~_inTerm || fcopy(_dest+_zip,_baseDIR+_zip) ?};
               _TAB.blank();
               _TAB.OSOBA:=$OSOBA.ref();
               _TAB.FORG:=_zip;
               _TAB.ZIP:={? _err='' || 'T' || 'N' ?};
               _TAB.SIGN:='N';
               _TAB.memo_set(_err,'MSG');
               _TAB.add() & _TAB.memo_put(,'MSG');
               {? _err=''
               || _files+=1
               ?}
            ?};
            obj_del(_array)
         ?}
      ?}
   ?};
   _prac.P.next()
!};
prgs_clr();
{? _inTerm
|| _zipTAB:=fdir(_dest);
   {? var_press('_zipTAB')<>type_of(~~) & _zipTAB.first()
   || {!
      |? fpack_add(_dest+'DOKUMENTACJA.zip',_dest+_zipTAB.NAME);
         _zipTAB.next()
      !}
   ?};
   {? dlg_save(_dest+'DOKUMENTACJA.zip')<>''
   || FUN.info('Plik z dokumentacją pracowniczą należy jeszcze uwierzytelnić podpisem elektronicznym.'@)
   || _files:=0
   ?}
?};
ZAOHI.cntx_pop();
ZAOH.cntx_pop();
P.cntx_pop();
OSOBA.cntx_pop();

{? _files
|| {? ~_inTerm
   || exec('esign_decl','#esign');
      _esign:=obj_new(@.CLASS.ESIGN);
      _dest:=_baseDIR;
      _esign.s_dir_add(_dest);
      _ret:=_esign.sign('PKCS#7',1);

      {? _TAB.first()
      || {!
         |? {? _TAB.ZIP='T' & _ret.TAB.find_key('',_TAB.FORG,)
            || {? _ret.TAB.SRESCODE='0'
               || _TAB.SIGN:='T';
                  _TAB.FS:=_ret.TAB.FS;
                  _TAB.put()
               || _TAB.memo_set(_ret.TAB.SRESTXT,'MSG');
                  _TAB.memo_put(,'MSG')
               ?}
            ?};
            _TAB.next()
         !}
      ?}
   ?};

   _ws:=_TAB.mk_sel('Raport z eksportu dokumentacji pracowniczej'@,,,'#ped_grp_azip',,,,,'U');
   _TAB.win_fld(_ws,,'FORG',,,32,,,,,'Nazwa przygotowanego pliku archiwum'@);
   _TAB.win_fld(_ws,,'FS',,,32,,,,,'Nazwa pliku archiwum opatrzonego podpisem elektronicznym'@);
   _TAB.win_fld(_ws,,'ZIP',,,,,,,,'Archiwum zostało przygotowane [T/N]'@,2,,"'T'","'N'");
   _TAB.win_fld(_ws,,'SIGN',,,,,,,,'Archiwum zostało opatrzone podpisem elektronicznym [T/N]'@
      ,2,,"'T'","'N'"
   );
   _TAB.win_fld(_ws,,'MSG',,,60);
   _TAB.win_act(_ws,,'Szukaj');
   _TAB.win_act(_ws,,'Kolejność');
   _TAB.win_sel(_ws);

   _we:=_TAB.mk_edit('Raport z eksportu dokumentacji pracowniczej'@,,'#ped_grp_azip');
   _TAB.win_esep(_we,'Dane podstawowe'@);
   _TAB.win_efld(_we,,'FORG',,,64,,,,,'Nazwa przygotowanego pliku archiwum'@);
   _TAB.win_efld(_we,,'FS',,,64,,,,,'Nazwa pliku archiwum opatrzonego podpisem elektronicznym'@);
   _TAB.win_esep(_we,'Wynik operacji'@);
   _TAB.win_efld(_we,,'ZIP',,,,,,,,'Archiwum zostało przygotowane'@
      ,'check-box','check_label="%1"' ['Tak, archiwum zostało przygotowane '@],"'T'","'N'"
   );
   _TAB.win_efld(_we,,'SIGN',,,,,,,,'Archiwum zostało opatrzone podpisem elektronicznym'@
      ,'check-box','check_label="%1"' ['Tak, archiwum zostało opatrzone podpisem elektronicznym'@],"'T'","'N'"
   );
   _TAB.win_efld(_we,,'MSG',,,64,-3,,,,'Przyczyna niepowodzenia'@);
   _TAB.win_edit(_we);

   _TAB.index(_NDX.FORG);
   _TAB.select()

?};

~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 4e313e449974a39ccf6c6344a54876c894dab9ae5ee08b36516f1a7773f07b20405ba16b799a75efc8d565f7bfcfcc5d7ab61ecc385aa5744b380b12a3d1929759556cf998dbc7ee6b2827c25fab321488db688a7866d48dd3c234f27f7eb1afa177347b80848bb241de93596f787e09653de1afe7de2c44d71423d2be868d6b

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_tec_drto.fml
:: Utworzony: 02.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_TEC_DRTO - Rejestrowanie operacji karty technologicznej
::            Uwaga: większość kodu jest wspólna z czynnością TTE_WTE_DRTO - w przypadku zmian modyfikować oba pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności rejestrowania listy operacji dla karty technologicznej (TTE_TEC_DRTO)
::       Zadaniem formuły jest zredagowanie listy operacji z zamiennkami stanowisk, atrybutami i dokumentami
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;

:: UPRAWNIENIA
::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na kartę technologiczną, required=T, keyref=T
{? var_pres('TKTL',_in)<>type_of(~~) & var_pres('TKTL',_in)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_in)=type_of(~~) || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=TKTL, type=_TKTL, name=Wskazanie na kartę technologiczną, required=N
{? var_pres('TKTL',_out)<>type_of(~~) & var_pres('TKTL',_out)<>type_of(null()) || return() ?};

_akcja:=_mp.akcja();
{? _akcja='START'
||
:: Wejście do okienka w ramach zakładek - uruchomienie czynności
   _uidref:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"TKTL.uidref()",'');
   {? _uidref<>''
   || _mp.keyRef(_uidref);
      _mp.lock()
   ?};
   ~~

|? _akcja='STOP'
||
:: Wyjście z okienka w ramach zakładek - anulowanie czynności
   {? _mp.isMicro()
   || _uidref:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"TKTL.uidref()",'');
      {? _uidref<>''
      || _mp.delRef(_uidref);
         _mp.cancel()
      ?}
   ?};
   _mp.unlock()
|? _mp.isMicro()
||
:: Obsługa poza procesem


:: Zakończenie rejestrowania
   {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
   ||
      VAR.A_KTL:=_in.TKTL;

::    Generuje następniki, jeżeli brak
      NASTOPER.cntx_psh();
      NASTOPER.index('OPER');
      NASTOPER.prefix('T',_in.TKTL);
      {? ~NASTOPER.first()
      || exec('er_tab','tech_oper',_in.TKTL);
         exec('gen_pow','tech_oper',_in.TKTL);
         exec('graph_oper','tech_oper',0)
      ?};
      NASTOPER.cntx_pop();

      {? exec('valid_op1','tech_oper',_in.TKTL)
            &
         exec('valid_op','tech_oper',_in.TKTL,0,1)
            &
         exec('valid_str','tech_oper',_in.TKTL)
            &
         exec('spr_pdok','magdok_prod','TOPER','DOK',_in.TKTL,'',1,1)
            &
         FUN.ask('Czy zakończyć rejestrację operacji dla całej karty technologicznej?'@)

      || exec('FindAndGet','#table',TKTL,_in.TKTL,,"STAT_O:='T'; STAN:='P'; put()")
      ?}

   |? _akcja='SEL'
   ||
::    Obsługa funkcji 'Operacje' wywołanej z menu okna wertowania
      exec('toper_main','tech_oper',_in.TKTL,1)
   ?}
||
:: Obsługa w procesie
   exec('toper_in_process','!tte_tec_drto',_in,_int,_out,_mp)
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności rejestrowania listy operacji dla karty technologicznej (TTE_TEC_DRTO)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj operacje dla karty technologicznej %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"'Zredaguj operacje dla karty technologicznej %1 wer. %2'@@[NRK,WER]",'')

:: jest parametr wejściowy to ustawiam odpowiedniego TKTL
|? var_pres('TKTL',_in)
|| _tmp:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj operacje dla karty technologicznej %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"'Zredaguj operacje dla karty technologicznej %1 wer. %2'@@[NRK,WER]",'')

?};
_desc


\toper_in_process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła rejestrowania listy operacji dla karty technologicznej
::       Używana przez exec('main','!tte_tec_drto')
::   WE: _a - [obj_new] - parametry wejściowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_int:=_b;
_out:=_c;
_mp:=_d;

_torw:=VAR.A_TORW:='T';
_arch:=VAR.ARCH:='N';

_akcja:=_mp.akcja();

_can_continue:=1;
_clean_result:=params_exec('clean','!tte_tec_drto',_mp,_in);
_can_continue:=_clean_result.RESULT;
_tktl:=_clean_result.TKTL;

_ok:=0;
_res:=~~;
{? _can_continue>0
||
   {? _tktl<>null()
   ||
      {? exec('FindAndGet','#table',TKTL,_tktl,,"TORW",'')<>_torw
      || _msg:='Niezgodność wywołania czynności.\nNie można redagować operacji dla wzorca technologii.';
         FUN.emsg(_msg);
         _mp.error(_msg)

      |? exec('FindAndGet','#table',TKTL,_tktl,,"ARCH",'')<>_arch
      || _msg:='Niezgodność wywołania czynności.\nNie można redagować operacji dla karty archiwalnej.';
         FUN.emsg(_msg);
         _mp.error(_msg)

      |? exec('FindAndGet','#table',TKTL,_tktl,,"TYP().OPER",'')<>'T'
      || _msg:='Niezgodność wywołania czynności.\nTyp karty wyklucza redagowanie operacji.';
         FUN.emsg(_msg);
         _mp.error(_msg)

      ||
         exec('tktl_cntx_psh','tech_common');
         exec('tktl_use','tech_common',ref_name(_tktl)+3);
         TKTL.clear();

         {? TKTL.seek(_tktl)
         ||
            _mp.keyRef(TKTL.uidref());
            _mp.descTodo();

            VAR.A_KTL:=TKTL.ref();
            VAR.GRUPA:='N';
            VAR.A_OP:=null();
            VAR.A_ZLEC:=null();

::          Właściwa obsługa
::          Zakończenie rejestrowania
            {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
            ||
::             Generuje następniki, jeżeli brak
               NASTOPER.cntx_psh();
               NASTOPER.index('OPER');
               NASTOPER.prefix('T',TKTL.ref());
               {? ~NASTOPER.first()
               || exec('er_tab','tech_oper',TKTL.ref());
                  exec('gen_pow','tech_oper',TKTL.ref());
                  exec('graph_oper','tech_oper',0)
               ?};
               NASTOPER.cntx_pop();

               {? exec('valid_op1','tech_oper',TKTL.ref())
                     &
                  exec('valid_op','tech_oper',TKTL.ref(),0,1)
                     &
                  exec('valid_str','tech_oper',TKTL.ref())
                     &
                  exec('spr_pdok','magdok_prod','TOPER','DOK',TKTL.ref(),'',1,1)
                     &
                  FUN.ask('Czy zakończyć rejestrację operacji dla całej karty technologicznej?'@)

               || TKTL.STAT_O:='T';
                  TKTL.STAN:='P';
                  TKTL.put()
               ?}

::          Wywołanie z menu okna wertowania
            |? _akcja='SEL'
            || exec('toper_main','tech_oper',TKTL.ref(),1)

::          Lista TODO
            ||
::             Już wcześniej zakończono redagowanie operacji
               {? TKTL.STAT_O='T'
               || FUN.info('Zakończono rejestrację operacji dla całej karty technologicznej.'@)

::             Nie zakończono jeszcze redagowania nagłówka (być może usunięto zatwierdzenie)
               |? TKTL.STAT_N='N'
               || {? exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTE')
                  || _choice:=FUN.choice('Nie zakończono rejestracji nagłówka karty.'@,,'Zakończ nagłówek i redaguj'@,'Podgląd'@);
                     {? _choice=1
                     || _args:=exec('mp_run_a','#b__box');
                        _args.ACT_UID:='TTE_TEC_DRTE';
                        _args.UIDREF:=TKTL.uidref();
                        _args.AKCJA:='ZAKOŃCZ_BEZ_PYTANIA';
                        exec('mp_run','#b__box',_args);
                        exec('toper_main','tech_oper',TKTL.ref(),1)
                     |? _choice=2
                     || exec('toper_main','tech_oper',TKTL.ref(),0)
                     ?}
                  || _choice:=FUN.info('Nie zakończono rejestracji nagłówka karty.\nModyfikacje niemożliwe.'@,,'Podgląd'@,'Anuluj'@);
                     {? _choice=1
                     || exec('toper_main','tech_oper',TKTL.ref(),0)
                     ?}
                  ?}
               || exec('toper_main','tech_oper',TKTL.ref(),1)
               ?}
            ?};

            TKTL.clear();
            {? TKTL.seek(_in.TKTL)
            ||
::             Ustawiony status - czynność zostaje zakończona
               {? TKTL.STAT_O='T'
               || _out.TKTL:=TKTL.ref();
                  _mp.save(,_out);
                  _mp.done();
                  _res:='#CLOSE#'

               ?}
            ?}
         ?};
         exec('tktl_cntx_pop','tech_common')

      ?}

   || _msg:='Nie przekazono karty technologicznej.';
      FUN.emsg(_msg);
      _mp.error(_msg)
   ?}
?};
_res


\action_menu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie operacji dla karty technologicznej - właściwa akcja z menu okna wertowania TKTL
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| set_help(exec('set_help','#help','TTE_TEC_DRTO'));
   exec('toper_main','tech_oper',TKTL.ref(),0)

|? TKTL.ARCH='N' & TKTL.STAT_O='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTO')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_TEC_DRTO';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='SEL';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_TEC_DRTO'));
   exec('toper_main','tech_oper',TKTL.ref(),1)
?}


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zakończenie rejestrowania operacji dla karty technologicznej - obsługa przycisku w oknie kart technologicznych
::       (obsługa z pytaniem)
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_O='N'
|| {? exec('tktl_lock','tech_common',,'O')
   || _args:=exec('mp_run_a','#b__box');
      _args.ACT_UID:='TTE_TEC_DRTO';
      _args.UIDREF:=TKTL.uidref();
      _args.AKCJA:='ZAKOŃCZ_Z_PYTANIEM';
      _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
      exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

      exec('mp_run','#b__box',_args);

      exec('tktl_unlock','tech_common',,'O')
   ?}

|| FUN.info('Zakończono rejestrację operacji dla karty technologicznej.\nModyfikacje niemożliwe.'@)
?};
'key:Esc'


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('tech_clean','tech_common',_mp,_in)

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:42 2f33831bbf98acd80cc690124ef5233ba0d820140a06a64439decb2a66635b322a1858d5c0a39a503d8795179bab0ab4f60e7acc3a9d3dc87f00a3a786693267ff7ec8b8ba51314fcbb04b9f42abf3f663138aeed9d97e2677ec618933ec5d8f89c2c3a4b67d076ea34d9877077d7fb7c9f601aaf61de763b4b277fe8883d4ca

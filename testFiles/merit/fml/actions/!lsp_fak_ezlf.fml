:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_fak_ezlf.fml
:: Utworzony: 25.04.2017
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Akceptacja zlecenia fakturowania
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP
::# parses=exec('parses','!lsp_fak_ezlf')
::# kind=WE,   symbol=FAKZ,      type=_FAKZ,    name=Zlecenie fakturowania,   required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

exec('init_fak','lsp');

_akcja:=_mp.akcja();
_auto:=_akcja<>'Akceptuj' & _mp.isAutoRun();

{? ~(var_pres('FAKZ',_in)=type_of(null()) & _in.FAKZ)
|| _mp.error('Brak wymaganego parametru FAKZ.')
|| FAKZ.cntx_psh();
   FAKZ.prefix();
   {? ~FAKZ.seek(_in.FAKZ)
   || _mp.error('Nie znaleziono zlecenia fakturowania.')
   || {? _r_lock:=exec('r_lock_one','#table',FAKZ,FAKZ.ref())
      || params_set('mp',_mp);
         {? _akcja='Akceptuj' | _auto
         || exec('akceptuj','!lsp_fak_ezlf',_auto)
         |? _mp.pathTodo()
            | _mp.pathArea() & _akcja=''
         || _win_red:=exec('fakz_win_edit','faktury_wspolne');
            _ff:="params_exec('fakz_akceptuj_todo','!lsp_fak_ezlf'); 'key:Esc'";
            FAKZ.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
            _ff:="'key:Esc'";
            FAKZ.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
            FAKZ.win_edit(_win_red);
::          Dalsza obsługa akceptacji w exec('akceptuj','!lsp_fak_ezlf')
            FAKZ.display();
::          Usunięcie definicji tymczasowych okien
            FAKZ.win_edit(''); FAKZ.win_edel(_win_red)
         || _mp.error('Nieobsługiwana ścieżka.')
         ?};
         exec('r_unlock_one','#table',FAKZ,FAKZ.ref(),_r_lock)
      || exec('who_rlock_fakz','faktury_wspolne')

      ?}
   ?};
   FAKZ.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FAKZ',_in)=type_of(null()) & _in.FAKZ
|| 'Zaakceptuj zlecenie fakturowania: %1'@@[exec('record','#to_string',_in.FAKZ)]
|| ''
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('FAKZ',_in)=type_of(null()) & _in.FAKZ
|| FAKZ.cntx_psh();
   FAKZ.use(ref_name(_in.FAKZ));
   FAKZ.prefix();
   {? FAKZ.seek(_in.FAKZ)
   || __PARSES.setVal('OddzialLogProd',FAKZ.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=FAKZ.AR;
      _args.AM:=FAKZ.AM;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FAKZ.cntx_pop()
?};

1


\fakz_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Dokument sprzedaży - Akceptuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LSP_FAK_EZLF';
_params.UIDREF:=FAKZ.uidref();
_params.AKCJA:='Akceptuj';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'FAKZ',FAKZ.ref());

exec('mp_run','#b__box',_params)


\fakz_akceptuj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Dokument sprzedaży - Akceptuj z todo
::   WE: params_get()   - ustawiane w exec('main','!lsp_fak_ezlf')
::----------------------------------------------------------------------------------------------------------------------
params_exec('akceptuj','!lsp_fak_ezlf');
''


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Dokument sprzedaży - Akceptacja dokumentu
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       params_get()   - ustawiane w exec('main','!lsp_fak_ezlf')
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_mp:=params_get().mp;

_result:=0;

{? ~exec('czy_z_ok','okresy',-2)
|| 1
|? FAKZ.STAT_REJ='T'
|| {? ~_auto || FUN.info('Zlecenie fakturowania zostało już zaakceptowany.'@) ?};
   _result:=1;
   _mp.done()
|? _auto | FUN.ask('Zaakceptować zlecenie fakturowania %1?'@[FAKZ.NR_ZEW])
|| FAKZ.STAT_REJ:='T';
   {? FAKZ.put()
   || _result:=1;
      _mp.done()
   ?}
?};

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 8640657801c035a2afb50aedf0a105016408e6778d9f6a0da1024f651f3f97c9cdf03cf893a8b90c9aef712e83d652b05a68adaee1b99423a920e2e39bd41fdd2c5f2f93f9b81536023f2b7155a151e344dd90dea4cb2c99d3dcf994c21c4fc79a97703168877c547eb58c9891b1da9d7b84c0b94b9a904f83a07667d9283c1f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ruc_tekc.fml
:: Utworzony: 22.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RUC_TEKC - Akceptacja planu faktoringu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja planu faktoringu - główna formuła czynności FKS_RUC_TEKC
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_par_nag','rozrach')
::# access=exec('upr_par_nag','rozrach')
::# kind=WE, symbol=PAR_NAG, type=_PAR_NAG, name=Wskazanie na nagłówek planu umowy faktoringu, required=T, keyref=T
::# kind=WY, symbol=PAR_NAG, type=_PAR_NAG, name=Wskazanie na nagłówek planu umowy faktoringu, required=T, keyref=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='F';
{? var_pres('PAR_NAG',_in) & ~var_pres('PAR_NAG',_out)
|| _out.PAR_NAG:=_in.PAR_NAG; _mp.save(,_out)
?};
{? _mp.pathTodo() | _mp.isAutoRun()
|| {? var_press('PAR_NAG',_in)>0
   || _ref:=BB.sqlint($_in.PAR_NAG); _nam:=form(($_in.PAR_NAG)-8);
      {? _ref<>0 & _nam<>''
      || PAR_NAG.cntx_psh(); PAR_NAG.prefix();
         {? PAR_NAG.seek(_ref,_nam)
         || {? PAR_NAG.TYP=SER_KOR.TYP
            || params_exec('kom_akc','rozrach',1)
            || FUN.info('Wybrana umowa nie jest umową faktoringu.'@); _mp.error()
            ?}
         || {? ~_mp.isAutoRun()
            || FUN.info('Nie znaleziono umowy faktoringu.'@)
            ?};
            _out.PAR_NAG:=null;
            _mp.save(,_out);
            _mp.done()
         ?};
         PAR_NAG.cntx_pop()
      || {? ~_mp.isAutoRun()
         || FUN.info('Nie znaleziono umowy faktoringu.'@)
         ?};
         _out.PAR_NAG:=null;
         _mp.save(,_out);
         _mp.done()
      ?}
   || {? ~_mp.isAutoRun()
      || FUN.info('Nie znaleziono umowy faktoringu.'@)
      ?};
      _out.PAR_NAG:=null;
      _mp.save(,_out);
      _mp.done()
   ?}
|? _akcja='Akceptuj'
|| params_exec('kom_akc','rozrach')
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zaakceptuj umowę faktoringu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('PAR_NAG',_we)
|| _ref:=BB.sqlint($_we.PAR_NAG); _nam:=form(($_we.PAR_NAG)-8);
   {? _ref<>0 & _nam<>''
   || PAR_NAG.cntx_psh(); PAR_NAG.use(_nam); PAR_NAG.prefix();
      {? PAR_NAG.seek(_ref,_nam)
      || _desc:='Zaakceptuj umowę faktoringu: %1 z dnia %2'@@[PAR_NAG.PAR_UM().SYM,$PAR_NAG.DATA]
      ?};
      PAR_NAG.cntx_pop()
   ?}
?};
_desc


\faktor_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja planu umowy faktoringu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RUC_TEKC';
_params.AKCJA:='Akceptuj';
_params.UIDREF:=PAR_NAG.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.PROC_START:='N';
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'PAR_NAG',PAR_NAG.ref());
exec('mp_run','#b__box',_params)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'parnag'>0
         || _record:=exec('FindAndGet','#table',PAR_NAG,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 05f743053ae2e4ad35d4ac8ef0a5cfdbc1dfa0be7bd8d64a4cc163ddd934b532e58199f0710af40dabbcabc46c1f4f4a50d6a1eddaabcae4f09c758661c193d38fb8248e380a476db7fa1d3256458c3c5cf2080d1d37493b36675cc8f96868e51bed7afc51e0dde0c8015904ff29981b2f347d1252b7a2d20996e4f6afe56c7b

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_plko.fml
:: Utworzony: 12.06.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_PLKO - Kompetencje.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Kompetencje - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
exec('init','poc');

_buf_komp:=exec('save_fml_type','#field',ZZ_KOMP,'BLANK');
_buf_kompk:=exec('save_fml_type','#field',ZZ_KOMPK,'BLANK');
_buf_komps:=exec('save_fml_type','#field',ZZ_KOMPS,'BLANK');
_buf_kompz:=exec('save_fml_type','#field',ZZ_KOMPZ,'BLANK');

_acr:=exec('setup_wnd','!zws_par_plko');
{? exec('chk_role','#b__box',OPERATOR.USER,'ZWS_PAR_PLKO')
|| ZZ_KOMP.dnd_sel('CFG',,'records.ZZ_KOMP',"exec('zz_komp_dnd','!zws_par_plko',dnd_info('dropped_records'))")
?};

_hist:=ZZ_POM.ZZ_HIST;
ZZ_HIST.cntx_psh();

ZZ_HIST.win_sel(_acr);
ZZ_HIST.select();

ZZ_HIST.cntx_pop();
ZZ_POM.ZZ_HIST:=_hist;

exec('restore_fml_type','#field',ZZ_KOMP,'BLANK',_buf_komp);
exec('restore_fml_type','#field',ZZ_KOMPK,'BLANK',_buf_kompk);
exec('restore_fml_type','#field',ZZ_KOMPS,'BLANK',_buf_komps);
exec('restore_fml_type','#field',ZZ_KOMPZ,'BLANK',_buf_kompz);

ZZ_KOMP.dnd_sel('CFG',,'records.ZZ_KOMP',"");
ZZ_KOMPK.actions_grayed('CFG');
ZZ_KOMPS.actions_grayed('CFG');
~~


\setup_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Tworzy okno panelu do obsługi listy kompetencji.
::   WE:
::   WY: akronim okienka tabeli ZZ_HIST
::----------------------------------------------------------------------------------------------------------------------
_upr:=exec('chk_role','#b__box',OPERATOR.USER,'ZWS_PAR_PLKO');
{? _upr<>0
|| _aid:='';
   {? ZZ_PAR.WAR_KOMW<>'T' | ZZ_PAR.WAR_KOMN<>'T'
   || _aid+='D:D'
   ?};
   {? ZZ_PAR.WAR_KOMW<>'T' & ZZ_PAR.WAR_KOMN<>'T'
   || _aid:='P'+_aid
   ?};
   ZZ_KOMPK.actions_grayed('CFG',_aid);
   ZZ_KOMPS.actions_grayed('CFG',_aid)
?};

_acr:='DEF%1%2'[ZZ_PAR.WAR_KOMP,$_upr];

{? (_wnd:=__WND.SEL.get(ZZ_HIST,_acr))=''
|| _wnd:=ZZ_HIST.grp_make('Kompetencje'@,
:     po wyświetleniu
      "  _link:=$'ZZ_KOMP.ZZ_DOK';
         _komp:=$'ZZ_KOMP.ref()';
         ZZ_KOMP.fld_fml('ZZ_HIST','BLANK',$'ZZ_HIST.ref()');
         ZZ_KOMP.fld_fml('ZZ_KOMP','BLANK',$
            '{? ~grp_empty(ZZ_KOMP,\\\'CFG\\\') '
            '|| ZZ_KOMP.cntx_psh(); '
            '   ZZ_KOMP.get();'
            '   _komp:=ZZ_KOMP.ZZ_KOMP; '
            '   ZZ_KOMP.cntx_pop(); '
            '   _komp '
            '?}'
         );
         ZZ_KOMPK.fld_fml('ZZ_LINK','BLANK',_link);
         ZZ_KOMPK.fld_fml('ZZ_KOMP','BLANK',_komp);
         ZZ_KOMPS.fld_fml('ZZ_LINK','BLANK',_link);
         ZZ_KOMPS.fld_fml('ZZ_KOMP','BLANK',_komp);
         ZZ_KOMPZ.fld_fml('ZZ_LINK','BLANK',_link);
         ZZ_KOMPZ.fld_fml('ZZ_KOMP','BLANK',_komp);

         ZZ_KOMP.index('NAZWA');
         ZZ_KOMPK.index('WARTOSC');
         ZZ_KOMPS.index('WARTOSC');
         exec('zz_pom_psh','phr_widok','');

         ZZ_HIST.index('DATA');
         ZZ_HIST.prefix(exec('ref_firma','ustawienia'));
         1
      ",
:     identyfikator
      '',
:     położenie
      0,0,
:     zamknięcie
      "  exec('zz_pom_pop','phr_widok');
         1
      "
   );
   __WND.SEL.put(ZZ_HIST,_acr,_wnd);

:  kompetencje
   ZZ_HIST.grp_sel(_wnd,ZZ_KOMP,'CFG',,
:     po odświeżeniu
      {? ZZ_PAR.WAR_KOMP='T'
      || {? _upr<>0
         || "  cur_tab(1,1).actions(cur_win(1,1),,,1);
               grp_disp(ZZ_KOMPS,'CFG',1);
               grp_disp(ZZ_KOMPK,'CFG',1);
               grp_disp(ZZ_KOMZB,__WND.SEL.get(ZZ_KOMZB,'CFG'),1)
            "
         || "  cur_tab(1,1).actions(cur_win(1,1),,,1);
               grp_disp(ZZ_KOMPS,'WEO',1);
               grp_disp(ZZ_KOMPK,'WEO',1);
               grp_disp(ZZ_KOMZB,__WND.SEL.get(ZZ_KOMZB,'WEO'),1)
            "
         ?}
      || ""
      ?},
:     położenie i wysokość
      ,,10,
:     przed obsługą
      "  {? _a
         || ZZ_POM.KREATORY:='#ZWS_PAR_PLKO#ZZ_KOMP#CFG#'
         ?};
         ZZ_KOMP.prefix(ZZ_HIST.ref())
      ",
:     po obsłudze
      "  {? _a
         || ZZ_POM.KREATORY:=''
         ?}
      ",
:     utrwalenie, aktywacja, wypełnienie
      0,0,'maximized_with_title'
   );

:  czy dopuszczalna zmiana przepisanych wartości?
:  jeśli nie, to zakończ tworzenie okienka
   {? ZZ_PAR.WAR_KOMP<>'T'
   || return(_wnd)
   ?};

   ZZ_HIST.grp_splt(_wnd,,'horizontal','bottom');

:  skala punktowa
   ZZ_HIST.grp_sel(_wnd,ZZ_KOMPS,{? _upr<>0 || 'CFG' || 'WEO' ?},'Skala punktowa'@,
:     po odświeżeniu
      "",
:     położenie i wysokość
      ,,7,
:     przed obsługą
      "  exec('zz_pom_psh','phr_widok','ZWS_PAR_PLKO');
         ZZ_KOMPS.prefix(ref_name(ZZ_KOMP.ZZ_DOK),ZZ_KOMP.ZZ_DOK,ZZ_KOMP.ref());
         {? grp_empty(ZZ_KOMP,'CFG')
         || '#disable'
         ?}
      ",
:     po obsłudze
      "exec('zz_pom_pop','phr_widok')",
:     utrwalenie, aktywacja, wypełnienie
      0,0,'maximized'
   );

:  wartości kodowane
   ZZ_HIST.grp_sel(_wnd,ZZ_KOMPK,{? _upr<>0 || 'CFG' || 'WEO' ?},'Wartości kodowane'@,
:     po odświeżeniu
      "",
:     położenie i wysokość
      ,,7,
:     przed obsługą
      "  exec('zz_pom_psh','phr_widok','ZWS_PAR_PLKO');
         ZZ_KOMPK.prefix(ref_name(ZZ_KOMP.ZZ_DOK),ZZ_KOMP.ZZ_DOK,ZZ_KOMP.ref());
         {? grp_empty(ZZ_KOMP,'CFG')
         || '#disable'
         ?}
      ",
:     po obsłudze
      "exec('zz_pom_pop','phr_widok')",
:     utrwalenie, aktywacja, wypełnienie
      0,0,'maximized'
   );

:  wartości bezpośrednie
   ZZ_HIST.grp_sel(_wnd,ZZ_KOMZB,__WND.SEL.get(ZZ_KOMZB,{? _upr<>0 || 'CFG' || 'WEO' ?}),'Wartości bezpośrednie'@,
:     po odświeżeniu
      "",
:     położenie i wysokość
      ,,7,
:     przed obsługą
      "  exec('zz_pom_psh','phr_widok','ZWS_PAR_PLKO');
         exec('zz_komzb_fill','phr_widok',ZZ_KOMP.ZZ_DOK,ZZ_KOMP.ref());
         {? grp_empty(ZZ_KOMP,'CFG')
         || '#disable'
         ?}
      ",
:     po obsłudze
      "exec('zz_pom_pop','phr_widok')",
:     utrwalenie, aktywacja, wypełnienie
      0,0,'maximized'
   )
?};

_wnd


\zz_komp_xxx_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Akcja "okienko przed" wszystkich okienek tabeli ZZ_KOMP.
::   WE: [_a] [NUMBER] - Okno hierarchiczne [0*/1]?
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('setup_icon','phr_widok','NAZWA',,var_pres('_a')=type_of(0) & _a);
exec('zz_pom_psh','phr_widok','ZWS_PAR_PLKO');
exec('metodyka_ukryj','phr_widok');

ZZ_KOMP.win_edit('RED');

1


\zz_komp_xxx_oa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Akcja "okienko po" wszystkich okienek tabeli ZZ_KOMP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab()<>ZZ_HIST
|| exec('metodyka_ukryj','phr_widok')
?};
exec('zz_pom_pop','phr_widok')


\zz_komp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Akcja "rekord po" okienek tabeli ZZ_KOMP.
::   WE:
::   WY: zgodna ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
exec('zz_komp_chk','phr_zz_tab',-menu_txt()='popraw')


\zz_komp_dnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Obsługa akcji "przeciągnij i upuść" w drzewku kompetencji.
::   WE: _a - alias tabeli zawierającej informacje o przeciąganych rekordach
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZZ_KOMP.cntx_psh();
ZZ_KOMP.prefix();
{? _a.first()
|| {!
   |? {? ZZ_KOMP.seek(_a.REF,)
      || ZZ_KOMP.ZZ_KOMP:=#dnd_info('dest_record');
         {? __CHK.index(ZZ_KOMP,1)=''
         || ZZ_KOMP.put()
         ?}
      ?};
      _a.next()
   !}
?};
ZZ_KOMP.cntx_pop();
ZZ_KOMP.get()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 9cca8e575c15dca4efe8aa54f66e99d332ea4fcee3e1bceaac127a7928842e79319a93cc172e2c2157cfc57f0e356f913667cac3162d385e1d7ce6827f48adf1561df15b957387ec5242bc7789771c5719921025d0b06bf9cb8deb47e3108e42078c306579f6483fb55c01e726f9a9f5b73dcabe07a6617d9590a88275b2203f

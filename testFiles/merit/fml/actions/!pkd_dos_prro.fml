:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_dos_prro.fml
:: Utworzony: 15.04.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_DOS_PRRO - Rejestracja członków rodziny.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja członków rodziny - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::# kind=WE,   symbol=OSOBA,     type=_OSOBA,   name=Wskazanie osoby,   required=T, keyref=T

_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.OSOBA);
{? _uidref=''
|| _result:=exec('error','!pkd_dos_prro')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_dos_prro',_in.OSOBA);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja członków rodziny - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','osoba',params_get().mp);

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj członków rodziny: %1 %2: Paszport - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT]
   |? +_tab.PESEL
   || 'Zarejestruj członków rodziny: %1 %2: PESEL - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL]
   || 'Zarejestruj członków rodziny: %1 %2: Data urodzenia - %3'@@[_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA]
   ?}
|| 'Zarejestruj członków rodziny'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie członków rodziny niemożliwe.\nNie znaleziono osoby.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie osoby.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
OSOBA.cntx_psh();
OSOBA.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & OSOBA.seek(_a)
|| RD.cntx_psh();
   RD.index('_RODZINA');
   RD.prefix(OSOBA.ref());

   RD.win_fml('WER',,'NA',,'ICON_BEFORE',"exec('x_zalacz_icon','zalacz')");
   RD.win_sel('WERI');
   RD.win_edit('RED');
   RD.win_patt('RED');

   _ret:=RD.select();
   RD.cntx_pop()
|| _ret:=exec('error','!pkd_dos_prro')
?};
OSOBA.cntx_pop();
_ret


\rd_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed modyfikacją rekordu. Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: Porządki - zerowanie pól, które są "niedostępne".
{? RD.STPK().KOD<>'11'
|| exec('blank','#field',RD,,'DZPRZYSP')
?};

{? RD.DZPRZYSP<>'T'
|| exec('blank','#field',RD,,'DTPRZYSP','ODROS')
?};
{? RD.ODROS<>'T'
|| exec('blank','#field',RD,,'ODROSDO')
?};
{? RD.STNP=null()
|| exec('blank','#field',RD,,'STNP_OD','STNP_DO')
?};
{? RD.ZP='N'
|| exec('blank','#field',RD,,'ZP_DO','ZP_DO')
?};
{? RD.KW<=0
|| exec('blank','#field',RD,,'KW_OD','KW_DO')
?};
{? RD.ADRINNY<>'T'
|| exec('blank','#field',RD,,'KRAJ','KOD','POCZTA','MIASTO','GMINA','ULICA','DOM','LOKAL')
?};

1


\rd_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_modb','!pkd_dos_prro')


\rd_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_modb','!pkd_dos_prro')


\rd_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',ZALACZ,'NAG',RD.uidref())


\rd_im_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.IM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RD.PLEC:={? -(fld()+1)='a' || 'K' || 'M' ?};
1


\rd_ojciec_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wartość początkowa pola RD.OJCIEC.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? RD.OSOBA().PLEC='M'
|| OSOBA.PIERWSZE
|| ''
?}


\rd_ojciecn_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wartość początkowa pola RD.OJCIECN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? RD.OSOBA().PLEC='M'
|| OSOBA.NAZWISKO
|| ''
?}


\rd_matka_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wartość początkowa pola RD.MATKA.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? RD.OSOBA().PLEC='K'
|| OSOBA.PIERWSZE
|| ''
?}


\rd_pesel_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [2006]
:: OPIS: Przed redagowaniem pola RD.PESEL.
::  OLD: \perdpr/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_dt:=RD.DA;
{? _dt<>date(0,0,0)
|| {? +|fld()<>11 || fld(exec('pesel_dt','#id',_dt)); set_cpos(7) ?}
?};
1


\rd_pesel_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [2006]
:: OPIS: Po redagowaniu pola RD.PESEL.
::  OLD: \perdpo/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_da:=RD.DA;
_d0:=date(0,0,0);
{? _da<>_d0 & RD.PESEL=exec('pesel_dt','#id',_da)
|| fld('');
   return(1)
?};

_kom:=
   {? _da=_d0
   || exec('pesel_ok','#id',fld())
   || exec('pesel_ok','#id',fld(),_da)
   ?};
{? +_kom=1 & type_of(_dt:=exec('pesel_dt','#id',fld()))=type_of(_d0) & _da=_d0
|| RD.DA:=_dt;
   RD.PLEC:=_kom
?};
{? +_kom>1
|| return(FUN.ask('Niepoprawny numer PESEL.\n'@+_kom+'\nZatwierdzić informację?'@))
?};
1


\rd_da_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [2006]
:: OPIS: Przed redagowaniem pola RD.DA.
::  OLD: \urrdpr/osoba.fml
::----------------------------------------------------------------------------------------------------------------------
_d0:=date(0,0,0);
{? fld()=_d0
|| _dt:=exec('pesel_dt','#id',RD.PESEL);
   {? type_of(_dt)=type_of(_d0) || fld(_dt) ?}
?};
1


\rd_stpk_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wartość początkowa pola RD.STPK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=null();
S_ZUS.cntx_psh();
S_ZUS.index('S_ZUS');
S_ZUS.prefix('R','11');
{? S_ZUS.first()
|| _ret:=S_ZUS.ref()
?};
S_ZUS.cntx_pop();
_ret


\rd_stpk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pol RD.STPK w oknie RED tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? RD.STPK().KOD='11'
|| {? RD.OSOBA().PLEC='M' & RD.OJCIECN='' & RD.OJCIEC=''
   || RD.OJCIECN:=RD.OSOBA().NAZWISKO;
      RD.OJCIEC:=RD.OSOBA().PIERWSZE
   ?}
|| {? RD.OSOBA().PLEC='M' & RD.OJCIECN=RD.OSOBA().NAZWISKO & RD.OJCIEC=RD.OSOBA().PIERWSZE
   || RD.OJCIECN:=RD.OJCIEC:=''
   ?}
?};
exec('rd_efld_opt','osoba')


\rd_kw_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.KW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba')


\rw_kwx_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pól: RD.KW_OD, RD.KW_DO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RD.KW>0


\rd_zp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.ZP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba')


\rw_zpx_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pól: RD.ZP_OD, RD.ZP_DO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RD.ZP='T'


\rd_dowod_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.DOWOD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
fld()='' | exec('dowod_ok','#id',fld())>0


\rd_nip_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.NIP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
fld()='' | exec('nip_ok','#id',fld())


\rd_tpid_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.TPID.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba')


\rd_stnp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.STNP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba')


\rd_dzprzysp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pola RD.DZPRZYSP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RD.STPK().KOD='11'


\rd_dzprzysp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.DZPRZYSP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba')


\rd_dzpx_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pól: RD.DTPRZYSP, RD.ODROS.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RD.DZPRZYSP='T'


\rd_odros_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.ODROS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba')


\rd_odrosdo_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pola RD.ODROSDO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RD.ODROS='T'


\rd_adrinny_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.ADRINNY.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? RD.ADRINNY='T'
|| RD.WSPGOSP:='N'
?};
exec('rd_efld_opt','osoba')


\rd_adresx_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pól: RD.KRAJ, RD.KOD, RD.POCZTA, RD.MIASTO, RD.GMINA, RD.ULICA, RD.DOM, RD.LOKAL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RD.ADRINNY='T'


\rd_kraj_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RD.KRAJ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba');
exec('rd_teryt_wyszarz','osoba')


\rd_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RD.blank();
exec('rd_efld_opt','osoba','*',RD,'RED')


\rd_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba','*',RD,'RED')


\rd_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli RD z doklejonym oknem redagowania tabeli OSOBA);
::          - w ramach obszaru roboczego (okno wertowania tabeli RD jako składowa okna grupowego tabeli OSOBA).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=RD
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\rd_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie WER tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_efld_opt','osoba','*',RD,'RED');
RD.display()


\rd_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po": Weryfikacja informacji o członku rodziny pracownika - rekord po
::   WE:
::   WY:
::  OLD: \rdrekpo/kali.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=__CHK.record(RD,,'NA','IM','DA','STPK','STNP'))<>''
|| return(_chk)
?};

{? (_kod:=RD.TPID().KOD)<>''
|| _fld:=
      {? _kod='1' || 'DOWOD'
      |?_kod='2' || 'PASZPORT'
      |?_kod='N' || 'NIP'
      |?_kod='P' || 'PESEL'
      || ''
      ?};
   {? _fld<>'' & (_chk:=__CHK.record(RD,,_fld))<>''
   || return(_chk)
   ?}
?};

{? RD.ZCZA='N' & (_chk:=__CHK.record(RD,,'UZOD'))<>''
|| return(_chk)
?};

_d0:=date(0,0,0);

{? RD.DTZGON<>_d0 & RD.DTZGON<RD.DA
|| FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'DTZGON'),MS.name(RD,'DA')]);
   return('DTZGON')
?};

{? RD.UZOD<>_d0
|| {? RD.DA<>_d0 & RD.UZOD<RD.DA
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'UZOD'),MS.name(RD,'DA')]);
      return('UZOD')
   |? RD.DTZGON<>_d0 & RD.DTZGON<RD.UZOD
   || FUN.emsg('"%1" nie może być późniejsza niż "%2".'@ [MS.name(RD,'UZOD'),MS.name(RD,'DTZGON')]);
      return('UZOD')
   |? RD.UZDO<>_d0 & RD.UZDO<RD.UZOD
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'UZDO'),MS.name(RD,'UZOD')]);
      return('UZOD')
   ?};
   P.cntx_psh();
   P.index('OSOBA');
:  przeglądanie danych we wszystkich firmach
   P.prefix(RD.OSOBA);
   {? P.first()
   || _ok:=0;
      {!
      |? _ok+=P.DZA<=RD.UZOD & (P.DZ=_d0 | RD.UZOD<=P.DZ);
         P.next()
      !}
   || _ok:=1
   ?};
   P.cntx_pop();
   {? ~_ok &
      ~FUN.ask('"%1" powinna przypadać na okres zatrudnienia pracownika.\nCzy akceptujesz dane?'@ [MS.name(RD,'UZOD')])
   || return('UZOD')
   ?}
?};

{? RD.STNP<>null()
|| {? RD.STNP_OD<>_d0 & RD.STNP_OD<RD.DA
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'STNP_OD'),MS.name(RD,'DA')]);
      return('STNP_OD')
   |? RD.STNP_OD<>_d0 & RD.STNP_DO<>_d0 & RD.STNP_DO<RD.STNP_OD
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'STNP_DO'),MS.name(RD,'STNP_OD')]);
      return('STNP_DO')
   ?}
?};

{? RD.STPK().KOD='11' & RD.DZPRZYSP='T'
|| {? (_chk:=__CHK.record(RD,,'DTPRZYSP'))<>''
   || return(_chk)
   |? RD.DTPRZYSP<RD.DA
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'DZPRZYSP'),MS.name(RD,'DA')]);
      return('DTPRZYSP')
   |? RD.ODROS='T'
   || {? (_chk:=__CHK.record(RD,,'ODROSDO'))<>''
      || return(_chk)
      |? RD.ODROSDO<RD.DA
      || FUN.emsg('"%1" nie może być wcześniejszy niż "%2".'@ [MS.name(RD,'ODROSDO'),MS.name(RD,'DA')]);
         return('ODROSDO')
      ?}
   ?}
?};

{? RD.ADRINNY='T'
|| {? (_chk:=__CHK.record(RD,,'KRAJ'))<>''
   || return(_chk)
   |? ~-RD.KRAJ().KODISO='PL'
   || _chk:=__CHK.record(RD,,'KOD','POCZTA','MIASTO','GMINA','DOM')
   || _chk:=__CHK.record(RD,,'KOD','MIASTO','DOM')
   ?};
   {? _chk<>''
   || return(_chk)
   ?}
?};

{? RD.ZP='T'
|| {? RD.ZP_OD<>_d0 & RD.ZP_OD<RD.DA
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'ZP_OD'),MS.name(RD,'DA')]);
      return('ZP_OD')
   |? RD.ZP_DO<>_d0 & RD.ZP_DO<RD.DA
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'ZP_DO'),MS.name(RD,'DA')]);
      return('ZP_DO')
   |? RD.ZP_OD<>_d0 & RD.ZP_DO<>_d0 & RD.ZP_DO<RD.ZP_OD
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'ZP_DO'),MS.name(RD,'ZP_OD')]);
      return('ZP_DO')
   ?}
?};

{? RD.KW<0
|| return(__CHK.err_fld(RD,'KW',1,'Wartość nie może być ujemna.'@))
|? RD.KW>0
|| {? RD.KW_OD<>_d0 & RD.KW_OD<RD.DA
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'KW_OD'),MS.name(RD,'DA')]);
      return('KW_OD')
   |? RD.KW_DO<>_d0 & RD.KW_DO<RD.DA
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'KW_DO'),MS.name(RD,'DA')]);
      return('KW_DO')
   |? RD.KW_OD<>_d0 & RD.KW_DO<>_d0 & RD.KW_DO<RD.KW_OD
   || FUN.emsg('"%1" nie może być wcześniejsza niż "%2".'@ [MS.name(RD,'KW_DO'),MS.name(RD,'KW_OD')]);
      return('KW_DO')
   ?}
?};

{? RD.win_edit('?')='RED'
|| return(exec('rd_teryt_sprawdz','!pkd_dos_prro'))
?};

''


\rd_zalaczniki_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.42]
:: OPIS: Obsługa akcji "Załączniki - przed" w oknach tabeli RD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('show_zalacz','zalacz','OSOBA','RD',exec('chk_role','#b__box',OPERATOR.USER,'PKD_DOS_PRRO'))


\rd_gmi_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: przycisk pola RD.GMINA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_gmi_wybierz','teryt','RD','GMINA')
?}


\rd_msc_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: przycisk pola RD.MIASTO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_msc_wybierz','teryt','RD','MIASTO','GMINA')
?}


\rd_ul_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: przycisk pola RD.UL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T'
|| exec('ter_uli_wybierz','teryt','RD','ULICA','MIASTO','GMINA')
?}


\rd_teryt_sprawdz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: wesos [20.14]
:: OPIS: sprawdzanie zgodności z TERYT danych adresowych tabeli RD
::   WE:
::   WY: 1 - zgodne 0 - niezgodne
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.TER_AKT='T' & XINFO.TER_KNT='T' & ~exec('rd_zagr','osoba')
|| _par:=exec('ter_sprawdz_a','teryt');
   _par.PYT_KOM:='P';
   _par.TAB:='RD';
   _par.ULI_P:='ULICA';
   _par.MSC_P:='MIASTO';
   _par.GMI_P:='GMINA';
   _par_ULI_ND:=1;
   _wyn:=exec('ter_sprawdz','teryt',_par);
   {? ~_wyn.DALEJ || _wyn.POLE || 1 ?}
|| 1
?}


\rd_stnp_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Wartość początkowa pola RD.STNP.
::UWAGA: Formuła przeniesiona do osoba.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_stnp_bl','osoba')


\rd_zcza_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Po redagowaniu pola RD.ZCZA.
::UWAGA: Formuła przeniesiona do osoba.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rd_zcza_ae','osoba')


:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 740b28093e74831ddeaa86c006f16838397bee38ea19b999c9c9b659da573617c7c28791475d3ae5f01ec8a1ee179a7523a95488d962dd329f82cafdac137805f3a5301dfbc6ea8dd0ccda71ebd50cf1239210148fc15994670e6e0019f7f27cbbbaa3dabc6810685b3b3bb06de6894610ad319500418b3ce0a1f4edcbfc3f4c

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_mat_pmsz.fml
:: Utworzony: 14.11.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PSZ_MAT_PMSZ - Matryca szkoleń.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Matryca szkoleń - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
_wnd:=exec('create_wnd','!psz_mat_pmsz');

_mk_ctx:="
   _ctx:=obj_new('key','pos','fml');
   _ctx.key:='SZK_STN';
   _ctx.pos:=null;
   _ctx.fml:=SZK_STN.fld_fml(_a,'*BLANK');
   _ctx
";
_ctx:=obj_new('sto','grp');
_ctx.sto:=_mk_ctx('STO');
_ctx.grp:=_mk_ctx('STN_GR');

params_set('ctx',_ctx);

UD_DEF.cntx_psh();
UD_DEF.win_sel(_wnd);
UD_DEF.select();
UD_DEF.cntx_pop();
UD_DEF.win_del(_wnd);

SZK_STN.fld_fml('STO','BLANK',_ctx.sto.fml);
SZK_STN.fld_fml('STN_GR','BLANK',_ctx.grp.fml);

~~


\create_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Tworzy okienko czynności.
::   WE:
::   WY: akronim okienka tymczasowego
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';

_wnd:=UD_DEF.grp_make('Matryca szkoleń'@,
:  po wyświetleniu
   "params_exec('adm_bf','!psz_mat_pmsz')",
:  identyfikator
   'psz_mat_pmsz',
:  położenie
   0,0,
:  zamknięcie
   "params_exec('adm_oc','!psz_mat_pmsz')"
);

: jednostki organizacyjne według domyślnego schematu
UD_DEF.grp_sel(_wnd,UD_DEF,'NAW_MID','Stanowiska w jednostkach organizacyjnych'@,
:  po odświeżeniu
   "params_exec('adm_def_ar','!psz_mat_pmsz')",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);
UD_DEF.win_sopt('NAW_MID','select_record_checkbox=0');
UD_DEF.tab_splt(_wnd,,'vertical','top_right',',25%');
: stanowiska w jednostce organizacyjnej
UD_DEF.grp_sel(_wnd,STO,'SEL',,
:  po odświeżeniu
   "params_exec('adm_sto_ar','!psz_mat_pmsz')",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "params_exec('adm_sto_bs','!psz_mat_pmsz',_a)",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

UD_DEF.tab_splt(_wnd,,'horizontal','bottom');
UD_DEF.grp_sel(_wnd,SZK_STN,'WER',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "params_exec('adm_sto_szk_bs','!psz_mat_pmsz',_a)",
:  po obsłudze
   "params_exec('adm_sto_szk_as','!psz_mat_pmsz',_a)",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

: grupy stanowisk
UD_DEF.grp_sel(_wnd,STN_GR,'SEL','Grupy stanowisk'@,
:  po odświeżeniu
   "params_exec('adm_grp_ar','!psz_mat_pmsz');
    params_exec('adm_grp_szk_bs','!psz_mat_pmsz',1)",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

UD_DEF.tab_splt(_wnd,,'horizontal','bottom');
UD_DEF.grp_sel(_wnd,SZK_STN,'WER',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "params_exec('adm_grp_szk_bs','!psz_mat_pmsz',_a)",
:  po obsłudze
   "params_exec('adm_grp_szk_as','!psz_mat_pmsz',_a)",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

_wnd


\adm_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przed wypełnieniem okienka.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
UD_TYP.cntx_psh();
UD_SCH.cntx_psh();
UD_SKL.cntx_psh();
UD_DEF.cntx_psh();

_typ:='PODZORG';
_sch:=exec('domyslny','schemat',_typ);
_skl:=exec('ud_skl_firma','schemat',_typ);
exec('ud_def_root','schemat','NAW_MID',_sch,_skl);
UD_DEF.index('SYMBOL');
UD_DEF.f_clear();
UD_DEF.prefix(_sch);

STO.cntx_psh();
STO.index('ST');

STN_GR.cntx_psh();
STN_GR.f_clear();
STN_GR.index('GRUPA');

SZK_STN.cntx_psh();
SZK_STN.f_clear();
SZK_STN.index('SZK_STN');

1


\adm_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przy zamykaniu okienka.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
SZK_STN.cntx_pop();
SZK_STN.actions('WER',,,1);

STO.cntx_pop();
STO.f_clear();

STN_GR.cntx_pop();
STN_GR.f_clear();

UD_DEF.cntx_pop();
UD_SKL.cntx_pop();
UD_SCH.cntx_pop();
UD_TYP.cntx_pop();

1


\adm_def_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Po odświeżeniu okienka NAW_MID tabeli UD_DEF.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(STO,'SEL',1,1);
~~


\adm_sto_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Po odświeżeniu okienka SEL tabeli STO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(SZK_STN,'WER',1,1);
~~


\adm_sto_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przed obsługą okienka SEL tabeli STO.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(UD_DEF,'NAW_MID')=1
|| return('#disable')
?};

STO.clear();
STO.f_set(
   'STN(ST)',
   'join UD_SKL using(STO.UD_SKL,UD_SKL.REFERENCE)',
   'UD_SKL.SCIEZKA like \':_a%\'',
   UD_DEF.UD_SKL().SCIEZKA
);

~~


\adm_sto_szk_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przed obsługą okienka WER tabeli SZK_STN.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(STO,'SEL')=1
|| return('#disable')
?};

_ctx:=params_get().ctx.sto;
_sto:="STO.ref()";
_grp:="null";

SZK_STN.index(_ctx.key);
SZK_STN.prefix(_sto(),_grp());

{? _a
|| SZK_STN.actions('WER',,,1);
   SZK_STN.fld_fml('STO','BLANK',_sto);
   SZK_STN.fld_fml('STN_GR','BLANK',_grp);
   SZK_STN.seek(_ctx.pos)
?};

~~


\adm_sto_szk_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Po obsłudze okienka WER tabeli SZK_STN.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _ctx:=params_get().ctx.sto;
   _ctx.key:=SZK_STN.index('?');
   _ctx.pos:=SZK_STN.ref()
?};
~~


\adm_grp_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Po odświeżeniu okienka SEL tabeli STN_GR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(SZK_STN,'WER',1,1);
~~


\adm_grp_szk_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Przed obsługą okienka WER tabeli SZK_STN.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(STN_GR,'SEL')=1
|| return('#disable')
?};

_ctx:=params_get().ctx.grp;
_sto:="null";
_grp:="STN_GR.ref()";

SZK_STN.index(_ctx.key);
SZK_STN.prefix(_sto(),_grp());

{? _a
|| SZK_STN.actions('WER','A:A',,1);
   SZK_STN.fld_fml('STO','BLANK',_sto);
   SZK_STN.fld_fml('STN_GR','BLANK',_grp);
   SZK_STN.seek(_ctx.pos)
?};

~~


\adm_grp_szk_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Po obsłudze okienka WER tabeli SZK_STN.
::   WE: _a [INTEGER] - tryb odrysowania okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _ctx:=params_get().ctx.grp;
   _ctx.key:=SZK_STN.index('?');
   _ctx.pos:=SZK_STN.ref()
?};
~~


\szk_stn_xxx_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Okienko przed" okienek tabeli SZK_STN.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_psh','phr_widok','PSZ_MAT_PMSZ');
exec('setup_icon','phr_widok','SZK_WZO','OPIS');

SZK_STN.win_edit('RED');
SZK_STN.win_patt('WZO');

SZK_WZO.cntx_psh();
SZK_WZO.win_dict('WER');

1


\szk_stn_xxx_oa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Okienko po" okienek tabeli SZK_STN.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_pop','phr_widok');

SZK_WZO.cntx_pop();

1


\szk_stn_wazne_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Po redakcji pola WAZNE wiersza tabeli SZK_STN.
::   WE:
::   WY: wynik testu poprawności
::----------------------------------------------------------------------------------------------------------------------
{? SZK_STN.WAZNE=SZK_STN.SZK_WZO().WAZNE
|| return(1)

|? SZK_STN.WAZNE=0
|| SZK_STN.WAZNE:=SZK_STN.SZK_WZO().WAZNE;
   return(1)
?};

{? FUN.ask(
      'Okres ważności szkolenia (%1) różni się od okresu ważności wzorca szkolenia (%2).\n'
      'Czy aktualizować okres ważności na podstawie wzorca?'@[$SZK_STN.WAZNE,$SZK_WZO.WAZNE]
   )
|| SZK_STN.WAZNE:=SZK_STN.SZK_WZO().WAZNE
?};
1


\szk_stn_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: "Rekord po" okienek tabeli SZK_STN.
::   WE:
::   WY: wynik testu poprawności
::----------------------------------------------------------------------------------------------------------------------
exec('szk_stn_chk','phr_sz_tab',-menu_txt()='popraw')


\szk_stn_arkusz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Obsługa akcji "Arkusz" w oknach tabeli SZK_STN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_col:='';
SZK_WZO.cntx_psh();
SZK_WZO.index('OPIS');
SZK_WZO.prefix();
_loop:=SZK_WZO.first();
{!
|? _loop
|! _txt:=SZK_WZO.OPIS;
   _col+=',\'WZO%1\',\'STRING[1]\',\'%2\''[$(#SZK_WZO.ref()),{? +_txt>60 || (55+_txt)+'(...)' || _txt ?}];
   _loop:=SZK_WZO.next()
!};
SZK_WZO.cntx_pop();

_TAB:=($(
   'tab_tmp(1,'
   '\'ID\',\'STRING[16]\',\'%1\','
   '\'WYDZ\',\'STRING[16]\',\'%2\','
   '\'STN\',\'STRING[80]\',\'%3\''
   '%4'
   ')'['Id'@,'Jednostka organizacyjna'@,'Stanowisko'@,_col]
))();

UD_SKL.cntx_psh();
STN.cntx_psh();
STO.cntx_psh();
SZK_STN.cntx_psh();
SZK_STN.index('SZK_STN');
SZK_STN.prefix();
_loop:=SZK_STN.first();
{!
|? _loop
|! {? SZK_STN.STO<>null
   || _mod:=0;
      SZK_STN.STO();
      _id:='%1%2'[$STO.UD_SKL+8,$STO.STN+8];
      {? _TAB.find_key(_id)
      || _mod:=1
      || _TAB.blank(1);
         _TAB.ID:=_id;
         _TAB.WYDZ:=STO.UD_SKL().SYMBOL;
         _TAB.STN:=STO.STN().ST
      ?};
      _TAB[3+#SZK_STN.SZK_WZO]:=SZK_STN.W;
      {? _mod
      || _TAB.put()
      || _TAB.add()
      ?}
   ?};
   _loop:=SZK_STN.next()
!};
SZK_STN.cntx_pop();
STO.cntx_pop();
STN.cntx_pop();
UD_SKL.cntx_pop();

_TAB.index(_TAB.ndx_tmp('Jednostka, Stanowisko'@,0,'WYDZ',,,'STN',,));
_wnd:=_TAB.mk_sel('Matryca szkoleń'@,'P',0,'mat_pmsz_pivot',,,,,'U',,,,,'html_maximized');
_TAB.win_fld(_wnd,,'WYDZ',,,-16);
_TAB.win_fld(_wnd,,'STN',,,-30);
{! _col:=4.._TAB.fld_num()
|! _TAB.win_fld(_wnd,,_TAB.fld_acr(_col),,,-2)
!};
_TAB.win_act(_wnd,,'Kolejność');
_TAB.win_sel(_wnd);
_TAB.select();

~~


\stn_gr_stanowiska_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Obsługa akcji "Stanowiska" w oknach tabeli STN_GR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
STN_GRP.cntx_psh();
STN_GRP.index('STN_GRP');
STN_GRP.prefix(STN_GR.ref());
STN_GRP.win_sel('SEL');
STN_GRP.select();
STN_GRP.cntx_pop();
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:53 895a61b44ed2b0e9c3ba9ef3f1085fa4d9d932ef0310a666f893ee46adeebe76ab44e181a7be2ff497c413e728863b23b90c1343adbc601ffcbb66275345507db1bbe9afb3e0e0f31b64b79b88c3b1d7846b0f1b6201ec26693fe4c08d54d83640ea6e94c57b0e4f73ef22e680e302206f587bf21507b4409ed56b2609cac029

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_rap_zzup.fml
:: Utworzony: 29.02.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_RAP_ZZUP - Uprawnienia do danych raportów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uprawnienia do danych raportów - główna formuła czynności CTR_RAP_ZZUP
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego - Wyswietla role użytkownikow dla Controllingu
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::  OLD: \k_role/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init_win','ctr_podz');
UNPAR.P1_BV:='';
_win:=K_ROLE.win_sel('?');
K_ROLE.win_sel('WER');
K_ROLE.hdr_sel();
K_ROLE.hdr_sel(' w raportach');
K_ROLE.win_sel(_win);
K_ROLE.index('NAZWA'); K_ROLE.prefix(REF.S_FIRMA,BPMN.SYM_DOM);
_b.grp_sel(_c,K_ROLE,'WER',_a,"exec('arf_k_role','ctr_podz')",,,,,,,,'maximized_with_title');
task_attach('CTR_RAP_ZZUP');
_b.tab_splt(_c,,'horizontal','panel1',10);
_b.grp_sel(_c,K_ROLE_U,'WER',,,,,,,,,,'maximized_with_title');
_b.tab_splt(_c,'panel1','vertical','panel2');
_b.grp_sel(_c,USERS,'SLO2_1',,"
   {? USERS.sel_size()=0
   || VAR_DEL.delete('UserRap');
      exec('bf_k_raporty','ctr',0)
   ?}
",,,,,"
   VAR_DEL.delete('UsrRole');
   {? USERS.sel_size()
   || UsrRole:=USERS.sel_aget()
   ?};
   1
",,,'maximized_with_title');
~~


\k_role_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Aktualizuje role na serwerze OLAP
::  OLD: \k_role_akt/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K_ROLE.sel_size()
|| TTROLE.NAZWA:=K_ROLE.NAZWA;
   TTROLE.REF:=#K_ROLE.ref();
   TTROLE.add()
|? exec('can_run','control') & FUN.ask('Czy wykonać aktualizację zakresu danych: %1\nna serwerze OLAP?'@[K_ROLE.NAZWA])
|| TTROLE:=tab_tmp(1,'NAZWA','STRING[100]',,'REF','INTEGER',);
   TTROLE.NAZWA:=K_ROLE.NAZWA;
   TTROLE.REF:=#K_ROLE.ref();
   TTROLE.add();
   {? ~exec('run_con','control',4)
   || K_ROLE.WYM_AKT:='N';
      K_ROLE.put()
   ?};
   VAR_DEL.delete('TTROLE')
?}


\bg_k_role_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Przed akcja grupowa aktualizacji roli na serwerze OLAP
::  OLD: \bg_k_role_akt/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('can_run','control') & FUN.ask('Czy wykonać aktualizację zaznaczonych\nzakresów danych na serwerze OLAP?'@)
|| TTROLE:=tab_tmp(1,'NAZWA','STRING[100]',,'REF','INTEGER',)
?}


\ag_k_role_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Po akcji grupowej aktualizacji rol na serwerze OLAP
::  OLD: \ag_k_role_akt/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
exec('run_con','control',4);
K_ROLE.cntx_psh(); K_ROLE.prefix();
{? TTROLE.first()
|| {!
   |? {? K_ROLE.seek(TTROLE.REF,)
      || K_ROLE.WYM_AKT:='N'; K_ROLE.put()
      ?};
      TTROLE.next()
   !}
?};
K_ROLE.cntx_pop();
VAR_DEL.delete('TTROLE')


\klik_analizy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja Analizy dla Qlik
::----------------------------------------------------------------------------------------------------------------------
{? K_INSMBN.KLIK='T'
|| K_ROLE.index('TYPE');
   K_ROLE.prefix(REF.FIRMA,'Q',);
   K_ROLE_D.index('K_ROLE');
   K_ROLE_D.prefix();
   _jest:=0;
   {? K_ROLE.first()
   || {!
      |? _jest:=K_ROLE_D.find_key(K_ROLE.ref());
         _jest=0 & K_ROLE.next()
      !}
   ?};
   {? _jest
   || {? SKID_MBN.sel_size()=0 & FUN.ask('Istnieje definicja zakresu danych dla modelu.\nCzy usunąć użycie modelu w tej definicji?'@)
      || do();
         {? K_ROLE.first()
         || {!
            |? K_ROLE_D.prefix(K_ROLE.ref(),SKID_MBN.ref());
               {? K_ROLE_D.first() || {! |? K_ROLE_D.del() !} ?};
               K_ROLE.next()
            !}
         ?};
         _jest:=~end()
      ?}
   ?};
   {? _jest=0
   || K_INSMBN.KLIK:='N';
      K_INSMBN.put()
   ?}
|| K_INSMBN.KLIK:='T';
   K_INSMBN.put()
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:48 a37f613abdfd6bd6df3ebea56d5d03ed071a45e67eae2f079067236606e7ca3a93160bbc3626d6305d7ff9de7d24b4d7123dc96962ef09533cf9b57b50645f399db6073cb45bdceac6ac573d3bd1b8962c55d3a34ec1b1ddfa71102094ecb26624f3e91020eccbbd797b2f85b3ded8b7e0e583de498e435400c40da86a470fa1

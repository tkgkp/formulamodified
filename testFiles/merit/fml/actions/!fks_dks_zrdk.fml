:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_zrdk.fml
:: Utworzony: 03.12.2014
:: Autor: RWR
::======================================================================================================================
:: Zawartosc: Formuły czynności FKS_DKS_ZRDK - Zestawienie dokumentów źródłowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Zestawienie dokumentów źródłowych - główna formuła czynności. Formuła może być wywołana z kafelka "Raporty" lub
::       "Szybkie akcje". W tym drugim przypadku do formuły przekazane zostaną odpowiednie parametry - udostępnijmy je
::       dalej.
::   WE: [_a] - [1]- z raportów
::               0 - z obszaru
::   WY: 0 - Dla wywołania z konkretnym raportem: pliku nie znaleziono.
::       1 - W przeciwnym wypadku.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
{? _=0 || _a:=1 ?};
params_set(params_get());
{? _a
|| exec('F','object');
   exec('RB','object')
?};
ROZNE.ROKKON:=SSTALE.AR;
{? ~_a || DOK.cntx_psh() ?};
exec('rep_exec','#b_report','FKS_DKS_ZRDK','fks_dzw*',,1);
{? ~_a || DOK.cntx_pop() ?}


\chk_pardz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [7.41] 06.06.2001
:: OPIS: Sprawdza poprawnosc wypelnienia pol zmiennej PAR_WYDR
::   WY: Akronim blednie wypelnionego pola
::  OLD: \chk_pardz/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:='';
{? PAR_WYDR.PAR3<>0
|| {? PAR_WYDR.PAR4=0 & PAR_WYDR.PAR3>0 || PAR_WYDR.PAR4:=PAR_WYDR.PAR3
   |? PAR_WYDR.PAR4<PAR_WYDR.PAR3 | PAR_WYDR.PAR3<0
   || FUN.info('Błędny zakres numerów w rejestrze.'@); _fld:='PAR4'
   ?}
?};
{? _fld=''
|| {? PAR_WYDR.DPAR1<>date(0,0,0)
   || {? PAR_WYDR.DPAR2=date(0,0,0) || PAR_WYDR.DPAR2:=PAR_WYDR.DPAR1
      |? PAR_WYDR.DPAR1>PAR_WYDR.DPAR2 || FUN.info('Błędny zakres dat.'@); _fld:='DPAR2'
      ?}
   || {? PAR_WYDR.DPAR2<>date(0,0,0)
      || FUN.info('Błędny zakres dat.'@); _fld:='DPAR1'
      ?}
   ?}
?};
{? _fld=''
|| {? (~ROZNE.UT_OKROD & ROZNE.UT_OKRDO) | (ROZNE.UT_OKROD & ~ROZNE.UT_OKRDO)
      | (ROZNE.UT_OKROD & ROZNE.UT_OKRDO & ROZNE.UT_OKROD().NR>ROZNE.UT_OKRDO().NR)
   || FUN.info('Błędny zakres okresów obrachunkowych.'@); _fld:='UT_OKROD'
   ?}
?};
{? _fld=''
|| {? PAR_WYDR.DPAR3<>date(0,0,0)
   || {? PAR_WYDR.DPAR4=date(0,0,0) || PAR_WYDR.DPAR4:=PAR_WYDR.DPAR3
      |? PAR_WYDR.DPAR3>PAR_WYDR.DPAR4 || FUN.info('Błędny zakres dat.'@); _fld:='DPAR4'
      ?}
   || {? ~ROZNE.UT_OKROD
      || FUN.info('Nie podano daty operacji.'@); _fld:='DPAR3'
      ?}
   ?}
?};
{? _fld=''
|| {? ROZNE.UT_OKROD & PAR_WYDR.DPAR3<ROZNE.UT_OKROD().POCZ & ROZNE.UT_OKROD().POCZ<>date(0,0,0)
   || FUN.info('Data zapisu spoza wybranego okresu obrachunkowego.'@); _fld:='DPAR3'
   |? ROZNE.UT_OKRDO & PAR_WYDR.DPAR4>ROZNE.UT_OKRDO().KON & ROZNE.UT_OKRDO().KON<>date(0,0,0)
   || FUN.info('Data zapisu spoza wybranego okresu obrachunkowego.'@); _fld:='DPAR4'
   ?}
?};
{? _fld='' & ~exec('dat_not','dok_fks',PAR_WYDR.DPAR3)
|| FUN.info('Data operacji musi być z bieżącego roku bilansowego.'@); _fld:='DPAR3'
|| {? _fld='' & ~exec('dat_not','dok_fks',PAR_WYDR.DPAR4)
   || FUN.info('Data operacji musi być z bieżącego roku bilansowego.'@); _fld:='DPAR4'
   ?}
?};
{? _fld=''
|| {? PAR_WYDR.DPAR5<>date(0,0,0)
   || {? PAR_WYDR.DPAR6=date(0,0,0) || PAR_WYDR.DPAR6:=PAR_WYDR.DPAR5
      |? PAR_WYDR.DPAR5>PAR_WYDR.DPAR6 || FUN.info('Błędny zakres dat.'@); _fld:='DPAR6'
      ?}
   || {? PAR_WYDR.DPAR6<>date(0,0,0)
      || FUN.info('Błędny zakres dat.'@); _fld:='DPAR5'
      ?}
   ?}
?};
{? _fld='' & PAR_WYDR.PAR5<>0
|| {? PAR_WYDR.PAR6=0 || PAR_WYDR.PAR6:=PAR_WYDR.PAR5
   |? PAR_WYDR.PAR6<PAR_WYDR.PAR5
   || FUN.info('Błędny zakres numerów pozycji.'@); _fld:='PAR6'
   ?}
?};
{? _fld='' & PAR_WYDR.RPAR2<>0
|| {? PAR_WYDR.RPAR3=0 || PAR_WYDR.RPAR3:=PAR_WYDR.RPAR2
   |? PAR_WYDR.RPAR3<PAR_WYDR.RPAR2 || FUN.info('Błędny zakres kwot.'@); _fld:='RPAR3'
   ?}
?};
{? _fld='' & PAR_WYDR.RPAR4<>0
|| {? PAR_WYDR.RPAR5=0 || PAR_WYDR.RPAR5:=PAR_WYDR.RPAR4
   |? PAR_WYDR.RPAR5<PAR_WYDR.RPAR4 || FUN.info('Błędny zakres kwot.'@); _fld:='RPAR5'
   ?}
?};
{? _fld='' & (PAR_WYDR.RPAR2<>0 | PAR_WYDR.RPAR3<>0) & (PAR_WYDR.RPAR4<>0 | PAR_WYDR.RPAR5<>0)
|| FUN.info('Nie można podać jednocześnie zakresu kwot dla strony Wn i Ma!'@,
      'UWAGA'@); _fld:='RPAR2'
?};
{? _fld='' & PAR_WYDR.WAL().SYM=FINFO.NAROD().KOD
|| FUN.info('Aby wyświetlić dokumenty dla waluty narodowej\n'
      ' należy pole Waluta pozostawić niewypełnione.'@);
  _fld:='WAL'
?};
{? _fld='' & PAR_WYDR.RPAR6<>0
|| {? PAR_WYDR.RPAR7=0 || PAR_WYDR.RPAR7:=PAR_WYDR.RPAR6
   |? PAR_WYDR.RPAR7<PAR_WYDR.RPAR6 || FUN.info('Błędny zakres kwot.'@); _fld:='RPAR7'
   ?}
?};
{? _fld=''
|| {? PAR_WYDR.DPAR7<>date(0,0,0)
   || {? PAR_WYDR.DPAR8=date(0,0,0) || PAR_WYDR.DPAR8:=PAR_WYDR.DPAR7
      |? PAR_WYDR.DPAR7>PAR_WYDR.DPAR8 || FUN.info('Błędny zakres dat.'@); _fld:='DPAR8'
      ?}
   || {? PAR_WYDR.DPAR8<>date(0,0,0)
      || FUN.info('Błędny zakres dat.'@); _fld:='DPAR7'
      ?}
   ?}
?};
{? _fld='' & PAR_WYDR.POCH<>'' &  ',FKS,LSP,LZK,PDK,FST,TTE,KAS,'*(','+PAR_WYDR.POCH+',')=0
|| FUN.info('Błędna wartość pola Pochodzenie.'@); _fld:='POCH'
?};
{? _fld='' & PAR_WYDR.ZAKR<>'N' & PAR_WYDR.ZAKR<>'A' & PAR_WYDR.ZAKR<>'P' & PAR_WYDR.ZAKR<>'K' & PAR_WYDR.ZAKR<>''
|| FUN.info('Błędna wartość pola Zakres.'@); _fld:='ZAKR'
?};
{? _fld='' & PAR_WYDR.PAR1=3
|| {? PAR_WYDR.IPAR1=''
   || FUN.info('Błędnie zdefiniowany indeks.'@); _fld:='IPAR1'
   || {? PAR_WYDR.IPAR2='' & (PAR_WYDR.IPAR3<>'' | PAR_WYDR.IPAR4<>'')
      || FUN.info('Błędnie zdefiniowany indeks.'@); _fld:='IPAR2'
      || {? PAR_WYDR.IPAR2=PAR_WYDR.IPAR1
         || FUN.info('Powtórzone pole w indeksie.'@); _fld:='IPAR2'
         || {? PAR_WYDR.IPAR3='' & PAR_WYDR.IPAR4<>''
            || FUN.info('Błędnie zdefiniowany indeks.'@); _fld:='IPAR3'
            || {? PAR_WYDR.IPAR3=PAR_WYDR.IPAR1 | (PAR_WYDR.IPAR3=PAR_WYDR.IPAR2 & PAR_WYDR.IPAR2<>'')
               || FUN.info('Powtórzone pole w indeksie.'@); _fld:='IPAR3'
               || {? PAR_WYDR.IPAR4<>'' & (PAR_WYDR.IPAR4=PAR_WYDR.IPAR1 | (PAR_WYDR.IPAR4=PAR_WYDR.IPAR2 & PAR_WYDR.IPAR2<>'')
                     | (PAR_WYDR.IPAR4=PAR_WYDR.IPAR3 & PAR_WYDR.IPAR3<>''))
                  || FUN.info('Powtórzone pole w indeksie.'@);
                     _fld:='IPAR4'
                  ?}
               ?}
            ?}
         ?}
      ?}
   ?}
?};
{? _fld=''
|| exec('wyd_selk','dok_fks_zest','DZWDOKZE');
   {? PAR_WYDR.PAR1=1
   || _v:=exec('par_ind','!fks_dks_zrdk','Rejestr');
      {? _v='' || _fld:='PAR1' ?};
      _v:=exec('par_ind','!fks_dks_zrdk','Nr');
      {? _v='' || _fld:='PAR1' ?}
   |? PAR_WYDR.PAR1=2
   || _v:=exec('par_ind','!fks_dks_zrdk','Data zap.');
      {? _v='' || _fld:='PAR1' ?};
      _v:=exec('par_ind','!fks_dks_zrdk','Rejestr');
      {? _v='' || _fld:='PAR1' ?};
      _v:=exec('par_ind','!fks_dks_zrdk','Nr');
      {? _v='' || _fld:='PAR1' ?}
   |? PAR_WYDR.PAR1=3
   || {? PAR_WYDR.IPAR1<>''
      || _v:=exec('par_ind','!fks_dks_zrdk',PAR_WYDR.IPAR1);
         {? _v='' || _fld:='IPAR1'?};
         {? PAR_WYDR.IPAR2<>''
         || _v:=exec('par_ind','!fks_dks_zrdk',PAR_WYDR.IPAR2);
            {? _v='' || _fld:='IPAR2' ?};
            {? PAR_WYDR.IPAR3<>''
            || _v:=exec('par_ind','!fks_dks_zrdk',PAR_WYDR.IPAR3);
               {? _v='' || _fld:='IPAR3' ?};
               {? PAR_WYDR.IPAR4<>''
               || _v:=exec('par_ind','!fks_dks_zrdk',PAR_WYDR.IPAR4);
                  {? _v='' || _fld:='IPAR4' ?}
               ?}
            ?}
         ?}
      ?}
   ?};
   {? _fld<>''
   || FUN.info('W skład indeksu wchodzą pola, niewystępujące na wydruku!\n'@,
         'UWAGA'@)
   ?}
?};
{? ROZNE.PAR4=4
|| _fld:=__CHK.record2(ROZNE,'KS_OD','Zakres kont od','KS_DO','Zakres kont do');
   {? _fld='' || {? ROZNE.KS_OD().SYM> ROZNE.KS_DO().SYM
               || FUN.info('Błędny zakres kont.'@); _fld:='KS_OD'
               ?}
   ?}
|? ROZNE.PAR4=5
|| _fld:=__CHK.record2(ROZNE,'KON_WYDR','Zestaw kont');
   {? _fld=''
   || KON_SCH.prefix(KON_WYDR.ref);
      _fld:={? KON_SCH.first()
          || ''
          || FUN.info('Wybrany zestaw kont nie ma zdefiniowanej definicji kont.'@); 'KON_WYDR'
          ?}
   ?}
?};
_fld


\par_ind
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [7.41] 06.06.2001
:: OPIS: Zwraca akronim pola i ew. pola zlaczeniowego jako string
::           do zbudowania lancucha definiujacego indeks tymczasowy.
::   WE: nazwa pola
::   WY: akronim pola i pole zlaczeniowe
::  OLD: \par_ind/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_str:='';
{? _a='Rejestr' & KOL_WYDR.find_key(1) & (KOL_WYDR.WSK_DRUK='X' | KOL_WYDR.WSK_DRUK='W' | KOL_WYDR.WSK_DRUK='D')
|| _str:='F23,F1'
|? _a='Nr' & KOL_WYDR.find_key(2) & (KOL_WYDR.WSK_DRUK='X' | KOL_WYDR.WSK_DRUK='W' | KOL_WYDR.WSK_DRUK='D')
|| _str:='F2,F25'
|? _a='Data oper.' & KOL_WYDR.find_key(6) & (KOL_WYDR.WSK_DRUK='X' | KOL_WYDR.WSK_DRUK='W' | KOL_WYDR.WSK_DRUK='D')
|| _str:='F6'
|? _a='Data zap.' & KOL_WYDR.find_key(7) & (KOL_WYDR.WSK_DRUK='X' | KOL_WYDR.WSK_DRUK='W' | KOL_WYDR.WSK_DRUK='D')
|| _str:='F7'
|? _a='Kontrahent' & KOL_WYDR.find_key(16) & (KOL_WYDR.WSK_DRUK='X' | KOL_WYDR.WSK_DRUK='W' | KOL_WYDR.WSK_DRUK='D')
|| _str:='F16'
|? _a='Rej. VAT' & KOL_WYDR.find_key(15) & (KOL_WYDR.WSK_DRUK='X' | KOL_WYDR.WSK_DRUK='W' | KOL_WYDR.WSK_DRUK='D')
|| _str:='F15'
|? _a='Zarejestr.' & KOL_WYDR.find_key(24) & (KOL_WYDR.WSK_DRUK='X' | KOL_WYDR.WSK_DRUK='W' | KOL_WYDR.WSK_DRUK='D')
|| _str:='F24'
?};
_str


\be_maska
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [7.41] 12.04.2001
:: OPIS: Udostepnia lub blokuje do redakcji pole MASKA
::  OLD: \be_maska/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_WYDR.OMASKA=2 || 1 || 0 ?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 5869770e4b8402ac793146fe67619447ffcefcc728000acd9993e5f8f457b86e7a3497cfa25a69ccdd726ea6fdf2c05ef3395862d85a1d3ddf6d36e2cb1f1c4628d8b48a8a13fb4166687c28cbad635960e91e47afccdb9bb42f0361c209b7f83f9f295b5c2c5343afc46b106d060711115010b89684fa72bbe28918d36a7eee

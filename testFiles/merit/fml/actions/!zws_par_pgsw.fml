:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pgsw.fml
:: Utworzony: 29.04.2020
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PGSW - Elementy warunków współpracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Elementy warunków współpracy - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::

_tabs:=obj_new('nr','ref1','ref2');
_tabs.nr:=1;

params_set(
   'tabs',_tabs
);

_firma:=exec('firma','ustawienia');

GSXM.cntx_psh();
GSXM.index('VIEW');
GSXM.prefix('GSWN',);
GSXW.cntx_psh();
BNFTT.cntx_psh();
BNFTT.index('AKRONIM');
BNFTT.prefix();
BNFTT.win_dict('SLO');
GSWN.cntx_psh();
GSWN.index('LP');
GSWN.prefix(_firma);
GSWN.win_edit('REDB');
GSWN.win_patt('SZUK');
GSWN.win_sel(exec('gswn_wnd','!zws_par_pgsw'));
GSWN.select();
GSWN.cntx_pop();
BNFTT.cntx_pop();
GSXW.cntx_pop();
GSXM.cntx_pop();
~~


\gswn_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła buduje okno grupowe do obsługi elementów warunków współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';

_grp:=GSWN.grp_make('Elementy warunków współpracy'@,,'#gswgrp',,,,,'maximized');

:: I-sza zakładka
GSWN.grp_sel(_grp,GSWN,'WER','Grupy'@,
   "  params_set(params_get());
      grp_disp(GSXW,'WER4GR')
   ",,,
   20,
   "  {? _a
      || _tabs:=params_get().tabs;
         {? _tabs.nr<>1
         || _tabs.nr:=1;
::          W ramach obsługi II-giej na tabelę GSWN zakładany jest filtr - upewnijmy się, że tutaj będzie wyłączony.
            GSWN.f_clear();
            GSWN.index('LP');
            GSWN.prefix(exec('ref_firma','ustawienia'));
::          Ustawienie / przywrócenie aktywnego rekordu (tabela GSWN występuje na obu zakładkach).
            {? _tabs.ref1=~~
            || GSWN.first()
            || GSWN.seek(_tabs.ref1)
            ?}
         ?}
      ?}
   ",
   "  {? _a
::       Zapamiętanie aktywnego rekordu tabeli GSWN na bieżącej zakładce.
      || _tabs:=params_get().tabs.ref1:=GSWN.ref()
      ?}
   ",,,
   _mode
);
GSWN.tab_splt(_grp,,'vertical','gspw');
GSWN.grp_sel(_grp,GSXW,'WER4GR',,,,,,
   "  {? grp_empty(GSWN,'WER') | GSWN.GSWN=0
      || return('#disable')
      ?};
      GSXW.index('OPIS');
      GSXW.prefix(GSWN.FIRMA,GSWN.uidref())
   ","GSXW.prefix()",,,
   _mode
);

:: II-ga zakładka
GSWN.grp_sel(_grp,GSXM,'WER','Widoki'@,
   "  params_set(params_get());
      grp_disp(GSWN,'VIEW')
   ",,,,
   "  {? _a
      || _tabs:=params_get().tabs;
         {? _tabs.nr<>2
         || _tabs.nr:=2;
::          Ustawienie / przywrócenie aktywnego rekordu (tabela GSXM występuje na obu zakładkach).
            {? _tabs.ref2=~~
            || GSXM.first()
            || GSXM.seek(_tabs.ref2)
            ?};
            GSWN.first()
         ?}
      ?}
   ",
   "  {? _a
::       Zapamiętanie aktywnego rekordu tabeli GSWN na bieżącej zakładce.
      || _tabs:=params_get().tabs.ref2:=GSXM.ref()
      ?}
   ",,,
   _mode
);
GSWN.tab_splt(_grp,,'vertical','gspw');
GSWN.grp_sel(_grp,GSWN,'VIEW',,,,,,
   "  {? grp_empty(GSXM,'WER')
      || return('#disable')
      ?};
      {? ~_a
      || GSWN.sel_adel()
      ?};
      GSWN.prefix();
      GSWN.f_set('GSWN,LP',,
         '""GSWN"".FIRMA=:_a and '
         '('':'' || ""GSWN"".%1 || ""GSWN"".REFERENCE) in (select GSXW.N from GSXW where GSXW.GSXM=:_b)'
         [GSWN.idadd_acr()],
         exec('ref_firma','ustawienia'),GSXM.ref()
      );
      ~~
   ",,,,
   _mode
);

_grp


\gswn_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('gswn_modb','!zws_par_pgsw') & exec('gswn_chk','!zws_par_pgsw',0)
|| exec('add_before','#order',GSWN,'LP','LP',GSWN.LP,GSWN.FIRMA,GSWN.GSWN)
?}


\gswn_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('gswn_modb','!zws_par_pgsw') & exec('gswn_chk','!zws_par_pgsw',1)


\gswn_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? GSWN.SYSTEM='T'
|| undo('(GSWN) Zapis systemowy. Usunięcie niemożliwe.'@);
   return(0)
?};
exec('del_ndx','#table',GSXW,'N',GSWN.FIRMA,GSWN.uidref())


\gswn_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Usuń - po" dla tabeli GSWN.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
exec('del_after','#order',GSWN,'LP','LP',bfld('LP'),bfld('FIRMA'),bfld('GSWN'));
~~


\gswn_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Przed modyfikacją rekordu.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? GSWN.BNFTT
|| BNFTT.cntx_psh();
   BNFTT.prefix();
   GSWN.BNFTT();
   {? GSWN.NAZWA<>BNFTT.AKRONIM
   || GSWN.NAZWA:=BNFTT.AKRONIM;
      GSWN.OPIS:=BNFTT.NAZWA
   |? GSWN.OPIS=''
   || GSWN.OPIS:=BNFTT.NAZWA
   ?};
   BNFTT.cntx_pop()
?};
1


\gswn_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli GSWN.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
{? ~_put
:: Ponieważ indeks po polu LP jest unikalny, to na czas weryfikacji unikalności zadbajmy o nią ręcznie (wartość -1 nie
:: powinna wystąpić w "normalnej" eksploatacji).
|| _lp:=-1;
   GSWN.LP==_lp
?};
_ret:=__CHK.validate(GSWN,
:: Najpierw pola, które nie są widoczne w oknach, ale są niezbędne.
   $("_a.table(_b,"+$_put+",,'FIRMA','LP','SYSTEM')"),
   "_a.in_set(_b,'SYSTEM',,'T','N')",
:: Teraz walidacja pól redagowalnych.
   "  {? _b.SYSTEM='T'
      || _a.record(_b,,'OPIS','NAZWA')
      || _a.record(_b,,'BNFTT','NAZWA','OPIS')
      ?}
   ",
   $("{? _b.FIRMA<>null() & _b.BNFTT<>null()
      || _ret:=1;
         BNFTT.cntx_psh();
         BNFTT.prefix();
         {? _b.FIRMA<>_b.BNFTT().FIRMA
         || undo(2-!_b+': "+'Brak jednoznaczności powiązania z firmą.'@+"');
            _ret:=0
         ?};
         BNFTT.cntx_pop();
         _ret
      || ''
      ?}
   "),
   $("{? _b.SYSTEM='T' & _b.BNFTT<>null()
      || _a.err_fld(_b,'BNFTT',1,'"+'Dla tej definicji wskazanie benefitu jest zabronione.'+"'@)
      || 1
      ?}
   "),
   $("_put:="+$_put+";
      _ret:='';
      {? _b.BNFTT
      || _ref:={? _put || _b.ref() || null() ?};
         _b.cntx_psh();
         _b.index('BNFTT');
         _b.prefix(_b.FIRMA,_b.BNFTT);
         {? _b.first() & _b.ref()<>_ref
         || _ret:=_a.err_fld(_b,'BNFTT',1,'"+'Element powiązany z tym benefitem już istnieje.'@+"')
         ?};
         _b.cntx_pop()
      ?};
      _ret
   ")
);
{? ~_put
:: Przywracamy pierwotną wartość.
|| GSWN.LP==_lp
?};
_ret


\gswn_bnftt_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła wykonywana po redagowaniu pola GSWN.BNFTT
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? GSWN.BNFTT
|| {? GSWN.NAZWA<>GSWN.BNFTT().AKRONIM | GSWN.OPIS=''
   || GSWN.NAZWA:=BNFTT.AKRONIM;
      GSWN.OPIS:=BNFTT.NAZWA
   ?}
|| GSWN.NAZWA:=GSWN.OPIS:=''
?};
1


\gswn_akcje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Funkcja dla bieżącego rekordu tabeli GSWN określa dostępność akcji w oknie wertowania.
::   WE:
::   WY: Tablica z elementami nazwanymi.
::----------------------------------------------------------------------------------------------------------------------
_akcje:=obj_new('dolacz','dolacz_za','dolacz_ponizej','usun','przesun_wyzej','przesun_nizej');
{! _lp:=1 .. obj_len(_akcje)
:: Wstępnie - wszystkie akcje są niedostępne.
|! _akcje[_lp]:=0
!};

{? GSWN.get()
|| _akcje.usun:=GSWN.SYSTEM<>'T';
   {? GSWN.GSWN
::    Rekord podrzędny
   || _akcje.dolacz:=_akcje.dolacz_za:=1;
      GSWN.cntx_psh();
      GSWN.index('LP');
      GSWN.prefix(GSWN.FIRMA,GSWN.GSWN);
      _lp:=GSWN.LP;
      _akcje.przesun_wyzej:=GSWN.find_key(_lp-1);
      _akcje.przesun_nizej:=GSWN.find_key(_lp+1);
      GSWN.cntx_pop()
::    Rekord nadrzędny
   || _akcje.dolacz_ponizej:=1
   ?}
?};

_akcje


\bnftt_f_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła definiuje filtr dla tabeli BNFTT - ogranicza dziedzine tabeli tylko do rekordów, które mogą zostać
::       wybrane w akcjach typu "Dołącz" i "Popraw" dla tabeli GSWN.
::   WE: [_a] [REFERENCE] - Ref poprawianego rekordu (null() dla dołącznego) [domyślnie: null()].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:={? var_pres('_a')=type_of(null()) || _a || null() ?};

BNFTT.f_set('AKRONIM',,
   '"BNFTT".FIRMA=:_a and "BNFTT".REFERENCE not in '+
      '(select distinct GSWN.BNFTT from GSWN where GSWN.BNFTT IS NOT NULL and GSWN.FIRMA=:_a'+
      {? _ref<>null() || ' and GSWN.REFERENCE<>:_b' || '' ?}+')',
   exec('firma','ustawienia'),_ref
);

~~


\gswn_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Dołącz" w oknach WER i VIEW tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ws:=cur_win(1,1);
{? _ws='WER'
|| {? exec('gswn_akcje','!zws_par_pgsw').dolacz
   || exec('bnftt_f_set','!zws_par_pgsw');
      exec('dolacz','#order','LP','before','GSWN',
         "exec('gswn_ae','!zws_par_pgsw',0)",
         "BNFTT.f_rfresh(); win_disp(); 1"
      );
      BNFTT.f_clear()
   || melody()
   ?}
|? _ws='VIEW'
:: Rekord dołączany jest do tabeli GSXM.
|| exec('gsxn_dolacz_b','portal_lib','"GSWN".GSWN>0')
?}


\gswn_dolaczza_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Dołącz za" w oknie WER tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('gswn_akcje','!zws_par_pgsw').dolacz_za
|| exec('bnftt_f_set','!zws_par_pgsw');
   exec('dolacz','#order','LP','after','GSWN',"exec('gswn_ae','!zws_par_pgsw',0)","BNFTT.f_rfresh(); win_disp(); 1");
   BNFTT.f_clear()
|| melody()
?}


\gswn_dolaczponizej_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Dołącz poniżej" w oknie WER tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('gswn_akcje','!zws_par_pgsw').dolacz_ponizej
|| exec('bnftt_f_set','!zws_par_pgsw');
   exec('dolacz','#order','LP','child_l','GSWN',"exec('gswn_ae','!zws_par_pgsw',0)","BNFTT.f_rfresh(); win_disp(); 1");
   BNFTT.f_clear()
|| melody()
?}


\gswn_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Popraw - przed" w oknie WER tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
{? GSWN.BNFTT=null()
|| GSWN.win_edit('REDW')
?};
exec('bnftt_f_set','!zws_par_pgsw',GSWN.ref());
{? GSWN.edit("exec('gswn_ae','!zws_par_pgsw',1)")
|| _ret:=GSWN.put()
?};
BNFTT.f_clear();
GSWN.win_edit('REDB');
_ret


\gswn_usun_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Usuń - grupa przed" w oknie WER tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
sel_nchk();
exec('del_ask','#table')


\gswn_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Usuń - przed" w oknie WER tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('gswn_akcje','!zws_par_pgsw').usun
|| _grp:=GSWN.sel_size();
   {? GSWN.SYSTEM='T'
   || {? ~_grp
      || FUN.info('Zapis systemowy. Usunięcie niemożliwe.'@)
      ?}
   |? _grp | exec('del_ask','#table')
   || GSWN.del()
   ?}
|| melody()
?}


\gswn_wyzej_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Przesuń wyżej" w oknie WER tabeli GSWN.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? exec('gswn_akcje','!zws_par_pgsw').przesun_wyzej
|| exec('przesun','#order','LP','W','GSWN')
|| melody()
?};
~~


\gswn_nizej_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Przesuń niżej" w oknie WER tabeli GSWN.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? exec('gswn_akcje','!zws_par_pgsw').przesun_nizej
|| exec('przesun','#order','LP','N','GSWN')
|| melody()
?};
~~


\gswn_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER tabeli GSWN.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _akcje:=exec('gswn_akcje','!zws_par_pgsw');
   _ha:='';
:: Co najmniej jedna z akcji typu "Dołącz" jest zawsze niedostępna.
   _ha+='D('+
      {? _akcje.dolacz || '' || 'D' ?}+
      {? _akcje.dolacz_za || '' || 'Z' ?}+
      {? _akcje.dolacz_ponizej || '' || 'E' ?}+
   ')';
   {? ~GSWN.sel_size() & ~_akcje.usun
   || _ha+='U'
   ?};
   {? ~_akcje.przesun_wyzej | ~_akcje.przesun_nizej
   || _ha+='E('+{? _akcje.przesun_wyzej || '' || 'W' ?}+{? _akcje.przesun_nizej || '' || 'N' ?}+')'
   ?};

   GSWN.actions_grayed('WER',_ha)
?};
0


\gswn_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli GSWN.
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
{? exec('gswn_modb','!zws_par_pgsw')
|| exec('gswn_chk','!zws_par_pgsw',_put)
?}


\gswn_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie WER tabeli GSWN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? GSWN.BNFTT=null()
|| GSWN.win_edit('REDW')
?};
GSWN.display();
GSWN.win_edit('REDB')

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:43 a27dd86317c7c4876957b9cf1698b486e6b167ae1e4420c9955b6431101a2a9ddad84c8bf2e95b5da082dff1f266f1fa852689dbde190d56bb5013ff5162eedb2ff0d6960cacf3bd629907f263a7676eed2b9bb8cbb1f9b4083f6b3290ca7cef2282d3b1080ad2acf474144a6b00eeb7bbd950f33eae74bb490d95fd5f62cfeb

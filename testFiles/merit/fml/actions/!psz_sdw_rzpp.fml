:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdw_rzpp.fml
:: Utworzony: 21.11.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności PSZ_SDW_RZPP - Zapotrzebowania podwładnych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Zapotrzebowania podwładnych - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('env_wt','psz')
|| return()
?};

web_params_set(
   exec('obj_ntab_set','#array',web_params_get(),
::    Po odświeżeniu okna WEBWER tabeli SZK_ZAP - wyszarzanie przycisków.
      'SZK_ZAP_WEBWER_AR',"exec('szk_zap_webwer_ar','!psz_sdw_rzpp')",
::    Pracownik, którego dotyczy zapotrzebowanie - formułą zwracająca SQL-ref'a.
      'SZK_ZAP_P',
         "  _ref:=web_win_ref('P_WEB_CX');
            P_WEB_CX.cntx_psh();
            P_WEB_CX.prefix();
            _ret:=${? P_WEB_CX.seek(_ref) || P_WEB_CX.P || null() ?};
            P_WEB_CX.cntx_pop();
            _ret
         ",
::    Typ zgłoszenia: S - zgłoszenie przełożonego.
      'SZK_ZAP_ZGL_TYP','S',
::    Podstawa realizacji: R - bieżąca potrzeba.
      'SZK_ZAP_POD_REAL','R',
::    Dziedzina konta kosztowego (formuła).
      'BPMN_KK_DOM',$('\''+BPMN.KK_DOM+'\'')
   )
);

P_WEB_CX.cntx_psh();
exec('p_web_cx_set_prefix','p_web',,'RODZAJ','P');
P_WEB_CX.web_select('SZK_ZAP');
P_WEB_CX.cntx_pop();

~~


\p_web_cx_szk_zap_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Przed obsługą okna WEBWER tabeli SZK_ZAP w ramach okna grupowego SZK_ZAP tabeli P_WEB_CX.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:=web_win_ref('P_WEB_CX');
{? _ref=~~
|| _ref:=null()
?};

P_WEB_CX.cntx_psh();
P_WEB_CX.prefix();
_ref:={? P_WEB_CX.seek(_ref) || P_WEB_CX.P || null() ?};
P_WEB_CX.cntx_pop();

SZK_ZAP.use('szk_zap');
SZK_ZAP.index('PT');
SZK_ZAP.prefix(_ref);
SZK_ZAP.first();
~~


\szk_zap_webwer_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: "Po odświeżeniu" dla okna WEBWER tabeli SZK_ZAP. Formuła wywoływana dynamicznie (poprzez zmienną
::       web_params_get().SZK_ZAP_WEBWER_AR). Odpowiada za ukrywanie przycisków.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Wstępnie blokujemy wszystko.
_ag1:='DPUAO';
_ag0:='D';

{? SZK_ZAP.get()
|| {? SZK_ZAP.SZK_PRAC=null() & SZK_ZAP.ZO_TEST=null()
::    Jest bieżący rekord jeszcze nie "skonsumowane" (nie odbyły się związane z nimi szkolenia) i
::    zapotrzebowanie nie jest związane z oceną,
::    to wstępnie odblokujmy wszystkie akcje.
   || _ag1:=_ag0:='';
      SLO_KOD.cntx_psh();
      _wer_wyn:=SZK_ZAP.WER_WYN().KOD;
      SLO_KOD.cntx_pop();
      {? _wer_wyn='Z'
      || _ag1+='A'
      |? _wer_wyn='O'
      || _ag1+='O'
      ?}

::    Na podstawie zapotrzebowania odbyło się szkolenie lub zapotrzebowanie związane z oceną
::    - blokujemy Popraw/Usuń/Akceptuj/Odrzuć.
   || _ag1:='PUAO';
      _ag0:=''
   ?}

:: Jeżeli dziedzina jest pusta (a tak zinterpretujmy brak możliwości wczytania bieżącego rekordu), to wycofajmy się
:: z ustawień wstępnych (blokady wszystkich akcji) i pozwólmy na "Dołącz".
|| _ag0:=''
?};

web_top_tab(0,0).web_win_opt(web_top_win(0,0),web_top_ident(0),'grayed=%1:%2' [_ag1,_ag0]);
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:59 c1cd2b7f06a19dd2bd326ee248ffa792788c1b6bd1721fad78d839fb313490040129be04ed831808e43fb521d3c87479173316fba1e6cfa6f901eb40c828b464de0979e00c36958a67322c6b6e84fd0a4ad7b9a9f77c6923275502e1f358e526d10ad75a9e1a730063e9a262cb96042f473489aa881a13cedad9d5fe79aaaca5

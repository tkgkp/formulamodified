:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_fak_dfop.fml
:: Utworzony: 29.02.2016
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa dokumentu sprzedaży rozliczającego opakowania zbiorcze
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP,TARS
::# kind=WE,   symbol=TYPYSP,    type=_TYPYSP,  name=Typ dokumentu sprzedaży, required=N, fml_val="exec('typysp','!lsp_fak_dfop')", fml_exp="exec('typysp_export','typysp',_a)"
::# kind=WY,   symbol=FAKS,      type=_FAKS,    name=Dokument sprzedaży,      required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_fak','lsp');

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_area_lsp_fak:=_mp.pathArea('LSP_FAK') | _mp.pathArea('LSS_POS');
_area:=_area_lsp_fak;
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo() | _mp.pathArea() & ~_area;
_grupa:=_mp.isGroup();
_autoakc:=exec('autoAkc','#b__box',_mp,300080,'LSP_FAK_EASP');

_auto:=_mp.isAutoRun();
_context:=
   {? var_press('context',params_get())=type_of(~~)
   || exec('context_lsp_fak_dzdm','faktury_nag')
   || params_get().context
   ?};

{? _todo
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('FAKS',_out)=type_of(null()) & _out.FAKS
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego FAKS.'@)
   ?}
?};

:: Sprawdzenie uprawnień
{? params_exec('permissions_chk','lsp','!lsp_fak_dfop')=0 || return() ?};

:: Wyzwalacz, który po dodaniu nagłówka dokumentu:
:: - add: dodaje rekord kluczowy nagłówka dokumentu
::   del: usuwa rekord kluczowy nagłówka dokumentu
:: - add: zapisuje parametr wyjściowy FAKS = wskazanie na nagłówek korekty
::   del: zapisuje parametr wyjściowy FAKS = null
_mp.trigRef('FAKS',,1,,exec('kind_out','#b_port'),'FAKS');

{? _akcja='Dołącz'
   | _proc
   | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
|| _typysp_t:=
      {? var_pres('TYPYSP',_in)=type_of(null())
      || exec('FindAndGet','#table',TYPYSP,_in.TYPYSP,,"TYPYSP.T")
      || ''
      ?};
   BEER.WYSTFAKS:=null;
   HELP.WMAG:=0;

:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
   params_set('out',_out,'mp',_mp,'in',_in);
   params_exec('opak_ppt','opakow',_typysp_t);

:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? var_pres('FAKS',_outa)<>type_of(~~) & _outa.FAKS
:: Dodano dokument
   ||
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      params_exec('seek_dok','faktury_wspolne',_area_lsp_fak,_outa.FAKS)
:: Wycofanie czynności bo nie dodano dokumentu
   || _mp.cancel()
   ?}

|? _akcja='Popraw'
   | _todo
|| {? var_pres('FAKS',_out)=type_of(null())
:: Uruchomione w procesie
   || FAKS.cntx_psh();
      FAKS.prefix();
      {? FAKS.seek(_out.FAKS)
      || exec('faks_win_edit_set','faktury_nag');

         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('fak_pop','faktury_nag');

         _mp.descTodo()
      ?};
      FAKS.cntx_pop()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || {? ~_todo
      || params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('fak_pop','faktury_nag')
      |? _akcja='Zakończ_wer'
      || {? exec('faks_zakoncz','faktury_nag',,_autoakc) || _mp.done() ?}
      || _mp.cancel()
      ?}
   |? _todo
:: Uruchomione zadanie z listy todo i brak _out.FAKS wtedy zadanie wycofujemy
   || _mp.cancel()
   ?}

|? _akcja='Usuń'
|| {? var_pres('FAKS',_out)=type_of(null())
:: Uruchomione w procesie
   || _faks:=null();
      FAKS.cntx_psh();
      {? ~_area_lsp_fak || FAKS.prefix() ?};
      {? FAKS.seek(_out.FAKS)
      ||
         exec('usun_fak','faktury_nag',{? _grupa || 2 || 1 ?});

         {? ~FAKS.seek(_out.FAKS)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _faks:=FAKS.ref()
      ?};
      FAKS.cntx_pop();
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _area_lsp_fak || {? _faks || FAKS.seek(_faks) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      exec('usun_fak','faktury_nag',{? _grupa || 2 || 1 ?});

      _mp.done()
   ?}

|? _akcja='Zakończ_wer' & _area_lsp_fak
|| {? exec('faks_zakoncz','faktury_nag',_grupa,_autoakc) || _mp.done() ?}

|| _mp.error('Nieobsługiwana ścieżka.'@)
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('FAKS',_out)=type_of(null()) & _out.FAKS
|| 'Zakończ rejestrację dokumentu sprzedaży: %1'@@[exec('record','#to_string',_out.FAKS)]
|| 'Zarejestruj dokument sprzedaży'@@
?}


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_wyn:=1;

:: Sprawdzenie uprawnień do typu dokumentu
{? _wyn & var_pres('TYPYSP',_in)=type_of(null()) & _in.TYPYSP
|| _typysp_t:=exec('FindAndGet','#table',TYPYSP,$_in.TYPYSP,,"TYPYSP.T",'');
   _wyn:=
      {? _typysp_t<>''
      || {? exec('FindInSet','#table','T2STS','UNIK',ST.STS)<>null()
         || exec('FindInSet','#table','T2STS','UNIK',_in.TYPYSP,ST.STS)<>null()
         || _where:=exec('tsp_where','typysp','P',,,,_typysp_t);
            exec('tsp_upr','typysp',_where,,0,,1,,_user)<>null()
         ?}
      || 1
      ?}
?};

_wyn


\faks_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Faktura pozostała - Dołącz
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('faks_dolacz','faktury_nag','LSP_FAK_DFOP')


\typysp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na wartość parametru TYPYSP
::   WY: TYPYSP.ref()
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:='S';
exec('tsp_fak','typysp','P')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 c16c282afd7348481f1fa62b2ddf52ff01e5cf3d272df1f6989f17378c73e7754c3b193c693e9e4b01f628433e3fe654cf511f682ddb7c9b7416efb75294afac2fbaa4bd74f3dce241bf6ab6da02a323ab52bae69349d32008e5d7c0575be8b9bb019c825b0221fd0bc581281b2c70c111ac1ec094d3bec52bb31773a73cacfc

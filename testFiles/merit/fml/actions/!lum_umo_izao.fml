:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lum_umo_izao.fml
:: Utworzony: 18.04.2019
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Zamknięcie okresu - Umowy
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=ROK,       type=NUMBER,   name=Rok,               required=N, fml_val="exec('rok','!lum_umo_izao')"
::# kind=WE,   symbol=MIESIAC,   type=NUMBER,   name=Miesiąc,           required=N, fml_val="exec('miesiac','!lum_umo_izao')"
::# kind=WY,   symbol=WYNIK,     type=MEMO,     name=Wynik zamknięcia,  required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: Uruchomiona akcja
_akcja:=_mp.akcja();

:: Uruchomiona automatycznie
_auto:=_akcja<>'Zamknij' & _mp.isAutoRun();

_rok:={? var_press('ROK',_in)=type_of(0) & _in.ROK || _in.ROK || ST.AR ?};
_miesiac:={? var_press('MIESIAC',_in)=type_of(0) & _in.MIESIAC || _in.MIESIAC || ST.AM ?};

_out.WYNIK:='';

OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_rok,_miesiac);
{? OKR.first()
|| _zam_mag:=1+OKR.ZAM;
   _zam_spr:=1+(1-OKR.ZAM);
   _zam_zak:=1+(2-OKR.ZAM);
   _zam_umo:=OKR.ZAM+1;
   _Tab:=sql($"
      select
         substr(ZLP.REFERENCE,4,1) ODDZ,
         SPACE(8) KH,
         ZLP.REFERENCE || SPACE(200) SYM
      from
         @ZLP
         join @ZLE
      where
         substr(ZLP.REFERENCE,5,4)=:_a
         and (ZLP.STAT_REJ='N')
         and :_b=1"
      ,(($OKR.ROK)+2)+form(OKR.MC,-2,0,'99'),_zam_umo='N');
   {? _Tab.last()
   || {!
      |? _Tab.KH:=exec('FindAndGet','#table',ZLP,form(_Tab.SYM),,"KH().KOD",'');
         _Tab.SYM:=exec('symbol','!lum_umo_izao',form(_Tab.SYM));
         _Tab.put(1);
         _Tab.prev()
      !};
      _txt:='Niezakończone zgłoszenia do umów i zgłoszenia jednorazowe'@;
      _out.WYNIK+=':\n';
      _loop:=_Tab.first();
      {!
      |? _loop
      |!
         _out.WYNIK+='\n'+_Tab.SYM;
         _loop:=_Tab.next()
      !};
      _wer:=_Tab.mk_sel(_txt,,0,'okr_zam_zak');
      _Tab.win_fld(_wer,,'ODDZ',,,10,,,'Oddział'@);
      _Tab.win_fld(_wer,,'KH',,,10,,,'Kontrahent'@);
      _Tab.win_fld(_wer,,'SYM',,,40,,,'Opis'@);
      _Tab.win_act(_wer,,'Formuła','Zamknij okno'@@,,,"sel_exit()",,1);
      _Tab.win_sel(_wer);
      _Tab.select()
   ?};
   {? _zam_umo='T'
   || FUN.info('Okres %1 jest już zamknięty.'@[OKR.NAZ]);
      _mp.done()
   |? FUN.ask('Zamknąć okres %1?'@[OKR.NAZ])
   || OKR.get();
      OKR.ZAM:=_zam_mag+_zam_spr+_zam_zak+'T';
      {? OKR.put()
      || _out.WYNIK+={? +_out.WYNIK || '\n' || '' ?}+'Okres %1 został zamknięty.'@[OKR.NAZ];
         _mp.done()
      ?}
   ?}
|| _out.WYNIK+='Brak okresu %1/%2'@[form(_miesiac,-2,,'9 '),form(_rok,-4,,'9 ')];
   _mp.done()
?};
OKR.cntx_pop();
_mp.save(,_out)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

_rok:={? var_press('ROK',_in)=type_of(0) & _in.ROK || _in.ROK || 0 ?};
_miesiac:={? var_press('MIESIAC',_in)=type_of(0) & _in.MIESIAC || _in.MIESIAC || 0 ?};

OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_rok,_miesiac);
_wyn:=
   {? OKR.first()
   || 'Zamknij okres %1 w umowach'@@[OKR.NAZ]
   || 'Zamknij okres w umowach'@@
   ?};
OKR.cntx_pop();
_wyn


\zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamknięcie okresu - Umowy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LUM_UMO_IZAO';
_params.AKCJA:='Zamknij';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);

OKR.cntx_psh();
OKR.prefix();
{? OKR.seek(exec('zwr_obs_okres','parses','LUM',OKRO.ref()))
||
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ROK',OKR.ROK);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'MIESIAC',OKR.MC);

   exec('mp_run','#b__box',_params)
|| FUN.info('Okres nie jest otworzony dla umów.'@)
?};
OKR.cntx_pop()


\rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła parametru - ROK
::   WE:
::   WY: rok lub ~~ jeśl nie wybrano roku
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
OKR.last();
_wer:=OKR.mk_sel('Wybór roku'@,,0,,,,OKR.size());
OKR.win_fld(_wer,,'ROK',,,20,,,'Rok'@);
OKR.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
OKR.win_sel(_wer);
{? OKR.select(,1,OKR.size()) || _wyn:=OKR.ROK ?};
OKR.cntx_pop();
_wyn


\miesiac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła parametru - OKRES
::   WE:
::   WY: miesiąc lub ~~ jeśl nie wybrano roku
::----------------------------------------------------------------------------------------------------------------------
_wyn:=~~;
_Tab:=tab_tmp(1
   ,'MC'    ,'INTEGER'     ,'Nr miesiąca'
   ,'NAZ'   ,'STRING[20]'  ,'Nazwa miesiąca');
{! _ii:=1..12 |! _Tab.MC:=_ii; _Tab.NAZ:=(date(,_ii)$8)-5; _Tab.add() !};
_Tab.find_key(date()~2);
_wer:=_Tab.mk_sel('Wybór miesiąca'@,,0,,,,12);
_Tab.win_fld(_wer,,'NAZ',,,,,,'Miesiąc'@);
_Tab.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_Tab.win_sel(_wer);
{? _Tab.select(,1,12) || _wyn:=_Tab.MC ?};
_wyn


\symbol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Buduje symbol do ZLP
::   WE: _a - ref SQL ZLP
::   WY: symbol
::----------------------------------------------------------------------------------------------------------------------
_res:=exec('FindAndGet','#table',ZLP,_a,
       ,"_sym:='';
         @.ZLE.cntx_psh();
         @.ZLE.use('zlecen_'+(1+(3-ZLP.name())));
         _sym:={? ZLE().CZUM='N' || '' || UM().SYM+': ' ?}+
               {? ZLE().POS<>null()
               || '%1, %2, %3'[ZLE().POS().MIA().NAZ,ZLE().POS().UL().UL,ZLE().POS().NR]
               || 'brak posesji'
               ?};
         @.ZLE.cntx_pop();
         _sym
        ",'Zgłoszenie');
_res

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:50 32d8d634328a45e78569037682965f31250126ab59fb2b1e90ac5c49bcade175f85db7671df9f095da1bb9e75dcf03c7fc0aaaad55125e028489c42d0c72caf57c167431da0529f1ca9a71b6d4f6a571cbac4b77bf94f064b56ebfe890975129b41cefb2308d00d9594df86481a31efa1c4b0b37ade8773c44cd1fa8d59bfb6e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmg_mag_dwyd.fml
:: Utworzony: 05.08.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa dokumentu magazynowego wydania
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# permissions=ODDZ,LMG,TARS
::# parses=exec('parses_nd','lmg')
::# access=exec('sprUprMag','%todo')
::# kind=WE, symbol=TYPYDOK, type=_TYPYDOK, name=Typ dokumentu magazynowego,    required=N, fml_val="exec('typydok','!lmg_mag_dwyd')", fml_exp="exec('typydok_export','magdok_nag',_a)"
::# kind=WE, symbol=ND,      type=_ND,      name=Dokument magazynowy,           required=N, keyref=T
::# kind=WE, symbol=RODZAJ,  type=STRING,   name=Rodzaj dokumentu,              required=N, fml_val="exec('rodzaj','magdok_nag')"
::# kind=WY, symbol=ND,      type=_ND,      name=Dokument magazynowy,           required=N
::# kind=WY, symbol=NDPOW,   type=_ND,      name=Dokument magazynowy powiązany, required=N
::# kind=WY, symbol=RESULT,  type=STRING,   name=Rezultat działania,            required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_context:=params_get().context;

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_service:=_mp.isService();
_proces:=_mp.pathProc();
_group:=_mp.isGroup();

{? _proces & ~_service
:: Sprawdzenie parametrów pracy dla czynności startowej
|| {? ST.AR=0 | ST.ODDZ_KOD='' | ST.MAG=null() || FUN.info('Ustaw parametry pracy.'@); _wyn:=0; return() ?}
?};

{? var_pres('RESULT',_out)<>type_of('') || _out.RESULT:='OK'; _mp.save('OUT','RESULT','OK') ?};

_rodzaj:={? var_pres('RODZAJ',_in)=type_of(~~) || '' || _in.RODZAJ ?};
_nd_ref:={? var_pres('ND',_in)=type_of(~~) || null() || _in.ND ?};
{? _nd_ref<>null() || _out.ND:=_nd_ref; _mp.save('OUT','ND',_nd_ref) ?};
_auto:=_nd_ref<>null() & (_mp.isAutoRun() | _service);
_autoakc:=exec('autoAkc','#b__box',_mp,600080,'LMG_MAG_EAMG');

{? _mp.pathTodo()
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('ND',_out)=type_of(null()) & _out.ND
   || 1
   |? ~_mp.isMicro()
   || _akcja:='START_TODO'
   || _mp.error('Brak parametru wyjściowego ND.')
   ?}
?};
:: Sprawdzenie uprawnień
{? params_exec('permissions_chk','lmg')=0 || return() ?};

:: Wyzwalacz, który po dodaniu nagłówka dokumentu:
:: - add: dodaje rekord kluczowy nagłówka dokumentu
::   del: usuwa rekord kluczowy nagłówka dokumentu
:: - add: zapisuje parametr wyjściowy ND = wskazanie na nagłówek dokumentu
::   del: zapisuje parametr wyjściowy ND = null
_mp.trigRef('ND',,1,,exec('kind_out','#b_port'),'ND');

{? _service & _nd_ref=null()
|| _mp.error('Dla czynności typu serwis brak wymaganego parametru ND.')

|? var_pres('ND',_out)=type_of(null()) & exec('FindAndGet','#table',ND,_out.ND,,"ND.STAT_REJ",'')<>'N' & ~_group
|| _mp.done()

|? ~_service &
   (_akcja='Dołącz'
    | (_proces & _nd_ref=null())
    | _akcja='START_TODO')
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
|| _typydok_t:=
      {? var_pres('TYPYDOK',_in)=type_of(~~)
      || {? _akcja='Dołącz'
         || BEER.TYPYDOK().T
         || ''
         ?}
      |? var_pres('TYPYDOK',_in)=type_of(null())
      || exec('FindAndGet','#table',TYPYDOK,_in.TYPYDOK,,"TYPYDOK.T")
      || ''
      ?};

:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
   params_set('out',_out,'mp',_mp,'akcja',_akcja);
   exec('n_add','magdok_nag',0,_typydok_t,,_rodzaj);

:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? var_pres('ND',_outa)<>type_of(~~) & _outa.ND
:: Dodano dokument
   ||
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      {? _mp.pathArea() || ND.seek(_outa.ND) ?}
:: Wycofanie czynności bo nie dodano dokumentu
   || _mp.cancel()
   ?}

|? _auto | (_akcja='Zakończ_wer' & _mp.pathArea())
||
:: dla serwisu ustalamy jego środowisko
   _msk:=ref_name(_nd_ref)+3;
   {? _nd_ref<>null()
   || exec('mag_psh','open_tab');
      {? _service  | (_auto & _msk<>(ND.name()+3))
      || exec('mag_open','open_tab',(1+_msk),(_msk+2))
      ?};
      ND.prefix();
      {? ND.seek(_nd_ref)
      || {? _service
         || ST.MAG:=ND.MAG;
            ST.AM:=ND.AM;
            ST.AR:=ND.AR
         ?}
      || _mp.error('Nie znaleziono dokumentu.')
      ?}
   ?};
   _arg:=
      {? var_pres('_context')<>type_of(~~) & var_pres('AKC_DOK',_context)=type_of(obj_new('a'))
      || _context.AKC_DOK
      || exec('akc_dok_a','magdok_nag')
      ?};
   _arg.SERVICE:=_mp.isService();
   {? var_pres('GROUP',_arg)
   || _arg.GROUP:=_group
   ?};
   {? exec('nd_zakoncz','magdok_nag',_auto,_arg,_autoakc)
   || _res:=1;
      ND.cntx_psh();
      {? BEER.NDPOW<>null()
      || _name:=ND.name();
         _namm:=ref_name(BEER.NDPOW);
         {? _namm<>_name
         || ND.cntx_psh();
            ND.use(_namm)
         ?};
         ND.clear();
         {? ND.seek(BEER.NDPOW)
         || _mp.save('OUT','NDPOW',BEER.NDPOW);
            _mp.save('OUT','ND',exec('FindAndGet','#table',ND,BEER.NDPOW().NDSQL,,,null()))
         || _res:=0
         ?};
         {? _namm<>_name
         || ND.use(_name);
            ND.cntx_pop()
         ?}
      ?};
      ND.cntx_pop();
      {? _res || _mp.done() || _mp.cancel() ?}
   |? _service
   || _out.RESULT:='BŁĄD';
      _mp.save(,_out);
      _mp.done()
   ?};
   {? _nd_ref<>null() || exec('mag_pop','open_tab') ?}

|? _akcja='Popraw'
   | _mp.pathTodo()
||
   _arg:=
      {? var_pres('_context')<>type_of(~~) & var_pres('POPRAW',_context)=type_of(obj_new('a'))
      || _context.POPRAW
      || exec('nag_edit_a','magdok_nag')
      ?};
   {? var_pres('ND',_out)=type_of(null())
:: Uruchomione w procesie
   || ND.cntx_psh();
      TYPYDOK.cntx_psh();
      ND.prefix();
      TYPYDOK.prefix();
      {? ND.seek(_out.ND) & TYPYDOK.seek(ND.TYP)
      || exec('nd_win_edit_set','magdok_nag');
         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('nag_edit','magdok_nag',_arg);

         _mp.descTodo()
      ?};
      ND.cntx_pop();
      TYPYDOK.cntx_pop()
   |? _mp.pathTodo()
:: Uruchomione zadanie z listy todo i brak _out.ND wtedy zadanie wycofujemy
   || _mp.cancel()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      params_set('out',_out,'mp',_mp,'akcja',_akcja);
      {? exec('nag_edit','magdok_nag',_arg)
      || _mp.done()
      || _mp.cancel()
      ?}
   ?}

|? _akcja='Usuń'
|| {? var_pres('ND',_out)=type_of(null())
:: Uruchomione w procesie
   || _nd:=null();
      ND.cntx_psh();
      _rec:=exec('rec_after_del','#table',ND);
      ND.prefix();
      {? ND.seek(_out.ND)
      ||
         _wyn:=exec('n_usun','magdok_nag');

         {? ~ND.seek(_out.ND)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _nd:=ND.ref()
      ?};
      ND.cntx_pop();
      {? _wyn=1 & _rec<>null() || ND.seek(_rec) ?};
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _mp.pathArea() || {? _nd || ND.seek(_nd) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      exec('n_usun','magdok_nag');

      _mp.done()
   ?}

|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_in','#b_port'));

_ref:=
   {? var_pres('ND',_out)=type_of(null()) & _out.ND || _out.ND
   |? var_pres('ND',_in)=type_of(null()) & _in.ND || _in.ND
   || _krefs:=_mp.getRefs();
      {? var_pres('[1]',_krefs)=type_of('') & _krefs[1]<>''
      || exec('FindAndGet','#table',ND,_krefs[1],,,null())
      || null()
      ?}
   ?};

{? _ref
|| 'Zakończ rejestrację w magazynie %1 '
   'dokumentu wydania %2'@@[exec('FindAndGet','#table',ND,_ref,,"ND.MAG().SYM",''),exec('record','#to_string',_ref)]
|| 'Zarejestruj dokument wydania magazynowego'@@
?}


\typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na wartość parametru TYPYDOK
::   WY: TYPYDOK.ref()
::----------------------------------------------------------------------------------------------------------------------
_prod:=exec('tte_lic','tte');
exec('typ_dok','lmg','"TYPYDOK".P=\'N\''
    +{? _prod='N' || ' and "TYPYDOK".WYR=\'N\'' || '' ?}
    +' and "TYPYDOK".DK<>\'W\''
    +' and not ("TYPYDOK".T in (\'INW-\',\'PRC-\',\'KR-\',\'KN-\'))'
     ,,,0,0,,,,,-1)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 cd562e921792add19a2c8a2acdc080dc9db13a79d728168781b4f1c5a28e2c432a71f5d38dab1bbcd7b0683a5dd15924459cb743a767461879630ed5c5bef4ff4d06c76b6bd492486fba41dc1d18b16c4bd581f0a6b30059364ca3dea1e4c52f548e1be5ff540f2045d6425284291f4d869a16cca3ad733d759b52b3f5246b09

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_fktt.fml
:: Utworzony: 09.02.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Typy transakcji kasowych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Czynność ZWS_PAR_FKTO - typy operacji
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejsciowe czynnosci
::       _int - [obj_new] - parametry wewnetrzne czynnosci
::       _out - [obj_new] - parametry wyjsciowe czynnosci
::       _mp  - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('mf3_poz','!zws_par_fktt')


\mf3_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: definicja transakcji
::  OLD: \mf3_poz/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.win_patt('SZUK1');
POZOPER.index('KSIEG');
POZOPER.prefix();
POZOPER.win_sel('SLO2');
POZOPER.win_edit('RED'+{? KPARAM.CZY_B='T' || '1' || '2' ?});
POZOPER.select(,,,{? PAR_SKID.get(80)='N' || 'F' || '' ?})


\blank_oper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Wartosc poczatkowa pola POZOPER.OPER
::  OLD: \blank_oper/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? -(6+menu_txt)<>'szukaj' || OPER.ref() || null ?}


\beoperpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Przed redakcja POZOPER.OPER
::  OLD: \beoperpo/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.efld_opt(POZOPER.win_edit('?'),{? POZOPER.MF_TIP='T' || 'mark=1' || 'mark=0' ?},,'ODDZ');
{? {? cur_afld()='OPER' || var_pres('__blamws')<>type_of(1) || 1 ?}
|| exec('count','!zws_par_fktt')=0
?}


\oper_nazwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Wartosc poczatkowa pola POZOPER.NAZWA
::  OLD: \oper_nazwa/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
OPER.NAZWA


\bl_oper_pm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wartosc poczatkowa POZOPER.PLUS_MIN
::  OLD: \bl_oper_pm/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__blamws')=type_of(1) & __blamws=1
|| OPER.PLUS_MIN
|| 'N'
?}


\bemf_tip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola POZOPER.MF_TIP
::  OLD: \bemf_tip/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.FIKS_TIP<>'T'


\aemf_tip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula po redakcji pola POZOPER.MF_TIP
::   WE:
::   WY:
::  OLD: \aemf_tip/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? -POZOPER.MF_TIP='t'
|| {? ~(exec('count','!zws_par_fktt')<>0 & -POZOPER.VAT='t')
   || POZOPER.VAT:=POZOPER.ZMDOMREJ:=POZOPER.ZMDOMRVT:=POZOPER.ZMDOMGRU:='N';
      POZOPER.GRPODDOM:=POZOPER.STVATDOM:=POZOPER.RVATDOM:=null;POZOPER.REJDOM:='';
      POZOPER.DMDOKREJ:=null; POZOPER.ZMDDKREJ:='N'
   || FUN.info('Istnieją pozycje dokumentów z daną transakcją.'@
              +'\nZmiana znacznika wymusiłaby wyłączenie obsługi VAT.'@);
      POZOPER.MF_TIP:='N'; 0
   ?};
   POZOPER.efld_opt(POZOPER.win_edit('?'),'mark=1',,'ODDZ')
|| POZOPER.STS:=POZOPER.ODDZ:=POZOPER.TYPYSP:=null();
   POZOPER.efld_opt(POZOPER.win_edit('?'),'mark=0',,'ODDZ')
?};
1


\befkstip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola POZOPER.FIKS_TIP
::  OLD: \befkstip/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.MF_TIP<>'T'


\aefkstip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Po redakcji pola POZOPER.FIKS_TIP
::  OLD: \aefkstip/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? -POZOPER.FIKS_TIP='n' || POZOPER.ANALIT:=SLOSLU.SLSLU:='' ?}; 1


\bepozana
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Przed redakcja pola POZOPER.ANALIT
::  OLD: \bepozana/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.FIKS_TIP='T'


\bevatpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pola POZOPER.VAT
::   WE:
::   WY:
::  OLD: \bevatpo/bevatpo.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('count','!zws_par_fktt')=0 & -POZOPER.MF_TIP='n' &
   exec('get_par','#parametr',90)='T'
|| 1
|| 0
?}


\aevatpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula po redakcji pola POZOPER.VAT
::   WE:
::   WY:
::  OLD: \aevatpo/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? -POZOPER.VAT='n'
|| POZOPER.GRPODDOM:=POZOPER.STVATDOM:=POZOPER.RVATDOM:=null; POZOPER.REJDOM:='';
   POZOPER.ZMDOMREJ:=POZOPER.ZMDOMRVT:=POZOPER.ZMDOMGRU:='N';
   POZOPER.DMDOKREJ:=null; POZOPER.ZMDDKREJ:='N'
|| POZOPER.ZMDOMREJ:=POZOPER.ZMDOMRVT:=POZOPER.ZMDOMGRU:=POZOPER.ZMDDKREJ:='T'
?}; 1


\begrpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pol POZOPER.GRPODDOM,
::       POZOPER.RVATDOM, POZOPER.DMDOKREJ
::   WE:
::   WY:
::  OLD: \begrpod/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? -POZOPER.VAT='t' & POZOPER.REJDOM<>''
|| REJ.index('KOD'); REJ.prefix(exec('blrejrok','rejestry'),exec('blrejodd','rejestry'));
   REJ.find_key(POZOPER.REJDOM,POZOPER.REJDOM)
|| 0 ?}


\aegrpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula po redakcji pola POZOPER.GRPODDOM
::   WE:
::   WY:
::  OLD: \aegrpod/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.GRPODDOM=null || POZOPER.ZMDOMGRU:='T' ?}; 1


\beslovat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pola POZOPER.STVATDOM
::   WE: _a - nazwa slownika
::   WY:
::  OLD: \beslovat/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? -POZOPER.VAT='t'
|| SLU.index('NAZ'); SLO.index('SL');
   SLU.prefix(); SLU.find_key(_a); 1
|| 0
?}


\aedrej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS:Formula po redakcji pola POZOPER.RVATDOM
::   WE:
::   WY:
::  OLD: \aedrej/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.RVATDOM=null || POZOPER.ZMDOMRVT:='T' ?}; 1


\bepvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pola POZOPER.REJDOM
::   WE:
::   WY:
::  OLD: \bepvat/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? -POZOPER.VAT='t' || 1 || 0 ?}


\f3_rejk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [8.60]
:: OPIS: Akcja klawisz F3 dla pol zawierajacych REJ.KOD
::  OLD: \f3_rejk/skid_rej.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;

ROK_F.cntx_psh();
ODD.cntx_psh();
ROK_F.index('KOD');
ROK_F.prefix(REF.FIRMA);
ROK_F.win_sel('ROK');
{? ROK_F.select()
|| SSTALE.AR:=ROK_F.ref();
   ODD.index('ODDZIALY');
   ODD.prefix(REF.FIRMA);
   ODD.win_sel('WYB');
   {? ODD.select()
   || OPERATOR.DEPT:=ODD.ref();
      REJ.index('KOD');
      REJ.prefix(exec('blrejrok','rejestry'),exec('blrejodd','rejestry'));
      _o:=REJ.win_sel('?');
      REJ.win_sel('SLO');
      {? _ok & REJ.select()
      || _r:=REJ.KOD
      || _r:=fld()
      ?};
      REJ.win_sel(_o)
   ?}
|| _r:=fld()
?};
ODD.cntx_pop();
ROK_F.cntx_pop();
_r


\aerejtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula po redakcji pola POZOPER.REJDOM
::   WE:
::   WY:
::  OLD: \aerejtr/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? POZOPER.REJDOM=''
|| POZOPER.GRPODDOM:=POZOPER.DMDOKREJ:=POZOPER.RVATDOM:=null;
   POZOPER.ZMDOMREJ:=POZOPER.ZMDOMGRU:=POZOPER.ZMDDKREJ:=POZOPER.ZMDOMRVT:='T'
|? POZOPER.REJDOM<>''
|| _continue:=1;
   REJ.cntx_psh();
   {? SSTALE.AR=null() | OPERATOR.DEPT=null()
   || {? SSTALE.AR=null()
      || ROK_F.index('NAZWA');
         ROK_F.prefix(REF.FIRMA);
         SSTALE.AR:={? ROK_F.find_key($(date()~1)) || ROK_F.ref() || null() ?}
      ?};
      {? SSTALE.AR
      || REJ.index('RKKO');
         REJ.prefix(exec('blrejrok','rejestry'));
         {? REJ.find_key(fld())
         || SSTALE.AR:=REJ.ROK;
            OPERATOR.DEPT:=REJ.ODD
         || FUN.info('Należy wybrać wartość ze słownika.'@);
            _continue:=0
         ?}
      || _continue:=0
      ?}
   ?};
   {? _continue
   || REJ.index('KOD'); REJ.prefix(exec('blrejrok','rejestry'),exec('blrejodd','rejestry'));
      {? ~REJ.find_key(POZOPER.REJDOM,POZOPER.REJDOM)
      || FUN.info('Brak rejestru %1 w aktualnym roku.'@[POZOPER.REJDOM]);_ok:=0
      || {? POZOPER.GRPODDOM<>null | POZOPER.DMDOKREJ<>null ||
            GR_VAT.cntx_psh();
            GR_VAT.clear(); _a:=POZOPER.GRPODDOM().GRVAT;
            GR_VAT.index('REJ_GR'); GR_VAT.prefix(REJ.ref(),_a);
            {? ~GR_VAT.first() || POZOPER.GRPODDOM:=null ?};
            GR_VAT.cntx_pop();
            DOK_REJ.cntx_psh(); _a:=POZOPER.DMDOKREJ().NAZ;
            DOK_REJ.index('NAZ'); DOK_REJ.prefix(REJ.ref(),_a);
            {? ~DOK_REJ.first() || POZOPER.DMDOKREJ:=null ?};
            DOK_REJ.cntx_pop();
            VAT_REJ.cntx_psh(); _a:=POZOPER.RVATDOM().RVAT;
            VAT_REJ.index('VAT_REJR'); VAT_REJ.prefix(_a,REJ.ref());
            {? ~VAT_REJ.first() || POZOPER.RVATDOM:=null ?};
            VAT_REJ.cntx_pop()
         ?}
      ?}
   ?};
   REJ.cntx_pop()
?};
_ok


\aedmdokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula po redakcji pola POZOPER.DMDOKREJ
::   WE:
::   WY:
::  OLD: \aedmdokr/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.DMDOKREJ=null || POZOPER.ZMDDKREJ:='T' ?}; 1


\bedrej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pola POZOPER.ZMDOMREJ
::   WE:
::   WY:
::  OLD: \bedrej/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.REJDOM<>'' || 1 || 0 ?}


\aezmdrej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula po redakcji pola POZOPER.ZMDOMREJ
::   WE:
::   WY:
::  OLD: \aezmdrej/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.ZMDOMREJ='T'
|| POZOPER.ZMDOMGRU:=POZOPER.ZMDDKREJ:=POZOPER.ZMDOMRVT:='T'
?}; 1


\bezmdrej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pola POZOPER.ZMDDKREJ
::   WE:
::   WY:
::  OLD: \bezmdrej/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.ZMDOMREJ='T' | POZOPER.DMDOKREJ=null || 0 || 1 ?}


\bezmdmgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pola POZOPER.ZMDOMGRU
::   WE:
::   WY:
::  OLD: \bezmdmgr/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.ZMDOMREJ='T' | POZOPER.GRPODDOM=null || 0 || 1 ?}


\bedrvt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pola POZOPER.ZMDOMRVT
::   WE:
::   WY:
::  OLD: \bedrvt/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.ZMDOMREJ='T' | POZOPER.RVATDOM=null || 0 || 1 ?}


\be_zmdomodd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: pred redakcja pola ZMDOMODD
::  OLD: \be_zmdomodd/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.FIKS_TIP='T'


\aeconbox
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Po redakcji pola PKONTROL tabeli POZOPER
::  OLD: \aeconbox/skid_ktr.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt<>'dołącz' & (fld='N' | fld='')
|| CON_DEF.index('LP'); CON_DEF.prefix(POZOPER.ref());
   {? CON_DEF.first()
   || fld('T'); FUN.info('Istnieją definicje pozycji controllingowych.\nZmiana znacznika jest niemożliwa.'@); 0
   || 1
   ?}
|| 1
?}


\poz_typb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola POZOPER.TYPYSP
::  OLD: \poz_typb/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.STS<>null


\poz_magb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola POZOPER.STS
::  OLD: \poz_magb/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.ODDZ<>null


\poz_maga
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji pola POZOPER.STS
::  OLD: \poz_maga/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.STS=null || POZOPER.TYPYSP:=null ?};
1


\be_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Przed redakcja pola POZOPER.ODDZ
::  OLD: \be_oddz/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.MF_TIP='T'


\ae_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji pola POZOPER.ODDZ
::  OLD: \ae_oddz/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.ODDZ=null
|| POZOPER.TYPYSP:=POZOPER.STS:=null
|? POZOPER.STS<>null
|| STS.cntx_psh();
   _kod:=POZOPER.STS().KOD;
   STS.index('KOD');
   STS.prefix('S',POZOPER.ODDZ().KOD,_kod);
   {? ~(STS.first() & STS.KOD=_kod) || POZOPER.TYPYSP:=POZOPER.STS:=null ?};
   STS.cntx_pop()
?};
1


\prdelpop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: Przed usuń pozycja operacji
::   WE:
::   WY:
::  OLD: \prdelpop/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZOPER.count()
|| FUN.info('Typ transakcji wykorzystywany w systemie.\nUsunięcie niemożliwe.'@);
   0
|| 1
?}


\count
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Sprawdzenie, czy wystepuja jakies pozycje dokumentow z dana transakcja
::   WE:
::   WY:
::  OLD: \count/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_count:=0;
{? -menu_txt='popraw'
|| POZOPER.cntx_psh();
   _count:=POZOPER.count();
   POZOPER.cntx_pop()
?};
_count


\wzslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula na wzorzec dla pol POZOPER.STVATDOM
::   WE: _a - nazwa slownika
::   WY:
::  OLD: \wzslo/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
SLO.win_dict('ONE'); SLO.win_sel('ONE');
SLO.hdr_sel(); SLO.hdr_sel('Stawki VAT'@);
SLU.index('NAZ'); SLO.index('SL');
SLU.prefix(); SLU.find_key(_a);
''




\pozoper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Dolacza rekordy do tabeli POZOPER przy imporcie
::   WE: _a - wczytany rekord
::  OLD: \pozoper/demo_ks.fml
::----------------------------------------------------------------------------------------------------------------------
POZOPER.blank();
_i:=_a*'@';   _op_kod:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _nazwa:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _kod:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _plusmin:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _mf_tip:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _fikstip:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _analit:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _vat:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _grejkod:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _grkod:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _stvatsl:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _stvatko:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _rvatsym:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _rejkod:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _rejdom:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _rejdr:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _dokr:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     POZOPER.ZMDOMREJ:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     POZOPER.ZMDOMGRU:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     POZOPER.ZMDOMRVT:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     POZOPER.DOMTYP:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     POZOPER.ZM_OBR:=(_i-1)+_a;
_i:=_a*'@';   _a:=_i-_a;  _i:=_a*'@';     _slu:=(_i-1)+_a;
{? OPER.find_key(_op_kod) & ~POZOPER.find_key(_op_kod,_kod)
|| POZOPER.OPER:=OPER.ref(); POZOPER.NAZWA:=_nazwa;
   POZOPER.KOD:=_kod;  POZOPER.PLUS_MIN:=_plusmin;
   POZOPER.MF_TIP:=_mf_tip;
   POZOPER.FIKS_TIP:=_fikstip;
   POZOPER.ANALIT:=_analit;
   POZOPER.VAT:=_vat;
   VAT_REJ.index('VAT_REJ'); VAT_REJ.prefix();
   RVAT.index('SYM'); RVAT.prefix(REF.FIRMA);
   {? |_grejkod<>'' & |_grkod<>'' &
      REJ.find_key(_grejkod,_grejkod) & GR_VAT.find_key(REJ.ref(),_grkod)
   || POZOPER.GRPODDOM:=GR_VAT.ref()
   ?};
   {? |_stvatsl<>'' & |_stvatko<>'' &
      SLU.find_key(_stvatsl) & SLO.find_key(SLU.ref(),_stvatko)
   || POZOPER.STVATDOM:=SLO.ref()
   ?};
   {? |_rvatsym<>'' & |_rejkod<>'' & RVAT.find_key(_rvatsym,_rvatsym) & VAT_REJ.find_key(RVAT.ref(),_rejkod)
   || POZOPER.RVATDOM:=VAT_REJ.ref()
   ?};
   {? |_rejdom<>''
   || POZOPER.REJDOM:=_rejdom
   ?};
   {? |_rejdr<>'' & |_dokr<>'' & REJ.find_key(_rejdr,_rejdr) & DOK_REJ.find_key(REJ.ref,_dokr)
   || POZOPER.DMDOKREJ:=DOK_REJ.ref()
   ?};
   POZOPER.DOTYCZY:={? |_slu<>'' & SLU.find_key(_slu) & SLU.NAZ=_slu || SLU.ref || null ?};
   {? ~POZOPER.find_rec() || POZOPER.add() ?}
?};
1

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:04 be4996aab687fa459a7570ef9a4e7efca171da3e8fc78f5fb9977a68f23ae4b064a651fcb67692a671498fcc11287ac8ae7f6cd488495beef913064e2234b2be23e2485df067b8a51b5e4d9514f26b239309581fb6d5a1bb0e6069ab631e5fe6bc8e7107952789ab88e3bd688b7130449eae0d22a61851912faf83430829fe9f

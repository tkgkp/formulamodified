:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !luo_kpo_wwkp.fml
:: Utworzony: 28.02.2019
:: Autor: Markus
::======================================================================================================================
:: Zawartość: Utworzenie karty przekazania odpadów w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# kind=WE,   symbol=KPO,    type=_KPO,  name=Karta przekazania odpadów, required=T,    keyref=T
::# kind=WE,   symbol=CZY_PDF, type=STRING, name=Utwórz PDF,           required=N,    keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE,   symbol=PRN_MSK, type=STRING, name=Maska wydruku,        required=N,    keyref=N, fml_val="exec('rep_exec','#b_report','LUO_KPO_WWKP','luo_kpo*','Wydruki kart przekazania odpadów',-1)"
::# kind=WY,   symbol=DOKUM,   type=_DOKUM, name=Załącznik,            required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

_akcja:=_mp.akcja();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' ?};
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || {? exec('ctrlMask','#b_report',_in.PRN_MSK,'luo_kpo') || _in.PRN_MSK || '' ?}
      || ''
      ?};

{? var_pres('KPO',_in)=type_of(null()) & _in.KPO
||
:: Ustawienie kontekstu wg instancji elementu w procesie

   KPO.cntx_psh();

   _use:=ref_name(_in.KPO)<>KPO.name();
   {? _use || exec('kpo_open','open_tab',(6+ref_name(_in.KPO))+1,(8+ref_name(_in.KPO))+2) ?};
   KPO.prefix();
   {? KPO.seek(_in.KPO)
   ||
      _dokum:={? VAR.GRUPA<>'T' || exec('kpo_druk','!luo_kpo_wwkp',_czy_pdf,_msk) || null ?};
      {? _czy_pdf & _dokum<>null()
      ||
         _out.DOKUM:=_dokum;
         _mp.save(,_out);
         {? ~_keep || _mp.done() ?}

      |? ~_czy_pdf & _dokum
      ||
         {? ~_keep || _mp.done() ?}
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};

   KPO.cntx_pop()

||
   _mp.error('Brak parametru wejściowego KPO.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('KPO',_in)<>type_of(~~) & _in.KPO
|| 'Utwórz PDF karty przekazania odpadów: %1'@@[exec('record','#to_string',_in.KPO)]
|| ''
?}


\kpo_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: wydruki dla kart przekazania odpadów
::   WE: [_a] - 1-PDF 0-normalny wydruk
::       [_b] - maska wydruku
::   WY: dla _a=1: ref/null _a=0: 0/1
::  OLD: \zam_druk/zk2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
_msk:={? var_pres('_b')=type_of('') || _b || '' ?};
_wyn:={? _a || null || 0 ?};

{? _msk<>''
|| _wydr:=_msk
|| _wydr:='luo_kpo*'
?};

{? _a=1
||
   _symb:=exec('str_to_pth','#string',KPO.SYM);
   _file:='Karta przekazania odpadow '+_symb+'.pdf';
   _wydr:=rep_exec(_wydr,,,_file,1)
|| _wydr:=exec('rep_exec','#b_report','LUO_KPO_WWKP',_wydr,'Karta przekazania odpadów',2,,,,,,'W')
?};

{? _a=1 & _wydr=1
|| _wyn:=exec('save_dok','dokum','KPO',_file,POLA.NAZ_WYDR,KPO.SYM,KPO.KH().EM
      ,'Karta przekazania odpadow: '+KPO.SYM,PARAM_W.JEZYK,'N')
|? _a=0
|| _wyn:=_wydr
?};

_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 47772b9f7e4bc46ba7d36719fdfa2b55bee3387ff303c6a44d18d56d5dbfa1d1ea42757c2c86b3f071c7bd38f0772d70590e91ede507a42863de48bbcbb3087e2db3b027694834431b7f3f7dcefd78e16b55d74989d1e02e79ba63c63899c80b61a8559d1576105a2894e3ffaf31b36e606b970316cd0d3fd1018ad67a453d5d

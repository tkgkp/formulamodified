:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_poj_edit.fml
:: Utworzony: 2017.05.12
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_POJ_EDIT - Edycja danych o pojazdach
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Formuła główna czynności ZWS_POJ_EDIT
::----------------------------------------------------------------------------------------------------------------------
::# parses=exec('parses','!zws_poj_edit')
::# access=exec('uprawnienia','!zws_poj_edit')
::
:: PARAMETRY WE:
::# kind=WE, symbol=SRSR, type=_SRSR, name=Wskazanie na środek trwały, required=N
::# kind=WE, symbol=POJAZD, type=_POJAZDY, name=Wskazanie na pojazd, required=N
:: PARAMETRY WY:
::# kind=WY, symbol=POJAZD, type=_POJAZDY, name=Wskazanie na pojazd, required=T
::# kind=WY, symbol=SRSR, type=_SRSR, name=Wskazanie na środek trwały, required=N
_mp:=params_get().mp;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_akcja:=-_mp.akcja();

_ok:=1;
_srsr:=null;
{? _mp.pathProc()
|| _akcja:='dołącz';
   menu_txt(,'Dołącz');
   exec('maski','srodobj');
   exec('set_srsr','samochody',1)
::   {? SRSR.select() || _srsr:=SRSR.ref() || _ok:=0 ?}
|? _mp.pathTodo()
|| _akcja:='dołącz';
   menu_txt(,'Dołącz');
   {? var_pres('SRSR',_we) & type_of(_we.SRSR)=type_of(null) & _we.SRSR<>null
   || SRSR.prefix();
      {? ~SRSR.seek(_we.SRSR)
      || FUN.emsg('Nie znaleziono wskazanego środka, rejestracja dokumentu nie jest możliwa.'@);
         _ok:=-1
      || _srsr:=SRSR.ref();
:: jeżeli środek nie jest pojazdem to puste przejście do dalszych czynności
         {? SRSR.POJAZD<>'T'
         || {? SRSR.GRP='T'
            || _pojazd:=exec('zestawp','fst',SRSR.ref())
            || _pojazd:=0
            ?}
         || _pojazd:=1
         ?};
         {? ~_pojazd || _ok:=-2 ?}
      ?}
   || FUN.emsg('Brak środka trwałego, rejestracja dokumentu nie jest możliwa.'@);
      _ok:=-1
   ?}
|? _mp.pathArea()
|| {? var_pres('POJAZD',_we) & type_of(_we.POJAZD)=type_of(null) & _we.POJAZD<>null
   || POJAZDY.prefix();
      {? ~POJAZDY.seek(_we.POJAZD)
      || FUN.emsg('Nie znaleziono wskazanego pojazdu, rejestracja nie jest możliwa.'@);
         _ok:=-1
      ?}
   ?}
?};

_poj_rf:=null;
{? _ok=1
|| {? _akcja='dołącz' || _poj_rf:=exec('poj_add','!zws_poj_edit',_srsr)
   |? _akcja='popraw' || _poj_rf:=exec('poj_pop','!zws_poj_edit')
   |? _akcja='usuń' || {? exec('poj_del','!zws_poj_edit') || _ok:=-1 || _ok:=0 ?}
   ?}
?};

{? _poj_rf
|| {? ~_mp.isMicro() & (type_of(_poj_rf)=type_of(null))
   || POJAZDY.cntx_psh();
      POJAZDY.prefix();
      {? POJAZDY.seek(_poj_rf)
      || _wy.POJAZD:=POJAZDY.ref();
         _wy.SRSR:={? _srsr=null || POJAZDY.SRSR || _srsr ?};
         _mp.keyRef(POJAZDY.uidref());
         _mp.save(,_wy);
         POJAZDY.cntx_pop()
      || _mp.error();
         POJAZDY.cntx_pop();
         return()
      ?}
   ?};
   _mp.done()
|? _poj_rf=null & _ok=-1
|| _wy.POJAZD:=null;
   _wy.SRSR:=null;
   _mp.save(,_wy);
   _mp.done()
|? _poj_rf=null & _ok=-2
|| _wy.POJAZD:=null;
   _wy.SRSR:=_srsr;
   _mp.save(,_wy);
   _mp.done()
|| _mp.cancel()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('SRSR',_in)=type_of(null()) & _in.SRSR & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
|| 'Zarejestruj zasób typu pojazd na podstawie środka: %1.'@@[SRSR.NRI]
|| 'Zarejestruj zasób typu pojazd.'@@
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Formuła ustala PARSES dla czynności na podstawie rekordu środka
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_in:=params_get().in;
_mp:=params_get().mp;
{? _mp.pathTodo()
|| {? var_pres('SRSR',_in) & var_pres('SRSR',_in)=type_of(null()) & _in.SRSR<>null
      & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
   || __PARSES.setVal('OkresRok',SRSR.OKRO_F);
      __PARSES.setVal('JednostkaKsięgowa',SRSR.ODD);
      _result:=1
   || _result:=1
   ?}
|| _result:=1
?};
_result


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh(); USERS.prefix();
{? USERS.seek(_user)
||
:: czy user ma uprawnienia do jednostki księgowej środka
   SRSR.cntx_psh();
   SRSR.prefix();
   {? var_pres('SRSR',_in)=type_of(null()) & _in.SRSR<>null() & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
   || {? exec('usr_fjks','b_perm',SRSR.ODD,USERS.ref())
      || _result:=1
      ?}
   || _result:=1
   ?};
   SRSR.cntx_pop()
?};
USERS.cntx_pop();
_result


\m_poj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Uruchomienie rejestracji pojazdu w obszarze roboczym
::----------------------------------------------------------------------------------------------------------------------
_stop:=0;
{?  POJAZDY.SRSR<>null
|| SRSR.cntx_psh();
   SRSR.prefix();
   {? SRSR.seek(POJAZDY.SRSR,SRSR.name())
   || {? SRSR.Z='T'
      || FUN.emsg('Wskazany środek został skreślony w kartotece środków trwałych. Akcja niedostępna.'@);
      _stop:=1
      ?}
   ?};
   SRSR.cntx_pop()
?};
{? ~_stop
||  _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='ZWS_POJ_EDIT';
   _params.AKCJA:=menu_txt();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'POJAZD',POJAZDY.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params)
?}


\poj_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Dołączanie pojazdów
::   WE: [_a] - opcjonalne wskazanie na środek trwały
::----------------------------------------------------------------------------------------------------------------------
POJAZDY.win_edit('RED'); POJAZDY.blank();
POJAZDY.memo_set(,'UWAGI');
SRSR.cntx_psh(); SRST.cntx_psh(); SRDO.cntx_psh();
{? SRSR.name()<>'srsrr'+REF.FIRMA().SYMBOL || SRSR.use('srsrr'+REF.FIRMA().SYMBOL) ?};
{? SRST.name()<>'srstr'+REF.FIRMA().SYMBOL || SRST.use('srstr'+REF.FIRMA().SYMBOL) ?};
{? SRDO.name()<>'srdor'+REF.FIRMA().SYMBOL || SRDO.use('srdor'+REF.FIRMA().SYMBOL) ?};
SRSR.prefix(); SRST.prefix(); SRDO.prefix();

{? _>0 & type_of(_a)=type_of(null) & _a<>null
||
   {? SRSR.seek(_a)
   || POJAZDY.SRSR:=SRSR.ref();
      SRST.index('PODAT');
      SRST.prefix(POJAZDY.SRSR);
      {? SRST.last()
      || {? POJAZDY.NAZ='' || POJAZDY.NAZ:=SRSR.NST ?};
         {? POJAZDY.DPS=date(0,0,0) || POJAZDY.DPS:=SRSR.DE ?}
      ?}
   ?}
?};
{? POJAZDY.edit("exec('spr_pojazdy','!zws_poj_edit')") & POJAZDY.add()
|| POJAZDY.memo_put(,'UWAGI');
:: dodanie zmian dla zasobu jeżeli istnieją dokumenty zmiany właściwości środka
   {? POJAZDY.SRSR<>null
   || SRDO.index('SRODZAJ');
      SRDO.prefix(POJAZDY.SRSR,'L');
      {? SRDO.first()
      || {! |?
            {? SRDO.TYP().JORG='T' | SRDO.TYP().OSOBA='T'
            || ZASOB_ZM.cntx_psh();
               ZASOB_ZM.prefix();
               exec('zsb_blank','zasoby_wspolne',POJAZDY.SRSR);
               ZM_ZASOB.ZASOBY:=ZASOB_ZM.ZASOBY;
               ZASOB_ZM.DATAOD:=SRDO.DO;
               ZASOB_ZM.OSOBA:=SRDO.OSOBA;
               ZASOB_ZM.JORG:=SRDO.JORG;
               ZASOB_ZM.SRDO_Z:=SRDO.ref();
               {? ZASOB_ZM.add() || exec('zam_poprz','zasoby_wspolne') ?};
               ZASOB_ZM.cntx_pop()
            ?};
            SRDO.next()
         !}
      ?}
   ?};
   _wy:=POJAZDY.ref()
|| _wy:=null
?};

SRSR.cntx_pop(); SRST.cntx_pop(); SRDO.cntx_pop();
_wy


\poj_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Poprawianie pojazdów
::----------------------------------------------------------------------------------------------------------------------
_wy:=null;
{? POJAZDY.get()
|| ZM_ZASOB.ZASOBY();
   {? ZASOBY.r_lock(1,1,1)
   || {? POJAZDY.r_lock(1,1,1)
      || POJAZDY.win_edit('RED');
         POJAZDY.memo_get(,'UWAGI');
         {? POJAZDY.edit("exec('spr_pojazdy','!zws_poj_edit')") & POJAZDY.put()
         || POJAZDY.memo_put(,'UWAGI');
            _wy:=POJAZDY.ref()
         ?}
      || FUN.info('Dane pojazdu wykorzystywane przez innego użytkownika.'@)
      ?};
      unlock_r()
   || FUN.info('Dane pojazdu wykorzystywane przez innego użytkownika.'@)
   ?}
|| FUN.info('Nie znaleziono pojazdu.'@)
?};
_wy


\poj_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Usuwanie pojazdów
::----------------------------------------------------------------------------------------------------------------------
_delr:=0;
{? POJAZDY.get()
|| ZM_ZASOB.ZASOBY();
   {? POJAZDY.r_lock(1,1,1) & ZASOBY.r_lock(1,1,1)
   || {? FUN.ask('Usunąć dane pojazdu?'@)
      || _przjazd:=exec('poj_przej','przejazdy',$POJAZDY.ref());
         {? ~_przjazd
         || do();
            _delr:=exec('del_zasob','zasoby_wspolne',1);
            {? _delr
            || exec('del_att','dok_fks',$POJAZDY.ref());
               _delr:=POJAZDY.del(,1);
               {? _delr=0 || undo() ?}
            || _delr:=0
            ?};
            end()
         || _delr:=0
         ?};
         {? ~_delr
         || FUN.info('Nie można usunąć danych pojazdu. Istnieją powiązane struktury.'@)
         ?}
      ?};
      unlock_r()
   || FUN.info('Dane pojazdu wykorzystywane przez innego użytkownika.'@);
      unlock_r()
   ?}
|| FUN.info('Nie znaleziono pojazdu.'@)
?};
_delr


\spr_pojazdy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Sprawdzanie danych pojazdów
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
_ref:={? -menu_txt()='popraw' || POJAZDY.ref() || null ?};
{? POJAZDY.NRREJ<>''
|| POJAZDY.cntx_psh(); POJAZDY.index('NRREJ'); POJAZDY.prefix(REF.FIRMA,POJAZDY.NRREJ,);
   {? POJAZDY.first() & (_ref=null | _ref<>POJAZDY.ref())
   || FUN.info('Znaleziono pojazd o podanym numerze rejestracyjnym.'@); _zwrot:='NRREJ'
   ?};
   POJAZDY.cntx_pop()
?};
{? _zwrot='' & POJAZDY.SRSR<>null
|| SRSR.cntx_psh();
   SRSR.prefix();
   {? ~SRSR.seek(POJAZDY.SRSR,SRSR.name())
   || FUN.emsg('Wskazany środek nie został odnaleziony w kartotece środków trwałych.'@);
      POJAZDY.SRSR:=null;
      _zwrot:='WLASNY'
   || {? SRSR.Z='T'
      || FUN.emsg('Wskazany środek został skreślony w kartotece środków trwałych.'@);
         _zwrot:='NAZ'
      |? SRSR.R='S'
      || FUN.emsg('Wskazany środek został zbyty na rzecz innej firmy z grupy kapitałowej.'@);
         _zwrot:='WLASNY'
      ?}
   ?};
   SRSR.cntx_pop()
?};
{? _zwrot=''
|| POJAZDY.cntx_psh(); POJAZDY.index('DISP'); POJAZDY.prefix(REF.FIRMA,POJAZDY.NAZ,POJAZDY.NRREJ,);
   {? POJAZDY.first() & (_ref=null | _ref<>POJAZDY.ref())
   || FUN.info('Znaleziono pojazd o podanej nazwie i numerze rejestracyjnym.'@); _zwrot:='NAZ'
   ?};
   POJAZDY.cntx_pop()
?};
{? _zwrot='' & POJAZDY.NAZ=''
|| FUN.info('Nie wprowadzono nazwy pojazdu.'@); _zwrot:='NAZ'
?};
{? _zwrot='' & POJAZDY.ZUZ_JEDN<0
|| FUN.info('Zużycie paliwa nie może być mniejsze od zera.'@); _zwrot:='ZUZ_JEDN'
?};
{? _zwrot='' & POJAZDY.ZUZ_JEDN<0
|| FUN.info('Zużycie paliwa nie może być mniejsze od zera.'@); _zwrot:='ZUZ_JEDN'
?};
{? _zwrot='' & POJAZDY.ST<0
|| FUN.info('Stan licznika nie może być mniejszy od zera.'@); _zwrot:='ST'
?};
{? _zwrot='' & POJAZDY.ST>0 & ~POJAZDY.JM
|| FUN.info('Nie podano jednostki stanu licznika.'@); _zwrot:='JM'
?};
{? _zwrot='' & POJAZDY.MOC<0
|| FUN.info('Moc silnika nie może być mniejsza od zera.'@); _zwrot:='MOC'
?};
{? _zwrot='' & POJAZDY.POJE<0
|| FUN.info('Moc silnika nie może być mniejsza od zera.'@); _zwrot:='POJE'
?};
{? _zwrot='' & POJAZDY.MW<0
|| FUN.info('Masa własna nie może być mniejsza od zera.'@); _zwrot:='MW'
?};
{? _zwrot='' & POJAZDY.MIEJSCA<0
|| FUN.info('Liczba miejsc nie może być mniejsza od zera.'@); _zwrot:='MIEJSCA'
?};
{? _zwrot='' & POJAZDY.SRSR<>null
|| {? -menu_txt()<>'dołącz' || _ref:=POJAZDY.ref() || _ref:=null ?};
   POJAZDY.cntx_psh();
   POJAZDY.index('FIRSRSR');
   POJAZDY.prefix(REF.FIRMA,POJAZDY.SRSR);
   {? POJAZDY.first() & POJAZDY.ref()<>_ref
   || FUN.emsg('Wskazany środek występuje już w bazie pojazdów.'@);
      _zwrot:='SRSR'
   ?};
   POJAZDY.cntx_pop()
?};
{? _zwrot='' & POJAZDY.SRSR<>null
|| {? POJAZDY.DPS<POJAZDY.SRSR().DE
   || FUN.emsg('Data przyjęcia na stan nie może być wcześniejsza\n'
               'niż data przyjęcia do eksploatacji środka trwałego.'@);
      _zwrot:='DPS'
   ?}
?};
{? _zwrot='' & POJAZDY.SRSR<>null
|| {? SSTALE.AO().KON<>date(0,0,0) & SSTALE.AO().KON<POJAZDY.SRSR().OKRO_F().POCZ
   || {? ~FUN.ask('Środek został wprowadzony do ewidencji w okresie późniejszym\n'
                  'niż bieżący okres w parametrach pracy. W bieżącym okresie nie\n'
                  'będzie można wprowadzać zmian dla pojazdu. Kontynuować?'@)
      || _zwrot:='SRSR'
      ?}
   ?}
?};
_zwrot


\pojazdy_ds_marka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Na wyświetl dla pola POJAZDY.MAR
::----------------------------------------------------------------------------------------------------------------------
{? +app_info('web_sesid')=0
|| {? ~POJAZDY.MAR
   || POJAZDY.efld_opt(POJAZDY.win_edit('?'),'mark=0,enable=0',POJAZDY,'POJMODEL')
   || POJAZDY.efld_opt(POJAZDY.win_edit('?'),'mark=0,enable=1',POJAZDY,'POJMODEL')
   ?}
?};
1


\pojazdy_ae_marka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Po redakcji pola POJAZDY.MAR
::----------------------------------------------------------------------------------------------------------------------
{? ~POJAZDY.MAR | (POJAZDY.POJMODEL & POJAZDY.POJMODEL().MAR<>POJAZDY.MAR)
|| POJAZDY.POJMODEL:=null
?};
1


\pojazdy_be_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Przed redakcją pola POJAZDY.POJMODEL
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=POJAZDY.MAR;
{? _zwrot || ZM_ZASOB.MAR:=POJAZDY.MAR ?};
_zwrot


\rpo_pojtypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Rekord po dla tabeli POJTYPY
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=__CHK.record(POJTYPY,,'NAZWA');
{? _zwrot=''
|| {? __CHK.index(POJTYPY,-menu_txt(,)='popraw')<>'' || _zwrot:='NAZWA' ?}
?};
_zwrot


\usup_pojtypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Usuń przed dla tabeli POJTYPY
::----------------------------------------------------------------------------------------------------------------------
{? POJTYPY.count()>0
|| FUN.info('Typ pojazdu jest wykorzystywany w danych. Usunięcie niemożliwe.'@); 0
|| 1
?}


\bl_mar_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Wartość początkowa pola POJMODEL.MAR
::----------------------------------------------------------------------------------------------------------------------
ZM_ZASOB.MAR


\usup_pojmodel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Usuń przed dla tabeli POJMODEL
::----------------------------------------------------------------------------------------------------------------------
{? POJMODEL.count()>0
|| FUN.info('Model pojazdu jest wykorzystywany w danych. Usunięcie niemożliwe.'@); 0
|| 1
?}


\usup_samdelty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Usuń przed dla tabeli SAMDELTY
::----------------------------------------------------------------------------------------------------------------------
{? SAMDELTY.count()>0
|| FUN.info('Rodzaj pojazdu jest wykorzystywany w danych. Usunięcie niemożliwe.'@); 0
|| 1
?}


\rpo_pojmodel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Rekord po dla tabeli POJMODEL
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=__CHK.record(POJMODEL,,'NAZWA');
{? _zwrot=''
|| {? __CHK.index(POJMODEL,-menu_txt(,)='popraw')<>'' || _zwrot:='NAZWA' ?}
?};
_zwrot


\mar_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Formula przed usunieciem marki pojazdu
::  OLD: \mar_usu/skid_sam.fml
::----------------------------------------------------------------------------------------------------------------------
{? MAR.count()>0
|| FUN.emsg('Marka pojazdu jest wykorzystywana danych. Usunięcie niemożliwe.'@); 0
|| 1
?}


\chk_mar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2011]
:: OPIS: Kontrola wypelnienia pol w tabeli MAR
::  OLD: \chk_mar/skid_sam.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=__CHK.record(MAR,,'NAZ');
{? _zwrot=''
|| {? __CHK.index(MAR,-menu_txt(,)='popraw')<>'' || _zwrot:='NAZ' ?}
?};
_zwrot


\pojazdy_trig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Triggery dla tabeli POJAZDY
::   WE: 1 - add po
::       2 - put po
::----------------------------------------------------------------------------------------------------------------------
ZASOBY.cntx_psh(); ZASOBY.index('ZASOBY'); ZASOBY.prefix(POJAZDY.ID);
{? ~ZASOBY.first()
|| ZASOBY.TYP:='P';
   ZASOBY.KARTOTEK:=POJAZDY.ID;
   ZASOBY.add(1)
?};
ZASOBY.cntx_pop();
~~


\be_pojazdy_wlasny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2009]
:: OPIS: Formula przed redakcja pola WLASNY w tabeli POJAZDY
::  OLD: \be_wlasny/skid_sam.fml
::  OLD: \be_wlasny/samochody.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()='dołącz'@
|| 1
|| _sql:='select * from @EDOKUMP where EDOKUMP.SAMREF='':_a''';
   _tmp:=sql(_sql,$POJAZDY.ref());
   ~_tmp.first()
?}


\ae_pojazdy_wlasny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2009]
:: OPIS: Formula po redakcji pola WLASNY w tabeli POJAZDY
::  OLD: \ae_wlasny/skid_sam.fml
::  OLD: \ae_wlasny/samochody.fml
::----------------------------------------------------------------------------------------------------------------------
{? POJAZDY.WLASNY<>'T' || POJAZDY.SRSR:=null ?};
1


\be_pojazdy_srsr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przed redakcją pola POJAZDY.SRSR
::  OLD: \be_sam_srsr/samochody.fml
::----------------------------------------------------------------------------------------------------------------------
{? POJAZDY.WLASNY<>'T'
|| 0
|| _edit:=1;
   {? -menu_txt()='popraw'
   || ZASOB_ZM.cntx_psh();
      ZASOB_ZM.index('DISP');
      ZASOB_ZM.prefix(ZM_ZASOB.ZASOBY);
      {? ZASOB_ZM.first() || _edit:=0 ?};
      ZASOB_ZM.cntx_pop()
   ?};
   {? _edit
:: aby wskazywać środek trwały powiązany z pojazdem należy mieć uprawnienia do ról przeglądania danych
:: środków trwałych
   || {? exec('chk_role','#b__box',OPERATOR.USER,'FST_EWI_ZASA') | exec('chk_role','#b__box',OPERATOR.USER,'FST_EWI_ZASB')
      || exec('set_srsr','samochody');
         1
      || 0
      ?}
   || 0
   ?}
?}


\ae_pojazdy_srsr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji pola POJAZDY.SRSR
::  OLD: \ae_sam_srsr/samochody.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? POJAZDY.SRSR<>null
|| {? -menu_txt()<>'dołącz' || _ref:=POJAZDY.ref() || _ref:=null ?};
   POJAZDY.cntx_psh();
   POJAZDY.index('FIRSRSR');
   POJAZDY.prefix(REF.FIRMA,POJAZDY.SRSR);
   {? POJAZDY.first() & POJAZDY.ref()<>_ref
   || FUN.emsg('Wskazany środek występuje już w bazie pojazdów.'@);
      _dalej:=0
   ?};
   POJAZDY.cntx_pop()
?};
{? _dalej & POJAZDY.SRSR<>null & (POJAZDY.NAZ='' | POJAZDY.OSOBA=null | POJAZDY.DPS=date(0,0,0))
|| SRSR.cntx_psh(); SRST.cntx_psh();
   {? SRSR.name()<>'srsrr'+REF.FIRMA().SYMBOL || SRSR.use('srsrr'+REF.FIRMA().SYMBOL) ?}; SRSR.prefix();
   {? SRST.name()<>'srstr'+REF.FIRMA().SYMBOL || SRST.use('srstr'+REF.FIRMA().SYMBOL) ?}; SRST.prefix();
   SRST.index('PODAT');
   SRST.prefix(POJAZDY.SRSR);
   {? SRST.last()
   || {? (POJAZDY.DPS<>date(0,0,0) & POJAZDY.DPS<>POJAZDY.SRSR().DE)
         | (POJAZDY.NAZ<>'' & POJAZDY.NAZ<>SRSR.NST)
      || {? FUN.ask('Dane pojazdu różnią sią od danych powiązanego środka trwałego.\n'
                    'Uzgodnić na podstawie danych środka?'@)
         || POJAZDY.NAZ:=SRSR.NST;
            POJAZDY.DPS:=POJAZDY.SRSR().DE
         ?}
      || {? POJAZDY.NAZ='' || POJAZDY.NAZ:=SRSR.NST ?};
         {? POJAZDY.DPS=date(0,0,0) || POJAZDY.DPS:=POJAZDY.SRSR().DE ?}
      ?}
   ?};
   SRSR.cntx_pop(); SRST.cntx_pop()
?};
{? _dalej  || SRSR.prefix(); SRSR.f_clear(); SRSR.win_dict('WER') ?};
_dalej


\ae_pojazdy_vin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2011]
:: OPIS: Formula po redakcji pola POJAZDY.VIN
::  OLD: \ae_sam_vin/skid_sam.fml
::  OLD: \ae_sam_vin/samochody.fml
::----------------------------------------------------------------------------------------------------------------------
{? +POJAZDY.VIN=0
|| return(1)
?};
{? +POJAZDY.VIN<17
|| FUN.emsg('Wprowadzony numer VIN jest krótszy niż wymagane 17 znaków.'@);
   return(0)
?};
_wy:=1;
_znaki:='ABCDEFGHJKLMNPRSTUVWXYZ1234567890';
_err:=0;
_err_char:='';
_mid:="_c+((_b-1)-_a)";
{! _i:=1..(+POJAZDY.VIN) |!
   _char:=_mid(POJAZDY.VIN,_i,1);
   {? ~(_znaki*_char)
   || _err+=1;
      _err_char+=_char
   ?}
!};
{? _err
|| FUN.emsg('Niedozwolone znaki w polu VIN:\n %1 \nDopuszczalny zestaw znaków to:\n %2'@[_err_char,_znaki]);
   _wy:=0
?};
_wy


\rpo_samdelty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Rekord po dla tabeli POJTYPY
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=__CHK.record(SAMDELTY,,'RODZAJ');
{? _zwrot=''
|| {? __CHK.index(SAMDELTY,-menu_txt(,)='popraw')<>''
   || _zwrot:='RODZAJ'
   ?}
?};
_zwrot

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 b89fc8423f6cbedaa7f34f094b6b26a1b6c030b1dde4d79c2fded5f61d4763ad83317efab34b94a8e4e2734864d4b509a025ae18998f173aeede616990799ea9fe7ba9b6ac22e9be216c68feb4bd24be2cb0fe5ec3d6a0bbc5af68e0c38b344081aa5cc1bf33ef57150b1c2b68a765bc3e1c343c895967ca6d3a3035df20cbc5

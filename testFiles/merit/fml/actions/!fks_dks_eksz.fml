:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_eksp.fml
:: Utworzony: 14.04.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_EKSZ - Księgowanie końcowe dokumentu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Księgowanie końcowe dokumentu - główna formuła czynności FKS_DKS_EKSZ.
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=T, keyref=T
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na nagłówek dokumentu księgowego, required=T
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
::# permissions=FJKS,FREJ
::# parses=exec('parses_dok','dok_fks')
::# access=exec('uprawnienia','!fks_dks_eksz')

:: parametr wyjściowy status - jeśli ma wartość 'R' to dokument jest na etapie rejestracji
::                             jeśli ma wartość 'P' to dokument jest zarejestrowany ale nie zaksięgowany próbnie
::                             wpw ma wartość ''
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('DOK',_we)>0
|| {? _we.DOK=null
   || {? ~_mp.isAutoRun()
      || FUN.info('Nie znaleziono dokumentu księgowego.'@)
      ?};
      _wy:=params_get().out;
      _wy.DOK:=null;
      {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
      _mp.save(,_wy);
      _mp.done()
   || _ref:=BB.sqlint($_we.DOK); _nam:=form(($_we.DOK)-8);
      {? _ref<>0 & _nam<>''
      || DOK.cntx_psh();
         DOK.prefix();
         {? DOK.seek(_ref,_nam)
         || _maski:=_mp.pathTodo() & ~_mp.isAutoRun();
            _ok:=exec('dok_ks_chk','dok_fks','K',_maski);
            {? _ok=0 | _ok=3
            || _wy:=params_get().out;
               _wy.DOK:=DOK.ref();
               {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:={? _ok=0 || 'R' || 'P' ?} ?};
               _mp.save(,_wy);
               _mp.done()
            || {? _maski & _ok=1
               || BtnKsg:=0;
                  exec('set_okno_todo','!fks_dks_eksz');
                  _ok:=BtnKsg;
                  VAR_DEL.delete('BtnKsg')
               ?};
               {? _ok & exec('ks_dok1','!fks_dks_eksz',~_mp.pathTodo() & ~_mp.isAutoRun(),~_mp.isAutoRun())
               || _wy:=params_get().out;
                  _wy.DOK:=DOK.ref();
                  {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
                  _mp.save(,_wy);
                  _mp.done()
               ?}
            ?}
         || FUN.info('Nie znaleziono dokumentu księgowego.'@);
            _wy:=params_get().out;
            _wy.DOK:=null;
            {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
            _mp.save(,_wy);
            _mp.done()
         ?};
         DOK.cntx_pop()
      ?}
   ?}
|| {? ~_mp.isAutoRun()
   || FUN.info('Nie znaleziono dokumentu księgowego.'@)
   ?};
   _wy:=params_get().out;
   _wy.DOK:=null;
   {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
   _mp.save(,_wy);
   _mp.done()
?};
1


\n_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Czynność FKS_DKS_EKSP - formuła główna w wersji nieprocesowej
::----------------------------------------------------------------------------------------------------------------------
:: Parametry wejściowe identyczne jak w wersji procesowej
_in:=params_get().in;
{? type_of(_in)=type_of(~~) | var_pres('DOK',_in)<=type_of(~~)
|| FUN.emsg('Brak wymaganego parametru DOK'@)
|| _grp:=DOK.sel_size()>0;
   _ref:=BB.sqlint($_in.DOK); _nam:=form(($_in.DOK)-8);
   {? _ref<>0 & _nam<>''
   || DOK.cntx_psh();
      DOK.use(_nam);
      DOK.prefix();
      {? ~DOK.seek(_ref,_nam)
      || _mp.error('Nie znaleziono dokumentu księgowego.'@)
      |? exec('dok_ks_chk','dok_fks','K',~_grp)
      || exec('ks_dok1','!fks_dks_eksz',1)
      ?};
      DOK.cntx_pop()
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie dokumentów księgowych - formuła na opis w TODO
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zaksięguj końcowo dokument'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('DOK',_we)
|| _ref:=BB.sqlint($_we.DOK); _nam:=form(($_we.DOK)-8);
   {? _ref<>0 & _nam<>''
   || DOK.cntx_psh(); DOK.use(_nam); DOK.prefix();
      {? DOK.seek(_ref,_nam)
      || {? DOK.NK<>'' & DOK.DTO<>date(0,0,0)
         || _desc:='Zaksięguj końcowo dokument: %1 z dnia %2: (%3/%4)'@@[DOK.NK,$DOK.DTO,DOK.REJ().KOD,$DOK.NR]
         || _desc:='Zaksięguj końcowo dokument: %1 z dnia %2 (%3/%4)'@@[DOK.NK,$DOK.DTO,DOK.REJ().KOD,$DOK.NR]
         ?}
      ?};
      DOK.cntx_pop()
   ?}
?};
_desc


\ksieguj_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja Księguj koncowo oknien tabeli DOK
::----------------------------------------------------------------------------------------------------------------------
{? exec('spr_mod','dok_fks')
|| {? ~DOK.sel_size()
   || FUN.info('Dokument wtórny dla wielu jednostek księgowych. Księgowanie możliwe od strony dokumentu pierwotnego.'@)
   ?}
|? DOK.ZK='T'
|| {? ~DOK.sel_size()
   || FUN.info('Dokument został wcześniej zaksięgowany końcowo.'@)
   ?}
|| _proces:=1;
   {? DOK.sel_size() & Plugin.exists('DOK_KS_K_001')
   || _proces:=Plugin.run('DOK_KS_K_001')
   ?};
   {? _proces
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='FKS_DKS_EKSZ';
      _params.AKCJA:='Księguj końcowo';
      _params.UIDREF:=DOK.uidref();
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOK',DOK.ref());
      _params.PROC_START:='N';
      exec('mp_run','#b__box',_params)
   || {? exec('zatw','!fks_dks_eksz') || DKS_OK+=1 ?}
   ?}
?}


\bg_ksp_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Grupa przed akcja ksiegowania dokumentow
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy księgować końcowo zaznaczone dokumenty?\nOperacja jest nieodwracalna.'@,'EXCLAM',1)
|| sel_nchk();
   DKS_SEL:=DOK.sel_size();
   DKS_OK:=0; DKS_W_OK:=0;
   1
?}


\ag_ksp_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Grupa po akcji księgowania dokumentow
::----------------------------------------------------------------------------------------------------------------------
{? DKS_W_OK>0
|| _nzaks:=(DKS_SEL-DKS_OK);
   {? _nzaks<0 || _nzaks:=0 ?};
   FUN.info('Liczba zaznaczonych dokumentów: %1\n'
            'Liczba zaksięgowanych dokumentów: %2\n'
            '(w tym dokumentów wtórnych: %3)\n'
            'Liczba nie zaksięgowanych dokumentów: %4.'@[$DKS_SEL,$DKS_OK,$DKS_W_OK, $_nzaks])
|| FUN.info('Liczba zaznaczonych dokumentów: %1\n'
            'Liczba zaksięgowanych dokumentów: %2\n'
            'Liczba nie zaksięgowanych dokumentów: %3.'@[$DKS_SEL,$DKS_OK,$(DKS_SEL-DKS_OK)])
?};
VAR_DEL.delete('DKS_SEL','DKS_OK','DKS_W_OK')


\ks_dok1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Księgowanie pojedynczego dokumentu
::   WE: _a - Pytanie o księgowanie
::       _b - Wyśwetlać podsumowanie
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
{? exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER,DOK.sel_size()=0)
|| _ok:=1
|? DOK.A<>'T'
|| {? DOK.sel_size()=0
   || FUN.info('Dokument \'%1\' jest w trakcie rejestracji.'@[DOK.NK])
   ?};
   _ok:=1
|? DOK.ZP<>'T'
|| {? DOK.sel_size()=0
   || FUN.info('Dokument \'%1\' nie jest zaksięgowany próbnie.'@[DOK.NK])
   ?};
   _ok:=1
|? DOK.ZK='T'
|| {? DOK.sel_size()=0 & _b
   || FUN.info('Dokument \'%1\' jest już zaksięgowany końcowo.'@[DOK.NK])
   ?};
   _ok:=1
|? DOK.sel_size() | ~_a | FUN.ask('Czy zaksięgować końcowo dokument?\nOperacja jest nieodwracalna.'@,'EXCLAM',1)
|| {? DOK.r_lock(1,1)
   || {? DOK.get()
      || KOMT:='';
         {? DOK.ZK='N'
         || _ok:=exec('zatw','!fks_dks_eksz')
         ?};
         echo('');
         {? _ok
         || {? DOK.sel_size()<>0 || DKS_OK+=1 ?}
         || {? DOK.sel_size()=0 || FUN.info('Księgowanie dokumentu \'%1\' nie powiodło się.%2'@[DOK.NK,KOMT]) ?}
         ?};
         &KOMT
      || {? DOK.sel_size()=0 || FUN.info('Księgowanie dokumentu nie powiodło się.'@) ?}
      ?};
      DOK.r_unlock()
   || {? DOK.sel_size()=0 || FUN.info('Dokument obsługuje inny operator.'@) ?}
   ?}
?};
_ok


\set_okno_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przygotowuje okno do wyświetlenia dokumentu z odpowiednimi przyciskami
::----------------------------------------------------------------------------------------------------------------------
_okno:=exec('okna_r','dok_fks',DOK.DOK_REJ().SLO().KOD);
_red:=DOK.mk_edit('Dokument księgowy'@);
DOK.win_etab(_red,'Nagłówek dokumentu'@);
DOK.win_ewin(_red,,_okno);
DOK.win_etab(_red,'Status'@);
DOK.win_ewin(_red,,'DOK_STAT');
DOK.win_ebtn(_red,'text=%1,panel=bottom,align=begin,display=1'['&Pozycje'@],"exec('poz_dok','dok_fks');''");
DOK.win_ebtn(_red,'text=%1,panel=bottom,align=end,display=1'['&Księguj końcowo'@],"
   {? FUN.ask('Zaksięgować końcowo dokument?'@)
   || BtnKsg:=1;
      'key:Esc'
   || ''
   ?}
");
_btnan:=DOK.win_ebtn(_red,'text=%1,panel=bottom,align=end,display=1'['&Anuluj'@],"'key:Esc'");
DOK.btn_eopt(_red,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
DOK.win_edit(_red);
{? exec('lic','#b_domain',exec('domain_ref','#b_domain','CTR'))
|| DOK.efld_opt('DOK_STAT','enable=1',DOK,'WSK_XD')
|| DOK.efld_opt('DOK_STAT','enable=0',DOK,'WSK_XD')
?};
DOK.display()


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
{? var_pres('DOK',_in) & var_pres('DOK',_in)=type_of(null()) & _in.DOK<>null()
|| USERS.cntx_psh(); USERS.clear();
   {? USERS.seek(_user)
   || DOK.cntx_psh();
      DOK.prefix();
      {? DOK.seek(_in.DOK,form(($_in.DOK)-8))
      || {? exec('usr_fjks','b_perm',DOK.ODD().OD,USERS.ref())
         || _result:=1
         ?}
      ?};
      DOK.cntx_pop()
   ?};
   USERS.cntx_pop()
:: czy w ogóle user ma uprawnienia do jakiejkolwiek jednostki księgowej
|| {? exec('usr_fjks_any','b_perm',USERS.ref())
   || _result:=1
   ?}
?};
_result


\zatw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Zatwierdzenie księgowania - dokument był zaksięgowany próbnie i jest księgowany końcowo
::  OLD: \zatw/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? DOK.get()
|| exec('nrdz','dok_fks_ks');
   DOK.KSIEGOWY:=exec('usr_zar','dok_fks');
   exec('poz_zn','dok_fks_ks',2);
   DOK.ZK:='T';
   DOK.DKS_K:=date;
   {? DOK.put()
   || {? exec('spr_mod','dok_fks')
      || DKS_W_OK+=1; _zwrot:=1
   || DOK.cntx_psh(); _refsql:=$DOK.ref();
      DOK.index('DOKZRODL'); DOK.prefix(_refsql,_refsql);
      {? DOK.first()
      || _zwrot:=1;
         {! |?
            _zwrot:=exec('zatw','!fks_dks_eksz');
                DKS_OK+=1;
            _zwrot & DOK.next()
         !}
      || _zwrot:=1
      ?};
      DOK.cntx_pop()
      ?}
  || {? KOMT='' || KOMT:='\nBłąd zapisu tabeli DOK.' ?}
  ?}
|| {? KOMT='' || KOMT:='\nDokument niedostępny.' ?}
?};
_zwrot


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Podglad dokumentu księgowego z listy zadań
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
exec('dok_spac','dok_fks',_we.DOK)


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? 4+ref_name(_kref)='doku'
         || _dok:=exec('FindAndGet','#table',DOK,_kref,,,null());
            {? _dok=null()
            || _wy.DOK:=null;
               {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
               _mp.save(,_wy);
               _mp.done()
            |? DOK.seek(_kref,ref_name(_kref),1) & DOK.ZK='T'
            || _wy.DOK:=DOK.ref();
               {? var_pres('STATUS',_wy)>=0 || _wy.STATUS:='' ?};
               _mp.save(,_wy);
               _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 a3ebdd372afbb5d7b259e2fa31c62ced904e05841f576adb9c52f3e63b1c1322a22b5a75931618f469faa1e51ccc23c24b693e2f53a7e0ed34ab096de066177d14a567e3b9866ceaaa5e9f72e9a4a7f6f188e2365ec3aa7eab4f3ac11d94e6182f73a259ef387a5a8bf4043170b2d4aee56db563972eb8c43cac67e25bb4de74

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rrk_dkad.fml
:: Utworzony: 04.11.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RRK_DKAD - Tworzenie wezwań do zapłaty
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tworzenie wezwań do zapłaty - główna formuła czynności FKS_RRK_DKAD
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_ser_nags','rozrach_kor')
::# kind=WE, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WY, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na wezwanie do zapłaty, required=T, keyref=T
::# kind=WY, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=LOOP
:: WARUNKI BRAMY
::# condition=Akceptacja, act_uid=FKS_RRK_DKAE, auto=T, formula=_a.SER_NAG<>null & _a.SER_NAG<>~~
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='W';
params_set(params_get());
{? var_pres('UNIK_ID',_in)>0 & _in.UNIK_ID<>''
|| params_exec('par_out','rozrach_kor',_in.UNIK_ID);
   _mp.done()
|| idnagpar:=tm_stamp();
:: czynność startowa
   {? _mp.pathProc()
   || SER_KOR.ZAK_WAL:=SSTALE.WAL;
      ROZRACH.KONTR:=null;
      kh_all:=1;
      exec('bw','rozrach_kor','W','SER_NAGW','WEZW');
      menu_txt(,'Dołącz');
      _mp.trigRef('SER_NAG');
      params_exec('wezw_new1','!fks_rrk_dkad');
      VAR_DEL.delete('kh_all')
   |? _akcja='Dołącz'
   || menu_txt(,'Dołącz');
      _mp.trigRef('SER_NAG');
      params_exec('wezw_new1','!fks_rrk_dkad')
   ?};
   params_exec('par_out','rozrach_kor',idnagpar);
   VAR_DEL.delete('idnagpar');
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ rejestrację wezwania do zapłaty'@@;
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('SER_NAG',_wy)
|| _ref:=BB.sqlint($_wy.SER_NAG); _nam:=form(($_wy.SER_NAG)-8);
   {? _ref<>0 & _nam<>''
   || SER_NAG.cntx_psh(); SER_NAG.use(_nam); SER_NAG.prefix();
      KH.cntx_psh(); KH.prefix();
      {? SER_NAG.seek(_ref,_nam)
      || _desc:='Zakończ rejestrację wezwania do zapłaty dla kontrahenta: %1 z dnia: %2'@@[SER_NAG.KH().SKR,$SER_NAG.DATA]
      ?};
      SER_NAG.cntx_pop(); KH.cntx_pop()
   ?}
?};
_desc


\n_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Czynność FKS_RRK_DKAD - formuła główna w wersji nieprocesowej
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_in:=_args.in;
_akcja:=_args.akcja;
_autoakc:=exec('autoAkc','#b__box',,300680,'FKS_RRK_DKAE');
SER_KOR.TYP:='W';
params_set(params_get());
:: czynność startowa
{? _akcja='Dołącz'
|| menu_txt(,'Dołącz');
   {? _autoakc
   || exec('tab_autoakc','rozrach_kor');
      params_exec('wezw_new1','!fks_rrk_dkad');
      exec('autoakc','rozrach_kor',1)
   || params_exec('wezw_new1','!fks_rrk_dkad')
   ?}
?};
~~


\wezw_new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie wezwania do zapłaty
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_RRK_DKAD';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\wezw_new1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie wezwania do zapłaty - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
exec('gen_kor1','rozrach_kor');
{? exec('okno2','!fks_rrk_dkad')
|| {? ROZNE.UT_DOK || exec('tmp_ods','rozrach_ods') ?};
   exec('initmpks','rozrach_kor');
   {? -SER_KOR.KONTR='w'
   || _wybrani:={? SER_KOR.KONTR='W' & (ROZNE.PAR4=0 | ROZNE.PAR4=2)
                || {? ROZNE.PAR4=0
                   || exec('wybr_kh','rozrach_kor',ROZNE.PREFIX)
                   || exec('wybr_kh','rozrach_kor',ROZNE.MASKA)
                   ?}
                || 0
                ?};
      {? SER_KOR.WYB_WAL=1 || exec('wyb_wal','rozrach_kor') ?};
      {? l_wal>0
      || exec('wezwk','!fks_rrk_dkad',_wybrani)
      ?};
      VAR_DEL.delete('TMPKH','ind_kh');
      {? SER_KOR.WYB_WAL=1 || exec('del_wal','rozrach_kor') ?}
   |? -SER_KOR.KONTR='b'
   || exec('initmpkh','rozrach_kor');
      KH.select();
      {? ~TMPKH.first()
      || FUN.info('Nie wybrano żadnego kontrahenta.'@)
      || {? SER_KOR.WYB_WAL=1 || exec('wyb_wal','rozrach_kor') ?};
         {? l_wal>0
         || exec('wezwk','!fks_rrk_dkad',1)
         ?};
         {? SER_KOR.WYB_WAL=1 || exec('del_wal','rozrach_kor') ?}
      ?};
      exec('deltmpkh','rozrach_kor')
   ?};
   exec('deltmpks','rozrach_kor')
?};
exec('gen_kor2','rozrach_kor')


\okno2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Okienko dla wezwań do zapłaty
::  OLD: \okno2/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? date()>=SSTALE.AO().POCZ & date()<=SSTALE.AO().KON
|| SER_KOR.DATA:=date()
|| SER_KOR.DATA:=SSTALE.AO().KON
?};
SER_KOR.KONTR:='W';
SER_KOR.TRYB_MON:=0;
RACHBANK.KB_1R:='';
SER_KOR.WYB_TRYB:=SER_KOR.SER_SCH:=SER_KOR.TAB_ODS:=SER_KOR.WYB_POZ:=null;
ROZNE.PAR4:=0; ROZNE.PREFIX:=ROZNE.MASKA:=''; ROZNE.KS_OD:=ROZNE.KS_DO:=null;
{? kh_all
|| SER_KOR.win_edit('SER_WEMA')
|| SER_KOR.win_edit('SER_WEM1')
?};
exec('odd_filtr','fst');
SER_KOR.edit("exec('spr_wezw','!fks_rrk_dkad')")


\spr_wezw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.62]
:: OPIS: Sprawdzanie parametrów wezwania do zapłaty
::  OLD: \spr_wezw/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_a:='';
{? ROZNE.PAR4=2
|| WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=ROZNE.MASKA;
   {? +WYDRUKI.MASKA < WYDRUKIN.DLKON
   || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
   ?}
|? ROZNE.PAR4=4
|| KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR);
   {? ROZNE.KS_OD=null || KS.first(); ROZNE.KS_OD:=KS.ref() ?};
   {? ROZNE.KS_DO=null || KS.last(); ROZNE.KS_DO:=KS.ref() ?};
   KS.cntx_pop()
?};
{? ~exec('dat_not','dok_fks',SER_KOR.DATA)
|| FUN.info('Podana data musi być z bieżącego roku bilansowego.'@); _a:='DATA'
?};
{? _a='' & ROZNE.PAR4=4 & ROZNE.KS_OD().SYM>ROZNE.KS_DO().SYM
|| FUN.info('Błędny zakres kont.'@); _a:='KS_OD'
?};
{? _a='' & SER_KOR.TRYB_MON=1 & SER_KOR.WYB_TRYB=null
|| FUN.info('Nie wybrano trybu monitowania.'@); _a:='WYB_TRYB'
?};
{? _a='' & SER_KOR.TRYB_MON=2 & SER_KOR.WYB_POZ=null
|| FUN.info('Nie wybrano poziomu.'@); _a:='WYB_POZ'
?};
{? _a='' & SER_KOR.WYB_WAL=0 & SER_KOR.WAL=null
|| FUN.info('Nie wybrano waluty.'@); _a:='WYB_WAL'
?};
{? _a=''
|| SLO.cntx_psh();
   _wal:={? SER_KOR.WYB_WAL=0
         || wal_kod:=SER_KOR.WAL().KOD; SER_KOR.WAL
         || null
         ?};
   SLO.cntx_pop;
   _a:=exec('szuksch1','rozrach_kor',0,0,0,_wal)
?};
_a


\wezwk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Generowanie wezwań do zapłaty
::   WE: _a=0 - wszyscy, 1 - wybrani
::  OLD: \wezwk/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
echo(); czy_mon:=1; _synt:=SSTALE.AR().SYNT; add_nag:=0; ile:=0;
ileprzed:=ilepo:=odsprter:=zwlprter:=zwlpoter:=0; dprzed:=dpo:=date(0,0,0);
{? ~kh_all || _a:=0 ?};
{? kh_all || KH.index('KOD'); KH.prefix(2) ?};
{? ~kh_all | (kh_all & KH.first)
|| _del:=0;
   {! |? wal_snag:=null; wal_kod:=''; add_nag:=0; maxzwl:=0; _del:=0;
        SLO.cntx_psh();
        {? l_wal=1 & SER_KOR.WYB_WAL=0
        || wal_snag:=SER_KOR.WAL; wal_kod:=SER_KOR.WAL().KOD
        |? l_wal=1 & SER_KOR.WYB_WAL=1
        || TMP_WAL.first(); exec('szu_wal','rozrach_kor')
        ?};
        SLO.cntx_pop();
        {? (~_a | (_a & (TMPKH.prefix(); TMPKH.find_key(KH.KOD,KH.KOD))))
        || {? ~exec('czy_kor','rozrach_kor',SER_KOR.TYP,SER_KOR.DATA,null,KH.KOD,null,wal_snag,SER_KOR.ODD().OD)
           || echo('Generowanie wezwań do zapłaty dla kontrahenta: %1'@[KH.SKR]);
              KH.cntx_psh(); czy_mon:=1;
              exec('szuksch','rozrach_kor',0,wal_snag);
              {? czy_mon & SER_KOR.TRYB_MON<>2 || czy_mon:=exec('szuk_poz','!fks_rrk_dkad') ?};
              {? czy_mon
              || {? SER_KOR.TRYB_MON=0 || SER_KOR.ODSPTER:=SER_KOR.SER_SCH().ODSPWEZW ?};
                 {? l_wal>1 || TMP_WAL.first() ?};
                 {! |?
                    {? l_wal>1 || exec('szu_wal','rozrach_kor') ?};
                    {? SER_KOR.ODD=null
                    || OP.index('KH2'); OP.prefix(wal_snag,KH.ref,'N')
                    || OP.index('KH3'); OP.prefix(wal_snag,SER_KOR.ODD,KH.ref,'N')
                    ?};
                    ZAP_OP.cntx_psh();
                    ZAP_OP.index('OP');
                    {? OP.first()
                    || {!|? ZAP_OP.prefix(OP.ref);
                            {? ZAP_OP.first()
                            || {? SER_KOR.TRYB_MON=0 || SER_KOR.RATY:=SER_KOR.SER_SCH().RATY_WEZ ?}; _raty:=0;
                               _tp:={? exec('get_par','#parametr',26)='T'
                                    || exec('dzienRob','kalendarz',OP.TZ,1,1)
                                    || OP.TZ
                                    ?};
                               {? _tp=date(0,0,0) || _tp:=OP.TZ ?};
                               {? -OP.CZY_MON='t' &
                                  {? ROZNE.PAR4=0 || _dlpref:=(+(|ROZNE.PREFIX)); (_dlpref+OP.AN)=(|ROZNE.PREFIX)
                                  |? ROZNE.PAR4=2 || exec('mask','konto',OP.AN)
                                  |? ROZNE.PAR4=4 || (_synt+OP.AN)>=ROZNE.KS_OD().SYM & (_synt+OP.AN)<=ROZNE.KS_DO().SYM
                                  || 1
                                  ?} &
                                  {? SER_KOR.RATY
                                  || {? var_pres('DRB',@.CLASS)<0 || exec('obj_drbt','raty') ?};
                                     {? var_pres('DRB')>0 || obj_del(DRB) ?}; DRB:=obj_new(@.CLASS.DRB);
                                     _raty:=DRB.set_tab(OP.ref());
                                     {? _raty
                                     || _data_p:=DRB.TPF; SER_KOR.DATA>_data_p & _data_p<>date(0,0,0)
                                     || {? SER_KOR.ODSPTER
                                        || {? SER_KOR.TRYB_MON<>2
                                           || {? (SER_KOR.DATA-_tp)>SER_KOR.WYB_TRYB().ILE || 1 || 0 ?}
                                           || SER_KOR.DATA>ZAP_OP.DATA+{? var_press('DNI_KH',SER_KOR)>0
                                                                       || {? SER_KOR.DNI_KH
                                                                          || exec('ile_dni','rozrach_ods',OP.KH().WIELKOSC)
                                                                          || SER_KOR.DNI_TP
                                                                          ?}
                                                                       || 30
                                                                       ?} | SER_KOR.DATA>_tp
                                           ?}
                                        || {? SER_KOR.TRYB_MON<>2
                                           || {? (SER_KOR.DATA-_tp)>SER_KOR.WYB_TRYB().ILE || 1 || 0 ?}
                                           || SER_KOR.DATA>_tp
                                           ?}
                                        ?}
                                     ?}
                                  || {? SER_KOR.ODSPTER
                                     || {? SER_KOR.TRYB_MON<>2
                                        || {? (SER_KOR.DATA-_tp)>SER_KOR.WYB_TRYB().ILE || 1 || 0 ?}
                                        || SER_KOR.DATA>ZAP_OP.DATA+{? var_press('DNI_KH',SER_KOR)>0
                                                                    || {? SER_KOR.DNI_KH
                                                                       || exec('ile_dni','rozrach_ods',OP.KH().WIELKOSC)
                                                                       || SER_KOR.DNI_TP
                                                                       ?}
                                                                    || 30
                                                                    ?} | SER_KOR.DATA>_tp
                                        ?}
                                     || {? SER_KOR.TRYB_MON<>2
                                        || {? (SER_KOR.DATA-_tp)>SER_KOR.WYB_TRYB().ILE || 1 || 0 ?}
                                        || SER_KOR.DATA>_tp
                                        ?}
                                     ?}
                                  ?} &
                                  {? exec('get_par','#parametr',46)='T'
                                  || {? exec('spr_rwal','rozrach_kor',OP.SYM_ROK,OP.KH)
                                     || ~exec('spr_rkor','rozrach_kor',OP.SYM_ROK,OP.KH,SER_KOR.TYP)
                                     || 1
                                     ?}
                                  || 1
                                  ?}
                               || odsprter:=zwlprter:=zwlpoter:=ileprzed:=ilepo:=0; dprzed:=dpo:=date(0,0,0);
                                  {? SER_KOR.RATY & _raty
                                  || exec('wezw2','!fks_rrk_dkad',SER_KOR.DATA,wal_snag)
                                  || {? SER_KOR.ODSPTER & OP.TYP='NAL' & FINFO.TODS_UST<>null
                                     || odsprter:=exec('odsetki1','rozrach_ods',OP.ref())
                                     ?};
                                     blad_roz:=0;
                                     zwlpoter:={? SER_KOR.DATA>_tp || (SER_KOR.DATA-_tp) || 0 ?};
                                     _own:=F.ROw(OP.ref(),SER_KOR.DATA); _oma:=F.ROm(OP.ref(),SER_KOR.DATA);
                                     PAR_NAG.cntx_psh(); PAR_NAG.index('KONTO_O');
                                     PAR_NAG.prefix(OP.WAL,'K',OP.ODD,'',OP.KH().KOD);
                                     PAR_POZ.index('POZ_ROZ');
                                     {? PAR_NAG.first()
                                     || {! |?
                                           {? PAR_NAG.ZN='T'
                                           || PAR_POZ.prefix(PAR_NAG.ref(),OP.SYM,OP.SYM);
                                              {? PAR_POZ.first()
                                              || {! |?
                                                    {? PAR_POZ.KONTO=OP.AN
                                                    || {? -PAR_POZ.STR='wn'
                                                       || _oma+=PAR_POZ.KWOTA
                                                       || _own+=PAR_POZ.KWOTA
                                                       ?}
                                                    ?};
                                                    PAR_POZ.next()
                                                 !}
                                              ?}
                                           ?};
                                           PAR_NAG.next()
                                        !}
                                     ?};
                                     PAR_NAG.cntx_pop();
                                     {? _own>_oma
                                     || exec('odsetki','rozrach_ods',OP.ref(),SER_KOR.DATA,1);
                                        {? zwlpoter>maxzwl || maxzwl:=zwlpoter ?};
                                        exec('wezw1','!fks_rrk_dkad',SER_KOR.DATA,wal_snag)
                                     ?};
                                     VAR_DEL.delete('blad_roz')
                                  ?}
                               ?}
                            ?};
                            OP.next()
                       !}
                    ?};
                    ZAP_OP.cntx_pop();
                    {? l_wal>1 || TMP_WAL.next() || 0 ?}
                 !}
              ?};
              KH.cntx_pop(); SER_NAG.get();
              {? SER_KOR.TRYB_MON<>2 & add_nag
              || {? SER_KOR.WYB_TRYB().TYP=1
                 || {? exec('szuk_poz1','rozrach_kor')
                    || SER_NAG.SER_TPOZ:=SER_TPOZ.ref();
                       SER_NAG.KOD:=SER_TPOZ.SER_DEF; SER_NAG.put();
                       SER_NAGS.cntx_psh();
                       SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref());
                       {? SER_NAGS.first || {! |? SER_NAGS.KOD:=SER_NAG.KOD; SER_NAGS.put(); SER_NAGS.next() !} ?};
                       SER_NAGS.cntx_pop()
                    || SER_POZ.index('SER_POZR'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
                       {! |? SER_POZ.del() !}; exec('serndel','rozrach_kor');
                       SER_NAG.del(); ile-=1; _del:=1; add_nag:=0
                    ?}
                 ?};
                 {? ~_del
                 || {? l_wal>1 || TMP_WAL.first() ?}; _an_kont:=null; _ile_roz:=0;
                    SER_WART.index('SERWART3'); SER_WART.prefix(SER_TPOZ.ref());
                    {! |?
                      {? l_wal>1 || exec('szu_wal','rozrach_kor') ?};
                      SER_POZ.index('SER_POZW'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),wal_kod);
                      _sum_w:=0; _dokum:=''; _tp:=date(0,0,0); _oddp:=null;
                      {? SER_POZ.first()
                      || {! |?
                           _sum_w+=(SER_POZ.ODS-SER_POZ.KWP);
                           {? SER_POZ.SYM_ROK<>_dokum | SER_POZ.TERM<>_tp | SER_POZ.ODD<>_oddp
                           || _sum_w+=SER_POZ.KW; _dokum:=SER_POZ.SYM_ROK; _tp:=SER_POZ.TERM; _oddp:=SER_POZ.ODD;
                              _ile_roz+=1
                           ?};
                           SER_POZ.next()
                         !}
                      ?};
                      _min_war:={? SER_WART.find_key(wal_kod) || SER_WART.KW_GR || 0 ?};
                      {? _sum_w$2<_min_war
                      || {! |? SER_POZ.del() !};
                         SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
                         {? ~SER_POZ.first || exec('serndel','rozrach_kor'); ile-=1; _del:=1; add_nag:=0; SER_NAG.del() ?}
                      ?};
                      {? l_wal>1 & ~_del || TMP_WAL.next() || 0 ?}
                    !};
                    {? ~_del & _ile_roz<SER_TPOZ.MIN_POZ
                    || SER_POZ.index('SER_POZR'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
                       {! |? SER_POZ.del() !};
                       exec('serndel','rozrach_kor'); SER_NAG.del(); add_nag:=0; ile-=1; _del:=1
                    ?};
                    {? ~_del || exec('blok_kh','rozrach_kor') ?}
                 ?}
              |? add_nag
              || exec('blok_kh','rozrach_kor')
              ?}
           || {? ROZNE.UT_DOK
              || SER_DEF.get();
                 exec('addtmpks','rozrach_kor',1,'Kontr.: '+KH.SKR+' generowano korespondencję'+
                      {? SER_KOR.ODD<>null || ' w jedn. ks.: '+SER_KOR.ODD().OD || '' ?});
                 {? wal_snag=FINFO.NAROD
                 || exec('addtmpks','rozrach_kor',1,'w walucie narodowej ('+FINFO.NAROD().KOD+') na dzień: '+$SER_KOR.DATA,1)
                 || exec('addtmpks','rozrach_kor',1,'w walucie obcej na dzień: '+$SER_KOR.DATA,1)
                 ?}
              ?}
           ?}
        ?};
        {? ~_del & add_nag & SER_NAG.WAL=null || exec('spr_wal','rozrach_kor') ?};
        {? ~_del & add_nag & SER_NAG.get()
        || _tab_mail:=exec('email_os_kon_t','kontrahent',SER_NAG.KH,'Windykacja');
           {? _tab_mail.size()=1
           || SER_NAG.EMAIL:='N'
           |? _tab_mail.size()>1
           || SER_NAG.EMAIL:='W'
           || SER_NAG.EMAIL:='B'
           ?}; SER_NAG.put(); &_tab_mail
        ?}; &maxzwl;
        kh_all & KH.next()
   !};
   echo();
   {? ROZNE.UT_DOK
   || TMPKS.prefix(1);
      {? TMPKS.first() || TMPKS.select() ?}
   ?};
   TMPKS.prefix(2);
   {? TMPKS.first() & FUN.ask('Dokonano blokady przynajmniej jednego kontrahenta. Pokazać szczegóły?'@)
   || TMPKS.select()
   ?};
   &wal_snag
?};
exec('ile_dok','rozrach_kor');
&odsprter; &zwlprter; &zwlpoter; &ileprzed; &ilepo; &dprzed; &dpo;
&ile; &czy_mon; &add_nag;
{? var_pres('DRB')>0 || obj_del(DRB) ?};
0


\wezw1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Generowanie wezwań do zapłaty - wewnętrzna
::   WE: _a - data generowania wezwania do zapłaty
::       _b - waluta
::  OLD: \wezw1/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_w1:=OP.SYM; _symz:=OP.SYM_ZEW; _w2:=OP.DO; _w3:=OP.TZ; pusty:=1;
_w5:=date(0,0,0);  _w6:=_w7:=0; _data1:=dpo;
_ma:=F.ROm(OP.ref,{? SER_KOR.ODSPTER & ileprzed>0 || dprzed || dpo ?}); _wn:=F.ROw(OP.ref,_a); _kwota:=F.SWn(_wn,_ma);
_w4:=_kwota; _ods:=0;
{? ileprzed=0
|| F.RObr(OP.ref(),dprzed); _kw:=F.SWn(F.rwn,F.rma);
   _kon_d:={? SER_KOR.DATA<OP.TZ || SER_KOR.DATA || OP.TZ ?};
   {? blad_roz<>2 & SER_KOR.SER_SCH().CZY_ODS1='T'
   || _ods:={? FINFO.TODS_UST & dprzed<>date(0,0,0) & OP.TYP='NAL'
            || (exec('ods','rozrach_ods',dprzed+1,_kon_d,_kw,0,_b))$2
            || 0
            ?}
   ?};
   _w5:=date(0,0,0); _w6:=0;
   {? zwlprter=0
   || _dat_gen:={? SER_KOR.DATA<OP.TZ || SER_KOR.DATA || OP.TZ ?};
      zwlprter:=_dat_gen-OP.DO-30; {? zwlprter<0 || zwlprter:=0 ?}
   ?};
   _w7:=zwlprter;
   {? _w7>0 & odsprter>0
   || {? ~add_nag
      || {? (SER_KOR.TRYB_MON=2 & (SER_KOR.WYB_POZ(); SER_KOR.WYB_POZ<>null)) |
             SER_KOR.TRYB_MON<>2 & _w7>0
         || _tab_ods:={? blad_roz<>2 & SER_KOR.SER_SCH().CZY_ODS1='T' || SER_KOR.TAB_ODS || null ?};
            _tab_poz:={? SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0 || SER_TPOZ.ref() || null ?};
            _wal_n:={? l_wal=1 || _b || null ?};
            _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
            _ser_def:={? (SER_KOR.TRYB_MON=2 | (SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0)) || SER_DEF.ref() || null ?};
            {? exec('dodaj','rozrach_kor',SER_KOR.TYP,_ser_def,_a,_tab_ods,_tab_poz,_wal_n,null,_obca)
            || add_nag:=1; exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w3,_w5,_w6,_w7,1,_ods,_b,1,_symz)
            ?}
         ?}
      || exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w3,_w5,_w6,_w7,1,_ods,_b,1,_symz)
      ?}
   ?}
?};
{? _kwota>0
|| ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref);
   {? ZAP_OP.first()
   || _op_tz:={? exec('get_par','#parametr',26)='T'
                   || exec('dzienRob','kalendarz',OP.TZ,1,1)
                   || OP.TZ
                   ?};
      {! |?
         {? odsprter>0 & ileprzed>0 & ZAP_OP.WN=0 & (ZAP_OP.DATA<=_op_tz) & (ZAP_OP.DATA>dprzed)
         || _kw:=F.SMa(ZAP_OP.WN,ZAP_OP.MA);
            pusty:=0; _w5:=ZAP_OP.DATA; _w6:=ZAP_OP.MA;
            _w7:={? blad_roz=2 | ZAP_OP.DATA-dprzed<=0 || 0 || ZAP_OP.DATA-dprzed ?};  _ods:=0;
            {? SER_KOR.SER_SCH().CZY_ODS1='T'
            || _ods:={? OP.TYP='NAL' || exec('ods','rozrach_ods',dprzed+1,ZAP_OP.DATA,_kw,0,_b)$2 || 0 ?};
               {? ZAP_OP.DATA>_data1 || _data1:=ZAP_OP.DATA ?}
            ?};
            {? ~add_nag
            || {? (SER_KOR.TRYB_MON=2 & (SER_KOR.WYB_POZ(); SER_KOR.WYB_POZ<>null)) | SER_KOR.TRYB_MON<>2
               || _tab_ods:={? SER_KOR.SER_SCH().CZY_ODS1='T' || SER_KOR.TAB_ODS || null ?};
                  _tab_poz:={? SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0 || SER_TPOZ.ref() || null ?};
                  _wal_n:={? l_wal=1 || _b || null ?};
                  _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
                  _ser_def:={? (SER_KOR.TRYB_MON=2 | (SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0)) || SER_DEF.ref() || null ?};
                  {? exec('dodaj','rozrach_kor',SER_KOR.TYP,_ser_def,_a,_tab_ods,_tab_poz,_wal_n,null,_obca)
                  || add_nag:=1; exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w3,_w5,_w6,_w7,1,_ods,_b,1,_symz)
                  ?}
               ?}
            || exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w3,_w5,_w6,_w7,1,_ods,_b,1,_symz)
            ?};
            {? (ZAP_OP.DATA-dprzed)>0 || _kwota-=ZAP_OP.MA ?}
         ?};
         {? ZAP_OP.WN=0 & ZAP_OP.DATA>_op_tz & ZAP_OP.DATA<=_a
         || pusty:=0; _w5:=ZAP_OP.DATA; _w6:=ZAP_OP.MA;
            _w7:={? blad_roz=2 | ZAP_OP.DATA-dpo<=0 || 0 || ZAP_OP.DATA-dpo ?};  _ods:=0;
            {? blad_roz<>2 & SER_KOR.SER_SCH().CZY_ODS1='T'
            || _ods:={? OP.TYP='NAL'
                     || {? OP.ZM$4<>0
                        || ((_kwota*({? ZAP_OP.DATA>_data1 || ZAP_OP.DATA-_data1 || 0?})*OP.ZM)/100)$2
                        || exec('ods','rozrach_ods',_data1+1,ZAP_OP.DATA,_kwota,1,_b)$2
                        ?}
                     || 0
                     ?};
               {? (ZAP_OP.DATA-dpo)>0 || _kwota-=ZAP_OP.MA ?};
               {? ZAP_OP.DATA>_data1 || _data1:=ZAP_OP.DATA ?}
            ?};
            {? ~add_nag
            || {? (SER_KOR.TRYB_MON=2 & (SER_KOR.WYB_POZ(); SER_KOR.WYB_POZ<>null)) | SER_KOR.TRYB_MON<>2
               || _tab_ods:={? blad_roz<>2 & SER_KOR.SER_SCH().CZY_ODS1='T' || SER_KOR.TAB_ODS || null ?};
                  _tab_poz:={? SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0 || SER_TPOZ.ref() || null ?};
                  _wal_n:={? l_wal=1 || _b || null ?};
                  _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
                  _ser_def:={? (SER_KOR.TRYB_MON=2 | (SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0)) || SER_DEF.ref() || null ?};
                  {? exec('dodaj','rozrach_kor',SER_KOR.TYP,_ser_def,_a,_tab_ods,_tab_poz,_wal_n,null,_obca)
                  || add_nag:=1; exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w3,_w5,_w6,_w7,1,_ods,_b,0,_symz)
                  ?}
               ?}
            || exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w3,_w5,_w6,_w7,1,_ods,_b,0,_symz)
            ?}
         ?};
         ZAP_OP.next()
      !};
      _ods:=0; _w5:=date(0,0,0);
      {? blad_roz<>2 & SER_KOR.SER_SCH().CZY_ODS1='T'
      || _ods:={? OP.TYP='NAL'
               || {? OP.ZM$4<>0
                  || ((_kwota*(SER_KOR.DATA-_data1)*OP.ZM)/100)$2
                  || exec('ods','rozrach_ods',_data1+1,SER_KOR.DATA,_kwota,1,_b)$2
                  ?}
               || 0
               ?}
      ?};
      _w6:=0;
      _w7:=SER_KOR.DATA-dpo;
      {? _w7>0
      || {? ~add_nag & (pusty=1 | _ods$2<>0)
         || {? (SER_KOR.TRYB_MON=2 & (SER_KOR.WYB_POZ(); SER_KOR.WYB_POZ<>null)) | SER_KOR.TRYB_MON<>2
            || _tab_ods:={? blad_roz<>2 & SER_KOR.SER_SCH().CZY_ODS1='T' || SER_KOR.TAB_ODS || null ?};
               _tab_poz:={? SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0 || SER_TPOZ.ref() || null ?};
               _wal_n:={? l_wal=1 || _b || null ?};
               _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
               _ser_def:={? (SER_KOR.TRYB_MON=2 | (SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0)) || SER_DEF.ref() || null ?};
               {? exec('dodaj','rozrach_kor',SER_KOR.TYP,_ser_def,_a,_tab_ods,_tab_poz,_wal_n,null,_obca)
               || add_nag:=1; exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w3,_w5,_w6,_w7,1,_ods,_b,0,_symz)
               ?}
            ?}
         |? add_nag & (pusty=1 | _ods$2<>0)
         || exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w3,_w5,_w6,_w7,1,_ods,_b,0,_symz)
         ?}
      ?}
   ?}
?}


\wezw2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Generowanie wezwań do zapłaty - wewnętrzna (raty)
::   WE: _a - data generowania wezwania do zapłaty
::       _b - waluta
::  OLD: \wezw2/kor_ser3.fml
::----------------------------------------------------------------------------------------------------------------------
_w1:=OP.SYM; _symz:=OP.SYM_ZEW; _w2:=OP.DO; _w33:=OP.TZ;
last_rat:=obj_new(2); last_rat[1]:=1; last_rat[2]:=0;
ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref);
_splaci:=0; _poz:=0;
{? ZAP_OP.first()
|| {! |?
      {? (ZAP_OP.WN<ZAP_OP.MA) & (ZAP_OP.DATA<=_a)
      || _splaci+=ZAP_OP.MA; {? ZAP_OP.WN<=0 || _splaci+=(-1)*ZAP_OP.WN ?}
      ?};
      ZAP_OP.next()
   !}
?};
_zmn:=0;
{? ZAP_OP.first()
|| {! |?
     {? ZAP_OP.WN<=0 & ZAP_OP.DATA<=_a
     || _splata:=F.SMa(ZAP_OP.WN,ZAP_OP.MA);
        ZAP_OP.cntx_psh(); ZAP_OP.prefix(OP.ref,ZAP_OP.DATA);
        _ref:=ZAP_OP.ref();
        {? ZAP_OP.size()>1 & ZAP_OP.next()
        || {! |?
              {? ZAP_OP.WN<ZAP_OP.MA || _splata+=F.SMa(ZAP_OP.WN,ZAP_OP.MA) ?};
              ZAP_OP.next()
           !};
           _ref:=ZAP_OP.ref()
        ?};
        ZAP_OP.cntx_pop(); ZAP_OP.seek(_ref);
        {! _i:=last_rat[1]..DRB.LRAT
        |? _splata>0
        |! {? t_drb[_i][1]>=_zmn
           || t_drb[_i][1]-=_zmn; _zmn:=0
           || t_drb[_i][1]:=0; _zmn-=t_drb[_i][1]
           ?};
           _iledosp:=t_drb[_i][1]-last_rat[2];
           _w6:={? _iledosp>_splata || _splata || _iledosp ?};
           _splata-=_w6;
           _w3:={? exec('get_par','#parametr',26)='T'
                || exec('dzienRob','kalendarz',t_drb[_i][3],1,1)
                || t_drb[_i][3]
                ?};
           _w33:=t_drb[_i][3];
           {? _w3=date(0,0,0) || _w3:=t_drb[_i][3] ?};
           _w4:=t_drb[_i][1]; _w5:=ZAP_OP.DATA;
           _w7:=_w5-_w3;
           _gen:=0;
           {? _iledosp=_w6
           || last_rat[1]+=1; last_rat[2]:=0
           || last_rat[2]+=_w6;
              {? _splaci<_iledosp || _gen:=1 ?}
           ?};
           {? _gen & _w7<=0 || _zmn+=_w6 ?};
           _splaci-=_w6;
           {? _gen & _w3<>date(0,0,0) & _w7>0 &
              {? SER_KOR.TRYB_MON<>2
              || {? (SER_KOR.DATA-_w3)>SER_KOR.WYB_TRYB().ILE || 1 || 0 ?}
              || SER_KOR.DATA>_w3
              ?}
           || _ods:=0;
              {? (SER_KOR.DATA-_w3)>maxzwl || maxzwl:=(SER_KOR.DATA-_w3) ?};
              {? SER_KOR.SER_SCH().CZY_ODS1='T'
              || _ods:={? OP.TYP='NAL'
                       || {? OP.ZM$4<>0
                          || ((_w7*_w6*OP.ZM)/100)$2
                          || exec('ods','rozrach_ods',_w3+1,ZAP_OP.DATA,_w6,1,_b)
                          ?}
                       || 0
                       ?}
              ?};
              _poz:=1; _zmn:=0;
              {? ~add_nag
              || {? (SER_KOR.TRYB_MON=2 & (SER_KOR.WYB_POZ(); SER_KOR.WYB_POZ<>null)) |
                     SER_KOR.TRYB_MON<>2
                 || _tab_ods:={? SER_KOR.SER_SCH().CZY_ODS1='T' || SER_KOR.TAB_ODS || null ?};
                    _tab_poz:={? SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0 || SER_TPOZ.ref() || null ?};
                    _wal_n:={? l_wal=1 || _b || null ?};
                    _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
                    _ser_def:={? (SER_KOR.TRYB_MON=2 | (SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0))
                              || SER_DEF.ref()
                              || null
                              ?};
                    {? exec('dodaj','rozrach_kor',SER_KOR.TYP,_ser_def,_a,_tab_ods,_tab_poz,_wal_n,null,_obca)
                    || add_nag:=1; exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w33,_w5,_w6,_w7,1,_ods,_b,0,_symz)
                    ?}
                 ?}
              || exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w33,_w5,_w6,_w7,1,_ods,_b,0,_symz)
              ?}
           ?}
        !}
     ?};
     ZAP_OP.next()
   !};
   {! _i:=last_rat[1]..DRB.LRAT
   |! _w3:={? exec('get_par','#parametr',26)='T'
           || exec('dzienRob','kalendarz',t_drb[_i][3],1,1)
           || t_drb[_i][3]
           ?};  _w33:=t_drb[_i][3];
      {? _w3=date(0,0,0) || _w3:=t_drb[_i][3] ?};
      {? _w3<>date(0,0,0) &
         {? SER_KOR.TRYB_MON<>2
         || {? (SER_KOR.DATA-_w3)>SER_KOR.WYB_TRYB().ILE || 1 || 0 ?}
         || SER_KOR.DATA>_w3
         ?}
      || _iledosp:={? _poz || t_drb[_i][1] || t_drb[_i][1]-last_rat[2] ?};
         _w6:=_iledosp; last_rat[2]:=0; _ods:=0;
         {? _iledosp>0
         || _w4:=_iledosp; _w5:=date(0,0,0);
            _w7:=SER_KOR.DATA-_w3;
            {? SER_KOR.SER_SCH().CZY_ODS1='T'
            || _ods:={? _w3<>date(0,0,0) & OP.TYP='NAL'
                     || {? OP.ZM$4<>0
                        || ((_w7*_w6*OP.ZM)/100)$2
                        || exec('ods','rozrach_ods',_w3+1,SER_KOR.DATA,_w6,1,_b)
                        ?}
                     || 0
                     ?}
            ?}; _w6:=0;
            {? _w7>0
            || {? (SER_KOR.DATA-_w3)>maxzwl || maxzwl:=(SER_KOR.DATA-_w3) ?};
               {? ~add_nag
               || {? (SER_KOR.TRYB_MON=2 & (SER_KOR.WYB_POZ(); SER_KOR.WYB_POZ<>null)) |
                     SER_KOR.TRYB_MON<>2
                  || _tab_ods:={? SER_KOR.SER_SCH().CZY_ODS1='T' || SER_KOR.TAB_ODS || null ?};
                     _tab_poz:={? SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0 || SER_TPOZ.ref() || null ?};
                     _wal_n:={? l_wal=1 || _b || null ?};
                     _obca:={? l_wal>1 | (l_wal=1 & SER_KOR.WAL<>FINFO.NAROD) || 1 || 0 ?};
                     _ser_def:={? (SER_KOR.TRYB_MON=2 | (SER_KOR.TRYB_MON<>2 & SER_KOR.WYB_TRYB().TYP=0))
                               || SER_DEF.ref()
                               || null
                               ?};
                     {? exec('dodaj','rozrach_kor',SER_KOR.TYP,_ser_def,_a,_tab_ods,_tab_poz,_wal_n,null,_obca)
                     || add_nag:=1; exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w33,_w5,_w6,_w7,1,_ods,_b,0,_symz)
                     ?}
                  ?}
               || exec('dodajw','!fks_rrk_dkad',_w1,_w4,_w2,_w33,_w5,_w6,_w7,1,_ods,_b,0,_symz)
               ?}
            ?}
         ?}
      ?}
   !}
?};
VAR_DEL.delete('last_rat')


\dodajw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: dodanie pozycji wezwania do zaplaty
::   WE: _a - symbol rozrachunku
::       _b - kwota faktury
::       _c - data faktury
::       _d - termin płatnosci
::       _e - data wplaty
::       _f - kwota wplaty
::       _g - ilosc dni zwloki
::       _h - stopien
::       _i - odsetki
::       _j - waluta
::       _k - (0/1) po/przed ter. pl.
::       _l - symbol zewnetrzny
::  OLD: \dodajw/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
SER_POZ.clear;
SER_POZ.blank();
SER_POZ.REF:=SER_NAG.ref;
SER_POZ.T:=SER_KOR.TYP;
SER_POZ.DOK:=_a;
SER_POZ.KW:=_b;
SER_POZ.DATA:=_c;
SER_POZ.TERM:=_d;
SER_POZ.DATW:=_e;
SER_POZ.KWP:=_f;
SER_POZ.ZWL:=_g;
SER_POZ.ST:=_h;
SER_POZ.ODS:=_i;
SER_POZ.KON:=OP.AN;
SER_POZ.ODD:=OP.ODD;
SER_POZ.WAL:=_j;
SER_POZ.ZWLPRTER:=zwlprter;
SER_POZ.ZWLPOTER:=zwlpoter;
SER_POZ.PRZEDPO:=_k;
SER_POZ.SYM_ROK:=exec('op_unik_sym','rozrach',SER_POZ.DOK,SER_POZ.DATA);
SER_POZ.SYM_ZEW:=_l;
SER_POZ.add();
exec('mod_nag','rozrach_kor')


\wezw_pop1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.50]
:: OPIS: Zmiana rodzaju wezwania do zaplaty
::  OLD: \pop_wezw/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
SER_NAG.cntx_psh(); SER_NAG.prefix(REF.FIRMA);
SER_NAGS.SER_NAG();
_zwrot:={? SER_NAG.ST_AKC='T'
        ||
            {? SER_NAG.ODD<>null
            || FUN.info('Wezwanie wystawione dla kontrahenta:\n%1\ndnia %2'
                    '\n w jednostce księgowej %3'
                    '\njest już zaakceptowane.\nEdycja zabroniona.'@[SER_NAG.KH().NAZ,$SER_NAG.DATA,SER_NAG.ODD().OD]);
                    0
            || FUN.info('Wezwanie wystawione dla kontrahenta:\n%1\ndnia %2'
                    '\njest już zaakceptowane.\nEdycja zabroniona.'@[SER_NAG.KH().NAZ,$SER_NAG.DATA]);
                    0
            ?}
        || 1
        ?};
{? _zwrot
|| SER_DEF.cntx_psh();
   SER_DEF.index('SER_DEFT'); SER_DEF.prefix('W');
   _wer:=SER_DEF.mk_sel('Wybierz rodzaj wezwania do zapłaty'@,,,'ser_def_wer');
   SER_DEF.win_fld(_wer,,'KOD',,,3);
   SER_DEF.win_fld(_wer,,'OP',,,20);
   SER_DEF.win_act(_wer,0,'Formuła','Wybierz'@@,,,,"sel_exit",1,,,,'W');
   SER_DEF.win_sel(_wer);
   {? ~SER_DEF.seek(SER_NAG.KOD) || SER_DEF.first() ?};
   _old_def:=SER_NAG.KOD;
   {? SER_DEF.select(,1) & SER_DEF.ref()<>_old_def
   || {? SER_NAG.SER_TPOZ<>null
      || _ref:=SER_TPOZ.SER_TM; _ref1:=SER_DEF.ref();
         SER_TPOZ.cntx_psh();
         SER_TPOZ.prefix(_ref);
         SER_TPOZ.blank(1); SER_TPOZ.SER_TM:=_ref; SER_TPOZ.SER_DEF:=_ref1;
         {? SER_TPOZ.find_rec()
         || SER_NAG.SER_TPOZ:=SER_TPOZ.ref()
         || FUN.info('Nie znaleziono poziomu trybu monitowania dla wybranego rodzaju.'@); _zwrot:=0
         ?};
         SER_TPOZ.cntx_pop()
      ?};
      {? _zwrot
      || do();
         SER_NAG.KOD:=SER_DEF.ref(); SER_NAG.put();
         SER_NAGS.cntx_psh(); SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref());
         {? SER_NAGS.first()
         || {! |?
               SER_NAGS.KOD:=SER_DEF.ref(); SER_NAGS.put();
               SER_NAGS.next()
            !}
         ?};
         SER_NAGS.cntx_pop();
         end()
      ?}
   ?};
   SER_DEF.cntx_pop()
?};
SER_NAG.cntx_pop()


\szuk_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.62]
:: OPIS: Przy generowaniu wezwań dla schematu lub wybranego poziomu
::       znajdowany jest poziom monitowania i wzorzec wezwania
::  OLD: \szuk_poz/kor_ser2.fml
::----------------------------------------------------------------------------------------------------------------------
_blad:=1; _zn_snag:=0;
SER_NAG.cntx_psh();
SER_TPOZ.index('SER_TPOZ'); SER_TPOZ.prefix(SER_KOR.WYB_TRYB);
{? ~SER_TPOZ.first()
|| exec('addtmpks','rozrach_kor',1,'Kontr.: '+KH.SKR+' tryb mon.: '+SER_KOR.WYB_TRYB().KOD+' - brak poziomów');
   _blad:=0
|| SER_TPOZ.SER_DEF()
?};
{? _blad
|| SER_NAG.index('TYPKH'); SER_NAG.prefix(REF.FIRMA,SER_KOR.TYP,KH.KOD,KH.KOD);
   {? SER_NAG.find_le(SER_KOR.DATA-1)
   || {? SER_KOR.WYB_TRYB().TYP=0
      || SER_TPOZ.cntx_psh(); _zna_poz:=0;
         {? SER_NAG.SER_TPOZ<>null
         || _sn_stp:=SER_NAG.SER_TPOZ;
            {? SER_TPOZ.first()
            || {! |?
                  {? _sn_stp=SER_TPOZ.ref() || _zna_poz:=1 ?};
                  ~_zna_poz & SER_TPOZ.next()
               !}
            ?};
            {? ~_zna_poz || SER_NAG.KOD() ?}
         || SER_NAG.KOD()
         ?};
         SER_TPOZ.cntx_pop();
         OP.index('KON_O'); OP.prefix();  _zn_snag:=1;
         SER_POZ.index('SER_POZR'); SER_POZ.prefix(REF.FIRMA,SER_NAG.ref); _nast_p:=0;
         {? SER_POZ.first()
         || {! |?
               {? OP.find_key(FINFO.NAROD,SER_POZ.ODD,SER_POZ.KON,SER_POZ.DOK,SER_POZ.DOK,SER_POZ.SYM_ROK)
               || F.RObr(OP.ref,SER_KOR.DATA); _swn:=F.SWn(F.rwn,F.rma);
                  {? _swn$2>0 || _nast_p:=1 ?}
               ?};
               ~_nast_p & SER_POZ.next()
            !}
         ?};
         {? _nast_p
         || {? _zna_poz
            || SER_NAG.SER_TPOZ()
            || _kod:=SER_DEF.ref(); _zna_poz:=0;
               {? SER_TPOZ.first()
               || {! |?
                     {? SER_TPOZ.SER_DEF=_kod || _zna_poz:=1 ?};
                     ~_zna_poz & SER_TPOZ.next()
                  !}
               ?};
               {? ~_zna_poz || SER_TPOZ.first(); SER_TPOZ.SER_DEF() ?}
            ?};
            {? _zna_poz
            || {? SER_TPOZ.next()
               || SER_TPOZ.SER_DEF()
               ||_blad:=0;
                  exec('addtmpks','rozrach_kor',1,
                       'Kontr.: '+KH.SKR+' tryb mon.: '+SER_KOR.WYB_TRYB().KOD+
                       ' poziom: '+$SER_NAG.SER_TPOZ().LP+' - brak kolejnego poziomu')
               ?}
            ?}
         || {? SER_TPOZ.first() || SER_TPOZ.SER_DEF() ?}
         ?}
      || {? (SER_KOR.DATA-SER_NAG.DATA)<SER_KOR.WYB_TRYB().ODSTEP
         || _blad:=0;
            exec('addtmpks','rozrach_kor',1,'Kontr.: '+KH.SKR+' tryb mon.: '+SER_KOR.WYB_TRYB().KOD+
                 ' - za mała l. dni od poprz. monitu')
         ?}
      ?}
   ?}
?};
{? SER_KOR.WYB_TRYB().TYP=0 & _blad & _zn_snag & (SER_KOR.DATA-SER_NAG.DATA)<SER_TPOZ.ILE
|| _blad:=0;
   exec('addtmpks','rozrach_kor',1,'Kontr.: '+KH.SKR+' tryb mon.: '+SER_KOR.WYB_TRYB().KOD+
        ' poziom: '+$SER_NAG.SER_TPOZ().LP+' - za mała l. dni od poprz. monitu')
?};
SER_NAG.cntx_pop();
_blad



:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 9042e0d0e6c899b01b0ea23ce686df6e301a9796666a65dcf471f1ec3a95b605647350030e559910fcc20130bf465fcf65eb8e4dd405b86397ec5db70c4b1cbbadfab8ca25a254022e9ec603bf8dc4db0147a6cdb6afe65f806e32f929f271f517749e7ed9900f00102d5a7d2597607e8e4db4590bbad338fd6c848449958ab8

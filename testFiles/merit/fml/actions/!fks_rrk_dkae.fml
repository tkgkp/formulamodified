:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rrk_dkae.fml
:: Utworzony: 04.11.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RRK_DKAE - Akceptacja wezwań do zapłaty
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja wezwań do zapłaty - główna formuła czynności FKS_RRK_DKAE
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_ser_nags','rozrach_kor')
::# access=exec('upr_ser_nags','rozrach_kor')
::# kind=WE, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na wezwanie do zapłaty, required=T, keyref=T
::# kind=WY, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na wezwanie do zapłaty, required=T, keyref=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
_auto:=(_akcja='AkceptujAuto' | _mp.isAutoRun());
SER_KOR.TYP:='W';
params_set(params_get());
{? var_pres('SER_NAG',_in) & ~var_pres('SER_NAG',_out)
|| _out.SER_NAG:=_in.SER_NAG; _mp.save(,_out)
?};
{? _mp.pathTodo() | _auto
|| menu_txt(,'Akceptuj');
   {? var_press('SER_NAG',_out)>0
   || _ref:=BB.sqlint($_in.SER_NAG); _nam:=form(($_in.SER_NAG)-8);
      {? _ref<>0 & _nam<>''
      || SER_NAG.cntx_psh(); SER_NAG.prefix();
         {? SER_NAG.seek(_ref,_nam)
         || {? SER_NAG.T=SER_KOR.TYP
            || SER_NAGS.cntx_psh(); SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref());
               {? SER_NAGS.first()
               || {? _auto
                  || {? params_exec('spr_dokument','rozrach_kor') || params_exec('akc_not1','rozrach_kor',0) ?}
                  || {? SER_NAGS.r_lock(1,1)
                     || params_exec('okno_ser_nag','rozrach_kor','WEZW','Wezwanie do zapłaty');
                        SER_NAGS.r_unlock()
                     || FUN.info('Dokument obsługuje inny operator.'@)

                     ?}
                  ?}
               || {? ~_auto || FUN.info('Nie znaleziono wezwania do zapłaty.'@) ?}; _mp.error()
               ?};
               SER_NAGS.cntx_pop()
            || {? ~_auto || FUN.info('Wybrany dokument nie jest wezwaniem do zapłaty.'@) ?}; _mp.error()
            ?}
         || {? ~_auto || FUN.info('Nie znaleziono wezwania do zapłaty.'@) ?};
            _out.SER_NAG:=null;
            _mp.save(,_out);
            _mp.done()
         ?};
         SER_NAG.cntx_pop()
      || {? ~_auto || FUN.info('Nie znaleziono wezwania do zapłaty.'@) ?};
         _out.SER_NAG:=null;
         _mp.save(,_out);
         _mp.done()
      ?}
   || {? ~_auto || FUN.info('Nie znaleziono wezwania do zapłaty.'@) ?};
      _out.SER_NAG:=null;
      _mp.save(,_out);
      _mp.done()
   ?}
|? _akcja='Akceptuj'
|| menu_txt(,'Akceptuj');
   _mp.trigRef('SER_NAG');
   params_exec('akc_not1','rozrach_kor',0)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zaakceptuj wezwanie do zapłaty'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('SER_NAG',_we)
|| _ref:=BB.sqlint($_we.SER_NAG); _nam:=form(($_we.SER_NAG)-8);
   {? _ref<>0 & _nam<>''
   || SER_NAG.cntx_psh(); SER_NAG.use(_nam); SER_NAG.prefix();
      KH.cntx_psh(); KH.prefix();
      {? SER_NAG.seek(_ref,_nam)
      || _desc:='Zaakceptuj wezwanie do zapłaty dla kontrahenta: %1 z dnia: %2'@@[SER_NAG.KH().SKR,$SER_NAG.DATA]
      ?};
      SER_NAG.cntx_pop(); KH.cntx_pop()
   ?}
?};
_desc


\n_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Czynność FKS_RRK_DKAE - formuła główna w wersji nieprocesowej
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_in:=_args.in;
_akcja:=_args.akcja;
_auto:=(_akcja='AkceptujAuto');
SER_KOR.TYP:='W';
params_set(params_get());
{? _akcja<>'akceptuj'
|| menu_txt(,'Akceptuj');
   _ref:=BB.sqlint($_in.SER_NAG); _nam:=form(($_in.SER_NAG)-8);
   {? _ref<>0 & _nam<>''
   || SER_NAG.cntx_psh(); SER_NAG.prefix();
      {? SER_NAG.seek(_ref,_nam)
      || {? SER_NAG.T=SER_KOR.TYP
         || SER_NAGS.cntx_psh(); SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref());
            {? SER_NAGS.first()
            || {? _auto
               || {? params_exec('spr_dokument','rozrach_kor') || params_exec('akc_not1','rozrach_kor',0) ?}
               || {? SER_NAGS.r_lock(1,1)
                  || params_exec('okno_ser_nag','rozrach_kor','WEZW','Wezwanie do zapłaty');
                     SER_NAGS.r_unlock()
                  || FUN.info('Dokument obsługuje inny operator.'@)
                  ?}
               ?}
            || {? ~_auto || FUN.info('Nie znaleziono wezwania do zapłaty.'@) ?}
            ?};
            SER_NAGS.cntx_pop()
         || {? ~_auto || FUN.info('Wybrany dokument nie jest wezwaniem do zapłaty.'@) ?}
         ?}
      || {? ~_auto || FUN.info('Nie znaleziono wezwania do zapłaty.'@) ?}
      ?};
      SER_NAG.cntx_pop()
   || {? ~_auto || FUN.info('Nie znaleziono wezwania do zapłaty.'@) ?}
   ?}
|? _akcja='Akceptuj'
|| menu_txt(,'Akceptuj');
   params_exec('akc_not1','rozrach_kor',0)
?};
~~


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'ser_nag'>0
         || _record:=exec('FindAndGet','#table',SER_NAG,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 5700111604929cb2865dbe9dd64731666baeba71eb91930c048fbe653e7f324bc6bb4b2f657e0d59dc3dc2611c5a7ec30e5b8bba3fbbe6a12ba5f997069d39a9c92e91dcfd9e49581ec6a08f81fdb5da1bb125df63c8624c6ec0c3ca0f22fc42f37503621183548beda2f37484e169304ab136bcddcbc8a832293361802a2a96

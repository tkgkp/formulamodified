:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_pll_rsok.fml
:: Utworzony: 18.01.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: PPL_PLL_RSOK - Rej. składników okresowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rej. składników okresowych - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::# parses=exec('parses_o','ppl',params_get().in.O)
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::# kind=WE, symbol=O, type=_O, name=Wskazanie listy płac, required=N, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.P);
{? _uidref=''
|| _result:=exec('error','!ppl_pll_rsok')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!ppl_pll_rsok',_in.P);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rej. składników okresowych - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj składniki okresowe: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj składniki okresowe: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj składniki okresowe: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj składniki okresowe'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie składników okresowych niemożliwa.\nNie znaleziono pracownika.'


\prem_okr_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli PREM_OKR.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::
::       Uwaga. Zmiany w bieżącej formule powinny być również uwzględnione w formułach obsługujących czynność grupowego
::              wprowadzania składników okresowych.
::
::   WE:  _a  [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(PREM_OKR)
|| _TAB:=_b
|? _tryb='1'
|| _TAB:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? _fld='' | _fld='SKP'
|| _val:=PREM_OKR.SKP<>null();
   _sval:=$_val;
   _TAB.efld_opt(_we,'enable='+_sval,PREM_OKR,'SKP','R_RT');
   _TAB.efld_opt(_we,'enable='+_sval,PREM_OKR,'RP');
   _TAB.efld_opt(_we,'enable='+_sval,PREM_OKR,'O_OD');
   _TAB.efld_opt(_we,'enable='+_sval,PREM_OKR,'O_DO');
   _TAB.efld_opt(_we,'enable='+_sval,PREM_OKR,'KW_N');
   _TAB.efld_opt(_we,'enable='+_sval,PREM_OKR,'KW_W')
?};

{? _fld='' | _fld='SKP' | _fld='B_WYP'
|| _val:=PREM_OKR.B_WYP='N' & ((PREM_OKR.SKP().RZ='T' & PREM_OKR.SKP().KZ='N') | PREM_OKR.SKP().R_N<>null());
   _TAB.efld_opt(_we,'mark='+$_val,PREM_OKR,'KW_N')
?};

{? _fld='' | _fld='SKP' | _fld='B_WYP' | _fld='KW_N'
|| _val:=PREM_OKR.SKP<>null() & PREM_OKR.B_WYP='N' &
      (PREM_OKR.SKP().RZ='N' | PREM_OKR.SKP().KZ='W') & PREM_OKR.SKP().CZY_FM='N';
   _TAB.efld_opt(_we,'mark='+$_val,PREM_OKR,'KW_W')
?};

1


\prem_okr_skp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola PREM_OKR.SKP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PREM_OKR.SKP().RP<>'S'
|| exec('prem_okr_efld_opt','!ppl_pll_rsok');
   {? PREM_OKR.SKP
   || PREM_OKR.RP:=PREM_OKR.SKP().RP
   || exec('blank','#field',PREM_OKR,,'RP')
   ?}
|| FUN.emsg('%1\n%2'
      [  'Wybrany składnik jest związany z okresem rozliczeniowym z obszaru rozliczenia czau pracy.'@,
         'Wprowadzanie ręczne nie jest dozwolone.'@
      ]
   )
?}


\prem_okr_kw_n_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola PREM_OKR.KW_N.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('prem_okr_efld_opt','!ppl_pll_rsok')


\prem_okr_kw_w_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed redagowaniem pola PREM_OKR.KW_W.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Jeżeli jest określona formuła na kwotę - uzupełnianie wartości nie ma sensu. Wartość zostanie i tak nadpisana.
PREM_OKR.SKP().CZY_FM='N'


\prem_okr_b_wyp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola PREM_OKR.B_WYP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('prem_okr_efld_opt','!ppl_pll_rsok')


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a [REFERENCE] - Wskazanie pracownika.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::  OLD: \sel_prok/skl_okr.fml
::  OLD: \pw_skl_n/skl_okr.fml   \
::  OLD: \f3_skl_n/skl_okr.fml    | Funkcjonalność obsługi wyboru definicji składnika okresowego w trakcie redagowania
::  OLD: \po_r_skl_n/skl_okr.fml  | jego wartości została znacząco zmieniona.
::  OLD: \p_w_skl_t/skl_okr.fml  /
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a)
|| OSOBA.cntx_psh();
   P.OSOBA();
   VAR.O();
   PREM_OKR.cntx_psh();
   PREM_OKR.index('PREM_OKR');
   PREM_OKR.prefix(P.ref());
   PREM_OKR.win_edit('RED');
   PREM_OKR.win_patt('SZUK');
   PREM_OKR.win_sel('WER_P');
   _ret:=PREM_OKR.select();
   PREM_OKR.cntx_pop();
   OSOBA.cntx_pop()
|| _ret:=exec('error','!ppl_pll_rsok')
?};
P.cntx_pop();
_ret


\prem_okr_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknach wertowania tabeli PREM_OKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PREM_OKR.blank();
exec('prem_okr_efld_opt','!ppl_pll_rsok','*',PREM_OKR,'RED')


\prem_okr_mod_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [2011]
:: OPIS: Przed akcjami modyfikującymi zapisy tabeli PREM_OKR.
::  OLD: \pop_s_pr/skl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_RetVal:=0;
{? VAR.JESTLIST
|| {? PREM_OKR.SKP().RP='S'
   || _RetVal:=-3
   |? PREM_OKR.LT=''
   || _RetVal:=1
   |? VAR.NAZWALIS<>PREM_OKR.LT
   || _RetVal:=-1
   || _RetVal:=exec('o_writable','lista_plac',PREM_OKR.LT)
   ?}
|| _RetVal:={? PREM_OKR.LT<>'' || -2 || 1 ?}
?};
{? _RetVal<0
|| FUN.emsg(
      {? _RetVal=-1
      || '%1\n%2'
            [  'Lista płac %1 nie rozlicza premii.'@[VAR.NAZWALIS],
               'Wybierz listę płac uwzględnioną w rozliczeniu.'@
            ]
      |? _RetVal=-2
      || '%1\n%2'
            [  'Premia została rozliczona.'@,
               'Wybierz odpowiednią listę płac.'@
            ]
      |? _RetVal=-3
      || '%1\n%2'
            [  'Wybrany składnik jest związany z okresem rozliczeniowym z obszaru rozliczenia czau pracy.'@,
               'Wprowadzanie ręczne nie jest dozwolone.'@
            ]
      || ''
      ?}
   )
?};
{? _RetVal<=0
|| 0
|| 1
?}


\prem_okr_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw - przed" w oknach wertowania tabeli PREM_OKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('prem_okr_mod_b','!ppl_pll_rsok')
|| exec('prem_okr_efld_opt','!ppl_pll_rsok','*',PREM_OKR,'RED')
?}


\prem_okr_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń - przed" w oknach wertowania tabeli PREM_OKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('prem_okr_mod_b','!ppl_pll_rsok')


\prem_okr_oo_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Okresy obowiązywania - przed" w oknach wertowania tabeli PREM_OKR.
::   WE:
::   WY:
::  OLD: \sel_pexc/skl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
PREM_EXC.cntx_psh();
PREM_EXC.index('PREM_EXC');
PREM_EXC.prefix(P.ref());
PREM_EXC.win_edit('RED');
PREM_EXC.win_patt('RED');
PREM_EXC.win_sel('WER');
PREM_EXC.select();
PREM_EXC.cntx_pop();
~~


\prem_okr_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknach wertowania tabeli PREM_OKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('prem_okr_efld_opt','!ppl_pll_rsok','*',PREM_OKR,'RED');
PREM_OKR.display()


\prem_okr_zakoncz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli PREM_OKR z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli PREM_OKR jako składowa okna grupowego tabeli P).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=PREM_OKR
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:19 4cea5199389ea590d619cd567ac2d30a950e868e2ca28f1655c0c03a4ef3eaa34401064851d8d737758f11ff82c6866e89d361a4b7df07e3139debf28ec1d314efccc3633e04ffb30316809a05e32be15751556c7c34e06a66d0d08742121e9476051740e0c3909765dc8a3909202ee5e55e863bf7b272982aec165044645343

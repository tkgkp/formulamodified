:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_wte_arte.fml
:: Utworzony: 02.06.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_WTE_ARTE - Akceptacja wzorca technologii
::            Uwaga: większość kodu jest wspólna z czynnością TTE_TEC_ARTE - w przypadku zmian modyfikować oba pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności akceptacji wzorca technologii (TTE_WTE_ARTE)
::       Zadaniem formuły jest zaakceptowanie całego wzorca technologii
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=T, keyref=T
{? var_pres('TKTL',_in)<>type_of(~~) & var_pres('TKTL',_in)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_in)=type_of(~~) || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=N
{? var_pres('TKTL',_out)<>type_of(~~) & var_pres('TKTL',_out)<>type_of(null()) || return() ?};

VAR.A_TORW:='W';

_res:='';

{? _in.TKTL
||
   {? exec('FindAndGet','#table',TKTL,_in.TKTL,,"TORW",'')<>'W'
   || _msg:='Niezgodność wywołania czynności.\nNie można wykonać akceptacji dla karty technologicznej.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   |? exec('FindAndGet','#table',TKTL,_in.TKTL,,"ARCH",'')<>'N'
   || _msg:='Niezgodność wywołania czynności.\nNie można wykonać akceptacji dla wzorca archiwalnego.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   ||
      exec('tktl_cntx_psh','tech_common');
      exec('tktl_use','tech_common',ref_name(_in.TKTL)+3);
      TKTL.clear();

      {? TKTL.seek(_in.TKTL)
      || {? TKTL.STAN='T'
         || FUN.emsg(exec('form','#string','L',
               'Akceptacja wzorca technologii '+TKTL.NRK+' wer. '+TKTL.WER,'',
               'Wzorzec już zaakceptowany.'
            ));
            _out.TKTL:=TKTL.ref();
            _mp.save(,_out);
            _mp.done();
            _res:='key:Esc'

         || VAR.A_KTL:=TKTL.ref();

::          Wywołanie z listy ToDo umożliwia podejrzenie elementów wzorca przed jej akceptacją
            {? _mp.pathTodo() & ~_mp.isAutoRun()
            || _mp.keep();
               {? exec('tktl_lock','tech_common',,'N') &
                  exec('tktl_lock','tech_common',,'O') &
                  exec('tktl_lock','tech_common',,'S') &
                  exec('tktl_lock','tech_common',,'P')
               || VAR.GRUPA:='N';
                  _red:=exec('win_edit','!tte_wte_arte');
                  TKTL.win_edit(_red);
                  TKTL.edit();
                  TKTL.win_edit();
                  TKTL.win_edel(_red)
               ?};
               exec('tktl_unlock','tech_common',,'N');
               exec('tktl_unlock','tech_common',,'O');
               exec('tktl_unlock','tech_common',,'S');
               exec('tktl_unlock','tech_common',,'P')

            |? _mp.akcja()=''
            || _ok:=exec('tktl_akceptuj','!tte_wte_arte',TKTL.ref(),_mp,-1);
               {? _ok=1
               || _out.TKTL:=TKTL.ref();
                  _mp.save(,_out);
                  _mp.done()
               ?}

            |? _mp.akcja()='MENU'
            || _ok:=exec('tktl_akceptuj','!tte_wte_arte',TKTL.ref(),_mp,0);
               {? _ok=1
               || _out.TKTL:=TKTL.ref();
                  _mp.save(,_out);
                  _mp.done()
               ?}

            |? _mp.akcja()='BUTTON'
            || _ok:=exec('tktl_akceptuj','!tte_wte_arte',TKTL.ref(),_mp,0);
               {? _ok=1
               || _out.TKTL:=TKTL.ref();
                  _mp.save(,_out);
                  _mp.done();
                  _res:='key:Esc'
               ?}
            ?}
         ?}
      ?};
      exec('tktl_cntx_pop','tech_common')
   ?}

|| _msg:='Nie przekazono albo usunięto wzorzec technologii.';
   FUN.emsg(_msg);
   _mp.error(_msg)
?};
_res


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności akceptacji wzorca technologii (TTE_WTE_ARTE)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zaakceptuj wzorzec technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"'Zaakceptuj wzorzec technologii %1 wer. %2'@@[NRK,WER]",'')

:: jest parametr wejściowy to ustawiam odpowiedniego TKTL
|? var_pres('TKTL',_in)
|| _tmp:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zaakceptuj wzorzec technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"'Zaakceptuj wzorzec technologii %1 wer. %2'@@[NRK,WER]",'')

?};
_desc


\action_menu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Akceptacja wzorca technologii - właściwa akcja z menu okna wertowania
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_WTE_ARTE';
_args.UIDREF:=TKTL.uidref();
_args.AKCJA:='MENU';
_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
_args.PROC_START:='N';

exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

exec('mp_run','#b__box',_args)


\action_button
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Akceptacja wzorca technologii - właściwa akcja z przycisku okna redakcyjnego
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_WTE_ARTE';
_args.UIDREF:=TKTL.uidref();
_args.AKCJA:='BUTTON';
_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
_args.PROC_START:='N';

exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

_res:=exec('mp_run','#b__box',_args);
{? type_of(_res)<>type_of('') || _res:='' ?};
_res


\tktl_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Akceptacja wzorca technologii (podstawowy kod)
::   WE: _a - TKTL.ref()
::       _b - obiekt odpowiedzialny za obsługę procesu
::       _c - wariant działania:
::            -1 - akceptacja bez pytania
::            0 - wyświetlać tylko pytanie o akceptację wzorca
::            1 - wyświetlać dodatkowo pytanie o podgląd wzorca
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;
_mp:=_b;
_variant:=_c;

_res:=0;

TKTL.cntx_psh();
TKTL.clear();
{? TKTL.seek(_tktl)
||
   _txt:='Akceptacja wzorca technologii %1 wer. %2'@[TKTL.NRK,TKTL.WER];
   {? TKTL.STAN='T'
   || FUN.emsg(exec('form','#string','L',_txt,'','Wzorzec już zaakceptowany.'@))

   |? TKTL.STAT_N='N'
   || FUN.emsg(exec('form','#string','L',_txt,'','Najpierw należy zakończyć redakcję nagłówka wzorca.'@))

   |? (TKTL.STAT_O='N' & TKTL.TYP().OPER='T') | TKTL.STAT_S='N' | TKTL.STAT_P='N'
   || _kropy:=obj_new(3); _kropy[1]:=_kropy[2]:=_kropy[3]:='';
      _kropa:=0;
      {? TKTL.STAT_O='N' & TKTL.TYP().OPER='T' || _kropa+=1; _kropy[_kropa]:='• '+'operacje'@ ?};
      {? TKTL.STAT_S='N' || _kropa+=1; _kropy[_kropa]:='• '+'surowce'@ ?};
      {? TKTL.STAT_P='N' || _kropa+=1; _kropy[_kropa]:='• '+'pozostałe dane'@ ?};
      _txt:=exec('form','#string','L',
         _txt,'','',
         'Najpierw należy zakończyć redakcję wzorca w następującym zakresie:'@,
         '',_kropy[1],_kropy[2],_kropy[3]
      );
      FUN.emsg(_txt)

   ||
::    Sprawdzana możliwość zablokowania wzorca w każdym zakresie
      {? exec('tktl_lock','tech_common',,'N') &
         exec('tktl_lock','tech_common',,'O') &
         exec('tktl_lock','tech_common',,'S') &
         exec('tktl_lock','tech_common',,'P')
      ||
         {? _variant=-1
         || TKTL.STAN:='T';
            {? TKTL.put()
            || _res:=1
            ?}

         |? _variant=0
         || {? FUN.ask('Czy zaakceptować wzorzec %1 wer. %2?'@[TKTL.NRK,TKTL.WER])
            || TKTL.STAN:='T';
               {? TKTL.put()
               || _res:=1
               ?}
            ?}

         |? _variant=1
         || _choice:=FUN.choice('Czy zaakceptować wzorzec %1 wer. %2?'@[TKTL.NRK,TKTL.WER],,'Podgląd'@,'Tak'@);
            {? _choice=1
            || _mp.keep();
               VAR.GRUPA:='N';
               _red:=exec('win_edit','!tte_wte_arte');
               TKTL.win_edit(_red);
               TKTL.edit();
               TKTL.win_edit();
               TKTL.win_edel(_red)
            |? _choice=2
            || TKTL.STAN:='T';
               {? TKTL.put()
               || _res:=1
               ?}
            ?}
         ?}
      ?};
      exec('tktl_unlock','tech_common',,'N');
      exec('tktl_unlock','tech_common',,'O');
      exec('tktl_unlock','tech_common',,'S');
      exec('tktl_unlock','tech_common',,'P')

   ?}
?};
TKTL.cntx_pop();

_res


\tktl_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Wycofanie akceptacji wzorca technologii
::   WE: [_a] - TKTL.ref() / bieżący wzorzec
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) || _tktl:=_a || _tktl:=TKTL.ref() ?};

_res:=0;

TKTL.cntx_psh();
TKTL.clear();
{? TKTL.seek(_tktl)
||
   {? TKTL.STAN='N'
   || FUN.emsg('Nie można wycofać akceptacji — wzorzec niezaakceptowany.'@)

   || _tab:=tab_tmp(,
         'STAN','INTEGER','Stan wzorca'@,
         'STAT_P','INTEGER','Pozostałe dane'@,
         'STAT_S','INTEGER','Surowce'@,
         'STAT_O','INTEGER','Operacje'@,
         'STAT_N','INTEGER','Nagłówek'@
      );
      _efld_opt:="
         {? _tab.STAN=0
         || _tab.efld_opt(_red,'enable=0',,'STAT_P');
            _tab.efld_opt(_red,'enable=0',,'STAT_S');
            _tab.efld_opt(_red,'enable=0',,'STAT_O');
            _tab.efld_opt(_red,'enable=0',,'STAT_N')
         || _tab.efld_opt(_red,'enable=1',,'STAT_P');
            _tab.efld_opt(_red,'enable=1',,'STAT_S');
            _tab.efld_opt(_red,'enable=1',,'STAT_O');
            _tab.efld_opt(_red,'enable=1',,'STAT_N')
         ?};
         ~~
      ";
      _formula:=$("_tab:=cur_tab(1,1);_red:=cur_win(1,1);"+_efld_opt);
      _tab.fld_fml('STAN','AFTER_EDIT',_formula);
      _tab.fld_fml('STAT_P','AFTER_EDIT',$(_formula+";{? _tab.STAT_P=0 || _tab.STAT_N:=0 ?};~~"));
      _tab.fld_fml('STAT_S','AFTER_EDIT',$(_formula+";{? _tab.STAT_S=0 || _tab.STAT_N:=0 ?};~~"));
      _tab.fld_fml('STAT_O','AFTER_EDIT',$(_formula+";{? _tab.STAT_O=0 || _tab.STAT_N:=0 ?};~~"));
      _tab.fld_fml('STAT_N','AFTER_EDIT',$(_formula+";{? _tab.STAT_N=1 || _tab.STAT_O:=_tab.STAT_S:=_tab.STAT_P:=1 ?};~~"));

      {? TKTL.STAN='P'
      || _red:=_tab.mk_edit('Wycofanie wzorca'@,,'wycofaj1');
         _tab.STAN:=1
      || _red:=_tab.mk_edit('Wycofanie wzorca'@,,'wycofaj2');
         _tab.STAN:=0;
         _tab.win_esep(_red,'Wycofanie akceptacji'@);
         _tab.win_efld(_red,,'STAN',,,,,,,,,'check-box',,"1","0")
      ?};
      _tab.STAT_N:=_tab.STAT_O:=_tab.STAT_S:=_tab.STAT_P:=0;
      _tab.win_esep(_red,'Wycofanie rejestracji'@);
      _tab.win_efld(_red,,'STAT_P',,,,,,,,,'check-box',,"1","0");
      _tab.win_efld(_red,,'STAT_S',,,,,,,,,'check-box',,"1","0");
      _tab.win_efld(_red,,'STAT_O',,,,,,,,,'check-box',,"1","0");
      _tab.win_efld(_red,,'STAT_N',,,,,,,,,'check-box',,"1","0");
      _tab.win_ebtn(_red,'text=%1,align=end'['&Zapisz'@],"'key:F2'");
      _tab.win_ebtn(_red,'text=%1,align=end'['&Anuluj'@],"'key:Esc'");
      ($("_tab:=_a;_red:=_b;"+_efld_opt))(_tab,_red);
      _tab.win_edit(_red);

      _valid:="
         _tab:=cur_tab(1,1);
         {? TKTL.STAN='P'
         || {? _tab.STAT_N=0 & _tab.STAT_O=0 & _tab.STAT_S=0 & _tab.STAT_P=0
            || FUN.info('Co najmniej jedno pole musi być zaznaczone.'@); 'STAT_P'
            || 1
            ?}
         || {? _tab.STAN=0
            || FUN.info('Pole musi być zaznaczone.'@); 'STAN'
            || 1
            ?}
         ?}
      ";

      {? _tab.edit(_valid)
      ||
::       Sprawdzana możliwość zablokowania wzorca w każdym zakresie
         {? exec('tktl_lock','tech_common',,'N') &
            exec('tktl_lock','tech_common',,'O') &
            exec('tktl_lock','tech_common',,'S') &
            exec('tktl_lock','tech_common',,'P')
         || TKTL.STAN:={? _tab.STAT_N & _tab.STAT_O & _tab.STAT_S & _tab.STAT_P || 'N' || 'P' ?};
            {? _tab.STAT_P || TKTL.STAT_P:='N' ?};
            {? _tab.STAT_S || TKTL.STAT_S:='N' ?};
            {? _tab.STAT_O || TKTL.STAT_O:='N' ?};
            {? _tab.STAT_N || TKTL.STAT_N:='N' ?};
            TKTL.put()
         ?};
         exec('tktl_unlock','tech_common',,'N');
         exec('tktl_unlock','tech_common',,'O');
         exec('tktl_unlock','tech_common',,'S');
         exec('tktl_unlock','tech_common',,'P')
      ?}
   ?}
?};

TKTL.cntx_pop();

_res


\win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Tworzy okno edycji wykorzystane na liście todo
::       Kontekst wywołania - rekord tabeli TKTL
::   WY: akronim okna
::----------------------------------------------------------------------------------------------------------------------
_red:=TKTL.mk_edit('Akceptacja wzorca technologii'@,,'tktl_ared');
TKTL.win_ewin(_red,,'DISP_');
_btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['Operacje'@],"exec('action_oper_button','tech_oper')");
task_attach('TTE_WTE_DRTO');
TKTL.btn_eopt(_red,_btn,{? TKTL.TYP().OPER='N' || 'state=grayed' || 'state=normal' ?});
_btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['Surowce'@],"exec('action_mater_button','tech_mater')");
task_attach('TTE_WTE_DRTS');
_btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['N-P-U'@],"exec('action_tools_button','tech_tool')");
task_attach('TTE_WTE_DRDP');
TKTL.btn_eopt(_red,_btn,{? TKTL.TYP().UTIL='N' || 'state=grayed' || 'state=normal' ?});
_btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['Parametry'@],"exec('action_params_button','tech_param')");
task_attach('TTE_WTE_DRDP');
TKTL.btn_eopt(_red,_btn,{? TKTL.TYP().PAR='N' || 'state=grayed' || 'state=normal' ?});
_btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['Atrybu&ty'@],"exec('action_attrib_button','tech_atr')");
task_attach('TTE_WTE_DRDP');
_btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['Doku&mentacja'@],"exec('action_docum_button','tech_doc')");
task_attach('TTE_WTE_DRDP');
_btn:=TKTL.win_ebtn(_red,'text=%1,align=end'['A&kceptuj'@],"exec('action_button','!tte_wte_arte')");
task_attach('TTE_WTE_ARTE');
TKTL.btn_eopt(_red,_btn,'default=1');
_btn:=TKTL.win_ebtn(_red,'text=%1,align=end'['&Anuluj'@],"'key:Esc'");
_red

:Sign Version 2.0 jowisz:1045 2023/07/27 11:25:59 bc33874067765a60e6214925223669337fd31c774580420fe17da8400db05c226b99380b8c0682c89a5db8d8758d861773be576ad0886d3646ba0cd33c155456f78fca5aae479a1923da79344064534bdd73eb1fcbfee8cd76b690aa48b32265bbf3fe37799fae861ec98d1ef185c79eee13a1c36e76dd2f41b9b77eef95e1aa

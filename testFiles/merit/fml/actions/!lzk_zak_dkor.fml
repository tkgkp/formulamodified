:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zak_dkor.fml
:: Utworzony: 09.07.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Rejestracja korekty zakupu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LZK,TARD
::# parses=exec('parses','!lzk_zak_dkor')
::# kind=WE,   symbol=TYPYSP, type=_TYPYSP,  name=Typ dokumentu zakupu,        required=N, fml_val="exec('typysp','!lzk_zak_dkor')"
::# kind=WE,   symbol=ZAK,    type=_FAKS,    name=Dokument zakupu,             required=N, keyref=T
::# kind=WE,   symbol=KORZ,   type=_FAKS,    name=Korekta zakupu,              required=N, keyref=T
::# kind=WE,   symbol=EDOKUM, type=_EDOKUM,  name=Dokument w obiegu,           required=N
::# kind=WY,   symbol=KORZ,   type=_FAKS,    name=Korekta zakupu,              required=N
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

exec('init_zak','lzk');

:: Przepisanie portu wejściowego KORZ na wyjściowy KORZ
{? var_press('KORZ',_in)=type_of(null()) & _in.KORZ
   & (var_press('KORZ',_out)<>type_of(null()) | _out.KORZ=null())
|| _out.KORZ:=_in.KORZ;
   _mp.save(,_out)
?};

:: Uruchomiona akcja
_akcja:=_mp.akcja();
_area_zws_khr:=_mp.pathArea('ZWS_KHR');
_area_lzk_zak:=_mp.pathArea('LZK_ZAK');
_area_zbl_dok:=_mp.pathArea('ZBL_DOK');
_area:=_area_zws_khr | _area_lzk_zak | _area_zbl_dok;
_proc:=_mp.pathProc();
_todo:=_mp.pathTodo() | _mp.pathArea() & ~_area;
_grupa:=_mp.isGroup();
_autoakc:=exec('autoAkc','#b__box',_mp,200080,'LZK_ZAK_EZAK');

{? _todo
:: Ustawienie kontekstu wg instancji elementu w procesie
|| {? var_pres('KORZ',_out)=type_of(null()) & _out.KORZ
:: Jest dokument korekty
   || 1
   |? var_pres('ZAK',_in)=type_of(null()) & _in.ZAK
:: Brak dokumentu korekty i jest na wejściu dokument sprzedaży do korekty
   || FAKS.use(ref_name(_in.ZAK));
      FAKS.prefix();
      {? FAKS.seek(_in.ZAK)
      || exec('open','open_tab',FAKS.ODDZ,form(FAKS.AR-2000,-2))
      ?};
      _akcja:='START_TODO'
   |? ~_mp.isMicro()
:: Brak dokumentu korekty i brak na wejściu dokumentu sprzedaży do korekty
   || _akcja:='START_TODO'
   || _mp.error('Brak parametrów wejściowego ZAK, wyjściowego KORZ.')
   ?}
?};

:: Sprawdzenie uprawnień
{? params_exec('permissions_chk','lzk','!lzk_zak_dkor')=0 || return() ?};

:: Pobranie uidref korygowanej faktury
_keyRef:='';
{? var_pres('ZAK',_in)=type_of(null()) & _in.ZAK
|| _keyRef:=exec('FindAndGet','#table',FAKS,$_in.ZAK,,"FAKS.uidref()",'')
?};

:: Wyzwalacz, który po dodaniu nagłówka korekty:
:: - add: dodaje rekord kluczowy nagłówka korekty
::   del: usuwa rekord kluczowy nagłówka korekty
:: - add: zapisuje parametr wyjściowy KORZ - wskazanie na nagłówek korekty
::   del: zapisuje parametr wyjściowy KORZ - null
:: - add: usuwa kluczowy rekord nagłówka dokumentu korygowanego
::   del: dodaje rekord kluczowy nagłówka dokumentu korygowanego
_mp.trigRef('FAKS',,1,,exec('kind_out','#b_port'),'KORZ',_keyRef);

_typysp_t:={? var_pres('TYPYSP',_in)=type_of(null())
           || exec('FindAndGet','#table',TYPYSP,_in.TYPYSP,,"TYPYSP.T")
           || ''
           ?};
_edokum:={? var_pres('EDOKUM',_in)=type_of(null()) || _in.EDOKUM || null() ?};
_zafakt:=var_pres('KORZ',_out)=type_of(null()) & _out.KORZ;

{? ~_zafakt & _edokum<>null()
|| _ctrl:=exec('ctrlEDOKUM','faktury_nag',_edokum,'Z','K');
   {? ~_ctrl
   || _mp.error('Niepoprawny parametr EDOKUM - typ dokumentu w obiegu niezgodny z czynnością.'@)
   |? _ctrl=-1
   || _mp.done()
   |? _ctrl=2
   || FUN.info('Dokument w obiegu nie został zaakceptowany.');
      _mp.cancel()
   || params_set('out',_out,'mp',_mp,'akcja',_akcja);
      _faks:=params_exec('GenWgEDOKUM','faktury_nag',_edokum,'Z','K',_typysp_t)
   ?}
|? _akcja='Dołącz'
   | _proc
   | _akcja='START_TODO'
:: Obsługa Dołącz - dołącz w oknie obszaru i start procesu z pulpitu
|| _typysp_t:=
      {? var_pres('TYPYSP',_in)=type_of(null())
      || exec('FindAndGet','#table',TYPYSP,_in.TYPYSP,,"TYPYSP.T")
      || ''
      ?};
   TAZ.RAB_N_BE:=0;
   BEER.WYSTFAKS:=null;
   HELP.WHERE:='K';
   HELP.WMAG:=0;

:: Korygowany dokument
   _faks:={? var_pres('ZAK',_in)=type_of(null()) & _in.ZAK || _in.ZAK || null() ?};

:: Parametr 'akcja' wykorzystywany w formułach przycisków 'Pozycje', 'Zakończ'
   params_set('out',_out,'mp',_mp,'akcja',_akcja,'context',_context);
   exec('wystkzak','faktury_nag',_faks,_typysp_t);

:: Podczytanie parametrów wyjściowych
   _outa:=_mp.load(exec('kind_out','#b_port'));
   {? (var_pres('KORZ',_out)=type_of(~~) | var_pres('KORZ',_out)=type_of(null()) & _out.KORZ=null())
      & var_pres('KORZ',_outa)<>type_of(~~) & _outa.KORZ
:: Dodano dokument
   ||
::    Ustawienie się na dodanym dokumencie w widoku obszaru
      params_exec('seek_dok','faktury_wspolne',_area_lzk_zak,_outa.KORZ)
:: Wycofanie czynności bo nie dodano dokumentu
   || _mp.cancel()
   ?}

|? _akcja='Businesslink'
||
   {? ~_mp.isMicro()
   ||
::    Przepisanie portu wejściowego FAKS na wyjściowy KORZ
      {? var_pres('KORZ',_in)=type_of(null()) & _in.KORZ
            &
         (var_pres('KORZ',_out)<>type_of(null()) | _out.KORZ=null())
      ||
         _out.KORZ:=_in.KORZ;
         _mp.save(,_out)
      ?};
      _mp.keep()
   ?}

|? _akcja='Popraw'
   | _todo
|| {? var_pres('KORZ',_out)=type_of(null())
:: Uruchomione w procesie
   || FAKS.cntx_psh();
      FAKS.use(ref_name(_out.KORZ));
      FAKS.cntx_psh();
      FAKS.prefix();
      {? FAKS.seek(_out.KORZ)
      || exec('faks_win_edit_set','faktury_nag');

         params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('fak_pop','faktury_nag');

         _mp.descTodo()
      ?};
      FAKS.cntx_pop();
      FAKS.cntx_pop()
   |? _mp.isMicro()
:: Uruchomione poza procesem
   || {? ~_todo
      || params_set('out',_out,'mp',_mp,'akcja',_akcja);
         exec('fak_pop','faktury_nag')
      |? _akcja='Zakończ_wer'
      || {? exec('faks_zakoncz','faktury_nag',,_autoakc) || _mp.done() ?}
      || _mp.cancel()
      ?}
   |? _todo
:: Uruchomione zadanie z listy todo i brak _out.FAKS wtedy zadanie wycofujemy
   || _mp.cancel()
   ?}

|? _akcja='Usuń'
|| {? var_pres('KORZ',_out)=type_of(null())
:: Uruchomione w procesie
   || _faks:=null();
      FAKS.cntx_psh();
      FAKS.use(ref_name(_out.KORZ));
      {? ~_area_lzk_zak || FAKS.prefix() ?};
      {? FAKS.seek(_out.KORZ)
      ||
         exec('usun_fak','faktury_nag',{? _grupa || 2 || 1 ?});

         {? ~FAKS.seek(_out.KORZ)
::       Wycofuję instancję jeśli nie znaleziono dokumentu
         || _mp.cancel()
         ?};
         _faks:=FAKS.ref()
      ?};
      FAKS.cntx_pop();
::    Ustawienie się na kolejnym dokumencie w widoku obszaru
      {? _area_lzk_zak || {? _faks || FAKS.seek(_faks) ?} ?}
   |? _mp.isMicro()
:: Uruchomione poza procesem
   ||
      exec('usun_fak','faktury_nag',{? _grupa || 2 || 1 ?});

      _mp.done()
   ?}

|? _akcja='Zakończ_wer' & _area_lzk_zak
|| {? exec('faks_zakoncz','faktury_nag',_grupa,_autoakc) || _mp.done() ?}

|| _mp.error('Nieobsługiwana ścieżka.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));

{? var_pres('KORZ',_out)=type_of(null()) & _out.KORZ
|| 'Zakończ rejestrację korekty zakupu: %1'@@[exec('record','#to_string',_out.KORZ)]
|| _in:=_mp.load(exec('kind_in','#b_port'));
   {? var_pres('KORZ',_in)=type_of(null()) & _in.KORZ
   || 'Zakończ rejestrację korekty zakupu: %1'@@[exec('record','#to_string',_in.KORZ)]
   |? var_pres('ZAK',_in)=type_of(null()) & _in.ZAK
   || 'Wystaw korektę do dokumentu zakupu: %1'@@[exec('record','#to_string',_in.ZAK)]
   |? var_pres('EDOKUM',_in)=type_of(null()) & _in.EDOKUM
   || 'Wystaw korektę zakupu wg dokumentu w obiegu: %1'@@ [exec('record','#to_string',_in.EDOKUM)]
   || 'Zarejestruj korektę zakupu'@@
   ?}
?}


\faks_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Faktura pozostała - Dołącz
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_faks:=FAKS.ref();
{? _r_lock:=exec('r_lock_one','#table',FAKS,_faks)
||
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='LZK_ZAK_DKOR';
   _params.UIDREF:=FAKS.uidref();
   _params.AKCJA:='Dołącz';
   _params.PROC_START:='T';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZAK',FAKS.ref());

   exec('mp_run','#b__box',_params);

   FAKS.f_rfresh();

   exec('r_unlock_one','#table',FAKS,_faks,_r_lock)
||
   exec('who_rlock_faks','faktury_nag')
?}


\typysp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła na wartość parametru TYPYSP
::   WY: TYPYSP.ref()
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:='Z';
exec('tsp_fak','typysp',,,,,,'KOR')


\permissions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła na uprawnienia dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_faks_where:=_ue:=_fis:=_spp:=_vatzpoz:='';
_typysp_t:='';

:: Sprawdzenie uprawnień do typu dokumentu
{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| _faks_where:=exec('FindAndGet','#table',FAKS,$_in.FAKS,,"FAKS.WHERE",'');
   _typysp:=exec('FindAndGet','#table',FAKS,$_in.FAKS,,"$FAKS.T",'');
   _ue:=exec('FindAndGet','#table',TYPYSP,_typysp,,"TYPYSP.UE",'');
   _fis:=exec('FindAndGet','#table',TYPYSP,_typysp,,"TYPYSP.FIS",'');
   _spp:=exec('FindAndGet','#table',TYPYSP,_typysp,,"TYPYSP.SPP",'');
   _vatzpoz:=exec('FindAndGet','#table',TYPYSP,_typysp,,"TYPYSP.VATZPOZ",'')
?};
{? var_pres('TYPYSP',_in)=type_of(null()) & _in.TYPYSP
|| _typysp_t:=exec('FindAndGet','#table',TYPYSP,$_in.TYPYSP,,"TYPYSP.T",'')
?};
{? _typysp_t<>''
   | _faks_where<>'' & _ue<>'' & _fis<>'' & _spp<>'' & _vatzpoz<>''
|| _where:=exec('tsp_where','typysp',_faks_where,_fis,,_spp,_typysp_t,'KOR',_ue,_vatzpoz);
   {? _typysp_t<>''
   || {? exec('FindInSet','#table','T2STS','UNIK',ST.STZ)<>null()
      || exec('FindInSet','#table','T2STS','UNIK',_in.TYPYSP,ST.STZ)<>null()
      || exec('tsp_upr','typysp',_where,,0,,1,,_user)<>null()
      ?}
   || exec('tsp_upr','typysp',_where,2,0,,,,_user)<>''
   ?}
|| 1
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('EDOKUM',_in)=type_of(null()) & _in.EDOKUM
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(_in.EDOKUM));
   EDOKUM.prefix();
   {? EDOKUM.seek(_in.EDOKUM)
   ||
::      __PARSES.setVal('OddzialLogProd',FAKZ.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=EDOKUM.DW~1;
      _args.AM:=EDOKUM.DW~2;
      __PARSES.setVal('OkresRok',_args)
   ?};
   EDOKUM.cntx_pop()
?};

1


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Lista zadań TO-DO, podgląd dokumentu
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_out:=_mp.load(exec('kind_out','#b_port'));
exec('display','faktury_nag',_out.KORZ)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 46cabb07066cb84482cfa930222308dc6ce23ba2c41d432036f64fae4f633b3c4fe6603886cc5c8f5ef7ff51c8435a48fb5cec6629fc45229f159b135c7e284ad77678fdf77974d140588a48e8a28c59a9e6a58ba4453d6c84ec92b06d8320374622f78e61a228be5ed29b53b390b08db7c7b61c3020ec06b99d5f50baf5f1ac

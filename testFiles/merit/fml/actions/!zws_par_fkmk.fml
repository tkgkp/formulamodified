:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_fkmk.fml
:: Utworzony: 10.02.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Multi kasa
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Czynność ZWS_PAR_FKTO - multi kasa
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejsciowe czynnosci
::       _int - [obj_new] - parametry wewnetrzne czynnosci
::       _out - [obj_new] - parametry wyjsciowe czynnosci
::       _mp  - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
exec('mkasav_def','!zws_par_fkmk')


\mkasav_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: definiowanie widokow multikas
::  OLD: \mkasav_def/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_czysc:="
   VAR_DEL.delete('__Mkv','__mkvwer','__mkv_gr','__ku_w')
";
_czysc();

:: start
__Mkv:=MKASA;

:: okno selekcji definiowania widokow multikas
__mkvwer:=MKASA.mk_sel('Multikasy'@,,,'mkv_fjdkfjdadfc',,,,,'U','U');
__Mkv.win_fld(__mkvwer,,'NAZ',,,35,,,'Nazwa'@);
_fa:="
   _ref:=__Mkv.ref();
   {? __Mkv.f_active() || __Mkv.f_rfresh(); __Mkv.f_seek(_ref) ?};
   exec('usr_upr_uzupełnij_pelne','users_upraw','MKAS');
   1";
__Mkv.win_act(__mkvwer,,'Dołącz',,,,,_fa,1);
__Mkv.win_act(__mkvwer,1,'Dołącz',,,,,_fa,1);
_fb:="
   {? FUN.ask('Usunąć definicję multikasy wraz z uprawnieniami do niej?'@)
   ||
      do;
      USRCONST.cntx_psh;
      USRCONST.prefix();
      MKASAUPR.index('MKASA'); MKASAUPR.prefix(MKASA.ref);
      {? MKASAUPR.first
      ||
         {!
         |?
            USRCONST.for_each(\"{? USRCONST.MKASA=MKASAUPR.ref || USRCONST.MKASA:=null; USRCONST.put ?}\");
            MKASAUPR.del
         !}
      ?};
      USRCONST.cntx_pop;
      USRCONST.get;
      KUSERUPR.index('MKASA'); KUSERUPR.prefix(MKASA.ref);
      {? KUSERUPR.first || {! |? KUSERUPR.del !} ?};
      MKASA.del;
      end
   ?}
";
__Mkv.win_act(__mkvwer,,'Formuła','&Usuń'@@,,,_fb,,,,,,'U');
__Mkv.win_act(__mkvwer,,'Popraw');

_fa:="
   _chk:=exec('mkasa_chk','kasa_wspolne');
   {? _chk<>''
   || _chk
   |? -menu_txt()='popraw'
   || do();
      MKASAUPR.cntx_psh();
      MKASAUPR.index('MKASA');
      MKASAUPR.prefix(MKASA.ref());
      _loop:=MKASAUPR.first();
      {!
      |? _loop
      |!
         MKASAUPR.MKASAN:=MKASA.NAZ;
         _loop:=MKASAUPR.put() & MKASAUPR.next()
      !};
      MKASAUPR.cntx_pop();
      end()
   || 1
   ?}
";
__Mkv.win_act(__mkvwer,,'Rekord',,,,,_fa);
__Mkv.win_act(__mkvwer,1,'Rekord',,,,,_fa);

:: okno pozycji (zakladek) multikasy
__ku_w:=KUSERUPR.mk_sel('Pozycje'@,,,'mkv_dappejlkcbg',,,,,'U','U');
KUSERUPR.win_fld(__ku_w,,'LP',,,5,,1,'Lp.'@);
KUSERUPR.win_fld(__ku_w,,'STANKAS','KOD','KOD',11,,,'Kod'@);
KUSERUPR.win_fld(__ku_w,,'STANKASN',,,30,,1,'Nazwa'@);
KUSERUPR.win_fld(__ku_w,,'MTAB',,,3,,1,'Zakładka'@,,,2,,"'T'","'N'");
KUSERUPR.win_fld(__ku_w,,'TAB',,,20,,,'Nazwa zakładki'@);
KUSERUPR.win_fld(__ku_w,,'DOKUMENT',,,3,,1,'Dokumenty'@,,,2,,"'T'","'N'");
KUSERUPR.win_fld(__ku_w,,'POZDOK',,,3,,1,'Pozycje'@,,,2,,"'T'","'N'");
_fa:="
   _ref:=KUSERUPR.ref();
   {? KUSERUPR.f_active() || KUSERUPR.f_rfresh(); KUSERUPR.f_seek(_ref) ?};
   1";
KUSERUPR.win_act(__ku_w,,'Dołącz',,,,,_fa,1);
KUSERUPR.win_act(__ku_w,1,'Dołącz',,,,,_fa,1);
KUSERUPR.win_act(__ku_w,,'Usuń');
KUSERUPR.win_act(__ku_w,,'Popraw');
_fb:="
   _wyn:=1;
   KUSERUPR.cntx_psh;
   _ref:=KUSERUPR.ref;
   _tab:={? KUSERUPR.TAB='' || KUSERUPR.STANKASN || KUSERUPR.TAB ?};
   {? KUSERUPR.first
   ||
      {!
      |?
         {? KUSERUPR.MTAB='T'
         ||
            _tab1:={? KUSERUPR.TAB='' || KUSERUPR.STANKASN || KUSERUPR.TAB ?};
            {? _tab1=_tab & (_ref=null | KUSERUPR.ref<>_ref)
            ||
               FUN.info('Zakładka %1 już istnieje.\nWłączenie zakładki jest niemożliwe.'@[_tab]);
               _wyn:=0
            ?}
         ?};
         KUSERUPR.next
      !}
   ?};
   KUSERUPR.cntx_pop;
   {? _wyn
   ||
      KUSERUPR.MTAB:={? KUSERUPR.MTAB='N' || 'T' || 'N' ?};
      KUSERUPR.put(1)
   ?}
";
KUSERUPR.win_act(__ku_w,,'Formuła','Zaznacz/odznacz &zakładka'@@,,,_fb,,,1,,,'Z');
_fa:="
   _wyn:=exec('chk_fldmk','!zws_par_fkmk');
   {? KUSERUPR.MTAB='T'
   ||
      KUSERUPR.cntx_psh;
      _ref:={? -(menu_pth+1)='p' || KUSERUPR.ref || null ?};
      _tab:={? KUSERUPR.TAB='' || KUSERUPR.STANKASN || KUSERUPR.TAB ?};
      {? KUSERUPR.first
      ||
         {!
         |?
            {? KUSERUPR.MTAB='T'
            ||
               _tab1:={? KUSERUPR.TAB='' || KUSERUPR.STANKASN || KUSERUPR.TAB ?};
               {? _tab1=_tab & (_ref=null | KUSERUPR.ref<>_ref)
               ||
                  FUN.info('Zakładka %1 już istnieje.'@[_tab]);
                  _wyn:='TAB'
               ?}
            ?};
            KUSERUPR.next
         !}
      ?};
      KUSERUPR.cntx_pop
   ?};
   {? _wyn='' || KUSERUPR.KU:=null; KUSERUPR.UPR:='T'; KUSERUPR.MKASA:=MKASA.ref ?};
   _wyn
";
KUSERUPR.win_act(__ku_w,,'Rekord',,,,,_fa);
KUSERUPR.win_act(__ku_w,1,'Rekord',,,,,_fa);
_fb:="
   KUSERUPR.DOKUMENT:={? KUSERUPR.DOKUMENT='N' || 'T' || 'N' ?};
   {? KUSERUPR.DOKUMENT='N' || KUSERUPR.POZDOK:='N' ?};
   KUSERUPR.put(1)
";
KUSERUPR.win_act(__ku_w,,'Formuła','Zaznacz/odznacz d&okumenty'@@,,,_fb,,,1,,,'O');
_fb:="
   {? KUSERUPR.DOKUMENT='N'
   ||
      FUN.info('Należy zaznaczyć dokument przed zaznaczeniem pozycji.'@)
   ||
      KUSERUPR.POZDOK:={? KUSERUPR.POZDOK='N' || 'T' || 'N' ?}
   ?};
   KUSERUPR.put(1)
";
KUSERUPR.win_act(__ku_w,,'Formuła','Zaznacz/odznacz poz&ycje'@@,,,_fb,,,1,,,'Y');
_dnd_fml:="
   KUSERUPR.cntx_psh();
   _ref:=dnd_info('dest_record');
   {? _ref
   ||
      {? KUSERUPR.seek(_ref) || exec('zmien_lp','#dragdrop','LP','MKASA',,OPERATOR.USER) ?}
   ?};
   KUSERUPR.cntx_pop()
";
KUSERUPR.dnd_sel(__ku_w,,'records.KUSERUPR',_dnd_fml);

KUSERUPR.win_act(__ku_w,,'Menu','Przesuń&'@@,,,,,,,,,'Ń');

_fb:="
   exec('zmien_lpa','#dragdrop','LP','MKASA',,,'U')
";
KUSERUPR.win_act(__ku_w,,'Formuła','W &górę'@@,'#Ń',,_fb,,,1,_fb,,'G');

_fb:="
   exec('zmien_lpa','#dragdrop','LP','MKASA',,,'D')
";
KUSERUPR.win_act(__ku_w,,'Formuła','W &dół'@@,'#Ń',,_fb,,,1,_fb,,'D');

_fb:="
   exec('zmien_lpa','#dragdrop','LP','MKASA',,,'N')
";
KUSERUPR.win_act(__ku_w,,'Formuła','Do &numeru'@@,'#Ń',,_fb,,,1,_fb,,'N');

:: grupowe okno selekcji definiowania widokow multikas
__mkv_gr:=__Mkv.grp_make('Definiowanie widoków multikas'@);
_far:="
   KUSERUPR.index('MKASA');
   _jest:={? MKASA.f_active() || MKASA.f_get() || MKASA.get() ?};
   {? _jest
   || KUSERUPR.prefix(MKASA.ref);
      KUSERUPR.actions_grayed(__ku_w,'')
   || KUSERUPR.prefix(0,-1);
      KUSERUPR.actions_grayed(__ku_w,':D')
   ?};
   grp_disp(KUSERUPR,__ku_w)
";
__Mkv.grp_sel(__mkv_gr,,__mkvwer,,_far,,,,,,,,'maximized_with_title');
__Mkv.grp_splt(__mkv_gr,,'vertical','mkv_ddactlllkkc','0,25%');
__Mkv.grp_sel(__mkv_gr,KUSERUPR,__ku_w,,,,,,,,,,'maximized_with_title');

:: select
KUSERUPR.cntx_psh;

__Mkv.win_sel(__mkv_gr);
__Mkv.select();

KUSERUPR.cntx_pop;

:: koniec
_czysc()


\chk_fld2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: sprawdzanie wypelnienia pol
::  OLD: \chk_fld2/i_o.fml
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(KUSERUPR,,'STANKAS','UPR')


\chk_fldmk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: sprawdzanie wypelnienia pol
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(KUSERUPR,,'STANKAS')

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:55 c518497f4004927a51433452542fe8a6127ff58acade25316d0661c3747c5a164948c10a31e989a3eb6cc4a6bfde356a001e9e348dd2ff1c3ba9b47666bc76951e823832b235df21d3403a3df18419366934337b34c02d07dd36fd265499df43679bf51018550dbabf089cee4aa850c63ec7de7b0de50e2d6a1e21b854971d34

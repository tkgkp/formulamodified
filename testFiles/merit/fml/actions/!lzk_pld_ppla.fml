:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_pld_ppla.fml
:: Utworzony: 12.05.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa planu dostaw - przegląd
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: plany dostaw
::  OLD: \pd/pd_.fml
::----------------------------------------------------------------------------------------------------------------------
_czysc:="
   VAR_DEL.delete('__pd','__openpdn')
";
PD_Z.ZAM_UPR:={? exec('chk_role','#b__box',OPERATOR.USER,'LSP_ZKN_DRZK') || 'Z' || '' ?}
             +{? exec('chk_role','#b__box',OPERATOR.USER,'LMG_ZAM_DRZK') || 'W' || '' ?};
_czysc();
__openpdn:=tab_tmp(1,'REF','STRING[16]','','NUM','INTEGER','');
PD_N.index('ODDZ_SYM');
PD_N.prefix();
{? PD_N.first()
|| _num:=0;
   {!
   |? {? PD_N.STATUS='N'
      || _num+=1;
         __openpdn.REF:=$PD_N.ref();
         __openpdn.NUM:=_num;
         __openpdn.add(1);
         PD_N.COLOR:=exec('complementaryColors','#color',__openpdn.NUM);
         PD_N.put(1)
      |? PD_N.COLOR<>''
      || PD_N.COLOR:='';
         PD_N.put(1)
      ?};
      PD_N.next()
   !}
?};

:: __pd - przestrzen nazw
__pd:=obj_new('PD_N','PD_P','PD_K','PD_K_ZAM','PD_K_ZL');

:: środowisko
_env:=obj_new('view','koszyk','pd_n','sel_exit','where');
_env.view:='nag';
_env.koszyk:=0;
_env.pd_n:=null();
_env.sel_exit:=0;
_env.where:='';
params_set('env',_env);

:: okno wertowania - planu dostaw - naglowki
__pd.PD_N:='WER';
_fb:="exec('pd_n_ico','!lzk_pld_ppla')";
PD_N.win_fml(__pd.PD_N,,'STATUS',,'ICON_BEFORE',_fb);
_fb:="exec('pd_n_sta','!lzk_pld_ppla')";
PD_N.fld_fml('STATUS','BEFORE_DISPLAY',_fb);

:: exec('pd_n_wer','pd_n');
:: okno wertowania - planu dostaw - pozycje
__pd.PD_P:='WER';
_fb:="exec('pd_p_ico','!lzk_pld_ppla')";
PD_P.win_fml(__pd.PD_P,,'M','KTM','ICON_BEFORE',_fb);
_fb:="exec('pd_p_ico_stat','!lzk_pld_ppla')";
PD_P.win_fml(__pd.PD_P,,'STAT_MG',,'ICON_BEFORE',_fb);
PD_P.fld_fml('ILP','DISPLAY_FORMAT',"'out_prec='+$ST.DOKL");
PD_P.fld_fml('ILR','DISPLAY_FORMAT',"'out_prec='+$ST.DOKL");
PD_P.fld_fml('STAN','DISPLAY_FORMAT',"'out_prec='+$ST.DOKL");
:: exec('pd_p_wer','pd_p');
:: okno wertowania - planu dostaw - zamowienia
__pd.PD_K_ZAM:='WER';
:: okno wertowania - planu dostaw - zlecenia
__pd.PD_K_ZL:='WER';
:: exec('zam_wer','pd_zam');
:: okno wertowania - planu dostaw - koszyk pozycji
__pd.PD_K:='WER';
_fb:="exec('pd_k_pd_n_ico','!lzk_pld_ppla')";
PD_K.win_fml(__pd.PD_K,,'PD_N','STATUS','ICON_BEFORE',_fb);
_fb:="exec('pd_k_pd_n_sta','!lzk_pld_ppla')";
PD_K.fld_fml('PD_N','BEFORE_DISPLAY',_fb);
:: exec('pd_k_wer','pd_k');

:: usuniecie zapisow po zamknieciu krzyzykiem systemu
:: DRO:[rr] brak
:: exec('delerrez','zk2');
:: wyswietlenie okna grupowego planu dostaw
PD_Z.WHERE:='N';
PD_N.index('ODDZ_SYM');
PD_N.prefix();
PD_N.first();
PD_Z.ZAKRES:='N';
_user:=exec('name','users');
OZ.index('OZ');
OZ.prefix(_user,_user);
{? ~OZ.first()
|| OZ.blank();
   OZ.USER:=_user;
   OZ.US:=OPERATOR.USER;
   OZ.add()
?};

AreaTitle.setTabWin(PD_N,~~);
AreaTitle.setTitle();

PD_K_ZAM.win_fml('WER',,'SYM',,'ICON_BEFORE',"exec('zdnag_sym_ib','!lzk_pld_ppla')");

{? exec('tte_lic','tte')='T'
|| PD_K.win_edit('RED_PROD')
|| PD_K.win_edit('RED')
?};
PD_K_ZL.fld_attr('RSQL',2);

_esc:=BEER.ESC;
{! |?
   BEER.ESC:=_esc;
   _env.sel_exit:=0;
   _env.koszyk:=0;
   exec('actions','!lzk_pld_ppla','PD_K');
:: okno grupowe dostaw
   _wer:={? OZ.PD_WIDOK='G' || 'GRP' || 'GRP_K' ?};
   PD_N.win_sel(_wer);
   PD_N.select(,1,2)
!};
PD_Z.WHERE:='';

_czysc()


\select_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Pozycje planu dostaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_pd_widok:=OZ.PD_WIDOK;
{? PD_N.PLANPROD='T' || OZ.PD_WIDOK:='P' ?};
PD_P.win_sel({? OZ.PD_WIDOK='G' || 'GRP' || 'GRP_K' ?});
exec('actions','!lzk_pld_ppla','PD_P');
PD_P.select();
OZ.PD_WIDOK:=_pd_widok;
PD_Z.ZAKRES:='N';
_env.view:='nag'


\actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: wl/wyl akcji okienek
::   WE: _a - akronim tabeli
::  OLD: \actions/pd_.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
params_set(params_get());
_env:=params_get().env;
_where:=_env.where;

{? _tab='PD_N'
||
   PD_N.get();
   _act:={? PD_N.STATUS='Z' || 'PUZF' || 'W' ?};
   {? PD_N.PLANPROD='T' || _act:='PUOZM'+_act ?};
   PD_N.actions_grayed(__pd.PD_N,_act)

|? _tab='PD_P'
||
   _buf:={? exec('get','#params',6600,2,OPERATOR.USER)='T' || '' || 'B' ?};
   PD_N.cntx_psh();
   _f_act:=PD_N.f_active();
   _ref:=PD_N.ref();
   _jest:={? _f_act || PD_N.f_first() || PD_N.first() ?};
   PD_N.cntx_pop();
   {? _f_act || {? _jest || PD_N.f_seek(_ref) ?} || PD_N.get() ?};
   PD_P.actions(__pd.PD_P,,'D:D',1);
   {? PD_N.STATUS<>'Z' & PD_P.STAT<>'Z' & _jest & PD_N.PLANPROD<>'T'
   || PD_P.actions_grayed(__pd.PD_P,_buf+'W')
   || {? PD_N.PLANPROD='T'
      || PD_P.actions_grayed(__pd.PD_P,_buf+'OYRDUMATFZWN:DYR');
         PD_P.actions(__pd.PD_P,,'G',1)
      |? ~_jest | PD_N.STATUS='Z'
      || PD_P.actions_grayed(__pd.PD_P,_buf+'YRDUGMATFZWN:DYR')
      || PD_P.actions_grayed(__pd.PD_P,_buf+'UGFZMATN:D')
      ?}
   ?};
   M.win_dict('SLO_M')

|? _tab='PD_K'
||
   PD_K.actions(__pd.PD_K,{? _env.koszyk || '' || 'A:A' ?},,1);
   PD_P.cntx_psh();
   _f_act:=PD_P.f_active();
   _ref:=PD_P.ref();
   _jest:={? _f_act || PD_P.f_first() || PD_P.first() ?};
   PD_P.cntx_pop();
   {? _f_act || {? _jest || PD_P.f_seek(_ref) ?} || PD_P.get() ?};
   {? PD_N.STATUS<>'Z' & PD_P.STAT<>'Z' & _jest & PD_N.PLANPROD<>'T'
   || _act:={? PD_Z.ZAM_UPR='' || 'Z' || '' ?}
           +{? PD_K.ILP<>0 || '' || 'PU' ?}
           +{? _where='PD_N' || 'D:D' || '' ?};
      PD_K.actions_grayed(__pd.PD_K,_act)
   || PD_K.actions_grayed(__pd.PD_K,'DPUMZN:DP')
   ?}

|? _tab='PD_TV'
||
   PD_TV.actions_grayed('WER','DPU:D')
?}


\pd_n_grp_far
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła po odswieżeniu dla PD_N
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_env.where:='PD_N';
{? OZ.PD_WIDOK='G' & _env.sel_exit=0 || grp_disp(PD_K,__pd.PD_K); grp_disp(PD_TV,'WER') ?}


\pd_k_zam_grp_fb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła przed dla PD_K_ZAM
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_env.view:='zam';
PD_Z.ZAKRES:='Z';
exec('zam_fill','!lzk_pld_ppla');
{? PD_K_ZAM.sel_size()=0 || PD_K_ZAM.f_rfresh() ?};
params_exec('actions','!lzk_pld_ppla','PD_K')


\pd_k_zam_grp_far
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła po odswieżeniu dla PD_K_ZAM
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_env.where:='PD_K_ZAM';
{? OZ.PD_WIDOK='G' || grp_disp(PD_K,__pd.PD_K) ?}


\pd_p_grp_fb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła przed dla PD_P
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_env.view:='poz';
PD_Z.ZAKRES:='P';
PD_P.index('POZ1');
PD_P.prefix(PD_N.ref());
{? PD_P.sel_size()=0 || PD_P.f_rfresh() ?};
params_exec('actions','!lzk_pld_ppla','PD_K')


\pd_p_grp_far
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła po odświeżeniu dla PD_P
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_env.where:='PD_P';
{? OZ.PD_WIDOK='G' || grp_disp(PD_K,__pd.PD_K) ?}


\pd_k_grp_fb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła przed dla PD_K
::   WE: _a - ref
::----------------------------------------------------------------------------------------------------------------------
{? PD_K.sel_size()>0 || return() ?};
ATR.MJS:='PD_K';
PD_K.f_clear();
PD_K.index('PD_K');
{? PD_Z.ZAKRES='P'
|| _pdn:=_pdp:=null();
   PD_P.cntx_psh();
   _f_act:=PD_P.f_active();
   _ref:=PD_P.ref();
   _jest:={? _f_act || PD_P.f_first() || PD_P.first() ?};
   PD_P.cntx_pop();
   {? _f_act & _jest || PD_P.f_seek(_ref) ?};
   {? _jest
   || _pdn:=PD_P.PD_N;
      _pdp:=PD_P.ref()
   ?};
   PD_K.index('PD_K');
   PD_K.prefix(_pdn,_pdp)
|? PD_Z.ZAKRES='N'
|| PD_N.cntx_psh();
   _f_act:=PD_N.f_active();
   _ref:={? var_pres('_a')=type_of(null()) || _a || PD_N.ref() ?};
   _jest:={? _f_act || PD_N.f_first() || PD_N.first() ?};
   PD_N.cntx_pop();
   {? _f_act & _jest || PD_N.f_seek(_ref) || PD_N.seek(_ref) ?};
   PD_K.index('PD_K');
   PD_K.prefix({? _jest || PD_N.ref() || null() ?})
|? PD_Z.ZAKRES='A'
|| PD_K.index('PD_K');
   PD_K.prefix()
|? PD_Z.ZAKRES='Z'
|| PD_K_ZAM.cntx_psh();
   _jest:={? PD_K_ZAM.f_active() || PD_K_ZAM.f_first() || PD_K_ZAM.first() ?};
   PD_K_ZAM.cntx_pop();
   PD_K.index('ZAM_REF');
   PD_K.prefix(PD_N.ref(),{? _jest || PD_K_ZAM.RSQL || 'brak' ?})
|? PD_Z.ZAKRES='L'
|| PD_K_ZL.cntx_psh();
   _jest:={? PD_K_ZL.f_active() || PD_K_ZL.f_first() || PD_K_ZL.first() ?};
   PD_K_ZL.cntx_pop();
   PD_K.clear();
   PD_K.f_set('M(N)','join PD_N using (PD_K.PD_N,PD_N.REFERENCE)
                      join ZLZAM using (ZLZAM.ZAMPOZ,PD_K.REFERENCE)
                      join ZL using (ZLZAM.ZL,ZL.REFERENCE)'
   ,'PD_N.REFERENCE=:_a and ZL.REFERENCE=\':_b\'',
   PD_N.ref(),PD_K_ZL.RSQL)
?};
params_exec('actions','!lzk_pld_ppla','PD_K')


\pd_k_grp_fa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: formuła po dla PD_K
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
~~


\pd_k_grp_far
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła po odświeżeniu dla PD_K
::----------------------------------------------------------------------------------------------------------------------
::params_set(params_get());
::_env:=params_get().env;
::{? _env.view='nag' || grp_disp(PD_N,__pd.PD_N)
::|? _env.view='poz' || grp_disp(PD_P,__pd.PD_P)
::|? _env.view='zam' || grp_disp(PD_K_ZAM,__pd.PD_K_ZAM)
::|? _env.view='zl' || grp_disp(PD_K_ZL,__pd.PD_K_ZL)
::?};
~~


\zam_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: wypełnia tabele PD_K_ZAM
::----------------------------------------------------------------------------------------------------------------------
_zam_ref:='';
PD_K_ZAM.clear();
{? PD_K_ZAM.first() || {! |? PD_K_ZAM.del() !} ?};

PD_K_ZAM.cntx_psh();
PD_K.cntx_psh();
PD_K.index('ZAM_REF');
PD_K.prefix(PD_N.ref());
_loop:=PD_K.first();
{!
|? _loop
|!
   {? PD_K.ZAM_REF<>_zam_ref
   || PD_K_ZAM.index('PD_N_ZAM');
      PD_K_ZAM.prefix(PD_N.ref(),PD_K.ZAM_REF,);
      {? ~PD_K_ZAM.first()
      || PD_K_ZAM.blank();
         PD_K_ZAM.RSQL:=PD_K.ZAM_REF;
         PD_K_ZAM.PD_N:=PD_N.ref();
         {? PD_K.TYPYZAM().R='D'
         || ZD_NAG.cntx_psh();
            ZD_NAG.use(8+PD_K.ZAM_REF);
            ZD_NAG.prefix();
            {? ZD_NAG.seek(PD_K.ZAM_REF)
            || PD_K_ZAM.DD:=ZD_NAG.DTPREAL;
               PD_K_ZAM.DZ:=ZD_NAG.DATA;
               PD_K_ZAM.SYM:=ZD_NAG.SYM;
               PD_K_ZAM.KH:=ZD_NAG.KH
            ?};
            ZD_NAG.cntx_pop()
         || ZK_N.cntx_psh();
            ZK_N.use(8+PD_K.ZAM_REF);
            ZK_N.prefix();
            {? ZK_N.seek(PD_K.ZAM_REF)
            || PD_K_ZAM.DD:=ZK_N.DT;
               PD_K_ZAM.DZ:=ZK_N.DP;
               PD_K_ZAM.SYM:=ZK_N.SYM;
               PD_K_ZAM.KH:=ZK_N.KH
            ?};
            ZK_N.cntx_pop()
         ?};
         PD_K_ZAM.add(1)
      ?};
      _zam_ref:=PD_K.ZAM_REF
   ?};
   _loop:=PD_K.next()
!};
PD_K_ZAM.cntx_pop();
PD_K.cntx_pop();

PD_K_ZAM.first()


\pd_n_bleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed legenda PD_N
::  OLD: \pd_n_bleg/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','PD_N#01')


\pd_n_brek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: rekord przed tabeli PD_N
::  OLD: \pd_n_brek/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a || params_exec('actions','!lzk_pld_ppla','PD_N') ?};
exec('rekprzed','color','PD_N#01')


\pd_p_bleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed legenda PD_P
::  OLD: \pd_p_bleg/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','PD_P#01')


\pd_p_brek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: rekord przed PD_P
::  OLD: \pd_p_brek/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a || params_exec('actions','!lzk_pld_ppla','PD_P') ?};
exec('rekprzed','color','PD_P#01')


\pd_k_bleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: legenda PD_K
::  OLD: \pd_k_bleg/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','PD_K#01','#PD_N#01')


\pd_k_brek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed rekord PD_K
::  OLD: \pd_k_break/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a || params_exec('actions','!lzk_pld_ppla','PD_K') ?};
ATR.MJS:='PD_K';
ATR.M_ATR:=PD_K.M().M_ATR;
ATR.UZUP:=exec('wz_uzup','mat_atr',ATR.M_ATR);
ATR.FLAG_ED:=(1+PD_K.MG().TYP)='D' & ATR.CZY_ATR & ATR.M_ATR().EDIT;
ATR.FLAG:={? ATR.FLAG_ED & PD_K.M().M_ATR<>null() || 2 || 0 ?};
{? ATR.FLAG_ED || {? PD_K.M().M_ATR().EDIT || ATR.FLAG_ED:=2 ?} ?};
{? PD_K.DK_C<>null() & PD_K.DK_C().M_ATR<>null()
|| {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():=($('PD_K.DK_C().WAR'+form(_i,-2,,'99')))() !}
|? PD_K.M().M_ATR=null() | PD_K.DK_C=null()
|| {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !}
?};
PD_Z.RODZ:=PD_K.TYPYZAM().R;
exec('pd_k_set_efld_opt','plan_dostaw');
exec('rekprzed','color','PD_K#01')


\pd_p_ico
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: ikony PD_P
::  OLD: \pd_p_ico/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_P.REFRESH='T' || 'xwin16.png:70'
|? PD_P.REFRESH='Z' || 'xwin16.png:157'
|| 'xwin16.png:110'
?}


\pd_p_ico_stat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: ikony PD_P
::  OLD: \pd_p_ico/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_P.STAT='P' & PD_P.STAT_MG>0 || 'xwin16.png:184'
|? PD_P.STAT='P' || 'xwin16.png:9'
|? PD_P.STAT='A' || 'xwin16.png:38'
|? PD_P.STAT='Z' || 'xwin16.png:6'
|| 'xwin16.png:110'
?}


\pd_ap_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: kolory tabeli analizy
::  OLD: \pd_ap_kol/pd_ap.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab(1,1);
{? _tab.DOSTEPNY=1 || 'PD_AP#01#01'
|? _tab.XD=0       || 'PD_AP#01#03'
|? _tab.XM=0 & _tab.XW>_tab.XD || 'PD_AP#01#05'
|? _tab.XM=0       || 'PD_AP#01#04'
                   || 'PD_AP#01#02'
?}


\pd_k_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: kolory PD_K
::  OLD: \pd_k_kol/pd_k.fml
::----------------------------------------------------------------------------------------------------------------------
TYPYZAM.cntx_psh();

_wyn:=
   {? PD_K.ILP=0 || 'PD_K#01#03'
   |? PD_K.TYPYZAM().R='D' || 'PD_K#01#01'
   |? TYPYZAM.R='W' || 'PD_K#01#02'
   || ''
   ?};

TYPYZAM.cntx_pop();

_wyn


\pd_n_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: kolory tabeli PD_N
::  OLD: \pd_n_kol/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_N.STATUS='N' || 'PD_N#01#01'
|? PD_N.STATUS='Z' || 'PD_N#01#02'
|| ''
?}


\pd_p_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: kolor PD_P
::  OLD: \pd_p_kol/pd_p.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_P.STAT='N' || 'PD_P#01#01'
|? PD_P.STAT='A' || 'PD_P#01#02'
|? PD_P.STAT='P' || 'PD_P#01#03'
|? PD_P.STAT='Z' || 'PD_P#01#04'
|| ''
?}


\tab1_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: kolory tabeli stanow wg dni
::  OLD: \tab1_kol/pd_a.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
{? exec('spr_kal','faktury_plat',_Tab.D,0)
|| 'PD_STAN#01#02'
|? _Tab.STAN<0
|| 'PD_STAN#01#01'
|| {? _Tab.TREE=0
   || _wyn:=0;
      _Tab.cntx_psh();
      _Tab.prefix(_Tab.T,_Tab.ref());
      _loop:=_Tab.first();
      {!
      |? _loop
      |!
         _wyn:=_Tab.STAN<0;
         _loop:=_wyn=0 & _Tab.next()
      !};
      _Tab.cntx_pop();
      {? _wyn || 'PD_STAN#01#03' || '' ?}
   || ''
   ?}
?}


\zam_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: kolory widoku zamowien
::  OLD: \zam_kol/pd_zam.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_K_ZAM.RSQL<>''
|| _rodz:={? (5+PD_K_ZAM.RSQL)='zdnag' || exec('FindAndGet','#table',ZD_NAG,PD_K_ZAM.RSQL,,"T().R",'')
          |? (5+PD_K_ZAM.RSQL)='zdnag' || exec('FindAndGet','#table',ZK_N,PD_K_ZAM.RSQL,,"T().R",'')
          || ''
          ?};
   {? _rodz='D' || 'PD_ZAM#01#01'
   |? _rodz='W' || 'PD_ZAM#01#02'
   || ''
   ?}
|| ''
?}


\kolor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: uruchamia formuly definicji kolorow
::  OLD: \kolor/pd_d.fml
::----------------------------------------------------------------------------------------------------------------------
_params:=obj_new('PAR','DEF');
_params.PAR:='K';
_params.DEF:=1;

PD_D.cntx_psh();
PD_D.index('KOD');
PD_D.prefix();
_loop:=PD_D.first();
{!
|? _loop
|!
   ($PD_D.FORMULA().FORMULA)(_params);
   _loop:=PD_D.next()
!};
{? PD_D.first() & PD_D.next() || _wyn:='' ?};
PD_D.cntx_pop()


\zam_bleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: legenda widoku zamowien
::  OLD: \zam_bleg/pd_zam.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','PD_ZAM#01')


\zam_brek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed rekord __Zam
::  OLD: \zam_brek/pd_zam.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_K_ZAM.RSQL<>''
|| {? (5+PD_K_ZAM.RSQL)='zdnag'
   || ZD_NAG.cntx_psh();
      ZD_NAG.use(8+PD_K_ZAM.RSQL);
      ZD_NAG.prefix();
      {? ~ZD_NAG.seek(PD_K_ZAM.RSQL) || PD_K_ZAM.del() ?};
      ZD_NAG.cntx_pop()
   |? (5+PD_K_ZAM.RSQL)='zknag'
   || ZK_N.cntx_psh();
      ZK_N.use(8+PD_K_ZAM.RSQL);
      ZK_N.prefix();
      {? ~ZK_N.seek(PD_K_ZAM.RSQL) || PD_K_ZAM.del() ?};
      ZK_N.cntx_pop()
   ?}
?};
exec('rekprzed','color','PD_ZAM#01')


\zam_bwys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed wyswietl zamowienia
::----------------------------------------------------------------------------------------------------------------------
exec('zam_wys','zamowienia',PD_K_ZAM)


\pd_n_ico
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: ikony PD_P
::  OLD: \pd_n_ico/pd_n.fml
::----------------------------------------------------------------------------------------------------------------------
{? PD_N.STATUS='N' || 'xwin16.png:156'
|? PD_N.STATUS='Z' || 'xwin16.png:157'
|| 'xwin16.png:110'
?}


\pd_n_sta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: kolorowanie PD_N.STATUS
::----------------------------------------------------------------------------------------------------------------------
''


\pd_k_pd_n_sta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: kolorowanie PD_K.PD_N().STATUS
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_wyn


\pd_k_pd_n_ico
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: ikony PD_N.STATUS
::----------------------------------------------------------------------------------------------------------------------
PD_N.cntx_psh();
_wyn:={? PD_K.PD_N().STATUS='N' || 'xwin16.png:156'
      |? PD_K.PD_N().STATUS='Z' || 'xwin16.png:157'
      || 'xwin16.png:110'
      ?};
PD_N.cntx_pop();
_wyn


\pd_k_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: ustawia zakres wyświetlania koszyka
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_ref:=null();
{? PD_Z.ZAKRES<>'A' || {? FUN.ask('Czy pokazać wszystkie pozycje koszyka?'@) || PD_Z.ZAKRES:='A' ?}
|? _env.view='nag' || {? FUN.ask('Czy pokazać pozycje koszyka powiązane z planem?'@) || PD_Z.ZAKRES:='N' ?}
|? _env.view='poz' || {? FUN.ask('Czy pokazać pozycje koszyka powiązane z pozycją planu?'@) || PD_Z.ZAKRES:='P' ?}
|? _env.view='zam' || {? FUN.ask('Czy pokazać pozycje koszyka powiązane z zamówieniem?'@) || PD_Z.ZAKRES:='Z' ?}
|? _env.view='zl' || {? FUN.ask('Czy pokazać pozycje koszyka powiązane ze zleceniem?'@) || PD_Z.ZAKRES:='L' ?}
?};
exec('pd_k_grp_fb','!lzk_pld_ppla',_env.pd_n);
~~


\pd_k_zakr_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: ustawia zakres wyświetlania koszyka
::----------------------------------------------------------------------------------------------------------------------
PD_Z.ZAKRES:='A';
'key:F2'


\pd_k_zakr_pd_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: ustawia zakres wyświetlania koszyka
::----------------------------------------------------------------------------------------------------------------------
PD_Z.ZAKRES:='N';
'key:F2'


\pd_k_zakr_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: ustawia zakres wyświetlania koszyka
::----------------------------------------------------------------------------------------------------------------------
PD_Z.ZAKRES:='P';
'key:F2'


\pd_k_zakr_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: ustawia zakres wyświetlania koszyka
::----------------------------------------------------------------------------------------------------------------------
PD_Z.ZAKRES:='Z';
'key:F2'


\pd_k_zakr_zl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [19.42]
:: OPIS: ustawia zakres wyświetlania koszyka
::----------------------------------------------------------------------------------------------------------------------
PD_Z.ZAKRES:='L';
'key:F2'


\zdnag_sym_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła określiająca ikony
::----------------------------------------------------------------------------------------------------------------------
_ref:=PD_K_ZAM.RSQL;
_stat_rej:='';
{? (5+_ref)='zdnag'
|| _stat_rej:=exec('FindAndGet','#table',ZD_NAG,_ref,,"STAT_REJ",'')
|? (5+_ref)='zknag'
|| _stat_rej:=exec('FindAndGet','#table',ZK_N,_ref,,"STAT_REJ",'')
?};
{? _stat_rej='T' || exec('zaakceptowany','icon')
|? _stat_rej='Z' || exec('zarejestrowany','icon')
|? _stat_rej='N' || exec('pusta','#icon')
|| exec('pusta','#icon')
?}


\pd_k_zl_grp_fb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [19.42]
:: OPIS: formuła przed dla PD_K_ZL
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_env.view:='zl';
PD_Z.ZAKRES:='L';
exec('zl_fill','!lzk_pld_ppla');
{? PD_K_ZL.sel_size()=0 || PD_K_ZL.f_rfresh() ?};
params_exec('actions','!lzk_pld_ppla','PD_K')


\pd_k_zl_grp_far
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [19.42]
:: OPIS: formuła po odswieżeniu dla PD_K_ZL
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_env.where:='PD_K_ZL';
{? OZ.PD_WIDOK='G' || grp_disp(PD_K,__pd.PD_K) ?}


\zl_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [19.42]
:: OPIS: wypełnia tabele PD_K_ZL
::----------------------------------------------------------------------------------------------------------------------
PD_K_ZL.clear();
{? PD_K_ZL.first() || {! |? PD_K_ZL.del() !} ?};

PD_K_ZL.cntx_psh();
PD_K_ZL.index('PD_N_ZL');
ZLZAM.cntx_psh();
ZLZAM.index('ZMZL');
PD_K.cntx_psh();
PD_K.index('PD_K');
PD_K.prefix(PD_N.ref());
_loop:=PD_K.first();
{!
|? _loop
|! ZLZAM.prefix($PD_K.ref);
   {? ZLZAM.first()
   || {!
      |? PD_K_ZL.prefix(PD_N.ref(),$ZLZAM.ZL);
         {? ~PD_K_ZL.first() & ZL.seek($ZLZAM.ZL)
         || PD_K_ZL.blank();
            PD_K_ZL.RSQL:=$ZL.ref;
            PD_K_ZL.PD_N:=PD_K.PD_N;
            PD_K_ZL.DZ:=ZL.OD;
            PD_K_ZL.DR:=ZL.DTR;
            PD_K_ZL.SYM:=ZL.SYM;
            PD_K_ZL.KTM:=exec('FindAndGet','#table',M,ZL.KTM,,"M.KTM",'');
            PD_K_ZL.NRK:=exec('FindAndGet','#table',TKTL,{? ZL.TKTL<>null || ZL.TKTL || ZL.KTL ?},,"TKTL.NRK",'');
            PD_K_ZL.IL:=ZL.IL;
            PD_K_ZL.JM:=exec('FindAndGet','#table',JM,ZL.KTM().J,,"JM.KOD",'');
            PD_K_ZL.add(1)
         ?};
         ZLZAM.next()
      !}
   ?};
   _loop:=PD_K.next()
!};
PD_K_ZL.cntx_pop();
ZLZAM.cntx_pop();
PD_K.cntx_pop();
PD_K_ZL.first()


\zl_bwys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [19.42]
:: OPIS: przed wyswietl zlecenia z planu dostaw
::----------------------------------------------------------------------------------------------------------------------
_ref:=PD_K_ZL.RSQL;
ZL.cntx_psh();
ZL.prefix();
{? ZL.seek(_ref)
|| exec('zl_display','zl_head')
?};
ZL.cntx_pop();
~~


\pd_widok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Plan dostaw - Widok
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
{? FUN.ask('Czy zmienić widok planu dostaw?'@)
||
   OZ.PD_WIDOK:={? OZ.PD_WIDOK='G' || 'P' || 'G' ?};
   OZ.put(1);
   BEER.ESC:=1;
   _env.sel_exit:=1;
   sel_exit()
?}


\pd_koszyk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Plan dostaw - Koszyk
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
PD_N.cntx_psh();
_env.koszyk:=1;
_zakres:=PD_Z.ZAKRES;
_env.pd_n:={? _zakres='N' || PD_N.ref() || null() ?};
exec('pd_k_grp_fb','!lzk_pld_ppla');
exec('actions','!lzk_pld_ppla','PD_K');
PD_K.win_sel('WER');
PD_K.select();
PD_Z.ZAKRES:=_zakres;
_env.koszyk:=0;
exec('actions','!lzk_pld_ppla','PD_K');
PD_N.cntx_pop()


\pd_tv_grp_fb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Wersje analizy - Przed obsługą okna
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PD_TV.index('PDNTDANR');
PD_TV.prefix(PD_N.ref());
params_exec('actions','!lzk_pld_ppla','PD_TV')


\pd_tv_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Wersje analizy - Usuń
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć wersję analizy?'@)
||
   exec('pda_ver_del','plan_dostaw',PD_TV.ref())
?}

:Sign Version 2.0 jowisz:1045 2024/02/09 09:16:59 64955c59c6072fe845aa5a98a1a2e2894994610b116c80320b05ceff54f0fbde4e9be5c9aa9e77bf74c4f1de36b4a8d351da869e0d0f118fddd1662781924ed6ec5130bef4657055d0387190bbfde759662159ff25f28a0dc1118b52b45ab6bd5bb40ffe6074f0a55b783d94956bafd9e26e19715f27bdf8bff19ae4d951fe2e

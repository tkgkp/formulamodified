:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pco_zdok.fml
:: Utworzony: 12.11.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PCO_ZDOK - Przeglądanie podziałów dla dokumentów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie podziałów dla dokumentów - główna formuła czynności CTR_PCO_ZDOK
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
_b.grp_sel(_c,DOK,'C_REN_XD',_a,"
   SKIDXD.index('DOKDSP');
   {? DOK.get() & exec('hide_podz_prac','control')=0
   || SKIDXD.prefix(DOK.ref());
      {? DOK.WSK_XD='T'
      || SKIDXD.actions('WERV','ZMduApR:ZMdr',,1)
      || SKIDXD.actions('WERV','ZM:ZM',,1)
      ?}
   || SKIDXD.prefix(null);
      SKIDXD.actions('WERV','DUPZM:DZM',,1)
   ?};
   exec('beobskxd','control');
   grp_disp(SKIDXD,'WERV')

",,,,"
__pardok:=1; __pfirma:=REF.FIRMA; __podd:=OPERATOR.DEPT; __par:=SSTALE.AR; __pao:=SSTALE.AO;
{? POMOC.CFIRMA<>null()
|| REF.FIRMA:=POMOC.CFIRMA; SSTALE.AR:=POMOC.CROK_F; SSTALE.AO:=POMOC.COKRO_F; OPERATOR.DEPT:=POMOC.CODD
?};
AreaTitle.setTitle()
","
{? POMOC.CFIRMA<>null()
|| REF.FIRMA:=__pfirma; OPERATOR.DEPT:=__podd; SSTALE.AR:=__par; SSTALE.AO:=__pao
?};
VAR_DEL.delete('__pardok','__pfirma','__pao','__par','__podd');
AreaTitle.setTitle()
",,,'maximized_with_title');
task_attach('CTR_PCO_ZDOK');
task_attach('CTR_PCO_DDOK');
_b.tab_splt(_c,,'horizontal','bottom',15);
::_b.grp_splt(_c,,'horizontal','bottom',39);
_b.grp_sel(_c,SKIDXD,'WERV',,"
SKIDXD.actions('WERV','ZM:ZM',,1)
",,,,"",,,,'maximized_with_title');
ROZNE.ZAKRES1:=0; ROZNE.ZAKRES:=2;
exec('zakres','control',0);
exec('zm_dok','!ctr_pco_zdok');
exec('ini_icon','!ctr_pco_zdok');
exec('set_var','control');
~~


\br_skidxd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2006]
:: OPIS: Rekord przed tabel z podzialami
::----------------------------------------------------------------------------------------------------------------------
_cur_tab:=cur_tab(1,1);
UD_POM.PBUD:=_cur_tab.PBUD().SYMBOL;
UD_POM.JORG:=_cur_tab.JORG().SYMBOL;
UD_POM.OKOSZ:=_cur_tab.OKOSZ().SYMBOL;
UD_POM.WYM4:=_cur_tab.WYM4().SYMBOL;
UD_POM.WYM5:=_cur_tab.WYM5().SYMBOL;
KONTO.K1:=SKIDXD.AN;
echo(_cur_tab.PBUD().OPIS+
{? _cur_tab.JORG || ' / '+_cur_tab.JORG().OPIS || '' ?}+
{? _cur_tab.OKOSZ || ' / '+_cur_tab.OKOSZ().OPIS || '' ?}+
{? _cur_tab.WYM4 || ' / '+_cur_tab.WYM4().OPIS || '' ?}+
{? _cur_tab.WYM5 || ' / '+_cur_tab.WYM5().OPIS || '' ?});
{? var_pres('TREE_VAT')>0
|| _act:='duApRZM:drZM';  _gray:='';
   SKIDXD.actions('WERV',_act,,1)
|| _gray:='';
   {? DOK.A='T' | var_press('PozView')>0 & PozView
   || _gray:='Z:Z'
   ?}
?};
SKIDXD.actions_grayed('WERV',_gray);
0


\spr_zgod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Sprawdzanie zgodnosci podzialow controllingowych z pozycjami
::       dokumentow zrodlowych dla biezacego dokumentu
::  OLD: \spr_zgod/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
exec('tab_dok','dok_fks',3,0);
{? ~exec('spr_dok','dok_fks',0)
|| KONT_ZG.select()
|| FUN.info('Nie wykryto niezgodności pomiędzy kwotami wprowadzonymi\n'
           'w pozycjach dokumentów źródłowych a kwotami wprowadzonymi\n'
           'w podziałach controllingowych.'@)
?};
exec('tab_dokd','dok_fks')


\sum_podz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Podsumowanie podzialow controllingowych
::   WE: _a=1 - tabela DOK (dokumenty proste)
::          2 - tabela DOK (dokumenty VAT)
::          3 - dokumenty w obiegu
::          4 - zapotrzebowania
::  OLD: \sum_podz/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
_tab_an:={? _a=1 | _a=2 || SKIDXD || EPODZ ?};
SUM_PODZ:=tab_tmp(1,'MODEL','STRING[8]','Model',
                    'MODELNAZ','STRING[60]','',
                    'WART','REAL','Razem',
                    'POZOST','REAL','Pozostało'
                  );
_ind:=SUM_PODZ.ndx_tmp(,1,'MODEL',,0,'MODEL',,0);
SUM_PODZ.index(_ind); SUM_PODZ.prefix();
_tab_an.cntx_psh();
{? _tab_an.first()
|| {! |?
      SKIDXDUD.cntx_psh();
      SKIDXDUD.index('POZ'); SKIDXDUD.prefix(_tab_an.PBUD);
      _wart:=(SKIDXDUD.first() & SKIDXDUD.WART_IL='W');
      SKIDXDUD.cntx_pop();
      {? _tab_an=EPODZ & EPODZ.WAL<>EDOKUM.WAL || _wart:=0 ?};
      {? _wart
      || {? SUM_PODZ.find_key(_tab_an.SKID_MB().KOD,SKID_MBN.KOD)
         || SUM_PODZ.WART+=_tab_an.WART; SUM_PODZ.put()
         || SUM_PODZ.MODEL:=SKID_MBN.KOD; SUM_PODZ.MODELNAZ:=SKID_MBN.NAZ;
            SUM_PODZ.WART:=_tab_an.WART; SUM_PODZ.add()
         ?}
      ?};
      _tab_an.next()
   !}
?};
_tab_an.cntx_pop();
_ile:=SUM_PODZ.size();
{? _ile>30 || _ile:=30 ?};
_wer:=SUM_PODZ.mk_sel('Podsumowanie'@,,,'sum_podz_wer',,,_ile);
SUM_PODZ.win_fld(_wer,,'MODEL',,,',8,13');
SUM_PODZ.win_fld(_wer,,'MODELNAZ');
SUM_PODZ.win_fld(_wer,,'WART',,,',10,20',6,,'Razem %1'@[{? _a=4 || '(%)' || '' ?}]);
{? _a=2 | _a=3 || SUM_PODZ.win_fld(_wer,,'POZOST',,,',10,20',6) ?};
SUM_PODZ.win_sel(_wer);
{? _a=2 | _a=3
|| SUM_PODZ.hdr_sel(', kwota do podziału %1'@[form(PAR_WYDR.RPAR3,,2,' ,')]);
   {? SUM_PODZ.first()
   || {! |?
         SUM_PODZ.POZOST:=PAR_WYDR.RPAR3-SUM_PODZ.WART; SUM_PODZ.put();
         SUM_PODZ.next()
      !}
   ?}
?};
SUM_PODZ.select();
VAR_DEL.delete('SUM_PODZ')


\zm_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Ustawianie zmiennych dla przegladania dokumentow
::  OLD: \zm_dok/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
BILANSP.ASYNT:=SSTALE.AR().SYNT; BILANSP.ASEP:=ROK_F.SEP;
ROZNE.ROKKON:=ROK_F.ref();
ROZNE.OKRKON:=SSTALE.AO


\zakr_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Ustawianie zakresu dokumentow (controlling)
::  OLD: \zakr_dok/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.win_edit('ZAKR_XD');
_zakres:=ROZNE.ZAKRES1;
{? ROZNE.edit()
|| exec('zakres','control',ROZNE.ZAKRES1)
|| ROZNE.ZAKRES1:=_zakres
?}


\zgod_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Sprawdzanie zgodnosci podzialow controllingowych z pozycjami
::       dokumentow zrodlowych dla wszystkich dokumentow z wybranego okresu
::  OLD: \zgod_all/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','CTR_PCO_ZDOK','wsp_zg',,,,,,,,'W')


\dokrprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Rekord przed okna z dokumentami księgowymi
::  OLD: \dokrprz/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
_gray_podz:='';
{? DOK.A='T'
|| _gray+='A'
|| _gray+='WPF'
?};
{? DOK.ZP='T'
|| _gray+='WP'
|| _gray+='F'
?};
{? DOK.WSK_XD='T'
|| _gray+='C';
   _gray_podz+='ADPU'
|| _gray+='Y'
?};
DOK.actions_grayed('C_REN_XD',_gray);
SKIDXD.actions_grayed('WERV',_gray_podz);
{? DOK.WSK_XD<>'T' || 'DOK#03#00' |? DOK.WSK_XD='T' & DOK.A='N' || 'DOK#03#01' ?}


\ini_icon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawienie formuł dla ikon okien dokumentu księgowego
::----------------------------------------------------------------------------------------------------------------------
_fml:="{? DOK.E_DOC || 'xwin16.png:51' || '' ?}";
DOK.win_fml('C_REN_XD',,'NK',,'ICON_BEFORE',_fml);
~~

:Sign Version 2.0 jowisz:1045 2023/10/17 16:17:24 4f1d30cb917b510fe590eef0a42ce7004864ece51dd791ba1ef04fbb6650681ea309713b5b8627d875f1f9631f91ead081179542f102e18966fc7d2a5dcdb2b08d456b870f850ca3ca63c87c23a9be8622d5ef4a0543345b8e67c59b6ac4025d904188a2c57bc7924ba3ff63281c524d18011e5ee7bcaf9227089206663e4a4c

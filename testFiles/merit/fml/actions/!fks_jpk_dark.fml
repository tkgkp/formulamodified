:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_jpk_dark.fml
:: Utworzony: 26.08.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_JPK_DARK - Tworzenie plików JPK
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Tworzenie plików JPK - główna formuła czynności FKS_JPK_DARK.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,HRB
:: PARAMETRY WE:
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres księgowy, required=N, keyref=N
::# kind=WE, symbol=RODZAJ, type=STRING, name=Rodzaj: FA; KR; MAG; VAT; WB; GV; V7M; V7K, required=N
::# kind=WE, symbol=TM_STAMP, type=STRING, name=Znacznik czasowy, required=N
::# kind=WE, symbol=E_SPR, type=_E_SPR, name=Wskazanie na definicję e-Sprawozdania, required=N
::# kind=WE, symbol=VAT_DEK, type=_VAT_DEK, name=Wskazanie na deklarację VAT, required=N, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=JPK,   type=_JPK, name=Wskazanie na plik JPK, required=T, keyref=T
::# kind=WY, symbol=WYNIK,   type=STRING, name=Powstały komunikaty typu 'W' ostrzeżenia lub 'B' błędy lub 'N' brak, required=T
::# kind=WY, symbol=TM_STAMP, type=STRING, name=Znacznik czasowy, required=N
:: WLAŚCIWOŚCI
::# properties=LOOP
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
{? _we.TM_STAMP<>~~
|| exec('first','!fks_jpk_dark',_wy,_we.TM_STAMP);
   _wy.TM_STAMP:=_we.TM_STAMP;
   _mp.save(,_wy);
   {? exec('next','!fks_jpk_dark',_we.TM_STAMP)
   || _mp.loop_continue()
   ?};
   _mp.done()
|| JpkStamp:=tm_stamp();
   OkroFInd:=1;
   _e_spr:=null;
   _vat_dek:=null;
   _vdtyp:='';
   _dalej:=1;
   {? var_press('E_SPR',_we)>0
   || E_SPR.cntx_psh();
      E_SPR.prefix();
      {? E_SPR.seek(_we.E_SPR)
      || _e_spr:=E_SPR.ref()
      ?};
      E_SPR.cntx_pop()
   ?};
   {? var_press('VAT_DEK',_we)>0
   || VAT_DEK.cntx_psh();
      VAT_DEK.prefix();
      {? VAT_DEK.seek(_we.VAT_DEK)
      || {? 2+VAT_DEK.TYP<>'V7' & 2+VAT_DEK.TYP<>'GV'
         || FUN.info('Przekazano deklarację niewłaściwego typu.'@);
            _wy.JPK:=null;
            _mp.save(,_wy);
            _mp.done();
            _dalej:=0
         |? VAT_DEK.STATUS<>'A'
         || FUN.info('Deklaracja nie jest zaakceptowana.'@);
            _dalej:=0
         || _vat_dek:=VAT_DEK.ref();
            _vdtyp:=VAT_DEK.TYP
         ?}
      ?};
      VAT_DEK.cntx_pop()
   || _refs:=_mp.getRefs();
      {! _ii:=1..obj_len(_refs)
      |? _dalej
      |! _sref:=_refs[_ii];
         {? var_pres('_sref')>0 & 7+(_sref+16)='vat_dek'
         || VAT_DEK.cntx_psh();
            VAT_DEK.prefix();
            {? ~VAT_DEK.seek(_sref)
            || _dalej:=0;
               FUN.info('Nie znaleziono danych do JPK.');
               _wy.TM_STAMP:=JpkStamp;
               _wy.JPK:=null;
               _mp.save(,_wy);
               _mp.done()
            ?};
            VAT_DEK.cntx_pop()
         ?}
      !}
   ?};
   {? _dalej
   || {? 2+_vdtyp='GV' & _vat_dek
      || _dalej:=exec('add_gv','!fks_jpk_dark',_vat_dek)
      |? _vat_dek
      || _dalej:=exec('add_v7','!fks_jpk_dark',_vat_dek)
      || _dalej:=exec('create_jpk','!fks_jpk_dark',_we.RODZAJ,_e_spr)
      ?}
   ?};
   {? _dalej & exec('first','!fks_jpk_dark',_wy,JpkStamp)
   || _wy.TM_STAMP:=JpkStamp;
      _mp.save(,_wy);
      {? exec('next','!fks_jpk_dark',JpkStamp)
      || _mp.loop_continue()
      ?};
      _mp.done()
   ?};
   VAR_DEL.delete('JpkStamp','OkroFInd')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis na liście TODO czynności
::----------------------------------------------------------------------------------------------------------------------
_desc:='Utwórz pliki JPK'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_press('VAT_DEK',_we)>0
|| VAT_DEK.cntx_psh();
   VAT_DEK.prefix();
   {? VAT_DEK.seek(_we.VAT_DEK)
   || _desc:='Utwórz plik JPK_ %1 za okres %2'@@[(3+VAT_DEK.TYP),VAT_DEK.OKRES]
   ?};
   VAT_DEK.cntx_pop()
|? var_press('E_SPR',_we)>0
|| E_SPR.cntx_psh();
   E_SPR.prefix();
   {? E_SPR.seek(_we.E_SPR)
   || _desc:='Utwórz e-Sprawozdanie: %1'@@[E_SPR.NAZ]
   ?};
   E_SPR.cntx_pop()
|? var_press('RODZAJ',_we)>0
|| _desc:='Utwórz pliki JPK_%1'@@[_we.RODZAJ]
?};
_desc


\first
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia plik JPK na wyjsciu czynności
::   WE: _a - wyjscie czynności
::       _b - znacznik czasowy
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
JPK.cntx_psh();
JPK.index('TM_STAMP');
JPK.prefix(_b,);
{? JPK.first()
|| _a.JPK:=JPK.ref();
   _a.WYNIK:=JPK.KOM;
   JPK.prefix();
   JPK.TM_STAMP:=_b+'1';
   JPK.put();
   _jest:=1
?};
JPK.cntx_pop();
_jest


\next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy istnieje następny plik JPK do przetworzenia
::   WE: _a - znacznik czasowy
::----------------------------------------------------------------------------------------------------------------------
JPK.cntx_psh();
JPK.index('TM_STAMP');
JPK.prefix(_a,);
_jest:=JPK.first();
JPK.cntx_pop();
_jest


\create_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja dolacz okna WER tabeli JPK
::   WE: _a - rodzaj JPK
::       _b - definicja e-Sprawozdania
::  OLD: \add_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.win_dict('SLO');
ISTDEF.win_sel('W_JPK');
HELPJPK.win_edit('RED2');
HELPJPK.blank(1);
HELPJPK.RODZAJ:={? var_press('_a')>0 || _a || '' ?};
{? var_press('_b')>0
|| HELPJPK.E_SPR:=_b;
   HELPJPK.RODZAJ:='SF';
   HELPJPK.ISTDEF:=HELPJPK.E_SPR().ISTDEF
?};
BPMN.SYM_DOM:='FKS';
SKID.ISTDEF:='J';
HELPJPK.CYKL:='N';
HELPJPK.EMAIL:=OPERATOR.USER().EMAIL;
{? var_press('DATE',HELPJPK)>0
|| HELPJPK.DATE:=date();
   HELPJPK.TIME:=time()
?};
_spr:=0;
{? HELPJPK.edit("exec('ar_jpk','jpk')") & ('VAT,V7M,V7K,GV'*HELPJPK.RODZAJ=0 | (_spr:=exec('sel_vat','!fks_jpk_dark')))
|| exec('uzu_daty','jpk');
   {? _spr>1 | exec('spr_ks','!fks_jpk_dark')
   || jpkOK:=0;
      jpkErr:=0;
      jpkWrn:=0;
      F.set_odd(null);
      {? HELPJPK.RODZAJ='KR'
      || _fml:="exec('jpk_kr','!fks_jpk_dark')";
         _ar:=SSTALE.AR;
         _ao:=SSTALE.AO;
         exec('loop_okr','!fks_jpk_dark',_fml);
         echo();
         SSTALE.AR:=_ar;
         SSTALE.AO:=_ao;
         exec('open_tabele','open_tab')
      |? HELPJPK.RODZAJ='WB'
      || _fml:="exec('jpk_wb','!fks_jpk_dark')";
         VAR_DEL.delete('TabRej','TabDok','JPK_RB','DataOd','DataDo','BrakOpisu','WbPw');
         TabRej:=sql(
            'select REJ.ROK, DOK_REJ.JPK_RB, DOK_REJ.REFERENCE as REF from DOK_REJ join REJ '+
            'where DOK_REJ.JPK_TYP=\'W\''+
            {? HELPJPK.RB<>'' || ' and DOK_REJ.JPK_RB=\''+HELPJPK.RB+'\'' || '' ?}+
            ' order by 1,2'
         );
         _ar:=SSTALE.AR;
         _ao:=SSTALE.AO;
         exec('loop_daty','!fks_jpk_dark',_fml);
         echo();
         {? var_press('PwInd')>0
         || PW.ndx_drop(PwInd);
            VAR_DEL.delete('PwInd')
         ?};
         VAR_DEL.delete('TabRej','TabDok','JPK_RB','DataOd','DataDo','BrakOpisu','WbPw');
         SSTALE.AR:=_ar;
         SSTALE.AO:=_ao;
         exec('open_tabele','open_tab')
      |? 'VAT,V7M,V7K'*HELPJPK.RODZAJ
      || _fml:="exec('jpk_vat','!fks_jpk_dark')";
         _ar:=SSTALE.AR;
         _ao:=SSTALE.AO;
         exec('loop_vat','!fks_jpk_dark',_fml);
         echo();
         SSTALE.AR:=_ar;
         SSTALE.AO:=_ao;
         exec('open_tabele','open_tab')
      |? HELPJPK.RODZAJ='GV'
      || _fml:="exec('jpk_gv','!fks_jpk_dark')";
         _ar:=SSTALE.AR;
         _ao:=SSTALE.AO;
         exec('loop_vat','!fks_jpk_dark',_fml);
         echo();
         SSTALE.AR:=_ar;
         SSTALE.AO:=_ao;
         exec('open_tabele','open_tab')
      |? HELPJPK.RODZAJ='FA'
      || _fml:="exec('jpk_fa','!fks_jpk_dark')";
         VAR_DEL.delete('TabRej','TabDok','TabDok1','TabDok2','DataOd','DataDo','BrakOpisu');
         TabRej:=sql(
            'select REJ.ROK, DOK_REJ.REFERENCE as REF from DOK_REJ join REJ '+
            'where DOK_REJ.JPK_TYP=\'F\' '+
            {? HELPJPK.FAK_TYP<>'W'
            || 'and DOK_REJ.JPK_FA_T=\''+HELPJPK.FAK_TYP+'\' '
            || ''
            ?}+'order by 1'
         );
         _ar:=SSTALE.AR;
         _ao:=SSTALE.AO;
         exec('loop_daty','!fks_jpk_dark',_fml);
         echo();
         VAR_DEL.delete('TabRej','TabDok','TabDok1','TabDok2','DataOd','DataDo','BrakOpisu');
         SSTALE.AR:=_ar;
         SSTALE.AO:=_ao;
         exec('open_tabele','open_tab')
      |? HELPJPK.RODZAJ='MAG'
      || _fml:="exec('jpk_mag','!fks_jpk_dark')";
         VAR_DEL.delete('TabRej','TabDok','JPK_MAG','DataOd','DataDo');
         _ar:=SSTALE.AR;
         _ao:=SSTALE.AO;
         exec('loop_daty','!fks_jpk_dark',_fml);
         echo();
         VAR_DEL.delete('TabRej','TabDok','JPK_MAG','DataOd','DataDo','BrakOpisu');
         SSTALE.AR:=_ar;
         SSTALE.AO:=_ao;
         exec('open_tabele','open_tab')
      |? HELPJPK.RODZAJ='SF'
      || XINFO.DKON:=KST.DKON;
         _fml:="exec('generuj','jpk_sf')";
         _ar:=SSTALE.AR;
         _ao:=SSTALE.AO;
         exec('loop_lata','jpk',_fml);
         echo();
         VAR_DEL.delete('DataOd','DataDo','BrakOpisu');
         SSTALE.AR:=_ar;
         SSTALE.AO:=_ao;
         exec('open_tabele','open_tab')
      ?};
      FUN.info(
         'Liczba utworzonych plików JPK: '+$jpkOK+'\n'+
         'Liczba plików JPK z błędami: '+$jpkErr+'\n'+
         'Liczba plików JPK z ostrzeżeniami: '+$jpkWrn+'.'
      );
      VAR_DEL.delete('jpkOK','jpkErr','jpkWrn')
   ?};
   1
?}


\add_jpk_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Akcja dolacz okna WER tabeli JPK
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_JPK_DARK';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\spr_ks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Sprawdzenie zaksiegowania/dekretowania dokumentow przed generowaniem JPK
::  OLD: \spr_ks/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('NotKs','Full','TabKom');
{? 'KR|SF'*HELPJPK.RODZAJ
|| NotKs:=0; Full:=0; TabKom:=~~;
   _fml:="
      DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
      DOK.index('PR');
      DOK.prefix('N');
      NotKs:=DOK.first();
      {? NotKs & Full
      || {!
         |? TabKom.REF:=$DOK.ref();
            TabKom.O:='Niezaakceptowany dokument: '+DOK.NK;
            TabKom.add();
            DOK.next()
         !}
      ?};
      {? NotKs=0 | Full
      || DOK.prefix('T','N');
         NotKs:=DOK.first();
         {? NotKs & Full
         || {!
            |? TabKom.REF:=$DOK.ref();
               TabKom.O:='Niezaksięgowany dokument: '+DOK.NK;
               TabKom.add();
               DOK.next()
            !}
         ?}
      ?};
      Full | NotKs=0
   ";
   DOK.cntx_psh();
   exec('loop_okr','!fks_jpk_dark',_fml);
   DOK.cntx_pop();
   {? NotKs
   || {!
      |? _ok:=FUN.choice('Istnieją niezaksięgowane dokumenty.\nCzy kontynuować?'@,,'Tak'@,'Pokaż szczegóły'@);
         {? _ok=2
         || {? var_press('TabKom')<=0
            || NotKs:=0; Full:=1;
               TabKom:=exec('tabKom','jpk');
               DOK.cntx_psh();
               exec('loop_daty','!fks_jpk_dark',_fml);
               DOK.cntx_pop()
            ?};
            TabKom.select();
            1
         ?}
      !}
   || _ok:=1
   ?};
   VAR_DEL.delete('NotKs','Full');
   _ok
|? HELPJPK.RODZAJ='WB'
|| NotKs:=0; Full:=0; TabKom:=~~;
   _fml:="
      DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
      DOK.index('PR');
      DOK.prefix('N');
      {? DOK.first()
      || {!
         |? NotKs:=DOK.DTW>=DataOd & DOK.DTW<=DataDo & DOK.DOK_REJ().JPK_TYP='W';
            {? NotKs & Full
            || TabKom.REF:=$DOK.ref();
               TabKom.O:='Niezaakceptowany wyciąg bankowy: '+DOK.NK;
               TabKom.add()
            ?};
            (Full | NotKs=0) & DOK.next()
         !}
      ?};
      {? NotKs=0
      || DOK.prefix('T','N');
         {? DOK.first()
         || {!
            |? NotKs:=DOK.DTW>=DataOd & DOK.DTW<=DataDo & DOK.DOK_REJ().JPK_TYP='W';
               {? NotKs & Full
               || TabKom.REF:=$DOK.ref();
                  TabKom.O:='Niezaksięgowany wyciąg bankowy: '+DOK.NK;
                  TabKom.add()
               ?};
               (Full | NotKs=0) & DOK.next()
            !}
         ?}
      ?};
      NotKs=0
   ";
   DOK.cntx_psh();
   exec('loop_daty','!fks_jpk_dark',_fml);
   DOK.cntx_pop();
   {? NotKs
   || {!
      |? _ok:=FUN.choice('Istnieją niezaksięgowane wyciągi bankowe.\nCzy kontynuować?'@,,'Tak'@,'Pokaż szczegóły'@);
         {? _ok=2
         || {? var_press('TabKom')<=0
            || NotKs:=0; Full:=1;
               TabKom:=exec('tabKom','jpk');
               DOK.cntx_psh();
               exec('loop_daty','!fks_jpk_dark',_fml);
               DOK.cntx_pop()
            ?};
            TabKom.select();
            1
         ?}
      !};
      VAR_DEL.delete('TabKom')
   || _ok:=1
   ?};
   VAR_DEL.delete('NotKs','Full');
   _ok
|? 'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ
|| _fml:="exec('is_edek_vat7','jpk',1)";
   NoEdek:=0;
   exec('loop_vat','!fks_jpk_dark',_fml);
   {? NoEdek & HELPJPK.RODZAJ='VAT'
   || _ok:=FUN.ask('Deklaracja VAT-7 nie posiada potwierdzenia UPO.\nCzy kontynuować?'@)
   || _ok:=1
   ?};
   VAR_DEL.delete('NoEdek');
   _ok
|? HELPJPK.RODZAJ='FA'
|| NotKs:=0;
   NotDekr:=0;
   Full:=0;
   TabKom:=~~;
   _fml:="
      _typ:={? HELPJPK.FAK_TYP='W' || '' || HELPJPK.FAK_TYP ?};
      {? NotDekr=0
      || NotDekr:=exec('jpk_fa_chk','jpk_log',DataOd,DataDo,_typ,Full,TabKom).first()
      ?};
      {? NotKs=0
      || DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
         DOK.index('PR');
         DOK.prefix('N');
         {? DOK.first()
         || {!
            |? NotKs:=DOK.DTW>=DataOd & DOK.DTW<=DataDo & DOK.DOK_REJ().JPK_TYP='F' &
               (HELPJPK.FAK_TYP='W' | HELPJPK.FAK_TYP=DOK_REJ.JPK_FA_T) & DOK.WAL=HELPJPK.WAL;
               {? NotKs & var_press('TabKom')>0
               || TabKom.REF:=$DOK.ref();
                  TabKom.O:='Niezaakceptowany dokument: '+DOK.NK;
                  TabKom.add()
               ?};
               (Full | NotKs=0) & DOK.next()
            !}
         ?};
         {? NotKs=0
         || DOK.prefix('T','N');
            {? DOK.first()
            || {!
               |? NotKs:=DOK.DTW>=DataOd & DOK.DTW<=DataDo & DOK.DOK_REJ().JPK_TYP='F' &
                  (HELPJPK.FAK_TYP='W' | HELPJPK.FAK_TYP=DOK_REJ.JPK_FA_T) & DOK.WAL=HELPJPK.WAL;
                  {? NotKs & var_press('TabKom')>0
                  || TabKom.REF:=$DOK.ref();
                     TabKom.O:='Niezaksięgowany dokument: '+DOK.NK;
                     TabKom.add()
                  ?};
                  (Full | NotKs=0) & DOK.next()
               !}
            ?}
         ?}
      ?};
      NotDekr=0 | NotKs=0
   ";
   DOK.cntx_psh();
   exec('loop_daty','!fks_jpk_dark',_fml);
   DOK.cntx_pop();
   {? NotKs | NotDekr
   || {!
      |? _ok:=FUN.choice(
            'Istnieją '+
            {? NotDekr || 'niezaakceptowane lub niezadekretowane faktury' || '' ?}+
            {? NotKs
            || {? NotDekr || ' i ' || '' ?}+'niezaksięgowane dokumenty'
            || ''
            ?}+
            '.\nCzy kontynuować?',,
            'Tak','Pokaż szczegóły'
         );
         {? _ok=2
         || {? var_press('TabKom')<=0
            || NotKs:=NotDekr:=0; Full:=1;
               TabKom:=exec('tabKom','jpk');
               DOK.cntx_psh();
               exec('loop_daty','!fks_jpk_dark',_fml);
               DOK.cntx_pop()
            ?};
            TabKom.select();
            1
         ?}
      !};
      VAR_DEL.delete('TabKom')
   || _ok:=1
   ?};
   VAR_DEL.delete('NotKs','NotDekr','Full');
   _ok
|? HELPJPK.RODZAJ='MAG'
|| NotKs:=0;
   NotDekr:=0;
   Full:=0;
   TabKom:=~~;
   _fml:="
      {? NotDekr=0
      || NotDekr:=exec('jpk_mag_chk','jpk_log','',DataOd,DataDo,Full,TabKom).first()
      ?};
      {? NotKs=0
      || DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
         DOK.index('PR');
         DOK.prefix('N');
         {? DOK.first()
         || {!
            |? NotKs:=DOK.DTW>=DataOd & DOK.DTW<=DataDo & DOK.DOK_REJ().JPK_TYP='M';
               {? NotKs & var_press('TabKom')>0
               || TabKom.REF:=$DOK.ref();
                  TabKom.O:='Niezaakceptowany dokument: '+DOK.NK;
                  TabKom.add()
               ?};
               (Full | NotKs=0) & DOK.next()
            !}
         ?};
         {? NotKs=0
         || DOK.prefix('T','N');
            {? DOK.first()
            || {!
               |? NotKs:=DOK.DTW>=DataOd & DOK.DTW<=DataDo & DOK.DOK_REJ().JPK_TYP='M';
                  {? NotKs & var_press('TabKom')>0
                  || TabKom.REF:=$DOK.ref();
                     TabKom.O:='Niezaksięgowany dokument: '+DOK.NK;
                     TabKom.add()
                  ?};
                  (Full | NotKs=0) & DOK.next()
               !}
            ?}
         ?}
      ?};
      NotDekr=0 | NotKs=0
   ";
   DOK.cntx_psh();
   exec('loop_daty','!fks_jpk_dark',_fml);
   DOK.cntx_pop();
   {? NotKs | NotDekr
   || {!
      |? _ok:=FUN.choice(
            'Istnieją '+
            {? NotDekr || 'niezadekretowane dokumenty magazynowe' || '' ?}+
            {? NotKs
            || {? NotDekr || ' i ' || '' ?}+'niezaksięgowane dokumenty'
            || ''
            ?}+
            '.\nCzy kontynuować?',,
            'Tak','Pokaż szczegóły'
         );
         {? _ok=2
         || {? var_press('TabKom')<=0
            || NotKs:=NotDekr:=0; Full:=1;
               TabKom:=exec('tabKom','jpk');
               DOK.cntx_psh();
               exec('loop_daty','!fks_jpk_dark',_fml);
               DOK.cntx_pop()
            ?};
            TabKom.select();
            1
         ?}
      !};
      VAR_DEL.delete('TabKom')
   || _ok:=1
   ?};
   VAR_DEL.delete('NotKs','NotDekr','Full','TabKom');
   _ok
?}


\loop_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Petla po okresach
::   WE: _a - formula do wykonania
::  OLD: \loop_okr/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
OKRO_F.index('ROK'); OKRO_F.prefix();
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.first()
|| HELPJPK.ROK_OD();
   HELPJPK.OKR_OD();
   {!
   |? OKRO_F.prefix(ROK_F.ref());
      {? ROK_F.ref()=HELPJPK.ROK_OD | OKRO_F.first()
      || {!
         |? {? OKRO_F.POCZ<>date(0,0,0) & OKRO_F.KON<>date(0,0,0) | HELPJPK.RODZAJ='KR' & OKRO_F.NR<>0
            || _dalej:=_a()
            || _dalej:=1
            ?};
            _dalej & HELPJPK.OKR_DO<>OKRO_F.ref() & OKRO_F.next()
         !}
      ?};
      _dalej & HELPJPK.ROK_DO<>ROK_F.ref() & ROK_F.next()
   !}
?}


\loop_daty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Petla po zakresie dat
::   WE: _a - formuła
::  OLD: \loop_daty/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
DataOd:=HELPJPK.DATA_OD;
OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
{!
|? {? OKRO_F.find_ge(DataOd)
   || DataDo:=OKRO_F.KON
   ?};
   {? DataDo>HELPJPK.DATA_DO
   || DataDo:=HELPJPK.DATA_DO
   ?};
   OKRO_F.ROK();
   OKRO_F.cntx_psh();
   _dalej:=_a();
   OKRO_F.cntx_pop();
   {? _dalej & DataDo<>HELPJPK.DATA_DO
   || DataOd:=DataDo+1;
      1
   ?}
!}


\loop_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Petla po deklaracjach VAT
::   WE: _a - formula
::  OLD: \loop_vat/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
HELPJPK.OKR_OD();
DataOd:=OKRO_F.POCZ;
DataDo:=OKRO_F.KON;
exec('czytaj','#stalesys',DataOd,XINFO);
exec('czytaj','#stalesys',{? var_pres('DATE',HELPJPK)>0 || HELPJPK.DATE || date() ?},XINFO,'URZAD');
OKRO_F.cntx_psh();
_dalej:=_a();
OKRO_F.cntx_pop();
exec('czytaj','#stalesys',date(),XINFO);
~~


\jpk_kr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzenie JPK_KR dla okresu
::  OLD: \jpk_kr/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_jpk:=null;
JPK.cntx_psh();
JPK.prefix();
_dp:=OKRO_F.POCZ;
_dk:=OKRO_F.KON;
{? OKRO_F.POCZ=date(0,0,0)
|| DOK.cntx_psh();
   DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
   _zak:=sql('select MIN(DTW) DN, MAX(DTW) DX from DOK where A=\'T\'');
   DOK.cntx_pop();
   {? _zak.first()
   || _dp:=_zak.DN;
      _dk:=_zak.DX
   ?};
   {? _dp=date(0,0,0)
   || _dp:=ROK_F.KON_ROK;
      _dk:=ROK_F.KON_ROK
   ?}
?};
{? exec('jpk_nag','jpk',OKRO_F.ref(),_dp,_dk,'Księgi rachunkowe '+OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ,'')
|| _jpk:=JPK.ref();
   SSTALE.AR:=OKRO_F.ROK;
   SSTALE.AO:=OKRO_F.ref();
   exec('open_tabele','open_tab');
   _err:=0;
   DOK.index('PR1');
   DOK.prefix('N'); _err:=DOK.first();
   {? _err=0
   || DOK.prefix('T','N');
      _err:=DOK.first()
   ?};
   {? _err
   || exec('addkom','jpk','W','Istnieją niezaksięgowane dokumenty księgowe w okresie '+OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ)
   ?};
   _file:=exec('gen_jpk','jpk');
   {? _file<>''
   || exec('upd_jpk','jpk');
      JPK.bl_put('PLIK',_file,1);
      {? JPK.put()
      || jpkOK+=1
      ?};
      ferase(_file,1)
   ?}
?};
JPK.cntx_pop();
{? _jpk || JPK.seek(_jpk) ?};
1


\jpk_wb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzenie JPK_WB dla zakresu dat
::  OLD: \jpk_wb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:=OKRO_F.ROK;
_okres:=OKRO_F.ref();
TabRej.prefix($_rok);
{? TabRej.first()
|| DOK_REJ.prefix();
   REJ.prefix();
   {!
   |? TabRej.prefix($_rok,TabRej.JPK_RB,);
      VAR_DEL.delete('TabRej2');
      TabRej2:=sql('select REF from :_a where ROK=\':_b\' and JPK_RB=\':_c\' order by 1',TabRej,$_rok,TabRej.JPK_RB);
      {? TabRej2.first()
      || DOK_REJ.seek(BIT.sqlint(TabRej2.REF),)
      ?};
      SKID_RBK.index('TRN');
      SKID_RBK.prefix(REF.INFO,0,null,STR.gsub(TabRej.JPK_RB,' ',''),);
      {? SKID_RBK.first()
      || JPK_RB:=SKID_RBK.KRAJ().KODISO+SKID_RBK.N;
         HELPJPK.WAL:={? SKID_RBK.WAL=null || FINFO.NAROD || SKID_RBK.WAL ?}
      || JPK_RB:=TabRej.JPK_RB;
         HELPJPK.WAL:=FINFO.NAROD;
         exec('addkom','jpk','E','Nie znaleziono rachunku bankowego '+JPK_RB)
      ?};
      _jpk:=null;
      JPK.cntx_psh();
      JPK.prefix();
      {? exec('jpk_nag','jpk',_okres,DataOd,DataDo,'Wyciąg bankowy '+JPK_RB+' w '+HELPJPK.WAL().KOD,JPK_RB)
      || _jpk:=JPK.ref();
         SSTALE.AR:=_rok;
         SSTALE.AO:=_okres;
         exec('open_tabele','open_tab');
         _err:=0;
         _te:=sql(
            'select count(*) as l from DOK '+
            'where (A=\'N\' or ZP=\'N\') and DOK_REJ in (select REF from :_a) and DTW between to_date(:_b) and to_date(:_c)',
            TabRej2,DataOd,DataDo
         );
         _err:=_te.first() & _te.L<>0;
         &_te;
         {? _err
         || exec('addkom','jpk','W','Istnieją niezaksięgowane wyciągi bankowe w zakresie od '+$DataOd+' do '+$DataDo)
         ?};
         VAR_DEL.delete('BrakOpisu');
         BrakOpisu:=0;
         _file:=exec('gen_jpk','jpk');
         {? _file<>''
         || exec('upd_jpk','jpk');
            JPK.bl_put('PLIK',_file,1);
            {? JPK.put()
            || jpkOK+=1
            ?};
            ferase(_file,1)
         ?}
      ?};
      JPK.cntx_pop();
      {? _jpk || JPK.seek(_jpk) ?};
      TabRej.last();
      TabRej.prefix($_rok);
      TabRej.next()
   !}
?};
1


\jpk_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzenie JPK_VAT dla deklaracji VAT
::  OLD: \jpk_vat/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_jpk:=null;
_kor:=0;
{? 2+HELPJPK.RODZAJ='V7' & var_pres('KOR_D',HELPJPK)>0
|| {? HELPJPK.RODZAJ='V7K' & 1+(VAT_DEK.OKRES+3)='/'
   || _kor:=2
   |? HELPJPK.KOR
   || _kor:={? HELPJPK.KOR_D || 1 || 0 ?}+{? HELPJPK.KOR_E || 2 || 0 ?}
   || _kor:=3
   ?}
?};
JPK.cntx_psh();
JPK.prefix();
_opis:={? HELPJPK.RODZAJ='VAT' | var_pres('PART',JPK)<=0
       || 'Ewidencja zakupu i sprzedaży VAT '+VAT_DEK.OKRES+' ('+VAT_DEK.TYP+' wer. '+$VAT_DEK.WER+')'
       || {? _kor=1
          || 'Deklaracja VAT-7 '
          |? _kor=2
          || 'Ewidencja zakupu i sprzedaży VAT '
          || 'Deklaracja VAT-7 z ewidencją zakupu i sprzedaży VAT '
          ?}+
          VAT_DEK.OKRES+' ('+HELPJPK.RODZAJ+' wer. '+$VAT_DEK.WER+')'
       ?};
{? exec('jpk_nag','jpk',OKRO_F.ref(),DataOd,DataDo,_opis,$VAT_DEK.ref(),,HELPJPK.LP,,_kor)
|| _jpk:=JPK.ref();
   SSTALE.AR:=OKRO_F.ROK;
   SSTALE.AO:=OKRO_F.ref();
   exec('open_tabele','open_tab');
   VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
   E_DEK.index('ZRODLO');
   E_DEK.prefix(VAT_DEK.ref());
   {? HELPJPK.RODZAJ='VAT' & (~E_DEK.first() | E_DEK.AKC<>'T')
   || exec('addkom','jpk','W','Brak potwierdzenia UPO dla deklaracji '+VAT_DEK.TYP+' '+VAT_DEK.OKRES+' wer. '+$VAT_DEK.WER)
   ?};
   exec('czytaj','#stalesys',,XINFO,'BLP_JPK');
   _file:=exec('gen_jpk','jpk');
   {? _file<>''
   || exec('upd_jpk','jpk');
      JPK.bl_put('PLIK',_file,1);
      {? JPK.put()
      || jpkOK+=1
      ?};
      ferase(_file,1)
   ?}
?};
JPK.cntx_pop();
{? _jpk || JPK.seek(_jpk) ?};
1


\jpk_fa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzenie JPK_FA dla zakresu dat
::  OLD: \jpk_fa/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:=OKRO_F.ROK;
_okres:=OKRO_F.ref();
_ttrok:=tab_tmp(1,'REF','STRING[16]',,'NAZ','STRING[4]',);
ROK_F.cntx_psh();
ROK_F.index('ROKPOCZ');
ROK_F.prefix(REF.FIRMA);
{? HELPJPK.OKR_TYP='D' & ROK_F.seek(_rok) |
   HELPJPK.OKR_TYP='O' & ROK_F.last()
|| {!
   |? _ttrok.REF:=$ROK_F.ref(); _ttrok.NAZ:=ROK_F.NAZ; _ttrok.add();
      ROK_F.ref()<>_rok & ROK_F.prev()
   !};
   _par85:=#PAR_SKID.get(85);
   _l_wstecz:={? _par85>-1
              || _par85
              || ROK_F.size()-1
              ?};
   {! _i:= 1 .. _l_wstecz |!  {? ROK_F.prev() || _ttrok.REF:=$ROK_F.ref; _ttrok.NAZ:=ROK_F.NAZ; _ttrok.add() ?} !};
   {? ROK_F.seek(_rok) & ROK_F.next()
   || _ttrok.REF:=$ROK_F.ref; _ttrok.NAZ:=ROK_F.NAZ; _ttrok.add()
   ?}
?};
ROK_F.cntx_pop();
TabRej.prefix($_rok);
{? TabRej.first()
|| DOK_REJ.prefix();
   REJ.prefix();
   TabRej.prefix($OKRO_F.ROK,);
   VAR_DEL.delete('TabRej2');
   TabRej2:=sql('select REF from :_a where ROK in (select REF from :_b) order by 1',TabRej,_ttrok);
   {? TabRej2.first()
   || DOK_REJ.seek(BIT.sqlint(TabRej2.REF),)
   ?};
   _naz:='Faktury VAT'+{? HELPJPK.FAK_TYP='Z' || ' zakupu' |? HELPJPK.FAK_TYP='S' || ' sprzedaży' || '' ?};
   _naz+=' w '+HELPJPK.WAL().KOD;
   _naz+=' '+{? HELPJPK.OKR_TYP='D' || ' dla zakresu dat' || ' dla okresu VAT' ?};
   _jpk:=null;
   JPK.cntx_psh();
   JPK.prefix();
   {? exec('jpk_nag','jpk',_okres,DataOd,DataDo,_naz,HELPJPK.FAK_TYP+HELPJPK.OKR_TYP+$HELPJPK.WAL)
   || _jpk:=JPK.ref();
      SSTALE.AR:=_rok;
      SSTALE.AO:=_okres;
      exec('open_tabele','open_tab');
      _err:=0;
      {? $FINFO.NAROD=2-JPK.DODAT
      || _wal:='(DOK.WAL is null or DOK.WAL=\''+(2-JPK.DODAT)+'\') '
      || _wal:='DOK.WAL=\''+(2-JPK.DODAT)+'\' '
      ?};
      {? HELPJPK.OKR_TYP='D'
      || _okr:='DTW between to_date(:_b) and to_date(:_c) ';
         _okr_kom:='zakresie od '+$DataOd+' do '+$DataDo
      || _okr:='DOK.OKRVAT=\''+$JPK.OKRO_F+'\' ';
         _okr_kom:='okresie VAT'
      ?};
      ROK_F.cntx_psh();
      OKRO_F.cntx_psh();
      OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
      {? HELPJPK.OKR_TYP='D' || SSTALE.AO() || OKRO_F.last() ?};
      {!
      |? DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
         _te:=sql(
            'select count(*) as l from DOK '+
            'where (A=\'N\' or ZP=\'N\') and '+_wal+' and DOK_REJ in (select REF from :_a) and '+_okr,
            TabRej2,DataOd,DataDo
         );
         _err:=_te.first() & _te.L<>0;
         &_te;
         {? _err & HELPJPK.OKR_TYP='O'
         || exec('addkom','jpk','W','Istnieją niezaksięgowane faktury w '+_okr_kom+' (okres obrachunkowy: '+OKRO_F.NAZ+' '+ROK_F.NAZ+')')
         ?};
         HELPJPK.OKR_TYP='O' & OKRO_F.prev() & OKRO_F.ZAM<>'T'
      !};
      OKRO_F.cntx_pop();
      ROK_F.cntx_pop();
      {? _err & HELPJPK.OKR_TYP='D'
      || exec('addkom','jpk','W','Istnieją niezaksięgowane faktury w '+_okr_kom)
      ?};
      _typ:={? HELPJPK.FAK_TYP='W' || '' || HELPJPK.FAK_TYP ?};
      {? exec('jpk_fa_chk','jpk_log',DataOd,DataDo,_typ,0).first()
      || exec('addkom','jpk','W','Istnieją niezadekretowane faktury w zakresie od '+$DataOd+' do '+$DataDo)
      ?};
      VAR_DEL.delete('BrakOpisu');
      BrakOpisu:=0;
      _file:=exec('gen_jpk','jpk');
      {? _file<>''
      || exec('upd_jpk','jpk');
         JPK.bl_put('PLIK',_file,1);
         {? JPK.put()
         || jpkOK+=1
         ?};
         ferase(_file,1)
      ?}
   ?};
   JPK.cntx_pop();
   {? _jpk || JPK.seek(_jpk) ?}
?};
1


\jpk_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzenie JPK_MAG dla zakresu dat
::  OLD: \jpk_mag/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:=OKRO_F.ROK;
_okres:=OKRO_F.ref();
SSTALE.AR:=_rok;
SSTALE.AO:=_okres;
exec('open_tabele','open_tab');
VAR_DEL.delete('TabMag');
TabMag:=sql(
   'select distinct DOK.MG from DOK join DOK_REJ where '+
   'DOK.ZP=\'T\' and DTW between to_date(:_a) and to_date(:_b) and DOK_REJ.JPK_TYP=\'M\'',
   DataOd,DataDo
);
{? TabMag.first()
|| DOK.cntx_psh();
   ND.cntx_psh();
   DK.cntx_psh();
   FAKS.cntx_psh();
   VAR_DEL.delete('MagNag','JPK_MAG');
   MagNag:=obj_new(9);
   {! _i:=1..obj_len(MagNag) |! MagNag[_i]:='' !};
   {!
   |? JPK_MAG:=TabMag.MG;
      _jpk:=null;
      JPK.cntx_psh();
      JPK.prefix();
      {? exec('jpk_nag','jpk',_okres,DataOd,DataDo,'Magazyn '+JPK_MAG,JPK_MAG)
      || _jpk:=JPK.ref();
         _err:=0;
         _te:=sql(
            'select count(*) as l from DOK '+
            'where (A=\'N\' or ZP=\'N\') and DOK.MG=\':_a\' and DTW between to_date(:_b) and to_date(:_c)',
            JPK_MAG,DataOd,DataDo
         );
         _err:=_te.first() & _te.L<>0;
         &_te;
         {? _err
         || exec('addkom','jpk','W','Istnieją niezaksięgowane dokumenty magazynowe w zakresie od '+$DataOd+' do '+$DataDo)
         ?};
         {? exec('jpk_mag_chk','jpk_log',JPK_MAG,DataOd,DataDo,0).first()
         || exec('addkom','jpk','W','Istnieją niezadekretowane dokumenty magazynowe w zakresie od '+$DataOd+' do '+$DataDo)
         ?};
         _file:=exec('gen_jpk','jpk');
         {? _file<>''
         || exec('upd_jpk','jpk');
            JPK.bl_put('PLIK',_file,1);
            {? JPK.put()
            || jpkOK+=1
            ?};
            ferase(_file,1)
         ?}
      ?};
      JPK.cntx_pop();
      {? _jpk || JPK.seek(_jpk) ?};
      TabMag.next()
   !};
   DOK.cntx_pop();
   ND.cntx_pop();
   DK.cntx_pop();
   FAKS.cntx_pop()
?};
VAR_DEL.delete('TabMag','JPK_MAG','MagNag','Suma','NrJPK');
1


\bv_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietl HELPJPK.ISTDEF
::  OLD: \bv_sch/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ='' | 'VAT,V7M,V7K'*HELPJPK.RODZAJ
|| exec('findfnrd','color')
|| ''
?}


\be_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja HELPJPK.ISTDEF
::  OLD: \be_sch/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.RODZAJ<>'' & 'VAT,V7M,V7K'*HELPJPK.RODZAJ=0


\bv_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietleniem HELPJPK.ROK_OD i HELPJPK.ROK_DO
::  OLD: \bv_rok/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ='' | HELPJPK.OKR_TYP<>'O' |
   cur_afld()='ROK_DO' & 'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ |
   cur_afld()='ROK_OD' & HELPJPK.RODZAJ='SF'
|| exec('findfnrd','color')
|| ''
?}


\be_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja HELPJPK.ROK_OD i HELPJPK.ROK_DO
::  OLD: \be_rok/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='O'
|| {? 'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ & cur_afld()='ROK_DO' |
      HELPJPK.RODZAJ='SF' & cur_afld()='ROK_OD'
   || win_disp();
      0
   || 1
   ?}
?}


\ae_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji HELPJPK.ROK_OD i HELPJPK.ROK_DO
::  OLD: \ae_rok/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.OKR_OD & HELPJPK.OKR_OD().ROK<>HELPJPK.ROK_OD
|| HELPJPK.OKR_OD:=null
|? HELPJPK.RODZAJ<>'SF' & HELPJPK.OKR_DO & HELPJPK.OKR_DO().ROK<>HELPJPK.ROK_DO
|| HELPJPK.OKR_DO:=null
?};
{? 'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ
|| HELPJPK.ROK_DO:=HELPJPK.ROK_OD;
   HELPJPK.OKR_DO:=HELPJPK.OKR_OD
|? HELPJPK.RODZAJ='SF'
|| {? HELPJPK.ROK_OD<>HELPJPK.ROK_DO
   || HELPJPK.ROK_OD:=HELPJPK.ROK_DO;
      OKRO_F.cntx_psh();
      OKRO_F.index('ROK'); OKRO_F.prefix(HELPJPK.ROK_OD);
      {? OKRO_F.first() & OKRO_F.POCZ=date(0,0,0) & OKRO_F.next()
      || HELPJPK.OKR_OD:=OKRO_F.ref()
      ?};
      {? HELPJPK.OKR_DO
      || HELPJPK.OKR_DO:={? OKRO_F.find_key(HELPJPK.OKR_DO().NR) || OKRO_F.ref() || null ?}
      || {? OKRO_F.last() & OKRO_F.POCZ=date(0,0,0) & OKRO_F.prev()
         || HELPJPK.OKR_DO:=OKRO_F.ref()
         ?}
      ?};
      OKRO_F.cntx_pop()
   ?}
?};
win_disp()


\bv_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietleniem HELPJPK.OKR_OD i HELPJPK.OKR_DO
::  OLD: \bv_okr/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'VAT'*HELPJPK.RODZAJ
|| HELPJPK.OKR_DO:=HELPJPK.OKR_OD;
   {? HELPJPK.KOR || exec('find_sch','jpk',date()) || exec('find_sch','jpk',HELPJPK.OKR_OD().POCZ) ?}
|? 'V7M,V7K,GV'*HELPJPK.RODZAJ
|| HELPJPK.OKR_DO:=HELPJPK.OKR_OD;
   exec('find_sch','jpk',HELPJPK.OKR_OD().POCZ)
?};
{? HELPJPK.RODZAJ='' | HELPJPK.OKR_TYP<>'O' |
   (cur_afld()='OKR_OD' & ~HELPJPK.ROK_OD) | (cur_afld()='OKR_DO' & ~HELPJPK.ROK_DO) |
   (cur_afld()='OKR_DO' & 'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ) |
   (cur_afld()='OKR_OD' & HELPJPK.RODZAJ='SF')
|| exec('findfnrd','color')
|| ''
?}


\be_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja HELPJPK.OKR_OD i HELPJPK.OKR_DO
::  OLD: \be_okr/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='O' &
   ((cur_afld()='OKR_OD' & HELPJPK.ROK_OD) | (cur_afld()='OKR_DO' & HELPJPK.ROK_DO)) &
   ('VAT,V7M,V7K,GV'*HELPJPK.RODZAJ=0 | cur_afld()='OKR_OD') &
   (HELPJPK.RODZAJ<>'SF' | cur_afld()='OKR_DO')
|| {? cur_afld()='OKR_OD'
   || HELPJPK.ROK_OD()
   || HELPJPK.ROK_DO()
   ?};
   1
?}


\bv_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietleniem HELPJPK.DATA_OD i HELPJPK.DATA_DO
::  OLD: \bv_data/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ='' | HELPJPK.OKR_TYP<>'D'
|| exec('findfnrd','color')
|| ''
?}


\be_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja HELPJPK.DATA_OD i HELPJPK.DATA_DO
::  OLD: \be_data/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='D'


\be_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja HELPJPK.OKR_TYP
::  OLD: \be_typ/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.RODZAJ<>'' & HELPJPK.RODZAJ='FA'


\ae_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji HELPJPK.OKR_TYP
::  OLD: \ae_typ/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.OKR_TYP='D'
|| HELPJPK.ROK_OD:=HELPJPK.ROK_DO:=HELPJPK.OKR_OD:=HELPJPK.OKR_DO:=null;
   {? HELPJPK.DATA_OD=date(0,0,0) & HELPJPK.DATA_DO=HELPJPK.DATA_OD
   || HELPJPK.DATA_OD:=SSTALE.AO().POCZ;
      HELPJPK.DATA_DO:=OKRO_F.KON
   ?}
|| HELPJPK.DATA_OD:=HELPJPK.DATA_DO:=date(0,0,0);
   {? HELPJPK.ROK_OD=null & HELPJPK.ROK_DO=null
   || HELPJPK.ROK_OD:=HELPJPK.ROK_DO:=SSTALE.AR;
      HELPJPK.OKR_OD:=HELPJPK.OKR_DO:=SSTALE.AO
   ?}
?};
win_disp()


\be_jpk_tf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redagowaniem pola HELPJPK.FAK_TYP
::  OLD: \be_jpk_tf/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=HELPJPK.RODZAJ='FA';
{? cur_afld()='WAL'
|| XINFO.SLWAL()
?};
_ok


\bv_jpk_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietleniem HELPJPK.WAL
::  OLD: \bv_jpk_wal/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('be_jpk_tf','!fks_jpk_dark')
|| exec('findfnrd','color')
|| ''
?}


\be_jpk_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja HELPJPK.KOR
::  OLD: \be_jpk_kor/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ & HELPJPK.ISTDEF().DATA>=date(2017,1,1)


\bv_hjpk_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietleniem pola HELPJPK.RB
::  OLD: \bv_hjpk_rb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ<>'WB'
|| exec('findfnrd','color')
|| ''
?}


\be_hjpk_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redagowaniem pola HELPJPK.RB
::  OLD: \be_hjpk_rb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.RODZAJ='WB'


\f3_hjpk_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Klawisz F3 pola HELPJPK.RB
::  OLD: \f3_hjpk_rb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('AllRB');
AllRB:=1;
OPERATOR.USER();
_wy:=exec('f3_vrbup','rachunki','HELPJPK','RB',REF.INFO,0);
{? type_of(_wy)=type_of('')
|| HELPJPK.RB:=_wy;
   exec('bd_varrb','rachunki','HELPJPK','RB',REF.INFO,0)
?};
0


\ae_hjpk_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji pola HELPJPK.RB
::  OLD: \ae_hjpk_rb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld<>''
|| {? exec('ae_kb','rachunki')
   || exec('bd_varrb','rachunki','HELPJPK','RB',REF.INFO,0);
      1
   ?}
|| 1
?}


\bv_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja HELPJPK.OKR_TYP
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.efld_opt('RED','enable='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='O'),HELPJPK,'ROK_OD');
HELPJPK.efld_opt('RED','enable='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='O'),HELPJPK,'ROK_DO');
HELPJPK.efld_opt('RED','enable='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='O'),HELPJPK,'OKR_OD');
HELPJPK.efld_opt('RED','enable='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='O'),HELPJPK,'OKR_DO');
HELPJPK.efld_opt('RED','enable='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='D'),HELPJPK,'DATA_OD');
HELPJPK.efld_opt('RED','enable='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='D'),HELPJPK,'DATA_DO');
HELPJPK.efld_opt('RED','mark='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='O'),HELPJPK,'ROK_OD');
HELPJPK.efld_opt('RED','mark='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='O'),HELPJPK,'ROK_DO');
HELPJPK.efld_opt('RED','mark='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='D'),HELPJPK,'DATA_OD');
HELPJPK.efld_opt('RED','mark='+$(HELPJPK.RODZAJ<>'' & HELPJPK.OKR_TYP='D'),HELPJPK,'DATA_DO');
~~


\bv_rodzaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed wyświetleniem pola HELPJPK.RODZAJ
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.RODZAJ<>''),HELPJPK,'ISTDEF');
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.RODZAJ='FA'),HELPJPK,'OKR_TYP');
exec('bv_typ','!fks_jpk_dark');
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.RODZAJ='FA'),HELPJPK,'FAK_TYP');
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.RODZAJ='FA'),HELPJPK,'WAL');
HELPJPK.efld_opt('RED2','mark='+$(HELPJPK.RODZAJ='FA'),HELPJPK,'WAL');
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.RODZAJ='WB'),HELPJPK,'RB');
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.RODZAJ<>''),HELPJPK,'CYKL');
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.RODZAJ<>''),HELPJPK,'KOR');
_enable:=HELPJPK.RODZAJ='VAT' & HELPJPK.ISTDEF().DATA>=date(2018,1,1);
_v7:=2+HELPJPK.RODZAJ='V7' | 2+HELPJPK.RODZAJ='GV';
_email:=_enable | _v7;
HELPJPK.efld_opt('RED2','enable='+$_email,HELPJPK,'EMAIL');
HELPJPK.efld_opt('RED2','mark='+$_v7,HELPJPK,'EMAIL');
HELPJPK.efld_opt('RED2','enable='+$(_enable & HELPJPK.KOR),HELPJPK,'LP');
HELPJPK.efld_opt('RED_XML','enable='+$_email,HELPJPK,'EMAIL');
HELPJPK.efld_opt('RED_XML','enable='+$_enable,HELPJPK,'LP');
_sf:=HELPJPK.RODZAJ='SF' & HELPJPK.ISTDEF;
HELPJPK.efld_opt('RED2','enable='+$_sf,HELPJPK,'E_SPR');
HELPJPK.efld_opt('RED2','mark='+$_sf,HELPJPK,'E_SPR');
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.E_SPR<>null),HELPJPK,'E_SPR_A');
~~


\be_cykl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja HELPJPK.CYKL
::  OLD: \be_cykl/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.RODZAJ<>''


\sel_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Znajduje deklaracje VAT-7 do generowania pliku JPK_VAT
::  OLD: \sel_vat/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
VAR_DEL.delete('SelVat');
SelVat:=tab_tmp(3,
   'DATA','DATE','Data wypełnienia',
   'TYP','STRING[10]','Typ',
   'WER','INTEGER','Wariant',
   'OPIS','STRING[40]','Opis',
   'REF','INTEGER','ref',
   'ZAK','INTEGER',
);
SelVat1:=SelVat.index('?');
SelVat2:=SelVat.ndx_tmp('',1,'ZAK',,0, 'DATA',,0, 'WER',,0);
HELPJPK.OKR_OD();
SelVatNr:=_nr:=exec('vat7_nr','jpk');
_okres:={? HELPJPK.RODZAJ='V7K' & OKRO_F.POCZ~2%*3=0
        || _okr:=OKRO_F.POCZ~2;
           _kw:={? _okr<4 || 1 |? _okr<7 || 2 |? _okr<10 || 3 || 4 ?};
           $(OKRO_F.POCZ~1)+'/'+$_kw
        || $(OKRO_F.POCZ~1)+'/'+form(OKRO_F.POCZ~2,-2)
        ?};
_typ:={? HELPJPK.RODZAJ='VAT' || 'VAT7' || HELPJPK.RODZAJ ?};
SelVatT:={? HELPJPK.KOR || _typ+'_KOR' || _typ ?};
VAT_DEK.index('VAT_DEKR');
VAT_DEK.prefix({? HELPJPK.RODZAJ='VAT' || 'V' |? HELPJPK.RODZAJ='GV' || 'G' |? HELPJPK.RODZAJ='V7M' || 'j' || 'J' ?},
               OKRO_F.ROK().POCZ_ROK,_okres,);
{? VAT_DEK.first()
|| {!
   |? {? VAT_DEK.NR().NR=_nr
      || SelVat.DATA:=VAT_DEK.DATA;
         SelVat.TYP:=VAT_DEK.TYP;
         SelVat.WER:=VAT_DEK.WER;
         SelVat.OPIS:=VAT_DEK.OPIS;
         SelVat.REF:=#VAT_DEK.ref();
         SelVat.ZAK:=VAT_DEK.TYP=SelVatT;
         SelVat.add()
      ?};
      VAT_DEK.next()
   !}
?};
{? SelVat.first()
|| {? SelVat.next()
   || rodzdekl:='V';
      vat_typ:='VAT7';
      _o:=SelVat.mk_sel(,'P',,'#selvat',,,,,'U');
      SelVat.win_fld(_o,,'DATA');
      SelVat.win_fld(_o,,'TYP');
      SelVat.win_fld(_o,,'WER');
      SelVat.win_fld(_o,,'OPIS');
      {? HELPJPK.RODZAJ='VAT'
      || SelVat.win_fld(_o,VAT7,'PAR1',,,,,,'E-deklaracje',,,2,,"1","0")
      ?};
      SelVat.win_fld(_o,HELPJPK,'VAT_JPK',,,,,,'JPK',,,2,,"'T'","'N'");
      SelVat.win_act(_o,,'Formuła','Wybierz',,,"
         _ok:=1;
         {? HELPJPK.RODZAJ='VAT' & ~exec('is_edek_vat7','jpk',0)
         || _ok:=FUN.ask('Wybrana deklaracja nie posiada\ne-deklaracji z potwierdzeniem UPO.\nCzy kontynuować?')
         ?};
         {? _ok || sel_exit() ?}
      ",,1);
      SelVat.win_act(_o,,'Formuła','Pozycje',,,"exec('vat_dpoz','fks_ved')");
      SelVat.win_act(_o,,'Formuła','uzAsadnienia',,,"exec('vd_uzas','fks_ved')");
      {? HELPJPK.RODZAJ='VAT'
      || SelVat.win_act(_o,,'Formuła','wyDruk',,,"exec('vat_dwy','!fks_ved_zv7p')");
         SelVat.win_act(_o,,'Formuła','E-deklaracje',,,"exec('edek_sel_vat7','fks_ved')")
      ?};
      SelVat.win_act(_o,,'Rekord',,,,"
         VAT7.PAR1:=
         {? VAT_DEK.seek(SelVat.REF,)
         || exec('is_edek','xml',VAT_DEK.TYP)
         ?};
         ~~
      ");
      SelVat.win_act(_o,,'Wyświetl',,,,"exec('vat_wys','fks_ved')");
      SelVat.win_sel(_o);
      exec('sel_vat_zak','!fks_jpk_dark');
      _ok:={? SelVat.select() || 2 ?}
   || _ok:=1
   ?};
   {? _ok
   || {? ~VAT_DEK.seek(SelVat.REF,) || _ok:=0 ?}
   ?}
|| FUN.info('Brak deklaracji '+_typ+' ('+$_nr+') dla okresu '+OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ+'.')
?};
VAR_DEL.delete('SelVat','SelVat1','SelVat2','SelVatNr','SelVatT');
_ok


\sel_vat_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia zakres selekcji deklaracji VAT
::  OLD: \sel_vat_zak/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:={? HELPJPK.RODZAJ='VAT' || 'VAT-7' || HELPJPK.RODZAJ ?};
SelVat.index(SelVat2);
SelVat.prefix(1);
SelVat.hdr_sel();
{? SelVatT+3='KOR'
|| SelVat.hdr_sel('Korekty deklaracji '+_typ+' ('+$SelVatNr+') za okres '+OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ)
|| SelVat.hdr_sel('Deklaracje '+_typ+' ('+$SelVatNr+') za okres '+OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ)
?}


\bv_jpk_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Przed wyświetleniem HELPJPK.KOD
::----------------------------------------------------------------------------------------------------------------------
_enable:=HELPJPK.RODZAJ='VAT' & HELPJPK.KOR & HELPJPK.ISTDEF().DATA>=date(2018,1,1);
HELPJPK.efld_opt('RED','enable='+$_enable,HELPJPK,'LP');
HELPJPK.efld_opt('RED_CEL','enable='+$_enable,HELPJPK,'LP');
~~


\ae_jpk_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Po redakcji pola HELPJPK.KOR
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.KOR & ~('V7M,V7K'*HELPJPK.RODZAJ) || exec('find_sch','jpk',date()) || exec('find_sch','jpk',HELPJPK.OKR_OD().POCZ) ?};
{? HELPJPK.ISTDEF().DATA<date(2018,1,1)
|| HELPJPK.LP:=0
|? HELPJPK.KOR
|| {? HELPJPK.LP=0
   || _okres:={? HELPJPK.OKR_OD || exec('okres','jpk',HELPJPK.OKR_OD) || exec('okres2','jpk',HELPJPK.DATA_OD) ?};
      JPK.cntx_psh();
      JPK.index('LP'); JPK.prefix(REF.FIRMA,_okres,HELPJPK.RODZAJ);
      HELPJPK.LP:={? JPK.last() || JPK.LP+1 || 1 ?};
      JPK.cntx_pop()
   ?}
|| HELPJPK.LP:=0;
   {? var_pres('KOR_D',HELPJPK)>0
   || HELPJPK.KOR_D:=HELPJPK.KOR_E:=0
   ?}
?};
win_disp();
1


\chg_jpk_cel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Zmiana celu złożenia pliku JPK
::----------------------------------------------------------------------------------------------------------------------
{? JPK.TYP<>'VAT'
|| FUN.info('Akcja dostępna dla plików JPK_VAT od wersji 2.'@);
   return
|? JPK.KOR & JPK.OKRO_F().POCZ<date(2018,1,1) & JPK.D_GEN>=date(2018,1,1)
|| FUN.info('Akcja niedostępna z uwagi na brak możliwości zmiany schematu.'@);
   return
|? ~JPK.KOR & JPK.OKRO_F().POCZ<date(2018,1,1) & date>=date(2018,1,1)
|| FUN.info('Akcja niedostępna z uwagi na brak możliwości zmiany schematu.'@);
   return
?};
_cel:=JPK.LP;
HELPJPK.RODZAJ:=JPK.TYP;
HELPJPK.ISTDEF:=JPK.ISTDEF;
HELPJPK.DATA_OD:=JPK.OD;
HELPJPK.KOR:=JPK.KOR;
HELPJPK.LP:=JPK.LP;
HELPJPK.win_edit('RED_CEL');
{? HELPJPK.edit("
      {? HELPJPK.ISTDEF().DATA<date(2018,1,1)
      || HELPJPK.LP:={? HELPJPK.KOR || 2 || 1 ?};
         1
      |? JPK.LP=HELPJPK.LP
      || FUN.info('Nie zmieniono celu złożenia pliku JPK.'@); {? HELPJPK.LP=0 || 'KOR' || 'LP' ?}
      || exec('chk_jpk_lp','jpk',REF.FIRMA,HELPJPK.DATA_OD,HELPJPK.RODZAJ,HELPJPK.LP,1)
      ?}
   ")
|| _fn:='jpk'+$JPK.tm_stamp()+'.xml';
   _fn2:='jpk'+$JPK.tm_stamp()+'_2.xml';
   _ok:=0;
   {? JPK.bl_get('PLIK',_fn,1)
   || {? _f:=fopen(_fn,'r',1)
      || {? _f2:=fopen(_fn2,'w',1)
         || {!
            |? _line:=fread(_f);
               _p:=_line*'<CelZlozenia>';
               {? _p
               || _p2:=_line*'</CelZlozenia>';
                  {? _p2
                  || _line:=((_p+12)+_line)+$HELPJPK.LP+((_p2-1)-_line);
                     _ok:=1
                  ?}
               ?};
               {? _line<>'\n'
               || fwrite(_f2,_line);
                  1
               ?}
            !};
            fclose(_f2)
         ?};
         fclose(_f)
      ?}
   ?};
   {? _ok
   || _name:=JPK.bl_info('PLIK','NAME');
      do();
      {? ~JPK.bl_put('PLIK',_fn2,1,,_name) || undo() ?};
      {? HELPJPK.ISTDEF().DATA>=date(2018,1,1)
      || JPK.LP:=HELPJPK.LP;
         JPK.KOR:={? JPK.LP=0 || 0 || 1 ?}
      || JPK.KOR:=HELPJPK.KOR
      ?};
      JPK.put();
      _ok:=end()
   ?};
   {? _ok
   || FUN.info('Zmieniono cel złożenia pliku JPK.'@)
   || FUN.info('Nie udało się zmienić celu złożenia pliku JPK.'@)
   ?};
   {? fexists(_fn,1) || ferase(_fn,1) ?};
   {? fexists(_fn2,1) || ferase(_fn2,1) ?}
?}


\add_v7_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Dołaczenie pliku JPK_V7M/K z poziomu deklaracji VAT_DEK.
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_JPK_DARK';
_params.UIDREF:=VAT_DEK.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'VAT_DEK',VAT_DEK.ref());
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\add_v7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dołaczenie pliku JPK_V7M/K z poziomu deklaracji VAT
::   WE: [_a] - wskazanie na deklarację
::  OLD: \add_v7/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
VAT_DEK.cntx_psh();
VAT_DEK.prefix();
{? var_pres('_a')>0
|| VAT_DEK.seek(_a)
?};
OKRO_F.win_dict('SLO');
ISTDEF.win_sel('W_JPK');
HELPJPK.win_edit('RED');
HELPJPK.blank(1);
HELPJPK.RODZAJ:=3+VAT_DEK.TYP;
HELPJPK.CYKL:='T';
{? var_press('EMAIL',HELPJPK)>0
|| HELPJPK.EMAIL:=VAT_DEK.EMAIL
?};
{? var_press('DATE',HELPJPK)>0
|| HELPJPK.DATE:=date();
   HELPJPK.TIME:=time()
?};
HELPJPK.OKR_TYP='O';
_mc:={? 1+(VAT_DEK.OKRES+3)='/'
     || #(VAT_DEK.OKRES+2)
     || _kw:=#(VAT_DEK.OKRES+1);
        _kw*3
     ?};
_date:=date(#(4+VAT_DEK.OKRES),_mc,1);
OKRO_F.cntx_psh();
OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
{? OKRO_F.find_ge(_date)
|| HELPJPK.OKR_OD:=OKRO_F.ref()
|| HELPJPK.OKR_OD:=SSTALE.AO
?};
OKRO_F.cntx_pop();
HELPJPK.OKR_DO:=HELPJPK.OKR_OD;
HELPJPK.ISTDEF:=null;
HELPJPK.KOR:=(VAT_DEK.TYP+3)='KOR';
exec('find_sch','jpk',HELPJPK.OKR_OD().POCZ);
HELPJPK.KOR_D:=HELPJPK.KOR_E:=1;
{? HELPJPK.ISTDEF=null
|| FUN.info('Nie znaleziono schematu JPK.')
|? HELPJPK.KOR & (_pyt:=FUN.choice('Czy utworzyć korektę plik JPK_%1 za okres %2?'@[3+VAT_DEK.TYP,VAT_DEK.OKRES],,'część Deklaracyjna','część Ewidencyjna','Obie części'))
   |
   ~HELPJPK.KOR & FUN.ask('Czy utworzyć plik JPK_%1 za okres %2?'@[3+VAT_DEK.TYP,VAT_DEK.OKRES])
|| {? HELPJPK.KOR
   || {? _pyt=1
      || HELPJPK.KOR_E:=0
      |? _pyt=2
      || HELPJPK.KOR_D:=0
      ?}
   ?};
   HELPJPK.LP:=exec('get_lp','jpk',HELPJPK.OKR_OD().POCZ);
   jpkOK:=0;
   jpkErr:=0;
   jpkWrn:=0;
   F.set_odd(null);
   _fml:="exec('jpk_vat','!fks_jpk_dark')";
   _ar:=SSTALE.AR;
   _ao:=SSTALE.AO;
   exec('loop_vat','!fks_jpk_dark',_fml);
   echo();
   SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   exec('open_tabele','open_tab');
   FUN.info(
      'Liczba utworzonych plików JPK: '+$jpkOK+'\n'+
      'Liczba plików JPK z błędami: '+$jpkErr+'\n'+
      'Liczba plików JPK z ostrzeżeniami: '+$jpkWrn+'.'
   );
   VAR_DEL.delete('jpkOK','jpkErr','jpkWrn');
   _ok:=1
?};
VAT_DEK.cntx_pop();
_ok


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'vat_dek'>0
         || _vat_dek:=exec('FindAndGet','#table',VAT_DEK,_kref,,,null());
            {? _vat_dek=null()
            || _mp.done()
            |? exec('FindAndGet','#table',VAT_DEK,_kref,,"@.VAT_DEK.STATUS='R'",1)
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1


\add_gv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dołącza JPK_GV z poziomu deklaracji
::   WE: [_a] - wskazanie na deklarację
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
VAT_DEK.cntx_psh();
VAT_DEK.prefix();
{? var_pres('_a')>0
|| VAT_DEK.seek(_a)
?};
OKRO_F.win_dict('SLO');
ISTDEF.win_sel('W_JPK');
HELPJPK.win_edit('RED');
HELPJPK.blank(1);
HELPJPK.RODZAJ:=2+VAT_DEK.TYP;
HELPJPK.CYKL:='T';
{? var_press('EMAIL',HELPJPK)>0
|| HELPJPK.EMAIL:=VAT_DEK.EMAIL
?};
{? var_press('DATE',HELPJPK)>0
|| HELPJPK.DATE:=date();
   HELPJPK.TIME:=time()
?};
HELPJPK.OKR_TYP='O';
_mc:={? 1+(VAT_DEK.OKRES+3)='/'
     || #(VAT_DEK.OKRES+2)
     || _kw:=#(VAT_DEK.OKRES+1);
        _kw*3
     ?};
_date:=date(#(4+VAT_DEK.OKRES),_mc,1);
OKRO_F.cntx_psh();
OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
{? OKRO_F.find_ge(_date)
|| HELPJPK.OKR_OD:=OKRO_F.ref()
|| HELPJPK.OKR_OD:=SSTALE.AO
?};
OKRO_F.cntx_pop();
HELPJPK.OKR_DO:=HELPJPK.OKR_OD;
HELPJPK.ISTDEF:=null;
HELPJPK.KOR:=(VAT_DEK.TYP+3)='KOR';
exec('find_sch','jpk',HELPJPK.OKR_OD().POCZ);
HELPJPK.KOR_D:=HELPJPK.KOR_E:=1;
{? HELPJPK.ISTDEF=null
|| FUN.info('Nie znaleziono schematu JPK.'@)
|? HELPJPK.KOR & (_pyt:=FUN.ask('Czy utworzyć korektę plik JPK_%1 za okres %2?'@[2+VAT_DEK.TYP,VAT_DEK.OKRES]))
   |
   ~HELPJPK.KOR & FUN.ask('Czy utworzyć plik JPK_%1 za okres %2?'@[2+VAT_DEK.TYP,VAT_DEK.OKRES])
|| HELPJPK.LP:=exec('get_lp','jpk',HELPJPK.OKR_OD().POCZ);
   jpkOK:=0;
   jpkErr:=0;
   jpkWrn:=0;
   F.set_odd(null);
   _fml:="exec('jpk_gv','!fks_jpk_dark')";
   _ar:=SSTALE.AR;
   _ao:=SSTALE.AO;
   exec('loop_vat','!fks_jpk_dark',_fml);
   echo();
   SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   exec('open_tabele','open_tab');
   FUN.info(
      'Liczba utworzonych plików JPK: '@+$jpkOK+'\n'+
      'Liczba plików JPK z błędami: '@+$jpkErr+'\n'+
      'Liczba plików JPK z ostrzeżeniami: '@+$jpkWrn+'.'
   );
   VAR_DEL.delete('jpkOK','jpkErr','jpkWrn');
   _ok:=1
?};
VAT_DEK.cntx_pop();
_ok


\jpk_gv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Tworzenie JPK_GV dla deklaracji grup vat
::----------------------------------------------------------------------------------------------------------------------
_jpk:=null;
JPK.cntx_psh();
JPK.prefix();
_opis:='Ewidencja wewnętrzna członka grupy VAT '+VAT_DEK.OKRES+' ('+VAT_DEK.TYP+' wer. '+$VAT_DEK.WER+')';
{? exec('jpk_nag','jpk',OKRO_F.ref(),DataOd,DataDo,_opis,$VAT_DEK.ref(),,HELPJPK.LP,,)
|| _jpk:=JPK.ref();
   SSTALE.AR:=OKRO_F.ROK;
   SSTALE.AO:=OKRO_F.ref();
   exec('open_tabele','open_tab');
   J_GV_POZ.use(exec('maska_poz','!fks_ved_dvgd'));
   exec('czytaj','#stalesys',,XINFO,'BLP_JPK');
   _file:=exec('gen_jpk','jpk');
   {? _file<>''
   || exec('upd_jpk','jpk');
      JPK.bl_put('PLIK',_file,1);
      {? JPK.put()
      || jpkOK+=1
      ?};
      ferase(_file,1)
   ?}
?};
JPK.cntx_pop();
{? _jpk || JPK.seek(_jpk) ?};
1


:Sign Version 2.0 jowisz:1045 2023/09/07 12:13:11 84a81ac1872c359a3fba19c8642f168a95808bb14fce6e8f27ca7ed2174149cf6298f69108ac87a4984012b0d96d0a8b57e1dc7eec5cc07e8028a36c97e5b8762987548f3959056b04d0fdc1e1e5f7550211ab728fec8517fc0b52e0806f44764a5237f800e63da584970720f78db35f82fc1617f0372e8088623901e9ccc0fb

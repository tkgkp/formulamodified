:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_tec_drte.fml
:: Utworzony: 20.02.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_TEC_DRTE - Redagowanie karty technologicznej
::            Uwaga: większość kodu jest wspólna z czynnością TTE_WTE_DRTE - w przypadku zmian modyfikować oba pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności rejestrowania karty technologicznej (TTE_TEC_DRTE)
::       Zadaniem formuły jest zredagowanie nagłówka karty technologicznej
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_a:=params_get().in;
_b:=params_get().int;
_c:=params_get().out;
_d:=params_get().mp;
_e:=params_get().context;

::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TPKTL, type=_TPKTL, name=Typ karty technologicznej, required=N, fml_val="exec('tpktl_select','tech_head',_a)", fml_exp="exec('tpktl_export','tech_head',_a)"
{? var_pres('TPKTL',_a)<>type_of(~~) & var_pres('TPKTL',_a)<>type_of(null()) || return() ?};
{? var_pres('TPKTL',_a)=type_of(~~) || _a.TPKTL:=null() ?};
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na kartę technologiczną, required=N, keyref=T
{? var_pres('TKTL',_a)<>type_of(~~) & var_pres('TKTL',_a)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_a)=type_of(~~) || _a.TKTL:=null() ?};
::# kind=WE, symbol=M, type=_M, name=Produkt, required=N
{? var_pres('M',_a)<>type_of(~~) & var_pres('M',_a)<>type_of(null()) || return() ?};
{? var_pres('M',_a)=type_of(~~) || _a.M:=null() ?};
::# kind=WE, symbol=WZOR, type=_TKTL, name=Wzorzec technologii, required=N
{? var_pres('WZOR',_a)<>type_of(~~) & var_pres('WZOR',_a)<>type_of(null()) || return() ?};
{? var_pres('WZOR',_a)=type_of(~~) || _a.WZOR:=null() ?};
::# kind=WE, symbol=DEFAULT, type=STRING, name=Czy ustawić kartę jako domyślną?, required=N,fml_val="exec('edit_boolean','#edit','N','Czy ustawić kartę jako domyślną?'@)"
{? var_pres('DEFAULT',_a)<>type_of(~~) & var_pres('DEFAULT',_a)<>type_of('') || return() ?};
{? var_pres('DEFAULT',_a)=type_of(~~) || _a.DEFAULT:='' ?};

:: PARAMETRY WY:
::# kind=WY, symbol=TKTL, type=_TKTL, name=Wskazanie na kartę technologiczną, required=N
::# kind=WY, symbol=OPER, type=STRING, name=Redagowanie listy operacji, required=N
{? var_pres('TKTL',_c)<>type_of(~~) & var_pres('TKTL',_c)<>type_of(null()) || return() ?};
{? var_pres('OPER',_c)<>type_of('') || _c.OPER:='N' ?};

exec('tech_red','!tte_tec_drte',_a,_b,_c,_d,_e)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności rejestrowania karty technologicznej (TTE_TEC_DRTE)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

_desc:='';
_keyRefs:=_mp.getRefs();

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"{? NRK*'dodawana'>0 || ' ' || NRK+' ||| '+WER ?}",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' ||
            {? _tmp<>' ' || 'Zredaguj kartę technologiczną %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]]
            || 'Zredaguj nową kartę technologiczną'@@
            ?}
          || ''
          ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,
::      "{? NRK*'dodawana'>0
::       || 'Zredaguj nową kartę technologiczną'@@
::       || 'Zredaguj kartę technologiczną %1 wer. %2'@@[NRK,WER]
::       ?}
::   ",'')

:: jest parametr wejściowy M to ustawiam odpowiedniego M
|? var_pres('M',_in)
|| _tmp:=exec('FindAndGet','#table',M,_in.M,,"KTM",'');
   _desc:={? _tmp<>'' || 'Zredaguj kartę technologiczną dla produktu %1'@@[_tmp] || '' ?}
::|| _desc:=exec('FindAndGet','#table',M,_in.M,,"'Zredaguj kartę technologiczną dla produktu %1'[KTM]",'')

|| _desc:='Zredaguj nową kartę technologiczną'@@

?};
_desc


\tech_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła rejestrowania technologii
::       Używana przez exec('main','!tte_tec_drte')
::   WE: _a - [obj_new] - parametry wejściowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::       _e - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_out:=_c;
_mp:=_d;
_context:=_e;

_var_torw:=VAR.A_TORW;
_var_arch:=VAR.ARCH;

_torw:=VAR.A_TORW:='T';
_arch:=VAR.ARCH:='N';

_one:=_mp.isMicro();
_czykey:=0;
_tktl:=null();
_act:=_mp.akcja();
_keyRefs:=_mp.getRefs();
_path:=_mp.path();
_dialog:=1;
{? _mp.isAutoRun()
|| _dialog:=0
?};

_can_continue:=1;
_clean_result:=params_exec('clean','!tte_tec_drte',_mp,_in);
_can_continue:=_clean_result.RESULT;
_tktl:=_clean_result.TKTL;

:: Sprawdzenie typu karty technologicznej
{? _can_continue>0 & _in.TPKTL
|| {? exec('FindAndGet','#table',TPKTL,_in.TPKTL,,"1+TYP",'')='~'
   || _msg:='Niezgodność wywołania czynności.\nPrzekazany typ nie może być zastrzeżony.';
      FUN.emsg(_msg);
      _mp.error(_msg);
      _can_continue:=0
   ?}
?};

:: Sprawdzenie wzorca technologii
{? _can_continue>0 & _in.WZOR
|| {? exec('FindAndGet','#table',TKTL,_in.WZOR,,"TORW",'')<>'W'
   || _msg:='Niezgodność wywołania czynności.\nPrzekazana technologia nie jest wzorcem.';
      FUN.emsg(_msg);
      _mp.error(_msg);
      _can_continue:=0
   ?}
?};

{? _can_continue>0 & _in.TKTL & _in.WZOR
|| _msg:='Niezgodność wywołania czynności.\nNie można przekazać jednocześnie wzorca oraz karty technologicznej.';
   FUN.emsg(_msg);
   _mp.error(_msg);
   _can_continue:=0
?};

_material:=_in.M;
{? _can_continue>0 & _material=null() & _dialog=0
|| _msg:='Niezgodność wywołania czynności.\n'
         'Materiał w trybie bezdialogowym musi być przekazany jako parametr wejściowy.';
   FUN.emsg(_msg);
   _mp.error(_msg);
   _can_continue:=0
?};

{? _can_continue>0 & _material<>null()
|| _rodz:=exec('FindAndGet','#table',M,_in.M,,"R",'');
   _akt:=exec('FindAndGet','#table',M,_in.M,,"A",'');
   {? _rodz='S' | _rodz='H' | (_rodz='' & exec('get','#params',500742,2)='N')
   || {? _dialog=0
      || _msg:='Karta technologiczna nie została wygenerowana dla '+
               {? _rodz='S' || 'surowca' |? _rodz='H' || 'wyrobu zewnętrznego' || 'usługi'?}+'.'
      || _msg:='Niezgodność wywołania czynności.\nNie można redagować karty dla '+
               {? _rodz='S' || 'surowca' |? _rodz='H' || 'wyrobu zewnętrznego' || 'usługi'?}+'.'
      ?};
      FUN.emsg(_msg);
      _mp.error(_msg);
      _can_continue:=0
   ?};
   {? _akt<>'T'
   || {? _dialog=0
      || _msg:='Karta technologiczna nie została wygenerowana dla nieaktywnego materiału.'
      || _msg:='Nie można redagować karty dla nieaktywnego materiału.'
      ?};
      FUN.emsg(_msg);
      _mp.error(_msg);
      _can_continue>0
   ?}
?};

{? _can_continue>0 & _tktl=null()
|| {? _act='KOPIUJ'
   || _tktl:=_context.TKTL
   |? (';POPRAW;USUŃ;ZAKOŃCZ;ZAKOŃCZ_BEZ_PYTANIA'*_act)>1 & _in.TKTL=null()
   || _tktl:=TKTL.ref()
   || _tktl:=_in.TKTL
   ?}
?};

{? _can_continue>0 & _tktl & _in.TPKTL & exec('FindAndGet','#table',TKTL,_tktl,,"TYP")<>exec('FindAndGet','#table',TPKTL,_in.TPKTL)
|| _msg:='Niezgodność wywołania czynności.\nTyp karty musi być zgodny z typem przekazanym w procesie.';
   FUN.emsg(_msg);
   _mp.error(_msg);
   _can_continue:=0
?};

{? _can_continue>0 & _dialog=0 & _in.TPKTL=null() & _in.WZOR=null()
|| _msg:='Niezgodność wywołania czynności.\n'
         'Typ karty lub wzorzec technologii w trybie bezdialogowym musi być przekazany jako parametr wejściowy.';
   FUN.emsg(_msg);
   _mp.error(_msg);
   _can_continue:=0
?};

{? _can_continue>0 & _act='DOŁĄCZ' & exec('FindAndGet','#table',TKTL,_tktl,,"ARCH='T'",0)
|| _msg:='Niezgodność wywołania czynności.\nNie można redagować archiwalnej karty.';
   FUN.emsg(_msg);
   _mp.error(_msg);
   _can_continue:=0
?};

{? _can_continue>0
||
   _mp.trigRef('TKTL',1,1);
::FUN.info(_mp.Path);
:: z listy ToDo - znany keyref i stoję na liście zadań - poprawianie nagłówka gdy możliwe
   {? _path='Todo' & _tktl<>null()
   || TKTL.cntx_psh();
      TKTL.clear();
      {? TKTL.seek(_tktl)
      || {? TKTL.STAT_N='T'
         || FUN.info('Zakończono rejestrację nagłówka karty.'@);
            _out.TKTL:=_tktl;
            _out.OPER:=TKTL.TYP().OPER;
            _mp.save(,_out);
            _mp.done()
         || {? exec('tktl_pop','tech_head',TKTL.ref(),_material,,_in.TPKTL)
            || TKTL.get();
               _out.TKTL:=_tktl;
               _out.OPER:=TKTL.TYP().OPER;
               _mp.save(,_out);
               {? TKTL.STAT_N='T'
               || _mp.done()
               ?}
            ?}
         ?}
      || _mp.error('Brak karty o refie '+$_tktl+'.');
         _can_continue:=0
      ?};
      TKTL.cntx_pop()

:: nie ma keyref, wywołanie z panelu czynności - powołanie nowej karty
   |? _path='Proc'
   || exec('tktl_use','tech_common');
      _tktl:=exec('tktl_add','tech_head',_material,_in.TPKTL,_in.WZOR);
      {? _tktl<>null()
      || _out.TKTL:=_tktl;
         _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
         _mp.save(,_out);
         {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
         || _mp.done()
         ?}
      ||
         _mp.cancel()
      ?}
   |? _dialog=0
   ||
      exec('tktl_use','tech_common');
      _tktl:=exec('tktl_add_nodialog','tech_head',_material,_in.TPKTL,_in.WZOR);
      {? _tktl<>null()
      || _out.TKTL:=_tktl;
         _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
         _mp.save(,_out);
         {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
         || _mp.done()
         ?}
      ||
         _mp.cancel()
      ?}
:: nie ma keyref, wywołanie z listy ToDo
   |? _path='Todo'
   ||
      exec('tktl_use','tech_common');
      _tktl:=exec('tktl_add','tech_head',_material,_in.TPKTL,_in.WZOR);
      {? _tktl<>null()
      || _out.TKTL:=_tktl;
         _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
         _mp.save(,_out);
         {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
         || _mp.done()
         ?}
      || _mp.cancel()
      ?}

:: Dołączanie nowej karty
   |? _act='DOŁĄCZ' | _act='DOŁĄCZ_WG_WZORCA'
   || {? _act='DOŁĄCZ'
      || _tktl:=exec('tktl_add','tech_head',_material,_in.TPKTL)
      || _tktl:=exec('tktl_add','tech_head',_material,,_in.WZOR)
      ?};
      {? _tktl<>null()
      || _out.TKTL:=_tktl;
         _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
         _mp.save(,_out);
         {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
         || _mp.done()
         ?}
      || _mp.cancel()
      ?}

:: Kopiowanie na nową kartę
   |? _act='KOPIUJ'
   || exec('tktl_cntx_psh','tech_common');
      exec('tktl_use','tech_common',ref_name(_tktl)+3);
      _args_copy:=exec('args_copy','tech_arch');
      {? exec('tktl_copy_act','tech_arch',_tktl,_args_copy)>0
      || _tktl:=_args_copy.TKTL_DST;
         {? _tktl<>null()
         || _out.TKTL:=_tktl;
            _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
            _mp.save(,_out);
            {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
            || _mp.done()
            ?}
         || _mp.cancel()
         ?}
      ?};
      exec('tktl_cntx_pop','tech_common')

:: Poprawianie karty
   |? _act='POPRAW'
   || {? exec('tktl_pop','tech_head',_tktl,_material,,_in.TPKTL)
      || _out.TKTL:=_tktl;
         _out.OPER:=exec('FindAndGet','#table',TKTL,$_tktl,,"TYP().OPER",'N');
         _mp.save(,_out);
         {? exec('FindAndGet','#table',TKTL,$_tktl,,"STAT_N",'N')='T'
         || _mp.done()
         ?}
      ?}

:: Usuwanie karty
   |? _act='USUŃ'
   || {? exec('TKTL_usun','tech_head',_tktl)
      || _out.TKTL:=null();
         {? _one
         || _mp.save(,_out);
            _mp.done()
         || _mp.error('Nie można kontynuować procesu — karta została usunięta.');
            _can_continue:=0
         ?}
      ?}

:: Zakończenie redagowania nagłówka
   |? _act='ZAKOŃCZ'
   || TKTL.cntx_psh();
      TKTL.clear();
      {? TKTL.seek(_tktl)
      || _chk:=exec('tktl_chk','tech_head',1);
         {? _chk
         || {? TKTL.IMP_ERR='T'
            || FUN.emsg('Karta została zaimportowana z błędami. Nie można zakończyć rejestracji nagłówka takiej karty.'@)
            || {? FUN.ask('Czy zakończyć rejestrację nagłówka karty technologicznej?'@)
               || TKTL.STAT_N:='T';
                  exec('stat_act','tech_head');
                  {? TKTL.put()
                  || _mp.save(,_out);
                     _mp.done()
                  ?}
               ?}
            ?}
         ?}
      || _mp.cancel()
      ?};
      TKTL.cntx_pop()

:: Zakończenie redagowania nagłówka (bez pytania)
   |? _act='ZAKOŃCZ_BEZ_PYTANIA'
   || TKTL.cntx_psh();
      TKTL.clear();
      {? TKTL.seek(_tktl)
      || TKTL.STAT_N:='T';
         exec('stat_act','tech_head');
         {? TKTL.put()
         || _mp.save(,_out);
            _mp.done()
         ?}
      || _mp.cancel()
      ?};
      TKTL.cntx_pop()

:: Tutaj nie powinno dojść, więc błąd
   || _mp.error('Nieobsłużony kontekst wywołania czynności.');
      _can_continue:=0
   ?}
?};

:: Dodanie (domyślnego) produktu do karty - tylko dla opcji automatycznej (bezdialogowej)
{? _can_continue>0 & type_of(_out.TKTL)>0 & _out.TKTL<>null() & _dialog=0 & _material<>null()
|| _old:=VAR.A_KTL;
   VAR.A_KTL:=_out.TKTL;
   _can_continue:=exec('tktlw_add','tech_prod',_in.DEFAULT='T');
   {? _can_continue=0
   || _msg:='Nie udało się ustalić '+{? _in.DEFAULT='T' || 'domyślnego ' || '' ?}+'produktu dla karty.';
      FUN.emsg(_msg);
      _mp.error(_msg)
   ?};
   VAR.A_KTL:=_old
?};

VAR.A_TORW:=_var_torw;
VAR.ARCH:=_var_arch;
~~


\action_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja karty technologicznej - akcja Dołącz
::   WE: [_a] - miejsce uruchomienia: [0] - obszar Technologii, 1 - Kartoteka materiałowa
::----------------------------------------------------------------------------------------------------------------------
_where:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_TEC_DRTE';
_args.AKCJA:='DOŁĄCZ';
_args.PROC_START:='T';
{? _where=1
|| _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'M',M.ref())
?};
exec('mp_run','#b__box',_args);

:: Odświeżenie listy technologii dla materiału
{? _where=1
|| TKTL.f_clear(1);
   TKTLW.f_clear(1);
   _ref:=TKTLW.ref();
   exec('tech_tow','material_prod',0);
   TKTLW.seek(_ref)
?}


\action_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja karty technologicznej - akcja kOpiuj
::----------------------------------------------------------------------------------------------------------------------
exec('tktl_cntx_psh','tech_common');

_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_TEC_DRTE';
_args.AKCJA:='KOPIUJ';
_args.PROC_START:='T';
_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
_args.CONTEXT:=obj_new('TKTL'); _args.CONTEXT.TKTL:=TKTL.ref();

::exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TPKTL',TKTL.TYP);

exec('mp_run','#b__box',_args);

exec('tktl_cntx_pop','tech_common')


\action_copy_4card_zl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Rejestracja karty technologicznej - akcja kOpiuj z poziomu zlecenia (jego karty technologicznej)
::----------------------------------------------------------------------------------------------------------------------
_tktl:=exec('FindAndGet','#table',TKTL,ZL.RTKTL,,"ref()",null());

{? _tktl<>null()
|| exec('tktl_cntx_psh','tech_common');

   _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_TEC_DRTE';
   _args.AKCJA:='KOPIUJ';
   _args.PROC_START:='T';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   _args.CONTEXT:=obj_new('TKTL'); _args.CONTEXT.TKTL:=_tktl;

   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TPKTL',exec('FindAndGet','#table',TKTL,_tktl,,"TYP",null()));

   exec('mp_run','#b__box',_args);

   exec('tktl_cntx_pop','tech_common')

|| FUN.info('Nie można utworzyć kopii karty — zlecenie nie ma własnej technologii.'@)
?};
~~


\action_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja karty technologicznej - akcja Popraw
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRP_MOD='' ||  VAR.GRP_MOD:='N' ?};
{? TKTL.STAT_N<>'T' & VAR.GRP_MOD='N'
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_TEC_DRTE';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='POPRAW';
   _args.PROC_START:='N';
   exec('mp_run','#b__box',_args)
|| exec('tktl_pop','tech_head',TKTL.ref())
?};
{? VAR.GRUPA<>'T'
|| _tab:=cur_tab(1,1);
   {? _tab=TKTL & _tab.sel_size()=0
   || {? _tab.f_active()
      || _tab.f_next();
         _ref:=_tab.ref();
         _tab.f_rfresh();
         {? ~_tab.f_seek(VAR.A_KTL) || _tab.f_seek(_ref)  ?}
      ?}
   ?}
?}


\action_modify_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Grupa przed dla akcji Popraw w oknach wertowania dla nagłówków kart technologicznych
::----------------------------------------------------------------------------------------------------------------------
_continue:=1;
TKTL.cntx_psh();
{? TKTL.sel_size()>1
|| _continue:=FUN.ask('Ilość zaznaczonych technologii: %1. Czy poprawić wybrane karty technologiczne?'@
      [form(TKTL.sel_size())]);
   {? _continue>0
   ||
      TKTL.seek(null(),,,1);
      TKTL.blank(1);
      VAR.GRP_MOD:='P';
      {? _continue>0
      || _buffer:=exec('obj_new','#buf','TKTL');
         _buffer.get();
         params_set('buffer',_buffer);
         TKTL.win_edit('GRP_POP');
         _valid:="
            _res:=1;
            params_set(params_get());
            _buffer:=exec('obj_new','#buf','TKTL');
            _buffer.get();
            {? exec('compare','#table',_buffer,params_get().buffer,0,'TERM_OD','TERM_DO','GKTL')
            || FUN.info('Przynajmniej jedno z pól musi zostać określone.'@);
               _res:=0
            ?};
            _res
         ";
         _continue:=TKTL.edit(_valid);
         {? _continue>0
         || _buffer_grp:=exec('obj_new','#buf','TKTL');
            _buffer_grp.get();
            {? var_pres('__BUFF_GRP')>100
            || VAR_DEL.delete('__BUFF_GRP')
            ?};
            __BUFF_GRP:=_buffer_grp;
            sel_nchk();
            KOMM.init(255,,'Grupowa modyfikacja kart technologicznych'@);
            VAR.GRP_MOD:='T';
            VAR.GRUPA:='T'
         || VAR.GRP_MOD:='N';
            VAR.GRUPA:='N';
            {? var_pres('__BUFF_GRP')>100
            || VAR_DEL.delete('__BUFF_GRP')
            ?}
         ?}
      ?}
   ?}
?};
TKTL.cntx_pop();
_continue


\action_modify_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Grupa po dla akcji Popraw w oknach wertowania dla nagłówków kart technologicznych
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRP_MOD='T'
|| VAR.GRUPA:='N';
   VAR.GRP_MOD:='N';
   {? var_pres('__BUFF_GRP')>100
   || VAR_DEL.delete('__BUFF_GRP')
   ?};
   KOMM.select()
?};
:: Odświeżenie filtru na TKTL (jeżeli poprawione rekordy wypadły z aktywnej dziedziny)
_tab:=cur_tab(1,1);
{? _tab=TKTL
|| {? _tab.f_active()
   || _tab.f_rfresh()
   ?}
?};
~~


\action_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja karty technologicznej - akcja Usuń
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N<>'T'
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_TEC_DRTE';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='USUŃ';
   exec('mp_run','#b__box',_args)
|| {? TKTL.sel_size()>0
   || KOMM.add('Zakończono rejestrację nagłówka karty %1 wer. %2 — modyfikacje niemożliwe.'@[TKTL.NRK,TKTL.WER])
   || FUN.info('Zakończono rejestrację nagłówka karty.\nModyfikacje niemożliwe.'@)
   ?}
?}


\action_delete_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja karty technologicznej - akcja Usuń przed grupą rekordów
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Ilość zaznaczonych kart technologicznych: %1. Czy usuwać?'@[form(TKTL.sel_size())])
|| VAR.GRUPA:='T';
   KOMM.init(,,'Usuwanie kart technologicznych'@,'');
   1
|| 0
?}


\action_delete_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja karty technologicznej - akcja Usuń po grupie rekordów
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
KOMM.select();
1


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestracja karty technologicznej - akcja Zakończ
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_TEC_DRTE';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='ZAKOŃCZ';
   exec('mp_run','#b__box',_args)
|| FUN.info('Zakończono rejestrację nagłówka karty.\nModyfikacje niemożliwe.'@)
?};
'key:Esc'


\tktl_dalej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obsługa przycisku 'Zakończ' w oknie redagowania nagłówka karty technologicznej
::       Zatwierdza nagłówek i popycha proces dalej
::----------------------------------------------------------------------------------------------------------------------
TKTL.STAT_N:='T';
'key:F2'


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('tech_clean','tech_common',_mp,_in)


\action_add_from_temp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Rejestracja karty technologicznej - akcja Dołącz wg wzorca
::   WE: [_a] - miejsce uruchomienia: [0] - obszar Technologii, 1 - Kartoteka materiałowa
::----------------------------------------------------------------------------------------------------------------------
_where:={? var_pres('_a')=type_of(0) || _a || 0 ?};

_torw:=VAR.A_TORW;
_arch:=VAR.ARCH;
_tktl:=null();
VAR.A_TORW:='W';
VAR.ARCH:='N';
:: Selekcja wzorców
TKTL.cntx_psh();
TKTL.f_clear(1);
exec('set_filter','tech_head');
TKTL.win_sel('SLO_');
TKTL.win_patt('WZOR_');
{? TKTL.select()
|| _tktl:=TKTL.ref()
?};
TKTL.cntx_pop();
VAR.A_TORW:=_torw;
VAR.ARCH:=_arch;
{? _tktl<>null()
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_TEC_DRTE';
   _args.AKCJA:='DOŁĄCZ_WG_WZORCA';
   _args.PROC_START:='T';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'WZOR',_tktl);
   {? _where=1
   ||  exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'M',M.ref())
   ?};
   exec('mp_run','#b__box',_args)
?};
{? _where=0
||
:: Odtworzenie filtra na technologii
   {? VAR.A_TABKTL='W'
   || exec('set_wfilter','tech_head')
   |? VAR.A_TABKTL='N'
   || exec('set_nfilter','tech_head')
   || exec('set_filter','tech_head')
   ?}
|? _where=1
||
:: Odświeżenie listy technologii dla materiału
   TKTL.f_clear(1);
   TKTLW.f_clear(1);
   _ref:=TKTLW.ref();
   exec('tech_tow','material_prod',0);
   TKTLW.seek(_ref)
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 df50f6472f4ab7fbf9e7ce9ccf43e3130e59e8c70c2486700359674d96bc38b14e18ae2fcae323bb611af3e37261fbbe4c401cb45de6f3fc4e4ecc39c637700c37553e1c6a07540aeaf83a458fb556083615aaa330c5a8b572026999e9882de2cc0db8048d687b24f7f57df9ac19b25e443f1a2d00531d5331f61fb50917a7e9

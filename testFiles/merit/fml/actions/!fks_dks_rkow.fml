:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_rkow.fml
:: Utworzony: 04.08.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_RKOW - Wykonanie rozliczeń kosztów
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wykonywanie rozliczeń kosztów - główna formuła czynności FKS_DKS_RKOW.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
::
:: PARAMETRY WE:
::# kind=WE, symbol=ROZ_NAZ, type=_ROZ_NAZ, name=Wskazanie na definicję rozliczenia kosztów, required=N
::# kind=WE, symbol=ODD, type=_ODD, name=Wskazanie na jednostkę księgową, required=N
::# kind=WE, symbol=ALL, type=NUMBER, name=Grupowe tworzenie wykonań , required=N, fml_val="exec('czy_grup','!fks_dks_rkow')"
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WE, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N

:: PARAMETRY WY:
::# kind=WY, symbol=ROW_NAG, type=_ROW_NAG, name=Wskazanie na wykonania rozliczenia kosztów, required=N
::# kind=WY, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N

:: WARUNKI BRAMY:
::# condition=Kontynuacja pętli, act_uid=FKS_DKS_RKOW, auto=T, formula=_a.UNIK_ID<>''
::# condition=Dekretacja rozliczenia, act_uid=FKS_DKS_RKOX, auto=T, formula=_a.ROW_NAG<>null

_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
{? var_pres('UNIK_ID',_we)>0 & _we.UNIK_ID<>''
|| params_exec('par_out','!fks_dks_rkow',_we.UNIK_ID,_mp,_wy);
   _mp.done()
|| {? ~var_pres('ALL',_we) || _we.ALL:=0; _mp.save(,_we) ?};
   {? ~var_pres('OKRO_F',_we) || _we.OKRO_F:=SSTALE.AO; _mp.save(,_we) ?};
   {? ~var_pres('ODD',_we) || _we.ODD:=OPERATOR.DEPT; _mp.save(,_we) ?};
   {? ~var_pres('ROZ_NAZ',_we) || _we.ROZ_NAZ:=null; _mp.save(,_we) ?};
   _ao:=SSTALE.AO; _ar:=SSTALE.AR; _dept:=OPERATOR.DEPT;
   _dalej:=1;
   ROK_F.cntx_psh(); ROK_F.prefix();
   OKRO_F.cntx_psh(); OKRO_F.prefix();
   ROZ_NAZ.cntx_psh(); ROW_NAG.cntx_psh(); ODD.cntx_psh();
   {? ~OKRO_F.seek(_we.OKRO_F)
   || FUN.info('Nie podano parametru - okres wejściowy.'@);
      _dalej:=0
   ?};
   {? _dalej & OKRO_F.POCZ=date(0,0,0)
   || FUN.info('Wykonań kosztów nie można dokonywać w okresach "Bilans otwarcia" i "Bilans zamknięcia".'@);
      _dalej:=0
   ?};
   {? _dalej
   || SSTALE.AO:=OKRO_F.ref(); SSTALE.AR:=OKRO_F.ROK; OPERATOR.DEPT:=_we.ODD;
      OKRO_F.ROK(); OPERATOR.DEPT();
      ROZ_NAZ.index('AKC'); ROZ_NAZ.prefix(SSTALE.AR,'T');
      {? ~ROZ_NAZ.first()
      || FUN.info('Brak zaakceptowanych definicji rozliczeń kosztów pośrednich.'@);
         _dalej:=0; _mp.done()
      ?}
   ?};
   {? _dalej
   || idwykpar:=tm_stamp();
      menu_txt(,'Dołącz');
      {? _we.ALL
      || ROW_NAG.win_edit('DOL_PRA');
         ROW_NAG.blank(); ROW_NAG.ODD:=_we.ODD;
         exec('odd_filtr','fst');
         {? ROW_NAG.edit("exec('row_spr','!fks_dks_rkow',1)")
         || _odd:=ROW_NAG.ODD; _dat:=ROW_NAG.DATA;
            ROZ_NAZ.index('AKC'); ROZ_NAZ.prefix(SSTALE.AR,'T');
            {? ROZ_NAZ.first()
            || {! |?
                  {? _odd
                  || ODD.prefix();
                     {? ODD.seek(_odd)
                     || ROW_NAG.blank(); ROW_NAG.ODD:=ODD.ref(); ROW_NAG.DATA:=_dat;
                        exec('wyk_roz1','!fks_dks_rkow',_args)
                     ?}
                  || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
                     {? ODD.first()
                     || {! |?
                           ROW_NAG.blank(); ROW_NAG.ODD:=ODD.ref(); ROW_NAG.DATA:=_dat;
                           exec('wyk_roz1','!fks_dks_rkow',_args);
                           ODD.next()
                        !}
                     ?}
                  ?};
                  ROZ_NAZ.next()
               !}
            ?};
            _mp.done()
         || _mp.cancel()
         ?}
      || ROW_NAG.win_edit('DOL_PR');
         ROW_NAG.blank(); SIK.AKC:='T';
         odd:=ROW_NAG.ODD; dat:=ROW_NAG.DATA;
         exec('odd_filtr','fst');
         {? ROW_NAG.edit("exec('row_spr','!fks_dks_rkow',0)")
         || ROW_NAG.ROZ_NAZ();
            ROW_NAG.ODD();
            ROW_NAG.OKRES();
            exec('wyk_roz1','!fks_dks_rkow',_args)
         || _mp.cancel()
         ?};
         VAR_DEL.delete('odd','dat')
      ?};
      ROW_NAG.win_edit('RED');
      params_exec('par_out','!fks_dks_rkow',idwykpar,_mp,_wy);
      VAR_DEL.delete('idwykpar')
   || params_exec('par_out','!fks_dks_rkow','',_mp,_wy)
   ?};
   ROZ_NAZ.cntx_pop(); ROW_NAG.cntx_pop(); ROK_F.cntx_pop(); OKRO_F.cntx_pop(); ODD.cntx_pop();
   SSTALE.AO:=_ao; SSTALE.AR:=_ar; OPERATOR.DEPT:=_dept
?};
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zakończ rozliczenie kosztów pośrednich'@@;
_desc


\par_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych czynności
::   WE: _a - UNIK_ID
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_wy:=_args.out;
ROW_NAG.cntx_psh(); ROW_NAG.index('TM_STAMP'); ROW_NAG.prefix(_a+'1',);
{? ROW_NAG.first()
|| _wy.ROW_NAG:=ROW_NAG.ref();
   _wy.UNIK_ID:={? ROW_NAG.size()>1 || _a || _wy.UNIK_ID:='' ?};
   ROW_NAG.TM_STAMP:=(ROW_NAG.TM_STAMP-1)+'2';
   ROW_NAG.cntx_psh(); ROW_NAG.prefix(); ROW_NAG.put(); ROW_NAG.cntx_pop()
|| _wy.UNIK_ID:=''; _wy.ROW_NAG:=null
?};
_mp.save(,_wy);
ROW_NAG.cntx_pop()


\wyk_roz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Tworzenie pojedynczego wykonania rozliczenia
::  OLD: \wyk_roz1/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_kom_pok:=~_a.in.ALL;
ROW_NAG.ROZ_NAZ:=ROZ_NAZ.ref();
ROW_NAG.TM_STAMP:=idwykpar+'1';
{? ROW_NAG.add(1)
|| ROK_F.cntx_psh(); OKRO_F.cntx_psh();
   ROW_DOC.cntx_psh(); ROW_DOC.use('rowd'+ROW_NAG.OKRES().ROK().KOD+form(OKRO_F.NR,-2));
   ROW_KON.cntx_psh(); ROW_KON.use('rowk'+ROW_NAG.OKRES().ROK().KOD+form(OKRO_F.NR,-2));
   ROW_WYR.cntx_psh(); ROW_WYR.use('roww'+ROW_NAG.OKRES().ROK().KOD+form(OKRO_F.NR,-2));
   KOSZTY.NAZ_REF:=ROW_NAG.ROZ_NAZ;
   KOSZTY.ODD:=ROW_NAG.ODD;
   KOSZTY.WAL:=ROW_NAG.WAL;
   KOSZTY.WYK_REF:=ROW_NAG.ref;
   KOSZTY.WARTOSC:=KOSZTY.SUMAKL:=KOSZTY.SUMADC:=KOSZTY.SUMAKZ:=0;
   ROW_DOC.index('KONTO'); ROW_DOC.prefix(KOSZTY.WYK_REF);
   ROZ_KON.index('ROZ_KON'); ROZ_KON.prefix(KOSZTY.NAZ_REF,'Z');
   {? ROZ_KON.first()
   || _kom:='';
      KOSZTY.TYP:='Z';
      ROW_KON.index('KONTO'); ROW_KON.prefix(ROW_NAG.ref());
      {! |? exec('kont_zrd','!fks_dks_rkow'); ROZ_KON.next() !};
      _a.out.ROW_NAG:={? _kom_pok || ROW_NAG.ref() || null ?};
      _a.mp.save(,_a.out);
      {? KOSZTY.SUMAKZ$2<>0
      || {? ROZ_NAZ.TYP='K'
         || ROZ_KON.index('ROZ_KON'); ROZ_KON.prefix(KOSZTY.NAZ_REF,'K');
            KOSZTY.TYP:='K';
            {? ROZ_KON.first() || {! |? exec('kont_zrd','!fks_dks_rkow'); ROZ_KON.next() !} ?};
            {? KOSZTY.SUMAKL$2<>0
            || exec('obl_narz','!fks_dks_rkow')
            || _kom:='Zerowa wartość klucza.'
            ?}
         || exec('kont_doc','!fks_dks_rkow')
         ?}
      || _kom:='Zerowa wartość do rozliczenia.'
      ?};
      {? _kom<>''
      || exec('row_usun','!fks_dks_rkow',1);
         {? _kom_pok || _a.mp.cancel() ?}
      || {? _kom_pok || _a.mp.done() ?}
      ?};
      {? _kom<>'' & _kom_pok || FUN.info(_kom) ?}
   || {? _kom_pok
      || FUN.info('Brak kont definicji kont źródłowych.\nRozliczenie nie może zostać wykonane.'@);
         _a.mp.cancel()
      ?}
   ?};
   ROK_F.cntx_pop(); OKRO_F.cntx_pop();
   ROW_DOC.cntx_pop(); ROW_KON.cntx_pop(); ROW_WYR.cntx_pop()
|| {? _kom_pok
   || FUN.info('Istnieje już wykonanie dla tego rozliczenia.'@);
      _a.mp.cancel()
   ?}
?}


\row_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Sprawdzenie poprawnosci danych w okienku edycyjnym tabeli ROW_NAG
::   WE: _a - 1/0 - czy operacja grupowa
::  OLD: \row_spr/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~ROW_NAG.ODD & _a=0
|| FUN.info('Nie wybrano jednostki księgowej.'@); 'ODD'
|? ~ROW_NAG.ROZ_NAZ & _a=0
|| FUN.info('Nie wybrano definicji rozliczenia'@); 'ROZ_NAZ'
|? ROW_NAG.DATA<SSTALE.AO().POCZ | ROW_NAG.DATA>SSTALE.AO().KON
|| FUN.info('Należy wybrać datę z bieżącego okresu.'@); 'DATA'
|| 1
?}


\wyk_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Wywolanie tworzenia wykonan rozliczen
::  OLD: \wyk_rozl/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_DKS_RKOW';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\bl_rnag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Wartosc poczatkowa dla pola ROW_DOC.ROW_NAG
::  OLD: \bl_rnag/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_NAG.ref()


\data_wyk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wartość początkowa dla pola ROW_NAG.DATA
::----------------------------------------------------------------------------------------------------------------------
{? date()>=SSTALE.AO().POCZ & date()<=SSTALE.AO().KON
|| date()
|| SSTALE.AO().KON
?}


\bl_rkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Wartosc poczatkowa dla pola ROW_DOC.KONTODC
::  OLD: \bl_rkon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.KONTO


\ae_rdoc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula po redakcji pola ROW_DOC.KONTODC
::  OLD: \ae_rdoc/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=exec('ae_konto','edkonto',_a,'ROW_DOC','KONTODC',1,2,1,"exec('po_dkon','!fks_dks_rkow')");
_a


\po_dkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po redakcji pola ROW_DOC.KONTODC
::  OLD: \po_dkon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(35)='T'
|| SSTALE.AR();
   _s:=ROK_F.SYNT;
   _lp:=1;
   {? _s+fld<>_s+KOSZTY.KONTO
   || KS_W.cntx_psh();
      KS_W.index('LP');
      KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+fld));
      {? KS_W.first()
      || {!|? ($('ROW_DOC.WYR'+$KS_W.LP+':=null'))();
              ($('KOSZTY.SLUA'+$KS_W.LP+':=KS_W.SLU'))();
              ($('KOSZTY.NAG'+$KS_W.LP+':=KS_W.SLU().SLU().NAZ'))();
              _lp+=1;
              KS_W.next()
         !}
      ?};
      {? _lp<6
      || {! _i:=_lp..6 |!
           ($('ROW_DOC.WYR'+$_i+':=null'))();
           ($('KOSZTY.SLUA'+$_i+':=null'))();
           ($('KOSZTY.NAG'+$_i+':='''''))()
         !}
      ?};
      KOSZTY.KONTO:=fld;
      win_disp();
      KS_W.cntx_pop()
   ?}
?};
1


\bl_rstr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Wartosc poczatkowa dla pola ROW_DOC.STR
::  OLD: \bl_rstr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.STR


\pw_rwsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po redakcji pola KOSZTY.WSP
::  OLD: \pw_rwsp/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('ref_wd')>0 || ref_wd:=ROW_DOC.ref() ?};
ROW_DOC.WSP*=100; 1


\pr_rkwkl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed red. pola ROW_DOC.KWOTAKL
::  OLD: \pr_rkwkl/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_DOC.WSP$8=0


\pr_rkl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po redakcji pola ROW_DOC.KWOTAKL
::  OLD: \pr_rkl/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
fld(fld$6);
{? fld()=0
|| FUN.info('Należy wprowadzić wartość niezerową.'@); 0
|| 1
?}


\pr_rwyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed redakcja dla pol: ROW_DOC.WYR?
::  WE: _a - numer do akronimu pola ROW_DOC.WYR?
::  OLD: \pr_rwyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
($('KOSZTY.SLUA'+$_a+'().SLU().NAZ'))()<>''


\rec_kdoc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Rekord przed w okienku wertowania WERK tabeli ROW_DOC
::  OLD: \rec_kdoc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_WYR.index('ROW_WYR');
ROW_WYR.prefix(ROW_DOC.ref);
KS_W.index('LP');
KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROW_DOC.KONTODC));
{! _i:=1..6 |!
   {? KS_W.find_key(_i)
   || ($('KOSZTY.NAG'+$_i+':=KS_W.SLU().SLU().NAZ'))()
   || ($('KOSZTY.NAG'+$_i+':='''''))()
   ?};
   {? ROW_WYR.find_key(_i)
   || {? ROW_WYR.next()
      || {? ROW_WYR.LP=_i || ($('KOSZTY.DSLO'+$_i+':=''<- F3 ->'''))() || ($('KOSZTY.DSLO'+$_i+':=ROW_WYR.SLO().KOD'))() ?}
      || ($('KOSZTY.DSLO'+$_i+':=ROW_WYR.SLO().KOD'))()
      ?}
   || ($('KOSZTY.DSLO'+$_i+':='''''))()
   ?}
!}; ''


\wyr_dsel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja wyrozniki - w okienkach wertowania tabeli ROW_DOC
::  OLD: \wyr_dsel/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KS_W.index('LP'); KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROW_DOC.KONTODC));
{? var_pres('ref_wd')>0 || ref_wd:=ROW_DOC.ref() ?};
{? KS_W.first()
|| {? KS_W.size=1
   || exec('roz_rdc','!fks_dks_rkow')
   || {? var_pres('SEL_DKSW')<0
      || SEL_DKSW:=KS_W.mk_sel(,,,'ks_w_wer1');
         KS_W.win_fld(SEL_DKSW,,'LP');
         KS_W.win_fld(SEL_DKSW,SLU,'NAZ');
         KS_W.win_act(SEL_DKSW,,'Rekord',,,,"KS_W.SLU().SLU();
                                             ROW_WYR.index('ROW_WYRK'); ROW_WYR.prefix(ROW_DOC.ref());
                                             {? ROW_WYR.find_key(KS_W.LP) || exec('findtmp','#color') || ''?} ");
         KS_W.win_act(SEL_DKSW,,'Formuła','Wybierz'@@,,,"exec('roz_rdc','!fks_dks_rkow')",,1,,,,'W')
      ?};
      KS_W.win_sel(SEL_DKSW);
      {! |?
         {? KS_W.select(,1)
         || KS_W.SLU().SLU(); exec('roz_rdc','!fks_dks_rkow'); 1
         || 0
         ?}
      !}
   ?}
|| FUN.info('Nie zdefiniowano słowników wyróżników dla docelowego konta.'@); 0
?}


\roz_rdc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Wyswietlenie wyroznikow dla wybranego slownika - z poziomu tabeli ROW_DOC
::  OLD: \roz_rdc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KS_W.SLU().SLU();
ROW_WYR.index('ROW_WYRK'); ROW_WYR.prefix(ROW_DOC.ref,KS_W.LP);
{? ROW_WYR.first()
|| ROW_WYR.win_sel('WER');
   KOSZTY.SUMA_WYR:=0;
   {! |? KOSZTY.SUMA_WYR+=ROW_WYR.KW; ROW_WYR.next() !};
   ROW_WYR.select()
|| FUN.info('Nie wypełniono wyróżników.'@)
?}


\rec_kzrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Rekord przed w okienku WERK tabeli ROW_DOC
::  OLD: \rec_kzrd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_maska:={? KOSZTY.TYP='Z' || ROW_KON.KONTOZ || ROW_DOC.KONTODC ?};
KS_W.index('LP'); KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+_maska));
{! _i:=1..6
|! {? KS_W.find_key(_i)
   || ($('KOSZTY.NAG'+$_i+':=KS_W.SLU().SLU().NAZ'))()
   || ($('KOSZTY.NAG'+$_i+':='''''))()
   ?}
!};
''


\pr_rdol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja przed popraw, dolacz, usun w okienkach wertowania WERR? tabeli
::       ROW_DOC
::  OLD: \pr_rdol/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.POZ:={? -menu_txt='dołącz' || 0 || ROW_DOC.KWOTAKL ?};
{? ROW_DOC.WSP$8=0
||  1
|| {? -menu_txt()<>'popraw'
   || FUN.info('Należy wcześniej usunąć kwoty narzutów.'@); 0
   || 1
   ?}
?}


\po_rmod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po akcjach usun, popraw, dolacz w okienku WERR? tabeli ROW_DOC
::  OLD: \po_rmod/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt='usuń'
|| KOSZTY.SUMAKL-=KOSZTY.POZ;
   ROW_DOC.cntx_psh();
   ROW_DOC.index('KONTO');
   ROW_DOC.prefix(ROW_NAG.ref());
   {? ~ROW_DOC.first() ||  exec('zer_nag','dok_fks') ?};
   ROW_DOC.cntx_pop()
|? -menu_txt='popraw'
|| KOSZTY.SUMAKL:=KOSZTY.SUMAKL-KOSZTY.POZ+ROW_DOC.KWOTAKL
|| KOSZTY.SUMAKL+=ROW_DOC.KWOTAKL
?};
1


\rek_rdoc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Rekord przed w oknach wertowania WERR,WERR0 tabeli ROW_DOC
::  OLD: \rek_rdoc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
KOSZTY.PR_PROC:=ROW_DOC.WSP*100;
_lp:=1;
KS_W.index('LP'); KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROW_DOC.KONTODC));
{? KS_W.first()
|| {!|? ($('KOSZTY.SLUA'+$KS_W.LP+':=KS_W.SLU'))();
        ($('KOSZTY.NAG'+$KS_W.LP+':=KS_W.SLU().SLU().NAZ'))();
        _lp+=1;
        KS_W.next()
   !}
?};
{? _lp<6
|| {! _i:=_lp..6
   |! ($('KOSZTY.SLUA'+$_i+':=null'))();
      ($('KOSZTY.NAG'+$_i+':='''''))()
   !}
?}; ''


\rekprdoc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Rekord po w oknach wertowania WERR,WERR0 tabeli ROW_DOC
::  OLD: \rekprdoc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROW_DOC.KWOTAKL=0
|| FUN.info('Należy wprowadzić wartość niezerową.'@); 'KWOTAKL'
|| 1
?}


\obl_narz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja oblicz narzuty - w okienkach WERR? tabeli ROW_DOC
::  OLD: \obl_narz/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_sum:=0;
ROW_WYR.index('ROW_WYR');
ROW_DOC.cntx_psh();
ROW_DOC.index('KONTO');
ROW_DOC.prefix(ROW_NAG.ref());
KOSZTY.SUMADC:=0;
{? ROW_DOC.first()
|| {! |?
      _wsp:=ROW_DOC.KWOTAKL/KOSZTY.SUMAKL;
      ROW_DOC.WSP:=_wsp;
      ROW_DOC.KWOTA:=(_wsp*KOSZTY.SUMAKZ)$2;
      KOSZTY.SUMADC+=ROW_DOC.KWOTA;
      ROW_DOC.put();
      exec('obl_wyr','!fks_dks_rkow');
      ROW_DOC.next()
   !};
   {? KOSZTY.SUMADC$2<>KOSZTY.SUMAKZ$2
   || ROW_DOC.KWOTA+=(KOSZTY.SUMAKZ-KOSZTY.SUMADC); ROW_DOC.put(); KOSZTY.SUMADC:=KOSZTY.SUMAKZ;
      exec('obl_wyr','!fks_dks_rkow')
   ?}
?};
ROW_DOC.cntx_pop()


\obl_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Utworzenie wyroznikow dla kont docelowych rozliczen typu R
::  OLD: \obl_wyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_WYR.prefix(ROW_DOC.ref());
{? ROW_WYR.first()
|| _i:=0;
   {! |?
      {? _i<>ROW_WYR.LP || _i:=ROW_WYR.LP; _suma:=_pr:=0 ?};
      ROW_WYR.KW:=(ROW_WYR.PROC/100*ROW_DOC.KWOTA)$2;
      _suma+=ROW_WYR.KW; _pr+=ROW_WYR.PROC;
      {? _pr=100 & _suma<>ROW_DOC.KWOTA || ROW_WYR.KW-=(_suma-ROW_DOC.KWOTA) ?};
      ROW_WYR.put();
      ROW_WYR.next()
   !}
?}


\wyc_narz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja usun kwoty narzutow - w okienkach WERR? tabeli ROW_DOC
::  OLD: \wyc_narz/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_DOC.cntx_psh(); ROW_DOC.index('KONTO'); ROW_DOC.prefix(ROW_NAG.ref());
{? ROW_DOC.first()
|| {! |?
      ROW_DOC.WSP:=0;
      ROW_DOC.KWOTA:=0;
      ROW_DOC.put();
      ROW_DOC.next()
   !}
?};
ROW_DOC.cntx_pop();
KOSZTY.SUMADC:=0


\obl_zrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Akcja oblicz wart. zrodlowe - w okienkach WERR? tabeli ROW_DOC
::  OLD: \obl_zrd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć wartości dla kont źródłowych i ponownie je naliczyć?'@)
|| ROW_KON.index('KONTO'); ROW_KON.prefix(ROW_NAG.ref());
   {? ROW_KON.first() || {! |? ROW_KON.del() !} ?};
   ROZ_KON.index('ROZ_KON'); ROZ_KON.prefix(ROW_NAG.ROZ_NAZ,'Z');
   {? ROZ_KON.first()
   || KOSZTY.SUMAKZ:=0;
      {! |? exec('kont_zrd','!fks_dks_rkow'); ROZ_KON.next() !}
   ?}
?}


\kont_zrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Utworzenie pozycji wykonania na podstawie jednej pozycji
::       definicji rozliczenia
::  OLD: \kont_zrd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_maska:=ROZ_KON.MASKA;
{! |?_maska+1='?'|! _maska:=_maska-1 !};
_maska:=_maska+'%';
KOSZTY.KWRODZ:=ROZ_KON.FUN().TRESC;
KOSZTY.STRONA:=ROZ_KON.STR;
ROZ_WYR.index('ROZ_WYR');
ROZ_WYR.prefix(ROZ_KON.ref());
{? ROZ_WYR.first()
|| SSTALE.AR();
   KS_W.index('LP');
   KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROZ_KON.MASKA));
   _ok:=_lp:=1; _wyr:='';
   {!|? {? _ok & KS_W.find_key(ROZ_WYR.LP)
        || {? KS_W.ROZDZ='T' & ROZ_WYR.MASKA*'?'>0 || _lp:=KS_W.LP; _ok:=0
           |? ROZ_WYR.MASKA*'?'>0 || _lp:=KS_W.LP
           ?}
        ?};
        _ok & ROZ_WYR.prev()
   !};
   ROZ_WYR.find_key(_lp);
   {? exec('utw_zrd','!fks_dks_rkow',_maska)
   || _ZRD2:=sql('select KONTO, WYR1, WYR2, WYR3, WYR4, WYR5, WYR6, '+
                 'sum(WN) as WN, sum(MA) as MA from :_a'+
                 'group by KONTO, WYR1, WYR2, WYR3, WYR4, WYR5, WYR6',ZRD);
      {? _ZRD2.first()
      || {!|? KOSZTY.WARTOSC:=exec('exec_k','!fks_dks_rkow',_ZRD2.WN,_ZRD2.MA);
              {? KOSZTY.WARTOSC$2<>0
              || exec('dop_kzd','!fks_dks_rkow',_ZRD2.KONTO,_ZRD2.WYR1,_ZRD2.WYR2,_ZRD2.WYR3,
                      _ZRD2.WYR4,_ZRD2.WYR5,_ZRD2.WYR6)
              ?};
              _ZRD2.next()
           !}
      ?}
   ?};
   {? var_pres('ZRD')>0 || obj_del(ZRD) ?}
|| _ZRD:=sql('select AN.SYM as KONTO, sum(WN) as WN, sum(MA) as MA from OBR '+
             'join AN using (OBR.AN, AN.REFERENCE) '+
             'join ODD using (OBR.ODD, ODD.REFERENCE) '+
             'join OKRO_F using (OBR.OKRO, OKRO_F.REFERENCE) '+
             'join SLO using (AN.WAL, SLO.REFERENCE) '+
             'where AN.SYM like '''+STR.gsub(_maska, '?', '_')+''' AND '+
             'SLO.KOD='''+FINFO.NAROD().KOD+''' AND '+
             {? KOSZTY.KWRODZ='Bilans otwarcia' || 'OKRO_F.NR=0'
             |? KOSZTY.KWRODZ='Miesiąc' | KOSZTY.KWRODZ='Miesiąc-różn. stron' || 'OKRO_F.NR='+$SSTALE.AO().NR
                                                                              || 'OKRO_F.NR<='+$SSTALE.AO().NR
             ?}+
             ' AND ODD.OD='''+ROW_NAG.ODD().OD+''' '+
             ' group by AN.SYM');
   {? _ZRD.first()
   || {! |?
         KOSZTY.WARTOSC:=exec('exec_k','!fks_dks_rkow',_ZRD.WN,_ZRD.MA);
         {? KOSZTY.WARTOSC$2<>0 || exec('dop_kzd','!fks_dks_rkow',_ZRD.KONTO) ?};
         _ZRD.next()
      !}
   ?}
?}


\exec_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Obliczenie wartosci pozycji wykonania
::   WE: _a - wartosc obrotow Wn
::       _b - wartosc obrotow Ma
::  OLD: \exec_k/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.KWRODZ='Bilans otwarcia' | KOSZTY.KWRODZ='Miesiąc' | KOSZTY.KWRODZ='Obroty'
|| {? KOSZTY.STRONA='Wn' || _a || _b ?}
|? KOSZTY.KWRODZ='Saldo' | KOSZTY.KWRODZ='Per Saldo'
|| {? KOSZTY.STRONA='Wn' || F.SWn(_a,_b) || F.SMa(_a,_b) ?}
|? KOSZTY.KWRODZ='Różnica stron' | KOSZTY.KWRODZ='Miesiąc-różn. stron'
|| {? KOSZTY.STRONA='Wn' || _a-_b || _b-_a ?}
|| 1
?}


\utw_zrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Utworzenie tabeli tymczasowej z pozycjami wykonania - dla jednej pozycji
::       definicji rozliczenia
::   WE: _a - maska konta w definicji
::  OLD: \utw_zrd/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_wyr:='';
_lp:=ROZ_WYR.LP;
{! _i:=1..6 |! {? _i<>ROZ_WYR.LP || _wyr+=' ''        '' as WYR'+$_i+', ' ?} !};
{? KOSZTY.KWRODZ='Bilans otwarcia'
|| _p:=_k:=0
|? KOSZTY.KWRODZ='Miesiąc' | KOSZTY.KWRODZ='Miesiąc-różn. stron'
|| SSTALE.AO(); _p:=_k:=OKRO_F.NR
|| SSTALE.AO(); _p:=0; _k:=OKRO_F.NR
?};
_r:=ROK_F.KOD;
_d:='MASK_FILTER(DOK,';
_w:='MASK_FILTER(POW,';
_z:='MASK_FILTER(POZ,';
{! _i:=_p.._k |!
   _d+='''doku'+_r+form(_i,-2)+''',';
   _z+='''pozy'+_r+form(_i,-2)+''',';
   _w+='''pow_'+_r+form(_i,-2)+''','
!};
_m1:='/*+'+((_d-1)+')')+' '+((_z-1)+')')+' '+' */ ';
_m2:='/*+'+((_d-1)+')')+' '+((_w-1)+')')+' '+((_z-1)+')')+' */ ';
_czy_w:=ROZ_WYR.MASKA=(+ROZ_WYR.MASKA)*'?';
{? _czy_w
|| _ZRD:=sql('select '+_m1+' POZ.REFERENCE as REF, POZ.KON as KONTO, '+
         'case POZ.STR when ''Wn'' then POZ.SUM end as WN, '+
         'case POZ.STR when ''Ma'' then POZ.SUM end as MA '+
         ' from @POZ '+
         'join @DOK using (POZ.DOK, DOK.REFERENCE) '+
         'join ODD using (DOK.ODD, ODD.REFERENCE) '+
         'where POZ.ZP=''T'' AND POZ.KON like '''+STR.gsub(_a, '?', '_')+'''  '+
         ' AND ODD.OD='''+ROW_NAG.ODD().OD+''' '+
         ' and ODD.FIRMA=:_a',REF.FIRMA
      );
  _ZRD.index(_ZRD.ndx_tmp('',1,'REF',,0,'KONTO',,0))
?};
ZRD:=sql('select '+_m2+' POZ.REFERENCE as REF, POZ.KON as KONTO, '+
         'case POZ.STR when ''Wn'' then POW.KW end as WN, '+
         'case POZ.STR when ''Ma'' then POW.KW end as MA, '+
         'SLO.KOD as WYR'+$ROZ_WYR.LP+', '+{? _wyr<>'' || _wyr-2 || '' ?}+
         ' from @POW '+
         'join @POZ using (POW.POZ, POZ.REFERENCE) '+
         'join SLO using (POW.SLO, SLO.REFERENCE) '+
         'join @DOK using (POZ.DOK, DOK.REFERENCE) '+
         'join ODD using (DOK.ODD, ODD.REFERENCE) '+
         'where POZ.ZP=''T'' AND POZ.KON like '''+STR.gsub(_a, '?', '_')+''' AND '+
         'POW.LP='+$_lp+
         {? ROZ_WYR.MASKA<>'?'*(+ROZ_WYR.MASKA)
         || ' AND SLO.KOD like '''+STR.gsub(ROZ_WYR.MASKA, '?', '_')+''' '
         || ''
         ?}+
         ' AND ODD.OD='''+ROW_NAG.ODD().OD+''' '+
         ' and ODD.FIRMA=:_a',REF.FIRMA
      );
ZRD.index(ZRD.ndx_tmp('',1,'REF',,0,'KONTO',,0));
{? _czy_w
|| {? _ZRD.first()
   || {! |?
         ZRD.prefix(_ZRD.REF);
         {? ~ZRD.first()
         || ZRD.blank();     ZRD.REF:=_ZRD.REF; ZRD.KONTO:=_ZRD.KONTO;
            ZRD.WN:=_ZRD.WN; ZRD.MA:=_ZRD.MA;   ZRD.add()
         || _w:=_m:=0;
            {! |? _w+=ZRD.WN$2; _m+=ZRD.MA$2; ZRD.next() !};
            {? _w$2<>_ZRD.WN$2 | _m$2<>_ZRD.MA$2
            || ZRD.blank();     ZRD.REF:=_ZRD.REF; ZRD.KONTO:=_ZRD.KONTO;
               ZRD.WN:=_ZRD.WN-_w; ZRD.MA:=_ZRD.MA-_m;   ZRD.add()
            ?}
         ?};
         _ZRD.del()
      !}
   ?}
?};
_mask:=_r:='';
ZRD.prefix();
{? ZRD.first()
|| POZ.cntx_psh(); POW.cntx_psh();
   {!|?  {? _r<>ZRD.REF
         || _r:=ZRD.REF; _m:=(8+ZRD.REF)+4; _ref:=BB.sqlint(ZRD.REF); _jest:=1;
            {? _mask<>_m
            || POZ.use('pozy'+_m); POZ.prefix(); POW.use('pow_'+_m); POW.index('POZ');
               _mask:=_m
            ?};
            {? POZ.seek(_ref,)
            || {? ROZ_WYR.first()
               || {!|? {? ROZ_WYR.LP<>_lp
                       || POW.prefix(POZ.ref,ROZ_WYR.LP);
                          {? POW.first()
                          || {? F.spr_mask(ROZ_WYR.MASKA,POW.SLO().KOD)
                             || ZRD.cntx_psh();
                                {!|? {? ZRD.REF=_r
                                     || ($('ZRD.WYR'+$ROZ_WYR.LP+':=SLO.KOD'))(); ZRD.put(); ZRD.next()
                                     || 0
                                     ?}
                                !};
                                ZRD.cntx_pop(); 1
                             || {! |? {? ZRD.REF=_r || _next:=ZRD.del || 0 ?} !};
                                _jest:=0; 0
                             ?}
                          |? ROZ_WYR.MASKA='?'*(+ROZ_WYR.MASKA)
                          || 1
                          ?}
                       ?};
                       _jest & ROZ_WYR.next()
                  !}
               ?}
            ?}
         ?};
         {? _jest || ZRD.next() || _next ?}
   !};
   POZ.cntx_pop(); POW.cntx_pop()
?};
ZRD.first()


\dop_kzd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Funkcja dopisuje jeden rekord do tabeli ROW_DOC (konta docelowe)
::       dla KOSZTY.TYP='K', wpp do tabeli ROW_KON (konta zrodlowe)
::   WE: _a - symbol konta
::      [_b - _g symbole wyroznikow]
::  OLD: \dop_kzd/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.TYP='K'
|| ROW_DOC.blank;
   ROW_DOC.ROW_NAG:=KOSZTY.WYK_REF;
   ROW_DOC.KONTOKL:=_a;
   _ref:=#ROZ_KON.ref();
   ROZ_KON.cntx_psh();
   ROZ_KON.index('ROZ_KLU');
   ROZ_KON.prefix(ROZ_NAZ.ref,'D',_ref);
   _str:={? ROZ_KON.first() || ROZ_KON.STR || 'Wn' ?};
   ROZ_KON.cntx_pop();
   ROW_DOC.STR:=_str;
   ROW_DOC.KWOTAKL:=KOSZTY.WARTOSC;
   KOSZTY.SUMAKL+=KOSZTY.WARTOSC;
   {? _>1
   || SSTALE.AR();
      SLO.index('SL');
      SLO.prefix();
      KS_W.index('LP');
      KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROZ_KON.MASKA));
      {! _i:=2..7 |!
         {? KS_W.find_key(_i-1) & |_[_i]<>''
         || {? SLO.find_key(KS_W.SLU().SLU,|_[_i])
            || ($('ROW_DOC.WYR'+$(_i-1)+':=SLO.ref()'))()
            ?}
         ?}
      !}
   ?};
   exec('uzu_kon','!fks_dks_rkow');
   {? ROW_DOC.add() || exec('uzu_kwyr','!fks_dks_rkow') ?}
|| ROW_KON.blank;
   ROW_KON.LP:=ROZ_KON.LP;
   ROW_KON.ROW_NAG:=KOSZTY.WYK_REF;
   ROW_KON.KONTOZ:=_a;
   ROW_KON.STR:=ROZ_KON.STR;
   ROW_KON.FUN:=ROZ_KON.FUN;
   ROW_KON.KWOTA:={? ROZ_KON.WSP$2<>0 || ROZ_KON.WSP*KOSZTY.WARTOSC || KOSZTY.WARTOSC ?};
   KOSZTY.SUMAKZ+=ROW_KON.KWOTA;
   {? _>1
   || SSTALE.AR();
      SLO.index('SL');
      SLO.prefix();
      KS_W.index('LP');
      KS_W.prefix(exec('jak_kon','konto',ROK_F.SYNT+ROZ_KON.MASKA));
      {! _i:=2..7 |!
         {? KS_W.find_key(_i-1) & |_[_i]<>''
         || {? SLO.find_key(KS_W.SLU().SLU,|_[_i])
            || ($('ROW_KON.WYR'+$(_i-1)+':=SLO.ref()'))()
            ?}
         ?}
      !}
   ?};
   ROW_KON.add()
?}


\uzu_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Uzupelnienie konta docelowego na podstawie konta klucza
::  OLD: \uzu_kon/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('ind_b')<0 || ind_b:=BUD.ndx_tmp('',1,'KS',,0,'SLU',,0) ?};
_ref:=#ROZ_KON.ref();
ROZ_KON.cntx_psh();
ROZ_KON.index('ROZ_KLU');
ROZ_KON.prefix(KOSZTY.NAZ_REF,'D',_ref);
{? ROZ_KON.first()
|| _konto:=ROZ_KON.MASKA;
   _synt:=ROK_F.SYNT;
   _k:=_konto*'?';
   {? _k<_synt
   || {? F.spr_mask(_synt+_konto,_synt+ROW_DOC.KONTOKL)
      || _konto:=(_synt+ROW_DOC.KONTOKL)+(_synt-_konto);
         _k:=_konto*'?'
      || _k:=0
      ?}
   ?};
   {? _k
   || _ref:=exec('jak_kon','konto',_synt+ROW_DOC.KONTOKL);
      BUD.index('KS');
      BUD.prefix(exec('jak_kon','konto',_synt+_konto));
      _s:=+ROK_F.SEP;  _i:=_synt; _pom:=_i-_konto; _sep:=ROK_F.SEP;
      {? BUD.first()
      || {!|? _dl:=BUD.SLU().SLU().DL;
              {? _i+_s<=_k & _i+_s+_dl+_s>=_k
              || BUD.cntx_psh();
                 BUD.index(ind_b);
                 BUD.prefix();
                 {? BUD.find_key(_ref,SLUAPPL.ref)
                 || _poz:=BUD.POZ;
                    BUD.index('KS');
                    BUD.prefix(_ref); _licz:=ROK_F.SYNT;
                    {? BUD.first()
                    || {!|? {? BUD.POZ<_poz
                            || _licz+=(_s+BUD.SLU().SLU().DL); BUD.next()
                            || _w:=BUD.SLU().SLU().DL+((_licz+_s)-ROW_DOC.KONTOKL);
                               _konto:=(_i+_konto)+_sep+_w+((_i+_s+_dl)-_konto);0
                            ?}
                       !}
                    ?}
                 ?};
                 BUD.cntx_pop()
              ?};
              _i+=_s+_dl;
              _pom:=(_dl+_s)-_pom;
              _k:=_pom*'?'+_i;
              BUD.next()
         !}
      ?};
      _konto:=_i+_konto
   ?};
   ROW_DOC.KONTODC:=_konto
?};
ROZ_KON.cntx_pop()


\uzu_kwyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Utworzenie wyroznikow konta docelowego na podstawie definicji typu K
::  OLD: \uzu_kwyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=#ROZ_KON.ref();
_kon:=ROK_F.SYNT+ROZ_KON.MASKA;
ROZ_KON.cntx_psh();
ROZ_KON.index('ROZ_KLU');
ROZ_KON.prefix(KOSZTY.NAZ_REF,'D',_ref);
{? ROZ_KON.first()
|| ROW_WYR.prefix();
   _pr:=_sum:=0;
   ROZ_WYR.index('ROZ_WYR');
   ROZ_WYR.prefix(ROZ_KON.ref);
   {? ROZ_WYR.first()
   || {! |?
         {? ROZ_WYR.ZN='W'
         || exec('dod_kwyr','!fks_dks_rkow',ROZ_WYR.SLO)
         || KS_W.cntx_psh();
            KS_W.index('KSSLU');
            KS_W.prefix(exec('jak_kon','konto',_kon),ROZ_WYR.SLUAPPL);
            {? KS_W.first()
            || _slo:=($('ROW_DOC.WYR'+$KS_W.LP))();
               {? _slo || exec('dod_kwyr','!fks_dks_rkow',_slo) ?}
            ?};
            KS_W.cntx_pop()
         ?};
         ROZ_WYR.next()
      !}
   ?}
?};
ROZ_KON.cntx_pop()


\dod_kwyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Dodanie wyroznika dla konta docelowego
::   WE: _a - SLO.ref wyroznika
::  OLD: \dod_kwyr/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_WYR.blank();
ROW_WYR.ROW_DOC:=ROW_DOC.ref();
ROW_WYR.LP:=ROZ_WYR.LP;
ROW_WYR.SLU:=ROZ_WYR.SLUAPPL;
ROW_WYR.SLO:=_a;
ROW_WYR.PROC:=ROZ_WYR.PROC;
ROW_WYR.add()


\rk_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula przed akcji 'deFinicja' okien ROW_KON.WER i ROW_KON.WER0
::  OLD: \rk_def/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_NAG.ROZ_NAZ();
ROZ_NAZ.AKC:='T';
exec('sel_kon','!fks_dks_rkod');
ROW_KON.cntx_psh();
KOSZTY.TYP:='Z';
KOSZTY.WYK_REF:=ROW_NAG.ref();
exec('suma_zrd','dok_fks');
ROW_KON.cntx_pop()


\odd_poc2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2010]
:: OPIS: Formula na wartosc poczatkowa pola ROW_NAG.ODD
::  OLD: \odd_poc2/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
OPERATOR.DEPT


\rec_row
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Rekord przed w okienku wertowania WER tabeli ROW_NAG
::  OLD: \rec_row/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=_gray:='';
{? ROW_NAG.ZAKS='T'
|| _zwrot:='ROW_NAG#01#01'; _gray:='UT(D)'
|| _zwrot:=''; _gray:='T(PNU)'
?};
ROW_NAG.actions_grayed('WER',_gray);
_zwrot


\row_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Usuwanie wykonania rozliczenia
::   WE: [_a - czy wyswietlac komunikaty]
::  OLD: \row_usun/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_a:={? _=1 | ROW_NAG.sel_size()>0 || 0 || 1 ?};
{? ROW_NAG.ZAKS='T'
|| {? _a || FUN.info('Rozliczenie zadekretowane'@) ?}; 0
|| {? _a=0 | FUN.ask(' Usunąć rozliczenie?'@)
   || do();
      ROW_NAG.cntx_psh();
      ROW_KON.index('KONTO');
      ROW_KON.prefix(ROW_NAG.ref);
      {? ROW_KON.first()
      || {! |? ROW_KON.del()!}
      ?};
      ROW_DOC.index('KONTO');
      ROW_DOC.prefix(ROW_NAG.ref);
      {? ROW_DOC.first()
      || ROW_WYR.index('ROW_WYR');
         {! |?
            ROW_WYR.prefix(ROW_DOC.ref());
            {? ROW_WYR.first() || {!|? ROW_WYR.del() !} ?};
            ROW_DOC.del()
         !}
      ?};
      ROW_NAG.del();
      ROW_NAG.cntx_pop();
      {? end() & var_pres('ile')>0 || ile+=1 ?}
   ?}
?}


\be_rusun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Przed akcja grupowa usuwania wykonan rozliczen
::  OLD: \be_rusun/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask(' Usunąć zaznaczone rozliczenia?'@)
|| max:=ROW_NAG.sel_size(); ile:=0; 1
?}


\ae_rusun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Po akcji grupowej usuwania wykonan rozliczen
::  OLD: \ae_rusun/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ile=max
|| FUN.info('Usunięto wszystkie zaznaczone rozliczenia.'@)
|| FUN.info('Liczba usuniętych rozliczeń: %1 z %2 zaznaczonych.'@[$ile,$max])
?};
VAR_DEL.delete('max','ile'); 1


\row_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Wyswietlenie wartosci kont roczliczen
::  OLD: \row_roz/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
wyr:='0';
ref_wd:=ref_wk:=null;
ROW_NAG.ROZ_NAZ();
ROW_KON.index('KONTO'); ROW_KON.prefix(ROW_NAG.ref());
{? PAR_SKID.get(35)='T' & ROW_KON.last()
|| {! |?
      {? ROW_KON.WYR1<>null | ROW_KON.WYR2<>null | ROW_KON.WYR3<>null |
         ROW_KON.WYR4<>null | ROW_KON.WYR5<>null | ROW_KON.WYR6<>null
      || wyr:='';0
      || ROW_KON.prev()
      ?}
   !}
?};
wyrd:='0';
ROW_WYR.index('ROW_WYR');
ROW_DOC.index('KONTO'); ROW_DOC.prefix(ROW_NAG.ref());
{? ROZ_NAZ.TYP<>'R' & PAR_SKID.get(35)='T' & ROW_DOC.last()
|| {! |?
      {? ROW_DOC.WYR1<>null | ROW_DOC.WYR2<>null | ROW_DOC.WYR3<>null |
         ROW_DOC.WYR4<>null | ROW_DOC.WYR5<>null | ROW_DOC.WYR6<>null
      || wyrd:='';0
      || ROW_WYR.prefix(ROW_DOC.ref());
         {? ROW_WYR.first()
         || wyrd:=''; 0
         || ROW_DOC.prev()
         ?}
      ?}
   !}
|? ROZ_NAZ.TYP='R'
|| wyrd:={? PAR_SKID.get(35)='T' || '' || '0' ?}
?};
_ok:=ROW_KON.grp_make('Wartości kont rozliczeń'@,"grp_disp(ROW_KON,'WER'+wyr);
                                                 grp_disp(ROW_DOC,{? ROZ_NAZ.TYP='R'
                                                                  || 'WERR'
                                                                  |? ROZ_NAZ.TYP='K'
                                                                  || 'WERK'
                                                                  || 'WERD'
                                                                  ?}+wyrd)",'grp_row_kon');
ROZNE.SZUK:=1;
ROW_KON.win_patt('SZUK');
{? 'WER'+wyr='WER'
|| ROW_KON.fld_opt('WER','col_name="%1"'[KOSZTY.NAG1],ROW_KON,'WYR1','KOD');
   ROW_KON.fld_opt('WER','col_name="%1"'[KOSZTY.NAG2],ROW_KON,'WYR2','KOD');
   ROW_KON.fld_opt('WER','col_name="%1"'[KOSZTY.NAG3],ROW_KON,'WYR3','KOD');
   ROW_KON.fld_opt('WER','col_name="%1"'[KOSZTY.NAG4],ROW_KON,'WYR4','KOD');
   ROW_KON.fld_opt('WER','col_name="%1"'[KOSZTY.NAG5],ROW_KON,'WYR5','KOD');
   ROW_KON.fld_opt('WER','col_name="%1"'[KOSZTY.NAG6],ROW_KON,'WYR6','KOD')
|? 'WER'+wyr='WERG'
|| ROW_KON.fld_opt('WERG','col_name="%1"'[KOSZTY.NAG1],ROW_KON,'WYR1','KOD');
   ROW_KON.fld_opt('WERG','col_name="%1"'[KOSZTY.NAG2],ROW_KON,'WYR2','KOD');
   ROW_KON.fld_opt('WERG','col_name="%1"'[KOSZTY.NAG3],ROW_KON,'WYR3','KOD');
   ROW_KON.fld_opt('WERG','col_name="%1"'[KOSZTY.NAG4],ROW_KON,'WYR4','KOD');
   ROW_KON.fld_opt('WERG','col_name="%1"'[KOSZTY.NAG5],ROW_KON,'WYR5','KOD');
   ROW_KON.fld_opt('WERG','col_name="%1"'[KOSZTY.NAG6],ROW_KON,'WYR6','KOD')
?};
ROW_KON.grp_sel(_ok,ROW_KON,'WER'+wyr,,,,,9,
                "ROW_KON.index('KONTO');
                 ROW_KON.prefix(ROW_NAG.ref);
                 KOSZTY.TYP:='Z';
                 KOSZTY.WYK_REF:=ROW_NAG.ref;
                 exec('suma_zrd','dok_fks');
                 {? ref_wk<>null || ROW_KON.seek(ref_wk) || ROW_KON.first() ?};
                 ROW_KON.actions('WER'+wyr,
                                 {? ROZ_NAZ.TYP<>'R'
                                 || 'DUP:D'
                                 || ''
                                 ?});
                 ROW_KON.win_edit('')",,,,'maximized_with_title');
ROW_KON.grp_splt(_ok,,'horizontal','left');
{? wyrd=''
|| {? ROZ_NAZ.TYP='R'
   || ROW_DOC.fld_opt('WERR','col_name="%1"'[KOSZTY.NAG1],ROW_DOC,'WYR1','KOD');
      ROW_DOC.fld_opt('WERR','col_name="%1"'[KOSZTY.NAG2],ROW_DOC,'WYR2','KOD');
      ROW_DOC.fld_opt('WERR','col_name="%1"'[KOSZTY.NAG3],ROW_DOC,'WYR3','KOD');
      ROW_DOC.fld_opt('WERR','col_name="%1"'[KOSZTY.NAG4],ROW_DOC,'WYR4','KOD');
      ROW_DOC.fld_opt('WERR','col_name="%1"'[KOSZTY.NAG5],ROW_DOC,'WYR5','KOD');
      ROW_DOC.fld_opt('WERR','col_name="%1"'[KOSZTY.NAG6],ROW_DOC,'WYR6','KOD')
   |? ROZ_NAZ.TYP='K'
   || ROW_DOC.fld_opt('WERK','col_name="%1"'[KOSZTY.NAG1],ROW_DOC,'WYR1','KOD');
      ROW_DOC.fld_opt('WERK','col_name="%1"'[KOSZTY.NAG2],ROW_DOC,'WYR2','KOD');
      ROW_DOC.fld_opt('WERK','col_name="%1"'[KOSZTY.NAG3],ROW_DOC,'WYR3','KOD');
      ROW_DOC.fld_opt('WERK','col_name="%1"'[KOSZTY.NAG4],ROW_DOC,'WYR4','KOD');
      ROW_DOC.fld_opt('WERK','col_name="%1"'[KOSZTY.NAG5],ROW_DOC,'WYR5','KOD');
      ROW_DOC.fld_opt('WERK','col_name="%1"'[KOSZTY.NAG6],ROW_DOC,'WYR6','KOD')
   || ROW_DOC.fld_opt('WERD','col_name="%1"'[KOSZTY.NAG1],KOSZTY,'DSLO1');
      ROW_DOC.fld_opt('WERD','col_name="%1"'[KOSZTY.NAG2],KOSZTY,'DSLO2');
      ROW_DOC.fld_opt('WERD','col_name="%1"'[KOSZTY.NAG3],KOSZTY,'DSLO3');
      ROW_DOC.fld_opt('WERD','col_name="%1"'[KOSZTY.NAG4],KOSZTY,'DSLO4');
      ROW_DOC.fld_opt('WERD','col_name="%1"'[KOSZTY.NAG5],KOSZTY,'DSLO5');
      ROW_DOC.fld_opt('WERD','col_name="%1"'[KOSZTY.NAG6],KOSZTY,'DSLO6')
   ?}
?};
ROW_KON.grp_sel(_ok,ROW_DOC,{? ROZ_NAZ.TYP='R'
                            || 'WERR'
                            |? ROZ_NAZ.TYP='K'
                            || 'WERK'
                            || 'WERD'
                            ?}+wyrd,,,,16,8,
                "{? ROZ_NAZ.TYP='R'
                 || KOSZTY.TYP:='R';
                    KOSZTY.STRONA:='Wn'
                 || KOSZTY.TYP:='D'
                 ?};
                 ROW_DOC.index('KONTO');
                 ROW_DOC.prefix(ROW_NAG.ref);
                 KOSZTY.WYK_REF:=ROW_NAG.ref;
                 exec('suma_doc','dok_fks');
                 {? ROZ_NAZ.TYP='R'
                 || exec('zer_nag','dok_fks');
                    ROZNE.SZUK:=0;
                    ROW_DOC.actions('WERR'+wyrd,{? ROW_NAG.ZAKS='T' || 'dpuONR:d' || '' ?},,1)
                 || ROZNE.SZUK:=1
                 ?};
                 {? ref_wd<>null || ROW_DOC.seek(ref_wd) || ROW_DOC.first() ?};
                 ROW_DOC.win_patt('SZUK')",,,,'maximized_with_title');
ROW_KON.win_sel(_ok);
ROW_KON.select();
&wyr; &wyrd; &ref_wd; &ref_wk;
''


\wydr_row
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Wydruk wykonania rozliczenia kosztow
::  OLD: \wydr_row/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','FKS_DKS_RKOW','fks_row_kosz',,,,,,,,'W')


\kont_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Wywolanie tworzenia kont docelowych (dla rozliczen typu 'P')
::  OLD: \kont_doc/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
_kom:='';
ROZ_KON.index('ROZ_KON');
ROZ_KON.prefix(KOSZTY.NAZ_REF,ROZ_NAZ.TYP);
{? ROZ_KON.first()
|| {!
   |? KOSZTY.STRONA:=ROZ_KON.STR;
      KOSZTY.WARTOSC:=0;
      KOSZTY.KWRODZ:=ROZ_KON.FUN().TRESC;
      exec('dop_kdc','!fks_dks_rkow',ROZ_KON.MASKA);
      ROZ_KON.next()
   !}
|| _kom:='Brak definicji kont docelowych!'
?};
_kom


\dop_kdc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Funkcja dopisuje jeden rekord do tabeli ROW_DOC (konta docelowe)
::       ZMIENNE:
::       KLUCZ   - kwota czastkowego klucza podzialowego
::       KWKLUCZ - klucz podzialowy (suma ze wszystkich kont klucza)
::       DOPRZEN - kwota do rozliczenia
::       KWDOCEL - dotychczasowa kwota na kontach docelowych
::   WE: _a - symbol konta
::  OLD: \dop_kdc/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
{? KOSZTY.SUMADC$2<KOSZTY.SUMAKZ$2
|| KLUCZ:=KOSZTY.WARTOSC;
   KWKLUCZ:=KOSZTY.SUMAKL;
   DOPRZEN:=KOSZTY.SUMAKZ;
   KWDOCEL:=KOSZTY.SUMADC;
   ROW_DOC.blank;
   ROW_DOC.ROW_NAG:=KOSZTY.WYK_REF;
   ROW_DOC.KONTOKL:='';
   ROW_DOC.KWOTAKL:=KLUCZ;
   ROW_DOC.KONTODC:=_a;
   ROW_DOC.STR:=KOSZTY.STRONA;
   {? ROZ_NAZ.TYP='P'
   || {? ROZ_KON.FORM<>''
      || ROW_DOC.KWOTA:=($ROZ_KON.FORM)();
         ROW_DOC.KWOTA:=ROW_DOC.KWOTA$2;
         ROW_DOC.WSP:=(ROW_DOC.KWOTA/KOSZTY.SUMAKZ)$8
      || ROW_DOC.KWOTA:=((ROZ_KON.WSP/100)*KOSZTY.SUMAKZ)$2;
         ROW_DOC.WSP:=ROZ_KON.WSP/100
      ?};
      {? ROW_DOC.KWOTA$2>KOSZTY.SUMAKZ$2-KOSZTY.SUMADC$2
      || ROW_DOC.KWOTA:=KOSZTY.SUMAKZ$2-KOSZTY.SUMADC$2;
         ROW_DOC.WSP:=(ROW_DOC.KWOTA/KOSZTY.SUMAKZ)$6
      ?};
      KOSZTY.SUMADC+=ROW_DOC.KWOTA$2;
      {? ROW_DOC.add() || exec('wyr_doc','!fks_dks_rkow') ?}
   ?};
   &KLUCZ; &KWKLUCZ ; &DOPRZEN ; &KWDOCEL
?}


\wyr_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Tworzenie wyroznikow konta docelowego na podstawie definicji typu P
::  OLD: \wyr_doc/roz_kos2.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_WYR.prefix();
_pr:=_sum:=0;
ROZ_WYR.index('ROZ_WYR');
ROZ_WYR.prefix(ROZ_KON.ref);
{? ROZ_WYR.first()
|| {! |?
      _kw:={? ROZ_WYR.PROC$2=0
           || ROW_DOC.KWOTA$2
           || (ROZ_WYR.PROC/100*ROW_DOC.KWOTA)$2
           ?};
      _pr+=ROZ_WYR.PROC$2;
      _sum+=_kw$2;
      {? _pr=100 & _sum$2<>ROW_DOC.KWOTA$2 || _kw:=_kw-(_sum-ROW_DOC.KWOTA$2) ?};
      ROW_WYR.blank();
      ROW_WYR.ROW_DOC:=ROW_DOC.ref();
      ROW_WYR.LP:=ROZ_WYR.LP;
      ROW_WYR.SLU:=ROZ_WYR.SLUAPPL;
      ROW_WYR.SLO:=ROZ_WYR.SLO;
      ROW_WYR.KW:=_kw;
      ROW_WYR.PROC:={? ROZ_WYR.PROC$2=0 || (_kw/ROW_DOC.KWOTA)$6 || ROZ_WYR.PROC ?};
      ROW_WYR.add();
      ROZ_WYR.next()
   !}
?}


\rek_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: Rekord przed w okienku SLO tabeli ROZ_NAZ
::  OLD: \rek_rozl/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
ROW_NAG.cntx_psh(); ROW_NAG.index('OKR_ODD'); ROW_NAG.prefix();
_ok:=~ROW_NAG.find_key(SSTALE.AO,odd,ROZ_NAZ.NAZWA,dat,ROZ_NAZ.ref());
ROW_NAG.cntx_pop();
{? _ok || exec('findtmp','#color') || '' ?}


\czy_grup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Pytanie czy grupowe tworzenie wykonań kosztów w procesie
::----------------------------------------------------------------------------------------------------------------------
FUN.ask('Grupowe tworzenie wykonań rozliczeń kosztów?'@)

:Sign Version 2.0 jowisz:1045 2023/09/21 11:23:38 93c365c5be42a7e465490d4e5354d19cf1fcbb875cd8049e42754571934a7be57576a92165a50532e9b63cdbd509ab31c11e362455f4c5e4fea9c0ae2a97d48e5291fef9ca600f68d62b9c85d547a89770d86e73b9ec83a7a56dc80d984146197a3ba01302a4e2b4fca4cc4d7a6eb5794a1de8f43122a9f28010557aaf8b5df7

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ppl_zlc_werh.fml
:: Utworzony: 20.09.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności PPL_ZLC_WERH - Wybór rachunku do umowy.
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wybór rachunku do umowy - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd','ZC','ZC.P().get()')
::
:: Parametr wejściowy ON_ESC określa sposób działania przy braku "wyboru" rachunku.
:: Parametr może przyjmować wartości:
::    DONE     - Czynność zostanie zakończona, a parametry wyjściowe przyjmą wartości puste.
::    KEEP     - Czynność zostanie odłożona na listę zadań aby użytkownik mógł do niej wrócić. Oznacza to, że czynności
::               nie da się zakończyć bez wyboru współpracownika  [DOMYŚLNIE].
::    CANCEL   - Jeśli czynność jest pierwszą w procesie to po zakończeniu formuły głównej instancja czynności zostanie
::               usunięta. Jeśli czynność jest kolejną w procesie to otrzyma status oczekująca i pozostanie na liście
::               zadań - czyli tak jak dla KEEP.
::    ERROR    - Czynność jest kończona a proces zatrzymywany z ustawioną flagą błędu.
::# kind=WE, symbol=ON_ESC, type=STRING, name=Sposób działania przy rezygnacji, required=T, fml_val="exec('on_esc','#bi_stat',{? _a=~~ || 'KEEP' || _a ?})"
::
::# kind=WE, symbol=ZC, type=_ZC, name=Wskazanie umowy cywilnoprawnej, required=T, keyref=T
::
::# kind=WY, symbol=RH, type=_RH, name=Wskazanie rachunku do umowy cywilnoprawnej, required=T
::
_par:=params_get();
_mp:=_par.mp;

_keyRefs:=_mp.getRefs();
{? var_pres('[1]',_keyRefs)<=0
|| _mp.error('Nieprawidłowy parametr wejściowy.');
   return()
?};

_in:=_par.in;
_out:=_par.out;

_ret:=exec('select','!ppl_zlc_werh',_in.ZC);
{? _ret.err<>''
:: Coś poszło nie tak ...
|| _mp.error(_ret.err)

|? _ret.RH=null()
:: Wyjście przez ESC.
|| {? _in.ON_ESC='KEEP'
   || _mp.keep()

   |? _in.ON_ESC='CANCEL'
   || _mp.cancel()

   |? _in.ON_ESC='ERROR'
   || _mp.error('ON_ESC=ERROR')

   || _mp.done()
   ?}

:: Umwa wybrana.
|| _out.RH:=_ret.RH;
   _mp.save(,_out);
   _mp.done()
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Wybór rachunku do umowy - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_keyRefs:=_mp.getRefs();
_ref:={? var_pres('[1]',_keyRefs) || _keyRefs[1] || _mp.load(exec('kind_in','#b_port')).ZC ?};
_tab:=exec('init_desc_tab','pracownik');
_nu:='';
_du:='';

OSOBA.cntx_psh();
P.cntx_psh();
P.prefix();
UD_SKL.cntx_psh();
UD_SKL.prefix();
ZC.cntx_psh();
ZC.prefix();
{? ZC.seek(_ref)
|| ZC.P().OSOBA();
   _tab.ZAW_DANE:='T';
   _tab.NAZWISKO:=P.OSOBA().NAZWISKO;
   _tab.PIERWSZE:=OSOBA.PIERWSZE;
   _tab.OBCY:=OSOBA.OBCY;
   _tab.PASZPORT:=OSOBA.PASZPORT;
   _tab.PESEL:=OSOBA.PESEL;
   _tab.UR_DATA:=$OSOBA.UR_DATA;
   _tab.T:=P.T;
   _tab.IP:=$P.IP;
   _tab.UD_SKL:=ZC.WYDZIAL().SYMBOL;
   _nu:=ZC.NU;
   _du:=ZC.DU$4
?};
ZC.cntx_pop();
UD_SKL.cntx_pop();
P.cntx_pop();
OSOBA.cntx_pop();

{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Wybierz rachunek do umowy cywilnoprawnej: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5 | %6 | Umowa nr %7 z dnia %8 r.'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP,_tab.UD_SKL,_nu,_du]
   |? +_tab.PESEL
   || 'Wybierz rachunek do umowy cywilnoprawnej: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5 | %6 | Umowa nr %7 z dnia %8 r.'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP,_tab.UD_SKL,_nu,_du]
   || 'Wybierz rachunek do umowy cywilnoprawnej: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5 | %6 | Umowa nr %7 z dnia %8 r.'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP,_tab.UD_SKL,_nu,_du]
   ?}
|| 'Wybierz rachunek do umowy cywilnoprawnej'@@
?}


\rh_wybi_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wypełnienie" okna grupowego WYBI tabeli RH.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
grp_edisp(P,'INFO_Z');
grp_edisp(ZC,'INFO');
~~


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.42]
:: OPIS: Właściwy wybór rachunku do umowy cywilnoprawnej.
::   WE: _a [REFERENCE] - Wskazanie umowy cywilnoprawnej.
::   WY: Tablica elementów nazwanych:
::          err - Opis błędu lub ''.
::          RH  - Wskazanie wybranego rachunku do umowy lub null().
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new('err','RH');
_ret.err:='';
_ret.RH:=null();

ZC.cntx_psh();
ZC.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & ZC.seek(_a)
|| __F_ZATR.push();
   __F_ZATR.mod('Z');
   OSOBA.cntx_psh();
   OSOBA.prefix();
   P.cntx_psh();
   P.prefix();
   ZC.P().OSOBA();
   RH.cntx_psh();
   RH.index('RACHUNKC');
   RH.prefix(ZC.ref());
   RH.index('RACHUNKI');
   RH.prefix(ZC.ref());
   RH.win_sel('WYBI');
:: Okno readagowania zalaeży od tego, czy jest to zasiłek czy też nie, dlatego będzie ustawiane dynamicznie
   RH.win_patt('WZO');
   exec('rh_icon','zlec_rh');
   exec('zc_efld_opt','zlec_rh','*',ZC,'INFO');
   {? RH.select()
   || _ret.RH:=RH.ref()
   ?};
   RH.cntx_pop();
   P.cntx_pop();
   OSOBA.cntx_pop();
   __F_ZATR.pop()
|| _ret.err:='Nie znaleziono umowy cywilnoprawnej.'
?};
ZC.cntx_pop();

_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 709ef78af4f74725175a1fd8f1ecc62538dea2ba2d4f7d8a9acf9880396dd2aac39083aab44d1ab5268669abe8fba74ff71f86f4e0d2b772309d7419427f379f2eace0e38d6922f44b434ead3339508db555bf3b7631edfbf714aac5913a7f7a08e50b053fb49f4c13258319345db2720daee8f28801ea10c553d6084385767a

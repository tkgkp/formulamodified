:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_mrao.fml [17.00]
:: Utworzony: 2016.01.05
:: Autor: AMK
::======================================================================================================================
:: Zawartosc: Formuły czynności FKS_KSR_MRAO - Aktualizacja bilansu otwarcia
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności FKS_KSR_MRAO - Aktualizacja bilansu otwarcia
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WE, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
::# kind=WY, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na dokument bilansu otwarcia, required=T, keyref=T
::# kind=WY, symbol=UNIK_ID, type=STRING, name=Unikalny identyfikator, required=N
:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=LOOP
::# condition=Redakcja dokumentu, act_uid=FKS_DKS_DARK, auto=T, formula=_a.DOK<>null & _a.DOK<>~~
_args:=params_get();
_we:=_args.in;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_wew:=_args.int;
_wy:=_args.out;
params_set(params_get());
akt_bo:=1;
DOK.cntx_psh();
{? var_pres('UNIK_ID',_we)>0 & _we.UNIK_ID<>''
|| params_exec('par_out','!fks_ksr_mrao',_we.OKRO_F,_we.UNIK_ID);
   _mp.done()
|| {? ~Perm.hasFull('FJKS',OPERATOR.USER)
   || FUN.info('Zalogowany użytkownik nie ma uprawnień do pracy we wszystkich jednostkach księgowych.'@)
   || exec('init','zam_okr_rok');
      iddekpar:=tm_stamp();
:: obszar roboczy lub czynność startowa
      {? _akcja='aktbo' | _mp.pathProc()
      || _dalej:=1;
         {? _akcja='aktbo' & OPERATOR.DEPT
         || FUN.info('Aktualizacja bilansu otwarcia musi być uruchamiana przy ustawieniu w obszarze\n'+
                     'pracy we wszystkich jednostkach księgowych.'@);
            _dalej:=0
         ?};
         {? _dalej
         || exec('ustaw_bo','zam_okr_rok',SSTALE.AR);
            OKRESY.ZO();
            {? OKRESY.ZO
            || _wy.OKRO_F:=OKRESY.ZO; _mp.save(,_wy);
               params_exec('akt_bo','!fks_ksr_mrao',1)
            ?};
            params_exec('par_out','!fks_ksr_mrao',OKRESY.ZO,iddekpar)
         ?}
      |? _mp.pathTodo() & _we.OKRO_F
      || OKRO_F.cntx_psh(); OKRO_F.prefix();
         {? OKRO_F.seek(_we.OKRO_F)
         || exec('ustaw_bo','zam_okr_rok',OKRO_F.ROK);
            {? OKRESY.ZO
            || _wy.OKRO_F:=OKRESY.ZO; _mp.save(,_wy);
               params_exec('akt_bo','!fks_ksr_mrao',~_mp.isAutoRun())
            ?}
         ?};
         OKRO_F.cntx_pop();
         params_exec('par_out','!fks_ksr_mrao',OKRESY.ZO,iddekpar)
      ?}
   ?}
?};
VAR_DEL.delete('akt_bo','iddekpar');
DOK.cntx_pop();
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Aktualizacja bilansu otwarcia'@@;
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('OKRO_F',_wy)
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKRO_F.seek(_wy.OKRO_F)
   || ROK_F.cntx_psh(); ROK_F.prefix(); OKRO_F.ROK();
      _desc:='Aktualizacja bilansu otwarcia: %1'@@[ROK_F.NAZ];
      ROK_F.cntx_pop()
   ?};
   OKRO_F.cntx_pop()
?};
_desc


\par_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych czynności
::   WE: _a - ref okresu (OKRO_F)
::       _b - UNIK_ID
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_wy:=_args.out;
_we:=_args.in;
_cont:=0;
OKRO_F.cntx_psh(); OKRO_F.prefix();
{? OKRO_F.seek(_a)
|| DOK.cntx_psh(); ROK_F.cntx_psh();
   DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
   DOK.index('DOKZRODL'); DOK.prefix(_b+'1',);
   {? DOK.first()
   || _wy.DOK:=DOK.ref();
      {? DOK.size()>1
      || _wy.UNIK_ID:=_b; _cont:=1
      || _wy.UNIK_ID:=''
      ?};
      _wy.OKRO_F:=_a;
      DOK.DOKZRODL:=(DOK.DOKZRODL-1)+'2';
      DOK.cntx_psh(); DOK.prefix(); DOK.put(); DOK.cntx_pop()
   || _wy.UNIK_ID:=''; _wy.DOK:=null; _wy.OKRO_F:=_a
   ?};
   _mp.save(,_wy);
   {? _cont || _mp.loop_continue() ?};
   exec('desc_update','#b__box',OKRO_F.uidref());
   DOK.cntx_pop(); ROK_F.cntx_pop()
?};
OKRO_F.cntx_pop()


\akt_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Aktualizacja bilansu otwarcia
::   WE: _a - 1/0 - czy komunikaty i pytania być mają
::  OLD: \akt_bo/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
VAR_DEL.delete('ZAMKNIJ');
ZAMKNIJ:=obj_new(@.CLASS.ZAM_CLASS);
_ar:=SSTALE.AR; _ao:=SSTALE.AO;
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
SSTALE.AR:=OKRESY.ZR;
SSTALE.AO:=OKRESY.ZO;
exec('open_tabele','open_tab');
exec('test_akt','!fks_ksr_mrao');
{? ZAMKNIJ.OK & _a
|| ZAMKNIJ.OK:=FUN.ask('Zaktualizować bilans otwarcia '+BILANSP.NROK().NAZ+'?');
   {? ~ZAMKNIJ.OK
   || ZAMKNIJ.ERRTXT:='Zrezygnowano z operacji.'
   ?}
?};
{? ZAMKNIJ.OK
|| {? exec('blok_tab','zam_okr_rok')
   || {? ZAMKNIJ.OK & BILANSP.BO & BILANSP.PROK & BILANSP.NROK
      || exec('tot_akt','zam_okr_rok')
      ?}
   || ZAMKNIJ.ERRTXT:='Nie powiodło się blokowanie tabel.\nOperacja przerwana.';
      ZAMKNIJ.OK:=0
   ?};
   exec('odbl_tab','zam_okr_rok')
?};
echo();
{? _a
|| {? ~ZAMKNIJ.OK & +ZAMKNIJ.ERRTXT
   || FUN.info(ZAMKNIJ.ERRTXT)
   |? ~ZAMKNIJ.OK & ~+ZAMKNIJ.ERRTXT
   || FUN.info('Niezidentyfikowany błąd podczas aktualizacji BO.'@)
   ?}
?};
{? ZAMKNIJ.OK
|| {? _a || FUN.info('Aktualizacja BO zakończona.'@) ?};
   _mp.done()
?};
echo();
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
SSTALE.AR:=_ar; SSTALE.AO:=_ao;
SSTALE.AO().ROK();
exec('open_tabele','open_tab');
_ok:=ZAMKNIJ.OK; VAR_DEL.delete('ZAMKNIJ');
_ok


\test_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza warunki niezbędne do aktualizacji BO
::  OLD: \test_akt/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); OKRO_F.index('ROK'); OKRO_F.prefix();
KS.prefix();
SSTALE.AO().ROK();
{? OKRO_F.NAZ<>'Bilans otwarcia'
|| ZAMKNIJ.ERRTXT:='Aktywnym okresem musi być \"Bilans otwarcia\".';
   ZAMKNIJ.OK:=0
?};
{? ZAMKNIJ.OK & ~BILANSP.PROK
|| ZAMKNIJ.ERRTXT:='Brak poprzedniego roku bilansowego w obszarze FKS.';
   ZAMKNIJ.OK:=0
?};
{? ZAMKNIJ.OK & ~BILANSP.BO
|| ZAMKNIJ.ERRTXT:='Brak ustalonego \"Bilansu otwarcia\" bieżącego roku w zmiennych.';
   ZAMKNIJ.OK:=0
?};
{? ZAMKNIJ.OK & BILANSP.BO & -BILANSP.BO().ZAM='t'
|| ZAMKNIJ.ERRTXT:='\"Bilans otwarcia\" roku '+SSTALE.AR().NAZ+' jest zamknięty.';
   ZAMKNIJ.OK:=0
?};
{? ZAMKNIJ.OK & (BILANSP.NROK(); ~KS.find_key(ROK_F.ref()))
|| ZAMKNIJ.ERRTXT:='Brak zdefiniowanego planu kont dla roku '+ROK_F.NAZ+'.';
   ZAMKNIJ.OK:=0
?};
{? ZAMKNIJ.OK & BILANSP.PROK
|| OKRO_F.prefix(BILANSP.PROK);
   {? OKRO_F.last() & OKRO_F.prev()
   || {! |?
         {? OKRO_F.prev()
         || {? OKRO_F.ROK=SSTALE.AR || ZAMKNIJ.OK:=(ZAMKNIJ.OK & -OKRO_F.ZAM='t') || 0 ?}
         || ZAMKNIJ.OK:=(ZAMKNIJ.OK & OKRO_F.NAZ='Bilans otwarcia' & -OKRO_F.ZAM='t'); 0
         ?} & ZAMKNIJ.OK
      !}
   || ZAMKNIJ.OK:=0; ZAMKNIJ.ERRTXT:='Niezidentyfikowany błąd okresów obrachunkowych.'
   ?};
   {? ~ZAMKNIJ.OK & ~+ZAMKNIJ.ERRTXT
   || ZAMKNIJ.ERRTXT:='Niezamknięty okres '+OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ+'.'
   ?};
   OKRO_F.prefix()
?};
{? ZAMKNIJ.OK & BILANSP.PROK().ZAM='T'
|| ZAMKNIJ.ERRTXT:='Poprzedni rok ('+BILANSP.PROK().NAZ+') jest zamknięty.';
   ZAMKNIJ.OK:=0
?};
{? ZAMKNIJ.OK
|| VAR_DEL.delete('__par491');
   __par491:=PAR_SKID.get(491);
   _c_prev:={? #__par491>0 & #__par491<=4 || #__par491 || 1 ?};
   _f_prev:="_ok:=1; _ii:=0;
      {!
      |? {? ~OKRO_F.prev() || _ok:=0 ?};
         _ii+=1;
         _ok & _ii<_a
      !};
      _ok";
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(BILANSP.PROK);
   {? OKRO_F.last() & _f_prev(_c_prev) & OKRO_F.prev() & OKRO_F.POCZ<>date(0,0,0) & OKRO_F.ZAM<>'T'
   || ZAMKNIJ.ERRTXT:={? _c_prev=1
                      || 'W poprzednim roku ('+BILANSP.PROK().NAZ+
                         ') może być otwarty tylko okres poprzedzający BZ.'
                      || 'W poprzednim roku ('+BILANSP.PROK().NAZ+
                         ') mogą być otwarte tylko '+
                         {? _c_prev=2 || 'dwa' |? _c_prev=3 || 'trzy' |? _c_prev=4 || 'cztery' ?}+
                         ' okresy poprzedzające BZ.'
                     ?};
      ZAMKNIJ.OK:=0
   ?};
   OKRO_F.cntx_pop();
   VAR_DEL.delete('__par491')
?};
ZAMKNIJ.OK


\aktbo_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Aktualizacja bilansu otwarcia z poziomu dokumentów
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_KSR_MRAO';
_params.AKCJA:='aktbo';
ROK_F.cntx_psh(); ROK_F.prefix();
OKRO_F.cntx_psh(); OKRO_F.prefix();
SSTALE.AO();
OKRESY.ZO:=OKRO_F.ref();
OKRESY.ZR:=OKRO_F.ROK;
_params.UIDREF:=OKRO_F.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',OKRO_F.ref());
_params.PROC_START:='T';
exec('mp_run','#b__box',_params);
ROK_F.cntx_pop(); OKRO_F.cntx_pop()


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 935124afea3895812e431083c8181e3e9bf4bc6ea8e3a40018e3fb34129aa36e5995e218fb96f15631e0dbaa325d83cc6fc694fce43c348de78ebd2d320fd253be79017df747cf628e32fbd3ed0065f4de1f0b18e729e663c8b2d546598f74c0ac36c33a464595069883b5bbfee754833530ac4da96df742dc01162c97dccf0f

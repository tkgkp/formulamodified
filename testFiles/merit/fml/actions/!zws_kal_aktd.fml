:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_kal_aktd.fml
:: Utworzony: 26.10.2020
:: Autor: MicKoc [21.14]
:: Systemy:
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_KAL_AKTD - Aktualizacja danych po zmianie definicji kalendarza
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.14]
:: OPIS: Aktualizacja danych - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# properties=SERVICE
::# kind=WE, symbol=CZY_INFO, type=STRING, name=Komunikaty informacyjne [T/N*], required=N, keyref=N, ^
::#   fml_val="exec('edit_boolean','#edit',{? _a=~~ || 'N' || _a ?}, 'Komunikaty informacyjne')"
::# kind=WE, symbol=CZY_LT, type=STRING, name=Aktualizacja obliczonych nieobecności [T*/N], required=N, keyref=N, ^
::#   fml_val="exec('edit_boolean','#edit',{? _a=~~ || 'N' || _a ?}, 'Aktualizacja obliczonych nieobecności')"
::# kind=WE, symbol=DEL_DNI, type=NUMBER, name=Ilość dni do usunięcia komunikatu [14], required=N, keyref=N, ^
::#   fml_val="exec('ustaw_dni','kal_dane_akt',_a)"
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK,BŁĄD)", required=N
::# kind=WY, symbol=SUB, type=STRING, name=Temat, required=N
::# kind=WY, symbol=RCV, type=MEMO, name=Lista odbiorców, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::
params_set(_par:=params_get());
_in:=_par.in;
_out:=_par.out;
_mp:=_par.mp;
_akcja:=_mp.akcja();
_result:='';
_out.SUB:='Informacja o aktualizacji danych po zminach w definicji kalendarzy.';
_out.RCV:='';
_out.RESULT:='BŁĄD';
{? var_press('DEL_DNI',_in)<>type_of(0) | _in.DEL_DNI<1
|| _in.DEL_DNI:=14
?};
{? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_id);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_id);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _ret:=exec('aktualizacja','kal_dane_akt');
      {? var_pres('STATUS',_ret)=type_of('') & var_press('OK',_ret)=type_of(0) & ~_ret.OK
      || _result:=_ret.STATUS
      |? var_pres('STATUS',_ret)=type_of('') & var_press('OK',_ret)=type_of(0) & _ret.OK
      || _out.RESULT:='OK';
         _mp.save(,_out);
         _mp.done()
      || _out.RESULT:='OK';
         _mp.save(,_out);
         _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   {? _czy_info | (var_press('OK',_ret)=type_of(0) & ~_ret.OK) || FUN.emsg(_result) ?}
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.14]
:: OPIS: Aktualizacja danych - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych po zmianie definicji kalendarza'@@

:Sign Version 2.0 jowisz:1048 2021/04/09 15:19:42 1341423c5d7646c04b7068fd3a77c7a60eeba66d85bdb12e589b6c320beb99b7a9e70e3c8f74cc8850456a03efddf215118d8c60f5b852f218ef846850fcf808d345d327e46001cbce98fa212b5834d2c61e4ced5b4f994f9b982343f7d31c3d89533f4e8ee8b5e392821b702a5871f796cdcdd8d2c64852edccaf4533d751e0

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pdm_patd.fml
:: Utworzony: 28.01.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PDM_ZNOS - Przegląd - swoje noty controllingowe
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przegląd - swoje noty controllingowe - główna formuła czynności CTR_PDM_ZNOS
::----------------------------------------------------------------------------------------------------------------------


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::  OLD: \k__noty_sel/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init_k_noty','control');
_b.grp_sel(_c,K__NOTY,'WER',_a,"
   K__PNOTY.index('K__NOTY');
   _filtr:=K__NOTY.f_active();
   {?  _filtr & K__NOTY.f_get() | ~_filtr & K__NOTY.get()
   || exec('bv_k_pnoty','control')
   || K__PNOTY.prefix(null);
      UD_POM.WXMODEL:=null;
      UD_POM.K1:=UD_POM.K2:=UD_POM.K3:=UD_POM.K4:=UD_POM.K5:='';
      K__PNOTY.actions('WER','DPU:D',,1);
      UD_POM.SUMA:=0
   ?};
   grp_disp(K__PNOTY,'WER',1)
",,,,"
   exec('k_noty_prefix','control')
",,,,'maximized_with_title');
task_attach('CTR_PDM_DNOT');
task_attach('CTR_PDM_ZNOS');
task_attach('CTR_PDM_ZNOW');
_b.tab_splt(_c,,'horizontal','panel1',15);
K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K1],K__PNOTY,'WYM1','SYMBOL');
K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K2],K__PNOTY,'WYM2','SYMBOL');
K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K3],K__PNOTY,'WYM3','SYMBOL');
K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K4],K__PNOTY,'WYM4','SYMBOL');
K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K5],K__PNOTY,'WYM5','SYMBOL');
_b.grp_sel(_c,K__PNOTY,'WER',,,,,,,,,,'maximized_with_title');
exec('k_noty_prefix','control');
exec('set_fml','#field',UD_POM,'SUMA',"exec('k_pnoty_suma','control')")


\k_noty_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Wydruk noty
::  OLD: \k_noty_druk/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? K__NOTY.sel_size()
|| __K_NOTY.LP:=lp; lp+=1;
   __K_NOTY.REF:=#K__NOTY.ref();
   __K_NOTY.REFSQL:=$K__NOTY.ref();
   __K_NOTY.add()
|| exec('rep_exec','#b_report','CTR_PDM_ZNOS','ctr_noty',,,,,,,,'W')
?}


\bg_k_noty_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed akcja grupowa wydruku not
::  OLD: \bg_k_noty_druk/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Wydrukować zaznaczone noty?'@)
|| VAR_DEL.delete('__K_NOTY');
   lp:=0;
   l_size:=K__NOTY.sel_size();
   l_druk:=czy_druk:=0;
   __K_NOTY:=tab_tmp(1,'LP','INTEGER',,'REF','INTEGER',,'REFSQL','STRING[16]',);
   1
?}


\ag_k_noty_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Po akcji grupowej wydruku not
::  OLD: \ag_k_noty_druk/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','CTR_PDM_ZNOS','ctr_noty',,,,,,,,'W');
VAR_DEL.delete('__K_NOTY')


\br_k_noty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Rekord przed K__NOTY
::  OLD: \br_k_noty/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
{? ~K__NOTY.sel_size()
|| {? K__NOTY.STAT='T'
   || _gray+='PUA'
   || _gray+='W'
   ?};
   {? K__NOTY.ZAR<>OPERATOR.USER().KOD
   || _gray+='P'
   ?}
?};
{? UNPAR.P11='U'
|| _gray+='O'
|| _gray+='Y'
?};
K__NOTY.actions_grayed('WER',_gray);
{? K__NOTY.STAT='T'
|| Color.rekprzed('K__NOTY#01#01')
|| ''
?}


\bv_ud_pom_w_naz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed wyswietleniem pol UD_POM.Wx_NAZ (x: 1..5)
::  OLD: \bv_ud_pom_w_naz/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
fld('');
{? UD_POM.WXMODEL
|| _nr:=#(1+(1-cur_afld()));
   SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(UD_POM.WXMODEL,_nr);
   _naz:={? SKID_MBP.first() || SKID_MBP.NAZ || '' ?};
   SKID_MBP.cntx_pop();
   {? _naz<>''
   || fld(_naz); ''
   || exec('findfnrd','color')
   ?}
|| exec('findfnrd','color')
?}


\bv_ud_pom_wo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed wyswietl pol UD_POM.Wx_OPIS (x: 1..5)
::  OLD: \bv_ud_pom_wo/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? UD_POM.WXMODEL
|| _nr:=#(1+(1-cur_afld()));
   SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(UD_POM.WXMODEL,_nr);
   _jest:=SKID_MBP.first();
   SKID_MBP.cntx_pop();
   {? _jest
   || UD_SKL.cntx_psh();
      UD_SKL.prefix();
      _fml:=($('UD_POM.W'+$_nr+'_FLD'))();
      _sym:=($(_fml+'().OPIS'))();
      fld(_sym);
      UD_SKL.cntx_pop();
      ''
   || exec('findfnrd','color')
   ?}
|| exec('findfnrd','color')
?}


\bv_ud_pom_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed wyswietl UD_POM.Wx (x: 1..5)
::  OLD: \bv_ud_pom_w/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? UD_POM.WXMODEL
|| _nr:=#(1+(1-cur_afld()));
   SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(UD_POM.WXMODEL,_nr);
   _jest:=SKID_MBP.first();
   SKID_MBP.cntx_pop();
   {? _jest
   || UD_SKL.cntx_psh();
      UD_SKL.prefix();
      _fml:=($('UD_POM.W'+$_nr+'_FLD'))();
      _sym:=($(_fml+'().SYMBOL'))();
      fld(_sym);
      UD_SKL.cntx_pop();
      ''
   || exec('findfnrd','color')
   ?}
|| exec('findfnrd','color')
?}


\k_pnoty_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Prezentacj wszystkich pozycji
::----------------------------------------------------------------------------------------------------------------------
K__NOTY.cntx_psh();
K__PNOTY.cntx_psh();
K__PNOTY.index('K__NOTY'); K__PNOTY.prefix();
K__PNOTY.win_sel('WER_FULL');
exec('k_pnoty_filtr','!ctr_pdm_znos',1);
K__PNOTY.hdr_sel();
{? UNPAR.P11='U'
|| K__PNOTY.hdr_sel(' - Użytkownika'@)
|| K__PNOTY.hdr_sel(' - Wszystkich użytkowników'@)
?};
K__PNOTY.fld_opt('WER_FULL','col_name="%1"'[POMOC.K1],K__PNOTY,'WYM1','SYMBOL');
K__PNOTY.fld_opt('WER_FULL','col_name="%1"'[POMOC.K2],K__PNOTY,'WYM2','SYMBOL');
K__PNOTY.fld_opt('WER_FULL','col_name="%1"'[POMOC.K3],K__PNOTY,'WYM3','SYMBOL');
K__PNOTY.fld_opt('WER_FULL','col_name="%1"'[POMOC.K4],K__PNOTY,'WYM4','SYMBOL');
K__PNOTY.fld_opt('WER_FULL','col_name="%1"'[POMOC.K5],K__PNOTY,'WYM5','SYMBOL');
K__PNOTY.select();
K__PNOTY.f_clear();
K__PNOTY.cntx_pop();
K__NOTY.cntx_pop()


\k_pnoty_nota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Wyswietla note z pozycji noty
::  OLD: \k_pnoty_nota/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
K__NOTY.cntx_psh();
K__PNOTY.K__NOTY();
K__NOTY.display();
K__NOTY.cntx_pop()


\br_k_pnoty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed redakcja K__PNOTY
::  OLD: \br_k_pnoty/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBP.cntx_psh();
SKID_MBP.index('LP'); SKID_MBP.prefix(K__PNOTY.SKID_MBN);
{? SKID_MBP.first()
|| {! |?
      ($('POMOC.K'+$SKID_MBP.LP+':=_a'))(SKID_MBP.NAZ);
      exec('ud_pom_wym_en','control',SKID_MBP.LP,1);
      SKID_MBP.next()
   !}
?};
{! _i:=SKID_MBP.LP+1..5
|! ($('POMOC.K'+$_i+':=_a'))('');
   exec('ud_pom_wym_en','control',_i,0)
!};
SKID_MBP.cntx_pop();
''


\k_pnoty_filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Ustawia filtr na tabeli K__PNOTY
::   WE: [_a] - 1-zawsze ustawiac [0]-ustawiac jesli wlaczony
::  OLD: \k_pnoty_filtr/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? _ | K__PNOTY.f_active()
|| K__PNOTY.clear();
   K__PNOTY.f_set(
      'OKRO_F(NR),SKID_MBN(KOD),WYM1(SYMBOL),WYM2(SYMBOL),WYM3(SYMBOL),WYM4(SYMBOL),WYM5(SYMBOL)',,
      {? UNPAR.P11='U'
      || 'K__PNOTY.ZAR=\''+OPERATOR.USER().KOD+'\''
      || ''
      ?}
   )
?}


\k_war_nota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Wyswietla note zwiazana z zapisem dla controllingu (K__WAR)
::  OLD: \k_war_nota/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
{? K__WAR.REF_DOK<>'' & 6+K__WAR.REF_DOK='notyco'
|| _ref:=BB.sqlint(K__WAR.REF_DOK);
   _nam:=form(K__WAR.REF_DOK-8);
   K__NOTY.cntx_psh();
   K__NOTY.use(_nam);
   K__NOTY.prefix();
   {? K__NOTY.seek(_ref,)
   || K__NOTY.win_edit('RED');
      K__NOTY.display();
      _ok:=1
   ?};
   K__NOTY.cntx_pop()
?};
{? _ok=0 || FUN.info('Nieznaleziono powiązanej noty dla controllingu.'@) ?}


\k_war_pnoty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Wyswietla pozycje noty dla controllingu powiazane z zapisami dla controllingu (K__WAR)
::  OLD: \k_war_pnoty/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
{? K__WAR.REF_DOK<>'' & 6+K__WAR.REF_DOK='notyco'
|| _ref:=BB.sqlint(K__WAR.REF_DOK);
   _nam:=form(K__WAR.REF_DOK-8);
   K__NOTY.cntx_psh(); K__PNOTY.cntx_psh();
   K__NOTY.use(_nam);
   K__PNOTY.use('notypo'+(_nam+2));
   K__NOTY.prefix();
   {? K__NOTY.seek(_ref,)
   || UD_POM.WXMODEL:=K__NOTY.SKID_MBN;
      SKID_MBP.cntx_psh();
      SKID_MBP.index('LP'); SKID_MBP.prefix(K__NOTY.SKID_MBN);
      {? SKID_MBP.first()
      || {!
         |? ($('POMOC.K'+$SKID_MBP.LP+':=_a'))(SKID_MBP.NAZ);
            exec('ud_pom_wym_en','control',SKID_MBP.LP,1);
            SKID_MBP.next()
         !}
      ?};
      {! _i:=SKID_MBP.LP+1..5
      |! ($('POMOC.K'+$_i+':=_a'))('');
         exec('ud_pom_wym_en','control',_i,0)
      !};
      SKID_MBP.cntx_pop();

      K__PNOTY.index('K__NOTY');
      K__PNOTY.prefix(K__NOTY.ref());
      K__PNOTY.win_sel('WER');
      K__PNOTY.hdr_sel();
      K__PNOTY.hdr_sel(' - %1'@[K__NOTY.SYM]);
      K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K1],K__PNOTY,'WYM1','SYMBOL');
      K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K2],K__PNOTY,'WYM2','SYMBOL');
      K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K3],K__PNOTY,'WYM3','SYMBOL');
      K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K4],K__PNOTY,'WYM4','SYMBOL');
      K__PNOTY.fld_opt('WER','col_name="%1"'[POMOC.K5],K__PNOTY,'WYM5','SYMBOL');
      K__PNOTY.select(,,,'DPU:D');
      _ok:=1
   ?};
   K__NOTY.cntx_pop(); K__PNOTY.cntx_pop()
?};
{? _ok=0 || FUN.info('Nieznaleziono powiązanej noty dla controllingu.'@) ?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 cdbeb45ba4324e601502f428f33077f60b0bc3a1a571a63afd2568cb1c54eb11ee331490c43143f4cdb7c0c71def849b423c77c27e7822438f53c9d430318debdb2d8ee2e3f338e716e0a2727a4c52128c3fb2598ce16a31092674d65982ea193b81140b50a9e337cf0d8b7e0472199530a77abea614bcce5258129a62641ce1

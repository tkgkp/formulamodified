:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_rbn_sakt.fml
:: Utworzony: 27.01.2020
:: Autor: AFI
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_RBN_SAKT - Wysyłanie e-maili z informacją o aktywności rachunków bankowych kontrah.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [19.42]
:: OPIS: Wysyłanie e-maili z informacją o aktywności rachunków bankowych kontrahenta
::       główna formuła czynności ZWS_RBN_SAKT
::----------------------------------------------------------------------------------------------------------------------
::# properties=SEND
::# kind=WE, symbol=ODBIORCA, type=STRING, name=Odbiorca powiadomienia e-mail, required=N

_mp:=params_get().mp;
_in:=params_get().in;
_email:={? var_press('ODBIORCA',_in)>0 || _in.ODBIORCA || '' ?};

_data:=date();
SKID_RBK.cntx_psh();
SKID_RBK.index('WEKTOR');
SKID_RBK.prefix('KH');
{? SKID_RBK.first()
|| _obj:=exec('obj','blp');
   _obj.API:=0;
   _obj.CHKGRVAT:=1;
   _dane:=exec('alert_obj','blp');
   KH.cntx_psh();
   KH.index('KOD'); KH.prefix();
   {!
   |? {? SKID_RBK.WAL=FINFO.NAROD & KH.seek(SKID_RBK.REF,) & KH.TYP='P'
      || _obj.check('B',KH.SNIP,_data-1,SKID_RBK.N,0);
         _akt:=_obj.ACTIVE;
         _obj.check('B',KH.SNIP,_data,SKID_RBK.N,0);
         {? _obj.ACTIVE<>'T'
         || _dane.add_rb('N')
         ?};
         {? _akt<>_obj.ACTIVE
         || _dane.add_rb({? _obj.ACTIVE='T' || '1' || '0' ?})
         ?}
      ?};
      SKID_RBK.next()
   !};
   KH.cntx_pop();

   {? _dane.toSend()
   || _args:=exec('add_email_a','#mailbox');
      _args.FROM:=_in.FROM;
      _args.SENDER:=_in.SENDER;
      _args.REPLYTO:=_in.REPLYTO;
      _args.RCV:=_email;
      _args.SUB:='Zmiany aktywności rachunków bankowych';
      _args.BODYH:=_dane.email();
      exec('add_email','#mailbox',_args)
   ?}
?};
SKID_RBK.cntx_pop();

_mp.done();
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_desc:='Wyślij powiadomienie e-mail o aktywności rachunków bankowych kontrahenta'@@;
_desc


:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 89918c17d9c7e4acf7761644400d37d5236a912e85601534689123d79ae2bd8ddeba081611a0f3765a692ae47e89c05cab0f54daabf34b1802a6f928fc74c186e16a45936a5bf9fea69ef1b9c0ff352c7b44de940d96b50f3eef1a66bfa871bce75327c64a35eeaf4fc1043b1bfc4a7ff239e5e5d5e508f078f1a66d4be6a706

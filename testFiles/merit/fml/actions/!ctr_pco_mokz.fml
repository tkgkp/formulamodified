:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pco_mokz.fml
:: Utworzony: 09.02.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PCO_MOKZ - Otwarcie okresu controllingowego
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Otwarcie okresu controllingowego - główna formuła czynności CTR_PCO_MOKZ.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::# kind=WE,   symbol=OKRO_F,   type=_OKRO_F,   name=Wskazanie na okres obrachunkowy,  required=N, keyref=T
::# kind=WY,   symbol=OKRO_F,   type=_OKRO_F,   name=Wskazanie na okres obrachunkowy,  required=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
_ok:=0;
{? _mp.pathProc()
|| {? ~SSTALE.AO
   || FUN.info('Nie wybrano okresu w stałych systemu.'@)
   || _out.OKRO_F:=SSTALE.AO;
      _mp.save(,_out);
      _ok:=exec('open','!ctr_pco_mokz',SSTALE.AO)
   ?}
|? _mp.pathTodo()
|| {? var_press('OKRO_F',_in)>0
   || _out.OKRO_F:=_in.OKRO_F;
      _mp.save(,_out);
      _ok:=exec('open','!ctr_pco_mokz',_in.OKRO_F)
   || FUN.info('Nie wskazano okresu do otwarcia.'@)
   ?}
|? (6+_akcja)='otwórz'
|| {? var_press('OKRO_F',_in)>0
   || _out.OKRO_F:=_in.OKRO_F;
      _mp.save(,_out);
      _ok:=exec('open','!ctr_pco_mokz',_in.OKRO_F)
   ?}
?};
{? _ok>0
|| _mp.done()
|? _ok<0
|| _mp.error()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Otwórz awaryjnie okres'@@;
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('OKRO_F',_in)
|| OKRO_F.cntx_psh();
   OKRO_F.prefix();
   {? OKRO_F.seek(_in.OKRO_F,)
   || _desc:='Otwórz awaryjnie okres: %1 %2.'@@[OKRO_F.NAZ,OKRO_F.ROK().NAZ]
   ?};
   OKRO_F.cntx_pop()
?};
_desc


\open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła otwiera okres w obszarze controlling
::   WE: _a - wskazanie na rekord OKRO_F
::   WY: 0 - nie udało się, 1 - udało się
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
OKRO_F.cntx_psh();
OKR_OBSZ.cntx_psh();
{? OKRO_F.seek(_a)
|| OKR_OBSZ.index('OKRO_F2');
   _ctr:=exec('szuk_b_dom','parses','CTR');
   OKR_OBSZ.prefix(_ctr,OKRO_F.ref());
   {? OKR_OBSZ.first()
   || _okres:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ;
      {? OKRO_F.ZAM_CON='T'
      || {? FUN.ask('Otworzyć awaryjnie okres: %1?'@[_okres])
         || _wy:=exec('hist_open','!ctr_pco_mokz',_a)
         ?}
      || FUN.emsg('Okres %1 nie jest zamknięty controllingowo.'@[_okres])
      ?}
   || _wy:=-1;
      FUN.emsg('Nie znaleziono okresu w obszarze controlling.'@)
   ?}
|| _wy:=-1;
   FUN.emsg('Nie znaleziono wskazanego okresu.'@)
?};
OKR_OBSZ.cntx_pop();
OKRO_F.cntx_pop();
_wy


\hist_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zapis w historii zamknięć i awaryjnych otwarć okresów informacji o otwarciu awaryjnym
::   WE: _a - wskazanie na OKRO_F
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
_ctr:=exec('szuk_b_dom','parses','CTR');
{? _ctr & _a
|| K__HIST.cntx_psh();
   OKRO_F.cntx_psh();
   OKRO_F.prefix();
   {? OKRO_F.seek(_a)
   || K__HIST.prefix();
      K__HIST.blank(1);
      K__HIST.STATUS:='O';
      K__HIST.STATOP:='Otwarcie';
      K__HIST.OKRO_F:=_a;
      K__HIST.B_DOMAIN:=_ctr;
      K__HIST.DATA:=date();
      K__HIST.CZAS:=time();
      K__HIST.USER:=OPERATOR.USER;
      K__HIST.win_edit('RED_OPEN');
      K__HIST.OKRO_F();
      OKRESY.NAZWA:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ;
      {? K__HIST.edit()
      || OKRO_F.ZAM_CON:='N';
         {? OKRO_F.put() & K__HIST.add() || _wy:=1 ?}
      ?}
   ?};
   OKRO_F.cntx_pop();
   K__HIST.cntx_pop()
?};
_wy


\br_k_hist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na rekord przed w tabeli K__HIST w oknach dla okresów środków
::----------------------------------------------------------------------------------------------------------------------
{? K__HIST.OKRO_F().ZAM_CON<>'T'
|| K__HIST.actions(cur_win(),'O:O',,1)
|| K__HIST.actions(cur_win(),'',,1)
?};
ROK_F.cntx_psh();
UD_POM.K2_OPIS:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ;
ROK_F.cntx_pop();
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:11 234899173b0b23cd4bc5cb157cbb8cf2190e24912d4d441b795ec3826b5d7c6db3b47519e521198e3b797a4ea09b69ac76e2200a55654c3c71d1452ccdae31bc0fde0720a569dbe1a73f53b83802626c6d8804e6f5f384866dd86027b2b691f82c856dac0470a8b3e8850bdc6ac2c65663421cc7f7266766f25a313960160925

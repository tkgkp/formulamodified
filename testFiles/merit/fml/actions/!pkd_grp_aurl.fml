:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_grp_aurl.fml
:: Utworzony: 04.03.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_GRP_AURL - Gr. akt. kart urlopowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Gr. akt. kart urlopowych - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \akt_prac/kart_url.fml - Zmiana w obsłudze.
::  OLD: \zal_kart/kart_url.fml - Zmiana w obsłudze.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::
::FUN.info(
::   'Aktualizacja kart urlopowych jest operacją składającą się z następujących kroków:\n'
::   '1. Określenie roku, za który będą zakładane / aktualizowane karty urlopowe.\n'
::   '2. Wybór pracowników, dla których przeprowadzone zostaną obliczenia.\n'
::   '3. Właściwe wprowadzenie informacji do bazy danych.\n'@
::);

_rok:=0;
_PAR:=tab_tmp(,'ROK','INTEGER','Rok kalendarzowy'@);
_we:=_PAR.mk_edit('Podaj');
_PAR.win_esep(_we,'Dane podstawowe'@);
_PAR.win_efld(_we,,'ROK',,,4,0,,,,'Rok, za który będą aktualizowane karty urlopowe.'@);
_PAR.efld_opt(_we,'mark=1',,'ROK');
exec('ok_esc','#window',_PAR,_we);
_PAR.win_edit(_we);
_PAR.ROK:=date()~1;
{? _PAR.edit(
      "  _PAR:=cur_tab();
         {? (_chk:=__CHK.record(_PAR,,'ROK'))<>''
         || return(_chk)
         ?};

         _maxr:=5+date()~1;
         {? _PAR.ROK<1900 | _PAR.ROK>_maxr
         || return(__CHK.err_fld(_PAR,'ROK',1,'Dozwolone wartości z przedziału %1-%2.'@ ['1900',$_maxr]))
         ?};
         ''
      "
   )
|| _rok:=_PAR.ROK
?};
&_we;
obj_del(_PAR);
&_PAR;

{? _rok=0
|| return()
?};

KST_URL.cntx_psh();
KST_URL.index('KST_URL');
KST_URL.prefix(_rok);
{? ~KST_URL.first()
|| FUN.emsg('Brak wymiarów urlopów na rok %1.\nUzupełnij brakujące dane i spróbuj ponownie.'@ [$_rok])

|? _args:=exec('wybierz_args','pracownik');
   _args.DOMAIN:='PKD';
   _args.F_ZATR:='P';
   _args.VIEW:='';
   _args.SQL_FROM:='';
   _args.SQL_WHERE:=
      'extract(year from "P".DZA)<=%1 and ("P".DZ is null or %1<=extract(year from "P".DZ)) and '
      '"P".REFERENCE not in (select KART_URL.P from KART_URL where KART_URL.ROK=%1 and KART_URL.STATUS<>\'A\')'
      [$_rok];
   _args.FML_TEST:='';
   _args.HDR_SEL:='Grupowa aktualizacja kart urlopowych';
   _ret:=exec('wybierz','pracownik',_args);
   ~(_ret.STATUS='' & _ret.P.first())
|| ~~

|| OSOBA.cntx_psh();
   OSOBA.prefix();
   P.cntx_psh();
   P.prefix();
   KART_URL.cntx_psh();
   KART_URL.index('PRAC_ROK');
   KART_URL.prefix();
   KART_URL.win_edit('RED');

   _P:=tab_tmp(1,
::    Pola identyfikujące pracownika.
      'T','STRING['+$MS.fld_len(P,'T')+']',MS.name(P,'T'),
      'IP','INTEGER',MS.name(P,'IP'),
      'NAZWISKO','STRING['+$MS.fld_len(OSOBA,'NAZWISKO')+']',MS.name(OSOBA,'NAZWISKO'),
      'PIERWSZE','STRING['+$MS.fld_len(OSOBA,'PIERWSZE')+']',MS.name(OSOBA,'PIERWSZE'),
::    Informacja o przetwarzaniu.
      'STATUS','STRING[40]','Status'@,
      'KART_URL','STRING[16]','KART_URL.ref()'
   );
   _P.fld_fml('T','DISPLAY_FORMAT',P.fld_fml('T','*DISPLAY_FORMAT'));

   _lp:=0;
   _size:=_ret.P.size();
   {!
   |? _lp+=1;
      _proc:=ceil(100*_lp/_size);
      progress(_proc,'Trwa aktualizacja kart urlopowych %1%%'@ [form(_proc,3,0)],'Przetwarzanie'@);
      {? P.seek(_ret.P.SQL)
      || P.OSOBA();
         _P.blank();
         _P.T:=P.T;
         _P.IP:=P.IP;
         _P.NAZWISKO:=OSOBA.NAZWISKO;
         _P.PIERWSZE:=OSOBA.PIERWSZE;
         _crcb:={? KART_URL.find_key(P.ref(),_rok) || KART_URL.crc() || ~~ ?};
         exec('aktualizuj_rok','kart_url',_rok,1,1,1);
         _crca:={? KART_URL.find_key(P.ref(),_rok) || KART_URL.crc() || ~~ ?};
         {? _crca<>~~
         || _P.KART_URL:=$KART_URL.ref()
         ?};
         _P.STATUS:=
            {?  _crcb=~~ & _crca=~~
            || 'Karty urlopowej nie udało się utworzyć.'@
            |? _crcb=_crca
            || {? KART_URL.STATUS='A'
               || 'Karta urlopowa nie wymagała aktualizacji.'@
               || 'Wyłączona automatyczna aktualizacja karty urlopowej.'@
               ?}
            |? _crcb=~~ & _crca<>~~
            || 'Karta urlopowa została utworzona.'@
            |? _crcb<>~~ & _crca<>~~ & _crcb<>_crca
            || 'Karta urlopowa została zaktualizowana.'@
            || ''
            ?};
         _P.add()
      ?};
      _ret.P.next()
   !};
   prgs_clr();

:  Przygotowanie okienek z raportem wykonania
   _ws:=_P.mk_sel('Raport z przetwarzania'@,,,'#premnauklicz',,,,,'U');
   _P.win_fld(_ws,,'T',,,,,,,,MS.comment(P,'T'));
   _P.win_fld(_ws,,'IP',,,,,,,,MS.comment(P,'IP'));
   _P.win_fld(_ws,,'NAZWISKO',,,,,,,,MS.comment(OSOBA,'NAZWISKO'));
   _P.win_fld(_ws,,'PIERWSZE',,,,,,,,MS.comment(OSOBA,'PIERWSZE'));
   _P.win_fld(_ws,,'STATUS',,,,,,,,'Status operacji'@);
   _P.win_act(_ws,,'Formuła','Pokaż'@@,,'Wyświetlenie aktualnych wartości karty urlopowej'@,
      "  _P:=cur_tab(1,1);
         {? KART_URL.seek(_P.KART_URL)
         || exec('kart_url_sumuj','kart_url');
            KART_URL.display()
         ?}
      ",,1,,,,'P');
   _P.win_act(_ws,,'Formuła','Zmian&y'@@,,'Informacje o modyfikacjach bieżącego zapisu'@,
      "  _P:=cur_tab(1,1);
         {? KART_URL.seek(_P.KART_URL)
         || exec('zmiany','#syslog',KART_URL)
         ?}
      ",,,,,,'Y');
   _P.win_act(_ws,,'Rekord',,,,
      "  _P:=cur_tab(1,1);
         _ws:=_P.win_sel('?');
         _ga:={? _P.KART_URL='' || 'PY' || '' ?};
         _P.actions_grayed(_ws,_ga)
      "
   );
   _P.win_sel(_ws);
   _P.select();

   KART_URL.cntx_pop();
   P.cntx_pop();
   OSOBA.cntx_pop()

?};
KST_URL.cntx_pop();
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 3b0e728cc2a3638257211bb46af22619a663a534889f6a50aaf85973f8d14fa1c7ab15ad08cf79a446fefc0923f84837fbe1e4a85e889166d958e3ee79e4ca2478b930660030c9381b9e5a77aa1ef0bd2c2bcb0b76f1749aaa1152dc069cdea64f418724c3cb6f5c96a64bde50006ec6ad71258365baaa2a96cf843d0c9563eb

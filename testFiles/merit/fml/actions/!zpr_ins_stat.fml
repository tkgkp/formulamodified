:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zpr_ins_stat.fml
:: Utworzony: 15.04.2019
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły czynności ZPR_INS_STAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Czynność informacji o użyciu czynności (ZPR_INS_STAT) - formuła główna
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE,GRP_FIRM

:: PARAMETRY WE:
::# kind=WE, symbol=ROK, type=NUMBER, name=Rok do analizy, required=N
{? var_pres('ROK',_in)<>type_of(~~) & var_pres('ROK',_in)<>type_of(0) || return() ?};
{? var_pres('ROK',_in)=type_of(~~) || _in.ROK:=0 ?};

::# kind=WE, symbol=MIESIAC, type=NUMBER, name=Miesiąc do analizy, required=N
{? var_pres('MIESIAC',_in)<>type_of(~~) & var_pres('MIESIAC',_in)<>type_of(0) || return() ?};
{? var_pres('MIESIAC',_in)=type_of(~~) || _in.MIESIAC:=0 ?};

::# kind=WE, symbol=WSTECZ, type=NUMBER, name=Ile miesięcy wstecz analizować, required=N
{? var_pres('WSTECZ',_in)<>type_of(~~) & var_pres('WSTECZ',_in)<>type_of(0) || return() ?};
{? var_pres('WSTECZ',_in)=type_of(~~) || _in.WSTECZ:=0 ?};

::# kind=WE, symbol=FILTER, type=MEMO, name=Lista czynności/dziedzin, required=N, fml_val="exec('filter_edit','!zpr_ins_stat',_a)"
{? var_pres('FILTER',_in)<>type_of(~~) & var_pres('FILTER',_in)<>type_of('') || return() ?};
{? var_pres('FILTER',_in)=type_of(~~) || _in.FILTER:='' ?};

:: PARAMETRY WY:
::# kind=WY, symbol=BODYT, type=MEMO, name=Treść w formacie tekstowym, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::# kind=WY, symbol=FILE_A, type=BLOB, name=Wynik w postaci załącznika, required=N
::# kind=WY, symbol=SUB, type=STRING, name=Temat wiadomości, required=N
::# kind=WY, symbol=ILOSC, type=NUMBER, name=Całkowita ilość uruchomień, required=N

::# required ROK,MIESIAC|WSTECZ|*

_can_continue:=1;

{? _mp.isService()=0 & _in.WSTECZ=0 & _in.ROK=0 & _in.MIESIAC=0
|| _wstecz:=exec('edit_number','#edit',1,'Ile miesięcy wstecz analizować?'@,0,0);
   {? type_of(_wstecz)=type_of(0)
   || _in.WSTECZ:=_wstecz
   || _can_continue:=0
   ?}
?};

{? _can_continue=0
|| _mp.cancel()
?};

:: Walidacja parametrów
{? _can_continue>0 & _in.WSTECZ<0
|| _can_continue:=0;
   _msg:='Parametr: %1 musi być równy bądź większy od zera'@['WSTECZ'];
   {? _mp.isService()=0
   || FUN.emsg(_msg)
   ?};
   _mp.error(_msg)
?};

{? _can_continue>0 & _in.ROK<0
|| _can_continue:=0;
   _msg:='Parametr: %1 musi być równy bądź większy od zera'@['ROK'];
   {? _mp.isService()=0
   || FUN.emsg(_msg)
   ?};
   _mp.error(_msg)
?};

{? _can_continue>0 & _in.MIESIAC<0
|| _can_continue:=0;
   _msg:='Parametr: %1 musi być równy bądź większy od zera'@['MIESIAC'];
   {? _mp.isService()=0
   || FUN.emsg(_msg)
   ?};
   _mp.error(_msg)
?};

{? _can_continue>0 & _in.MIESIAC>0 & _in.ROK=0
|| _can_continue:=0;
   _msg:='Parametr: %1 musi być większy od zera'@['ROK'];
   {? _mp.isService()=0
   || FUN.emsg(_msg)
   ?};
   _mp.error(_msg)
?};

{? _can_continue>0 & _in.MIESIAC=0 & _in.ROK>0
|| _can_continue:=0;
   _msg:='Parametr: %1 musi być większy od zera'@['MIESIAC'];
   {? _mp.isService()=0
   || FUN.emsg(_msg)
   ?};
   _mp.error(_msg)
?};

{? _can_continue>0
||
   _tab:=tab_tmp(3
      ,'ACT_UID','STRING[20]','Symbol czynności'
      ,'USR_GUID','STRING[100]','ID użytkownika'
      ,'WHEN','STRING[100]','Kiedy?'
      ,'ROK','INTEGER','Rok'
      ,'MIESIAC','INTEGER','Miesiąc'
      ,'ACT_NAME','STRING[100]','Nazwa czynności'
      ,'USR_KOD','STRING[20]','Kod użytkownika'
      ,'USR_DANE','STRING[100]','Dane użytkownika'
      ,'COUNTER','REAL','Ilość uruchomień'
   );
   _ndx:=_tab.ndx_tmp('',,'ROK',,,'MIESIAC',,,'ACT_UID',,,'USR_KOD',,);
   _args:=obj_new('TAB'
                  ,'ROK'
                  ,'MIESIAC'
                  ,'FILTER'
                  ,'ILOSC'
                  ,'STARTD'
                  ,'ENDD'
                  ,'VISITED');
   _args.TAB:=_tab;
   _args.ROK:=_in.ROK;
   _args.MIESIAC:=_in.MIESIAC;
   _args.FILTER:=_in.FILTER;
   _args.ILOSC:=0;
   _args.VISITED:=exec('ref_table','#table');

   _startd:=date(0,0,0);
   _endd:=date(0,0,0);
:: Wymyślam maskę startową i końcową
   {? _in.ROK>0 & _in.MIESIAC>0
   || _startd:=date(_in.ROK,_in.MIESIAC,1);
      _endd:=date(_in.ROK,_in.MIESIAC,0);
      _mask_start:=4+B_USEHIS.name()+(($(_in.ROK))+2)+form(_in.MIESIAC,-2);
      _mask_end:=_mask_start
   ||
      _mask_start:='';
      _ar:=date()~1;
      _am:=date()~2;

      {? _in.WSTECZ>0
      || {! _it:=1.._in.WSTECZ
         |! _am-=1;
            {? _am=0
            || _am:=12;
               _ar-=1
            ?}
         !}
      ?};
      _startd:=date(_ar,_am,1);
      _mask_start:=4+B_USEHIS.name()+(($(_startd~1))+2)+form(_startd~2,-2);

      {? _in.WSTECZ>0
      || _ar:=date()~1;
         _am:=date()~2;
         _am-=1;
         {? _am=0
         || _ar-=1;
            _am:=12
         ?};
         _endd:=date(_ar,_am,0)
      || _endd:=date()
      ?};
      _mask_end:=4+B_USEHIS.name()+(($(_endd~1))+2)+form(_endd~2,-2)
   ?};
   _out.SUB:='Informacja o użyciu czynności w firmie: %1 od: %2 do: %3'[REF.FIRMA().OPIS,_startd$8,_endd$8];

   _args.STARTD:=_startd;
   _args.ENDD:=_endd;

:: Uruchamiam zasilanie z masek
   _fml:="
      exec('mask_load','!zpr_ins_stat',_a,_b)
   ";
   _tm_start:=SYSLOG.tm_stamp();
   exec('for_each_mask','#table',B_USEHIS,_fml,_mask_start,_mask_end,_args,1);
   _tm_end:=SYSLOG.tm_stamp();

   _benchmark:=_tm_end-_tm_start;
   _benchmark:=_benchmark/exec('second','#tm_stamp');
   {? _benchmark#4=0
   || _benchmark+=0.0001
   ?};

   _tab.index(_ndx);
   _done:=1;
   {? _mp.isService()=0
   || exec('win','!zpr_ins_stat',_args,_out.SUB);
      _tab.prefix();
      _can_continue:=_tab.select()
   ?};

   {? _can_continue=0
   || _mp.cancel()
   ?};

   {? _can_continue>0
   ||
      _out.ILOSC:=_args.ILOSC;

::    Tworzę zawartość tekstową
      _out.BODYT:='Analiza w firmie %1 od: %2 do: %3. '
                  'Suma uruchomień: %4. '
                  'Wykonanie analizy trwało: %5 sek\n\n'@[REF.FIRMA().OPIS,_startd$8,_endd$8,$_args.ILOSC,form(_benchmark,,4)];
      _tab.cntx_psh();
      _tab.clear();
      {? _tab.first()
      || {!
         |? _out.BODYT+=_tab.WHEN+';';
            _out.BODYT+=_tab.ACT_UID+';';
            _out.BODYT+=_tab.USR_KOD+';';
            _out.BODYT+=$_tab.COUNTER+'\n';
            _tab.next()
         !}
      ?};

::    Tworzę zawartość HTML
      _out.BODYH:='Analiza w firmie <b>%1</b> od: <b>%2</b> do: <b>%3</b>. '
                  'Suma uruchomień: <b>%4</b>. '
                  'Wykonanie analizy trwało: <b>%5</b> sek<br><br>'@[REF.FIRMA().OPIS,_startd$8,_endd$8,$_args.ILOSC,form(_benchmark,,4)];

      _out.BODYH+='
        <table [[STYLE_TABLE]]>
        <tr [[STYLE_TABLE_TR]]>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%1</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%2</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%3</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%4</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%5</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%6</span></th>
        </tr>
      '['Kiedy?'@,'Czynność'@,'Nazwa czynności'@,'Użytkownik'@,'Dane użytkownika'@,'Ilość uruchomień'@];

      {? _tab.first()
      || {!
         |?
            _out.BODYH+='
                <tr [[STYLE_TABLE_TR]]>
                <td [[STYLE_TABLE_TD]]>%1</td>
                <td [[STYLE_TABLE_TD]]>%2</td>
                <td [[STYLE_TABLE_TD]]>%3</td>
                <td [[STYLE_TABLE_TD]]>%4</td>
                <td [[STYLE_TABLE_TD]]>%5</td>
                <td [[STYLE_TABLE_TD]]>%6</td>
                </tr>
            '[_tab.WHEN,_tab.ACT_UID,_tab.ACT_NAME,_tab.USR_KOD,_tab.USR_DANE,$_tab.COUNTER];
            _tab.next()
         !}
      ?};
      _out.BODYH+='</table>';


::    Eksport do pliku CSV
      _name:='act_stat_'+$SYSLOG.tm_stamp()+'.csv';
      _can_continue:=_tab.export(_name,,,'UTF-8',,
                                    'WHEN',,1,,
                                    'ACT_UID',,2,,
                                    'ACT_NAME',,3,,
                                    'USR_KOD',,4,,
                                    'USR_DANE',,5,,
                                    'COUNTER',,6,);
      {? _can_continue>0
      || {? _mp.bl_add('FILE_A',exec('kind_out','#b_port'),_name,1)>0
         || ferase(_name,1)
         ?}
      ?};
      _tab.cntx_pop();

::    Obsługa done
      _mp.save(,_out);
      {? _done>0
      || _mp.done()
      ?}
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Formuła na opis TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('ROK',_in)=type_of(~~) || _in.ROK:=0 ?};
{? var_pres('MIESIAC',_in)=type_of(~~) || _in.MIESIAC:=0 ?};
{? var_pres('WSTECZ',_in)=type_of(~~) || _in.WSTECZ:=0 ?};

_startd:=date(0,0,0);
{? _in.ROK>0 & _in.MIESIAC>0
|| _startd:=date(_in.ROK,_in.MIESIAC,1)
|| _ar:=date()~1;
   _am:=date()~2-_in.WSTECZ;
   {? _am<=0
   || _ar-=1;
      {! _it:=1..fabs(_am)
      |! {? _it%*12=0
         || _ar-=1
         ?}
      !}
   ?};
   _startd:=date(_ar,_am,1)
?};
_desc:='Wykonaj analizę użycia czynności od: %1'@@[_startd$8];
_desc


\mask_load
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Ładuje do tabeli wynikowej liczniki
::   WE: _a - TABLE - uchwyt tabeli
::       _b - obj_new - argumenty funkcji
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_args:=_b;
_tab:=_args.TAB;

_result:=0;
_can_continue:=1;

B_USEHIS.cntx_psh();
B_USEHIS.index('ACTION');

_name:=B_USEHIS.name();
_ar:=#((2+$(date()~1))+(2+(_name+4)));
_am:=#(_name+2);

_date:=date(_ar,_am,1);

{? _date>=_args.STARTD & _date<=_args.ENDD
||
   {? _args.FILTER<>''
   ||
      _split:=spli_str(_args.FILTER,'\n');
      {? obj_len(_split)>0
      ||
         {! _it:=1..obj_len(_split)
         |!
            _uid:=_split[_it];
            {? _uid<>''
            ||
               B_USEHIS.prefix(REF.FIRMA,_uid);
               {? B_USEHIS.first()
               || {!
                  |? _can_continue:=exec('usehist2tab','!zpr_ins_stat',_args);
                     B_USEHIS.next() & _can_continue>0
                  !}
               ?}
            ?}
         !}
      ?}
   || B_USEHIS.prefix(REF.FIRMA,);
      {? B_USEHIS.first()
      || {!
         |? _can_continue:=exec('usehist2tab','!zpr_ins_stat',_args);
            B_USEHIS.next() & _can_continue>0
         !}
      ?}
   ?}
?};
B_USEHIS.cntx_pop();
{? _can_continue>0
|| _result:=1
?};
_result


\usehist2tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Dodaje do tabelki wynikowej jeden rekord
::   WE: _a - obj_new - argumenty funkcji
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_args:=_a;
_tab:=_args.TAB;
_visited:=_args.VISITED;

_result:=1;

{? _visited.r_find(B_USEHIS.ref())=0
|| _visited.add(B_USEHIS.ref());
   _tab.cntx_psh();
   _when:=B_USEHIS.DATE$8;
   _tab.prefix(B_USEHIS.ACT_UID,B_USEHIS.USR_GUID,_when,);
   {? _tab.first()=0
   || _tab.blank();
      _tab.ACT_UID:=B_USEHIS.ACT_UID;
      _tab.ACT_NAME:=B_USEHIS.ACT_NAME;
      _tab.USR_KOD:=B_USEHIS.USR_KOD;
      _tab.USR_DANE:=B_USEHIS.USR_DANE;
      _tab.USR_GUID:=B_USEHIS.USR_GUID;
      _tab.COUNTER:=B_USEHIS.COUNTER;
      _args.ILOSC+=B_USEHIS.COUNTER;
      _tab.WHEN:=_when;
      _tab.ROK:=B_USEHIS.DATE~1;
      _tab.MIESIAC:=B_USEHIS.DATE~2;
      _result:=_tab.add()
   || _tab.COUNTER+=B_USEHIS.COUNTER;
      _args.ILOSC+=B_USEHIS.COUNTER;
      _result:=_tab.put()
   ?};
   _tab.cntx_pop()
?};
_result


\win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Tworzy okno wertowania użytkowników w zakresie czasu
::   WE: _a - args - środowisko działania
::       _b - STRING - tytuł okienka
::   WY: STRING - uchwyt do okna
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

_env:=_a;
_title:=_b;

_wid:='#b_ins_stat';

_tab:=_env.TAB;

_wer:=_tab.mk_sel(_title,'P',0,_wid,1,1,,,'U');

_tab.win_fld(_wer,,'WHEN'  ,,,10,,,'Kiedy?'@,0);
_tab.win_fld(_wer,,'ROK'  ,,,10,,,'Rok'@,0);
_tab.win_fld(_wer,,'MIESIAC'  ,,,10,,,'Miesiąc'@,0);
_tab.win_fld(_wer,,'ACT_UID'  ,,,15,,,'Czynność'@,0);
_tab.win_fld(_wer,,'USR_KOD'  ,,,10,,,'Użytkownik'@,0);
_tab.win_fld(_wer,,'USR_DANE' ,,,40,,,'Dane użytkownika'@,0);
_tab.win_fld(_wer,,'COUNTER'  ,,,10,0,,'Liczba uruchomień'@,0);

_tab.win_act(_wer,,'Kolejność');

_fb:="
   sel_exit()
";
_tab.win_act(_wer,,'Formuła','&Zamknij'@@,,,_fb,,,,,,'Z');
_btn:=_tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Zamknij'@],'menu:Z',,,,,,'');
_tab.btn_opt(_btn,'tooltip=%1'[exec('help_red_esc','#window','S')]);

_tab.win_sel(_wer);
_wer


\filter_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Formuła na redagowanie parametru filter
::   WE: _a - STRING - aktualna wartość parametru
::   WY: STRING - wartość parametru
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_before:=_a;

_result:=~~;

_tab_dom:=tab_tmp(1
   ,'SYMBOL','STRING[20]','Symbol'@
   ,'NAME','STRING[30]','Nazwa'@
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx_dom:=_tab_dom.ndx_tmp(,,'SELECTED',,,'SYMBOL',,);

_tab_act:=tab_tmp(1
   ,'UID','STRING[20]','Symbol'@
   ,'NAME','STRING[30]','Nazwa'@
   ,'SELECTED','STRING[1]','Czy wybrano'
);
_ndx_act:=_tab_act.ndx_tmp(,,'SELECTED',,,'UID',,);

_tab_bef:=tab_tmp(1
   ,'VALUE','STRING[20]','Wartość'
);

{? type_of(_before)=type_of('')
||
   _split:=spli_str(_before,'\n');
   {? obj_len(_split)>0
   ||
      {! _it:=1..obj_len(_split)
      |!
         _val:=_split[_it];
         {? _val<>''
         || _tab_bef.blank();
            _tab_bef.VALUE:=_val;
            _tab_bef.add()
         ?}
      !}
   ?}
?};

exec('proenv','#b_proman');
_domain:=__proenv.DOM_LIC;
_domain.cntx_psh();
_domain.prefix();
{? _domain.first()
|| {!
   |? _tab_dom.blank();
      _tab_dom.NAME:=_domain.NAME;
      _tab_dom.SYMBOL:=_domain.SYMBOL;

      _tab_bef.prefix(_domain.SYMBOL,);
      {? _tab_bef.first()
      || _tab_dom.SELECTED:='T'
      ?};

      _tab_dom.add();
      _domain.next()
   !}
?};
_domain.cntx_pop();

B_ACTION.cntx_psh();
B_ACTION.index('B_DOMAIN');
B_ACTION.prefix('T');
{? B_ACTION.first()
|| {!
   |? _tab_act.blank();
      _tab_act.UID:=B_ACTION.UID;
      _tab_act.NAME:=B_ACTION.NAME;

      _tab_bef.prefix(B_ACTION.UID,);
      {? _tab_bef.first()
      || _tab_act.SELECTED:='T'
      ?};

      _tab_act.add();
      B_ACTION.next()
   !}
?};
B_ACTION.cntx_pop();

_sel:=_tab_dom.mk_sel('Dziedziny'@,'P',,'#ind_domflt',,,,,'U');
_tab_dom.win_fld(_sel,,'SELECTED',,,,,1,'Zaznaczony?'@,,'Czy wybrano? (T/N)'@,2,,"'T'","'N'");
_tab_dom.win_fld(_sel,,'SYMBOL',,,20,,,);
_tab_dom.win_fld(_sel,,'NAME',,,30,,,);
exec('filter_actions','!zpr_ins_stat',_tab_dom,_sel,,1);

_sel2:=_tab_act.mk_sel('Czynności'@,'P',,'#ind_actflt',,,,,'U');
_tab_act.win_fld(_sel2,,'SELECTED',,,,,1,'Zaznaczony?'@,,'Czy wybrano? (T/N)'@,2,,"'T'","'N'");
_tab_act.win_fld(_sel2,,'UID',,,20,,,);
_tab_act.win_fld(_sel2,,'NAME',,,30,,,);
exec('filter_actions','!zpr_ins_stat',_tab_act,_sel2,1,2);

_env:=obj_new('TAB_DOM','TAB_ACT','WER_DOM','WER_ACT');
_env.TAB_DOM:=_tab_dom;
_env.TAB_ACT:=_tab_act;
_env.WER_DOM:=_sel;
_env.WER_ACT:=_sel2;
params_set('env_fff',_env);

_grp:=_tab_dom.grp_make('Redakcja parametru'@,,'#ingrstatflt',,,,,'normal');

_after_rfr:="
   _env:=params_get().env_fff;
   _tab_dom:=_env.TAB_DOM;
   _tab_act:=_env.TAB_ACT;

   _tab_act.cntx_psh();
   _ref:=null();
   _tab_act.prefix(_tab_dom.SYMBOL);
   {? _tab_act.first()
   || _ref:=_tab_act.ref()
   ?};
   _tab_act.cntx_pop();
   {? _ref<>null()
   || _tab_act.seek(_ref)
   ?};
   grp_disp(_tab_act,_env.WER_ACT);
   ~~
";
_tab_dom.grp_sel(_grp,_tab_dom,_sel,,_after_rfr);

_tab_dom.grp_splt(_grp,,'horizontal','bottom',10);

_tab_dom.grp_sel(_grp,_tab_act,_sel2);

_tab_dom.win_sel(_grp);

{? _tab_dom.select()>0
||
   _tab_dom.cntx_psh();
   _tab_dom.index(_ndx_dom);
   _tab_dom.prefix('T');
   {? _tab_dom.first()
   || _result:='';
      {!
      |? _result+=_tab_dom.SYMBOL+'\n';
         _tab_dom.next()
      !}
   ?};
   _tab_dom.cntx_pop();

   _tab_act.cntx_psh();
   _tab_act.index(_ndx_act);
   _tab_act.prefix('T');
   {? _tab_act.first()
   || {? type_of(_result)<>type_of('')
      || _result:=''
      ?};

      {!
      |? _result+=_tab_act.UID+'\n';
         _tab_act.next()
      !}
   ?};

   {? type_of(_result)<>type_of('')
   || _result:='***clean***'
   ?};
   _tab_act.cntx_pop();
   ~~
?};
_result


\filter_actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Akcje dla redagowania pola FILTER
::   WE: _a - tab_tmp - tabela tymczasowa z polem SELECTED
::       _b - STRING - akronim okna
::       _c - INTEGER - [0]/1 - czy dodawać akceptuja i anuluja
::       _d - INTEGER - 1 - Dziedziny, 2-Czynności
::  TAG: <PRYWATNA>
:: ~OST: INWINBTN
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_sel:=_b;
_akc:=0;
{? var_pres('_d')<=0 || _d:=1 ?};
{? var_pres('_c')=type_of(0)
|| _akc:=_c
?};

:: Zaznacz
_fb:="
   _tab:=cur_tab(1,1);
   _tab.SELECTED:='T';
   _tab.put();
   1
";
_gr1:="
   1
";
_gr2:="
   ~~
";
_tab.win_act(_sel,,'Formuła','&Zaznacz'@@,,{? _d=1 || 'Zaznacz dziedzinę'@ || 'Zaznacz czynność'@ ?},
             _fb,,,1,_gr1,_gr2,'Z');
_tab.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['&Zaznacz'@],'menu:Z',,,,,,'noempty');

:: Odznacz
_fb:="
   _tab:=cur_tab(1,1);
   _tab.SELECTED:='N';
   _tab.put()
";
_tab.win_act(_sel,,'Formuła','&Odznacz'@@,,{? _d=1 || 'Odznacz dziedzinę'@ || 'Odznacz czynność'@ ?},_fb,,,1,,,'O');
_tab.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['&Odznacz'@],'menu:O',,,,,,'noempty');

{? _akc>0
||
:: Akceptuj
   _fb:="
      sel_exit()
   ";
   _tab.win_act(_sel,,'Formuła','&Akceptuj'@@,,,_fb,,,,,,'A');
   _btn:=_tab.win_btn(_sel,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Akceptuj'@],
                      'menu:A',,,,,,'noempty');
   _tab.btn_opt(_btn,'tooltip=%1'[exec('help_red_ok','#window','P')]);
   _btn:=_tab.win_btn(_sel,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   _tab.btn_opt(_btn,'tooltip=%1'[exec('help_red_esc','#window','A')])
?};
:: Rekord
_fb:="
   _lastdraw:=1;
   {? var_pres('_a')=type_of(0)
   || _lastdraw:=_a
   ?};
   _grayed:='';
   _tab:=cur_tab(1,1);
   _sel:=cur_win(1,1);

   {? _lastdraw>0
   ||
      {? _tab.sel_size()=0
      ||
::       Pojedyncze zaznaczenie
         {? _tab.SELECTED='T'
         || _tab.actions(_sel,,'O',1);
            _grayed+='Z'
         |? _tab.SELECTED='N' | _tab.SELECTED=''
         || _tab.actions(_sel,,'Z',1);
            _grayed+='O'
         ?}
      ||
::       Zaznaczenie grupowe
         _tab.actions(_sel,,'Z',1)
      ?};

      _anyselected:=0;
      _current:=_tab.ref();
      _tab.first();
      {!
      |? _anyselected:=(_tab.SELECTED='T');
         _tab.next() & ~_anyselected
      !};
      _tab.seek(_current);
      _tab.actions_grayed(_sel,_grayed)

   ?}
";
_tab.win_act(_sel,,'Rekord',,,,_fb);
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 a62ceebae1378849f254ea600ed7fcdbc2c237b275a2c02f7ca8effd4204ccef003e083a8ef5e7bb065a9f077dae989806fdee3983e4720ce453a93ec124dfa979d0316f171f1f2ace17b0425cf8803a2b43acb92f510bc32efcfcc1736f714256f78b04289fa0f7afe7f5406226d587ab12537a7a1a9d98dfd9fb011e0e6887

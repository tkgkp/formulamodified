:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zbl_bil_dokk.fml
:: Utworzony: 24.08.2020
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa czynności aktualizacji danych o kotrahentach w usłudze Businesslink
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Formuła główna czynności aktualizacji kontrahentów (ZBL_BIL_DOKK)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_action:=_mp.akcja();

::# properties=SERVICE

::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, BŁĄD)", required=N
{? var_pres('RESULT',_out)<>type_of(~~) & var_pres('RESULT',_out)<>type_of('') || return() ?};
::# kind=WY, symbol=DESC, type=MEMO, name="Opis wyniku", required=N
{? var_pres('DESC',_out)<>type_of(~~) & var_pres('DESC',_out)<>type_of('') || return() ?};

_result:=exec('partners_find','!zbl_bil_dokk',~(_mp.isAutoRun() | _mp.isService()));
{? _result.STATUS | _mp.isService()
|| _out.RESULT:={? _result.STATUS || 'OK' || 'BŁĄD' ?};
   _out.DESC:=_result.DESC;
   _mp.save(,_out);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Opis dla czynności aktualizacji danych o kontrahentach (ZBL_BIL_DOKK)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_desc:='Aktualizuj dane o kontrahentach w usłudze Businesslink'@@;
_desc


\partners_find
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Aktualizuje informacje o kontrahentach w usłudze Businesslink
::       - bez dialogu: tylko zarejestrowani w usłudze
::       - z dialogiem: można wybrać wszystkich (muszą mieć wpisany NIP), tylko zarejestrowanych w usłudze
::         (z punktu widzenia ERP) albo pobrać zmiany z chmury
::   WE: _a - czy wyświetlać dialogi/progress
::   WY: wynik przetwarzania, 1 - jeżeli ok, 0 - w przypadku błędów (np. brak kontaktu z portalem Businesslink)
::----------------------------------------------------------------------------------------------------------------------
_dialog:=_a;
_result:=obj_new('STATUS','DESC');
_result.STATUS:=1;
_result.DESC:='';

_can_continue:=1;

{? _dialog
||
::   _choice:=FUN.choice('Czy aktualizować dane kontrahentów?'@,1
::      ,'A&ktualizacja'@
::      ,'Cała kartoteka'@
::      ,'Tylko z zaproszeniami'@
::   );
   exec('czytaj','#stalesys',,XINFO,'BL_TP');
   _choice:=FUN.ask(
      'Czy aktualizować dane kontrahentów?\n\n'
      'Uwzględnione zostaną zmiany po stronie usługi Businesslink, '
      'które zostały wykonane po %1.'@[gsub(XINFO.BL_TP,'T',' ')]
   );
   {? _choice=0
   || _can_continue:=0
   || _mode:=_choice
   ?}
|| _mode:=1
?};

{? _can_continue>0
|| {? _mode=1
   || _res:={? _dialog || exec('PartnerGetManual','businesslink3',0) || exec('PartnerGet','businesslink3',0) ?};
      _result.STATUS:=_res.STATUS;
      {? _result.STATUS
      || _result.DESC:='Kontrahenci zostali zaktualizowani.';
         {? _dialog
         || FUN.info('Kontrahenci zostali zaktualizowani.'@)
         ?}
      || _result.DESC:=
            'Nie udało się zaktualizować wszystkich kontrahentów.'+
            {? _res.MESSAGE<>'' || '\n'+_res.MESSAGE || '' ?};
         {? _dialog
         || FUN.info(
               'Nie udało się zaktualizować wszystkich kontrahentów.'@+
               {? _res.MESSAGE<>'' || '\n'+_res.MESSAGE || '' ?}
            )
         ?}
      ?}
   ?}
?};

_result

:Sign Version 2.0 jowisz:1045 2023/12/11 09:04:08 bc28d981ba1cea6365e3d9b99fabd87850a8f42826f9a29abc2728b994a11be89f5f3f0d8d4ed2037e96eae604d1a649fd23dd3a9b39ccdc56d26a4ddbd30829f90ff2e6a3c4f20823a03c95b3d1f92f18cf976c58cf554712d74c2db2f65f0028acbb52de8caa6887409805254d4dc26b3bdf3c52ca56d31eebe3a795b33877

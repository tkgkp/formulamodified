:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pzsl.fml
:: Utworzony: 18.05.2016
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PZSL - Zależności służbowe
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zależności służbowe - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

:: utwórz panel zarządzania zależnościami
_wnd:=exec('mk_wnd','!zws_par_pzsl');

:: kontekst działania
_ctx:=obj_new('p_ref');
_ctx.p_ref:=null;
params_set('ctx',_ctx);

:: udostępnij dane
ZS_TYP.cntx_psh();
ZS_TYP.win_sel(_wnd);
ZS_TYP.select();
ZS_TYP.cntx_pop();
ZS_TYP.win_del(_wnd);
~~


\mk_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Tworzy panel zarządzania zależnościami służbowymi.
::   WE:
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
:: utwórz panel zarządzania zależnościami służbowymi
_wnd:=ZS_TYP.grp_make('Zależności służbowe'@,
:: przed otwarciem
   "params_exec('wnd_bo','!zws_par_pzsl')",
:: identyfikator
   '#zws_par_pzsl',,,
:: podczas zamykania
   "params_exec('wnd_oc','!zws_par_pzsl')"
);

:: dodaj nawigator dla typów zależności służbowych
ZS_TYP.grp_sel(_wnd,ZS_TYP,'WER',,
:: po zmianie bieżącego wiersza
   "params_exec('zs_typ_ar','!zws_par_pzsl')"
);

:: dodaj poziomy podział okienka
ZS_TYP.grp_splt(_wnd,,'horizontal','bottom');
:: dodaj okienko przeglądania zależności
ZS_TYP.grp_sel(_wnd,ZS_DEF,'WER',,
:: po zmianie bieżącego wiersza
   "params_exec('zs_def_ar','!zws_par_pzsl')",,,15,
:: przed obsługą
   "params_exec('zs_def_bs','!zws_par_pzsl',_a)",,,,,,
:: ustaw jako główne okno panelu
   1
);

:: dodaj pionowy podział okienka
ZS_TYP.grp_splt(_wnd,,'vertical','right');
:: dodaj okienko współpracowników
ZS_TYP.grp_sel(_wnd,P,'ZS_DEF',,
:: po zmianie bieżącego wiersza
   "params_exec('p_ar','!zws_par_pzsl')",,,,
:: przed obsługą
   "params_exec('p_bs','!zws_par_pzsl',_a)",
:: po obsłudze
   "params_exec('p_as','!zws_par_pzsl',_a)"
);

:: panel
_wnd


\wnd_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Przed otwarciem panelu zarządzania zależnościami służbowymi.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: parametry formuł grupy
params_set(params_get());

:: zachowaj stan kartoteki osobowej
P.cntx_psh();
OSOBA.cntx_psh();

:: przygotuj tabele
exec('wnd_bo','zs_lib');

:: odrysuj okno zależności
grp_disp(ZS_TYP,'WER',1);
1


\wnd_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Podczas zamykania panelu zarządzania zależnościami służbowymi.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: przywróć stan tabel
exec('wnd_oc','zs_lib');
exec('def_dnd_setup','zs_lib','WER',0);
OSOBA.cntx_pop();
P.cntx_pop();
1


\zs_typ_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Po odświeżeniu wiersza w oknie WER tabeli ZS_TYP.
::   WE: _a STRING - akronim okienka współpracowników
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: parametry dla formuł grupy
params_set(params_get());

_aid:='';
{? ZS_TYP.ZS_TYP<>0
|| _aid+='WJC'
?};
::{? ZS_TYP.OK<>'N' & _aid*'W'=0
::|| _aid+='W'
::?};
ZS_TYP.cntx_psh();
ZS_TYP.index('TREE');
ZS_TYP.prefix(exec('ref_firma','ustawienia'),0);
{? ZS_TYP.size()<=1 & _aid*'J'=0
|| _aid+='J'
?};
ZS_TYP.cntx_pop();
:: nie pozwalaj usunąć domyślnego
:: {? ZS_TYP.DOMYSLNY='T'
:: || _aid+='U'
:: ?};
ZS_TYP.actions_grayed('WER',_aid);
exec('def_dnd_setup','zs_lib','WER',ZS_TYP.ZS_TYP=0);

:: odrysuj okno zależności
grp_disp(ZS_DEF,'WER',1,1);
ZS_DEF.P().OSOBA();
~~


\zs_def_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Po odświeżeniu wiersza w oknie WER tabeli ZS_DEF.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: parametry dla formuł grupy
params_set(params_get());

_aid:='';
_pia:='';
{? grp_empty(ZS_TYP,'WER')<>0 | ZS_TYP.ZS_TYP<>0
:: brak typu lub typ jest pochodną innego
|| _aid:='DPUWN:D';
   _pia:='DU:DU'

|? ~exec('dostepny_ud_skl','schemat',,'PKD',ZS_DEF.P().WYDZIAL)
:: brak dostępu do jednostki organizacyjnej współpracownika
|| _aid:='DPUWN';
   _pia:='U:U'

|? ZS_DEF.OK='T'
:: zweryfikowany
|| _aid:='W'
?};

{? _pia='' & (ZS_DEF.sel_size()<>0 | grp_empty(ZS_DEF,'WER')<>0)
:: pusta struktura lub wiele zaznaczonych
|| _pia:='U:U'
?};

ZS_DEF.actions_grayed('WER',_aid);
P.actions_grayed('ZS_DEF',_pia);

:: odrysuj listę współpracowników
grp_disp(P,'ZS_DEF',,1);
~~


\zs_def_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Przed obsługą okna WER tabeli ZS_DEF.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: ignoruj aktywowanie przez użytkownika
{? _a<>0 || return() ?};

:: ogranicz dziedzinę lub zablokuj okno
exec('zs_def_bs','zs_lib','WER')


\p_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Po odświeżeniu wiersza w oknie ZS_DEF tabeli P.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: parametry dla formuł grupy
params_set(params_get());

:: odrysuj okno zależności
grp_disp(ZS_DEF,'WER',,1);
~~


\p_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Przed obsługą okna ZS_DEF tabeli P.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(ZS_TYP,'WER')<>0
:: brak wskazanego typu
|| return('#disable')
?};

P.prefix();
_typ:=exec('ustal','zs_typ',ZS_TYP.SLO_KOD);
_firma:=exec('ud_def_firma','schemat','PODZORG');
exec('filtruj_p','schemat','PKD',_firma.REF,'','T','*',,
   'P.REFERENCE not in ('
   '  select ZS_DEF.P'
   '  from ZS_DEF'
   '  where ZS_DEF.ZS_TYP=\'%1\''
   ')'[$_typ.dane.REF]
);

P.f_seek(params_get().ctx.p_ref);
~~


\p_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Po obsłudze okna ZS_DEF tabeli P.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _a<>0
|| params_get().ctx.p_ref:=P.ref();
   return()
?};
~~


\main_old
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Poprzednia wersja
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

exec('__RUB','object');

{? PAR_SKID.get(232)='T'
|| exec('zal_strst_p','stanprac')
|| exec('zal_sluz','stanprac')
?};
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 b48698f0d9aaeb6fb5999d6cacd4d81884d1018638dbe7c82f340611c06b68c9504f3128f2447647aa8267298971da4286025823d8275a6d6ccaa6824dea7641676b56f6796f980b532dbb544a0fa7e053eef6e6b5c5b6a014cb6dbe2dae2b7d51b58d918e28aeed0cc941b2b40343e15383cffb82ed44594590193cc255ff37

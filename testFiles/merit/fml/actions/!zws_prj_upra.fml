:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_prj_upra.fml
:: Utworzony: 18.10.2021
:: Autor: PD
:: Systemy:
::======================================================================================================================
:: Zawartość: Uprawnienia do projektów
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Czynność ZWS_PAR_UPRA - magazyny do realizacji zamówień wewnętrznych
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       _in  - [obj_new] - parametry wejściowe czynności
::       _int - [obj_new] - parametry wewnętrzne czynności
::       _out - [obj_new] - parametry wyjściowe czynności
::       _mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
exec('select','!zws_prj_upra');
''


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
PROJUSR.cntx_psh();
PROJUSR.index('PROJ');
PROJUSR.prefix(PROJEKTY.ref());
PROJUSR.first();
PROJUSR.win_sel('WER');
_act:={? exec('chk_role','!zws_prj_upra') &  exec('get','#params',800302)='T' & PROJEKTY.UPR='T'
      || ''
      || 'DURP:DRP'
      ?};
PROJUSR.actions('WER',_act);
PROJUSR.select(,1);
PROJUSR.cntx_pop();
~~



\projusr_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
_user:=exec('name','users');

_tab:=tab_tmp(1,
              'USER','STRING[10]','Użytkownik'@,
              'DANE','STRING[50]','Dane użytkownika'@,
              'ACCESS','STRING[1]','Uprawnienie'@,
              'USER_REF','STRING[16]','Użytkownik'@);

USERS.cntx_psh();
USERS.index('USR_AKOD');
USERS.prefix('T');
{? USERS.first()
|| {!
   |? {? exec('proj_upraw','!zws_prj_upra')
      ||
         _tab.USER_REF:=$USERS.ref();
         _tab.USER:=USERS.KOD;
         _tab.DANE:=USERS.DANE;
         _tab.ACCESS:='N';
         _tab.add()
      ?};
      USERS.next()
   !}
?};
USERS.cntx_pop();

{? _tab.size()=0
|| FUN.info('Brak użytkowników uprawnionych do projektu.'@);
   return(~~)
?};

_okn:=_tab.mk_sel('Użytkownicy'@,'P',,'user',,,,,'U');
_tab.win_fld(_okn,,'ACCESS',,,,,0,,,'Uprawnienie [T/N]?'@,2,,"'T'","'N'");
_tab.win_fld(_okn,,'USER',,,20,,1);
_tab.win_fld(_okn,,'DANE',,,30,,1);
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Zaznacz'@@,,,_fml,,1,1,,,'Z');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
_tab.win_act(_okn,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');
_fb:="
   _lastdraw:=1;
   {? var_pres('_a')=type_of(0)
   || _lastdraw:=_a
   ?};
   _grayed:='';
   _tab:=cur_tab();
   _sel:=cur_win();
   {? _lastdraw>0
   || {? _tab.sel_size()=0
::       Pojedyncze zaznaczenie
      || {? _tab.ACCESS='T'
         || _tab.actions(_sel,,'O',1);
            _grayed+='Z'
         |? _tab.ACCESS='N' | _tab.ACCESS=''
         || _tab.actions(_sel,,'Z',1);
            _grayed+='O'
         ?}
::       Zaznaczenie grupowe
      || _tab.actions(_sel,,'Z',1)
      ?};
      _tab.actions_grayed(_sel,_grayed)
   ?}
";
_tab.win_act(_okn,,'Rekord',,,,_fb);

_tab.win_sel(_okn);
_tab.first();

{? _tab.select(,1)
|| _jest:=0;
   {? _tab.first()
   || {!
      |? {? _tab.ACCESS='T'
         || _jest:=1
         ?};
         ~_jest & _tab.next()
      !}
   ?};
   {? _jest
   || USERS.cntx_psh();
      USERS.clear();
      _tab.first();
      {!
      |? {? _tab.ACCESS='T' & USERS.seek(_tab.USER_REF)
         || PROJUSR.blank();
            PROJUSR.PROJEKTY:=PROJEKTY.ref();
            PROJUSR.USERS:=USERS.ref();
            PROJUSR.add(1)
         ?};
         _tab.next()
      !};
      USERS.cntx_pop()
   ?}
?};

~~

\projusr_dolacz_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami do roli
::----------------------------------------------------------------------------------------------------------------------
_user:=exec('name','users');

_tab:=tab_tmp(1,
              'NAME','STRING[100]','Nazwa roli'@,
              'ACCESS','STRING[1]','Uprawnienie'@,
              'ROLE_REF','STRING[16]','Rola'@);

B_ROLE.cntx_psh();
B_ROLE.index('UNIK');
B_ROLE.prefix(REF.FIRMA);
{? B_ROLE.first()
|| {!
   |?
      {? exec('proj_upraw_role','!zws_prj_upra')
      ||
         _tab.ROLE_REF:=$B_ROLE.ref();
         _tab.NAME:=B_ROLE.NAME;
         _tab.ACCESS:='N';
         _tab.add()
      ?};
      B_ROLE.next()
   !}
?};
B_ROLE.cntx_pop();

{? _tab.size()=0
|| FUN.info('Brak ról uprawnionych do projektu.'@);
   return(~~)
?};

_okn:=_tab.mk_sel('Role'@,'P',,'role',,,,,'U');
_tab.win_fld(_okn,,'ACCESS',,,,,0,,,'Uprawnienie [T/N]?'@,2,,"'T'","'N'");
_tab.win_fld(_okn,,'NAME',,,40,,1);
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Zaznacz'@@,,,_fml,,1,1,,,'Z');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
_tab.win_act(_okn,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');
_fb:="
   _lastdraw:=1;
   {? var_pres('_a')=type_of(0)
   || _lastdraw:=_a
   ?};
   _grayed:='';
   _tab:=cur_tab();
   _sel:=cur_win();
   {? _lastdraw>0
   || {? _tab.sel_size()=0
::       Pojedyncze zaznaczenie
      || {? _tab.ACCESS='T'
         || _tab.actions(_sel,,'O',1);
            _grayed+='Z'
         |? _tab.ACCESS='N' | _tab.ACCESS=''
         || _tab.actions(_sel,,'Z',1);
            _grayed+='O'
         ?}
::       Zaznaczenie grupowe
      || _tab.actions(_sel,,'Z',1)
      ?};
      _tab.actions_grayed(_sel,_grayed)
   ?}
";
_tab.win_act(_okn,,'Rekord',,,,_fb);

_tab.win_sel(_okn);
_tab.first();

{? _tab.select(,1)
|| _jest:=0;
   {? _tab.first()
   || {!
      |? {? _tab.ACCESS='T'
         || _jest:=1
         ?};
         ~_jest & _tab.next()
      !}
   ?};
   {? _jest
   || USERS.cntx_psh();
      USERS.clear();
      B_ROLE.cntx_psh();
      B_ROLE.clear();
      _tab.first();
      {!
      |?
         {? _tab.ACCESS='T' & B_ROLE.seek(_tab.ROLE_REF)
         ||
            B_USRROL.cntx_psh();
            B_USRROL.index('WER1');
            B_USRROL.prefix(REF.FIRMA,B_ROLE.ref());
            {? B_USRROL.first()
            ||
               {!|?
                  {? USERS.seek(B_USRROL.USERS)
                  || PROJUSR.blank();
                     PROJUSR.PROJEKTY:=PROJEKTY.ref();
                     PROJUSR.USERS:=USERS.ref();
                     PROJUSR.add(1)
                  ?};
                  B_USRROL.next()
               !}
            ?};
            B_USRROL.cntx_pop()
         ?};
         _tab.next()
      !};
      USERS.cntx_pop();
      B_ROLE.cntx_pop()
   ?}
?};

~~


\proj_upraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
PROJUSR.cntx_psh();
PROJUSR.index('PROJ');
PROJUSR.prefix(PROJEKTY.ref(),USERS.ref());
_wyn:={? PROJUSR.first() || 0 || 1 ?};
PROJUSR.cntx_pop();
_wyn


\proj_upraw_usr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Uprawnienia dla aktualnego użytkownika
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',800302)='N'
| PROJEKTY.UPR='N'
| (PROJUSR.cntx_psh();
   PROJUSR.index('PROJ');
   PROJUSR.prefix(PROJEKTY.ref(),OPERATOR.USER);
   _wyn:=PROJUSR.first();
   PROJUSR.cntx_pop();
   _wyn)
| (_super:='N';
   USERS.cntx_psh();
   USERS.prefix();
   {? USERS.seek(OPERATOR.USER)
   || _super:=USERS.SUPER
   ?};
   USERS.cntx_pop();
   _super='T')


\proj_upraw_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
B_USRROL.cntx_psh();
USERS.cntx_psh();
B_USRROL.index('WER1');
B_USRROL.prefix(REF.FIRMA,B_ROLE.ref());
{? B_USRROL.first()
||
   {!|?
      PROJUSR.cntx_psh();
      PROJUSR.index('PROJ');
      PROJUSR.prefix(PROJEKTY.ref(),B_USRROL.USERS);
      _wyn:={? PROJUSR.first() | B_USRROL.USERS().AKT='N'  || 1 || 0 ?};
      PROJUSR.cntx_pop();
      B_USRROL.next() & _wyn
   !}
|| _wyn:=1
?};
B_USRROL.cntx_pop();
USERS.cntx_pop();
~_wyn


\proj_upraw_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
_projekt:=_a;
{?
(
PROJEKTY.UPR='N'
|  (PROJUSR.cntx_psh();
   PROJUSR.index('PROJ');
   PROJUSR.prefix(PROJEKTY.ref(),OPERATOR.USER);
   _wyn:=PROJUSR.first();
   PROJUSR.cntx_pop();
   _wyn)
| (_super:='N';
   USERS.cntx_psh();
   USERS.prefix();
   {? USERS.seek(OPERATOR.USER)
   || _super:=USERS.SUPER
   ?};
   USERS.cntx_pop();
   _super='T')
)
&
(
   _wyn:=0;
   PROJUSR.cntx_psh();
   PROJUSR.index('AUT');
   PROJUSR.prefix(PROJEKTY.ref(),'N');
   {? PROJUSR.first()
   ||
      {!|?
         _user:=PROJUSR.USERS;
         PROJUSR.cntx_psh();
         PROJUSR.index('PROJ');
         PROJUSR.prefix(_projekt,_user);
         _wyn:={? PROJUSR.first() || 1 || 0 ?};
         PROJUSR.cntx_pop();
         PROJUSR.next() & _wyn
      !}
   || _wyn:=1
   ?};
   PROJUSR.cntx_pop();
   ~_wyn
)
|| 1
|| 0
?}


\action
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
exec('np_run','#b__box','ZWS_PRJ_UPRA')


\chk_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
exec('chk_role','#b__box',OPERATOR.USER,'ZWS_PRJ_UPRA')


\projekty_upr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
{? PROJEKTY.UPR='T'
||
   PROJUSR.cntx_psh();
   PROJUSR.index('PROJ');
   PROJUSR.prefix(PROJEKTY.ref(),PROJEKTY.USERADD);
   {? PROJUSR.first()
   || {? PROJUSR.AUT<>'U'
      || PROJUSR.AUT:='U';
         PROJUSR.put()
      ?}
   || PROJUSR.prefix();
      PROJUSR.blank();
      PROJUSR.PROJEKTY:=PROJEKTY.ref();
      PROJUSR.USERS:=PROJEKTY.USERADD;
      PROJUSR.AUT:='U';
      PROJUSR.add()
   ?};
   PROJUSR.cntx_pop();

   _user:=null();
   {? PROJEKTY.HAN<>null
   ||
      USERS.cntx_psh();
      USERS.index('OSOBA');
      USERS.prefix(PROJEKTY.HAN, 'T',);
      {? USERS.first() & USERS.ref()<>PROJEKTY.USERADD || _user:=USERS.ref() ?};
      USERS.cntx_pop();

      _put:=0;
      {? _user<>null()
      ||
         PROJUSR.cntx_psh();
         PROJUSR.index('PROJ');
         PROJUSR.prefix(PROJEKTY.ref(),_user);
         {? PROJUSR.first() & PROJUSR.AUT<>'U' || PROJUSR.AUT:='H'; _put:=PROJUSR.put(1) ?};
         PROJUSR.cntx_pop()
      ?};

      {? ~_put & _user<>null()
      ||
         PROJUSR.cntx_psh();
         PROJUSR.index('AUT');
         PROJUSR.prefix(PROJEKTY.ref(),'H');
         {? PROJUSR.first() || PROJUSR.USERS:=_user; _put:=PROJUSR.put(1) ?};
         PROJUSR.cntx_pop()
      ?};

      {? ~_put & _user=null()
      ||
         PROJUSR.cntx_psh();
         PROJUSR.index('AUT');
         PROJUSR.prefix(PROJEKTY.ref(),'H');
         {? PROJUSR.first() || PROJUSR.del(1) ?};
         PROJUSR.cntx_pop()
      ?};

      {? ~_put & _user<>null()
      ||
         PROJUSR.cntx_psh();
         PROJUSR.prefix();
         PROJUSR.blank();
         PROJUSR.PROJEKTY:=PROJEKTY.ref();
         PROJUSR.USERS:=_user;
         PROJUSR.AUT:='H';
         PROJUSR.add(1);
         PROJUSR.cntx_pop()
      ?}
   ||
      PROJUSR.cntx_psh();
      PROJUSR.index('AUT');
      PROJUSR.prefix(PROJEKTY.ref(),'H');
      {? PROJUSR.first() || PROJUSR.del(1) ?};
      PROJUSR.cntx_pop()
   ?};

   {? PROJEKTY.SZEF<>null
   ||
      USERS.cntx_psh();
      USERS.index('OSOBA');
      USERS.prefix(PROJEKTY.SZEF, 'T',);
      {? USERS.first()
      & ((_user<>null & _user<>USERS.ref() & USERS.ref()<>PROJEKTY.USERADD)
      | (_user=null & USERS.ref()<>PROJEKTY.USERADD))
      || _user:=USERS.ref()
      || _user:=null()
      ?};
      USERS.cntx_pop();

      _put:=0;
      {? _user<>null()
      ||
         PROJUSR.cntx_psh();
         PROJUSR.index('PROJ');
         PROJUSR.prefix(PROJEKTY.ref(),_user);
         {? PROJUSR.first() & PROJUSR.AUT<>'U'|| PROJUSR.AUT:='S'; _put:=PROJUSR.put(1) ?};
         PROJUSR.cntx_pop()
      ?};

      {? ~_put & _user<>null()
      ||
         PROJUSR.cntx_psh();
         PROJUSR.index('AUT');
         PROJUSR.prefix(PROJEKTY.ref(),'S');
         {? PROJUSR.first() || PROJUSR.USERS:=_user; _put:=PROJUSR.put(1) ?};
         PROJUSR.cntx_pop()
      ?};

      {? ~_put & _user=null()
      ||
         PROJUSR.cntx_psh();
         PROJUSR.index('AUT');
         PROJUSR.prefix(PROJEKTY.ref(),'S');
         {? PROJUSR.first() || PROJUSR.del(1) ?};
         PROJUSR.cntx_pop()
      ?};

      {? ~_put & _user<>null()
      ||
         PROJUSR.cntx_psh();
         PROJUSR.prefix();
         PROJUSR.blank();
         PROJUSR.PROJEKTY:=PROJEKTY.ref();
         PROJUSR.USERS:=_user;
         PROJUSR.AUT:='S';
         PROJUSR.add(1);
         PROJUSR.cntx_pop()
      ?}
   ||
      PROJUSR.cntx_psh();
      PROJUSR.index('AUT');
      PROJUSR.prefix(PROJEKTY.ref(),'S');
      {? PROJUSR.first() || PROJUSR.del(1) ?};
      PROJUSR.cntx_pop()
   ?}
||
   PROJUSR.cntx_psh();
   PROJUSR.index('AUT');
   PROJUSR.prefix(PROJEKTY.ref(),'H');
   {? PROJUSR.first() || PROJUSR.del(1) ?};
   PROJUSR.prefix(PROJEKTY.ref(),'S');
   {? PROJUSR.first() || PROJUSR.del(1) ?};
   PROJUSR.cntx_pop()
?};
~~


\projekty_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Wartość początkowa pola PROJEKTY.UPR
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',800302)='T' || exec('get','#params',800303) || 'N' ?}


\projusr_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
_act:={? exec('chk_role','!zws_prj_upra') &  exec('get','#params',800302)='T' & PROJEKTY.UPR='T'
      || {? PROJUSR.AUT='N' || '' || 'U' ?}
      || 'DURP:DRP'
      ?};
PROJUSR.actions('WER',_act,,1)


\projusr_gr_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
PROJUSR.cntx_psh();
PROJUSR.prefix();
_tab:=PROJUSR.sel_aget();
{? _tab.first()
||
   {!|?
      {? PROJUSR.seek(_tab.REF)
      ||
         {? PROJUSR.AUT<>'N' || PROJUSR.sel_del() ?}
      ?};
      _tab.next()
   !}
?};
PROJUSR.cntx_pop();
1


\proj_usr_oper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Dodaje uprawnienia dla osoby któa zakłada projekt
::----------------------------------------------------------------------------------------------------------------------
PROJUSR.cntx_psh();
PROJUSR.prefix();
PROJUSR.blank();
PROJUSR.PROJEKTY:=PROJEKTY.ref();
PROJUSR.USERS:=OPERATOR.USER;
PROJUSR.AUT:='U';
PROJUSR.add(1);
PROJUSR.cntx_pop()


\projusr_dolacz_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Okno z uprawnieniami
::----------------------------------------------------------------------------------------------------------------------
_user:=exec('name','users');
_projekt:=PROJEKTY.ref();

_tab:=tab_tmp(1,
              'PROJEKT','STRING[30]','Projekt'@,
              'ACCESS','STRING[1]','Uprawnienie'@,
              'PROJ_REF','STRING[16]','Referencja projektu'@);

PROJEKTY.cntx_psh();
PROJEKTY.index('UPR');
PROJEKTY.prefix(REF.FIRMA,1);
{? PROJEKTY.first()
|| {!
   |? {? exec('proj_upraw_proj','!zws_prj_upra',_projekt)
      ||
         _tab.PROJ_REF:=$PROJEKTY.ref();
         _tab.PROJEKT:=PROJEKTY.SYM;
         _tab.ACCESS:='N';
         _tab.add()
      ?};
      PROJEKTY.next()
   !}
?};
PROJEKTY.cntx_pop();

{? _tab.size()=0
|| FUN.info('Brak uprawnionych projektów.'@);
   return(~~)
?};

_okn:=_tab.mk_sel('Projekty'@,'P',,'projekty',,,,,'U');
_tab.win_fld(_okn,,'ACCESS',,,,,0,,,'Uprawnienie [T/N]?'@,2,,"'T'","'N'");
_tab.win_fld(_okn,,'PROJEKT',,,30,,1);
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Zaznacz'@@,,,_fml,,1,1,,,'Z');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
_tab.win_act(_okn,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');
_fb:="
   _lastdraw:=1;
   {? var_pres('_a')=type_of(0)
   || _lastdraw:=_a
   ?};
   _grayed:='';
   _tab:=cur_tab();
   _sel:=cur_win();
   {? _lastdraw>0
   || {? _tab.sel_size()=0
::       Pojedyncze zaznaczenie
      || {? _tab.ACCESS='T'
         || _tab.actions(_sel,,'O',1);
            _grayed+='Z'
         |? _tab.ACCESS='N' | _tab.ACCESS=''
         || _tab.actions(_sel,,'Z',1);
            _grayed+='O'
         ?}
::       Zaznaczenie grupowe
      || _tab.actions(_sel,,'Z',1)
      ?};
      _tab.actions_grayed(_sel,_grayed)
   ?}
";
_tab.win_act(_okn,,'Rekord',,,,_fb);

_tab.win_sel(_okn);
_tab.first();

{? _tab.select(,1)
|| _jest:=0;
   {? _tab.first()
   || {!
      |? {? _tab.ACCESS='T'
         || _jest:=1
         ?};
         ~_jest & _tab.next()
      !}
   ?};
   {? _jest
   || USERS.cntx_psh();
      USERS.clear();
      PROJEKTY.cntx_psh();
      PROJEKTY.clear();
      _tab.first();
      {!
      |?
         {? _tab.ACCESS='T' & PROJEKTY.seek(_tab.PROJ_REF)
         ||
            PROJUSR.cntx_psh();
            PROJUSR.index('AUT');
            PROJUSR.prefix(PROJEKTY.ref(),'N',);
            {? PROJUSR.first()
            ||
               {!|?
                  {? USERS.seek(PROJUSR.USERS)
                  || PROJUSR.cntx_psh();
                     PROJUSR.prefix();
                     PROJUSR.blank();
                     PROJUSR.PROJEKTY:=_projekt;
                     PROJUSR.USERS:=USERS.ref();
                     PROJUSR.add(1);
                     PROJUSR.cntx_pop()
                  ?};
                  PROJUSR.next()
               !}
            ?};
            PROJUSR.cntx_pop()
         ?};
         _tab.next()
      !};
      USERS.cntx_pop();
      PROJEKTY.cntx_pop()
   ?}
?};

~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 076b408d024a0a581223fdea5f1b942c052a158ca0232305f8e52d364c51ecef0f43b67da19d4422a410852fd2ccca7dc5139d26018d4a4c18a50463c6c8162cb59d2f4e8375147911887751dcca68047bf18ffce82506f4ab2dfd1d59c661ee25d49d002398b5b1e52bfd131c4d96c8cc76e6d1711cabf72b13c6857db57fb7

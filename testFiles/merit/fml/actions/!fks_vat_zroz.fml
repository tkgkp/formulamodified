:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_vat_zroz.fml
:: Utworzony: 06.07.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VAT_ZROZ - Zawieszone rozliczenia VAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zawieszone rozliczenia VAT - główna formuła czynności FKS_VAT_ZROZ.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodanie zakładki do okna grupowego obszaru evidencji VAT (FKS_VAT)
::   WE: _a - nazwa zakładki
::       _b - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
_grp:=_b;
_o:=exec('tt_rvat','!fks_vat_zroz');
_win_fml:="exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
_hid_d:='';_hid:='';
DOK.prefix();
{? PAR_SKID.get(82)='N'
||      DOKUM.index('DOKUM');
         DOKUM.prefix(REF.FIRMA,$ROZLVAT.DOK().ref);
         {? DOKUM.first()
         ||  ''
         || _hid_d+='Z'
         ?}
?};'załącznik';
{? ROZLVAT.DOK().DOKZRODL='' || _hid_d+='D' ?};'dokument źródłowy';
{? ~ROZLVAT.DOK().E_DOC || _hid_d+='E' ?};'skan ';
{? _hid_d<>'' || _hid+='E('+_hid_d+')' ?};
ROZLVAT.actions_grayed('WER',_hid);
{? ROZLVAT.DOK().E_DOC || 'xwin16.png:51' || '' ?}";
ROZLVAT.win_fml('WER',,'SYM',,'ICON_BEFORE',_win_fml);
TT_RVAT.win_sopt(_o,'select_record_checkbox = 0');
TREE_VAT.grp_sel(_grp,TT_RVAT,_o,_a,"exec('arw_tt_rvat','!fks_vat_zroz')",,,,"__wgkraj:=0","",,,'maximized_with_title');
task_attach('FKS_VAT_ZROZ');
task_attach('FKS_VAT_DROZ');
TREE_VAT.tab_splt(_grp,,'vertical','left',',28%');
TREE_VAT.grp_sel(_grp,ROZLVAT,'WER',,"exec('arw_rozlvat','!fks_vat_zroz')",0,0,14,"","",,,'maximized_with_title');
TREE_VAT.tab_splt(_grp,'left','horizontal','bottom');
TREE_VAT.grp_sel(_grp,VPOZ,'R_SEL',,"",0,0,,"","",,,'maximized_with_title');

::TREE_VAT.grp_sel(_grp,ROZLVAT,'WER',_a,"exec('arw_rozlvat','!fks_vat_zroz')",0,0,14,"","",,,'maximized_with_title');
::TREE_VAT.tab_splt(_grp,,'horizontal','bottom');
::TREE_VAT.grp_sel(_grp,VPOZ,'R_SEL',,"",0,0,,"","",,,'maximized_with_title');
VAT7.EWI:=null;
VAT7.RVAT:=null;
exec('zakres','!fks_vat_zroz');
vpozvat:=1


\tt_rvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Tworzy tabelę i okno z rodzajami ewidencji VAT
::   WY: okno tymczasowe tabeli z rodzajami VAT
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TT_RVAT');
TT_RVAT:=tab_tmp(3,
   'TREE','TREE_REF',,
   'NAME','STRING[71]','Rodzaje ewidencji VAT',
   'REF','INTEGER',
);
SLU.index('NAZ'); SLU.prefix();
{? SLU.find_key('~RODZAJE EWIDENCJI',)

|| SLO.index('SL'); SLO.prefix(SLU.ref());
   {? SLO.first()
   || {!
      |? TT_RVAT.TREE:=0;
         TT_RVAT.NAME:=SLO.KOD+' - '+SLO.TR;
         TT_RVAT.REF:=SLO.ref();
         {? TT_RVAT.add()
         || _ref:=TT_RVAT.ref();
            RVAT.index('RVAT_SYM');
            RVAT.prefix(REF.FIRMA,SLO.ref());
            {? RVAT.first()
            || {!
               |? TT_RVAT.TREE:=_ref;
                  TT_RVAT.NAME:=RVAT.SYM+' - '+RVAT.NAZ;
                  TT_RVAT.REF:=RVAT.ref();
                  TT_RVAT.add();
                  RVAT.next()
               !}
            ?}
         ?};
         SLO.next()
      !}
   ?}
?};
_o:=TT_RVAT.mk_sel('Rodzaje ewidencji VAT'@,'P',,'#ttrvatsel',,,,1);
TT_RVAT.win_fld(_o,,'NAME',,,25);
::TT_RVAT.win_act(_o,,'Formuła','Nazwa_akcji',,,"przed","po",1);
TT_RVAT.win_fml(_o,,'NAME',,'ICON_BEFORE',"
   {? TT_RVAT.TREE=0
   || {? TT_RVAT.tr_state()=1
      || 'xwin16.png:75'
      || 'xwin16.png:74'
      ?}
   || 'xwin16.png:76'
   ?}
");
TT_RVAT.tr_fml(_o,,"{? _a=-1 || 1 || _a ?}");
TT_RVAT.first();
_o


\arw_tt_rvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po odświeżeniu okna rodzajów VAT okna grupowego
::----------------------------------------------------------------------------------------------------------------------
VAT7.EWI:=null;
VAT7.RVAT:=null;
{? TT_RVAT.TREE=0
|| SLO.cntx_psh();
   SLO.prefix();
   {? SLO.seek(TT_RVAT.REF,)
   || VAT7.EWI:=SLO.ref()
   ?};
   SLO.cntx_pop()
|| RVAT.cntx_psh();
   RVAT.prefix();
   {? RVAT.seek(TT_RVAT.REF,)
   || VAT7.RVAT:=RVAT.ref();
      VAT7.EWI:=RVAT.EWID
   ?};
   RVAT.cntx_pop()
?};
exec('zakres','!fks_vat_zroz');
grp_disp(ROZLVAT,'WER',1)


\dok_use
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [?????]
:: OPIS: Przed wyswietleniem pola dokument - zlaczenie na DOK
::  OLD: \dok_use/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=ROZLVAT.OKRO().ROK().KOD;
_b:=ROZLVAT.OKRO().NR;
exec('mask','dok_fks_ks',_a,_b);
1


\rb_vpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Wyswietlanie pozycji VAT w zawieszonych rozliczeniach
::  OLD: \rb_vpoz/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
{? VPOZ.OKR_Z | VPOZ.OKR_C
|| _gray+='COZ'
?};
VPOZ.actions_grayed('R_SEL',_gray);
{? VPOZ.OKRVAT=null || PARAM.OKROVAT:='' ?};
{? (cur_tab()=ROZLVAT | (var_pres('vpozvat')>0 & vpozvat=1))
|| {? VPOZ.OKRVAT | VPOZ.OKR_Z & VPOZ.OKR_C
   || Color.fnd_kol('ROZLVAT#01#01')
   |? VPOZ.OKR_Z
   || Color.fnd_kol('ROZLVAT#01#Z')
   |? VPOZ.OKR_C
   || Color.fnd_kol('ROZLVAT#01#C')
   |? exec('is_rozl_cz','fks_vat')
   || {? exec('is_rozl_cz_full','fks_vat')
      || Color.fnd_kol('ROZLVAT#01#01')
      || Color.fnd_kol('ROZLVAT#01#02')
      ?}
   || ''
   ?}
|| ''
?}


\arw_rozlvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PB] [8.70]
:: OPIS: Formula po odswiezeniu okienka wertowania tabeli VPOZ w oknach grupowych tabeli ROZLVAT
::  OLD: \rozlvatd/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZLVAT.get
|| exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
   ROZLVAT.DOK();
   VPOZ.index('VDRV');
   {? DOK.DOK_REJ().KOR_NAG='T' & 4+DOK.KN='doku'
   || VPOZ.prefix(DOK.ref)
   || VPOZ.prefix(DOK.ref,ROZLVAT.RVAT,ROZLVAT.DOP)
   ?};
   exec('suma_vat','dok_fks','VPOZ',1)
|| VPOZ.index('VDOK'); VPOZ.prefix(0)
?};
grp_disp(VPOZ,'R_SEL');
1


\rozldok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [?????]
:: OPIS: Formula do wyswietlania dokumentu z nierozliczonym VAT
::  OLD: \rozldok/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
DOK.prefix();
ROZLVAT.DOK();
exec('dok_okn','dok_fks');
DOK.display();
exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR)


\wys_rvpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Akcja 'Wyświetl' w okienku wertowania R_SEL tabeli VPOZ
::  OLD: \wys_rvpo/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=DOK.DOK_REJ().SLO().KOD;
{? _a='VAT'
|| {? PAR_SKID.get(83)='T' || VPOZ.win_edit('RED_PR') || VPOZ.win_edit('RED') ?}
|? _a='SAD'
|| {? PAR_SKID.get(83)='T' || VPOZ.win_edit('IMP_PR') || VPOZ.win_edit('IMP') ?}
?};
VPOZ.display();
VPOZ.win_edit();
0


\rozlvat_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja zakres okna ROZLVAT
::----------------------------------------------------------------------------------------------------------------------
VAT7.win_edit('ROZLVAT');
exec('ae_vat7_ewi','!fks_vat_zroz');
{? VAT7.edit("__CHK.record2(VAT7,'EWI','Rodzaj VAT')")
|| exec('zakres','!fks_vat_zroz')
?}


\zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia zakres prezentowanych zawieszonych dokumentów VAT
::----------------------------------------------------------------------------------------------------------------------
{? OPERATOR.DEPT
|| {? VAT7.RVAT
   || ROZLVAT.index('REJ_VO');
      ROZLVAT.prefix(OPERATOR.DEPT,VAT7.RVAT)
   || ROZLVAT.index('REJ_O');
      ROZLVAT.prefix(OPERATOR.DEPT,VAT7.EWI)
   ?}
|| {? VAT7.RVAT
   || ROZLVAT.index('REJ_V');
      ROZLVAT.prefix(VAT7.RVAT)
   || ROZLVAT.index('REJ');
      ROZLVAT.prefix(VAT7.EWI)
   ?}
?}


\be_vat7_ewi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed redakcją VAT7.EWI
::----------------------------------------------------------------------------------------------------------------------
SLU.index('NAZ'); SLU.prefix();
SLU.find_key('~RODZAJE EWIDENCJI',)


\ae_vat7_ewi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Po redakcji VAT7.EWI
::----------------------------------------------------------------------------------------------------------------------
VAT7.efld_opt('ROZLVAT','enable='+$(VAT7.EWI<>null),,'RVAT');
{? VAT7.RVAT & VAT7.RVAT().EWID<>VAT7.EWI
|| VAT7.RVAT:=null
?};
1


\roz_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.60]
:: OPIS: Akcja 'Wydruk' w okienku wertowania tabeli ROZLVAT
::  OLD: \roz_wyd/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.index('NAZ'); SLU.prefix();
{? SLU.find_key('~KRAJE UE')
|| SLO.index('SL'); SLO.prefix(SLU.ref()); SLO.find_key('PL');
   SLO.win_sel('ONE_SEL');
   {? SLO.select(,1)
   || VAT7.KRAJ:=SLO.ref();
      {? 1+VAT7.EWI().KOD='Z'
      || exec('rep_exec','#b_report','FKS_VAT_ZROZ','fks_rvl_zak',,,,,,,,'W')
      |? 1+VAT7.EWI().KOD='S' | 6+VAT7.EWI().KOD='_WWspD'
      || tryb:=4; exec('rep_exec','#b_report','FKS_VAT_ZROZ','fks_rvl_spr',,,,,,,,'W'); &tryb
      || tryb:=1; exec('rep_exec','#b_report','FKS_VAT_ZROZ','fks_rvl*',,1); &tryb
      ?}
   ?}
?}


\bvzdoper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Formula przed wyswietleniem pol DOPEROD, DOPERDO zmiennej VAT7
::  OLD: \bvzdoper/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.ZDOPER='T'
|| {? VAT7.DOPEROD=date(0,0,0) & VAT7.DOPERDO=date(0,0,0)
   || SSTALE.AO(); VAT7.DOPEROD:=OKRO_F.POCZ; VAT7.DOPERDO:=OKRO_F.KON
   ?}
|| VAT7.DOPEROD:=VAT7.DOPERDO:=date(0,0,0)
?};
VAT7.efld_opt('RVL','enable='+$(VAT7.ZDOPER='T'),VAT7,'DOPEROD');
VAT7.efld_opt('RVL','enable='+$(VAT7.ZDOPER='T'),VAT7,'DOPERDO');
1


\be_doper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.DOPEROD , VAT7.DOPERDO
::  OLD: \be_doper/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
VAT7.ZDOPER='T'


\bvzdwyst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Formula przed wyswietleniem pol DWYSTOD, DWYSTDO zmiennej VAT7
::  OLD: \bvzdwyst/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.ZDWYST='T'
|| {? VAT7.DWYSTOD=date(0,0,0) & VAT7.DWYSTDO=date(0,0,0)
   || SSTALE.AO(); VAT7.DWYSTOD:=OKRO_F.POCZ; VAT7.DWYSTDO:=OKRO_F.KON
   ?}
|| VAT7.DWYSTOD:=VAT7.DWYSTDO:=date(0,0,0)
?};
VAT7.efld_opt('RVL','enable='+$(VAT7.ZDWYST='T'),VAT7,'DWYSTOD');
VAT7.efld_opt('RVL','enable='+$(VAT7.ZDWYST='T'),VAT7,'DWYSTDO');
1


\be_dwyst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed Redakcja VAT7.DWYSTOD , VAT7.DWYSTDO
::  OLD: \be_dwyst/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
VAT7.ZDWYST='T'


\be_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Przed wyświetleniem Rejestru VAT w oknie zawieszonych rozliczeń.
::       Kropki, w przypadku, gdy dokument zawieszony ma pozycje VAT z róznych rejestrów
::----------------------------------------------------------------------------------------------------------------------
exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
DOK.prefix();
ROZLVAT.DOK();
{? DOK.DOK_REJ().KOR_NAG='T' & 4+DOK.KN='doku'
|| _ret:=exec('sprvpozv','dok_fks',DOK.ref);
   exec('mask','dok_fks_ks',SSTALE.AR().KOD,SSTALE.AO().NR);
   {? _ret=0
   || 0
   || ''
   ?}
|| ''
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 c8004db3f6f6cbbde9e1c03dafaa8f4d74efda0901bc40ef005b001aa72967e1c4f660b5fc258c80911a9bc0fc2eab3705cf498d05d311e4b8ead70d704e2d0d57db5a2211eacc0b09c4fe601ef7a794ab067a75adf8f68fd2ddf7b8514ad01c033100bdf4e4bc60f5c2fcc9994015f6ae2357d8c2456a2914226cabbf756203

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrze�one
::======================================================================================================================
:: Nazwa pliku: !pkd_grp_serp.fml
:: Utworzony: 28.01.2021
:: Autor: MicKoc [21.14]
::======================================================================================================================
:: Zawartość: Grupowe generowanie wydruków aneksów i przeszeregowań
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.14]
:: OPIS: Wydruki aneksów i przeszeregowań
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

exec('init','pkd');

{? ~__F_ZATR.upr('P')
|| FUN.info('Brak wymaganego dostępu do formy współpracy "%1".'@ ['P']);
   return()
?};

_txt:=exec('uprawnieniawrap','pkd','Przebiegu zatrudnienia','PKD_EZK_PPZA','PKD_EZK_RPZA');
{? +|_txt
|| FUN.emsg('Brak wymaganych uprawnień do przeglądania informacji o:%1'@[_txt]);
   return()
?};

P_FILTER.UD_SCH:=exec('domyslny','schemat','PODZORG');

_JOS:=exec('jos_create','pkd_grp');

_cfg:=obj_new('nav','lastref');
_cfg.nav:=obj_new('main','side');
: main - okno ze współpracownikami
: side - okno ze strukturą hierarchiczną
_cfg.nav.side:=_JOS.WS;

_ud_skl:=__PARSES.getVal('JednostkaOrganizacyjna').SYMBOL;
_ustaw:=exec('mk_buff','!pkd_grp_serp','ustaw');

_dane:=exec('wybierz_umowy','!pkd_grp_serp',
   exec('mk_buff','!pkd_grp_serp','dane'),
   _ustaw
);
{? ~_ustaw.ESC
|| _umo:=exec('mk_buff','!pkd_grp_serp','umowy');
   _zal:=exec('mk_buff','!pkd_grp_serp','zalacznik');
   params_set(
      'cfg',_cfg,
      'JOS',_JOS,
      'ud_skl',_ud_skl,
      'DANE',_dane,
      'USTAW',_ustaw,
      'UMO',_umo,
      'ZAL',_zal
   );
   exec('pkd_conf_cntx','pkd','psh');

   Cntx.psh(OSOBA,P,UD_SCH,UD_SKL,UD_DEF,H,H_UM,ZALACZ,SD_DOK);
   Cntx.clr(UD_SCH,UD_SKL,UD_DEF,SD_DOK);
   P.win_edit();

   _wnd:=exec('wnd','!pkd_grp_serp',_JOS,_cfg,_umo,_zal);

   _JOS.TAB.win_sel(_wnd);
   _JOS.TAB.select();

   Cntx.pop(OSOBA,P,UD_SCH,UD_SKL,UD_DEF,H,H_UM,ZALACZ,SD_DOK);
   exec('pkd_conf_cntx','pkd','pop')
?};
P.f_rfresh();
~~


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.14]
:: OPIS: Definicja okienek
::   WE:
::   WY:
:: ~OST: INTMPDIR, INCLIEXEC
::----------------------------------------------------------------------------------------------------------------------
_JOS:=_a;
_cfg:=_b;
_umo:=_c;
_zal:=_d;
: Najpierw zwykłe okno do prezentacji współpracowników.
_acr:='GRPSERP';
: użyj istniejące okienko
{? (_ws:=__WND.SEL.get(P,_acr))=''
|| _ws:=P.mk_sel(,'P',,'#pkdgrpserp',,,,,'U','U');
   P.win_fld(_ws,,'T',,,9,,,,,MS.comment(P,'T'));
   P.win_fld(_ws,,'IP',,,-5,,,,,MS.comment(P,'IP'));
   P.win_fld(_ws,,'OSOBA','NAZWISKO',,20,,,,,MS.comment(OSOBA,'NAZWISKO'));
   P.win_fld(_ws,,'OSOBA','PIERWSZE',,15,,,,,MS.comment(OSOBA,'PIERWSZE'));
   P.win_fld(_ws,,'OSOBA','PESEL',,11,,,,,MS.comment(OSOBA,'PESEL'));
   P.win_fld(_ws,,'OSOBA','DOWOD',,11,,,'Dowód osobisty'@,,MS.comment(OSOBA,'DOWOD'));
   P.win_fld(_ws,,'F_ZATR','KOD',,-2,,,MS.name(P,'F_ZATR'),,MS.comment(F_ZATR,'KOD'));
   P.win_fld(_ws,,'WYDZIAL','SYMBOL',,-16,,,MS.name(P,'WYDZIAL'),,'Symbol jednostki organizacyjnej'@);
   P.win_fld(_ws,,'ST','ST',,18,,,MS.name(P,'ST'),,MS.comment(STN,'ST'));
   P.win_fld(_ws,,'ZA',,,-5,,,MS.name(P,'ZA'),,MS.comment(P,'ZA'),2,,"'T'","'N'");

   _fb:="
      _par:=params_get();
      _cur:=P.ref();
      exec('wybierz_umowy','!pkd_grp_serp',_par.DANE,_par.USTAW);
      {? ~_par.USTAW.ESC & P.f_first()
      || {! |?
            {? _par.DANE.TAB.find_key($P.ref())
            || P.f_next()
            || P.f_del()
            ?}
         !};
         P.f_seek(_cur) | P.f_first()
      ?};
      _par.USTAW.TAB.SIZE:=P.f_size();
      params_set(_par);
      1
   ";
   P.win_act(_ws,0,'Formuła','Filtrowanie umów'@@,,'Filtrowanie umów współpracowników do wydruku'@,_fb,,,,,,'F');
   P.win_act(_ws,1,'Formuła','Filtrowanie umów'@@,,'Filtrowanie umów współpracowników do wydruku'@,_fb,,,,,,'F');

   _fb:="
      params_set(params_get());
      exec('generuj','!pkd_grp_serp');
      0";
   P.win_act(_ws,,'Formuła','Generuj'@@,,'Generowanie załączników'@,
      _fb,"",1,1,_fb,,'G');
   _f_b:=
      "  {? exec('cli_functions','#system')=0
         || FUN.emsg(exec('indevice_nacc_msg','#system'));
            return(0)
         || params_set(_par:=params_get());
            _par.DANE.TAB.index(_par.DANE.NDX.P);
            _par.UMO.TAB.prefix();
            {? _par.DANE.TAB.find_key($P.ref,) & _par.UMO.TAB.seek(_par.DANE.TAB.H,)
            || _par.ZAL.TAB.prefix(_par.UMO.TAB.uidref(),_par.USTAW.TAB.TYP_ZAL)
            ?};
            _par.ZAL.TAB.first()
         ?}
      ";
   _f_a:=
      "  params_set(_par:=params_get());
         {? ~P.sel_size() &
            ~FUN.ask(
               'Za chwilę uruchomiona zostanie\n'
               'skojarzona w systemie operacyjnym aplikacja umożliwiająca\n'
               'wydrukowanie związanego z załącznikiem pliku na drukarce domyślnej.\n'
               'Czy na pewno chcesz kontynuować?'@
            )
         || return(0)
         ?};
         _dir:='@'+tmp_dir();
         _fn:=_dir+
            {? sys_name(1)='U_LINUX' || '/' || '\\\\' ?}+
            exec('get_unique_file_name','#file',_par.ZAL.TAB.ZAL_NAME,_dir);
         {? _par.ZAL.TAB.bl_get('ZAL',_fn,0)
         || cli_exec(1-_fn,'print')
         ?}
      ";
   _fg_b:="  {? exec('cli_functions','#system')=0
             || FUN.emsg(exec('indevice_nacc_msg','#system'));
                return(0)
             || FUN.ask(
                'Za chwilę uruchomiona zostanie\n'
                'skojarzona w systemie operacyjnym aplikacja umożliwiająca\n'
                'wydrukowanie związanego z załącznikiem pliku na drukarce domyślnej.\n'
                'Czy na pewno chcesz kontynuować?'@
                )
             ?}
          ";
   P.win_act(_ws,,'Formuła','Drukuj'@@,,'Drukowanie z załączników'@,
      _f_b,_f_a,,1,_fg_b,,'D',,'icon=print');
   _f_a:="
      {? P.sel_size()=0
      || exec('pdok_act_paperles','paperless','H',P.uidref(),H.uidref())
      ?}";
   _fg_b:="
      _par:=params_get();
      _ret:=exec('wybierz','pracownik');
      _kon:=exec('tab_kontekst','paperless');
      _prac:=P.sel_aget();
      P.sel_adel();
      _jest:=0;
      {? _prac.first()
      || Cntx.psh(P,OSOBA);
         _par.DANE.TAB.index(_par.DANE.NDX.P);
         {!
         |? {? P.seek(_prac.REF,)
            || _ret.P.blank();
               _ret.P.SQL:=$P.ref();
               _ret.P.UID:=P.uidref();
               _ret.P.REF:=#P.ref();
               _ret.P.CRC:=P.crc();
               _ret.P.LP:=_ret.P.size()+1;
               _ret.P.OSQL:=$P.OSOBA().ref();
               _ret.P.OUID:=OSOBA.uidref();
               _ret.P.OREF:=#OSOBA.ref();
               _ret.P.OCRC:=OSOBA.crc();
               _jest+=_ret.P.add();
               _par.UMO.TAB.prefix();
               {? _par.DANE.TAB.find_key($P.ref(),) & _par.UMO.TAB.seek(_par.DANE.TAB.H,)
               || _kon.P:=_ret.P.UID;
                  _kon.TAB:=_par.UMO.TAB.uidref();
                  _kon.add()
               ?}
            ?};
            _prac.next()
         !};
         Cntx.pop(P,OSOBA)
      ?};
      {? _jest
      || _filtr:=exec('get','#filter',P);
         exec('dokumenty_grupowe','paperless_grp',_ret,_par.USTAW.TAB.TYP_ZAL,_kon);
         exec('set','#filter',P,_filtr)
      ?};
      _jest
   ";
   _fg_a:="
      _par:=params_get();
      {? P.f_active() || P.f_rfresh() ?};
      {? P.sel_size()=0
      || _cur:=P.ref();
         {? P.f_first()
         || _par.DANE.TAB.index(_par.DANE.NDX.P);
            {! |?
               {? _par.DANE.TAB.find_key($P.ref())
               || P.f_next()
               || P.f_del()
               ?}
            !};
            P.f_seek(_cur) | P.f_first()
         ?}
      ?}
   ";
   P.win_act(_ws,,'Formuła','Paperless &HR'@@,,'Uruchomienie obiegu dokumentów Paperless'@,
      ,_f_a,,1,_fg_b,_fg_a,'H');
   P.win_act(_ws,,'Menu','Szukaj'@@,,,,,,,,,'S');
   P.win_act(_ws,,'Formuła','Szukaj dokładnie'@@,'#S','Szukanie zapisu zgodnego ze zredagowanym wzorcem'@,
      "exec('p_ustaw_ezk','szukaj')","exec('p_d','szukaj')",,,,,'S');
   P.win_act(_ws,,'Formuła','Szukaj &kontekstowo'@@,'#S','Szukanie zapisu zgodnego ze zredagowanym wzorcem'@,
      "exec('p_ustaw_ezk','szukaj')","exec('p_k','szukaj')",,,,,'K');
   P.win_act(_ws,,'#F3',,,,"exec('p_ustaw_ezk','szukaj')","exec('p_f3_wnd','szukaj')");
   P.win_act(_ws,,'Kolejność');

   P.win_btn(_ws,'text='+'Generuj'@+',btn_label_align=center,panel=right,align=begin','menu:G');
   P.win_btn(_ws,'text='+'Drukuj'@+',btn_label_align=center,panel=right,align=begin','menu:D');
   P.win_btn(_ws,'text='+'Paperless &HR'@+',btn_label_align=center,panel=right,align=begin','menu:H');
   P.win_btn(_ws,'text='+'Filtrowanie umów'@+',btn_label_align=center,panel=bottom,align=begin','menu:F');

   __WND.SEL.put(P,_acr,_ws)
?};

_cfg.nav.main:=_ws;
{? ~exec('lic','#b_domain','PEP') || P.actions(_ws,'H') ?};
: Teraz okno grupowe.
_mode:='maximized';

_wnd:=_JOS.TAB.grp_make('Grupowa aktualizacja zatrudnienia'@,
:  Po wyświetleniu (załaduj kontrolkę, wyświetl drzewko).
   "  _par:=params_get();
      params_set(_par);
      _cfg:=_par.cfg;
      _ud_skl:=_par.ud_skl;
      _JOS:=_par.JOS;
      exec('jos_fill','pkd_grp',_JOS,P_FILTER.UD_SCH,_ud_skl);
      grp_disp(_JOS.TAB,_cfg.nav.side,1,1)
   ",'pkd_grp_azat'
);

: Struktura.
_JOS.TAB.grp_sel(_wnd,_JOS.TAB,_cfg.nav.side,,
:  Po odświeżeniu (wyświetl współpracowników).
   "  _par:=params_get();
      params_set(_par);
      _cfg:=_par.cfg;
      grp_disp(P,_cfg.nav.main,1,1)
   ",,,18,,,,,_mode,_cfg.nav.side
);

: Współpracownicy.
_JOS.TAB.grp_splt(_wnd,,'vertical','p',',25%');
_JOS.TAB.grp_sel(_wnd,P,_cfg.nav.main,,
   "  _par:=params_get();
      {? P.sel_size()=0
      || {? _par.USTAW.TAB.SIZE<>P.f_size()
         || _cur:=P.ref();
            {? P.f_first()
            || _par.DANE.TAB.index(_par.DANE.NDX.P);
               {! |?
                  {? _par.DANE.TAB.find_key($P.ref())
                  || P.f_next()
                  || P.f_del()
                  ?}
               !};
               P.f_seek(_cur) | P.f_first()
            ?};
            _par.USTAW.TAB.SIZE:=P.f_size()
         ?}
      ?};
      params_set(_par);
      grp_disp(_par.UMO.TAB,_par.UMO.WS,1,1)
   ",,,22,
:  Przed obsługą (ustaw filtr na tabeli P).
: !!! Proteza. Zapamiętujemy numer rekordu tabeli _JOS.TAB, dla którego odświeżany był widok współpracowników.
   "  _par:=params_get();
      _JOS:=_par.JOS;
      _cfg:=_par.cfg;
      {? _cfg.lastref<>_JOS.TAB.ref() & UD_DEF.seek(_JOS.TAB.UD_DEF)
      || _cfg.lastref:=_JOS.TAB.ref();
         _where:={? _JOS.TAB.TYP='S' || '\"P\".ST=\\\''+_JOS.TAB.STN+'\\\'' || '' ?};
         exec('filtruj_p','schemat','PKD',UD_DEF.ref(),'P','','W',,_where);
         _cur:=P.ref();
         {? P.f_first()
         || _par.DANE.TAB.index(_par.DANE.NDX.P);
            {! |?
               {? _par.DANE.TAB.find_key($P.ref())
               || P.f_next()
               || P.f_del()
               ?}
            !};
            P.f_seek(_cur) | P.f_first()
         ?};
         _par.USTAW.TAB.SIZE:=P.f_size()
      ?};
      params_set(_par);
      0
   ",,,,_mode,_cfg.nav.main
);
_JOS.TAB.grp_splt(_wnd,'p','horizontal','z1');
_JOS.TAB.grp_sel(_wnd,_umo.TAB,_umo.WS,,"",,,,
   "  _par:=params_get();
      params_set(_par);
      _par.DANE.TAB.index(_par.DANE.NDX.P);
      _par.UMO.TAB.prefix();
      {? _par.DANE.TAB.find_key($P.ref,) & _par.UMO.TAB.seek(_par.DANE.TAB.H,)
      || _par.UMO.TAB.prefix(P.ref(),H.KZ().KZ,H.OD)
      || _par.UMO.TAB.prefix(null())
      ?}
   "
);

_wnd


\mk_buff
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.14]
:: OPIS: Tworzy składowe interfejsu
::   WE: _a [STRING] - wyróżnik wskazujący na interfejs
::       _b .. [STRING] - dodatkowe parametry
::   WY: obiekt przechowujący elementy interfejsu
::----------------------------------------------------------------------------------------------------------------------
{? _a='ustaw'
|| _obj:=obj_new('TAB','NDX','WS','WE','PAR','ESC');
   _obj.ESC:=0;
   _obj.NDX:=obj_new('LP');
   _obj.TAB:=tab_tmp(2,
      'LP','INTEGER','Lp.',
      'OD','DATE','Data od',
      'DO','DATE','Data do',
      'CZY_AP','STRING[1]','Aneksy czy Przeszeregowania',
      'TYP_ZAL','STRING['+$MS.fld_len(SLO_NAZ,'NAZWA')+']','Typ załącznika',
      'CZY_WER','STRING[1]','Czy weryfikować istnienie',
      'SZABLON','STRING['+$MS.fld_len(SD_DOK,'SYMBOL')+']','Szablon',
      'SIZE','INTEGER','Rozmiar filtra'
      );
   _obj.NDX.LP:=_obj.TAB.ndx_tmp('Lp.',,'LP',,);

   _obj.WE:=_obj.TAB.mk_edit('Filtrowanie umów'@,0,'#pkdgrpserppar');
   _obj.TAB.win_esep(_obj.WE,'Parametry filtrowania'@);
   _obj.TAB.win_efld(_obj.WE,,'OD',,,10,,,'Data od'@,,'Data od zmiany zapisu w umowie'@);
   _obj.TAB.efld_opt(_obj.WE,'mark=1',,'OD');
   _obj.TAB.win_efld(_obj.WE,,'DO',,,10,,,'Data do'@,,'Data do zmiany zapisu w umowie'@);
   _obj.TAB.win_efld(_obj.WE,,'CZY_AP',,,,,,'Rodzaj wydruku'@,,,'radio-buttons',,
      'Aneks'@,"'A'",'Przeszeregowanie'@,"'P'"
   );
   _obj.TAB.efld_opt(_obj.WE,'mark=1',,'CZY_AP');
   _obj.TAB.win_efld(_obj.WE,,'TYP_ZAL',,,30,,,'',,'Wybór typu załącznika'@,,'F3_button=1');
   _obj.TAB.efld_opt(_obj.WE,'mark=1',,'TYP_ZAL');
   _obj.TAB.win_efld(_obj.WE,,'CZY_WER',,,,,,'Pomiń umowy'@,,
      'Sprawdzać, czy załącznik już istnieje i nie pobierać do filtra?'@,
      'check-box','left_label=1,check_label="%1"'['Jeśli załącznik istnieje'],
      "'T'","'N'"
   );
   _obj.TAB.win_efld(_obj.WE,,'SZABLON',,,30,,,'',,'Wybór szablonu dokumentu załącznika'@,,'F3_button=1');
   _obj.TAB.efld_opt(_obj.WE,'mark=1',,'SZABLON');

   exec('ok_esc','#window',_obj.TAB,_obj.WE);

   _obj.TAB.fld_fml('OD','BLANK',"date()");
   _obj.TAB.fld_fml('DO','BLANK',"date()");
   _obj.TAB.fld_fml('CZY_AP','BLANK',"'A'");
   _rule:="
      fld(
         exec('wybierz_nazwy','ext_slo','ZAL',1,,
            $('SLO_NAZ.f_set(
               ''NAZWA'',
               ''join ZAOTM using(SLO_NAZ.reference,ZAOTM.SLO_NAZ)'',
               ''ZAOTM.MIEJSCE='''':_a'''''',
               ''H''
               )
            ')
         ).WYBOR.NAZWA
      )
   ";
   _obj.TAB.fld_fml('TYP_ZAL','F3',_rule);
   _obj.TAB.fld_fml('TYP_ZAL','AFTER_EDIT',"
      {? ~exec('slo_naz','ext_slo','ZAL',fld,1)
      || fld('')
      ?};
      1
   ");
   _rule:="
      _sz:={? cur_tab(1,1).CZY_AP='A' || 'PXX_ZASWUMAN' |? cur_tab(1,1).CZY_AP='P' || 'PXX_ZASWPLAC' || ~~ ?};
      fld(
         {? SD_DOK.seek(exec('wybierz','szablon',_sz),) || SD_DOK.SYMBOL || '' ?}
      )
   ";
   _obj.TAB.fld_fml('SZABLON','F3',_rule);
   _obj.TAB.fld_fml('SZABLON','AFTER_EDIT',"
      _sz:={? cur_tab(1,1).CZY_AP='A' || 'PXX_ZASWUMAN' |? cur_tab(1,1).CZY_AP='P' || 'PXX_ZASWPLAC' || ~~ ?};
      fld(
         {? +|fld() & SD_DOK.seek(exec('wybierz','szablon',_sz,fld),) || fld() || fld('') ?}
      );
      1
   ");

   _obj.TAB.index(_obj.NDX.LP);
   _obj.TAB.win_edit(_obj.WE)

|? _a='dane'
|| _obj:=obj_new('TAB','NDX');
   _obj.TAB:=tab_tmp(1,'P','STRING[16]','Pracownik','H','STRING[16]','Przebieg zatrudnienia');
   _obj.NDX:=obj_new('P','H');
   _obj.NDX.P:=_obj.TAB.ndx_tmp('Pracownik',,'P',,,'H',,);
   _obj.NDX.H:=_obj.TAB.ndx_tmp('Umowa',,'H',,,'P',,)

|? _a='umowy'
|| _obj:=obj_new('TAB','NDX','WS');
   _obj.NDX:=obj_new('OD');
   _obj.TAB:=H;
   _obj.NDX.OD:='_HISTKOD';

   _obj.WS:=_obj.TAB.mk_sel('Umowa o pracę'@,'P',0,'#pkdgrpserpumo');
   _obj.TAB.win_fld(_obj.WS,,'OD',,,-13,,1,'Data rozpoczęcia'@);
   _obj.TAB.win_fld(_obj.WS,,'DO',,,-13,,1,'Data zakończenia'@);
   _obj.TAB.win_fld(_obj.WS,,'RU','K',,2,,1,'Typ'@);
   _obj.TAB.win_fld(_obj.WS,,'RU','O',,20,,1,'Opis'@);
   _obj.TAB.win_fld(_obj.WS,,'ST','ST',,20,,1,'Stanowisko'@);
   _obj.TAB.win_fld(_obj.WS,,'WYDZIAL','SYMBOL',,-20,,1,'Jednostka organizacyjna'@);
   _obj.TAB.win_fld(_obj.WS,,'RWY',,,5,3,1,'Wymiar'@);
   _obj.TAB.win_fld(_obj.WS,,'S1',,,-12,2,1,'Stawka wynagrodzenia'@);
   _obj.TAB.win_fld(_obj.WS,,'S4',,,-12,2,1,'Płaca oddelegowanie'@);

   _obj.TAB.win_act(_obj.WS,0,'Formuła','Załączniki'@,,,"
      _par:=params_get();
      params_set(_par);
      _par.ZAL.TAB.cntx_psh();
      _par.ZAL.TAB.index(_par.ZAL.NDX.NAG);
      _par.ZAL.TAB.prefix(_par.UMO.TAB.uidref(),_par.USTAW.TAB.TYP_ZAL);
      _par.ZAL.TAB.win_sel(_par.ZAL.WS);
      _par.ZAL.TAB.select();
      _par.ZAL.TAB.cntx_pop()
   ",,1);
   _obj.TAB.win_act(_obj.WS,0,'Wyświetl',,,,"
      Cntx.psh(P,H,H_UM,ZALACZ);
      params_exec('h_bw','!pkd_ezk_rpza');
      Cntx.pop(P,H,H_UM,ZALACZ)
   ");

   _obj.TAB.index(_obj.NDX.OD);
   _obj.TAB.win_sel(_obj.WS)

|? _a='zalacznik'
|| _obj:=obj_new('TAB','NDX','WS');
   _obj.NDX:=obj_new('NAG');
   _obj.TAB:=ZALACZ;
   _obj.NDX.NAG:='NAG';

   _obj.WS:=_obj.TAB.mk_sel('Załącznik'@,'P',0,'#pkdgrpserpzal');

   _obj.TAB.win_fld(_obj.WS,,'TYP_ZAL','NAZWA',,20,,,MS.name(ZALACZ,'TYP_ZAL'));
   _obj.TAB.win_fld(_obj.WS,,'ZAL_NAME',,,25,,,MS.name(ZALACZ,'ZAL_NAME'));
   _obj.TAB.win_fld(_obj.WS,,'LOK',,,20,,,MS.name(ZALACZ,'LOK'));
   _obj.TAB.win_fld(_obj.WS,,'SIZE',,,10,,,MS.name(ZALACZ,'SIZE'));
   _obj.TAB.win_fld(_obj.WS,,'DATA',,,10,,,MS.name(ZALACZ,'DATA'));

   _obj.TAB.index(_obj.NDX.NAG);
   _obj.TAB.win_sel(_obj.WS)
?};
_obj


\err
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.14]
:: OPIS: Komunikaty o błędach
::   WE: _a [INTEGER] - wybór komunikatu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| FUN.error('Nieprawidłowo zdefiniowane kolumny do eksportu.'@)
|? _a=2
|| FUN.error('Nieprawidłowy lub nieobsługiwany typ danych.'@)
|? _a=3
|| FUN.error('Brak pliku z danymi.'@)
|? _a=4
|| FUN.error('Nieprawidłowy format pliku.'@)
|| FUN.error('Niespodziewany błąd.'@)
?}


\wybierz_umowy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.14]
:: OPIS: Wybieracz umów
::   WE:
::   WY: tabela z ref
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new('DANE','USTAW');
_ret.DANE:=_a;
_ret.USTAW:=_b;
_tab:=_ret.USTAW.TAB;
{? _tab.OD=#0 || _tab.blank() ?};
EDIT_VAR.SLO_TYP:='ZAL';
SLO_NAZ.cntx_psh();
SLO_NAZ.f_clear();
SLO_NAZ.index('NAZWA');
SLO_NAZ.prefix();
_chk:="
   _t:=cur_tab(1,1);
   _sz:={? _t.CZY_AP='A' || 'PXX_ZASWUMAN' |? _t.CZY_AP='P' || 'PXX_ZASWPLAC' || ~~ ?};
   {? _t.OD>_t.DO & _t.DO<>#0
   || FUN.emsg('Data od nie może być późniejsza niż Data do.'@);
      _chk:='OD'
   |? +|_t.SZABLON & ~SD_DOK.seek(exec('wybierz','szablon',_sz,_t.SZABLON),)
   || _chk:='SZABLON'
   || _chk:=__CHK.record(cur_tab(1,1),,'OD','TYP_ZAL','SZABLON')
   ?};
   _chk
";
{? ~_tab.edit(|_chk)
|| _ret.USTAW.ESC:=1
|| _ret.USTAW.ESC:=0;
   _ret.DANE.TAB.erase();
   Cntx.psh(P,H_UM,H,ZALACZ);
   H.index('_HISTDAT');
   H.prefix();
   exec('otworz_h_um','pracownik',exec('h_um_name','pracownik'));
   H_UM.index('OD');
   ZALACZ.index('NAG');
   ZALACZ.prefix();
   P.f_rfresh();
   {? P.f_first()
   || {! |?
         H.prefix(P.ref());
         {? {? _tab.DO<>#0 || H.find_le(_tab.DO) || H.last() ?} &
            H.OD>=_tab.OD &
            {? _tab.CZY_WER='T' || ~ZALACZ.find_key(H.uidref(),_tab.TYP_ZAL,) || 1 ?}
         || _ret.DANE.TAB.blank(1);
            _ret.DANE.TAB.P:=$P.ref();
            _ret.DANE.TAB.H:=$H.ref();
            {? _tab.CZY_AP='P'
            || _um:=H.UMOWA().ref();
               _s1:=H.S1;
               _s2:=H.S2;
               _s3:=H.S3;
               _s4:=H.S4;
               _st:=H.ST;
               _od:=H.OD;
               _waluta:={? H.CZYWAL='T'   || H.WAL().KOD || 'zł' ?};
               _waluta2:={? H.CZYWAL2='T' || H.WAL().KOD || 'zł' ?};
               _waluta3:={? H.CZYWAL3='T' || H.WAL().KOD || 'zł' ?};
               _waluta4:={? H.CZYWAL4='T' || H.WAL().KOD || 'zł' ?};
               _s2tn:=H.S2T; _s2pn:=H.S2P().NAZWA;
               _s3tn:=H.S3T; _s3pn:=H.S3P().NAZWA;

               _stannew:=H.ST().ST;
               _charprac:=H.CP().S;
               {? H.prev()
                  &
                  (_umprev:=H.UMOWA().ref();
                   _walprev:={? H.CZYWAL='T'   || H.WAL().KOD || 'zł' ?};
                   _walpr2:={? H.CZYWAL2='T' || H.WAL().KOD || 'zł' ?};
                   _walpr3:={? H.CZYWAL3='T' || H.WAL().KOD || 'zł' ?};
                   _walpr4:={? H.CZYWAL4='T' || H.WAL().KOD || 'zł' ?};
                   _s2tp:=H.S2T; _s2pp:=H.S2P().NAZWA;
                   _s3tp:=H.S3T; _s3pp:=H.S3P().NAZWA;
                   _waluta<>_walprev | _waluta2<>_walpr2 | _waluta3<>_walpr3 | _waluta4<>_walpr4
                   | _s1<>H.S1 | _s2<>H.S2 | _s3<>H.S3 | _s4<>H.S4 | _st<>H.ST
                   | _s2tn<>_s2tp | _s2pn<>_s2pp | _s3tn<>_s3tp | _s3pn<>_s3pp
                  ) & _um=_umprev
               || _ret.DANE.TAB.add()
               ?}
            || _pos:=exec('h_mod_pos','pracownik',H.ref(),H.UMOWA);
               {? _pos>=2
               || _ret.DANE.TAB.add()
               ?}
            ?}
         ?};

         P.f_next()
      !}
   ?};
   Cntx.pop(P,H_UM,H,ZALACZ)
?};
::params_set('parametry',_tab);
SLO_NAZ.cntx_pop();
SLO_NAZ.f_clear();
_ret.DANE


\generuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.14]
:: OPIS: Generowanie załączników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_typ:=exec('slo_naz','ext_slo','ZAL',_par.USTAW.TAB.TYP_ZAL,1);
{? P.sel_size()=0 || sel_add() ?};
_tab:=P.sel_aget();
{? _tab.first()
|| Cntx.psh(P,H,H_UM,ZALACZ);
   Cntx.clr(P,H,H_UM,ZALACZ);
   _firma:=exec('firma','ustawienia');
   _par.DANE.TAB.index(_par.DANE.NDX.P);
   _rodz:=_par.USTAW.TAB.CZY_AP;
   {!
   |? {? P.seek(_tab.REF,) & _par.DANE.TAB.find_key($P.ref(),) & H.seek(_par.DANE.TAB.H,)
      || {? var_pres('_ret')>0 || &_ret ?};
         _ret:=exec('h_drukuj','pracownik',
            H.ref(),
            {? _rodz='P' || 'prze' |? _rodz='A' || 'anum' || 'zalacz' ?}+'_seria',
            _par.USTAW.TAB.SZABLON
         );
         {? var_pres('INFO',_ret)=type_of('')
         || _fname:=gsub(_ret.INFO,{? sys_name(1)='U_LINUX' || '/' || '\\' ?}+pth_dir('~test.docx'),'');
            {? fexists(_fname,0)
            || do();
               ZALACZ.blank(1);
               ZALACZ.OSOBA:=P.OSOBA;
               ZALACZ.P:=P.ref();
               ZALACZ.DATA:=date();
               ZALACZ.TYP_ZAL:=_typ;
               ZALACZ.RODZAJ:='H';
               ZALACZ.NAG:=H.uidref();
               ZALACZ.FIRMA:=_firma;
:: Dokumenty nie powinny być widoczne, ponieważ nie są podpisane
               {?  exec('lic','#b_domain','PEP')
               || ZALACZ.WWWE:='N';
                  ZALACZ.WWW:='N'
               || ZALACZ.WWWE:='T';
                  ZALACZ.WWW:='T'
               ?};
               ZALACZ.WWWS:='N';
               _nazwa:=
                  {? _rodz='A' || 'Aneks'
                  |? _rodz='P' || 'Przeszeregowanie'
                  || 'Zalacznik'
                  ?}+' od dnia - '+STR.maz2nop(H.OD$6)+'.docx';
               {? ZALACZ.add()
               || {? ZALACZ.bl_put('ZAL',_fname,0,1,_nazwa,1)
                  || ferase(_fname);
                     {? PAR_SKID.get(180)='T'
                     || {? H.name()='_hist'
                        || ZALACZ.cntx_psh();
                           ZALACZ.NAG:=H.IDLNK;
                           ZALACZ.add();
                           ZALACZ.cntx_pop()
                        || H.put()
                        ?}
                     ?}
                  ?}
               ?};
               end()
            ?}
         ?}
      ?};
      _tab.next()
   !};
   Cntx.pop(P,H,H_UM,ZALACZ)
?};
P.sel_adel()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 8e57e537c93c1ee029ab839b59f0ef3f7ba48fc26aaaf69da5c2ef00568323afafcff8f3fa8032f3c9e9aed70ec5a894f3d13000c68dfd850a441fe31e3c45fa600795bca4c6178f4a6014ae658da1d79094e3a3568c78af5cbe016a445669bd14f02002e688ef57ad813a8aeced99be7ddc231f9f235b8b0a18e08ce03c7bcb

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !hbn_prz_zapr.fml
:: Utworzony: 15.02.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności HBN_PRZ_ZAPR - przeglądanie przelewów oczekujących
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie przelewów oczekujących - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=HRB,HRP,FJKS
exec('ini_icon','!hbn_prz_zapr');
exec('przelewy','!hbn_prz_zapr')


\przelewy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Wyswietlenie przelewow do eksportu.
::   WE: [_a] - header do doklejenia
::  OLD: \przelewy z proc_01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,FINFO,'NAROD');
exec('czytaj','#stalesys',,FINFO,'SLWAL');
SKID_RBK.prefix();
{? var_pres('TAB_SPEC')>0
|| SKID_RBK.seek(TAB_SPEC.SKID_RBK,'skid_rbk');
   _vr:=RB.get_rbel(2,TAB_SPEC.RB,TAB_SPEC.KRAJ)
|| SKID_RBK.first()
?};

PAR_WYDR.RPAR1:=PAR_WYDR.RPAR2:=PAR_WYDR.RPAR3:=0;
RACHBANK.KB_4R_BD:='RACHBANK.KB_4R:=RB.get_rbtx(2,PB.RW,PB.NBW().KODISO().KODISO)';
RACHBANK.KB_5R_BD:='RACHBANK.KB_5R:=RB.get_rbtx(2,PB.RD,PB.NBD().KODISO().KODISO)';
PB.use('pbxxxx'); PB_OP.use('popxxxx'); PBHIST.use('phxxxx');

:: filtry - zakładanie
{? ~exec('setf','hbn_filtr',0,'E','D') || return(0) ?};

PB.hdr_sel();
{? _>0 & _a<>'' || PB.hdr_sel(_a) ?};
{? PAR_SKID.get(68)='N'
|| exec('sum_pb','hbn_filtr');
   PB.btn_eopt('RED_WER','PB_SUMA','state=grayed')
|| PB.btn_eopt('RED_WER','PB_SUMA','state=normal')
?};
AreaTitle.setTabWin(PB,~~);
AreaTitle.setTitle();

_fml:=" _grayed:='';
      {? (PB.USTYPPL<>'' & PB.RODZ='KU') | PB.RODZ='KZ'
      || _grayed+='H'
      || PB_OP.index('PB');
         PB_OP.prefix(PB.ref());
         {? ~PB_OP.first() || _grayed+='H' ?}
      ?};
      {? PB.sel_size()>0 & 1+PB.KID='l' || _grayed+='ZY' ?};
      {? PB.sel_size()>0 || _default:='T'
      |? PB.ZT='T' || _grayed+='ZPUF(C)I'; _default:='Y'
      |? PB.ZT='N' || _grayed+='Y'; _default:='Z'
      ?};
      {? PB.ZT='T' &  PB.sel_size()=0 || _grayed+='F(T)' ?};
      {? PB.sel_size()=0 & PB.WSK_S<>'T' || _grayed+='J' ?};
      {? PB.sel_size()=0 & PB.WSK_S='T' || _grayed+='G' ?};
      {? PB.sel_size()=0 & PB.ZT='N' || _grayed+='G' ?};
      PB.actions_grayed(cur_win(1,0),_grayed);
      PB.actions(cur_win(1,0),'',_default,1);
      exec('set_split_field','homebank','PB_EDI1');
      RACHBANK.KB_5R_BD:='RACHBANK.KB_5R:=RB.get_rbtx(2,PB.RD,PB.NBD().KODISO().KODISO)';
      RACHBANK.KB_4R_BD:='RACHBANK.KB_4R:=RB.get_rbtx(2,PB.RW,PB.NBW().KODISO().KODISO)';
      Color.rekprzed({? PB.ZT='T' || 'PB#01#01' || '' ?})";

_grp:=PB.grp_make('Przelewy z rozrachunków'@,"",'hbn_prz_dlis',,,"exec('exit','zws')");

_mob:='mobile_visible=1';
_wnd:=PB.mk_sel(,'P',0,'pb_wer1',,,,,'U');
PB.win_fld(_wnd,,'ZT',,,,,,,,,2,,"'T'","'N'",,_mob);
PB.win_fld(_wnd,,'ODD','OD',,8,,,'Jednostka księgowa'@,,'Jednostka księgowa'@);
_icon:="{? PB.RODZ='K'
        || {? PB.KD='PPL: Płace' || 'xwin16.png:100'
           |? PB.KD='PPL: Umowy' || 'xwin16.png:87'
           |? PB.KD='PPL: Komornik' || 'xwin16.png:114'
           |? PB.KD='PPL: ZUS' || 'xwin16.png:66'
           |? PB.KD='HBN: ZUS' || 'xwin16.png:66'
           |? PB.KH<>null || 'xwin16.png:99'
           || 'xwin16.png:110'
           ?}
        |? PB.RODZ='KU' || 'xwin16.png:65'
        |? PB.RODZ='KZ' || 'xwin16.png:66'
        |? PB.RODZ='W' || 'xwin16.png:49'
        || ''
        ?}";
PB.win_fml(_wnd,,'ODD','OD','ICON_BEFORE',_icon);
PB.win_fld(_wnd,,'DP',,,10,,,'Data przelewu'@,,'Data przelewu'@);
PB.win_fld(_wnd,,'DZ',,,10,,,'Data utworzenia'@,,'Data utworzenia'@);
PB.win_fld(_wnd,,'W',,,23,,,'Odbiorca'@,,'Odbiorca'@,,,,,,'mobile_visible=1, mobile_header=1');
_icon:="{? PB.EMAIL='T' || 'xwin16.png:77' || '' ?}";
PB.win_fml(_wnd,,'W',,'ICON_BEFORE',_icon);
PB.win_fld(_wnd,,'TYT',,,20,,,'Tytułem'@,,'Tytułem'@,,,,,,_mob);
_icon:="{? PB.WSK_S='T' || 'xwin16.png:73' || '' ?}";
PB.win_fml(_wnd,,'TYT',,'ICON_BEFORE',_icon);
{? var_press('S_VAT',PB)>0
|| PB.win_fld(_wnd,,'S_VAT',,,,,,,,,2,,"'T'","'N'");
   PB.win_fml(_wnd,PB,'S_VAT',,'ICON_BEFORE',"exec('icona_svat','blp',PB.S_VAT)")
?};
PB.win_fld(_wnd,,'SP',,,,,,'SP',,,2,,"'T'","'N'");
PB.win_fld(_wnd,,'WAL','KOD',,3,,,'Waluta'@,,'Waluta'@,,,,,,_mob);
PB.win_fld(_wnd,,'KW',,,15,2,,'Kwota'@,,'Kwota'@,,,,,,_mob);
PB.win_btn(_wnd,'text=%1,btn_label_align=center,panel=right,align=begin'['Kr&ajowy'@],'menu:DR');
{? PAR_SKID.get(68)='T'
|| PB.win_btn(_wnd,'text=%1,btn_label_align=center,panel=right,align=begin'['&Walutowy'@],'menu:DT')
?};
PB.win_btn(_wnd,'text=%1,btn_label_align=center,panel=right,align=begin'['Do U&rzędu Skarbowego/Celnego'@],'menu:DW');
PB.win_btn(_wnd,'text=%1,btn_label_align=center,panel=right,align=begin'['D&o ZUS'@],'menu:DZ');
PB.win_btn(_wnd,'text=%1,btn_label_align=center,panel=right,align=begin'['Z rozrachu&nków'@],'menu:CN');
PB.win_btn(_wnd,'text=%1,btn_label_align=center,panel=right,align=begin'['Do zal&iczek'@],'menu:CO');

PB.win_act(_wnd,,'Menu','Dołącz'@);
PB.win_act(_wnd,,'Formuła','K&rajowy'@@,'Dołącz'@,'Nowy przelew zwykły krajowy'@,"exec('akc_add','!hbn_prz_dark','K')"); task_attach('HBN_PRZ_DARK');
{? PAR_SKID.get(68)='T'
|| PB.win_act(_wnd,,'Formuła','Walu&towy'@@,'Dołącz'@,'Nowy przelew zwykły walutowy'@,"exec('akc_add','!hbn_prz_dark','W')"); task_attach('HBN_PRZ_DARK')
?};
PB.win_act(_wnd,,'Formuła','Do Urzędu Skarbo&wego/Celnego'@@,'Dołącz'@,'Nowy przelew do Urządu Skarbowego/Celnego'@,"exec('akc_add','!hbn_prz_dark','U')"); task_attach('HBN_PRZ_DARK');
PB.win_act(_wnd,,'Formuła','Do &ZUS'@@,'Dołącz'@,'Nowy przelew do ZUS'@,"exec('akc_add','!hbn_prz_dark','Z')",,,,,,'Z'); task_attach('HBN_PRZ_DARK');
PB.win_act(_wnd,1,'Menu','Dołącz'@);
PB.win_act(_wnd,1,'Formuła','K&rajowy'@@,'Dołącz'@,'Nowy przelew zwykły krajowy'@,"exec('akc_add','!hbn_prz_dark','K')"); task_attach('HBN_PRZ_DARK');
PB.win_act(_wnd,1,'Formuła','Do Urzędu Skarbo&wego/Celnego'@@,'Dołącz'@,'Nowy przelew do Urządu Skarbowego/Celnego'@,"exec('akc_add','!hbn_prz_dark','U')"); task_attach('HBN_PRZ_DARK');
PB.win_act(_wnd,1,'Formuła','Do &ZUS'@@,'Dołącz'@,'Nowy przelew do ZUS'@,"exec('akc_add','!hbn_prz_dark','Z')"); task_attach('HBN_PRZ_DARK');
{? PAR_SKID.get(68)='T'
|| PB.win_act(_wnd,1,'Formuła','Walu&towy'@@,'Dołącz'@,'Nowy przelew zwykły walutowy'@,"exec('akc_add','!hbn_prz_dark','W')"); task_attach('HBN_PRZ_DARK')
?};
PB.win_act(_wnd,0,'Menu','Generuj z ewiden&cji'@);
PB.win_act(_wnd,0,'Formuła','Z rozrachu&nków'@,'Generuj z ewiden&cji','Generowanie przelewów z rozrachunków'@,"","exec('gen_all','rozrach_przel')");
PB.win_act(_wnd,0,'Formuła','Zbiorczy &split payment'@,'Generuj z ewiden&cji','Generowanie przelewów zbiorczych split payment'@,"exec('zbiorczy_sp','rozrach_przel')","");
PB.win_act(_wnd,0,'Formuła','D&o zaliczek'@,'Generuj z ewiden&cji','Utworzenie przelewów do zaliczek'@,"","exec('przelewy','zaliczki')");task_attach('HBN_PRZ_DZAL');
PB.win_act(_wnd,0,'Formuła','Z &list płac'@,'Generuj z ewiden&cji','Utworzenie przelewów na podstawie listy płac'@,"exec('hbn_prz_dlis','hbn')","");task_attach('HBN_PRZ_DLIS');
PB.win_act(_wnd,0,'Formuła','Wybrane składniki list'@,'Generuj z ewiden&cji','Utworzenie przelewów wybranych składników list płac'@,"exec('hbn_prz_dwsk','hbn')","");task_attach('HBN_PRZ_DWSK');
PB.win_act(_wnd,0,'Formuła','Z &umów-zleceń'@,'Generuj z ewiden&cji','Utworzenie przelewów na podstawie umów-zleceń'@,"exec('hbn_prz_dluz','hbn')","");task_attach('HBN_PRZ_DLUZ');
PB.win_act(_wnd,0,'Formuła','Dla &komorników'@,'Generuj z ewiden&cji','Utworzenie przelewów dla komorników'@,"exec('hbn_prz_dlkr','hbn')","");task_attach('HBN_PRZ_DLKR');
PB.win_act(_wnd,0,'Formuła','Dla P&IT-4'@,'Generuj z ewiden&cji','Utworzenie przelewu dla deklaracji PIT-4'@,"exec('hbn_prz_dlus_4','hbn')","");task_attach('HBN_PRZ_DLUS');
PB.win_act(_wnd,0,'Formuła','Dla PI&T-8AR'@,'Generuj z ewiden&cji','Utworzenie przelewu dla deklaracji PIT-8AR'@,"exec('hbn_prz_dlus_8a','hbn')","");task_attach('HBN_PRZ_DLUS');
PB.win_act(_wnd,0,'Formuła','Dla &PPK'@,'Generuj z ewiden&cji','Utworzenie przelewu dla wpłat na PPK'@,"exec('hbn_prz_dppk','hbn')","");task_attach('HBN_PRZ_DPPK');
PB.win_act(_wnd,1,'Menu','Generuj z ewiden&cji'@);
PB.win_act(_wnd,1,'Formuła','Z rozrachu&nków'@,'Generuj z ewiden&cji','Generowanie przelewów z rozrachunków'@,"","exec('gen_all','rozrach_przel')");
PB.win_act(_wnd,1,'Formuła','Zbiorczy &split payment'@,'Generuj z ewiden&cji','Generowanie przelewów zbiorczych split payment'@,"exec('zbiorczy_sp','rozrach_przel')","");
PB.win_act(_wnd,1,'Formuła','D&o zaliczek'@,'Generuj z ewiden&cji','Utworzenie przelewów do zaliczek'@,"","exec('przelewy','zaliczki')");task_attach('HBN_PRZ_DZAL');
PB.win_act(_wnd,1,'Formuła','Z &list płac'@,'Generuj z ewiden&cji','Utworzenie przelewów na podstawie listy płac'@,"exec('hbn_prz_dlis','hbn')","");task_attach('HBN_PRZ_DLIS');
PB.win_act(_wnd,1,'Formuła','Wybrane składniki list'@,'Generuj z ewiden&cji','Utworzenie przelewów wybranych składników list płac'@,"exec('hbn_prz_dwsk','hbn')","");task_attach('HBN_PRZ_DWSK');
PB.win_act(_wnd,1,'Formuła','Z &umów-zleceń'@,'Generuj z ewiden&cji','Utworzenie przelewów na podstawie umów-zleceń'@,"exec('hbn_prz_dluz','hbn')","");task_attach('HBN_PRZ_DLUZ');
PB.win_act(_wnd,1,'Formuła','Dla &komorników'@,'Generuj z ewiden&cji','Utworzenie przelewów dla komorników'@,"exec('hbn_prz_dlkr','hbn')","");task_attach('HBN_PRZ_DLKR');
PB.win_act(_wnd,1,'Formuła','Dla P&IT-4'@,'Generuj z ewiden&cji','Utworzenie przelewu dla deklaracji PIT-4'@,"exec('hbn_prz_dlus_4','hbn')","");task_attach('HBN_PRZ_DLUS');
PB.win_act(_wnd,1,'Formuła','Dla PI&T-8AR'@,'Generuj z ewiden&cji','Utworzenie przelewu dla deklaracji PIT-8AR'@,"exec('hbn_prz_dlus_8a','hbn')","");task_attach('HBN_PRZ_DLUS');
PB.win_act(_wnd,1,'Formuła','Dla &PPK'@,'Generuj z ewiden&cji','Utworzenie przelewu dla wpłat na PPK'@,"exec('hbn_prz_dppk','hbn')","");task_attach('HBN_PRZ_DPPK');
PB.win_act(_wnd,0,'Formuła','Popraw'@@,,'Poprawianie zawartości wskazanego rekordu'@,"exec('mod','!hbn_prz_dark')");
PB.win_act(_wnd,0,'Formuła','Usuń'@@,,'Usuwanie wskazanego zapisu'@,"exec('del','!hbn_prz_dark')","exec('ad_pb','!hbn_prz_dark')",,1,"exec('bgd_pb','!hbn_prz_dark')","exec('agd_pb','!hbn_prz_dark')");
PB.win_act(_wnd,0,'Formuła','Zatwierdź'@@,,'Zatwierdzenie przelewu'@,"exec('pb_confirm','hbn',0)","",1,1,"exec('gb_pbzt','!hbn_prz_eakc')","exec('ga_pbzt','!hbn_prz_eakc')");
PB.win_act(_wnd,0,'Formuła','W&ycofaj'@@,,'Anulowanie zatwierdzonego przelewu'@,"exec('pb_confirm','hbn',0)","",,1,"exec('gb_pbzt','!hbn_prz_eakc')","exec('ga_pbzt','!hbn_prz_eakc')");
PB.win_act(_wnd,0,'Formuła','Rozrac&hunki',,'Rozrachunki związane z przelewem'@,"exec('f_pbop','rozrach_przel','PB')");
PB.win_act(_wnd,0,'Formuła','Grupuj'@@,,'Łączenie przelewów na ten sam rachunek'@,"","exec('main','!hbn_prz_xskl',0)",,1,"exec('gf_pbsklb','!hbn_prz_xskl')","exec('gfa_pbsklb','!hbn_prz_xskl')");
PB.win_act(_wnd,0,'Formuła','Rozgrupu&j'@@,,'Rozklejanie przelewu zbiorczego'@,"","exec('main_r','!hbn_prz_xskl',0)",,1,"exec('gf_pbrklb','!hbn_prz_xskl')","exec('gfa_pbrklb','!hbn_prz_xskl')");
PB.win_act(_wnd,0,'Formuła','&Eksportuj',,'Eksport zleceń do pliku tekstowego'@,"exec('main','!hbn_prz_xprz')"); task_attach('HBN_PRZ_XPRZ');

PB.win_act(_wnd,0,'Menu','Funkcje'@@);
PB.win_act(_wnd,,'Formuła','Zmień ra&chunek'@@,'Funkcje','Zmiana rachunku bankowego licencjobiorcy na przelewach'@,
"exec('zm_pbrb','!hbn_prz_dark','K')","",,1,"exec('gb_zmrb','!hbn_prz_dark')","exec('ga_zmrb','!hbn_prz_dark')",'C'); task_attach('HBN_PRZ_DARK');
PB.win_act(_wnd,,'Formuła','Druku&j'@@,'Funkcje','Wydruki'@,"","exec('raporty','!hbn_prz_zbpr')");
PB.win_act(_wnd,,'Formuła','Filtruj'@@,'Funkcje','Ustawienia filtra'@,"exec('setf','hbn_filtr',1,exec('par_be','hbn_filtr'),'B')");
PB.win_act(_wnd,,'Formuła','Podsumowanie &Split payment'@,'Funkcje','Podsumowanie przelewów Split payment'@,"","exec('sum_pb_vat','homebank')");
PB.win_act(_wnd,0,'Menu','Sprawdzenie &rachunku'@,'Funkcje');
PB.win_act(_wnd,,'Formuła','Sprawdź aktywność rachunku'@@,'Funkcje,Sprawdzenie &rachunku'@,'Sprawdzenie aktywości VAT rachunku bankowego'@,
"exec('chk_rb_pb','blp')","",,1,"exec('bg_chk_pb','blp')","exec('ag_chk_pb','blp')");
PB.win_act(_wnd,,'Formuła','Wydruk sprawdzenia'@@,'Funkcje,Sprawdzenie &rachunku'@,,"exec('chk_pb_prn','blp')");
PB.win_act(_wnd,,'Formuła','Zadania'@@,'Funkcje','Zadania związane z bieżącym zapisem'@,"","exec('todo_select','#b__box',PB.uidref())");
PB.win_act(_wnd,,'Formuła','Zmian&y'@@,'Funkcje','Historia modyfikacji'@,"","exec('zmiany','#syslog')");
PB.win_act(_wnd,,'Formuła','Zmień &typ przelewu'@@,'Funkcje','Zmień znacznik przelewu ekspresowego.'@,"","exec('pb_change_eks','!hbn_prz_zapr')",,1,
"exec('pb_change_eks_grp_bf','!hbn_prz_zapr')");
PB.win_act(_wnd,0,'Formuła','Archiwu&m'@,,,"","exec('main','!hbn_prz_zbpr')"); task_attach('HBN_PRZ_ZBPR');
PB.win_act(_wnd,1,'Formuła','Archiwu&m'@,,,"","exec('main','!hbn_prz_zbpr')"); task_attach('HBN_PRZ_ZBPR');
PB.win_act(_wnd,1,'Menu','Funkcje'@@);
PB.win_act(_wnd,1,'Formuła','Filtruj'@@,'Funkcje','Ustawienia filtra'@,"exec('setf','hbn_filtr',1,exec('par_be','hbn_filtr'),'B')");
PB.win_act(_wnd,,'Szukaj',,,,"exec('set_okpb','homebank')");
PB.win_act(_wnd,,'Kolejność');
PB.win_act(_wnd,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,,"exec('legenda','color','PB#01')");
PB.win_act(_wnd,,'Rekord',,,,_fml,"exec('ar_pb','!hbn_prz_dark')",1);
PB.win_act(_wnd,,'Wyświetl',,,,"exec('bv_pb','homebank')","",1);
PB.win_act(_wnd,1,'Okienko',,,,,"exec('bl_pb','!hbn_prz_dark')");

PB.grp_sel(_grp,,_wnd,,"exec('pb_grp_red','!hbn_prz_zapr')",,,,"",,,,'maximized','pb_grp');
PB.grp_splt(_grp,,'horizontal','panel2','20,66%');
PB.grp_edit(_grp,,'RED_WER');
PB.win_sel(_grp);
PB.select();
PB.hdr_sel();

:: filtry - sprzątanie
PB.f_clear();
VAR_DEL.delete('TAB_SPEC','TT_TYPP','TT_JKS','TT_SORT','ws_tp','ws_jk','STAB_SPE','STT_TYPP','STT_JKS','STT_SORT');
1


\podsuma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.60]
:: OPIS: Podsumowanie przelewy zatwierdzone i niezatwierdzone w dziedzinie filtra
::  OLD: \sum_pb/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
PB.cntx_psh();
_ref:=PB.ref();
_fa:=PB.f_active();
{? _fa & PB.f_first() | ~_fa & PB.first()
|| TT_PB:=tab_tmp(1,'WAL','STRING[3]','Waluta','Z','REAL','Zatwierdzone',
      'N','REAL','Niezatwierdzone','W','REAL','Wszystkie');
   {! |?
      {? TT_PB.find_key(PB.WAL().KOD)
      || {? PB.ZT='T' || TT_PB.Z+=PB.KW || TT_PB.N+=PB.KW ?}; TT_PB.W+=PB.KW; TT_PB.put()
      || TT_PB.blank(); TT_PB.WAL:=PB.WAL().KOD;
         {? PB.ZT='T' || TT_PB.Z+=PB.KW || TT_PB.N+=PB.KW ?}; TT_PB.W+=PB.KW; TT_PB.add()
      ?};
      _fa & PB.f_next() | ~_fa & PB.next()
   !};
   {? TT_PB.first()
   || TT_PB.win_sel(TT_PB.mk_sel('Podsumowania przelewów'@,'N',1,,,,20));
      TT_PB.select()
   ?};
   obj_del(TT_PB);
   &TT_PB
?};
{? _fa || PB.f_seek(_ref) ?};
PB.cntx_pop();
win_activate('pb_grp');
''


\raporty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wydruk przelewów oczekujących
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','HBN_PRZ_ZAPR','hbn_pb1',,,,,,,,'W')


\mailkhpb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Wypelnianie pola PB.KH().EM
::  OLD: \mailkhpb/mail_hb.fml
::----------------------------------------------------------------------------------------------------------------------
KH.prefix();
{? PB.KH<>null
|| PB.KH();
   undefine();
   define('EM',KH.EM,'E-mail'@,'Kontaktowy adres e-mail kontrahenta'@,100,60);
   _fun:="DEFINE.EM:=|DEFINE.EM;
         {? DEFINE.EM<>'' & ~exec('mail_ok','#email',DEFINE.EM)
         || FUN.emsg('Niepoprawnie zdefiniowany adres e-mail'
                     '\ndla kontrahenta %1'@[KH.SKR]); 0
         || 1
         ?}";
    {? def_edit(_fun,'Adres E-mail kontrahenta'@)
    || KH.EM:=DEFINE.EM;
      exec('kh_mod','kontrahent');
      KH.put()
   ?}
|| FUN.info('Funkcja dostępna tylko dla przelewów dla kontrahentów.'@)
?};
undefine()


\ini_icon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Ustawienie formuł dla ikon okien przelewów
::  OLD: \ini_leg/skid_kus.fml
::----------------------------------------------------------------------------------------------------------------------
formikon:="{? PB.RODZ='K'
           || {? PB.KD='PPL: Płace' || 'xwin16.png:100'
              |? PB.KD='PPL: Umowy' || 'xwin16.png:87'
              |? PB.KD='PPL: Komornik' || 'xwin16.png:114'
              |? PB.KD='PPL: ZUS' || 'xwin16.png:66'
              |? PB.KD='HBN: ZUS' || 'xwin16.png:66'
              |? PB.KH<>null || 'xwin16.png:99'
              || 'xwin16.png:110'
              ?}
           |? PB.RODZ='KU' || 'xwin16.png:65'
           |? PB.RODZ='KZ' || 'xwin16.png:66'
           |? PB.RODZ='W' || 'xwin16.png:49'
           || ''
           ?}";
PB.win_fml('PB_WER1W',PB,'ODD','OD','ICON_BEFORE',formikon);
PB.win_fml('PB_WER1',PB,'ODD','OD','ICON_BEFORE',formikon);
formikon:="{? PB.WSK_S='T' || 'xwin16.png:73' || '' ?}";
PB.win_fml('PB_WER1W',PB,'TYT',,'ICON_BEFORE',formikon);
PB.win_fml('PB_WER1',PB,'TYT',,'ICON_BEFORE',formikon);
PB.win_fml('PB_WER2',PB,'TYT',,'ICON_BEFORE',formikon);
formikon:="{? PB.PR='A' || 'xwin16.png:9' || '' ?}";
PB.win_fml('PB_WER2',PB,'D_EXP',,'ICON_BEFORE',formikon);
formikon:="{? PB.EMAIL='T' || 'xwin16.png:77' || '' ?}";
PB.win_fml('PB_WER2',PB,'W',,'ICON_BEFORE',formikon)


\pb_grp_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po odwieżeniu okna wertowania w oknie grupowym PB_GRP/PB_GRPW tabeli PB
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,1) = PB & cur_win(1,1) = 'PB_WER1'
|| {? f_usr_changed()
   || PB.cntx_psh();
      PAR_WYDR.RPAR1:=PAR_WYDR.RPAR2:=PAR_WYDR.RPAR3:=0;
      {? PB.f_active()
      || {? PB.f_first()
         || {! |? {? PB.ZT='T' || PAR_WYDR.RPAR1+=PB.KW || PAR_WYDR.RPAR2+=PB.KW ?}; PB.f_next() !};
            PAR_WYDR.RPAR3:=PAR_WYDR.RPAR1+PAR_WYDR.RPAR2
         ?}
      || PAR_WYDR.RPAR1:=PAR_WYDR.RPAR2:=PAR_WYDR.RPAR3:=99
      ?};
      PB.cntx_pop()
   ?}
?};
grp_edisp(PB,'RED_WER')


\pb_change_eks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła dla akcji 'Zmień typ przelewu'
::       window_displayed - służy do tego aby w przypadku grupowego wywołania akcji popup wyświetlił się tylko raz.
::                          (usuwana w ostatniej iteracji akcji grupowej)
::       failed_changes  - zlicza nieudane zmiany spowodowane tym, że przelew jest już zatwierdzony.
::                          Zmienna iniciowana wewnątrz warunków aby wywołanie grupowe nie zerowało wartości.
::            payment_sym - przechowuje symbol typu przelewu wybrany przez użytkownika za pomoc popup'a.
::                          (usuwana w ostatniej iteracji akcji grupowej)
::----------------------------------------------------------------------------------------------------------------------
: Akcja pojedyncza
{? PB.sel_size()=0
|| payment_sym:=exec('p_eks_window','!hbn_prz_dark',1,1);
   failed_changes:=0;
   changes:=0
?};
{? payment_sym<>'-1'
||
   {? PB.ZT='N' & (-(5+PB.NBD().NB)='mbank' | (2-(6+PB.RD))='1140')
   || {? payment_sym<>''
      || PB.P_EKS:=payment_sym;
         PB.put();
         changes+=1
      ?}
   || failed_changes+=1
   ?}
?};
:: Ostatnia iteracja akcji grupowej (sel_size=1) lub akcja pojedyncza (sel_size=0)
{? PB.sel_size()<=1 & payment_sym<>'-1'
|| _selected_text:='';
   {? PB.sel_size()=1 || _selected_text:='Zaznaczone przelewy:%1\n'@[$selected] ?};
   _info:=_selected_text;
   {? changes=1
   || _info+='Zmieniono typ 1 przelewu'@
   |? changes>1
   || _info+='Zmieniono typ %1 przelewów'@[$changes]
   ?};
   {? failed_changes=1
   || _info+='\nNie udało się zmienić typu 1 przelewu'@
   |? failed_changes>1
   || _info+='\nNie udało się zmienić typu %1 przelewów'@[$failed_changes]
   ?};
   FUN.info(_info);
   VAR_DEL.delete('window_displayed');
   VAR_DEL.delete('payment_sym');
   VAR_DEL.delete('failed_changes');
   VAR_DEL.delete('changes');
   VAR_DEL.delete('selected')
?}


\pb_change_eks_grp_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła dla akcji 'Zmień typ przelewu' wywoływana podczas "Grupa przed".
::       Wyświetla okienko z wyborem symbolu typu przelewu jaki ma być ustawiony.
::   WY: 1 - Kontynuowanie wykonywania funkcji
::       0 - Anulowanie wykonywania funkcji
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
{? var_pres('window_displayed')<0
|| payment_sym:=exec('p_eks_window','!hbn_prz_dark',0,1);
   window_displayed:=1;
   failed_changes:=0;
   changes:=0;
   selected:=PB.sel_size()
?};
{? payment_sym='-1'
|| VAR_DEL.delete('window_displayed');
   VAR_DEL.delete('payment_sym');
   VAR_DEL.delete('failed_changes');
   VAR_DEL.delete('changes');
   VAR_DEL.delete('selected');
   _res:=0
?};
_res

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 5b82a6d568af378e0f1713881ea3c2ff858e7129e6fb9c0ab7707fc3fe2b6bddafc9df8c87a88afa84a7f19c4ad83b8cf4c5287d280796c925e830b6d7bfc524e6d4bce0039b943422a9f54a310a3da946c72ada3429c6be850ab034b3173218790e9c77fe5bdf95e27fd7af0b8173fd1d7d318f0e5d871f1b9b0246a02683cc

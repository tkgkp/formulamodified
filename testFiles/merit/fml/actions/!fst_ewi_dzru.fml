:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_dzru.fml
:: Utworzony: 18.02.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DZRU - Rejest. ulg podatkowych środków
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Rejest. ulg podatkowych  środków - główna formuła czynności FST_EWI_DZRU.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
exec('srzf_select_u','!fst_ewi_dzru')


\srzf_select_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla ulgi inwestycyjne dla bieżącego środka
::----------------------------------------------------------------------------------------------------------------------
{? SRST.GRP='T'
|| FUN.emsg('W przypadku zestawów środków ulgi muszą być przypisane do elementów składowych zestawu'@);
   return()
?};
SRZD.win_sel('WER');
SRZD.win_dict('WER');
SRZF.win_sel('WER_U');
SRZF.win_edit('ULGA');
SRZF.index('SRODEKU');
SRZF.prefix(SRSR.ref(),'U');
EDIT_ES.R_DOFIN:='S';
SRZF.select()


\srzf_add_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła dodaje nową ulgę dla środka
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('okr_mod','fst') || return() ?};
{? SRSR.r_lock(1,1,1)
|| SRZF.win_edit('ULGA');
   {? FINFO.TOR_D='T'
   || SRZF.efld_opt('ULGA', 'enable=1',,'KONDOD')
   || SRZF.efld_opt('ULGA', 'enable=0',,'KONDOD')
   ?};
   SRZF.blank();
   SRZF.TYP:='U';
   SRZF.DATA:={? SSTALE.AO().POCZ<SRST.SRSR().DE
              || SRST.SRSR().DE
              || SSTALE.AO().POCZ
              ?};
   SRZF.SRSR:=SRSR.ref();
   {? SRZF.edit("exec('chk_srzf_u','!fst_ewi_dzru','ADD')")
   || {? SRZF.add()
      || SRD.updateRecState(SRZF.OKRO_F,1)
      ?}
   ?};
   SRSR.r_unlock()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?}


\chk_srzf_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formułą kontroluje poprawność wypełnienia danych w oknie redagowania ulgi
::   WE: _a = 'ADD' - podczas dodawania, 'EDIT' podczas poprawiania
::   WY: Kod błędnie wypełnionego pola lub ''
::----------------------------------------------------------------------------------------------------------------------
SRZF.WARP:=SRZF.WARP_P;
{? FINFO.TOR_D='T'
|| _wy:=__CHK.record(SRZF,,'DATA','KONPOD','KONFIN','KONDOD')
|| _wy:=__CHK.record(SRZF,,'DATA','KONPOD','KONFIN')
?};
{? _wy=''
|| {? SRZF.OKRO_F=null
   || FUN.emsg('Należy wskazać okres od którego będzie uwzględniana ulga.'@);
      _wy:='OKRO_F'
   ?}
?};
:: kontrola okresu
{? _wy='' & SRZF.OKRO_F<>null
|| {? exec('test_new_year','fst',SRZF.OKRO_F().ROK)
   || FUN.emsg('Wskazany okres obowiązywania pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='OKRO_F'
   ?}
?};
{? _wy=''
|| {? SRZF.WARP_P<=0
   || FUN.emsg('Kwota ulgi powinna być większa od zera.'@);
      _wy:='WARP_P'
   ?}
?};
{? _wy=''
|| _okro_f:=SRZF.OKRO_F;
   _rok_f:=SRZF.ROK_F;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRZF.SRSR,_rok_f,_okro_f);
   {? SRST.first()
   || _txt:='';
      {? SRZF.WARP_P>SRST.WARP
      || _txt:='Kwota ulgi nie może przekroczyć wartości podatkowej środka (%1).'@[$SRST.WARP]
      |? SRZF.WARP_P>SRST.WARF
      || _txt:='Kwota ulgi nie może przekroczyć wartości bilansowej środka (%1).'@[$SRST.WARF]
      |? (FINFO.TOR_D='T' & SRZF.WARP_P>SRST.WARD)
      || _txt:='Kwota ulgi nie może przekroczyć wartości dodatkowej środka (%1).'@[$SRST.WARD]
      ?};
      {? +_txt
      || FUN.emsg(_txt);
         _wy:='WARP_P'
      ?}
   ?};
   SRST.cntx_pop()
?};

{? _wy=''
|| _okro_f:=SRZF.OKRO_F;
   _rok_f:=SRZF.ROK_F;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRZF.SRSR,_rok_f,_okro_f);
   {? SRST.first()
   || {? SRZF.WARP>SRST.NETP
      || FUN.emsg('Kwota ulgi do rozliczenia podatkowo (%1) nie może przekroczyć\n'
                  'bieżącej nieumorzonej podatkowej wartości środka (%2).'@[$SRZF.WARP,$SRST.NETP]);
         _wy:='WARP_P'
      ?};
      {? _wy='' & SRZF.WARF>SRST.NETF
      || FUN.emsg('Kwota ulgi do rozliczenia bilansowo (%1) nie może przekroczyć\n'
                  'bieżącej nieumorzonej bilansowej wartości środka (%2).'@[$SRZF.WARF,$SRST.NETF]);
         _wy:='WARP_P'
      ?};
      {? _wy='' & FINFO.TOR_D='T' & SRZF.WARD>SRST.NETD
      || FUN.emsg('Kwota ulgi do rozliczenia dodatkowo (%1) nie może przekroczyć\n'
                  'bieżącej nieumorzonej dodatkowej wartości środka (%2).'@[$SRZF.WARD,$SRST.NETD]);
         _wy:='WARP_P'
      ?}
   ?};
   SRST.cntx_pop()
?};

{? _wy='' & SRZF.OKRO_F().POCZ<SRST.SRSR().OKRO_F().POCZ
|| FUN.emsg('Ulga nie może obowiązywać przed wprowadzeniem środka do ewidencji.'@);
   _wy:='OKRO_F'
?};

{? _wy='' & SRZF.OKRO_F().POCZ<SRZF.DATA
|| FUN.emsg('Pierwszy okres rozliczania nie może być wcześniejszy niż data wprowadzenia ulgi.'@);
   _wy:='DATA'
?};

{? _wy='' & SRZF.SRSR().R='N' & ~(SRZF.OKRO_F=SRZF.SRSR().OKRO_F | SRZF.OKRO_F().OES=SRZF.SRSR().OKRES+SRZF.SRSR().MP().MP
   | SRZF.OKRO_F().OES=exec('first_month_of_year','fst',SRZF.OKRO_F().RES))
|| FUN.emsg('W przypadku środków niskocenych ulga powinna obowiązywać od pierwszego okresu\n '
            ' ewidencjonowania środka lub pierwszego okresu kolejnego roku podatkowego.'@);
   _wy:='OKRO_F'
?};

{? _wy=''
|| _okro_f:=SRZF.OKRO_F;
   _rok_f:=SRZF.ROK_F;
   _kwotap:=SRZF.WARP_P;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRZF.SRSR,_rok_f,_okro_f);
   {? SRST.first()
   || _warp:=SRST.WARP;
      _warf:=SRST.WARF;
      _ward:=SRST.WARD
   || _warp:=_warf:=_ward:=0
   ?};
   SRST.cntx_pop();

   {? _a='EDIT' || _ref:=SRZF.ref() || _ref:=null ?};
   SRZF.cntx_psh();
   SRZF.index('SRODEK');
   SRZF.prefix(SRSR.ref());
   _suma:=0;
   {? SRZF.first()
   || {! |?
         {? SRZF.ref()<>_ref || _suma+=SRZF.WARP_P ?};
         SRZF.next()
      !}
   ?};
   SRZF.cntx_pop();
   _suma+=_kwotap;
   {? _suma>_warp | _suma>_warf | (FINFO.TOR_D='T' & _suma>_ward)
   || FUN.emsg('Suma kwot dofinansowań i ulg nie może przekroczyć wartości środka.'@);
      _wy:='WARP_P'
   ?}
?};
_wy


\srzf_edit_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Edycja bieżącej ulgi dla środka
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('okr_mod','fst') || return() ?};
{? exec('test_nal_amo','fst',SRZF.SRSR,SRZF.OKRO_F().RES, SRZF.OKRO_F().OES)
|| FUN.emsg('Dla środka naliczono już amortyzację w okresach, w których obowiązuje ulga.\n'
            'Aby zmodyfikować ulgę należy usunąć naliczoną amortyzację.'@);
   return()
?};
{? SRSR.r_lock(1,1,1)
|| {? SRZF.r_lock(1,1,1)
   || SRZF.win_edit('ULGA');
      {? FINFO.TOR_D='T'
      || SRZF.efld_opt('ULGA', 'enable=1',,'KONDOD')
      || SRZF.efld_opt('ULGA', 'enable=0',,'KONDOD')
      ?};
      {? SRZF.edit("exec('chk_srzf_u','!fst_ewi_dzru','EDIT')")
      || {? SRZF.put()
         || SRD.updateRecState(SRZF.OKRO_F,1)
         ?}
      ?};
      SRZF.r_unlock()
   || FUN.emsg('Ulga zablokowana przez innego użytkownika.'@)
   ?};
   SRSR.r_unlock()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?}


\srzf_del_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła usuwa bieżącą ulgę dla środka
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('okr_mod','fst') || return() ?};
{? exec('test_nal_amo','fst',SRZF.SRSR,SRZF.OKRO_F().RES, SRZF.OKRO_F().OES)
|| FUN.emsg('Dla środka naliczono już amortyzację w okresach, w których obowiązuje ulga.\n'
            'Aby usunąć ulgę należy najpierw sunąć naliczoną amortyzację.'@);
   return()
?};
SRDO.cntx_psh();
SRDO.index('SRODZAJD');
SRDO.prefix(SRZF.SRSR,'D',SRZF.ref());
{? SRDO.first() || _doc:=1 || _doc:=0 ?};
SRDO.cntx_pop();
{? _doc
|| FUN.emsg('Wskazana ulga posiada co najmniej jeden dokument zmiany właściwości.\n'
            'Aby usunąć ulgę należy najpierw usunąć powiązane dokumenty.'@);
   return()
?};
{? SRSR.r_lock(1,1,1)
|| {? SRZF.r_lock(1,1,1)
   || {? FUN.ask('Usunąć ulgę?'@)
      || SRZF.r_unlock();
         _wy:=0;
         do();
            {? SRD.delValueCompAll(SRST.SRSR,SRZF.ref())
            || _okro_f:=SRZF.OKRO_F;
               {? SRZF.del(1,1)
               || SRD.updateRecState(_okro_f,1);
                  _wy:=1
               ?}
            ?};
            {? ~_wy || undo() ?};
         end();
         {? ~_wy || FUN.emsg('Podczas usuwania ulgi wystąpił błąd, usuwanie zostało anulowane.'@) ?}
      || SRZF.r_unlock()
      ?}
   || FUN.emsg('Ulga zablokowana przez innego użytkownika.'@)
   ?};
   SRSR.r_unlock()
|| FUN.emsg('Środek zablokowany przez innego użytkownika.'@)
?}


\srzf_historia_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Historia rozliczeń ulgi inwestycyjnej
::----------------------------------------------------------------------------------------------------------------------
SRSW.cntx_psh();
SRSW.index('HISTORIA');
SRSW.prefix(SRZF.ref());
SRSW.first();
SRSW.win_sel('HISTORIU');
_hdr:=': %1, dla środka: %2, kwota ulgi: %3'@[SRZF.NAZWA,SRZF.SRSR().NRI,form(SRZF.WARP,,2)];
SRSW.hdr_sel(_hdr);
SRSW.select();
SRSW.hdr_sel();
SRSW.cntx_pop()


\srzf_show_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na wyświetl w tabeli SRZF, wersja dla ulgi, okno wertowania WER_U
::----------------------------------------------------------------------------------------------------------------------
SRZF.win_edit('SHOW_U');
{? FINFO.TOR_D='T'
|| SRZF.efld_opt('SHOW_U', 'enable=1',,'KONDOD');
   SRZF.efld_opt('SHOW_U', 'enable=1',,'ROZD');
   SRZF.efld_opt('SHOW_U', 'enable=1',,'DATA_ZD')
|| SRZF.efld_opt('SHOW_U', 'enable=0',,'KONDOD');
   SRZF.efld_opt('SHOW_U', 'enable=0',,'ROZD');
   SRZF.efld_opt('SHOW_U', 'enable=0',,'DATA_ZD')
?};
SRZF.display();
SRZF.efld_opt('SHOW_U', 'enable=1',,'KONDOD');
SRZF.efld_opt('SHOW_U', 'enable=1',,'ROZD');
SRZF.efld_opt('SHOW_U', 'enable=1',,'DATA_ZD');
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 42f1b41f4425e694b5ab5c324587d362748f64a2754919d1d8e45b04dd36a2eaa585d398928a87320cb6123187a33d55aed90f096e8dc3dff7ab1024922e8fc27ba551920aad662e50262079a3ea2147570a78565aec5daed27c12a3c6ed47208644a025ea95d9eb6bf736b574f3a7e658fa9a5298c6c6696244c386fbecf0da

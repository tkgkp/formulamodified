:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdo_wpsz.fml
:: Utworzony: 07.12.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PSZ_SDO_WPSZ - Weryfikacja zapotrzebowań.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.02]
:: OPIS: Weryfikacja zapotrzebowań - główna formuła czynności.
::   WE:
::   WY:
::UWAGA: Do obsługi parametrów należy wykorzystać params_get()
::       - wymagane jest aby parametry zawierały wskazanie na tablicę nazwaną 'in'
::       - w tablicy 'in' wymagane jest pole 'SZK_ZAP' typu _SZK_ZAP
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

_par:=params_get();
{? var_pres('_par')<100 | var_pres('in',_par)<100 | var_pres('SZK_ZAP',_par.in)<>type_of(null)
|| FUN.emsg('Brak parametru wywołania czynności \'%1\'.'@['PSZ_SDO_WPSZ']);
   return()
|? _par.in.SZK_ZAP=null | ref_tab(_par.in.SZK_ZAP)<>SZK_ZAP
|| FUN.emsg('Błędny parametr wywołania czynności \'%1\'.'@['PSZ_SDO_WPSZ']);
   return()
?};

SZK_ZAP.cntx_psh();
SZK_ZAP.prefix();
{? SZK_ZAP.seek(_par.in.SZK_ZAP)
|| _firma:=exec('ref_firma','ustawienia');
   _osoba:=OPERATOR.USER().OSOBA;
   SZK_ZAP.WER_KTO:=null;
   REF.OSOBA:=null;
   {? _osoba<>null
   || P.cntx_psh();
      P.index('OSOZATR');
      P.prefix(_firma,_osoba,'T');
:     ustalenie pracownika
      {? P.first()
      || {? P.size()=1
         ||
:           nie ma wątpliwości, bo dla osoby jest tylko jeden
            SZK_ZAP.WER_KTO:=P.ref()
         ||
:           w przypadku więcej niż jednego etatu
:           należy wskazać właściwy spośród dostępnych
            _arg:=exec('wybierz_args','pracownik');
            _arg.DOMAIN:='PSZ';
            _arg.WIELU:=0;
            _arg.SQL_WHERE:=
               'P.REFERENCE in ('
               '  select P.REFERENCE '
               '  from P '
               '  where P.FIRMA=\'%1\' and P.OSOBA=\'%2\' and P.ZA=\'T\''
               ')'[$_firma,$_osoba];
            _P:=exec('wybierz','pracownik',_arg).P;
            {? _P.first() & P.seek(_P.SQL)
            || SZK_ZAP.WER_KTO:=P.ref();
               REF.OSOBA:=_osoba
            || SZK_ZAP.cntx_pop();
               P.cntx_pop();
               return()
            ?}
         ?};
         REF.OSOBA:=_osoba
      ?};
      P.cntx_pop()
   ?};
   SZK_ZAP.WER_DAT:=date();
   SZK_ZAP.OSOBA:=OPERATOR.USER().OSOBA;
   SZK_ZAP.win_edit('WPSZ');
   {? SZK_ZAP.edit("__CHK.record(SZK_ZAP,,'WER_WYN')")
   || SZK_ZAP.put()
   ?}
?};
SZK_ZAP.cntx_pop();

~~

:Sign Version 2.0 jowisz:1045 2023/11/15 14:14:34 b10edee9ea652f01215b8e307e0e8c4beb5100cf2b18d09de205ed633e0b09344046aeefcfb1a170558732996b817b9edd34950414bef83738a0eca1fbd77bff210ddd6c8ac123f3436eea2b123074e2d02286d41694ccfc8f4c12e236adfbe3a94a2a5f093579a2103c73b9498ef83aa6a31c86006708673367dc558377e712

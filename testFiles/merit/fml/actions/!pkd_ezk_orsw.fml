:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orsw.fml
:: Utworzony: 18.05.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORSW - Rejestracja stałych składników
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja stałych składników - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
params_set(_par);

_in:=_par.in;
_ib:=_par.int;
_rv:=_par.out;
_mp:=_par.mp;

_id:=exec('ref2uid','#table',_in.P);
_do:=_mp.akcja();
_result:='';

{? _id=''
|| _result:=exec('error','!pkd_ezk_orsw')

|? _mp.isMicro()
|| {? _do='START'
   || _mp.keyRef(_id);
      _mp.keep()
   |? _do='STOP'
   || _mp.delRef(_id);
      _mp.cancel()
   |? _do<>''
   || _result:='Czynność '+_mp.buf_act.UID+' nie obsługuje akcji '+_do+'.'
   ?}

|| _mp.save(_ib,_rv);
   {? _do='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orsw',_in.P);
      {? type_of(_value)=type_of(0)
      || {? _value<>0
         || _mp.done()
         || _mp.keep()
         ?}
      || _result:=_value
      ?}
   ?}
?};

{? _result<>''
:  obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Rejestracja stałych składników - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj składniki wynagrodzenia: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj składniki wynagrodzenia: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj składniki wynagrodzenia: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj składniki wynagrodzenia'@@
?}


\check
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Kontrola poprawności danych.
::   WE:
::   WY: zgodna ze specyfikacją dla akcji "rekord po"
::  OLD: \spr_st/kali.fml
::----------------------------------------------------------------------------------------------------------------------
: sprawdź wypełnienie i unikalność
_chk:=__CHK.table(LSS,-menu_txt='popraw',,'S','KW');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & ~_chk)
:  porażka podstawowego testu
|| return(_chk)
?};

: dodatkowe testy poprawności
{? LSS.OD<>date(0,0,0) & LSS.DO<>date(0,0,0) & LSS.DO<LSS.OD
|| FUN.emsg('Data "Od dnia" musi być wcześniejsza od daty "Do dnia".');
   'OD'
|| ''
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa czynności wykonywanej z listy zadań.
::   WE: _a - wskazanie pracownika
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err_msg:=exec('error','!pkd_ezk_orsw');

{? var_pres('_a')<>type_of(null) | _a=null
|| return(_err_msg)
?};

P.cntx_psh();
P.prefix();
{? ~P.seek(_a)
|| P.cntx_pop();
   return(_err_msg)
?};

: ustal osobę
OSOBA.cntx_psh();
P.OSOBA();

: przeglądanie rubryk
R.cntx_psh();
R.win_dict('SLO');
R.win_patt('WZO');

: pokaż składniki
LSS.cntx_psh();
LSS.index('PROPOZYC');
LSS.prefix(_a);
LSS.win_sel({? exec('dest','f_zatr',P.F_ZATR,'KOD')='Z' || 'WER_Z' || 'WER_P' ?});
LSS.win_edit('RED');
_result:=LSS.select();
LSS.cntx_pop();

: porządki
R.cntx_pop();
P.cntx_pop();
OSOBA.cntx_pop();

_result


\done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::       - po wywołaniu z listy zadań (okno wertowania tabeli LSS z doklejonym oknem redagowania tabeli P);
::       - w ramach obszaru roboczego (okno wertowania tabeli LSS jako składowa okna grupowego tabeli UD_DEF).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=LSS
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca treść komunikatu błędu
::   WE:
::   WY: treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie składników wynagrodzenia niemożliwe.\nNie znaleziono pracownika.'


\lss_okno_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
R.win_dict('SLO_LS');
R.f_set('RN',,'RK=\'S\'');
1


\lss_okno_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.cntx_pop();
R.f_clear();
1

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 be1299b51ee07947d142b8ed06b31b09e38f0fd63879033858db5ae334383ab4f8179f94c274f1679a0727e82957c6578d4d693f6875858d57fd19c1f2eb266e689368f48402f7e84a001e118e03be33701ea90d0353b429c00b8ae1117ffa80204e370927ee5105501e0ff02c41189841ff130fc5eb8b89b80340671ec4eb7a

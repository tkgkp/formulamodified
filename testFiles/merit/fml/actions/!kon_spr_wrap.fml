:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !kon_spr_wrap.fml
:: Utworzony: 29.08.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności KON_SPR_WRAP - Raporty kontrolne do wyłączeń
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Raporty kontrolne do wyłączeń - główna formuła czynności KON_SPR_WRAP
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Dodaje okno do okna grupowego
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
_b.grp_sel(_c,KON_NRAP,'SLO',_a,,,,,,,,,'maximized_with_title');
task_attach('KON_SPR_WRAP');
~~


\kon_nrap_run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Akcja Wykonaj dla okna SLO tabeli KON_NRAP
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
VAR_DEL.delete('KonRap','KonPoz','KonKol','KonTab');
KonRap:=tab_tmp(2,
   'F1','INTEGER','Firma1',
   'F2','INTEGER','Firma2',
   'WYL','REAL','Wyłączenie'
);
_i1:=KonRap.index('?');
_i2:=KonRap.ndx_tmp('',1,'F2',,0, 'F1',,0);
KonPoz:=tab_tmp(5,
   'RAP','INTEGER','Nagłówek',
   'TYP','STRING[8]','Typ',
   'SPRKOD','STRING[10]','Sprawozdanie - kod',
   'KOLLP','INTEGER','Kolumna - lp',
   'WKOD','STRING[15]','Wiersz - kod',
   'SPRNAZ','STRING[40]','Sprawozdanie - nazwa',
   'KOLNAZ','STRING[60]','Sprawozdanie - nazwa',
   'WNAZ','STRING[80]','Wiersz nazwa',
   'WYL','REAL','Wyłączenie',
   'KREF','INTEGER','Kolumna ref',
   'WREF','INTEGER','Wiersz ref',
   'T','STRING[1]','Typ ref'
);
OSPR.index('OKRES3');
OSPR.prefix(REF.S_FIRMA,SSTALE.AR,SSTALE.AO,SSTALE.AO);
{? OSPR.first()
|| _ospr1:=OKRESY.OKR_RAP:=OSPR.ref();
   KON_PRAP.index('KON_PRAP');
   KON_PRAP.prefix(KON_NRAP.ref());
   {? KON_PRAP.first()
   || FIRMA.index('SYMBOL2');
      FIRMA.prefix('S');
      {? FIRMA.first()
      || OSPR.prefix();
         {!
         |? _ospr:=exec('ospr_fg','konsolidacja',_ospr1,'F',FIRMA.ref());
            {? _ospr & OSPR.seek(_ospr)
            || WARTOSCI.use('wsik_'+OSPR.ROK().KOD);
               WARTOSCI.index('WERSJA');
               {? KON_PRAP.first()
               || {!
                  |? WARTOSCI.prefix(KON_PRAP.DEFW,KON_PRAP.GR_KOL,null,'','N',_ospr);
                     {? WARTOSCI.first()
                     || {!
                        |? {? WARTOSCI.WERSJA().WYKON='W' & WERSJE.FIRMAWYL
                           || _add:=1;
                              _wyl:=WARTOSCI.WARTOSC*KON_PRAP.WSP*{? KON_PRAP.TYP='N' || 1 || -1 ?};
                              {? KON_PRAP.TYP='N' || KonRap.index(_i1) || KonRap.index(_i2) ?};
                              KonRap.prefix(FIRMA.ref(),WERSJE.FIRMAWYL);
                              {? KonRap.first()
                              || KonRap.WYL+=_wyl;
                                 KonRap.put()
                              |? KON_PRAP.TYP='N'
                              || KonRap.F1:=FIRMA.ref();
                                 KonRap.F2:=WERSJE.FIRMAWYL;
                                 KonRap.WYL:=_wyl;
                                 KonRap.add()
                              || KonRap.F2:=FIRMA.ref();
                                 KonRap.F1:=WERSJE.FIRMAWYL;
                                 KonRap.WYL:=_wyl;
                                 KonRap.add()
                              ?};
                              KonPoz.RAP:=KonRap.ref();
                              KonPoz.TYP:={? KON_PRAP.TYP='N' || 'Pion' || 'Poziom' ?};
                              KonPoz.SPRKOD:=KON_PRAP.GR_SIK().SKROT;
                              KonPoz.SPRNAZ:=GR_SIK.NAZWA;
                              KonPoz.KOLLP:=KON_PRAP.GR_KOL().LP;
                              KonPoz.KOLNAZ:=KON_PRAP.GR_KOL().NAZWA;
                              KonPoz.KREF:=KON_PRAP.GR_KOL;
                              KonPoz.WKOD:=KON_PRAP.DEFW().KOD;
                              KonPoz.WNAZ:=KON_PRAP.DEFW().NAZWA;
                              KonPoz.WREF:=KON_PRAP.DEFW;
                              KonPoz.T:=KON_PRAP.TYP;
                              KonPoz.WYL:=_wyl;
                              KonPoz.add()
                           ?};
                           WARTOSCI.next()
                        !}
                     ?};
                     KON_PRAP.next()
                  !}
               ?}

            ?};
            FIRMA.next()
         !}
      ?}
   || FUN.info('Definicja raportu nie zawiera pozycji.'@);
      _ok:=0
   ?}
|| FUN.index('Nie znaleziono okresu sprawozdawczego dla %1.'@[SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ]);
   _ok:=0
?};
{? _ok
|| KonRap.index(_i1);
   KonRap.prefix();
:: KonRap.win_sel(KonRap.mk_sel(,,1));
:: KonRap.select();
   {? KonRap.first()
   || KonKol:=tab_tmp(1,
         'F','INTEGER',,
         'SYM','STRING[80]',,
         'LP','INTEGER',
      );
      exec('konkol_win','!kon_spr_wrap');
      _ik1:=KonKol.index('?');
      _ik2:=KonKol.ndx_tmp('',1,'SYM',,0);
      {!
      |? {? ~KonKol.find_key(KonRap.F1)
         || KonKol.F:=KonRap.F1;
            FIRMA.prefix();
            KonKol.SYM:={? FIRMA.seek(KonRap.F1,) || FIRMA.SYMBOL+' - '+FIRMA.OPIS || 'Firma' ?};
            KonKol.add()
         ?};
         KonRap.prefix(KonRap.F1);
         KonRap.last();
         KonRap.prefix();
         KonRap.next()
      !};
      _tab:='tab_tmp(1,'+
      '\'F\',\'STRING[87]\',\'Firma symbol\'';
      KonKol.index(_ik2);

      {? 0
      || {! _i:=KonKol.size()+1..50
         |! KonKol.F:=10;
            KonKol.SYM:=('0'+$_i)+2;
            KonKol.LP:=_i;
            KonKol.add()
         !}
      ?};

      _ile:=KonKol.size();
      {? KonKol.first()
      || _lp:=1;
         {!
         |? KonKol.LP:=_lp;
            KonKol.put();
            _tab+=',\'F'+$_lp+'\',\'REAL\',\''+KonKol.SYM+'\'';
            _lp+=1;
            KonKol.next()
         !}
      ?};
      {? KonKol.first()
      || _lp:=1;
         {!
         |? _tab+=',\'J'+$_lp+'\',\'INTEGER\',\'JEST'+$_lp+'\'';
            _lp+=1;
            KonKol.next()
         !}
      ?};
      _tab+=',\'REF\',\'INTEGER\',\'Firma ref\'';
      _tab+=')';
      KonTab:=($_tab)();
      _win:=exec('kontab_win','!kon_spr_wrap');
      KonKol.index(_ik1);
      KonRap.index(_i2);
      KonRap.prefix();
      {? KonRap.first()
      || {!
         |? KonRap.prefix(KonRap.F2);
            {? KonRap.first()
            || KonTab.blank();
               KonTab.F:={? FIRMA.seek(KonRap.F2,) || FIRMA.SYMBOL+' - '+FIRMA.OPIS || 'Firma' ?};
               KonTab.REF:=KonRap.F2;
               {!
               |? _lp:={? KonKol.find_key(KonRap.F1) || KonKol.LP+1 || 0 ?};
                  {? _lp
                  || KonTab[_lp]+=KonRap.WYL;
                     KonTab[_lp+_ile]:=1
                  ?};
                  KonRap.next()
               !};
               KonTab.add()
            ?};
            KonRap.prefix();
            KonRap.next()
         !}
      ?};
      KonRap.index(_i1);
      KonRap.prefix();
      KonTab.win_sel(_win);
      KonTab.hdr_sel();
      KonTab.hdr_sel(': '+KON_NRAP.SYM);
      KonTab.prefix();
      KonTab.select()
   || FUN.info('Nie znaleziono wyłączeń.'@)
   ?}
?};
VAR_DEL.delete('KonRap','KonPoz','KonKol','KonTab')


\kontab_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Okno wertowania raportu
::----------------------------------------------------------------------------------------------------------------------
_ile:=KonKol.size();
_win:=KonTab.mk_sel('Raport kontrolny'@,'P',,'#kontbasel',,,,,'U');
KonTab.win_fld(_win,,'F',,,10,,,'Firma'@);
{? KonKol.first()
|| _max:=ceil((180-11-_ile)/_ile);
   {? _max>12 || _max:=12 ?};
   {!
   |? KonTab.win_fld(_win,POMOC,'F'+$KonKol.LP,,,_max,,,'1');
      ($('POMOC.K'+$KonKol.LP+':=_a'))(KonKol.SYM);
      KonTab.fld_opt(_win,'col_name="%1"'[($('POMOC.K'+$KonKol.LP))()],POMOC,'F'+$KonKol.LP);
      POMOC.fld_fml('F'+$KonKol.LP,'BEFORE_DISPLAY',
         $('POMOC.F'+$KonKol.LP+':=KonTab.F'+$KonKol.LP+';KonTab.J'+$KonKol.LP)
      );
      KonKol.next()
   !}
?};
KonTab.win_act(_win,,'Formuła','Szczegóły'@@,,,"exec('details','!kon_spr_wrap')",,1);
KonTab.win_btn(_win,'text=Szczegóły,btn_label_align=center,panel=right,align=begin','menu:S');
_win


\konkol_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Okno wertowania kolumn raportu
::----------------------------------------------------------------------------------------------------------------------
_win:=KonKol.mk_sel('Firma z kolumny'@,'P',,'#konkolsel',,,,,'U');
KonKol.win_fld(_win,,'SYM',,,40,,,'Firma'@);
KonKol.win_act(_win,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
KonKol.win_btn(_win,'text=Wybierz,btn_label_align=center,panel=right,align=begin','menu:W');
KonKol.win_sel(_win)


\details
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Akcja szczegóły okna raportu
::----------------------------------------------------------------------------------------------------------------------
{? KonKol.select()
|| {? KonRap.find_key(KonKol.F,KonTab.REF)
   ||
      KonPoz.prefix(KonRap.ref());
      exec('konpoz_win','!kon_spr_wrap');
      KonPoz.hdr_sel();
      KonPoz.hdr_sel(': '+KonTab.F+' z '+KonKol.SYM);
      KonPoz.select()
   || FUN.info('Nie znaleziono wyłączeń.'@)
   ?}
?}


\konpoz_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Ustawia okno wertownia pozycji raportu
::----------------------------------------------------------------------------------------------------------------------
{? KonPoz.win_sel('?')=''
|| _win:=KonPoz.mk_sel('Wyłączenia'@,'P',,'#konpozsel',,,,,'U');
   KonPoz.win_fld(_win,,'TYP',,,,,,'Typ'@);
   KonPoz.win_fld(_win,,'SPRKOD',,,,,,'Sprawozdanie'@);
   KonPoz.win_fld(_win,,'SPRNAZ',,,30,,,,1);
   KonPoz.win_fld(_win,,'KOLLP',,,,,,'Kolumna'@);
   KonPoz.win_fld(_win,,'KOLNAZ',,,30,,,,1);
   KonPoz.win_fld(_win,,'WKOD',,,,,,'Wiersz'@);
   KonPoz.win_fld(_win,,'WNAZ',,,30,,,,1);
   KonPoz.win_fld(_win,,'WYL',,,16,2,,'Wyłączenie'@);
   KonPoz.win_act(_win,,'Formuła','Algorytmy'@@,,,"exec('algorytmy','!kon_spr_wrap')",,1);
   KonPoz.win_btn(_win,'text=Algorytmy,btn_label_align=center,panel=right,align=begin','menu:A');
   KonPoz.win_sel(_win)
?}


\algorytmy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Akcja Algorytmy okna pozycji raportu
::----------------------------------------------------------------------------------------------------------------------
GR_KOL.prefix();
DEFW.prefix();
{? GR_KOL.seek(KonPoz.KREF,) & DEFW.seek(KonPoz.WREF,)
|| _firma:=REF.FIRMA;
   _ospr:=OKRESY.OKR_RAP;
   _wyk:=REF.WYKON;
   _sik_wyl:=SIK.WYL;
   FIRMA.cntx_psh();
   FIRMA.prefix();
   _nr:={? KonPoz.T='N' || KonKol.F || KonTab.REF ?};
   REF.FIRMA:={? FIRMA.seek(_nr,) || FIRMA.ref() || null ?};
   _nr:={? KonPoz.T='Z' || KonKol.F || KonTab.REF ?};
   REF.FIRMAWYL:={? FIRMA.seek(_nr,) || FIRMA.ref() || null ?};
   FIRMA.cntx_pop();
   SIK.WYL:='W';
   REF.WYKON:=exec('wykon_firmy','konsolidacja',REF.FIRMA);
   OKRESY.OKR_RAP:=exec('ospr_fg','konsolidacja',_ospr,'F',REF.FIRMA);
   WARTOSCI.cntx_psh(); W_ALGPAR.cntx_psh();
   WYRAZ.WIERSZ:=DEFW.ref();
   _rok:=OKRESY.OKR_RAP().ROK().KOD;
   WARTOSCI.use('wsik_'+_rok);
   W_ALGPAR.use('walg_'+_rok);
   WARTOSCI.index('OKRES');
   WARTOSCI.prefix(OKRESY.OKR_RAP,REF.WYKON,DEFW.ref(),GR_KOL.ref(),null);
   PARAM.KW1:={? WARTOSCI.first || WARTOSCI.WARTOSC || 0 ?};
   PARAM.AKTUAL:='1';
   UNPAR.P1_AE:=exec('get_lang','lang',DEFW.ref());
   UNPAR.P2_AE:=exec('get_lang','lang',GR_KOL.ref());
   UNPAR.P3_AE:={? ALG_PAR.get() & ALG_PAR.GR_KOL || exec('get_lang','lang',ALG_PAR.GR_KOL) || '' ?};
   WYRAZ.GRUPA:=DEFW.GRUPA;
   ALG_PAR.win_sel('PAR_PLAN');
   exec('title','lang',ALG_PAR);
   ALG_PAR.hdr_sel(' - %1'@[OKRESY.OKR_RAP().NAZWA]);
   _wyl:={? REF.WYKON().WYKON='W' || 'T' |? WERSJE.WYKON='T' || 'N' || '' ?};
   VAR_DEL.delete('AlgMno');
   {? _wyl<>'' || AlgMno:=1 ?};
   {? REF.FIRMA=REF.GRUPA
   || ALG_PAR.prefix();
      ALG_PAR.f_set(
         'FIRMA(SYMBOL),ROK(NAZ),LP',
         'join ROK_F using(ALG_PAR.ROK,ROK_F.REFERENCE)',
         'DEFW=:_a and GR_KOL=:_b '+
         {? _wyl<>'' || 'and WYL=\':_c\' ' || '' ?}+
         {? SIK.ROK1<>null || 'and ROK_F.POCZ_ROK=TO_DATE(\':_d\') ' || '' ?}+
         'and ALG_PAR.FIRMA<>:_e',
         DEFW.ref(),GR_KOL.ref(),_wyl,$SIK.ROK1().POCZ_ROK,REF.FIRMA
      )
   || {? ALG_PAR.f_active() || ALG_PAR.f_clear() ?};
      {? _wyl=''
      || ALG_PAR.index('ALG_PAR1'); ALG_PAR.prefix(REF.FIRMA,DEFW.ref(),GR_KOL.ref(),OKRESY.OKR_RAP().ROK().NAZ)
      || ALG_PAR.index('ALG_PAR7'); ALG_PAR.prefix(REF.FIRMA,DEFW.ref(),GR_KOL.ref(),_wyl,OKRESY.OKR_RAP().ROK().NAZ)
      ?}
   ?};
   ALG_PAR.select(,,,{? REF.FIRMA=REF.GRUPA || 'Z' || '' ?});

   WARTOSCI.cntx_pop(); W_ALGPAR.cntx_pop();
   REF.FIRMA:=_firma;
   OKRESY.OKR_RAP:=_ospr;
   REF.WYKON:=_wyk;
   SIK.WYL:=_sik_wyl
|| FUN.info('Nie znaleziono kolumny lub wiersza sprawozdania.'@)
?}

:Sign Version 2.0 jowisz:1045 2021/09/17 15:16:57 9fb13b0e9d652c60f74a377ac5ad7b3a289dcf9f04e3e6bd3eecd1be07c90edfbe837cafdbde301e98a59dafebb2b2fc7a7ef5aa66ed6b2accb44a13840d1b8eff12e6378eb1375f14e01662d8e8e6763fe1ba3d6953bd2fdae1a5058a1a0eb524f72adf2e8de9e9019b3ca096d3ef68bb4e65a8bfcd108437b51ff201d56cdc

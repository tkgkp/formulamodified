:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_vat_zrej.fml
:: Utworzony: 23.07.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VAT_ZREV - Analizy dokumentów VAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Analizy dokumentów VAT - główna formuła czynności FKS_VAT_ZREV.
::  OLD: \ana_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
VAR_DEL.delete('TTVAT','TTPAR');
TTPAR:=obj_new('PROC','ALL');
:odpowiedz NIE dla grup podatkowych 'zinwpods' i 'zakupods' prezentuje w kolumnie VAT do odliczenia 50% podatku VAT z dokumentu
TTPAR.PROC:=1;
{? var_pres('POMIN',DVAT)>0 & JpkV7.DATA<>date(0,0,0) & SSTALE.AO().POCZ>=exec('start','jpk_v')
|| TTPAR.ALL:=FUN.ask('Czy prezentować pozycje pominięte w JPK_V7?'@)
|| TTPAR.ALL:=-1
?};
{? var_press('__wgkraj')<=0 || __wgkraj:=0 ?};
TTVAT:=tab_tmp(2,
   'TREEREF','TREE_REF',,
   'LABEL','STRING[50]',,
   'NETTO','REAL','Netto',
   'VAT','REAL','VAT',
   'VAT_ODL','REAL','VAT do odliczenia'@,
   'BRUTTO','REAL','Brutto',
   'LP','INTEGER',,
   'POZ','INTEGER',,
   'REF','INTEGER',,
   'KH','STRING[150]','Kontrahent'@,
   'NIP','STRING[20]','NIP',
   'DATA_OPE','STRING[10]','Data oper.',
   'DATA_ZAP','STRING[10]','Data zap.',
   'DATA_DOK','STRING[10]','Data dokumentu',
   'DATA_TP','STRING[10]','Termin płatności',
   'DATA_OTR','STRING[10]','Data otrz.',
   'POZ2','STRING[8]',,
   'NALICZ','INTEGER',,
   'SYM','STRING[50]',,
   'POMIN','INTEGER','Pomiń w deklaracji',
   'KRAJ','STRING[6]','Kraj opodatkowania',
   'KWARTAL','STRING[6]','Kwartał',
   'DATA_ROZ','DATE','Termin płatności/Data zapłaty'
);
TTVAT.TREEREF:=0;
TTVAT.LABEL:='Naliczony'; TTVAT.add(); _r_c:=TTVAT.ref();
TTVAT.LABEL:='Należny'; TTVAT.add(); _r_z:=TTVAT.ref();
_kraj:=exec('kod_pl','slo_slu');
_waluta:=exec('kod_pln','slo_slu');
_gr:='SprArt22,Próbki,Prezenty,Druki,SprPozKr,SprPKWSU';
_g1:='SprArt22,Próbki,Prezenty,Druki';
DVAT.cntx_psh(); PVAT.cntx_psh(); DOK.cntx_psh();
{? var_press('WAL',VAT7)<=0
|| {? OPERATOR.DEPT
   || DVAT.index('NR_O'); DVAT.prefix(OPERATOR.DEPT,_kraj,SSTALE.AO)
   || DVAT.index('NR'); DVAT.prefix(_kraj, SSTALE.AO)
   ?}
|| {? OPERATOR.DEPT
   || {? __wgkraj
      || DVAT.index('NRW2_O'); DVAT.prefix(OPERATOR.DEPT,SSTALE.WAL,SSTALE.AO().ROK)
      || DVAT.index('NR_OW'); DVAT.prefix(OPERATOR.DEPT,_kraj,_waluta,SSTALE.AO)
      ?}
   || {? __wgkraj
      || DVAT.index('NRWK'); DVAT.prefix(SSTALE.WAL,SSTALE.AO().ROK)
      || DVAT.index('NRW'); DVAT.prefix(_kraj,_waluta,SSTALE.AO)
      ?}
   ?}
?};
PVAT.index('NR');
{? DVAT.first()
|| {!
   |? {? DVAT.USUNIETY=0 & (TTPAR.ALL | DVAT.POMIN=0)
      || _pomin:={? var_pres('POMIN',DVAT)>0 || DVAT.POMIN ?};
         TTVAT.cntx_psh();
         _rn:=ref_name(DVAT.DOK);
         DOK.use(_rn); DOK.prefix();
         PVAT.prefix(DVAT.ref());
         {? PVAT.first()
         || {!
            |? _kv:=DVAT.RVAT().KVAT().SYM;
               _gv:=PVAT.GRVAT().KOD;
               _odl:=(_gv='ZInwPodS' | _gv='ZInwPodZ' | _gv='ZInwPods' | _gv='ZInwePod' | _gv='ZakPRopZ' | _gv='ZakuPRop'
                     | _gv='ZakuPodS' | _gv='ZakuPodZ' | _gv='ZakuPods' | _gv='ZakupPod');
               {? (((PVAT.STVAT().KOD<>' -' & PVAT.STVAT().KOD<>' -o')| _kv='_WWspNat' | _kv='SprzNOp' | _kv='_SprzNOp')
                  & (PVAT.STVAT().KOD<>' -zw0' | (4+_gv='SprP' & _gv<>'SprPKWSU')))
                  & _g1*_gv=0
               || _x:=(( _kv='_WWspNab' | _kv='_WWspNaw' | _kv='_WWspNST' | _kv='ImpUsług' | _kv='ImpTowUp'
                       | _kv='_WWspNus' | _kv='ZakOpod' | _kv='ZakOpoZ' | _kv='_WWspNat') )+1;
                  {!
                  |? {? _gv='ZPozNPod' | _gv='ZInwNPod' | _gv='ZPozNPal' | _gv='ZInwNSam' | _gv='ZakupNpr'
                     || _r:=0
                     || _r:=1
                     ?};
                     {? _r
                     || {? (1+PVAT.GRVAT().KOD)='Z' & _x=1
                        || _r:={? PVAT.TYP_VAT<>'Z' || _r_c || null ?}
                        || _r:={? PVAT.TYP_VAT<>'C' || _r_z || null ?}
                        ?}
                     ?};
                     {? _r<>null
                     || TTVAT.prefix(_r,DVAT.RVAT().SYM,);
                        {? ~TTVAT.first()
                        || TTVAT.blank();
                           TTVAT.TREEREF:=_r;
                           TTVAT.LABEL:=DVAT.RVAT().SYM;
                           TTVAT.POZ:=1;
                           TTVAT.add()
                        ?};
                        _poz2:={? _r=_r_c || PVAT.GRVAT().KOD || PVAT.STVAT().KOD ?};
                        TTVAT.prefix(TTVAT.ref(),_poz2,);
                        {? ~TTVAT.first()
                        || TTVAT.blank();
                           TTVAT.TREEREF:=TTVAT.ref();
                           TTVAT.LABEL:=_poz2;
                           TTVAT.POZ:=2;
                           TTVAT.add()
                        ?};
                        _okr:=DVAT.DOKOKRO().NAZ+' '+OKRO_F.ROK().NAZ;
                        TTVAT.prefix(TTVAT.ref(),_okr,);
                        {? ~TTVAT.first()
                        || TTVAT.blank();
                           TTVAT.TREEREF:=TTVAT.ref();
                           TTVAT.LABEL:=_okr;
                           TTVAT.POZ:=3;
                           TTVAT.LP:=OKRO_F.NR;
                           TTVAT.add()
                        ?};
                        _netto:={? TTPAR.PROC
                                || {?'ZInwPodS,ZInwPodZ,ZakPRopZ,ZakuPodS,ZakuPodZ,ZInwNPod,ZPozNPal,ZPozNPod,ZakupNpr'*_gv & _r=_r_c
                                   || {? PVAT.VAT || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2 || PVAT.SUM2$2 ?}
                                   |? _gv='ZakuPods' | _gv='ZInwPods'
                                 || {? PVAT.VAT || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2 || PVAT.SUM2$2 ?}
                                 || PVAT.SUM2$2
                                 ?}
                                || {? _r=_r_z | _odl || PVAT.SUM2 || 0 ?}
                                ?};
                        _vat:={? _gr*PVAT.GRVAT().KOD=0
                              || PVAT.VAT
                              || 0
                              ?};
                        _vat_odl:={?  _r=_r_c & _odl
                                  || {? TTPAR.PROC
                                     || PVAT.VAT_ODL
                                     |? -PVAT.GRVAT().KOD='zinwpods' | -PVAT.GRVAT().KOD='zakupods'
                                     || PVAT.VAT_ODL
                                     || PVAT.VAT
                                     ?}
                                  || 0
                                  ?};
                        _brutto:=PVAT.SUM1;
                        _dok:='['+$DVAT.NR+'] '+DVAT.SYM1;
                        TTVAT.prefix(TTVAT.ref(),_dok,);
                        {? ~TTVAT.first()
                        || TTVAT.prefix();
                           TTVAT.blank();
                           TTVAT.TREEREF:=TTVAT.ref();
                           TTVAT.LP:=DVAT.NR;
                           TTVAT.POZ:=4;
                           TTVAT.REF:=#DVAT.ref();
                           TTVAT.LABEL:=_dok;
                           TTVAT.KH:=DVAT.KH;
                           TTVAT.NIP:=DVAT.NIP;
                           TTVAT.DATA_ZAP:=$DVAT.DAT1;
                           TTVAT.DATA_DOK:=$DVAT.DAT2;
                           TTVAT.DATA_TP:=$DVAT.DAT3;
                           TTVAT.DATA_OTR:=$DVAT.DAT4;
                           TTVAT.DATA_OPE:=$DVAT.DAT5;
                           TTVAT.SYM:=DVAT.SYM1;
                           TTVAT.NETTO:=_netto;
                           TTVAT.VAT:=_vat;
                           TTVAT.VAT_ODL:=_vat_odl;
                           TTVAT.BRUTTO:=_brutto;
                           TTVAT.POZ2:=_poz2;
                           TTVAT.NALICZ:=_r=_r_c;
                           TTVAT.POMIN:=_pomin;
                           TTVAT.KRAJ:=DVAT.KRAJ().KOD;
                           TTVAT.KWARTAL:=$DVAT.OBR().KWARTAL;
                           TTVAT.add()
                        || TTVAT.NETTO+=_netto;
                           TTVAT.VAT+=_vat;
                           TTVAT.BRUTTO+=_brutto;
                           TTVAT.VAT_ODL+=_vat_odl;
                           TTVAT.put()
                        ?};
                        TTVAT.prefix();
                        {? _pomin=0
                        || {!
                           |? TTVAT.seek(TTVAT.TREEREF,);
                              TTVAT.NETTO+=_netto;
                              TTVAT.VAT+=_vat;
                              TTVAT.VAT_ODL+=_vat_odl;
                              TTVAT.BRUTTO+=_brutto;
                              TTVAT.put();
                              TTVAT.TREEREF<>0
                           !}
                        ?}
                     ?};
                     _x-=1;
                     _x
                  !}
               ?};
               PVAT.next()
            !}
         ?};
         TTVAT.cntx_pop()
      ?};
      DVAT.next()
   !}
?};
_w:=TTVAT.mk_sel('Analiza VAT'@,'P',0,'#ttvat_ana',,,,1);
TTVAT.win_fld(_w,,'LABEL',,,30,,1,,1);
{? TTPAR.ALL=1
|| TTVAT.win_fld(_w,,'POMIN',,,3,,1,'Pomiń',0,'Pozycja pomijana w JPK_V7',2,,"1","0")
?};
{? __wgkraj
|| TTVAT.win_fld(_w,,'KRAJ',,,16,,1,,0,'Kraj opodatkowania');
   TTVAT.win_fld(_w,,'KWARTAL',,,16,,1,,0,'Kwartał');
   TTVAT.win_fld(_w,,'NETTO',,,16,,1,,0,'Wartość Netto');
   TTVAT.win_fld(_w,,'VAT',,,16,,1,,0,'Wartość podatku VAT');
   TTVAT.win_fld(_w,,'VAT_ODL',,,16,,1,,0,'Wartość podatku VAT do odliczenia');
   TTVAT.win_fld(_w,,'BRUTTO',,,16,,1,,0,'Wartość brutto');
   TTVAT.win_fld(_w,,'KH',,,16,,1,,0,'Nazwa kontrahenta');
   TTVAT.win_fld(_w,,'DATA_DOK',,,,,1,,0,'Data wystawienia dokumentu');
   TTVAT.win_fld(_w,,'DATA_TP',,,,,1,,0,'Termin płatności')
|| TTVAT.win_fld(_w,,'NETTO',,,16,,1,,0,'Wartość Netto');
   TTVAT.win_fld(_w,,'VAT',,,16,,1,,0,'Wartość podatku VAT');
   TTVAT.win_fld(_w,,'VAT_ODL',,,16,,1,,0,'Wartość podatku VAT do odliczenia');
   TTVAT.win_fld(_w,,'BRUTTO',,,16,,1,,0,'Wartość brutto');
   TTVAT.win_fld(_w,,'KH',,,16,,1,,0,'Nazwa kontrahenta');
   TTVAT.win_fld(_w,,'DATA_DOK',,,,,1,,0,'Data wystawienia dokumentu');
   TTVAT.win_fld(_w,,'DATA_TP',,,,,1,,0,'Termin płatności')
?};
TTVAT.win_fml(_w,,'LABEL',,'ICON_BEFORE',"
   {? TTVAT.POZ=4
   || 'xwin16.png:76'
   || {? TTVAT.tr_state() || 'xwin16.png:75' || 'xwin16.png:74' ?}
   ?}
");
TTVAT.win_act(_w,0,'Rekord',,,,"
   {? TTVAT.REF
   || TTVAT.actions_grayed(TTVAT.win_sel('?'))
   || TTVAT.actions_grayed(TTVAT.win_sel('?'),'ZTW')
   ?};
   ''
");
TTVAT.win_act(_w,0,'Wyświetl',,,,"
   DVAT.prefix();
   {? TTVAT.REF & DVAT.seek(TTVAT.REF,)
   || DVAT.RVAT();
      exec('vat_disp','!fks_vat_zrev')
   ?}
");
TTVAT.win_act(_w,0,'Formuła','Zwiń/rozwiń &całość'@@,,'Zwija/rozwija wszystkie gałęzie'@,"
   {! |? TTVAT.seek(TTVAT.TREEREF,) & TTVAT.TREEREF !};
   TTVAT.tr_set(~TTVAT.tr_state(),TTVAT.win_sel('?'),0,0)
",,,,,,'C');
TTVAT.win_act(_w,0,'Formuła','Po&zycje'@@,,'Pozycje dokumentu'@,"
   DVAT.prefix();
   {? TTVAT.REF
   || {? exec('seek_dvat','!fks_vat_zrev')
      || DOK.cntx_psh(); POZ.cntx_psh(); POW.cntx_psh(); AN.cntx_psh(); AN_SLU.cntx_psh();
         _rn:=ref_name(DVAT.DOK);
         DOK.use(_rn); DOK.prefix();
         _rn:=4-_rn;
         POZ.use('pozy'+_rn);
         POW.use('pow_'+_rn);
         _rn:=2+_rn;
         AN.use('koan__'+_rn);
         AN_SLU.use('koansl'+_rn);
         DVAT.DOK();
         PVAT.index('NR'); PVAT.prefix(DVAT.ref());
         POZ.index('DOK'); POZ.prefix(DOK.ref()); POZ.first();
         {? PAR_SKID.get(36)='T' & PAR_SKID.get(35)='T'
         || POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?}+'2')
         || POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?})
         ?};
         _w:=PVAT.grp_make('Pozycje dokumentu: %1'@[DVAT.SYM1],,'#pvatana');
         PVAT.grp_sel(_w,,'WER','Pozycje VAT'@,,,,,,,,,'maximized_with_title');
         PVAT.grp_sel(_w,POZ,'SEL','Dekrety'@,,,,,\"
            ROZRACH.TABELA:='POZ';
            exec('suma_dok','dok_fks');
            SLO.win_dict('ONE'); SLO.win_sel('ONE'); 1
         \",,,,'maximized_with_title');
         exec('gtu4dok','jpk_v',PVAT,_w,,,0);
         PVAT.win_sel(_w);
         POZ.actions('SEL','ATDpUZvs:D');
         PVAT.select();
         POZ.actions('SEL');
         DOK.cntx_pop(); POZ.cntx_pop(); POW.cntx_pop(); AN.cntx_pop(); AN_SLU.cntx_pop()
      ?}
   || FUN.info('Akcja dostępna dla dokumentów.'@)
   ?}
",,1,,,,'Z');
TTVAT.win_act(_w,0,'Formuła','Dokumen&t źródłowy'@@,,'Dokument źródłowy'@,"
   DVAT.prefix();
   {? TTVAT.REF
   || {? exec('seek_dvat','!fks_vat_zrev')
      || DOK.cntx_psh();
         DOK.use(ref_name(DVAT.DOK)); DOK.prefix();
         DVAT.DOK();
         exec('dok_spac','dok_fks');
         DOK.cntx_pop()
      ?}
   || FUN.info('Akcja dostępna dla dokumentów.'@)
   ?}
",,,,,,'T');
TTVAT.win_act(_w,0,'Formuła','&Wycofaj z VAT'@@,,'Wycofanie dokumentu z rejestru VAT'@,"
   DVAT.prefix();
   {? TTVAT.REF
   || {? exec('seek_dvat','!fks_vat_zrev') & exec('wyc_vat','!fks_vat_ddok')
      || exec('ana_vat_wyc','!fks_vat_zrev')
      ?}
   || FUN.info('Akcja dostępna dla dokumentów.'@)
   ?}
",,,,,,'W');
TTVAT.win_act(_w,0,'Formuła','&Drukuj'@@,,'Wydruk prezentowanych danych'@,"
   exec('rep_exec','#b_report','FKS_VAT_ZREV','fks_vat_ana',,,,,,,,'W')
",,,,,,'D');
TTVAT.win_sel(_w);
TTIND:=TTVAT.ndx_tmp(,,'TREEREF',,,'LP',,);
TTIND_R:=TTVAT.ndx_tmp(,,'REF',,);
TTVAT.index(TTIND);
DOK.cntx_psh(); POZ.cntx_psh(); POW.cntx_psh(); AN.cntx_psh(); AN_SLU.cntx_psh();
TTVAT.select();
DOK.cntx_pop(); POZ.cntx_pop(); POW.cntx_pop(); AN.cntx_pop(); AN_SLU.cntx_pop();
DVAT.cntx_pop(); PVAT.cntx_pop(); DOK.cntx_pop();
VAR_DEL.delete('TTVAT','TTIND','TTIND_R','TTPAR')


\vat_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wyświetla dokument VAT
::  OLD: \vat_disp/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_k:=RVAT.KVAT().SYM;
{? _k='SprzEksp'
|| DVAT.win_edit('RED_E')
|? 1+_k='S' | 1+_k='_'
|| DVAT.win_edit('RED_S')
|? 1+_k='Z' | 4+_k='ImpU'
|| DVAT.win_edit('RED_Z')
|? 4+_k='ImpT'
|| DVAT.win_edit('RED_I')
?};
DVAT.display()


\seek_dvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2009+]
:: OPIS: Analiza VAT - czy istnieje dokument VAT
::  OLD: \seek_dvat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
DVAT.prefix();
{? DVAT.seek(TTVAT.REF,) & DVAT.SYM1=TTVAT.SYM
|| 1
|| FUN.info('Nie znaleziono dokumentu VAT.'@);
   TTVAT.cntx_psh(); TTVAT.index(TTIND_R);
   TTVAT.prefix(0); TTVAT.last(); TTVAT.prefix();
   {? TTVAT.next()
   || {!
      |? {? DVAT.seek(TTVAT.REF,) & DVAT.SYM1=TTVAT.SYM
         || TTVAT.LABEL:='['+$DVAT.NR+'] '+DVAT.SYM1;
            TTVAT.put();
            TTVAT.next()
         || _vn:=TTVAT.NETTO; _vv:=TTVAT.VAT; _vb:=TTVAT.BRUTTO;
            _tr:=TTVAT.TREEREF;
            _ok:=TTVAT.del();
            TTVAT.cntx_psh(); TTVAT.prefix();
            {!
            |? TTVAT.seek(_tr,);
               _tr:=TTVAT.TREEREF;
               TTVAT.NETTO-=_vn;
               TTVAT.VAT-=_vv;
               TTVAT.BRUTTO-=_vb;
               {? TTVAT.NETTO | TTVAT.VAT | TTVAT.BRUTTO | TTVAT.TREEREF=0
               || TTVAT.put()
               || TTVAT.del()
               ?};
               _tr
            !};
            TTVAT.cntx_pop();
            _ok
         ?}
      !}
   ?};
   TTVAT.cntx_pop();
   TTVAT.first();
   0
?}


\ana_vat_wyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2009+]
:: OPIS: Analiza VAT - wycofanie dokumentu
::  OLD: \ana_vat_wyc/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_del:=_next:=0;
_gr:='SprArt22,Próbki,Prezenty,Druki,SprPozKr,SprPKWSU';
TTVAT.cntx_psh();
TTVAT.index(TTIND_R); TTVAT.prefix(TTVAT.REF);
{? TTVAT.first()
|| {!
   |? TTVAT.cntx_psh();
      _nxt:={? TTVAT.next() || TTVAT.ref() || 0 ?};
      TTVAT.cntx_pop();
      _netto:=_vat:=_brutto:=_vat_odl:=0;
      {? PVAT.first()
      || {!
         |? _gv:=PVAT.GRVAT().KOD;
            _r_c:=TTVAT.NALICZ & TTVAT.POZ2=_gv;
            _r_z:=~TTVAT.NALICZ & TTVAT.POZ2=PVAT.STVAT().KOD;
            {? (_r_c | _r_z) & (PVAT.TYP_VAT='' | _r_c & PVAT.TYP_VAT='C' | _r_z & PVAT.TYP_VAT='Z')
            || _netto+={? TTPAR.PROC
                       || {?'ZInwPodS,ZInwPodZ,ZakPRopZ,ZakuPodS,ZakuPodZ,ZInwNPod,ZPozNPal,ZPozNPod,ZakupNpr'*_gv
                          || {? PVAT.VAT || (PVAT.SUM2*PVAT.VAT_ODL/PVAT.VAT)$2 || PVAT.SUM2$2 ?}
                          |? _gv='ZakuPods' | _gv='ZInwPods'
                          || {? PVAT.VAT || (PVAT.SUM2*(PVAT.VAT/2)$2/PVAT.VAT)$2 || PVAT.SUM2$2 ?}
                          || PVAT.SUM2
                          ?}
                       || PVAT.SUM2
                       ?};

               _vat+={? _gr*PVAT.GRVAT().KOD=0
                     || PVAT.VAT
                     || 0
                     ?};
               {? _r_c || _vat_odl+={? TTPAR.PROC
                                    || PVAT.VAT_ODL
                                    |? -_gv='zinwpods' | -_gv='zakupods'
                                    || (PVAT.VAT/2)$2
                                    || PVAT.VAT
                                    ?}
               ?};
               _brutto+=PVAT.SUM1
            ?};
            PVAT.next()
         !}
      ?};
      _rn:=_netto-TTVAT.NETTO;
      _rv:=_vat-TTVAT.VAT;
      _ro:=_vat_odl-TTVAT.VAT_ODL;
      _rb:=_brutto-TTVAT.BRUTTO;
      _pomin:=TTVAT.POMIN;
      TTVAT.cntx_psh(); TTVAT.prefix();
      {!
      |? TTVAT.NETTO+=_rn;
         TTVAT.VAT+=_rv;
         TTVAT.VAT_ODL+=_ro;
         TTVAT.BRUTTO+=_rb;
         _tr:=TTVAT.TREEREF;
         {? TTVAT.NETTO<>0 | TTVAT.VAT<>0 | TTVAT.VAT_ODL<>0 | TTVAT.BRUTTO<>0
           | (TTVAT.cntx_psh(); TTVAT.index(TTIND); TTVAT.prefix(#TTVAT.ref); _jest:=TTVAT.first(); TTVAT.cntx_pop(); _jest)
         || TTVAT.put()
         || TTVAT.cntx_psh(); TTVAT.index(TTIND); TTVAT.prefix(TTVAT.TREEREF);
            _next:={? TTVAT.next() | TTVAT.prev() || #TTVAT.ref() || TTVAT.TREEREF ?};
            TTVAT.cntx_pop();
            TTVAT.del(); _del:=1
         ?};
         {? _pomin
         || _rn:=_rv:=_ro:=_rb:=0
         ?};
         TTVAT.seek(_tr,) & TTVAT.TREEREF
      !};
      TTVAT.NETTO+=_rn;
      TTVAT.VAT+=_rv;
      TTVAT.VAT_ODL+=_ro;
      TTVAT.BRUTTO+=_rb;
      TTVAT.put();
      TTVAT.cntx_pop();
      _nxt & TTVAT.seek(_nxt)
   !}
?};
{? _del
|| TTVAT.prefix(0); TTVAT.last(); TTVAT.prefix();
   {? TTVAT.next()
   || DVAT.prefix();
      {!
      |? {? DVAT.seek(TTVAT.REF,)
         || TTVAT.LABEL:='['+$DVAT.NR+'] '+DVAT.SYM1;
            TTVAT.put()
         ?};
         TTVAT.next()
      !}
   ?}
?};
TTVAT.cntx_pop();
{? _next || TTVAT.seek(_next,) ?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 c51937ea47983a06049693ac5d4ef53d04c5fd417ab12aa09b48754afaab7dd90a2eda6295c592f347bec4a4a3e0bf821ec5d8164c9a722ff07254fd66de1d8abb34e75ef19aa3a1fab55471f4cc0f57a869fbe1e766cdfc34cf24ccefbca37c45dd8e0019a84b9b6ffb3b641f843d1b999a109e75ccf78f5798f62bcd7b2b83

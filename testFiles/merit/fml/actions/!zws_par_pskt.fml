:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_pskt.fml
:: Utworzony: 20.01.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PSKT - Atrybuty składników płacowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Redagowanie atrybutów składników płacowych - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \def_sel/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

:  Zmienna do obsługi wyboru symbolu atrybutu.
_rasym:=obj_new('SYMBOL','ED','F3');
_rasym.SYMBOL:=_rasym.ED:=_rasym.F3:=0;

:  Uchwyt do tablicy z tabelą tymczasową do przechowywania rubryk (przywiązanych do atrybutu) i okienkiem wertowania.
_rub:=obj_new('TAB','WS');

: Uchwyt do tablicy z tabelą i oknem do obsługi wirtualnych grup atrybutu (rzeczywiste + dziedziczone).
_grpn:=obj_new('TAB','WS');

: Uchwyt do tablicy z tabelą i oknem do obsługi atrybutów bieżącej rubryki.
_atr4x:=exec('atr4x','!zws_par_pskt');

: Uchwyt do tablicy z tabelą i oknem do obsługi położenia atrybutu (drzewko).
_def_tree:=exec('def_tree','rubatr');

: Uchwyt do tablicy z tabelą, oknem wertowania i oknem grupowym do obsługi atrybutów, które moga być dodane do bieżącej
: rubryki płacowej. W oknie grupowym prezentowane jest jeszcze położenie bieżącego atrybutu - stąd obowiązkowy parametr
: _def_tree.
_atrd:=exec('atrd','rubatr',_def_tree);

:  Flagi (w tablicy).
_flagi:=obj_new('tryb','status');
:  Tryb pracy - bieżąca zakładka.
_flagi.tryb:='';
:  Zmienna wykorzystywana w akcjach grupowych do przekazywania statusu operacji.
_flagi.status:='';

params_set(
   'rasym',_rasym,
   'rub',_rub,
   'grpn',_grpn,
   'atr4x',_atr4x,
   'def_tree',_def_tree,
   'atrd',_atrd,
   'flagi',_flagi
);

R.cntx_psh(); R.index('RUBKOD'); R.clear();
{? R.first()
|| RA_GRPN.cntx_psh(); RA_GRPN.index('SYMBOL'); RA_GRPN.prefix(); RA_GRPN.first(); RA_GRPN.actions('WER','W');
   RA_GRPP.cntx_psh();

   RA_USE.cntx_psh();
   RA_VAL.cntx_psh();
   RA_DEF.cntx_psh();
   RA_DEF.clear();
   {? ~RA_DEF.first() &
      FUN.ask(
         'W systemie nie zdefiniowano jeszcze atrybutów rubryk płacowych.\n'
         'Czy utworzyć zapisy z domyślną konfiguracją?'@
      )
   || exec('defaults','rubatr');
      exec('ra_grpn_import','rubatr',1)
   ?};
   RA_DEF.index('RA_DEF');
   RA_DEF.win_sel(exec('panel','!zws_par_pskt'));
   RA_DEF.select();
   RA_DEF.cntx_pop();
   RA_VAL.cntx_pop();
   RA_USE.cntx_pop();
   RA_GRPP.cntx_pop();
   RA_GRPN.cntx_pop();

: !!! Zmiany w atrybutach mogą wymagać przebudowy obiektów wyliczających.
::LICZ.Build();
::LICZRHS.Build();
   exec('jednoznaczne','rubatr')
|| FUN.info('Redagowanie atrybutów rubryk płacowych będzie możliwe po zdefiniowaniu rubryk płacowych.'@)
?};
R.cntx_pop();

~~


\ra_def_typ_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed redagowaniem pola RA_DEF.TYP. Jeśli dla bieżącego atrybutu określono jego wartości, to pole staje się
::       niedostępne do edycji.
::   WE:
::   WY:
::  TAG: <BE>
::  OLD: \ratypbe/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()='popraw'
|| RA_VAL.index('UNIQUE');
   RA_VAL.prefix(RA_DEF.ref());
   ~RA_VAL.first()
|| 1
?}


\ra_tmp_symb_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed redagowaniem pola RA_TMP.SYMB. Zapamiętuje wartość pola. Blokuje redagowanie pola dla akcji Popraw
::       atrybutu systemowego.
::   WE:
::   WY:
::  TAG: <BE>
::  OLD: \tdef_prr/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_rasym:=params_get().rasym;
_rasym.SYMBOL:=RA_TMP.SYMB;
_rasym.F3:=0;
_rasym.ED


\ra_tmp_symb_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji F3 dla pola RA_TMP.SYMB. Pozwala na wybór symbolu atrybutu nadrzędnego.
::   WE:
::   WY:
::  TAG: <F3>
::  OLD: \tdef_f3/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_rasym:=params_get().rasym;
_rasym.F3:=1;

RA_DEF.cntx_psh();
RA_DEF.win_sel('WYB');
RA_DEF.prefix();
RA_DEF.index('SYMBOL');
RA_DEF.find_key(fld());
{? RA_DEF.select(,1)
|| RA_TMP.RA_DEF:=RA_DEF.ref();
   RA_TMP.SYMB:=RA_DEF.SYMBOL;
   RA_TMP.NAZWA:=RA_DEF.NAZWA
|| RA_TMP.SYMB:=_rasym.SYMBOL
?};
RA_DEF.cntx_pop();
RA_TMP.SYMB


\ra_tmp_symb_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola RA_TMP.SYMB wykorzystywanego w okienkach redakcji. Sprawdza, czy podany w polu symbol
::       odpowiada symbolowi atrybutu.
::   WY: 1 - Podany w polu symbol jest symbolem atrybutu.
::       0 - Nie udało się odnaleźć atrybutu o podanym symbolu.
::   WY:
::  TAG: <AE>
::  OLD: \tdef_por/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_rasym:=params_get().rasym;

{? RA_TMP.SYMB=0
|| RA_TMP.RA_DEF:=null();
   RA_TMP.NAZWA:='';
   1

|? _rasym.SYMBOL=RA_TMP.SYMB
:  Redagowanie nic nie zmieniło.
|| 1

|? _rasym.F3
:  Obsługa przez F3 załatwia wszystko.
|| 1

|| _ret:=0;
   RA_DEF.cntx_psh();
   RA_DEF.index('SYMBOL');
   RA_DEF.prefix();
   {? RA_DEF.find_key(fld())
   || RA_TMP.RA_DEF:=RA_DEF.ref();
      RA_TMP.SYMB:=RA_DEF.SYMBOL;
      RA_TMP.NAZWA:=RA_DEF.NAZWA;
      _ret:=1
   || FUN.info('Brak pozycji w słowniku.'@)
   ?};
   RA_DEF.cntx_pop();
   _ret

?}


\ra_def_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Dołącz dla tabeli RA_DEF w panelu zarządzania atrybutami. Tworzy parametr użytkownika.
::   WE:
::   WY:
::  TAG: <ADD>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_par.rasym.ED:=1;
params_set(_par);

exec('ra_tmp_set','rubatr',#RA_DEF.ref());
RA_DEF.blank();
RA_DEF.memo_set('','OPIS');
RA_DEF.win_edit('RED');
{? RA_DEF.edit("exec('ra_def_ae','!zws_par_pskt',0)") & exec('add','#table')
|| RA_DEF.memo_put(,'OPIS')
?}


\ra_def_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Popraw dla tabeli RA_DEF w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <PUT>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_par.rasym.ED:=0;
params_set(_par);

RA_DEF.memo_get(,'OPIS');
exec('ra_tmp_set','rubatr',RA_DEF.RA_DEF);
{? RA_DEF.edit("exec('ra_def_ae','!zws_par_pskt',1)") & exec('put','#table')
|| RA_DEF.memo_put(,'OPIS')
?}


\ra_def_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Usuń dla tabeli RA_DEF w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? RA_DEF.RODZAJ='S'
|| FUN.info('Atrybut systemowy nie może być usunięty.'@)
|? exec('del_conf','#table')
|| RA_DEF.del()
?}


\ra_def_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redakcji rekordu tabeli RA_DEF.
::   WE: _a - Tryb modyfikacji:
::          0 - Dołączanie;
::          1 - Poprawianie.
::   WY:
::  TAG: <CHK>
::  OLD: \def_chk/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
: Ustaw wybrany nadrzędny.
RA_DEF.RA_DEF:=RA_TMP.RA_DEF;
: Sprawdź wypełnienie pól i unikalność kluczy.
__CHK.table(RA_DEF,_a,,'SYMBOL','NAZWA')


\ra_def_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wartość początkowa pól wskazujących na atrybut.
::   WE:
::   WY:
::  OLD: \RA_DEF/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
RA_DEF.ref()


\set_val
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [2009]
:: OPIS: Zamienia wartość na formułę.
::   WE: _a - Numer typu wartości.
::  OLD: \set_val/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_val:=($('RA_TMP.WARTOSC'+$RA_DEF.TYP))();
RA_VAL.WARTOSC:=
   {? RA_DEF.TYP=1 || form(_val,,,'9.')
   |? RA_DEF.TYP=2 || '\''+gsub(gsub(_val,'\\','\\\\'),'\'','\\\'')+'\''
   |? RA_DEF.TYP=4 || 'date('+$(_val~1)+','+$(_val~2)+','+$(_val~3)+')'
   |? RA_DEF.TYP=5 || 'time('+$(_val~1)+','+$(_val~2)+','+$(_val~3)+')'
   ?}


\ra_val_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Dołącz w okienku WER tabeli RA_VAL w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <ADD>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_blank:=
   {? RA_DEF.TYP=1 || 0
   |? RA_DEF.TYP=2 || ''
   |? RA_DEF.TYP=4 || date(0,0,0)
   |? RA_DEF.TYP=5 || time(0,0,0)
   ?};
($('RA_TMP.WARTOSC'+$RA_DEF.TYP+':=_a'))(_blank);
RA_VAL.blank();
{? RA_VAL.edit("exec('ra_val_ae','!zws_par_pskt',0)")
|| exec('set_val','!zws_par_pskt');
   exec('add','#table')
?}


\ra_val_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Popraw w okienku WER tabeli RA_VAL w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <PUT>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? RA_VAL.edit("exec('ra_val_ae','!zws_par_pskt',1)")
|| exec('set_val','!zws_par_pskt');
   exec('put','#table')
?}


\ra_val_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Usuń w okienku WER tabeli RA_VAL w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('del_ask','#table') || RA_VAL.del(,1) ?}


\ra_val_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed" (przed wyświetleniem rekordu) dla tabeli RA_VAL w panelu zarządzania atrybutami.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_val:=($RA_VAL.WARTOSC)();
_typ:=RA_VAL.RA_DEF().TYP;
:: Pole RA_TMP.WARTOSC2 (typu napisowego) prezentowane jest w oknie wertowania - sformatujmy wartość (zawsze).
RA_TMP.WARTOSC2:=
   {? _typ=1 || form(_val,30,3)
   |? _typ=2 || _val
   |? _typ=4 || _val$1
   |? _typ=5 || _val$3
   || ''
   ?};
:: Pola pozostałych typów prezentowane są w oknie redagowania (Wyświetl / Popraw) - przypiszmy wartość.
{? _typ<>2
|| ($('RA_TMP.WARTOSC'+$_typ+':=_a'))(_val)
?};
0


\ra_val_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redakcji rekordu tabeli RA_VAL.
::   WE: _a - Tryb modyfikacji:
::          0 - Dołączanie;
::          1 - Poprawianie.
::   WY:
::  TAG: <CHK>
::  OLD: \menu/rubobj.fml
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__CHK.index(RA_VAL,_a)=''


\panel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła tworząca okienko grupowe (panel) do zarządzania atrybutami rubryk.
::   WE:
::   WY: Uchwyt utworzonego okienka.
::  OLD: \raconfig/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();

: Formuły obsługująca akcje, domyślnie wywoływane z bieżącego pliku.
_fml0:='"params_set(params_get()); '+
       'exec(\'"+_a+"\',\'"+{? var_pres(\'_b\')=type_of(\'\') || _b || \'!zws_par_pskt\' ?}+"\')"';
_fml1:='"params_set(params_get()); '+
       'exec(\'"+_a+"\',\'"+{? var_pres(\'_b\')=type_of(\'\') || _b || \'!zws_par_pskt\' ?}+"\',_a"+'+
       '{? var_pres(\'_c\')=type_of(\'\') || ",\'"+_c+"\'" || "" ?}+")"';

: Wykorzystywane rubryki
_tab:=_par.rub.TAB:=__RUB.buf();
_ws:=_par.rub.WS:=_tab.mk_sel('Rubryki'@,'T',,'wykrub',,,,,'U');
_tab.win_fld(_ws,,'RN',,,5,,,,,'Symbol / kod'@);
_tab.win_fld(_ws,,'RT',,,20,,,,,'Krótki opis'@);
_tab.win_act(_ws,1,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_fml0)('rub_dolacz'),,1,,,,'D');
_tab.win_act(_ws,0,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_fml0)('rub_dolacz'),,,,,,'D');
_tab.win_act(_ws,0,'Formuła','Usuń'@@,,'Usunięcie bieżącego zapisu'@,$($_fml0)('rub_ub'),$($_fml0)('rub_ua'),,
   1,$($_fml0)('rub_ubg'),$($_fml0)('rub_uag'),'U');
_tab.win_act(_ws,0,'Szukaj');
_tab.win_act(_ws,0,'Kolejność');
_tab.win_btn(_ws,'text=%1' ['Dołącz'@],'menu:D',,,,,,'');
_tab.win_btn(_ws,'text=%1' ['Usuń'@],'menu:U',,,,,,'noempty');
obj_del(_tab); &_tab;
&_ws;

: Tabela z wirtualnymi grupami atrybutu (rzeczywiste + dziedziczone)
_tab:=_par.grpn.TAB:=tab_tmp(1,
   'SYMBOL','STRING[10]','Symbol'@,
   'OPIS','STRING[60]','Opis'@,
   'D','INTEGER','Dziedziczone?'@
);

_ws:=_par.grpn.WS:=_tab.mk_sel('Grupy atrybutu'@,'N',,'a4r_grpn',,,,,'U');
_tab.win_fld(_ws,,'SYMBOL',,,15,,,,,'Unikalny symbol'@);
_tab.win_fld(_ws,,'OPIS',,,60,,,,,'Krótki opis'@);
_tab.win_act(_ws,1,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_fml0)('grpn_dolacz'),,1,,,,'D');
_tab.win_act(_ws,0,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_fml0)('grpn_dolacz'),,,,,,'D');
_tab.win_act(_ws,0,'Formuła','Usuń'@@,,'Usunięcie bieżącego zapisu'@,$($_fml0)('grpn_ub'),$($_fml0)('grpn_ua'),,
   1,$($_fml0)('grpn_ubg'),$($_fml0)('grpn_uag'),'U');
_tab.win_act(_ws,0,'Formuła','Legenda'@@,,'Legenda'@@,$($_fml0)('grpn_legenda'),,,,,,'L',,'target=window');
_tab.win_act(_ws,,'Rekord',,,,$($_fml0)('grpn_bd'));
_tab.win_btn(_ws,'text=%1' ['Dołącz'@],'menu:D',,,,,,'');
_tab.win_btn(_ws,'text=%1' ['Usuń'@],'menu:U',,,,,,'noempty');
obj_del(_tab); &_tab;
&_ws;
_wer:=exec('ra_def_tab','!zws_par_pskt');
: Budowa panelu
_mode:='maximized_with_title';

: Zakładka nr 1: Atrybut -> Rubryki.
_gr:=RA_DEF.grp_make('Atrybuty rubryk'@,,'ra_def_config',,,,,'html_maximized');
RA_DEF.grp_sel(_gr,RA_DEF,_wer,'Rubryki wg atrybutów'@,$($_fml0)('ra_def_ar'),,,,"params_get().flagi.tryb:='AR'","",,,
   _mode);
RA_DEF.tab_splt(_gr,,'vertical','rub');
RA_DEF.grp_sel(_gr,_par.rub.TAB,_par.rub.WS,,$($_fml0)('rub_ar'),,,,$($_fml1)('rub_bs'),$($_fml1)('rub_as'),,,_mode);
RA_DEF.tab_splt(_gr,'rub','vertical','daty');
RA_DEF.grp_sel(_gr,RA_USE,'WER',,"",,,,$($_fml1)('ra_use_bs','rubatr'),"",,,_mode);

: Zakładka nr 2: Rubryka -> Atrybuty.
RA_DEF.grp_sel(_gr,R,'SLO','Atrybuty wg rubryk'@,$($_fml0)('r_ar'),,,,"params_get().flagi.tryb:='RA';_ws:=params_get().atr4x.WS;params_get().atr4x.TAB.actions_grayed(_ws,)","",,,_mode);
RA_DEF.tab_splt(_gr,,'vertical','atrybuty');
RA_DEF.grp_sel(_gr,_par.atr4x.TAB,_par.atr4x.WS,,$($_fml0)('atr4x_ar','!zws_par_pskt'),,,,
   $($_fml1)('atr4x_bs','rubatr'),$($_fml1)('atr4x_as'),,,_mode);
RA_DEF.tab_splt(_gr,,'vertical','daty');
RA_DEF.grp_sel(_gr,RA_USE,'WER',,"",,,,$($_fml1)('ra_use_bs','rubatr'),"",,,_mode);

: Zakładka nr 3: Grupy (i przywiązane do nich rubryki oraz atrybuty).
RA_DEF.grp_sel(_gr,RA_GRPN,'WER','Grupy'@,$($_fml0)('ra_grpn_ar'),,,,"params_get().flagi.tryb:='GX';_ws:=params_get().atr4x.WS;params_get().atr4x.TAB.actions_grayed(_ws,'R')",,,,_mode);
RA_DEF.tab_splt(_gr,,'vertical','rubryki');
RA_DEF.grp_sel(_gr,RA_GRPP,'WERR',,"",,,,$($_fml1)('ra_grpp_bs','!zws_par_pskt'),$($_fml1)('ra_grpp_as','!zws_par_pskt'),,,_mode);
RA_DEF.tab_splt(_gr,'tab0','horizontal','atrybuty',7);
RA_DEF.grp_sel(_gr,_par.atr4x.TAB,_par.atr4x.WS,,$($_fml0)('atr4x_ar','!zws_par_pskt'),,,6,
   $($_fml1)('atr4x_bs','rubatr'),$($_fml1)('atr4x_as'),,,_mode);

: Zakładka nr 4: Grupy atrybutów (i przywiązane do nich rubryki oraz atrybuty).
RA_DEF.grp_sel(_gr,RA_DEF,'WER','Grupy atrybutów'@,$($_fml0)('ra_def_ar'),,,,"params_get().flagi.tryb:='GA'","",,,
   _mode);
RA_DEF.tab_splt(_gr,,'vertical','rub');
RA_DEF.grp_sel(_gr,_par.rub.TAB,_par.rub.WS,,$($_fml0)('rub_ar'),,,,$($_fml1)('rub_bs'),$($_fml1)('rub_as'),,,_mode);
RA_DEF.tab_splt(_gr,'tab0','horizontal','grpn');
RA_DEF.grp_sel(_gr,_par.grpn.TAB,_par.grpn.WS,,"",,,,$($_fml1)('grpn_bs'),"",,,_mode);

_gr


\ra_def_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po odświeżeniu okienka tabeli RA_DEF w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  OLD: \wnd_ar/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
grp_disp(_par.rub.TAB,_par.rub.WS);
{? _par.flagi.tryb='GA'
|| grp_disp(_par.grpn.TAB,_par.grpn.WS)
?}


\ra_val_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed obsługą okienka tabeli RA_VAL w panelu zarządzania atrybutami.
::   WE: _a - Moment (przyczyna) wywołania:
::          0 - Okienko odświeżane na skutek wywołania grp_disp().
::          1 - Okienko zostało aktywowane.
::   WY:
::  OLD: \wnd_bs/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| RA_VAL.index('UNIQUE');
   RA_VAL.prefix(RA_DEF.ref());
   RA_VAL.first()
|? _a=1
|| {? RA_DEF.ref()=null()
   || _ha:='DPU:D';
      _da:=''
   || RA_VAL.win_edit('RED'+$RA_DEF.TYP);
      _ha:='';
      _da:=':D'
   ?};
   RA_VAL.actions('WER',_ha,_da,1)
?}


\rub_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po odświeżeniu okienka tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  OLD: \wnd_ar/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
grp_disp(RA_USE,'WER')


\rub_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed obsługą okienka tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE: _a - Moment (przyczyna) wywołania:
::          0 - Okienko odświeżane na skutek wywołania grp_disp().
::          1 - Okienko zostało aktywowane.
::   WY:
::  OLD: \wnd_bs/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a || return() ?};
_tab:=params_get().rub.TAB;
_tab.erase();
_tab.blank();

R.cntx_psh();
R.prefix();
RA_USE.cntx_psh();
RA_USE.index('RA_USE');
RA_USE.prefix(RA_DEF.ref());
{? RA_USE.first()
|| {!
   |? {? RA_USE.R<>_tab.R
      || _tab.RN:=RA_USE.R().RN;
         _tab.RT:=R.RT;
         _tab.R:=R.ref();
         _tab.add(1)
      ?};
      RA_USE.next()
   !};
   _tab.first()
?};
RA_USE.cntx_pop();
R.cntx_pop()


\rub_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po obsłudze okienka tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE: _a - Moment (przyczyna) wywołania:
::          0 - Okienko odświeżane na skutek wywołania grp_disp().
::          1 - Okienko zostało aktywowane.
::   WY:
::  OLD: \wnd_as/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~_a
|| params_set(params_get());
   grp_disp(RA_USE,'WER')
?}


\r_wbg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Wybierz - przed" w okienku WERX tabeli R. Wykorzystywana przy dodawaniu rubryk do:
::          - atrybutu (AR);
::          - grupy atrybutów (GX).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FUN.ask(
   {? params_get().flagi.tryb='GX'
   || 'Czy dodać zaznaczone rubryki do bieżącej grupy?'@
   || 'Czy dodać zaznaczone rubryki do bieżącego atrybutu?'@
   ?}
)


\r_wb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wybierz - przed" w okienku WERX tabeli R - właściwe dodanie rubryki do atrybutu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_tryb:=_par.flagi.tryb;

{? _tryb='AR' | _tryb='GA'
:  Właściwe dodawanie rubryki do atrybutu.
|| _tab:=_par.rub.TAB;

   _tab.RN:=R.RN;
   _tab.RT:=R.RT;
   _tab.R:=R.ref();
   {? exec('add','#table',_tab)
   || exec('use_add','rubatr',RA_DEF.ref(),R.ref())
   ?}

|? _tryb='GX'
:  Właściwe dodawanie rubryki do grupy.
|| RA_GRPP.blank();
   RA_GRPP.RA_GRPN:=RA_GRPN.ref();
   RA_GRPP.R:=R.ref();
   RA_GRPP.add()
?}


\r_wa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wybierz - po" w okienku WERX tabeli R. Jeżeli akcja nie jest wykonywana dla grupy rekordów -
::       formuła kończy pracę z okienkiem.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~cur_tab().sel_size() || sel_exit() ?}


\r_wag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Wybierz - po" w okienku WERX tabeli R - kończy pracę z okienkiem.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
sel_exit()


\rub_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Dołącz w okienku tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);

R.cntx_psh();
R.index('RUBKOD');
R.prefix();
R.f_set('RN',,'"R".RN not in (select RN from :_a)',_par.rub.TAB);
R.win_sel('WYBX');
R.select();
R.f_clear();
R.cntx_pop();
0


\rub_ubg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Usuń - przed" w okienku tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
params_get().flagi.status:='';
exec('del_ask','#table')


\rub_ub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń - przed" w okienku tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
params_get().rub.TAB.sel_size() | exec('del_ask','#table')


\rub_ua
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń - po" w okienku tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_tab:=_par.rub.TAB;

do();
RA_USE.cntx_psh();
RA_USE.index('RA_USE');
RA_USE.prefix(RA_DEF.ref(),_tab.R);
_ok:=1;
{? RA_USE.first()
|| {!
   |? _del:=RA_USE.del(,1);
      {? ~_del || _ok:=0 |? _del=1 || 0 || 1 ?}
   !}
?};
RA_USE.cntx_pop();
{? _ok
|| _tab.del()
|| undo()
?};
{? ~end()
|| {? _tab.sel_size()
   || {? _par.flagi.status*'u'=0 || _par.flagi.status+='u' ?}
   || FUN.emsg('Usunięcie rubryki nie powiodło się.'@)
   ?}
?}


\rub_uag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Usuń - po" w okienku tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? params_get().flagi.status*'u'
|| FUN.emsg('Usunięcie wszystkich zaznaczonych rubryk nie powiodło się.'@)
?}


\grpn_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Dołącz dla tabeli tymczasowej grpn.TAB.
::   WE:
::   WY:
::  TAG:
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
RA_GRPN.cntx_psh();
RA_GRPN.index('SYMBOL');
RA_GRPN.prefix();
RA_GRPN.win_sel('WER');
_ha:=RA_GRPN.actions('WER','dpUA','W');
RA_GRPN.f_set('SYMBOL',,'"RA_GRPN".REFERENCE not in (select RA_GRPN from RA_GRPP where RA_DEF=\':_a\')',$RA_DEF.ref());
{? RA_GRPN.f_size()
|| RA_GRPP.cntx_psh(); RA_GRPP.prefix();
   RA_GRPN.select();
   RA_GRPP.cntx_pop()
|| FUN.info('Brak grup do dołączenia.'@)
?};
RA_GRPN.f_clear();
RA_GRPN.actions('WER',_ha);
RA_GRPN.cntx_pop();
~~


\grpn_ubg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Usuń - przed" w okienku tabeli tymczasowej grpn.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
params_get().flagi.status:='';
exec('del_ask','#table',,'Grupy dziedziczone nie są usuwane.'@)


\grpn_ub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń - przed" w okienku tabeli tymczasowej grpn.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab(1,1);
{? _tab.sel_size()
|| ~_tab.D
|? _tab.D
|| FUN.info('Usunięcie grupy dziedziczonej nie jest możliwe.'@);
    0
|| exec('del_ask','#table')
?}


\grpn_ua
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Usuń - po" w okienku tabeli tymczasowej grpn.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_grpn:=_par.grpn;
_flagi:=_par.flagi;

RA_GRPP.cntx_psh();
RA_GRPP.index('PA');
RA_GRPP.prefix(null(),RA_DEF.RODZAJ,RA_DEF.SYMBOL,_grpn.TAB.SYMBOL,);
{? RA_GRPP.first() & RA_GRPP.del(,1)
|| {? _flagi.status*'+'=0 || _flagi.status+='+' ?}
|| {? _flagi.status*'1'=0 || _flagi.status+='1' ?}
?};
{? ~_grpn.TAB.sel_size()
|| {? _flagi.status='1'
   || FUN.emsg('Usunięcie grupy nie powiodło się.'@)

   |? _flagi.status='+'
:     Jeżeli w wyniku obsługi akcji lub grupy usunięty został choć jeden rekord z tabeli RA_GRPP to powinniśmy
:     odświeżyć zawartość tabeli grpn.TAB. W tym celu wywołajmy akcję 'przed obsługą', odpowiedzialną za wypełnienie
:     (po wcześniejszym usunięciu wszystkich rekordów) tabeli grpn.TAB. Może się bowiem zdarzyć, że usuwana grupa
:     pojawi się w okienku jako dziedziczona. Pomysł realizowany jest poprzez ręczne wywołanie:
   || exec('grpn_bs','!zws_par_pskt',0)
   ?}
?};
RA_GRPP.cntx_pop()


\grpn_uag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji grupowej "Usuń - po" w okienku tabeli tymczasowej grpn.TAB w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG: <DEL>
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_status:=_par.flagi.status;

{? '1'*_status
|| FUN.emsg('Co najmniej jednej z zaznaczonych grup nie udało się usunąć.'@)
|? '+'*_status
|| exec('grpn_bs','!zws_par_pskt',0)
?}


\grpn_legenda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji Legenda dla tabeli tymczasowej grpn.TAB.
::   WE:
::   WY:
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','RA_DEF#04')


\grpn_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed" (przed wyświetleniem rekordu) okienka tabeli tymczasowej grpn.TAB.
::   WE:
::   WY:
::  TAG:
::  OLD: \menu/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,1).D || Color.fnd_kol('RA_DEF#04#01') || '' ?}


\grpn_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed obsługą okienka tabeli tymczasowej grpn.TAB w panelu zarządzania atrybutami. Formuła wykorzystywana do
::       odświeżenia zawartości tabeli grpn.TAB również po usuwaniu z niej rekordów.
::   WE: _a - Moment (przyczyna) wywołania:
::          0 - Okienko odświeżane na skutek wywołania grp_disp().
::          1 - Okienko zostało aktywowane.
::   WY:
::  OLD: \wnd_bs/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_grpn:=params_get().grpn;

{? ~_a
|| RA_DEF.cntx_psh(); RA_DEF.prefix();
   RA_GRPN.cntx_psh(); RA_GRPN.prefix();
   RA_GRPP.cntx_psh(); RA_GRPP.index('PA');
   _grpn.TAB.erase();
   _dziedzicz:=0;
   {!
   |? RA_GRPP.prefix(null(),RA_DEF.RODZAJ,RA_DEF.SYMBOL,);
      {? RA_GRPP.first()
      || {!
         |? {? ~_grpn.TAB.find_key(RA_GRPP.RA_GRPN().SYMBOL,)
            || _grpn.TAB.blank();
               _grpn.TAB.SYMBOL:=RA_GRPN.SYMBOL;
               _grpn.TAB.OPIS:=RA_GRPN.OPIS;
               _grpn.TAB.D:=_dziedzicz;
               _grpn.TAB.add()
            ?};
            RA_GRPP.next()
         !}
      ?};
      _dziedzicz+=1;
      {? RA_DEF.RA_DEF
      || RA_DEF.seek(RA_DEF.RA_DEF,)
      || 0
      ?}
   !};
   &_dziedzicz;
   RA_GRPP.cntx_pop();
   RA_GRPN.cntx_pop();
   RA_DEF.cntx_pop();
   _grpn.TAB.first()
?};
~~


\r_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po odświeżeniu okienka tabeli R w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  OLD: \wnd_ar/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
grp_disp(_par.atr4x.TAB,_par.atr4x.WS)


\ra_grpn_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po odświeżeniu okienka tabeli RA_GRPN w panelu zarządzania atrybutami.
::   WE:
::   WY:
::  TAG:
::  OLD: \wnd_ar/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_atr4x:=_par.atr4x;

grp_disp(RA_GRPP,'WERR',1);
grp_disp(_atr4x.TAB,_atr4x.WS,1)


\atr4x_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Po obsłudze okienka tabeli tymczasowej atr4x.TAB w panelu zarządzania atrybutami.
::       Formuła wymaga odpowiedniego kontekstu wywołania - przekazanych argumentów.
::   WE: _a - Moment (przyczyna) wywołania:
::          0 - Okienko odświeżane na skutek wywołania grp_disp().
::          1 - Okienko zostało aktywowane.
::   WY:
::  OLD: \wnd_as/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
{? ~_a
|| grp_disp(RA_USE,'WER')
?}


\atr4x
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Formuła tworzy tablicę z uchwytem do tabeli tymczasowej i jej okienka wertowania. Tabela jest wykorzystywana
::       do prezentowania atrybutów powiązanych z rubryką płacową. Tabela i okienko wykorzystywane są w panelu do
::       zarządzania atrybutami oraz z poziomu tabeli rubryk.
::       Formuła zakłada, że stworzone okienko będzie wyświetlane w odpowiednim kontekście - z przekazanymi argumentami.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_atr4x:=obj_new('TAB','WS');

_tab:=_atr4x.TAB:=tab_tmp(3,
   'RODZAJ','STRING[1]','Rodzaj',
   'SYMBOL','INTEGER','Symbol',
   'NAZWA','STRING[50]','Nazwa'
);
_tab.fld_fml('SYMBOL','DISPLAY_FORMAT',"'alignment=left'");

: Formuła obsługująca akcje
_fml0:='"params_set(params_get()); exec(\\\'"+_a+"\\\',\\\'rubatr\\\')"';

_ws:=_atr4x.WS:=_tab.mk_sel('Przypisane atrybuty'@,'N',,'a4r_przypa',,,,,'U');
_tab.win_fld(_ws,,'SYMBOL',,,25,,,,,'Unikalny symbol'@);
_tab.win_fld(_ws,,'NAZWA',,,47,,,,,'Krótki opis'@);

{! _empty:=0 .. 1
|! _tab.win_act(_ws,_empty,'Formuła','Dołącz'@@,,'Dołączenie nowego zapisu'@,$($_fml0)('atr4x_dolacz'),,1,,,,'D')
!};
_tab.win_act(_ws,0,'Formuła','Usuń'@@,,'Usunięcie zaznaczonych zapisów lub bieżącego zapisu'@,
   $($_fml0)('atr4x_ub'),$($_fml0)('atr4x_ua'),,1,$($_fml0)('atr4x_ubg'),$($_fml0)('atr4x_uag'),'U');
_tab.win_act(_ws,0,'Formuła','Położenie atrybutu'@@,,'Położenie atrybutu'@
            ,"_par:=params_get();
            params_exec('def_tree_bs','rubatr',0);
            _par.def_tree.TAB.win_sel(_par.def_tree.WS);

            _par.def_tree.TAB.select()
            ",,,,,,'A');
_tab.win_act(_ws,0,'Formuła','Grupy atrybutów rubryki'@@,,'Grupy atrybutów rubryki'@
            ,"
            params_set(params_get());
            params_exec('ra_grpp_bs','!zws_par_pskt',0,0);
            RA_GRPP.win_sel('WER');
            RA_GRPP.select()"
            ,,,,,,'R');
_tab.win_act(_ws,0,'Formuła','Legenda'@@,,'Legenda'@,$($_fml0)('atr4x_legenda'),,,,,,'L',,'target=window');
_tab.win_act(_ws,0,'Rekord',,,,$($_fml0)('atr4x_bd'));
_tab.win_btn(_ws,'text='+'Dołącz'@,'menu:D',,,,,,'');
_tab.win_btn(_ws,'text='+'Usuń'@,'menu:U',,,,,,'noempty');

_atr4x

\atr4x_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Po odświeżeniu okienka tabeli tymczasowej atr4x.TAB w panelu zarządzania atrybutami.
::       Formuła wymaga odpowiedniego kontekstu wywołania - przekazanych argumentów.
::   WE:
::   WY:
::  OLD: \wnd_ar/rubobj.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
params_set(_par);
_def_tree:=_par.def_tree;
{? -_par.flagi.tryb='ra'
|| grp_disp(RA_USE,'WER')
?}

\ra_grpp_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Przed obsługą okienka tabeli RA_GRPP w panelu zarządzania atrybutami i z poziomu redagowania rubryk płacowych.
::       Formuła wykorzystywana do odświeżenia zawartości tabeli RA_GRPP.
::       Formuła wymaga odpowiedniego kontekstu wywołania - przekazanych argumentów.
::   WE: _a [INTEGER] - Moment (przyczyna) wywołania:
::                      0 - Okienko odświeżane na skutek wywołania grp_disp().
::                      1 - Okienko zostało aktywowane.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_tryb:=_par.flagi.tryb;
{? var_pres('_b')=type_of(0) || _b=1 ?};

{? -_tryb='ra'
|| RA_GRPN.cntx_psh();
   {? ~_a
   || RA_GRPP.hdr_sel();
      RA_GRPP.hdr_sel(' rubryki');
      RA_GRPP.index('PR');
      RA_GRPP.prefix(null(),R.RN,);
      RA_GRPP.first()
   ?};
   {? ~_b || RA_GRPN.cntx_pop() ?}
: RA_GRPN.cntx_pop() - Kontekst jest pobierany po obsłudze okienka.

|? _tryb='GX'
|| R.cntx_psh();
   {? ~_a
   || RA_GRPP.index('NR');
      RA_GRPP.prefix(RA_GRPN.SYMBOL,null(),);
      RA_GRPP.first()
   || {? RA_GRPN.size()
      || _hid:='';
         _def:=':D'
      || _hid:=':D';
         _def:=''
      ?};
      RA_GRPP.actions('WERR',_hid,_def,1)
   ?}
:R.cntx_pop() - Kontekst jest pobierany po obsłudze okienka.
?}


\ra_grpp_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Po obsłudze okienka tabeli tymczasowej rub.TAB w panelu zarządzania atrybutami.
::   WE: _a [INTEGER] - Moment (przyczyna) wywołania:
::                      0 - Okienko odświeżane na skutek wywołania grp_disp().
::                      1 - Okienko zostało aktywowane.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tryb:=params_get().flagi.tryb;
{? -_tryb='ra' | -_tryb='ga'
|| RA_GRPN.cntx_pop()
|? _tryb='GX'
|| R.cntx_pop()
?}


\ra_def_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Funkcja tworzy okienko wertowania dla tabeli RA_DEF. Okienko wykorzystywane jest do
::       wyświetlania struktury drzewiastej atrybutów.
::   WE:
::   WY: uchwyt do tabeli RA_DEF
::----------------------------------------------------------------------------------------------------------------------
_win:=RA_DEF.mk_sel('Atrybuty','P',,'atryb',,,,1);

RA_DEF.win_fld(_win,,'SYMBOL',,,-25,,,,,,,,,,,'mobile_visible=1');
RA_DEF.win_fld(_win,,'NAZWA',,,-50,,,,,,,,,,,'mobile_visible=1');

RA_DEF.win_act(_win,,'Formuła','Dołącz'@,,,"params_exec('ra_def_dolacz','!zws_par_pskt')");
RA_DEF.win_act(_win,1,'Formuła','Dołącz'@,,,"params_exec('ra_def_dolacz','!zws_par_pskt')");
RA_DEF.win_act(_win,,'Formuła','Popraw'@,,,"params_exec('ra_def_popraw','!zws_par_pskt')");
RA_DEF.win_act(_win,,'Formuła','Usuń'@,,,"params_exec('ra_def_usun','!zws_par_pskt')");
RA_DEF.win_act(_win,,'Formuła','Grupy atrybutu'@,,,"
            _par:=params_get();
            params_set(params_get());
            params_exec('grpn_bs','!zws_par_pskt',0);
            _par.grpn.TAB.win_sel(_par.grpn.WS);
            _par.grpn.TAB.select()");
RA_DEF.win_act(_win,,'Formuła','Wartości'@,,,"
            _par:=params_get();
            params_exec('ra_val_bs','!zws_par_pskt',0);
            RA_VAL.win_edit('RED'+$RA_DEF.TYP);
            RA_VAL.win_sel('WER');
            RA_VAL.select()");
RA_DEF.win_act(_win,,'Formuła','Zmian&y'@,,,"params_exec('zmiany','#syslog')");
RA_DEF.win_act(_win,,'Formuła','Zwiń/&rozwiń'@,,,"params_exec('zwin_rozwin','#tree')",,,,,,,,'target=window');
RA_DEF.win_act(_win,,'Szukaj');
RA_DEF.win_act(_win,,'Kolejność');
RA_DEF.win_act(_win,,'Formuła','Legenda'@,,,"params_exec('ra_def_legenda','rubatr')",,,,,,'L',,'target=window');
RA_DEF.win_act(_win,,'Wyświetl',,,,"params_exec('ra_def_wyswietl','rubatr')");
RA_DEF.win_act(_win,,'Rekord',,,,"params_exec('ra_def_bd','rubatr')");

RA_DEF.win_btn(_win,'text=Dołącz','menu:D');
RA_DEF.win_btn(_win,'text=Popraw','menu:P');
RA_DEF.win_btn(_win,'text=Usuń','menu:U');

_win


:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 c2da483223f530d54e23f21ffcac47013b91bde3dbbfb5f7a23389cb0d9babe24424961030dd3e41b2fa7b378e40ae8a44c577be522ffb8e815854784bb37a583e5c14a8b6648b8ec35fa97606e82db59b35c778de2eae50c2ceea20eb370530a510589ad73fb1b01b0a3cb07c2872168de4d6e1d51070d5da3cd98803c22f2b

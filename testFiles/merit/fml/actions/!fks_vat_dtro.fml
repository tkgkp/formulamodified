:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_vat_ztro.fml
:: Utworzony: 27.07.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_VAT_DTRO - Rejestracja korekt nabyć VAT
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rejestracja nabyć do korekt VAT - główna formuła czynności FKS_VAT_DTRO.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS


\sa_kor_v
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formula przed red. pol: VAT_SR.OKRES, VAT_SR.VAT, VAT_SR.PROC, VAT_SR.ILE_LAT
::  OLD: \sa_kor_v/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='OKRES' || VAT7.WYP=0 & VAT7.TRYB=0 || VAT7.TRYB<>1 ?}


\po_okn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formula po red. VAT_SR.OKRES
::  OLD: \po_okn/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_SR.OKRES
|| exec('po_v_dkn','!fks_vat_dtro'); 1
|| 1
?}


\po_v_dkn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formula po red. VAT7.REJ_N i VAT7.NR_N
::  OLD: \po_v_dkn/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_wyczysc:=0;
{? VAT7.REJ_N=null
|| _wyczysc:=1
|| {? VAT7.NR_N<>0
   || DOK.cntx_psh();
      VPOZ.cntx_psh();
      DOK.use('doku'+VAT_SR.OKRES().ROK().KOD+form(VAT_SR.OKRES().NR,-2));
      VPOZ.use('pozv'+VAT_SR.OKRES().ROK().KOD+form(VAT_SR.OKRES().NR,-2));
      DOK.index('REJ');
      DOK.prefix();
      {? DOK.find_key(VAT7.REJ_N,VAT7.NR_N)
      || _r:=DOK.DOK_REJ().SLO().KOD;
         {? _r='VAT' | _r='SAD' | _r='BO' | _r='KOR_BO'
         || _vat:=0;
            {? _r*'BO'>0 | (_vat:=exec('spr_dok1v','dok_fks'))$2<>0
            || _dok:=VAT_SR.DOK;
               VAT_SR.DOK:=DOK.ref();
               {? VAT_SR.VAT$2=0 | _dok<>DOK.ref()
               || VAT_SR.cntx_psh();
                  VAT_SR.index('DOK_N'); VAT_SR.prefix(REF.FIRMA,VAT_SR.OKRES,DOK.ref());
                  _z:=0;
                  _ref:={? -menu_txt(,)='popraw' || VAT_SR.ref || null ?};
                  {? VAT_SR.first()
                  || {!|? {? _ref<>VAT_SR.ref || _z+=VAT_SR.VAT ?}; VAT_SR.next() !}
                  ?};
                  VAT_SR.cntx_pop();
                  VAT_SR.VAT:={? _z$2>_vat$2 || 0 || _vat-_z ?}
               ?};
               exec('ust_vats','dok_fks')
            || FUN.info('Wskazany dokument nie posiada pozycji VAT\n'
                        'z kwotą podatku związanego z grupą:\n'
                        'ZInwPodZ (Nabycie środków trw. - częściowe odlicz.) lub\n'
                        'ZInwPodS (Nabycie samochodów - częściowe odlicz.)'@);
               _wyczysc:=1;
               _ok:=0
            ?}
         || FUN.info('Wskazany dokument nie jest typu:\n VAT, SAD, BO lub KOR_BO.'@);
            _ok:=0
         ?}
      || FUN.info('Brak dokumentu o numerze %1\n w rejestrze %2'@[$VAT7.NR_N,VAT7.REJ_N().KOD]);
         _wyczysc:=1; _ok:=0
      ?};
      DOK.cntx_pop();
      VPOZ.cntx_pop()
   || _wyczysc:=1
   ?}
?};
{? _wyczysc || VAT7.SYM_N:=''; VAT7.DATA_N:=date(0,0,0); VAT7.RVAT_N:=null; VAT7.NR_N:=0 ?};
_ok


\bl_v_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Wartość początkowa pola VAT_SR.DOK
::  OLD: \bl_v_dok/vat2.fml
::  OLD: \bl_v_dok/dok_fks.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT7.WYP || DOK.ref() || null ?}


\bl_v_odl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Wart. pocz. pola VAT_SR.VAT
::  OLD: \bl_v_odl/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_vat:=_z:=0;
{? VAT_SR.DOK<>null
|| VAT_SR.DOK();
   _vat:=exec('spr_dok1v','dok_fks');
   VAT_SR.cntx_psh();
   VAT_SR.index('DOK_N'); VAT_SR.prefix(REF.FIRMA,VAT_SR.OKRES,DOK.ref());
   _z:=0;
   {? VAT_SR.first()
   || {! |? _z+=VAT_SR.VAT; VAT_SR.next() !}
   ?};
   VAT_SR.cntx_pop()
?};
{? _z$2<=_vat$2 || _vat-_z |? _z$2>_vat$2 || 0 || _vat ?}


\obl_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła po redakcji pól VAT_SR.VAT i VAT_SR.PROC
::  OLD: \obl_vat/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_SR.VAT:=VAT_SR.VAT$2;
_proc:=VAT_SR.PROC$2;
VAT_SR.PROC:=_proc;
_prewsk:={? PAR_SKID.get(83)='T' || VAT_SR.PROC_PR || 100 ?}$2;
VAT_SR.PROC_PR:=_prewsk;
VAT_SR.ODLICZ:=(VAT_SR.VAT*(_proc/100)*(_prewsk/100))$2;
1


\bl_v_prc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Wartość początkowa pola VAT_SR.PROC
::  OLD: \bl_v_prc/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_SR.OKRES().ROK().PROC_VAT


\bl_v_lat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Wartość początkowa pola VAT_SR.ILE_LAT
::  OLD: \bl_v_lat/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
5


\po_oks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła po redakcji pola VAT_SR.OKRES_S
::  OLD: \po_oks/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_SR.OKRES_S
|| {? VAT_SR.OKRES_S(); 6+OKRO_F.NAZ='Bilans'
   || FUN.info('Okres %1 jest zabroniony.'@[OKRO_F.NAZ]);0
   |? VAT_SR.OKRES().POCZ>VAT_SR.OKRES_S().POCZ
   || FUN.info('Okres zbycia nie może być wcześniejszy niż okres nabycia.'@); 0
   || exec('po_v_dks','!fks_vat_dtro');
      1
   ?}
|| 1
?}


\po_v_dks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła po redakcji pól VAT7.REJ_S i VAT7.NR_S
::  OLD: \po_v_dks/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_wyczysc:=0;
{? VAT7.REJ_S=null
|| _wyczysc:=1
|| {? VAT7.NR_S<>0
   || DOK.cntx_psh();
      DOK.use('doku'+VAT_SR.OKRES_S().ROK().KOD+form(VAT_SR.OKRES_S().NR,-2));
      DOK.index('REJ');
      DOK.prefix();
      {? DOK.find_key(VAT7.REJ_S,VAT7.NR_S)
      || VAT_SR.DOK_S:=DOK.ref();
         exec('ust_vats','dok_fks')
      || FUN.info('Brak dokumentu o numerze %1 w rejestrze %2.'@[$VAT7.NR_S,VAT7.REJ_S().KOD]);
         _wyczysc:=1
      ?};
      DOK.cntx_pop()
   || _wyczysc:=1
   ?}
?};
{? _wyczysc || VAT7.SYM_S:=''; VAT7.DATA_S:=date(0,0,0); VAT7.NR_S:=0 ?};
1


\obl_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła po redakcji pól VAT_SR.PROC2 i VAT7.ILE_POZ
::   WE: [_a - jesli jest podany - brak kontroli pola VAT_SR.PROC2]
::  OLD: \obl_spr/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_proc2:=VAT_SR.PROC2:=VAT_SR.PROC2$2;
_prewsk2:={? PAR_SKID.get(83)='N' || 100 || VAT_SR.PROC_PR2 ?}$2;
VAT_SR.PROC_PR2:=_prewsk2;

{? _=0 & cur_afld()='PROC2' & (_proc2<0 | _proc2>100)
|| FUN.info('Podaj wartość od 0 do 100!'@); 0
|? _=0 & cur_afld()='PROC_PR2' & (_prewsk2<0 | _prewsk2>100)
|| FUN.info('Podaj wartość od 0 do 100!'@); 0
|| VAT7.ODLICZ:=(VAT_SR.VAT*(_prewsk2/100)*(_proc2/100))$2;
   VAT7.KOREKTA:=(VAT7.ILE_POZ*(VAT7.ODLICZ-VAT_SR.ODLICZ)/VAT_SR.ILE_LAT)$2;
   1
?}


\ust_vatd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Dołączanie rekordów do tabeli VAT_SR
::  OLD: \ust_vatd/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:={? -menu_txt(,)='dołącz'
     || {? VAT7.ZAKRES='T'
        || FUN.info('Ustawiono zakres nabyć rozliczonych. Dołączanie nabyć niemożliwe.'@); 0
        |? VAT7.ROK<>null & VAT7.ROK<>SSTALE.AR
        || FUN.info('Ustawiono zakres nabyć dla roku %1.'
                    '\nDołączanie nabyć dla roku %2 niemożliwe.'@[VAT7.ROK().NAZ,SSTALE.AR().NAZ]); 0
        || 1
        ?}
     || 1
     ?};
{? _ok
|| VAT7.TRYB:=0;
   VAT7.SYM_N:=''; VAT7.DATA_N:=date(0,0,0);
   VAT7.REJ_N:=VAT7.ODD_N:=VAT7.RVAT_N:=null;
   VAT7.NR_N:=0;
   VAT7.SYM_S:=''; VAT7.DATA_S:=date(0,0,0);
   VAT7.REJ_S:=VAT7.ODD_S:=null;
   VAT7.ROK_S:=null;
   VAT7.ROK_N:=SSTALE.AR;
   VAT7.NR_S:=0;
   {? VAT7.WYP
   || VAT_SR.NST:=DOK.TR;
      VAT_SR.OKRES:=SSTALE.AO;
      VAT_SR.DOK:=VPOZ.DOK;
      exec('ust_vats','dok_fks')
   ?};
   1
?}


\poz_vats
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła KWOTY KOREKT w okienku VAT_SR
::  OLD: \poz_vats/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_SR.r_lock(1,1)
|| VAT_KW.index('VAT_KW');
   VAT_KW.prefix(VAT_SR.ref);
   _okn:={? PAR_SKID.get(83)='T' || '_PR' || '' ?};
   VAT_KW.win_sel('WER'+_okn);
   VAT_KW.select(,,,{? VAT_SR.OKRES_S<>null || 'dU:d' || '' ?});
   VAT_SR.r_unlock()
|| FUN.info('Dane środka redaguje inny operator.'@)
?}


\rek_vats
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formula Rekord przed w okienku WER tabeli VAT_SR
::  OLD: \rek_vats/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT7.ROK_N:=VAT_SR.OKRES().ROK;
exec('rekprzed','color','VAT_SR#01#01')


\kol_vats
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formula decydująca o kolorowaniu rekordow w okienku WER tabeli VAT_SR
::  OLD: \kol_vats/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_SR.ROZL='T' || 'VAT_SR#01#01' || '' ?}


\obl_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła Oblicz kwoty korekt w okienku WER tabeli VAT_SR
::  OLD: \obl_kor/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT7.PROC:=0;
{? PAR_SKID.get(83)<>'T'
|| VAT7.PROC_PR:=100;
   VAT7.win_edit('OBL_KOR')
|| VAT7.PROC_PR:=0;
   VAT7.win_edit('OBL_K_PR')
?};
VAT7.ZN:=1;
exec('ae_rok_n','!fks_vat_dtro',0);
{? VAT7.edit("exec('chk_obl','!fks_vat_dtro')")
|| VAT_SR.cntx_psh();
   VAT_SR.index('OK'); VAT_SR.prefix(REF.FIRMA);
   _licz:=_dod:=_pop:=0;
   echo('Trwają obliczenia...'@);
   {? VAT_SR.first()
   || {! |?
         {? VAT_SR.ROZL='N' & '<='*exec('por_lat','!fks_vat_dtro',VAT_SR.OKRES().ROK,VAT7.ROK_N)>0
         || {? (_i:=exec('obl_poj','!fks_vat_dtro'))>0
            || _licz+=1;
               {? _i=1 || _dod+=1 || _pop+=1 ?}
            ?};
            VAT_KW.cntx_psh();
            VAT_KW.index('VAT_KW');
            VAT_KW.prefix(VAT_SR.ref);
            _s:=VAT_KW.size();
            VAT_KW.cntx_pop();
            {? _s=VAT_SR.ILE_LAT
            || VAT_SR.cntx_psh();
               VAT_SR.prefix(REF.FIRMA);
               VAT_SR.ROZL:='T'; VAT_SR.put();
               VAT_SR.cntx_pop()
            ?}
         ?};
         VAT_SR.next()
      !}
   ?};
   echo();
   {? _licz>0
   || FUN.info('Liczba wykonanych obliczeń: %1'
               '\n w tym:                     '
               '\n dodano:                    %2'
               '\n zmodyfikowano:             %3'@[form(_licz,6),form(_dod,6),form(_pop,6)])
   || FUN.info('Nie wykonano żadnego obliczenia.'@)
   ?};
   VAT_SR.cntx_pop()
?}


\chk_obl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Sprawdzenie parametrow obliczania kwot korekt
::  OLD: \chk_obl/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT7.PROC:=VAT7.PROC$2;
VAT7.PROC_PR:=VAT7.PROC_PR$2;
{? VAT7.ROK_N=null
|| 'ROK_N'
|? VAT7.PROC<0 | VAT7.PROC>100
|| FUN.info('Należy wprowadzić wartość od 0 do 100.'@); 'PROC'
|? VAT7.PROC_PR<0 | VAT7.PROC_PR>100
|| FUN.info('Należy wprowadzić wartość od 0 do 100.'@); 'PROC_PR'
|| ''
?}


\por_lat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła porównująca (chronologicznie) lata bilansowe
::   WE: _a - rok (ref ROK_F)
::       _b - rok (ref ROK_F)
::   WY: string: '=' lub '<' lub '>'
::  OLD: \por_lat/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_wynik:='';
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(_a);
{? OKRO_F.first() & OKRO_F.next()
|| _d1:=OKRO_F.POCZ;
   OKRO_F.prefix(_b);
   {? OKRO_F.first() & OKRO_F.next()
   || _wynik:={? OKRO_F.POCZ=_d1 || '='
              |? OKRO_F.POCZ<_d1 || '>'
              |? OKRO_F.POCZ>_d1 || '<'
              ?}
   ?}
?};
OKRO_F.cntx_pop();
_wynik


\obl_poj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Obliczenie korekty dla pojedyńczego środka
::  OLD: \obl_poj/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
VAT_KW.index('VAT_KW');
VAT_KW.prefix(VAT_SR.ref());
{? VAT_KW.find_key(VAT7.ROK_N().NAZ,null)
|| {? VAT7.ZN
   || VAT_KW.PROC:={? exec('licz_proc','!fks_vat_dtro') || VAT7.PROC || 100 ?};
      VAT_KW.PROC_PR:={? exec('licz_pre','!fks_vat_dtro') || VAT7.PROC_PR || 100 ?};
      _ok:=2;
      exec('pol_vat','!fks_vat_dtro',0);
      VAT_KW.put()
   ?}
|| ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   _p:={? VAT_KW.last() || VAT_KW.ROK(); ROK_F.next() || VAT_SR.OKRES().ROK(); 1 ?};
   {? _p & ROK_F.ref=VAT7.ROK_N
   || VAT_KW.blank();
      VAT_KW.ROK:=VAT7.ROK_N;
      VAT_KW.PROC:={? exec('licz_proc','!fks_vat_dtro') || VAT7.PROC || 100 ?};
      VAT_KW.PROC_PR:={? exec('licz_pre','!fks_vat_dtro') || VAT7.PROC_PR || 100 ?};
      _ok:=1;
      exec('pol_vat','!fks_vat_dtro',0);
      VAT_KW.add()
   ?}
?};
_ok


\pol_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła po redakcji pola VAT_KW.PROC
::  OLD: \pol_vat/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_KW.PROC:=VAT_KW.PROC$2;
{? _=0 & (VAT_KW.PROC<0 | VAT_KW.PROC>100)
|| FUN.info('Należy wprowadzić wartość od 0 do 100.'@); 0
|| VAT_KW.PROC:=VAT_KW.PROC$2;
   _proc:=VAT_KW.PROC;
   _prewsk:={? PAR_SKID.get(83)='T'  || VAT_KW.PROC_PR || 100 ?};
   VAT_SR.cntx_psh();
   VAT_KW.VAT_SR();
   VAT_KW.ODLICZ:=(VAT_SR.VAT*(_prewsk/100)*(_proc/100))$2;
   {? VAT_KW.OKRES=null
   || VAT_KW.KOREKTA:=((VAT_KW.ODLICZ-VAT_SR.ODLICZ)/VAT_SR.ILE_LAT)$2
   || VAT_KW.KOREKTA:=(VAT_KW.ILE_POZ*(VAT_KW.ODLICZ-VAT_SR.ODLICZ)/VAT_SR.ILE_LAT)$2
   ?};
   VAT_SR.cntx_pop();
   1
?}


\dru_vats
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.50]
:: OPIS: Wydruki ewidencji korekty podatku VAT
::  OLD: \dru_vats/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(83)='T'
|| exec('rep_exec','#b_report','FKS_VAT_DTRO','fks_evpr*',,1)
|| exec('rep_exec','#b_report','FKS_VAT_DTRO','fks_evat*',,1)
?}


\skor_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.50]
:: OPIS: Sprawdza poprawnosc danych okienka SKOR_VAT zmiennej ROZNE
::  OLD: \skor_vat/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~ROZNE.KV_ROKOD | ~ROZNE.KV_ROKDO | ~ROZNE.KV_OKROD | ~ROZNE.KV_OKRDO |
   ~(OKRO_F.index('ROK'); OKRO_F.prefix(ROZNE.KV_ROKOD); OKRO_F.seek(ROZNE.KV_OKROD)) |
   ~(OKRO_F.prefix(ROZNE.KV_ROKDO); OKRO_F.seek(ROZNE.KV_OKRDO)) |
   ROZNE.KV_ROKOD().KOD>ROZNE.KV_ROKDO().KOD |
   (ROZNE.KV_ROKOD().KOD=ROZNE.KV_ROKDO().KOD & ROZNE.KV_OKROD().NR>ROZNE.KV_OKRDO().NR)
|| FUN.info('Należy wprowdzić poprawnie przedział okresów dla dokumentów.'@);
   {? ~ROZNE.KV_ROKOD || 'KV_ROKOD'
   |? ~ROZNE.KV_ROKDO || 'KV_ROKDO'
   |? ~ROZNE.KV_OKROD || 'KV_OKROD'
   |? ~ROZNE.KV_OKROD || 'KV_OKRDO'
   || 'KV_ROKOD' ?}
|| 1
?}


\ae_zakrv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [8.50]
:: OPIS: Po redakcji pola VAT7.ZAKRES
::  OLD: \ae_zakrv/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_nfld='Status nabyć' & ROZNE.win_edit('?')*'KOR_VAT'>0
|| {? VAT7.ZAKRES='N'
   || ROZNE.KV_ROKOD:=ROZNE.KV_ROKDO:=ROZNE.KV_OKROD:=ROZNE.KV_OKRDO:=null;
      win_disp()
   ?}
?}; 1


\vpoz_srt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Nabycia srodkow w okienkach wert. tabeli VPOZ
::  OLD: \vpoz_srt/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
VAT_SR.index('DOK_N'); VAT_SR.prefix(REF.FIRMA,SSTALE.AO,DOK.ref);
{? VAT_SR.first() | exec('spr_dok1v','dok_fks')
|| _ok:=1
|| FUN.info('Wskazany dokument nie posiada pozycji VAT\n'
            'z kwotą podatku związanego z grupą:\n'
            'ZInwPodZ (Nabycie środków trw. - częściowe odlicz.) lub\n'
            'ZInwPodS (Nabycie samochodów - częściowe odlicz.)'@)
?};
{? _ok
|| VAT7.WYP:=1;
   _dod:={? PAR_SKID.get(83)='T' || '_PR' || '' ?};
   VAT_SR.win_sel('WER'+_dod);
   VAT_SR.win_edit('RED'+_dod);
   VAT_SR.win_patt('SZUK'+_dod);
   VAT_SR.hdr_sel();
   VAT_SR.hdr_sel(', Dokument: %1'@[DOK.NK]);
   VAT_SR.select(,,,'ROJZTA:RA')
?}


\spr_vats
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formuła Zbycie srodka w okienku WER tabeli VAT_SR
::  OLD: \spr_vats/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_SR.r_lock(1,1)
|| {? VAT_SR.OKRES_S<>null
   || {? FUN.ask('Zarejestrowano już zbycie środka.\nWycofać informacje o zbyciu?'@)
      || VAT_KW.index('VAT_KW');
         VAT_KW.prefix(VAT_SR.ref(),VAT_SR.OKRES_S().ROK().NAZ,VAT_SR.OKRES_S);
         {? VAT_KW.first() || VAT_KW.del() ?};
         VAT_SR.OKRES_S:=VAT_SR.DOK_S:=null;
         VAT_SR.PROC2:=0;
         VAT_SR.PROC_PR2:=0;
         VAT_SR.cntx_psh();
         VAT_SR.prefix(REF.FIRMA);
         VAT_SR.ROZL:='N';
         VAT_SR.put();
         VAT_SR.cntx_pop()
      ?}
   || VAT_KW.index('VAT_KW');
      VAT_KW.prefix(VAT_SR.ref());
      _last:=VAT_KW.last();
      _r:='Rejestracja zbycia środka w roku '+$SSTALE.AR().NAZ+' niemożliwa.';
      {? _last & exec('por_lat','!fks_vat_dtro',VAT_KW.ROK,SSTALE.AR)='>'
      || FUN.info('Wprowadzono kwoty korekt dla lat następnych.\n%1'@[_r])
      |? _last & exec('por_lat','!fks_vat_dtro',VAT_KW.ROK,SSTALE.AR)='='
      || FUN.info('Wprowadzono kwotę korekty dla roku %1.\n%2'@[SSTALE.AR().NAZ,_r])
      |? ~_last & exec('por_lat','!fks_vat_dtro',VAT_SR.OKRES().ROK,SSTALE.AR)='<'
      || FUN.info('Brak kwot korekt dla lat wcześniejszych\n niż rok %1.\n%2'@[SSTALE.AR().NAZ,_r])
      |? _last & exec('por_lat','!fks_vat_dtro',VAT_KW.ROK,SSTALE.AR)='<' &
         ~exec('nas_lat','dok_fks',VAT_KW.ROK,SSTALE.AR)
      || FUN.info('Brak kwot korekt do roku %1\n%2'@[SSTALE.AR().NAZ,_r])
      |? (~_last & exec('por_lat','!fks_vat_dtro',VAT_SR.OKRES().ROK,SSTALE.AR)='>')
      || FUN.info('Środek nabyty w roku %1.\n%2'@[VAT_SR.OKRES().ROK().NAZ,_r])
      || _okn:={? PAR_SKID.get(83)='T' || '_PR' || '' ?};
         VAT_SR.win_edit('SPR'+_okn);
         exec('ust_vatd','!fks_vat_dtro');
         VAT_KW.cntx_psh();
         VAT_KW.index('VAT_KW');
         VAT_KW.prefix(VAT_SR.ref());
         VAT7.ILE_POZ:=VAT_SR.ILE_LAT-VAT_KW.size();
         VAT7.ROK_S:=SSTALE.AR;
         VAT_SR.OKRES_S:=SSTALE.AO;
         VAT_SR.PROC_PR2:=SSTALE.AR().PREWSK;
         VAT_SR.PROC2:=SSTALE.AR().PROC_VAT;
         exec('obl_spr','!fks_vat_dtro',1);
         VAT_KW.cntx_pop();
         {? VAT_SR.edit("chk_rec('OKRES_S','ILE_POZ')")
         || VAT_KW.blank();
            VAT_KW.OKRES:=VAT_SR.OKRES_S;
            VAT_KW.ROK:=VAT_SR.OKRES_S().ROK;
            VAT_KW.PROC:=VAT_SR.PROC2;
            VAT_KW.PROC_PR:=VAT_SR.PROC_PR2;
            VAT_KW.ODLICZ:=VAT7.ODLICZ$2;
            VAT_KW.KOREKTA:=VAT7.KOREKTA$2;
            VAT_KW.ILE_POZ:=VAT7.ILE_POZ;
            VAT_KW.add();
            VAT_SR.cntx_psh();
            VAT_SR.prefix(REF.FIRMA);
            VAT_SR.ROZL:='T';
            VAT_SR.put();
            VAT_SR.cntx_pop()
         ?};
         VAT_SR.win_edit('RED')
      ?}
   ?};
   VAT_SR.r_unlock()
|| FUN.info('Dane środka redaguje inny operator.'@)
?}


\wys_vats
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formula Wyświetl w okienku WER tabeli VAT_SR
::  OLD: \wys_vats/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ust_vats','dok_fks');
VAT_SR.win_edit('RED'+{? PAR_SKID.get(83)='T' || '_PR' || '' ?});
VAT_SR.display()


\pvat_srt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formula Nabycia srodkow w okienkach wert. tabeli DVAT
::  OLD: \pvat_srt/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
VAT_SR.cntx_psh();
VAT_SR.index('DOK_N'); VAT_SR.prefix(REF.FIRMA,DVAT.DOKOKRO,DVAT.DOK);
{? VAT_SR.first()
|| DOK.cntx_psh();
   DOK.use('doku'+DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2));
   DOK.prefix();
   VAT_SR.win_sel('WER');
   VAT_SR.hdr_sel();
   VAT_SR.hdr_sel(', Dokument: %1'@[DVAT.SYM1]);
   VAT_SR.select(,,,'dUpROJZTA');
   DOK.cntx_pop()
|| FUN.info('Wskazany dokument nie posiada zarejestrowanego\nnabycia do korekty podatku VAT.'@)
?};
VAT_SR.cntx_pop()


\bl_v_pre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAK [12.41]
:: OPIS: Wart. pocz. pola VAT_SR.PROC_PR
::  OLD: \bl_v_pre/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(83)='T' & exec('bl_c_prewsk','!fks_vat_dtro') || VAT_SR.OKRES().ROK().PREWSK || 100 ?}


\bl_vk_pre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAK [12.41]
:: OPIS: Wart. pocz. pola VAT_KW.PROC_PR
::----------------------------------------------------------------------------------------------------------------------
VAT_KW.PROC_PR:={? PAR_SKID.get(83)='T' & exec('licz_pre','!fks_vat_dtro')
                || exec('proc_rok_next','dok_fks',VAT_KW.ROK,2)
                || 100
                ?};
exec('pol_vat','!fks_vat_dtro');
VAT_KW.PROC_PR


\bl_c_prewsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wartosc poczatkowa pola VAT_SR.C_PREWSK
::  OLD: \bl_c_prewsk/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_SR.OKRES().ROK().POCZ_ROK~1>2015


\bv_c_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietleniem VAT7.C_PW
::  OLD: \bv_c_pw/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_fld:=2-cur_afld();
_p1:=+_fld=2;
{? 2+_fld='PW'
|| {? _p1
   || fld(_tab.PROC_PR<>100)
   || fld(_tab.PROC_PR2<>100)
   ?}
|| {? _p1
   || fld(_tab.PROC<>100)
   || fld(_tab.PROC2<>100)
   ?}
?};
1


\be_c_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja VAT7.C_PW
::  OLD: \be_c_pw/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_fld:=2-cur_afld();
_p1:=+_fld=2;
{? 2+_fld='PW'
|| PAR_SKID.get(83)='T' & (_tab=VAT7 | exec('licz_pre','!fks_vat_dtro')) & (_tab<>VAT_SR | VAT7.TRYB=0)
|| _tab<>VAT_SR | VAT7.TRYB=0 & VAT_SR.C_PROC
?}


\ae_c_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: po redakcji VAT7.C_PW
::  OLD: \ae_c_pw/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_fld:=2-cur_afld();
_p1:=+_fld=2;
{? 2+_fld='PW'
|| {? _p1
   || _tab.PROC_PR:={? fld()
                    || {? _tab=VAT7
                       || exec('proc_rok_next','dok_fks',VAT7.ROK_N,2)
                       |? _tab=VAT_KW
                       || {? VAT_KW.OKRES
                          || _tab.ROK().PREWSK
                          || exec('proc_rok_next','dok_fks',_tab.ROK,2)
                          ?}
                       || _tab.OKRES().ROK().PREWSK
                       ?}
                    || 100
                    ?}
   || _tab.PROC_PR2:={? fld() || _tab.OKRES_S().ROK().PREWSK || 100 ?}
   ?}
|| {? _p1
   || _tab.PROC:={? fld()
                 || {? _tab=VAT7
                    || exec('proc_rok_next','dok_fks',VAT7.ROK_N,1)
                    |? _tab=VAT_KW
                    || {? VAT_KW.OKRES
                       || _tab.ROK().PROC_VAT
                       || exec('proc_rok_next','dok_fks',_tab.ROK,1)
                       ?}
                    || _tab.OKRES().ROK().PROC_VAT
                    ?}
                 || 100
                 ?}
   || _tab.PROC2:={? fld() || _tab.OKRES_S().ROK().PROC_VAT || 100 ?}
   ?}
?};
{? _tab=VAT_SR
|| {? 3+VAT_SR.win_edit('?')='RED'
   || exec('obl_vat','!fks_vat_dtro')
   || exec('obl_spr','!fks_vat_dtro')
   ?}
|? _tab<>VAT7
|| exec('pol_vat','!fks_vat_dtro')
?};
1


\licz_pre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy dla srodka wyliczac prewskaznik
::  OLD: \licz_pre/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_SR.C_PREWSK


\be_c_prewsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja VAT_SR.C_PREWSK
::  OLD: \be_c_prewsk/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT7.TRYB=0 | VAT_SR.OKRES().ROK().POCZ_ROK~1<2016


\ae_c_prewsk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji VAT_SR.C_PREWSK
::  OLD: \ae_c_prewsk/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_SR.C_PREWSK
|| VAT_SR.PROC_PR:=VAT_SR.OKRES().ROK().PREWSK
|| VAT_SR.PROC_PR:=100
?};
win_disp();
exec('obl_vat','!fks_vat_dtro');
1


\ae_rok_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji VAT7.ROK_N
::   WE: [_a] - uwzgledniac chceckboxy
::  OLD: \ae_rok_n/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
VAT7.PROC:={? _a=0 | VAT7.C_PS
           || exec('proc_rok_next','dok_fks',VAT7.ROK_N,1)
           || 100
           ?};
VAT7.PROC_PR:={? _a=0 | VAT7.C_PW
              || exec('proc_rok_next','dok_fks',VAT7.ROK_N,2)
              || 100
              ?};
1


\bl_c_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wartosc poczatkowa pola VAT_SR.C_PROC
::  OLD: \bl_c_proc/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_SR.OKRES().ROK().PROC_VAT<>100


\be_c_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja pola VAT_STR.C_PORC
::  OLD: \be_c_proc/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT7.TRYB=0


\ae_c_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji pola VAT_SR.C_PROC
::  OLD: \ae_c_proc/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_SR.C_PROC
|| VAT_SR.PROC:=VAT_SR.OKRES().ROK().PROC_VAT
|| VAT_SR.PROC:=100
?};
win_disp();
exec('obl_vat','!fks_vat_dtro');
1


\licz_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy dla srodka wyliczac procent struktury
::  OLD: \licz_proc/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('C_PROC',VAT_SR)>0
|| VAT_SR.C_PROC
|| 1
?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:43 9cb082806e1e88e3986e729b9a1134c82ca04a43b50eddb2364ab7ed10f0431eb8ffa872446c15a5f5bd708ee5dfcde34029ccb20825eab99d097909a3ea9b811d532a99fb7b2f50735f7a482945c042e0252c818fcfe40d3ea92985a30bf50b95a973cf0105b226c81fac7b9ebe16e8aad453419b8e75d6e346b34f9fc7ddf0

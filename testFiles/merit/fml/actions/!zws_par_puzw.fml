:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_puzw.fml
:: Utworzony: 02.09.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PUZW - Uprawnienia zawodowe.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Uprawnienia zawodowe - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SLO_TYP.cntx_psh();
_dict:=SLO_TYP.win_dict('WER');
_ha:=SLO_TYP.actions('WER','W');

UPR_ZAWK.cntx_psh();
UPR_ZAWK.index('NAZWA');
UPR_ZAWK.prefix();
UPR_ZAWK.win_sel('WER');
UPR_ZAWK.win_edit('RED');
UPR_ZAWK.win_patt('SZUK');
UPR_ZAWK.select();
UPR_ZAWK.cntx_pop();

SLO_TYP.actions('WER',_ha);
SLO_TYP.win_dict(_dict);
SLO_TYP.cntx_pop();
~~


\upr_zawk_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed modyfikacją rekordu tabeli UPR_ZAWK.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: Porządki - zerowanie pól, które są "niedostępne".
{? UPR_ZAWK.SLO_TYP=null()
|| exec('blank','#field',UPR_ZAWK,,'DOK_WYD')
?};
1


\upr_zawk_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli UPR_ZAWO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawk_modb','!zws_par_puzw')


\upr_zawk_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawk_modb','!zws_par_puzw')


\upr_zawk_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli UPR_ZAWK.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE: [_a] [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(UPR_ZAWK)
|| _tab:=_b
|? _tryb='1'
|| _tab:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? _fld='' | _fld='SLO_TYP'
|| _sval:=$(UPR_ZAWK.SLO_TYP<>null());

   _tab.efld_opt(_we,'enable='+_sval,UPR_ZAWK,'DOK_WYD')
?};

1


\upr_zawk_slo_typ_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed redagowaniem pola UPR_ZAWK.SLO_TYP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UPR_ZAWK.SLO_TYP=null() | -menu_txt()<>'popraw' | UPR_ZAWK.count()=0


\upr_zawk_slo_typ_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola UPR_ZAWK.SLO_TYP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawk_efld_opt','!zws_par_puzw');
1


\upr_zawk_db
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER tabeli UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UPR_ZAWK.blank();
exec('upr_zawk_efld_opt','!zws_par_puzw','*',UPR_ZAWK,'RED')


\upr_zawk_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw" w oknie WER tabeli UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawk_efld_opt','!zws_par_puzw','*',UPR_ZAWK,'RED')


\upr_zawk_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl" w oknie WER tabeli UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_zawk_efld_opt','!zws_par_puzw','*',UPR_ZAWK,'RED');
UPR_ZAWK.display()


\upr_zawk_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER tabeli UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ga:={? UPR_ZAWK.SLO_TYP=null() || 'C' || '' ?};
UPR_ZAWK.actions_grayed('WER',_ga);
0


\upr_zawk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" w oknie WER tabeli UPR_ZAWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(UPR_ZAWK,-menu_txt()='popraw',,'SYMBOL','NAZWA');
{? (type_of(_chk)=type_of('') & _chk<>'') | (type_of(_chk)=type_of(0) & ~_chk)
|| return(_chk)
?};

{? UPR_ZAWK.SLO_TYP<>null() & UPR_ZAWK.DOK_WYD='N'
|| FUN.info(
      'Jeżeli wskazany został "Słownik certyfikatorów",\n'+
      'to kolumna "Organ wydający" nie może mieć wartości [Niedostępna].');
   return('DOK_WYD')
?};

''


\upr_zawk_cb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Certyfikatorzy".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? UPR_ZAWK.SLO_TYP=null()
|| return()
?};

_slotyp:=UPR_ZAWK.SLO_TYP().SYMBOL;
EDIT_VAR.SLO_TYP==_slotyp;
ADRES.cntx_psh();
ADRES.index('SYMBOL');
ADRES.prefix(UPR_ZAWK.SLO_TYP);
ADRES.index('ADRES');
ADRES.prefix(UPR_ZAWK.SLO_TYP);
ADRES.win_sel('WER');
ADRES.win_edit('RED');
ADRES.win_patt('RED');
ADRES.select();
ADRES.cntx_pop();
EDIT_VAR.SLO_TYP==_slotyp;
1

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:06 c437bcfc8fd1b1db0a7a5042dcc3c51a217737093eb1875caba82939c2da0fb53bb6729581992c8c96933fc290a50ae617c0533ff4380662728a2240cc71734454ad35e962d2c59e5e28c7c02877360c16f49e977ff7d2deb5081232d2512a204788d42fdb967dc2692e6a89d51c276711fc3ad97920990889d1b55677b93237

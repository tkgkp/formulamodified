:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_grp_adys.fml
:: Utworzony: 15.02.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_GRP_ADYS - Gr. akt. dyscypliny pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Grupowa aktualizacja dyscypliny pracy - główna formuła czynności.
::       Sposób działania:
::       1. W okienku redakcji określane są parametry działania i wprowadzane danych:
::          - rok, dla którego będą aktualizowane zapisy - pole liczbowe
::          - nieobecność kontrolowana w dyscyplinie (atrybut 197) - wybór z filrtowanego słownika rubryk
::          - sposób wyrażenia czasu nieobecności - pole opcji:
::            Dni kalendarzowe (K)
::            Dni robocze (R)
::            Godziny robocze (G)
::          - bieżący limit kontrolowanej nieobecności, dla 13 nie może przekraczać wartości określonej dla atrybutu
::            19711 lub 19712 zależnie od wybranego sposobu wyrażenia - pole liczbowe
::            - dla nieobecności 13, jeśli w polu nie podano limitu podpowiadana jest wartość określona dla atrybutu
::              19711 lub 19712 zależnie od wybranego sposobu wyrażenia nieobecności
::          - bieżący limit kontrolowanej nieobecności, dla 7128 nie może przekraczać wartości określonej dla atrybutu
::            19731 lub 19732 zależnie od wybranego sposobu wyrażenia - pole liczbowe
::            - dla nieobecności 7128, jeśli w polu nie podano limitu podpowiadana jest wartość określona dla atrybutu
::              19731 lub 19732 zależnie od wybranego sposobu wyrażenia nieobecności
::          - określenie dla przeniesienia limitu niewykorzystanego w poprzednim roku - pole znacznika:
::            ustawione (T) - niewykorzystany limit będzie przeniesiony jako zaległy,
::            wyłączone (N) (domyślnie) - limit niewykorzystany w poprzednim roku nie będzie przenoszony
::          - określenie sposobu aktualizacji istniejących danych - pole znacznika:
::            ustawione (T) - istniejące informacje zostaną nadpisane
::            wyłączone (N) (domyślnie) - istniejące dane pozostaną w niezmienionej postaci
::       2. W oknie selekcji dokonywany jest wybór współpracowników
::          - dziedzina: PKD
::          - forma współpracy: P
::       3. Tworzona jest lista zawierająca dane wybranych wcześniej współpracowników rozszerzona o informacje o limi-
::          tach wynikających z pkt 1
::          - na tym etapie nie są jeszcze wprwadzane modyfikacje w kartotece dyscypliny pracy
::          - lista zawiera znaczniki kontrolne, określające:
::            - czy informacje z danego wiersza mają być naniesione do bazy danych, znacznik może być zmieniony dla
::              grupy zaznaczonych wierszy poprzez wywołanie akcji z menu - pole znacznika:
::              wyszarzone (X) - informacje nie mogą być wprowadzone - tego stanu nie można zmienić
::              ustawione (T) (domyślnie) - informacje będą zapisane w bazie danych
::              wyłączone (N) - informacje zostaną pominięta
::            - czy dla pracownika w podanym roku istnieją już informacje o kontroli wykorzystania limitu nieobecnosci:
::              wyszarzone (R) - występują i są różne od tych, które mają być wprowadzone
::              ustawione (T) - występują i są zgodne z wprowadzanymi
::              wyłączone (N) - danych nie znaleziono w bazie
::          - możliwe jest filtrowanie i sortowanie danych przy użyciu standardowych mechanizmów
::          - zmiana znacznika wprowadzania informacji do tabeli docelowej możliwa jest dzięki funkcjom działającym na
::            grupie wybranych wierszy
::          - poprzez wywołanie funkcji "popraw" możliwa jest zmiana przysługującego limitu nieobecności
::          - wywołanie polecenia "dalej" powoduje przejście do następnego kroku
::       4. Modyfikowana jest tabela DS zgodnie z wartościami określonymi dla rekordów o statusie wprowadzania ustawio-
::          nym na T
::       5. Wyświetlany jest raport z przebiegu działania funkcji
::   WE:
::   WY:
::  OLD: \start_grup_ds/grupds.fml
::  OLD: \grup_ds/grupds.fml
::  OLD: \init_undo_ds/grupds.fml
::  OLD: \undo_ds/grupds.fml
::  OLD: \end_undo_ds/grupds.fml
::  OLD: \anuluj_ds/grupds.fml
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

exec('init','pkd');

{? ~exec('dostepna_a','f_zatr',__F_ZATR.P,'PKD_EZK_ORDS')
|| FUN.info('Funkcjonalność nie jest dostępna dla wybranej formy współpracy.'@);
   return()
?};

: pobierz parametry i dane do wprowadzenia
EDIT_VAR.ADYS_ROK:=date()~1;
EDIT_VAR.ADYS_NB:=null;
EDIT_VAR.ADYS_NN:=0;
EDIT_VAR.ADYS_NZ:='N';
EDIT_VAR.ADYS_SA:='N';
EDIT_VAR.win_edit('ADYS');
{? ~EDIT_VAR.edit("exec('adys_ae','!pkd_grp_adys')")
|| R.f_clear();
   exec('msg_cancel','!pkd_grp_adys');
   return()
?};

: wybór wielu pracowników
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='PKD';
_args.F_ZATR:={? +__F_ZATR.P || __F_ZATR.P || '*T' ?};
_args.VIEW:='';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
_args.UD_SKL:=__PARSES.getVal('JednostkaOrganizacyjna').REF;
_args.XFILTER:=1;
_ret:=exec('wybierz','pracownik',_args);
: wybrano kogoś?
{? _ret.P.size()=0
|| R.f_clear();
   exec('msg_cancel','!pkd_grp_adys');
   return()
?};
R.f_clear();

_DS:=tab_tmp(3,
   'NAZWISKO','STRING['+$MS.fld_len('OSOBA','NAZWISKO')+']',MS.name('OSOBA','NAZWISKO'),
   'PIERWSZE','STRING['+$MS.fld_len('OSOBA','PIERWSZE')+']',MS.name('OSOBA','PIERWSZE'),
   'PESEL','STRING['+$MS.fld_len('OSOBA','PESEL')+']',MS.name('OSOBA','PESEL'),
   'T','STRING['+$MS.fld_len('P','T')+']',MS.name('P','T'),
   'IP','INTEGER',MS.name('P','IP'),
   'WYDZIAL','STRING['+$MS.fld_len('UD_SKL','SYMBOL')+']',MS.name('P','WYDZIAL'),
   'STN','STRING['+$MS.fld_len('STN','ST')+']',MS.name('P','ST'),
   'P','STRING[16]',P.comment(),
   'NZ','REAL',MS.name('DS','NZ'),
   'NN','REAL',MS.name('DS','NN'),
   'NW','REAL',MS.name('DS','NW'),
   'NP','REAL',MS.name('DS','NP'),
   'DS','STRING[16]',DS.comment(),
   'OK','STRING[1]','Wprowadzić?'@,
   'EQ','STRING[1]','Zgodność zapisów'@,
   'CK','STRING[1]','Wynik operacji'@,
   'OPIS','STRING[120]','Opis'@
);

_DS.fld_fml('T','DISPLAY_FORMAT',P.fld_fml('T','*DISPLAY_FORMAT'));

_title:='Aktualizacja dyscypliny pracy za rok %1 dla rodzaju %2'@[$EDIT_VAR.ADYS_ROK,EDIT_VAR.ADYS_NB().RT];

: okienko podglądu wprowadzanych zmian
_wnd:=exec('ds_utworz_okno','!pkd_grp_adys',_DS,'','#pkd_wnd_zmia');
: widok proponowanych zmian
{? EDIT_VAR.ADYS_NZ='T'
|| _DS.win_fld(_wnd,,'NZ',,,-9,2,1,,,MS.comment('DS','NZ'))
?};
_DS.win_fld(_wnd,,'NN',,,-9,2,0,,,MS.comment('DS','NN'));
_DS.win_fld(_wnd,,'NW',,,-9,2,1,,,MS.comment('DS','NW'));
_DS.win_fld(_wnd,,'NP',,,-9,2,1,,,MS.comment('DS','NP'));
: obsługa danych
_put:="cur_tab(1,1).put()";
_DS.win_act(_wnd,,'Formuła','Popraw'@@,,
   'Poprawienie bieżącego lub wybranych zapisów'@,
   "exec('ds_popraw_b','!pkd_grp_adys')",_put,,1,
   "exec('ds_popraw_grp_b','!pkd_grp_adys')"
);
_DS.win_act(_wnd,,'Formuła','U&względnij'@@,,
   'Uwzględnia bieżące lub wybrane informacje'@,
   "exec('ds_wprowadz_b','!pkd_grp_adys')",_put,,1
);
_DS.win_act(_wnd,,'Formuła','P&omiń'@@,,
   'Pomija bieżące lub wybrane informacje'@,
   "exec('ds_pomin_b','!pkd_grp_adys')",_put,,1
);
_DS.win_act(_wnd,,'Formuła','Dalej'@@,,
   'Rozpoczyna wprowadzanie danych'@,,
   "sel_exit()",,,,,,,'target=window'
);
_DS.win_act(_wnd,,'Rekord',,,,,"exec('ds_oblicz','!pkd_grp_adys',cur_tab(1,1))");
_DS.win_btn(_wnd,'text=%1,panel=right,align=begin'['Popraw'@],'menu:P');
_DS.win_btn(_wnd,'text=%1,panel=bottom,align=end'['Dalej'@],'menu:D');
: obudowa dla polepszenia funkcjonalności
_grp:=_DS.grp_make(_title,,'#pkd_grp_adys',,,"
   {? _a='sel_exit'
   || FUN.ask('Czy na pewno rozpocząć wprowadzanie informacji do dyscypliny pracy?'@)
   || FUN.ask('Czy na pewno porzucić wprowadzanie zmian w dyscyplinie pracy?'@)
   ?}",,
   'html_maximized'
);
_DS.grp_sel(_grp,,_wnd,,
   $("_TAB:=cur_tab(1,1);
      _ai:='';
      {? _TAB.sel_size()=0
      || {? _TAB.OK='T' || _ai:='W'
         |? _TAB.OK='N' || _ai:='O'
         || _ai:='WO'
         ?}
      ?};
      _TAB.actions_grayed("+'\''+_wnd+'\''+",_ai)
   "),,,,,,,,'maximized'
);
: ustaw widok
_DS.win_sel(_grp);

: uzupełnienie danych
DS.cntx_psh();
DS.index('DYSCYPLI');
P.cntx_psh();
P.prefix();

_P:=_ret.P;
_loop:=_P.first();
{!
|? _loop
|! {? P.seek(_P.SQL)
   || P.OSOBA();
      _DS.blank();
      _DS.P:=$P.ref();
      _DS.NAZWISKO:=OSOBA.NAZWISKO;
      _DS.PIERWSZE:=OSOBA.PIERWSZE;
      _DS.PESEL:=OSOBA.PESEL;
      _DS.T:=P.T;
      _DS.IP:=P.IP;
      _DS.WYDZIAL:=P.WYDZIAL().SYMBOL;
      _DS.STN:=P.ST().ST;
      _DS.NN:=EDIT_VAR.ADYS_NN;
      _DS.OK:='T';
      _DS.EQ:='N';
      _DS.CK:='X';
      DS.prefix(P.ref());
      {? DS.find_key(EDIT_VAR.ADYS_ROK,EDIT_VAR.ADYS_NB)
      || _DS.DS:=$DS.ref();
         _DS.EQ:={? EDIT_VAR.ADYS_NN=DS.NN || 'T' || 'R' ?};
         {? EDIT_VAR.ADYS_SA='N'
         || _DS.OK:='N'
         ?}
      ?};
      {? EDIT_VAR.ADYS_NZ='T' & DS.find_key(EDIT_VAR.ADYS_ROK-1,EDIT_VAR.ADYS_NB)
      || {? EDIT_VAR.ADYS_NR<>DS.NR
         || _DS.OPIS:='Niezgodny z podanym sposób wyrażenia nieobecności w poprzednim roku.'@;
            _DS.OK:='X'
         || _DS.NZ:=DS.NP
         ?}
      ?};
      exec('ds_oblicz','!pkd_grp_adys',_DS);
      _DS.NP:=_DS.NZ+_DS.NN-_DS.NW;
      _DS.add()
   ?};
   _loop:=_P.next()
!};
P.cntx_pop();

: wykorzystanie
_rehabil:=__RUB.sys_attr(EDIT_VAR.ADYS_NB,1171,N.OD);
{? _rehabil
|| _rn:=__RUB.sys_kod(1113,,1);
   {? _rn<>0
   || _url_nsp:=__RUB.ref(_rn);
      _N:=sql(
      'select N.P, sum(N'+EDIT_VAR.ADYS_NR+') as NW from N '+
      'where N.KOR=\'N\' and N.ROK=:_b and (N.NB=:_c or N.NB=:_d) and N.P in (select :_a.P from :_a)'
      'group by N.P',
      _DS,
      EDIT_VAR.ADYS_ROK,
      EDIT_VAR.ADYS_NB,
      _url_nsp
      )
   ?}
|| _N:=sql(
   'select N.P, sum(N'+EDIT_VAR.ADYS_NR+') as NW from N '+
   'where N.KOR=\'N\' and N.ROK=:_b and N.NB=:_c and N.P in (select :_a.P from :_a)'
   'group by N.P',
   _DS,
   EDIT_VAR.ADYS_ROK,
   EDIT_VAR.ADYS_NB
   )
?};

: czy zapytanie się powiodło?
{? type_of(_N)<>type_of(SYSLOG)
|| exec('msg_cancel','!pkd_grp_adys');
   DS.cntx_pop();
   return()
?};

: nanieś informacje o wykorzystaniu
_loop:=_N.first();
{!
|? _loop
|! _DS.blank(1);
   _DS.P:=_N.P;
   {? _DS.find_rec()
   || _DS.NW:=_N.NW;
      exec('ds_oblicz','!pkd_grp_adys',_DS);
      _DS.put()
   ?};
   _loop:=_N.next()
!};

: udostępnij
_DS.first();
{? ~_DS.select(,1)
|| DS.cntx_pop();
   return()
?};

_ustaw:="
   DS.NR:=EDIT_VAR.ADYS_NR;
   DS.NZ:=_a.NZ;
   DS.NN:=_a.NN;
   DS.NW:=_a.NW;
   DS.NP:=DS.NN-DS.NW+DS.NZ
";

DS.clear();
: wprowadzanie!
_loop:=_DS.first();
{!
|? _loop
|! {? _DS.OK='T'
   || {? _DS.DS<>''
      || {? DS.seek(_DS.DS)
         || _ustaw(_DS);
            {? DS.put()
            || _DS.OPIS:='Zmodyfikowano istniejące dane.'@;
               _DS.CK:='T'
            || _DS.OPIS:='Modyfikacja danych nie powiodła się.'@;
               _DS.CK:='N'
            ?};
            _DS.put()
         || _DS.OPIS:='Nie znaleziono zapisu do aktualizacji.'@;
            _DS.put()
         ?}
      |? ~P.seek(_DS.P)
      || _DS.OPIS:='Nie znaleziono pracownika.'@;
         _DS.put()
      || DS.blank();
         DS.R:=EDIT_VAR.ADYS_ROK;
         DS.NB:=EDIT_VAR.ADYS_NB;
         _ustaw(_DS);
         {? DS.add()
         || _DS.OPIS:='Utworzono informacje w dyscyplinie pracy.'@;
            _DS.CK:='T'
         || _DS.OPIS:='Utowrzenie danych nie powiodło się.'@;
            _DS.CK:='N'
         ?};
         _DS.put()
      ?}
   |? _DS.OPIS=''
   || _DS.OPIS:='Informacje zostały pominięte.'@;
      _DS.put()
   ?};
   _loop:=_DS.next()
!};

DS.cntx_pop();

: okienko podglądu przebiegu działania czynności
_wnd:=exec('ds_utworz_okno','!pkd_grp_adys',_DS,_title,'#pkd_wnd_adys');
_DS.win_fld(_wnd,,'CK',,,-3,,1,,,'Wynik wprowadzania danych [T/N/X]'@,2,,"'T'","'N'","'X'");
_DS.win_fld(_wnd,,'OPIS',,,-40,,1,,,'Krótki opis'@);
: ustaw widok
_DS.win_sel(_wnd);
_DS.select();

: koniec
~~


\msg_cancel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyświetla komunikat o zaprzestaniu działania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Anulowano aktualizację dyscypliny pracy.'@)


\msg_lock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyświetla komunikat o blokadzie statusu wprowadzania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FUN.emsg('Zmiana informacji o wprowadzeniu danych nie jest możliwa.'@)


\ds_utworz_okno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy podstawową część okienka przeglądania listy pracowników.
::   WE: _a - alias tabeli
::       _b - tytuł okna
::       _c - identyfikator okna
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wnd:=_a.mk_sel(_b,'P',0,_c,,,,,'U','T',,,,'html_maximized');
_a.win_fld(_wnd,,'OK',,,-3,,1,,,'Wprowadzić zmiany do kartoteki [T/N/X]'@,2,,"'T'","'N'","'X'");
_a.win_fld(_wnd,,'EQ',,,-3,,1,,,'Istnienie zapisu w kartotece [T/N/R]'@);
_a.win_fld(_wnd,,'T',,,-9,,1,,,MS.comment('P','T'));
_a.win_fld(_wnd,,'IP',,,-5,,1,,,MS.comment('P','IP'));
_a.win_fld(_wnd,,'NAZWISKO',,,-20,,1,,,MS.comment('OSOBA','NAZWISKO'));
_a.win_fld(_wnd,,'PIERWSZE',,,-20,,1,,,MS.comment('OSOBA','PIERWSZE'));
_a.win_fld(_wnd,,'PESEL',,,-11,,1,,,MS.comment('OSOBA','PESEL'));
_a.win_fld(_wnd,,'WYDZIAL',,,-16,,1,,,MS.comment('P','WYDZIAL'));
_a.win_fld(_wnd,,'STN',,,-20,,1,,,MS.comment('P','ST'));
: gotowe
_wnd


\ds_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_DS:=cur_tab(1,1);
{? {? _DS.sel_size()=0
   || exec('ds_popraw_grp_b','!pkd_grp_adys')
   || 1
   ?}
|| _DS.NN:=EDIT_VAR.ADYS_NN;
   exec('ds_oblicz','!pkd_grp_adys',_DS)
?}


\ds_popraw_grp_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_buf:=exec('save_fml_type','#field',EDIT_VAR,'BEFORE_EDIT');
EDIT_VAR.fld_fml('ADYS_ROK','BEFORE_EDIT',"0");
EDIT_VAR.fld_fml('ADYS_NB','BEFORE_EDIT',"0");
EDIT_VAR.fld_fml('ADYS_NR','BEFORE_EDIT',"0");
EDIT_VAR.fld_fml('ADYS_NZ','BEFORE_EDIT',"0");
EDIT_VAR.fld_fml('ADYS_SA','BEFORE_EDIT',"0");
_edit:=EDIT_VAR.edit("exec('adys_ae','!pkd_grp_adys')");
exec('restore_fml_type','#field',EDIT_VAR,'BEFORE_EDIT',_buf);
_edit


\ds_wprowadz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed akcją Wprowadź.
::   WE:
::   WY: 1 - zapis może być zmieniony
::       0 - nie można zmienić zapisu
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_DS:=cur_tab(1,1);
{? _DS.OK='X'
|| {? _DS.sel_size()=0
   || {? _DS.OPIS<>''
      || FUN.emsg(_DS.OPIS)
      || exec('msg_lock','!pkd_grp_adys')
      ?}
   ?}
|| _DS.OK:='T';
   _ret:=1
?};
_ret


\ds_pomin_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed akcją Pomiń.
::   WE:
::   WY: 1 - zapis może być zmieniony
::       0 - nie można zmienić zapisu
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_DS:=cur_tab(1,1);
{? _DS.OK='X'
|| {? _DS.sel_size()=0
   || exec('msg_lock','!pkd_grp_adys')
   ?}
|| _DS.OK:='N';
   _ret:=1
?};
_ret


\ds_oblicz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przelicza wartości.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a.NP:=_a.NZ+_a.NN-_a.NW;
~~


\edit_var_adys_nb_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wzorzec dla pola ADYS_NB zmiennej EDIT_VAR.
::   WE:
::   WY: ''
::----------------------------------------------------------------------------------------------------------------------
exec('slownik_atr','rubryki',197);
''


\edit_var_adys_x_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po redakcji pól ADYS_XX zmiennej EDIT_VAR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? EDIT_VAR.ADYS_ROK<0
|| EDIT_VAR.ADYS_ROK:=0
?};
{? EDIT_VAR.ADYS_ROK=0
|| return(1)
?};
_info:=0;
_rub_g:=0;
_rub_d:=0;

_data:=date(EDIT_VAR.ADYS_ROK,1,1);
{? __RUB.sys_attr(EDIT_VAR.ADYS_NB,1971,_data)
|| _info:=1;
   _rub_g:=19712;
   _rub_d:=19711
|? __RUB.sys_attr(EDIT_VAR.ADYS_NB,1973,_data)
|| _info:=1;
   _rub_d:=19731
:: Limity dla siły wyższej
|? __RUB.sys_attr(EDIT_VAR.ADYS_NB,1974,_data)
|| _info:=1;
   _rub_g:=19742;
   _rub_d:=19741
:: Limity urlopu opiekuńczego
|? __RUB.sys_attr(EDIT_VAR.ADYS_NB,1975,_data)
|| _info:=1;
   _rub_d:=19751
?};
{? _info
|| _atr:={? EDIT_VAR.ADYS_NR='G' || _rub_g || _rub_d ?};
   _val:=__RUB.sys_val(_atr,_data,0);
   {? _val<>0 & (EDIT_VAR.ADYS_NN=0 | (EDIT_VAR.ADYS_NN<>_val & FUN.ask(
         '"Limit bieżący" jest różny od wartości określonej w parametrach systemu.\n'
         'Czy przypisać polu wartość zgodną z parametryzacją programu?'@
      )))
   || EDIT_VAR.ADYS_NN:=_val
   ?}
?};
1


\adys_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdza wypełnienie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(EDIT_VAR,,'ADYS_ROK','ADYS_NB')

:Sign Version 2.0 jowisz:1045 2023/07/21 13:25:24 0c5e49e953490c6d4e33e9be2bfcf7cc2801b4a71a060e643e4107dbf606010cc77147029ef480f2f9fd959c92398af02440cc2b4b893647e8ce1c0457a8724c263d9d5cb69ed31fad910b5268439590831b4fc2690fa403436cd474b594a11597f836640273f94f2d649e5e692e362dfb3e31629af5cb5a658cc502e3dcffc2

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_dks_dsab.fml
:: Utworzony: 16.05.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_DKS_DSAB - Dekretacja amortyzacji bilans.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dekretacja amortyzacji bilans. - główna formuła czynności FKS_DKS_DSAB
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,FREJ
::# parses=exec('parses_amort','dok_fks')
::# access=exec('upr_amort','dok_fks')
:: PARAMETRY WE:
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=T, keyref=T
::# kind=WE, symbol=ODD, type=_ODD, name=Wskazanie na jednostkę księgową, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=DOK, type=_DOK, name=Wskazanie na dokument księgowy, required=T

_mp:=params_get().mp;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_akcja:=-_mp.akcja();

exec('odd_filtr','fst');
OKRO_F.prefix();
{? var_pres('OKRO_F',_we) & var_pres('ODD',_we) & OKRO_F.seek(_we.OKRO_F) & ODD.f_seek(_we.ODD)
|| exec('fl_decl','dekret_aut');
   exec('dek_decl','dekret_aut');
   exec('sd_decl','dekret_aut');
   _mp.descTodo();
   _auto:=_mp.isAutoRun();
:: z obszaru Dokumenty księgowe
   {? _akcja='dekretuj'
   || _dekret:=exec('dekretuj','!fks_dks_dsab');
      {? _dekret=1
      || _wy.DOK:=DOK.ref();
         _mp.save(,_wy);
         _mp.done()
      |? _dekret=-1
      || _wy.DOK:=null;
         _mp.save(,_wy);
         _mp.done()
      ?}
:: z ToDo lub uruchamiana automatycznie
   |? _mp.pathTodo() | _mp.isAutoRun()
   || exec('SRD','object');
      SRD.FST:=exec('szuk_b_dom','parses','FST');
      SRD.maski('r');
      POMOC.OKR:=SSTALE.AO;
      EKSG.SYS:='FST';
      {? _mp.isAutoRun()
      || BtnDek:=1
      || BtnDek:=0;
         EDIT_ES.OKRO_F:=_we.OKRO_F;
         EDIT_ES.ROK_F:=EDIT_ES.OKRO_F().ROK;
         EDIT_ES.ODD:=_we.ODD;
         _title:='Amortyzacja bilansowa - dekretowanie'@;
         _win:=EDIT_ES.mk_edit(_title,,'_amof_dekret');
         EDIT_ES.win_ewin(_win,,'PAR_AMOR');
         _par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Dekretuj'@];
         _btndek:=EDIT_ES.win_ebtn(_win,_par,"BtnDek:=1; 'key:Esc'");
         _par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@];
         _btnan:=EDIT_ES.win_ebtn(_win,_par,"BtnDek:=0; 'key:Esc'");
         EDIT_ES.btn_eopt(_win,_btndek,'tooltip='+'Dekretuj amortyzację bilansową'@);
         EDIT_ES.btn_eopt(_win,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
         EDIT_ES.win_edit(_win);
         EDIT_ES.display()
      ?};
      {? BtnDek
      ||
         {? exec('initWARL','dok_fks_aut_dok',SSTALE.AR,'FST','EF')
         || exec('init','dok_fks_aut_dok','Informacje o błędach dekretacji amortyzacji bilansowej');
            SSTALE.AO();
            exec('SRD','object');
            SRD.FST:=exec('szuk_b_dom','parses','FST');
            {? exec('is_okro_fst','fst')
            || exec('set_fld','dok_fks_aut_dok','O','EF');
               POMOC.OKR:=SSTALE.AO;
               exec('odd_filtr','fst');
               SRST.cntx_psh();
               SRST.index('ROK');
               SRST.prefix(SSTALE.AR,SSTALE.AO);
               _dekret:=exec('dekretuj','!fks_dks_dsab');
               {? _dekret=1
               || _wy.DOK:=DOK.ref();
                  _mp.save(,_wy);
                  _mp.done();
                  {? ~_mp.isAutoRun() || FUN.info('Amortyzacja bilansowa zadekretowana.'@) ?}
               |? _dekret=-1
               || _wy.DOK:=null;
                  _mp.save(,_wy);
                  _mp.done()
               || _wy.DOK:=null;
                  _mp.save(,_wy);
                  {? ~_mp.isAutoRun() || FUN.info('Dekretowanie amortyzacji bilansowej nie powiodło się.'@) ?}
               ?};
               SRST.cntx_pop();
               exec('done','dok_fks_aut_dok');
               echo;
               prgs_clr()
            || FUN.emsg('Bieżący okres nie jest okresem aktywnym w dziedzinie Środki trwałe.'@);
               _mp.cancel()
            ?}
         ?}
      || _mp.cancel()
      ?};
      VAR_DEL.delete('BtnDek')
   ?}
|| {? _akcja<>'dekretuj' || FUN.info('Niepoprawne parametry wejściowe.'@) ?};
   _wy:=_args.out;
   _wy.DOK:=null;
   _mp.save(,_wy);
   _mp.done()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:=_okres:=_odd:='';
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('OKRO_F',_we)
|| OKRO_F.cntx_psh();
   OKRO_F.prefix();
   {? OKRO_F.seek(_we.OKRO_F) || _okres:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ ?};
   OKRO_F.cntx_pop()
?};
{? var_pres('ODD',_we)
|| ODD.cntx_psh();
   ODD.prefix();
   {? ODD.seek(_we.ODD) || _odd:=ODD.OD ?};
   ODD.cntx_pop()
?};
_desc:='Zadekretuj amortyzację bilansową w okresie: %1 dla j. księgowej %2.'@@[_okres,_odd];
_desc


\dekretuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Funkcja dekretująca
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
exec('stalesys','#stalesys');
DEK_NAG.cntx_psh();
DEK_NAG.index('DEK_NAG'); DEK_NAG.prefix();
{? DEK_NAG.find_key(SSTALE.AR,'FST','EF') & DEK_NAG.r_lock(1,1,1)
|| EKSG.REF_ODD:=ODD.ref();
   {? ~exec('czy_nal_amor','fst',SSTALE.AO,ODD.ref())
   || {? ~FUN.ask('W sytemie istnieją środki z niepoliczoną amortyzacją w bieżącym okresie.\nKontynuować?'@)
      || _err:='Dla jednostki księgowej %1 istnieją środki z niepoliczoną amortyzacją w bieżącym okresie.'@[ODD.OD];
         exec('add_err','dok_fks_aut_dok','',_err,'EF');
         DEK_NAG.r_unlock(); DEK_NAG.cntx_pop();
         return(_wy)
      ?}
   ?};
   {? ~SRST.lock(1,1,1)

   || _err:='Dane środków trwałych zostały zablokowane przez innego użytkownika, uruchomienie funkcji nie jest możliwe.'@;
      exec('add_err','dok_fks_aut_dok','',_err,'EF');
      DEK_NAG.r_unlock(); DEK_NAG.cntx_pop();
      return(_wy)
   ?};
   {? exec('spr_war','dok_fks_aut_dok',SSTALE.AR,'FST','EF')
   || _p_okr:=exec('find_prev_okro','fst',SSTALE.AO().ROK,SSTALE.AO().NR);

:: jeżeli w poprzednim okresie suma amortyzacji wynosi zero to nie można wymagać zadekretowanej
:: amortyzacji za poprzedni okres
      _czy_n_p:=exec('czy_wart_amor','fst',_p_okr,ODD.ref(),'F');
      {? _czy_n_p=0 || _p_okr:=null ?};

      {? _p_okr<>null || _maska:=exec('dok_maska','fst',_p_okr) || _maska:='' ?};
      {? _p_okr=null | ~exec('spr_czy_ksie','dok_fks_aut_dok','EF'+$(#_p_okr),_maska)
         | FINFO.SPR_AMOR='T'
      || {? exec('spr_czy_ksie','dok_fks_aut_dok','EF'+$(#SSTALE.AO))
         || exec('set_fld','dok_fks_aut_dok','N','EF');
            {? exec('pozycje','dok_fks_aut_dok',SSTALE.AR,'FST','EF')
            || _wy:=1
            || _err:='Dla jednostki księgowej %1 nie udało się utworzyć dokumentu amortyzacji bilansowej w bieżącym okresie.'@[ODD.OD];
               exec('add_err','dok_fks_aut_dok','',_err,'EF');
::             wycofanie znaczników dekretowania
               exec('wycofaj_dekret','fst',SSTALE.AO,ODD.ref())
            ?}
         || _err:='Dla jednostki księgowej %1 istnieje dokument amortyzacji bilansowej w bieżącym okresie.'@[ODD.OD];
            exec('add_err','dok_fks_aut_dok','',_err,'EF');
            _wy:=-1
         ?}
      || _okres:=exec('okro_f_naz','fst',_p_okr);
         _err:='Dla jednostki księgowej %1 nie zadekretowano amortyzacji bilansowej '
               'w poprzednim okresie (%2).'@[ODD.OD,_okres];
         exec('add_err','dok_fks_aut_dok','',_err,'EF');
         _wy:=0
      ?}
   ?};
   SRST.unlock();
   DEK_NAG.r_unlock()
|| _err:='Procedura dekretowania amortyzacji została uruchomiona przez innego użytkownika.'@;
   exec('add_err','dok_fks_aut_dok','',_err,'EF')
?};
DEK_NAG.cntx_pop();
_wy

:Sign Version 2.0 jowisz:1045 2023/07/04 09:25:49 d46b95b7d9364296c1b1e49bc811d3a449a9bd1af6b9b49c7167e0c4bd1de401cdfe49de5c153591560e3fa64337d40ba9b15a01b7e0f944c3781a325d7e5bd44ce741ae97504d0d90a86e2d58dec1704a3e65335914b4e1dbbc34a44d4663ca5fb2069513cd3abdf642ecd84c65747df1f75a82637388dbbf2f7b177f94c754

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_mokz.fml
:: Utworzony: 19.01.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_MOKZ - Otwarcie okresu śr. trwałych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Otwarcie okresu śr. trwałych - główna formuła czynności FST_EWI_MOKZ.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
exec('open','!fst_ewi_mokz',_a)


\m_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Otwieranie okresu w obszarze środków trwałych
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| _b_dom:=exec('szuk_b_dom','parses','FST');
   {? ~_b_dom
   || FUN.info('W systemie nie znaleziono zdefiniowanego obszaru Środki trwałe.'@);
      return(0)
   ?};
   OKRO_F.prefix();
   OKR_OBSZ.index('UNIK');
   OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
   {? _b_dom & OKR_OBSZ.first()
   || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
      OKR_OBSZ.OKRO_F()
   || FUN.info('Nie znaleziono dla wybranego wspólnego okresu odpowiedniego okresu obszaru Środki trwałe.'@);
      return(0)
   ?}
?};

{? OKRO_F.AMOR<>'T'
|| FUN.info('Wskazany okres nie jest zamknięty.'@);
   return(0)
?};

{? OKRO_F.ZAM='T'
|| FUN.info('Wskazany okres jest zamknięty w obszarze Finanse, należy najpierw otworzyć okres w tym obszarze.'@);
   return(0)
?};

exec('main','!fst_ewi_mokz',OKRO_F.ref())


\open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła otwiera okres w obszarze środków trwałych
::   WE: _a - wskazanie na rekord OKRO_F
::   WY: 0 - nie udało się, 1 - udało się
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
OKRO_F.cntx_psh();
OKR_OBSZ.cntx_psh();
{? OKRO_F.seek(_a)
|| OKR_OBSZ.index('OKRO_F2');
   _fst:=exec('szuk_b_dom','parses','FST');
   OKR_OBSZ.prefix(_fst,OKRO_F.ref());
   {? OKR_OBSZ.first()
   || _okres:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ;
      {? FUN.ask('Otworzyć awaryjnie okres: %1?'@[_okres])
      || {? OKRO_F.AMOR='T'
         || {? OKRO_F.ZAM<>'T'
            || {? exec('okr_test_open','!fst_ewi_mokz',_a)
               || _wy:=exec('hist_open','!fst_ewi_mokz',_a)
               || FUN.emsg('Wskazany okres nie może zostać otwarty.\n'
                           'Istnieją późniejsze zamknięte okresy w obszarze Środki trwałe.'@)
               ?}
            || FUN.emsg('Wskazany okres jest zamknięty w obszarze Finanse, należy najpierw otworzyć okres w tym obszarze.'@)
            ?}
         || FUN.emsg('Wskazany okres nie jest zamknięty.'@)
         ?}
      ?}
   || FUN.emsg('Nie znaleziono wskazanego okresu w obszarze środków trwałych.'@)
   ?}
|| FUN.emsg('Nie znaleziono wskazanego okresu.'@)
?};
OKR_OBSZ.cntx_pop();
OKRO_F.cntx_pop();
_wy


\okr_test_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła weryfikuje czy można otworzyć okres w obszarze FST
::       sprawdzając czy wszystkie późniejsze okresy w obszarze FST są otwarte
::   WE: ref OKRO_F
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_wy:=1;
OKRO_F.cntx_psh();
OKR_OBSZ.cntx_psh();
OKRO_F.prefix();
{? OKRO_F.seek(_a)
|| _nr:=OKRO_F.NR;
   _rok:=OKRO_F.ROK;
   OKR_OBSZ.index('OKRO_ES2');
   _fst:=exec('szuk_b_dom','parses','FST');
   OKR_OBSZ.prefix(REF.FIRMA,_fst);
   {? OKR_OBSZ.find_key(_rok,_nr)
   || {! |?
         {? OKR_OBSZ.OKRO_F<>_a & OKR_OBSZ.OKRO_F().AMOR='T'
         || _wy:=0
         ?};
         _wy & OKR_OBSZ.next()
      !}
   ?}
?};
OKR_OBSZ.cntx_pop();
OKRO_F.cntx_pop();
_wy


\hist_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zapis w historii zamknięć i awaryjnych otwarć okresów informacji o otwarciu awaryjnym
::   WE: _a - wskazanie na OKRO_F
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
_fst:=exec('szuk_b_dom','parses','FST');
{? _fst & _a
|| K__HIST.cntx_psh();
   OKRO_F.cntx_psh();
   OKRO_F.prefix();
   {? OKRO_F.seek(_a)
   || K__HIST.prefix();
      K__HIST.blank(1);
      K__HIST.STATUS:='O';
      K__HIST.STATOP:='Otwarcie';
      K__HIST.OKRO_F:=_a;
      K__HIST.B_DOMAIN:=_fst;
      K__HIST.DATA:=date();
      K__HIST.CZAS:=time();
      K__HIST.USER:=OPERATOR.USER;
      K__HIST.win_edit('RED_OPEN');
      K__HIST.OKRO_F();
      OKRESY.NAZWA:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ;
      {? OKRO_F.r_lock(1,1,1)
      || {? K__HIST.edit()
         || {? exec('okr_test_open','!fst_ewi_mokz',_a)
            || OKRO_F.AMOR:='N';
               {? OKRO_F.put() & K__HIST.add()
               || exec('srst_amort_open','!fst_ewi_mokz',OKRO_F.ref());
                  _wy:=1
               ?}
            || FUN.emsg('Wskazany okres nie może zostać otwarty.\n'
                     'Istnieją późniejsze zamknięte okresy w obszarze Środki trwałe.'@)
            ?}
         ?};
         OKRO_F.r_unlock()
      || FUN.emsg('Okres zablokowany przez innego użytkownika.'@)
      ?}
   ?};
   OKRO_F.cntx_pop();
   K__HIST.cntx_pop()
?};
_wy


\rp_k_hist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na rekord przed w tabeli K__HIST w oknach dla okresów środków
::----------------------------------------------------------------------------------------------------------------------
{? K__HIST.OKRO_F().AMOR='N'
|| K__HIST.actions(cur_win(),'O:O',,1)
|| K__HIST.actions(cur_win(),'',,1)
?};
~~


\srst_amort_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła automatycznie ustawia znacznik naliczenia amortyzacji na N
::       podczas otwierania okresu BO lub BZ.
::   WE: ref OKRO_F
::----------------------------------------------------------------------------------------------------------------------
SRST.cntx_psh();
OKRO_F.cntx_psh();
OKRO_F.prefix();
{? OKRO_F.seek(_a) & OKRO_F.POCZ=date(0,0,0)
|| SRST.index('ROK');
   SRST.prefix(OKRO_F.ROK,OKRO_F.ref());
   {? SRST.first()
   || {! |?
         {? SRST.NAL='T'
         || SRST.NAL:='N';
            SRST.put()
         ?};
         SRST.next()
      !}
   ?}
?};
OKRO_F.cntx_pop();
SRST.cntx_pop()

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:44 c3436e9001e2f097653a51d5c944587bd95a522702c30b02054fdbd32cf52cdd02948ebef9288b9cd9a3f37c8e5d6290149d5a56f4c091f6a18e940137f90318564ed38e8a819a9166358ddf5a6a6d1a9c057596a6215015180769c986641d4a2f01a4b617b6dd8608eb7f2637f1acfbc549794e53f21a88bdecfa1fedb881f3

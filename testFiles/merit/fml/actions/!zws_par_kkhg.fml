:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_kkhg.fml
:: Utworzony: 30.09.2019
:: Autor: KN
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_KKHG - Wysyłanie e-maili z informacją o zmodyfikowanych i usuniętych
::            kontrahentach w bazie GUS.
::======================================================================================================================
\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Wysyłanie e-maili z informacją o zmodyfikowanych kontrahentach
::       Czynność uruchamiana jako usługa (serwis).
::----------------------------------------------------------------------------------------------------------------------
::# properties=SEND,SERVICE
::# kind=WE, symbol=DNIWSTECZ, type=NUMBER, name=Dla którego dnia wstecz wykonane ma być sprawdzenie (1-7), required=N
::# kind=WE, symbol=ODBIORCA, type=STRING, name=Odbiorca powiadomienia e-mail, required=T
_mp:=params_get().mp;
_in:=params_get().in;
_komunik:=(_mp.pathTodo() | _mp.pathProc()) & ~_mp.isAutoRun() & ~_mp.isService();
_email:={? var_press('ODBIORCA',_in)>0 || _in.ODBIORCA || '' ?};
_dni:={? var_press('DNIWSTECZ',_in)>0 || _in.DNIWSTECZ || 1 ?};
_ok:=1;
{? _email=''
|| {? _komunik
   || FUN.info('Nie podano adresu e-mail odbiorcy wiadomości.'@)
   ?}
|? _dni>7
|| {? _komunik
   || FUN.info('Nie można wykonać sprawdzenia kontrahentów na więcej niż 7 dni wstecz.'@)
   ?}
|? _dni=0
|| {? _komunik
   || FUN.info('Nie można wykonać sprawdzenia kontrahentów dla aktualnego dnia.'@)
   ?}
|? exec('mail_ok','#email',_email)=0
|| {? _komunik
   || FUN.info('Niepoprawny adresu e-mail odbiorcy wiadomości.'@)
   ?}
|| _date:=date()-_dni;
   {? _date~2>9
   || _data:=$(_date~1)+'-'+$(_date~2)
   || _data:=$(_date~1)+'-0'+$(_date~2)
   ?};
   {? _date~3>9
   || _data+='-'+$(_date~3)
   || _data+='-0'+$(_date~3)
   ?};
   _txt1:='Sprawdzenie aktualności kontrahentów z bazą GUS na dzień '+_data;
   _los:= "int(_a*rand)";
   nazwa:=$_los(9999999999)+'.xml';
   exec('tab','!zws_par_kkhg',_data);
   {? ~KONTRAHENCI.first()
   || {? _komunik
      || FUN.info('Brak zmodyfikowanych kontrahentów.'@)
      ?};
      _ok:=1
   || _ok:=1;
      {? _ok
      || exec('send','!zws_par_kkhg',_in.FROM,_in.SENDER,_in.REPLYTO,_email,date()-_dni,_txt1,ZMODYFIKOWANI)
      ?}
   ?};
   &nazwa
?};
{? _ok || _mp.done() ?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_dni:={? var_press('DNIWSTECZ',_in)>0 || _in.DNIWSTECZ || 1 ?};
_desc:='Wyślij sprawdzenie aktualności danych kontrahentów na %1 wstecz.'@@[$_dni];
_desc


\tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Tabela ze zmodyfikowanymi i usuniętymi kontrahentami
::   WE: _a - data na którą ma być wykonane sprawdzenie
::----------------------------------------------------------------------------------------------------------------------
_data:=_a;
VAR_DEL.delete('KONTRAHENCI');
VAR_DEL.delete('ZMODYFIKOWANI');
KONTRAHENCI:=tab_tmp(1,'NAZWA','STRING[60]','Nazwa skrócona','KOD','STRING[8]','Kod','NIP','STRING[22]','Nip kontrahenta','REGON','STRING[15]','Regon kontrahenta');
ZMODYFIKOWANI:=tab_tmp(1,'NAZWA','STRING[60]','Nazwa skrócona','KOD','STRING[8]','Kod','NIP','STRING[22]','Nip kontrahenta','REGON','STRING[15]','Regon kontrahenta','TYP','STRING[15]','Typ zmiany');
KH.cntx_psh();
KH.index('GLN');
KH.prefix();
{? KH.first()
|| {! |?
   {? KH.REG<>''
   || KONTRAHENCI.REGON:=KH.REG
   || {? KH.NIP<>''
      || _nip:=KH.NIP;
         {! |? _nip*'-'<>0
         |! _nip1:=((_nip*'-')-1)+_nip;
            _nip2:=_nip+(+_nip-(_nip*'-'));
            _nip:=_nip1+_nip2
         !};
         _regon:='';
         _regon:=9+exec('znajdz_regon','gus_ws',_nip);
         {? _regon<>''
         || KONTRAHENCI.REGON:=_regon;
            KH.get();
            KH.REG:=_regon;
            KH.put()
         || KH.get();
            KH.REG:='.';
            KH.put()
         ?}
      ?}
   ?};
   KONTRAHENCI.NAZWA:=KH.NAZ;
   KONTRAHENCI.KOD:=KH.KOD;
   KONTRAHENCI.NIP:=KH.NIP;
   KONTRAHENCI.add();
   KH.next()
   !}
?};
KH.cntx_pop();
exec('raport_zbiorczy','gus_ws','BIR11AktualizowanePodmiotyPrawneOrazDzialalnosciOsFizycznych',_data);
{? REGONY.first()
|| {! |?
      {? KONTRAHENCI.first()
      || {! |?
            {? KONTRAHENCI.REGON=REGONY.REGON
            || ZMODYFIKOWANI.REGON:=KONTRAHENCI.REGON;
               ZMODYFIKOWANI.NAZWA:=KONTRAHENCI.NAZWA;
               ZMODYFIKOWANI.KOD:=KONTRAHENCI.KOD;
               ZMODYFIKOWANI.NIP:=KONTRAHENCI.NIP;
               ZMODYFIKOWANI.TYP:='Zmodyfikowany';
               ZMODYFIKOWANI.add()
            ?};
            KONTRAHENCI.next()
         !}
      ?};
      REGONY.next()
   !}
?};
exec('raport_zbiorczy','gus_ws','BIR11SkreslonePodmiotyPrawneOrazDzialalnosciOsFizycznych',_data);
{? REGONY.first()
|| {! |?
      {? KONTRAHENCI.first()
      || {! |?
            {? KONTRAHENCI.REGON=REGONY.REGON
            || ZMODYFIKOWANI.REGON:=KONTRAHENCI.REGON;
               ZMODYFIKOWANI.NAZWA:=KONTRAHENCI.NAZWA;
               ZMODYFIKOWANI.KOD:=KONTRAHENCI.KOD;
               ZMODYFIKOWANI.NIP:=KONTRAHENCI.NIP;
               ZMODYFIKOWANI.TYP:='Skreślony';
               ZMODYFIKOWANI.add()
            ?};
            KONTRAHENCI.next()
         !}
      ?};
      REGONY.next()
   !}
?};
~~


\send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Wysłanie powiadomień o wynikach sprawdzenia kontrahentów z bazą GUS
::   WE: _a - nadawca
::       _b - od
::       _c - odpowiedź do
::       _d - odbiorca
::       _e - data sprawdzenia
::       _f - tytuł
::       _g - tabela ze zmodyfikowanymi kontrahentami
::----------------------------------------------------------------------------------------------------------------------
_tab:=_g;
{? _tab.first()
|| _mail:=exec('tabMail','!zws_par_kkhg');
   _mail.add('<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Final//EN">');
   _mail.add('<html>');
   _mail.add('<head>');
   _mail.add('<meta http-equiv="content-type" content="text/html; charset=iso-8859-2">');
   _mail.add('</head>');
   _mail.add('<body>');
   _mail.add('<BR><BR>');
   _mail.add('<b>'+_f+'</b><br><br>');
   _mail.add('<table valign=\"top\" border=\"1\" cellspacing=\"0\" cellpadding=\"0\"  >');
   _mail.add(
      '<tr class="head3">'
      '<td width="200">Nazwa skrócona</td>'
      '<td width="20">Kod</td>'
      '<td width="50">Nip kontrahenta</td>'
      '<td width="50">Regon kontrahenta</td>'
      '<td width="50">Typ zmiany</td>'
      '</tr>'
   );
   {!
   |? _mail.add('<tr class=\"wiersz\">');
      _mail.add('<td class=\"wiersz\" width=\"250\">'+ZMODYFIKOWANI.NAZWA+'</td>');
      _mail.add('<td class=\"wiersz\" width=\"50\">'+ZMODYFIKOWANI.KOD+'</td>');
      _mail.add('<td class=\"wiersz2\" width=\"80\">'+ZMODYFIKOWANI.NIP+'</td>');
      _mail.add('<td class=\"wiersz\" width=\"80\">'+ZMODYFIKOWANI.REGON+'</td>');
      _mail.add('<td class=\"wiersz\" width=\"90\">'+ZMODYFIKOWANI.TYP+'</td>');
      _mail.add('</tr>');
      _tab.next()
   !};
   _mail.add('</table>');
:: Dodanie e-maila
   _args:=exec('add_email_a','#mailbox');
   _args.FROM:=_a;
   _args.SENDER:=_b;
   _args.REPLYTO:=_c;
   _args.RCV:=_d;
   _args.SUB:=_f;
   _args.BODYH:=_mail.BODY;
   exec('add_email','#mailbox',_args)
?};
1


\tabMail
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [19.42]
:: OPIS: Tablica do przechowywania treści e-maila
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('BODY','LP','add');
_tab.LP:=0;
_tab.BODY:=tab_tmp(1,
   'LP','INTEGER','Lp',
   'TEKST','STRING[255]','Tekst'
);
_tab.add:="
   {!
   |? .LP+=1;
      .BODY.LP:=.LP;
      .BODY.TEKST:=255+_a; _a:=255-_a;
      .BODY.add();
      _a<>''
   !}
";
_tab

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 295b66aede4e6a4aad16ade0abcc3d260afe5f65ddd2df30a4395a0f38b5652dd116d6be3ef76acc40d61beda847a1f33a64f895e1b8fc4d863ee7650730bdedb2013bd81d32456f443530d326e82800a10e2c1f1168874675fc42c742adb573c93df09c056dab4e67e0370bbb0c322e2396a5c3292011e9ecc6c41c93a3099d

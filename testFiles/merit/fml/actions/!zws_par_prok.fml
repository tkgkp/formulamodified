:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_prok.fml
:: Utworzony: 22.06.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PROK - Typy ocen dla kompetencji.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Typy ocen dla kompetencji - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','poc');
{? ZZ_PAR.TYP_KOMP<>'T' & ZZ_PAR.MET_KOMP<>'T'
|| FUN.emsg('Funkcja nie jest dostępna przy aktualnych parametrach pracy.'@);
   return()
?};

_modul:='O';
ZZ_POM.MODUL==_modul;
ZZ_POM.CZYNNOSC:='';

_cfg:=exec('cfg','!zws_par_prok');
params_set('cfg',_cfg);

_tblank:='BLANK';

ZZ_HIST.cntx_psh();
ZZ_HIST.f_clear();
ZZ_HIST.index('DATA');
ZZ_HIST.prefix(exec('ref_firma','ustawienia'));
ZZ_HIST.win_sel(_cfg.ZZ_HIST.wnd);

ZZ_KOMP.cntx_psh();
ZZ_KOMP.f_clear();
ZZ_KOMP.index('NAZWA');

ZZ_TYP.cntx_psh();
exec('zz_typ_fset','phr_widok');
ZZ_TYP.win_dict('SLO');

ZZ_PROFT.cntx_psh();
ZZ_PROFT.f_clear();
ZZ_PROFT.clear();
_proft:=exec('save_fml_type','#field',ZZ_PROFT,_tblank);
ZZ_PROFT.fld_fml('ZZ_LINK',_tblank,"ZZ_KOMP.ZZ_DOK");
ZZ_PROFT.win_edit('RED');

ZZ_MET.cntx_psh();
ZZ_MET.f_clear();
ZZ_MET.prefix();
ZZ_MET.win_dict('SLO');

ZZ_PROFO.cntx_psh();
ZZ_PROFO.f_clear();
ZZ_PROFO.index('KOD');
_profo:=exec('save_fml_type','#field',ZZ_PROFO,_tblank);
ZZ_PROFO.fld_fml('ZZ_PROFK',_tblank,"exec('get_profk','phr_dane',ZZ_KOMP.ZZ_DOK,ZZ_KOMP.ref())");
ZZ_PROFO.fld_fml('ZZ_PROFT',_tblank,"ZZ_PROFT.ref()");
ZZ_PROFO.win_edit('RED');

ZZ_HIST.select();

exec('restore_fml_type','#field',ZZ_PROFO,_tblank,_profo);
ZZ_PROFO.actions(_cfg.ZZ_PROFO.ws,'');
ZZ_PROFO.f_clear();
ZZ_PROFO.cntx_pop();

ZZ_MET.f_clear();
ZZ_MET.cntx_pop();

exec('restore_fml_type','#field',ZZ_PROFT,_tblank,_proft);
ZZ_PROFT.actions(_cfg.ZZ_PROFT.ws(),'');
ZZ_PROFT.f_clear();
ZZ_PROFT.cntx_pop();

ZZ_TYP.f_clear();
ZZ_TYP.cntx_pop();

ZZ_KOMP.actions(_cfg.ZZ_KOMP.ws,'');
ZZ_KOMP.f_clear();
ZZ_KOMP.cntx_pop();

ZZ_HIST.f_clear();
ZZ_HIST.cntx_pop();


ZZ_POM.MODUL==_modul;
~~


\wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła tworzy okno grupowe do obsługi ocen dla kompetencji.
::   WE: _a [ARRAY] - Tablica elementów nazwanych z konfiguracją.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=_a;

_fid:="'prok%1%2%3' [_a,-ZZ_PAR.TYP_KOMP,-ZZ_PAR.MET_KOMP]";

_acr:=~_fid('_');

{? (_wnd:=__WND.SEL.get(ZZ_HIST,_acr))<>''
|| return(_wnd)
?};

_wnd:=ZZ_HIST.grp_make('Typy ocen dla kompetencji'@,,_fid('grp'),,,,,'maximized');
::__WND.SEL.put(ZZ_HIST,_acr,_wnd);

_mode:='maximized_with_title';

:: Kompetencje (drzewko)
ZZ_HIST.grp_sel(_wnd,ZZ_KOMP,_cfg.ZZ_KOMP.ws,,
   "  cur_tab(1,1).actions(cur_win(1,1),,,1);
      {? ZZ_PAR.TYP_KOMP='T' | ZZ_PAR.MET_KOMP='T'
      || _par:=params_get();
         params_set(_par);
         _cfg:=_par.cfg;
         grp_disp(ZZ_PROFT,_cfg.ZZ_PROFT.ws(),1)
      ?}
   ",,,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? _a
      || ZZ_POM.KREATORY:='#ZWS_PAR_PROK#ZZ_KOMP#WEO#'
      ?};
      ZZ_KOMP.prefix(ZZ_POM.ZZ_HIST)
   ",
   "  {? _a
      || ZZ_POM.KREATORY:=''
      ?}
   ",,,
   _mode,
   _fid('zz_komp')
);

{? ZZ_PAR.TYP_KOMP='T' | ZZ_PAR.MET_KOMP='T'
:: Typy oceny
|| ZZ_HIST.grp_splt(_wnd,,'vertical','zz_proft');
   ZZ_HIST.grp_sel(_wnd,ZZ_PROFT,_cfg.ZZ_PROFT.ws(),,
      "  {? ZZ_PAR.MET_KOMP='T'
         || params_set(_par:=params_get());
            _cfg:=_par.cfg;
            grp_disp(ZZ_PROFO,_cfg.ZZ_PROFO.ws)
         ?}
      ",,,10,
      "  params_set(_par:=params_get());
         _cfg:=_par.cfg;
         {? _a & ZZ_PAR.TYP_KOMP='T'
         || exec('zz_pom_psh','phr_widok',_cfg.ACT)
         ?};
         {? grp_empty(ZZ_KOMP,_cfg.ZZ_KOMP.ws)
         || '#disable'
         || ZZ_PROFT.f_set('ZZ_TYP(KOD)','join ZZ_TYP','ZZ_PROFT.ZZ_LINK=:_a and ZZ_TYP.KOD<>\\\':_b\\\'',
               ZZ_KOMP.ZZ_DOK,'OP'
            )
         ?}
      ",
      "  {? _a & ZZ_PAR.TYP_KOMP='T'
         || exec('zz_pom_pop','phr_widok')
         ?}
      ",,,
      _mode,
      _fid('zz_proft')
   );
   {? ZZ_PAR.MET_KOMP='T'
::    Metody oceny
   || ZZ_HIST.grp_splt(_wnd,'zz_proft','horizontal','zz_profo');
      ZZ_HIST.grp_sel(_wnd,ZZ_PROFO,_cfg.ZZ_PROFO.ws,,,,5,,
         "  params_set(_par:=params_get());
            _cfg:=_par.cfg;
            {? _a
            || exec('zz_pom_psh','phr_widok',_cfg.ACT)
            ?};
            {? grp_empty(ZZ_PROFT,_cfg.ZZ_PROFT.ws())
            || '#disable'
            || ZZ_PROFO.prefix(exec('get_profk','phr_dane',ZZ_KOMP.ZZ_DOK,ZZ_KOMP.ref()),ZZ_PROFT.ref())
            ?}
         ",
         "  {? _a
            || exec('zz_pom_pop','phr_widok')
            ?}
         ",,,
         _mode,
         _fid('zz_profo')
      )
   ?}
?};

_wnd


\cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.28]
:: OPIS: Formuła przygotowuje środowisko pracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=obj_new('ACT','ZZ_KOMP','ZZ_PROFT','ZZ_PROFO','ZZ_HIST');

_cfg.ACT:='ZWS_PAR_PROK';

_cfg.ZZ_KOMP:=obj_new('ws');
_cfg.ZZ_KOMP.ws:='WEO';

_cfg.ZZ_PROFT:=obj_new('ws');
_cfg.ZZ_PROFT.ws:="{? ZZ_PAR.TYP_KOMP='T' || 'KOMP' || 'KOMO' ?}";

_cfg.ZZ_PROFO:=obj_new('ws');
_cfg.ZZ_PROFO.ws:='CFG';

_cfg.ZZ_HIST:=obj_new('ws','wnd');
_cfg.ZZ_HIST.ws:='SEL';
_cfg.ZZ_HIST.wnd:=exec('wnd','!zws_par_prok',_cfg);

_cfg

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:02 33872ca31e701d8b8f323e859e96f593ad4afd4e414a3f5b1c2893422265d89f21592880af703148fc93ee0e46dcd38350261afc86690b91e7cc675806880d5955593eaf87b277a9a1784f8553f81b0bf60ce42c530d765f1fe8e0f284e6268716cc994663974f17e65c0beedf23655f109e1d59423c23d0364d5aa425b66c89

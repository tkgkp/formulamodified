:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_ggr_pkey.fml
:: Utworzony: 14.07.2017
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Klucz grupujący
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=LOOP,SERVICE
::# kind=WE,   symbol=PARAM,  type=STRING,   name=Parametr czynności,         required=T, fml_val="exec('param','!zws_ggr_pkey')"
::# kind=WE,   symbol=ITEM,   type=STRING,   name=Pozycja klucza grupującego, required=N
::# kind=WY,   symbol=ITEM,   type=STRING,   name=Pozycja klucza grupującego, required=N
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

_param:=_in.PARAM;
{? _param=~~ | _param=''
||
   _mp.done();
   return()
?};

{? _param*'|formula|'
||
   _formula:=(+'|formula|')-_param;
   params_set(params_get());
   ($_formula)()
?};

{? _param*'|LOOP|'
||
   {? _in.GRPKEY<>~~ & _in.GRPKEY<>''
   ||
      _mp.grpkey(_in.GRPKEY);
      _item:=_mp.grpkeyGet();
      _mp.save(exec('kind_out','#b_port'),'ITEM',_item);
      _mp.grpkeyDel();
      {? _mp.grpkeyGet()<>~~
      || _mp.loop_continue()
      ?}
   ?}
?};

:: generuje klucz grpkey
{? _param*'|grpkey|'
||
   _mp.grpkey()
?};
:: generuje klucz ggrpkey
{? _param*'|ggrpkey|'
||
   _mp.ggrpkey()
?};
:: dodanie pozycji klucza grpkey
{? _param*'|grpkeyAdd|'
||
   _mp.grpkey(_in.GRPKEY);

   _in_item:=0;
   _item:={? var_pres('p01',_in)>0 || _in.p01 || _in_item:=1; _in.ITEM ?};
   _item:=
      {? _in_item
      || _item
      |? type_of(_item)=type_of(null())
      || _tab_acr:=exec('tab_acr','#table',ref_tab(_in.p01));
         exec('val2fml','#convert',_item,type_of(_item),_tab_acr)
      || exec('val2fml','#convert',_item,type_of(_item))
      ?};
   _mp.grpkeyAdd(_item)
?};
:: dodanie pozycji klucza ggrpkey
{? _param*'|ggrpkeyAdd|'
||
   _mp.ggrpkey(_in.GGRPKEY);

   _in_item:=0;
   _item:={? var_pres('p01',_in)>0 || _in.p01 || _in_item:=1; _in.ITEM ?};
   _item:=
      {? _in_item
      || _item
      |? type_of(_item)=type_of(null())
      || _tab_acr:=exec('tab_acr','#table',ref_tab(_in.p01));
         exec('val2fml','#convert',_item,type_of(_item),_tab_acr)
      || exec('val2fml','#convert',_item,type_of(_item))
      ?};
   _mp.ggrpkeyAdd(_item)
?};

:: pobranie pozycji klucza
{? _param='|grpkeyGet|'
||
   {? _in.GRPKEY<>~~ & _in.GRPKEY<>''
   ||
      _mp.grpkey(_in.GRPKEY);
      _item:=_mp.grpkeyGet();
      {? _item<>~~
      ||
         _mp.save(exec('kind_out','#b_port'),'ITEM',_item)
      ?}
   ?}
:: usunięcie pozycji klucza
|? _param='|grpkeyDel|'
||
   {? _in.GRPKEY<>~~ & _in.GRPKEY<>''
   ||
      _mp.grpkey(_in.GRPKEY);
      _mp.grpkeyDel()
   ?}
:: wyświetlenie pozycji klucza
|? _param='|select|'
||
   {? _in.GRPKEY<>~~ & _in.GRPKEY<>''
   ||
      _mp.grpkey(_in.GRPKEY);
      _Tab1:=_mp.grpkeyGetAll();
      _wer:=_Tab1.mk_sel(_mp.GRPKEY,,,'grpkeygetall');
      _Tab1.win_fld(_wer,,'ITEM',,,50);
      _Tab1.win_fld(_wer,,'IDADD',,,31);
      _Tab1.win_sel(_wer);
      _Tab1.select()
   ?}
?};

{? _param*'|formula|'=0 || _mp.done() ?}


\param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Słownik parametrów czynności
::   WE:
::   WY: wybrana
::----------------------------------------------------------------------------------------------------------------------
_value:=cur_tab(1,1).VALUE;

_return:='';

_stale_formula:='|formula|';
_formula:='';

_Tab:=tab_tmp(1
   ,'LP'    ,'INTEGER'     ,'L.p.'
   ,'PARAM' ,'STRING[50]'  ,'Parametr'
   ,'O'     ,'STRING[100]' ,'Opis');
_add:="
   _a.LP:=_b;
   _a.PARAM:=_c;
   _a.O:=_d;
   _a.add()
";
_lp:=0;
_add(_Tab,_lp+=1,'|LOOP|','Wystawienie kolejnej pozycji klucza');
_add(_Tab,_lp+=1,'|grpkey|','Generuje klucz grupujący GRPKEY');
_add(_Tab,_lp+=1,'|ggrpkey|','Generuje klucz grupujący GGRPKEY');
_add(_Tab,_lp+=1,'|grpkey|ggrpkey|','Generuje klucze grupujące GRPKEY i GGRPKEY');
_add(_Tab,_lp+=1,'|grpkeyAdd|','Dodaje pozycję klucza grupującego GRPKEY');
_add(_Tab,_lp+=1,'|ggrpkeyAdd|','Dodaje pozycję klucza grupującego GGRPKEY');
_add(_Tab,_lp+=1,'|grpkeyAdd|ggrpkeyAdd|','Dodaje pozycję kluczy grupujących GRPKEY i GGRPKEY');
_add(_Tab,_lp+=1,'|grpkeyGet|','Pobiera pozycję klucza grupującego GRPKEY');
_add(_Tab,_lp+=1,'|grpkeyDel|','Usuwa pozycję klucza grupującego GRPKEY');
_add(_Tab,_lp+=1,_stale_formula,'Własna formuła obsługi klucza grupującego');
_add(_Tab,_lp+=1,'|select|','Wyświetla pozycje klucza grupującego GRPKEY');

{? _value*_stale_formula
||
   _formula:=(+_stale_formula)-_value
||
   _wer:=_Tab.mk_sel('Parametry'@,,,'grpkey_params');
   _Tab.win_fld(_wer,,'LP',,,5,,,'L.p.'@);
   _Tab.win_fld(_wer,,'PARAM',,,25,,,'Parametr'@);
   _Tab.win_fld(_wer,,'O',,,50,,,'Opis'@);
   _Tab.win_act(_wer,,'Formuła','Wybierz'@,,,"sel_exit",,1);
   _Tab.win_sel(_wer);
   {? _Tab.select()
   ||
      {? _Tab.PARAM=_stale_formula
      ||
         _formula:=1
      ||
         _return:=_Tab.PARAM
      ?}
   ?}
?};

_int:=type_of(_formula)=type_of(1);

{? _int | _formula<>''
||
   {? _int || _formula:='' ?};
   undefine();
   define('FORMULA',_formula,'Formuła'@,,100);
   def_btn('text=%1'['Zapisz'@],"'key:F2'");
   def_btn('text=%1'['Anuluj'@],"'key:Esc'");
   _valid:="
      _formula:=DEFINE.FORMULA;
      {? _formula=''
      ||
         FUN.info('Należy podać formułę.'@);
         'FORMULA'
      ||
         _Tab:=form_chk($_formula);
         {? _Tab.first()
         || _win:=_Tab.mk_sel('Lista błędów'@,,1,'form_chk',1,1);
            _Tab.win_sel(_win);
            _Tab.select();
            'FORMULA'
         || ''
         ?}
      ?}
   ";
   {? def_edit(_valid,'Parametr'@)
   || '|formula|'+DEFINE.FORMULA
   |? +_formula
   || '|formula|'+_formula
   || ''
   ?}
||
   _return
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 6a93c968fa97164d218d43bebf33b3760f99193ba1f39989333ca1cbf020a2ed819b6316ebdfb2437ce0719c96707d62aee6adba779520f27d9b728e4e4ba3cb3a7d87dc2a74767f0a784587f3c5babff5a7ac64cf67d81f79934a5290dfe4c9fd4139eb0921fed7131f1b36849d1ceb72a6e95c556d67fca12376086c896f48

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ruc_kmpk.fml
:: Utworzony: 13.10.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RUC_KMPK - Utworzenie PDF do planu kompensaty
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Utworzenie PDF do planu kompensaty - główna formuła czynności FKS_RUC_KMPK
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# parses=exec('parses_par_nag','rozrach')
::# kind=WE, symbol=PAR_NAG, type=_PAR_NAG, name=Wskazanie na nagłówek planu umowy kompensaty, required=T, keyref=T
::# kind=WE, symbol=TYP_KON, type=STRING, name=Nazwa typu kontaktu z kontrahentem, required=N, fml_val="exec('zdarzt_sel','dokum_zal')"
::# kind=WY, symbol=DOKUM, type=_DOKUM, name=Wskazanie na załącznik z plikiem pdf, required=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
_dokum:=null;
SER_KOR.TYP:='K';
_auto:=_mp.isAutoRun();
_typ:={? var_press('TYP_KON',_in)>0 || _in.TYP_KON || '' ?};
{? _mp.pathTodo() | _auto
|| {? var_press('PAR_NAG',_in)>0
   || _ref:=BB.sqlint($_in.PAR_NAG); _nam:=form(($_in.PAR_NAG)-8);
      {? _ref<>0 & _nam<>''
      || PAR_NAG.cntx_psh(); PAR_NAG.prefix();
         {? PAR_NAG.seek(_ref,_nam)
         || {? PAR_NAG.TYP<>SER_KOR.TYP
            || {? _auto
               || _mp.error('Wybrana umowa nie jest umową kompensaty.'@)
               || FUN.info('Wybrana umowa nie jest umową kompensaty.'@); _mp.error()
               ?}
            |? PAR_NAG.ST_AKC<>'T'
            || {? ~_auto || FUN.info('Umowa kompensaty nie jest zaakceptowana.'@) ?}
            |? PAR_NAG.KH1().JEZYK & ~exec('sprwzwyd','rozrach','fks_km_p',PAR_NAG.KH1().JEZYK().KOD,0)
            || {? ~_auto || FUN.info('Brak wzorca dla pliku PDF w języku %1 do kontaktu z kontrahentem %2.'@[SLO.KOD,KH.SKR]) ?}
            |? _auto | FUN.ask('Czy utworzyć plik PDF do umowy kompensaty\n\'%1\' z dnia %2?'@[PAR_NAG.PAR_UM().SYM,$PAR_NAG.DATA])
            || _dokum:=exec('add_pdf','!fks_ruc_kmpk',{? _auto || 2 || 1 ?},_typ)
            ?}
         || {? ~_mp.isAutoRun()
            || FUN.info('Nie znaleziono umowy kompensaty.'@)
            ?};
            _out.DOKUM:=null;
            _mp.save(,_out);
            _mp.done()
         ?};
         PAR_NAG.cntx_pop()
      || {? ~_mp.isAutoRun()
         || FUN.info('Nie znaleziono umowy kompensaty.'@)
         ?};
         _out.DOKUM:=null;
         _mp.save(,_out);
         _mp.done()
      ?}
   || {? ~_mp.isAutoRun()
      || FUN.info('Nie znaleziono umowy kompensaty.'@)
      ?};
      _out.DOKUM:=null;
      _mp.save(,_out);
      _mp.done()
   ?}
|? _akcja='AddPDF'
|| {? PAR_NAG.ST_AKC<>'T'
   || FUN.info('Umowa kompensaty nie jest zaakceptowana.'@)
   |? PAR_NAG.KH1().JEZYK & ~exec('sprwzwyd','rozrach','fks_km_p',PAR_NAG.KH1().JEZYK().KOD,0)
   || FUN.info('Brak wzorca dla pliku PDF w języku %1 do kontaktu z kontrahentem %2.'@[SLO.KOD,KH.SKR])
   |? FUN.ask('Czy utworzyć plik PDF do umowy kompensaty\n\'%1\' z dnia %2?'@[PAR_NAG.PAR_UM().SYM,$PAR_NAG.DATA])
   || _dokum:=exec('add_pdf','!fks_ruc_kmpk',1,_typ)
   ?}
?};
{? _dokum
|| _out.DOKUM:=_dokum;
   _mp.save(,_out);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Utwórz PDF do planu umowy kompensaty'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('PAR_NAG',_we)>0
|| _ref:=BB.sqlint($_we.PAR_NAG); _nam:=form(($_we.PAR_NAG)-8);
   {? _ref<>0 & _nam<>''
   || PAR_NAG.cntx_psh(); PAR_NAG.use(_nam); PAR_NAG.prefix();
      {? PAR_NAG.seek(_ref,_nam)
      || _desc:='Utwórz PDF do planu umowy kompensaty: %1 z dnia %2'@@[PAR_NAG.PAR_UM().SYM,$PAR_NAG.DATA]
      ?};
      PAR_NAG.cntx_pop()
   ?}
?};
_desc


\add_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodanie załacznika w postacji wydruku do PDF
::   WE: _a - nadpisać? 0-nie [1]-pytać 2-tak
::       _b - nazwa typu kontaktu
::   WY: Wskazanie na załacznik do wysłania (DOKUM.ref)
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
_ref:=null;
_dalej:=1;
_nag:=$PAR_NAG.ref();
DOKUM.cntx_psh();
DOKUM.index('TYP'); DOKUM.prefix(_nag,'P');
_jest:=DOKUM.first();
{? _jest
|| _blob:=DOKUM.DOKUM;
   {? _a=1
   || _dalej:=FUN.ask('Istnieje już plik PDF.\nCzy nadpisać istniejący?'@)
   |? _a=2
   || _dalej:=1
   || _dalej:=0
   ?}
?};
{? _dalej
|| ToPDF:=1;
   _plik:='plan_kompensaty_rozrachunków'+$DOKUM.tm_stamp()+'.pdf';
   _plik_baza:='plan_kompensaty_rozrachunków.pdf';
   _email:=exec('email_os_kon','kontrahent',PAR_NAG.KH1,'Kompensaty');
   ZDARZT.cntx_psh();
   ZDARZT.index('ZDARZT'); ZDARZT.prefix('N',_b,);
   _typ_kon:={? ZDARZT.first() || ZDARZT.ref() || null ?};
   ZDARZT.cntx_pop();
   _ok:=exec('rep_exec','#b_report','FKS_RUC_KMPK','fks_km_plan',,0,_plik,0,,,,'W');
   {? _ok
   || _ok:=exec('add','dokum_zal',_nag,_plik,~_jest,_email,_plik_baza,
         PAR_NAG.KH1,{? _email<>'' || KH_OSOB.NAZWISKO+' '+KH_OSOB.IMIE || '' ?},
         PAR_NAG.ODD,_typ_kon,'Plan kompensaty rozrachunków'
      );
      VAR_DEL.delete('ToPDF');
      {? _ok
      || {? _jest
         || _jest:=exec('find_dokumz','bloby',DOKUM.ref(),_blob)
         ?};
         DOKUMZ.prefix();
         exec('add_dokumz2','bloby',_plik_baza,~_jest);
         _ref:=DOKUM.ref()
      ?};
      ferase(_plik,1)
   ?}
?};
DOKUM.cntx_pop();
_ref


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego lub dokument jest zaakceptowany
::       zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
_wy:=_mp.load(exec('kind_out','#b_port'));
_keyRefs:=_mp.getRefs();
{? obj_len(_keyRefs)>0
|| {! _it:=1..obj_len(_keyRefs)
   |! _kref:=_keyRefs[_it];
      {? type_of(_kref)>0
      || {? ref_name(_kref)*'parnag'>0
         || _record:=exec('FindAndGet','#table',PAR_NAG,_kref,,,null());
            {? _record=null()
            || _mp.done()
            ?}
         ?}
      ?}
   !}
?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 c64fd7e123d336d03827cc624d5b6ae2da44c41982347797f479a38212395662aae25f1c981d3e2d2658b3e2939d3d91d568dc20da6501d59540d0e9371fdb325c8307593227def6154f1635a4934dc8d5967de06bbbd9758edd58f6de2e5b895c5da200d9901bbec4ed90907b70479c3090bc8e6809b11226d5c505f0afd63d

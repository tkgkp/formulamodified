:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_ezk_orhk.fml
:: Utworzony: 31.03.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności PKD_EZK_ORHK - Rejestracja kont kosztów.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja kont kosztów - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();

_result:='';

_uidref:=exec('ref2uid','#table',_in.P);
{? _uidref=''
|| _result:=exec('error','!pkd_ezk_orhk')

|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()

   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()

   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]

   ?}

|| {? _akcja='ZAKOŃCZ'
   || _mp.done()

   |? _mp.pathTodo()
   || _value:=exec('select','!pkd_ezk_orhk',_in.P);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};

{? _result<>''
:  Obsługa błędów
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja kont kosztów - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Zarejestruj konto kosztów: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Zarejestruj konto kosztów: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Zarejestruj konto kosztów: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Zarejestruj konto kosztów'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Słownik komunikatów o błędach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Rejestrowanie konta kosztów niemożliwe.\nNie znaleziono pracownika.'


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa czynności wywołanej z listy zadań
::   WE: _a - Wskazanie pracownika.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::  OLD: \start/kontohis.fml - Zmiana obsługi.
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a)
|| OSOBA.cntx_psh();
   P.OSOBA();
   P_KK.cntx_psh();
   P_KK.index('OX');
   P_KK.prefix(P.ref());
   P_KK.index('OD');
   P_KK.prefix(P.ref());
   {? exec('dest','f_zatr',P.F_ZATR,'KOD')='Z'
   || P_KK.win_sel('WER_Z')
   || P_KK.win_sel('WER_P')
   ?};
   P_KK.win_edit('RED');
   {? ~P_KK.first() & P.KK<>null()
   || P_KK.blank();
      P_KK.OD:=P.DZA;
      P_KK.KK:=P.KK;
      P_KK.add()
   ?};
   {? PAR_SKID.get(236)='T'
   || P_KK.index('OX')
   || P_KK.index('OD')
   ?};
   _ret:=P_KK.select();
   P_KK.cntx_pop();
   OSOBA.cntx_pop()
|| _ret:=exec('error','!pkd_ezk_orhk')
?};
P.cntx_pop();
_ret


\p_kk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [2006]
:: OPIS: Obsługa akcji "Rekord po". Sprawdzenie poprawności dołączonych danych do tabeli P_KK.
::  OLD: \chk_kk/kontohis.fml
::  OLD: \be_add/kontohis.fml - Zmiana obsługi.
::  OLD: \be_popraw/kontohis.fml - Zmiana obsługi.
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(P_KK,-menu_txt()='popraw',,'OD','KK');
{? (type_of(_chk)=type_of('') & _chk<>'') | (type_of(_chk)=type_of(0) & ~_chk)
|| return(_chk)
?};

_f_zatr:=exec('dest','f_zatr',P.F_ZATR,'KOD');
{? _f_zatr='P'
|| _dza:=MS.name(P,'DZA');
   _dz:=MS.name(P,'DZ')
|| _dza:='Data rozpoczęcia współpracy'@;
   _dz:='Data zakończenia współpracy'@
?};

{? P_KK.OD<P.DZA
|| P_KK.OD:=P.DZA;
   FUN.emsg('"%1" nie może być wcześniejsza niż "%2" (%3).'@ [MS.name(P_KK,'OD'),_dza,$P.DZA]);
   return('OD')
?};
{? P.DZ<>date(0,0,0) & P.DZ<=P_KK.OD &
   ~FUN.ask('"%1" jest późniejsza niż "%2" (%3).\nCzy akceptujesz dane?'@ [MS.name(P_KK,'OD'),_dz,$P.DZ])
|| P_KK.OD:=P.DZ-1;
   return('OD')
?};
''


\p_kk_zakoncz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Zakończ". Formuła wykonywana w dwóch środowiskach:
::          - po wywołaniu z listy zadań (okno wertowania tabeli P_KK z doklejonym oknem redagowania tabeli P);
::          - w ramach obszaru roboczego (okno wertowania tabeli P_KK jako składowa okna grupowego tabeli P).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=P_KK
|| sel_exit()

|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\p_kk_ao
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [2006]
:: OPIS: Obsługa akcji "Okienko po". Przepisanie ostatniego konta kosztów z tabeli P_KK do pola P.KK.
::       Akcja jest wykonywana wyłącznie dla określonego P. W czynności jest to badane wprost, w mikroczynności okno
::       jest "wyłączane" (enable=0) jeżeli nie ma określonego P i akcja "Okienko po" nie jest wykonywana.
::  OLD: \okno_po/kontohis.fml
::----------------------------------------------------------------------------------------------------------------------
exec('pracownik_aktualizuj','pracownik','P_KK');
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 c55af4d41b844151b77e0b81817f36533c33c5693aac490c6117520db456d04cfdc20af4abb0d0b26463c31dbba9ff11e07afb0fa4c048fb98ab3fbde5c1360c815be617b194b0dc865401396be998e65b751a9218cc61c288c14b8c14b30a2f856e3bc360dae75e421c9329428a1cc1307620d11af10549d732e524ee2701b6

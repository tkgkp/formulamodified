:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_dmpr.fml
:: Utworzony: 26.06.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_DMPR - Marginesy pracownika.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Redakcja marginesów we/wy dla współpracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();
_result:='';

_uidref:=exec('ref2uid','#table',_in.P);
{? _uidref=''
|| _result:=exec('error','!prc_czp_dmpr')
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uidref);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uidref);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('select','!prc_czp_dmpr',_in.P);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Redakcja marginesów pracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('desc','pracownik',params_get().mp);
{? _tab.ZAW_DANE='T'
|| {? _tab.OBCY='T'
   || 'Dodaj marginesy wejścia i wyjścia: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
   |? +_tab.PESEL
   || 'Dodaj marginesy wejścia i wyjścia: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
   || 'Dodaj marginesy wejścia i wyjścia: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
         [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
   ?}
|| 'Dodaj marginesy wejścia i wyjścia'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Komunikat o błędzie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Dodanie marginesów nie jest możliwe.'@,'Nie znaleziono pracownika.'@]


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a [REFERENCE] - Wskazanie pracownika.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.prefix();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a,,1)
|| OSOBA.cntx_psh();
   P.OSOBA();
   R_MARG.cntx_psh();
   R_MARG.actions_grayed('WER',{? R_MARG.size() || 'D' || '' ?});
   R_MARG.win_sel('WER_P');
   R_MARG.win_edit('RED');
   R_MARG.index('R_MARG');
   R_MARG.prefix(P.ref());
   _ret:=R_MARG.select();
   R_MARG.cntx_pop();
   OSOBA.cntx_pop()
|| _ret:=exec('error','!prc_czp_dmpr')
?};
P.cntx_pop();
_ret


\r_marg_bfz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Obsługa akcji "Zakończ".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(0,0)=R_MARG
|| sel_exit()
|| params_set(params_get());
   exec('pkd_run','pkd','ZAKOŃCZ')
?}


\r_marg_afd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.28]
:: OPIS: Obsługa po akcji "Dołącz".
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R_MARG.blank();
{? R_MARG.edit()
|| R_MARG.add()
?};
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 a77a6bc4321d6939d8a0a5497b6456faac1763ca8a7dc98c2c0021600ba19f241151286a041ac17aa4aeaa5dd51c46c0c3d6f266b10ac15e030c9ec08e5b0af6f2c4269e7f0438f25ff611b8eed7ff7938523561c6b0851b77a72891f80017f5cd18db1fa8572085ba3e6adba42d8f575b02048566018808f8f1c5f133389299

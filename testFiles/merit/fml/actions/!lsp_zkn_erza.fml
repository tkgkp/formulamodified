:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_zkn_erza.fml
:: Utworzony: 19.10.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Akceptacja zamówienia sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ
::# parses=exec('parses','!lsp_zkn_erza')
::# kind=WE,   symbol=ZK_N,    type=_ZK_N,  name=Zamówienie sprzedaży,    required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

_akcja:=_mp.akcja();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & _mp.isAutoRun());

exec('init_zkn','lsp');

:: WSTĘPNE WALIDACJE
_clean_result:=params_exec('clean','!lsp_zkn_erza',_mp,_in);
_can_continue:=_clean_result.RESULT;
{? _can_continue=0
|| return()
?};

{? ~(var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N)
|| _mp.error('Brak wymaganego parametru ZK_N.')
|? ref_name(_in.ZK_N)<>ZK_N.name()
|| _mp.error('Zamówienie jest w archiwum. Akceptacja niedostępna.')
|| ZK_N.cntx_psh();
   ZK_N.prefix();
   _ok:=1;
   {? ~ZK_N.seek(_in.ZK_N)
   || _ok:=0;
      _mp.error('Nie znaleziono zamówienia sprzedaży.')
   |? ZK_N.T().R<>'Z'
   || _ok:=0;
      _mp.error('Błędny parametr wejściowy ZK_N. Wymagane zamówienie sprzedaży.')
   ?};
   {? _ok
   || params_set('mp',_mp);

      {? _akcja='Akceptuj' | _auto
      || exec('akceptuj','!lsp_zkn_erza',_auto)
      |? _mp.pathTodo()
         | _mp.pathArea() & _akcja=''
      || _win_red:=exec('zknag_win_edit','zamsiw_nag','RED');
         _ff:="params_exec('zknag_pozycje_todo','!lsp_zkn_erza')";
         ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
         _ff:="params_exec('zknag_akceptuj_todo','!lsp_zkn_erza'); 'key:Esc'";
         ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
         _ff:="'key:Esc'";
         ZK_N.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
         ZK_N.win_edit(_win_red);
         exec('set_efld_opt','zamsiw_nag',_win_red);
         ZK_N.display();
::       Usunięcie definicji tymczasowych okien
         ZK_N.win_edit(''); ZK_N.win_edel(_win_red)
      || _mp.error('Nieobsługiwana ścieżka.')
      ?}
   ?};
   ZK_N.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZK_N',_in)<>type_of(~~) & _in.ZK_N
|| 'Zaakceptuj zamówienie sprzedaży: %1'@@[exec('record','#to_string',_in.ZK_N)]
|| ''
?}


\zknag_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zakupu - Akceptuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('zknag_akceptuj','zamsiw_nag')


\zknag_akceptuj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Akceptuj z todo
::   WE: params_get()
::----------------------------------------------------------------------------------------------------------------------
params_exec('akceptuj','!lsp_zkn_erza')


\zknag_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('zam_poz','zamsiw_poz',0);
''


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie sprzedaży - Akceptacja zamówienia
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       params_get()
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(1) || _a || 0 ?};
_mp:=params_get().mp;

_result:=0;

{? ZK_N.STAT_REJ='T'
|| {? ~_auto || FUN.info('Zamówienie zostało już zaakceptowany.'@) ?};
   _result:=1;
   _mp.done()
|? ZK_N.STAT_REJ='N'
|| FUN.info('Nie zakończono jeszcze rejestracji zamówienia.\nAkceptacja niemożliwa.'@);
   _mp.error('Nie zakończono jeszcze rejestracji zamówienia. Akceptacja niemożliwa.');
   _result:=1

|? ZK_N.OBI_POW<>'' & exec('edokum_status','obiegi_log',ZK_N.OBI_POW)=''
||
   FUN.info('Nie zakończono jeszcze obiegu zamówienia.\nAkceptacja niemożliwa.'@);
   _result:=0

|? ZK_N.OBI_POW<>'' & exec('edokum_status','obiegi_log',ZK_N.OBI_POW)='N'
||
   FUN.info('Obieg zamówienia zakończył się odrzuceniem.\nAkceptacja niemożliwa.'@);
   _mp.error('Obieg zamówienia zakończył się odrzuceniem. Akceptacja niemożliwa.');
   _result:=1

|? ZK_N.OBI_MUS='T' & ZK_N.OBI_POW=''
|| FUN.info('Nie rozpoczęto jeszcze obiegu zamówienia.\nAkceptacja niemożliwa.'@);
   return(0)

|? ZK_N.T().VALACC<>null() & ~exec('validate','wzorce',ZK_N.T().VALACC,ZK_N,ZK_N.ref())
|| _result:=0
|? _auto | FUN.ask('Zaakceptować zamówienie %1?'@[ZK_N.SYM])
|| ZK_N.AKC:='T';
   ZK_N.STAT_REJ:='T';
   {? ZK_N.put()
   || _result:=1;
::    Dokument Businesslink
      exec('zkn_bs_act','zamsiw_nag');
      _mp.done()
   ?}
?};

_result


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('ZK_N',_in)=type_of(null()) & _in.ZK_N
|| ZK_N.cntx_psh();
   ZK_N.use(ref_name(_in.ZK_N));
   ZK_N.prefix();
   {? ZK_N.seek(_in.ZK_N)
   || __PARSES.setVal('OddzialLogProd',ZK_N.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LZK';
      _args.AR:=ZK_N.DP~1;
      _args.AM:=ZK_N.DP~2;
      __PARSES.setVal('OkresRok',_args)
   ?};
   ZK_N.cntx_pop()
?};

1


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
params_exec('zam_clean','zamsiw_nag',_mp,_in)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 3110b6567dc1bcec5ba0d542f2214a8819edb6e921c67498165727c0fa581401469ba489e61d92fce57831731171c448937340a8566614a9b10d3ab68287aed4975d80fd80df7e82ec75fd58775055f344614f77d31a04b18842fd794a398a490f092fcd432efed4ec1467f52b124019412a48b869c017526eccc70a5c20fc12

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_khgi.fml
:: Utworzony: 31.07.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa czynności ZWS_PAR_KHGI - Rej. udostępnienia danych dla kontrahenta
::            Czynność jest potrzebna wyłącznie ze względu na uprawnienia.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rej. udostępnienia danych - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
~~


\kh_put_gi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła wyzwalacza po poprawieniu rekordu tabeli KH. Aktualizuje zapis dotyczący ochrony danych osobowych.
::----------------------------------------------------------------------------------------------------------------------
_pomin:=',PLATNOSC,OBS,GRKH,P,SNIP,MP,LOTUSUID,NASZ_KOD,IL_D_N,IL_D_O,STR_PRZ,WD,RTRANSPO,DREA,PODWYKON,PROJKZAS,OPIS';
_pomin+=',KOLOR,WIELKOSC,CRE_USER,CRE_DATE,CRE_TIME,MOD_USER,MOD_DATE,MOD_TIME,NRS,RAB_TYP,JEZYK,IDADD,IDPUT,FIZYCZNY';
_pomin+=',REJ_INFO,KRAJE,POWIAD,POWTEAMS,POWNTC,';
_zmiana:='';
{? KH.FIZYCZNY=1
|| {! _lp:=1..KH.fld_num()
   |! _acr:=KH.fld_acr(_lp);
      {? _pomin*(','+_acr+',')=0
      || _wb:=bfld(_acr);
         _wa:=($('_a.'+_acr))(KH);
         {? _wb<>_wa
         || _wbs:={? type_of(_wb)=2 || _wb || $_wb ?};
            _was:={? type_of(_wa)=2 || _wa || $_wa ?};
            _vp:=var_pres(_acr,KH);
            {? _acr='NDT' | _acr='WP'
            || {? _wbs<>'' || _wbs:=exec('szuk_slo','!zws_par_khgi',_wbs) ?};
               {? _was<>'' || _was:=exec('szuk_slo','!zws_par_khgi',_was) ?}
            |? _vp=26
            || _wbs:=exec('record','#to_string',_wb);
               _was:=exec('record','#to_string',_wa)
            ?};
            _zmiana+='\n'+KH.fld_name(_lp)+' Było: ['+_wbs+'] Jest: ['+_was+']'
         ?}
      ?}
   !}
?};
{? _zmiana<>''
|| exec('zapisz','giodo','kh',KH.ref(),date(),'M','Zakres zmian: '+_zmiana)
?};
~~


\szuk_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Szuka opisu dla słownika mając ref-a
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=_a;
{? +_a=16
|| SLO.cntx_psh(); SLO.prefix();
   {? SLO.seek(BB.sqlint(_a),SLO.name()) || _zwrot:=SLO.KOD+' '+SLO.TR ?};
   SLO.cntx_pop()
?};
_zwrot

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:41 5c8484ee8db302367824583b18ef88fc25da05e26c606eb34ca2b8f3b1d5db6e8b07f6147c93e9de2c6e314dc785a445256246458a9341ff8bfb1e0372c2ca993d0bdbb98274b450965865d471df13633ed5fd28805f57092fcc473a1e3c878f9b2d73c4cc2ae23e7c39e65a1cfef7d2691dbb95bf503808c46029d6013cf342

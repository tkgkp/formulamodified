:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rrk_zkpz.fml
:: Utworzony: 19.11.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RRK_ZKPZ - Przeglądanie potwierdzeń sald
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przeglądanie potwierdzeń sald - główna formuła czynności FKS_RRK_ZKPZ
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
~~


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dodanie zakładki do okna grupowego obszaru korespondencji z rozrachunków (FKS_RRK)
::   WE: _a - nazwa zakładki
::       _b - okno grupowe
::----------------------------------------------------------------------------------------------------------------------
SER_NAGS.grp_sel(_b,,'SER_NAGN',_a,,0,0,,"exec('bw','rozrach_kor','P','SER_NAGN','POTW')","",,,'maximized_with_title');
task_attach('FKS_RRK_DKPD');
task_attach('FKS_RRK_DKPE');
task_attach('FKS_RRK_DKPW');
task_attach('FKS_RRK_ZKPZ')


\przegsta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Statystyka potwierdzen sald
::  OLD: \przegsta/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
POTWIERS:=tab_tmp(7,'TREE','TREE_REF','',
                    'WART','STRING[32]','',
                    'REF_WAL','INTEGER','',
                    'STATUS','STRING[16]','Status',
                    'REF_KH','INTEGER','',
                    'KONTO','STRING[35]','',
                    'REF_ODD','INTEGER','',
                    'DOK','STRING[20]','',
                    'DO','STRING[10]','Data otwarcia',
                    'POZIOM','INTEGER','Poziom',
                    'DNASZ','REAL','Dobro nasze',
                    'DWASZ','REAL','Dobro wasze',
                    'KOMEN','STRING[60]','Uwagi kontrahenta',
                    'SER_POZ','INTEGER','Ref pozycji korespondencji',
                    'STATINT','INTEGER','Status cyfrowo',
                    'SYM_ROK','STRING[25]','Unikalny symbol'
                 );
POTWIND1:=POTWIERS.index('?');
POTWIND2:=POTWIERS.ndx_tmp('',1,'POZIOM',,0,'REF_WAL',,0,'STATUS',,0,'REF_KH',,0,'KONTO',,0,'REF_ODD',,0,'DOK',,0,'DOK',,0,'SYM_ROK',,0);
POTW_OKN:=POTWIERS.mk_sel('Statystyka potwierdzeń sald na dzień '@,'N',,'#potw_sld_an1',,,,1);
POTWIERS.win_fld(POTW_OKN,,'WART',,,33,,,,,'Wartość poziomu'@);
POTWIERS.win_fld(POTW_OKN,,'DO',,,12,,,,,'Data otwarcia rozrachunku'@);
POTWIERS.win_act(POTW_OKN,0,'Formuła','Zwiń/&rozwiń całość'@@,,'Zwijanie/rozwijanie całości'@,
                 "exec('zwrw_all','#tree','POTWIERS','TREE',POTW_OKN)",,,,,,'R');
POTWIERS.win_fml(POTW_OKN,,'WART',,'ICON_BEFORE',
                 "POTWIERS.cntx_psh(); POTWIERS.index(POTWIND1); POTWIERS.prefix(POTWIERS.ref());
                  _zwrot:={? POTWIERS.first()
                          || POTWIERS.cntx_pop();
                             {? POTWIERS.tr_state()=1
                             || 'xwin16.png:75'
                             || 'xwin16.png:74'
                             ?}
                          || POTWIERS.cntx_pop(); 'xwin16.png:76'
                          ?}; _zwrot");
POTWIERS.win_act(POTW_OKN,,'Formuła','Oblicz obroty'@@,,'Obroty i salda zaznaczonych pozycji'@,,
                 "exec('obl_obr','!fks_rrk_zkpz')",1,
                 1,"exec('be_gobr','!fks_rrk_zkpz')","exec('ae_gobr','!fks_rrk_zkpz')",'O');
POTWIERS.win_act(POTW_OKN,,'Formuła','Potwierdzenie'@@,,'Potwierdzenie zaznaczonych pozycji'@,,
                 "exec('potw_tmp','rozrach_kor',1)",,1,
                 "exec('bepottmp','rozrach_kor',1)","exec('aepottmp','rozrach_kor',1)",'P');
task_attach('FKS_RRK_DKPW');
POTWIERS.win_act(POTW_OKN,,'Formuła','O&drzucenie'@@,,'Odrzucenie zaznaczonych pozycji'@,,
                 "exec('potw_tmp','rozrach_kor',2)",,1,
                 "exec('bepottmp','rozrach_kor',2)","exec('aepottmp','rozrach_kor',2)",'D');
task_attach('FKS_RRK_DKPW');
POTWIERS.win_act(POTW_OKN,0,'Formuła','Aktualizuj'@@,,'Aktualizacja zawartości'@, "sel_exit(); gen_new:=1",,,,,,'A');
POTWIERS.win_fld(POTW_OKN,,'DNASZ',,,14,2,,,,'Dobro nasze'@);
POTWIERS.win_fld(POTW_OKN,,'DWASZ',,,14,2,,,,'Dobro wasze'@);
POTWIERS.win_fld(POTW_OKN,,'KOMEN',,,46,,,,,'Uwagi kontrahenta'@);
POTWIERS.win_act(POTW_OKN,,'Szukaj');
{! |?
   gen_new:=0;
   echo('Trwa analizowanie danych'@);
   SER_NAGS.cntx_psh(); SER_NAG.cntx_psh(); SER_POZ.cntx_psh();
   SLO.cntx_psh(); KH.cntx_psh(); ODD.cntx_psh();
   _data:=SER_NAGS.SER_NAG().DATA;
   {? SER_NAGS.first()
   || POTWIERS.index(POTWIND2); POTWIERS.prefix();
      SER_POZ.index('SER_POZO');
      {! |?
         _dalej:=0;
         SER_NAG.prefix(REF.FIRMA); SER_NAGS.SER_NAG();
         {? SER_NAG.DATA=_data
         || {? OPERATOR.DEPT
            || SER_POZ.prefix(REF.FIRMA,SER_NAG.ref(),OPERATOR.DEPT().OD)
            || SER_POZ.prefix(REF.FIRMA,SER_NAG.ref())
            ?};
            {? SER_POZ.first()
            || {! |?
                  {? OPERATOR.DEPT=null | SER_POZ.ODD=OPERATOR.DEPT
                  || _wal:=#SER_POZ.WAL; _odd:=#SER_POZ.ODD; _kon:=SER_POZ.KON;
                     _dok:=SER_POZ.DOK; _kh:=#SER_NAG.KH; _wal_kod:=SER_POZ.WAL().KOD;
                     _kh_skr:=SER_NAG.KH().SKR; _odd_od:=SER_POZ.ODD().OD;
                     _poziom1:=_poziom2:=_poziom3:=_poziom4:=_poziom5:=null;
                     SER_KOR.STAP_POT:={? SER_POZ.POTWIER=0
                                       || 'niezaakceptowane'
                                       |? SER_POZ.POTWIER=1
                                       || 'odrzucone'
                                       || 'potwierdzone'
                                       ?};
                     _pel_war:=_wal_kod;
                     _poziom1:=exec('add_ptmp','!fks_rrk_zkpz',0,_wal,'',0,0,'','',_wal_kod,1,1,'','','');
                     {? _poziom1<>null
                     || _pel_war+=SER_KOR.STAP_POT;
                        _poziom2:=exec('add_ptmp','!fks_rrk_zkpz',_poziom1,_wal,SER_KOR.STAP_POT,0,0,'','',SER_KOR.STAP_POT,0,2,'','','');
                        {? _poziom2<>null
                        || _pel_war+=(_kh_skr+_kh_skr);
                           _poziom3:=exec('add_ptmp','!fks_rrk_zkpz',_poziom2,_wal,SER_KOR.STAP_POT,_kh,0,'','',_kh_skr,0,3,'','','');
                           {? _poziom3<>null
                           || _pel_war+=(_odd_od+_odd_od);
                              _poziom4:=exec('add_ptmp','!fks_rrk_zkpz',_poziom3,_wal,SER_KOR.STAP_POT,_kh,_odd,'','',_odd_od,0,4,'','','');
                              {? _poziom4<>null
                              || _pel_war+=_kon;
                                 _poziom5:=exec('add_ptmp','!fks_rrk_zkpz',_poziom4,_wal,SER_KOR.STAP_POT,_kh,_odd,_kon,'',_kon,0,5,'','','');
                                 {? _poziom5<>null
                                 || _pel_war+=(_dok+_dok);
                                    exec('add_ptmp','!fks_rrk_zkpz',_poziom5,_wal,SER_KOR.STAP_POT,_kh,_odd,_kon,_dok,_dok,0,6,SER_POZ.KOMEN,$SER_POZ.DATA,SER_POZ.SYM_ROK)
                                 ?}
                              ?}
                           ?}
                        ?}
                     ?}
                  ?};
                  SER_POZ.next()
               !}
            ?}
         ?};
         SER_NAGS.next()
      !}
   ?};
   echo();
   POTWIERS.win_sel(POTW_OKN); POTWIERS.index(POTWIND1);
   POTWIERS.prefix();
   POTWIERS.hdr_sel($_data); POTWIERS.select();
   SLO.cntx_pop(); KH.cntx_pop(); ODD.cntx_pop();
   SER_NAGS.cntx_pop(); SER_NAG.cntx_pop(); SER_POZ.cntx_pop();
   POTWIERS.erase(); gen_new
!};
VAR_DEL.delete('POTWIERS','POTWIND1','POTWIND2','POTW_OKN')


\be_gobr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Grupa przed dla liczenia obrotow
::  OLD: \be_gobr/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.WN:=ROZNE.MA:=ROZNE.SWN:=ROZNE.SMA:=0;
ROZNE.win_edit('OBROTY2');
ROZNE.hdr_edit();
ROZNE.hdr_edit('Obroty i salda zaznaczonych pozycji'@);
1


\ae_gobr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Grupa po dla liczenia obrotow
::  OLD: \ae_gobr/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.SWN:=F.SWn(ROZNE.WN$2,ROZNE.MA$2);
ROZNE.SMA:=F.SMa(ROZNE.WN$2,ROZNE.MA$2);
ROZNE.display()


\add_ptmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Dodawanie rekordów do tabeli tymczasowej - statystyka potwierdzeń sald
::   WE: _a - numer rekordu nadrzednego (TREE_REF)
::       _b - ref waluty (INTEGER)
::       _c - status
::       _d - ref kontrahenta (INTEGER)
::       _e - ref jednostki ksiegowej (INTEGER)
::       _f - symbol konta analitycznego
::       _g - symbol dokumentu (rozrachunku)
::       _h - wartosc wyswietlana
::       _i - znacznik, czy pozycja widoczna
::       _j - poziom drzewa
::       _k - komentarz kontrahenta
::       _l - data otwarcia rozrachunku
::       _m - unikalny symbol rozrachunku
::   WY: ref/null - ref rekordu
::  OLD: \add_ptmp/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
{? ~POTWIERS.find_key(_j,_b,_c,_d,_f,_e,_g,_g,_m)
|| POTWIERS.blank(1);
   POTWIERS.TREE:=_a;
   POTWIERS.REF_WAL:=_b;
   POTWIERS.STATUS:=_c;
   POTWIERS.REF_KH:=_d;
   POTWIERS.REF_ODD:=_e;
   POTWIERS.KONTO:=_f;
   POTWIERS.DOK:=_g;
   POTWIERS.WART:=_h;
   POTWIERS.POZIOM:=_j;
   POTWIERS.DNASZ:=SER_POZ.DNASZ;
   POTWIERS.DWASZ:=SER_POZ.DWASZ;
   POTWIERS.KOMEN:=_k;
   POTWIERS.DO:=_l;
   POTWIERS.SYM_ROK:=_m;
   {? POTWIERS.POZIOM=6
   || POTWIERS.SER_POZ:=#SER_POZ.ref();
      POTWIERS.STATINT:=SER_POZ.POTWIER
   ?};
   {? POTWIERS.add() || _zwrot:=POTWIERS.ref() ?}
|| POTWIERS.DNASZ+=SER_POZ.DNASZ;
   POTWIERS.DWASZ+=SER_POZ.DWASZ;
   {? POTWIERS.put() || _zwrot:=POTWIERS.ref() ?}
?}


\obl_obr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Obliczenie obrotow i sald dla pozycji
::  OLD: \obl_obr/kor_ser.fml
::----------------------------------------------------------------------------------------------------------------------
{? POTWIERS.sel_size()=0
|| ROZNE.WN:=POTWIERS.DNASZ$2; ROZNE.MA:=POTWIERS.DWASZ$2;
   ROZNE.SWN:=F.SWn(ROZNE.WN,ROZNE.MA); ROZNE.SMA:=F.SMa(ROZNE.WN,ROZNE.MA);
   ROZNE.win_edit('OBROTY2');
   ROZNE.hdr_edit();
   ROZNE.hdr_edit('Obroty i salda zaznaczonych pozycji'@);
   ROZNE.display();
   ROZNE.hdr_edit()
|| ROZNE.WN+=POTWIERS.DNASZ$2; ROZNE.MA+=POTWIERS.DWASZ$2
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:49 2d6253ed6704a2a1145af82fac350be08c9ed6c2d4e00efbe034fd281527c9413b49cd0ae7b821829ef6dfe230d3da24bacb11c47ae7568f31b22bb3408216fdcf05a442a2003d65bb4d1749367707436c05a769dc429e76828b838d2306985b879fb83cbe9377abbe93ad2a3a6331d3997946af2d5eee88651eef55e49e04f3

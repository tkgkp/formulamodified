:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !ctr_pdm_zmoa.fml
:: Utworzony: 16.12.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności CTR_PDM_ZMOA - Redakcja wartości modeli
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Redakcja wartości dla modeli - główna formuła czynności CTR_PDM_ZMOA
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM


\pop_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Poprawianie modeli
::  OLD: \pop_mod/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
{? K__NAG.r_lock(1,1)
|| {? K__NAG.edit() || K__NAG.put() ?};
   K__NAG.r_unlock()
|| exec('msgmoderr','control')
?}


\usu_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Usuwanie modeli
::  OLD: \usu_mod/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'ZF'*K__NAG.K_WERSJE().CZY_SYS>0
|| FUN.info('Wartości dla wersji związanej z kontrolą budżetu.\nUsunięcie niemożliwe.'@)
|? K_WERSJE.CZY_SYS<>'N' & exec('czy_rok_zam_con','konsolidacja',K__NAG.ROK_F)
|| FUN.info('W roku %1 występuje okres zamknięty controllingowo.\nUsunięcie niemożliwe.'@[K__NAG.ROK_F().NAZ])
|| K__NAG.cntx_psh();
   K__NAG.index('DISP');
   K__NAG.prefix(K__NAG.FIRMA,#K__NAG.ref());
   _podwer:=K__NAG.first();
   K__NAG.cntx_pop();
   {? K__NAG.r_lock(1,1)
   || _budz:=exec('has_bud','!ctr_pdm_zmoa');
      {? _podwer
      || _pyt:={? _budz
               || FUN.choice('Model zawiera wartości w podwersjach i z harmonogramów budżetowych. Usunąć?'@,'EXCLAM','Tylko wartości'@)
               || FUN.choice('Model zawiera wartości w podwersjach. Usunąć?'@,,'Tylko wartości'@)
               ?};
         {? _pyt=1 || _pyt:=2 ?}
      || _pyt:={? _budz
               || FUN.choice('Model zawiera wartości z harmonogramów budżetowych.\nCo usunąć?'@,'EXCLAM','Wartości i definicję modelu'@,'Tylko wartości'@)
               || FUN.choice('Co usunąć?'@,,'Wartości i definicję modelu'@,'Tylko wartości'@)
               ?}
      ?};
      {? _pyt
      || _tabarch:=K__POZ.names();
         _tabwart:=K__WAR.names();
         do();
         exec('usu_mod0','control',_tabarch,_tabwart,_pyt);
         end()
      || K__NAG.r_unlock()
      ?}
   || exec('msgmoderr','control')
   ?}
?}


\usu_mod1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Usuwanie wartosci dla modeli dla zadanego przedzialu okresow
::  OLD: \usu_mod1/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'ZF'*K__NAG.K_WERSJE().CZY_SYS>0
|| FUN.info('Wartości dla wersji związanej z kontrolą budżetu.\nUsunięcie niemożliwe.'@)
|? K__NAG.r_lock(1,1)
|| _fml:="{? var_pres('MAPAUSU')>0 || exec('usun_wybrane','control') || 1 ?}";
   exec('ust_zm_okr','!ctr_pdm_zmoa');
   {? UD_POM.edit("exec('sprparar1','!ctr_pdm_zmoa')")
   || do();
      exec('usu_mod2','control', '0,1,2,3,4,5,6,7',_fml);
      end()
   ?};
   K__NAG.r_unlock()
|| exec('msgmoderr','control')
?}


\ust_zm_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Ustawianie zmiennych - okresy
::  OLD: \ust_zm_okr/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POM.OKR_P:=UD_POM.OKR_K:=null; K__NAG.ROK_F();
OKRO_F.cntx_psh();
OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
{? OKRO_F.first() || UD_POM.OKR_P:=OKRO_F.ref() ?};
{? OKRO_F.last() || UD_POM.OKR_K:=OKRO_F.ref() ?};
OKRO_F.cntx_pop();
UD_POM.win_edit('USU_MOD')


\sprparar1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Sprawdzanie parametrow w okienku USU_MOD zmiennej UD_POM
::  OLD: \sprparar1/skid_xd.fml
::----------------------------------------------------------------------------------------------------------------------
{? UD_POM.OKR_P=null
|| FUN.emsg('Nie wprowadzono okresu początkowego.'@); 'OKR_P'
|? UD_POM.OKR_K=null
|| FUN.emsg('Nie wprowadzono okresu końcowego.'@); 'OKR_K'
|? UD_POM.OKR_P().NR>UD_POM.OKR_K().NR
|| FUN.info('Nie wprowadzono poprawnie przedziału okresów.'@); 'OKR_P'
|? K__NAG.K_WERSJE().CZY_SYS<>'N' & exec('czy_okr_zam_con','konsolidacja',1,UD_POM.OKR_P,UD_POM.OKR_K)
|| 'OKR_P'
|| 1
?}


\expkont
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Eksport modelu i drzew
::  OLD: \expkont/skid_xd.fml
:: ~OST: INFILECHOOSER,INFOPEN
::----------------------------------------------------------------------------------------------------------------------
{? K__NAG.SKID_MB().TYP='P' & ~exec('chk_upr_plac','control')
|| FUN.info('Brak uprawnień do przeglądania wartości podziałów\ndla controllingu opartych o model kadrowo-płacowy.'@)

|? K__NAG.r_lock(1,1)
|| {? UD_SCH.lock(1,1)
   || path_lok:=''; erroreks:=0;
      exec('ust_zm_okr','!ctr_pdm_zmoa');
      {? UD_POM.edit("exec('sprparar1','!ctr_pdm_zmoa')")
      || _ok:=0;
         dwym:=obj_new(15); nwym:=obj_new(15);
         {! _i:=1..15 |! dwym[_i]:=null; nwym[_i]:='' !};
         SKID_MBP.index('LP'); SKID_MBP.prefix(K__NAG.SKID_MB);
         {? SKID_MBP.first()
         || {! |?
               {? SKID_MBP.LP=1
               || {? SKID_MBP.UD_SCH=null
                  || FUN.info('Nie zdefiniowano w systemie schematu\n'+'pozycji budżetowych. Eksport przerwano.'@)
                  || _ok:=1;
                     dwym[1]:=SKID_MBP.UD_SCH; nwym[1]:=SKID_MBP.NAZ
                  ?}
               || dwym[SKID_MBP.LP]:=SKID_MBP.UD_SCH; nwym[SKID_MBP.LP]:=SKID_MBP.NAZ
               ?};
               _ok & SKID_MBP.next()
            !}
         ?};
         {? _ok
         || _Interm:=exec('interm','#system');
            {? _Interm
            || tmpDir:=fmk_tmp_dir(0);
               path_lok:=fmkdir(tmpDir.get_path(),'dir')
            || path_lok:='@'+exec('filechooser','#file','Wybierz folder dla eksportowanych plików'@,0,,,,'DIRECTORIES_ONLY',1)
            ?}
         ?};
         {? _ok & path_lok<>'' || _ok:=exec('eksp_sch','control',1,SKID_MBP.LP,path_lok) ?};
         {? _ok & path_lok<>''
         || _plik:=path_lok+'/'+(2-K__NAG.KOD)+'.txt';
            _plik:=STR.maz2w95(_plik);
            _uchwyt:=fopen(_plik,'w',0);
            {? _uchwyt
            || SKID_MBP.cntx_psh();
               SKID_MBP.index('LP'); SKID_MBP.prefix(K__NAG.SKID_MB);
               linia_k:=''; _ile:=0;
               {? SKID_MBP.first
               || {! |?
                     _ile+=1;
                     linia_k+=SKID_MBP.NAZ+'\t';
                     SKID_MBP.next()
                  !}
               ?};
               SKID_MBP.cntx_pop();
               OKRO_F.cntx_psh();
               OKRO_F.index('ROK'); OKRO_F.prefix(K__NAG.ROK_F);
               {? OKRO_F.seek(UD_POM.OKR_P)
               || {! |?
                     linia_k+=(form(OKRO_F.NR,-2)+'/'+ROK_F.NAZ+'\t');
                     OKRO_F.ref()<>UD_POM.OKR_K & OKRO_F.next()
                  !}
               ?};
               linia_k:=linia_k-1;
               linia_k:=maz_utf8(linia_k);
               fwrite(_uchwyt,linia_k);
               exec('war_mod_ini','control',2);
               _sel1:='';
               {! _i..ile_wym
               |! {? _sel1<>'' || _sel1+=', ' ?};
                  _sel1+='WYMIAR'+form(_i,-2)
               !};
               _tt1:=sql('select distinct '+_sel1+' from :_a order by '+_sel1, TAB_WAR);
               {? _tt1.first()
               || TAB_WAR.index(ind_war3);
                  {! |?
                     linia_k:='';
                     {! _i:=1..ile_wym
                     |! ($('linia_k+=_a.WYMIAR'+form(_i,-2)))(_tt1);
                        linia_k+='\t'
                     !};
                     _i:=UD_POM.OKR_P().NR;
                     {!|? _nr:=(_i%7)+1;
                          _prefiks:='TAB_WAR.prefix('+$_nr;
                          {! _j:=1..ile_wym
                          |! _prefiks+=(', \''+($('_a.WYMIAR'+form(_j, -2)))(_tt1)+'\'')
                          !};
                          _prefiks+=')';
                          ($_prefiks)();
                          {? TAB_WAR.first() & ($('TAB_WAR.WYMIAR'+form(ile_wym, -2)))()=($('_a.WYMIAR'+form(ile_wym, -2)))(_tt1)
                          || {!|? ($('linia_k+=form(TAB_WAR.WAR'+form((_i%*7)+1,-2)+',,,\' ,\')'+'+\'\\t\''))();
                                  _i+=1;
                                  _i<=UD_POM.OKR_K().NR & (_i%*7)>0
                             !}
                          ?};
                          _i<=UD_POM.OKR_K().NR
                     !};
                     linia_k:=linia_k-1;
                     linia_k:=maz_utf8(linia_k);
                     fwrite(_uchwyt,linia_k);
                     _tt1.next()
                  !}
               ?};
               obj_del(_tt1);
               exec('war_mod_del','control');
               OKRO_F.cntx_pop();
               fclose(_uchwyt)
            || FUN.info('Błąd otwarcia pliku\n%1'@[_plik]);
               _ok:=0
            ?};
            {? _ok
            || {? _Interm
               || _zipname:=tmpDir.get_path()+'/Archiwum.zip';
                  fpack_add(_zipname,path_lok);
                  dlg_save(_zipname,0)
               || {? erroreks
                  || FUN.info('Wystąpiły błędy podczas eksportu danych.'@)
                  || FUN.info('Dane wyeksportowano do katalogu:\n%1.'@[1-path_lok])
                  ?}
               ?}
            ?}
         ?};
         VAR_DEL.delete('dwym','nwym','il_wym','linia_k','tmpDir')
      ?};
      K__NAG.r_unlock(); VAR_DEL.delete('path_lok','erroreks'); UD_SCH.unlock()
   || FUN.info('Schematy są obsługiwane przez innego operatora.'@)
   ?}
|| exec('msgmoderr','control')
?}


\has_bud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy model zawiera wartości z harmonogramów budżetowych
::----------------------------------------------------------------------------------------------------------------------
_has:=0;
OKRO_F.index('ROK');
OKRO_F.prefix(K__NAG.ROK_F);
{? OKRO_F.first()
|| {!
   |? K__POZ.cntx_psh();
      K__POZ.use('yb'+exec('maska','control'));
      K__POZ.index('RODZAJ'); K__POZ.prefix(3,K__NAG.ref());
      _has:=K__POZ.first();
      K__POZ.cntx_pop();
      _has=0 & OKRO_F.next()
   !}
?};
_has

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 13e040ab1f74b98ad6b66e815d1cd631af59a9d9685876baea70fd98fab0195c35a3e249699cdd06d910cacb755c0e600dcdc44d35e215680a219a9b97ec257bf850bcaec0312b9f16696ffbe7546ecf1879aa1691397b071acd97c2cb777e91835ae478cefd84c2af94b4aa736c6fd313e18dd115b880f571b8acd6104980f2

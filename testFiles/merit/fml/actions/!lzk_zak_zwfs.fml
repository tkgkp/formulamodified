:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lzk_zak_zwfs.fml
:: Utworzony: 11.08.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Utworzenie dokumentu w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LZK
::# parses=exec('parses','!lzk_zak_zwfs')
::# kind=WE,   symbol=ZAK,       type=_FAKS,  name=Dokument zakupu,  required=T, keyref=T
::# kind=WE,   symbol=CZY_PDF,   type=STRING, name=Utwórz PDF,       required=N, keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE,   symbol=PRN_MSK,   type=STRING, name=Maska wydruku,    required=N, keyref=N
::# kind=WY,   symbol=DOKUM,     type=_DOKUM, name=Załącznik,        required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_zak','lzk');

_akcja:=_mp.akcja();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' ?};
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || _in.PRN_MSK
      || ''
      ?};

{? var_pres('ZAK',_in)=type_of(null()) & _in.ZAK
||
:: Ustawienie kontekstu wg instancji elementu w procesie
   FAKS.cntx_psh();
   FAKS.prefix();
   {? FAKS.seek(_in.ZAK)
   ||
      _result:=exec('fak_druk','faktury_wspolne',_czy_pdf,_msk);
      {? _czy_pdf & _result.WYN
      ||
         _out.DOKUM:=_result.REF;
         _mp.save(,_out);
         {? ~_keep || _mp.done() ?}

      |? ~_czy_pdf & _result.WYN
      ||
         {? ~_keep || _mp.done() ?}
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};
   FAKS.cntx_pop()
||
   _mp.error('Brak parametru wejściowego ZAK.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('ZAK',_in)=type_of(null()) & _in.ZAK
|| {? exec('FindAndGet','#table',FAKS,_in.ZAK,,"FAKS.WHERE='E'",0)
   || 'Utwórz PDF faktury wewnętrznej : %1'@@[exec('record','#to_string',_in.ZAK)]
   || 'Utwórz PDF dokumentu zakupu: %1'@@[exec('record','#to_string',_in.ZAK)]
   ?}
|| ''
?}


\zak_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument sprzedaży - Utwórz PDF
::----------------------------------------------------------------------------------------------------------------------
exec('fak_druk_gr','faktury_wspolne',1,'LZK_ZAK_ZWFS','Area Utwórz PDF')


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('ZAK',_in)=type_of(null()) & _in.ZAK
|| FAKS.cntx_psh();
   FAKS.use(ref_name(_in.ZAK));
   FAKS.prefix();
   {? FAKS.seek(_in.ZAK)
   || __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LZK';
      _args.AR:=FAKS.AR;
      _args.AM:=FAKS.AM;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FAKS.cntx_pop()
?};

1


\zak_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AWI] [17.00]
:: OPIS: Dokument sprzedaży - Drukuj
::----------------------------------------------------------------------------------------------------------------------
exec('fak_druk_gr','faktury_wspolne',0,'LZK_ZAK_ZWFS','Area Drukuj')

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:15 8bbe2575b306860bc401026489485f1ae7584b2e246d6c9fdffda44e831e5f7d0b495410f27420cd62211e2dc0dbf4f0d0aee1d2365886bbef7c99a26c9579c66b2bcbaaa7fac60a2a0d23744f151ae457127d67a5b26f0bd784a59d07da69551d68ff9dea5ddbbbad82ba7aafc59cd1ca9bd689bf4a878f56e0f21f33cb4d4c

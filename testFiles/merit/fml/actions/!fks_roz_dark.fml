:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_roz_dark.fml
:: Utworzony: 23.04.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_ROZ_DARK - Redakcja danych rozrachunku
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Redakcja danych rozrachunku - główna formuła czynności FKS_ROZ_DARK.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::
1


\poprroz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [?????]
:: OPIS: Poprawianie rozrachunku wraz z rozrachunkami powiazanymi
::UWAGA: Limit zmiennych lokalnych jest juz w formule wykorzystany
::   WY: Czy dokonano edycji rozrachunku
::  OLD: \poprroz/rozrach.fml
::----------------------------------------------------------------------------------------------------------------------
_zmiana:=0;
{? OP.r_lock(1,1)
|| OP.cntx_psh(); OP.prefix(); OP.win_edit('RED');
   {? var_pres('TAB_ROZ')>0
   || _twal:=OP.WAL; _todd:=OP.ODD().OD; _tan:=OP.AN; _tsym:=OP.SYM
   ?};
   SLO.cntx_psh(); SLO.prefix();
   _op_typ:=_op_stan:='';
   _opwal2:=_opwal3:=null; _l_wal:=1;
   _opwal21:=_opwal31:='';
   OP.cntx_psh();
   {? SSTALE.WAL<>FINFO.NAROD
   || OP.index('KON_O'); OP.prefix(FINFO.NAROD,OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK); OP.first();
      _l_wal:=2; _opwal2:=FINFO.NAROD; _opwal21:=FINFO.NAROD().KOD
   ?};
   ZAP_OP.cntx_psh();
   ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
   {? ZAP_OP.first()
   || {! |?
         {? ZAP_OP.WAL2<>null & ZAP_OP.WAL2<>SSTALE.WAL
         || {? _l_wal=1
            || _opwal2:=ZAP_OP.WAL2; _opwal21:=ZAP_OP.WAL2().KOD; _l_wal:=2
            |? _l_wal=2 & _opwal2<>ZAP_OP.WAL2
            || _opwal3:=ZAP_OP.WAL2; _opwal31:=ZAP_OP.WAL2().KOD; _l_wal:=3
            ?}
         ?};
         _l_wal<3 & ZAP_OP.next()
      !}
   ?};
   ZAP_OP.cntx_pop();
   OP.cntx_pop();
   KS.cntx_psh();
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); SSTALE.AR();
   KS.index('SYM'); KS.prefix(ROK_F.ref(),ROK_F.SYNT+OP.AN);
   {? KS.first() & KS.ROZR<>'Z' & KS.OBTYPROZ & OP.TYP<>KS.T_ROZ
   || {? FUN.ask('Rozrachunek '+OP.SYM+' jest typu '+OP.TYP+'.'+
                 '\n W definicji konta '+KS.SYM+' wpisany jest obowiązkowy typ '+KS.T_ROZ+
                 '\nCzy zmienić typ rozrachunku '+
                 {? _l_wal=2
                 || 'i rozrachunku powiązanego '
                 |? _l_wal=3
                 || 'i rozrachunków powiązanych '
                 || ''
                 ?}+'na '+KS.T_ROZ+'?')
      || _op_typ:=KS.T_ROZ
      ?}
   ?};
   ROK_F.cntx_pop(); KS.cntx_pop();
   {? _op_typ='' & F.SWn(OP.WN,OP.MA) & -(1+OP.TYP)='z'
   || {? _l_wal=1
      || {? -OP.TYP='zob'
         || {? exec('op_typ','!fks_roz_dark','Zobowiązanie',0,'','','Należność','Wn','Ma') || _op_typ:='NAL' ?}
         || {? exec('op_typ','!fks_roz_dark','Odsetki od zobowiązań',0,'','','Odsetki od należności','Wn','Ma') || _op_typ:='NOD' ?}
         ?}
      || OP.cntx_psh();
         _licz:=1; _wal2:=_wal3:=''; _typroz:=OP.TYP;
         {? (_ref:=exec('szuk2roz','!fks_roz_dark',_opwal2); _ref<>null) & OP.seek(_ref) & F.SMa(OP.WN,OP.MA)
         || _wal2:=_opwal21; _licz:=2
         ?};
         {? _l_wal=3
         || {? (_ref:=exec('szuk2roz','!fks_roz_dark',_opwal3); _ref<>null) & OP.seek(_ref) & F.SMa(OP.WN,OP.MA)
            || {? _licz=1 || _licz:=2; _wal2:=_opwal31 || _licz:=3; _wal3:=_opwal31 ?}
            ?}
         ?};
         {? -_typroz='zob'
         || {? exec('op_typ','!fks_roz_dark','Zobowiązanie',_licz,_wal2,_wal3,'Należność','Wn','Ma') || _op_typ:='NAL' ?}
         || {? exec('op_typ','!fks_roz_dark','Odsetki od zobowiązań',_licz,_wal2,_wal3,'Odsetki od należności','Wn','Ma') || _op_typ:='NOD' ?}
         ?};
         OP.cntx_pop()
      ?}
   |? _op_typ='' & F.SMa(OP.WN,OP.MA) & -(1+OP.TYP)='n'
   || {? _l_wal=1
      || {? -OP.TYP='nal'
         || {? exec('op_typ','!fks_roz_dark','Należność',0,'','','Zobowiązanie','Ma','Wn') || _op_typ:='ZOB' ?}
         || {? exec('op_typ','!fks_roz_dark','Odsetki od należności',0,'','','Odsetki od zobowiązań','Ma','Wn') || _op_typ:='ZOD' ?}
         ?}
      || OP.cntx_psh();
         _licz:=1; _wal2:=_wal3:=''; _typroz:=OP.TYP;
         {? (_ref:=exec('szuk2roz','!fks_roz_dark',_opwal2); _ref<>null) & OP.seek(_ref) & F.SWn(OP.WN,OP.MA)
         || _wal2:=_opwal21; _licz:=2
         ?};
         {? _l_wal=3
         || {? (_ref:=exec('szuk2roz','!fks_roz_dark',_opwal3); _ref<>null) & OP.seek(_ref) & F.SWn(OP.WN,OP.MA)
            || {? _licz=1 || _licz:=2; _wal2:=_opwal31 || _licz:=3; _wal3:=_opwal31 ?}
            ?}
         ?};
         {? -_typroz='nal'
         || {? exec('op_typ','!fks_roz_dark','Należność',_licz,_wal2,_wal3,'Zobowiązanie','Ma','Wn') || _op_typ:='ZOB' ?}
         || {? exec('op_typ','!fks_roz_dark','Odsetki od należności',_licz,_wal2,_wal3,'Odsetki od zobowiązań','Ma','Wn') || _op_typ:='ZOD' ?}
         ?};
         OP.cntx_pop()
      ?}
   ?};
   {? (OP.WN$2=OP.MA$2) & -OP.STAN<>'r'
   || {? exec('op_stan','!fks_roz_dark',1) || _op_stan:='R' ?}
   |? (OP.WN$2<>OP.MA$2) & -OP.STAN='r'
   || {? exec('op_stan','!fks_roz_dark',2) || _op_stan:='N' ?}
   ?};
   {? _op_typ<>''  || OP.TYP:=_op_typ   ?};
   {? _op_stan<>'' || OP.STAN:=_op_stan ?};
   typ_roz:=OP.TYP;
   exec('op_enable','rozrach');
   {? OP.edit("exec('chk_op','!fks_roz_dark',typ_roz)")
   || {? (1+OP.TYP)='R' & OP.TYP<>typ_roz
      || exec('rmk_edyt','!fks_roz_dark'); RMK.index('NAZ');
         {? ~RMK.find_key(OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK)
         || FUN.info('Nie uzupełniono danych rozliczenia międzyokresowego.\n'
                     'Przywrócono poprzedni typ rozrachunku.'@);
            OP.TYP:=typ_roz
         ?}
      ?};
      do();
      {? (1+typ_roz)='R' &  OP.TYP<>typ_roz
      || RMK.index('NAZ'); RMK.prefix();
         {? RMK.find_key(OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK)
         || exec('kasujrrmk','rmk',RMK.ref()); RMK.del()
         ?}
      ?};
      _ntyp:=OP.TYP; _ntz:=OP.TZ; _nop:=OP.OP; _nods:=OP.ODS;  _nstan:=OP.STAN;
      _nczymon:=OP.CZY_MON; _nczyods:=OP.CZY_ODS; _nrb:=OP.RB; _nzm:=OP.ZM; han_ref:=OP.HAN;
      OP.put(); exec('pop_all','rozrach',0);
      {? _l_wal>1 & (_ref:=exec('szuk2roz','!fks_roz_dark',_opwal2); _ref<>null) & OP.seek(_ref)
      || OP.TYP:=_ntyp; OP.TZ:=_ntz; OP.OP:=_nop; OP.ODS:=_nods; OP.ZM:=_nzm;
         OP.CZY_MON:=_nczymon; OP.CZY_ODS:=_nczyods; {? ~OP.RB || OP.RB:=_nrb ?}; OP.HAN:=han_ref;
         OP.put(); exec('pop_all','rozrach',0)
      ?};
      {? _l_wal=3 & (_ref:=exec('szuk2roz','!fks_roz_dark',_opwal3); _ref<>null) & OP.seek(_ref)
      || OP.TYP:=_ntyp; OP.TZ:=_ntz; OP.OP:=_nop; OP.ODS:=_nods; OP.ZM:=_nzm;
         OP.CZY_MON:=_nczymon; OP.CZY_ODS:=_nczyods; {? ~OP.RB || OP.RB:=_nrb ?}; OP.HAN:=han_ref;
         OP.put(); exec('pop_all','rozrach',0)
      ?};
      end();
      &han_ref;
      _zmiana:=1
   ?};
   &typ_roz;
   OP.cntx_pop(); SLO.cntx_pop(); OP.r_unlock();
   exec('tab_f_rfresh','slo_filtr',OP)
|| FUN.info('Rozrachunek wykorzystywany przez innego użytkownika.'@)
?}; _zmiana


\op_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Pytanie o zmiane typu
::   WE: _a - jaki jest typ (STRING)
::       _b - liczba powiazanych rozrachunkow wykazujacych saldo po
::            przeciwnej stronie niz biezacy rozrachunek
::       _c - kod 2 waluty
::       _d - kod 3 waluty
::       _e - jaki ma byc typ (STRING)
:;       _f, _g - strony ('Wn' lub 'Ma')
::   WY: 0/1
::  OLD: \op_typ/rozrach.fml
::----------------------------------------------------------------------------------------------------------------------
_wyb:=FUN.ask('Rozrachunek '+OP.SYM+' jest typu '+_a+','+
              '\na jego saldo jest po stronie '+_f+'.'+
              {? _b=2
              || '\nPowiązany rozrachunek w walucie '+{? |_c<>'' || _c || _d ?}+
                 '\nwykazuje saldo po stronie '+_g
              || ''
              ?}+
              {? _b=3
              || '\nPowiązany rozrachunek w walucie '+_d+
                 '\nwykazuje saldo po stronie '+_g
              || ''
              ?}+
              '\nCzy zmienić typ rozrachunku '+
              {? _b=2
              || 'i rozrachunku powiązanego '
              |? _b=3
              || 'i rozrachunków powiązanych '
              || ''
              ?}+'na '+_e+'?');
_wyb


\szuk2roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Szuka powiazanego rozrachunku w podanej walucie dla biezacego rozrachunku
::   WY: Ref rozrachunku (lub null jesli nie ma)
::   WE: _a - ref waluty
::  OLD: \szuk2roz/rozrach.fml
::----------------------------------------------------------------------------------------------------------------------
_refroz:=null;
OP.cntx_psh(); OP.index('KON_O'); OP.prefix();
{? OP.find_key(_a,OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK) || _refroz:=OP.ref() ?};
OP.cntx_pop();
_refroz


\op_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Pytanie o zmiane stanu rozrachunku
::   WE: _a 1-rozliczony (OP.WN=OP.MA) a stan jest nierozliczony, 2 - wpp
::   WY: 0/1
::  OLD: \op_stan/rozrach.fml
::----------------------------------------------------------------------------------------------------------------------
_wyb:={? _a=1
      || FUN.ask('\nSaldo rozrachunku %1 jest równe zero,'
                 '\na jest oznaczony jako nierozliczony.'
                 '\nCzy zmienić oznaczenie rozrachunku na rozliczony ?'@[OP.SYM])
      || FUN.ask('\nSaldo rozrachunku %1 jest różne od zera,'
                 '\na jest oznaczony jako rozliczony.'
                 '\nCzy zmienić oznaczenie rozrachunku na nierozliczony ?'@[OP.SYM])
      ?};
_wyb


\chk_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Sprawdzanie parametrow rozrachunku podczas (akcja Popraw)
::   WE: _a - typ rozrachunku
::  OLD: \chk_op/rozrach.fml
::----------------------------------------------------------------------------------------------------------------------
{? (1+_a)='R' & OP.TYP<>_a & OP.STAN='R'
|| FUN.info('Nie można zmienić typu rozrachunku na '+OP.TYP+' dla rozrachunków rozliczonych.'@);
   OP.TYP:=_a; 'TYP'
|? RACHBANK.KB_1R<>'' & RACHBANK.KB_3R<>''
|| FUN.info('Pola "Subkonto" i "Rachunek bankowy"\n nie mogą być jednocześnie wypełnione.'@); 'KB_1R'
|| {? RACHBANK.KB_1R<>''
   || OP.RB:=RB.getrrban(RACHBANK.KB_1R,REF.FIRMA().SYMBOL+'KH2',#OP.KH)
   |? RACHBANK.KB_3R<>''
   || OP.RB:=RB.getrrban(RACHBANK.KB_3R,'KH',#OP.KH)
   ?};
   ''
?}


\rmk_edyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Edycja rekordu RMK z poziomu przegladania rozrachunkow
::  OLD: \rmk_edyt/rozlicz.fml
::  OLD: \rmk_edyt/rmk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('__KAL','object');
{? -OP.TYP='rmk' | -OP.TYP='rmp'
|| RMK.index('NAZ'); RMK.prefix();
   {? -OP.TYP='rmk'
   || RMK.win_edit('REDOP')
   |? -OP.TYP='rmp'
   || RMK.win_edit('REDOP2')
   ?};
   {? RMK.find_key(OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK)
   || exec('rmk_ed','!fks_roz_dark')
   || RMK.blank();
      RMK.ODD:=OP.ODD;
      RMK.KONP:=OP.AN;
      RMK.SYM:=OP.SYM;
      RMK.SYM_ROK:=OP.SYM_ROK;
      RMK.BL:='N';
      RMK.ILEMIES:=1;
      RMK.DATAOP:=SSTALE.AO().KON+1;
      _wn:=OP.WN$2; _ma:=OP.MA$2;
      SUM.SALWN:=F.SWn(_wn,_ma)$2;
      SUM.SALMA:=F.SMa(_wn,_ma)$2;
      SUM.POMOCWN:=SUM.POMOCMA:=0;
      SUM.SUMWN:=F.SWn(SUM.SALWN,SUM.SALMA)$2;
      SUM.SUMMA:=F.SMa(SUM.SALWN,SUM.SALMA)$2;
      RMK.STRZ:='Wn';
      PAR_WYDR.RMK1:=0;
      PAR_WYDR.RMK2:='P';
      RMK.add();
      {? RMK.edit("exec('spr_rmk','rmk')")
      || RMK.put()
      || exec('kasujrrmk','rmk',RMK.ref()); RMK.del()
      ?}
   ?}
|| FUN.info('Dana pozycja nie ma związku z rozliczeniami międzyokresowymi.'@)
?}


\rmk_ed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Edycja rekordu RMK z poziomu przegladania rozrachunków
::  OLD: \rmk_ed/rozlicz.fml
::  OLD: \rmk_ed/rmk.fml
::----------------------------------------------------------------------------------------------------------------------
_wn:=OP.WN$2; _ma:=OP.MA$2;
SUM.SALWN:=F.SWn(_wn,_ma)$2;
SUM.SALMA:=F.SMa(_wn,_ma)$2;
SUM.POMOCWN:=SUM.POMOCMA:=0;
SUM.SUMWN:=F.SWn(SUM.SALWN,SUM.SALMA)$2;
SUM.SUMMA:=F.SMa(SUM.SALWN,SUM.SALMA)$2;
PAR_WYDR.RMK1:={? RRMK.prefix(RMK.ref()); RRMK.first()
               || PAR_WYDR.RMK2:={? RRMK.RODZ='P' || 'P' || 'W' ?}; 1
               || PAR_WYDR.RMK2:='P'; 0
               ?};
{? RMK.edit("exec('spr_rmk','rmk')") || RMK.put() ?}


\blokuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Formula blokuje rekord RMK do rozliczen. Jesli rozrachunek jest typu R to rekord RMK jest blokowany
::  OLD: \blokuj/rozlicz.fml
::  OLD: \blokuj/rmk.fml
::----------------------------------------------------------------------------------------------------------------------
{? -OP.TYP='rmk' | -OP.TYP='rmp'
|| {? exec('find_rmk','rmk')
   || {? exec('bl_rmk','!fks_roz_dark')
      || RMK.BL:='T';
         RMK.BLOD:=SSTALE.AO().POCZ;
         RMK.BLDO:=SSTALE.AO().KON+1;
         {? -OP.TYP='rmk' || RMK.win_edit('REDOPBL') |? -OP.TYP='rmp' || RMK.win_edit('REDOPBL2') ?} ;
         exec('rmk_ed','!fks_roz_dark')
      ?}
   ?}
|| FUN.info('Dana pozycja nie ma związku z rozliczeniami międzyokresowymi.'@)
?}


\odblokuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Formula zdejmujaca blokade rekordu RMK do rozliczen.
::  OLD: \odblokuj/rozlicz.fml
::  OLD: \odblokuj/rmk.fml
::----------------------------------------------------------------------------------------------------------------------
{? -OP.TYP='rmk' | -OP.TYP='rmp'
|| {? exec('find_rmk','rmk')
   || {? exec('odbl_rmk','!fks_roz_dark')
      || RMK.BL:='N';
         RMK.BLOD:=date(0,0,0);
         RMK.BLDO:=date(0,0,0);
         RMK.put()
      ?}
   ?}
|| FUN.info('Dana pozycja nie ma związku z rozliczenia międzyokresowymi.'@)
?}


\odbl_rmk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Pytajaca formula uzyta w \odblokuj
::  OLD: \odbl_rmk/rozlicz.fml
::  OLD: \odbl_rmk/rmk.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=0;
{? -RMK.BL='t'
|| _a:=FUN.ask('\nPozycja jest zablokowana w zakresie:'+
               '\nod %1'
               '\ndo %2'
               '\nOdblokować ?'@[$RMK.BLOD,$RMK.BLDO])
|| FUN.info('Dana pozycja nie jest zablokowana.'@); _a:=0
?};
_a


\bl_rmk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Pytajaca formula uzyta w \blokuj
::  OLD: \bl_rmk/rozlicz.fml
::  OLD: \bl_rmk/rmk.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=0;
{? -RMK.BL='t'
|| _a:=FUN.ask('Pozycja jest zablokowana w zakresie:'+
               '\nod %1'
               '\ndo %2\nZmienić?'@[$RMK.BLOD,$RMK.BLDO])
|| _a:=1
?};
_a

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:12 47da6fbd49d3df216b968ea1be798585f67e332dd3d8465f67899391425083cd713cbacb0ce28b9b536dc18ad30317999e5114b55a719293a7aa82b8d2f529d74fa1184d884df13502288a159affd7adc445bbaf27249ea9ba4b84a71c974649235fcbf264672937ac7f79f373a741579ddea7368ac716e613fec6551aa55379

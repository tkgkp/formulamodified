:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksp_zawd.fml
:: Utworzony: 07.10.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_KSP_ZAWD - Wydruki sprawozdań definiowalnych
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wydruki sprawozdań definiowalnych - główna formuła czynności FKS_KSP_ZAWD.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::# permissions=FJKS
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Okres bilansowy, required=N
::# kind=WE, symbol=GR_SIK, type=_GR_SIK, name=Sprawozdanie, required=N, fml_val="exec('in_gr_sik','sprfin',_a,_b)"
::# kind=WY, symbol=GR_SIK, type=_GR_SIK, name=Sprawozdanie, required=T
_ar:=SSTALE.AR;
_ao:=SSTALE.AO;
GR_SIK.cntx_psh();
_args:=params_get();
_ok:=1;
{? var_press('_args')>0 & (var_press('in',_args)>0 | var_press('out',_args)>0)
|| {? var_press('out',_args)>0 & var_press('GR_SIK',_args.out)>0
   || GrSikSel:=_args.out.GR_SIK
   |? var_press('in',_args)>0 & var_press('GR_SIK',_args.in)>0
   || GrSikSel:=_args.in.GR_SIK
   ?};
   {? var_press('GrSikSel')>0
   || GR_SIK.prefix();
      {? GR_SIK.seek(GrSikSel)
      || {? GR_SIK.AKC<>'T'
         || _ok:=0;
            FUN.info('Sprawozdanie \'%1\' nie jest zaakceptowane.'@[GR_SIK.SKROT])
         ?}
      ?}
   ?};
   {? var_press('in',_args)>0 & var_press('OKRO_F',_args.in)>0
   || SSTALE.AO:=_args.in.OKRO_F; SSTALE.AR:=SSTALE.AO().ROK
   ?}
?};
{? _ok
|| REF.fld_fml('FIRMA','BEFORE_EDIT',"__F_ref:=''; REF.S_FIRMA=REF.GRUPA");
   SIK.WYL:='T';
   REF.WYKON:=exec('wykon_firmy','konsolidacja',REF.FIRMA);
   params_set(_args);
   _tab:=~~;
   {? var_press('_args')=0 | var_press('RAP_ID',_args)<>type_of('') | _args.RAP_ID=''
   || _tab:=exec('REPTAB','#object');
      _tab.
      add('A. Wydruk uniwersalny sprawozdania'@,'fks_sprfin1',"exec('spr','!fks_ksp_zawd')").
      add('B. Wydruk algorytmów sprawozdania'@,'fks_sprfin2',"exec('spr','!fks_ksp_zawd')")
   ?};
   exec('rep_exec','#b_report','FKS_KSP_ZAWD',,,~(var_press('_args')>0 & var_press('in',_args)>0),,,,_tab);
   {? var_press('_args')>0 & var_press('mp',_args)>0 & var_press('out',_args)>0
   || _out:=_args.out;
      _mp:=_args.mp;
      _out.GR_SIK:=GR_SIK.ref();
      _mp.save(,_out);
      {? var_press('GrSikPrn')>0 || _mp.done() |? ~_mp.pathProc() || _mp.keep() ?}
   ?};
   REF.fld_fml('FIRMA','BEFORE_EDIT',"*")
?};
SSTALE.AR:=_ar;
SSTALE.AO:=_ao;
GR_SIK.cntx_pop();
VAR_DEL.delete('GrSikSel','GrSikPrn')


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='';
_args:=params_get();
_mp:=_args.mp;
_keys:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_out','#b_port'));
_gr_sik:={? var_press('GR_SIK',_in)>0 || _in.GR_SIK |? var_press('GR_SIK',_out)>0 || _out.GR_SIK  || null ?};
_okres:={? var_press('OKRO_F',_in)>0 || _in.OKRO_F || null ?};
GR_SIK.prefix();
OKRO_F.prefix();
{? _gr_sik & GR_SIK.seek(_gr_sik)
|| {? _okres & OKRO_F.seek(_okres)
   || _desc:='Drukuj sprawozdanie: %1 za okres %2 %3'@@[GR_SIK.SKROT,OKRO_F.NAZ,OKRO_F.ROK().NAZ]
   || _desc:='Drukuj sprawozdanie: %1'@@[GR_SIK.SKROT]
   ?}
|| {? _okres & OKRO_F.seek(_okres)
   || _desc:='Drukuj sprawozdania za okres %1 %2'@@[OKRO_F.NAZ,OKRO_F.ROK().NAZ]
   || _desc:='Drukuj sprawozdania'@@
   ?}
?};
_desc


\tab_pom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2008]
:: OPIS: Tworzy pomocnicza tabele tymczasowa dla wydrukow sprawozdan definiowalnych
::   WE: _a - 0 - wartosc dokladna dla biezacej kolumny
::            1 - wartosc dokladna dla wszystkich kolumn sprawozdania
::            2 - wartosc zaokraglona dla wszystkich kolumn sprawozdania
::       [_b] - opcjonalny na potrzeby sprawozdan uwzgledniajacych dane rok wstecz
::              (przekazywany argument OSPR.ref lub brak argumentu)
::              formula z 2 parametrem wywolywana tylko dla sprawozdan z jedna zdefiniowana kolumna
::  OLD: \tab_pom/skid_spr.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TAB_WAR');
{? var_pres('_b')<0
|| TAB_WAR:=tab_tmp(2,'REF','INTEGER','ref',
                      'REF_KOL','INTEGER','ref kol',
                      'WAR','REAL','Wartosc')
|| TAB_WAR:=tab_tmp(2,'REF','INTEGER','ref',
                      'REF_KOL','INTEGER','ref kol',
                      'WAR','REAL','Wartosc',
                      'WAR2','REAL','Wartosc rok wstecz')
?};
OKRESY.OKR_RAP();
DEFW.index('KOLEJ2'); DEFW.prefix(GR_SIK.ref());
GR_KOL.index('LP'); GR_KOL.prefix(GR_SIK.ref());
{? DEFW.first()
|| {! |?
      TAB_WAR.REF:=#DEFW.ref();
      {? _a=0 | GR_KOL.first()
      || {! |?
            TAB_WAR.REF_KOL:=GR_KOL.ref();
            TAB_WAR.cntx_psh(); _w:=exec('tab_war','!fks_ksp_zawd',_a=2); TAB_WAR.cntx_pop();
            TAB_WAR.WAR:=_w;
            {? var_pres('_b')>0
            || WARTOSCI.cntx_psh();
               OSPR.cntx_psh();
               OSPR.prefix(REF.FIRMA);
               OSPR.seek(_b);
               WARTOSCI.use('wsik_'+OSPR.ROK().KOD);
               TAB_WAR.cntx_psh(); _w:=exec('tab_war','!fks_ksp_zawd',_a=2,1); TAB_WAR.cntx_pop();
               TAB_WAR.WAR2:=_w;
               OSPR.cntx_pop();
               WARTOSCI.cntx_pop()
            ?};
            TAB_WAR.add();
            _a>0 & GR_KOL.next()
         !}
      ?};
      DEFW.next()
   !}
?};
1


\tab_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2008]
:: OPIS: Wyznacza wartosc wiersza w walucie
::   WE: _a - czy wartosc zaokraglona (1) czy dokladna (0)
::       [_b]- opcjonalny w celu wyliczenia wartosci rok wstecz ( 0 - aktualny, <>0 - poprzedni)
::             wczesniej wymagane ustawienie odpowiedniej maski tab. WARTOSCI oraz bufora tab. OSPR
::   WY: wartosc biezacego wiersza biezacego sprawozdania i kolumny
::  OLD: \tab_war/skid_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<0 || _b:=0 ?};
_kurs:=__KURS;
{? DEFW.ALGORYTM='P'
|| 0
|? DEFW.ALGORYTM='S'
|| _wynik:=0;
   DEFA.index('LP'); DEFA.prefix(DEFW.ref,GR_KOL.ref());
   {? DEFA.first()
   || {! |?
         {? TAB_WAR.find_key(#DEFA.ARGUMENT,#DEFA.ARG_KOL)
         || {? _b=0 || _wynik+=TAB_WAR.WAR*DEFA.WSP || _wynik+=TAB_WAR.WAR2*DEFA.WSP ?}
         || WARTOSCI.index('KOD'); WARTOSCI.prefix(REF.WYKON,DEFA.ARGUMENT,OSPR.ref(),odd,'N','',DEFA.ARG_KOL);
            {? WARTOSCI.first()
            || _wynik+=({? _a || WARTOSCI.WZAOKR*DEFA.WSP || WARTOSCI.WARTOSC*DEFA.WSP ?}/_kurs)${? _a || DEFW.DOKL || 2 ?}
            ?}
         ?};
         DEFA.next()
      !};
      _wynik
   || 0
   ?}
|? DEFW.ALGORYTM='I'
|| _wynik:=1;
   DEFA.index('LP'); DEFA.prefix(DEFW.ref,GR_KOL.ref());
   {? DEFA.first()
   || {! |?
         {? TAB_WAR.find_key(#DEFA.ARGUMENT,#DEFA.ARG_KOL)
         || {? _b=0 || _wynik*=TAB_WAR.WAR*DEFA.WSP || _wynik*=TAB_WAR.WAR2*DEFA.WSP ?}
         || WARTOSCI.index('KOD'); WARTOSCI.prefix(REF.WYKON,DEFA.ARGUMENT,OSPR.ref(),odd,'N','',DEFA.ARG_KOL);
            {? WARTOSCI.first()
            || _wynik*=({? _a || WARTOSCI.WZAOKR*DEFA.WSP || WARTOSCI.WARTOSC*DEFA.WSP ?}/_kurs)${? _a || DEFW.DOKL || 2 ?}
            ?}
         ?};
         DEFA.next()
      !};
      _wynik
   || 0
   ?}
|? DEFW.ALGORYTM='W'
|| _l:=_m:=0;
   DEFA.index('LP'); DEFA.prefix(DEFW.ref,GR_KOL.ref());
   {? DEFA.first()
   || {!
      |? {? TAB_WAR.find_key(#DEFA.ARGUMENT,#DEFA.ARG_KOL)
         || {? _b=0
            || {? DEFA.LM='L' || _l+=TAB_WAR.WAR*DEFA.WSP || _m+=TAB_WAR.WAR*DEFA.WSP ?}
            || {? DEFA.LM='L' || _l+=TAB_WAR.WAR2*DEFA.WSP || _m+=TAB_WAR.WAR2*DEFA.WSP ?}
            ?}
         || WARTOSCI.index('KOD'); WARTOSCI.prefix(REF.WYKON,DEFA.ARGUMENT,OSPR.ref(),odd,'N','',DEFA.ARG_KOL);
            {? WARTOSCI.first()
            || {? DEFA.LM='L'
               || _l+=({? _a || WARTOSCI.WZAOKR*DEFA.WSP || WARTOSCI.WARTOSC*DEFA.WSP ?}/_kurs)${? _a || DEFW.DOKL || 2 ?}
               || _m+=({? _a || WARTOSCI.WZAOKR*DEFA.WSP || WARTOSCI.WARTOSC*DEFA.WSP ?}/_kurs)${? _a || DEFW.DOKL || 2 ?}
               ?}
            ?}
         ?};
         DEFA.next()
      !};
      _wynik:={? _m=0 || 0 || _l/_m ?}${? _a || DEFW.DOKL || 2 ?}
   || 0
   ?}
|? DEFW.ALGORYTM='F'
|| ALG_PAR.index('ALG_PAR1'); ALG_PAR.prefix(REF.FIRMA,DEFW.ref(),GR_KOL.ref(),OSPR.ROK().NAZ);
   W_ALGPAR.index('W_ALGPAR'); W_ALGPAR.prefix();
   _wynik:=0;
   {? ALG_PAR.first()
   || {! |?
         {? W_ALGPAR.find_key(ALG_PAR.ref(),OSPR.ref(),odd)
         || _wynik+=W_ALGPAR.WAR*ALG_PAR.WSP
         ?};
         ALG_PAR.next()
      !};
      {? _wynik
      || _wynik/=_kurs;
         _wynik:={? _a || (_wynik/DEFW.ZAOKR)$DEFW.DOKL || _wynik$2 ?}
      || WARTOSCI.index('KOD'); WARTOSCI.prefix(REF.WYKON,DEFW.ref(),OSPR.ref(),odd,'N','',GR_KOL.ref());
         {? WARTOSCI.first()
         || _wynik:=({? _a || WARTOSCI.WZAOKR*ALG_PAR.WSP || WARTOSCI.WARTOSC*ALG_PAR.WSP ?}/_kurs)${? _a || DEFW.DOKL || 2 ?}
         ?}
      ?}
   ?};
   _wynik
|| 0
?}


\tab_pom1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Tworzy pomocnicza tabele tymczasowa dla wydrukow sprawozdan definiowalnych
::   WE: _a - 0 - wartosc dokladna dla biezacej kolumny
::            1 - wartosc dokladna dla wszystkich kolumn sprawozdania
::            2 - wartosc zaokraglona dla wszystkich kolumn sprawozdania
::       _b - opcjonalny na potrzeby sprawozdan uwzgledniajacych dane rok wstecz
::              (przekazywany argument OSPR.ref lub brak argumentu)
::  OLD: \tab_pom1/skid_spr.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TAB_WAR1');
TAB_WAR1:=tab_tmp(2,
   'REF','INTEGER','ref',
   'KOD','STRING[35]','kod',
   'WAR','REAL','Wartosc',
   'WAR2','REAL','Wartosc',
   'TR','STRING[80]','Wartosc'
);
OKRESY.OKR_RAP();
_ospr1:=OSPR.ref();
_rok1:=OSPR.ROK().KOD;
OSPR.cntx_psh(); OSPR.index('NAZWA'); OSPR.prefix(REF.FIRMA);
{? OSPR.seek(_b)
|| _ospr2:=OSPR.ref();
   _rok2:=OSPR.ROK().KOD
|| _ospr2:=null;
   _rok2:='__'
?};
OSPR.cntx_pop();
exec('tab_pom2','!fks_ksp_zawd',_ospr1,_rok1,1);
exec('tab_pom2','!fks_ksp_zawd',_ospr2,_rok2,2)


\tab_pom2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Tworzy pomocnicza tabele tymczasowa dla wydrukow sprawozdan definiowalnych
::   WE: _a - okres
::       _b - kod roku
::       _c - typ wartosci
::  OLD: \tab_pom2/skid_spr.fml
::----------------------------------------------------------------------------------------------------------------------
DEFW.index('KOLEJ2'); DEFW.prefix(GR_SIK.ref());
GR_KOL.index('LP'); GR_KOL.prefix(GR_SIK.ref());
{? DEFW.first() & GR_KOL.first()
|| WARTOSCI.cntx_psh();
   WARTOSCI.use('wsik_'+_b);
   {!
   |? WARTOSCI.index('OKRES'); WARTOSCI.prefix(_a,REF.WYKON,DEFW.ref(),GR_KOL.ref(),odd,'T');
      {? WARTOSCI.first()
      || {!
         |? {? TAB_WAR1.find_key(#DEFW.ref(),WARTOSCI.KOD,)
            || {? _c=1
               || TAB_WAR1.WAR:={? pyt=1 || WARTOSCI.WARTOSC || WARTOSCI.WZAOKR ?}
               || TAB_WAR1.WAR2:={? pyt=1 || WARTOSCI.WARTOSC || WARTOSCI.WZAOKR ?}
               ?};
               TAB_WAR1.put()
            || TAB_WAR1.REF:=#DEFW.ref();
               TAB_WAR1.KOD:=WARTOSCI.KOD;
               TAB_WAR1.WAR:=TAB_WAR1.WAR2:=0;
               {? _c=1
               || TAB_WAR1.WAR:={? pyt=1 || WARTOSCI.WARTOSC || WARTOSCI.WZAOKR ?}
               || TAB_WAR1.WAR2:={? pyt=1 || WARTOSCI.WARTOSC || WARTOSCI.WZAOKR ?}
               ?};
               TAB_WAR1.TR:=WARTOSCI.TR;
               TAB_WAR1.add()
            ?};
            WARTOSCI.next()
         !}
      ?};
      DEFW.next()
   !};
   WARTOSCI.cntx_pop()
?}


\spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia sprawozdanie
::   WE: _a - nr sprawozdania
::----------------------------------------------------------------------------------------------------------------------
{? var_press('GrSikSel')<=0
|| _o:=GR_SIK.mk_sel('Sprawozdania'@,'P',,'#grsiksel',,,,,'U');
   GR_SIK.win_fld(_o,,'SKROT',,,,,,'Kod'@);
   GR_SIK.win_fld(_o,,'NAZWA',,,,,,'Nazwa'@);
   GR_SIK.win_fld(_o,,'ZAOKR',,,,,,'Wskaźnik zaokrąglenia'@);
   GR_SIK.win_fld(_o,,'DOKL',,,,,,'Dokładność'@);
   GR_SIK.win_fld(_o,,'AKC',,,,,,'Akceptacja'@,,,2,,"'T'","'N'");
   GR_SIK.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
   GR_SIK.win_sel(_o);
   exec('f_set_gr_sik','sprfin','T');
   _ok:={? GR_SIK.select()
        || GrSikSel:=GR_SIK.ref();
           1
        ?};
   GR_SIK.f_clear();
   _ok
|| 1
?}


\aepardr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: Po redakcji SIK.PARWPOD i SIK.PARWINN
::  OLD: \aepardr/skid_sil.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~SIK.PARWPOD & ~SIK.PARWINN || SIK.PARDRUK:=0 ?}; 1


\bepdruk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: Przed redakcja SIK.PARDRUK
::  OLD: \bepdruk/skid_sil.fml
::----------------------------------------------------------------------------------------------------------------------
SIK.PARWPOD | SIK.PARWINN


\ae_srok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Po redakcji pola ROK i ROK2 zmiennej SSTALE
::  OLD: \ae_srok/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
_2:={? cur_afld()='ROK2' || '2' || '' ?};
($('{? SSTALE.ROK'+_2+'<>SSTALE.OSPR'+_2+'().OKRES_OD().ROK || SSTALE.OSPR'+_2+':=null ?}'))();
1


\be_sik_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Przed redakcji pola SIK.WIDOK
::  OLD: \be_sik_w/plan_spr.fml
::----------------------------------------------------------------------------------------------------------------------
__WV_TYP:='W';
WER_VIEW.win_edit();
WER_VIEW.win_sel('WERW');
WER_VIEW.win_dict('WERW');
{? SIK.win_edit('?')='PLAN_ANA'
|| SSTALE.WARIANT<100 | UNPAR.P16='A'
|| 1
?}


\be_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przed redakcja pola OSPR i OSPR2 zmiennej SSTALE
::  OLD: \be_ospr/tree_spr.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()+1='2' || SSTALE.ROK2() || SSTALE.ROK() ?}; 1


\set_par_win1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Ustawia okno redakcji parametrów wydruku
::----------------------------------------------------------------------------------------------------------------------
_red:=ROZNE.mk_edit('Parametry wydruku'@,,'grsikparwydr1');
{? REF.S_FIRMA=REF.GRUPA
|| ROZNE.win_ewin(_red,,'SPR_DEFF')
?};
{? var_press('Konsolid')>0
|| ROZNE.win_ewin(_red,,'SPR_DEFK')
?};
{? _a=1
|| ROZNE.win_ewin(_red,,'SPR_DEF1')
|| ROZNE.win_ewin(_red,,'SPR_DEF')
?};
_btn:=ROZNE.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['OK'],"'key:F2'");
ROZNE.btn_eopt(_red,_btn,'tooltip='+exec('help_red_ok','#window','P'));
_an:=ROZNE.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'],'key:Esc');
ROZNE.btn_eopt(_red,_an,'tooltip='+exec('help_red_esc','#window','A'));
ROZNE.win_edit(_red);
~~


\set_par_win2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Ustawia okno redakcji parametrów wydruku
::----------------------------------------------------------------------------------------------------------------------
_red:=SIK.mk_edit('Parametry wydruku'@,,'grsikparwydr2');
{? REF.S_FIRMA=REF.GRUPA
|| SIK.win_ewin(_red,ROZNE,'SPR_DEFF')
?};
{? var_press('Konsolid')>0
|| SIK.win_ewin(_red,ROZNE,'SPR_DEFK')
?};
SIK.win_ewin(_red,,'ZAKRES1');
_btn:=SIK.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['OK'],"'key:F2'");
SIK.btn_eopt(_red,_btn,'tooltip='+exec('help_red_ok','#window','P'));
_an:=SIK.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'],'key:Esc');
SIK.btn_eopt(_red,_an,'tooltip='+exec('help_red_esc','#window','A'));
SIK.win_edit(_red);
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 ad29b74bf34eef1af8be2aa8e7d982128276f5906d5d7ee390240d155fb6fdc2ae48591f195cee84cff09acb907d9dc2e7404e4f637fd1a3f217b274308fcc9f4541e7ee0bd26e713018858d0e833ba06067a313e2c7cde4872b9b621887267dfdd359fcf83db29960025e3b0fbe11579d3a7a89152c67db73d87b43bca03517

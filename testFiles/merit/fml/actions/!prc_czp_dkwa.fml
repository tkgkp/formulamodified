:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !prc_czp_dkwa.fml
:: Utworzony: 26.09.2017
:: Autor: areKc
::======================================================================================================================
:: Zawartość: Formuły czynności: PRC_CZP_DKWA - Kwalifikacja czasu pracy.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Kwalifikacja czasu pracy dla współpracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL
::# access=exec('run_cond_p','pkd')
::
::# kind=WE, symbol=P, type=_P, name=Wskazanie współpracownika, required=T, keyref=T
::# kind=WE, symbol=A_OKR, type=_A_OKR, name=Wskazanie na okres rozliczeniowy, required=T, keyref=T
::
_par:=params_get();
_mp:=_par.mp;
_in:=_par.in;
_akcja:=_mp.akcja();
_result:='';

_uid_P:=exec('ref2uid','#table',_in.P);
_uid_OKR:=exec('ref2uid','#table',_in.A_OKR);
{? _uid_P=''
|| _result:=exec('error','!prc_czp_dkwa','Nie znaleziono współpracownika.'@)
|? _uid_OKR=''
|| _result:=exec('error','!prc_czp_dkwa','Nie znaleziono okresu rozliczeniowego.'@)
|? _mp.isMicro()
|| {? _akcja='START'
::    Wejście do okienka w ramach obszaru roboczego - uruchomienie procesu i pozostawienie go na liście zadań.
   || _mp.keyRef(_uid_P);
      _mp.keyRef(_uid_OKR);
      _mp.keep()
   |? _akcja='STOP'
::    Wyjście z okienka w ramach obszaru roboczego - anulowanie procesu.
   || _mp.delRef(_uid_P);
      _mp.delRef(_uid_OKR);
      _mp.cancel()
   |? _akcja<>''
   || _result:='Czynność %1 nie obsługuje akcji %2.'@ [_mp.buf_act.UID,_akcja]
   ?}
|| {? _akcja='ZAKOŃCZ'
   || _mp.done()
   |? _mp.pathTodo()
   || _value:=exec('kwalifikacja','!prc_czp_dkwa',_in.P,_in.A_OKR);
      {? type_of(_value)=type_of('')
      || _result:=_value
      |? _value
      || _mp.done()
      || _mp.keep()
      ?}
   ?}
?};
::  Obsługa błędów
{? _result<>''
|| _mp.error(_result);
   FUN.emsg(_result)
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Kwalifikacja czasu pracy dla współpracownika.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_a_okr:=obj_new(
:: Czy tablica zawiera dane o okresie rozliczeniowym [T/N]
   'ZAW_DANE',
:: Nazwa okresu rozliczeniowego
   'A_OKRN',
:: Data od
   'OD',
:: Data do
   'DO'
);
:: inicjalizacja tablicy
{! _ii..obj_len(_a_okr) |! _a_okr[_ii]:='' !};

A_OKR.cntx_psh();
A_OKRN.cntx_psh();
{? A_OKR.seek(_mp.load(exec('kind_in','#b_port')).A_OKR,,1)
|| _a_okr.ZAW_DANE:='T';
   _a_okr.A_OKRN:=A_OKR.NAZ().NAZ;
   _a_okr.OD:=A_OKR.OD$1;
   _a_okr.DO:=A_OKR.DO$1
|| _a_okr.ZAW_DANE:='N'
?};
A_OKRN.cntx_pop();
A_OKR.cntx_pop();

_tab:=exec('desc','pracownik',_mp);
{? _tab.ZAW_DANE='T'
|| {? _a_okr.ZAW_DANE='T'
   || {? _tab.OBCY='T'
      || 'Wykonaj kwalifikację czasu pracy w okresie %1 %2 %3 współpracownika: %4 %5: Paszport - %6, Numer teczki - %7, Identyfikator - %8'@@
            [_a_okr.A_OKRN,_a_okr.OD,_a_okr.DO,_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
      |? +_tab.PESEL
      || 'Wykonaj kwalifikację czasu pracy w okresie %1 %2 %3 współpracownika: %4 %5: PESEL - %6, Numer teczki - %7, Identyfikator - %8'@@
            [_a_okr.A_OKRN,_a_okr.OD,_a_okr.DO,_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
      || 'Wykonaj kwalifikację czasu pracy w okresie %1 %2 %3 współpracownika: %4 %5: Data urodzenia - %6, Numer teczki - %7, Identyfikator - %8'@@
            [_a_okr.A_OKRN,_a_okr.OD,_a_okr.DO,_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
      ?}
   || {? _tab.OBCY='T'
      || 'Wykonaj kwalifikację czasu pracy w okresie ? współpracownika: %1 %2: Paszport - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PASZPORT,_tab.T,_tab.IP]
      |? +_tab.PESEL
      || 'Wykonaj kwalifikację czasu pracy w okresie ? współpracownika: %1 %2: PESEL - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.PESEL,_tab.T,_tab.IP]
      || 'Wykonaj kwalifikację czasu pracy w okresie ? współpracownika: %1 %2: Data urodzenia - %3, Numer teczki - %4, Identyfikator - %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.UR_DATA,_tab.T,_tab.IP]
      ?}
   ?}
|| 'Wykonaj kwalifikację czasu pracy w okresie'@@
?}


\error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Komunikat o błędzie.
::   WE: _a - [STRING] Informacja o rodzaju błędu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'%1\n%2'['Wykonanie kwalifikacji czasu pracy nie jest możliwe.'@,_a]


\opis_okresu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Opis wybranego okresu rozliczeniowego.
::   WE: _a - [REFERENCE] Wskazanie na wybrany okres rozliczeniowy.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
A_OKR.cntx_psh();
A_OKRN.cntx_psh();
_okr:=
   {? A_OKR.seek(_a,,1)
   || '%1 %2 %3'@[A_OKR.NAZ().NAZ,A_OKR.OD$1,A_OKR.DO$1]
   || '?'
   ?};
A_OKRN.cntx_pop();
A_OKR.cntx_pop();
_okr


\kwalifikacja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Obsługa czynności wywołanej z listy zadań.
::   WE: _a - [REFERENCE] Wskazanie współpracownika.
::       _b - [REFERENCE] Wskazanie na okres rozliczeniowy.
::   WY: Komunikat o błędzie lub informacja o wyjściu z okna poprzez wywołanie sel_exit() - czyli "Zakończ".
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
P.cntx_psh();
{? type_of(_a)=type_of(null()) & _a<>null() & P.seek(_a,,1)
|| A_OKR.cntx_psh();
   OSOBA.cntx_psh();
   P.OSOBA();
   {? type_of(_b)=type_of(null()) & _b<>null() & A_OKR.seek(_b,,1)
   || A_OKRP.cntx_psh();
      A_OKRP.index('A_OKRPR');
      A_OKRP.prefix(A_OKR.ref(),P.ref());
      {? A_OKRP.first()
      || exec('oblicz','okres',0)
      || _result:=exec('error','!prc_czp_dkwa','Nie znaleziono okresu rozliczeniowego współpracownika.'@)
      ?};
      A_OKRP.cntx_pop()
   || _result:=exec('error','!prc_czp_dkwa','Nie znaleziono okresu rozliczeniowego'@)
   ?};
   OSOBA.cntx_pop();
   A_OKR.cntx_pop()
|| _result:=exec('error','!prc_czp_dkwa','Nie znaleziono współpracownika.'@)
?};
P.cntx_pop();
_result


\sql_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Dodatkowe tabele do filtra ograniczającego widok wyświetlanych danych.
::   WE: _a - [STRING] Informacja o rodzaju błędu.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'left join A_OKRP using (A_OKRP.P,P.REFERENCE)'


\sql_where
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Warunek ograniczenia widoku wyświetlanych danych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'"P".REFERENCE in (select "A_OKRP".P from "A_OKRP" where "A_OKRP".OKR=\''+$params_get().in.p01+'\' order by 1)'


\hdr_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [17.42]
:: OPIS: Tytuł okna wyboru współpracowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_okr:=exec('opis_okresu','!prc_czp_dkwa',params_get().in.p01);
'W okresie rozliczeniowym: %1'@[_okr]

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:20 afe055b0d2f589de4231860803d2af4d8501f218bc8558757e089dde853ae8b3ab034101c273120e20bb529d56b3aa7ed763acfcf0d60cf18d346bf2ff92039aab15021672208e81f5b14668e4a9df8f6d5edf6281c0865a9e1a632198466c5a30e4910c2085b606328d391d013bcad7e7b97fa11943e7a4f85b63ee6a599769

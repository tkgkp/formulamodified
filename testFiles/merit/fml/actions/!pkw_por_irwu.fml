:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkw_por_irwu.fml
:: Utworzony: 27.06.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Obsługa czynności webTerm-owej PKW_POR_IRWU - Rejestracja wniosku o urlop.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja wniosku o urlop (webTerm) - obsługa procesowości główna formuła czynności.
::       Opis statusów wniosków znajduje się w procedurze \status/wnioski_urlopowe.fml.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::  Parametr nie jest obowiązkowy, aby czynność mogła być startowa.
::# kind=WE, symbol=P, type=_P, name=Wskazanie pracownika, required=N, keyref=N
::
::# kind=WEW, symbol=NWU, type=_NWU, name=Wskazanie wniosku, required=N
::
::# kind=WY, symbol=NWU, type=_NWU, name=Wskazanie wniosku, required=N
::
params_exec('nwu_main','wnioski_urlopowe')


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja wniosku o urlop (webTerm) - główna formuła czynności.
::   WE:  _a  [STRING]    - Miejsce wywołania (Proc, Todo, Area).
::       [_b] [REFERENCE] - Wskazanie na instancję procesu (dla Proc i Todo).
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='Area' | _a='Proc' | _a='Todo')
|| _path:=_a
|| return()
?};

_fcontext:=
   "  _context:=obj_new('NWU','BTN');
      _context.NWU:=_a;
      _context.BTN:=_b;
   ";

{? _path='Area'
:: Uruchomienie z obszaru roboczego. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
|| exec('run_from_area','wnioski_urlopowe')

|? _path='Todo'
:: Uruchomienie z listy zadań (czynność w procesie, wywołana z parametrem - wskazaniem pracownika).  - - - - - - - - - -
|| {? var_pres('_b')=type_of(null()) & _b<>null() & ref_tab(_b)=BI_PREL & exec('env_wt','b_proces') & exec('init','pkw')
   || _bi_prel:=_b
   || return()
   ?};

   exec('run_from_todo','wnioski_urlopowe',_bi_prel)

|? _path='Proc'
:: Uruchomienie z pulpitu (czynność startowa).   - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
|| {? var_pres('_b')=type_of(null()) & _b<>null() & ref_tab(_b)=B_PREL & exec('env_wt','b_proces') & exec('init','pkw')
   || _b_prel:=_b
   || return()
   ?};

:: Zakładamy, że czynność uruchamiana z pulpitu zawsze wykonywana jest na rzecz użytkownika, a nie jego podwładnego
:: (bez względu na ustawienia w P_WEB_CX). Dlatego też obsługa jest wzbogacona o ewentualny wybór kontekstu, gdyby
:: okazało się, że z użytkownikiem związanych jest kilka rekordów (umów) typu W.

   _fml_b:="exec('p_web_cx_set_prefix','p_web',,'RODZAJ','W');
::       jeżeli ścieżka to start procesu i mamy tylko jeden rekord to nie pokazujemy
::       okna wyboru z przebiegami pracownika
         {? P_WEB_CX.first() & (P_WEB_CX.size()=1)
         || exec('p_web_wer_wybierz','p_web',1);
            0
         || 1
         ?}";

   exec('run_from_proc','wnioski_urlopowe','PKW_POR_IRWU',_b_prel,_fml_b)

?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Rejestracja wniosku o urlop (webTerm) - formuła opisu zadania.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=params_exec('desc','wnioski_urlopowe',1);
{? _tab.ZAW_DANE='T'
|| 'Zarejestruj wniosek o urlop: %1 %2, Numer teczki - %3, Jednostka - %4, Stanowisko %5'@@
      [_tab.NAZWISKO,_tab.PIERWSZE,_tab.T,_tab.UD_SKL,_tab.STN]
|| 'Zarejestruj wniosek o urlop'@@
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 2066c25a8c86b9504be69f4c3c38eb520f1e84878564e8d3c44671f0e68432c9d1f95c7b84e5a1e708d038c0438cd461722a1428864c495bf3b47bd7f77962f1a8b62f424f5cda480afcc8e2f7242effa8ca5c4ae347121b2a84166449c342621eff02f948f4299a785d6169ed8630fc3b65a71485eee5012d12af838679cce2

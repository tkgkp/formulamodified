:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obe_fdl_ebkp.fml
:: Utworzony: 23.06.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności OBE_FDL_EBKP - Akceptacja delegacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Akceptacja delegacji - główna formuła czynności OBE_FDL_EBKP
::----------------------------------------------------------------------------------------------------------------------
::# parses=exec('parses_edokum','obiegi')
::# access=exec('upr_oper','obiegi_akc')
::# properties=TODOTRIG,~RELEASE

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na delegację w obiegu, required=T, keyref=T
::# kind=WE, symbol=USERS, type=_USERS, name=Użytkownik, required=N
::# kind=WE, symbol=B_PREL, type=STRING, name=Poprzedni element w procesie, required=N
::# kind=WE, symbol=B_PRELS, type=STRING, name=Symbol poprzedniego elementu w procesie, required=N
::# kind=WE, symbol=STATUS, type=STRING, name=Status, required=N

::# kind=WE, symbol=FORM_PRZ, type=STRING, name=Formuła wykonywana przy kończeniu akceptacji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_WAL, type=STRING, name=Formuła walidacyjna przy kończeniu akceptacji, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORM_ODR, type=STRING, name=Formuła wykonywana przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"
::# kind=WE, symbol=FORMWODR, type=STRING, name=Formuła walidacyjna przy kończeniu odrzucania, required=N, fml_val="exec('form_etap','obiegi','E',_a)", fml_exp="exec('sch_form_fml_exp','obiegi2',_a)"

::# kind=WE, symbol=ED_PODZ, type=STRING, name=Edycja podziałów controllingowych, required=N, fml_val="exec('czy_ed_podz','obiegi')"
::# kind=WE, symbol=ED_PROJ, type=STRING, name=Edycja projektów, required=N, fml_val="exec('czy_ed_proj','obiegi')"
::# kind=WE, symbol=ED_INND, type=STRING, name=Edycja informacji dodatkowych, required=N, fml_val="exec('czy_inn_dan','obiegi')"
::# kind=WE, symbol=ED_PRZEJ, type=STRING, name=Edycja przejazdów, required=N, fml_val="exec('czy_ed_przej','obiegi')"
::# kind=WE, symbol=ED_DIET, type=STRING, name=Edycja diet, required=N, fml_val="exec('czy_ed_diet','obiegi')"
::# kind=WE, symbol=ED_WYD, type=STRING, name=Edycja wydatków, required=N, fml_val="exec('czy_ed_wyd','obiegi')"
::# kind=WE, symbol=UWODRWYM, type=STRING, name=Wymagane wypełnienie uwag przy odrzuceniu, required=N, fml_val="exec('czy_wymoduw','obiegi_akc')"
::# kind=WE, symbol=KONT_BUD, type=STRING, name=Kontrola budżetu, required=N, fml_val="exec('kont_bud','obiegi',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na delegację w obiegu, required=N
::# kind=WY, symbol=USERS, type=_USERS, name=Użytkownik, required=N
::# kind=WY, symbol=B_PREL, type=STRING, name=Element w procesie, required=N
::# kind=WY, symbol=B_PRELS, type=STRING, name=Symbol elementu w procesie, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status, required=N
::# kind=WY, symbol=DEKR, type=STRING, name=Dekretacja, required=N
::# kind=WY, symbol=EM_NAD, type=STRING, name=Adres e-mail użytkownika akceptujacego (nadawca), required=N
::# kind=WY, symbol=EM_ODB, type=MEMO, name=Adres e-mail użytkownika akceptującego (odbiorca), required=N
::# kind=WY, symbol=EM_NAG, type=STRING, name=Nagłówek wiadomości e-mail, required=N
::# kind=WY, symbol=EM_TR, type=MEMO, name=Treść wiadomości e-mail, required=N
::# kind=WY, symbol=BUDZ_COND, type=NUMBER, name=Budżet przekroczony warunkowo?, required=N

:: PARAMETRY WEW:
::# kind=WEW, symbol=USERSN, type=_USERS, name=Użytkownik, required=N

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();

{? ~exec('is_act','#b__box','OBE_FDL_EBKP')
|| FUN.info('Nie znaleziono powiązanego, zaakceptowanego procesu obiegu delegacji.'@);
   _mp.cancel(); return(~~)
|? _mp.isMicro()
|| FUN.info('Delegacja nie jest w trakcie akceptacji.'@); return(~~)
?};

{? _akcja='Portal'
|| {? var_pres('EDOKUM',_we)>0 & _we.EDOKUM
   || EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_we.EDOKUM));
      {? EDOKUM.seek(_we.EDOKUM)
      || params_exec('ustaw_bprel','obiegi');
         _wy.EDOKUM:=EDOKUM.ref();
         _wy.DEKR:=EDOKUM.TYP().DEKR;
         _wy.STATUS:={? EDOKUM.STATUS='D' || 'O' || 'A' ?};
         _wy.EM_NAD:=_wy.EM_ODB:=OPERATOR.USER().EMAIL;
         _wy.EM_NAG:={? EDOKUM.STATUS='Y' || 'Zaakceptowano' || 'Odrzucono' ?}+
                     ' delegację: '+EDOKUM.ID+', data operacji '+$EDOKUM.DOP;
         _wy.B_PREL:=OBIEGI.B_PREL;
         _wy.B_PRELS:=OBIEGI.B_PRELS;

         {? EDOKUM.STATUS='D'
         || {? var_pres('FORM_ODR',_we)>0 & _we.FORM_ODR<>''
            || _fml:=_we.FORM_ODR;
               {? _fml<>''
               || ($_fml)()
               ?}
            ?}
         || {? var_pres('FORM_PRZ',_we)>0 & _we.FORM_PRZ<>''
            || _fml:=_we.FORM_PRZ;
               {? _fml<>''
               || ($_fml)()
               ?}
            ?}
         ?};
         {? var_pres('_args')>0 & var_pres('context',_args)>0 & var_pres('DONE',_args.context)>0
         || _args.context.B_PREL:=OBIEGI.B_PREL;
            _args.context.DONE:=1
         ?};
         _mp.save(,_wy);
         _mp.keep();
         _mp.done()
      ?};
      EDOKUM.cntx_pop()
   ?};
   return(~~)
?};

zak_akc_ed:=0;
budz_cond:=0;
OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
{? _we.EDOKUM & EDOKUM.seek(_we.EDOKUM,,1)
|| params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
:: sprawdzenie z poziomu webterma
   {? _akcja='Akceptuj2'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('akceptuj2','obiegi_akc','A');
      {? zak_akc_ed=1 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'A');
         no_bprel:=_nobp:=exec('no_bprel','obiegi_akc',_wy.B_PREL);
         _mp.save(,_wy);
         _mp.done();
         no_bprel:=_nobp
      ?}
   |? _akcja='Odrzuc2'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('odrzuc2','obiegi_akc');
      {? zak_akc_ed=2 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'O');
         _mp.save(,_wy);
         _mp.done()
      ?}
   |? _akcja='chkWT'
   || {? app_info('web_sesid')<>''
      || web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                               'popetap',_we.B_PRELS
                               ))
      ?};
      params_get().context.SPR_WT:=1
:: akceptacja z pulpitu
   |? _akcja='Akceptuj'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('akceptuj','obiegi_akc','A');
      {? zak_akc_ed=1 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'A');
         no_bprel:=_nobp:=exec('no_bprel','obiegi_akc',_wy.B_PREL);
         _mp.save(,_wy);
         _mp.done();
         no_bprel:=_nobp
      ?}
   |? _akcja='Odrzuć'
   || params_get().context.EDOKUM;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('odrzuc','obiegi_akc');
      {? zak_akc_ed=2 & EDOKUM.get()
      || exec('ustaw_out','obiegi_akc',_wy,'O');
         _mp.save(,_wy);
         _mp.done()
      ?}
   ?}
?};
VAR_DEL.delete('zak_akc_ed','zawin','budz_cond');
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zaakceptuj delegację w obiegu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_stat:=exec('getStatus','#bi_prel',_mp.bi_prel);
_set:=(_stat=exec('OCZEKUJACA','#bi_stat') | _stat=exec('URUCHOMIONA','#bi_stat'));
params_exec('ustaw_bprel','obiegi');
{? var_pres('EDOKUM',_we)
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
         _desc:='Zaakceptuj delegację w obiegu: %1, data polecenia %2, delegowany %3 %4'@@[EDOKUM.ID,$EDOKUM.DATAW,EDOKUM.DOSTAWCA().NAZWISKO,OSOBA.PIERWSZE];
         exec('ust_biez_czynn1','obiegi',OBIEGI.B_PREL,OBIEGI.B_PRELS,_set)
      ?};
      {? var_pres('co_dalej')>0 &
         ((4+web_top_win(0,0))='WPRG' | (4+web_top_win(1,0))='WPRG' | (4+web_top_win(2,0))='WPRG')
      || co_dalej:='A'
      ?};
      {? var_pres('STATUS',_we)>0 & _we.STATUS='O' & _mp.akcja()<>'Akceptuj' & _set
      || {? OBIEGI.B_PREL<>''
         || EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
            {? EDOKOS.first()
            || {! |?
                  EDOKOS.STATUS:='O'; EDOKOS.put();
                  EDOKOS.next()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?}
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc


\del_ob_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja delegacji w obiegu
::   WE: _a - zadeklarowane i równe 1 - odpalanie z TODO na WEBTERM
::----------------------------------------------------------------------------------------------------------------------
{? ~_ || _a:=0 ?};
{? _a
|| exec('todo_run','obiegi',1); todo_run:=1;
   _dalej:=exec('env_edokum','obiegi','AD',0)
|| exec('todo_run','obiegi',0);
   _dalej:=exec('env_edokum','obiegi','AD',1);
   EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
   EDOKUM.prefix(); EDOKUMW.EDOKUM()
?};
VAR_DEL.delete('todo_run');
{? _dalej
|| _dalej:=exec('is_act_user','#b__box','OBE_FDL_EBKP',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
   {? ~_dalej
   || FUN.info('Akceptacja delegacji nie jest na liście zadań użytkownika.'@)
   ?}
?};
{? _dalej
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FDL_EBKP';
   _params.AKCJA:='chkWT';
   _params.UIDREF:=EDOKUM.uidref();
   _params.WT_SKIP:=1;
   _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
   _params.CONTEXT.EDOKUM:=EDOKUM.ref();
   _params.CONTEXT.SPR_WT:=0;
   exec('maski_w','obiegi',1);
   exec('mp_run','#b__box',_params);
   {? _params.CONTEXT.SPR_WT
   || BPMN.END:=1;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('akceptuj','obiegi_akc','A')
   ?}
?}


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Uruchomienie czynności z WWW
::   WE: _a - 'Proc' lub 'Todo' lub 'Area'
::       _b - B_PREL.ref() lub BI_PREL.ref() lub exec('mp_run_a','#b__box')
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
{? _path='Todo'
|| _path_param:=_b;
   _keyrefs:=exec('getRef4InstProc','#b_keyref',_path_param);
   _prel:=_path_param;
   {? var_pres('_keyrefs')>0 & var_pres('[1]',_keyrefs)>0 & $_keyrefs[1]<>''
   || _name:=((_keyrefs[1]-8)+8);
      EDOKUM.use(_name); EDOKUM.prefix();
      {? EDOKUM.seek(_keyrefs[1],,1)
      || exec('maski_w','obiegi',1);
         EDOKUM.memo_get(,'UW_OPDL');
         exec('bobs_edokum','obiegi','D');
         SKID.SL_KH:=SKID.NIP:='';
         web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'ed_kh_ed',0));
         EDOKUM.web_display('WD04',,"EDOKUM.web_eclose()")
      ?}
   || FUN.info('Nie znaleziono rekordu kluczowego.'@)
   ?}
?}


\todo_triggers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Trigger dla czynności OBE_FDL_EBKP
::  WE: _a - 'add po', 'put przed', 'put po', 'del przed'
::      _b - wynik akcji
::----------------------------------------------------------------------------------------------------------------------
{? _a='del_przed' | _a='add_po' | _a='put_po'
|| _par:={? _a='del_przed'
         || 2
         |? _a='add_po'
         || 3
         |? _a='put_po'
         || 4
         ?};
   exec('bi_todo_trig','obiegi',_par,'OBE_FDL_EBKP')
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [19.22]
:: OPIS: Podglad dokumentu obiegu z listy zadań
::  OLD: \display/!obe_fdl_cprz.fml
::----------------------------------------------------------------------------------------------------------------------
params_exec('disp_wni','obiegi2',3)

:Sign Version 2.0 jowisz:1045 2023/12/08 07:41:16 6e803d9e930d79f88b1b64d6ce347ca8dffa4a5b902d316069e2faa439da73c0588cd99555b62d1bc9dd25de6977a6172eeb5974fef7af9f3d32c293fbe8e7214b6d2d3abd59345ca35188c8c8599598c1c9296e995310bde5203825e12abb235e1e3c985c53a2c1c86049daa4f8cd6577b02181d891281ece2d5d638a0c647b

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fst_ewi_dobl.fml
:: Utworzony: 06.11.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_DOBL - Obliczanie amortyzacji
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Obliczanie amortyzacji - główna formuła czynności FST_EWI_DOBL.
::   WE: Jeżeli _a i _a=1 to pozaprocesowe obliczanie amortyzacji dla bieżącego środka
::       Jeżeli _=0 | _a=0 to proces obliczania amortyzacji dla firmy
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
:: PARAMETRY WE:
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WE, symbol=ODD, type=_ODD, name=Wskazanie na jednostkę księgową, required=N
:: PARAMETRY WY:
::# kind=WY, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=T
::# kind=WY, symbol=ODD, type=_ODD, name=Wskazanie na jednostkę księgową, required=T
::# kind=WY, symbol=AMOP, type=NUMBER, name=Suma amortyzacji podatkowej, required=N
::# kind=WY, symbol=AMOF, type=NUMBER, name=Suma amortyzacji bilansowej, required=N
::# kind=WY, symbol=AMOD, type=NUMBER, name=Suma amortyzacji dodatkowej, required=N

exec('init','fst');
:: pozaprocesowe obliczanie amortyzacji dla bieżącego środka
{? _>0 & _a=1
||
   exec('calc_one','!fst_ewi_dobl');
   return()
?};

:: czynność procesowa obliczania amortyzacji dla środków firmy
_mp:=params_get().mp;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_akcja:=-_mp.akcja();

_ok:=1;

OKRO_F.cntx_psh();
ODD.cntx_psh();
SRST.cntx_psh();
SRSW.cntx_psh();

:: parametry

{? _mp.pathTodo()
||
:: w przypadku gdy zadanie z ToDo powstało po uruchomieniu czynności jako startowej (wykryto środki uproszczone)
:: parametry są  zapisane jako wyjściowe, wejściowych zaś nie ma, w innych przypadkach czynność powinna
:: działać standardowo
   {? (~var_pres('OKRO_F',_we) | _we.OKRO_F=null) & var_pres('OKRO_F',_wy) || _we.OKRO_F:=_wy.OKRO_F ?};
   {? (~var_pres('ODD',_we) | _we.ODD=null) & var_pres('ODD',_wy) || _we.ODD:=_wy.ODD ?}
|| {? ~var_pres('OKRO_F',_we) || _we.OKRO_F:=SSTALE.AO  ?};

   {? ~var_pres('ODD',_we)
   || exec('odd_filtr','fst');
      ODD.win_sel('WYB');
      {? ODD.f_first()
      || {? OPERATOR.DEPT<>null & ODD.f_seek(OPERATOR.DEPT)
         || _we.ODD:=ODD.ref()
         ?}
      ?}
   ?}
?};

_wy.OKRO_F:=_we.OKRO_F;
_wy.ODD:=_we.ODD;
_mp.save(,_wy);

BtnDek:=0;

EDIT_ES.OKRO_F:=_we.OKRO_F;
EDIT_ES.ROK_F:=EDIT_ES.OKRO_F().ROK;
{? ~var_pres('ODD',_we) || EDIT_ES.ODD:=null() || EDIT_ES.ODD:=_we.ODD ?};
_title:='Amortyzacja - naliczanie'@;
_win:=EDIT_ES.mk_edit(_title,,'_amort_obl');
EDIT_ES.win_ewin(_win,,'OBL_AMOR');
_par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&OK'@];
_btn_id:=EDIT_ES.win_ebtn(_win,_par,"_wy:=exec('chk_par','!fst_ewi_dobl');
                                     {? _wy='' || BtnDek:=1; 'key:Esc' || 'edit:'+_wy ?}");
EDIT_ES.btn_opt(_btn_id,'default=1,tooltip='+exec('help_red_ok','#window','P'));
_par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@];
_btn_an:=EDIT_ES.win_ebtn(_win,_par,"BtnDek:=0; 'key:Esc'");
EDIT_ES.btn_eopt(_win,_btn_an,'tooltip='+exec('help_red_esc','#window','A'));
EDIT_ES.win_edit(_win);

{? _mp.pathProc()
|| {? EDIT_ES.edit("exec('chk_par','!fst_ewi_dobl')") || BtnDek:=1 ?}
|? _mp.pathTodo()
|| _tmp:=EDIT_ES.fld_fml('ODD','BEFORE_EDIT',"0");
   {? EDIT_ES.edit("exec('chk_par','!fst_ewi_dobl')") || BtnDek:=1 ?};
   EDIT_ES.fld_fml('ODD','BEFORE_EDIT',_tmp)
|? _mp.pathArea()
|| _tmp:=EDIT_ES.fld_fml('ODD','BEFORE_EDIT',"0");
   {? EDIT_ES.edit() || BtnDek:=1 ?};
   EDIT_ES.fld_fml('ODD','BEFORE_EDIT',_tmp)
?};

{? BtnDek=1
|| {? _mp.pathProc() || _we.ODD:=EDIT_ES.ODD ?};
   OKRO_F.index('SRODKI');
   OKRO_F.prefix(REF.FIRMA);
   ODD.index('ODDZIALY');
   ODD.prefix(REF.FIRMA);
   {? _ok & OKRO_F.seek(_we.OKRO_F) & exec('okr_mod','fst',OKRO_F.ref()) & ODD.seek(_we.ODD)
   || _mp.descTodo();
      _ok:=exec('calc_all','!fst_ewi_dobl',OKRO_F.ref(),ODD.ref())
   || _ok:=-99
   ?}
|| {? _mp.pathProc() || _ok:=-99 || _ok:=0 ?}
?};

{? _ok=1 | _ok=-1
|| _wy.OKRO_F:=_we.OKRO_F;
   _wy.ODD:=_we.ODD;
   _wy.AMOP:=exec('suma_nal_amor','fst',_wy.OKRO_F,_wy.ODD,'P');
   _wy.AMOF:=exec('suma_nal_amor','fst',_wy.OKRO_F,_wy.ODD,'F');
   {? FINFO.TOR_D='T' || _wy.AMOD:=exec('suma_nal_amor','fst',_wy.OKRO_F,_wy.ODD,'D') || _wy.AMOD:=0 ?};
   _mp.save(,_wy);
   _mp.done()
|? _ok=-99
|| _mp.cancel()
|| _wy.OKRO_F:=_we.OKRO_F;
   _wy.ODD:=_we.ODD;
   _mp.save(,_wy);
   _mp.save(,_we);
   _mp.keep();
   {? _mp.pathTodo()
   || FUN.info('Nie udało się naliczyć amortyzacji. Zadanie naliczania amortyzacji pozostanie na liście ToDo.'@)
   ?}
?};
SRSW.cntx_pop();
SRST.cntx_pop();
ODD.cntx_pop();
OKRO_F.cntx_pop()


\m_obl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Uruchomienie w obszarze roboczym
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.AO().AMOR<>'T'
|| {? SRST.lock(1,1,1)
   || {? OPERATOR.DEPT<>null
      || exec('run_obl','!fst_ewi_dobl',OPERATOR.DEPT)
      || ODD.cntx_psh();
         exec('odd_filtr','fst');
         {? ODD.f_first()
         || {! |?
               exec('run_obl','!fst_ewi_dobl',ODD.ref());
               ODD.f_next()
            !}
         ?};
         ODD.cntx_pop()
      ?};
      SRST.unlock()
   || FUN.emsg('Uruchomienie naliczania amortyzacji niemożliwe,\n'
               'dane zablokowane przez innego użytkownika.'@)
   ?}
|| FUN.emsg('Bieżący okres został już zamknięty w obszarze Środków trwałych.\n'
            'Przeliczanie amortyzacji nie jest możliwe.'@)
?}


\run_obl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Uruchomienie w obszarze roboczym dla konkretnej jednostki księgowej
::   WE: _a - wskazanie na jednostkę księgową (ODD.ref())
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FST_EWI_DOBL';
_params.AKCJA:=-menu_txt();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
SSTALE.AO();
_params.UIDREF:=OKRO_F.uidref();
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',OKRO_F.ref());
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ODD',_a);
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:=_okres:=_odd:='';
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('OKRO_F',_we)
|| OKRO_F.cntx_psh(); ROK_F.cntx_psh();
   OKRO_F.prefix();
   {? OKRO_F.seek(_we.OKRO_F) || _okres:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ ?};
   OKRO_F.cntx_pop(); ROK_F.cntx_pop()
|? var_pres('OKRO_F',_wy)
|| OKRO_F.cntx_psh(); ROK_F.cntx_psh();
   OKRO_F.prefix();
   {? OKRO_F.seek(_wy.OKRO_F) || _okres:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ ?};
   OKRO_F.cntx_pop(); ROK_F.cntx_pop()
?};
{? var_pres('ODD',_we)
|| ODD.cntx_psh();
   ODD.index('ODDZIALY');
   ODD.prefix(REF.FIRMA);
   {? ODD.seek(_we.ODD) || _odd:=ODD.OD ?};
   ODD.cntx_pop()
|? var_pres('ODD',_wy)
|| ODD.cntx_psh();
   ODD.index('ODDZIALY');
   ODD.prefix(REF.FIRMA);
   {? ODD.seek(_wy.ODD) || _odd:=ODD.OD ?};
   ODD.cntx_pop()
?};
_desc:='Oblicz amortyzację dla okresu: %1, w j. księgowej: %2.'@@[_okres,_odd];
_desc


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła ustala PARSES dla czynności na podstawie parametru,
::       wskazanie na rekord tabeli OKRO_F jest przekazane poprzez parametr OKRO_F czynności.
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_in:=params_get().in;
_out:=params_get().out;
{? var_pres('OKRO_F',_in) & var_pres('OKRO_F',_in)=type_of(null()) & _in.OKRO_F<>null()
|| OKRO_F.prefix();
   {? OKRO_F.seek(_in.OKRO_F)
   || __PARSES.setVal('OkresRok',OKRO_F.ref());
      _result:=1
   || FUN.emsg('Nie znaleziono wskazanego okresu obrachunkowego.'@)
   ?}
:: jeżeli czynność startowa to parametry z bieżących ustawień pulpitu lub z wyboru użytkownika
|| _result:=1
?};
_result


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh(); USERS.prefix();
{? USERS.seek(_user)
|| {? var_pres('ODD',_in) & var_pres('ODD',_in)=type_of(null()) & _in.ODD<>null()
   || ODD.cntx_psh();
      ODD.index('ODDZIALY');
      ODD.prefix(REF.FIRMA);
      {? ODD.seek(_in.ODD)
      || {? exec('usr_fjks','b_perm',ODD.OD,USERS.ref())
         || _result:=1
         ?}
      ?};
      ODD.cntx_pop()
   ||
:: czy w ogóle user ma uprawnienia do jakiejkolwiek jednostki księgowej
      {? exec('usr_fjks_any','b_perm',USERS.ref())
      || _result:=1
      ?}
   ?}
?};
USERS.cntx_pop();
_result


\calc_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Obliczanie amortyzacji dla bieżącego środka
::   WE: _a - naliczanie amortyzacji grupowo, jeśli brak to nie
::----------------------------------------------------------------------------------------------------------------------
{? SRST.OKRO_F().AMOR='T'
|| {? SRD.KOMM='T'
   || KOMM.add('Okres (%1 %2) jest zamknięty w obszarze FST.'@[SRST.OKRO_F().NAZ,SRST.ROK_F().NAZ])
   || FUN.info('Okres (%1 %2) jest zamknięty w obszarze FST.'@[SRST.OKRO_F().NAZ,SRST.ROK_F().NAZ])
   ?};
   return(0)
?};
{? (1+SRST.MP().T='D' | 1+SRST.MF().T='D' | (FINFO.TOR_D='T' & 1+SRST.MD().T='D'))
   & FINFO.SRST_MET=null
|| {? SRD.KOMM='T'
   || KOMM.add('W podstawowych parametrach środków trwałych '
               'nie wskazano typu dokumentu zmiany metody obsługującego przejście z metody degresywnej '
               'na liniową. Amortyzacja dla środka z metodą degresywną nie może być obliczona.'@)
   || FUN.emsg('W podstawowych parametrach środków trwałych '
               'nie wskazano typu dokumentu zmiany metody obsługującego przejście z metody degresywnej '
               'na liniową. Amortyzacja dla środka z metodą degresywną nie może być obliczona.'@)
   ?};
   return(0)
?};
SRSR.cntx_psh();
{? var_press('wer_srot')<0
|| {? ~exec('calc_ask','!fst_ewi_dobl')
   || SRSR.cntx_pop();
      return(0)
   ?}
|| exec('calc_ask_grp','!fst_ewi_dobl')
?};
SRSR.cntx_pop();
{? SRD.isElement()
|| {? SRD.KOMM='T'
   || KOMM.add('Amortyzacja dla elementów składowych naliczana jest automatycznie podczas '
               'naliczania amortyzacji dla zestawu.'@)
   || FUN.info('Amortyzacja dla elementów składowych naliczana jest automatycznie podczas\n'
               'naliczania amortyzacji dla zestawu.'@)
   ?};
   return(0)
?};
{? SRST.r_lock(1,1,1)
|| {? SRST.GRP='T' || SRD.calcForSet() || SRD.calculation() ?};
   SRST.r_unlock()
|| {? SRD.KOMM='T'
   || KOMM.add('Dane zablokowane przez innego użytkownika.'@)
   || FUN.info('Dane zablokowane przez innego użytkownika.'@)
   ?};
   return(0)
?}


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie amortyzacji środka
::----------------------------------------------------------------------------------------------------------------------
{? SRST.OKRO_F().AMOR='T'
|| {? SRD.KOMM='T'
   || KOMM.add('Okres (%1 %2) jest zamknięty w obszarze FST.'@[SRST.OKRO_F().NAZ,SRST.ROK_F().NAZ])
   || FUN.info('Okres (%1 %2) jest zamknięty w obszarze FST.'@[SRST.OKRO_F().NAZ,SRST.ROK_F().NAZ])
   ?};
   return(0)
?};
{? SRST.DEKRET='T'
|| {? SRD.KOMM='T'
   || KOMM.add('Amortyzacja w bieżącym okresie została już zadekretowana dla co najmniej jednego '
               'toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian mogących wpłynąć '
               'na jej wartość nie jest możliwe.'@)
   || FUN.emsg('Amortyzacja w bieżącym okresie została już zadekretowana dla co najmniej jednego\n'
               'toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian mogących wpłynąć\n'
               'na jej wartość nie jest możliwe.'@)
   ?};
   return(0)
|| {? FINFO.AMOR='O' & SSTALE.AO <> SRST.OKRO_F
   || {? ~FUN.ask('Usunąć amortyzację w okresie innym niż bieżący (%1 %2)?'@[SSTALE.AO().NAZ,$SSTALE.AO().RES])
      || return(0)
      ?}
   ?}
?};
:: kontrola czy nie zadekretowani amortyzacji w późniejszych okresach
SRST.cntx_psh();
_rok:=SRST.ROK;
_okres:=SRST.OKRES;
SRST.index('PODAT');
SRST.prefix(SRST.SRSR);
_loop:=1;
{? SRST.find_key(_rok,_okres)
|| {! |?
      {? SRST.DEKRET='T' || _loop:=0 ?};
      _loop & SRST.next()
   !}
?};
SRST.cntx_pop();
{? _loop=0
|| {? SRD.KOMM='T'
   || KOMM.add('Amortyzacja w jednym z kolejnych okresów została już zadekretowana dla co najmniej jednego '
               'toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian mogących wpłynąć '
               'na jej wartość nie jest możliwe.'@)
   || FUN.emsg('Amortyzacja w jednym z kolejnych okresów została już zadekretowana dla co najmniej jednego\n'
               'toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian mogących wpłynąć\n'
               'na jej wartość nie jest możliwe.'@)
   ?};
   return(0)
?};

{? SRD.isElement()
|| {? SRD.KOMM='T'
   || KOMM.add('[Element: %1]: Usuwanie amortyzacji dla elementów składowych przeprowadzane jest'
               ' automatycznie podczas usuwania amortyzacji dla zestawu.'@[SRST.NRI])
   || FUN.info('Usuwanie amortyzacji dla elementów składowych przeprowadzane jest automatycznie\n'
               'podczas usuwania amortyzacji dla zestawu.'@)
   ?};
   return(0)
?};
{? SRST.GRP='T' || SRD.delCalcForSet() || SRD.delCalculation() ?}


\grp_calc_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła grupa przed - obliczanie amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? SRST.lock(1,1,1)
|| wer_srot:=tab_tmp(, 'NRI', 'STRING[18]', 'Numer inwentarzowy'@
                  , 'NST', 'STRING[100]', 'Nazwa'@
                  ,'DE', 'DATE', 'W eksploatacji'@);
   SRD.KOMM:='T';
   sel_nchk();
   KOMM.init(255,,'Uwagi dotyczące obliczania amortyzacji'@);
   1
|| FUN.emsg('Dane zablokowane przez innego użytkownika.'@);
   0
?}


\grp_calc_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła grupa po - obliczanie amortyzacji
::----------------------------------------------------------------------------------------------------------------------
{? wer_srot.size()>0
|| _wer:=wer_srot.mk_sel('Środki bez dokumentu OT'@,,,'wer_ot');
   wer_srot.win_fld(_wer,,'NRI');
   wer_srot.win_fld(_wer,,'NST');
   wer_srot.win_fld(_wer,,'DE');
   wer_srot.win_sel(_wer);
   {! |? FUN.choice('Brak dokumentu OT dla co najmniej jednego środka'@,0,'Szczególy'@,,,,'OK'@)=1
      |!  wer_srot.select()
   !}
?};
SRST.unlock();
&wer_srot;
{? ~KOMM.empty() || KOMM.select() ?};
SRD.KOMM:='N';
1


\grp_calc_del_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła grupa przed - usuwanie amortyzacji
::----------------------------------------------------------------------------------------------------------------------
_exit:=0;
_tab:=SRST.sel_aget();
{? _tab.first()
|| SRST.cntx_psh();
   {! |?
      {? SRST.seek(_tab.REF,) & SRST.DEKRET='T' || _exit:=1 ?};
      ~_exit & _tab.next()
   !};
   SRST.cntx_pop()
?};
{? ~_exit
|| {? SRST.lock(1,1,1)
   || SRD.KOMM:='T';
      sel_nchk();
      KOMM.init(255,,'Uwagi dotyczące usuwania amortyzacji'@);
      1
   || FUN.emsg('Dane zablokowane przez innego użytkownika.'@);
      0
   ?}
|| FUN.emsg('Amortyzacja w bieżącym okresie (lub kolejnych) została już zadekretowana dla co\n'
            'najmniej jednego toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian\n'
            'mogących wpłynąć na jej wartość nie jest możliwe.'@);
   SRST.sel_adel();
   0
?}


\grp_calc_del_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła grupa po - usuwanie amortyzacji
::----------------------------------------------------------------------------------------------------------------------
SRST.unlock();
{? ~KOMM.empty() || KOMM.select() ?};
SRD.KOMM:='N';
1


\calc_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Obliczanie amortyzacji dla wszystkich środków (w bieżącym lub wsazanym okresie)
::   WE: _a - wskazanie na okres, jeśli brak _a to bieżący okres
::       _b - wskazanie na jednostkę księgową
::   WY: 1- naliczono amortyzację bez problemów, 0 - pojawiły się problemy
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
_nazwa:=_odd:='';
OKRO_F.cntx_psh();
ROK_F.cntx_psh();
{? _>0 & _a<>null
|| OKRO_F.prefix();
   {? OKRO_F.seek(_a) || _nazwa:=OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ ?}
|| _nazwa:=SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ
?};
ROK_F.cntx_pop();
OKRO_F.cntx_pop();
ODD.cntx_psh();
ODD.index('ODDZIALY');
ODD.prefix(REF.FIRMA);
{? ODD.seek(_b) || _odd:=ODD.OD || _odd:='' ?};
ODD.cntx_pop();
{? _nazwa<>'' & _odd<>''
|| _wy:=SRD.calculationAll(_a,_b)
|| FUN.emsg('Niepoprawne dane wejściowe dotyczące okresu naliczania amortyzacji lub jednostki księgowej.'@)
?};
{? _wy=1
|| _sql:=exec('calc_ask_all','!fst_ewi_dobl',_a,_b);
   {? _sql.size()<>0
   || {! |? FUN.choice('Zakończono naliczanie amortyzacji w okresie: %1  %2 dla j. księgowej: %3.\nBrak dokumentu OT dla co najmniej jednego środka.'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ,_odd],0,'Szczególy'@,,,,'OK'@)
         |!  _sql.select()
      !}
      || FUN.info('Zakończono naliczanie amortyzacji w okresie: %1  %2 dla j. księgowej: %3.'@[SSTALE.AO().NAZ,SSTALE.AR().NAZ,_odd])
   ?}
?};
_wy


\del_calc_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Usuwanie amortyzacji dla wszystkich środków (w bieżącym okresie)
::----------------------------------------------------------------------------------------------------------------------
{? SRST.OKRO_F().AMOR='T'
|| {? SRD.KOMM='T'
   || KOMM.add('Okres (%1 %2) jest zamknięty w obszarze FST.'@[SRST.OKRO_F().NAZ,SRST.ROK_F().NAZ])
   || FUN.info('Okres (%1 %2) jest zamknięty w obszarze FST.'@[SRST.OKRO_F().NAZ,SRST.ROK_F().NAZ])
   ?};
   return(0)
?};
{? SRST.DEKRET='T'
|| {? SRD.KOMM='T'
   || KOMM.add('Amortyzacja w bieżącym okresie została już zadekretowana dla co najmniej jednego '
               'toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian mogących wpłynąć '
               'na jej wartość nie jest możliwe.'@)
   || FUN.emsg('Amortyzacja w bieżącym okresie została już zadekretowana dla co najmniej jednego\n'
               'toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian mogących wpłynąć\n'
               'na jej wartość nie jest możliwe.'@)
   ?};
   return(0)
?};
:: kontrola czy nie zadekretowani amortyzacji w późniejszych okresach
SRST.cntx_psh();
_rok:=SRST.ROK;
_okres:=SRST.OKRES;
SRST.index('PODAT');
SRST.prefix(SRST.SRSR);
_loop:=1;
{? SRST.find_key(_rok,_okres)
|| {! |?
      {? SRST.DEKRET='T' || _loop:=0 ?};
      _loop & SRST.next()
   !}
?};
SRST.cntx_pop();
{? _loop=0
|| {? SRD.KOMM='T'
   || KOMM.add('Amortyzacja w jednym z kolejnych okresów została już zadekretowana dla co najmniej jednego '
               'toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian mogących wpłynąć '
               'na jej wartość nie jest możliwe.'@)
   || FUN.emsg('Amortyzacja w jednym z kolejnych okresów została już zadekretowana dla co najmniej jednego\n'
               'toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian mogących wpłynąć\n'
               'na jej wartość nie jest możliwe.'@)
   ?};
   return(0)
?};


{? SRST.lock(1,1,1)
|| {? FUN.ask('Uruchomić usuwanie naliczonej amortyzacji dla wszystkich dostępnych środków?'@)
   || SRD.delCalculationAll()
   ?};
   SRST.unlock()
|| FUN.emsg('Uruchomienie usuwania naliczonej amortyzacji niemożliwe,\n'
            'dane zablokowane przez innego użytkownika.'@)
?}


\chk_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła sprawdza parametry naliczania amortyzacji
::   WY: '' lub akronim błędnie wypełnionego pola
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? EDIT_ES.ODD=null
|| FUN.emsg('Należy wskazać jednostkę księgową.'@);
   _wy:='ODD'
?};
_wy


\calc_ask
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Komunikaty dla środków bez dokumentów OT oraz tabela ze szczególami przy naliczaniu amortyzacji.
::----------------------------------------------------------------------------------------------------------------------
{? SRST.GRP='T'
|| SRST.cntx_psh();
   SRST.index('TREE');
   SRST.prefix(SRST.R,SRST.OKRO_F,SRST.ODD,SRST.ref);
   _wer_srot:=tab_tmp(, 'NRI', 'STRING[18]', 'Numer inwentarzowy'@
                      , 'NST', 'STRING[100]', 'Nazwa'@
                      ,'DE', 'DATE', 'W eksploatacji'@);
    _wer:=_wer_srot.mk_sel('Elementy bez dokumentu OT'@,,,'wer_ot');
    _wer_srot.win_fld(_wer,,'NRI');
    _wer_srot.win_fld(_wer,,'NST');
    _wer_srot.win_fld(_wer,,'DE');
    _wer_srot.win_sel(_wer);
   {? SRST.first()
   || {!
      |? {? SRST.SRSR().DOKPRZ=null
         || _wer_srot.NST:=SRST.NST;
            _wer_srot.NRI:=SRST.NRI;
            _wer_srot.DE:=SRST.DE;
            _wer_srot.add()
         ?};
         SRST.next()
      !}
   ?};
   SRST.cntx_pop();
   _choice:=1;
   {? (FINFO.AMOR='O' & SSTALE.AO <> SRST.OKRO_F)
   || {? _wer_srot.size()>0
      || {! |? _choice=1
            |! _choice:=FUN.choice('Obliczanie amortyzacji w okresie innym niż bieżący (%1 %2)\noraz brak dokumentu OT dla co najmniej jednego elementu.\nCzy chcesz kontynuować?'@[SSTALE.AO().NAZ,$SSTALE.AO().RES],0,'Szczególy'@,'Tak'@,,,'Nie');
               {? _choice=1
               || _wer_srot.select()
               |? _choice=0
               || return(0)
               ?}
         !}
      || {? ~FUN.ask('Obliczyć amortyzację w okresie innym niż bieżący (%1 %2)?'@[SSTALE.AO().NAZ,$SSTALE.AO().RES])
         || return(0)
         ?}
      ?}
   || {? _wer_srot.size()>0
      || {! |? _choice=1
            |! _choice:=FUN.choice('Brak dokumentu OT dla co najmniej jednego elementu'@,0,'Szczególy'@,'OK'@);
               {? _choice=1
               || _wer_srot.select()
               |? _choice=0
               || return(0)
               ?}
         !}
      ?}
   ?}
||
   {? (FINFO.AMOR='O' & SSTALE.AO <> SRST.OKRO_F)
   || {? SRST.SRSR().DOKPRZ=null()
      || {? ~FUN.ask('Obliczanie amortyzacji w okresie innym niż bieżący (%1 %2) oraz brak dokumentu OT.\nCzy chcesz kontynuować?'@[SSTALE.AO().NAZ,$SSTALE.AO().RES])
         || return(0)
         ?}
      || {? ~FUN.ask('Obliczyć amortyzację w okresie innym niż bieżący (%1 %2)?'@[SSTALE.AO().NAZ,$SSTALE.AO().RES])
         || return(0)
         ?}
      ?}
   || {? SRST.SRSR().DOKPRZ=null()
      || {? ~FUN.ask('Brak dokumentu OT. Czy chcesz kontynuować?'@)
         || return(0)
         ?}
      ?}
   ?}
?};
return(1)


\calc_ask_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Utworzenie tabeli środków bez dokumentu OT przy naliczaniu amortyzacji dla grupy środków.
::----------------------------------------------------------------------------------------------------------------------
{? SRST.GRP='T'
|| SRST.cntx_psh();
   SRST.index('TREE');
   SRST.prefix(SRST.R,SRST.OKRO_F,SRST.ODD,SRST.ref);
   {? SRST.first()
   || {!
      |? {? SRST.SRSR().DOKPRZ=null
         || wer_srot.NST:=SRST.NST;
            wer_srot.NRI:=SRST.NRI;
            wer_srot.DE:=SRST.DE;
            wer_srot.add()
         ?};
       SRST.next()
       !}
   ?};
   SRST.cntx_pop()
||
   {? SRST.SRSR().DOKPRZ=null
   || wer_srot.NST:=SRST.NST;
      wer_srot.NRI:=SRST.NRI;
      wer_srot.DE:=SRST.DE;
      wer_srot.add()
   ?}
?};
return(1)


\calc_ask_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [18.42]
:: OPIS: Zapytanie SQL oraz utworzenie okienka wertowania do wyświetlenia środków nie posiadających dokumentu OT
::       podczas naliczania amortyzacji dla całej firmy
::   WE: _a - wskazanie na okres
::       _b - wskazanie na jednostkę księgową
::   WY: _sql - tablica zawierająca środki bez dokumentu OT
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.AMO_RODZ='B'
|| _rodzaj:='and SRST.R = '':_e'' '
|| _rodzaj:=''
?};
_sql_que:='select SRST.NRI, SRST.NST, SRST.DE, ODD.OD as OD from SRST '
      + 'join SRSR using (SRST.SRSR, SRSR.REFERENCE) '
      + 'join ODD using (SRST.ODD, ODD.REFERENCE) '
      + 'where SRST.ODD = :_a and SRST.OKRO_F = :_b and SRSR.DOKPRZ IS NULL and SRST.DE <= to_date(:_c) '
      + 'and SRST.DE >= to_date(:_d) and (SRST.GRP=''N'' or SRST.KIND=''N'') '
      + _rodzaj
      + 'order by SRST.NRI';
_sql:=sql(_sql_que, _b , _a, SSTALE.AO().KON, SSTALE.AO().POCZ, EDIT_ES.R);
_wer:=_sql.mk_sel('Środki bez dokumentu OT'@,,,'wer');
_sql.win_sel(_wer);
_sql.win_fld(_wer,_sql,'NRI',,,,,,'Numer inwentarzowy'@);
_sql.win_fld(_wer,_sql,'NST',,,,,,'Nazwa'@);
_sql.win_fld(_wer,_sql,'DE',,,,,,'Data eksploatacji'@);
_sql.win_fld(_wer,_sql,'OD',,,,,,'Jednostka księgowa'@);
return(_sql)


\calc_for_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Obliczenie amortyzacji dla grupy środków
::   WE: _a - 'T' - trwały, 'N' - niskocenny
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('grp_help','!fst_ewi_dobl');
_tab.prefix();
{? _tab.first() & _tab.GR<>'' || _ok:=1 || _ok:=0; return(0) ?};
{? SRST.lock(1,1,1)
|| wer_srot:=tab_tmp(, 'NRI', 'STRING[18]', 'Numer inwentarzowy'@
                  , 'NST', 'STRING[100]', 'Nazwa'@
                  ,'DE', 'DATE', 'W eksploatacji'@);
   SRD.KOMM:='T';
   KOMM.init(255,,'Uwagi dotyczące obliczania amortyzacji'@);
   1
|| FUN.emsg('Dane zablokowane przez innego użytkownika.'@);
   0
?};
{? _ok
|| SRST.cntx_psh();
   _ndx:=SRST.ndx_tmp(,1,'R',,,'OKRO_F',,,'GR','GR',); SRST.index(_ndx);
   SRST.prefix(_a,SSTALE.AO,_tab.GR); _size:=SRST.size(); _lp:=1;
   {? SRST.first()
   || {! |?
         SRST.SRSR();
         progress(100*_lp/_size,'Trwa obliczanie amortyzacji dla grupy środków...'@);
         exec('calc_one','!fst_ewi_dobl',1);
         _lp+=1;
         SRST.next()
      !};
      prgs_clr()
   || FUN.info('Brak środków dla grupy %1'@[_tab.GR])
   ?};
   SRST.cntx_pop()
?};
{? wer_srot.size()>0
|| _wer:=wer_srot.mk_sel('Środki bez dokumentu OT'@,,,'wer_ot');
   wer_srot.win_fld(_wer,,'NRI');
   wer_srot.win_fld(_wer,,'NST');
   wer_srot.win_fld(_wer,,'DE');
   wer_srot.win_sel(_wer);
   {! |? FUN.choice('Brak dokumentu OT dla co najmniej jednego środka'@,0,'Szczególy'@,,,,'OK'@)=1
      |!  wer_srot.select()
   !}
?};
SRST.unlock();
&wer_srot;
{? ~KOMM.empty() || KOMM.select() ?};
SRD.KOMM:='N';
1


\grp_help
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła pomocnicza do obsługi grup środków
::----------------------------------------------------------------------------------------------------------------------
_fml:="TAM.cntx_psh(); TAM.index('KST');
       _kst:=exec('kst_obow','fst_kst',#SSTALE.AR().NAZ,1);
       TAM.prefix(_kst);
       TAM.win_sel('SLO2');
       TAM.select();
       _wyn:=TAM.GR;
       TAM.cntx_pop();
       _wyn";
_tab:=tab_tmp(,'GR','STRING[15]','Grupa');
_tab.fld_fml('GR','F3',_fml);
_ewin:=_tab.mk_edit('Wybór grupy',0);
_tab.win_efld(_ewin,_tab,'GR',,,,,0,,0,,,'F3_button=1');
_tab.win_ebtn(_ewin,'text=OK','key:F2');
_tab.win_ebtn(_ewin,'text=Anuluj','key:Esc');
_tab.win_edit(_ewin);
{? _tab.edit("{? cur_tab(,1).GR='' || FUN.info('Wskaż grupę'@); 'GR' || '' ?}")
|| _tab.add()
?};
_tab


\del_for_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Usunięcie amortyzacji dla grupy
::   WE: _a - 'T' - trwały, 'N' - niskocenny
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('grp_help','!fst_ewi_dobl');
_tab.prefix();
{? _tab.first() & _tab.GR<>'' || _ok:=1 || _ok:=0; return(0) ?};
{? _ok
|| SRST.cntx_psh();
   _ndx:=SRST.ndx_tmp(,1,'R',,,'OKRO_F',,,'GR','GR',); SRST.index(_ndx);
   SRST.prefix(_a,SSTALE.AO,_tab.GR);
   _ref:=tab_tmp(,'REF','STRING[16]','Ref');
   {? SRST.first()
   || {! |?
         _ref.REF:=$SRST.ref();
         _ref.add();
         SRST.next()
      !}
   || FUN.info('Brak środków dla grupy %1'@[_tab.GR]); SRST.cntx_pop(); return(0)
   ?};
   exec('del_grp_help','!fst_ewi_dobl',_ref);
   SRST.index(_ndx);
   SRST.prefix(_a,SSTALE.AO,_tab.GR); _size:=SRST.size(); _lp:=1;
   {? SRST.first()
   || {! |?
         SRST.SRSR();
         progress(100*_lp/_size,'Trwa usuwanie amortyzacji dla grupy środków...'@);
         exec('delete','!fst_ewi_dobl');
         _lp+=1;
         SRST.next()
      !};
      prgs_clr()
   ?};
   exec('grp_calc_del_a','!fst_ewi_dobl');
   SRST.cntx_pop()
?};
1


\del_grp_help
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła pomocnicza przy usuwaniu naliczonej amortyzacji dla wybranej grupy
::   WE: _a - tabela tymczasowa z refami SRST do usunięcia amor.
::----------------------------------------------------------------------------------------------------------------------
_exit:=0;
_tab:=_a;
{? _tab.first()
|| SRST.cntx_psh();
   {! |?
      {? SRST.seek(_tab.REF,) & SRST.DEKRET='T' || _exit:=1 ?};
      ~_exit & _tab.next()
   !};
   SRST.cntx_pop()
?};
{? ~_exit
|| {? SRST.lock(1,1,1)
   || SRD.KOMM:='T';
      KOMM.init(255,,'Uwagi dotyczące usuwania amortyzacji'@);
      1
   || FUN.emsg('Dane zablokowane przez innego użytkownika.'@);
      0
   ?}
|| FUN.emsg('Amortyzacja w bieżącym okresie (lub kolejnych) została już zadekretowana dla co\n'
            'najmniej jednego toru amortyzacji. Usuwanie amortyzacji lub wprowadzanie zmian\n'
            'mogących wpłynąć na jej wartość nie jest możliwe.'@);
   SRST.sel_adel();
   0
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 5f48d5f3a8c6d675cd820002ad9f1b6d68682bef1e291347279514ab80ab04a1e75fb52ab2c50e910aac25194b335c1c2d2c67c2dc149300dbccbf81ec561e7d8f21924344eee5ff2f03db8d3c3e5341c24efbd96f54f1dd189ec50ef04ca33fa0652ec8ad75bc0e6eda4d7986e793e8620b099cf8eecd6c53f1bd142ae57639

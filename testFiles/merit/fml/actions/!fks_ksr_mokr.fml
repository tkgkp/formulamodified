:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ksr_mokr.fml [17.00]
:: Utworzony: 2015.12.15
:: Autor: AMK
::======================================================================================================================
:: Zawartosc: Formuły czynności FKS_KSR_MOKR - Zamknięcie okresu - Finanse
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła główna czynności FKS_KSR_MOKR - Zamknięcie okresu - Finanse
::   WE: _a - [obj_new] - parametry wejsciowe czynnosci
::       _b - [obj_new] - parametry wewnetrzne czynnosci
::       _c - [obj_new] - parametry wyjsciowe czynnosci
::       _d - obiekt odpowiedzialny za obsluge procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# kind=WE, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
::# kind=WY, symbol=OKRO_F, type=_OKRO_F, name=Wskazanie na okres, required=N
_args:=params_get();
_we:=_args.in;
_mp:=_args.mp;
_akcja:=_mp.akcja();
_wew:=_args.int;
_wy:=_args.out;
params_set(params_get());
OKR_OBSZ.cntx_psh(); OKRO.cntx_psh(); OKRO_F.cntx_psh(); ROK_F.cntx_psh();
:: czynność startowa
{? _mp.pathProc()
|| {? ~SSTALE.AO
   || FUN.info('Nie wybrano okresu w stałych systemu.'@)
   || exec('lic','zam_okr_rok');
      _wy.OKRO_F:=SSTALE.AO; _mp.save(,_wy);
      OKRESY.ZO:=OKRO_F.ref();
      OKRESY.ZR:=OKRO_F.ROK;
      OKRESY.ZO();
      {? OKRO_F.r_lock(1,1)
      || exec('init','zam_okr_rok');
         zam_okr:=0;
         params_exec('zam_okr1','zam_okr_rok',0,1);
         params_exec('zam_okr2','zam_okr_rok',0);
         params_exec('zam_okr3','zam_okr_rok',0,1);
         VAR_DEL.delete('zam_okr');
         unlock_r()
      || FUN.info('Inny użytkownik obsługuje okres obrachunkowy.'@)
      ?}
   ?}
|? _mp.pathTodo()
|| exec('lic','zam_okr_rok');
   {? var_press('OKRO_F',_we)=0 || _we.OKRO_F:=SSTALE.AO ?};
   _wy.OKRO_F:=_we.OKRO_F; _mp.save(,_wy);
   _komunik:=~_mp.isAutoRun();
   {? var_press('OKRO_F',_we)>0
   || OKRESY.ZO:=_we.OKRO_F;
      OKRESY.ZR:=OKRO_F.ROK;
      OKRESY.ZO();
      {? OKRO_F.r_lock(1,1)
      || exec('init','zam_okr_rok');
         zam_okr:=0;
         params_exec('zam_okr1','zam_okr_rok',0,_komunik);
         params_exec('zam_okr2','zam_okr_rok',0);
         params_exec('zam_okr3','zam_okr_rok',0,_komunik);
         VAR_DEL.delete('zam_okr');
         unlock_r()
      |? _komunik
      || FUN.info('Inny użytkownik obsługuje okres obrachunkowy.'@)
      ?}
   || FUN.info('Nie podano okresu zamykanego.'@); _mp.error()
   ?}
|? (7+_akcja)='zamknij'
|| {? OKRO_F.r_lock(1,1)
   || exec('init','zam_okr_rok');
      zam_okr:=0;
      params_exec('zam_okr1','zam_okr_rok',0,1);
      params_exec('zam_okr2','zam_okr_rok',0);
      params_exec('zam_okr3','zam_okr_rok',0,1);
      VAR_DEL.delete('zam_okr');
      unlock_r()
   || FUN.info('Inny użytkownik obsługuje okres obrachunkowy.'@)
   ?}
?};
OKR_OBSZ.cntx_pop(); OKRO.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop();
1


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Zamykanie okresu dla Finansów'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
_wy:=_mp.load(exec('kind_out','#b_port'));
{? var_pres('OKRO_F',_we) & ~var_pres('OKRO_F',_wy)
|| _wy.OKRO_F:=_we.OKRO_F; _mp.save(,_wy)
?};
{? var_pres('OKRO_F',_we)
|| OKRO_F.cntx_psh(); OKRO_F.prefix();
   {? OKRO_F.seek(_we.OKRO_F)
   || ROK_F.cntx_psh(); ROK_F.prefix(); OKRO_F.ROK();
      _desc:='Zamykanie okresu dla Finansów: %1 %2'@@[OKRO_F.NAZ,ROK_F.NAZ];
      ROK_F.cntx_pop()
   ?};
   OKRO_F.cntx_pop()
?};
_desc


\zam_okrf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamknięcie okresu
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_KSR_MOKR';
{? _a=1
|| _params.AKCJA:='zamknij1'
|| _params.AKCJA:='zamknij2'
?};
_dalej:=1;
{? _a=1
|| _obszar:=exec('szuk_b_dom','parses','FKS');
   OKRO_F.cntx_psh(); OKRO_F.prefix();
   OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(OKRO.ref(),_obszar);
   {? _obszar & OKR_OBSZ.first()
   || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
      OKR_OBSZ.OKRO_F()
   || FUN.info('Nie znaleziono dla wybranego okresu wspólnego okresu finansowego.'@);
      _dalej:=0
   ?}
?};
{? _dalej
|| OKRESY.ZO:=OKRO_F.ref();
   OKRESY.ZR:=OKRO_F.ROK;
   _params.UIDREF:=OKRO_F.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',OKRO_F.ref());
   exec('mp_run','#b__box',_params)
?};
{? _a=1
|| OKRO_F.cntx_pop();
   OKR_OBSZ.cntx_pop()
?}

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:12 cebb4ff12a34a71ba2f4d512e3952b5953faa99b41b0b3270d969113472991f8a5f6c31912d74da6303b1476230c7a89e31a6e3abf13ba3c4641aa3f4a1edee314c0e01afc26e1b1ddcbedd893ab8b08612571921226a3bed3dc08f09bdb760fbc58340de74bd76257f36db92396a3db6c0be2e9db36e80073a266c8738523ba

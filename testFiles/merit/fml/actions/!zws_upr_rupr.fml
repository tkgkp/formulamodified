:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_upr_rupr.fml
:: Utworzony: 07.27.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_UPR_RUPR - Upraw. do schematów danych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Upraw. do schematów danych - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
::
exec('dane_sys','schemat');

:: utwórz bufor nawigatora po danych UDB_GRP i UDB_SYS
_SYS:=exec('mk_sys_buf','!zws_upr_rupr');

:: utwórz panel i okna zarządzania
_wnd:=exec('mk_wnd','!zws_upr_rupr',_SYS);

:: udostępnij uprawnienia
_SYS.win_sel(_wnd);
_SYS.select();

:: porządki
obj_del(_SYS);
~~


\mk_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy panel zarządzania uprawnieniami.
::   WE: _a [TABLE] - alias tabeli nawigatora
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_BUF:=_a;
_div:=12;

:: dedykowane okienko dla tabel UDB_GRP i UDB_SYS
_sys:=exec('mk_sys_wnd','!zws_upr_rupr',_BUF);

:: panel zarządzania uprawnieniami
_wnd:=_BUF.grp_make('Uprawnienia'@,
:: przed otwarciem
   "exec('adm_bf','!zws_upr_rupr')",
:: identyfikator
   '#zws_upr_rupr',,,
:: podczas zamykania
   "exec('adm_oc','!zws_upr_rupr')"
);
:: nawigator po grupach dziedzin i typach danych
_BUF.grp_sel(_wnd,,_sys,,
:: po zmianie bieżącego wiersza
   "exec('adm_sys_ar','!zws_upr_rupr')",,,,
:: przed obsługą
   "exec('adm_sys_bs','!zws_upr_rupr',_a)"
);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: uprawnienia według elementów
_BUF.grp_splt(_wnd,,'vertical','right','0,40%');
_BUF.grp_sel(_wnd,UD_DEF,'NAW_MAX','Wg elementów'@,
:: po zmianie bieżącego wiersza
   "exec('adm_def_ar','!zws_upr_rupr')",,,_div,
:: przed obsługą
   "exec('adm_def_bs','!zws_upr_rupr',_a)"
);
:: uprawnienia użytkowników
_BUF.tab_splt(_wnd,,'horizontal','bottom');
_BUF.grp_sel(_wnd,UDB_UPR,'USERS',,
:: po zmianie bieżącego wiersza
   "exec('adm_usr_upr_users_ar','!zws_upr_rupr')",,,,
:: przed obsługą
   "exec('adm_def_upr_bs','!zws_upr_rupr',_a)"
);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: uprawnienia według użytkowników
_BUF.grp_sel(_wnd,USERS,'NAW','Wg użytkowników'@,
:: po zmianie bieżącego wiersza
   "exec('adm_usr_ar','!zws_upr_rupr')",,,_div,
:: przed obsługą
   "exec('adm_usr_bs','!zws_upr_rupr',_a)"
);
:: uprawnienia do elementów
_BUF.tab_splt(_wnd,,'horizontal','bottom');
_BUF.grp_sel(_wnd,UDB_UPR,'UD_SKL',,
:: po zmianie bieżącego wiersza
   "exec('adm_usr_upr_ud_skl_ar','!zws_upr_rupr')",,,,
:: przed obsługą
   "exec('adm_usr_upr_bs','!zws_upr_rupr',_a)"
);

_wnd


\mk_sys_buf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Tworzy tabelę nawigatora po danych UDB_GRP i UDB_SYS.
::   WE:
::   WY: alias tabeli
::----------------------------------------------------------------------------------------------------------------------
_max:="${? _a>_b || _a || _b ?}";

_BUF:=tab_tmp(2,
   'TREE_REF','TREE_REF','Nadrzędny'@,
   'SYMBOL','STRING[%1]'[_max(MS.fld_len(UDB_GRP,'SYMBOL'),MS.fld_len(UD_TYP,'SYMBOL'))],'Symbol'@,
   'OPIS','STRING[%1]'[_max(MS.fld_len(UDB_GRP,'OPIS'),MS.fld_len(UD_TYP,'OPIS'))],'Opis'@,
   'REF','STRING[16]','Wskazanie'@
)


\mk_sys_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Tworzy okienko nawigatora po danych UDB_GRP i UDB_SYS.
::   WE: _a [TABLE] - alias tabeli nawigatora
::   WY: akronim okienka
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_BUF:=_a;

_wnd:=_BUF.mk_sel('Typy w grupach dziedzin'@,'P',,'#udb_sys_naw',,,,1);
_BUF.win_fld(_wnd,,'SYMBOL',,,15,,,,,MS.comment(UD_TYP,'SYMBOL'),,,,,,'mobile_visible=1');
_BUF.win_fld(_wnd,,'OPIS',,,50,,,,,MS.comment(UD_TYP,'OPIS'),,,,,,'mobile_visible=1');

_BUF.win_act(_wnd,,'Rekord',,,,"
   _BUF:=cur_tab();
   {? ref_tab(_BUF.REF)=UDB_SYS
   || UDB_SYS.seek(_BUF.REF)
   ?};
   ~~
");

_wnd


\udb_upr_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dołącz przed" tabeli UDB_UPR
::----------------------------------------------------------------------------------------------------------------------
UDB_UPR.DOMYSLNY:=UDB_UPR.UD_SKL().DOMYSLNY;
1


\udb_upr_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dołącz po" tabeli UDB_UPR
::   WE: _a - wynik akcji dołączania: 1 - udało się dodać rekord, 0 - rekord nie został dodany
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie dodano rekordu
{? ~_a | do_state()<>1 || return() ?};

{? UDB_UPR.DOSTEP='T'
|| exec('grant_upr','!zws_upr_rupr')
|| exec('udb_upr_zmien','schemat')
?};
~~


\udb_upr_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw przed" tabeli UDB_UPR
::----------------------------------------------------------------------------------------------------------------------
{? UDB_UPR.UD_SKL
|| UD_SKL.cntx_psh();
   UDB_UPR.DOMYSLNY:=UDB_UPR.UD_SKL().DOMYSLNY;
   UD_SKL.cntx_pop();
   1
?}


\udb_upr_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw po" tabeli UDB_UPR
::   WE: _a - wynik akcji poprawiania: 1 - udało się zmienić rekord, 0 - rekord nie został zmieniony
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie zmieniono rekordu
{? ~_a | do_state()<>1 || return() ?};

{? UDB_UPR.DOSTEP<>bfld('DOSTEP')
|| {? UDB_UPR.DOSTEP='T'
   || exec('grant_upr','!zws_upr_rupr')
   || exec('udb_upr_delb','!zws_upr_rupr')
   ?}
?};
{? UDB_UPR.DOMYSLNY<>bfld('DOMYSLNY')
|| exec('udb_upr_zmien','schemat')
?};
~~


\udb_upr_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "usuń przed" tabeli UDB_UPR
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',UDB_WID,'UNIQUE',UDB_UPR.ref())


\udb_wid_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "dołącz przed" tabeli UDB_WID
::----------------------------------------------------------------------------------------------------------------------
exec('udb_wid_akt','!zws_upr_rupr')


\udb_wid_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyzwalacz "popraw przed" tabeli UDB_WID
::----------------------------------------------------------------------------------------------------------------------
exec('udb_wid_akt','!zws_upr_rupr')


\udb_wid_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Aktualizuj anomalie.
::   WE:
::   WY: 0 - brak niezbędnych informacji, 1 - dane zostały uzupełnione
::----------------------------------------------------------------------------------------------------------------------
:: wymagane informacje pierwotne: UDB_WID.UDB_UPR, UDB_WID.UD_DEF
{? UDB_WID.UDB_UPR=null | UDB_WID.UD_DEF=null
|| return(0)
?};
:: dane wynikające z informacji pierwotnych
UDB_WID.UDB_SYS:=UDB_WID.UDB_UPR().UDB_SYS;
UDB_WID.USERS:=UDB_UPR.USERS;
UDB_WID.UD_SCH:=UDB_WID.UD_DEF().UD_SCH;
UDB_WID.UD_SKL:=UD_DEF.UD_SKL;
UDB_WID.SCIEZKA:=UD_DEF.SCIEZKA;
:: ok
1


\grant_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Aktualizuje widoki związane z uprawnieniem.
::----------------------------------------------------------------------------------------------------------------------
UDB_WID.cntx_psh();
UDB_WID.prefix();
UD_DEF.cntx_psh();
UD_DEF.index('SKLREF');
UD_DEF.prefix(UDB_UPR.UD_SKL);
_loop:=UD_DEF.first();
{!
|? _loop
|! UDB_WID.blank(1);
   UDB_WID.UDB_UPR:=UDB_UPR.ref();
   UDB_WID.UD_DEF:=UD_DEF.ref();
   UDB_WID.add();
   _loop:=UD_DEF.next()
!};
UD_DEF.cntx_pop();
UDB_WID.cntx_pop();
~~


\udb_upr_zmien_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zmienia uprawnienia na odwrotne.
::----------------------------------------------------------------------------------------------------------------------
UDB_UPR.DOSTEP:={? UDB_UPR.DOSTEP='T' || 'N' || 'T' ?};
~~


\udb_upr_nadaj_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Nadaje uprawnienie.
::----------------------------------------------------------------------------------------------------------------------
UDB_UPR.DOSTEP:='T';
~~


\udb_upr_odbierz_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wycofuje uprawnienie.
::----------------------------------------------------------------------------------------------------------------------
UDB_UPR.DOSTEP:='N';
~~


\udb_upr_zmien_aa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zapisuje zmianę uprawnień.
::----------------------------------------------------------------------------------------------------------------------
{? UDB_UPR.put()
|| exec('udb_upr_zmien','schemat')
?};
~~


\adm_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wypełnieniem panelu zarządzania uprawnieniami.
::----------------------------------------------------------------------------------------------------------------------
UDB_GRP.cntx_psh();
UDB_GRP.index('SYMBOL');
UDB_GRP.prefix();

UDB_SYS.cntx_psh();
UDB_SYS.index('OCHRONA');

UD_DEF.cntx_psh();
UD_DEF.index('SYMBOL');

USERS.cntx_psh();
USERS.index('USR_KKOD');

1


\adm_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Podczas zamykania panelu zarządzania uprawnieniami.
::----------------------------------------------------------------------------------------------------------------------
UDB_GRP.cntx_pop();
UDB_SYS.cntx_pop();
UD_DEF.cntx_pop();
USERS.cntx_pop();
1


\adm_grp_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okna nawigatora w oknie ADM.
::   WE: _a [STRING] - akronim okienka tabeli UDB_SYS
::----------------------------------------------------------------------------------------------------------------------
grp_disp(UDB_SYS,_a,1,1);
~~


\adm_sys_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okna NAW tabeli UDB_SYS w oknie ADM.
::----------------------------------------------------------------------------------------------------------------------
grp_disp(UD_DEF,'NAW_MAX',1,1);
grp_disp(USERS,'NAW',1,1);
~~


\adm_sys_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna NAW tabeli UDB_SYS w oknie ADM.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
exec('synchronize_sys','!zws_upr_rupr',cur_tab());
~~


\adm_def_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okna NAW_MAX tabeli UD_DEF w oknie ADM.
::----------------------------------------------------------------------------------------------------------------------
grp_disp(UDB_UPR,'USERS',1,1);
~~


\adm_def_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna NAW_MAX tabeli UD_DEF w oknie ADM.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
{? exec('dom_empty','#table',cur_tab())
:: brak pozycji w nawigatorze
|| return('#disable')

|? ref_tab(cur_tab().REF)<>UDB_SYS
:: noda nie reprezentuje UDB_SYS
|| return('#disable')
?};

UD_DEF.prefix(exec('domyslny','schemat',UDB_SYS.UD_TYP));
~~


\adm_def_upr_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna USERS tabeli UDB_UPR w oknie ADM.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(UD_DEF,'NAW_MAX',1)
|| return('#disable')
?};
UDB_UPR.index('W_USR');
UDB_UPR.prefix(UDB_SYS.ref(),UD_DEF.UD_SKL);
~~


\adm_usr_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okna NAW tabeli USERS w oknie ADM.
::----------------------------------------------------------------------------------------------------------------------
grp_disp(UDB_UPR,'UD_SKL',1,1);
~~


\adm_usr_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Przed obsługą okna NAW tabeli USERS w oknie ADM.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
{? exec('dom_empty','#table',cur_tab())
:: brak pozycji w nawigatorze
|| return('#disable')

|? ref_tab(cur_tab().REF)<>UDB_SYS
:: noda nie reprezentuje UDB_SYS
|| return('#disable')
?};
~~


\adm_usr_upr_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed obsługą okna UD_SKL tabeli UDB_UPR w oknie ADM.
::   WE: 0 - wywołana przez grp_disp, nie zero - wywołana przez aktywowanie
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(USERS,'NAW',1)
|| return('#disable')
?};
{? USERS.AKT<>'T' | (USERS.JTERM<>'T' & USERS.EKIOSK<>'T')
|| return('#disable')
?};
UDB_UPR.index('W_SKL');
UDB_UPR.prefix(UDB_SYS.ref(),USERS.ref());
~~


\adm_usr_upr_users_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka USERS tabeli UDB_UPR w oknie grupowym ADM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('adm_usr_upr_actions','!zws_upr_rupr','USERS')


\adm_usr_upr_ud_skl_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po odświeżeniu okienka UD_SKL tabeli UDB_UPR w oknie grupowym ADM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('adm_usr_upr_actions','!zws_upr_rupr','UD_SKL')


\adm_usr_upr_actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Formuła odpowiedzialna za dostępność akcji w podanym oknie tabeli UDB_UPR.
::   WE: _a [STRING] - Akronim okna, w którym ustawiana będzie dostępność akcji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_hide:='';
{? UDB_UPR.sel_size()=0
|| {? UDB_UPR.DOSTEP='T' || _hide:='N'
   |? UDB_UPR.DOSTEP='N' || _hide:='O'
   ?}
?};
UDB_UPR.actions_grayed(_a,_hide)


\dostep_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przed wyświetleniem pola DOSTEP zmiennej UD_POM. Prezentacja znacznika uprawnień do elementu.
::----------------------------------------------------------------------------------------------------------------------
UDB_UPR.index('UNIQUE');
UDB_UPR.prefix(UDB_SYS.ref(),USERS.ref());
{? UDB_UPR.find_key(UD_DEF.UD_SKL)
|| UD_POM.DOSTEP:=UDB_UPR.DOSTEP
|| UD_POM.DOSTEP:='X'
?}


\synchronize_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Synchronizuje zawartość nawigatora z tabelami UDB_GRP i UDB_SYS
::   WE: _a [TABLE] - alias tabeli nawigatora
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_BUF:=_a;

_BUF.cntx_psh();
UD_TYP.cntx_psh();
UDB_GRP.cntx_psh();
UDB_GRP.index('SYMBOL');
UDB_SYS.cntx_psh();
UDB_SYS.index('UNIQUE');

UDB_GRP.prefix();
_loop:=UDB_GRP.first();
{!
|? _loop
|! _grp:=null;
   _ref:=$UDB_GRP.ref();
   {? _BUF.find_tab(1,'REF',,'=',_ref)
   || _grp:=_BUF.ref()
   || _BUF.blank();
      _BUF.SYMBOL:=UDB_GRP.SYMBOL;
      _BUF.OPIS:=UDB_GRP.OPIS;
      _BUF.REF:=_ref;
      {? _BUF.add()
      || _grp:=_BUF.ref()
      ?}
   ?};
   {? _grp<>null
   || UDB_SYS.prefix(UDB_GRP.ref());
      _loop:=UDB_SYS.first();
      {!
      |? _loop
      |! _ref:=$UDB_SYS.ref();
         {? ~_BUF.find_tab(1,'REF',,'=',_ref)
         || UDB_SYS.UD_TYP();
            _BUF.blank();
            _BUF.TREE_REF:=_grp;
            _BUF.SYMBOL:=UD_TYP.SYMBOL;
            _BUF.OPIS:=UD_TYP.OPIS;
            _BUF.REF:=_ref;
            {? _BUF.add()
            || _BUF.ref()
            ?}
         ?};
         _loop:=UDB_SYS.next()
      !};
      _loop:=UDB_GRP.next()
   ?}
!};

UDB_GRP.prefix();
UDB_SYS.prefix();

_BUF.f_set(,,'TREE_REF<>0');
_loop:=_BUF.f_first();
{!
|? _loop
|! {? UDB_SYS.seek(_BUF.REF)
   || UDB_SYS.UD_TYP();
      {? _BUF.SYMBOL<>UD_TYP.SYMBOL | _BUF.OPIS<>UD_TYP.OPIS
      || _BUF.SYMBOL:=UD_TYP.SYMBOL;
         _BUF.OPIS:=UD_TYP.OPIS;
         _BUF.put()
      ?};
      _loop:=_BUF.f_next()
   || _loop:=_BUF.del()
   ?}
!};
_BUF.f_set(,,'TREE_REF=0');
_loop:=_BUF.f_first();
{!
|? _loop
|! {? UDB_GRP.seek(_BUF.REF)
   || {? _BUF.SYMBOL<>UDB_GRP.SYMBOL | _BUF.OPIS<>UDB_GRP.OPIS
      || _BUF.SYMBOL:=UDB_GRP.SYMBOL;
         _BUF.OPIS:=UDB_GRP.OPIS;
         _BUF.put()
      ?};
      _loop:=_BUF.f_next()
   || _loop:=_BUF.del()
   ?}
!};
_BUF.f_clear();

UDB_SYS.cntx_pop();
UDB_GRP.cntx_pop();
UD_TYP.cntx_pop();
_BUF.cntx_pop();
~~


:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 bb382abd1be26b82704aa1d6720ba8fe083af6b66a8bbf813fadd8d9cd134e76828555376a8223d22919be886f91e2eeca77c22f5c0471a2504bc3aa09d131dd52196fe2b89ce5366cb6df285bc6efe503908a7f1911ffc71041f348d0eba076e398b979e6cc10fe5821f103637def8db115d30a2bceb998678cbc9869676ea7

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_fak_easp.fml
:: Utworzony: 16.04.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa akceptacji dokumentu sprzedaży
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP
::# properties=SERVICE
::# parses=exec('parses','!lsp_fak_easp')
::# kind=WE,   symbol=FAKS,      type=_FAKS,    name=Dokument sprzedaży,   required=T,    keyref=T
_mp:=params_get().mp;
_in:=params_get().in;

exec('init_fak','lsp');

_akcja:=_mp.akcja();
_auto:=_akcja='AkceptujAuto' | (_akcja<>'Akceptuj' & (_mp.isAutoRun() | _mp.isService()));
_group:=_mp.isGroup();

{? ~(var_pres('FAKS',_in)=type_of(null()) & _in.FAKS)
|| _mp.error('Brak wymaganego parametru FAKS.')
|| FAKS.cntx_psh();
   FAKS.prefix();
   {? ~FAKS.seek(_in.FAKS)
   || _mp.error('Nie znaleziono dokumentu.')
   || {? _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref())
      || exec('polafaks_get','jpk_log',FAKS.ref(),FAKS);
         params_set('mp',_mp);
         {? _akcja='Akceptuj' | _auto | _group
         || exec('akceptuj','!lsp_fak_easp',_auto,_group)
         |? _mp.pathTodo()
            | _mp.pathArea() & _akcja=''
         || _win_red:=exec('faks_win_edit','faktury_nag');
            _ff:="params_exec('faks_pozycje_todo','!lsp_fak_easp')";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],_ff);
            {? FAKS.WHERE='K'
            || _ff:="params_exec('faks_kzf_red','!lsp_fak_easp')";
               FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Korygowane dokumenty'@],_ff)
            ?};
            _ff:="params_exec('faks_akceptuj_todo','!lsp_fak_easp'); 'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
            _ff:="'key:Esc'";
            FAKS.win_ebtn(_win_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
            FAKS.win_edit(_win_red);
            exec('set_efld_opt','faktury_nag',_win_red);
::          Dalsza obsługa akceptacji w exec('akceptuj','!lsp_fak_easp')
            FAKS.display();
::          Usunięcie definicji tymczasowych okien
            FAKS.win_edit(''); FAKS.win_edel(_win_red)
         || _mp.error('Nieobsługiwana ścieżka.')
         ?};
         exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
      || exec('who_rlock_faks','faktury_nag')

      ?}
   ?};
   FAKS.cntx_pop()
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| _kor:=exec('FindAndGet','#table',FAKS,_in.FAKS,,"FAKS.T().KOR",'');
   {? _kor='T'
   || 'Zaakceptuj korektę sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   |? _kor='Z'
   || 'Zaakceptuj korektę zbiorczą: %1'@@[exec('record','#to_string',_in.FAKS)]
   || 'Zaakceptuj dokument sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   ?}
|| ''
?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| FAKS.cntx_psh();
   FAKS.use(ref_name(_in.FAKS));
   FAKS.prefix();
   {? FAKS.seek(_in.FAKS)
   || __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=FAKS.AR;
      _args.AM:=FAKS.AM;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FAKS.cntx_pop()
?};

1


\faks_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument sprzedaży - Akceptuj
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('faks_akceptuj','faktury_nag')


\faks_akceptuj_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument sprzedaży - Akceptuj z todo
::   WE: params_get()   - ustawiane w exec('main','!lsp_fak_easp')
::----------------------------------------------------------------------------------------------------------------------
params_exec('akceptuj','!lsp_fak_easp');
''


\faks_pozycje_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument sprzedaży - Pozycje z todo
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
exec('pozycje_fak','faktury_poz',0);
''


\faks_kzf_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument sprzedaży - Korygowane pozycje z todo
::----------------------------------------------------------------------------------------------------------------------
exec('faks_kzf','faktury_nag');
''


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument sprzedaży - Akceptacja dokumentu
::   WE: [_a] - 1-automatycznie 0-nie(domyślnie)
::       [_b] - 0/1 - operacja grupowa
::       params_get()   - ustawiane w exec('main','!lsp_fak_easp')
::----------------------------------------------------------------------------------------------------------------------
_auto:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_group:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_mp:=params_get().mp;

_result:=0;

{? ~exec('czy_z_ok','okresy',-2)
||
   1

|? FAKS.STAT_REJ='A'
||
   _txt:='Dokument jest anulowany.\nAkceptacja niemożliwa.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',gsub(_txt,'\n',' '),1,FAKS.SYM) || FUN.info(_txt) ?} ?};
   _mp.done()

|? FAKS.STAT_REJ='T'
||
   _txt:='Dokument jest już zaakceptowany.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',gsub(_txt,'\n',' '),1,FAKS.SYM) || FUN.info(_txt) ?} ?};
   _mp.done()

|? FAKS.STAT_REJ='N'
||
   _txt:='Nie zakończono rejestracji dokumentu.\nAkceptacja niemożliwa.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',gsub(_txt,'\n',' '),5,FAKS.SYM) || FUN.info(_txt) ?} ?};
   {? ~_group || _mp.error(gsub(_txt,'\n',' ')) ?}

|? FAKS.OBI_POW<>'' & exec('edokum_status','obiegi_log',FAKS.OBI_POW)=''
||
   _txt:='Nie zakończono obiegu dokumentu.\nAkceptacja niemożliwa.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',gsub(_txt,'\n',' '),5,FAKS.SYM) || FUN.info(_txt) ?} ?}

|? FAKS.OBI_POW<>'' & exec('edokum_status','obiegi_log',FAKS.OBI_POW)='N'
||
   _txt:='Obieg dokumentu zakończył się odrzuceniem.\nAkceptacja niemożliwa.'@;
   {? ~_auto || {? _group || exec('add_kom','#message',gsub(_txt,'\n',' '),6,FAKS.SYM) || FUN.info(_txt) ?} ?};
   _mp.error(gsub(_txt,'\n',' '))

|? FAKS.T().VALACC<>null() & ~exec('validate','wzorce',FAKS.T().VALACC,FAKS,FAKS.ref())
||
   ~~

|? _auto | _group | FUN.ask('Zaakceptować dokument %1?'@[FAKS.SYM])
||
   FAKS.STAT_REJ:='T';
   {? FAKS.put()
   ||
      _txt:='Zaakceptowano dokument.'@;
      {? _group || exec('add_kom','#message',gsub(_txt,'\n',' '),1,FAKS.SYM) ?};
      {? FAKS.T().KOR='T' & FAKS.T().HIS='T' || exec('hist_akc','faktury_nag',1,'T') ?};

      {? FAKS.EDOKUMEN='T'
      ||
         {? _mp.isGroup()
         ||
            _Sel:=FAP.sel_aget();
            _Sel.REF:=#FAKS.ref(); _Sel.CRC:=FAKS.crc(); _Sel.add();
            exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Akceptacja E-dokument',,1,_Sel)
         ||
            exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Akceptacja E-dokument')
         ?}

      |? FAKS.CZY_DRUK='X' & FAKS.STAT_REJ='T' & FAKS.WHERE<>'G'
      ||
         {? _mp.isGroup()
         ||
            _Sel:=FAP.sel_aget();
            _Sel.REF:=#FAKS.ref(); _Sel.CRC:=FAKS.crc(); _Sel.add();
            exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Akceptacja Utwórz PDF',,1,_Sel)
         ||
            exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Akceptacja Utwórz PDF')
         ?}
      ?};

      _mp.done()
   ?}
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 d3d44336fd81ff8c4e98206f9ca39e13748789237d21cf22281284e29026c452cf36ab8c38bd14f7d1b24df16df766ce1d6502e8f9ff441b472f6627bc018aaa99706b93f877570aa4c0d3f1b04e66174502c0ed47b36d9c1e0737f627939bc38a3016db9776b81db323238ff341d9d5ab878dbd54cac64e7d05cb91f6f27f4f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_wkj_epro.fml
:: Utworzony: 28.07.2022
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_WKJ_EPRO - Rozwiązywanie problemów prod.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Formuła główna czynności rozwiązywania problemu produkcyjnego (TTE_WKJ_EPRO)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

:: PARAMETRY WE:
::# kind=WE, symbol=EK_PROB, type=_EK_PROB, name=Problem produkcyjny, required=T, keyref=T
{? var_pres('EK_PROB',_in)<>type_of(~~) & var_pres('EK_PROB',_in)<>type_of(null()) || return() ?};
{? var_pres('EK_PROB',_in)=type_of(~~) || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=EK_PROB, type=_EK_PROB, name=Problem produkcyjny, required=N
{? var_pres('EK_PROB',_out)<>type_of(~~) & var_pres('EK_PROB',_out)<>type_of(null()) || return() ?};

_result:=0;
_done:=0;

EK_PROB.cntx_psh();
EK_PROB.prefix();

{? EK_PROB.seek(_in.EK_PROB)
||
   _ek_prob_put:="
      _desc:=_a;
      _result:=0;
      do();
      EK_PROB.cntx_psh();
      EK_PROB.prefix();
      EK_PROB.ROZ_US:=OPERATOR.USER;
      EK_PROB.ROZ_D:=date();
      EK_PROB.ROZ_T:=time();
      EK_PROB.PROBLEM:='R';
      EK_PROB.memo_set(_desc,'ROZ_DESC');
      {? EK_PROB.put()
      || EK_PROB.memo_put(,'ROZ_DESC');
         _result:=1
      ?};
      EK_PROB.cntx_pop();
      end();
      _result
   ";

   {? EK_PROB.PROBLEM='T'
   || {? exec('simple_problem','zl_wkj')
      || {? FUN.ask('Czy problem został rozwiązany?'@)
         || _result:=_ek_prob_put('');
            {? _result || _done:=1 ?}
         ?}
      || {? VEK.TERM='T'
         || _choice:=FUN.choice('Czy problem został rozwiązany?'@,1,'Tak'@,'Tak, z opisem'@);
            {? _choice=1
            || _result:=_ek_prob_put('')
            |? _choice=2
            || _desc:=exec('problem_desc','ekioski','R');
               {? type_of(_desc)=type_of('')
               || _result:=_ek_prob_put(_desc);
                  {? _result || _done:=1 ?}
               ?}
            ?}
         || _red:=exec('problem_win','zl_wkj',1);
            EK_PROB.win_edit(_red);
            _formula:="
               _desc:=exec('problem_desc','zl_wkj','R');
               {? type_of(_desc)=type_of('')
               || EK_PROB.memo_set(_desc,'ROZ_DESC')
               ?};
               ''
            ";
            EK_PROB.win_ebtn(_red,'text=%1,align=begin'['Podpowiedź'@],_formula);
            exec('ok_esc','#window',EK_PROB,_red);
            EK_PROB.ROZ_US:=OPERATOR.USER;
            EK_PROB.ROZ_D:=date();
            EK_PROB.ROZ_T:=time();
            EK_PROB.PROBLEM:='R';
            EK_PROB.memo_set('','ROZ_DESC');
            {? EK_PROB.edit()
            || {? EK_PROB.put()
               || EK_PROB.memo_put(,'ROZ_DESC');
                  _done:=1
               ?}
            ?}
         ?}
      ?}
   || FUN.info('Problem już został rozwiązany przez %1.'@[EK_PROB.ROZ_US().DANE]);
      _done:=1
   ?}
?};

EK_PROB.cntx_pop();

{? var_pres('_context')<>type_of(~~) & var_pres('RESULT',_context)=type_of(0) || _context.RESULT:=_result ?};
{? _done
|| _out.EK_PROB:=_in.EK_PROB;
   _mp.save(,_out);
   _mp.done()
?};

~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Opis dla czynności rozwiązywania problemu produkcyjnego (TTE_WKJ_EPRO)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

_formula:="
   _res:=obj_new('EK_SLOP','ZGL_D','ZGL_T');
   @.EK_SLOP.cntx_psh();
   _res.EK_SLOP:=EK_SLOP().KOD;
   _res.ZGL_D:=$ZGL_D;
   _res.ZGL_T:=$ZGL_T;
   @.EK_SLOP.cntx_pop();
   _res
";

:: jest rekord kluczowy to ustawiam odpowiedniego EK_PROB
{? var_pres('[1]',_keyRefs)
||
   _tmp:=exec('FindAndGet','#table',EK_PROB,_keyRefs[1],,_formula,~~);
   _desc:=
      {? type_of(_tmp)>100
      || 'Rozwiąż problem \'%1\' zgłoszony %2 %3'@@[_tmp.EK_SLOP,_tmp.ZGL_D,_tmp.ZGL_T]
      || ''
      ?}

:: jest parametr wejściowy to ustawiam odpowiedniego EK_PROB
|? var_pres('EK_PROB',_in)
|| _tmp:=exec('FindAndGet','#table',EK_PROB,_in.EK_PROB,,_formula,~~);
   _desc:=
      {? type_of(_tmp)>100
      || 'Rozwiąż problem \'%1\' zgłoszony %2 %3'@@[_tmp.EK_SLOP,_tmp.ZGL_D,_tmp.ZGL_T]
      || ''
      ?}

?};
_desc


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Formuła display TO-DO
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
{? type_of(_in)<>type_of(~~) & var_pres('EK_PROB',_in)>type_of(~~)
|| EK_PROB.cntx_psh();
   EK_PROB.prefix();
   {? EK_PROB.seek(_in.EK_PROB)
   || exec('problem_display','zl_wkj')
   ?};
   EK_PROB.cntx_pop()
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 1a71102de49a9f0af2b2595983f8951662b7c9726a8ea6d075e2f96eec51fa749c804c49fb40e75e47adfb18ab44e4a7553b637d1109fb0d186a2f7e91ea2b6196bb8dc3a61c6c801ff0d66841fb596015bf39c773e64abd37640ce95686e29199e3ddb9ff60f1a94fa14c70faed269ee308c51f380481612230e79e5a82cb35

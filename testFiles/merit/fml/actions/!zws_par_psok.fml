:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_psok.fml
:: Utworzony: 18.01.2016
:: Autor: RWR
::======================================================================================================================
:: Zawartość: ZWS_PAR_PSOK - Składniki okresowe.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Składniki okresowe - główna formuła czynności.
::   WE:
::   WY:
::  OLD: \sel_cfpr/skl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

R.cntx_psh();
R.clear();
R.win_dict('SLO');

PAR_POKR.cntx_psh();
PAR_POKR.index('PAR_POKR');
PAR_POKR.prefix(exec('ref_firma','ustawienia'));
PAR_POKR.win_edit('RED');
PAR_POKR.win_patt('RED');
PAR_POKR.win_sel('WER');
PAR_POKR.select();
PAR_POKR.cntx_pop();

R.cntx_pop();
~~


\par_pokr_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed modyfikacją rekordu. Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli
::       PAR_POKR.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
:: Pielęgnacja anomalii.
PAR_POKR.R_RN:=PAR_POKR.R().RN;
PAR_POKR.R_RT:=PAR_POKR.R().RT;

: Porządki - zerowanie pól, które są "niedostępne".
{? PAR_POKR.RZ='N'
|| exec('blank','#field',PAR_POKR,,'KZ','WZ_OD','WZ_DO','DZ_12','PTZO')
?};

{? PAR_POKR.CZY_FM='N'
|| exec('blank','#field',PAR_POKR,,'FM')
?};

{? PAR_POKR.CZY_PKN='N'
|| exec('blank','#field',PAR_POKR,,'R_N')
?};
1


\par_pokr_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła wyzwalacza przed dołączeniem rekordu tabeli PAR_POKR.
::   WE:
::   WY: 1 - Właściwa akcja może zostać wykonana.
::       0 - Usunięcie rekordu nie jest możliwe.
::----------------------------------------------------------------------------------------------------------------------
exec('par_pokr_modb','!zws_par_psok')


\par_pokr_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła wyzwalacza przed dołączeniem rekordu tabeli PAR_POKR.
::   WE:
::   WY: 1 - Właściwa akcja może zostać wykonana.
::       0 - Usunięcie rekordu nie jest możliwe.
::----------------------------------------------------------------------------------------------------------------------
exec('par_pokr_modb','!zws_par_psok')


\par_pokr_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól tabeli PAR_POKR.
::       Formuła wywoływana jest w dwóch kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE:  _a  [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(PAR_POKR)
|| _tab:=_b
|? _tryb='1'
|| _tab:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? _fld='' | _fld='RZ'
|| _sval:=$(PAR_POKR.RZ='T');
   _tab.efld_opt(_we,'enable='+$_sval,PAR_POKR,'KZ');
   _tab.efld_opt(_we,'enable='+$_sval,PAR_POKR,'WZ_OD');
   _tab.efld_opt(_we,'enable='+$_sval,PAR_POKR,'WZ_DO');
   _tab.efld_opt(_we,'enable='+$_sval,PAR_POKR,'DZ_12');
   _tab.efld_opt(_we,'enable='+$_sval,PAR_POKR,'PTZO')
?};

{? _fld='' | _fld='CZY_FM'
|| _sval:=$(PAR_POKR.CZY_FM='T');
   _tab.efld_opt(_we,'enable='+$_sval,PAR_POKR,'FM');
   _tab.efld_opt(_we,'mark='+$_sval,PAR_POKR,'FM')
?};

{? _fld='' | _fld='CZY_PKN'
|| _sval:=$(PAR_POKR.CZY_PKN='T');
   _tab.efld_opt(_we,'enable='+$_sval,PAR_POKR,'R_N');
   _tab.efld_opt(_we,'mark='+$_sval,PAR_POKR,'R_N','RN')
?};


1


\dostepny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła sprawdza, czy podany składnik może być użyty w tabeli składników okresowych. Zasadniczo jesteśmy
::       nieufni. Czyli jeżeli nie znajdziemy dowodów na to, że można użyc danej rubryki (np. Esc podczas SQLa)
::       to znaczy, że nie można jej użyć.
::   WE:  _a  [REFERENCE] - Wskazanie rubryki.
::   WY: Wynik testu:
::       1 - Składnik może być użyty w roli składnika okresowego
::       0 - Składnik nie może być użyty w roli składnika okresowego.
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;

{? var_pres('_a')=type_of(null()) & ref_name(_a)=R.name()
|| _TAB:=sql(
       'select TOP 1 LS.RB '
       'from @LS join O using (LS.O,O.REFERENCE) '
       'where LS.RB=:_a and O.FIRMA=:_b'
       'order by LS.RB',
       _a,exec('ref_firma','ustawienia'));
   {? type_of(_TAB)<>type_of(~~)
   || _ret:=~_TAB.first()
   ?}
?};

_ret


\par_pokr_r_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [2011]
:: OPIS: Przed redagowaniem pola PAR_POKR.R.
::       Dla akcji "Popraw" sprawdzamy, czy bieżąca rubryka nie została już użyta na liście. Jeżeli tak - zmiana rubryki
::       (jej edycja) nie jest możliwa.
::  OLD: \zap_nrb/skl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();
{? -menu_txt()='popraw' & _fld<>null()
|| exec('dostepny','!zws_par_psok',_fld)
|| 1
?}


\par_pokr_rx_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [2011]
:: OPIS: Po redagowaniu pól PAR_POKR.R i PAR_POKR.R_N.
::   WE:
::   WY:
::  OLD: \spr_wkn/skl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();
{? _fld
|| {? ~exec('dostepny','!zws_par_psok',_fld)
   || FUN.emsg(
         'Wskazana rubryka została już użyta na listach płac.\n'
         'Jej wykorzystanie w roli składnika okresowego nie jest możliwe.'@
      );
      return(0)

   |? __RUB.ref(7006)=_fld
   || FUN.emsg(
         'Wskazana rubryka jest używana w składnikach okresowych jako premia specjalna.\n'
         'Jej wykorzystanie w roli dodatkowego składnika okresowego nie jest możliwe.'@
      );
      return(0)
   ?};

   _ca:=cur_afld();

   {? _ca='R_N'
   || PAR_POKR.cntx_psh();
      PAR_POKR.index('R');
      PAR_POKR.prefix(exec('ref_firma','ustawienia'),_fld);
      _err:=PAR_POKR.first();
      PAR_POKR.cntx_pop();
      {? _err
      || FUN.emsg(
            'Wskazana rubryka jest używana w definicji składnika okresowego.\n'
            'Jej wykorzystanie w roli składnika nominalnego nie jest możliwe.'@
         );
         return(0)
      ?}
   ?};

   _txt:='';
   {? _ca='R' & PAR_POKR.R().CHO<>'N'
   || _txt+=
         'W definicji składników płacowych podany składnik \n'
         'nie ma znacznika "Nie wliczany do zasiłku".\n'
         'W celu uniknięcia błędów w wyliczeniu listy płac\n'
         'należy ustawić znacznik "Nie wliczaj do zasiłku".\n\n'@
   ?};

   FM.cntx_psh();
   FM.index('S1');
   FM.prefix(_fld);
   {? FM.first()
   || _txt+=
         'Podany składnik listy płac posiada przypisane formuły obliczeniowe.\n'
         'W celu uniknięcia błędów w wyliczeniu listy płac\n'
         'należy usunąć formuły z podanego składnika!\n\n'@
   ?};
   FM.cntx_pop();

   {? _txt<>''
   || FUN.info(_txt-2)
   ?}
?};
1


\par_pokr_zasx_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pól związanych z rozliczaniem premii w zasiłkach.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PAR_POKR.RZ='T'


\par_pokr_fm_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Przed wyświetleniem pola PAR_POKR.FM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PAR_POKR.CZY_FM='T'


\par_pokr_r_n_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [2011]
:: OPIS: Przed wyświetleniem pola PAR_POKR.R_N.
::   WE:
::   WY:
::  OLD: \czy_wkn/skl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_POKR.CZY_PKN='T'


\par_pokr_rz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola PAR_POKR.RZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('par_pokr_efld_opt','!zws_par_psok')


\par_pokt_czy_fm_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola PAR_POKR.CZY_FM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('par_pokr_efld_opt','!zws_par_psok')


\par_pokr_czy_pkn_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu pola PAR_POKR.CZY_PKN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('par_pokr_efld_opt','!zws_par_psok')


\par_pokr_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknach wertowania tabeli PAR_POKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PAR_POKR.blank();
exec('par_pokr_efld_opt','!zws_par_psok','*',PAR_POKR,'RED')


\par_pokr_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Popraw - przed" w oknach wertowania tabeli PAR_POKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('par_pokr_efld_opt','!zws_par_psok','*',PAR_POKR,'RED')


\par_pokr_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknach wertowania tabeli PAR_POKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('par_pokr_efld_opt','!zws_par_psok','*',PAR_POKR,'RED');
PAR_POKR.display()


\par_pokr_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Okienko - przed" w oknach wertowania tabeli PAR_POKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
R.clear();
R.f_set('RN','','"R".RK=\'S\' and "R".CHO=\'N\' and "R".REFERENCE not in (SELECT FM.R from FM)');
R.win_dict('SLO');
~~


\par_pokr_ao
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Okienko - po" w oknach wertowania tabeli PAR_POKR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.f_clear();
R.cntx_pop();
~~


\par_pokr_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Obsługa akcji "Rekord - przed" w oknie WER tabeli PAR_POKR.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _ga:={? PAR_POKR.RP='S' || 'PU' || '' ?};
   PAR_POKR.actions_grayed(cur_win(1,1),_ga)
?};
~~


\par_pokr_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [2011]
:: OPIS: Obsługa akcji "Rekord - po". Sprawdzenie poprawności dołączonych/poprawionych danych tabeli PAR_POKR.
::  OLD: \chk_kk/skl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
_put:=-menu_txt()='popraw';

{? PAR_POKR.RP='S'
|| FUN.emsg('Składnik "Systemowy" może być dodawany wyłącznie systemowo.'@);
   return('RP')
?};

: Sprzątanie związane z obsługą 'enable' zazwyczaj robione jest w wyzwalaczu przed modyfikacją. Jednak w tym przypadku,
: ponieważ pole PAR_POKR.WZ_OD wchodzi do unikalnego klucza indeksowego, i tę unikalność chcemy zweryfikować,
: musimy mieć prawidłową (czyli zerową) wartość w polu PAR_POKR.WZ_OD (przynajmniej w chwili badania unikalności).
_wz_od:=PAR_POKR.WZ_OD;
{? PAR_POKR.RZ='N'
|| PAR_POKR.WZ_OD:=date(0,0,0)
?};

_chk:=__CHK.table(PAR_POKR,_put,,'R');
{? (type_of(_chk)=type_of('') & _chk<>'') | (type_of(_chk)=type_of(0) & ~_chk)
|| return(_chk)
?};

{? PAR_POKR.CZY_FM='T'
|| {? (_chk:=__CHK.record(PAR_POKR,,'FM'))<>''
   || return(_chk)
   ?};
   _tab:=form_chk($PAR_POKR.FM);
   {? _tab.first()
   || FUN.emsg(
         'Formuła ma nieprawidłową składnię.\n\nZnak: %1\n%2\n\nNależy poprawić treść formuły.'@
         [$_tab.ERR_COL,_tab.ERR_DESC]
      );
      return('FM')
   ?}
?};

{? PAR_POKR.CZY_PKN='T'
|| {? (_chk:=__CHK.record(PAR_POKR,,'R_N'))<>''
   || return(_chk)
   |? PAR_POKR.R_N=PAR_POKR.R
   || FUN.emsg(
         'Wartość pola "%1" musi być różna od\nwartości pola "%2".'@
         [MS.name(PAR_POKR,'R_N'),MS.name(PAR_POKR,'R')]
      );
      return('R_N')
   ?}
?};

exec('check','overlap',{? _put || PAR_POKR.ref() || null() ?},
   PAR_POKR,'WZ_OD','WZ_DO',,,,'PAR_POKR',exec('ref_firma','ustawienia'),PAR_POKR.R().RN,PAR_POKR.RZ
)

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:06 cb0587c5bc7ba42f6daa21657e0945d676efafe0127f39b50e29d33b77517811c72e3f204c2758a89fc1a5bd59008640f2b53f893c785d773fbe1940bf8c0324524a078e214cecc851e49547374764902a886948d33575adebbb01f0cabdb8082ab6348c7b5923806fc06bf796a9c4a67d9716378919e0ef9476ee6c15f1bd5f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !obg_fdl_zako.fml
:: Utworzony: 03.08.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły czynności OBG_FDL_ZAKO - Ustalenie statusu delegacji w obiegu
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustalenie statusu delegacji w obiegu - główna formuła czynności OBG_FDL_ZAKO
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS

:: PARAMETRY WE:
::# kind=WE, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na delegację w obiegu, required=T, keyref=T
::# kind=WE, symbol=TYP, type=STRING, name=Ustalić status/Wycofać status, required=N, fml_val="exec('edokum_zak','obiegi_log',_a)"

:: PARAMETRY WY:
::# kind=WY, symbol=EDOKUM, type=_EDOKUM, name=Wskazanie na delegację w obiegu, required=N
::# kind=WY, symbol=STATUS, type=STRING, name=Status delegacji w obiegu, required=N

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

exec('czytaj','#stalesys');
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_service:=_mp.isService();
_auto:=_mp.isAutoRun();
{? _we.EDOKUM<>~~ & _we.EDOKUM
|| _name:=ref_name(_we.EDOKUM);
   EDOKUM.cntx_psh(); EDOKUM.use(_name); EDOKUM.prefix();
   {? EDOKUM.seek(_we.EDOKUM)
   || {? _we.TYP=~~ | _we.TYP='' | _we.TYP='Ustalić status'
      || {? _service | _auto | FUN.ask('Ustalić status delegacji w obiegu?'@)
         || EDOKUM.STATUS:=exec('ustal_status','obiegi_log',EDOKUM.uidref());
            {? EDOKUM.STATUS<>''
            || EDOKUM.B_PREL:=''; EDOKUM.B_PRELS:='';
               exec('add_elog','obiegi_akc','Zakończono obieg dokumentu',1)
            ?};
            EDOKUM.put();
            _wy.EDOKUM:=EDOKUM.ref(); _wy.STATUS:=EDOKUM.STATUS;
            _mp.save(,_wy);
            _mp.done()
         ?}
      || {? _service | _auto | FUN.ask('Wycofać status delegacji w obiegu?'@)
         || EDOKUM.STATUS:=''; EDOKUM.put();
            _wy.EDOKUM:=null; _wy.STATUS:=EDOKUM.STATUS;
            _mp.save(,_wy);
            _mp.done()
         ?}
      ?}
   |? _service
   || _wy.EDOKUM:=null; _wy.STATUS:='';
      _mp.save(,_wy);
      _mp.done()
   ?};
   EDOKUM.cntx_pop()
|| _wy.EDOKUM:=null; _wy.STATUS:='';
   _mp.save(,_wy);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Ustal status delegacji w obiegu'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? _we.EDOKUM<>~~ & _we.EDOKUM
|| _ref:=BB.sqlint($_we.EDOKUM); _nam:=form(($_we.EDOKUM)-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || _desc:='Ustal status delegacji w obiegu: %1, data polecenia %2, delegowany %3 %4'@@[EDOKUM.ID,$EDOKUM.DW,EDOKUM.DOSTAWCA().NAZWISKO,OSOBA.PIERWSZE]
      ?};
      EDOKUM.cntx_pop()
   ?}
?};
_desc

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:51 d0b6d975ab09932bddd0ea9a731cccb480be0dfd47477e6819f8a795faf2dab1af1bef54a96dd87be475abb10dceda81183f075b5b7a89e9e2cd9a576a4fc8796c4846cd8202ae2b551efb58b964b6b6412d0f7b30fe85cf756777da915f0e0cc10aec58df3ba79fe4608d59449a5160a149ff92fc0223257ab40858b2629f82

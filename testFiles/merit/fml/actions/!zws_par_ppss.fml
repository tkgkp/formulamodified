:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ppss.fml
:: Utworzony: 06.04.2023
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_PPSS - Sposoby rozliczania pracy zd.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Definiowanie sposobów rozliczania pracy zdalnej.
::   WE:
::   WY:
::  OLD: \ppsf_sn_grp_init/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_SN.cntx_psh();
PPSF_SN.index('KOD');
PPSF_SN.prefix(exec('ref_firma','ustawienia'));
PPSF_SP.cntx_psh();
PPSF_SP.index('NRN');
PPSF_SP.prefix();

:: okno grupowe:
_grp:=PPSF_SN.grp_make('Sposoby rozliczania pracy zdalnej stałej'@,"grp_disp(PPSF_SP,'WER',1)",'#ppsf_sn_grp');

:: okienko nagłówków:
PPSF_SN.grp_sel(_grp,PPSF_SN,'WER',,"grp_disp(PPSF_SP,'WER')",,,,"~~",,,,'maximized_with_title');

:: okienko pozycji:
PPSF_SN.grp_splt(_grp,,'horizontal','bottom',18);
PPSF_SN.grp_sel(_grp,PPSF_SP,'WER',,,,,,"{? PPSF_SN.size() || PPSF_SP.prefix(PPSF_SN.ref()) || '#disable' ?}",,,,
                'maximized_with_title');

PPSF_SN.win_sel(_grp);
PPSF_SN.select(,1);

PPSF_SP.cntx_pop();
PPSF_SN.cntx_pop();
~~


\ppsf_sp_ppsf_sn_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Formuła na wartość początkową pola PPSF_SN tabeli PPSF_SP
::   WE:
::   WY:
::  OLD: \ppsf_sp_ppsf_sn_bl/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_SN.ref()


\ppsf_sp_r_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Formuła "Przed redagowaniem" pola PPSF_SP.R
::   WE:
::   WY: 0/1
::  OLD: \ppsf_sp_r_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_rFset:=0;
:: 1031 - Wypłata dodatku za pracę zdalną
_rubSql:=__RUB.sys_sql(1031);
{? +_rubSql
|| _rFset:=R.f_set(,,'R.RN in (:_a)',_rubSql)
|| _rFset:=R.f_set(,,'0=1')
?};
_rFset


\ppsf_sp_r_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Formuła "Po redagowaniu" pola PPSF_SP.R
::   WE:
::   WY: 0/1
::  OLD: \ppsf_sp_r_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
R.f_clear()


\ppsf_sp_r_lg_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Formuła "Przed redagowaniem" pola PPSF_SP.R_LG
::   WE:
::   WY: 0/1
::  OLD: \ppsf_sp_r_lg_be/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
_rFset:=0;
:: 10321 - Suma godzin do wyliczeń bieżący miesiąc
:: 10322 - Suma godzin do wyliczeń poprzedni miesiąc
_rubSql:=__RUB.sys_sql({? PPSF_SP.OKR_ROZL='B' || 10321 || 10322 ?});
{? +_rubSql
|| _rFset:=R.f_set(,,'R.RN in (:_a)',_rubSql)
|| _rFset:=R.f_set(,,'0=1')
?};
_rFset


\ppsf_sp_r_lg_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Formuła "Po redagowaniu" pola PPSF_SP.R_LG
::   WE:
::   WY: 0/1
::  OLD: \ppsf_sp_r_lg_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
R.f_clear()


\ppsf_sp_sp_rozl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Po redagowaniu pola SP_ROZL tabeli PPSF_SP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_sp_efld_opt','ppsf')


\ppsf_sp_kw_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Formuła "Przed wyświetleniem" pola KW tabeli PPSF_SP
::   WE:
::   WY: 0/1
::  OLD: \ppsf_sp_kw_bd/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
PPSF_SP.INDYW<>'T'


\ppsf_sp_indyw_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Po redagowaniu pola INDYW tabeli PPSF_SP.
::   WE:
::   WY:
::  OLD: \ppsf_sp_indyw_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ppsf_sp_efld_opt','ppsf')


\ppsf_sp_okr_rozl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Formuła "Po redagowaniu" pola PPSF_SP.OKR_ROZL
::   WE:
::   WY: 0/1
::  OLD: \ppsf_sp_okr_rozl_ae/ppsf.fml
::----------------------------------------------------------------------------------------------------------------------
:: 10321 - Suma godzin do wyliczeń bieżący miesiąc
:: 10322 - Suma godzin do wyliczeń poprzedni miesiąc
{? PPSF_SP.R_LG & ~__RUB.sys_attr(PPSF_SP.R_LG().RN,{? PPSF_SP.OKR_ROZL='B' || 10321 || 10322 ?})
|| PPSF_SP.R_LG:=null()
?};
1

:Sign Version 2.0 jowisz:1045 2023/04/28 15:59:41 c75299d69cbabb39a216fc984d6cd93360bfb2d3d461ad25ecf67bc5a63650dcf806a303b0c9416ee8e0eb70cc52c7f8537a8c7587d9f7fa58418c842732fb0088950a6b30f92f90d50f6a71559353504e40dd85729cac703082b0d6f902dea6b2216567c9fdaa7ee17678df4444ed77b1f7b60cf3dd90c67467a05db8089ca4
